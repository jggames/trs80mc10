0 CLEAR1000:DIMT,P$(23),O,CC,ZZ,M$,I$:MC=16384:GOTO1000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:RETURN
3 FORT=0TO15:PRINT@T*32,P$(O+T);:NEXT:RETURN
4 DATA1,,1,-1,,-1,-1,-1,-1,,-1,1,,1,1,1
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
8 C$=MID$(P$(Y),X,1):C=VAL(C$):RETURN
10 FORT=1TO8:IFX(T)>0THENIFY(T)-O>-1ANDY(T)-O<16THENPRINT@32*(Y(T)-O)+(X(T)-1),;:M$=M$(T):GOSUB7
11 NEXT:RETURN
12 RESTORE:FORT=1TO8:READCC,ZZ:R=Y+(ZZ*C):Q=X+(CC*C)
13 X(T)=0:Y(T)=0:IFQ<1ORQ>23ORR<1ORR>23THENNEXT:RETURN
14 I$=MID$(P$(R),Q,1):IFI$>"0"ANDI$<":"THENX(T)=Q:Y(T)=R:M$(T)=I$:NEXT:RETURN
15 IFI$="ï"THENIFMID$(P$(R-ZZ),Q-CC,1)<>"ï"THENX(T)=Q:Y(T)=R:M$(T)=I$:NEXT:RETURN
16 NEXT:RETURN
17 D=D+1:M=M+VAL(C$):PRINT@7*32+25,D;:PRINT@9*32+25,M;:RETURN
20 I$=INKEY$:CC=32*(Y-O)+(X-1):IFCC<0ORCC>511THEN22
21 PRINT@CC,CHR$(191);:FORZZ=1TO100:NEXT:PRINT@CC,C$;:FORZZ=1TO100:NEXT:IFI$=""THEN20
22 IFI$>"0"ANDI$<":"THENT=VAL(I$):IFX(T)>0THENGOSUB17:X=X(T):Y=Y(T):GOSUB8:GOSUB12:GOSUB3:GOSUB10:ON-(C$<>"ï")GOTO20:GOTO100
23 IFI$="W"THENO=O-4:IFO<1THENO=1
24 IFI$="Z"THENO=O+4:IFO>8THENO=8
25 IFI$="W"ORI$="Z"THENGOSUB3:GOSUB10:GOTO20
26 IFI$="G"THEN200
30 GOTO20
100 FORZZ=1TO20:SOUND100,1:SOUND101,1:NEXT
105 CLS:M$="DESPITE EXPECTATIONS TO THE CONTRARY, YOU HAVE GOTTEN OUT OF THE WOODS!":GOSUB1
110 M$="IT TOOK YOU"+STR$(D)+" DAYS.":GOSUB1
120 M$="YOU COVERED A DISTANCE OF"+STR$(M)+" MILES.":GOSUB1
130 PRINT
140 INPUT"CARE TO TRY IT AGAIN (Y/N)";I$
150 IFI$="Y"THEN900
160 IFI$="N"THENEND
170 GOTO140
200 CLS:M$="TSK, TSK. READY TO GIVE UP ALREADY?":GOSUB1
210 M$="FROM THE WAY YOU WERE PLAYING, I ADMIT I AM NOT SURPRISED.":GOSUB1
220 M$="ALTHOUGH YOU'VE BEEN WANDERING AROUND THE FOREST FOR"+STR$(D)+" DAYS "
230 M$=M$+"AND HAVE TRAVELLED"+STR$(M)+" MILES, YOU ARE STILL FAR FROM YOUR GOAL.":GOSUB1
240 M$="I KNOW YOU WILL DO BETTER NEXT TIME!":GOSUB1
250 GOTO130
900 CLS:X=12:Y=12:D=0:M=0:GOSUB8:O=X-8:GOSUB12:GOSUB3:GOSUB10:GOSUB4000:GOTO20
1000 GOSUB2100:GOSUB3000:GOTO900
2100 P$(01)="         ïïïïï         "
2110 P$(02)="      ïïïï477ïïïï      "
2120 P$(03)="    ïïï544833463ïïï    "
2130 P$(04)="   ïï1451114517135ïï   "
2140 P$(05)="  ïï494967555876685ïï  "
2150 P$(06)="  ï37298356739187585ï  "
2160 P$(07)=" ïï14784292711822763ïï "
2170 P$(08)=" ï7218553113133428613ï "
2180 P$(09)=" ï4267252422543281773ï "
2190 P$(10)="ïï4165111914344319827ïï"
2200 P$(11)="ï435232232425351135537ï"
2210 P$(12)="ï271511315332423775427ï"
2220 P$(13)="ï252261244634121265188ï"
2230 P$(14)="ïï4375193445294195748ïï"
2240 P$(15)=" ï4167834341312323624ï "
2250 P$(16)=" ï7326153923215758954ï "
2260 P$(17)=" ïï16734811121228941ïï "
2270 P$(18)="  ï25478756135787293ï  "
2280 P$(19)="  ïï656467252263474ïï  "
2290 P$(20)="   ïï2312333213211ïï   "
2300 P$(21)="    ïïï744573447ïïï    "
2310 P$(22)="      ïïïï334ïïïï      "
2320 P$(23)="         ïïïïï         "
2360 RETURN
3000 CLS:PRINTTAB(1)"back from the klondike":FORT=1TO14:PRINTP$(T):NEXT:
3001 PRINT@2*32+25,"by";:PRINT@4*32+25,"sam";:PRINT@6*32+25,"loyd";:PRINT@480,;
3004 GOSUB3080:FORT=15TO23:PRINTP$(T):NEXT
3005 M$="   START FROM THE THREE IN THE CENTRE. GO THREE STEPS IN A STRAIGHT LINE IN ANY ONE OF THE EIGHT DIRECTIONS, "
3010 M$=M$+"NORTH, SOUTH, EAST, WEST, NORTHEAST, NORTHWEST, SOUTHEAST, OR SOUTHWEST.":GOSUB1:GOSUB3080
3011 M$="   WHEN YOU HAVE GONE THREE STEPS "
3020 M$=M$+"IN A STRAIGHT LINE YOU WILL REACH A SQUARE WITH A NUMBER ON IT, WHICH INDICATES THE SECOND DAY'S JOURNEY, "
3030 M$=M$+"AS MANY STEPS AS IT TELLS, IN A STRAIGHT LINE IN ANY ONE OF THE EIGHT DIRECTIONS.":GOSUB1:GOSUB3080
3031 M$="   FROM THIS NEW POINT, "
3040 M$=M$+"MARCH ON AGAIN ACCORDING TO THE NUMBER INDICATED, AND CONTINUE ON IN THIS MANNER UNTIL YOU COME UPON A "
3050 M$=M$+"SQUARE WITH A NUMBER WHICH WILL CARRY YOU JUST ONE STEP BEYOND THE BORDER, THUS SOLVING THE PUZZLE."
3060 GOSUB1:GOSUB3080:RETURN
3080 INPUT"HIT enter TO CONTINUE";I$
3090 FORT=1TO32:PRINTCHR$(8);:NEXT:RETURN
4000 PRINT@0*32+25,"4 3 2";
4010 PRINT@1*32+25," "CHR$(92)"!/";
4020 PRINT@2*32+25,"5-*-1";
4030 PRINT@3*32+25," /!"CHR$(92);
4040 PRINT@4*32+25,"6 7 8";
4050 PRINT@6*32+25,"days";:PRINT@8*32+25,"miles";
4055 PRINT@11*32+25,"SCROLL";
4060 PRINT@12*32+25,"W=UP";
4070 PRINT@13*32+25,"Z=DOWN";
4080 PRINT@14*32+25,"G=GIVE";
4090 PRINT@15*32+25,"UP";
5000 RETURN
6000 REM JIM GERRIE 2016

