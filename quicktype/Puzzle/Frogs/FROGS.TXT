1000 CLS:T=5
1010 E=0
1020 FORJ=1TO4
1030 P(J)=ASC("*")
1040 NEXTJ
1050 P(5)=ASC(" ")
1060 FORJ=6TO9
1070 P(J)=ASC("%")
1080 NEXTJ
1090 PRINT"WELCOME TO THE GAME FROGS!"
1100 PRINT
1110 PRINT"FOR INSTRUCTIONS, JUST PRESS"
1120 PRINT"'ENTER'."
1130 REM
1140 REM PRINT BOARD
1150 REM AFTER ERROR AND EVERY T MOVES
1160 FORJ=1TO9
1170 PRINT" ";CHR$(P(J));
1180 NEXTJ
1190 IFM<24THEN1350
1200 FORJ=1TO4: REM CHECK FOR WIN
1210 IFP(J)=ASC("%")THEN1230
1220 GOTO1350
1230 NEXTJ
1240 FORJ=6TO9
1250 IFP(J)=ASC("*")THEN1270
1260 GOTO1350
1270 NEXTJ
1280 PRINT
1290 IFM<>24THEN1310
1300 PRINT"*** CONGRATULATIONS ***":SOUND100,2:SOUND200,2
1310 PRINT"YOU'VE REVERSED THE FROGS"
1315 PRINT"IN";M;"MOVES"
1320 PRINT"TYPE RUN TO PLAY AGAIN"
1330 PRINT
1340 GOTO1880
1350 IFINT(E/T)<>E/TTHEN1390:REM SEE IF SQUARES SHOULD BE NUMBERED
1360 PRINT
1370 PRINT
1380 PRINT " 1 2 3 4 5 6 7 8 9";
1390 E=E+1
1400 PRINT"  ";
1410 REM
1420 INPUTC$:REM GET USER'S MOVE
1430 IFLEN(C$)=0THEN1710
1440 IFLEN(C$)<>2THEN1650
1450 C(1)=ASC(MID$(C$+"0",1,1)):C(2)=ASC(MID$(C$+"0",2,1))
1460 IF(57-C(1))*(C(1)-49)<0THEN1650:REM LOOK FOR A DIGIT FROM 1-9
1470 C(1)=C(1)-48
1480 IF(57-C(2))*(C(2)-49)<0THEN1650
1490 C(2)=C(2)-48
1500 REM SEEE IF LEGAL MOVE
1510 IFP(C(1))=32THEN1630
1520 IFP(C(2))<>32THEN1610
1530 IFABS(C(1)-C(2))>2THEN1590
1540 REM TALLY AND IMPLEMENT MOVES
1550 M=M+1
1560 P(C(2))=P(C(1))
1570 P(C(1))=ASC(" ")
1580 GOTO1160:REM CONTINUE THE GAME
1590 PRINT"THAT'S TOO BIG A JUMP!"
1600 GOTO1670
1610 PRINT"SORRY, BUT SQUARE";C(2);"IS OCCUPIED!";
1620 GOTO1670
1630 PRINT"YOU CAN'T MOVE AN EMPTY SPACE!"
1640 GOTO1670
1650 PRINT"TO MOVE A PIECE, TYPE 2 DIGITS"
1655 PRINT"(BUT NOT ZERO)."
1660 PRINT"FOR INSTRUCTIONS, JUST PRESS"
1665 PRINT"'ENTER'."
1670 PRINT"INPUT IGNORED. PLEASE TRY"
1675 PRINT"AGAIN..."
1680 E=0
1690 GOTO1160
1700 REM INSTRUCTIONS
1710 PRINT
1720 PRINT"YOU START WITH 8 PIECES ON 9"
1725 PRINT"SQUARES (NUMBERED 1 THROUGH 9)."
1730 PRINT"THE OBJECT IS TO REVERSE THE"
1740 PRINT"TWO SETS OF PIECES IN THE"
1741 PRINT"FEWEST POSSIBLE MOVES (24)."
1742 PRINT"THE STARS (*) MUST END UP"
1743 PRINT"IN THE SQUARES 6 TO 9 AND"
1744 PRINT"THE PERCENT SIGNS (%) IN"
1750 PRINT"SQUARES 1 TO 4."
1760 PRINT:GOSUB1900
1770 PRINT"AT EACH MOVE YOU MAY SLIDE"
1771 PRINT"OR JUMP ONE PIECE. A SLIDE IS"
1772 PRINT"A MOVE TO AN ADJACENT EMPTY"
1773 PRINT"SQUARE. A JUMP MEANS TO HOP"
1790 PRINT"OVER AND ADJACENT PIECE TO"
1791 PRINT"THE EMPTY SQUARE JUST BEYOND"
1792 PRINT"IT."
1800 PRINT:GOSUB1900
1810 PRINT"FOR YOUR MOVE YOU MUST TYPE"
1811 PRINT"2 NUMBERS. THE FIRST NAMES"
1812 PRINT"THE PIECE YOU WISH TO MOVE;"
1820 PRINT"THE SECOND, THE SQUARE TO GO"
1821 PRINT"TO. FOR EXAMPLE, 57 MEANS"
1822 PRINT"THE PIECE AT SQUARE 5 JUMPS"
1823 PRINT"TO SQUARE 7."
1840 PRINT
1850 PRINT"TYPE YOUR MOVE AFTER THE"
1851 PRINT"QUESTION MARK."
1852 PRINT:GOSUB1900
1860 GOTO1680
1870 REM
1880 END
1900 INPUT"HIT 'ENTER' TO CONTINUE";M$
1910 FORZZ=1TO32:PRINTCHR$(8);:NEXT:RETURN
1920 REM MAC OGLESBY 12/01/74
1930 REM PEOPLE'S COMPUTER
1940 REM COMPANY VOL.5
1950 REM NO.3 NOV.-DEC. '76
2000 REM A REVERSING GAME
2010 REM WITH TWO SETS OF
2020 REM PIECES
2030 REM NAME FROGS***
