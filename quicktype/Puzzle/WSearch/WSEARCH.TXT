5 CLEAR 500:DIMW:W=2
10 CLS: PRINT@76/2,"WORD SEARCH PUZZLE":PRINT@396/W, "BY DAVID N. DURKEE"
15 PRINT@518/2,"TRANSLATED BY JON VOSKUIL": PRINT@716/W, "COPYRIGHT (C) 1981"
20 FOR I=1 TO 1000: NEXT I
40 CLS: PRINT"TO CREATE A PUZZLE, SIMPLY TYPE IN A WORD AFTER THE '?' PROMPT, AND PRESS 'ENTER'. WHEN YOU'RE"
45 PRINT"FINISHED, ENTER 'STOP' AS YOUR  LAST WORD, AND THE COMPUTER WILLDO THE REST."
50 PRINT:PRINT"PLEASE CHOOSE:": PRINT" 1 - FOR NORMAL DISPLAY DURING       ENTRY"
52 PRINT" 2 - FOR BLANK DISPLAY (SO THAT      YOU CAN'T SEE THE PUZZLE)"
55 K$=INKEY$: K=VAL(K$): IF K<1 OR K>2 THEN 55
60 DIM W$(200), A(32,14), B(3,3)
80 CLS
90 Z=Z+1
92 PRINT@896/W, "WORD #";Z;: INPUT A$: IF A$="" THEN 92
95 W$(Z)=A$
100 IF A$="STOP" THEN 490
120 U=RND(15): L=RND(32)
160 FOR X=-1 TO 1: FOR Y=-1 TO 1
170 IF X=0 AND Y=0 THEN 270
180 X1=L: Y1=U
190 FOR C=1 TO LEN(A$)
200 X1=X1+X: Y1=Y1+Y
210 IF X1>32 OR X1<1 OR Y1>14 OR Y1<1 THEN B(X+2,Y+2)=0: GOTO 270
220 IF A(X1,Y1)=0 THEN 250
230 IF A(X1,Y1)<>ASC(MID$(A$,C,1)) THEN B(X+2,Y+2)=0: GOTO 270
240 B(X+2,Y+2)= B(X+2,Y+2)+1
250 NEXT C
260 B(X+2,Y+2)=B(X+2,Y+2)+1: B=B+1
270 NEXT Y,X
280 IF B=0 THEN 120
310 R=2:D=2
320 FOR X=1 TO 3: FOR Y=1 TO 3
330 IF B(X,Y)>B(R,D) THEN R=X:D=Y
340 NEXT Y,X
350 X=R-2: Y=D-2
360 IF X=-1 AND Y=-1 AND B(1,1)=1 THEN 380
370 GOTO 400
380 X=RND(3)-2: Y=RND(3)-2
390 IF (X=0 AND Y=0) OR B(X+2,Y+2)=0 THEN 380
400 X1=L: Y1=U
420 FOR C=1 TO LEN(A$)
430 X1=X1+X: Y1=Y1+Y
440 A(X1,Y1)=ASC(MID$(A$,C,1))
445 IF K=2 THEN 460
450 PRINT@((Y1-1)*64+(X1-1)*2)/W,CHR$(A(X1,Y1));
460 NEXT C
470 B=0: FOR X=1 TO 3: FOR Y=1 TO 3: B(X,Y)=0: NEXT Y,X
480 PRINT@896/2,"                                ";: GOTO 90
490 PRINT@896/W, "PLEASE WAIT A FEW MOMENTS. . .";
500 FOR X=1 TO 32: FOR Y=1 TO 14
510 IF A(X,Y)<>0 THEN 530
520 A(X,Y)=45:IFK<>2THEN PRINT@((Y-1)*64+(X-1)*2)/W,"-";
530 NEXT Y,X
540 PRINT@896/W,"";: INPUT"POSITION PAPER AND HIT ENTER";K$
550 LPRINT" ":LPRINT" WORD PUZZLE ANSWER KEY"
555 GOSUB 670
560 FOR I=1 TO 31: LPRINT" ":NEXT I
570 PRINT@896/W, "PLEASE WAIT A FEW MOMENTS. . .";
590 FOR X=1 TO 32: FOR Y=1 TO 14
600 IF A(X,Y)<>45 THEN 620
610 B=RND(26)+64: A(X,Y)=B
620 NEXT Y, X
630 LPRINT" ": LPRINT"    COMPUTER-GENERATED": LPRINT"    WORD SEARCH PUZZLE"
640 GOSUB 670
650 GOTO 720
670 LPRINT" "
680 FORY=1 TO 14: FOR X=1 TO 32:
690 LPRINT CHR$(A(X,Y));" ";
700 NEXT X: LPRINT: NEXT Y
710 RETURN
720 LPRINT" ":LPRINT" WORD LIST:": LPRINT" "
730 FOR I=1 TO Z-1: LPRINT W$(I): NEXT I
760 PRINT@896/W,"                                ";
765 PRINT@896/W,"";: INPUT "WOULD YOU LIKE ANOTHER COPY";K$: IF LEFT$(K$,1)="N" THEN END
770 PRINT@896/W,"";: INPUT"ADVANCE PAPER AND HIT ENTER";K$: GOTO 630
