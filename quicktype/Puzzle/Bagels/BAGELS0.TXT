0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO30
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
30 DIM A1(6),A(3),B(3)
40 Y=RND(-(PEEK(9)*256+PEEK(10))):Y=0:T=255
70 PRINT "GAME OF BAGLES.":PRINT"WOULD YOU LIKE THE RULES (Y/N)";
80 INPUT A$
90 IF LEFT$(A$,1)="N" THEN 150
100 PRINT:M$="I AM THINKING OF A THREE-DIGIT NUMBER. TRY TO GUESS "
110 M$=M$+"MY NUMBER AND I WILL GIVE YOU CLUES AS FOLLOWS:":GOSUB1
120 M$="pico   - ONE DIGIT CORRECT BUT IN THE WRONG POSITION":GOSUB1
130 M$="fermi  - ONE DIGIT CORRECT AND IN THE RIGHT POSITION":GOSUB1
140 M$="bagels - NO DIGITS CORRECT":GOSUB1
150 FOR I=1 TO 3
160 A(I)=INT(10*RND(0))
165 IF I-1=0 THEN 200
170 FOR J=1 TO I-1
180 IF A(I)=A(J) THEN J=I-1:NEXT J:GOTO 160
190 NEXT J
200 NEXT I
210 PRINT:PRINT"O.K. I HAVE A NUMBER IN MIND."
220 FOR I=1 TO 20
230 PRINT "GUESS #"I;
240 INPUT A$
245 IF LEN(A$)<>3 THEN 630
250 FOR Z=1 TO 3:A1(Z)=ASC(MID$(A$,Z,1)):NEXT Z
260 FOR J=1 TO 3
270 IF A1(J)<48 THEN 300
280 IF A1(J)>57 THEN 300
285 B(J)=A1(J)-48
290 NEXT J
295 GOTO 320
320 IF B(1)=B(2) THEN 650
330 IF B(2)=B(3) THEN 650
340 IF B(3)=B(1) THEN 650
350 C=0:D=0
360 FOR J=1 TO 2
370 IF A(J)<>B(J+1) THEN 390
380 C=C+1
390 IF A(J+1)<>B(J) THEN 410
400 C=C+1
410 NEXT J
420 IF A(1)<>B(3) THEN 440
430 C=C+1
440 IF A(3)<>B(1) THEN 460
450 C=C+1
460 FOR J=1 TO 3
470 IF A(J)<>B(J) THEN 490
480 D=D+1
490 NEXT J
500 IF D=3 THEN 680
505 IF C=0 THEN 545
520 FOR J=1 TO C
530 PRINT "pico ";
540 NEXT J
545 IF D=0 THEN 580
550 FOR J=1 TO D
560 PRINT "fermi ";
570 NEXT J
580 IF C+D<>0 THEN 600
590 PRINT "bagels";
600 PRINT
605 NEXT I
610 PRINT "OH WELL"
615 M$="THAT'S 20 GUESSES. MY NUMBER WAS"+STR$(100*A(1)+10*A(2)+A(3)):GOSUB1
620 GOTO 700
630 M$="TRY GUESSING A THREE-DIGIT NUMBER.":GOSUB1:GOTO 230
650 M$="OH, I FORGOT TO TELL YOU THAT THE NUMBER I HAVE IN "
660 M$=M$+"MIND HAS NO TWO DIGITS THE SAME.":GOSUB1:GOTO 230
680 PRINT "YOU GOT IT!!!":PRINT
685 FOR I=1 TO 10:SOUND 203,1:NEXT I
690 Y=Y+1
700 PRINT "PLAY AGAIN (Y/N)";:INPUT A$
720 IF LEFT$(A$,1)="Y" THEN 150
730 IF Y=0 THEN 750
740 PRINT:M$="A"+STR$(Y)+" - POINT BAGLES BUFF!!":GOSUB1
750 PRINT "HOPE YOU HAD FUN. BYE."
999 END
1005 REM *** BAGLES NUMBER GUESSING GAME
1010 REM *** ORIGINAL SOURCE UNKNOWN BUT SUSPECTED TO BE LAWRENCE HALL
1015 REM *** OF SCIENCE, U.C. BERKELEY.  MODIFIED FOR TIMESHARED-8
1020 REM *** BY STUDENTS OF LEXINGTON HIGH SCHOOL, MA AND MODIFIED FOR 
1025 REM *** RSTS/E BY DAVE AHL OF DIGITAL 11/45 GROUP
