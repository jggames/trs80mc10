20 CLS:PRINT:PRINT"   REVERSE -- A GAME OF SKILL":PRINT
30 FOR S=1 TO 30:SOUND S*5+100,1:NEXT
40 PRINT "DO YOU WANT THE RULES <Y/N>? ";
50 B$=INKEY$:R=RND(10):IF B$="" THEN 50 
51 IF B$="N" THEN PRINT B$:GOTO 170
60 CLS:SOUND 100,2: PRINT: PRINT "THIS IS THE GAME OF *REVERSE*.
62 PRINT"TO WIN, ALL YOU HAVE TO DO IS"
64 PRINT"ARRANGE A RANDOM LIST OF TEN"
70 PRINT"(NUMBERED FROM 1 THROUGH 0)"
72 PRINT"IN ASCENDING ORDER FROM TOP"
74 PRINT"(SMALLEST) TO BOTTOM (LARGEST)."
80 PRINT
82 PRINT"TO MOVE, YOU TELL ME HOW MANY"
84 PRINT"BARS (COUNTING FROM THE TOP) 
86 PRINT"YOU WANT ME TO REVERSE."
90 PRINT@483, "PRESS ANY KEY TO CONTINUE";
100 B$=INKEY$: IF B$="" THEN 100
110 CLS: SOUND 100,2: PRINT "FOR EXAMPLE, IF A LIST OF 
112 PRINT "NUMBERS IS:"
114 PRINT "2 3 4 5 6 1 7 8 9 0" 
120 PRINT"AND YOU REVERSE FIVE NUMBERS,"
122 PRINT"THE RESULT WILL BE:
124 PRINT"6 5 4 3 2 1 7 8 9 0" 
130 PRINT "NOW, IF YOU REVERSE 6, YOU WIN! 1 2 3 4 5 6 7 8 9 0"
140 PRINT: PRINT "NO DOUBT YOU WILL LIKE THIS 
142 PRINT"GAME OF SKILL, BUT IF YOU WANT"
144 PRINT"TO STOP, PRESS <Q> TO QUIT."
150 PRINT@483,"PRESS ANY KEY TO CONTINUE";
160 B$=INKEY$: IF B$="" THEN 160
170 SOUND 150,2: PRINT@480, " THANK YOU...ONE MOMENT PLEASE "; 
180 REM RANDOMIZING LIST
190 FOR D=1 TO 10:A(D)=D:NEXT D
192 FOR NN=1 TO 5
194 FOR D=1 TO 10:R=RND(10):GOSUB 550:NEXT D
196 NEXT NN
200 GOSUB 390: REM PRINTING ROUTINE
205 T=0:WW=0:GOSUB 570
210 REM IF WW=1 THEN WIN CONDITION
220 PRINT@0, " HOW MANY SHALL I REVERSE? ";
230 R$=INKEY$: IF R$="" THEN 230
231 IF R$="Q" THEN PRINT R$;:GOTO 370
232 IF R$<"0" OR R$>"9" THEN PRINT@448,"INPUT A NUMBER FROM 0 TO 9";:SOUND 1,25:GOTO220
234 PRINT R$;
236 R=VAL(R$):IF R=0 THEN R=10
240 GOSUB 550: REM REVERSE R ELEMENTS
242 GOSUB 390: REM PRINTING ROUTINE
245 T=T+1 
250 GOSUB 570: REM CHECKWIN
260 IF WW=1 THEN 290
270 GOTO 220
290 PRINT@0,"YOU WON IN ONLY"T" MOVES. ";
300 REM DETERMINE RESPONSE BASED ON NUMBER OF MOVES 
310 IF M$(1)="" THEN FOR M=1 TO 6: READ M$(M): NEXT
320 DATA " WOW! ! THAT'S FANTASTIC!! "," EXCELLENT SCORE!! "," VERY GOOD SCORE! "
322 DATA " THAT'S NOT A BAD SCORE. ", " THAT'S OK, BUT YOU CAN IMPROVE."
324 DATA " TRY TO DO BETTER NEXT TIME. "
330 M=INT(T/2 - 2): IF T<8 THEN M=1
335 IF T>15 THEN M=6
340 PRINT@32, M$(M);
345 FOR S=100 TO 235 STEP 5: SOUND S,1: NEXT 
350 PRINT@448," TRY AGAIN (Y/N)? "; 
360 B$=INKEY$:IF B$="" THEN 360
362 IF B$="Y" THEN PRINT B$;:GOTO 170
364 IF B$="N" THEN PRINT B$;:GOTO 370
366 SOUND 1,10:GOTO 360
370 CLS:PRINT@416,"THANK YOU. I HOPE YOU HAD FUN!!";:END 
380 REM SUBROUTINE FOR PRINTING BARS ON SCREEN 
390 CLS0
395 FOR Y=1 TO 10
397 W=Y:IF Y=10 THEN W=0
400 PRINT@((Y+2)*32),W;: C=A(Y): IF A(Y)>8 THEN C=A(Y)-8
410 SOUND 200-10*A(Y),1
420 FOR X=10 TO 10+5*A(Y):SET(X,Y*2+4,C):NEXT X
430 NEXT Y
440 RETURN 
550 REM REVERSING BARS - REVERSE R ELEMENTS
560 FOR K=1 TO INT(R/2): Z=A(K): A(K)=A(R-K+1): A(R-K+1)=Z: NEXT K
566 RETURN
570 REM SUBROUTINE CHECK WIN CHECK TO SEE IF IN NUMERICAL ORDER
580 WW=1: FOR K=1 TO 10:IF A(K)<>K THEN WW=0
582 NEXT K
585 RETURN: REM RETURN WW=1 IF WIN
610 REM ADAPTED BY DONALD CLERC LOUISVILLE, KY
620 REM AUSTRALIAN RAINBOW COCO MICO AUGUST 84 NO 38 PAGE 59-61
630 REM ADAPTED MORE FOR MC-10 +16K RAM EXPANSION
