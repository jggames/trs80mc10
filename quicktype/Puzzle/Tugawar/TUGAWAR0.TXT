0 CLS:DIMA,C1,CC,C2,MC,J,COL(11):MC=16384:INPUT"TUG-A-WAR INSTRUCTIONS (Y/N)";I$:GOTO30
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:GOSUB11:IFC1<LEN(M$)THEN2
4 RETURN
10 FORA=16709TO16731STEP4:POKEA,PEEK(A)-64:POKEA+1,PEEK(A+1)-64:NEXT:RETURN
11 C2=(PEEK(17024)AND1)*256+PEEK(17025)
12 IFC2<=448THENRETURN
13 GOSUB3040
14 I$=INKEY$:IFI$=""THEN14
15 CLS:RETURN
20 FORROW=0TO1:PRINT@(Y+ROW)*32+(PS*2+X)+2,"";:PRINTCHR$(143+((C-1)*16))CHR$(143+((C-1)*16));:NEXT:RETURN
30 IFLEFT$(I$,1)="Y"THENGOSUB1000
40 CLS0:BP=6:C(1)=7:C(2)=6:C(3)=8:C(4)=2:PL=0:A=RND(-(PEEK(9)*256+PEEK(10)))
50 MOV(1)=-1:MOV(2)=-2:MOV(3)=1:MOV(4)=2
60 GOSUB190
70 PL=(PL=0):GOSUB230:GOSUB240
80 K$=INKEY$:IFK$="A"THENSP=11:GOTO110
90 IFK$="0"THENSP=10:GOTO110
100 IFK$<"1"ORK$>"9"THEN80
101 SP=VAL(K$)
110 IF(PL=0AND12-SP>BP)OR(PL=-1ANDSP<BP)THEN130
120 BP=BP+MOV(COL(BP)):IFBP>12THENBP=12:GOTO130
121 IFBP=-1THENBP=0
130 GOSUB260:IFBP>0ANDBP<12THEN70
140 GOSUB230::C=3:IFBP=0THENPRINT@448+10,"BLUE WINS!!";:GOTO160
150 PRINT@448+10,CHR$(128);"RED WINS!!";
160 PRINT@480+7,"PLAY AGAIN (Y/N)?";
170 A$=INKEY$:IFA$=""THEN170
171 IFA$="Y"THENRUN
172 IFA$="N"THENEND
173 GOTO170
190 CLS0:PRINT@11,"TUG-A-WAR ";:Y=2:X=0:FORA=1TO4:PS=4+A:C=C(A):GOSUB20:NEXT
200 C=14:PRINT@32*4+12,"< <<> >>";:FORA=16524TO16528STEP4:POKEA,PEEK(A)-64:POKEA+1,PEEK(A+1)-64:NEXT
210 Y=8:X=1:C=3:PS=0:GOSUB20:FORA=1TO11:Q=RND(4):COL(A)=Q:C=C(Q):PS=A:GOSUB20:NEXT:C=4:PS=12:GOSUB20
220 C=10:PRINT@387,"PRESS (0-9) OR 'A' FOR ALL";:RETURN
230 C=15:RESET(BP*4+7,17):RESET(BP*4+8,17):RESET(BP*4+7,18):RESET(BP*4+8,18):RETURN
240 IFPL=0THENPRINT@426,CHR$(128)"RED'S TURN";:PRINT@325," A 0";:FORA=9TO1STEP-1:PRINTSTR$(A);:NEXT:GOSUB10:RETURN
250 PRINT@426,"BLUE'S TURN";:PRINT@325,"";:FORA=1TO9:PRINTSTR$(A);:NEXT:PRINT" 0 A";:GOSUB10:RETURN
260 IFPL=0THEN280
270 FORA=1TOSP:GOSUB290:NEXT:RETURN
280 FORA=11TO12-SPSTEP-1:GOSUB290:NEXT:RETURN
290 COL(A)=COL(A)+1+4*(COL(A)=4):C=C(COL(A)):PS=A:GOSUB20:RETURN
300 DATA"
301 DATA"IN THE GAME OF TUG-A-WAR, TWO SETS OF COLORED BOXES APPEAR, ONE ABOVE THE OTHER. "
302 DATA"THE LOWER, LONGER SERIES OF SQUARES IS THE PLAYING FIELD. "
303 DATA"NEAR THE MIDDLE OF THE PLAYFIELD AREA IS A BLACK BALL; "
304 DATA"THE OUTERMOST BOXES AT EACH END OF THE PLAYFIELD REPRESENT EACH PLAYER'S HOME POSITION. "
305 DATA"THE PLAYERS ALTERNATE TURNS, EACH TRYING TO MOVE THE BALL IN THEIR OWN DIRECTION, "
306 DATA"UNTIL IT REACHES ONE OF THE HOME SQUARES. "
307 DATA"SO FAR, SO GOOD-BUT HOW DO YOU MOVE THE BALL? "
308 DATA"IT'S DONE NOT BY PULLING A ROPE, BUT BY CHANGING THE COLORS OF BOXES IN THE PLAYFIELD. "
309 DATA"THE COLOR OF THE SQUARE UNDER THE BALL DETERMINES WHICH DIRECTION IT MOVES AND HOW FAR IT TRAVELS. "
310 DATA"ON ANY GIVEN TURN, THE BALL CAN MOVE EITHER ONE OR TWO SQUARES TO THE LEFT, OR ONE OR TWO SQUARES TO THE RIGHT. "
311 DATA"AT THE TOP OF THE SCREEN ARE FOUR BOXES THAT SHOW YOU WHICH COLORS ARE LINKED TO WHICH DIRECTIONS. "
312 DATA"FOR INSTANCE, THE LEFTMOST BOX SHOWS YOU WHICH COLOR MAKES THE BALL MOVE ONE SQUARE TO THE LEFT. "
313 DATA"THE NEXT BOX TO THE RIGHT SHOWS YOU WHICH COLOR MAKES IT MOVE TWO SQUARES TO THE LEFT. "
314 DATA"THE SECOND PAIR OF BOXES SHOW YOU WHICH COLORS MAKE THE BALL MOVE IN THE OPPOSITE DIRECTION, TO THE RIGHT. "
315 DATA"BY CHANGING THE COLOR OF THE BOX WHERE THE BALL IS CURRENTLY LOCATED, YOU CAN MAKE IT MOVE TOWARD YOUR HOME SQUARE. "
316 DATA"THE PLAYFIELD CONTAINS 11 BOXES. "
317 DATA"WHEN THE GAME BEGINS, EACH OF THESE BOXES IS RANDOMLY GIVEN ONE OF THE FOUR COLORS SHOWN AT THE TOP OF THE SCREEN. "
318 DATA"ON EACH TURN, YOU MAY CHANGE THE COLOR OF ONE, SEVERAL, OR ALL OF THE BOXES "
319 DATA"(HOWEVER, YOU MUST ALWAYS CHANGE AT LEAST ONE BOX). "
320 DATA"BELOW EACH BOX IS A NUMBER WHICH REPRESENTS ITS DISTANCE FROM THE HOME POSITION OF THE PLAYER WHOSE TURN IT IS. "
321 DATA"FOR INSTANCE, IF YOU ARE THE PLAYER ON THE LEFT, THEN ON YOUR TURN THE BOXES ARE NUMBERED 1, 2, 3, ETC., "
322 DATA"FROM LEFT TO RIGHT (THE TENTH BOX IS MARKED WITH A 0, AND THE ELEVENTH WITH AN A). "
323 DATA"WHEN IT'S THE RIGHT PLAYER'S TURN, THE NUMBERING IS REVERSED (THE RIGHTMOST BOX IS 1, ETC.). "
324 DATA"TO TAKE A TURN, YOU MUST SELECT A NUMBER THAT CORRESPONDS TO THE NUMBERS SHOWN BELOW THE BOXES IN THE PLAYFIELD. "
325 DATA"THIS IS DONE BY PRESSING A SINGLE KEY. "
326 DATA"PRESS A NUMBER KEY FROM 1-0 TO SELECT ONE OF THE FIRST TEN VALUES, "
327 DATA"OR PRESS THE A KEY TO CHOOSE THE ELEVENTH BOX. "
328 DATA"THE NUMBER YOU CHOOSE DETERMINES HOW MANY BOXES CHANGE COLOR. "
329 DATA"FOR INSTANCE, IF YOU PRESS 1, ONLY ONE BOX (THE ONE NEAREST YOUR HOME SQUARE) CHANGES COLOR. "
330 DATA"IF YOU PRESS 2, THE TWO BOXES NEAREST YOUR HOME BOX CHANGE, AND SO ON. "
331 DATA"WHERE DO THE NEW COLORS COME FROM? "
332 DATA"EVERY BOX CYCLES THROUGH THE SAME SERIES OF FOUR COLORS SHOWN IN THE UPPERMOST SET OF BOXES, "
333 DATA"GOING FROM LEFT TO RIGHT. "
334 DATA"FOR EXAMPLE, IF THE COLORS SHOWN THERE ARE YELLOW-CYAN-ORANGE-PURPLE, "
335 DATA"THEN A YELLOW SQUARE ALWAYS CHANGES TO CYAN; "
336 DATA"A CYAN SQUARE ALWAYS CHANGES TO ORANGE; A PURPLE SQUARE CHANGES TO YELLOW, AND SO ON. "
337 DATA"IN OTHER WORDS, THE BOX'S CURRENT COLOR DETERMINES WHICH COLOR IT GETS AFTER THE NEXT COLOR CHANGE. "
338 DATA"THOUGH EVERY TURN INVOLVES AT LEAST ONE COLOR CHANGE, THE BALL DOESN'T NECESSARILY MOVE ON EVERY TURN. "
339 DATA"IT ONLY MOVES WHEN YOU CHANGE ALL THE BOXES BETWEEN YOUR HOME POSITION AND THE CURRENT POSITION OF THE BALL. "
340 DATA"FOR EXAMPLE, IF THE BALL IS THREE BOXES AWAY FROM YOUR HOME SQUARE, "
341 DATA"THEN YOU MUST CHANGE THE COLOR OF AT LEAST THREE BOXES IN ORDER TO MOVE IT AT ALL. "
342 DATA"AS YOU CAN SEE, THERE'S MUCH MORE TO THIS GAME THAN APPEARS ON THE SURFACE. "
343 DATA"AT FIRST YOU MIGHT BE TEMPTED TO TRY TO MOVE THE BALL AS OFTEN AS POSSIBLE. "
344 DATA"BUT THAT'S USUALLY A LOSING STRATEGY. "
345 DATA"REMEMBER, THE DIRECTION THE BALL MOVES DEPENDS ON THE COLOR OF ITS SQUARE BEFORE YOU TAKE THE TURN. "
346 DATA"IN MANY CASES, YOU'LL WANT TO MOVE THE BALL ONLY IF IT'S ON A COLOR THAT MOVES IT TOWARD YOUR GOAL. "
347 DATA"BUT LIKE OTHER GAMES OF STRATEGY, "
348 DATA"TUG-A-WAR REWARDS THE PLAYER WHO LOOKS BEYOND THE CURRENT MOVE AND TRIES TO SET THINGS UP FOR FUTURE MOVES; "
349 DATA"SOMETIMES IT'S WISE TO MAKE A SMALL, TEMPORARY SACRIFICE IN ORDER TO BENEFIT LATER IN THE GAME. "
350 DATA"BECAUSE THE BOXES CHANGE COLORS IN THE SAME SEQUENCE, THE EFFECT OF YOUR OWN MOVE IS ALWAYS COMPLETELY PREDICTABLE. "
351 DATA"HOWEVER, SINCE A SINGLE TURN CAN CHANGE THE COLOR OF MANY BOXES, DRAMATIC CHANGES OF FORTUNE ARE ALSO POSSIBLE. "
352 DATA"END"
1000 RESTORE
1010 READM$:IFM$="END"THENGOSUB3040:GOSUB14:RETURN
1020 GOSUB1:GOTO1010
3040 C2=(PEEK(17024)AND1)*256+PEEK(17025)+MC+2:PRINT "  **HIT ANY KEY TO CONTINUE**";:FORJ=C2TOC2+26:POKEJ,PEEK(J)-64:NEXT
3050 RETURN
