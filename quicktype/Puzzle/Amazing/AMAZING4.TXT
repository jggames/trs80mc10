0 CLS:DIMT,R,M,B,L,P,H,K(255),Z,U,G:M=16384:B=128:L=191:H=480:T=RND(-(PEEK(9)*256+PEEK(10))):GOTO2000
1 P=P-1:IFPEEK(P)<>BTHENONK(PEEK(P))GOSUB50,60,70,80
2 POKEP+1,B:POKEP,42:RETURN
3 P=P+1:IFPEEK(P)<>BTHENONK(PEEK(P))GOSUB50,60,70,80
4 POKEP-1,B:POKEP,42:RETURN
5 P=P-32:IFPEEK(P)<>BTHENONK(PEEK(P))GOSUB50,60,70,80
6 POKEP+32,B:POKEP,42:RETURN
7 P=P+32:IFPEEK(P)<>BTHENONK(PEEK(P))GOSUB50,60,70,80
8 POKEP-32,B:POKEP,42:RETURN
20 FORZ=1TOG:FORT=1TOU:R=RND(H)+M:IFPEEK(R)=LTHENPOKER,B:NEXT:GOTO23
21 IFPEEK(R)=BTHENPOKER,L:NEXT:GOTO23
22 NEXT
23 R=RND(480)+M:IFPEEK(R)=LTHENPOKER,B
24 ONK(PEEK(2)ANDPEEK(17023))GOSUB1,3,5,7:NEXT
30 IFZ=G+2THEN100
31 IFZ=G+3THEN200
32 CLS:PRINT"TIME'S UP!":SOUND1,2:GOTO220
50 SOUND100,2:S=S+15*(U-3):RETURN
60 SOUND200,3:S=S+20*(U-3):RETURN
70 Z=G+2:RETURN
80 Z=G+1:RETURN 
100 CLS:PRINT"YOU MADE IT HOME-- AT LAST!":FORT=1TO10:SOUND200,1:SOUND100,1:NEXT
110 PRINT:PRINT:PRINT"YOUR SCORE="S
120 IFS>HSTHENHS=S
130 PRINT"HIGH SCORE="HS:GOTO300
200 POKEP,106:SOUND10,2:SOUND1,2:CLS:PRINT"BAD LUCK !":PRINT"YOU HIT THE WALL !":PRINT:PRINT
210 PRINT"YOU ARE DEAD !*?":
220 PRINT:PRINT:PRINT"NO SCORE":GOTO130
300 PRINT"ANOTHER GO (Y OR N)? "CHR$(128);
310 FORZ=1TO150:NEXT:I$=INKEY$:PRINTCHR$(8);:FORZ=1TO150:NEXT:PRINTCHR$(128);:IFI$=""THEN310
320 IFLEFT$(I$,1)="Y"THENPRINTCHR$(8)"Y";:S=0:GOTO1000
330 IFLEFT$(I$,1)="N"THENPRINTCHR$(8):END
340 GOTO310
600 CLS
610 PRINTTAB(12)"AMAZING!":PRINT:PRINT"YOU ARE AN ASTERISK-- (*)"
620 PRINT"YOU MUST FIND YOUR WAY TO THE":PRINT"TOP LEFT OF THE SCREEN-- (#)":PRINT
640 PRINT"    'W'             UP"
650 PRINT"'A'     'S'     LEFT  RIGHT"
660 PRINT"    'Z'            DOWN":PRINT
670 PRINT"YOU MUST PICK UP AS MANY":PRINT"POINTS AS POSSIBLE!"
680 PRINT"$=15 X LEVEL; !=20 X LEVEL. BUT":PRINT"BEWARE, THE MAZE IS CHANGING!":PRINT
690 PRINT"PRESS ANY KEY TO START "CHR$(128);
700 FORZ=1TO150:NEXT:I$=INKEY$:PRINTCHR$(8);:FORZ=1TO150:NEXT:PRINTCHR$(128);:IFI$=""THEN700
710 CLS:RETURN
1000 CLS0:FORT=0TO31:POKEM+T,107:POKEM+T+480,107:NEXT
1010 FORT=0TO15:POKE32*T+M,104:POKE32*T+M+31,105:NEXT
1020 POKEM,27:POKEM+31,29:POKEM+480,27:POKEM+511,29
1030 FORT=1TO10
1040 R=RND(476)+M:IFPEEK(R)<>BTHEN1040
1050 POKER,33:NEXT:FORT=1TO10
1060 R=RND(476)+M:IFPEEK(R)<>BTHEN1060
1070 POKER,36:NEXT
1200 FORT=1TO(U-3)*75
1210 R=RND(476)+M:IFPEEK(R)<>BTHEN1210
1220 POKER,L:NEXT
1280 P=M+448+30:POKEP,42:SOUND150,1:POKEM+33,35:SOUND225,1
1290 GOTO20
2000 GOSUB600:K(65)=1:K(83)=2:K(87)=3:K(90)=4
2010 K(36)=1:K(33)=2:K(191)=3:K(35)=4:K(107)=3:K(104)=3:K(105)=3
2020 CLS:PRINT"WHICH LEVEL-1 TO 3 (3=HARD)";:INPUTT
2030 IFT<1ORT>3THEN2020
2040 U=3+T:G=((4-T)*1000)-(T*100)
2090 GOTO1000
3000 C=0:CLS:FORT=16384TO16384+128:POKET,C:C=C+1:NEXT:PRINT@256,;:END
4000 REM THE BEST OF INTERFACE
4010 REM PAGE 25-26 'AMAZING!'
4020 REM BY R. HEINS & R. HEARD
4030 REM MC-10 VER JIM GERRIE
