0 DIMC1,CC,M$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 GOSUB9000
20 GOSUB4000
30 GOSUB5000
40 IFFJTHEN8000
50 GOSUB7000
60 GOSUB3000
70 GOSUB6000
80 GOTO20
3000 FORI=0TO3
3020 IFMHTHENIM=I:IN=I:GOTO3040
3030 JM=I:JN=I
3040 N=0:K=0:FORJ=0TO3
3050 IFMETHENJM=J:JN=K:GOTO3090
3060 IFMDTHENJM=3-J:JN=3-K:GOTO3090
3070 IFMCTHENIM=J:IN=K:GOTO3090
3080 IM=3-J:IN=3-K
3090 M=T(IM,JM):IFM=0THEN3140
3100 IFN=.THENT(IM,JM)=0:T(IN,JN)=M:N=M:GOTO 3140
3110 IFM<>NTHENJ=J-1:GOTO3130
3120 T(IM,JM)=0:M=M+1:T(IN,JN)=M:IFM=10THENOA=V
3130 N=0:K=K+1
3140 NEXTJ,I
3150 RETURN 
4000 PC=F:PB=F:PE=F:PD=F:PV=F:PH=F:PT=F
4020 FORI=0TO3:FORJ=0TO2
4030 IFPHTHEN4080
4040 M=T(I,J):N=T(I,J+1)
4050 IFNOT(PE)THENIFNTHENIFM=.THENPE=V:PH=PD:PT=PHANDPV:GOTO4080
4060 IFNOT(PD)THENIFMTHENIFN=.THENPD=V:PH=PE:PT=PHANDPV:GOTO4080
4070 IFMTHENIFM=NTHENPE=V:PD=V:PH=V:PT=PV
4080 IFPVTHEN4130
4090 M=T(J,I):N=T(J+1,I)
4100 IFNOT(PC)THENIFNTHENIFM=.THENPC=V:PV=PB:PT=PHANDPV:GOTO4130
4110 IFNOT(PB)THENIFMTHENIFN=.THENPB=V:PV=PC:PT=PHANDPV:GOTO4130
4120 IFMTHENIFM=NTHENPC=V:PB=V:PV=V:PT=PT
4130 IFPTTHENJ=2:I=3
4140 NEXTJ,I
4150 FJ=NOT(PCORPBORPEORPD)
4160 RETURN 
5000 FORI=0TO3:FORJ=0TO3
5020 PRINT@32*(5+I*2)+(7+J*5),N$(T(I,J));
5030 NEXTJ,I
5040 RETURN 
6000 I=INT(RND(0)*4):J=INT(RND(0)*4)
6020 IFT(I,J)=.THENT(I,J)=1+ABS(RND(0)>.9):RETURN
6030 J=J+1:IFJ=4THENJ=.:I=I+1:IFI=4THENI=.
6040 GOTO6020
7000 MC=F:MB=F:ME=F:MD=F:MV=F:MH=F
7020 GOSUB9510
7030 I$=INKEY$
7040 IFPCTHENIFI$=MC$THENPRINT@112,"W";:MC=V:MV=V:RETURN 
7050 IFPBTHENIFI$=MB$THENPRINT@432,"S";:MB=V:MV=V:RETURN 
7060 IFPETHENIFI$=ME$THENPRINT@261,"A";:ME=V:MH=V:RETURN 
7070 IFPDTHENIFI$=MD$THENPRINT@283,"D";:MD=V:MH=V:RETURN 
7080 GOTO7030
8000 IFOATHENPRINT@37,;:M$="OBJECTIVE ATTAINED! :-D":GOSUB7:GOTO 8030
8020 PRINT@37,;:M$="BOARD IS BLOCKED... :-(":GOSUB7
8030 PRINT@488,;:M$="PLAY AGAIN (Y/N)?":GOSUB7
8040 I$=INKEY$
8050 IFI$="Y"THENRUN
8060 IFI$="N"THENCLS:END 
8070 GOTO8040
9000 C1=20000:FORI=0TO4:READCC:POKEC1+I,CC:NEXT:DATA198,32,126,251,214
9010 V=-1:F=0
9100 DIMT(3,3):GOSUB6000:GOSUB6000
9110 DIMN$(10):FORI=0TO10:READN$(I):NEXT
9120 DATA "    ","  2 ","  4 ","  8 "," 16 "," 32 "," 64 "," 128"," 512","1024","2048"
9130 MC$="W":MB$="S":ME$="A":MD$="D"
9140 OA=F
9150 FJ=F
9500 CLS:PRINT"              2048              ";:EXEC20000
9510 PRINT@112,"w";:PRINT@432,"s";:PRINT@261,"a";:PRINT@283,"d";:RETURN
