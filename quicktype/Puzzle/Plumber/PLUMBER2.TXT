0 CLS:CLEAR500:M=16384:GOSUB6000:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
8 FORZZ=1TO3:CC=PEEK(9)AND128:POKE49151,CC+64:POKE49151,192-CC:NEXTZZ:RETURN
10 CLEAR500:CC=0:ZZ=0:M$="":I$="":M=16384
20 LM=1:AX=5:AY=3:DL=800:DM=31:SD=20:SW=32
30 BK=175:BA$=CHR$(8)
40 SP$=CHR$(32):MX=AX*5+3:MY=AY*4+3
50 DIMMP(MX,MY),V(5,2),Z$(3)
60 Z$(0)="BLINKING":Z$(1)="SOAKING"
70 Z$(2)="QUIT?":Z$(3)="SURFACE?"
80 BL$=CHR$(BK):FORX=2TOMX:BL$=BL$+CHR$(BK):NEXT
90 FORX=1TOHX:FORY=1TOMY:MP(X,Y)=BK:NEXTY,X
100 VC=5:RY=2:FORY=2TOMY-1
110 IFY=RYTHENS=1:RY=RY+AY:GOTO120
111 S=AX:FORX=2TOMX:MP(X,Y)=32:NEXTX
120 FORX=2TOMX-1STEPS:MP(X,Y)=128:NEXTX,Y
130 FORY=1TO5:READT$:FORX=1TO6
140 T=ASC(MID$(T$,X)):T=T-(T=66)*62
150 MP(AX*(X-1)+2,AY*(Y-1)+2)=T:NEXTX,Y
160 FORL=1TO5:READT:Y=INT(T/6):X=T-6*Y
170 V(L,1)=X*AX+2:V(L,2)=Y*AY+2:NEXTL
180 SEL=0:CX=4*AX+2:CY=3*AY+2:PM$="c":GOSUB6060
190 CLS0:POKE49151,64:CLS:FORY=1TOMY:PRINT@32*(Y-1),LEFT$(BL$,2);:FORX=1TOMX
200 PRINTCHR$(MP(X,Y));:NEXTX:IFY=1ORY=16THENPRINT:GOTO203
202 PRINT:REM PRINT@32*Y+3,"€    €    €    €    €    €"
203 NEXTY:PRINT@32*14+13,LEFT$(BL$,16);:PRINT@32*1+29,LEFT$(BL$,3);
205 PRINT@9,"TOWN OF GRINDEL":PRINT@480,"ijkl=MOVE, s=SURFACE AND q=QUIT";
210 GOSUB1000:SEL=ABS((K=81)*2+(K=83)*3)
220 IFSEL=0THEN270
230 CLS:N$="DO YOU WANT TO "+Z$(SEL):GOSUB3000
240 GOSUB1000:IFK<>78ANDK<>89THENGOSUB8:GOTO240
250 IFK=78THENSEL=0:GOTO190
260 ONSEL-1GOTO580,410
270 DX=(K=74)-(K=76):DY=(K=73)-(K=75)
280 IFDX=0ANDDY=0THEN320
290 TX=CX+DX*5:TY=CY+DY*3:IFTX>27THENTX=27
291 IFTX<2THENTX=2
292 IFTY<2THENTY=2
293 IFTY>14THENTY=14
300 T=MP(TX,TY):IFT=BKTHEN330
310 CX=TX:CY=TY:PM$=CHR$(T):GOTO210
320 IFK=67ORK=79THEN340
330 GOSUB8:GOTO210
340 T=MP(CX,CY):IFT-32=KOR(T<>99ANDT<>111)THEN330
350 MP(CX,CY)=K+32:PM$=CHR$(K+32):GOSUB2000
360 VC=VC-(K=79)+(K=67):IFVC<6THEN210
370 FORD=1TODL:NEXTD:X=LM+1
380 FORY=MYTO1STEP-1:GOSUB4000:PRINTBL$;
390 FORD=1TODL/3:NEXTD,Y
400 FORD=1TODL:NEXTD:F=1:GOTO450
410 F=0:IFVC<>5THEN450
420 FORL=1TO5
430 IFMP(V(L,1),V(L,2))=111THENVC=VC-1
440 NEXTL:IFVC=0THENVC=-1
450 CLS:M$="LUKE EMERGES "+Z$(F)
460 M$=M$+" INTO THE BRIGHT SUNLIGHT AND IS GREETED"
470 M$=M$+" BY THE TOWNFOLK, WHO SHOUT,"
480 IFLEN(M$)<=SWTHENN$=M$:GOSUB3000:GOTO530
490 B=SW+1:C=1:FORA=BTO2STEP-1
500 IFMID$(M$,A,1)=SP$THENC=0:B=A:A=2
510 NEXTA:N$=LEFT$(M$,B-1):GOSUB3000
520 M$=RIGHT$(M$,LEN(M$)-B+C):GOTO480
530 FORD=1TODL*3:NEXTD
540 CLS:IFVC=-1THEN570
550 FORI=1TODM:PRINT"YOU'RE FIRED! ";
560 FORD=1TOSD:NEXTD,I:GOTO580
570 N$="WHY, YOU CLEVER WATER RAT, YOU!":GOSUB3000
580 PRINT@456,"TRY AGAIN? (y/n)";
590 M$=INKEY$:IFM$="Y"THENPRINT@448,"":GOTO10
600 IFM$<>"N"THEN590
610 POKE49151,0:END
1000 K$=INKEY$:IFSEL=0THENGOSUB2000
1010 IFK$=""THEN1000
1011 K=ASC(K$)
1020 K=K+32*(K>96)*(K<123):RETURN
2000 Y=CY:X=LM+CX:GOSUB4000
2010 PRINTCHR$(207);:FORZZ=1TO25:NEXT:PRINTBA$;PM$;:FORZZ=1TO25:NEXT:RETURN
3000 IFLEN(N$)=SWTHENPRINTN$;:RETURN
3010 PRINTTAB((SW-LEN(N$))/2);N$:RETURN
4000 PRINT@SW*(Y-1)+X,;:RETURN
5000 DATAeBBcee,eBcBoe,cco+oc,eoeoce,ceecee
5010 DATA3,13,16,17,19
6000 M$="  SUBTERRANEAN PLUMBER'S BLUES  ":GOSUB7
6003 M$="    BY STEVEN C.M. CHEN 1986    ":GOSUB7
6005 M$="YOU ARE LUKE WATERMANE, THE TOWN OF GRINDEL'S NEW PLUMBER.":GOSUB1
6010 M$="YOU MUST oPEN AND cLOSE THE VALVES OF THE OLD UNDERGOUND WATER SYSTEM SO "
6020 M$=M$+"THAT CERTAIN eSTABLISHMENTS OF TOWN CAN GET EXTRA WATER FROM THE +SOURCE.":GOSUB1
6030 M$="BUT DO NOT OPEN MORE THAN 5 VALVES AT A TIME OR THE SYSTEM WILL "
6040 M$=M$+"BREAK DOWN AND THE TOWN WILL BE FLOODED. WATER FLOWS THROUGH THE PIPES AND oS.":GOSUB1
6050 PRINT"o=OPEN VALVE. c=CLOSED VALVE":M$="+":PRINT@8*32+21,;:GOSUB7:RETURN
6060 PRINT@486,;:M$="HIT ANY KEY TO BEGIN":GOSUB7
6070 M$=INKEY$:IFM$=""THEN6070
6080 RETURN
6090 REM MC-10 EDITS JIM GERRIE
