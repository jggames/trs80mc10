
1 REM HURKLE 12/83
2 REM 
3 REM PRACTICE IN USING GRIDS
4 REM
5 REM RADIO SHACK COLOR BASIC
6 REM 16K
7 REM AUTHOR UNKNOWN
8 REM TRANSLATED FROM THE RADIO       SHACK MODEL 3 SOFTSWAP          BY JASON LAWHERN
9 REM
10 REM
11 REM ***********************
12 REM *                     *
13 REM *  SAN MATEO COUNTY   *
14 REM * OFFICE OF EDUCATION *
15 REM *        AND          *
16 REM *   COMPUTER-USING    *
17 REM *      EDUCATORS      *
18 REM *                     *
19 REM *      SOFTSWAP       *
20 REM *                     *
21 REM *   333 MAIN STREET   *
22 REM *     REDWOOD CITY    *
23 REM *       CA 94063      *
24 REM *    (415) 363-5472   *
25 REM *                     *
26 REM *                     *
27 REM ***********************
28 REM *PROGRAM MAY BE COPIED*
29 REM *FOR EDUCATIONAL USE &*
30 REM *MAY NOT BE SOLD.COPY-*
31 REM *RIGHT 1983 CUE,INC   *
32 REM ***********************
100 REM HURKLE
105 CLS
110 CLEAR 500
120 DIM GRID$(12,12)
130 A$="HURKLE":PRINT@15-(LEN(A$)/2),A$;
160 PRINT@64,"     HOW LARGE DO YOU WANT THE  GRID TO BE (4 X 4 TO 9 X 9)?    ENTER THE NUMBER OF COLUMNS AND THE NUMBER OF ROWS SEPARATED BY A COMMA.";CHR$(31);
170 INPUT C$,R$
180 IF R$="" OR C$="" THEN 160
190 IF ASC(R$)>57 OR ASC(C$)>57 THEN 160
200 R=VAL(R$):C=VAL(C$)
210 IF C<4 OR R<4 THEN 160
220 IF R>9 OR C>9 THEN 160
230 GOSUB 440
240 CLS:PRINT@0,"WOULD YOU LIKE INSTRUCTIONS?";
250 I$=INKEY$:IF I$="" THEN 250
260 IF I$="N" THEN 290
270 IF I$<>"Y" THEN 250
280 GOSUB 830 'INSTRUCTIONS
290 GOSUB 530 'DRAW GRID
300 GOSUB 620 
310 GOSUB 690 'GET COLUNM AND ROW
329 PRINT@384," ":PRINT @384,"GO ";
330 IF Y=B THEN 380
340 IF Y<B THEN 370
350 PRINT"NORTH";
360 GOTO 380
370 PRINT"SOUTH";
380 IF X=A THEN 430 
390 IF X<A THEN 420
400 PRINT"WEST";
410 GOTO 430
420 PRINT"EAST";
430 GOTO 300
435 REM ****           ****
440 REM **** DRAW GRID ****
445 REM ****           ****
450 CLS:A=RND(C):B=RND(R)
460 T$="O":H$="o"
470 FOR ROW= 1 TO R
480 FOR COLUMN = 1 TO C
490 GRID$(COLUMN,ROW)=H$
500 NEXT COLUMN
510 NEXT ROW
520 RETURN
530 CLS:PRINT@0," ";
540 PRINT;:FOR I= 1 TO C:PRINTUSING"##";I;:NEXTI:PRINT
550 FOR ROW= 1 TO R
560 PRINTUSING"#";ROW;
570 FOR COLUMN= 1 TO C
580 PRINT" ";GRID$(COLUMN,ROW);
590 NEXT COLUMN
600 PRINT
610 NEXT ROW
620 N$="  NORTH  "
630 E$="WEST+EAST"
640 S$="  SOUTH  "
650 PRINT@31-LEN(N$),N$;
660 PRINT@63-LEN(E$),E$;
670 PRINT@95-LEN(S$),S$;
680 RETURN
685 REM ****
690 REM **** GET ROW AND COLUMN
695 REM ****
700 R1$="":C1$=""
710 PRINT@416," ":PRINT@416,"ENTER (COLUMN,ROW)";CHR$(30);
720 INPUT C1$,R1$
725 PRINT@416+32," "
730 IF C1$="" OR R1$="" THEN 710
740 IF ASC(C1$)>57 OR ASC(R1$)>57 THEN 710
750 R1=VAL(R1$):C1=VAL(C1$)
760 IF C1>C OR R1>R OR C1<1 OR R1<1 THEN 710
770 IF C1=A AND R1=B THEN 1010 '    WIN
780 L=L+1
790 IF L>4 THEN 1050 'LOSE
800 PRINT@(32*R1)+(C1*2),T$;
810 X=C1:Y=R1
820 RETURN
825 REM ****              ****
830 REM **** INSTRUCTIONS ****
835 REM ****              ****
840 CLS
850 PRINT"A HURKLE IS HIDING IN A ";C"BY";R;"GRID. TRY TO FIND THE HURKLE BY GUESSING WHAT COLUMN AND ROW IT IS IN."
860 PRINT"YOUR GUESS MUST NOT BE LARGER   THAN";C;"BY";R;"."
870 PRINT
880 PRINT"EXAMPLE..."
890 PRINT
900 PRINT"3,4 MEANS 3RD COLUMN,4TH ROW."
910 PRINT
920 PRINT"AFTER EACH TRY,  I WILL HELP YOUBY TELLING YOU THE DIRECTION TO GO TO FIND THE HURKLE. YOU HAVE FIVE (5) GUESSES."
930 GOSUB 950
940 RETURN
945 REM ****       ****
950 REM **** PAUSE ****
955 REM ****       ****
960 M$="PRESS ANY KEY TO CONTINUE"
970 PRINT@495-(LEN(M$)/2),M$;
980 I$=INKEY$:IF I$="" THEN 980
990 CLS
1000 RETURN
1010 CLS
1020 REM
1030 PRINT"YOU FOUND THE HURKLE IN";L+1;"        MOVE(S) !!!"
1040 GOTO 1090
1050 REM 
1060 CLS
1070 PRINT"SORRY THAT WAS 5 MOVES."
1080 GOTO 1090
1090 PRINT
1100 PRINT"WOULD YOU LIKE TO PLAY AGAIN ?"
1110 I$=INKEY$:IF I$="" THEN 1110
1120 IF I$="Y" THEN RUN ELSE IF I$="N" THEN 1140 ELSE GOTO 1110
1140 PRINT
1150 PRINT"IS THERE ANYONE ELSE WAITING TO PLAY?"
1160 I$=INKEY$:IF I$="" THEN 1160
1170 IF I$="Y" THEN RUN ELSE IF I$="N" THEN 1190 ELSEGOTO 1160
1190 PRINT"O.K. GOODBYE FOR NOW."
1200 END
