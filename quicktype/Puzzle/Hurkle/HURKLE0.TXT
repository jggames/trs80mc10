10 PRINT "             HURKLE"
20 PRINT "     (C) CREATIVE COMPUTING"
25 PRINT "ADAPTED FOR MC-10 BY CHRIS HAVEL"
30 PRINT
110 N=7
120 G=10
210 PRINT
220 PRINT "A HURKLE IS HIDING ON A";G;"BY";G
225 PRINT "GRID. HOMEBASE ON THE GRID IS"
230 PRINT "POINT 0,0 AND ANY GRIDPOINT IS A"
240 PRINT "PAIR OF WHOLE NUMBERS SEPARATED"
245 PRINT "BY A COMMA. TRY TO GUESS THE"
250 PRINT "HURKLE'S GRIDPOINT. YOU GET";N
255 PRINT "TRIES. AFTER EACH TRY, I WILL"
260 PRINT "TELL YOU THE APPROXIMATE"
265 PRINT "DIRECTION TO GO TO LOOK FOR THE
270 PRINT "HURKLE."
280 PRINT
285 A=INT(G*RND(1))
286 B=INT(G*RND(1))
310 FOR K=1 TO N
320 PRINT "GUESS #";K;
330 INPUT X,Y
340 IF ABS(X-A)+ABS(Y-B)=0 THEN 500
350 REM PRINT INFO
360 GOSUB 610
370 PRINT
380 NEXT K
410 PRINT
420 PRINT "SORRY, THAT's";N;"GUESSES."
430 PRINT "THE HURKLE IS AT ";A;",";B
440 PRINT
450 PRINT "LET'S PLAY AGAIN. THE HURKLE IS
455 PRINT "HIDING."
460 PRINT
470 GOTO 285
500 REM
510 PRINT
520 PRINT "YOU FOUND HIM IN";K;"GUESSES!"
540 GOTO 440
610 PRINT "GO ";
620 IF Y=B THEN 670
630 IF Y<B THEN 660
640 PRINT "SOUTH";
650 GOTO 670
660 PRINT "NORTH";
670 IF X=A THEN 720
680 IF X<A THEN 710
690 PRINT "WEST";
700 GOTO 720
710 PRINT "EAST";
720 PRINT
730 RETURN
999 END