10 CLS:PRINT"  SIMPLIFIED WATCHMAN PROGRAM":PRINT
11 PRINT"YOU MUST PATROL THE WHOLE":PRINT"VILLAGE WITHOUT RETRACING"
12 PRINT"YOUR STEPS":PRINT
20 DIM A(425),D(23):REM A(17,25)=15X23 FRAMED WITH ZEROES
30 REM...GOSUB TO PRINT INSTRUCTIONS (OMITTED)
40 V=ASC("*"):REM VALUE SOUGHT TO STAY ON ROAD
50 GOSUB 1000:REM READ MAP, COUNT STEPS, LABEL CORNERS
60 PRINT "VILLAGE MAP:"
70 GOSUB 2000:REM PRINT MAP
80 FORCC=1TO4:READ B(CC):NEXT:REM N, S, E, W BEARINGS
100 C1=26:REM COLUMNS+1
110 D4=1:REM INITIAL SEARCH DIRECTION
120 PRINT "START AT WHAT CORNER (1-"MID$(STR$(N),2)")";
130 INPUT J
140 L=C(J)
150 PRINT "ENTER HEADING (0 FOR ROUTE MAP)":PRINT"(::::=FOOTPRINTS)
160 PRINT "HEADINGS: N=1, S=2, E=3, W=4"
170 GOSUB 500:REM SEARCH FOR UNTROD PATH
180 IF T=0 THEN 390:REM NONE FOUND?
190 PRINT "YOU'RE AT CORNER"A(L)-48"HEADING";
200 INPUT H
210 IF H>0 AND H<5 THEN 240
220 GOSUB 2000
230 GOTO 170
240 D4=B(H)
250 C1=26*SGN(D4)
260 H1=D4:REM SAVE INITIAL HEADING
270 GOSUB 500
280 IF D4=H1 THEN 330:REM NO FORCED TURN?
290 PRINT "***ILLEGAL MOVE--TRY AGAIN"
300 GOTO 200
310 GOSUB 500:REM ---->NEXT STEP
320 IF T=0 THEN 370:REM BLOCKED?
330 L=T:REM MOVE FWD OR TURN
340 A(L)=ASC(":"):REM MAKE FOOTPRINT, BLOCK RETRACE STEP
350 S=S+1:REM COUNT STEP
360 GOTO 310
370 L=L+D4:REM STEP TO CORNER
380 GOTO 170:REM REPLACE WITH GOSUB60 FOR NON-TRS-80 COMPUTERS
390 PRINT "YOU'RE TRAPPED AT CORNER"A(L)-48:PRINT"WANT FINAL MAP (Y/N)";
400 INPUT A$
410 IF LEFT$(A$,1)<>"Y" THEN 430
420 GOSUB 2000
430 IF S=S9 THEN 460:REM ALL STEPS TROD?
440 GOSUB4000:REM OMIT FOR NON-TANDY COMPUTERS AND UNREM NEXT LINE
441 REM PRINT "YOU WERE SUPPOSED TO PATROL":PRINT"THE WHOLE VILLAGE!"
450 GOTO 9000
460 PRINT "CONGRATULATIONS! TRY AGAIN";:REM WINNER
470 GOTO 9010
500 REM ***SUB*** TRACKING RT-ANGLE SEARCH ON. LINEAR ARRAY
510 FOR I=1 TO 2
520 FOR J=1 TO 2
530 T=L+D4
540 IF A(T)<>V THEN 560
550 RETURN
560 D4=C1-D4
570 NEXT J
580 D4=D4-C1
590 C1=-C1
600 NEXT I
610 T=0
620 RETURN
1000 REM -SUB- INPUT STREET PLAN
1010 V0=ASC("0")
1020 FOR I=27 TO 377 STEP 25
1030 READ D$
1040 FORCC=1TO23:D(CC)=ASC(MID$(D$,CC,1)):NEXT
1050 J=0
1060 FOR L=I TO I+22
1070 J=J+1
1080 T=D(J)
1090 A(L)=T
1100 IF T<>V THEN 1120
1110 S9=S9+1
1120 IF T<>V0 THEN 1160
1130 N=N+1
1140 C(N)=L
1150 A(L)=N+48
1160 NEXT L
1170 NEXT I
1180 DATA "      0*********0      "
1190 DATA "      * 1ST ST. *      "
1200 DATA "      *         *      "
1210 DATA "******0*********0******"
1220 DATA "*1   2* 2ND ST. *3   4*"
1230 DATA "*S   N*         *R   T*"
1240 DATA "*T   D* 3RD ST. *R   H*"
1250 DATA "*     0*********0     *"
1260 DATA "*A   A*         *A   A*"
1270 DATA "*V   V*         *V   V*"
1280 DATA "*E   E* 4TH ST. *E   E*"
1290 DATA "*N    0*********0******"
1300 DATA "*U              *      "
1310 DATA "*E      5TH ST. *      "
1320 DATA "*****************      "
1330 RETURN
2000 REM -SUB- PRINT STREET MAP
2010 PRINT
2020 FOR I=27 TO 377 STEP 25
2030 FOR J=I TO I+22
2040 PRINT CHR$(A(J));
2050 NEXT J
2060 PRINT
2070 NEXT I
2080 GOSUB3010:REM REPLACE THIS GOSUB WITH 'PRINT' FOR NON-TRS-80 COMPUTERS
2090 RETURN
3000 DATA -25,25,1,-1
3010 PRINT@11*32+24,"headings";
3020 PRINT@12*32+24,"1=N  2=S";
3030 PRINT@13*32+24,"3=E  4=W";
3040 PRINT@14*32+24,"0=MAP   ";:RETURN
4000 PRINT@8*32+24, "YOU WERE";
4020 PRINT@9*32+24, "SUPPOSED";
4030 PRINT@10*32+24,"   TO   ";
4031 PRINT@11*32+24," PATROL ";
4040 PRINT@12*32+24,"   THE  ";
4041 PRINT@13*32+24,"  WHOLE ";
4042 PRINT@14*32+24,"VILLAGE!";:RETURN
9000 PRINT "WANT TO TRY AGAIN";
9010 INPUT M$:IFLEFT$(M$,1)="Y"THENRUN
9020 END
9030 REM MAC OGLESBY
9040 REM SCOTT DAVIDSON 1978
9050 REM CREATIVE COMPUTING
9060 REM MC-10 EDITS JIM GERRIE
