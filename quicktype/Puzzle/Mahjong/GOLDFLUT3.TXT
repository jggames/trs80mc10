0 CLEAR500:DIMCC,TT,M$,I$,X,Y,Z,A(100),B(100),C(9),C$(29):Z=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 TT=1:CC=32:FORCC=CCTOTTSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,TT,CC-TT):TT=CC+1:CC=TT+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 TT=LEN(M$):PRINTTAB((32-TT)/2)M$;:RETURN
5 ONVGOSUB7,8,9,10,11,12,6,6,13,14,6,15,16,16,17,6,6,6,6,18,19,20,6,6,6,6,21,22,23
6 DATA".","W","M","P","T","S","D",".",".","G","g",".","C","X","X","F",".",".",".","."
7 DATA"*","s","d",".",".",".",".","R","!","w"
19 REM * SET UP *
20 FORTT=0TO29:READC$(TT):NEXT
30 CLS:PRINT"€€€Ñ€€€€Ñµ··°°°°€ÐÕßßÚ€€’›";
31 PRINT"€“““€ÕÓßßÛ×€µ€·¿¿²€€ÕßßÚ€—Ÿ’žŸ";
32 PRINT"•ÎÎÏšÑÐÕ××ß€€µ€»³·º€€ÝÝÝÞ€€›“šš˜";
33 PRINT"•ÌÏÌšÕÐÕßßß€€µ·½¿º·½ºÖßßÙ•ž“•››€";
34 PRINT"•åïêšÝÛÞßßÕß€½¸µ¿º»±ºßÓÓß•’—•žž";
35 PRINT"•äïèšÐÐÑßßÖÜ€µ°¼¾½´¾€ÝßßÞ€™š˜";
36 PRINT"•€ï€š€Ð€ÚÐÚ€€µ°°ºµ°€€ÔÞÝØ€Ÿ•š";
37 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
40 PRINT:PRINT:M$="THE GOLDEN FLUTE":GOSUB3:PRINT
50 PRINT:M$="BY DELTON T. HORN":GOSUB3:PRINT:PRINT:PRINT
55 PW=0:GW=0
60 INPUT"WHAT IS YOUR NAME";N$
61 IF N$="" THEN M$="TRY AGAIN...":GOSUB3:PRINT:GOTO 60
70 BR=1:DG=1:GR=1:RU=1:SJ=1:JS=1:AL=1:PM=1 
80 AX=0:LC=1:M=1:SW=0:FL=0:MO=0:GL=0:MZ=0
90 FOR X=1 TO 100:A(X)=0:B(X)=0:NEXT
95 FOR X=1 TO 9:C(X)=1:NEXT
100 PRINT:M$="SETTING VARIABLES":GOSUB3
109 REM * WOODS *
110 A(1)=1:A(2)=1:A(11)=1:A(12)=1
119 REM * ORACLE *
120 A(33)=2
129 REM * PITS *
130 X=RND(7)+2:Y=RND(4)+4:X=X+Y*10:Y=X+1 
140 A(X)=3:A(Y)=3:X=X+10:Y=Y+1:A(X)=3:A(Y)=3
149 REM * WALL *
150 Q=0
160 Q=Q+1:X=(RND(4)+3)*10+RND(3)+3:Y=X+5:Z=0 
170 IF Q>7 GOTO 220
180 FOR V=X TO Y:IF A(V)>0 THEN Z=1
190 IF A(V+10)>0 THEN Z=1
200 NEXT:IF Z=1 GOTO 160
210 FOR V=X TO Y:A(V)=13:A(V+10)=14:NEXT
220 FOR X=1 TO 100:B(X)=A(X):NEXT
229 REM * HIDDEN ITEMS *
230 X=RND(100)
232 IF B(X)>0 GOTO 230
235 B(X)=29
239 REM * TERAK & GUARDS *
240 X=(RND(4)+4)*10+RND(7)+2:IF B(X)>0 GOTO 240
250 B(X)=4:Z=9:Y=X-1:GOSUB 5010
260 Y=X+10:GOSUB 5010
270 Y=X-10:GOSUB 5010
280 Y=X+1:GOSUB 5010
290 Y=Y+10:GOSUB 5010
300 Y=Y-20:GOSUB 5010
309 REM * GARGOYLES *
310 U=RND(10)+2:FOR X=1 TO U:Y=RND(100):GOSUB 5010:NEXT
320 Y=RND(70)+30:PRINT"*";: IF B(Y)>0 GOTO 320
330 B(Y)=5
340 Y=RND(100):IF B(Y)>0 GOTO 340
350 B(Y)=21:FOR X=1 TO 3
360 Y=RND(100):IF B(Y)>0 GOTO 360
370 B(Y)=22:NEXT
380 Y=RND(50):IF B(Y)>0 GOTO 380
390 B(Y)=23
400 Y=RND(50):IF B(Y)>0 GOTO 400
410 B(Y)=24
420 Y=RND(90):IF B(Y)>0 GOTO 420
430 B(Y)=25
440 Y=RND(80)+15:IF B(Y)>0 GOTO 440
450 B(Y)=26:FOR X=1 TO 5
460 Y=RND(80)+15:IF B(Y)>0 GOTO 460
470 B(Y)=27:NEXT
480 Y=RND(100):IF B(Y)>0 GOTO 480
490 B(Y)=26
500 Z=6:FOR X=1 TO 10:Y=RND(80)+20:GOSUB 5010:NEXT
510 Z=8:FOR X=1 TO 10:Y=RND(100):GOSUB 5010:NEXT
520 Y=RND(100):IF B(Y)> 0 GOTO 520
530 B(Y)=7:Z=10:FOR X=1 TO 10:Y=RND(95)+5:GOSUB 5010:NEXT
540 Y=RND(100):IF B(Y)>0 GOTO 540
550 B(Y)=11:QQ=0
560 F=RND(100):IF B(F)>0 GOTO 560
570 B(F) =15
580 Y=RND(55)+45:IF B(Y)>0 GOTO 580
590 B(Y)=17:BZ=0
599 REM * INTRO *
600 CLS
610 M$="YOU HAVE JOINED THE MAGICAL CREATURES OF THE WOODLANDS IN THEIR "
620 M$=M$+"QUEST TO RECOVER THE GOLDEN FLUTE OF LOVE FROM THE "
630 M$=M$+"CLUTCHES OF THE EVIL GREMLIN, TERAK.":GOSUB1:M$="YOU MUST RETURN IT TO THE "
640 M$=M$+"WOODLANDS.":GOSUB1:M$="AT ALL TIMES WATCH OUT FOR DRAGONS, SIRENS, "
650 M$=M$+"GOBLINS, GARGOYLES, AND THE DREADED HOPELESS PITS!":GOSUB1
660 M$="CORAMBLE, THE GREAT ORACLE OF PURLICON MOUNTAIN MAY BE OF "
670 M$=M$+"ASSISTANCE.":GOSUB1
680 INPUT"DO YOU WANT INSTRUCTIONS";Q$:Q$=LEFT$(Q$,1)
690 IF Q$="Y" THEN GOSUB 5030
695 CLS:LC=1:A(LC)=12
699 REM * CHARIOT CONTENTS *
700 PRINT:GOSUB 5000
710 PRINT"THE MAGIC CHARIOT IS CARRYING:"
720 M$=N$+" THE HUMAN, "
730 IF BR=1 THENM$=M$+"BROMBIRAN THE ELF, "
740 IF DG=1 THENM$=M$+"DAGGLETTE THE ELF, "
750 IF GR=1 THENM$=M$+"GROMPHLUR THE ELF, "
760 IF RU=1 THENM$=M$+"RULF THE SATYR, "
770 IF SJ=1 THENM$=M$+"SEJJAN THE FAIRY, "
780 IF JS=1 THENM$=M$+"JESSAN THE FAIRY, "
790 IF AL=1 THENM$=M$+"ALLEGRECIA THE QUEEN OF THE SPRITES, "
800 IF PM=1 THENM$=M$+"PRINCESS MELVA, "
801 IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2):GOSUB1
810 IF FL=1 THENM$=M$+"THE GOLDEN FLUTE, "
820 IF MO=1 THENM$=M$+"A MAGIC ORB, " 
830 IF SW=1 THENM$=M$+"A MAGIC SWORD, "
840 IF BZ=1 THENM$=M$+"A MAGIC BAZOOKA, "
850 IF GL<1 GOTO 880
860 M$=M$+MID$(STR$(GL),2)+" PIECE":IF GL>1 THENM$=M$+"S"
870 M$=M$+" OF GOLD, "
880 IF MZ=1 THENM$=M$+"A MAGIC ZITHER, "
881 IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2):GOSUB1
890 REM INPUT"PLEASE PRESS 'enter' ";Q$
899 REM * MAIN PLAY *
900 ZZ=LC:PRINT"LOCATION #";LC,"MOVE #";M
910 INPUT"YOUR MOVE? (ENTER 'k' FOR KEY)";Q$:Q$=LEFT$(Q$,1) 
920 IF Q$="K" GOTO 4000
925 IF Q$="M" GOTO 7000
930 IF Q$="B" AND BZ=1 GOTO 4030
935 M=M+1:A(LC)=B(LC)
940 IF Q$="D" GOTO 1000 
950 IF Q$="U" GOTO 1190 
960 IF Q$="R" GOTO 1200 
970 IF Q$="L" GOTO 1220
980 M$="INVALID MOVE!":GOSUB3:PRINT
990 GOTO 910
1000 LC=LC+10:IF LC>100 GOTO 1020
1010 GOTO 4250
1015 M$="IT TOOK YOU"+STR$(M)+" MOVES.":GOSUB1:RETURN
1020 PRINT:M$="THE MAGIC CHARIOT HAS LEFT THE BOUNDARIES OF THE MAGIC KINGDOM":GOSUB1:PRINT
1030 GOSUB 5000
1040 M$="WITHOUT MAGIC TO HOLD IT UP, THE MAGIC CHARIOT CRASHES!":GOSUB1:PRINT
1050 GOSUB 5000:M=M-1:PRINT"YOU LOSE THIS TIME."
1060 GOSUB1015
1070 IF PW=0 OR PW=M GOTO 1100
1080 M$="PREVIOUS WIN RECORD WAS "+STR$(PW):GOSUB1
1100 PRINT
1110 GW=GW+1:PRINT"THIS WAS GAME #";GW
1120 PRINT:INPUT"PLAY AGAIN";Q$:Q$=LEFT$(Q$,1)
1130 IF Q$="Y" GOTO 8000
1140 END
1150 M=M-1:PRINT"YOU WIN THIS TIME!":PRINT:GOSUB 1015
1160 IF PW=0 GOTO 1175
1170 M$="PREVIOUS BEST SCORE WAS"+STR$(PW):GOSUB1:IF PW<M GOTO 1100
1175 PW=M:GOTO 1100
1190 LC=LC-10:IF LC<1 GOTO 1020
1191 GOTO 4250
1200 LC=LC+1:X=LC/10:Y=(X-INT(X))*10:IF Y=0 OR Y>1.5 GOTO 4250
1210 GOTO 1020
1220 LC=LC-1:X=LC/10:Y=(X-INT(X))*10:IF Y=0 GOTO 1020
1230 GOTO 4250
4000 PRINT"u=UP,d=DOWN,r=RIGHT,l=LEFT,m=MAP";
4010 IF BZ>0 THENPRINT"b=FIRE BAZOOKA,";
4020 PRINT"k=KEY":GOTO 910
4030 INPUT"BAZOOKA LOADED-AIM? (X=DISARM)";Q$
4040 Q$=LEFT$(Q$,1):IF Q$="X" GOTO 900
4050 IF Q$="U" GOTO 4100
4060 IF Q$="D" GOTO 4160
4070 IF Q$="R" GOTO 4190
4080 IF Q$="L" GOTO 4210
4090 GOTO 4030
4100 BU=10000:BS=1:BT=-10
4110 M$="KER-":GOSUB3:PRINT:FORX=1TO25:SOUNDX,1:NEXT:M$="POW!":GOSUB3:PRINT:PRINT:X=LC
4120 X=X+BT:IF X<BS GOTO 900
4130 IF X>BU GOTO 900
4140 IF X<1 OR X>100 GOTO 900
4150 A(X)=20:B(X)=20:GOTO 4120
4160 BU=10000:BS=0:BT=10
4170 GOTO 4110
4180 M$="PHFFFT!":GOSUB3:PRINT:PRINT:GOTO 900
4190 G=INT(LC/10):IF G=LC/10 GOTO 4180
4200 BS=0:BU=G*10+10:BT=1:GOTO 4110
4210 G=INT(LC/10):H=LC/10-G:IF H=0.1 GOTO 4180
4220 BU=10000:BS=G*10+1:BT=-1:GOTO 4110
4250 PRINT:PRINT:A(LC)=12
4260 XX=B(LC)
4270 IF LC=1 OR LC=2 OR LC=11 OR LC=12 GOTO 10000
4280 IF LC=33 THENGOSUB 5120
4290 IF (LC>21 AND LC<25) OR (LC>41 AND LC<45) THENGOSUB 5300
4300 IF LC=32 OR LC=34 THENGOSUB 5300
4310 IFXX=4 THENGOSUB 5310
4320 P=RND(150):IF P>148 AND FL=0 GOTO 10030
4330 IF XX=3 GOTO 10080
4340 IF XX=5 GOTO 10200
4350 X=LC-1:GOSUB 5580
4360 X=LC+1:GOSUB 5580
4370 X=LC-10:GOSUB 5580
4380 X=X-1:GOSUB 5580
4390 X =X+2:GOSUB 5580
4400 X=LC+10:GOSUB 5580
4410 X=X-1:GOSUB 5580
4420 X=X+2:GOSUB 5580
4430 IF XX=6 GOTO 10240
4440 IF XX=7 THENGOSUB 5600
4450 IF XX=8 THENGOSUB 5610
4460 IF XX=9 GOTO 10250
4470 IF XX=10 GOTO 10260
4480 IF XX=11 THENGOSUB 5630
4490 IF XX=13 OR XX=14 GOTO 11700
4500 IF XX=15 GOTO 11780
4510 IF XX=17 THENGOSUB 5640
4520 IF XX=20 THEN M$="THIS AREA IS A SMOULDERING RUIN.":GOSUB1
4525 IF XX=21 GOTO 10270 
4527 IF XX=22 GOTO 10280 
4530 IF XX=23 THENGOSUB 5660 
4540 IF XX=24 THENGOSUB 5670 
4550 IF XX=25 THENGOSUB 5750 
4560 IF XX=26 THENGOSUB 6000 
4570 IF XX=27 GOTO 11800 
4580 IF XX=28 THENGOSUB 6010 
4590 IF XX=29 THENGOSUB 6050 
4900 GOTO 700
5000 FOR TT=1 TO 2500:NEXT:RETURN
5009 REM * BLANK SPACE CHECK *
5010 IF B(Y)=0 THEN B(Y)=Z
5020 PRINT"#";:RETURN
5028 PRINT"PLEASE PRESS 'enter' ";:INPUT Q$:RETURN
5029 REM * INSTRUCTIONS *
5030 CLS
5040 M$="EACH MOVE MAY BE UP, DOWN, RIGHT, OR LEFT. ONLY THE FIRST LETTER IS NEEDED. NEVER LEAVE THE MAP BOUNDARIES!":GOSUB1
5050 M$="IF YOU WANT TO SEE A MAP OF THE MAGIC KINGDOM, ENTER 'M' AS YOUR MOVE. THIS WILL NOT INCREMENT YOUR MOVE COUNTER."
5051 GOSUB1
5070 M$="IF YOU HAVE THE MAGIC BAZOOKA, YOU MAY ENTER 'B' TO FIRE.":GOSUB1:GOSUB5028
5080 CLS:M$="TO HAVE TERAK'S LOCATION DISPLAYED ON THE MAP, YOU MUST VISIT THE ORACLE ON PURLICON MOUNTAIN.":GOSUB1
5090 M$="IF YOU ARE CARRYING A MAGIC ORB HE MAY EVEN TELL YOU MORE. OR HE MAY NOT.":GOSUB1
5110 M$="THE OBJECT OF THE GAME IS TO RETURN THE GOLDEN FLUTE TO THE WOODLANDS IN THE FEWEST MOVES":GOSUB1
5115 GOSUB 5028:RETURN
5120 IF XX=20 GOTO 5290
5130 M$="CORAMBLE, THE GREAT ORACLE, REVEALS THAT TERAK'S LAIR IS AT LOCATION #"
5150 FOR Y=1 TO 100:IF B(Y)=4 GOTO 5170
5160 NEXT:IF MO=1 GOTO 5180
5161 RETURN 
5170 M$=M$+STR$(Y):GOSUB1:A(Y)=4:GOTO 5160
5180 GOSUB1:IF AX=3 THEN RETURN
5190 AX=AX+1:A=RND(6)+4:IF A=5 OR A=6 OR A=9 OR A=10 GOTO 5210
5200 RETURN
5210 M$="HE ALSO REVEALS THE LOCATION OF ALL:":GOSUB1
5220 IF A=5 THENPRINT"SIRENS"
5230 IF A=6 THENPRINT"DRAGONS"
5240 IF A=9 THENPRINT"GARGOYLES"
5250 IF A=10 THENPRINT"GOBLINS"
5260 M$="THEY WILL BE SHOWN ON YOUR NEXT MAP.":GOSUB1
5270 FOR X=1 TO 100:IF B(X)=A THEN A(X)=B(X)
5280 NEXT:RETURN
5290 M$="THE GREAT ORACLE'S BODY LIES SMOKING IN THE CORNER.":GOSUB1:RETURN
5300 M$="YOU ARE AT THE FOOT OF PURLICON MOUNTAIN.":GOSUB1:RETURN
5310 M$="YOU HAVE INFILTRATED TERAK'S LAIR!":GOSUB1:GOSUB 5000
5320 IF FL=1 GOTO 5360
5330 FL=1:PRINT"YOU RECOVER THE GOLDEN FLUTE!"
5340 IF MO>0 GOTO 5540
5350 RETURN
5360 D=RND(8):IF D=1 AND BR=0 GOTO 5530
5370 IF D=2 AND DG=0 GOTO 5530
5380 IF D=3 AND GR=0 GOTO 5530
5390 IF D=4 AND RU=0 GOTO 5530
5400 IF D=5 AND SJ=0 GOTO 5530
5410 IF D=6 AND JS=0 GOTO 5530
5420 IF D=7 AND AL=0 GOTO 5530
5430 IF D=8 AND PM=0 GOTO 5530
5440 IF D=1 THENPRINT"BROMBIRAN";:BR=0
5450 IF D=2 THENPRINT"DAGGLETTE";:DG=0
5460 IF D=3 THENPRINT"GROMPHLUR";:GR=0
5470 IF D=4 THENPRINT"RULF";:RU=0
5480 IF D=5 THENPRINT"SEJJAN";:SJ=0
5490 IF D=6 THENPRINT"JESSAN";:JS=0
5500 IF D=7 THENPRINT"ALLEGRECIA";:AL=0
5510 IF D=8 THENPRINT"PRINCESS MELVA";:PM=0
5520 PRINT" IS DEAD.":PRINT
5530 RETURN
5540 A=RND(3):IF A=1 GOTO 5360
5550 IF A=3 GOTO 5570
5560 RETURN
5570 M$="THE MAGIC ORB IS DESTROYED!":GOSUB1:MO=0:RETURN
5580 IF X<1 OR X>100 THEN RETURN
5590 IF B(X)=5 THENM$="AN EERIE SWEET SINGING IS HEARD IN THE DISTANCE.":GOSUB1
5595 RETURN
5600 PRINT:M$="YOU JUST FOUND A MAGIC SWORD!":GOSUB1:B(LC)=0:SW=1:RETURN
5610 X=RND(199)+1:M$="YOU JUST FOUND"+STR$(X)+" PIECES OF GOLD!":GOSUB1
5620 B(LC)=0:GL=GL+X:PRINT:RETURN
5630 M$="YOU JUST FOUND A MAGIC ORB!":GOSUB1:PRINT:MO=1:B(LC)=0:RETURN
5640 B(LC)=0:PRINT"YOU JUST FOUND A...":GOSUB 5000
5650 PRINT"***** MAGIC BAZOOKA *****":BZ=1:RETURN
5660 G$="KLUFFOOT":GOTO 5680
5670 G$="FRIEK"
5680 IF FL=0 THEN RETURN
5690 M$=G$+", THE GARGOYLE SLAVE OF TERAK,"
5700 IF SW=1 GOTO 5720
5710 M$=M$+" STEALS THE GOLDEN FLUTE AGAIN!":GOSUB1:FL=0:RETURN
5720 M$=M$+" ATTEMPTS TO STEAL THE GOLDEN FLUTE AGAIN!":GOSUB1:PRINT 
5730 M$="BUT THE MAGIC SWORD KILLS "+G$+"!":GOSUB1:PRINT
5740 B(LC)=0:RETURN
5750 M$="YOU ARE IN THE ENCHANTED FOREST!":GOSUB1:PRINT
5760 ZX=BR+DG+GR+RU+SJ+JS+AL+PM
5770 IF ZX>5 THEN RETURN
5780 R=RND(10):IF R>8 THEN RETURN
5790 IF R=1 AND BR=0 GOTO 5880
5800 IF R=2 AND DG=0 GOTO 5890
5810 IF R=3 AND GR=0 GOTO 5900
5820 IF R=4 AND RU=0 GOTO 5910
5830 IF R=5 AND SI=0 GOTO 5920
5840 IF R=5 AND JS=0 GOTO 5930
5850 IF R=7 AND AL=0 GOTO 5940
5860 IF R=8 AND PM=0 GOTO 5950
5870 RETURN
5880 M$="BROMBIRAN":BR=1:GOTO 5970
5890 M$="DAGGLETTE":DG=1:GOTO 5970
5900 M$="GROMPHLUR":GR=1:GOTO 5970
5910 M$="RULF":RU=1:GOTO 5970
5920 M$="SEJJAN":SJ=1:GOTO 5970
5930 M$="JESSAN":JS=1:GOTO 5970
5940 M$="ALLEGRECIA":AL=1:GOTO 5970
5950 M$="PRINCESS MELVA":PM=1
5970 C(R)=1.5:B(LC)=0:M$=M$+" IS MAGICALLY RESTORED TO LIFE!":GOSUB1
5980 PRINT:RETURN
6000 M$="YOU JUST FOUND A MAGIC ZITHER!":GOSUB1:PRINT:MZ=1:B(LC)=0:RETURN
6010 M$="THE MAGIC CHARIOT JUST FLEW OVER AN ENCHANTED LAND MINE!":GOSUB1:PRINT
6020 GOSUB 5000:FOR X=1 TO 50:XZ=RND(512)-1:PRINT@XZ,"*";
6030 U=XZ*X:NEXT:PRINT@480,""
6040 GOTO 5360
6050 IF GL<2 THEN RETURN
6060 M$="A WICKED WITCH STEALS ALL OF YOUR GOLD COINS!":GOSUB1:PRINT
6070 GL=0:PRINT:M$="HEE HEE HEE!":GOSUB3:PRINT:PRINT
6080 RETURN
6090 PRINT@CC*32+16,;:RETURN
6999 REM * MAP *
7000 CLS:PRINT:Z=1:FORX=1TO10:PRINT"   ";:FORY=1TO10:PRINTC$(A(Z));:Z=Z+1:NEXT:PRINT:NEXT
7210 CC=0:GOSUB6090:PRINT"C MAGIC CHARIOT";:CC=1:GOSUB6090:PRINT"D=DRAGON":CC=2:GOSUB6090:PRINT"d=DWARF, F=FOG"
7220 CC=3:GOSUB6090:PRINT"G=GARGOYLE":CC=4:GOSUB6090:PRINT"g=GOBLIN, *=RUIN";:CC=5:GOSUB6090:PRINT"M=MOUNTAIN"
7221 CC=6:GOSUB6090:PRINT"P=HOPELESS PITS":CC=7:GOSUB6090:PRINT"R=ROCK, S=SIREN"
7230 CC=8:GOSUB6090:PRINT"s=MAGIC SPARROW":CC=9:GOSUB6090:PRINT"T=TERAK'S LAIR"
7240 CC=10:GOSUB6090:PRINT"W=WOODLANDS":CC=11:GOSUB6090:PRINT"w=WITCH, !=MINE"
7250 GOTO900
8000 FOR X=1 TO 100:A(X)=0:B(X)=0:PRINT"&";:NEXT
8010 GOTO 70
10000 M$="YOU ARE IN THE WOODLANDS, HOME OF THE ELVES.":GOSUB1:PRINT
10010 IF FL=1 THENM$="YOU HAVE RECOVERED THE GOLDEN FLUTE!":GOSUB1:GOTO 1150
10020 GOTO 4280
10030 M$="TERAK FEARS YOUR APPROACH AND MOVES HIS LAIR!":GOSUB1:PRINT
10040 FOR X=1 TO 100:IF B(X)=4 THEN B(X)=0:A(X)=0
10050 NEXT
10060 X=RND(100):IF B(X)>1 GOTO 10060
10070 B(X)=4:GOTO 4310
10080 M$="THE MAGIC CHARIOT IS MIRED IN THE HOPELESS PITS!":GOSUB1:PRINT
10090 IF MO=1 GOTO 10130
10100 A=RND(5):IF A>3 THENPRINT"YOU ARE DOOMED!":GOTO 1050
10110 IF A<3 THENGOSUB 5360
10120 M$="YOU MANAGE TO GET THE MAGIC CHARIOT FREE OF THE MUCK.":GOSUB1:GOTO 700
10130 INPUT"DO YOU RUB YOUR MAGIC ORB";Q$:Q$=LEFT$(Q$,1)
10140 IF Q$="Y" GOTO 10160
10150 GOTO 10100
10160 GOSUB 5000:CLS:PRINT:PRINT:PRINT:PRINT
10170 M$="*** POOF ***":GOSUB3:PRINT:PRINT:PRINT:PRINT
10180 LC=RND(100):M$="THE MAGIC CHARIOT IS MAGICALLY TRANSPORTED TO LOCATION # "+STR$(LC):GOSUB1
10190 A(ZZ)=B(ZZ):A(LC)=12:GOTO 700
10200 PRINT:M$="THE SWEET SONG OF THE SIRENS MESMERIZES YOU!":GOSUB1:PRINT
10210 GOSUB 5000
10220 PRINT"THE MAGIC CHARIOT CRASHES!":SOUND1,10:PRINT:GOSUB 5000
10230 GOTO 1050
10240 M$="A DRAGON ":FO=1:GOTO 10300
10250 M$="A GARGOYLE ":FO=2:GOTO 10300
10260 M$="A GOBLIN ":FO=3:GOTO 10300
10270 M$="A MAGIC SPARROW ":FO=4:GOTO 10300
10280 M$="AN ANCIENT DWARF ":GM=RND(150):FO=5
10300 AM=RND(4):M$=M$+"IS IN YOUR PATH!":GOSUB1:PRINT:GOSUB 5000
10310 A(LC)=B(LC)
10320 M$="POSSIBLE ACTIONS":GOSUB3:PRINT:PRINT
10330 PRINT" 1 - MOVE THE MAGIC CHARIOT"
10340 PRINT" 2 - THROW SOME GOLD OVERBOARD"
10350 PRINT" 3 - HAND TO HAND COMBAT"
10360 IF MO=1 THENPRINT" 4 - RUB MAGIC ORB"
10370 IF SW=1 THENPRINT" 5 - UNSHEATH MAGIC SWORD"
10380 IF MZ=1 THENPRINT" 6 - PLAY MAGIC ZITHER"
10390 M$="YOUR CHOICE":GOSUB3:INPUTP
10400 IF P=1 GOTO 10490
10410 IF P=2 GOTO 10660 
10420 IF P=3 GOTO 11000
10430 IF MO=1 AND P=4 GOTO 10160
10440 IF SW=1 AND P=5 GOTO 11500
10450 IF MZ=1 AND P=6 GOTO 11630
10460 M$="INVALID SELECTION!":GOSUB3:PRINT:PRINT:GOTO 10320
10490 IF FO>3 GOTO 910
10500 PRINT:INPUT"DIRECTION";D$:D$=LEFT$(D$,1)
10520 IF D$="U" GOTO 10570 
10530 IF D$="D" GOTO 10580 
10540 IF D$="R" GOTO 10590 
10550 IF D$="L" GOTO 10600 
10560 GOTO 10500
10570 IF AM=1 GOTO 1190
10571 GOTO 10610
10580 IF AM=2 GOTO 1000
10581 GOTO 10610
10590 IF AM=3 GOTO 1200
10591 GOTO 10610
10600 IF AM=4 GOTO 1220
10610 PRINT:M$="THE ":IF FO=1 THENM$=M$+"DRAGON"
10620 IF FO=2 THENM$=M$+"GARGOYLE"
10630 IF FO=3 THENM$=M$+"GOBLIN"
10640 M$=M$+" WILL NOT LET YOU GO THAT WAY!":GOSUB1
10650 GOTO 10320
10660 PRINT:PRINT"HOW MUCH GOLD DO YOU TOSS":INPUT"OVERBOARD";H
10670 A=ABS(H):H=INT(H):IF H=0 GOTO 10660
10680 IF H>GL GOTO 10995 
10690 GL=GL-H
10700 IF FO=1 GOTO 10900 
10710 IF FO=2 GOTO 10910 
10720 IF FO=3 GOTO 10940 
10730 IF FO=4 GOTO 10980
10740 M$="THE DWARF THANKS YOU VERY POLITELY"
10750 GM=GM-H:IF GM>1 THENGOSUB1:GOTO 10320
10760 M$=M$+" AND HE REVEALS THE LOCATION OF ALL "
10770 DF=RND(4):IF DF=1 GOTO 10840
10780 IF DF=2 GOTO 10860
10790 IF DF=3 GOTO 10880
10800 M$=M$+"SIRENS":FOR X=1 TO 100:IF B(X)=5 THEN A(X)=5
10810 NEXT
10820 GOSUB1:M$="THIS INFORMATION WILL BE DISPLAYED ON YOUR NEXT MAP.":GOSUB1
10830 A(LC)=12:GOTO 700
10840 M$=M$+"GOBLINS":FOR X=1 TO 100:IF B(X)=10 THEN A(X)=10 
10850 NEXT:GOTO 10820
10860 M$=M$+"GARGOYLES":FOR X=1 TO 100:IF B(X)=9 THEN A(X)=9 
10870 NEXT:GOTO 10820
10880 M$=M$+"DRAGONS":FOR X=1 TO 100:IF B(X)=6 THEN A(X)=6
10890 NEXT:GOTO 10820
10900 M$="DRAGONS HAVE NO INTEREST IN GOLD.":GOSUB1:PRINT:GOTO 10320
10910 M$="THE GARGOYLE PUTS THE GOLD INTO ITS SACK"
10920 I=RND(200):IF H<I THEN GOSUB1:PRINT:GOTO 10320
10930 M$=M$+" AND LEAVES":GOSUB1:B(LC)=0:A(LC)=12:GOTO 700
10940 I=RND(200):M$="THE GOBLIN EATS THE GOLD GREEDILY"
10950 IF H<I THEN GOSUB1:PRINT:GOTO 10320
10960 M$=M$+" AND DIES OF TERMINAL INDIGESTION.":GOSUB1:PRINT
10970 B(LC)=0:A(LC)=12:GOTO 700
10980 M$="WHAT WOULD A BIRD WANT WITH"+STR$(H)+" PIECES OF GOLD?":GOSUB1:PRINT
10990 GOTO 10320
10995 M$="YOU DO NOT HAVE"+STR$(H)+" PIECES OF GOLD!":GOSUB1:GOSUB 5360:GOTO 10320
11000 PRINT:M$="YOUR CHAMPION?":GOSUB3:PRINT
11010 IF BR=1 THENPRINT" 1 - BROMBIRAN"
11020 IF DG=1 THENPRINT" 2 - DAGGLETTE"
11030 IF GR=1 THENPRINT" 3 - GROMPHLUR"
11040 IF RU=1 THENPRINT" 4 - RULF"
11050 IF SJ=1 THENPRINT" 5 - SEJJAN"
11060 IF JS=1 THENPRINT" 6 - JESSAN"
11070 IF AL=1 THENPRINT" 7 - ALLEGRECIA"
11080 IF PM=1 THENPRINT" 8 - PRINCESS MELVA"
11090 M$=" 9 - "+N$:GOSUB1
11100 M$="YOUR CHOICE":GOSUB3:INPUTCH
11110 IF CH=1 AND BR=1 GOTO 11210
11120 IF CH=2 AND DG=1 GOTO 11220
11130 IF CH=3 AND GR=1 GOTO 11230
11140 IF CH=4 AND RU=1 GOTO 11240
11150 IF CH=5 AND SJ=1 GOTO 11250
11160 IF CH=6 AND JS=1 GOTO 11260
11170 IF CH=7 AND AL=1 GOTO 11270
11180 IF CH=8 AND PM=1 GOTO 11280
11190 IF CH=9 GOTO 11290
11200 M$="WHO???":GOSUB3:PRINT:PRINT:GOTO 11010
11210 I=50:GOTO 11300
11220 I=55:GOTO 11300 
11230 I=45:GOTO 11300 
11240 I=85:GOTO 11300 
11250 I=25:GOTO 11300 
11260 I=25:GOTO 11300 
11270 I=50:GOTO 11300 
11280 I=40:GOTO 11300 
11290 I=75
11300 H=I*C(CH):C(CH)=C(CH)-.1:GOSUB 5000
11310 PRINT:PRINT:RS=RND(100)
11320 IF FO=4 GOTO 11390 
11330 IF FO=5 GOTO 11490 
11340 IF RS>H GOTO 11390
11350 M$="THE ":IF FO=1 THENM$=M$+"DRAGON"
11360 IF FO=2 THENM$=M$+"GARGOYLE"
11370 IF FO=3 THENM$=M$+"GOBLIN"
11380 M$=M$+" IS SLAIN!":GOSUB1:PRINT:B(LC)=0:A(LC)=12:GOTO 700
11390 IF CH=1 THENPRINT"BROMBIRAN";:BR=0
11400 IF CH=2 THENPRINT"DAGGLETTE";:DG=0
11410 IF CH=3 THENPRINT"GROMPHLUR";:GR=0
11420 IF CH=4 THENPRINT"RULF";:RU=0
11430 IF CH=5 THENPRINT"SEJJAN";:SJ=0
11440 IF CH=6 THENPRINT"JESSAN";:JS=0
11450 IF CH=7 THENPRINT"ALLEGRECIA";:AL=0
11460 IF CH=8 THENPRINT"PRINCESS MELVA";:PM=0
11470 IF CH=9 THENM$=N$+" IS SLAIN!":GOSUB1:PRINT"GAME OVER!":GOTO 1050
11480 C(CH)=0:PRINT" IS SLAIN!":PRINT:GOTO 10320
11490 PRINT"THE DWARF IS SLAIN!":B(LC)=0:A(LC)=12:GOTO 700
11500 Q=RND(100):IF FO=1 GOTO 11550
11510 IF FO=2 GOTO 11600
11520 IF FO=3 GOTO 11350
11530 IF FO=4 GOTO 11390
11540 IF FO=5 GOTO 11490
11550 IF Q<65 GOTO 11350
11560 M$="THIS DRAGON IS IMMUNE TO YOUR SWORD!":GOSUB1:PRINT:GOSUB 5000
11570 M$="IT ATTACKS THE MAGIC CHARIOT!":GOSUB1:PRINT:GOSUB 5000
11580 GOSUB 5360:IF Q<80 GOTO 10320
11590 M$="YOUR MAGIC SWORD IS DESTROYED!":GOSUB1:PRINT:SW=0:GOTO 10320
11600 IF Q<50 GOTO 11350
11610 M$="THE GARGOYLE DRAWS ITS OWN MAGIC SWORD!":GOSUB1:PRINT:GOSUB 5000
11620 GOTO 11580
11630 IF FO=1 GOTO 11350 
11640 IF FO=3 GOTO 11670
11650 M$="NO ONE IS PARTICULARLY IMPRESSED WITH YOUR TALENT.":GOSUB1:PRINT
11660 GOTO 10320
11670 Q=RND(10):IF Q>5 GOTO 11350
11680 GOSUB 5360:GOTO 10320
11699 REM * WALL *
11700 PRINT:M$="YOU JUST RAN INTO A BRICK WALL!":GOSUB1:PRINT
11710 QQ=RND(3)+QQ:IF QQ<7 THENGOSUB 5360
11720 IF QQ>10 GOTO 11750
11730 A(LC)=B(LC):IF XX=13 THEN LC=LC-20:GOTO11740
11731 LC=LC+20
11740 M$="THE MAGIC CHARIOT IS HURLED BACKWARDS!":GOSUB1:GOTO 4250
11750 M$="THE MAGIC CHARIOT IS REDUCED TO A PILE OF JUNK.":GOSUB1:PRINT
11760 GOSUB 5000
11770 M$="YOU AND YOUR ENTIRE PARTY ARE DEAD.":GOSUB1:PRINT:GOTO 1050
11780 M$="THE MAGIC CHARIOT HAS FLOWN INTO A THICK, MYSTERIOUS FOG!":GOSUB1
11790 FOR X=1 TO 192:PRINT"#";:NEXT:A(LC)=15:LC=RND(50)+30:GOSUB5000
11795 PRINT"THE FOG CLEARS.":ZZ=A(LC):A(LC)=12:GOTO 700
11800 PRINT:M$="A GIGANTIC BOULDER BLOCKS YOUR PATH!":GOSUB1:PRINT
11810 R=RND(4)
11820 A(LC)=27:ZZ=27
11830 INPUT"WHICH WAY DO YOU TRY TO GO";D$:D$=LEFT$(D$,1)
11840 M=M+1
11850 IF D$="D" AND R=1 GOTO 1000
11860 IF D$="U" AND R=2 GOTO 1190
11870 IF D$="R" AND R=3 GOTO 1200
11880 IF D$="L" AND R=4 GOTO 1220
11890 M$="THE BOULDER IS IN YOUR WAY!":GOSUB1:PRINT
11895 FOR X=1 TO 9:C(X)=C(X)-(C(X)/20):NEXT:GOTO 11830
11910 REM * THE GOLDEN FLUTE *
11920 REM *  DELTON T. HORN  *
