100 CLEAR50:CLS:GOTO1040
110 M=M+1:IF S>6 THEN P=(S-6)*32: GOTO130
120 P=S*32
130 P=P+P2:P1=(M*3)-3:P=P+P1:PRINT@P, X$;:SOUND175,1:RETURN
140 CLS2:PRINT@8,"instructions";
150 PRINT@66,"THE COLORS THAT I USE ARE : ";
160 PRINT@135,"BLUE   - ENTER 'B'";
170 PRINT@167,"RED    - ENTER 'R'";
180 PRINT@199,"ORANGE - ENTER 'O'";
190 PRINT@231,"GREEN  - ENTER 'G'";
200 PRINT@263,"YELLOW - ENTER 'Y'";
210 PRINT@295,"PURPLE - ENTER 'P'";
220 PRINT@354," USE THE 'ENTER' KEY IF YOU ";
230 PRINT@386,"  WISH TO CANCEL AN ENTRY.  ";
240 PRINT@452," PRESS 'ENTER' TO START ";
250 I$= INKEY$:IF I$="" THEN 270
260 IF I$= CHR$(13) THEN 300
270 FORI=1TO200:NEXT
280 PRINT@460,"enter";:FORI=1TO200:NEXT: GOTO240
290 FORI=1TO200:NEXT: GOTO240
300 DIMC$(4),S$(4),T(4)
310 FORI=1TO4:R= RND(6)
320 IF R=1 THEN C$(I)="B"
330 IF R=2 THEN C$(I)="R"
340 IF R=3 THEN C$(I)="O"
350 IF R=4 THEN C$(I)="G"
360 IF R=5 THEN C$(I)="Y"
370 IF R=6 THEN C$(I)="P"
380 NEXT
390 B$=CHR$(175):B$=B$+B$
400 R$=CHR$(191):R$=R$+R$
410 O$=CHR$(255):O$=O$+O$
420 G$=CHR$(143):G$=G$+G$
430 Y$=CHR$(159):Y$=Y$+Y$
440 P$=CHR$(239):P$=P$+P$
450 S=1:P2=97:CLS5
460 FOR I=1TO11:PRINT@32+I, CHR$(128);:NEXT:M=0
470 PRINT@46,"ENTER YOUR COLORS";
480 M$=INKEY$:IF M$="" THEN 480
490 IF M$=CHR$(13) THEN FOR I=1TO11:PRINT@P-((M-1)*3)+I-1,CHR$(207);:NEXT:M=0:GOTO 480
500 IF M$="B" THEN X$=B$:GOSUB110: GOTO600
510 IF M$="R" THEN X$=R$:GOSUB110: GOTO600
520 IF M$="O" THEN X$=O$:GOSUB110: GOTO600
530 IF M$="G" THEN X$=G$:GOSUB110: GOTO600
540 IF M$="Y" THEN X$=Y$:GOSUB110: GOTO600
550 IF M$="P" THEN X$=P$:GOSUB110: GOTO600
560 PRINT@46,"INCORRECT COLOR  ";
570 FOR I=1TO17:PRINT@I+77, CHR$(140);:NEXT:SOUND90,2
580 FORI=1TO500:NEXT
590 FORI=1TO17:PRINT@I+77, CHR$(207);:NEXT: GOTO470
600 S$(M)=M$:IF M=4  GOTO620
610 GOTO480
620 S=S+1:M=0:P2=P2+32:IF S=7 THEN P2=113
630 B1=0:B=0:FOR I=1TO4:T(I)=1:NEXT
640 FORI=1TO4
650 IF S$(I)=C$(I) THEN B=B+1
660 NEXT
670 FORI=1TO4
680 IF T(1)=0 THEN 700
690 IF S$(I)=C$(1) THEN B1=B1+1:T(1)=0:GOTO 760
700 IF T(2)=0 THEN 720
710 IF S$(I)=C$(2) THEN B1=B1+1:T(2)=0:GOTO 760
720 IF T(3)=0 THEN 740
730 IF S$(I)=C$(3) THEN B1=B1+1:T(3)=0:GOTO 760
740 IF T(4)=0 THEN 760
750 IF S$(I)=C$(4) THEN B1=B1+1:T(4)=0
760 NEXT
770 I$= RIGHT$( STR$(B1),1)+ CHR$(47)+ RIGHT$( STR$(B),1):PRINT@P+3, I$;
780 IF B1=4 AND B=4 THEN 810
790 IF S=13 THEN 810
800 GOTO480
810 FORI=1TO11:PRINT@I, CHR$(195);:NEXT
820 PRINT@0,CHR$(193);:PRINT@12,CHR$(194);:PRINT@32,CHR$(197);:PRINT@44,CHR$(202);:PRINT@64,CHR$(196);:PRINT@76,CHR$(200);
880 FORI=1TO11:PRINT@I+64, CHR$(204);:NEXT
890 PRINT@46,"  WOULD YOU LIKE ";
900 PRINT@78,"  ANOTHER GAME ? ";
910 FOR I=1TO11:PRINT@I+32,  CHR$(207);:NEXT
920 FOR I=1TO4 
930 IF C$(I)="B" THEN X$=B$
940 IF C$(I)="G" THEN X$=G$
950 IF C$(I)="R" THEN X$=R$
960 IF C$(I)="O" THEN X$=O$
970 IF C$(I)="Y" THEN X$=Y$
980 IF C$(I)="P" THEN X$=P$
990 PRINT@33+((I-1)*3), X$;:NEXT
1000 I$= INKEY$
1010 IF I$="Y" THEN 310
1020 IF I$="N" THEN CLS:END
1030 GOTO1000
1040 DATA 41,43,41,40,40,41,40,40,41,40,40,41,40,40,41,40,40,41,41,43,41,40,40,41,41,43,41,40,40,43
1050 DATA 29,37,33,32,40,33,32,40,41,43,33,43,32,41,43,32,40,33,29,37,33,43,33,43,29,41,33,33,43,33
1060 DATA 33,43,33,33,43,33,40,40,33,43,33,43,32,40,41,33,34,43,33,43,33,40,32,41,33,39,33,32,40,35
1070 FORI=384TO448 STEP32:FORJ=1TO30:READ I$:V= VAL(I$):PRINT@I+J, CHR$(V+100);
1080 IF J/3= INT(J/3) THEN SOUNDJ+100,1
1090 NEXT J:NEXT I
1100 FORI=1TO7:PRINT@480,"":SOUNDI*10+50,1:NEXT:FORI=1TO2000:NEXT
1110 GOTO140
