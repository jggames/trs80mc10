0 CLEAR500:DIMC1,C2,M$:M=16384:GOTO30
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
30 CLS6:POKE49151,64:R=40:FORI=1TO8
50 PRINT@10+32*4," SUPER MIND ";:FORJ=1TOR:NEXTJ:R=R+30:PRINT@10+32*4,"           ";:FORJ=1TOR/2:NEXTJ:NEXTI
60 PRINT@10+32*4,;:M$=" SUPER MIND ":GOSUB7
70 PRINT@5+32*9, "HAVEN'T PLAYED BEFORE?";
80 PRINT@5+32*11,"IF YOU HAVE QUESTIONS,";
90 PRINT@5+32*13,"PRESS ? DURING GAME...";
100 FORJ=1TO4000:NEXTJ
120 DATA35,42,31,36,37,38,30,29
130 FORI=1TO8:READC(I):NEXTI
140 CLS3
150 PRINT@32*6,;:M$="--------------------------------":GOSUB7
160 PRINT@32*8,;:M$="    DO YOU HAVE A COLOR TV?     ":GOSUB7
170 PRINT@32*10,;:M$="        (PRESS Y OR N)          ":GOSUB7
180 PRINT@32*12,;:M$="--------------------------------":GOSUB7:FORK=0TO1STEP0:K=-(INKEY$=""):NEXT
190 Z$=INKEY$:C1=RND(0):IFZ$=""THEN190
200 IFZ$="?"THENGOSUB8000:GOTO140
210 IFZ$<>"Y"ANDZ$<>"N"THEN190
220 IFZ$="Y"THENZ=1:GOTO500
221 Z=2
500 CLS0:POKE49151,64
510 PRINT@1+32*1,"+---------------------------+";
515 PRINT@1+32*2,"!*                         *!";
520 PRINT@1+32*3,"! YOUR MC-10 OFFERS YOU A   !";
530 PRINT@1+32*4,"! CHOICE...                 !";
540 PRINT@1+32*5,"! 4 LEVELS OF DIFFICULTY    !";
550 PRINT@1+32*6,"! ARE AVAILABLE:            !";
560 PRINT@1+32*7,"!    -1-LEVEL 1 (SINGLES)   !";
565 PRINT@1+32*8,"!    -2-LEVEL 2 (DOUBLES)   !";
580 PRINT@1+32*9,"!    -3-LEVEL 3 (TRIPLES)   !";
590 PRINT@1+32*10,"!    -4-LEVEL 4 (QUADRUPLES)!";
595 PRINT@1+32*11,"!                           !";
600 PRINT@1+32*12,"!         YOUR CHOICE?      !";
605 PRINT@1+32*13,"!*                         *!";
610 PRINT@1+32*14,"+---------------------------+";
620 W$=INKEY$:IFW$=""THEN620
625 IFW$="?"THENGOSUB8000:GOTO605
630 IFW$<>"1"ANDW$<>"2"ANDW$<>"3"ANDW$<>"4"THEN620
640 W=VAL(W$)
650 REM DETERMINE SOLUTION
660 CLS:PRINT@5+32*8,"I AM CALCULATING....";
670 FORI=1TO4:X(I)=INT(RND(0)*8):SOUNDRND(100),1:NEXTI
700 ONWGOTO1000,1100,1200,2000
1000 REM SINGLES
1010 IFX(1)=X(2)ORX(1)=X(3)ORX(1)=X(4)ORX(2)=X(3)ORX(2)=X(4)ORX(3)=X(4)THEN670
1011 GOTO2000
1100 REM DOUBLET
1110 IFX(3)=X(1)ANDX(3)=X(2)THEN670
1120 IFX(3)=X(1)ANDX(3)=X(4)THEN670
1130 IFX(4)=X(1)ANDX(4)=X(2)THEN670
1140 IFX(4)=X(2)ANDX(4)=X(3)THEN670
1141 GOTO2000
1200 REM TRIPLET
1210 IFX(1)=X(2)ANDX(1)=X(3)ANDX(1)=X(4)THEN670
1211 GOTO2000
2000 CLS0
2005 PRINT@3+32*0,;:M$="+---------+--------------+":GOSUB7
2010 PRINT@3+32*1,;:M$="!SOLUTION !              !":GOSUB7
2020 PRINT@3+32*2,;:M$="+---------+  O  O  O  O  !":GOSUB7
2030 PRINT@3+32*3,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2040 PRINT@3+32*4,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2050 PRINT@3+32*5,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2060 PRINT@3+32*6,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2070 PRINT@3+32*7,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2080 PRINT@3+32*8,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2090 PRINT@3+32*9,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2100 PRINT@3+32*10,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2110 PRINT@3+32*11,;:M$="! . . . . !  O  O  O  O  !":GOSUB7
2120 PRINT@3+32*12,;:M$="+---------+--------------+":GOSUB7:PRINT@448,;
2130 GOSUB7100:REM GOSUB4500:REM CHEET
2500 N=0:IFZ=1THENPOKE49151,0
2510 N=N+1:GOSUB3000:GOSUB4000:IFN=10THEN5000
2520 IFO=4THEN6000
2521 GOTO2510
3000 REM THE PLAYER GOES
3005 I=1:FORJ=1TO4:R(J)=3:NEXTJ
3010 IFI=5THENI=1:GOTO3020
3011 IFI=0THENI=4
3020 PRINT@11+3*I+32*(12-N),;:M$=" >":GOSUB7:R$(I)=INKEY$:IFR$(I)=""THEN3020
3030 IFR$(I)="?"THENGOSUB8000:GOSUB7100:GOTO3010
3040 IFR$(I)="A"THENPRINT@11+3*I+32*(12-N),;:M$="  ":GOSUB7:I=I-1:GOTO3010
3050 IFR$(I)="S"THENPRINT@11+3*I+32*(12-N),;:M$="  ":GOSUB7:I=I+1:GOTO3010
3060 IFR$(I)=CHR$(13)THEN3250
3130 IFR$(I)<>"1"ANDR$(I)<>"2"ANDR$(I)<>"3"ANDR$(I)<>"4"ANDR$(I)<>"5"ANDR$(I)<>"6"ANDR$(I)<>"7"ANDR$(I)<>"8"THEN3020
3140 R(I)=VAL(R$(I))-1
3150 IFZ=2THEN3200
3160 PRINT@11+3*I+32*(12-N),;:M$="  ":GOSUB7:PRINTCHR$(140+16*R(I));:I=I+1:GOTO3010
3200 PRINT@11+3*I+32*(12-N),;:M$="  ":GOSUB7:POKEM+13+3*I+32*(12-N),C(R(I)+1):I=I+1:GOTO3010
3250 PRINT@11+3*I+32*(12-N),;:M$="  ":GOSUB7:RETURN

4000 REM MC-10'S RESPONSE
4010 O=0:P=0
4020 FORI=1TO4:IFR(I)=X(I)THENO=O+1
4030 NEXTI
4035 FORI=1TO4
4040 FORJ=1TO4
4050 IFR(J)<>X(I)THEN4070
4060 P=P+1:R(J)=10:GOTO4080
4070 NEXTJ
4080 NEXTI
4090 P=P-O:IFO=0THEN4110
4100 FORI=1TOO:IFN<10THENPRINT@3+2*(I)+32*(12-N),;:M$="*":GOSUB7:NEXTI
4110 IFP=0THEN4130
4120 FORI=1TOP:IFN<10THENPRINT@3+2*O+2*(I)+32*(12-N),;:M$="O":GOSUB7:NEXTI
4130 RETURN

REM DISPLAY SOLUTION
4500 PRINT@4+32*1,;:M$="         ":GOSUB7:IFZ=1THEN4550
4520 FORI=1TO4:PRINT@2*I+2+32*1,;:M$=" ":GOSUB7:POKE1+M+2*I+2+32*1,C(X(I)+1):NEXTI:GOTO4590
4550 FORI=1TO4:PRINT@2*I+2+32*1,;:M$=" ":GOSUB7:PRINTCHR$(140+16*X(I));:NEXTI
4590 RETURN

5000 REM RESPONSE LAST TRY
5010 GOSUB4500
5090 GOSUB7000:IFO=4THEN5100
5091 GOTO5110
5100 C1=13:M$="YOU WON WITH 0 TRIES LEFT!":GOSUB7200:GOTO5120
5110 C1=13:M$="YOU HAVE LOST!":GOSUB7200
5120 GOTO6120

6000 REM PLAYER WINS
6010 GOSUB4500
6070 GOSUB7000:C1=13:M$="YOU WON IN"+STR$(N)+" TRIES!":GOSUB7200
6110 IFN>6THEN6120
6111 C1=14:M$="GOOD GAME!":GOSUB7200
6120 C1=15:M$="PLAY AGAIN? (Y/N)":GOSUB7200
6130 M$=INKEY$:IFM$="Y"THEN500
6140 IFM$="N"THENCLS:END
6150 GOTO6130
7000 FORC1=416TO480STEP32:PRINT@C1,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";:NEXT:RETURN
7010 C1=15:M$="(PRESS A KEY)":GOSUB7200
7020 M$=INKEY$:IFM$=""THEN7020
7030 GOSUB7000:RETURN

7100 GOSUB7000:PRINT@448,;:M$=" 1   2   3   4   5   6   7   8 ":GOSUB7:IFZ=1THEN7120
7110 FORC1=0TO7:POKEM+1+32*15+C1*4,C(C1+1):NEXT:GOTO7130
7120 FORC1=0TO7:PRINT@1+32*15+C1*4,CHR$(131+16*C1);:NEXT
7130 RETURN

7200 C2=INT((32-LEN(M$))/2):PRINT@32*C1+C2,;:GOSUB7:RETURN

8000 GOSUB7000
8010 PRINT@32*13,;:M$="THE GOAL OF THE GAME IS TO":GOSUB7
8011 PRINT@32*14,;:M$="GUESS THE 4 PIECE SOLUTION.":GOSUB7:GOSUB7010
8020 PRINT@32*13,;:M$="LEVEL 1: 4 DIFFERENT PIECES":GOSUB7
8040 PRINT@32*14,;:M$="LEVEL 2: POSSIBLE PAIRS":GOSUB7:GOSUB7010
8050 PRINT@32*13,;:M$="LEVEL 3: POSSIBLE TRIPLETS":GOSUB7
8060 PRINT@32*14,;:M$="LEVEL 4: POSSIBLE QUADRUPLETS":GOSUB7:GOSUB7010
8080 PRINT@32*13,;:M$="COMPOSE A LINE BY SELECTING 1-8":GOSUB7
8085 PRINT@32*14,;:M$="MODIFY USING <A> OR <S> KEYS":GOSUB7:GOSUB7010
8090 PRINT@32*13,;:M$="WHEN CORRECT, HIT <ENTER> AND":GOSUB7
8110 PRINT@32*14,;:M$="THE MC-10 CHECKS YOUR SOLUTION.":GOSUB7:GOSUB7010
8120 PRINT@32*13,;:M$="*=CORRECT GUESS IN RIGHT PLACE":GOSUB7
8130 PRINT@32*14,;:M$="O=CORRECT GUESS IN WRONG PLACE":GOSUB7:GOSUB7010
8150 RETURN
9005 REM-COPYRIGHT MARC PETITQUEUX
9006 REM-& MICRO INFLUX---
9007 REM---(VERSION DU 21/05/83)
