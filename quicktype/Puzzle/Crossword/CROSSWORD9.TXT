0 CLEAR5000:DIMCC,ZZ,M$,I$,CU$,A$,ZU:MC=16384:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 PRINT@480,"                               ";:POKEMC+511,96:PRINT@480,;:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
10 ZU=1:CU$=CHR$(128)+" ":GOTO100
50 REM INPUT ROUTINE
51 INPUTZ$
57 RETURN
59 REM GET KEY ROUTINE
60 PRINTCHR$(8)MID$(CU$,ZU+1,1);:FORCC=1TO65:Z$=INKEY$:IFZ$<>""THEN62
61 NEXT:ZU=-(ZU=0):IFZ$=""THEN60
62 IFZC$<>""THENFORZX=1TOLEN(ZC$):ZF=ZF+ZX*-(Z$=MID$(ZC$,ZX,1)):NEXT:IFZF=0THEN60
63 PRINTCHR$(8);:ZC$="":ZF=0:RETURN
100 :
101 REM MAIN LINE
110 GOSUB 10000
120 GOSUB 9000
130 GOSUB 1000
140 GOSUB 8000
200 :
210 GOSUB 11000
220 GOSUB 2500
230 GOSUB 3000
238 IF NF<>0 THEN GOSUB5:PRINTMID$(STR$(NF),2);" DIDN'T FIT.";
250 FORCC=1TO60:PRINT@496,"HIT a FOR AGAIN";:POKEMC+511,96:A$=INKEY$:IFA$<>""THENCC=60:NEXT:GOTO260
251 NEXT
252 FORCC=1TO60:PRINT@496,"HIT p TO PLAY  ";:POKEMC+511,96:A$=INKEY$:IFA$<>""THENCC=60:NEXT:GOTO260
253 NEXT
260 IFA$<>"A"ANDA$<>"P"THEN250
270 IFA$="A"THEN200
280 IFA$<>"P"THEN250
400 :
410 REM PRINTING
530 GOSUB13000:REM PRINT CROSSWORD GRID
540 GOSUB5:PRINT"HIT enter TO SEE CLUES, q=QUIT";
550 G$=INKEY$
560 G$=INKEY$:IF G$<>CHR$(13) AND G$<>"Q" THEN 560
565 IFG$="Q"THENGOSUB5:GOTO5710
570 GOSUB5000
580 GOTO540
1000 :
1001 REM INPUT WORDS AND DEFINITIONS
1002 DIMW$(25),D$(25)
1005 I=0
1010 CLS
1030 I=I+1
1035 IF I<1THENI=1
1040 PRINT:PRINT "WORD NO.";I" ";:GOSUB 50:W$(I)=Z$:B$=Z$
1045 IF RIGHT$(B$,1)="^" THEN I=I-1:GOTO 1053
1050 IF B$<>""THEN1055
1051 PRINT"DONE? (y/n)?  ";
1052 ZC$="YN":GOSUB60:IFZ$="Y"THENN=I-1:IFI>2THENRETURN
1053 B$="":W$(I)="":Z$="":FA=0:GOTO1035
1055 FOR J=1 TO LEN(W$(I))
1056 IF(MID$(W$(I),J,1)<"A"OR MID$(W$(I),J,1)>"Z")OR(LEN(W$(I))>13OR LEN(W$(I))<3)THEN FA=1:J=LEN(W$(I))
1057 NEXT J
1058 IF FA=1 THEN FA=0:PRINT:PRINT "LETTERS ONLY! 3-13 CHARS.":GOTO 1053:REM REDO WORD
1060 PRINT:PRINT "DEFINITION NO.";I
1065 GOSUB 50
1070 B$=Z$:IF RIGHT$(B$,1)="^"THEN I=I-1:GOTO1053
1075 IFB$=""THEN1051
1080 D$(I)=B$
1090 IF LEN(D$(I))>27THENPRINT"DEFINITION IS TOO LONG":GOTO1060
1100 IF I=24 THEN N=24:RETURN
1110 GOTO1030
2500 :
2501 REM POKE FIRST WORD
2510 NF=0:FOR K=0 TO N:DR(K)=0:NEXT
2520 D(1)=1:D(2)=32
2525 K=INT((N-1)*RND(0))+2
2530 W$=W$(1):W$(1)=W$(K):W$(K)=W$
2535 D$=D$(1):D$(1)=D$(K):D$(K)=D$
2540 T=1:W$=W$(T):LW=LEN(W$)
2550 K=INT(2*RND(0))+1:DR(T)=D(K)
2560 X=INT(11*RND(0))+6:IF K=1 AND X+LW>31 THEN 2560
2570 Y=INT(7*RND(0))+1:IF K=2 AND Y+LW>14 THEN 2570
2580 LC=MC+32*Y+X
2590 GOSUB 7000:REM POKE WORD
2599 RETURN
3000 :
3001 REM MAKE CROSSWORD
3010 T=T+1:U=0
3020 FOR C=1 TO N:IF DR(C)<>0 THEN U=U+1:G(U)=C:NEXTC
3030 FOR I=1 TO U:S=1+INT(U*RND(0)):GG=G(I):G(I)=G(S):G(S)=GG:NEXTI
3100 :
3110 W$=W$(T):LW=LEN(W$):NF=NF+1:IFNF>=N THENC=N:T=N:SOUND1,10:NEXTC:GOTO3499
3130 PRINT@480,LEFT$(W$+"           ",15);
3170 FORC=1TO U:E=G(C):C$=W$(E)
3190 FORL=1 TO LW:Z1$=MID$(W$,L,1)
3210 FORZQ=1 TO LEN(C$):Z2$=MID$(C$,ZQ,1)
3220 IF Z1$<>Z2$ THEN3370
3240 IF DR(E)=32 THEN DR(T)=1
3250 IF DR(E)=1 THEN DR(T)=32
3280 LC=LO(E)+DR(E)*ZQ-DR(T)*L:IFLC<MC OR LC+LW*DR(T)>MC+480THEN3370
3285 WT=PEEK(LC):IF WT<27THEN3360
3286 IFPEEK(LC+(LW+1)*DR(T))<27THEN3360
3290 POKELC,175
3301 :
3305 O=0
3310 FOR V=1 TO LW
3315 Q0=LC+V*DR(T):WI=PEEK(Q0):POKEQ0,175:POKEQ0,WI
3317 IF DR(T)=32 THEN 3322
3318 IF PEEK(Q0+32)<>96 AND FQ(Q0+32-AD)=DR(T)THEN3345
3319 IF PEEK(Q0-32)<>96 AND FQ(Q0-32-AD)=DR(T)THEN3345
3320 GOTO3324
3322 IF PEEK(Q0+1)<>96 AND FQ(Q0+1-AD)=DR(T)THEN3345
3323 IF PEEK(Q0-1)<>96 AND FQ(Q0-1-AD)=DR(T)THEN3345
3324 FOR Y=1 TO U:IF DR(Y)=DR(T)THEN3327
3325 IF Q0=LO(Y)THEN O=1:Y=U:GOTO3327
3326 IF Q0=LO(Y)+DR(Y)*(LEN(W$(Y))+1) THEN O=1:Y=U
3327 NEXTY
3328 IF O=1 THEN 3345
3340 IF WI<>96 AND WI<>ASC(MID$(W$,V,1))-64 THEN3345
3344 GOTO3350
3345 V=LW:O=1
3350 NEXTV
3355 IF O=0THENGOSUB7000:NF=0:ZQ=LEN(C$):L=LW:C=U
3360 POKELC,WT
3370 NEXTZQ,L,C
3410 IFNF=0THEN3490
3420 IF N-U=NF THEN3499
3430 W$=W$(U+1):D$=D$(U+1)
3440 FOR C=U+2 TO N:W$(C-1)=W$(C):D$(C-1)=D$(C):NEXTC
3475 W$(N)=W$:D$(N)=D$
3480 GOTO3100
3490 IF T<N THEN3000
3499 RETURN
5000 :
5001 REM PRINT CLUES
5030 GOSUB5:PRINT"ACROSS";:GOSUB5900
5080 FOR I=1 TO A
5090 IFNA(I)>0THENGOSUB5:M$=MID$(STR$(NA(I)),2)+" ":PRINTM$DA$(I);:M$=M$+"ACROSS: ":GOSUB5500
5110 NEXT I
5120 GOSUB5:PRINT"DOWN";:GOSUB5900
5150 FOR I=1 TO D
5160 IFND(I)>0THENGOSUB5:M$=MID$(STR$(ND(I)),2)+" ":PRINTM$DD$(I);:M$=M$+"DOWN: ":GOSUB5600
5180 NEXT I
5200 RETURN
5500 GOSUB6000:IFA$=CHR$(13)THENRETURN
5505 IFM$=WA$(I)THENGOSUB5:PRINT"CORRECT!";:NA(I)=-1:GOTO5700
5510 GOSUB5:PRINT"INCORRECT!";:FORCC=1TO2500:NEXT:RETURN
5600 GOSUB6000:IFA$=CHR$(13)THENRETURN
5605 IFM$=WD$(I)THENGOSUB5:PRINT"CORRECT!";:ND(I)=-1:GOTO5700
5610 GOSUB5:PRINT"INCORRECT!";:FORCC=1TO2500:NEXT:RETURN
5700 FORCC=1TO2500:NEXT:CG=CG+1:IFCG=A+DTHEN5705
5701 RETURN
5705 GOSUB5:PRINT"YOU WIN! ";:GOSUB5800
5710 PRINT"PLAY AGAIN? (y/n)?  ";:ZC$="YN":GOSUB60
5720 IFZ$="Y"THENRUN
5730 END
5800 FORI=1TOA:ZZ=LA(I):FORCC=1TOLEN(WA$(I)):ZZ=ZZ+DA(I):POKEZZ,ASC(MID$(WA$(I),CC,1)):NEXT:NEXT
5810 FORI=1TOD:ZZ=LD(I):FORCC=1TOLEN(WD$(I)):ZZ=ZZ+DD(I):POKEZZ,ASC(MID$(WD$(I),CC,1)):NEXT:NEXT:RETURN
5900 A$=INKEY$:IFA$=""THEN5900
5910 IFA$<>CHR$(13)THENSOUND1,1:GOSUB5:PRINT"enter=CONTINUE g=GUESS";:GOTO5900
5920 RETURN
6000 A$=INKEY$:IFA$=""THEN6000
6010 IFA$=CHR$(13)THENRETURN
6020 IFA$<>"G"THEN6000
6030 GOSUB5:PRINTM$" ";:M$="":GOSUB6500:REM GUESSES
6040 RETURN
6500 PRINTCHR$(8)MID$(CU$,ZU+1,1);:FORCC=1TO65:A$=INKEY$:IFA$<>""THENPRINTCHR$(8);:GOTO6510
6501 NEXTCC:ZU=-(ZU=0):IFA$=""THEN6500
6510 IFA$=CHR$(8)THENIFLEN(M$)>=1THENM$=LEFT$(M$,LEN(M$)-1):GOTO6500
6520 IFA$=CHR$(13)THENA$="":RETURN
6530 IFA$<"A"ORA$>"Z"ORLEN(M$)>=13THENSOUND1,1:PRINT" ";:GOTO6500
6540 M$=M$+A$:PRINTA$" ";:GOTO6500
7000 :
7001 REM POKING THE WORD
7010 FOR I=1 TO LW:POKE LC+I*DR(T),ASC(MID$(W$,I,1))-64
7020 FQ=LC+I*DR(T)-MC:FQ(FQ)=INT(DR(T))
7030 NEXTI
7040 CF=LC+DR(T):CL=LC+LW*DR(T)
7050 C0=32*INT(CF/32):CF=CF-C0
7060 C1=32*INT(CL/32):CL=CL-C1:C1=C1+32
7070 IF C0<UB THEN UB=C0
7080 IF C0>LB THEN LB=C0
7090 IF C1<UB THEN UB=C1
7100 IF C1>LB THEN LB=C1
7150 LO(T)=LC
7160 RETURN
8000 :
8001 REM SET UP
8020 DIMDR(25),LO(25),G(25),FQ(511),LA(25),DA(25),LD(25),DD(25),DA$(25),DD$(25),WA$(25),WD$(25)
8025 DIMQ$(25),NA(25),ND(25)
8030 QZ=RND(-(PEEK(9)*256+PEEK(10))):REM SEED RANDOM GENERATOR
8040 AD=MC
8400 :
8480 UB=LC:LB=LC+DR(T)*(LEN(W$)+1)
8490 RETURN
9000 :
9001 REM INSTRUCTIONS
9010 GOSUB14000:PRINT:PRINT"DO YOU WANT INSTRUCTIONS (y/n)?  ";:ZC$="YN":GOSUB60
9020 IF Z$="N"THEN 9310
9025 GOSUB9600:PRINT
9030 M$="CROSSWORD PUZZLE ALLOWS THE USER TO MAKE AND SOLVE A CROSSWORD PUZZLE.":GOSUB1:PRINT
9060 M$="THE COMPUTER WILL ASK YOU TO ENTER WORDS AND THEIR CORRESPONDING DEFINITIONS.":GOSUB1:PRINT
9090 M$="ALWAYS END THE CURRENT LINE YOU'RE TYPING IN BY PRESSING THE <ENTER> KEY.":GOSUB1
9110 GOSUB9500
9120 CLS:M$="THERE ARE EDITING FEATURES IN THIS PROGRAM WHICH MAKE IT EASIER TO USE.":GOSUB1
9180 PRINT:M$="TO CORRECT THE PREVIOUS LINE HIT <CTRL><W> AND THEN <ENTER>.":GOSUB1
9200 PRINT:M$="WHEN YOU HAVE FINISHED ENTERING ALL OF YOUR WORDS AND DEFINITIONS THEN JUST HIT <ENTER>.":GOSUB1
9230 GOSUB9500
9240 CLS:M$="WHEN YOU ENTER A WORD OR DEFINITION YOU MUST USE ONLY LETTERS.":GOSUB1:PRINT
9280 M$="THE DEFINITION MAY ONLY BE 27 CHARACTERS LONG.":GOSUB1:PRINT
9285 M$="THE PROGRAM CAN HANDLE UP TO 24 WORDS.":GOSUB1:PRINT
9286 M$="WHEN VIEWING DEFINITIONS, HIT <G> TO GUESS THE WORD OR <ENTER> TO VIEW NEXT.":GOSUB1
9290 GOSUB9500
9310 RETURN
9500 PRINT:PRINT"PRESS enter TO CONTINUE  ";
9510 ZC$=CHR$(13):GOSUB60
9520 RETURN
9600 CLS:PRINTTAB(8);:M$="CROSSWORD PUZZLE":GOSUB7:PRINT:RETURN
10000 :
10001 REM TITLE
10010 CLS
10030 TT$="CROSSWORD PUZZLE":PRINT@32*7+8,;
10040 FOR I=1 TO LEN(TT$)
10060 FORK=1TO50:NEXT
10070 PRINTMID$(TT$,I,1);
10080 NEXT I:FORCC=1TO1500:NEXT
10085 PRINT
10100 RETURN
11000 :
11010 CLS
11020 FORCC=0TO14:POKEMC+32*CC,128:POKEMC+32*CC+31,128:NEXT
11070 RETURN
11090 END
13000 :
13001 REM PRINT CROSSWORD GRID
13040 A=0:D=0:CW=1
13100 :
13110 FORR=0TO14
13140 GOSUB5:PRINT"PREPARING";R+1;
13200 :
13210 FORC=0TO31
13220 XX=AD+32*R+C:X=PEEK(XX)
13230 IFX<27OR(X>47ANDX<58)THEN13310
13240 POKEXX,128:NEXTC:NEXTR:RETURN
13299 :
13310 FS=0:E=1:FORI=1TON:IFLO(I)+DR(I)<>XX THEN13370
13320 POKEXX,ASC(MID$(STR$(CW),2,1)):IFDR(I)=1THENA=A+1:NA(A)=CW:LA(A)=LO(I):DA(A)=DR(I):WA$(A)=W$(I):DA$(A)=D$(I)
13340 IFDR(I)=32THEND=D+1:ND(D)=CW:LD(D)=LO(I):DD(D)=DR(I):WD$(D)=W$(I):DD$(D)=D$(I)
13350 IF(DR(I)=1ORDR(I)=32)ANDCW>9THENPOKEXX+DR(I),ASC(MID$(STR$(CW),3,1))
13360 FS=1
13370 NEXTI:IFFS=1THENCW=CW+1:NEXTC:NEXTR:RETURN
13720 IFX<27THENPOKEXX,46:NEXTC:NEXTR:RETURN
13730 NEXTC:NEXTR:RETURN
14000 GOSUB9600:PRINT:PRINT
14003 PRINT"    THIS PROGRAM WAS WRITTEN"
14004 PRINT"     UNDER A GRANT FROM THE"
14005 PRINT" ONTARIO MINISTRY OF EDUCATION"
14006 PRINT"   EXPERIENCE '81 PROJECT"
14007 PRINT"   WENTWORTH COUNTY BOARD OF"
14008 PRINT"         EDUCATION"
14009 PRINT"     PROGRAM COORDINATORS-"
14010 PRINT"    DON WOOD  AND  NEAL REID":PRINT:RETURN
14011 :
14012 REM PET 16K AND PRINTER
14013 REM TRS-80 MC-10 EDIT
14014 REM JIM GERRIE
