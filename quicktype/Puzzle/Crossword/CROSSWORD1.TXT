1 REM CROSSWORD PUZZLE
2 :
3 REM    THIS PROGRAM WAS WRITTEN
4 REM     UNDER A GRANT FROM THE
5 REM  ONTARIO MINISTRY OF EDUCATION
6 REM   EXPERIENCE '81 PROJECT
7 REM   WENTWORTH COUNTY BOARD OF
8 REM         EDUCATION
9 REM  PROGRAM COORDINATORS-
10 REM    DON WOOD  AND  NEAL REID
11 :
12 REM PET 16K AND PRINTER
13 :
14 REM MODIFIED FOR SOFTSWAP BY J LEWIS
21 REM"
22 REM"       SAN MATEO COUNTY      
23 REM"     OFFICE OF EDUCATION     
24 REM"             &               
25 REM"  COMPUTER-USING EDUCATORS   
26 REM"                             
27 REM"          SOFTSWAP           
28 REM"                             
29 REM"333 MAIN STREET, REDWOOD CITY
30 REM"CA  94063      (415) 363-5472
31 REM"
32 REM"
33 REM" PROGRAMS MAY BE COPIED FOR  
34 REM" EDUCATIONAL USE ONLY AND    
35 REM" ARE NOT TO BE SOLD.         
36 REM" COPYRIGHT 1983    CUE INC.  
37 REM"
49 GOTO101
50 REM"INPUT ROUTINE
51 DEFFNZ(Z)=PEEK(196)+256*PEEK(197)+PEEK(198):POKE158,0:ZZ$="":POKE167,0
52 GETZ$:IFZ$=""ONZ$=CHR$(147)ONZ$=CHR$(19)ONZ$=CHR$(17)ONZ$=CHR$(145)ONZ$=CHR$(29)ONZ$=CHR$(157)ONZ$=CHR$(148)THEN52
53 IFZ$=CHR$(13)THENPOKE167,1:POKEFNZ(Z),32:Z$=ZZ$:PRINT:GOTO57
54 IF(ZZ$=""ANDZ$=CHR$(20))THEN51
55 IFZCANDLEN(ZZ$)>ZC-1THENGOSUB58:GOTO51
56 ZZ$=ZZ$+Z$:ZZ$=LEFT$(ZZ$,LEN(ZZ$)+(CHR$(20)=Z$)*2):PRINTZ$;:GOTO52
57 ZC=0:RETURN
58 FORZX=1TOZC:PRINTCHR$(20);:NEXT:RETURN
59 REM"GET ROUTINE
60 GETZ$:PRINTMID$(" "+CHR$(157)+CHR$(175)+CHR$(157),(ZU+1)*2-1,2);:ZU=-1*(ZU=0):IFZ$=""THEN60
61 IFZC$<>""THENFORZX=1TOLEN(ZC$):ZF=ZF+ZX*-(Z$=MID$(ZC$,ZX,1)):NEXT:IFZF=0THEN60
62 ZC$="":ZF=0:RETURN
101 REM MAIN LINE
105 REM GOTO130
110 GOSUB 10000
120 GOSUB  9000
130 GOSUB  1000
135 REM GOSUB4010
140 GOSUB  8000
200 :
210 GOSUB 11000
220 GOSUB 2500
230 GOSUB  3000
235 PRINT CHR$(19);"                                       "
238 IF NF<>0 THEN PRINT CHR$(19);"        ";NF;"WORD(S) DIDN'T FIT"
240 POKE 216,2 : PRINT CHR$(145);"        PRESS ";CHR$(18);"P";CHR$(146);" TO PRINT PUZZLE"
250 POKE 216,25 : PRINT CHR$(145);"  PRESS ";CHR$(18);"A";CHR$(146);" FOR A DIFFERENT ARRANGEMENT";CHR$(19)
255 GET A$ : GET A$ : GET A$
260 GET A$ : IF A$="" GOTO 260
270 IF A$="A" GOTO 200
280 IF A$<>"P" GOTO 260
400 :
410 OPEN 1,4 : OPEN 2,4,5 : OPEN 6,4,6
420 FOR IO=0 TO 1
425 PRINT#6,CHR$(18)
430 POKE 216,25 : PRINT CHR$(145);BL$;CHR$(19)
435 IF IO=0 THEN PRINT CHR$(19);"                ANSWERS               "
437 IF IO=1 THEN PRINT CHR$(19);"                PUZZLE                "
440 PRINT "      INSERT PAPER AND PRESS ";CHR$(18);"RETURN";CHR$(146)
450 GET G$ : GET G$ : GET G$
460 GET G$ : IF G$<>CHR$(13) GOTO 460
500 :
510 PRINT CHR$(19);BL$
520 PRINT "       PRINTING                        "
530 GOSUB 13000 PRINT CROSSWOND GRID
531 PRINT#6,CHR$(32)
535 GOSUB5000
540 IF IO=0 THEN POKE 216,25 : PRINT CHR$(145);"         ANSWERS COMPLETED           ";CHR$(19)
550 NEXT IO
560 POKE 216,25 : PRINT CHR$(145);"           PUZZLE COMPLETED       ";CHR$(19)
980 CLOSE 1 : CLOSE 2 : CLOSE 6
990 CLOSE6: END
1000 :
1001 REM INPUT WORDS AND DEFINITIONS
1002 DIM W$(25), D$(25)
1005 I=0
1010 PRINTCHR$(147);
1015 PRINT"TITLE  "; : ZC=38: GOSUB50   : TL$=Z$
1017 PRINTCHR$(147);
1030 I=I+1
1035 IF I<1THENI=1
1040 PRINT : PRINT "WORD NO.";I" "; : GOSUB 50   : W$(I)=Z$:B$=Z$
1045 IF B$="^" THEN I=I-1 : GOTO 1035
1050 IF B$<>""THEN1055
1051 PRINTCHR$(145);"DONE? (Y/N)        ";CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);
1052 ZC$="YN":GOSUB60:IFZ$="Y"THENN=I-1:FL=0:RETURN
1053 PRINTCHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);"                 ";CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(145);:GOTO1035
1055 FOR J=1 TO LEN(W$(I))
1056 IF MID$(W$(I),J,1)<"A" ON MID$(W$(I),J,1)>"Z" THEN FA=1 : J=LEN(W$(I))
1057 NEXT J
1058 IF FA=1 THEN FA=0 : GOSUB 1150 : GOTO 1040 REDO WOND
1060 PRINT : PRINT "DEFINITION NO.";I
1065 PRINT"     ";:ZC=74:GOSUB50
1070 B$=Z$      : IF B$="^"THEN I=I-1 : GOTO1035
1075 IFB$=""THENPRINTCHR$(145);:GOTO1051
1080 D$(I)=B$
1090 IF LEN(D$(I))>74THENPRINTCHR$(17);"THE DEFINITION IS TOO LONG" : I=I-2
1100 IF I<20 THEN 1030
1110 N=20:RETURN
1150 PRINT : PRINT "LETTERS ONLY PLEASE!"
1160 RETURN
2500 :
2501 REM POKE FIRST WORD
2510 FOR K=0 TO N : DR(K)=0 : NEXT
2520 D(1)=1 : D(2)=40
2525 K=INT((N-1)*RND(0))+2
2530 W$=W$(1) : W$(1)=W$(K) : W$(K)=W$
2535 D$=D$(1) : D$(1)=D$(K) : D$(K)=D$
2540 T=1 : W$=W$(T) : LW=LEN(W$)
2550 K=INT( 2*RND(0))+1 : DR(T)=D(K)
2560 X=INT(18*RND(0))+9 : IF K=1 AND X+LW>30 GOTO 2560
2570 Y=INT(10*RND(0))+6 : IF K=2 AND Y+LW>19 GOTO 2570
2580 LC=32768+40*Y+X
2590 GOSUB 7000 POKE WOND
2599 RETURN
2800 :
2801 REM PROMPT TO ENTER DITTO
2810 POKE 216,25 : PRINT CHR$(145);BL$;CHR$(19);BL$
2820 PRINT "      INSERT PAPER AND PRESS ";CHR$(18);"RETURN";CHR$(146)
2830 GET G$ : GET G$ : GET G$
2840 GET G$ : IF G$<>CHR$(13) GOTO2840
2850 RETURN
3000 :
3001 REM MAKE CROSSWORD
3010 T=T+1 : U=0
3020 FOR C=1 TO N : IF DR(C)<>0 THEN U=U+1 : G(U)=C : NEXT C
3030 FOR I=1 TO U :S=1+INT(U*RND(1)) :GG=G(I) :G(I)=G(S) :G(S)=GG :NEXT I
3100 :
3110 W$=W$(T) : LW=LEN(W$) : NF=NF+1
3130 PRINT CHR$(19);"       ";W$;"           "
3170 FOR C=1TO U : E=G(C) : C$=W$(E)
3190 FOR L=1 TO LW      : Z1$= MID$(W$,L,1)
3210 FORZQ=1 TO LEN(C$) : Z2$= MID$(C$,ZQ,1)
3220 IF Z1$ <> Z2$ THEN 3370
3240 IF DR(E)=40 THEN DR(T)=1
3250 IF DR(E)=1 THEN DR(T)=40
3280 LC= LO(E) +DR(E)*ZQ -DR(T)* L
3281 IF LC<32895 THEN 3370
3282 IF LC+LW*DR(T)>33640 THEN 3370
3284 WT=PEEK(LC)
3285 IF WT<27THEN3360
3286 IF PEEK(LC+(LW+1)*DR(T))<27 THEN3360
3290 POKELC ,102
3301 :
3305 O=0
3310 FOR V=1 TO LW
3315 Q0=LC+V*DR(T) : WI=PEEK (Q0) : POKE Q0,102 : POKEQ0,WI
3317 IF DR(T)=40 THEN 3322
3318 IF PEEK(Q0+40)<>32 AND FQ%(Q0+40-AD)=DR(T) THEN 3345
3319 IF PEEK(Q0-40)<>32 AND FQ%(Q0-40-AD)=DR(T) THEN 3345
3320 GOTO 3324
3322 IF PEEK(Q0 +1)<>32 AND FQ%(Q0 +1-AD)=DR(T) THEN 3345
3323 IF PEEK(Q0 -1)<>32 AND FQ%(Q0 -1-AD)=DR(T) THEN 3345
3324 FOR Y=1 TO U : IF DR(Y)=DR(T) THEN 3327
3325 IF Q0=LO(Y)THEN O=1 :Y=U :GOTO3327
3326 IF Q0=LO(Y)+DR(Y)*(LEN(W$(Y))+1) THEN O=1 :Y=U
3327 NEXT Y
3328 IF O=1 GOTO 3345
3340 IF WI<>32 AND WI<> ASC( MID$(W$,V,1))-64 THEN 3345
3344 GOTO 3350
3345 V=LW : O=1
3350 NEXT V
3355 IF O=0 THEN GOSUB 7000 : NF=0 :ZQ=LEN(C$) : L=LW : C=U
3360 POKE LC,WT
3370 NEXTZQ,L,C
3410 IF NF=0 GOTO 3490
3420 IF N-U=NF GOTO 3499
3430 W$=W$(U+1) : D$=D$(U+1)
3440 FOR C=U+2 TO N : W$(C-1)=W$(C) : D$(C-1)=D$(C) : NEXT C
3475 W$(N)=W$ : D$(N)=D$
3480 GOTO 3100
3490 IF T<N GOTO 3000
3499 RETURN
4000 :
4010 OPEN6,4,6 : PRINT#6,CHR$(18):RETURN
4500 Q$(SC)=Q$(SC)+LEFT$(L$,1)+CHR$(254)+" ":SC=SC+1
4530 FORI=1TOLEN(Q$(SC-1)):Q$(SC)=Q$(SC)+" ": NEXT I
4540 RETURN
5000 :
5001 REM  PRINT STUDENTS' COPY
5030 PRINT# 1,CHR$(19)
5040 PRINT#1, SPC(34);CHR$(1);"ACROSS"
5060 PRINT#1, SPC(34);CHR$(1);CHR$(184);CHR$(184);CHR$(184);CHR$(184);CHR$(184);CHR$(184)
5080 FOR I=1 TO A
5090 PRINT#1,NA(I);" ";DA$(I)
5110 NEXT I
5111 PRINT#1
5120 PRINT#1,SPC(36);CHR$(1);"DOWN"
5130 PRINT#1,SPC(36);CHR$(1);CHR$(184);CHR$(184);CHR$(184);CHR$(184)
5150 FOR I=1 TO D
5160 PRINT#1,ND(I);" ";DD$(I)
5180 NEXT I
5200 RETURN
6000 :
6001 REM   INPUT DATA
6010 GOSUB50:B$=Z$
6020 IF B$="E" THEN FL=1
6030 RETURN
7000 :
7001 REM POKING THE WORD
7010 FOR I=1 TO LW : POKE LC+I*DR(T) , ASC (MID$ (W$,I,1))-64
7020 FQ=LC+I*DR(T)-32768 : FQ%(FQ)=DR(T)
7030 NEXTI
7040 CF=LC+DR(T) : CL=LC+LW*DR(T)
7050 C0= 40*INT(CF/40) : CF=CF-C0
7060 C1= 40*INT(CL/40) : CL=CL-C1 :C1=C1+40
7070 IF C0<UB THEN UB=C0
7080 IF C0>LB THEN LB=C0
7090 IF C1<UB THEN UB=C1
7100 IF C1>LB THEN LB=C1
7150 LO(T)=LC
7160 RETURN
8000 :
8001 REM SET UP
8020 DIM LC(25),DR(25),LO(25),G(25),FQ%(1000),A$(19),DA$(25),DD$(25)
8025 DIM TB(25),Q$(25),NA(25),ND(25)
8030 QZ= RND (RND (-TI^3)) : REM SEED RANDOM GENERATOR
8040 AD=32768
8050 BL$="                                       "
8400 :
8410 FOR J=0TO9
8420 FOR I=1 TO 8 : READ A : A$(J)=A$(J)+CHR$(A) : NEXT I
8425 A$(J+10)=CHR$(124)+CHR$(0)+LEFT$(A$(J),6)
8430 NEXT J
8480 UB=LC : LB=LC+DR(T)*(LEN(W$)+1)
8490 RETURN
8500 DATA 56,68,68,56,0,0,0,0
8510 DATA 124,0,0,0,0,0,0,0
8520 DATA 36,76,84,36,0,0,0,0
8530 DATA 0,68,84,104,0,0,0,0
8540 DATA 24,40,124,8,0,0,0,0
8550 DATA 116,84,84,72,0,0,0,0
8560 DATA 56,84,84,72,0,0,0,0
8570 DATA 64,76,80,96,0,0,0,0
8580 DATA 40,84,84,40,0,0,0,0
8590 DATA 36,84,84,56,0,0,0,0
9000 :
9001 REM INSTRUCTIONS
9010 PRINTCHR$(147);"DO YOU WANT INSTRUCTIONS (Y/N)  ";:ZC$="YN":GOSUB60
9020 IF Z$="N"THEN 9310
9024 PRINTCHR$(147);TAB(12)CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164):PRINTTAB(12)CHR$(18);"CROSSWORD PUZZLE"
9030 PRINTCHR$(17);"CROSSWORD PUZZLE ALLOWS THE USER TO MAKE";
9040 PRINTCHR$(17);"A CROSSWORD PUZZLE."
9060 PRINT : PRINT : PRINT"AS SOON AS YOU RUN THE PROGRAM THE "
9070 PRINTCHR$(17);"COMPUTER WILL ASK YOU TO ENTER WORDS AND";
9080 PRINTCHR$(17);"THEIR CORRESPONDING DEFINITIONS.  ALWAYS";
9090 PRINTCHR$(17);"END THE CURRENT LINE YOU'RE TYPING IN BY";
9100 PRINTCHR$(17);"PRESSING THE <RETURN> KEY."
9110 GOSUB 9500
9120 PRINTCHR$(147);"THERE ARE EDITING FEATURES IN THIS"
9130 PRINT"PROGRAM WHICH MAKE IT EASIER TO USE."
9180 PRINT:PRINT:PRINT"TO CORRECT THE PREVIOUS LINE ENTERED HIT";
9190 PRINT"<RETURN> BEFORE TYPING ANYTHING ELSE."
9200 PRINT:PRINT:PRINT"WHEN YOU HAVE FINISHED ENTERING ALL "
9210 PRINT"OF YOUR NAMES AND DEFINITIONS TYPE ";CHR$(18);"E";CHR$(146);" AND";
9220 PRINT"<RETURN>."
9230 GOSUB9500
9240 PRINTCHR$(147);"WHEN YOU ENTER A WORD FOR THE PUZZLE YOU";
9250 PRINTCHR$(17);"MUST USE LETTERS ONLY.  DEFINITIONS MAY"
9255 PRINTCHR$(17);"CONTAIN ANY CHARACTER FOUND ON THE PET"
9270 PRINTCHR$(17);"KEYBOARD.  THE DEFINITION MAY BE UP TO"
9280 PRINTCHR$(17);"TWO LINES LONG."
9285 PRINTCHR$(17);CHR$(17);"THE PROGRAM CAN HANDLE UP TO 19 WORDS   ";CHR$(17);"AND THEIR DEFINITIONS."
9290 GOSUB9500
9310 RETURN
9500 PRINTCHR$(19);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);CHR$(17);"PRESS ";CHR$(18);"RETURN";CHR$(146);CHR$(145);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(157);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(164);CHR$(17);" TO CONTINUE ";
9510 ZC$=CHR$(13):GOSUB60
9520 RETURN
10000 :
10001 REM TITLE
10010 PRINTCHR$(147);
10020 POKE216,12 : PRINTTAB(12);CHR$(145);
10030 TT$="CROSSWORD PUZZLE"
10040 FOR I=1TOLEN(TT$)
10050 PRINTCHR$(166);
10060 FORK=1TO50 : NEXT
10070 PRINTCHR$(157);MID$(TT$,I,1);
10080 NEXT I
10085 PRINT
10090 POKE216,18
10100 RETURN
11000 :
11020 PRINT CHR$(147);CHR$(18);CHR$(17);CHR$(17);CHR$(17);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);
11030 FORI=1 TO 26 : PRINTCHR$(162); :NEXT :PRINTCHR$(187);CHR$(157);CHR$(17);
11040 FOR I=1TO18 : PRINTCHR$(161);CHR$(157);CHR$(17); :NEXT :PRINTCHR$(190);CHR$(157);CHR$(157);CHR$(146);
11050 FOR I= 1TO 25 :PRINTCHR$(162);CHR$(157);CHR$(157); :NEXT:PRINTCHR$(18);CHR$(188);CHR$(146);CHR$(157);CHR$(145);
11060 FOR I=1TO18 :PRINTCHR$(161);CHR$(157);CHR$(145); :NEXT:PRINTCHR$(18);CHR$(172);CHR$(146)
11070 RETURN
11090 END
13000 :
13001 REM PRINT CROSSWORD GRID
13020 PRINT#1, SPC(40-INT(LEN(TL$)/2));CHR$(1);TL$
13030 PRINT#1 : PRINT#1
13040 A=0 : D=0 : CW=0 : EE=0
13100 :
13110 FOR R=4 TO 22
13120 FOR I=1 TO SC :Q$(I)="" :NEXT
13130 P$="" : R$="" : SC=1 : E=0
13140 PRINT CHR$(19);CHR$(17);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);CHR$(29);R-3
13200 :
13210 FOR C=7 TO 32
13220 XX=AD+40*R+C : X=PEEK(XX)
13230 IF X>26 GOTO 13700  NOT A LETTER
13300 :
13310 P$=P$+CHR$(219)+CHR$(192)+CHR$(192) : FS=0 : E=1
13320 FOR I=1 TO N
13330 IF LO(I)+DR(I)<>XX GOTO 13370
13340 IF DR(I)=1  THEN A=A+1 : NA(A)=CW+SC : DA$(A)=D$(I) :
13350 IF DR(I)=40 THEN D=D+1 : ND(D)=CW+SC : DD$(D)=D$(I)
13360 FS=1
13370 NEXT I
13400 :
13410 IF FS=0 THEN Q$(SC)=Q$(SC)+CHR$(221)+"  " : GOTO 13500
13420 Q$(SC)=Q$(SC)+CHR$(221)+CHR$(254)+" "
13430 SC=SC+1 : TB(SC)=3*(C-6)
13500 :
13510 IF IO=1 THEN R$=R$+CHR$(221)+"  " : GOTO 13900
13520 R$=R$+CHR$(221)+" "+CHR$(X+64) : GOTO 13900
13700 :
13710 Y=PEEK(XX-1) :ZQ=PEEK(XX-40) : V=PEEK(XX-41)
13720 IFZQ<27 THEN P$=P$+CHR$(219)+CHR$(192)+CHR$(192) : GOTO 13800
13730 IF V>26 AND Y>26 THEN P$=P$+"   " : GOTO 13800
13740 P$=P$+CHR$(219)+"  "
13800 :
13810 IF Y<27 THEN Q$(SC)=Q$(SC)+CHR$(221)+"  " : R$=R$+CHR$(221)+"  " : GOTO 13900
13820 Q$(SC)=Q$(SC)+"   " : R$=R$+"   "
13900 :
13910 NEXT C
13915 IF E+EE=0 GOTO 13960
13920 EE=1 : PRINT#1, P$
13930 GOSUB 14000 PRINT Q$
13940 PRINT#1, R$
13950 CW=CW+SC-1
13955 IF E+EE=1 THEN R=22
13960 NEXT R
13990 RETURN
14000 :
14010 IF SC=1 GOTO 14060
14020 FOR I=1 TO SC-1
14030 PRINT#2, A$(CW+I)
14040 PRINT#1, TAB(TB(I));Q$(I);CHR$(141);
14050 NEXT I
14060 PRINT#1, TAB(TB(SC));Q$(SC)
14070 RETURN
