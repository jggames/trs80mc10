10 CLEAR200:DIMG(5,5),C$(15),I,J,Z,X,Y,C,M:I=RND(-TIMER):GOSUB97:GOSUB83:GOSUB89:SCREEN0,1
15 FORC=1 TO8:FORI=1 TO7:C$(C)=C$(C)+CHR$(143+((C-1)*16)):NEXT:NEXT
16 FORC=9 TO15:FORI=1 TO7:C$(C)=C$(C)+CHR$(137+((C-8)*16)):NEXT:NEXT
20 M=-1:GOSUB1000:GOTO105
83 ' ENABLE QPRINT
84 FORI=3584TO3660:READA$:POKEI,VAL("&H"+A$):NEXT:EXEC3584:RETURN
85 ' DISABLE QPRINT
86 EXEC3630:RETURN
87 DATA CE,1,67,30,8D,0,3F,AC,41,27,22,A6,C4,A7,8D,0,3E,EC,41,ED,8D,0,39,86,7E,A7,C4,AF,41,8E,BC,AB,B6,FF,FF,81,B4
88 DATA 27,3,8E,A3,A,BF,E,4D,39,30,8D,0,14,BC,1,68,26,E,A6,8D,0,14,B7,1,67,EC,8D,0,E,FD,1,68,39,D,6F,26,5,32,62,7E
89 ' ENABLE DRAGON SPEEDKEY
90 IF PEEK(65535)<>180 THEN 92
91 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
92 RETURN
93 ' DISABLE DRAGON SPEEDKEY
94 IF PEEK(65535)<>180 THEN 96
95 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
96 RETURN
97 CLS:A$="Y":IFPEEK(65535)<>27 THEN INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
99 POKE209,255:POKE210,255:IF A$="Y" THEN 100 ELSE IF A$="N" THEN 101 ELSE 97
100 POKE210,215:IFPEEK(65535)=27 THEN POKE210,217:POKE65497,0 ELSE POKE 65495,0
101 RETURN
105 CLS:PRINT:IFM THEN GOSUB1000
108 L=-1:W=0:FORI=1 TO4:PRINT"  ";:FORJ=1 TO4:IFG(I,J)=0 THENPRINTSTRING$(7,128);:GOTO115
110 C=LOG(G(I,J))/LOG(2):PRINTC$(C);
115 NEXT:PRINT"    ";:FORJ=1 TO4:IFG(I,J)=2048 THENW=-1:L=0
130 IFG(I,J)=0 THENL=0:PRINTSTRING$(7,128);:GOTO150
140 C=LOG(G(I,J))/LOG(2):Z=LEN(STR$(G(I,J))):PRINTRIGHT$(C$(C)+RIGHT$(STR$(G(I,J)),Z-1),5);LEFT$(C$(C),2);
150 NEXT:PRINT"    ";:FORJ=1 TO4:IFG(I,J)=0 THENPRINTSTRING$(7,128);:GOTO152
151 C=LOG(G(I,J))/LOG(2):PRINTC$(C);
152 NEXT:PRINT:NEXT
190 X$=INKEY$:IFX$="" THEN190
200 IFW THENPRINT"YOU WIN!!!",:GOTO620
210 M=0:IFX$="0" THEN620
230 IFX$<>CHR$(94)THEN300
240 FORJ=1 TO4:FORZ=1 TO3:FORI=2 TO4:IFG(I-1,J)=0 THENG(I-1,J)=G(I,J):G(I,J)=0:M=1
250 NEXT:NEXT:FORI=2 TO4:IFG(I-1,J)=G(I,J) THENG(I-1,J)=G(I,J)*2:G(I,J)=0:M=1:S=S+G(I-1,J)
280 NEXT:NEXT:FORJ=1 TO4:FORZ=1 TO3:FORI=2 TO4:IFG(I-1,J)=0 THENG(I-1,J)=G(I,J):G(I,J)=0:M=1
290 NEXT:NEXT:NEXT:GOTO600
300 IFX$<>CHR$(8)THEN400
310 FORI=1 TO4:FORZ=1 TO3:FORJ=2 TO4:IFG(I,J-1)=0 THENG(I,J-1)=G(I,J):G(I,J)=0:M=1
320 NEXT:NEXT:FORJ=2 TO4:IFG(I,J-1)=G(I,J) THENG(I,J-1)=G(I,J)*2:G(I,J)=0:M=1:S=S+G(I,J-1)
330 NEXT:NEXT:FORI=1 TO4:FORZ=1 TO3:FORJ=2 TO4:IFG(I,J-1)=0 THENG(I,J-1)=G(I,J):G(I,J)=0:M=1
340 NEXT:NEXT:NEXT:GOTO600
400 IFX$<>CHR$(9)THEN500
410 FORI=1 TO4:FORZ=1 TO3:FORJ=3 TO1 STEP-1:IFG(I,J+1)=0 THENG(I,J+1)=G(I,J):G(I,J)=0:M=1
420 NEXT:NEXT:FORJ=3 TO1 STEP-1:IFG(I,J+1)=G(I,J) THENG(I,J+1)=G(I,J)*2:G(I,J)=0:M=1:S=S+G(I,J+1)
430 NEXT:NEXT:FORI=1 TO4:FORZ=1 TO3:FORJ=3 TO1 STEP-1:IFG(I,J+1)=0 THENG(I,J+1)=G(I,J):G(I,J)=0:M=1
440 NEXT:NEXT:NEXT:GOTO600
500 IFX$<>CHR$(10)THEN580
510 FORJ=1 TO4:FORZ=1 TO3:FORI=3 TO1 STEP-1:IFG(I+1,J)=0 THENG(I+1,J)=G(I,J):G(I,J)=0:M=1
520 NEXT:NEXT:FORI=3 TO1 STEP-1:IFG(I+1,J)=G(I,J) THENG(I+1,J)=G(I,J)*2:G(I,J)=0:M=1:S=S+G(I+1,J)
530 NEXT:NEXT:FORJ=1 TO4:FORZ=1 TO3:FORI=3 TO1 STEP-1:IFG(I+1,J)=0 THENG(I+1,J)=G(I,J):G(I,J)=0:M=1
540 NEXT:NEXT:NEXT:GOTO600
580 GOTO190
600 IFM=0ANDL THENPRINT"YOU LOSE!",:GOTO620
610 GOTO105
620 PRINT"SCORE"S:IFPEEK(65535)=27THENPOKE65496,0 ELSE POKE65494,0
630 END
1000 X=RND(4):Y=RND(4):IFG(X,Y)=0 THENG(X,Y)=2+2*INT(RND(0)*2):RETURN
1010 GOTO1000
