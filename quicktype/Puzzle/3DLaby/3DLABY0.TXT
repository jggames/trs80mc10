1 REM IRRGARTEN IN 3 D BASIERT AUF EINEN PROGRAMM AUS DER ELCOMP
2 REM UND WAR FUER DEN APPLE GESCHRIEBEN,UMGESCHRIEBEN VON
3 REM DIETER FREYER 1 BERLIN 36
4 REM TEL.030/611 71 12
5 REM   > 26.5.84 <             UND UM EINIGES ERWEITERT....
6 U=28672:CLS
7 POKE30862,80:POKE30863,52
10 GOTO7000
100 INPUT"GROESSE IN X-RICHTUNG :";WX
101 IFWX<4ORWX>30THENPRINT"ZWISCHEN 4 UND 30 ":PRINT:GOTO100
102 PRINT:PRINT
103 FORK=1TOWX:Q=RND(9):NEXT
104 INPUT"GROESSE IN Y-RICHTUNG:";WY
105 IFWY<4ORWY>15THENPRINT"ZWISCHEN 4 UND 15 ":PRINT:GOTO104
106 IFINT(WX/2)<>WX/2THENWX=WX+1
107 IFINT(WY/2)<>WY/2THENWY=WY+1
120 PRINT:INPUT"SCHWIERIGKEITSSTUFE 1-4";S:IFS<1ORS>4THEN120
200 DIMA(WX,WY)
220 PRINT"MOMENT BITTE":X=INT(WX/2):Y=INT(WY/2):A(X,Y)=1
230 IFRND(2)>1.1THENXS=SGN(RND(2)-1.1):YS=0:GOTO250
240 XS=0:YS=SGN(RND(2)-1.1)
250 FORI=0TO1:X=X+XS:Y=Y+YS:A(X,Y)=1
255 IFFTHENIFX=1ORX=WX-1ORY=1ORY=1ORY=WY-1THEN280
260 IFX=0ORX=WXORY=0ORY=WYTHEN280
270 NEXT:GOTO230
280 F=F+1:IFF<STHEN220
2000 CLS:COLOR3
2010 X=INT(WX/2):Y=INT(WY/2):X1=X:Y1=Y:R=0
2020 X(0)=1:Y(0)=0:X(1)=0:Y(1)=-1:X(2)=-1:Y(2)=0:X(3)=0:Y(3)=1
2030 RR=R+1:IFRR>3THENRR=0
2035 XR=X(RR):YR=Y(RR)
2040 SOUND3,1:X1=X:Y1=Y:I=1.5:MODE(1):GOSUB3000
2045 FORC%=8TO119:SET(C%,9):SET(C%,54):NEXT
2046 FORC%=9TO54:SET(8,C%):SET(119,C%):NEXT
2050 I=I+.5:X1=X1+X(R):Y1=Y1+Y(R)
2051 IFX1<0ORX1>WXORY1<0ORY1>WYTHENGOSUB3300:GOTO2065
2052 IFA(X1,Y1)=0THEN2065
2055 GOSUB3000
2060 GOTO2050
2065 GOSUB4000
2070 JO=(X5T(43)AND31)
2072 A$=INKEY$
2075 IFJO=15ORINKEY$="X"THENGOSUB5000:GOTO2040
2080 IFJO=27ORINKEY$="M"THENR=R-1:GOTO2200
2085 IFJO=23ORINKEY$=","THENR=R+1:GOTO2200
2090 IFJO=30ORINKEY$="."THEN2100
2095 GOTO2070
2100 X1=X+X(R):Y1=Y+Y(R):IFX1<0ORX1>WXORY1<0ORY1>WYTHEN6000
2110 IFA(X1,Y1)=0THEN2070
2115 X=X1:Y=Y1:GOTO2030
2200 SOUND31,1:IFR<0THENR=3:GOTO2030
2210 IFR>3THENR=0
2220 GOTO2030
3000 A1=60+59/I:B1=25+24/I:B2=60-B1:A3=127-A1
3001 B3=B1:B4=B2
3010 C1=60+59/(I-.5):D1=25+24/(I-.5):D2=60-D1:C3=127-C1
3011 D3=D1:D4=D2
3015 XT=X1+XR:YT=Y1+YR:IFXT<0ORXT>WXORYT<0ORYT>WYTHEN3040
3020 IFA(XT,YT)=1THEN3040
3029 COLOR2:BA=B1:BB=B2:FORK%=A1TOC1STEP2
3030 FORQ%=BBTOBASTEP2:SET(K%,Q%):NEXT:BB=BB-1:BA=BA+1:NEXT
3031 GOTO3050
3040 COLOR3:FORK=A1TOC1:FORQ=B2TOB1:SET(K,Q):NEXT:NEXT
3050 COLOR2:FORQ=B2TOB1:SET(A1,Q):NEXT:AQ=USR(AQ)
3090 XT=X1-XR:YT=Y1-YR:IFXT<0ORXT>WXORYT<0ORYT>WYTHEN3140
3100 IFA(XT,YT)=1THEN3140
3129 COLOR2:BB=B4:BA=B3:FORK=A3TOC3STEP-2
3130 FORQ=BBTOBASTEP2:SET(K,Q):NEXT:BB=BB-1:BA=BA+1
3131 NEXT:GOTO3150
3140 COLOR3:FORK=A3TOC3STEP-1:FORQ=B4TOB3:SET(K,Q):NEXT:NEXT
3150 COLOR2:FORQ=B4TOB3:SET(A3,Q):NEXT:AQ=USR(AQ)
3200 RETURN
3300 COLOR4
3310 FORK=B2TOB1:SET(62,K):NEXTK:FORQ=A3TOA1:SET(Q,30):NEXT
3320 RETURN
4000 COLOR2
4010 FORK%=A3TOA1STEP2:FORQ%=B2TOB1:SET(K%,Q%):NEXT:NEXT
4020 RETURN
5000 CLS:FORZ=0TOWY:FORQ=WXTO0STEP-1
5005 IFQ=XTHENIFZ=YTHENPRINT"O";:GOTO5050
5010 IFA(Q,Z)=0THEN5040
5015 IFA(Q,Z)=1THENPRINT" ";:GOTO5050
5020 IFQ=XANDZ=YTHENPRINT"*";:GOTO5050
5040 PRINT"";
5050 NEXT:PRINT:NEXT:PRINT
5055 A$=INKEY$
5060 IFINKEY$="X"THENRETURNELSEA$=INKEY$:GOTO5060
6000 COLOR1:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
6002 PRINT"    €ŒŒŒŒŒŒŒŒŒŒŒŒŒŒ€
6003 COLOR2
6004 PRINT"    €€
6006 PRINT"     €ÄÅÍ€
6008 PRINT"      €€
6010 PRINT"       €ÓÉÅÇÅÒ€
6012 PRINT"        €€
6014 PRINT"         „ŒŒŒŒˆ          €€€
6016 PRINT"           …Š            €ñ€
6018 PRINT"           …Š         €€€€€€€€€
6020 PRINT"          ‡‹‚        €ò€€€€€ó€
6021 COLOR5
6022 PRINT"         €ŒŒŒŒ€
6029 G=250
6030  COLOR1:PRINT@G,"â";:PRINT@G+31,"‰€†";:PRINT@G+64,"€";
6032 PRINT@G+95,"‰ †";
6033 SOUND5,2;10,2;15,2;20,2;25,2;30,5;20,3;30,3;20,3;30,3;20,1
6040 FORX=1TO20:PRINT@G+31,"ƒ€ƒ";:FORZ=1TO100:NEXTZ
6041 PRINT@G+31,"‰€†";:FORZ=1TO100:NEXTZ:NEXTX
6050 INPUT"NOCHMAL J/N";A$:IFA$="J"THENRUN
6055 CLS:PRINT@480,"T S C H U E S S ! !":END
7000 COLOR3
7005 PRINT
7010 PRINT" ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ‚"
7020 GOSUB7500
7030 PRINT" …      IRRGARTEN IN 3D       Š":GOSUB7500
7040 PRINT" „ŒŒŒŒŒÖÏÎàÄÉÅÔÅÒàÆÒÅÙÅÒŒŒŒŒŒŒˆ"
7100 RESTORE:FORX=1TO9:READA:READB:SOUNDA,B:NEXT
7120 CLS:PRINT"ZIEL DES SPIELES IST DEN AUS-
7130 PRINT"GANG IN EINEN LABYRINTH ZU
7140 PRINT"FINDEN,IN DEM SIE SICH BEFINDEN.
7150 PRINT"MIT DEN TASTEN >M< , >,< UND
7160 PRINT">.< LINKS,RECHTS ODER GERADEAUS
7170 PRINT"MIT DER TASTE >X< SEHEN SIE DAS
7180 :REMx8
7190 PRINT"LABYRINTH VON OBEN,NOCHMALIGES
7200 PRINT"DRUECKEN FUEHRT WIEDER ZURUECK.
7210 PRINT"EIN KREUZ KENNZEICHNET IHREN
7220 PRINT"AUSGANG. ÖÉÅÌàÓÐÁÓÓàÂÅÉÍàÓÐÉÅÌá"
7250 PRINT@480,"×ÅÉÔÅÒàÍÉÔàÒÅÔÕÒÎþþ";:INPUTA
7300 CLS:GOTO100
7500 FORX=1TO3:PRINT" …";TAB(30)"Š":NEXT:RETURN
8000 DATA23,2,20,2,23,2,20,2,21,2,20,2,18,2,16,2,0,3
