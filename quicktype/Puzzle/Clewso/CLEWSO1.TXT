0 CLEAR500:DIMC1,C2,M$,I$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 PRINT:INPUT"HIT ENTER TO CONTINUE";M$:CLS:RETURN
9 M$="BILL, MARY, JOHN, SUZY AND PAUL ARE THE HOUSE GUESTS.":GOSUB1:RETURN
10 CLS:DIMP(5,9)
11 REM S$(20),R$(36),A$(3),Z$(36),Y$(36),M$(36)
20 PRINT"********************************";
30 PRINT"*                              *";
40 PRINT"*      INSPECTOR CLEW-SO       *";
50 PRINT"*      BY: RON CARLSON         *";
60 PRINT"*      FEB. 1979               *";
70 PRINT"*                              *";
80 PRINT"********************************";
90 S$="BILLMARYPAULSUZYJOHN"
100 R$="LOUNGEATRIUMLIVINGDININGTROPHYGARAGE":GOTO210
120 REM (Z$,Y$,L,C2)
130 FORA=1TOC2
140 IFZ$<>MID$(Y$,(A-1)*L+1,L)THEN170
150 P=A:A=C2:NEXTA:GOTO190
170 NEXTA
180 P=0
190 RETURN
210 M$="BILL, MARY, JOHN, SUZY AND PAUL ARE HOUSE GUESTS.":GOSUB1
220 M$="THEIR HOST WAS MURDERED BY ONE OF THEM BETWEEN 1 PM. AND 9 PM.":GOSUB1
230 M$="YOUR JOB AS INSPECTOR CLEW-SO, IS TO FIND THE KILLER, TIME & ROOM.":GOSUB1:GOSUB3
240 M$="YOU WILL BE GIVEN A HOUSE DIAGRAM AND A SET OF QUESTIONS":GOSUB1
250 M$="FOR THE SUSPECTS, BUT THE GUILTY PERSON MAY TRY TO MISLEAD BY LYING SOME OF THE TIME.":GOSUB1
270 M$="IF ONE OF THE SUSPECTS CLAIM THAT THE HOST WAS ALREADY DEAD, "
280 M$=M$+"OR THAT THE HOST WAS STILL ALIVE, THEN YOU HAVE FOUND THE ROOM WHERE THE MURDER TOOK PLACE."
300 GOSUB1:GOSUB3
310 M=0:H=0
320 C=0:CF=0
330 C1=0:GOSUB340:GOTO451
340 PRINT"+--------!--------!--------+"
350 PRINT"!        !        O garage !"
360 PRINT"!        O trophy !        !"
370 PRINT"! dining !---!-- -+ -+     !"
380 PRINT"!                    !-----+"
390 PRINT"+--O---------!      -+     !"
400 PRINT"!                          +---+";
410 PRINT"!            !-- ------        !";
420 PRINT"! living  ---+       !         !";
430 PRINT"+--       !          O lounge  !";
440 PRINT"  !       O  atrium  !    -----+";
450 PRINT"  +-------!----------!----!"::RETURN
451 PRINT"O=OPEN DOORS TO CONNECTED ROOMS";:GOSUB3
460 FORK=1TO5
470 P(K,1)=INT(6*RND(0))+1
480 NEXTK
490 FORL=2TO9
500 FORK=1TO5
510 A=INT(6*RND(0))+1
520 IFA=P(K,L-1)THEN510
530 P(K,L)=A
540 NEXTK
550 NEXTL
560 REM ESTABLISHED SUSPECT'S MOVEMENTS
570 REM RANDOM ASSIGNMENT OF KILLER, TIME AND ROOM
580 M=INT(5*RND(0))+1
590 T=INT(9*RND(0))+1
600 R=P(M,T)
610 INPUT"INSPECTOR CLEW-SO WHO IS YOUR   SUSPECT";S1$:IFS1$=""THENGOSUB340:GOSUB9
620 Z$=S1$:Y$=S$:L=4:C2=5:GOSUB130:S=P
630 IFS=0THEN610
640 PRINT
650 M$="QUESTION "+S1$+" ABOUT:":GOSUB1
660 PRINT"1 - THE SUSPECT'S WHEREABOUTS"
661 PRINT"    AT A PARTICULAR TIME"
670 PRINT"2 - WHAT TIME THE SUSPECT WAS"
671 PRINT"    IN A CERTAIN ROOM"
680 PRINT"3 - THE CRIME IS SOLVED"
681 INPUTA
690 IF A<1 OR A>3 THEN650
700 C=C+1
710 ON A GOTO 720,990,1200
720 M$=S1$+" WHERE WERE YOU AT (TIME)?":GOSUB1
721 INPUTT1
750 IFT1<1 OR T1>9 THEN740
760 R1=P(S,T1)
770 IFS<>MTHEN860
780 REM LIAR SECTION
790 IFRND(0)<.5THEN910
800 R1=INT(6*RND(0))+1
810 IFRND(0)<.5THEN840
820 M$="THE HOST WAS ALREADY DEAD.":GOSUB1
830 GOTO910
840 M$="OUR HOST WAS STILL ALIVE.":GOSUB1
850 GOTO910
860 IFR1=RTHEN880
870 GOTO910
880 IFRND(0)<.5THEN910
890 IFT1<TTHENM$="THE HOST WAS STILL ALIVE.":GOSUB1
900 IFT1>TTHENM$="THE HOST WAS ALREADY DEAD.":GOSUB1
910 M$="I WAS IN THE "+MID$(R$,(R1-1)*6+1,6)+" ROOM.":GOSUB1
920 FORK=1TO5
930 IFK=STHEN960
940 IFP(K,T1)=R1THENM$="I WAS WITH "+MID$(S$,(K-1)*4+1,4):GOSUB1
950 IFABS(R1-P(K,T1))=1THENM$="I SAW "+MID$(S$,(K-1)*4+1,4):GOSUB1
960 NEXTK
970 GOTO610
980 REM ROOM QUESTIONING
990 M$=S1$+" WHAT TIME WHERE YOU IN (ROOM)?":GOSUB1
1000 INPUTR1$:IFR1$=""THENGOSUB340
1010 Z$=R1$:Y$=R$:L=6:C2=6:GOSUB130:T1=P
1020 IFT1=0THEN1000
1030 IFS<>MTHEN1110
1040 IFRND(0)<.5THEN1110
1050 T1=INT(RND(0)*6)+1
1060 GOTO1090
1070 IFT1=FTHENM$="I WAS NOT IN THAT ROOM.":GOSUB1
1080 GOTO610
1090 M$="I WAS IN THAT ROOM AT"+STR$(T1):GOSUB1
1100 GOTO610
1110 K=0
1120 FORB=1TO9
1130 IFP(S,B)<>T1THEN1160
1140 M$="I WAS IN THAT ROOM AT"+STR$(B):GOSUB1
1150 K=1
1160 NEXTB
1170 IFK=0THENM$="I WAS NOT IN THAT ROOM.":GOSUB1
1180 GOTO610
1190 REM CONFRONTATION SECTION
1200 CF=CF+1
1210 M$="INSPECTOR, DO YOU THINK YOU KNOW:":GOSUB1
1220 PRINT"  1  KILLER"
1230 PRINT"  2  ROOM"
1240 PRINT"  3  TIME"
1250 PRINT"  4  TOTALLY BAFFLED"
1251 INPUTA
1260 ONAGOTO1270,1420,1350,1550
1270 INPUT"THE KILLER IS";S1$
1280 Z$=S1$:Y$=S$:L=4:C2=5:GOSUB130:X=P
1290 IFX=0THEN1210
1300 IFS1$<>MID$(S$,(M-1)*4+1,4)THEN1530
1310 M$="YOU HAVE THE KILLER, INSPECTOR CLEW-SO.":GOSUB1
1320 H=H+1
1330 IFH=3THEN1500
1340 GOTO610
1350 INPUT"TIME OF THE MURDER";T1
1360 IFT1<1ORT1>9THEN1350
1370 IFT1<>TTHEN1530
1380 M$="INSPECTOR YOU HAVE THE RIGHT TIME.":GOSUB1
1390 H=H+1
1400 IFH=3THEN1500
1410 GOTO610
1420 INPUT"ROOM OF THE MURDER";R1$:IFR1$=""THENGOSUB340
1430 Z$=R1$:Y$=R$:L=6:C2=6:GOSUB130:X=P
1440 IFX=0THEN1420
1450 IFR1$<>MID$(R$,(R-1)*6+1,6)THEN1530
1460 M$="INSPECTOR, YOU NOW HAVE THE ROOM.":GOSUB1
1470 H=H+1
1480 IFH<>3THEN610
1490 REM CONFIRMATIONS
1500 M$="YOU ARE BRILLIANT INSPECTOR CLEW-SO!":GOSUB1
1510 M$="IT TOOK YOU"+STR$(C)+" QUESTIONS AND"+STR$(CF)+" CONFRONTATIONS.":GOSUB1
1520 GOTO1590
1530 M$="INSPECTOR CLEW-SO YOU ARE A BUMBLING IDIOT, TRY AGAIN.":GOSUB1
1540 GOTO610
1550 M$="TOO BAD INSPECTOR CLEW-SO, THE FACTS ARE:":GOSUB1
1570 M$=MID$(S$,(M-1)*4+1,4)+" KILLED THE HOST AT"+STR$(T)+" O'CLOCK IN THE "+MID$(R$,(R-1)*6+1,6)+" ROOM."
1580 GOSUB1
1590 PRINT"DO YOU WANT A NEW CASE":INPUT"INSPECTOR (Y/N)";A$
1600 IFLEFT$(A$,1)="Y"THEN210
1610 END
