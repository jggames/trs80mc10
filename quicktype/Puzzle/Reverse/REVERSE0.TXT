10 CLS:DIM D(8,6),A(9)
20 DATA 0,64,66,2,-62,-64,0 
30 DATA 0,64,68,4,-60,-64,0
40 DATA 0,128,134,6,-122,-128,0
50 DATA 0,128,136,8,-120,-128,0
60 DATA 0,192,202,10,-182,-192,0
70 DATA 0,192,204,12,-180,-192,0
80 DATA 0,256,270,14,-242,-256,0
90 DATA 0,256,272,16,-240,-256,0
105 CLEAR 100
110 FOR J=1 TO 8:FOR I=0 TO 6: READ D(J,I):NEXT I:NEXT J
120 CLS:PRINT@6,"THE GAME OF REVERSE";
122 PRINT@35,"DO YOU NEED INSTRUCTIONS?";
124 A$=INKEY$:IF A$="" THEN 124
126 IF A$="Y" THEN 3000
128 CLS:PRINT@6,"------REVERSE------"
130 PRINT@32,"HOW MANY NUMBERS DO YOU WANT?";
131 A$=INKEY$:ON-(A$="")GOTO131:N=VAL(A$)
132 PRINT@61,N;:IF N=0 OR N=1 THEN 232
133 C=-1:X7=468
134 FOR I=1 TO N
135 S=RND(N):IF A(S)=0 THEN A(S)=I:GOTO137
136 GOTO 135
137 NEXT I
140 FOR I=0 TO 2*(N-1) STEP 2 :PRINT@(X7+I)/2,MID$(STR$(A(I/2+1)),2);:NEXT
142 PRINT@33,;:GOTO210
150 PRINT@32,"":PRINT@37,"NUMBER TO REVERSE?";
152 A$=INKEY$:ON-(A$="")GOTO152:L1=VAL(A$)
153 PRINT@55,L1;
154 IF L1=0 GOTO 232
155 IF L1>N GOTO 150
160 FOR I=1 TO 3:X=X7:K1=1:K2=L1:L=L1
170 FOR J=1 TO INT(L/2):X1=D(L-1,I)+X
180 X2=D(L-1,I-1)+X:X3=D(L-1,I+3)+X:X4=D(L-1,I+2)+X
190 PRINT@X2/2," ";:PRINT@X1/2,MID$(STR$(A(K1)),2);:PRINT@X4/2," ";
192 PRINT@X3/2,MID$(STR$(A(K2)),2);
200 L=L-2:K1=K1+1:K2=K2-1:X=X+2:NEXT J:GOSUB 2000:NEXT I
205 K1=1:K2=L1:FOR I=1 TO INT(L1/2)
206 S=A(K1):A(K1)=A(K2):A(K2)=S:K1=K1+1:K2=K2-1
207 NEXT I
210 L1=0
220 FOR I=1 TO N:IF A(I)<>I THEN L1=1:GOTO 222
221 NEXT I
222 C=C+1
225 IF L1=1 GOTO 150
230 PRINT@32,"":PRINT@39,"THEY'RE IN ORDER!":PRINT"  IT TOOK THIS MANY REVERSES"+STR$(C)
232 PRINT@107,"":PRINT@107,"NEW GAME?";
233 A$=INKEY$:IF A$="" THEN 233
235 PRINT@117,A$;
236 FOR I=1 TO 9:A(I)=0:NEXT
240 IF A$="Y" THEN 128
242 CLS
245 PRINT"THANKS FOR PLAYING...";
246 END
2000 FOR M=1 TO 550:NEXT:RETURN
3000 GOSUB 3500
3020 PRINT"THIS IS THE GAME OF REVERSE.":PRINT"I WILL GIVE YOU A SCRAMBLED"
3030 PRINT"LIST OF NUMBERS.":PRINT"YOU UNSCRAMBLE THE LIST."
3040 PRINT"YOU PUT IT IN NUMERICAL ORDER.":PRINT"FOR EXAMPLE..."
3050 PRINT"SCRAMBLED LIST:       2514763"
3060 PRINT"PUT IT IN THIS ORDER: 1234567"
3070 PRINT"WHEN YOU ARE READY, HIT ANY KEY":PRINT"KEY, AND I WILL"
3080 PRINT"TELL YOU MORE ABOUT THE GAME":PRINT"OF REVERSE.";
3090 A$=INKEY$:IF A$="" GOTO 3090
3100 GOSUB 3500
3110 PRINT"YOU MAY REVERSE TWO OR MORE":PRINT"NUMBERS IN THE LIST,"
3120 PRINT"STARTING WITH THE LEFT END OF":PRINT"THE LIST. FOR EXAMPLE:"
3140 PRINT"ORIGINAL LIST 2514763"
3150 PRINT"REVERSE 3     1524763"
3160 PRINT"YOU MAY CHOOSE THE SIZE OF":PRINT"THE LIST, FROM 2 TO 9 NUMBERS."
3170 PRINT"HITTING ANY OTHER KEYS WHEN"
3180 PRINT"ASKED FOR LIST SIZE OR NUMBER"
3190 PRINT"TO REVERSE WILL GIVE YOU THE":PRINT"CHANCE TO START A NEW GAME."
3200 PRINT"IF YOU ARE READY TO BEGIN,":PRINT"HIT ANY KEY...";
3220 A$=INKEY$:ON-(A$="")GOTO3220:GOTO128
3500 CLS:PRINT"INSTRUCTIONS FOR REVERSE"
3510 PRINT
3520 RETURN
4000 REM ** REVERSE
4001 REM 16K LEVEL II BASIC
4003 REM TRS-80
4004 REM R. ZAMORA 9/13/78 **
