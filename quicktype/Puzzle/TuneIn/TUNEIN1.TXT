0 GOTO250
7 C3=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC3+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C3-M+C2,:RETURN
9 PRINT@C1,A$(I);:PRINT@C1+32,A$(I);:PRINT@C1+64,A$(I);:POKEC1+M+34,V(I):RETURN
10 PRINT@C1,B$(I);:PRINT@C1+32,C$(I);:PRINT@C1+64,D$(I);:RETURN

20 I=I+1+8*(I=8):C1=J(I):PRINT@C1,B$(I);:PRINT@C1+32,C$(I);:PRINT@C1+64,D$(I);:SOUNDM(I),8
21 GOSUB9:B$=INKEY$:IFB$=""THEN20
22 PL=VAL(B$):IFPL<1ORPL>4THEN20
25 PRINT@206,"ÄÄÄÄÄ";:PRINT@238,"ÄÄÄÄÄ";:PRINT@270,"ÄÄÄÄÄ";:GOTO50

30 B$=INKEY$:IFB$="."THENRUN
31 IFB$="0"THENIFPTHENNO(P)=NO(P)+1:FF=1
40 RETURN

REM Initialize information about all players.

50 FORP=1TO4:LV(P)=1:D(P)=0:LF(P)=3:NO(P)=1:NEXT:P=0

REM Check to see if any players have lives remaining.

60 Y=0:FORZ=1TOPL:Y=Y+LF(Z):NEXT:IFY=0THEN230

REM Determine which player is next.

70 P=P+1:IFP>PLTHENP=1
80 IFLF(P)=0THEN70
81 FORZ=1TOLV(P)*10:N(Z)=RND(8):NEXT:IFP=1THENS2=0:GOTO90
82 IFP=2THENS2=27:GOTO90
83 IFP=3THENS2=448:GOTO90
84 S2=475

REM Allow player to restart program, increase the number of notes he or she is to attempt, or start turn.

90 IFNO(P)>10THENNO(P)=1
100 B$=STR$(LV(P)*10-10+NO(P)):B$=CHR$(LF(P)+48)+"-"+LEFT$(O$,4-LEN(B$))+RIGHT$(B$,LEN(B$)-1)
101 FORI=1TO3:PRINT@S2,B$;:SOUND100,1:FORK=1TO150:NEXT
102 PRINT@S2,"ÄÄÄÄÄ";:FORK=0TO150:NEXT:NEXT:GOSUB190:PRINT@S2,M$;:PRINT@206,"START";:PRINT@239,"= 5";

110 GOSUB30:IFFFTHENFF=0:GOTO90
111 NN=0:IFB$<>"5"THEN110
112 PRINT@206,"ÄÄÄÄÄ";:PRINT@239,"ÄÄÄÄÄ";

REM Display current score, and check if the player has completed the required number of notes.
REM If not, then present note pattern.

120 GOSUB180:PRINT@S2,M$;:NN=NN+LV(P):FORZ=1TO4:GOSUB30:NEXT:IFNN>LV(P)*NO(P)THEN200
121 FORZ=1TONN:C1=J(N(Z)):I=N(Z):GOSUB10:SOUNDM(N(Z)),8:GOSUB9:FORY=1TO150:NEXT:NEXT:MM=0
130 MM=MM+1:IFMM>NNTHEN120

REM Input player's guess of the note sequence.

140 B$=INKEY$:IFB$=""THEN140
141 IFB$<"1"ORB$>"9"THENGOSUB30:ON-(FF=0):GOTO140:IFFFTHENFF=0:GOTO90
150 W=VAL(MID$("654703812",VAL(B$),1)):IFW=0THEN140
151 C1=J(W):I=W:GOSUB10
160 IFW=N(MM)THENSOUNDM(W),8:GOSUB10:D(P)=D(P)+10*LV(P):GOSUB180:PRINT@S2,M$;:GOSUB9:GOTO130
161 LF(P)=LF(P)-1:FORC3=1TO3:SOUND9,1:SOUND1,1:NEXT:GOSUB9:GOSUB195:GOTO60

REM Subroutine to wrap score around if necessary, and to display score.

180 IF999999<D(P)THEND(P)=0
190 B$=STR$(D(P)):M$=LEFT$(O$,6-LEN(B$))+RIGHT$(B$,LEN(B$)-1):RETURN
195 GOSUB190:PRINT@S2,;:GOSUB7:RETURN

REM This routine is reached when the player has completed the required number of notes.
REM If an increase in level is needed, that is done here. In any case, the program
REM allows the player to try a harder series of notes.

200 NO(P)=NO(P)+1:FORI=200TO215:SOUNDI,1:NEXT:GOSUB195:IFNO(P)>10THENNO(P)=1:LV(P)=LV(P)+1

210 IFLV(P)<19THEN60
211 CLS0:POKE49151,64:PRINT"CONGRATULATIONS, PLAYER"STR$(P)"!"
212 FORI=0TO999:NEXT:PRINT"YOU HAVE MANAGED TO OVERLOAD"
213 PRINTTAB(5)"THIS GAME. GOODBYE.":FORI=1TO4500:NEXT
214 FORI=0TO1STEP0:POKEM+RND(96)-1,128:FORC1=0TO99:NEXT:I=-(INKEY$<>""):NEXT:CLS0:SOUND100,1

REM When game is over, allow the game to be repeated.

230 PRINT@206,"AGAIN";:PRINT@240,"=";:PRINT@270,"ENTER";
240 IFINKEY$=CHR$(13)THENRUN
241 I=RND(8)
242 C1=J(I):GOSUB10:SOUNDM(I),4
243 GOSUB9:GOTO240

250 CLS:PRINT@173,"TUNEIN";:PRINT@225,"(C) WM. MORRIS & J. COPE 1982":PRINT@288,"MC-10 TRANSLATION BY JIM GERRIE"
251 CLEAR700:M=16384:DIMC3,C2,C1,N(200),M(8),LV(4),NO(4),D(4),LF(4),J(8),V(8),A$(8),B$(8),C$(8),D$(8):O$="00000000"
252 M(1)=87:M(2)=103:M(3)=113:M(4)=129:M(5)=142
260 M(6)=149:M(7)=161:M(8)=171:V(1)=56:V(2)=57:V(3)=54:V(4)=51:V(5)=50:V(6)=49:V(7)=52:V(8)=55
261 J(1)=14:J(2)=53:J(3)=214:J(4)=373:J(5)=398:J(6)=359:J(7)=198:J(8)=39
270 FORI=1TO8:A$(I)=MID$("èèèèèüüüüüØØØØØøøøøøœœœœœﬂﬂﬂﬂﬂÔÔÔÔÔˇˇˇˇˇ",(I-1)*5+1,5):NEXT
280 B$(1)="éååäé":C$(1)="äèÖää":D$(1)="àãÉÉä"
285 FORI=2TO8:FORC1=1TO5:B$(I)=B$(I)+CHR$(ASC(MID$(B$(1),C1,1))+16*(I-1))
286 C$(I)=C$(I)+CHR$(ASC(MID$(C$(1),C1,1))+16*(I-1))
287 D$(I)=D$(I)+CHR$(ASC(MID$(D$(1),C1,1))+16*(I-1)):NEXT:NEXT
REM 288 C$(I)=LEFT$(C$(I),1)+MID$(STR$(I),2,1)+MID$(C$(I),3):NEXT
REM 289 C$(1)=LEFT$(C$(1),1)+MID$(STR$(I),2,1)+MID$(C$(1),3)
290 C1=RND(-(PEEK(9)*256+PEEK(10)))
300 FORI=0TO2500:NEXT:CLS0:PRINT@206," HOW ";:PRINT@238,"MANY?";:PRINT@270,"(1-4)";
310 FORI=1TO8:C1=J(I):GOSUB9:NEXT:I=0:GOTO20


400 REM **********************
404 REM *      'TUNEIN'      *
405 REM *   WILLIAN MORRIS   *
406 REM *   AND JOHN COPE    *
407 REM * TRANSL: RONNY ONG  *
408 REM * COPYRIGHT (C) 1982 *
409 REM * SOFTSIDE PUB., INC *
410 REM **********************
