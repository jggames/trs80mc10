100 REM *** REVERSE - A GAME OF SKILL
110 REM *** PEOPLE'S COMPUTER COMPANY, MENLO PARK CA
120 A=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
130 CLS:DIM A(20)
140 REM *** N=NUMBER OF NUMBERS <1 THRU N)
150 N=9
160 PRINT "WANT THE RULES (1=YES 0=NO)";
170 INPUT A
180 IF A=0 THEN 210
190 GOSUB 710
200 REM *** MAKE A RANDOM LIST A(1) TO A(N)
210 A(1)=INT((N-1)*RND(0))+2
220 FOR K=2 TO N
230 A(K)=INT(N*RND(0))+1
240 FOR J=1 TO K-1
250 IF A(K)=A(J) THEN 230
260 NEXT J
270 NEXT K
280 REM *** PRINT ORIGINAL LIST AND START GAME
290 PRINT
300 PRINT "HERE WE GO... THE LIST IS:"
310 T=0
320 GOSUB 610
330 PRINT "HOW MANY SHALL I REVERSE";
340 INPUT R
350 IF R=0 THEN 520
360 IF R<=N THEN 390
370 PRINT "OOPS! TOO MANY--":PRINT"I CAN REVERSE AT MOST";N
380 GOTO 330
390 T=T+1
400 REM *** REVERSE R NUMBERS AND PRINT NEW LIST
410 FOR K=1 TO INT(R/2)
420 Z=A(K)
430 A(K)=A(R-K+1)
440 A(R-K+1)=Z
450 NEXT K
460 GOSUB 610
470 REM *** CHECK FOR A WIN
480 FOR K=1 TO N
490 IF A(K)<>K THEN 330
500 NEXT K
510 PRINT "YOU WON IN";T;"MOVES!"
520 PRINT
530 PRINT "AGAIN (1=YES 0=NO)";
540 INPUT A
550 IF A<>0 THEN 210
560 END
600 REM *** SUBROUTINE *** PRINT LIST A(1) TO A(N)
610 PRINT
620 FOR K=1 TO N
630 PRINT A(K);
640 NEXT K
650 PRINT
660 PRINT
670 RETURN
700 REM *** SUBROUTINE *** PRINT THE RULES
710 CLS
720 PRINT "THIS IS THE GAME OF 'REVERSE'."
721 PRINT "TO WIN, ALL YOU HAVE TO DO IS"
730 PRINT "ARRANGE THE LIST OF NUMBERS"
731 PRINT "(1 THROUGH";STR$(N);") IN NUMERICAL"
740 PRINT "ORDER FROM LEFT TO RIGHT. TO"
750 PRINT "MOVE, TELL ME HOW MANY NUMBERS"
751 PRINT "(COUNTING FROM THE LEFT) TO"
760 PRINT "REVERSE. FOR EXAMPLE, IF THE"
761 PRINT "CURRENT LIST IS:  234516789"
800 PRINT "AND YOU REVERSE 4, THE RESULT
801 PRINT "WILL BE:          543216789"
840 PRINT "NOW, IF YOU REVERSE 5 YOU WIN!"
880 PRINT "IF YOU WANT TO QUIT, REVERSE 0."
890 INPUT "HIT enter TO CONTINUE";M$
900 RETURN
999 END
1000 REM 1973-05 
1010 REM PEOPLE'S COMPUTER COMPANY
1020 REM MC-10 EDITS JIM GERRIE
