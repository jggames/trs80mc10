10 REM UNSCRAMBLE
20 CLS
30 PRINT "THE GAME OF
40 PRINT:PRINT "   ABCELMNRSU!"
50 PRINT:FOR X=1 TO 2500:NEXT X
52 PRINT "...UNSCRAMBLE!"
75 PRINT:INPUT "NAME, PLEASE";NA$:GOTO 100
80 R=INT(RND(0)*12)+1:REM CHANGE '12' TO NUMBER OF WORDS IN DATA LINES 500-
81 FOR K=1 TO R:READ O$
82 NEXT K:RESTORE
84 LO=LEN(O$):FOR LI=1 TO LO:W$(LI)=MID$(O$,LI,1):NEXT LI
85 FOR LL=2 TO LO:LI=LL-1:P$=W$(LL)
86 IF P$>W$(LI) THEN 90
88 W$ (LI+1)=W$(LI):LI=LI-1:IF L>0 THEN 86
90 W$(LI+1)=P$: NEXT LL:RETURN
100 CLS
102 Q=Q+1:PRINT "UNSCRAMBLE WORD #";Q :GOSUB 80
105 C=C+1:PRINT:PRINT "ATTEMPT #";C
110 PRINT:PRINT "UNSCRAMBLE THIS WORD...":PRINT
114 FOR LI=1 TO LO:PRINT W$(LI);:NEXTLI:PRINT
120 INPUT "ANSWER";A$:IF A$=O$ THEN 200
125 PRINT:IF T=1 THEN PRINT "SORRY, THE WORD IS":PRINT O$:GOTO 300
130 PRINT:PRINT NA$;", ONE LAST TRY":T=1:GOTO 105
200 PRINT:SOUND 200,1:V=V+1
250 PRINT:PRINT "CONGRATULATIONS, YOU WIN"
300 PRINT:INPUT "ANOTHER WORD";Y$:IF LEFT$(Y$,1)="Y" THEN T=0:GOTO 100
310 CLS
315 PRINT "OUT OF";Q;"UNSCRAMBLES"
320 PRINT NA$;" HAS";V;"CORRECT,"
325 PRINT "USING";C;"ATTEMPTS!"
330 P=INT((V/C)*100)
335 PRINT:PRINT "YOUR SCORE IS";P
400 PRINT:INPUT "ANOTHER GAME";Y$
405 IF LEFT$(Y$,1)="N" THEN END
410 V=0:Q=0:C=0:GOTO 20
500 DATA "EAR","TABLE","KITCHEN","MOTHER","COMPUTER"
510 DATA "FACE","AUTOMOBILE","RUBBERBAND","DIAMOND"
520 DATA "VIBRATE","TENACIOUS","MONSTER","ESCAPE"
600 REM HENRY KONG
610 REM SINGAPORE
620 REM COMPUTE! MAGAZINE
630 REM DEC. 1981
640 REM MC-10 JIM GERRIE
