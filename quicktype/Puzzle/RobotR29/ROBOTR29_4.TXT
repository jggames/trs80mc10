0 REM ROBOT R-29
4 REM TOMASZ ROGACEWICZ
5 REM "MOJE ATARI" 1991
10 A=0:GOSUB320:F=49151:NM=6:REM NM=THE NUMBER OF MAZES
40 PL=1:ZY=5:W=129
50 X=1:Y=1:B=0:C=0:D=0:E=0:CLS:ONPLGOSUB500,520,540,560,580,600:REM ADD CALL TO NEW LEVELS HERE AND CHANGE NM IN LINE 10
60 PRINT@32*0+22,"BOMBS-";:PRINT@32*4+22,"MAGNETS-";:PRINT@32*8+22,"SPANNERS-";:PRINT@32*12+22,"LIVES-";ZY
99 REM MAIN LOOP
100 X1=X:Y1=Y:K=PEEK(2)ANDPEEK(17023)
101 IFK=83THENX=X+1:W=129:POKE16384+32*Y1+X1,W
102 IFK=65THENX=X-1:W=130:POKE16384+32*Y1+X1,W
103 IFK=90THENY=Y+1
104 IFK=87THENY=Y-1
110 L=PEEK(16384+32*Y+X):IFL<>96ANDL<>129ANDL<>130THENGOSUB200
120 POKE16384+32*Y1+X1,96:POKE16384+32*Y+X,W:ONEGOTO50,40
130 GOTO100
200 IFL=79THENB=B+1:GOSUB400:PRINT@32*(1-(B>10)-(B>20))+21+B+(B>10)*10+(B>20)*10,"O";:RETURN
203 IFL=85THENC=C+1:GOSUB400:PRINT@32*(5-(C>10)-(C>20))+21+C+(C>10)*10+(C>20)*10,"U";:RETURN
204 IFL=64THEND=D+1:GOSUB400:PRINT@32*(9-(D>10)-(D>20))+21+D+(D>10)*10+(D>20)*10,"@";:RETURN
205 IFL=15THENPRINT@32*(1-(B>10)-(B>20))+21+B+(B>10)*10+(B>20)*10," ";:B=B-1:GOSUB410:X1=X:Y1=Y:IFB<0THEN300
206 IFL=21THENPRINT@32*(5-(C>10)-(C>20))+21+C+(C>10)*10+(C>20)*10," ";:C=C-1:GOSUB420:X1=X:Y1=Y:IFC<0THEN300
207 IFL=0THENPRINT@32*(9-(D>10)-(D>20))+21+D+(D>10)*10+(D>20)*10," ";:D=D-1:GOSUB430:X1=X:Y1=Y:IFD<0THEN300
208 IFL=69THEN350
299 X=X1:Y=Y1:RETURN
300 GOSUB405:POKE16384+32*Y+X,W:GOSUB410:ZY=ZY-1:PRINT@32*12+21,"LIVES-";ZY:IFZY<1THEN310
305 E=1:RETURN
310 PRINT@480,;:FORA=150TO75STEP-1:SOUNDA,1:PRINT"THE END.":NEXT:SOUND1,2:E=2
320 CLS
330 PRINT"           ROBOT R-29"
331 PRINT"        TOMASZ ROGACEWICZ"
332 PRINT"         ELBLAG 20.11.90":PRINT
333 PRINT" ¯ - WALL    ‚ - THE ROBOT":PRINT
334 PRINT"     tool        removes"    
335 PRINT" @ - SPANNER "CHR$(96)" - TEMPORAL FIELD"
336 PRINT" O - BOMB    o - LAND MINE"
337 PRINT" U - MAGNET  u - MAGNETIC FIELD":PRINT
338 PRINT" E - EXIT":PRINT
339 PRINT" W=UP,Z=DOWN,A=LEFT AND S=RIGHT"
340 PRINT@480,"      PRESS SPACE TO BEGIN";
345 I$=INKEY$:IFI$<>" "THEN345
347 RETURN
350 GOSUB410:PL=PL+1:IFPL>NM THEN370
360 E=1:RETURN
370 PRINT@480,;:FORA=75TO150:SOUNDA,1:PRINT"YOU WIN! WELL DONE R-29!":NEXT:SOUND200,2:SOUND210,2:SOUND200,1:E=2:GOTO320
400 SOUND200,1:RETURN
405 POKE16384+32*Y1+X1,96:RETURN
410 FORA=1TO4:PRINT@Y*32+X,"*";:SOUND1,1:POKE16384+Y*32+X,42:SOUND20,1:NEXT:SOUND1,1:GOSUB405:RETURN
420 FORZ=1TO5:PRINT@Y*32+X,"*";:QQ=PEEK(9)AND128:POKEF,0+QQ:POKE16384+Y*32+X,42:NEXTZ:GOSUB405:RETURN
430 FORA=1TO5:SOUND100+A*10,1:NEXT:GOSUB405:FORA=5TO1STEP-1:SOUNDA*10,1:NEXT
450 IFK=83THENX=X+1
451 IFK=65THENX=X-1
452 IFK=90THENY=Y+1
453 IFK=87THENY=Y-1
455 RETURN
499 REM PUZZLE 1
500 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯"
501 PRINT"¯ ¯ oOO o¯ oo¯"
502 PRINT"¯ o ¯ ¯¯OoO¯ ¯"
503 PRINT"¯ ¯ ¯ ¯ o¯¯¯ ¯"
504 PRINT"¯O¯  o¯o¯¯¯  ¯"
505 PRINT"¯ ¯¯¯ ¯  o  ¯¯"
506 PRINT"¯oOO¯  O¯¯ ooE"
507 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯":RETURN
519 REM PUZZLE 2
520 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯"
521 PRINT"¯ ¯¯¯¯¯¯¯¯¯¯"
522 PRINT"¯Oo o "CHR$(96)"  U¯¯"
523 PRINT"¯ ¯ ¯¯¯u¯u¯¯"
524 PRINT"¯o¯ Oo¯o  ¯¯"
525 PRINT"¯ ¯ ¯O¯ ¯¯¯¯"
526 PRINT"¯OOo¯@¯E¯   "
527 PRINT"¯¯¯¯¯¯¯¯¯   ":RETURN
539 REM PUZZLE 3
540 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯"
541 PRINT"¯ ¯   ¯¯ooo¯¯¯"
542 PRINT"¯ ¯   o¯ E ¯¯¯"
543 PRINT"¯ ¯ O ¯¯"CHR$(96)"¯"CHR$(96)"¯¯¯"
544 PRINT"¯ ¯ ¯uououo¯¯¯"
545 PRINT"¯U¯ ¯ouououo¯¯"
546 PRINT"¯U¯U¯uouou@u¯¯"
547 PRINT"¯UOO¯Ououou¯¯¯"
548 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯":RETURN
559 REM PUZZLE 4
560 PRINT"¯¯¯¯¯¯¯¯¯"
561 PRINT"¯ ¯¯¯   ¯"
562 PRINT"¯UOo@"CHR$(96)"¯u¯"
563 PRINT"¯u¯¯"CHR$(96)"¯¯o¯"
564 PRINT"¯Eo  o@@¯"
565 PRINT"¯¯¯¯¯¯¯¯¯":RETURN
579 REM PUZZLE 5
580 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯"
581 PRINT"¯   O  ¯ U ouuuUUUUU¯"
582 PRINT"¯¯¯¯¯¯ ¯o¯ ¯ ¯¯@UUUU¯"
583 PRINT"¯ u uo o ¯  u¯¯¯UUUU¯"
584 PRINT"¯u u ¯o¯ ¯ ¯¯¯¯¯¯¯¯¯¯"
585 PRINT"¯ u u¯ ¯U o o"CHR$(96)"O   ¯O¯"
586 PRINT"¯u@u ¯ ¯¯¯¯¯¯¯¯U¯ ¯ ¯"
587 PRINT"¯ uOu¯ o OO ¯ ¯¯¯   ¯"
588 PRINT"¯u u ¯ ¯¯¯¯   ¯¯¯o¯¯¯"
589 PRINT"¯¯¯¯¯¯ Oo¯Oo¯ ¯¯¯ ¯E¯"
590 PRINT"¯@¯¯u¯ ¯o¯o¯¯ ¯ o ¯ ¯"
591 PRINT"¯¯u¯     o    ¯O¯ ¯ ¯"
592 PRINT"¯O o ¯¯¯¯ ¯¯¯ ¯o u¯@¯"
593 PRINT"¯O¯¯ o O   oU ¯¯¯ "CHR$(96)" ¯"
594 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯":RETURN
599 REM PUZZLE 6
600 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯"
601 PRINT"¯  O UUU @ ouuuoUUUU¯"
602 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯"CHR$(96)"¯¯¯¯¯¯¯"
603 PRINT"¯                   ¯"
604 PRINT"¯"CHR$(96)""CHR$(96)""CHR$(96)""CHR$(96)"¯o¯u¯u¯¯¯¯¯¯¯¯¯¯"
605 PRINT"¯    ¯@¯ O  "CHR$(96)" u "CHR$(96)" ¯@¯"
606 PRINT"¯"CHR$(96)""CHR$(96)""CHR$(96)""CHR$(96)"¯o¯¯¯¯U¯@¯O¯U¯ ¯"
607 PRINT"¯    ¯ @ OOO¯¯¯¯¯U  ¯"
608 PRINT"¯oUUo¯¯¯¯¯¯¯¯U¯u¯o¯¯¯"
609 PRINT"¯¯¯¯¯¯¯O "CHR$(96)"UO¯U¯u¯ ¯o¯"
610 PRINT"¯Euuuu¯¯o¯o¯¯  u  ¯o¯"
611 PRINT"¯uuuuuo  ¯    ¯u¯ ¯o¯"
612 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯":RETURN
800 REM MAX SIZE 21 X 15
990 REM ADD NEW LEVELS HERE
991 REM THEN SEE LINES 10 & 50
999 REM MC-10 EDITS JIM GERRIE
