10 WIDTH 32:REM NOT NEEDED ON COCO 1&2
20 POKE 65497,0:REM REPLACE FOR 65495,0 ON COCO 1&2
30 GOSUB 3000:REM INTRO & INSTRUCTIONS, NO NEED TO TYPE LINES 3000 TO 3230
40 DIM M(27,14)
50 MR=45:ML=45:REM MR IS MINES FLAGGED, ML MINES REALLY LEFT
60 FOR X=1 TO 27:FOR Y=1 TO 14:M(X,Y)=0:NEXT Y,X:REM CLEAR MINEFIELD
70 BL=0:HS=5000
80 FOR A=1 TO 45
90 X=RND(25)+1:Y=RND(12)+1
100 IF M(X,Y)=0 THEN M(X,Y)=9 ELSE 90
110 NEXT A
120 CLS 0
130 PRINTCHR$(161);STRING$(25,CHR$(163));CHR$(162)
140 FOR A=1 TO 12
150 PRINTCHR$(165);STRING$(25,CHR$(160));CHR$(170)
160 NEXT A
170 PRINT CHR$(164);STRING$(25,CHR$(172));CHR$(168)
180 TIMER=0
200 REM MAIN LOOP
210 H=INT(JOYSTK(0)/2.56)+1
220 V=INT(JOYSTK(1)/5.3)+1
230 S=PEEK(1024+V*32+H)
240 POKE 1024+V*32+H,223
250 FOR LO=1 TO 10
260 BT=PEEK(65280)
270 IF BT=254 OR BT=126 GOTO 500
280 IF BT=251 OR BT=123 THEN GOSUB 900
290 A$=INKEY$:IF A$="" THEN 320
300 IF A$=" " GOTO 500
310 IF ASC(A$)=13 GOSUB 900
320 NEXT LO
330 POKE 1024+V*32+H,S
340 GOTO 210
500 REM IS THERE A MINE?
510 PRINT@219,"TIME";:PRINT@251,INT(TIMER/60);
520 X=H+1:Y=V+1:C=0
530 IF S=191 AND M(X,Y)<>9 THEN MR=MR+1:PRINT@28,"LEFT";:PRINT@60,MR;
540 IF MR=0 AND ML=0 THEN TT=INT(TIMER/60):GOTO 4000
550 IF M(X,Y)=9 THEN GOSUB 2000:GOTO 210
560 FOR A=X-1 TO X+1
570 FOR B=Y-1 TO Y+1
580 IF B<2 THEN NEXT B
590 IF B>13 THEN GOTO 630
600 IF A<2 THEN NEXT A
610 IF A>26 THEN GOTO 640
620 IF M(A,B)=9 THEN C=C+1
630 NEXT B,A
640 IF C=0 THEN C=31:REM TO GET A SPACE IN THE NEXT POKE
650 IF Y=1 OR Y=14 OR X=1 OR X=27 THEN 670
660 POKE 1024+(Y-1)*32+X-1,C+112
670 IF C=31 AND Z=0 THEN GOTO 750
680 IF Z=1 THEN RETURN
690 GOTO 210
750 REM THE SQUARE WAS CLEAR
760 REM SO, SHOW ALL AROUND
770 Z=1
780 FOR CX=H TO H+2: FOR CY=V TO V+2
790 C=0:X=CX:Y=CY:GOSUB 560
800 NEXT CY,CX
810 Z=0
820 GOTO 210
900 REM PUT A FLAG
910 IF S=191 THEN RETURN
920 PLAY"T32EFE"
930 POKE 1024+V*32+H,191:MR=MR-1
940 S=191
950 IF M(H+1,V+1)=9 THEN ML=ML-1
960 IF MR=0 AND ML=0 THEN TT=INT(TIMER/60):GOTO 4000
970 IF ML=0 THEN PLAY"T12CDEFCDEFCDEF"
980 IF MR=0 THEN PLAY"T8FCFCFC"
990 PRINT@28,"LEFT";:PRINT@60,MR;
1000 RETURN
2000 PLAY"O1V31T8BDV<T>ACV<T>GA"
2010 PRINT@448,"YOU BLASTED A MINE";
2020 PRINT@480,"KEEP GOING ANYWAY (Y/N)?";
2030 A$=INKEY$:IF A$="" THEN 2030
2040 IF A$="N" THEN 2110
2050 IF A$<>"Y" THEN 2030
2060 BL=BL+1
2070 PRINT@448,STRING$(28,CHR$(128));:PRINT@480,STRING$(28,CHR$(128));
2080 PRINT@443,"MINE";:PRINT@474,"BLASTS";:PRINT@507,BL;
2090 GOSUB 900
2100 RETURN
2110 FOR X=2 TO 26:FOR Y=2 TO 13
2120 IF M(X,Y)=9 THEN PRINT@32*(Y-1)+X-1,"M";
2130 NEXT Y,X
2135 PRINT@448,"PRESS ANY KEY TO CONTINUE";
2136 EXEC 44539
2137 CLS 6
2140 PRINT@32,"YOU WERE ABLE TO FIND";
2150 PRINT@64,"A TOTAL OF"45-ML"MINES";
2160 GOTO 4080
3000 REM INTRO
3010 CLS 0:SCREEN 0,2
3020 PRINT@10,"'MINE CAMP'";
3030 PRINT@32,"DO YOU WANT INSTRUCTIONS? (Y/N)";
3040 A$=INKEY$:IF A$="Y" THEN 3060 ELSE IF A$="" THEN 3040
3050 RETURN
3060 PRINT@32,STRING$(32,CHR$(128));
3070 PRINT@64,"LAND MINES ARE ONE OF THE";
3080 PRINT@96,"GREATEST DANGERS CIVILIANS FACE IN AREAS WERE THERE IS, OR WAS AN ARMED CONFLICT.";
3090 PRINT@192,"BEFORE BEING DEPLOYED TO HELP UNAND RELIEF CONVOYS, YOU MUST";
3100 PRINT@256,"COMPLETE YOUR BASIC MINE-HUNTINGTECHNIQUES AT THE 'MINE CAMP'";
3110 PRINT@320,"USE THE RIGHT JOYSTICK TO MOVE";:PRINT@352,"THE MARKER, THE RED BUTTON OR";
3120 PRINT@384,"THE space KEY TO DIG FOR MINES,";
3130 PRINT @482,"* PRESS A KEY TO CONTINUE *";
3140 A$=INKEY$:IF A$="" THEN 3140
3150 CLS 0
3160 PRINT@0,"AND THE BLACK BUTTON OR THE";:PRINT@32,"enter KEY TO PUT A FLAG AND MARKA MINE.";
3170 PRINT@96,"EVERY TIME YOU DIG, A NUMBER";:PRINT@128,"WILL INDICATE YOU HOW MANY MINESARE THERE IN THE SURROUNDING";
3180 PRINT@192,"TERRAIN";
3190 PRINT@258,"'MINE CAMP' V. 1.0, NOV 2005";
3200 PRINT@293,"DO YOU WANT TO SEE THE";
3210 PRINT@323,"INSTRUCTIONS AGAIN? (Y/N)";
3220 A$=INKEY$:IF A$="Y" THEN CLS0:GOTO 3060 ELSE IF A$="" THEN 3220
3230 RETURN
4000 CLS 6
4010 PRINT@9,"CONGRATULATIONS";
4020 PRINT@39,"YOU FOUND THEM ALL";
4030 PRINT@64,"IT TOOK YOU ";TT;"SECONDS"
4040 PRINT@96,"";
4050 IF BL=0 THEN PRINT"AND YOU DIDN'T BLAST ANY MINES"; ELSE PRINT"AND YOU BLASTED";BL;"MINES";
4060 SC=TT+(BL*25):PRINT@128,"YOUR TOTAL SCORE IS"SC;
4070 IF SC<HS THEN 4110
4080 PRINT@192,"THE HIGH SCORE SO FAR IS"HS;
4090 PRINT"BY "HS$;
4100 GOTO 4140
4110 PRINT@197,"YOU HAVE THE HIGH SCORE... SO FAR"
4120 INPUT"WHAT'S YOUR NAME";HS$
4130 HS=SC
4140 PRINT@416,"DO YOU WANT TO PLAY AGAIN (Y/N)?";
4150 A$=INKEY$:IF A$="N" THEN PRINT:PRINT"BYE":END
4160 IF A$="Y" THEN 50
4170 GOTO 4150
