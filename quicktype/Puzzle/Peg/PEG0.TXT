1 CLEAR1000:DIM B(9,9),X(33),Y(33)
3 Q=0
4 CLS:RESTORE:FOR X=1 TO 9:FOR Y=1 TO 9:READ B(X,Y):NEXT Y:NEXT X
5 FOR I=1 TO 33:READ X(I),Y(I):NEXTI
6 GOSUB59
7 GOSUB700
9 K=1:FORU=0TO384STEP64:PRINT@U,RIGHT$(STR$(K),2);
10 IFU>64AND(U<288)THEN K=K+7:NEXTU:GOTO12
11 K=K+3:NEXTU
12 K=3:FORU=30TO414STEP64:PRINT@U,RIGHT$(STR$(K),2);:IFU>30AND(U<286)THENK=K+7:NEXTU:GOTO14
13 K=K+3:NEXTU
14 D=2
15 FORX=2TO8
16 FORY=2TO8
17 IF B(X,Y)=1 THEN PRINT@D,H$;:PRINT@D+32,L$;:GOTO19
18 IF B(X,Y)=0 THEN PRINT@D,TL$;:PRINT@D+32,TH$;:GOTO19
19 D=D+4:NEXTY:D=D+36:NEXTX:IF Q=1 THEN RETURN
20 PRINT@454,"WHAT IS YOUR MOVE";:INPUT M$
21 F=0:E=0:L=0:W=0
22 REM SEPARATE STRING
23 M1=VAL((LEFT$(M$,2))):M2=VAL((RIGHT$(M$,2)))
24 IF M1<1 OR (M1>33) OR (M2<1) OR (M2>33) THEN E=1:GOTO45
25 X1=X(M1):Y1=Y(M1):X2=X(M2):Y2=Y(M2)
26 REM CHECK FOR VALID MOVE
27 IF B(X1,Y1)=0 THEN 31
28 IF B(X2,Y2)=1 THEN31
29 IF ABS(X2-X1)=2 THEN IF Y1=Y2 THEN32
30 IF ABS(Y2-Y1)=2 THEN IF X1=X2 THEN32
31 E=1:GOTO45
32 IF X1=X2 AND (Y2>Y1) THEN F=1
33 IF X1=X2 AND (Y1>Y2) THEN F=2
34 IF Y1=Y2 AND (X2>X1) THEN F=3
35 IF Y1=Y2 AND (X1>X2) THEN F=4
36 IF F<1 OR F>4 THEN E=1:GOTO45
37 ONFGOTO100,110,120,130
41 B(X1,Y1)=0:B(X2,Y2)=1
42 REM CHECK FOR END OF GAME
43 FORI=2TO8:FORJ=2TO8:IFB(I,J)=1THENL=L+1:IFB(I-1,J)=1OR(B(I+1,J)=1)OR(B(I,J-1)=1)OR(B(I,J+1)=1)THENW=0:GOTO50
44 NEXTJ:NEXTI:W=1:GOTO51
45 PRINT@472,"WRONG!";:FOR I=1 TO 10:SOUND X(I),1:NEXTI:GOTO14
50 G=50:FOR I=1 TO 4:SOUND G,4:G=G+RND(30):NEXTI:FOR I=472 TO 478:PRINT@I,CHR$(143);:NEXTI:GOTO14
51 Q=1:GOSUB14:Q=0:ON L GOTO 52,53,54,55,56:GOTO57
52 PRINT@485,"YOU ARE A GENIUS!!!";:GOTO58
53 PRINT@485,"PRETTY DARN GOOD!!";:GOTO58
54 PRINT@481,"YOU QUALIFY FOR A SILVER STAR!!";:GOTO58
55 PRINT@483,"THAT'S A JOB WELL DONE!";:GOTO58
56 PRINT@480,"THAT'S BETTER THAN MOST PEOPLE!";:GOTO58
57 PRINT@480,"YOU SHOULD TRY IT ONE MORE TIME";:GOTO58
58 GOTO900
59 GOSUB800
62 PRINT@482,"PRESS <ENTER> TO CONTINUE";:INPUT AS$:CLS:RETURN
100 IFB(X1,Y2-1)=0THENE=1:GOTO45
105 B(X1,Y2-1)=0:GOTO41
110 IF B(X1,Y1-1)=0THENE=1:GOTO45
115 B(X1,Y1-1)=0:GOTO41
120 IF B(X2-1,Y1)=0THENE=1:GOTO45
125 B(X2-1,Y1)=0:GOTO41
130 IF B(X1-1,Y1)=0 THEN E=1:GOTO45
135 B(X1-1,Y1)=0:GOTO41
700 H$=CHR$(128)+CHR$(161)+CHR$(162)+CHR$(128)
710 L$=CHR$(128)+CHR$(164)+CHR$(168)+CHR$(128)
720 TL$=CHR$(128)+CHR$(129)+CHR$(130)+CHR$(128)
730 TH$=CHR$(128)+CHR$(132)+CHR$(136)+CHR$(128)
740 RETURN
800 CLS2:PRINT@32,"";
810 PRINT" SO YOU WANT TO TEST YOUR IQ??"
820 PRINT"WELL, HERE ARE THE RULES."
830 PRINT"(1) THE MOVES ARE MADE IN JUMPS-";
840 PRINT"    AS IN CHECKERS"
850 PRINT"(2) NO DIAGONAL MOVES ALLOWED"
860 PRINT"(3) THERE ARE 33 POSITIONS FROM"
870 PRINT"    01 TO 33. AN EXAMPLE MOVE IS";
880 PRINT"    05-17, OR 01-03"
885 PRINT"(4) MAKE ONE JUMP AT A TIME"
890 PRINT"(5) THE GOAL IS TO HAVE ONE PEG"
895 PRINT"    LEFT AT THE END":PRINT@423,"HAVE A GOOD TIME!!";:
899 RETURN
900 FORI=1TO2000:NEXT
910 CLS0:PRINT@228,"WANT TO PLAY AGAIN (Y/N)";:PRINT@485,"WHAT IS YOUR CHOICE";:INPUT AS$:IFAS$="Y"THEN4
920 END
1000 DATA -1,-1,-1,-1,-1,-1,-1,-1,-1 
1010 DATA -1,-1,-1,1,1,1,-1,-1,-1,-1
1020 DATA -1,-1,1,1,1,-1,-1,-1,-1,1
1030 DATA 1,1,1,1,1,1,-1,-1,1,1,1,0
1040 DATA 1,1,1,-1,-1,1,1,1,1,1,1,1
1050 DATA -1,-1,-1,-1,1,1,1,-1,-1,-1
1055 DATA -1,-1,-1,1,1,1,-1,-1,-1
1060 DATA -1,-1,-1,-1,-1,-1,-1,-1,-1
1070 DATA 2,4,2,5,2,6,3,4,3,5,3,6,4,2
1080 DATA 4,3,4,4,4,5,4,6,4,7,4,8,5,2
1090 DATA 5,3,5,4,5,5,5,6,5,7,5,8,6,2
1100 DATA 6,3,6,4,6,5,6,6,6,7,6,8,7,4
1110 DATA 7,5,7,6,8,4,8,5,8,6
