10 CLS1:K=1:INPUT"NAME";P$
20 K=1
21 CLS1:PRINT:PRINTTAB(11);"COLORMIND":PRINT
30 PRINT"THE COMPUTER CHOOSES FOUR":PRINT"COLORS.":PRINT"YOU MUST GUESS THEM IN 10":PRINT"TRIES OR LESS."
40 PRINT:PRINT"THE COLORS ARE:"
50 PRINT" (B)LUE   ";CHR$(175);" (C)YAN   ";CHR$(223)
60 PRINT" (Y)ELLOW ";CHR$(159);" (P)URPLE ";CHR$(239)
70 PRINT" (O)RANGE ";CHR$(255);" (R)ED    ";CHR$(191)
80 PRINT:PRINT"HIT <SPACE> TO CONTINUE"
90 A$=INKEY$:IFA$=" "THENCLS1:GOTO110
100 GOTO90
110 PRINT:PRINT"THE COMPUTER WILL GIVE YOU":PRINT"HINTS ABOUT YOUR GUESSES."
120 PRINT" A BLACK BLOB MEANS:":PRINT" A GOOD COLOR IN THE RIGHT":PRINT" PLACE"
130 PRINT" A WHITE BLOB MEANS:":PRINT" A GOOD COLOR BUT NOT THE":PRINT" RIGHT PLACE":PRINT
140 PRINT"HIT <SPACE> TO CONTINUE"
150 A$=INKEY$:I=RND(6):IFA$=" "THEN170
160 GOTO150
170 FORI=1TO4:B(I)=RND(6):NEXT:CLS1:GOSUB700
171 REM GOSUB 770
172 PRINT " COLOR CODES: B";CHR$(175);" Y";CHR$(159);" O";CHR$(255);" C";CHR$(223);" P";CHR$(239);" R";CHR$(191);
180 I=(K+1)*32:PRINT@I," ENTER YOUR GUESS";:INPUTA$
190 AA$=LEFT$(A$,1):GOSUB640
200 A(1)=AA:AA$=MID$(A$,2,1):GOSUB640
210 A(2)=AA:AA$=MID$(A$,3,1):GOSUB640
220 A(3)=AA:AA$=RIGHT$(A$,1):GOSUB640
230 A(4)=AA:N=0:B=0:FORW=1TO4:D(W)=0:NEXT
240 FORW=1TO4:C(W)=0:NEXT:K=K+1
250 I=K*32:PRINT@I,"TRY#";K-1;:IFK<=10THENPRINT" ";
260 PRINT" ";:FORI=1TO4:PRINTCHR$(A(I));:PRINT" ";:NEXT
270 FORI=1TO4:IFA(I)=B(I)THENN=N+1
280 IFA(I)=B(I)THENC(I)=1
290 IFA(I)=B(I)THEND(I)=1
300 NEXTI
310 FORI=1TO4:FORJ=1TO4
320 IFD(I)=1THEN360
330 IFI=JTHEN350
340 IFB(I)=A(J)THEN380
350 NEXTJ
360 NEXTI
370 GOTO400
380 IFC(J)=1THEN350
390 C(J)=1:D(I)=1:B=B+1:GOTO360
400 PRINT" ";:IFN=0THEN420
410 FORI=1TON:PRINTCHR$(208);" ";:NEXT
420 IFB=0THEN460
430 IFB=1THENPRINTCHR$(207);:GOTO460
440 FORI=1TO(B-1):PRINTCHR$(207);" ";:NEXT
450 PRINTCHR$(207);
460 PRINT""
470 IFN=4THEN530
480 REM IFK=10THEN510
490 IFK>=11THENGOSUB780:GOTO570
500 GOTO180
510 REM PRINT" THAT WAS YOUR TENTH TRY. HAVE ANOTHER TRY:"
520 REM GOTO180
530 PRINT" WINNER!":PRINT" ANOTHER GAME <Y/N> ";
540 INPUTA$:IFA$="Y"THEN20
550 CLS1
560 GOTO620
570 CLS1:PRINT:PRINT" YOU DIDN'T GET IT!":PRINT:PRINT:PRINT" YOU NEED TO THINK!":PRINT:PRINT
580 PRINT" THE SOLUTION WAS:"
590 PRINT:PRINT" ";:GOSUB 770
600 PRINT:PRINT:PRINT:PRINT" PLAY AGAIN <Y/N>";:INPUTA$:IFA$="Y"THEN20
610 GOTO550
620 PRINT@225,"SO LONG ";P$
630 PRINT:END
640 IFAA$="Y"THENAA=159:RETURN
650 IFAA$="B"THENAA=175:RETURN
660 IFAA$="R"THENAA=191:RETURN
670 IFAA$="C"THENAA=223:RETURN
680 IFAA$="P"THENAA=239:RETURN
690 IFAA$="O"THENAA=255:RETURN
700 FORI=1TO4:FORJ=1TO6
710 IFB(I)=JTHENB(I)=143+J*16
720 IFB(I)>100ANDJ=>4THENB(I)=B(I)+16
730 IFB(I)>100THEN750
740 NEXTJ
750 NEXTI
760 RETURN
770 FORI=1TO4:PRINTCHR$(B(I));:PRINT" ";:NEXTI:RETURN
780 FORI=1TO2000:NEXTI:RETURN
