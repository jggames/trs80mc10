1100 CLEAR1000:X=RND(-TIMER):GOTO1300
1200 CLS:A$="":IZ=0:IP=1:IN=-1:X4=0:X2=0:X=39:Y=15:B=256:A=255:X1=1:Y1=0:F3=0:F=0:F0=-3:F4=0:IS=0:X2=0:Q=0:I0=0
1205 IB=-1:P=0:P1=.9:IT=1200:IQ=0:F7=16:F8=39:FZ=32:MC=1024:I8=0:FV=128:F9=32:RETURN
1300 GOSUB1200:DIMI(IT):PRINT@197,"C O L O R  H U S T L E"
1305 PRINT@354,"DO YOU WANT DIRECTIONS <Y/N>?";
1306 A$=INKEY$:IFA$="N" THEN2700
1400 IFA$="Y" THEN1410
1401 GOTO1306
1405 CLS:PRINT:PRINTTAB(5)"C O L O R  H U S T L E":PRINT:RETURN
1410 GOSUB1405
1500 PRINT"THE OBJECT IS TO SCORE AS MANY"
1505 PRINT"POINTS AS POSSIBLE BY REACHING"
1600 PRINT"THE TARGET WITHOUT TOUCHING A"
1605 PRINT"LINE OR REVERSING UPON"
1700 PRINT"YOURSELF. YOUR RATING REFERS TO"
1705 PRINT"THE NUMBER OF TARGETS THAT YOU"
1800 PRINT"GOT MULTIPLIED BY YOUR SCORE,"
1805 PRINT"AND DIVIDED BY THE TIME."
1810 PRINT:PRINT"<N>EXT SCREEN"
1815 A$=INKEY$
1820 IFA$="N" THEN2000
1825 GOTO1815
2000 GOSUB1405
2005 PRINT:PRINT"COMMANDS ARE:"
2100 PRINTCHR$(87),"MOVE UP"
2200 PRINTCHR$(90),"MOVE DOWN"
2300 PRINTCHR$(65),"MOVE LEFT"
2400 PRINTCHR$(83),"MOVE RIGHT"
2500 PRINT"T"," TO S TOP PLAYING"
2600 PRINT:PRINT" TO START PRESS THE <ENTER> KEY"
2605 PRINT"<P>REVIOUS SCREEN"
2610 A$=INKEY$
2615 IFA$=CHR$(13) THEN2700
2620 IFA$="P" THEN1410
2625 GOTO2610
2700 GOSUB5700:REM DRAW SCREEN
2800 GOSUB3100:GOSUB3500:GOSUB5200:GOSUB10000:GOTO2800
2900 FORD=1 TO4:NEXTD:RETURN
3000 REM NEXT SBR. PROCESSES COMMANDS
3100 A$=INKEY$:IFA$="" THENRETURN
3105 IFA$="T" THENRUN
3110 Q=ASC(A$):IFQ=65 THENX1=IN:Y1=IZ:RETURN
3205 IFQ=83 THENX1=IP:Y1=IZ:RETURN
3210 IFQ=87 THENX1=IZ:Y1=IN:RETURN
3215 IFQ=90 THENX1=IZ:Y1=IP:RETURN
3300 RETURN
3400 REM NEXT SBR. RESETS POINTER AND MOVES DOT
3500 X=X+X1:Y=Y+Y1:F3=F3+IP:IFF3>IT THENF3=IZ:IQ=IQ+IT
3600 IFF0<IZ THENF0=F0+IP:GOTO3700
3605 F=F+IP:IFF>IT THENF=IZ
3700 U=I(F):RESET(U AND A,U/B):I(F3)=Y*B+X:IFPOINT(X,Y) THEN3900
3800 SET(X,Y,6):RETURN
3900 P=INT((Y/2))*32+INT(X/2):ON1-(P=F4 OR P=F4+1 OR P=F4+2) GOTO4500
4000 Q=RND(9):IS=IS+Q:F0=F0-Q*2:I8=I8+1:FORI=IP TO8:PRINT@F4,STR$(Q)" ";:GOSUB2900
4105 PRINT@F4,STRING$(3,143+16);:PRINT@F4,STRING$(3,143+48);:NEXT
4200 X2=INT(I8*IS*1000/(IQ+F3)):IFX2>X4 THENX4=X2
4300 PRINT@F4,STRING$(3,208);:F4=IP:IB=IN:PRINT@456,IQ+F3;:PRINT@472,IS;:PRINT@488,X;:PRINT@504,X4;
4305 GOTO3800
4400 REM YOU LOSE HERE
4500 GOSUB20000:CLS:PRINT@107,"GAME OVER":PRINT@161,"FINAL SCORE:";IS:PRINT@193,"CURRENT RATING:";X2
4505 PRINT@225,"HIGHEST RATING IN GAME:";X4
4600 FORI=IZ TOTI:I(I)=IZ:NEXT I
4700 IFIS>HI THENHI=IS
4800 IFX4>X5 THENX5=X4
4810 PRINT@9*32," HIGHEST SCORE SO FAR:";HI
4820 PRINT@10*32," HIGHEST RATING SO FAR:";X5
4900 PRINT@480,"   HIT <ENTER> FOR NEW GAME";
4905 A$=INKEY$
4910 IFA$=CHR$(13) THEN5000
4915 GOTO4905
5000 GOSUB1200:GOSUB5700:RETURN
5100 REM THIS SUBR PUTS BOXES ON SCREEN
5200 IFIB>IZ THENIB=IB-IP:RETURN
5205 IFIB=IZ THENPRINT@F4,STRING$(3,208);:IB=IN
5300 IFRND(IZ)<P1 THENRETURN
5305 C=0
5350 C=C+IP:IFC>10 THENRETURN
5400 T1=RND(12):T2=RND(28):F4=T1*32+T2
5405 FORI=MC+F4 TOMC+F4+2:U=PEEK(I):IFU<>208 THENI=MC+511:NEXT:GOTO5350
5410 NEXT:PRINT@F4,STRING$(3,191);:IB=RND(65)+25:RETURN
5600 REM FOLLOWING SUBR. DRAWS SCREEN
5700 CLS:FORI=0 TO13:PRINT@I*32,STRING$(32,208);:NEXT:FORI=1 TO26:SET(1,I,3):SET(62,I,3):NEXT
5800 FORI=1 TO62:SET(I,1,3):SET(I,26,3):NEXT
6400 PRINT@14*32,"TIME";:PRINT@14*32+16,"SCORE";:PRINT@15*32,"RATING";:PRINT@15*32+16,"HIGHEST";:RETURN
10000 FORI=1 TO15:NEXTI:RETURN
10900 REM -- COLOR HUSTLE -- 
11000 REM BY PETER TREFONAS,    U.N.O. (12/6/78)
11005 REM MC-10 COLOR ADAPTATION BY ERIC UNDERHILL (7/28/2013)
11007 REM FROM A MODIFIED SOURCE BY P. J. HYDE (10/10/79)
11008 REM SOME ADDITIONAL EDITS BY JIM GERRIE (8/12/2013)
11009 REM EXPLOSION ANIMATION BY ROBERT SIEG (1/8/2014)
20000 READA:ONA+5 GOTO20040,20030,20000,20010:PS=A+SC:READA:X=MC+P+PS-109:ON1-(X>MC AND X<MC+448) GOTO20000,20005
20005 POKEX,A:GOTO20000
20010 SC=0:GOTO20000
20030 GOSUB20100:GOTO20000
20040 RES TORE:RETURN
20100 FORZ=1 TO100:NEXTZ:RETURN
30000 DATA-1,109,177,142,184,110,162,141,164,109,184,142,177,76,177,110,166,141,166,175,184
30050 DATA76,168,79,162,110,178,141,180,172,164,175,161,79,180,172,162,79,164,107,164,110,162
30090 DATA144,168,111,180,140,178,79,128,107,128,110,128,172,128,76,128,142,128,111,128,141,128
30170 DATA109,128,175,128,140,128,144,128,-3,-3,-3,-3,-3,-3,-3,-4
