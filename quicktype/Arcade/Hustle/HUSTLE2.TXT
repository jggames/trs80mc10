900 REM     -- C O L O R   H U S T L E --
1000 REM WRITTEN BY PETER TREFONAS, U.N.O. (12/6/78)
1005 REM MC-10 COLOR ADAPTATION BY ERIC UNDERHILL (7/28/2013)
1007 REM FROM A MODIFIED SOURCE BY P. J. HYDE (10/10/79)
1100 CLEAR1000:GOTO1300
1200 CLS:A$="":IZ=0:IP=1:IN=-1:X4=0:X2=0:X=39:Y=15:B=256:A=255:X1=1:Y1=0:F3=0:F=0:F0=-3:F4=0:IS=0:X2=0:Q=0:I0=0
1205 IB=-1:P=0:P1=.9:IT=1200:IQ=0:F7=16:F8=26:FZ=32 :FY=16384:FX=16383:FW=16385:I8=0:FV=128:F9=32:RETURN
1300 GOSUB1200:DIMI(IT):PRINT@196,"C O L O R  H U S T L E"
1305 PRINT@ 356,"DO YOU WANT DIRECTIONS";:INPUTA$:IFLEFT$(A$,1)<>"Y"THEN GOTO2700
1400 GOTO 1410
1405 CLS:PRINT:PRINTTAB(5);"C O L O R  H U S T L E":PRINT:RETURN
1410 GOSUB 1405
1500 PRINT"THE OBJECT IS TO SCORE AS MANY"
1505 PRINT"POINTS AS POSSIBLE BY REACHING"
1600 PRINT"THE TARGET WITHOUT TOUCHING A"
1605 PRINT"LINE OR REVERSING UPON"
1700 PRINT"YOURSELF. YOUR RATING REFERS TO"
1705 PRINT"THE NUMBER OF TARGETS THAT YOU"
1800 PRINT"GOT MULTIPLIED BY YOUR SCORE,"
1805 PRINT"AND DIVIDED BY THE TIME."
1810 PRINT:PRINT"<N>EXT SCREEN"
1815 A$=INKEY$
1820 IF A$="N" THEN GOTO 2000
1825 GOTO 1815
2000 GOSUB 1405
2005 PRINT:PRINT"COMMANDS ARE:"
2100 PRINTCHR$(87),"MOVE UP"
2200 PRINTCHR$(90),"MOVE DOWN"
2300 PRINTCHR$(65),"MOVE LEFT"
2400 PRINTCHR$(83),"MOVE RIGHT"
2500 PRINT"T","TO STOP PLAYING"
2600 PRINT:PRINT"TO START PRESS THE ENTER KEY"
2605 PRINT"<P>REVIOUS SCREEN"
2610 A$=INKEY$
2615 IF A$=CHR$(13) THEN GOTO 2700
2620 IF A$="P" THEN GOTO 1410
2625 GOTO 2610
2700 GOSUB 5700:REM DRAW SCREEN
2800 GOSUB 3100:GOSUB 3500:GOSUB 5200:GOSUB 10000:GOTO 2800
2900 FOR D=1 TO 4:NEXT D:RETURN
3000 REM NEXT SBR. PROCESSES COMMANDS
3100 A$=INKEY$:IF A$="" THEN RETURN
3105 IF A$="T" THEN RUN
3110 Q=ASC(A$)
3200 IF Q=65 THEN X1=IN:Y1=IZ
3205 IF Q=83 THEN X1=IP:Y1=IZ
3210 IF Q=87 THEN X1=IZ:Y1=IN
3215 IF Q=90 THEN X1=IZ:Y1=IP
3300 RETURN
3400 REM NEXT SBR. RESETS POINTER AND MOVES DOT
3500 X=X+X1:Y=Y+Y1:F3=F3+IP:IF F3 > IT THEN F3=IZ:IQ=IQ+IT
3600 IF F0 < IZ THEN F0=F0+IP:GOTO 3700
3605 F=F+IP:IF F > IT THEN F=IZ
3700 U=I(F):RESET(U AND A,U/B):I(F3)=Y*B+X:IF POINT(X,Y) THEN GOTO 3900
3800 SET(X,Y,6):RETURN
3900 P=INT((Y/2))*32+INT(X/2):IF P-1=F4 OR P=F4 OR P+1=F4 THEN GOTO 4000
3905 GOTO 4500
4000 Q=RND(9):IS=IS+Q:F0=F0-Q*2:I8=I8+1
4100 FOR I=IP TO 8:PRINT@F4-IP,STR$(Q);" ";:GOSUB 2900
4105 PRINT@F4-IP,CHR$(143+16)+CHR$(143+16)+CHR$(143+16);:PRINT@F4-IP,CHR$(143+48)+CHR$(143+48)+CHR$(143+48);:NEXT I
4200 X2=INT(I8*IS*1000/(IQ+F3)):IF X2 > X4 THEN X4=X2
4300 PRINT@F4-IP,CHR$(128)+CHR$(128)+CHR$(128);:F4=IP:IB=IN:PRINT @32,IQ+F3;:PRINT @128,IS;:PRINT@224,X;:PRINT@480,X4;
4305 GOTO 3800
4400 REM YOU LOSE HERE
4500 CLS:PRINT @106," GAME OVER   ":PRINT @160,"FINAL SCORE:";IS:PRINT @192," CURRENT RATING:";X2
4505 PRINT @224," HIGHEST RATING IN GAME:";X4
4600 FOR I=IZ TO TI:I(I)=IZ:NEXT I
4700 IF IS > HI THEN HI=IS
4800 IF X4 > X5 THEN X5=X4
4900 PRINT @288,"   HIT ENTER FOR NEW GAME";
4905 A$=INKEY$
4910 IF A$=CHR$(13) THEN GOTO 5000
4915 GOTO 4905
5000 GOSUB 1200:GOSUB 5700:RETURN
5100 REM THIS SUBR PUTS BOXES ON SCREEN
5200 IF IB > IZ THEN IB=IB-IP:RETURN
5205 IF IB=IZ THEN PRINT@F4-IP,CHR$(128)+CHR$(128)+CHR$(128);:IB=IN
5300 IF RND(IZ) < P1 THEN RETURN
5305 C=0
5350 C=C+IP:IF C > 10 THEN RETURN
5400 T1=RND(F7):T2=RND(FZ):F4=T1*F9+F8+T2:IF F4>479 THEN GOTO 5400
5405 FOR I=FX+F4 TO FW+F4:U=PEEK(I):IF U<>FV THEN I=F4+FW+IP:NEXT I:GOTO 5350
5410 NEXT I
5500 PRINT @F4-1,CHR$(143+48);:PRINT @F4,CHR$(143+48);:PRINT @F4+1,CHR$(143+48);:IB=RND(65)+25:RETURN
5505 F4=1:RETURN
5600 REM FOLOWING SUBR. DRAWS SCREEN
5700 CLS 0:FOR I=0 TO 29:SET(12,I,3):SET(63,I,3):NEXT I
5800 FOR I=13 TO 62:SET(I,0,3):SET(I,29,3):NEXT I
6400 PRINT @1,"TIME";:PRINT @96,"SCORE";:PRINT @192,"RATING";
6405 PRINT @288,"BEST";:PRINT @320,"SCORE";:PRINT @417,"HIGH";:PRINT @448,"RATING";:PRINT @491,"BEST RATING";
6410 PRINT @352,HI;:PRINT @503,X5;:RETURN
6415 PRINT @64,CHR$(144+48)+CHR$(144+48)+CHR$(144+48)+CHR$(144+48);
6420 PRINT @160,CHR$(144+48)+CHR$(144+48)+CHR$(144+48)+CHR$(144+48);
6425 PRINT @256,CHR$(144+48)+CHR$(144+48)+CHR$(144+48)+CHR$(144+48);
6430 PRINT @357,CHR$(144+48);:PRINT @384,CHR$(144+48)+CHR$(144+48)+CHR$(144+48)+CHR$(144+48);:RETURN 
10000 FOR I=1 TO 15:NEXT I:RETURN
