0 CLS:DIMK(255),MC:MC=15360:K(65)=1:K(87)=2:K(83)=3:K(90)=4:GOTO100
1 H1=32:V=32:ONK(PEEK(17023)ANDPEEK(2))GOTO2,3,4,5:RETURN
2 H1=0:RETURN
3 V=0:RETURN
4 H1=63:RETURN
5 V=63:RETURN
100 CLS
110 PRINT@232, "b e e   t r a p"
130 SOUND 1,30:FOR I=1 TO 500
140 NEXTI
150 CLS
160 PRINT:PRINT"   YOU ARE A GUIDE BEE COMMIS-"
170 PRINT"SIONED BY THE QUEEN BEE TO TAKE"
180 PRINT"LOST BEES SAFELY TO THE FLOWER"
190 PRINT"BED. YOU WILL GET CREDIT FOR"
200 PRINT"ONLY ONE BEE AT A TIME. BEWARE"
210 PRINT"THE VICIOUS VENUS BEE TRAPS"
220 PRINT"WHICH BLOOM PROFUSELY ACROSS"
230 PRINT"THE FIELD."
240 PRINT@362,"GOOD LUCK !!!"
250 FOR I=1 TO 350:NEXT I
260 PRINT@454,"PRESS enter TO BEGIN"
270 GOSUB 600
280 CLS:E=0
290 K1=1104:P=1024:SC=0:S=0:D1=0:D2=0:R=0:H=240:K=1503+RND(32)
300 REM PRINT FLOWERS
310 FOR I=1 TO 32:C=128+16*RND(7)+6:PRINT CHR$(C);:NEXT I
320 FOR I=1 TO 32:PRINT"^";:NEXT I
330 E=E+1:POKEMC+K1+D1,96:REM ERASE GUIDE BEE
340 GOSUB1
350 IFH1=OHANDV=OVTHEN370
360 SOUND1,1
370 IFH1=0THEND1=D1-1:GOTO410
380 IFH1=63THEND1=D1+1:GOTO410
390 IFV=0THEND1=D1-32:GOTO410
400 IFV=63THEND1=D1+32
410 OH=H1:OV=V:IFK1+D1>1535THEND1=D1-32:REM PREVENT SCREEN EXIT
420 IFK1+D1<1088ANDS>0THEN680:REM ENTER FLOWERS WITH BEE
430 IFK1+D1<1088THEND1=0:SC=SC-1:SOUND50,3:REM ENTER W/O BEE
440 IFD2<70ANDX<2THENXX=RND(304)+64:POKEMC+K1+XX,126:REM RANDOM TRAP PLACEMENT
450 IFPEEK(MC+K1+D1)=64THEND2=0:S=S+1:H=H-10:E=-30:REM CATCH LOST BEE
460 IFPEEK(MC+K1+D1)=126THEN760:REM GUIDE BEE IS ZAPPED
470 POKEMC+K1+D1,99:REM PRINT GUIDE BEE
480 IFH<130THENH=240
490 POKEMC+K-D2,96:REM ERASE LOST BEE
500 REM LOST BEE MOVEMENT
510 X=RND(H/5-23)
520 IFX<3ANDE>5THEND2=D2+32
530 IFX=3ORX=4THEND2=D2+1
540 IFX=5THEND2=D2-1
550 IFK-D2<1088THEND2=0:K=RND(32)+1503:REM BEE FINDS FLOWERS W/OUT GUIDE
560 IFPEEK(MC+K-D2)=99ANDD2>90THEND2=0:S=S+1:H=H-10:E=-35:REM LOST BEE FINDS GUIDE BEE
570 IFPEEK(MC+K-D2)=126THEN630:REM LOST BEE IS ZAPPED
580 IFE>5ANDS=0THENPOKEMC+K-D2,64:REM PRINT LOST BEE
590 GOTO 330
600 REM WAIT FOR ENTER KEY
610 I$=INKEY$:I=RND(0):IFI$<>CHR$(13)THEN610
620 RETURN
630 REM LOST BEE ZAPPED
640 D2=0:E=-5
650 R=R+1:IFR=50THEN760
660 SOUND 50,5:K=1503+RND(32)
670 GOTO 330
680 REM GUIDE SUCCESSFULLY CARRIES LOST BEE INTO FLOWER BED
690 SC=SC+1:E=-2
700 FOR I=130 TO 190 STEP 5:SOUND I,1: NEXT I
710 PRINT@72, "credit ";SC;
720 IF SC=1 THEN PRINT" bee":GOTO 740
730 PRINT" bees"
740 FOR I=1 TO 500:NEXTI:PRINT@70,"                        "
750 S=O:D1=O:D2=0:K=1503+RND(32):GOTO330
760 FOR I=190TO110STEP-5:SOUNDI,1:NEXT I
770 PRINT@68," final credit";SC;
780 IF SC=1 THEN PRINT" bee":GOTO 810
790 PRINT" bees"
800 FOR I=1 TO 250:NEXT I
810 PRINT@101,"PLAY AGAIN (y OR n)";
820 INPUT A$:IF A$="Y" THEN 280
830 CLS:END
