10 DIM MC,M1:MC=16384
15 CLS3:GOSUB1100:PRINT@234-128,"CITY BOMBER";:AA$="HIGH:"+STR$(HS):PRINT@230+32-6+((32-LEN(AA$))/2),AA$;
20 PRINT@420-32,"   SELECT SKILL LEVEL   ";
30 PRINT@420,"(1-HARD 2-MEDIUM 3-EASY)";
40 SKILL=VAL(INKEY$):IF SKILL<1 OR SKILL >3 THEN 40
50 GOSUB 1000
100 FOR LEVEL=0 TO 14:FOR TRY=1 TO SKILL
110 FOR CLM=0 TO 31
120 PPOS=LEVEL*32+CLM
130 IFPEEK(MC+PPOS)<>175THENCLM=31:NEXT CLM:TRY=SKILL:NEXT TRY:LEVEL=14:NEXT LEVEL:GOTO500
140 PRINT@PPOS,A1$;:IF CLM>0 THEN PRINT@PPOS-1,A2$;:IF CLM>1 THEN PRINT@PPOS-2,A3$;:IF CLM>2 THEN PRINT@PPOS-3,CHR$(175);
150 IF BPOS<>0 THEN PRINT@BPOS,CHR$(175);:BPOS=BPOS+32:IF BPOS>479 THEN BPOS=0
160 FOR DLY=1TO30:NEXT:IF BPOS=0 AND LEVEL<15 AND INKEY$<>"" THEN BPOS=PPOS+32:SH=SH+1
170 IF BPOS<>0 THEN PRINT@BPOS,"*";
180 IF LEVEL=14 THEN FOR W=1 TO CLM*8:NEXTW
190 NEXT CLM
200 PRINT@PPOS-2,"¯¯¯";
210 IF BPOS=0 THEN 240
211 PRINT@BPOS,CHR$(175);
220 IF BPOS<448 THEN BPOS=BPOS+32:PRINT@BPOS,"*";:GOTO230
221 BPOS=0
230 FOR DLY=1TO25:NEXT
240 FORW=16832TO16863:IFPEEK(W)<>175THENW=16863:NEXT:GOTO250
242 NEXT:SC=SC+((4-SKILL)*10):AA$="SCORE:"+STR$(SC):PRINT@0+((32-LEN(AA$))/2),AA$;:TRY=SKILL+1
250 IFBPOS=0THENFORDLY=1TO1000:NEXT:GOTO260
251 PRINT@BPOS,CHR$(175);:IFBPOS>=448THEN260
252 BPOS=BPOS+32:PRINT@BPOS,"*";:FORDLY=1TO80:NEXT:GOTO251
260 IFLEVEL=14THENTRY=SKILL+1
270 NEXT TRY
280 NEXT LEVEL
290 BN=INT(1000/SH):SC=SC+BN:FORW=1TO5:AA$="+ ACCURACY BONUS:"+STR$(BN):PRINT@64+((32-LEN(AA$))/2),AA$;
295 SOUNDRND(20),1:NEXT::FORDLY=1TO2500:NEXT
300 FOR J=1 TO 5
310 CLS RND(4)
320 PRINT@235-32,"WELL DONE";:AA$="FINAL SCORE:"+STR$(SC):PRINT@230+32-6+((32-LEN(AA$))/2),AA$;
330 FOR W=1TO250:NEXT W
340 NEXT J:GOTO15
500 FOR J=1 TO 10
510 CLS RND(4)
520 PRINT@237,"CRASH!";
530 FOR W=1TO250:NEXT W
540 NEXT J:GOTO15
1000 CLS 3
1010 PRINT@480,"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
1020 FOR BDG=4 TO 31
1030 BCOL=16
1040 ROOF=RND(8) + 6
1050 FOR J=14 TO ROOF STEP-1
1060 PRINT@J*32 + BDG,CHR$(143+BCOL);
1070 NEXT J
1080 NEXT BDG
1090 A1$=CHR$(172):A2$=CHR$(168):A3$=CHR$(166):RETURN
1100 SH=0:IFSC>HSTHENHS=SC
1110 SC=0:RETURN
