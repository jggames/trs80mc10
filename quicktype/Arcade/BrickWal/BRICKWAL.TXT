1 CLS:POKE3,0:POKE16925,0:POKE16926,1:HS=RND(-(PEEK(9)*256+PEEK(10))):HS=0:P=0:OL=2
2 CLS0:K=RND(62):L=30:X=28:J=3:C=4:PK=1:PL=-1
3 PRINT@0,"THE WALL OF BRICKS"
5 PRINT@288,"COPYRIGHT EDICIEL & ROLLAND"
6 PRINT@288+64,"HIGH SCORE:";HS:GOTO50
10 IFHS<>0THEN30
20 PRINT@288+128,"INSTRUCTIONS? (Y/N)"
21 M$=INKEY$:IFM$="N"ORPEEK(3)=66THEN40
22 IFM$="Y"THEN24
23 GOTO21
24 PRINT@288,"TO DESTROY THE BRICKS YOU MUST  MAKE THE BALL REBOUND ON THE    WALL USING THE RACKET. PRESS THE";
25 PRINT"A AND D KEYS FOR LEFT AND RIGHT.":GOTO40
30 PRINT@288+128,"LAST SCORE:";P*LV:P=0
40 PRINT@480,"LEVEL? (1-3)                   ";:POKE16895,96
41 M$=INKEY$:LV=VAL(M$):IFPEEK(3)=66THENLV=OL
42 IFLV<1ORLV>3THEN41
43 OL=LV:FORA=17TO30:FORI=0TO63:RESET(I,A):NEXT:NEXT:GOTO130
50 FORD=2TO15STEP2:FORS=0TO63STEP2:SET(S,D,C):SET(S+1,D,C):SET(S,D+1,C):SET(S+1,D+1,C):C=C+1:IFC=9THENC=1
102 IFC=5THENC=6
110 NEXT:NEXT:GOTO10
130 PRINT@20,"SCORE:";STR$(P*LV):CC=200-30*LV:X=28
150 FORI=XTOX+5:SET(I,31,5):NEXTI:IFQ<>68THEN164
163 RESET(X-3,31):RESET(X-2,31):RESET(X-1,31):GOTO180
164 IFQ<>65THEN180
166 RESET(X+8,31):RESET(X+7,31):RESET(X+6,31)
180 FORZZ=1TOCC:NEXTZZ
190 IFPEEK(2)AND1ANDNOTPEEK(16946)THENQ=65:X=X-3:IFX<.THENX=.
191 IFPEEK(3)=70THENQ=65:X=X-3:IFX<.THENX=.
200 IFPEEK(2)AND1ANDNOTPEEK(16949)THENQ=68:X=X+3:IFX>58THENX=58
201 IFPEEK(3)=74THENQ=68:X=X+3:IFX>58THENX=58
233 K=K+PK:L=L+PL:SET(K,L,5):RESET(K-PK,L-PL):GOSUB3000:IFK<1ORK>62THENPK=-PK
242 IFL<3THENPL=-PL
245 IFL>29THEN900
300 GOTO150
700 P=P+10:PRINT@26,STR$(P*LV):SOUND180,2:IFP<>2240THENRETURN
720 PRINT@300,"you€win€€€€€€€€€€€€€"
730 GOTO2500
900 IFPL=-1THEN300
904 W=PL:IFK=63THEN910
906 IFPOINT(K+1,L+PL)=5THENPL=-PL
907 IFK=0THEN915
910 IFPOINT(K-1,L+PL)=5THENPL=-PL
915 IFPOINT(K,L+PL)=5THENPL=-PL
916 IFPL=WTHEN2000
920 GOTO300
1000 FORI=1TO10:SOUND200,1:SOUND70,1:NEXT:SOUND30,2:J=J-1:PRINT@0,"BALLS:";J;TAB(10)"LEVEL:";LV;
1005 K=RND(62):L=30:PK=1:PL=-1
1006 PRINT@20,"SCORE:";STR$(P*LV):IFJ=0THEN1010
1007 FORD=0TO63:RESET(D,31):NEXTD
1008 GOTO130
1010 PRINT@300,"you€lose€€€€€€€€€€€€"
1016 GOTO2500
2000 K=K+PK:L=L+PL
2010 RESET(K-PK,L-PL)
2020 REM FORI=XTOX+5:RESET(I,31):NEXTI:FORI=1TO100:NEXTI
2030 GOTO1000
2500 IFP*LV>HSTHENHS=P*LV
2510 PRINT@328,"PLAY AGAIN? (Y/N)"
2520 M$=INKEY$
2521 IFM$="Y"ORPEEK(3)=66THEN2
2530 IFM$="N"THENEND
2540 GOTO2520
3000 IFPL=1THEN5000
3019 IFINT((L+1)/2)=(L+1)/2THEN3500
3020 T=POINT(K,L+PL)
3030 IFT=0THEN8000
3035 IFT=-1THEN3500
3040 IFPK=1ANDK=63THEN3056
3045 IFPK=-1ANDK=0THEN3056
3050 Y=POINT(K+PK,L+PL):GOTO3060
3056 Y=T-1
3060 IFY=TTHENGOSUB8400
3070 IFY<>TTHENGOSUB8300
3080 PL=-PL:GOSUB700
3090 IFY=TTHENRETURN
3100 GOTO4000
3500 IFPK=1ANDK=63THENRETURN
3510 IFPK=-1ANDK=0THENRETURN
3520 T=POINT(K+PK,L)
3530 IFT=0THENRETURN
3540 IFL<>2THEN3550
3545 Y=T-1:GOTO3570
3550 Y=POINT(K+PK,L+PL)
3560 IFT=YTHENGOSUB8200
3570 IFT<>YTHENGOSUB8100
3580 PK=-PK:GOSUB700
3590 RETURN
4000 IFPK=1ANDK=63THENRETURN
4010 IFPK=-1ANDK=0THENRETURN
4020 T=POINT(K+PK,L)
4030 IFT=5THENRETURN
4035 IFT=0THEN5000
4050 Y=POINT(K+PK,L+PL)
4060 IFT=YTHENGOSUB8200
4070 IFY<>TTHENGOSUB8100
4080 PK=-PK:GOSUB700
4090 RETURN
5000 T=POINT(K,L+PL)
5010 IFT=0THEN5500
5012 IFT=5THENRETURN
5015 IFPK=1ANDK=63THEN5025
5017 IFPK=-1ANDK=0THEN5025
5020 Y=POINT(K+PK,L+PL)
5022 GOTO5030
5025 Y=T-1
5030 IFT=YTHENGOSUB8400
5040 IFT<>YTHENGOSUB8300
5050 PL=-PL:GOSUB700
5060 IFY=TTHENRETURN
5070 GOTO3500
5500 IFPK=1ANDK=63THENRETURN
5510 IFPK=-1ANDK=0THENRETURN
5520 Y=POINT(K+PK,L)
5530 IFY<>0THEN4000
5540 R=POINT(K+PK,L+PL)
5545 IFR=5THENRETURN
5549 IFR=0ORR=5THENRETURN
5550 GOSUB8600
5560 PL=-PL:GOSUB700
5570 RETURN
8000 IFPK=1ANDK=63THENRETURN
8010 IFPK=-1ANDK=0THENRETURN
8020 Y=POINT(K+PK,L)
8030 IFY<>0THEN3500
8040 R=POINT(K+PK,L+PL)
8049 IFR=0THENRETURN
8050 GOSUB8600
8060 PL=-PL:GOSUB700
8070 RETURN
8100 RESET(K+PK,L):RESET(K+PK,L-PL):RESET(K+2*PK,L):RESET(K+2*PK,L-PL):RETURN
8200 RESET(K+PK,L):RESET(K+PK,L+PL):RESET(K+2*PK,L+PL):RESET(K+2*PK,L):RETURN
8300 RESET(K,L+PL):RESET(K-PK,L+PL):RESET(K,L+2*PL):RESET(K-PK,L+2*PL):RETURN
8400 RESET(K+PK,L+PL):RESET(K,L+2*PL):RESET(K,L+PL):RESET(K+PK,L+2*PL):RETURN
8600 RESET(K+PK,L+PL):RESET(K+2*PK,L+2*PL):RESET(K+2*PK,L+PL):RESET(K+PK,L+2*PL):RETURN
