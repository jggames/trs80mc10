0 GOSUB305:CLEAR999:GOTO203
2 RETURN
5 G=Q:A=1:J=E(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO98,146,2,2,2:ONPOINT(J,K+2)GOTO98,146,2,2,2
8 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:X(T)=X(T)+1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
11 A=W:J=G(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO98,146,2,2,2:ONPOINT(J,K+2)GOTO98,146,2,2,2
14 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:X(T)=X(T)-1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
17 B=1:J=F(X(T)):K=E(Y(T)):ONPOINT(J,K)GOTO98,146,2,2,2:ONPOINT(J+2,K)GOTO98,146,2,2,2
20 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)+1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
23 B=W:J=F(X(T)):K=G(Y(T)):ONPOINT(J,K)GOTO98,146,2,2,2:ONPOINT(J+2,K)GOTO98,146,2,2,2
26 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)-1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
29 C=A:U=A*2:Z=B:V=B*2:R=X(T)*2+O(A+1):S=Y(T)*2+P(B+1):ONPOINT(R,S)GOTO95,104,95,95,95:SET(R,S,4):RETURN
32 RESET(R,S):ONPOINT(R+U,S+V)GOTO95,104,95,95,95:R=R+U:S=S+V:ONPOINT(R+C,S+Z)GOSUB2,104,2,2:SET(R,S,4):RETURN
35 RESET(L,M):ONPOINT(L+N,M+O)GOTO122,104,110,110,110,110,122:L=L+N:M=M+O:SET(L,M,7):RETURN
38 FORE=1TO50:FORT=1TO6:G=F:ONRND(R(T))GOTO50,50,50,50,53,53:A=0:B=0:IFX-X(T)>0THENGOSUB5
41 IFX-X(T)<0THENGOSUB11
44 IFY-Y(T)>0THENGOSUB17
47 IFY-Y(T)<0THENGOSUB23
48 ONRGOSUB29
50 IFR>1THENGOSUB32
51 IFL>0THENGOSUB35
52 ONK(PEEK(P)ANDPEEK(2))GOSUB71,74,77,80,83,86,89,92,134:NEXT:NEXT:GOTO155
53 A=0:B=0:ONRND(4)GOSUB5,11,17,23:GOTO50
56 J=F(X+H):K=F(Y+I):ONPOINT(J,K)GOTO113,59,113,137,143,113
59 ONPOINT(J+2,K)GOTO113,62,113,137,143,113
62 ONPOINT(J,K+2)GOTO113,65,113,137,143,113
65 ONPOINT(J+2,K+2)GOTO113,68,113,137,143,113
68 PRINT@A(X,Y),B$;:PRINT@B(X,Y),B$;:X=X+H:Y=Y+I:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:RETURN
71 H=1:I=0:D=3:GOTO56
74 H=1:I=W:D=4:GOTO56
77 H=1:I=1:D=5:GOTO56
80 H=W:I=0:D=6:GOTO56
83 H=W:I=W:D=7:GOTO56
86 H=W:I=1:D=8:GOTO56
89 H=0:I=1:D=9:GOTO56
92 H=0:I=W:D=9:GOTO56
95 R=1:S=1:C=0:Z=0:RETURN
98 PRINT@A(X(T),Y(T)),B$(11);:PRINT@B(X(T),Y(T)),B$(11);:SOUND1,1
101 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:R(T)=1:G=0:RETURN
104 RESET(R,S):SET(R+U,S+V,4):SOUND100,2:GOSUB95:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2THEN116
107 RETURN
110 L=0:M=0:N=0:O=0:RETURN
113 GOSUB68:PRINT@A(X,Y),B$(11);:PRINT@B(X,Y),B$(11);:SOUND200,4:GOTO119
116 FORT=1TO5:PRINT@A(X,Y),A$(10);:PRINT@B(X,Y),B$(10);:SOUND200,1:PRINT@A(X,Y),A$(9);:PRINT@B(X,Y),B$(9);:SOUND100,1:NEXT
119 PRINT@480,"OUCH!";:SOUND200,4:E=65000:T=6:G=0:LF=LF-1:HT=0:EV=2:RETURN
122 SET(L+N,M+O,7):FORM=1TO6:ONR(M)GOTO128:J=2*X(M):K=2*Y(M)
125 IFPOINT(J,K)=7ORPOINT(J+2,K)=7ORPOINT(J,K+2)=7ORPOINT(J+2,K+2)=7THENGOSUB131
128 NEXT:GOSUB110:RETURN
131 PRINT@A(X(M),Y(M)),B$(11);:PRINT@B(X(M),Y(M)),B$(11);:SOUND1,1:PRINT@A(X(M),Y(M)),B$;:PRINT@B(X(M),Y(M)),B$;:R(M)=1:SC=SC+100:RETURN
134 IFLTHENRETURN
135 N=H*2:O=I*2:L=X*2+O(H+1):M=Y*2+P(I+1):ONPOINT(L,M)GOTO122,104,110,110,110:SET(L,M,7):RETURN
137 GOSUB68:SET(R,S,4):SOUND100,2:GOSUB95:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2THEN119
140 RETURN
143 GOSUB68:PRINT@480,"YOU ESCAPED!";:SOUND100,3:SOUND50,3:SOUND1,3:SOUND1,10:FORT=1TO6:GOSUB185:NEXT:E=65000:T=6:EV=1:RETURN
146 PRINT@A(X,Y),B$(11);:PRINT@B(X,Y),B$(11);
149 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)+B:X(T)=X(T)+A:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);
152 SOUND200,4:GOTO119
155 IFE<65000THENFORT=1TO6:SOUND200,1:GOSUB101:NEXT:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:R(RND(6))=20:F=12:Q=12:GOTO38
158 IFEV=2THENIFLF>=0THENSOUND1,5:CLS0:GOTO233
161 IFEV=1ANDBN<>6THENPRINT@480,"CHICKEN! FIGHT LIKE A ROBOT!";:FORT=1TO10:SOUND101-(10*T),1:SOUND1,1:NEXT:GOTO167
164 IFEV=1THENPRINT@480,"LEVEL CLEARED.";:FORT=1TO10:SOUND100,1:SOUND200,1:NEXT:SC=SC+250
167 IFEV=1THENPRINT@480,"GET READY FOR NEXT LEVEL ...";:FORT=1TO10:SOUND10*T,1:NEXT:EN=NE:EX=EX(RND(3)):GOTO227
170 I$=INKEY$:RESET(2,0):PRINT@480,"YOU ARE DEAD. PLAY AGAIN (Y/N)";:PRINT@0,"SCORE:";SC;:PRINT@16,"HIGH:";HS;
173 I$=INKEY$:IFI$=""THEN173
176 IFI$="Y"THEN197
179 IFI$="N"THENEND
182 GOTO173
185 IFR(T)=1THENBN=BN+1
188 RETURN
191 FORQ=1TO6:IFDR(EN,Q,0)=J(Y,0)ANDDR(EN,Q,1)=I(X,0)THENQ=6:NEXT:GOTO236
194 NEXT:X(T)=I(X,0):Y(T)=J(Y,0):I(X,1)=1:J(Y,1)=1:NEXT:GOTO242
197 CLS0:LV=5:LF=2:M(1)=9:HT=0:EN=4:EX=RND(3):IFSC>HSTHENHS=SC
200 SC=0:GOTO230
203 DIMA$(13),B$(13),X(6),Y(6),R(6),O(2),P(2),K(255),A(31,14),B(31,14),E(64),F(64),G(64),H(64),I(12,1),J(6,1),DR(4,6,1)
206 DIMT,D,X,Y,A,B,P,C,E,F,H,I,G,R,L,S,M,W,J,K,N,O,Q,U,V,Z,B$,M(12),EX(3),LV,EV,SC,EN,EX,BN,SR,HS
209 DIMXX,YY,CX,CY,XC,YC,PC,RR,HT,I$,A$:B$=CHR$(128)+CHR$(128):LV=8:LF=3:GOSUB320:GOSUB299
212 GOSUB497:FORT=0TO13:FORY=0TO3:READA$:FORX=1TO4:I=VAL(MID$(A$,X,1)):IFI>0THENSET(X-1,Y,I)
215 NEXT:NEXT:A$(T)=CHR$(PEEK(MC))+CHR$(PEEK(MC+1)):B$(T)=CHR$(PEEK(MC+32))+CHR$(PEEK(MC+33))
218 GOSUB308:NEXT:GOSUB311:GOSUB362:GOSUB311:W=-1:O(0)=-1:O(1)=3:O(2)=5:P(0)=-1:P(1)=1:P(2)=5:I=0:GOSUB494
221 FORT=1TO12:READI(T,0):NEXT:FORT=1TO6:READJ(T,0):NEXT:FOREN=1TO4:FORT=1TO6:READDR(EN,T,0),DR(EN,T,1):NEXT:NEXT
224 EN=4:EX=RND(3)
227 FORT=1TO12:READM(T):NEXT
230 IFM(1)=9THENLV=LV+3:LF=LF+1:RESTORE:FORT=1TO(14*4):READA$:NEXT:FORT=1TO12+6+(4*12):READC:NEXT:GOSUB464:GOTO227
233 FORT=1TO6:R(T)=LV:NEXT:FORT=1TO12:I(T,1)=0:NEXT:FORT=1TO6:J(T,1)=0:NEXT:FORT=1TO6
236 X=RND(12):Y=RND(6):IFI(X,1)=0ANDJ(Y,1)=0THEN191
239 GOTO236
242 CLS0:FORX=1TO63:SET(X,1,3):SET(X,11,3):SET(X,21,3):SET(X,31,3):NEXT
245 FORY=1TO31:SET(1,Y,3):SET(17,Y,3):SET(33,Y,3):SET(49,Y,3):SET(63,Y,3):NEXT
248 IFLF>0THENFORT=1TOLF:SET(T*2,0,3):NEXT
251 IFHT>0THENSET(1,1,4)
254 GOSUB263:FORY=1TO30:SET(63,Y,3):NEXT:GOSUB284
257 Y=DR(EN,1,0):X=DR(EN,1,1):FORT=1TO6:G=RND(2):PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:NEXT
260 PRINT@A(X,Y),A$(3);:PRINT@B(X,Y),B$(3);:R=1:H=1:I=0:D=3:J=F(X+H):K=F(Y+I):F=1:Q=2:BN=0:GOSUB110:GOSUB95:GOTO38
263 C=1:FORY=1TO30STEP10:FORX=1TO62STEP16
266 T=M(C):C=C+1:ONTGOSUB269,275,278:NEXT:NEXT:RETURN
269 FORR=X+1TOX+15:IFR<64THENRESET(R,Y):NEXT:RETURN
272 NEXT:RETURN
275 FORS=Y+1TOY+9:RESET(X,S):NEXT:RETURN
278 GOSUB269:GOSUB275:IFPOINT(X,Y-1)=0ANDPOINT(X-1,Y)=0THENRESET(X,Y)
281 RETURN
284 ONEXGOSUB287,290,293,296:RETURN 
287 FORX=18TO31:SET(X,1,5):NEXT:NE=4:EX(1)=1:EX(2)=2:EX(3)=3:RETURN
290 FORY=12TO18:SET(1,Y,5):NEXT:NE=3:EX(1)=1:EX(2)=2:EX(3)=4:RETURN
293 FORY=12TO18:SET(63,Y,5):NEXT:NE=2:EX(1)=1:EX(2)=3:EX(3)=4:RETURN
296 FORX=34TO46:SET(X,31,5):NEXT:NE=1:EX(1)=2:EX(2)=3:EX(3)=4:RETURN
299 FORX=0TO31:FORY=0TO14:A(X,Y)=X+Y*32:B(X,Y)=X+Y*32+32:NEXT:NEXT
302 FORX=0TO64:E(X)=X*2+5:F(X)=X*2+1:G(X)=X*2-1:H(X)=X*2+1:NEXT:RETURN
305 CLS:PRINT"PLEASE WAIT...":RETURN
308 PRINT@0,B$;:PRINT@32,B$;:RETURN
311 PRINT@483,"PRESS ANY KEY TO CONTINUE";
314 I$=INKEY$:T=RND(1000):IFI$=""THEN314
317 RETURN
320 CLS0:PRINT@4,"THE ASTRO DATE IS 3200":A$="BERZERK":FORT=1TOLEN(A$):PRINT@95+(T*32)+RND(3),MID$(A$,T,1);:NEXT
323 PRINT@(32*1)+4,"AND YOU ARE THE LAST"
326 PRINT@(32*2)+4,"SURVIVOR OF A SMALL GROUP"
329 PRINT@(32*3)+4,"OF EARTH PEOPLE WHO CAME"
332 PRINT@(32*4)+4,"TO EXPLORE THE PLANET"
335 PRINT@(32*5)+4,"MAZEON. SOON AFTER LANDING"
338 PRINT@(32*6)+4,"YOU DISCOVERED THE PLANET"
341 PRINT@(32*7)+4,"IS A DARK UNINHABITABLE"
344 PRINT@(32*8)+4,"PLACE. BUT BY THEN IT WAS"
347 PRINT@(32*9)+4,"TOO LATE TO TURN BACK"
350 PRINT@(32*10)+4,"BECAUSE YOUR SPACECRAFT"
353 PRINT@(32*11)+4,"HAD BEEN DESTROYED BY THE"
356 PRINT@(32*12)+4,"AUTO-MAZEONS. NOW YOU ARE"
359 PRINT@(32*13)+4,"A PRISONER HERE.":PRINT@491,"BY JIM GERRIE";:RETURN
362 CLS0:PRINT"YOU ARE TRAPPED IN A MAZE"
365 PRINT"WHERE EVEN THE WALLS ARE"
368 PRINT"DEATH TO TOUCH. GRIM ROBOT"
371 PRINT"THUGS STALK YOU RELENTLESSLY..."
374 PRINT@160,"USE:";
377 PRINT@224,"Q W E";
380 PRINT@256,"A S D";CHR$(128);"OR";
383 PRINT@288,"Z X C";
386 PRINT@234,"T Y U";
389 PRINT@266," G H ";
392 PRINT@298,"V B N";
395 PRINT@244,"SPACE";
398 PRINT@276," TO  ";
401 PRINT@308," FIRE";
404 PRINT@352,"THE WHITE DOOR IS THE EXIT. YOU"
407 PRINT"GET AN EXTRA MAN FOR EVERY 10"
410 PRINT"LEVELS THAT YOU COMPLETE."
413 RETURN
416 XC=0:YC=RR:PC=3-2*RR 
419 GOSUB437
422 IFPC<0THENPC=PC+4*XC+6:GOTO428
425 PC=PC+4*(XC-YC)+10:YC=YC-1   
428 XC=XC+1:IFXC<YCTHEN419
431 IFXC=YCTHENGOSUB437 
434 RETURN
437 XX=CX+XC:YY=CY+YC:SET(XX,YY,6)
440 XX=CX-XC:YY=CY+YC:SET(XX,YY,6)
443 XX=CX+XC:YY=CY-YC:SET(XX,YY,6)
446 XX=CX-XC:YY=CY-YC:SET(XX,YY,6)
449 XX=CX+YC:YY=CY+XC:SET(XX,YY,6)
452 XX=CX-YC:YY=CY+XC:SET(XX,YY,6) 
455 XX=CX+YC:YY=CY-XC:SET(XX,YY,6)
458 XX=CX-YC:YY=CY-XC:SET(XX,YY,6)
461 RETURN
464 CLS0:IFSC=0THENGOSUB494:RETURN
467 IFSC<15000THENA$="THE HUMANOID MUST NOT ESCAPE!":SM=-1
470 IFSC>=15000ANDSC<30000THENA$="THE HUMANOID IS ABOUT TO ESCAPE":SM=0
473 IFSC>=25000THENA$="THE HUMANOID ESCAPED!":SM=-1
476 RR=5:CX=31:CY=15:GOSUB416:PRINT@239,A$(12);:IFSMTHENPRINT@271,B$(12);:GOTO482
479 PRINT@239+32,B$(13);
482 PRINT@480+INT((32-LEN(A$))/2),;:FORT=1TOLEN(A$):PRINTMID$(A$,T,1);:SOUND222,1:NEXT
485 FORT=1TO2000:NEXT
488 IFSC>=25000THENPRINT:PRINT"DEDICATED TO P,M,C & N":END
491 RETURN
494 FORT=3TO4:CLST:PRINT@233,"INTRUDER ALERT!";:SOUND50,2:SOUND5,2:SOUND1,2:SOUND1,2:SOUND1,2:NEXT:RETURN
497 P=17023:K(68)=1:K(69)=2:K(67)=3:K(65)=4:K(81)=5:K(90)=6:K(88)=7:K(87)=8:K(32)=9:K(83)=7
500 K(72)=1:K(85)=2:K(78)=3:K(71)=4:K(84)=5:K(86)=6:K(66)=7:K(89)=8
503 MC=16384:RETURN
506 DATA "...."
509 DATA "...."
512 DATA "...."
515 DATA "...."
518 DATA "1111"
521 DATA "1.11"
524 DATA "1111"
527 DATA "1..1"
530 DATA "1111"
533 DATA "11.1"
536 DATA "1111"
539 DATA "1..1"
542 DATA ".2.."
545 DATA "2222"
548 DATA ".2.."
551 DATA "2.2."
554 DATA ".2.2"
557 DATA "222."
560 DATA ".2.."
563 DATA "2.2."
566 DATA ".2.."
569 DATA "222."
572 DATA ".2.2"
575 DATA "2.2."
578 DATA "..2."
581 DATA "2222"
584 DATA "..2."
587 DATA ".2.2"
590 DATA "2.2."
593 DATA ".222"
596 DATA "..2."
599 DATA ".2.2"
602 DATA "..2."
605 DATA ".222"
608 DATA "2.2."
611 DATA ".2.2"
614 DATA "..2."
617 DATA ".222"
620 DATA "..2."
623 DATA ".2.2"
626 DATA "..8."
629 DATA ".888"
632 DATA "..8."
635 DATA ".8.8"
638 DATA "4.4."
641 DATA ".4.4"
644 DATA "4.4."
647 DATA ".4.4"
650 DATA "6..6"
653 DATA "...."
656 DATA "6..6"
659 DATA ".66."
662 DATA "6..6"
665 DATA "...."
668 DATA ".66."
671 DATA "6..6"
674 DATA 1,3,5,9,11,13,17,19,21,25,27,29
677 DATA 1,3,6,8,11,13
680 DATA 1,11,1,9,1,13,3,9,3,11,1,13
683 DATA 6,1,6,3,6,5,8,1,8,3,8,5
686 DATA 6,29,6,25,6,27,8,25,8,27,8,29
689 DATA 13,19,11,17,11,19,11,21,13,17,13,21
692 DATA 0,0,2,2,1,2,3,2,1,0,3,2
695 DATA 0,2,2,2,0,3,2,1,1,2,2,3
698 DATA 0,2,2,0,0,1,3,3,1,3,2,3
701 DATA 0,2,0,0,0,3,3,3,1,1,2,3
704 DATA 0,2,2,2,0,2,3,3,1,3,2,2
707 DATA 0,2,0,2,0,3,3,3,1,1,2,1
710 DATA 0,2,2,2,1,1,3,1,1,3,3,3
713 DATA 0,2,2,2,1,0,2,1,1,3,3,3
716 DATA 0,2,2,0,0,3,1,3,1,3,1,3
719 DATA 9,9,9,9,9,9,9,9,9,9,9,9
