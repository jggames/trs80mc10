10 CLS:PRINT@32*1+13,"bricks":PRINT
20 PRINTTAB(10)"STEVE OSTROM"
30 PRINTTAB(9)"APRIL 17, 1982":PRINT
40 PRINTTAB(3)"MC-10 VERSION JIM GERRIE":PRINT
50 PRINT"'A' AND 'S' FOR LEFT AND RIGHT
60 PRINT"OR ',' AND '.' FOR FAST MOVEMENT";
70 PRINT"HIT SPACE TO BEGIN"
100 E$=CHR$(128)+CHR$(128):Z$=CHR$(159)+CHR$(159)
110 Z=RND(-(PEEK(9)*256+PEEK(10)))
150 PRINT@416,"DIFFICULTY FACTOR:"
160 INPUT"(1=EASY,3=MEDIUM,9=HARD)";Z1
170 IFZ1=9THENF1=2.173:O=2:GOTO200
180 IFZ1=3THENF1=2.334:O=4:E$=E$+E$:Z$=Z$+Z$:GOTO200
190 IFZ1<>1THEN150
191 F1=2.521:O=6:E$=E$+E$+E$:Z$=Z$+Z$+Z$
200 P=0
210 M=3
220 CLS(0)
230 Z=464
240 FORY=2TO3
250 FORX=0TO63
260 SET(X,Y,6)
270 SET(X,Y+6,4)
280 SET(X,Y+8,5)
290 SET(X,Y+10,3)
300 SET(X,Y+28,7)
310 NEXTX
320 NEXTY
330 FORX=0TO1
340 FORY=4TO29
350 SET(X,Y,1)
360 SET(X+62,Y,1)
370 NEXTY:NEXTX
380 PRINT@Z,Z$;
390 PRINT@1,"MEN LEFT";
400 PRINT@11,M;
410 PRINT@17,"POINTS";
420 PRINT@25,P;
430 CT=0
440 Y=14
450 B=1:K=PEEK(2)ANDPEEK(17023):IFK=32THEN460
451 PRINT@Z,E$;:Z=Z+(K=65)-(K=83)+(K=44)*2-(K=46)*2:IFZ<449ORZ>479-OTHENZ=Z-(K=65)+(K=83)-(K=44)*2+(K=46)*2
452 PRINT@Z,Z$;:GOTO450
460 IFRND(2)=1THENX=61:A=-1:GOTO480
470 X=2:A=1
480 RESET(X,Y)
490 X=X+A:Y=Y+B
500 SET(X,Y,8)
510 PRINT@Z,E$;
520 K=PEEK(2)ANDPEEK(17023):Z=Z+(K=65)-(K=83)+(K=44)*2-(K=46)*2:IFZ<449ORZ>479-OTHENZ=Z-(K=65)+(K=83)-(K=44)*2+(K=46)*2
530 PRINT@Z,Z$;
540 ON(POINT(X+A,Y+B))+1GOTO480,570,580,590,660,650,580,670
550 SOUND1,1
560 IFPOINT(X+A,Y+B)>0THEN540
561 GOTO480
570 A=-A:GOTO550
580 FG=1:B=-B:A=(RND(3)-2)*RND(2):GOTO550
590 P=P+Z1:S=50:CH=1
600 PRINT@25,P;
610 PRINT@(INT((X+A)/2)+32*INT((Y+B)/2)),CHR$(128);:SOUND9,1:RESET(X,Y):X=X+A:Y=Y+B:SET(X,Y,8):GOSUB680
620 IFCH=1THENIFABS(B)=2THENB=B/2
630 IFCH=2THENIFABS(B)=1THENB=B*2
640 CT=CT+1:IFCT<90THEN560
641 M=M+1:GOTO220
650 P=P+2*Z1:S=100:GOTO600
660 P=P+4*Z1:S=150:CH=2:GOTO600
670 RESET(X,Y):SOUND1,25:M=M-1:PRINT@11,M;:IFM>0THEN440
671 FORJ=1TO2500:NEXTJ:GOTO200
680 IFFG=1THENB=-B
690 FG=0:RETURN
