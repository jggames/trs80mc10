0 DIMZZ,CC,M$,MC,N(21):MC=16384:POKE16925,0:POKE16926,1
1 CC=1:FORZZ=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",ZZ,3)):CC=CC+1
2 NEXT:GOTO100
4 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
100 REM 
110 REM DECLARE MAIN VARIABLES
120 DIMAM(12,14),TN(9,4),QU(9):HS=100
130 FORA=0TO9:FORB=0TO4
140 READ TN(A,B)
150 NEXT B,A
160 FORA=0TO9
170 READQU(A)
180 NEXT
190 REM
200 CLS
210 PRINT@2+32*2+8,;:M$="CLEAR A PATH":GOSUB7
215 PRINT@2+32*11+7,"KUNUHISA OGAWA";
220 PRINT@1+32*14+8,"PUSH SPACE KEY";
230 REM SCORES
240 PRINT@4+32*5+8,"HI-SCORE";
250 PRINT@5+32*8+8,"SCORE";
270 PRINT@5+32*6+8,HS;
280 PRINT@5+32*9+8,SC;
285 FORCC=1TO25:M$=INKEY$:NEXT
290 M$=INKEY$:CC=RND(0):IFM$<>" "THEN290
300 REM INITIALIZE
310 REM ROUND 1
320 RO=-1:SC=0
330 CLS:RO=RO+1
340 IFRO=10THENPRINT@3+32*7+8,;:M$="100 BONUS!":GOSUB7:SC=SC+100:GOTO1230
350 PRINT@5+32*7+9,"START"
360 FORA=0TO12:FORB=0TO14
370 AM(A,B)=100
380 NEXT B,A
390 REM MAIN SCREEN
400 CLS3
401 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
402 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯©£­¥¯¯¡£§®¦¯¡£­";
403 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¥¯¯¥¯¯¤¬¯¥¯¥¤¬§";
404 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¥¯­¥¯¯¥¯¯¡£¥¥¦¯";
405 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯«£¯££§££§§¯§§¯§";
406 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯¯­¯¯¯¯¯¯¯";
407 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯©«­¯¯¯¯¯¯";
408 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯¤¬¥¯¯¯¯¯¯";
409 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯¥¯¥¯¯¯¯¯¯";
410 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
411 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¡£­®¦¯£¡§¥¯¥¯";
412 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¤¬§¥¯¥¯¥¯¤¬¥¯";
413 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯¥¯¯¡£¥¯¥¯¥¯¥¯";
414 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯¯¯¯¯¯§¯¯§¯§¯§¯§¯§¯";
415 PRINT"ª¯¯ª¯¯ª¯¯ª¯¯ª¯";:M$="USE: AWSZ OR IJKL":GOSUB7:PRINT@497,;:M$="SPACE=BRIDGE":GOSUB7
425 FORA=0TO14
430 FORB=0TO4
440 AM(B*3,A)=0
450 NEXTB,A
460 FORA=0TO3
470 B=INT(RND(0)*14)+1
480 IFAM(A*3,B)=-1THEN470
490 AM(A*3,B)=1:AM(A*3+3,B)=-1
500 PRINT@A*3+32*B,;:PRINT"ª¬¬¨";
510 NEXT
520 REM GOAL MARKERS
540 FORA=0TO4
550 PRINT@A*3+32*15,;:PRINTMID$(STR$(TN(RO,A)),2,1);
560 NEXT
570 REM ROUND INFO
580 PRINT@14+32*2,"RO";
590 PRINT@14+32*5,"SC  ";
600 PRINT@14+32*8,"MS  ";
610 PRINT@14+32*11,"QU  ";
620 REM PRINT
630 REM VALUES
640 PRINT@14+32*3,MID$(STR$(RO+1)+"  ",2,2);
650 PRINT@14+32*6,MID$(STR$(SC)+"   ",2,4);
660 PRINT@14+32*9,MID$(STR$(10)+"   ",2,4);
670 PRINT@14+32*12,MID$(STR$(QU(RO))+"   ",2,4);
680 MK=10:X=1:Y=10
690 MY=0:MX=INT(RND(0)*5)*3:HO=0:CL=4+2*(MK=1ORMK=5)
700 REM CURSOR
710 PRINT@MX+32*MY,;:PRINT"Ÿ";
720 REM REPLACE SCREEN UNDER CURSOR
730 PRINT@MX+32*MY,;:A=AM(MX,MY)
740 IFA=0THENPRINT"ª";
750 IFA=-1THENPRINT"¨";
760 IFA=1THENPRINT"ª";
770 IFA=100THENPRINT"¬";
780 IFHO=0THENMY=MY+1
790 MX=MX+(HO=-1)-(HO=1)
800 IFMY=15THEN1030
810 REM PRINT NEW CURSOR
820 Q=PEEK(9)AND128:POKE49151,Q:POKE49151,128-Q
830 PRINT@MX+32*MY,CHR$(159+16*CL);
840 IFAM(MX,MY)=0ORAM(MX,MY)=100THEN870
850 A=AM(MX,MY)
860 HO=HO+(A=-1)-(A=1)
870 S=0:FORZZ=1TO10:K=PEEK(2)ANDPEEK(17023):IFK<>0THENZZ=25:FORCC=ZZTO10:IFCC<>0THENNEXT
871 NEXT
872 IFK=65ORK=74THENS=7
873 IFK=83ORK=76THENS=3
874 IFK=87ORK=73THENS=1
875 IFK=90ORK=75THENS=5
900 PRINT@X+32*Y,;:PRINT"¯";
910 IFAM(X-1,Y)=1THENPRINT@X+32*Y,;:PRINT"¬";
920 X=X+3*(-(S=3ANDX<10)+(S=7ANDX>1))
930 Y=Y+(S=1ANDY>2)-(S=5ANDY<14)
950 PRINT@X+32*Y,CHR$(126);
960 IFK<>32THEN700
970 IFAM(X-1,Y-1)=-1ORAM(X+2,Y-1)=1THENSOUND1,2:GOTO700
980 M$="C1D1E1F1":GOSUB4
990 REM PLACE BRIDGE
1000 PRINT@(X-1)+32*(Y-1),"ª¬¬¨";
1010 AM(X-1,Y-1)=1:AM(X+2,Y-1)=-1
1020 GOTO700
1030 REM CALC SCORE
1040 M$="D2K2":GOSUB4
1050 PRINT@14+32*6,MID$(STR$(SC)+"   ",2,4);
1060 PRINT@14+32*9,MID$(STR$(MK)+"   ",2,4);
1070 SC=SC+TN(RO,MX/3)-(CL=2)*TN(RO,MX/3)
1080 MK=MK-1
1090 REM CALC BONUS
1100 PRINT@14+32*6,MID$(STR$(SC)+"   ",2,4);
1110 PRINT@14+32*9,MID$(STR$(MK)+"   ",2,4);
1120 IFMK>0THEN690
1130 REM END OF ROUND
1140 FORI=0TO2500:NEXT
1150 CLS
1160 PRINT@1+32*1+4,"YOUR SCORE WAS";
1170 PRINT@3+32*2+4,MID$(STR$(SC),2);" FOR THIS ROUND."
1180 PRINT@1+32*4+4,"YOU NEEDED TO SCORE";
1190 PRINT@3+32*5+4,MID$(STR$(QU(RO)),2);" TO CONTINUE."
1200 IFSC>=QU(RO)THENPRINT@4+32*9+4,;:M$="YOU SUCCEEDED!":GOSUB7:FORI=0TO4000:NEXT:GOTO330
1220 PRINT@4+32*9,"THANKS FOR PLAYING."
1230 FORI=0TO4000:NEXT
1240 IFHS<SCTHENHS=SC
1250 GOTO200
1260 DATA 0,1,2,3,4,9,0,4,1,2
1270 DATA 4,3,5,4,6,3,3,3,3,0
1280 DATA 3,7,6,0,1,0,0,5,0,0
1290 DATA 2,1,0,3,4,3,1,0,2,2
1300 DATA 0,0,1,0,9,9,0,8,0,7
1310 DATA 25,85,130,163,220,270
1320 DATA 315,335,400,515
1330 REM     ORIGINAL FOR 
1340 REM     NEC PC-6001
1350 REM   BY KUNUHISA OGAWA
1360 REM MC-10 EDITS JIM GERRIE
