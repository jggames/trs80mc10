0 CLS:GOSUB6000:GOTO130
1 VP=VARPTR(M$):C1=1:LL=LEN(M$):ON1-(CC>0ANDCC<=LL)GOTO3:CC=CC-1
2 ON1-(MID$(I$,C1,1)<>"")GOTO3:POKE(PEEK(VP+2)*256)+PEEK(VP+3)+CC,ASC(MID$(I$,C1,1)):C1=C1+1:CC=CC+1:ON1-(CC<=LL)GOTO3,2
3 RETURN
100 REM * ROBOT RUBBER DUCKIES FROM HYPERSPACE / TRS-80 EXTENDED BASIC 16K 
110 REM * ELMER'S ARCADE / AUGUST / R.RAMELLA  
130 CLEAR2000 
135 B$="€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€"
140 DATA FEATHERHEAD,PADDLEFOOT,DUCKLING,DUKE,DUCKESS,DRAKE,DONALD,WEBFOOT FIRST CLASS,SUPER DUCK,EL DUCKO SUPREMO 
150 FORA=0TO9 
160 READF$(A) 
170 HH=(RND(2)*.25)+.50 
180 ZZ=RND(4)*.25 
190 UU=RND(4)*.25 
200 NEXTA 
210 S=-150 
220 G$="€€€€€" 
230 C=32 
240 V=30 
250 A$(1)=CHR$(139+16)+CHR$(131+16)+CHR$(134+16) 
260 A$(2)=CHR$(137+48)+CHR$(131+48)+CHR$(135+48) 
270 A$(3)=CHR$(139+80)+CHR$(131+80)+CHR$(134+80) 
280 FORA=1TO3 
290 B$(A)=B$+B$+B$+B$+B$+B$+B$+LEFT$(B$,31)
300 NEXT 
310 FORA=1TO30 
320 B(1)=RND(250)+1 
330 B(2)=RND(251)+1 
340 B(3)=RND(251)+1 
350 FORY=1TO3 
360 IFMID$(B$(Y),B(Y)-1,5)=G$THENM$=B$(Y):CC=B(Y):I$=A$(Y):GOSUB1:B$(Y)=M$
370 NEXT 
380 NEXT 
390 GOSUB6015:CLS0 
400 H=251
410 U=251 
420 Z=1
430 PRINT@495,CHR$(135+112)+CHR$(143+112)+CHR$(139+112);
440 IFINKEY$<>""THENF=F-5:GOSUB540 
450 PRINT@0,F;:PRINT@15,S;:SET(C,30,8):IFS>FTHEN740 
480 S=S+1
490 IFQ=5THENGOSUB550 
500 PRINT@64,MID$(B$(1),H,32);:H=H-HH:IFH<1THENH=223 
510 PRINT@128,MID$(B$(2),Z,32);:Z=Z+ZZ:IFZ>223THENZ=1 
520 PRINT@192,MID$(B$(3),U,32);:U=U-UU:IFU<1THENU=223 
530 GOTO440
540 Q=5
550 SET(C,V,8) 
560 IFPOINT(C,V-1)=6THENFORT=1TO2:SOUND32,2:SOUND32,2:SOUND45,2:SOUND45,2:NEXT:F=F-30:RESET(C,V):Q=0:V=30 
570 IFPOINT(C,V-1)=4THENF=F-30:GOSUB640:RETURN 
580 IFPOINT(C,V-1)=2THENPRINT@C/2+64,"DING";:FORT=1TO5:SOUND200,1:NEXT:F=F+75 
590 RESET(C,V)
600 V=V-4
610 IFV<1THENV=30:Q=0 
620 RETURN
640 L=240:G=128+(C/2):M$=B$(2):CC=Z+(C/2)-3:I$=G$:GOSUB1:B$(2)=M$
670 PRINT@G-2,"€€€€€€€"; 
680 PRINT@G,A$(2); 
690 SOUNDL,1 
700 L=L-15 
710 IFG>492ANDG<498THENPRINT@G-70,"PRESSED DUCK";:SOUND1,10:GOTO740 
720 PRINT@G,"€€€";
730 IFG>480THENRESET(C,V):Q=0:V=30:RETURN
731 G=G+RND(3)+30:GOTO680 
740 FORT=1TO750
750 NEXTT 
760 IFF<0THENF=0:GOTO770
761 IFF>1800THENF=1800 
770 PRINT@480,"GAME OVER.€PLAY AGAIN?";
780 SOUND204,3 
790 SOUND204,3 
800 SOUND193,3 
810 SOUND210,3 
820 SOUND204,6 
830 SOUND193,6 
840 PRINT@267,"RATING:";:PRINT@299,F$(INT(F/200)); 
850 I$=INKEY$:IFI$=""THEN780
860 IFI$="Y"THENRUN
870 IFI$="N"THENEND
880 GOTO850
6000 PRINT"ROBOT RUBBER DUCKIES FROM SPACE."
6002 PRINT"THERE ARE 3 ROWS OF FLYING" 
6003 PRINT"DUCKS. YELLOW DUCKS AT THE TOP" 
6004 PRINT"AND GREEN DUCKS AT THE BOTTOM"
6005 PRINT"FLY EAST. RED DUCKS IN THE MID"
6006 PRINT"ROW FLY WEST. AT THE BOTTOM IS"
6007 PRINT"A HIDE. TAP ANY KEY TO SHOOT."
6008 PRINT"YOU GET 75 PTS FOR EVERY YELLOW"
6009 PRINT"DUCK. IT COSTS 5 PTS FOR EACH"
6010 PRINT"SHOT. IF YOU HIT A RED OR GREEN"
6011 PRINT"DUCK, YOU LOSE 30 POINTS. ALSO,"
6012 PRINT"HIT A RED DUCK & IT FALLS FROM" 
6013 PRINT"THE SKY. IF IT LANDS ON YOU,"
6014 PRINT"THAT'S A 'PRESSED DUCK'.":RETURN
6015 CLS:PRINT"ONE OTHER FEATURE COMPLICATES"
6016 PRINT"THINGS--AT THE TOP ARE TWO #S,"
6017 PRINT"THE SCORE AND TIMER. THE TIMER"
6018 PRINT"STARTS AT -150. IF IT CATCHES
6019 PRINT"UP WITH THE SCORE, YOU LOSE."
6020 PRINT"HERE ARE THE RATINGS:"
6021 PRINT"UNDER 200, FEATHERHEAD"
6022 PRINT"200-399, PADDLEFOOT"
6023 PRINT"400-599, DUCKLING"
6024 PRINT"600-799, DUKE, -999, DUCKESS"
6026 PRINT"1000-1199, DRAKE"
6027 PRINT"1200-1399, DONALD"
6028 PRINT"1400-1599, WEBFOOT 1ST CLASS"
6029 PRINT"1600-1799, SUPER DUCK"
6030 PRINT"1800-, EL DUCKO SUPREMO.":PRINT"HIT <ENTER> TO BEGIN";
6040 I$=INKEY$:IFI$<>CHR$(13)THEN6040
6050 RETURN
8000 REM MC-10 EDIT BY JIM GERRIE
