1 '     ***********************************
        *      T A N K    B A T T L E     *
        *  F O R  A  T R S - 8 0   1 6 K  *
        * BY MARK KOENIG - STRONGSVILLE,O.*
        *  W R I T T E N   11 / 27 / 80   *
2 '     ***********************************
20 CLEAR500:DEFSTRA,K,S:A(1)=CHR$(188)+CHR$(191)+CHR$(188):A(2)=CHR$(191)+CHR$(191)+CHR$(140):A(3)=CHR$(143)+CHR$(191)+CHR$(143):A(4)=CHR$(140)+CHR$(191)+CHR$(191):A(5)=CHR$(188)+CHR$(179)+CHR$(188):A(6)=CHR$(191)+CHR$(179)+CHR$(140):P1=0:P2=0
30 K=CHR$(140):A(7)=CHR$(143)+CHR$(179)+CHR$(143):A(8)=CHR$(140)+CHR$(179)+CHR$(191):CLS:PRINTTAB(24);"TANK BATTLE":INPUT"NAME OF PLAYER 1";O$:INPUT"NAME OF PLAYER 2";T$
50 SA=STRING$(3,24):S(0)=CHR$(191)+CHR$(131)+CHR$(191)+CHR$(26)+SA+CHR$(143)+CHR$(140)+CHR$(143):S(1)=" "+CHR$(191)+CHR$(26)+CHR$(24)+CHR$(143):S(2)=CHR$(179)+CHR$(179)+CHR$(191)+CHR$(26)+SA+CHR$(143)+CHR$(140)+CHR$(140)
52 S(3)=CHR$(131)+CHR$(179)+CHR$(191)+CHR$(26)+SA+CHR$(140)+CHR$(140)+CHR$(143):S(4)=CHR$(191)+CHR$(176)+CHR$(191)+CHR$(26)+SA+"  "+CHR$(143):S(5)=CHR$(191)+CHR$(179)+CHR$(179)+CHR$(26)+SA+CHR$(140)+CHR$(140)+CHR$(143)
54 S(6)=CHR$(191)+CHR$(179)+CHR$(179)+CHR$(26)+SA+CHR$(143)+CHR$(140)+CHR$(143):S(7)=CHR$(131)+CHR$(131)+CHR$(191)+CHR$(26)+CHR$(24)+CHR$(143)
56 S(8)=CHR$(191)+CHR$(179)+CHR$(191)+CHR$(26)+SA+CHR$(143)+CHR$(140)+CHR$(143):S(9)=CHR$(191)+CHR$(179)+CHR$(191)+CHR$(26)+CHR$(24)+CHR$(143)
60 INPUT"HIT ENTER TO BEGIN";E$
65 CLS:AW=STRING$(80,191):AB=STRING$(80,32):GOSUB1000
70 B1=14337:B2=14338:B3=14340:B4=14344:B5=14352:B6=14368:B7=14400:UNO=195:TWO=889:S=STRING$(3,32):C1=2:C2=8:X=1:GOSUB570
80 IFX=1THEN90ELSE320
90 X=-X:IFPEEK(B7)=16THEN100ELSEIFPEEK(B1)=2THEN120ELSEIFPEEK(B3)=8THEN140ELSEIFPEEK(B1)=16THEN190ELSE80
100 IFC1=1THENC1=4ELSEC1=C1-1
110 PRINT@UNO,A(C1);:GOTO80
120 IFC1=4THENC1=1ELSEC1=C1+1
130 PRINT@UNO,A(C1);:GOTO80
140 ONC1GOTO150,160,170,180
150 U=UNO+15360:IF(UNO>63)AND(PEEK(U-64)=32)AND(PEEK(U-63)=32)AND(PEEK(U-62)=32)THENPRINT@UNO,S;:UNO=UNO-64:PRINT@UNO,A(C1);:GOTO80ELSE80
160 IF(UNO+3)/64<>INT((UNO+3)/64)ANDPEEK(UNO+15363)=32THENPRINT@UNO,S;:UNO=UNO+1:PRINT@UNO,A(C1);:GOTO80ELSE80
170 U=UNO+15360:IFUNO<896ANDPEEK(U+64)=32ANDPEEK(U+65)=32ANDPEEK(U+66)=32THENPRINT@UNO,S;:UNO=UNO+64:PRINT@UNO,A(C1);:GOTO80ELSE80
180 IFUNO/64<>INT(UNO/64)ANDPEEK(UNO+15359)=32THENPRINT@UNO,S;:UNO=UNO-1:PRINT@UNO,A(C1);:GOTO80ELSE80
190 ONC1GOTO200,230,260,290
200 IFUNO<64THEN80ELSEI=UNO-63
210 IFPEEK(I+15360)<>32THEN220ELSEPRINT@I,K;:FORO=1TO5:NEXT:PRINT@I," ";:I=I-64:IFI<=0THEN80ELSE210
220 IFI=TWO+2ORI=TWO+1ORI=TWOTHEN550ELSEPRINT@I,CHR$(191);:GOTO80
230 IF(UNO+3)/64=INT((UNO+3)/64)THEN80ELSEI=UNO+3
240 IFPEEK(I+15360)<>32THEN250ELSEPRINT@I,K;:PRINT@I," ";:I=I+3:IFI=INT(UNO/64)*64+63THEN80ELSE240
250 IF(I=TWO)OR(I-1=TWO)OR(I-2=TWO)THEN550ELSEPRINT@I,CHR$(191);:GOTO80
260 IFUNO>895THEN80ELSEI=UNO+65
270 IFPEEK(I+15360)<>32THEN280ELSEPRINT@I,K;:FORO=1TO5:NEXT:PRINT@I," ";:I=I+64:IFI>=960THEN80ELSE270
280 IFI=TWOORI=TWO+1ORI=TWO+2THEN550ELSEPRINT@I,CHR$(191);:GOTO80
290 IFUNO/64=INT(UNO/64)THEN80ELSEI=UNO-1
300 IFPEEK(I+15360)<>32THEN310ELSEPRINT@I,K;:FORO=1TO3:NEXT:PRINT@I," ";:I=I-3:IFI<=INT(UNO/64)*64THEN80ELSE300
310 IF(I=TWO+2)OR(I=TWO+1)OR(I=TWO)THEN550ELSEPRINT@I,CHR$(191);:GOTO80
320 X=-X:IFPEEK(B2)=4THEN330ELSEIFPEEK(B2)=8THEN350ELSEIFPEEK(B2)=16THEN370ELSEIFPEEK(B6)=8THEN420ELSE80
330 IFC2=5THENC2=8ELSEC2=C2-1
340 PRINT@TWO,A(C2);:GOTO80
350 IFC2=8THENC2=5ELSEC2=C2+1
360 PRINT@TWO,A(C2);:GOTO80
370 ONC2-4GOTO380,390,400,410
380 T=TWO+15360:IFTWO>63ANDPEEK(T-64)=32ANDPEEK(T-63)=32ANDPEEK(T-62)=32THENPRINT@TWO,S;:TWO=TWO-64:PRINT@TWO,A(C2);:GOTO80ELSE80
390 IFPEEK(15360+TWO+3)=32AND(TWO+3)/64<>INT((TWO+3)/64)THENPRINT@TWO,S;:TWO=TWO+1:PRINT@TWO,A(C2);:GOTO80ELSE80
400 T=TWO+15360:IFTWO<896ANDPEEK(T+64)=32ANDPEEK(T+65)=32ANDPEEK(T+66)=32THENPRINT@TWO,S;:TWO=TWO+64:PRINT@TWO,A(C2);:GOTO80ELSE80
410 IFPEEK(TWO+15359)=32ANDTWO/64<>INT(TWO/64)THENPRINT@TWO,S;:TWO=TWO-1:PRINT@TWO,A(C2);:GOTO80ELSE80
420 ONC2-4GOTO430,460,490,520
430 IFTWO<64THEN80ELSEI=TWO-63
440 IFPEEK(I+15360)<>32THEN450ELSEPRINT@I,K;:FORO=1TO5:NEXT:PRINT@I," ";:I=I-64:IFI<=0THEN80ELSE440
450 IFI=UNOORI=UNO+1ORI=UNO+2THEN560ELSEPRINT@I,CHR$(191);:GOTO80
460 IF(TWO+3)/64<>INT((TWO+3)/64)THENI=TWO+3ELSE80
470 IFPEEK(I+15360)<>32THEN480ELSEPRINT@I,K;:FORO=1TO3:NEXT:PRINT@I," ";:I=I+3:IFI>=INT(TWO/64)*64+63THEN80ELSE470
480 IF(I=UNO)OR(I-1=UNO)OR(I-2=UNO)THEN560ELSEPRINT@I,CHR$(191);:GOTO80
490 IFTWO>895THEN80ELSEI=TWO+65
500 IFPEEK(I+15360)<>32THEN510ELSEPRINT@I,K;:FORO=1TO5:NEXT:PRINT@I," ";:I=I+64:IFI>=960THEN80ELSE500
510 IFI=UNOORI=UNO+1ORI=UNO+2THEN560ELSEPRINT@I,CHR$(191);:GOTO80
520 IFTWO/64=INT(TWO/64)THEN80ELSEI=TWO-1
530 IFPEEK(I+15360)<>32THEN540ELSEPRINT@I,K;:FORO=1TO3:NEXT:PRINT@I," ";:I=I-3:IFI<=INT(TWO/64)*64THEN80ELSE530
540 IF(I=UNO+2)OR(I=UNO+1)OR(I=UNO)THEN560ELSEPRINT@I,CHR$(191);:GOTO80
550 FORI=1TO10:PRINT@TWO,"HIT";:FORIK=1TO50:NEXT:PRINT@TWO,"   ";:FORIK=1TO50:NEXT:NEXT:P1=P1+1:IFP1=10THEN580ELSEGOSUB570:PRINT@UNO,S;:PRINT@TWO,S;:GOTO70
560 FORI=1TO10:PRINT@UNO,"HIT";:FORIK=1TO50:NEXT:PRINT@UNO,"   ";:FORIK=1TO50:NEXT:NEXT:P2=P2+1:IFP2=10THEN590ELSEGOSUB570:PRINT@UNO,S;:PRINT@TWO,S;:GOTO70
570 PRINT@0,LEFT$(AB,64);:PRINT@64,LEFT$(AB,64);:PRINT@20,S(P1);:PRINT@40,S(P2);:PRINT@UNO,A(C1);:PRINT@TWO,A(C2);:RETURN
580 O$=O$:GOTO600
590 O$=T$
600 Q$=O$+" IS THE WINNER!!":FORI=1TO300:PRINT@RND(950),Q$;:NEXT:FORI=1TO100:NEXT:RUN
1000 PRINT@128,"";
1005 READL1,L2:IFL1=99ORL2=99THENRESTORE:RETURNELSEPRINTLEFT$(AW,L1);LEFT$(AB,L2);:GOTO1005
1100 DATA67,57,7,3,15,3,15,3,15,3,7,3,3,45,3,3,7,3,3,3,3,3,9
1110 DATA3,3,3,9,3,3,3,3,3,7,9,3,3,3,21,3,3,3,9,7,3,3,3,3,3,3,3,15,3,3,3,3,3,3,3,7
1120 DATA9,3,3,3,21,3,3,3,9,7,3,3,3,3,3,9,3,3,3,9,3,3,3,3,3,7,3,3,45,3,3,7
1130 DATA3,9,3,27,3,9,3,7,57,68,0,0
11010 DATA99

