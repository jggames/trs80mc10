0 M=16384:GOTO110

10 REM C64 DUNGEON CONSTRUCTION SET
20 REM ----------------------------

80 REM     1    2    3    4    5    6    7    8    9   10   11   12   13   14   15   16
81 DATA "WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE"
82 DATA "WNSE WN.. .NS. .NS. .N.. .NS. .NS. .NS. .N.. .NS. .NS. .NS. .NS. .NS. .N.E WNSE"
83 DATA "WNSE W..E WNSE WNSE W..E WNSE WNSE WNSE W..E WNSE WNSE WNSE WNSE WNSE W..E WNSE"
84 DATA "WNSE W..E WNSE .N.E ..S. .NS. .NS. .NS. .... WNSE WN.. .NS. .NS. .NS. ...E WNSE"
85 DATA "WNSE W..E WNSE W..E WNSE WNSE WNSE WNSE W..E WNSE W..E WNSE WNSE WNSE W..E WNSE"
86 DATA "WNSE W... .NS. .... .NS. .NS. .NS. .NS. ...E WNSE W... .NS. .NS. .NS. ...E WNSE"
87 DATA "WNSE W..E WNSE W..E WNSE WNSE WNSE WNSE W..E WNSE W..E WNSE WNSE WNSE W..E WNSE"
88 DATA "WNSE W... .NS. ..S. .N.. .NS. .NS. .NS. ...E WNSE W..E WNSE WN.. .NS. ...E WNSE"
89 DATA "WNSE W..E WNSE WNSE W..E WNSE WNSE WNSE W..E WNSE W.S. .NS. ...E WNSE W..E WNSE"
90 DATA "WNSE W..E WNSE WN.. ..S. .N.E WNSE WN.. ..S. .N.E WNSE WNSE W..E WNSE W..E WNSE"
91 DATA "WNSE W..E WNSE W..E WNSE W..E WNSE W..E WNSE W.S. .N.E WNSE W..E WNSE W..E WNSE"
92 DATA "WNSE W..E WNSE W..E WNSE W..E WNSE W..E WNSE WNSE W..E WNSE W..E WNSE W..E WNSE"
93 DATA "WNSE W.S. .NS. ..S. .NS. ..S. .NS. ..S. .NS. .NS. ..S. .NS. ..S. .NS. ..SE WNSE"
94 DATA "WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE WNSE"
95 DATA ""

99 REM FORMAT FOR EACH SPACE: WNSE FOR WALLS OR . IF NO WALL THAT DIRECTION

100 REM INITIALISATION
110 CLS
120 DIM M(16,16),DI$(3),WA$(1),MA(2,2):DI$(0)="NORTH":DI$(1)="EAST ":DI$(2)="SOUTH":DI$(3)="WEST ":WA$(1)="-WALL"
125 WA$(0)="     "

130 X=0:Y=0:DI=0:XS=0:YS=0:WA=0
135 PRINT"SETTING UP MAP":REM READ MAP
140 REM INPUT L$
145 READ L$
150 IF LEN(L$)=0 GOTO 250
160 CZ=0
170 FOR I=1 TO LEN(L$)
180 C$=MID$(L$,I,1)
190 IF C$="N" THEN CZ=CZ OR 8
200 IF C$="E" THEN CZ=CZ OR 4
205 IF C$="S" THEN CZ=CZ OR 2
210 IF C$="W" THEN CZ=CZ OR 1
215 IF C$=" " THEN M(X,Y)=CZ:CZ=0:X=X+1
220 IF X>=XS THEN XS=X
225 NEXT
230 M(X,Y)=CZ:X=0:Y=Y+1
240 GOTO 140
250 REM COME FROM 150
260 CLS
265 YS=Y:XS=XS+1
270 X=1:Y=1

500 REM LOOP
510 GOSUB 1000: REM STATUS
515 GOSUB 2000: REM RENDER
520 GOSUB 55000: REM INPUT
530 GOTO 500

1000 REM DISPLAY CURRENT (X,Y) VALUE
1010 SX=17
1020 SY=11
1030 SL$="    "
1035 SW=5
1040 GOSUB 63900
1050 CZ=M(X,Y)
1060 SX=17:SL$=".":IF CZ AND 8 THEN SL$="N"
1065 GOSUB 63900
1070 SX=18:SL$=".":IF CZ AND 4 THEN SL$="E"
1075 GOSUB 63900
1080 SX=19:SL$=".":IF CZ AND 2 THEN SL$="S"
1085 GOSUB 63900
1090 SX=20:SL$=".":IF CZ AND 1 THEN SL$="W"
1095 GOSUB 63900
1100 RETURN

2000 REM RENDER DUNGEON
2010 REM --------------
2020 REM CLEAR AREA
2040 FOR SY=0 TO 15
2045 PRINT@SY*32,"€€€€€€€€€€€€€€€€";
2070 NEXT
2090 REM FIND CELLS / REORIENT SW
2100 REM STORE IN MA - WE'RE AT (0,1)
2110 SX=X:SY=Y
2113 CO=DI+X+Y AND 1
2115 FOR TY=0 TO 2
2120 GOSUB 59800:REM LEFT/RIGHT SX/SY
2125 MA(1,TY)=0
2126 IF SX>=0 AND SY>=0 AND SX<XS AND SY<YS THEN MA(1,TY)=M(SX,SY)
2130 MA(0,TY)=RL
2140 MA(2,TY)=RR
2150 GOSUB 59900:REM ADVANCE
2160 NEXT
2170 REM DRAW BACK WALLS
2180 SA=MA(1,2):GOSUB 59700
2190 IF RF=0 GOTO 2245
2195 SZ=143:SW=5:IF CO=1 THEN SW=6
2200 FOR SY=5 TO 9
2210 FOR SX=5 TO 10
2220 POKEM+SX+SY*32,SZ+SW*16
2230 NEXT
2240 NEXT
2245 SW=6:IF CO=1 THEN SW=5
2250 FOR DE=0 TO 2 STEP 2 
2260 SA=MA(DE,2):GOSUB 59700
2270 IF RF=0 GOTO 2350
2280 FOR SX=DE*5.5 TO 4+DE*5.5
2290 FOR SY=5 TO 9
2300 POKEM+SX+SY*32,SZ+SW*16
2310 NEXT
2340 NEXT 
2350 NEXT
2360 REM 1,2 LEFT WALL
2370 SA=MA(1,2):GOSUB 59700
2380 IF RL=0 GOTO 2430
2390 SX=4:SZ=143
2400 FOR SY=5 TO 9:POKEM+SX+SY*32,SZ+SW*16:NEXT
2410 SY=4:SZ=139:POKEM+SX+SY*32,SZ+SW*16
2420 SY=10:SZ=142:POKEM+SX+SY*32,SZ+SW*16
2430 REM 1,2 RIGHT WALL
2440 IF RR=0 GOTO 2490
2450 SX=11:SZ=143
2460 FOR SY=5 TO 9:POKEM+SX+SY*32,SZ+SW*16:NEXT
2470 SY=4:SZ=135:POKEM+SX+SY*32,SZ+SW*16
2480 SY=10:SZ=141:POKEM+SX+SY*32,SZ+SW*16
2490 REM 1,1 BACK WALL
2500 SA=MA(1,1):GOSUB 59700
2510 SZ=143
2520 SW=5:IF CO=1 THEN SW=6
2520 IF RF=0 GOTO 2580
2530 FOR SY=4 TO 10
2540 FOR SX=4 TO 11
2550 POKEM+SX+SY*32,SZ+SW*16
2560 NEXT
2570 NEXT
2580 REM (0-2),1 BACK WALLS
2590 SW=5:IF CO=1 THEN SW=6
2600 FOR DE=0 TO 2 STEP 2
2610 SA=MA(DE,1):GOSUB 59700
2620 IF RF=0 GOTO 2680
2630 FOR SX=DE*6 TO 3+DE*6
2640 FOR SY=4 TO 10
2650 POKEM+SX+SY*32,SZ+SW*16
2660 NEXT
2670 NEXT
2680 NEXT 
2690 REM 1,1 LEFT SIDE WALL
2700 SW=5:IF CO=1 THEN SW=6
2710 SA=MA(1,1):GOSUB 59700
2720 IF RL=0 GOTO 2760
2730 FOR SX=1 TO 3
2735 SY=SX:SZ=139:POKEM+SX+SY*32,SZ+SW*16
2736 SY=14-SX:SZ=142:POKEM+SX+SY*32,SZ+SW*16
2737 SZ=143
2740 FOR SY=1+SX TO 13-SX:POKEM+SX+SY*32,SZ+SW*16:NEXT
2750 NEXT
2760 REM 1,1 RIGHT SIDE WALL
2770 IF RR=0 GOTO 2850
2780 FOR QX=1 TO 3
2790 SX=15-QX
2800 SY=QX:SZ=135:POKEM+SX+SY*32,SZ+SW*16
2810 SY=14-QX:SZ=141:POKEM+SX+SY*32,SZ+SW*16
2820 SZ=143
2830 FOR SY=1+QX TO 13-QX:POKEM+SX+SY*32,SZ+SW*16:NEXT
2840 NEXT
2850 REM 0,1 BACK WALL
2860 SA=MA(1,0):GOSUB 59700
2870 IF RF=0 GOTO 2930
2880 FOR SY=1 TO 13
2890 FOR SX=1 TO 14
2900 POKEM+SX+SY*32,SZ+SW*16
2910 NEXT
2920 NEXT
2930 REM (0,2)-0 BACK WALLS
2940 SW=6:IF CO=1 THEN SW=5
2950 FOR DE=0 TO 2 STEP 2
2960 SA=MA(DE,0):GOSUB 59700
2970 IF RF=0 GOTO 3000
2980 SX=DE*7.5
2990 FOR SY=1 TO 13:POKEM+SX+SY*32,SZ+SW*16:NEXT
3000 NEXT
3010 REM (1,0) LEFT SIDE WALL
3020 SA=MA(1,0):GOSUB 59700
3030 IF RL=0 GOTO 3080
3040 SX=0:SY=0:SZ=139:POKEM+SX+SY*32,SZ+SW*16
3050 SY=14:SZ=142:POKEM+SX+SY*32,SZ+SW*16
3060 SZ=143
3070 FOR SY=1 TO 13:POKEM+SX+SY*32,SZ+SW*16:NEXT
3080 REM (1,0) RIGHT SIDE WALL
3085 IF RR=0 GOTO 3130
3090 SX=15:SY=0:SZ=135:POKEM+SX+SY*32,SZ+SW*16
3100 SY=14:SZ=141:POKEM+SX+SY*32,SZ+SW*16
3110 SZ=143
3120 FOR SY=1 TO 13:POKEM+SX+SY*32,SZ+SW*16:NEXT
3130 REM DONE
3140 RETURN

55000 REM ASK FOR PROMPT & HANDLE INPUT
55010 PRINT@17+0*32,X+1","Y+1:PRINT@17+1*32,"GOING ";DI$(DI):PRINT@17+2*32,"SIZE"XS"X"YS
55015 PRINT@17+3*32,WA$(WA):PRINT@17+4*32,"fORWARD":PRINT@17+5*32,"bACKWARD":PRINT@17+6*32,"lEFT"
55030 PRINT@17+7*32,"rIGHT":PRINT@17+8*32,"qUIT":PRINT@17+9*32,;:INPUT C$
55040 IF C$="Q" GOTO 63999
55050 IF C$="F" THEN DM=1:GOTO 55100
55060 IF C$="B" THEN DM=-1:GOTO 55100
55070 IF C$="L" THEN DI=(DI-1)AND 3
55080 IF C$="R" THEN DI=(DI+1)AND 3
55090 RETURN
55100 GOTO 55200:REM CHECK WALLS
55110 IF DI=0 THEN Y=Y-DM
55120 IF DI=1 THEN X=X+DM
55130 IF DI=2 THEN Y=Y+DM
55140 IF DI=3 THEN X=X-DM
55145 WA=0
55146 IF Y>=YS THEN Y=0
55147 IF Y<0   THEN Y=YS-1
55148 IF X>=XS THEN X=0
55149 IF X<0   THEN X=XS-1
55150 RETURN
55200 REM CHECK WALLS
55205 CZ=M(X,Y)
55207 IF DM=-1 GOTO 55280
55210 IF (DI=0) AND (CZ AND 8) GOTO 55260
55220 IF (DI=1) AND (CZ AND 4) GOTO 55260
55230 IF (DI=2) AND (CZ AND 2) GOTO 55260
55240 IF (DI=3) AND (CZ AND 1) GOTO 55260
55250 GOTO 55110
55260 WA=1
55270 RETURN : REM WALL IN THE WAY
55280 REM BACKWARD
55290 IF (DI=2) AND (CZ AND 8) GOTO 55260
55300 IF (DI=3) AND (CZ AND 4) GOTO 55260
55310 IF (DI=0) AND (CZ AND 2) GOTO 55260
55320 IF (DI=1) AND (CZ AND 1) GOTO 55260
55330 GOTO 55110

59700 REM RETURN FRONT/BACK/LEFT/RIGHT
59710 REM GIVEN SA AND D
59720 SN=0:IF SA AND 8 THEN SN=1
59725 SE=0:IF SA AND 4 THEN SE=1
59730 SS=0:IF SA AND 2 THEN SS=1
59735 ZW=0:IF SA AND 1 THEN ZW=1
59740 IF DI=0 THEN RF=SN:RR=SE:RB=SS:RL=ZW
59745 IF DI=1 THEN RF=SE:RR=SS:RB=ZW:RL=SN
59750 IF DI=2 THEN RF=SS:RR=ZW:RB=SN:RL=SE
59755 IF DI=3 THEN RF=ZW:RR=SN:RB=SE:RL=SS
59760 RETURN

59800 REM RETURN LEFT/RIGHT FROM SX/SY/D
59810 IF DI=0 THEN LY=SY:RY=SY:LX=SX-1:RX=SX+1
59820 IF DI=1 THEN LX=SX:RX=SX:LY=SY-1:RY=SY+1
59830 IF DI=2 THEN LY=SY:RY=SY:LX=SX+1:RX=SX-1
59840 IF DI=3 THEN LX=SX:RX=SX:LY=SY+1:RY=SY-1
59850 RL=0:RR=0
59860 IF LX<0 OR LX>=XS OR LY<0 OR LY>=YS GOTO 59880
59870 RL=M(LX,LY)
59880 IF RX<0 OR RX>=XS OR RY<0 OR RY>=YS GOTO 59895
59890 RR=M(RX,RY)
59895 RETURN

59900 REM STEP FORWARD
59910 IF DI=0 THEN SY=SY-1:REM N
59920 IF DI=1 THEN SX=SX+1:REM E
59930 IF DI=2 THEN SY=SY+1:REM S
59940 IF DI=3 THEN SX=SX-1:REM W
59950 RETURN

63850 REM SET CURSOR POSITION
63851 REM SX=X SY=Y
63860 PRINT@SY*32+SX,;
63895 RETURN

63900 REM WRITE STR TO SCREEN
63901 REM SL$ = STRING
63910 GOSUB 63850
63920 PRINT SL$;
63930 RETURN

63998 REM QUIT PROGRAM
63999 CLS:END
