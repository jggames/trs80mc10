NEW


1 CLS
2 PRINT@ 74, "W E L C O M E"
4 PRINT@ 142, "T O"
6 PRINT@ 204, "C R A M"
8 PRINT@ 456, "INSTRUCTIONS (Y/N?)"
9 A$=INKEY$
10 IF A$="Y"THEN 15
12 IF A$="N"THEN 38
13 GOTO 8
14 REM INSTRUCTIONS
15 CLS:PRINT@ 33,"THE OBJECT OF THE GAME IS TO"
16 PRINT@ 65,"CRAM AS MUCH OF THE LINE ONTO"
17 PRINT@ 97,"THE SCREEN WITHOUT RUNNING"
18 PRINT@129,"INTO THAT SAME LINE BY HITTING"
19 PRINT@161,"ANY KEY TO MAKE A RIGHT ANGLE"
20 PRINT@193,"TURN THE COMPLETE NUMBER OF"
21 PRINT@225,"YOUR TURNS AND PREVIOUS HIGH"
22 PRINT@257,"SCORE WILL BE RECORDED ON THE"
23 PRINT@289,"SCREEN AFTER EVERY GAME. IF"
24 PRINT@321,"YOU RUN INTO YOUR LINE A"
25 PRINT@353,"CERTAIN PITCH OF TONES WILL"
26 PRINT@385,"END YOUR GAME."
27 PRINT@450,"HIT A KEY FOR A DEMONSTRATION";:A$=""
28 A$=INKEY$:IF A$=""THEN28
29 REM DEMONSTRATION
30 CLS7:TL=0:TR=63:VT=1:VB=31
31 FOR K=TL TO TR:SET(K,TL,5):NEXT
32 FOR K=VT TO VB:SET(TR,K,5):NEXT
33 FOR K=TR-1 TO TL STEP -1:SET(K,VB,5):NEXT
34 FOR K=VB-1 TO VT-1 STEP-1:SET(TL,K,5):NEXT
35 TL=TL+1:TR=TR-1:VT=VT+1:VB=VB-1
36 IF TL=16 THEN 8
37 GOTO 31
38 CLS7
39 GOSUB 280
40 L=0:R=63:T=1:B=31:N=0:CLS(7):GOSUB250
50 REM MOVE RIGHT
60 J=J+1:IF J=R THEN 200
65 SET(J,I,5):GOSUB300
70 IF INKEY$=""THEN60
72 SOUND 128,1:N=N+1:R=J
80 REM MOVE DOWN
90 I=I+1:IF I=B THEN 200
95 SET(J,I,5):GOSUB300
100 IF INKEY$="" THEN 90
102 SOUND128,1:N=N+1:B=I
110 REM MOVE LEFT
120 J=J-1:IF J=L THEN 200
125 SET(J,I,5):GOSUB300
130 IF INKEY$=""THEN 120
132 SOUND128,1:N=N+1:L=J
140 REM MOVE UP
150 I=I-1:IF I=T THEN 200
155 SET(J,I,5):GOSUB300
160 IF INKEY$="" THEN 150
162 SOUND128,1:N=N+1:T=I:GOTO60
170 REM DELAY VERTICAL
180 FOR Z=1 TO 30:NEXT
190 REM GAME OVER
195 FOR X=1 TO 255:SOUND X,1:NEXT
200 FOR X=215 TO 150 STEP-2:SOUNDX,1:NEXT
205 CLS:PRINT@228,"TURN";N;"PREVIOUS HIGH";M:IF N>M THEN M=N
210 FOR I=1 TO 2000:NEXT I:CLS:GOTO40
250 I=1:J=0:A$=INKEY$:RETURN
280 M=0:RETURN 
300 FORZ=1TO15:NEXT:RETURN
