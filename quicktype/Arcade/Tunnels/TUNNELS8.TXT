0 DIMK(255),A(6),O(6),T,M,A,C,R:T=RND(-(PEEK(9)*256+PEEK(10))):M=16384:GOTO2000
1 RETURN
2 D=-1:ONK(PEEK(M+P-1))GOTO3,40,1,50,23,62,64,66,68:RETURN
3 POKEM+P,B:P=P-1:PRINT@P,Y$;:SOUND40,1:B=143:PRINT@P,X$;:RETURN
4 FORT=1TO2:ON1-(P-32>0)GOTO30:ONK(PEEK(M+P-32+D))GOSUB5,1,1,52:NEXT:FORT=1TO2:ONK(PEEK(M+P+32+D))GOSUB6,1,1,53:NEXT:RETURN
5 PRINT@P,B$;:P=P-32+D:PRINT@P,X$;:GOSUB18:RETURN
6 PRINT@P,B$;:P=P+32+D:PRINT@P,X$;:GOSUB18:RETURN
7 POKEM+A(A),O(A):A(A)=A(A)+D(A):O(A)=C:PRINT@A(A),"A";:RETURN
8 D=1:ONK(PEEK(M+P+1))GOTO9,40,1,51,29,63,65,67,69:RETURN
9 POKEM+P,B:P=P+1:PRINT@P,Y$;:SOUND40,1:B=143:PRINT@P,X$;:RETURN
10 PRINT@T,"^";:SOUND1,1:PRINT@T,B$;:NEXT:RETURN
11 ONK(PEEK(M+A(A)+D(A)+32))GOTO13,7,37,7:GOTO7
12 PRINT@P,B$;:P=P+32:PRINT@P,X$;:GOTO21
13 D(A)=D(A)*-1:RETURN
14 FORT=P-32TO0STEP-32:ONK(PEEK(M+T))GOTO10:PRINT@T,B$;:ONK(PEEK(M+T+1))GOTO16,16:PRINT@T+1,B$;:ONK(PEEK(M+T-1))GOTO17,17
15 PRINT@T-1,B$;:RETURN
16 ONK(PEEK(M+T-1))GOTO17:GOTO15
17 T=0:NEXT:RETURN
18 ONK(PEEK(M+A(A)+32))GOTO28:C=PEEK(M+A(A)+D(A)):ONK(C)GOTO11,13,39,11,11,11:GOTO13
20 FORZ=1TOE:ONK(PEEK(2)ANDPEEK(17023))GOSUB25,2,14,8,55,57,58,59,61:A=RND(6):GOSUB18
21 ONK(PEEK(M+P+32))GOTO12:NEXT:ONZ-EGOTO35,31,42:STOP
23 POKEM+P,B:P=P-1:PRINT@P,Y$;:SOUND40,1:B=95:PRINT@P,X$;:RETURN
25 ON1-(P-32>0)GOTO30:ON1-(PEEK(M+P-32)=95)GOTO27,26:RETURN
26 POKEM+P,B:P=P-32:B=95:POKEM+P,88:RETURN
27 ON1-(B=95)GOTO4:ON1-(PEEK(M+P-32)=143)GOTO1:POKEM+P,B:P=P-32:B=143:POKEM+P,88:RETURN
28 PRINT@A(A),B$;:A(A)=A(A)+32:PRINT@A(A),"A";:GOTO18
29 POKEM+P,B:P=P+1:PRINT@P,Y$;:SOUND40,1:B=95:PRINT@P,X$;:RETURN
30 Z=E+1:PRINT@P,B$;:X$="":Y$="":RETURN
31 CLS:LV=LV+1:PRINT@256,"LEVEL";LV:FORZ=1TO4000:NEXT:CLS1:GOTO100
35 CLS:PRINT@256,"YOU TOOK TOO LONG! THEY GASSED":PRINT"YOU AND RETURNED YOU TO THE":PRINT"BOTTOM OF THE MINE!"
36 FORZ=1TO4000:NEXT:CLS1:LV=1:SC=0:GOTO100
37 ON1-(PEEK(M+A(A)+D(A)+32)=32)GOTO38,1
38 PRINT@A(A),B$;:A(A)=A(A)+D(A):PRINT@A(A),"A";:SOUND1,2:PRINT@A(A),B$;:A(A)=A(A)+32:PRINT@A(A),"A";:GOTO40
39 PRINT@A(A),B$;:A(A)=A(A)+D(A):PRINT@A(A),"A";
40 FORZ=1TO10:PRINT@P,X$;:SOUND1,1:PRINT@P,B$;:SOUND2,1:PRINT@A(A),"A";:SOUND3,1:NEXT:Z=E+2:LF=LF-1:RETURN
42 IFLF=0THEN80
43 P=OS:B=PEEK(M+P):PRINT@P,X$;:GOSUB310:GOTO20
50 GOSUB3:SOUND1,1:SOUND100,1:SC=SC+100:GOSUB70:RETURN
51 GOSUB9:SOUND1,1:SOUND100,1:SC=SC+100:GOSUB70:RETURN
52 GOSUB5:SOUND1,1:SOUND100,1:SC=SC+100:GOSUB70:RETURN
53 GOSUB6:SOUND1,1:SOUND100,1:SC=SC+100:GOSUB70:RETURN
55 ON1-(O>0)GOTO1:O=O-1:B=95:FORT=P-32TO(P-96)*ABS(P-96>0)STEP-32:ONK(PEEK(M+T))GOTO56:T=0:NEXT:GOSUB300:RETURN
56 PRINT@T,CHR$(95);:NEXT:GOSUB300:RETURN
57 ON1-(PEEK(M+P+32)=95)GOTO1:POKEM+P,B:P=P+32:B=95:POKEM+P,88:RETURN
58 D=-1:RETURN
59 D=1:RETURN
60 C2=(PEEK(17024)AND1)*256+PEEK(17025):PRINTN;:N$=STR$(N):R=LEN(N$):FORC1=M+C2TOM+C2+R:POKEC1,PEEK(C1)-64:NEXT:RETURN
61 ON1-(DR>0)GOTO1:ON1-(PEEK(M+P+32)=175)GOTO1:POKEM+P+32,143:DR=DR-1:GOSUB320:RETURN
62 ON1-(O<9)GOTO1:O=O+1:GOSUB300:GOTO3
63 ON1-(O<9)GOTO1:O=O+1:GOSUB300:GOTO9
64 ON1-(LF<9)GOTO1:LF=LF+1:GOSUB310:GOTO3
65 ON1-(LF<9)GOTO1:LF=LF+1:GOSUB310:GOTO9
66 ON1-(DR<9)GOTO1:DR=DR+1:GOSUB320:GOTO3
67 ON1-(DR<9)GOTO1:DR=DR+1:GOSUB320:GOTO9
68 SC=SC+3000:GOSUB70:PL=1:GOTO3
69 SC=SC+3000:GOSUB70:PL=1:GOTO9
70 N=SC:PRINT@486,"";:GOSUB60:RETURN
80 PRINT@493,"PLAY AGAIN? (Y/N) ";:POKEM+511,96:FORT=M+493TOM+511:POKET,PEEK(T)-64:NEXT
81 I$=INKEY$:IFI$=""THEN81
82 IFI$="Y"THENRUN
83 IFI$="N"THENEND
84 GOTO81
90 O=1:DR=2:LF=3:P=16:LV=1:PL=0
100 OS=P+448:IFOS>479THENOS=448+16
105 P=OS:IFLV=11THEN150
110 GOSUB200:GOTO20
150 CLS6:FORT=1TO25:C1=RND(11):C2=RND(6):R=RND(384):PRINT@R,LEFT$(C$,C1);:PRINT@R,MID$(C$,C2,5);
160 NEXT:GOSUB330:SC=SC+5000:GOSUB70:PRINT@P,X$;:PRINT@263,"YOU ESCAPED ALIVE!";
170 IFPL>0THENPRINT@256-64+4,"YOU SAVED THE EARTH AND";
175 PRINT@32*1+4,S1$;:PRINT@32*2+4,S2$;:PRINT@32*3+4,S2$;:PRINT@32*4+4,S1$;
180 GOTO80
200 CLS1:X$="X":Y$="Y":FORT=1TO20:R=RND(459):L=5+RND(15)
210 PRINT@R,LEFT$(L$,L);:NEXT:FORT=0TO14:R=RND(25):PRINT@T*32+R,LEFT$(L$,5+RND(5));:NEXT
220 FORT=1TO14:PRINT@T*32-1,CHR$(191);CHR$(191);:NEXT:POKEM+479,191:POKEM,191:GOSUB330
240 FORT=1TO6
250 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO250:A(T)=R:D(T)=1:O(T)=143:PRINT@R,"A";:NEXT
260 FORT=1TO10
265 IFLV=4ORLV=5ORLV=6THENR=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO272:PRINT@R,"o";:GOTO280
270 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO270:PRINT@R,"*";:NEXT
271 ONRND(3)GOTO272,273,274
272 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO272:PRINT@R,CHR$(127);:GOTO280
273 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO273:POKEM+R,43:GOTO280
274 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO274:PRINT@R,"V";
280 B=143:PRINT@P,"X";
290 GOSUB300:GOSUB310:GOSUB320:RETURN
300 N=O:PRINT@508,"";:GOSUB60:RETURN
310 N=LF:PRINT@497,"";:GOSUB60:RETURN
320 N=DR:PRINT@503,"";:GOSUB60:RETURN
330 PRINT@480,"SCORE:       MEN:    D:   G:   ";:FORC1=M+480TOM+511:POKEC1,PEEK(C1)-64:NEXT:POKEM+511,32:RETURN
2000 CLS
2010 PRINT"               * RARE ELEMENT"
2020 PRINT"               o SECRET PLANS"
2030 PRINT"                 FOR D-DEVICE"
2040 PRINT"               "CHR$(127)" LADDER GUN"
2050 PRINT"               "CHR$(95)" SPIKE LADDER"
2060 PRINT"               + FIRST AID KIT":POKEM+175,43
2070 PRINT"               V DRILL"
2080 PRINT
2081 PRINT@0*32,"èèèèèøøøøøøèèè";
2082 PRINT@1*32,"èèèøøøøøøøøøøè";
2083 PRINT@2*32,"èèøøøøøøøøøøøø";
2084 PRINT@3*32,"èèøøøèèøøèèøøø";
2085 PRINT@4*32,"èèøøøøøøøøøøøø";
2086 PRINT@5*32,"èèèèèøøèèøøèèè";
2087 PRINT@6*32,"èèèèøøèèèèøøèè";
2088 PRINT@7*32,"èèøøèèèøøèèèøø"
2110 PRINT"YOU HAVE BEEN IMPRISONED AT THE"
2120 PRINT"BOTTOM OF A MINE BY AN INVADING"
2130 PRINT"ALIEN RACE.  THEY ARE MINING A"
2140 PRINT"RARE ELEMENT NEEDED FOR THEIR"
2150 PRINT"DOOMSDAY DEVICE. YOU HAVE NOW"
2160 PRINT"OVERWHELMED YOUR CAPTORS. GET"
2170 PRINT"TO THE SURFACE AND WARN EARTH!"
3000 K(87)=1:K(65)=2:K(32)=3:K(83)=4:K(71)=5:K(90)=6:K(8)=7:K(9)=8:K(68)=9:K(143)=1:K(191)=2:K(88)=3:K(106)=4:K(95)=5
3005 K(31)=6:K(43)=7:K(86)=8:K(79)=9:M=16384:E=1000
3010 FORT=1TO31:L$=L$+CHR$(175):NEXT:B$=CHR$(143):C$="ﬂﬂœœœœœœœﬂﬂ":X$="X":Y$="Y"
3015 S1$=CHR$(223)+CHR$(159)+CHR$(159)+CHR$(223):S2$=CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)
4000 PRINT"      HIT ANY KEY FOR HELP";
4010 ON1-(INKEY$="")GOTO4020,4010
4020 CLS:PRINTTAB(10)"tunnel jumper"
4030 PRINT"A=MOVE LEFT   CTRL-A=FACE LEFT"
4040 PRINT"S=MOVE RIGHT  CTRL-S=FACE RIGHT"
4045 PRINT"W=JUMP OR CLIMB UP"
4050 PRINT"Z=CLIMB DOWN"
4060 PRINT"G=FIRE SPIKE LADDER GUN"
4070 PRINT"D=DRILL DOWN"
4080 PRINT:PRINT"RARE ELEMENT =  100 POINTS"
4085 PRINT"SECRET PLANS = 3000 POINTS"
4090 PRINT:PRINT"ALIENS 'A' CAN GET YOU FROM"
4095 PRINT"ABOVE, BUT YOU CAN WALK ON TOP"
4096 PRINT"OF THEM. AVOID THE RED WALLS.":PRINT"THERE ARE 10 LEVELS TO CLIMB!"
4099 PRINT"      HIT ANY KEY TO BEGIN";
5010 ON1-(INKEY$="")GOTO90,5010
