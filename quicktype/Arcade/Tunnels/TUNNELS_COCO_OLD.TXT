0 GOSUB10000:DIMK(255),A(6),O(6),T,M,A,C,K,R:M=1024:GOTO2000
1 RETURN
2 D=-1:K=K(PEEK(M+P-1)):ONK GOTO3,1,50,50,24,50,50,50,50:STOP
3 POKEM+P,B:P=P-1:PRINT@P,Y$;:SOUND40,1:B=143:PRINT@P,X$;:RETURN
5 POKEM+P,B:P=P-32+D:PRINT@P,X$;:B=143:GOSUB18:RETURN
6 POKEM+P,B:P=P+32+D:PRINT@P,X$;:B=143:GOSUB18:RETURN
7 POKEM+A(A),O(A):A(A)=A(A)+D(A):O(A)=C:PRINT@A(A),"A";:RETURN
8 D=1:K=K(PEEK(M+P+1)):ONK GOTO9,1,51,51,29,51,51,51,51:STOP
9 POKEM+P,B:P=P+1:PRINT@P,Y$;:SOUND40,1:B=143:PRINT@P,X$;:RETURN
10 PRINT@T,"^";:SOUND10,1:PRINT@T,B$;:NEXT:RETURN
11 ONK(PEEK(M+A(A)+D(A)+32))GOTO13,7,37,7:GOTO7
12 PRINT@P,B$;:P=P+32:PRINT@P,X$;:GOTO21
13 D(A)=D(A)*-1:RETURN
14 ON1-(P>31)GOTO1:FORT=P-32TO0STEP-32:ONK(PEEK(M+T))GOTO10:SOUND1,1:PRINT@T,B$;:ON1-(PEEK(M+T-1)<>191)GOTO15:PRINT@T-1,B$;
15 ON1-(PEEK(M+T+1)<>191)GOTO1:PRINT@T+1,B$;:RETURN
17 POKE340,0:POKE341,0:POKE342,0:POKE343,0:POKE344,0:POKE345,0:RETURN
18 ONK(PEEK(M+A(A)+32))GOTO28:C=PEEK(M+A(A)+D(A)):ONK(C)GOTO11,13,39,11,11,11,11,11,11:GOTO13
19 ONK(PEEK(135))GOSUB25,2,14,8,54,57,58,59,61,33,55:RETURN
20 FORZ=1TOE:GOSUB17:ON1-((PEEK(340)ANDPEEK(341)ANDPEEK(342)ANDPEEK(343)ANDPEEK(344)ANDPEEK(345))=255)GOSUB19:A=RND(6):GOSUB18
21 ONK(PEEK(M+P+32))GOTO12:NEXT:ONZ-E GOTO33,31,42:STOP
22 FORT=1TO2:ON1-(P-32>0)GOTO30:K=K(PEEK(M+P-32+D)):ONK GOSUB5,1,1,52,45,52,52,52,52:NEXT
23 FORT=1TO2:K=K(PEEK(M+P+32+D)):ONK GOSUB6,1,1,53,46,53,53,53,53:NEXT:RETURN
24 POKEM+P,B:P=P-1:PRINT@P,Y$;:SOUND40,1:B=95:PRINT@P,X$;:RETURN
25 ON1-(P-32>0)GOTO30:ON1-(PEEK(M+P-32)=95)GOTO27,26:RETURN
26 POKEM+P,B:P=P-32:B=95:POKEM+P,88:RETURN
27 ON1-(B=95)GOTO22:K=K(PEEK(M+P-32)):ONK GOTO47,1,47,47,47,47,47,47,47:RETURN
28 PRINT@A(A),B$;:A(A)=A(A)+32:PRINT@A(A),"A";:GOTO18
29 POKEM+P,B:P=P+1:PRINT@P,Y$;:SOUND40,1:B=95:PRINT@P,X$;:RETURN
30 Z=E+1:PRINT@P,B$;:X$="":Y$="":RETURN
31 CLS:LV=LV+1:PRINT@269,"LEVEL";LV:FORZ=1TO4000:NEXT:CLS1:GOTO100
33 CLS:PRINT@224,"YOU TOOK SO LONG THAT THEY WERE":PRINT"ABLE TO RELEASE TOXIC GAS, WHICHFORCED YOU TO RETREAT A LEVEL!"
34 FORZ=1TO5000:NEXT:CLS1:LV=LV-1:IFLV<1THENLV=1
35 GOTO100
37 ON1-(PEEK(M+A(A)+D(A)+32)=32)GOTO38,1
38 PRINT@A(A),B$;:A(A)=A(A)+D(A):PRINT@A(A),"A";:SOUND1,2:PRINT@A(A),B$;:A(A)=A(A)+32:PRINT@A(A),"A";:GOTO40
39 PRINT@A(A),B$;:A(A)=A(A)+D(A):PRINT@A(A),"A";
40 FORZ=1TO10:PRINT@P,X$;:SOUND1,1:PRINT@P,B$;:SOUND2,1:PRINT@A(A),"A";:SOUND3,1:NEXT:Z=E+2:LF=LF-1:X$="":Y$="":RETURN
41 POKEM+P,B:P=P+32:B=143:POKEM+P,88:ONK GOSUB1,1,1,69,1,62,64,66,68:RETURN
42 IFLF=0THEN80
43 X$="X":Y$="Y":P=OS:B=PEEK(M+P):PRINT@P,X$;:GOSUB310:GOTO20
44 POKEM+P,B:P=P+32:B=95:POKEM+P,88:RETURN
45 POKEM+P,B:P=P-32+D:PRINT@P,X$;:B=95:GOSUB18:RETURN
46 POKEM+P,B:P=P+32+D:PRINT@P,X$;:B=95:GOSUB18:RETURN
47 POKEM+P,B:P=P-32:B=143:POKEM+P,88:ONK GOSUB1,1,1,69,1,62,64,66,68:RETURN
50 GOSUB3:ONK GOSUB1,40,1,69,1,62,64,66,68:RETURN
51 GOSUB9:ONK GOSUB1,40,1,69,1,62,64,66,68:RETURN
52 GOSUB5:ONK GOSUB1,1,1,69,1,62,64,66,68:RETURN
53 GOSUB6:ONK GOSUB1,1,1,69,1,62,64,66,68:RETURN
54 D=0:SOUND15,1:RETURN
55 ON1-(O>0)GOTO1:O=O-1:B=95:FORT=P-32TO(P-96)*ABS(P-96>0)STEP-32:ONK(PEEK(M+T))GOTO56:T=0:NEXT:GOSUB300:RETURN
56 PRINT@T,CHR$(95);:NEXT:GOSUB300:RETURN
57 K=K(PEEK(M+P+32)):ONK GOTO1,1,1,41,44,41,41,41,41:RETURN
58 D=-1:SOUND15,1:RETURN
59 D=1:SOUND15,1:RETURN
60 C2=(PEEK(136)AND1)*256+PEEK(137):PRINTN;:N$=STR$(N):R=LEN(N$):FORC1=M+C2 TOM+C2+R:POKEC1,PEEK(C1)-64:NEXT:RETURN
61 ON1-(DR>0)GOTO1:ON1-(PEEK(M+P+32)=175)GOTO1:POKEM+P+32,143:DR=DR-1:GOSUB320:RETURN
62 ON1-(O<9)GOTO1:SOUND120,1:O=O+1:GOSUB300:RETURN
64 ON1-(LF<3)GOTO1:SOUND140,1:LF=LF+1:GOSUB310:RETURN
66 ON1-(DR<9)GOTO1:SOUND160,1:DR=DR+1:GOSUB320:RETURN
68 SOUND1,5:SOUND100,5:SC=SC+3000:GOSUB70:PL=1:RETURN
69 SOUND1,1:SOUND100,1:SC=SC+100:GOSUB70:RETURN
70 N=SC:PRINT@486,"";:GOSUB60:RETURN
80 PRINT@493,"PLAY AGAIN? (Y/N) ";:POKEM+511,96:FORT=M+493TOM+511:POKET,PEEK(T)-64:NEXT
81 I$=INKEY$:IFI$=""THEN81
82 IFI$="Y"THEN85
83 IFI$="N"THENGOSUB10003:END
84 GOTO81
85 IFSC>HS THENHS=SC
86 GOSUB5020:PRINT@267-32,"LAST:";SC:PRINT@267+32,"HIGH:";HS:PRINT@480,"";:GOTO4099
90 O=1:DR=2:LF=3:P=16:LV=1:PL=0:SC=0
100 OS=P+448:IFOS>479THENOS=448+16
105 P=OS:IFLV=10THEN150
110 GOSUB200:GOTO20
150 CLS6:FORT=1TO25:C1=RND(11):C2=RND(6):R=RND(384):PRINT@R,LEFT$(C$,C1);:PRINT@R,MID$(C$,C2,5);
160 NEXT:GOSUB330:SC=SC+5000:GOSUB70:PRINT@P,X$;:PRINT@263,"YOU ESCAPED ALIVE!";
170 IFPL>0THENPRINT@196,"YOU SAVED THE EARTH AND";
175 PRINT@32*1+4,S1$;:PRINT@32*2+4,S2$;:PRINT@32*3+4,S2$;:PRINT@32*4+4,S1$;
180 GOTO80
200 CLS1:X$="X":Y$="Y":FORT=1TO20:R=RND(459):L=5+RND(15)
210 PRINT@R,LEFT$(L$,L);:NEXT:FORT=0TO14:R=RND(25):PRINT@T*32+R,LEFT$(L$,5+RND(5));:NEXT
220 FORT=1TO14:PRINT@T*32-1,CHR$(191);CHR$(191);:NEXT:POKEM+479,191:POKEM,191:GOSUB330
240 FORT=1TO6
250 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO250:A(T)=R:D(T)=1:O(T)=143:PRINT@R,"A";:NEXT
255 IFLV=4 OR LV=5 OR LV=6THENR=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO255:PRINT@R,"o";
260 FORT=1TO10
270 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO270:PRINT@R,"*";:NEXT
271 ONRND(3)GOTO272,273,274
272 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO272:PRINT@R,CHR$(127);:GOTO280
273 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO273:POKEM+R,43:GOTO280
274 R=RND(448):ON1-(PEEK(M+R)=143ANDPEEK(M+R+32)=175)GOTO274:PRINT@R,"V";
280 B=143:PRINT@P,"X";
290 GOSUB70:GOSUB300:GOSUB310:GOSUB320:RETURN
300 N=O:PRINT@508,"";:GOSUB60:RETURN
310 N=LF:PRINT@498,"";:GOSUB60:RETURN
320 N=DR:PRINT@503,"";:GOSUB60:RETURN
330 PRINT@480,"SCORE:          X:   D:   G:   ";:FORC1=M+480TOM+511:POKEC1,PEEK(C1)-64:NEXT:POKEM+511,32:RETURN
2000 CLS
2010 PRINT"               * RARE ELEMENT"
2020 PRINT"               o SECRET PLANS"
2030 PRINT"                 FOR D-DEVICE"
2040 PRINT"               "CHR$(127)" LADDER GUN"
2050 PRINT"               "CHR$(95)" SPIKE LADDER"
2060 PRINT"               + FIRST AID KIT":POKEM+175,43
2070 PRINT"               V DRILL"
2080 PRINT
2081 PRINT@0*32,CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(143);
2082 PRINT@1*32,CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(143);
2083 PRINT@2*32,CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191);
2084 PRINT@3*32,CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191);
2085 PRINT@4*32,CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191);
2086 PRINT@5*32,CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(143);
2087 PRINT@6*32,CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143);
2088 PRINT@7*32,CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)CHR$(143)CHR$(143)CHR$(143)CHR$(191)CHR$(191)
2110 PRINT"YOU HAVE BEEN IMPRISONED AT THE"
2120 PRINT"BOTTOM OF A MINE BY AN INVADING"
2130 PRINT"ALIEN RACE.  THEY ARE MINING A"
2140 PRINT"RARE ELEMENT NEEDED FOR THEIR"
2150 PRINT"DOOMSDAY DEVICE. YOU HAVE NOW"
2160 PRINT"OVERWHELMED YOUR CAPTORS. GET"
2170 PRINT"TO THE SURFACE AND WARN EARTH!"
3000 K(94)=1:K(8)=2:K(32)=3:K(9)=4:K(95)=5:K(10)=6:K(21)=7:K(93)=8:K(68)=9:K(82)=10:K(71)=11
3005 K(143)=1:K(65)=2:K(191)=2:K(175)=2:K(88)=3:K(106)=4:K(95)=5:K(31)=6:K(43)=7:K(86)=8:K(15)=9:E=1650
3010 FORT=1TO31:L$=L$+CHR$(175):NEXT:B$=CHR$(143):C$=CHR$(223)+CHR$(223)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(223)+CHR$(223):X$="X":Y$="Y"
3015 S1$=CHR$(223)+CHR$(159)+CHR$(159)+CHR$(223):S2$=CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)
4000 PRINT"      HIT ANY KEY FOR HELP";
4010 T=RND(1000):ON1-(INKEY$="")GOTO4020,4010
4020 GOSUB5020
4030 PRINT"LEFT ARROW    CTRL-A=FACE LEFT"
4040 PRINT"RIGHT ARROW   CTRL-S=FACE RIGHT"
4045 PRINT"UP=JUMP OR CLIMB  R=RESIGN"
4050 PRINT"DOWN=CLIMB DOWN  D=DRILL DOWN"
4060 PRINT"G=FIRE SPIKE LADDER GUN UP"
4080 PRINT:PRINT"RARE ELEMENT =  100 POINTS"
4085 PRINT"SECRET PLANS = 3000 POINTS"
4090 PRINT:PRINT"ALIENS ('A') CAN GET YOU FROM"
4095 PRINT"ABOVE, BUT YOU CAN WALK ON TOP"
4096 PRINT"OF THEM. THERE ARE 9 LEVELS TO":PRINT"CLIMB TO GET TO THE SURFACE."
4099 PRINT"      HIT ANY KEY TO BEGIN";
5010 ON1-(INKEY$="")GOTO90,5010
5020 CLS:PRINTTAB(10)"tunnel jumper"
5030 PRINTTAB(7)"for cococoding 2013":FORT=M+54TOM+57:POKET,PEEK(T)-64:NEXT:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
