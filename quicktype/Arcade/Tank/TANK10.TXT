0 CLS:GOTO8000
1 C=-1:Q=Q-1:IFQ<1THENQ=8:C=7
2 D=Q:P=1:GOSUB50:Q=Q-C:C=0:RETURN
3 C=-1:Q=Q+1:IFQ>8THENQ=1:C=7
4 D=Q:P=1:GOSUB50:Q=Q+C:C=0:RETURN
5 IFM1<1THENRETURN
6 M1=M1-1:D=Q:P=1:V=143:GOSUB50:V=96:RETURN
10 C=-1:R=R-1:IFR<1THENR=8:C=7
11 D=R:P=2:GOSUB50:R=R-C:C=.:RETURN
12 C=-1:R=R+1:IFR>8THENR=1:C=7
13 D=R:P=2:GOSUB50:R=R+C:C=.:RETURN
14 IFM2<1THENRETURN
15 M2=M2-1:D=R:P=2:V=143:GOSUB50:V=96:RETURN
17 IFS(1)=.THENIFS1>.THENS1=S1-1:L(1)=B(1):S(1)=1:V(1)=R(1):PRINT@6,;:M$=STR$(S1):GOSUB199
18 ONS1GOSUB260,260:RETURN
19 IFS(2)=.THENIFS2>.THENS2=S2-1:L(2)=B(2):S(2)=1:V(2)=R(2):PRINT@23,;:M$=STR$(S2):GOSUB199
20 ONS2GOSUB265,265:RETURN
21 N=PEEK(L(P)+V(P)):IFN=VTHENL(P)=L(P)+V(P):POKEL(P),S:S(P)=2:RETURN
22 GOTO25
23 N=PEEK(L(P)+V(P)):IFN<>VTHENPOKEL(P),V:GOTO25
24 POKEL(P),V:L(P)=L(P)+V(P):POKEL(P),S:RETURN
25 IFN=T(1)ORN=T(2)ORN=D(1,2)ORN=D(2,2)ORN=D(3,2)ORN=D(4,2)THENONPGOSUB40,45:GOTO29
26 IFN>113THENL(P)=L(P)+V(P):POKEL(P),V:X=PEEK(9)AND128:POKEF,0+X:POKEF,128-X
28 IFN=STHENRETURN
29 S(P)=.:IFS1=.THEN45
30 IFS2=.THEN40
31 RETURN
40 NT(2)=NT(2)-1:FORX=.TO7:POKEP(2),143+16*X:POKEB(2),126:SOUND226,1:NEXT
41 P(2)=M+443:M$=STR$(NT(2)):PRINT@18,;:GOSUB199
42 IFPEEK(P(2))<>96THENP(2)=P(2)-32:ON-(P(2)>M+32)GOTO42:P(2)=M+443
43 IFNT(2)>.THENB(2)=P(2)-1:R(2)=D(1,1):POKEP(2),T(2):POKEP(2)+D(1,1),D(1,2):R=1:C=.:S2=4+INT(SL/2+.5):M2=INT(SL/3+.5)
44 PRINT@23,;:M$=STR$(S2):GOSUB199:PRINT@28,;:M$=STR$(M2):GOSUB199:GOSUB210:RETURN
45 NT(1)=NT(1)-1:FORX=0TO7:POKEP(1),143+16*X:POKEB(1),126:SOUND226,1:NEXT
46 P(1)=M+68:M$=STR$(NT(1)):PRINT@1,;:GOSUB199
47 IFPEEK(P(1))<>96THENP(1)=P(1)+32:ON-(P(1)<M+480)GOTO47:P(1)=M+68
48 IFNT(1)>.THENB(1)=P(1)+1:R(1)=D(5,1):POKEP(1),T(1):POKEP(1)+D(5,1),D(5,2):Q=5:C=0:S1=4+INT(SL/2+.5):M1=INT(SL/3+.5)
49 PRINT@6,;:M$=STR$(S1):GOSUB199:PRINT@11,;:M$=STR$(M1):GOSUB199:GOSUB210:RETURN
50 X=PEEK(P(P)+D(D,1)):IFX=D(D,2)THEN60
52 IFX=96THENC=0:POKEB(P),V:B(P)=P(P)+D(D,1):POKEB(P),D(D,2):R(P)=D(D,1):RETURN
53 IFX=143THENC=0:POKEB(P),V:B(P)=P(P)+D(D,1):POKEB(P),D(D,2):R(P)=D(D,1):ONPGOSUB201,202:ONPGOTO45,40
54 RETURN
60 C=.:X=PEEK(P(P)+(D(D,1)*2))
65 IFX=96THENPOKEP(P),V:P(P)=P(P)+D(D,1):POKEP(P),T(P):B(P)=P(P)+D(D,1):POKEB(P),D(D,2):RETURN
70 IFX=143THENPOKEP(P),V:P(P)=P(P)+D(D,1):POKEP(P),T(P):B(P)=P(P)+D(D,1):POKEB(P),D(D,2):ONPGOSUB201,202:ONPGOTO45,40
80 RETURN
90 FORL=-1TO.:A=.:I$=INKEY$:IFI$<>""THENA=ASC(I$)
91 ONK(A)GOSUB1,3,4,17,19,10,12,13,5,14:P=1:ONS(P)GOSUB21,23:P=2:ONS(P)GOSUB21,23:L=(NT(1)>.ANDNT(2)>.):NEXT
92 IFS(2)>.THENP=2:FORL=1TO30:ONS(P)GOSUB21,23:NEXT
93 IFS(1)>.THENP=1:FORL=1TO30:ONS(P)GOSUB21,23:NEXT
94 IFNT(1)>NT(2)THENM$="* MONTY WON!":P1=P1+1
95 IFNT(2)>NT(1)THENM$="# ROMMEL WON!":P2=P2+1
96 IFNT(1)=NT(2)THENM$="IT WAS A DRAW!"
97 M$=M$+" PLAY AGAIN Y/N?":PRINT@480,;:GOSUB199
99 I$=INKEY$:C=RND(0):IFI$=""THEN99
100 IFI$="Y"THENCLS:GOTO8020
101 IFI$="N"THENCLS:PRINT"* VICTORIES =";P1:PRINT"# VICTORIES =";P2:END
102 GOTO99
199 PRINTM$;:Y=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORX=Y-LEN(M$)+1TOY:POKEX,PEEK(X)-64:NEXT:RETURN
201 PRINT@11,;:M$=STR$(M1):GOSUB199:PRINT@28,;:M$=STR$(M2):GOSUB199:PRINT@480,;:M$="*":GOTO205
202 PRINT@11,;:M$=STR$(M1):GOSUB199:PRINT@28,;:M$=STR$(M2):GOSUB199:PRINT@496,;:M$="#":GOTO205
205 M$=M$+" HIT A MINE!":GOSUB199:FORX=1TO19:SOUND226,1:NEXT
210 FORX=0TO31:POKEM+480+X,32:NEXT:RETURN
260 PRINT@480,;:GOTO270
265 PRINT@496,;
270 SOUND1,1:M$="LOW SHOTS":GOSUB199:RETURN
8000 DIMD(8,4),P(2),B(2),T(2),L(2),V(2),R(2),K(255),P,D,A,X,Y,T,N,C,Q,L,M,S,V,B,NT(2),F,S1,S2,M1,M2,SL,P1,P2
8005 GOSUB8500:P1=0:P2=0:S=103:V=96:B=134:F=49151:FORY=1TO8:FORX=1TO2:READD(Y,X):NEXT:NEXT
8010 M=16384:X=1
8015 K(65)=1:K(68)=2:K(87)=3:K(32)=4:K(50)=4:K(83)=9
8016 K(75)=6:K(59)=7:K(79)=8:K(13)=5:K(57)=5:K(76)=10
8020 FORX=1TO20+(SL*2):Y=RND(444):PRINT@Y+1,"†";:PRINT@Y+32,"†††";:PRINT@Y+65,"†";:NEXT
8030 P=0:D=0:A=0:X=0:Y=0:T=0:N=0:C=0:L=0:S(1)=0:S(2)=0
8040 PRINT@0,;:M$="T    S    M      T    S    M    ":GOSUB199
8090 NT(1)=3:NT(2)=3
8100 T(1)=42:GOSUB46:T(2)=35:GOSUB41
8230 GOTO90
8500 PRINT" TANK! CONCEPT BY SHAWN MEEHAN "
8505 PRINT"               & MIKE ROWLEY   "
8506 PRINT"       MC-10 VERSION J. GERRIE "      
8507 PRINT" ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ "
8508 PRINT" MONTY VS. THE DESERT FOX      "
8509 PRINT
8510 PRINT" USE THE FOLLOW KEYS TO MOVE:  "
8511 PRINT
8520 PRINT"       left tank    right tank "
8525 PRINT
8525 PRINT" STEER =    W              O   "
8530 PRINT"          A   D          K   ; "
8540 PRINT" LAY MINE = S              L   "
8542 PRINT" FIRE = SPACE OR 2   ENTER OR 9"
8550 PRINT
8555 PRINT" PRESS enter TO BEGIN ";
8560 I$=INKEY$:C=RND(0):IFI$<>CHR$(13)THEN8560
8600 CLS:PRINT@32*7,"10 IS THE HARDEST"
8610 PRINT@32*6,"WHAT SKILL LEVEL (1-10)";:INPUTSL:IFSL<1ORSL>10THEN8610
8690 CLS:RETURN
9000 DATA -1,109
9010 DATA -33,92
9020 DATA -32,97
9030 DATA -31,111
9040 DATA 1,109
9050 DATA 33,92
9060 DATA 32,97
9070 DATA 31,111
