0 CLS:PRINT"USE a & s KEYS":GOTO740
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORCC=1TOLEN(M$):POKEC1+CC,ASC(MID$(M$,CC))AND63:NEXT:?@C1-M+CC,:RETURN
9 FORC1=1TOLEN(M$):ONASC(MID$(M$,C1,1))-64GOSUB10,11,12,13,14,15,16:NEXT:RETURN
10 SOUND171,2:RETURN
11 SOUND181,2:RETURN
12 SOUND185,2:RETURN
13 SOUND189,2:RETURN
14 SOUND200,2:RETURN
15 SOUND203,2:RETURN
16 SOUND209,2:RETURN
17 FORCC=1TO25:NEXT:RETURN
20 REM *** START SCREEN ***
30 X=11:B=.:H=.:S=.:R=1:D=.
40 G=0:C=.:IFOTHEN60
50 FORI=0TO98:R(I)=RND(0)*19+1:NEXT
60 R$=R$(R-1):FORI=0TO15:PRINT@22+15*32,R$;:NEXT
70 O=0:F=0:L=R*.8+H/15+D/10
80 PRINT@5+5*32,"R O U N D "R;
90 M$="CBCBCBCBDADADADAEGEGEGEGDADADADA":GOSUB9
100 FORI=0TO700:NEXT
110 PRINT@5+5*32,"            ";
120 REM *** MAIN LOOP ***
130 FORZ=0TO199
140 IFZ>184THENFORI=0TO75:NEXT:GOTO180
150 T=T+3-T(T):PRINT@R(T)+(Y-B*4)*32,D$(L);
160 CC=R(T+1)+Y*32
161 IFPEEK(CC+M-32)=5ORPEEK(CC+M-31)=5ORPEEK(CC+M-30)=5ORPEEK(CC+M)=5ORPEEK(CC+M+1)=5ORPEEK(CC+M+2)=5THEN170
165 PRINT@CC,;:C1=INT(RND(1)*L+B):PRINTA$(C1);:IFC1>2THENPRINT@CC-32,A$(26);
170 CC=R(T+2)+1+Y*32:F=F+V:PRINT@CC,C$(FAND3);:IFPEEK(CC+M)=5ANDPEEK(CC+M-32)<>111ANDPEEK(CC+M-32)<>92THENPRINT@CC-32," ";
180 PRINT@502,R$;:SOUND100+20*B,1:IFPEEK(2)THENIF1ANDNOTPEEK(16946)THENIFX>1THENX=X-1
200 IFPEEK(2)THENIF4ANDNOTPEEK(16948)THENIFX<21THENX=X+1
210 P=PEEK(M+X):PRINT@X,B$(B);:IFP=96THEN310
220 IFP-5THEN260
230 REM *** FUEL HIT ***
240 L=L+.04:G=G+1:SOUND185,1:SOUND200,1:B=B+.25+(B>2.5)*.25:GOTO310
250 REM *** BONUS HIT ***
260 IFP>32ANDP<128THEN290
270 B=2.75:S=S+500:PRINT@23+14*32,"500 PTS.";
280 M$="ABA":GOSUB9:GOTO310
290 B=B-1:C=C+1:L=L-.2:SOUND171,1:SOUND181,1:IFB<0THEN330
300 IFG=0ANDC=2THENPRINT@X+13*32,"dp";
310 L=L+U:NEXT:GOTO470
320 REM *** GAME OVER ***
330 Q=12:FORI=1TO5:FORJ=0TO8
340 A=X+X(J)*I:IFA>.THENIFA<23THENPRINT@A+INT(Y(J)*I)*32,"+";
350 Q=Q-.25:SOUND255,1
360 NEXT:NEXT:S=S+Z+G*10:O=1
370 PRINT@3+3*32,;:M$="G A M E  O V E R":GOSUB7
380 M$="SCORE"+STR$(S):PRINT@6*32+((23-LEN(M$))*.5),;:GOSUB7
390 IFHS<STHENHS=S
400 M$="HIGH SCORE"+STR$(HS):PRINT@8*32+((23-LEN(M$))*.5),;:GOSUB7
410 PRINT@4+11*32,;:M$="PRESS SPACE KEY":GOSUB7
420 I$=INKEY$:IFI$=" "THEN30
430 IFI$<>CHR$(13)ORR>10THEN420
440 B=INT(L/1.25)/4:IFB=>3THENB=2.75
450 X=10:S=0:GOTO40
460 REM *** ROUND COMPLETE ***
470 H=H+G:D=D+5-C
480 FORI=0TO2:M$="CBAGFGAB":GOSUB9:NEXT:M$="CHHC":GOSUB9
490 M$="ROUND"+STR$(R)+" CLEAR":PRINT@3*32+((23-LEN(M$))*.5),;:GOSUB7
500 LB=H*10+INT(L*50)
510 M$="LEVEL BONUS"+STR$(LB):PRINT@6*32+((23-LEN(M$))*.5),;:GOSUB7
520 IFG>14THENLB=LB+LB:PRINT@24+6*32,;:M$="X2":GOSUB7
530 CB=300-C*50:IFC>5THENCB=500*(C-4)
540 M$="CLASH BONUS"+STR$(CB):PRINT@8*32+((23-LEN(M$))*.5),;:GOSUB7
550 S=S+LB+CB+Z
560 M$="SCORE"+STR$(S):PRINT@10*32+((23-LEN(M$))*.5),;:GOSUB7:FORC1=1TO1500:NEXT
570 R=R+1:IFR<11THEN40
580 REM *** WIN ***
590 FORI=0TO1500:NEXT
600 M$="CDG":FORI=0TO15
610 GOSUB9:READA$
620 PRINT@1+I*32,"";
630 PRINT@2+I*32,A$;:NEXT
640 S=S+B*4000+L*1000:Z=10000
650 IFHS<STHENHS=INT(S)
660 PRINT@13+12*32,;:M$=STR$(HS)+" ":GOSUB7
720 GOTO420
730 REM *** INITIALIZE ***
740 CLEAR1000:POKE16925,0:POKE16926,1
750 DIMC1,CC,M$,M,D$(25),A$(27),R$(9),R(98),T(96):M=16384:C1=RND(-(PEEK(9)*256+PEEK(10)))
760 B=0:L=0:T=0:X=0:P=0:Y=0
770 A$(0)="*":A$(1)="TT":A$(2)="XXX"
780 FORI=3TO25:A$(I)=CHR$(92)+"-/":NEXT:A$(26)="/-"+CHR$(92):A$(27)="/e"+CHR$(92)
790 FORI=0TO4:READA:X(I)=-A:X(8-I)=A:Y(4-I)=A:Y(4+I)=A:NEXT
800 DATA1,.4,.7,.9,0
810 FORI=5TO13:READD$(I):NEXT
820 DATA#,@,# #,@ @,##,@@,###,@@@,####
830 FORI=14TO25:D$(I)="@@@@":NEXT:C$(1)="e"
840 FORI=0TO2:READB$(I):NEXT
850 DATAY,V,W
860 FORI=0TO9:READA$,B$:R$(I)=A$+CHR$(13)+B$+"                     "+A$:NEXT
861 I=8:A$=CHR$(91):B$=CHR$(93):R$(I)=A$+CHR$(13)+B$+"                     "+A$
862 I=9:A$=CHR$(93):B$=CHR$(91):R$(I)=A$+CHR$(13)+B$+"                     "+A$
870 DATA I,I,^,^,=,=,O,O,(,),H,H,<,>,),(,),(,),(
880 V(3)=1:V(7)=-1:U=.01:V=2.4:Y=14
900 T(94)=97:T(95)=97:T(96)=97
910 CLS:GOTO30
920 DATA ""
921 DATA ""
925 DATA "YOU "
930 DATA "MANAGED"
940 DATA "TO ESCAPE"
950 DATA "THE DEADLY"
960 DATA "ASTEROID"
970 DATA "FIELD!"
975 DATA ""
976 DATA ""
980 DATA,,"HIGH SCORE"
990 DATA,"GAME OVER",
1050 REM <<<((((((III))))))>>>
1060 REM <  ASTEROID ESCAPE  >
1080 REM <   '86 12/29    I  >
1090 REM <<<((((((III))))))>>>
2000 REM http://p6ers.net/kaw/
2010 REM MC-10 EDITS J. GERRIE
