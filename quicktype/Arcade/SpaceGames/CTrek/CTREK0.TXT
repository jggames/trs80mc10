0 CLEAR50:YG=11
1 Y$(1)=CHR$(159):Y$(2)=CHR$(255):Y$(3)=CHR$(207):Y$(4)=CHR$(191)
20 CLS1:P=14:E=10000:D=36:W5=1
21 DIMB(D),S1(D),S2(D),H(D,7)
25 INPUT"NEW GAME";U$:IF LEFT$(U$,1)="Y"THEN30ELSE7040
30 CLS:INPUT"GAME#(1-10)";IU:IFIU<1ORIU>10ORIU<>INT(IU)THEN30
40 GOSUB50:GOTO60
50 YG=YG-1:PRINT@96,"WORKING";YG:SOUNDYG*24,1:RETURN
60 FORI=1TOD:RR=7-RND(3):C=0
65 IFI/4=INT(I/4)THENGOSUB50
70 FORS=68TO260STEP32:C=C+1
80 V=RND(10):IFV<4ANDRR>0THENRR=RR-1:NEXTS,I:GOTO100
85 S1(I)=S1(I)+1
90 R(C)=RND(21):H(I,C)=S+R(C):NEXTS,I
95 X8=X:Y8=Y:W8=W
100 FORI=1TO1+RND(2):B1(I)=RND(D):IV=1:X=B1(I):GOSUB2140:X7(I)=W:Y7(I)=Y:NEXT:IV=0
101 X=X8:Y=Y8:W=W8
120 FORI=1TO6:READT$(I):NEXT
130 FORI=1TO9:READD$(I):NEXT
133 PRINT@104,"0":SOUND1,5
135 IFLEFT$(U$,1)="N"THEN177
150 X=1:Y=1:W=1:Z=0
170 FORA=2TO36:B(A)=RND(4)-1:IFIU>6THENB(A)=RND(3)
171 Z=Z+B(A):NEXT:ZZ=Z:B1(1)=0
172 IFZ<40+(3*IU)THENZ=0:ZZ=0:GOTO170
175 SD=38+INT(Z/4):SD=SD-(4*IU):A=1:SX=SD
177 CLS1
180 EL=RND(21)+4:FORI=1TO2+RND(7):EL=EL+32:NEXTI:GOSUB190:GOTO210
190 C1=4+INT(EL/32)*32:C2=EL-C1:RETURN
210 GOSUB570:GOSUB580:GOSUB590
270 IFW5=1THENW5=0:K1=0:M1=0:N1=0:GOTO300
280 GOTO340
300 L1=0:B2=0:B3=0
310 IFB1(1)=A ORB1(2)=A ORB1(3)=A THENB2=1
320 FORI=1TO23:RESET(7,I):RESET(56,I):NEXT
322 FORI=7TO56:RESET(I,23):RESET(I,1):NEXT
323 A=X:IFES>0THENFORI=9TO54:SET(I,22,8):NEXTELSEFORI=9TO54:SET(I,22,1):NEXT
340 GU=1:GOSUB2620
350 PRINT@K1," ";:PRINT@M1," ";:PRINT@N1," ";:K1=K:M1=M:N1=N
400 IFB2=1THENFORI=1TORND(10):B3=B3+32:NEXTI:B3=B3+RND(20)+5:PRINT@B3,"B";:B2=B3
405 IF B2>0THENPRINT@B3,"B";
500 GOTO720
510 R=RND(4):ON R GOTO520,530,540,550
520 MV=-1:GOTO560
530 MV=1:GOTO560
540 MV=32:GOTO560
550 MV=-32
560 RETURN
570 K3=0:K2=RND(20)+5:FORI=1TORND(10):K3=K3+32:NEXT:K=K2+K3:IFK=BH ORK=EL THEN570ELSERETURN
580 M3=0:M2=RND(20)+5:FORI=1TORND(10):M3=M3+32:NEXT:M=M2+M3:IFM=BH ORM=EL THEN580ELSERETURN
590 N3=0:N2=RND(20)+5:FORI=1TORND(10):N3=N3+32:NEXT:N=N2+N3:IFN=BH ORN=EL THEN590ELSERETURN
600 IF B(A)=0THEN K=0:M=0:N=0:GOTO500
610 K1=K:M1=M:N1=N
620 GOSUB510:K=K+MV
630 IFB(A)>1THENGOSUB510:M=M+MV:GOTO640
635 M=0:N=0:M1=0:N1=0
640 IFB(A)=3THENGOSUB510:N=N+MV:GOTO650
645 N=0:N1=0
650 FORI=37TO325STEP32:IFK=I ORM=I ORN=I ORK=I+21ORM=I+21ORN=I+21THEN210ELSENEXTI
670 ON B(A) GOTO700,690,680
680 IFN<59ORN>314THENGOSUB590
690 IFM<59ORM>314THENGOSUB580
700 IFK<59ORK>314THENGOSUB570
710 GOTO340
720 F=F-1:IFF<1ANDB(A)>0THENF=6:SOUND99,1:GOTO8960
730 IFE<1ORSD<1ORZ<1THEN6000
735 IFRT>1THEN770
740 D1=RND(100*(11-IU))
745 IFD1<7THEN750ELSE770
750 PRINT@384,"STORM, "T$(D1)" DAMAGED"
760 RT=RND(4):PRINT"REPAIR HOURS"RT
770 I3=0:PL=0:GOTO2620
772 IFZ7=0THEND3=RND(50):IFD3<4THEN774ELSE780
774 IFA=B1(D3)THEND3=0:Z7=0:GOTO780
775 IFB1(D3)>0ANDB(B1(D3))>0THENSOUND244,1:GOSUB9300:PRINT@448,"SOS!! STARBASE"B1(D3);"SECTOR";X7(D3);Y7(D3):Z7=Z7+1
778 IFZ7=(25-(IU*2))THENPRINT@448,"BASE IN SECTOR";X7(D3);Y7(D3);"DESTROYED":B1(D3)=0:D3=0:Z7=0
780 A$=INKEY$:RT=RT-.05:SD=SD-.05:IFRT<0THENRT=0
781 IFA$="Z"THEN6090
782 IFINT(SD)<SX THENSX=SX-1:SOUND230,1
785 IFA$="R"THEN2720
790 IFA$="D"THEN2490
795 IFA$="C"THEN5500
800 IFA$="S"ANDD1<>4THEN2690
810 IFA$="W"AND D1<>3THENPRINT@K," ";:PRINT@M," ";:PRINT@N," ";:PRINT@B3," ";:GOTO1870
820 IFA$="P"ANDD1<>5ANDP>0THENPL=0:GOTO905
830 IFA$="L"ANDD1<>6ANDB(A)>0THEN2810
840 IFA$="I"THENPL=1:GOTO910
880 IFA$="Q"ANDD1<>1THEN2340
890 IFA$="G"ANDD1<>2THEN1350
900 K3=0:M3=0:N3=0:GOTO600
905 P=P-1:GU=1:GOSUB2620
910 GOSUB9300:PRINT@384,"A 1 B":PRINT"2 X 3":PRINT"C 4 D"
940 PRINT@390,"MAIN HEADING";:INPUTF$
950 F1=VAL(F$):F2$=RIGHT$(F$,1)
960 PRINT@422,"POWER VARIENTS";:INPUTI1,I2
970 F3=EL:IFF1=0ORI1=0THEN1070
975 FORI3=1TOI1:F4=F3
980 ON F1 GOTO990,1000,1010,1020
990 F3=F3-32:GOTO1030
1000 F3=F3-1:GOTO1030
1010 F3=F3+1:GOTO1030
1020 F3=F3+32
1030 IFF3>346THENF3=F3-32:GOTO1130
1031 IFF3<32THENF3=F3+32:GOTO1130
1033 IFF3=BH THEN1270
1035 FORC=1TO7:IFF3=H(A,C)THEN1205ELSENEXTC
1036 IFF3=B3 ANDPL<>1THEN1220
1037 IFF3=B3 THEN3500
1040 FORI=36TO324STEP32:IFF3=I ORF3=I+23THEN1130ELSENEXTI
1055 IFPL=1THENPRINT@F3,"E";:PRINT@F4," ";:NEXTI3:GOTO1070
1060 PRINT@F3,"*";:PRINT@F4," ";:NEXTI3
1070 IFASC(F2$)<65ORASC(F2$)>68THEN1130
1085 FORI3=1TOI2:F4=F3
1089 IFPL<>1THENPRINT@EL,"E";
1090 IFF2$="A"THENF3=F3-33:GOTO1125
1100 IFF2$="B"THENF3=F3-31:GOTO1125
1110 IFF2$="C"THENF3=F3+31:GOTO1125
1120 IFF2$="D"THENF3=F3+33:GOTO1125
1125 GOTO1160
1130 I3=0:GOSUB9300:PRINT@F3," ";:PRINT@F4," ";:IF PL=1THEN3000
1131 PRINT@K," ";:PRINT@M," ";:PRINT@N," ";
1132 PRINT@EL,"E";
1139 IFB(A)=0THENB$="0":GOTO1145
1140 IFF3=K ORF3=M ORF3=N THENPRINT@384,"":GOTO1146
1141 GU=1:GOSUB2640
1145 PRINT@384,"MISS":SOUND99,10:GOTO770
1146 SOUND207,1:FORI=1TORND(11):KE=KE-1:PRINT@F3,"k";:SOUND200,1:PRINT@F3,"K";:SOUND200,1:NEXTI:IF KE<1THEN1147ELSE770
1147 SOUND211,1:PRINT@384,"KLINGON DESTROYED":Z=Z-1:KE=3
1148 IFB(A)=3ANDK=F3 THENN1=N:N=K:K=N1:GOTO1290
1149 IFB(A)=3ANDM=F3 THENN1=N:N=M:M=N1:GOTO1290
1150 IFB(A)=3THEN1290
1152 IFB(A)=2ANDM<>F3 THENK=M:M=F3:GOTO1290
1153 GOTO1290
1160 IFF3<32THENF3=F3+32:GOTO1130
1161 IFF3>346THENF3=F3-32:GOTO1130
1163 IFF3=BH THEN1270
1165 FORC=1TO7:IFF3=H(A,C)THEN1205ELSENEXTC
1166 IFF3=B3 ANDPL=1THEN3500
1167 IFF3=B3 THEN1220
1170 FORI=36TO325STEP32:IFF3=I OR F3=I+23THEN1130ELSENEXTI
1180 IFPL=1THENPRINT@F3,"E";:PRINT@F4," ";:NEXTI3:GOTO1130
1190 PRINT@F3,"*";:PRINT@F4," ";:NEXT I3
1200 GOTO1130
1205 IFPL=1THENEL=F4:GOTO1790
1210 GOSUB9300:PRINT@384,"STAR DESTROYED":SOUND99,3:H(A,C)=0:S1(A)=S1(A)-1:PRINT@F3,"*";:PRINT@F4," ";:SOUND20,3:PRINT@F3," ";:GOTO770
1220 GOSUB9300:PRINT@384,"BASE DESTROYED, GOOD JOB!!":PRINT@F3,"*";:PRINT@F4," ";:SOUND99,10:PRINT@F3," ";
1225 B2=0
1230 IFB1(1)=A THENB1(1)=0
1240 IFB1(2)=A THENB1(2)=0
1250 IFB1(3)=A THENB1(3)=0
1260 B3=0:GOTO770
1290 PRINT@F3," ";:B(A)=B(A)-1:SOUND183,10:PRINT@F3," ";:GOTO770
1300 B9=1:Y2=INT(B3/32):X2=B3-INT(Y2*32):Y2=Y2*2:X2=X2*2:GOTO8980
1350 CLS1:FORA=1TO6:GOSUB1420:NEXT:PRINT
1360 FORA=7TO12:GOSUB1420:NEXT:PRINT
1370 FORA=13TO18:GOSUB1420:NEXT:PRINT
1380 FORA=19TO24:GOSUB1420:NEXT:PRINT
1390 FORA=25TO30:GOSUB1420:NEXT:PRINT
1400 FORA=31TO36:GOSUB1420:NEXT:PRINT
1410 GOTO1750
1420 B1$=STR$(B(A)):B$=RIGHT$(B1$,1)
1440 S1$=STR$(S1(A)):S$=RIGHT$(S1$,1)
1490 IF B1(1)=A OR B1(2)=A OR B1(3)=A THEN D$="1"ELSE D$="X"
1495 IF S2(A)=1THENPRINTB$+S$+D$+" ";:RETURN
1497 PRINT"XXX ";:RETURN
1500 IF M1$="X"THENRETURN ELSE CL=0
1505 C1=0:C2=0:GOSUB190
1510 IFM6=0OR I1=0THEN1640
1520 CL=EL
1530 IFCL<32OR CL>346THEN1620
1540 IF M6=2OR M6=3THEN IF CL<C1 OR CL>C1+22THEN1620
1545 PRINT@EL," ";:PRINT@CL,"E";
1550 EL=CL:ON M6 GOTO1560,1570,1580,1590
1560 CL=CL-32:GOTO1600
1570 CL=CL-1:GOTO1600
1580 CL=CL+1:GOTO1600
1590 CL=CL+32
1600 FORC=1TO7:IF CL=H(A,C)THEN1630ELSENEXTC
1610 GOTO1530
1620 WP=1:RETURN
1630 WP=0:RETURN
1640 CL=EL:IFI2=0ORM1$=""THENW5=1:GOTO210
1650 IFCL<32ORCL>346THEN1620
1660 FORC1=4TO356STEP32:IF CL=C1 OR CL=C1+23THEN1620ELSENEXT
1670 PRINT@EL," ";:PRINT@CL,"E";:EL=CL
1680 ON ASC(M1$)-64 GOTO1690,1700,1710,1720
1690 CL=CL-33:GOTO1730
1700 CL=CL-31:GOTO1730
1710 CL=CL+31:GOTO1730
1720 CL=CL+33
1730 FORC=1TO7:IF CL=H(A,C)THEN1630ELSE NEXTC
1740 GOTO1650
1750 PRINT@256,"PRESENT COORDINATES"W;Y
1760 A$=INKEY$:IFA$=""THEN1760ELSE W5=1:CLS1:A=X:ZW=1:GOTO210
1770 GOSUB1500:IF M1$="X"THEN 770
1780 IF WP=1THEN WP=0:PRINT@384,"HYPERDRIVE ENGAGED":GOTO1930
1790 GOSUB9300:PRINT@384,"BLOCKED BY STAR":PRINT@EL,"E";:GOTO770
1870 W5=1:GOSUB9300:E=E-50:GOTO2260
1880 PRINT@256,"A1B";:PRINT@288,"2X3";:PRINT@320,"C4D";
1890 PRINT@416,"HEADING";:INPUTM$
1895 GOSUB9300:PRINT@403,""
1900 M6=VAL(M$):M1$=RIGHT$(M$,1)
1902 IFM6<0OR M6>4THEN1890
1903 IFM1$=""THEN1910
1905 IFASC(M1$)<65ORASC(M1$)>68THENM1$=""
1910 PRINT@416,"WARP VARIANTS";:INPUT I1,I2
1920 GOSUB9300:PRINT@256,"   ";:PRINT@288,"   ";:PRINT@320,"   ";:PRINT@EL,"E";:GOTO1770
1930 C$(1)=".":C$(2)="+":C$(3)="*":C$(4)="0":C$(5)="O":C$(6)=" "
1940 SOUND10,1:C=0:FORI=1TO6:FORC1=1TO7
1950 IF H(A,C1)<>0THENPRINT@H(A,C1),C$(I);
1960 NEXTC1,I:SOUND233,1:PRINT@K," ";:PRINT@M," ";:PRINT@N," ";:PRINT@EL," ";:PRINT@B3," ";
1970 K=0:M=0:N=0
1990 IFM6=0ORI1=0THEN2070
2000 FORI=1TOI1
2010 ON M6 GOTO2020,2030,2040,2050
2020 X=X-6:GOTO2060
2030 X=X-1:GOTO2060
2040 X=X+1:GOTO2060
2050 X=X+6
2060 NEXT
2070 IF M1$=""OR I2=0THEN2133
2080 FOR I=1TO I2
2090 IFM1$="A"THENX=X-7
2100 IFM1$="B"THENX=X-5
2110 IFM1$="C"THENX=X+5
2120 IFM1$="D"THENX=X+7
2130 NEXT
2133 IFX<1ORX>36THENX=RND(36)
2140 W=X:IF X<7THEN Y=1:GOTO2200
2150 IFX>6ANDX<13THENW=W-6:Y=2:GOTO2200
2159 IFX>6ANDX<13THENW=W-6:Y=2:GOTO2200
2160 IFX>12ANDX<19THENW=W-12:Y=3:GOTO2200
2170 IFX>18ANDX<25THENW=W-18:Y=4:GOTO2200
2180 IFX>24ANDX<31THENW=W-24:Y=5:GOTO2200
2190 IFX>30THENW=W-30:Y=6
2200 IFIV=1THENIV=0:RETURN
2201 A=X:SOUND233,1:FORI=6TO1STEP-1:FORC1=1TO7
2220 IF H(A,C1)<>0THENPRINT@H(A,C1),C$(I);
2230 NEXTC1,I:SOUND10,1
2240 PRINT@B3," ";:GOSUB9300:W5=1:GOTO180
2260 PRINT@403,"";:AA=A:FOR I=AA-7TO AA-5:IF I<1THENPRINT"XXX ";:NEXTI:GOTO2280
2270 A=I:GOSUB1420:NEXT
2280 PRINT@435,"";:FOR I=AA-1TO AA+1:IF I<1OR I>36THENPRINT"XXX ";:NEXT:GOTO2310
2300 A=I:GOSUB1420:NEXT
2310 PRINT@467,"";:FOR I=AA+5TO AA+7:IF I>36THENPRINT"XXX ";:NEXTI:GOTO2330
2320 A=I:GOSUB1420:NEXT
2330 A=AA:GOTO1880
2340 S2(A)=1:IF A-1>0THEN S2(A-1)=1
2350 IFA+1<37THENS2(A+1)=1
2360 IFA-6>0THENS2(A-6)=1
2370 IFA+6<36THENS2(A+6)=1
2380 GOSUB9300:PRINT@384,"QUAD SCAN:"
2390 PRINT@407,"";:AA=A:IF AA-6>0THEN A=AA-6:GOSUB1420:GOTO2410
2400 PRINT"XXX ";
2410 PRINT@435,"";:FORI=AA-1TO AA+1:IF I<1OR I>36THENPRINT"XXX ";:NEXT:GOTO2430
2420 A=I:GOSUB1420:NEXT
2430 PRINT@471,"";:IF AA+6<37THEN A=AA+6:GOSUB1420:GOTO2450
2440 PRINT"XXX ";
2450 A=AA:GOTO770
2490 IF B1(1)=A OR B1(2)=A OR B1(3)=A THEN2495ELSE770
2495 GOSUB9300:D7=D7+1:IFD7>3THENPRINT@384,"BASE OUT OF SUPPLIES":GOTO770
2500 Y1=INT(EL/32):X1=EL-INT(Y1*32):Y1=Y1*2:X1=X1*2
2510 Y2=INT(B3/32):X2=B3-INT(Y2*32):Y2=Y2*2:X2=X2*2
2520 IF ABS(Y1-Y2)>ABS(X1-X2)THEN2570
2530 FORI=X1 TO X2 STEP SGN(X2-X1)
2540 I1=Y1+(Y1-Y2)/(X1-X2)*(I-X1)+.5
2541 I2=INT(I1/2)*32+INT(I/2)
2545 IFO1=1THENPRINT@I2,Y$(RND(4));:PRINT@I3," ";:I3=I2:PRINT@K,"K";:PRINT@EL,"E";:IF I2=B3 THEN9180
2546 IF O1=1THENNEXT:GOTO2605
2550 PRINT@I2,"E";:PRINT@I3," ";:I3=I2:NEXT:GOTO2610
2570 FORI=Y1 TO Y2 STEP SGN(Y2-Y1)
2580 I1=X1+(I-Y1)*(X2-X1)/(Y2-Y1)+.5
2590 I2=INT(I/2)*32+INT(I1/2)
2595 IFO1=1THENPRINT@I2,Y$(RND(4));:PRINT@I3," ";:PRINT@EL,"E";:I3=I2:PRINT@K,"K";:IF I2=B3 THEN9180ELSENEXT:GOTO2605
2600 PRINT@I2,"E";:PRINT@I3," ";:I3=I2:NEXT
2605 IF O1=1THEN O1=0:POKE 65314,10:POKE 65314,0:RETURN
2610 EL=I2+1:P=16:E=10000:PRINT@B3,"B";
2615 PRINT@384,"ENTERPRISE IS DOCKED"
2620 FORC=1TO7:IFH(A,C)<>0THENPRINT@H(A,C),".";
2630 NEXT:PRINT@EL,"E";
2640 IF B(A)=0THEN2680ELSE ON B(A) GOTO2670,2660,2650
2650 PRINT@N,"K";
2660 PRINT@M,"K";
2670 PRINT@K,"K";
2680 IFGU=1THENGU=0:RETURN
2685 IFZW=1THENZW=0:GOTO782
2686 GOTO772
2690 GOSUB9300
2700 PRINT@384,"SHIELD ENERGY";:INPUTI:ES=ES+I:E=E-I:PRINT@384,"":GOTO770
2720 GOSUB9300:PRINT@384,"";
2740 FORI=1TO9:FORI1=1TOLEN(D$(I)):PRINTMID$(D$(I),I1,1);:FORL=1TO8:NEXTL,I1:IFI=1THEN2755
2750 PRINT"--";:ON I GOSUB2755,2762,2764,2766,2768,2770,2772,2774,2776
2755 FORL=1TO900:NEXT:GOSUB9300:PRINT@384,"";:A$=INKEY$:IFA$="E"THEN770ELSENEXTI
2756 IFED=1THEN6010ELSE770
2762 PRINTZ:RETURN
2764 PRINTZZ-Z:RETURN
2766 PRINTE:RETURN
2768 PRINTP:RETURN
2770 PRINTES:RETURN
2772 PRINTSD:RETURN
2774 VD=(ZZ-Z)*100+(IU*10):IFZ=0THENVD=VD+(6000-(D7*2000))+500+E+(P*625):IFVD<0THENVD=0
2775 PRINTVD:RETURN
2776 IFD1>6THENPRINT"NONE":RETURN
2777 PRINTT$(D1):PRINT@416,"HOURS TO REPAIR"INT(RT):RETURN
2785 IFED=1THEN ED=0:RETURN
2800 A$=INKEY$:IFA$=""THEN2800ELSEW5=1:GOTO210
2810 GOSUB9300:PRINT@384,"LASER ENERGY";:INPUTS3
2820 E=E-S3:IFS3>B(A)*(RND(200)+333)THEN2830
2825 PRINT@EL,"E";:GOTO2640
2830 FORI=1TOB(A):PRINT@384,"KLINGON DESTROYED":SOUND200,5:PRINT@384,"":SOUND200,5
2831 FOR I1=1TO20:L=RND(2):L$(1)="k":L$(2)="K":ON B(A) GOTO2834,2833,2832
2832 PRINT@N,L$(L);
2833 PRINT@M,L$(L);
2834 PRINT@K,L$(L);
2835 NEXTI1,I
2836 PRINT@K," ";:PRINT@M," ";:PRINT@N," ";
2840 Z=Z-B(A):B(A)=0:K=0:M=0:N=0:GOTO770
3000 FORI=36TO324STEP32:IFF3=I THENEL=F3+1:GOTO770
3001 IFF3=I+23THENEL=F3-1:GOTO770
3002 NEXTI:EL=F3:GOTO770
3500 GOSUB9300:EL=F4:PRINT@EL,"E";:PRINT@B3,"B";:PRINT@384,"BLOCKED BY BASE":GOTO770
5000 DATAQUAD SCAN,GALACTIC RECORDS,WARP,SHEILDS,PHOTON TORPEDOES,LASERS
5010 DATASTATUS REPORT,KLINGONS REMAINING,KLINGONS DESTROYED,ENERGY,PHOTON TORPEDOES,SHEILD ENERGY,STARDATE,RATING,DAMAGE:
5500 CLS1:SOUND99,1:PRINT@11,"COMMANDS":PRINT
5510 PRINT"lASERS":PRINT"pHOTON TORPEDOES":PRINT"wARP":PRINT"qUAD SCAN":PRINT"gALACTIC RECORDS":PRINT"iMPULSE"
5515 PRINT"sHEILDS":PRINT"dOCK":PRINT"STATUS rEPORT":PRINT"z=SAVE GAME":PRINT"cOMMAND LIST"
5520 GOTO1760
6000 GOTO6010
6010 ED=1:GOSUB2720
6090 CLS:PRINT"READY CASSETTE, PRESS ENTER":INPUTA$
7000 OPEN"O",-1,"GAME":PRINT#-1,SD,SX,E,P,Z,ZZ,VD,X,Y,W,ES,D7
7010 FORI=1TO36:PRINT#-1,B(I),S2(I):NEXT
7030 CLOSE-1:END
7040 OPEN"I",-1,"GAME":INPUT#-1,SD,SX,E,P,Z,ZZ,VD,X,Y,W,ES,D7
7050 FORI=1TO36:IFEOF(-1)THEN60ELSEINPUT#-1,B(I),S2(I):NEXT:CLOSE-1:GOTO60
8960 IFEL=K OR EL=M OR EL=N THEN770
8961 IF B3>0THEN R=RND(12):IF R<5THEN 1300
8963 K1=K
8964 XX=0:GOSUB9300
8965 XX=XX+1:IF XX>B(A)THEN9145
8967 IFB9=1THEN8980
8970 IF XX=2THENK=M
8972 IF XX=3THENK=N
8974 GU=1:GOSUB2620
8975 PRINT@K1,"K";
8980 Y1=INT(K/32):X1=K-INT(Y1*32):Y1=Y1*2:X1=X1*2
8985 IFB9=1THEN8995
8990 Y2=INT(EL/32):X2=EL-INT(Y2*32):Y2=Y2*2:X2=X2*2
8995 PRINT@EL,"E";:PRINT@K,"K";
9000 O1=1:GOSUB2520
9028 IFI2=B3 THEN9180
9130 K=K1:SOUND50,1:GOTO8965
9145 IF K=B3 THEN 9180
9150 DE=(RND(90)+20)*XX:PRINT@383,DE"EG UNITS HIT ON ENTERPRISE":IS=(ES/10)*(3+RND(0)):IS=INT(IS):IFIS>DE THENIS=DE
9155 PRINT@415,IS"EG UNITS DEFLECTED":ES=ES-INT(IS*.333):DE=DE-IS:IFES<0THENES=0:SOUND171,1
9160 E=E-DE:FORI=1TO8:PRINT@EL,"e";:FORI1=1TO15:NEXTI1:PRINT@EL,"E";:FORI1=1TO15:NEXTI1:NEXTI:IFE<1THEN6000
9165 IFES<0THENES=0
9170 GOTO770
9180 IFB1(1)=A THENB1(1)=0:GOTO9210
9190 IFB1(2)=A THENB1(2)=0:GOTO9210
9200 B1(3)=0
9210 PRINT@384,"BASE DESTROYED":SOUND99,10:B2=0:PRINT@B3," ";:PRINT@416,"":B3=0:B9=0:GOTO770
9300 PRINT@384,"":PRINT@416,"":PRINT@448,"":RETURN
