0 CLS:CLEAR8000:GOSUB800:GOSUB8000:GOTO900
1 ONK(PEEK(135))GOSUB4,5,6,13,31:RETURN
2 RETURN
4 D=D-1:OND GOTO2,2,2,2,2,2,2,2:D=8:RETURN
5 D=D+1:OND GOTO2,2,2,2,2,2,2,2:D=1:RETURN
6 SET(S(X),T(Y),1):RESET(S(X),T(Y)):PRINT@P,S$;:PRINT@P+A,T$;:X=X+V(D):Y=Y+W(D):ON1-(X>29)GOTO7:X=1:GOTO8
7 ON1-(X=0)GOTO8:X=28
8 ON1-(Y=0)GOTO9:Y=13:GOTO10
9 ON1-(Y=14)GOTO10:Y=1
10 P=A*Y+X:ONPOINT(S(X),T(Y))GOSUB50,50,50,50,50,50,50,50:RETURN
11 PRINT@P(H),D$(H);:PRINT@P(H)+A,E$(H);:PRINT@P(H)+B,F$(H);:PRINT@P(H),A$(H);:PRINT@P(H)+A,B$(H);:PRINT@P(H)+B,C$(H);:SOUND100,1:RETURN
13 ONF GOSUB15:M=V(D)*2:L=W(D)*2:I=2*X+1:J=2*Y+1:F=1:RETURN
14 RESET(I,J):I=I+M:J=J+L:ONPOINT(I,J)GOTO16,16,16,16,16,16,16,16:SET(I,J,5):ON1-(I<60ANDI>2ANDJ>2ANDJ<29)GOTO15:RETURN
15 RESET(I,J):F=0:RETURN
16 H=POINT(I,J):H(H)=H(H)-1:A$(H)=X$(H,H(H)):B$(H)=Y$(H,H(H)):C$(H)=Z$(H,H(H)):GOSUB11:Q=Q+1:F=0:ONH(H)GOTO19,2
17 A$(H)=X$(H,H(Z)):B$(H)=Y$(H,H(Z)):C$(H)=Z$(H,H(Z)):D$(H)=D$(Z):E$(H)=E$(Z):F$(H)=F$(Z):H(H)=H(Z):P(H)=P(Z):GOSUB11
18 X(H)=X(Z):Y(H)=Y(Z):A$(Z)="":B$(Z)="":C$(Z)="":D$(Z)="":E$(Z)="":F$(Z)="":SOUND120,1:Z=Z-1:ON1-(Q<24)GOTO60,2
19 D$(H)=B$(0):E$(H)=B$(0):F$(H)=B$(0):RETURN
20 FORE=1TO1000:FORT=1TOZ:O=P(T):X(T)=X(T)+A(T,X(T)):Y(T)=Y(T)+B(T,Y(T)):P(T)=A*INT(Y(T))+X(T):PRINT@O,D$(T);:PRINT@O+A,E$(T);:PRINT@O+B,F$(T);:PRINT@P(T),A$(T);:PRINT@P(T)+A,B$(T);:PRINT@P(T)+B,C$(T);:ONPOINT(S(X),T(Y))GOSUB45,45,45,45,45,45,45,45
24 POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:ON1-((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341))=255)GOSUB1:PRINT@P,S$(D);:PRINT@P+A,T$(D);:ONF GOSUB14,40,40,40,40:NEXT:NEXT
26 IFSH>=3THENPRINT@0,"SHIP DESTROYED!";:GOSUB55:GOTO100
30 Q=0:GOSUB2000:GOTO20
31 ONS GOTO2:ONF GOTO32,2,2,2,2
32 GOSUB15:F=2:S=S-1:RETURN
40 PRINT@P-A-1,CHR$(150)STRING$(2,156)CHR$(153);:PRINT@P-1,CHR$(154)STRING$(2,144)CHR$(149);:PRINT@P+A-1,CHR$(153)STRING$(2,147)CHR$(150);:SOUND250,1
41 PRINT@P-A-1,STRING$(4,128);:PRINT@P-1,STRING$(4,128);:PRINT@P+A-1,STRING$(4,128);:PRINT@P,S$(D);:PRINT@P+A,T$(D);:F=F+1:RETURN
45 ONF GOTO50,40,40,40,40
50 H=POINT(S(X),T(Y)):H$=CHR$(16*(H-1)+134):PRINT@P,H$;H$;:PRINT@P+A,H$;H$;:SOUND150,1:SOUND150,1:SOUND200,2:SOUND1,1
51 SH=SH+1:GOSUB999:IFSH=3THENE=1000
52 RETURN
55 SC=SC+Q*LV:PRINT@18,"SCORE=";SC:RETURN
60 PRINT@0,"FIELD";LC;"CLEARED!";:TM=1000-E:SC=SC+TM:GOSUB15:GOSUB55:GOSUB8060:E=1000:RETURN
100 PRINT@487,"PLAY AGAIN (Y/N)?";
110 I$=INKEY$:IFI$=""THEN110
120 IFI$="Y"THENGOSUB8000:GOSUB8060:GOSUB2000:GOTO20
130 IFI$="N"THENGOSUB10010:GOSUB10003:END
140 GOTO110
800 DIMS(34),T(15),X(8),Y(8),K(255),A$(8),B$(8),C$(8),D$(8),E$(8),F$(8),S$(8),T$(8),P(8),A(8,31),B(8,15),H(8)
810 DIMX$(8,4),Y$(8,4),Z$(8,4),N(2),T,O,P,D,A,B,X,Y,C,E,F,H,I,J,K,L,M,Q,R,Z,S,SH,SC,HS,LV,LC,LM,LN,TM,B$,S$,T$,I$
830 R=RND(-TIMER):GOSUB10005:GOSUB10000:RETURN
900 GOSUB1000:GOTO20
999 PRINT@480,STRING$(12,128);:ON1-(SH<3)GOTO2:FORC=1TO3-SH:SET(C*2,31,1):NEXT:RETURN
1000 N(1)=-1:N(2)=1
1010 K(9)=1:K(8)=2:K(94)=3:K(32)=4:K(10)=5:K=17023
1020 FORT=0TO31:S(T)=2*T+1:NEXT:FORT=0TO15:T(T)=2*T+1:NEXT
1030 A=32:B=64
1050 S$(0)=CHR$(128)+CHR$(128):T$(0)=CHR$(128)+CHR$(128):S$=S$(0):T$=T$(0)
1051 S$(1)=CHR$(142)+CHR$(128):T$(1)=CHR$(128)+CHR$(128):V(1)=-1:W(1)=-1
1052 S$(2)=CHR$(134)+CHR$(128):T$(2)=CHR$(132)+CHR$(128):V(2)=-1:W(2)=0
1053 S$(3)=CHR$(130)+CHR$(128):T$(3)=CHR$(140)+CHR$(128):V(3)=-1:W(3)=1
1054 S$(4)=CHR$(130)+CHR$(130):T$(4)=CHR$(132)+CHR$(128):V(4)=0:W(4)=1
1055 S$(5)=CHR$(128)+CHR$(130):T$(5)=CHR$(132)+CHR$(136):V(5)=1:W(5)=1
1056 S$(6)=CHR$(132)+CHR$(130):T$(6)=CHR$(132)+CHR$(128):V(6)=1:W(6)=0
1057 S$(7)=CHR$(132)+CHR$(138):T$(7)=CHR$(128)+CHR$(128):V(7)=1:W(7)=-1
1058 S$(8)=CHR$(134)+CHR$(130):T$(8)=CHR$(128)+CHR$(128):V(8)=0:W(8)=-1
1100 A$(0)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
1110 B$(0)=CHR$(128)+CHR$(128)
1120 C$(0)=CHR$(128)
1200 FORC=0TO7
1210 X$(C+1,3)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(130+(C*16))
1220 Y$(C+1,3)=CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))
1230 Z$(C+1,3)=CHR$(132+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1261 X$(C+1,2)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(139+(C*16))+CHR$(128+(C*16))
1262 Y$(C+1,2)=CHR$(141+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1263 Z$(C+1,2)=CHR$(128+(C*16))+CHR$(140+(C*16))+CHR$(136+(C*16))+CHR$(128+(C*16))
1271 X$(C+1,1)=CHR$(135+(C*16))+CHR$(139+(C*16))
1272 Y$(C+1,1)=CHR$(141+(C*16))+CHR$(142+(C*16))
1273 Z$(C+1,1)=""
1280 NEXT:GOSUB8060
2000 CLS0:GOSUB999:Z=8:S=4:LV=LV+1:LC=LC+1:IFLV>10 THENLV=10
2005 LM=5+LV+RND(5):LN=5+INT(LV/2):F=0:TM=0:D=4:X=16:Y=0:P=A*Y+X:GOSUB3000
2010 FORC=1TO8:A(C,0)=29:A(C,1)=28:R=(RND(LM)/10)*N(RND(2)):FORT=2TO29:A(C,T)=R:NEXT:A(C,30)=-28:A(C,31)=-29:NEXT:GOSUB3000
2015 FORC=1TO8:B(C,0)=11:R=(RND(LN)/10)*N(RND(2)):FORT=1TO11:B(C,T)=R:NEXT:B(C,12)=-11:NEXT:GOSUB3000
2025 FORC=1TO8:H(C)=3:A$(C)=X$(C,3):B$(C)=Y$(C,3):C$(C)=Z$(C,3):D$(C)=A$(0):E$(C)=A$(0):F$(C)=A$(0):NEXT:GOSUB3000
2080 FORT=1TO8:Y(T)=T+2:X(T)=2:P(T)=A*INT(Y(T))+X(T):NEXT:FORT=16TO256STEP32:PRINT@T,STRING$(2,128);:NEXT:RETURN
3000 PRINT@P,S$;:PRINT@P+A,CHR$(186);:Y=Y+2:P=A*Y+X:PRINT@P,S$(D);:PRINT@P+A,T$(D);:PRINT@P-31,CHR$(186);:RETURN
8000 CLS:PRINT"** THE RAINBOW ASTEROID FIELD **";TAB(10)"BY JIM GERRIE":PRINT
8001 PRINT"YOU ARE IN COMMAND OF A SHIP"
8002 PRINT"EXPLORING THE FAMOUS RAINBOW"
8003 PRINT"CRYSTAL ASTEROIDS OF ARCTURUS."
8004 PRINT"UNFORTUNATELY THE FIELD HAS BE-"
8005 PRINT"COME UNSTABLE AND NOW THREATENS"
8006 PRINT"A NEARBY COLONY. IT IS YOUR"
8007 PRINT"SAD TASK TO CLEAR THE FIELD."
8010 PRINT"USE: LEFT=COUNTER CLOCKWISE"
8020 PRINT"     RIGHT=TURN CLOCKWISE"
8030 PRINT"     UP=THRUST  DOWN=SHIELDS"
8040 PRINT"     SPACE=FIRE      SHIPS=3"
8045 IFSC>HS THENHS=SC
8050 SC=0:LV=0:LC=0:SH=0:Q=0:I$="high="+STR$(HS):PRINTTAB((32-LEN(I$))/2)I$;:RETURN
8060 FORT=1TO50:I$=INKEY$:NEXT:PRINT@485,"PRESS ANY KEY TO BEGIN ";
8065 IFLC>0ANDLC/3-INT(LC/3)=0 THENSH=SH-1:PRINT@256,"bonus";CHR$(128);"ship";CHR$(128);"earned";:IFSH<0 THENSH=0
8070 I$=INKEY$:ON1-(I$<>"") GOTO8070:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN
