0 POKE16925,0:POKE16926,1:GOTO100
1 X=I-O:Y=J+O:A=.:B=.:GOTO90
2 X=I+O:Y=J+O:A=.:B=.:GOTO90
8 IFT<6THENRESET(X+A,Y+B):GOTO22
9 GOTO22
10 IFI>3THENSET(I+O,J+O,O):RESET(I,J):RESET(I+O,J+O):I=I-O:SET(I+O,J+O,8):K=POINT(I-O,J+O):SET(I-O,J+O,8):SET(I,J,8):IFK=OTHEN1
11 GOTO26
12 IFI<RTHENSET(I-O,J+O,O):RESET(I,J):RESET(I-O,J+O):I=I+O:SET(I-O,J+O,8):K=POINT(I+O,J+O):SET(I+O,J+O,8):SET(I,J,8):IFK=OTHEN2
13 GOTO26
14 IFSORPOINT(I,J-O)THEN26
15 Z=Z+O:IFZ=16THENZ=10
16 IFA(Z)THEN26
17 X(Z)=I:Y(Z)=J-O:A(Z)=O:SET(I,J-O,.):S=O:GOTO26
18 FORQ=OTOL:S=.:FORT=OTO6:A=A(T):B=B(T):X=X(T):Y=Y(T):IFATHEN21
19 IFB=.THEN26
20 IFY=27THENSET(X,Y,4):SOUND100,3:SET(X,Y,O):B(T)=.:RESET(X,Y):X(T)=.:Y(T)=.:GOTO26
21 K=POINT(X+A,Y+B):IFKTHENONKGOTO80,22,22,22,22,22,8,60
22 A=A*V(-(POINT(X+A,Y)=.)):B=B*V(-(POINT(X,Y+B)=.)):IFPOINT(X+A,Y+B)THENIFPOINT(X+A,Y+B)<>OTHENA=-A:B=-B
23 RESET(X,Y):X=X+A:Y=Y+B:SET(X,Y,.):A(T)=A:B(T)=B:X(T)=X:Y(T)=Y:IFPEEK(2)ANDOANDNOTPEEK(16946)THEN10
24 IFPEEK(2)ANDOANDNOTPEEK(16949)THEN12
25 IFPEEK(2)AND8ANDNOTPEEK(16952)THEN14
26 M=T+9:IFA(M)THENX=X(M):Y=Y(M):RESET(X,Y):Y=Y-O:Y(M)=Y:K=POINT(X,Y):SET(X,Y,.):IFKTHEN45
27 NEXT:NEXT:FORT=OTO6:IFB(T)THENNEXT:GOTO18
28 SOUND230,O:GOSUB400:T=6:NEXT:GOTO18
45 IFK>OTHENA(M)=.:GOTO27
46 FORA=OTO5:IFX(A)=XTHENIFY(A)=YTHENIFA(A)<>.THEN:A(A)=.:B(A)=O:SET(X,Y,4):SOUNDO,2:SET(X,Y,O):A(M)=.:SC=SC+20:GOTO58
47 NEXT:IFX(6)=XTHENIFY(6)=YTHENSET(X,Y,4):SOUNDO,2:SET(X,Y,O):A(M)=.:SC=SC+50:GOTO70
58 GOSUB610:RESET(X,Y):GOTO27
60 A(T)=.:B(T)=.:SET(X+A,Y+B,4):RESET(X,Y)
61 D=D+O:GOSUB500:SOUND50,2:SOUND100,2:SET(X+A,Y+B,8):IFD=5THEN65
62 GOTO27
65 T=6:NEXT:Q=L:NEXT:PRINT@7*32+11,"GAME OVER";:GOTO700
70 T=6:NEXT:Q=L:NEXT:GOSUB610:SET(X(6),Y(6),4):PRINT@7*32+11,"NEW JAIL!";
71 D=D-O:IFD<OTHEND=O
72 GOSUB500:FORK=1TO15000:NEXT:GOTO175
80 IFA=.THEN23
81 FORK=10TO15:IFABS(X-X(K))>OORABS(Y-Y(K))>OTHENNEXT:GOTO22
82 A(T)=.:B(T)=O:SET(X,Y,4):SOUNDO,2:SET(X,Y,O):A(K)=.:RESET(X(K),Y(K)):SC=SC+20:K=15:NEXT:IFT=6THENSC=SC+30:GOTO70
83 GOTO58
90 FORK=OTO6:IFX<>X(K)ORY<>Y(K)THENNEXT:GOTO26
91 SET(X,Y,4):SC=SC+20:SOUNDO,2:GOSUB610:A(K)=.:B(K)=.:SET(X,Y,O):RESET(X,Y):X(K)=.:Y(K)=.:K=K+6:NEXT:IFK=13THENSC=SC+30:GOTO70
92 GOTO61
100 CLS:DIMX,Y,A,B,T,V(1),X(15),Y(15),A(15),B(15),K,I,J,Z,S,M,Q,L,R,O:V(0)=-1:V(1)=1:O=1
110 HS=RND(-(PEEK(9)*256+PEEK(10))):R=60:HS=0:CLS0:POKE49151,64:GOSUB990
150 CLS0:POKE49151,0:CLS:SC=0:L=120:LL=2:M$="^^^€€€€":D=1
175 L=L-10:IFL<60THENL=60
200 CLS0:GOSUB300:FORX=1TO62:SET(X,1,6):SET(X,28,6):NEXT
205 FORY=1TO31:SET(0,Y,6):SET(1,Y,6):SET(62,Y,6):SET(63,Y,6):NEXT
210 FORT=1TO15:X(T)=.:Y(T)=.:A(T)=.:B(T)=.:NEXT:GOSUB500:GOSUB600
215 LL=LL+1:IFLL>6THENLL=6
220 FORT=1TOLL:GOSUB400:NEXT
225 X(6)=28+RND(5):Y(6)=10:A(6)=V(RND(2)-1):B(6)=V(RND(2)-1)
230 I=31:J=26:SET(I,J,8):SET(I-1,J+1,8):SET(I,J+1,8):SET(I+1,J+1,8):Z=9
240 GOTO18
300 J=3:PRINT@J*32+6,"ïïïïïïïïïïïïïïïïïïï";
310 PRINT@(J+1)*32+6,"ï€€€€€€€€€€€€€€€€€ï";
320 PRINT@(J+2)*32+6,"ï€€€€€€€€€€€€€€€€€ï";
330 PRINT@(J+3)*32+6,"ïïïïïïïïïïïïïïïïïïï";:RETURN
400 X(T)=3+RND(56):Y(T)=15+RND(8):A(T)=V(RND(2)-1):B(T)=V(RND(2)-1):RESTORE:FORK=1TO8:READA,B:SET(X(T)+A,Y(T)+B,.):NEXT
410 SOUND200,1:SOUND240,1:SOUND235,1:RESTORE:FORK=1TO8:READA,B:RESET(X(T)+A,Y(T)+B):NEXT:SET(X(T),Y(T),.):RETURN
500 PRINT@482,MID$(M$,D,3);:RETURN
600 IFSC>HSTHENHS=SC
605 PRINT@486,"HIGH";HS;
610 PRINT@498,"SCORE";SC;:RETURN
700 GOSUB600:PRINT@9*32+7,"PLAY AGAIN (Y/N)?";
710 M$=INKEY$:IFM$=""THEN710
720 IFM$="Y"THEN150
730 IFM$="N"THENEND
740 GOTO710
900 DATA-2,-2,2,-2,-2,2,2,2,-1,-1,1,-1,-1,1,1,1
990 CLS:PRINT@448,;:M=16384
1000 M$="         GALAXY JAILBREAK       ":GOSUB2000:PRINT@480,"":PRINT@448,;
1010 PRINT" BY ROLAND STOKES & JIM GERRIE"
1020 M$="THEME:":GOSUB2000:PRINT
1030 PRINT"YOU HAVE CAPTURED SOME EVIL"
1040 PRINT"ALIEN GENERALS. HOWEVER THEIR"
1050 PRINT"SOLDIERS ARE ATTEMPTING TO FREE"
1060 PRINT"THEM BY WARPING INTO THE AREA"
1070 PRINT"AND ATTACKING THE JAIL."
1080 M$="OBJECT:":GOSUB2000:PRINT
1090 PRINT"DEFEND THE JAIL. YOU CONTROL A"
1100 PRINT"MISSILE LAUNCHER WHICH CAN FIRE"
1110 PRINT"6 SHOTS AT A TIME. WHEN HIT,"
1120 PRINT"THE ENEMY WILL DIVE ON YOU."
1130 PRINT"HOLD THE GENERAL AS LONG AS YOU"
1140 PRINT"CAN BY KILLING SOLDIERS."
1150 GOSUB1500:PRINT:PRINT:PRINT
1200 M$="THE KEYS USED ARE:":GOSUB2000:PRINT:PRINT
1210 PRINT"  A     = LEFT."
1220 PRINT"  D     = RIGHT."
1230 PRINT"  SPACE = FIRE.":PRINT
1240 PRINT"EACH GENERAL SCORES 50."
1250 PRINT"EACH SOLDIER SCORES 20.":PRINT
1260 PRINT"EACH GENERAL THAT IS KILLED"
1270 PRINT"WILL SWITCH YOU TO A NEW JAIL"
1280 PRINT"WITH EVEN MORE FANATIC SOLDIERS.";
1290 PRINT"YOU GET AN ADDED LIFE FOR EVERY":PRINT"NEW JAIL.":PRINT
1500 PRINT@480,;:M$="       ANY KEY TO CONTINUE     ":GOSUB2000:POKEM+511,32:FORX=1TO25:M$=INKEY$:NEXT
1510 IFINKEY$=""THEN1510
1520 PRINT:RETURN
2000 X=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORY=1TOLEN(M$):A=ASC(MID$(M$,Y)):POKEX+Y,A-(AAND64):NEXT:?@X-M+Y,;:RETURN
2010 REM INSPIRED BY SINCLAIR
2020 REM ZX81 GAME. 2021
