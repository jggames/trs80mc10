0 CLS:DIMP(511),K(255),L(3),X(3),Y(3),A(3),D(3),F(3),C(4),T(3),R,Z,J,K,G,H,O,M,N,I,L,A,B,C,D,E,F,P,Q,S,T,U,V,W,X,Y,MC:GOTO1000
1 RETURN
2 F(R)=(F(R)*B)*RND(2):PRINT@L(R),B$;:PRINT@L(R)+G,B$;:Y(R)=INT(Y(R))+SGN(F(R)):GOTO26
3 F(R)=U:A(R)=0:PRINT@L(R),B$;:PRINT@L(R)+G,B$;:Y(R)=INT(Y(R))+SGN(F(R)):X(R)=INT(X(R))+SGN(A(R)):GOTO26
4 A(R)=A(R)*M:PRINT@L(R),B$;:PRINT@L(R)+G,B$;:X(R)=INT(X(R))+SGN(A(R)):Y(R)=Y(R)+U:GOTO26
5 PRINT@L(R),A$(D(R)+9);:F(R)=F(R)=U:RETURN
7 A(R)=S(INT((A(R)-W)*S)+V):D(R)=3:RETURN
8 A(R)=S(INT((A(R)+W)*S)+V):D(R)=1:RETURN
9 PRINT@L(R),A$(D(R)+1);:PRINT@L(R)+G,B$(D(R)+1);:F(R)=W(R):RETURN
10 ONK(PEEK(C)ANDPEEK(2))GOSUB7,8,9,5:RETURN
11 IFX(R)<X(1)THENA(R)=S(INT((A(R)+A)*S)+V):D(R)=5:GOTO13
12 A(R)=S(INT((A(R)-A)*S)+V):D(R)=7
13 IFY(R)>=Y(1)THENPRINT@L(R),A$(D(R)+1);:PRINT@L(R)+G,B$(D(R)+1);:F(R)=W(R):RETURN
14 RETURN
17 A(R)=S(INT((A(R)-A)*S)+V):D(R)=7:RETURN
18 A(R)=S(INT((A(R)+A)*S)+V):D(R)=5:RETURN
19 GOSUB100:SOUND1,10:ONRGOSUB60,84,84:NEXT:NEXT:GOTO40
20 FORZ=1TOE:FORR=1TO3:ONR(R)GOTO34,31:ONRGOSUB10,11,11:ONK(PEEK(P(L(R))+J))GOTO2,3,50,90,2:ONK(PEEK(P(L(R))+K))GOTO2,3,50,90,2
21 ONK(PEEK(P(L(R))+H))GOTO2,19,50,90,2:ONK(PEEK(P(L(R))+O))GOTO2,19,50,90,2:ONK(PEEK(P(L(R))+M))GOTO4,19,50,90,4
22 ONK(PEEK(P(L(R))+N))GOTO4,19,50,90,4:ONK(PEEK(P(L(R))+I))GOTO4,4,50,90,2:ONK(PEEK(P(L(R))+L))GOTO4,4,50,90,2
23 PRINT@L(R),B$;:PRINT@L(R)+G,B$;:F(R)=F(R)+F:Y(R)=Y(R)+F(R):X(R)=X(R)+A(R):IFX(R)>GTHENX(R)=X(R)-G
24 IFX(R)<0THENX(R)=X(R)+G
26 L(R)=G*INT(Y(R))+X(R):PRINT@L(R),A$(D(R));:PRINT@L(R)+G,B$(D(R));:NEXT:NEXT:GOTO40
31 PRINT@L(R),CHR$(207);:T(R)=T(R)+1:ON1-(T(R)<TM)GOTO32:NEXT:NEXT:GOTO40
32 PRINT@L(R),CHR$(143);:R(R)=0:L(R)=L(R)-65:Y(R)=INT(L(R)/G):X(R)=((L(R)/G)-Y(R))*G:D(R)=5:F(R)=-.25:GOSUB2095
33 SOUND1,2:SOUND55,3:SOUND25,2:NEXT:NEXT:GOTO40
34 ONRND(15+LV)GOTO35,36,37,38:NEXT:NEXT:GOTO40
35 FORT=0TO96STEP32:PRINT@508-T,B$(9);:PRINT@476-T,A$(9);:NEXT:NEXT:NEXT:GOTO40
36 FORT=96TO32STEP-32:PRINT@508-T,B$;:PRINT@476-T,B$;:NEXT:NEXT:NEXT:GOTO40
37 FORT=0TO96STEP32:PRINT@485-T,B$(9);:PRINT@453-T,A$(9);:NEXT:NEXT:NEXT:GOTO40
38 FORT=96TO32STEP-32:PRINT@485-T,B$;:PRINT@453-T,B$;:NEXT:NEXT:NEXT:GOTO40
40 IFZ>65100THENLV=LV+1:PRINT@32,"YE MADE IT! CAVERN";LV;"CLEARED!";:FORZ=1TO2500:NEXT:GOSUB160:GOSUB2000:GOTO20
45 PRINT@32,"PLAY AGAIN (Y/N)?";
46 I$=INKEY$:RR=RND(1000):IFI$=""THEN46
47 IFI$="Y"THENGOSUB3000:LF=0:LV=0:SC=0:TM=50:GOTO1260
48 IFI$="N"THENEND
49 GOTO46
50 IFY(R)<3THEN2
55 PRINT@G,"LAVA!";:PRINT@L(R),A$(9);:PRINT@L(R)+G,B$(9);:SOUND20,10:ONRGOSUB61,81,81:PRINT@G,"     ";
56 IFL(R)>448THENPRINT@L(R)+30,B$(9);B$(9);
57 NEXT:NEXT:GOTO40
60 PRINT@L(R),A$(11);:PRINT@L(R)+G,A$(11);:SOUND50,1
61 PRINT@L(R),B$;:PRINT@L(R)+G,B$;:R(1)=1:GOSUB100:LF=LF+1:IFLF<>2THENPRINT@G,3-LF;"MEN LEFT!";:GOTO64
62 PRINT@G,3-LF;"MAN LEFT!";
64 IFLF=3THENR=3:Z=65000:FORT=1TO3000:NEXT:GOTO80
65 ONRND(2)GOTO66,67
66 R(R)=0:X(R)=24:Y(R)=3:D(R)=1:GOSUB2095:IFPEEK(MC+L(R))=143ANDPEEK(MC+L(R)+33)=143ANDPEEK(MC+L(R)-32)=143THEN70
67 R(R)=0:X(R)=5:Y(R)=3:D(R)=3:GOSUB2095:IFPEEK(MC+L(R))=143ANDPEEK(MC+L(R)+33)=143ANDPEEK(MC+L(R)-32)=143THEN70
68 R(R)=0:X(R)=15:Y(R)=11:D(R)=3:GOSUB2095
70 FORT=1TO5:PRINT@L(R),A$(11);:PRINT@L(R)+G,A$(11);:FORX=1TO100:NEXT:PRINT@L(R),A$(D(R));:PRINT@L(R)+G,B$(D(R));
71 FORX=1TO100:NEXT:NEXT
80 PRINT@32,"             ";:RETURN
81 PRINT@L(R),B$;:PRINT@L(R)+G,B$;:MN=MN+1:R(R)=1:GOTO88
84 X=100:PRINT@L(R),B$;:PRINT@L(R)+G,B$;:PRINT@L(1),A$(1);:PRINT@L(1)+G,B$(1);:FORY=L(R)+33TO511STEP32:P=PEEK(MC+Y)
85 Q=PEEK(MC+Y+32):IFQ>240THENPRINT@Y,CHR$(255);:SOUND200,2:PRINT@Y,CHR$(P);:SOUND150,2:Y=511:NEXT:R(R)=1:MN=MN+1:GOTO88
86 PRINT@Y,CHR$(207);:SOUNDX,1:X=X-5:IFQ=128ORQ=207ORQ<64THENSOUND1,1:R(R)=2:L(R)=Y:Y=511:SC=SC+10:NEXT:GOTO88
87 PRINT@Y,CHR$(P);:NEXT:R(R)=1:MN=MN+1
88 SC=SC+10:A$=STR$(SC):X=13:Y=15:GOSUB150:IFMN=2THENR=3:Z=65100
89 RETURN
90 P=PEEK(P(L(R))+I):IFP=207THENP=(P(L(R))+I)-MC:GOTO95
91 P=PEEK(P(L(R))+L):IFP=207THENP=(P(L(R))+L)-MC:GOTO95
92 P=PEEK(P(L(R))+J):IFP=207THENP=(P(L(R))+J)-MC:GOTO95
93 P=PEEK(P(L(R))+K):IFP=207THENP=(P(L(R))+K)-MC:GOTO95
95 FORT=2TO3:IFINT(L(T))=PTHENR(T)=1:SOUND1,1:SOUND100,1:SOUND1,1:SOUND100,1:SOUND1,1:PRINT@L(T),CHR$(143);:SOUND100,1
96 NEXT:SC=SC+90:A$=STR$(SC):X=13:Y=15:MN=MN+1:GOSUB88:NEXT:NEXT:GOTO40
100 FORT=1TO3:ONR(T)GOTO110,110:PRINT@L(T),A$(D(T));:PRINT@L(T)+G,B$(D(T));
110 NEXT:RETURN
150 PRINT@(G*Y)+X+1,A$;:FORT=1TOLEN(A$):Q=MC+(G*Y)+X+T:POKEQ,PEEK(Q)-64:NEXT:RETURN
160 IF(LV/3)-INT(LV/3)=0THENPRINT@32,"YOU EARNED A BONUS DEMON RIDER!":SOUND200,5:SOUND1,1:SOUND200,3:LF=LF-1:IFLF<0THENLF=0
170 FORT=1TO2500:NEXT
180 TM=TM-(LV*2):IFTM<30THENTM=30
190 RETURN
1000 DIMA$(12),B$(9),S(40),W(3),B$,I$,NM$,SC,HS,TM,RR:GOSUB3000:D=48:FORT=1TO57:K(T)=3:NEXT
1005 A$(1)=CHR$(143)+CHR$(132)+"-"
1010 B$(1)=CHR$(131)+CHR$(143+16)+CHR$(130)
1020 A$(2)=CHR$(143)+CHR$(143+16)+"-"
1030 B$(2)=CHR$(131)+CHR$(131)+CHR$(130)
1040 A$(3)="-"+CHR$(136)+CHR$(143)
1050 B$(3)=CHR$(129)+CHR$(143+16)+CHR$(131)
1060 A$(4)="-"+CHR$(143+16)+CHR$(143)
1070 B$(4)=CHR$(129)+CHR$(131)+CHR$(131)
1100 A$(5)=CHR$(143)+CHR$(132)+"-"
1110 B$(5)=CHR$(131)+CHR$(143+D)+CHR$(130)
1120 A$(6)=CHR$(143)+CHR$(143+D)+"-"
1130 B$(6)=CHR$(131)+CHR$(131)+CHR$(130)
1140 A$(7)="-"+CHR$(136)+CHR$(143)
1150 B$(7)=CHR$(129)+CHR$(143+D)+CHR$(131)
1160 A$(8)="-"+CHR$(143+D)+CHR$(143)
1170 B$(8)=CHR$(129)+CHR$(131)+CHR$(131)
1171 A$(9)=CHR$(143)+CHR$(255)+CHR$(143)
1172 B$(9)=CHR$(255)+CHR$(255)+CHR$(255)
1173 A$(10)=CHR$(143)+CHR$(132)+"/"
1174 A$(11)=CHR$(134)+CHR$(134)+CHR$(134)
1175 A$(12)=CHR$(92)+CHR$(136)+CHR$(143)
1190 B$=CHR$(143)+CHR$(143)+CHR$(143):C(1)=138:C(2)=133:C(3)=135:C(4)=139
1210 MC=16384:C=17023:F=.05:G=32:A=.2:B=-.25:E=65000:TM=50:U=.02:S=10:V=20:W(1)=-.4:W(2)=-.3:W(3)=-.25:W=.35
1220 FORT=0TO511:P(T)=MC+T:NEXT
1230 H=-33:I=32-1:J=64-1:K=J+4:L=K-32:M=-1:N=3:O=-32+3:SC=0
1240 READX,Y:IFX=99THEN1255
1250 K(X)=Y:GOTO1240
1255 FORT=0TO40:READS(T):NEXT
1260 PRINT@480,"HIT space TO PLAY OR h FOR HELP";
1270 I$=INKEY$:RR=RND(1000):IFI$=""THEN1270
1280 IFI$="H"THEN9100
1299 GOSUB2000:GOTO20
2000 CLS1:FORT=0TO31:PRINT@T,CHR$(C(RND(4)));:NEXT:PRINT@0,CHR$(128);:PRINT@31,CHR$(128);
2010 FORT=0TO30:PRINT@480+T,CHR$(255);:NEXT:POKE16895,255
2020 FORT=10TO22:PRINT@480+T,CHR$(128);:NEXT:PRINT@480+10,CHR$(251);:PRINT@480+22,CHR$(247);
2025 FORT=10TO22:PRINT@448+T,CHR$(128);:NEXT:PRINT@448+9,CHR$(139);:PRINT@448+23,CHR$(135);
2030 RR=192+RND(128):IFRR>300THEN2080
2040 PRINT@RR,CHR$(139);:FORT=1TO14+RND(4):PRINTCHR$(128);:NEXT:PRINTCHR$(135);
2080 R=1:R(R)=0:X(R)=24:Y(R)=3:D(R)=3:GOSUB2095
2082 R=2:R(R)=0:X(R)=5:Y(R)=3:D(R)=5:GOSUB2095
2084 R=3:R(R)=0:X(R)=15:Y(R)=11:D(R)=5:GOSUB2095
2090 MN=0:X=13:Y=15:A$=STR$(SC):GOSUB150:RETURN
2095 L(R)=32*INT(Y(R))+X(R):F(R)=-F:A(R)=0:T(R)=0:RETURN
3000 CLS:IFSC>HSTHENINPUT"HIGH SCORE! ENTER NAME (8 CHAR) ";NM$:HS=SC
3010 NM$=LEFT$(NM$,8):CLS:PRINT
3020 PRINT"       â    à Å  Ç    â   â"
3030 PRINT"      à    âä Ü  ä   àå  âÖ"
3040 PRINT"      ä   â ä  Ü ä    ä   Ö"
3050 PRINT"    àåà  àååà   ÜÅ àååà   Ö"
3060 FORY=1TO10
3065 FORX=Y-1TO20-YSTEP2
3070 PRINT@(32*(Y+4))+6+X,"*";
3075 NEXT:NEXT
3085 PRINT@352+4,"BY:";:PRINT@384+4,"JIM";:PRINT@416+4,"GERRIE";
3086 PRINT@352+22,"HIGH:";:PRINT@384+21,HS;:PRINT@416+22,NM$;
3090 RETURN
9000 DATA 65,1,83,2,32,3,87,3,90,4
9010 DATA 241,1,242,1,131,1,135,1,139,1
9020 DATA 129,2,130,2,159,2,191,2
9030 DATA 255,3
9040 DATA 207,4
9050 DATA 128,5,138,5,133,5
9060 DATA 99,99
9070 DATA -1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-.9,-.8,-.7,-.6,-.5,-.4,-.3,-.2,-.1,0
9080 DATA .1,.2,.3,.4,.5,.6,.7,.8,.9,1,1,1,1,1,1,1,1,1,1,1
9100 CLS:PRINT"     * JOUST INSTRUCTIONS *"
9110 PRINT"HOLD a & s TO MOVE LEFT & RIGHT.";
9115 PRINT"spc=FLAP TO GAIN HEIGHT. z=HALT"
9120 PRINT"HEIGHT GAIN. COLLECT EGGS (=100"
9125 PRINT"POINTS). TIPS: A FLAP TAKES TIME";
9130 PRINT"TO REGISTER, SO TIME IT BEFORE"
9135 PRINT"MAKING YOUR ADVANCE. TO DEFEAT"
9140 PRINT"AN ENEMY OR COLLECT AN EGG, THEY";
9145 PRINT"MUST BE DOWN AND JUST IN FRONT"
9150 PRINT"OR BEHIND YOU. TAPPING a OR s"
9155 PRINT"REDUCES OR INCREASES SPEED IN
9160 PRINT"SMALL INCREMENTS. LURE ENEMIES"
9165 PRINT"INTO LAVA SPOUTS. BOUNCING OFF"
9170 PRINT"FLOORS CAN HELP, BUT FLAPPING"
9175 PRINT"AS YOU TOUCH CAN CAUSE BREAKS.";
9180 GOTO1260
