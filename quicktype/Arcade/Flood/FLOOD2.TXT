0 DIMC1,CC,M$,MC:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO110
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
110 DIM A(2000),B(299)
120 B(1)=1:B(2)=-1:B(3)=32:B(4)=-32
130 B(83)=1:B(65)=-1:B(87)=-32:B(90)=32
140 CLS:FORI=0TO31
150 POKEMC+I,42:POKEMC+480+I,42
160 POKEMC+INT(I/2)*32,42
170 POKEMC+31+INT(I/2)*32,42
180 NEXT
190 FORW=1TO2
200 A(W)=INT(RND(0)*416)+MC+32
210 IFPEEK(A(W))<>96THEN200
220 POKEA(W),175
230 NEXT
240 B=2:A=1:R=96
250 H=MC+256+16:POKEH,106
260 L=0
270 FORI=ATOB
280 FORJ=1TO4
290 IFPEEK(A(I)+B(J))<>96THEN310
300 L=L+1:A(B+L)=A(I)+B(J):POKEA(B+L),175
310 NEXT
320 GOSUB370
330 NEXT
340 A=B:B=B+L
350 IFL=0THENSC=419-B:PRINT@5,;:M$="SCORE"+STR$(SC):GOSUB7:GOTO500
360 GOTO260
370 R$=INKEY$+"  "
380 IFASC(R$)=13THENR=42
390 IFPEEK(H+B(ASC(R$)))<>96THENRETURN
400 POKEH,R
410 H=H+B(ASC(R$))
420 R=PEEK(H):POKEH,106
430 RETURN
500 PRINT@488,;:M$="PLAY AGAIN (Y/N)?":GOSUB7
510 IFSC>HSTHENHS=SC
520 PRINT@19,;:M$="HIGH"+STR$(HS):GOSUB7
530 R$=INKEY$:IFR$=""THEN530
540 IFR$="Y"THENL=0:GOTO140
550 IFR$="N"THENEND
560 GOTO530
600 REM COMPUTE!'S GAZETTE
610 REM NOVEMBER 1991
620 REM "FLOOD" BY GEZA LUCZ
630 REM MC-10 EDITS JIM GERRIE
640 REM A,W,S,Z TO MOVE
650 REM ENTER TO PLACE WALL
660 REM SAVE AS MUCH LAND
670 REM FROM THE FLOOD AS
680 REM YOUR CAN!
