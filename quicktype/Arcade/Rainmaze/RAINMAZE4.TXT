1 CLEAR500:POKE16925,0:POKE16926,1:GOTO20
2 SET(P,Y,1):SET(P,Y+1,1):RETURN
3 RESET(P,Y):RESET(P,Y+1):RETURN
20 DIM B,B1,B2,I,J,K,M,N,P,W,Y,Q,XX,YY,PK,P(200),Y(200),K(255)
30 SC(10)=6640:SC$(10)="JAMES GARON"
35 SC(9)=2180:SC$(9)="JIM GERRIE"
40 W=2:PK=17023:K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(13)=5
70 CLS0:PRINT"TIME: 1000        SCORE: 0      ";
80 PRINT@120+32,"RAINBOW";:PRINT@216+32,"MAZE";:PRINT@312+32,"SEARCH";
90 FORP=1TO61:FORY=4TO29STEP2:SET(P,Y,1):SET(P,Y+1,1):NEXT:NEXT
200 P=32:Y=10:RESET(P,Y):RESET(P,Y+1):P(0)=P:Y(0)=Y:K=1
210 IFPOINT(P+2,Y)=1THEN260
220 IFPOINT(P-2,Y)=1THEN260
230 IFPOINT(P,Y+4)=1THEN260
240 IFPOINT(P,Y-4)=1THEN260
250 GOTO350
260 ONRND(4)GOTO270,280,290,300
270 XX=0:YY=-1:IFPOINT(P,Y+(YY*4))=1THENRESET(P,Y+(YY*4)):RESET(P,Y+(YY*3)):RESET(P,Y+(YY*2)):RESET(P,Y+(YY*1)):GOTO330
275 GOTO260
280 XX=0:YY=1:IFPOINT(P,Y+(YY*4))=1THENRESET(P,Y+(YY*5)):RESET(P,Y+(YY*4)):RESET(P,Y+(YY*3)):RESET(P,Y+(YY*2)):GOTO330
285 GOTO260
290 XX=1:YY=0:IFPOINT(P+(XX*2),Y)=1THENRESET(P+(XX*2),Y):RESET(P+(XX*2),Y+1):RESET(P+XX,Y):RESET(P+XX,Y+1):GOTO330
295 GOTO260
300 XX=-1:YY=0:IFPOINT(P+(XX*2),Y)=1THENRESET(P+(XX*2),Y):RESET(P+(XX*2),Y+1):RESET(P+XX,Y):RESET(P+XX,Y+1):GOTO330
305 GOTO260
330 P=P+(XX*2):Y=Y+(YY*4):P(K)=P:Y(K)=Y:K=K+1:GOTO210
350 K=K-1:P=P(K):Y=Y(K):SET(P,Y,1):RESET(P,Y)
360 IFK=0THEN400
370 GOTO210
400 FORI=1TO26
410 P=RND(55)+4:Y=(RND(11)*2)+4:IFPOINT(P,Y)=1THENSET(P,Y,RND(6)+1):NEXT:GOTO600
415 GOTO410
600 T=1000:SC=0:FORI=1TO10:GOSUB3:SOUND100,1:GOSUB2:NEXT
610 GOSUB3:XX=P:YY=Y:T=T-1:PRINT@6,T;
620 IFT=0THEN800
630 GOSUB2:ONK(PEEK(PK)ANDPEEK(W))GOTO640,650,660,670,70
635 GOTO610
640 XX=XX-1:GOTO680
650 XX=XX+1:GOTO680
660 YY=YY-2:GOTO680
670 YY=YY+2:GOTO680
680 M=POINT(XX,YY):IFM=0THEN610
690 IFM>1THENSC=SC+40:PRINT@24,SC;:GOSUB3:GOSUB2:SOUND200,1
700 P=XX:Y=YY:IFSC<1000THEN610
800 GOSUB2:T=T*10:SC=SC+T:PRINT@0,"BONUS";:SOUND1,1
810 FORI=0TO17:PRINT@I," BONUS:";T;:SOUND1,2:NEXT
820 PRINT@0,"BONUS:";T;:PRINT@18,"SCORE:";SC;:SOUND25,5:SOUND50,5
2000 M=0:FORI=1TO10
2010 IFSC>SC(I) THENM=I
2020 NEXT:IFM=0THEN2100
2030 PRINT@72,"CONGRATUALTIONS!";
2040 PRINT@98,"YOUR SCORE IS IN THE TOP TEN";
2050 PRINT@133,"PLEASE ENTER YOUR NAME";
2060 SC$="":PRINT@233,"";:INPUT SC$:IFSC$=""THEN2060
2070 CLS:SC$=LEFT$(SC$,15)
2080 FORI=1TOM:SC(I-1)=SC(I):SC$(I-1)=SC$(I):NEXT
2090 SC(M)=SC:SC$(M)=SC$
2100 CLS:PRINT"TOP TEN SCORES":PRINT
2110 FORI=10TO1STEP-1
2120 IFSC$(I)>"" THEN PRINT SC$(I),SC(I):GOTO2130
2125 PRINT"..."," ..."
2130 NEXT
2140 PRINT"PRESS =ENTER= TO PLAY AGAIN"
2150 IF INKEY$<>CHR$(13)THEN2150
2160 GOTO70
