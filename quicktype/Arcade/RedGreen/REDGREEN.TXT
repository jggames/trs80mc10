5 DIM K(255),MC,R,P,NG,DL,PK,TI,W,BL$,D$
10 CLS:PRINT:PRINT" RED-GREEN GAME.":PRINT" INSTRUCTIONS? (Y/N)?"
15 GOSUB2000
20 A$=INKEY$:R=RND(1000):IFA$=""THEN20
22 IFA$="N"THEN30
24 IFA$="Y"THEN260
26 GOTO20
30 CLS4:R=0:W$=CHR$(207):C$=CHR$(191):BL$=CHR$(128)
40 A$=CHR$(128)+C$:D$=CHR$(128)+CHR$(128)+CHR$(128):E$=CHR$(128)+C$+CHR$(128)
50 W1$=CHR$(207)+CHR$(207)+CHR$(207):W2$=W$+C$+W$:DL=9
60 FORA=1TO15:B$=B$+A$:NEXT
70 TI=0:CLS0:FOR A=32TO416STEP64:PRINT@A,B$;:NEXTA
80 FORA=1TO3:R=RND(15)*2-1+RND(7)*64-32:POKEMC+R,143:NEXTA:NG=3
90 P=0
100 IFPEEK(P+33+MC)=143THENNG=NG-1:SOUND100,1
110 PRINT@P,W1$;:PRINT@P+32,W2$;:PRINT@P+64,W1$;
120 TI=TI+1:PRINT@480,NG;
130 IFNG=0THEN210
132 IFNG=10THEN220
140 R=RND(15)*2-1+RND(7)*64-32
150 IFRND(DL)=DLTHENIFPEEK(R+MC)<>143THENNG=NG+1:POKEMC+R,143:SOUND10,1
155 ONK(PEEK(PK)ANDPEEK(W))GOTO160,170,180,190:GOTO120
160 IFP>63THENPRINT@P,D$;:PRINT@P+32,BL$;:PRINT@P+34,BL$;:PRINT@P+64,D$;:P=P-64:GOTO100
162 GOTO120
170 IFP<349THENPRINT@P,D$;:PRINT@P+32,BL$;:PRINT@P+34,BL$;:PRINT@P+64,D$;:P=P+64:GOTO100
172 GOTO120
180 IF(P/64)<>INT(P/64)THENPRINT@P,D$;:PRINT@P+32,BL$;:PRINT@P+34,BL$;:PRINT@P+64,D$;:P=P-2:GOTO100
182 GOTO120
190 IF(P+36)/64<>INT((P+36)/64)THENPRINT@P,D$;:PRINT@P+32,BL$;:PRINT@P+34,BL$;:PRINT@P+64,D$;:P=P+2:GOTO100
200 GOTO120
210 SC=SC+(10-DL)*INT(100000/TI):DL=DL-1:IFDL=0THEN250
212 CLS4:PRINT@230,"DIFFICULTY LEVEL";10-DL;:PRINT@298,"SCORE";SC;:FORT=1TO800:NEXTT:GOTO70
220 CLS:PRINT"FINAL SCORE";SC
230 PRINT:PRINT"PLAY AGAIN (Y/N)?"
240 I$=INKEY$:IFI$="Y"THENRUN
242 IFI$="N"THENEND
244 GOTO240
250 CLS:PRINT"YOU DID THE IMPOSSIBLE.":PRINT"YOU CLEARED THE SCREEN AT":PRINT"DIFFICULTY LEVEL 9"
252 FORT=1TO2000:NEXTT:GOTO220
260 CLS:PRINT"RED-GREEN GAME BY JAMES W. WOOD"
265 PRINT"MC-10 VERSION BY JIM GERRIE":PRINT
270 PRINT"USE THE ARROW KEYS TO GUIDE THE"
272 PRINT"WHITE BOX. AS THE BOX MOVES":PRINT"ONTO A GREEN SQUARE, IT TURNS":PRINT"RED. AT THE SAME TIME, THE"
280 PRINT"COMPUTER IS CHANGING THE RED":PRINT"SQUARES BACK TO GREEN. CAN YOU"
282 PRINT"CHANGE THEM ALL TO RED BEFORE":PRINT"THE COMPUTER GETS TEN GREENS?
290 PRINT"IF YOU CAN, THE NEXT LEVEL OF":PRINT"PLAY BECOMES FASTER.":PRINT
292 PRINT"GOOD LUCK!":PRINT"PRESS (S) TO START THE GAME.";
300 IFINKEY$="S"THEN30
310 GOTO300
1999 REM INIT VARIABLES
2000 K(87)=1:K(90)=2:K(65)=3:K(83)=4
2010 W=2:PK=17023:MC=16384
2999 RETURN
