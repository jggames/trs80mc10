10 CLS:GOTO4000
500 OX=DX:OY=DY:DX=0:DY=0:C=0
510 IFPEEK(KP)THENIF4ANDNOTPEEK(UP)THENDY=-1:C=1
520 IFPEEK(KP)THENIF8ANDNOTPEEK(DN)THENDY=1:C=1
530 IFPEEK(KP)THENIF1ANDNOTPEEK(LT)THENDX=-1:C=1
540 IFPEEK(KP)THENIF4ANDNOTPEEK(RT)THENDX=1:C=1
550 IF C=0 THEN DX=OX:DY=OY
560 PX=PX+DX:PY=PY+DY
570 WX(HP)=PX:WY(HP)=PY:HP=HP+1
580 IF HP>EL THEN HP=0
590 PT=POINT(PX,PY):IF PT<1 THEN 620
600 IF PT=WC THEN 2000
610 GOSUB 1000
620 SET(PX,PY,WC)
630 RESET(WX(TP),WY(TP))
640 TP=TP+1:IF TP>EL THEN TP=0
650 GOTO 500
1000 SC=SC+PT:REM score
1010 SOUND T1,1
1020 PB=SW*INT(PY/2)+INT(PX/2)+UL
1030 POKEPB,BL
1040 PRINT@LL,"SCORE"SC,"CRASHES LEFT"CT-CS;
1050 EL=EL+1-(PT>3)-(PT>5)
1060 IF EL>EM THEN EL=EM
1100 TL=RND(RH)*SW+RND(RW)+UL
1110 IF PEEK(TL)/DV<>INT(PEEK(TL)/DV) THEN 1100
1120 IF TL=PB THEN 1100
1130 POKE TL,CL(PT)
1140 RETURN
2000 FOR I=1 TO 5:REM crash
2010 SET(PX,PY,WC)
2020 SOUND T2,1
2030 RESET(PX,PY)
2040 SOUND T3,1
2050 NEXT I
2060 CS=CS+1
2070 FOR I=0 TO EL
2080 WX(I)=0:WY(I)=0
2090 NEXT I
2100 IF CS=CT THEN 5000
2101 GOTO 4080
3000 CLS0:REM draw border
3010 FOR I=1 TO 62
3020 SET(I,1,WC):SET(I,28,WC)
3030 NEXT I
3040 FOR I=1 TO 31
3050 SET(1,I,WC):SET(62,I,WC)
3060 NEXT I
3070 RETURN
4000 DIM WX(205),WY(205):WC=8
4010 CT=5:UL=16384:LL=481:WC=8:BL=128:RW=30:RH=13:SW=32
4020 T1=176:T2=89:T3=58:EM=200:DV=16
4030 KP=2:UP=16952:DN=16947:LT=16946:RT=16948
4040 CL(1)=143:CL(2)=159:CL(3)=175:CL(4)=191:CL(5)=207:CL(6)=223:CL(7)=239
4060 GOSUB 3000:GOSUB 6000
4070 SC=0:CS=0
4080 EL=10:HP=9:TP=0
4090 PX=60:PY=27
4100 GOSUB 3000
4110 PRINT@LL,"SCORE"SC,"CRASHES LEFT"CT-CS;
4120 DX=0:DY=-1
4130 FOR PT=1 TO 7
4140 GOSUB 1100
4150 NEXT PT
4160 GOTO 500
5000 IF SC>HS THEN HS=SC
5010 CLS:PRINT@167,"YOUR SCORE WAS"SC;
5020 PRINT@231,"HIGH SCORE IS"HS;
5030 PRINT@295,"WANT TO PLAY AGAIN?";
5040 AN$=INKEY$:IF AN$="Y" THEN 4070
5050 IF AN$<>"N" THEN 5040
5060 CLS:END 
6000 TI$(0)="SLITHER":TI$(1)="slither"
6010 I=1:J=0:TL=RND(-(PEEK(9)*256+PEEK(10)))
6020 PRINT@481,"      NEED INSTRUCTIONS?      ";
6030 PRINT@205,LEFT$(TI$(J),I);
6040 I=I+1:IF I=8 THEN I=1:J=ABS(J-1)
6050 SOUND60+5*I-10*I*J,2
6060 AN$=INKEY$:IF AN$="N" THEN 6170 
6070 IF AN$<>"Y" THEN 6030
6080 CLS:PRINT@12,"SLITHER"
6090 PRINT" SCORE AS MANY POINTS AS YOU     CAN BEFORE YOU CRASH"CT"TIMES."
6100 PRINTTAB(10)CHR$(138)" "CHR$(133)," 1 POINT"
6110 FOR I=2 TO 7
6120 PRINTTAB(10)CHR$(138)CHR$(CL(I))CHR$(133),I"POINTS"
6130 NEXT
6140 PRINT" THE wasz KEYS CONTROL YOUR":PRINT" DIRECTION. YOU CAN ALSO MOVE"
6141 PRINT" DIAGONALLY.  THE 'SNAKE' GETS":PRINT" LONGER AFTER EACH TARGET HIT."
6150 PRINT@485,"PRESS "CHR$(91)"enter"CHR$(93)" TO BEGIN";
6160 IF INKEY$<>CHR$(13) THEN 6160
6170 RETURN
7001 REM      SLITHER
7002 REM DONALD L. MCGARRY
7003 REM 212 JOHNSON STREET
7004 REM CENTERPORT, NY 11721
