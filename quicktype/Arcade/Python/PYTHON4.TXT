10 CLEAR1000:DIMD(4),NG$,PE,O,N,P,C,D:M=16384
20 NG$="***********"
40 CLS:PRINT@42,NG$:PRINT@74,"P Y T H O N":PRINT@106,NG$:PRINT@162,"CREATED BY: ANDREW SIDDELEY"
41 PRINTTAB(7);CHR$(91);"FRI/10/SEPT/82";CHR$(93):PRINT@259,"MC-10 EDITS BY JIM GERRIE":PRINT:PRINT
45 REM INITIALIZATION
50 DIML(20),C(20),TR(160),A$(20)
60 D(1)=-32:D(2)=32:D(3)=-1:D(4)=1
70 P$(1)=CHR$(191):P$(2)=CHR$(207):P$(3)=CHR$(159):P$(4)=CHR$(255):P$(5)=CHR$(239):P$(6)=CHR$(223)
80 REM COMPOSE MAZE
90 FORT=1TO15
100 A=A+1
110 READL(A),C(A)
120 IFL(A)=99THEN150
130 FORV=1TOL(A):A$(T)=A$(T)+CHR$(C(A)):NEXT
140 GOTO100
150 A=A-1:IFA<1THENA=0:NEXTT:GOTO180
160 FORV=1TOL(A):A$(T)=A$(T)+CHR$(C(A)):NEXT
170 GOTO 150
180 REM SELECT LEVEL OF PLAY
190 PRINTTAB(3)"ENTER LEVEL OF DIFFICULTY":PRINTTAB(6);CHR$(91);"HARD(1)-EASY(8)";CHR$(93);:INPUTDF:IFDF<1ORDF>8THENDF=4
200 DL=DF
210 PRINTTAB(4)"ENTER LENGTH OF PYTHON":PRINTTAB(4)CHR$(91);"10-150";CHR$(93);:INPUTLP:IFLP<10ORLP>150THENLP=100
215 REM MENU
220 CLS:PRINT@42,"P Y T H O N":PRINT
230 PRINT" D)EMO":PRINT" I)NSTRUCTIONS":PRINT" C)HANGE LEVEL":PRINT" S)TART"
235 IFSC>HSTHENHS=SC
236 PRINT@449,"high score:";HS:SC=0:PC=0:SN=0:PRINT@224,;
240 K$=INKEY$
250 IFK$="D"THEN280
260 IFK$="S"THEN690
265 IFK$="I"THEN1000
267 IFK$="C"THEN180
270 GOTO240
275 REM DRAW MAZE
280 CLS:FORT=1TO15:PRINTA$(T);:NEXTT:GOSUB770
290 P=230:D=4:ST=0:EN=1
300 FORA=1TOLP:TR(A)=P:NEXTA
310 IFK$="D"THEN 810
320 REM MAIN PROGRAM LOOP
330 FORC=1TODL:C$=INKEY$:IFC$="D"THENO=D:D=4
360 IFC$="A"THENO=D:D=3
370 IFC$="W"THENO=D:D=1
380 IFC$="S"THENO=D:D=2
390 NEXT:N=D:F=.:PE=PEEK(M+P+D(N)):IFPE>128THENN=O:PE=PEEK(M+P+D(N)):GOTO410
400 O=.
410 IFPE>128THENF=1:D=.:O=.:N=.:GOTO430
420 IFPE=110THENSC=SC+1:IFSC=OBTHENPRINT@P+D(D),P$(PC);:GOTO540
430 P=P+D(N)
440 PRINT@P,P$(PC);
450 IFF=1THEN480
460 ST=ST+1:IFST>LPTHENST=1
470 TR(ST)=P
480 EN=EN+1:IFEN>LPTHENEN=1
490 PRINT@TR(EN)," ";
500 IFST=ENTHEN910
510 GOSUB780
520 GOTO330
530 REM END MAIN CYCLE
535 REM CALCULATE PYTHON LEFT AND AWARD BONUS PTS
540 EN=EN+1:IFEN>LPTHENEN=1
550 SOUND129,1:PRINT@TR(EN)," ";
560 SC=SC+(11-DL):GOSUB780
570 IFEN<>STTHEN540
580 TM=RND(2)
590 GOTO650
600 C1=1:FORC2=1TO2
610 SOUND113,4*C1:FORV=1TO8*C1:NEXT:SOUND113,4*C1:FORV=1TO8*C1:NEXT:SOUND142,2*C1:FORV=1TO8*C1:NEXT:NEXT
620 SOUND113,1*C1:FORV=1TO4*C1:NEXT:SOUND113,4*C1:FORV=1TO16*C1:NEXT:SOUND129,1*C1:FORV=1TO4*C1:NEXT
621 SOUND149,4*C1:FORV=1TO16*C1:NEXT:SOUND149,4*C1:FORV=1TO16*C1:NEXT:SOUND149,1:FORV=1TO16*C1:NEXT
622 SOUND142,4*C1:FORC1=1TO4*C1:SOUND129,2*C1:FORV=1TO8*C1:SOUND113,2*C1:SOUND142,2*C1:FORV=1TO2*C1:NEXT
630 SOUND113,2*C1:FORV=1TO32*C1:NEXT:SOUND113,2*C1:FORV=1TO16*C1:NEXT:SOUND129,2*C1:FORV=1TO32*C1:NEXT
631 SOUND149,2*C1:FORV=1TO32*C1:NEXT:SOUND149,2*C1:FORV=1TO16*C1:NEXT:SOUND142,1*C1:FORV=1TO2*C1:NEXT
640 SOUND129,2*C1:FORV=1TO8*C1:NEXT:SOUND113,2*C1:FORV=1TO8*C1:NEXT:SOUND25,2*C1:SOUND38,2*C1:SOUND25,2*C1
641 SOUND113,2*C1:FORV=1TO8*C1:NEXT:FORC2=1TO3:SOUND113,4*C1:FORV=1TO16*C1:NEXT:NEXT:RETURN
645 REM PLAY MELODIE
650 GOSUB600
655 REM INTERLUDE
660 CLS:PRINTTAB(3);"YOU FINISHED THIS SCREEN!!"
665 REM INCREASE LEVEL DIFFICULTY  AND SHORTEN PYTHON
670 LP=LP-20:IFLP<10THENLP=10
680 DF=DF-.5:DL=INT(DF):IFDL<1THENDF=1:DL=1
690 OB=SC+229:SN=SN+1
700 PC=PC+1:IFPC>6THENPC=1
710 SOUND113,2:SOUND129,2:SOUND142,2:SOUND149,2:SOUND161,2:SOUND87,2:SOUND103,2
711 PRINT:PRINT" score:";SC:PRINT:PRINT" length of python:"LP:PRINT:PRINT" screens: ";
720 FORT=1TOSN
721 SOUND87,2:SOUND25,2:SOUND64,2
722 PRINTP$(PC);:PRINT" ";:FORTI=1TO115:NEXTTI,T
730 SOUND103,2:SOUND87,2:SOUND161,2:SOUND149,2:SOUND142,2:SOUND129,2:SOUND113,2
740 FORT=1TO1150:NEXTT
750 GOTO280
760 REM PRINT SCORE SUBROUTINE
770 PRINT@480,"SCORE:";
780 PRINT@488,SC;
790 RETURN
800 REM DEMO PROGRAM
810 PRINT@480," ** PRESS ANY KEY TO RETURN **";
820 IFPEEK(M+P+D(D))>128THEND=RND(4):GOTO820
830 P=P+D(D)
840 PRINT@P,P$(RND(6));
850 ST=ST+1:IFST>10THENST=1
860 EN=EN+1:IFEN>10THENEN=1
870 TR(ST)=P
880 PRINT@TR(EN)," ";
890 IFINKEY$<>""THEN220
900 GOTO820
905 REM END OF DEMO PROGRAM
907 REM GAME OVER MESSAGE
910 FORT=25TO90STEP2:POKE49151,64:FORT1=1TOT:NEXTT1:SOUND87,1:NEXTT:FORV=1TO32:NEXT
911 SOUND87,10:FORV=1TO8:NEXT:SOUND64,10:FORV=1TO8:NEXT:SOUND53,12
920 PRINT@231,"TOO BAD, GAME OVER";
930 FORT=1TO2500:NEXTT
940 PRINT@224,"DO YOU WANT TO PLAY AGAIN "CHR$(91)"Y/N"CHR$(93)"?";
950 K$=INKEY$
960 IFK$="Y"THEN215
970 IFK$="N"THENEND
980 GOTO950
990 REM INSTRUCTIONS
1000 CLS:PRINT
1010 PRINT"      THE OBJECT OF THIS GAME    IS TO GUIDE THE PYTHON THROUGH  THE MAZE AND EAT UP ALL THE   "
1020 PRINT" DOTS BEFORE THE TAIL CATCHES    UP WITH THE HEAD,AND BLOWS THE  PYTHON TO TINY BITS."
1030 PRINT:PRINT"      THE CONTROLS ARE:"
1035 PRINT
1040 PRINTTAB(12);"W UP":PRINTTAB(12);"S DOWN":PRINTTAB(12);"A LEFT":PRINTTAB(12);"D RIGHT"
1050 PRINT:PRINT"   ** PRESS ENTER TO RETURN **";
1060 IFINKEY$=CHR$(13)THEN220
1061 GOTO1060
1090 REM DATA FOR MAZE
2000 DATA16,175,99,99
2010 DATA1,175,12,46,1,175,2,46,99,99
2020 DATA1,175,1,46,7,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2030 DATA1,175,7,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2040 DATA1,175,1,46,2,175,1,46,2,175,1,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2050 DATA1,175,1,46,2,175,1,46,2,175,1,46,1,175,6,46,1,175,99,99
2060 DATA1,175,4,46,2,175,1,46,1,175,1,46,6,175,99,99
2070 DATA4,175,12,46,99,99
2080 DATA1,175,4,46,2,175,1,46,1,175,1,46,2,175,1,46,3,175,99,99
2090 DATA1,175,1,46,2,175,1,46,2,175,1,46,1,175,6,46,1,175,99,99
2100 DATA1,175,1,46,2,175,1,46,2,175,1,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2110 DATA1,175,14,46,1,175,99,99
2120 DATA1,175,1,46,5,175,1,46,4,175,1,46,1,175,1,46,1,175,99,99
2130 DATA1,175,12,46,1,175,2,46,99,99
2140 DATA16,175,99,99
