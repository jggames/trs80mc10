10 CLEAR 1000:DIMNG$
20 NG$="***********"
40 CLS:PRINT@42,NG$:PRINT@74,"P Y T H O N"
41 PRINT@106,NG$:PRINT@258-96,"CREATED BY: ANDREW SIDDELEY":PRINT"  ";CHR$(91);"FRI/10/SEPT/82";CHR$(93):PRINT
42 PRINT@258,"MC-10 EDITS BY JIM GERRIE":PRINT:PRINT
45 REM INITIALIZATION
50 DIML(20),C(20),TR(160),A$(20)
60 DV(1)=-32:DV(2)=32:DV(3)=-1:DV(4)=1:DV(5)=0
70 P$(1)=CHR$(191):P$(2)=CHR$(207):P$(3)=CHR$(159):P$(4)=CHR$(255):P$(5)=CHR$(239):P$(6)=CHR$(223)
80 REM COMPOSE MAZE
90 FOR T=1 TO 15
100 A=A+1
110 READ L(A),C(A)
120 IFL(A)=99 THEN 150
130 FORV=1TOL(A):A$(T)=A$(T)+CHR$(C(A)):NEXT
140 GOTO100
150 A=A-1:IF A<1 THEN A=0:NEXT T:GOTO180
160 FORV=1TOL(A):A$(T)=A$(T)+CHR$(C(A)):NEXT
170 GOTO 150
180 REM SELECT LEVEL OF PLAY
190 PRINT"ENTER LEVEL OF DIFFICULTY       ";CHR$(91);"HARD(1)-EASY(8)";CHR$(93);" ";:INPUTDF:IF DF<1 OR DF>8 THEN DF=4
200 DL=DF
210 PRINT"ENTER LENGTH OF PYTHON ";CHR$(91);"10-150";CHR$(93);" ";:INPUTLP:IF LP<10 OR LP>150 THEN LP=100
215 REM MENU
220 CLS:PRINT@74,"P Y T H O N":PRINT
230 PRINT" D)EMO":PRINT" I)NSTRUCTIONS":PRINT" C)HANGE LEVEL":PRINT" S)TART"
240 K$=INKEY$
250 IFK$="D"THEN 280
260 IFK$="S"THEN 690
265 IFK$="I"THEN 1000
267 IFK$="C"THEN 180
270 GOTO 240
275 REM DRAW MAZE
280 CLS:FOR T=1 TO 15:PRINTA$(T);:NEXTT:GOSUB770
290 P=230:DI=4:ST=0:EN=1
300 FOR A=1 TO LP:TR(A)=P:NEXTA
310 IF K$="D" THEN 810
320 REM MAIN PROGRAM CYCLE
330 FORC=1TODL
340 C$=INKEY$
350 IFC$="S"THENDI=4
360 IFC$="A"THENDI=3
370 IFC$="W"THENDI=1
380 IFC$="Z"THENDI=2
390 NEXT
400 PE=PEEK(16384+P+DV(DI))
410 IF PE=110 THEN SC=SC+1:IF SC=OB THEN PRINT@P+DV(DI),P$(PC);:GOTO540
420 IF PE>128 THEN F=1:DI=5:GOTO430
421 F=0
430 P=P+DV(DI)
440 PRINT@P,P$(PC);
450 IF F=1 THEN 480
460 ST=ST+1:IF ST>LP THEN ST=1
470 TR(ST)=P
480 EN=EN+1:IF EN>LP THEN EN=1
490 PRINT@TR(EN)," ";
500 IF ST=EN THEN 910
510 GOSUB 780
520 GOTO 320
530 REM END MAIN CYCLE
535 REM CALCULATE PYTHON LEFT AND AWARD BONUS PTS
540 EN=EN+1:IF EN>LP THEN EN=1
550 SOUND100,1:PRINT@TR(EN)," ";
560 SC=SC+10:GOSUB780
570 IF EN<>ST THEN 540
580 TM=RND(2)
590 GOTO650
600 SOUND32,1:FORV=1TO8:NEXT:SOUND32,1:SOUND96,2:FORV=1TO16:NEXT
610 SOUND32,1:FORV=1TO8:NEXT:SOUND32,1:FORV=1TO8:NEXT:SOUND96,2:FORV=1TO16:NEXT
620 SOUND32,3:FORV=1TO16:NEXT:SOUND32,1:FORV=1TO2:NEXT:SOUND64,3:FORV=1TO16:NEXT:SOUND128,1:FORV=1TO4:NEXT:SOUND128,1
621 FORV=1TO16:NEXT:SOUND64,2:FORV=1TO8:SOUND32,1:SOUND96,1:FORV=1TO16:NEXT
630 SOUND32,1:FORV=1TO2:NEXT:SOUND32,1:FORV=1TO24:NEXT:SOUND64,1:FORV=1TO2:SOUND64,2:FORV=1TO24:NEXT:SOUND128,1:SOUND96,4
640 SOUND64,2:FORV=1TO8:NEXT:SOUND32,1:SOUND16,1:SOUND1,1:SOUND16,1:SOUND32,1:FORV=1TO2:NEXT:SOUND32,1:RETURN
645 REM PLAY MELODIE
650 GOSUB600
655 REM INTERLUDE
660 CLS:PRINTTAB(3);"YOU FINISHED THIS SCREEN !!"
665 REM INCREASE LEVEL DIFFICULTY  AND SHORTEN PYTHON
670 LP=LP-20:IF LP<10 THEN LP=10
680 DF=DF-.5:DL=INT(DF):IF DL<1 THEN DF=1:DL=1
690 OB=SC+229:SN=SN+1
700 PC=PC+1:IF PC>6 THEN PC=1
710 SOUND32,3:SOUND64,3:SOUND96,3:SOUND128,3:SOUND160,3:SOUND1,3:SOUND16,3:
711 PRINT:PRINT" score:";SC:PRINT:PRINT" length of python:"LP:PRINT:PRINT" screens: ";
720 FOR T=1 TO SN:
721 SOUND64,2:SOUND1,2:SOUND32,2
722 PRINTP$(PC);:PRINT" ";:FOR TI=1 TO 100:NEXT TI,T
730 SOUND16,3:SOUND1,3:SOUND160,3:SOUND128,3:SOUND96,3:SOUND64,3:SOUND32,3
740 FOR T=1 TO 1000:NEXT T
750 GOTO 280
760 REM PRINT SCORE SUBROUTINE
770 PRINT@480,"SCORE:";
780 PRINT@488,"";:PRINTSC;
790 RETURN
800 REM DEMO PROGRAM
810 PRINT@480," ** PRESS ANY KEY TO RETURN **";
820 IF PEEK(16384+P+DV(DI))>128 THEN DI=RND(4):GOTO820
830 P=P+DV(DI)
840 PRINT@P,P$(RND(6));
850 ST=ST+1:IF ST>10 THEN ST=1
860 EN=EN+1:IF EN>10 THEN EN=1
870 TR(ST)=P
880 PRINT@TR(EN)," ";
890 IF INKEY$<>"" THEN 220
900 GOTO 820
905 REM END OF DEMO PROGRAM
907 REM GAME OVER MESSAGE
910 FOR T=1 TO 90 STEP 2
911 POKE49151,64:FOR T1=1 TO T:NEXT T1
912 SOUND32,1
913 NEXTT
914 SOUND96,10:SOUND64,10:FORV=1TO4:NEXT:SOUND1,10
920 PRINT@231,"TOO BAD, GAME OVER";
930 FOR T=1 TO 1500:NEXTT
940 PRINT@224,"DO YOU WANT TO PLAY AGAIN ";CHR$(91);"Y/N";CHR$(93);"?";
950 K$=INKEY$
960 IF K$="Y" THEN CLS:SC=0:PC=0:SN=0:GOTO215
970 IF K$="N" THEN CLS:END
980 GOTO 950
990 REM INSTRUCTIONS
1000 CLS:PRINT
1010 PRINT"      THE OBJECT OF HIS GAME     IS TO GUIDE THE PYTHON THROUGH  THE MAZE AND EAT UP ALL THE   "
1020 PRINT" DOTS BEFORE THE TAIL CATCHES    UP WITH THE HEAD,AND BLOWS THE  PYTHON TO TINY BITS."
1030 PRINT:PRINT"      THE CONTROLS ARE :"
1035 PRINT
1040 PRINTTAB(12);"W UP":PRINTTAB(12);"Z DOWN":PRINTTAB(12);"A LEFT":PRINTTAB(12);"S RIGHT"
1050 PRINT:PRINT"   ** PRESS ENTER TO RETURN **";
1060 IF INKEY$=CHR$(13) THEN 220
1061 GOTO1060
1090 REM DATA FOR MAZE
2000 DATA 16,175,99,99
2010 DATA 1,175,12,46,1,175,2,46,99,99
2020 DATA 1,175,1,46,7,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2030 DATA 1,175,7,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2040 DATA 1,175,1,46,2,175,1,46,2,175,1,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2050 DATA 1,175,1,46,2,175,1,46,2,175,1,46,1,175,6,46,1,175,99,99
2060 DATA 1,175,4,46,2,175,1,46,1,175,1,46,6,175,99,99
2070 DATA 4,175,12,46,99,99
2080 DATA 1,175,4,46,2,175,1,46,1,175,1,46,2,175,1,46,3,175,99,99
2090 DATA 1,175,1,46,2,175,1,46,2,175,1,46,1,175,6,46,1,175,99,99
2100 DATA 1,175,1,46,2,175,1,46,2,175,1,46,1,175,1,46,2,175,1,46,1,175,1,46,1,175,99,99
2110 DATA 1,175,14,46,1,175,99,99
2120 DATA 1,175,1,46,5,175,1,46,4,175,1,46,1,175,1,46,1,175,99,99
2130 DATA 1,175,12,46,1,175,2,46,99,99
2140 DATA 16,175,99,99
