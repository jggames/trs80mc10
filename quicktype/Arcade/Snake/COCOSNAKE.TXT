0 X=RND(-TIMER):GOTO100
14 H=K(PEEK(135)):ONH GOTO250,250,250,250,245:GOTO410
100 CLS
110 REM SNAKE GAME
120 CLEAR 300
130 DIM X,Y,X1,Y1,S,T,H,OH,D,K,G,F,U
140 DIM D(31,9),V(5),W(5),K(255):K(94)=1:K(9)=2:K(10)=3:K(8)=4:K(32)=5
150 AH=10000
160 LE=3
170 INPUT"INPUT LEVEL (1-4)";LE
180 IF LE>4 OR LE<1 THEN 170
190 RESTORE:FOR I=1 TO 4:READ V(I),W(I):NEXT
200 GOSUB1340
210 AA=0:MAN=5:ME=0
220 X=30:Y=4:S=1
230 GOSUB920:T=1
240 H=0:POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:POKE340,255:ON1-((PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341)ANDPEEK(340))=255)GOTO14:GOTO410
245 ON1-(D(X+V(OH),Y+W(OH))=1)GOTO260:D(X+V(OH),Y+W(OH))=0:SOUND5,1:H=OH:PRINT@X+V(H)+32*(Y+W(H)-1)," ";:GOTO400
250 OH=H
260 PRINT@X+32*(Y-1)," ";:X=X+V(H):Y=Y+W(H):D=D(X,Y):OND+1 GOTO400,340,350,350
340 X1=X+V(H):Y1=Y+W(H):F=D(X1,Y1):ONF GOTO350,350,350:GOTO370
350 X=X-V(H):Y=Y-W(H):GOTO400
370 D(X,Y)=0:D(X1,Y1)=1:SOUND2,1:PRINT@X1+32*(Y1-1),CHR$(191);
400 PRINT@X+32*(Y-1),"Y";:SOUND40,1:PRINT@X+32*(Y-1),"X";
410 PRINT@S+32*(T-1)," ";:D(S,T)=0:ON1-(K>0)GOTO440:K=K-1:IFD(S+V(G),T+W(G))=0THEN500
440 K=INT(RND(0)*5)+2:U=0:G=RND(4)
470 OND(S+V(G),T+W(G))+1 GOTO500:G=(G-4*INT(G/4))+1:U=U+1:ONU GOTO470,470,470,710
500 S=S+V(G):T=T+W(G):PRINT@S+32*(T-1),"S";:D(S,T)=3:ON1-(X=S AND T=Y)GOTO540,570
540 TI=TI-1:ON1-(TI<0)GOTO550,570
550 PRINT@16+32*10,TI:GOTO240
570 FORI=40TO8 STEP-5:SOUNDI,1:PRINT@X+32*(Y-1),"@";:SOUND50,1:PRINT@X+32*(Y-1),"*";:NEXT
620 MAN=MAN-1
630 PRINT@16+32*9,MAN
640 IF MAN>0 THEN 220
650 IF AA>AH THEN AH=AA
670 GOSUB1070
680 PRINT@14*32+11,"GAME OVER"
690 PRINT@15*32+7,"PLAY AGAIN "+CHR$(91)+"Y/N"+CHR$(93)+"?";
700 FORI=1TO2500:A$=INKEY$:ON1-(A$<>"")GOTO705:ON1-(A$<>"Y")GOTO210:ON1-(A$<>"N")GOTO2000
705 NEXT:GOSUB1340:GOTO210
710 T=T-1:GOSUB1740
720 IF TI<101 THEN 780
730 SOUND80,10
740 TI=TI-100:AA=AA+LE*100
750 PRINT@16+32*11,AA
760 PRINT@16+32*10,TI
770 GOTO720
780 IF TI<11 THEN 840
790 SOUND40,5
800 TI=TI-10:AA=AA+LE*10
810 PRINT@16+32*11,AA
820 PRINT@16+32*10,TI
830 GOTO780
840 IF TI=0 THEN 900
850 SOUND20,1
860 TI=TI-1:AA=AA+LE
870 PRINT@32*11+16,AA
880 PRINT@32*10+16,TI
890 GOTO840
900 ME=ME+1:IF ME=10 THEN ME=7
910 GOTO220
920 CLS
930 FOR I=1 TO 30
940 FOR J=1 TO 8
950 D(I,J)=0
960 NEXT
970 NEXT
980 FOR I=0 TO 31:D(I,0)=2:D(I,9)=2:NEXT
990 FOR J=1 TO 8:D(0,J)=2:D(31,J)=2:NEXT
1000 TI=1000-ME*100
1010 FOR T=1 TO (5-LE)*10
1020 I=INT(RND(0)*28)+2:J=INT(RND(0)*6)+2
1030 PRINT@I+32*(J-1),CHR$(191);
1040 D(I,J)=1:SOUND3,1
1050 NEXT
1055 FORT=0TO31:PRINT@T+32*8,CHR$(128);:NEXT:FORT=0TO8:PRINT@31+32*T,CHR$(128);:PRINT@32*T,CHR$(128);:NEXT
1060 PRINT@X+32*(Y-1),"X";
1070 PRINT@9*32,"MEN";
1080 PRINT@10*32,"TIME";
1090 PRINT@11*32,"SCORE";
1100 PRINT@12*32,"HIGH SCORE";
1110 PRINT@16+32*9,MAN
1120 PRINT@16+32*11,AA
1130 PRINT@16+32*12,AH
1140 PRINT"  -==========SNAKE===========-":RETURN
1150 DATA 0,-1,1,0,0,1,-1,0
1340 CLS:GOSUB1140
1355 PRINT:PRINT"GAME OBJECTIVE:":PRINT
1360 PRINT"TO TRAP THE SNAKE WITH THE RED"
1361 PRINT"ROCK PILES WHILE AVOIDING BEING"
1362 PRINT"BITTEN BY THE SNAKE!"
1410 PRINT@32*11+25,CHR$(191);
1420 PRINT@32*9+5, " "CHR$(191)"     "CHR$(191);
1430 PRINT@32*10+5,CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)" "CHR$(191)CHR$(191);
1440 PRINT@32*11+5,CHR$(191);
1450 PRINT@32*12+5,CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)CHR$(191)" "CHR$(191)CHR$(191);
1460 PRINT@32*13+5," "CHR$(191)"     "CHR$(191)" ";
1470 K=1:T=3:S=12+(32*7):X=26:Y=11
1480 FOR X=30 TO 26 STEP -1
1490 PRINT@X+32*Y,"X ";:SOUND60,5:PRINT@X+32*Y,"Y ";
1510 PRINT@S+32*T," ";:T=T+K:K=-K
1520 PRINT@S+32*T,"S";
1530 NEXT
1540 FOR X=24 TO 13 STEP -1
1550 PRINT@X+32*Y,CHR$(191)"X ";:SOUND30,3:PRINT@X+32*Y,CHR$(191)"Y";
1560 PRINT@S+32*T," ";:T=T+K:K=-K
1570 PRINT@S+32*T,"S";
1580 NEXT
1590 FOR X=11 TO 6 STEP -1
1600 PRINT@X+32*Y,"S"CHR$(191)+"Y ";:SOUND90,3:PRINT@X+32*Y,"S"CHR$(191)+"X ";
1620 NEXT
1630 S=6+(32*7)
1640 GOSUB1740
1650 GOSUB1710:ON1-(A$<>"")GOTO1340
1680 CLS:GOSUB1140:PRINT:PRINT"FROM A NEC PC-8201A GAME"
1685 PRINT"MC-10 EDITS BY JIM GERRIE":PRINT:PRINT" -=======KEY OPERATION========-":PRINT
1690 PRINT"X(MAN)  "CHR$(91)"LEFT ARROW"CHR$(93)" "CHR$(91)"RIGHT ARROW"CHR$(93)
1695 PRINT"        "CHR$(91)"UP ARROW"CHR$(93)"   "CHR$(91)"DOWN ARROW"CHR$(93)
1700 PRINT"TO REMOVE CHR$(191)+ .... "CHR$(91)"SPACE KEY"CHR$(93);:GOSUB1710:ON1-(A$<>"")GOTO1340:RETURN
1710 PRINT@480,"PRESS ANY KEY TO CONTINUE";
1720 FORI=1TO2500:A$=INKEY$:ON1-(A$<>"")GOTO1725:I=2500
1725 NEXT
1730 RETURN
1740 FORI=1TO 10:SOUND30,1:PRINT@S+32*T,"S";:SOUND50,1:PRINT@S+32*T," ";:NEXT:RETURN
2000 END
