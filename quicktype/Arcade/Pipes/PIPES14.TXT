0 CLEAR200:GOSUB4000:GOTO800
1 RETURN
2 ON1-(X>0)GOTO1:PRINT@B(X,Y),C$(S(X,Y));:X=X-1:RETURN
3 ON1-(X<8)GOTO1:PRINT@B(X,Y),C$(S(X,Y));:X=X+1:RETURN
4 ON1-(Y>0)GOTO1:PRINT@B(X,Y),C$(S(X,Y));:Y=Y-1:RETURN
5 ON1-(Y<6)GOTO1:PRINT@B(X,Y),C$(S(X,Y));:Y=Y+1:RETURN
6 ONK(PEEK(A(X,Y)+M))GOTO1:S(X,Y)=P(P):PRINT@A(X,Y),A$(S(X,Y));:PRINT@B(X,Y),B$(S(X,Y));:P=P+1
7 PRINT@20,A$(P(P))A$(P(P+1))A$(P(P+2))A$(P(P+3))A$(P(P+4));:PRINT@52,B$(P(P))B$(P(P+1))B$(P(P+2))B$(P(P+3))B$(P(P+4));:RETURN
8 D=G:C=C+1:SOUND5,10:GOTO20
9 C=C+1:GOTO20
20 FORW=1TO25:FORZ=1TO64:PRINT@B(X,Y),C$;:ONK(PEEK(2)ANDPEEK(K))GOSUB2,3,4,5,6:PRINT@B(X,Y),C$(S(X,Y));:SOUNDC(Z),1
21 ONPOINT(A+X(D),B+Y(D))GOTO30,26,26,24,25,24,26,24:T=D:E=RND(2):D=N(T,E):ONPOINT(A+X(D),B+Y(D))GOTO30,26,26,24,25,24,26,24
23 D=M(T,E):ONPOINT(A+X(D),B+Y(D))GOTO30,26,26,24,25,24,26
24 A=A+X(D):B=B+Y(D):GOSUB55:SOUND147,8:SOUND123,8:SOUND89,8:T=20:GOSUB50:Z=64:NEXT:W=25:NEXT:GOTO40
25 S=S+L*10
26 A=A+X(D)*L:B=B+Y(D)*L:SET(A,B,3):NEXT:PRINT@119,INT(S);:ONCGOTO9,8:NEXT:GOTO60
30 A=A+X(D):B=B+Y(D):SET(A,B,3):SOUND89,4:SOUND125,4:SOUND133,4:SOUND147,16:Z=64:NEXT:W=25:NEXT:S=INT(S+(500*L))
31 GOSUB90:PRINT@32*5+21,"CONTINUE?";
32 I$=INKEY$:IFI$=""THEN32
33 IFI$="C"ORI$="Y"THEN100
34 IFI$<>"N"THEN32
40 GOSUB90:PRINT@32*5+20,"PLAY AGAIN?";:SOUND1,1
42 I$=INKEY$:IFI$=""THEN42
43 IFI$="Y"THENS=0:L=0:GOTO100
44 IFI$="N"THENEND
45 GOTO42
50 FORY=6TO0STEP-1:FORX=0TO8:PRINT@A(X,Y),A$(T);:PRINT@B(X,Y),B$(T);:NEXT:NEXT:RETURN
55 FORT=BTO29:SET(A,T,3):NEXTT:FORT=ATO2STEP-1:SET(T,29,3):NEXT:FORT=ATO37:SET(T,29,3):NEXT:RETURN
60 PRINT@480,"TOO SLOW! YOU'RE FIRED!";:SOUND1,20:GOTO40
70 PRINT@32*5+20,"LEVEL (1-9)?";:SOUND1,1:GOSUB94
75 I$=INKEY$:IFI$=""THEN75
80 IFVAL(I$)>0THENL=VAL(I$)/10:S=0:RETURN
85 GOTO75
90 PRINT@32*3+20,MID$(STR$(S)+" @ LV"+STR$(L*10),2,12);:IFS>HSTHENHS=S
93 RETURN
94 PRINT@32*3+20,"HIGH:";STR$(HS);:RETURN
95 PRINT@32*5+20,B$;:PRINT@32*5+23,"LV";INT(L*10);:RETURN
100 GOSUB3000:FORY=0TO6:FORX=0TO8:S(X,Y)=0:NEXT:NEXT:T=0:GOSUB50
110 FORX=1TO5:P(X)=RND(11):NEXT:P=1:GOSUB7
120 GOSUB300:FORX=2TO37:SET(X,1,6):NEXT:FORY=1TO29:SET(38,Y,6):NEXT:ON1-(L>0)GOSUB70:PRINT@116,B$;:PRINT@116,"GET READY!";
130 GOSUB95:FORX=6TO500:P(X)=RND(11):NEXT:P=1
170 X=4:Y=3:D=0:C=1:PRINT@116,B$;:GOTO20
300 ONRND(4)GOSUB310,320,330,340:GOSUB400:OX=X:OY=Y
301 X=RND(7):ONABS(X-OX)GOTO301,301
302 Y=RND(5):ONABS(Y-OY)GOTO302,302:ON1-(OX=XANDOY=Y)GOTO304,301
304 T=11+RND(4):GOSUB400:RETURN
310 T=16:X=RND(7):Y=RND(5):GOSUB400:A=(X*4)+4:B=(Y*4)+3:G=1:RETURN
320 T=17:X=RND(7):Y=RND(5):GOSUB400:A=(X*4)+3:B=(Y*4)+3:G=2:RETURN
330 T=18:X=RND(7):Y=RND(5):GOSUB400:A=(X*4)+4:B=(Y*4)+4:G=4:RETURN
340 T=19:X=RND(7):Y=RND(5):GOSUB400:A=(X*4)+4:B=(Y*4)+3:G=3:RETURN
400 S(X,Y)=T:PRINT@A(X,Y),A$(S(X,Y));:PRINT@B(X,Y),B$(S(X,Y));:RETURN
800 DIMA$(20),B$(20),C$(20),A(8,6),B(8,6),C(64),K(255),S(8,6),P(500),X(4),Y(4),N(4,2),M(4,2),X,Y,A,B,P,D,T,C,E,L,M,G,S,Z,W,K,C$
900 D$=CHR$(128):C$=CHR$(255)
1000 A$(0)=CHR$(218)+CHR$(128):B$(0)=CHR$(219)+CHR$(211)
1001 A$(1)=CHR$(195)+CHR$(203):B$(1)=D$+CHR$(202)
1002 A$(2)=CHR$(195)+CHR$(195):B$(2)=D$+D$
1003 A$(3)=CHR$(195)+CHR$(202):B$(3)=D$+D$
1004 A$(4)=D$+CHR$(195):B$(4)=CHR$(192)+CHR$(202)
1005 A$(5)=CHR$(195)+CHR$(194):B$(5)=CHR$(192)+CHR$(202)
1006 A$(6)=CHR$(192)+CHR$(202):B$(6)=CHR$(192)+CHR$(202)
1007 A$(7)=CHR$(192)+CHR$(203):B$(7)=D$+D$
1008 A$(8)=CHR$(195)+CHR$(203):B$(8)=D$+CHR$(192)
1009 A$(9)=CHR$(195)+CHR$(195):B$(9)=CHR$(192)+CHR$(202)
1010 A$(10)=CHR$(195)+CHR$(202):B$(10)=D$+CHR$(202)
1011 A$(11)=D$+CHR$(203):B$(11)=D$+CHR$(202)
1012 A$(12)=CHR$(191)+CHR$(131):B$(12)=CHR$(191)+D$
1013 A$(13)=CHR$(131)+CHR$(191):B$(13)=D$+CHR$(191)
1014 A$(14)=CHR$(191)+CHR$(191):B$(14)=D$+CHR$(138)
1015 A$(15)=D$+CHR$(138):B$(15)=CHR$(191)+CHR$(191)
1016 A$(16)=CHR$(175)+CHR$(195):B$(16)=CHR$(175)+CHR$(192)
1017 A$(17)=CHR$(195)+CHR$(175):B$(17)=CHR$(192)+CHR$(175)
1018 A$(18)=CHR$(175)+CHR$(175):B$(18)=CHR$(192)+CHR$(202)
1019 A$(19)=CHR$(192)+CHR$(202):B$(19)=CHR$(175)+CHR$(175)
1020 A$(20)=CHR$(175)+CHR$(175):B$(20)=CHR$(175)+CHR$(175)
2090 FORX=0TO19:C$(X)=LEFT$(B$(X),1):NEXT
2100 X=RND(-(PEEK(9)*256+PEEK(10))):FORX=0TO8:FORY=0TO6:A(X,Y)=(32*(Y*2)+(X*2))+33:B(X,Y)=A(X,Y)+32:NEXT:NEXT
2110 FORX=1TO64:READC(X):NEXT:FORX=1TO12:B$=B$+CHR$(128):NEXT
2120 K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(13)=5:K(32)=5:M=16385:K=17023:T=128+32:GOSUB2150:T=128+48:GOSUB2150:T=129:GOSUB2150
2130 N(1,1)=3:N(2,1)=4:N(3,1)=1:N(4,1)=2:N(1,2)=4:N(2,2)=3:N(3,2)=2:N(4,2)=1
2135 M(1,1)=4:M(2,1)=3:M(3,1)=2:M(4,1)=1:M(1,2)=3:M(2,2)=4:M(3,2)=1:M(4,2)=2
2136 X(1)=1:Y(1)=0:X(2)=-1:Y(2)=0:X(3)=0:Y(3)=-1:X(4)=0:Y(4)=1:
2137 E$=CHR$(177)+CHR$(183)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(187)
2138 F$=CHR$(128)+CHR$(151)+CHR$(157)+CHR$(159)+CHR$(157)+CHR$(155)
2139 G$=CHR$(128)+CHR$(157)+CHR$(159)+CHR$(151)+CHR$(159)+CHR$(158)+CHR$(144)
2140 H$=CHR$(128)+CHR$(135)+CHR$(191)+CHR$(143)+CHR$(191)+CHR$(139)
2141 M$=CHR$(135)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(138)
2142 J$=CHR$(159)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(187)+CHR$(180)+CHR$(157)
2143 K$=CHR$(128)+CHR$(191)+CHR$(191)+CHR$(188)+CHR$(187)+CHR$(183)+CHR$(156)
2144 L$=CHR$(215)+CHR$(223)+CHR$(218)+CHR$(128)+CHR$(213)+CHR$(223)+CHR$(219)
2149 GOSUB2160:GOTO100
2150 FORX=TTOT+15:K(X)=1:NEXT:RETURN
2160 PRINT@485,"PRESS ANY KEY TO BEGIN";
2170 IFINKEY$=""THEN2170
2180 RETURN
2200 DATA 135,155,176,135
2210 DATA 149,166,181,155
2220 DATA 153,176,189,176
2230 DATA 166,181,196,181
2240 DATA 95,129,149,129
2250 DATA 103,135,155,135
2260 DATA 121,149,166,149
2270 DATA 196,206,217,206
2280 DATA 206,203,196,189
2290 DATA 166,176,181,189
2300 DATA 206,203,196,206
2310 DATA 212,206,203,217
2320 DATA 219,217,212,206
2330 DATA 203,206,212,217
2340 DATA 212,206,203,196
2350 DATA 206,203,196,189
2360 DATA 181,155,135,121
2370 DATA 103,135,149,155
2380 DATA 166,181,176,196
2390 DATA 189,203,212,217
2400 DATA 219,227,232,231
2410 DATA 227,219,223,212
2420 DATA 206,196,189,181
2430 DATA 176,155,135,103
3000 CLS0
3010 PRINT@244,"pipe"CHR$(128)"frenzy";:PRINT@278,E$;:PRINT@310,F$;:PRINT@342,G$;:PRINT@374,H$;:PRINT@406,M$;
3070 PRINT@438,J$;:PRINT@470,K$;:PRINT@502,L$;:RETURN
4000 CLS:PRINT@32*3+11,"PIPE FRENZY":PRINT@32*5+10,"BY JIM GERRIE":PRINT@32*7+2,"FOR COCO CODING CONTEST 2013"
4010 PRINT:PRINT:PRINT" CONNECT THE PIPE FROM THE BLUE"
4020 PRINT" RESERVOIR TO THE GREEN INTAKE"
4030 PRINT" PIPE OF THE RED RESERVOIR.":RETURN
