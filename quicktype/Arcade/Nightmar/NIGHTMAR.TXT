0 CLS0:POKE49151,64:CLEAR1000:DIMZZ,CC,M$,MC:MC=16384:M$="NIGHTMARE PARK.":GOSUB7:X=32768:GOSUB9900:GOTO20
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
20 W=RND(TI):S$="                                      "
30 PRINT@32*2,;:M$="THE SHORTEST ROUTE MAY NOT BE":GOSUB7
35 PRINT@32*3,;:M$="THE BEST.":GOSUB7
36 PRINT@32*5,;:M$="YOU MAY ONLY MOVE N, S OR E.":GOSUB7
40 PRINT@32*7,;:M$="STAY ON THE PATH.":GOSUB7:PRINT@32*9,;:M$="YOU HAVE THREE LIVES.":GOSUB7
45 PRINT@32*11,;:M$="ABANDON HOPE ALL YE WHO":GOSUB7:PRINT@32*12,;:M$="ENTER...":GOSUB7
47 GOSUB9600
50 FORN=13TO15:PRINT@32*N+10,"NIGHTMARE PARK";:GOSUB9600:NEXT
110 P1=MC+32*6+2:P=P1:E3=0:Y1=0:R=0
270 GOSUB9000
275 W$=INKEY$:ZZ=RND(0):IFW$<>""THEN275
280 P=P1
285 W$=INKEY$:IFW$=""THEN285
290 IFW$="N"THENA=-32:GOTO330
300 IFW$="S"THENA=32:GOTO330
310 IFW$="E"THENA=1:GOTO330
320 GOTO275
330 P1=P+A:IFPEEK(P1)=128ORP1>MC+511THEN10000
340 IFPEEK(P1)=5THEN12000
350 POKEP,175:POKEP1,E3
352 X=INT(1+5*RND(0)):J=4
355 IFP1>MC+198ANDP1<212THENJ=3
357 IFX<JTHEN280
358 Y1=Y1+1
360 X=INT(1+15*RND(0))
365 IFX=7ANDY1>10THEN360
370 ONXGOTO1000,1500,2000,2500,3000,3500,4000,4500,5000,5500,6000,6500,7000,7500,8000
390 GOTO280

1000 B$=CHR$(95):W$="UNICORNS!":F=95
1002 CLS:PRINT"STAMPEDING ";W$:PRINT"PRESS a FOR LEFT, s FOR RIGHT."
1005 X=64:D=32*3+16+MC
1010 GOSUB9700:CLS:FORN=1TO20:A$=INKEY$:PRINT:NEXT:C=32*3+16+MC:POKEC,64
1020 FORN=1TOX:A=INT(3+6*RND(0)):POKEC,96:FORB=1TO2:PRINTLEFT$(S$,A)B$LEFT$(S$,A);:FORZZ=1TO5:NEXT
1022 NEXT:A$=INKEY$:IFA$="A"THEND=C-1
1023 IFA$="S"THEND=C+1
1025 POKEC,96:C=D:POKEC,64
1027 IFPEEK(C+32)=FTHENPOKEC,96:PRINT@511," ";:POKEC,191:GOTO1080
1030 NEXT:IFX=50THEN1070
1040 X=50:B$=" ":GOTO1020
1070 GOSUB9600:CLS:PRINT"SURVIVED!":GOSUB9600:GOSUB9810:GOTO270
1080 GOSUB9600:CLS:PRINT"TRAMPLED!":GOTO9800

1500 W$="BLABBERS!":F=15
1501 CLS:PRINT"SURROUNDED BY ";W$:PRINT"FIND NOS."
1502 PRINT"1-8 TO":PRINT"VANQUISH"
1503 E1=RND(8):E2=RND(8):E4=RND(8)
1504 D1=F:D2=F:D3=F
1510 POKE32*8+16+MC,64:C1=32*1+9:C2=32*1+16:C3=32*1+23:C4=32*8+9:C5=32*8+23:C6=32*15+9
1520 C7=32*15+16:C8=32*15+23:FORN=1TO6:POKEMC+C1,96:C1=C1+33:POKEMC+C1,D1:POKEMC+C2,96
1530 C2=C2+32:POKEMC+C2,D2:POKEMC+C3,96:C3=C3+31:POKEMC+C3,D3:POKEMC+C4,96:C4=C4+1
1540 POKEMC+C4,D1:POKEMC+C5,96:C5=C5-1:POKEMC+C5,D2:POKEMC+C6,96:C6=C6-31:POKEMC+C6,D3
1550 POKEMC+C7,96:C7=C7-32:POKEMC+C7,D2:POKEMC+C8,96:C8=C8-33:POKEMC+C8,D1
1555 IFC1=272THEN1700
1560 A$=INKEY$:IFA$=""THEN1560
1565 A=VAL(A$):IFA=E1THEND1=96
1570 IFA=E2THEND2=96
1580 IFA=E4THEND3=96
1590 IFD1=96ANDD2=96ANDD3=96THENN=10
1600 NEXT:IFN<10THEN1700
1610 CLS:PRINT"LUCKY!":GOSUB9600:GOTO270
1700 CLS:PRINT"SCROMP!!!":GOTO9800

2000 CLS:PRINT"THE MAD MATHEMATICIAN!!":PRINT"YOU HAVE 10 SECONDS TO ANSWER"
2005 PRINT"THE FOLLOWING QUESTION:"
2010 T=0:GOSUB9600:FORN=1TO3+RND(3):A=INT(1+9*RND(0)):T=T+A
2020 PRINTA"+";:NEXT:PRINTCHR$(8)"= ?":TI=USR(0):INPUT"WHAT IS YOUR ANSWER";A
2030 IFA<>TTHEN2060
2035 CLS:PRINT"CORRECT...":GOSUB9600
2040 TI=INT(USR(1)/64):PRINTMID$(STR$(TI),2)" SECONDS":IFTI>10THENPRINT"YOU TOOK TOO LONG!!!":GOTO2070
2050 PRINT"YOU HAVE CHEATED DEATH!":GOSUB9600:GOTO270
2060 CLS:PRINT"WRONG!!"
2070 PRINT"PREPARE TO DIE!!!":GOTO9800

2500 CLS:PRINT"DEATH RAYS! KEEP STILL AND HOPE!":FORN=1TO5:Z(N)=0:NEXT
2510 GOSUB9600:CLS0:POKE32*8+16+MC,E3:FORB=1TO5
2512 F=0:A=INT(14*RND(0)):FORA2=1TOB:IFA=Z(A2)THENA2=B:F=1
2515 NEXTA2:IFF=1THEN2512
2520 Z(B)=A:A1=A+16:FORN=0TO15:POKEMC+32*N+A,153
2522 A=A+1:FORX=1TO50:NEXTX:NEXTN
2525 IFPEEK(32*8+16+MC)<>E3THEN2550
2527 FORN=0TO15:POKEMC+32*N+A1,150:A1=A1-1:FORX=1TO50:NEXTX:NEXTN
2528 IFPEEK(32*8+16+MC)<>E3THEN2550
2540 NEXTB:GOSUB9600:CLS:PRINT"A NEAR THING!":GOSUB9600:GOTO270
2550 CLS:PRINT"ZAAAAP!":GOTO9800

3000 W$="LURGI!":F=42
3005 CLS:PRINT"MULTIPLYING ";W$:PRINT"YOU HAVE 1 MINUTE TO REACH THE":PRINT"EXIT! USE: a w s z"
3010 GOSUB9700
3020 B=32*14+26+MC:TI=USR(0):CLS0:POKEMC,5:POKEB,E3
3025 FORN=1TO16:A=INT(510*RND(0))+MC+1:IFPEEK(A)=E3THEN3025
3028 POKEA,F:NEXT:TI=INT(USR(1)/64):IFTI>60THEN3200
3030 FORN=1TO6
3035 A$=INKEY$:IFA$=""THEN3035
3040 POKEB,128:IFA$="W"THENB=B-32
3050 IFA$="Z"THENB=B+32
3060 IFA$="A"THENB=B-1
3070 IFA$="S"THENB=B+1
3080 IFPEEK(B)=FTHENPOKEB,191:GOTO3200
3085 IFB>MC+511THENB=B-511
3086 IFB<MCTHENB=B+511
3090 IFB=MCTHEN3190
3100 POKEB,E3:NEXT:GOSUB9810:GOTO3025
3190 CLS:PRINT"ESCAPED!!":GOSUB9600:GOTO270
3200 GOSUB9600:CLS:PRINT"SMITTEN BY ";W$:GOTO9800

3500 CLS6:PRINT"CRUSHER! 1 LETTER WILL STOP IT! ";:FORN=1TO15:PRINT@32*N+8,"ÐÐÐÐÐÐÐÐÐÐÐÐÐÐÐÐ";:NEXT:POKEMC+480+16,E3
3505 X=INT(65+25*RND(0))
3510 FORN=1TO14
3514 PRINT@N*32+8,"ÓÓÓÓ×ÓÓÓÓÓÓ×ÓÓÓÓ";
3515 IFN>20THEN3540
3520 A$=INKEY$:IFA$=""THEN3520
3530 IFASC(A$)=XTHEN3590
3540 PRINT@N*32+8,"ÐÐÐÐÕÐÐÐÐÐÐÕÐÐÐÐ";:NEXT:CLS:PRINT"CRUSHED!":GOTO9800
3590 CLS:PRINT"ESCAPED!!":GOSUB9600:GOTO270

4000 CLS:PRINT"BACK TO THE START!!!":P1=MC+32*6+2:P=P1:GOSUB9600:GOTO270

4500 CLS:PRINT"MANIACAL MEMORY TESTER!!":PRINT"MEMORISE THIS TO PACIFY HIM"
4510 GOSUB9700:FORN=1TO5:Z(N)=INT(65+26*RND(0)):NEXT:PRINT"...";
4520 FORN=1TO5:PRINTCHR$(Z(N));" ";:NEXT:FORN=1TO1500:NEXT:CLS
4530 FORN=1TO5:PRINT"WHAT WAS LETTER";N;
4535 A$=INKEY$:IFA$=""THEN4535
4537 PRINTA$:IFASC(A$)<>Z(N)THEN4560
4540 NEXT:CLS:PRINT"FLUKE!!":GOSUB9600:GOTO270
4560 PRINT"WRONG!! CURTAINS FOR YOU!":GOTO9800

5000 CLS:PRINT"HAIL OF BARBS!!":PRINT"PRESS a FOR LEFT, s FOR RIGHT."
5010 C=32*9+16+MC:GOSUB9600:CLS
5020 B$=" VVV  V   VV  VV     V  VV   VV    VVV V     VV   V V   VVV  V   VV    V  VV  VVV    VVV  VV  VV    VVVV  VV":POKEC,64
5040 CC=-1:A=RND(20):FORN=0TO14:A=A+2:IFA>50THENA=50
5045 PRINT@32*N,MID$(B$,A,32);:IFPEEK(C)=86THENCC=.
5046 A$=INKEY$:IFA$="A"ANDCCTHENIFC>16673THENPOKEC,96:C=C-1:GOTO5070
5060 IFA$="S"ANDCCTHENIFC<16704THENPOKEC,96:C=C+1
5070 IFPEEK(C)<>96THEN5080
5075 POKEC,64
5080 FORZZ=1TO250:NEXT:NEXT:PRINT@32*N,MID$(B$,A,31);
5090 IFPEEK(C)=86THEN5120
5100 GOSUB9600
5110 CLS:PRINT"MISSED!":GOSUB9600:GOTO270
5120 CLS:PRINT"EXTERMINATED!!":GOTO9800

5500 CLS:PRINT"THE EGOTISTICAL GORILLA!!":PRINT"TELL HIM HIS NAME OR BE"
5510 PRINT"DISMANTLED!!  A FLASH":PRINT"OF LIGHTNTNG REVEALS IT BRIEFLY"
5520 A=INT(1+4*RND(0)):IFA=1THENA$="CLARENCE"
5530 FORN=1TO6000:NEXT:IFA=2THENA$="PERCIVAL"
5540 IFA=3THENA$="STANLEY"
5550 IFA=4THENA$="WINSTON"
5555 A=INT(4+8*RND(0)):FORN=1TOA:PRINT:NEXT
5560 PRINTA$:FORN=1TO100:NEXT:CLS:PRINT"WHAT'S MY NAME ?":INPUTB$
5570 CLS:PRINT"DER...UM...ERR.....DAT IS......":GOSUB9600:IFB$<>A$THEN5590
5580 PRINT"GRRR! RIGHT!":GOSUB9600:GOTO270
5590 CLS:PRINT"WRONG!  RIP!...CRACK!...SQUASH!":GOTO9800

6000 CLS:PRINT"THE DOORS!!":PRINT"BEHIND ONE IS A STARVING LION!"
6010 FORN=1TO3:A$(N)="ßßßßß":NEXT:PRINTTAB(8);"1";TAB(14);"2";
6020 PRINTTAB(20);"3":A1=4:GOSUB6070:FORN=1TO3:A$(N)="     ":NEXT:PRINT
6030 PRINT"CHOOSE";
6035 A$=INKEY$:IFA$=""THEN6035
6037 X=VAL(A$):IFX<1ORX>3THEN6035
6038 A1=INT(1+3*RND(0)):E=1000
6040 GOSUB6070:GOTO6150
6070 A=6:FORB=1TO3:FORN=1TO7:PRINT@32*(N+3)+A*B,A$(B);
6080 IFN=3ANDB=A1THENA$(B)="LION!"
6090 IFN=4ANDA1<4THENA$(B)="     "
6100 FORC=1TO200:NEXT:NEXT:NEXT:RETURN
6150 IFA1<>XTHENPRINT"FORTUNE HAS SMILED ON YOU":GOSUB9600:GOTO270
6160 PRINT"CHOMP!":GOTO9800

6500 B$="^":W$="STINGERS!":F=94:GOTO1002

7000 W$="NAJJERS!":F=35:GOTO1501

7500 W$="ZIGZAGGERS!":F=26:GOTO3005

8000 CLS:PRINT"THE GRUESOME GAMBLER!!":PRINT"HE CHALLENGES YOU TO THROW A"
8005 PRINT"HIGHER OR LOWER DICE ROLL.":PRINT"UNFORTUNATELY, HE WON'T SAY"
8007 PRINT"WHICH UNTIL AFTER YOU THROW!":PRINT"YOUR LIFE IS THE STAKE."
8010 GOSUB9700:GOSUB9600:CLS:PRINT"HE THROWS..."
8020 A=INT(1+6*RND(0)):B=INT(1+6*RND(0)):PRINT"...";A;"  AND  ";
8022 GOSUB9600:PRINTB;:D=A+B:PRINT"   =  ";D:GOSUB9600
8025 PRINT"WILL YOU TRY FOR A HIGHER OR":PRINT"LOWER SCORE (H OR L)"
8030 A$=INKEY$
8040 IFA$="H"THENA=6:B=6:GOTO8050
8042 IFA$<>"L"THEN8030
8045 A=3:B=3
8050 A=INT(1+A*RND(0)):B=INT(1+B*RND(1)):PRINT"YOU THROW..."
8060 PRINT"...";A;"  AND  ";
8062 GOSUB9600:PRINTB;:A=A+B:PRINT"   =  ";A:PRINT"":GOSUB9600
8070 GOSUB9600:PRINT:PRINT"THE BET WAS THAT HE WOULD SCORE ";
8072 B=INT(1+2*RND(1)):IFB=2THENPRINT"LESS!":GOTO8100
8075 PRINT"MORE!":GOSUB9700:IFD>ATHEN8200
8080 GOTO8150
8100 GOSUB9700:IFD<ATHEN8200
8150 PRINT"CURSES!! YOU WIN!":GOSUB9600:GOTO270
8200 PRINT"AHA!! YOU LOSE!":GOTO9800

9000 CLS0
9010 PRINT"€€€€¯¯¯¯¯¯¯¯¯¯¯¯€nightmare€park€";
9020 PRINT"€€€€¯€€€¯€€€¯€€¯€€€€€€€€€€€€€€€€";
9030 PRINT"€€€€¯€€€¯€€€¯€€¯€€€€€€€€€€€€€€€€";
9040 PRINT"€€€€¯¯¯¯¯¯¯¯¯¯¯¯€€€€¯¯¯¯€€€€€€€€";
9050 PRINT"€€€€¯€€€¯€€€¯€€¯€€€€¯€€¯€€€€€€€€";
9060 PRINT"€€€€¯€€€¯€€€¯€€¯€€€€¯€€¯€€€€€€€€";
9070 PRINT"in¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯€€¯€€€€€€€€";
9080 PRINT"€€€€¯€€¯€€€€€¯€€€€€€¯¯¯¯¯¯¯¯exit";
9090 PRINT"€€€€¯€€¯€€€€€¯€€€€€€¯€€€¯€€€€€€€";
9091 PRINT"€€€€¯€€¯€€€€€¯€€€€€€¯€€€¯€€€€€€€";
9092 PRINT"€€€€¯€€¯€€€€€¯€€€€€€¯€€€¯€€€€€€€";
9110 PRINT"€€€€¯€€¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯€€€€€€€";
9120 PRINT"€€€€¯€€€€€€€€¯€€€¯€€¯€€€¯€€€€€€€";
9121 PRINT"€€€€¯€€€€€€€€¯€€€¯€€¯€€€¯€€€€€€€";
9122 PRINT"€€€€¯€€€€€€€€¯€€€¯€€¯€€€¯€€€€€€€";
9130 PRINT"€€€€¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯€€€€€€";
9140 PRINT@32*12+26,;:M$="N, S ":GOSUB7:PRINT@32*13+26,;:M$="OR E?":GOSUB7
9150 POKEP1,E3
9160 RETURN
9600 FORZZ=1TO2500:NEXT:RETURN
9700 FORZZ=1TO4000:NEXT:RETURN
9800 GOSUB9600:R=R+1:IFR=3THEN9808
9802 IFR=1THENPRINT:PRINT"YOU HAVE TWO LIVES LEFT!!"
9803 IFR=2THENPRINT:PRINT"YOU HAVE ONE LIFE LEFT!!!"
9804 GOSUB9600:GOTO270
9808 GOSUB9600:CLS:PRINT"NIGHTMARE PARK CLAIMS ANOTHER   VICTIM!!!"
9809 PRINT"    Ï":PRINT"   ÏÏÏ":PRINT"    Ï":PRINT"    Ï":GOSUB9810:GOTO9990
9810 FORN=1TO20:A$=INKEY$:NEXT:RETURN
9900 Z=16896:GOSUB9901:Z=X:GOSUB9901:EXECX:RETURN
9901 READ U$:FOR Y=1 TO LEN(U$) STEP 2:POKE Z,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
9902 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
9903 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
9990 PRINT:PRINT"TRY AGAIN? (Y/N)"
9991 M$=INKEY$:IFM$=""THEN9991
9992 IFM$="Y"THEN110
9993 IFM$="N"THENPOKE8,0:SOUND1,1:END
9994 GOTO9991
10000 CLS0:M$="YOU FELL INTO THE ABYSS!!":GOSUB7:GOTO9808
12000 CLS:PRINT"YOU BEAT THE PARK THIS TIME":PRINT"BUT NEXT TIME......"
12020 GOSUB9810:GOTO9890
12030 REM**NIGHTMARE PARK**
12040 REM****BOB CHAPPEL***
12050 REM**** 14/5/80 *****
