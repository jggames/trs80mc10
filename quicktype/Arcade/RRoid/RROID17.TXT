
1 CLS:GOSUB10000:CLEAR8000:POKE16925,0:POKE16926,1:GOSUB800:GOSUB8000:GOTO900
3 D=D-1-(D<2)*8:RETURN
4 D=D+1+(D>7)*8:RETURN
5 SET(S(X),T(Y),1):V=SGN(V+V(D)):W=SGN(W+W(D)):SOUND240,1:R=N:PRINT@P,S$;:PRINT@P+A,T$;:X=X+V+(X>29)*29-(X<2)*29:Y=Y+W+(Y>13)*13-(Y<1)*13:P=A*Y+X:IFPOINT(S(X),T(Y))THENGOSUB50
6 RETURN
11 ?@P(H),D$(H);:?@P(H)+A,E$(H);:?@P(H)+B,F$(H);:?@P(H),A$(H);:?@P(H)+A,B$(H);:?@P(H)+B,C$(H);:SOUND100,1:RETURN
12 D$(H)=B$(.):E$(H)=B$(.):F$(H)=B$(.):RETURN
13 IFFTHENRESET(I,J)
14 M=V(D)*2:L=W(D)*2:I=2*X+1:J=2*Y+1:F=1:RETURN
17 H(H)=H(H)-1:A$(H)=X$(H,H(H)):B$(H)=Y$(H,H(H)):C$(H)=Z$(H,H(H)):GOSUB11:Q=Q+1:F=.:ONH(H)GOTO12,2:A$(H)=X$(H,H(Z)):B$(H)=Y$(H,H(Z)):C$(H)=Z$(H,H(Z)):D$(H)=D$(Z):E$(H)=E$(Z):F$(H)=F$(Z)
18 H(H)=H(Z):P(H)=P(Z):GOSUB11:X(H)=X(Z):Y(H)=Y(Z):A$(Z)="":B$(Z)="":C$(Z)="":D$(Z)="":E$(Z)="":F$(Z)="":SOUND120,1:Z=Z-1:IFQ<24THENRETURN
19 GOTO60
20 FORE=1TO1000:FORT=1TOZ:O=P(T):X(T)=X(T)+A(T,X(T)):Y(T)=Y(T)+B(T,Y(T)):P(T)=A*INT(Y(T))+X(T):PRINT@O,D$(T);:PRINT@O+A,E$(T);:PRINT@O+B,F$(T);:PRINT@P(T),A$(T);:PRINT@P(T)+A,B$(T);:PRINT@P(T)+B,C$(T);:IFPOINT(S(X),T(Y))THENGOSUB50
23 R=R-1:IFR=1THENR=N:PRINT@P,S$;:PRINT@P+A,T$;:X=X+V+(X>29)*29-(X<1)*29:Y=Y+W+(Y>13)*13-(Y<1)*13:P=A*Y+X:IFPOINT(S(X),T(Y))THENGOSUB50
24 ONK(PEEK(K)ANDPEEK(2))GOSUB3,4,5,13,31:PRINT@P,S$(D);:PRINT@P+A,T$(D);:IFFTHENRESET(I,J):I=I+M:J=J+L:H=POINT(I,J):F=(I>1ANDI<62ANDJ>1ANDJ<30):IFFTHENSET(I,J,H):IFHTHENGOSUB17
25 NEXT:NEXT
26 IFSH>=3THENPRINT@0,"SHIP DESTROYED!";:GOSUB55:GOTO100
30 Q=0:GOSUB2000:GOTO20
31 S=S-1:IFS<1THENRETURN 
32 X=RND(25)+5:Y=RND(10)+3:C1=A*Y+X+16384:IFPEEK(C1)<>128ORPEEK(C1-1)<>128ORPEEK(C1+1)<>128ORPEEK(C1+2)<>128THEN32
33 IFPEEK(C1-32)<>128ORPEEK(C1+32)<>128ORPEEK(C1+33)<>128ORPEEK(C1-31)<>128THEN32
34 PRINT@P-A-1,"ñúúô";:PRINT@P-1,"öêêï";:PRINT@P+A-1,"ôììñ";:SOUND250,1
35 PRINT@P-A-1,"ÄÄÄÄ";:PRINT@P-1,"ÄÄÄÄ";:PRINT@P+A-1,"ÄÄÄÄ";
36 P=A*Y+X:PRINT@P,S$(D);:PRINT@P+A,T$(D);:SOUND100,2:RETURN
50 H=POINT(S(X),T(Y)):H$=CHR$(16*(H-1)+134):PRINT@P,H$H$;:PRINT@P+A,H$H$;:SOUND150,1:SOUND150,1:SOUND200,2:SOUND1,1
51 SH=SH+1:GOSUB999:IFSH=3THENE=1000
52 RETURN
55 SC=(SC+Q*LV)*(12-N):PRINT@18,"SCORE="SC:RETURN
60 PRINT@P,S$(D);:PRINT@P+A,T$(D);
61 PRINT@0,"FIELD"LC"CLEARED!";:TM=1000-E:SC=SC+TM:GOSUB55:GOSUB8060:E=1000:RETURN
100 PRINT@487,"PLAY AGAIN (Y/N)?";
110 I$=INKEY$:IFI$=""THEN110
120 IFI$="Y"THENGOSUB8000:GOSUB8090:GOSUB2000:GOTO20
130 IFI$="N"THENEND
140 GOTO110
800 DIMT,A,B,S(34),T(15),X(8),Y(8),K(255),A$(8),B$(8),C$(8),D$(8),E$(8),F$(8),S$(8),T$(8),P(8),A(8,31),B(8,15),H(8)
810 DIMX$(8,4),Y$(8,4),Z$(8,4),N(2),O,P,D,X,Y,W,V,R,C,E,F,H,I,J,N,K,L,M,Q,Z,S,SH,SC,HS,LV,LC,LM,LN,TM,B$,S$,T$,I$
830 V=RND(-(PEEK(9)*256+PEEK(10))):RETURN
900 GOSUB1000:GOTO20
999 PRINT@480,"ÄÄÄÄÄÄÄÄÄÄÄÄ";:ON1-(SH<3)GOTO6:FORC=1TO3-SH:SET(C*2,31,1):NEXT:RETURN
1000 N(1)=-1:N(2)=1
1010 K(83)=1:K(65)=2:K(87)=3:K(32)=4:K(66)=5:K=17023
1020 FORT=0TO31:S(T)=2*T+1:NEXT:FORT=0TO15:T(T)=2*T+1:NEXT
1030 A=32:B=64
1050 S$(0)="ÄÄ":T$(0)="ÄÄ":S$=S$(0):T$="ÄÄ"
1051 S$(1)="éÄ":T$(1)="ÄÄ":V(1)=-1:W(1)=-1
1052 S$(2)="ÜÄ":T$(2)="ÑÄ":V(2)=-1:W(2)=0
1053 S$(3)="ÇÄ":T$(3)="åÄ":V(3)=-1:W(3)=1
1054 S$(4)="ÇÇ":T$(4)="ÑÄ":V(4)=0:W(4)=1
1055 S$(5)="ÄÇ":T$(5)="Ñà":V(5)=1:W(5)=1
1056 S$(6)="ÑÇ":T$(6)="ÑÄ":V(6)=1:W(6)=0
1057 S$(7)="Ñä":T$(7)="ÄÄ":V(7)=1:W(7)=-1
1058 S$(8)="ÜÇ":T$(8)="ÄÄ":V(8)=0:W(8)=-1
1100 A$(0)="ÄÄÄÄ"
1110 B$(0)="ÄÄ"
1120 C$(0)="Ä"
1200 FORC=0TO7
1210 X$(C+1,3)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(130+(C*16))
1220 Y$(C+1,3)=CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))
1230 Z$(C+1,3)=CHR$(132+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1261 X$(C+1,2)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(139+(C*16))+CHR$(128+(C*16))
1262 Y$(C+1,2)=CHR$(141+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1263 Z$(C+1,2)=CHR$(128+(C*16))+CHR$(140+(C*16))+CHR$(136+(C*16))+CHR$(128+(C*16))
1271 X$(C+1,1)=CHR$(135+(C*16))+CHR$(139+(C*16))
1272 Y$(C+1,1)=CHR$(141+(C*16))+CHR$(142+(C*16))
1273 Z$(C+1,1)=""
1280 NEXT:GOSUB8090
2000 CLS0:GOSUB999:Z=8:S=4:LV=LV+1:LC=LC+1:IFLV>10THENLV=10
2005 LM=5+LV+RND(5):LN=5+INT(LV/2):F=0:TM=0:D=4:X=16:Y=0:P=A*Y+X:V=0:W=0:GOSUB3000
2010 FORC=1TO8:A(C,0)=29:A(C,1)=28:R=(RND(LM)/10)*N(RND(2)):FORT=2TO29:A(C,T)=R:NEXT:A(C,30)=-28:A(C,31)=-29:NEXT:GOSUB3000
2015 FORC=1TO8:B(C,0)=11:R=(RND(LN)/10)*N(RND(2)):FORT=1TO11:B(C,T)=R:NEXT:B(C,12)=-11:NEXT:GOSUB3000
2025 FORC=1TO8:H(C)=3:A$(C)=X$(C,3):B$(C)=Y$(C,3):C$(C)=Z$(C,3):D$(C)=A$(0):E$(C)=A$(0):F$(C)=A$(0):NEXT:GOSUB3000
2080 FORT=1TO8:Y(T)=T+2:X(T)=2:P(T)=A*INT(Y(T))+X(T):NEXT:FORT=16TO256STEP32:PRINT@T,CHR$(128);CHR$(128);:NEXT:R=9:C1=0:RETURN
3000 PRINT@P,S$;:PRINT@P+A,CHR$(186);:Y=Y+2:P=A*Y+X:PRINT@P,S$(D);:PRINT@P+A,T$(D);:PRINT@P-31,CHR$(186);:RETURN
8000 CLS:I$="** THE RAINBOW ASTEROID FIELD **":GOSUB8080:PRINTTAB(10)"BY JIM GERRIE"
8001 PRINT"YOU ARE IN COMMAND OF A STARSHIP";
8002 PRINT"EXPLORING THE FAMOUS RAINBOW"
8003 PRINT"CRYSTAL ASTEROIDS OF ARCTURUS."
8004 PRINT"UNFORTUNATELY THE FIELD HAS BE-"
8005 PRINT"COME UNSTABLE AND NOW THREATENS"
8006 PRINT"A NEARBY COLONY. IT IS YOUR TASK";
8007 PRINT"TO CLEAR IT. ";:I$="BEWARE:":GOSUB8080:PRINT" DESTROYING" 
8008 PRINT"ASTEROIDS AFFECTS THE OTHERS!"
8010 PRINT"USE: A=TURN COUNTER CLOCKWISE"
8020 PRINT"     S=TURN CLOCKWISE"
8030 PRINT"     W=THRUST    B=WARP X 3"
8040 PRINT"     SPACE=FIRE  SHIPS  = 3"
8045 IFSC>HSTHENHS=SC
8050 SC=0:LV=0:LC=0:SH=0:Q=0:I$="HIGH="+STR$(HS):PRINTTAB((32-LEN(I$))/2);:GOSUB8080:RETURN
8060 PRINT@485,;"HIT <ENTER> TO CONTINUE";
8065 IFLC>0ANDLC/3-INT(LC/3)=0THENSH=SH-1:PRINT@256,;:I$="BONUS SHIP EARNED":GOSUB8080:IFSH<0THENSH=0
8070 I$=INKEY$:ON-(I$<>CHR$(13))GOTO8070:N=11-VAL(I$):RETURN
8080 PRINTI$;:R=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORC1=R-LEN(I$)+1TOR:POKEC1,PEEK(C1)-64:NEXT:RETURN
8090 PRINT@485,;"DIFFICULTY LEVEL (1-9)?";:I$=INKEY$:ON-(I$<"1"ORI$>"9")GOTO8090:N=11-VAL(I$):RETURN
9998 DATA60,54,55,206,67,101,236,1,221,246,236,3,221,248,236,5,221,250,236,7,221,252
9999 DATA51,50,56,57,240,129,58,37,1,57,126,225,204
10000 FORT=0TO34:READM:POKE17227+T,M:NEXT:EXEC17227:RETURN

