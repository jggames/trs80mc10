0 GOSUB8000:CLEAR3000:DIMS(34),T(15),X(8),Y(8),K(255),A$(8),B$(8),C$(8),D$(8),E$(8),F$(8),S$(8),T$(8),P(8),A(8,31),B(8,15),H(8)
1 DIMX$(8,4),Y$(8,4),Z$(8,4),N(2),T,O,P,D,A,B,X,Y,C,E,F,H,I,J,K,L,M,N,Q,R,S,W,Z,SH,SC,HS,LV,LC,LM,TM,B$,S$,T$,I$:GOTO900
2 RETURN
4 D=D-1:ONDGOTO2,2,2,2,2,2,2,2:D=8:RETURN
5 D=D+1:ONDGOTO2,2,2,2,2,2,2,2:D=1:RETURN
6 SET(S(X),T(Y),1):RESET(S(X),T(Y)):PRINT@P,S$;:PRINT@P+A,T$;:X=X+V(D):Y=Y+W(D):ON1-(X>29)GOTO7:X=1:GOTO8
7 ON1-(X=0)GOTO8:X=28
8 ON1-(Y=0)GOTO9:Y=13:GOTO10
9 ON1-(Y=14)GOTO10:Y=1
10 P=A*Y+X:ONPOINT(S(X),T(Y))GOSUB50,50,50,50,50,50,50,50:RETURN
11 PRINT@P(H),D$(H);:PRINT@P(H)+A,E$(H);:PRINT@P(H)+B,F$(H);:PRINT@P(H),A$(H);:PRINT@P(H)+A,B$(H);:PRINT@P(H)+B,C$(H);
12 SOUND100,1:SOUND110,1:SOUND100,1:RETURN
13 ONFGOTO2:M=V(D)*2:L=W(D)*2:I=2*X+1:J=2*Y+1:F=1:RETURN
14 RESET(I,J):I=I+M:J=J+L:ONPOINT(I,J)GOTO16,16,16,16,16,16,16,16:SET(I,J,5):ON1-(I<60ANDI>2ANDJ>2ANDJ<29)GOTO15:RETURN
15 RESET(I,J):F=0:RETURN
16 H=POINT(I,J):H(H)=H(H)+1:A$(H)=X$(H,H(H)):B$(H)=Y$(H,H(H)):C$(H)=Z$(H,H(H)):GOSUB11:Q=Q+1:F=0:ONH(H)GOTO2,17,18
17 D$(H)=B$(0):E$(H)=B$(0):F$(H)=B$(0):RETURN
18 D$(H)="":E$(H)="":F$(H)="":ON1-(Q<24)GOTO60,2
20 FORE=1TO1000:FORT=1TO8:O=P(T):X(T)=X(T)+A(T,X(T)):Y(T)=Y(T)+B(T,Y(T)):P(T)=A*INT(Y(T))+X(T):?@O,D$(T);:?@O+A,E$(T);
22 ?@O+B,F$(T);:?@P(T),A$(T);:?@P(T)+A,B$(T);:?@P(T)+B,C$(T);:ONPOINT(S(X),T(Y))GOSUB50,50,50,50,50,50,50,50
24 ONK(PEEK(K)ANDPEEK(2))GOSUB4,5,6,13:?@P,S$(D);:PRINT@P+A,T$(D);:ONFGOSUB14:NEXT:NEXT
26 IFSH>=3THENPRINT@0,"SHIP DESTROYED!";:GOSUB55:GOTO100
30 Q=0:GOSUB2000:GOTO20
50 H=POINT(S(X),T(Y)):H$=CHR$(16*(H-1)+134):PRINT@P,H$;H$;:PRINT@P+A,H$;H$;:SOUND150,1:SOUND150,1:SOUND200,2:SOUND1,1
51 SC=SC-20:SH=SH+1:IFSH=3THENE=1000
52 RETURN
55 SC=SC+Q*LV:PRINT@18,"SCORE=";SC:RETURN
60 PRINT@0,"FIELD";LC;"CLEARED!";:TM=1000-E:SC=SC+INT(TM/10):GOSUB15:GOSUB55:GOSUB8060:E=1000:RETURN
100 PRINT@487,"PLAY AGAIN (Y/N)?";
110 I$=INKEY$:IFI$=""THEN110
120 IFI$="Y"THENGOSUB8000:GOSUB8060:GOSUB2000:GOTO20
130 IFI$="N"THENEND
140 GOTO110
900 GOSUB1000:GOTO20
1000 N(1)=-1:N(2)=1
1010 K(83)=1:K(65)=2:K(87)=3:K(32)=4:K=17023
1020 FORT=0TO31:S(T)=2*T+1:NEXT:FORT=0TO15:T(T)=2*T+1:NEXT
1030 A=32:B=64
1050 S$(0)=CHR$(128)+CHR$(128):T$(0)=CHR$(128)+CHR$(128):S$=S$(0):T$=T$(0)
1051 S$(1)=CHR$(142)+CHR$(128):T$(1)=CHR$(128)+CHR$(128):V(1)=-1:W(1)=-1
1052 S$(2)=CHR$(134)+CHR$(128):T$(2)=CHR$(132)+CHR$(128):V(2)=-1:W(2)=0
1053 S$(3)=CHR$(130)+CHR$(128):T$(3)=CHR$(140)+CHR$(128):V(3)=-1:W(3)=1
1054 S$(4)=CHR$(130)+CHR$(130):T$(4)=CHR$(132)+CHR$(128):V(4)=0:W(4)=1
1055 S$(5)=CHR$(128)+CHR$(130):T$(5)=CHR$(132)+CHR$(136):V(5)=1:W(5)=1
1056 S$(6)=CHR$(132)+CHR$(130):T$(6)=CHR$(132)+CHR$(128):V(6)=1:W(6)=0
1057 S$(7)=CHR$(132)+CHR$(138):T$(7)=CHR$(128)+CHR$(128):V(7)=1:W(7)=-1
1058 S$(8)=CHR$(134)+CHR$(130):T$(8)=CHR$(128)+CHR$(128):V(8)=0:W(8)=-1
1100 A$(0)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
1110 B$(0)=CHR$(128)+CHR$(128)
1120 C$(0)=CHR$(128)
1200 FORC=0TO7
1210 X$(C+1,0)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(130+(C*16))
1220 Y$(C+1,0)=CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))
1230 Z$(C+1,0)=CHR$(132+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1261 X$(C+1,1)=CHR$(129+(C*16))+CHR$(143+(C*16))+CHR$(139+(C*16))+CHR$(128+(C*16))
1262 Y$(C+1,1)=CHR$(141+(C*16))+CHR$(143+(C*16))+CHR$(143+(C*16))+CHR$(136+(C*16))
1263 Z$(C+1,1)=CHR$(128+(C*16))+CHR$(140+(C*16))+CHR$(136+(C*16))+CHR$(128+(C*16))
1271 X$(C+1,2)=CHR$(135+(C*16))+CHR$(139+(C*16))
1272 Y$(C+1,2)=CHR$(141+(C*16))+CHR$(142+(C*16))
1273 Z$(C+1,2)=""
1280 NEXT:GOSUB8060
2000 CLS0:LV=LV+1:LC=LC+1:IFLV>10THENLV=10
2005 LM=10+LV:F=0:TM=0:D=4:X=16:Y=0:P=A*Y+X:GOSUB3000
2010 FORC=1TO8:A(C,0)=29:A(C,1)=28:R=(RND(LM)/10)*N(RND(2)):FORT=2TO29:A(C,T)=R:NEXT:A(C,30)=-28:A(C,31)=-29:NEXT:GOSUB3000
2015 FORC=1TO8:B(C,0)=11:R=(RND(10)/10)*N(RND(2)):FORT=1TO11:B(C,T)=R:NEXT:B(C,12)=-11:NEXT:GOSUB3000
2025 FORC=1TO8:H(C)=0:A$(C)=X$(C,0):B$(C)=Y$(C,0):C$(C)=Z$(C,0):D$(C)=A$(0):E$(C)=A$(0):F$(C)=A$(0):NEXT:GOSUB3000
2080 FORT=1TO8:Y(T)=T+2:X(T)=2:P(T)=A*INT(Y(T))+X(T):NEXT:FORT=16TO256STEP32:PRINT@T,CHR$(128);CHR$(128);:NEXT:RETURN
3000 PRINT@P,S$;:PRINT@P+A,CHR$(186);:Y=Y+2:P=A*Y+X:PRINT@P,S$(D);:PRINT@P+A,T$(D);:PRINT@P-31,CHR$(186);:RETURN
8000 CLS:PRINT"** THE RAINBOW ASTEROID FIELD **"
8001 PRINT"YOU ARE IN COMMAND OF A SHIP"
8002 PRINT"EXPLORING THE FAMOUS RAINBOW"
8003 PRINT"ASTEROIDS OF ARCTURUS."
8004 PRINT"UNFORTUNATELY THE FIELD HAS BE-"
8005 PRINT"COME UNSTABLE AND NOW THREATENS"
8006 PRINT"A NEARBY COLONY. IT IS YOUR"
8007 PRINT"SAD TASK TO CLEAR THE FIELD."
8010 PRINT"USE: A=TURN COUNTER CLOCKWISE"
8020 PRINT"     S=TURN CLOCKWISE"
8030 PRINT"     W=THRUST"
8040 PRINT"     SPACE=FIRE":PRINT
8045 IFSC>HSTHENHS=SC
8050 SC=0:LV=0:LC=0:SH=0:Q=0:I$="high="+STR$(HS):PRINTTAB((32-LEN(I$))/2)I$;:RETURN
8060 FORT=1TO50:I$=INKEY$:NEXT:PRINT@485,"PRESS ANY KEY TO BEGIN ";
8065 IFLC>0ANDLC/6-INT(LC/6)=0THENSH=SH-1:PRINT@256,"bonus";CHR$(128);"ship";CHR$(128);"earned";:IFSH<0THENSH=0
8070 I$=INKEY$:R=RND(1000):IFI$=""THEN8070
8090 RETURN
8100 REM COPYRIGHT (C) 2012 JIM GERRIE
