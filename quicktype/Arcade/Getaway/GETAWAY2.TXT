1 CLS:CLEAR500:DIMC$(26):PRINT@128,"INSTRUCTIONS "CHR$(91)"Y/N"CHR$(93)
2 INPUTI$:IFLEFT$(I$,1)="Y"THENGOSUB8500
50 DATA5,244,58,239,89,232,125,227,147,218,170,204,185
100 CLS:PRINT@88,"$$$":PRINT@120,"$$$":PRINT@151,"$$$$$":PRINT@182,"$$    $"
110 PRINT@213,"$$":PRINT@246,"$$$$$$$":PRINT@278,"$$$$$$$":PRINT@316,"$$"
120 PRINT@342,"$    $$":PRINT@375,"$$$$$":PRINT@408,"$$$":PRINT@440,"$$$"
135 FORY=1TO3:RESTORE:FORX=201TO213:V$=V$+CHR$(191):SOUNDX,2:NEXTX,Y:GOSUB8800
140 PRINT@98,"'THE GETAWAY'";:PRINT@290,"PRESS "CHR$(91)"ENTER"CHR$(93);:PRINT@324,"TO BEGIN";
150 C1=RND(0):I$=INKEY$:IFI$<>CHR$(13)THEN150
160 W$=LEFT$(V$,5):X$=LEFT$(V$,7):R$=CHR$(128):T$=LEFT$(V$,3)+CHR$(187):E$=R$+R$+R$+R$
165 LV=2500:ST=32:DL=1:V1=0:P=2:DD=0:SC=0
170 CLS0:CC=0:H=RND(28):OH=H
200 FORX=16384TO16479:POKEX,36:NEXT
260 PRINT@46,"bank";:PRINT@448+OH,T$;
270 FORC1=1TO15+RND(10):F=RND(32):FORV=V1TO416STEPST:Z=16415+F+V:OL=PEEK(Z):POKEZ,100:FORX=1TODL:NEXT:OH=H:IFPEEK(2)=.THEN392
360 IF1ANDNOTPEEK(16949)THENH=H+P:IFH>28THENH=28
370 IF1ANDNOTPEEK(16946)THENH=H-P:IFH<.THENH=.
392 POKE16415+F+V,OL:IFH<>OHTHENPRINT@448+OH,E$;:PRINT@448+H,T$;
440 NEXT:IFOL=128THENSOUND1,1:SC=SC-20:PRINT@77,SC;:GOTO480
450 SOUND170,1:SC=SC+100:CC=CC+1:PRINT@77,SC;
460 IFSC>LVTHENLV=LV+2500:L=L+1:ST=64:V1=-32:P=2.5:FORX=1TO10:SOUND100,1:NEXT:DL=60-(5*L):IFDL<30THENDL=15
480 NEXT:GOSUB670:IFSC=0THEN165
490 GOTO170
670 HF=6+INT(CC/3+.4)
680 PRINT@260,"THE POLICE ARE COMING!!";
690 FORS=1TO3:SOUND230,5:FORX=1TO100:NEXTX,S
730 FORX=1TO1200:NEXT:CLS0:T=0
750 U=16673+T:POKEU,175:POKEU+1,172:POKEU+2,172:POKEU+3,175
760 FORX=31TO37:POKEU+X,175:NEXT
770 POKEU+38,152:POKEU+64,172:POKEU+68,172
810 IFT>0THEN1000
820 Y=264+HF:PRINT@Y,W$;
830 PRINT@Y+32,W$;CHR$(188)CHR$(191);
840 PRINT@Y+64,X$;CHR$(152);
850 PRINT@Y+97,CHR$(188);:PRINT@Y+101,CHR$(188);
900 IFEE=-1THENRETURN
910 GOSUB8810
920 C1=RND(26):G$=C$(C1):IFG$=""THEN920
930 L$=INKEY$:IFL$<>""THEN930
940 PRINT@0,"CHOOSE YOUR ESCAPE ROUTE (A-Z)"
950 PRINT@32,"DON'T RE-USE OLD ONES THAT WORK"
1000 L$=INKEY$:POKEU-30,179
1005 IFL$=""ORL$<"A"ORL$>"Z"THENPOKEU-30,128:GOTO1000
1010 PRINT@34+ASC(L$),L$;:SOUND220,4:IFL$=G$THEN2000
1040 IFPEEK(U+39)=191THEN3000
1050 POKEU,128:POKEU+31,128:POKEU-30,128
1060 POKEU+68,128:POKEU+64,128:T=T+1:GOTO750
2000 PRINT@0,"YOU ESCAPED! THE LETTER WAS "L$"."
2010 PRINT@32,"NOW THE POLICE KNOW THAT ROUTE.":C$(C1)=""
2015 SOUND200,4:SOUND220,3:SOUND240,4
2020 FORX=1TO1200:NEXT:EE=-1:FORQ=HFTO15
2060 FORX=0TO2:PRINT@Y+X*32,R$;:NEXTX
2070 PRINT@Y+101,R$;:PRINT@Y+97,R$;
2090 HF=HF+1:GOSUB820:NEXTQ
2120 EE=0:FORX=1TO1500:NEXT:RETURN
3000 PRINT@32,"   THE CORRECT LETTER WAS "G$
3010 DD=DD+1:IFDD=4THEN7000
3030 PRINT@0,"THEY HAVE CAUGHT"DD"OF YOUR MEN"
3050 FORX=101TO1STEP-10:SOUNDX,4:NEXT
3080 FORX=1TO1500:NEXT:RETURN
7000 PRINT@0,"      THEY'VE NAILED YOU!":PRINT@32
7010 FORX=1TO1000:NEXT:SOUND55,5:SOUND55,5:SOUND55,5
7020 SOUND1,12:IFSC>HITHENHI=SC
7030 PRINT@32,"    THE HIGH SCORE IS"HI
7040 PRINT:PRINT"    PLAY AGAIN "CHR$(91)"Y/N"CHR$(93)"?":PRINT
7060 A$=INKEY$
7080 IFA$="Y"THENSC=0:RETURN
7090 IFA$="N"THENCLS:END
7091 GOTO7060
8500 PRINT@64,"YOU HAVE 4 MEN IN YOUR GANG.":PRINT:PRINT"YOU DRIVE THE GETAWAY VAN."
8555 PRINT"YOU MUST CATCH THE MONEY YOUR   PARTNERS THROW OUT OF THE":PRINT"BANKS YOU HIT.":PRINT
8600 PRINT"STEER THE VAN WITH THE (A,D)    DIRECTION KEYS."
8700 PRINT@448,"PRESS "CHR$(91)"ENTER"CHR$(93)" TO CONTINUE..."
8701 C1=RND(0):IFINKEY$<>CHR$(13)THEN8701
8702 RETURN
8800 FORC1=1TO26:C$(C1)=CHR$(RND(26)+64):NEXT:RETURN
8810 M$="":FORC1=1TO26:M$=M$+C$(C1):NEXT:RETURN
