0 GOSUB10005:GOSUB10000:CLS:CLEAR9800:GOSUB4000:GOTO1000
1 FORC=P+N TOE STEPR:ONK(PEEK(C))GOTO6:NEXT:RETURN
2 FORC=P TOE STEPR:ONK(PEEK(C))GOTO6:NEXT:RETURN
3 P=P-Q:H=H-1:RETURN
4 P=P+Q:H=H+1:RETURN
5 X=H:S=1:A=109:N=1:U=P+N:I=0:RETURN
6 U(M)=C:M(M)=1:C=E:NEXT:RETURN
7 X=H:S=1:A=106:N=33:U=P+N:I=1:RETURN
8 U(M)=U(M)-33:ONK(PEEK(U(M)))GOTO41,41,10,11:POKEU(M),J:RETURN
9 FORS=S TOS+11:L=PEEK(U):POKEU,A:X=X+I:ONK(L)GOTO50,51,52,51,49:POKEU,V:U=U+N:NEXT:S=S-1:U=U-1:NEXT:GOTO25
10 GOSUB63:GOSUB12:IFLV<1THENY=5:Z=132:FU=-1
11 M(M)=1+RND(2):RETURN
12 S$=STR$(LV):PRINT@29,S$;:FORT=B+29 TOB+28+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
14 ONK(PEEK(135))GOSUB3,4,7,5:RETURN
16 FU=FU+1:S$=STR$(FU):PRINT@21,S$;:FORT=B+21TOB+20+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
20 FORY=1TO3:FORZ=ZZ TO132:POKE345,255:POKE340,255:POKE339,255:IF(PEEK(345)ANDPEEK(340)ANDPEEK(339))<>255THENGOSUB14
21 PRINT@F,A$MID$(B$,Z,32)MID$(C$,Z,32)MID$(D$,Z,32)MID$(E$,Z,32)MID$(F$,Z,32)MID$(G$,Z,32)MID$(H$,Z,32)MID$(I$,Z,32)MID$(J$,Z,32);
23 ONK(PEEK(P))GOSUB39,39,39,4,16:POKEP,D:FORM=1TO3:ONM(M)GOSUB8,2,1:NEXT:ONS GOTO9,9,9,9,9,9,9,9,9,9,9,9,9:NEXT
25 ZZ=Q:IFFU=0 THENS$="RETURN TO BASE":C=50:GOSUB90:Y=4:SOUND80,20
26 GOSUB61:FU=0:POKEB+22,48:POKEB+23,Q:NEXT:IFY=4 THEN GOSUB93:IFLV>0 THEN1080
27 POKEP,62:S$="PLAY AGAIN (Y/N)?":C=32:GOSUB90
28 I$=INKEY$:ON1-(I$<>"") GOTO28:ON1-(I$<>"Y") GOTO1000:ON1-(I$<>"N") GOTO4230,28
30 W=VARPTR(B$):RETURN
31 W=VARPTR(C$):RETURN
32 W=VARPTR(D$):RETURN
33 W=VARPTR(E$):RETURN
34 W=VARPTR(F$):RETURN
35 W=VARPTR(G$):RETURN
36 W=VARPTR(H$):RETURN
37 W=VARPTR(I$):RETURN
38 W=VARPTR(J$):RETURN
39 POKEP,62:FU=-1:LV=0:GOSUB12
40 Y=6:Z=132:SOUND1,9:SOUND10,9
41 RETURN
49 POKEU,17:SOUNDD,1:GOSUB16:GOSUB62:S=99:NEXT:NEXT:GOTO25
50 POKEU,159:ONX GOSUB30,31,32,33,34,35,36,37,38:POKE(PEEK(W+2)*O)+PEEK(W+3)+S+2+Z,Q:POKEU,V:S=99:NEXT:NEXT:GOTO25
51 POKEU,128:S=99:NEXT:NEXT:GOTO25
61 SC=SC+10*FU
62 SC=SC+10:S$=STR$(SC):PRINT@0,S$;:FORT=B TOB-1+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
63 POKEP,42:SOUND1,1:LV=LV-1:RETURN
70 ONT GOTO72,73,74,75,76,77,78,79
71 B$=B$+B$(Z):RETURN
72 C$=C$+B$(Z):RETURN
73 D$=D$+B$(Z):RETURN
74 E$=E$+B$(Z):RETURN
75 F$=F$+B$(Z):RETURN
76 G$=G$+B$(Z):RETURN
77 H$=H$+B$(Z):RETURN
78 I$=I$+B$(Z):RETURN
79 J$=J$+B$(Z):RETURN
80 FORT=8TO0STEP-1:Z=2:IFH>T THENZ=1
81 IFH=T THENZ=L(RND(4))
82 GOSUB70:NEXT:RETURN
90 PRINT@C,S$;:FORT=B+C TOB+(C-1)+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
91 GOSUB39:RETURN
93 FORT=1TO10:SOUND185,1:NEXT
94 Y=17+LV*(LV<9):GOSUB296:S$="":S$=S$+LEFT$(A$,32):FORT=1TO96:S$=S$+MID$(CHR$(95)+LEFT$(A$,16),RND(Y),1):NEXT
95 S$=S$+A$:B$=B$+MID$(S$,32+RND(32)):C$=C$+MID$(S$,32+RND(32)):D$=D$+MID$(S$,32+RND(32)):E$=E$+MID$(S$,32+RND(32))
96 F$=F$+MID$(S$,32+RND(32)):G$=G$+MID$(S$,32+RND(32)):H$=H$+MID$(S$,32):I$=I$+MID$(S$,32):J$=J$+MID$(S$,32)
97 FORZ=1TO132:PRINT@F,MID$(S$,Z,32)MID$(S$,Z,32)MID$(S$,Z,32)MID$(B$,Z,32)MID$(C$,Z,32)MID$(D$,Z,32);
98 PRINTMID$(E$,Z,32)MID$(F$,Z,32)MID$(G$,Z,32)MID$(H$,Z,32)MID$(I$,Z,32)MID$(J$,Z,32);
99 ONK(PEEK(P))GOSUB63,91,63,4:POKEP,D:POKE345,255:POKE340,255:POKE339,255:IF(PEEK(345)ANDPEEK(340)ANDPEEK(339))<>255THENGOSUB14
100 NEXT:LV=LV+1:GOSUB12:RETURN
105 ONK(PEEK(135))GOSUB3,4:RETURN
200 MN=MN+1:PRINT@O,"GET READY FOR MISSION"MN:GOSUB275:GOSUB280
210 FORM=1TO100:H=5+RND(4):S=(6-RND(12))/2:R=RND(25):FORG=1TOR:IFG<>(R/2) THENH=H+S:IFH>=0ANDH<9 THEN240
230 S=(S*-1)/RND(2):H=H+S:IFH>=0ANDH<9THEN240
235 H=5+RND(4)
240 GOSUB80:M=M+1:PRINT@V,101-M;:IFM>99 THENG=R
270 NEXT:M=M-1:NEXT:R=66:GOSUB290:RETURN
275 B$="":C$="":D$="":E$="":F$="":G$="":H$="":I$="":J$="":RETURN
280 B$=LEFT$(A$,32)+B$:C$=LEFT$(A$,32)+C$:D$=LEFT$(A$,32)+D$:E$=LEFT$(A$,32)+E$:F$=LEFT$(A$,32)+F$
285 G$=LEFT$(A$,32)+G$:H$=LEFT$(A$,32)+H$:I$=LEFT$(A$,32)+I$:J$=LEFT$(A$,32)+J$:RETURN
290 B$=B$+MID$(B$,32,32):C$=C$+MID$(C$,32,32):D$=D$+MID$(D$,32,32):E$=E$+MID$(E$,32,32):F$=F$+MID$(F$,32,32)
295 G$=G$+MID$(G$,32,32):H$=H$+MID$(H$,32,32):I$=I$+MID$(I$,32,32):J$=J$+MID$(J$,32,32):RETURN
296 B$=RIGHT$(B$,32):C$=RIGHT$(C$,32):D$=RIGHT$(D$,32):E$=RIGHT$(E$,32):F$=RIGHT$(F$,32)
297 G$=RIGHT$(G$,32):H$=RIGHT$(H$,32):I$=RIGHT$(I$,32):J$=RIGHT$(J$,32):RETURN
300 FORT=1024TO1087:POKET,Q:NEXT:RETURN
310 FORT=1472TO1535:POKET,128:NEXT:RETURN
1000 CLS:IFSC>HS THENHS=SC
1070 SC=-10:LV=3:FU=-1:MN=0:P=B+259
1080 S$="":GOSUB200:GOSUB300:GOSUB310
1085 S$=STR$(HS):PRINT@10,S$;:FORT=B+10TOB+9+LEN(S$):POKET,PEEK(T)-F:NEXT
1086 GOSUB12:GOSUB62:GOSUB16
1090 S=0:H=(P-B-3)/32-4:ZZ=1:FORT=1TO4:M(T)=2:NEXT:GOTO20
4000 DIMA$,B$,C$,D$,E$,F$,G$,H$,I$,J$,B$(4),K(128),M(4),U(4),Z:SCREEN0,1:PRINT"   MC-SCRAMBLE BY JIM GERRIE"
4010 FORZ=1024TO1055:POKEZ,PEEK(Z)-64:NEXT:PRINT"FLY YOUR AIRCRAFT '>' OVER THE":PRINT"MOUNTAINOUS TERRAIN SHOOTING"
4030 PRINT"AT ANTI-AIRCRAFT EMPLACEMENTS":PRINT"'A' THAT WILL SHOOT BACK AT YOU":PRINT"UNLESS YOU DESTROY THEM WITH"
4040 PRINT"YOUR GUNS 'SPACE BAR.'  USE":PRINT"GUNS OR BOMBS 'B' TO ATTACK THE":PRINT"FUEL EMPLACEMENTS 'Q' OF THE"
4070 PRINT"ENEMY. YOU MUST ATTACK ENOUGH":PRINT"FUEL EMPLACEMENTS OR YOU WILL":PRINT"BE RE-CALLED FROM YOUR MISSION."
4080 PRINT"NAVIGATE THE FINAL ONSLAUGHT,":PRINT"AND YOU WILL BE ASSIGNED NEW":PRINT"MISSIONS. 'A'=UP AND 'Z'=DOWN."
4085 DIMM,T,K,P,D,S,A,U,B,Y,C,X,R,E,G,H,I,J,L,N,Q,V,W,L(4),O,F,S$,ZZ,LV,FU,MN
4090 K=RND(-TIMER):B$(1)=" ":B$(2)=CHR$(128):B$(3)="A":B$(4)="Q":Q=32:K=17023
4095 D=126:F=64:E=16832+(1024-16384):J=94:B=16384+(1024-16384):HS=0:V=96:FORT=1 TO96:A$=A$+" ":NEXT:O=256:K(65)=1:K(32)=4:K(90)=2:K(66)=3:K(128)=2
4099 K(126)=3:K(95)=1:K(81)=5:L(1)=3:L(2)=3:L(3)=3:L(4)=4:S$="PRESS ANY KEY TO BEGIN":C=485:GOSUB90
4200 I$=INKEY$:ON1-(I$<>"")GOTO4200:SCREEN0,0:RETURN
4230 GOSUB10010:GOSUB10003:END
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN
