0 CLS:GOTO100
1 IFCX>=0ANDCX<=Q(1)ANDCY>=0ANDCY<=Q(2)ANDCX+P1>=0ANDCX+P1<=Q(1)ANDCY+P2>=0ANDCY+P2<=Q(2)THENLINE(CX,CY)-(CX+P1,CY+P2),PSET
2 CX=CX+P1:CY=CY+P2:RETURN
3 IFCX>=0ANDCX<=Q(1)ANDCY>=0ANDCY<=Q(2)ANDP1>=0ANDP1<=Q(1)ANDP2>=0ANDP2<=Q(2)THENLINE(CX,CY)-(P1,P2),PSET
4 CX=P1:CY=P2:RETURN
10 REM*******************
20 REM*                 *
30 REM*    SPACEWAR     *
40 REM*   FOR 1 OR 2    *
50 REM*  JOBST BRANDT   *
60 REM* HEWLETT-PACKARD *
61 REM*   (01 APR 82)   *
65 REM*                 *
66 REM*  MC-10 VERSION  *
67 REM*  NEEDS MCX-128  *
68 REM* BY CHRIS OSBORN *
69 REM*   (26 MAR 15)   *
80 REM*                 *
90 REM*******************
100 DIMC(46),O(21,3),S(16,2),E(8,2),X,Y,M,I,J,N,P1$,A$,R1,R2,R3,R5,R6,A,B,U,U1,U2,V,V1,V2,E,F,F1,F2,F3,Z,P,P1,P2,S
120 REM STAR LOCS AND SIZE:XMAX=720,YMAX=360,SMAX=0,SMIN=0
130 DATA0,0,0,0,10,0,-2,0,0,2,-1,0,3,2,-8,0,-2,-2,-4,0,-4
140 DATA-2,4,-2,4,0,2,-2,8,0,-3,2,1,0,0,2
145 DATA-2,6,-2,-6,-6,-2,6,-2,2,-6,2,6,6,2,-6,2
150 FORI=1TO4:READQ(I):NEXT:FORI=1TO16:FORJ=1TO2:READS(I,J):NEXTJ,I:FORI=1TO8:FORJ=1TO2:READE(I,J):NEXTJ,I
160 GOSUB2740:G=R1
170 M=RND(-(PEEK(9)*256+PEEK(10))):REM-Q CONTAINS XLIM,YLIM(SCREEN),DISTANCE,MAXSIZE(STARS)
220 M=INT(2*RND(0)+1):O=0
230 N1$="1":P$="2"
250 GOSUB2370
270 CLS
280 F1=0:F2=0:F3=0:V1$(1)="20":V1$(2)="20"
290 PRINT"(";P$;") 1 OR 2 PLAYERS";
300 INPUT"";A$:P1$=A$
310 GOSUB2000:ONR1GOTO290,290,290,270,290,2980
320 IFA$=""THENA$=P$
330 Q=VAL(A$)
340 IFQ<>1ANDQ<>2THEN290
350 NP=Q
360 P$=STR$(NP)
370 IFNP=2THEN400
380 N$(1)="PLAYER":N$(2)="ENEMY"
390 GOTO470
400 N$(1)="SPOCK":N$(2)="DARTH":FORJ=1TO2
410 PRINT"("N$(J)") CAPTAIN #";J
420 INPUT"NAME";A$
430 P1$=A$:GOSUB2000:ONR1GOTO280,440,280,270,280,2980
440 IFA$=""THEN460
450 P1$=A$:GOSUB2610:N$(J)=R1$
460 NEXTJ
470 PRINT"(";N1$;") # OF STARS? (0-8)";
480 INPUT"";A$
490 P1$=A$:GOSUB2000:ONR1GOTO280,470,470,470,280,2980
500 IFA$=""THENA$=N1$
510 N=VAL(A$)
530 IFABS(N)<0ORABS(N)>8THEN470
540 N1$=STR$(N)
550 REM O=BLACKHOLE
560 O=0:IFN<0THENO=1
570 N=ABS(N)
580 Q(4)=INT(10-N/6)
590 Q(3)=INT(Q(2)/3)-N
600 CLS:PRINT"PLEASE WAIT..."
610 REM---MAKE STARS---
620 N=N+O
630 FORI=1TON
640 R=0
650 R=R+1
660 IFR>20THENNJ=I-1:NEXTJ:I=N:NEXTI:GOTO630
670 O(I,3)=Q(4)
680 REM--J:1=XPOS,2=YPOS,3=MASS/SIZE--
690 FORJ=3TO1STEP-1
700 O(I,J)=INT((Q(J)-2*O(I,3))*RND(0)+O(I,3))
710 NEXTJ
720 FORJ=1TOI-1:IFJ=ITHEN750
730 T=(O(I,3)+O(J,3)+10)^2:IF(O(I,1)-O(J,1))^2+(O(I,2)-O(J,2))^2<TTHEN650
750 NEXTJ:NEXTI
770 REM---PLACE SPACESHIPS---
780 RJ=0:REM---SPACESHIP SIZE=5---
790 O(N+1,3)=5:O(N+2,3)=5
800 FORJ=1TO2
810 T=4*LEN(N$(J)):IFT<10THENT=10
820 IFJ=1THENI=.28*Q(1):Q=0:GOTO840
830 T=-T:I=-.28*Q(1):Q=Q(1)
840 R=0
850 R=R+1:RK=0
860 IFR>20THENJ=3:RJ=1:GOTO940
870 X=INT(I*RND(0))+Q+T
880 Y=INT((Q(2)-30)*RND(0)+20)
890 FORK=1TON
900 IF(X-O(K,1))^2+(Y-O(K,2))^2<(O(K,3)*2)^2THENK=N+1:RK=1
910 NEXTK:IFRKTHEN850
920 O(N+J,1)=X
930 O(N+J,2)=Y
940 NEXTJ:IFRJTHEN630
950 A1$(1)="0":A1$(2)="180"
960 IFNP>1THENA$=N$(1):N$(1)=N$(2):N$(2)=A$
970 REM---PLOT STARS---
980 PMODE2:SCREEN2:PCLS
990 Z1=3.14159*(1/180)
1000 FORI=0TO45:C(I+1)=COS(I*10*Z1):NEXTI
1030 FORI=1TON-O:FORJ=1TO37:X=INT(O(I,1)+O(I,3)*C(J)+.5):Y=INT(O(I,2)-O(I,3)*C(J+9)+.5)
1070 IFJ=1THENLINE(X,Y)-(X,Y),PSET
1080 LINE-(X,Y),PSET:NEXTJ:NEXTI
1130 REM---PLOT SPACESHIPS---
1140 FORI=1TO2:P1=O(N+I,1):P2=O(N+I,2):P3$=N$(I):P4=I:GOSUB2120:NEXTI
1170 IFA$(1,1)="I"THEN1450
1180 GOTO1210
1190 REM---PLAY GAME---
1200 M=M-INT(M/2)*2+1
1210 IFNP=1THENM=1
1230 SOUND1,1
1240 A$=INKEY$:IFA$=""THEN1240
1245 PRINT"("+A1$(M)+") ";N$(M)
1250 B=20:F1=1
1260 INPUT"LAUNCH ANGLE";A$
1270 IFA$=""THENA$=A1$(M)
1280 P1$=A$:GOSUB2000:ONR1GOTO1240,980,1240,270,1240,2980
1290 A=VAL(A$):A=-SGN(A)*(ABS(A)-INT(ABS(A)/360)*360):A1$(M)=STR$(-A)
1320 IFN=0THEN1450
1330 PRINT"(";V1$(M);") ";N$(M)
1350 INPUT"INITIAL VELOCITY";A$
1360 IFA$=""THENA$=V1$(M)
1370 P1$=A$:GOSUB2000:ONR1GOTO1240,980,1330,270,1240,2980
1380 B=VAL(A$)
1390 IFB>=0ANDB<=20THEN1420
1400 PRINT"VELOCITY MUST BE 0-20"
1410 GOTO1330
1420 V1$(M)=STR$(B)
1450 U1=0:U2=0:V1=0:V2=0:F3=0:X=.35*B*COS(A*Z1):Y=.35*B*SIN(A*Z1):U=X+O(N+M,1)+8*COS(A*Z1):V=Y+O(N+M,2)+8*SIN(A*Z1)
1500 P1=U:P2=V:GOSUB2230
1510 IFF2THEN1570
1520 REM---DRAW ROCKET PATH---
1525 SCREEN2:U1=INT(U+.5):V1=INT(V+.5):IFU1<>U2ORV1<>V2THENP1=U1:P2=V1:GOSUB3
1550 U2=U1:V2=V1
1570 E=0:F=0
1580 FORJ=1TON:R5=O(J,1)-U:R6=O(J,2)-V:R4=R5^2+R6^2:R3=SQR(R4):R2=R5/R3:R1=R6/R3:G=.017*(O(J,3)^3)/R4:E=E+G*R2:F=F+G*R1:NEXT
1690 X=X+E:Y=Y+F:U=U+X:V=V+Y:REM---FIND COLLISIONS---
1740 IFF2THEN1790
1750 RJ=0:FORJ=1TON+2:IFJ=N+1THEN1780
1760 IFOANDJ=NTHEN1780
1770 IF(U-O(J,1))^2+(V-O(J,2))^2<O(J,3)^2THENF1=J:J=N+3:RJ=1
1780 NEXTJ:IFRJTHENJ=F1:GOTO1880
1790 IFU<-Q(1)ORU>Q(1)*2ORV<-Q(2)ORV>Q(2)*2THEN1200
1800 F2=(U<0ORU>Q(1)ORV<0ORV>Q(2)):REM--F2=F3(NO CHANGE) F2=1(JUST WENT OFF SCREEN) F3=1(JUST CAME ON SCREEN)--
1840 IFF2=F3THEN1510
1850 IFF3THENP1=U:P2=V:GOSUB2230
1860 F3=F2:GOTO1510
1880 REM---MAKE EXPLOSIONS---
1890 P1=U+2:P2=V+2:GOSUB2230
1900 FORI=1TO8:P1=E(I,1):P2=E(I,2):GOSUB1:NEXT
1910 IFJ<=NTHEN1200
1920 SOUND100,10:SOUND200,10:SOUND1,1
1925 A$=INKEY$:IFA$=""THEN1925
1930 PRINT"CONGRATULATIONS ";N$(M);"!":PRINTN$(J-N);"'S SHIP DESTROYED!"
1940 PRINT"(Y) AGAIN";
1950 INPUT"";A$
1970 IFMID$(A$,1,2)="//"THEN2980
1980 A$=LEFT$(A$,1):IFA$="D"ORA$="I"THEN980
1985 IFA$="N"ORA$="E"THEN2980
1990 GOTO270
2000 REM***INPUT*FNC***
2010 P1$=LEFT$(P1$,1)
2060 IFP1$=""THENR1=0:RETURN
2061 P=7
2062 P=P-1:IFP=0THEN2100
2063 IFMID$("BDHS/E",P,1)=P1$THEN2080
2064 GOTO2062
2080 IFMID$(A$,1,2)="//"THENP=6
2090 IFP=3THENGOSUB2370
2100 R1=P:RETURN
2119 REM***MAKESHIPS*FNS***
2120 S=(P4-1)*2-1:GOSUB2230:FORK=1TO16:P1=S(K,1)*S:P2=S(K,2):GOSUB1:NEXT:RETURN
2229 REM***GCURSOR*FNG$**
2230 CX=P1:CY=P2:RETURN
2370 REM***HELP*FNH***
2410 SCREEN0:CLS
2420 PRINT:PRINTTAB(10)"* SPACEWAR *":PRINT
2430 PRINT"COMMANDS:":PRINT
2440 PRINT"H=HELP"
2450 PRINT"S=START"
2460 PRINT"D=DISPLAY"
2470 PRINT"I=INSTANT REPLAY AFTER HIT"
2480 PRINT"B=BACK"
2490 PRINT"E=EXIT":
2500 PRINT:PRINT"NEGATIVE # OF STARS=BLACKHOLE"
2510 PRINT:PRINT"(Y) READY TO PLAY (Y/N)";
2520 INPUT"";A$
2530 A$=LEFT$(A$,2):IFA$>""THENA=ASC(A$):A$=CHR$(A-32*(A>96ANDA<123))+MID$(A$,2)
2540 IFA$="N"ORA$="E"THEN2580
2550 IFA$="H"THEN2410
2560 RETURN
2580 CLS
2590 END
2610 REM***FUNCTION DOWN SHIFT*FNS$***
2640 M=0:IFP1$=""THENRETURN
2650 FORI=1TOLEN(P1$)
2660 K=ASC(MID$(P1$,I,1))
2670 IFK=32THENM=0
2680 IFK<65ORK>91THEN2710
2690 IFMTHENP1$=MID$(P1$,1,I-1)+CHR$(K+32)+MID$(P1$,I+1)
2700 M=1
2710 NEXTI:R1$=P1$:RETURN
2740 REM***POLL TERMINAL CAPABILITY*FNI***
2750 R1=1:Q(1)=255:Q(2)=191
2760 RETURN
2970 REM***EXIT***
2980 CLS
2990 END
