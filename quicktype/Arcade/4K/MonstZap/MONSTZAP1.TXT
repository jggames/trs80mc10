0 DIMC1,C2,M$:M=16384:GOTO10
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:RETURN
10 CLS0:PRINT@256+11,;:M$="MONSTER ZAP":GOSUB7
20 FORT=0TO256STEP32:PRINT@T,;:M$="                                ":GOSUB7:NEXT
30 REM******************************
40 REM INITIALISE
50 GOSUB 1000
60 REM******************************
100 REM DRAW SCENE
110 GOSUB 3000
120 REM*****************************
150 REM MAIN LOOP MOVEMENT
160 FOR C=0 TO 31
170 POKEM+C+R*32,42
180 FOR T=1 TO DE:NEXT T
185 KEY$=INKEY$
190 IF KEY$=" " THEN GOSUB 5000 
200 POKEM+C+32*R,32
210 NEXT C
220 IFMLTHEN160
230 REM REPEAT MAIN LOOP
240 GOTO50
250 REM*****************************
990 REM INITIALISE
1000 R=2:ML=1
1005 SC=0:
1010 F=50
1020 DE=35:GL=128
1080 RETURN 
1090 REM****************************
2990 REM DRAW SCENE
3000 PRINT@288,;:M$=" HH     HH     HH     HH     HH ":GOSUB7
3010 PRINT@320,;:M$=" HH "+CHR$(92)+" / HH "+CHR$(92)+" / HH "+CHR$(92)+" / HH "+CHR$(92)+" / HH ":GOSUB7
3020 PRINT@352,;:M$=" HH 0*0 HH 0*0 HH 0*0 HH 0*0 HH ":GOSUB7
3030 PRINT@384,;:M$=" HH < > HH < > HH < > HH < > HH ":GOSUB7
3040 PRINT@416,"ÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿ";
3410 PRINT@448,;:M$=STR$(F)+" SHOTS LEFT     MONSTER ZAP  ":GOSUB7:DE=DE-5:IFDE<15THENDE=15
3420 PRINT@480,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";:RETURN
3430 REM****************************
4990 REM ZAP
5000 FOR I=96 TO 384 STEP 32
5040 POKEM+I+C,33:POKEM+I+C,32
5050 NEXT I
5080 F=F-1
5090 PRINT@448,;:M$=STR$(F)+" ":GOSUB7
6000 IFF=.THEN6020
6010 RETURN
6020 FORI=16672TO16799:T=PEEK(I):POKEI,PEEK(I)+64:IFT=32THENSC=SC+1:IFSC=GLTHENGL=GL+128:SOUND100,2:F=40:GOSUB3000:RETURN
6030 POKEI,PEEK(I)-64:NEXT:PRINT@484,;:M$="SCORE"+STR$(SC):GOSUB7:IFSC>HSTHENHS=SC
6040 PRINT@500,;:M$="HIGH"+STR$(HS):GOSUB7
6050 PRINT@8,;:M$="TRY AGAIN (Y/N)?":GOSUB7
6060 KEY$=INKEY$:IFKEY$=""THEN6060
6070 IFKEY$="Y"THENPRINT@8,;:M$="                ":GOSUB7:POKEM+C+32*R,32:C=31:ML=0:RETURN
6080 IFKEY$="N"THENEND
6090 GOTO6060
7020 REM MONSTERZAP
7022 REM CORE PROGRAM
7024 REM COPYRIGHT FRED HARRIS
7026 REM TANDY/DRAGON VERSION:  MIKE MOORE
7028 REM MC-10 JIM GERRIE
