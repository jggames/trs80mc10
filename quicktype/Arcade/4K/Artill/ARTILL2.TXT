0 GOSUB500:DIMA(13):I=RND(-(PEEK(9)*256+PEEK(10))):FORI=0TO1STEP0:I=-(INKEY$<>""):NEXT
5 FORI=0TO13:A(I)=0:NEXT:Z=0:V2=9:EL=45:V3=10:CL=40:PI=3.1416/180:CLS0:GOSUB390
10 A(0)=B/2+3:A(1)=A-4:A(5)=-2:A(4)=2:A(7)=62-C/2:A(8)=E-4:C$="EL,MV"
20 X=B/2:Y=A-1:X1=0:GOSUB420:X=63-C/2:Y=E-1:X1=1:GOSUB420
30 IFA(13)=0THEN90
40 I=1:GOSUB300:GOSUB310:IFZ=0THENA(9)=H1:A(10)=V1:GOTO120
41 A(13)=0:ONZGOTO120,260,50,50,80
50 IFH1>B/2THEN52
51 CL=CL+3:GOTO110
52 IFH1>B+DTHEN54
53 GOTO100
54 IFCL>45THEN56
55 GOTO60 
56 IFV3>12THEN60
57 GOTO100
60 CL=CL+5:IFCL>75THENCL=72:V3=V3+.5
70 GOTO110
80 GOSUB250:GOTO120
90 GOSUB240:A(11)=-SIN(PI*(90-CL))*V3*.25:A(12)=SIN(CL*PI)*V3*.25:A(13)=1:A(9)=A(7):A(10)=A(8):GOTO120
100 V3=V3+1
110 PRINT@498,C$STR$(CL)","MID$(STR$(V3),2)" ";
120 IFA(6)THEN140
130 IFA$="F"ORA$=" "THENA(4)=SIN(PI*(90-EL))*V2*.3:A(5)=SIN(EL*PI)*V2*.3:A(6)=1:A(2)=A(0):A(3)=A(1):GOSUB240
140 A$=INKEY$:IFA$="W"ORA$="A"THEN160
141 IFA$="Z"ORA$="S"THEN180
142 IFA$="1"THENV2=(V2-1)AND15:GOTO190
143 IFA$="2"THENV2=(V2+1)AND15:GOTO190
144 IFA$="D"THENIFA(6)THENA(6)=0:IFA(3)>0THENRESET(A(2),A(3))
150 GOTO200
160 IFEL<85THENEL=EL+5
170 GOTO190
180 IFEL>5THENEL=EL-5
190 PRINT@482,C$STR$(EL)","MID$(STR$(V2),2)" ";
200 IFA(6)=0THEN30
201 I=0:GOSUB300:GOSUB310:A(2)=H1:A(3)=V1:IFZ=1THENGOSUB260
210 IFZ=5THEN GOSUB250
220 IFZTHENA(6)=0
230 GOTO30
240 FORI=1TO3:SOUNDRND(16)+230,1:NEXTI:RETURN
250 GOSUB340:A(6)=0:A(13)=0:RESET(A(2),A(3)):RESET(A(9),A(10)):RETURN
260 K=((K+1)*7)AND7:FORI=1TO45:SOUNDRND(255),1:SET(A(K)-7+4*K/7+RND(8),A(K+1)-5+RND(8),VAL(MID$("2478",RND(4),1))):NEXT
270 PRINT@0,"YOU ";:IFZ=1THENPRINT"WIN";:VI=VI+1:GOTO280
271 PRINT"LOSE";:LO=LO+1
280 PRINT"!!!","YOU"VI;" ME"LO:PRINT"WANT TO PLAY ANOTHER GAME (Y/N)?";
290 A$=INKEY$:IFA$="N"THENEND
291 IFA$<>"Y"THEN290
292 GOTO5
300 K=I*7:H1=A(K)+A(K+4)*A(K+6):V1=A(K+1)-A(K+5)*A(K+6)+A(K+6)*A(K+6)*.1:A(K+6)=A(K+6)+1:IFA(K+3)<0THENRETURN
301 RESET(A(K+2),A(K+3)):RETURN
310 Z=0:IFH1<.ORH1>63ORV1>31THENZ=4:GOSUB350:RETURN
320 IFV1<0THENRETURN
321 IFPOINT(H1,V1)>0THENZ=3:GOSUB350:RETURN
330 SET(H1,V1,6):RETURN
340 FORI=1TO4:SOUND RND(99),1:NEXTI:RETURN
350 GOSUB340:IF(H1>=B/2)AND(H1<B/2+3)AND(V1>A-4)AND(V1<A+2)THENZ=2:RETURN
360 IF(I=0ANDINT(H1)=INT(A(9))ANDINT(V1)=INT(A(10)))OR(I=1ANDINT(H1)=INT(A(2))ANDINT(V1)=INT(A(3)))THENZ=5:GOSUB340:RETURN
370 IF(H1>63-C/2)AND(H1<66-C/2)AND(V1>E-4)AND(V1<E+2)THENZ=1
380 RETURN
390 B=RND(12)+8:C=RND(12)+8:D=RND(62-B-C):F=6+RND(8):IFRND(2)=2THENA=27:E=RND(14)+12:GOTO400
391 E=27:A=RND(14)+12
400 X=0:FORI=0TOB:Y=A:GOSUB410:NEXT:FORI=1TOD:Y=A-(I*(A-F)/D):GOSUB410:NEXT
401 FORI=1TO63-B-C-D:Y=F+(I*(E-F)/(63-B-C-D)):GOSUB410:NEXT:FORI=1TOC:Y=E:GOSUB410:NEXT:RETURN
410 FORJ=INT(Y)TO29:SET(X,J,1):NEXT:X=X+1:RETURN
420 SET(X,Y,7):SET(X+1,Y,7):SET(X+2,Y,7):SET(X+1,Y-1,7):SET(X+1,Y-2,7):SET(X,Y-2-X1,7):SET(X+2,Y-3+X1,7):RETURN
500 CLS:PRINT@201,"ARTILLERY DUEL":PRINT@261,"BY SPECTRAL ASSOCIATES":PRINT@300,"FEB 1981"
505 PRINT@416,"AZ=ADJUST ANGLE  12=ADJUST POWER":PRINT@449,"D=CANCEL SHOT  SPACE OR F=FIRE":RETURN
510 REM JIM GERRIE 2023
