0 CLEAR100:DIMR(4):M=16384
5 CLS:P=0:PP=0:T=0:S=0:TI=0:C=0:A=0:K=0:F=0:N=0:Q=16831
10 PRINT"      M A D   M O N K E Y S"
20 PRINT""
30 PRINT""
40 PRINT""
50 PRINT""
60 PRINT""
70 PRINT""
80 PRINT""
90 PRINT""
91 PRINT""
92 PRINT""
93 PRINT""
94 PRINT""
95 IFL<>0THENPRINTTAB(8)"HIGH SCORE:"HS:GOTO180
96 PRINT"         INSTRUCTIONS";
110 INPUTA$:IFA$="Y"THEN120
111 GOTO180
120 CLS:PRINT"MONKIES HAVE BEEN UNLEASHED"
121 PRINT"ON THE CITY BY A MADMAN WHO HAS"
122 PRINT"EQUIPPED THEM WITH TIME BOMBS.
123 PRINT"YOUR JOB IS TO TRACK THEM DOWN"
124 PRINT"AND TRANQUILIZE THEM, SO THE"
125 PRINT"BOMBS CAN BE DEFUSED."
126 PRINT"I MUST WARN YOU-- THEY CAN JUMP!";
140 PRINT"THE BOMBS WILL GO OFF IN THIRTY"
141 PRINT"MINUTES.":PRINT
150 PRINT"MOVE USING wasz AND SHOOT WITH"
160 PRINT"THE space KEY. WATCH OUT!"
170 PRINT:PRINT"         **-GOOD LUCK-**"
180 PRINT@448,;:INPUT"DIFICULTY LEVEL (1-9)";L:IFL>9ORL<1THEN180
190 CLS:FORT=1TO10+9*L:POKERND(447)+M,32:NEXT
200 FORT=448TO479:POKET+M,32:NEXT:NN=INT(L/3+.67)+1:PRINT@480,"TIME LEFT:";
210 P=16384:PP=P:S=94:FORT=1TONN:R(T)=Q-T:NEXT:TI=300
220 FORC=1TONN:POKEPP,96:POKEP,S:PP=P:PRINT@490,INT(TI/10);:TI=TI-1:IFTI<1THEN390
250 IFRND(0)<.1THENIFR(C)THENPOKER(C),96:FORT=.TO1STEP.:R(C)=RND(447)+M:T=-(PEEK(R(C))=96):NEXT:SOUND100,1:SOUND1,1:POKER(C),88
260 IFPEEK(2)THENIF4ANDNOTPEEK(16948)THENS=126:P=P+1:GOTO310
270 IFPEEK(2)THENIF1ANDNOTPEEK(16946)THENS=124:P=P-1:GOTO310
280 IFPEEK(2)THENIF8ANDNOTPEEK(16947)THENS=86:P=P+32:GOTO310
290 IFPEEK(2)THENIF4ANDNOTPEEK(16952)THENS=94:P=P-32:GOTO310
300 IFPEEK(2)THENIF8ANDNOTPEEK(16952)THEN400
310 IFP=PPTHEN350
320 T=PEEK(P):IFT=88THEN340
330 IFP>QORP<MORT<>96THENP=PP
331 NEXTC:GOTO220
340 POKEPP,96:POKEP,191:TI=0:GOTO390
350 IFR(C)THENONRND(4)GOTO370,370,371,380
360 NEXTC:GOTO220
370 IFPEEK(R(C)+1)=96THENPOKER(C),96:R(C)=R(C)+1:GOTO385
371 IFPEEK(R(C)-1)=96THENPOKER(C),96:R(C)=R(C)-1:GOTO385
380 IFPEEK(R(C)+32)=96THENIFR(C)+32<QTHENPOKER(C),96:R(C)=R(C)+32:GOTO385
381 IFPEEK(R(C)-32)=96THENIFR(C)-32>MTHENPOKER(C),96:R(C)=R(C)-32
385 POKER(C),88:NEXTC:GOTO220
390 PRINT@0,"         ***YOU LOSE***":GOTO500
400 A=.:IF(P+32>QANDS=86)OR(P-32<MANDS=94)THENNEXTC:GOTO220
401 F=P:IFS=94THENK=-32:T=97:GOTO430
410 IFS=86THENK=32:T=97:GOTO430
420 IFS=124THENK=-1:T=109:GOTO430
421 K=1:T=109
430 IFPEEK(F+K)<>96ORF+K<MTHEN460
440 POKEF+K,T:POKEP,S:F=F+K:A=A+1:REM DELAY IN SHOT
441 IFA<16THENPOKEF,96:POKEP,S:GOTO430
450 POKEF,96:POKEP,S:NEXTC:GOTO220
460 IFPEEK(F+K)<>88THENNEXTC:GOTO220
485 F=F+K
490 FORT=1TONN:IFF=R(T)THENPOKEF-K,96:POKEP,S:POKEF,106:R(T)=.:N=N+1:POKEF,223:T=NN
491 NEXT:SOUND200,3:IFN<NNTHENNEXTC:GOTO220
492 PRINT@0,"      **YOU GOT THEM ALL**"
500 PRINT@495,"HIT enter";:SC=INT(TI/10)*L*N:PRINTSC;
510 IFSC>HSTHENHS=SC
520 IFINKEY$<>CHR$(13)THEN520
530 GOTO5
