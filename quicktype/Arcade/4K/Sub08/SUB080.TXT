10 TL=16384
15 CLS
20 PRINT"SUBMARINES"
25 PRINT"BY IAN CREASEY / ALAIN STEMMER"
30 PRINT"SPACE BAR TO DROP BOMB"
35 PRINT"A AND S TO MOVE TARGET"
50 PRINT:PRINT "HIT SPACE BAR"
60 I$=INKEY$:IF I$<>" "THEN60
65 I$=INKEY$:IF I$<>""THEN65
110 CLS
120 FOR I=1TO352:PRINT@127+I,CHR$(128):NEXTI
140 P=TL+64:SC=0:S=0:G=TL+160:M=5:TS=G:C=0:T=TL+480:B=0
145 GOSUB310
150 GOSUB200:IF S=0THEN S=G:G=G+32:C=C+1:IF C=M THEN TS=TS+32:G=TS:C=0:M=M-1:IF M=0THEN TS=TL+160:M=5
160 IF PEEK(S+2)=143+112THEN SOUND220,15:GOTO320
170 POKE S,128:S=S+1:POKE S,135+48:POKE S+1,139+48
180 K$=INKEY$:GOSUB200:POKE T,96
182 IF (K$="A"AND T>TL+480 AND PEEK(T-1)=128) THEN T=T-1
184 IF (K$="S" AND T<TL+511) THEN T=T+1
190 POKE T,143+112:GOTO 150
200 GOSUB240:POKE P,96:P=P-1
201 IF P=TL+34 THEN POKE P,96:POKE P-1,96:POKE P-2,96:P=TL+63
210 POKE P,128:POKE P-1,140:POKE P-2,140
215 POKE P-3,140:GOSUB240
220 IF K$=" " AND B=0 THEN B=P+32:N=13:D=32+((T-(TL+480))-(P-(TL+32.5)))/N:H=96
230 RETURN
240 IF B=0 THEN RETURN
250 POKE B,H:B=B+D:H=PEEK(B):IF H=135+48 OR H=139+48 THEN280
260 POKE B,143+64:SOUND180+N,2:N=N-1:IF N=0THEN POKE B,96:B=0
270 RETURN
280 FOR I=0TO50STEP5
290 SOUND100+I,3:POKE B,42:POKE B+1,42
295 POKE B-1,42:NEXT I
300 POKE B,128:POKE B+1,128:POKE B-1,128:B=0:S=0:SC=SC+N:IF SC>HI THEN HI=SC
310 PRINT@0,"SCORE :"SC TAB(11) "RECORD :"HI:RETURN
320 PRINT@64,"ANOTHER GAME?(Y/N)"
330 I$=INKEY$:IF I$<>"Y" AND I$<>"N" THEN330
340 IF I$="Y"THEN110
350 CLS:END
700 REM VARIABLES
705 REM P POSITION OF PLANE
710 REM SC SCORE
715 REM S POSITION OF SUBMARINE
720 REM T POSITION OF TARGET
725 REM B POSITION OF BOMB
730 REM D DIRECTION OF BOMB
735 REM HI HIGH SCORE
