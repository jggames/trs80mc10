10 REM SLALOM 
15 REM TABLEAU DES POSITIONS 
16 REM DU SKIEUR 
17 VND=16384+511
20 DIM S$(1) 
30 FOR I=1 TO 32 
40 E$=E$+CHR$(207)
50 NEXT I 
55 REM SKIEUR ALLANT A GAUCHE 
60 S$(0)=CHR$(201) 
65 REM SKIEUR ALLANT A DROITE 
70 S$(1)=CHR$(198) 
75 REM ECRAN BLANC 
80 CLS 5 
85 REM DIRECTION INITIALE 
86 REM GAUCHE 
90 D=-1 
95 REM POSITION INITIALE 
96 REM DU SKIEUR 
100 J=16 
105 REM DESSIN DES PORTES 
110 P$=CHR$(181)+CHR$(207)+CHR$(207)+CHR$(170) 
115 REM BOUCLE PRINCIPALE 
120 FOR K=1 TO 300 
125 REM CALCUL DES COORDONNEES 
126 REM DU SKIEUR 
130 Y=INT(J/32)*2 
140 X=(J-16*Y)*2 
145 REM SKIEUR AU NIVEAU D UNE 
146 REM PORTE 
150 IF K>=16 AND (K-5)/10=INT((K-5)/10) THEN GOSUB 350 
155 REM AFFICHAGE D UNE PORTE 
160 IF K<284 AND K/10=INT(K/10) THEN GOSUB 400 
165 REM MOUVEMENT DU SKIEUR 
170 IF INKEY$<>"" THEN D=-D 
180 J=J+D 
190 IF J<2 THEN J=2 
200 IF J>29 THEN J=29 
210 PRINT@ 511,E$;:POKEVND,207 
220 PRINT@ J,S$(D/2+0.5); 
230 NEXT K 
235 REM FIN DESCENTE 
240 PRINT@ 164,"PORTE(S) RATEE(S) :";T; 
250 PRINT@ 230,"UNE AUTRE DESCENTE ?"; 
260 D$=INKEY$ 
270 IF D$="" THEN 260 
280 IF D$<>"N" THEN RUN 
290 CLS 
295 REM AFFICHAGE PORTE 
300 END 
345 REM PORTE RATEE 
350 IF POINT(X-2,Y)<>0 OR POINT(X+4,Y)<>3 THEN IF POINT(X-4,Y)<>0 OR POINT(X+2,Y)<>3 THEN T=T+1:SOUND 1,1 
360 RETURN 
400 P1=RND(3)-2 
410 P=P-6*P1 
420 IF P<482 THEN P=488 
430 IF P>506 THEN P=500 
440 PRINT@ P,P$; 
450 RETURN
