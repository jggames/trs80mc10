100 REM * BROKEN FIELD NIGHTMARE * TRS-80 COLOR COMPUTER *
110 REM * COLOR BASIC, EXTENDED COLOR BASIC, MC-10 BASIC.
120 REM * ELMER'S ARCADE # 8 / RICHARD RAMELLA
130 Q=16384
140 REM *** FOR CCOC MAKE LINE 130 Q=1024
150 CLS0
160 PRINT@133,"BROKEN FIELD NIGHTMARE ";
170 FORT=1TO2500
180 NEXTT
190 CLS0
200 M=10 
210 H=301
220 U$="W":REM COCO USE CHR$(94)
230 D$="Z":REM COCO USE CHR$(10)
240 L$="A":REM COCO USE CHR$(8)
250 R$="S":REM COCO USE CHR$(9)
260 A$=CHR$(246)
270 Z$=CHR$(233)
280 FORA=160TO190
290 IFA=160THENFORB=ATOA+320STEP32:PRINT@B,CHR$(207);:PRINT@B+30,CHR$(175);:NEXTB
300 PRINT@A,CHR$(207);
310 PRINT@A+320,CHR$(207);
320 NEXTA
330 PRINT@15,"TIME:";H-N-1;
340 PRINT@47,"TACKLERS:"M;
350 FORT=1TO2500
360 NEXTT
370 FORC=1TOM
380 PRINT@163+RND(9)*32+RND(26),Z$;
390 NEXT
400 A=161+RND(9)*32
410 PRINT@A,A$;
420 B$=INKEY$
430 N=N+1
440 PRINT@98,H-N;
450 IFH-N<1THEN720
460 S=A
470 IFB$<>U$THENIFB$<>L$THENIFB$<>D$THENIFB$<>R$THEN420
480 IFB$=U$THENIFPEEK(Q+A-32)=128THENA=A-32:B$=""
490 IFB$=D$THENIFPEEK(Q+A+32)=128THENA=A+32:B$=""
500 IFB$=L$THENIFPEEK(Q+A-1)=128THENA=A-1:B$=""
510 IFB$=R$THENIFPEEK(Q+A+1)=128THENA=A+1:B$=""
520 PRINT@S,CHR$(128);
530 PRINT@A,A$;
540 IFPEEK(Q+A+1)=175THEN580
550 IFB$=U$ANDPEEK(Q+A-32)<>128ORB$=D$ANDPEEK(Q+A+32)<>128ORB$=L$ANDPEEK(Q+A-1)<>128THEN720
560 IFB$=R$THENIFPEEK(Q+A+1)<>128THEN720
570 GOTO420
580 SC=SC+(H-N)
590 PRINT@0,"SCORE:"SC;
600 H=H-10
610 N=0
620 M=M+5
630 FORT=1TO5
640 SOUND176,1
650 SOUND193,1
660 SOUND204,1
670 SOUND219,1
680 NEXTT
690 CLS0
700 PRINT@0,"SCORE:"SC;
710 GOTO280
720 FORA=0TO14
730 PRINT@A,CHR$(128);
740 NEXT
750 PRINT@98,"FINAL SCORE:"SC*M;:PRINT@0,"AGAIN?";
760 FORT=1TO2
770 SOUND218,2
780 SOUND216,2
790 SOUND210,2
800 SOUND204,4
810 SOUND210,6
820 SOUND216,6
830 SOUND218,6
840 IFT=1THENSOUND227,2:SOUND227,4:FORK=1TO10:NEXTK:SOUND227,6
850 IFT=2THENSOUND229,8
860 FORK=1TO50
870 NEXTK
880 NEXT
890 M$=INKEY$:IFM$="Y"THENRUN
895 IFM$<>"N"THEN760
900 END
