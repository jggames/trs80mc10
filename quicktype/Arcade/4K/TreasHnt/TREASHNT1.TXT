20 SC=16384
30 D$="ßßßßßßßßßßß"
40 GOTO 1000
100 REM **SCREEN**
110 CLS6
120 PRINT"ßßßßßßßßßßßßßßßßßß ßßßßßßßßßßßßß";
130 PRINT"ß   3ß2ßß     6ß   ßßßTREASUREßß";
140 PRINT"ß5ß ßß    ßß4ßß  ßßßßßßßHUNTßßßß";
150 PRINT"ßßß   ß ß  ßß5ß ßß7ßßßßßßßßßßßßß";
160 PRINT"ß   ß3ß ßß    ß    ßßßßßUSE:ßßßß";
170 PRINT"ß ß ßß   ß ß ßß ßßßßßßßßWASDßßßß";
180 PRINT"ß ß  ß ß ß ß5ß  ß 9ßßßßßßßßßßßßß";
190 PRINT"ß ßß   ß3ß ßßß ßß ßßßßßßßßßßßßßß";
200 PRINT"ß   ßß ßßß ß4ß  ß 8ßßßßßßßßßßßßß";
210 PRINT"ß ß ß3 ß     ßß ß ßßßßßßßßßßßßßß";
220 PRINT"ßßß ßßß1 ßßßß3ß ß  ßßßßßßßßßßßßß";
230 PRINT"ß    1ßß ß    ß ßß ßßßßßßßßßßßßß";
240 PRINT"ß ß ßß3ß ß ß ßß    ßßßßßßßßßßßßß";
250 PRINT"ß ß    ß   ß    ßß7ßßßßßßßßßßßßß";
260 PRINT"ß ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";
300 Y=14:X=1
310 PL=32*Y+X
320 POKESC+PL,64
330 PRINT@32*7+24,;P2;:PRINT@32*9+24,NC;
340 RETURN
400 REM **PLAYER MOVES**
410 T1=400-INT(30*NC*RND(0))
420 FORDU=1TOT1
430 DI$=INKEY$:IFDI$=""THEN510
440 VX=(DI$="A")-(DI$="D")
450 VY=(DI$="W")-(DI$="S")
460 NP=32*(Y+VY)+(X+VX)
470 PK=PEEK(SC+NP):IFPK<>96AND(PK<113ORPK>121)THEN510
480 POKESC+PL,96:X=X+VX:Y=Y+VY:PL=NP
490 PO=PK-112:IFPO<0THEN510
500 P1=P1+PO:P2=P2+PO:PRINT@32*7+24,P2;
510 POKESC+PL,64:IFPL=32THENDU=T1
520 NEXTDU:IFPL>32*15THEN410
530 RETURN
600 REM **BIRD ATTACKS**
610 DT=15-3*NC
613 FORI=1TO10:DI$=INKEY$:NEXT
620 BU=32*Y+19+RND(5)
630 PK=PEEK(SC+BU):POKESC+BU,95:FORDE=1TODT:NEXT:DI$=INKEY$:IFDI$=" "ORPK=64THENRETURN
680 POKESC+BU,PK:BU=BU-1:GOTO630
1000 REM **MAIN PROGRAM**
1010 GOSUB100:REM SCREEN
1020 GOSUB400:REM MOVE
1030 IFPL<32THENSOUND200,1:NC=NC+1:P2=P2+NC*P1:P1=0:GOTO1010
1040 GOSUB600:REM BIRD
1050 IFPK<>64THENPOKESC+BU,PK:P1=P1+1:GOTO1020
1060 SOUND1,1:PRINT@32*12+22,"TRY AGAIN";:PRINT@32*13+24,"(Y/N)";:PRINT@32*14+26,"?";
1070 DI$=INKEY$:IFDI$=""THEN1070
1080 IFDI$="Y"THENRUN
1090 IFDI$="N"THENEND
2000 GOTO1070
2010 REM **TREASURE HUNT**
2020 REM HAL RENK/SAM EDWARDS
2010 REM MC-10 EDITS JIM GERRIE
