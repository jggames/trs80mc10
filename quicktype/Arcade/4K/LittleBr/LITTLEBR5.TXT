5 CLS:DIMT,X,Y,I,J,K,Q,W,V,E,A,B,C,D,P,F,G,O,L,A$,B$:POKE16925,0:POKE16926,1:GOTO15
10 P=PEEK(2)ANDPEEK(O):IFPTHENFORT=QTOQ+4STEP2:SET(.,T,A):NEXT
11 Q=Q+(P=F)*2-(P=G)*2:IFQ<.THENQ=.:GOTO13
12 IFQ>19THENQ=20
13 FORT=QTOQ+4STEP2:SET(.,T,D):NEXT:RETURN
15 A=3:B=6:C=8:D=2:E=5:F=65:G=90:O=17023:CLS:PRINT@8,"*** BREAKOUT ***":PRINT
20 PRINT"OBJECT IS TO DESTROY ALL BRICKS":PRINT"USE a & z KEYS TO MOVE PADDLE"
21 PRINT:INPUT "HI, WHAT'S YOUR NAME";A$:A$=LEFT$(A$,10)
25 INPUT"STANDARD COLORS AND KEYS (Y/N)";B$:IFLEFT$(B$,1)<>"N"THEN40
26 PRINT"BACKGROUND";:GOSUB95:A=E:PRINT"EVEN BRICK";:GOSUB95:B=E:PRINT"ODD BRICK";:GOSUB95:C=E
27 PRINT"PADDLE ";:GOSUB95:D=E:PRINT"BALL";:GOSUB95:INPUT"UP KEY";B$:F=ASC(B$+"A"):INPUT"DOWN KEY";B$:G=ASC(B$+"Z")
40 CLSA:Y=0:FORI=32TO62STEP4:FORT=0TO24:SET(I,T,B):NEXT:X=X+1:Y=(1ANDX)*2:FORJ=YTO24STEP4:SET(I,J,C):NEXT
45 NEXT:PRINT@416,"                1 2 3 4 5 6 7 8 ";:FORT=16384+480TO16384+511:POKET,96:NEXT
46 PRINT@448,"SCORE":S=0:X=10:Y=10:L=6:Q=8
50 SET(X,Y,A):X=22:Y=RND(12)*2:V=-2:W=INT(RND(0)*5)-2:L=L-1:IFL<1THEN120
51 IFL>1THENPRINT@448+16,MID$(STR$(L),2)" BALLS LEFT":FORI=1TO35:GOSUB10:NEXT
55 IFL=1THENPRINT@448+16,"last ball":FORI=1TO35:GOSUB10:NEXT
56 I=X:K=Y:J=Y
60 SET(X,Y,A):SET(I,K,E):X=I:Y=J:J=Y+W:IFJ>=.THENIFJ<26THEN65
61 W=-W:J=Y:SOUND50,1
65 I=X+V:IFI<.THENSOUND1,1:GOTO50
66 GOSUB10:K=J:IFI>63THEN75
67 IFPOINT(I,K)=ATHEN60
68 IFITHEN100
70 SOUND50,1:V=2:W=RND(5)-3:GOTO60
75 I=X
80 V=-V:SOUND50,1:GOTO60
90 PRINT"INVALID, REENTER";
95 INPUT" COLOR (1 TO 8)";E:IFE<0ORE>8THEN90
96 RETURN
100 IFPOINT(I,K)=BORPOINT(I,K)=CTHENV=ABS(V):SET(I,K,A):S=S+I/4-7:PRINT@453,S;:W=RND(5)-3
105 SOUND100,1 
110 IFS<468THEN80
115 PRINT@416,"CONGRATULATIONS, ";A$:PRINT"YOU WIN!":GOTO161
120 SOUND1,8:PRINT@416,A$" YOUR SCORE OF"S"IS ":PRINT@448,;:ONINT(S/66.86)GOTO130,135,140,145,155,160
125 PRINT"TERRIBLE!":GOTO161
130 PRINT"LOUSY.":GOTO161
135 PRINT"POOR.":GOTO161
140 PRINT"GOOD.":GOTO161
145 PRINT"VERY GOOD.":GOTO161
155 PRINT"EXCELLENT.":GOTO161
160 PRINT"NEARLY PERFECT."
161 IFS>HSTHENHS=S
162 PRINT@464,"HIGH"HS
165 PRINT@480,"ANOTHER GAME (Y/N)?";
166 B$=INKEY$:IFB$="Y"THENCLS:GOTO15
167 IFB$="N"THENCLS:PRINT"GAME OVER":END
168 GOTO166
170 REM LITTLE BRICK OUT BY
180 REM STEVE WOZNIAK
190 REM MC-10 EDITS JIM GERRIE
