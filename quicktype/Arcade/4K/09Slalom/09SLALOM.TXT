10 TL=16384:DIM G(4)
15 CLS
20 PRINT"SLALOM"
25 PRINT"BY IAN CREASEY / ALAIN STEMMER"
30 PRINT"H TO CHANGE HORIZ DIRECTION"
35 PRINT"V TO CHANGE VERT DIRECTION"
50 PRINT:PRINT "HIT SPACE BAR"
60 I$=INKEY$:IF I$<>" "THEN60
65 I$=INKEY$:IF I$<>""THEN65
100 CLS:PRINT"GATES":PRINT"NARROW, MEDIUM, WIDE?(N/M/W)";
110 I$=INKEY$:IF I$=""THEN110
112 IF I$="N"THEN W=2:GOTO120
114 IF I$="M"THEN W=3:GOTO120
116 IF I$="W"THEN W=4:GOTO 120
118 GOTO110
120 PRINT I$
130 IF G(1)=0THEN 170
140 PRINT:PRINT"NEW COURSE?(Y/N) ";
150 I$=INKEY$:IF I$<>"Y"AND I$<>"N"THEN150
160 PRINT I$:IF I$="N"THEN200
170 FOR I=1TO4:G(I)=RND(12-W):NEXT I
200 CLS:FOR I=TL+32TO TL+62:POKE I,134+48:POKE I+416,134+48:NEXT I
210 FOR I=TL+64TO TL+416STEP32:FOR J=0TO30STEP6:POKE I+J,134+48:NEXT J:NEXT I
220 FOR I=1TO4:FOR J=1TO W:POKE TL+(G(I)+J)*32+I*6,96:NEXT J:NEXT I
230 X=RND(3):Y=RND(11)+1
232 IF RND(2)>1THEN XD=-1:YD=-1:GOTO238
234 XD=1:YD=1
238 G=0:S=0
300 IF PEEK(TL+X+32*Y+XD)<>96THEN XD=-XD:SOUND130,5
305 S=S+1
310 IF PEEK(TL+X+32*Y+YD*32)<>96THENYD=-YD:SOUND130,5
315 IF PEEK(TL+X+32*Y+XD+YD*32)<>96THEN XD=-XD:YD=-YD:SOUND130,5
320 I$=INKEY$:IF I$="H" THEN XD=-XD:SOUND155,5:GOTO300
330 IF I$="V"THENYD=-YD:SOUND155,5:GOTO300
340 POKE(TL+X+32*Y),96:X=X+XD:Y=Y+YD:POKE(TL+X+32*Y),42
345 IF X=29THEN500
350 IF X/6>INT(X/6)THEN300
400 G=G+1:FORI=1TO W:POKE TL+(G(G)+I)*32+G*6,48+G:NEXT I
410 FOR I=200TO240:SOUNDI,1:NEXT I
415 X=X+1:GOTO300
500 FOR I=0TO4:SOUND100,10:FORJ=1TO20:NEXT J:NEXT I
510 PRINT@0,"TIME "S" SECONDS"
520 PRINT@327,"ANOTHER GAME?(Y/N)";
530 I$=INKEY$:IF I$<>"Y"AND I$<>"N"THEN530
540 IF I$="Y"THEN100
550 CLS:END
700 REM VARIABLES
705 REM W WIDTH OF GATES
710 REM X,Y YOUR COORDS
715 REM XD,YD YOUR DIRECTION
720 REM G NUMBER OF GATE
725 REM S TIME
