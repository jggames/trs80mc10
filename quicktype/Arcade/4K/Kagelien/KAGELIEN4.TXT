0 CLEAR200:DIMC1,C2,C3,O,M$,X,Y,C,D,P,Q,B,W,V:O=16384:POKE16925,0:POKE16926,1:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO1000
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+O:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3+64*(C3>63):NEXT:RETURN
8 PRINT@23,;:M$=LEFT$("XXXXXXXX",M)+" ":GOSUB7:RETURN
9 FORC1=2TO13:C3=O+32*C1:FORC2=C3+1TOC3+13:POKEC2,32:NEXT:NEXT:RETURN
1000 FORC1=0TO4:POKE20000+C1,VAL(MID$("198032126251214",C1*3+1,3)):NEXT:GOSUB2000
1210 S=0:R=1:M=2:U=500
1220 EXEC20000:Z=R*7
1230 PRINT@0,;:M$="SCORE"+STR$(S):GOSUB7:PRINT@14,;:M$="ROUND"+STR$(R):GOSUB7:GOSUB8
1240 B$="#############################"
1250 PRINT@32,;:M$=B$:GOSUB7:FORI=2TO13:POKE32*I+O,35:POKE32*I+O+14,35:POKE32*I+O+28,35:NEXT:PRINT@32*14,;:GOSUB7
1260 FORI=1TOR*4
1270 POKEO+14+RND(13)+32*(1+RND(12)),35:NEXT
1280 POKEO+7+32*7,24:POKEO+7+32*7+14,24
1290 FORI=1TOR*7
1300 A=14+RND(13):B=1+RND(12):IFPEEK(O+A+32*B)<>32THEN1300
1310 POKEO+A+32*B,36:NEXT
1320 X=7:Y=7:C=1:D=2:P=32:Q=32:A=32:B=(R+5)/20:F=0:G=8:T=(R+1)*100
1330 POKEO+C+A*D,0
1340 REM MAIN LOOP
1350 FORC1=150TOLV*50STEP-1:NEXT:K=PEEK(2)ANDPEEK(17023):IFK<>AORS<100THEN1380
1360 POKE200,1:POKE49151,64:S=S-100:F=F-B*G:GOSUB9:SOUND1,1:PRINT@5,:M$=STR$(S)+"  ":GOSUB7:G=G-1:IFG<1THENG=1
1380 V=X+(K=65)-(K=68):W=Y+(K=87)-(K=83):POKEO+X+A*Y,A:POKEO+X+A*Y+14,A
1400 P=PEEK(O+V+14+A*W):IFP=35THENV=X:W=Y:GOTO1400
1410 X=V:Y=W:Q=PEEK(O+X+A*Y):POKEO+X+A*Y,24:POKEO+X+A*Y+14,24
1420 IFP=36THENSOUND113,1:SOUND129,1:P=32:S=S+10:Z=Z-1:PRINT@5,:M$=STR$(S):GOSUB7
1430 IFS<UTHEN1450
1440 SOUND103,4:SOUND87,4:SOUND161,4:SOUND87,4:U=U+1000:M=M+1:GOSUB8
1450 IFZ=.THEN1540
1460 T=T-1:IFT<50THENPOKE49151,64:IFT=.THEN1580
1470 IFQ=42ORQ=.THEN1580
1480 F=F+B:IFF<1THEN1350
1490 J=A:IFRND(0)<.3ANDPEEK(O+C+14+A*D)=ATHENJ=42
1500 F=F-1:POKEO+C+A*D,J:C=C+SGN(X-C):D=D+SGN(Y-D):Q=PEEK(O+C+A*D):POKEO+C+A*D,.:IFQ=24THEN1580
1520 GOTO1350
1530 REM LEVEL CLEARED
1540 PRINT@3+32*4,"C L E A R";
1550 FORI=0TO7:SOUND113,1:SOUND142,1:SOUND161,1:SOUND103,1:NEXT:FORI=1TO500:NEXT
1560 R=R+1:S=S+T:GOTO1220
1570 REM MAN LOST
1580 POKEO+X+14+32*Y,191:POKE49151,64:FORI=1TO300:NEXT:SOUND1,2:M=M-1:IFM<0THEN1630
1590 GOSUB9
1600 GOSUB8:POKEO+X+14+32*Y,P
1610 GOTO1320
1620 REM GAME OVER
1630 IFS>HSTHENHS=S
1635 PRINT@480,;:M$="HIGH"+STR$(HS):GOSUB7:PRINT@3+32*4,"GAME OVER";
1640 PRINT@494,;:M$="TRY AGAIN (Y/N)?":GOSUB7
1650 M$=INKEY$:IFM$=""THEN1650
1660 IFM$="Y"THEN1210
1670 IFM$="N"THENEND
1680 GOTO1650
2000 EXEC20000
2040 PRINT@32*5+5,;:M$="<<<((("+CHR$(91)+CHR$(91)+CHR$(91)+"IIII"+CHR$(93)+CHR$(93)+CHR$(93)+")))>>>":GOSUB7
2050 PRINT@32*6+5,;:M$="<      KAGELIEN      >":GOSUB7
2060 PRINT@32*7+5,;:M$="<MICOM BASIC MAGAZINE>":GOSUB7
2070 PRINT@32*8+5,;:M$="<      AUG 1985      >":GOSUB7
2080 PRINT@32*9+5,;:M$="<<<((("+CHR$(91)+CHR$(91)+CHR$(91)+"IIII"+CHR$(93)+CHR$(93)+CHR$(93)+")))>>>":GOSUB7
2085 PRINT@32*11+12,;:M$="USE WASD":GOSUB7:PRINT@490,;:M$="LEVEL? (1-3)":GOSUB7
3000 LV=VAL(INKEY$):IFLV<1ORLV>3THEN3000
3010 RETURN
