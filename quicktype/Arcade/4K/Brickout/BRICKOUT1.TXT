    2 POKE -16298,0
    5 CALL -936: GOSUB 200
   10 DIM A$(15),B$(3): TEXT : CALL -936: PRINT "     *** BREAKOUT ***": PRINT : PRINT "OBJECT IS TO DESTROY ALL BRICKS": PRINT : PRINT : GOTO 35
   15 Q=( PDL (0)-20)/6: IF Q<0 THEN Q=0: IF Q>=34 THEN Q=34: COLOR= D: VLIN Q,Q+5 AT 0: COLOR= A: IF P>Q THEN 20: IF Q THEN VLIN 0,Q-1 AT 0:P=Q: RETURN 
   20 IF P=Q THEN RETURN : IF Q#34 THEN VLIN Q+6,39 AT 0:P=Q: RETURN 
   25 FOR I=1 TO 80:Q= PEEK (-16336): NEXT I: GOTO 75
   30 GR : GOSUB 60
   35 INPUT "HI, WHAT'S YOUR NAME? ",A$: CALL -936: VTAB 19:A=1:B=13:C=9:D=6:E=15: GR : GOSUB 60
   40 CALL -936: PRINT "STANDARD COLORS ";A$;"? ";: INPUT B$
   45 GR : IF LEN(B$)=0 THEN 70: IF B$(1,1)="Y" THEN 70: FOR I=0 TO 31: COLOR= I/2: VLIN 0,39 AT I: NEXT I: CALL -936
   50 POKE 34,20: FOR I=0 TO 15: VTAB 21+I MOD 2: TAB I+I+1: PRINT I;: NEXT I: POKE 34,22: VTAB 24: PRINT : PRINT "BACKGROUND";
   55 GOSUB 120:A=E: PRINT "EVEN BRICK";: GOSUB 120:B=E: PRINT "ODD BRICK";: GOSUB 120:C=E: PRINT "PADDLE";: GOSUB 120:D=E: PRINT "BALL";: GOSUB 120: GOSUB 60: GOTO 75
   60 POKE 34,20: COLOR= A: FOR I=0 TO 39: VLIN 0,39 AT I: NEXT I: FOR I=20 TO 34 STEP 2: TAB I+1: PRINT I/2-9;: COLOR= B: VLIN 0,39 AT I: COLOR= C: FOR J=I MOD 4 TO 39 STEP 4
   65 VLIN J,J+1 AT I: NEXT J,I: TAB 5: PRINT "SCORE = 0": PRINT : PRINT : POKE 34,21:S=0:P=S:L=S:X=19:Y=19:L=4: RETURN 
   70 GOSUB 60
   75 COLOR= A: PLOT X,Y/3:X=19:Y= RND (120):V=-1:W= RND (5)-2:L=L-1: IF L<1 THEN 145: TAB 6: IF S<(15) THEN IF L>S THEN L=L+2
   80 IF L>1 THEN PRINT L;" BALLS LEFT.": IF L=1 THEN PRINT "LAST BALL, ";A$: PRINT : FOR I=1 TO 100: GOSUB 15: NEXT I:M=1:N=0
   85 J=Y+W: IF J>=0 AND J<120 THEN 90:W=-W:J=Y: FOR I=1 TO 6:K= PEEK (-16336): NEXT I
   90 I=X+V: IF I<0 THEN 25: GOSUB 15: COLOR= A:K=J/3: IF I>39 THEN 100: IF SCRN( I,K)=A THEN 110: IF I THEN 125:N=N+1:V=(N>5)+1:W=(K-P)*2-5:M=1
   95 Z= PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)+ PEEK (-16336): GOTO 110
  100 FOR I=1 TO 6:M= PEEK (-16336): NEXT I:I=X:M=0
  105 V=-V
  110 PLOT X,Y/3: COLOR= E: PLOT I,K:X=I:Y=J: GOTO 85
  115 PRINT "INVALID.  REENTER";
  120 INPUT " COLOR (0 TO 15)",E: IF E<0 OR E>15 THEN 115: RETURN 
  125 IF M THEN V= ABS (V): VLIN K/2*2,K/2*2+1 AT I:S=S+I/2-9: VTAB 21: TAB 13: PRINT S
  130 Q= PEEK (-16336)- PEEK (-16336)+ PEEK (-16366)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)+ PEEK (-16336)- PEEK (-16336)
  135 IF S<720 THEN 105
  140 PRINT "CONGRATULATIONS, ";A$;" YOU WIN!": GOTO 190
  145 PRINT "YOUR SCORE OF ";S;" IS ";: GOTO 150+(S/100)*5
  150 PRINT "TERRIBLE!": GOTO 190
  155 PRINT "LOUSY.": GOTO 190
  160 PRINT "POOR.": GOTO 190
  165 PRINT "FAIR.": GOTO 190
  170 PRINT "GOOD.": GOTO 190
  175 PRINT "VERY GOOD.": GOTO 190
  180 PRINT "EXCELLENT.": GOTO 190
  185 PRINT "NEARLY PERFECT."
  190 GOSUB 250
  192 CALL -756
  195 INPUT "SAME COLORS? ",B$: GOTO 45
  200 VTAB 3: PRINT "* *  BREAKOUT FOR BEGINNERS * *": PRINT 
  205 PRINT "* *    FROM THE RED BOOK    * *": PRINT 
  210 PRINT "* *  MOD. BY LE ROY LARSEN  * *": PRINT : PRINT : PRINT 
  220 PRINT "   THIS BREAKOUT WILL AUTOMATICALLY ": PRINT 
  225 PRINT "ADJUST TO A BEGINNERS SKILL BY GIVING": PRINT 
  230 PRINT "'EXTRA' BALLS.. IT PLAYS THE SAME OLD": PRINT 
  235 PRINT "GAME TO THE MORE SKILLED..": PRINT : PRINT 
  250 VTAB 21: PRINT " PRESS 'ESC' TO END"
  255 PRINT " PRESS THE SPACE BAR TO CONTINUE..."
  260 IF PEEK (-16384)=155 THEN 290
  265 IF PEEK (-16384)<>160 THEN 250
  270 RETURN 
  290 POKE -16298,0: TEXT : CALL -936: VTAB 10: TAB 10: PRINT "* * * THE  END * * *": END 
  295 REM *COPYRIGHT 1978 APPLE         COMPUTER, INC.*
  300 REM 
  305 REM   MODIFIED TO ADJUST
  310 REM   FOR BEGINNERS...
  315 REM 
  320 REM   BY LE ROY W. LARSEN 
  325 REM          FOR  
  330 REM  APPLE USERS EVERYWHERE
  335 REM 
  340 REM   'BREAKOUT/B'
  345 END 

