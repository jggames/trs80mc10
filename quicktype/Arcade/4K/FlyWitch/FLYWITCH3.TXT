REM Usborne book called Weird Computer Games, p. 10
10 POKE49151,64:CLEAR250:I=RND(-(PEEK(9)*256+PEEK(10))):GOSUB 700 : GOSUB 620
REM 20 Set up variables (See if you can work outwhat they are.)
20 NW=4 : M=0 : FM=50
REM Choose new height "NH". "H" is heigh
30 GOSUB 530 : H=NH
40 GOSUB 550
50 EF=0 : FF=0 : HH=H
60 FOR I=1 TO 4 : G(I)=0 : NEXT I
70 FOR I=1 TO 4 : A$=P$(I)+":" : X=1
80 Y=1+I : GOSUB 610 : NEXT I
90 GOSUB 490 : GOSUB 430
100 IF M=FM THEN 250
110 IF EF=1 THEN 230
120 IF INKEY$="" THEN 90
130 HH=H-1 : GOSUB 510
140 GOSUB 490 : GOSUB 430
150 H=HH
160 IF H>0 THEN 130
170 IF P=17 THEN GOSUB 320
180 IF FF=1 THEN 240
190 GOSUB 530
200 HH=H+1 : GOSUB 510 : GOSUB 490
210 H=HH : IF H=NH THEN 90
220 GOSUB 430 : GOTO 200
230 A$="SUPERWITCH IS PLEASED WITH YOU!": GOTO260
240 A$="OUT OF BROOMSTICKS!" : GOTO 260
250 A$="TOO LATE!"
260 X=0 : Y=0 : GOSUB 610
270 POKE49151,0:END
280 A$="W" : X=P : GOSUB 610
290 FOR T=1 TO 1000 : NEXT T
300 NW=NW-1 : IF NW=0 THEN FF=1
310 RETURN
320 IF W=1 THEN GOSUB 280 : RETURN
330 G(R)=G(R)+1
340 A$=STR$(G(R)) : X=17
350 Y=1+R : GOSUB 610
360 P=19 : GOSUB 430
370 F=0
380 FOR I=1 TO 4
390 IF G(I)>=N(I) THEN F=F+1
400 NEXT I
410 IF F=4 THEN EF=1
420 RETURN
430 GD=GD+1 : IF GD=6 THEN GD=1
440 A$=G$(GD) : X=1 : Y=15 : GOSUB 610
450 A$=" " : X=P : Y=14 : GOSUB 610
460 P=P+1 : IF P=20 THEN GOSUB 550
470 X=P : A$=C$ : GOSUB 610
480 RETURN
490 A$="-Y-=" : X=16 : Y=14-HH
500 GOSUB 610 : RETURN
510 A$="    " : X=16 : Y=14-H
520 GOSUB 610 : RETURN
REM 530-540: Choose new height for witch.
530 NH = INT(RND(0)*5+3)
540 RETURN
REM 550: Chooses an object.
550 R=INT(RND(0)*4)+1
REM 560: Random chance of object being Broomstick Snatcher.
560 W=0 : IF RND(0)>0.8 THEN W=1
570 C$=LEFT$(P$(R),1) : P=1
580 M=M+1
590 IF M=FM THEN C$="CAULDRON „ˆ"
600 RETURN

610 PRINT@Y*32+X,A$;: RETURN
620 CLS : PRINT : PRINT
630 PRINT "THE POTION MUST HAVE" : PRINT
640 FOR I=1 TO 4
650 N=INT(RND(0)*3)+2 : N(I)=N
660 PRINT : PRINT; N; " "; P$(I)
670 NEXT I : PRINT : PRINT
680 PRINT "PRESS enter TO PLAY";
690 INPUT X$ : CLS : RETURN
700 DIM P$(4) : DIM N(4) : DIM G(4)
710 DIM G$(5)
720 FOR I=1 TO 4 : READ P$(I) : NEXT I
730 DATA "TOES OF NEWTS", "EYES OF LIZARDS", "ROOTS OF IVY", "BRAINS OF BATS"
740 H$=">====>====>====>====>===="
750 FOR I=1 TO 5 : G$(6-I)=MID$(H$,I,I+20)
760 NEXT I : GD=1
770 RETURN
