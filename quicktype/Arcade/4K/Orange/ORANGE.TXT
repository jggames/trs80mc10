30 CLS:PRINT@11,"COLOR RUN"; 
40 PRINT@33,"USE THE ARROWS TO MOVE YOUR COLORED SQUARE. EACH TIME YOU MOVE OVER AN AREA ITS COLOR CHANGES. ";
41 PRINT"THE COLORS CHANGE IN THIS ORDER.";:FORA=143TO255STEP16:PRINTCHR$(128)+CHR$(A);:NEXTA:PRINTCHR$(128)
50 PRINT:PRINT" WHEN YOU CROSS OVER AN ORANGE  AREA THE GAME ENDS. HOW FAR CAN YOU TRAVEL BEFORE ";
51 PRINT"THE":PRINT@483,"PRESS ANY KEY TO CONTINUE";
60 PRINT@394,"ÿÿÿÿÿÿ ÿÿÿÿ";
70 FORT=1TO10:NEXTT
80 PRINT@394,"orange trap";
90 FORT=1TO10:NEXTT
100 IFINKEY$<>""THEN110
101 GOTO60
110 CLS:C$=CHR$(143) :D$=CHR$(128)
120 FORA=1TO28:A$=A$+C$:NEXTA
130 B$=C$+D$+D$:FORA=1TO9:E$=E$+B$:NEXTA:E$=E$+C$
140 CLS0:FORA=65TO385STEP64:PRINT@A,A$;:PRINT@A+32,E$;:NEXTA:PRINT@449,A$;
150 X=143:FORA=94TO318STEP32:PRINT@A,CHR$(X);:X=X+16:NEXTA
160 PRINT@4,"MOVES";
170 P=1089-1024+16384:C=159
180 IFC>255THEN270
181 POKEP,C
190 PRINT@10,N;
200 K=PEEK(2)ANDPEEK(17023):IFK=ASC("W")ANDPEEK(P-32)<>128THENP=P-32:GOTO260
210 IFK=ASC("Z")ANDPEEK(P+32)<>128THENP=P+32:GOTO260
220 IFK=ASC("A")ANDPEEK(P-1)<>128THENP=P-1:GOTO260
230 IFK=ASC("S")ANDPEEK(P+1)<>128THENP=P+1:GOTO260
240 POKEP,128:FORT=1TO15:NEXTT:POKEP,C
250 GOTO200
260 N=N+1:C=PEEK(P):C=C+16:GOTO180
270 PRINT@20,"TRY AGAIN?";
280 I$=INKEY$:IFI$=""THEN280
285 IFI$="Y"THENRUN
290 IFI$="N"THENEND
295 GOTO280
300 REM ORANGE TRAP
310 REM BY JAMES W. WOOD 1983
320 REM MC-10 EDITS JIM GERRIE
