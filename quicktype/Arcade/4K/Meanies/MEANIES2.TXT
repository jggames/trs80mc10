0 CLS:MC=16384
1 CLS3:FORA=0TO31:POKEMC+A,(AAND7)*16+143:POKEMC+A+480,(AAND7)*16+143:NEXT
4 PRINT@32*3+9," BLUE MEANIES ";
5 PRINT@32*4+9,"     FROM     ";:V=50
6 PRINT@32*5+9," OUTER SPACE  ";:D$=".":EN=1000
7 PRINT@32*9+9,"BY DUANE LATER";:SC=0
8 PRINT@32*10+9,"   (C) 1981   ";
9 PRINT@32*13+11,"HIT A KEY";
10 C$=INKEY$:IFC$=""THEN10
15 CLS6:M=MC+384+6:U=128
30 FORI=0TO12:PRINT"ßßß€€€€€€€€€€€€€€€€€€€€€€€€€€ßßß";:NEXT
65 FORI=4TO11STEP3:PRINT@I*32+3,"Ì";:PRINT@I*32+28,"Ì";:NEXT:POKEMC+480+15,42:POKEMC+480+16,42:POKEMC+480+17,42
70 POKEM,24:IFB=0THENB=1:A=MC+10+(RND(2)-1)*10
74 POKEA,128:A=A+INT(RND(0)*5)-2:IFRND(0)<.5THENA=A+32
75 IFRND(0)<.02ANDP=0THENP=1:Q=MC+4+RND(23):SOUND1,1
76 IFP=0THEN80
77 POKEQ,128:Q=Q+32:IFPEEK(Q)=223ORPEEK(Q)<96THENEN=EN+500:P=0:M$="REFUELEDß":GOTO1100
78 SOUND250,1:IFQ>MC+511THENP=0:SOUND100,1:GOTO80
79 POKEQ,15
80 IFPEEK(A)<>128THEN200
85 IFA>MC+511THENB=0:GOTO70
91 POKEA,175:IFG=0THENG=1:H=MC+10+(RND(2)-1)*10
92 POKEH,128:H=H+INT(RND(0)*3)-1:IFRND(0)<.5THENH=H+32
93 IFPEEK(H)<>128THEN210
94 IFH>MC+511THENB=0:GOTO70
95 POKEH,175:IFEN<0THENM$="NO ENERGY":GOTO1100
100 C$=INKEY$:IFC$=""THEN70
105 IFC$="Q"THENIFPEEK(MC+131)=204THENF=MC+132:GOTO150
110 IFC$="A"THENIFPEEK(MC+227)=204THENF=MC+228:GOTO150
115 IFC$="Z"THENIFPEEK(MC+323)=204THENF=MC+324:GOTO150
116 IFC$="R"THENIFPEEK(MC+156)=204THENF=MC+155:GOTO300
117 IFC$="D"THENIFPEEK(MC+252)=204THENF=MC+251:GOTO300
118 IFC$="X"THENIFPEEK(MC+348)=204THENF=MC+347:GOTO300
120 IFC$="I"THENIFU=223THENPOKEM,U:M=M-32:U=PEEK(M):POKEM,24:GOTO70
121 IFC$="L"THENIFPEEK(M+33)=223ORPEEK(M+33)<96THENPOKEM,U:M=M+1:U=PEEK(M):POKEM,24:GOTO70
122 IFC$="K"THENIFPEEK(M+32)=223ORPEEK(M+32)=128THENPOKEM,U:M=M+32:U=PEEK(M):POKEM,24:GOTO70
123 IFC$="J"THENIFPEEK(M+31)=223ORPEEK(M+31)<96THENPOKEM,U:M=M-1:U=PEEK(M):POKEM,24:GOTO70
124 IFC$=" "THENIFU=128THENPOKEM,223:U=223:EN=EN-100:M$="BUILDINGß":GOTO1100:GOTO70
140 GOTO70
150 EN=EN-50:GOSUB1200:FORE=FTOF+23:IFPEEK(E)<>128THEN600
160 POKEE,156:NEXT
165 R=E-1:FORE=FTOR:POKEE,128:NEXT:GOTO70
200 IFPEEK(A)=42THENPOKEA,175:GOTO1500
207 POKEA,128:B=0:GOTO216
210 IFPEEK(H)=42THENPOKEH,175:GOTO1500
215 POKEH,128:G=0
216 M$="SHIP HITß":EN=EN-V:GOTO1100
300 EN=EN-50:GOSUB1200:FORE=FTOF-23STEP-1:IFPEEK(E)<>128THEN710
320 POKEE,156:NEXT
325 R=E+1:FORE=FTORSTEP-1:POKEE,128:NEXT:GOTO70
500 PRINT@503,"GOOD HIT";:POKEMC+511,223:SC=SC+10
501 PRINT@13,SC;:GOSUB1200
505 POKEE,RND(10)+176:POKEE-32,RND(10)+176:POKEE-1,RND(10)+176:POKEE-33,RND(10)+176:SOUND1,1
510 POKEE,128:POKEE-32,128:POKEE-1,128:POKEE-33,128
512 T=T+1:IFT=20THENV=75:PRINT@8,"WE'RE GONNA BE";:PRINT@42,"MEANER NOW!";:FORI=1TO3000:NEXT:GOTO15
513 RETURN
600 IFPEEK(E)=15THENP=0:SC=SC-80:GOSUB501:GOTO165
700 IFE=HTHENG=0:GOSUB500:GOTO165
705 B=0:GOSUB500:GOTO165
710 IFPEEK(E)=15THENP=0:SC=SC-80:GOSUB501:GOTO325
720 IFE=HTHENG=0:GOSUB500:GOTO325
725 B=0:GOSUB500:GOTO325
1100 PRINT@503,LEFT$(M$,8);:POKEMC+511,ASC(RIGHT$(M$,1)):GOSUB1200:GOTO70
1200 PRINT@480,"ENERGY"EN"ßßß";:RETURN
1500 PRINT@480,"OVERRUN!ßßßßßß";:PRINT@0,;:FORI=1TO40:POKE49151,64:FORE=1TO25:NEXT:SOUNDI*5,1:NEXT:RUN
2000 REM qaz=LEFT
2010 REM rdx=RIGHT LASERS
2020 REM ijkl=MOVE REPAIR BOT
2030 REM SPACE=BUILD
2040 REM MC-10 EDITS
2050 REM JIM GERRIE 2018
®¬¬¬­€
ª© ¦¥
ª¡£¢¥
ª¨€¤¥
¬¬¬¬¬
