1 CLEAR200:MC=16384:GOSUB2000
10 REM TANDY-NEW YORK, NEW YORK 
20 DIM CR(4),LN(4),LI(4),CA(8),SP(8),V(8),S(8),LO(8),C$(8)
30 FOR C=1 TO 4
40 READ CR(C) 
50 NEXT
60 DATA 71,83,275,263
70 FOR L=0 TO 3 
80 READ LI(L)
90 NEXT
100 DATA 32,37,133,128
110 FOR C=1 TO 8
120 READ SP(C)
130 NEXT
140 DATA 8,20,502,490,160,352,158,350
150 FOR C1=1 TO 7 STEP 2
160 READ V,A1,A2,LO
170 C$=CHR$(A1)+CHR$(A2)
180 FOR C2=C1 TO C1+1
190 V(C2)=V
200 C$(C2)=C$
210 LO(C2)=LO+MC
220 NEXT C2,C1
230 DATA 32,144,159,-1,-32,159,144,2,2,156,156,33,-2,147,147,-32
240 D$=CHR$(128)+CHR$(128)
250 CLS:INPUT "LEVEL (1-3)";LE
260 LE=INT(LE)
270 IF LE<1 OR LE>3 THEN 250
280 LE=16-LE*4
290 GOTO 1000
300 REM**SCREEN**
310 CLS 3
320 S$=D$+D$
330 L$=S$+S$+S$+S$+S$+S$+S$+S$
340 FOR R=0 TO 15
350 PRINT@32*R+8,S$; 
360 PRINT@32*R+20,S$;
370 NEXT
380 FOR R=1 TO 2 
390 PRINT@32*(R+3),L$;
400 PRINT@32*(R+9),L$;
410 NEXT
420 PRINT@69,"q";:PRINT@90, "w"; 
430 PRINT@421,"a";:PRINT@442,"s";
440 FOR C=1 TO 4 
450 PRINT@CR(C)+LI(0),CHR$(191);
460 FOR L=1 TO 3 
470 PRINT@CR(C)+LI(L)," ";
480 NEXT L,C
490 PRINT@0,"POINTS:";:PRINT@32,"       ";
500 PRINT@473,"CLOCK:";
510 RETURN 
600 REM**CAR DRIVES**
610 IFS(C)=0THEN615
611 GOTO620
615 IF RND(LE)=1THENCA(C)=SP(C):GOTO620
616 RETURN
620 IF S(C)<>3 AND S(C)<>9 THEN 640
630 IF PEEK(CA(C)+LO(C))=191 THEN SOUND 150,1:PO=PO-10:RETURN
640 PRINT@CA(C),D$; 
650 CA(C)=CA(C)+V(C)
660 S(C)=S(C)+1
670 IF S(C)=16 THEN S(C)=0:SOUND 200,2:PO=PO+40:RETURN
680 IF PEEK(MC+CA(C))=128 THEN PRINT@CA(C),C$(C);:RETURN
690 PRINT@CA(C)-1,CHR$(241)CHR$(249)CHR$(249);:SOUND 1,10
700 PO=PO-200
710 S(C)=0
720 RETURN
800 REM**CHANGE LIGHTS** 
810 CR=-(IN$="Q")-2*(IN$="W")-3*(IN$="S")-4* (IN$="A")
820 IF CR=0 THEN RETURN
830 PRINT@CR(CR)+LI (LN(CR))," ";
840 LN(CR)=LN(CR)+1 AND 3
850 PRINT@CR(CR)+LI (LN(CR)),CHR$(191);
860 RETURN
1000 REM**MAIN PROGRAM**
1010 GOSUB 300:REM SCREEN
1020 PO=200:CL=200
1030 FOR C=1 TO 8
1040 GOSUB 600:REM CAR DRIVES
1050 IN$=INKEY$
1060 IF IN$>"" THEN GOSUB 800:REM CHANGE LIGHTS
1070 FORCC=1TOLE:NEXT:NEXT C
1080 CL=CL-1 
1090 IF PO<0 THEN PO=0
1100 PRINT@32,PO" ";
1110 PRINT@505,CL" ";
1120 IF PO>0 AND CL>0 THEN 1030
1130 REM
1140 PRINT@480,;
1150 INPUT"ONCE AGAIN (Y/N)";AN$
1160 IF LEFT$(AN$, 1)="Y" THEN RUN
1170 END
2000 CLS0:TT$="NEW YORK NEW YORK":TB$="HAL RENKO & SAM EDWARDS":TD$="TERRIFIC GAMES FOR"
2010 TV$="YOUR TANDY COLOR":TC$=CHR$(159):GOSUB 9010
2020 GOSUB9930
2500 RETURN

9010 TM=LEN(TT$):IF LEN(TB$)>TM THEN TM=LEN(TB$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32+((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32+((32-LEN(TB$))/2),TB$;
9060 ?@(BY+5)*32+((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32+((32-LEN(TV$))/2),TV$;
9100 RETURN

9410 BR$=""
9420 BR$="":FORCC=1TOBW:BR$=BR$+BC$:NEXT
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 CLS0:RETURN

9930 BC$="€":?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN
9940 REM DAFT SPANIEL 2023
