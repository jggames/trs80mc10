100 CLS:DIMX,Y,Q,R,A,B:M=16946:N=16948:K=16952:L=16947:G=127
110 CI$="CGKO"
120 CLS:PRINT@42,"air defense",,,,,:POKE16429,32
130 PRINT"SAVE YOUR CITY FROM FALLING","BOMBS. USE THE wasz AND space"
150 PRINT"TO INTERCEPT BOMBS BEFORE THEY","LAND. THE SOONER YOU HIT THE","BOMB, THE BETTER YOUR SCORE IS.":PRINT
160 PRINT"THE GAME CONTINUES UNTIL YOUR"
170 PRINT"CITY IS DESTROYED (20 HITS)."
180 PRINT@480,"      PRESS enter TO BEGIN    ";
190 IFINKEY$<>CHR$(13)THEN190
200 CLS0:SC=0:CS=20:PRINT@481,;
210 FORI=1TO30:PRINTCHR$(ASC(MID$(CI$,RND(4)))+64+8*RND(7));:NEXT:PRINT:POKE16863,128
240 X=31:Y=15:Q=X:R=Y:PRINT@500,CS;
250 A=RND(60)+1
260 FORB=1TO28STEP2:SET(A,B,RND(8)):FORC=1TO2:RESET(Q,R):RESET(Q-1,R+1):RESET(Q+1,R+1):RESET(Q,R+2)
270 IFPEEK(2)THENY=Y-(GAND4ANDNOTPEEK(K))/4+(GAND8ANDNOTPEEK(L))/8
280 IFPEEK(2)THENX=X-(GAND1ANDNOTPEEK(M))+(GAND4ANDNOTPEEK(N))/4:IF8ANDNOTPEEK(16952)THENX=X+16
300 IFX<1THENX=62
305 IFX>62THENX=1
310 IFY<1THENY=25
320 IFY>25THENY=1
325 SET(X,Y,5):SET(X-1,Y+1,5):SET(X+1,Y+1,5):SET(X,Y+2,5):Q=X:R=Y:IFX=ATHENIFY=BORY=B+1THENC=3:NEXTC:GOTO1500
330 NEXT:NEXT
340 CS=CS-1:PRINT@500,CS;:IFCS<1THEN1000
349 REM EXPLOSION
350 GOSUB2000:SOUND250,1:SOUND251,1:SOUND253,1:SOUND254,1
355 FORI=1TO5
360 SET(A-2,B-2,8):SET(A,B-2,2):SET(A+2,B-2,8):SET(A-1,B-1,2):SET(A+1,B-1,2):SET(A,B,4)
370 RESET(A-2,B-2):RESET(A,B-2):RESET(A+2,B-2):RESET(A-1,B-1):RESET(A+1,B-1):RESET(A,B)
380 NEXT:RESET(A,28):RESET(A,29):RESET(A-1,28):RESET(A-1,29):RESET(A+1,28):RESET(A+1,29)
390 GOTO250
1000 REM GAME OVER
1020 FORI=29TO0STEP-1
1030 FORJ=1TO5
1040 SET(RND(63),I,RND(8))
1050 NEXT:NEXT
1060 IFSC>HSTHENHS=SC
1070 PRINT@0,"SCORE"SC,"HIGH"HS:GOTO180
1500 GOSUB2000:SOUND240,5
1510 SC=SC+INT(100-B*100/31)
1520 PRINT@488,INT(SC);
1530 GOTO250
2000 FORI=BTO0STEP-2:RESET(A,I):NEXT:RETURN
3000 REM AIR DEFENSE
3010 REM T.L. WAHL
3020 REM COMPUTE! APRIL 1983
3030 REM MC-10 EDITS JIM GERRIE
