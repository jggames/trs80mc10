0 DIMK(255),P(100),X,Y,A,B,Q,R,T,C,P,V,N,Q(511):X=RND(-(PEEK(9)*256+PEEK(10))):GOTO5000
1 V=-1:RETURN
2 V=-32:RETURN
3 V=1:RETURN
4 V=32:RETURN
6 T=T+1:P(T)=P:POKEP,191:RETURN
7 FORC=1TOT:POKEP(C),223:NEXT:T=0:V=0:RETURN
8 V=0:RETURN
9 X=A:Y=B:A=0
11 IFPOINT(X,Y-1)THENQ=(RND(0)*.75+.25):A=1:GOTO13
12 IFPOINT(X,Y+1)THENQ=-(RND(0)*.75+.25):A=1
13 IFPOINT(X-1,Y)THENR=(RND(0)*.75+.25):GOTO22
14 IFPOINT(X+1,Y)THENR=-(RND(0)*.75+.25):GOTO22
16 IFATHEN22
17 Q=(RND(0)*.75+.25)*-SGN(Q):R=(RND(0)*.75+.25)*-SGN(R):GOTO22
20 FORA=1TO5000:A=X:B=Y:RESET(X,Y):X=X+R:Y=Y+Q:ONPOINT(X,Y)GOTO9,9,9,200,200,9,9,9:SET(X,Y,1)
22 A=X:B=Y:RESET(X,Y):X=X+R:Y=Y+Q:ONPOINT(X,Y)GOTO9,9,9,200,200,9,9,9:SET(X,Y,1)
23 ONK(ASC(INKEY$+"*"))GOSUB1,2,3,4,50:ONK(PEEK(P+V))GOSUB6,7,8,90:P=P+V:POKEP,207:NEXT
24 IFA=9000THEN205
30 IFSC>HSTHENHS=SC
31 PRINT@0,"SCORE"SC;
32 IFSC>ELTHENEL=EL+500:FORT=1TO8:PRINT@16,"lives+1 ";:SOUNDRND(50)+100,5:PRINT@16,"LIVES+1 ";:FORX=1TO99:NEXT:NEXT:LF=LF+1
33 PRINT@16,"LIVES";LF;
35 PRINT@480,"HIT c TO CONTINUE";
40 I$=INKEY$:IFI$<>"C"THEN40
41 GOTO2010
50 Q=0:N=0:X=INT(X/2):Y=INT(Y/2):PRINT@32*Y+X,CHR$(128);
51 Q=Q+1:Q(Q)=32*Y+X+16384:GOTO60
52 POKEQ(N),175:IFPEEK(Q(N)-1)=128THENQ=Q+1:Q(Q)=Q(N)-1:POKEQ(Q),239
53 IFPEEK(Q(N)+1)=128THENQ=Q+1:Q(Q)=Q(N)+1:POKEQ(Q),239
54 IFPEEK(Q(N)-32)=128THENQ=Q+1:Q(Q)=Q(N)-32:POKEQ(Q),239
55 IFPEEK(Q(N)+32)=128THENQ=Q+1:Q(Q)=Q(N)+32:POKEQ(Q),239
60 IFN=QTHENSC=SC+INT(75-(Q*100/419)+.5):A=5000:RETURN
61 N=N+1:B=PEEK(Q(N)):IFB=128ORB=239THEN52
62 GOTO60
90 T=T+1:P(T)=P:POKEP,191:P=P+V:POKEP,207:LF=LF-1:PRINT@16,"LIVES"LF;:SOUND1,3
105 IFLF=0THENT=0:V=0:PRINT@16,"ßßßßßßßßßßßßßßß";:A=8999:RETURN
110 P(0)=P:FORC=TTO0STEP-1:POKEP(C),128:NEXT:P=P(1):T=0:V=0:POKEP,223:PRINT@16,"ßßßßßßßßßßßßßßß";:RETURN
200 LF=LF-1:PRINT@16,"LIVES"LF;:SOUND1,3:IFLF=0THEN205
201 IFT=0THENP(1)=P:T=1
202 P(0)=P:FORC=TTO0STEP-1:POKEP(C),128:NEXT:P=P(1):T=0:V=0:X=A:Y=B:POKEP,223:PRINT@16,"ßßßßßßßßßßßßßßß";:GOTO22
205 POKEP,207:PRINT@0,"SCORE"SC;:PRINT@16,"HIGH"HS"ßß";:PRINT@480,"YOU'RE DEAD!  PLAY AGAIN (y/n)?";
210 I$=INKEY$:SET(X,Y,1):SET(X,Y,4):SET(X,Y,8):SET(X,Y,2):IFI$=""THEN210
220 IFI$="Y"THEN2005
230 IFI$="N"THENEND
240 GOTO210 
2000 K(65)=1:K(87)=2:K(83)=3:K(90)=4:K(32)=5:K(71)=1:K(89)=2:K(74)=3:K(72)=4
2001 K(128)=1:K(223)=2:K(191)=3:FORX=129TO143:K(X)=4:NEXT
2005 SC=0:LF=3:EL=500
2010 CLS0:FORX=16384TO16415:POKEX,223:NEXT:FORX=16447TO16895STEP32:POKEX,223:NEXT
2020 FORX=16894TO16864STEP-1:POKEX,223:NEXT:FORX=16832TO16416STEP-32:POKEX,223:NEXT
2030 X=10+RND(40):Y=10+RND(10):R=(RND(0)*.75+.25):Q=(RND(0)*.75+.25)
2040 ONRWND(2)GOTO2041,2042
2041 Q=(RND(0)*.75+.25):GOTO2050
2042 Q=-(RND(0)*.75+.25)
2050 ONRND(2)GOTO2051,2052
2051 R=(RND(0)*.75+.25):GOTO2060
2052 R=-(RND(0)*.75+.25)
2060 ONRND(4)GOTO2061,2062,2063,2064
2061 P=16417:GOTO2090
2062 P=16446:GOTO2090
2063 P=16862:GOTO2090
2064 P=16833
2090 POKEP,207:V=0:T=0:P(1)=P:GOTO20
5000 CLS:PRINTTAB(14)"QIX":PRINT
5010 PRINTTAB(9)"BY JIM GERRIE":PRINT
5030 PRINT"  awsz  = MOVE       YOU   = "CHR$(207)
5040 PRINT"  space = END ROUND  LIVES = 3":PRINT
5050 PRINT"  TRY TO CONFINE THE GREEN DOT"
5060 PRINT"  AS MUCH AS POSSIBLE BEFORE"
5070 PRINT"  ENDING A ROUND. DON'T LET IT"
5080 PRINT"  HIT YOU OR THE RED TRAIL YOU"
5090 PRINT"  LEAVE. HIT COMPLETED WALLS"
5095 PRINT"  TO CONVERT YOUR TRAIL TO"
5096 PRINT"  WALL. 500 PTS = BONUS LIFE":PRINT:PRINTTAB(6)"HIT ANY KEY TO BEGIN";
5097 FORX=16384TO16415:POKEX,PEEK(X)-64:NEXT:FORX=16447TO16895STEP32:POKEX,PEEK(X)-64:NEXT
5098 FORX=16894TO16864STEP-1:POKEX,PEEK(X)-64:NEXT:FORX=16832TO16416STEP-32:POKEX,PEEK(X)-64:NEXT
6010 IFINKEY$=""THEN6010
6020 GOTO2000
