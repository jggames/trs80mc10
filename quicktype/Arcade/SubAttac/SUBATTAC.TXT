1 CLS:PRINT@128,"INSTRUCTIONS "CHR$(91)"Y/N"CHR$(93)"?"
2 I$=INKEY$:Z=RND(0):IFI$=""THEN2
3 IFI$="Y"THENGOSUB850
5 CLS0:Z=128
10 DATA 3,4,3,5,11,6,11,1,3,0,3,9,-1,17,0,18,28,19,22,-1,1,9,1,5,-1
15 DATA 113,116,-1,4,6,15,14,-60,9,-48,10,15,12,8,1,-1,29,28,31,22,31,26,31,29
20 DATA 28,20,-1,13,4,-48,9,-44,9,8,12,-1,125,118,127,117,126,124,-1,35,0,35,0
25 DATA 43,0,35,0,34,0,-1
27 FOR B=1 TO 9
30 READ C
35 IF C=-1 THEN B$(B)=CHR$(Z)+B$(B):NEXT B:GOTO 50
40 READ D:B$(B)=B$(B)+CHR$(Z+C):T$(B)=T$(B)+CHR$(Z+D):GOTO 30
50 B$(9)=B$(9)+CHR$(Z):T$(9)=T$(9)+CHR$(Z)
60 CLS 0:FORB=0 TO 190:PRINT@B,CHR$(175);:NEXT B
62 FOR B=1 TO 7:T$=T$+"^":IF B=3 THEN PRINT@94,T$;
64 IF B=5 THEN PRINT@125,T$;:PRINT@187,T$;
66 NEXT B:PRINT@156,T$;:H=490:PRINT@H,B$(9);
80 PRINT@237,"PRESS";:PRINT@269,"A KEY";
90 IF INKEY$="" THEN 90
100 PRINT@237,T$(9);:PRINT@269,T$(9);
110 R=RND(4)*32+191:B=RND(4):Y=R
120 X=X+1:PRINT@Y,B$(B);:PRINT@Y+32,B$(B+4);
125 POKE16948,255:POKE16946,255:I$=INKEY$
130 IFI$="A"ANDH>481THENH=H-1
140 IFI$="S"ANDH<504THENH=H+1
150 PRINT@H,B$(9);:IFI$=" " AND L=0 THEN GOSUB700
170 IF L=1 THEN GOSUB710
180 Y=Y+1:IF X=550 THEN 250
200 IF PEEK(R+16448)<>128 THEN POKE R+16448,128:PRINT@Y,T$(9);:PRINT@Y+32,T$(9);:GOTO 110
210 GOTO 120
250 IF S>HI THEN HI=S
260 PRINT@224,"HIGH SCORE...";HI;
270 PRINT@256,"YOUR SCORE...";S;
280 PRINT@320,"DO YOU WISH TO PLAY AGAIN (Y/N)?";:A$=INKEY$:IFA$=""THEN280
290 IF A$="Y" THEN S=0:X=0:L=0:T$="":GOTO60
300 IF A$="N" THEN END
310 GOTO280
700 F=H+16387:L=1
710 F=F-32:P=PEEK(F):POKE F,30:IF F<16606 THEN L=0:GOTO 750
740 IFP<>128THENPRINT@Y+1,T$(B);:PRINT@Y+33,T$(B+4);:L=0:S=S+B*20:SOUND9,1:SOUND120,2:POKE R+16448,128
750 POKE F,Z:RETURN
850 PRINT@32,"YOUR MISSION: TO DESTROY AS"
851 PRINTTAB(14)"MANY PIRATE SHIPS":PRINTTAB(14)"AS POSSIBLE IN":PRINTTAB(14)"ONE MINUTE."
860 PRINT@192,"THERE ARE FOUR TYPES OF SHIPS","WORTH 20,40,60 AND 80 POINTS."
870 PRINT@288,"USE THE "CHR$(91)"A,S"CHR$(93)" ARROW KEYS TO","GUIDE YOUR SUBMARINE, AND THE"
871 PRINT"SPACE BAR TO FIRE TORPEDOES."
880 PRINT@448,"PRESS A KEY TO CONTINUE..."
890 IFINKEY$=""THEN880
900 RETURN
960 REM SUB ATTACK
970 REM STUART HALL 1986
980 REM MC-10 EDITS JIM GERRIE
