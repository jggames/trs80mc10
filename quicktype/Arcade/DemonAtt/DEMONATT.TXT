5 CLS:HS=0
10 CLEAR2000:DIME$(2)
15 RL=RND(5):RL=RL*32+160+15:EP=RL:EU=1:PP=460
16 SC=0:LI=3:SL=0:PF=0:MI=-1:BS=128
20 IFGO=1THENGO=0:RETURN
25 P$=CHR$(135+16)+CHR$(140+16)+CHR$(139+16)
26 FORX=1TO32:B$=B$+CHR$(128):NEXTX
27 FORX=1TO32:M$=M$+CHR$(143+80):NEXTX
28 FORX=1TO30:M1$=M1$+CHR$(143+80):NEXTX
29 FORX=1TO28:M2$=M2$+CHR$(143+80):NEXTX
30 E$(1)=CHR$(128)+CHR$(134+64)+CHR$(140+32)+CHR$(137+64)+CHR$(128)
31 E$(2)=CHR$(128)+CHR$(137+64)+CHR$(131+32)+CHR$(134+64)+CHR$(128)
35 GOSUB900
36 FORX=1TO2:FORY=220TO250STEP10:SOUNDY,2:NEXTY,X
40 CLS0
42 PRINT@0,M$;:PRINT@32,M$;:PRINT@65,M1$;:PRINT@98,M2$;
44 FORX=1TO15:RESET(RND(62),RND(8)):NEXTX
45 REM PLAY !!
46 PRINT@489,"demon€attack";
47 PRINT@481,SC;:PRINT@PP,P$;
48 PRINT@503,LI;"lives";
49 IFLI=0THEN800
50 EM=RND(6)
51 IFEM=1THENED=1
52 IFEM=3THENED=2
53 IFEM=6THENED=3
60 IFED=1THENEP=EP-1
70 IFED=2THENEP=EP
80 IFED=3THENEP=EP+1
85 IFEP<RL-16THENEP=EP+1:ED=3
86 IFEP>RL+12THENEP=EP-1:ED=1
89 IFEU=1THENPRINT@EP,E$(1);
90 IFEU=2THENPRINT@EP,E$(2);
95 FI=RND(10):IFFI=5THENGOSUB400
100 PM=PEEK(17023) AND PEEK(2)
110 IFPM=ASC("1")THENGOSUB200
120 IFPM=ASC("-")THENGOSUB300
121 IFPM=ASC(" ")ANDPF=0THENSL=PP-31:PS=1:PF=1
125 IFMI=SLTHEN500
130 IFPS=1THENPRINT@SL,CHR$(128);
140 IFPS=1THENSL=SL-32
150 IFPS=1ANDSL<128THENPS=0:PF=0:SL=0
160 IFPS=1THENPRINT@SL,CHR$(140+48);
161 IFSL=EP+1THEN700
162 IFSL=EP+2THEN700
163 IFSL=EP+3THEN700
195 IFFI=5THENRETURN
198 IFEU=1THENEU=2:GOTO50
199 IFEU=2THENEU=1:GOTO50
200 PRINT@448,B$;
210 PP=PP-2
220 IFPP<448THENPP=448
230 PRINT@PP,P$;
240 RETURN
300 PRINT@448,B$;
310 PP=PP+2
320 IFPP>477THENPP=477
330 PRINT@PP,P$;
340 RETURN
400 MI=EP+34
406 SOUND240,1
410 PRINT@MI,CHR$(131+32);
420 GOSUB100
430 PRINT@MI,CHR$(128);
435 IFMI=SLTHEN500
440 MI=MI+32
450 IFMI>480THENFI=0:MI=-1:RETURN
460 PRINT@MI,CHR$(131+32);
466 IFMI=PPTHEN600
467 IFMI=PP+1THEN600
468 IFMI=PP+2THEN600
470 GOTO420
500 SOUND210,1
505 PRINT@MI,CHR$(128);
510 PRINT@SL,CHR$(128);
520 PS=0:PF=0:MI=-1:SL=0:FI=0
530 GOTO45
600 :PRINT@448,B$;:X=PP-1:Z=PP+2
601 IFX<448THENX=448
602 IFZ>478THENZ=478
605 PRINT@X,CHR$(134+16)+CHR$(128);
606 PRINT@X+2,CHR$(140+16);
607 PRINT@Z,CHR$(128)+CHR$(137+16);
610 LI=LI-1
620 PS=0:PF=0:MI=-1:SL=0:FI=0
622 SOUND30,5
625 PRINT@X+2,CHR$(131+16);
626 PP=460
630 FORX=1TO500:NEXTX
640 FORX=1TO11:PRINT@BS,B$;:BS=BS+32:NEXTX
650 BS=128
660 GOTO45
700 FORX=240TO210STEP-10:SOUNDX,1:NEXT
710 SC=SC+50
720 RL=RND(5):RL=RL*32+160+15
725 EP=RL
730 PS=0:PF=0:MI=-1:SL=0:FI=0
740 FORX=1TO11:PRINT@BS,B$;:BS=BS+32:NEXTX
750 BS=128
760 GOTO45
800 FORX=1TO2:FORY=250TO230STEP-10:SOUNDY,2:NEXTY,X
810 PRINT@200,"game€over";
820 PRINT@264,"PRESS C TO CONTINUE";
830 PRINT@328,"PRESS Q TO QUIT";
840 IFSC>HSTHENHS=SC
845 GO=1:GOSUB15
850 PM=PEEK(17023) AND PEEK(2)
860 IFPM=ASC("C")THENCLS:GOTO35
870 IFPM=ASC("Q")THENCLS:END
880 GOTO850
900 CLS0
905 FORX=0TO31
910 PRINT@132,E$(1);
920 PRINT@200,"demon€attack";
930 PRINT@271,"MC-10";
940 PRINT@360,"PRESS 1 FOR LEFT";
950 PRINT@392,"PRESS - FOR RIGHT";
960 PRINT@424,"PRESS SPACE TO FIRE";
965 PRINT@488,"HIGH SCORE=";HS;
966 PRINT@456,"press€s€to€play";
970 PRINT@132,E$(2);
980 PM=PEEK(17023) AND PEEK(2)
985 IFPM=ASC("S")THENRETURN
990 SET(0,X,RND(8)):SET(63,X,RND(8))
994 FORY=1TO50:NEXTY
995 NEXTX
999 GOTO905
9999 CHAZBEENHAD@HOTMAIL.COM
