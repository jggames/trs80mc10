0 CLS:DIMN(21),T,C1,G,H,C,P,W,U,Q,Y,OO,NN:Q=32:POKE16925,0:POKE16926,1
1 T=1:FORC1=1TO63STEP3:N(T)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):T=T+1
2 NEXT:GOTO20
4 FORT=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,T,1))-64),ASC(MID$(M$,T+1,1))-48:NEXT:RETURN
11 FORC1=1TO250:NEXT:RETURN
20 REM 
30 REM
40 REM BLASTAR BY E.R.MUSK
50 REM
60 SH=5
70 PRINT@Q*6,;:GOSUB1250
80 REM BLASTAR TUNE
90 M$="C2E2C2E2C2D2B2A2B2A2D2A2C2E2C2E2B2D2A2C2D2B2D2C2E2G2G2":GOSUB4
100 M$="J2L2J2L2J2K2I2H2I2H2K2H2J2L2J2L2I2K2H2J2K2I2K2J2L2N2N2":GOSUB4
110 M$="C2E2C2E2C2D2B2A2B2A2D2A2C2E2C2E2B2D2A2C2D2B2D2C2E2G2G2":GOSUB4
120 M$="J2L2J2L2J2K2I2H2I2H2K2H2J2L2J2L2I2K2H2J2K2I2K2J2L2N2N2":GOSUB4
130 CLS
140 PRINT"  BY E.R.MUSK"
150 FORT=1TO2500:NEXTT
160 CLS
170 PRINT@Q*7+14,"BLASTAR":PRINT:PRINT"    DO YOU NEED INSTRUCTIONS?"
180 PRINTTAB(14)"(Y/N)"
190 M$=INKEY$
200 IFM$=""THEN190
210 SOUND1,1
220 IFM$="N"THEN280
230 IFM$<>"Y"THEN190
240 REM INSTRUCTIONS
250 CLS:PRINT:GOSUB1250
260 PRINT:PRINT"        USE w,a,s AND d":PRINT"        FOR CONTROL AND"
261 PRINT"           space KEY":PRINT"           TO SHOOT":PRINT
265 PRINT"mission:DESTROY ALIEN FREIGHTER":PRINT"CARRYING DEADLY HYDROGEN BOMBS"
266 PRINT"AND STATUS BEAM MACHINES"
270 IFINKEY$=""THEN270
280 CLS0
560 REM MAIN PROGRAM
570 PRINT@0,;
580 PRINT"SCORE";SC,"SHIPS";SH
590 P=16:C=15
600 H=0:G=1
610 PRINT@Q*G+H,"×Û";
620 IFPEEK(2)AND4ANDNOTPEEK(16948)THENPRINT@Q*C+P,"€€";:C=C+1:IFC>15THENC=15
630 IFPEEK(2)AND4ANDNOTPEEK(16952)THENPRINT@Q*C+P,"€€";:C=C-1:IFC<2THENC=2
640 IFPEEK(2)AND1ANDNOTPEEK(16949)THENPRINT@Q*C+P,"€€";:P=P+1:IFP>29THENP=29
670 IFPEEK(2)AND1ANDNOTPEEK(16946)THENPRINT@Q*C+P,"€€";:P=P-1:IFP<1THENP=1
680 IFPEEK(2)AND8ANDNOTPEEK(16952)THENF=-1
740 PRINT@Q*C+P,"Û×";:IFP=HTHENIFC=GORC=G-1ORC=G+1THEN1180
790 IFY=1THENRETURN
810 IFH=PTHEN1050
820 PRINT@Q*G+H,"€€";:H=H+1:IFH>30THENH=.
835 PRINT@Q*G+H,"×Û";:IFF=-1THEN870
850 GOTO620
860 REM SHOOT ROCKETS
870 U=P:W=C-1:F=.:M$="D1F1":GOSUB4
890 PRINT@Q*G+H,"€€";:H=H+1:IFH>30THENH=.
910 PRINT@Q*G+H,"×Û";:PRINT@Q*W+U,"€€";:W=W-1:PRINT@Q*W+U,"øô";:IFH=PTHEN1050
960 Y=1:GOSUB620:Y=.:PRINT@Q*W+U,"€€";
980 IFW=GAND(U=HORU=H+1ORU=H-1)THEN1030
990 IFF=-1THEN870
1000 IFW=1THENW=.:GOTO620
1010 GOTO890
1020 REM HIT
1030 SC=SC+80:FORA=1TO20:PRINT@Q*G+H,CHR$(127+RND(128))CHR$(127+RND(128));:PRINT@Q*G+H,"€€";:NEXTA
1035 PRINT@0,"SCORE";SC,"SHIPS";SH:W=0:H=0:G=2+RND(10):GOTO620
1040 REM ALIEN STATUS BEAM/ROCKET BOMB
1050 PRINT@Q*G+H,"×Û";:OO=H:NN=G:Y=1:GOSUB620:Y=.:IFH<>PTHENIFWTHEN960
1070 IFH<>PTHEN620
1080 NN=NN+1
1090 Y=1:PI=P:GOSUB620:Y=.:PRINT@Q*C+P,"€€";:P=PI:PRINT@Q*C+P,"Û×";
1100 PRINT@.,"          STATUS BEAM"
1110 PRINT@Q*NN+OO,"åê";:M$="G1":GOSUB4
1120 IFNN=CTHENGOSUB1180
1140 IFNN>14THEN1185
1170 GOTO1080
1180 SH=SH-1:W=.:H=.:G=2+RND(10)
1181 FORA=1TO20:PRINT@Q*C+P,CHR$(127+RND(128))CHR$(127+RND(128));:PRINT@Q*C+P,"€€";:NEXTA
1185 CLS0:PRINT@0,"SCORE";SC,"SHIPS";SH:IFSH<0THEN1200
1190 FORT=1TO500:NEXTT:GOTO620
1200 CLS:PRINTTAB(13);"BLASTAR":PRINTTAB(8)"FLEET DESTROYED":PRINTTAB(2)"WOULD YOU LIKE ANOTHER GAME?"
1210 M$=INKEY$:IFM$=""THEN1210
1220 IFM$="Y"THENRUN
1230 IFM$="N"THENEND
1240 GOTO1210
1250 PRINT"     ‚ƒ…Ž†‰ƒƒ‡Ž†ƒ"
1252 PRINT"     ŠŒ‡………†Œ………„Œ‡"
1253 PRINT"     Š……ƒ………ƒ……†"
1254 PRINT"     ƒƒƒƒ‡‡‡‹ƒ‡‡‡‡‡":RETURN
1255 REM BLASTAR BY 12-YEAR-OLD
1260 REM ELON MUSK
1270 REM MC-10 EDITS JIM GERRIE
1280 REM Û× PLAYER
1290 REM øô SHOT
1300 REM ×Û FREIGHTER
1310 REM åê BEAM
