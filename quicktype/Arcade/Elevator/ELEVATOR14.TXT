0 CLS0:GOTO1000
1 RETURN
2 ON-(D<>2)GOTO5:ONK(PEEK(P-3+M))GOTO30,50,90,82:P=P-3:GOTO30
3 ON-(D>.)GOTO5:ONK(PEEK(P+3+M))GOTO30,50,93,82:P=P+3:GOTO30
4 D=-(D=1)*3-(D=.)-(D=2)*3-(D=3):GOTO30
5 D=-(D=.)*2-(D=1)*.-(D=2)*.-(D=3)*2:GOTO30
6 SET(I,13,4):I=I+J*2:J=.:D=-(D<2)-(D>1)*3:GOTO48
7 SET(X,13,4):X=T:GOSUB44:RETURN
8 I=I+J*2:J=.:RETURN
9 T=B(F+1):V=A(F+1):O=C(F+1):?@.,V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T)Z$;
10 T=B(F):V=A(F):O=C(F):?V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T)Z$;
11 T=B(F-1):V=A(F-1):O=C(F-1):?V$F$(T)W$B$(V)G$(T)B$(O)X$C$(V)H$(T)C$(O)X$D$(V)I$(T)D$(O)Y$J$(T);:RETURN

20 FORN=.TO5STEP.:GOSUB9:PRINT@P,M$(D);:PRINT@P+32,N$(D);:PRINT@P+64,O$(D);:ONK(PEEK(2)ANDPEEK(R))GOTO2,3,70,4,87
30 IFGTHEN?@G,M$(H);:?@G+32,N$(H);:?@G+64,O$(H);:IFJ=.THENIFRND(Q)=1THENJ=SGN(P-G):H=5+J:J=J*2:I=L+J*2:GOTO40
31 W=W+1:IFW=3THENW=.:T=2-RND(3):IFABS(E+T-F)<2THENIFE+T>1ANDE+T<31THENE=E+T:B(E+1)=1:B(E)=2:B(E-1)=.
34 IFJTHENFORI=ITOI+J*2STEPJ:ONPOINT(I,13)GOSUB1,6,1,1,8:RESET(I,13):NEXT
36 NEXT:GOTO200

40 IFH=5THENONRND(3)GOSUB42,43,44
41 NEXT:GOTO200
42 H=4:D=1:GOSUB47:GOTO48
43 H=6:D=1:GOSUB47:GOTO48
44 SOUND1,1:H=H+1:Z=Z+1:?@G,M$(H);:?@G+32,N$(H);:?@G+64,O$(H);:IFZ=3THENG=.:SC=SC+10:GOSUB120:SOUND38,1
45 RETURN
47 PRINT@P,M$(D);:PRINT@P+32,N$(D);:PRINT@P+64,O$(D);:RETURN
48 N=N+1:POKEM+511,117-N:SOUND1,4:RETURN

50 P=207:J=.:W=.
51 GOSUB9:PRINT@P,M$(D);:PRINT@P+32,N$(D);:PRINT@P+64,O$(D);:ONK(PEEK(2)ANDPEEK(R))GOTO64,65,60,55:GOTO51

55 IFF=2ANDU-S=.THEN250
56 IFF>2THENF=F-1:E=E-1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F;:SOUND200,1
57 GOSUB99:GOTO51
60 IFF<29THENF=F+1:E=E+1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F;:SOUND200,1
61 GOSUB99:GOTO51
64 P=P-4:D=2:NEXT:GOTO200
65 P=P+4:D=.:NEXT:GOTO200

66 IFK(PEEK(J(O+V)-32+M))<>1THENP=J(O+V)-32:GOSUB75:GOSUB47:P=P+32:IFK(PEEK(J(O+V+V)+M))<>1THENP=J(O+V+V):GOSUB75:GOSUB47
67 GOSUB75:IFGTHEN?@G,M$(H);:?@G+32,N$(H);:?@G+64,O$(H);
68 T=J(O+V):ONK(PEEK(P+M+1))GOTO30,50,91,85:GOTO30

70 IFD=1THEND=.:GOSUB47:GOTO30
71 IFD=3THEND=2:GOSUB47:GOTO30
72 IFD=.THENV=1:GOSUB78:GOTO66
73 V=-1:GOSUB78:GOTO66
75 ?@160,V$F$(B(F))W$B$(A(F))G$(B(F))B$(C(F))X$C$(A(F))H$(B(F))C$(C(F))X$D$(A(F))I$(B(F))D$(C(F))Y$J$(B(F))Z$;:RETURN
78 FORT=1TO7:IFP=J(T)THENO=T
79 NEXT:RETURN

82 A=P:B=D:D=.:FORP=207TO335STEP64:GOSUB9:GOSUB47:SOUNDP/2,1:D=D+1:NEXT
83 IFE=F-1THENGOSUB9:D=.:P=P-32:GOSUB47:F=E:PRINT@493,"FLOOR"F;:GOSUB99:GOTO50
84 GOSUB9:GOSUB47:SOUNDP/2,1:D=D+1:GOSUB48:P=A:D=B:GOTO30
85 P=T:GOTO82

86 X=T:RETURN

87 IFGTHENPRINT@G,M$(H);:PRINT@G+32,N$(H);:PRINT@G+64,O$(H);
88 Y=(D=2)-(D=.):X=((P/32)-INT(P/32))*64+2+Y*3:IFY=.THEN30
89 T=-(Y=-1)+63*-(Y=1):FORX=XTOTSTEPY*2:ONPOINT(X,13)GOSUB1,1,1,1,86,1,1,7:RESET(X,13):NEXT:GOTO30

90 P=P-3
91 IFP>207THENC(F)=C(F)+2:GOSUB75:C(F)=1:GOTO97
92 A(F)=A(F)+2:GOSUB75:A(F)=1:GOTO97

93 P=P+3
94 IFP>207THENC(F)=C(F)+2:GOSUB75:C(F)=1:GOTO97
96 A(F)=A(F)+2:GOSUB75:A(F)=1:GOTO97

97 S=S+1:SC=SC+1:GOSUB120:SOUND100,1:SOUND200,1:IFG=.THENIFRND(2)=1THENGOSUB99
98 GOTO30

99 G=.:IFRND(2)=1THENRETURN
100 IFRND(2)=1THENT=RND(2):G=211+T*3:I=40+T*6:L=I:H=6:GOTO110
101 T=RND(2):G=194+T*3:I=6+T*6:L=I:H=4:GOTO110
110 J=.:Z=.:IFPEEK(G+M+1)=159THEN100
111 RETURN

120 PRINT@480,"SCORE"SC;:IFSC>NLTHENNL=NL+50:N=N+(N>0):POKEM+511,53-N
121 RETURN

200 PRINT@480,"SCORE"SC"           ";:PRINT@494,"PLAY AGAIN (Y/N)?";:POKEM+511,96
210 M$=INKEY$:IFM$=""THEN210
220 IFM$="Y"THENGOSUB2000:GOTO1070
230 IFM$="N"THENEND
240 GOTO210

250 P=367:D=.:E=1:B(E+1)=1:B(E)=2:B(E-1)=.:PRINT@493,"FLOOR"F+1;:GOSUB9:GOSUB47
280 PRINT@480,"MISSION COMPLETE. 100 PT BONUS.";:POKEM+511,96:SC=SC+100:NL=SC+50
281 FORT=1TO25:SOUND100,1:SOUND200,1:NEXT
282 GOSUB2000:FORT=1TO1500:NEXT:GOSUB1080:NEXT:GOTO200

1000 DIMA(31):GOSUB3000:DIMB(31),C(31),K(255),T,E,F,D,I,J,X,Y,X,Y,P,H,M,W,M,N,L,R,G
1001 M=RND(-(PEEK(9)*256+PEEK(10))):M=16384:R=17023
1002 V$="ÄÄÄÄœﬂﬂﬂﬂﬂﬂﬂﬂﬂ":W$="ﬂﬂﬂﬂﬂﬂﬂﬂﬂœÄÄÄÄÄÄÄœ":X$="œÄÄÄÄÄÄÄœ":Y$="œÄÄÄÄÄÄÄœœœœœœœœœœ":Z$="œœœœœœœœœœÄÄÄ"
1003 DIMB$(7),C$(7),D$(7),F$(2),G$(2),H$(2),I$(2),J$(2),M$(7),N$(7),O$(7),V,Q,U,O,Z,S,J(8),A,B,SC,HS,NL
1011 B$(1)="ﬂØØØﬂØØØﬂ":B$(2)="ﬂøøøﬂØØØﬂ":B$(3)="ﬂØØØﬂøøøﬂ":B$(4)="ﬂ–––ﬂØØØﬂ":B$(5)="ﬂØØØﬂ–––ﬂ"
1012 C$(1)="ﬂÆØØﬂÆØØﬂ":C$(2)="ﬂæøøﬂÆØØﬂ":C$(3)="ﬂÆØØﬂæøøﬂ":C$(4)="ﬂ–––ﬂÆØØﬂ":C$(5)="ﬂÆØØﬂ–––ﬂ"
1013 D$(1)="ﬂØØØﬂØØØﬂ":D$(2)="ﬂøøøﬂØØØﬂ":D$(3)="ﬂØØØﬂøøøﬂ":D$(4)="ﬂ–––ﬂØØØﬂ":D$(5)="ﬂØØØﬂ–––ﬂ"
1014 B$(6)="ﬂ–––roofﬂ":B$(7)="ﬂ–––exitﬂ"
1015 C$(6)="ﬂ––œﬂﬂﬂﬂﬂ":C$(7)="ﬂ–––ﬂﬂﬂﬂﬂ"
1016 D$(6)="ﬂ–œœﬂﬂﬂﬂﬂ":D$(7)="ﬂ–––ﬂﬂﬂﬂﬂ"

1020 F$(0)="–––––":F$(1)="–Â–Í–":F$(2)="ÔﬂﬂﬂÔ"
1021 G$(0)="–––––":G$(1)="–Â–Í–":G$(2)="ÔﬂÔﬂÔ"
1022 H$(0)="–––––":H$(1)="–Â–Í–":H$(2)="ÔﬂﬂﬂÔ"
1023 I$(0)="–––––":I$(1)="–Â–Í–":I$(2)="ÔﬂﬂﬂÔ"
1024 J$(0)="–––––":J$(1)="–Â–Í–":J$(2)="ÏÏÏÏÏ"

1030 M$(0)="ﬂüﬂ"
1031 N$(0)="ﬂ‘—":N$(1)="ﬂﬂﬂ"
1032 O$(0)="ﬁ÷ﬂ":O$(1)="ÿ“ù"

1033 M$(2)="ﬂüﬂ"
1034 N$(2)="“ÿﬂ":N$(3)="ﬂﬂﬂ"
1035 O$(2)="ﬂŸ›":O$(3)="û—‘"

1040 M$(4)="ﬂˇﬂ"
1041 N$(4)="“ÿﬂ":N$(5)="ﬂﬂﬂ"
1042 O$(4)="ﬂŸ›":O$(5)="ÿ“˝"

1043 M$(6)="ﬂˇﬂ"
1044 N$(6)="ﬂ‘—":N$(7)="ﬂﬂﬂ"
1045 O$(6)="ﬁ÷ﬂ":O$(7)="˛—‘"

1050 J(0)=194:J(1)=197:J(2)=200:J(3)=203:J(4)=207:J(5)=211:J(6)=214:J(7)=217:J(8)=220
1060 K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(32)=5:K(74)=1:K(76)=2:K(73)=3:K(75)=4
1061 K(128)=1:K(207)=1:K(239)=2:K(191)=3:K(208)=4:K(229)=4:K(234)=4

1065 GOSUB3150:Q=18:GOSUB2000

1070 IFSC>HSTHENHS=SC
1075 SC=0:Q=18-A(.):Z=0:N=0:NL=50:GOSUB1080:GOSUB9:PRINTZ$;:GOTO20

1080 GOSUB2100:U=.:FORT=2TO29:E=RND(3):A(T)=E:IFE>1THENU=U+1
1081 E=RND(3):C(T)=E:IFE>1THENU=U+1
1082 B(T)=.:NEXT
1083 A(30)=6:C(30)=6:A(1)=7:C(1)=7
1084 F=29:E=29:B(E+1)=1:B(E)=2:B(E-1)=.:P=211:D=.:S=.:Z=.:Q=Q-1:IFQ<6THENQ=6
1087 GOSUB99:POKEM+511,117-N
1090 RETURN

2000 CLS0:PRINT@32*2+12,"elevator";:PRINT@32*4+13,"action";:PRINT@256,;
2010 PRINT"–––––––––––––œœœœœœœ––––––––––––";
2020 PRINT"–––––––––––––œ–––––œ––––––––––––";
2030 PRINT"–––––––––––––œ–––––Ø––––––––––––";
2040 PRINT"–––––––––––––œ–––––Ø––––––––––––";
2050 PRINT"–––––––––––––œ–––––Ø––––––––––––";:IFQ<12THENGOSUB4000
2060 PRINT"––––œœœœœœœœœœ–––––œœœœœœœœœœ–––";
2070 PRINT"––––œœœœœœœœœœ––––œœœœœœœœœœœ–––";
2080 PRINT"––––œœœœœœœœœœ–––œœœœœœœœœœœœ–––";:RETURN
2100 Y=0:FORX=55TO40STEP-1:SET(X,Y,8):Y=Y+1:NEXT:PRINT@490,"BY JIM GERRIE";:FORT=1TO1500:NEXT
2110 IFSC>HSTHENHS=SC
2120 PRINT@490,"HIGH SCORE:"HS"  ";
2200 D=2:FORP=59TO276STEP31:GOSUB47:SOUNDP/2,2:PRINT@P,"–––";:PRINT@P+32,"–––";:PRINT@P+64,"–––";:NEXT
2210 GOSUB47:SOUNDP/2,2:SOUND200,2:FORT=1TO2500:NEXT:PRINT@480,"SCORE        FLOOR 29      MEN";:?@493,"FLOOR 29";:RETURN
3000 CLS6:PRINT@8,"elevatorﬂaction";:PRINT@45,"⁄";:PRINT@51,"’";:PRINT@64,;
3005 PRINT"ﬂﬂﬂﬂﬂﬂUSE: awsz OR jikl TOﬂﬂﬂﬂﬂﬂ";
3010 PRINT"ﬂﬂﬂﬂﬂﬂMOVE & space TO FIREﬂﬂﬂﬂﬂﬂ";
3020 PRINT"ﬂﬂﬂﬂﬂﬂDOWN= CROUCH-- HIT  ﬂﬂﬂﬂﬂﬂ";
3030 PRINT"ﬂﬂﬂﬂﬂﬂAGAIN TO CHANGE DIR.ﬂﬂﬂﬂﬂﬂ";
3040 PRINT"ﬂﬂﬂﬂﬂﬂUP=STAND UP OR JUMP.ﬂﬂﬂﬂﬂﬂ";
3060 PRINT"ﬂﬂﬂﬂﬂﬂNEW LIFE EVERY 50   ﬂﬂﬂﬂﬂﬂ";
3070 PRINT"ﬂﬂﬂﬂﬂﬂPOINTS. 10 PTS FOR  ﬂﬂﬂﬂﬂﬂ";
3080 PRINT"ﬂﬂﬂﬂﬂﬂEVERY AGENT KILLED. ﬂﬂﬂﬂﬂﬂ";
3090 PRINT"ﬂﬂﬂﬂﬂﬂOPEN ALL RED DOORS, ﬂﬂﬂﬂﬂﬂ";
3100 PRINT"ﬂﬂﬂﬂﬂﬂTHEN DESCEND TO 1ST ﬂﬂﬂﬂﬂﬂ";
3110 PRINT"ﬂﬂﬂﬂﬂﬂFLOOR FOR BONUS AND ﬂﬂﬂﬂﬂﬂ";
3130 PRINT"ﬂﬂﬂﬂﬂﬂYOUR NEXT MISSION...ﬂﬂﬂﬂﬂﬂ";:RETURN
3150 PRINT@486,"levelﬂofﬂplay";:POKE16884,40:POKE16885,49:POKE16886,45:POKE16887,51:POKE16888,41:POKE16889,63
3160 A(.)=VAL(INKEY$):IFA(.)<1ORA(.)>4THEN3160
3170 A(.)=A(.)*2:RETURN
4000 PRINT@256+64,;
4030 PRINT"––––––HERON––œ–––––Ø––––––––––––";
4040 PRINT"––––––POINT––œ–––––Ø––––––––––––";
4050 PRINT"–––––––ä–Ö–––œ–––––Ø––––––––––––";:RETURN
