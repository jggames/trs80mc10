0 REM COPYRIGHT 1990 FALSOFT
10 REM
20 GOTO250
30 REM MAIN LOOP
40 X=PEEK(A(H)):IF X<>C THEN100
50 POKEA(H-U),Q:POKEA(H),P:IFG<U THEN POKEA(H-L),C:GOTO60
55 G=G-U:L=L+U:PRINT@26,L;
60 SOUND100,1:IFH=M THENGOSUB170
70 K$=INKEY$:IFK$>"D"ANDK$<"I"THENK=ASC(K$)-68
80 A(H+U)=A(H)+D(K):H=H+U:GOTO40
90 REM EATS FOOD
100 IFX<>R THEN150
105 SOUND200,1
110 F=16448+RND(414):IFPEEK(F)<>CTHEN110
120 V=3+INT(L/14):O=5-INT(L/50):IFO<U THENO=U
130 G=G+RND(16):POKEF,R:GOTO50
140 REM
150 IFX=B THEN SOUND1,1:PRINT@106,"hit the wall!";:GOTO190
155 SOUND 145,1:PRINT@106,"hit yourself!";:GOTO190
160 REM
170 FORI=1TOL:A(I)=A(H-L+I):NEXTI:H=I-U:RETURN
180 REM
190 IFL>Z THENZ=L
200 PRINT@426,"another game?";:GOSUB220:IFK$="N"THENEND
205 GOTO330
210 REM
220 K$=INKEY$:IFK$<>""THEN220
230 K$=INKEY$:IFK$=""THEN230
235 RETURN
240 REM
250 CLS:PRINT"INSTRUCTIONS:",,,,"USE THE E,F,G&H KEYS TO MOVE THEWORM AROUND THE SCREEN.  EAT THEFOOD TO GROW LONGER. IF YOU";
255 PRINT" HIT YOURSELF OR THE EDGE OF THE     SCREEN YOU'LL DIE.  HOW LONG CANYOU GROW?"
260 REM
270 M=999:DIMA(M),D(4):FORI=1TO4:READD(I):NEXTI
280 P=35
289 P=99
290 Q=79
300 R=103
310 PRINT@416,"CHOOSE DEGREE OF DIFFICULTY     (1 IS HARD  7 IS EASY)":GOSUB220:D=VAL(K$):IFD<1ORD>7THEN310
315 T=INT(128/D-10):L$=K$
320 REM
330 CLS0:B=PEEK(16384)
340 C=143
350 PRINT@1,"     ";:PRINT@1,Z;:PRINT@14,"wormy";:PRINT@26,"     ";
360 FORI=64TO448STEP32:PRINT@I,CHR$(B);"èèèèèèèèèèèèèèèèèèèèèèèèèèèèèè";CHR$(B);:NEXTI:PRINTL$;
370 A(0)=16416+32*RND(13)+RND(16):POKEA(0),Q:A(1)=A(0)+1:POKEA(1),Q:A(2)=A(1)+1:POKEA(2),P:L=2:H=2:K=4:U=1:GOTO110
380 DATA -32,32,-1,1
