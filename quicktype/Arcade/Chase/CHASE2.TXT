10 GOSUB580:DIMEZ(11),Z1(11),W(30):MC=16384
20 CLS0:PRINT@22,"chase";:GOSUB500
25 WC=0:CC=RND(5):C$=CHR$(166+16*CC):W$=CHR$(175+16*CC):B$="€"
30 FORI=0TO15
40 PRINT@I+1,C$;
45 PRINT@32*I,C$;
50 PRINT@32*I+16,C$;
60 Z=RND(15)
70 IFRND(0)<.5THENWC=WC+1:W(WC)=32*Z+I:PRINT@32*Z+I,W$;
80 S=RND(10):S1=S
90 Z(INT(I/2)+1)=4+S
100 PRINT@480+I,C$;
110 NEXTI:IFWC<10THEN30
115 FORJ=1TO5
116 Z(J)=RND(10)+4:Z(J+5)=RND(10)+4:IFPEEK(MC+32*Z(J)+Z(J+5))<>128THEN116
119 Z1(J)=Z(J):Z1(J+5)=Z(J+5):POKEMC+32*Z(J)+Z(J+5),43:NEXTJ
120 K=RND(14)+1:K1=K:FORJ=1TO5:IFS=Z(J)ANDK=Z(J+5)THENJ=999
125 NEXTJ:IFJ>=999THEN120
126 GOTO200
130 FORI=1TO5
135 IFZ(I)=0THEN190
140 PRINT@32*Z(I)+Z(I+5),B$;
150 Z(I)=Z(I)-SGN(Z(I)-S)
160 Z(I+5)=Z(I+5)-SGN(Z(I+5)-K)
165 IFPEEK(MC+32*Z(I)+Z(I+5))=ASC(W$)THENZ(I)=0:GOTO190
170 POKEMC+32*Z(I)+Z(I+5),43
180 IFZ(I)=SANDZ(I+5)=KTHEN280
190 NEXTI
200 POKE32*S+K+MC,42
210 A$=INKEY$:IFA$=""THEN210
220 PRINT@32*S+K,B$;
230 IFZ(1)+Z(2)+Z(3)+Z(4)+Z(5)=0THEN360
231 IFA$="L"THEN550
235 K=K-1*((A$="D")+(A$="E")+(A$="C")-(A$="A")-(A$="Q")-(A$="Z"))
240 S=S-1*((A$="X")+(A$="Z")+(A$="C")-(A$="W")-(A$="Q")-(A$="E"))
250 IFK<=0ORK>=16ORPEEK(32*S+K+MC)=ASC(W$)THEN280
260 IFS<=0ORS>=15THEN280
270 GOTO130
280 PRINT@309-96,"YOU'RE ";
285 PRINT@309-64,"DEAD!!!";:SOUND150,20
290 PRINT@310-2,"REDO? (R)";
295 PRINT@310+64+1,"OR";
300 PRINT@466-32+2,"NEW MAZE?";
305 PRINT@466+4,"(Y/N)";
310 A$=INKEY$:IFA$=""THEN310
320 IFA$="Y"THEN20
330 IFA$="N"THEN350
335 IFA$="R"THENGOSUB400:GOTO200
340 GOTO310
350 END
360 POKEMC+32*S+K,42:PRINT@32*S+K+1,"SAFE!";:SOUND230,15
370 PRINT@8*32+23,"ALL";:PRINT@9*32+20,"OPPONENTS";
380 PRINT@10*32+20,"DESTROYED";
390 GOTO300
400 CLS0:PRINT@22,"chase";:S=S1:K=K1:GOSUB500
405 FORI=1TOWC:PRINT@W(I),W$;:NEXTI
410 FORI=0TO15
415 PRINT@I+1,C$;
420 PRINT@32*I,C$;
430 PRINT@(32*I+16),C$;
440 PRINT@480+I,C$;
450 NEXTI
470 FORJ=1TO5:POKEMC+32*Z1(J)+Z1(J+5),43:Z(J)=Z1(J):Z(J+5)=Z1(J+5):NEXTJ
480 RETURN
500 PRINT@2*32+23,"USE";:PRINT@3*32+22,"q€w€e";:PRINT@4*32+22,"a€s€d";:PRINT@5*32+22,"z€x€c";
510 PRINT@309-95,"lEAP!";:RETURN
550 PRINT@9*32+20,"6,000,000";:PRINT@10*32+20,"$ JUMP!!!";:FORCC=1TO100STEP10:SOUNDCC,1:NEXT
551 FORCC=110TO10STEP-10:SOUNDCC,1:NEXT:PRINT@9*32+20,"€€€€€€€€€";:PRINT@10*32+20,"€€€€€€€€€";
560 S=RND(13):K=RND(15):CC=PEEK(32*S+K+MC):IFCC<>128THENPOKEMC+32*S+K,106:SOUND150,2:POKEMC+32*S+K,CC:GOTO280
570 POKEMC+32*S+K,42:SOUND1,1:GOTO235
580 CLS:PRINTTAB(13);"CHASE":PRINTTAB(8)"COPYRIGHT 1979":PRINTTAB(6)"CREATIVE COMPUTING":PRINT
581 PRINT"YOU ARE WITHIN THE WALLS OF"
582 PRINT"OF A HIGH VOLTAGE MAZE. THERE"
583 PRINT"ARE 5 SECURITY MACHINES TRYING"
584 PRINT"TO DESTROY YOU. YOU ARE THE '*'"
585 PRINT"THE INTERCEPTORS ARE THE '+'."
586 PRINT"THE AREAS MARKED IN COLOR ARE"
587 PRINT"HIGH VOLTAGE. YOUR ONLY CHANCE"
588 PRINT"OF SURVIVAL IS TO MANEUVER EACH"
589 PRINT"INTERCEPTOR INTO A WALL."
590 PRINT"KEEP MOVING UNTIL YOU ARE SAFE!":PRINT"----------GOOD LUCK------------":PRINT@483,"PRESS ANY KEY TO CONTINUE";
592 CC=RND(0):IFINKEY$=""THEN592
593 RETURN
