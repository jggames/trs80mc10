0 CLEAR400:DIMM,C1,C2,C3,M$,I$,M,X,Y,D(2):M=16384:U=225:GOSUB2000:GOTO60
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-M+C2,:RETURN
10 REM - START -
60 V=2:S=0:N=0:SP=20
70 X=2:Y=8:G=1:D(1)=13:E(1)=4:D(2)=5:E(2)=5:R=0
80 CLS0:FORC1=0TO15:FORC2=0TO15:POKEM+32*C1+C2,32:NEXT:NEXT
90 REM
140 REM  TITLE SCREEN
150 PRINT@32*15,;:M$=" "+CHR$(91)+CHR$(93)+" MOSQUITO "+CHR$(91)+CHR$(93):GOSUB7
170 PRINT@32*2,;:PRINT"吵吵吵吵吵吵吵吵";
175 PRINT@32*13,;:PRINT"技技技技技技技技";
180 PRINT@32*1+1,;:M$="12345":GOSUB7
200 REM - SELECT SPRAY RANGE -
210 B=RND(5):IFB=<3THENB=RND(5)
220 PRINT@1,;:M$="     ":GOSUB7:FORI=1TOB:PRINT@B,;:M$="+":GOSUB7
240 NEXTI
250 A$=INKEY$
260 IFA$<>"S"THEN210
280 PRINT@39,;:M$="SPRAY"+STR$(SP)+" ":GOSUB7
290 REM - MAIN LOOP -
300 POKEM+X+32*Y,32
320 X=X-(PEEK(2)AND1ANDNOTPEEK(16946))+(PEEK(2)AND4ANDNOTPEEK(16948))/4
330 Y=Y-(PEEK(2)AND4ANDNOTPEEK(16952))/4+(PEEK(2)AND8ANDNOTPEEK(16947))/8
340 X=X-(X<1)+(X>15):Y=Y-(Y<3)+(Y>12)
350 POKEM+X+32*Y,95:SOUNDU,1:POKEM+X+32*Y,31
400 FORI=1TO2:C1=RND(4):POKEM+D(I)+32*E(I),32:ONC1GOTO550,560,570,580
450 D(I)=D(I)-(D(I)<1)+(D(I)>15):E(I)=E(I)-(E(I)<3)+(E(I)>12)
460 POKEM+D(I)+32*E(1),61:POKEM+D(I)+32*E(I),43
480 IFX=D(1)ANDY=E(1)THEN990
485 IFX=D(2)ANDY=E(2)THEN990
490 NEXTI:IFINKEY$=" "THEN640
510 GOTO300
550 D(I)=D(I)+1:GOTO450
560 D(I)=D(I)-1:GOTO450
570 E(I)=E(I)+1:GOTO450
580 E(I)=E(I)-1:GOTO450
630 REM - SPRAY -
640 SP=SP-1:PRINT@44,;:M$=STR$(SP)+" ":GOSUB7
650 C1=PEEK(M+X-G+32*Y):IFC1=61ORC1>127THEN730
660 POKEM+X-G+32*Y,58:SOUND142,1:SOUND129,1:SOUND113,1
680 IFX-G=D(1)ANDY=E(1)THENR=1:N=N+1:GOTO730
690 IFX-G=D(2)ANDY=E(2)THENR=2:N=N+1:GOTO730
700 G=G+1:IFG-1=BTHEN720
710 GOTO650
720 REM - RETRACT SPRAY -
730 FORI=1TOG:POKEM+X-G+I+32*Y,32:SOUND113,2:NEXTI:G=1
770 IFR>0THEN880
780 PRINT@44,;:M$=STR$(SP)+" ":GOSUB7
785 IFSP=0THEN990
790 GOTO290
800 REM - CLEAR -
810 PRINT@32*4,"---- CLEAR ---- ";
820 PRINT@2+32*6," BONUS 100 ";:S=S+100
830 SOUND185,4:SOUND200,4:SOUND203,4
831 SOUND142,4:SOUND87,4:SOUND129,4:SOUND129,4:SOUND142,4:SOUND142,4
832 SOUND129,4:SOUND113,4
840 FORI=0TO2000:NEXTI
850 V=N+1:N=0:GOTO70
880 REM  - CLEAR MOSQUITOS? -
890 S=S+10
900 IFN=VTHEN800
910 FORI=1TO2
920 PRINT@D(I)+32*E(I),;:M$="=":GOSUB7:PRINT@D(I)+32*E(I),;:M$="+":GOSUB7
930 POKEM+D(I)+32*E(I),32:IFI=RTHENPRINT@D(R)+32*E(R),;:M$=":":GOSUB7
950 NEXTI
960 R=0
970 D(1)=13:E(1)=7:D(2)=5:E(2)=5
975 IFSP=0THEN990
980 GOTO300:REM BY MR. SLUMP
990 REM - GAME OVER -
1000 CLS0:SOUND113,1:SOUND113,1:SOUND142,1:SOUND149,1:SOUND87,1:SOUND113,1
1001 SOUND231,1:SOUND229,1:SOUND225,1:SOUND221,1:SOUND215,1:SOUND229,1:SOUND225,1
1002 SOUND129,1:SOUND113,4:POKE49151,64:FORC1=0TO480STEP32:PRINT@C1,"                ";:NEXT
1010 PRINT@2+32*4,"* GAME OVER";
1020 PRINT@2+32*6," * SCORE *";
1030 PRINT@5+32*7,S;:FORC1=0TO1STEP0:C1=-(INKEY$=""):NEXTC1
1040 PRINT@2+32*9,"HIT SPACE KEY";
1050 IFINKEY$<>" "THEN1050
1060 CLS0:POKE49151,0:GOTO10

2000 CLS:PRINT@258,;:M$=CHR$(91)+CHR$(93)+" MOSQUITO "+CHR$(91)+CHR$(93):GOSUB7:PRINT"  BY MR. SLUMP":FORC1=1TO3000:NEXT:CLS
2010 M$="PRESS 'S' TO START THE GAME. MOVE THE SPRAY CAN AROUND WITH THE ARROW KEYS (AWSZ).":GOSUB1
2030 M$="PRESS SPACEBAR TO SPRAY IT. IF THE SPRAY HITS A MOSQUITO IT WILL DIE. "
2050 M$=M$+"IF IT HITS THE = THAT APPEAR ON THE SCREEN IT WILL STOP THE SPRAY.":GOSUB1
2060 M$="KILL ENOUGH MOSQUITOS AND THE LEVEL WILL ADVANCE. IF A MOSQUITO TOUCHES YOUR SPRAY CAN IT IS GAME OVER.":GOSUB1
2080 M$="IT'S ALSO GAME OVER WHEN YOU RUN OUT OF SPRAY. "
2090 M$=M$+"YOU START WITH 20 SHOTS OF IT.":GOSUB1:PRINTTAB(9);"HIT SPACE KEY";
3000 M$=INKEY$:C1=RND(0):IFM$=""THEN3000
3010 RETURN

REM FORT=0TO127:POKE16384+T,T:NEXT:PRINT@256,;
