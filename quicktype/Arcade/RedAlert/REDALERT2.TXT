5 CLEAR1000:CLS:HS=0
10 GOSUB520
30 M=270:N=270:CO=0:S=0
40 J=9:G=9:CC=.5:TI=0
50 CLS
60 FORI=0TO480STEP32:PRINT@I,CHR$(175)"                              "CHR$(175);:NEXT
80 B=RND(0):C=RND(0)
100 TI=TI+OE:IFTI=100THENIFCO<5THENTI=0:CC=CC+.05:CO=CO+1
130 IFB<CCTHENJ=J+1:GOTO150
140 J=J-OE
150 IFC<CCTHENG=G+1:GOTO170
160 G=G-OE
170 IFJ>ZOANDG>ETTHENG=G-OE:J=J-OE
180 IFJ+G>T8THENJ=J-OE:G=G-OE
190 IFJ<OETHENJ=OE
195 IFG<OETHENG=OE
200 POKE16952,255:POKE16947,255:POKE16948,255:POKE16946,255:C$=INKEY$
201 IFC$=""THENU=0:GOTO216
202 IFC$="A"THENU=N1:GOTO214
204 IFC$="S"THENU=OE:GOTO214
206 IFC$="Z"THENU=TT:GOTO214
208 IFC$="W"THENU=NT:GOTO214
210 IFC$=CHR$(13)THENGOSUB370:GOTO214
212 GOSUB420:GOTO216
214 IFPEEK(49151)=247THENGOSUB420
216 IFM+U<S4ORM+U>479THENM=M-U
218 M=M+U:P=PEEK(MC+M):PP=PEEK(M1+M)
220 PRINT@N-TT,"  ";:PRINT@N-S4,"  ";:PRINT@M-TT,CHR$(132)CHR$(136);:PRINT@M,CHR$(139)CHR$(135);
222 IFP=96ANDPP=96THEN230
224 IFP=139ORPP=135THEN230
228 GOTO350
230 N=M
240 PRINT@SM,LEFT$(CB$(CO),G);LEFT$(BL$,TT-(J+G));LEFT$(CB$(CO),J)"SCORE:"S;
245 IFRND(TH)=TWTHENPRINT@AP+G,"@";
250 IFRND(EI)=FVTHENPRINT@BP-J,;
260 GOTO80
350 SOUND1,1:FORK=1TO10:PRINT@M-32,CHR$(136+K)CHR$(132+K);:SOUND100,1:NEXTK:GOTO490
370 I=M-33
380 P=PEEK(I+MC)
385 IFP=96THEN410
390 IFP=64THENS=S+200:PRINT@I,"  ";:SOUND200,1:RETURN
395 IFP=O3THENS=S+50:SOUND50,1:GOTO410
400 S=S-50:SOUND200,1:PRINT@I,CHR$(134);" ";:SOUND1,2:PRINT@I,CHR$(137);:RETURN
410 PRINT@I,"< ";:I=I-OE:GOTO380
420 I=M
430 P=PEEK(I+MC):PP=PEEK(I+M1)
432 IFP=96ANDPP=96THEN460
435 IFP=O3ORPP=O3THENS=S+50:SOUND50,1
440 IFP=S4ORPP=S4THENS=S+200:SOUND200,1
450 IFP=175+16*COORPP=175+16*COTHENRETURN
460 PRINT@I,CHR$(133);CHR$(138);:PRINT@I,"  ";
470 I=I+TT:IFI>M+150ORI>511THENRETURN
480 GOTO430
490 FORK=1TO90:NEXT:CLS:PRINT@0,"YOUR SCORE WAS:";S
495 IFS>HSTHENHS=S
497 PRINT"HIGH SCORE IS:";HS
500 PRINT:INPUT"TRY AGAIN (Y/N)";AA$:IFAA$="Y"THEN30
510 CLS:PRINT"BYE.":END
520 CLS:PRINT@12,"RED ALERT"
525 PRINT@12+32, "*********"
560 PRINT:PRINT"TO MOVE USE:":PRINT
570 PRINT"          W"
580 PRINT"        A   S"
590 PRINT"          Z"
600 PRINT:PRINT" <ENTER> FIRES TO THE LEFT":PRINT"<SPACE BAR> FIRES DOWNWARD"
605 GOSUB 1000:PRINT@480,"PRESS ANY KEY TO BEGIN THE GAME";
610 AA$=INKEY$:A=RND(10):IFAA$=""THEN610
620 RETURN
1000 C=3:FORX=0TO5
1010 FORT=0TO32
1020 CB$(X)=CB$(X)+CHR$(128+16*(C-1)+15)
1025 NEXT:C=C+1:NEXT X:C=0:T=0:X=0
1030 MC=16384:M1=16385:O3=132
1040 TT=32:OE=1:OH=100:T8=28:ET=18:ZO=0
1050 SM=480:AP=448:BP=477:NT=-32
1060 TH=3:TW=2:FV=5:EI=8:N1=-1
1070 S4=64:FE=511:SB$="  "
1080 BL$="                                "
1090 RETURN
