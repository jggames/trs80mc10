10 REM HEIANKO-ALIEN
11 I=1:GOTO30
20 D(I)=RND(4)-1:V=X(I)+E(D(I)):P=PEEK(V):IFP=XTHEN20
25 C(I)=RND(4):IFINT(D(I)/2)*2=D(I)THENC(I)=C(I)*5:RETURN
29 C(I)=C(I)*3:RETURN
30 GOSUB6500
40 GOSUB5000:S=.:B=3
60 FORF=4TO8STEP2:W=.:N=F:G=N:GOSUB6600
100 REM MAIN
110 FORT=60TO1STEP-1:PRINT@19,T;:FORU=1TO8:FORI=1TO8:IFI<=NTHENGOSUB410
200 NEXT:GOSUB1210:IFW=1THEN260
220 NEXT:NEXT:PRINT@19,0;:SOUND200,10:GOTO3000
260 NEXT:GOTO60
400 REM SUB ALIEN
410 IFD(I)=4THEN620
420 V=X(I)+E(D(I)):P=PEEK(V):IFP=RTHEN500
450 IFP=XTHENGOSUB20
460 IFP=YTHEN710
470 IFP=24THEN800
500 POKEX(I),R:POKEV,.:X(I)=V:C(I)=C(I)-1:IFC(I)=.THENGOSUB20
530 RETURN
600 REM STOPPED ALIEN
620 C(I)=C(I)-1:IFC(I)>.THENRETURN
640 GOSUB20:P=PEEK(V):IFP=RTHEN500
650 GOTO450
700 REM OCHITA/FALL
710 POKEX(I),R:POKEV,25:FORJ=200TO100STEP10:SOUNDJ,1:NEXT
720 POKEV,35
730 X(I)=V:D(I)=4:C(I)=20:RETURN
800 REM TABETA/EATEN
810 POKEX(I),R:X(I)=V:FORJ=1TO12
820 POKEV,R:IFB=.THEN880
860 IFPEEK(17023)=66THEN1000
880 SOUND245,1:POKEV,24:NEXT
900 REM KEBIISHI SHINU/PLAYER DIES
910 FORI1=1TO8:SOUND20,1:POKEV,24
920 POKEV,.:SOUND30,1:NEXT:POKEV,.:GOTO3000
1000 REM BEKKO AME/CANDY
1010 POKEV,42:B=B-1:B1=4-B
1015 C(I)=7:D(I)=4
1020 POKE(B1-1)+14*R+MC,R
1130 RETURN
1200 REM SUB KEBIISHI/PLAYER
1210 J=PEEK(2)ANDPEEK(17023):IFJ=.THEN1310
1220 IFJ=65THENQ=-1:GOTO1310
1230 IFJ=87THENQ=-R:GOTO1310
1240 IFJ=68THENQ=1:GOTO1310
1250 IFJ=83THENQ=R:GOTO1310
1260 IFJ=86THEN1400
1270 IFJ=78THEN1510
1300 REM KEBIISHI SUSUME/MOVE PLAYER
1310 O=M+Q:IFPEEK(O)<>RTHENRETURN
1330 POKEM,R:M=O:POKEO,24:RETURN
1400 REM ANAHORI/DIG
1410 O=M+Q:P=PEEK(O):IFP=53THENP=106:SOUND150,1
1420 IFP<53THENIFP>48THENP=P+1:SOUND100,1
1440 IFP=RTHENP=49:SOUND240,1
1450 POKEO,P:RETURN
1500 REM ANAUME/FILL HOLE
1510 O=M+Q:P=PEEK(O):IFP=49THENP=R:SOUND1,1
1530 IFP=57THEN1600
1540 IFP<54THENIFP>49THENP=P-1:SOUND100,1
1550 IFP>53THENIFP<57THENP=P+1:SOUND50,1
1560 IFP=106THENP=53:SOUND150,1
1570 IFP=35THENP=54:SOUND175,1
1580 POKEO,P:RETURN
1600 REM ALIEN SHINU/ALIEN DIES
1604 FORI1=1TO8:POKEO,106:SOUND100,1
1605 POKEO,175:SOUND1,1:NEXT
1607 POKEO,R
1610 IFN>1THEN1650
1620 W=1:S=S+T*10+200:GOSUB4100:GOTO1710
1650 FORI1=1TON:IFX(I1)=OTHEN1700
1660 NEXT
1700 X(I1)=X(N):D(I1)=D(N):C(I1)=C(N):N=N-1:S=S+200
1710 FORI1=1TO3:SOUND220,1:PRINT@5,"      ";:PRINT@5,S;:NEXT:RETURN
3000 REM GAME OVER
3010 SOUND100,4:FORI1=1TO4:PRINT@5,"      ";
3020 SOUND50,4:PRINT@.,"SCORE"S;:PRINT@10+15*32," game over! ";:SOUND25,4:M$=INKEY$
3030 PRINT@10+15*32," GAME OVER! ";:SOUND1,4:M$=INKEY$
3040 NEXT:PRINT@10+15*32," PLAY AGAIN? ";
3050 M$=INKEY$:IFM$=""THEN3050
3060 IFM$="Y"THEN40
3070 IFM$="N"THENEND
3080 GOTO3050
4100 REM SUB WAIT2
4120 FORZ=1TO300:NEXT:RETURN
5000 REM SUB TITLE
5010 CLS
5020 FORI=0TO31:PRINT"*";:NEXT
5025 PRINT"*        HEIANKYO-ALIEN        *";
5040 FORI=0TO31:PRINT"*";:NEXT:PRINT
5050 PRINT
5070 PRINT" CREATED BY T.S.G."
5080 PRINT" ALL RIGHTS RESERVED":PRINT
5085 PRINT
5090 PRINT"   W    V=DIG HOLE"
5100 PRINT"   !    B=DROP CANDY"
5110 PRINT" A- -D  N=FILL HOLE"
5120 PRINT"   !    C=START"
5130 PRINT"   S":IFS>HSTHENHS=S
5140 PRINT:PRINT" HIGH SCORE"HS;
5150 PRINT@21+5*32,"àÄÄçèàÄÄåè";
5160 PRINT@21+6*32,"ååÅÑèååÅÄè";
5170 PRINT@21+7*32,"ãÉáÄèãÉáÄè";
5180 PRINT@21+8*32,"±≥≥≥≥≥≥≥≥Ö";
5200 PRINT@21+9*32,"µøø≤ø≤øøøÖ";
5210 PRINT@21+10*32,"µøøøøøøøøÖ";
5220 PRINT@21+11*32,"¥Å¥Å¥Å¥ΩøÖ";
5230 PRINT@21+12*32,"èåçåçåçµøÖ";
5240 PRINT@21+13*32,"±∑≥∑≥∑≥∑øÖ";
5250 PRINT@21+14*32,"ÉÉÉ≥É≥ÉÉÉá";
5260 PRINT@21+15*32,"øøøøèøøøøè";
5270 SOUND225,2:SOUND200,2:SOUND100,2
5280 IFINKEY$<>"C"THEN5280
5290 RETURN
6000 REM SUB LOAD
6010 CLS2:PRINT@.,"SCORE"S:PRINT@15,"TIME"T
6020 REM FORY=1TO15:FORX=0TO31
6030 REM POKEX+Y*32+MC,159:NEXT:NEXT
6040 FORY=2TO14STEP3:FORX=5TO30
6050 POKEX+Y*32+MC,32:NEXT:NEXT
6060 FORX=5TO30STEP5:FORY=2TO14
6070 POKEX+Y*32+MC,32:NEXT:NEXT
6080 R=4:FORI1=1TO2
6090 R1=RND(4)-1:IFR=R1THEN6090
6100 R=R1:X=R1*5+10:Y=(RND(4)-1)*3+3:POKEX+Y*32+MC,159
6110 Y=Y+1:POKEX+Y*32+MC,159
6120 X=(RND(5)-1)*5+6:Y=(RND(3)-1)*3+5
6130 PRINT@X+Y*32,"üüüü";:NEXT
6140 PRINT@1+12*32,"bon";:PRINT@1+13*32,"bon";
6150 IFB=3THENPOKE1+14*32+MC,42:POKE1+14*32+MC+1,42:POKE1+14*32+MC+2,42
6160 IFB=2THENPOKE1+14*32+MC,32:POKE1+14*32+MC+1,42:POKE1+14*32+MC+2,42
6170 IFB=1THENPOKE1+14*32+MC,32:POKE1+14*32+MC+1,32:POKE1+14*32+MC+2,42
6180 IFB=0THENPOKE1+14*32+MC,32:POKE1+14*32+MC+1,32:POKE1+14*32+MC+2,32
6190 X=159:Y=106:R=32:RETURN
6500 REM SUB INIT-1
6510 DIMX(8),D(8),C(8),E(3),I,Z,P,V,M,N,O,J,T,U,Q,W,F,G,X,Y,R:MC=16384
6520 I=RND(-(PEEK(9)*256+PEEK(10)))
6430 Q=-(RND(3)-1)*31-2
6540 E(0)=1:E(1)=32:E(2)=-1:E(3)=-32
6550 RETURN
6600 REM SUB INIT2
6610 T=60:GOSUB6000:D(1)=(RND(3)-1):IFN=1THEN6660
6630 FORI=1TON:D(I)=1-D(I-1):GOSUB25:NEXT
6660 FORI=1TON:X(I)=MC+69:NEXT
6690 FORI=1TON:GOSUB25:NEXT
6730 M=MC+69+409:POKEM,24:SOUND100,2:SOUND200,2:SOUND225,2
6780 RETURN
