0 CLS:POKE16925,0:POKE16926,1:DIMT,P,S(6),K(255),D(6),C(6),T(6),R(2),B,M,L,Z,J:GOTO1000
1 D(T)=D(T)*-1:POKES(T),83:ONK(PEEK(S(T)+32))GOTO22,22,22,22,3
2 POKES(T),B:S(T)=S(T)+32:POKES(T),83:IFPEEK(S(T)+32)=BTHEN2
3 IFPEEK(S(T)+32)=88THENPOKES(T),B:S(T)=S(T)+32:POKES(T),83:GOTO88
4 POKES(T),64:SOUND9,1:D(T)=0:C(T)=64:T(T)=L:GOTO22
5 IFD(T)THEN1
6 T(T)=T(T)-1:IFT(T)THEN22
7 IFPEEK(S(T)-1)<>BTHENIFPEEK(S(T)+1)<>BTHENONK(PEEK(S(T)+32))GOTO65,8,8,8,68,69,65
8 POKES(T),83:D(T)=R(RND(2)):C(T)=B:IFS(T)<16864THEN22
9 PRINT@.,"BASE CAPTURED!  TRY AGAIN (Y/N)?";:GOTO100
10 POKEP,B:ONK(PEEK(P-1))GOTO11,70,18,70,18,11,18
11 P=P-1:GOTO18
12 POKEP,B:ONK(PEEK(P+1))GOTO13,71,18,71,18,13,18
13 P=P+1:GOTO18
14 POKEP,B:ONK(PEEK(P-32))GOTO15,72,18,72,18,15,18
15 P=P-32:GOTO18
16 POKEP,B:ONK(PEEK(P+32))GOTO17,73,18,73,18,17,18
17 P=P+32:GOTO18
18 POKEP,89:QQ=PEEK(9)AND128:POKEF,QQ:POKEP,88:RETURN
20 FORL=12TO4STEP-2:FORG=1TO250:FORT=1TO6:POKES(T),C(T):ONK(PEEK(S(T)+D(T)))GOTO1,1,1,5,88,55
21 S(T)=S(T)+D(T):POKES(T),83:D(T)=R(RND(2)):IFPEEK(S(T)+32)<>32THEN1
22 IFPEEK(2)THENGOSUB25
23 NEXT:NEXT:FORZ=1TO10:SOUNDRND(50)+25,1:NEXT:NEXT:GOTO49
25 IF4ANDNOTPEEK(16952)THEN14
26 IF1ANDNOTPEEK(16946)THEN10
27 IF4ANDNOTPEEK(16948)THEN12
28 IF8ANDNOTPEEK(16947)THEN16
29 IF32ANDNOTPEEK(16949)THEN50
30 IF32ANDNOTPEEK(16951)THEN60
31 RETURN
49 PRINT@.,"TIME IS UP!     ";:SOUND100,10:PRINT"TRY AGAIN (Y/N)?";:GOTO100
50 IFPEEK(P-1)<>BTHENRETURN
51 IFJ>=3THENJ=3:POKE16895,96:RETURN
52 J=J+1:Z=P-1:POKEZ,110:SOUND243,1:GOTO63
55 S(T)=S(T)+D(T):POKES(T),83:GOTO3
60 IFPEEK(P+1)<>BTHENRETURN
61 IFJ>=3THENJ=3:POKE16895,96:RETURN
62 J=J+1:Z=P+1:POKEZ,110:SOUND243,1
63 IFPEEK(Z+32)=32ORZ>16863THENRETURN
64 POKEZ,143:Z=Z+32:POKEZ,110:GOTO63
65 T(T)=T(T)+L:POKES(T),B:S(T)=S(T)+32:POKES(T),64:PRINT@498,"  **WARNING**";:SOUND59,3
66 IFS(T)<16864THEN22
67 GOTO9
68 T(T)=T(T)+F:POKES(T),B:S(T)=S(T)+32:POKES(T),64:GOTO88
69 T(T)=T(T)+F:POKES(T),B:S(T)=S(T)+32:POKES(T),64:SOUND9,1:GOTO22
70 P=P-1:GOTO75
71 P=P+1:GOTO75
72 P=P-32:GOTO75
73 P=P+32:GOTO75
75 FORZ=1TO6:IFS(Z)=PTHENS(Z)=16384+Z*3+RND(10):C(Z)=143:POKES(Z),83:SOUND9,1:Z=6
76 NEXT:IFPEEK(P)<>64THEN88
78 POKEP,79:SOUND200,2:SOUND225,2:SOUND230,1:J=J-1:POKE16895,110:IFJ<0THENJ=0
79 SC=SC+10:GOSUB80:GOTO18
80 PRINT@480,LEFT$("SCORE"+STR$(SC)+" ++++++++",15);:PRINT@498,RIGHT$("++++++ HIGH"+STR$(HS),13);:RETURN
88 FORZ=1TO6:POKEP,125:SOUND50,2:POKEP,126:SOUND25,2:POKEP,125:SOUND50,2:POKEP,124:SOUND25,2:NEXTZ
99 PRINT@0,"GULP!           TRY AGAIN (Y/N)?";
100 I$=INKEY$:IFI$=""THEN100
110 IFI$="Y"THEN150
120 IFI$="N"THENEND
130 GOTO100
150 IFSC>HSTHENHS=SC
200 CLS1:FORT=16416TO16863:POKET,32:NEXT:POKE16384,42:POKE16415,42
210 FORT=6*32+16TO480STEP32:POKEM+T,B:NEXT
220 FORT=6*32+1TO6*32+30:POKEM+T,B:NEXT:POKE16879,42:POKE16881,42
230 FORT=1TO6:POKE32*T+3+M,B:POKEM+32*T+28,B:NEXT
240 FORT=1TO6:S(T)=M+T*3:D(T)=R(RND(2)):C(T)=B:NEXT:P=16880:POKEP,88
250 J=0:SC=0:L=10:GOSUB80:POKE16895,110:GOTO20
1000 CLS0:T=RND(-(PEEK(9)*256+PEEK(10)))
1001 PRINT"───────────цццб─────────────────";
1002 PRINT"е─йй─ццокц─кццй──ей─────фахеее─е";
1003 PRINT"гбкойнлллл─кццй─цгкц───ахгцаццец";
1004 PRINT"ммнйййцонхацццц─лмнл──амемлегггл";
1005 PRINT"е─йхййкцкб─кгац──гк─ацбедеоеггее";
1006 PRINT"гййд─йкцеб─кгбф─анебееее─ец─ецен";
1007 PRINT"л─кцййццбкакгфбагхдкблее─елеохфй";
1008 PRINT"──лл─хллхлдлд─лдл──лхдхд─д──д──л";
1030 POKE49151,64:PRINT@256,;"********* CAVERN 2160 **********";
1040 PRINT"   S ENEMY    @ FROZEN ENEMY"
1045 PRINT"                (CATCHABLE)"
1050 PRINT"   X PLAYER   . FOOD"
1055 PRINT"  "CHR$(91)"A"CHR$(93)CHR$(91)"W"CHR$(93)CHR$(91)"S"CHR$(93)CHR$(91)"Z"CHR$(93);
1056 PRINT" TO MOVE PLAYER"
1060 PRINT"  "CHR$(91)","CHR$(93)CHR$(91)"."CHR$(93)" TO DROP FOOD (3 MAX)"
1065 PRINT"ORIGINAL PROGRAM BY TAKAYA ARITA";
1066 PRINT"I/O MAGAZINE JUL/1980 hit a key";:POKE16895,96:POKE16891,32:POKE16889,32
1100 FORT=0TO255:K(T)=7:NEXT:K(143)=0
1105 K(32)=1:K(83)=2:K(42)=3:K(64)=4:K(88)=5:K(110)=6
1110 R(1)=-1:R(2)=1
1120 K(65)=1:K(83)=2:K(87)=3:K(90)=4:K(88)=5:K(67)=6:K(44)=5:K(46)=6:M=16384:F=49151:B=6
1500 FORP=0TO63:FORT=0TO14:IFPOINT(P,T)THENSET(P,T,B):IFINKEY$<>""THEN1540
1510 NEXT:NEXT
1520 B=B+1:IFB=8THENB=2
1530 GOTO1500
1540 CLS1:SOUND1,1:T=0:P=0:B=143
1550 GOTO200
2000 REM MC-10 VERSION
1010 REM BY JIM GERRIE
