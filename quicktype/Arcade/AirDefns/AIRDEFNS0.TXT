100 REM MC-10 COMPUTER
110 REM AIR DEFENSE
120 CLS:PRINT@10,"air defense":PRINT
130 PRINT"SAVE YOUR CITY FROM FALLING"
140 PRINT "BOMBS. USE THE wasz KEYS"
150 PRINT"TO INTERCEPT BOMBS BEFORE THEY"
155 PRINT"LAND. THE SOONER YOU HIT THE"
157 PRINT"BOMB, THE BETTER YOUR SCORE IS.":PRINT
160 PRINT"THE GAME CONTINUES UNTIL"
170 PRINT"YOUR CITY IS GONE."
180 PRINT:PRINT"PRESS space TO BEGIN"
190 IFINKEY$<>" "THEN190
195 CI$="CGKO"
200 CLS0:PRINT@481,"";
210 FORI=1TO30:PRINTCHR$(ASC(MID$(CI$,RND(4)))+64+8*RND(7));:NEXT:PRINT:POKE16895-32,128
220 DIM CI(63):FORI=1TO63:CI(I)=1:NEXT
240 CX=31:CY=15:OX=CX:OY=CY
250 BX=RND(60)+1
260 FORB=1TO28STEP2:FORA=1TO2
280 RESET(OX,OY):RESET(OX-1,OY+1):RESET(OX+1,OY+1):RESET(OX,OY+2)
290 C1=-(PEEK(2)AND1ANDNOTPEEK(16946))+(PEEK(2)AND4ANDNOTPEEK(16948))/4:CX=CX+C1*-(CX+C1>1ANDCX+C1<62)
291 C2=-(PEEK(2)AND4ANDNOTPEEK(16952))/4+(PEEK(2)AND8ANDNOTPEEK(16947))/8:CY=CY+C2*-(CY+C2>1ANDCY+C2<30)
322 SET(CX,CY,5):SET(CX-1,CY+1,5):SET(CX+1,CY+1,5):SET(CX,CY+2,5)
325 OX=CX:OY=CY:IFABS(CX-BX)<1ANDABS(CY-B)<1THENA=2:NEXT:GOTO1500
326 NEXT
330 SET(BX,B,RND(8))
340 NEXT
341 CI(BX)=0
342 CS=CS+1:IFCS=60THEN1000
350 REM EXPLOSION
351 GOSUB2000:SOUND250,1:SOUND251,1:SOUND253,1:SOUND254,1
355 FORI=1TO5
360 SET(BX-2,B-2,8):SET(BX,B-2,2):SET(BX+2,B-2,8):SET(BX-1,B-1,2):SET(BX+1,B-1,2):SET(BX,B,4)
370 RESET(BX-2,B-2):RESET(BX,B-2):RESET(BX+2,B-2):RESET(BX-1,B-1):RESET(BX+1,B-1):RESET(BX,B)
380 NEXT
390 GOTO250
499 END
1000 REM GAME OVER
1010 FORI=0TO31
1020 FORI=31TO0STEP-1
1030 FORJ=1TO5
1040 SET(RND(63),I,RND(8))
1050 NEXT:NEXT
1060 PRINT@0,"PRESS space TO PLAY AGAIN":GOTO190
1500 GOSUB2000:SOUND240,5
1510 SC=SC+100-B*100/31
1520 PRINT@490,INT(SC);
1530 GOTO250
2000 FORI=BTO0STEP-2:RESET(BX,I):NEXT:RETURN
