100 CLS:DIMX,Y,Q,R,A,B
110 CI$="CGKO"
120 CLS:PRINT@10,"air defense":PRINT
130 PRINT"SAVE YOUR CITY FROM FALLING"
140 PRINT "BOMBS. USE THE w a s AND z KEYS"
150 PRINT"TO INTERCEPT BOMBS BEFORE THEY"
155 PRINT"LAND. THE SOONER YOU HIT THE"
157 PRINT"BOMB, THE BETTER YOUR SCORE IS.":PRINT
160 PRINT"THE GAME CONTINUES UNTIL YOUR"
170 PRINT"CITY IS GONE (30 HITS)."
180 PRINT:PRINT"PRESS space TO BEGIN"
190 IFINKEY$<>" "THEN190
200 CLS0:SC=0:CS=30:PRINT@481,"";
210 FORI=1TO30:PRINTCHR$(ASC(MID$(CI$,RND(4)))+64+8*RND(7));:NEXT:PRINT:POKE16895-32,128
240 X=31:Y=15:Q=X:R=Y:PRINT@500,CS;
250 A=RND(60)+1
260 FORB=1TO28STEP2:SET(A,B,RND(8)):FORC=1TO2:RESET(Q,R):RESET(Q-1,R+1):RESET(Q+1,R+1):RESET(Q,R+2)
290 X=X-(PEEK(2)AND1ANDNOTPEEK(16946))+(PEEK(2)AND4ANDNOTPEEK(16948))/4
291 Y=Y-(PEEK(2)AND4ANDNOTPEEK(16952))/4+(PEEK(2)AND8ANDNOTPEEK(16947))/8
300 IFX<1THENX=62:GOTO310
305 IFX>62THENX=1
310 IFY<1THENY=25:GOTO322
320 IFY>25THENY=1
322 SET(X,Y,5):SET(X-1,Y+1,5):SET(X+1,Y+1,5):SET(X,Y+2,5):Q=X:R=Y:IFX=ATHENIFY=BORY=B+1THENC=3:NEXTC:GOTO1500
330 NEXT:NEXT
342 CS=CS-1:PRINT@500,CS;:IFCS<1THEN1000
350 REM EXPLOSION
351 GOSUB2000:SOUND250,1:SOUND251,1:SOUND253,1:SOUND254,1
355 FORI=1TO5
360 SET(A-2,B-2,8):SET(A,B-2,2):SET(A+2,B-2,8):SET(A-1,B-1,2):SET(A+1,B-1,2):SET(A,B,4)
370 RESET(A-2,B-2):RESET(A,B-2):RESET(A+2,B-2):RESET(A-1,B-1):RESET(A+1,B-1):RESET(A,B)
380 NEXT:RESET(A,28):RESET(A,29):RESET(A-1,28):RESET(A-1,29):RESET(A+1,28):RESET(A+1,29)
390 GOTO250
499 END
1000 REM GAME OVER
1020 FORI=29TO0STEP-1
1030 FORJ=1TO5
1040 SET(RND(63),I,RND(8))
1050 NEXT:NEXT
1060 IFSC>HSTHENHS=SC
1070 PRINT@0,"air defense HIGH SCORE:"HS
1090 PRINT@32,"PRESS space TO PLAY AGAIN":GOTO190
1500 GOSUB2000:SOUND240,5
1510 SC=SC+INT(100-B*100/31)
1520 PRINT@488,INT(SC);
1530 GOTO250
2000 FORI=BTO0STEP-2:RESET(A,I):NEXT:RETURN
3000 REM AIR DEFENSE
3010 REM T.L. WAHL
3020 REM COMPUTE! APRIL 1983
3030 REM MC-10 EDITS JIM GERRIE
