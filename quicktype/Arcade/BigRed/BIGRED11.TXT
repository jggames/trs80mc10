0 CLS:DIMA(202),V(4,2),I,X,Y,Z,E,C,D,F,T,XX,YY,ZZ,S,DP,S(6,2),NM$(6):Z=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
1 PRINT@270,"±²";:PRINT@302,"½¾";:RETURN
3 PRINT@269,"°³³²";:PRINT@301,"µ»»¿";:PRINT@333,"´»³¾";:RETURN
6 PRINT@301,"·¼¾½»";:PRINT@332,"µ¿³»·¿º";:PRINT@364,"µ¿€¸¸¿º";:PRINT@397,"½»³·¾";:RETURN
14 PRINT@236,"±·¿¿¿¿³";:PRINT@267,"·¿¿¿¿¿¿¿»²";:PRINT@298,"·¿¿°°¿°°¿¿¿";:PRINT@330,"¿¿¿°°¿°°¿¿¿";
15 PRINT@362,"¿¿¼¼¿¼½¾¼¿¿";:PRINT@394,"½¿º°´°°¸±¿¿";:PRINT@427,"½¿³°°°°·¿¸";:PRINT@460,"¼¿¿¿¿¿¾";:RETURN
20 GOSUB6000:FORDD=1TO1500:NEXT:Y=V+1:Z=1:DD=1:RETURN
21 PRINT@270,"€€";:PRINT@302,"€€";:RETURN
23 PRINT@269,"€€€€";:PRINT@301,"€€€€";:PRINT@333,"€€€€";:RETURN
26 PRINT@301,"€€€€€";:PRINT@332,"€€€€€€€";:PRINT@364,"€€€€€€€";:PRINT@397,"€€€€€";:RETURN
34 PRINT@236,"€€€€€€€";:PRINT@267,"€€€€€€€€€€";:PRINT@298,"€€€€€€€€€€€";:PRINT@330,"€€€€€€€€€€€";
35 PRINT@362,"€€€€€€€€€€€";:PRINT@394,"€€€€€€€€€€€";:PRINT@427,"€€€€€€€€€€";:PRINT@460,"€€€€€€€";:RETURN
40 PRINT" ******************************":PRINT" * 'BIG RED' FROM DON SCALES' *":PRINT" *     LABYRINTH (C) 1980     *"
43 PRINT" * FOR MC-10 JIM GERRIE 2014  *":PRINT" ******************************"
45 PRINT:PRINT"YOU FIND YOURSELF IN A 10 X 10":PRINT"MAZE (ENTER SOUTH EXIT NORTH)":PRINT"WITH A GIANT KILLER TOMATO!"
46 PRINT"YOU MUST FIND THE EXIT BEFORE":PRINT"HE FINDS YOU.  GOOD LUCK.":PRINT:RETURN
100 V(1,1)=1:V(2,0)=1:V(3,1)=-1:V(4,0)=-1:V(1,2)=3:V(2,2)=4:V(3,2)=1:V(4,2)=2:H=10:V=10
110 CLS:GOSUB40:PRINT"THINKING...":A=H*V+1:DD=0:FORI=1TOA+A:A(I)=0:NEXT
120 Q=0:Z=0:X=RND(H)
130 A(A)=X
140 A(X)=1:C=2
150 R=X:S=1:GOTO220
160 IFR<>HTHEN200
170 IFS<>VTHEN190
180 R=1:S=1:GOTO210
190 R=1:S=S+1:GOTO210
200 R=R+1
210 IFA(R+(S-1)*H)=0THEN160
220 IFR-1=0THEN610
230 IFA(R-1+(S-1)*H)<>0THEN610
240 IFS-1=0THEN420
250 IFA(R+(S-2)*H)<>0THEN420
260 IFR=HTHEN320
270 IFA(R+1+(S-1)*H)<>0THEN320
280 X=RND(3)
290 IFX=1THEN960
300 IFX=2THEN1000
310 GOTO1040
320 IFS<>VTHEN350
330 IFZ=1THEN400
340 Q=1:GOTO360
350 IFA(R+S*H)<>0THEN400
360 X=RND(3)
370 IFX=1THEN960
380 IFX=2THEN1000
390 GOTO1110
400 X=RND(2)
410 GOTO370
420 IFR=HTHEN540
430 IFA(R+1+(S-1)*H)<>0THEN540
440 IFS<>VTHEN470
450 IFZ=1THEN520
460 Q=1:GOTO480
470 IFA(R+S*H)<>0THEN520
480 X=RND(3)
490 IFX=1THEN960
500 IFX=2THEN1040
510 GOTO1110
520 X=RND(2)
530 GOTO490
540 IFS<>VTHEN570
550 IFZ=1THEN960
560 Q=1:GOTO580
570 IFA(R+S*H)<>0THEN960
580 X=RND(2)
590 IFX=1THEN960
600 GOTO1110
610 IFS-1=0THEN820
620 IFA(R+(S-2)*H)<>0THEN820
630 IFR=HTHEN750
640 IFA(R+1+(S-1)*H)<>0THEN750
650 IFS<>VTHEN680
660 IFZ=1THEN730
670 Q=1:GOTO690
680 IFA(R+S*H)<>0THEN730
690 X=RND(3)
700 IFX=1THEN1000
710 IFX=2THEN1040
720 GOTO1110
730 X=RND(2)
740 GOTO700
750 IFS<>VTHEN780
760 IFZ=1THEN1000
770 Q=1:GOTO790
780 IFA(R+S*H)<>0THEN1000
790 X=RND(2)
800 IFX=1THEN1000
810 GOTO1110
820 IFR=HTHEN910
830 IFA(R+1+(S-1)*H)<>0THEN910
840 IFS<>VTHEN870
850 IFZ=1THEN1040
860 Q=1:GOTO880
870 IFA(R+S*H)<>0THEN1040
880 X=RND(2)
890 IFX=1THEN1040
900 GOTO1110
910 IFS<>VTHEN940
920 IFZ=1THEN160
930 Q=1:GOTO 950
940 IFA(R+S*H)<>0THEN160
950 GOTO1110
960 A(R-1+(S-1)*H)=C
970 C=C+1:A(A+R-1+(S-1)*H)=2:R=R-1
980 IFC=ATHEN1210
990 Q=0:GOTO220
1000 A(R+(S-2)*H)=C
1010 C=C+1
1020 A(A+R+(S-2)*H)=1:S=S-1:IFC=ATHEN1210
1030 Q=0:GOTO220
1040 A(R+1+(S-1)*H)=C
1050 C=C+1:IFA(A+R+(S-1)*H)=0THEN1070
1060 A(A+R+(S-1)*H)=3:GOTO1080
1070 A(A+R+(S-1)*H)=2
1080 R=R+1
1090 IFC=ATHEN1210
1100 GOTO610
1110 IFQ=1THEN1170
1120 A(R+S*H)=C:C=C+1:IFA(A+R+(S-1)*H)=0THEN1140
1130 A(A+R+(S-1)*H)=3:GOTO1150
1140 A(A+R+(S-1)*H)=1
1150 S=S+1:IFC=ATHEN1210
1160 GOTO220
1170 Z=1
1180 IFA(A+R+(S-1)*H)=0THEN1200
1190 A(A+R+(S-1)*H)=3:Q=0:GOTO160
1200 A(A+R+(S-1)*H)=1:Q=0:R=1:S=1:GOTO210
1210 IFZ<>1THENX=A+RND(H)+(V-1)*H:A(X)=A(X)+1
1610 XX=RND(H):YY=RND(V):ZZ=RND(4):PRINT:INPUT"ENTER YOUR NAME";I$:NM$=LEFT$(I$,8)
1630 GOSUB1900
1640 FORI=1TOA-1
1650 J=I+A
1660 A(I)=(3-A(J))*2
1670 NEXTI
1710 W=A(A)
1720 FORJ=1TOV
1730 K=(J-1)*H
1740 FORI=1TOH
1750 L=I+K
1760 IFJ<>1THEN1790
1770 IFI=WTHEN1820
1780 M=1:GOTO1810
1790 M=INT(A(L-H)/2)
1800 M=M-INT(M/2)*2
1810 A(L)=A(L)+M*8
1820 IFI=1THENM=1:GOTO1850
1830 M=INT(A(L-1)/4)
1840 M=INT(M-INT(M/2)*2)
1850 A(L)=A(L)+M
1860 NEXTI:NEXTJ
1880 X=W:Y=0:Z=1:CLS0
1890 GOTO3050
1900 CLS
1910 PRINT"PRESS A TO TURN LEFT"
1920 PRINT"      S TO TURN RIGHT"
1930 PRINT"      W TO GO FORWARD"
1940 PRINT"      H FOR HELP"
1950 PRINT"      Q TO GIVE UP!"
1960 PRINT:PRINT"RECENT SCORES:"
1970 FORT=1TO5:PRINTTAB(6)NM$(T),RIGHT$("   "+STR$(INT(S(T,2))),3)" % OF"S(T,0)+S(T,1):NEXT:RETURN
2000 PRINT@0,"YOU ARE AT "; 
2010 PRINT@32,X;"EAST";
2020 PRINT@64,Y;"NORTH";
2030 PRINT@128,"YOU ARE FACING ";
2040 IFZ=1THENPRINT"NORTH";
2050 IFZ=2THENPRINT"EAST";
2060 IFZ=3THENPRINT"SOUTH";
2070 IFZ=4THENPRINT"WEST";
2080 PRINT@0,;:GOTO2200
2150 S=0:LX=X:LY=Y:FORCC=1TODP:IFLX=XXANDLY=YYTHENONCCGOSUB20,14,6,3,1:S=CC:ZZ=V(Z,2):CC=DP:NEXT:RETURN
2160 LX=LX+V(Z,0):LY=LY+V(Z,1):NEXT:RETURN
2200 IFY>VTHEN5000
2210 GOSUB2150
2223 Q=Z:ONRND(7)GOTO2224,2228,2228,2228,2228:Z=ZZ:A=XX:B=YY:GOSUB2600:IFE=0THEN2225
2224 ZZ=RND(4):GOTO2228
2225 I=ABS(XX-X):T=ABS(YY-Y):IFI<3ANDT<3THENSOUND(I+T)*25,1
2226 XX=XX+V(Z,0):YY=YY+V(Z,1):Z=Q:IFSTHENONSGOSUB20,34,26,23,21:GOTO3050
2227 GOSUB2150:IFSTHENONSGOSUB20,34,26,23,21:GOTO3050
2228 Z=Q:I$=INKEY$:IFI$=""THEN2223
2230 IFI$="A"THEN2300
2240 IFI$="S"THEN2350
2250 IFI$="W"THEN3000
2260 IFI$="H"THEN2000
2270 IFI$="Q"THEN5030
2280 GOTO2223
2300 Z=Z-1:IFZ<1THENZ=Z+4
2320 GOTO3045
2350 Z=Z+1:IFZ>4THENZ=Z-4
2370 GOTO3045
2500 IFYY>VTHENZZ=3:E=0:RETURN
2510 E=2:RETURN
2600 IFB=0THEN2710
2610 IFB>VTHEN2500
2620 F=A(A+(B-1)*H):IFZ=1THEN2670
2640 FORI=2TOZ:F=INT(F/2)+(F-INT(F/2)*2)*8:NEXT
2670 C=F-INT(F/2)*2:D=INT(F/4)-INT(F/8)*2:E=INT(F/2)-INT(F/4)*2:RETURN
2710 C=0:D=0:E=-1:IFZ<>1THEN2760
2730 E=1:IFA=WTHENE=0
2750 RETURN
2760 IFZ=3THENE=2
2770 IFZ=2THENIFA=HTHENE=2
2780 IFZ=4THENIFA=1THENE=2
2790 RETURN
2800 IFE>0THEN2930
2810 A=A+V(Z,0):B=B+V(Z,1):RETURN
2930 IFE=1THENPRINT@13,"WALL!";
2950 IFE=2THENPRINT@9,"NO MAN'S LAND";
2960 FORI=1TO15:SOUND1,1:NEXTI:PRINT@9,"€€€€€€€€€€€€€";:RETURN
3000 A=X:B=Y:GOSUB2600:GOSUB2800:X=A:Y=B:IFE>0THEN2200
3045 CLS0
3050 A=X:B=Y:GOSUB2600:FORT=1TO5:GOSUB3200:IFE<>0THENT=5:GOTO3130
3100 GOSUB2800:GOSUB2600:IFE=2THENT=5:GOTO3130
3130 NEXT:GOTO2200
3200 ONTGOTO3210,3610,4010,4410,4810
3210 DP=1:IFE<0THENRETURN
3230 IFE>1THENRETURN
3240 FORI=33TO481STEP32:PRINT@I,"Õ";:PRINT@I+28,"Ú";:NEXT:IFE=0THENRETURN
3290 PRINT@34,"ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ";:PRINT@482,"ÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓ";:RETURN
3610 IFC=0THEN3690
3620 PRINT@34,"Ù";:PRINT@68,"Ù";:PRINT@102,"Ù";:PRINT@482,"Ö";:PRINT@452,"Ö";:PRINT@422,"Ö";:GOTO3730
3690 PRINT@130,"ÜÜÜÜÜ";:PRINT@386,"ÓÓÓÓÓ";
3730 IFD=0THEN3810
3740 PRINT@60,"Ö";:PRINT@90,"Ö";:PRINT@120,"Ö";:PRINT@508,"Ù";:PRINT@474,"Ù";:PRINT@440,"Ù";:GOTO3850
3810 PRINT@152,"ÜÜÜÜÜ";:PRINT@408,"ÓÓÓÓÓ";
3850 FORI=135TO391STEP32:PRINT@I,"Õ";:PRINT@I+16,"Ú";:NEXT
3890 DP=2:IFE=0THENRETURN
3900 PRINT@136,"ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ";:PRINT@392,"ÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓ";:RETURN
4010 IFC=0THEN4070
4020 PRINT@136,"Ù";:PRINT@170,"Ù";:PRINT@392,"Ö";:PRINT@362,"Ö";:GOTO4110
4070 PRINT@200,"ÜÜÜ";:PRINT@328,"ÓÓÓ";
4110 IFD=0THEN4170
4120 PRINT@150,"Ö";:PRINT@180,"Ö";:PRINT@406,"Ù";:PRINT@372,"Ù";:GOTO4210
4170 PRINT@212,"ÜÜÜ";:PRINT@340,"ÓÓÓ";
4210 FORI=203TO331STEP32:PRINT@I,"Õ";:PRINT@I+8,"Ú";:NEXT:DP=3:IFE=0THENRETURN
4260 PRINT@204,"ÜÜÜÜÜÜÜ";:PRINT@332,"ÓÓÓÓÓÓÓ";:RETURN
4410 IFC=0THEN4450
4420 PRINT@204,"Ù";:PRINT@332,"Ö";:GOTO4470
4450 PRINT@236,"Ü";:PRINT@300,"Ó";
4470 IFD=0THEN4510
4480 PRINT@210,"Ö";:PRINT@338,"Ù";:GOTO4530
4510 PRINT@242,"Ü";:PRINT@306,"Ó";
4530 FORI=237TO301STEP32:PRINT@I,"Õ";:PRINT@I+4,"Ú";:NEXT
4570 DP=4:IFE=0THENRETURN
4580 PRINT@238,"ÜÜÜ";:PRINT@302,"ÓÓÓ";:RETURN
4810 IFC=0THEN4850
4820 PRINT@238,"Ù";:PRINT@302,"Ö";:GOTO4870
4850 PRINT@238,"Ó";:PRINT@302,"Ü";
4870 IFD=0THEN4910
4880 PRINT@240,"Ö";:PRINT@304,"Ù";:GOTO4930
4910 PRINT@240,"Ó";:PRINT@304,"Ü";
4930 PRINT@270,"Õ";:PRINT@272,"Ú";
4950 DP=5:IFE=0THENRETURN
4960 PRINT@239,"Ó";:PRINT@303,"Ü";:RETURN
5000 IFDD=1THENFORT=1TO10:SOUND9,1:SOUND5,1:FORZ=1TO50:NEXT:NEXT:GOTO5030
5010 CLS6
5011 PRINT"ßßßßßßßßßßßßßßßŸßßßßŸßßßßßßßßßßß";:PRINT"ßßßßßßßßßßßßŸßßŸßßßŸßßßßßßßßßßßß";
5013 PRINT"ßßßßßßßßßßßßŸŸŸŸŸŸŸŸßßŸßßßßßßßßß";:PRINT"ßßßßßßßßßßßßßßŸŸŸŸŸŸŸŸßßßßßßßßßß";
5015 PRINT"ßßßßßßßßßßßŸŸŸŸŸŸŸŸŸßßßßßßßßßßßß";:PRINT"ßßßßßßßßßßßßßßŸŸŸŸŸŸŸßßßßßßßßßßß";
5017 PRINT"ßßßßßßßßßßßßßŸŸßßŸßßŸŸßßßßßßßßßß";:PRINT"ßßßßßßßßßßßßßŸßßßŸßßßŸßßßßßßßßßß";
5020 PRINT@480,"     YOU MADE IT OUT ALIVE!    ";:POKE16895,96:FORI=1TO15:SOUND150,1:NEXT:PRINT@0,;
5030 FORT=1TO5:IFNM$=NM$(T)THENS(T,0)=S(T,0)+(1-DD):S(T,1)=S(T,1)+DD:S(T,2)=S(T,0)/(S(T,0)+S(T,1))*100:T=5:NEXTT:GOTO5032
5031 NEXTT:NM$(6)=NM$:S(6,0)=S(6,0)+(1-DD):S(6,1)=S(6,1)+DD:S(6,2)=S(6,0)/(S(6,0)+S(6,1))*100
5032 FORT=6TO2STEP-1:IFS(T,2)<S(T-1,2)THEN5035
5033 C=S(T-1,0):D=S(T-1,1):E=S(T-1,2):S(T-1,0)=S(T,0):S(T-1,1)=S(T,1):S(T-1,2)=S(T,2):S(T,0)=C:S(T,1)=D:S(T,2)=E
5034 I$=NM$(T-1):NM$(T-1)=NM$(T):NM$(T)=I$
5035 NEXTT
5036 PRINT@426,"PLAY AGAIN?";
5040 I$=INKEY$:IFI$=""THEN5040
5050 IFI$="Y"THEN110
5060 IFI$="N"THENEND
5070 GOTO5040
6000 CLS4
6001 PRINT"¿¿¿¿¼¼¼½¿¿¿¼¼¼¼¼¼¼¿¿¿¾¼½¿¿¿¿¿¿¿¿";:PRINT"¿¿¼€€€€µ¿¿¿€€€€€€€¿¿¿º€€€€€´¼¼¿¿";
6003 PRINT"¿¸€€€€€µ¿¿¿€€€€€€€¿¿¿º€€€€€€€€€µ";:PRINT"¾€€€€€€´¿¿¿€€€€€€€µ¿¿¸€€€€€€€€€µ";
6005 PRINT"º€€€€€€€¿¿º€€€€€€€µ¿¿€€€€€€€€€€µ";:PRINT"º€€€€€€€½¿º€€€€€€€€¿¿€€€€€€€€€€µ";
6007 PRINT"º€€€€€€€µ¿¸€€€€€€€€¿º€€€€€€€€€€µ";:PRINT"º€€€€€€€´¿€€€€€€€€€µº€€€€€€€€€€µ";
6009 PRINT"º€€€€€€€€¿€€€€€€°€€µ¸€€€€€€€€€€µ";:PRINT"º€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€µ";
6011 PRINT"º€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€·";:PRINT"º€€€€€€big€red€got€you€€€€€€€°·¿";
6013 PRINT"º€€€€€€€€€€€€€€€€€€€€€€€€€€€·¿¿¿";:PRINT"»€€€€€€€€€€€€€€€€€€€€€€€€€±¿¿¿¿¿";
6015 PRINT"¿»³²€€€€€€€€€€€€€€€€€€€€±·¿¿¿¿¿¿";:PRINT"¿¿¿¿¿»³³³³³³³³³³³³³³³³·¿¿¿¿¿¿¿¿";:POKE16759,33:RETURN
