0 DIMN(21),CC,C1,J,R,H:M=16448:CLS:PRINT"+*+*+*+*+* RORD RUNNER +*+*+*+*+        BY PHILIP OLIVER"
1 CC=1:FORC1=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):CC=CC+1
2 NEXT:GOTO5
4 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
5 GOSUB13
6 GOSUB39:REM "V" SHAPE TO STOP CARS ESCAPING
7 FORL=1TO9999999:GOSUB23:GOSUB19:NEXT:IFS>0THEN6
10 GOSUB33
11 GOSUB31
12 GOTO5
13 C=0:BE=1:D1=12:D=18:R=6:A$="¯¯":CC=RND(-(PEEK(9)*256+PEEK(10))):POKE3,0:POKE16925,0:POKE16926,1
14 P=16946:Q=16949:INPUT"HOW MANY LIVES WOULD YOU LIKE   (1-9)";LE
15 IFLE>9ORLE<1ORLE<>INT(LE)THEN14
16 FORZ=0TOLE:H=C(Z):NEXTZ:PRINT:PRINT"USE A & D KEYS TO STEER":REM SET HI SCORE
17 S=LE:M$="A2B2C2D2B2C2D2E2C2D2E2F2D2E2F2G2":GOSUB4
18 RETURN
19 J=J-((PEEK(2)AND1ANDNOTPEEK(P))ORPEEK(3)=70ANDJ>.)+((PEEK(2)AND1ANDNOTPEEK(Q))ORPEEK(3)=74ANDJ<31)
20 IFPOINT(J*2,4)>.THENSOUND200,2:S=S-1:L=9999999:RETURN
21 POKEM+J,48+S:RETURN
23 CC=RND(3)-2:R=R+CC*-(R+CC>1ANDR+CC<D1):PRINT@R+480,A$;:PRINT@RND(D-1)+481+R,"ÿ";:PRINT@R+D+479,A$
27 C=C+10:IFINT(C/1000)=C/1000THEND=D-1:IFINT(C/10000)=C/10000THENGOSUB59
28 PRINT@.,C:IFC>HTHENIFBE=1THENPRINT@0,;:M$=STR$(C):GOSUB48:M$="A2B2C2D2E2F2G2F2E2D2C2B2A2":GOSUB4:BE=0
30 RETURN
31 PRINT:PRINT:INPUT"WOULD YOU LIKE ANOTHER GAME";M$
32 ON-(LEFT$(M$,1)="N")GOTO51:RETURN
33 PRINT"TOUGH LUCK! ";:M$="YOU'RE DEAD":GOSUB48:PRINT
34 PRINT"+*+*+*+* HIGH SCORE *+*+*+*+*+*"
35 FORZ=1TO9
36 IFLE=ZTHENIFC>C(Z)THENC(Z)=C:REM UPDATING HI SCORE
37 PRINT"LEVEL"Z"="C(Z):NEXTZ
38 RETURN
39 IFD<8THEND=8
40 CLS:J=D1+0.5*D:FORL=1TO15:V=R:IFL<=RTHENV=L
42 PRINT@V+480,A$;:V1=V+D:IF-L+30>V+DTHENV1=30-L
44 PRINT@V1+478,A$:IFL>45THENGOSUB19
46 NEXT
47 RETURN
48 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC1=1TOLEN(M$):POKECC+C1,ASC(MID$(M$,C1))AND63:NEXT:?@CC-16384+C1,:RETURN
51 END
59 M$="A2B2C2B2C2D2C2D2E2D2E2F2E2F2G2":GOSUB4:S=S+1:RETURN
60 REM RORD RUNNER BY PHILIP OLIVER
62 REM FOR THE DRAGON 32
63 REM MC-10 EDITS J GERRIE
