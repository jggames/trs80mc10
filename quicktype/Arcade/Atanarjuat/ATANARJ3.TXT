0 CLS:POKE16925,0:POKE16926,1:L=RND(-(PEEK(9)*256+PEEK(10))):L=4:GOTO400
20 I=PEEK(2)ANDPEEK(17023):IFI>32THENA=(I=65)-(I=83):B=(I=87)-(I=90):IFR=1THENR=0:GOTO22
21 RESET(X,Y):IFI=32THENR=1
22 IFPOINT(X+A,Y+B)THENA=0:B=0
23 X=X+A:Y=Y+B:SET(X,Y,2):A=0:B=0:
24 E=-(POINT(C+1,D)=0):IFE=0THENT=F:F=-(POINT(C,D+F)=0)*F:IFF=0THENF=-T
25 T=F*-(E=0):SET(C,D,2):RESET(C,D):C=C+E:D=D+T:E=0:SET(C,D,7):IFX<60ANDC<60THEN20
30 SET(X,Y,3):PRINT@0,;
31 IFX=60THENPRINT"YOU WIN!":V=V+1
32 IFC=60THENPRINT"HE WINS.":V1=V1+1
33 SC=SC+57+(X-C):PRINT@16,"SCORE";SC
34 PRINT@480,"YOU"V;TAB(12)"HIM"V1;TAB(25)"HIT c ";:IFG=5THEN300
40 I$=INKEY$:IFI$<>"C"THEN40
50 G=G+1:SOUND100,1
100 CLS0:FORX=2TO60:SET(X,2,2):SET(X,28,2):NEXT
110 FORY=2TO28:SET(2,Y,2):SET(60,Y,2):NEXT
120 FORX=4TO58STEP2:FORY=2TO28:IFRND(L)-1THENSET(X,Y,2)
130 NEXT:RESET(X,2+RND(25)):NEXT:RESET(60,2+RND(25))
200 X=3:Y=3:C=3:D=27:E=0:F=-1:T=-1:GOTO20
300 PRINT@0,"HIGH"HS"  ";:IFSC>HSTHENHS=SC:FORG=1TO95STEP5:SOUNDG,1:NEXT:FORG=1TO10:SOUND100,1:NEXT
310 PRINT@0,"HIGH"HS"  ";:SOUND1,2:G=0:V=0:V1=0:SC=0:POKE49151,64:GOTO40
400 CLS:PRINT"atanarjuat: THE FAST RUNNER"
410 PRINT"        BY: JIM GERRIE":PRINT
450 PRINT"YOU MUST RUN 5 RACES."
460 PRINT"YOUR GOAL FOR EACH RACE IS"
470 PRINT"TO REACH THE EXIT ON THE"
475 PRINT"RIGHT SIDE OF THE MAZE"
480 PRINT"BEFORE YOUR OPPONENT DOES."
490 PRINT"BUT DON'T DESPAIR IF HE GETS"
500 PRINT"THERE FIRST--YOU EARN HIGHER"  
510 PRINT"POINTS THE SHORTER THE DISTANCE"
520 PRINT"BETWEEN YOU AND HIM--SO TRY" 
530 PRINT"YOUR BEST. USE awsz TO MOVE."
535 PRINT"HIT space TO LEAVE A BLOCK."
540 PRINT
550 PRINT"HIT ANY KEY TO BEGIN";
560 IFINKEY$=""THEN560
570 GOTO50
600 REM JIM GERRIE 2017
610 REM FOR THE TRS-80 MC-10
