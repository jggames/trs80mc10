0 CLS:CLEAR750:DIMC1,C2,M$:GOTO10
1 C1=1:C2=32
2 IFMID$(M$,C2,1)<>""ANDMID$(M$,C2,1)<>" "THENC2=C2-1:GOTO2
3 PRINTMID$(M$,C1,C2-C1):C1=C2+1:C2=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 C1=RND(-(PEEK(9)*256+PEEK(10)))
20 A$="ÌÌÌÌÌÌÌÍÍÌÍÌÍÌÍÌÍÌÍÌÍÌÍÌÎÌÌÌÌÌÌÌ"
30 B$="ÎÀÀÀÀÀÀÀÀÀÀÀÀÀÀÀÀÍ"
40 L$(1)="Ó“ÃÓ“ÃÓ—ÇÓ—Ã×“ÇÓ—Ã×“ÇÓ—ÃÛ“ÃÓ“ÃÓ“"
50 L$(2)="“ÃÓ“ÃÓ“Ç×“ÇÓ—Ã×“ÇÓ—Ã×“ÇÓ›ÃÓ“ÃÓ“Ã"
60 L$(3)="ÃÓ“ÃÓ“Ã×—Ã×“ÇÓ—Ã×“ÇÓ—Ã×“ËÓ“ÃÓ“ÃÓ"
70 DIMS(8,8),G(8,8),Z(8,8),C(9,2),K(3,3),D$(8),B$(30),C$(3)
80 Z$="                ":Y$="  ............. ":X$="       ":V$="ENTERPRISE":U$="FEDERATION":CO$="##,##" 
90 CLS:PRINT@458,"MC SPACE TREK"TAB(40)"BY JAKE COMMANDER";:FORZZ=1TO254:BL$=BL$+" ":NEXT
100 C$(0)="DOCKED ":C$(1)=CHR$(132)+"ŒŒŒŒŒ"+CHR$(136):C$(2)=CHR$(148)+"œœœœœ"+CHR$(152):C$(3)=CHR$(180)+"¼¼¼¼¼"+CHR$(184)
110 A$(0)="ÀÀ":A$(1)=CHR$(237)+CHR$(232):A$(2)="ÀÀ":A$(3)=CHR$(220)+CHR$(214):A$(4)=CHR$(192)+CHR$(146)
111 A$(5)=CHR$(166)+CHR$(169)
120 FORY=1TO2:FORX=1TO9:READC(X,Y):NEXTX,Y:FORX=1TO8:READD$(X):NEXT:FORX=1TO30:READB$(X):NEXT 
130 GOSUB1370:GOSUB1570:T=(RND(20)+19)*100+RND(100):T0=T+1:TT=32:GOSUB1520:L=RND(8):M=RND(8):U=RND(8):V=RND(8)
131 PRINT@320,"AWAIT FURTHER ORDERS." 
140 DATA,1,1,1,,-1,-1,-1,,-1,-1,,1,1,1,,-1,-1
150 DATAWARP ENGINES,LOCAL SCANNER,REMOTE SCANNER,PHASER CONTROL,PHOTON TUBES,SHIELD CONTROL,DAMAGE CONTROL,COMPUTER
151 DATASET,COURSE,LOCAL,SCANNER
160 DATAREMOTE,SCANNER,FIRE,PHASERS,PHOTON,TORPEDO,SHIELD,CONTROL,DAMAGE,REPORT,ONBOARD,CMPUTER
161 DATACOMMAND,MODE,CMPUTER,GUIDNCE,STATUS,REPORT,TORPEDO,DATA,COMPUTE,COURSE,GUIDED,TORPEDO,MISSION,RECORD 
170 FORJ=1TO8:FORI=1TO8:R=RND(100):IFR>96THENK=3:GOTO180
171 IFR>90THENK=2:GOTO180
172 IFR>75THENK=1:GOTO180
173 K=0
180 KT=KT+K:R=RND(100):IFR<98THENB=0:GOTO190
181 B=1:BT=BT+1
190 S=RND(8):G(I,J)=K*100+B*10+S:Z(I,J)=0:NEXTI,J:K7=KT:IFBT=0THENI=RND(8):J=RND(8):G(I,J)=G(I,J)+10:BT=1
200 IFKT=0THEN170
201 GOSUB520:PRINT@320,"YOU HAVE 32 STARDATES TO DESTROYFLEET OF";
202 PRINTSTR$(KT)" KLINGONS. THERE ARE"STR$(BT)" STARBASES IN THIS GALAXY.":GOSUB1350
210 CL=0:K=0:B=0:S=0:S(U,V)=3:IFL<1ORL>8ORM<1ORM>8THEN300
220 X=G(L,M):K=INT(X/100):X=X-K*100:B=INT(X/10):S=X-B*10
230 IFK=0THEN270
231 IFG>200THEN260
232 IFZ9=1ORZ7=1THEN250
240 PRINT@320,;
241 M$="YOU HAVE ENTERED A COMBAT AREA WITH YOUR SHIELDS LOW. THIS TIME THEY DIDN'T SHOOT. BE WARNED!":GOSUB1:Z9=1:GOTO260
250 M$="COMBAT AREA... CONDITION RED-- SHIELDS ARE DANGEROUSLY LOW.":GOSUB1
260 GOSUB1330
270 IFB<>0THENFORI=1TOB:GOSUB1310:S(X,Y)=5:NEXT 
280 IFS<>0THENFORI=1TOS:GOSUB1310:S(X,Y)=4:NEXT 
290 ST=4:GOSUB1420
300 PRINT@128,STR$(INT(L))","RIGHT$(STR$(INT(M)),2);:PRINT@192,STR$(INT(U))","RIGHT$(STR$(INT(V)),2);
301 IFCL=1THENPRINT@320,;:M$="SENSORS DETECT KLINGONS CLOAKING-- PHASERS NECESSARY":GOSUB1:GOSUB1640
310 IFD(2)<0THENO=0:A=2:GOTO370
311 O=1:Z=38:FORY=1TO8:FORX=1TO8:PRINT@X*2+Z,A$(S(X,Y));:NEXTX:Z=Z+32:NEXTY
320 IFK=0THEN330
321 IFZ7=1THENIFRND(2)>1THENGOSUB1020:GOTO330
322 Z7=1 
330 A=0:GOSUB380:PRINT@217,"CMD?  ";:M2=0
340 H=0 
350 GOSUB390:IFA$=CHR$(13)THEN360
351 IFA=0ORA>8THEN350
352 PRINT@249,B$(A*2-1);:PRINT@281,B$(A*2);:GOTO350 
360 PRINT@320,;:IFA<3ORD(A)>=0THENONA+1GOTO330,450,300,710,770,850,980,1000,1100,330
370 PRINT@320,D$(A);"-- INOPERATIVE":GOSUB1640
371 IFA=4ANDCL=1THENM$="ILLOGICAL TO REMAIN HERE, KLINGONS CLOAKING":GOSUB1:GOTO330
372 GOTO330 
380 PRINT@249,X$;:PRINT@281,X$;:RETURN 
390 A$=INKEY$:IFA$=""THEN420
400 IFA$=CHR$(13)THENPRINT@320,;:GOTO1640 
410 A=VAL(A$):GOSUB380:PRINT@221,A;:RETURN
420 GOSUB1650:H=H+1:IFH<280THEN390 
430 H=0:GOSUB1520:PRINT@320,;:GOSUB490:IFK=0THEN435
431 IFRND(2)>1THENGOSUB1020:GOTO340
432 GOTO340
435 IFRND(10)<9THEN340
436 GOSUB1520:IFL<1ORL>8ORM<1ORM>8THEN340
437 FORN=1TO8:FORQ=1TO8:X=INT(G(N,Q)/100):IFX=0THENNEXTQ,N
440 G(N,Q)=G(N,Q)-X*100:G(L,M)=G(L,M)+X*100:K=X
441 M$="KLINGONS HAVE JUST ENTERED THIS QUADRANT":GOSUB1:GOSUB1640:GOSUB1330:GOSUB1420:GOTO300 
450 PRINT@320,;:INPUT"COURSE(1-9)";C:IFC=0THEN330
451 IFC<1ORC>9THEN450
452 PRINT@253,RIGHT$(STR$(INT(C)),1);
453 IFC-INT(C)>0THENPRINT".";MID$(STR$(C-INT(C)),3,1);:IFC=9THENC=1
460 PRINT@320,;:INPUT"WARP FACTOR (0-8)";W:IFW=0THEN330
461 IFW<0ORW>8THEN460
470 IFD(1)<0ANDW>.2THENM$=D$(1)+" DAMAGED, MAX. SPEED IS WARP 0.2":GOSUB1:GOSUB1350:W=.25:GOTO480
471 IFW<.9THENW=W*1.25
480 GOSUB490:GOTO560
490 IFST=0THENRETURN
491 R=0:FORI=1TO8:IFD(I)>=0THENNEXT:GOTO500
492 D(I)=D(I)+1:IFD(I)=0THENR=I:I=8
493 NEXT
500 IFD(7)<0THENRETURN
501 PRINT@320,;:IFR<>0THENGOSUB520:M$=D$(R)+"-- NOW FUNCTIONAL.":GOSUB1:GOTO550
510 IFRND(10)<9THENRETURN
511 R=RND(8):ONRND(2)GOTO530,540 
520 SOUND240,9:SOUND236,7:RETURN 
530 D(R)=D(R)-RND(5):IFD(R)<0THENGOSUB520:M$=D$(7)+" REPORT:":GOSUB1:M$=D$(R)+" OUT":GOSUB1:GOTO550
531 RETURN 
540 D(R)=D(R)+RND(5):GOSUB520:M$=D$(7)+" REPORT:":GOSUB1:M$=D$(R)+"-- UPGRADED":GOSUB1
550 GOSUB1640:GOSUB1350:GOTO1500
560 GOSUB1020:N=W*8:F=E/(5*(G/100+1)):IFN<=FTHENF=0:GOTO570
561 N=F:F=1 
570 X=U:Y=V:S(X,Y)=0
580 GOSUB650:FORH=1TON:GOSUB660:ONZGOTO590,595,594 
590 NEXTH:IFF=0THEN640
591 H=H-1:GOTO620
594 TM=H:H=N:NEXTH:H=TM:GOTO680
595 TM=H:H=N:NEXTH:H=TM
600 M$="":IFM2<>0THENM$="SHIP HAS SUSTAINED DAMAGE. ":FORI=1TO8:D(I)=D(I)-RND(M2+1)+1:NEXT:IFM2=5THEN630
610 M$=M$+"NAVIGATIONAL ERROR: ":GOTO630
620 GOSUB1460:M$="ENERGY DEPLETED-- "
630 X=X-X1:Y=Y-Y1:N=H-1:M$=M$+"ENGINES SHUT DOWN AT SECTOR"+STR$(INT(X))+","+RIGHT$(STR$(INT(Y)),2):GOSUB1
631 GOSUB1350:GOSUB1640:GOSUB1350
640 U=INT(X+.5):V=INT(Y+.5):S(U,V)=3:GOSUB1390:IFW<1THEN300
641 GOSUB1520:GOTO300
650 D=INT(C):X1=C(D,1)+(C(D+1,1)-C(D,1))*(C-D):Y1=C(D,2)+(C(D+1,2)-C(D,2))*(C-D):RETURN 
660 X=X+X1:Y=Y+Y1:C=INT(X+.5):D=INT(Y+.5):IFC<1ORC>8ORD<1ORD>8THENZ=3:RETURN
661 IFS(C,D)<>0THENZ=2:M2=RND(2)-1:RETURN
670 Z=1:RETURN
680 FORJ=1TO8:FORI=1TO8:S(I,J)=0:NEXTI,J:U=L*8+U+X1*N:V=M*8+V+Y1*N:L=INT(U/8):M=INT(V/8):U=INT(U-L*8+.5):V=INT(V-M*8+.5)
681 IFU=0THENL=L-1:U=8
690 IFV=0THENM=M-1:V=8
700 GOSUB1520:GOSUB1390:GOTO210
710 O=0:A=40:PRINT@A,Y$;:FORJ=M-1TOM+1:FORZ=1TO3:N(Z)=0:NEXT
720 FORI=L-1TOL+1:IFI<1ORI>8ORJ<1ORJ>8THEN750 
730 N(I-L+2)=G(I,J):IFD(8)<0THEN750
740 Z(I,J)=G(I,J)
750 NEXT
760 A=A+32:PRINT@A,"  :"RIGHT$("00"+MID$(STR$(INT(N(1))),2),3)":";
761 PRINTRIGHT$("00"+MID$(STR$(INT(N(2))),2),3)":";
762 PRINTRIGHT$("00"+MID$(STR$(INT(N(3))),2),3);": ";
764 A=A+32:PRINT@A,Y$;:NEXT:A=A+32:PRINT@A,Z$;:GOTO330
770 IFST=0THENPRINTC$(0);:GOTO370
771 IFK=0THENGOSUB1620:GOTO330
780 GOSUB1500:Z7=1:M$="":IFD(8)<0THENM$=D$(8)+" FAILURE-- MANUALLY "
790 M$=M$+"LOCKED ON. ENERGY="+STR$(INT(E)):GOSUB1
800 INPUT"UNITS TO FIRE";F:IFF<=0THEN330
801 IFF>ETHEN780
810 PRINT@320,;:E=E-F:GOSUB1400:IFD(8)<0THENF=RND(F)
820 R=320:FORI=1TO3:IFK(I,3)<0THEN840
830 H=F/K:J=.98:GOSUB1320:K(I,3)=K(I,3)-H:PRINT"KLINGON-- ":GOSUB1640
831 IFK(I,3)<0THENX=K(I,1):Y=K(I,2):GOSUB1560:GOTO840
832 PRINT"AT SECTOR"STR$(INT(K(I,1)))","RIGHT$(STR$(INT(K(I,2))),2)" ("+MID$(STR$(INT(K(I,3))),2,3)+" LEFT)":GOSUB1350
840 NEXT:GOSUB1020:GOTO330
850 IFP>0THEN860
851 PRINT"ALL PHOTON TORPEDOES EXPENDED.":GOTO330
860 IFCL<>0THENGOSUB1630:GOTO330
870 PRINT@320,B$(10);:INPUT" COURSE (1-9)";C:IFC=0THEN330
871 IFC<1ORC>9THEN870
872 IFC=9THENC=1
880 Z7=1:GOSUB1530:GOSUB650:X=U-X1:Y=V-Y1:GOSUB1690:GOTO910
890 PRINT@Q,CHR$(214);:GOTO910
900 PRINT@Q,CHR$(128);
910 GOSUB660:IFZ=3THEN960
911 IFY=INT(Y)ANDO=1THENGOSUB1600:PRINT@Q,CHR$(159);:ONS(C,D)+1GOTO900,920,900,890,940,950:STOP
912 ONS(C,D)+1GOTO910,920,910,910,940,950
920 FORI=1TO3:IFC=K(I,1)ANDD=K(I,2)THENK(I,3)=-1:X=C:Y=D:R=320:GOSUB1560
930 NEXT:GOTO970
940 PRINT@352,;:M$=B$(10)+" CAPTURED BY STAR GRAVITY AT SECTOR"+STR$(INT(X))+","+RIGHT$(STR$(INT(Y)),2):GOSUB1
941 GOSUB1350:GOTO970
950 GOSUB1580:PRINT@320,;:M$="WELL DONE! STARBASE DESTROYED!":GOSUB1:B=B-1:BT=BT-1:M2=4:X=C:Y=D:GOSUB1550
951 PRINT@352,;:M$="DAMAGE HAS THROWN YOU OFF COURSE":GOSUB1:C=RND(8):W=RND(8)/2:GOTO560
960 PRINT@352,B$(10);" MISSED.":GOSUB1350
970 GOSUB1020:GOTO300
980 IFST=0THENPRINTC$(0);:GOTO370
990 PRINT@320,;:INPUT"TOTAL UNITS ON SHIELDS";X:IFX<0ORX>E+GTHEN990
991 E=E+G-X:G=X:GOSUB1400:GOSUB1410
992 PRINT@352,"ENERGY CONSUMPTION IS NOW";5*(G/100+1):PRINT"UNITS PER SECTOR":GOSUB1640:GOTO330
1000 R=0:PRINT@320,"NEXT...HIT '^'  "; 
1010 A$=INKEY$:IFA$=""THEN1010
1011 IFASC(A$)=13THEN330
1012 IFA$<>"^"ANDA$<>"W"ANDA$<>" "THEN1010
1013 R=R+1:IFR>8THEN330
1014 GOSUB1510:GOTO1010 
1020 IFK=0THENRETURN
1021 IFST=0THEN1025
1022 R=320:FORI=1TO3:H=K(I,3):IFH<1THENNEXT:GOTO1410
1023 J=1:GOSUB1320:PRINTV$:M$="FROM SECTOR"+STR$(INT(K(I,1)))+","+RIGHT$(STR$(INT(K(I,2))),2):GOSUB1640:GOSUB1
1024 G=G-H:GOSUB1360:NEXT:GOTO1410
1025 Z7=0:PRINT@384,;:M$="STARBASE SHIELDS PROTECT THE "+V$:GOSUB1:RETURN
1040 M$="THE "+V$+" IS DEAD IN SPACE. YOU ARE AT THE KLINGONS' MERCY":GOSUB1:FORZ=1TO6:GOSUB1350:NEXTZ
1050 IFK=0THEN1080
1051 GOSUB1020:GOTO1050
1060 PRINT"IT IS STARDATE "STR$(T)".":GOSUB1640:GOTO1080
1070 PRINT@320,"THE "V$" WAS DESTROYED."
1080 PRINT"THE ";U$;" WILL BE CONQUERED";:PRINT"THERE ARE STILL";KT;"KLINGON":PRINT"BATTLE-CRUISERS SURVIVING.":END
1090 PRINT@320,"LAST BATTLE-CRUISER DESTROYED!":PRINT"THE ";U$;" IS SAVED!!"
1091 PRINT"YOUR EFFICIENCY RATING ="INT(K7/(T-T0)*1000):END
1100 CD=0
1110 A=0:GOSUB380:PRINT@217,"OPT?  ";
1120 GOSUB390:IFA>6THEN1120
1130 IFA$=CHR$(13)THENONA+1GOTO330,1290,1150,1160,1170,1280,1140:GOTO1120
1131 PRINT@249,B$(A*2+17);:PRINT@281,B$(A*2+18);:GOTO1120
1140 A=0:FORJ=1TO8:FORI=1TO8:CC=INT(Z(I,J)/100):O=INT((Z(I,J)-CC*100)/10)
1141 POKE16424+A+(I-1)*2,CC+112:POKE16425+A+(I-1)*2,Z(I,J)-CC*100-O*10+112-64*O:NEXT:A=A+32:NEXT:O=0:GOTO1110
1150 O=0:FORA=40TO266STEP32:PRINT@A,Y$;:NEXT:PRINT@41,"STATUS REPORT:";:PRINT@106,"KLINGONS ="KT;
1151 PRINT@138,"STARDATES="INT(T0+TT-T);:PRINT@170,"STARBASES="BT;:GOTO1110
1160 IFK=0THENGOSUB1620:GOTO1110
1161 CC=U:A=V:FORF=1TO3:IFK(F,3)<0THEN1270
1162 TM=F:F=3:NEXT:F=TM:W=K(F,1):X=K(F,2):GOTO1180
1170 PRINT@384,"ENTER START AND END CO-ORDINATES":PRINT@320,;:INPUT"(X,Y,X,Y)";CC,A,W,X
1171 IFCC-W=0ANDA-X=0THENCC=0:X=0:A=0:GOTO1260
1180 PRINT@352,;:X=X-A:A=CC-W:IFX<0THEN1220
1181 IFA<0THEN1240
1182 IFX>0THEN1190
1183 IFA=0THEN1230
1190 CC=5
1200 IFABS(A)<=ABS(X)THEN1210
1201 CC=CC+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO1260
1210 CC=CC+ABS(A)/ABS(X):GOTO1260
1220 IFA>0THENCC=7:GOTO1250
1221 IFX=0THEN1240
1230 CC=1:GOTO1200
1240 CC=3
1250 IFABS(A)>=ABS(X)THENCC=CC+ABS(X)/ABS(A):GOTO1260
1251 CC=CC+(((ABS(X)-ABS(A))+ABS(X))/ABS(X))
1260 PRINT@352,"COURSE =";STR$(CC);:CD=INT(SQR(X^2+A^2)):PRINT", DISTANCE =";CD:GOTO1110
1270 NEXT:GOTO1110
1280 IFD(5)<0THEN370
1282 IFP<1ORCL=1THEN850
1283 IFCD<>0THENC=CC:GOTO880
1284 GOTO1300
1290 IFCD<>0THENC=CC:W=CD:GOTO470
1300 M$="NO DATA AVAILABLE FOR GUIDANCE SYSTEM":GOSUB1:GOTO1110 
1310 X=RND(8):Y=RND(8):IFS(X,Y)<>0THEN1310
1311 RETURN
1320 H=(H*2/SQR((K(I,1)-U)^2+(K(I,2)-V)^2))*(J*RND(0)+.2)
1321 PRINT@R,MID$(STR$(INT(H)),2,3)+" UNIT HIT ON ";:GOSUB1640:R=R+32:RETURN
1330 IFK<>3THENIFRND(10)>7THENCL=1 
1340 FORI=1TO3:K(I,3)=-1:NEXT:FORI=1TOK:GOSUB1310:S(X,Y)=CL+1:K(I,1)=X:K(I,2)=Y:K(I,3)=200:NEXT:RETURN 
1350 FORZZ=1TO1500:NEXT:RETURN 
1360 FORZZ=H*.25+1TO1STEP-1:POKE49151,64:SOUND255,1:NEXT:RETURN 
1370 PRINT@0,A$;:FORX=1TO8:PRINTTAB(7)B$:NEXT:PRINTL$(1);
1380 PRINT@33,"DATE";:PRINT@97,"QUAD";:PRINT@90,"FUEL";:PRINT@161,"SECT";:PRINT@154,"SHLD";:PRINT@225,"TORP";:GOTO1500 
1390 E=E-((N*5)*(G/100+1))
1400 PRINT@121,LEFT$(STR$(INT(E)),5):GOSUB1420 
1410 PRINT@185,LEFT$(STR$(INT(G)),5):GOSUB1500:IFG<0THEN1070
1411 RETURN 
1420 FORZ=U-1TOU+1:IFZ<1ORZ>8THENNEXT:GOTO1430
1421 IFS(Z,V)=5THENZ=U+1:GOSUB1610:NEXT:GOTO1470
1422 NEXT
1430 IFK=0THEN1450
1440 IFST<>3THENST=3:GOTO1480
1441 GOTO1470
1450 IFE>300THENST=1:GOTO1470
1451 IFE>5*(G/100+1)THEN1455
1452 GOTO1460
1455 IFST<>2THENST=2:GOTO1480
1456 GOTO1470 
1460 PRINT@320,;:IFG<=0THEN1040
1461 M$="YOU HAVE"+STR$(INT(E))+" UNITS OF ENERGY. SHIELDS ARE TAKING"+STR$(INT(G))+" UNITS":GOSUB1:GOSUB1350
1462 IFD(6)<0THENM$="BUT, "+D$(6)+" IS OUT. SITUATION IRRECOVERABLE.":GOSUB1:GOTO1040
1463 GOTO1440
1470 PRINT@57,C$(ST);:GOTO1500
1480 FORFL=1TO5:PRINT@57,C$(ST);:FORZZ=1TO75:NEXT:PRINT@57,C$(1);:FORZZ=100TO50STEP-4:SOUNDZZ,1:NEXTZZ
1481 IFST=0THENSOUND200,1:GOTO1490
1482 SOUND1,1 
1490 NEXTFL:GOTO1470
1500 PRINT@320,;:RETURN
1510 GOSUB1640:PRINT@320,D$(R);TAB(14),-D(R);:GOTO1500
1520 T=T+.75:PRINT@64,INT(T);:GOSUB1500:IFT>T0+TTTHEN1060
1521 RETURN 
1530 IFST=0THENP=10:GOTO1540
1531 P=P-1 
1540 PRINT@257,P;:GOTO1500
1550 GOSUB1580:S(X,Y)=0:G(L,M)=K*100+B*10+S:RETURN
1560 K=K-1:IFK<0THENK=0:SOUND100,20
1561 GOSUB1550:GOSUB1420:PRINT@R,"KLINGON AT"STR$(INT(X))","RIGHT$(STR$(INT(Y)),2)" DESTROYED";
1562 R=R+32:GOSUB1640:KT=KT-1:IFKT<=0THEN1090
1563 GOSUB1350:IFK=0THENCL=0
1564 RETURN
1570 ST=0:FORZ=1TO8:D(Z)=0:NEXT:E=5000:PRINT@121,E;:GOSUB1530:G=0:GOTO1410
1580 IFO<>1THENRETURN
1581 GOSUB1600:FORZ=1TO40:ZZ=RND(10)*10:SOUNDZZ,1:PRINT@Q+RND(2)-2,CHR$(128+RND(62));:NEXT
1590 PRINT@Q-1,CHR$(128)CHR$(128);:GOTO1500
1600 Q=7.5+X*2+32*Y:RETURN
1610 IFST=0THENRETURN
1611 PRINT@320,"SHIELDS DROPPED FOR DOCKING":GOSUB1640:GOSUB1570:GOTO1480
1620 M$="SENSORS DETECT NO KLINGONS IN THIS QUADRANT":GOSUB1:RETURN
1630 M$=D$(5)+" WILL BE OF NO USE-- USE "+B$(8):GOSUB1
1640 ZZ=PEEK(17025):PRINTLEFT$(BL$,16895-(PEEK(17024)*256+PEEK(17025)));:POKE17025,ZZ:RETURN
1650 SC=SC+1:IFSC>3THENSC=1
1660 PRINT@288,L$(SC);:IFST>1THEN1680
1661 IFSO=0THEN1665
1662 GOTO1670
1665 IFRND(50)>49THENSO=16:GOTO1670
1666 RETURN
1670 SO=SO-2:IFSO<2THENSO=0:RETURN
1671 SOUND150+SO,1:RETURN
1680 SOUND200,5:RETURN
1690 SOUND25,1:FORZO=5TO3STEP-1:FORZN=12TO1STEP-1:SOUNDZO,1:NEXTZN,ZO:RETURN
1700 O=RND(5):N=RND(12):FORX=10TO30STEP10:SOUNDX,1:NEXT:RETURN
