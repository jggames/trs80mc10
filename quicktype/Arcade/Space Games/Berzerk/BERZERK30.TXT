1 CLS:GOSUB98:GOTO64
2 RETURN
3 G=Q:A=1:J=E(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO28,45,2,2,2:ONPOINT(J,K+2)GOTO28,45,2,2,2
4 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:X(T)=X(T)+1:?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);:RETURN
5 A=W:J=G(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO28,45,2,2,2:ONPOINT(J,K+2)GOTO28,45,2,2,2
6 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:X(T)=X(T)-1:?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);:RETURN
7 B=1:J=F(X(T)):K=E(Y(T)):ONPOINT(J,K)GOTO28,45,2,2,2:ONPOINT(J+2,K)GOTO28,45,2,2,2
8 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:Y(T)=Y(T)+1:?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);:RETURN
9 B=W:J=F(X(T)):K=G(Y(T)):ONPOINT(J,K)GOTO28,45,2,2,2:ONPOINT(J+2,K)GOTO28,45,2,2,2
10 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:Y(T)=Y(T)-1:?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);:RETURN
11 C=A:U=A*2:Z=B:V=B*2:R=X(T)*2+O(A+1):S=Y(T)*2+P(B+1):ONPOINT(R,S)GOTO27,30,27,27,27:SET(R,S,4):RETURN
12 RESET(R,S):ONPOINT(R+U,S+V)GOTO27,30,27,27,27:R=R+U:S=S+V:ONPOINT(R+C,S+Z)GOSUB2,30,2,2:SET(R,S,4):RETURN
13 RESET(L,M):ONPOINT(L+N,M+O)GOTO36,30,32,32,32,32,36:L=L+N:M=M+O:SET(L,M,7):RETURN
14 FORE=1TO50:FORT=1TO6:G=F:ONRND(R(T))GOTO16,16,16,16,26,26:A=.:B=.:ONSGN(X-X(T))+2GOSUB5,2,3:ONSGN(Y-Y(T))+2GOSUB9,2,7
15 IFR=1THENGOSUB11:GOTO17
16 IFR>1THENGOSUB12
17 IFLTHENGOSUB13
18 J=K(PEEK(P)ANDPEEK(2)):IFJ=9THENGOSUB40:NEXT:NEXT:GOTO48
19 IFJTHENGOSUB21:NEXT:NEXT:GOTO48
20 NEXT:NEXT:GOTO48
21 H=N(J):I=Q(J):D=L(J):J=F(X+H):K=F(Y+I):ONPOINT(J,K)GOTO33,22,33,42,44,33
22 ONPOINT(J+2,K)GOTO33,23,33,42,44,33
23 ONPOINT(J,K+2)GOTO33,24,33,42,44,33
24 ONPOINT(J+2,K+2)GOTO33,25,33,42,44,33
25 ?@A(X,Y),B$;:?@B(X,Y),B$;:X=X+H:Y=Y+I:?@A(X,Y),A$(D);:?@B(X,Y),B$(D);:RETURN
26 A=.:B=.:ONRND(4)GOSUB3,5,7,9:GOTO16
27 R=1:S=1:C=.:Z=.:RETURN
28 ?@A(X(T),Y(T)),B$(11);:?@B(X(T),Y(T)),B$(11);:SOUND1,1
29 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:R(T)=1:G=.:RETURN
30 RESET(R,S):SET(R+U,S+V,4):SOUND100,2:GOSUB27:?@A(X,Y),A$(D);:?@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2THEN34
31 RETURN
32 L=.:M=.:N=.:O=.:RETURN
33 GOSUB25:?@A(X,Y),B$(11);:?@B(X,Y),B$(11);:SOUND200,4:GOTO35
34 FORT=1TO5:?@A(X,Y),A$(10);:?@B(X,Y),B$(10);:SOUND200,1:?@A(X,Y),A$(9);:?@B(X,Y),B$(9);:SOUND100,1:NEXT
35 ?@480,"OUCH!";:SOUND200,4:E=65000:T=6:G=.:LF=LF-1:HT=.:EV=2:RETURN
36 SET(L+N,M+O,7):FORM=1TO6:ONR(M)GOTO38:J=2*X(M):K=2*Y(M)
37 IFPOINT(J,K)=7ORPOINT(J+2,K)=7ORPOINT(J,K+2)=7ORPOINT(J+2,K+2)=7THENGOSUB39
38 NEXT:GOSUB32:RETURN
39 ?@A(X(M),Y(M)),B$(11);:?@B(X(M),Y(M)),B$(11);:SOUND1,1:?@A(X(M),Y(M)),B$;:?@B(X(M),Y(M)),B$;:R(M)=1:SC=SC+100:RETURN
40 IFLTHENRETURN
41 N=H*2:O=I*2:L=X*2+O(H+1):M=Y*2+P(I+1):ONPOINT(L,M)GOTO36,30,32,32,32:SET(L,M,7):RETURN
42 GOSUB25:SET(R,S,4):SOUND100,2:GOSUB27:?@A(X,Y),A$(D);:?@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2THEN35
43 RETURN
44 GOSUB25:?@480,"YOU ESCAPED!";:SOUND100,3:SOUND50,3:SOUND1,3:SOUND1,10:FORT=1TO6:GOSUB58:NEXT:E=65000:T=6:EV=1:RETURN
45 ?@A(X,Y),B$(11);:?@B(X,Y),B$(11);
46 ?@A(X(T),Y(T)),B$;:?@B(X(T),Y(T)),B$;:Y(T)=Y(T)+B:X(T)=X(T)+A:?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);
47 SOUND200,4:GOTO35
48 IFE<65000THENFORT=1TO6:SOUND200,1:GOSUB29:NEXT:?@A(X,Y),A$(D);:?@B(X,Y),B$(D);:R(RND(6))=20:F=12:Q=12:GOTO14
49 IFEV=2THENIFLF>=.THENSOUND1,5:CLS0:GOTO74
50 IFEV=1ANDBN<>6THEN?@480,"CHICKEN! FIGHT LIKE A ROBOT!";:FORT=1TO10:SOUND101-(10*T),1:SOUND1,1:NEXT:GOTO52
51 IFEV=1THEN?@480,"LEVEL CLEARED.";:FORT=1TO10:SOUND100,1:SOUND200,1:NEXT:SC=SC+250
52 IFEV=1THEN?@480,"GET READY FOR NEXT LEVEL ...";:FORT=1TO10:SOUND10*T,1:NEXT:EN=NE:EX=EX(RND(3)):GOTO72
53 I$=INKEY$:RESET(2,0):?@480,"YOU ARE DEAD. PLAY AGAIN (Y/N)";:?@0,"SCORE:";SC;:?@16,"HIGH:";HS;
54 I$=INKEY$:IFI$=""THEN54
55 IFI$="Y"THEN62
56 IFI$="N"THENEND
57 GOTO54
58 IFR(T)=1THENBN=BN+1
59 RETURN
60 FORQ=1TO6:IFDR(EN,Q,0)=J(Y,0)THENIFDR(EN,Q,1)=I(X,0)THENQ=6:NEXT:GOTO75
61 NEXT:X(T)=I(X,0):Y(T)=J(Y,0):I(X,1)=1:J(Y,1)=1:NEXT:GOTO77
62 CLS0:LV=5:LF=2:M(1)=9:HT=.:EN=4:EX=RND(3):IFSC>HSTHENHS=SC
63 SC=.:GOTO73
64 DIMA$(13),B$(13),X(6),Y(6),R(6),O(2),P(2),K(255),A(31,14),B(31,14),E(64),F(64),G(64),H(64),I(12,1),J(6,1)
65 DIMT,D,X,Y,A,B,P,C,E,F,H,I,G,R,L,S,M,W,J,K,N,O,Q,U,V,Z,B$,M(12),N(8),Q(8),L(8),DR(4,6,1),EX(3):POKE16925,0:POKE16926,1
66 DIMLV,EV,SC,EN,EX,BN,SR,HS,XX,YY,CX,CY,XC,YC,PC,RR,HT,I$,A$:B$=CHR$(128)+CHR$(128):LV=8:LF=3:GOSUB103:GOSUB96
67 GOSUB162:FORT=.TO13:FORY=.TO3:READA$:FORX=1TO4:I=VAL(MID$(A$,X,1)):IFI>.THENSET(X-1,Y,I)
68 NEXT:NEXT:A$(T)=CHR$(PEEK(MC))+CHR$(PEEK(MC+1)):B$(T)=CHR$(PEEK(MC+32))+CHR$(PEEK(MC+33))
69 GOSUB99:NEXT:GOSUB100:GOSUB117:GOSUB100:W=-1:O(0)=-1:O(1)=3:O(2)=5:P(0)=-1:P(1)=1:P(2)=5:I=.:GOSUB161
70 FORT=1TO12:READI(T,0):NEXT:FORT=1TO6:READJ(T,0):NEXT:FOREN=1TO4:FORT=1TO6:READDR(EN,T,0),DR(EN,T,1):NEXT:NEXT
71 EN=4:EX=RND(3)
72 FORT=1TO12:READM(T):NEXT
73 IFM(1)=9THENLV=LV+3:LF=LF+1:RESTORE:FORT=1TO(14*4):READA$:NEXT:FORT=1TO12+6+(4*12):READC:NEXT:GOSUB151:GOTO72
74 FORT=1TO6:R(T)=LV:NEXT:FORT=1TO12:I(T,1)=.:NEXT:FORT=1TO6:J(T,1)=.:NEXT:FORT=1TO6
75 X=RND(12):Y=RND(6):IFI(X,1)=.THENIFJ(Y,1)=.THEN60
76 GOTO75
77 CLS0:FORX=1TO63:SET(X,1,3):SET(X,11,3):SET(X,21,3):SET(X,31,3):NEXT
78 FORY=1TO31:SET(1,Y,3):SET(17,Y,3):SET(33,Y,3):SET(49,Y,3):SET(63,Y,3):NEXT
79 IFLF>.THENFORT=1TOLF:SET(T*2,0,3):NEXT
80 IFHT>.THENSET(1,1,4)
81 GOSUB84:FORY=1TO30:SET(63,Y,3):NEXT:GOSUB91
82 Y=DR(EN,1,0):X=DR(EN,1,1):FORT=1TO6:G=RND(2):?@A(X(T),Y(T)),A$(G);:?@B(X(T),Y(T)),B$(G);:NEXT
83 ?@A(X,Y),A$(3);:?@B(X,Y),B$(3);:R=1:H=1:I=0:D=3:J=F(X+H):K=F(Y+I):F=1:Q=2:BN=0:GOSUB32:GOSUB27:GOTO14
84 C=1:FORY=1TO30STEP10:FORX=1TO62STEP16
85 T=M(C):C=C+1:ONTGOSUB86,88,89:NEXT:NEXT:RETURN
86 FORR=X+1TOX+15:IFR<64THENRESET(R,Y):NEXT:RETURN
87 NEXT:RETURN
88 FORS=Y+1TOY+9:RESET(X,S):NEXT:RETURN
89 GOSUB86:GOSUB88:IFPOINT(X,Y-1)=.THENIFPOINT(X-1,Y)=.THENRESET(X,Y)
90 RETURN
91 ONEXGOSUB92,93,94,95:RETURN 
92 FORX=18TO31:SET(X,1,5):NEXT:NE=4:EX(1)=1:EX(2)=2:EX(3)=3:RETURN
93 FORY=12TO18:SET(1,Y,5):NEXT:NE=3:EX(1)=1:EX(2)=2:EX(3)=4:RETURN
94 FORY=12TO18:SET(63,Y,5):NEXT:NE=2:EX(1)=1:EX(2)=3:EX(3)=4:RETURN
95 FORX=34TO46:SET(X,31,5):NEXT:NE=1:EX(1)=2:EX(2)=3:EX(3)=4:RETURN
96 FORX=0TO31:FORY=0TO14:A(X,Y)=X+Y*32:B(X,Y)=X+Y*32+32:NEXT:NEXT
97 FORX=0TO64:E(X)=X*2+5:F(X)=X*2+1:G(X)=X*2-1:H(X)=X*2+1:NEXT:RETURN
98 CLS:?"PLEASE WAIT...":RETURN
99 ?@0,B$;:?@32,B$;:RETURN
100 ?@483,"PRESS ANY KEY TO CONTINUE";
101 I$=INKEY$:T=RND(1000):IFI$=""THEN101
102 RETURN
103 CLS0:?@4,"THE ASTRO DATE IS 3200":A$="BERZERK":FORT=1TOLEN(A$):?@95+(T*32)+RND(3),MID$(A$,T,1);:NEXT
104 ?@32*1+4,"AND YOU ARE THE LAST"
105 ?@32*2+4,"SURVIVOR OF A SMALL GROUP"
106 ?@32*3+4,"OF EARTH PEOPLE WHO CAME"
107 ?@32*4+4,"TO EXPLORE THE PLANET"
108 ?@32*5+4,"MAZEON. SOON AFTER LANDING"
109 ?@32*6+4,"YOU DISCOVERED THE PLANET"
110 ?@32*7+4,"IS A DARK UNINHABITABLE"
111 ?@32*8+4,"PLACE. BUT BY THEN IT WAS"
112 ?@32*9+4,"TOO LATE TO TURN BACK"
113 ?@32*10+4,"BECAUSE YOUR SPACECRAFT"
114 ?@32*11+4,"HAD BEEN DESTROYED BY THE"
115 ?@32*12+4,"AUTO-MAZEONS. NOW YOU ARE"
116 ?@32*13+4,"A PRISONER HERE.":?@491,"BY JIM GERRIE";:RETURN
117 CLS0:?"YOU ARE TRAPPED IN A MAZE"
118 ?"WHERE EVEN THE WALLS ARE"
119 ?"DEATH TO TOUCH. GRIM ROBOT"
120 ?"THUGS STALK YOU RELENTLESSLY..."
121 ?@160,"USE:";
122 ?@224,"DIAGONAL";:?@234-32,"  UP/DOWN ";
123 ?@256,"  A S   ";
124 ?@288,"   Z X  ";
125 ?@234,"LEFT/RIGHT";
126 ?@266,"    I     ";
127 ?@298,"  J K L   ";
128 ?@246,"SPACE";
129 ?@278," TO  ";
130 ?@310," FIRE";
131 ?@352,"THE WHITE DOOR IS THE EXIT. YOU"
132 ?"GET AN EXTRA MAN FOR EVERY 10"
133 ?"LEVELS THAT YOU COMPLETE."
134 RETURN
135 XC=0:YC=RR:PC=3-2*RR 
136 GOSUB142
137 IFPC<.THENPC=PC+4*XC+6:GOTO139
138 PC=PC+4*(XC-YC)+10:YC=YC-1   
139 XC=XC+1:IFXC<YCTHEN136
140 IFXC=YCTHENGOSUB142 
141 RETURN
142 XX=CX+XC:YY=CY+YC:SET(XX,YY,6)
143 XX=CX-XC:YY=CY+YC:SET(XX,YY,6)
144 XX=CX+XC:YY=CY-YC:SET(XX,YY,6)
145 XX=CX-XC:YY=CY-YC:SET(XX,YY,6)
146 XX=CX+YC:YY=CY+XC:SET(XX,YY,6)
147 XX=CX-YC:YY=CY+XC:SET(XX,YY,6) 
148 XX=CX+YC:YY=CY-XC:SET(XX,YY,6)
149 XX=CX-YC:YY=CY-XC:SET(XX,YY,6)
150 RETURN
151 CLS0:IFSC=0THENGOSUB161:RETURN
152 IFSC<15000THENA$="THE HUMANOID MUST NOT ESCAPE!":SM=-1
153 IFSC>=15000ANDSC<30000THENA$="THE HUMANOID IS ABOUT TO ESCAPE":SM=0
154 IFSC>=25000THENA$="THE HUMANOID ESCAPED!":SM=-1
155 RR=5:CX=31:CY=15:GOSUB135:?@239,A$(12);:IFSMTHEN?@271,B$(12);:GOTO157
156 ?@239+32,B$(13);
157 ?@480+INT((32-LEN(A$))/2),;:FORT=1TOLEN(A$):?MID$(A$,T,1);:SOUND222,1:NEXT
158 FORT=1TO2000:NEXT
159 IFSC>=25000THEN?:?"DEDICATED TO P,M,C & N":END
160 RETURN
161 FORT=3TO4:CLST:?@233,"INTRUDER ALERT!";:SOUND50,2:SOUND5,2:SOUND1,2:SOUND1,2:SOUND1,2:NEXT:RETURN
162 P=17023:K(76)=1:K(83)=2:K(88)=3:K(74)=4:K(65)=5:K(90)=6:K(75)=7:K(73)=8:K(32)=9
163 N(1)=1:Q(1)=.:L(1)=3:N(2)=1:Q(2)=-1:L(2)=4:N(3)=1:Q(3)=1:L(3)=5:N(4)=-1:Q(4)=.:L(4)=6
164 N(5)=-1:Q(5)=-1:L(5)=7:N(6)=-1:Q(6)=1:L(6)=8:N(7)=.:Q(7)=1:L(7)=9:N(8)=.:Q(8)=-1:L(8)=9
165 MC=16384:RETURN
169 DATA "...."
170 DATA "...."
171 DATA "...."
172 DATA "...."
173 DATA "1111"
174 DATA "1.11"
175 DATA "1111"
176 DATA "1..1"
177 DATA "1111"
178 DATA "11.1"
179 DATA "1111"
180 DATA "1..1"
181 DATA ".2.."
182 DATA "2222"
183 DATA ".2.."
184 DATA "2.2."
185 DATA ".2.2"
186 DATA "222."
187 DATA ".2.."
188 DATA "2.2."
189 DATA ".2.."
190 DATA "222."
191 DATA ".2.2"
192 DATA "2.2."
193 DATA "..2."
194 DATA "2222"
195 DATA "..2."
196 DATA ".2.2"
197 DATA "2.2."
198 DATA ".222"
199 DATA "..2."
200 DATA ".2.2"
201 DATA "..2."
202 DATA ".222"
203 DATA "2.2."
204 DATA ".2.2"
205 DATA "..2."
206 DATA ".222"
207 DATA "..2."
208 DATA ".2.2"
209 DATA "..8."
210 DATA ".888"
211 DATA "..8."
212 DATA ".8.8"
213 DATA "4.4."
214 DATA ".4.4"
215 DATA "4.4."
216 DATA ".4.4"
217 DATA "6..6"
218 DATA "...."
219 DATA "6..6"
220 DATA ".66."
221 DATA "6..6"
222 DATA "...."
223 DATA ".66."
224 DATA "6..6"
225 DATA 1,3,5,9,11,13,17,19,21,25,27,29
226 DATA 1,3,6,8,11,13
227 DATA 1,11,1,9,1,13,3,9,3,11,1,13
228 DATA 6,1,6,3,6,5,8,1,8,3,8,5
229 DATA 6,29,6,25,6,27,8,25,8,27,8,29
230 DATA 13,19,11,17,11,19,11,21,13,17,13,21
231 DATA 0,0,2,2,1,2,3,2,1,0,3,2
232 DATA 0,2,2,2,0,3,2,1,1,2,2,3
233 DATA 0,2,2,0,0,1,3,3,1,3,2,3
234 DATA 0,2,0,0,0,3,3,3,1,1,2,3
235 DATA 0,2,2,2,0,2,3,3,1,3,2,2
236 DATA 0,2,0,2,0,3,3,3,1,1,2,1
237 DATA 0,2,2,2,1,1,3,1,1,3,3,3
238 DATA 0,2,2,2,1,0,2,1,1,3,3,3
239 DATA 0,2,2,0,0,3,1,3,1,3,1,3
240 DATA 9,9,9,9,9,9,9,9,9,9,9,9
