0 GOSUB10005:GOSUB10000:GOSUB420:CLEAR999:GOTO100
1 ONK(PEEK(135))GOSUB25,26,27,28,29,30,31,32,47:RETURN
2 RETURN
3 G=Q:A=1:J=E(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO34,51,2,2,2:ONPOINT(J,K+2)GOTO34,51,2,2,2:PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:X(T)=X(T)+1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
5 A=W:J=G(X(T)):K=F(Y(T)):ONPOINT(J,K)GOTO34,51,2,2,2:ONPOINT(J,K+2)GOTO34,51,2,2,2:PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:X(T)=X(T)-1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
7 B=1:J=F(X(T)):K=E(Y(T)):ONPOINT(J,K)GOTO34,51,2,2,2:ONPOINT(J+2,K)GOTO34,51,2,2,2:PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)+1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
9 B=W:J=F(X(T)):K=G(Y(T)):ONPOINT(J,K)GOTO34,51,2,2,2:ONPOINT(J+2,K)GOTO34,51,2,2,2:PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)-1:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:RETURN
10 A=0:B=0:ONRND(4)GOSUB3,5,7,9:GOTO18
11 C=A:U=A*2:Z=B:V=B*2:R=X(T)*2+O(A+1):S=Y(T)*2+P(B+1):ONPOINT(R,S)GOTO33,36,33,33,33:SET(R,S,4):RETURN
12 RESET(R,S):ONPOINT(R+U,S+V)GOTO33,36,33,33,33:R=R+U:S=S+V:ONPOINT(R+C,S+Z)GOSUB2,36,2,2:SET(R,S,4):RETURN
13 RESET(L,M):ONPOINT(L+N,M+O)GOTO42,36,38,38,38,38,42:L=L+N:M=M+O:SET(L,M,7):RETURN
14 FORE=1TO50:FORT=1TO6:G=F:ONRND(R(T))GOTO18,18,18,18,10,10:ONR GOSUB11:A=0:B=0:IFX-X(T)>0THENGOSUB3
15 IFX-X(T)<0THENGOSUB5
16 IFY-Y(T)>0THENGOSUB7
17 IFY-Y(T)<0THENGOSUB9
18 ON1-(R>1)GOSUB2,12:ON1-(L>0)GOSUB2,13:POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:POKE340,255:POKE339,255:POKE338,255:IF(PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341)ANDPEEK(340)ANDPEEK(339)ANDPEEK(338))<>255THENGOSUB1
19 NEXT:NEXT:GOTO60
20 J=F(X+H):K=F(Y+I):ONPOINT(J,K)GOTO39,21,39,48,50,39
21 ONPOINT(J+2,K)GOTO39,22,39,48,50,39
22 ONPOINT(J,K+2)GOTO39,23,39,48,50,39
23 ONPOINT(J+2,K+2)GOTO39,24,39,48,50,39
24 PRINT@A(X,Y),B$;:PRINT@B(X,Y),B$;:X=X+H:Y=Y+I:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:RETURN
25 H=1:I=0:D=3:GOTO20
26 H=1:I=W:D=4:GOTO20
27 H=1:I=1:D=5:GOTO20
28 H=W:I=0:D=6:GOTO20
29 H=W:I=W:D=7:GOTO20
30 H=W:I=1:D=8:GOTO20
31 H=0:I=1:D=9:GOTO20
32 H=0:I=W:D=9:GOTO20
33 R=1:S=1:C=0:Z=0:RETURN
34 PRINT@A(X(T),Y(T)),B$(11);:PRINT@B(X(T),Y(T)),B$(11);:SOUND1,1
35 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:R(T)=1:G=0:RETURN
36 RESET(R,S):SET(R+U,S+V,4):SOUND100,2:GOSUB33:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2 THEN40
37 RETURN
38 L=0:M=0:N=0:O=0:RETURN
39 GOSUB24:PRINT@A(X,Y),B$(11);:PRINT@B(X,Y),B$(11);:SOUND200,4:GOTO41
40 FORT=1 TO5:PRINT@A(X,Y),A$(10);:PRINT@B(X,Y),B$(10);:SOUND200,1:PRINT@A(X,Y),A$(9);:PRINT@B(X,Y),B$(9);:SOUND100,1:NEXT
41 PRINT@480,"OUCH!";:SOUND200,4:E=65000:T=6:G=0:LF=LF-1:HT=0:EV=2:RETURN
42 SET(L+N,M+O,7):FORM=1TO6:ONR(M)GOTO45:J=2*X(M):K=2*Y(M)
44 ON1-(POINT(J,K)=7ORPOINT(J+2,K)=7ORPOINT(J,K+2)=7ORPOINT(J+2,K+2)=7)GOSUB2,46
45 NEXT:GOSUB38:RETURN
46 PRINT@A(X(M),Y(M)),B$(11);:PRINT@B(X(M),Y(M)),B$(11);:SOUND1,1:PRINT@A(X(M),Y(M)),B$;:PRINT@B(X(M),Y(M)),B$;:R(M)=1:SC=SC+100:RETURN
47 ON1-(L=0) GOTO2:N=H*2:O=I*2:L=X*2+O(H+1):M=Y*2+P(I+1):ONPOINT(L,M)GOTO42,36,38,38,38:SET(L,M,7):RETURN
48 GOSUB24:SET(R,S,4):SOUND100,2:GOSUB33:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:HT=HT+1:SET(1,1,4):IFHT>2 THEN41
49 RETURN
50 GOSUB24:PRINT@480,"YOU ESCAPED!";:SOUND100,3:SOUND50,3:SOUND1,3:SOUND1,10:FORT=1 TO6:GOSUB76:NEXT:E=65000:T=6:EV=1:RETURN
51 PRINT@A(X,Y),B$(11);:PRINT@B(X,Y),B$(11);
52 PRINT@A(X(T),Y(T)),B$;:PRINT@B(X(T),Y(T)),B$;:Y(T)=Y(T)+B:X(T)=X(T)+A:PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);
53 SOUND200,4:GOTO41
60 IFE<65000THENFORT=1TO6:SOUND200,1:GOSUB35:NEXT:PRINT@A(X,Y),A$(D);:PRINT@B(X,Y),B$(D);:R(RND(6))=20:F=12:Q=12:GOTO14
63 IFEV=2THENIFLF>=0THENSOUND1,5:CLS0:GOTO150
64 IFEV=1ANDBN<>6THENPRINT@480,"CHICKEN! FIGHT LIKE A ROBOT!";:FORT=1TO10:SOUND101-(10*T),1:SOUND1,1:NEXT:GOTO68
66 IFEV=1THENPRINT@480,"LEVEL CLEARED.";:FORT=1TO10:SOUND100,1:SOUND200,1:NEXT:SC=SC+250
68 IFEV=1THENPRINT@480,"GET READY FOR NEXT LEVEL ...";:FORT=1 TO10:SOUND10*T,1:NEXT:EN=NE:EX=EX(RND(3)):GOTO145
70 I$=INKEY$:RESET(2,0):PRINT@480,"YOU ARE DEAD. PLAY AGAIN (Y/N)";:PRINT@0,"SCORE:";SC;:PRINT@16,"HIGH:";HS;
71 I$=INKEY$:IFI$="" THEN71
72 IFI$="Y" THEN95
73 IFI$="N" THENGOSUB10010:GOSUB10003:END
74 GOTO71
76 IFR(T)=1THENBN=BN+1
78 RETURN
80 FORQ=1TO6:IFDR(EN,Q,0)=J(Y,0)ANDDR(EN,Q,1)=I(X,0)THENQ=6:NEXT:GOTO155
90 NEXT:X(T)=I(X,0):Y(T)=J(Y,0):I(X,1)=1:J(Y,1)=1:NEXT:GOTO165
95 CLS0:LV=5:LF=2:M(1)=9:HT=0:EN=4:EX=RND(3):IFSC>HS THENHS=SC
99 SC=0:GOTO147
100 DIMA$(13),B$(13),X(6),Y(6),R(6),O(2),P(2),K(255),A(31,14),B(31,14),E(64),F(64),G(64),H(64),I(12,1),J(6,1),DR(4,6,1)
101 DIMT,D,X,Y,A,B,P,C,E,F,H,I,G,R,L,S,M,W,J,K,N,O,Q,U,V,Z,B$,M(12),EX(3),LV,EV,SC,EN,EX,BN,SR,HS
102 DIMXX,YY,CX,CY,XC,YC,PC,RR,HT,I$,A$:B$=CHR$(128)+CHR$(128):LV=8:LF=3:GOSUB500:GOSUB400
105 GOSUB800:FORT=0TO13:FORY=0TO3:READA$:FORX=1TO4:I=VAL(MID$(A$,X,1)):IFI>0THENSET(X-1,Y,I)
110 NEXT:NEXT:A$(T)=CHR$(PEEK(MC))+CHR$(PEEK(MC+1)):B$(T)=CHR$(PEEK(MC+32))+CHR$(PEEK(MC+33))
120 GOSUB450:NEXT:GOSUB460:GOSUB514:GOSUB460:W=-1:O(0)=-1:O(1)=3:O(2)=5:P(0)=-1:P(1)=1:P(2)=5:I=0:GOSUB780
130 FORT=1TO12:READI(T,0):NEXT:FORT=1 TO6:READJ(T,0):NEXT:FOREN=1TO4:FORT=1 TO6:READDR(EN,T,0),DR(EN,T,1):NEXT:NEXT
140 EN=4:EX=RND(3)
145 FORT=1TO12:READM(T):NEXT
147 IFM(1)=9 THENLV=LV+3:LF=LF+1:RESTORE:FORT=1TO(14*4):READA$:NEXT:FORT=1TO12+6+(4*12):READC:NEXT:GOSUB700:GOTO145
150 FORT=1TO6:R(T)=LV:NEXT:FORT=1TO12:I(T,1)=0:NEXT:FORT=1TO6:J(T,1)=0:NEXT:FORT=1TO6
155 X=RND(12):Y=RND(6):IFI(X,1)=0ANDJ(Y,1)=0THEN80
160 GOTO155
165 CLS0:FORX=1TO63:SET(X,1,3):SET(X,11,3):SET(X,21,3):SET(X,31,3):NEXT
170 FORY=1TO31:SET(1,Y,3):SET(17,Y,3):SET(33,Y,3):SET(49,Y,3):SET(63,Y,3):NEXT
180 IFLF>0THENFORT=1 TOLF:SET(T*2,0,3):NEXT
181 IFHT>0THENSET(1,1,4)
185 GOSUB210:FORY=1TO30:SET(63,Y,3):NEXT:GOSUB300
190 Y=DR(EN,1,0):X=DR(EN,1,1):FORT=1TO6:G=RND(2):PRINT@A(X(T),Y(T)),A$(G);:PRINT@B(X(T),Y(T)),B$(G);:NEXT
200 PRINT@A(X,Y),A$(3);:PRINT@B(X,Y),B$(3);:R=1:H=1:I=0:D=3:J=F(X+H):K=F(Y+I):F=1:Q=2:BN=0:GOSUB38:GOSUB33:GOTO14
210 C=1:FORY=1TO30STEP10:FORX=1TO62STEP16
220 T=M(C):C=C+1:ONT GOSUB250,260,270:NEXT:NEXT:RETURN
250 FORR=X+1TOX+15:ON1-(R<64)GOTO255:RESET(R,Y):NEXT:RETURN
255 NEXT:RETURN
260 FORS=Y+1TOY+9:RESET(X,S):NEXT:RETURN
270 GOSUB250:GOSUB260:IFPOINT(X,Y-1)=0ANDPOINT(X-1,Y)=0THENRESET(X,Y)
280 RETURN
300 ONEX GOSUB310,330,340,360:RETURN 
310 FORX=18TO31:SET(X,1,5):NEXT:NE=4:EX(1)=1:EX(2)=2:EX(3)=3:RETURN
330 FORY=12TO18:SET(1,Y,5):NEXT:NE=3:EX(1)=1:EX(2)=2:EX(3)=4:RETURN
340 FORY=12TO18:SET(63,Y,5):NEXT:NE=2:EX(1)=1:EX(2)=3:EX(3)=4:RETURN
360 FORX=34TO46:SET(X,31,5):NEXT:NE=1:EX(1)=2:EX(2)=3:EX(3)=4:RETURN
400 FORX=0TO31:FORY=0 TO14:A(X,Y)=X+Y*32:B(X,Y)=X+Y*32+32:NEXT:NEXT
420 CLS:PRINT"PLEASE WAIT...":RETURN
410 FORX=0TO64:E(X)=X*2+5:F(X)=X*2+1:G(X)=X*2-1:H(X)=X*2+1:NEXT:RETURN
450 PRINT@0,B$;:PRINT@32,B$;:RETURN
460 PRINT@483,"PRESS ANY KEY TO CONTINUE";
470 I$=INKEY$:T=RND(1000):IFI$="" THEN470
480 RETURN
500 CLS0:PRINT@4,"THE ASTRO DATE IS 3200":A$="berzerk":FORT=1TOLEN(A$):PRINT@95+(T*32)+RND(3),MID$(A$,T,1);:NEXT
501 PRINT@(32*1)+4,"AND YOU ARE THE LAST"
502 PRINT@(32*2)+4,"SURVIVOR OF A SMALL GROUP"
503 PRINT@(32*3)+4,"OF EARTH PEOPLE WHO CAME"
504 PRINT@(32*4)+4,"TO EXPLORE THE PLANET"
505 PRINT@(32*5)+4,"MAZEON. SOON AFTER LANDING"
506 PRINT@(32*6)+4,"YOU DISCOVERED THE PLANET"
507 PRINT@(32*7)+4,"IS A DARK UNINHABITABLE"
508 PRINT@(32*8)+4,"PLACE. BUT BY THEN IT WAS"
509 PRINT@(32*9)+4,"TOO LATE TO TURN BACK"
510 PRINT@(32*10)+4,"BECAUSE YOUR SPACECRAFT"
511 PRINT@(32*11)+4,"HAD BEEN DESTROYED BY THE"
512 PRINT@(32*12)+4,"AUTO-MAZEONS. NOW YOU ARE"
513 PRINT@(32*13)+4,"A PRISONER HERE.":PRINT@491,"BY JIM GERRIE";:RETURN
514 CLS0:PRINT"YOU ARE TRAPPED IN A MAZE"
515 PRINT"WHERE EVEN THE WALLS ARE"
516 PRINT"DEATH TO TOUCH. GRIM ROBOT"
517 PRINT"THUGS STALK YOU RELENTLESSLY..."
540 PRINT@160,"USE:";
542 PRINT@224,"Q W E";
544 PRINT@256,"A s D";CHR$(128);"or";
546 PRINT@288,"Z x C";
552 PRINT@234,"T Y U";
554 PRINT@266," G H ";
556 PRINT@298,"V B N";
562 PRINT@244,"SPACE";
564 PRINT@276," TO  ";
566 PRINT@308," FIRE";
570 PRINT@352,"THE WHITE DOOR IS THE EXIT. YOU"
580 PRINT"GET AN EXTRA MAN FOR EVERY 10"
583 PRINT"LEVELS THAT YOU COMPLETE."
585 RETURN
600 XC=0:YC=RR:PC=3-2*RR 
635 GOSUB670
640 IFPC<0 THENPC=PC+4*XC+6:GOTO650
645 PC=PC+4*(XC-YC)+10:YC=YC-1   
650 XC=XC+1:IFXC<YC THEN635
660 IFXC=YC THEN GOSUB670 
665 RETURN
670 XX=CX+XC:YY=CY+YC:SET(XX,YY,6)
671 XX=CX-XC:YY=CY+YC:SET(XX,YY,6)
672 XX=CX+XC:YY=CY-YC:SET(XX,YY,6)
673 XX=CX-XC:YY=CY-YC:SET(XX,YY,6)
674 XX=CX+YC:YY=CY+XC:SET(XX,YY,6)
675 XX=CX-YC:YY=CY+XC:SET(XX,YY,6) 
676 XX=CX+YC:YY=CY-XC:SET(XX,YY,6)
677 XX=CX-YC:YY=CY-XC:SET(XX,YY,6)
679 RETURN
700 CLS0:IFSC=0THENGOSUB780:RETURN
701 IFSC<15000THENA$="THE HUMANOID MUST NOT ESCAPE!":SM=-1
702 IFSC>=15000ANDSC<30000THENA$="THE HUMANOID IS ABOUT TO ESCAPE":SM=0
703 IFSC>=25000THENA$="THE HUMANOID ESCAPED!":SM=-1
704 RR=5:CX=31:CY=15:GOSUB600:PRINT@239,A$(12);:IFSM THENPRINT@271,B$(12);:GOTO710
705 PRINT@239+32,B$(13);
710 PRINT@480+INT((32-LEN(A$))/2),"";:FORT=1TOLEN(A$):PRINTMID$(A$,T,1);:SOUND222,1:NEXT
730 FORT=1TO2000:NEXT
740 IFSC>=25000 THENPRINT:PRINT"DEDICATED TO P,M,C & N":GOSUB10010:GOSUB10003:END
750 RETURN
780 FORT=3TO4:CLST:PRINT@233,"INTRUDER ALERT!";:SOUND50,2:SOUND5,2:SOUND1,2:SOUND1,2:SOUND1,2:NEXT:RETURN
800 P=17023:K(68)=1:K(69)=2:K(67)=3:K(65)=4:K(81)=5:K(90)=6:K(88)=7:K(87)=8:K(32)=9:K(83)=7
805 K(72)=1:K(85)=2:K(78)=3:K(71)=4:K(84)=5:K(86)=6:K(66)=7:K(89)=8
810 MC=1024:RETURN
910 DATA "...."
920 DATA "...."
930 DATA "...."
940 DATA "...."
954 DATA "1111"
956 DATA "1.11"
958 DATA "1111"
960 DATA "1..1"
970 DATA "1111"
972 DATA "11.1"
973 DATA "1111"
974 DATA "1..1"
1010 DATA ".2.."
1020 DATA "2222"
1030 DATA ".2.."
1040 DATA "2.2."
2010 DATA ".2.2"
2020 DATA "222."
2030 DATA ".2.."
2040 DATA "2.2."
3010 DATA ".2.."
3020 DATA "222."
3030 DATA ".2.2"
3040 DATA "2.2."
4010 DATA "..2."
4020 DATA "2222"
4030 DATA "..2."
4040 DATA ".2.2"
5010 DATA "2.2."
5020 DATA ".222"
5030 DATA "..2."
5040 DATA ".2.2"
6010 DATA "..2."
6020 DATA ".222"
6030 DATA "2.2."
6040 DATA ".2.2"
7010 DATA "..2."
7020 DATA ".222"
7030 DATA "..2."
7040 DATA ".2.2"
7110 DATA "..8."
7120 DATA ".888"
7130 DATA "..8."
7140 DATA ".8.8"
7210 DATA "4.4."
7220 DATA ".4.4"
7230 DATA "4.4."
7240 DATA ".4.4"
7250 DATA "6..6"
7260 DATA "...."
7270 DATA "6..6"
7280 DATA ".66."
7282 DATA "6..6"
7284 DATA "...."
7285 DATA ".66."
7286 DATA "6..6"
7300 DATA 1,3,5,9,11,13,17,19,21,25,27,29
7310 DATA 1,3,6,8,11,13
7400 DATA 1,11,1,9,1,13,3,9,3,11,1,13
7410 DATA 6,1,6,3,6,5,8,1,8,3,8,5
7420 DATA 6,29,6,25,6,27,8,25,8,27,8,29
7430 DATA 13,19,11,17,11,19,11,21,13,17,13,21
8000 DATA 0,0,2,2,1,2,3,2,1,0,3,2
8001 DATA 0,2,2,2,0,3,2,1,1,2,2,3
8002 DATA 0,2,2,0,0,1,3,3,1,3,2,3
8003 DATA 0,2,0,0,0,3,3,3,1,1,2,3
8004 DATA 0,2,2,2,0,2,3,3,1,3,2,2
8005 DATA 0,2,0,2,0,3,3,3,1,1,2,1
8010 DATA 0,2,2,2,1,1,3,1,1,3,3,3
8020 DATA 0,2,2,2,1,0,2,1,1,3,3,3
8021 DATA 0,2,2,0,0,3,1,3,1,3,1,3
9000 DATA 9,9,9,9,9,9,9,9,9,9,9,9
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN

