5 'COPYRIGHT (C) T&D SOFTWARE 1983
10 POKE 113,0
20 CLS:PRINT"NOW TYPE 'RUN' TO RUN THE GRID  FACTOR."
30 FORRO=&H1FE TO &H218:READ X1$:X1$="&H"+X1$:X1=VAL(X1$):POKE RO,X1:NEXT
40 DATA 8E,4,0,A6,84,81,41,24,4,8B,40,20,6,81,80,24,2,80,40,A7,80,8C,6,0,26,E9,39
50 DEL 10-50
60 DEFUSR0=&H1FE
70 GOTO 90
80 GOTO 100
90 PCLEAR1:GOTO 80
100 CLEAR 400
110 US$="O2T255ACEGB"
120 CLS3:PRINT"CAN YOUR COMPUTER HANDLE THE":PRINT@32,"HIGH-SPEED POKE? (Y/N)"
130 SP$=INKEY$:IF SP$=""THEN 130 ELSE IF SP$="Y" THEN POKE 65495,0ELSE IF SP$="N" THEN 140 ELSE 130
140 GOSUB 1770
150 GOSUB 1360
160 GOSUB 800
170 CLS:PRINT@0,"INPUT SKILL LEVEL";
180 PRINT:PRINT:PRINT" <1> BEGINNER  (RATHER EASY)";:PRINT:PRINT" <2> EXPERT  (FORGET IT!!)";
190 IN$=INKEY$:IF IN$="1" THEN PRINT:PRINT"OK, ONE IT IS!":SK=1 ELSE IF IN$="2" THENPRINT:PRINT"AH, A DAREDEVIL FOR OUR         AMUSEMENT!":SK=2 ELSE 190
200 FOR G=1 TO 1000:NEXT
210 CLS0:PRINT@0,"MISSION:";:FOR G=1 TO 1000:NEXT:PRINT@163,"RID GRID SECTORS OF ALIENS";:FOR G=1 TO 2000:NEXT:CLS0
220 DIM AL(6,4):FOR G=1 TO 6:FOR H=1 TO 4:AL(G,H)=RND(7):NEXT:NEXT
230 IF SK=1 THEN WP=26 ELSEWP=28
240 CLS0:AF=1:GOSUB 250:GOTO 510
250 EB$="0ÿÿÿ1ÿÿÿ2ÿÿÿ3ÿÿÿ4ÿÿÿ5"
260 X=1:Y=1
270 TIMER=0:TI=0:TM=0
280 C$=""
290 A$="¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯"
300 B$="¯€€€":FOR G=1 TO 6:C$=C$+B$:NEXT:C$=C$+"¯"
310 FORG=33TO417STEP96:PRINT@G,A$;:NEXT
320 FORG=65TO353STEP96:PRINT@G,C$;:PRINT@G+32,C$;:NEXT
330 A=66
340 RE$=STRING$(3,255)
350 B$=CHR$(153)+CHR$(159)+CHR$(150):C$=CHR$(150)+CHR$(159)+CHR$(153)
360 EN=21
370 FOR G=1 TO 6:FOR H=1 TO 4:IFAL(G,H)=0 THEN PRINT@RG(G,H),RE$;:PRINT@RG(G,H)+32,RE$;:NEXT:NEXT ELSE NEXT:NEXT
380 PRINT@128+27,"WARP";:PRINT@160+27,"LEFT";:PRINT@192+27,WP;:PRINT@192+27,CHR$(128);:PRINT@192+30,CHR$(128);
390 PRINT@27+32,"TIME:";:PRINT@27+64," 30  ";
400 PRINT@256+27,"SCORE";:PRINT@288+27,USING"#####";SC;:PRINT@26,CHR$(128);:PRINT@320,CHR$(128);
410 PRINT@4,"intersectorial"+CHR$(128)+"grid";
420 PRINT@448+5,"LOCATION";
430 PRINT@448+22,"ALIENS:";
440 PRINT@A,B$;:PRINT@A+32,C$;
450 POKE 1024+448+29,AL(X,Y)+112
460 POKE 1024+448+15,X+112:PRINT@448+16,",";:POKE 1024+448+17,Y+112
470 IF DA=1 THEN 1180
480 IF YW=1 THEN 1580
490 FORG=1TO6:FORH=1TO4:IFAL(G,H)<>0 THEN 500 ELSE NEXT:NEXT:GOTO 1390
500 IF AF=1 THEN AF=0:RETURN
510 IF PEEK(65280)=254 OR PEEK(65280)=126 THEN 630 ELSEJY(0)=JOYSTK(0):JY(1)=JOYSTK(1):POKE 1024+448+29,AL(X,Y)+112:POKE 1024+448+15,X+112:PRINT@448+16,",";:POKE 1024+448+17,Y+112:PRINT@A,B$;:PRINT@A+32,C$;
520 IF TIMER>TI+50 THEN TI=TI+50:TM=TM+1
530 PRINT@27+64,30-TM;:IF TM=30 THEN FOR G=1 TO500:NEXT:GOTO 1180ELSE IF TM>=20 THEN SOUND 200,1
540 IF JY(1)<>0 THEN 560 ELSEIF A-96>=66 THEN A=A-96:Y=Y-1:PRINT@A+96,STRING$(3,128);:PRINT@A+128,STRING$(3,128);:IFAL(X,Y+1)=0THEN PRINT@RG(X,Y+1),RE$;:PRINT@RG(X,Y+1)+32,RE$;
550 PRINT@A,B$;:PRINT@A+32,C$;:GOTO 510
560 IF JY(1)<>63 THEN580 ELSEIFA+96<=352+22 THEN A=A+96:Y=Y+1:PRINT@A-96,STRING$(3,128);:PRINT@A-64,STRING$(3,128);:IFAL(X,Y-1)=0THEN PRINT@RG(X,Y-1),RE$;:PRINT@RG(X,Y-1)+32,RE$;
570 PRINT@A,B$;:PRINT@A+32,C$;:GOTO 510
580 IF JY(0)<>0 THEN600ELSE FOR G=30 TO 350 STEP 32:IF A-4=G THEN 440 ELSE NEXT:X=X-1:A=A-4:PRINT@A+4,STRING$(3,128);:PRINT@A+36,STRING$(3,128);:IF AL(X+1,Y)=0 THEN PRINT@RG(X+1,Y),RE$;:PRINT@RG(X+1,Y)+32,RE$;
590 PRINT@A,B$;:PRINT@A+32,C$;:GOTO 510
600 IF JY(0)<>63THEN510 ELSE FORG=26+64 TO 26+352 STEP 32:IFA+4=G THEN440 ELSENEXT:X=X+1:A=A+4:PRINT@A-4,STRING$(3,128);:PRINT@A+28,STRING$(3,128);:IFAL(X-1,Y)=0 THEN PRINT@RG(X-1,Y),RE$;:PRINT@RG(X-1,Y)+32,RE$;
610 PRINT@A,B$;:PRINT@A+32,C$;:GOTO510
620 GOTO 510
630 IF AL(X,Y)=0 THEN SOUND 1,2:GOTO440
640 GOSUB 660
650 GOTO 730
660 PRINT@448,STRING$(32,128);
670 PRINT@449,"NOW WARPING TO SECTOR";:POKE 1026+449+20,X+112:PRINT@451+21,",";:POKE 1026+449+22,Y+112
680 FOR DL=1 TO 1500:NEXT
690 GOSUB 700:GOTO 780
700 W$=STRING$(8,255)
710 RESTORE
720 FOR G=1 TO 16:READ SP:IF SP=384+24 THEN 740 ELSEPRINT@SP,W$;:PRINT@SP+32,W$;:PRINT@SP+64,W$;:PRINT@SP+96,W$;:PLAY"T255O1CDEDCD":NEXT
730 GOTO 750
740 PRINT@408,W$;:PRINT@408+32,W$;:PRINT@408+64,W$;:PRINT@408+96,STRING$(7,255);;:POKE 1535,255:PLAY"CDEDED":NEXT
750 RESTORE:W$=STRING$(8,128):FOR G=1 TO 16:READ SP:IF SP=408 THEN 790 ELSE PRINT@SP,W$;:PRINT@SP+32,W$;:PRINT@SP+64,W$;:PRINT@SP+96,W$;:PLAY"CDEDCD":NEXT
760 IF KL=1 THEN KL=0:RETURN
770 RETURN
780 GOTO 860
790 PRINT@SP,W$;:PRINT@SP+32,W$;:PRINT@SP+64,W$;:PRINT@SP+96,STRING$(7,128);:POKE 1535,128:PLAY"CDEDCD":NEXT
800 M1$=CHR$(193)+CHR$(201)+CHR$(195)+CHR$(201):M2$=CHR$(193)+CHR$(193)+CHR$(128)+CHR$(128)+CHR$(198)+CHR$(198)+CHR$(194)+CHR$(193)+STRING$(3,128)+CHR$(193)+CHR$(194)+CHR$(198)+CHR$(201)+CHR$(200)+STRING$(3,128)+CHR$(201)+CHR$(198)+CHR$(194)
810 M2$=M2$+CHR$(195)+CHR$(201)+CHR$(128)+CHR$(198)+CHR$(194)+CHR$(193)+CHR$(201)+CHR$(198)
820 M3$=CHR$(198)+CHR$(201)+CHR$(200)+CHR$(200)+CHR$(201)+CHR$(198)+STRING$(2,128)+CHR$(196)+CHR$(200)+CHR$(204)+CHR$(198)+CHR$(195)+CHR$(200)+CHR$(196)+STRING$(8,128)+CHR$(196)+STRING$(2,128)+CHR$(204)+CHR$(128)+CHR$(196)+CHR$(200)
830 CA$(1)=CHR$(215)+STRING$(3,223)+CHR$(219):CA$(2)=CHR$(187)+CHR$(223)+CHR$(128)+CHR$(223)+CHR$(128)+CHR$(223)+CHR$(183):CA$(3)=CHR$(184)+STRING$(5,223)+CHR$(180):CA$(4)=CHR$(223)+STRING$(3,220)+CHR$(223)
840 CA$(5)=CHR$(212)+CHR$(220)+CHR$(223)+CHR$(220)+CHR$(216):CA$(6)=CHR$(193)+CHR$(195)+CHR$(195)+CHR$(207)+CHR$(195)+CHR$(195)+CHR$(194):CA$(7)=CHR$(159)+STRING$(7,207)+CHR$(159):CA$(8)=CA$(7):CA$(9)=CA$(7)
850 RETURN
860 PRINT@416+17,M1$;:PRINT@450,M2$;:PRINT@480,M3$;
870 IF RND(3)=1 THEN BO=1
880 MO=RND(24)+164
890 LO=174
900 NO=AL(X,Y)
910 LE$(1)=CHR$(241)+CHR$(241)+CHR$(128):LE$(2)=CHR$(169)+CHR$(169)+CHR$(168)
920 FO$(1)=CHR$(134)+CHR$(140)+CHR$(130):FO$(2)=CHR$(132)+CHR$(140)
930 CH$=CHR$(152)+CHR$(152)
940 GOTO 960
950 DATA 144,0,408,264,16,280,384,128,272,152,392,8,256,136,24,400
960 CS$=CHR$(183)+CHR$(178):CS$(2)=CHR$(180)
970 PRINT@LO,CH$;:PRINT@LO+32,CH$;:IFPEEK(65280)=254ORPEEK(65280)=126THENPRINT@LO,CS$;:PRINT@LO+32,CS$(2)+CHR$(128);:PLAYUS$:FORG=1TO10:NEXT:EN=EN-1:PRINT@LO,CH$;:PRINT@LO+32,CH$;:IF LO=MO THEN GOSUB1730ELSEIF LO=MO+1AND SK=1 THEN GOSUB 1730
980 IF PEEK(339)=191THENPRINT@0,STRING$(32,128);:PRINT@8,"NUMBER LEFT:";NO;:GOTO1000
990 IF EN<=0 THEN PRINT@8,STRING$(5,128);:FOR J=1 TO 500:NEXT:KL=1:DA=1:GOSUB 700:GOTO 240 ELSE PRINT@0,"energy"+CHR$(128)+CHR$(128);:PRINT@8,STRING$(21,128);:PRINT@8,LEFT$(EB$,EN);
1000 IF EN=21 THEN 1010 ELSEER=ER+1:IF ER=2 THEN ER=0: EN=EN+1 
1010 '
1020 PRINT@LO,CH$;:PRINT@LO+32,CH$;
1030 IF NO>0 THENGOSUB 1630
1040 PRINT@LO,CH$;:PRINT@LO+32,CH$;:JY(0)=JOYSTK(0):JY(1)=JOYSTK(1)
1050 IF JY(1)=0 AND LO>=64 THEN LO=LO-32:PRINT@LO+64,CHR$(128)+CHR$(128);:PRINT@LO,CH$;:PRINT@LO+32,CH$;:GOTO 1090
1060 IF JY(1)=63 AND LO<=350 THEN LO=LO+32:PRINT@LO-32,CHR$(128)+CHR$(128);:PRINT@LO,CH$;:PRINT@LO+32,CH$;:GOTO 1090
1070 IF JY(0)=0 AND LO>=33 THEN LO=LO-1:PRINT@LO+2,CHR$(128);:PRINT@LO+34,CHR$(128);:GOTO 1090
1080 IF JY(0)=63 AND LO<=28+352 THEN LO=LO+1:PRINT@LO-1,CHR$(128);:PRINT@LO+31,CHR$(128);:GOTO 1090
1090 IF PEEK(338)=191 THEN 1110 
1100 GOTO 970
1110 PRINT@0,STRING$(32,128);:PRINT@6,"WARPING BACK TO GRID";:PRINT@LO,CH$;:PRINT@LO+32,CH$;:IF NO=0 THEN 1120 ELSE IF BO=1 THENPRINT@MO,LE$(1);:PRINT@MO+32,LE$(2); ELSE PRINT@MO,FO$(1);:PRINT@MO+32,FO$(2);
1120 WP=WP-1:IF WP=0 THEN FORG=1TO1000:NEXT:KL=1:GOSUB700:DA=1:GOTO240
1130 AL(X,Y)=NO
1140 FOR G=1 TO 1000:NEXT
1150 KL=1
1160 GOSUB 700
1170 GOTO 240
1180 REM ** DEATH SEQUENCE **
1190 IF WP=0 THEN PRINT@485,"OUT OF WARPS!"; ELSE IF TM=30 THEN PRINT@485,"OUT OF TIME!!"; ELSE IF EB=1 THEN PRINT@485,"EGGS GOTCHA!!";ELSE  PRINT@485,"SHIELDS OUT!!";
1200 FOR G=1 TO 1500:NEXT
1210 PRINT@485,STRING$(17,128);:PRINT@485,"WE'RE EXPLODING!";
1220 FOR G=1 TO 350:SCREEN 0,0:SCREEN 0,1:NEXT
1230 IF SP$="Y" THEN POKE 65497,0:FORG=1TO5500:NEXT:POKE 65496,0:CLS0:GOTO 1280
1240 CLS:G=0:US=USR(0):PRINT@192+13,STRING$(7,"*");:PRINT@224+13,"*BOOM!*";:PRINT@256+13,STRING$(7,"*");
1250 G=G+1:PRINT@192+13-(G*32)-G,CHR$(169);:PRINT@192+19-(G*32)+G,CHR$(166);:PRINT@256+13+(G*32)-G,CHR$(166);:PRINT@256+19+(G*32)+G,CHR$(169);:IFG<>6 THEN US=USR(0):PLAY"T255O1CGEFDCEBAD":GOTO 1250
1260 G=0:PRINT@486,CHR$(166);:PRINT@506,CHR$(169);:PLAY"CFEFDCEBAD":FOR G=1 TO 20:US=USR(0):PLAY"CFEFDCEBAD":NEXT:FORG=1TO500:NEXT
1270 CLS0
1280 PRINT@96+12,CA$(1);:PRINT@128+11,CA$(2);:PRINT@160+11,CA$(3);:PRINT@192+12,CA$(4);:PRINT@224+12,CA$(5);:PRINT@256+11,CA$(6);:PRINT@288+10,CA$(7);:PRINT@320+10,CA$(8);:PRINT@352+10,CA$(9);:PRINT@384,STRING$(32,140);
1290 PRINT@417,"FEDERATION CAPTAIN REPORTING:";:PRINT@454,"WE HAVE FOUND SPACE";:PRINT@485,"DUST OF A GRID RUNNER";
1300 IF SP$="Y" THEN POKE 65494,0
1310 PLAY"T3O3L2CP128L2CL9CL2CP128D#P128L9DL2DP128L9CP235L2CP235O2BO3L1C":FOR G=1 TO 1000:NEXT
1320 CLS0:PRINT@169,"THE GRID FACTOR";
1330 PRINT@224+9,"LAST SCORE: ";SC;
1340 PRINT@490,"ANOTHER GAME?";
1350 E$=INKEY$:IF E$="N" THEN POKE 113,1:EXEC40999 ELSE IF E$="Y" THEN RUN ELSE 1350
1360 DIM RG(6,4)
1370 RG(1,1)=64+2:RG(2,1)=64+6:RG(3,1)=64+10:RG(4,1)=64+14:RG(5,1)=64+18:RG(6,1)=64+22:RG(1,2)=160+2:RG(2,2)=160+6:RG(3,2)=160+10:RG(4,2)=160+14:RG(5,2)=160+18:RG(6,2)=160+22:RG(1,3)=256+2:RG(2,3)=256+6:RG(3,3)=256+10:RG(4,3)=256+14:RG(5,3)=256+18
1380 RG(6,3)=256+22:RG(1,4)=352+2:RG(2,4)=352+6:RG(3,4)=352+10:RG(4,4)=352+14:RG(5,4)=352+18:RG(6,4)=352+22:RETURN
1390 POKE 65494,0:AL(1,1)=1:PLAY"T3O2L4CP8L7DL4D#L7CP8L25GP255GP255GP255GP255GP255GP255GP255GP255GP255L7G":FOR G=1 TO 1500:NEXT:CLS0:PRINT@0,"NEW MISSION:";:FOR G=1 TO 1000:NEXT:PRINT@192+2,"DESTROY THE ALIEN'S EGG NEST";
1400 IF SP$="Y" THEN POKE 65495,0
1410 FOR G=1 TO 300:SCREEN 0,1:SCREEN0,0:NEXT:CLS0:AF=1:GOSUB 250:X1=RND(6):Y1=RND(4):IF Y1=1 THEN FOR G=67 TO 87 STEP 4:XX=XX+1:IF XX=X1 THENXX=G:GOTO 1460 ELSE NEXT
1420 XX=0:IF Y1=2 THEN FOR G=163 TO 183 STEP 4:XX=XX+1:IF XX=X1 THEN XX=G:GOTO 1460 ELSE NEXT
1430 XX=0:IF Y1=3 THEN FOR G=259 TO 279 STEP 4:XX=XX+1:IF XX=X1 THEN XX=G:GOTO 1460 ELSE NEXT
1440 XX=0:FOR G=355 TO 375 STEP 4:XX=XX+1:IF XX=X1 THEN XX=G:GOTO 1460 ELSE NEXT
1450 X=1:Y=1:A=66
1460 PRINT@XX,"e";:JY(0)=JOYSTK(0):JY(1)=JOYSTK(1):POKE 1024+448+29,AL(X,Y)+112:POKE 1024+448+15,X+112:PRINT@448+16,",";:POKE 1024+448+17,Y+112:PRINT@A,B$;:PRINT@A+32,C$;
1470 IF PEEK(65280)<>254 AND PEEK(65280)<>126 THEN 1530 
1480 IF X<>X1 THENSOUND 1,2:GOTO 1530 ELSE IF Y<>Y1 THEN SOUND 1,2:GOTO 1530
1490 AL(1,1)=0:GOSUB 700
1500 GOSUB 1910
1510 IF DA=1 THEN EB=1:GOSUB 700:GOTO 240
1520 YW=1:GOSUB 700:GOTO 240
1530 IF JY(1)<>0 THEN 1540 ELSE IF A-96>=66 THEN A=A-96:Y=Y-1:PRINT@A+96,RE$;:PRINT@A+128,RE$;:PRINT@A,B$;:PRINT@A+32,C$;:GOTO 1460
1540 IF JY(1)<>63 THEN 1550ELSE IF A+96<=352+22 THENA=A+96:Y=Y+1:PRINT@A-96,RE$;:PRINT@A-64,RE$;:PRINT@A,B$;:PRINT@A+32,C$;:GOTO 1460
1550 IF JY(0)<>0 THEN 1560 ELSE FOR G=30 TO 350 STEP 32:IF A-4=G THEN 1460 ELSE NEXT:X=X-1:A=A-4:PRINT@A+4,RE$;:PRINT@A+36,RE$;:PRINT@A,B$;:PRINT@A+32,C$;
1560 IF JY(0)<>63 THEN 1460 ELSE FOR G=26+64 TO 26+352 STEP 32:IF A+4=G THEN 1460 ELSE NEXT:X=X+1:A=A+4:PRINT@A-4,RE$;:PRINT@A+28,RE$;:PRINT@A,B$;:PRINT@A+32,C$;
1570 PRINT@A,B$;:PRINT@A+32,C$;:GOTO 1460
1580 POKE65494,0:PLAY"T3O3L9GO3B-B-B-O4CO3B-B-B-O4D#O3B-B-B-O4CO3B-B-B-FB-O4CDE-"
1590 PRINT@480+5,"YOU WIN, CAPTAIN!";
1600 FOR G=1 TO 1500:NEXT
1610 PRINT@480+5,"PLAY AGAIN?"+STRING$(6,128);
1620 AG$=INKEY$:IF AG$="Y"THENRUN ELSE IF AG$="N" THEN POKE 113,1:EXEC40999 ELSE1620
1630 IF HY=0 THEN XY=XY+1:IF XY=4 THEN XY=0:HY=1 ELSE RETURN
1640 IF BO=1 THEN PRINT@MO,LE$(1);:PRINT@MO+32,LE$(2); ELSE PRINT@MO,FO$(1);:PRINT@MO+32,FO$(2);
1650 ON RND(5) GOSUB 1680,1690,1700,1710,1720
1660 IF BO=1 THEN PRINT@MO,LE$(1);:PRINT@MO+32,LE$(2); ELSE PRINT@MO,FO$(1);:PRINT@MO+32,FO$(2);
1670 RETURN
1680 MO=MO-32:PRINT@MO+32,STRING$(4,128);:PRINT@MO+64,STRING$(4,128);:IF MO<32 THEN MO=MO+32:RETURN ELSE RETURN
1690 IF MO+32>320+29THEN RETURN ELSE MO=MO+32:PRINT@MO-32,STRING$(3,128);:RETURN
1700 IF MO<33 THEN RETURN ELSE MO=MO-1:PRINT@MO,STRING$(4,128);:PRINT@MO+32,STRING$(4,128);:RETURN
1710 IF MO<348 THEN MO=MO+1:PRINT@MO-1,CHR$(128);:PRINT@MO+31,CHR$(128);:RETURN ELSE RETURN
1720 IFRND(5)>4THEN RETURN ELSEPRINT@MO,STRING$(3,239);:PRINT@MO+32,STRING$(2,236)+CHR$(232);:PRINT@MO+34,CHR$(128);:IF BO=1 THEN EN=EN-3 :RETURN ELSE EN=EN-2:RETURN
1730 PRINT@MO,STRING$(3,128);:PRINT@MO+32,STRING$(3,128);:SOUND 25,1:SOUND 75,1:SOUND 150,1:PRINT@LO,CH$;:PRINT@LO+32,CH$;:NO=NO-1:IF BO=1 THEN SC=SC+50 ELSE IF BO=0 THEN SC=SC+10
1740 IF NO=0 THEN AL(X,Y)=0:MO=0:RETURN
1750 MO=RND(24)+164:IF RND(3)=1 THEN BO=1 ELSE BO=0
1760 RETURN
1770 CLS:KL=1:GOSUB700
1780 PRINT@128+4,"* * THE GRID FACTOR * *";:PRINT@256+10,CHR$(241)+CHR$(241);:PRINT@288+10,CHR$(169)+CHR$(169)+CHR$(168);
1790 PRINT@192+6,"by"+CHR$(128)+"steve"+CHR$(128)+"skrzyniarz";
1800 PRINT@256+19,CHR$(131)+CHR$(130);:PRINT@288+18,CHR$(132)+CHR$(131)+CHR$(134);
1810 FOR K=1 TO 10
1820 PRINT@427,"red"+CHR$(128)+"alert";
1830 FOR H=1 TO 45:NEXT
1840 PLAYUS$
1850 PRINT@427,STRING$(9,128);:FOR H=1 TO 45:NEXT
1860 PRINT@427,"red"+CHR$(128)+"alert";
1870 NEXT
1880 FOR K=1 TO 200:NEXT
1890 PLAY"T2O2CP128CP128T9CP128T3C"
1900 FOR G=1 TO 1500:NEXT:KL=0:RETURN
1910 ' EGG SEQUENCE
1920 CLS0
1930 FOR G=9 TO 480+9 STEP 32:PRINT@G,CHR$(149);:PRINT@G+13,CHR$(154);:NEXT
1940 PRINT@34,"time";:PRINT@32+25,"level";:X=16:LE=1
1950 EG$=CHR$(194):PRINT@416+13,EG$;:PRINT@416+19,EG$;:PRINT@448+12,EG$;:PRINT@448+14,EG$;:PRINT@448+18,EG$;:PRINT@448+20,EG$;:PRINT@480+11,EG$;:PRINT@480+13,EG$;:PRINT@480+15,EG$;:PRINT@480+17,EG$;:PRINT@480+19,EG$;:PRINT@480+21,EG$;
1960 TIMER=0
1970 EG=0
1980 BR$=STRING$(12,207)
1990 IF SK=1 THEN W=5000 ELSE W=2000
2000 BT(1)=74:BT(2)=234
2010 PRINT@64+27,USING"##";LE;
2020 IF TIMER>W THEN PRINT@66,USING"####";0;DA=1:RETURN ELSE PRINT@66,USING"####";W-TIMER;
2030 PRINT@X,"x";
2040 IF X=16 AND EG=1 AND PEEK(345)=247 OR X=16 AND EG=1 AND PEEK(345)=247 THEN SC=SC+1000:ELSE 2080
2050 FOR G=1TO3:PRINT@160+13,"BOOM!!";:PLAY"O1T255BAGDFDBCDGAEFAGDCBDEG":H=G*32:PRINT@384+H+10,STRING$(12,128);:FORAA=1TO250:NEXT:NEXT:FORG=1TO20:CLS0:PRINT@135,"1000 POINT BONUS!";
2060 PLAY"T255O3CDEFGAB":CLS5:PRINT@135,"1000 POINT BONUS!";:PLAY"T255O3CDEFGAB":NEXT
2070 RETURN
2080 IF PEEK(341)=247 AND LE<>1 THEN X=X-32:LE=LE-1:PRINT@X+32,CHR$(128);:PRINT@X,"x";
2090 IF PEEK(342)=247 AND LE<>16 THEN X=X+32:LE=LE+1:PRINT@X-32,CHR$(128);:PRINT@X,"x";
2100 IF LE=16 AND EG=0 THEN EG=1:SOUND 50,1:PRINT@256+24,"charge";:PRINT@288+25,"set";:GOTO 2010
2110 IF EG=1 AND SK=1 THEN CO=CO+1:IF CO=3 THEN XQ=RND(11)+1:PRINT@(XQ*32)+10,BR$;:CO=0:IF X-6=(XQ*32)+10 THEN DA=1:FORG=1TO500:NEXT:RETURN ELSE PRINT@(XQ*32)+10,STRING$(12,128);:GOTO 2010 ELSE 2010
2120 IF EG=1 THEN XQ=RND(11)+1:PRINT@(XQ*32)+10,BR$;:CO=0:IF X-6=(XQ*32)+10 THEN DA=1:FORG=1TO500:NEXT:RETURN ELSE PRINT@(XQ*32)+10,STRING$(12,128);:GOTO 2010
2130 BT=BT+1:IF BT<>5 THEN 2010
2140 BT=0:PRINT@BT(1),BR$;:PRINT@BT(2),BR$;:IF X-6=BT(1) THEN DA=1:FORG=1TO500:NEXT:RETURN ELSE IF X-6=BT(2) THEN DA=1:FORG=1TO500:NEXT:RETURN
2150 PRINT@BT(1),STRING$(12,128);:PRINT@BT(2),STRING$(12,128);
2160 IF BT(1)=202 THEN BT(1)=74 ELSE BT(1)=BT(1)+32
2170 IF BT(2)=362 THEN BT(2)=234 ELSE BT(2)=BT(2)+32
2180 PRINT@BT(1),STRING$(12,128);:PRINT@BT(2),STRING$(12,128);:GOTO 2010
2190 GOTO2010
