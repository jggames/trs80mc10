0 DIMX,Y,A,B,H,V,P,F,D,J,K,L,M,M$(15),E$(15),W(15):P=RND(-(PEEK(9)*256+PEEK(10))):GOSUB9000:GOTO60
9 IFF=0THENRETURN
10 H=0:V=0:FORP=1TO20-D:I$=INKEY$:RESET(A,B):SET(X,Y,5):SET(X,Y,6):SET(X,Y,1):RESET(X,Y)
11 IFI$<>""THENH=(I$="Q"ORI$="A"ORI$="Z")-(I$="E"ORI$="D"ORI$="C"):V=(I$="Q"ORI$="W"ORI$="E")-(I$="Z"ORI$="X"ORI$="C")
12 NEXT:IFH=0ANDV=0THENRETURN
13 J=X:K=Y:FORP=1TO5:J=J+H:K=K+V:IFPOINT(J,K)>0THENRESET(J,K):SOUND200,1:SET(J,K,1):NEXT:GOTO15
14 P=5:NEXT
15 IFJ=AANDK=BTHENGOSUB1000
16 SOUND1,1:I$="":RETURN
20 H=(I$="Q"ORI$="A"ORI$="Z")-(I$="E"ORI$="D"ORI$="C"):V=(I$="Q"ORI$="W"ORI$="E")-(I$="Z"ORI$="X"ORI$="C")
22 P=POINT(X+H,Y+V):IFPTHENSET(X,Y,ABS(P)):X=X+H:Y=Y+V:IFX<44THENSC=SC+D:PRINT@5,SC;
31 IFP<0THENGOSUB900
32 SET(X,Y,P):RESET(X,Y):H=(A>X)-(A<X):V=(B>Y)-(B<Y):IFRND(D)=1THENH=-H
45 IFRND(D)=1THENV=-V
48 IFPOINT(A+H,B)=1THENSET(A,B,1):A=A+H
49 IFPOINT(A,B+V)=1THENSET(A,B,1):B=B+V
50 I$=INKEY$:J=ABS(A-X):K=ABS(B-Y):IFJ<10ANDK<10THENRESET(A,B):IFJ<2ANDK<2THENGOSUB3000
51 SET(A,B,1):IFI$=" "THENGOSUB9
52 IFHP>0THEN20
60 CLS0:SC=0:N=4:X=44:Y=17:HP=200:F=0:GOSUB8000:FORJ=1TO15:W(J)=0:NEXT
61 FORJ=1TO15
62 K=RND(15):IFW(K)<>0THEN62
63 W(K)=J:NEXT
65 CLS:GOSUB90:GOSUB70:GOSUB100:GOTO50
70 PRINT@32,;
71 PRINT"    éåÅÉÇ                       ";
72 PRINT"ÅÉÉÉÇèAèäå                      ";
73 PRINT"ÖèèèãÉèÉÅãÇÇåå                  ";
74 PRINT"ÖèBèèçCèÖDäèèäÉÇåå              ";
75 PRINT"ÑåèéåÑçèèèèèEèèFäãÉÇç           ";
76 PRINT"ÖèèèèèáèÖèäèèäèèäèGèÇç éàÉÉÑå   ";
77 PRINT"ÖèèèèèååÑèàçèàçèàåèéåÄåÄåçèåàÉÑç";
78 PRINT"ÄÄèHèèèèèèèèèèèèèèèèèèèèèèèèèèèÄ";
79 PRINT"ÖèèèèèÉÉÅèÇáèÇáèÇÉèãÉÄÉÄÉáèÉÇåÅá";
80 PRINT"ÖèèèèèçèÖèäèèäèèäèIèàá ãÇååÅÉ   ";
81 PRINT"ÅÉèãÉÅáèèèèèKèèJäéåàá           ";
82 PRINT"ÖèNèèáMèÖLäèèäåàÉÉ              ";
83 PRINT"ÖèèèéåèåÑéààÉÉ                  ";
84 PRINT"ÑåååàèOèäÉ  YOU ARE CARRYING:-  ";
85 PRINT"    ãÉÑåà   ";:GOSUB150:RETURN
90 PRINT@0,"SCORE"SC;TAB(12)"CREW"N;TAB(22)"ALIEN"HP;:RETURN
100 A=1+RND(23):B=7+RND(20):IFPOINT(A,B)<>1THEN100
101 RETURN
150 PRINT@32*8+21,"üè";
151 PRINT@492,M$(F);
152 PRINT@32*1+14,"":PRINT@32*2+14,"":PRINT@32*3+14,"":RETURN
900 P=16384+INT(X/2)+32*INT(Y/2):TF=PEEK(P)-64:SOUND1,1:POKEP,143
910 IFF<>0THENGOSUB950
920 F=TF:GOSUB151:P=1:RETURN
950 IFPEEK(P-32)=143THENPOKEP-32,64+F:RETURN
952 IFPEEK(P+32)=143THENPOKEP+32,64+F:RETURN
953 IFPEEK(P-1)=143THENPOKEP-1,64+F:RETURN
954 IFPEEK(P+1)=143THENPOKEP+1,64+F:RETURN
960 IFPEEK(P-33)=143THENPOKEP-33,64+F:RETURN
961 IFPEEK(P-31)=143THENPOKEP-31,64+F:RETURN
962 IFPEEK(P+31)=143THENPOKEP+31,64+F:RETURN
963 IFPEEK(P+33)=143THENPOKEP+33,64+F:RETURN
990 RETURN
1000 REM FIGHT
1080 SET(A,B,4):RESET(A,B):H=INT(RND(F*10)):IFH<10THENH=0
1090 HP=HP-H:IFHP<0THENHP=0
1095 SC=SC+H:GOSUB90:PRINT@1*32+18,E$(INT(H/10));:IFH>=10ANDHP>0THENPRINT@2*32+18,"IT FLEES...";
1100 FORP=1TO2500:NEXT:GOSUB152:SET(A,B,1):IFH>=10ANDHP>0THENGOSUB100
1110 IFHP<1THENGOSUB5000
1120 RETURN
3000 SET(X,Y,4):RESET(X,Y)
3001 PRINT@32*1+14,"A SICKENING SCREAM";
3002 PRINT@32*2+14,"RINGS THROUGH THE";
3003 PRINT@32*3+14,"SHIP. IT GOT YOU!";
3004 FORP=1TO2500:NEXT:F=0
3005 N=N-1:IFN=0THENGOSUB4000:HP=0:RETURN
3010 GOSUB90:SET(X,Y,1):SET(A,B,1):GOSUB100:X=44:Y=17:GOSUB150:RETURN
4000 CLS:PRINTTAB(8)"THE FATAL DEFEAT":PRINTTAB(8)"----------------"
4010 PRINT"1000 YEARS AFTER THIS GRUESOME"
4011 PRINT"EPISODE A STRANGE SHIP APPEARS  IN THE SOLAR SYSTEM. AS IT"
4012 PRINT"SWINGS TOWARDS THEM THE PEOPLE  OF EARTH PUZZLE OVER ITS ORIGINS";
4013 PRINT"WHILE ON BOARD THE ANCIENT      NOSTRODAMUS THE ALIEN BROOD SITSAND WAITS."
4014 PRINT"YOUR ATTEMPT TO DEFEAT THE      CREATURE, THOUGH NOBLE, FAILED."
4015 PRINT"YOU ATTAINED A SCORE OF        "SC"POINTS":IFSC>HSTHENHS=SC
4020 PRINT"HIGH SCORE="HS:PRINT@480,"DO YOU WANT ANOTHER GAME?";
4025 I$=INKEY$:IFI$=""THEN4025
4030 IFI$="Y"THENRETURN
4040 IFI$="N"THENEND
4050 GOTO4025
5000 CLS:SC=SC+(50*N)
5005 PRINT"       üüüüüüüüüüüüüüüüüü"
5010 PRINT"       ü YOU ARE SAVED! ü"
5015 PRINT"       üüüüüüüüüüüüüüüüüü":PRINT:PRINT
5020 PRINT"YOU MANAGED TO DESTROY THE ALIENBEFORE IT WIPED OUT YOUR CREW!":PRINT
5025 GOTO4015
8000 M$(0)="NOTHING          "
8005 M$(1)="A LASER GUN      "
8010 M$(2)="A FLAME THROWER  "
8015 M$(3)="A WELDING TORCH  "
8020 M$(4)="BOTTLES OF ACID  "
8025 M$(5)="GAS GRENADES     "
8030 M$(6)="EXPLOSIVES       "
8035 M$(7)="A SONIC GUN      "
8040 M$(8)="A FORCE GENERATOR"
8045 M$(9)="GAS GRENADES     "
8050 M$(10)="A LASER GUN      "
8055 M$(11)="A SONIC GUN      "
8060 M$(12)="GAS GRENADES     "
8065 M$(13)="EXPLOSIVES       "
8070 M$(14)="A WELDING TORCH  "
8075 M$(15)="A FORCE GENERATOR"
8100 REM EFFECTS
8100 E$(0)="NO EFFECT"
8110 E$(1)="STUNNED"
8115 E$(2)="STUNNED"
8120 E$(3)="STUNNED"
8125 E$(4)="SLIGHT DAMAGE"
8130 E$(5)="SLIGHT DAMAGE"
8135 E$(6)="MINOR DAMAGE"
8140 E$(7)="MINOR DAMAGE"
8145 E$(8)="MINOR DAMAGE"
8160 E$(9)="MINOR DAMAGE"
8165 E$(10)="MAJOR DAMAGE"
8170 E$(11)="MAJOR DAMAGE"
8175 E$(12)="MAJOR DAMAGE"
8180 E$(13)="MAJOR DAMAGE"
8185 E$(14)="MAJOR DAMAGE"
8190 E$(15)="MAJOR DAMAGE":RETURN
9000 CLS
9007 PRINTTAB(13)"alien":PRINTTAB(9)"by p.merriman":POKE16429,46:POKE16427,32:PRINT
9012 PRINT"  YOU ARE ON BOARD THE TRANSPORTSHIP 'NOSTRODAMUS' EARTHWARD"
9013 PRINT"BOUND, WHEN YOU PICK UP A MAYDAYSIGNAL FROM A NEARBY PLANET."
9015 PRINT"  WHEN YOU INVESTIGATE YOU      DISCOVER AN ANCIENT ALIEN SHIP"
9016 PRINT"AND WHILE EXPLORING IT, ONE OF  THE CREW MEMBERS IS ATTACKED."
9020 PRINT"  YOU MANAGE TO GET HIM BACK TO THE SHIP WITH A STRANGE LIVING"
9021 PRINT"CREATURE ATTACHED TO HIS FACE.":PRINT
9024 PRINTTAB(8)"enter level (1-3)";
9025 I$=INKEY$:IFI$="1"THEND=2:GOTO9030
9026 IFI$="2"THEND=3:GOTO9030
9027 IFI$="3"THEND=5:GOTO9030
9029 GOTO9025
9030 CLS:PRINT"  EVENTUALLY THE CREMAN DIES ANDTHE ORGANISM WHICH HAS BEEN"
9031 PRINT"GROWING IN HIS STOMACH ESCAPES  AND GROWS AT AN PHENOMENAL RATE.";
9032 PRINT"  EARTH FORBIDS YOU TO RETURN   WITH THE CREATURE STILL ALIVE SO";
9033 PRINT"YOU MUST TRY AND KILL IT.":PRINT
9034 PRINT"THE GAME SIMULATES YOUR ATTEMPTSTO DO SO AS YOU STALK ROUND THE"
9035 PRINT"CRAFT HOPING THE WEAPON YOU HAVEPICKED UP WILL WORK AGAINST THE":PRINT"CREATURE."
9036 PRINT"         press any key"
9037 IFINKEY$=""THEN9037
9038 CLS
9040 PRINT"          HOW TO PLAY"
9045 PRINT"          -----------"
9050 PRINT"MOVEMENT IS IN EIGHT DIRECTIONS USING THE KEYS SHOWN BELOW"
9055 PRINT"Q  W  E     *  !  *"
9057 PRINT"             "CHR$(92)" ! / "
9058 PRINT"              "CHR$(92)"!/  "
9060 PRINT"A  S  D  =  *--+--*" 
9065 PRINT"              /!"CHR$(92)
9067 PRINT"             / ! "CHR$(92)
9070 PRINT"Z  X  C     *  *  *"
9075 PRINT"WHERE 'S' CORRESPONDS TO YOUR   POSITION."
9080 PRINTTAB(9)"press any key"
9085 IFINKEY$=""THEN9085
9090 CLS 
9095 PRINT"  YOU ARE SHOWN AS A SMALL PIXELNEAR THE BRIDGE. THE VARIOUS"
9096 PRINT"LETTERS SCATTERED AROUND THE    SHIP ARE WEAPONS, YOU DON'T"
9097 PRINT"KNOW WHICH PARTICULAR ONES UNTILYOU PICK THEM UP."
9100 PRINT"  TO DO SO YOU SIMPLY MOVE      THROUGH THE LETTER AND YOU WILL"
9101 PRINT"PICK IT UP AUTOMATICALLY.YOU CANHOWEVER ONLY CARRY ONE OBJECT ATA TIME."
9102 PRINT"  THE DISPLAY WILL INDICATE THE NAME OF THE WEAPON, THOUGH YOU"
9103 PRINT"WON'T KNOW ITS EFFECT ON THE    ALIEN UNTIL YOU TRY IT OUT."
9106 PRINTTAB(9)"press any key";
9107 IFINKEY$=""THEN9107
9109 CLS 
9110 PRINT"  TO LEAVE THE WEAPON JUST MOVE OVER ONE OF THE OTHER WEAPONS."
9111 PRINT"  IF THERE IS NO ROOM FOR IT"
9112 PRINT"TO BE DROPPED THEN IT WILL BE   DESTROYED."
9115 PRINT"  IN ORDER TO USE A WEAPON      AGAINST THE ALIEN SHE MUST BE"
9116 PRINT"WITHIN FIVE MOVES OF YOU AND    THERE MUST BE A CLEAR LINE OF"
9117 PRINT"SIGHT (WHICH MEANS NO WEAPONS   OR WALLS IN THE WAY)."   
9120 PRINTTAB(9)"press any key"
9122 IFINKEY$=""THEN9122
9125 CLS 
9130 PRINT"  ONCE THESE CONDITIONS HAVE    BEEN SATISFIED YOU FIRE BY"
9131 PRINT"PRESSING THE space KEY AND THEN WHILE YOUR PIXEL IS FLASHING,"
9132 PRINT"PRESSING A DIRECTION KEY."
9133 PRINT"  THE ALIEN IS ALSO DISPLAYED   AS A SINGLE PIXEL BUT IT CAN"
9134 PRINT"ONLY BE SEEN WHEN IT'S WITHIN   ABOUT TEN MOVES OF YOU. IN "
9135 PRINT"GENERAL IT WILL MOVE DIRECTLY   TOWARDS YOU  NAVIGATING ITS WAY AROUND WALLS ETC."
9140 PRINTTAB(9)"press any key"
9142 IFINKEY$=""THEN9142
9143 CLS 
9145 PRINT"  THE CREATURE STARTS WITH 200  POINTS BUT ANY HITS WILL REDUCE"
9146 PRINT"THIS VALUE. WHEN THE POINTS     GO BELOW ZERO THE CREATURE IS   DEAD."
9150 PRINT"  WHEN ALL FOUR MEMBERS OF YOUR CREW HAVE BEEN KILLED THE GAME  ENDS. THE SQUARE ü IS AN"
9151 PRINT"ENERGY BARRIER THROUGH WHICH    ONLY YOU CAN PASS.":PRINTTAB(10)"GOOD LUCK..."
9155 PRINT"TO START THE GAME PRESS KEY s.  TO GET THE INSTRUCTIONS AGAIN   PRESS KEY r."
9160 I$=INKEY$:IFI$="S"THENRETURN
9165 IFI$="R"THEN9000
9170 GOTO9160
9200 REM RE-MAKE FOR THE MC-10
9210 REM BY JIM GERRIE
9220 REM INSPIRED BY A ZX-81
9230 REM GAME BY P. MERRIMAN
