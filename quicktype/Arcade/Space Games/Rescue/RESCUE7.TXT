0 CLEAR2000:CLS0:GOTO1000
3 IFX>3THENX1=X-2
4 RETURN
5 IFX>2THENX1=X-1
6 RETURN
10 IFX<30THENX1=X+2
11 RETURN
12 IFX<30THENX1=X+1
13 RETURN
14 BU=1:RETURN
15 CLS0:POKE49151,0:D1=0:SC=0:SL=3:T=1
20 S=25:D=-1:P1=6:P2=0:SH$=CHR$(166)+CHR$(169):FL$=CHR$(148)+CHR$(152):BC$=CHR$(128):BL$=BC$+BC$
25 FORI=13TO15:PRINT@I*32+1,"x";:POKEMC+I*32+31,24:NEXT
30 L=RND(19)+4:PRINT@447+L,BC$CHR$(135)CHR$(143)CHR$(143)CHR$(139);
31 PRINT@479+L,CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(143)CHR$(143);:B=0:G=0
40 PRINT@479+L,SC;:FORAZ=1TOSL:SOUND37,1:SOUND1,1:NEXT
50 S=S+D:M1=S-1:M2=S-1+32:PRINT@M1,M1$;:PRINT@M2,M2$;:IFS<2ORS>25THEND=-D
65 IFPEEK(PK)ANDPEEK(2)THEN80
66 ?@32*5,MID$(A$,T,32);:?@32*7,MID$(B$,96-T,32);:?@32*9,MID$(C$,T,32);:?@32*11,MID$(D$,96-T,32);:T=T+1:IFT>95THENT=1
70 FORW=1TO5:NEXT:GOTO50
80 X=S+3:Y=1
100 BU=0:U=0:X1=X:ONK(PEEK(PK)ANDPEEK(2))GOSUB3,5,10,12,14,14:Y1=Y+1+RR:IFRR<0THENRR=RR+.5:IFRR>0THENRR=0
130 FORI=X1-1TOX1:A=PEEK(MC+(I+(INT(Y1)*32))):IFA>178THENI=X1:NEXT:GOTO500
135 NEXT:IFPEEK(MC+X+INT(Y+1)*32)>178ORPEEK(MC+X-1+INT(Y+1)*32)>178THEN500
140 PRINT@X-1+INT(Y)*32,BL$;:PRINT@X+INT(Y)*32+31,BL$;
141 ?@32*5,MID$(A$,T,32);:?@32*7,MID$(B$,96-T,32);:?@32*9,MID$(C$,T,32);:?@32*11,MID$(D$,96-T,32);:T=T+1:IFT>95THENT=1
142 X=X1:Y=Y1:PRINT@X-1+INT(Y)*32,SH$;:IFBU=1THENRR=RR-1:PRINT@X+INT(Y)*32+31,FL$;:IFRR<-2THENRR=-2
150 IFX=L+2ANDINT(Y)=13THEN400
155 IFY>13ORY<1THEN500
160 FORW=1TO25:NEXT:GOTO100
200 X=L+2:Y=13
205 FORI=1TOD1*15+20:PRINT@(1+RND(9))*32+RND(31),CHR$(179);:NEXT:PRINT@479+L,SC;
207 PRINT@447+L,BC$;CHR$(135)CHR$(143)CHR$(143)CHR$(139);
210 X1=X:Y1=Y
215 ONK(PEEK(PK)ANDPEEK(2))GOSUB3,5,10,12
220 C=C+1:IFC=3THENC=0:Y1=Y-1
225 FORI=X1-1TOX1:A=PEEK(MC+I+INT(Y1)*32):IFA>178THEN500
227 NEXT
230 PRINT@X-1+INT(Y)*32,BL$;:PRINT@X+INT(Y)*32+31,BL$;:X=X1:Y=Y1
231 PRINT@X-1+INT(Y)*32,SH$;:PRINT@X+INT(Y)*32+31,FL$;
240 IF(PEEK(PK)ANDPEEK(2))=32ANDY>3.5THENGOSUB340
250 IFG=1THENGOSUB350
255 IFINT(Y)>1THEN210
270 IFX<>S+3THEN500
275 P2=P2+1
280 FORI=2TO12:PRINT@I*32,;:FORZ=1TO32:PRINTBC$;:NEXT:NEXT
281 FORI=13TO15:PRINT@I*32+2,;:FORZ=1TO29:PRINTBC$;:NEXT:NEXT
285 IFP1=0THENP1=6:D1=D1+1:GOTO650
290 GOTO30
320 RETURN
340 FORAZ=5TO35STEP10:SOUNDAZ,1:NEXT:IFG=1THENPRINT@XM+INT(YM)*32,BC$;
345 G=1:XM=X-1:YM=Y
350 PRINT@XM+INT(YM)*32,BC$;:YM=YM-1
360 A=PEEK(MC+XM+INT(YM)*32):IFA>178THENGOSUB390:RETURN
370 PRINT@XM+INT(YM)*32,CHR$(197);:IFYM<3THENPRINT@XM+INT(YM)*32,BC$;:G=0
380 RETURN
390 PRINT@XM+INT(YM)*32,BC$;:G=0:SC=SC+20:PRINT@479+L,SC;:RETURN
395 RETURN
400 PRINT@X+INT(Y)*32+31,"OK";:SC=SC+50:PRINT@L+479,SC;
405 Y=19-P1:X=31:X1=29:X2=L+5:IFP1<4THENY=16-P1:X=1:X1=2:X2=L-2
410 FORI=1TO4:FORJ=24TO25:POKEMC+Y*32+X,J:FORW=1TO30:NEXTW,J
420 PRINT@Y*32+X1,"OK";:IFI/2=INT(I/2)THENGOSUB480
430 NEXTI:POKEMC+(Y*32+X),128
440 FORI=X+SGN(X1-X)TOX2STEPSGN(X2-X)
450 FORJ=24TO25:POKEMC+480+I,J:FORW=1TO30:NEXT
455 NEXT:POKEMC+480+I,128:NEXT
460 X=I+SGN(X1-X2):Y=15:FORI=1TO2:X=X+SGN(L-X1):Y=Y-1
465 FORJ=24TO25:POKEMC+Y*32+X,J:FORW=1TO40:NEXTW:POKEMC+Y*32+X,128:NEXTI
470 P1=P1-1:U=1:GOTO200
480 PRINT@Y*32+X1,BL$;:FORAY=77TO7STEP-7:SOUNDAY,1:NEXT:FORAY=7TO77STEP7:SOUNDAY,1:NEXT
485 RETURN
500 B=0:E=0:RR=0:IFY>13THENY=13
510 IFY<1THENY=0
520 X1=X-1:X2=X+1:X0=X:YS=2:PRINT@X-1+(INT(Y)*32),BL$;:PRINT@X+INT(Y)*32+31,BL$;
530 FORI=INT(Y)TO14:PRINT@X2+I*32,CHR$(169);:PRINT@X1+I*32,CHR$(166);:IFU=1THENPRINT@X0+I*32,"x";
540 SOUNDRND(255),RND(2):YS=YS-.25:IFYS<0THENYS=0
550 PRINT@X1+I*32,BC$;:PRINT@X2+I*32,BC$;:IFU=1THENPRINT@X0+I*32,BC$;
560 X1=X1-YS:IFX1<2THENX1=2
570 X2=X2+YS:IFX2>30THENX2=30
580 NEXT:SL=SL-1:IFSL=0THEN600
590 GOTO280
600 CLS:PRINT@139,"GAME OVER";:PRINT@263,"YOUR SCORE IS";SC;
601 IFSC>HSTHENHS=SC:POKE49151,64
605 PRINT@199,"HIGH SCORE IS";HS;:A$="":B$="":C$="":D$=""
606 FORT=1TO96:A$=A$+R$(RND(30)):B$=B$+R$(RND(30)):C$=C$+R$(RND(30)):D$=D$+R$(RND(30)):NEXT
607 A$=A$+MID$(A$,2,32):B$=B$+MID$(B$,2,32):C$=C$+MID$(C$,2,32):D$=D$+MID$(D$,2,32):T=1
610 PRINT@322,"PRESS (ENTER) TO PLAY AGAIN";
620 IF(PEEK(PK)ANDPEEK(2))<>13THEN620
625 GOTO15
650 CLS0
660 PRINT@205,"*BONUS*";:PRINT@267,"SCORE=";SC;
670 FORI=1TOP2:PRINT@329+I*2,"x";:SC=SC+50:PRINT@273,SC;
680 FORAZ=ITO10:SOUNDI*7+15,1:SOUNDDI*7+30,1:NEXT:FORW=1TO40:NEXT:NEXT
690 FORAZ=ITO50:SOUND51-AZ,1:SOUNDAZ,1:NEXT
691 IFP2=6THENPRINT@203,"BONUS SHIP!";:SL=SL+1:FORP2=1TO4:FORAZ=40TO60STEP3:SOUNDAZ,1:SOUNDAZ+25,1:NEXT:NEXT
695 A$="":B$="":C$="":D$=""
696 FORT=1TO96:A$=A$+R$(RND(30)):B$=B$+R$(RND(30)):C$=C$+R$(RND(30)):D$=D$+R$(RND(30)):NEXT
697 A$=A$+MID$(A$,2,32):B$=B$+MID$(B$,2,32):C$=C$+MID$(C$,2,32):D$=D$+MID$(D$,2,32):T=1
700 GOTO20
1000 DIMK(255),PK,MC,M1,M2,S,D,A,B,C,E:POKE49151,64
1001 PK=17023:K(44)=1:K(65)=2:K(46)=3:K(83)=4:K(30)=5:K(87)=6:K(32)=7:MC=16384
1005 DIMR$(30),BU,T:K(74)=2:K(76)=4:K(73)=6
1100 CLS0:PRINT@141,"space";:PRINT@173,"rescue";:POKEMC+146,32:Z=16*RND(7)
1105 PRINT@260,"ORIGINAL BY MATT RUTTER";:PRINT@294,"MC-10 BY JIM GERRIE";
1110 PRINT@289+64,"USE: AWS OR JIL KEYS TO STEER";
1111 PRINT@289+96,"SPACE = LAUNCH OR FIRE LASER ";
1112 M1$=CHR$(128)+CHR$(141)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(142)+CHR$(128)
1113 M2$=CHR$(128)+CHR$(128)+CHR$(141)+CHR$(128)+CHR$(128)+CHR$(142)+CHR$(128)
1115 PRINT@108,CHR$(143+Z)CHR$(140+Z)CHR$(140+Z)CHR$(140+Z)CHR$(140+Z)CHR$(140+Z)CHR$(140+Z)CHR$(143+Z);
1120 PRINT@140,CHR$(143+Z);:PRINT@147,CHR$(143+Z);:PRINT@172,CHR$(143+Z);:PRINT@179,CHR$(143+Z);
1121 PRINT@204,CHR$(143+Z)CHR$(131+Z)CHR$(131+Z)CHR$(131+Z)CHR$(131+Z)CHR$(131+Z)CHR$(131+Z)CHR$(143+Z);
1122 IFT=1THENPRINT@485,"PRESS ANY KEY TO BEGIN";:GOTO1130
1126 R$(1)=CHR$(179):R$(2)=CHR$(188):FORT=2TO29:R$(T+1)=CHR$(128):NEXT
1127 FORT=1TO96:A$=A$+R$(RND(30)):B$=B$+R$(RND(30)):C$=C$+R$(RND(30)):D$=D$+R$(RND(30)):NEXT
1128 A$=A$+MID$(A$,2,32):B$=B$+MID$(B$,2,32):C$=C$+MID$(C$,2,32):D$=D$+MID$(D$,2,32):T=1
1130 Z=16*RND(7):IF(PEEK(PK)ANDPEEK(2))=0THEN1115
1160 CLS0:POKE49151,0:GOTO15
