0 CLS:CLEAR8700:GOSUB4000:GOSUB4100:GOTO1000
1 FORY=P+NTOESTEPR:ONK(PEEK(Y))GOTO6:NEXT:RETURN
2 FORY=PTOESTEPR:ONK(PEEK(Y))GOTO6:NEXT:RETURN
3 P=P-Q:H=H-1:RETURN
4 P=P+Q:H=H+1:RETURN
5 X=H:S=1:A=109:N=1:U=P+N:I=0:RETURN
6 POKEY,V:U(M)=Y:M(M)=1:Y=E:NEXT:RETURN
7 X=H:S=1:A=106:N=33:U=P+N:I=1:RETURN
8 U(M)=U(M)-33:ONH(PEEK(U(M)))GOTO10,11,11:POKEU(M),J:RETURN
9 FORS=STOS+11:L=PEEK(U):POKEU,A:X=X+I:ONH(L)GOTO51,51,52,50,49:POKEU,V:U=U+N:NEXT:NEXT:GOTO25
10 M(M)=1+RND(2):RETURN
11 POKEP,42:SOUND1,1:LV=LV-1:S$=STR$(LV):PRINT@29,S$;:FORT=MC+29TOMC+28+LEN(S$):POKET,PEEK(T)-F:NEXT
12 IFLV=0THENZZ=5:Z=100:FU=-1
13 GOTO10
16 FU=FU+1:S$=STR$(FU):PRINT@21,S$;:FORT=MC+21TOMC+20+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
20 FORZZ=1TO1:FORZ=1TO100:ONK(PEEK(K)ANDPEEK(2))GOSUB3,4,5,7:B$=MID$(B$+B$(C(Z,0)),2):C$=MID$(C$+B$(C(Z,1)),2)
21 D$=MID$(D$+B$(C(Z,2)),2):E$=MID$(E$+B$(C(Z,3)),2):F$=MID$(F$+B$(C(Z,4)),2):G$=MID$(G$+B$(C(Z,5)),2)
22 H$=MID$(H$+B$(C(Z,6)),2):I$=MID$(I$+B$(C(Z,7)),2):J$=MID$(J$+B$(C(Z,8)),2):?@F,A$B$C$D$E$F$G$H$I$J$;
23 ONH(PEEK(P))GOSUB4,39,39,39,16:POKEP,D:FORM=1TO3:ONM(M)GOSUB8,2,1:NEXT:ONSGOTO9,9,9,9,9,9,9,9,9,9,9,9,9:NEXT
25 IFFU=0THENS$="RETURN TO BASE":PRINT@50,S$;:FORT=MC+50TOMC+49+LEN(S$):POKET,PEEK(T)-F:NEXT:ZZ=4:SOUND100,20
26 GOSUB61:FU=0:POKEMC+22,48:POKEMC+23,Q:NEXT:IFZZ=2THENGOSUB94:IFZZ<>6THENLV=LV+1:GOTO1080
27 POKEP,62:S$="PLAY AGAIN (Y/N)?":PRINT@32,S$;:FORT=MC+32TOMC+31+LEN(S$):POKET,PEEK(T)-F:NEXT
28 I$=INKEY$:ON1-(I$<>"")GOTO28:ON1-(I$<>"Y")GOTO1060:ON1-(I$<>"N")GOTO4230,28
30 W=VARPTR(B$):RETURN
31 W=VARPTR(C$):RETURN
32 W=VARPTR(D$):RETURN
33 W=VARPTR(E$):RETURN
34 W=VARPTR(F$):RETURN
35 W=VARPTR(G$):RETURN
36 W=VARPTR(H$):RETURN
37 W=VARPTR(I$):RETURN 
38 W=VARPTR(J$):RETURN
39 POKEP,62:S$="CRASHED!":PRINT@52,S$;:FORT=MC+52TOMC+51+LEN(S$):POKET,PEEK(T)-F:NEXT:FU=-1
40 ZZ=6:Z=100:SOUND1,10:SOUND10,10:RETURN
49 GOSUB16
50 POKEU,159:ONXGOSUB30,31,32,33,34,35,36,37,38:POKE(PEEK(W+2)*O)+PEEK(W+3)+S+3,Q:POKEU,V:S=99:NEXT:GOSUB62:NEXT:GOTO25
51 POKEU,128:S=99:NEXT:NEXT:GOTO25
52 POKEU,D:S=99:NEXT:NEXT:GOTO25
60 S=99:NEXT:RETURN
61 SC=SC+10*FU
62 SC=SC+10:S$=STR$(SC):PRINT@0,S$;:FORT=MCTOMC-1+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
83 C(C,T)=3:RETURN
84 C(C,T)=4:RETURN
90 FORT=0TO9:IFH<TTHENC(C,T)=2:NEXT:RETURN
91 C(C,T)=1:IFH=TTHENONRND(4)GOSUB83,83,83,84:NEXT:RETURN
92 NEXT:RETURN
94 SOUND159,4:SOUND176,4:SOUND185,4:S$="":S$=S$+LEFT$(A$,8):FORT=1TO120:S$=S$+MID$("        "+CHR$(95),RND(9),1):NEXT
95 B$=B$+MID$(S$,32+RND(32),64):C$=C$+MID$(S$,32+RND(32),64):D$=D$+MID$(S$,32+RND(32),64):E$=E$+MID$(S$,32+RND(32),64)
96 F$=F$+MID$(S$,32+RND(32),64):G$=G$+MID$(S$,32+RND(32),64):H$=H$+S$:I$=I$+S$:J$=J$+S$
97 FORT=1TO64:PRINT@F,MID$(S$,T,32)MID$(S$,T,32)MID$(S$,T,32)MID$(B$,T,32)MID$(C$,T,32)MID$(D$,T,32);
98 PRINTMID$(E$,T,32)MID$(F$,T,32)MID$(G$,T,32)MID$(H$,T,32)MID$(I$,T,32)MID$(J$,T,32);
99 ONH(PEEK(P))GOSUB4,39,39,39:POKEP,D:ONK(PEEK(K)ANDPEEK(2))GOSUB3,4:NEXT:RETURN
1000 DIMG,H,I,J,L,N,Q,V,W,O,S$,LV,FU,S$,MN:K=RND(-(PEEK(9)*256+PEEK(10)))
1015 B$(1)=" ":B$(2)=CHR$(128):B$(3)="A":B$(4)="Q":Q=32:K=17023:B=65:D=126:F=64:E=16832:J=94:MC=16384:HS=0:V=96
1020 FORT=1TO96:A$=A$+" ":NEXT:O=256
1030 K(87)=1:K(90)=2:K(32)=3:K(65)=1:K(66)=4
1040 H(128)=2:H(126)=3:H(32)=1:H(65)=4:K(95)=4:H(81)=5
1060 CLS:IFSC>HSTHENHS=SC
1070 SC=0:LV=3:FU=-1:MN=0
1080 B$="":S$="":B$=LEFT$(A$,32):C$=B$:D$=B$:E$=B$:F$=B$:G$=B$:H$=B$:I$=B$:J$=B$:GOSUB2000:GOSUB3000:GOSUB3100
1085 S$=STR$(HS):PRINT@10,S$;:FORT=MC+10TOMC+9+LEN(S$):POKET,PEEK(T)-F:NEXT
1086 S$=STR$(LV):PRINT@29,S$;:FORT=MC+29TOMC+28+LEN(S$):POKET,PEEK(T)-F:NEXT
1087 S$=STR$(SC):PRINT@0,S$;:FORT=MCTOMC-1+LEN(S$):POKET,PEEK(T)-F:NEXT:GOSUB16
1090 S=0:H=4:FORT=1TO4:M(T)=2:NEXT:GOTO20
2000 P=MC+259:C=1:MN=MN+1:PRINT@256,"GET READY FOR MISSION"MN
2010 H=5+RND(4):S=(6-RND(12))/2:R=RND(25):FORG=1TOR:IFG=(R/2)THEN2030
2020 H=H+S:IFH>=0ANDH<9THENGOSUB90:GOTO2040
2030 S=(S*-1)/RND(2):H=H+S:IFH>=0ANDH<9THENGOSUB90:GOTO2040
2035 H=5+RND(4):GOSUB90
2040 C=C+1:IFC<=100THENNEXT:GOTO2010
2070 G=R:NEXT:R=66:RETURN
3000 FORT=16384TO16447:POKET,Q:NEXT:RETURN
3100 FORT=16832TO16895:POKET,128:NEXT:RETURN
4000 PRINT"   mc-scramble by jim gerrie":PRINT"FLY YOUR AIRCRAFT '>' OVER THE":PRINT"MOUNTAINOUS TERRAIN SHOOTING"
4040 PRINT"AT ANTI-AIRCRAFT EMPLACEMENTS":PRINT"'A' THAT WILL SHOOT BACK AT YOU":PRINT"UNLESS YOU DESTROY THEM WITH"
4055 PRINT"YOUR GUNS 'SPACE BAR.'  USE":PRINT"YOUR BOMBS 'B' TO DESTROY THE":PRINT"FUEL EMPLACEMENTS 'Q' OF THE"
4070 PRINT"ENEMY. YOU MUST DESTROY ENOUGH":PRINT"FUEL EMPLACEMENTS OR YOU WILL":PRINT"BE RE-CALLED FROM YOUR MISSION."
4085 PRINT"SUCCESSFULLY DESTROY ENOUGH,":PRINT"AND YOU WILL BE ASSIGNED NEW"
4095 PRINT"MISSIONS. 'A'=UP & 'Z'=DOWN."
4098 DIMA$,B$,C$,D$,E$,F$,G$,H$,I$,J$,B$(4),C(100,9),K(128),H(128),M(4),U(4),Z,M,T,K,P,D,S,A,F,U,MC,ZZ,X,Y,R,B,C,E
4099 RETURN
4100 PRINT@480,"     press any key to begin";
4200 I$=INKEY$:IFI$=""THEN4200 
4210 IFI$=""THEN4210
4220 CLS:RETURN
4230 END
