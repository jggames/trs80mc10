0 CLS:CLEAR9700:GOSUB4000:GOTO1000
1 FORY=P+NTOESTEPR:ONK(PEEK(Y))GOTO6:NEXT:RETURN
2 FORY=PTOESTEPR:ONK(PEEK(Y))GOTO6:NEXT:RETURN
3 P=P-Q:H=H-1:RETURN
4 P=P+Q:H=H+1:RETURN
5 X=H:S=1:A=109:N=1:U=P+N:I=0:RETURN
6 U(M)=Y:M(M)=1:Y=E:NEXT:RETURN
7 X=H:S=1:A=106:N=33:U=P+N:I=1:RETURN
8 U(M)=U(M)-33:ONK(PEEK(U(M)))GOTO41,11,11,10:POKEU(M),J:RETURN
9 FORS=STOS+11:L=PEEK(U):POKEU,A:X=X+I:ONK(L)GOTO50,51,52,51,49:POKEU,V:U=U+N:NEXT:NEXT:GOTO25
10 M(M)=1+RND(2):RETURN
11 GOSUB63:GOSUB12:ON1-(LV=0)GOTO10:ZZ=5:Z=100:FU=-1:GOTO10
12 S$=STR$(LV):PRINT@29,S$;:FORT=B+29TOB+28+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
16 FU=FU+1:S$=STR$(FU):PRINT@21,S$;:FORT=B+21TOB+20+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
20 FORZZ=1TO3:FORZ=1TO100:ONK(PEEK(K)ANDPEEK(2))GOSUB3,4,7,5:B$=MID$(B$+B$(B(Z)),2):C$=MID$(C$+B$(C(Z)),2)
21 D$=MID$(D$+B$(D(Z)),2):E$=MID$(E$+B$(E(Z)),2):F$=MID$(F$+B$(F(Z)),2):G$=MID$(G$+B$(G(Z)),2)
22 H$=MID$(H$+B$(H(Z)),2):I$=MID$(I$+B$(I(Z)),2):J$=MID$(J$+B$(J(Z)),2):?@F,A$B$C$D$E$F$G$H$I$J$;
23 ONK(PEEK(P))GOSUB39,39,39,4,16:POKEP,D:FORM=1TO3:ONM(M)GOSUB8,2,1:NEXT:ONSGOTO9,9,9,9,9,9,9,9,9,9,9,9,9:NEXT
25 IFFU=0THENS$="RETURN TO BASE":PRINT@50,S$;:FORT=B+50TOB+49+LEN(S$):POKET,PEEK(T)-F:NEXT:ZZ=4:SOUND100,20
26 GOSUB61:FU=0:POKEB+22,48:POKEB+23,Q:NEXT:IFZZ=4THENGOSUB93:IFLV>0THEN1080
27 POKEP,62:S$="PLAY AGAIN (Y/N)?":PRINT@32,S$;:FORT=B+32TOB+31+LEN(S$):POKET,PEEK(T)-F:NEXT
28 I$=INKEY$:ON1-(I$<>"")GOTO28:ON1-(I$<>"Y")GOTO1000:ON1-(I$<>"N")GOTO4230,28
30 W=VARPTR(B$):RETURN
31 W=VARPTR(C$):RETURN
32 W=VARPTR(D$):RETURN
33 W=VARPTR(E$):RETURN
34 W=VARPTR(F$):RETURN
35 W=VARPTR(G$):RETURN
36 W=VARPTR(H$):RETURN
37 W=VARPTR(I$):RETURN 
38 W=VARPTR(J$):RETURN
39 POKEP,62:FU=-1
40 ZZ=6:Z=100:SOUND1,10:SOUND10,10
41 RETURN
49 GOSUB16:GOSUB62
50 POKEU,159:ONXGOSUB30,31,32,33,34,35,36,37,38:POKE(PEEK(W+2)*O)+PEEK(W+3)+S+3,Q:POKEU,V:S=99:NEXT:NEXT:GOTO25
51 POKEU,128:S=99:NEXT:NEXT:GOTO25
61 SC=SC+10*FU
62 SC=SC+10:S$=STR$(SC):PRINT@0,S$;:FORT=BTOB-1+LEN(S$):POKET,PEEK(T)-F:NEXT:RETURN
63 POKEP,42:SOUND1,1:LV=LV-1:RETURN
70 IFH<0THENB(C)=2:GOTO72
71 B(C)=1:IFH=0THENB(C)=L(RND(4))
72 IFH<1THENC(C)=2:GOTO74
73 C(C)=1:IFH=1THENC(C)=L(RND(4))
74 IFH<2THEND(C)=2:GOTO76
75 D(C)=1:IFH=2THEND(C)=L(RND(4))
76 IFH<3THENE(C)=2:GOTO78
77 E(C)=1:IFH=3THENE(C)=L(RND(4))
78 IFH<4THENF(C)=2:GOTO80
79 F(C)=1:IFH=4THENF(C)=L(RND(4))
80 IFH<5THENG(C)=2:GOTO82
81 G(C)=1:IFH=5THENG(C)=L(RND(4))
82 IFH<6THENH(C)=2:GOTO84
83 H(C)=1:IFH=6THENH(C)=L(RND(4))
84 IFH<7THENI(C)=2:GOTO86
85 I(C)=1:IFH=7THENI(C)=L(RND(4))
86 IFH<8THENJ(C)=2:RETURN
87 J(C)=1:IFH=8THENJ(C)=L(RND(4))
88 RETURN
93 FORT=1TO10:SOUND185,1:NEXT
94 ZZ=17+LV*(LV<9):S$="":S$=S$+LEFT$(A$,32):FORT=1TO96:S$=S$+MID$(CHR$(95)+LEFT$(A$,16),RND(ZZ),1):NEXT
95 S$=S$+A$:B$=B$+MID$(S$,32+RND(32)):C$=C$+MID$(S$,32+RND(32)):D$=D$+MID$(S$,32+RND(32)):E$=E$+MID$(S$,32+RND(32))
96 F$=F$+MID$(S$,32+RND(32)):G$=G$+MID$(S$,32+RND(32)):H$=H$+MID$(S$,32):I$=I$+MID$(S$,32):J$=J$+MID$(S$,32)
97 FORZ=1TO129:PRINT@F,MID$(S$,Z,32)MID$(S$,Z,32)MID$(S$,Z,32)MID$(B$,Z,32)MID$(C$,Z,32)MID$(D$,Z,32);
98 PRINTMID$(E$,Z,32)MID$(F$,Z,32)MID$(G$,Z,32)MID$(H$,Z,32)MID$(I$,Z,32)MID$(J$,Z,32);
99 ONK(PEEK(P))GOSUB63,63,63,4:POKEP,D:ONK(PEEK(K)ANDPEEK(2))GOSUB3,4:NEXT:LV=LV+1:GOSUB12:RETURN
1000 CLS:IFSC>HSTHENHS=SC
1070 SC=-10:LV=3:FU=-1:MN=0:P=B+259
1080 B$="":S$="":B$=LEFT$(A$,32):C$=B$:D$=B$:E$=B$:F$=B$:G$=B$:H$=B$:I$=B$:J$=B$:GOSUB2000:GOSUB3000:GOSUB3100
1085 S$=STR$(HS):PRINT@10,S$;:FORT=B+10TOB+9+LEN(S$):POKET,PEEK(T)-F:NEXT
1086 GOSUB12:GOSUB62:GOSUB16
1090 S=0:H=(P-B-3)/32-4:FORT=1TO4:M(T)=2:NEXT:GOTO20
2000 C=1:MN=MN+1:PRINT@256,"GET READY FOR MISSION"MN
2010 H=5+RND(4):S=(6-RND(12))/2:R=RND(25):FORG=1TOR:IFG=(R/2)THEN2030
2020 H=H+S:IFH>=0ANDH<9THENGOSUB70:GOTO2040
2030 S=(S*-1)/RND(2):H=H+S:IFH>=0ANDH<9THENGOSUB70:GOTO2040
2035 H=5+RND(4):GOSUB70
2040 C=C+1:IFC<=100THENNEXT:GOTO2010
2070 G=R:NEXT:R=66:RETURN
3000 FORT=16384TO16447:POKET,Q:NEXT:RETURN
3100 FORT=16832TO16895:POKET,128:NEXT:RETURN
4000 PRINT"   mc-scramble by jim gerrie":PRINT"FLY YOUR AIRCRAFT '>' OVER THE":PRINT"MOUNTAINOUS TERRAIN SHOOTING"
4040 PRINT"AT ANTI-AIRCRAFT EMPLACEMENTS":PRINT"'A' THAT WILL SHOOT BACK AT YOU":PRINT"UNLESS YOU DESTROY THEM WITH"
4055 PRINT"YOUR GUNS 'SPACE BAR.'  USE":PRINT"YOUR BOMBS 'B' TO DESTROY THE":PRINT"FUEL EMPLACEMENTS 'Q' OF THE"
4070 PRINT"ENEMY. YOU MUST DESTROY ENOUGH":PRINT"FUEL EMPLACEMENTS OR YOU WILL":PRINT"BE RE-CALLED FROM YOUR MISSION."
4080 PRINT"NAVIGATE THE FINAL ONSLAUGHT,":PRINT"AND YOU WILL BE ASSIGNED NEW":PRINT"MISSIONS. 'A'=UP & 'Z'=DOWN."
4085 DIMA$,B$,C$,D$,E$,F$,G$,H$,I$,J$,B$(4),B(100),C(100),D(100),E(100),F(100),G(100),H(100),I(100),J(100)
4086 DIMK(128),M(4),U(4),Z,M,T,K,P,D,S,A,F,U,B,ZZ,X,Y,R,C,E,G,H,I,J,L,N,Q,L(4)
4090 DIMV,W,O,S$,LV,FU,MN:K=RND(-(PEEK(9)*256+PEEK(10))):B$(1)=" ":B$(2)=CHR$(128):B$(3)="A":B$(4)="Q":Q=32:K=17023
4095 D=126:F=64:E=16832:J=94:B=16384:HS=0:V=96:FORT=1TO96:A$=A$+" ":NEXT:O=256:K(65)=1:K(32)=4:K(90)=2:K(66)=3:K(128)=2
4099 K(126)=3:K(95)=1:K(81)=5:L(1)=3:L(2)=3:L(3)=3:L(4)=4:PRINT@485,"press any key to begin";
4200 I$=INKEY$:ON1-(I$<>"")GOTO4200:RETURN
4230 END
