0 CLS:
1 PRINTTAB(10)"SPACE DUEL":PRINTTAB(8)"DAVE EDICK 1982"
2 PRINT:PRINT"THE OBJECT OF THIS GAME IS TO "
3 PRINT"HIT THE SPACE FIGHTER AT THE TOPOF THE SCREEN AS HANY TIMES AS  POSSIBLE BEFORE IT HITS YOU 3   TIMES."
4 PRINT"TO STEER YOUR SHIP USE THE LEFT AND RIGHT ARROW KEYS.":PRINT"TO FIRE USE THE SPACE KEY.":?:?"PRESS ENTER TO BEGIN."
5 IFINKEY$<>CHR$(13)THEN5
6 REM SET UP GRAPHIC STRINGS
7 FORA=1TO32:E$=E$+CHR$(128):NEXT:V=3:SHIP$=CHR$(183)+CHR$(187):TAR$=CHR$(249)+CHR$(248)
9 REM SET UP SCREEN DISPLAY
10 CLS0:GOSUB5000:POS=450+RND(23)
50 REM SET UP BARIERS
60 DATA131,141,151,387,397,407
70 FORT=1TO6:READX
72 FORA=1TO3:PRINT@X+A,CHR$(207);:NEXTA,T
75 REM ERASE AND REPOSITION TARGET
76 PRINT@64,E$;:IFI=0THENTAR=(POS-384)+(3-RND(6)):IFTAR<65THENTAR=65
78 IFTAR>95THENTAR=95
80 PRINT@TAR,TAR$;:GOTO92
90 GOTO76
91 REM MAIN CONTROL LOOP
92 A=PEEK(17023)ANDPEEK(2):CC=CC+1:PRINT@448,E$; 
100 IFA=65THENPOS=POS-1:IFPOS<449THENPOS=449
110 IFA=83THENPOS=POS+1:IFPOS>478THENPOS=478
112 PRINT@POS,SHIP$;
115 IFI=1THEN10050
123 IFA=32ANDCC>2THEN1000
125 IFRND(9)<2THEN10000
126 GOTO76
999 REM PLAYER'S FIRE ROUTINE 
1000 TAR=POS-384:X=(TAR-64)*2+1:CC=0:FORY=25TO5STEP-4:SOUNDY*2,1:RESET(X,Y+4)
1030 IFPOINT(X,Y)=8THEN2000
1040 IFPOINT(X,Y-1)=0THENSET(X,Y,3):NEXT
1065 S=S+1:GOSUB5000:I=0:RESET(X,Y+4):GOTO90
1999 REM HIT COMPUTER'S SHIP ROUTINE
2000 RESET(X,Y):PRINT@TAR,CHR$(173)CHR$(173);:FORT=60TO150STEP10:SOUNDT,1:NEXT:S=S+1:HI=HI+1:GOSUB5000:GOTO80
4999 REM PRINT SCORE ROUTINE 
5000 PRINT@0,"SHOTS="S,"HITS="HI:RETURN
9999 REM COMPUTER'S FIRE ROUTINE
10000 TAR=POS-384:X=(TAR-64)*2+1:I=1:IFPOINT(X,9)THENI=0:GOTO90
10020 FORY=5TO24STEP4:SOUND(31-Y)*4,1:SET(X,Y,1)
10040 IFRND(9)>3THEN90
10050 RESET(X,Y):NEXT:I=0
10060 IFPOINT(X,29)=4THEN20000
10061 RESET(X,Y-4):GOTO90
19999 REM BIT PLAYER'S SHIP ROUTINE
20000 FORA=1TO8:CLSA:SOUND(10-A)*10,1:NEXT:I=0:V=V-1
20017 IFV=0THEN50000 
20020 CLS:PRINT@256,"YOU HAVE BEEN HIT.":PRINT"YOU HAVE"V"SHIPS REMAINING."
20030 RESTORE:FORT=1TO1500:NEXT:GOTO10 
50000 CLS:PRINT@235,"GAME OVER":PRINT"TOTAL SHOTS="S,"TOTAL HITS="HI:FORT=1TO500:NEXT:PRINT:PRINT"DO YOU WANT TO PLAY AGAIN?"
50010 T$=INKEY$
50030 A$=INKEY$:IFA$=""THEN50030
50040 IFA$="Y"THENRUN7
50050 IFA$="N"THENEND
50060 GOTO50030
