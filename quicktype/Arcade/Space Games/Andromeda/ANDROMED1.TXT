0 KEY OFF:COLOR 0,7:CLS:RANDOMIZE:DEFINT E,F,H,K,L-N,P,R-T,X:CE3K$="t80mnmbl4o4abgo3gl3o4d":GOSUB 10000:GOTO 10
1 SOUND 2000,3:RETURN
2 DIM SX(MX),SY(MX),ST(MX),SL(MX),TE(MX),PL(MX),RE(MX),HO(MX)
3 DIM NO(MI),EC(MI),RA(MI),TX(MI),TY(MI),MF(MI)
4 DIM PT(NP),HX(NP),HY(NP),LT(NP),FI(8):DIM LL$(10):OLNUM$="0123456789":RETURN
10 CLS:FOR I=1 TO 50*SCRW\40:LOCATE INT(RND*25)+1,INT(RND*SCRW)+1:PRINT CHR$(15);:NEXT I
11 COLOR 16:LOCATE 5,14+IND:FOR I=1 TO 11:PRINT CHR$(205);:NEXT I:LOCATE 5,13+IND:PRINT CHR$(201):LOCATE 5,25+IND:PRINT CHR$(187):FOR I=1 TO 2:LOCATE 5+I,13+IND:PRINT CHR$(186);:LOCATE 5+I,25+IND:PRINT CHR$(186):NEXT I
12 LOCATE 8,13+IND:PRINT CHR$(200);:FOR I=1 TO 11:PRINT CHR$(205);:NEXT I:PRINT CHR$(188):LOCATE 6,15+IND:PRINT"Andromeda":LOCATE ,15+IND:PRINT"Conquest ":FOR I=1 TO 2000:NEXT I:COLOR 0
13 LOCATE 15,13+IND:PRINT"Copyright 1982":LOCATE ,5+IND:PRINT"The Avalon Hill Game Company":LOCATE ,10+IND:PRINT"All Rights Reserved":LOCATE ,7+IND:PRINT"Computer Program and Audio"
14 LOCATE ,7+IND:PRINT"Visual Display Copyrighted"
16 FOR I=1 TO 3000:NEXT I:CLS:LOCATE 12,IND
17 PRINT"Number of players (max 4)?":GOSUB 8189:NP=KB-48 :IF NP<1 OR NP>4 THEN LOCATE 12,IND:GOTO 17
19 MI=NP*9:SZ=11+NP*4:MX=NP*12:GOSUB 2
23 CLS:FOR I=1 TO NP:GOSUB 8151:GOSUB 8192:PRINT"Player ";I;" what life":LOCATE,IND:PRINT"form do you"
24 LOCATE,IND: PRINT"choose?":GOSUB 8189:KB=KB-48:IF KB<1 OR KB>9 THEN LOCATE 20,IND:GOTO 24
25 PT(I)=KB:NEXT I
28 CLS:LOCATE 5,4+IND:PRINT"Stand by for Galaxy Creation":GOSUB 8234:ID=0
41 ID= ID*ABS(ID<NP)+1:PRINT" "
42 HD=(ID-1)*9+1:CLS:RK=0:SK=0:TK=0
49 FOR I=1 TO MX:IF HO(I)<>ID THEN 52
50 SK=SK+1:RK=RK+RE(I)
52 NEXT I
53 IF SK=0 THEN 41 ELSE IF SK=10 THEN 8219
55 FOR I=HD TO HD+8:MF(I)=0:IF NO(I)+EC(I)+RA(I)>0 THEN TK=TK+1
56 NEXT I:CLS:GOSUB 23000:GOSUB 8009:FOR D=1 TO 1500:NEXT:GOSUB 23000:GOSUB 8164:GOSUB 8223:GOSUB 8126:IF NN+NE+NR<>0 THEN GOSUB 8040
62 GOSUB 8009:FT=HEAD:GOSUB 8000:GOTO 80
66 GOSUB 8205:LOCATE TMAR+1,LMAR:PRINT"Fleet";FT-HD+1;"status":LOCATE ,LMAR:FOR I=1 TO 15:PRINT CHR$(196);:NEXT I:PRINT
67 LOCATE ,LMAR:PRINT"Nova Ships:";NO(FT):LOCATE ,LMAR:PRINT"Echo Ships:";EC(FT):LOCATE ,LMAR:PRINT"Rama Ships:";RA(FT)
68 LOCATE ,LMAR:PRINT"X Coord   :";TX(FT):LOCATE ,LMAR:PRINT"Y Coord   :";TY(FT):LOCATE ,LMAR:PRINT"War Power :";NO(FT)*5+RA(FT)
70 MV=5:IF NO(FT)<>0 THEN MV=3
72 IF EC(FT)<>0 THEN MV=2
74 LOCATE ,LMAR:PRINT"Max Speed :";MV:RETURN
80 GOSUB 8195:PRINT"1: Tactical":LOCATE ,LMAR:PRINT"2: Strategic":LOCATE ,LMAR:PRINT"3: Attack":LOCATE ,LMAR:PRINT"4: Probe"
81 LOCATE ,LMAR:PRINT"5: Navigation":LOCATE ,LMAR:PRINT"6: Join Fleets":LOCATE ,LMAR:PRINT"7: Fleet Control":LOCATE ,LMAR:PRINT"8: End Turn"
91 GOSUB 8189:KB=KB-48:IF KB=8 THEN 41
93 'if kb=-29 then 9500
94 IF KB<1 OR KB>7 THEN 80
96 ON KB GOSUB 2000,3000,4000,5000,6000,7000,8000:GOSUB 8009
98 GOTO 80
2000 GOSUB 8200:GOSUB 8210:FOR I=1 TO MX:IF HO(I)<>ID THEN 2008
2005 XR=SX(I):YR=SY(I):GOSUB 8031:IF FL=1 THEN PRINT CHR$(15);: IF I<10 THEN PRINT MID$(OLNUM$,I+1,1) ELSE PRINT MID$(OLNUM$,I\10+1,1);MID$(OLNUM$,I MOD 10+1,1)
2007 GOTO 2010
2008 XR=SX(I):YR=SY(I):GOSUB 8031:IF FL=1 THEN PRINT CHR$(15);
2010 NEXT I:FOR I=1 TO MI:IF I<HD OR I>HD+8 THEN 2018
2013 XR=TX(I):YR=TY(I):GOSUB 8031:IF FL=1 THEN COLOR 0,7:PRINT MID$(OLNUM$,I-HD+2,1):COLOR 0,7:FOR D=1 TO 500:NEXT D
2017 GOTO 2020
2018 XR=TX(I):YR=TY(I):GOSUB 8031:IF FL=1 THEN PRINT CHR$(232)
2020 NEXT I:XR=TX(FT):YR=TY(FT):GOSUB 8031:COLOR 16:PRINT MID$(OLNUM$,FT-HD+2,1):COLOR 0:RETURN
3000 CLS:FOR I=1 TO MX:IF HO(I)<>ID THEN 3003
3001 IF SX(I)>39 OR SY(I)>23 THEN 3004
3002 LOCATE SY(I),SX(I)*(1-(SCRW=80))+(SCRW=80):PRINT"*";:IF I<10 THEN PRINT MID$(OLNUM$,I+1,1) ELSE PRINT MID$(OLNUM$,I\10+1,1);MID$(OLNUM$,I MOD 10+1,1)
3003 IF SX(I)<40 AND SY(I)<24 THEN LOCATE SY(I),SX(I)*(1-(SCRW=80))+(SCRW=80):PRINT CHR$(15)
3004 NEXT I
3005 FOR I=HD TO HD+8:IF TX(I)>0 AND TX(I)<40 AND TY(I)>0 AND TY(I)<24 THEN LOCATE TY(I),TX(I)*(1-(SCRW=80))+(SCRW=80):COLOR 16:PRINT MID$(OLNUM$,I-HD+2,1):COLOR 0
3008 NEXT I:LOCATE 25,IND:PRINT"Press any key to return";:GOSUB 8189:CLS:GOSUB 66:RETURN
4000 GOSUB 8195:IF MF(FT)>1 THEN PRINT"Fleet";FT-HD+1;"power":LOCATE,LMAR:PRINT"already expended":GOSUB 1:FOR D=1 TO 1500:NEXT D:RETURN
4003 PRINT"Is Target a":LOCATE,LMAR:PRINT"System or":LOCATE,LMAR:PRINT"Alien fleet":LOCATE,LMAR:PRINT"(S or A)?":GOSUB 8189:IF KB=83 OR KB=115 THEN 4024
4007 IF KB<>65 AND KB<>97 THEN RETURN
4008 GOSUB 5008:IF AA=0 THEN RETURN
4009 MF(FT)=MF(FT)+1:B=NO(FT)*5+RA(FT):IF B<2 THEN RETURN
4012 EL=INT(RND*(B/2)+1):GOSUB 25000:'attack sound
4014 FL=0:FOR I=1 TO AA
4015 IF RA(FI(I))>0 THEN RA(FI(I))=RA(FI(I))-1:EL=EL-1:FL=1:IF EL<1 THEN 4023
4017 NEXT I:IF FL=1 THEN 4014
4018 FL=0:FOR I=1 TO AA
4019 IF NO(FI(I))>0 THEN NO(FI(I))=NO(FI(I))-1:EL=EL-1:FL=1:IF EL<1 THEN 4023
4021 NEXT I:IF FL=1 THEN 4018
4022 FOR I=1 TO AA:EC(FI(I))=0:NEXT I
4023 GOSUB 5008:RETURN
4024 GOSUB 5039:IF Z=0 THEN RETURN
4025 MF(FT)=MF(FT)+1:B=NO(FT)*5+RA(FT):IF B<2 THEN RETURN
4028 GOSUB 25000:PL(Z)=PL(Z)-INT(RND*(B/2)+1):IF PL(Z)<-7 THEN PL(Z)=-7
4031 GOSUB 5039:IF PL(Z)+TE(Z)>0 THEN RETURN
4033 IF NO(FT)<1 THEN RETURN
4034 COLOR 16:GOSUB 8195:PRINT CHR$(201);STRING$(15,CHR$(205));CHR$(187):FOR I=1 TO 3:LOCATE,LMAR:PRINT CHR$(186);:LOCATE,38+IND:PRINT CHR$(186):NEXT I:LOCATE,LMAR
4035 PRINT CHR$(200);STRING$(15,CHR$(205));CHR$(188);:COLOR 0:LOCATE 14,23+IND:PRINT"Nova ship ready":LOCATE,LMAR+1:PRINT"to destroy":LOCATE,LMAR+1:PRINT"System...."
4040 PRINT:LOCATE,LMAR+1:PRINT"Destroy?";:GOSUB 8189:IF KB<>89 AND KB<>121 THEN RETURN
4043 SX(Z)=300:SY(Z)=300:GOSUB 8195:PRINT:LOCATE,LMAR+3:PRINT"SYSTEM":LOCATE,LMAR+3:PRINT"DESTROYED"
4044 HO(Z)=0:PLAY"mft120o2mnl4g.g.l8gl4gp8b-.l8al4a.l8gl4g.l8f#l4g.":RETURN
5000 GOSUB 8195:PRINT"System probe":LOCATE,LMAR:PRINT"or alien fleet":LOCATE,LMAR:PRINT"probe (S or A)?":GOSUB 8189:IF KB=83 OR KB=115 THEN 5039
5007 IF KB<>65 AND KB<>97 THEN RETURN
5008 AA=0:NN=0:NE=0:NR=0:FOR I=1 TO 8:FI(I)=0:NEXT I:FOR I=1 TO MI:IF I>=HD AND I<HD+9 THEN 5018
5012 IF TX(I)<>TX(FT) OR TY(I)<>TY(FT) THEN 5018
5013 NN=NN+NO(I):NE=NE+EC(I):NR=NR+RA(I):AA=AA+1:FI(AA)=I
5018 NEXT I
5019 IF AA=0 THEN GOSUB 8195:PRINT"Probe negative";:GOSUB 1:FOR D=1 TO 1000:NEXT D:RETURN
5023 GOSUB 8205:PRINT:LOCATE,LMAR:PRINT"Probe Readout":LOCATE,LMAR:FOR D=1 TO 13:PRINT CHR$(196);:NEXT:PRINT:LOCATE,LMAR:PRINT"Player:";
5028 FOR I=1 TO AA:A=FI(I):PRINT A\9+1;:NEXT I
5032 PRINT:LOCATE,LMAR:PRINT"Fleets    :";AA:LOCATE,LMAR:PRINT"Nova Ships:";NN:LOCATE,LMAR:PRINT"Echo Ships:";NE:LOCATE,LMAR:PRINT"Rama Ships:";NR:LOCATE,LMAR:PRINT"War Power :";NN*5+NR:RETURN
5039 Z=0:FOR I=1 TO MX:IF TX(FT)=SX(I) AND TY(FT)=SY(I) THEN Z=I:GOTO 5048
5043 NEXT I:GOSUB 8195:PRINT"Probe negative":GOSUB 1:FOR D=1 TO 1500:NEXT D:RETURN
5048 GOSUB 8205:PRINT:LOCATE,LMAR:PRINT"Probe Readout":LOCATE,LMAR:FOR D=1 TO 13:PRINT CHR$(196);:NEXT:PRINT:LOCATE,LMAR:PRINT"System:";Z:LOCATE,LMAR:PRINT"Type  :";ST(Z)
5054 ON SL(Z)+1 GOTO 5055,5056,5057,5058,5059,5060,5061,5062,5063,5064
5055 SL$="None":GOTO 5065
5056 SL$="Reptilian":GOTO 5065
5057 SL$="Amphibian":GOTO 5065
5058 SL$="Mammalian":GOTO 5065
5059 SL$="Molluscan":GOTO 5065
5060 SL$="Humanoid":GOTO 5065
5061 SL$="Aquatic":GOTO 5065
5062 SL$="Insectoid":GOTO 5065
5063 SL$="Robotic":GOTO 5065
5064 SL$="Avian"
5065 LOCATE,LMAR:PRINT"Life: ";SL$:LOCATE,LMAR:PRINT"Tech  :";TE(Z):LOCATE,LMAR:PRINT"Pliability :";PL(Z)
5067 D=(TE(Z)+PL(Z))*ABS((TE(Z)+PL(Z))>0)
5068 LOCATE,LMAR:PRINT"Defenses   :";D:LOCATE,LMAR:PRINT"Resources  :";RE(Z):LOCATE,LMAR:PRINT"Colony Cost:";ABS(ST(Z)-PT(ID))+RE(Z):RETURN
6000 GOSUB 8200:XF=TX(FT):YF=TY(FT):GOSUB 8210:FOR I=1 TO MX:XR=SX(I):YR=SY(I):GOSUB 8031:IF FL=1 THEN PRINT MID$(OLNUM$,ST(I)+1,1)
6006 NEXT I:FOR I=1 TO MI:IF I>=HD AND I<HD+9 THEN 6012
6009 XR=TX(I):YR=TY(I):GOSUB 8031:IF FL=1 THEN PRINT CHR$(232)
6011 GOTO 6014
6012 XR=TX(I):YR=TY(I):GOSUB 8031:IF FL=1 THEN PRINT CHR$(236)
6014 NEXT I:XR=TX(FT):YR=TY(FT):GOSUB 8031:IF FL=1 THEN COLOR 16:PRINT CHR$(236):COLOR 0
6017 GOSUB 8195:IF MF(FT)<>0 THEN PRINT"Fleet";FT-HD+1;"power":LOCATE,LMAR:PRINT"Already expended":GOSUB 1:FOR D=1 TO 1500:NEXT D:RETURN
6023 MF(FT)=1:FOR I=1 TO MV:GOSUB 8195:PRINT"Navigation":LOCATE,LMAR:PRINT"Control":PRINT:LOCATE,LMAR:PRINT"7 8 9":LOCATE,LMAR:PRINT"4 5 6":LOCATE,LMAR:PRINT"1 2 3":PRINT
6030 LOCATE 19,LMAR:PRINT"Move";I;"?":GOSUB 8189:IF KB=53 THEN 6048
6034 KB=KB-48:IF KB<1 OR KB>9 THEN 6030
6036 BB=2-(KB+2)\3:AA=(KB+2) MOD 3 -1
6041 XR=XF:YR=YF:GOSUB 8031:IF FL=1 THEN PRINT " "
6044 XF=XF+AA:YF=YF+BB:XR=XF:YR=YF:GOSUB 8031:IF FL=1 THEN COLOR 16:PRINT CHR$(236):COLOR 0
6047 NEXT I
6048 TX(FT)=XF:TY(FT)=YF:GOSUB 66:RETURN
7000 GOSUB 8195:PRINT"which fleets":LOCATE,LMAR:INPUT;AA,BB:AA=AA+HD-1:BB=BB+HD-1:IF AA<HD OR AA>HD+8 OR BB<HD OR BB>HD+8 THEN LOCATE,LMAR:PRINT"ERROR":FOR D=1 TO 1500:NEXT D:RETURN
7004 IF TX(AA)<>TX(BB) OR TY(AA)<>TY(BB) THEN GOSUB 8195:PRINT"Fleets not at":LOCATE,LMAR:PRINT"same location!":GOSUB 1:FOR D=1 TO 1500:NEXT D:RETURN
7009 IF AA=BB THEN RETURN
7010 NO(AA)=NO(AA)+NO(BB):NO(BB)=0:EC(AA)=EC(AA)+EC(BB):EC(BB)=0:RA(AA)=RA(AA)+RA(BB):RA(BB)=0:TX(BB)=300:TY(BB)=300:FT=AA
7013 IF MF(AA)<MF(BB) THEN MF(AA)=MF(BB)
7014 GOSUB 66:RETURN
8000 GOSUB 8195:PRINT"Fleet Control":LOCATE,LMAR:PRINT"Which Fleet?":GOSUB 8189:A=KB-48:IF A<1 OR A>9 THEN 8000
8003 A=A+HD-1
8005 IF NO(A)+EC(A)+RA(A)<1 THEN GOSUB 8195:PRINT"Fleet";A-HD+1:LOCATE,LMAR:PRINT"does not respond":GOSUB 1:FL=0:GOSUB 25100:IF FL=0 THEN GOTO 41 ELSE GOTO 8000
8008 FT=A:GOSUB 66:RETURN
8009 GOSUB 8192:PRINT"Operational":LOCATE,LMAR:PRINT"Space Fleets":LOCATE,LMAR:FOR D=1 TO 12:PRINT CHR$(196);:NEXT D:PRINT:FOR I=HD TO HD+8:IF NO(I)+EC(I)+RA(I)<1 THEN 8018
8010 IF I=FT THEN COLOR 16
8016 LOCATE I-HD+16,LMAR:PRINT"F:";I-HD+1;"N:";NO(I);"E:";EC(I);"R:";RA(I);:LOCATE,IND+2:IF MF(I)=1 THEN PRINT"+";:GOTO 8019 ELSE IF MF(I)>1 THEN PRINT"*";:GOTO 8019 ELSE GOTO 8019
8018 TX(I)=300:TY(I)=300
8019 COLOR 0:NEXT I:RETURN
8022 FL=0:A=HX(ID)-XR:B=HY(ID)-YR:IF 10-A>19 OR 10-A<2 THEN RETURN
8024 IF 5-B>10 OR 5-B<1 THEN RETURN
8026 LOCATE 6-B,(10-A+1)*(1-(SCRW=80)):FL=1:RETURN
8031 FL=0:A=TX(FT)-XR:B=TY(FT)-YR:IF (10-A)>19 OR (10-A)<2 THEN RETURN
8034 IF (6-B)>11 OR (6-B)<1 THEN RETURN
8036 LOCATE 6-B,(10-A+1)*(1-(SCRW=80)):FL=1:RETURN
8040 GOSUB 8195:PRINT"How many Fleets":LOCATE,LMAR:PRINT"do you want":LOCATE,LMAR:INPUT"to create";Q:IF Q=0 THEN RETURN
8042 IF Q<0 OR Q>NN+NE+NR THEN 8040
8044 FOR I=1 TO Q
8046 GOSUB 8195:PRINT"What system":LOCATE,LMAR:PRINT"will fleet";I:LOCATE,LMAR:INPUT"launch from";B:IF B>MX THEN 8046
8048 IF HO(B)<>ID THEN GOSUB 8195:PRINT"No colony":LOCATE,LMAR:PRINT"in that system":GOSUB 1:FOR D=1 TO 1000:NEXT D:GOTO 8046
8050 Z=0:FOR J=HD TO HD+8:IF NO(J)+RA(J)+EC(J)=0 THEN Z=J:GOTO 8061
8052 NEXT J
8061 IF Z=0 THEN GOSUB 8195:PRINT"You have too":LOCATE,LMAR:PRINT"many fleets!":GOSUB 1:FOR D=1 TO 1000:NEXT D:RETURN
8063 TX(Z)=SX(B):TY(Z)=SY(B):IF I=Q THEN NO(Z)=NN:EC(Z)=NE:RA(Z)=NR:RETURN
8064 FOR A=1 TO 3:IF A=1 THEN SL$="Nova":NUM=NN ELSE IF A=2 THEN SL$="Echo":NUM=NE ELSE SL$="Rama":NUM=NR
8065 IF NUM=0 THEN B=0:GOTO 8073 ELSE GOSUB 8195:PRINT"How many ";SL$:LOCATE,LMAR:PRINT"ships go to":LOCATE,LMAR:PRINT"fleet";I;:INPUT B:IF NUM-B<0 THEN GOSUB 8217:GOTO 8065
8073 ON A GOTO 8074,8075,8076
8074 NO(Z)=B:NN=NN-B:GOTO 8077
8075 EC(Z)=B:NE=NE-B:GOTO 8077
8076 RA(Z)=B:NR=NR-B
8077 NEXT A:GOSUB 8195:NEXT I:RETURN
8090 GOSUB 8192:FL=0:PRINT"Echo Reports":LOCATE,IND:FOR D=1 TO 12:PRINT CHR$(196);:NEXT D:PRINT:FOR I=HD TO HD+8::IF EC(I)<1 THEN 8098
8093 IF TX(I)>100 OR TY(I)>100 THEN 8098
8094 FOR A=1 TO MX:IF HO(A)=ID THEN 8097
8096 IF EC(I)>0 AND TX(I)=SX(A) AND TY(I)=SY(A) THEN LOCATE,6+IND:PRINT A;:LOCATE,11+IND:PRINT RE(A);:LOCATE,16+IND:PRINT ABS(ST(A)-PT(ID))+RE(A);:LOCATE,IND:PRINT"system:";:LOCATE,10+IND:PRINT"R:";:LOCATE,15+IND:PRINT"C:"
8097 NEXT A
8098 NEXT I
8099 GOSUB 8195:PRINT"What star":LOCATE,LMAR:INPUT"system";A:IF A=0 THEN RETURN
8100 IF A<0 OR A>MX THEN 8099
8101 FOR I=HD TO HD+8:IF EC(I)>0 AND TX(I)=SX(A) AND TY(I)=SY(A) THEN 8109
8102 NEXT I:GOSUB 8195:PRINT"No colony ship":LOCATE,LMAR:PRINT"in that system":GOSUB 1:FOR D=1 TO 1000:NEXT D:GOTO 8099
8109 IF TE(A)+PL(A)>0 THEN GOSUB 8195:PRINT"Natives still":LOCATE,LMAR:PRINT"defend that":LOCATE,LMAR:PRINT"system":GOSUB 1:FOR D=1 TO 1000:NEXT D:GOTO 8099
8114 KK=ABS(ST(A)-PT(ID))+RE(A):IF AC-KK<0 THEN GOSUB 8183:GOTO 8099
8118 HO(A)=ID:TE(A)=6:SL(A)=LT(ID):RK=RK+RE(A):SK=SK+1:GOSUB 8223:AC=AC-KK:RETURN
8126 GOSUB 8172:GOSUB 8195:AC=RK:PRINT"Allocate";AC:LOCATE,LMAR:PRINT"resources":LOCATE,LMAR:FOR D=1 TO 12:PRINT CHR$(196);:NEXT D:PRINT:LOCATE,LMAR:PRINT"Establish a"
8128 LOCATE,LMAR:PRINT"Colony?";:GOSUB 8189:IF KB=121 OR KB=89 THEN GOSUB 8090
8133 GOSUB 8164:GOSUB 8195:PRINT"Allocate";AC:LOCATE,LMAR:PRINT"resources":LOCATE,LMAR:FOR I=1 TO 10:PRINT CHR$(196);:NEXT:PRINT:LOCATE 16,LMAR:INPUT"No. of Novas ",NN
8134 LOCATE 17,LMAR:INPUT"No. of Echos ",NE:LOCATE 18,LMAR:INPUT"No. of Ramas ",NR:GOSUB 8195
8136 PRINT"Number of":LOCATE 14,LMAR:INPUT"defenses ",ND:KK=NN*12+NE*10+NR*2+ND:IF AC-KK<0 THEN GOSUB 8183:GOTO 8133
8141 AC=AC-KK:IF ND=0 THEN RETURN
8142 LOCATE,LMAR:INPUT"What system ";S:IF S>MX THEN 8142
8144 IF S=0 THEN RETURN
8146 IF HO(S)<>ID THEN LOCATE,LMAR:PRINT"Not colonized":GOSUB 1:FOR D=1 TO 1200:NEXT:GOTO 8142
8148 PL(S)=PL(S)+ND:GOSUB 8164:RETURN
8151 GOSUB 8200:PRINT"1:  Reptilian":LOCATE,IND:PRINT"2:  Amphibian":LOCATE,IND:PRINT"3:  Mammalian":LOCATE,IND:PRINT"4:  Molluscan":LOCATE,IND:PRINT"5:  Humanoid":LOCATE,IND:PRINT"6:  Robotic"
8153 LOCATE,IND:PRINT"7:  Aquatic":LOCATE,IND:PRINT"8:  Insectoid":LOCATE,IND:PRINT"9:  Avian"
8159 RETURN
8164 GOSUB 8192:PRINT"Empire";ID;"statistics":LOCATE,LMAR:FOR I=1 TO 19:PRINT CHR$(196);:NEXT:PRINT
8168 Q=0:FOR I=1 TO MX:IF HO(I)=ID THEN LOCATE Q+15,6+IND:PRINT I;:LOCATE,11+IND:PRINT RE(I);:LOCATE,16+IND:PRINT PL(I)+TE(I);:LOCATE,IND:PRINT"system:";:LOCATE,10+IND:PRINT"R:";:LOCATE,15+IND:PRINT"D:";:Q=Q+1
8170 NEXT:RETURN
8172 GOSUB 8205:PRINT:LOCATE,LMAR:PRINT"Resource Costs":LOCATE,LMAR:FOR I=1 TO 14:PRINT CHR$(196);:NEXT:PRINT:LOCATE,LMAR:PRINT"Nova ships: 12":LOCATE,LMAR:PRINT"Echo ships: 10"
8174 LOCATE,LMAR:PRINT"Rama ships:  2":LOCATE,LMAR:PRINT"1 Defense :  1":LOCATE,LMAR:PRINT"Colony: Variable":RETURN
8183 GOSUB 8195:PRINT"Not enough":LOCATE,LMAR:PRINT"resources!":GOSUB 1:FOR D=1 TO 1200:NEXT:RETURN
8189 DEF SEG:POKE 106,0:G$=INKEY$
8190 IF G$="" THEN 8189 ELSE IF ASC(G$)<48 THEN 8189
8191 KB=ASC(G$):RETURN
8192 LMAR=IND:TMAR=13:FOR T=0 TO 12:LOCATE TMAR+T,1:PRINT SPC(21+IND);:NEXT:LOCATE TMAR,LMAR:RETURN
8195 LMAR=22+IND:TMAR=13:FOR T=11 TO 0 STEP -1:LOCATE TMAR+T,LMAR:PRINT SPC(17);:NEXT:LOCATE TMAR,LMAR:RETURN
8200 LMAR=IND:TMAR=1:FOR T=0 TO 11:LOCATE TMAR+T,1:PRINT SPC(21+IND);:NEXT:LOCATE TMAR,LMAR:RETURN
8205 LMAR=22+IND:TMAR=1:FOR T=0 TO 11:LOCATE TMAR+T,LMAR:PRINT SPC(18);:NEXT:LOCATE TMAR,LMAR:RETURN
8210 LOCATE 6,IND\5+2+(SCRW=80):FOR I=1 TO 1+2*SCRW\10:PRINT CHR$(196);:NEXT I:PRINT CHR$(197);:FOR I=1 TO 1+2*SCRW\10:PRINT CHR$(196);:NEXT I:PRINT"X"
8212 LOCATE 1,3+2*SCRW\10-3*(SCRW=80):PRINT"Y":FOR I=1 TO 4:LOCATE,3+2*SCRW\10-3*(SCRW=80):PRINT CHR$(179):NEXT:FOR I=6 TO 11:LOCATE I,3+2*SCRW\10-3*(SCRW=80):PRINT CHR$(179):NEXT:RETURN
8217 GOSUB 8195:PRINT"Not enough":LOCATE,LMAR:PRINT SL$;" ships":LOCATE,LMAR:PRINT"were built!":GOSUB 1:FOR D=1 TO 1200:NEXT:RETURN
8219 CLS:GOSUB 39000:LOCATE 13,9+IND:COLOR 16:PRINT"Player";ID;"has won!!":COLOR 0:END
8223 GOSUB 8200:FOR I=1 TO MX:IF HO(I)<>ID THEN 8229
8226 XR=SX(I):YR=SY(I):GOSUB 8022:IF FL=1 THEN PRINT CHR$(15);:IF I<10 THEN PRINT MID$(OLNUM$,I+1,1) ELSE PRINT MID$(OLNUM$,I\10+1,1);MID$(OLNUM$,I MOD 10 +1,1)
8228 GOTO 8231
8229 XR=SX(I):YR=SY(I):GOSUB 8022:IF FL=1 THEN PRINT CHR$(15)
8231 NEXT:RETURN
8234 FOR I=1 TO MX
8236 A=INT(RND*SZ+1)
8237 B=INT(RND*SZ+1):IF B>23 THEN 8237
8238 FOR J=1 TO I-1:IF SX(J)=A AND SY(J)=B THEN 8236
8240 NEXT J:SX(I)=A:SY(I)=B:RE(I)=INT(RND*10):ST(I)=INT(RND*10):HO(I)=0
8244 IF INT(RND*3)=1 THEN SL(I)=INT(RND*9+1):TE(I)=INT(RND*10):PL(I)=INT(RND*11)-5
8246 NEXT I
8251 FOR I=1 TO NP
8252 A=INT(RND*MX)+1:IF HO(A)<>0 THEN 8252
8254 HO(A)=I:RE(A)=10:SL(A)=PT(I):LT(I)=PT(I):TE(A)=6:PL(A)=5:PT(I)=ST(A):HX(I)=SX(A):HY(I)=SY(A):NEXT I
8269 FOR I=1 TO MI:TX(I)=300:NEXT I:RETURN
10000 SCRW=80: PRINT SPC(41);: IF POS(0) < 40 THEN SCRW=40
10010 IND = (SCRW-40)/2+1:RETURN
23000 PLAY"xce3k$;":GOSUB 8195:PRINT"Player";ID:LOCATE,LMAR:PRINT"hit any key":GOSUB 8189:RETURN
25000 N$="L32N67"
25010 FOR P=1 TO 10:PLAY "MSL64N25":NEXT
25020 FOR P=1 TO 10:PLAY N$:NEXT:PLAY "L64N66"
25030 RETURN
25100 FOR D=HD TO HD+8:IF NO(D)+RA(D)+EC(D)>0 THEN FL=1
25110 NEXT D:RETURN
39000 PLAY"mbo3c4c8c16c8o2g#8o3c8o2g#8o3e4e8e16e8c8e8c8g4e8c8g4e8c8g4g8g16g4":RETURN
