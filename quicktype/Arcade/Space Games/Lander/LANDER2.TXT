1 CLS:DIMB,X,Y,Z,SL,SY,DY,X(11),Y(11),XX,YY,CX,CY,XC,YC,V,VV,C,G,M,E,R,U,FL,M$(4),I$,CC
2 GOSUB1000:MC=16384:GOTO100
30 XC=0:YC=R:PC=3-2*R
35 GOSUB70
40 IFPC<0THENPC=PC+4*XC+6:GOTO50
45 PC=PC+4*(XC-YC)+10:YC=YC-1   
50 XC=XC+1
55 IFXC<YCTHEN35
60 IFXC=YCTHENGOSUB70 
65 RETURN
70 XX=CX+XC:YY=CY+YC:SET(XX,YY,C)
71 XX=CX-XC:YY=CY+YC:SET(XX,YY,C)
72 XX=CX+XC:YY=CY-YC:SET(XX,YY,C)
73 XX=CX-XC:YY=CY-YC:SET(XX,YY,C)
74 XX=CX+YC:YY=CY+XC:SET(XX,YY,C)
75 XX=CX-YC:YY=CY+XC:SET(XX,YY,C) 
76 XX=CX+YC:YY=CY-XC:SET(XX,YY,C)
77 XX=CX-YC:YY=CY-XC:SET(XX,YY,C)
79 RETURN
100 CLS0:M=.2:U=FL:MM=0:F=0:E=63:C=5:YY=29:FORXX=0TO7:SET(XX,YY,3):NEXT:Y=31:FORX=8TOE-1:SET(X,Y,7):NEXT
110 PRINT@480,"fuel";:CC=0:FORXX=7TO56STEP8:X(0)=XX:Y(0)=29:CC=CC+1
120 X(1)=XX+INT(RND(.)*12)-1:IFX(1)>63THENX(1)=63
125 Y(1)=10+RND(10):IF(RND(5)=1ANDF=.)OR(XX>54ANDF=.)THENX(.)=X(.)+1:X(1)=XX+8:Y(1)=29:C=4:F=1:MN=CC
130 GOSUB400:C=5:X(.)=X(1):Y(.)=Y(1):X(1)=XX+8:Y(1)=29:GOSUB400:NEXT
160 PRINT@59,CHR$(223)CHR$(223);:PRINT@91,CHR$(223)CHR$(223);
170 XX=0:YY=0:C=6:CX=55:CY=4:FORR=2TO3:GOSUB30:NEXT:FORR=1TO5:XX=53+RND(4):YY=2+RND(4):SET(XX,YY,1):NEXT
180 YY=0:FORY=26TO123STEP32:YY=YY+1:M$(YY)="":FORX=0TO3:M$(YY)=M$(YY)+CHR$(PEEK(MC+Y+X)):NEXT:NEXT
190 X=2:Y=27:L=0:V=0
200 SET(X,Y,3):SET(X+1,Y,3):SET(X+2,Y,3):SET(X,Y+1,3):SET(X+2,Y+1,3):I$=INKEY$
210 PRINT@26,M$(1);:PRINT@58,M$(2);:PRINT@90,M$(3);:PRINT@122,M$(4);:IFI$="W"THENV=V-M:E=E-U:RESET(E,31):IFE<9THENM=.:U=.
230 IFI$="D"THENL=L+M:E=E-U:RESET(E,31):IFE<9THENM=.:U=.
240 IFI$="A"THENL=L-M:E=E-U:RESET(E,31):IFE<9THENM=.:U=.
250 RESET(X,Y):RESET(X+1,Y):RESET(X+2,Y):RESET(X,Y+1):RESET(X+2,Y+1):V=V+G:Y=Y+V:X=X+L
260 IFY>27THENY=27:VV=V:V=.
265 IFX<.ORX>61ORY<.THEN380
270 ONPOINT(X,Y+2)GOTO280,280,280,350,300
280 ONPOINT(X+2,Y+2)GOTO200,200,200,350,300,200,200,200:GOTO200
300 SET(X,Y,3):SET(X+1,Y,3):SET(X+2,Y,3):SET(X,Y+1,3):SET(X+2,Y+1,3):CX=X:CY=Y:C=8:IFX>60THENCX=60
301 IFX<3THENCX=3
310 FORR=0TO3:GOSUB30:NEXT:GOSUB750:PRINT@0,"LANDER DESTROYED.";:GOTO700
350 ONPOINT(X+2,Y+2)GOTO300,300,300,355,300,300,300,300
355 ONPOINT(X,Y+2)GOTO300,300,300,360,300,300,300,300
360 IFVV>.5THEN300
370 SET(X,Y,3):SET(X+1,Y,3):SET(X+2,Y,3):SET(X,Y+1,3):SET(X+2,Y+1,3):GOSUB750:PRINT@0,"TOUCHDOWN.";
371 MM=MM+1:IFMM=2THENPRINT@32,"MISSION COMPLETE!";:SC=SC+MN*LV*100:PRINT@64,"SCORE="SC;:GOTO600
372 PRINT@32,"PRESS r TO RE-LAUNCH.";:YY=29:FORXX=0TO5:SET(XX,YY,4):NEXT
374 I$=INKEY$:IFI$<>"R"THEN374
375 GOSUB800:L=0:V=0:GOTO200
380 GOSUB750:PRINT@0,"YOU WANDERED INTO ORBIT.";:GOTO700
400 DX=X(1)-X(.):DY=Y(1)-Y(.):SX=SGN(DX):SY=SGN(DY):IFDX=.THEN550
450 SL=DY/DX:B=Y(.)-SL*X(.):FORZ=X(.)TOX(1)STEP1/SQR(SL*SL+1)*SX:X=INT(Z+.5):Y=INT(SL*Z+B+.5):SET(X,Y,C):NEXT
525 RETURN
550 X=X(.):FORY=Y(.)TOY(1)STEPSY:SET(X,Y,C):NEXT:RETURN
600 PRINT@96,"PRESS c FOR NEXT MISSION.";
610 I$=INKEY$:IFI$=""THEN610
620 IFI$="C"THEN100
630 IFI$="Q"THENEND
640 GOTO610
700 PRINT@32,"SCORE="SC;:PRINT@64,"PLAY AGAIN (Y/N)?";
710 I$=INKEY$:IFI$=""THEN710
720 IFI$="Y"THENGOSUB1000:GOTO100
730 IFI$="N"THENEND
740 GOTO710
750 FORCC=0TO2:PRINT@CC*32,"                         ";:NEXT:RETURN
800 FORCC=0TO2:PRINT@CC*32,"€€€€€€€€€€€€€€€€€€€€€€€€€";:NEXT:RETURN
1000 MN=0:IFSC>HSTHENHS=SC
1003 SC=0:PRINT@480,"HIGH SCORE=";HS;
1005 PRINT@0,"    * LUNAR LANDER RESCUE *     "
1010 PRINT"YOU MUST PILOT YOUR LUNAR LANDERTO THE RED LANDING PAD AND BACK.";
1020 PRINT"USE: ad TO STEER AND w FOR LIFT."
1030 PRINT"DIFFICULTY LEVEL (1-3)?"
2000 I$=INKEY$:R=RND(0):IFI$=""THEN2000
2010 IFI$="1"THENG=.03:FL=.7:LV=1:RETURN
2020 IFI$="2"THENG=.034:FL=.7:LV=2:RETURN
2030 IFI$="3"THENG=.038:FL=.7:LV=3:RETURN
2040 GOTO2000
3000 REM LUNAR LANDER RESCUE
3010 REM BY JIM GERRIE 2011
