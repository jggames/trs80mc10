0 ON-(HS=0)GOSUB100:CLS:D=1:G=1:LV=1:P=0:GOSUB240:X=1+RND(10):Y=RND(13):K=65:IFRND(2)=1THENK=83
1 FORZ=1TO200:NEXT:CLS:FORV=0TO14:?@32*V,CHR$(137);:?@32*V+31,CHR$(137);:NEXT:FORT=1TOG:A(T)=20+RND(10):B(T)=RND(13):NEXT
2 FORW=0TO31:?@W,CHR$(137);:?@448+W,CHR$(137);:NEXT:FORQ=1TO8+RND(4):W=RND(30):V=1+RND(12):?@32*V+W,CHR$(128);:NEXT
3 FORT=1TOG:?@32*B(T)+A(T),"#";:NEXT:?@480,"SCORE"P;:?@32*Y+X,"@";:?@493,"HIGH"HS;:?@505,"R"LV;:FORZ=1TO1450:NEXT
4 ?@32*Y+X," ";:Q=PEEK(17023)ANDPEEK(2):IFQTHENK=Q
5 X=X+(K=65)-(K=83):Y=Y+(K=87)-(K=90):IFPEEK(16384+32*Y+X)<>96THEN18
6 PRINT@32*Y+X,"@";:PRINT@485,P;:FORT=1TOG:ONC(T)GOTO7:NEXT:GOTO13
7 P=P+1:PRINT@32*B(T)+A(T)," ";:IFA(T)<XTHENA(T)=A(T)+C(T):IFPEEK(16384+32*B(T)+A(T))=128THEN16
8 IFA(T)>XTHENA(T)=A(T)-C(T):IFPEEK(16384+32*B(T)+A(T))=128THEN16
9 IFB(T)<YTHENB(T)=B(T)+C(T):IFPEEK(16384+32*B(T)+A(T))=128THEN16
10 IFB(T)>YTHENB(T)=B(T)-C(T):IFPEEK(16384+32*B(T)+A(T))=128THEN16
11 PRINT@32*B(T)+A(T),"#";:IFA(T)=XANDB(T)=YTHENT=G:NEXT:GOTO18
12 NEXT
13 ONDGOTO4,4,4:GOSUB240:LV=LV+1:D=G:ON-(LV<>20ANDLV<>40)GOTO1:G=G+1:GOSUB240:P=P+100:PRINT@237,"bonus";:SOUND100,15:GOTO1
16 ?@32*B(T)+A(T),"#";:SOUND100,2:?@32*B(T)+A(T),CHR$(255);:SOUND100,2:PRINT@32*B(T)+A(T),CHR$(128);
17 C(T)=0:D=D-1:GOTO11
18 PRINT@32*Y+X,CHR$(191);:Z=2:GOSUB300:PRINT@480,"SCORE"P;:IFP>HSTHENHS=P:PRINT@493,"HIGH"HS;
20 PRINT@505,"AGAIN?";
21 I$=INKEY$:IFI$=""THEN21
22 IFI$="Y"THEN0
23 IFI$="N"THENEND
24 GOTO21
100 DIMA(3),B(3),C(3),X,Y,T,K,Q,P:CLS:PRINT@9,"CEMETERY CHASE!";:PRINT@32*2+10,"BY JIM GERRIE":PRINT
110 PRINT"OH WHAT A NIGHTMARE! YOU (@) ARE";
120 PRINT"BEING CHASED BY A TERRIFYING"
130 PRINT"MONSTER (#) THROUGH A GRAVEYARD.";
140 PRINT"YOU MUST LURE IT INTO AN OPEN"
150 PRINT"GRAVE TO ESCAPE WHILE AVOIDING"
160 PRINT"THE SPIKED FENCE SURROUNDING THE";
170 PRINT"GRAVEYARD AND THE OPEN GRAVES."
180 PRINT"SUCCEED, AND THE NIGHTMARE "
190 PRINT"CONTINUES. FAIL, AND YOU ARE"
200 PRINT"DEAD...  20 ROUNDS=BONUS+MONSTER"
210 PRINT"      HIT ANY KEY TO BEGIN";
220 I$=INKEY$:Y=RND(0):IFI$=""THEN220
230 HS=HS+1:RETURN
240 FORT=1TOG:C(T)=1:NEXT:RETURN
300 SOUND58,2*Z:SOUND58,3*Z:SOUND58,1*Z:SOUND58,2*Z:SOUND89,3*Z:SOUND78,1*Z
310 SOUND78,3*Z:SOUND58,1*Z:SOUND58,3*Z:SOUND58,1*Z:SOUND58,4*Z
320 RETURN
