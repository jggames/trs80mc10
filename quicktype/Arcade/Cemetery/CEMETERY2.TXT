0 ONHS+1GOSUB100:CLS:P=0:X=1+RND(10):Y=RND(13):K=65:IFRND(2)=1THENK=83
1 FORZ=1TO200:NEXT:CLS:FORV=0TO14:?@32*V,CHR$(137);:?@32*V+31,CHR$(137);:NEXT
2 FORW=0TO31:?@W,CHR$(137);:?@448+W,CHR$(137);:NEXT:A=20+RND(10):B=RND(13):FORQ=1TO8+RND(4):W=RND(30):V=1+RND(12)
3 PRINT@32*V+W,CHR$(128);:NEXT:PRINT@480,"SCORE"P;:?@32*B+A,"#";:PRINT@32*Y+X,"@";:PRINT@493,"HIGH"HS;:FORZ=1TO1500:NEXT
4 ?@32*Y+X," ";:PT=PEEK(17023)ANDPEEK(2):IFPTTHENK=PT
5 X=X+(K=65)-(K=83):Y=Y+(K=87)-(K=90):L=PEEK(16384+32*Y+X):IFL<>96THEN13
6 PRINT@32*Y+X,"@";:P=P+1:PRINT@485,P;:PRINT@32*B+A," ";
7 IFA<XTHENA=A+1:L=PEEK(16384+32*B+A):IFL=128THEN15
8 IFA>XTHENA=A-1:L=PEEK(16384+32*B+A):IFL=128THEN15
9 IFB<YTHENB=B+1:L=PEEK(16384+32*B+A):IFL=128THEN15
10 IFB>YTHENB=B-1:L=PEEK(16384+32*B+A):IFL=128THEN15
11 PRINT@32*B+A,"#";:IFA=XANDB=YTHEN13
12 FORZ=1TO10:NEXT:GOTO4
13 IFP>HSTHENHS=P:PRINT@493,"HIGH"HS;
14 GOTO16
15 PRINT@32*B+A,"#";:SOUND100,1:FORZ=1TO100:NEXT:PRINT@32*B+A,CHR$(255);:SOUND100,1:PRINT@32*B+A,CHR$(128);:GOTO1
16 PRINT@32*Y+X,CHR$(191);:?@505,"AGAIN?";
17 I$=INKEY$:IFI$=""THEN17
18 IFI$="Y"THEN0
19 IFI$="N"THENEND
20 GOTO17
100 CLS:PRINT@9,"CEMETERY CHASE!";:PRINT@32*2+10,"BY JIM GERRIE":PRINT
110 PRINT"OH WHAT A NIGHTMARE! YOU (#) ARE";
120 PRINT"BEING CHASED BY A TERRIFYING"
130 PRINT"MONSTER (#) THROUGH A GRAVEYARD.";
140 PRINT"YOU MUST LURE IT INTO AN OPEN"
150 PRINT"GRAVE TO ESCAPE WHILE AVOIDING"
160 PRINT"THE SPIKED FENCE SURROUNDING THE";
170 PRINT"GRAVEYARD AND THE OPEN GRAVES."
180 PRINT"SUCCEED, AND THE NIGHTMARE "
190 PRINT"CONTINUES. FAIL, AND YOU ARE"
200 PRINT"DEAD...":PRINT
210 PRINT"      HIT ANY KEY TO BEGIN";
220 I$=INKEY$:Y=RND(0):IFI$=""THEN220
230 RETURN
