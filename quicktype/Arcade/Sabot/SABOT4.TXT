REM LOAD SPEEDUP
1 CLS:POKE16925,0:POKE16926,1:GOSUB10000:GOTO950
2 C(1)=D*32+C+M:E=1:N=1
3 F=C(E)-33-M:FORT=1TO9:E(T)=PEEK(C(E)+V(T)):IFE(T)=24THENS=S+10:E(T)=L:N=N+1:C(N)=C(E)+V(T)
4 IFE(T)=83THENE(T)=191:Z=65001
5 IFE(T)=106THENE(T)=191:IFZ<64991THENZ=65003
6 NEXT:PRINT@F,"œœœ";:PRINT@F+32,"œœœ";:PRINT@F+64,"œœœ";:FORT=1TO9:POKEC(E)+V(T),E(T):NEXT:E=E+1:IFE<=NTHEN3
7 E=.:PRINT@502,;:M$=STR$(S):GOSUB99:GOSUB190:RETURN
8 E=E+1:POKED*32+C+M,69:SOUND100,1:POKED*32+C+M,L:SOUND150,1:RETURN
18 A=A-(K=65)+(K=83):B=B-(K=87)+(K=90):RETURN
19 FORZ=1TO65000
20 K=PEEK(2)ANDPEEK(17023):IFKTHENPOKEB*32+A+M,L:A=A+(K=65)-(K=83):B=B+(K=87)-(K=90):IFPEEK(B*32+A+M)<>LTHENGOSUB18
21 POKEB*32+A+M,83:IFK=32THENIFE=.THENIFG>.THENC=A:D=B:E=1:G=G-1:PRINT@483,;:M$=STR$(G):GOSUB99:IFG=.THENZ=64991
22 ONRND(20)GOSUB23,24,25,26,23,24,25,26,27,28,29,30,31:ONEGOSUB8,8,8,8,8,8,8,8,2:NEXT:GOTO100
23 POKER*32+Q+M,L:Q=Q+(Q>AANDPEEK(R*32+Q+M-1)=L):POKER*32+Q+M,106:RETURN
24 POKER*32+Q+M,L:Q=Q-(Q<AANDPEEK(R*32+Q+M+1)=L):POKER*32+Q+M,106:RETURN
25 POKER*32+Q+M,L:R=R+(R>BANDPEEK((R-1)*32+Q+M)=L):POKER*32+Q+M,106:RETURN
26 POKER*32+Q+M,L:R=R-(R<BANDPEEK((R+1)*32+Q+M)=L):POKER*32+Q+M,106:RETURN
27 POKER*32+Q+M,L:Q=Q+(PEEK(R*32+Q+M-1)=L):POKER*32+Q+M,106:RETURN
28 POKER*32+Q+M,L:Q=Q-(PEEK(R*32+Q+M+1)=L):POKER*32+Q+M,106:RETURN
29 POKER*32+Q+M,L:R=R+(PEEK((R-1)*32+Q+M)=L):POKER*32+Q+M,106:RETURN
30 POKER*32+Q+M,L:R=R-(PEEK((R+1)*32+Q+M)=L):POKER*32+Q+M,106:RETURN
31 X=Q:Y=R:H=(X>A)-(X<A):V=(Y>B)-(Y<B)
32 P=PEEK((Y+V)*32+X+H+M):IFP=LTHENPOKEY*32+X+M,L:X=X+H:Y=Y+V:POKEY*32+X+M,215:GOTO32
33 IFP=83THENPOKEY*32+X+M,L:X=X+H:Y=Y+V:POKEY*32+X+M,191:Z=65002:POKER*32+Q+M,106:RETURN
34 POKEY*32+X+M,L
35 POKER*32+Q+M,106:RETURN
98 FORT=16384TO16415:POKET,32:NEXT:RETURN
99 PRINTM$;:T=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORP=T-LEN(M$)+1TOT:POKEP,PEEK(P)-64:NEXT:RETURN
100 IFZ=65002THENPRINT@0,;:M$="YOU WERE CAUGHT IN THE BLAST.":GOSUB99
110 IFZ=65003THENPRINT@0,;:M$="YOU WERE SHOT ON SIGHT.":GOSUB99
120 IFZ=65004THENPRINT@0,;:M$="YOU KILLED A GUARD.":GOSUB99:S=S+100:PRINT@502,;:M$=STR$(S):GOSUB99:GOTO200
125 IFZ=65001THENPRINT@0,;:M$="NOW FOR YOUR NEXT MISSION...":GOSUB99:GOTO210
130 IFS>HSTHENHS=S
140 PRINT@484,;:M$=" HIGH"+STR$(HS)+"  ":GOSUB99
150 PRINT@424,"TRY AGAIN? (Y/N)";
160 M$=INKEY$:IFM$=""THEN160
170 IFM$="N"THENEND
180 IFM$="Y"THENS=0:LV=-10:GOTO700
185 GOTO160
190 FORT=1TO10
191 X=RND(30):Y=RND(14):IFPEEK(Y*32+X+M)<>LTHEN191
195 POKEY*32+X+M,24:NEXT:POKE478+M,32:RETURN
198 Q=RND(30):R=RND(14):IFPEEK(R*32+Q+M)<>LTHEN198
199 RETURN
200 FORT=1TO1500:NEXT:GOSUB98:GOSUB198:GOTO19
210 FORT=1TO2000:NEXT:GOSUB98
700 GOSUB2000:LV=LV+10:IFLV>80THENLV=80
800 CLS6:FORT=0TO32:POKET+M,32:POKET+M+480,32:NEXT
810 FORT=0TO15:POKE32*T+M,32:POKE32*T+31+M,32:NEXT
900 FORT=1TO150-LV:X=RND(30):Y=RND(14):POKEY*32+X+M,24:NEXT:POKE478+M,32
910 A=16:B=7:E=0:GOSUB198:G=8:PRINT@486,"charges";:PRINT@483,;:M$=STR$(G):GOSUB99
920 PRINT@497,;:M$="SCORE"+STR$(S):GOSUB99:GOTO19
950 DIMV(9),E(9),C(100),F,E,N,C,A,B,X,Y,H,V,P,Q,R,K,Z:M=16384:L=223
960 T=RND(-(PEEK(9)*256+PEEK(10))):GOSUB1000:GOSUB2000:GOTO800
970 DATA60,54,55,150,246,129,126,38,25,220,247,195,1,5,131,1,1,221,252,206,67,113,236,1
980 DATA221,246,236,3,221,248,236,5,221,250,51,50,56,57,240,129,58,37,1,57,126
999 DATA-33,-32,-31,-1,0,1,31,32,33
1000 CLS:FORT=1TO9:READV(T):NEXT
1010 PRINT"S A B O T A G E -- YOU ARE THE"
1020 PRINT"SABOTEUR! YOU MOVE USING THE"
1030 PRINT"W, S, A, Z KEYS AND PLACE YOUR"
1040 PRINT"BOMBS BY PRESSING SPACE.":PRINT
1050 PRINT"THE OBJECTIVE IN THIS MISSION IS";
1060 PRINT"TO BLOW UP BOXES AND ALSO THE"
1070 PRINT"GUARD WITHOUT HURTING YOURSELF."
1080 PRINT"PLACE BOMBS, RUN AWAY, AND WE"
1090 PRINT"WISH YOU GOOD LUCK...":PRINT
1095 PRINT"DEVISED BY DON PRIESTLEY"
1096 PRINT"MC-10 VERSION JIM GERRIE":PRINT
1100 PRINT"              PRESS A KEY..."
1110 M$=INKEY$:IFM$=""THEN1110
1120 RETURN
2000 CLS:FORT=1TO10:M$=INKEY$:NEXT
2005 PRINT@0,"        PRESS ANY KEY FOR"
2010 PRINT@32,;:FORT=1TO100:NEXT
2011 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2012 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2013 PRINT"èﬂﬂ––––ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2014 PRINT"èﬂXXXXﬂﬂﬂﬂ–ﬂﬂﬂ–––ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2015 PRINT"èﬂX–ﬂﬂﬂﬂ–X–ﬂﬂXXX–ﬂ–ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2016 PRINT"èﬂX––––XXX–ﬂ–X–X–X––ﬂﬂﬂﬂ–––ﬂﬂﬂﬂè";
2017 PRINT"èﬂXXXX–X–X–X–X–X–XXﬂﬂﬂ–XXX–ﬂ–––è";
2018 PRINT"èﬂﬂﬂﬂX–X–X–X–XXXﬂX–ﬂ–X–X–X–XXX–è";
2019 PRINT"èﬂﬂ––X–XXXXX–––ﬂﬂX–XXX–X–X–X–X–è";
2020 PRINT"èﬂXXXXﬂﬂﬂﬂﬂXXX–ﬂﬂXXX–X–XXXXXXXﬂè";
2021 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂX–X–ﬂﬂﬂﬂX–X––ﬂX–X–––è";
2022 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂX–X–ﬂﬂﬂﬂXXXXﬂﬂX–XXXﬂè";
2023 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂXXXﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂXX”ﬂﬂﬂﬂè";
2024 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2030 PRINT@32,;:FORT=1TO100:NEXT
2031 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2032 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2033 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2034 PRINT"èﬂxxxxﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2035 PRINT"èﬂxﬂﬂﬂﬂﬂﬂxﬂﬂﬂxxxﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2036 PRINT"èﬂxﬂﬂﬂﬂxxxﬂﬂﬂxﬂxﬂxﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2037 PRINT"èﬂxxxxﬂxﬂxﬂxﬂxﬂxﬂxxﬂﬂﬂﬂxxxﬂﬂﬂﬂﬂè";
2038 PRINT"èﬂﬂﬂﬂxﬂxﬂxﬂxﬂxxxﬂxﬂﬂﬂxﬂxﬂxﬂxxxﬂè";
2039 PRINT"èﬂﬂﬂﬂxﬂxxxxxﬂﬂﬂﬂﬂxﬂxxxﬂxﬂxﬂxﬂxﬂè";
2040 PRINT"èﬂxxxxﬂﬂﬂﬂﬂxxxﬂﬂﬂxxxﬂxﬂxxxxxxxﬂè";
2041 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂxﬂxﬂﬂﬂﬂﬂxﬂxﬂﬂﬂxﬂxﬂﬂﬂè";
2042 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂxﬂxﬂﬂﬂﬂﬂxxxxﬂﬂxﬂxxxﬂè";
2043 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂxxxﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂxxﬂﬂﬂﬂﬂè";
2044 PRINT"èﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂè";
2050 IFINKEY$=""THEN2010
2199 RETURN
10000 FORT=0TO44:READM:POKE17227+T,M:NEXT:EXEC17227:RETURN
