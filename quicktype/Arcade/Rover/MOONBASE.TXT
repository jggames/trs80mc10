	REM SURVIVAL: WRITTEN BY    ???
	REM Published in CREATIVE COMPUTING about Jan-Feb 1982
	REM Typed in by R Logan & Ian Lycholm CBASIC 2 VER

	REM This version is for =====> CBASIC 2 <=====

	DIM T$(47)
	DIM M(42,8)
	DIM O(14)

	REM	**INITIALIZE TEXT AND MOVE MATRICES

	PRINT"WELCOME TO THE GAME OF SURVIVAL. WOULD "
	PRINT"YOU LIKE INSTRUCTIONS ?"
	INPUT D$
	IF D$="Y" THEN GOSUB 5050
	FOR I=1 TO 47
		READ T$(I)
	NEXT I
	FOR I=1 TO 42
		FOR J=1 TO 8
			READ M(I,J)
		NEXT J
	NEXT I

	REM	**INITIALIZE OTHER PROGRAM VARIABLES

	REM	** P=CURRENT POSITION       C=NO.OF ITEMS CARRIED
	REM	** T1=CURRENT ELAPSED TIME  F4=ILLUMINATOR ON FLAG
	REM	** T2=OXYGEN REMAINING      F1=METEOR SHOWER FLAG
	REM	** F0=OXYGEN IN USE FLAG    F2=OPEN SHED FLAG
	REM	** V=VISITS TO COMP ROOM    F7=BOMB DEACTIVATED FLAG
	REM	** P1=PWR IN POWER UNIT   F9=OXYGEN REQ'D IN STATION
	REM	** P2=PWR IN POWER PACK

320	LET P=1
	LET C=2
	LET T1=0
	LET T2=185
	LET P1=230
	LET P2=50
	LET V=0
	LET F0=1
	LET O(1)=21
	LET O(2)=19
	LET O(3)=99
	LET O(4)=6
	LET O(5)=32
	LET O(6)=0
	LET O(7)=38
	LET O(8)=35
	LET O(9)=0
	LET O(10)=35
	LET O(11)=99
	LET O(12)=33
	LET O(13)=34
	LET O(14)=37
	LET M(14,4)=0
	LET M(14,8)=M(14,7)+1
	LET M(2,8)=M(2,7)+1
	LET F1=0
	LET F2=0
	LET F4=0
	LET F7=0
	LET F9=0

	REM	**DISPLAY CURRENT STATUS AND LOCATION INFO

650	PRINT "ELAPSED TIME: "; T1; "MINUTES"
	IF O(11)=99 THEN PRINT "POWER UNIT: "; P1; "UNITS"
	IF O(14)=99 THEN PRINT "POWER PACK: "; P2; "UNITS"
	LET T1=T1+5
	IF O(11)=99 AND P1>5 THEN LET P1=P1-5
	IF O(14)=99 AND  P2>5 THEN LET P2=P2-5
	IF O(11)=99 AND  P1=0 THEN 3680
	IF O(14)=99 AND  P2=0 THEN 3680

730	IF T1>400 THEN 2960
	IF T1>350 THEN 3840
	IF T1>200 THEN 3740

760	IF F0=1 THEN LET T2=T2-5
	IF T2<0 THEN LET T2=0
	IF F0=0 THEN 800
	IF T2>0 THEN 840

800	IF F9=0 THEN 820
	IF P>21 THEN 2900

820	IF P<18 THEN 2900
	IF P=38 THEN 1700
840	IF P=38 THEN 3590
850	IF F0=1 THEN PRINT "OXYGEN REMAINING: "; T2; "MINUTES"
	PRINT "PRESENT LOCATION STATUS: YOU ARE"
	FOR I=M(P,7) TO M(P,8)
		PRINT T$ (I)
	NEXT I
	PRINT "##"

	REM	**DISPLAY ANY OBJECTS PRESENT	

940	IF P=2 THEN 1920
950 	FOR I=1 TO 14
		IF O(I)<>P THEN 990
		GOSUB 4410
		PRINT "THERE IS ";B$; " HERE."
990	NEXT I
	GOTO 2000

1010	REM
	REM	**READ & PROCESS KEYBOARD RESPONSE
	REM

1040	INPUT B$
	LET I=0
	IF LEN(B$)<>1 THEN GOTO 1320	

1070	IF B$="N" THEN LET I=1
	IF B$="S" THEN LET I=2
	IF B$="E" THEN LET I=3
	IF B$="W" THEN LET I=4
	IF B$="U" THEN LET I=5
	IF B$="D" THEN LET I=6
	IF B$="Q" THEN 6200
	IF I=0 THEN 1570
	IF M(P,I)=0 THEN 1270
	IF M(P,I)=99 THEN 2940
	LET Q=M(P,I)
	IF P=12 THEN 3060
	IF P=13 THEN 3180
	IF P=22 THEN 3310
	IF P=23 THEN 3420
	IF P=29 THEN 3470

1230	LET R=P
	LET P=Q
	IF P=35 THEN LET V=V+1
	GOTO 650

1270	PRINT "YOU CANNOT GO IN THAT DIRECTION!"
	GOTO 1040

	REM
	REM	** PROCESS 2 OR MORE CHARACTER COMMANDS
	REM

1320	LET C$=LEFT$(B$,3) 
	IF C$="LOO" THEN 650
	IF C$="DES" THEN 650
	IF C$="GET" THEN 2190
	IF C$="TAK" THEN 2190
	IF C$="KEE" THEN 2190
	IF C$="DRO" THEN 2580
	IF C$="LEA" THEN 2580
	IF C$="PUT" THEN 2580
	IF C$="INV" THEN 2780
	IF C$="QUI" THEN 6200
	IF C$="END" THEN 6200
	IF C$="TRA" THEN 1750
	IF C$="DIG" THEN 1860
	IF C$="DEB" THEN 1610
	IF C$="FUE" THEN 4030
	IF C$="REA" THEN 4250
	IF C$="DEA" THEN 3890
	IF C$="BLA" THEN 4110
	IF C$="UP" THEN 1640
	IF C$="DOW" THEN 1640
	IF C$="NOR" THEN 1640
	IF C$="SOU" THEN 1640
	IF C$="EAS" THEN 1640
	IF C$="WES" THEN 1640

1570	PRINT "INVALID COMMAND!"
	GOTO 1040

1590	PRINT "I CANNOT PROCESS YOUR REQUEST!"
	GOTO 1040

1610	 PRINT "ENTER LOCATION"
	INPUT P
	GOTO 650

1640	LET D$=B$
	LET B$=D$
	GOTO 1070

	REM
	REM	**PROCESS ENTRY TO HANGER FROM AIR LOCK"
	REM

1700	IF R<>39 THEN 2900
	GOTO 840

	REM
1730	REM	**PROCESS TRANSPORT COMMAND
1740	REM

1750	IF P<>36 THEN 1800
1760	IF O(8)=99 THEN 1590
1770	LET P=O(8)
1780	PRINT "BEAMING IN PROCESS."
1790	GOTO 650

1800	IF P<>O(8) THEN 1590
1810	LET P=36
1820	GOTO 1780

1830	REM
1840	REM	**PROCESS DIG COMMAND
1850	REM

1860	IF P<>10 THEN 1590
1870	LET O(9)=10
1880	GOTO 940

1890	REM
1900	REM	**DROP ILLUMINATOR IF AT OVERLOOK
1910	REM

1920	IF O(4)<>99 THEN 950
1930	LET O(4)=100
1940	PRINT "YOU DROPPED YOUR ILLUMINATOR! YOU "
1950	PRINT "CANNOT RETRIEVE IT."
1960	GOTO 950

1970	REM
1980	REM	**PROCESS ROBOT
1990	REM

2000	IF O(5)=28 THEN LET O(5)=35
2010	IF O(5)=42 THEN LET O(5)=28
2020	IF O(5)=41 THEN LET O(5)=42
2030	IF O(5)=27 THEN LET O(5)=41
2040	IF O(5)=25 THEN LET O(5)=27
2050	IF O(5)<>35 THEN 2090
2060	IF P<>28 THEN 1010
2070	LET M(28,1)=35
2080	GOTO 1010

2090	IF O(5)<>32 THEN 1010
2100	IF P<>32 THEN 1010
2110	LET O(5)=25
2120	IF O(13)=99 THEN 1010
2130	PRINT "ROBOT FAILS TO RECOGNIZE YOU. IT "
2140	PRINT "FIRES A PHASOR WEAPON AT YOU!"
2150	GOTO 2980

2170	REM	**PROCESS GET OR TAKE COMMAND

2190	GOSUB 4590
2200	IF I>0 THEN 2260
2210	IF I<0 THEN 2240
2220	PRINT "I DON'T RECOGNIZE "; RIGHT$(B$,LEN(B$)-J);"."
2230	GOTO 1040

2240	PRINT "WHAT ITEM?"
2250	GOTO 1040

2260	IF O(I)<>P THEN 2370
2270	IF C>3 THEN 2390
2280	IF I=5 THEN 2410
2290	IF I=10 THEN 2430
2300	IF I=11 THEN 2460
2310	IF I=14 THEN 2490
2320	LET C=C+1
2330	O(I)=99
2340	IF I=3 THEN LET F0=1
2350	PRINT "O.K."
2360	GOTO 1040

2370	PRINT"THERE IS NO "; RIGHT$(B$,LEN(B$)-J);" HERE!"
2380	GOTO 1040

2390	PRINT "YOU CAN'T CARRY ANY MORE!"
2400	GOTO 1040

2410	PRINT "YOU CAN'T CARRY A ROBOT!"
2420	GOTO 1040

2430	PRINT "YOU CAN'T GET THE MESSAGE, IT'S "
2440	PRINT "ON THE TERMINAL SCREEN."
2450	GOTO 1040

2460	IF O(14)=99 THEN 2520
2470	O(11)=99
2480	GOTO 2320

2490	IF O(11)=99 THEN 2520
2500	O(14)=99
2510	GOTO 2320

2520	PRINT "YOU CAN'T HAVE MORE THEN ONE"
2530	PRINT "POWER SUPPLY."
2540	GOTO 1040

2550	REM
2560	REM	**PROCESS DROP OR LEAVE COMMAND
2570	REM

2580	GOSUB 4590
2590	IF I>0 THEN 2620
2600	IF I<0 THEN 2240
2610	GOTO 2220

2620	IF O(I)<>99 THEN 2690
2630	LET C=C-1	
2640	O(I)=P
2650	IF I=3 THEN LET F0=0
2660	IF I=11 THEN 2710
2670	IF I=14  THEN 2710
2680	GOTO 2350

2690	PRINT"YOU DON'T HAVE "; RIGHT$(B$,LEN(B$)-J); "!"
2700	GOTO 1040

2710	IF P<22 THEN 2870
2720	IF P=38 THEN 2870
2730	IF F9=1 THEN 2870
2740	GOTO 2350

2760	REM	**PROCESS INVENTORY COMMAND

2780	FOR I=1 TO 14
2790		IF O(I)<>99 THEN 2820
2800		GOSUB 4410
2810		PRINT "YOU HAVE "; B$;"."
2820	NEXT I
2830	GOTO 1040

2850	REM	**PROGRAM TERMINATION PROCESSING

2870	PRINT "YOU HAVE NO POWER OR POWER PACK."
2880	PRINT "YOU HAVE FROZEN TO DEATH."
2890	GOTO 2980

2900	PRINT "OXYGRN REQUIRED HERE, NONE AVAILABLE."
2910	GOTO 2980

2920	PRINT "A NUCLEAR DETONATION HAS JUST OCCURRED."
2930	GOTO 2980

2940	PRINT "YOU HAVE FALLEN TO YOUR DEATH."
2950	GOTO 2980

2952	PRINT "YOU HAVE BEEN ZAPPED BY THE LASER."
2954	GOTO 2980

2960	PRINT "THE MOON BASE HAS JUST BEEN DESTROYED "
2970	PRINT "BY A LARGE ASTEROID."
2980	PRINT "YOU HAVE FAILED TO SURVIVE."
2990	PRINT "DO YOU WISH TO TRY AGAIN?"
3000	INPUT D$
3010	IF D$="Y" THEN 320
3020	GOTO 6200

3030	REM
3040	REM	**PROCESS METEOR SHOWER

3060	IF M(P,I)<>13 THEN 1230
3070	IF F2=1 THEN 1230
3080	PRINT "THERE IS A METEOR SHOWER, YOUR SPACE "
3090	PRINT"SUIT HAS DEVELOPED A LEAK!"
3100	GOSUB 4890
3110	IF I<>2 THEN 2980
3120	PRINT "YOUR SUIT IS NOW SEALED."
3130	LET F2=1
3140	GOTO 1230

3150	REM
3160	REM	**PROCESS LOCKED SHED
3170	REM

3180	IF M(P,I)<>22 THEN 1230
3190	IF F1=1 THEN 1230
3200	PRINT "THE SHED IS LOCKED!"
3210	GOSUB 4890
3220	IF I<>1 THEN 3260
3230	PRINT "YOU RE IN THE SHED AIR LOCK."
3240	LET F1=1
3250	GOTO 1230

3260	PRINT "YOUR ATTEMPT FAILS."
3270	GOTO 1040

3280	REM
3290	REM	**PROCESS DARK VENTILATOR SHAFT
3300	REM

3310	IF M(P,I)<>23 THEN 1230
3320	IF F4=1 THEN 1230
3330	PRINT "IT IS DANGEROUS TO PROCEED IN THE DARK!"
3340	GOSUB 4890
3350	IF I<>4 THEN 2940
3360	PRINT "THE SHAFT IS NOW ILLUMINATED."
3370	LET F4=1
3380	GOTO 1230

3390	REM
3400	REM	**PROCESS SHAFT WITH NO ILLUMINATOR
3410	REM

3420	IF O(4)<>99 THEN 2940
3430	GOTO 1230

3450	REM	**PROCESS LASER BEAM

3470	IF M(P,I)<>37 THEN 1230
3480	IF F3=1 THEN 1230
3490	PRINT "THERE IS A LASER BEAM HERE.  PASSAGE NOT "
3500	PRINT "POSSIBLE WITH BEAM PRESENT."
3510	GOSUB 4890
3520	IF I<>12 THEN 2952
3530	PRINT "THE BEAM IS NOW DEFLECTED."
3540	LET F3=1
3550	GOTO 1230

3570	REM	**PROCESS BLOWN SEAL IN SPACE STATION

3590	IF R<>29 THEN 850
3600	IF F9=1 THEN 850
3610	LET F9=1
3620	PRINT "YOU HAVE JUST BLOWN AIR SEAL IN "
3630	PRINT "SPACE STATION."
3640	GOTO 850

3660	REM	**POWER REQUIRED TESTING

3680	IF P<22 THEN 2870
3690	IF F9=1 THEN 2870
3700	GOTO 730

3720	REM	**EXPOSE DEACTIVATOR

3740	IF F5=1 THEN 760
3750	LET O(6)=14
3760	LET M(2,8)=M(2,7)
3770	LET M(14,8)=M(14,7)
3780	LET M(14,4)=2
3790	LET F5=1
3800	GOTO 760

3820	REM	**DETONATE BOMB

3840	IF F7=0 THEN 2920
3850	GOTO 760

3870	REM	**DEACTIVATE BOMB

3890	IF O(6)<>99 THEN 3940
3900	IF O(7)<>99 THEN 3980
	LET F7=1
3910	PRINT "BOMB IS NOW DEACTIVATED."
3920	GOTO 1040

3940	PRINT "YOU HAVE NOTHING TO DO WITH IT!"
3950	GOTO 1040

3960	PRINT"THERE IS NOTHING TO DO IT TO!"
3970	GOTO 1040

3980	PRINT"YOU CAN'T DO IT FROM HERE!"
3990	GOTO 1040

4010	REM	**FUEL ROCKET

4030	IF P<>19 THEN 3980
4040	IF O(9)<>99 THEN 3940
4050	LET O(9)=98
4060	PRINT "FUEL IS NOW LOADED."
4070	GOTO 1040

4090	REM	**BLASTOFF PROCESSING

4110	IF P<>21 THEN 3980
4120	IF O(9)<>98 THEN 4200
4130	IF F7=1 THEN 4160
4140	PRINT "REPAIRS NOT YET COMPLETE."
4150	GOTO 1040

4160	PRINT "CONGRATULATIONS. YOU HAVE JUST BLASTED "
4170	PRINT "OFF AND ARE ON YOUR WAY TO EARTH."
4180	PRINT "YOUR   ESCAPE TIME:"; T1; "MINUTES."
4190	GOTO 2990

4200	PRINT "YOU'RE SPACE CRAFT HAS NO FUEL!"
4210	GOTO 1040

4230	REM	**COMPUTER READOUT PROCESSING

4250	IF P<>35 THEN 3980
4260	GOSUB 4590
4270	IF I<>10 THEN 3960
4280	IF V<>0 THEN 4320
4290	PRINT "BOMB DE-ACTIVATOR LOCATED SOMEWHERE EAST"
4300	PRINT " OF SPACE STATION, ON MOON'S SURFACE."
4310	GOTO 4360

4320	IF V<>1 THEN 4350
4330	PRINT "LOCAL FUEL SOURCE: DILITHIUM CRYSTAL."
4340	GOTO 4360

4350	PRINT "DILITHIUM FOUND IN SOFT SURFACES."
4360	IF F7=1 THEN PRINT "SPACECRAFT REPAIRS COMPLETED."
4370	GOTO 1040

4390	REM	**SUBROUTINE TO DESCRIBE ITEMS AT LOCATION

4410	IF I=1 THEN LET B$="AN ELECTRONIC KEY"
4420	IF I=2 THEN LET B$="SEALANT"
4430	IF I=3 THEN LET B$="AN OXYGEN MODULE"
4440	IF I=4 THEN LET B$="AN ILLUMINATOR"
4450	IF I=5 THEN LET B$="A ROBOT"
4460	IF I=6 THEN LET B$="A DEACTIVATOR"
4470	IF I=7 THEN LET B$="A NUCLEAR BOMB"
4480	IF I=8 THEN LET B$="A TRANSPORT UNIT"
4490	IF I=9 THEN LET B$="DILITHIUM CRYSTALS"
4500	IF I=10 THEN LET B$="A  COMPUTER MESSAGE"
4510	IF I=11 THEN LET B$="A  POWER UNIT"
4520	IF I=12 THEN LET B$="A  MIRROR"
4530	IF I=13 THEN LET B$="A CODED BADGE"
4540	IF I=14 THEN LET B$="A POWER PACK"
4550	RETURN

4570	REM	**SUBROUTINE TO CONVERT AN ITEM TO A NUMERIC VALUE

4590	IF MID$(B$,4,1)=" " THEN 4640	 REM FOR J=1 TO LEN (B$)
4600	I=0 REM IF MID$(B$,J,1)=" " THEN 4640
4610	REM NEXT J
4620	REM I=J
4630	RETURN
4640	LET C$=MID$(B$,5,3)
4650	LET I=0
4660	IF C$="ELE" THEN LET I=1
4670	IF C$="KEY" THEN LET I=1
4680	IF C$="SEA" THEN LET I=2
4690	IF C$="OXY" THEN LET I=3
4700	IF C$="MOD" THEN LET I=3
4710	IF C$="ILL" THEN LET I=4
4720	IF C$="ROB" THEN LET I=5
4730	IF C$="DEA" THEN LET I=6
4740	IF C$="NUC" THEN LET I=7
4750	IF C$="BOM" THEN LET I=7
4760	IF C$="TRA" THEN LET I=8
4770	IF C$="DIL" THEN LET I=9
4780	IF C$="CRY" THEN LET I=9
4790	IF C$="COM" THEN LET I=10
4800	IF C$="MES" THEN LET I=10
4810	IF C$="UNI" THEN LET I=11
4820	IF C$="MIR" THEN LET I=12
4830	IF C$="BAD" THEN LET I=13
4840	IF C$="PAC" THEN LET I=14
4850	RETURN

4870	REM	**SUBROUTINE TO PROCESS TRY COMMAND

4890	INPUT B$
4900	REM LET C$=B$
4910	IF MID$(B$,1,3)="TRY" THEN 4950 
4920	IF MID$(B$,1,3)="USE" THEN 4950
4930	I%=-1
4940	RETURN

4950	GOSUB 4590
4960	IF I<1 THEN 5050
4970	IF O(I)=99 THEN RETURN
4980	PRINT "YOU DON'T HAVE "; RIGHT$(B$,LEN(B$)-J); "!"
4990	GOTO 4930

5000	PRINT "YOU'RE ATTEMPT FAILS!"
5010	GOTO 4930

5030	REM	**PRINT INSTRUCTIONS
5050	PRINT "YOU'VE CRASH LANDED ON THE MOON. YOU HAVE LIMITED "
5070	PRINT "SUPPLIES AND TIME IN WHICH TO SURVIVE. TO TRAVEL, YOU MAY "
5090	PRINT "ENTER DIRECTIVES SUCH AS NORTH OR N, AS WELL AS S, E, W AND "
5110	PRINT "U,AND D (UP AND DOWN). YOU WILL ENCOUNTER VARIOUS ITEMS "
5130	PRINT "AND SITUATIONS DURING YOUR TRAVELS. TO COMMUNICATE, ENTER "
5150	PRINT "COMMANDS Z(VERBS), FOLLOWED BY OBJECT NAMES, IF APPLICABLE.  "
5170	PRINT "FOR EXAMPLE, GET XXX, LEAVE, USE, AND INVENTORY"
5190	PRINT " "
5200	PRINT "ONCE YOU HAVE SURVIVED, THE  "
5210	PRINT "OBJECT THEN IS TO ACHIEVE THE "
5220	PRINT "OPTIMUM SURVIVAL TIME. GOOD LUCK!!!"
	RETURN

	REM	**TEXT LOCATION DESCRIPTIONS

	DATA "AT MARE SERENITATIA."
	DATA "ON RIM OF POSIDONIUS."
	DATA "TOTAL DARKNESS TO E."
	DATA "TWIXT DAWES & PLINIUS."
	DATA "AT PASS IN HAEMUS MTNS."
	DATA "AT CRATER MANILUS."
	DATA "AT MARE VAPORUM."
	DATA "AT MT. EUDOXUS."
	DATA "INSIDE ARISTOTELES."
	DATA "IN CHANGING AREA."
	DATA "IN LACUS SOMNORUM."
	DATA "AT LACUS MORTIS."
	DATA "THE SURFACE IS VERY SOFT HERE."
	DATA "AT E. SIDE OF MARE IMBRIUM."
	DATA "AT THE BASE OF PLATO CRATER. A "
	DATA "SHINY OBJECT IS SEEN W.  "
	DATA "BEFORE A METAL SHED. A "
	DATA "SIGN READS VENT. SHAFT #2."
	DATA "E. OF MARE SERENITATIS."
	DATA "THERE IS TOTAL DARKNESS."
	DATA "AT CRASH SITE OF A SPACE CRAFT."
	DATA "THE SHIP ENTRANCE IS BEFORE YOU."	
	DATA "AT THE CENTER OF MERE IMBRIUM."
	DATA "IN THE AIR LOCK CHAMBER OF THE SHIP."
	DATA "IN THE AFT CARGO AND FUEL STORAGE ROOM."
	DATA "IN THE ENGINE ROOM OF THE SPACECRAFT."
	DATA "IN THE SHIP'S CONTROL ROOM."
	DATA "IN AN AIR LOCK CHAMBER."
	DATA "INSIDE A VENTILATOR SHED. A LADDER "
	DATA "LEADS DOWN INTO A LARGE METAL SHAFT."
	DATA "IN A VENTILATOR PASSAGE."
	DATA "AT A VENTILATOR OPENING. THROUGH THE "
	DATA "OPENING A LIT PASSAGEWAY CAN BE SEEN."
	DATA "IN A LIGHTED SPACE STATION CORRIDOR."
	DATA "IN THE SPACE STATION INFIRMARY."
	DATA "IN THE RECREATION ROOM AND LIBRARY."
	DATA "IN THE MESS HALL. ABANDONED FOOD TRAYS "
	DATA "ARE STILL ON THE TABLES."
	DATA "IN THE STORAGE ROOM AND SUPPLY AREA."
	DATA "IN THE SLEEPING QUARTERS."
	DATA "IN AN ELEVATOR AT SUBSURFACE LEVEL."
	DATA "IN AN ELEVATOR AT SURFACE LEVEL."
	DATA "IN THE STATION CONTROL CENTER."
	DATA "IN THE TRANSPORTER ROOM."
	DATA "IN THE SPACE STATION LABORATORY."
	DATA "IN THE HANGAR AREA. THE LAUNCH AREA "
	DATA "IS JUST SOUTH OF HERE."

	REM	**MOVEMENT AND TEXT POINTER MATRIX

	DATA 07,04,02,15,00,00,01,01
	DATA 09,03,14,01,00,00,02,03
 	DATA 02,05,14,04,00,00,04,04
 	DATA 01,05,03,00,00,00,05,05
	DATA 04,00,03,06,00,00,06,06
	DATA 00,00,05,00,00,00,07,07
	DATA 08,01,09,11,00,00,08,08
	DATA 00,07,10,00,00,00,09,09
	DATA 10,02,14,07,00,00,11,11
	DATA 00,09,14,08,00,00,12,13
	DATA 12,15,07,16,00,00,14,14
	DATA 00,11,00,13,00,00,15,16
	DATA 00,16,12,22,00,00,17,18
	DATA 99,99,99,99,00,00,19,20
	DATA 11,18,01,00,00,00,21,22
	DATA 17,16,07,16,00,00,23,23
	DATA 16,17,11,17,00,00,23,23
	DATA 15,19,00,00,00,00,24,24
	DATA 18,00,20,00,00,00,25,25
	DATA 00,00,00,19,21,00,26,26
	DATA 00,00,00,00,00,20,27,27
	DATA 00,00,13,00,00,23,29,30
	DATA 24,00,00,00,22,00,31,31
	DATA 25,23,00,00,00,00,32,33
	DATA 27,26,33,32,24,00,34,34
	DATA 25,00,30,31,00,00,34,34
	DATA 34,25,41,00,00,00,34,34
	DATA 00,29,42,36,00,00,34,34
	DATA 28,38,40,37,00,00,34,34
	DATA 00,00,00,26,00,00,35,35
	DATA 00,00,26,00,00,00,36,36
	DATA 00,00,25,00,00,00,37,38
	DATA 00,00,00,25,00,00,40,40
	DATA 00,27,00,00,00,00,39,39
	DATA 00,28,00,00,24,00,43,43
	DATA 00,00,28,00,00,00,44,44
	DATA 00,00,29,00,00,00,45,45
	DATA 29,00,39,00,00,00,46,47
	DATA 40,00,00,38,00,00,28,28
	DATA 00,39,00,29,00,00,10,10
	DATA 00,00,00,27,42,00,41,41
	DATA 00,00,00,28,00,41,42,42

6200	END