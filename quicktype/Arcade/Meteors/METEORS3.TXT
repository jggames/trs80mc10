0 DIMN(21)
1 CC=1:FORC1=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):CC=CC+1
2 NEXT:GOTO10
4 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
10 REM METEORS
20 GOTO590
30 CLS
40 INPUT"SKILL LEVEL (1-3)";SK
50 IFSK<1ORSK>3THEN40
60 CLS:LV=4-SK:RA=16384+192+32*LV:SH=16384+80:LI=3:MI=0:SC=0
90 POKESH,22:FORN=0TORND(5-LV):PRINT@480+RND(30),"O";:PRINT@480+RND(30),"*";:NEXT
150 FORN=0TOLV*10:NEXT:POKESH,96:IFMI=0THEN230
160 POKEMI,143:MI=MI+32:IFMI>RATHENMI=0
190 IFPEEK(MI)=106ORPEEK(MI+32)=106THENSC=SC+5*SK:POKEMI,175:SOUND250,1:POKEMI,96:POKEMI+32,96:MI=0
200 IFPEEK(MI)=79ORPEEK(MI+32)=79THENSOUND1,1:MI=0
230 PRINT:IFPEEK(SH)=106ORPEEK(SH)=79THENGOSUB370
270 A$=INKEY$:IFA$=""THEN340
290 IFA$="S"THENSH=SH+1:IFSH>16479THENSH=16479
300 IFA$="A"THENSH=SH-1:IFSH<16448THENSH=16448
310 IFA$=" "ANDMI=0THENMI=SH
340 IFMI>0THENPOKEMI,103
350 PRINT@0,"LIVES:"LI;TAB(20)"SCORE:"SC:GOTO90
370 POKESH,191
380 SOUND100,2
390 FORN=0TO1000:NEXT
400 LI=LI-1
410 IFLI=0THEN510
420 RETURN
510 CLS:M$="G4G4G8G8B4A4A4G4G4F4G4":GOSUB4
520 PRINT@192,"YOU'RE DEAD"
530 PRINT"BUT YOU DID SCORE"SC;"POINTS!"
535 IFSC>HSTHENHS=SC
536 PRINT"HIGH SCORE IS"HS
540 PRINT"ANOTHER GO (Y/N)?"
550 A$=INKEY$
560 IFA$="Y"THEN30
570 IFA$="N"THENEND
580 GOTO550
590 CLS
600 PRINT@12,"meteors"
610 PRINT"THE IDEA OF THIS GAME IS TO"
620 PRINT"DODGE THE METEORS WHICH ARE"
630 PRINT"COMING UP THE SCREEN TOWARDS YOU";
640 PRINT"AND TO DESTROY AS MANY OF THEM"
650 PRINT"AS POSSIBLE."
660 PRINT"YOU MOVE USING THE 'A' AND"
670 PRINT"'S' ARROW KEYS AND FIRE USING"
680 PRINT"THE SPACE BAR."
690 PRINT"THE SYMBOLS USED ARE:"
700 PRINT"O - METEOR - AVOID"
710 PRINT"* - METEOR - 5 X LV POINTS"
720 PRINT"v - YOUR SHIP"
730 PRINT"' - YOUR MISSILE":PRINT
740 PRINT"     PRESS ANY KEY TO START";
750 IFINKEY$=""THEN750
760 GOTO30
