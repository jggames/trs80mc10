10 REM METEORS
20 GOTO590
30 CLS
40 INPUT"SKILL LEVEL (1-3)";SK
50 IFSK<1ORSK>3THEN40
60 CLS:RA=16384+256+32*SK
70 SH=16384+80:LI=3
80 MI=0:SC=0
90 POKESH,22:FORN=0TORND(5-SK):PRINT@480+RND(30),"O";:PRINT@480+RND(30),"*";:NEXT:POKESH,143
150 FORN=0TOSK*10:NEXT:IFMI=0THEN230
160 POKEMI,143:MI=MI+32:IFMI>RATHENMI=0
190 K=PEEK(MI):IFK=106ORK=79THENGOSUB430
200 K=PEEK(MI+32):IFK=106ORK=79THENGOSUB430
230 PRINT:IFPEEK(SH)=106ORPEEK(SH)=79THENGOSUB370
270 K=PEEK(2)ANDPEEK(17023):IFK=0THEN340
280 POKESH,143
290 IFK=83THENSH=SH+1
300 IFK=65THENSH=SH-1
310 IFK=32ANDMI=0THENMI=SH
320 IFSH>16479THENSH=16479
330 IFSH<16448THENSH=16448
340 IFMI>0THENPOKEMI,103
350 PRINT@0,"LIVES:"LI;TAB(20)"SCORE:"SC:GOTO90
370 POKESH,191
380 SOUND100,2
390 FORN=0TO1000:NEXT
400 LI=LI-1
410 IFLI=0THEN510
420 RETURN
430 IFPEEK(MI)=106ORPEEK(MI+32)=106THENSC=SC+5:SOUND250,1
450 IFPEEK(MI)=79ORPEEK(MI+32)=79THENSC=SC+10:SOUND250,2
470 POKEMI,175
480 POKEMI,143
490 POKEMI+32,143
500 MI=0:RETURN
510 CLS:REM M$="G4G4G8G8B4A4A4G4G4F4G4":GOSUB4
520 PRINT@192,"YOU'RE DEAD"
530 PRINT"BUT YOU DID SCORE";SC;"POINTS!"
540 PRINT"ANOTHER GO (Y/N)?"
550 A$=INKEY$
560 IFA$="Y"THEN30
570 IFA$="N"THENEND
580 GOTO550
590 CLS
600 PRINT@12,"meteors"
610 PRINT"THE IDEA OF THIS GAME IS TO"
620 PRINT"DODGE THE METEORS WHICH ARE"
630 PRINT"COMING UP THE SCREEN TOWARDS YOU";
640 PRINT"AND TO DESTROY AS MANY OF THEM"
650 PRINT"AS POSSIBLE."
660 PRINT"YOU MOVE USING THE 'A' AND"
670 PRINT"'S' ARROW KEYS AND FIRE USING"
680 PRINT"THE SPACE BAR."
690 PRINT"THE SYMBOLS USED ARE:"
700 PRINT"O - METEOR - 10 POINTS"
710 PRINT"* - METEOR - 5 POINTS"
720 PRINT"v - YOUR SHIP"
730 PRINT"' - YOUR MISSILE":PRINT
740 PRINT"     PRESS ANY KEY TO START";
750 IFINKEY$=""THEN750
760 GOTO30
