10 CLS:CLEAR400,36762:POKE16925,0:POKE16926,1:GOTO100
15 FORCC=0TO103:POKE36762+CC,PEEK(64022+CC):NEXT
16 FORCC=0TO4:READCR:POKE36762+CC,CR:NEXT:POKE36855,128:POKE17033,143:POKE17034,154:POKE17032,126
17 CC=8:CR=3:Z=0:RETURN
20 FORK=1TO100:NEXT:RETURN
100 REM ROAD RACE
110 REM EDIT JIM GERRIE
140 REM
150 LC=0.5
155 IZ=65:KP=17023:DS=2:DR=68:U=1
160 CR=3:CC=8
170 L$="A":R$="S"
200 RC=1-LC
210 G=16384
250 GOSUB 3000
300 GOSUB 3600
310 PRINT:T=0:N=0
320 INPUT"ROAD WIDTH (4-15)";W
330 W=INT(W):PRINT
340 IF W<4 OR W>15 THEN 310
350 PRINT"VISIBILITY CONDITIONS"
360 PRINT"  1 - TERRIBLE"
370 PRINT"  2 - BAD"
380 PRINT"  3 - FAIR"
390 PRINT"  4 - GOOD"
400 INPUT"VISIBILITY (1-4)";V
410 V=INT(V):GOSUB2000
420 IF V<1 OR V>4 THEN 400
500 N=N+1:EL=449:ER=478-W:H=0
510 Z=527-64*V:L=463-INT(W/2)
520 FORJ=1TO16:PRINT@480,B$;
530 PRINT@L,RS$;:Q$=INKEY$:NEXT
540 PRINT@Z,C$;:GOSUB4000
600 H=H+1:Q=RND(0):PRINT@Z-33,"€€€€";:PRINT@480,B$;:PRINT@Z-32,C$;
610 IFQ>RCTHENIFL<ERTHEN640
620 IFQ<LCTHENIFL>ELTHEN650
630 PRINT@L,RS$;:GOTO700
640 PRINT@L,RR$;:L=L+1:GOTO700
650 L=L-1:PRINT@L,RL$;
700 A=PEEK(KP)ANDPEEK(DS):K=Z
710 IFA=IZTHENIFZ>UTHENZ=Z-U
720 IFA=DRTHENZ=Z+U
730 IFPEEK(Z+G)<>128THEN800
740 IFPEEK(Z+G+1)<>128THEN800
750 GOTO600
800 PRINT@K-32,"€€";:FOR J=1TO 8:Q$=INKEY$
810 PRINT@Z,D$;:SOUND 150,3
820 FOR K=1 TO 10:NEXT
830 PRINT@Z,C$;
840 FOR K=1 TO 10:NEXT:NEXT
900 M=H*5:T=T+M
910 REM
920 PRINT@480,CHR$(143)
925 PRINT"YOU WENT";M;"MILES FOR A TOTAL"
930 PRINT"OF";T;"MILES IN";N;"DAY(S)":PRINT
940 PRINT"PRESS 'C' - CONTINUE RACE"
950 PRINT"      'R' - RESTART RACE"
960 PRINT"      'Q' - QUIT"
970 Q$=INKEY$
980 IF Q$="C" THEN 500
990 IF Q$="R" THEN 1010
1000 IF Q$<>"Q" THEN 970
1010 PRINT
1020 PRINT"AVERAGE MILES PER DAY WAS"
1030 PRINT T/N
1040 IF Q$="R" THEN 310
1050 END
2000 Q=121+CC*16:K=118+CC*16
2010 C$=CHR$(Q)+CHR$(K)
2020 Q=127+CR*16:RS$=CHR$(Q)
2030 FOR J=1 TO W
2040 RS$=RS$+CHR$(128):NEXT
2050 RS$=RS$+CHR$(Q)
2060 Q=119+CR*16:K=120+CR*16
2070 RL$=CHR$(Q)+CHR$(K)
2080 FOR J=1 TO (W-1)
2090 RL$=RL$+CHR$(128):NEXT
2100 RL$=RL$+CHR$(Q)+CHR$(K)
2110 Q=116+CR*16:K=123+CR*16
2120 RR$=CHR$(Q)+CHR$(K)
2130 FOR J=1 TO (W-1)
2140 RR$=RR$+CHR$(128):NEXT
2150 RR$=RR$+CHR$(Q)+CHR$(K)
2160 B$="":FOR J=1TO 32
2170 B$=B$+CHR$(128):NEXT
2180 D$=CHR$(128)+CHR$(128)
2200 RETURN
3000 W=7:GOSUB2000:CLS(0)
3010 FOR J=1 TO 15:READ Q
3020 PRINT@Q,RS$;:NEXT
3030 FOR J=1 TO 600:NEXT
3040 RESTORE:FORJ=1TO3
3050 READQ:PRINT@Q+3,C$;
3060 GOSUB20:NEXT:READQ:PRINT@Q+4,"S";:GOSUB20
3016 READQ:PRINT@Q+4,"P";:GOSUB20:READQ:PRINT@Q+4,"E";:GOSUB20
3070 T$="ED DEMON":FOR J =1 TO 8
3080 READ Q:PRINT@Q+3,C$;
3090 Q$=CHR$(128)+MID$(T$,J,1)
3100 FOR K=1 TO 100:NEXT
3110 PRINT@Q+3,Q$;:NEXT
3120 READ Q:PRINT@Q+3,C$;
3130 SOUND 150,20
3140 FOR J=1 TO 500:NEXT
3150 PRINT@480,""
3160 RETURN
3200 DATA 12,44,77,110,141,172
3210 DATA 205,238,271,304,337
3220 DATA 370,403,436,469
3600 PRINT"USE THE 'A' KEY TO MOVE LEFT"
3602 PRINT"USE THE 'D' KEY TO MOVE RIGHT"
3610 Z=RND(-(PEEK(9)*256+PEEK(10)))
3620 GOSUB15
3640 RETURN
4000 Q=175:K=195
4010 N$=CHR$(Q)+CHR$(Q)+CHR$(Q)
4020 M$=CHR$(Q)+CHR$(K)+CHR$(Q)
4030 Q=Z-INT(W/2)-5:K=Q-128
4040 FOR J=K TO Q STEP 32
4050 PRINT@J,N$;:NEXT
4060 FOR J=K TO Q STEP 32
4070 FOR R=1TO300:NEXT
4080 PRINT@J,M$;:SOUND 100,4
4090 NEXT:RETURN
5000 DATA49,49,60,55,54
