10 REM TOWERS OF HANOI
20 REM WRITTEN FOR 80-C BY:
30 REM ROBERT KELLEY
40 REM 59 S. PIONEER AVENUE
50 REM TRUCKSVILLE, PA 18708
60 GOSUB 830
70 CLEAR:CLEAR 750:CLS0
80 DIM PEG$(3,8),DISK(8)
90 FORX=1TO9:BL$=BL$+CHR$(128):NEXT
100 MV=1
110 A$="====1===="
120 B$="====2===="
130 C$="====3===="
140 REM BUILD COLORED DISKS
150 DISK$(1)=CHR$(133)+CHR$(143)+CHR$(138)
160 FOR X=1TO3:DISK$(2)=DISK$(2)+CHR$(159):NEXT
170 DISK$(3)=CHR$(165):FORX=1TO3:DISK$(3)=DISK$(3)+CHR$(175):NEXT:DISK$(3)=DISK$(3)+CHR$(170)
180 FOR X=1TO5:DISK$(4)=DISK$(4)+CHR$(191):NEXT
190 DISK$(5)=CHR$(197):FORX=1TO5:DISK$(5)=DISK$(5)+CHR$(207):NEXT:DISK$(5)=DISK$(5)+CHR$(202)
200 FORX=1TO7:DISK$(6)=DISK$(6)+CHR$(223):NEXT
210 DISK$(7)=CHR$(229):FORX=1TO7:DISK$(7)=DISK$(7)+CHR$(239):NEXT:DISK$(7)=DISK$(7)+CHR$(234)
220 FORX=1TO9:DISK$(8)=DISK$(8)+CHR$(255):NEXT
230 FOR X=1TO8:PEG$(1,X)=DISK$(X):NEXTX
240 REM DISPLAY CURRENT STATUS
250 IF PEG$(3,1)<>"" THEN FLAG=1
260 CLS0:PRINT@260,A$;:PRINT@276,B$;:PRINT@460,C$;
270 FOR X=1TO8:PRINT@(32*(X-1)+8)-INT(LEN(PEG$(1,X))/2),PEG$(1,X);:NEXT
280 FOR X=1TO8:PRINT@(32*(X-1)+24)-INT(LEN(PEG$(2,X))/2),PEG$(2,X);:NEXT
290 FOR X=1TO8:PRINT@192+(32*(X-1)+16)-INT(LEN(PEG$(3,X))/2),PEG$(3,X);:NEXT
300 IF FLAG=1 THEN END
310 REM DISPLAY PROMPTS
320 PRINT@320,"MOVE #";:PRINT@326,MV;
330 PRINT@342,MSG$;:IF MSG$<>""THEN MSG$="NO MOVE":SOUND 10,10
340 PRINT@352,"FROM  ";
350 PRINT@374,MSG$;
360 PRINT@386,"TO ";
370 IF MSG$=""THEN 400 
380 FOR X=1 TO 1000:NEXT
390 PRINT@342,BL$;:PRINT@374,BL$;
400 MSG$=""
410 IF MV=1 THEN 420:FOR X=1 TO 1000:NEXT
420 PRINT@359,CHR$(143);:FOR X=1TO75:NEXT:PRINT@359,CHR$(128);:FOR X=1TO50:NEXT
430 FR$=INKEY$:IF FR$="" THEN420
435 PRINT@359,FR$;
440 PRINT@391,CHR$(143);:FORX=1TO75:NEXT:PRINT@391,CHR$(128);:FORX=1TO50:NEXT
450 OB$=INKEY$:IF OB$="" THEN440
455 PRINT@391,OB$;
460 GOSUB 490
470 GOSUB 770
480 GOTO240
490 REM SOURCE & OBJECT PEG TESTS
500 OB=VAL(OB$):FR=VAL(FR$)
510 OB=INT(OB):FR=INT(FR)
520 IF OB<1 OR OB>3 THEN 750
530 IF FR<1 OR FR>3 THEN 750
540 IF OB=FR THEN 750
550 IFPEG$(FR,8)="" THEN 750
560 FOR X=1TO8
570 IF PEG$(FR,X)<>"" THEN F=X:GOTO 590
580 NEXT X
590 FOR X=1TO8
600 IF PEG$(OB,X)<>"" THEN O=X-1:GOTO 690
610 NEXT
620 O=8
630 IFPEG$(OB,8)="" THEN 690
640 FOR X=1TO8:IF PEG$(FR,F)=DISK$(X) THEN 660
650 NEXT
660 FOR T=1TO8:IF PEG$(OB,O+1)=DISK$(T) THEN 680
670 NEXT
680 IF X>XX THEN 750
690 REM TEST FOR LARGE ON SMALL
700 IF PEG$(OB,8)="" THEN 740
710 TP=ASC(PEG$(FR,F))
720 BOT=ASC(PEG$(OB,O+1))
730 IF TP>BOT THEN 750
740 RETURN
750 MSG$="ILLEGAL!!"
760 GOTO260
770 REM MOVE PIECE
780 PEG$(OB,O)=PEG$(FR,F)
790 PEG$(FR,F)=""
800 MV=MV+1
810 O=0
820 RETURN
830 REM INSTRUCTIONS
840 CLS:PRINT TAB(9) "TOWERS OF HANOI"
850 PRINT:PRINT"THIS IS A CLASSIC GAME, SAID TO HAVE BEEN ORIGINATED BY HOLY MENIN THE FAR EAST."
860 PRINT"THE OBJECT IS TO TRANSFER ALL OFTHE COLORED DISKS FROM PEG#1 TO PEG#3. ONLY ONE DISK MAY BE"
870 PRINT"MOVED AT A TIME, AND AT NO TIME MAY A LARGER DISK BE PLACED ON  A SMALLER DISK."
880 PRINT:PRINT"THE TASK CAN BE ACCOMPLIHED IN  NO FEWER THAN 255 MOVES."
890 PRINT:INPUT"HIT <ENTER> TO CONTINUE";EN$
900 RETURN
