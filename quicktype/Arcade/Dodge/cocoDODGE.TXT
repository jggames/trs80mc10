0 GOSUB10005:GOSUB10000:CLS:CLEAR6000:GOTO900
2 IFP>226+O THENPRINT@P,C$;:PRINT@Q,C$;:P=P-3:Q=Q-3
3 PRINT@P,H$;:PRINT@Q,L$;:NEXT:NEXT:GOTO20
4 IFP<250+O THENPRINT@P,C$;:PRINT@Q,C$;:P=P+3:Q=Q+3
5 PRINT@P,G$;:PRINT@Q,K$;:NEXT:NEXT:GOTO20
6 IFP+64<317THENPRINT@P,C$;:PRINT@Q,C$;:P=P+64:Q=Q+64:O=64
7 ?@P,F$;:?@Q,J$;:NEXT:NEXT:GOTO20
8 IFP-64>224THENPRINT@P,C$;:PRINT@Q,C$;:P=P-64:Q=Q-64:O=0
9 ?@P,F$;:?@Q,J$;:NEXT:NEXT:GOTO20
10 IFO<>64THENNEXT:NEXT:GOTO20
11 ?@P,F$;:?@Q,J$;:?@Q+32,CHR$(158)CHR$(157);:?@Q+64,CHR$(169)CHR$(168);:?@Q+96,CHR$(164);:SOUND100,1:?@Q+32,C$;:?@Q+64,CHR$(158)CHR$(157);:?@Q+96,CHR$(169)CHR$(168);:?@Q+128,CHR$(220);
12 SOUND25,1:S=S+100*L:NEXT:NEXT:GOTO20
14 ONK(PEEK(135))GOTO2,4,6,8,10:GOTO31
20 CLS0:GOSUB2300
22 L=L+1:IFL>5THENL=5
23 LV=LV+1:PRINT@0,"LEVEL";LV;:GOSUB2400:PRINT@0,C$;C$;C$;C$;:PRINT@24,HS;
24 FORZ=1TO3:FORT=1TO100:S=S+L:?@0,S;:IFPEEK(Q+M+V)<>D ORPEEK(Q+M+A)<>D ORPEEK(Q+M+B)<>D THEN60
25 ?@G,E$(A(T))E$(B(T))E$(C(T))E$(D(T))E$(E(T))E$(F(T))E$(G(T))E$(H(T))E$(I(T))E$(J(T))B$D$(A(T))D$(B(T))D$(C(T))D$(D(T));
26 ?D$(E(T))D$(F(T))D$(G(T))D$(H(T))D$(I(T))D$(J(T))B$C$(A(T))C$(B(T))C$(C(T))C$(D(T))C$(E(T))C$(F(T))C$(G(T))C$(H(T))C$(I(T));
27 ?C$(J(T))B$B$(A(T))B$(B(T))B$(C(T))B$(D(T))B$(E(T))B$(F(T))B$(G(T))B$(H(T))B$(I(T))B$(J(T))B$A$(A(T))A$(B(T))A$(C(T));
29 ?A$(D(T))A$(E(T))A$(F(T))A$(G(T))A$(H(T))A$(I(T))A$(J(T));
30 POKE345,255:POKE344,255:POKE343,255:POKE342,255:POKE341,255:POKE340,255:IF(PEEK(345)ANDPEEK(344)ANDPEEK(343)ANDPEEK(342)ANDPEEK(341)ANDPEEK(340))<>255THEN14
31 ?@P,F$;:?@Q,J$;:NEXT:NEXT:GOTO20
60 PRINT@12,"CRASH!";
70 IFS>HS THENHS=S:PRINT@24,HS;
80 PRINT@136,"PLAY AGAIN (Y/N)?";:FORT=1 TO50:I$=INKEY$:NEXT
90 I$=INKEY$:T=160+RND(5)*16+RND(14)
91 PRINT@Q+(RND(3)-2)*32+RND(4)-1,CHR$(T);
99 IFI$="" THEN90
100 IFI$="Y"THENLV=0:L=0:S=0:O=0:G=289:P=237:Q=P+32:GOTO20
110 IFI$="N"THENGOSUB10010:GOSUB10003:END
120 GOTO90
900 GOSUB3000:DIMT,A$(12),B$(12),C$(12),D$(12),E$(12),A(100),B(100),C(100),D(100),E(100),F(100),G(100),H(100),I(100),J(100)
910 DIMK(255),P,G,K,Q,S,M,A,B,V,Z,F$,G$,H$,J$,K$,L$,B$,C$,L,C,O,D,HS,I$,LV:T=RND(-TIMER):GOTO1000
1000 G=289:P=237:Q=P+32:C=64:K(8)=1:K(9)=2:K(10)=3:K(94)=4:K(32)=5:K=17023:D=128:M=1024:A=33:B=34:V=32
1001 E$(0)=CHR$(128)+CHR$(128)+CHR$(128)
1002 D$(0)=CHR$(128)+CHR$(128)+CHR$(128)
1003 C$(0)=CHR$(128)+CHR$(128)+CHR$(128)
1004 B$(0)=CHR$(128)+CHR$(128)+CHR$(128)
1005 A$(0)=CHR$(128)+CHR$(128)+CHR$(128)
1020 E$(1)=E$(0)
1030 D$(1)=D$(0)
1040 C$(1)=C$(0)
1050 B$(1)=B$(0)
1060 A$(1)=CHR$(128)+CHR$(129+C)+CHR$(128)
1120 E$(2)=E$(1)
1130 D$(2)=D$(1)
1140 C$(2)=C$(1)
1150 B$(2)=B$(1)
1160 A$(2)=CHR$(128)+CHR$(133+C)+CHR$(128)
1220 E$(3)=E$(2)
1230 D$(3)=D$(2)
1240 C$(3)=C$(2)
1250 B$(3)=CHR$(128)+CHR$(129+C)+CHR$(128)
1260 A$(3)=CHR$(128)+CHR$(133+C)+CHR$(128)
1320 E$(4)=E$(3)
1330 D$(4)=D$(3)
1340 C$(4)=C$(3)
1350 B$(4)=CHR$(128)+CHR$(143+C)+CHR$(128)
1360 A$(4)=CHR$(128)+CHR$(143+C)+CHR$(128)
1420 E$(5)=E$(4)
1430 D$(5)=D$(4)
1440 C$(5)=CHR$(128)+CHR$(131+C)+CHR$(128)
1450 B$(5)=B$(4)
1460 A$(5)=A$(4)
1520 E$(6)=E$(5)
1530 D$(6)=D$(5)
1540 C$(6)=CHR$(128)+CHR$(143+C)+CHR$(128)
1550 B$(6)=B$(5)
1560 A$(6)=A$(5)
1620 E$(7)=E$(6)
1630 D$(7)=CHR$(128)+CHR$(131+C)+CHR$(130+C)
1640 C$(7)=CHR$(128)+CHR$(143+C)+CHR$(138+C)
1650 B$(7)=CHR$(128)+CHR$(143+C)+CHR$(138+C)
1660 A$(7)=CHR$(128)+CHR$(143+C)+CHR$(138+C)
1720 E$(8)=E$(7)
1730 D$(8)=CHR$(128)+CHR$(143+C)+CHR$(138+C)
1740 C$(8)=C$(7)
1750 B$(8)=B$(7)
1760 A$(8)=A$(7)
1820 E$(9)=CHR$(128)+CHR$(131+C)+CHR$(130+C)
1830 D$(9)=D$(8)
1840 C$(9)=C$(8)
1850 B$(9)=B$(8)
1860 A$(9)=A$(8)
1920 E$(10)=CHR$(128)+CHR$(143+C)+CHR$(138+C)
1930 D$(10)=D$(9)
1940 C$(10)=C$(9)
1950 B$(10)=B$(9)
1960 A$(10)=A$(9)
2020 E$(11)=CHR$(133+C)+CHR$(143+C)+CHR$(143+C)
2030 D$(11)=CHR$(133+C)+CHR$(143+C)+CHR$(143+C)
2040 C$(11)=CHR$(133+C)+CHR$(143+C)+CHR$(143+C)
2050 B$(11)=CHR$(133+C)+CHR$(143+C)+CHR$(143+C)
2060 A$(11)=CHR$(133+C)+CHR$(143+C)+CHR$(143+C)
2120 E$(12)=CHR$(143+C)+CHR$(143+C)+CHR$(143+C)
2130 D$(12)=CHR$(143+C)+CHR$(143+C)+CHR$(143+C)
2140 C$(12)=CHR$(143+C)+CHR$(143+C)+CHR$(143+C)
2150 B$(12)=CHR$(143+C)+CHR$(143+C)+CHR$(143+C)
2160 A$(12)=CHR$(143+C)+CHR$(143+C)+CHR$(143+C)
2200 B$=CHR$(128)+CHR$(128):C$=B$+CHR$(128)
2220 F$=CHR$(131)+CHR$(131)+CHR$(131):J$=CHR$(129)+CHR$(140)+CHR$(130)
2230 G$=CHR$(128)+CHR$(137)+CHR$(128):K$=CHR$(128)+CHR$(141)+CHR$(137)
2240 H$=CHR$(128)+CHR$(134)+CHR$(128):L$=CHR$(134)+CHR$(142)+CHR$(128)
2250 GOSUB3020:GOTO20
2300 PRINT@448,"";:FORT=0 TO31:PRINTCHR$(220);:NEXT:RETURN
2400 FORT=88 TO100:A(T)=0:B(T)=0:C(T)=0:D(T)=0:E(T)=0:F(T)=0:G(T)=0:H(T)=0:I(T)=0:J(T)=0:NEXT
2409 FORT=1TO88:A(T)=0:IFRND(10)<=L THENFORC=1 TO12:A(T)=C:T=T+1:NEXT
2410 NEXT
2420 FORT=1TO88:B(T)=0:IFRND(10)<=L THENFORC=1TO12:B(T)=C:T=T+1:NEXT
2440 NEXT
2450 FORT=1TO88:C(T)=0:IFRND(10)<=L THENFORC=1TO12:C(T)=C:T=T+1:NEXT
2460 NEXT
2500 FORT=1TO88:D(T)=0:IFRND(10)<=L THENFORC=1TO12:D(T)=C:T=T+1:NEXT
2510 NEXT
2520 FORT=1TO88:E(T)=0:IFRND(10)<=L THENFORC=1TO12:E(T)=C:T=T+1:NEXT
2540 NEXT
2550 FORT=1TO88:F(T)=0:IFRND(10)<=L THENFORC=1TO12:F(T)=C:T=T+1:NEXT
2560 NEXT
2600 FORT=1TO88:G(T)=0:IFRND(10)<=L THENFORC=1TO12:G(T)=C:T=T+1:NEXT
2610 NEXT
2620 FORT=1TO88:H(T)=0:IFRND(10)<=L THENFORC=1TO12:H(T)=C:T=T+1:NEXT
2640 NEXT
2650 FORT=1TO88:I(T)=0:IFRND(10)<=L THENFORC=1TO12:I(T)=C:T=T+1:NEXT
2660 NEXT
2670 FORT=1TO88:J(T)=0:IFRND(10)<=L THENFORC=1TO12:J(T)=C:T=T+1:NEXT
2680 NEXT
2900 RETURN
3000 CLS:PRINT@10,"TOWER DODGE";
3010 PRINT@42,"BY J GERRIE";:PRINT@67,"INSPIRED BY 'POLE DODGE'";
3011 PRINT@98,"FOR THE ZX81 BY STEVEN REID":PRINT
3012 PRINT"YOU'RE THE PILOT OF A PLANE THAT";
3013 PRINT"WAS TO DO A NIGHT PARA DROP."
3014 PRINT"CATASTROPHE STRIKES! YOUR ENGINE";
3015 PRINT"IS FAILING AND YOU HAVE WANDERED";
3016 PRINT"(INADVERTENTLY) IN TO A CITY. NOW";
3017 PRINT"YOU MUST AVOID THE TOWERS (USE"
3018 PRINT"ARROW KEYS) AND DROP LOW ENOUGH"
3019 PRINT"TO ALLOW FOR SAFE DROPS (SPACE":PRINT"KEY).":RETURN
3020 PRINT@484,"PRESS ANY KEY TO BEGIN.";
3030 I$=INKEY$:IFI$="" THEN3030
3040 RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN
