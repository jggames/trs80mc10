0 CLS0:DIMC4,C3,C5,C1,C2:M=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOSUB5000:GOTO9000
1 C3=C1*2+C2*64:C5=C4*2:PRINT@C3,MID$(A$,C5,2);:PRINT@C3+32,MID$(B$,C5,2);:RETURN

10 GOSUB4000
20 D=D(YR):R=R(YR)
30 IFD/1000>=1THENA=1:C2=0:C4=19:FORC1=7TO9:GOSUB1:NEXT:D=D-1000
40 IFD/100>=1THENB=1:C1=15:C4=19:FORC2=3TO4:GOSUB1:NEXT:D=D-100
50 IFD/10>=1THENC=1:C2=7:C4=19:FORC1=7TO9:GOSUB1:NEXT:D=D-10
60 IFDTHENC1=0:C4=19:FORC2=3TO4:GOSUB1:NEXT
70 RETURN

80 FORC5=1TO15:GOSUB160:FORC3=1TO75:NEXT:NEXT:RETURN
85 FORC5=1TO8:GOSUB160:FORC3=1TO75:NEXT:NEXT:RETURN

95 C4=INT(RND(0)*7)+1:IFR(C4)=.THENRETURN
96 GOTO95

100 PRINT@1,"DOCTORόWHO";:PRINT@22,"ADVENTURE";:REM PRINT@0,R;YR;
105 PRINT@503," 00.00";:XX=1:RETURN

110 IFATHENIFY=0THENYR=YR-1:Y=6:GOSUB240:NR=NR+1:GOTO150
120 IFBTHENIFX=15THENYR=YR+4:X=1:GOSUB240:NR=NR+1:GOTO150
130 IFCTHENIFY=7THENYR=YR+1:Y=1:GOSUB240:NR=NR+1:GOTO150
140 IFDTHENIFX=0THENYR=YR-4:X=14:GOSUB240:NR=NR+1:GOTO150
150 RETURN

160 TL=TL-.01:PRINT@503,LEFT$(STR$(TL),6);:IFTL<.THENPRINT@503," 00.00";:GOTO8000
170 RETURN

180 C1=X:C2=Y:IFXXORYYTHENC4=1:GOSUB1
190 GOSUB160:XX=-(PEEK(2)AND1ANDNOTPEEK(16946))+(PEEK(2)AND4ANDNOTPEEK(16948))*.25
200 YY=-(PEEK(2)AND4ANDNOTPEEK(16952))*.25+(PEEK(2)AND8ANDNOTPEEK(16947))*.125
210 L=PEEK(M+(X+XX)*2+(Y+YY)*64):IFL<>128THENXX=.:YY=.:RETURN
220 C4=19:GOSUB1:X=X+XX:Y=Y+YY
230 RETURN

240 GOSUB300:GOSUB100:RETURN

300 K(0)=18:GOSUB10:IFRTHENONRGOSUB500,700,230,1100,1300,1500,1700,1900,2100,2300,2500,2700,2900,3100,3300
310 IFNRTHENNR=NR-1:RETURN
370 RETURN

380 GOSUB240
390 GOSUB180:GOSUB110:NR=.:IFXXORYYTHENIFO=.THENIFSPTHENIFY=6ORY=1THENIFX=14ORX=1THENGOSUB460
395 IFINKEY$=" "THENIFOANDR(YR)=.THENGOSUB410
400 GOTO390

410 IFPEEK(M+(X-1)*2+Y*64)=128THENC1=X-1:C2=Y:C4=O(O):GOSUB1:R(YR)=O+7:GOTO450
411 IFPEEK(M+(X+1)*2+Y*64)=128THENC1=X+1:C2=Y:C4=O(O):GOSUB1:R(YR)=O+7:GOTO450
412 SOUND1,1:RETURN
450 O=0:GOSUB4020:SOUND100,1:RETURN

460 IFRND(3)<3THENRETURN
470 GOSUB3005:PRINT@480,"SONICόSPANNER";:O=7:GOSUB4020:SOUND200,3:SP=0:RETURN

500 C3=3*32+8:PRINT@C3,"€‘€€";:C3=C3+32:PRINT@C3,"¥­­€";:PRINT@C3,"¥­­€";:C3=C3+32:PRINT@C3,"¥―― ";:C3=C3+32:PRINT@C3,"§――Ά";
510 C3=2*32+8:PRINT@C3,"ππππ";:PRINT@480,"YOURόTARDIS";:GOSUB100:GOSUB105
520 GOSUB180:GOSUB110:IFNRTHENRETURN
530 IFL>159THENIFL<178THEN570
540 GOTO520

570 GOSUB580:PRINT@480,"INSIDETARDIS";:GOSUB105:X=14:Y=4:GOTO600
580 K(0)=17:GOSUB4000
585 FORC3=2*32+16TO4*32+16STEP32:PRINT@C3,"•";:NEXT:PRINT@5*32+14,"€€€€";
590 PRINT@6*32+14,"ήήήήήή";:PRINT@7*32+14,"ίίίίίί";:PRINT@8*32+14,"ίΧππίΧ";:PRINT@9*32+14,"ίΧππίΧ";
595 C1=15:C2=3:C4=19:GOSUB1:C2=4:GOSUB1:RETURN
600 GOSUB180:IFINKEY$=" "THEN670
610 IFX<15THEN600
660 X=6:Y=3:XX=1:K(0)=18:GOSUB10:GOSUB100:GOTO500

670 IFO<>6THENPRINT@480,"CAN'T DROP THAT HERE!";:SOUND1,1:GOSUB80:GOSUB3020:GOTO600
680 O=0:GOSUB4020:GOSUB900:FORC5=1TO15:POKE49151,64:FORC3=1TO80:NEXT:SOUND100+10*C5,3:NEXT:REM HEY CHARLIE
681 REM REPLACE THIS SILLY SOUND ROUTINE WITH SOME REAL DOCTOR WHO MUSIC
690 PRINT@13*32+11," SCORE"INT(TL*LV);:GOTO8020

700 GOSUB705:GOSUB100:GOTO720
705 C3=2*32+8:PRINT@C3,"‡‹";:C3=C3+32:PRINT@C3,"";:C3=C3+32:PRINT@C3,"σς€";:C3=C3+32:PRINT@C3,"·Ό²€";
710 C3=C3+32:PRINT@C3,"Ώ°Ώ²";:C3=C3+32:PRINT@C3,"ΏΏΏΏ";:PRINT@480,"MASTER'SόTARDIS";:GOSUB105:RETURN
720 GOSUB180:GOSUB110:IFNRTHENRETURN
721 IFL>175ANDL<192THEN725
722 GOTO720
725 IFMT=0ANDO<>1THENGOSUB3010:PRINT@480,"NEEDόKEY!";:GOSUB80:GOSUB3010:GOTO720
730 MT=1:GOSUB580:PRINT@480,"MASTER'SCONTROL ROOM";:GOSUB900:GOSUB105:X=14:Y=3:Q=7:W=6:C1=Q:C2=W
740 C1=X:C2=Y:C4=1:GOSUB1:C1=Q:C2=W:C4=2:GOSUB1
750 GOSUB160
760 IFX=QTHENIFY=WTHEN850
770 C1=Q:C2=W:C4=2:GOSUB1:GOSUB160:GOSUB180:QQ=-(Q<X)+(Q>X):WW=-(W<Y)+(W>Y):IFRND(LV)=1THENQQ=.:WW=.
780 C1=Q:C2=W:C4=19:GOSUB1:Q=Q+QQ:W=W+WW:L=PEEK(M+Q*2+W*64):IFL=147THENGOSUB1:GOTO820
785 IFINKEY$=" "THENIFOTHENGOSUB410:R(YR)=.
790 IFNRTHENRETURN
800 IFX<15THEN760
810 X=6:Y=3:XX=1:K(0)=18:GOSUB10:GOSUB705:GOSUB100:GOTO720

820 FORC5=1TO100STEP10:SOUNDC5,1:NEXT:C4=19:GOSUB1:C1=Q:C2=W:C4=10:GOSUB1
830 GOSUB3020:PRINT@480,"HE'S DEAD!";:GOSUB80:PRINT@480,"THE RAY GUN";:GOSUB80
840 PRINT@480,"MALFUNCTIONED";:GOSUB80:GOSUB3020:PRINT@480,"WHEN HE TRIED TO";
841 GOSUB80:PRINT@480,"USE IT AGAINST YOU!";:GOSUB80
842 GOSUB3020:GOSUB910:R(YR)=0:O=6:GOSUB4020:SOUND200,3
845 PRINT@480,"YOU NOW HAVE";:GOSUB80:PRINT@480,"HIS TIME DRIVE";:GOSUB80
846 X=6:Y=3:XX=1:K(0)=18:GOSUB10:GOSUB100:GOTO720
850 C1=Q:C2=W:C4=2:GOSUB1:GOSUB3020:PRINT@480,"HE GOT YOU!";:GOTO8000

900 C1=8:C2=4:C4=13:GOSUB1:RETURN
910 C1=8:C2=4:C4=19:GOSUB1:RETURN

1100 GOSUB1101:PRINT@480,"AόPIT";:GOSUB100:GOSUB105:GOTO1120
1101 C5=4*32+4:PRINT@C5,"όόόόόόόόόόόόόόόόόόόόόό";:C5=C5+32
1102 PRINT@C5,"ϊωσσσσσσσσσσσσσσσσσσσσφυ";:C5=C5+32
1103 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1104 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1105 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1106 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1107 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1108 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1109 PRINT@C5,"ϊφόόόόόόόόόόόόόόόόόόόόωυ";:C5=C5+32
1110 PRINT@C5,"σσσσσσσσσσσσσσσσσσσσσσ";:RETURN
1120 C1=X:C2=Y:C4=1:GOSUB1:IFO=4THENPI=0
1130 C4=19:GOSUB1:C4=1:FORC2=Y+1TO5:GOSUB1101:GOSUB1:SOUND200-10*C2,2:GOSUB160:NEXT:Y=5:C2=Y:GOSUB1
1150 IFPI>=0THENC1=11:C2=5:C4=6:GOSUB1:PRINT@480,"BROKENόRAYόGUN";:FORC5=1TO15:SOUND100,2:NEXTC5:O=5:GOSUB4020:IFPI=0THENPI=-1
1160 C1=11:C2=5:C4=19:GOSUB1:IFPI>0THENPRINT@480,"YOU ARE TRAPPED!";:GOTO8000
1170 PRINT@480,"YOUόUSEόTHEόROPE";
1171 C1=X:C2=Y:C4=19:GOSUB1:C4=1:FORC2=Y-1TO1STEP-1:GOSUB1101:GOSUB1
1172 SOUND200-10*C2,2:GOSUB160:NEXT:Y=1:C2=Y:GOSUB1:RETURN

1300 GOSUB1301:PRINT@480,"SNAKEόPIT";:GOSUB100:GOSUB105:GOTO1320
1301 C5=4*32+4:PRINT@C5,"όόόόόόόόόόόόόόόόόόόόόό";:C5=C5+32
1302 PRINT@C5,"ϊωσσσσσσσσσσσσσσσσσσσσφυ";:C5=C5+32
1303 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1304 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϋχ";:C5=C5+32
1305 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€€€";:C5=C5+32
1306 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€€€";:C5=C5+32
1307 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ώύ";:C5=C5+32
1308 PRINT@C5,"ϊυ€€€€€€€€€€€€€€€€€€€€ϊυ";:C5=C5+32
1309 PRINT@C5,"ϊφόόόόόόόόόόόόόόόόόόόόωυ";:C5=C5+32
1310 PRINT@C5,"σσσσσσσσσσσσσσσσσσσσσσ";:RETURN
1320 IFSL=0THENRETURN
1330 C1=X:C2=Y:C4=19:GOSUB1:X=X+2:C1=X:C2=Y:C4=1:GOSUB1:SOUND1,2
1340 Q=12:W=4:C1=Q:C2=W:C4=7:GOSUB1:C1=X:C2=Y:C4=1:GOSUB1
1350 IFX=QTHENIFY=WTHEN1400
1360 C1=Q:C2=W:C4=7:GOSUB1:GOSUB160:GOSUB180:QQ=-(Q<X)+(Q>X):WW=-(W<Y)+(W>Y):IFRND(LV)=1THENQQ=.:WW=.
1365 IFRND(LV)=1THENQQ=.:WW=3-RND(2)
1366 L=PEEK(M+(Q+QQ)*2+(W+WW)*64):IFK(L)<>1THENIFL<>128THENWW=.
1367 IFX=1ORX=15THENSL=0:PRINT@480,"SNAKEόAVOIDED!";:GOSUB85
1370 C1=Q:C2=W:C4=19:GOSUB1:Q=Q+QQ:W=W+WW:GOSUB110:IFNRTHENRETURN
1380 GOTO1350
1400 C1=Q:C2=W:C4=7:GOSUB1:PRINT@480,"SNAKEόBITόYOU!";
1410 IFO<>2ORSW=-1THEN8000
1420 GOSUB85:PRINT@480,"SWORDόPROTECTSόYOU!";:GOSUB85:GOSUB3010:SW=-1
1430 X=X+1:C1=X:C2=Y:C4=1:GOSUB1:GOTO1360

1500 EN=16:PRINT@480,"AόSPHINX!";:GOSUB150:GOSUB100:GOTO1950

1700 GOSUB1701:PRINT@480,"PRIEST'SόTEMPLE";:GOSUB100:GOSUB105:GOTO1720
1701 C5=4*32+4:PRINT@C5,"                        ";:C5=C5+32
1702 PRINT@C5,"                        ";:C5=C5+32
1703 PRINT@C5,"  €€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1704 PRINT@C5,"  €€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1705 PRINT@C5,"€€€€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1706 PRINT@C5,"€€€€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1707 PRINT@C5,"  €€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1708 PRINT@C5,"  €€€€€€€€€€€€€€€€€€€€  ";:C5=C5+32
1709 PRINT@C5,"                        ";:C5=C5+32
1710 PRINT@C5,"                        ";:RETURN
1720 IFSW<1THENRETURN
1730 C1=X:C2=Y:C4=19:GOSUB1:X=X+2:C1=X:C2=Y:C4=1:GOSUB1:SOUND1,2
1740 Q=11:W=4:C1=Q:C2=W:C4=15:GOSUB1:C1=X:C2=Y:C4=1:GOSUB1:C1=12:C2=4:C4=9:GOSUB1
1750 IFX=QTHENIFY=WTHEN1800
1760 C1=Q:C2=W:C4=15:GOSUB1:GOSUB160:GOSUB180:QQ=-(Q<X)+(Q>X):WW=-(W<Y)+(W>Y):IFRND(LV)<3THENQQ=.:WW=.
1765 IFRND(LV)=1THENQQ=.:WW=3-RND(2)
1766 L=PEEK(M+(Q+QQ)*2+(W+WW)*64):IFK(L)<>1THENIFL<>128THENWW=.
1770 C1=Q:C2=W:C4=19:GOSUB1:Q=Q+QQ:W=W+WW:GOSUB110:IFNRTHENRETURN
1780 GOTO1750

1800 C1=Q:C2=W:C4=15:GOSUB1:PRINT@480,"PRIESTόTOUCHESόYOU!";:GOSUB1810:GOTO1820
1810 FORC5=1TO15:POKE49151,64:FORC1=1TO40:NEXT:GOSUB160:SOUND20,1:NEXT:RETURN
1820 PRINT@480,"LIGHTENING HITS YOU!";:GOSUB1810
1830 GOSUB3010:IFO<>3THENPRINT@480,"YOUόAREόDEAD.";:GOTO8000
1840 C1=X:C2=Y:C4=1:GOSUB1:PRINT@480,"THEόRINGόPROTECTSόYOU!τ";:C1=Q:C2=W:C4=1:GOSUB1
1850 GOSUB80:GOSUB3010:PRINT@480,"YOUόHAVEόTHEόSWORD";:SW=0
1860 GOSUB1:C1=12:C2=4:C4=19:GOSUB1:O=2:GOSUB4020:RETURN

1870 ONRND(4)GOTO1875,1880,1885,1890
1875 Q=1:W=1:C1=Q:C2=W:RETURN
1880 Q=14:W=1:C1=Q:C2=W:RETURN
1885 Q=1:W=6:C1=Q:C2=W:RETURN
1890 Q=14:W=6:C1=Q:C2=W:RETURN

1900 GOSUB1910:IFNRTHENRETURN
1901 PRINT"MASTER'SόKEY";:C4=4:GOTO2990

1910 GOSUB100:C1=7:C2=4:C4=11:GOSUB1:PRINT@480,"AόMUMMYόCASEτό";:GOSUB105
1920 GOSUB180:GOSUB110:IFNRTHENRETURN
1921 IFX=7THENIFY=3THEN1930
1922 GOTO1920
1930 IFR=14ORR=15THEN1950
1940 V=O:O=R(YR)-7:R(YR)=V+7+(V=0)*7:C1=X:C2=Y:C4=O(O):GOSUB1:Y=Y-1:C1=X:C2=Y:C4=1:GOSUB1:GOSUB3000:RETURN

1950 GOSUB1870:C4=EN:GOSUB1:C1=X:C2=Y:C4=1:GOSUB1
1960 IFX=QTHENIFY=WTHEN2050
1970 C1=Q:C2=W:C4=EN:GOSUB1:GOSUB110:IFNRTHENRETURN
1971 GOSUB160:GOSUB180:QQ=-(Q<X)+(Q>X):WW=-(W<Y)+(W>Y):IFRND(LV)=1THENQQ=.:WW=.
2010 C1=Q:C2=W:C4=19:GOSUB1:Q=Q+QQ:W=W+WW:GOTO1960

2050 C1=Q:C2=W:C4=EN:GOSUB1:IFR=14THEN:PRINT@480,"MUMMYόGOTόYOU!";:GOTO8000
2060 IFR=15THENPRINT@480,"EXTERMINATE!";:GOTO8000
2070 IFO<>2THENPRINT@480,"THEόSPHINXόGOTόYOU!";:GOTO8000
2080 C1=X:C2=Y:C4=1:GOSUB1:PRINT@480,"THEόSWORDόSCARESόIT!";:Q=Q+1
2090 C2=W:C4=16:FORC1=QTO14:C4=16:GOSUB1:SOUNDC1*10,2:C4=19:GOSUB1:NEXT:SN=0:RETURN

2100 GOSUB1910:IFNRTHENRETURN
2101 PRINT"AόSWORD";:C4=9:GOTO2990
2300 GOSUB1910:IFNRTHENRETURN
2301 PRINT"MAGICόRING";:C4=8:GOTO2990
2500 GOSUB1910:IFNRTHENRETURN
2501 PRINT"SOMEόROPE";:C4=12:GOTO2990
2700 GOSUB1910:IFNRTHENRETURN
2701 PRINT"BROKENόRAYόGUN";:C4=6:GOTO2990
2900 GOSUB1910:IFNRTHENRETURN
2901 PRINT"THEόTIMEόDRIVE";:C4=13:GOTO2990
2990 C1=X:C2=Y+1:GOSUB1:GOSUB85:C4=O(V):GOSUB1:GOSUB4020:RETURN

3000 PRINT@480,"τότότότότότότό";:PRINT@480,;:RETURN
3005 PRINT@480,"τότότότότότότό";:IFPEEK(M+494)<>128THENPRINT"τότότότότ";
3006 RETURN
3010 PRINT@480,"τότότότότότότότότότότότ";:RETURN
3020 PRINT@480,"";:RETURN

3100 EN=14:GOTO1910
3300 EN=3:GOTO1910

REM DRAW WALLS
4000 CLS0:C4=K(0):FORC1=0TO14:C2=.:GOSUB1:C2=7:GOSUB1:NEXT
4010 FORC2=0TO6:C1=0:GOSUB1:C1=15:GOSUB1:NEXT:FORC2=0TO7:C1=0:GOSUB1:NEXT
4020 C4=O(O):IFO=.THENC4=K(0)
4030 C5=C4*2:PRINT@478,MID$(A$,C5,2);:PRINT@510,MID$(B$,C5,1);:POKEM+511,ASC(MID$(B$,C5+1,1)):RETURN

5000 A$="€Χ·²·Ί“ Σ““€Α€ΧΣΖΛ§«ω€αΰΗΖχς€—›όψ€€":B$="€Φ¶²·»—άΫή …‡ΙΘΤΠΞΝ――€ωοκΖΒτό€€":RETURN

8000 FORC5=1TO15:SOUND90,1:NEXT:C4=K(0):FORC2=0TO6:FORC1=0TO15:GOSUB1:NEXT:SOUND100-(C2*10),1:NEXT
8005 FORC1=0TO14:GOSUB1:NEXT:SOUND100-(C2*10),1:O=0:GOSUB4020
8010 PRINT@5*32+11," YOU LOSE ";:PRINT@7*32+6,"THE MASTER WILL RULE";:PRINT@9*32+6,"+++ THE UNIVERSE +++";
8020 PRINT@15*32,"      PRESS enter TO BEGIN     ";:POKEM+511,96:POKE49151,64
8030 M$=INKEY$:ON-(M$<>CHR$(13))GOTO8030:SOUND1,1:RUN

9000 DIMR(12),D(12),X,Y,Q,W,D,A,B,C,O,TL,YR,XX,YY,QQ,WW,K(255),O(7)
9010 K(215)=1:K(183)=2:K(183)=3:K(144)=4:K(160)=5:K(147)=6:K(141)=7:K(193)=8:K(176)=9:K(198)=10
9020 K(167)=11:K(249)=12:K(225)=13:K(199)=14:K(247)=15:K(141)=16:K(151)=17:K(252)=18:K(128)=19
9030 O(0)=19:O(1)=4:O(2)=9:O(3)=8:O(4)=12:O(5)=6:O(6)=13:O(7)=5
9080 FORN=1TO12:READA,B:D(N)=A:R(N)=B:NEXTN
9090 TL=30:X=8:Y=4:YR=4:O=0:SN=1:SL=1:PI=1:MT=0:SW=1:SP=5:FORC5=1TO4:GOSUB95:READA:R(C4)=A:NEXTC5
9100 CLS0:POKE49151,64:CLS:PRINTTAB(6)"DOCTOR WHO ADVENTURE"
9110 PRINTTAB(6)"BY JEREMY GUGGENHIEM":PRINTTAB(2)"COMPUTER & VIDEO GAMES 1983"
9120 PRINT"ANOTHER TIME, ANOTHER PLACE..."
9130 PRINT"THE DOCTOR IS TRAPPED ON THE "
9140 PRINT"PLANET STRARDOS IV IN A PYRAMID"
9150 PRINT"BUILT BY HIS ARCH ENEMY, THE
9160 PRINT"MASTER, WHO PLANS TO DESTROY"
9161 PRINT"THE DOCTOR WITH A TIME BOMB. THE";
9162 PRINT"MASTER HAS STOLEN THE DOCTOR'S"
9163 PRINT"TIME DRIVE. WITHOUT IT, THE"
9164 PRINT"DOCTOR CANNOT ESCAPE THE DOOMED"
9165 PRINT"PLANET. EXPLORE THE PYRAMID TO"
9167 PRINT"DISCOVER THE MEANS TO HIS ESCAPE";
9168 PRINT"WHILE AVOIDING ITS MANY HAZARDS.";
9200 PRINT@485,"DIFFICULTY LEVEL (1-3)?";
9210 LV=VAL(INKEY$):C4=RND(18):C1=0:C2=0:GOSUB1:C4=RND(18):C1=15:C2=0:GOSUB1:IFLV<1ORLV>3THEN9210
9220 LV=LV*3:CLS0:SOUND1,1:GOTO380

9900 DATA110,0,1110,0,1010,0,1000,1,101,5,111,0,1110
9910 DATA0,1000,4,1,2,1,7,11,6,1000,8,10,11,14,15
9920 REM JIM GERRIE 2023
