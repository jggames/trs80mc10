0 GOSUB5500:CLEAR2000:DIMS1$(6),S2$(6),BL$,B$,T$,B1$,B2$,B3$,B4$,B5$,I$,WB$,N$
1 DIMRN(1000),K(128),S,SS,C,D,T,U,W,J,K,R1,R2,R3,R4,R5,TT,VI,V1,V2,V3,V4,V5,VC,CL
2 DIM LV,E,F,G,G2,GG,RL,TK,HT,SC,TC,CR,LC,PK,LF,RT,HS,HL,Z:GOTO50
3 POKEVC,CHOR(PEEK(VC)AND7):RETURN
10 IFSS=LFTHENRETURN
20 PRINT@S,BL$;:PRINT@SS,BL$;:D=J:S=S-U:SS=SS-U:V3=VI+SS+U:V5=V3-U:RETURN
30 IFSS=125THENRETURN
40 PRINT@S,BL$;:PRINT@SS,BL$;:D=U:S=S+U:SS=SS+U:V3=VI+SS+U:V5=V3+U:RETURN
45 R2=K:RETURN
46 C=RL-1:RETURN
50 GOSUB5000:PRINT "     PRESS ANY KEY TO BEGIN"
60 I$=INKEY$:Z=RND(10):IFI$=""THEN60
70 CLS
100 IFLV<5THEN5400
105 LC=0:PRINT"GET READY FOR SLOPE #";(10-(LV/5))+1:GOSUB5300
110 CLS4:GOSUB3:D=0:S=80:SS=112:V3=VI+SS+U:V5=V3-U:TK=0:HT=0
120 R1=RN(0):R2=1:B$=MID$(B1$,R1,32)+MID$(B2$,R1,31):B5$=MID$(B2$,R1,32)
200 FORC=UTORL:PRINT@S,BL$;:PRINT@SS,BL$;:PRINT@V4,B$;:POKEV1,V2:IFPEEK(V3)<>V2ORPEEK(V5)<>V2THENGOSUB350
220 ONK(PEEK(PK)ANDPEEK(W))GOSUB10,30,45,46
230 PRINT@S,S1$(D);:PRINT@SS,S2$(D);:ONR2GOTO275,276,277,278,279
275 B$=B5$+MID$(B3$,R1,CL):B5$=MID$(B3$,R1,TT):R2=W:NEXT:GOTO300
276 B$=B5$+MID$(B4$,R1,CL):B5$=MID$(B4$,R1,TT):R2=E:NEXT:GOTO300
277 R1=RN(C):B$=B5$+MID$(B1$,R1,CL):B5$=MID$(B1$,R1,TT):R2=F:NEXT:GOTO300
278 B$=B5$+MID$(B2$,R1,CL):B5$=MID$(B2$,R1,TT):R2=U:NEXT:GOTO300
279 TC=C:C=RL:NEXT:SOUND100,1:CLS:PRINT"YOUR RUN IS OVER.":GOTO305
300 CLS4:TC=C-1:FORT=1TO12:PRINT@S+(T*32),S1$(D);:PRINT@SS+(T*32),S2$(D);:FORZ=1TO50:NEXTZ
301 PRINT@S+(T*32),BL$;:PRINT@SS+(T*32),BL$;:NEXTT 
302 SOUND100,1:CLS:PRINT"YOU COMPLETED SLOPE #";(10-(LV/5));CHR$(8);"!":LC=1 
305 SC=TC-(HT*(LV/5))
310 PRINT "SCORE =";SC
325 PRINT "BRANCHES HIT =";HT
330 PRINT "TRUNKS OR BOULDERS HIT =";TK
331 IFSC>=HSOR(10-(LV/5))>HLTHENINPUT"INPUT YOUR NAME";N$:N$=LEFT$(N$+" ",5):HS=SC:HL=(10-(LV/5))
332 PRINT "HIGH SCORE =";HS;"/";HL;"BY ";N$
335 PRINT "TRY AGAIN (Y) OR NEXT RUN (R)?"
340 GOTO6000
350 G=PEEK(V3):G2=PEEK(V5)
360 HT=HT+U:PRINT@S,S1$(W);:PRINT@SS,S2$(W);:FORZ=1TO90:NEXTZ:IFG=128ORG2=128THENR2=K:TK=U
370 RETURN
5000 VI=16384:VC=49151:PK=17023:TT=32:CH=16:LF=96:RT=125:RL=1000
5010 J=0:U=1:W=2:E=3:F=4:K=5:G=0:GG=218:V1=16895:V2=191:V4=480:CL=31:LV=50:CR=0
5020 K(65)=1:K(83)=2:K(13)=3:K(32)=4:HS=100:HL=1:N$="JIMG"
5040 B3$=CHR$(191):BL$=B3$+B3$+B3$:FORT=1TO63:WB$=WB$+B3$:NEXTT
5050 S1$(0)=CHR$(155)+CHR$(145)+CHR$(189)
5060 S2$(0)=CHR$(184)+CHR$(152)+CHR$(191)
5070 S1$(1)=CHR$(190)+CHR$(162)+CHR$(167)
5080 S2$(1)=CHR$(191)+CHR$(164)+CHR$(180)
5081 S1$(2)=CHR$(255)+CHR$(255)+CHR$(255)
5082 S2$(2)=CHR$(255)+CHR$(255)+CHR$(255)
5100 S1$(3)=CHR$(191)+CHR$(191)+CHR$(096)+CHR$(191)+CHR$(191)
5105 S2$(3)=CHR$(191)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(191)
5110 S3$(3)=CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)
5115 S4$(3)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
5120 S1$(4)=CHR$(191)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(191)
5125 S2$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
5130 S3$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
5135 S4$(4)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
5140 S1$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
5145 S2$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
5150 S3$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
5155 S4$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
5160 S1$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5165 S2$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5170 S3$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5185 S4$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5190 RETURN
5300 B1$="":B2$="":B3$="":B4$="":B5$=""
5310 Z=0:FORT=UTO50
5315 R1=RND(LV+K):IFR1<KTHEN5319
5317 IFZ<10THENIFRND(W)=UGOTO5315
5318 R1=6:GOTO5320
5319 Z=Z+U:R1=RND(W)+W:IFRND(10)=UTHENR1=K
5320 B1$=B1$+S1$(R1)
5330 B2$=B2$+S2$(R1)
5331 B3$=B3$+S3$(R1)
5332 B4$=B4$+S4$(R1)
5340 NEXT
5350 FORT=ETORLSTEPF:RN(T)=RND(GG):NEXT:RN(J)=RND(GG):RN(RL)=RND(GG)
5360 LV=LV-K
5370 RETURN
5400 PRINT"YOU HAVE TRIED ALL 10 RUNS.":IFCR=10THENPRINT"AND YOU COMPLETED ALL 10 RUNS!":SOUND200,5
5410 PRINT"WOULD YOU LIKE TO TRY A"
5420 PRINT"DIFFERENT MOUNTAIN (Y/N)?"
5430 LV=50:CR=0:GOTO6060
5500 CLS:PRINT@42,"snow boarder":PRINT
5520 PRINT "USE <A> TO MOVE LEFT,":PRINT"AND <S> TO MOVE RIGHT."
5530 PRINT "HIT <ENTER> TO QUIT."
5540 PRINT:PRINT "TRY TO COMPLETE ALL 10 SLOPES."
5545 PRINT "THE DIFFICULTY LEVEL INCREASES"
5546 PRINT "FOR EACH SLOPE YOU TRY."
5550 PRINT "HITTING BRANCHES = LOSE POINTS."
5560 PRINT "IF YOU HIT A TREE STUMP, OR A"
5570 PRINT "BOULDER THEN THE RUN IS OVER.":PRINT 
5599 RETURN
6000 I$=INKEY$:IFI$=""THEN6000
6010 IFI$="Y" THEN 110
6020 IFI$="R" THEN 6040
6030 GOTO6000
6040 IFLC=1THENCR=CR+1
6045 PRINT"ARE YOU SURE YOU WANT TO TRY"
6050 PRINT"A DIFFERENT SLOPE (Y/N)?"
6060 I$=INKEY$:IFI$=""THEN6060
6070 IFI$="Y"THEN100
6080 IFI$="N"THEN6090
6085 GOTO6060
6090 END
