0 L=RND(-(PEEK(9)*256+PEEK(10)))
6 CLS:GOSUB250:INPUT"LEVEL 1=HARD-5=EASY)";H:IFH<1ORH>5THEN6
9 CLS0:PRINT@511,"";
12 L=RND(42)
15 E=0:RT=0:J=0
18 F=0:G=0
21 B=6+RND(21)
24 D=10+RND(17)
27 FORC=29TODSTEP-1:IF(E+B-G)-(E+F)<1THEN42
30 FORA=E+FTOE+B-G
33 IFA>63THEN66
36 SET(A,C,5):NEXTA
39 F=F+1:G=G+1
42 NEXTC
45 E=E+B
48 IFE>=LANDJ=0THEN54
51 GOTO18
54 IFE/2-INT(E/2)>0THENE=E-1
55 FORK=0TOH+2
57 SET(E+K,29,3):SET(E+K,28,3)
58 IFE+K>=63THENE=E+K:J=1:K=H+4:NEXTK:GOTO66
60 NEXTK
63 E=E+K:J=1:GOTO18
66 IFH=1THENFORK=1TORND(4):U=RND(6)*32+RND(28):PRINT@U,"";:PRINT@U+32,"";:NEXTK
70 M=3+RND(55):N=2+RND(10):U=(INT(N/2)-1)*32+(M/2)-2:GOSUB4000
72 GR=RND(12)-H:IFGR<=4THENGR=3
75 FU=200+H*50:SV=RND(11):IFSV<=5THENSV=-SV:GOTO81
79 IFSV>6THENSV=SV-5:GOTO81
80 SV=0
81 VE=RND(30):IFRT=0THEN138
87 BX=0:BU=0:VE=VE+INT(GR/3):IFFU<=0THENFU=0:GOTO114
93 FORPE=1TO4:BX=BX+BX+1:K=PEEK(2)ANDPEEK(17023):IFK=48+PETHENBU=BX:SOUND90-(BU*2),1
102 NEXTPE
105 FU=FU-BU:K=PEEK(2)ANDPEEK(17023):IFK=65THENSV=SV-1:SOUND100,1:FU=FU-1
111 IFK=83THENSV=SV+1:SOUND100,1:FU=FU-1
114 VE=INT(VE-BU*.5):AL=(28-N)*10:IFAL<50THENAL=(28-N)*2
120 IFN>27THEN177
121 IFVE>0ANDVE<20THENN=N+1:GOTO126
122 IFVE>=20THENN=N+2:GOTO126
123 IFVE<0ANDVE>-20THENN=N-1:GOTO126
124 IFVE<=-20THENN=N-2
126 IFSV=0THENS=0:GOTO135
129 S=INT(SV/2):IFS=0THENS=1
132 M=M+S
135 IFM<=2ORM>60ORN<=0THEN144
138 SET(M-1,N+1,2):SET(M+1,N+1,2):SET(M,N,2):SET(M+1,N+2,2):SET(M,N+1,2):SET(M-1,N+2,2)
144 PRINT@480,"F"FU;:PRINT@485," A"AL;:PRINT@492," S";SV;:PRINT@499," V"VE;:PRINT@504," G"GR;
147 IFM<=2THENPOKE16895,62:GOTO87
148 IFM>60THENPOKE16895,60:GOTO87
149 POKE16895,96:IFN<=0THEN87
150 IFN=25THENZX=1:GOTO153
151 ZX=0
153 IFN=25THENCV=3:GOTO156
154 CV=4
156 IFN=25THENBN=4:GOTO159
157 BN=3
159 FORQW=ZXTOCV:ER=POINT(M-2+QW,N+BN)
162 IFER=5THEN177
163 IFER=3THEN204
165 NEXTQW
168 RESET(M-1,N+1):RESET(M+1,N+1):RESET(M,N):RESET(M+1,N+2):RESET(M,N+1):RESET(M-1,N+2)
171 RT=1
174 GOTO87
177 PRINT@0,"CRASHED! ";:IFM<2ORM>60THEN180
178 SET(M-RND(2),N-RND(2),8):SET(M+RND(3),N-RND(2),8):SET(M,N,8):SET(M+RND(2),N+RND(2),8):SET(M,N+RND(2),8)
179 SET(M+RND(3)-2,N+3,8):SET(M-1,N+RND(3),8)
180 IFVE>5ANDAL<19THENPRINT" GOING TOO FAST"
183 IFFU<=0THENPRINT"RAN OUT OF FUEL"
186 IFAL>19THENPRINT"RAN RIGHT INTO IT"
189 IFAL<19ANDER=5THENPRINT"MISSED THE PAD"
192 IFSV<-1ORSV>1THENPRINT"TOO MUCH SIDE MOTION"
195 FORU=255TO1STEP-3
198 SOUNDU,1:NEXTU
201 GOTO219
204 IFVE>5THEN177
207 IFSV<-1ORSV>1THEN177
210 PRINT@0,"GREAT LANDING!"
213 FORU=1TO255STEP5
216 SOUNDU,1:NEXTU
219 PRINT:FORU=1TO32:PRINTCHR$(8);:NEXT:INPUT"TRY AGAIN Y/N";D$
222 IFD$="Y"THEN6
225 END
250 PRINT:PRINTTAB(7)"hyperspace lander":PRINT
260 PRINTTAB(7)"BY ROBERT TOSCANI":PRINT
300 PRINT"F IS FUEL"
310 PRINT"A IS ALTITUDE"
320 PRINT"S IS SIDEWAYS MOTION (A&S KEYS)
330 PRINT"V IS VERTICAL VELOCITY"
335 PRINT"(-V=GOING UP.  KEYS 1-4=THRUST)"
340 PRINT"G IS GRAVITY FIELD STRENGTH"
345 PRINT"WHEN YOU LAND:
350 PRINT"S CANNOT BE > +1 OR < -1"
360 PRINT"V CANNOT BE > 5":PRINT
370 RETURN
400 REM LANDER BY ROBERT TOSCANI
410 REM MC-10 EDITS JIM GERRIE
4000 FORCC=1TO5
4010 PRINT@U,"";:PRINT@U+32,"";:PRINT@U+64,"";:PRINT@U+96,"";:PRINT@U+128,"";:SOUND250,1
4020 PRINT@U,"";:PRINT@U+32,"";:PRINT@U+64,"";:PRINT@U+96,"";:PRINT@U+128,"";:SOUND220,1
4030 PRINT@U,"";:PRINT@U+32,"";:PRINT@U+64,"";:PRINT@U+96,"";:PRINT@U+128,"";:SOUND200,1
4050 NEXT:RETURN

