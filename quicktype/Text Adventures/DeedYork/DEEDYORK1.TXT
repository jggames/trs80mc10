0 CLS:CLEAR500:DIMC1,CC,M$:GOSUB180:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 REM "THE DEED OF THE YORK"  BY CHRIS HARLAND - 4 DAVIDSON CRES., REGINA, SASK., CANADA
11 FOR X=1 TO 3000:NEXT X
12 DATA 83,72,65,75,69,83,80,69,65,82,69
13 CLS
14 PRINT@6,"the deed of the york":PRINT
15 GOSUB200
16 PRINT:M$="IF YOU CAN FIND THAT DEED BEFORE THE CHOMIUKS DO, YOU CAN SAVE YOUR FAMILY'S FORTUNE!":GOSUB1
17 PRINT@480,"PRESS ANY KEY TO CONTINUE";:IF INKEY$="" THEN 17
18 CLS:PRINT@6,"the deed of the york":PRINT
19 PRINT"DO YOU WISH TO FIND THE DEED???
20 PRINT"REMEMBER THAT THE HOUSE IS      SUPPOSED TO BE haunted!!!!"
21 PRINT:PRINT"IF YOU ARE BRAVE ENOUGH,        THEN    E N T E R !!"
22 PRINT:PRINT"THE DOOR WILL ONLY OPEN IF YOU  SHOUT 'PIZZA'"
23 PRINT@480,"PRESS ANY KEY TO CONTINUE";:IF INKEY$=""THEN 23
24 CLS:PRINT@6,"the deed of the york":PRINT
25 GOSUB210
26 PRINT:M$="IF YOU FORGET WHERE YOU ARE, TYPE 'WHERE'":GOSUB1
27 PRINT@480,"PRESS ANY KEY TO START";:IFINKEY$=""THEN27
28 CLS
29 PRINT:INPUT"WHAT NOW";A$
30 IFA$="WHERE" THEN M$="YOU'RE OUTSIDE AN OLD HOUSE, STUPID!":GOSUB1:GOTO29
31 IFRIGHT$(A$,5)="PIZZA"ORRIGHT$(A$,7)="'PIZZA'"THEN 33
32 M$="YOU'RE A BIG COWARD, BUT I DON'T BLAME YOU-- REMEMBER, YOU CAN ALWAYS ENTER AGAIN BY TYPING IN 'PIZZA'":GOSUB1:END
33 IFDD1=0THENPRINT"THE DOOR HAS SWUNG OPEN!!":DD1=1
34 GOSUB220
35 PRINT:PRINT"WHAT NOW";:GOTO230
48 PRINT"WHICH ONE?":GOTO230
49 M$="IT'S JUST AN ORDINARY COATRACK WITH THE INITIALS 'W.S.' CARVED IN IT.":GOSUB1:GOTO 35
50 GOSUB270
51 PRINT"THERE IS ALSO A DOOR GOING LEFT."
52 PRINT:PRINT"WHAT WILL YOU DO NOW";
53 INPUTC$:IFRIGHT$(C$,5)="COUCH"THEN600
54 IFC$="WHERE"THEN50
55 IFLEFT$(C$,7)="SLIDING"THEN620
56 IFLEFT$(C$,7)="SMALLER"THEN620
57 IFLEFT$(C$,5)="LARGE"THEN69
58 IFLEFT$(C$,7)="FOLDING"THEN69
59 IFRIGHT$(C$,6)="SHEETS"THEN68
60 IFLEFT$(C$,4)="BACK"ORRIGHT$(C$,4)="LEFT"THEN34
61 IFRIGHT$(C$,4)="DOOR"THEN169
62 IFRIGHT$(C$,5)="CHAIR"THEN65
63 IFRIGHT$(C$,5)="COUCH"THENM$="ON THE RIGHT SIDE OF THE COUCH IS ENGRAVED: WILLIAM & ANNE-- 1582":GOSUB1:GOTO52
64 M$="DOESN'T SOUND GOOD TO ME-- TRY SOMETHING ELSE":GOSUB1:GOTO52
65 PRINT"NOTHING HAPPENS.":GOTO52
68 PRINT"JUST A COUCH AND CHAIRS":GOTO52
69 GOSUB260
70 PRINT:PRINT"WHAT WILL YOU DO NOW";
71 INPUTF$:IFLEFT$(F$,4)="TURN"THEN84
72 IFLEFT$(F$,8)="SWINGING"THEN85
73 IFF$="WHERE"THEN69
74 GOTO280
76 PRINT"WHAT?":GOTO70
77 GOTO290
79 PRINT"NOTHING HAPPENS. WHAT NOW";:GOTO71
80 IFLEFT$(F$,7)="FOLDING"THEN50
81 IFRIGHT$(F$,7)="PICTURE"THENFL=1:GOTO610
82 GOTO500
83 PRINT"WHICH DOOR";:GOTO71
84 PRINT"LIGHT FILLS THE ROOM.":GOTO70
85 GOSUB300
86 PRINT:PRINT"WHAT WILL YOU DO";:GOTO310
90 M$="AH! BUT THE CUPBOARD IS BARE, SAVE FOR ONE DUSTY OLD GLASS. LOOK ELSEWHERE.":GOSUB1:GOTO86
91 PRINT"WHICH ONE WAS THAT AGAIN?":GOTO310
92 GOSUB330
93 INPUT"SO WHAT NOW";K$
94 GOTO340
99 GOSUB360
100 IFL$="LEFT"THEN103
101 IFL$="RIGHT"THEN104
102 M$="OOPS! YOU JUST DIED WHERE YOU STOOD!!!":GOSUB1:END
103 M$="YOU STAGGERED INTO THE CENTRAL BALLROOM AND BLED TO DEATH.":GOSUB1:END
104 GOTO370
105 M$="INSIDE THE ICEBOX IS A ROLL OF PAPER. YOU UNROLL IT AND READ: 'SHELVE ALL PLOTS AS THE BARD WAVES HIS JAVELIN.'"
106 GOSUB1:GOTO86
108 PRINT:PRINT"NOW WHAT";:GOTO400
114 PRINT"WHICH DOOR":GOTO 400
115 GOTO420
117 GOSUB430
118 GOTO122
119 GOTO440
121 GOTO450
122 PRINT:PRINT"WHAT DO YOU WANT TO DO";:GOTO470
126 PRINT"WHICH DOOR?":GOTO470
127 PRINT:M$="YOU ARE IN THE LIBRARY. ALONG ONE WALL IS A SHELF OF BOOKS. ONE DOOR IS BEHIND YOU, AND A "
128 M$=M$+"METAL PANEL IS NEAR THE SHELF.":GOSUB1
129 PRINT:INPUT"WHAT WILL YOU DO NOW";O$:IFO$="WHERE"THEN127
130 RESTORE:W$="":FORTT=1TO11:READN:W$=W$+CHR$(N):NEXTTT:IFRIGHT$(O$,11)=W$THEN510
131 GOTO151
132 IFLEFT$(O$,4)="BACK"THEN117
133 IFRIGHT$(O$,5)="PANEL"THEN570
134 M$="NOTHING HAPPENS. TRY SOMETHING DIFFERENT.":GOSUB1:GOTO129
137 GOSUB520
139 PRINT:PRINT"WHAT NOW";:GOTO550
144 PRINT"WHICH DOOR?":GOTO 550
145 M$="AH! NOW YOU'RE THINKING!! BUT NOTHING HAPPENS. TRY SOMETHING ELSE.":GOSUB1:GOTO139
146 M$="NOTHING HAPPENS. TRY SOMETHING DIFFERENT.":GOSUB1:GOTO139
147 M$="THE ONLY THING ON OR INSIDE OF THE DESK IS AN OLD LIBRARY CARD. THE ONLY THING WRITTEN ON THE CARD IS '1600S'.":GOSUB1
150 GOTO122
151 IFRIGHT$(O$,4)="DOOR"ORRIGHT$(O$,4)="BACK"THEN117
152 GOTO132
153 M$="AS YOU APPROACH THE PIANO, IT STARTS TO PLAY! ITS KEYS ARE BEING STRUCK BY UNSEEN FINGERS!":GOSUB1:FORI=1TO3000:NEXT
154 M$="YOU RECOGNIZE THE TUNE! IT IS 'AS YOU LIKE IT'.":GOSUB1:FORI=1TO3000:NEXT:PRINT"THE MUSIC DIES AWAY.":GOTO139
159 M$="THE CHAIR FOLDS UP AND PINCHES YOUR FINGER. OTHERWISE, NOTHING HAPPENS.":GOSUB1:GOTO573
169 PRINT"WHICH DOOR":GOTO53
170 END
180 A$=CHR$(128):CLS0
183 PRINT@70,"the"+A$+"deed"+A$+"of"+A$+"the"+A$+"york";
184 PRINT@134,"a"+A$+"game"+A$+"of"+A$+"adventure";
185 PRINT@192+15,"by";
186 PRINT@266,"DWIGHT LOGAN";
187 PRINT@326,"modified"+A$+"for"+A$+"the"+A$+"80c";
188 PRINT@384+14,"by";
189 PRINT@458,"CHRIS HARLAND";:RETURN
200 M$="YOU ARE OUTSIDE OF AN OLD HOUSE ON THE OUTSKIRTS OF GRANDE PRAIRIE. "
201 M$=M$+"YOU KNOW THAT INSIDE THE HOUSE THERE IS HIDDEN THE DEED TO THE LAND THAT THE YORK HOTEL IS ON!":GOSUB1:RETURN
210 M$="THERE ARE CLUES SCATTERED AROUND THE HOUSE TO HELP YOU FIND THE MAGIC NECESSARY TO LOCATE THE DEED, "
211 M$=M$+"BUT REMEMBER-- THERE IS ALSO DANGER!":GOSUB1:RETURN
220 PRINT:PRINT:M$="INSIDE, THERE IS A HALLWAY WITH A VERY WORN CARPET. THE HALL IS EMPTY EXCEPT FOR A COATRACK. "
221 M$=M$+"A DOOR IS TO YOUR RIGHT AND ANOTHER TO YOUR LEFT. A THIRD DOOR, MADE OF GLASS, IS STRAIGHT AHEAD OF YOU."
222 GOSUB1:RETURN
230 INPUTA$:IFLEFT$(A$,5)="GLASS"THEN350
237 IFA$="WHERE"THEN34
238 IFRIGHT$(A$,5)="RIGHT"THEN50
239 IFRIGHT$(A$,4)="LEFT"THEN570
240 IFRIGHT$(A$,6)="CARPET"THEN245
241 GOTO250
245 M$="THE ONLY REMARKABLE FEATURE OF THE CARPET IS A LITTLE MESSAGE UNDERNEATH WHICH SAYS: "
246 M$=M$+"'MADE BY LORD CHAMBERLAIN'S MEN'S SEWING COMPANY-- 1605'":GOSUB1:GOTO35
250 IFA$="STRAIGHT"THEN350
254 IFRIGHT$(A$,4)="DOOR"THEN48
255 IFRIGHT$(A$,3)="OUT"THENM$="THE DOOR IS LOCKED BEHIND YOU-- YOU CAN'T GET OUT!":GOSUB1:GOTO35
256 IFRIGHT$(A$,8)="COATRACK"THEN49
257 PRINT"HUH?":GOTO35
260 PRINT:M$="YOU ARE IN WHAT MUST HAVE BEEN THE DINING ROOM. THERE IS A LONG OAK TABLE AND 8 CHAIRS. "
261 M$=M$+"TWO PICTURES AND A SWITCH ARE ON ONE OF THE WALLS.":GOSUB1
262 M$="STRAIGHT AHEAD IS A SWINGING DOOR AND BEHIND YOU IS A FOLDING DOOR.":GOSUB1:RETURN
270 PRINT:M$="YOU ARE IN WHAT WAS OBVIOUSLY THE LIVING ROOM. IT CONTAINS A COUCH AND TWO EASY CHAIRS, "
271 M$=M$+"ALL OF WHICH ARE COVERED WITH DUSTY SHEETS. ON ONE WALL IS A LARGE FOLDING DOOR.":GOSUB1
272 M$="ON ANOTHER WALL IS A SMALLER SLIDING DOOR.":GOSUB1:RETURN
280 IFRIGHT$(F$,5)="TABLE"THEN77
281 IFRIGHT$(F$,8)="PICTURES"THENFL=2:GOTO610
282 IFRIGHT$(F$,6)="CHAIRS"THEN79
283 IFLEFT$(F$,4)="BACK"THEN50
284 GOTO80
290 M$="WHEN YOU TOUCH THE TABLE, A TRAPDOOR IN THE FLOOR OPENS. YOU FALL INTO A COMPLETELY BARE BASEMENT. "
291 M$=M$+"ALL YOU CAN DO IS SHOUT FOR HELP.":GOSUB1:M$="EVENTUALLY YOU ARE SAVED, BUT BY THAT TIME, THE "
292 M$=M$+"CHOMIUKS HAVE FOUND THE DEED AND YOUR FAMILY EXPELS YOU!":GOSUB1:END
300 PRINT:M$="YOU ARE IN THE KITCHEN. THERE ARE DOORS TO YOUR LEFT RIGHT, AND STRAIGHT AHEAD. "
301 M$=M$+"A SWINGING DOOR IS BEHIND YOU. A CUPBOARD IS ON ONE WALL AND AN OLD ICEBOX IS AGAINST ANOTHER.":GOSUB1:RETURN
310 INPUTA$:IFLEFT$(A$,8)="SWINGING"THEN69
311 IFRIGHT$(A$,8)="SWINGING"THEN69
312 IFA$="WHERE"THEN85
313 IFRIGHT$(A$,6)="ICEBOX"THEN105
314 IFRIGHT$(A$,8)="CUPBOARD"THEN90
315 IFRIGHT$(A$,4)="BACK"THEN69
316 IFLEFT$(A$,5)="RIGHT"THEN92
317 IFLEFT$(A$,4)="LEFT"THEN350
318 IFLEFT$(A$,8)="STRAIGHT"THEN99
319 IFA$="AHEAD"THEN99
320 IFRIGHT$(A$,4)="DOOR"THEN91
321 PRINT"WHAT?":GOTO86
330 PRINT:M$="YOU ARE NOW OUTSIDE ON THE BACK PORCH. THERE IS NOTHING HERE. BESIDES, YOU KNOW THE DEED IS IN THE "
331 M$=M$+"HOUSE. THE ONLY WAY TO GO BACK IS THE DOOR TO YOUR LEFT.":GOSUB1:RETURN
340 IFRIGHT$(K$,4)="BACK"THEN85
341 IFRIGHT$(K$,4)="DOOR"THEN85
342 IFK$="WHERE"THEN92
343 IFRIGHT$(K$,4)="LEFT"THEN85
344 PRINT"HUH?":GOTO92
350 PRINT:M$="YOU'RE IN THE CENTRAL BALLROOM. A HUGE CHANDELIER HANGS IN THE CENTRE OF THE ROOM. "
351 M$=M$+"A BROWN DOOR LEADS OFF TO YOUR RIGHT. A GLASS DOOR IS BEHIND YOU, AND ORNATE DOUBLE DOOR IS ON YOUR ":GOSUB1
352 M$="LEFT. A SETTEE IS ALONG ONE WALL.":GOSUB1:M$="A LARGE WINDING STAIRCASE LEADS TO THE SECOND FLOOR. "
353 M$=M$+"A GREEN DOOR IS NEAR THE STAIRCASE.":GOSUB1:GOTO108
360 M$="AS YOU OPEN THIS DOOR, YOU JUST HAVE ENOUGH TIME TO SEE THAT IT LEADS TO THE PANTRY "
361 M$=M$+"BEFORE A DISEMBODIED ARM SWINGS AN AXE AT YOU!":GOSUB1
362 M$="YOU ARE CUT! YOU STAGGER OUT ANOTHER DOOR BLEEDING BADLY! WHICH OTHER DOOR--":GOSUB1
363 INPUT"LEFT OR RIGHT?";L$:RETURN
370 M$="YOU STAGGERED OUTSIDE, WHERE SOMEONE HEARS YOUR SCREAMS AND TAKES YOU TO A HOSPITAL. "
371 M$=M$+"TRY THE HOUSE AGAIN TOMORROW!":GOSUB1:END
400 INPUTI$:IF LEFT$(I$,5)="GREEN" THEN 137
401 IFLEFT$(I$,5)="GLASS" THEN 34
402 IFLEFT$(I$,5)="BROWN" THEN 85
403 IFRIGHT$(I$,6)="SETTEE" THEN 115
404 IFLEFT$(I$,6)="ORNATE"ORLEFT$(I$,6)="DOUBLE"THEN117
405 IFRIGHT$(I$,9)="STAIRCASE"THEN119
406 IFRIGHT$(I$,5)="RIGHT" THEN 117
407 IFRIGHT$(I$,4)="LEFT" THEN 85
408 IFRIGHT$(I$,2)="UP"ORRIGHT$(I$,1)="U"THEN119
409 IFRIGHT$(I$,10)="CHANDELIER"THEN121
410 IFRIGHT$(I$,4)="DOOR"THEN114
411 IFRIGHT$(I$,8)="UPSTAIRS"THEN119
412 IFI$="WHERE"THEN350
413 IFRIGHT$(I$,4)="BACK"THEN 34
414 PRINT"TRY SOMETHING ELSE.":GOTO 108
420 M$="AS YOU TOUCH THE SETTEE, A PANEL OPENS IN THE CEILING ABOVE YOU, AND A CARVED PUMPKIN FALLS ON YOUR HEAD! "
421 M$=M$+"YOU STAGGER AROUND IN A DAZE.":GOSUB1:FOR I=1 TO 4000:NEXT I
422 M$="YOU COME AROUND AGAIN IN A KITCHEN.":GOSUB1:FOR I=1 TO 3000:NEXT I:CLS:GOTO 85
430 PRINT:M$="YOU HAVE ENTERED THE STUDY. IN THE CENTRE OF THE ROOM IS A LARGE, OLD DESK. "
431 M$=M$+"TWO DOORS LEAD OUT TO THE LEFT AND TO THE RIGHT, AS WELL AS AN ORNATE DOUBLE DOOR BEHIND YOU. "
432 M$=M$+"ALONG THE WALL OPPOSITE YOU IS A BIG FIREPLACE.":GOSUB1:GOTO122
440 M$="AS YOU START UP, YOU HEAR A LOUD WAILING NOISE!":GOSUB1:FORI=1TO3000:NEXT:PRINT:
441 M$="SUDDENLY, A GHOST IN WHITE APPEARS AT THE TOP OF THE STAIRSAND RUSHES DOWN AT YOU!":GOSUB1:FORI=1TO2500:NEXTI
442 PRINT:M$="BEFORE IT CAN REACH YOU, YOU SCREAM AND RUN DOWN AND OUT IN PURE TERROR! "
443 M$=M$+"YOU REMAIN WHITE FOR THREE DAYS-- TOO LONG! YOUR FAMILY DISOWNS YOU FOR NOT SAVING THEIR LAND!":GOSUB1:END
450 M$="THE CHANDELIER IS VERY OLD AND BEAUTIFUL, BUT EVERY TIME YOU TRY TO REACH, TOUCH, OR THROW ANYTHING AT IT, "
451 M$=M$+"IT SWAYS AWAY!":GOSUB1:FORI=1TO2500:NEXT:PRINT"TRY SOMETHING ELSE":GOTO108
470 INPUTN$
471 IFRIGHT$(N$,4)="DESK"THEN147
472 IFLEFT$(N$,4)="ORNA"ORLEFT$(N$,4)="DOUB"THEN350
473 IFRIGHT$(N$,9)="FIREPLACE"THEN540
474 IFLEFT$(N$,4)="LEFT"THEN127
475 IFLEFT$(N$,5)="RIGHT"THEN137
476 IFRIGHT$(N$,4)="DOOR"THEN126
477 IFN$="WHERE"THEN117
478 PRINT"WHERE? WHAT? TRY AGAIN!":GOTO470
500 IFRIGHT$(F$,4)="DOOR"THEN83
501 IFRIGHT$(F$,6)="SWITCH"THEN84
502 GOTO76
510 PRINT:M$="ONE BOOK FLIES OFF THE SHELF AND FALLS IN THE MIDDLE OF THE ROOM! IT FALLS OPEN TO A PAGE WHICH "
511 M$=M$+"HAS BEEN INSERTED IN IT-- THE DEED TO THE YORK! YOU HAVE SAVED YOUR FAMILY AND ARE NOW A HERO! ":GOSUB1
512 M$="YOU HAVE WON THIS GAME, BUT DO NOT TELL ANYONE THE SECRETS!!":GOSUB1:END
520 PRINT:M$="YOU ARE IN A MUSIC ROOM. TO YOUR RIGHT IS A GREEN DOOR, AND BEHIND YOU IS A RED DOOR. "
521 M$=M$+"ON ONE WALL ARE PORTRAITS OF FAMOUS ENGLISH AUTHORS AND PLAYRIGHTS. AGAINST ANOTHER WALL IS A PIANO.":GOSUB1
522 RETURN
530 M$="JUST AN OLD, BROKEN TV SET. BUT WAIT A MINUTE! WRITTEN IN THE DUST ON THE TOP OF THE SET ARE THE WORDS "
531 M$=M$+"'BOOKS ARE BETTER'!":GOSUB1:GOTO 622
540 M$="AS YOU APPROACH THE FIREPLACE, A SUDDEN GUST OF WIND BLOWS ASHES TOWARDS YOU. THE ASHES "
541 M$=M$+"SUDDENLY FORM INTO THE SHAPE OF A HAND, WHICH GRABS YOUR THROAT!! YOU STRUGGLE AGAINST THE CHOKING SENSATION!!"
542 GOSUB1:FOR I=1 TO 3000:NEXT I
543 M$="YOU STUMBLE BLINDLY BACKWARDS!!":GOSUB1:FORI=1TO2500:NEXTI
544 M$="THE CHOKING SENSATION ENDS, AND THE HAND DISAPPEARS! YOU FIND THAT YOU HAVE STUMBLED BACK INTO THE BALLROOM!"
545 GOSUB1:FORI=1TO2500:NEXTI:GOTO350
550 INPUTP$:IFP$="WHERE"THEN137
551 IFLEFT$(P$,3)="RED"THEN117
552 IFLEFT$(P$,4)="GREE"THEN350
553 IFRIGHT$(P$,4)="DOOR"THEN144
554 IFRIGHT$(P$,11)="PLAYRIGHTS"ORRIGHT$(P$,7)="AUTHORS"THEN146
555 IFRIGHT$(P$,5)="RIGHT"THEN350
556 IFRIGHT$(P$,9)="PORTRAITS"THEN146
557 IFRIGHT$(P$,5)="PIANO"THEN153
558 IFRIGHT$(P$,5)="FIREPLACE"THEN 540
559 IFRIGHT$(O$,4)="BACK"THEN117
560 PRINT"WHAT? WHERE? WHO? TRY SOMETHING DIFFERENT!":GOTO 139
570 PRINT:M$="YOU ARE IN A LONG, NARROW, ENCLOSED PORCH. A DOOR IS BEHIND YOU. ON ONE WALL IS A METAL PANEL. "
571 M$=M$+"ON THE OPPOSITE WALL IS A MIRROR. IN THE MIDDLE OF THE PORCH STANDS AN OLD LAWN CHAIR "
572 M$=M$+"WITH A NOVEL LYING ON IT.":GOSUB1
573 PRINT:INPUT"WHAT WILL YOU DO NOW";A$:IFA$="WHERE"THEN570
574 IFRIGHT$(A$,5)="PANEL"ORRIGHT$(R$,5)="PANEL"THEN127
575 IFRIGHT$(A$,4)="BACK" OR RIGHT$(A$,4)="DOOR"THEN34
576 IFRIGHT$(A$,6)="MIRROR"THEN580
577 IFRIGHT$(A$,5)="CHAIR"THEN159
578 IFRIGHT$(A$,5)="NOVEL"THEN590
579 M$="NOTHING HAPPENS. DO SOMETHING DIFFERENT.":GOSUB1:GOTO573
580 M$="AS YOU TOUCH THE MIRROR, FROM NOWHERE, A HUMMING SOUND STARTS, YOU RECOGNIZE THE TUNE, AND YOU SING THE WORDS "
581 M$=M$+"SOFTLY TO YOURSELF: 'I'M HENRY THE EIGHTH I AM'!":GOSUB1:FORI=1TO3000:NEXTI:PRINT"BUT NOTHING ELSE HAPPENS."
582 GOTO573
590 M$="YOU OPEN THE BOOK, BUT ALL THE  PAGES ARE BLANK EXCEPT FOR THE FIRST AND THE LAST. ON THE FIRST PAGE IS WRITTEN: "
591 M$=M$+"THIS IS NOT 'A SUMMER'S TALE.' ON THE LAST PAGE IS WRITTEN: 'ALL'S WELL THAT ENDS WELL.'":GOSUB1:GOTO573
600 M$="THE COUCH SHIFTS ASIDE, AND AS IT DOES, A PANEL OPENS IN THE WALL. AN ARROW SHOOTS FROM THE PANEL, "
601 M$=M$+"AND NARROWLY MISSES YOU! A NOTE IS ATTACHED TO THE ARROW. THE NOTE SAYS:":GOSUB1
602 M$="DON'T MAKE MUCH ADO ABOUT NOTHING.":GOSUB1:GOTO65
610 M$="AS YOU MOVE ONE OF THE PICTURES, A NOTE FALLS OUT! PRINTED ON THE NOTE ARE THESE LINES:":GOSUB1
611 PRINT" HE GOT A PIGLET,":PRINT" HE GOT A BIG MAC,":PRINT" BUT WHEN HE LEARED,":PRINT" HE GOT HISTORICAL."
612 PRINT:PRINT"NOTHING ELSE HAPPENS."
613 GOTO70
620 PRINT:M$="YOU'RE IN A SMALL, COZY ROOM WHICH PROBABLY WAS THE DEN. AGAINST ONE WALL IS AN OLD TV SET. "
621 M$=M$+"THE ONLY DOOR IS THE ONE YOU CAME IN.":GOSUB1
622 PRINT:PRINT"WHAT NOW";
623 INPUTA$:IFA$="WHERE"THEN620
624 IFRIGHT$(A$,4)="T.V."ORRIGHT$(A$,2)="TV"ORRIGHT$(A$,10)="TELEVISION"ORRIGHT$(A$,3)="SET"THEN530
625 IFRIGHT$(A$,4)="DOOR"ORRIGHT$(A$,4)="BACK"ORLEFT$(A$,4)="BACK"THEN50
626 PRINT"WHAT ARE YOU TRYING TO DO???":GOTO 623
