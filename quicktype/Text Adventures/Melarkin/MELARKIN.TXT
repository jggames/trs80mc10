0 CLS:DIMC1,CC,P,K:GOTO5:REM WORD WRAP ROUTINE
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 REM THE MELARKIAN - BY PAUL PANKS (***@YAHOO.COM)
6 CLEAR500:CLS:TI=RND(-(PEEK(9)*256+PEEK(10)))
10 DATA "YOU ARE","IN A FOREST","IN A GRASSY PLAIN","IN THE VILLAGE","INSIDE A TAVERN"
20 DATA "INSIDE THE CHURCH","NEAR THE VILLAGE WELL","BY THE MOUNTAINS","ON A BRANCH ATOP THE FOREST"
30 DATA "IN A SMALL CLEARING","IN A MEADOW BY A POND","ON A DUSTY SHORE","BY THE LAKE"
40 DATA "INSIDE A SMALL HUT","WITHIN A DARK CAVE"
50 DATA LANTERN,OIL,BOOK,SWORD,CHAINMAIL,SHIELD,HELMET,CLOAK,RING,ORB
51 DATA ROPE,SCROLL,WEREWOLF,VILLAGER,GOBLIN,HOBBIT,BARBARIAN,VAMPIRE,DRAGON
52 DATA GO,GET,TAKE,DROP,WIELD,UNWIELD,WEAR,REMOVE,LIGHT,USE,EXAMINE,KILL
53 DATA CLIMB,0,2,0,0,0,0,1,4,3,0,0,0,0,0,0,2,0,0,2,5,0,0,0,0
54 DATA 4,8,6,9,0,0,7,0,26,5,0,0,0,6,0,0,0,0,5,13,0,0,0,0,11,0,5,0,0,0
55 DATA 26,0,0,0,0,0,0,9,0,12,0,0,0,0,11,0,0,0,8,24,18,23,0,0,15,0,0,26,0,0
56 DATA 0,14,0,16,0,0,17,0,15,0,0,0,0,16,0,0,0,0,19,0,20,13,0,0,0,18,0,0,0,0
57 DATA 0,0,0,18,0,0,0,0,22,0,0,20,0,0,0,21,0,0,0,0,13,0,0,0,13,0,25,0,0,0,0,0,0,24,0,0,0,10,14,6,0,0
58 DATA "YOU CAN'T DO THAT.","YOU AREN'T STRONG ENOUGH!","YOU DON'T SEE THAT HERE.","NOTHING HAPPENS."
59 DATA "YOU LANDED A SOLID BLOW!","YOU MISSED...","IT DIED. HOORAY!","YOU DIED...","THE END","CONGRATULATIONS!","WHAT?"
60 DATA 5,7,6,4,3,3,3,2,12,13,12,12,2,8,8,8,15,10,2,2,9,14,2,2,2,3
61 DIM D$(15),N$(25),V$(13),M(26,6),R$(11),W(26),L(25),P$(25):FOR X=1 TO 15:READ D$(X):NEXT:FOR X=7 TO 25:READ N$(X):NEXT
62 FOR X=1 TO 13:READ V$(X):NEXT:FOR X=1 TO 26:FOR Y=1 TO 6:READ M(X,Y):NEXT Y:NEXT X
63 FOR X=1 TO 11:READ R$(X):NEXT:FOR X=1 TO 26:READ W(X):NEXT
64 GOSUB2000
70 CLS:PRINT"THE MELARKIAN":PRINT"BY: PAUL PANKS":PRINT"MC-10 EDITS BY JIM GERRIE":PRINT:GOSUB1070
71 PRINT:DATA1,3,22,12,22,17,10,22,22,17,3,17,14,2,13,22,12,16,17:FORX=7TO25:READL(X):NEXT
72 R=1:LT=0:H=100:E=0:G=25:N$(1)="NORTH":N$(2)="SOUTH":N$(3)="EAST":N$(4)="WEST":N$(5)="UP":N$(6)="DOWN"
75 M$=D$(1)+" "+D$(W(R))+".":GOSUB1:GOSUB260:M$="EXITS: < ":FOR X=1 TO 6:IF M(R,X)>0 THEN M$=M$+N$(X)+" "
77 NEXT:M$=M$+">":GOSUB1:FOR X=7 TO 25:IF L(X)=R THEN M$=N$(X):GOSUB1
78 NEXT:I=INT(RND(0)*50)+1:IF I=12 THEN M$="YOU SEE CLOUDS GATHERING IN THE DISTANCE.":GOSUB1
79 IF I=13 THEN M$="A STEADY RAIN PELTS THE GROUND.":GOSUB1
80 IF I=14 THEN M$="A COOL WIND BLOWS FROM THE SOUTHEAST.":GOSUB1
81 IF I=15 THEN M$="LIGHTNING FLASHES ACROSS THE SKY, SCARRING IT.":GOSUB1
85 A$="":N$="":V$="":V=0:N=0:XL$="":INPUTA$:XL$=A$:Z=0:FORX=1TOLEN(A$):IFMID$(A$,X,1)=" "THEN V$=LEFT$(A$,X-1):Z=X
86 NEXT:N$=MID$(A$,Z+1,Z+25):IF XL$="QUIT" OR XL$="LOOK" OR XL$="L" OR XL$="LK" THEN GOTO 220
87 IF XL$="I" OR A$="INV" OR XL$="INVENTORY" THEN145
88 IF XL$="HELP" THEN GOTO 112
89 IF XL$="SCORE" THEN GOTO 114
90 V=0:FOR X=1 TO 12:IF V$=V$(X) THEN V=X:X=13
91 NEXT:GOSUB 250:IF V=0 THEN F=11:GOTO 500
92 N=0:FOR X=1 TO 25:IF N$=N$(X) THEN N=X:X=26
93 NEXT:GOSUB 250:IF N=0 OR N<7 AND V<>1 THEN F=1:GOTO500
94 IF N>18 THEN IF V<>11 AND V<>12 THEN F=1:GOTO 500
95 ON V GOTO 96,100,100,102,104,106,108,110,115,117,122,124
96 IF M(R,N)=0 THEN M$="YOU CAN'T GO THAT WAY.":GOSUB1:GOTO 85
97 IFR=14ANDL(19)=RANDN=1THENM$="THE WEREWOLF BLOCKS YOUR PATH! IT GROWLS, 'YOU MAY NOT ENTER, HUMAN!'":GOSUB1:GOTO85
98 IFR=16ANDL(24)=RANDN=1THENPRINT"THE VAMPIRE STEPS IN FRONT! HE SHOUTS, 'THE SACRED TOMB IS NOT YOURS!'":GOSUB1:GOTO85
99 Z=R:R=M(R,N):GOSUB 225:GOTO75
100 IF N>6 AND L(N)=R THEN L(N)=101:PRINT"TAKEN.":GOTO85
101 F=3:GOTO500
102 IF N>6 AND L(N)=101 THEN L(N)=R:PRINT"DROPPED.":GOTO85
103 F=1:GOTO500
104 IF N>6 THEN IF N=10 AND L(N)=101 THEN L(N)=105:PRINT"WIELDED.":GOTO85
105 F=1:GOTO500
106 IF N>6 THEN IF N=10 AND L(N)=105 THEN L(N)=101:PRINT"UNWIELDED.":GOTO85
107 F=1:GOTO500
108 IF N>6 AND N>10 AND N<15 THEN IF L(N)=101 THEN L(N)=205:PRINT"WORN.":GOTO85
109 F=1:GOTO500
110 IF N>6 AND N>10 AND N<15 THEN IF L(N)=205 THEN L(N)=101:PRINT"REMOVED.":GOTO85
111 F=1:GOTO500
112 PRINT"EXAMPLE: USE LANTERN":PRINT:M$="VERBS: GO, GET, TAKE, DROP, WIELD, UNWIELD, WEAR, REMOVE, EXAMINE, LIGHT, USE,"
113 M$=M$+" KILL, INVENTORY, LOOK, SCORE, QUIT.":GOSUB1:PRINT:PRINT"REC ARTS INT-FICTION":GOTO85
114 M$="YOU HAVE"+STR$(E)+" EXPERIENCE AND"+STR$(H)+" HIT POINTS. YOU CARRY"+STR$(G)+" GOLD COINS.":GOSUB1:GOTO85
115 IFN>6THENIFN=7THENIFL(7)=101ORL(7)=RTHENIFL(8)=101ORL(8)=RTHENIFLT=0THEN1000
116 F=4:GOTO500
117 IF N>6 THEN IF N=7 THEN 115
118 IF N>6 THEN IF N>6 AND N<17 THEN F=4:GOTO500
119 IF N>6 THEN IF N=17 AND R=20 THEN R=21:PRINT"(USE ROPE)":PRINT"YOU CLIMB UP THE TREE...":GOTO75
120 IF N>6 THEN IF N=17 AND R=21 THEN R=20:PRINT"(USE ROPE)":PRINT"YOU CLIMB DOWN THE TREE...":GOTO75
121 F=4:GOTO500
122 IF N>6 THEN IF L(N)=R OR L(N)=101 OR L(N)=105 OR L(N)=205 THEN M$=N$(N)+": "+P$(N):GOSUB1:GOTO85
123 F=1:GOTO500
124 IF N>6 THEN IF N>18 AND L(N)=R THEN126
125 F=1:GOTO500
126 MH=INT(RND(0)*250)+1:GOSUB 200:WD=2:IF L(10)=105 THEN WD=8
127 I=INT(RND(0)*10)+1:PRINT"YOU ATTACK:":IF I<5 OR I>9 AND L(10)<>105 THENM$=R$(6):GOSUB1
128 IF I>4 AND I<10 THEN M$=R$(5):GOSUB1:DT=WD*4+I:MH=MH-DT
129 IF I>9 THEN IF L(10)=105 THENGOSUB1010
130 PRINT:IF MH<1 THEN M$=R$(7):GOSUB1:L(N)=998:GOTO140
131 DT=0:I=INT(RND(0)*10)+1:PRINT"<MORE...> *** PRESS A KEY ***"
132 A$=INKEY$:IF A$="" THEN132
133 PRINT:IF I<5 OR I>9THENM$="THE "+N$(N)+" MISSED YOU.":GOSUB1
134 IF I>4 AND I<10 THENM$="THE "+N$(N)+" HIT YOU VERY HARD!":GOSUB1:DT=AC:IF AC<35 THEN DT=DT-I
135 IF I=8 AND N=24 THENM$="THE "+N$(N)+" SINKS HIS TEETH INTO YOUR NECK! YOU ARE DRAINED OF BLOOD!":GOSUB1:H=H-100
136 IF I=8 AND N=19 THENM$="THE "+N$(N)+" LUNGES AT YOU! IT BITES YOU VERY HARD!":GOSUB1:H=H-75
137 IF I=8 AND N=25 THENGOSUB1030
138 IF DT>0 THEN H=H-DT
139 IF H<1 THEN M$=R$(8):GOSUB1:PRINT:M$=R$(9):GOSUB1:GOTO210
140 IF MH<1 THEN142
141 GOTO127
142 M$="YOU GAINED"+STR$(I)+" EXPERIENCE AND"+STR$(DT+I)+" GOLD COINS.":GOSUB1:G=G+DT+I:E=E+I:IFI>6THENGOSUB1060
143 IF N=25 AND L(N)=998 THEN M$=R$(10):GOSUB1:PRINT:GOTO215
144 GOTO85
145 I=0:PRINT"YOU ARE CARRYING:":M$="":FOR X=7 TO 18:IF L(X)=101 THEN M$=M$+" "+N$(X):I=1
146 IF L(X)=105 THEN M$=M$+" "+N$(X)+" (WIELDED)":I=1
147 IF L(X)=205 THEN M$=M$+" "+N$(X)+" (WORN)":I=1
148 NEXT:IF I=0 THEN M$="ALAS, YOU ARE EMPTY HANDED."
149 GOSUB1:GOTO85
200 AC=0:FOR X=7 TO 25:IF L(X)=205 THEN AC=AC+X
201 NEXT:IF AC=0 THEN AC=1
202 RETURN
210 PRINT:PRINT"PLAY AGAIN (Y/N)?"
211 A$=INKEY$:IF A$="" THEN211
212 IF A$="Y" OR A$="Y" THEN RUN
213 IF A$="N" OR A$="N" THEN CLS:PRINT"THANKS FOR PLAYING!":END
214 GOTO211
215 M$="THE DEAD DRAGON LAY AT YOUR FEET. YOUR SWORD IS NOW SOAKED":GOSUB1
216 M$="IN BLOOD, BUT YOU DON'T CARE. YOU'VE SLAYED THE DRAGON HAUNTING":GOSUB1
217 M$="THE COUNTRYSIDE. TIME FOR A BEER, AND SOME WELL DESERVED REST...":GOSUB1:PRINT:M$=R$(9):GOSUB1:GOTO210
220 IF XL$="QUIT" THEN CLS:PRINT"THANKS FOR PLAYING!":END
221 IF XL$="LOOK" OR XL$="L" OR XL$="LK" THEN IF R=21 THEN 1020
222 GOTO75
225 IF L(7)=101 AND L(8)=101 THEN IF LT=0 THEN M$="IT'S TOO DARK TO MOVE.":GOSUB1:PRINT"(STOPPED...)":R=Z
226 IF LT=0 OR L(7)<>R AND L(7)<>101 THEN IF Z=4 AND N=2 THENGOSUB1050
227 RETURN
250 IF XL$="N" OR XL$="NORTH" THEN N=1:V=1
251 IF XL$="S" OR XL$="SOUTH" THEN N=2:V=1
252 IF XL$="E" OR XL$="EAST" THEN N=3:V=1
253 IF XL$="W" OR XL$="WEST" THEN N=4:V=1
254 IF XL$="U" OR XL$="UP" THEN N=5:V=1
255 IF XL$="D" OR XL$="DOWN" THEN N=6:V=1
256 RETURN
260 IF R=20 THEN M$="THERE IS A TREE HERE.":GOSUB1
261 RETURN
500 IF F=0 THEN F=11
501 M$=R$(F):GOSUB1:GOTO85
1000 LT=1:PRINT"CLICK!":PRINT"THE LANTERN IS NOW AGLOW.":GOTO85
1010 M$="YOUR DAGGER GLOWS! A BOLT OF LIGHTNING STRIKES THE "+N$(N)+"!":GOSUB1:DT=DM*8+I*2:MH=MH-DT:RETURN
1020 M$="LOOKING DOWN, YOU SEE THE FOLLOWING: "+D$(1)+" "+D$(13):GOSUB1:PRINT"EXITS: < NORTH >":GOTO85
1030 M$="THE "+N$(N)+" BREATHES FIRE ON YOU!":GOSUB1:PRINT ">>>>>>>>>>>>>>> FIREBALL!!!"
1040 M$="YOU ARE ENGULFED BY THE FLAMES!!!":GOSUB1:H=H-250:RETURN
1050 M$="IT'S TOO DARK TO MOVE "+N$(N)+"!":GOSUB1:R=Z:RETURN
1060 H=H+100:PRINT"YOU GAINED A LEVEL!":RETURN
1070 M$="'CAN YOU FIND THE MAGICAL ORB AND DEFEAT THE DRAGON?'":GOSUB1:RETURN
2000 P$(7)="A BRASS LANTERN.":P$(8)="A FLASK OF OIL.":P$(9)="AN ANCIENT BOOK.":P$(10)="A METAL SWORD."
2010 P$(11)="A FINE SUIT OF CHAINLINKED ARMOR.":P$(12)="AN IRON SHIELD, WITH A GRYPHON ON THE FRONT."
2020 P$(13)="A BRASS HELMET, STAINED IN BLOOD.":P$(14)="A MAGICAL ELVEN CLOAK."
2030 P$(15)="A RING OF PROTECTION. IT GLOWS SOFTLY.":P$(16)="A MYSTERIOUS WHITE ORB. IT GLOWS FROM WITHIN!"
2040 P$(17)="A COIL OF ROPE ABOUT 10 FEET IN LENGTH.":P$(18)="A TATTERED, OLD SCROLL."
2050 P$(20)="A SIMPLE PEASANT OF THE VILLAGE.":P$(21)="A MENACING GOBLIN WITH DARK EYES."
2060 P$(22)="A STRANGE LOOKING CREATURE WITH FURRY FEET.":P$(23)="AN EVIL BARBARIAN. HE LOOKS MEAN."
2070 P$(24)="A BLOOD-THIRSTY VAMPIRE. WATCH OUT!":P$(19)="A WEREWOLF WITH FIERY EYES. HE GROWLS AT YOU!"
2080 P$(25)="AN EVIL DRAGON WITH TOUGH SCALES AND POWERFUL CLAWS! LOOK OUT!":RETURN
