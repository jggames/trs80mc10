10 GOTO 1000
20 GOTO 1010
100 CLS:RETURN
110 V=VE+1:H=HO+1:IF V>24 THEN V=24 ELSE IF V<1 THEN V=1
111 IF H>80 THEN H=80 ELSE IF H<1 THEN H=1
112 LOCATE V,H:RETURN
120 VE=CSRLIN-1:HO=POS(0)-1:RETURN
200 IN$=INKEY$:RETURN
210 IN$=INKEY$:IF IN$="" THEN 210 ELSE RETURN
250 BEEP:RETURN
260 RANDOMIZE TIMER:RV=RND:RETURN
270 FR=FRE(0):RETURN
300 SR$=STR$(SR):SR$=RIGHT$(SR$,(LEN(SR$)-1)):RETURN
310 SR$="":IF SR>=10^(CT-CN-SGN(CN)) THEN SR$=STRING$(CT,42):RETURN
311 IF SR<-.5*10^-CN THEN SR$="-"
312 SR$=SR$+MID$(STR$(INT(ABS(SR)+.5*10^-CN)),2):IF CN=0 THEN SR$=STRING$(CT-LEN(SR$),32)+SR$:RETURN
313 SR$=SR$+MID$(STR$(1+ABS(SR)-INT(ABS(SR))+.5*10^-CN),3,CN+1):SR$=STRING$(CT-LEN(SR$),32)+SR$:RETURN
350 LPRINT SR$;:RETURN
360 LPRINT:RETURN
1000 A=400:GOTO 20
1010 GOSUB 10000:E$="KIJK"
1100 PT=PT-1:IF P=6 THEN BL=BL+1
1110 DK=DR
1120 IF LP<>1 THEN 1130
1125 L$="":I$="0":GOSUB 12000:IF W=-1 THEN 4100
1130 IF P<>6 THEN 1140
1132 IF BL>8 THEN PRINT "DE GEUR VAN DE BLOEMEN BEDWELMT JE."
1133 IF BL>8 THEN 3990
1134 IF BL<6 THEN 1140
1135 M$="THE STRANGE WHITE FLOWERS SMELL VERY PECULIAR…":GOSUB1
1136 L$="WHITE FLOWERS"
1140 IF PT<=0 THEN 3990
1145 IF LP=0 THEN 1370
1150 IF W+VP=2 THEN M$="WEENA WALKS WITH YOU.":GOSUB1:WP=P
1155 GOSUB 260:LM=RV
1160 IF DK=1 THEN 14000
1170 IF P=0 THEN P=6:LP=1:VP=0:GOTO 1100
1180 IF P=-1 THEN P=4:LP=1:VP=0:GOTO 1100
1185 IF P=-2 THEN P=ZX:LP=1:VP=0:GOTO 1100
1190 S=0:IF (WP=P) AND (W=0) THEN S=2
1200 VP=0:FOR A=1 TO AO
1210 IF ((G(A)<>P) OR (A-1+SC=0)) AND (S<>2) THEN 1250
1220 IFS<>1THENPRINT"YOU SEE:":S=S+1
1230 IF S=3 THEN S=1:PRINT "WEENA":W=1:GOTO 1250
1240 PRINT G$(A)
1250 NEXT A
1270 PRINT "  MOGELIJKE UITGANGEN ZIJN:"
1280 FOR A=1 TO 10
1290 IF D(P,A)=0 THEN 1310
1300 GOSUB 19000:PRINT " ";
1310 NEXT A:PRINT
1320 IF L=0 THEN 1370
1330 GOSUB 260:IF RV>.05 THEN 1370
1340 PRINT "  EEN PLOTSELINGE WINDVLAAG BLAAST DE"
1350 PRINT "LAMP UIT.":L=0
1370 IF DK=1 THEN PRINT "  DE DRAAK KIJKT JE WOEST EN HONGERIG"
1372 IF DK=1 THEN PRINT "AAN."
1373 Q$="  JE BENT "
1374 IF PT>=5 THEN 1380
1375 PRINT Q$;"UITGEPUT EN ERG VERZWAKT. JE"
1378 PRINT "KUNT GEEN STAP MEER VERZETTEN.":GOTO 1400
1380 IF PT<8 THEN PRINT Q$;"ERG HONGERIG.":GOTO 1400
1390 IF PT<11 THEN PRINT Q$;"VERMOEID EN HONGERIG.":GOTO 1400
1396 IF PT>=15 THEN 1400
1397 PRINT "  JE ZOU ZO ZOETJESAAN EENS AAN EEN"
1398 PRINT "MAALTIJD MOETEN GAAN DENKEN."
1400 IF (P=2) AND (W=1) THEN 5000
1405 IF LP=0 THEN 1420
1410 PRINT "======================================="
1420 PRINT:IF E$<>"" THEN LP=0:GOTO 1427
1425 PRINT "WAT WIL JE DOEN?":INPUT E$:LP=0:CP=0
1427 GOSUB 18000:CP=1:IF C$="" THEN 1425
1430 D$=MID$(C$+"   ",1,3)
1440 A$="N  NO O  ZO Z  ZW W  NW OP NEE"
1450 B$="N  NO O  ZO Z  ZW W  NW OP NEE"
1460 GOSUB 16000:IF C=0 THEN 1520
1470 IF D(P,C)=0 THEN PRINT "DAAR KUN JE NIET HEEN.":GOTO 3800
1480 IF PT<5 THEN 1510
1490 ZX=P:P=D(P,C):LP=1:VP=1:IF P=30 THEN W=0
1500 A=C:PRINT "JE GAAT ";
1502 IF A<9 THEN PRINT "NAAR HET ";
1505 GOSUB 19000:IF A<9 THEN PRINT "EN.";
1506 PRINT:FOR A=1 TO 500:NEXT A:GOTO 1100
1510 PRINT "JE BENT TE ZWAK OM TE LOPEN.":E$="":GOTO 1400
1520 A$="KIJOPELAMINVPAKLEGEETONDSTOHEL"
1530 B$="KIJOPELAMINVPAKLEGEETONDSTOHEL"
1540 GOSUB 16000:IF C=0 THEN 1610
1550 IF C=1 THEN LP=1
1555 IF C>5 THEN 1570
1560 ON C GOTO 1100,2000,2100,2200,2300
1570 ON C-5 GOTO 2400,2500,2800,6000,7000
1610 PRINT "IK BEGRIJP JE NIET.":E$="":GOTO 1400
2000 IF P=2 THEN 2010
2002 IF P=6 THEN 2040
2004 IF P=25 THEN 2080
2006 PRINT "JE KUNT HIER NIETS OPENEN.":GOTO 3800
2010 IF BK<>1 THEN 2006
2015 PRINT "DE BRANDKAST HEEFT EEN NUMMERSLOT."
2020 PRINT "WELKE COMBINATIE WIL JE PROBEREN?"
2025 PRINT ">>>";:INPUT CO
2030 IF BC<>CO THEN PRINT "DE COMBINATIE IS FOUT.":GOTO 1100
2031 PRINT "HET SLOT KLIKT ZACHTJES, EN DE BRAND-"
2032 PRINT "KAST GAAT OPEN. BINNEN LIGT EEN KRIS-"
2033 PRINT "TALLEN STAAF."
2034 G(2)=P:BK=2:GOTO 1100
2040 IF G(8)<>0 THEN 2060
2045 PRINT "JE FORCEERT HET PANEEL MET HET BREEK-"
2050 PRINT "IJZER. HET PANEEL IS OPEN."
2055 LP=1:D(P,1)=5:GOSUB 15000:GOTO 1100
2060 PRINT "JE PROBEERT HET PANEEL OPEN TE BREKEN."
2070 PRINT "MET JE BLOTE HANDEN GAAT HET IN IEDER"
2075 PRINT "GEVAL NIET.":GOTO 1100
2080 IF G(10)=0 THEN 2090
2085 PRINT "DE DEUR ZIT OP SLOT.":GOTO 1100
2090 PRINT "JE MAAKT DE DEUR OPEN MET DE SLEUTEL."
2095 LP=1:D(P,9)=26:GOSUB 15000:GOTO 1100
2100 IF LEN(C$)<>8 THEN 1610
2110 D$=MID$(C$,6,3)
2120 IF (D$="AAN") OR (D$="AAN") THEN 2150
2130 IF (D$<>"UIT") AND (D$<>"UIT") THEN 1610
2135 IF G(11)<>0 THEN 2155
2140 IF L=0 THEN PRINT "DE LAMP WAS AL UIT.":GOTO 1100
2145 L=0:PRINT "O.K.":GOTO 1100
2150 IF L=1 THEN PRINT "DE LAMP WAS AL AAN.":GOTO 1100
2155 IF G(11)<>0 THEN PRINT "JE HEBT GEEN LAMP.":GOTO 3800
2156 IF G(14)<>0 THEN PRINT "JE HEBT GEEN LUCIFERS.":GOTO 3800
2157 IF (P<35) OR (P=42) OR (LM>.4) THEN 2160
2158 PRINT "HET WAAIT NU TE HARD OM DE LAMP AAN"
2159 PRINT "TE STEKEN.":GOTO 3800
2160 L=1:PRINT "O.K.":GOTO 1100
2200 PRINT "JE HEBT BIJ JE:":S=0
2210 FOR A=1 TO AO:IF G(A)<>0 THEN 2230
2220 S=1:PRINT "  ";G$(A)
2230 NEXT A
2240 IF S=0 THEN PRINT "  NIETS."
2250 GOTO 1100
2300 S=0:B=0:Q=-1:FOR A=1 TO AO
2310 IF G(A)=P THEN S=S+1:Q=A
2315 IF G(A)=0 THEN B=B+1
2320 NEXT A:M=SGN(LEN(L$))
2325 IF B>=4 THEN PRINT "JE KUNT NIETS MEER DRAGEN.":GOTO 3800
2330 IF S+M=0 THEN PRINT "ER LIGT HIER NIETS.":GOTO 3800
2340 IF S+M=1 THEN 2390
2345 PRINT "WAT WIL JE PAKKEN:"
2350 I=1:FOR Q=1 TO S
2355 IF G(I)<>P THEN I=I+1:GOTO 2355
2360 PRINT " ";Q;": ";G$(I):I=I+1:NEXT Q
2362 IF M<>0 THEN PRINT " ";Q;": ";L$
2365 GOSUB 17000
2370 IF (V<1) OR (V>S+M) THEN PRINT "JE PAKT NIETS.":GOTO 1100
2372 IF V>S THEN PRINT "JE KUNT DAT NIET PAKKEN.":GOTO 3800
2375 I=1:FOR Q=1 TO V
2380 IF G(I)<>P THEN I=I+1:GOTO 2380
2385 I=I+1:NEXT Q:Q=I-1
2390 IF (Q=1) AND (SC=0) THEN S=0:GOTO 2330
2391 IF Q=-1 THEN V=9:GOTO 2372
2392 IF Q=15 THEN PRINT "HET IS TE ZWAAR.":GOTO 3800
2394 PRINT "JE PAKT: ";G$(Q)
2396 G(Q)=0:GOTO 1100
2400 S=0:FOR A=1 TO AO
2410 IF G(A)=0 THEN S=S+1:Q=A
2420 NEXT A
2430 IF S=0 THEN PRINT "JE HEBT NIETS.":E$="":GOTO 1400
2440 IF S=1 THEN 2490
2441 PRINT "WAT WIL JE NEERLEGGEN?":I=1:FOR Q=1 TO S
2445 IF G(I)<>0 THEN I=I+1:GOTO 2445
2450 PRINT " ";Q;": ";G$(I):I=I+1
2455 NEXT Q
2460 GOSUB 17000
2465 IF (V<1) OR (V>S) THEN PRINT "JE DOET NIETS.":GOTO 1400
2470 I=1:FOR Q=1 TO V
2475 IF G(I)<>0 THEN I=I+1:GOTO 2475
2480 I=I+1:NEXT Q:Q=I-1
2490 PRINT "O.K.":G(Q)=P
2491 DE=0:IF (Q=3) OR (Q=5) OR (Q=13) THEN DE=1
2492 IF (DE=1) AND (DR=1) THEN 3200
2493 IF Q=11 THEN L=0
2495 IF (P>29) AND (P<33) THEN 3500
2496 IF Q<>6 THEN 1100
2497 PRINT "JE ZET DE URN WAT ONHANDIG NEER. DE URN"
2498 PRINT "VALT IN SCHERVEN UITEEN."
2499 G(6)=-1:G(9)=P:GOTO 1100
2500 IF G(13)=0 THEN PRINT "JE EET DE ";G$(13):GOTO 3100
2505 IF G(3)=0 THEN PRINT "JE EET HET ONTBIJT.":GOTO 2530
2510 IF G(5)<>0 THEN PRINT "JE HEBT NIETS OM TE ETEN.":GOTO 3800
2515 PRINT "JE EET DE VRUCHTEN."
2520 G(5)=-1:GOTO 2540
2530 G(3)=-1
2540 PT=100:GOTO 1100
2600 IF SC=1 THEN 2870
2605 PRINT "JE HAALT HET SCHILDERIJ WEG.":SC=1
2610 PRINT "ACHTER HET SCHILDERIJ ZIE JE EEN"
2620 PRINT "BRANDKAST.":BK=1
2630 GOTO 1100
2700 PRINT "JE LEEST ZE VLUCHTIG DOOR. JE ZIET"
2710 PRINT "INGEWIKKELDE FORMULES, EN KRETEN ALS"
2720 PRINT "'VIERDE DIMENSIE', EN 'RUIMTE-TIJD-"
2730 PRINT "CONTINUUM', EN GETALLEN, ZOALS 4536,"
2740 PRINT "7285,";BC;", 1435":GOTO 1100
2800 S=0:FOR A=1 TO AO
2802 IF (G(A)=0) OR (G(A)=P) THEN S=S+1:Q=A
2804 NEXT A
2805 LL=LEN(L$)
2806 IF S+LL=0 THEN PRINT "JE HEBT NIETS.":GOTO 3800
2807 IF S=0 THEN 3400
2808 IF (S=1) AND (LL=0) THEN 2850
2810 PRINT "WAT WIL JE ONDERZOEKEN?"
2812 I=1:FOR A=1 TO S
2814 IF (G(I)<>0) AND (G(I)<>P) THEN I=I+1:GOTO 2814
2816 PRINT " ";A;": ";G$(I):I=I+1
2818 NEXT A:IF L$<>"" THEN PRINT " ";A;": ";L$:S=A
2819 GOSUB 17000
2820 IF (V<1) OR (V>S) THEN PRINT "JE DOET NIETS.":GOTO 3800
2822 IF V+SGN(LEN(L$))>S THEN 3400
2824 Q=0:FOR A=1 TO V:Q=Q+1
2825 IF (G(Q)<>0) AND (G(Q)<>P) THEN Q=Q+1:GOTO 2825
2826 NEXT A
2850 PRINT "JE ONDERZOEKT: ";G$(Q):PRINT
2851 IF Q=1 THEN 2600
2855 IF Q=4 THEN 2700
2860 IF Q=9 THEN 2900
2865 IF Q=7 THEN 3000
2866 IF Q=13 THEN 3300
2867 IF Q=15 THEN 3700
2870 PRINT "JE ZIET NIETS BIJZONDERS.":GOTO 1100
2900 PRINT "TUSSEN DE SCHERVEN LIGT EEN KLEINE"
2910 PRINT "SLEUTEL.":G(10)=P:GOTO 1100
3000 PRINT "HET BOEK IS HEEL ERG OUD EN STOFFIG. JE"
3010 PRINT "KUNT NOG NET DE TITEL ONTCIJFEREN:"
3020 PRINT "'THE TIME MACHINE', DOOR HG WELLS.":GOTO 1100
3100 PRINT "DE ";G$(13);" IS VERGIFTIGD.":PT=0
3110 GOTO 1140
3200 G(Q)=-1:IF Q=13 THEN 3210
3202 PRINT "DE DRAAK EET ";
3204 IF Q=3 THEN PRINT "HET OP."
3206 IF Q=5 THEN PRINT "ZE OP."
3208 GOTO 1100
3210 PRINT "DE DRAAK EET DE ";G$(13);" OP."
3215 PRINT "  DE TAART IS VERGIFTIGD. MET ZIJN"
3220 PRINT "LAATSTE ADEMTOCHT SLAAKT HIJ EEN VER-"
3225 PRINT "SCHRIKKELIJKE KREET, EN STERFT."
3230 G(15)=P:DR=2:I$="0":GOTO 1100
3300 PRINT "DE TAART RUIKT NAAR AMANDELEN.":GOTO 1100
3400 PRINT "JE ONDERZOEKT: ";L$:PRINT
3401 IF (L$="PANEEL") AND (D(6,1)=0) THEN 3430
3402 IF L$="PUT" THEN 3440
3403 IF L$="LUIK" THEN 3710
3404 IF L$="WITTE BLOEMEN" THEN 3750
3405 IF L$="TIJDMACHINE" THEN 3720
3406 IF L$<>"VITRINE" THEN 2870
3409 IF G(14)<>-1 THEN 2870
3410 PRINT "IN DE ";L$;" LIGT EEN DOOSJE LUCIFERS."
3420 G(14)=P:GOTO 1100
3430 PRINT "HET PANEEL ZIT GOED DICHT. JE ZIET AAN"
3435 PRINT "DE LINKERKANT EEN SCHARNIER.":GOTO 1100
3440 PRINT "JE KIJKT OMLAAG. DE PUT LIJKT ONTZET-"
3445 PRINT "TEND DIEP. JE ZIET EEN LADDER NAAR"
3450 PRINT "BENEDEN LOPEN.":GOTO 1100
3500 IF P=30 THEN 3550
3510 IF (LK=0) AND (P=31) THEN 3600
3520 PRINT "HET VALT NAAR BENEDEN.":G(Q)=33:GOTO 1100
3550 PRINT "HET VALT NAAR BENEDEN.":G(Q)=31+2*LK
3555 PRINT "ONDER JE HOOR JE EEN LUID";
3560 IF Q<>8 THEN PRINT "E PLOF.":GOTO 1100
3570 PRINT " GEKRAAK."
3580 G(Q)=33:LK=1:D(31,10)=32
3590 FOR A=1 TO AO:IF G(A)=31 THEN G(A)=33
3595 NEXT A:GOTO 1100
3600 S=0:FOR A=1 TO AO
3610 IF G(A)=P THEN S=S+1
3620 NEXT A:IF L=0 THEN 3640
3630 IF S<=2 THEN PRINT "HET LUIKJE BUIGT VERVAARLIJK DOOR..."
3640 IF S<>3 THEN 1100
3650 PRINT "HET LUIKJE BEGEEFT HET ONDER HET GROTE"
3660 PRINT "GEWICHT. JE VALT IN DE AFGROND.":PT=0
3670 GOTO 1140
3700 PRINT "DE DRAAK KIJKT EEN BEETJE TREURIG.":GOTO 1100
3710 PRINT "HET ZIET ER NIET AL TE STEVIG UIT.":GOTO 1100
3720 PRINT "HOEWEL JE GEEN EXPERT OP HET GEBIED VAN"
3730 PRINT "TIJDMACHINES BENT, ZIE JE TOCH AL GAUW"
3740 PRINT "DAT HIER WAARSCHIJNLIJK NOG EEN"
3745 PRINT G$(2);" MIST.":GOTO 1100
3750 PRINT "JE KUNT DE BLOEMEN NIET GOED THUIS-"
3760 PRINT "BRENGEN. ZE RUIKEN ERG STERK.":GOTO 1100
3800 E$="":GOTO 1100
3990 FOR W=1 TO 600:NEXT W:PRINT "JE BENT DOOD."
4000 PRINT:PRINT:PRINT "WIL JE HET NOG EEN KEER PROBEREN?"
4010 GOSUB 210
4020 IF (IN$="J") OR (IN$="J") THEN RUN
4030 IF (IN$<>"N") AND (IN$<>"N") THEN 4010
4040 END
4100 PRINT:PRINT "  JE HEBT DE OPDRACHT HELAAS NIET KUNNEN"
4110 PRINT "VOLBRENGEN. PAS DE VOLGENDE KEER EEN"
4120 PRINT "BEETJE BETER OP WEENA!":GOTO 4000
5000 PRINT
5010 PRINT "JE BENT WEER THUIS. JE BENT NAAR DE"
5020 PRINT "TOEKOMST GEGAAN, EN JE HEBT WEENA GE-"
5030 PRINT "RED. DE MENSHEID IS TROTS OP JE."
5040 PRINT "GEFELICITEERD!!!"
5050 GOTO 4000
6000 GOSUB 100
6010 PRINT "WEET JE HET ZEKER?"
6020 GOSUB 210
6030 IF (IN$="J") OR (IN$="J") THEN RUN
6040 LP=1:GOTO 1100
7000 IF PT>=5 THEN 7030
7010 PRINT "WAT DACHT JE VAN EEN LEKKERE, STEVIGE"
7020 PRINT "MAALTIJD...":GOTO 1400
7030 IF LEN(I$)<>1 THEN PRINT I$:GOTO 1400
7040 IF I$<>"1" THEN 7070
7050 PRINT "IN HET NOORDOOSTEN WACHT JE EEN AKELIGE"
7060 PRINT "DOOD...":GOTO 1400
7070 IF I$<>"2" THEN 7100
7080 PRINT "JE BENT ERGENS ANDERS DAN WAAR JE DENKT"
7090 PRINT "TE ZIJN. WEEST GEWAARSCHUWD!":GOTO 1400
7100 IF I$<>"4" THEN 7200
7105 PRINT "OP DIT MOMENT ZOU DE DRAAK ZICH ELK"
7110 PRINT "HAPJE LEKKER DOEN SMAKEN...":GOTO 1400
7200 IF I$="0" THEN 7230
7210 PRINT "DE ELOI SCHIJNEN EEN GOEDE SMAAK TE"
7220 PRINT "HEBBEN, WAT ETEN BETREFT.":GOTO 1400
7230 PRINT "JE DOET HET PRIMA. GA ZO DOOR!":GOTO 1400
10000 GOSUB 100
10010 PRINT "UITLEG 'DE TIJDMACHINE'"
10020 PRINT
10030 PRINT "  DE BEDOELING VAN DIT SPEL IS, DAT JE,"
10040 PRINT "DOOR OPDRACHTEN IN TE TYPEN, NAAR DE"
10050 PRINT "TOEKOMST REIST. DAAR MOET JE WEENA"
10060 PRINT "VINDEN EN NAAR HET HEDEN TERUGBRENGEN."
10070 PRINT "WEENA IS EEN MEISJE, DAT ALS EEN VAN DE"
10080 PRINT "LAATSTEN VAN ALLE MENSEN NOG OVER IS,"
10090 PRINT "SINDS DE AFSCHUWELIJKE MORLOCHS DE"
10100 PRINT "AARDE AAN HET VEROVEREN ZIJN."
10110 PRINT "  DE OPDRACHTEN, DIE JE IN MAG TIKKEN"
10120 PRINT "OM DEZE TAAK TE VOLBRENGEN, ZIJN:"
10130 GOSUB 10900
10140 PRINT "N, NO, NW, Z, ZO, ZW, W, O, OP, NEER,"
10150 PRINT
10160 PRINT "PAK       LAMP AAN"
10170 PRINT "LEG       LAMP UIT"
10180 PRINT "OPEN      INVENTARIS"
10190 PRINT "EET       ONDERZOEK"
10200 PRINT "KIJK      STOP"
10210 PRINT:PRINT "EN HELP.":PRINT
10220 PRINT "DE OPDRACHTEN MOGEN IN HOOFD- EN KLEINE"
10230 PRINT "LETTERS WORDEN INGETIKT. ZE MOGEN WOR-"
10240 PRINT "DEN AFGEKORT TOT DE EERSTE DRIE LETTERS"
10250 PRINT "VAN DIE OPDRACHT, BEHALVE LAMP AAN EN"
10255 PRINT "LAMP UIT."
10256 PRINT "JE KUNT OOK MEERDERE OPDRACHTEN INEENS"
10257 PRINT "INGEVEN. DE OPDRACHTEN MOETEN DAN VAN"
10258 PRINT "ELKAAR GESCHEIDEN WORDEN DOOR KOMMA'S"
10259 PRINT "OF PUNTKOMMA'S."
10260 HO=0:VE=20:GOSUB 110
10270 PRINT "EVEN GEDULD A.U.B.":GOSUB 11000
10280 GOSUB 110:GOTO 10910
10900 PRINT:PRINT:PRINT
10910 PRINT "...TOETS...       ":GOSUB 210:GOSUB 100:RETURN
11000 RESTORE
11010 DIM D(42,10)
11020 FOR L=1 TO 42:I=1
11030 IF I>10 THEN NEXT L:GOTO 11036
11031 READ S:IF S=-1 THEN S=I-11
11032 IF S<0 THEN 11034
11033 D(L,I)=S:I=I+1:GOTO 11030
11034 D(L,I)=0:I=I+1:S=S+1:IF S<>0 THEN 11034
11035 GOTO 11030
11036 AO=16
11040 DIM G$(AO),G(AO):FOR A=1 TO AO
11050 READ G$(A),G(A):NEXT A
11060 CP=0:F=1:PT=40:W=0:WP=28:LP=0:BK=0:LK=0
11070 VP=0:AM=0:BL=0:DR=0:SC=0:P=2:L=0:D=0
11080 GOSUB 260:BC=INT(RV*9000)+1000:RETURN
12000 GOSUB 100:J$="JE BENT IN "
12003 IF P>24 THEN 12060
12007 ON INT((P+5)/6) GOTO 12010,12020,13120,12040
12010 ON P GOTO 13000,13010,13020,13030,13040,13050
12020 ON P-6 GOTO 13060,13070,13080,13090,13100,13110
12040 IF P<21 THEN 13120
12050 ON P-20 GOTO 13140,13150,13160,13170
12060 IF P>29 THEN 12070
12065 ON P-24 GOTO 13180,13190,13200,13210,13220
12070 IF P<34 THEN 13500
12075 IF P=34 THEN 13400
12076 IF P=42 THEN 13230
12080 IF L<>0 THEN 12200
12090 GOSUB 260:I$="WEG HIER!"
12100 PRINT "JE ZIET NIET VEEL. HET IS DONKER."
12110 IF RV<.7 THEN 12150
12130 PRINT "  JE VOELT EEN HARIGE HAND LANGS JE"
12140 PRINT "GEZICHT STRIJKEN...":RETURN
12150 PRINT "  DE MORLOCHS VALLEN JE AAN, MAAR JE"
12160 PRINT "VERWEERT JE DAPPER.":PT=PT-10
12165 PRINT "JE VOELT JE KRACHTEN SNEL AFNEMEN."
12170 IF (G(8)<>0) OR (RV<.3) THEN 12180
12175 PRINT "JE DOODT ENKELE MORLOCHS MET HET BREEK-"
12176 PRINT "IJZER."
12180 IF PT<=0 THEN PRINT "DE MORLOCHS OVERMEESTEREN JE."
12190 RETURN
12200 GOSUB 260
12202 IF P=39 THEN I$="2"
12204 IF P=40 THEN I$="IENE MIENE MUTTE..."
12206 IF P=41 THEN I$="1"
12210 PRINT J$;"EEN LANGE, MUFFE TUNNEL."
12220 IF RV<.8 THEN RETURN
12230 PRINT "JE ZIET EEN MORLOCH HAASTIG WEGVLUCHTEN"
12240 PRINT "VOOR HET LICHT.":RETURN
13000 PRINT J$;"DE EETKAMER. IN HET MIDDEN"
13001 PRINT "VAN DE KAMER STAAT EEN TAFEL."
13002 L$="TAFEL":I$="ZOEK HET ZELF MAAR UIT.":RETURN
13010 PRINT J$;"DE WOONKAMER VAN EEN GROOT"
13011 I$="ONDERZOEK EENS WAT.":IF SC=1 THEN 13014
13012 PRINT "HUIS. AAN DE WAND HANGT EEN MOOI SCHIL-"
13013 PRINT "DERIJ.":RETURN
13014 PRINT "HUIS. OP DE PLAATS WAAR HET SCHILDERIJ"
13015 PRINT "HING, ZIE JE NU EEN BRANDKAST.":RETURN
13020 PRINT J$;"HET LABORATORIUM.":RETURN
13030 PRINT "JE ZIT IN DE TIJDMACHINE.":IF F=0 THEN RETURN
13031 L$="TIJDMACHINE":IF G(2)=0 THEN 13034
13032 PRINT "DE TIJDMACHINE IS NOG NIET HELEMAAL"
13033 PRINT "KLAAR.":I$="ZOEK OVERAL.":RETURN
13034 PRINT "JE MONTEERT DE KRISTALLEN STAAF OP DE"
13035 PRINT "JUISTE PLAATS, EN HAALT DE HANDLE OVER."
13036 PRINT "EEN OORVERDOVEND GERAAS KLINKT IN JE"
13037 PRINT "OREN. ALS JE DE TIJDMACHINE STOPT, RAAK"
13038 PRINT "JE BEWUSTELOOS."
13039 GOSUB 15000:P=0:G(2)=-1:RETURN
13040 PRINT "JE BEVINDT JE IN DE SPHINX. HIER IS OOK"
13041 PRINT "DE TIJDMACHINE!! JE NEEMT PLAATS, MAAR"
13042 PRINT "PLOTSELING KLAPT DE DEUR DICHT EN ZIT"
13043 PRINT "JE GEVANGEN. DE MORLOCHS VALLEN JE AAN."
13044 PRINT "JE PROBEERT DE TIJDMACHINE TE STARTEN,"
13045 PRINT "MAAR DE MORLOCHS HEBBEN DE STARTHANDLE"
13046 PRINT "ERAF GEHAALD!":IF G(12)=0 THEN 13300
13047 PRINT "DE MORLOCHS OVERMEESTEREN JE."
13048 PT=0:RETURN
13050 PRINT J$;"EEN TUIN MET VREEMDE BLOEMEN"
13051 PRINT "AAN DE NOORDKANT STAAT EEN WITTE SPHINX"
13053 L$="PANEEL":IF F<>1 THEN RETURN
13054 PRINT "JE BENT NOG EEN BEETJE VERDWAASD."
13055 PRINT "ALS JE WEER WAT BENT BIJGEKOMEN, BEMERK"
13056 PRINT "JE, DAT DE TIJDMACHINE WEG IS! JE ZIET"
13057 PRINT "EEN PANEEL IN DE ONDERKANT VAN DE"
13058 PRINT "SPHINX DICHTKLAPPEN..."
13059 F=0:RETURN
13060 PRINT J$;"DE EETZAAL VAN DE ELOI, DE"
13061 PRINT "BEWONERS VAN DE AARDE IN HET JAAR"
13062 PRINT "802.701":I$="3":RETURN
13070 PRINT J$;"DE SLAAPZAAL. LANGS DE MUREN"
13071 PRINT "LIGGEN ZACHTE KUSSENS.":L$="KUSSEN":RETURN
13080 PRINT "JE BENT AAN DE ZUIDELIJKE OEVER VAN"
13081 PRINT "EEN SNELSTROMENDE RIVIER.":IF WP<>28 THEN RETURN
13082 PRINT "SOMMIGE ELOI ZIJN AAN HET ZWEMMEN."
13083 IF (WP<>28) OR (W=1) THEN RETURN
13084 PRINT "JE ZIET ER EEN BIJNA VERDRINKEN!!!"
13085 I$="PROBEER HAAR TE REDDEN!":RETURN
13090 PRINT J$;"DE STAD. OVERAL STAAN MOOIE,"
13091 PRINT "MAAR VERVALLEN HUIZEN. ER SCHIJNT GEEN"
13092 PRINT "DUIDELIJK STRATENPLAN TE ZIJN.":RETURN
13100 PRINT "JE BENT OP EEN PLEINTJE. IN HET MIDDEN"
13101 PRINT "STAAT IETS, DAT LIJKT OP EEN WATERPUT."
13102 L$="PUT":IF W=0 THEN RETURN
13103 PRINT "WEENA KRUIPT PLOTSELING DICHT TEGEN"
13104 PRINT "JE AAN. ZE ZIET ER BANG UIT."
13105 I$="LAAT HAAR HIER MAAR ACHTER.":RETURN
13110 PRINT "JE STAAT OP EEN HOGE HEUVEL. IN HET"
13111 PRINT "WESTEN ZIE JE DE STAD LIGGEN, EN VER"
13112 PRINT "IN HET OOSTEN EEN GROOT, GROEN GEBOUW.":RETURN
13120 PRINT "JE BENT OP EEN KRONKELIG BOSPAD, IN EEN"
13121 PRINT "WOEST EN DONKER WOUD."
13122 I$="ZOEK JE WEG DOOR HET BOS.":GOSUB 260
13123 IF RV>.2 THEN 13130
13124 PRINT "IN DE VERTE ZIE JE WAT MORLOCHS LOPEN.":RETURN
13130 IF RV<.85 THEN RETURN
13131 PRINT "  PLOTSELING VALLEN ENKELE MORLOCHS JE"
13132 AM=AM+1:IF AM>8 THEN 13135
13133 PRINT "AAN. MAAR JE WAS OP JE HOEDE, EN JE"
13134 PRINT "VERJAAGT ZE.":RETURN
13135 IF W=0 THEN 13133
13136 PRINT "AAN. VOOR JE ER ERG IN HEBT, ONTVOEREN"
13137 PRINT "ZE WEENA, EN BEN JE ALLEEN."
13138 W=-1:RETURN
13140 PRINT "JE STAAT VOOR DE INGANG VAN EEN OUD EN"
13141 PRINT "IN ONBRUIK GERAAKT GROEN PORCELEINEN"
13142 PRINT "MUSEUM.":RETURN
13150 PRINT "JE STAAT IN EEN GROTE MUSEUMHAL. ER"
13151 PRINT "LIGT EEN DIKKE LAAG STOF OP DE VLOER.":RETURN
13160 PRINT "JE STAAT IN EEN KLEINE ZAAL, MET VELE"
13161 PRINT "KAPOTTE VITRINES.":L$="VITRINE":RETURN
13170 PRINT J$;"EEN ENORME ZAAL, MET GROTE"
13171 PRINT "MACHINES. DE MACHINES ZIEN ER OUD EN"
13172 PRINT "VERWAARLOOSD UIT.":L$="MACHINE":RETURN
13180 PRINT J$;"EEN KLEINE MUSEUMZAAL."
13181 PRINT "JE KUNT OMHOOG, VIA EEN TRAP ACHTER"
13182 PRINT "EEN DEUR."
13183 I$="ZOEK DE SLEUTEL OP.":RETURN
13190 PRINT "JE BENT OP DE MUSEUMZOLDER. HET IS HIER"
13191 PRINT "ERG STOFFIG. VREEMDE DIEREN OP STERK"
13192 PRINT "WATER KIJKEN JE GLAZIG AAN."
13193 L$="DIER OP STERK WATER":RETURN
13200 PRINT J$;"EEN GROTE MUSEUMZAAL,"
13201 PRINT "MET GROTE, GLAZEN VITRINES.":L$="VITRINES"
13202 RETURN
13210 IF WP<>28 THEN 13214
13211 PRINT "JE WAADT DE RIVIER IN, EN REDT HET"
13212 PRINT "MEISJE. ZE IS JE ERG DANKBAAR."
13213 W=1:RETURN
13214 PRINT "JE WAADT DE RIVIER IN. DE STROOM GRIJPT"
13215 PRINT "JE, JE VERLIEST JE EVENWICHT, EN"
13216 PRINT "VERDRINKT.":PT=0:RETURN
13220 PRINT J$;"EEN KLEIN EN ROMMELIG"
13221 PRINT "STUDEERVERTREK. OVERAL LIGGEN OPENGE-"
13222 PRINT "SLAGEN BOEKEN, EN STAPELS PAPIEREN.":RETURN
13230 GOSUB 13120:IF DR<>0 THEN RETURN
13231 PRINT "  JE ZIET EEN GROTE, HONGERIGE DRAAK."
13232 PRINT "DE DRAAK KOMT LANGZAAM OP JE AF.":DR=1
13233 I$="4":RETURN
13300 PRINT "WAT EEN GELUK, DAT JE 'M BIJ JE HEBT!!"
13305 PRINT "JE MONTEERT DE HANDLE, EN HAALT HEM"
13310 PRINT "OVER...."
13320 P=-1:GOTO 15000
13400 PRINT "JE VALT PLOTSELING IN EEN IJSKOUD MEER."
13405 IF G(16)=0 THEN 13480
13410 PRINT "JE PROBEERT TEGEN DE KANT OP TE KLIM-"
13420 PRINT "MEN, MAAR DIE IS VEEL TE STEIL."
13430 PRINT "JE VOELT JE KRACHTEN LANGZAAM WEGEBBEN."
13440 PRINT "MET EEN LAATSTE KRACHTSINSPANNING PRO-"
13450 PRINT "BEER JE JE NOG OP TE TREKKEN, MAAR"
13460 PRINT "TEVERGEEFS. HET WATER SLUIT ZICH ONVER-"
13470 PRINT "BIDDELIJK BOVEN JE...":PT=0:RETURN
13480 PRINT "GELUKKIG HEB JE DE AUTOBAND BIJ JE,"
13481 PRINT "WAARMEE JE TOCH EEN BEETJE BLIJFT DRIJ-"
13482 PRINT "VEN. NA ENIG PROBEREN LUKT HET JE TEGEN"
13483 PRINT "DE KANT OP TE KLIMMEN.":GOSUB 15000
13484 P=-2:RETURN
13500 IF L=0 THEN PRINT "HET IS DONKER.":RETURN
13510 IF P=31 THEN 13600
13520 IF P=33 THEN 13700
13525 P$=""
13530 PRINT J$;"EEN DIEPE PUT. VER BOVEN JE"
13540 PRINT "ZIE JE NOG EEN KLEIN STUKJE VAN DE"
13550 PRINT "BLAUWE LUCHT.":IF P$<>"" THEN PRINT P$
13560 RETURN
13600 P$="JE KUNT OMLAAG DOOR EEN KAPOT LUIK."
13605 IF LK=1 THEN L$="VERSPLINTERD LUIK":GOTO 13530
13610 L$="LUIK":I$="HEB JE NIKS ZWAARS BIJ JE?"
13620 PRINT J$;"EEN DIEPE PUT. JE STAAT OP"
13630 PRINT "EEN LUIKJE, DAT DOORBUIGT ONDER JE"
13640 PRINT "GEWICHT.":RETURN
13700 PRINT "JE STAAT OP DE BODEM VAN EEN DIEPE PUT."
13710 I$="ZOEK HET EENS WAT HOGEROP.":RETURN
14000 PRINT "DE DRAAK KOMT OP JE AF. HIJ ZIET ER"
14001 PRINT "WOEST EN HONGERIG UIT. JE PROBEERT NOG"
14002 PRINT "WEG TE RENNEN, MAAR TEVERGEEFS. DE"
14003 PRINT "DRAAK EET JE OP."
14010 PRINT "JE BENT DOOD.":GOTO 4000
15000 PRINT:PRINT "<TOETS>":GOSUB 210:RETURN
16000 C=0:FOR A=1 TO LEN(A$)-2 STEP 3
16010 IF (MID$(A$,A,3)=D$) OR (MID$(B$,A,3)=D$) THEN C=(A+2)/3
16020 NEXT A:RETURN
17000 PRINT ">>>";:INPUT V:RETURN
18000 IF E$="" THEN C$="":RETURN
18010 A=1
18020 IF (MID$(E$,A,1)=",") OR (MID$(E$,A,1)=";") THEN 18040
18030 A=A+1:IF A<=LEN(E$) THEN 18020
18035 C$=E$:E$="":GOTO 18060
18040 C$="":IF A<>1 THEN C$=LEFT$(E$,A-1)
18045 IF A=LEN(E$) THEN E$="":GOTO 18060
18050 E$=RIGHT$(E$,LEN(E$)-A)
18060 IF LEN(C$)<2 THEN 18070
18063 IF LEFT$(C$,1)<>" " THEN 18070
18065 C$=RIGHT$(E$,LEN(E$)-1):GOTO 18060
18070 IF CP=1 THEN PRINT " ";C$
18080 RETURN
19000 IF (A<=2) OR (A=8) THEN PRINT "NOORD";
19010 IF (A>3) AND (A<7) THEN PRINT "ZUID";
19020 IF (A>1) AND (A<5) THEN PRINT "OOST";
19030 IF (A>5) AND (A<9) THEN PRINT "WEST";
19040 IF A=9 THEN PRINT "OP";
19050 IF A=10 THEN PRINT "NEER";
19060 RETURN
25000 DATA -6,2,-1,-2,1,29,3,-1,2,-5,4,-1,-2,3,-1,-1,-2,10,0
25010 DATA 7,-1,6,-3,8,-1,7,-1,28,-3,10,-1,9,13,12,0,11,0,6
25020 DATA -1,10,0,14,15,-5,30,-2,18,-2,14,10,13,-1,-3,12,-2
25030 DATA 10,-1,-4,15,0,11,12,-1,14,19,-4,11,-1,-4,42,18,20
25040 DATA -1,20,-3,18,0,13,-1,0,17,-2,19,0,12,-1,18,-3,15,0
25050 DATA 14,-1,-3,16,17,-1,-4,22,0,42,-1,21,-3,23,-1,22,0
25060 DATA 25,-1,-4,25,0,22,27,-1,24,-5,23,-1,-9,25,-4,24,-1
25070 DATA -4,9,-1,-7,2,-1,-8,11,31,-8,30,-1,-6,35,0,31,33,36
25080 DATA -7,32,-1,-1,-4,41,36,41,-1,35,41,0,38,-1,-3,39,0
25090 DATA 36,35,-1,41,37,-4,36,-1,40,32,0,34,-1,0,34,-4,34
25100 DATA 35,-1,35,34,0,36,-1,0,21,18,-2,18,-1
26000 DATA "SCHILDERIJ",2,"KRISTALLEN STAAF",-1
26010 DATA "ONTBIJT",1,"STAPELTJE PAPIEREN",29
26020 DATA "VRUCHTEN",7,"URN",27
26030 DATA "BOEK",27,"ZWAAR BREEKIJZER",24
26040 DATA "GEBROKEN URN",-1,"SLEUTEL",-1
26050 DATA "LAMP",26,"HANDLE VAN TIJDMACHINE",40
26060 DATA "AMANDELTAART",7,"LUCIFERS",-1
26070 DATA "DODE DRAAK",-1,"AUTOBAND",15
