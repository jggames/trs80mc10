10 goto1000
20 goto1010
100 CLS:RETURN
110 V1=VE+1:H1=HO+1:IF V1>24 THEN V1=24:GOTO112
111 IF V1<1 THEN V1=1
112 IF H1>80 THEN H1=80
113 IF H1<1 THEN H1=1:GOTO114
114 LOCATE V1,H1:RETURN
120 VE=CSRLIN-1:HO=POS(0)-1:RETURN
200 IN$=INKEY$:RETURN
210 IN$=INKEY$:IF IN$="" THEN 210
211 RETURN
250 SOUND 1,1:RETURN
260 RV=RND(0):RETURN
270 FR=FRE(0):RETURN
300 SR$=STR$(SR):SR$=RIGHT$(SR$,(LEN(SR$)-1)):RETURN
310 SR$="":IF SR>=10^(CT-CN-SGN(CN)) THEN SR$=STRING$(CT,42):RETURN
311 IF SR<-.5*10^-CN THEN SR$="-"
312 SR$=SR$+MID$(STR$(INT(ABS(SR)+.5*10^-CN)),2):IF CN=0 THEN SR$=STRING$(CT-LEN(SR$),32)+SR$:RETURN
313 SR$=SR$+MID$(STR$(1+ABS(SR)-INT(ABS(SR))+.5*10^-CN),3,CN+1):SR$=STRING$(CT-LEN(SR$),32)+SR$:RETURN
350 LPRINT SR$;:RETURN
360 LPRINT:RETURN
1000 A=400:goto20
1010 GOSUB10000:E$="LOOK"
1100 PT=PT-1:IF P=6 THEN BL=BL+1
1110 DK=DR
1120 IF LP<>1 THEN 1130
1125 L$="":I$="0":GOSUB12000:IF W=-1 THEN 4100
1130 IF P<>6 THEN 1140
1132 IF BL>8 THEN PRINT "The scent of the flowers is stunning."
1133 IF BL>8 THEN 3990
1134 IF BL<6 THEN 1140
1135 PRINT "The strange white flowers smell very strange...":L$="white flowers"
1140 IF PT<=0 THEN 3990
1145 IF LP=0 THEN 1370
1150 IF W+VP=2 THEN PRINT "Weena walks with you.":WP=P
1155 GOSUB260:LM=RV
1160 IF DK=1 THEN 14000
1170 IF P=0 THEN P=6:LP=1:VP=0:goto1100
1180 IF P=-1 THEN P=4:LP=1:VP=0:goto1100
1185 IF P=-2 THEN P=ZX:LP=1:VP=0:goto1100
1190 S=0:IF (WP=P) AND (W=0) THEN S=2
1200 VP=0:FOR A=1 TO AO
1210 IF ((G(A)<>P) OR (A-1+SC=0)) AND (S<>2) THEN 1250
1220 IF S<>1 THEN PRINT "You see here:":S=S+1
1230 IF S=3 THEN S=1:PRINT "Weena":W=1:goto1250
1240 PRINT G$(A)
1250 NEXT A
1270 PRINT "Possible outputs are:"
1280 FOR A=1 TO 10
1290 IF D(P,A)=0 THEN 1310
1300 GOSUB19000:PRINT " ";
1310 NEXT A:PRINT
1320 IF L=0 THEN 1370
1330 GOSUB260:IF RV>.05 THEN 1370
1340 PRINT "A sudden gust of wind blows the lamp out.":L=0
1370 IF DK=1 THEN PRINT "The dragon looks furious and hungry"
1372 IF DK=1 THEN PRINT "on."
1373 Q$="You are "
1374 IF PT>=5 THEN 1380
1375 PRINT Q$+"exhausted and very weakened. You can no longer take a step.":goto1400
1380 IF PT<8 THEN PRINT Q$+"very hungry.":goto1400
1390 IF PT<11 THEN PRINT Q$+"tired and hungry.":goto1400
1396 IF PT>=15 THEN 1400
1397 PRINT "  You should start thinking about a meal."
1400 IF (P=2) AND (W=1) THEN 5000
1405 IF LP=0 THEN 1420
1410 PRINT "======================================="
1420 PRINT:IF E$<>"" THEN LP=0:goto1427
1425 PRINT "What do you want to do?":INPUT E$:LP=0:CP=0
1427 GOSUB18000:CP=1:IF C$="" THEN 1425
1430 D$=MID$(C$+"   ",1,3)
1440 A$="N  NE E  SE S  SW W  NW UP DOW"
1460 GOSUB16000:IF C=0 THEN 1520
1470 IF D(P,C)=0 THEN PRINT "You can't go there.":goto3800
1480 IF PT<5 THEN 1510
1490 ZX=P:P=D(P,C):LP=1:VP=1:IF P=30 THEN W=0
1500 A=C:PRINT "You go ";
1502 IF A<9 THEN PRINT "to the ";
1505 GOSUB19000:IF A<9 THEN PRINT "and.";
1506 PRINT:FOR A=1 TO 500:NEXT A:goto1100
1510 PRINT "You are too weak to walk.":E$="":goto1400
1520 A$="KIJOPELAMINVPAKLEGEETONDSTOHEL"
1540 GOSUB16000:IF C=0 THEN 1610
1550 IF C=1 THEN LP=1
1555 IF C>5 THEN 1570
1560 ON C goto1100,2000,2100,2200,2300
1570 ON C-5 goto2400,2500,2800,6000,7000
1610 PRINT "I don't understand.":E$="":goto1400
2000 IF P=2 THEN 2010
2002 IF P=6 THEN 2040
2004 IF P=25 THEN 2080
2006 PRINT "You can't open anything here.":goto3800
2010 IF BK<>1 THEN 2006
2015 PRINT "The safe has a combination lock. Which combination do you want to try?"
2025 PRINT ">>>";:INPUT CO
2030 IF BC<>CO THEN PRINT "The combination is wrong.":goto1100
2031 PRINT "The lock clicks softly, and the safe opens. Inside is a crystal bar."
2034 G(2)=P:BK=2:goto1100
2040 IF G(8)<>0 THEN 2060
2045 PRINT "You force the panel with the crowbar. The panel is open."
2055 LP=1:D(P,1)=5:GOSUB15000:goto1100
2060 PRINT "You try to break open the panel. In any case, it is not possible with your bare hands.":goto1100
2080 IF G(10)=0 THEN 2090
2085 PRINT "The door is locked.":goto1100
2090 PRINT "You open the door with the key."
2095 LP=1:D(P,9)=26:GOSUB15000:goto1100
2100 IF LEN(C$)<>8 THEN 1610
2110 D$=MID$(C$,6,3)
2120 IF D$="ON" THEN 2150
2130 IF D$<>"FROM" THEN 1610
2135 IF G(11)<>0 THEN 2155
2140 IF L=0 THEN PRINT "The lamp was already off.":goto1100
2145 L=0:PRINT "O.K.":goto1100
2150 IF L=1 THEN PRINT "The lamp was already on.":goto1100
2155 IF G(11)<>0 THEN PRINT "You don't have a lamp.":goto3800
2156 IF G(14)<>0 THEN PRINT "You have no matches.":goto3800
2157 IF (P<35) OR (P=42) OR (LM>.4) THEN 2160
2158 PRINT "It is now blowing too hard to light the lamp.":goto3800
2160 L=1:PRINT "O.K.":goto1100
2200 PRINT "You have with you:":S=0
2210 FOR A=1 TO AO:IF G(A)<>0 THEN 2230
2220 S=1:PRINT "  ";G$(A)
2230 NEXT A
2240 IF S=0 THEN PRINT "Nothing."
2250 goto1100
2300 S=0:B=0:Q=-1:FOR A=1 TO AO
2310 IF G(A)=P THEN S=S+1:Q=A
2315 IF G(A)=0 THEN B=B+1
2320 NEXT A:M=SGN(LEN(L$))
2325 IF B>=4 THEN PRINT "You can no longer wear anything.":goto3800
2330 IF S+M=0 THEN PRINT "There is nothing here.":goto3800
2340 IF S+M=1 THEN 2390
2345 PRINT "What do you want to grab:"
2350 I=1:FOR Q=1 TO S
2355 IF G(I)<>P THEN I=I+1:goto2355
2360 PRINT " ";Q;": ";G$(I):I=I+1:NEXT Q
2362 IF M<>0 THEN PRINT " ";Q;": ";L$
2365 GOSUB17000
2370 IF (V<1) OR (V>S+M) THEN PRINT "You don't take anything.":goto1100
2372 IF V>S THEN PRINT "You can't take that.":goto3800
2375 I=1:FOR Q=1 TO V
2380 IF G(I)<>P THEN I=I+1:goto2380
2385 I=I+1:NEXT Q:Q=I-1
2390 IF (Q=1) AND (SC=0) THEN S=0:goto2330
2391 IF Q=-1 THEN V=9:goto2372
2392 IF Q=15 THEN PRINT "It is too heavy.":goto3800
2394 PRINT "You take: ";G$(Q)
2396 G(Q)=0:goto1100
2400 S=0:FOR A=1 TO AO
2410 IF G(A)=0 THEN S=S+1:Q=A
2420 NEXT A
2430 IF S=0 THEN PRINT "You have nothing.":E$="":goto1400
2440 IF S=1 THEN 2490
2441 PRINT "What do you want to put down?":I=1:FOR Q=1 TO S
2445 IF G(I)<>0 THEN I=I+1:goto2445
2450 PRINT " ";Q;": ";G$(I):I=I+1
2455 NEXT Q
2460 GOSUB17000
2465 IF (V<1) OR (V>S) THEN PRINT "You do nothing.":goto1400
2470 I=1:FOR Q=1 TO V
2475 IF G(I)<>0 THEN I=I+1:goto2475
2480 I=I+1:NEXT Q:Q=I-1
2490 PRINT "O.K.":G(Q)=P
2491 DE=0:IF (Q=3) OR (Q=5) OR (Q=13) THEN DE=1
2492 IF (DE=1) AND (DR=1) THEN 3200
2493 IF Q=11 THEN L=0
2495 IF (P>29) AND (P<33) THEN 3500
2496 IF Q<>6 THEN 1100
2497 PRINT "You put the urn down awkwardly. The urn breaks into fragments."
2499 G(6)=-1:G(9)=P:goto1100
2500 IF G(13)=0 THEN PRINT "You eat the ";G$(13):goto3100
2505 IF G(3)=0 THEN PRINT "You eat breakfast.":goto2530
2510 IF G(5)<>0 THEN PRINT "You have nothing to eat.":goto3800
2515 PRINT "You eat the fruits."
2520 G(5)=-1:goto2540
2530 G(3)=-1
2540 PT=100:goto1100
2600 IF SC=1 THEN 2870
2605 PRINT "You take the painting away.":SC=1
2610 PRINT "You see a safe behind the painting.":BK=1
2630 goto1100
2700 PRINT "You read them quickly."
2720 PRINT "You see complex formulas, and phrases like "fourth dimension," and "space-time continuum," and numbers, like 4536,"
2740 PRINT "7285,";BC;", 1435":goto1100
2800 S=0:FOR A=1 TO AO
2802 IF (G(A)=0) OR (G(A)=P) THEN S=S+1:Q=A
2804 NEXT A
2805 LL=LEN(L$)
2806 IF S+LL=0 THEN PRINT "You have nothing.":goto3800
2807 IF S=0 THEN 3400
2808 IF (S=1) AND (LL=0) THEN 2850
2810 PRINT "What do you want to examine?"
2812 I=1:FOR A=1 TO S
2814 IF (G(I)<>0) AND (G(I)<>P) THEN I=I+1:goto2814
2816 PRINT " ";A;": ";G$(I):I=I+1
2818 NEXT A:IF L$<>"" THEN PRINT " ";A;": ";L$:S=A
2819 GOSUB17000
2820 IF (V<1) OR (V>S) THEN PRINT "You do nothing.":goto3800
2822 IF V+SGN(LEN(L$))>S THEN 3400
2824 Q=0:FOR A=1 TO V:Q=Q+1
2825 IF (G(Q)<>0) AND (G(Q)<>P) THEN Q=Q+1:goto2825
2826 NEXT A
2850 PRINT "You examine: ";G$(Q):PRINT
2851 IF Q=1 THEN 2600
2855 IF Q=4 THEN 2700
2860 IF Q=9 THEN 2900
2865 IF Q=7 THEN 3000
2866 IF Q=13 THEN 3300
2867 IF Q=15 THEN 3700
2870 PRINT "You don't see anything special.":goto1100
2900 PRINT "There is a small key between the fragments.":G(10)=P:goto1100
3000 PRINT "The book is very old and dusty. You can just decipher the title:"
3020 PRINT "'The Time Machine', by HG Wells.":goto1100
3100 PRINT "The "+G$(13)+" is poisoned.":PT=0
3110 goto1140
3200 G(Q)=-1:IF Q=13 THEN 3210
3202 PRINT "The dragon eats ";
3204 IF Q=3 THEN PRINT "it up."
3206 IF Q=5 THEN PRINT "them up."
3208 goto1100
3210 PRINT "The dragon eats the "+G$(13)+" up."
3215 PRINT "The cake has been poisoned. With his last breath, he gives a terrible cry and dies."
3230 G(15)=P:DR=2:I$="0":goto1100
3300 PRINT "The cake smells like almonds.":goto1100
3400 PRINT "You examine: ";L$:PRINT
3401 IF (L$="panel") AND (D(6,1)=0) THEN 3430
3402 IF L$="pit" THEN 3440
3403 IF L$="hatch" THEN 3710
3404 IF L$="White flowers" THEN 3750
3405 IF L$="time machine" THEN 3720
3406 IF L$<>"case" THEN 2870
3409 IF G(14)<>-1 THEN 2870
3410 PRINT "In the";L$;" is a box of matches."
3420 G(14)=P:goto1100
3430 PRINT "The panel is closed properly. You will see a hinge on the left.":goto1100
3440 PRINT "You look down. The well seems incredibly deep. You see a ladder going down.":goto1100
3500 IF P=30 THEN 3550
3510 IF (LK=0) AND (P=31) THEN 3600
3520 PRINT "It falls down.":G(Q)=33:goto1100
3550 PRINT "It falls down.":G(Q)=31+2*LK
3555 PRINT "Below you hear a loud";
3560 IF Q<>8 THEN PRINT " thud.":goto1100
3570 PRINT " crack."
3580 G(Q)=33:LK=1:D(31,10)=32
3590 FOR A=1 TO AO:IF G(A)=31 THEN G(A)=33
3595 NEXT A:goto1100
3600 S=0:FOR A=1 TO AO
3610 IF G(A)=P THEN S=S+1
3620 NEXT A:IF L=0 THEN 3640
3630 IF S<=2 THEN PRINT "The hatch bends dangerously ..."
3640 IF S<>3 THEN 1100
3650 PRINT "The hatch collapses under the great weight. You fall into the abyss.":PT=0
3670 goto1140
3700 PRINT "The dragon looks a bit sad.":goto1100
3710 PRINT "It doesn't look too firm.":goto1100
3720 PRINT "Although you are not an expert in the field of time machines, you will soon see another one here"
3745 PRINT G$(2);" fog.":goto1100
3750 PRINT "You cannot properly bring the flowers home. They smell very strong.":goto1100
3800 E$="":goto1100
3990 FOR W=1 TO 600:NEXT W:PRINT "You're dead."
4000 PRINT:PRINT:PRINT "Do you want to try again?"
4010 GOSUB210
4020 IFIN$="Y"THENRUN
4030 IFIN$<>"N"THEN4010
4040 END
4100 PRINT:PRINT "Unfortunately, you were unable to complete the assignment. Be a little better for Weena next time!":goto4000
5000 PRINT
5010 PRINT "You are back home. You went to the future, and you saved Weena. Humanity is proud of you."
5040 PRINT "CONGRATULATIONS!"
5050 goto4000
6000 GOSUB100
6010 PRINT "ARE YOU SURE?"
6020 GOSUB210
6030 IFIN$="Y"THENRUN
6040 LP=1:goto1100
7000 IF PT>=5 THEN 7030
7010 PRINT "How about a nice, hearty meal...":goto1400
7030 IF LEN(I$)<>1 THEN PRINT I$:goto1400
7040 IF I$<>"1" THEN 7070
7050 PRINT "In the northeast, awful death awaits ...":goto1400
7070 IF I$<>"2" THEN 7100
7080 PRINT "You are somewhere other than where you think you are. Be warned!":goto1400
7100 IF I$<>"4" THEN 7200
7105 PRINT "At this point, the dragon would make any snack taste good ...":goto1400
7200 IF I$="0" THEN 7230
7210 PRINT "The Eloi seem to have good taste when it comes to food.":goto1400
7230 PRINT "You're doing fine. Keep it up!":goto1400
10000 GOSUB100
10010 PRINT "'THE TIME MACHINE'"
10020 PRINT
10030 PRINT "The aim of this game is that you type in the future by typing in assignments."
10060 PRINT "There you have to find Weena and bring her back to the present."
10070 PRINT "Weena is one of the last people left since the horrible Morlochs conquered the earth."
10110 PRINT "The commands you can type in to complete this task are:"
10130 GOSUB10900
10140 PRINT "N, NE, NW, S, SE, SW"
10150 PRINT ", W, E, UP, DOWN,"
10160 PRINT "TAKE      LAMP ON"
10170 PRINT "PUT       LAMP OFF"
10180 PRINT "OPEN      INVENTORY"
10190 PRINT "EAT       EXAMINE"
10200 PRINT "LOOK      STOP"
10210 PRINT:PRINT "AND HELP.":PRINT
10230 PRINT "They may be abbreviated to the first three letters of that command, except LAMP ON and LAMP OFF."
10256 PRINT "You can also enter multiple commands at once."
10257 print "The commands must then be separated by commas or semicolons."
10260 HO=0:VE=20:GOSUB110
10270 PRINT "Please wait...":GOSUB11000
10280 GOSUB110:goto10910
10900 PRINT:PRINT:PRINT
10910 PRINT "...key...       ":GOSUB210:GOSUB100:RETURN
11000 RESTORE
11010 DIM D(42,10)
11020 FOR L=1 TO 42:I=1
11030 IF I>10 THEN NEXT L:goto11036
11031 READ S:IF S=-1 THEN S=I-11
11032 IF S<0 THEN 11034
11033 D(L,I)=S:I=I+1:goto11030
11034 D(L,I)=0:I=I+1:S=S+1:IF S<>0 THEN 11034
11035 goto11030
11036 AO=16
11040 DIM G$(AO),G(AO):FOR A=1 TO AO
11050 READ G$(A),G(A):NEXT A
11060 CP=0:F=1:PT=40:W=0:WP=28:LP=0:BK=0:LK=0
11070 VP=0:AM=0:BL=0:DR=0:SC=0:P=2:L=0:D=0
11080 GOSUB260:BC=INT(RV*9000)+1000:RETURN
12000 GOSUB100:J$="You're in "
12003 IF P>24 THEN 12060
12007 ON INT((P+5)/6) goto12010,12020,13120,12040
12010 ON P goto13000,13010,13020,13030,13040,13050
12020 ON P-6 goto13060,13070,13080,13090,13100,13110
12040 IF P<21 THEN 13120
12050 ON P-20 goto13140,13150,13160,13170
12060 IF P>29 THEN 12070
12065 ON P-24 goto13180,13190,13200,13210,13220
12070 IF P<34 THEN 13500
12075 IF P=34 THEN 13400
12076 IF P=42 THEN 13230
12080 IF L<>0 THEN 12200
12090 GOSUB260:I$="Get out of here!"
12100 PRINT "You don't see much. It is dark."
12110 IF RV<.7 THEN 12150
12130 PRINT "You feel a hairy hand brushing your face...":RETURN
12150 PRINT "The Morlochs attack you, but you bravely defend yourself.":PT=PT-10
12165 PRINT "You can feel your powers quickly diminishing."
12170 IF (G(8)<>0) OR (RV<.3) THEN 12180
12175 PRINT "You kill some Morlochs with the crowbar."
12180 IF PT<=0 THEN PRINT "The Morlochs overpower you."
12190 RETURN
12200 GOSUB260
12202 IF P=39 THEN I$="2"
12204 IF P=40 THEN I$="a common nut..."
12206 IF P=41 THEN I$="1"
12210 PRINT J$+"a long, musty tunnel."
12220 IF RV<.8 THEN RETURN
12230 PRINT "You see a Morloch rushing away from the light.":RETURN
13000 PRINT J$+"the dining room. In the center of the room is a table."
13002 L$="table":I$="Find out for yourself.":RETURN
13010 PRINT J$+"the living room of a large"
13011 I$="examine something.":IF SC=1 THEN 13014
13012 PRINT "House. A beautiful painting hangs on the wall.":RETURN
13014 PRINT "House. At the place where the painting hung, you now see a safe.":RETURN
13020 PRINT J$+"the laboratory.":RETURN
13030 PRINT "You are in the time machine.":IF F=0 THEN RETURN
13031 L$="time machine":IF G(2)=0 THEN 13034
13032 PRINT "The time machine is not quite ready yet.":I$="Search everywhere.":RETURN
13034 PRINT "You mount the crystal bar in the right place, and pull the handle."
13036 PRINT "A deafening roar rings in your ears. As you stop the time machine, you become unconscious."
13039 GOSUB15000:P=0:G(2)=-1:RETURN
13040 PRINT "You are in the sphinx. Here is also the time machine!"
13042 PRINT "You take a seat, but suddenly the door closes and you are caught. The Morlochs are attacking you."
13044 PRINT "You are trying to start the time machine, but the Morlochs have taken off the start handle!"
13046 PRINT IF G(12)=0 THEN 13300
13047 PRINT "The Morlochs overpower you."
13048 PT=0:RETURN
13050 PRINT J$+"a garden with strange flowers On the north side is a white sphinx"
13053 L$="panel":IF F<>1 THEN RETURN
13054 PRINT "You are still a bit dazed. When you have recovered a bit, you will notice that the time machine is gone!"
13056 PRINT "You see a panel in the bottom of the sphinx close ..."
13059 F=0:RETURN
13060 PRINT J$+"the dining room of the Eloi, the inhabitants of the earth in the year 802,701":I$="3":RETURN
13070 PRINT J$+"the dormitory. Soft cushions line the walls.":L$="cushion":RETURN
13080 PRINT "You are on the south bank of a fast flowing river.":IF WP<>28 THEN RETURN
13082 PRINT "Some Eloi are swimming."
13083 IF (WP<>28) OR (W=1) THEN RETURN
13084 PRINT "You will see one almost drowning!"
13085 I$="Try to save her!":RETURN
13090 PRINT J$+"he city. There are beautiful, but dilapidated houses everywhere. There doesn't seem to be a clear street plan.":RETURN
13100 PRINT "You are on a square. In the middle is something that looks like a well."
13102 L$="well":IF W=0 THEN RETURN
13103 PRINT "Weena suddenly crawls close to you. She looks scared."
13105 I$="Leave her here.":RETURN
13110 PRINT "You are on a high hill. In the west you can see the city, and far in the east a large, green building.":RETURN
13120 PRINT "You are on a winding forest path, in a wild and dark forest."
13122 I$="Find your way through the forest.":GOSUB260
13123 IF RV>.2 THEN 13130
13124 PRINT "In the distance you can see what Morlochs are walking.":RETURN
13130 IF RV<.85 THEN RETURN
13131 PRINT "Suddenly some Morlochs attack you"
13132 AM=AM+1:IF AM>8 THEN 13135
13133 PRINT "On. But you were on your guard, and you chase them away.":RETURN
13135 IF W=0 THEN 13133
13136 PRINT "On. Before you know it, they kidnap Weena, and you're alone."
13138 W=-1:RETURN
13140 PRINT "You are standing at the entrance of an old and disused green porcelain museum.":RETURN
13150 PRINT "You are in a large museum hall. There is a thick layer of dust on the floor.":RETURN
13160 PRINT "You are in a small room, with many broken display cases.":L$="cases":RETURN
13170 PRINT J$+"a huge hall, with large machines. The machines look old and neglected.":L$="machine":RETURN
13180 PRINT J$+"a small museum hall. You can go up, via a staircase behind a door."
13183 I$="Find the key.":RETURN
13190 PRINT "You are in the museum attic. It is very dusty here. Exotic taxidermy animals look at you with glassy eyes."
13193 L$="exotic animals":RETURN
13200 PRINT J$+"a large museum hall, with large glass display cases.":L$="cases"
13202 RETURN
13210 IF WP<>28 THEN 13214
13211 PRINT "You wade into the river and save the girl. She is very grateful to you."
13213 W=1:RETURN
13214 PRINT "You wade into the river. The current grabs you, you lose your balance, and you drown.":PT=0:RETURN
13220 PRINT J$+"a small and messy study room. Everywhere there are open books and piles of papers.":RETURN
13230 GOSUB13120:IF DR<>0 THEN RETURN
13231 PRINT "You see a big, hungry dragon. The dragon is slowly approaching you.":DR=1
13233 I$="4":RETURN
13300 PRINT "How lucky that you have it with you! You mount the handle, and pull it over..."
13320 P=-1:goto15000
13400 PRINT "You suddenly fall into an ice-cold lake."
13405 IF G(16)=0 THEN 13480
13410 PRINT "You try to climb to the side, but it is too steep."
13430 PRINT "You feel your powers slowly ebbing away."
13440 PRINT "With a last effort you try to pull yourself up, but in vain."
13441 PRINT "The water closes inexorably above you...":PT=0:RETURN
13480 PRINT "Fortunately, you have the car tire with you, which allows you to float a bit."
13483 PRINT "After some trying you manage to climb to the side.":GOSUB15000
13484 P=-2:RETURN
13500 IF L=0 THEN PRINT "It is dark.":RETURN
13510 IF P=31 THEN 13600
13520 IF P=33 THEN 13700
13525 P$=""
13530 PRINT J$+"a deep well. Far above you see a small piece of the blue sky.":IFP$<>""THENPRINTP$
13560 RETURN
13600 P$="You can go down through a broken hatch."
13605 IF LK=1 THEN L$="broke hatch":goto13530
13610 L$="luik":I$="Do you have nothing heavy with you?"
13620 PRINT J+"a deep well. You are standing on a hatch that bends under your weight.":RETURN
13700 PRINT "You are at the bottom of a deep well."
13710 I$="Look a little higher up.":RETURN
14000 PRINT "The dragon is coming at you. He looks ferocious and hungry."
14002 PRINT "You are still trying to run away, but to no avail. You eat the dragon."
14010 PRINT "You're dead.":goto4000
15000 PRINT:PRINT "<KEY>":GOSUB210:RETURN
16000 C=0:FOR A=1 TO LEN(A$)-2 STEP 3
16010 IF MID$(A$,A,3)=D$ THEN C=(A+2)/3
16020 NEXT A:RETURN
17000 PRINT ">>>";:INPUT V:RETURN
18000 IF E$="" THEN C$="":RETURN
18010 A=1
18020 IF (MID$(E$,A,1)=",") OR (MID$(E$,A,1)=";") THEN 18040
18030 A=A+1:IF A<=LEN(E$) THEN 18020
18035 C$=E$:E$="":goto18060
18040 C$="":IF A<>1 THEN C$=LEFT$(E$,A-1)
18045 IF A=LEN(E$) THEN E$="":goto18060
18050 E$=RIGHT$(E$,LEN(E$)-A)
18060 IF LEN(C$)<2 THEN 18070
18063 IF LEFT$(C$,1)<>" " THEN 18070
18065 C$=RIGHT$(E$,LEN(E$)-1):goto18060
18070 IF CP=1 THEN PRINT " ";C$
18080 RETURN
19000 IF (A<=2) OR (A=8) THEN PRINT "north";
19010 IF (A>3) AND (A<7) THEN PRINT "south";
19020 IF (A>1) AND (A<5) THEN PRINT "east";
19030 IF (A>5) AND (A<9) THEN PRINT "west";
19040 IF A=9 THEN PRINT "up";
19050 IF A=10 THEN PRINT "down";
19060 RETURN
25000 DATA -6,2,-1,-2,1,29,3,-1,2,-5,4,-1,-2,3,-1,-1,-2,10,0
25010 DATA 7,-1,6,-3,8,-1,7,-1,28,-3,10,-1,9,13,12,0,11,0,6
25020 DATA -1,10,0,14,15,-5,30,-2,18,-2,14,10,13,-1,-3,12,-2
25030 DATA 10,-1,-4,15,0,11,12,-1,14,19,-4,11,-1,-4,42,18,20
25040 DATA -1,20,-3,18,0,13,-1,0,17,-2,19,0,12,-1,18,-3,15,0
25050 DATA 14,-1,-3,16,17,-1,-4,22,0,42,-1,21,-3,23,-1,22,0
25060 DATA 25,-1,-4,25,0,22,27,-1,24,-5,23,-1,-9,25,-4,24,-1
25070 DATA -4,9,-1,-7,2,-1,-8,11,31,-8,30,-1,-6,35,0,31,33,36
25080 DATA -7,32,-1,-1,-4,41,36,41,-1,35,41,0,38,-1,-3,39,0
25090 DATA 36,35,-1,41,37,-4,36,-1,40,32,0,34,-1,0,34,-4,34
25100 DATA 35,-1,35,34,0,36,-1,0,21,18,-2,18,-1
26000 DATA "painting",2,"crystal staff",-1
26010 DATA "breakfast",1,"stack of papers",29
26020 DATA "fruits",7,"urn",27
26030 DATA "book",27,"heavy crowbar",24
26040 DATA "broken urn",-1,"key",-1
26050 DATA "lamp",26,"handle of time machine",40
26060 DATA "almond cake",7,"matches",-1
26070 DATA "dead dragon",-1,"tire",15
