0 CLEAR500:DIMCC,ZZ,M$,Z$:GOTO22
2 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
4 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
6 GOSUB2:RETURN
8 GOSUB2:GOTO26
14 PRINT@480,;:RETURN
16 IN$=INKEY$:ZZ=RND(0):IFIN$=""THEN16
18 RETURN
20 RV=RND(0):RETURN
22 A=400
24 GOSUB532:E$="LOOK"
26 PT=PT-1:IFP=6THENBL=BL+1
28 DK=DR
30 IFLP<>1THEN34
32 L$="":I$="0":GOSUB578:IFW=-1THEN492
34 IFP<>6THEN44
36 IFBL>8THENM$="THE SCENT OF THE FLOWERS IS STUNNING.":GOSUB2
38 IFBL>8THEN480
40 IFBL<6THEN44
42 M$="THE STRANGE WHITE FLOWERS SMELL VERY STRANGE...":L$="FLOWERS":GOSUB2
44 IFPT<=0THEN480
46 IFLP=0THEN86
48 IFW+VP=2THENM$="WEENA WALKS WITH YOU.":WP=P:GOSUB2:FORZZ=1TO1000:NEXT
50 GOSUB20:LM=RV
52 IFDK=1THEN796
54 IFP=0THENP=6:LP=1:VP=0:GOTO26
56 IFP=-1THENP=4:LP=1:VP=0:GOTO26
58 IFP=-2THENP=ZX:LP=1:VP=0:GOTO26
60 S=0:IFWP=PANDW=0THENS=2
62 VP=0:FORA=1TOAO:IF((G(A)<>P)OR(A-1+SC=0))AND(S<>2)THEN70
64 IFS<>1THENM$="YOU SEE HERE:":S=S+1:GOSUB2
66 IFS=3THENS=1:PRINT"WEENA":W=1:GOTO70
68 M$=G$(A):GOSUB2
70 NEXTA
72 M$="POSSIBLE EXITS ARE: ":GOSUB2
74 FORA=1TO10:IFD(P,A)=0THEN78
76 GOSUB836:M$=M$+", "
78 NEXTA:M$=LEFT$(M$,LEN(M$)-2):GOSUB2
80 IFL=0THEN86
82 GOSUB20:IFRV>.05THEN86
84 M$="A SUDDEN GUST OF WIND BLOWS THE LAMP OUT.":L=0:GOSUB2
86 IFDK=1THENM$="THE DRAGON LOOKS FURIOUS AND HUNGRY":GOSUB2
88 IFDK=1THENM$="ON.":GOSUB2
90 Q$="YOU ARE "
92 IFPT>=5THEN96
94 M$=Q$+"EXHAUSTED AND VERY WEAKENED. YOU CAN NO LONGER TAKE A STEP.":GOSUB2:GOTO104
96 IFPT<8THENM$=Q$+"VERY HUNGRY.":GOSUB2:GOTO104
98 IFPT<11THENM$=Q$+"TIRED AND HUNGRY.":GOSUB2:GOTO104
100 IFPT>=15THEN104
102 M$="YOU SHOULD START THINKING ABOUT A MEAL.":GOSUB2
104 IFP=2ANDW=1THEN494
106 IFLP=0THEN110
108 PRINT"================================";
110 IFE$<>""THENLP=0:GOTO114
112 PRINT"WHAT DO YOU WANT TO DO?":INPUTE$:LP=0:CP=0
114 GOSUB810:CP=1:IFC$=""THEN112
116 D$=MID$(C$+"   ",1,3)
118 A$="N  NE E  SE S  SW W  NW UP DOW"
120 GOSUB802:IFC=0THEN138
122 IFD(P,C)=0THENPRINT"YOU CAN'T GO THERE.":GOTO478
124 IFPT<5THEN136
126 ZX=P:P=D(P,C):LP=1:VP=1:IFP=30THENW=0
128 A=C:M$="YOU GO "
130 IFA<9THENM$=M$+"TO THE "
132 GOSUB836:IFA<9THENM$=M$+" AND... "
134 GOSUB2:FORZZ=1TO1500:NEXTZZ:GOTO26
136 M$="YOU ARE TOO WEAK TO WALK.":E$="":GOSUB2:GOTO104
138 A$="LOOOPELAMINVGETDROEATEXAQUIHEL"
140 GOSUB802:IFC=0THEN146
142 IFC=1THENLP=1
144 ONCGOTO26,148,182,208,218,264,308,336,500,508
146 PRINT"I DON'T UNDERSTAND.":E$="":GOTO104
148 IFP=2THEN156
150 IFP=6THEN166
152 IFP=25THEN174
154 M$="YOU CAN'T OPEN ANYTHING HERE.":GOSUB2:GOTO478
156 IFBK<>1THEN154
158 M$="THE SAFE HAS A COMBINATION LOCK. WHICH COMBINATION DO YOU WANT TO TRY?":GOSUB2
160 INPUT">>>>";CO
162 IFBC<>COTHENM$="THE COMBINATION IS WRONG.":GOTO8
164 M$="THE LOCK CLICKS SOFTLY, AND THE SAFE OPENS. INSIDE IS A CRYSTAL.":G(2)=P:BK=2:GOTO8
166 IFG(8)THEN172
168 M$="YOU FORCE THE PANEL WITH THE CROWBAR. THE PANEL IS OPEN.":GOSUB2
170 LP=1:D(P,1)=5:GOSUB800:GOTO26
172 M$="YOU TRY TO BREAK OPEN THE PANEL. IN ANY CASE, IT IS NOT POSSIBLE WITH YOUR BARE HANDS.":GOTO8
174 IFG(10)=0THEN178
176 PRINT"THE DOOR IS LOCKED.":GOTO26
178 M$="YOU OPEN THE DOOR WITH THE KEY.":GOSUB2
180 LP=1:D(P,9)=26:GOSUB800:GOTO26
182 IFLEN(C$)<7ANDLEN(C$)>8THEN146
184 D$=MID$(C$,6,3)
186 IFD$="ON"THEN196
188 IFD$<>"OFF"THEN146
190 IFG(11)THEN198
192 IFL=0THENM$="THE LAMP WAS ALREADY OFF.":GOTO8
194 L=0:PRINT"O.K.":GOTO26
196 IFL=1THENM$="THE LAMP WAS ALREADY ON.":GOTO8
198 IFG(11)THENM$="YOU DON'T HAVE A LAMP.":GOSUB2:GOTO478
200 IFG(14)THENM$="YOU HAVE NO MATCHES.":GOSUB2:GOTO478
202 IFP<35ORP=42ORLM>.4THENL=1:PRINT"O.K.":GOTO26
204 M$="IT IS NOW BLOWING TOO HARD TO LIGHT THE LAMP.":GOSUB2:GOTO478
208 M$="YOU HAVE WITH YOU: ":S=0
210 FORA=1TOAO:IFG(A)THEN214
212 S=1:M$=M$+G$(A)+", "
214 NEXTA:IFS=0THENM$=M$+"NOTHING.":GOTO8
216 M$=LEFT$(M$,LEN(M$)-2):GOTO8
218 S=0:B=0:Q=-1:FORA=1TOAO
220 IFG(A)=PTHENS=S+1:Q=A
222 IFG(A)=0THENB=B+1
224 NEXTA:M=SGN(LEN(L$))
226 IFB>=4THENM$="YOU ARE UNABLE TO CARRY ANYTHING MORE.":GOSUB2:GOTO478
228 IFS+M=0THENM$="THERE IS NOTHING HERE.":GOSUB2:GOTO478
230 IFS+M=1THEN254
232 M$="WHAT DO YOU WANT TO GET:":GOSUB2
234 I=1:FORQ=1TOS
236 IFG(I)<>PTHENI=I+1:GOTO236
238 M$=" "+STR$(Q)+": "+G$(I):I=I+1:GOSUB2:NEXTQ
240 IFMTHENM$=" "+STR$(Q)+": "+L$:GOSUB2
242 GOSUB808
244 IFV<1ORV>S+MTHEN M$="YOU DON'T TAKE ANYTHING.":GOTO8
246 IFV>STHENPRINT"YOU CAN'T TAKE THAT.":GOTO478
248 I=1:FORQ=1TOV
250 IFG(I)<>PTHENI=I+1:GOTO250
252 I=I+1:NEXTQ:Q=I-1
254 IFQ=1ANDSC=0THENS=0:GOTO228
256 IFQ=-1THENV=9:GOTO246
258 IFQ=15THENPRINT"IT IS TOO HEAVY.":GOTO478
260 M$="YOU TAKE: "+G$(Q):GOSUB2
262 G(Q)=0:GOTO26
264 S=0:FORA=1TOAO
266 IFG(A)=0THENS=S+1:Q=A
268 NEXTA
270 IFS=0THENPRINT"YOU HAVE NOTHING.":E$="":GOTO104
272 IFS=1THEN292
274 M$="WHAT DO YOU WANT TO PUT DOWN?":GOSUB2:I=1:FOR Q=1 TO S
276 IFG(I)THENI=I+1:GOTO276
278 M$=" "+STR$(Q)+": "+G$(I):GOSUB2:I=I+1
280 NEXTQ
282 GOSUB808
284 IFV<1ORV>STHENPRINT"YOU DO NOTHING.":GOTO104
286 I=1:FORQ=1TOV
288 IFG(I)THENI=I+1:GOTO288
290 I=I+1:NEXTQ:Q=I-1
292 PRINT"O.K.":G(Q)=P
294 DE=0:IFQ=3ORQ=5ORQ=13THENDE=1
296 IFDE=1ANDDR=1THEN394
298 IFQ=11THENL=0
300 IFP>29ANDP<33THEN436
302 IFQ<>6THEN26
304 M$="YOU PUT THE URN DOWN AWKWARDLY. THE URN BREAKS INTO FRAGMENTS.":GOSUB2
306 G(6)=-1:G(9)=P:GOTO26
308 IFG(13)=0THENM$="YOU EAT THE "+G$(13):GOSUB2:GOTO392
310 IFG(3)=0THENPRINT"YOU EAT BREAKFAST.":GOTO318
312 IFG(5)THENM$="YOU HAVE NOTHING TO EAT.":GOSUB2:GOTO478
314 PRINT"YOU EAT THE FRUITS."
316 G(5)=-1:GOTO320
318 G(3)=-1
320 PT=100:GOTO26
322 IFSC=1THEN386
324 M$="YOU TAKE THE PAINTING DOWN.":GOSUB2:SC=1
326 M$="YOU SEE A SAFE BEHIND THE PAINTING.":GOSUB2:BK=1
328 GOTO26
330 M$="YOU READ THEM QUICKLY.":GOSUB2
332 M$="YOU SEE COMPLEX FORMULAS, AND PHRASES LIKE 'FOURTH DIMENSION,' AND 'SPACE-TIME CONTINUUM,' AND NUMBERS, LIKE 4536,"
334 M$=M$+" 7285,"+STR$(BC)+", 1435":GOTO8
336 S=0:FORA=1TOAO
338 IFG(A)=0ORG(A)=PTHENS=S+1:Q=A
340 NEXTA
342 LL=LEN(L$)
344 IFS+LL=0THENPRINT"YOU HAVE NOTHING.":GOTO478
346 IFS=0THEN412
348 IFS=1ANDLL=0THEN372
350 M$="WHAT DO YOU WANT TO EXAMINE?":GOSUB2
352 I=1:FORA=1TOS
354 IFG(I)<>0ANDG(I)<>PTHENI=I+1:GOTO354
356 M$=" "+STR$(A)+": "+G$(I):I=I+1:GOSUB2
358 NEXTA:IFL$<>""THENM$=" "+STR$(A)+": "+L$:S=A:GOSUB2
360 GOSUB808
362 IFV<1ORV>STHENPRINT"YOU DO NOTHING.":GOTO478
364 IFV+SGN(LEN(L$))>STHEN412
366 Q=0:FORA=1TOV:Q=Q+1
368 IFG(Q)<>0ANDG(Q)<>PTHENQ=Q+1:GOTO368
370 NEXTA
372 M$="YOU EXAMINE: "+G$(Q):GOSUB2:PRINT
374 IFQ=1THEN322
376 IFQ=4THEN330
378 IFQ=9THEN388
380 IFQ=7THEN390
382 IFQ=13THEN410
384 IFQ=15THEN468
386 M$="YOU DON'T SEE ANYTHING SPECIAL.":GOTO8
388 M$="THERE IS A SMALL KEY BETWEEN THE FRAGMENTS.":G(10)=P:GOTO8
390 M$="THE BOOK IS VERY OLD AND DUSTY. YOU CAN JUST DECIPHER THE TITLE: 'THE TIME MACHINE', BY HG WELLS.":GOTO8
392 M$="THE "+G$(13)+" IS POISONED.":GOSUB2:PT=0:GOTO44
394 G(Q)=-1:IFQ=13THEN404
396 M$="THE DRAGON EATS "
398 IFQ=3THENM$=M$+"IT UP."
400 IFQ=5THENM$=M$+"THEM UP."
402 GOTO8
404 M$="THE DRAGON EATS THE "+G$(13)+" UP.":GOSUB2
406 M$="THE CAKE HAS BEEN POISONED. WITH HIS LAST BREATH, HE GIVES A TERRIBLE CRY AND DIES.":GOSUB2
408 G(15)=P:DR=2:I$="0":GOTO26
410 M$="THE CAKE SMELLS LIKE ALMONDS.":GOTO8
412 M$="YOU EXAMINE: "+L$:GOSUB2:PRINT
414 IFL$="PANEL"ANDD(6,1)=0THEN432
416 IFL$="WELL"THEN434
418 IFL$="HATCH"THEN470
420 IFL$="FLOWERS"THEN476
422 IFL$="TIME MACHINE"THEN472
424 IFL$<>"CASES"THEN386
426 IFG(14)<>-1THEN386
428 M$="IN THE "+L$+" IS A BOX OF MATCHES.":GOSUB2
430 G(14)=P:GOTO26
432 M$="THE PANEL IS TIGHTLY CLOSED. YOU SEE A HINGE ON THE LEFT.":GOTO8
434 M$="YOU LOOK DOWN. THE WELL SEEMS INCREDIBLY DEEP. YOU SEE A LADDER GOING DOWN.":GOTO8
436 IFP=30THEN442
438 IFLK=0ANDP=31THEN456
440 PRINT"IT FALLS DOWN.":G(Q)=33:GOTO26
442 PRINT"IT FALLS DOWN.":G(Q)=31+2*LK
444 M$="BELOW YOU HEAR A LOUD"
446 IFQ<>8THENM$=M$+" THUD.":GOTO8
448 M$=M$+" CRACK.":GOSUB2
450 G(Q)=33:LK=1:D(31,10)=32
452 FORA=1TOAO:IFG(A)=31THENG(A)=33
454 NEXTA:GOTO26
456 S=0:FORA=1TOAO
458 IFG(A)=PTHENS=S+1
460 NEXTA:IFL=0THEN464
462 IFS<=2THENM$="THE HATCH BENDS DANGEROUSLY...":GOSUB2
464 IFS<>3THEN26
466 M$="THE HATCH COLLAPSES UNDER THE GREAT WEIGHT. YOU FALL INTO THE ABYSS.":PT=0:GOSUB2:GOTO44
468 M$="THE DRAGON LOOKS A BIT SAD.":GOTO8
470 M$="IT DOESN'T LOOK TOO FIRM.":GOTO8
472 M$="ALTHOUGH YOU ARE NOT AN EXPERT IN THE FIELD OF TIME MACHINES, YOU NOTICE THAT A "
474 M$=M$+G$(2)+" IS MISSING FROM THE MACHINE.":GOTO8
476 M$="YOU CANNOT POSSIBLY BRING THE FLOWERS HOME. THEY SMELL VERY STRONG.":GOTO8
478 E$="":GOTO26
480 FORW=1TO2500:NEXTW:PRINT"YOU ARE DEAD."
482 PRINT:PRINT"DO YOU WANT TO TRY AGAIN?";
484 GOSUB16
486 IFIN$="Y"THENRUN
488 IFIN$<>"N"THEN484
490 END
492 PRINT:M$="UNFORTUNATELY, YOU WERE UNABLE TO COMPLETE THE ASSIGNMENT. YOU MUST SAVE WEENA NEXT TIME!":GOSUB2:GOTO482
494 PRINT
496 M$="YOU ARE BACK HOME. YOU WENT TO THE FUTURE, AND YOU SAVED WEENA. HUMANITY IS PROUD OF YOU. CONGRATULATIONS!
498 GOSUB2:GOTO482
500 CLS:PRINT"ARE YOU SURE?"
502 GOSUB16
504 IFIN$="Y"THENRUN
506 LP=1:GOTO26
508 IFPT>=5THEN512
510 M$="HOW ABOUT A NICE, HEARTY MEAL...":GOSUB2:GOTO104
512 IFLEN(I$)<>1THENM$=I$:GOSUB2:GOTO104
514 IFI$<>"1"THEN518
516 M$="TO THE NORTHEAST, AWFUL DEATH AWAITS...":GOSUB2:GOTO104
518 IFI$<>"2"THEN522
520 M$="YOU ARE SOMEWHERE OTHER THAN WHERE YOU THINK YOU ARE. BE WARNED!":GOSUB2:GOTO104
522 IFI$<>"4"THEN526
524 M$="AT THIS POINT, THE DRAGON MIGHT THINK ANY SNACK MIGHT TASTE GOOD...":GOSUB2:GOTO104
526 IFI$="0"THEN530
528 M$="THE ELOI SEEM TO HAVE GOOD TASTE WHEN IT COMES TO FOOD.":GOSUB2:GOTO104
530 M$="YOU'RE DOING FINE. KEEP IT UP!":GOSUB2:GOTO104
532 CLS:PRINTTAB(7)"'THE TIME MACHINE'"
534 PRINT:M$="THE AIM OF THIS GAME IS TO FIND WEENA AND BRING HER BACK TO THE PRESENT.":GOSUB2
536 M$="WEENA IS ONE OF THE LAST PEOPLE LEFT SINCE THE HORRIBLE MORLOCKS CONQUERED THE EARTH.":GOSUB2
538 M$="COMMANDS MAY BE ABBREVIATED TO THE FIRST THREE LETTERS, EXCEPT LAMP ON AND LAMP OFF.":GOSUB2
540 M$="YOU CAN ALSO ENTER MULTIPLE COMMANDS AT ONCE SEPARATED BY SEMICOLONS.":GOSUB2
542 GOSUB14:M$="PLEASE WAIT...":GOSUB550
544 GOSUB14:PRINT"HIT ANY KEY... ";:GOSUB16:CLS:RETURN
550 DIMD(42,10):FORL=1TO42:I=1
554 IFI>10THENNEXTL:GOTO566
556 READS:IFS=-1THENS=I-11
558 IFS<0THEN562
560 D(L,I)=S:I=I+1:GOTO554
562 D(L,I)=0:I=I+1:S=S+1:IFS<>0THEN562
564 GOTO554
566 AO=16
568 DIMG$(AO),G(AO):FORA=1TOAO:READG$(A),G(A):NEXTA
570 CP=0:F=1:PT=40:W=0:WP=28:LP=0:BK=0:LK=0:VP=0:AM=0:BL=0:DR=0:SC=0:P=2:L=0:D=0
576 GOSUB20:BC=INT(RV*9000)+1000:RETURN
578 CLS:J$="YOU'RE IN "
580 IFP>24THEN592
582 ONINT((P+5)/6)GOTO584,586,708,588
584 ONPGOTO638,642,648,650,662,674
586 ONP-6GOTO684,686,688,698,700,706
588 IFP<21THEN708
590 ONP-20GOTO726,728,730,732
592 IFP>29THEN596
594 ONP-24GOTO734,736,740,742,748
596 IFP<34THEN774
598 IFP=34THEN756
600 IFP=42THEN750
602 IFLTHEN624
604 GOSUB20:I$="GET OUT OF HERE!"
606 M$="YOU DON'T SEE MUCH. IT'S DARK.":GOSUB2
608 IFRV<.7THEN612
610 M$="YOU FEEL A HAIRY HAND BRUSHING YOUR FACE...":GOTO6
612 M$="THE MORLOCKS ATTACK YOU, BUT YOU BRAVELY DEFEND YOURSELF.":PT=PT-10:GOSUB2
614 M$="YOU CAN FEEL YOUR POWERS QUICKLY DIMINISHING.":GOSUB2
616 IFG(8)<>0ORRV<.3THEN620
618 M$="YOU KILL SOME MORLOCKS WITH THE CROWBAR.":GOSUB2
620 IFPT<=0THENM$="THE MORLOCKS OVERPOWER YOU.":GOSUB2
622 RETURN
624 GOSUB20:IFP=39THENI$="2"
628 IFP=40THENI$="A COMMON NUT..."
630 IFP=41THENI$="1"
632 M$=J$+"A LONG, MUSTY TUNNEL.":GOSUB2
634 IFRV<.8THENRETURN
636 M$="YOU SEE A MORLOCK RUSHING AWAY FROM THE LIGHT.":GOTO6
638 M$=J$+"THE DINING ROOM. IN THE CENTRE OF THE ROOM IS A TABLE.":GOSUB2
640 L$="TABLE":I$="FIND OUT FOR YOURSELF.":RETURN
642 M$=J$+"THE LIVING ROOM OF A LARGE "
644 I$="EXAMINE SOMETHING.":IFSC=1THENM$=M$+"HOUSE. AT THE PLACE WHERE THE PAINTING HUNG, YOU NOW SEE A SAFE.":GOTO6
646 M$=M$+"HOUSE. A BEAUTIFUL PAINTING HANGS ON THE WALL.":GOTO6
648 M$=J$+"THE LABORATORY.":GOTO6
650 M$="YOU ARE IN THE TIME MACHINE.":GOSUB2:IFF=0THENRETURN
652 L$="TIME MACHINE":IFG(2)=0THEN656
654 M$="THE TIME MACHINE IS NOT QUITE READY YET.":I$="SEARCH EVERYWHERE.":GOTO6
656 M$="YOU MOUNT THE CRYSTAL IN THE RIGHT PLACE, AND PULL THE HANDLE.":GOSUB2
658 M$="A DEAFENING ROAR RINGS IN YOUR EARS. AS YOU STOP THE TIME MACHINE, YOU BECOME UNCONSCIOUS.":GOSUB2
660 GOSUB800:P=0:G(2)=-1:RETURN
662 M$="YOU ARE IN THE SPHINX. THE TIME MACHINE IS ALSO HERE!":GOSUB2
664 M$="YOU TAKE A SEAT, BUT SUDDENLY THE DOOR CLOSES AND YOU ARE CAUGHT. THE MORLOCKS ARE ATTACKING YOU.":GOSUB2
666 M$="YOU ARE TRYING TO START THE TIME MACHINE, BUT THE MORLOCKS HAVE REMOVED THE START HANDLE!":GOSUB2
668 IFG(12)=0THEN754
670 PRINT"THE MORLOCKS OVERPOWER YOU."
672 PT=0:RETURN
674 M$=J$+"A GARDEN WITH STRANGE FLOWERS ON THE NORTH SIDE IS A WHITE SPHINX.":GOSUB2
676 L$="PANEL":IFF<>1THENRETURN
678 M$="YOU ARE STILL A BIT DAZED. WHEN YOU HAVE RECOVERED A BIT, YOU NOTICE THAT THE TIME MACHINE IS GONE!":GOSUB2
680 M$="NEARBY YOU SEE A PANEL IN THE BOTTOM OF THE SPHINX...":GOSUB2
682 F=0:RETURN
684 M$=J$+"THE DINING ROOM OF THE ELOI, THE INHABITANTS OF THE EARTH IN THE YEAR 802,701":I$="3":GOTO6
686 M$=J$+"THE DORMITORY. SOFT CUSHIONS LINE THE WALLS.":L$="CUSHION":GOTO6
688 M$="YOU ARE ON THE SOUTH BANK OF A FAST FLOWING RIVER.":GOSUB2:IFWP<>28THENRETURN
690 M$="SOME ELOI ARE SWIMMING.":GOSUB2
692 IFWP<>28ORW=1THENRETURN
694 M$="YOU SEE ONE WHO IS DROWNING!":GOSUB2
696 I$="TRY TO SAVE HER!":RETURN
698 M$=J$+"THE CITY. THERE ARE BEAUTIFUL, BUT DILAPIDATED HOUSES EVERYWHERE. THERE'S NO CLEAR STREET PLAN.":GOTO6
700 M$="YOU ARE ON A SQUARE. IN THE MIDDLE IS SOMETHING THAT LOOKS LIKE A WELL.":GOSUB2
702 L$="WELL":IFW=0THENRETURN
704 M$="WEENA SUDDENLY CRAWLS CLOSE TO YOU. SHE LOOKS SCARED.":I$="LEAVE HER HERE.":GOTO6
706 M$="YOU ARE ON A HIGH HILL. IN THE WEST YOU CAN SEE THE CITY, AND FAR IN THE EAST A LARGE, GREEN BUILDING.":GOTO6
708 M$="YOU ARE ON A WINDING PATH, IN A WILD AND DARK FOREST.":GOSUB2:I$="FIND YOUR WAY THROUGH THE FOREST.":GOSUB20
710 IFRV>.2THEN714
712 M$="IN THE DISTANCE YOU CAN SEE MORLOCKS WALKING.":GOTO6
714 IFRV<.85THENRETURN
716 M$="SUDDENLY SOME MORLOCKS ATTACK.":GOSUB2:AM=AM+1:IFAM>8THEN722
720 M$="BUT YOU WERE ON YOUR GUARD, AND YOU CHASE THEM AWAY.":GOTO6
722 IFW=0THEN720
724 M$="BEFORE YOU KNOW IT, THEY KIDNAP WEENA, AND YOU ARE ALONE.":W=-1:GOTO6
726 M$="YOU ARE STANDING AT THE ENTRANCE OF AN OLD AND DISUSED GREEN PORCELAIN MUSEUM.":GOTO6
728 M$="YOU ARE IN A LARGE MUSEUM HALL. THERE IS A THICK LAYER OF DUST ON THE FLOOR.":GOTO6
730 M$="YOU ARE IN A SMALL ROOM, WITH MANY BROKEN DISPLAY CASES.":L$="CASES":GOTO6
732 M$=J$+"A HUGE HALL, WITH LARGE MACHINES. THE MACHINES LOOK OLD AND NEGLECTED.":L$="MACHINE":GOTO6
734 M$=J$+"A SMALL MUSEUM HALL. YOU CAN GO UP, VIA A STAIRCASE BEHIND A DOOR.":I$="FIND THE KEY.":GOTO6
736 M$="YOU ARE IN THE MUSEUM ATTIC. IT IS VERY DUSTY HERE. EXOTIC TAXIDERMY ANIMALS LOOK AT YOU WITH GLASSY EYES."
738 L$="EXOTIC ANIMALS":GOTO6
740 M$=J$+"A LARGE MUSEUM HALL, WITH LARGE GLASS DISPLAY CASES.":L$="CASES":GOTO6
742 IFWP<>28THEN746
744 M$="YOU WADE INTO THE RIVER AND SAVE THE GIRL. SHE IS VERY GRATEFUL TO YOU.":W=1:GOTO6
746 M$="YOU WADE INTO THE RIVER. THE CURRENT GRABS YOU, YOU LOSE YOUR BALANCE, AND YOU DROWN.":PT=0:GOTO6
748 M$=J$+"A SMALL AND MESSY STUDY. EVERYWHERE THERE ARE OPEN BOOKS AND PILES OF PAPERS.":GOTO6
750 GOSUB708:IFDRTHENRETURN
752 M$="YOU SEE A BIG, HUNGRY DRAGON. THE DRAGON IS SLOWLY APPROACHING YOU.":DR=1:I$="4":GOTO6
754 M$="HOW LUCKY THAT YOU HAVE IT WITH YOU! YOU MOUNT THE HANDLE, AND PULL IT...":P=-1:GOSUB2:GOTO800
756 M$="YOU SUDDENLY FALL INTO AN ICE-COLD LAKE.":GOSUB2
758 IFG(16)=0THEN768
760 M$="YOU TRY TO CLIMB TO THE SIDE, BUT IT IS TOO STEEP.":GOSUB2
762 M$="YOU FEEL YOUR POWERS SLOWLY EBBING AWAY.":GOSUB2
764 M$="WITH A LAST EFFORT YOU TRY TO PULL YOURSELF UP, BUT IN VAIN.":GOSUB2
766 M$="THE WATER CLOSES INEXORABLY ABOVE YOU...":PT=0:GOTO6
768 M$="FORTUNATELY, YOU HAVE THE CAR TIRE WITH YOU, WHICH ALLOWS YOU TO FLOAT A BIT.":GOSUB2
770 M$="AFTER SOME TRYING YOU MANAGE TO CLIMB TO THE SIDE.":GOSUB2:GOSUB800
772 P=-2:RETURN
774 IFL=0THENPRINT"IT'S DARK.":RETURN
776 IFP=31THEN786
778 IFP=33THEN794
780 P$=""
782 M$=J$+"A DEEP WELL. FAR ABOVE YOU SEE A SMALL PIECE OF THE BLUE SKY.":GOSUB2:IFP$<>""THENM$=P$:GOSUB2
784 RETURN
786 P$="YOU CAN GO DOWN THROUGH A BROKEN HATCH."
788 IFLK=1THENL$="BROKEN HATCH":GOTO782
790 L$="HATCH":I$="DO YOU HAVE ANYTHING HEAVY WITH YOU?"
792 M$=J$+"A DEEP WELL. YOU ARE STANDING ON A HATCH THAT BENDS UNDER YOUR WEIGHT.":GOTO6
794 M$="YOU ARE AT THE BOTTOM OF A DEEP WELL.":I$="LOOK A LITTLE HIGHER UP.":GOTO6
796 M$="THE DRAGON IS COMING AT YOU. HE LOOKS FEROCIOUS AND HUNGRY.":GOSUB2
798 M$="YOU ARE STILL TRYING TO RUN AWAY, BUT TO NO AVAIL. THE DRAGON EATS YOU.":GOSUB2:PRINT"YOU'RE DEAD.":GOTO482
800 GOTO544
802 C=0:FORA=1TOLEN(A$)-2STEP3
804 IFMID$(A$,A,3)=D$THENC=(A+2)/3
806 NEXTA:RETURN
808 INPUT">>>";V:RETURN
810 IFE$=""THENC$="":RETURN
812 A=1
814 IFMID$(E$,A,1)=";"THEN820
816 A=A+1:IFA<=LEN(E$)THEN814
818 C$=E$:E$="":GOTO826
820 C$="":IFA<>1THENC$=LEFT$(E$,A-1)
822 IFA=LEN(E$)THENE$="":GOTO826
824 E$=RIGHT$(E$,LEN(E$)-A)
826 IFLEN(C$)<2THEN832
828 IFLEFT$(C$,1)<>" "THEN832
830 C$=RIGHT$(E$,LEN(E$)-1):GOTO826
832 IFCP=1THENM$=" "+C$:GOSUB2
834 RETURN
836 IFA<=2ORA=8THENM$=M$+"NORTH"
838 IFA>3ANDA<7THENM$=M$+"SOUTH"
840 IFA>1ANDA<5THENM$=M$+"EAST"
842 IFA>5ANDA<9THENM$=M$+"WEST"
844 IFA=9THENM$=M$+"UP"
846 IFA=10THENM$=M$+"DOWN"
848 RETURN
850 DATA-6,2,-1,-2,1,29,3,-1,2,-5,4,-1,-2,3,-1,-1,-2,10,,7,-1,6,-3,8,-1,7,-1,28,-3,10,-1,9,13,12,,11,,6
852 DATA-1,10,,14,15,-5,30,-2,18,-2,14,10,13,-1,-3,12,-2,10,-1,-4,15,,11,12,-1,14,19,-4,11,-1,-4,42,18,20
854 DATA-1,20,-3,18,,13,-1,,17,-2,19,,12,-1,18,-3,15,,14,-1,-3,16,17,-1,-4,22,,42,-1,21,-3,23,-1,22,0
856 DATA25,-1,-4,25,,22,27,-1,24,-5,23,-1,-9,25,-4,24,-1,-4,9,-1,-7,2,-1,-8,11,31,-8,30,-1,-6,35,,31,33,36
858 DATA-7,32,-1,-1,-4,41,36,41,-1,35,41,,38,-1,-3,39,,36,35,-1,41,37,-4,36,-1,40,32,,34,-1,,34,-4,34
860 DATA35,-1,35,34,,36,-1,,21,18,-2,18,-1
862 DATAPAINTING,2,CRYSTAL,-1,BREAKFAST,1,STACK OF PAPERS,29,FRUITS,7,URN,27,BOOK,27,HEAVY CROWBAR,24
864 DATABROKEN URN,-1,KEY,-1,LAMP,26,TIME MACHINE HANDLE,40,ALMOND CAKE,7,MATCHES,-1,DEAD DRAGON,-1,TIRE,15
920 REM BY GERTON A. LUNTER
930 REM MC-10 EDITS JIM GERRIE
