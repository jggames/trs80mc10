0 CLEAR500:DIMCC,ZZ,M$,Z$:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB1:RETURN
4 GOSUB1:GOTO1100
20 GOTO1000
100 CLS:RETURN
110 PRINT@480,;:RETURN
210 IN$=INKEY$:IFIN$=""THEN210
211 RETURN
260 RV=RND(0):RETURN
1000 A=400
1010 GOSUB10000:E$="LOOK"
1100 PT=PT-1:IFP=6THENBL=BL+1
1110 DK=DR
1120 IFLP<>1THEN1130
1125 L$="":I$="0":GOSUB12000:IFW=-1THEN4100
1130 IFP<>6THEN1140
1132 IFBL>8THENM$="THE SCENT OF THE FLOWERS IS STUNNING.":GOSUB1
1133 IFBL>8THEN3990
1134 IFBL<6THEN1140
1135 M$="THE STRANGE WHITE FLOWERS SMELL VERY STRANGE...":L$="WHITE FLOWERS":GOSUB1
1140 IFPT<=0THEN3990
1145 IFLP=0THEN1370
1150 IFW+VP=2THENM$="WEENA WALKS WITH YOU.":WP=P:GOSUB1
1155 GOSUB260:LM=RV
1160 IFDK=1THEN14000
1170 IFP=0THENP=6:LP=1:VP=0:GOTO1100
1180 IFP=-1THENP=4:LP=1:VP=0:GOTO1100
1185 IFP=-2THENP=ZX:LP=1:VP=0:GOTO1100
1190 S=0:IFWP=PANDW=0THENS=2
1200 VP=0:FORA=1TOAO:IF((G(A)<>P)OR(A-1+SC=0))AND(S<>2)THEN1250
1220 IFS<>1THENM$="YOU SEE HERE:":S=S+1:GOSUB1
1230 IFS=3THENS=1:PRINT"WEENA":W=1:GOTO1250
1240 M$=G$(A):GOSUB1
1250 NEXTA
1270 M$="POSSIBLE EXITS ARE: ":GOSUB1
1280 FORA=1TO10:IFD(P,A)=0THEN1310
1300 GOSUB19000:M$=M$+", "
1310 NEXT A:M$=LEFT$(M$,LEN(M$)-2):GOSUB1
1320 IFL=0THEN1370
1330 GOSUB260:IFRV>.05THEN1370
1340 M$="A SUDDEN GUST OF WIND BLOWS THE LAMP OUT.":L=0:GOSUB1
1370 IFDK=1THENM$="THE DRAGON LOOKS FURIOUS AND HUNGRY":GOSUB1
1372 IFDK=1THENM$="ON.":GOSUB1
1373 Q$="YOU ARE "
1374 IFPT>=5THEN1380
1375 M$=Q$+"EXHAUSTED AND VERY WEAKENED. YOU CAN NO LONGER TAKE A STEP.":GOSUB1:GOTO1400
1380 IFPT<8THENM$=Q$+"VERY HUNGRY.":GOSUB1:GOTO1400
1390 IFPT<11THENM$=Q$+"TIRED AND HUNGRY.":GOSUB1:GOTO1400
1396 IFPT>=15THEN1400
1397 M$="YOU SHOULD START THINKING ABOUT A MEAL.":GOSUB1
1400 IFP=2ANDW=1THEN5000
1405 IFLP=0THEN1420
1410 PRINT"================================";
1420 IFE$<>""THENLP=0:GOTO1427
1425 PRINT"WHAT DO YOU WANT TO DO?":INPUTE$:LP=0:CP=0
1427 GOSUB18000:CP=1:IFC$=""THEN1425
1430 D$=MID$(C$+"   ",1,3)
1440 A$="N  NE E  SE S  SW W  NW UP DOW"
1460 GOSUB16000:IFC=0THEN1520
1470 IFD(P,C)=0THENPRINT"YOU CAN'T GO THERE.":GOTO3800
1480 IFPT<5THEN1510
1490 ZX=P:P=D(P,C):LP=1:VP=1:IFP=30THENW=0
1500 A=C:M$="YOU GO "
1502 IFA<9THENM$=M$+"TO THE "
1505 GOSUB19000:IFA<9THENM$=M$+"AND "
1506 FORA=1TO1500:NEXTA:GOTO4
1510 M$="YOU ARE TOO WEAK TO WALK.":E$="":GOSUB1:GOTO1400
1520 A$="LOOOPELAMINVGETPUTEATEXESTOHEL"
1540 GOSUB16000:IFC=0THEN1610
1550 IFC=1THENLP=1
1560 ONCGOTO1100,2000,2100,2200,2300,2400,2500,2800,6000,7000
1610 PRINT"I DON'T UNDERSTAND.":E$="":GOTO1400
2000 IFP=2THEN2010
2002 IFP=6THEN2040
2004 IFP=25THEN2080
2006 M$="YOU CAN'T OPEN ANYTHING HERE.":GOSUB1:GOTO3800
2010 IFBK<>1THEN2006
2015 M$="THE SAFE HAS A COMBINATION LOCK. WHICH COMBINATION DO YOU WANT TO TRY?":GOSUB1
2025 INPUT">>>>";CO
2030 IFBC<>COTHENM$="THE COMBINATION IS WRONG.":GOTO4
2031 M$="THE LOCK CLICKS SOFTLY, AND THE SAFE OPENS. INSIDE IS A CRYSTAL BAR.":G(2)=P:BK=2:GOTO4
2040 IFG(8)THEN2060
2045 M$="YOU FORCE THE PANEL WITH THE CROWBAR. THE PANEL IS OPEN.":GOSUB1
2055 LP=1:D(P,1)=5:GOSUB15000:GOTO1100
2060 M$="YOU TRY TO BREAK OPEN THE PANEL. IN ANY CASE, IT IS NOT POSSIBLE WITH YOUR BARE HANDS.":GOTO4
2080 IFG(10)=0THEN2090
2085 PRINT"THE DOOR IS LOCKED.":GOTO1100
2090 M$="YOU OPEN THE DOOR WITH THE KEY.":GOSUB1
2095 LP=1:D(P,9)=26:GOSUB15000:GOTO1100
2100 IFLEN(C$)<>8THEN1610
2110 D$=MID$(C$,6,3)
2120 IFD$="ON"THEN2150
2130 IFD$<>"FROM"THEN1610
2135 IFG(11)THEN2155
2140 IFL=0THENM$="THE LAMP WAS ALREADY OFF.":GOTO4
2145 L=0:PRINT"O.K.":GOTO1100
2150 IFL=1THENM$="THE LAMP WAS ALREADY ON.":GOTO4
2155 IFG(11)THENM$="YOU DON'T HAVE A LAMP.":GOSUB1:GOTO3800
2156 IFG(14)THENM$="YOU HAVE NO MATCHES.":GOSUB1:GOTO3800
2157 IFP<35ORP=42ORLM>.4THEN2160
2158 M$="IT IS NOW BLOWING TOO HARD TO LIGHT THE LAMP.":GOSUB1:GOTO3800
2160 L=1:PRINT"O.K.":GOTO1100
2200 M$="YOU HAVE WITH YOU: ":S=0
2210 FORA=1TOAO:IFG(A)THEN2230
2220 S=1:M$=M$+G$(A)+", "
2230 NEXTA:IFS=0THENM$=M$+"NOTHING.":GOTO4
2250 M$=LEFT$(M$,LEN(M$)-2):GOTO4
2300 S=0:B=0:Q=-1:FORA=1TOAO
2310 IFG(A)=PTHENS=S+1:Q=A
2315 IFG(A)=0THENB=B+1
2320 NEXTA:M=SGN(LEN(L$))
2325 IFB>=4THENM$="YOU CAN NO LONGER WEAR ANYTHING.":GOSUB1:GOTO3800
2330 IFS+M=0THENM$="THERE IS NOTHING HERE.":GOSUB1:GOTO3800
2340 IFS+M=1THEN2390
2345 M$="WHAT DO YOU WANT TO GRAB:":GOSUB1
2350 I=1:FORQ=1TOS
2355 IFG(I)<>PTHENI=I+1:GOTO2355
2360 M$=" "+STR$(Q)+" : "+G$(I):I=I+1:GOSUB1:NEXTQ
2362 IFMTHENM$=" "+STR$(Q)+": "+L$:GOSUB1
2365 GOSUB17000
2370 IFV<1ORV>S+MTHEN M$="YOU DON'T TAKE ANYTHING.":GOTO4
2372 IFV>STHENPRINT"YOU CAN'T TAKE THAT.":GOTO3800
2375 I=1:FORQ=1TOV
2380 IFG(I)<>PTHENI=I+1:GOTO2380
2385 I=I+1:NEXTQ:Q=I-1
2390 IFQ=1ANDSC=0THENS=0:GOTO2330
2391 IFQ=-1THENV=9:GOTO2372
2392 IFQ=15THENPRINT"IT IS TOO HEAVY.":GOTO3800
2394 M$="YOU TAKE: "+G$(Q):GOSUB1
2396 G(Q)=0:GOTO1100
2400 S=0:FORA=1TOAO
2410 IFG(A)=0THENS=S+1:Q=A
2420 NEXTA
2430 IFS=0THENPRINT"YOU HAVE NOTHING.":E$="":GOTO1400
2440 IFS=1THEN2490
2441 M$="WHAT DO YOU WANT TO PUT DOWN?":GOSUB1:I=1:FOR Q=1 TO S
2445 IFG(I)THENI=I+1:GOTO2445
2450 M$=" "+STR$(Q)+" : "+G$(I):GOSUB1:I=I+1
2455 NEXTQ
2460 GOSUB17000
2465 IFV<1ORV>STHENPRINT"YOU DO NOTHING.":GOTO1400
2470 I=1:FORQ=1TOV
2475 IFG(I)THENI=I+1:GOTO2475
2480 I=I+1:NEXTQ:Q=I-1
2490 PRINT"O.K.":G(Q)=P
2491 DE=0:IFQ=3ORQ=5ORQ=13THENDE=1
2492 IFDE=1ANDDR=1THEN3200
2493 IFQ=11THENL=0
2495 IFP>29ANDP<33THEN3500
2496 IFQ<>6THEN1100
2497 M$="YOU PUT THE URN DOWN AWKWARDLY. THE URN BREAKS INTO FRAGMENTS.":GOSUB1
2499 G(6)=-1:G(9)=P:GOTO1100
2500 IFG(13)=0THENM$="YOU EAT THE "+G$(13):GOSUB1:GOTO3100
2505 IFG(3)=0THENPRINT"YOU EAT BREAKFAST.":GOTO2530
2510 IFG(5)THENM$="YOU HAVE NOTHING TO EAT.":GOSUB1:GOTO3800
2515 PRINT"YOU EAT THE FRUITS."
2520 G(5)=-1:GOTO2540
2530 G(3)=-1
2540 PT=100:GOTO1100
2600 IFSC=1THEN2870
2605 M$="YOU TAKE THE PAINTING AWAY.":GOSUB1:SC=1
2610 M$="YOU SEE A SAFE BEHIND THE PAINTING.":GOSUB1:BK=1
2630 GOTO1100
2700 M$="YOU READ THEM QUICKLY.":GOSUB1
2720 M$="YOU SEE COMPLEX FORMULAS, AND PHRASES LIKE 'FOURTH DIMENSION,' AND 'SPACE-TIME CONTINUUM,' AND NUMBERS, LIKE 4536,"
2740 M$=M$+"7285,"+STR$(BC)+", 1435":GOTO4
2800 S=0:FORA=1TOAO
2802 IFG(A)=0ORG(A)=PTHENS=S+1:Q=A
2804 NEXTA
2805 LL=LEN(L$)
2806 IFS+LL=0THENPRINT"YOU HAVE NOTHING.":GOTO3800
2807 IFS=0THEN3400
2808 IFS=1ANDLL=0THEN2850
2810 M$="WHAT DO YOU WANT TO EXAMINE?":GOSUB1
2812 I=1:FORA=1TOS
2814 IFG(I)<>0ANDG(I)<>PTHENI=I+1:GOTO2814
2816 M$=" "+STR$(A)+" : "+G$(I):I=I+1:GOSUB1
2818 NEXTA:IFL$<>""THENM$=" "+STR$(A)+" : "+L$:S=A:GOSUB1
2819 GOSUB17000
2820 IFV<1ORV>STHENPRINT"YOU DO NOTHING.":GOTO3800
2822 IFV+SGN(LEN(L$))>STHEN3400
2824 Q=0:FORA=1TOV:Q=Q+1
2825 IFG(Q)<>0ANDG(Q)<>PTHENQ=Q+1:GOTO2825
2826 NEXTA
2850 M$="YOU EXAMINE: "+G$(Q):GOSUB1:PRINT
2851 IFQ=1THEN2600
2855 IFQ=4THEN2700
2860 IFQ=9THEN2900
2865 IFQ=7THEN3000
2866 IFQ=13THEN3300
2867 IFQ=15THEN3700
2870 M$="YOU DON'T SEE ANYTHING SPECIAL.":GOTO4
2900 M$="THERE IS A SMALL KEY BETWEEN THE FRAGMENTS.":G(10)=P:GOTO4
3000 M$="THE BOOK IS VERY OLD AND DUSTY. YOU CAN JUST DECIPHER THE TITLE: 'THE TIME MACHINE', BY HG WELLS.":GOTO4
3100 M$="THE "+G$(13)+" IS POISONED.":GOSUB1:PT=0:GOTO1140
3200 G(Q)=-1:IFQ=13THEN3210
3202 M$="THE DRAGON EATS "
3204 IFQ=3THENM$=M$+"IT UP."
3206 IFQ=5THENM$=M$+"THEM UP."
3208 GOTO4
3210 M$="THE DRAGON EATS THE "+G$(13)+" UP.":GOSUB1
3215 M$="THE CAKE HAS BEEN POISONED. WITH HIS LAST BREATH, HE GIVES A TERRIBLE CRY AND DIES.":GOSUB1
3230 G(15)=P:DR=2:I$="0":GOTO1100
3300 M$="THE CAKE SMELLS LIKE ALMONDS.":GOTO4
3400 M$="YOU EXAMINE: "+L$:GOSUB1:PRINT
3401 IFL$="PANEL"ANDD(6,1)=0THEN3430
3402 IFL$="PIT"THEN3440
3403 IFL$="HATCH"THEN3710
3404 IFL$="FLOWERS"THEN3750
3405 IFL$="TIME MACHINE"THEN3720
3406 IFL$<>"CASE"THEN2870
3409 IFG(14)<>-1THEN2870
3410 M$="IN THE"+L$+" IS A BOX OF MATCHES.":GOSUB1
3420 G(14)=P:GOTO1100
3430 M$="THE PANEL IS CLOSED PROPERLY. YOU WILL SEE A HINGE ON THE LEFT.":GOTO4
3440 M$="YOU LOOK DOWN. THE WELL SEEMS INCREDIBLY DEEP. YOU SEE A LADDER GOING DOWN.":GOTO4
3500 IFP=30THEN3550
3510 IFLK=0ANDP=31THEN3600
3520 PRINT"IT FALLS DOWN.":G(Q)=33:GOTO1100
3550 PRINT"IT FALLS DOWN.":G(Q)=31+2*LK
3555 M$="BELOW YOU HEAR A LOUD";
3560 IFQ<>8THENM$=" THUD.":GOTO4
3570 M$=" CRACK.":GOSUB1
3580 G(Q)=33:LK=1:D(31,10)=32
3590 FORA=1TOAO:IFG(A)=31THENG(A)=33
3595 NEXTA:GOTO1100
3600 S=0:FORA=1TOAO
3610 IFG(A)=PTHENS=S+1
3620 NEXTA:IFL=0THEN3640
3630 IFS<=2THENM$="THE HATCH BENDS DANGEROUSLY...":GOSUB1
3640 IFS<>3THEN1100
3650 M$="THE HATCH COLLAPSES UNDER THE GREAT WEIGHT. YOU FALL INTO THE ABYSS.":PT=0:GOSUB1:GOTO1140
3700 M$="THE DRAGON LOOKS A BIT SAD.":GOTO4
3710 M$="IT DOESN'T LOOK TOO FIRM.":GOTO4
3720 M$="ALTHOUGH YOU ARE NOT AN EXPERT IN THE FIELD OF TIME MACHINES, YOU WILL SOON SEE ANOTHER ONE HERE":GOSUB1
3745 M$=G$(2)+" FOG.":GOTO4
3750 M$="YOU CANNOT PROPERLY BRING THE FLOWERS HOME. THEY SMELL VERY STRONG.":GOTO4
3800 E$="":GOTO1100
3990 FORW=1TO2500:NEXTW:PRINT"YOU'RE DEAD."
4000 PRINT:PRINT:PRINT"DO YOU WANT TO TRY AGAIN?"
4010 GOSUB210
4020 IFIN$="Y"THENRUN
4030 IFIN$<>"N"THEN4010
4040 END
4100 PRINT:M$="UNFORTUNATELY, YOU WERE UNABLE TO COMPLETE THE ASSIGNMENT. YOU MUST SAVE WEENA NEXT TIME!":GOSUB1:GOTO4000
5000 PRINT
5010 M$="YOU ARE BACK HOME. YOU WENT TO THE FUTURE, AND YOU SAVED WEENA. HUMANITY IS PROUD OF YOU.":GOSUB1
5040 PRINT"CONGRATULATIONS!":GOTO4000
6000 CLS:PRINT"ARE YOU SURE?"
6020 GOSUB210
6030 IFIN$="Y"THENRUN
6040 LP=1:GOTO1100
7000 IFPT>=5THEN7030
7010 M$="HOW ABOUT A NICE, HEARTY MEAL...":GOSUB1:GOTO1400
7030 IFLEN(I$)<>1THENM$=I$:GOSUB1:GOTO1400
7040 IFI$<>"1"THEN7070
7050 M$="IN THE NORTHEAST, AWFUL DEATH AWAITS...":GOSUB1:GOTO1400
7070 IFI$<>"2"THEN7100
7080 M$="YOU ARE SOMEWHERE OTHER THAN WHERE YOU THINK YOU ARE. BE WARNED!":GOSUB1:GOTO1400
7100 IFI$<>"4"THEN7200
7105 M$="AT THIS POINT, THE DRAGON WOULD MAKE ANY SNACK TASTE GOOD...":GOSUB1:GOTO1400
7200 IFI$="0"THEN7230
7210 M$="THE ELOI SEEM TO HAVE GOOD TASTE WHEN IT COMES TO FOOD.":GOSUB1:GOTO1400
7230 M$="YOU'RE DOING FINE. KEEP IT UP!":GOSUB1:GOTO1400
10000 CLS:PRINTTAB(8)"'THE TIME MACHINE'"
10020 PRINT:M$="THE AIM OF THIS GAME IS TO FIND WEENA AND BRING HER BACK TO THE PRESENT.":GOSUB1
10070 M$="WEENA IS ONE OF THE LAST PEOPLE LEFT SINCE THE HORRIBLE MORLOCHS CONQUERED THE EARTH.":GOSUB1
10230 M$="COMMANDS MAY BE ABBREVIATED TO THE FIRST THREE LETTERS, EXCEPT LAMP ON AND LAMP OFF.":GOSUB1
10256 M$="YOU CAN ALSO ENTER MULTIPLE COMMANDS AT ONCE SEPARATED BY SEMICOLONS.":GOSUB1
10270 GOSUB110:M$="PLEASE WAIT...":GOSUB11000
10280 GOSUB110
10910 PRINT"HIT ANY KEY... ";:GOSUB210:CLS:RETURN
11000 RESTORE
11010 DIMD(42,10)
11020 FORL=1TO42:I=1
11030 IFI>10THENNEXTL:GOTO11036
11031 READS:IFS=-1THENS=I-11
11032 IFS<0THEN11034
11033 D(L,I)=S:I=I+1:GOTO11030
11034 D(L,I)=0:I=I+1:S=S+1:IFS<>0THEN11034
11035 GOTO11030
11036 AO=16
11040 DIMG$(AO),G(AO):FORA=1TOAO
11050 READG$(A),G(A):NEXTA
11060 CP=0:F=1:PT=40:W=0:WP=28:LP=0:BK=0:LK=0
11070 VP=0:AM=0:BL=0:DR=0:SC=0:P=2:L=0:D=0
11080 GOSUB260:BC=INT(RV*9000)+1000:RETURN
12000 CLS:J$="YOU'RE IN "
12003 IFP>24THEN12060
12007 ONINT((P+5)/6)GOTO12010,12020,13120,12040
12010 ONPGOTO13000,13010,13020,13030,13040,13050
12020 ONP-6GOTO13060,13070,13080,13090,13100,13110
12040 IFP<21THEN13120
12050 ONP-20GOTO13140,13150,13160,13170
12060 IFP>29THEN12070
12065 ONP-24GOTO13180,13190,13200,13210,13220
12070 IFP<34THEN13500
12075 IFP=34THEN13400
12076 IFP=42THEN13230
12080 IFLTHEN12200
12090 GOSUB260:I$="GET OUT OF HERE!"
12100 M$="YOU DON'T SEE MUCH. IT IS DARK.":GOSUB1
12110 IFRV<.7THEN12150
12130 M$="YOU FEEL A HAIRY HAND BRUSHING YOUR FACE...":GOTO3
12150 M$="THE MORLOCHS ATTACK YOU, BUT YOU BRAVELY DEFEND YOURSELF.":PT=PT-10:GOSUB1
12165 M$="YOU CAN FEEL YOUR POWERS QUICKLY DIMINISHING.":GOSUB1
12170 IFG(8)<>0ORRV<.3THEN12180
12175 M$="YOU KILL SOME MORLOCHS WITH THE CROWBAR.":GOSUB1
12180 IFPT<=0THENM$="THE MORLOCHS OVERPOWER YOU.":GOSUB1
12190 RETURN
12200 GOSUB260
12202 IFP=39THENI$="2"
12204 IFP=40THENI$="A COMMON NUT..."
12206 IFP=41THENI$="1"
12210 M$=J$+"A LONG, MUSTY TUNNEL.":GOSUB1
12220 IFRV<.8THENRETURN
12230 M$="YOU SEE A MORLOCH RUSHING AWAY FROM THE LIGHT.":GOTO3
13000 M$=J$+"THE DINING ROOM. IN THE CENTER OF THE ROOM IS A TABLE.":GOSUB1
13002 L$="TABLE":I$="FIND OUT FOR YOURSELF.":RETURN
13010 M$=J$+"THE LIVING ROOM OF A LARGE":GOSUB1
13011 I$="EXAMINE SOMETHING.":IFSC=1THENM$=M$+"HOUSE. AT THE PLACE WHERE THE PAINTING HUNG, YOU NOW SEE A SAFE.":RETURN
13012 M$=M$+"HOUSE. A BEAUTIFUL PAINTING HANGS ON THE WALL.":RETURN
13020 M$=J$+"THE LABORATORY.":GOTO3
13030 M$="YOU ARE IN THE TIME MACHINE.":GOSUB1:IFF=0THENRETURN
13031 L$="TIME MACHINE":IFG(2)=0THEN13034
13032 M$="THE TIME MACHINE IS NOT QUITE READY YET.":I$="SEARCH EVERYWHERE.":GOTO3
13034 M$="YOU MOUNT THE CRYSTAL BAR IN THE RIGHT PLACE, AND PULL THE HANDLE.":GOSUB1
13036 M$="A DEAFENING ROAR RINGS IN YOUR EARS. AS YOU STOP THE TIME MACHINE, YOU BECOME UNCONSCIOUS.":GOSUB1
13039 GOSUB15000:P=0:G(2)=-1:RETURN
13040 M$="YOU ARE IN THE SPHINX. HERE IS ALSO THE TIME MACHINE!":GOSUB1
13042 M$="YOU TAKE A SEAT, BUT SUDDENLY THE DOOR CLOSES AND YOU ARE CAUGHT. THE MORLOCHS ARE ATTACKING YOU.":GOSUB1
13044 M$="YOU ARE TRYING TO START THE TIME MACHINE, BUT THE MORLOCHS HAVE TAKEN OFF THE START HANDLE!":GOSUB1
13046 IFG(12)=0THEN13300
13047 PRINT"THE MORLOCHS OVERPOWER YOU."
13048 PT=0:RETURN
13050 M$=J$+"A GARDEN WITH STRANGE FLOWERS ON THE NORTH SIDE IS A WHITE SPHINX":GOSUB1
13053 L$="PANEL":IFF<>1THENRETURN
13054 M$="YOU ARE STILL A BIT DAZED. WHEN YOU HAVE RECOVERED A BIT, YOU WILL NOTICE THAT THE TIME MACHINE IS GONE!":GOSUB1
13056 M$="YOU SEE A PANEL IN THE BOTTOM OF THE SPHINX CLOSE...":GOSUB1
13059 F=0:RETURN
13060 M$=J$+"THE DINING ROOM OF THE ELOI, THE INHABITANTS OF THE EARTH IN THE YEAR 802,701":I$="3":GOTO3
13070 M$=J$+"THE DORMITORY. SOFT CUSHIONS LINE THE WALLS.":L$="CUSHION":GOTO3
13080 M$="YOU ARE ON THE SOUTH BANK OF A FAST FLOWING RIVER.":GOSUB1:IFWP<>28THENRETURN
13082 M$="SOME ELOI ARE SWIMMING.":GOSUB1
13083 IFWP<>28ORW=1THENRETURN
13084 M$="YOU WILL SEE ONE ALMOST DROWNING!":GOSUB1
13085 I$="TRY TO SAVE HER!":RETURN
13090 M$=J$+"THE CITY. THERE ARE BEAUTIFUL, BUT DILAPIDATED HOUSES EVERYWHERE. THERE'S NO CLEAR STREET PLAN.":GOTO3
13100 M$="YOU ARE ON A SQUARE. IN THE MIDDLE IS SOMETHING THAT LOOKS LIKE A WELL.":GOSUB1
13102 L$="WELL":IFW=0THENRETURN
13103 M$="WEENA SUDDENLY CRAWLS CLOSE TO YOU. SHE LOOKS SCARED.":I$="LEAVE HER HERE.":GOTO3
13110 M$="YOU ARE ON A HIGH HILL. IN THE WEST YOU CAN SEE THE CITY, AND FAR IN THE EAST A LARGE, GREEN BUILDING.":GOTO3
13120 M$="YOU ARE ON A WINDING FOREST PATH, IN A WILD AND DARK FOREST.":GOSUB1:I$="FIND YOUR WAY THROUGH THE FOREST.":GOSUB260
13123 IFRV>.2THEN13130
13124 M$="IN THE DISTANCE YOU CAN SEE WHAT MORLOCHS ARE WALKING.":GOTO3
13130 IFRV<.85THENRETURN
13131 M$="SUDDENLY SOME MORLOCHS ATTACK YOU":GOSUB1
13132 AM=AM+1:IFAM>8THEN13135
13133 M$="ON. BUT YOU WERE ON YOUR GUARD, AND YOU CHASE THEM AWAY.":GOTO3
13135 IFW=0THEN13133
13136 M$="ON. BEFORE YOU KNOW IT, THEY KIDNAP WEENA, AND YOU'RE ALONE.":W=-1:GOTO3
13140 M$="YOU ARE STANDING AT THE ENTRANCE OF AN OLD AND DISUSED GREEN PORCELAIN MUSEUM.":GOTO3
13150 M$="YOU ARE IN A LARGE MUSEUM HALL. THERE IS A THICK LAYER OF DUST ON THE FLOOR.":GOTO3
13160 M$="YOU ARE IN A SMALL ROOM, WITH MANY BROKEN DISPLAY CASES.":L$="CASES":GOTO3
13170 M$=J$+"A HUGE HALL, WITH LARGE MACHINES. THE MACHINES LOOK OLD AND NEGLECTED.":L$="MACHINE":GOTO3
13180 M$=J$+"A SMALL MUSEUM HALL. YOU CAN GO UP, VIA A STAIRCASE BEHIND A DOOR.":I$="FIND THE KEY.":GOTO3
13190 M$="YOU ARE IN THE MUSEUM ATTIC. IT IS VERY DUSTY HERE. EXOTIC TAXIDERMY ANIMALS LOOK AT YOU WITH GLASSY EYES."
13193 L$="EXOTIC ANIMALS":GOTO3
13200 M$=J$+"A LARGE MUSEUM HALL, WITH LARGE GLASS DISPLAY CASES.":L$="CASES":GOTO3
13210 IFWP<>28THEN13214
13211 M$="YOU WADE INTO THE RIVER AND SAVE THE GIRL. SHE IS VERY GRATEFUL TO YOU.":W=1:GOTO3
13214 M$="YOU WADE INTO THE RIVER. THE CURRENT GRABS YOU, YOU LOSE YOUR BALANCE, AND YOU DROWN.":PT=0:GOTO3
13220 M$=J$+"A SMALL AND MESSY STUDY ROOM. EVERYWHERE THERE ARE OPEN BOOKS AND PILES OF PAPERS.":GOTO3
13230 GOSUB13120:IFDRTHENRETURN
13231 M$="YOU SEE A BIG, HUNGRY DRAGON. THE DRAGON IS SLOWLY APPROACHING YOU.":DR=1:I$="4":GOTO3
13300 M$="HOW LUCKY THAT YOU HAVE IT WITH YOU! YOU MOUNT THE HANDLE, AND PULL IT OVER...":P=-1:GOSUB1:GOTO15000
13400 M$="YOU SUDDENLY FALL INTO AN ICE-COLD LAKE.":GOSUB1
13405 IFG(16)=0THEN13480
13410 M$="YOU TRY TO CLIMB TO THE SIDE, BUT IT IS TOO STEEP.":GOSUB1
13430 M$="YOU FEEL YOUR POWERS SLOWLY EBBING AWAY.":GOSUB1
13440 M$="WITH A LAST EFFORT YOU TRY TO PULL YOURSELF UP, BUT IN VAIN.":GOSUB1
13441 M$="THE WATER CLOSES INEXORABLY ABOVE YOU...":PT=0:GOTO3
13480 M$="FORTUNATELY, YOU HAVE THE CAR TIRE WITH YOU, WHICH ALLOWS YOU TO FLOAT A BIT.":GOSUB1
13483 M$="AFTER SOME TRYING YOU MANAGE TO CLIMB TO THE SIDE.":GOSUB1:GOSUB15000
13484 P=-2:RETURN
13500 IFL=0THENPRINT"IT IS DARK.":RETURN
13510 IFP=31THEN13600
13520 IFP=33THEN13700
13525 P$=""
13530 M$=J$+"A DEEP WELL. FAR ABOVE YOU SEE A SMALL PIECE OF THE BLUE SKY.":GOSUB1:IFP$<>""THENM$=P$:GOSUB1
13560 RETURN
13600 P$="YOU CAN GO DOWN THROUGH A BROKEN HATCH."
13605 IFLK=1THENL$="BROKE HATCH":GOTO13530
13610 L$="LUIK":I$="DO YOU HAVE NOTHING HEAVY WITH YOU?"
13620 M$=J$+"A DEEP WELL. YOU ARE STANDING ON A HATCH THAT BENDS UNDER YOUR WEIGHT.":GOTO3
13700 M$="YOU ARE AT THE BOTTOM OF A DEEP WELL.":I$="LOOK A LITTLE HIGHER UP.":GOTO3
14000 M$="THE DRAGON IS COMING AT YOU. HE LOOKS FEROCIOUS AND HUNGRY.":GOSUB1
14002 M$="YOU ARE STILL TRYING TO RUN AWAY, BUT TO NO AVAIL. THE DRAGON EATS YOU.":GOSUB1:PRINT"YOU'RE DEAD.":GOTO4000
15000 PRINT:M$="<HIT ANY KEY>":GOSUB210:RETURN
16000 C=0:FORA=1TOLEN(A$)-2STEP3
16010 IFMID$(A$,A,3)=D$THENC=(A+2)/3
16020 NEXT A:RETURN
17000 M$=">>>";:INPUTV:RETURN
18000 IFE$=""THENC$="":RETURN
18010 A=1
18020 IFMID$(E$,A,1)=";"THEN18040
18030 A=A+1:IF A<=LEN(E$)THEN18020
18035 C$=E$:E$="":GOTO18060
18040 C$="":IFA<>1THENC$=LEFT$(E$,A-1)
18045 IF A=LEN(E$)THENE$="":GOTO18060
18050 E$=RIGHT$(E$,LEN(E$)-A)
18060 IFLEN(C$)<2THEN18070
18063 IFLEFT$(C$,1)<>" "THEN18070
18065 C$=RIGHT$(E$,LEN(E$)-1):GOTO18060
18070 IFCP=1THENM$=" "+C$:GOSUB1
18080 RETURN
19000 IFA<=2ORA=8THENM$=M$+"NORTH"
19010 IFA>3ANDA<7THENM$=M$+"SOUTH"
19020 IFA>1ANDA<5THENM$=M$+"EAST"
19030 IFA>5ANDA<9THENM$=M$+"WEST"
19040 IFA=9THENM$=M$+"UP"
19050 IFA=10THENM$=M$+"DOWN"
19060 RETURN
25000 DATA-6,2,-1,-2,1,29,3,-1,2,-5,4,-1,-2,3,-1,-1,-2,10,0,7,-1,6,-3,8,-1,7,-1,28,-3,10,-1,9,13,12,0,11,0,6
25020 DATA-1,10,0,14,15,-5,30,-2,18,-2,14,10,13,-1,-3,12,-2,10,-1,-4,15,0,11,12,-1,14,19,-4,11,-1,-4,42,18,20
25040 DATA-1,20,-3,18,0,13,-1,0,17,-2,19,0,12,-1,18,-3,15,0,14,-1,-3,16,17,-1,-4,22,0,42,-1,21,-3,23,-1,22,0
25060 DATA25,-1,-4,25,0,22,27,-1,24,-5,23,-1,-9,25,-4,24,-1,-4,9,-1,-7,2,-1,-8,11,31,-8,30,-1,-6,35,0,31,33,36
25080 DATA-7,32,-1,-1,-4,41,36,41,-1,35,41,0,38,-1,-3,39,0,36,35,-1,41,37,-4,36,-1,40,32,0,34,-1,0,34,-4,34
25100 DATA35,-1,35,34,0,36,-1,0,21,18,-2,18,-1
26000 DATAPAINTING,2,CRYSTAL STAFF,-1,BREAKFAST,1,STACK OF PAPERS,29,FRUITS,7,URN,27,BOOK,27,HEAVY CROWBAR,24
26040 DATABROKEN URN,-1,KEY,-1,LAMP,26,HANDLE OF TIME MACHINE,40,ALMOND CAKE,7,MATCHES,-1,DEAD DRAGON,-1,TIRE,15
