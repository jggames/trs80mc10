0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 GOSUB1:GOTO3000
10 PRINT:PRINT"PRESS ANY KEY TO CONTINUE";
20 IFINKEY$=""THEN20
30 FORCC=1TO25:PRINTCHR$(8);:NEXT:RETURN
40 DIMS(19),O(19),O$(19):PRINT"'THE SWORD OF RASCHKIL'":PRINT"BY MAC VAUGHN":PRINT
50 M$="THIS  Q U E S T  IS THE FIRST OF A SERIES. YOUR MISSION IS TO SEEK OUT THE FAMED SWORD OF RASCHKIL. ":GOSUB1
51 M$="TO DO SO YOU MUST FIND THE PROPER ITEMS AND USE THE CORRECT COMMANDS. YOU MUST NOT FAIL!":GOSUB1
52 GOSUB10:FORI=1TO19:S(I)=-1:NEXTI
55 S(3)=-2:S(6)=-2:FORI=8TO12:S(I)=-2:NEXTI:S(16)=-2:S(17)=-2
60 M$="YOU WILL FIND THAT YOU ARE IN A WHOLE NEW WORLD. YOU CAN PICK UP THINGS (AND PUT THEM DOWN).":GOSUB1
61 M$="USE ONE- OR TWO-WORD COMMANDS TO CONTROL ME (YOUR PUPPET) IN THIS QUEST":GOSUB1
62 M$="(I.E. CLIMB LADDER, SWIM) USE N, S, E OR W (OR D FOR DOWN) TO MOVE. GOOD LUCK!":GOSUB1
67 M$="NOTE: TO TAKE INVENTORY TYPE 'I', TO QUIT TYPE 'Q'":GOSUB1
70 GOSUB10
81 O(1)=32:O(2)=7:O(3)=4:O(4)=9:O(5)=31
90 O(6)=8:O(7)=13:O(8)=14:O(9)=15:O(10)=13:O(11)=2:O(12)=18
100 CLS:O(13)=19:O(14)=23:O(15)=0:O(16)=25:O(17)=30:O(18)=4
110 O(19)=15:O$(1)="SWORD":O$(2)="WATER":O$(3)="TREE"
120 O$(4)="JAR":O$(5)="KEY":O$(6)="CASTLE":O$(7)="STAFF"
130 O$(8)="PIT":O$(9)="WIZARD":O$(10)="LADDER":O$(11)="HOLE"
140 O$(12)="HOUSE":O$(13)="RAT":O$(14)="PIE":O$(15)="DIAMOND"
150 O$(16)="GHOST":O$(17)="DOOR":O$(18)="LEAF":O$(19)="SIGN"
160 RO=1:GOTO2000
190 GOTO6000
200 REM INPUT COMMANDS
210 INPUTX$:IFNH=1THENGG=1:NH=0:RO=10:M$="YOU RUN OUT OF AIR AND SURFACE.":GOSUB1
215 FORI=1TOLEN(X$):IFMID$(X$,I,1)=" "THEN300
220 NEXTI:IFX$="LOOK"THEN2000
221 IFLEN(X$)=1THEN1000
230 IFX$="SWIM"THEN280
231 IFX$="DIVE"THEN250
240 IFX$="JUMP"THEN270
241 IFX$="SURFACE"THEN290
245 C$=X$:GOTO5020
250 IFRO<>10THEN5000
260 NH=1:RO=31:GOSUB5010:GOTO2000
270 IFRO=130RRO=9THEN5030
271 GOTO5000
280 IFRO<>7THEN285
281 RO=10:GOTO2000
285 IFRO=10THENRO=7:GOTO2000
286 GOTO5000
290 IFRO=31THENRO=10:GOSUB5010:GOTO2000
291 GOTO5000
300 C$=LEFT$(X$,I-1):D$=RIGHT$(X$,(LEN(X$)-LEN(C$)-1))
310 RESTORE:FORI=1TO16:READX$:IFX$=C$THENV=I:GOTO330
311 NEXTI
320 GOTO5020
330 FORI=1TO19:IFD$=O$(I)THEN340
331 NEXTI:GOTO5080
340 ONVGOTO370,370,700,720,720,780,820,860,860,920,950,970
350 ONV-12GOTO3500,3540,3560,3600
370 ONIGOTO390,440,5090,490,510,5090,550,5090,5890,5090
380 ONI-10GOTO5090,5090,570,610,630,5090,5090,650,670
390 IFRO<>32THEN5000
391 IFS(18)=1THEN410
400 M$="IT LEVITATES OUT OF YOUR REACH.":GOSUB1:GOTO200
410 M$="AS YOU SEIZE THE HILT, THE SWORD BEGINS TO GLOW.":GOSUB1
420 M$="YOUR QUEST IS AT AN END. YOU NOW POSSESS THE SWORD OF RASCHKIL.":GOSUB1:GOTO6000
440 IFRO<>7ANDRO<>10ANDRO<>31THEN5000
450 IFS(4)=1THEN470
460 PRINT"FIRST I NEED A CONTAINER.":GOTO200
470 IFS(2)=1THEN5100
480 GOSUB5010:S(2)=1:GOTO200
490 IFRO<>9THEN5000
491 IFS(4)=1THEN5100
500 GOSUB5010:S(4)=1:O(4)=-1:GOTO200
510 IFGG=1THENGG=0:GOTO530
511 IFRO<>31THEN5000
520 IFS(5)=1THEN5100
530 GOSUB5010
540 RO=10:S(5)=1:O(5)=-1:GOTO2000
550 IFRO<>13THEN5000
551 IFS(7)=1THEN5100
560 S(7)=1:O(7)=-1:GOSUB5010:GOTO200
570 IFRO<>19THEN5000
580 PRINT"THE RAT BIT YOU.":KJ=RND(20):IFKJ<>1THEN200
590 M$="AS SOON AS HE BITES YOU A STRANGE PAIN ENVELOPS YOUR BODY. YOU SLOWLY COLLAPSE AND DIE.":GOSUB1:GOTO6000
610 IFRO<>23THEN5000
611 IFS(14)=1THEN5100
620 GOSUB5010:S(14)=1:O(14)=-1:GOTO200
630 IFS(15)=-2THEN5000
631 IFRO<>O(15)THEN5000
640 IFS(15)=1THEN5100
641 S(15)=1:O(15)=-1:GOSUB5010:GOTO200
650 IFRO<>4THEN5000
651 IFS(18)=1THEN5100
660 S(18)=1:O(18)=-1:GOSUB5010:GOTO200
670 IFRO<>15THEN5000
680 M$="THE WIZARD SAYS, 'LEAVE MY SHINGLE ALONE.'":GOSUB1
690 GOTO200
700 IFS(I)<>1THEN5110
710 GOSUB5010:S(I)=-1:O(I)=RO:GOTO2000
720 IFRO<>13ANDRO<>4THEN5000
730 IFD$="LADDER"ANDRO=13THEN750
740 IFD$="TREE"ANDRO=4THEN770
741 GOTO5000
750 IFC$="SCALE"THENPRINT"TRY 'CLIMB'":GOTO200
760 GOSUB5010:RO=2:GOTO2000
770 GOSUB5010:RO=9:GOTO2000
780 IFRO<>7ANDRO<>10ANDRO<>31THEN5000
790 IFD$<>"JAR"THEN5000
800 IFS(2)=1THENPRINT"IT'S ALREADY FULL.":GOTO200
810 S(2)=1:GOSUB5010:GOTO200
820 IFD$<>"WATER"THEN5000
821 S(2)=-1:IFRO=15THEN840
830 GOSUB5010:M$="BOY THAT REALLY HIT THE SPOT!":GOSUB1:GOTO200
840 GOSUB5010:M$="EVERYTHING BEGINS TO SPIN AROUND AND...":GOSUB1
850 RO=17:GOTO2000
860 IFD$<>"PIE"ORS(14)=-1THEN5000
870 IFRO=15THENM$="YOU HIT THE WIZARD WITH A PIE.":GOSUB1
880 IFRO=15THENM$="HE STRIKES YOU DEAD WITH HIS TOUCH.":GOSUB1
890 IFRO=15THEN6000
900 M$="YOU HIT THE GHOST WITH A PIE.":GOSUB1
910 M$="HE GETS MAD AND PUNCHES YOU, THE FORCE OF THE BLOW IS SO GREAT THAT IT KILLS YOU.":GOSUB1:GOTO6000
920 IFRO<>15ORD$<>"SIGN"THEN5000
930 M$="IT SAYS, 'I AM CLUTON. THROW A PIE AND YOU WILL DIE.'":GOSUB1
940 GOTO200
950 IFS(2)=-1ORD$<>"WATER"THEN5000
951 S(2)=-1
960 GOSUB5010:M$="YOU MADE A PUDDLE ON THE GROUND.":GOSUB1:GOTO200
970 IFD$<>"DOOR"ORS(5)<>1ORRO<>30THEN5000
971 GOSUB5010
980 PRINT"THE DOOR SWINGS OPEN.":S(17)=-3:GOTO2000
1000 REM MOVE, QUIT OR INVENTORY?
1010 IFX$="Q"THENPRINT"OH WELL... BYE...":GOTO6000
1020 IFX$="I"THEN3700
1021 IFX$="E"ORX$="W"ORX$="N"THEN1030
1025 IFX$<>"D"ANDX$<>"S"THEN5120
1027 IFX$="D"ANDRO<>9THEN5000
1030 ONROGOTO1060,1080,1100,1120,1130,1150,1170,1180,1200,1210
1040 ONRO-10GOTO1220,1240,1250,1260,1280,1300,1310,1330,1350
1050 ONRO-19GOTO1360,1380,1390,1410,1430,1450,1470,1490,1510
1055 ONRO-28GOTO1530,1550,1570,1570
1060 IFX$="S"THENRO=2:GOTO2000
1061 IFX$="E"THENRO=6:GOTO2000
1070 GOTO5120
1080 IFX$="W"THEN5120
1081 IFX$="E"THENRO=5:GOTO2000
1090 IFX$="N"THENRO=1:GOTO2000
1091 IFX$="S"THENRO=3:GOTO2000
1100 IFX$="N"THENRO=2:GOTO2000
1101 IFX$="E"THENRO=4:GOTO2000
1110 GOTO5120
1120 IFX$<>"W"THEN5120
1121 RO=3:GOTO2000
1130 IFX$="S"THEN5120
1131 IFX$="N"THENRO=6:GOTO2000
1140 IFX$="W"THENRO=2:GOTO2000
1141 RO=8:GOTO2000
1150 IFX$="N"THEN5120
1151 IFX$="S"THENRO=5:GOTO2000
1160 IFX$="E"THENRO=7:GOTO2000
1161 RO=1:GOTO2000
1170 IFX$<>"W"THEN5120
1171 RO=6:GOTO2000
1180 IFX$="W"THENRO=5:GOTO2000
1181 IFX$="E"THENRO=11:GOTO2000
1190 GOTO2000
1200 IFX$<>"D"THEN5120
1201 RO=4:GOTO2000
1210 GOTO5120
1220 IFX$="N"THENRO=12:GOTO2000
1221 IFX$="S"THENRO=16:GOTO2000
1230 IFX$="E"THENRO=15:GOTO2000
1231 RO=8:GOTO2000
1240 IFX$="E"THENRO=14:GOTO2000
1241 IFX$="S"THENRO=11:GOTO2000
1250 GOTO5120
1260 IFX$="S"THENRO=15:GOTO2000
1261 IFX$="W"THENRO=12:GOTO2000
1270 GOTO5120
1280 IFX$="N"THENRO=14:GOTO2000
1281 IFX$="W"THENRO=11:GOTO2000
1290 GOTO5120
1300 IFX$<>"N"THEN5120
1301 RO=11:GOTO2000
1310 IFX$="W"THEN5120
1311 IFX$="E"THENRO=19:GOTO2000
1320 IFX$="N"THENRO=18:GOTO2000
1321 RO=4:GOTO2000
1330 IFX$="S"THENRO=17:GOTO2000
1331 IFX$="E"THENRO=20:GOTO2000
1340 GOTO5120
1350 IFX$="W"THENRO=17:GOTO2000
1351 GOTO5120
1360 IFX$="S"THEN5120
1361 IFX$="N"THENRO=21:GOTO2000
1370 IFX$="E"THENRO=22:GOTO2000
1371 RO=18:GOTO2000
1380 IFX$="S"THENRO=20:GOTO2000
1381 GOTO5120
1390 IFX$="S"THEN5120
1391 IFX$="N"THENRO=23:GOTO2000
1400 IFX$="E"THENRO=24:GOTO2000
1401 RO=20:GOTO2000
1410 IFX$="S"THENRO=22:GOTO2000
1411 IFX$="E"THENRO=25:GOTO2000
1420 GOTO5120
1430 IFX$="N"THENRO=25:GOTO2000
1431 IFX$<>"W"THEN5120
1440 RO=22:GOTO2000
1450 IFX$="S"THENRO=24:GOTO2000
1451 IFX$="W"THENRO=23:GOTO2000
1455 IFX$="E"ANDS(16)=-3THENRO=26:GOTO2000
1457 IFX$="E"THENM$="THE GHOST WON'T LET YOU BY.":GOSUB1:GOTO2000
1460 GOTO5120
1470 IFX$="N"THENRO=27:GOTO2000
1471 IFX$="S"THENRO=24:GOTO2000
1480 GOTO5120
1490 IFX$="E"THEN5120
1491 IFX$="W"THENRO=28:GOTO2000
1500 IFX$="N"THENRO=29:GOTO2000
1501 RO=26:GOTO2000
1510 IFX$="N"THENRO=30:GOTO2000
1511 IFX$="E"THENRO=27:GOTO2000
1520 GOTO5120
1530 IFX$="S"THENRO=27:GOTO2000
1531 IFX$="W"THENRO=30:GOTO2000
1540 GOTO5120
1550 IFX$="S"THENRO=28:GOTO2000
1551 IFX$="E"THENRO=29:GOTO2000
1560 IFX$="W"ANDS(17)=2THENRO=32:GOTO1580
1570 GOTO5120
1580 M$="THE DOOR SLAMS BEHIND YOU AND...":GOSUB1:GOTO2000
2000 REM PRINT PRESENT LOCATION
2010 ONROGOTO2040,2060,2080,2090,2110,2120,2140,2160,2170,2180,2210
2020 ONRO-11GOTO2220,2240,2270,2290,2330,2340,2370,2380,2400,2410,2420
2030 ONRO-22GOTO2430,2440,2450,2460,2480,2500,2520,2540,2570,2580
2040 M$="YOU ARE AT THE EDGE OF A FOREST.":GOSUB1
2050 M$="PATHS LEAD SOUTH AND EAST.":GOTO8
2060 M$="YOU ARE IN A GROVE OF OAK TREES.":GOSUB1
2070 PRINT"A HOLE IS NEARBY.":GOTO3000
2080 PRINT"YOU ARE IN THE MEADOW.":GOTO3000
2090 M$="YOU ARE ON A SMALL PATH. A LARGE OAK IS NEARBY.":GOSUB1
2100 IFO(18)=4THENM$="A GOLDEN LEAF IS ON THE GROUND.":GOSUB1
2105 GOTO3000
2110 M$="YOU ARE IN A BLEAK, GRASSY FIELD.":GOTO8
2120 M$="YOU ARE IN A LOVELY FOREST WITH THE CALLS OF MANY BIRDS SOUNDING ALL AROUND YOU.":GOTO8
2140 M$="YOU ARE IN AN ANCIENT ORCHARD.":GOSUB1
2150 M$="A POOL OF MURKY WATER IS BEFORE YOU.":GOTO8
2160 M$="YOU ARE AT THE ENTRANCE TO A SMALL CASTLE.":GOTO8
2170 M$="YOU ARE AT THE TOP OF AN OAK.":GOTO8
2180 M$="YOU ARE SWIMMING IN A POOL OF WATER.":GOSUB1
2190 IFO(5)=31THENM$="AT THE BOTTOM OF THE POOL IS A KEY.":GOSUB1
2200 GOTO3000
2210 M$="YOU ARE IN THE CASTLE ENTRY.":GOTO8
2220 M$="YOU ARE IN THE NORTH WING. A DRAFT ISSUES FROM THE EAST EXIT.":GOTO8
2240 M$="YOU ARE ON A LEDGE JUST INSIDE A BOTTOMLESS HOLE.":GOSUB1
2250 M$="A LADDER LEADS UP. I ADVISE YOU NOT TO GO DOWN.":GOTO8
2270 IFS(7)=-1THENM$="YIIIIEEEE... YOU FELL IN A PIT!":GOSUB1:GOTO190
2280 M$="YOU ARE LEVITATING ABOVE A PIT.":GOTO8
2290 M$="YOU ARE IN THE WIZARD'S ROOM. THE WIZARD IS IN THIS ROOM. A SIGN IS NEARBY.":GOTO8
2330 M$="YOU ARE IN THE SOUTH WING.":GOTO8
2340 M$="YOU ARE ON THE HEATH OF ORIONE'S MANOR.":GOSUB1
2350 M$="TO THE NORTH IS A HOUSE, TO THE EAST, A DUMP.":GOTO8
2370 M$="YOU ARE AT THE ENTRANCE TO A HOUSE.":GOTO8
2380 M$="YOU ARE IN THE DUMP. A LARGE RAT IS WATCHING YOU.":GOTO8
2400 M$="YOU ARE IN THE ENTRY HALL.":GOTO8
2410 M$="YOU ARE IN AN ANCIENT BEDCHAMBER.":GOTO8
2420 M$="YOU ARE IN THE BACK ROOM.":GOTO8
2430 M$="YOU ARE IN THE KITCHEN.":GOTO8
2440 M$="YOU ARE IN THE BACK PORCH.":GOTO8
2450 M$="YOU ARE IN THE PANTRY. A GHOST IS HERE.":GOTO8
2460 M$="YOU ARE IN THE GARDEN. A BARN IS TO THE NORTH.":GOTO8
2480 M$="YOU ARE IN THE BARN. TO THE NORTH ARE STABLES, AND A PASSAGE LEADING WEST.":GOTO8
2500 M$="YOU ARE IN THE DIM FEED STORAGE ROOM. TO THE NORTH IS ANOTHER DIMLY LIT ROOM.":GOTO8
2520 M$="YOU ARE IN THE STABLE. A PASSAGE LEADS WEST.":GOTO8
2540 M$="YOU ARE IN THE LEATHERSHOP. A DOOR IS TO THE WEST.":GOSUB1
2550 IFS(17)=2THENPRINT"IT IS UNLOCKED.":GOTO3000
2560 M$="IT IS LOCKED.":GOTO8
2570 M$="YOU ARE AT THE BOTTOM OF A POOL.":GOTO8
2580 M$="YOU ARE IN A MISTY ROOM. A SWORD IS LEVITATING IN MIDAIR.":GOTO8
3000 REM PRINT VISIBLE ITEMS
3010 PRINT"VISIBLE ITEMS:":FORI=1TO19:IFO(I)=ROTHENI1=1:PRINTO$(I)
3020 NEXTI:IFI1=1THENI1=0:GOTO3030
3021 PRINT"NOTHING"
3030 GOTO200
3500 IFD$<>"DOOR"THEN5120
3510 IFRO<>30THEN5120
3520 IFS(5)=-1THENPRINT"YOU NEED A KEY.":GOTO200
3530 S(17)=2:GOSUB5010:GOTO200
3540 IFD$<>"DIAMOND"THEN5120
3541 IFRO<>25THEN5120
3550 M$="THE GHOST ACCEPTS THE DIAMOND.":GOSUB1:S(16)=-3:GOTO200
3560 IFD$="RAT"THEN570
3561 IFD$<>"PIE"THEN5120
3570 M$="YOU ENJOY A DELICIOUS SARA LEE PIE.":GOSUB1
3580 M$="HIDDEN INSIDE IT IS A DIAMOND.":GOSUB1:O(15)=RO:S(15)=-1
3590 O(14)=-1:S(14)=-1:GOTO200
3600 IFD$="POOL"THENPRINT"TRY 'SWIM'.":GOTO200
3610 IFD$<>"HOLE"ORRO<>2THEN5120
3620 RO=13:GOTO2000
3700 M$="YOU ARE CARRYING THE FOLLOWING:":GOSUB1:FORI=1TO19
3710 IFS(I)=1THENI1=1:PRINTO$(I)
3720 NEXTI:IFI1=0THENPRINT"NOTHING":GOTO3730
3721 I1=0
3730 GOTO200
5000 M$="I CAN'T DO THAT AT THIS TIME.":GOSUB1:GOTO200
5010 PRINT"O.K.":RETURN
5020 M$="I DON'T KNOW HOW TO "+C$+" SOMETHING!":GOSUB1:GOTO200
5030 M$="YOU FELL AND BROKE EVERY BONE IN YOUR BODY!":GOSUB1
5040 PRINT"YOU'RE DEAD!!!":PRINT:GOTO6000
5050 DATA"GET","TAKE","DROP","CLIMB","SCALE","FILL","DRINK"
5060 DATA"THROW","HURL","READ","POUR","LOCK","UNLOCK","GIVE"
5070 DATA"EAT","ENTER"
5080 M$="I DON'T KNOW WHAT A "+D$+" IS!":GOSUB1:GOTO200
5090 PRINT"DON'T BE RIDICULOUS!!":GOTO200
5100 PRINT"I'VE GOT IT ALREADY!!":GOTO200
5110 PRINT"I DON'T HAVE IT!!":GOTO200
5120 PRINT"WHAT??":GOTO200
6000 INPUT"PLAY AGAIN";X$:X$=LEFT$(X$,1):IFX$<>"N"THENRUN
6001 END
6010 REM VERSION 1.32 8/20/81
6020 REM BY MAC VAUGHN
