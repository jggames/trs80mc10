0 CLEAR450:DIMC1,C2,M$,MC:M=16384:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-M+C2,:RETURN
10 REM INITIALIZE
20 REM
25 GOSUB3100
30 DIMN(15),E(15),S(15),O(15),V$(13),O$(18),L(15),OB(18),G$(18)
40 G$(1)="A ":G$(2)="A ":G$(4)="OF THE ":G$(5)="OF THEM ":TT=23
50 Z$="YOU ARE ":LU=20
60 DF$="THAT WOULD BE DIFFICULT":CF$="DONE.":RA$="NOTHING IN PARTICULAR TO NOTE"
70 REM MAP
80 FORL=1TO15
90 READN(L),E(L),S(L),O(L)
100 NEXTL:L=1
110 DATA2,0,0,0,5,3,1,4
120 DATA0,0,0,2,0,2,0,0
130 DATA6,0,2,0,8,7,5,0
140 DATA0,0,0,6,11,0,6,9
150 DATA10,8,0,0,12,11,9,0
160 DATA13,15,8,10,0,13,10,0
170 DATA0,14,11,12,0,0,15,13
180 DATA14,0,0,11
190 REM OBJECTS
200 FORI=1TO18
210 READOB(I):NEXTI
220 DATA3,5,4,0,1,0,0,8,0,0,0,0,15,0,0,0,0,0
230 REM VERBS
240 FORI=1TO13
250 READV$(I):NEXT I
260 DATA"DROP","GET","OPEN","CLOSE","EXAMINE","SEARCH","TAKE","READ","TURN","REMOVE","LOOK","MOVE","LIGHT"
270 REM "P",   "P",   "O",   "F",    "F",     "F",     "R",     "L",   "T",     "E",     "R",   "S",   "G"
280 FORI=1TO18
290 READG$(I):NEXTI
300 DATA"A","SOME","A","A","AN","A","A","A","A","A","A","A","A","A","SOME","A","A","SOME"
310 REM NOUNS
320 FORI=1TO18
330 READO$(I):NEXTI
340 DATA"SKELETON","FOAM","CALENDAR","HATCH","ENTRY","BOX","KEY"
341 DATA"CUPBOARD","BOOK","PAPER","DIAL","SAFE","PORTRAIT","GATE","BARS"
342 DATA"UNIFORM","CANDLE","MATCHES"
350 GOTO570
360 REM MAIN LOOP
370 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1:IFC1<479THEN380
371 PRINT"PRESS ANY KEY TO CONTINUE"
372 M$=INKEY$:IFM$=""THEN372
373 CLS:GOTO570
380 IFAL<>0THENAL=AL-1
385 INPUT"> ACTION";A$
390 IFA$=""THEN370
391 IFA$="LOOK"THEN570
392 IFA$="HELP"THENGOSUB3000:GOTO370
395 IFA$="QUIT"THENPRINT:PRINT"CHICKEN!":END
400 IFLEN(A$)>1THEN960
410 IFA$="N"ANDN(L)>0THENL=N(L):GOTO570
420 IFA$="E"ANDE(L)>0THENL=E(L):GOTO570
430 IFA$="S"ANDS(L)>0THENL=S(L):GOTO570
440 IFA$="W"ANDO(L)>0THENL=O(L):GOTO570
450 IFA$="I"THENGOSUB470:GOTO370
455 IFA$="H"THENGOSUB3000:GOTO370
460 PRINT"IMPOSSIBLE...":GOTO370
470 REM INVENTORY
480 TS=0
490 PRINT"YOUR ARE CARRYING:"
491 PRINT"------------------"
500 FORI=1TO18
510 IFOB(I)=50THENPRINT"- "O$(I);:TS=TS+1:IFI=18THENPRINT" (";MID$(STR$(LU),2)")";
511 IFOB(I)=50THENPRINT
520 NEXTI
540 IFTS=0THENPRINT"NOTHING!"
550 RETURN
560 REM -------------------------
570 GOSUB640:IFOV=1ANDL=1THEN3430
580 IFLU=0THENM$="YOU DON'T HAVE ANY MATCHES!":OB(18)=0:GOSUB1
585 IFTR=1THEN600
590 IFLU=0OR(L>4ANDAL=0)THENPRINT"IT'S PITCH BLACK!":GOTO370
600 GOSUB920
610 GOTO370
620 REM DESCRIPTIONS
630 REM
640 IFTR=1THEN645
641 IFL>4ANDAL=0THENRETURN
645 CLS
650 ONLGOSUB670,690,710,730,750,770,790,830,860,860,880,860,860,860,900
655 PRINT:REM "ROOM"L
660 RETURN
670 REM #1
680 M$=Z$+"OUTSIDE, AT THE BASE OF A CLIFF AND TOWARDS THE NORTH... ":GOSUB1:RETURN
690 REM #2
700 M$=Z$+"AT A N-S, E-W CROSSROADS":GOSUB1:RETURN
710 REM #3
720 M$=Z$+"IN A DARK ROOM":GOSUB1:RETURN
730 REM #4
740 M$=Z$+"IN A DUSTY ROOM":GOSUB1:RETURN
750 REM #5
760 M$=Z$+"IN A DAMP N-S CORRIDOR":GOSUB1:RETURN
770 REM #6
780 M$=Z$+"IN A PASSAGE (N.E.S)":GOSUB1:RETURN
790 REM #7
800 IFIM<5THENM$="A LONG SLIPPERY (E-W) CORRIDOR":GOSUB1:IM=IM+1:GOTO820
810 PRINT"IT'S A DEAD END":GOSUB1:IM=0:GOTO370
820 INPUT "> ACTION";A$:GOTO800
830 REM #8
840 M$=Z$+"IN A NEARLY EMPTY ROOM":GOSUB1
850 RETURN
860 REM #9,10,12,13,14
870 M$=Z$+"IN A NARROW CORRIDOR":GOSUB1:RETURN
880 REM #11
890 M$=Z$+"IN A ROOM (NSEW EXITS)":GOSUB1:RETURN
900 REM #15
910 M$=Z$+"IN A ROOM (NW EXITS)":GOSUB1:RETURN
920 REM OBJECTS IN ROOM
930 FORI=1TO18
940 IFOB(I)=LTHENM$="YOU SEE "+G$(I)+" "+O$(I):GOSUB1
950 NEXTI
951 PRINT"--------------------------------";
955 RETURN
960 REM PARSER
970 V$="":N$="":T=1
980 GOSUB1070
990 GOSUB1160
1000 IFX=0THEN370
1010 GOSUB1230
1020 IFY=0THEN370
1030 GOSUB1300
1040 GOTO370
1050 REM PARSING
1070 FORI=1TOLEN(A$)
1080 B$=MID$(A$,I,1)
1090 IFB$=" "THENT=T+1:GOTO1130
1100 ONTGOTO1110,1120:GOTO1130
1110 V$=V$+B$:GOTO1130
1120 N$=N$+B$
1130 NEXTI
1140 IFN$=""THENPRINT"I DON'T UNDERSTAND?"
1150 RETURN
1160 REM VERIFY VERB
1170 X=0
1180 FORI=1TO13
1190 IFV$=V$(I)THENX=I
1200 NEXTI
1210 IFX=0THENM$="I DON'T KNOW HOW TO '"+V$+"'":GOSUB1
1220 RETURN
1230 REM VERIFY NOUN
1240 Y=0
1250 FORI=1TO18
1260 IFLEFT$(N$,5)=LEFT$(O$(I),5)AND(OB(I)=LOROB(I)=50)THENY=I
1270 NEXTI
1280 IFY=0THEN1295
1290 RETURN
1295 IFRIGHT$(N$,1)="S"THENM$="THERE ARE NO '"+N$+"' HERE.":GOSUB1:GOTO1290
1296 M$="THERE IS NO '"+N$+"' HERE.":GOSUB1:GOTO1290
1300 REM RESPONSES/ACTIONS
1310 ONXGOSUB1330,1410,1530,1650,1710,1770,1820,1840,1910,1960,2030,2210,2300
1320 RETURN
1330 REM DROP
1340 IFY<>6THEN1380
1350 PRINT"WHERE";:INPUTR$
1360 IFR$="SAFE"ORR$="IN THE SAFE"ORR$="IN SAFE"THENPRINT"OK":TF=TF-10
1370 IFTF<=3THENOB(14)=0:S(2)=1
1380 IFOB(Y)<>50THENM$="YOU DO NOT HAVE THAT ITEM!":GOSUB1:GOTO1400
1390 IFOB(Y)=50THENM$="YOU DROP THE "+O$(Y)+".":GOSUB1:OB(Y)=L:IFY=15THENTF=TF-VAL(C$):BA=0
1400 RETURN
1410 REM GET
1420 IFTS>5THENM$="YOU CAN'T CARRY ANY MORE ITEMS":GOSUB1:RETURN
1430 IFY=1ORY=2ORY=4ORY=5ORY=8ORY=11ORY=12THENM$=DF$:GOSUB1:GOTO1520
1440 IFY<>15THEN1490
1450 PRINT"HOW MANY?":INPUTC$
1460 IFVAL(C$)>20THENPRINT"THAT'S TOO MUCH":GOTO1450
1470 TF=TF+VAL(C$):BA=VAL(C$)
1480 IFVAL(C$)>3THENOB(14)=2:S(2)=0
1490 IFOB(Y)=LTHENPRINT"DONE!":OB(Y)=50:GOTO1520
1500 IFOB(Y)=50THENPRINT"YOU ALREADY HAVE IT!":GOTO1520
1510 M$="I DO NOT SEE A '"+N$+"' HERE!"
1520 RETURN
1530 REM OPEN
1540 IFY=1ORY=2ORY=5ORY=7ORY=11THENM$=DF$:GOSUB1:GOTO1520
1550 IFY=3THENM$="YOU SEE CARDBOARD!":GOSUB1:GOTO1640
1560 IFY=4ANDOB(6)=0THENM$="CLICK! THERE IS A LEAD BOX INSIDE!!":GOSUB1:TF=TF+1:OB(6)=5:GOTO1640
1570 IFY=6ANDOB(7)=0THENM$="THERE IS A KEY INSIDE!!":GOSUB1:OB(7)=5:GOTO1640
1580 IFY=8ANDOB(9)=0THENM$="CLICK! THERE IS A BOOK INSIDE!!":GOSUB1:TF=TF+1:OB(9)=8:GOTO1640
1590 IFY=14THENM$="CLICK! A HATCH OPENS UNDER YOUR FEET... HELLO TO THE GREAT BEYOND!":GOSUB1:END
1600 IFY=9THENPRINT"OK, TO WHICH PAGE?!":GOTO1640
1610 IFY=10THENPRINT"IT IS OPEN":GOTO1640
1620 IFOV=0ANDY=12THENM$="GOOD IDEA, HOW DO YOU DO THAT?":GOSUB1:GOTO1640
1630 IFOV=1ANDY=12THENM$="IT IS OPEN... IT IS FILLED WITH GOLD BARS!?":GOSUB1
1640 RETURN
1650 REM CLOSE
1660 IFY=1ORY=2ORY=3ORY=5ORY=7ORY=11THENM$=DF$:GOSUB1:GOTO1700
1670 IFY=6ORY=9ORY=10THENM$=CF$:GOSUB1:GOTO1700
1680 IFY=12THENOV=0
1690 IFY=4ORY=8ORY=12THENM$=CF$:GOSUB1:TF=TF-1
1700 RETURN
1710 REM EXAMINE
1720 IFY=3THENPRINT"THE DATES ARE FLIPPING...":GOTO1760
1730 IFY=9ANDFL=0THENPRINT"A PAPER FALLS OUT":OB(10)=L:FL=1:GOTO1760
1740 IFY=9ANDFL=1THENPRINT"IT IS EMPTY":GOTO1760
1750 M$=DF$:GOSUB1
1760 RETURN
1770 REM SEARCH
1780 IFY=12THENOB(15)=L:GOTO2190
1790 IFFM=0THENIFY=16OROB(16)=50THENM$="IN THE POCKET YOU FIND SOME MATCHES":GOSUB1:OB(18)=L:FM=1:GOTO1810
1800 M$=RA$:GOSUB1
1810 RETURN
1820 REM TAKE
1830 GOTO1410
1840 REM READ
1850 IFY=1ORY=2ORY=4ORY=5ORY=6ORY=7ORY=8ORY=12THENM$=DF$:GOSUB1:GOTO1900
1860 IFY=3THENM$="YOU READ... JULY 25, 1944":GOSUB1:GOTO1900
1870 IFY=9THEN1905
1880 IFY=10THENPRINT"YOU READ... U40.W25.U12":GOTO1900
1890 IFY=11THENM$="IT IS AT POSITION"+STR$(TT):GOSUB1
1900 RETURN
1905 M$="YOU READ... TO MAKE A SAFE TOTALLY THEFT PROOF...":GOSUB1
1906 FORI=1TO512:C1=RND(32)
1907 PRINTMID$("ABCDEFGHIJKLMNOPQRSTUVWXYZ    ,.",C1,1);:IFC1>30THENI=I+1:C1=30:GOTO1907
1908 NEXTI:PRINT:GOTO1900
1910 REM TURN
1920 IFY=1ORY=2ORY=4ORY=5ORY=8ORY=12THENPRINTM$=DF$:GOSUB1:GOTO1950
1930 IFY=3ORY=6ORY=7ORY=9ORY=10THENM$=RA$:GOSUB1:GOTO1950
1940 IFY=11THEN2330
1950 RETURN
1960 REM MOVE
1970 IFY=1ORY=4ORY=5ORY=8ORY=11ORY=12THENM$=DF$:GOSUB1:GOTO2020
1980 IFY=6ORY=7ORY=9ORY=10THENM$=CF$:GOSUB1:M$=RA$:GOSUB1:GOTO2020
1990 IFY=2ANDTA=0ANDL=5THENPRINT"A HATCH APPEARS...":TA=1:OB(4)=5:GOTO2020
2000 IFY=2ANDTA=1ANDL=5THENPRINT"THERE IS NOTHING ELSE":GOTO2020
2010 IFY=13THEN2280
2020 RETURN
2030 REM LOOK
2040 IFY=1AND(OB(16)=0OROB(16)=3)THENPRINT"HE HAS A DARK GREEN UNIFORM":OB(16)=3:GOTO2200
2050 IFY=2THENPRINT"IT IS VERY GREEN":GOTO2200
2060 IFY=3THEN1840
2070 IFY=4THENM$="IT'S AN ORDINARY LOOKING HATCH":GOSUB1:GOTO2200
2080 IFY=5THENM$="IT SEEMS TO DATE FROM 40 YEARS AGO":GOSUB1:GOTO2200
2090 IFY=6THENM$="IT IS NOT LOCKED":GOSUB1:GOTO2200
2100 IFY=7THENM$=RA$:GOSUB1:GOTO2200
2110 IFY=8THENPRINT"IT HAS TWO DOORS":GOTO2200
2120 IFY=9THENM$="HYDRAULIC SAFE ALARMS BY LEON SHMURZ":GOSUB1:GOTO2200
2130 IFY=10THEN1880
2140 IFY=11THEN1840
2150 IFY=12ANDOV=0THENPRINT"IT HAS A DIAL":GOTO2200
2160 IFY=13THENGOSUB3500:GOTO2200
2170 IFY=14THENM$="IT IS HUGE. IT HAS SOME WRITING 'TRAGFAHIGKEIT>3=KAPUT'":GOSUB1:GOTO2200
2175 IFY=15ANDOV=1THENPRINT"THEY'RE SOLID GOLD":GOTO2200
2180 IFY=16THENM$="IT IS FULL OF MOTHS":GOSUB1:GOTO2200
2190 IFY=12ANDOV=1THENM$="IT IS FILLED WITH GOLD BARS":GOSUB1:GOTO2200
2200 RETURN
2210 REM MOVE
2220 IFY=5ORY=8ORY=11ORY=12THENM$=DF$:GOSUB1:GOTO2290
2230 IFY=3ORY=6ORY=9THENM$=RA$:GOSUB1:GOTO2290
2240 IFY=1THENPRINT"HE FALLS TO PIECES":GOTO2290
2250 IFY=2THEN1960
2260 IFY=4THEN1530
2270 IFY=7ORY=10THENPRINT"GOOD EFFORT!":GOTO2290
2280 IFY=13THENM$="THERE IS A SAFE BEHIND IT":GOSUB1:OB(11)=15:OB(12)=15:GOTO2290
2290 RETURN
2300 REM LIGHT
2310 IFOB(18)=50ANDLU>0THENLU=LU-1:AL=5:M$="SCRRITCH... THE MATCH LIGHTS":GOSUB1:RETURN
2320 M$="YOU DO NOT HAVE ANY":GOSUB1:RETURN
2330 REM OPEN SAFE
2340 FORI=1TO5
2350 INPUT"IN WHICH DIRECTION (R OR L)";S$
2360 IFS$="L"THENSG=-1:GOTO2380
2370 SG=1
2380 INPUT"BY HOW MANY NOTCHES";CR
2390 TT=TT+(CR*SG):PRINT"DIAL IS AT POSITION"TT
2400 IFTT=0THENI=5
2410 IFTT=27ANDI=3THEN2430
2420 NEXTI:IFTT=0THEN2450
2430 IFTT=27THENM$="CLICK! THE SAFE OPENS":GOSUB1:OV=1:OB(15)=L:TF=TF+1:GOTO370
2440 PRINT"IT DOES NOT OPEN"
2450 INPUT"RESTART (Y/N)";R$
2460 IFR$="N"THEN1950
2470 GOTO2340
3000 REM HELP
3005 PRINT:PRINT"VOCABULARY:"
3006 PRINT"-----------"
3008 K=0
3010 FORI=1TO13
3015 IFK=1THENPRINTTAB(15);V$(I):GOTO3025
3020 PRINTV$(I);
3025 K=1-K
3030 NEXTI
3040 PRINT
3059 REM HELP
3060 PRINT "ENTER A PHRASE:"
3070 PRINT "     <verb> <noun>"
3075 PRINT "DIRECTIONS: N S E O"
3080 PRINT "INVENTORY: I  HELP: H"
3090 RETURN
3100 REM INTRO
3105 CLS
3110 TI$="MISSION ADVENTURE"
3120 LT=LEN(TI$)
3130 XM=15-INT(LT/2)
3140 Y=7
3150 FORI=1TOLT
3160 C$=MID$(TI$,I,1)
3170 IFI/2=INT(I/2)THEN3240
3180 FORJ=1TOY
3190 PRINT@XM+I+32*J,C$;
3200 GOSUB3400
3210 IFJ<YTHENPRINT@XM+I+32*J," ";
3220 NEXTJ
3230 GOTO3300
3240 FORJ=15TOYSTEP-1
3250 PRINT@XM+I+32*J,C$;
3260 GOSUB3400
3270 IFJ>YTHENPRINT@XM+I+32*J," ";
3280 NEXTJ
3300 NEXTI
3310 PRINT@XM+1+32*Y,;:M$=TI$:GOSUB7
3320 PRINT:PRINT
3325 PRINTTAB(8)"J&S N29 OCT 1984"
3326 PRINTTAB(5)"** MICHEL BRASSINNE **"
3328 PRINT
3330 M$="   YOU ARE WALKING IN THE MOUNTAINS, HANDS IN YOUR POCKETS.":GOSUB1
3350 M$="   THE SUMMER HAS BEEN DRY AND SOME OF THE TREES HAVE DIED.":GOSUB1
3360 M$="   ONE OF THEM EXPOSES THE ENTRANCE TO A CAVE.":GOSUB1
3375 M$="   YOU DECIDE TO VENTURE FORTH...":GOSUB1:PRINT
3380 PRINT"     PRESS ANY KEY TO BEGIN";
3385 M$=INKEY$:IFM$=""THEN3385
3390 CLS:RETURN
3400 REM PAUSE
3410 FORK=1TO20:NEXTK
3420 RETURN

3430 PRINT:PRINT"YOU ESCAPED WITH"BA"GOLD BARS"
3440 END


3500 CLS
3501 PRINT"";
3502 PRINT"";
3503 PRINT"";
3504 PRINT"";
3505 PRINT"";
3506 PRINT"";
3507 PRINT"";
3508 PRINT"";
3509 PRINT"";
3510 PRINT"";
3511 PRINT"";
3512 PRINT"";
3513 PRINT"";
3514 PRINT"";
3515 PRINT"";
3580 PRINT"      SOME MILITARY DUDE.";
3585 M$=INKEY$:IFM$=""THEN3585
3590 CLS:RETURN

4001 REM MISSION AVENTURE
4002 REM MICHEL BRASSINNE
4003 REM JEUX ET STRATEGIE 29
