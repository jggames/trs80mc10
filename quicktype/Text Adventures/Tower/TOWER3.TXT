0 CLEAR500:CLS:DIMZZ,CC,M$,I$:GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 NR=14:NT=11:NP=11:NV=29:NO=NT+NP
20 DIM AC(NR,6),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV),TL(NT),TF(NT)
99 REM *INITIALIZATION* 
100 RM=1:CT=0:CF=0:RT=-1:CA=0:MF=0:CD=0:WD=-1:LI=0:BT=-1
120 FOR I=1 TO NR:READ RM$(I):NEXT I
130 FOR I=1 TO NV:READ VB$(I),VN(I):NEXT I 
140 FOR I=1 TO NO:READ OB$(I):NEXT I 
150 FOR I=1 TO NT:READ TD$(I),TL(I),TF(I):NEXT I 
160 FOR I=1 TO NR:READ AC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT I 
199 REM *MAIN LOOP: ROOM DESCRIPTION* 
200 IF WD=0 THEN 400 
210 WD=0:M$=RM$(RM):GOSUB1
220 K=0
230 I$="":FOR I=1 TO NT
240 IF TL(I)<>RM THEN 270 
250 IF K=0 THEN I$=I$+"YOU SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXT I:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
300 I$="EXITS ARE: "
310 FOR I=1 TO 6 
320 IF AC(RM,I)<>0 THEN I$=I$+VB$(I)+", " 
330 NEXT I
340 IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
399 REM *MAIN LOOP: COMMAND INPUT AND PARSER* 
400 INPUT C$
410 L=LEN(C$):IF L=0 THEN 400
420 C1$="":C2$="":C2=0:X=0 
430 FOR I=1 TO L
440 A$=MID$(C$,I,1):A=ASC(A$):IF A>=97 AND A<=122 THEN A$=CHR$(A-32)
450 IF A$<>" " THEN 460
455 IF C2$<>"" THEN 500 
457 X=1:GOTO 490
460 IF X=0 THEN C1$=C1$+A$:GOTO 490
470 C2$=C2$+A$ 
490 NEXT I 
500 IF C1$="" THEN PRINT "EXCUSE ME?":GOTO 400 
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FOR C1=1 TO NV 
530 IF VB$(C1)=C1$ THEN C1=VN(C1):GOTO 600 
540 NEXT C1
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO 400
600 IF C2$="" THEN 700 
610 FOR C2=1 TO NO
620 IF OB$(C2)=C2$ THEN 700
630 NEXT C2
635 IF C1=11 THEN C2=1:GOTO 700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO 400
699 REM * PRELIMINARY FILTERING OF COMMANDS*
700 IF C2>NT+5 AND C1<>7 AND C1<>11 THEN 640
710 IF C2<NT+5 AND C2>NT THEN IF C1<>11 THEN 8010
720 IF C2>NT THEN 750
725 TL=TL(C2)
730 IF C1<>11 AND TL<>RM AND TL<>0 THEN PRINT "IT ISN'T HERE.":GOTO 400
750 ON C1 GOTO 1000,1000,1000,1000,1000,1000,1100,1200,1300,1400
760 ON C1-10 GOTO 1500,1800,1900,2000,2100,2200,2300,2400,2500,9999
770 ON C1-20 GOTO 2600,2700,2800 
999 REM *DIRECTIONS*
1000 IF AC(RM,C1)=0 THEN PRINT "YOU CAN'T GO THAT WAY.":GOTO 200
1010 IF RM=4 AND RT<>0 AND C1=3 THEN M$="THE RATS LOOK TOO FIERCE.":GOSUB1:GOTO 8200
1020 RM=AC(RM,C1):WD=-1:GOTO 8200
1099 REM *GO*
1100 IF C2=0 THEN 8050
1105 IF C2<=16 THEN 8040
1110 C1=C2-16:GOTO 1000
1199 REM *EAT*
1200 IF C2<>6 THEN 8010
1210 TL(C2)=-1:PRINT"IT TASTES STALE.":GOTO 8200
1299 REM *KICK* 
1300 IF C2=0 THEN 8050
1305 IF C2<>7 OR CD<>0 THEN 8020
1310 IF CA<>0 THEN CT=9:GOTO 8200
1320 PRINT "THE COMPUTER STARTS UP!"
1330 M$="THE CONSOLE DISPLAYS: 'PLEASE LOG IN.'":GOSUB1:CA=-1:GOTO 8200
1399 REM * INSERT*
1400 IF C2=0 THEN 8050
1410 IF C2<>5 THEN 8010
1420 IF RM<>6 THEN 8030
1430 TL(5)=-1:TL(6)=RM:PRINT "A CANDY BAR COMES OUT.":GOTO 8200
1499 REM *TYPE* 
1500 IF C2=0 THEN 8050
1502 IF RM<>10 THEN 8030
1510 IF CA=0 THEN PRINT "THE COMPUTER ISN'T RUNNING.":GOTO 8200
1520 IF LI<>0 THEN 1600
1530 IF C2<>12 THEN PRINT "'INVALID LOGIN ID.'":GOTO 8200
1540 LI=-1:M$="'"+C2$+" LOGGED IN.'":GOSUB1:GOTO 8200 
1600 IF CP<>0 THEN 1700
1605 IF C2<=12 THEN PRINT "'INVALID COMMAND .'":GOTO8200
1610 IF C2=13 THEN PRINT "'COPY LOGOU ADVEN'":GOTO8200
1620 IF C2=14 THEN M$="'WELCOME TO ADVENTURE! W#ULD Y#$*'":GOSUB1:CT=9:GOTO 8200
1630 IF C2=15 THEN M$="'MOUNT TAPE THEN TYPE FILE NAME.'":GOSUB1:CP=-1:GOTO 8200 
1640 PRINT "'LOGGED OUT.'":LI=0:GOTO 8200
1700 CP=0:IF C2<=12 THEN PRINT "'NO SUCH FILE.'":GOTO 8200
1710 IF MT=0 THEN PRINT "'ERROR: TAPE NOT MOUNTED'":GOTO 8200
1720 PRINT "THE TAPE SPINS ..." :FOR I=1 TO 4000:NEXT I
1730 PRINT "'FILE COPIED.'"
1740 IF C2=14 THEN M$="CONGRATULATIONS, YOU'VE DONE IT!":GOSUB1:END
1750 GOTO 8200
1799 REM *TAKE* 
1800 IF C2=0 THEN 8050
1805 IF TF(C2)=0 THEN PRINT "THAT'S BEYOND YOUR ABILITY.":GOTO 8200
1810 IF TL=0 THEN PRINT "YOU ALREADY HAVE IT!":GOTO 200
1820 IF C2=4 AND CF=0 THEN PRINT "THERE WAS A COIN UNDER IT.":TL(5)=RM:CF=-1
1825 IF C2=2 THEN MT=0 
1830 TL(C2)=0:GOTO 8000
1899 REM *DROP*
1900 IF C2=0 THEN 8050
1905 IF TL<>0 THEN PRINT "YOU DON'T HAVE IT.":GOTO 200
1910 TL(C2)=RM:IF RM<>4 OR C2<>6 THEN 8000
1920 M$="THE RATS DEVOUR THE CANDY AND GET SLEEPY.":GOSUB1
1930 TD$(1)="SLEEPY RATS":TL(6)=-1:RT=0
1940 GOTO 8000
1999 REM * INVENTORY*
2000 K=0:I$="YOU ARE CARRYING: "
2010 FOR I=1 TO NT
2020 IF TL(I)=0 THEN I$=I$+TD$(I)+", ":K=1
2030 NEXT I
2040 IF K=0 THEN M$=I$+"NOTHING.":GOSUB1:GOTO8200
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO 8200
2099 REM *MOUNT*
2100 IF C2=0 THEN 8050
2110 IF C2<>2 THEN 8010
2120 IF RM<>10 OR MT<>0 THEN 8030
2130 TL(2)=RM:MT=-1:GOTO 8000
2199 REM *READ*
2200 IF C2=0 THEN 8050
2210 IF C2=3 THEN PRINT "'INSERT COIN.'":GOTO 8200
2220 IF C2<>10 THEN M$="NOTHING IS WRITTEN ON IT.":GOSUB1:GOTO 8200
2230 PRINT "'...USER ID IS ROAD...'"
2240 M$="'TYPE DIR FOR LIST OF COMMANDS...' THE REST IS ILLEGIBLE.":GOSUB1:GOTO 8200
2299 REM *FIGHT*
2300 PRINT "THAT WON'T WORK.":GOTO 8200
2399 REM * START*
2400 IF C2=0 THEN 8050
2410 IF C2=7 THEN 8020
2420 GOTO 8010
2499 REM *OPEN*
2500 IF C2=0 THEN 8050
2510 IF C2<>9 THEN 8010
2520 IF MF<>0 THEN PRINT "IT ALREADY IS.":GOTO 8200
2530 PRINT "INSIDE IT IS A MANUAL.":TL(10)=RM:MF=-1:GOTO 8200
2599 REM *LOOK*
2600 IF C2<>0 THEN 8040
2610 WD=-1:GOTO 8200
2699 REM *WIND*
2700 IF C2<>11 THEN 8010
2710 IF BT=0 THEN PRINT "IT'S FULLY WOUND.":GOTO 8200
2720 M$="THE CLOCK CHIMES DEAFENINGLY AND SOMETHING FLIES PAST.":GOSUB1
2730 BT=0:TL(8)=-1
2740 GOTO 8200
2799 REM * EXAMINE*
2800 IF C2=3 OR C2=10 THEN PRINT "SOMETHING IS WRITTEN THERE.":GOTO 8200
2810 IF C2=9 AND MF=0 THEN PRINT "IT IS CLOSED.":GOTO 8200
2820 IF C2=4 THEN PRINT "IT LOOKS BEYOND REPAIR.":GOTO 8200
2830 IF C2=7 THEN M$="THIS IS AN ANCIENT MAINFRAME WITH A CONSOLE.":GOSUB1:GOTO 8200
2840 IF C2=11THEN M$="THERE IS A LARGE HANDLE FOR WINDING THE CLOCK.":GOSUB1:GOTO 8200
2845 IF C2=2 AND MT<>0 THEN M$="IT IS MOUNTED ON THE COMPUTER.":GOSUB1:GOTO 8200
2850 PRINT "YOU SEE NOTHING SPECIAL.":GOTO 8200
7999 REM *COMPLETION MESSAGES*
8000 PRINT "OK.":GOTO 8200
8010 PRINT "THAT'S SILLY!":GOTO 200
8020 PRINT "NOTHING HAPPENS.":GOTO 8200
8030 PRINT "YOU CAN'T DO THAT NOW.":GOTO 8200
8040 PRINT "WHO'S YOUR ENGLISH TEACHER?":GOTO 200
8050 PRINT "PLEASE GIVE AN OBJECT.":GOTO 200
8199 REM *COMPLETION ROUTINES*
8200 IF CA=0 THEN 8300
8210 CT=CT+1:IF CT<10 THEN 8300
8220 IF RM=10 THEN M$="THE COMPUTER DIES WITH A LOUD POP.":GOSUB1
8230 CD=-1:CA=0:TD$(7)="DEAD COMPUTER"
8300 IF RM=TL(8) THEN M$="A HORDE OF BATS CARRIES YOU OUT.":GOSUB1:RM=1:WD=-1
8310 GOTO 200
9000 DATA YOU ARE IN FRONT OF AN OLD FACTORY WITH A CLOCK TOWER.
9002 DATA YOU ARE AT THE BOTTOM OF A STAIRWELL.
9004 DATA YOU ARE AT THE TOP OF THE BASEMENT STEPS.
9006 DATA YOU ARE IN A DAMP CELLAR.
9008 DATA YOU ARE IN A STOREROOM.
9010 DATA YOU'RE IN THE CAFETERIA.
9012 DATA YOU'RE AT A LANDING ON THE STAIRS.
9014 DATA AROUND YOU IS A MANUFACTURING AREA.
9016 DATA YOU'RE AT A LANDING ON THE THIRD FLOOR.
9018 DATA YOU ARE IN THE COMPUTER ROOM.
9020 DATA YOU ARE INSIDE THE CLOCK TOWER.
9022 DATA YOU'RE AT THE TOP OF THE STAIRS.
9024 DATA YOU ARE IN A LONG CORRIDOR GOING EAST.
9026 DATA YOU'RE AT THE EAST END OF THE CORRIDOR.
9050 DATA N,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,KICK,9,INSER,10,DEPOS,10,TYPE,11
9055 DATA TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14,MOUNT,15,READ,16
9060 DATA FIGHT,17,KILL,17,START,18,POWER,18,OPEN,19,QUIT,20,LOOK,21
9065 DATA WIND,22,EXAMI,23
9100 DATA RATS,TAPE,MACHI,TERMI,COIN,CANDY,COMPU,BATS,DESK,MANUA,CLOCK
9105 DATA ROAD,DIR,ADVEN,COPY,LOGOU,NORTH,SOUTH,EAST,WEST,UP,DOWN
9150 DATA HUNGRY RATS,4,0,COMPUTER TAPE,5,1,VENDING MACHINE,6,0
9155 DATA BROKEN-DOWN TERMINAL,8,1,COIN,-1,1,CANDY BAR,-1,1,COMPUTER,10,0
9160 DATA BATS,13,0,DESK,14,0,COMPUTER MANUAL,-1,1,ELABORATE CLOCKWORK,11,0
9200 DATA 2,0,0,0,0,0
9205 DATA 3,1,0,0,7,0
9210 DATA 0,2,0,0,0,4
9215 DATA 0,0,5,0,3,0
9220 DATA 0,0,0,4,0,0
9225 DATA 0,0,7,0,0,0
9230 DATA 0,0,8,6,9,2
9235 DATA 0,0,0,7,0,0
9240 DATA 0,0,10,0,12,7
9245 DATA 0,0,0,9,0,0
9250 DATA 0,12,0,0,0,0
9255 DATA 11,0,13,0,0,9
9260 DATA 0,0,14,12,0,0
9270 DATA 0,0,0,13,0,0
9999 END
