0 CLEAR2500:CLS:DIMZZ,CC,M$,I$:GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 NR=86:NT=11:NP=11:NV=29:NO=NT+NP
20 DIMAC(NR,6),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV),TL(NT),TF(NT)
99 REM *INITIALIZATION* 
100 RM=1:CT=0:CF=0:RT=-1:CA=0:MF=0:CD=0:WD=-1:LI=0:BT=-1
120 FORI=1TONR:READRM$(I):NEXT
130 FORI=1TONV:READVB$(I),VN(I):NEXT
140 FORI=1TONO:READOB$(I):NEXT 
150 FORI=1TONT:READTD$(I),TL(I),TF(I):NEXT
160 FORI=1TONR:READAC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT
199 REM *MAIN LOOP: ROOM DESCRIPTION* 
200 IFWD=0THEN400 
210 WD=0:M$=RM$(RM):GOSUB1
220 K=0
230 I$="":FORI=1TONT
240 IFTL(I)<>RMTHEN270 
250 IFK=0THENI$=I$+"YOU SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
300 I$="EXITS ARE: "
310 FORI=1TO6 
320 IFAC(RM,I)<>0THENI$=I$+VB$(I)+", "
330 NEXT
340 IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
399 REM *MAIN LOOP: COMMAND INPUT AND PARSER* 
400 INPUTC$
410 L=LEN(C$):IFL=0THEN400
420 C1$="":C2$="":C2=0:X=0 
430 FORI=1TOL
440 A$=MID$(C$,I,1):A=ASC(A$):IFA>=97ANDA<=122THENA$=CHR$(A-32)
450 IFA$<>" "THEN460
455 IFC2$<>""THEN500 
457 X=1:GOTO490
460 IFX=0THENC1$=C1$+A$:GOTO490
470 C2$=C2$+A$
490 NEXT
500 IFC1$=""THENPRINT"EXCUSE ME?":GOTO400 
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FORC1=1TONV 
530 IFVB$(C1)=C1$THENC1=VN(C1):GOTO600 
540 NEXT
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700
610 FORC2=1TONO
620 IFOB$(C2)=C2$THEN700
630 NEXT
635 IFC1=11THENC2=1:GOTO700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO 400
699 REM * PRELIMINARY FILTERING OF COMMANDS*
700 IFC2>NT+5ANDC1<>7ANDC1<>11THEN640
710 IFC2<NT+5ANDC2>NTTHENIFC1<>11THEN8010
720 IFC2>NT THEN750
725 TL=TL(C2)
730 IFC1<>11ANDTL<>RMANDTL<>0THEN PRINT "IT ISN'T HERE.":GOTO400
750 ONC1GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400
760 ONC1-10GOTO 1500,1800,1900,2000,2100,2200,2300,2400,2500,9999
770 ONC1-20GOTO 2600,2700,2800 
999 REM *DIRECTIONS*
1000 IFAC(RM,C1)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO200
1010 IFRM=4ANDRT<>0ANDC1=3THENM$="THE RATS LOOK TOO FIERCE.":GOSUB1:GOTO8200
1020 RM=AC(RM,C1):WD=-1:GOTO8200
1099 REM *GO*
1100 IFC2=0THEN8050
1105 IFC2<=16THEN8040
1110 C1=C2-16:GOTO1000
1199 REM *EAT*
1200 IFC2<>6THEN8010
1210 TL(C2)=-1:PRINT"IT TASTES STALE.":GOTO 8200
1299 REM *KICK* 
1300 IFC2=0THEN8050
1305 IFC2<>7ORCD<>0THEN8020
1310 IFCA<>0THEN CT=9:GOTO8200
1320 PRINT "THE COMPUTER STARTS UP!"
1330 M$="THE CONSOLE DISPLAYS: 'PLEASE LOG IN.'":GOSUB1:CA=-1:GOTO 8200
1399 REM * INSERT*
1400 IF C2=0 THEN 8050
1410 IF C2<>5 THEN 8010
1420 IF RM<>6 THEN 8030
1430 TL(5)=-1:TL(6)=RM:PRINT "A CANDY BAR COMES OUT.":GOTO 8200
1499 REM *TYPE* 
1500 IF C2=0 THEN 8050
1502 IF RM<>10 THEN 8030
1510 IF CA=0 THEN PRINT "THE COMPUTER ISN'T RUNNING.":GOTO 8200
1520 IF LI<>0 THEN 1600
1530 IF C2<>12 THEN PRINT "'INVALID LOGIN ID.'":GOTO 8200
1540 LI=-1:M$="'"+C2$+" LOGGED IN.'":GOSUB1:GOTO 8200 
1600 IF CP<>0 THEN 1700
1605 IF C2<=12 THEN PRINT "'INVALID COMMAND .'":GOTO8200
1610 IF C2=13 THEN PRINT "'COPY LOGOU ADVEN'":GOTO8200
1620 IF C2=14 THEN M$="'WELCOME TO ADVENTURE! W#ULD Y#$*'":GOSUB1:CT=9:GOTO 8200
1630 IF C2=15 THEN M$="'MOUNT TAPE THEN TYPE FILE NAME.'":GOSUB1:CP=-1:GOTO 8200 
1640 PRINT "'LOGGED OUT.'":LI=0:GOTO 8200
1700 CP=0:IF C2<=12 THEN PRINT "'NO SUCH FILE.'":GOTO 8200
1710 IF MT=0 THEN PRINT "'ERROR: TAPE NOT MOUNTED'":GOTO 8200
1720 PRINT "THE TAPE SPINS ..." :FOR I=1 TO 4000:NEXT I
1730 PRINT "'FILE COPIED.'"
1740 IF C2=14 THEN M$="CONGRATULATIONS, YOU'VE DONE IT!":GOSUB1:END
1750 GOTO 8200
1799 REM *TAKE* 
1800 IF C2=0 THEN 8050
1805 IF TF(C2)=0 THEN PRINT "THAT'S BEYOND YOUR ABILITY.":GOTO 8200
1810 IF TL=0 THEN PRINT "YOU ALREADY HAVE IT!":GOTO 200
1820 IF C2=4 AND CF=0 THEN PRINT "THERE WAS A COIN UNDER IT.":TL(5)=RM:CF=-1
1825 IF C2=2 THEN MT=0 
1830 TL(C2)=0:GOTO 8000
1899 REM *DROP*
1900 IF C2=0 THEN 8050
1905 IF TL<>0 THEN PRINT "YOU DON'T HAVE IT.":GOTO 200
1910 TL(C2)=RM:IF RM<>4 OR C2<>6 THEN 8000
1920 M$="THE RATS DEVOUR THE CANDY AND GET SLEEPY.":GOSUB1
1930 TD$(1)="SLEEPY RATS":TL(6)=-1:RT=0
1940 GOTO 8000
1999 REM * INVENTORY*
2000 K=0:I$="YOU ARE CARRYING: "
2010 FOR I=1 TO NT
2020 IF TL(I)=0 THEN I$=I$+TD$(I)+", ":K=1
2030 NEXT I
2040 IF K=0 THEN M$=I$+"NOTHING.":GOSUB1:GOTO8200
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO 8200
2099 REM *SAY*
2100 IF C2=0 THEN 8050
2110 IF C2<>2 THEN 8010
2120 IF RM<>10 OR MT<>0 THEN 8030
2130 TL(2)=RM:MT=-1:GOTO 8000
2199 REM *READ*
2200 IF C2=0 THEN 8050
2210 IF C2=3 THEN PRINT "'INSERT COIN.'":GOTO 8200
2220 IF C2<>10 THEN M$="NOTHING IS WRITTEN ON IT.":GOSUB1:GOTO 8200
2230 PRINT "'...USER ID IS ROAD...'"
2240 M$="'TYPE DIR FOR LIST OF COMMANDS...' THE REST IS ILLEGIBLE.":GOSUB1:GOTO 8200
2299 REM *FIGHT*
2300 PRINT "THAT WON'T WORK.":GOTO 8200
2399 REM * START*
2400 IF C2=0 THEN 8050
2410 IF C2=7 THEN 8020
2420 GOTO 8010
2499 REM *OPEN*
2500 IF C2=0 THEN 8050
2510 IF C2<>9 THEN 8010
2520 IF MF<>0 THEN PRINT "IT ALREADY IS.":GOTO 820
2530 PRINT "INSIDE IT IS A MANUAL.":TL(10)=RM:MF=-1:GOTO 8200
2599 REM *LOOK*
2600 IF C2<>0 THEN 8040
2610 WD=-1:GOTO 8200
2699 REM *WIND*
2700 IF C2<>11 THEN 8010
2710 IF BT=0 THEN PRINT "IT'S FULLY WOUND.":GOTO 8200
2720 M$="THE CLOCK CHIMES DEAFENINGLY AND SOMETHING FLIES PAST.":GOSUB1
2730 BT=0:TL(8)=-1
2740 GOTO 8200
2799 REM * EXAMINE*
2800 IF C2=3 OR C2=10 THEN PRINT "SOMETHING IS WRITTEN THERE.":GOTO 8200
2810 IF C2=9 AND MF=0 THEN PRINT "IT IS CLOSED.":GOTO 8200
2820 IF C2=4 THEN PRINT "IT LOOKS BEYOND REPAIR.":GOTO 8200
2830 IF C2=7 THEN M$="THIS IS AN ANCIENT MAINFRAME WITH A CONSOLE.":GOSUB1:GOTO 8200
2840 IF C2=11THEN M$="THERE IS A LARGE HANDLE FOR WINDING THE CLOCK.":GOSUB1:GOTO 8200
2845 IF C2=2 AND MT<>0 THEN M$="IT IS MOUNTED ON THE COMPUTER.":GOSUB1:GOTO 8200
2850 PRINT "YOU SEE NOTHING SPECIAL.":GOTO 8200
7999 REM *COMPLETION MESSAGES*
8000 PRINT "OK.":GOTO 8200
8010 PRINT "THAT'S SILLY!":GOTO 200
8020 PRINT "NOTHING HAPPENS.":GOTO 8200
8030 PRINT "YOU CAN'T DO THAT NOW.":GOTO 8200
8040 PRINT "WHO'S YOUR ENGLISH TEACHER?":GOTO 200
8050 PRINT "PLEASE GIVE AN OBJECT.":GOTO 200
8199 REM *COMPLETION ROUTINES*
8200 IF CA=0 THEN 8300
8210 CT=CT+1:IF CT<10 THEN 8300
8220 IF RM=10 THEN M$="THE COMPUTER DIES WITH A LOUD POP.":GOSUB1
8230 CD=-1:CA=0:TD$(7)="DEAD COMPUTER"
8300 IF RM=TL(8) THEN M$="A HORDE OF BATS CARRIES YOU OUT.":GOSUB1:RM=1:WD=-1
8310 GOTO 200
9001 DATABEFORE A SMALL BRICK BUILDING SURROUNDED BY FOREST. A STREAM FLOWS OUT OF THE BUILDING AND DOWN A GULLY
9002 DATASTILL IN THE FORES. THERE IS A BUILDING IN THE DISTANCE
9003 DATAINSIDE A WELLHOUSE FOR A LARGE SPRING
9004 DATAIN A VALLEY IN THE FOREST BESIDE A STREAM TUMBLING ALONG A ROCKY BED
9005 DATAIN OPEN FOREST WITH A DEEP VALLEY TO ONE SIDE
9006 DATAIN OPEN FOREST NEAR A VALLEY AND A ROAD
9007 DATAAT YOUR FEET THE STREAM SPLASHES INTO A SLIT IN THE ROCK. DOWNSTREAM THE STREAMBED IS BARE ROCK
9008 DATAIN A 20 FOOT DEPRESSION WITH A DIRT FLOOR. A DRY STREAMBED LEADS TO A STEEL GRATE MOUNTED IN CONCRETE
9009 DATAIN A SMALL CHAMBER BENEATH A 3X3 STEEL GRATE TO THE SURFACE
9010 DATACRAWLING OVER COBBLES IN A LOW PASSAGE. THERE'S A DIM LIGHT AT THE E END OF THE PASSAGE
9011 DATAIN A DEBRIS FILLED ROOM. THE PASSAGE IS BLOCKED. A GRAFITTI READS 'MAGIC WORD XYZZY'
9012 DATAIN AN AWKWARD SLOPING E/W CANYON
9013 DATAIN A 30 FT HIGH CHAMBER WITH WALLS OF ORANGE STONE
9014 DATAAT YOUR FEET A SMALL PIT FILLED WITH WHITE MIST
9015 DATAA VAST MISTY HALL STAIRCASE LEADING DOWN
9016 DATAON THE EAST BANK OF A FISSURE DIVIDING THE HALL. THE MIST IS QUITE THICK. THE FISSURE IS TOO WIDE TO JUMP
9017 DATATHIS IS A LOW ROOM WITH A CRUDE GRAFITTI 'YOU WON'T GET IT UP THE STEPS'
9018 DATAIN THE HALL OF THE MOUNTAIN KING
9019 DATAON THE WEST SIDE OF THE FISSURE IN THE HALL OF MISTS
9020 DATAIN AT A HOLE IN THE FLOOR
9021 DATAIN THE SOUTH SIDE CHAMBER
9022 DATAIN THE WEST SIDE CHAMBER OF HALL OF MT KING.
9023 DATAIN A LARGE ROOM. EAST IS A WALL OF BROKEN ROCK. IN THE CENTER YOU SEE 'Y2' WRITTEN ON A ROCK
9024 DATAIN A JUMBLE OF ROCK WITH CRACKS EVERYWHERE
9025 DATAAT A WINDOW ONTO A HUGE SHAFT. A LANDING IS JUST VISIBLE 50 FT BELOW. DIRECTLY OPPOSITE--A SIMILAR WINDOW
9026 DATAIN A DIRTY BROKEN PASSAGE. ABOVE IS A HOLE TO ANOTHER PASSAGE
9027 DATAON THE BRINK OF A SMALL CLEAN CLIMBABLE PIT
9028 DATAIN THE BOTTOM OF A SMALL PIT WITH A LITTLE STREAM WHICH ENTERS AND EXITS THROUGH TINY SLITS
9029 DATAIN A LARGE ROOM FULL OF DUSTY ROCKS. THERE'S A BIG HOLE IN THE FLOOR & CRACKS EVERYWHERE
9030 DATAA VERY LOW PASSAGE PARALLEL TO AND SOUTH OF THE HALL OF MISTS
9031 DATATHE WEST END OF A HALL OF MISTS. SOUTH YOU SEE A PASSAGE 6 FT UP THE WALL
9032 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9033 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9034 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9035 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9036 DATADEAD END
9037 DATADEAD END
9038 DATADEAD END
9039 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9040 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9041 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9042 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9043 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9044 DATADEAD END
9045 DATAIN A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE.
9046 DATADEAD END
9047 DATATHE BRINK OF A 30 FT PIT WITH A SMOOTH ORANGE COLUMN. YOU COULD CLIMB DOWN BUT NOT UP
9048 DATADEAD END
9049 DATAA VERY LOW PASSAGE PARALLEL TO AND NORTH OF THE HALL OF MISTS
9050 DATATHE EAST END OF A VERY LONG HALL
9051 DATAAT THE WEST END OF A VERY LONG FEATURELESS HALL
9052 DATAAT A CROSSOVER OF A HIGH N/S PASSAGE AND A LOW E/W ONE
9053 DATADEAD END
9054 DATAA LOW NORTH CRAWL JOINS A HIGHER EAST CRAWL TO MAKE A PASSAGE GOING WEST A LARGE ROOM IS ABOVE. THE AIR IS DAMP
9055 DATAIN BEDQUILT. A LONG E/W PASSAGE WITH HOLES EVERYWHERE
9056 DATAIN A ROOM WHOSE WALLS RESEMBLE SWISS CHEESE. THERE IS A LARGE BEDROCK BLOCK HERE
9057 DATAROOM WITH 2 PITS. THIN ROCK SLABS DESCEND EACH. MANY SMALL HOLES & A BIG ONE OVER THE EAST PIT
9058 DATAIN A CIRCULAR CHAMBER. THE FLOOR IS AN IMMENSE SLAB FALLEN FROM ABOVE. PASSAGES N/S/E/W ARE BOULDER FILLED
9059 DATAIN A SECRET N/S CANYON ABOVE A LARGE ROOM
9060 DATAIN A SECRET N/S CANYON ABOVE A SIZABLE PASSAGE
9061 DATAIN SECRET CANYON AT A JUNCTION OF 3 CANYONS BEARING N/S/E. THE N ONE IS AS TALL AS THE OTHER TWO COMBINED
9062 DATAA LARGE LOW ROOM
9063 DATADEAD END
9064 DATAA SECRET CANYON RUNS E/W HERE. 15 FT BELOW ANOTHER CANYON CROSSES. IF YOU GO DOWN YOU MAY NOT GET BACK UP
9065 DATAAT A WIDE PLACE IN A VERY TIGHT N/S CANYON
9066 DATATHE CANYON HERE BECOMES TO TIGHT TO GO FURTHER SOUTH
9067 DATAIN A TALL E/W CANYON. A LOW TIGHT CRAWL GOES 3 FEET N AND SEEMS TO OPEN UP
9068 DATATHE CANYON RUNS INTO A MASS OF BOULDERS-- DEAD END
9069 DATATHE STREAM FLOWS OUT THROUGH A PAIR OF 1 FOOT DIAMETER SEWER PIPES. IT WOULD BE ADVISABLE TO USE THE DOOR
9070 DATAEND OF THE ROAD
9071 DATAAT HILL IN ROAD
9072 DATAINSIDE BUILDING
9073 DATAIN VALLEY
9074 DATAIN FOREST
9075 DATAIN FOREST
9076 DATAAT SLIT IN STREAMBED
9077 DATAOUTSIDE GRATE
9078 DATABELOW THE GRATE
9079 DATAIN COBBLE CRAWL
9080 DATAIN DEBRIS ROOM
9081 DATAIN BIRD CHAMBER
9082 DATAAT TOP OF SMALL PIT
9083 DATAIN HALL OF MISTS
9084 DATAON EAST BANK OF FISSURE
9085 DATAIN NUGGET OF GOLD ROOM
9086 DATAIN HALL OF MT KING
9150 DATA N,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,KICK,9,INSER,10,DEPOS,10,TYPE,11
9155 DATA TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14,SAY,15,READ,16
9160 DATA FIGHT,17,KILL,17,START,18,POWER,18,OPEN,19,QUIT,20,LOOK,21
9165 DATA WIND,22,EXAMI,23
9200 DATA RATS,TAPE,MACHI,TERMI,COIN,CANDY,COMPU,BATS,DESK,MANUA,CLOCK,ROAD
9205 DATA DIR,ADVEN,COPY,LOGOU,NORTH,SOUTH,EAST,WEST,UP,DOWN
9250 DATA HUNGRY RATS,4,0,COMPUTER TAPE,5,1,VENDING MACHINE,6,0
9255 DATA BROKEN-DOWN TERMINAL,8,1,COIN,-1,1,CANDY BAR,-1,1,COMPUTER,10,0
9260 DATA BATS,13,0,DESK,14,0,COMPUTER MANUAL,-1,1,ELABORATE CLOCKWORK,11,0
9301 DATA 2,0,0,0,0,0
9302 DATA 3,1,0,0,7,0
9303 DATA 0,2,0,0,0,4
9304 DATA 0,0,5,0,3,0
9305 DATA 0,0,0,4,0,0
9306 DATA 0,0,7,0,0,0
9307 DATA 0,0,8,6,9,2
9308 DATA 0,0,0,7,0,0
9309 DATA 0,0,10,0,12,7
9310 DATA 0,0,0,9,0,0
9311 DATA 2,0,0,0,0,0
9312 DATA 3,1,0,0,7,0
9313 DATA 0,2,0,0,0,4
9314 DATA 0,0,5,0,3,0
9315 DATA 0,0,0,4,0,0
9316 DATA 0,0,7,0,0,0
9317 DATA 0,0,8,6,9,2
9318 DATA 0,0,0,7,0,0
9319 DATA 0,0,10,0,12,7
9320 DATA 0,0,0,9,0,0
9321 DATA 2,0,0,0,0,0
9322 DATA 3,1,0,0,7,0
9323 DATA 0,2,0,0,0,4
9324 DATA 0,0,5,0,3,0
9325 DATA 0,0,0,4,0,0
9326 DATA 0,0,7,0,0,0
9327 DATA 0,0,8,6,9,2
9328 DATA 0,0,0,7,0,0
9329 DATA 0,0,10,0,12,7
9330 DATA 0,0,0,9,0,0
9331 DATA 2,0,0,0,0,0
9332 DATA 3,1,0,0,7,0
9333 DATA 0,2,0,0,0,4
9334 DATA 0,0,5,0,3,0
9335 DATA 0,0,0,4,0,0
9336 DATA 0,0,7,0,0,0
9337 DATA 0,0,8,6,9,2
9338 DATA 0,0,0,7,0,0
9339 DATA 0,0,10,0,12,7
9340 DATA 0,0,0,9,0,0
9341 DATA 2,0,0,0,0,0
9342 DATA 3,1,0,0,7,0
9343 DATA 0,2,0,0,0,4
9344 DATA 0,0,5,0,3,0
9345 DATA 0,0,0,4,0,0
9346 DATA 0,0,7,0,0,0
9347 DATA 0,0,8,6,9,2
9348 DATA 0,0,0,7,0,0
9349 DATA 0,0,10,0,12,7
9350 DATA 0,0,0,9,0,0
9351 DATA 2,0,0,0,0,0
9352 DATA 3,1,0,0,7,0
9353 DATA 0,2,0,0,0,4
9354 DATA 0,0,5,0,3,0
9355 DATA 0,0,0,4,0,0
9356 DATA 0,0,7,0,0,0
9357 DATA 0,0,8,6,9,2
9358 DATA 0,0,0,7,0,0
9359 DATA 0,0,10,0,12,7
9360 DATA 0,0,0,9,0,0
9361 DATA 2,0,0,0,0,0
9362 DATA 3,1,0,0,7,0
9363 DATA 0,2,0,0,0,4
9364 DATA 0,0,5,0,3,0
9365 DATA 0,0,0,4,0,0
9366 DATA 0,0,7,0,0,0
9367 DATA 0,0,8,6,9,2
9368 DATA 0,0,0,7,0,0
9369 DATA 0,0,10,0,12,7
9370 DATA 0,0,0,9,0,0
9371 DATA 2,0,0,0,0,0
9372 DATA 3,1,0,0,7,0
9373 DATA 0,2,0,0,0,4
9374 DATA 0,0,5,0,3,0
9375 DATA 0,0,0,4,0,0
9376 DATA 0,0,7,0,0,0
9377 DATA 0,0,8,6,9,2
9378 DATA 0,0,0,7,0,0
9379 DATA 0,0,10,0,12,7
9380 DATA 0,0,0,9,0,0
9381 DATA 2,0,0,0,0,0
9382 DATA 3,1,0,0,7,0
9383 DATA 0,2,0,0,0,4
9384 DATA 0,0,5,0,3,0
9385 DATA 0,0,0,4,0,0
9386 DATA 0,0,7,0,0,0
9999 END
