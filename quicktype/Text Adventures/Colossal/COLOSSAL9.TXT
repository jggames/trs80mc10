0 CLS:CLEAR1800:DIMZZ,CC,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 M$="SOMEWHERE NEARBY IS COLOSSAL CAVE, WHERE OTHERS HAVE FOUND FORTUNES IN TREASURE AND GOLD, THOUGH IT IS RUMORED "
11 M$=M$+"THAT SOME WHO ENTER ARE NEVER SEEN AGAIN.":GOSUB1
20 NR=79:NT=20:NP=11:NV=29:NO=NT+NP:OO=NT*2:LV=25:DIMAC(NR,6),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV)
100 RM=1:WD=-1:AC(OO+1,0)=100:AC(OO+2,0)=7:AC(OO+3,0)=0:AC(OO+4,0)=250:AC(OO+5,0)=0
120 FORI=1TONR:READRM$(I):NEXT:FORI=1TONV:READVB$(I),VN(I):NEXT:FORI=1TONO:READOB$(I):NEXT:
150 FORI=1TONT:READTD$(I),AC(I,0),AC(NT+I,0):NEXT:FORI=1TONR:READAC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT
170 AC(6,0)=24+RND(2):INPUT"LOAD GAME";M$:IFLEFT$(M$,1)="Y"THENCLOAD*AC,"COLOSDAT":RM=AC(OO+7,0)
200 IFRM>10ANDAC(OO+5,0)=0THENPRINT"IT'S TOO DARK TO SEE!":GOTO400
201 IFWD=0THEN400
210 WD=0:M$=RM$(RM):GOSUB1:K=0:I$="":FORI=1TONT:IFAC(I,0)<>RMTHEN270
250 IFK=0THENI$=I$+"YOU SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2):GOSUB1
300 I$="OBVIOUS EXITS: ":FORI=1TO6:IFAC(RM,I)<>0THENI$=I$+VB$(I)+", "
330 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2):GOSUB1
400 INPUTC$:L=LEN(C$):IFL=0THEN400
420 C1$="":C2$="":C2=0:X=0 
430 FORI=1TOL:A$=MID$(C$,I,1)
450 IFA$<>" "THEN460
455 IFC2$<>""THEN500 
457 X=1:GOTO490
460 IFX=0THENC1$=C1$+A$:GOTO490
470 C2$=C2$+A$
490 NEXT
500 IFC1$=""THENPRINT"EXCUSE ME?":GOTO400 
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FORC1=1TONV:IFVB$(C1)=C1$THENCC=C1:C1=NV:NEXT:C1=VN(CC):GOTO600
540 NEXT
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700
610 FORC2=1TONO:IFOB$(C2)=C2$THENCC=C2:C2=NO:NEXT:C2=CC:GOTO700
630 NEXT:IFC1=11ORC1=16ORC1=17THEN700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO400
700 IFC2>NT+4THENIFC1<>7ANDC1<>18ANDC1<>16ANDC1<>11ANDC1<>17THEN640
710 IFC2<NT+5ANDC2>NTTHENIFC1<>11ANDC1<>10ANDC1<>17THEN8010
720 IFC2>NTTHEN750
725 TL=AC(C2,0)
730 IFC1<>11ANDTL<>RMANDTL<>0THENPRINT"IT ISN'T HERE":GOTO400
750 ONC1GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400,1500,1800,1900,2000,2100,2200,2300,2400,2500,2900,2600,2700,2800
1000 IFRM>12ANDAC(OO+5,0)=0THENIFRND(10)=1THEN2450
1001 IF(RM=66ORRM=65)AND(C1=5ORC1=6)THEN1050
1002 IFC1<>6THEN1010
1005 IFRM=67THENRM=24+RND(2):GOTO1030
1006 IFRM=3THENRM=23:GOTO1030
1007 IFRM=14THENRM=15:GOTO1030
1010 IFAC(RM,C1)=0THENPRINT"YOU CAN'T GO THAT WAY":GOTO200
1020 RM=AC(RM,C1):IFRM=33ANDAC(OO+6,0)=0THENPRINT"A HOLLOW VOICE SAYS 'PLUGH'":AC(OO+6,0)=-1
1030 WD=-1:GOTO8200
1050 ONRND(8)GOTO1051:ONRND(2)GOTO1059:RM=66:GOTO1030
1051 RM=67+RND(12):GOTO1030
1059 RM=65:GOTO1030
1100 IFC2=0THEN8050
1101 IFC2=NT+5ANDRM=3THENRM=23:WD=-1:GOTO8200
1105 IFC2<=NT+5THEN8040
1110 C1=C2-(NT+5):GOTO1000
1200 IFC2=17ANDAC(17,0)=0THENPRINT"IT TASTES STALE":AC(17,0)=-1:AC(OO+1,0)=100:GOTO8200
1210 IFC2=9ANDAC(9,0)=0THENPRINT"IT TASTES GAMEY":AC(9,0)=-1:GOTO8200
1280 GOTO8010
1300 IFC2=0THEN8050
1305 IFC2<>4THEN8020
1310 IFRM=8ANDAC(8,6)=0THENPRINT"IT'S OPEN":AC(8,6)=9:GOTO8200
1330 GOTO8020
1400 IFAC(8,0)<>0THENPRINT"YOU DON'T HAVE IT":GOTO8200
1410 IFC2=NT+3THENIFAC(OO+4,0)>0THENPRINT"IT'S ON":AC(OO+5,0)=-1:GOTO8200
1420 IFC2=NT+4THENPRINT"IT'S OFF":AC(OO+5,0)=0:GOTO8200
1430 GOTO8010
1500 IFC2=0THEN8050
1505 IFC2<>NT+1THEN1540
1507 FORI=1TONT:IFAC(I,0)=0THENAC(I,0)=RM
1508 NEXT
1510 IFRM=3THENRM=11:GOTO1520
1515 RM=3
1520 PRINT"THERE'S A BLINDING FLASH!":C2=0:GOTO8200
1540 IFC2<>NT+2THEN1590
1550 IFRM=33THEN1515
1590 GOTO8020
1800 IFC2=0THEN8050
1805 IFAC(NT+C2,0)=0THENPRINT"THAT'S BEYOND YOUR ABILITY":GOTO8200
1810 IFTL=0THENPRINT"YOU ALREADY HAVE IT!":GOTO200
1820 IFC2<>9THEN1830
1821 IFRM<>13THEN1830
1822 IFAC(3,0)=0ORAC(2,0)<>0THENM$="IT LOOKS FRIGHTENED AND FLIES AWAY":GOSUB1:GOTO8200
1830 IFC2<>13THEN1880
1832 AC(15,5)=0
1880 AC(C2,0)=0:GOTO8000
1900 IFC2=0THEN8050
1905 IFTL<>0THENPRINT"YOU DON'T HAVE IT":GOTO200
1910 IFC2<>2ANDC2<>9THEN1940
1915 IFAC(9,0)<>0ORAC(2,0)<>0THEN1940
1920 IFRM<>19THEN1923
1921 M$="THE LITTLE BIRD ATTACKS THE SNAKE AND IN AN ASTOUNDING FLURRY DRIVES IT AWAY!":GOSUB1
1922 AC(1,0)=-1:AC(19,1)=28:AC(19,2)=29:AC(19,4)=30
1923 PRINT"THE BIRD GOT AWAY!":AC(9,0)=13:AC(2,0)=RM:GOTO8200
1940 AC(C2,0)=RM:GOTO8000
2000 K=0:I$="YOU ARE CARRYING: "
2010 FORI=1TONT
2020 IFAC(I,0)=0THENI$=I$+TD$(I)+", ":K=1
2030 NEXT
2040 IFK=0THENM$=I$+"NOTHING.":GOSUB1:GOTO8200
2050 M$=LEFT$(I$,LEN(I$)-2):GOSUB1:GOTO8200
2100 IFC2=0THEN8050
2110 IFC2<>3THEN2120
2111 IFAC(3,0)=0ANDRM=17ANDAC(16,0)=-1THENPRINT"A BRIDGE APPEARS!":AC(16,0)=17:AC(17,4)=27:GOTO8200
2120 IFC2<>15THEN2130
2121 IFAC(15,0)=0ANDRM=32THENPRINT"YOU WEDGE IT IN THE CLAM. BANG! THE CLAM COUGHS":AC(19,0)=32:AC(15,0)=-1:GOTO8200
2130 IFC2<>6THEN2140
2131 IFAC(6,0)=0THENPRINT"YOU PUT IT IN THE LAMP":AC(OO+4,0)=250:AC(6,0)=-1:GOTO8200
2140 IFC2<>20THEN2190
2150 IFAC(20,0)=0THENPRINT"YOU PUT IT IN THE LAMP":AC(OO+4,0)=350:AC(20,0)=-1:GOTO8200
2190 GOTO8010
2200 IFRM=11THENPRINT"'MAGIC WORD XYZZY'":GOTO8200
2210 IFRM=18THENPRINT"'YOU WON'T GET IT UP THE STEPS'":GOTO8200
2220 IFC2=0THEN8050
2240 PRINT"NOTHING IS WRITTEN ON IT":GOTO8200
2300 IFRM=19ANDAC(1,0)=19THENM$="YOU CAN'T KILL THE SNAKE, DRIVE IT AWAY, OR AVOID IT!":GOSUB1:GOTO8200
2305 IFC2=9ANDAC(9,0)=0THENPRINT"IT'S DEAD":AC(9,0)=-1:GOTO8200
2310 IFAC(OO+3,0)=0ORAC(14,0)<>0THEN2390
2320 IFRND(2)=1THENPRINT"YOU KILLED A LITTLE DWARF":AC(OO+3,0)=0:AC(OO+2,0)=AC(OO+2,0)-1:GOTO8200
2330 M$="YOU ATTACK A LITTLE DWARF WITH THE AXE, BUT HE DODGES OUT OF THE WAY":GOSUB1:GOTO8200
2390 PRINT"THAT WON'T WORK":GOTO8200
2400 IFC2=0THEN8050
2410 IFC2<NT+6THEN8010
2420 IFC2=NT+11ANDRM=14THENRM=15:WD=-1:GOTO8200
2430 IF(C2=NT+9ANDRM=17)OR(C2=NT+8ANDRM=27)THEN2450
2440 GOTO8020
2450 M$="YOU ARE AT THE BOTTOM OF A PIT WITH A BROKEN NECK!":GOSUB1:END
2500 SC=0:FORI=1TONT:IF(I=13ORI=12ORI=5ORI=18ORI=7ORI=19)ANDAC(I,0)=0THENSC=SC+50
2510 NEXT:IFRM=3THENSC=SC+50
2520 PRINT"SCORE"SC"OUT OF 350":IFSC=350THENPRINT"YOU WIN!":END 
2530 GOTO200
2600 IFC2<>0THEN8040
2610 WD=-1:GOTO8200
2700 IFC2<>10THEN8010
2710 IF(RM=23ORRM=38ORRM=61)ANDAC(10,0)=0THENPRINT"IT'S FILLED":AC(OO+1,0)=100:GOTO8200
2740 GOTO8030
2800 IFC2=4THENPRINT"THERE'S A LOCK ON IT":GOTO8200
2810 IFC2=8THENPRINT"IT HAS AN ON/OFF SWITCH":GOTO8200
2820 IFC2<>10THEN2850
2830 IFAC(OO+1,0)<=20THENPRINT"IT'S ALMOST EMPTY":GOTO8200
2840 PRINT"THERE'S SOME WATER IN IT":GOTO8200
2850 PRINT"YOU SEE NOTHING SPECIAL":GOTO8200
2900 INPUT"SAVE GAME";M$:IFLEFT$(M$,1)="Y"THENAC(OO+7,0)=RM:CSAVE*AC,"COLOSDAT"
2910 END
8000 PRINT"OK.":GOTO8200
8010 PRINT"THAT'S SILLY!":GOTO200
8020 PRINT"NOTHING HAPPENS":GOTO8200
8030 PRINT"YOU CAN'T DO THAT NOW":GOTO8200
8040 PRINT"WHO'S YOUR ENGLISH TEACHER?":GOTO200
8050 PRINT"PLEASE GIVE AN OBJECT":GOTO200
8200 IFAC(OO+5,0)THENAC(OO+4,0)=AC(OO+4,0)-1
8204 IFAC(OO+4,0)>20ORAC(8,0)<>0ORAC(OO+5,0)=0THEN8210
8205 PRINT"YOUR LAMP IS GETTING DIM.":IFAC(OO+4,0)<1THENPRINT"IT'S GONE OUT!":AC(OO+5,0)=0
8210 AC(OO+1,0)=AC(OO+1,0)-1:IFAC(OO+1,0)>20THEN8240
8220 IFAC(OO+1,0)>=0THENPRINT"YOU FEEL FAINT.":GOTO8900
8240 IFAC(OO+2,0)>0ORAC(OO+3,0)THENIF(RND(LV)=1ANDRM>24)ORAC(OO+3,0)THEN8500
8310 GOTO200
8500 IFAC(OO+2,0)<>7ORAC(14,0)<>-1THEN8510
8505 M$="A LITTLE DWARF JUST WALKED AROUND A CORNER, SAW YOU & THREW A LITTLE AXE WHICH MISSED!":GOSUB1:AC(14,0)=RM:GOTO200
8510 M$="THERE IS A THREATENING LITTLE DWARF IN THE ROOM WITH YOU! ONE SHARP NASTY KNIFE IS THROWN AT YOU!":GOSUB1
8520 AC(OO+3,0)=-1:IFRND(10)=1THENPRINT"HE GETS YOU!":AC(OO+1,0)=AC(OO+1,0)-RND(10):GOTO8900
8530 PRINT"HE MISSED!":GOTO200
8900 IFAC(OO+1,0)<1THENM$="YOU COLLAPSE FROM EXHAUSTION! YOU'RE DEAD":GOSUB1:END
8910 GOTO200
9001 DATABEFORE A SMALL BRICK BUILDING SURROUNDED BY FOREST. A STREAM FLOWS OUT OF THE BUILDING AND DOWN A GULLY
9002 DATASTILL IN THE FOREST. THE ROAD SLOPES BACK DOWN THE OTHER SIDE OF THE HILL. THERE IS A BUILDING IN THE DISTANCE
9003 DATAINSIDE A WELL HOUSE FOR A LARGE SPRING
9004 DATAA VALLEY IN THE FOREST BESIDE A STREAM TUMBLING ALONG A ROCKY BED
9005 DATAOPEN FOREST WITH A DEEP VALLEY TO ONE SIDE
9006 DATAOPEN FOREST NEAR A VALLEY AND A ROAD
9007 DATAAT YOUR FEET THE WATER OF THE STREAM SPLASHES INTO A SLIT IN THE ROCK. DOWNSTREAM THE STREAMBED IS BARE ROCK
9008 DATAA 20 FOOT DEPRESSION WITH A DIRT FLOOR. A DRY STREAMBED LEADS TO A STEEL GRATE MOUNTED IN CONCRETE
9009 DATAA SMALL CHAMBER BENEATH A 3X3 STEEL GRATE TO THE SURFACE. A CRAWLWAY OVER COBBLES LEADS INWARD TO THE WEST
9010 DATACRAWLING OVER COBBLES IN A LOW PASSAGE. THERE IS A DIM LIGHT AT THE EAST END OF THE PASSAGE
9011 DATAA DEBRIS FILLED ROOM. THE PASSAGE IS BLOCKED BUT A CANYON LEADS UP & WEST. YOU SEE A CRUDE GRAFITTI
9012 DATAAN AWKWARD SLOPING E/W CANYON
9013 DATAA 30 FT HIGH CHAMBER WITH WALLS OF ORANGE STONE. AN AWKWARD CANYON AND A GOOD PASSAGE EXIT E & W
9014 DATAAT YOUR FEET IS A SMALL PIT BREATHING TRACES OF WHITE MIST. THE PASSAGE ENDS HERE EXCEPT FOR A SMALL CRACK LEADING ON
9015 DATAA VAST MISTY HALL STETCHING WEST WITH N & S OPENINGS & A STAIRCASE LEADING DOWN. A DOME BEHIND YOU HAS A PASSAGE
9016 DATATHE CRACK IS FAR TOO SMALL FOR YOU TO FOLLOW
9017 DATAON THE EAST BANK OF A FISSURE DIVIDING THE HALL. THE MIST IS QUITE THICK. THE FISSURE IS TOO WIDE TO JUMP
9018 DATATHIS IS A LOW ROOM WITH A CRUDE GRAFITTI
9019 DATATHE HALL OF THE MOUNTAIN KING WITH PASSAGES OFF IN ALL DIRECTIONS
9020 DATA*
9021 DATA*
9022 DATA*
9023 DATABOTTOM OF A WELL
9024 DATAWEDGED IN A TINY SLIT!
9025 DATABOTTOM OF A PIT
9026 DATABOTTOM OF A PIT
9027 DATAON THE WEST SIDE OF THE FISSURE IN THE HALL OF MISTS
9028 DATAA LOW N/S PASSAGE AT A HOLE IN THE FLOOR. THE HOLE GOES DOWN TO AN E/W PASSAGE
9029 DATATHE SOUTH SIDE CHAMBER
9030 DATATHE WEST SIDE CHAMBER OF HALL OF MT KING. A PASSAGE CONTINUES WEST AND UP HERE
9031 DATA*
9032 DATAA LARGE NATURAL CAVERN WITH DAMP GLISTENNING WALLS. THERE IS AN ENORMOUS CLAM HERE WITH ITS SHELL TIGHTLY CLOSED
9033 DATAA LARGE ROOM WITH PASSAGES S & W. EAST IS A WALL OF BROKEN ROCK. IN THE CENTER YOU SEE 'Y2' WRITTEN ON A ROCK
9034 DATAA JUMBLE OF ROCK WITH CRACKS EVERYWHERE
9035 DATAAT A WINDOW ONTO A HUGE SHAFT. A LANDING IS JUST VISIBLE 50 FT BELOW. DIRECTLY OPPOSITE--A SIMILAR WINDOW
9036 DATAA DIRTY BROKEN PASSAGE WITH A CRAWLWAY E & A LARGE PASSAGE W. ABOVE IS A HOLE TO ANOTHER PASSAGE
9037 DATAON THE BRINK OF A SMALL CLEAN CLIMBABLE PIT. A CRAWL LEADS W
9038 DATATHE BOTTOM OF A SMALL PIT WITH A LITTLE STREAM WHICH ENTERS AND EXITS THROUGH TINY SLITS
9039 DATAA LARGE ROOM FULL OF DUSTY ROCKS. THERE'S A BIG HOLE IN THE FLOOR & CRACKS EVERYWHERE. A PASSAGE LEADS EAST
9040 DATAYOU HAVE CRAWLED THROUGH A VERY LOW PASSAGE PARALLEL TO AND N OF THE HALL OF MISTS
9041 DATATHE W END OF A HALL OF MISTS. LOW CRAWLAYS GO W & N. SOUTH YOU SEE A PASSAGE 6 FT UP THE WALL
9042 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9043 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9044 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9045 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9046 DATADEAD END
9047 DATADEAD END
9048 DATADEAD END
9049 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9050 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9051 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9052 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9053 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9054 DATADEAD END
9055 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9056 DATADEAD END
9057 DATATHE BRINK OF A 30 FT PIT WITH A SMOOTH ORANGE COLUMN. YOU COULD CLIMB DOWN BUT NOT UP. THE MAZE CONTINUES
9058 DATADEAD END
9059 DATAYOU HAVE CRAWLED THROUGH A VERY LOW PASSAGE PARALLEL TO AND N OF THE HALL OF MISTS
9060 DATATHE EAST END OF A VERY LONG HALL. TO THE EAST A LOW CRAWL SLANTS UP. TO THE NORTH A LOW CRAWL SLANTS DOWN
9061 DATAAT THE WEST END OF A VERY LONG HALL. A SMALL STREAM FLOWS HERE
9062 DATAAT A CROSSOVER OF A HIGH N/S PASSAGE AND A LOW E/W ONE
9063 DATADEAD END
9064 DATAA LOW N CRAWL JOINS A HIGHER E CRAWL TO MAKE A PASSAGE GOING W. SIGN 'CAVE UNDER CONSTRUCTION. PROCEED AT OWN RISK.'
9065 DATAIN BEDQUILT. A LONG E/W PASSAGE WITH HOLES EVERYWHERE IN THE FLOOR AND CEILING
9066 DATAA ROOM WHOSE WALLS RESEMBLE SWISS CHEESE. THERE IS A LARGE BEDROCK BLOCK HERE
9067 DATAROOM WITH 2 PITS. THIN ROCK SLABS DESCEND EACH. A PATH GOES E. WATER DRIPS INTO THE PITS FROM ABOVE
9068 DATAA CIRCULAR CHAMBER. THE FLOOR IS AN IMMENSE SLAB FALLEN FROM ABOVE. N & E & W PASSAGES ARE BOULDER FILLED
9069 DATAA SECRET N/S CANYON ABOVE A LARGE ROOM
9070 DATAA SECRET N/S CANYON ABOVE A SIZABLE PASSAGE
9071 DATASECRET CANYON AT A JUNCTION OF 3 CANYONS BEARING N&S&E. THE N ONE IS AS TALL AS THE OTHER TWO COMBINED
9072 DATAA LARGE LOW ROOM
9073 DATADEAD END
9074 DATAA SECRET CANYON RUNS E/W HERE. 15 FT BELOW ANOTHER CANYON CROSSES. IF YOU GO DOWN YOU MAY NOT GET BACK UP
9075 DATAAT A WIDE PLACE IN A VERY TIGHT N/S CANYON
9076 DATATHE CANYON HERE BECOMES TOO TIGHT TO GO FURTHER
9077 DATAA TALL E/W CANYON. A LOW TIGHT CRAWL GOES 3 FEET NORTH AND SEEMS TO OPEN UP
9078 DATATHE CANYON RUNS INTO A MASS OF BOULDERS-- DEAD END
9079 DATATHE STREAM FLOWS OUT THROUGH A PAIR OF 1 FOOT DIAMETER SEWER PIPES. IT WOULD BE ADVISABLE TO USE THE DOOR
9150 DATAN,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,UNLOC,9,LIGHT,10,LAMP,10,SAY,11
9155 DATATAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14,USE,15,READ,16
9160 DATAFIGHT,17,KILL,17,JUMP,18,LEAP,18,SCORE,19,QUIT,20,LOOK,21
9165 DATAFILL,22,EXAMI,23
9200 DATASNAKE,CAGE,ROD,GRATE,COINS,BATTE,BARS,LAMP,BIRD,BOTTL,KEYS,DIAMO,NUGGE,AXE,FIREW,BRIDG,SANDW,JEWEL,PEARL,ENERG
9205 DATAXYZZY,PLUGH,ON,OFF,WELL,NORTH,SOUTH,EAST,WEST,UP,DOWN
9250 DATAHUGE GREEN SNAKE,19,0,WICKER CAGE,10,1,BLACK ROD,11,1
9255 DATASTEEL GRATE,8,0,MANY COINS,30,1,BATTERY,-1,1,SILVER BARS,28,1
9260 DATABRASS LAMP,3,1,A CHEERFUL BIRD,13,1,WATER BOTTLE,3,1,KEYS,23,1,DIAMONDS,27,1,GOLD NUGGET,18,1,SMALL AXE,-1,1
9265 DATARED FIREWORK,71,1,CRYSTAL BRIDGE,-1,0,A SANDWICH,73,1,PRECIOUS JEWELRY,29,1,GIANT PEARL,-1,1,ENERGIZER,52,1
9301 DATA5,4,3,2,,4
9302 DATA1,5,1,,,1
9303 DATA0,,,1,,0
9304 DATA1,7,5,5,5,7
9305 DATA0,5,4,5,,4
9306 DATA1,5,4,4,,4
9307 DATA4,8,5,5,4,24
9308 DATA7,5,5,5,,0
9309 DATA0,,,10,8,0
9310 DATA0,,9,11,,0
9311 DATA0,,10,12,12,0
9312 DATA0,,11,13,13,11
9313 DATA0,,12,14,,0
9314 DATA0,,13,16,,0
9315 DATA19,18,,17,14,19
9316 DATA0,,14,,,0
9317 DATA0,,15,,,0
9318 DATA15,,,,,0
9319 DATA0,,15,,15,0
9320 DATA0,,,,,0
9321 DATA0,,,,,0
9322 DATA0,,,,,0
9323 DATA0,,,,3,0
9324 DATA0,,,,7,0
9325 DATA0,,,,67,0
9326 DATA0,,,,67,0
9327 DATA59,,0,41,,0
9328 DATA33,19,,,,36
9329 DATA19,,0,,0,0
9330 DATA0,,19,62,62,0
9331 DATA0,,,,,0
9332 DATA0,64,,0,,0
9333 DATA0,28,34,35,,0
9334 DATA0,,,,15,33
9335 DATA0,,33,,,0
9336 DATA0,,37,39,28,0
9337 DATA0,,,36,,38
9338 DATA0,,,,37,0
9339 DATA0,,36,,,64
9340 DATA40,40,40,40,59,40
9341 DATA59,42,27,60,42,0
9342 DATA0,44,43,41,,0
9343 DATA0,44,45,42,,0
9344 DATA42,50,43,,,48
9345 DATA43,47,46,,,0
9346 DATA0,,,45,,0
9347 DATA45,,0,,,0
9348 DATA0,,,,44,0
9349 DATA0,,50,51,,50
9350 DATA0,52,44,49,49,0
9351 DATA0,53,52,49,,0
9352 DATA50,,55,51,53,0
9353 DATA52,54,,51,,0
9354 DATA0,,53,,,0
9355 DATA0,,57,52,,56
9356 DATA0,,,,55,0
9357 DATA0,58,,55,,13
9358 DATA0,,,57,,0
9359 DATA41,27,41,27,27,41
9360 DATA62,,41,61,41,62
9361 DATA0,,60,,,0
9362 DATA63,,30,60,,0
9363 DATA0,62,,,,0
9364 DATA32,,0,65,39,0
9365 DATA0,,64,66,,0
9366 DATA72,,65,67,,0
9367 DATA72,,66,,,0
9368 DATA0,66,,,69,0
9369 DATA0,74,,,,68
9370 DATA71,,,,,0
9371 DATA0,70,,,39,65
9372 DATA73,66,,67,,0
9373 DATA0,72,,,,0
9374 DATA0,,19,69,,75
9375 DATA77,76,,,,0
9376 DATA75,,,,,0
9377 DATA66,,75,78,,0
9378 DATA0,,77,,,0
9379 DATA66,,,,,65
