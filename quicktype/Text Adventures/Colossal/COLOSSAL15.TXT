0 CLS:CLEAR1800:DIMZZ,CC,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO90
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
5 IFAC(O+5,0)THENAC(O+4,0)=AC(O+4,0)-1
6 IFAC(O+4,0)>20ORAC(8,0)<>0ORAC(O+5,0)=0THEN10
7 PRINT"YOUR LAMP IS GETTING DIM.":IFAC(O+4,0)<1THENPRINT"IT'S GONE OUT!":AC(O+5,0)=0
10 AC(O+1,0)=AC(O+1,0)-1:IFAC(O+1,0)<20THENPRINT"YOU FEEL FAINT."
40 IFAC(O+2,0)>0ORAC(O+3,0)THENIF(RND(25)=1ANDRM>24)ORAC(O+3,0)THEN8500
50 IFAC(O+1,0)<1THENPRINT"YOU COLLAPSE FROM EXHAUSTION!":GOTO8920
60 GOTO200
90 M$="SOMEWHERE NEARBY IS COLOSSAL CAVE, WHERE OTHERS HAVE FOUND FORTUNES IN TREASURE AND GOLD, THOUGH IT IS RUMORED "
95 M$=M$+"THAT SOME WHO ENTER ARE NEVER SEEN AGAIN.":GOSUB1:NR=78:NT=24:NP=11:NV=29:NO=NT+NP:O=NT*2
99 DIMAC(NR,6),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV):RM=1:WD=-1:AC(O+1,0)=100:AC(O+2,0)=7:AC(O+4,0)=250
120 FORI=1TONR:READRM$(I):NEXT:FORI=1TONV:READVB$(I),VN(I):NEXT:FORI=1TONO:READOB$(I):NEXT
150 FORI=1TONT:READTD$(I),AC(I,0),AC(NT+I,0):NEXT:FORI=1TONR:READAC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT
170 AC(6,0)=24+RND(2):INPUT"LOAD GAME";M$:IFLEFT$(M$,1)="Y"THENPRINT"PRESS PLAY":CLOAD*AC,"COLOSDAT":RM=AC(O+7,0)
200 IFRM>10AND(AC(O+5,0)=0OR(AC(8,0)<>0ANDAC(8,0)<>RM))THENPRINT"IT'S TOO DARK TO SEE!":GOTO400
201 IFWD=0THEN400
210 WD=0:M$=RM$(RM):GOSUB1:IFRM<>63THEN220
211 INPUT"ARE YOU A PRINCE";M$:IFLEFT$(M$,1)="Y"THENM$="YOU'LL MAKE A FITTING HUSBAND FOR MY DAUGHTER!":GOSUB1:GOTO8920
212 PRINT"THEN GO!":GOTO220
220 K=0:I$="":FORI=1TONT:IFAC(I,0)<>RMTHEN270
250 IFK=0THENI$=I$+"YOU SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2):GOSUB1
300 I$="OBVIOUS EXITS: ":FORI=1TO6:IFAC(RM,I)<>0THENI$=I$+VB$(I)+", "
330 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2):GOSUB1
400 INPUTC$:L=LEN(C$):IFL=0THEN400
410 IFLEFT$(C$,4)="HELP"THENM$="VERBS I KNOW: ":FORCC=7TONV:M$=M$+VB$(CC)+" ":NEXT:GOSUB1:GOTO400
420 C1$="":C2$="":C2=0:X=0:FORI=1TOL:A$=MID$(C$,I,1):IFA$<>" "THEN460
455 IFC2$<>""THENCC=I:I=L:NEXT:I=CC:GOTO500
457 X=1:GOTO490
460 IFX=0THENC1$=C1$+A$:GOTO490
470 C2$=C2$+A$
490 NEXT
500 IFC1$=""THEN8040
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5):FORC1=1TONV:IFVB$(C1)=C1$THENCC=C1:C1=NV:NEXT:C1=VN(CC):GOTO600
540 NEXT:M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700
610 FORC2=1TONO:IFOB$(C2)=C2$THENCC=C2:C2=NO:NEXT:C2=CC:GOTO700
630 NEXT:IFC1=11ORC1=16ORC1=17THEN700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO400
700 IFC2>NT+4THENIFC1<>7ANDC1<>18ANDC1<>16ANDC1<>11ANDC1<>17THEN640
710 IFC2<NT+5ANDC2>NTTHENIFC1<>11ANDC1<>10ANDC1<>17THEN8010
720 IFC2>NTTHEN750
730 TL=AC(C2,0):IFC1<>11ANDTL<>RMANDTL<>0THENPRINT"IT ISN'T HERE.":GOTO400
750 ONC1GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400,1500,1800,1900,2000,2100,2200,2300,2400,2500,2900,2600,2700,2800
1000 IFRM>10ANDAC(O+5,0)=0THENIFRND(3)=1THEN8900
1001 IF((RM=17ANDC1=4)OR(RM=27ANDC1=3))ANDAC(16,0)=-1THEN8900
1002 IFC1=6AND(RM=17ORRM=27ORRM=35)THEN8900
1003 IF(RM=66ORRM=65)AND(C1=5ORC1=6)THEN1050
1004 IFC1<>6THEN1010
1005 IFRM=67THENRM=24+RND(2):GOTO1030
1006 IFRM=3THENRM=23:GOTO1030
1007 IFRM<>14THEN1010
1008 RM=15:IFAC(15,5)=0THENPRINT"OUCH!":AC(O+1,0)=AC(O+1,0)-10
1009 GOTO1030
1010 IFAC(RM,C1)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO200
1020 RM=AC(RM,C1):IFRM=33ANDAC(O+6,0)=0THENPRINT"A HOLLOW VOICE SAYS 'PLUGH'":AC(O+6,0)=-1
1030 WD=-1:GOTO5
1050 ONRND(6)GOTO1051:RM=64+RND(2):GOTO1030
1051 RM=68+RND(2):GOTO1030
1100 IFC2=0THEN8050
1101 IFC2=NT+5ANDRM=19THENRM=74:WD=-1:GOTO5
1105 IFC2<=NT+5THEN8040
1110 C1=C2-(NT+5):GOTO1000
1200 IF(C2=17ANDAC(17,0)=0)OR(C2=24ANDAC(24,0)=0)THENPRINT"IT TASTES STALE.":AC(C2,0)=-1:GOSUB1970:AC(O+1,0)=100:GOTO5
1210 IFC2=9ANDAC(9,0)=0THENPRINT"IT TASTES GAMEY.":AC(9,0)=-1:GOSUB1970:GOTO5
1280 GOTO8010
1300 IFC2=0THEN8050
1305 IFC2=4THENIFRM=8ANDAC(8,6)=0ANDAC(11,0)=0THENPRINT"IT'S OPEN.":AC(8,6)=9:GOTO5
1330 GOTO8030
1400 IFAC(8,0)<>0THENPRINT"YOU DON'T HAVE IT.":GOTO5
1410 IFC2=NT+3THENIFAC(O+4,0)>0THENPRINT"IT'S ON.":AC(O+5,0)=-1:GOTO5
1420 IFC2=NT+4THENPRINT"IT'S OFF.":AC(O+5,0)=0:GOTO5
1430 GOTO8010
1500 IFC2=0THEN8050
1505 IFC2<>NT+1THEN1540
1510 AC(O+8,0)=0:GOSUB1700:IFRM=3THENRM=11:GOTO1520
1515 RM=3
1520 PRINT"THERE'S A BLINDING FLASH!":C2=0:GOTO5
1540 IFC2<>NT+2THEN8020
1550 IFRM=3ANDAC(O+6,0)=-1THENRM=33:GOTO1520
1555 IFRM<>33THEN8020
1560 IFAC(13,0)=0THENAC(13,0)=33:GOSUB1970
1560 GOTO1515
1700 FORI=1TONT:IFAC(I,0)=0ANDI<>8THENAC(I,0)=RM:GOSUB1970
1710 NEXT:RETURN
1800 IFC2=0THEN8050
1801 IFAC(O+8,0)=7THENPRINT"YOU HAVE TOO MUCH!":GOTO200
1805 IFAC(NT+C2,0)=0THENPRINT"THAT'S BEYOND YOUR ABILITY.":GOTO5
1810 IFTL=0THENPRINT"YOU ALREADY HAVE IT!":GOTO200
1820 IFC2=9ANDAC(3,0)=0THENM$="THE BIRD IS FRIGHTENED RIGHT NOW AND YOU CANNOT CATCH IT.":GOSUB1:GOTO5
1825 IFC2=9ANDAC(2,0)<>0THENPRINT"YOU'VE NOTHING TO KEEP IT IN.":GOTO200
1830 IFC2=13THENAC(15,5)=0:AC(23,0)=-1
1880 GOSUB1950:GOTO8000
1900 IFC2=0THEN8050
1905 IFTL<>0THENPRINT"YOU DON'T HAVE IT.":GOTO200
1910 IFC2<>2ANDC2<>9THEN1940
1915 IFAC(9,0)<>0ORAC(2,0)<>0THEN1940
1920 IFRM<>19THEN1923
1921 M$="THE LITTLE BIRD ATTACKS THE SNAKE AND IN AN ASTOUNDING FLURRY DRIVES IT AWAY!":GOSUB1
1922 AC(1,0)=-1:AC(19,1)=28:AC(19,2)=29:AC(19,4)=30
1923 PRINT"THE BIRD GOT AWAY!":AC(9,0)=13:GOSUB1970:C2=2:GOSUB1960:GOTO5
1940 GOSUB1960:GOTO8000
1950 AC(C2,0)=0:AC(O+8,0)=AC(O+8,0)+1:RETURN
1960 AC(C2,0)=RM
1970 AC(O+8,0)=AC(O+8,0)-1:RETURN
2000 K=0:I$="YOU ARE CARRYING: ":FORI=1TONT:IFAC(I,0)=0THENI$=I$+TD$(I)+", ":K=1
2030 NEXT:IFK=0THENI$=I$+"NOTHING.  "
2050 M$=LEFT$(I$,LEN(I$)-2):GOSUB1:GOTO5
2100 IFC2=0THEN8050
2110 IFC2<>3THEN2120
2111 IFAC(3,0)=0AND(RM=17ORRM=27)THENPRINT"A BRIDGE APPEARS!":AC(16,0)=RM:AC(17,4)=27:AC(27,3)=17:GOTO5
2120 IFC2<>15THEN2130
2121 IFAC(15,0)=0ANDRM=32THENPRINT"YOU WEDGE IT IN THE CLAM. BANG! THE CLAM COUGHS.":AC(19,0)=32:AC(15,0)=-1:GOSUB1970:GOTO5
2130 IFC2=6ORC2=20THENIFAC(C2,0)=0THENAC(C2,0)=-1:GOSUB1970:PRINT"YOU PUT IT IN THE LAMP.":AC(O+4,0)=250:GOTO5
2190 GOTO8010
2200 IFRM=11THENPRINT"'MAGIC WORD XYZZY'":GOTO5
2210 IFRM=18THENPRINT"'YOU WON'T GET IT UP THE STEPS'":GOTO5
2220 IFC2=0THEN8050
2240 PRINT"NOTHING IS WRITTEN ON IT.":GOTO5
2300 IFRM=19ANDAC(1,0)=19THENM$="YOU CAN'T KILL THE SNAKE, DRIVE IT AWAY OR AVOID IT!":GOSUB1:GOTO5
2305 IFC2=9ANDAC(9,0)=0THENPRINT"IT'S DEAD":AC(9,0)=-1:GOTO5
2310 IFAC(O+3,0)=0ORAC(14,0)<>0THEN2390
2320 IFRND(3)=1THENPRINT"YOU KILLED A LITTLE DWARF.":AC(O+3,0)=0:AC(O+2,0)=AC(O+2,0)-1:GOTO5
2330 M$="YOU ATTACK THE LITTLE DWARF WITH THE AXE, BUT HE DODGES OUT OF THE WAY.":GOSUB1:GOTO5
2390 PRINT"THAT WON'T WORK.":GOTO5
2400 PRINT"THAT'S A RELIEF!":GOSUB1700:GOTO200
2500 SC=0:FORI=1TONT:IF(I=13ORI=12ORI=5ORI=18ORI=7ORI=19)AND(AC(I,0)=0ORAC(I,0)=3)THENSC=SC+50
2510 NEXT:IFRM=3THENSC=SC+50
2520 PRINT"SCORE"SC"OUT OF 350":IFSC=350THENPRINT"YOU WIN!":GOTO8920
2530 GOTO200
2600 IFC2<>0THEN8040
2610 WD=-1:GOTO5
2700 IFC2<>10THEN8010
2710 IF(RM=23ORRM=38ORRM=61ORRM=79)ANDAC(10,0)=0THENPRINT"IT'S FILLED.":AC(O+1,0)=100:GOTO5
2740 GOTO8030
2800 IFC2=4THENPRINT"THERE'S A LOCK ON IT.":GOTO5
2810 IFC2=8THENPRINT"IT HAS AN ON/OFF SWITCH.":GOTO5
2815 IFC2=21THENPRINT"SOMETHING GLINTS.":GOTO5
2816 IFC2<>22THEN2819
2817 M$="THE STREAM FLOWS OUT THROUGH A PAIR OF 1 FT DIAMETER SEWER PIPES. IT WOULD BE ADVISABLE TO USE THE DOOR":GOSUB1:GOTO5
2819 IFC2=3THENPRINT"THERE'S A RUSTY STAR ON IT.":GOTO5
2820 IFC2<>10THEN2850
2830 IFAC(O+1,0)<=20THENPRINT"IT'S ALMOST EMPTY.":GOTO5
2840 PRINT"THERE'S SOME WATER IN IT.":GOTO5
2850 PRINT"YOU SEE NOTHING SPECIAL.":GOTO5
2900 INPUT"SAVE GAME";M$:IFLEFT$(M$,1)="Y"THENAC(O+7,0)=RM:INPUT"PRESS RECORD & HIT ENTER";M$:CSAVE*AC,"COLOSDAT"
2910 END
8000 PRINT"OK.":GOTO5
8010 PRINT"THAT'S SILLY!":GOTO200
8020 PRINT"NOTHING HAPPENS":GOTO5
8030 PRINT"YOU CAN'T DO THAT NOW.":GOTO5
8040 PRINT"WHO'S YOUR ENGLISH TEACHER?":GOTO200
8050 PRINT"PLEASE GIVE AN OBJECT.":GOTO200
8500 IFAC(O+2,0)<>7ORAC(14,0)<>-1THEN8510
8505 M$="A LITTLE DWARF JUST WALKED AROUND A CORNER, SAW YOU & THREW A LITTLE AXE WHICH MISSED!":GOSUB1:AC(14,0)=RM:GOTO50
8510 M$="THERE IS A THREATENING LITTLE DWARF IN THE ROOM WITH YOU! A SHARP NASTY KNIFE IS THROWN AT YOU!":GOSUB1
8520 AC(O+3,0)=-1:IFRND(4)=1THENPRINT"HE GETS YOU!":AC(O+1,0)=AC(O+1,0)-10:GOTO50
8530 PRINT"HE MISSED!":GOTO50
8900 M$="YOU ARE AT THE BOTTOM OF A PIT WITH A BROKEN NECK!":GOSUB1
8920 PRINT"GAME OVER":END
9001 DATABEFORE A SMALL BRICK BUILDING SURROUNDED BY FOREST. A SMALL STREAM FLOWS OUT OF THE BUILDING AND DOWN A GULLY
9002 DATASTILL IN THE FOREST. THE ROAD SLOPES BACK DOWN THE OTHER SIDE OF THE HILL. THERE IS A BUILDING IN THE DISTANCE
9003 DATAINSIDE A WELL HOUSE FOR A LARGE SPRING,A VALLEY IN THE FOREST BESIDE A STREAM TUMBLING ALONG A ROCKY BED
9005 DATAOPEN FOREST WITH A DEEP VALLEY TO ONE SIDE,OPEN FOREST NEAR A VALLEY AND A ROAD
9007 DATAAT YOUR FEET THE WATER OF THE STREAM SPLASHES INTO A SLIT IN THE ROCK. DOWNSTREAM THE STREAMBED IS BARE ROCK
9008 DATAA 20 FOOT DEPRESSION WITH A DIRT FLOOR. A DRY STREAMBED LEADS TO A STEEL GRATE MOUNTED IN CONCRETE
9009 DATAA SMALL CHAMBER BENEATH A 3X3 STEEL GRATE TO THE SURFACE. A CRAWLWAY OVER COBBLES LEADS INWARD TO THE WEST
9010 DATACRAWLING OVER COBBLES IN A LOW PASSAGE. THERE IS A DIM LIGHT AT THE EAST END OF THE PASSAGE
9011 DATAA DEBRIS FILLED ROOM. THE PASSAGE IS BLOCKED BUT A CANYON LEADS W & UPWARD. YOU SEE A CRUDE GRAFITTI
9012 DATAAN AWKWARD SLOPING E/W CANYON
9013 DATAA 30 FT HIGH CHAMBER WITH WALLS OF ORANGE STONE. AN AWKWARD CANYON EXITS E AND A GOOD PASSAGE W
9014 DATAAT YOUR FEET IS A SMALL PIT BREATHING TRACES OF WHITE MIST. THE PASSAGE ENDS HERE EXCEPT FOR A SMALL CRACK LEADING ON
9015 DATAA VAST MISTY HALL STETCHING W WITH N & S OPENINGS AND A WIDE STAIRCASE LEADING DOWN. A DOME BEHIND YOU HAS A PASSAGE
9016 DATATHE CRACK IS FAR TOO SMALL FOR YOU TO FOLLOW
9017 DATAON THE EAST BANK OF A FISSURE DIVIDING THE HALL. THE MIST IS QUITE THICK. THE FISSURE IS TOO WIDE TO JUMP
9018 DATATHIS IS A LOW ROOM WITH A CRUDE GRAFITTI,THE HALL OF THE MOUNTAIN KING WITH PASSAGES OFF IN ALL DIRECTIONS,*,*,*
9023 DATADOWN THE WELLSPRING,WEDGED IN A TINY SLIT!,BOTTOM OF A PIT,BOTTOM OF A PIT
9027 DATAON THE WEST SIDE OF THE FISSURE IN THE HALL OF MISTS
9028 DATAA LOW N/S PASSAGE AT A HOLE IN THE FLOOR. THE HOLE GOES DOWN TO AN E/W PASSAGE,THE SOUTH SIDE CHAMBER
9030 DATATHE WEST SIDE CHAMBER OF HALL OF MT KING. A PASSAGE CONTINUES WEST AND UP HERE,*
9032 DATAA LARGE NATURAL CAVERN WITH DAMP GLISTENNING WALLS. THERE IS AN ENORMOUS CLAM HERE WITH ITS SHELL TIGHTLY CLOSED
9033 DATAA LARGE ROOM WITH PASSAGES S & W. EAST IS A WALL OF BROKEN ROCK. IN THE CENTER YOU SEE 'Y2' WRITTEN ON A ROCK
9034 DATAA JUMBLE OF ROCK WITH CRACKS EVERYWHERE
9035 DATAAT A WINDOW ONTO A HUGE SHAFT. A LANDING IS JUST VISIBLE 50 FT BELOW. DIRECTLY OPPOSITE--A SIMILAR WINDOW
9036 DATAA DIRTY BROKEN PASSAGE WITH A CRAWLWAY E AND A LARGE PASSAGE W. ABOVE IS A HOLE TO ANOTHER PASSAGE
9037 DATAON THE BRINK OF A SMALL CLEAN CLIMBABLE PIT. A CRAWL LEADS W
9038 DATATHE BOTTOM OF A SMALL PIT WITH A LITTLE STREAM WHICH ENTERS AND EXITS THROUGH TINY SLITS
9039 DATAA LARGE ROOM FULL OF DUSTY ROCKS. THERE'S A BIG HOLE IN THE FLOOR & CRACKS EVERYWHERE. A PASSAGE LEADS E
9040 DATAYOU HAVE CRAWLED THROUGH A VERY LOW PASSAGE PARALLEL TO AND N OF THE HALL OF MISTS
9041 DATATHE W END OF A HALL OF MISTS. LOW CRAWLWAYS GO W & N. SOUTH YOU SEE A PASSAGE 6 FT UP THE WALL
9042 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9044 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,DEAD END,DEAD END,DEAD END
9049 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9051 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE
9053 DATAA MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,DEAD END,A MAZE OF TWISTY LITTLE PASSAGES ALL ALIKE,DEAD END
9057 DATATHE BRINK OF A 30 FT PIT WITH A SMOOTH ORANGE COLUMN. YOU COULD CLIMB DOWN BUT NOT UP. THE MAZE CONTINUES,DEAD END
9059 DATAYOU HAVE CRAWLED THROUGH A VERY LOW PASSAGE PARALLEL TO AND N OF THE HALL OF MISTS
9060 DATATHE EAST END OF A VERY LONG HALL. TO THE EAST A LOW CRAWL SLANTS UP. TO THE NORTH A LOW CRAWL SLANTS DOWN
9061 DATAAT THE WEST END OF A VERY LONG HALL. A SMALL STREAM FLOWS HERE
9062 DATAAT A CROSSOVER OF A HIGH N/S PASSAGE AND A LOW E/W ONE,AN UGLY TROLL ON A THRONE!
9064 DATAA LOW N CRAWL JOINS A HIGHER E CRAWL TO MAKE A PASSAGE GOING W. SIGN 'CAVE UNDER CONSTRUCTION. PROCEED AT OWN RISK.'
9065 DATAIN BEDQUILT. A LONG E/W PASSAGE WITH HOLES EVERYWHERE IN THE FLOOR AND CEILING
9066 DATAA ROOM WHOSE WALLS RESEMBLE SWISS CHEESE. THERE IS A LARGE BEDROCK BLOCK HERE
9067 DATAROOM WITH 2 PITS. THIN ROCK SLABS DESCEND EACH. A PATH GOES E. WATER DRIPS INTO THE PITS FROM ABOVE
9068 DATAA LARGE CIRCULAR CHAMBER. THE FLOOR IS AN IMMENSE SLAB FALLEN FROM ABOVE. E & W PASSAGES ARE BOULDER FILLED
9069 DATAA SECRET NS CANYON ABOVE A LARGE ROOM,A SECRET N/S CANYON ABOVE A SIZABLE PASSAGE
9071 DATASECRET CANYON AT A JUNCTION OF 3 CANYONS BEARING N & S & E. THE N ONE IS AS TALL AS THE OTHER TWO COMBINED
9072 DATAA LARGE LOW ROOM,DEAD END
9074 DATAA SECRET CANYON RUNS E/W HERE. 15 FT BELOW ANOTHER CANYON CROSSES. IF YOU GO DOWN YOU MAY NOT GET BACK UP
9075 DATAAT A WIDE PLACE IN A VERY TIGHT N/S CANYON,THE CANYON HERE BECOMES TOO TIGHT TO GO FURTHER
9077 DATAA TALL E/W CANYON. A LOW TIGHT CRAWL GOES 3 FEET NORTH AND SEEMS TO OPEN UP
9078 DATATHE CANYON RUNS INTO A MASS OF BOULDERS-- DEAD END
9150 DATAN,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,UNLOC,9,LIGHT,10,LAMP,10,SAY,11,TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14
9160 DATAUSE,15,READ,16,FIGHT,17,KILL,17,JUMP,7,UNLOA,18,SCORE,19,QUIT,20,LOOK,21,FILL,22,EXAMI,23
9200 DATASNAKE,CAGE,ROD,GRATE,COINS,BATTE,BARS,LAMP,BIRD,BOTTL,KEYS,DIAMO,NUGGE,AXE,FIREW,BRIDG,SANDW,JEWEL,PEARL,DURAC
9205 DATAPITS,PIPES,STEPS,FOOD,XYZZY,PLUGH,ON,OFF,SE,NORTH,SOUTH,EAST,WEST,UP,DOWN
9250 DATAHUGE GREEN SNAKE,19,,WICKER CAGE,10,1,BLACK ROD,11,1,STEEL GRATE,8,,MANY COINS,30,1,BATTERY,-1,1,SILVER BARS,28,1
9260 DATABRASS LAMP,3,1,A CHEERFUL BIRD,13,1,WATER BOTTLE,3,1,KEYS,23,1,DIAMONDS,27,1,GOLD NUGGET,18,1,SMALL AXE,-1,1
9265 DATARED FIREWORK,71,1,CRYSTAL BRIDGE,-1,,A SANDWICH,73,1,PRECIOUS JEWELRY,29,1,GIANT PEARL,-1,1,A DURACELL (R),52,1
9266 DATATWO PITS,67,,PIPES,3,,ROUGH STONE STEPS,15,,FOOD,78,1
9301 DATA5,4,3,2,,4,1,5,1,,,1,,,,1,,,1,7,5,5,5,7,,5,4,5,,4,1,5,4,4,,4,4,8,5,5,4,24,7,5,5,5,,0
9309 DATA0,,,10,8,,,,9,11,,,,,10,12,12,,,,11,13,13,11,,,12,14,,,,,13,16,,,19,18,,17,14,19,,,14,,,0
9317 DATA0,,15,,,,15,,,,,,,,15,,15,,,,,,,,,,,,,,,,,,,,,,,,3,,,,,,7,0
9325 DATA0,,,,67,,,,,,67,,59,,,41,,,33,19,,,,36,19,,,,,,,,19,62,62,,,,,,,,,64,,,,0
9333 DATA0,28,34,35,,,,,,,15,33,,,33,,,,,,37,39,28,,,,,36,,38,,,,,37,,,,36,,,64,40,40,40,40,40,59
9341 DATA40,42,27,60,42,,,44,43,41,,,,44,45,42,,,42,50,43,,,48,43,47,46,,,,,,,45,,,45,,,,,,,,,,44,0
9349 DATA0,,50,51,,50,,52,44,49,49,,,53,52,49,,,50,,55,51,53,,52,54,,51,,,,,53,,,,,,57,52,,56,,,,,55,0
9357 DATA0,58,,55,,13,,,,57,,,41,27,41,27,27,41,62,,41,61,41,62,,,60,,,,63,,30,60,,,,62,,,,,32,,,65,39,0
9365 DATA0,,64,66,,,72,,65,67,,,72,,66,,,,,66,,,69,,,74,,,,68,71,,,,,,,70,,,39,65,73,66,,67,,0
9373 DATA0,72,,,,,,,19,69,,75,77,76,,,,,75,,,,,,66,,75,78,,,,,77,,,0
9999 REM MC-10 MODS JIM GERRIE
