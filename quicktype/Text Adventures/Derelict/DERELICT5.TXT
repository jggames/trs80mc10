0 CLEAR400:O=100:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

3 WT=WT-1:C(ZZ)=O+R:GOTO13
4 GOSUB1
5 FORZZ=1TO3500:NEXT:RETURN
6 M$="THE "+M$+"DOOR IS OPEN.":GOTO8
7 WT=WT+1:C(ZZ)=1:GOTO13
8 GOSUB1:GOTO3000
9 PRINT"OKAY.":GOTO50

10 M$=A$(1):GOTO8
12 M$=A$(2)+C$:GOTO8
13 M$=A$(3):GOTO8
14 M$=A$(4):GOTO8
15 M$=A$(5):GOTO8
17 M$=A$(7):GOTO8
18 M$=A$(8):GOTO8
19 M$=A$(9):GOTO8

30 FORX=1TO16:IFC(X)=O+RTHENFL=1:X=16
32 NEXTX:IFFL=0THENRETURN
34 M$="THE FOLLOWING ITEMS ARE IN THIS AREA:":GOSUB1:FORX=1TO16
36 IFC(X)=O+RTHENM$=O$(X):GOSUB1
37 NEXTX:FL=0:RETURN

REM ROBOT
40 IFC(13)=0THENRETURN
42 T=T+1:IFT=1THENRETURN
44 IFT=2THENPRINT"THE ROBOT IS ATTACKING ME!":RETURN
45 IFT=3THEN16000

50 RESTORE:FORCC=1TOR:READR$(1),R$(2),N,S,E,W:NEXT
51 T=0:IFR=23THEN63
52 M$=R$(1)
53 IFR=11ANDC(11)<>105THEN55
54 M$=M$+" "+R$(2)
55 GOSUB1:GOSUB30
56 IFR=23THEN3000
57 I$="":IFN=1THENI$=I$+"NORTH, "
58 IFS=1THENI$=I$+"SOUTH, "
59 IFE=1THENI$=I$+"EAST, "
60 IFW=1THENI$=I$+"WEST, "
61 IFI$<>""THENM$="I CAN GO: "+LEFT$(I$,LEN(I$)-2):GOSUB1
62 GOTO3000
63 IFL=0ORC(8)<>1THENPRINT"IT'S TOO DARK TO SEE!"
64 IFC(8)<>1THEN3000
65 IFL=1THEN52
66 GOSUB5:PRINT"I AM TURNING ON MY FLASHLIGHT.":GOSUB5:L=1:GOTO52

100 CLS:DIMA$(22),O$(16),C(16),D(17):GOSUB2000
160 M$="SERIES R ROBOT EXPLORER NUMBER 182-X3 CALLING CENTRAL CONTROL...":GOSUB4
162 M$="TURNING COMMAND OVER TO YOU...":GOSUB4:PRINT:GOTO50

REM ROOM 1
310 DATAI AM HANGING ON TO A HAND HOLD AT THE ONLY ENTRANCE TO THE SHIP.
311 DATA"AN OPEN AIRLOCK LEADS INSIDE.",,,,

REM ROOM 2
320 DATAI AM NOW IN THE AIRLOCK. THERE IS A DOOR HERE THAT LEADS INTO THE SHIP.
321 DATA"NEXT TO THE DOOR IS A PANEL OF 3 BUTTONS, BLUE, BLACK, AND GREEN.",,,,

REM ROOM 3
330 DATAI AM IN AN EMPTY ROOM.,"",,1,1,1

REM ROOM 4
340 DATAI AM IN A VERY LARGE ROOM. THERE IS A PENTANGULAR ELECTRONIC DEVICE IN THE CENTRE.
341 DATAITS FUNCTION IS UNCLEAR. TOWARD THE EASTERN WALL THERE IS A VERY LARGE HOLOGRAM.,,1,,1

REM ROOM 5
350 DATAI AM IN A LARGE ROOM WITH A PEDESTAL IN THE CENTRE. 
351 DATASOME SORT OF DEVICE IS PLANTED IN THE CEILING.,1,,,

REM ROOM 6
360 DATATHIS LARGE ROOM HAS NO DISTINGUISHING CHARACTERISTICS.,"",1,1,1,

REM ROOM 7
370 DATATHIS ROOM IS QUITE LARGE. IN THE CENTRE THERE IS A PEDESTAL.
371 DATA"NEXT TO THE DOOR THERE ARE 3 BUTTONS, BLUE, BLACK, AND GREEN.",1,,,

REM ROOM 8
380 DATAI AM IN A LONG PASSAGEWAY.
381 DATASOMETHING LIKE FOOTPRINTS ARE VISIBLE IN THE DUST LEADING NORTH.,1,1,,

REM ROOM 9
390 DATATHE PASSAGE INTERSECTS WITH ANOTHER PASSAGEWAY.,THERE ARE FOOTPRINTS LEADING WEST.,,1,1,1

REM ROOM 10
400 DATATHIS IS THE END OF THE PASSAGE.,THE FOOTPRINTS LEAD TO THE WESTERN DOOR.,1,1,1,1

REM ROOM 11
410 DATAI AM IN A PASSAGEWAY LEADING TO A ROOM SIMILAR TO THE LAST ONE.
411 DATATHERE IS A TWISTED METAL OBJECT IN THE SOUTHERN ROOM.,1,1,,

REM ROOM 12
420 DATATHIS ROOM SEEMS TO BE A CREW'S QUARTERS WITH NUMEROUS PENTANGULAR OBJECTS THAT I PRESUME TO BE BEDS.
421 DATATHERE IS STORAGE SPACE HERE BUT IT HAS BEEN CLEARED OF ALL OBJECTS.,1,,,

REM ROOM 13
430 DATATHIS WOULD SEEM TO BE AN ENGINE ROOM.
431 DATATHERE IS THE USUAL CONFIGURATION OF TUBES ASSOCIATED WITH THE KRANTZ-WARREN HYPERSPACE DRIVE.
433 DATA,,1,

REM ROOM 14
440 DATATHIS WOULD SEEM TO BE AN ENGINE ROOM.
441 DATATHERE IS THE USUAL CONFIGURATION OF TUBES ASSOCIATED WITH THE KRANTZ-WARREN HYPERSPACE DRIVE.
443 DATA,,,1

REM ROOM 15
450 DATA"IT IS THE MAIN CONTROL ROOM, WITH BUTTONED NAVIGATION PANEL, PENTAGONAL CHAIRS WITH CONSOLES AND A PEDESTAL,"
451 DATA"APPARENTLY UNCONNECTED TO THE CONTROL FUNCTIONS. THERE ARE DOORS: NORTHEAST, SOUTHEAST, NORTHWEST SOUTHWEST."
453 DATA,,,

REM ROOM 16
460 DATAI AM IN A PASSAGEWAY TERMINATING AT AN EASTERN DOORWAY.,"",,,1,1

REM ROOM 17 AND 18
470 DATAI AM IN A PASSAGEWAY.,"",,,1,1,I AM IN A PASSAGEWAY.,THERE ARE DOORS NORTH AND SOUTH.,1,1,1,1

REM ROOMS 19
490 DATATHIS ROOM SEEMS TO BE A CREW'S QUARTERS WITH NUMEROUS PENTANGULAR OBJECTS THAT I PRESUME TO BE BEDS.
491 DATATHERE IS STORAGE SPACE HERE BUT IT HAS BEEN CLEARED OF ALL OBJECTS.,1,,,

REM ROOM 20
500 DATATHE END OF THE PASSAGEWAY.,"",,,,1

REM ROOM 21
510 DATA"I AM IN WHAT SEEMS TO BE A KITCHEN. THERE ARE VARIOUS STEEL ALLOY TABLES,"
511 DATA"A SCULLERY, CONVECTION OVENS, AND A LARGE OPEN AND EMPTY CUPBOARD.",1,1,,

REM ROOM 22
520 DATATHIS AREA SEEMS TO BE AN EXERCISE ROOM.
521 DATA"THERE IS A MARKED-OFF BALL(?) COURT, AND A LARGE SWIMMING(?) POOL, FROZEN SOLID.",1,1,,

REM ROOM 23
530 DATATHIS IS AN ENORMOUS ROOM STRETCHING ALL THE WAY TO WHAT MUST BE THE FRONT OF THE SHIP.
531 DATA"THERE ARE 3 DOORS, ONE TO THE NORTH, ONE TO THE SOUTHWEST, AND THE OTHER TO THE SOUTHEAST."
533 DATA1,,,

REM ROOM 24
540 DATAI AM IN A DRAMATICALLY COLOURFUL TRIANGULAR CRYSTAL ROOM.
541 DATAAT THE NORTHERN CORNER IS A CONTROL PANEL WITH A SLOT IN IT.,,1,,

REM SET VARIABLES, HOUSEKEEPING
2000 R=1:A$(1)="PLEASE CLARIFY.":A$(2)="I CANNOT RESPOND TO THE COMMAND ":A$(3)="OKAY."
2060 A$(4)="I CANNOT GO IN THAT DIRECTION.":A$(5)="THERE IS A LOCKED DOOR IN THAT DIRECTION."
2080 A$(6)="I CANNOT DO THAT AT THIS TIME.":A$(7)="THAT ITEM IS NOT HERE.":A$(8)="TELL ME HOW."
2110 A$(9)="COULD YOU BE MORE SPECIFIC?":A$(10)="IT SEEMS TO BE SOME SORT OF MACHINE PART. IT FITS EASILY IN MY HAND."
2130 A$(11)="IT LOOKS LIKE A CHILD'S CLICKING TOY."
2140 A$(12)="THE LIGHT PLAYS AROUND IN IT AS IF IT HAS A LIFE OF ITS OWN."
2150 A$(13)="IT IS NOT FIVE-SIDED. IT APPEARS TO BE NON-FUNCTIONAL."
2160 A$(14)="IT OPENS EASILY.":A$(15)="IT IS OF UNKNOWN CRYSTALLINE STRUCTURE."
2180 A$(16)="IT IS THE STANDARD ISSUE.":A$(17)="SHARP CUTTING EDGE."
2200 A$(18)="IT APPEARS TO BE MADE OF AN ALUMINUM-TYPE ALLOY."
2210 A$(19)="IT IS HUMANOID IN SHAPE AND APPEARS TO HAVE BEEN BLASTED BY A HEAT WEAPON."
2220 A$(20)="IT IS SPIDER LIKE AND DEFINITELY LETHAL."
2230 A$(21)="NOTHING HAPPENS.":A$(22)="I DON'T SEE ANYTHING REMARKABLE."
2420 L=0:WT=2:RETURN

2260 O$(1)="ODD PIECE OF METAL":C(1)=119:O$(2)="SMALL LEAD PENTAGON":C(2)=104
2280 O$(3)="CRYSTAL PENTAGON":C(3)=107:O$(4)="UNIDENTIFIED WEAPON":C(4)=121
2300 O$(5)="PENTAGONAL BOX":C(5)=105:O$(6)="CRYSTAL STAR":C(6)=0
2320 O$(7)="LASER PISTOL":C(7)=1:O$(8)="FLASHLIGHT":C(8)=1:O$(9)="LARGE BLADE KNIFE":C(9)=121
2350 O$(10)="METAL BUCKET":C(10)=121:O$(11)="RUINED ALIEN ROBOT":C(11)=105
2370 O$(12)="STRONG HEMP ROPE":C(12)=0:O$(13)="GIANT FIVE-LEGGED ROBOT":C(13)=123
2390 O$(14)="SMALL AREA OF WATER AT EDGE OF ICE":C(14)=0:O$(15)="FILLED BUCKET":C(15)=0
2410 O$(16)="DEACTIVATED ROBOT":C(16)=0

REM CONWCOM
3000 IFR=23THENGOSUB40
3040 PRINT:INPUT"COMMAND";C$:IFC$=""THEN10
3070 IFLEN(C$)=1THEN3180
3080 CC=LEN(C$):FORZZ=1TO9:IFMID$(C$,ZZ+1,1)=" "THENCC=ZZ
3081 NEXT:IFLEN(C$)=2THEN12
3090 IFCC=2THEN3840
3100 IFCC=3THEN4390
3110 IFCC=4THEN4790
3120 IFCC=5THEN6850
3130 IFCC=6THEN7570
3140 IFCC=7THEN7790
3150 IFCC=8THEN7790
3160 IFCC=9THEN7790
3170 GOTO12

3180 IFC$="S"THEN3470
3190 IFC$="N"THEN3230
3200 IFC$="W"THEN3620
3210 IFC$="E"THEN3730
3220 GOTO10

3230 IFR<3THEN10
3240 IFN<>1THEN14
3250 IFR=5THENR=11:GOTO9
3260 IFR=6ANDD(1)=0THEN15
3270 IFR=6ANDD(1)=1THENR=8:GOTO9
3280 IFR=7ANDD(2)=1THENR=6:GOTO9
3290 IFR=7ANDD(2)=0THEN15
3300 IFR=8THENR=9:GOTO9
3310 IFR=10ANDD(4)=1THENR=22:GOTO9
3320 IFR=10THEN15
3330 IFR=11THENR=4:GOTO9
3340 IFR=12ANDD(3)=1THENR=10:GOTO9
3350 IFR=12THEN15
3360 IFR=18ANDD(5)=1THENR=21:GOTO9
3370 IFR=18THEN15
3380 IFR=19ANDD(6)=1THENR=18:GOTO9
3390 IFR=19THEN15
3400 IFR=21ANDD(7)=1THENR=23:GOTO9
3410 IFR=21THEN15
3420 IFR=22ANDD(8)=0THEN15
3430 IFR=22THENR=23:GOTO9
3440 IFR=23ANDD(9)=1THENR=24:GOTO9
3450 IFR=23THEN15
3460 GOTO10

3470 IFR<3THEN10
3480 IFS<>1THEN14
3490 IFR=3ANDD(10)=1THENR=2:GOTO9
3500 IFR=4THENR=11:GOTO9
3510 IFR=11ANDD(11)=1THENR=5:GOTO9
3520 IFR=11THEN16000
3530 IFR=6ANDD(2)=1THENR=7:GOTO9
3540 IFR=8ANDD(1)=1THENR=6:GOTO9
3550 IFR=9THENR=8:GOTO9
3560 IFR=10ANDD(3)=1THENR=12:GOTO9
3570 IFR=18ANDD(6)=1THENR=19:GOTO9
3580 IFR=21ANDD(5)=1THENR=18:GOTO9
3590 IFR=22ANDD(4)=1THENR=10:GOTO9
3600 IFR=24THENR=23:GOTO9
3610 GOTO15

3620 IFR<3THEN10
3630 IFW<>1THEN14
3640 IFR=3ANDD(12)THENR=6:GOTO9
3650 IFR=4THENR=3:GOTO9
3660 IFR=9THENR=10:GOTO9
3670 IFR=14ANDD(14)=1THENR=15:GOTO9
3680 IFR=16THENR=9:GOTO9
3690 IFR=17ANDD(17)=1THENR=15:GOTO9
3700 IFR=18THENR=17:GOTO9
3710 IFR=20THENR=18:GOTO9
3720 GOTO15

3730 IFR<3THEN10
3740 IFE<>1THEN14
3750 IFR=3THENR=4:GOTO9
3760 IFR=6ANDD(12)=1THENR=3:GOTO9
3770 IFR=9THENR=16:GOTO9
3780 IFR=10THENR=9:GOTO9
3790 IFR=13ANDD(15)=1THENR=15:GOTO9
3800 IFR=16ANDD(16)=1THENR=15:GOTO9
3810 IFR=17THENR=18:GOTO9
3820 IFR=18THENR=20:GOTO9
3830 GOTO15

REM 2 LETTER 1ST WORDS
3840 IFC$="GO"THEN10
3860 IFLEFT$(C$,2)<>"GO"THEN12
3870 N$=MID$(C$,3)
3880 IFN$=" NORTH"THEN3230
3890 IFN$=" SOUTH"THEN3470
3900 IFN$=" WEST"THEN3620
3910 IFN$=" EAST"THEN3730
3920 IFR<>23THEN3970
3930 IFN$<>" SOUTHEAST"THEN3970
3931 IFD(8)=1THENR=22:GOTO9
3950 IFD(7)=1THENR=21:GOTO9
3960 GOTO15
3970 IFR<>15THEN4060
3980 IFN$=" NORTHEAST"ANDD(17)=1THENR=17:GOTO9
3990 IFN$=" SOUTHEAST"ANDD(14)=1THENR=14:GOTO9
4000 IFN$=" NORTHWEST"ANDD(16)=1THENR=16:GOTO9
4010 IFN$=" SOUTHWEST"ANDD(15)=1THENR=13:GOTO9
4020 GOTO15
4030 IFC$="GO PANEL"THENM$="THERE ARE NUMEROUS CONTROLS, BUT NONE OF THEM SEEM TO BE OPERATIVE.":GOTO8

4040 IFC$<>"GO PEDESTAL"THEN4060
4041 IFC(6)<>0ANDC(6)<>115THEN13
4050 IFC(6)=115THENM$="THERE'S SOMETHING ON TOP OF THE PEDESTAL.":GOTO8

4060 IFC$<>"GO HOLOGRAM"THEN4080
4061 IFR<>4THEN17
4065 M$="IT IS SOME SORT OF MAP, BUT IT IS INCOMPREHENSIBLE.":GOSUB1
4066 IFC(3)=1THENM$="I AM RECEIVING A STRANGE SUBVERBAL MESSAGE. IT SAYS 'THINK, EARTHLING.'":GOSUB1
4070 GOTO3000

4080 IFN$<>" PANEL"THEN4110
4081 IFR=2ORR=7ORR=24THEN13
4100 GOTO17

4110 IFN$<>" AIRLOCK"ANDN$<>" DOOR"ANDN$<>" SHIP"THEN4180
4111 IFR<>1THEN4150
4141 R=2:GOTO50
4150 IFR<>2THEN10
4151 IFD(10)=1THENR=3:GOTO50
4160 GOTO15

4180 IFLEFT$(N$,4)<>" PED"THEN4200
4181 IFC(3)=107ANDR=7THENM$="OKAY. THAT IS WHERE THE CRYSTAL IS.":GOTO8
4190 IFR=7THEN13

4200 IFN$=" BED"THENM$="ROBOTS HAVE NO NEED OF SLEEP.":GOTO8

4250 IFN$=" COURT"ORN$=" ROPE"ORN$=" POOL"THEN4260
4251 GOTO4280
4260 IFR=22THEN13
4270 GOTO17

4280 IFC$<>"GO DEVICE"THEN19
4290 IFR=5THENM$="IT IS INACCESSIBLE.":GOTO8
4300 IFR<>4THEN17
4310 M$="IT IS SOME SORT OF TRANSMITTING DEVICE. IT IS SENDING A MESSAGE IN CODE.":GOSUB4
4320 M$="THE CONTROL COMPUTER SHOULD BE ABLE TO TRANSLATE IT...":GOSUB4
4330 M$="PEOPLE OF THE SYSTEM OF THE 9 PLANETS: THIS IS A MESSAGE FROM THE FEDERATION OF KRENN.":GOSUB4
4340 M$="THIS SHIP IS A NUCLEAR EXPLOSIVE DEVICE. IT IS AIMED DIRECTLY AT YOUR HOME PLANET.":GOSUB4
4350 M$="NOW THAT YOU HAVE DEVELOPED FASTER THAN LIGHT TECHNOLOGY, YOU ARE A THREAT.":GOTO8

REM 3 LETTER WORDS
4390 IFLEFT$(C$,3)="GET"THENC$="TAKE"+MID$(C$,4):GOTO4790
4400 IFLEFT$(C$,3)<>"TIE"THEN4460
4401 IFC(12)=1THEN10
4450 M$=A$(8):GOTO8
4460 IFC$="SAY"THEN10
4470 IFC$<>"SAY OPEN"THEN4720
4480 PRINT"OPEN"
4490 IFC(3)<>1THEN4710
4500 IFR=6ANDD(1)=0THENM$="NORTH ":D(1)=1:GOTO6
4510 IFR=7ANDD(2)=0THENM$="NORTH ":D(2)=1:GOTO6
4520 IFR=11THENM$="I AM WILLING TO GO IN IF YOU THINK IT'S ADVISABLE.":D(11)=1:GOTO8
4530 IFR<>10THEN4570
4531 IFD(3)=1ANDD(4)=1THEN4710
4540 IFD(4)=0THENM$="NORTH ":D(4)=1:GOTO6
4550 IFD(3)=0THENM$="SOUTH ":D(3)=1:GOTO6
4560 GOTO3000
4570 IFR=22ANDD(4)=0THENM$="SOUTH ":D(4)=1:GOTO6
4580 IFR=16ANDD(16)=0THENM$="EAST ":D(16)=1:GOTO6
4590 IFR=17THENFORX=14TO17:D(X)=1:NEXTX:GOTO5600
4600 IFR=13ANDD(15)=0THEN D(15)=1:GOTO6
4610 IFR=14ANDD(14)=0THEN D(14)=1:GOTO6
4620 IFR<>15THEN4680
4630 IFD(16)=0THENM$="NORTHWEST ":D(16)=1
4640 IFD(15)=0THENM$="SOUTHNEST ":D(15)=1
4650 IFD(17)=0THENM$="NORTHEAST ":D(17)=1
4660 IFD(14)=0THENM$="SOUTHEAST ":D(14)=1
4670 GOTO8
4680 IFR=17ANDD(17)=0THENM$="WEST ":D(17)=1:GOTO6
4690 IFR=18ANDD(6)=0THENM$="SOUTH ":D(6)=1:GOTO6
4700 IFR=19ANDD(6)=0THEN D(6)=1:GOTO6
4710 M$=A$(21):GOTO8

4720 IFC$<>"CUT ROPE"THEN4760
4730 IFR=22ANDC(12)=0ANDC(9)=1THENC(12)=122:GOTO13
4740 IFC(9)<>1THEN5220
4750 IFC(12)<>O+ROR(R<>22ANDC(12)=0)ORC(12)<>1THEN10
4760 GOTO12

REM 4 LETTER WORDS
4790 S$=LEFT$(C$,4):N$=MID$(C$,5):IFS$="READ"THEN12
4830 IFS$="PUSH"THENC$="PRESS"+N$:GOTO3080

4840 IFS$<>"TAKE"THEN5240
4850 IFC$="TAKE"THEN19
4870 IFS$="TAKE"ANDWT=5THENM$="I AM CARRYING TOO MUCH.":GOTO8
4900 IFN$=" METAL"ANDC(1)=O+RTHENZZ=1:GOTO7
4920 IFN$=" PENTAGON"ANDC(2)=O+RTHENZZ=2:GOTO7
4940 IFN$=" CRYSTAL"ANDC(3)=O+RTHENZZ=3:GOTO7
4960 IFN$=" WEAPON"ANDC(4)=O+RTHENZZ=4:GOTO7
4980 IFN$=" BOX"ANDC(5)=O+RTHENZZ=5:GOTO7
5000 IFN$<>" STAR"THEN5040
5001 IFC(6)=115ANDC(1)=1ANDR=15THENZZ=6:GOTO7
5010 IFC(6)=O+RANDR<>15THENZZ=6:GOTO7
5020 IFC(6)=O+RTHENM$="IT'S ATTACHED SOMEHOW. I NEED SOMETHING TO LOOSEN IT.":GOTO8
5040 IFN$=" LASER"ANDC(7)=O+RTHENZZ=7:GOTO7
5060 IFN$=" FLASHLIGHT"THENC(8)=O+RTHENZZ=8:GOTO7
5080 IFN$=" KNIFE"ANDC(9)=O+RTHENZZ=9:GOTO7
5100 IFN$<>" BUCKET"THEN5130
5101 IFC(10)=O+RTHENZZ=10:GOTO7
5110 IFC(15)=O+RTHENZZ=15:GOTO7
5130 IFN$<>" ROBOT"THEN5170
5131 IFC(11)=O+RTHENZZ=11:GOTO7
5140 IFC(13)=O+RTHEN10
5150 IFC(16)=O+RTHENZZ=16:GOTO7
5160 GOTO10
5170 IFN$<>" ROPE"THEN5200
5171 IFC(12)=O+RTHENZZ=12:GOTO7
5180 IFC(12)=0ANDR=22THENM$=A$(8):GOTO8
5200 IFN$<>" WATER"THEN5220
5201 IFR=22ANDC(14)=122ANDC(10)=1THENC(10)=0:ZZ=15:GOTO7
5210 IFR=22ANDC(14)=122THEN10
5220 GOTO17

5240 IFS$<>"QUIT"THEN5290
5260 INPUT"ARE YOU SURE";C$:IFLEFT$(C$,1)="Y"THEN17080
5270 GOTO3000

5290 IFC$<>"KILL ROBOT"THEN5340
5291 IFR=23THENM$=A$(8):GOTO8
5330 GOTO10

5340 IFS$<>"OPEN"THEN5820
5350 IFN$<>"OPEN BOX"THEN5380
5351 IF(C(5)=1ORC(5)=O+R)ANDC(6)=5THENM$="THE CRYSTAL STAR IS IN IT.":GOTO8
5360 IFC(5)=1ORC(5)=O+RTHEN13
5370 GOTO17

5380 IFC$<>"OPEN DOOR"THEN10
5390 IFR=1ORR=11THENM$="THERE IS NO DOOR HERE.":GOTO8
5400 IFR<4THENM$=A$(8):GOTO8
5410 IFR<6ORR=8ORR=9ORR=20THENM$="THERE IS NO DOOR HERE.":GOTO8
5420 IFR<>6THEN5440
5421 IFD(1)=1ANDD(2)=1THEN5600
5430 GOTO18
5440 IFR<>7THEN5460
5441 IFD(2)=0THEN18
5450 GOTO6
5460 IFR<>10THEN5500
5461 IFD(4)=0ANDD(3)=0ANDD(13)=0THEN18
5470 IFD(4)=1THENM$="NORTH ":GOTO6
5480 IFD(3)=1THENM$="SOUTH ":GOTO6
5490 GOTO3000
5500 IFR<>12THEN5520
5501 IFD(3)=0THEN18
5510 GOTO6
5520 IFR<>13THEN5540
5521 IFD(15)=0THEN18
5530 GOTO6
5540 IFR<>14THEN5560
5541 IFD(14)=0THEN18
5550 GOTO6
5560 IFR<>15THEN4610
5561 FORX=14TO17:IFD(X)=0THENPRINTA$(8):X=17:FL=1
5580 NEXTX:IFFL=1THENFL=0:GOTO3000
5600 M$="ALL THE DOORS HERE ARE OPEN.":GOTO8
5610 IFR<>16THEN5630
5611 IFD(16)=0THEN18
5620 GOTO6
5630 IFR<>17THEN5650
5631 IFD(17)=0THEN18
5640 GOTO6
5650 IFR<>18THEN5690
5651 IFD(5)=0ANDD(6)=0THEN18
5660 IFD(5)=1ANDD(6)=1THEN5600
5670 IFD(5)=1ANDD(6)=0THEN5740
5680 GOTO5741
5690 IFR<>19THEN5710
5691 IFD(6)=0THEN18
5700 GOTO6
5710 IFR<>21THEN5750
5711 IFD(7)=0ANDD(5)=0THEN18
5720 IFD(7)=1ANDD(5)=1THEN5600
5730 IFD(7)=1THEN5741
5740 M$="THE SOUTH DOOR IS OPEN. HOW DO I OPEN THE NORTH ONE?":GOTO8
5741 M$="THE NORTH DOOR IS OPEN. HOW DO I OPEN THE SOUTH ONE?":GOTO8

5750 IFR<>22THEN5790
5751 IFD(8)=0ANDD(4)=0THEN18
5760 IFR=22ANDD(8)=1ANDD(4)=1THEN5600
5770 IFR=22ANDD(8)=1THEN5741
5780 GOTO5740

5790 IFR<>23THEN5810
5791 IFD(8)+D(9)+D(7)=3THEN5600
5800 GOTO18
5810 IFR=24THEN6

5820 IFS$<>"LOOK"THEN6570
5830 IFC$="LOOK"THEN50
5840 IFN$=" AIRLOCK"ANDR=1ORR=2THEN6180
5860 IFN$=" SHIP"ANDR=1THENM$="IT IS ENORMOUS AND FIVE-SIDED.":GOTO8
5900 IFN$=" PANEL"THENIFR=2ORR=7ORR=15ORR=24THENM$="6 INCHES ACROSS. THE BUTTONS ARE NOT LABELED.":GOTO8
5930 IFN$=" METAL"THENIFC(1)=1ORC(1)=O+RTHENM$=A$(10):GOTO8
5950 IF(N$=" ROBOT"ORN$=" OBJECT")ANDR=11THENM$=A$(19):GOTO8
5960 IFN$="SLOT"ANDR=24THENM$="7 INCHES WIDE.":GOTO8
5980 IFN$=" PENTAGON"THENIFC(2)=1ORC(2)=O+RTHENM$=A$(11):GOTO8
6010 IFN$=" CRYSTAL"THENIFC(3)=1ORC(3)=O+RTHENM$=A$(12):GOTO8
6030 IFN$=" WEAPON"THENC(4)=1ORC(4)=O+RTHENM$=A$(13):GOTO8
6060 IFN$=" HOLOGRAM"ORN$=" DEVICE"THENM$="I COULD TELL YOU MORE IF I WERE CLOSER.":GOTO8
6070 IFN$<>" PEDESTAL"THEN6130
6071 IFR=7THENM$="5 FEET HIGH. THE CRYSTAL IS ON TOP OF IT.":GOTO8
6080 IFR=5ORR=15THENM$="4 FEET HIGH.":GOSUB1
6090 IFR=5ANDC(5)=105THENM$="THAT IS WHERE THE BOX IS.":GOTO8
6100 IFR=15ANDC(6)=115THENM$="THAT IS WHERE THE STAR IS.":GOTO8
6120 GOTO17
6130 IFN$<>" BOX"THEN6170
6131 IFC(5)=1ORC(5)=O+RTHENM$=A$(14):GOSUB1:IFC(6)=5THENPRINT"THE CRYSTAL STAR IS IN IT."
6150 IFC(5)=1ORC(5)=O+RTHEN3000
6160 GOTO17
6170 IFN$<>" CEILING"THEN6190
6171 IFR=5THENM$="IT'S TOO HIGH FOR ME TO SEE DETAIL.":GOTO8
6180 M$=A$(22):GOTO8
6190 IFN$=" DEVICE"THEN6171
6210 IFN$<>" FOOTPRINTS"ANDN$<>" DUST"THEN6250
6211 IFR=8ORR=9ORR=10THEN6180
6220 GOTO10
6250 IFN$=" STAR"THENIFC(6)=1ORC(6)=O+RTHENM$=A$(15):GOTO8
6290 IFN$=" DOOR"THEN6180
6300 IFN$=" BED"AND(R=12ORR=19)THEN6180
6340 IF(N$=" COMPUTER"ORN$=" CONSOLE"ORN$=" CHAIR")ANDR=15THEN6180
6360 IFN$=" BUTTONS"AND(R=15ORR=2ORR=7)THEN6180
6380 IF(N$=" LASER"AND(C(7)=1ORC(7)=O+R))OR(N$=" FLASHLIGHT"AND(C(8)=1ORC(8)=O+R))THENM$=A$(16):GOTO8
6420 IFN$=" KNIFE"AND(C(9)=O+RORC(9)=1)THENM$=A$(17):GOTO8
6440 IFR=22AND(N$=" COURT"ORN$=" ROPE")THEN6180
6460 IFN$=" POOL"ANDR=22THENM$="IT IS VERY COLD. THE WATER HAS COMPLETELY FROZEN.":GOTO8
6470 IFN$=" POOL"THEN17
6480 IFN$<>" BUCKET"THEN6510
6481 IFC(10)=1ORC(10)=O+RTHENM$=A$(18):GOTO8
6490 IFC(15)=1ORC(15)=O+RTHENM$=A$(18):GOTO8
6500 GOTO17
6510 IFN$<>" ROBOT"THEN6550
6511 IFC(11)=1ORC(11)=O+RTHENM$=A$(19):GOTO8
6520 IFC(13)=1ORC(13)=O+RTHENM$=A$(20):GOTO8
6530 IFC(16)=1ORC(16)=O+RTHENM$="IT IS OUT OF COMMISSION. THE WATER WAS EFFECTIVE.":GOTO8
6540 GOTO17
6550 IFN$=" ROPE"ORN$=" WATER" THENM$=N$+" IS"+N$:GOTO8
6560 GOTO17

6570 IFS$<>"DROP"THEN6760
6590 ZZ=1:IFN$=" METAL"ANDC(1)=1THEN3
6600 ZZ=2:IFN$=" PENTAGON"ANDC(2)=1THEN3
6610 ZZ=3:IFN$=" CRYSTAL"ANDC(3)=1THEN3
6620 ZZ=4:IFN$=" WEAPON"ANDC(4)=1THEN3
6630 ZZ=5:IFN$=" BOX"ANDC(5)=1THEN3
6640 ZZ=6:IFN$=" STAR"ANDC(6)=1THEN3
6650 ZZ=7:IFN$=" LASER"ANDC(7)=1THEN3
6660 ZZ=8:IFN$=" FLASHLIGHT"ANDC(8)=1THEN3
6670 ZZ=9:IFN$=" KNIFE"ANDC(9)=1THEN3
6680 ZZ=10:IFN$=" BUCKET"ANDC(10)=1THEN3
6690 ZZ=15:IFN$=" BUCKET"ANDC(15)=1THEN3
6700 ZZ=11:IFN$=" ROBOT"ANDC(11)=1THEN3
6710 ZZ=12:IFN$=" ROPE"ANDC(12)=1THEN3
6720 ZZ=13:IFN$=" ROBOT"ANDC(13)=1THEN3
6730 IFN$=" WATER"ANDC(15)=1THENC(10)=1:C(15)=0:M$="IT FREEZES IMMEDIATELY.":GOTO3000
6740 ZZ=16:IFN$=" ROBOT"ANDC(16)=1THEN3
6750 GOTO10

6760 IFS$="FIND"THEN18

6770 IFS$<>"MELT"THEN6820
6780 IFN$=" ICE"ANDC(7)=1ANDR=22THENC(14)=O+R:GOTO3000
6790 IFN$<>" ICE"THENM$="I CAN'T MELT "+N$:GOTO8
6800 IFC(7)<>1THEN5220
6810 IFR<>22THEN5220

6820 IFS$="LOCK"THEN5220
6840 GOTO12

REM START 5 LETTER WORDS
6850 S$=LEFT$(C$,5):N$=MID$(C$,6):IFS$<>"PRESS"THEN7050
6880 IFR<>2THEN6960
6890 IFN$=" BUTTON"THEN19
6900 IFN$=" BLACK"ANDAL=0THENAL=1:M$="THE AIRLOCK HAS CLOSED BEHIND ME.":GOTO8
6910 IFN$=" BLACK" AND AL=1 THEN4710
6920 IFN$=" BLUE"THEN16000
6930 IFN$=" GREEN"ANDD(10)=0THENM$="THE DOOR INTO THE SHIP HAS OPENED.":D(10)=1:GOTO8
6940 IFN$=" GREEN" THEN4710
6950 GOTO10
6960 IFR<>7THEN7010
6970 IFN$=" BLUE"ANDD(2)=1THENM$="THE DOOR HAS CLOSED.":D(2)=0:GOTO8
6980 IFN$=" BLACK"OR(N$=" BLUE"ANDD(2)=0)THEN4710
6990 IFN$=" GREEN"THEN16000
7000 GOTO10
7010 IFR=24THEN4710
7020 IFR<>15THEN17
7030 IFLEFT$(N$,5)=" BUTT"ORN$=" PANEL"THEN4710
7040 GOTO10
7050 IFS$="CLIMB"ANDR=22ANDC(12)=0THENPRINT"OKAY.":GOSUB5:GOTO16000
7090 IFC$<>"ENTER"ANDC$<>"ENTER SHIP"THEN7140
7091 IFR=2ANDD(10)=1THENR=3:GOTO50
7100 IFR=2THEN15
7110 IFR=1THENR=2:GOTO50
7130 GOTO10

7140 IFS$<>"LIGHT"THEN7180
7150 IFN$<>" ON"ANDN$<>" OFF"THEN10
7160 IFC(8)<>1THEN17
7170 IFN$=" OFF"THENL=0:GOTO9
7171 L=1:GOTO9

7180 IFC$<>"THROW"THEN7280
7210 IFN$=" WATER"ANDC(15)=1ANDR=23ANDC(13)=123THENM$="IT STOPPED HIM DEAD IN HIS TRACKS.":C(13)=0:C(16)=123:GOTO8
7220 IFN$=" WATER"ANDC(15)=1THEN6590
7230 GOTO10

7280 IFC$="THINK"THENM$="THINK WHAT?:GOTO8
7290 IFC$="THINK OPEN"THEN4480

7300 IFS$<>"SHOOT"THEN7380
7310 IFC$="SHOOT"THEN19
7320 IFC(7)<>1THENM$="I AM NOT CARRYING MY PISTOL.":GOTO8
7330 IFN$=" ROBOT"ANDC(13)=O+RTHENM$="NO EFFECT.":GOTO8
7340 IFN$=" ICE"ANDR=22THENC(14)=O+R:GOTO9
7350 IFN$=" DOOR"ANDR=23ANDD(9)=0ANDC(16)=123THENM$="I CAN GET THROUGH NOW.":D(9)=1:GOTO8
7360 IFN$=" DOOR"ANDR=23ANDC(16)=0THEN16000
7370 GOTO12

7380 IFS$<>"CLICK"THEN12
7381 IFC(2)<>1ORN$<>" PENTAGON"THEN10
7420 IFR=3ANDD(12)=0THENM$="WEST ":D(12)=1:GOTO6
7430 IFR=6ANDD(2)=0THENM$="SOUTH ":D(2)=1:GOTO6
7440 IFR=18ANDD(5)=0THENM$="NORTH ":D(5)=1:GOTO6
7450 IFR<>21THEN7490
7451 IFD(7)=1ANDD(5)=1THEN4710
7460 IFD(5)=0THENM$="SOUTH ":D(5)=1:GOTO6
7470 IFD(7)=0THEND(7)=1:M$="NORTH ":GOTO6
7480 GOTO3000
7490 IFR=22ANDD(8)=0THEND(8)=1:M$="NORTH ":GOTO6
7500 IFR<>23THEN7540 
7501 IFD(8)=1ANDD(7)=1THENM$=A$(21)
7510 IFD(8)=0THEND(8)=1:M$="SOUTHWESTERN ":GOTO6
7520 IFD(7)=0THEND(7)=1:M$="SOUTHEASTERN ":GOTO6
7530 IFR=23THEN3000
7540 IFR=10THEN16000
7550 GOTO4710

REM START 6 LETTER WORDS HERE
7570 S$=LEFT$(C$,6):N$=MID$(C$,7)
7590 IFC$="INSERT"THEN19
7600 IFS$<>"INSERT"THEN7740
7610 IFR=24THEN7670
7620 IFC(5)<>1THENM$="I HAVE NOTHING TO INSERT.":GOTO8
7630 IFN$=" METAL"ORN$=" PENTAGON"ORN$=" WEAPON"THEN10
7640 IFN$=" CRYSTAL"ANDC(3)=1THEN16000
7650 IFN$=" STAR"ANDC(6)=1THENC(6)=5:M$="IT FITS LIKE A GLOVE.":WT=WT-1:GOTO8
7660 GOTO10
7670 IFN$=" BOX"THEN7700
7680 IFN$=" STAR"ANDC(6)=1ANDC(5)=1THENM$="I'LL PUT IT INTO THE BOX. IT FITS NICELY.":WT=WT-1:C(6)=5:GOTO8
7690 GOTO10
7700 IFC(6)=5ANDC(5)=1THEN17000
7710 IFC(5)=1THEN16000
7720 GOTO17
7740 IFS$="UNLOCK"THEN18
7750 IFC$="REMOVE"THEN19
7760 IFC$="REMOVE STAR"THENC(6)=O+R:GOTO3000
7770 IFLEFT$(C$,6)="REMOVE"THEN10
7780 GOTO12

REM START 7 LETTER WORDS HERE
7790 S$=LEFT$(C$,7):N$=MID$(C$,8)
7850 IFS$<>"EXAMINE"THEN7860
7851 C$="LOOK"+N$:GOTO5830
7860 IFS$<>"INVENTO"THEN12

13100 I$="I AM CARRYING: ":FL=0:FORX=1TO16:IFC(X)=1THENI$=I$+O$(X)+", ":FL=1
13190 NEXT:IFFLTHENM$=LEFT$(I$,LEN(I$)-2):FL=0:GOTO8
13210 M$=I$+"NOTHING":FL=0:GOTO8

16000 FORN=1TO10:SOUNDRND(10),1:PRINT"END TRANSMISSION":NEXT
16040 PRINT:M$="IT LOOKS LIKE THE END OF THE SOLAR SYSTEM. TOO BAD...":GOSUB1:GOTO17080

17000 SOUND200,10:CLS
17060 M$="CONGRATULATIONS, PEOPLE OF THE SYSTEM OF THE 9 PLANETS.":GOSUB4
17061 M$="YOU HAVE MANAGED TO DEFUSE THE SHIP. THIS SURPRISES US.":GOSUB4
17070 M$="BUT BEWARE... THE KRENN WILL BE BACK AGAIN...":GOSUB4
17080 INPUT"TRY AGAIN";C$:IFLEFT$(C$,1)="Y"THENRUN
17090 END
