0 CLEAR450:DIMCC,ZZ,M$,I$:O=100:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

3 WT=WT-1:CO(ZZ)=O+R:GOTO13
4 GOSUB1
5 FORZZ=1TO3500:NEXT:RETURN
6 M$="THE "+M$+"DOOR IS OPEN.":GOTO8
7 WT=WT+1:CO(ZZ)=1:GOTO13
8 GOSUB1:GOTO3000
9 PRINT"OKAY.":GOTO50

10 M$=A$(1):GOTO8
12 M$=A$(2)+C$:GOTO8
13 M$=A$(3):GOTO8
14 M$=A$(4):GOTO8
15 M$=A$(5):GOTO8
17 M$=A$(7):GOTO8
18 M$=A$(8):GOTO8
19 M$=A$(9):GOTO8

30 FORX=1TO16:IFCO(X)=O+RTHENFL=1:X=16
32 NEXT:IFFL=0THENRETURN
34 M$="THE FOLLOWING ITEMS ARE IN THIS AREA:":GOSUB1:FORX=1TO16
36 IFCO(X)=O+RTHENM$=CO$(X):GOSUB1
37 NEXT:FL=0:RETURN

REM ROBOT
40 IFCO(13)=0THENRETURN
42 T=T+1:IFT=1THENRETURN
44 IFT=2THENPRINT"THE ROBOT IS ATTACKING ME!":RETURN
45 IFT=3THEN16000

50 RESTORE:FORCC=1TOR:READR$(1),R$(2),N,S,E,W:NEXT
51 T=0:IFR=23THEN63
52 M$=R$(1)
53 IFR=11ANDCO(11)<>105THEN55
54 M$=M$+" "+R$(2)
55 GOSUB1:GOSUB30
56 IFR=23THEN3000
57 I$="":IFN=1THENI$=I$+"NORTH, "
58 IFS=1THENI$=I$+"SOUTH, "
59 IFE=1THENI$=I$+"EAST, "
60 IFW=1THENI$=I$+"WEST, "
61 IFI$<>""THENM$="I CAN GO: "+LEFT$(I$,LEN(I$)-2):GOSUB1
62 GOTO3000
63 IFL=0ORC(8)<>1THENPRINT"IT'S TOO DARK TO SEE!"
64 IFC(8)<>1THEN3000
65 IFL=1THEN52
66 GOSUB5:PRINT"I AM TURNING ON MY FLASHLIGHT.":GOSUB5:L=1:GOTO52

100 CLS:DIMA$(22),CO$(16),CO(16),D(17):GOSUB2000
160 M$="SERIES R ROBOT EXPLORER NUMBER 1S2-X3 CALLING CENTRAL CONTROL...":GOSUB4
162 M$="TURNING COMMAND OVER TO YOU...":GOSUB4:PRINT:GOTO50

REM ROOM 1
310 DATAI AM HANGING ON TO A HAND HOLD AT THE ONLY ENTRANCE TO THE SHIP.
311 DATA"AN OPEN AIRLOCK LEADS INSIDE.",,,,

REM ROOM 2
320 DATAI AM NOW IN THE AIRLOCK. THERE IS A DOOR HERE THAT LEADS INTO THE SHIP.
321 DATA"NEXT TO THE DOOR IS A PANEL OF THREE BUTTONS, BLUE, BLACK, AND GREEN.",,,,

REM ROOM 3
330 DATAI AM IN AN EMPTY ROOM.,"",,1,1,1

REM ROOM 4
340 DATAI AM IN A VERY LARGE ROOM. THERE IS A PENTANGULAR ELECTRONIC DEVICE IN THE CENTRE.
341 DATAITS FUNCTION IS UNCLEAR. TOWARD THE EASTERN WALL THERE IS A VERY LARGE HOLOGRAM.,,1,,1

REM ROOM 5
350 DATAI AM IN A LARGE ROOM WITH A PEDESTAL IN THE CENTRE. 
351 DATASOME SORT OF DEVICE IS PLANTED IN THE CEILING.,1,,,

REM ROOM 6
360 DATATHIS LARGE ROOM HAS NO DISTINGUISHING CHARACTERISTICS.,"",1,1,1,

REM ROOM 7
370 DATATHIS ROOM IS QUITE LARGE. IN THE CENTRE THERE IS A PEDESTAL.
371 DATA"NEXT TO THE DOOR THERE ARE THREE BUTTONS, BLUE, BLACK, AND GREEN.",1,,,

REM ROOM 8
380 DATAI AM IN A LONG PASSAGEWAY.
381 DATASOMETHING LIKE FOOTPRINTS ARE VISIBLE IN THE DUST LEADING NORTH.,1,1,,

REM ROOM 9
390 DATATHE PASSAGE INTERSECTS WITH ANOTHER PASSAGEWAY.,THERE ARE FOOTPRINTS LEADING WEST.,,1,1,1

REM ROOM 10
400 DATATHIS IS THE END OF THE PASSAGE.,THE FOOTPRINTS LEAD TO THE WESTERN DOOR.,1,1,1,1

REM ROOM 11
410 DATAI AM IN A PASSAGEWAY LEADING TO A ROOM SIMILAR TO THE LAST ONE.
411 DATATHERE IS A TWISTED METAL OBJECT IN THE SOUTHERN ROOM.,1,1,,

REM ROOM 12
420 DATATHIS ROOM SEEMS TO BE A CREW'S QUARTERS WITH NUMEROUS PENTANGULAR OBJECTS THAT I PRESUME TO BE BEDS.
421 DATATHERE IS STORAGE SPACE HERE BUT IT HAS BEEN CLEARED OF ALL OBJECTS.,1,,,

REM ROOM 13
430 DATATHIS WOULD SEEM TO BE AN ENGINE ROOM.
431 DATATHERE IS THE USUAL CONFIGURATION OF TUBES ASSOCIATED WITH THE KRANTZ-WARREN HYPERSPACE DRIVE.
433 DATA,,1,

REM ROOM 14
440 DATATHIS WOULD SEEM TO BE AN ENGINE ROOM.
441 DATATHERE IS THE USUAL CONFIGURATION OF TUBES ASSOCIATED WITH THE KRANTZ-WARREN HYPERSPACE DRIVE.
443 DATA,,,1

REM ROOM 15
450 DATA"IT IS THE MAIN CONTROL ROOM, WITH BUTTONED NAVIGATION PANEL, PENTAGONAL CHAIRS WITH CONSOLES AND A PEDESTAL,"
451 DATA"APPARENTLY UNCONNECTED TO THE CONTROL FUNCTIONS. THERE ARE DOORS: NORTHEAST, SOUTHEAST, NORTHWEST SOUTHWEST."
453 DATA,,,

REM ROOM 16
460 DATAI AM IN A PASSAGEWAY TERMINATING AT AN EASTERN DOORWAY.,"",,,1,1

REM ROOM 17 AND 18
470 DATAI AM IN A PASSAGEWAY.,"",,,1,1,I AM IN A PASSAGEWAY.,THERE ARE DOORS NORTH AND SOUTH.,1,1,1,1

REM ROOMS 19
490 DATATHIS ROOM SEEMS TO BE A CREW'S QUARTERS WITH NUMEROUS PENTANGULAR OBJECTS THAT I PRESUME TO BE BEDS.
491 DATATHERE IS STORAGE SPACE HERE BUT IT HAS BEEN CLEARED OF ALL OBJECTS.,1,,,

REM ROOM 20
500 DATATHE END OF THE PASSAGEWAY.,"",,,,1

REM ROOM 21
510 DATA"I AM IN WHAT SEEMS TO BE A KITCHEN. THERE ARE VARIOUS STEEL ALLOY TABLES,"
511 DATA"A SCULLERY, CONVECTION OVENS, AND A LARGE OPEN AND EMPTY CUPBOARD.",1,1,,

REM ROOM 22
520 DATATHIS AREA SEEMS TO BE AN EXERCISE ROOM.
521 DATA"THERE IS A MARKED-OFF BALL(?) COURT, AND A LARGE SWIMMING(?) POOL, FROZEN SOLID.",1,1,,

REM ROOM 23
530 DATATHIS IS AN ENORMOUS ROOM STRETCHING ALL THE WAY TO WHAT MUST BE THE FRONT OF THE SHIP.
531 DATA"THERE ARE THREE DOORS, ONE TO THE NORTH, ONE TO THE SOUTHWEST, AND THE OTHER TO THE SOUTHEAST."
533 DATA1,,,

REM ROOM 24
540 DATAI AM IN A DRAMATICALLY COLOURFUL TRIANGULAR CRYSTAL ROOM.
541 DATAAT THE NORTHERN CORNER IS A CONTROL PANEL WITH A SLOT IN IT.,,1,,

REM SET VARIABLES, HOUSEKEEPING
2000 R=1:A$(1)="PLEASE CLARIFY.":A$(2)="I CANNOT RESPOND TO THE COMMAND ":A$(3)="OKAY."
2060 A$(4)="I CANNOT GO IN THAT DIRECTION.":A$(5)="THERE IS A LOCKED DOOR IN THAT DIRECTION."
2080 A$(6)="I CANNOT DO THAT AT THIS TIME.":A$(7)="THAT ITEM IS NOT HERE.":A$(8)="TELL ME HOW."
2110 A$(9)="COULD YOU BE MORE SPECIFIC?":A$(10)="IT SEEMS TO BE SOME SORT OF MACHINE PART. IT FITS EASILY IN MY HAND."
2130 A$(11)="IT LOOKS LIKE A CHILD'S CLICKING TOY."
2140 A$(12)="THE LIGHT PLAYS AROUND IN IT AS IF IT HAS A LIFE OF ITS OWN."
2150 A$(13)="IT IS NOT FIVE-SIDED. IT APPEARS TO BE NON-FUNCTIONAL."
2160 A$(14)="IT OPENS EASILY.":A$(15)="IT IS OF UNKNOWN CRYSTALLINE STRUCTURE."
2180 A$(16)="IT IS THE STANDARD ISSUE.":A$(17)="SHARP CUTTING EDGE."
2200 A$(18)="IT APPEARS TO BE MADE OF AN ALUMINUM-TYPE ALLOY."
2210 A$(19)="IT IS HUMANOID IN SHAPE AND APPEARS TO HAVE BEEN BLASTED BY A HEAT WEAPON."
2220 A$(20)="IT IS SPIDER LIKE AND DEFINITELY LETHAL."
2230 A$(21)="NOTHING HAPPENS.":A$(22)="I DON'T SEE ANYTHING REMARKABLE."
2420 L=0:WT=2:RETURN

2260 CO$(1)="ODD PIECE OF METAL":CO(1)=119:CO$(2)="SMALL LEAD PENTAGON":CO(2)=104
2280 CO$(3)="CRYSTAL PENTAGON":CO(3)=107:CO$(4)="UNIDENTIFIED WEAPON":CO(4)=121
2300 CO$(5)="PENTAGONAL BOX":CO(5)=105:CO$(6)="CRYSTAL STAR":CO(6)=0
2320 CO$(7)="LASER PISTOL":CO(7)=1:CO$(8)="FLASHLIGHT":CO(8)=1:CO$(9)="LARGE BLADE KNIFE":CO(9)=121
2350 CO$(10)="METAL BUCKET":CO(10)=121:CO$(11)="RUINED ALIEN ROBOT":CO(11)=105
2370 CO$(12)="STRONG HEMP ROPE":CO(12)=0:CO$(13)="GIANT FIVE-LEGGED ROBOT":CO(13)=123
2390 CO$(14)="SMALL AREA OF WATER AT EDGE OF ICE":CO(14)=0:CO$(15)="FILLED BUCKET":CO(15)=0
2410 CO$(16)="DEACTIVATED ROBOT":CO(16)=0

REM CONWCOM
3000 IFR=23THENGOSUB40
3040 PRINT:INPUT"WHAT SHOULD I DO";C$:IFC$=""THEN10
3070 IFLEN(C$)=1THEN3180
3080 CC=LEN(C$):FORZZ=1TO9:IFMID$(C$,ZZ+1,1)=" "THENCC=ZZ
3081 NEXT:IFLEN(C$)=2THEN12
3090 IFCC=2THEN3840
3100 IFCC=3THEN4390
3110 IFCC=4THEN4790
3120 IFCC=5THEN6850
3130 IFCC=6THEN7570
3140 IFCC=7THEN7790
3150 IFCC=8THEN7870
3160 IFCC=9THEN7890
3170 GOTO12

3180 IFC$="S"THEN3470
3190 IFC$="N"THEN3230
3200 IFC$="W"THEN3620
3210 IFC$="E"THEN3730
3220 GOTO10

3230 IFR<3THEN10
3240 IFN<>1THEN14
3250 IFR=5THENR=11:GOTO9
3260 IFR=6ANDD(1)=0THEN15
3270 IFR=6ANDD(1)=1THENR=8:GOTO9
3280 IFR=7ANDD(2)=1THENR=6:GOTO9
3290 IFR=7ANDD(2)=0THEN15
3300 IFR=8THENR=9:GOTO9
3310 IFR=10ANDD(4)=1THENR=22:GOTO9
3320 IFR=10THEN15
3330 IFR=11THENR=4:GOTO9
3340 IFR=12ANDD(3)=1THENR=10:GOTO9
3350 IFR=12THEN15
3360 IFR=18ANDD(5)=1THENR=21:GOTO9
3370 IFR=18THEN15
3380 IFR=19ANDD(6)=1THENR=18:GOTO9
3390 IFR=19THEN15
3400 IFR=21ANDD(7)=1THENR=23:GOTO9
3410 IFR=21THEN15
3420 IFR=22ANDD(8)=0THEN15
3430 IFR=22THENR=23:GOTO9
3440 IFR=23ANDD(9)=1THENR=24:GOTO9
3450 IFR=23THEN15
3460 GOTO10

3470 IFR<3THEN10
3480 IFS<>1THEN14
3490 IFR=3ANDD(10)=1THENR=2:GOTO9
3500 IFR=4THENR=11:GOTO9
3510 IFR=11ANDD(11)=1THENR=5:GOTO9
3520 IFR=11THEN16000
3530 IFR=6ANDD(2)=1THENR=7:GOTO9
3540 IFR=8ANDD(1)=1THENR=6:GOTO9
3550 IFR=9THENR=8:GOTO9
3560 IFR=10ANDD(3)=1THENR=12:GOTO9
3570 IFR=18ANDD(6)=1THENR=19:GOTO9
3580 IFR=21ANDD(5)=1THENR=18:GOTO9
3590 IFR=22ANDD(4)=1THENR=10:GOTO9
3600 IFR=24THENR=23:GOTO9
3610 GOTO15

3620 IFR<3THEN10
3630 IFW<>1THEN14
3640 IFR=3ANDD(12)THENR=6:GOTO9
3650 IFR=4THENR=3:GOTO9
3660 IFR=9THENR=10:GOTO9
3670 IFR=14ANDD(14)=1THENR=15:GOTO9
3680 IFR=16THENR=9:GOTO9
3690 IFR=17ANDD(17)=1THENR=15:GOTO9
3700 IFR=18THENR=17:GOTO9
3710 IFR=20THENR=18:GOTO9
3720 GOTO15

3730 IFR<3THEN10
3740 IFE<>1THEN14
3750 IFR=3THENR=4:GOTO9
3760 IFR=6ANDD(12)=1THENR=3:GOTO9
3770 IFR=9THENR=16:GOTO9
3780 IFR=10THENR=9:GOTO9
3790 IFR=13ANDD(15)=1THENR=15:GOTO9
3800 IFR=16ANDD(16)=1THENR=15:GOTO9
3810 IFR=17THENR=18:GOTO9
3820 IFR=18THENR=20:GOTO9
3830 GOTO15

REM 2 LETTER 1ST WORDS
3840 IFC$="GO"THEN10
3860 IFLEFT$(C$,2)<>"GO"THEN12
3870 S2$=MID$(C$,3)
3880 IFS2$=" NORTH"THEN3230
3890 IFS2$=" SOUTH"THEN3470
3900 IFS2$=" WEST"THEN3620
3910 IFS2$=" EAST"THEN3730
3920 IFR<>23THEN3970
3930 IFS2$<>" SOUTHEAST"THEN3970
3931 IFD(8)=1THENR=22:GOTO9
3950 IFD(7)=1THENR=21:GOTO9
3960 GOTO15
3970 IFR<>15THEN4030
3980 IFS2$=" NORTHEAST"ANDD(17)=1THENR=17:GOTO9
3990 IFS2$=" SOUTHEAST"ANDD(14)=1THENR=14:GOTO9
4000 IFS2$=" NORTHWEST"ANDD(16)=1THENR=16:GOTO9
4010 IFS2$=" SOUTHWEST"ANDD(15)=1THENR=13:GOTO9
4020 GOTO15
4030 IFC$="GO PANEL"THENM$="THERE ARE NUMEROUS CONTROLS, BUT NONE OF THEM SEEM TO BE OPERATIVE.":GOTO8

4040 IFC$<>"GO PEDESTAL"THEN4060
4041 IFCO(6)<>0ANDCO(6)<>115THEN13
4050 IFCO(6)=115:M$="THERE'S SOMETHING ON TOP OF THE PEDESTAL.":GOTO8

4060 IFC$<>"GO HOLOGRAM"THEN4080
4061 IFR<>4THEN17
4065 M$="IT IS SOME SORT OF MAP, BUT IT IS INCOMPREHENSIBLE.":GOSUB1
4066 IFCO(3)=1THENM$="I AM RECEIVING A STRANGE SUBVERBAL MESSAGE. IT SAYS 'THINK, EARTHLING.'":GOSUB1
4070 GOTO3000

4080 IFS2$<>" PANEL"THEN4110
4081 IFR=2ORR=7ORR=24THEN13
4100 GOTO17

4110 IFS2$<>" AIRLOCK"ANDS2$<>" DOOR"ANDS2$<>" SHIP"THEN4180
4111 IFR<>1THEN4150
4141 R=2:GOTO50
4150 IFR<>2THEN10
4151 IFD(10)=1THENR=3:GOTO50
4160 GOTO15

4180 IFLEFT$(S2$,4)<>" PED"THEN4200
4181 IFCO(3)=107ANDR=7THENM$="OKAY. THAT IS WHERE THE CRYSTAL IS.":GOTO8
4190 IFR=7THEN13

4200 IFS2$=" BED"THENM$="ROBOTS HAVE NO NEED OF SLEEP.":GOTO8

4250 IFS2$=" COURT"ORS2$=" ROPE"ORS2$=" POOL"THEN4260
4251 GOTO4280
4260 IFR=22THEN13
4270 GOTO17

4280 IFC$<>"GO DEVICE"THEN19
4290 IFR=5THENM$="IT IS INACCESSIBLE.":GOTO8
4300 IFR<>4THEN17
4310 M$="IT IS SOME SORT OF TRANSMITTING DEVICE. IT IS SENDING A MESSAGE IN CODE.":GOSUB4
4320 M$="THE CONTROL COMPUTER SHOULD BE ABLE TO TRANSLATE IT...":GOSUB4
4330 M$="PEOPLE OF THE SYSTEM OF THE 9 PLANETS: THIS IS A MESSAGE FROM THE FEDERATION OF KRENN.":GOSUB4
4340 M$="THIS SHIP IS A NUCLEAR EXPLOSIVE DEVICE. IT IS AIMED DIRECTLY AT YOUR HOME PLANET.":GOSUB4
4350 M$="NOW THAT YOU HAVE DEVELOPED FASTER THAN LIGHT TECHNOLOGY, YOU ARE A THREAT.":GOTO8

REM THREE LETTER WORDS
4390 IFLEFT$(C$,3)="GET"THENC$="TAKE"+MID$(C$,4):GOTO4790
4400 IFLEFT$(C$,3)<>"TIE"THEN4460
4401 IFCO(12)=1THEN10
4450 M$=A$(8):GOTO8
4460 IFC$="SAY"THEN10
4470 IFC$<>"SAY OPEN"THEN4720
4480 PRINT"OPEN"
4490 IFCO(3)<>1THEN4710
4500 IPR=6ANDD(1)=0THEND(1)=1:M$="NORTHERN ":GOTO6
4510 IFR=7ANDD(2)=0THEND(2)=1:M$="NORTH ":GOTO6
4520 IFR=11THEND(11)=1:M$="I AM WILLING TO GO IN IF YOU THINK IT'S ADVISABLE.":GOTO8
4530 IFR<>10THEN4570
4531 IFD(3)=1ANDD(4)=1THEN4710
4540 IFD(4)=0THEND(4)=1:M$="NORTH ":GOTO6
4550 IFD(3)=0THEND(3)=1:M$="SOUTH ":GOTO6
4560 GOTO3000
4570 IFR=22ANDD(4)=0THEND(4)=1:M$="SOUTH ":GOTO6
4580 IFR=16ANDD(16)=0THEND(16)=1:M$="EAST ":GOTO6
4590 IFR=17THENFORX=14TO17:D(X)=1:NEXTX:GOTO5600
4600 IFR=13ANDD(15)=0THEND(15)=1:GOTO6
4610 IFR=14ANDD(14)=0THEND(14)=1:GOTO6
4620 IFR<>15THEN4680
4630 IFD(16)=0THEND(16)=1:M$="NORTHWEST "
4640 IFD(15)=0THEND(15)=1:M$="SOUTHNEST "
4650 IFD(17)=0THEND(17)=1:M$="NORTHEAST "
4660 IFD(14)=0THEND(14)=1:M$="SOUTHEAST "
4670 GOTO8
4680 IFR=17ANDD(17)=0THEND(17)=1:M$="WEST ":GOTO6
4690 IFR=18ANDD(6)=0THEND(6)=1:M$="SOUTH ":GOTO6
4700 IFR=19ANDD(6)=0THEND(6)=1:GOTO6
4710 M$=A$(21):GOTO8

4720 IFC$<>"CUT ROPE"THEN4760
4730 IFR=22ANDCO(12)=0ANDCO(9)=1THENCO(12)=122:GOTO13
4740 IFCO(9)<>1THEN5220
4750 IFCO(12)<>O+ROR(R<>22ANDCO(12)=0)ORCO(12)<>1THEN10
4760 GOTO12

REM 4 LETTER WORDS
4790 S$=LEFT$(C$,4):S2$=MID$(C$,5):IFS$="READ"THEN12
4830 IFS$="PUSH"THENC$="PRESS"+S2$:GOTO3080

4840 IFS$<>"TAKE"THEN5240
4850 IFC$="TAKE"THEN19
4870 IFS$="TAKE"ANDWT=5THENM$="I AM CARRYING TOO MUCH.":GOTO8

4900 IFS2$<>" METAL"THEN4920
4901 IFCO(1)=O+RTHENZZ=1:GOTO7
4910 GOTO17

4920 IFS2$<>" PENTAGON"THEN4940
4921 IFCO(2)=O+RTHENZZ=2:GOTO7
4930 GOTO17

4940 IFS2$<>" CRYSTAL"THEN4960
4941 IFCO(3)=O+RTHENZZ=3:GOTO7
4950 GOTO17

4960 IFS2$<>" WEAPON"THEN4980
4961 IFCO(4)=O+RTHENZZ=4:GOTO7
4970 GOTO17

4980 IFS2$<>" BOX"THEN5000
4981 IFCO(5)=O+RTHENZZ=5:GOTO7
4990 GOTO17

5000 IFS2$<>" STAR"THEN5040
5001 IFCO(6)=115ANDCO(1)=1ANDR=15THENZZ=6:GOTO7
5010 IFCO(6)=O+RANDR<>15THENZZ=6:GOTO7
5020 IFCO(6)=O+RTHENM$="IT'S ATTACHED SOMEHOW. I NEED SOMETHING TO LOOSEN IT.":GOTO8
5030 GOTO17

5040 IFS2$<>" LASER"THEN5060
5041 IFCO(7)=O+RTHENZZ=7:GOTO7
5050 GOTO17

5060 IFS2$<>" FLASHLIGHT"THEN5080
5061 IFCO(8)=O+RTHENZZ=8:GOTO7
5070 GOTO17

5080 IFS2$<>" KNIFE"THEN5100
5081 IFCO(9)=O+RTHENZZ=9:GOTO7
5090 GOTO17

5100 IFS2$<>" BUCKET"THEN5130
5101 IFCO(10)=O+RTHENZZ=10:GOTO7
5110 IFCO(15)=O+RTHENZZ=15:GOTO7
5120 GOTO17

5130 IFS2$<>" ROBOT"THEN5170
5131 IFCO(11)=O+RTHENZZ=11:GOTO7
5140 IFCO(13)=O+RTHEN10
5150 IFCO(16)=O+RTHENZZ=16:GOTO7
5160 GOTO10

5170 IFS2$<>" ROPE"THEN5200
5171 IFCO(12)=O+RTHENZZ=12:GOTO7
5180 IFCO(12)=0ANDR=22THENM$=A$(8):GOTO8
5190 GOTO17

5200 IFS2$<>" WATER"THEN5240
5201 IFR=22ANDCO(14)=122ANDCO(10)=1THENCO(10)=0:ZZ=15:GOTO7
5210 IFR=22ANDCO(14)=122THEN10
5220 M$=A$(6):GOTO8

5240 IFS$<>"QUIT"THEN5290
5260 INPUT"ARE YOU SURE";C$:IFLEFT$(C$,1)="Y"THENEND
5270 GOTO5550

5290 IFC$<>"KILL ROBOT"THEN5340
5291 IFR=23THENM$=A$(8):GOTO8
5330 GOTO10

5340 IFS$<>"OPEN"THEN5820
5350 IFS2$<>"OPEN BOX"THEN5380
5351 IF(CO(5)=1ORCO(5)=O+R)ANDCO(6)=5THENM$="THE CRYSTAL STAR IS IN IT.":GOTO8
5360 IFCO(5)=1ORCO(5)=O+RTHEN13
5370 GOTO17

5380 IFC$<>"OPEN DOOR"THEN10
5390 IFR=1ORR=11THENM$="THERE IS NO DOOR HERE.":GOTO8
5400 IFR<4THENM$=A$(8):GOTO8
5410 IFR<6ORR=8ORR=9ORR=20THENM$="THERE IS NO DOOR HERE.":GOTO8

5420 IFR<>6THEN5440
5421 IFD(1)=1ANDD(2)=1THEN5600
5430 GOTO18

5440 IFR<>7THEN5460
5441 IFD(2)=0THEN18
5450 GOTO6

5460 IFR<>10THEN5500
5461 IFD(4)=0ANDD(3)=0ANDD(13)=0THEN18
5470 IFD(4)=1THENM$="NORTH ":GOTO6
5480 IFD(3)=1THENM$="SOUTH ":GOTO6
5490 GOTO3000

5500 IFR<>12THEN5520
5501 IFD(3)=0THEN18
5510 GOTO6

5520 IFR<>13THEN5540
5521 IFD(15)=0THEN18
5530 GOTO6

5540 IFR<>14THEN5560
5541 IFD(14)=0THEN18
5550 GOTO6

5560 IFR<>15THEN4610
5561 FORX=14TO17:IFD(X)=0THENPRINTA$(8):X=17:FL=1
5580 NEXTX:IFFL=1THENFL=0:GOTO3000
5600 M$="ALL THE DOORS HERE ARE OPEN.":GOTO8

5610 IFR<>16THEN5630
5611 IFD(16)=0THEN18
5620 GOTO6

5630 IFR<>17THEN5650
5631 IFD(17)=0THEN18
5640 GOTO6

5650 IFR<>18THEN5690
5651 IFD(5)=0ANDD(6)=0THEN18
5660 IFD(5)=1ANDD(6)=1THEN5600
5670 IFD(5)=1ANDD(6)=0THEN5740
5680 GOTO5741

5690 IFR<>19THEN5710
5691 IFD(6)=0THEN18
5700 GOTO6

5710 IFR<>21THEN5750
5711 IFD(7)=0ANDD(5)=0THEN18
5720 IFD(7)=1ANDD(5)=1THEN5600
5730 IFD(7)=1THEN5741
5740 M$="THE SOUTH DOOR IS OPEN. HOW DO I OPEN THE NORTH ONE?":GOTO8
5741 M$="THE NORTH DOOR IS OPEN. HOW DO I OPEN THE SOUTH ONE?":GOTO8

5750 IFR<>22THEN5790
5751 IFD(8)=0ANDD(4)=0THEN18
5760 IFR=22ANDD(8)=1ANDD(4)=1THEN5600
5770 IFR=22ANDD(8)=1THEN5741
5780 GOTO5740

5790 IFR<>23THEN5810
5791 IFD(8)+D(9)+D(7)=3THEN5600
5800 GOTO18
5810 IFR=24THEN6

5820 IFS$<>"LOOK"THEN6570
5830 IFC$="LOOK"THEN50

5840 IFS2$<>" AIRLOCK"THEN5860
5841 IFR=1ORR=2THEN6180
5850 GOTO17

5860 IFS2$<>" SHIP"THEN5900
5861 IFR=1THENM$="IT IS ENORMOUS AND FIVE-SIDED.":GOTO8
5890 GOTO17
5900 IFS2$<>" PANEL"THEN5930
5901 IFR=2ORR=7ORR=15ORR=24THENM$="6 INCHES ACROSS. THE BUTTONS ARE NOT LABELED.":GOTO8
5920 GOTO17

5930 IFS2$<>" METAL"5950
5931 IFCO(1)=1ORCO(1)=O+RTHENM$=A$(10):GOTO8
5940 GOTO17

5950 IF(S2$=" ROBOT"ANDR=11)ORS2$=" OBJECT"THENM$="I HAVE NOTHING NEW TO ADD.":GOTO8

5960 IFS2$<>"SLOT"THEN5980
5961 IFR=24THENM$="7 INCHES WIDE.":GOTO8
5970 GOTO17

5980 IFS2$<>" PENTAGON"6010
5981 IFCO(2)=1ORCO(2)=O+RTHENM$=A$(11):GOTO8
6000 GOTO17

6010 IFS2$<>" CRYSTAL"THEN6030
6011 IFCO(3)=1ORCO(3)=O+RTHENM$=A$(12):GOTO8
6020 GOTO17

6030 IFS2$<>" WEAPON"THEN6060
6031 IFCO(4)=1ORCO(4)=O+RTHENM$=A$(13):GOTO8
6050 GOTO17

6060 IFS2$=" HOLOGRAM"ORS2$=" DEVICE"THENM$="I COULD TELL YOU MORE IF I WERE CLOSER.":GOTO8
6070 IFS2$<>" PEDESTAL"THEN6130
6071 IFR=7THENM$="5 FEET HIGH. THE CRYSTAL IS ON TOP OF IT.":GOTO8
6080 IFR=5ORR=15THENM$="4 FEET HIGH.":GOSUB1
6090 IFR=5ANDCO(5)=105THENM$="THAT IS WHERE THE BOX IS.":GOTO8
6100 IFR=15ANDCO(6)=115THENM$="THAT IS WHERE THE STAR IS.":GOTO8
6120 GOTO17

6130 IFS2$<>" BOX"THEN6170
6131 IFCO(5)=1ORCO(5)=O+RTHENM$=A$(14):GOSUB1:IFCO(6)=5THENPRINT"THE CRYSTAL STAR IS IN IT."
6150 IFCO(5)=1ORCO(5)=O+RTHEN3000
6160 GOTO17

6170 IFS2$<>" CEILING"THEN6190
6171 IFR=5THENM$="IT'S TOO HIGH FOR ME TO SEE DETAIL.":GOTO8
6180 M$=A$(22):GOTO8

6190 IFS2$=" DEVICE"THEN6171

6210 IFS2$<>" FOOTPRINTS"ANDS2$<>" DUST"THEN6250
6211 IFR=8ORR=9ORR=10THEN6180
6220 GOTO10

6250 IFS2$<>" STAR"THEN6290
6251 IFCO(6)=1ORCO(6)=O+RTHENM$=A$(15):GOTO8
6280 GOTO17

6290 IFS2$=" DOOR"THEN6180
6300 IFS2$=" BED"AND(R=12ORR=19)THEN6180
6340 IF(S2$=" COMPUTER"ORS2$=" CONSOLE"ORS2$=" CHAIR")ANDR=15THEN6180
6360 IFS2$=" BUTTONS"AND(R=15ORR=2ORR=7)THEN6180
6380 IF(S2$=" LASER"AND(CO(7)=1ORCO(7)=O+R))OR(S2$=" FLASHLIGHT"AND(CO(8)=1ORCO(8)=O+R))THENM$=A$(16):GOTO8
6420 IFS2$=" KNIFE"AND(CO(9)=O+RORCO(9)=1)THENM$=A$(17):GOTO8
6440 IFR=22AND(S2$=" COURT"ORS2$=" ROPE")THEN6180
6460 IFS2$=" POOL"ANDR=22THENM$="IT IS VERY COLD. THE WATER HAS COMPLETELY FROZEN.":GOTO8
6470 IFS2$=" POOL" THEN17
6480 IFS2$<>" BUCKET"THEN6510
6481 IFCO(10)=1ORCO(10)=O+RTHENM$=A$(18):GOTO8
6490 IFCO(15)=1ORCO(15)=O+RTHENM$=A$(18):GOTO8
6500 GOTO17
6510 IFS2$<>" ROBOT"THEN6550
6551 IFCO(11)=1ORCO(11)=O+RTHENM$=A$(19):GOTO8
6520 IFCO(13)=1ORCO(13)=O+RTHENM$=A$(20):GOTO8
6530 IFCO(16)=1ORCO(16)=O+RTHENM$="IT IS OUT OF COMMISSION. THE WATER WAS EFFECTIVE.":GOTO8
6540 GOTO17
6550 IFS2$=" ROPE"ORS2$=" WATER" THENM$=S2$+" IS"+S2$:GOTO8
6560 GOTO12

6570 IFS$<>"DROP"THEN6760
6590 ZZ=1:IFS2$=" METAL"ANDCO(1)=1THEN3
6600 ZZ=2:IFS2$=" PENTAGON"ANDCO(2)=1THEN3
6610 ZZ=3:IFS2$=" CRYSTAL"ANDCO(3)=1THEN3
6620 ZZ=4:IFS2$=" WEAPON"ANDCO(4)=1THEN3
6630 ZZ=5:IFS2$=" BOX"ANDCO(5)=1THEN3
6640 ZZ=6:IFS2$=" STAR"ANDCO(6)=1THEN3
6650 ZZ=7:IFS2$=" LASER"ANDCO(7)=1THEN3
6660 ZZ=8:IFS2$=" FLASHLIGHT"ANDCO(8)=1THEN3
6670 ZZ=9:IFS2$=" KNIFE"ANDCO(9)=1THEN3
6680 ZZ=10:IFS2$=" BUCKET"ANDCO(10)=1THEN3
6690 ZZ=15:IFS2$=" BUCKET"ANDCO(15)=1THEN3
6700 ZZ=11:IFS2$=" ROBOT"ANDCO(11)=1THEN3
6710 ZZ=12:IFS2$=" ROPE"ANDCO(12)=1THEN3
6720 ZZ=13:IFS2$=" ROBOT"ANDCO(13)=1THEN3
6730 IFS2$=" WATER"ANDCO(15)=1THENCO(10)=1:CO(15)=0:M$="IT FREEZES IMMEDIATELY.":GOTO3000
6740 ZZ=16:IFS2$=" ROBOT"ANDCO(16)=1THEN3
6750 GOTO10

6760 IFS$="FIND"THEN18

6770 IFS$<>"MELT"THEN6820
6780 IFS2$=" ICE"ANDCO(7)=1ANDR=22THENCO(14)=O+R:GOTO3000
6790 IFS2$<>" ICE"THENM$="I CAN'T MELT "+S2$:GOTO8
6800 IFCO(7)<>1THEN5220
6810 IFR<>22THEN5220

6820 IFS$="LOCK"THEN5220
6840 GOTO12

REM START 5 LETTER WORDS
6850 S$=LEFT$(C$,5):S2$=MID$(C$,6):IFS$<>"PRESS"THEN7050
6880 IFR<>2THEN6960
6890 IFS2$=" BUTTON"THEN19
6900 IFS2$=" BLACK"ANDAL=0THENAL=1:M$="THE AIRLOCK HAS CLOSED BEHIND ME.":GOTO8
6910 IFS2$=" BLACK" AND AL=1 THEN4710
6920 IFS2$=" BLUE"THEN16000
6930 IFS2$=" GREEN"ANDD(10)=0THEND(10)=1:M$="THE DOOR INTO THE SHIP HAS OPENED.":GOTO8
6940 IFS2$=" GREEN" THEN4710
6950 GOTO10
6960 IFR<>7THEN7010
6970 IFS2$=" BLUE"ANDD(2)=1THEND(2)=0:M$="THE DOOR HAS CLOSED.":GOTO8
6980 IFS2$=" BLACK"OR(S2$=" BLUE"ANDD(2)=0)THEN4710
6990 IFS2$=" GREEN"THEN16000
7000 GOTO10
7010 IFR=24THEN4710
7020 IFR<>15THEN17
7030 IFLEFT$(S2$,5)=" BUTT"ORS2$=" PANEL"THEN4710
7040 GOTO10
7050 IFS$="CLIMB"ANDR=22ANDCO(12)=0THENPRINT"OKAY.":GOSUB5:GOTO16000
7090 IFC$<>"ENTER"ANDC$<>"ENTER SHIP"THEN7140
7091 IFR=2ANDD(10)=1THENR=3:GOTO50
7100 IFR=2THEN15
7110 IFR=1THENR=2:GOTO50
7130 GOTO10

7140 IFS$<>"LIGHT"THEN7180
7150 IFS2$<>" ON"ANDS2$<>" OFF"THEN10
7160 IFCO(8)<>1THEN17
7170 IFS2$=" OFF"THENL=0:GOTO9
7171 L=1:GOTO9

7180 IFC$<>"THROW"THEN7240
7210 IFS2$=" WATER"ANDCO(15)=1ANDR=23ANDCO(13)=123THENM$="IT STOPPED HIM DEAD IN HIS TRACKS.":CO(13)=0:CO(16)=123:GOTO8
7220 IFS2$=" WATER"ANDCO(15)=1THEN6590
7230 GOTO10

7240 IFS$<>"SHOOT"THEN7380
7310 IFC$="SHOOT"THEN19
7320 IFCO(7)<>1THENM$="I AM NOT CARRYING MY PISTOL.":GOTO8
7330 IFS2$=" ROBOT"ANDCO(13)=O+RTHENM$="NO EFFECT.":GOTO8
7340 IFS2$=" ICE"ANDR=22THENCO(14)=O+R:GOTO9
7350 IFS2$=" DOOR"ANDR=23ANDD(9)=0ANDCO(16)=123THENM$="I CAN GET THROUGH NOW.":D(9)=1:GOTO8
7360 IFS2$=" DOOR"ANDR=23ANDCO(16)=0THEN16000
7370 GOTO12

7380 IFS$<>"CLICK"THEN12
7381 IFCO(2)<>1ORS2$<>" PENTAGON"THEN10
7420 IFR=3ANDD(12)=0THEND(12)=1:M$="WEST ":GOTO6
7430 IFR=6ANDD(2)=0THEND(2)=1:M$="SOUTH ":GOTO6
7440 IFR=18ANDD(5)=0THEND(5)=1:M$="NORTH ":GOTO6
7450 IFR<>21THEN7490
7451 IFD(7)=1ANDD(5)=1THEN4710
7460 IFD(5)=0THEND(5)=1:M$="SOUTH ":GOTO6
7470 IFD(7)=0THEND(7)=1:M$="NORTH ":GOTO6
7480 GOTO3000
7490 IFR=22ANDD(8)=0THEND(8)=1:M$="NORTH ":GOTO6
7500 IFR<>23THEN7540 
7501 IFD(8)=1ANDD(7)=1THENM$=A$(21)
7510 IFD(8)=0THEND(8)=1:M$="SOUTHWESTERN ":GOTO6
7520 IFD(7)=0THEND(7)=1:M$="SOUTHEASTERN ":GOTO6
7530 IFR=23THEN3000
7540 IFR=10THEN16000
7550 GOTO4710

REM START 6 LETTER WORDS HERE
7570 S$=LEFT$(C$,6):S2$=MID$(C$,7)
7590 IFC$="INSERT"THEN19
7600 IFS$<>"INSERT"THEN7740
7610 IFR=24THEN7670
7620 IFCO(5)<>1THENM$="I HAVE NOTHING TO INSERT.":GOTO8
7630 IFS2$=" METAL"ORS2$=" PENTAGON"ORS2$=" WEAPON"THEN10
7640 IFS2$=" CRYSTAL"ANDCO(3)=1THEN16000
7650 IFS2$=" STAR"ANDCO(6)=1THENCO(6)=5:M$="IT FITS LIKE A GLOVE.":WT=WT-1:GOTO8
7660 GOTO10
7670 IFS2$=" BOX"THEN7700
7680 IFS2$=" STAR"ANDCO(6)=1ANDCO(5)=1THENM$="I'LL PUT IT INTO THE BOX. IT FITS NICELY.":WT=WT-1:CO(6)=5:GOTO8
7690 GOTO10
7700 IFCO(6)=5ANDCO(5)=1THEN17000
7710 IFCO(5)=1THEN16000
7720 GOTO17
7740 IFS$="UNLOCK"THEN3000
7750 IFC$="REMOVE"THEN3000
7760 IFC$="REMOVE STAR"THENCO(6)=O+R:GOTO3000
7770 IFLEFT$(C$,6)="REMOVE"THEN10
7780 GOTO12

REM START 7 LETTER WORDS HERE
7790 S$=LEFT$(C$,7):S2$=MID$(C$,8)
7850 IFS$<>"EXAMINE"THEN13100
7851 C$="LOOK"+S2$:GOTO5830
7860 GOTO12

13100 FORX=1TO16:IFCO(X)=1THENFL=1:X=16
13120 NEXT:IFFL=1THEN13160
13140 PRINT"I AM NOT CARRYING ANYTHING."
13150 GOTO3000
13160 PRINT"I AM CARRYING:":I$="":FORX=1TO16
13180 IFCO(X)=1THENI$=I$+CO$(X)+", "
13190 NEXT:M$=LEFT$(I$,LEN(I$)-2):GOSUB1
13210 GOTO3000

16000 FORN=1TO10:SOUNDRND(10),1:PRINT"END TRANSMISSION":NEXT
16040 PRINT:M$="IT LOOKS LIKE THE END OF THE SOLAR SYSTEM. TOO BAD...":GOSUB1:END

17000 SOUND200,10:CLS
17060 M$="CONGRATULATIONS, PEOPLE OF THE SYSTEM OF THE 9 PLANETS.":GOSUB4
17061 M$="YOU HAVE MANAGED TO DEFUSE THE SHIP. THIS SURPRISES US.":GOSUB4
17070 M$="BUT BEWARE... THE KRENN WILL BE BACK AGAIN...":GOSUB4:END
