0 CLS:CLEAR1100:DIMC1,C2,M$,I$:GOTO100
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 C1=LEN(I$):IFLEFT$(R$,C1+1)=I$+" "THENR$=M$+MID$(R$,C1+1)
4 RETURN
5 IFLEN(R$)<>1THENRETURN
6 FORC1=1TO4:IFR$=LEFT$(D$(C1),1)THENR$="GO "+D$(C1)
7 NEXT:RETURN
8 GOSUB1:RETURN
9 FORC1=1TO2500:NEXT:RETURN
100 DIMVA$(30),CA$(10),D$(4),CA(10),RD$(10),RE(10,4),OA$(75),OD$(75),OM(75),OL(75),OV(75),OT(75),LI(10)
110 PRINT,,TAB(5)"ŽˆŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ„":PRINTTAB(5)"‹ƒ€€€€€€€€€€€€€€€€€Ø‰":PRINTTAB(16)"‹€Š€€€€…"
111 PRINTTAB(10)"C.I.A.‹€ŒŒ€ØØØ":FORC1=1TO3:PRINTTAB(21)"ŠØØØ":NEXT:PRINT
150 IC=0:TT=0:ET=1000:RESTORE:G=0:GOSUB700:R=1:GOSUB510
REM ---- MAIN PROGRAM ----
215 PRINT:GOSUB300:PRINT:IFB>0THEN245
235 GOSUB900:GOTO215
245 GOSUB400:IFV=0ORN=0THEN215
255 GOSUB2000:GOTO215
REM INPUT ROUTINE
300 R$="":TT=TT+3
310 IFTT>ETTHENM$="SORRY... YOU RAN OUT OF TIME.":GOSUB1:END
315 INPUT"NOW WHAT";R$:I$="TURN ON":M$="ON":GOSUB3:I$="SWITCH ON":GOSUB3:I$="START":GOSUB3
316 I$="TURN OFF":M$="OFF":GOSUB3:I$="SWITCH OFF":GOSUB3:I$="EXAMINE":M$="LOOK":GOSUB3
317 I$="MOVE":M$="PUSH":GOSUB3:I$="PRESS":GOSUB3:GOSUB5
325 FORB=1TOLEN(R$):IFMID$(R$,B,1)=" "THENC1=B:B=255:NEXT:B=C1:RETURN
335 NEXT:B=0:RETURN
REM --- PARSING ---
REM --- VERB ---
400 V$=LEFT$(R$,B-1):FORV=1TONV:IFV$=VA$(V)THENC1=V:V=NV:NEXT:V=C1:GOTO435
415 NEXT:M$="I DON'T KNOW HOW TO "+V$:V=0:GOTO8
REM --- NOUN ---
435 L=LEN(R$):N$=MID$(R$,B+1,L)
445 FORN=1TONN:IFN$=OA$(N)AND(OM(N)=ROROM(N)=100)THENC1=N:N=NN:NEXT:N=C1:RETURN
455 NEXT:N=0:M$="IT WON'T HELP":GOTO8
REM NEW ROOM
500 CLS
510 M$=RD$(R)+".":GOTO8
REM MOVE ROUTINE
600 FORJ=1TO4:IFD$(J)=N$THENC1=J:J=4:NEXT:J=C1:GOTO625
610 NEXT:GOTO645
625 IFRE(R,J)=0THEN645
630 R=RE(R,J):GOTO500
645 M$="I CAN'T GO THAT DIRECTION":GOTO8
REM READ DATA
700 READNV,NR,NC,NN:FORI=1TONR:READRD$(I):FORJ=1TO4:READRE(I,J):NEXT:NEXT:FORI=1TO10:READH$(I):NEXT
740 FORI=1TONC:READCA$(I):NEXT:FORI=1TONV:READVA$(I):NEXT:FORI=1TONN:READOA$(I),OD$(I),OM(I),OL(I),OV(I),OT(I):NEXT
765 D$(1)="NORTH":D$(2)="EAST":D$(3)="SOUTH":D$(4)="WEST":RETURN
REM --- COMMANDS ---
900 FORC=1TONC:IFR$=CA$(C)THENC1=C:C=NC:NEXT:C=C1:GOTO925
910 NEXT:M$="I CAN'T UNDERSTAND "+R$:GOTO8
925 ONCGOTO930,934,940,1000,1030,1060,1100
926 M$="INVALID COMMAND CODE "+STR$(C):GOTO8
REM --- HELP ---
930 M$=H$(R)+".":GOSUB1:RETURN
REM --- QUIT ---
934 M$="ARE YOU SURE YOU WANT TO QUIT?":GOSUB1:INPUTR$:IFR$="NO"ORR$="N"THENRETURN
936 GOSUB1030:GOSUB1060:END
REM --- INVENTORY ---
940 IFIC=0THENM$="YOU AREN'T CARRYING ANYTHING":GOTO8
945 I$="YOU HAVE: ":FORI=1TOIC:I$=I$+OA$(CA(I))+", "
960 NEXT:M$=LEFT$(I$,LEN(I$)-2):I$="":GOSUB1:RETURN
REM --- LOOK ---
1000 M$=RD$(R)+".":GOSUB1:RETURN
REM TIME
1030 M$="ELAPSED TIME IS"+STR$(TT)+" MINUTES.":GOTO8
REM SCORE
1060 IFIC=0THENM$="YOU AREN'T CARRYING ANYTHING":GOTO8
1065 S=0:FORI=1TOIC:S=S+OV(CA(I)):NEXT:M$="YOU HAVE"+STR$(S)+" POINTS FOR EVIDENCE.":GOTO8
REM RESTART
1100 M$="ARE YOU SURE YOU WANT TO RESTART?":GOSUB1:INPUTR$:IFR$="YES"ORR$="Y"THEN150
1120 M$="SINCE YOU DON'T WANT TO RESTART":GOTO8
REM ---VERB ROUTINES ---
2000 IFOT(N)<>2THEN2010 
2001 M$="YOU CAN'T "+B$+" "+N$+" YET.":GOTO8
2010 ONVGOTO2100,2200,2200,2300,2300,2300,2400,2500,2600,2700:IFV<11THEN2012
2011 ONV-10GOTO2800,2900,3100,3200,3300,3400,3500,3500,3600,3700,3800,3900,4000,4100
2012 PRINT"INVALID VERB":RETURN
REM ---VERB---LOOK---
2100 M$=OD$(N)+".":GOSUB1
2110 IFOL(N)=0THENRETURN
2115 N=OL(N):IFOM(N)=RTHEN2100
2125 GOTO2110
REM ---VERBS-TAKE, GET---
2200 K=OT(N):ONKGOTO2210,2270,2240,2260,2270:PRINT"INVALID OBJECT";N:RETURN
2210 IFIC>5THENM$="YOU CAN'T CARRY ANYTHING ELSE":GOTO8
2220 IFOM(N)=100THENPRINT"YOU ALREADY HAVE IT":RETURN
2230 PRINT"TAKEN":OM(N)=100:IC=IC+1:CA(IC)=N:RETURN
2240 M$="SILLY, THAT'S TOO HEAVY TO CARRY":GOTO8
2260 PRINT"THAT'S RIDICULOUS!":RETURN
2270 M$="YOU CAN'T TAKE "+N$+" YET.":GOTO8
REM ---VERB--GO
2300 GOSUB600:RETURN
REM ---VERB-OPEN
2400 IFN=12THEN2420
2401 IFN=18THEN2430
2402 IFN=44THEN2440
2403 IFN=49THENPRINT"OPENED":OT(51)=1:OL(49)=51:RD$(10)=RD$(10)+" (OPEN)":RETURN
2410 IFN=17THENM$="YOU STAB YOURSELF WITH THE TIP, WHICH IS A POISONED DART. YOU ARE RUSHED TO HOSPITAL.":GOSUB1:END
2411 IFN=21THENPRINT"OPENED":OL(21)=57:OT(57)=1:RETURN
2412 IFN=37THENPRINT"OPENED":OL(37)=38:OT(38)=1:RD$(8)=RD$(8)+" (OPEN)":RETURN
2419 M$="A "+N$+" CAN'T BE OPENED":GOTO8
2420 IFOT(12)=4ANDOT(13)=4THENPRINT"OPENED":RE(2,1)=3:RETURN
2422 IFOT(12)=5THENPRINT"THE DOOR IS LOCKED.":RETURN
2424 IFOT(13)=5THENM$="YOU DIDN'T DISCONNECT THE ALARM. IT GOES OFF AND THE POLICE COME AND ARREST YOU.":GOSUB1:END
2426 PRINT"CAN'T GET THROUGH DOOR YET":RETURN
2430 INPUT"COMBINATION";C$
2432 IFC$="2-4-8"THENPRINT"OPENED":OD$(18)=OD$(18)+". PARTS OF AN RR-13 RIFLE ARE INSIDE":RETURN
2435 M$="SORRY-- YOU DON'T HAVE THE RIGHT COMBINATION":GOTO8
2440 INPUT"COMBINATION";C$:IFC$="20-15-9"THENPRINT"OPENED":OL(44)=45:OT(45)=1:RETURN
2445 M$="SORRY-- COMBINATION ISN'T RIGHT":GOTO8
REM ---VERB-READ-
2500 IFR=3ANDN=16THEN2502
2501 GOTO2505
2502 M$="THE TELEPHONE BILL IS MADE OUT TO 322-9678 -V.GRIM, P.O. X GRAND CENTRAL STATION NYC. "
2503 M$=M$+"THE AMOUNT IS $247.36 FOR LONG DISTANCE CHARGES TO WASHINGTON  D.C.":GOTO8
2505 IFN=20THENM$="YOU CAN JUST MAKE OUT A MESSAGE HEL-ZXT.93.ZARF.1":GOTO8
2510 IFN=23THENM$="THE BILL IS MADE OUT TO 322-8721 AMBASSADOR V. GRIMINSKI 14 PARKSIDE AVE. NYC FOR $68.34. LOCAL CALLS":GOTO8
2515 IFN=25THENPRINT"322-8721":RETURN
2520 IFN=30THENPRINT"322-9678":RETURN
2525 IFN=42THENPRINT"20-15-9":RETURN
2530 IFN=56THENPRINT"322-8721":RETURN
2590 PRINT"NOTHING TO READ":RETURN
2595 FORC1=1TO32:PRINT"-";:NEXT:RETURN
REM ---VERB-DROP
2600 FORI=1TOIC:IFN=CA(I)THENC1=I:I=IC:NEXT:I=C1:GOTO2620
2610 NEXT
2615 M$="YOU AREN'T CARRYING A "+N$:GOTO8
2620 OM(CA(I))=R:CA(I)=CA(IC):IC=IC-1:PRINT"DROPPED":RETURN
REM ---VERB-CALL-
2700 IFN=53AND(R=5ORR=6ORR=9)THEN2710
2708 IFN<>53THENM$="IT'S NO USE TO CALL "+N$:GOTO8
2709 PRINT"YOU'RE NOT NEAR A PHONE":RETURN
2710 M$="RING...RING-- HELLO, AGENT. THIS IS CONTROL SPEAKING-- LIST YOUR TANGIBLE EVIDENCE.":GOSUB1
2715 EV=0:LL=0:FORI=1TOIC:LI(I)=0:NEXT:GOSUB2780:IFEV>=40THEN2730
2720 M$="I'M SORRY, YOU HAVE INSUFFICIENT EVIDENCE FOR A CONVICTION. CALL ME WHEN YOU HAVE MORE INFORMATION.":GOTO8
2730 M$="FANTASTIC JOB! WE'LL BE OVER IN A FLASH TO ARREST THE SUSPECT!":GOSUB1:TT=TT+6:IFTT>ETTHEN310
2734 GOSUB2595:M$="AMBASSADOR GRIMINSKI ARRIVES HOME AT 10:30 TO FIND OPERATIVES "
2736 M$=M$+"WAITING TO ARREST HIM.":GOSUB1:GOSUB2595
2737 M$="YOU ARE HANDSOMELY REWARDED FOR YOUR CLEVER SLEUTHING. YOU SOLVED THE MYSTERY IN"+STR$(TT)+" MINUTES.":GOSUB1:END
REM --- GET EVIDENCE ---
2780 INPUTR$
2781 IFR$=""THENRETURN
2782 FORI=1TOIC:IFR$=OA$(CA(I))THENC1=I:I=IC:NEXT:I=C1:GOTO2786
2784 NEXT:M$="YOU'RE NOT CARRYING A "+R$:GOSUB1:GOTO2780
2786 FORJ=1TOLL:IFLI(J)=CA(I)THENC1=J:J=LL:NEXT:J=C1:M$="YOU ALREADY SAID "+R$:GOSUB1:GOTO2780
2787 NEXT:EV=EV+OV(CA(I)):LL=LL+1:LI(LL)=CA(I):GOTO2780
REM --- UNSCREW ---
2800 IFN=13THEN2810
2805 M$="YOU CAN'T UNSCREW A "+N$:GOTO8
2810 FORI=1TOIC:IFOA$(CA(I))="SCREWDRIVER"THENC1=I:I=IC:NEXT:I=C1:GOTO2830
2820 NEXT:M$="YOU HAVE NOTHING TO UNSCREW WITH":GOTO8
2830 M$="THE ALARM SYSTEM IS OFF":GOSUB1:OT(13)=4:OD$(13)="THE ALARM IS DISABLED":RETURN
REM---VERB-SPRAY
2900 IFN=14ORN=10THEN2910
2905 M$="YOU CAN'T SPRAY A "+N$:GOTO8
2910 FORI=1TOIC:IFCA(I)=10THENC1=I:I=IC:NEXT:I=C1:GOTO2930
2920 NEXT:M$="YOU HAVE NOTHING TO SPRAY WITH":GOTO8
2930 M$="THE DOG IS DRUGGED AND FALLS HARMLESSLY AT YOUR FEET.":GOSUB1:RE(3,1)=5:RE(3,2)=9:RE(3,4)=4
2940 RD$(3)=RD$(3)+" (DRUGGED)":OD$(14)="THE FIERCE DOBERMANN LIES DRUGGED ON THE FLOOR"
2945 CA(I)=CA(IC):IC=IC-1:M$="THE DRUG IS USED UP AND IS NO LONGER IN YOUR INVENTORY.":GOTO8
REM --- PUSH ---
3100 IFN=26THEN3110
3109 M$="IT DOESN'T DO ANY GOOD TO PUSH A "+N$:GOTO8
3110 RE(5,2)=6:OD$(26)=OD$(26)+". A HIDDEN ROOM CAN BE SEEN BEHIND ONE PANEL"
3115 M$="THE PANEL POPS OPEN TO REVEAL THE ENTRANCE TO A PREVIOUSLY HIDDEN ROOM.":GOTO8
REM --- LOAD ---
3200 IFN=28THEN32103205 M$="CAN'T LOAD A "+N$:GOTO8
3210 IFOM(28)=6THENM$="THE PROGRAM IS ALREADY LOADED":GOTO8
3215 M$="THAT WON'T HELP YOU":GOTO8
---VERB-RUN
3300 IFN<>28THENM$="YOU CAN'T RUN A "+N$:GOTO8
3310 IFOT(31)=5OROT(32)=5OROT(33)=5THENM$="THE COMPUTER CAN'T RUN THE PROGRAM YET.":GOTO8
3311 OT(28)=1:M$="THE PROGRAM DIALS A WASHINGTON D.C. NUMBER. A MESSAGE APPEARS ON THE MONITOR.":GOSUB1
3313 INPUT"PLEASE LOG IN";C$
3314 IFC$="HEL-ZXT.93.ZARF.1"THEN3320
3317 IFG=0THEN3329
3318 PRINT"INVALID LOGON CODE":RETURN
3320 M$="THE FOLLOWING MESSAGE APPEARS: THIS IS THE UNITED STATES DEFENSE DEPARTMENT'S "
3321 M$=M$+"TOP SECRET ACCOUNT FOR WEAPONS DEVELOPMENT AND RADAR RESISTANT AIRCRAFT.":GOSUB1
3322 PRINT"ALL INFORMATION IS CLASSIFIED.":RETURN
3329 G=2
3330 PRINT:PRINT"INVALID LOGON!":GOSUB9:PRINT"THE SCREEN GOES BLANK":GOSUB9:PRINT"YOU HEAR FOOTSTEPS!":GOSUB9
3331 M$="GRIMINSKI LOOMS IN THE DOORWAY WITH AN 8MM LUGER IN HAND!":GOSUB1:GOSUB9
3332 M$="YOU'D BETTER HAVE BROUGHT THE PPK PISTOL FROM THE DEPARTMENT OR YOU'RE FINISHED!":GOSUB1
3333 GOSUB300:IFB=0THEN3335
3334 GOSUB400:IFV$="SHOOT"AND(N=8ORN=58)THEN3805
3335 M$="IT'S HOPELESS! GRIMINSKI FIRES! YOU CRUMPLE TO THE FLOOR, DEAD.":GOSUB1:END
REM ---VERB-DRINK
3400 IFN=36THENM$="YOU ARE POISONED, STAGGER TO THE PHONE AND CALL AN AMBULANCE...":GOSUB1:END
3405 M$="YOU CAN'T DRINK "+N$:GOTO8
---VERBS-CHEW,EAT
3500 IFN=39ORN=54THENM$="YOU FOOL! THESE ARE CYANIDE CAPSULES! YOU FALL WRITHING TO THE FLOOR AND DIE IN AGONY...":GOSUB1:END
3505 IFN=45THENM$="YOU IDIOT! THE GUM IS A PLASTIC EXPLOSIVE. YOU HAVE JUST BLOWN YOURSELF TO SMITHEREENS!":GOSUB1:END
3510 M$="YOU CAN'T "+V$+" "+N$:GOTO8
---VERB-UNWRAP
3600 IFN=45THENM$="THE WRAPPER CONCEALS A TINY STRIP OF MICROFILM":OT(46)=1:GOTO8
3609 M$="IT DOESN'T HELP TO UNWRAP "+N$:GOTO8
---VERB-TALK
3700 IFN=14THENPRINT"HE DOESN'T SPEAK ENGLISH":RETURN
3705 PRINT"THAT WON'T HELP YOU":RETURN
---VERB-SHOOT
3800 IFN=8ORN=14ORN=58THEN3805
3804 PRINT"THAT WON'T HELP":RETURN
3805 FORI=1TOIC:IFCA(I)=8THENC1=I:I=IC:NEXT:I=C1:GOTO3811
3807 NEXT:IFR=6ANDG=2THENM$="YOU DON'T HAVE THE PISTOL. ANYTHING ELSE TAKES TOO TOO MUCH TIME.":GOTO3335
3809 PRINT"YOU HAVE NOTHING TO SHOOT WITH":RETURN
3811 IFR=3AND(N=8ORN=14)THENPRINT"THE DOG BITES YOUR HAND!":RETURN
3814 IFR<>6THENPRINT"THAT JUST MAKES A BIG MESS!":RETURN
3815 IFG<>2THEN3804
3816 M$="YOUR SHOT GRAZES HIS FOREHEAD. HE CRASHES TO THE FLOOR OUT COLD.":GOSUB1:G=1
3817 RD$(6)=RD$(6)+". GRIMINSKI LIES UNCONSCIOUS":M$="YOU HAVE TIME TO GATHER ADDITIONAL EVIDENCE AGAINST HIM.":GOTO8
---VERB-UNLOCK
3900 IFN=12THEN3920
3919 M$="YOU CAN'T "+V$+" A "+N$:GOTO8
3920 FORI=1TOIC:IFOA$(CA(I))="KEY"THENC1=I:I=IC:NEXT:I=C1:PRINT"UNLOCKED":OT(12)=4:RETURN
3922 NEXT:M$="YOU HAVE NOTHING TO USE TO UNLOCK THE DOOR":GOTO8
---VERB-ON
4000 IF(N>=31ANDN<=33)THEN4020
4010 M$="YOU CAN'T TURN ON A "+N$:GOTO8
4020 IFRIGHT$(OD$(N),1)<>")"THENOD$(N)=OD$(N)+" (ON)"
4025 PRINT"ON":OT(N)=3:RETURN
REM ---VERB-OFF
4100 IF(N>=31ANDN<=33)THEN4110
4109 M$="YOU CAN'T TURN OFF A "+N$:GOTO8
4110 PRINT"OFF":OT(N)=5:IFRIGHT$(OD$(N),1)=")"THENOD$(N)=LEFT$(OD$(N),LEN(OD$(N))-5)
4115 RETURN
5097 DATA24,10,7,59
5098 DATA"YOU ARE IN YOUR OFFICE AT THE CIA. ON THE SHELVES ARE TOOLS OF YOUR TRADE. AMBASSADOR GRIMINSKI'S APARTMENT IS NORTH"
5099 DATA2,,,0
6000 DATA"YOU ARE AT 14 PARKSIDE AVE-- ENTRANCE TO AMBASSADOR GRIMINSKI'S SMALL BUT ELEGANT BACHELOR APARTMENT",,,1,0
6001 DATA"THIS IS THE MARBLED FOYER. THERE ARE ROOMS EAST AND NORTH AND A CLOSET WEST. THERE IS TABLE AND A GUARD DOG"
6002 DATA0,,2,0
6003 DATA"YOU ARE IN A CEDAR CLOSET WITH HEAVY TRENCHCOATS AND BOOTS. OTHER ITEMS ARE IN THE CORNER",,3,,0
6006 DATA"YOU ARE IN THE DRAWING ROOM. THERE'S A DESK, SOFA, TABLE AND FIREPLACE SET IN THE PANELED EAST WALL. A ROOM IS NORTH"
6007 DATA7,,3,0
6008 DATA"YOU CAN SEE A COMPUTER, PHONE AND MODEM IN THIS OVER-SIZED CLOSET. A CHAIR AND SHELVES ARE HERE",,,,5
6009 DATA"YOU ARE A DINING ROOM. THE TABLE SEATS SIX. A TRAY IS ON A SIDEBOARD. THE KITCHEN IS TO THE NORTH",8,,5,0
6020 DATA"YOU ARE THE KITCHEN. THERE ARE CHROME APPLIANCES, BUTCHER BLOCK, COUNTERS, A CABINET AND A STEEL SINK",,,7,0
6021 DATA"THIS IS AMBASSADOR GRIMINSKI'S BEDROOM. THERE IS A BED, SMALL TABLE AND WALLSAFE. THE BATHROOM IS NORTH",10,,,3
6022 DATA"YOU ARE IN A BATHROOM/DRESSING ROOM. CLOTHES HANG NEATLY ON RODS AND SHELVES. THERE IS A MEDICINE CABINET"
6023 DATA0,,9,0
6024 DATA"YOU'LL NEED TOOLS TO GET IN TO THE APARTMENT"
6025 DATA"MAYBE YOUR TOOLS WILL HELP YOU"
6026 DATA"SOMETHING FROM YOUR OFFICE COULD BE HELPFUL NOW"
6027 DATA"FIRST IMPRESSIONS CAN BE DECEIVING"
6028 DATA"THERE IS MORE HERE THAN MEETS THE EYE"
6029 DATA"RUNNING A PROGRAM IS ALWAYS INTERESTING"
6030 DATA"I CAN'T HELP YOU HERE"
6031 DATA"BE SUSPICIOUS OF WHAT'S IN SMALL BOTTLES"
6032 DATA"THINGS NOT OFTEN NOT WHAT THEY SEEM"
6033 DATA"DON'T OVERLOOK THE OBVIOUS"
6034 DATAHELP,QUIT,INVENTORY,LOOK,TIME,SCORE,RESTART
6035 DATALOOK,GET,TAKE,GO,CRAWL,WALK,OPEN,READ,DROP,CALL,UNSCREW,SPRAY
6036 DATAPUSH,LOAD,RUN,DRINK,EAT,CHEW,UNWRAP,TALK,SHOOT,UNLOCK,ON,OFF
7001 DATANORTH,"IT DOESN'T HELP",100,,,4
7002 DATAEAST,"IT DOESN'T HELP",100,,,4
7003 DATASOUTH,"IT DOESN'T HELP",100,,,4
7004 DATAWEST,"IT DOESN'T HELP",100,,,4
8005 DATASHELVES,"SHELVES FOR WEAPONS AND TOOLS THAT MAY HELP YOU",1,6,,3
8006 DATASCREWDRIVER,"AN ALL-PURPOSE SCREWDRIVER WITH COLLAPSIBLE HANDLE",1,7,,1
8007 DATABOMB,"A MARK MX HIGH INTENSITY SMOKE BOMB",1,8,,1
8008 DATAPISTOL,"AN AUTOMATIC PPK PISTOL",1,9,,1
8009 DATAKEY,"A SKELETON KEY",1,10,,1
8010 DATADRUG,"A SMALL CAN OF INSTA-KNOCKOUT DRUG",1,11,,1
8011 DATAGUN,"A MARK 3K HARPOON GUN WITH GRAPPLE AND LINE",1,,,1
8012 DATADOOR,"YOU CAN SEE HOLES EITHER SIDE OF THE DOOR WHICH MUST RADIATE AN ELECTRONIC ALARM BEAM",2,13,,5
8013 DATAALARM,"THE ALARM IS SCREWED INTO PLACE",2,,,5
8014 DATADOG,"THE SAVAGE DOBERMAN'S FANGS ARE BARED. HE WILL NOT LET YOU PASS",3,,,4
8015 DATATABLE,"THE TABLE HAS A TORTOISESHELL LETTER TRAY ON IT FOR BUSINESS CARDS AND MAIL. THERE IS A LETTER",3,,,1
8016 DATALETTER,"APPARENTLY A TELEPHONE BILL PAID TO THE TELEPHONE COMPANY",3,,10,1
8017 DATAUMBRELLA,"THERE IS A BLACK UMBRELLA WITH A POINTED END",4,18,,1
8018 DATABRIEFCASE,"A BLACK BRIEFCASE WITH A COMBINATION LOCK",4,,,1
8019 DATADESK,"THE DESK HAS A BLOTTER, PEN SET, PHONE AND BLANK PAD ON IT. IT HAS A PIGEONHOLE AND DRAWER",5,,,1
8020 DATAPAD,"ALTHOUGH THE NOTEPAD IS BLANK YOU CAN SEE THE INDENTATION OF WRITING ON IT",5,,,1
8021 DATADRAWER,"THIS IS A STANDARD DESK DRAWER",5,,,4
8022 DATAPIGEONHOLE,"THE PIGEONHOLE HAS A PAID BILL IN IT",5,,,4
8023 DATABILL,"THE BILL IS FROM THE TELEPHONE COMPANY",5,,,1
8024 DATAPHONE,"THIS IS A BEIGE PUSHBUTTON DESK PHONE",5,25,,4
8025 DATANUMBER,"THE TELEPHONE NUMBER IS PRINTED ON THE BASE",5,,,4
8026 DATAPANEL,"ONE OF THE WOOD PANELS SEEMS MORE WORN THAN THE OTHERS",5,,,4
8027 DATASHELVES,"THERE IS SOFTWARE, BLANK DISKS AND MANUALS ON THE SHELVES",6,,,4
8028 DATAPROGRAM,"ONE PROGRAM IS FOR COMMUNICATING WITH THE U.S. DEFENSE DEPARTMENT'S MAINFRAME COMPUTER",6,,10,5
8029 DATAPHONE,"THIS IS A STANDARD DIAL TELEPHONE. THE RECEIVER IS SET INTO A MODEM",6,30,,4
8030 DATANUMBER,"THE TELEPHONE NUMBER IS PRINTED ON THE BASE",6,,,1
8031 DATACOMPUTER,"THIS IS A STANDARD BUSINESS COMPUTER AND MONITOR WITH A PROGRAM DISK IN ONE OF THE DRIVES",6,,,5
8032 DATAMONITOR,"THIS IS A HI-RES COMPUTER MONITOR",6,,,5
8033 DATAMODEM,"THE MODEM IS ONE THAT CAN USE AN AUTOMATIC COMMUNICATIONS PROGRAM",6,,,5
8034 DATATRAY,"THE SILVER TRAY HOLDS A DECANTER PARTIALLY FILLED WITH CLARET",7,,,1
8035 DATADECANTER,"THE DECANTER IS OF ETCHED CRYSTAL. IT PROBABLY HOLDS SOME CLARET",7,,,1
8036 DATACLARET,"AN AMBER LIQUID",7,,,1
8037 DATACABINET,"THIS IS A STANDARD KITCHEN CABINET",8,,,4
8038 DATABOTTLE,"A BOTTLE OF CAPSULES ARE HERE",8,39,,2
8039 DATACAPSULE,"THE CAPSULES HAVE A SLIGHT AROMA OF BURNT ALMONDS",8,,,1
8040 DATATABLE,"THE BEDSIDE TABLE HAS A PHONE ON IT. A PIECE OF PAPER AND A LAMP ARE HERE",9,,,3
8041 DATAPHONE,"THERE IS A NUMBER PRINTED ON THE PHONE",9,,,4
8042 DATAPAPER,"A PIECE OF MONOGRAMMED WRITING PAPER",9,43,,1
8043 DATACOMBINATION,"THERE IS A COMBINATION WRITTEN ON IT",9,,,4
8044 DATASAFE,"THIS IS A STANDARD COMBINATION SAFE",9,,,4
8045 DATAGUM,"INSIDE IS A PACK OF PEPPERMINT GUM. EACH STICK IS WRAPPED IN PAPER",9,,,2
8046 DATAMICROFILM,"MICROFILM REQUIRING SPECIAL EQUIPMENT TO VIEW, BUT IT'S PRETTY CERTAIN WHAT YOU HAVE FOUND",9,,10,2
8047 DATASHELVES,"A VERY SOPHISTICATED CAMERA IS ON ONE OF THE SHELVES",10,,,4
8048 DATACAMERA,"THIS CAMERA IS USED TO PHOTOGRAPH DOCUMENTS ON MICROFILM",10,,10,1
8049 DATACABINET,"THIS IS A LARGE MIRRORED BATHROOM CABINET",10,,,4
8050 DATABUREAU,"A WALL SAFE IS SET INTO THE WALL ABOVE A LOW MAHOGANY CARVED BUREAU",9,,,3
8051 DATABOTTLES,"BOTTLES OF FIXER AND PHOTOFLO ARE ON THE SHELVES",10,52,,2
8052 DATATANK,"THERE IS A FILM DEVELOPING TANK, APRON AND TANK COVER HERE TOO",10,,,2
8053 DATAHEADQUARTERS,"HEADQUARTERS",100,,,4
8054 DATACAPSULES,"THE CAPSULES HAVE A SLIGHT AROMA OF BURNT ALMONDS",8,,,1
8055 DATASIDEBOARD,"A LARGE ORNATE SIDEBOARD WITH A BEVELLED GLASS MIRROR DOMINATES THE EAST WALL",7,34,,4
8056 DATANUMBER,"THE NUMBER IS PRINTED ON THE PHONE",9,,,1
8057 DATAPAPER,"THE NUMBERS 2-4-8 ARE WRITTEN ON A PIECE OF PAPER ON THE TOP OF THE DRAWER",5,,,2
8058 DATAGRIMINSKI,"THE WHITE-HAIRED MAN IS DRESSED IN EVENING CLOTHES",6,,,4
8059 DATACORNER,"YOU ARE LOOKING AT THE CORNER OF THE CLOSET",4,17,,4
