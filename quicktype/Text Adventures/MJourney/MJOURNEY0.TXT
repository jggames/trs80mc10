1 ' MAGICAL JOURNEY      BY PETER KIRSCH       FINAL VERSION         FEB. 1980
5 GOTO200
10 IFDT=1THEN320ELSEONAGOTO11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80,81,82,83,84,85,86
11 PRINT"IN A FOREST.":W=1:N=3:E=1:S=1:GOTO350
12 PRINT"ON TOP OF A TREE.":D=1:GOTO350
13 PRINT"AT THE BASE OF A MOUNTAIN.":S=1:E=4:GOTO350
14 PRINT"ON AN OPEN PASTURE.":W=3:GOTO350
15 PRINT"ON TOP OF THE MOUNTAIN.":D=3:GOTO350
16 PRINT"IN AN UNDERGROUND CAVERN.":W=7:N=8:E=10:S=9:U=5:GOTO350
17 PRINT"IN A PASSAGEWAY.":W=13:E=6:S=9:GOTO350
18 PRINT"IN A TUNNEL.":E=12:S=6:GOTO350
19 PRINT"IN A TUNNEL.":W=7:N=6:E=10:GOTO350
20 PRINT"IN A TUNNEL.":W=6:S=9:GOTO350
21 PRINT"IN A ROOM. TUNNEL ENDS.":S=12:GOTO350
22 PRINT"IN A TUNNEL.":W=8:N=11:GOTO350
23 PRINT"AT A DEAD END.":E=7:GOTO350
24 PRINT"IN A STRANGE FOREST.":W=17:N=15:E=16:S=17:GOTO350
25 PRINT"IN A STRANGE FOREST.":W=17:N=19:E=16:S=14:GOTO350
26 PRINT"IN A STRANGE FOREST.":W=14:N=15:E=16:S=16:GOTO350
27 PRINT"IN A STRANGE FOREST.":W=14:N=15:E=14:S=14:GOTO350
28 PRINT"IN THE WOODSHED.":S=17:GOTO350
29 PRINT"AT A JUNCTION.":W=23:N=39:E=57:S=15:GOTO350
30 PRINT"ON TOP OF A RED TREE.":D=14:GOTO350
31 PRINT"ON TOP OF A BLUE TREE.":D=15:GOTO350
32 PRINT"ON TOP OF A GREEN TREE.":D=16:GOTO350
33 PRINTJ$" THE LAND OF OOZE.":W=24:E=19:GOTO350
34 PRINT"STANDING ON FARM LAND.":W=27:N=36:E=23:S=26:GOTO350
35 PRINT"IN A KITCHEN.":W=37:E=24:GOTO350
36 PRINT"ON FARMLAND.":W=26:N=24:E=26:S=26:GOTO350
37 PRINT"IN A CORN FIELD.":W=27:N=28:E=24:S=27:GOTO350
38 PRINT"AT VALLEY JUNCTION.":W=29:N=30:E=31:S=27:GOTO350
39 PRINT"IN GOLDEN VALLEY.":E=28:GOTO350
40 PRINT"IN MEDIEVAL VALLEY.":E=28:GOTO350
41 PRINT"IN TERROR VALLEY.":W=28:GOTO350
42 PRINT"IN A HALLWAY.":W=33:S=30:U=34::GOTO350
43 PRINT"IN A SMALL CHAMBER.":E=32:GOTO350
44 PRINT"IN A TOWER":D=32:GOTO350
45 PRINT"IN A VERY SMALL ROOM.":W=34:GOTO350
46 PRINT"ON A GREEN PASTURE.":S=24:GOTO350
47 PRINT"IN A LAUNDRY ROOM.":N=38:E=25:GOTO350
48 PRINT"IN A PANTRY.":S=37:GOTO350
49 PRINTJ$" THE LAND OF ZARTZ.":N=40:S=19:GOTO350
50 PRINT"AT WOODS JUNCTION.":W=41:N=45:E=49:S=39:GOTO350
51 PRINT"IN INSECT WOODS.":W=43:N=44:E=40:S=42:GOTO350
52 PRINT"NEAR A SMALL POND.":N=41:GOTO350
53 PRINT"IN A FOREST.":W=43:N=44:E=41:S=43:GOTO350
54 PRINT"IN A FOREST.":W=43:N=43:E=43:S=41:GOTO350
55 PRINT"IN TERROR WOODS.":W=46:N=47:E=48:S=40:GOTO350
56 PRINT"NEAR A SMALL PIT.":E=45:GOTO350
57 PRINT"IN A FOREST.":S=45:GOTO350
58 PRINT"NEAR THE ENTRANCE OF A CAVE":W=45:GOTO350
59 PRINT"IN DARK WOODS.":W=40:N=50:E=51:S=52:GOTO350
60 PRINT"IN A FOREST.":W=50:N=50:E=51:S=49:GOTO350
61 PRINT"IN A FOREST.":W=49:N=50:E=50:S=50:GOTO350
62 PRINT"IN A FOREST.":W=50:N=49:E=51:S=52:GOTO350
63 PRINT"IN THE BOTTOM OF THE PIT.":W=55:U=46:GOTO350
64 PRINT"IN A SMALL STORAGE ROOM.":E=55:GOTO350
65 PRINT"IN A TUNNEL.":E=53:GOTO350
66 PRINT"INSIDE THE CAVE.":W=48:GOTO350
67 PRINTJ$" THE LAND OF OOLA.":W=19:GOTO350
68 PRINT"ON A PLAIN.":W=57:E=59:GOTO350
69 PRINT"ON RUGGED LAND.":W=58:N=60:E=65:GOTO350
70 PRINT"NEAR A MAGIC GARDEN.":W=75:S=59:GOTO350
71 PRINT"ON TOP OF THE HILL.":D=65:GOTO350
72 PRINT"IN A BLUE GARDEN.":S=60:GOTO350
73 PRINT"IN A GREEN GARDEN.":S=62:GOTO350
74 PRINT"AT A DEAD END.":S=68:GOTO350
75 PRINT"AT THE BOTTOM OF A HILL.":W=59:GOTO350
76 PRINT"IN THE DWARF HOUSE.":S=63:GOTO350
77 PRINT"NEAR SOME ANCIENT RUINS.":W=68:GOTO350
78 PRINT"IN ANCIENT VALLEY.":N=64:E=67:S=69:GOTO350
79 PRINT"AT DRAGON CROSSING.":N=68:GOTO350
80 PRINT"IN A WELL-LIT TUNNEL.":W=71:E=69:GOTO350
81 PRINTCHR$(29)"TUNNEL CONTINUES.":W=72:E=70:GOTO350
82 PRINTCHR$(29)"END OF TUNNEL.":E=71:GOTO350
83 PRINT"IN A RED GARDEN.":S=66:GOTO350
84 PRINT"STANDING IN THE RUINS.":W=67:GOTO350
85 PRINT"OUTSIDE OF A TOOL SHED.":E=60:GOTO350
86 PRINT"INSIDE THE TOOL SHED.":E=75:GOTO350
200 CLEAR500:DIMA$(62),A(62),B(12),B$(33),C$(5),D(33),H$(62),I(62)
210 FORA=1TO56:READA$(A):READA(A):H$(A)=A$(A):NEXT:FORA=1TO33:READB$(A):NEXT:FORA=13TO33:READD(A):NEXT
220 A=1:G=1:G(4)=1:N$="LOWERED DRAWBRIDGE":J$="ON THE BORDER OF":W$="$$$ BOTTLE OF GOLDEN MILK"
300 N=0:W=0:E=0:S=0:U=0:D=0:Y=0:CLS:PRINT"YOU'RE ";:IF(DK=0)*(A>5)DT=1
305 IF A = 72 GOTO 82
310 GOTO10
320 PRINTCHR$(29)"IT'S TOO DARK TO SEE CLEARLY!  GET A LIGHT!":U=5:DT=0
350 PRINT:IFN>0ORW>0ORE>0ORS>0ORU>0ORD>0THENPRINT"SOME EXITS ARE: ";:FORB=1TO12:B(B)=0:NEXT
360 IFW>0PRINT" WEST";:B(1)=W:B(2)=W
361 IF(A=39)*(PF=0)THEN363ELSEIFN>0PRINT" NORTH";:B(3)=N:B(4)=N
362 IFE>0PRINT" EAST";:B(5)=E:B(6)=E
363 IFS>0PRINT" SOUTH";:B(7)=S:B(8)=S
364 IFU>0PRINT" UP";:B(9)=U:B(10)=U
365 IFD>0PRINT" DOWN";:B(11)=D:B(12)=D
366 PRINT:PRINT
370 FORB=1TO62:IFABS(A(B))=ATHENC=C+1:GOTO372
371 NEXT:PRINT:GOTO374
372 IFC=1PRINT"THINGS YOU SEE HERE:"
373 PRINT"  "A$(B):GOTO371
374 IFGO=1GOTO6000
375 IFG=1PRINT"YOU ARE CARRYING A LARGE SACK FOR STORING YOUR TREASURES.":G=0
376 IFA=11PRINT"GROUND IS VERY SOFT."
377 IF(A=31)*(AM=0)THEN1500ELSEIF(A=42)*(FR=0)THEN1510ELSEIF(A=39)*(DW=0)THEN1520ELSEIF(A=47)*(CH=0)THEN1530ELSEIF(A=56)*(RS=0)THEN1540ELSEIF(A=57)*(TR=0)THEN1550ELSEIF(A=69)*(DR=0)GOTO1560
380 ONERRORGOTO3000:C=0:PRINT:PRINT"WHAT DO YOU WANT TO DO";:INPUTA$:IFA$="LOOK"THEN300ELSEPRINT:FORB=1TO12:IFA$=B$(B)THEN385ELSENEXT:GOTO387
385 IFB(B)<>0THENA=B(B):GOTO300ELSEPRINT"YOU CAN'T MOVE THAT WAY.":GOTO380
387 IFA$<>"I"ANDRIGHT$(A$,3)<>"ORY"THEN400ELSEPRINT"YOU ARE CARRYING:":PRINT"    A BAG WITH"NT"TREASURES";:FORK=1TO5:PRINTTAB(POS(0)+4)C$(K);:IFPOS(0)>45PRINT
388 NEXT:GOTO380
400 FORB=13TO33:F=LEN(B$(B)):IFLEFT$(A$,F)=B$(B)THEN450ELSENEXT:PRINT"DON'T KNOW WHAT "CHR$(34)A$CHR$(34)" MEANS.":GOTO380
450 D$=RIGHT$(A$,(LEN(A$)-F)-1):E$=RIGHT$(D$,3)
460 IFD(B)<>1THEN490
463 FORJ=1TO62:IFE$=RIGHT$(A$(J),3)ANDA=ABS(A(J))THEN470
464 NEXT
465 PRINT"THERE'S NO "D$" HERE.":GOTO380
470 IFSGN(A(J))=-1PRINT"BE REASONABLE NOW.  THAT'S IMPOSSIBLE.":GOTO380
471 IF((A=31)*(AM=0))+((A=42)*(FR=0))+((A=56)*(RS=0))+((A=47)*(CH=0)):MM=1:GOTO1000
472 IFLEFT$(A$(J),1)="$"NT=NT+1:A$(J)="":A(J)=0:PRINT"OK!":GOTO5000
473 IFI>4THENPRINT"YOU CAN'T CARRY ANYMORE.":GOTO380
474 IFA=44IFE$="IES"IFI(32)=0THEN1000ELSEPRINT"FLIES STICK LIKE GLUE TO FLYPAPER":AF=1:A$(31)="FLIES STUCK ON FLYPAPER":H$(60)=A$(31):A(31)=44:K3=32:R$="":GOSUB1100:M$="":GOSUB1200:GOTO5000
480 PRINT"OK!":FORK=1TO5:IFC$(K)=""THENC$(K)=A$(J):GOSUB3500:H(K)=J:A$(J)="":A(J)=0:I=I+1:GOTO5000ELSENEXT
490 IFD(B)<>2THEN600
500 FORJ=1TO5:IFE$<>RIGHT$(C$(J),3)ELSE520
510 NEXT:PRINT"YOU'RE NOT CARRYING IT.":GOTO380
520 FORK=1TO62:IFA$(K)=""THENA$(K)=C$(J):I=I-1:A(K)=A:H(J)=0ELSENEXT
530 PRINT"OK!":FORK1=1TO62:IFC$(J)=H$(K1)THENC$(J)="":GOTO545ELSENEXT
545 IFA=31IFE$="DER"IFAM=0THENAM=1:PRINT"AXMAN DROPS HIS AX AND SNEEZES WILDLY. QUICK, GRAB THE COINS!":A(57)=-31:A$(57)="SNEEZING AXMAN":A(58)=31:A$(58)="AX":H$(58)=A$(58):GOTO590
550 IFA=47IFE$="ORN"IFCH=0THENCH=1:PRINT"CHICKEN DEVOURS CORN, GETS UP, AND WALKS AWAY.":A(K)=47:A$(K)="$$$ GOLDEN EGG":GOTO590
555 IFA=56IFE$="OOD"IFRS=0THENRS=1:PRINT"SNAKES EAT FOOD AND SLITHER INTO A DARK CORNER TO SLEEP.":A(K)=0:A$(K)="":GOTO590
560 IFE$="GAS"IFI(46)=0THENPRINT"WHERE'S YOUR "H$(46)"? YOU FELL ASLEEP AND ARE EATEN BY A GIANT MOTH!":GOTO6100ELSEIFA=69IFDR=0THENDR=1:PRINT"DRAGON FALLS ASLEEP.":A(K)=-69:A$(K)="SLEEPING DRAGON":GOTO590
565 IFA=39IFE$="PIE"DW=1:A$(K)="":A(K)=0:PRINT"DWARF GULPS THE PIE AND VANISHES.":PF=1:GOTO590
570 IFA=42IFE$="PER"IFFR=0THENIFAF=1THENFR=1:PRINT"FROG LICKS FLIES ON PAPER AND DISAPPEARS INTO THE POND.":A$(K)="LICKED FLIES ON PAPER":H$(61)=A$(K):A(K)=A:GOTO5000
580 IFA=57IFE$="LAR"IFI(36)=1PRINT"TROLLS GRABS YOUR BUCK, TURNS A CRANK.":TR=1:K3=36:DB=1:A$(39)=N$:R$="":M$="":GOSUB1100:GOSUB1200:GOTO5000
590 I(K1)=A:GOTO5000
600 IFD(B)<>3THEN620ELSEIFA=1ANDE$="REE"THENA=2:GOTO300ELSEIFA=3ANDE$="AIN"THENA=5:GOTO300ELSEIF(A>13)*(A<17)*(E$="REE")A=A+6:GOTO300ELSEIFA=65ANDE$="ILL"A=61:GOTO300
620 IFD(B)<>4THEN650ELSEIFI(3)=1IFE$="OLE"ORE$="UND"ORE$="NEL"IFA=11THENPRINT"YOU DUG A TUNNEL AND CRAWLED UNDER THE DOOR. HOLE MAGICALLY VANISHES.":A=14:GOTO5000ELSEPRINT"YOU FOUND NOTHING OF VALUE":GOTO5000
650 IFD(B)<>11THEN700ELSEIFA=5ANDE$="OLE"THENA=6:Y=1ELSEIFA=17ORA=75ANDE$="HED"THENA=A+1:Y=1ELSEIF((A=24)+(A=63))ANDE$="USE"THENA=A+INT(A/10)/2:Y=1ELSEIFA=30ANDE$="TLE"THENA=32:Y=1ELSEIFA=48ANDE$="AVE"THENA=56:Y=1ELSEGOSUB7000
653 IFA=46ANDE$="PIT"THENA=53:Y=1ELSEIFA=67ANDE$="INS"A=74:Y=1
660 IFA=11ORA=34ORA=55ORA=60ORA=62ORA=66IFE$="OOR"IFG((A/10-INT(A/10))*10)=0PRINT"THE DOOR'S LOCKED.":Z=1:GOTO5000ELSEIFD(B)=5THENRETURNELSEIFA=60THENA=62:GOTO300ELSEIFA=66THENA=73:GOTO300ELSEIFA=55THENA=54:GOTO300ELSEA=A+1:GOTO300
670 IFA=69IFE$="AVE"IFDR=1THENA=70:GOTO300ELSEPRINT"DRAGON WON'T LET YOU. HE SURE LOOKS MEAN.":GOTO5000
672 IFA=57IFE$="DGE"THENIF(DB=1)*(TR=1)PRINT"YOU CROSS THE DRAWBRIDGE":A=58:GOTO5000ELSEPRINT"DRAWBRIDGE IS UP! TROLL SAYS, "CHR$(34)"PAY ME A BUCK!"CHR$(34):GOTO5000
674 IFA=61IFE$="DER"IFFA=0THENPRINT"GLIDER WON'T FLY! THERE'S NO WIND!":GOTO5000ELSEPRINT"GLIDER SWOOPS YOU INTO THE VALLEY BELOW. IT DROPS YOU OFF AND A GUST OF WIND CARRIES IT AWAY.":FORJ=1TO2500:NEXT:A=68:GOTO300
680 IFY=1THEN300
700 IFD(B)<>5THEN800ELSEIFA=11ORA=55THEN660ELSEIF((I(10)=1)*(A=66))OR((I(13)=1)*(A=62))OR((I(14)=1)*(A=60))THENGOSUB4000:G((A/10-INT(A/10))*10)=1:PRINT"DOOR'S OPEN.":GOTO5000ELSEIF(A=60)+(A=62)+(A=66)PRINT"GET THE RIGHT KEY!":GOTO5000
800 IFD(B)<>9THEN830ELSEIFA=25IFE$="PIE"IFPE=0 IF((I(25)=1)+(I(25)=25))*((I(21)=25)+(I(21)=1))THEN810ELSE1000
805 GOTO830
810 PRINT"YOU HAVE JUST BAKED A RHUBARB PIE.":PE=1:A$(59)="RHUBARB PIE":H$(59)=A$(59):A(59)=25:K3=21:R$="":GOSUB1100:K3=25:GOSUB1100:M$="":K3=21:GOSUB1200:K3=25:GOSUB1200:GOTO5000
830 IFD(B)<>6THEN850ELSEIFE$="OTE"IFI(12)=AORI(12)=1OR(A=20)*(I(12)=0)THENPRINT"NOTE SAYS:"CHR$(34)"THERE ARE 17 TREASURES HIDDEN. CAN YOU FIND THEM ALL?"CHR$(34):GOTO380
850 IFD(B)<>7THEN900ELSEIFA=36IFE$="COW"IFI(23)=36AND(I(24)=36ORI(24)=1)IFI(19)=36PRINT"WOW! COW GIVES GOLDEN MILK. FILL YOUR BOTTLE UP!":K3=24:R$="":GOSUB1100:K3=19:M$=W$:GOSUB1200:K3=24:M$="":GOSUB1200:GOTO5000ELSEPRINT"COW IS HUNGRY!":GOTO5000
900 IFD(B)<>10THEN930ELSEIFE$="PIE"IFI(59)=1ORI(59)=AOR((A=25)*(I(59)=0))THENPRINT"YOU CAN'T STAND THE TASTE OF RHUBARB! BESIDES, YOU'RE NOT HUNGRY.":GOTO5000
930 IFD(B)<>13THEN950ELSEIFE$="IGS"IFI(2)=1THENDK=1:R$="BURNING TWIGS":K3=2:GOSUB1100:H$(2)=R$:PRINT"GOOD DEDUCTION! YOU HAVE CREATED A SMALL FIRE AND YOU CAN SEE!":GOTO5000
950 IFD(B)<>15THEN960ELSEIFA=55IFI(58)=1IFG(5)=0THENPRINT"WHACK!!!! DOOR SPLINTERS IT'S OPEN":G(5)=1:X=35:GOSUB4100:GOTO5000
960 IFD(B)<>12THEN970ELSEIFE$="TON"IF(A=61)*(FA=0)THENFA=1:PRINT"FAN SPINS. IT'S GETTING WINDY!":A$(55)="SPINNING FAN":H$(55)=A$(55):GOTO5000ELSEIFA=72THENA=1:GO=1:GOTO300
970 IFD(B)<>14THEN1000ELSEIFE$="LAR"ORE$="OLL"IFI(36)=1IFA=57THENE$="LAR":GOTO580
1000 PRINT"YOU CAN'T DO THAT HERE."
1010 IFMM=1PRINT"YOU MIGHT GET KILLED!":MM=0
1020 GOTO380
1100 FORK2=1TO5:IFC$(K2)=H$(K3)THENC$(K2)=R$:GOSUB1150:RETURNELSENEXT:RETURN
1150 IFR$=""THENI=I-1:I(K3)=0
1160 RETURN
1200 FORK2=1TO62:IFA$(K2)=H$(K3)THENA$(K2)=M$:IFM$<>""THENRETURNELSEA(K2)=0:I(K3)=0
1210 NEXT:RETURN
1499 GOTO5000
1500 PRINT"THERE IS AN AX-WIELDING MADMAN IN THE VALLEY GUARDIN THE COINS.  HE IS SWINGING HIS AX MADLY.":GOTO380
1510 PRINT"THERE IS A GIANT KILLER FROG IN THE POND.":GOTO380
1520 PRINT"THERE IS A PATH NORTH, BUT A WICKED PIE-EATING DWARF BLOCKS YOUR WAY. HE SAYS, "CHR$(34)"GIMME A PIE."CHR$(34):GOTO380
1530 PRINT"THERE IS A GIANT CHICKEN IN THE FOREST. HE SEEMS TO BE HIDING SOMETHING. HE LOOKS HUNGRY.":GOTO380
1540 PRINT"A PAIR OF HISSING RATTLESNAKES GUARD THE STATUE. WATCH YOUR STEP. THEY HAVEN'T EATEN TODAY YET.":GOTO380
1550 PRINT"A FAT TROLL STANDS GUARD."CHR$(34)"PAY THE TROLL,"CHR$(34)"HE SAYS.":GOTO380
1560 PRINT"THE ENTRANCE TO THE CAVE IS GUARDED BY A MEAN DRAGON. HE IS WIDE AWAKE.":GOTO380
2000 DATATREE,-1,2 SMALL TWIGS,2,SHOVEL,4,DARK HOLE,-5,LOCKED DOOR,-11,RED TREE,-14,BLUE TREE,-15,GREEN TREE,-16,WOOD SHED,-17,RED KEY,18,$$$ VALUABLE COINS,31,PAPER NOTE,20,GREEN KEY,21,BLUE KEY,22,FARMHOUSE,-24,GOLDEN COW,-36
2005 DATASNEEZING POWDER,26
2010 DATAGIANT CORN,27,GOLDEN GRASS,29,CASTLE,-30,PIE SHELL,38,OVEN,-25,MILKING STOOL,37,EMPTY BOTTLE,37,PIE FILLING,38,$$$ SILVER CROWN,33,OPEN DOOR,-34,$$$ PEARL NECKLACE,35,$$$ RARE BOOK,35
2020 DATA$$$ GOLDEN LILY PAD,42,SWARM OF FLIES,44,FLYPAPER,50,$$$ EMERALD,51,SNAKE FOOD,52,LOCKED DOOR,-55,SILVER DOLLAR,54,$$$ BAR OF GOLD,54,$$$ GOLDEN STATUE,56
2030 DATA"RAISED DRAW BRIDGE",-57,"BLUE LOCKED DOOR",-60,"$$$ RARARE STAMPS",62,"$$$ ANCIENT VASE",62,"GREEN LOCKED DOOR",-62
2035 DATA"DWARF'S HOUSE",-63,"$$$ GOLD WATCH",73,"GAS MASK",64,"SLEEPING GAS",64,"CAVE",-69
2040 DATAPUSH BUTTON,-72,$$$ PEARL,66,$$$ DIAMOND,66,RED LOCKED DOOR,-66,$$$ RARE FOSSIL,74,HANGGLIDER,-61,BIG FAN.  PUSH BUTTON ON FAN,-61,$$$ RING,76
2100 DATAWEST,W,NORTH,N,EAST,E,SOUTH,S,UP,U,DOWN,D,GET,TAKE,DROP,PUT,GIVE,CLIMB,DIG,OPEN,READ,MILK,BAKE,EAT,GO,ENTER,PRESS,PUSH,RUB,PAY,HIT,BREAK,SMASH
2110 DATA1,1,2,2,2,3,4,5,6,7,9,10,11,11,12,12,13,14,15,15,15
3000 PRINT"WHAT?":RESUME5100
3500 FORK4=1TO62:IFA$(J)=H$(K4)THENI(K4)=1ELSENEXT
3600 RETURN
4000 IFA=60THENX=40ELSEIFA=62THENX=43ELSEX=52
4100 A$(X)="OPEN DOOR":RETURN
5000 IF(Z=1)*(A=55)PRINT"BUT IT LOOKS LIKE IF AN AX MIGHT SMASH THROUGH":Z=0
5100 FORV=1TO2000:NEXT:GOTO300
6000 PRINT"CONGRATULATIONS! YOU'VE MADE IT ALL THE WAY THROUGH AND BACK.":IFNT=17PRINT"YOU FOUND ALL 17 TREASURES.":GOTO6100
6050 PRINT"YOU ONLY FOUND"NT"TREASURES, HOWEVER. THERE ARE"17-NT"STILL OUT THERE SOMEWHERE."
6100 INPUT"TO PARTAKE ANOTHER JOURNEY, HIT < ENTER >";A$:RUN
7000 IFA=65ANDE$="ILL"THENA=61:Y=1
7010 RETURN