0 CLS:CLEAR600:DIMCC,ZZ,Z$,I$:GOTO200
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 Z$="YOU'RE "+Z$:GOSUB1:GOTO350
6 IFDT=1THEN320
7 ONAGOTO11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39
8 ONA-29GOTO40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,60,61,62,63,64,65,66,67
9 ONA-57GOTO68,69,70,71,72,73,74,75,76,77,78,79,80,81,82,83,84,85,86:GOTO11
10 GOTO6
11 Z$="IN A FOREST.":W=1:N=3:E=1:S=1:GOTO5
12 Z$="ON TOP OF A TREE.":D=1:GOTO5
13 Z$="AT THE BASE OF A MOUNTAIN.":S=1:E=4:GOTO5
14 Z$="ON AN OPEN PASTURE.":W=3:GOTO5
15 Z$="ON TOP OF THE MOUNTAIN.":D=3:GOTO5
16 Z$="IN AN UNDERGROUND CAVERN.":W=7:N=8:E=10:S=9:U=5:GOTO5
17 Z$="IN A PASSAGEWAY.":W=13:E=6:S=9:GOTO5
18 Z$="IN A TUNNEL.":E=12:S=6:GOTO5
19 Z$="IN A TUNNEL.":W=7:N=6:E=10:GOTO5
20 Z$="IN A TUNNEL.":W=6:S=9:GOTO5
21 Z$="IN A ROOM. TUNNEL ENDS.":S=12:GOTO5
22 Z$="IN A TUNNEL.":W=8:N=11:GOTO5
23 Z$="AT A DEAD END.":E=7:GOTO5
24 Z$="IN A STRANGE FOREST.":W=17:N=15:E=16:S=17:GOTO5
25 Z$="IN A STRANGE FOREST.":W=17:N=19:E=16:S=14:GOTO5
26 Z$="IN A STRANGE FOREST.":W=14:N=15:E=16:S=16:GOTO5
27 Z$="IN A STRANGE FOREST.":W=14:N=15:E=14:S=14:GOTO5
28 Z$="IN THE WOODSHED.":S=17:GOTO5
29 Z$="AT A JUNCTION.":W=23:N=39:E=57:S=15:GOTO5
30 Z$="ON TOP OF A RED TREE.":D=14:GOTO5
31 Z$="ON TOP OF A BLUE TREE.":D=15:GOTO5
32 Z$="ON TOP OF A GREEN TREE.":D=16:GOTO5
33 Z$=J$+" THE LAND OF OOZE.":W=24:E=19:GOTO5
34 Z$="STANDING ON FARM LAND.":W=27:N=36:E=23:S=26:GOTO5
35 Z$="IN A KITCHEN.":W=37:E=24:GOTO5
36 Z$="ON FARMLAND.":W=26:N=24:E=26:S=26:GOTO5
37 Z$="IN A CORN FIELD.":W=27:N=28:E=24:S=27:GOTO5
38 Z$="AT VALLEY JUNCTION.":W=29:N=30:E=31:S=27:GOTO5
39 Z$="IN GOLDEN VALLEY.":E=28:GOTO5
40 Z$="IN MEDIEVAL VALLEY.":E=28:GOTO5
41 Z$="IN TERROR VALLEY.":W=28:GOTO5
42 Z$="IN A HALLWAY.":W=33:S=30:U=34::GOTO5
43 Z$="IN A SMALL CHAMBER.":E=32:GOTO5
44 Z$="IN A TOWER":D=32:GOTO5
45 Z$="IN A VERY SMALL ROOM.":W=34:GOTO5
46 Z$="ON A GREEN PASTURE.":S=24:GOTO5
47 Z$="IN A LAUNDRY ROOM.":N=38:E=25:GOTO5
48 Z$="IN A PANTRY.":S=37:GOTO5
49 Z$=J$+" THE LAND OF ZARTZ.":N=40:S=19:GOTO5
50 Z$="AT WOODS JUNCTION.":W=41:N=45:E=49:S=39:GOTO5
51 Z$="IN INSECT WOODS.":W=43:N=44:E=40:S=42:GOTO5
52 Z$="NEAR A SMALL POND.":N=41:GOTO5
53 Z$="IN A FOREST.":W=43:N=44:E=41:S=43:GOTO5
54 Z$="IN A FOREST.":W=43:N=43:E=43:S=41:GOTO5
55 Z$="IN TERROR WOODS.":W=46:N=47:E=48:S=40:GOTO5
56 Z$="NEAR A SMALL PIT.":E=45:GOTO5
57 Z$="IN A FOREST.":S=45:GOTO5
58 Z$="NEAR THE ENTRANCE OF A CAVE":W=45:GOTO5
59 Z$="IN DARK WOODS.":W=40:N=50:E=51:S=52:GOTO5
60 Z$="IN A FOREST.":W=50:N=50:E=51:S=49:GOTO5
61 Z$="IN A FOREST.":W=49:N=50:E=50:S=50:GOTO5
62 Z$="IN A FOREST.":W=50:N=49:E=51:S=52:GOTO5
63 Z$="IN THE BOTTOM OF THE PIT.":W=55:U=46:GOTO5
64 Z$="IN A SMALL STORAGE ROOM.":E=55:GOTO5
65 Z$="IN A TUNNEL.":E=53:GOTO5
66 Z$="INSIDE THE CAVE.":W=48:GOTO5
67 Z$=J$+" THE LAND OF OOLA.":W=19:GOTO5
68 Z$="ON A PLAIN.":W=57:E=59:GOTO5
69 Z$="ON RUGGED LAND.":W=58:N=60:E=65:GOTO5
70 Z$="NEAR A MAGIC GARDEN.":W=75:S=59:GOTO5
71 Z$="ON TOP OF THE HILL.":D=65:GOTO5
72 Z$="IN A BLUE GARDEN.":S=60:GOTO5
73 Z$="IN A GREEN GARDEN.":S=62:GOTO5
74 Z$="AT A DEAD END.":S=68:GOTO5
75 Z$="AT THE BOTTOM OF A HILL.":W=59:GOTO5
76 Z$="IN THE DWARF HOUSE.":S=63:GOTO5
77 Z$="NEAR SOME ANCIENT RUINS.":W=68:GOTO5
78 Z$="IN ANCIENT VALLEY.":N=64:E=67:S=69:GOTO5
79 Z$="AT DRAGON CROSSING.":N=68:GOTO5
80 Z$="IN A WELL-LIT TUNNEL.":W=71:E=69:GOTO5
81 CLS:Z$="TUNNEL CONTINUES.":W=72:E=70:GOTO5
82 CLS:Z$="END OF TUNNEL.":E=71:GOTO5
83 Z$="IN A RED GARDEN.":S=66:GOTO5
84 Z$="STANDING IN THE RUINS.":W=67:GOTO5
85 Z$="OUTSIDE OF A TOOL SHED.":E=60:GOTO5
86 Z$="INSIDE THE TOOL SHED.":E=75:GOTO5
200 DIMA$(62),A(62),B(12),B$(33),C$(5),D(33),H$(62),I(62)
210 FORA=1TO56:READA$(A):READA(A):H$(A)=A$(A):NEXT:FORA=1TO33:READB$(A):NEXT:FORA=13TO33:READD(A):NEXT
220 A=1:G=1:G(4)=1:N$="LOWERED DRAWBRIDGE":J$="ON THE BORDER OF":W$="$$$ BOTTLE OF GOLDEN MILK"
300 N=0:W=0:E=0:S=0:U=0:D=0:Y=0:CLS:IF(DK=0)*(A>5)THENDT=1
305 REM IFA=72THEN82
310 GOTO10
320 CLS0:PRINT"IT'S TOO DARK TO SEE CLEARLY!":U=5:DT=0:GOSUB5200:PRINT"GET A LIGHT!"
350 Z$="":PRINT:IFN>0ORW>0ORE>0ORS>0ORU>0ORD>0THENZ$="SOME EXITS ARE:":FORB=1TO12:B(B)=0:NEXT
360 IFW>0THENZ$=Z$+" WEST":B(1)=W:B(2)=W
361 ON-((A=39)*(PF=0)<>0)GOTO363:IFN>0THENZ$=Z$+" NORTH":B(3)=N:B(4)=N
362 IFE>0THENZ$=Z$+" EAST":B(5)=E:B(6)=E
363 IFS>0THENZ$=Z$+" SOUTH":B(7)=S:B(8)=S
364 IFU>0THENZ$=Z$+" UP":B(9)=U:B(10)=U
365 IFD>0THENZ$=Z$+" DOWN":B(11)=D:B(12)=D
366 GOSUB1:PRINT
367 FORB=1TO62:IFABS(A(B))=ATHENC=C+1:GOTO372
368 NEXTB:IFZ$<>""THENZ$=LEFT$(Z$,LEN(Z$)-2)+".":GOSUB1:PRINT
370 GOTO374
372 IFC=1THENZ$="THINGS YOU SEE HERE: "
373 Z$=Z$+A$(B)+", ":GOTO368
374 IFGO=1THEN6000
375 IFG=1THENZ$="YOU ARE CARRYING A LARGE SACK FOR STORING YOUR TREASURES.":GOSUB1:G=0
376 IFA=11THENPRINT"GROUND IS VERY SOFT."
377 IF(A=31)*(AM=0)THEN1500
378 IF(A=42)*(FR=0)THEN1510
379 IF(A=39)*(DW=0)THEN1520
380 IF(A=47)*(CH=0)THEN1530
381 IF(A=56)*(RS=0)THEN1540
382 IF(A=57)*(TR=0)THEN1550
383 IF(A=69)*(DR=0)THEN1560
384 C=0:PRINT:PRINT"WHAT DO YOU WANT TO DO?":INPUTA$:IFA$="LOOK"THEN300
385 PRINT:FORB=1TO12:IFA$=B$(B)THEN387
386 NEXT:GOTO389
387 IFB(B)<>0THENA=B(B):GOTO300
388 PRINT"YOU CAN'T MOVE THAT WAY.":GOTO384
389 IFA$<>"I"ANDRIGHT$(A$,3)<>"ORY"ANDA$<>"INV"THEN400
390 Z$="YOU ARE CARRYING: A BAG WITH"+STR$(NT)+" TREASURES, "
391 FORK=1TO5:IFC$(K)<>""THENZ$=Z$+C$(K)+", "
392 NEXTK:Z$=LEFT$(Z$,LEN(Z$)-2)+".":GOSUB1:GOTO384
400 FORB=13TO33:F=LEN(B$(B)):IFLEFT$(A$,F)=B$(B)THEN450
401 NEXT
402 Z$="DON'T KNOW WHAT "+CHR$(34)+A$+CHR$(34)+" MEANS.":GOTO384
450 CC=(LEN(A$)-F)-1:IFCC>=0THEND$=RIGHT$(A$,CC):E$=RIGHT$(D$,3):GOTO460
451 GOTO402
460 IFD(B)<>1THEN490
463 FORJ=1TO62:IFE$=RIGHT$(A$(J),3)ANDA=ABS(A(J))THENCC=J:J=62:NEXTJ:J=CC:GOTO470
464 NEXT
465 PRINT"THERE'S NO "D$" HERE.":GOTO384
470 IFSGN(A(J))=-1THENZ$="BE REASONABLE NOW. THAT'S IMPOSSIBLE.":GOSUB1:GOTO384
471 IF((A=31)*(AM=0))+((A=42)*(FR=0))+((A=56)*(RS=0))+((A=47)*(CH=0))THENMM=1:GOTO1000
472 IFI>4THENPRINT"YOU CAN'T CARRY ANYMORE.":GOTO384
473 IFLEFT$(A$(J),1)="$"THENNT=NT+1:A$(J)="":A(J)=0:PRINT"OK!":GOTO5000
474 IFA<>44THEN480
475 IFE$="IES"THENIFI(32)=0THEN1000
476 PRINT"FLIES STICK LIKE GLUE TO FLYPAPER":AF=1:A$(31)="FLIES STUCK ON FLYPAPER":H$(60)=A$(31)
477 A(31)=44:K3=32:R$="":GOSUB1100:M$="":GOSUB1200:GOTO5000
480 PRINT"OK!":FORK=1TO5:IFC$(K)=""THENC$(K)=A$(J):GOSUB3500:H(K)=J:A$(J)="":A(J)=0:I=I+1:GOTO5000
481 NEXT
490 IFD(B)<>2THEN600
500 FORJ=1TO5:IFE$<>RIGHT$(C$(J),3)THEN510
501 CC=J:J=5:NEXTJ:J=CC:GOTO520
510 NEXTJ:PRINT"YOU'RE NOT CARRYING IT.":GOTO384
520 FORK=1TO62:IFA$(K)=""THENA$(K)=C$(J):I=I-1:A(K)=A:H(J)=0:CC=K:K=62:NEXTK:K=CC:GOTO530
521 NEXTK
530 PRINT"OK!":FORK1=1TO62:IFC$(J)=H$(K1)THENC$(J)="":CC=K1:K1=62:NEXTK1:K1=CC:GOTO540
531 NEXTK1
540 IFA=31THEN545
541 GOTO550
545 IFE$<>"DER"THEN550
546 IFAM<>0THEN550
547 AM=1:Z$="HE DROPS HIS AX AND SNEEZES WILDLY. QUICK, GRAB THE COINS!":GOSUB1
548 A(57)=-31:A$(57)="SNEEZING AXMAN":A(58)=31:A$(58)="AX":H$(58)=A$(58):GOTO590
550 IFA=47THEN552
551 GOTO555
552 IFE$<>"ORN"THEN555
553 IFCH=0THENCH=1:Z$="CHICKEN DEVOURS CORN, GETS UP, AND WALKS AWAY.":GOSUB1:A(K)=47:A$(K)="$$$ GOLDEN EGG":GOTO590
555 IFA<>56THEN560
556 IFE$<>"OOD"THEN560
557 IFRS=0THENRS=1:Z$="SNAKES EAT FOOD AND SLITHER INTO A DARK CORNER TO SLEEP.":GOSUB1:A(K)=0:A$(K)="":GOTO590
560 IFE$<>"GAS"THEN565
561 IFI(46)=0THENZ$="WHERE'S YOUR "+H$(46)+"? YOU FELL ASLEEP AND ARE EATEN BY A GIANT MOTH!":GOSUB1:GOTO6100
562 IFA<>69THEN565
563 IFDR=0THENDR=1:PRINT"DRAGON FALLS ASLEEP.":A(K)=-69:A$(K)="SLEEPING DRAGON":GOTO590
565 IFA<>39THEN570
566 IFE$="PIE"THENDW=1:A$(K)="":A(K)=0:Z$="DWARF GULPS THE PIE AND VANISHES.":GOSUB1:PF=1:GOTO590
570 IFA<>42THEN580
571 IFE$<>"PER"THEN580
572 IFFR<>0THEN580
573 IFAF=1THEN575
574 GOTO580
575 FR=1:Z$="FROG LICKS FLIES ON PAPER AND DISAPPEARS INTO THE POND.":GOSUB1
576 A$(K)="LICKED FLIES ON PAPER":H$(61)=A$(K):A(K)=A:GOTO5000
580 IFA<>57THEN590
581 IFE$<>"LAR"THEN590
582 IFI(36)=1THEN585
583 GOTO590
585 Z$="TROLLS GRABS YOUR BUCK, TURNS A CRANK.":GOSUB1
586 TR=1:K3=36:DB=1:A$(39)=N$:R$="":M$="":GOSUB1100:GOSUB1200:GOTO5000
590 I(K1)=A:GOTO5000
600 IFD(B)<>3THEN620
601 IFA=1ANDE$="REE"THENA=2:GOTO300
602 IFA=3ANDE$="AIN"THENA=5:GOTO300
603 IF(A>13)*(A<17)*(E$="REE")THENA=A+6:GOTO300
604 IFA=65ANDE$="ILL"THENA=61:GOTO300
620 IFD(B)<>4THEN630
621 IFI(3)<>1THEN630
622 IFE$="OLE"ORE$="UND"ORE$="NEL"THEN625
623 GOTO630
625 IFA=11THENZ$="YOU DUG A TUNNEL AND CRAWLED UNDER THE DOOR. HOLE MAGICALLY VANISHES.":GOSUB1:A=14:GOTO5000
626 PRINT"YOU FOUND NOTHING OF VALUE":GOTO5000
630 IFD(B)<>11THEN700
631 IFA=5ANDE$="OLE"THENA=6:Y=1:GOTO653
632 IFA=17ORA=75ANDE$="HED"THENA=A+1:Y=1:GOTO653
633 IF((A=24)+(A=63))ANDE$="USE"THENA=A+INT(A/10)/2:Y=1:GOTO653
634 IFA=30ANDE$="TLE"THENA=32:Y=1:GOTO653
635 IFA=48ANDE$="AVE"THENA=56:Y=1:GOTO653
636 GOSUB7000
653 IFA=46ANDE$="PIT"THENA=53:Y=1:GOTO660
654 IFA=67ANDE$="INS"THENA=74:Y=1
660 IFA=11ORA=34ORA=55ORA=60ORA=62ORA=66THEN662
661 GOTO670
662 IFE$="OOR"THENIFG((A/10-INT(A/10))*10)=0THENPRINT"THE DOOR'S LOCKED.":Z=1:GOTO5000
663 IFD(B)=5THENRETURN
664 IFA=60THENA=62:GOTO300
665 IFA=66THENA=73:GOTO300
666 IFA=55THENA=54:GOTO300
667 A=A+1:GOTO300
670 IFA=69THEN672
671 GOTO680
672 IFE$<>"AVE"THEN680
673 IFDR=1THENA=70:GOTO300
674 Z$="DRAGON WON'T LET YOU. HE SURE LOOKS MEAN.":GOSUB1:GOTO5000
680 IFA=57THEN682
681 GOTO690
682 IFE$<>"DGE"THEN690
683 IF(DB=1)*(TR=1)THENPRINT"YOU CROSS THE DRAWBRIDGE":A=58:GOTO5000
684 Z$="DRAWBRIDGE IS UP! TROLL SAYS, "+CHR$(34)+"PAY ME A BUCK!"+CHR$(34):GOSUB1:GOTO5000
690 IFA=61THEN692
691 GOTO699
692 IFE$<>"DER"THEN699
693 IFFA=0THENZ$="GLIDER WON'T FLY! THERE'S NO WIND!":GOSUB1:GOTO5000
694 Z$="GLIDER SWOOPS YOU INTO THE VALLEY BELOW. IT DROPS YOU OFF AND A GUST OF WIND CARRIES IT AWAY.":GOSUB1
695 GOSUB5200:A=68:GOTO300
699 IFY=1THEN300
700 IFD(B)<>5THEN800
701 IFA=11ORA=55THEN660
702 IF((I(10)=1)*(A=66))OR((I(13)=1)*(A=62))OR((I(14)=1)*(A=60))THEN705
703 GOTO710
705 GOSUB4000:G((A/10-INT(A/10))*10)=1:PRINT"DOOR'S OPEN.":GOTO5000
710 IF(A=60)+(A=62)+(A=66)THENPRINT"GET THE RIGHT KEY!":GOTO5000
800 IFD(B)<>9THEN830
801 IFA=25THEN805
802 GOTO810
805 IFE$<>"PIE"THEN810
806 IFPE<>0THEN810
807 IF((I(25)=1)+(I(25)=25))*((I(21)=25)+(I(21)=1))THEN820
808 GOTO1000
810 GOTO830
820 Z$="YOU HAVE JUST BAKED A RHUBARB PIE.":GOSUB1:PE=1:A$(59)="RHUBARB PIE"
821 H$(59)=A$(59):A(59)=25:K3=21:R$="":GOSUB1100:K3=25:GOSUB1100
822 M$="":K3=21:GOSUB1200:K3=25:GOSUB1200:GOTO5000
830 IFD(B)<>6THEN850
831 IFE$<>"OTE"THEN850
832 IFI(12)=AORI(12)=1OR(A=20)*(I(12)=0)THEN835
833 GOTO850
835 Z$="NOTE SAYS: "+CHR$(34)+"THERE ARE 17 TREASURES HIDDEN. CAN YOU FIND THEM ALL?"+CHR$(34):GOSUB1:GOTO384
850 IFD(B)<>7THEN900
851 IFA<>36THEN900
852 IFE$<>"COW"THEN900
853 IFI(23)=36AND(I(24)=36ORI(24)=1)THEN855
854 GOTO900
855 IFI(19)=36THEN860
856 PRINT"COW IS HUNGRY!":GOTO5000
860 Z$="WOW! COW GIVES GOLDEN MILK. FILL YOUR BOTTLE UP!":GOSUB1:K3=24:R$="":GOSUB1100
861 K3=19:M$=W$:GOSUB1200:K3=24:M$="":GOSUB1200:GOTO5000
900 IFD(B)<>10THEN930
901 IFE$<>"PIE"THEN930
902 IFI(59)=1ORI(59)=AOR((A=25)*(I(59)=0))THEN905
903 GOTO930
905 Z$="YOU CAN'T STAND THE TASTE OF RHUBARB! BESIDES, YOU'RE NOT HUNGRY.":GOSUB1:GOTO5000
930 IFD(B)<>13THEN950
931 IFE$<>"IGS"THEN950
932 IFI(2)=1THEN935
933 GOTO950
935 DK=1:R$="BURNING TWIGS":K3=2:GOSUB1100:H$(2)=R$
936 Z$="GOOD DEDUCTION! YOU HAVE CREATED A SMALL FIRE AND YOU CAN SEE!":GOSUB1:GOTO5000
950 IFD(B)<>15THEN960
951 IFA<>55THEN960
952 IFI(58)=1THENIFG(5)=0THENZ$="WHACK!! DOOR SPLINTERS IT'S OPEN":GOSUB1:G(5)=1:X=35:GOSUB4100:GOTO5000
960 IFD(B)<>12THEN970
961 IFE$<>"TON"THEN970
962 IF(A=61)*(FA=0)THEN965
963 IFA=72THENA=1:GO=1:GOTO300
964 GOTO970
965 FA=1:Z$="FAN SPINS. IT'S GETTING WINDY!":GOSUB1:A$(55)="SPINNING FAN":H$(55)=A$(55):GOTO5000
970 IFD(B)<>14THEN1000
971 IFE$="LAR"ORE$="OLL"THENIFI(36)=1THENIFA=57THENE$="LAR":GOTO580
1000 PRINT"YOU CAN'T DO THAT HERE."
1010 IFMM=1THENPRINT"YOU MIGHT GET KILLED!":MM=0
1020 GOTO384
1100 FORK2=1TO5:IFC$(K2)=H$(K3)THENC$(K2)=R$:GOSUB1150:CC=K2:K2=5:NEXTK2:K2=CC:RETURN
1101 NEXTK2:RETURN
1150 IFR$=""THENI=I-1:I(K3)=0
1160 RETURN
1200 FORK2=1TO62:IFA$(K2)=H$(K3)THENA$(K2)=M$:IFM$<>""THENCC=K2:K2=62:NEXTK2:K2=CC:RETURN
1201 A(K2)=0:I(K3)=0
1210 NEXTK2:RETURN
1499 GOTO5000
1500 Z$="THERE IS AN AX-WIELDING MADMAN IN THE VALLEY GUARDIN THE COINS. HE IS SWINGING HIS AX MADLY.":GOSUB1:GOTO384
1510 Z$="THERE IS A GIANT KILLER FROG IN THE POND.":GOSUB1:GOTO384
1520 Z$="THERE IS A PATH NORTH, BUT A WICKED PIE-EATING DWARF BLOCKS YOUR WAY.":GOSUB1
1521 Z$="HE SAYS, "+CHR$(34)+"GIMME A PIE."+CHR$(34):GOSUB1:GOTO384
1530 Z$="THERE IS A GIANT CHICKEN IN THE FOREST. HE SEEMS TO BE HIDING SOMETHING. "
1531 Z$=Z$+"HE LOOKS HUNGRY.":GOSUB1:GOTO384
1540 Z$="A PAIR OF HISSING RATTLESNAKES GUARD THE STATUE. WATCH YOUR STEP. "
1541 Z$=Z$+"THEY HAVEN'T EATEN TODAY YET.":GOSUB1:GOTO384
1550 Z$="A FAT TROLL STANDS GUARD. "+CHR$(34)+"PAY THE TROLL,"+CHR$(34)+" HE SAYS.":GOSUB1:GOTO384
1560 Z$="THE ENTRANCE TO THE CAVE IS GUARDED BY A MEAN DRAGON. HE IS WIDE AWAKE.":GOSUB1:GOTO384
2000 DATATREE,-1,2 SMALL TWIGS,2,SHOVEL,4,DARK HOLE,-5,LOCKED DOOR,-11,RED TREE,-14
2001 DATABLUE TREE,-15,GREEN TREE,-16,WOOD SHED,-17,RED KEY,18,$$$ VALUABLE COINS,31
2002 DATAPAPER NOTE,20,GREEN KEY,21,BLUE KEY,22,FARMHOUSE,-24,GOLDEN COW,-36
2005 DATASNEEZING POWDER,26
2010 DATAGIANT CORN,27,GOLDEN GRASS,29,CASTLE,-30,PIE SHELL,38,OVEN,-25,MILKING STOOL,37
2011 DATAEMPTY BOTTLE,37,PIE FILLING,38,$$$ SILVER CROWN,33,OPEN DOOR,-34,$$$ PEARL NECKLACE,35
2012 DATA$$$ RARE BOOK,35
2020 DATA$$$ GOLDEN LILY PAD,42,SWARM OF FLIES,44,FLYPAPER,50,$$$ EMERALD,51,SNAKE FOOD,52
2021 DATALOCKED DOOR,-55,SILVER DOLLAR,54,$$$ BAR OF GOLD,54,$$$ GOLDEN STATUE,56
2030 DATA"RAISED DRAW BRIDGE",-57,"BLUE LOCKED DOOR",-60,"$$$ RARARE STAMPS",62
2031 DATA"$$$ ANCIENT VASE",62,"GREEN LOCKED DOOR",-62
2035 DATA"DWARF'S HOUSE",-63,"$$$ GOLD WATCH",73,"GAS MASK",64,"SLEEPING GAS",64,"CAVE",-69
2040 DATAPUSH BUTTON,-72,$$$ PEARL,66,$$$ DIAMOND,66,RED LOCKED DOOR,-66,$$$ RARE FOSSIL,74
2041 DATAHANGGLIDER,-61,BIG FAN. PUSH BUTTON ON FAN,-61,$$$ RING,76
2100 DATAWEST,W,NORTH,N,EAST,E,SOUTH,S,UP,U,DOWN,D,GET,TAKE,DROP,PUT,GIVE,CLIMB,DIG,OPEN,READ
2101 DATAMILK,BAKE,EAT,GO,ENTER,PRESS,PUSH,RUB,PAY,HIT,BREAK,SMASH
2110 DATA1,1,2,2,2,3,4,5,6,7,9,10,11,11,12,12,13,14,15,15,15
3000 PRINT"WHAT?":RESUME5100
3500 FORK4=1TO62:IFA$(J)=H$(K4)THENI(K4)=1:CC=K4:K4=62:NEXTK4:K4=CC:GOTO3600
3501 NEXT
3600 RETURN
4000 IFA=60THENX=40:GOTO4100
4001 IFA=62THENX=43:GOTO4100
4002 X=52
4100 A$(X)="OPEN DOOR":RETURN
5000 IF(Z=1)*(A=55)THENZ$="BUT IT LOOKS LIKE IF AN AX MIGHT SMASH THROUGH.":GOSUB1:Z=0
5100 GOSUB5200:GOTO300
5200 FORZZ=1TO2200:NEXTZZ:RETURN
6000 Z$="CONGRATULATIONS! YOU'VE MADE IT ALL THE WAY THROUGH AND BACK.":GOSUB1
6001 IFNT=17THENZ$="YOU FOUND ALL 17 TREASURES.":GOSUB1:GOTO6100
6050 Z$="YOU ONLY FOUND"+STR$(NT)+" TREASURES, HOWEVER. THERE ARE"+STR$(17-NT)+" STILL OUT THERE SOMEWHERE."
6051 GOSUB1
6100 PRINT"TO PARTAKE ANOTHER JOURNEY":INPUT"HIT enter";A$:RUN
7000 IFA=65ANDE$="ILL"THENA=61:Y=1
7010 RETURN
8000 REM MAGICAL JOURNEY
8001 REM BY PETER KIRSCH
8002 REM FINAL VERSION
8003 REM FEB. 1980
