0 CLS:CLEAR1000:DIMZZ,CC,M$:GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 CLS
20 PRINT@201,"mars adventure":POKE1024+205,32:PRINT@260,"BY AARDVARK LTD. SOFTWARE":PRINT@356,"MC-10 EDITS BY JIM GERRIE"
25 FORQ=1TO2000:NEXTQ:CLS
100 DIMD$(18),O$(54),L(248),P(18,20):L=1:L2=1
110 FORX=1TO18:READD$(X):NEXT
120 DATAA CONTROL ROOM,AN ENGINE ROOM,A BARREN FIELD,A MOAT
130 DATAA PLAZA,AN ANCIENT TEMPLE,A DRY WELL,A DESERTED ROOM,A GARDEN,AN OBSERVATORY
150 DATAA KITCHEN--I THINK,A MAZE OF HALLS,A DANK CELLAR,A SECRET PASSAGE
160 DATAXPTL'S SHOP OF MATING SCENTS
170 DATAA MARTIAN HABITAT,AN OPENNING OVERLOOKING THE CITY,THE SANDSLED
180 FORX=1TO54:READO$(X):O$=O$+LEFT$(O$(X),2):IFX>6THENREADL(X-6)
200 NEXT:DATANORTH,SOUTH,EAST,WEST,UP,DOWN
210 DATANETS,2,FASTGRO,20,HOLY GRAIL,5,SEEDS,20
220 DATAALCHOHOL,20,BRASS TUBING,13,STATUE OF FLUTE PLAYER,3
230 DATA GOLD BARS,13,TORCH,6
240 DATAINJECTOR PUMP,7,HARMONICA,20,LIGHTER,20,SNEAKERS,19,TABLE,15,HELMET,20
260 DATAMARTIAN,20,TIMEPIECE,19,BILL OF MATERIALS FOR STILL,19,JEWELED CLUB,18
280 DATAOXYBOTTLE,19,AIRROCKS,3,FLASKS,15,TEXTBOOK,1,CRATES--CLOSED,19
290 DATATRAPDOOR--OPEN,20,MIRROR ON WALL,8
300 DATAROPE,17,SIGN,5,RUG,8,WATER,4,ORGAN,8,FIRE,20,(SIGN) USE FERTILIZER,9
320 DATABUTTON--RED PUSH TYPE,1,SANDSLED,3,OVEN,11,KETTLE,11
330 DATACONTROL PANEL,1,RACK--SAYS PLACE INJECTOR HERE,2
340 DATALOW-OX WARNING--ON,2,RECHARGING STATION,2,STATUE ON DIAS,6
350 DATARAMP,7,LOCKER,2,DRY WELL,6,VIEWPORT,1,(SIGN) BRING FUEL HERE,2
360 DATAPOCKETS,20
370 FORY=1TO18:FORX=1TO6:READP(Y,X):NEXTX,Y
380 DATA,,,,,2,,,3,,1,,4,4,5,2,,,3,3,4,4,4,4
390 DATA6,15,8,3,,,,5,9,,,,,,12,,,
400 DATA,,,5,,,,,,6,,,,,,17,
410 DATA14,,16,,,,,,,,,,,,,12,,,
420 DATA,,,10,,8,5,,,,16,,11,,,,,15
430 DATA,,10,,,,,,,,,3
440 W$="GOTALODRHIOPTILIUNPUPLGEREBR":GOTO760
460 IFL=12THENFORX=1TO6:P(12,X)=0:NEXT:P(12,INT(2*RND(0))+1)=13
470 P(12,(INT(RND(0)*2)+3))=7:IFL=2THENO=0
480 IFL(4)<>11ORL(6)<>11ORL(32)<>11ORL(30)<>11ORGR=0THEN500
490 O$(43)="STILL":IFL(5)=20THENL(5)=11
500 PRINT:INPUT"TELL ME WHAT TO DO";A$:PRINT:IFLEFT$(A$,4)="HELP"THEN1800
505 B$="":FORX=1TOLEN(A$)
510 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
520 NEXT:F=0:S=0:GOSUB1660
540 L2=L:IFB$="TU"THENB$="BR"
560 IFB$="WH"THENB$="SE"
570 IFB$="GR"THENB$="HO"
590 IFB$="CL"THENB$="JE"
600 G=G+1:IFG<60ORRND(0)<.9ORL(16)<>20THEN630
610 X=INT(RND(0)*31):IFL(X)<>0ANDL(X)<>20THENL(X)=INT(RND(0)*18+1)
620 PRINT"I HEAR A FAINT RUSTLING SOUND"
630 IFL(16)=20THENIFRND(0)>.9THENPRINT"I AM BEING WATCHED":FORZZ=1TO500:NEXT
640 IFL(4)=9THENIFL(30)=9THENIFL(2)=9THENGR=1:O$(10)="WHEAT":L(30)=4
650 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2:IFF=12THENF=2
660 NEXT:FORX=1TOLEN(O$)STEP2:IFMID$(O$,X,2)=B$THENS=(X+1)/2
670 NEXT:IFA$="IN"THEN1030
680 IFM=4ANDF<>2THENM=0:PRINT:PRINT"HE LEFT":L(16)=20:PRINT
690 IFL>2THENO=O+1:IFO>50THENPRINT"LOW OX--RECHARGE"
700 IFL>2ANDL(20)<>0THENO=O+7
710 IFO>63ANDL>2THENPRINT"GASP GASP! NO AIR! DEAD":END
720 IFF<1THENPRINT"HUH?":GOTO460
730 ONF-1GOTO820,1060,1230,1390,1450,1490,1530,1590,1600,1680,820,1060,1390
740 IFS<1ORS>6ORL=6ANDS=4THEN1270
750 IFP(L,S)THENL=P(L,S):L2=L
760 M$="I AM IN "+D$(L):GOSUB1:PRINT:Y=0
770 IFLI=0AND(L=7ORL=13ORL=14)THENPRINT"DARK--CAN'T SEE!":GOTO460
780 PRINT"I SEE:":A$="":FORX=1TO47:IFLEN(A$)>223THENM$=LEFT$(A$,LEN(A$)-2):GOSUB1:A$=""
785 IFL(X)=L2THENA$=A$+O$(X+6)+", ":Y=Y+1
790 NEXTX:IFY=0THENPRINT"NOTHING":GOTO800
795 M$=LEFT$(A$,LEN(A$)-2):GOSUB1
800 PRINT:PRINT"OBVIOUS EXITS ARE ":M$="":FORX=1TO6:IFP(L,X)>0THENM$=M$+O$(X)+" * "
810 NEXTX:GOSUB1:PRINT:GOTO460
820 IFS<6THENPRINT"HUH?":GOTO460
830 IFS=11OR(S=22ANDL(16)=L)THEN940
850 IFL(S-6)<>L THEN PRINT"I DON'T SEE IT HERE":GOTO460
860 IFC>6THENPRINT"CAN'T. I'M CARRYING TOO MUCH":GOTO460
870 IFS>37THENPRINT"I CAN'T TAKE THAT":GOTO460
880 IFS>31THENONS-31GOTO900,910,920,930,940
890 L(S-6)=0:C=C+1:GOTO460
900 M$="IT'S GLUED TO THE WALL--BUT IT'S FRAGILE":GOSUB1:GOTO460
910 PRINT"SIGN FELL":L(27)=0:GOTO460
920 M$="IT'S TIED TO A LEDGE ON THE SECOND FLOOR":GOSUB1:GOTO460
930 L(29)=0:IFL=8THENPRINT"AHA!":L(25)=8:GOTO460
940 INPUT"IN WHAT";A$:GOSUB1660
950 IFS=22AND(A$<>"NE"ORL(1)<>0)THENPRINT"GOT AWAY":L(16)=20:GOTO460
960 IFS=22THEN1740
970 IFS=11ANDA$<>"FL"THENPRINT"IT EVAPORATED":GOTO460
980 IFS=11THENL(5)=0:GOTO460
990 IFA$="FL"ANDL(22)=0THENPRINT"IT DISSOLVED":L(22)=15:GOTO460
1000 IFA$="HO"ORA$="GR"THENM$="IT LEAKED OUT--IT'S A HOLEY GRAIL":GOSUB1:GOTO460
1010 IFA$="HE"ANDL(15)=0THENL(30)=0:GOTO460
1020 PRINT"I DON'T THINK THAT WILL WORK":GOTO460
1030 PRINT"I AM CARRYING: ":FORX=1TO34:IFL(X)=0THENPRINTO$(X+6)
1050 NEXT:PRINT:GOTO460
1060 IFB$=""THEN760
1065 IFS<7THENIFP(L,S)>0THENPRINTD$(P(L,S)):GOTO460
1070 IF(S=29ORS=24ORS=9ORS=23)THENIFL(S-6)<>0THEN1220
1080 IFS<>32ORL<>8THEN1100
1090 M$="I SEE RUGGED PERSON IN POCKETED SURVIVAL SUIT":GOSUB1:GOTO500
1100 IFS=54ANDL(11)=20THENPRINT"A HARMONICA AND A LIGHTER":L(11)=0:L(12)=0:GOTO460
1120 IFS=4ANDL=6THENPRINT"THERE IS SOMETHING THERE":GOTO460
1130 IFS=34THENPRINT"EARTHY GO HOME!!":GOTO460
1140 IFS=41ANDL=3THENL2=18:GOTO780
1150 IFS=52ANDL=1THENL2=3:GOTO780
1160 IFS=9THENPRINT"SEEDS!":L(4)=L:GOTO460
1180 IFS=29THENM$="SUGGESTED FUELS: HYDRAZINE OR ALCHOHOL":GOSUB1:GOTO460
1190 IFS=24THENM$="WATER, POT, TUBING, FIRE, GRAIN":GOSUB1:GOTO460
1200 IFS=23THENPRINTCHR$(8);60-O"MINUTES OF AIR LEFT":GOTO460
1210 PRINT"NOTHING SPECIAL":GOTO460
1220 PRINT"DON'T HAVE IT":GOTO460
1230 IFB$=""ORS<6THENPRINT"HUH?":GOTO460
1240 IFL(S-6)<>0THEN1220
1250 IFB$="RU"ANDL=8THENL(25)=20
1260 L(S-6)=L:C=C-1:GOTO460
1270 IFS=41ANDL=3THENL=18:L2=L:GOTO760
1280 IFL<>6THEN1310
1290 IFS=4ANDDR=0THENPRINT"I FELL IN AND DIED":END
1300 IFS=4THENL=7
1310 IFL(25)=8ANDS=31ANDL(13)<>0THEN1370
1320 IFS=31ANDL=8THENL=7:L2=L:GOTO760
1330 IFS=49ANDL=7ANDL(29)=8THENPRINT"BLOCKED":GOTO460
1340 IFS=49ANDL=7ANDC>1THENPRINT"CARRYING TOO MUCH":GOTO460
1350 IFS=49ANDL=7THENL=8:L2=L:GOTO760
1360 GOTO460
1370 M$="I SLIPPED DOWN AN INCLINE AND DIED":GOSUB1:END
1380 GOTO460
1390 INPUT"WITH WHAT";A$:GOSUB1660:IFA$="JE"THENA$="CL"
1400 IFA$="FI"ORA$="HA"ORA$="FO"THENPRINT"YOU'RE KIDDING!":GOTO460
1410 IFS>32ORS<30ORA$<>"CL"ORL(19)<>0THENPRINT"NOTHING YET":GOTO460
1420 IFS=32THENO$(32)="BROKEN MIRROR":P(8,5)=14:GOTO460
1430 IFS=30THENO$(30)="SPLINTERS":L(2)=L
1440 GOTO460
1450 IFS=30THENPRINT"GLUED":GOTO460
1460 IFS<>50ORL<>2THEN460
1470 FORX=1TO40:IFL(X)=19THENL(X)=2
1480 NEXT:GOTO460
1490 IFS<>33THEN1520
1500 INPUT"TO WHAT";A$:GOSUB1660
1510 IFA$="ST"THENR=1:P(6,6)=7:P(7,5)=6:GOTO460
1520 PRINT"CANT":GOTO460
1530 IFS=15ANDL(9)=0THENLI=1:GOTO460
1540 IFS<>38THENPRINT"CAN'T":GOTO460
1550 IFL<>11THENPRINT"NOT HERE--ISN'T SAFE":GOTO460
1560 IFL(14)<>11THENPRINT"NOT ENOUGH WOOD":GOTO460
1570 IFL(21)<>11THENPRINT"NOT ENOUGH AIR":GOTO460
1580 L(32)=11:GOTO460
1590 LI=0:GOTO460
1600 IFS<>40THENPRINT"NOTHING HAPPENED":GOTO460
1610 IFL(5)<>2THENPRINT"COMPUTER REPORTS: NO FUEL"
1620 IFL(10)<>2THENPRINT"NO INJECTOR PUMP"
1630 IFL(8)<3ORL(19)<3THENPRINT"TOO HEAVY FOR TAKEOFF":GOTO460
1640 IFL(5)=2ANDL(10)=2THEN1750
1650 GOTO460
1660 IFLEN(A$)>1THENA$=LEFT$(A$,2)
1670 RETURN
1680 IFS<>17ORL(11)<>0THENPRINT"CAN'T":GOTO460
1690 M=M+1:M$="THE RUSTLING SOUND IS GETTING LOUDER":GOSUB1
1700 IFM>2THENPRINT"....MUCH LOUDER!"
1710 IFM<4THEN460
1720 PRINT:PRINT"A MARTIAN JUST WALKED IN":L(16)=L:GOTO460
1740 O$(7)="NETFUL OF MARTIAN":M=5:L(16)=21:L(15)=L:GOTO460
1750 GOSUB2000:PRINT"CONGRATULATIONS!"
1760 M$="YOU HAVE COMPLETED THE MARS ADVENTURE!":GOSUB1:END
1800 IFL=1THENPRINT"LOOK AROUND THE SHIP":GOTO460
1810 IFL=8THENPRINT"NOT ALL EXITS ARE OBVIOUS":GOTO460
1820 IFL(15)=20ANDRND(0)>.5THENPRINT"WHAT'S THAT RUSTLING?"
1830 M$="WHY ASK ME FOR HELP--IT'S YOUR ADVENTURE":GOSUB1:GOTO460
2000 FORZZ=1TO13:PRINT:SOUNDZZ,1:NEXT
2100 PRINT"               ^":SOUND14,1
2110 PRINT"              / "CHR$(92):SOUND15,1
2120 PRINT"             /   "CHR$(92):SOUND16,1
2130 PRINT"            /     "CHR$(92):SOUND17,1
2140 PRINT"           /       "CHR$(92):SOUND18,1
2160 PRINT"           !-------!":SOUND19,1
2170 PRINT"           !       !":SOUND20,1
2180 PRINT"           !       !":SOUND21,1
2190 PRINT"           !   E   !":SOUND22,1
2200 PRINT"           !   A   !":SOUND23,1
2210 PRINT"           !   R   !":SOUND24,1
2220 PRINT"           !   T   !":SOUND25,1
2230 PRINT"           !   H   !":SOUND26,1
2240 PRINT"           !       !":SOUND27,1
2250 PRINT"           !   E   !":SOUND28,1
2260 PRINT"           !   X   !":SOUND29,1
2270 PRINT"           !   P   !":SOUND30,1
2280 PRINT"           !   R   !":SOUND31,1
2290 PRINT"           !   E   !":SOUND32,1
2300 PRINT"           !   S   !":SOUND33,1
2310 PRINT"           !   S   !":SOUND34,1
2320 PRINT"           !       !":SOUND35,1
2330 PRINT"          /!!!!!!!!!"CHR$(92):SOUND36,1
2340 PRINT"          !!!!!!!!!!!":SOUND37,1
2350 PRINT"          !         !":SOUND38,1
2360 PRINT"          !         !":SOUND39,1
2360 PRINT"          !         !":SOUND40,1
2380 PRINT"          !         !":SOUND41,1
2390 PRINT"          !         !":SOUND42,1
2400 PRINT"          !         !":SOUND43,1
2410 PRINT"          !         !":SOUND44,1
2420 PRINT"          !         !":SOUND45,1
2430 PRINT"          !!!!!!!!!!!":SOUND46,1
2440 PRINT"          !         !":SOUND47,1
2450 PRINT"          !         !":SOUND48,1
2460 PRINT"          !         !":SOUND49,1
2470 PRINT"          !         !":SOUND50,1
2480 PRINT"          !         !":SOUND51,1
2490 PRINT"         /-----------"CHR$(92):SOUND52,1
2500 PRINT"        ///////!"CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92):SOUND53,1
2510 PRINT"       ////////!"CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92)CHR$(92):SOUND54,1
2520 PRINT"       !!!!!!!!!!!!!!!!!":SOUND55,1
2530 PRINT"      /-----------------"CHR$(92):SOUND56,1
2540 PRINT"      !!!!!!!!!!!!!!!!!!!":SOUND57,1:FORZZ=58TO92:PRINT:SOUNDZZ,1:NEXT
2550 RETURN
