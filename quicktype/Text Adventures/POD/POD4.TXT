0 CLEAR500:CLS:DIMZZ,CC,M$,I$:GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 NR=22:NT=31:NP=13:NV=33:NO=NT+NP
20 DIMAC(NT,8),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV),TF(NT)
30 M$="WELCOME TO the planet of death.":GOSUB1
40 M$="(C) 1981 BY 'ARTIC COMPUTING'. PORTED TO MC-10 BY JIM GERRIE.":GOSUB1
60 M$="IN THIS ADVENTURE YOU FIND YOURSELF STRANDED ON AN ALIEN PLANET. YOUR AIM "
70 M$=M$+"IS TO ESCAPE FROM THIS PLANET BY FINDING YOUR, NOW CAPTURED AND DISABLED, SPACE SHIP.":GOSUB1
80 M$="YOU WILL MEET VARIOUS HAZARDS AND DANGERS ON YOUR ADVENTURE, "
90 M$=M$+"SOME NATURAL, SOME NOT, ALL OF WHICH YOU MUST OVERCOME TO SUCCEED." :GOSUB1
95 M$="GOOD LUCK, YOU WILL NEED IT!":GOSUB1
99 REM *INITIALIZATION*
100 RM=1:CF=0:RT=-1:WD=-1:LI=0:BT=-1
120 FORI=1TONR:READRM$(I):NEXT
130 FORI=1TONV:READVB$(I),VN(I):NEXT
140 FORI=1TONO:READOB$(I):NEXT
150 FORI=1TONT:READTD$(I),AC(I,8),TF(I):NEXT
160 FORI=1TONR:READAC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT
170 PRINT"PRESS ANY KEY TO START";
180 M$=INKEY$:IFM$=""THEN180
190 CLS
199 REM *MAIN LOOP: ROOM DESCRIPTION* 
200 IF WD=0 THEN 400 
210 WD=0:M$=RM$(RM):GOSUB1
220 K=0
230 I$="":FOR I=1 TO NT
240 IFAC(I,8)<>RMTHEN270 
250 IF K=0 THEN I$=I$+"I CAN ALSO SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXT I:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
300 I$="OBVIOUS EXITS ARE: "
310 FOR I=1 TO 6 
320 IF AC(RM,I)<>0 THEN I$=I$+VB$(I)+", " 
330 NEXT I
340 IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
399 REM *MAIN LOOP: COMMAND INPUT AND PARSER* 
400 PRINT"TELL ME WHAT TO DO.":INPUT C$
410 L=LEN(C$):IF L=0 THEN 400
420 C1$="":C2$="":C2=0:X=0
430 FOR I=1 TO L
440 A$=MID$(C$,I,1):A=ASC(A$):IF A>=97 AND A<=122 THEN A$=CHR$(A-32)
450 IF A$<>" " THEN 460
455 IF C2$<>"" THEN 500 
457 X=1:GOTO490
460 IF X=0 THEN C1$=C1$+A$:GOTO490
470 C2$=C2$+A$ 
490 NEXT I
500 IFC1$="" THEN PRINT "EXCUSE ME?":GOTO400
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FORC1=1TONV
530 IFVB$(C1)=C1$THENC1=VN(C1):GOTO600 
540 NEXTC1
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700 
610 FORC2=1TONO
620 IFOB$(C2)=C2$THENTM=C2:C2=NO:NEXT:C2=TM:GOTO700
630 NEXTC2
635 IFC1=11THENC2=1:GOTO700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO400
699 REM *PRELIMINARY FILTERING OF COMMANDS*
700 IFC2>NT+7ANDC1<>7ANDC1<>11THEN640
710 IFC2<=NT+7ANDC2>NT THENIFC1<>11THEN8010
720 IFC2>NT THEN750
725 TL=AC(C2,8)
730 IF C1<>11 AND TL<>RM AND TL<>0 THEN PRINT "IT ISN'T HERE.":GOTO400
750 ON C1 GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400
760 ON C1-10 GOTO1500,1800,1900,2000,2100,2200,2300,2400,2500,9999
770 ON C1-20 GOTO2600,2700,2800,2900,3000,3100,3200
999 REM *DIRECTIONS*
1000 IF(RM=5ORRM=2)AND(C1=4ORC1=6)ANDAC(RM,C1)=0THENM$="IT'S TOO WIDE AND TOO STEEP. I FELL AND BROKE MY NECK.":GOSUB1:END
1005 IFAC(RM,C1)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO200
1010 IFRM=5ANDC1=2ANDAC(6,7)=0THENPRINT"BRRR. THE WATER IS TOO COLD.":GOTO200
1080 RM=AC(RM,C1):WD=-1:IFRM=20THEN1600
1090 GOTO8200
1099 REM *GO*
1100 IFC2=0THEN8050
1110 IFC2=31THENM$="I WOULD KILL MYSELF IF I DID.":GOSUB1:GOTO8200
1120 IFC2<=NTTHEN8040
1190 C1=C2-(NT+7):GOTO1000
1199 REM *WEAR*
1200 IFC2<>2ANDC2<>11THEN8010
1210 IFAC(11,8)=0THENAC(5,7)=1
1220 IFAC(2,8)=0THENAC(6,7)=1
1290 GOTO8000
1299 REM *DANCE* 
1300 IFRM<>16THEN8020
1310 IFAC(9,8)<>0THENM$="THE FORCE FIELD WAS AFFECTED, BUT I FELL AND KNOCKED MYSELF OUT.":GOSUB1:WD-1:GOTO8200
1315 IFAC(7,7)<2THENM$="THE FORCE FIELD IS STILL TOO STRONG. I FELL AND KNOCKED MYSELF OUT.":GOSUB1:WD=-1:RM=20:GOTO8200
1320 RM=17:WD=-1:GOTO8000
1399 REM *USE*
1400 IFC2=0THEN8050
1405 IFTL<>0THENPRINT"YOU DON'T HAVE IT.":GOTO8200
1410 IFRM=2ANDAC(30,8)=0THENAC(RM,4)=13:AC(RM,6)=13:AC(13,5)=2:AC(30,8)=-1:AC(2,7)=AC(2,7)-1:GOTO8000
1415 IFRM=5ANDAC(13,8)=0THENAC(RM,4)=7:AC(RM,6)=7:AC(13,8)=-1:AC(2,7)=AC(2,7)-1:GOTO8000
1420 IFRM=12ANDAC(16,8)=0THENRM=13:WD=-1:GOTO8000
1430 IFC2=5THENPRINT"TO DO WHAT?":GOTO8200
1470 IFC2=15ORC2=18ORC2=25THEN8010
1480 IFC2=20ORC2=24ORC2=20THEN1100
1490 GOTO8030
1499 REM *TYPE* 
1500 IFC2=0THEN8050
1510 IFRM<>15THEN8030
1520 IFC2=32THENM$="COMPUTER: 1 ALARM 2 FIELD 3 SPEAKER 4 DIRECTIONS":GOSUB1:GOTO8200
1530 IFC2=33ORC2=34ORC2=35THENAC(8,7)=AC(8,7)+1:M$="COMPUTER: LOGIN REQUIRED.":GOSUB1:GOTO8200
1535 IFAC(8,7)>2THENAC(8,7)=0:GOTO1600
1540 IFC2=36THENM$="COMPUTER: 2 WEST, 2 SOUTH FOR SPACE FLIGHT.":GOSUB1:GOTO8200
1550 M$="COMPUTER: SN ERROR-- FOR HELP TYPE 'HELP'":GOSUB1:GOTO8200
1600 PRINT"AN ALARM SOUNDS!":WD=-1:RM=20:GOTO8200
1799 REM *TAKE* 
1800 IFC2=0THEN8050
1801 IFAC(2,7)>7THENM$="I CAN'T CARRY ANYMORE!:GOSUB1:GOTO8200
1802 IFTF(C2)=0THENPRINT"THAT'S BEYOND MY ABILITY.":GOTO8200
1806 IFAC(C2,8)<>RMTHEN8060
1810 IFTL=0THENPRINT"I ALREADY HAVE IT!":GOTO200
1820 IFC2=18THENAC(18,8)=-1:C2=19:AC(9,8)=13:IFAC(5,7)=0THENAC(4,7)=1:PRINT"UGH! HE IS ALL SLIMY!"
1830 IFC2=12THENC2=30:IFAC(30,8)=-1THENM$="HOW? IT WON'T COME LOOSE!":GOSUB1:GOTO8200
1890 AC(C2,8)=0:AC(2,7)=AC(2,7)+1:GOTO8000
1899 REM *DROP*
1900 IFC2=0THEN8050
1905 IFTL<>0THENPRINT "I DON'T HAVE IT.":GOTO8200
1910 IFC2=11THENAC(5,7)=0
1920 IFC2=2THENAC(6,7)=0
1990 GOSUB1995:AC(C2,8)=RM:GOTO8000
1995 IFAC(C2,8)=0THENAC(2,7)=AC(2,7)-1
1996 RETURN
1999 REM *INVENTORY*
2000 K=0:I$="I AM CARRYING: "
2010 FORI=1TONT
2020 IFAC(I,8)=0THENI$=I$+TD$(I)+", ":K=1
2030 NEXTI
2040 IFK=0THENM$=I$+"NOTHING.":GOSUB1:GOTO8200
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO8200
2099 REM *UNLOCK*
2100 IF C2=0 THEN 8050
2110 IF C2<>2 THEN 8010
2120 IF RM<>10 OR MT<>0 THEN 8030
2130 AC(2,8)=RM:MT=-1:GOTO8000
2199 REM *READ*
2200 IF C2=0 THEN 8050
2210 IF C2=3 THEN PRINT "'INSERT COIN.'":GOTO8200
2220 IF C2<>10 THEN M$="NOTHING IS WRITTEN ON IT.":GOSUB1:GOTO8200
2230 PRINT "'...USER ID IS ROAD...'"
2240 M$="'TYPE DIR FOR LIST OF COMMANDS...' THE REST IS ILLEGIBLE.":GOSUB1:GOTO8200
2299 REM *KILL/SHOOT*
2300 IFAC(5,8)<>0THENPRINT"HOW?":GOTO8200
2310 IFRM=16ANDC2=28THEN"IT HAS WEAKENED IT.":AC(7,7)=AC(7,7)+1:GOTO8200
2320 IFAC(C2,8)<>0ANDAC(C2,8)<>RMTHENPRINT"NO ONE'S HERE.":GOTO8200
2330 GOSUB1995
2340 PRINT"HE VANISHED IN A PUFF OF SMOKE.":AC(C2,8)=-1
2350 IFC2=18THEN"I ALSO BROKE THE MIRROR.":C2=18:AC(9,8)=-1:GOSUB1995:AC(10,8)=13
2390 GOTO8200
2399 REM *BREAK/SMASH*
2400 IFC2=0THEN8050
2405 IFAC(C2,8)<>RMANDAC(C2,8)<>0THEN8060
2410 IFC2=9THENPRINT"IT'S SMASHED.":GOSUB1995:AC(10,8)=AC(9,8):AC(9,8)=-1:GOTO8200
2420 IFC2=13THENPRINT"IT'S BROKEN.":GOSUB1995:AC(14,8)=AC(13,8):AC(13,8)=-1:GOTO8200
2430 IFC2=22ANDAC(22,8)=20THENAC(22,8)=-1:AC(23,8)=20:AC(20,5)=13:AC(7,8)=20:GOTO8000
2450 IFAC(5,8)<>0THENPRINT"HOW?":GOTO8200
2460 IFRM=16ANDC2=28THEN"IT HAS WEAKENED IT.":AC(7,7)=AC(7,7)+1:GOTO8200
2490 GOTO8010
2499 REM *OPEN*
2500 IF C2=0 THEN 8050
2510 IF C2<>9 THEN 8010
2520 IF MF<>0 THEN PRINT "IT ALREADY IS.":GOTO820
2530 GOTO8200
2599 REM *LOOK*
2600 IF C2<>0 THEN 8040
2610 WD=-1:GOTO8200
2699 REM *CUT*
2700 IFC2=0THEN8050
2710 IFAC(C2,8)<>RMANDAC(C2,8)<>0THEN8060
2720 IFC2=12ANDRM=4ANDAC(1,8)=0THENM$="IT HAS FALLEN TO THE FOREST FLOOR.":GOSUB1:AC(12,8)=-1:AC(30,8)=4:GOTO8200
2730 IFC2=12THEN8030
2790 GOTO8010
2799 REM *EXAMINE*
2800 IFRM=3ANDC2=27THENM$="IT SHOWS A MAN CLIMBING DOWN A PIT USING A ROPE.":GOSUB1:GOTO8200
2810 IFRM=12ANDC2=31THENM$="IT'S A SHEET OF JAGGED ICE.":GOSUB1:GOTO8200
2820 IFRM=20ANDC2=22THENM$="THE BARS LOOK LOOSE.":GOSUB1:GOTO8200
2850 PRINT "YOU SEE NOTHING SPECIAL.":GOTO8200
2900 REM *ENTER*
2990 GOTO8200
3000 REM *HELP*
3090 GOTO8200
3100 REM *WITH*
3190 GOTO8200
3200 REM *BRIBE*
3290 GOTO8200
7999 REM *COMPLETION MESSAGES*
8000 PRINT"OK.":GOTO8200
8010 PRINT"THAT'S SILLY!":GOTO200
8020 PRINT"NOTHING HAPPENS.":GOTO8200
8030 PRINT"I CAN'T DO THAT NOW.":GOTO8200
8040 PRINT"I ONLY UNDERSTAND ENGLISH?":GOTO200
8050 PRINT"PLEASE GIVE AN OBJECT.":GOTO200
8060 PRINT"IT'S NOT HERE."GOTO8200
8199 REM *COMPLETION ROUTINES*
8200 IFAC(16,8)=0THENAC(3,7)=A(3,7)+1:IFAC(3,7)>5THENM$="THE ICE HAS MELTED.":GOSUB1:AC(16,8)=-1:AC(17,8)=RM
8210 IFAC(19,8)=RMORAC(25,8)=0THENIFRND(0)>.9THENM$="THE GREEN MAN AWOKE AND THROTTLED ME.":GOSUB1:END
8220 IFAC(4,7)THENAC(4,7)=AC(4,7)+1:IFAC(4,7)>4THENM$="I HAVE TURNED GREEN AND DROPPED DEAD.":GOSUB1:END
8230 IFAC(25,8)=RMORAC(25,8)=0THENIFRND(0)>.9THENM$="THE GUARD WOKE UP AND SHOT ME.":GOSUB1:END
8500 REM
8900 GOTO200
9001 DATA"I AM ON A MOUNTAIN PLATEAU. TO THE NORTH THERE IS A STEEP CLIFF."
9002 DATA"I AM AT THE EDGE OF A DEEP PIT."
9003 DATA"I AM IN A DAMP CAVE."
9004 DATA"I AM IN A DENSE FOREST."
9005 DATA"I AM BESIDE A LAKE. THERE IS A RAVINE TO THE WEST."
9006 DATA"I AM IN A STRANGE HOUSE. THE DOOR IS TO THE NORTH."
9007 DATA"I AM IN AN OLD SHED."
9008 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9009 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9010 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9011 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9012 DATA"I AM IN AN ICE CAVERN."
9013 DATA"I AM IN A QUIET CAVERN."
9014 DATA"I AM IN A WIND TUNNEL. THERE IS A CLOSED DOOR AT THE END."
9015 DATA"I AM IN A ROOM WITH A COMPUTER AND KEYBOARD."
9016 DATA"I AM IN A PASSAGE. THERE IS A LOUDSPEAKER WITH DANCE MUSIC COMING OUT."
9017 DATA"I AM IN A LARGE HANGAR. THERE IS A DOOR TO THE WEST."
9018 DATA"I AM IN A TALL LIFT. THE BUTTONS ARE VERY HIGH. THE EXIT IS ON THE WEST."
9019 DATA"I AM IN THE LIFT CONTROL ROOM. THERE ARE 3 SWITCHES, ALL OFF. A SIGN SAYS: '5, 4 NO DUSTY BIN RULES'."
9020 DATA"I AM IN A PRISON CELL, WHICH IS GUARDED BY A VICIOUS SECURITY GUARD."
9021 DATA"I AM IN A SPACE SHIP. THERE IS NO VISIBLE EXIT. THERE IS A SMALL OPEN WINDOW AND 2 BUTTONS (MAIN/AUX)."
9022 DATA"I AN IN THE LAKE."
9050 DATA N,1,S,2,E,3,W,4,U,5,D,6,GO,7,WEAR,8,DANCE,9,USE,10,PUSH,11,TYPE,11
9055 DATA TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14,UNLOC,15,JUMP,7
9060 DATA SHOOT,17,KILL,17,SMASH,18,BREAK,18,OPEN,19,QUIT,20,LOOK,21
9065 DATA CUT,22,EXAMI,23,ENTER,24,HELP,25,WITH,26,BRIBE,27
9100 DATA FLINT,BOOTS,MOTOR,KEY,GUN,SIGN,BAR,COIN,MIRRO,GLASS
9101 DATA GLOVE,VINE,BOARD,BOARD,STALA,ICE,WATER,MAN,ALIEN,DOOR
9102 DATA DOOR,WINDO,HOLE,SHIP,GUARD,STONE,DRAWI,FIELD,SIGN,ROPE
9103 DATA SLOPE
9105 DATA HELP,"1","2","3","4",AUX,MAIN,NORTH,SOUTH,EAST,WEST,UP,DOWN
9150 DATA"A PIECE OF SHARP FLINT",1,1,"A PAIR OF BOOTS",6,1,"A STARTER MOTOR",18,1,"A KEY",15,1,"A LASER GUN",7,1
9155 DATA"AN 'OUT OF ORDER' SIGN",18,0,"A METAL BAR",-1,0,"A GOLD COIN",22,1,"A MIRROR",-1,1,"BROKEN GLASS",-1,1
9160 DATA"A PAIR OF SLIMY GLOVES",14,1,"A VINE HANGING FROM A TREE",4,1
9161 DATA"A FLOOR BOARD",6,1,"A BROKEN FLOOR BOARD",-1,1,"STALACTITES",3,0
9165 DATA"A BLOCK OF ICE",12,1,"A PUDDLE OF WATER",-1,0,"A SMALL GREEN MAN SLEEPING ON A MIRROR",13,1
9166 DATA"A SMALL GREEN ALIEN",-1,1,"A LOCKED DOOR TO THE WEST",17,0
9170 DATA"AN OPEN DOOR TO THE WEST",-1,0,"A BARRED WINDOW",21,0,"A HOLE IN THE WALL",-1,0
9171 DATA"A SMALL BUT POWERFUL SPACE SHIP",17,0,"A SLEEPING SECURITY GUARD",17,0
9175 DATA"SOME STONES",2,1,"A DRAWING ON THE WALL",3,0,"A FORCE FIELD TO THE SOUTH",16,0
9176 DATA"A SIGN: 'SECURITY AREA-- KEEP OUT'",16,0,"A ROPE",-1,1,"A STEEP SLOPE",12,0
9201 DATA 0,0,3,2,0,4
9202 DATA 0,0,1,0,0,0
9203 DATA 8,0,0,1,0,0
9204 DATA 0,5,0,1,0,0
9205 DATA 4,22,6,0,0,0
9206 DATA 5,0,0,0,0,0
9207 DATA 0,0,5,0,0,0
9208 DATA 9,8,8,8,0,8
9209 DATA 8,10,8,8,0,8
9210 DATA 8,8,11,8,0,8
9211 DATA 3,8,8,12,0,8
9212 DATA 0,0,8,0,0,0
9213 DATA 0,16,14,20,0,0
9214 DATA 0,0,15,13,0,0
9215 DATA 0,0,0,14,0,0
9216 DATA 13,20,20,20,0,0
9217 DATA 16,20,18,0,0,0
9218 DATA 0,0,0,17,0,0
9219 DATA 0,0,17,0,0,0
9220 DATA 0,0,0,0,0,0
9221 DATA 0,0,0,0,0,0
9222 DATA 5,0,0,0,0,0
9999 END

REM ,"IT HAD NO EFFECT."
REM ,,"WITH WHAT?"
REM "THE FUSE HAS JUST BLOWN.","THE LIFT HAS BEEN ACTIVATED."
REM "I SEE NOTHING SPECIAL.","KEEP OFF THE MIDDLE MEN, ONE MAY BE SHOCKING!"
REM "VANITY WALTZ!","TRY HELP...","POINTS OF COMPASS!","TRY LOOKING AROUND."
REM "I CAN SEE A ROPE HANGING DOWN THE CHIMNEY."
REM "I AM NOT THAT DAFT. IT IS TOO DEEP."
REM "THE SPACE SHIP BLEW UP AND KILLED ME."
REM "THE SHIP HAS FLOWN INTO THE LARGE LIFT AND IS HOVERING THERE. THERE ARE 4 BUTTONS OUTSIDE THE WINDOW MARKED 1, 2, 3 & 4."
REM "THE LIFT HAS TAKEN ME UP TO A PLATEAU."
REM "CONGRATULATIONS, YOU HAVE MANAGED TO COMPLETE THIS ADVENTURE WITHOUT GETTING KILLED."
REM "THE LIFT HAS BECOME ELECTRIFIED.","I HAVE BEEN ELECTROCUTED."
REM "IT IS A GOOD JOB I WAS WEARING RUBBER SOLED BOOTS."
REM ,"AT WHAT?"
REM "SAVE IS NOT SUPPORTED YET. FORTUNATELY, THIS GAME IS SHORT ENOUGH THAT IT MAY BE EASILY SOLVED WITHOUT SAVING..."
REM M$="AN ALARM SOUNDS. A SECURITY GUARD SHOT ME FOR TRESPASSING.":GOSUB1:END
