0 CLEAR500:CLS:DIMZZ,CC,M$,I,C1,C2,I$:CC=RND(-TIMER):GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
5 I$=I$+", ":RETURN
6 I$=I$+" AND ":RETURN
9 FORCC=1TO10:SOUND100,1:SOUND150,1:NEXT:RETURN
10 NR=22:NT=31:NP=13:NV=35:NO=NT+NP
20 DIMAC(NT,8),VB$(NV),OB$(NO),RM$(NR),TD$(NT),VN(NV),TF(NT)
30 M$="WELCOME TO the planet of death.":GOSUB1
40 M$="(C) 1981 BY 'ARTIC COMPUTING'. PORTED TO COCO BY JIM GERRIE.":GOSUB1
60 M$="IN THIS ADVENTURE YOU FIND YOURSELF STRANDED ON AN ALIEN PLANET. YOUR AIM "
70 M$=M$+"IS TO ESCAPE FROM THIS PLANET BY FINDING YOUR, NOW CAPTURED AND DISABLED, SPACE SHIP.":GOSUB1
80 M$="YOU WILL MEET VARIOUS HAZARDS AND DANGERS ON YOUR ADVENTURE, "
90 M$=M$+"SOME NATURAL, SOME NOT, ALL OF WHICH YOU MUST OVERCOME TO SUCCEED." :GOSUB1
95 M$="GOOD LUCK, YOU WILL NEED IT!":GOSUB1
99 REM *INITIALIZATION*
100 RM=1:CF=0:RT=-1:WD=-1:LI=0:BT=-1
120 FORI=1TONR:READRM$(I):NEXT
130 FORI=1TONV:READVB$(I),VN(I):NEXT
140 FORI=1TONO:READOB$(I):NEXT
150 FORI=1TONT:READTD$(I),AC(I,8),TF(I):NEXT
160 FORI=1TONR:READAC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT
170 PRINT"PRESS ANY KEY TO START";
180 M$=INKEY$:IFM$=""THEN180
190 CLS:INPUT"LOAD A SAVED GAME";A$:IFLEFT$(A$,1)="Y"THEN195 ELSE 200
195 PRINT"PRESS PLAY ON THE CASSETTE."
196 OPEN"I",#-1,"PODDATA":FORI=1TONT:INPUT#-1,AC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT:CLOSE#-1:RM=AC(1,7)
199 REM *MAIN LOOP: ROOM DESCRIPTION* 
200 IFWD=0THEN400 
210 WD=0:M$=RM$(RM):GOSUB1
220 K=0
230 I$="":FORI=1TO NT
240 IFAC(I,8)<>RM THEN270 
250 IFK=0THENI$=I$+"I CAN ALSO SEE: ":K=1
260 I$=I$+TD$(I)+", "
270 NEXTI:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
300 ZZ=-1:FORI=1TO6:IFAC(RM,I)<>0THENZZ=ZZ+1
310 NEXT:CC=0:I$="":IFZZ=0THENI$="THERE IS AN EXIT "
315 IFZZ>0THENI$="EXITS ARE "
320 FORI=1TO6:IFAC(RM,I)<>0THENCC=CC+1:I$=I$+OB$(38+I):ON1-(CC=ZZ)GOSUB5,6
330 NEXT
340 IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
399 REM *MAIN LOOP: COMMAND INPUT AND PARSER* 
400 PRINT"TELL ME WHAT TO DO.":INPUTC$
410 L=LEN(C$):IFL=0THENPRINT"EXCUSE ME?":GOTO400
420 C1$="":C2$="":C2=0:X=0
430 FORI=1TOL
440 A$=MID$(C$,I,1):A=ASC(A$):IFA>=97ANDA<=122THENA$=CHR$(A-32)
450 IFA$<>" "THEN460
455 IFC2$<>""THENTM=I:I=L:NEXTI:I=TM:GOTO500
457 X=1:GOTO490
460 IFX=0THENC1$=C1$+A$:GOTO490
470 C2$=C2$+A$
490 NEXTI
500 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FORC1=1TONV
530 IFVB$(C1)=C1$THENC1=VN(C1):GOTO600 
540 NEXTC1
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700 
610 FORC2=1TO NO
620 IFOB$(C2)=C2$THENTM=C2:C2=NO:NEXT:C2=TM:GOTO700
630 NEXTC2
635 IFC1=11THENC2=1:GOTO700
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO400
699 REM *PRELIMINARY FILTERING OF COMMANDS*
700 IFC2>NT+7ANDC1<>7ANDC1<>11ANDC1<>9THEN640
710 IFC2<=NT+7ANDC2>NT THENIFC1<>11THEN8010
720 IFC2>NT THEN750
725 TL=AC(C2,8)
730 IFC1<>11ANDC1<>19ANDTL<>RM ANDTL<>0THENPRINT"IT ISN'T HERE.":GOTO400
750 ONC1 GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400
760 ONC1-10 GOTO1500,1800,1900,2000,2100,2200,2300,2400,2500,9999
770 ONC1-20 GOTO2600,2700,2800,2900,3000,3100,3200
999 REM *DIRECTIONS*
1000 IF(RM=5ORRM=2)AND(C1=4ORC1=6)ANDAC(RM,C1)=0THENM$="IT'S TOO WIDE AND TOO STEEP. I FELL AND BROKE MY NECK.":GOSUB1:END
1005 IFAC(RM,C1)=0THENPRINT"I CAN'T GO THAT WAY.":GOTO200
1010 IFRM=5ANDC1=2ANDAC(6,7)=0THENPRINT"BRRR. THE WATER IS TOO COLD.":GOTO200
1015 IFRM=14ANDC1=3ANDAC(14,7)=0THENPRINT"THE DOOR IS CLOSED.":GOTO200
1020 IFRM<>17ORC1<>3ORAC(12,7)<>1THEN1080
1030 M$="THE LIFT HAS BECOME ELECTRIFIED.":GOSUB1
1040 IFAC(6,7)=1THENM$="IT IS A GOOD JOB I WAS WEARING RUBBER SOLED BOOTS.":GOSUB1:GOTO1080
1050 PRINT"I HAVE BEEN ELECTROCUTED.":END
1080 AC(7+RND(4),5)=7+RND(4):RM=AC(RM,C1):WD=-1:IFRM=20THEN8060
1090 GOTO8200
1099 REM *GO*
1100 IFC2=0THEN8050
1110 IFC2=31THENM$="I WOULD KILL MYSELF IF I DID.":GOSUB1:GOTO8200
1120 IFC2<=NT THEN8040
1190 C1=C2-(NT+7):GOTO1000
1199 REM *WEAR*
1200 IFC2<>2ANDC2<>11THEN8010
1210 IFAC(11,8)=0THENAC(5,7)=1
1220 IFAC(2,8)=0THENAC(6,7)=1
1290 GOTO8000
1299 REM *DANCE* 
1300 IFRM<>16THEN8020
1305 IFC2>40ORC2=39THENC1=7:GOTO1100
1310 IFAC(9,8)<>0THENM$="THE FORCE IS TOO STRONG. I FELL AND KNOCKED MYSELF OUT.":GOSUB1:GOTO8060
1315 IFAC(7,7)<2THENM$="THE FORCE FIELD IS STILL TOO STRONG. I FELL AND KNOCKED MYSELF OUT.":GOSUB1:GOTO8060
1320 RM=17:WD=-1:GOTO8000
1399 REM *USE*
1400 IFC2=0THEN8050
1405 IFTL<>0THENPRINT"I DON'T HAVE IT.":GOTO8200
1410 IFRM=2ANDC2=30THENAC(RM,4)=13:AC(RM,6)=13:AC(13,5)=2:AC(30,8)=-1:AC(2,7)=AC(2,7)-1:GOTO8000
1415 IFRM=5ANDC2=13THENAC(RM,4)=7:AC(RM,6)=7:AC(13,8)=-1:AC(2,7)=AC(2,7)-1:GOTO8000
1420 IFRM=12ANDC2=16THENPRINT"WWWWHHHHEEEE!":RM=13:WD=-1:GOTO8200
1430 IFRM=17ANDC2=4THENAC(RM,4)=19:AC(20,8)=-1:AC(21,8)=17:GOTO8000
1440 IFRM=19ANDC2=7THENAC(12,7)=0:GOSUB1995:AC(7,7)=-1:GOTO8000
1450 IFRM=4ANDC2=1THENM$="THE VINE HAS FALLEN TO THE GROUND.":GOSUB1:AC(12,8)=-1:AC(30,8)=4:GOTO8200
1460 IFC2=5THENPRINT"TO DO WHAT?":GOTO8200
1470 IFC2=15ORC2=26ORC2=24THEN8030
1490 GOTO8010
1499 REM *TYPE* 
1500 IFC2=0THEN8050
1510 IFRM<>15THEN1610
1520 IFC2=32THENM$="COMPUTER: 1 ALARM 2 FIELD 3 SPEAKER 4 DIRECTIONS":GOSUB1:GOTO8200
1530 IFC2=33ORC2=34ORC2=35THENAC(8,7)=AC(8,7)+1:PRINT"COMPUTER: LOGIN REQUIRED.":GOTO8200
1535 IFAC(8,7)>2THENAC(8,7)=0:GOTO8060
1540 IFC2=36THENM$="COMPUTER: 2 WEST, 2 SOUTH FOR SPACE FLIGHT.":GOSUB1:GOTO8200
1550 M$="COMPUTER: SN ERROR-- FOR HELP TYPE 'HELP'":GOSUB1:GOTO8200
1610 IFRM<>19THEN1700
1620 IFC2=33ANDAC(9,7)=0THENAC(9,7)=0:AC(10,7)=0:GOTO8000
1630 IFC2=33THENPRINT"THE LIFT HAS BEEN ACTIVATED.":AC(11,7)=1:AC(9,7)=0:AC(10,7)=0:GOTO8200
1640 IFC2=34ANDAC(10,7)=0THENPRINT"THE FUSE HAS BLOWN.":AC(12,7)=1:AC(29,8)=RM:GOTO8200
1650 IFC2=34THENAC(9,7)=1:GOTO8000
1660 IFC2=35THENAC(10,7)=1:GOTO8000
1700 IFRM<>21THEN1790
1710 IFC2=38ANDAC(13,7)=0THENM$="THE SPACE SHIP BLEW UP AND KILLED ME.":GOSUB1:END
1720 IFC2<>37THEN1760
1725 IFAC(3,8)<>0ANDAC(3,8)<>21THENPRINT"IT HAD NO EFFECT.":GOTO8200
1730 M$="THE SHIP HAS FLOWN INTO THE LIFT AND IS HOVERING THERE. THERE ARE 4 BUTTONS OUTSIDE THE WINDOW MARKED 1, 2, 3 & 4."
1740 GOSUB1:AC(13,7)=1:AC(24,8)=18:AC(22,8)=21:GOTO8200
1760 IF(C2=33ORC2=34ORC2=35)ANDAC(25,8)<>-1THENM$="AN ALARM SOUNDS. A GUARD SHOT ME FOR TRESPASSING.":GOSUB1:GOSUB9:END
1765 IFC2=33ORC2=34ORC2=35THEN8000
1770 IFC2<>36THEN1790
1775 IFAC(11,7)=0THEN8020
1780 M$="THE LIFT HAS TAKEN ME UP TO A PLATEAU.":GOSUB1
1785 M$="congratulations, YOU HAVE MANAGED TO COMPLETE THIS ADVENTURE WITHOUT GETTING KILLED.":GOSUB1:END
1790 GOTO8030
1799 REM *TAKE* 
1800 IFC2=0THEN8050
1810 IFAC(2,7)>5THENPRINT"I CAN'T CARRY ANYMORE!":GOTO8200
1820 IFTF(C2)=0THENPRINT"THAT'S BEYOND MY ABILITY.":GOTO8200
1830 IFTL=0THENPRINT"I ALREADY HAVE IT!":GOTO200
1840 IFC2=15ANDAC(15,7)=0THEN8030
1850 IFC2=12THENC2=30:IFAC(30,8)=-1THENM$="HOW? IT WON'T COME LOOSE!":GOSUB1:GOTO8200
1880 IFC2=18THENAC(18,8)=-1:C2=19:AC(9,8)=13:IFAC(5,7)=0THENAC(4,7)=1:PRINT"UGH! HE IS ALL SLIMY!"
1890 AC(C2,8)=0:AC(2,7)=AC(2,7)+1:GOTO8000
1899 REM *DROP*
1900 IFC2=0THEN8050
1905 IFTL<>0THENPRINT"I DON'T HAVE IT.":GOTO8200
1910 IFC2=11THENAC(5,7)=0
1920 IFC2=2THENAC(6,7)=0
1930 IFRM=2ANDC2=26THENM$="THEY FALL... FOR A LONG TIME! I'M NOT DAFT. IT'S TOO DEEP.":GOSUB1:GOSUB1995:AC(C2,8)=13:GOTO8200
1990 GOSUB1995:AC(C2,8)=RM:GOTO8000
1995 IFAC(C2,8)=0THENAC(2,7)=AC(2,7)-1
1996 RETURN
1999 REM *INVENTORY*
2000 K=0:I$="I AM CARRYING: "
2010 FORI=1TONT
2020 IFAC(I,8)=0THENI$=I$+TD$(I)+", ":K=1
2030 NEXTI
2040 IFK=0THENM$=I$+"NOTHING.":GOSUB1:GOTO8200
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO8200
2099 REM *UNLOCK*
2100 IFC2=0THEN8050
2110 IFAC(4,8)<>0ORRM<>17THEN8030
2120 IFC2=20THENAC(RM,4)=19:AC(20,8)=-1:AC(21,8)=17:GOTO8000
2130 GOTO8010
2199 REM *SAVE*
2200 M$="READY TAPE AND HIT ENTER (ENTER Q TO ABORT)":GOSUB1:INPUTA$:IFA$="Q"THEN200
2210 AC(1,7)=RM:OPEN"O",#-1,"PODDATA":FORI=1TONT:PRINT#-1,AC(I,1),AC(I,2),AC(I,3),AC(I,4),AC(I,5),AC(I,6):NEXT:CLOSE#-1
2212 GOTO8000
2299 REM *KILL/SHOOT*
2300 IFAC(5,8)<>0THENPRINT"HOW?":GOTO8200
2305 SOUND250,10
2310 IFRM=16ANDC2=28THENPRINT"IT HAS WEAKENED IT.":AC(7,7)=AC(7,7)+1:GOTO8200
2330 IFC2<>18ANDC2<>19ANDC2<>25ANDC2<>9THEN8010
2340 PRINT"HE VANISHED IN A PUFF OF SMOKE.":GOSUB1995:AC(C2,8)=-1
2350 IFC2=18ORC2=9THENPRINT"I ALSO BROKE THE MIRROR.":AC(9,8)=-1:GOSUB1995:AC(10,8)=13
2390 GOTO8200
2399 REM *BREAK/SMASH*
2400 IFC2=0THEN8050
2410 IFC2=9THENPRINT"IT'S SMASHED.":GOSUB1995:AC(10,8)=AC(9,8):AC(9,8)=-1:GOTO8200
2420 IFC2=13THENPRINT"IT'S BROKEN.":GOSUB1995:AC(14,8)=AC(13,8):AC(13,8)=-1:GOTO8200
2425 IFC2=15THENPRINT"SOME BROKE OFF.":AC(15,7)=1:GOTO8200
2430 IFC2=22ANDAC(22,8)=20THENAC(22,8)=-1:AC(23,8)=20:AC(20,5)=13:AC(7,8)=20:GOTO8000
2440 IFC2=6THEN8060
2450 IFAC(5,8)<>0THENPRINT"HOW?":GOTO8200
2460 IFRM=16ANDC2=28THENSOUND250,10:PRINT"IT HAS WEAKENED IT.":AC(7,7)=AC(7,7)+1:GOTO8200
2490 GOTO8010
2499 REM *OPEN*
2500 IFC2=0THEN8050
2510 IFC2<>20THEN8010
2520 IF(RM=14ANDAC(14,7)=1)ORRM=6THENPRINT"IT ALREADY IS.":GOTO8200
2530 IFRM=14THENAC(14,7)=1:GOTO8000
2540 IFRM=20OR(RM=17ANDAC(20,8)<>-1)THENPRINT"IT'S LOCKED."
2560 IFRM=20THENM$="BUT THERE'S A LITTLE HATCH THROUGH WHICH THE GUARD IS WATCHING.":GOSUB1
2570 GOTO8200
2599 REM *LOOK*
2600 IFC2<>0THEN8040
2610 WD=-1:GOTO8200
2699 REM *CUT*
2700 IFC2=0THEN8050
2710 IFAC(1,8)<>0ANDAC(5,8)<>0THEN8030
2715 IFAC(5,8)=0THENSOUND250,10:IFC2=15THENPRINT"SOME BROKE OFF.":AC(15,7)=1:GOTO8200
2720 IFC2=12ANDRM=4THENM$="IT HAS FALLEN TO THE FOREST FLOOR.":GOSUB1:AC(12,8)=-1:AC(30,8)=4:GOTO8200
2730 IFC2=13THENPRINT"IT'S CUT IN TWO.":GOSUB1995:AC(14,8)=AC(13,8):AC(13,8)=-1:GOTO8200
2780 IFTL<>0THENPRINT"I DON'T HAVE IT.":GOTO8200
2790 GOTO8010
2799 REM *EXAMINE*
2800 IFRM=3ANDC2=27THENM$="IT SHOWS A MAN CLIMBING DOWN A PIT USING A ROPE.":GOSUB1:GOTO8200
2810 IFRM=12ANDC2=31THENM$="IT'S A SHEET OF JAGGED ICE.":GOSUB1:GOTO8200
2820 IFRM=20ANDC2=22THENM$="THE BARS LOOK LOOSE.":GOSUB1:GOTO8200
2830 IFRM=21ANDAC(24,8)=21ANDC2=22THENM$="THERE ARE 4 BUTTONS OUTSIDE THE WINDOW MARKED 1, 2, 3 & 4.":GOSUB1:GOTO8200
2840 IFRM=4ANDC2=12THENM$="IT LOOKS LIKE IT WOULD MAKE A FINE ROPE.":GOSUB1:GOTO8200
2850 IFC2=29THENPRINT"IT'S A SMALL BAR OF METAL.":GOTO8200
2860 IFC2=7THENM$="IT LOOKS LIKE IT WOULD MAKE A FINE FUSE.":GOSUB1:GOTO8200
2890 PRINT"I SEE NOTHING SPECIAL.":GOTO8200
2899 REM *ENTER*
2900 IFC2=0THEN8050
2910 IFC2=24THENWD=-1:RM=21:GOTO8000
2990 GOTO8010
2999 REM *HELP*
3000 IFRM>7ANDRM<12THENPRINT"POINTS OF COMPASS!":GOTO8200
3010 IFRM=16THENPRINT"VANITY WALTZ!":GOTO8200
3020 IFRM=19THENM$="KEEP OFF THE MIDDLE MEN, ONE MAY BE SHOCKING!":GOSUB1:GOTO8200
3030 IFRM=15THENPRINT"TRY TYPING 'HELP'":GOTO8200
3090 PRINT"TRY LOOKING AROUND.":GOTO8200
3099 REM *FIX*
3100 IFC2=0THEN8050
3110 IFRM=19ANDC2=24ANDAC(7,8)=0THENM$="I USED THE BAR TO REPLACE IT.":GOSUB1:GOTO8200
3190 GOTO8010
3199 REM *GIVE*
3200 IFC2=0THEN8050
3210 IFRM=20ANDC2=8ANDAC(8,8)=0THENGOSUB1995:AC(8,8)=-1:WD=-1:RM=13:GOTO8200
3290 GOTO8020
7999 REM *COMPLETION MESSAGES*
8000 PRINT"OK.":GOTO8200
8010 PRINT"THAT'S SILLY!":GOTO200
8020 PRINT"NOTHING HAPPENS.":GOTO8200
8030 PRINT"I CAN'T DO THAT NOW.":GOTO8200
8040 PRINT"I ONLY UNDERSTAND ENGLISH.":GOTO200
8050 PRINT"PLEASE GIVE AN OBJECT.":GOTO200
8060 PRINT"AN ALARM SOUNDS!":GOSUB9:WD=-1:RM=20:AC(14,7)=0
8070 IFAC(4,8)=0THENC2=4:GOSUB1995:AC(4,8)=15
8080 IFAC(5,8)=0THENC2=5:GOSUB1995:AC(5,8)=15
8090 GOTO8200
8199 REM *COMPLETION ROUTINES*
8200 IFAC(16,8)=0THENAC(3,7)=AC(3,7)+1:IFAC(3,7)>16THENM$="THE ICE HAS MELTED.":GOSUB1:AC(16,8)=-1:AC(17,8)=RM
8210 IFAC(18,8)<>RM ANDAC(18,8)<>0ANDAC(19,8)<>RM ANDAC(19,8)<>0THEN8220
8215 AC(16,7)=AC(16,7)+1:IFRND(20)<AC(16,7)THENM$="THE GREEN MAN AWOKE AND THROTTLED ME.":GOSUB1:END
8220 IFAC(25,8)<>RM ANDAC(25,8)<>0THEN8230
8225 AC(17,7)=AC(17,7)+1:IFRND(20)<AC(17,7)THENM$="THE GUARD WOKE UP AND SHOT ME.":GOSUB1:END
8230 IFAC(4,7)THENAC(4,7)=AC(4,7)+1:IFAC(4,7)>8THENM$="I HAVE TURNED GREEN AND DROPPED DEAD.":GOSUB1:END
8900 GOTO200
9001 DATA"I AM ON A MOUNTAIN PLATEAU. TO THE NORTH THERE IS A STEEP CLIFF."
9002 DATA"I AM AT THE EDGE OF A DEEP PIT."
9003 DATA"I AM IN A DAMP LIMESTONE CAVE."
9004 DATA"I AM IN A DENSE FOREST."
9005 DATA"I AM BESIDE A LAKE. THERE IS A RAVINE TO THE WEST."
9006 DATA"I AM IN A STRANGE HOUSE. THE DOOR IS TO THE NORTH."
9007 DATA"I AM IN AN OLD SHED."
9008 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9009 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9010 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9011 DATA"I AM IN A MAZE. THERE ARE PASSAGES EVERYWHERE."
9012 DATA"I AM IN AN ICE CAVERN."
9013 DATA"I AM IN A QUIET CAVERN."
9014 DATA"I AM IN A WIND TUNNEL. THERE IS A DOOR AT THE END."
9015 DATA"I AM IN AN OFFICE."
9016 DATA"I AM IN A PASSAGE. THERE IS A LOUDSPEAKER WITH DANCE MUSIC COMING OUT AND A SIGN: 'SECURITY AREA-- NO TRESPASSING'"
9017 DATA"I AM IN A LARGE HANGAR."
9018 DATA"I AM IN A TALL LIFT. THE BUTTONS ARE VERY HIGH. THERE IS AN 'OUT OF ORDER' SIGN HERE."
9019 DATA"I AM IN THE LIFT CONTROL ROOM. THERE ARE 3 SWITCHES, ALL OFF. A SIGN SAYS: '5, 4 NO DUSTY BIN RULES'."
9020 DATA"I AM IN A PRISON CELL, WHICH IS GUARDED BY A VICIOUS SECURITY GUARD."
9021 DATA"I AM IN A SPACE SHIP. THERE IS NO VISIBLE EXIT. THERE IS A SMALL OPEN WINDOW AND 2 BUTTONS (MAIN/AUX)."
9022 DATA"I AN IN THE LAKE."
9050 DATA N,1,S,2,E,3,W,4,U,5,D,6,GO,7,JUMP,7,WEAR,8,DANCE,9,USE,10,WITH,10,PUSH,11,TYPE,11
9055 DATA TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I,14,UNLOC,15,SAVE,16
9060 DATA SHOOT,17,KILL,17,SMASH,18,BREAK,18,OPEN,19,QUIT,20,LOOK,21
9065 DATA CUT,22,EXAMI,23,ENTER,24,HELP,25,FIX,26,GIVE,27
9100 DATA FLINT,BOOTS,MOTOR,KEY,GUN,COMPU,BAR,COIN,MIRRO,GLASS
9101 DATA GLOVE,VINE,BOARD,WOOD,STALA,ICE,WATER,MAN,ALIEN,DOOR
9102 DATA DOOR,WINDO,HOLE,SHIP,GUARD,STONE,DRAWI,FIELD,FUSE,ROPE
9103 DATA SLOPE
9105 DATA HELP,"1","2","3","4",AUX,MAIN,NORTH,SOUTH,EAST,WEST,UP,DOWN
9150 DATA"A PIECE OF SHARP FLINT",1,1,"A PAIR OF BOOTS",6,1,"A STARTER MOTOR",18,1,"A KEY",15,1,"A LASER GUN",7,1
9155 DATA"A COMPUTER AND KEYBOARD",15,0,"A METAL BAR",-1,1,"A GOLD COIN",22,1,"A MIRROR",-1,1,"BROKEN GLASS",-1,1
9160 DATA"A PAIR OF SLIMY GLOVES",14,1,"A VINE HANGING FROM A TREE",4,1
9161 DATA"A FLOOR BOARD",6,1,"BROKEN PIECES OF WOOD",-1,1,"STALACTITES",3,1
9165 DATA"A BLOCK OF ICE",12,1,"A PUDDLE OF WATER",-1,0,"A SMALL GREEN MAN SLEEPING ON A MIRROR",13,1
9166 DATA"A SMALL GREEN ALIEN",-1,1,"A LOCKED DOOR TO THE WEST",17,0
9170 DATA"AN OPEN DOOR TO THE WEST",-1,0,"A WINDOW",20,0,"A HOLE IN THE WALL",-1,0
9171 DATA"A SMALL BUT POWERFUL SPACE SHIP",17,0,"A SLEEPING SECURITY GUARD",17,0
9175 DATA"SOME STONES",2,1,"A DRAWING ON THE WALL",3,0,"A FORCE FIELD TO THE SOUTH",16,0
9176 DATA"FUSE",-1,1,"A ROPE",-1,1,"A STEEP SLOPE",12,0
9201 DATA 0,0,3,2,0,4
9202 DATA 0,0,1,0,0,0
9203 DATA 8,0,0,1,0,0
9204 DATA 0,5,0,1,0,0
9205 DATA 4,22,6,0,0,0
9206 DATA 5,0,0,0,0,0
9207 DATA 0,0,5,0,0,0
9208 DATA 9,8,8,8,0,8
9209 DATA 8,10,8,8,0,8
9210 DATA 8,8,11,8,0,8
9211 DATA 3,8,8,12,0,8
9212 DATA 0,0,8,0,0,0
9213 DATA 0,16,14,20,0,0
9214 DATA 0,0,15,13,0,0
9215 DATA 0,0,0,14,0,0
9216 DATA 13,20,20,20,0,0
9217 DATA 16,20,18,0,0,0
9218 DATA 0,0,0,17,0,0
9219 DATA 0,0,17,0,0,0
9220 DATA 0,0,0,0,0,0
9221 DATA 0,0,0,0,0,0
9222 DATA 5,0,0,0,0,0
9999 END
