0 CLS:CLEAR400:PRINT@227,"SPACE TREK I BY M.EDWARDS":GOSUB20000:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(J$,CC,1):IFI$=" "ORI$=""THEN?MID$(J$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:J$="":RETURN
9 PRINT:PRINT" ******************************":PRINT:RETURN
10 GOSUB1000:L=N(PL+26,1):GOSUB1030
11 PRINT" WHAT DO YOU WANT ME TO DO?":INPUT" ";D$
12 V$="":O$="":FORI=1TOLEN(D$):IFMID$(D$,I,1)=" "THENV$=LEFT$(D$,I-1):O$=RIGHT$(D$,LEN(D$)-I):I=LEN(D$)+4
13 NEXTI:IFV$=""THENV$=D$
14 M$="I CAN'T DO THAT":VB=0:OB=0
15 LL=LEN(V$):FORI=1TONV:L1=LL:IFLEN(V$(I))<LLTHENL1=LEN(V$(I))
17 IFLEFT$(V$(I),L1)=LEFT$(V$,L1)THENVB=I:I=NV+1
18 NEXTI
19 IFVB=0THENM$="I DON'T UNDERSTAND THAT VERB"
20 IFO$=""THEN25
21 LL=LEN(O$)
22 FORI=1TONO:L1=LL:IFLEN(N$(I,0))<LLTHENL1=LEN(N$(I,0))
23 IFLEFT$(N$(I,0),L1)=LEFT$(O$,L1)THENOB=I:I=NO+1
24 NEXTI
25 IFOB=0ANDO$<>""THENM$="I DON'T KNOW WHAT ONE OF THOSE IS":GOTO27
26 IFV$=""THENVB=0
27 ON-(VB>13ORVB<1)GOTO28:ONVBGOTO2000,2003,2004,2005,2006,2007,2008,2008,2032,2033,2018,2040,2071
28 ON-(VB>27ORVB<1)GOTO30:ONVB-13GOTO2072,2074,2200,2209,2211,2218,2221,2225,2241,2242,2246,2250,2256
30 IFOB=17ANDVB=15THENM$="COM-UNITS HAVE ON/OFF PUSH SWITCHES ON THEM"
31 IFOB=20ANDVB=15AND(L=46ORL=43)THENM$="IT HAS A RED BUTTON ON IT"
32 IFLEFT$(O$,3)="CON"ANDVB=15ANDL=5THENM$="THEY ARE MAINLY BROKEN"
33 IFL=60THENFORI=1TO4:IFG(PL,I)<>33THENNEXTI:M$="I AM DROWNED":GOTO100
34 IFVB=15AND(LEFT$(O$,4)="VIEW"ORLEFT$(O$,4)="SCRE")ANDL=4THEN40
35 GOTO99
40 IFAR=0THENM$="IT SHOWS DEEP SPACE":GOTO99
41 IFAR=1ANDPL$="HOLBERG"THENM$="IT SHOWS THE PLANET HOLBERG"
99 J$=M$:GOSUB1:PRINT:GOTO11
100 CLS:J$=M$:GOSUB1:PRINT:PRINTN$(PL+26,1);" IS DEAD!":PRINT"GAME OVER!"
102 PRINT:PRINT"PLAY AGAIN (Y/N)?"
103 I$=INKEY$:IFI$="N"THENEND
104 ON-(I$<>"Y")GOTO103:RUN
200 ON-(AR=1ANDSA=1ANDPL$="STARFLEET HQ")GOTO201:GOSUB1030:J$=M$:GOSUB1:GOTO11
201 CLS:J$="'CAPTAIN KIRK, THERE IS AN URGENT MESSAGE FOR YOU FROM STARFLEET HQ,' SAYS UHURA":GOSUB1:PRINT
202 J$="'CAPTAIN KIRK-- WE HAVE BEEN MONITORING COORDINATES 10:20:8 AND HAVE DEDECTED AN UNKNOWN FORCE":GOSUB1
203 J$="IT'S NOT A BLACK HOLE BUT EVERYTHING THAT GOES IN DOES NOT COME OUT!":GOSUB1
204 J$="THE REAL ADVENTURE IS JUST BEGINNING...'":GOSUB1:END
300 FORI=43TO46:N(I,1)=0:NEXTI:N(51,1)=0:N(54,1)=0:GOTO30
1000 CLS:GOSUB9:PRINT@107,"1. KIRK":PRINT@171,"2. SPOCK":PRINT@235,"3. MCCOY":PRINT@299,"4. SCOTTY":GOSUB9
1001 PRINTTAB(9)"WHO TO REPORT?"
1002 I$=INKEY$:ON-(I$<"1"ORI$>"4")GOTO1002:PL=VAL(I$):RETURN
1010 CLS:GOSUB9:PRINT@101,"1. SET COURSE":PRINT@133,"2. SELECT WARP OR STOP":PRINT@165,"3. ENGINEERING REPORT"
1012 PRINT@197,"4. SICK BAY REPORT":PRINT@229,"5. DAMAGE REPORT":PRINT@261,"6. AIM PHASERS"
1013 PRINT@293,"7. FIRE PHASERS":GOSUB9:PRINTTAB(9)"WHICH COMMAND?"
1014 I$=INKEY$:ON-(I$<"1"ORI$>"7")GOTO1014:CM=VAL(I$):RETURN
1020 CLS:GOSUB9:PRINT@101,"1. CALL SECURITY":AS=1:BS=2:CS=3:ON-(PL<1ORPL>3)GOTO1023
1022 AS=2:BS=3:CS=4:ON-(PL=1)GOTO1023:AS=1:BS=3:CS=4:ON-(PL=2)GOTO1023:AS=1:BS=2:CS=4
1023 PRINT@133,"2. CALL "N$(AS+26,1):PRINT@165,"3. CALL "N$(BS+26,1):PRINT@197,"4. CALL "N$(CS+26,1)
1024 PRINT@229,"5. SWITCH OFF":GOSUB9:PRINTTAB(13);"WHICH?";
1025 I$=INKEY$:ON-(I$<"1"ORI$>"5")GOTO1025:CM=VAL(I$):RETURN
1030 CLS:PRINTN$(PL+26,1);" REPORTING:":PRINT:N(PL+26,1)=L
1031 IFL<>60THEN1040
1035 FORI=1TO4:IFG(PL,I)=33THEN1040
1036 NEXTI:M$="I AM DROWNED!":GOTO100
1040 PRINT"I AM:":RESTORE:FORI=1TOL:READN$,A,B,C,D,E,F:NEXTI:J$=N$:GOSUB1
1041 IFL=5THENJ$="THE INSTRUMENTS SHOW RYETALYN ON THE PLANET HOLBERG":GOSUB1
1042 IFL=59ANDUN=1THENB=62
1043 PRINT:GOSUB1070:PRINT"I CAN SEE:":NS=0:FORI=1TONO:IFN(I,1)=LTHENNS=NS+1
1050 NEXTI:IFNS=1THENPRINT"NOTHING":GOTO1060
1051 NS=NS-1:FORI=1TONO:IFN(I,1)=LANDI<>PL+26THENNS=NS-1:J$=J$+N$(I,1)+", "
1052 NEXTI:J$=LEFT$(J$,LEN(J$)-2):GOSUB1
1060 PRINT:RETURN
1070 J$="EXITS: ":IFATHENJ$=J$+"N "
1071 IFBTHENJ$=J$+"E "
1072 IFCTHENJ$=J$+"S "
1073 IFDTHENJ$=J$+"W "
1074 IFETHENJ$=J$+"U "
1075 IFFTHENJ$=J$+"D "
1076 IF(A+B+C+D+E+F)=0THENM$=M$+"NONE "
1077 J$=LEFT$(J$,LEN(J$)-1):GOSUB1:RETURN
1100 IFZ=0THENM$="I CAN'T GO THAT WAY":GOTO30
1102 IFL=66ANDSH=1ANDZ=CTHENM$="THE SHIELD STOPS ME":GOTO30
1103 IFL=63ANDGU=1THENM$="THE GUARD DROID STOPS ME":GOTO30
1104 IFL=68ANDZ=CANDG2=1THENM$="THE HOLBERG STOPS ME":GOTO30
1105 L=Z:GOTO2071
1200 IFN(OB,0)=1THENM$="I CAN'T GET THAT":GOTO30
1201 IF(N(OB,0)>2ANDN(OB,0)<7)ANDPL<>N(OB,0)-2THENM$="I'M AFRAID ONLY "+N$(24+N(OB,0),1)+" CAN GET IT":GOTO30
1203 IFN(OB,1)>=0ANDN(OB,1)<>LTHENM$="I DON'T SEE IT":GOTO30
1204 IFOB=38THENM$="IT RUNS THROUGH MY FINGERS":GOTO30
1205 Z=N(OB,1):IFZ>-1THEN2011
1206 GOTO2010
1300 IFAR=0THENM$="OK. COURSE IS SET FOR "+PL$:AR=-1:RETURN
1301 IFAR=-1THENM$="COURSE SET ALREADY.":RETURN
1302 IFAR=1THENM$="WE'RE THERE ALREADY!"
1303 RETURN
1400 PRINT"IT CONTAINS:":M$="":FORI=1TONO:IFN(I,1)=-3THENJ$=N$(I,1):GOSUB1
1410 NEXTI:RETURN
1450 IFL>54ANDL<62THENM$="IT SHOWS IMPURE RYETALYN":GOTO2077
1460 IFL>61ANDL<70THENM$="IT SHOWS PURE RYETALYN":GOTO2077
1470 M$="IT SHOWS NOTHING NOW":GOTO2077
1490 A1=0:A2=0:FORI=1TO4:IFG(PL,I)=52THENA1=1
1491 IFG(PL,I)=53THENA2=1
1492 NEXTI:GOTO2253
1500 IFAR=-1THENM$="OK. TRAVELLING AT WARP SPEED TO "+PL$+"... WE HAVE ARRIVED SIR":AR=1:GOTO200
1510 IFAR=0THENM$="NO COURSE SET YET, SIR.":GOTO2065
1520 M$="WE'RE THERE NOW!":GOTO2065
1590 M$="THE SAFE DOOR SWINGS OPEN":FORI=1TONO:IFN(I,1)=-48THENN(I,1)=L
1591 NEXTI:GOTO30
1600 M$="":PRINT"IT CONTAINS:":FORI=1TONO:IFN(I,1)=-12THENPRINTN$(I,1)
1610 NEXTI:RETURN
1700 IFSA=1THENM$="THEY SAY 'REPORT TO STARFLEET HQ FOR YOUR NEXT ASSIGNMENT'":PL$="STARFLEET HQ":AR=0:RETURN
1710 M$="THEY SAY: RIGELLIAN FEVER IS VERY DANGEROUS! PROCEED TO "+PL$+". GOOD LUCK JIM":RETURN
1800 FORI=1TO4:IFG(PL,I)=41THENM$="YOU HAVE THE KEY":GOTO2105
1801 NEXTI:GOTO2102
1900 FORI=1TO4:IFG(PL,I)=44THENA1=1
1901 IFG(PL,I)=45THENA2=1
1902 IFG(PL,I)=46THENA3=1
1903 IFG(PL,I)=51THENA4=1
1904 IFG(PL,I)=54THENA5=1
1905 IFG(PL,I)=36THENA5=-1
1906 NEXTI:RETURN
1950 FORI=1TO4:IFG(PL,I)=43ORG(PL,I)=44ORG(PL,I)=45ORG(PL,I)=46ORG(PL,I)=54ORG(PL,I)=51THENG(PL,I)=0
1955 NEXTI:GOTO300
1990 IFL<>67THENM$="I DON'T SEE A SAFE HERE":GOTO30
1991 IFN(51,1)<>-48THENM$="THE SAFE HAS BEEN OPENED BY SOMEONE ELSE":GOTO30
1992 GOTO2243
2000 Z=A:GOTO1100
2003 Z=B:GOTO1100
2004 Z=C:GOTO1100
2005 Z=D:GOTO1100
2006 Z=E:GOTO1100
2007 Z=F:GOTO1100
2008 IFOB=0THEN30
2009 GOTO1200
2010 IFG(PL,1)<>-ZANDG(PL,2)<>-ZANDG(PL,3)<>-ZANDG(PL,4)<>-ZTHENM$="IT IS IN THE "+N$(-Z,1):GOTO30
2011 FORI=1TO4:IFG(PL,I)=0THENG(PL,I)=OB:N(OB,1)=0:M$="I HAVE THE "+N$(OB,1):GOTO2013
2012 NEXTI:M$="I AM CARRYING TOO MUCH"
2013 IFN(OB,1)=0ANDOB=40ANDN(41,1)=0THEN2015
2014 GOTO30
2015 FORI=1TO4:FORJ=1TO4:IFG(I,J)=41THEN30
2016 NEXTJ,I:N(41,1)=L:M$="WOW! IT'S UNCOVERED SOMETHING!":GOTO30
2018 IFOB=0THENM$="I DON'T KNOW WHAT ONE OF THOSE IS":GOTO30
2019 NN=0:FORI=1TO4:IFG(PL,I)=OBTHENNN=I:I=5
2020 NEXTI:IFNN=0THENM$="I DON'T HAVE IT":GOTO30
2021 IFN(OB,0)=2THEN2025
2022 G(PL,NN)=0:N(OB,1)=L:M$="OK":GOTO30
2025 M$="I CAN'T DROP IT!":IFOB=1THENM$=M$+" STARFLEET RULES: LANDING PARTIES MUST CARRY PHASERS"
2026 GOTO30
2032 N(PL+26,1)=L:GOSUB1000:L=N(PL+26,1):GOTO2071
2033 CLS:PRINT"REALLY QUIT (Y/N)?"
2034 I$=INKEY$:IFI$="N"THEN2071
2035 ON-(I$="")GOTO2034:GOTO102
2040 IFOB<>18THEN2050
2041 IFL<>7ANDL<>10ANDL<>34ANDL<>43ANDL<>23ANDL<>24ANDL<>21ANDL<>22ANDL<>26ANDL<>52THENM$="THERE'S NO COM-UNIT":GOTO30
2042 GOSUB1020:IFCM=1THENN(35,1)=L
2044 IFCM=2THENON1-(N(26+AS,1)<55)GOTO2048:N(26+AS,1)=L
2045 IFCM=3THENON1-(N(26+BS,1)<55)GOTO2048:N(26+BS,1)=L
2046 IFCM=4THENON1-(N(26+CS,1)<55)GOTO2048:N(26+CS,1)=L
2047 GOTO2071
2048 M$="HE'S NOT ON THE SHIP":GOSUB1030:GOTO30
2050 IFOB<>19THEN2060
2051 IFL<>43ANDL<>45THENM$="I DON'T SEE AN INTERCOM HERE":GOTO30
2052 M$="":NC=0:FORI=1TO4:IFN(I+26,1)=L+5THENNC=NC+1
2053 NEXTI:IFNC=0THENM$="THERE IS NO ONE IN THE SHUTTLECRAFT":GOTO30
2054 NN=NC:FORI=1TO4:IFN(I+26,1)=L+5THENM$=M$+N$(I+26,1):NC=NC-1:M$=M$+", "
2055 NEXTI:M$=M$+"SAY":IFNN=1THENM$=M$+"S HELLO":GOTO30
2056 M$=M$+" HELLO":GOTO30
2060 IFOB<>34THENM$="DON'T BE RIDICULOUS":GOTO30
2061 IFL<>2THENM$="I DON'T SEE THE CAPTAIN'S UNIT HERE":GOTO30
2062 IFPL<>1THENM$="ONLY CAPTAIN KIRK CAN DO THAT!":GOTO30
2063 GOSUB1010:IFCM=1THENGOSUB1300
2064 IFCM=2THEN1500
2065 IFCM=3THENM$=EM$:ON-(AR=1)GOTO2066:M$="WE'VE ONLY GOT 10 PEOPLE DOWN HERE WORKING"
2066 IFCM=4THENM$=SR$:ON-(SI=1)GOTO2067:M$="ONLY ONE PATIENT WITH FEVER DOWN HERE SIR":SI=1
2067 IFCM=5THENM$="THERE'S NO MAJOR DAMAGE SIR. JUST A MINOR SCRATCH ON THE HULL"
2068 IFCM=6THENM$="NOTHING TO AIM PHASERS AT"
2069 IFCM=7THENM$="THE PHASERS AREN'T AIMED"
2070 GOSUB1030:J$=M$:GOSUB1:GOTO11
2071 GOSUB1030:GOTO11
2072 PRINT:PRINT"I AM CARRYING:":FORI=1TO4:IFG(PL,I)>0THENPRINTN$(G(PL,I),1)
2073 NEXTI:GOTO11
2074 M$="NOTHING SPECIAL":FORI=1TO4:ON-(G(PL,I)=OB)GOTO2075:NEXTI:IFN(OB,1)<>LTHENM$="I DON'T SEE IT":GOTO30
2075 IFOB=1THENM$="IT'S SET TO KILL"
2076 IFOB=2THEN1450
2077 IFOB=3THENGOSUB1400
2078 IFOB=4THENM$="DARK GREEN AND THICK IN A HYPO"
2079 IFOB=5THENM$="CLEAR AND WATERY IN A SMALL HYPO"
2080 IFOB=6THENM$="STRONGER THAN A PINT OF SCOTCH":ON-(PL=4)GOTO2082:M$="YELLOWISH IN COLOUR IN A HYPO"
2082 IFOB=8THENM$="VERY SHARP AND SHINY"
2084 IFOB=11THENIFPL=4THENM$="ANCIENT AND DELICIOUS!"
2085 IFOB=12THENGOSUB1600
2086 IFOB=13THENM$="ADJUSTABLE WITH A SMALL SCREW"
2087 IFOB=23THENM$="SHE IS VERY BUSY"
2088 IFOB=24THENM$="HE IS CHECKING THE SHIP'S PHASER BANKS"
2089 IFOB=22THENM$="IT HAS A TOUCH SWITCH"
2090 IFOB=25THENM$="HE'S AT THE HELM"
2091 IFOB=26THENM$="HE IS WAITING TO BEAM SOMEONE DOWN":ON-(AR=1)GOTO2092:M$="HE'S DOING NOTHING--WE'RE IN DEEP SPACE"
2092 IF(OB>=27ANDOB<=30)ANDPL<>OB-26THENM$="HE IS NOT DOING ANYTHING SPECIAL AND HE JUST TOLD ME TO STOP STARING"
2093 IF(OB>=27ANDOB<=30)ANDPL=OB-26THENM$="THAT'S ME!"
2094 IFOB=31OROB=32OROB=33THENM$="WELL MADE TO ACHIEVE ITS PURPOSE"
2095 IFOB=36THENM$="THEY HAVE IRILLIUM IMPURITIES IN THEM"
2096 IFOB=37THENM$="IT'S WET"
2097 IFOB=38THENM$="PURE AND EXECELLENT"
2098 IFOB=9THENGOSUB1700
2099 IFOB=39THENIFN$(39,1)="ANTITOXIN HYPO"THENM$="IT'S READY TO SAVE THE CREW"
2100 IFOB<>55THEN2105
2101 GOTO1800
2102 FORI=1TO4:FORJ=1TO4:IFG(I,J)=41THENM$=N$(I+26,1)+"HAS THE KEY":GOTO2105
2103 NEXTJ,I:M$="NO ONE HAS THE KEY"
2105 IFOB=42THENM$="IT IS FLOATING ABOVE THE GROUND ON A CUSHION OF AIR"
2106 IFOB=43THENM$="IT CONTAINS DISTILLING EQUIPMENT: ":FORI=1TONO:IFN(I,1)=-43THENM$=M$+N$(I,1)+", "
2107 IFOB=43THENNEXTI:M$=LEFT$(M$,LEN(M$)-2)
2108 IFOB=47THENM$="IT WON'T LET ME THROUGH. IT IS INVISIBLE AND ELECTRIFIED"
2109 IFOB=48THENM$="IT HAS A KEYPAD TO TYPE THE COMBINATION ON"
2110 IFOB=51THENM$="IT HAS A NOZZLE TO FIT A BUNSEN BURNER"
2111 IFOB=52THENM$="IT IS ENCODED-- YOU NEED A TAPE READER"
2112 IFOB=54THENM$="IT IS PERFECT FOR MAKING THE ANTITOXIN"
2199 GOTO30
2200 IFO$<>"UP"ANDO$<>"DOWN"ANDO$<>""THENM$="I CAN'T DO THAT!?":GOTO30
2201 IFO$="UP"ANDL<55THENM$="I HAVEN'T BEAMED down YET!":GOTO30
2202 IFO$="DOWN"ANDL>54THENM$="I'M ALREADY BEAMED DOWN":GOTO30
2203 O$="UP":ON-(L>54)GOTO2204:O$="DOWN"
2204 IFAR=0THENM$="I CAN'T-- WE'RE IN DEEP SPACE!":GOTO30
2205 IFO$="UP"THENL=53
2206 IFO$="DOWN"ANDL<>53THENM$="I CAN'T FROM HERE":GOTO30
2207 IFO$="DOWN"THENL=55
2208 FORCC=150TO250STEP10:SOUNDCC,1:NEXT:FORCC=240TO150STEP-10:SOUNDCC,1:NEXT:GOTO2071
2209 IFL<>57THENM$="THERE'S NO WATER TO DIVE INTO":GOTO30
2210 L=60:GOTO2071
2211 IFOB=50THENM$="IT IS FILLED ALREADY!":GOTO30
2212 IFOB<>39THENM$="I CAN'T FILL THAT":ON-(OB=45)GOTO30:M$="DON'T BE RIDICULOUS!":GOTO30
2213 IFN(38,1)<>LTHENM$="THE ANTITOXIN ISN'T HERE":GOTO30
2214 IFG(PL,1)<>39ANDG(PL,2)<>39ANDG(PL,3)<>39ANDG(PL,4)<>39THENM$="I DON'T HAVE A EMPTY HYPO":GOTO30
2215 N(38,1)=0
2216 N$(39,1)="ANTITOXIN HYPO":SA=1:N$="ME":ON-(PL=1)GOTO2217:N$="KIRK"
2217 M$="UHURA CALLS "+N$+". 'A REMINDER HAS ARRIVED FROM STARFLEET. PLEASE REVIEW RECENTLY SENT ORDERS!'":GOTO30
2218 IFL<>59ORUN=1THENM$="THERE ISN'T A LOCKED DOOR HERE":GOTO30
2219 IFG(PL,1)<>41ANDG(PL,2)<>41ANDG(PL,3)<>41ANDG(PL,4)<>41THENM$="I DON'T HAVE THE KEY!":GOTO30
2220 UN=1:N$(55,1)="OPEN DOOR":GOTO2071
2221 IFOB=42ANDGU=1THENM$="FSSSS! THE GUARD DROID JUST BLEW UP!":GU=0:N$(42,1)="SMASHED DROID":GOTO30
2222 IFOB=49ANDG2=1THENM$="THE GUARD WAS TOO QUICK AND HE SHOT ME!":GOTO100
2223 IFOB=47ANDSH=1THENM$="THE SHIELD ABSORBED MY SHOT":GOTO30
2224 M$="DON'T BE RIDICULOUS!":GOTO30
2225 A1=0:A2=0:A3=0:A4=0:A5=0:IFN(38,1)<>0ORSA=1THENM$="I CAN'T DO THAT!":GOTO30
2226 IFG(PL,1)<>43ANDG(PL,2)<>43ANDG(PL,3)<>43ANDG(PL,4)<>43THEN2230
2227 IFN(44,1)=-43THENA1=1
2228 IFN(45,1)=-43THENA2=1
2229 IFN(46,1)=-43THENA3=1
2230 GOSUB1900
2232 IFA1=1ANDA2=1ANDA3=1ANDA4=1ANDA5=1THEN2240
2233 M$="YOU DON'T HAVE: :IFA1=0THENM$=M$+"A BUNSEN, "
2234 IFA2=0THENM$=M$+"A GLASS JAR, "
2235 IFA3=0THENM$=M$+"A TUBE, "
2236 IFA4=0THENM$=M$+"A GAS BOTTLE, "
2237 IFA5=0THENM$=M$+"ANY RYETALYN, "
2238 IFA5=-1THENM$=M$+"ANY PURE RYETALYN, "
2239 M$=LEFT$(M$,LEN(M$)-2):GOTO30
2240 M$="THE ANTITOXIN DRIPS OUT FROM THE TUBE":N(38,1)=L:N(9,1)=26:GOTO1950
2241 M$="USE ASSEMBLE":GOTO30
2242 GOTO1990
2243 IFO$=""THENINPUT"TYPE WHAT";O$
2244 IFO$<>CB$THENM$="THE SAFE DOOR STAYS SHUT!":GOTO30
2245 GOTO1590
2246 IFPL<>2THENM$="ONLY SPOCK KNOWS HOW TO NERVE-PINCH PEOPLE":GOTO30
2247 IFOB<>49THENM$="DON'T BE RIDICULOUS!":GOTO30
2248 IFL<>N(OB,1)THENM$="HE IS NOT HERE":GOTO30
2249 M$="HE IS ALREADY STUNNED":ON-(G2=0)GOTO2250:G2=0:N$(49,1)="STUNNED GUARD":M$="HE SAID 'AAAAGH!'":GOTO30
2250 IFOB=9THENM$="TRY 'EXAMINE ORDERS'":GOTO30
2251 IFOB<>52THENM$="DON'T BE SILLY":GOTO30
2252 GOTO1490
2253 IFA1=0THENM$="I DON'T HAVE THE TAPE":GOTO30
2254 IFA2=0THENM$="I DON'T HAVE A TAPE READER":GOTO30
2255 M$="THE TAPE SAYS: THE SAFE'S COMBINATION IS "+CB$:GOTO30
2256 IFO$<>"WATER"THENM$="I CAN'T":GOTO30
2257 FORI=1TO4:IFG(PL,I)=50THEN2259
2258 NEXTI:M$="I DON'T HAVE THE WATER":GOTO30
2259 IFN$(50,1)="EMPTY BUCKET"THENM$="I HAVE ALREADY USED THE WATER SOMEWHERE ELSE":GOTO30
2260 N$(50,1)="EMPTY BUCKET":IFL<>66ORSH=0THENM$="SPLASH! THAT WAS STUPID!":GOTO30
2261 SH=0:M$="SPLASH! FSSSS! THE SHIELD SHORT-CIRCUITED.":N$(47,1)="SMASHED SHIELD":GOTO30
10001 DATAAT THE HELM OF THE ENTERPRISE. SULU IS AT THE MAIN CONTROLS,,3,2,,,0
10002 DATAIN THE COMMAND SEAT. I CAN SEE A ROW OF LABELLED SWITCHES IN FRONT OF ME,1,7,,,,0
10003 DATAAT PHASER CONTROL. CHEKOV IS BUSY CALIBRATING THE AIMING SYSTEM,4,5,,1,,,BY THE MAIN VIEWSCREEN,,,3,,,0
10005 DATAAT THE SCIENCE OFFICER'S POST. I CAN SEE MANY CONTROLS BUT MOST ARE BROKEN,,,6,3,,0
10006 DATAAT UHURA'S POST. UHURA IS SCANNING SUB-SPACE FREQUENCIES,5,,,7,,0
10007 DATAIN THE BRIDGE OF THE ENTERPRISE. UHURA IS BUSY TO THE RIGHT. THERE IS A COM-UNIT ON THE WALL,,6,8,2,,0
10008 DATAIN THE LIFT,7,,,,,9,IN THE LIFT,10,,,,8,16,IN SICKBAY. THERE ARE TWO BEDS AND A COM-UNIT,12,11,9,15,,0
10011 DATABY A BED MARKED I. THERE IS A SCANNER ABOVE IT,,,,10,,,BY SOME SHELVES,,13,10,14,,0
10013 DATABY A BED MARKED II. THERE IS A SCANNER ABOVE IT,,,,12,,,BY SOME OPEN CUPBOARDS,,12,15,,,0
10015 DATABY A DRAWER FULL OF TRICORDERS. ONLY ONE IS OPERATIVE,14,10,,,,,IN THE LIFT,17,,,,9,31
10017 DATAIN A CORRIDOR ON LEVEL 3. THERE ARE ROOMS ON EACH SIDE,18,21,16,24,,0
10018 DATAIN A CORRIDOR. THERE ARE MORE ROOMS ,19,22,17,23,,0
10019 DATAIN A CORRIDOR. THERE IS A SMALL SERVICE HATCH,,20,18,25,,0
10020 DATAIN A SERVICE HATCH. THERE IS A LADDER GOING UP,,,,19,27,0
10021 DATAIN A ROOM WITH A TABLE WITH A COM-UNIT. THE DOOR HAS A SIGN READING 'C. KIRK',,,,17,,0
10022 DATAIN A ROOM WITH VULCAN FURNISHINGS,,,,18,,0
10023 DATAIN A ROOM WITH A SIGN ON THE DOOR SAYING 'DR MCCOY',,18,,,,0
10024 DATAIN A ROOM WITH A SIGN ON THE DOOR SAYING 'MR. SCOTT' AND A DRINKS CABINET,,17,,,,0
10025 DATAIN A CORRIDOR OUTSIDE A ROOM. THE ROOM HAS A SIGN SAYING 'BRIEFING ROOM',26,19,,,,0
10026 DATAIN THE BRIEFING ROOM. A LARGE MONITOR DOMINATES THE TABLE,,,25,,,0
10027 DATAIN A SERVICE VENT,,,,,28,20,BETWEEN TWO SERVICE LADDERS,,29,,,,27,ON A SERVICE LADDER,,,,28,,30
10030 DATAON A SERVICE LADDER,,,,,29,70,IN THE LIFT,33,,,,16,42,BY A SERVICE LADDER,,,,40,71,0
10033 DATA"IN ENGINEERING. A MAP SHOWS THE LEVELS: 1=BRIDGE 2=SICKBAY 3=QUARTERS 4=ENGINEERING",37,,31,,,0
10034 DATABY THE MAIN ENGINE CONTROL PANEL,,,35,,,,ON SOME STAIRS,34,,36,,,37,BY THE ENGINES IT IS HOT,35,,,,,0
10037 DATABY SOME STAIRS,41,38,33,,35,,BEHIND A WALL TO THE NORTH OF SOME CONTROL PANELS,,,39,37,,0
10039 DATABY SOME CONTROL PANELS,38,,,,,,BY A SERVICE HATCH,,32,,41,,0
10041 DATA"BY A MAP OF THE ENTERPRISE'S LOWER LEVELS: 5=SHUTTLE BAY 6=TRANSPORTER",,40,37,,,,IN THE LIFT,43,,,,31,51
10043 DATABY A SHUTTLE BAY MARKED I. THERE'S A COM-UNIT AND AN INTERCOM TO TALK TO THE SHUTTLECRAFT CREW,45,44,42,,,0
10044 DATAIN SHUTTLE BAY I. THERE ARE STEPS LEADING UP,,,,43,47,0
10045 DATABY A SHUTTLE BAY MARKED II. THERE IS AN INTERCOM,,,43,46,,0
10046 DATAIN SHUTTLE BAY II. THERE ARE STEPS LEADING UP,,45,,,49,,IN THE AIRLOCK OF SHUTTLECRAFT I,48,,,,,44
10048 DATAIN SHUTTLECRAFT I,,,47,,,,IN THE AIRLOCK OF SHUTTLECRAFT II,50,,,,,46,IN SHUTTLECRAFT II,,,49,,,0
10051 DATAIN THE LIFT,52,,,,42,0
10052 DATAIN THE TRANSPORTER ROOM. THERE'S A COM-UNIT. MR RECK IS AT THE CONTROLS. THE PLATES ARE EAST,,53,51,54,,0
10053 DATAON THE TRANSPORTER PLATES,,,,52,,,IN A STORE ROOM,,52,,,,,ON THE LANDING AREA,56,,,,,,NEAR A POOL,,57,55,,,0
10057 DATABY A POOL,,58,59,56,,,NEAR A POOL,,61,,57,,,BY THE MAIN DOORS OF A LARGE BUILDING,57,,,,,0
10060 DATAAT THE BOTTOM OF A POOL,,,,,57,,BY A PILE OF ROCKS,,,,58,,0
10062 DATAJUST INSIDE THE MAIN DOORS OF THE BUILDING,,63,66,59,,,OUTSIDE A STORE ROOM,,64,65,62,,0
10064 DATAIN A STORE ROOM,,,,63,,,BY A TABLE,63,,,,,,IN A LONG PASSAGE,62,,67,,,,IN A WELL FURNISHED ROOM,66,,68,,,0
10068 DATANEAR A LABORATORY,67,,69,,,,IN A LABORATORY. THERE ARE PILES OF BROKEN GLASSWARE EVERYWHERE,68,,,,,0
10070 DATANEAR THE LOWER LEVELS,,,,,30,71,JUST OFF THE FLOOR,,,,,70,32
11001 DATAPHAS,PHASER,2,,TRIC,TRICORDER,,15,MEDI,MEDICAL KIT,5,12,TRUT,TRUTH SERUM,,-3
11005 DATASNAK,SNAKE-BITE SERUM,,-3,TRAN,TRANQUILLISER SERUM,,-3,LIFE,LIFE-READER,,-3
11008 DATASCAL,SCALPEL,,14,ORDE,ORDERS AND BRIEF,3,26,TORC,TORCH,,23,WHIS,BOTTLE OF WHISKY,6,24,TOOL,TOOL KIT,,34
11013 DATASPAN,SPANNER,,-12,SCRE,SCREWDRIVER,,-12,PLIE,PLIERS,,-12,CUTT,WIRE CUTTERS,,-12,COM,COM-UNIT,1,0
11018 DATASWIT,SWITCH,1,-17,BUTT,RED BUTTON,1,-20,INTE,INTERCOM,1,,SIGN,SIGN,1,,CAPT,CAPTAIN'S COM-UNIT,1,2
11023 DATAUHUR,UHURA,1,6,CHEK,CHEKOV,1,3,SULU,SULU,1,1,RECK,MR RECK,1,52,KIRK,KIRK,1,2,SPOC,SPOCK,1,5
11029 DATAMCCO,MCCOY,1,10,SCOT,SCOTTY,1,33,RADI,RADIATION SUIT,,54,VACU,VACUUM SUIT,,54,DIVI,DIVING SUIT,,54
11034 DATATOUC,TOUCH BUTTON,1,-22,GUAR,SECURITY GUARD,1,,BAD,BAD RYETALYN CRYSTALS,,60,POOL,LARGE POOL,1,57
11038 DATAANTI,ANTITOXIN,,,HYPO,EMPTY HYPO,,-3,ROCK,SMALL ROCK,,61,KEY,GOLDEN KEY,,,DROI,GUARD DROID,1,63
11043 DATAEQUI,BAG OF EQUIPMENT,,64,BUNS,BUNSEN BURNER,,-43,JAR,GLASS JAR,,-43,TUBE,LONG COLD TUBE,,-43
11047 DATASHIE,FORCE SHIELD,1,66,SAFE,WALL SAFE,1,67,HOLB,HOLBERG,1,68,BUCK,BUCKET OF WATER,,60,BOTT,GAS BOTTLE,,-48
11052 DATATAPE,TICKER TAPE,,65,READ,TAPE READER,,5,PERF,PERFECT RYETALYN,,69,DOOR,LOCKED DOOR,1,59
12001 DATANORTH,EAST,SOUTH,WEST,UP,DOWN,GET,TAKE,MENU,QUIT,DROP,PUSH,LOOK,INVENTORY,EXAMINE,BEAM,DIVE,FILL
12002 DATAUNLOCK,SHOOT,ASSEMBLE,DISTILL,TYPE,NERVEPINCH,READ,THROW
20000 NV=26:NO=55:NL=71:DIMV$(NV),N$(NO,1),N(NO,1),G(4,4):G(1,1)=1:G(2,1)=1:G(3,1)=1:G(4,1)=1:CB$=MID$(STR$(100000),2)
20002 FORI=1TONL:READN$,A,B,C,D,E,F:NEXTI:FORI=1TONO:READN$(I,0),N$(I,1),N(I,0),N(I,1):NEXTI
20004 FORI=1TONV:READV$(I):NEXTI:EM$="WE'VE ONLY GOT 5 PEOPLE WORKING DOWN HERE":UN=00:SH=1:GU=1:G2=1
20007 SI=0:SR$="WE'VE GOT A BAD OUTBREAK OF RIGELLIAN FEVER. WE NEED pure RYETALYN TO PROCESS INTO AN ANTITOXIN"
20008 SS=0:PL$="HOLBERG":RETURN
