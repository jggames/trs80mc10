0 CLS:PRINT@235,"PLEASE WAIT"
1 CLEAR 2000:GOSUB 20000
2 CLS:PRINT"LOAD(Y/N)?"
3 I$=INKEY$:IF I$="N" THEN 10
4 IF I$<>"Y" THEN 3 ELSE INPUT"TITLE";A$:OPEN"I",#-1,A$
5 INPUT#-1,L,PL,KN,AA,FI,FM$,SI,SR$,DA,PL$,AR,DR$,UN,SH,GU,G2,SS,SA
6 FOR I=1 TO 4:FOR J=1 TO 4:INPUT#-1,G(I,J):NEXT J,I
7 FOR I=1 TO NO:INPUT#-1,N$(I,0),N$(I,1),N(I,0),N(I,1):NEXT I
8 FOR I=1 TO NV:INPUT#-1,V$(I):NEXT I
9 CLOSE #-1:GOSUB 1030:GOTO 11
10 GOSUB 1000:L=N(PL+26,1):GOSUB 1030:PRINT"SPACE TREK I BY M.EDWARDS"
11 PRINT" WHAT DO YOU WANT ME TO DO?":PRINT" ";:INPUT D$
12 V$="":O$="":FOR I=1 TO LEN(D$):IF MID$(D$,I,1)=" " THEN V$=LEFT$(D$,I-1):O$=RIGHT$(D$,LEN(D$)-I):I=LEN(D$)+4
13 NEXT I:IF V$="" THEN V$=D$
14 M$="I CAN'T DO THAT":VB=0:OB=0
15 LL=LEN(V$):FOR I=1 TO NV
16 L1=LL:IF LEN(V$(I))<LL THEN L1=LEN(V$(I))
17 IF LEFT$(V$(I),L1)=LEFT$(V$,L1) THEN VB=I:I=NV+1
18 NEXT I
19 IF VB=0 THEN M$="I DON'T UNDERSTAND THAT VERB"
20 IF O$="" THEN 25
21 LL=LEN(O$)
22 FOR I=1 TO NO:L1=LL:IF LEN(N$(I,0))<LL THEN L1=LEN(N$(I,0))
23 IF LEFT$(N$(I,0),L1)=LEFT$(O$,L1) THEN OB=I:I=NO+1
24 NEXT I
25 IF OB=0 AND O$<>""THEN M$="I DON'T KNOW WHAT ONE OF THOSE  IS." ELSE IF V$="" THEN VB=0
26 ON VB GOTO 2000,2003,2004,2005,2006,2007,2008,2008,2022,2023,2018,2041,2071,2072,2074,2200,2209,2211,2218,2221,2225,2241,2242,2246,2250,2256
30 IF OB=17 AND VB=15 THEN M$="COM-UNITS HAVE ON/OFF PUSH      SWITCHES ON THEM."
31 IF OB=20 AND VB=15 AND (L=46 OR L=43) THEN M$="IT HAS A RED BUTTON ON IT."
32 IF LEFT$(OB$,3)="CONTROLS" AND VB=15 AND L=5 THEN M$="THEY ARE MAINLY BROKEN."
33 IF L=60 THEN FOR I=1 TO 4:IF G(PL,I)<>33 THEN NEXT I:M$="I AM DROWNED":GOTO 100
34 IF VB=15 AND(LEFT$(O$,4)="VIEW" OR LEFT$(O$,4)="SCRE") AND L=4 THEN IF AR=0 THEN M$="IT SHOWS DEEP SPACE" ELSE IF AR=1 AND PL$="HOLBERG" THEN M$="IT SHOWS THE PLANET HOLBERG."
99 PRINTM$:PRINT:GOTO 11
100 CLS:PRINTM$:PRINT:PRINTN$(PL+26,1);" IS DEAD!"
101 PRINT"GAME OVER!"
102 PRINT:PRINT"PLAY AGAIN(Y/N)?"
103 I$=INKEY$:IF I$="N" THEN END
104 IF I$<>"Y" THEN 103
105 RUN
200 IF AR=1 AND SA=1 AND PL$="STARFLEET HQ" THEN ELSE GOSUB 1030:PRINTM$:GOTO 11
201 CLS:PRINT"'CAPTAIN JAMES, THERE IS AN     URGENT MESSAGE FOR YOU FROM     STARFLEET HQ,' SAID BHAN."
202 PRINT"'CAPTAIN JAMES - WE HAVE NEWS OFA DISCOVERY. WE HAVE RECENTLY   BEEN MONITORING COORDINATES     10:20:8 AND THERE IS AN UNKNOWN FORCE THERE. IT ISN'T A BLACK   HOLE BUT EVERYTHING THAT GOES   IN DOESN'T COME OUT! I THINK"
203 PRINT"THE REAL ADVENTURE IS JUST      BEGINNING.":PRINT:PRINT"TO BE CONTINUED ...":PRINT"... IN SPACE TREK II !"
204 PRINT@480,"PRESS A KEY";
205 IF INKEY$="" THEN 205
206 CLS:END
300 FOR I=43 TO 46:N(I,1)=0:NEXT I:N(51,1)=0:N(54,1)=0:GOTO 30
999 END
1000 CLS:PRINT@33,STRING$(30,"*"):PRINT:PRINTTAB(11);"1. JAMES":PRINT:PRINTTAB(11);"2. SELVON":PRINT:PRINTTAB(11);"3. SPEARS":PRINT:PRINTTAB(11);"4. MC'CRINDLE":PRINT:PRINT" ";STRING$(30,"*")
1001 PRINT:PRINTTAB(9);"WHO TO REPORT?"
1002 I$=INKEY$:IF I$<"1" OR I$>"4" THEN 1002 ELSE PL=VAL(I$)
1003 RETURN
1010 CLS:PRINT@33,STRING$(30,"*"):PRINT
1011 PRINTTAB(5);"1. SET COURSE":PRINTTAB(5);"2. SELECT WARP OR STOP":PRINTTAB(5);"3. ENGINEERING REPORT":PRINTTAB(5);"4. SICK BAY REPORT":PRINTTAB(5);"5. DAMAGE REPORT":PRINTTAB(5);"6. AIM PHASERS":PRINTTAB(5);"7. FIRE PHASERS":PRINT:PRINT" ";
1012 PRINTSTRING$(30,"*"):PRINT:PRINTTAB(9);"WHICH COMMAND?"
1013 I$=INKEY$:IF I$<"1" OR I$>"7" THEN 1013
1014 CM=VAL(I$):RETURN
1020 CLS:PRINT@33,STRING$(30,"*")
1021 PRINT:PRINTTAB(8);"1. CALL SECURITY":PRINT
1022 IF PL=1 THEN AS=2:BS=3:CS=4 ELSE IF PL=2 THEN AS=1:BS=3:CS=4 ELSE IF PL=3 THEN AS=1:BS=2:CS=4 ELSE AS=1:BS=2:CS=3
1023 PRINTTAB(8);"2. CALL ";N$(AS+26,1):PRINT:PRINTTAB(8);"3. CALL ";N$(BS+26,1):PRINT:PRINTTAB(8);"4. CALL ";N$(CS+26,1):PRINT:PRINTTAB(8);"5. SWITCH OFF":PRINT:PRINT" ";STRING$(30,"*"):PRINT:PRINTTAB(13);"WHICH?";
1024 I$=INKEY$:IF I$<"1" OR I$>"5" THEN 1024
1025 CM=VAL(I$):RETURN
1030 CLS:PRINTN$(PL+26,1);" REPORTING:":PRINT:N(PL+26,1)=L:IF L=60 THEN FOR I=1 TO 4:IF G(PL,I)=33 THEN ELSE NEXT I:M$="I AM DROWNED!":GOTO 100
1040 PRINT"I AM:":RESTORE:FOR I=1 TO L:READ N$,A,B,C,D,E,F:NEXT I:PRINTN$:IF L=5 THEN PRINT"THE INSTRUMENTS SHOW RYETALYN ONTHE PLANET HOLBERG." ELSE IF L=59 AND UN=1 THEN B=62
1041 PRINT:PRINT"EXITS GO:";:IF A THEN PRINT"N:";
1042 IF B THEN PRINT"E:";
1043 IF C THEN PRINT"S:";
1044 IF D THEN PRINT"W:";
1045 IF E THEN PRINT"U:";
1046 IF F THEN PRINT"D:";
1047 IF (A OR B OR C OR D OR E OR F)=0 THEN PRINT"NONE:";
1048 PRINT
1049 PRINT"I CAN SEE:":NS=0:FOR I=1 TO NO:IF N(I,1)=L THEN NS=NS+1
1050 NEXT I:IF NS=1 THEN PRINT"NOTHING":GOTO 1060
1051 NS=NS-1:FOR I=1 TO NO:IF N(I,1)=L AND I<>PL+26 THEN NS=NS-1:PRINTN$(I,1);:IF NS=0 THEN PRINT"." ELSE PRINT","
1052 NEXT I
1060 PRINT:RETURN
2000 Z=A
2001 IF Z=0 THEN M$="I CAN'T GO THAT WAY.":GOTO 30 ELSE IF L=66 AND SH=1 AND Z=C THEN M$="THE SHIELD STOPS ME.":GOTO 30 ELSE IF L=63 AND GU=1 THEN M$="THE GUARD DROID STOPS ME.":GOTO30 ELSE IF L=68 AND Z=C AND G2=1 THEN M$="THE HOLBERG STOPS ME.":GOTO 30
2002 L=Z:GOSUB 1030:GOTO 11
2003 Z=B:GOTO 2001
2004 Z=C:GOTO 2001
2005 Z=D:GOTO 2001
2006 Z=E:GOTO 2001
2007 Z=F:GOTO 2001
2008 IF OB=0 THEN 30 ELSE IF N(OB,0)=1 THEN M$="I CAN'T GET THAT":GOTO 30 ELSE IF (N(OB,0)>2 AND N(OB,0)<7) AND PL<>N(OB,0)-2 THEN M$="I'M AFRAID ONLY "+N$(24+N(OB,0),1)+" CAN GET IT.":GOTO 30
2009 IF N(OB,1)>=0 AND N(OB,1)<>L THEN M$="I DON'T SEE IT HERE":GOTO 30 ELSE IF OB=38 THEN M$="IT RUNS THROUGH MY FINGERS":GOTO 30
2010 Z=N(OB,1):IF Z>-1 THEN 2015
2011 IF G(PL,1)<>-Z AND G(PL,2)<>-Z AND G(PL,3)<>-Z AND G(PL,4)<>-Z THEN M$="IT IS IN THE "+N$(-Z,1):GOTO 30
2015 FOR I=1 TO 4:IF G(PL,I)=0 THEN G(PL,I)=OB:N(OB,1)=0:M$="I HAVE THE "+N$(OB,1) ELSE NEXT I:M$="I AM CARRYING TOO MUCH."
2016 IF N(OB,1)=0 AND OB=40 AND N(41,1)=0 THEN ELSE 30
2017 FOR I=1 TO 4:FOR J=1 TO 4:IF G(I,J)=41 THEN 30 ELSE NEXT J,I:N(41,1)=L:M$="WOW! IT'S UNCOVERED SOMETHING!":GOTO 30
2018 IF OB=0 THEN M$="I DON'T KNOW WHAT ONE OF THOSE  IS.":GOTO30 ELSENN=0:FOR I=1 TO 4:IF G(PL,I)=OB THEN NN=I:I=5
2019 NEXT I:IF NN=0 THEN M$="I DON'T HAVE IT":GOTO 30
2020 IF N(OB,0)=2 THEN M$="I CAN'T DROP IT!!":IF OB=1 THEN M$=M$+CHR$(13)+"STARFLEET RULES: LANDING PARTY  MUST HAVE PHASERS.":GOTO 30 ELSE 30
2021 G(PL,NN)=0:N(OB,1)=L:M$="OK":GOTO 30
2022 N(PL+26,1)=L:GOSUB 1000:L=N(PL+26,1):GOSUB 1030:GOTO 11
2023 CLS:PRINT"REALLY QUIT (Y/N)?"
2024 I$=INKEY$:IF I$="N" THEN GOSUB 1030:GOTO 11
2025 IF I$<>"Y" THEN 2024 ELSE CLS:PRINT"SAVE(Y/N)?"
2026 I$=INKEY$:IF I$="N" THEN 102
2027 IF I$<>"Y" THEN 2026
2028 INPUT"TITLE";A$:OPEN"O",#-1,A$
2029 PRINT#-1,L,PL,KN,AA,FI,FM$,SI,SR$,DA,PL$,AR,DR$,UN,SH,GU,G2,SS,SA
2030 FOR I=1 TO4:FOR J=1 TO4:PRINT#-1,G(I,J):NEXT J,I
2031 FOR I=1 TO NO:PRINT#-1,N$(I,0),N$(I,1),N(I,0),N(I,1):NEXT I
2032 FOR I=1 TO NV:PRINT#-1,V$(I):NEXT I:CLOSE #-1
2033 CLS:PRINT"CONTINUE GAME(Y/N)?"
2034 I$=INKEY$:IF I$="Y" THEN M$="SPACE TREK I BY M.EDWARDS":GOSUB 1030:GOTO 30
2035 IF I$<>"N" THEN 2034
2040 GOTO 102
2041 IF OB<>18 THEN 2050 ELSEIF L<>7 AND L<>10 AND L<>34 AND L<>43 AND L<>23 AND L<>24 AND L<>21 AND L<>22 AND L<>26 AND L<>52 THEN M$="I DON'T SEE A COM-UNIT.":GOTO 30
2042 GOSUB 1020
2043 IF CM=1 THEN N(35,1)=L
2044 IF CM=2 THEN IF N(26+AS,1)<55 THEN N(26+AS,1)=L ELSE M$="HE'S NOT ON THE SHIP.":GOSUB 1030:GOTO 30
2045 IF CM=3 THEN IF N(26+BS,1)<55 THEN N(26+BS,1)=L ELSE M$="HE'S NOT ON THE SHIP.":GOSUB 1030:GOTO 30
2046 IF CM=4 THEN IF N(26+CS,1)<55 THEN N(26+CS,1)=L ELSE M$="HE'S NOT ON THE SHIP.":GOSUB 1030:GOTO 30
2047 GOSUB 1030:GOTO 11
2050 IF OB<>19 THEN 2060
2051 IF L<>43 AND L<>45 THEN M$="I DON'T SEE AN INTERCOM HERE.":GOTO 30
2052 M$="":NC=0:FOR I=1 TO 4:IF N(I+26,1)=L+5 THEN NC=NC+1
2053 NEXT I:IF NC=0 THEN M$="THERE IS NO-ONE IN SHUTTLECRAFT "+STRING$((L-41)/2,"I"):GOTO 30 ELSE NN=NC
2054 FOR I=1 TO 4:IF N(I+26,1)=L+5 THEN M$=M$+N$(I+26,1):NC=NC-1:IF NC=1 THEN M$=M$+" AND " ELSE IF NC>1 THEN M$=M$+", "
2055 NEXT I:M$=M$+" SAY":IF NN=1 THEN M$=M$+"S HELLO.":GOTO 30 ELSE M$=M$+" HELLO.":GOTO 30
2060 IF OB<>34 THEN M$="DON'T BE RIDICULOUS":GOTO 30
2061 IF L<>2 THEN M$="I DON'T SEE THE CAPTAIN'S UNIT  HERE.":GOTO 30 ELSE IF PL<>1 THEN M$="I DAREN'T - ONLY CAPTAIN JAMES  CAN DO THAT!!":GOTO 30
2062 GOSUB 1010
2063 IF CM=1 THEN IF AR=0 THEN M$="OK. COURSE IS SET FOR "+PL$:AR=-1 ELSE IF AR=-1 THEN M$="COURSE SET ALREADY." ELSE IF AR=1 THEN M$="WE'RE THERE ALREADY!"
2064 IF CM=2 THEN IF AR=-1 THEN M$="OK. TRAVELLING AT WARP SPEED TO "+PL$+"........"+CHR$(13)+"WE HAVE ARRIVED SIR.":AR=1:GOTO 200 ELSE IF AR=0 THENM$="NO COURSE SET YET, SIR." ELSE M$="WER'E THERE NOW!"
2065 IF CM=3 THEN IF AR=1 THEN M$=EM$ ELSE M$="WE'VE ONLY GOT 10 PEOPLE DOWN   HERE WORKING."
2066 IF CM=4 THEN IF SI=1 THEN M$=SR$ ELSE M$="ONLY ONE PATIENT WITH FLU DOWN  HERE, SIR."
2067 IF CM=5 THEN IF DA=1 THEN M$=DR$ ELSE M$="THERE'S NO DAMAGE AT ALL, SIR,  EXCEPT A SMALL DAMAGED PATCH ON THE OUTER HULL."
2068 IF CM=6 THEN IF KN=1 THEN AA=1:M$="OK. PHASERS AIMED." ELSE M$="NOTHING TO AIM PHASERS AT."
2069 IF CM=7 THEN IF KN=0 OR AA=0 THEN M$="THE PHASERS AREN'T AIMED" ELSE M$="FIRED AND HIT!!":FI=1
2070 GOSUB 1030:PRINTM$:GOTO 11
2071 GOSUB 1030:GOTO 11
2072 PRINT:PRINT"I AM CARRYING:":FOR I=1 TO 4:IF G(PL,I)>0 THEN PRINTN$(G(PL,I),1)
2073 NEXT I:GOTO 11
2074 M$="NOTHING SPECIAL":FOR I=1 TO 4:IF G(PL,I)=OB THEN ELSE NEXT I:IF N(OB,1)<>L THEN M$="I DON'T SEE IT.":GOTO 30
2075 IF OB=1 THEN M$="ITS SET TO KILL."
2076 IF OB=2 THEN IF L>54 AND L<62 THEN M$="IT SHOWS IMPURE RYETALYN." ELSE IF L>61 AND L<70 THEN M$="IT SHOWS PURE RYETALYN." ELSE M$="IT SHOWS NOTHING NOW."
2077 IF OB=3 THEN PRINT"IT CONTAINS:":M$="":FOR I=1 TO NO:IF N(I,1)=-3 THEN PRINTN$(I,1):NEXT I ELSE NEXT I
2078 IF OB=4 THEN M$="DARK GREEN AND THICK IN A HYPO."
2079 IF OB=5 THEN M$="CLEAR AND WATERY IN A SMALL     HYPO."
2080 IF OB=6 THEN IF PL=4 THEN M$="STRONGER THAN A PINT OF SCOTCH" ELSE M$="YELLOWISH IN COLOUR IN A HYPO."
2082 IF OB=8 THEN M$="VERY SHARP AND SHINY."
2084 IF OB=11 THEN IF PL=4 THEN M$="ANCIENT AND DELICIOUS!"
2085 IF OB=12 THEN M$="":PRINT"IT CONTAINS:":FOR I=1 TO NO:IF N(I,1)=-12 THEN PRINTN$(I,1):NEXT I ELSE NEXT I
2086 IF OB=13 THEN M$="ADJUSTABLE WITH A SMALL SCREW."
2087 IF OB=23 THEN M$="SHE IS VERY BUSY."
2088 IF OB=24 THEN M$="HE IS CHECKING THE SHIP'S       PHASER BANKS."
2089 IF OB=22 THEN M$="IT HAS A TOUCH SWITCH."
2090 IF OB=25 THEN M$="HE IS MANNING THE HELM."
2091 IF OB=26 THEN IF AR=1 THEN M$="HE IS WAITING TO BEAM SOMEONE   DOWN." ELSE M$="HE IS DOING NOTHING AS WE ARE INDEEP SPACE."
2092 IF (OB>=27 AND OB<=30) AND PL<>OB-26 THEN M$="HE IS NOT DOING ANYTHING SPECIALAND HE JUST TOLD ME TO STOP     STARING."
2093 IF (OB>=27 AND OB<=30) AND PL=OB-26 THEN M$="THAT'S ME!"
2094 IF OB=31 OR OB=32 OR OB=33 THEN M$="VERY WELL MADE TO SUIT ITS      PURPOSE."
2095 IF OB=36 THEN M$="THEY HAVE IRILLIUM IMPURITIES   IN THEM."
2096 IF OB=37 THEN M$="IT IS WET."
2097 IF OB=38 THEN M$="PURE AND EXECELLENT."
2098 IF OB=9 THEN IF SA=1 THEN M$="THEY SAY 'REPORT TO STARFLEET HQFOR YOUR NEXT ASSIGNMENT'":PL$="STARFLEET HQ":AR=0 ELSE M$="THEY SAY:"+CHR$(13)+"GOOD LUCK, CHRIS."
2099 IF OB=39 THEN IF N$(39,1)="ANTITOXIN HYPO" THEN M$="IT IS READY TO SAVE THE CREW."
2100 IF OB<>55 THEN 2105
2101 FOR I=1 TO 4:IF G(PL,I)=41 THEN M$="YOU HAVE THE KEY.":GOTO 2105 ELSE NEXT I
2102 FOR I=1 TO4:FOR J=1 TO 4:IF G(I,J)=41 THEN M$=N$(I+26,1)+" HAS THE KEY.":GOTO 2105 ELSE NEXT J,I
2103 M$="NOONE HAS THE KEY."
2105 IF OB=42 THEN M$="IT IS FLOATING ABOVE THE GROUND ON A CUSHION OF AIR."
2106 IF OB=43 THEN M$="IT CONTAINS DISTILLING EQUIPMENT":FOR I=1 TO NO:IF N(I,1)=-43 THEN M$=M$+N$(I,1)+CHR$(13)
2107 IF OB=43 THEN NEXT I
2108 IF OB=47 THEN M$="IT WON'T LET ME PAST IT. IT IS  INVISIBLE AND ELECTRIFIED."
2109 IF OB=48 THEN M$="IT HAS A KEYPAD TO TYPE THE     COMBINATION ON."
2110 IF OB=51 THEN M$="IT HAS A NOZZLE TO FIT A BUNSEN BURNER."
2111 IF OB=52 THEN M$="IT IS ENCODED - YOU NEDD A TAPE READER TO READ IT."
2112 IF OB=54 THEN M$="IT IS PERFECT FOR MAKING THE    ANTITOXIN."
2199 GOTO 30 ' END OF EXAMINES
2200 IF O$<>"UP" AND O$<>"DOWN" AND O$<>"" THEN M$="I CAN'T DO THAT!?":GOTO 30
2201 IF O$="UP" AND L<55 THEN M$="I HAVEN'T BEAMED down YET!":GOTO 30
2202 IF O$="DOWN" AND L>54 THEN M$="I'M ALREADY BEAMED DOWN.":GOTO 30
2203 IF L>54 THEN O$="UP" ELSE O$="DOWN"
2204 IF AR=0 THEN M$="I CAN'T - WER'E IN DEEP SPACE!":GOTO 30
2205 IF O$="UP" THEN L=53
2206 IF O$="DOWN" AND L<>53 THEN M$="I CAN'T FROM HERE.":GOTO 30
2207 IF O$="DOWN" THEN L=55
2208 PLAY"L255T255O1CDEFGABCDEFGABCDEFGABCDEFGAB":GOSUB 1030:GOTO 11
2209 IF L<>57 THEN M$="THERE'S NO WATER TO DIVE INTO.":GOTO 30
2210 L=60:GOSUB 1030:GOTO 11
2211 IF OB=50 THEN M$="IT IS FILLED ALREADY!":GOTO 30
2212 IF OB<>39 THEN IF OB=45 THEN M$="I CAN'T FILL THAT":GOTO 30 ELSE M$="DON'T BE RIDICULOUS!":GOTO 30
2213 IF N(38,1)<>L THEN M$="THE ANTITOXIN ISN'T HERE.":GOTO 30
2214 IF G(PL,1)<>39 AND G(PL,2)<>39 AND G(PL,3)<>39 AND G(PL,4)<>39 THEN M$="I DON'T HAVE A SPARE EMPTY    HYPO.":GOTO 30
2215 N(38,1)=0
2216 N$(39,1)="ANTITOXIN HYPO":SA=1:IF PL=1 THEN N$="ME" ELSE N$="JAMES"
2217 M$="BHAN SUDDENLY CALLS "+N$+"."+CHR$(13)+"'SOME ORDERS HAVE ARRIVED FROM  STARFLEET COMMAND. PLEASE FIND  THEM AS THEY ARE LOST ON THE    SHIP.'":GOTO 30
2218 IF L<>59 OR UN=1 THEN M$="THERE ISN'T A LOCKED DOOR HERE.":GOTO 30
2219 IF G(PL,1)<>41 AND G(PL,2)<>41 AND G(PL,3)<>41 AND G(PL,4)<>41 THEN M$="I DON'T HAVE THE KEY!":GOTO 30
2220 UN=1:N$(55,1)="OPEN DOOR":GOSUB 1030:GOTO 11
2221 IF OB=42 AND GU=1 THEN M$="FSSSS! THE GUARD DROID JUST BLEWUP!":GU=0:N$(42,1)="SMASHED DROID":GOTO 30
2222 IF OB=49 AND G2=1 THEN M$="THE GUARD WAS TOO QUICK AND HE  SHOT ME!!!":GOTO 100
2223 IF OB=47 AND SH=1 THEN M$="THE SHIELD ABSORBED MY SHOT":GOTO 30
2224 M$="DON'T BE RIDICULOUS!":GOTO 30
2225 A1=0:A2=0:A3=0:A4=0:A5=0:IF N(38,1)<>0 OR SA=1 THEN M$="I CAN'T DO THAT!":GOTO 30
2226 IF G(PL,1)<>43 AND G(PL,2)<>43 AND G(PL,3)<>43 AND G(PL,4)<>43 THEN 2230
2227 IF N(44,1)=-43 THEN A1=1
2228 IF N(45,1)=-43 THEN A2=1
2229 IF N(46,1)=-43 THEN A3=1
2230 FOR I=1 TO 4:IF G(PL,I)=44 THEN A1=1 ELSE IF G(PL,I)=45 THEN A2=1 ELSE IF G(PL,I)=46 THEN A3=1 ELSE IF G(PL,I)=51 THEN A4=1 ELSE IF G(PL,I)=54 THEN A5=1 ELSE IF G(PL,I)=36 THEN A5=-1
2231 NEXT I
2232 IF A1=1 AND A2=1 AND A3=1 AND A4=1 AND A5=1 THEN 2240
2233 M$="YOU DON'T HAVE:"+CHR$(13):IF A1=0 THEN M$=M$+"A BUNSEN"+CHR$(13)
2234 IF A2=0 THEN M$=M$+"A GLASS JAR"+CHR$(13)
2235 IF A3=0 THEN M$=M$+"A TUBE"+CHR$(13)
2236 IF A4=0 THEN M$=M$+"A GAS BOTTLE"+CHR$(13)
2237 IF A5=0 THEN M$=M$+"ANY RYETALYN" ELSE IF A5=-1 THEN M$=M$+"ANY PURE RYETALYN"
2238 GOTO 30
2240 M$="THE ANTITOXIN DRIPS OUT FROM THETUBE.":N(38,1)=L:N(9,1)=26:FOR I=1 TO 4:IF G(PL,I)=43 OR G(PL,I)=44 OR G(PL,I)=45 OR G(PL,I)=46 OR G(PL,I)=54 OR G(PL,I)=51 THEN G(PL,I)=0:NEXT I:GOTO 300 ELSENEXT I:GOTO 300
2241 M$="USE ASSEMBLE":GOTO 30
2242 IF L<>67 THEN M$="I DON'T SEE A SAFE HERE.":GOTO 30 ELSE IF N(51,1)<>-48 THEN M$="THE SAFE HAS BEEN OPENED BY     SOMEONE ELSE.":GOTO 30
2243 IF O$="" THEN INPUT"TYPE WHAT";O$
2244 IF O$<>CB$ THEN M$="THE SAFE DOOR STAYS SHUT!":GOTO30
2245 M$="THE SAFE DOOR SWINGS OPEN":FOR I=1 TONO:IF N(I,1)=-48 THEN N(I,1)=L:NEXT I:GOTO 30 ELSE NEXT I:GOTO 30
2246 IF PL<>2 THEN M$="ONLY SELVON KNOWS HOW TO NERVE- PINCH PEOPLE.":GOTO 30
2247 IF OB<>49 THEN M$="DON'T BE RIDICULOUS!":GOTO 30
2248 IF L<>N(OB,1) THEN M$="HE IS NOT HERE.":GOTO 30
2249 IF G2=0 THEN M$="HE IS ALREADY STUNNED" ELSE G2=0:N$(49,1)="STUNNED GUARD":M$="HE SAID 'AAAAGH!'":GOTO 30
2250 IF OB=9 THEN M$="TRY 'EXAMINE ORDERS'":GOTO 30
2251 IF OB<>52 THEN M$="DON'T BE SILLY":GOTO 30
2252 A1=0:A2=0:FOR I=1 TO 4:IF G(PL,I)=52 THEN A1=1 ELSE IF G(PL,I)=53 THEN A2=1
2253 NEXT I:IF A1=0 THEN M$="I DON'T HAVE THE TAPE.":GOTO 30
2254 IF A2=0 THEN M$="I DON'T HAVE A TAPE READER.":GOTO 30
2255 M$="THE TAPE SAYS:"+CHR$(13)+"THE SAFE'S COMBINATION IS "+CB$:GOTO 30
2256 IF O$<>"WATER" THEN M$="I CAN'T":GOTO 30
2257 FOR I=1 TO 4:IF G(PL,I)=50 THEN ELSE NEXT I:M$="I DON'T HAVE THE WATER.":GOTO 30
2258 IF N$(50,1)="EMPTY BUCKET" THEN M$="I HAVE ALREADY USED THE WATER SOMEWHERE ELSE.":GOTO 30
2259 N$(50,1)="EMPTY BUCKET":IF L<>66 THEN M$="SPLASH! THAT WAS STUPID!":GOTO 30
2260 IF SH=0 THEN M$="SPLASH! THAT WAS STUPID!":GOTO 30
2261 SH=0:M$="SPLASH! FSSSS! THE SHIELD SHORT-CIRCUITED.":N$(47,1)="SMASHED SHIELD":GOTO 30
9999 END
10000 ' ENDEAVOUR LOCATIONS
10001 DATA AT THE HELM OF THE ENDEAVOUR.   MITCHELL IS HERE MANNING THE    MAIN CONTROLS,0,3,2,0,0,0
10002 DATA IN THE COMMAND SEAT. I CAN SEE  A ROW OF LABELLED SWITCHES IN   FRONT OF ME.,1,7,0,0,0,0
10003 DATA AT THE PHASER CONTROL PANEL.    SOREF IS BUSY CALIBRATING THE   AIMING SYSTEM.,4,5,0,1,0,0
10004 DATA BY THE MAIN VIEWSCREEN.,0,0,3,0,0,0
10005 DATA AT THE SCIENTIFIC OFFICER'S MAINPOST. I CAN SEE LOTS OF CONTROLSBUT THEY ARE MOSTLY BROKEN.,0,0,6,3,0,0
10006 DATA AT BHAN'S POST. BHAN IS SCANING THE SUB-SPACE FREQUENCIES.,5,0,0,7,0,0
10007 DATA IN THE BRIDGE OF THE ENDEAVOUR. BHAN IS BUSY TO THE RIGHT AND   THERE IS A COM-UNIT ON THE      WALL.,0,6,8,2,0,0
10008 DATA IN THE LIFT.,7,0,0,0,0,9
10009 DATA IN THE LIFT.,10,0,0,0,8,16
10010 DATA IN THE SICKBAY. THERE ARE TWO   BEDS TO MY RIGHT AND A COM-UNIT ON A TABLE.,12,11,9,15,0,0
10011 DATA BY A BED MARKED 'I'. THERE IS A SCANNER ABOVE IT.,0,0,0,10,0,0
10012 DATA BY SOME SHELVES.,0,13,10,14,0,0
10013 DATA BY A BED MARKED 'II'. THERE IS ASCANNER ABOVE IT.,0,0,0,12,0,0
10014 DATA BY SOME OPEN CUPBOARDS.,0,12,15,0,0,0
10015 DATA BY A DRAW FULL OF TRICORDERS.   ONLY ONE IS OPERATIVE.,14,10,0,0,0,0
10016 DATA IN THE LIFT.,17,0,0,0,9,31
10017 DATA IN A CORRIDOR ON LEVEL 3. THERE ARE ROOMS ON EACH SIDE.,18,21,16,24,0,0
10018 DATA IN A CORRIDOR. THERE ARE MORE   ROOMS. ,19,22,17,23,0,0
10019 DATA IN A CORRIDOR. THERE IS A SMALL HATCH TO THE RIGHT FOR SERVICE.,0,20,18,25,0,0
10020 DATA IN A SERVICE HATCH. THERE IS A  LADDER GOING UP.,0,0,0,19,27,0
10021 DATA IN A ROOM. THE DOOR HAS A SIGN  READING 'C. JAMES' AND THE ROOM HAS A TABLE ON WHICH A COM-UNIT STANDS.,0,0,0,17,0,0
10022 DATA IN A ROOM. THE FURNISHINGS ARE  VULCAN IN STYLE. A SIGN ON THE  DOOR SAYS 'DR SELVON' AND THERE IS A COM-UNIT ON A TABLE TO THE RIGHT.,0,0,0,18,0,0
10023 DATA IN A ROOM. THERE IS A SIGN ON   THE DOOR SAYING 'DR M. SPEARS'  AND THERE IS A COM-UNIT ON A    TABLE.,0,18,0,0,0,0
10024 DATA IN A ROOM. THERE IS A SIGN ON   THE DOOR SAYING 'H. MC'CRINDLE' AND THE WALLS ARE FURNISHED WITHDRINKS CABINETS. THERE IS A COM-UNIT ON THE WALL.,0,17,0,0,0,0
10025 DATA IN A CORRIDOR OUTSIDE A ROOM.   THE ROOM HAS A SIGN SAYING 'MAINBREIFING ROOM'.,26,19,0,0,0,0
10026 DATA IN THE BRIEFING ROOM. A MONITOR AND A COM-UNIT DOMINATE THE     TABLE.,0,0,25,0,0,0
10027 DATA IN A SERVICE VENT.,,0,0,0,28,20
10028 DATA BETWEEN TWO SERVICE LADDERS.,0,29,0,0,0,27
10029 DATA ON A SERVICE LADDER.,0,0,0,28,0,30
10030 DATA ON A SERVICE LADDER.,0,0,0,0,29,70
10031 DATA IN THE LIFT.,33,0,0,0,16,42
10032 DATA BY A SERVICE LADDER.,0,0,0,40,71,0
10033 DATA IN ENGINEERING SECTION. THERE ISA MAP ON THE WALL SHOWING THE   LEVELS  -                       LEVEL 1 = BRIDGE                LEVEL 2 = SICKBAY               LEVEL 3 = COMMANDERS' QUARTERS  LEVEL 4 = ENGINEERING,37,0,31,0,0,0
10034 DATA BY THE MAIN ENGINE CONTROL WALL PANEL. THERE IS A COM-UNIT ON   THE WALL.,0,0,35,0,0,0
10035 DATA ON SOME STAIRS.,34,0,36,0,0,37
10036 DATA BY THE ENGINES. IT IS HOT HERE.,35,0,0,0,0,0
10037 DATA BY SOME STAIRS.,41,38,33,0,35,0
10038 DATA BEHIND A WALL TO THE NORTH OF   SOME CONTROL PANELS.,0,0,39,37,0,0
10039 DATA BY SOME CONTROL PANELS.,38,0,0,0,0,0
10040 DATA BY A SERVICE HATCH.,0,32,0,41,0,0
10041 DATA BY A MAP OF THE ENDEAVOUR'S     LOWER LEVELS -                  LEVEL 5 = SHUTTLECRAFT BAYS     LEVEL 6 = TRANSPORTER BAY,0,40,37,0,0,0
10042 DATA IN THE LIFT.,43,0,0,0,31,51
10043 DATA BY A SHUTTLECRAFT BAY MARKED    'I'. THERE IS A COM-UNIT ON THE WALL AND ALSO AN INTERCOM TO    TALK TO THE SHUTTLECRAFT CREW.,45,44,42,0,0,0
10044 DATA IN SHUTTLECRAFT BAY I. THERE ARESOME STEPS LEADING UP.,0,0,0,43,47,0
10045 DATA BY A SHUTTLECRAFT BAY MARKED    'II'. THERE IS AN INTERCOM ON   THE WALL.,0,0,43,46,0,0
10046 DATA IN SHUTTLECRAFT BAY II. THERE ARESOM STEPS LEADING UP.,0,45,0,0,49,0
10047 DATA IN THE AIRLOCK OF SHUTTLECRAFT  I.,48,0,0,0,0,44
10048 DATA IN THE BRIDGE OF SHUTTLECRAFT   I.,0,0,47,0,0,0
10049 DATA IN THE AIRLOCK OF SHUTTLECRAFT  II.,50,0,0,0,0,46
10050 DATA IN THE BRIDGE OF SHUTTLECRAFT   II.,0,0,49,0,0,0
10051 DATA IN THE LIFT.,52,0,0,0,42,0
10052 DATA IN THE TRANSPORTER ROOM. MR RECKIS MANNING THE CONTROLS AND     THERE IS A COM-UNIT ON THE WALL.THE PLATES ARE TO THE EAST.,0,53,51,54,0,0
10053 DATA ON THE TRANSPORTER PLATES.,0,0,0,52,0,0
10054 DATA IN THE STORE ROOM.,0,52,0,0,0,0
10055 DATA ON THE LANDING AREA.,56,0,0,0,0,0
10056 DATA NEAR A POOL.,0,57,55,0,0,0
10057 DATA BY A DEEP POOL.,0,58,59,56,0,0
10058 DATA NEAR A POOL.,0,61,0,57,0,0
10059 DATA BY THE MAIN DOOR OF A LARGE     BUILDING.,57,0,0,0,0,0
10060 DATA AT THE BOTTOM OF A POOL.,0,0,0,0,57,0
10061 DATA BY A PILE OF ROCKS.,0,0,0,58,0,0
10062 DATA JUST INSIDE THE MAIN DOOR OF THEBUILDING.,0,63,66,59,0,0
10063 DATA OUTSIDE A STORE ROOM.,0,64,65,62,0,0
10064 DATA IN A STORE ROOM.,0,0,0,63,0,0
10065 DATA BY A TABLE.,63,0,0,0,0,0
10066 DATA IN A LONG PASSAGE.,62,0,67,0,0,0
10067 DATA IN A WELL FURNISHED ROOM.,66,0,68,0,0,0
10068 DATA NEAR A LABORATORY.,67,0,69,0,0,0
10069 DATA IN A LABORATORY. THERE ARE PILESOF BROKEN GLASSWARE EVERYWHERE.,68,0,0,0,0,0
10070 DATA NEAR THE LOWER LEVELS.,0,0,0,0,30,71
10071 DATA JUST OFF THE FLOOR,0,0,0,0,70,32
11000 ' OBJECTS
11001 DATA PHAS,PHASER,2,0
11002 DATA TRIC,TRICORDER,0,15
11003 DATA MEDI,MEDICAL KIT,5,12
11004 DATA TRUT,TRUTH SERUM,0,-3
11005 DATA SNAK,SNAKE-BITE SERUM,0,-3
11006 DATA TRAN,TRANQUILLISER SERUM,0,-3
11007 DATA LIFE,LIFE-READER,0,-3
11008 DATA SCAL,SCALPEL,0,14
11009 DATA ORDE,ORDERS AND BRIEF,3,26
11010 DATA TORC,TORCH,0,23
11011 DATA WHIS,BOTTLE OF WHISKY,6,24
11012 DATA TOOL,TOOL KIT,0,34
11013 DATA SPAN,SPANNER,0,-12
11014 DATA SCRE,SCREWDRIVER,0,-12
11015 DATA PLIE,PLIERS,0,-12
11016 DATA CUTT,WIRE CUTTERS,0,-12
11017 DATA COM,COM-UNIT,1,0
11018 DATA SWIT,SWITCH,1,-17
11019 DATA BUTT,RED BUTTON,1,-20
11020 DATA INTE,INTERCOM,1,0
11021 DATA SIGN,SIGN,1,0
11022 DATA CAPT,CAPTAIN'S COM-UNIT,1,2
11023 DATA BHAN,BHAN,1,6
11024 DATA SORE,SOREF,1,3
11025 DATA MITC,MITCHELL,1,1
11026 DATA RECK,MR RECK,1,52
11027 DATA JAME,JAMES,1,2
11028 DATA SELV,SELVON,1,5
11029 DATA SPEA,SPEARS,1,10
11030 DATA MC'C,MC'CRINDLE,1,33
11031 DATA RADI,RADIATION SUIT,0,54
11032 DATA VACU,VACUUM SUIT,0,54
11033 DATA DIVI,DIVING SUIT,0,54
11034 DATA TOUC,TOUCH BUTTON,1,-22
11035 DATA GUAR,SECURITY GUARD,1,0
11036 DATA BAD,BAD RYETALYN CRYSTALS,0,60
11037 DATA POOL,LARGE POOL,1,57
11038 DATA ANTI,ANTITOXIN,0,0
11039 DATA HYPO,EMPTY HYPO,0,-3
11040 DATA ROCK,SMALL ROCK,0,61
11041 DATA KEY,GOLDEN KEY,0,0
11042 DATA DROI,GUARD DROID,1,63
11043 DATA EQUI,BAG OF EQUIPMENT,0,64
11044 DATA BUNS,BUNSEN BURNER,0,-43
11045 DATA JAR,GLASS JAR,0,-43
11046 DATA TUBE,LONG COLD TUBE,0,-43
11047 DATA SHIE,FORCE SHIELD,1,66
11048 DATA SAFE,WALL SAFE,1,67
11049 DATA HOLB,HOLBERG,1,68
11050 DATA BUCK,BUCKET OF WATER,0,60
11051 DATA BOTT,GAS BOTTLE,0,-48
11052 DATA TAPE,TICKER TAPE,0,65
11053 DATA READ,TAPE READER,0,5
11054 DATA PERF,PERFECT RYETALYN,0,69
11055 DATA DOOR,LOCKED DOOR,1,59
12000 ' VERBS
12001 DATA NORTH,EAST,SOUTH,WEST,UP,DOWN,GET,TAKE,MENU,QUIT,DROP,PUSH,LOOK,INVENTORY,EXAMINE,BEAM,DIVE,FILL,UNLOCK,SHOOT,ASSEMBLE,DISTILL,TYPE,NERVEPINCH,READ,THROW
20000 NV=26:NO=55:NL=71
20001 DIM V$(NV),N$(NO,1),N(NO,1),G(4,4):G(1,1)=1:G(2,1)=1:G(3,1)=1:G(4,1)=1
20002 FOR I=1 TO NL:READ N$,A,B,C,D,E,F:NEXT I
20003 FOR I=1 TO NO:READ N$(I,0),N$(I,1),N(I,0),N(I,1):NEXT I
20004 FOR I=1 TO NV:READ V$(I):NEXT I
20005 KN=0:AA=0:FI=0
20006 EM$="WE'VE ONLY GOT 5 PEOPLE WORKING DOWN HERE."
20007 SI=1:SR$="WE'VE GOT A BAD OUTBREAK OF     RIGELLIAN FEVER. WE NEED SOME   RYETALYN TO PROCESS INTO SOME   ANTITOXIN BUT IT MUST BE PURE   RYETALYN."
20008 DA=0
20009 PL$="HOLBERG"
20010 UN=00:SH=1:GU=1:G2=1
20011 SS=0
20012 CB$="EDDIE":FOR I=1 TO 5:MID$(CB$,I,1)=CHR$(ASC(MID$(CB$,I,1))-16):NEXT I
20099 RETURN
