0 CLS:PRINT@227,"SPACE TREK I BY M.EDWARDS":GOSUB20000:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(J$,CC,1):IFI$=" "ORI$=""THEN?MID$(J$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:J$="":RETURN
9 PRINT:PRINT" ******************************":PRINT:RETURN
10 GOSUB1000:L=N(PL+26,1):GOSUB1030
11 PRINT" WHAT DO YOU WANT ME TO DO?":INPUT" ";D$
12 V$="":O$="":FORI=1TOLEN(D$):IFMID$(D$,I,1)=" "THENV$=LEFT$(D$,I-1):O$=RIGHT$(D$,LEN(D$)-I):I=LEN(D$)+4
13 NEXTI:IFV$=""THENV$=D$
14 M$="I CAN'T DO THAT":VB=0:OB=0
15 LL=LEN(V$):FORI=1TONV
16 L1=LL:IF LEN(V$(I))<LLTHENL1=LEN(V$(I))
17 IFLEFT$(V$(I),L1)=LEFT$(V$,L1)THENVB=I:I=NV+1
18 NEXTI
19 IFVB=0THENM$="I DON'T UNDERSTAND THAT VERB"
20 IFO$=""THEN25
21 LL=LEN(O$)
22 FORI=1TONO:L1=LL:IFLEN(N$(I,0))<LLTHENL1=LEN(N$(I,0))
23 IFLEFT$(N$(I,0),L1)=LEFT$(O$,L1)THENOB=I:I=NO+1
24 NEXTI
25 IFOB=0ANDO$<>""THENM$="I DON'T KNOW WHAT ONE OF THOSE IS.":GOTO27
26 IFV$=""THENVB=0 
27 ON-(VB>13)GOTO28:ONVBGOTO2000,2003,2004,2005,2006,2007,2008,2008,2022,2023,2018,2040,2071
28 ON-(VB>27)GOTO30:ONVB-13GOTO2072,2074,2200,2209,2211,2218,2221,2225,2241,2242,2246,2250,2256
30 IFOB=17ANDVB=15THENM$="COM-UNITS HAVE ON/OFF PUSH SWITCHES ON THEM."
31 IFOB=20ANDVB=15AND(L=46ORL=43)THENM$="IT HAS A RED BUTTON ON IT."
32 IFLEFT$(OB$,3)="CONTROLS"ANDVB=15ANDL=5THENM$="THEY ARE MAINLY BROKEN."
33 IFL=60THENFORI=1TO4:IFG(PL,I)<>33THENNEXTI:M$="I AM DROWNED":GOTO100
34 IFVB=15AND(LEFT$(O$,4)="VIEW"ORLEFT$(O$,4)="SCRE")ANDL=4THEN40
35 GOTO99
40 IFAR=0THENM$="IT SHOWS DEEP SPACE":GOTO99
41 IFAR=1ANDPL$="HOLBERG"THENM$="IT SHOWS THE PLANET HOLBERG."
99 J$=M$:GOSUB1:PRINT:GOTO11
100 CLS:J$=M$:GOSUB1:PRINT:PRINTN$(PL+26,1);" IS DEAD!":PRINT"GAME OVER!"
102 PRINT:PRINT"PLAY AGAIN(Y/N)?"
103 I$=INKEY$:IFI$="N"THENEND
104 ON-(I$<>"Y")GOTO103:RUN
200 ON-(AR=1ANDSA=1ANDPL$="STARFLEET HQ")GOTO201:GOSUB1030:J$=M$:GOSUB1:GOTO11
201 CLS:PRINT"'CAPTAIN KIRK, THERE IS AN URGENT MESSAGE FOR YOU FROM STARFLEET HQ,' SAYS UHURA."
202 J$="'CAPTAIN KIRK-- WE HAVE BEEN MONITORING COORDINATES 10:20:8 AND HAVE DEDECTED AN UNKNOWN FORCE.":GOSUB1
203 GOSUB1:J$="IT'S NOT A BLACK HOLE BUT EVERYTHING THAT GOES IN DOES NOT COME OUT!":GOSUB1
204 J$="THE REAL ADVENTURE IS JUST BEGINNING...":GOSUB1:END
300 FORI=43TO46:N(I,1)=0:NEXTI:N(51,1)=0:N(54,1)=0:GOTO30
1000 CLS:GOSUB9:PRINTTAB(11)"1. KIRK":PRINT:PRINTTAB(11)"2. SPOCK":PRINT
1001 PRINTTAB(11)"3. MCCOY":PRINT:PRINTTAB(11)"4. SCOTTY":GOSUB9:PRINTTAB(9)"WHO TO REPORT?"
1002 I$=INKEY$:ON-(I$<"1"ORI$>"4")GOTO1002:PL=VAL(I$):RETURN
1010 CLS:GOSUB9
1011 PRINT"1. SET COURSE":PRINT"2. SELECT WARP OR STOP":PRINT"3. ENGINEERING REPORT"
1012 PRINT"4. SICK BAY REPORT":PRINT"5. DAMAGE REPORT":PRINT"6. AIM PHASERS"
1013 PRINT"7. FIRE PHASERS":GOSUB9:PRINTTAB(9)"WHICH COMMAND?"
1014 I$=INKEY$:ON-(I$<"1"ORI$>"7")GOTO1014:CM=VAL(I$):RETURN
1020 CLS:GOSUB9:PRINT"1. CALL SECURITY":PRINT:AS=1:BS=2:CS=3:ON-(PL<1ORPL>3)GOTO1023
1022 AS=2:BS=3:CS=4:ON-(PL=1)GOTO1023:AS=1:BS=3:CS=4:ON-(PL=2)GOTO1023:AS=1:BS=2:CS=4
1023 PRINT"2. CALL "N$(AS+26,1):PRINT:PRINT"3. CALL "N$(BS+26,1):PRINT:PRINT"4. CALL "N$(CS+26,1)
1024 PRINT:PRINT"5. SWITCH OFF":GOSUB9:PRINTTAB(13);"WHICH?";
1025 I$=INKEY$:ON-(I$<"1"ORI$>"5")GOTO1024:CM=VAL(I$):RETURN
1030 CLS:PRINTN$(PL+26,1)" REPORTING:":PRINT:N(PL+26,1)=L
1031 ON-(L=60)GOTO1035:GOTO1040
1035 FORI=1TO4:IFG(PL,I)=33THENC1=I:I=4:NEXT:I=C1
1036 NEXTI:M$="I AM DROWNED!":GOTO100
1040 PRINT"I AM:":RESTORE:FORI=1TOL:READN$,A,B,C,D,E,F:NEXTI:J$=N$:GOSUB1
1041 IFL=5THENJ$="THE INSTRUMENTS SHOW RYETALYN ON THE PLANET HOLBERG":GOSUB1
1042 IFL=59ANDUN=1THENB=62
1043 PRINT:PRINT"EXITS: ";:PRINTLEFT$("N",A)" "LEFT$("E",B)" "LEFT$("S",C)" "LEFT$("W",D)" ";
1044 PRINTLEFT$("U",E)" "LEFT$("D",F)" "
1047 IF(A+B+C+D+E+F)=0THENPRINT"NONE"
1049 PRINT"I CAN SEE:":NS=0:FORI=1TONO:IFN(I,1)=LTHENNS=NS+1
1050 NEXTI:IFNS=1THENPRINT"NOTHING":GOTO1060
1051 NS=NS-1:FORI=1TONO:IFN(I,1)=LANDI<>PL+26THENNS=NS-1:J$=J$+N$(I,1)+","
1052 NEXTI:J$=LEFT$(J$,LEN(J$)-1):GOSUB1
1060 PRINT:RETURN
1100 IFZ=0THENM$="I CAN'T GO THAT WAY":GOTO30
1102 IFL=66ANDSH=1ANDZ=CTHENM$="THE SHIELD STOPS ME":GOTO30
1103 IFL=63ANDGU=1THENM$="THE GUARD DROID STOPS ME":GOTO30
1104 IFL=68ANDZ=CANDG2=1THENM$="THE HOLBERG STOPS ME":GOTO30
1102 L=Z:GOSUB1030:GOTO11
2000 Z=A:GOTO1100
2003 Z=B:GOTO1100
2004 Z=C:GOTO1100
2005 Z=D:GOTO1100
2006 Z=E:GOTO1100
2007 Z=F:GOTO1100
2008 IF OB=0 THEN 30 ELSE IF N(OB,0)=1 THEN M$="I CAN'T GET THAT":GOTO 30 ELSE IF (N(OB,0)>2 AND N(OB,0)<7) AND PL<>N(OB,0)-2 THEN M$="I'M AFRAID ONLY "+N$(24+N(OB,0),1)+" CAN GET IT.":GOTO 30
2009 IF N(OB,1)>=0 AND N(OB,1)<>L THEN M$="I DON'T SEE IT HERE":GOTO 30 ELSE IF OB=38 THEN M$="IT RUNS THROUGH MY FINGERS":GOTO 30
2010 Z=N(OB,1):IF Z>-1 THEN 2015
2011 IF G(PL,1)<>-Z AND G(PL,2)<>-Z AND G(PL,3)<>-Z AND G(PL,4)<>-Z THEN M$="IT IS IN THE "+N$(-Z,1):GOTO 30
2015 FOR I=1 TO 4:IF G(PL,I)=0 THEN G(PL,I)=OB:N(OB,1)=0:M$="I HAVE THE "+N$(OB,1) ELSE NEXT I:M$="I AM CARRYING TOO MUCH."
2016 IF N(OB,1)=0 AND OB=40 AND N(41,1)=0 THEN ELSE 30
2017 FOR I=1 TO 4:FOR J=1 TO 4:IF G(I,J)=41 THEN 30 ELSE NEXT J,I:N(41,1)=L:M$="WOW! IT'S UNCOVERED SOMETHING!":GOTO 30
2018 IF OB=0 THEN M$="I DON'T KNOW WHAT ONE OF THOSE  IS.":GOTO30 ELSENN=0:FOR I=1 TO 4:IF G(PL,I)=OB THEN NN=I:I=5
2019 NEXT I:IF NN=0 THEN M$="I DON'T HAVE IT":GOTO 30
2020 IF N(OB,0)=2 THEN M$="I CAN'T DROP IT!!":IF OB=1 THEN M$=M$+CHR$(13)+"STARFLEET RULES: LANDING PARTY  MUST HAVE PHASERS.":GOTO 30 ELSE 30
2021 G(PL,NN)=0:N(OB,1)=L:M$="OK":GOTO 30
2022 N(PL+26,1)=L:GOSUB 1000:L=N(PL+26,1):GOSUB 1030:GOTO 11
2023 CLS:PRINT"REALLY QUIT (Y/N)?"
2024 I$=INKEY$:IFI$="N"THENGOSUB1030:GOTO11
2025 GOTO102
2040 IFOB<>18THEN2050
2041 IFL<>7ANDL<>10ANDL<>34ANDL<>43ANDL<>23ANDL<>24ANDL<>21ANDL<>22ANDL<>26ANDL<>52THENM$="THERE'S NO COM-UNIT":GOTO30
2042 GOSUB1020:IFCM=1THENN(35,1)=L
2044 IFCM=2THENON1-(N(26+AS,1)<55)GOTO2048:N(26+AS,1)=L 
2045 IFCM=3THENON1-(N(26+BS,1)<55)GOTO2048:N(26+BS,1)=L
2046 IFCM=4THENON1-(N(26+CS,1)<55)GOTO2048:N(26+CS,1)=L
2047 GOSUB1030:GOTO11
2048 M$="HE'S NOT ON THE SHIP":GOSUB1030:GOTO30
2050 IF OB<>19 THEN 2060
2051 IF L<>43 AND L<>45 THEN M$="I DON'T SEE AN INTERCOM HERE.":GOTO 30
2052 M$="":NC=0:FOR I=1 TO 4:IF N(I+26,1)=L+5 THEN NC=NC+1
2053 NEXT I:IF NC=0 THEN M$="THERE IS NO-ONE IN SHUTTLECRAFT "+STRING$((L-41)/2,"I"):GOTO 30 ELSE NN=NC
2054 FOR I=1 TO 4:IF N(I+26,1)=L+5 THEN M$=M$+N$(I+26,1):NC=NC-1:IF NC=1 THEN M$=M$+" AND " ELSE IF NC>1 THEN M$=M$+", "
2055 NEXT I:M$=M$+" SAY":IF NN=1 THEN M$=M$+"S HELLO.":GOTO 30 ELSE M$=M$+" HELLO.":GOTO 30
2060 IF OB<>34 THEN M$="DON'T BE RIDICULOUS":GOTO 30
2061 IF L<>2 THEN M$="I DON'T SEE THE CAPTAIN'S UNIT  HERE.":GOTO 30 ELSE IF PL<>1 THEN M$="I DAREN'T - ONLY CAPTAIN KIRK  CAN DO THAT!!":GOTO 30
2062 GOSUB 1010
2063 IF CM=1 THEN IF AR=0 THEN M$="OK. COURSE IS SET FOR "+PL$:AR=-1 ELSE IF AR=-1 THEN M$="COURSE SET ALREADY." ELSE IF AR=1 THEN M$="WE'RE THERE ALREADY!"
2064 IF CM=2 THEN IF AR=-1 THEN M$="OK. TRAVELLING AT WARP SPEED TO "+PL$+"........"+CHR$(13)+"WE HAVE ARRIVED SIR.":AR=1:GOTO 200 ELSE IF AR=0 THENM$="NO COURSE SET YET, SIR." ELSE M$="WER'E THERE NOW!"
2065 IF CM=3 THEN IF AR=1 THEN M$=EM$ ELSE M$="WE'VE ONLY GOT 10 PEOPLE DOWN   HERE WORKING."
2066 IF CM=4 THEN IF SI=1 THEN M$=SR$ ELSE M$="ONLY ONE PATIENT WITH FLU DOWN  HERE, SIR."
2067 IF CM=5 THEN IF DA=1 THEN M$=DR$ ELSE M$="THERE'S NO DAMAGE AT ALL, SIR,  EXCEPT A SMALL DAMAGED PATCH ON THE OUTER HULL."
2068 IF CM=6 THEN IF KN=1 THEN AA=1:M$="OK. PHASERS AIMED." ELSE M$="NOTHING TO AIM PHASERS AT."
2069 IF CM=7 THEN IF KN=0 OR AA=0 THEN M$="THE PHASERS AREN'T AIMED" ELSE M$="FIRED AND HIT!!":FI=1
2070 GOSUB1030:PRINTM$:GOTO11
2071 GOSUB1030:GOTO11
2072 PRINT:PRINT"I AM CARRYING:":FORI=1TO4:IFG(PL,I)>0THENPRINTN$(G(PL,I),1)
2073 NEXTI:GOTO11
2074 M$="NOTHING SPECIAL":FOR I=1 TO 4:IF G(PL,I)=OB THEN ELSE NEXT I:IF N(OB,1)<>L THEN M$="I DON'T SEE IT.":GOTO 30
2075 IF OB=1 THEN M$="ITS SET TO KILL."
2076 IF OB=2 THEN IF L>54 AND L<62 THEN M$="IT SHOWS IMPURE RYETALYN." ELSE IF L>61 AND L<70 THEN M$="IT SHOWS PURE RYETALYN." ELSE M$="IT SHOWS NOTHING NOW."
2077 IF OB=3 THEN PRINT"IT CONTAINS:":M$="":FOR I=1 TO NO:IF N(I,1)=-3 THEN PRINTN$(I,1):NEXT I ELSE NEXT I
2078 IF OB=4 THEN M$="DARK GREEN AND THICK IN A HYPO."
2079 IF OB=5 THEN M$="CLEAR AND WATERY IN A SMALL     HYPO."
2080 IF OB=6 THEN IF PL=4 THEN M$="STRONGER THAN A PINT OF SCOTCH" ELSE M$="YELLOWISH IN COLOUR IN A HYPO."
2082 IF OB=8 THEN M$="VERY SHARP AND SHINY."
2084 IF OB=11 THEN IF PL=4 THEN M$="ANCIENT AND DELICIOUS!"
2085 IF OB=12 THEN M$="":PRINT"IT CONTAINS:":FOR I=1 TO NO:IF N(I,1)=-12 THEN PRINTN$(I,1):NEXT I ELSE NEXT I
2086 IF OB=13 THEN M$="ADJUSTABLE WITH A SMALL SCREW."
2087 IF OB=23 THEN M$="SHE IS VERY BUSY."
2088 IF OB=24 THEN M$="HE IS CHECKING THE SHIP'S       PHASER BANKS."
2089 IF OB=22 THEN M$="IT HAS A TOUCH SWITCH."
2090 IF OB=25 THEN M$="HE IS MANNING THE HELM."
2091 IF OB=26 THEN IF AR=1 THEN M$="HE IS WAITING TO BEAM SOMEONE   DOWN." ELSE M$="HE IS DOING NOTHING AS WE ARE INDEEP SPACE."
2092 IF (OB>=27 AND OB<=30) AND PL<>OB-26 THEN M$="HE IS NOT DOING ANYTHING SPECIALAND HE JUST TOLD ME TO STOP     STARING."
2093 IF (OB>=27 AND OB<=30) AND PL=OB-26 THEN M$="THAT'S ME!"
2094 IF OB=31 OR OB=32 OR OB=33 THEN M$="VERY WELL MADE TO SUIT ITS      PURPOSE."
2095 IF OB=36 THEN M$="THEY HAVE IRILLIUM IMPURITIES   IN THEM."
2096 IF OB=37 THEN M$="IT IS WET."
2097 IF OB=38 THEN M$="PURE AND EXECELLENT."
2098 IF OB=9 THEN IF SA=1 THEN M$="THEY SAY 'REPORT TO STARFLEET HQFOR YOUR NEXT ASSIGNMENT'":PL$="STARFLEET HQ":AR=0 ELSE M$="THEY SAY:"+CHR$(13)+"GOOD LUCK, CHRIS."
2099 IF OB=39 THEN IF N$(39,1)="ANTITOXIN HYPO" THEN M$="IT IS READY TO SAVE THE CREW."
2100 IF OB<>55 THEN 2105
2101 FOR I=1 TO 4:IF G(PL,I)=41 THEN M$="YOU HAVE THE KEY.":GOTO 2105 ELSE NEXT I
2102 FOR I=1 TO4:FOR J=1 TO 4:IF G(I,J)=41 THEN M$=N$(I+26,1)+" HAS THE KEY.":GOTO 2105 ELSE NEXT J,I
2103 M$="NOONE HAS THE KEY."
2105 IF OB=42 THEN M$="IT IS FLOATING ABOVE THE GROUND ON A CUSHION OF AIR."
2106 IF OB=43 THEN M$="IT CONTAINS DISTILLING EQUIPMENT":FOR I=1 TO NO:IF N(I,1)=-43 THEN M$=M$+N$(I,1)+CHR$(13)
2107 IF OB=43 THEN NEXT I
2108 IF OB=47 THEN M$="IT WON'T LET ME PAST IT. IT IS  INVISIBLE AND ELECTRIFIED."
2109 IF OB=48 THEN M$="IT HAS A KEYPAD TO TYPE THE     COMBINATION ON."
2110 IF OB=51 THEN M$="IT HAS A NOZZLE TO FIT A BUNSEN BURNER."
2111 IF OB=52 THEN M$="IT IS ENCODED - YOU NEDD A TAPE READER TO READ IT."
2112 IF OB=54 THEN M$="IT IS PERFECT FOR MAKING THE    ANTITOXIN."
2199 GOTO 30 ' END OF EXAMINES
2200 IF O$<>"UP" AND O$<>"DOWN" AND O$<>"" THEN M$="I CAN'T DO THAT!?":GOTO 30
2201 IF O$="UP" AND L<55 THEN M$="I HAVEN'T BEAMED down YET!":GOTO 30
2202 IF O$="DOWN" AND L>54 THEN M$="I'M ALREADY BEAMED DOWN.":GOTO 30
2203 IF L>54 THEN O$="UP" ELSE O$="DOWN"
2204 IF AR=0 THEN M$="I CAN'T - WER'E IN DEEP SPACE!":GOTO 30
2205 IF O$="UP" THEN L=53
2206 IF O$="DOWN" AND L<>53 THEN M$="I CAN'T FROM HERE.":GOTO 30
2207 IF O$="DOWN" THEN L=55
2208 SOUND1,1:GOSUB1030:GOTO11
2209 IF L<>57 THEN M$="THERE'S NO WATER TO DIVE INTO.":GOTO 30
2210 L=60:GOSUB 1030:GOTO 11
2211 IF OB=50 THEN M$="IT IS FILLED ALREADY!":GOTO 30
2212 IF OB<>39 THEN IF OB=45 THEN M$="I CAN'T FILL THAT":GOTO 30 ELSE M$="DON'T BE RIDICULOUS!":GOTO 30
2213 IF N(38,1)<>L THEN M$="THE ANTITOXIN ISN'T HERE.":GOTO 30
2214 IF G(PL,1)<>39 AND G(PL,2)<>39 AND G(PL,3)<>39 AND G(PL,4)<>39 THEN M$="I DON'T HAVE A SPARE EMPTY    HYPO.":GOTO 30
2215 N(38,1)=0
2216 N$(39,1)="ANTITOXIN HYPO":SA=1:IF PL=1 THEN N$="ME" ELSE N$="KIRK"
2217 M$="UHURA SUDDENLY CALLS "+N$+"."+CHR$(13)+"'SOME ORDERS HAVE ARRIVED FROM  STARFLEET COMMAND. PLEASE FIND  THEM AS THEY ARE LOST ON THE    SHIP.'":GOTO 30
2218 IF L<>59 OR UN=1 THEN M$="THERE ISN'T A LOCKED DOOR HERE.":GOTO 30
2219 IF G(PL,1)<>41 AND G(PL,2)<>41 AND G(PL,3)<>41 AND G(PL,4)<>41 THEN M$="I DON'T HAVE THE KEY!":GOTO 30
2220 UN=1:N$(55,1)="OPEN DOOR":GOSUB 1030:GOTO 11
2221 IF OB=42 AND GU=1 THEN M$="FSSSS! THE GUARD DROID JUST BLEWUP!":GU=0:N$(42,1)="SMASHED DROID":GOTO 30
2222 IF OB=49 AND G2=1 THEN M$="THE GUARD WAS TOO QUICK AND HE SHOT ME!":GOTO100
2223 IF OB=47 AND SH=1 THEN M$="THE SHIELD ABSORBED MY SHOT":GOTO 30
2224 M$="DON'T BE RIDICULOUS!":GOTO 30
2225 A1=0:A2=0:A3=0:A4=0:A5=0:IF N(38,1)<>0 OR SA=1 THEN M$="I CAN'T DO THAT!":GOTO 30
2226 IF G(PL,1)<>43 AND G(PL,2)<>43 AND G(PL,3)<>43 AND G(PL,4)<>43 THEN 2230
2227 IF N(44,1)=-43 THEN A1=1
2228 IF N(45,1)=-43 THEN A2=1
2229 IF N(46,1)=-43 THEN A3=1
2230 FOR I=1 TO 4:IF G(PL,I)=44 THEN A1=1 ELSE IF G(PL,I)=45 THEN A2=1 ELSE IF G(PL,I)=46 THEN A3=1 ELSE IF G(PL,I)=51 THEN A4=1 ELSE IF G(PL,I)=54 THEN A5=1 ELSE IF G(PL,I)=36 THEN A5=-1
2231 NEXT I
2232 IF A1=1 AND A2=1 AND A3=1 AND A4=1 AND A5=1 THEN 2240
2233 M$="YOU DON'T HAVE:"+CHR$(13):IF A1=0 THEN M$=M$+"A BUNSEN"+CHR$(13)
2234 IF A2=0 THEN M$=M$+"A GLASS JAR"+CHR$(13)
2235 IF A3=0 THEN M$=M$+"A TUBE"+CHR$(13)
2236 IF A4=0 THEN M$=M$+"A GAS BOTTLE"+CHR$(13)
2237 IF A5=0 THEN M$=M$+"ANY RYETALYN" ELSE IF A5=-1 THEN M$=M$+"ANY PURE RYETALYN"
2238 GOTO 30
2240 M$="THE ANTITOXIN DRIPS OUT FROM THETUBE.":N(38,1)=L:N(9,1)=26:FOR I=1 TO 4:IF G(PL,I)=43 OR G(PL,I)=44 OR G(PL,I)=45 OR G(PL,I)=46 OR G(PL,I)=54 OR G(PL,I)=51 THEN G(PL,I)=0:NEXT I:GOTO300 ELSENEXT I:GOTO 300
2241 M$="USE ASSEMBLE":GOTO 30
2242 IF L<>67 THEN M$="I DON'T SEE A SAFE HERE.":GOTO 30 ELSE IF N(51,1)<>-48 THEN M$="THE SAFE HAS BEEN OPENED BY     SOMEONE ELSE.":GOTO 30
2243 IF O$="" THEN INPUT"TYPE WHAT";O$
2244 IF O$<>CB$ THEN M$="THE SAFE DOOR STAYS SHUT!":GOTO30
2245 M$="THE SAFE DOOR SWINGS OPEN":FOR I=1 TONO:IF N(I,1)=-48 THEN N(I,1)=L:NEXT I:GOTO 30 ELSE NEXT I:GOTO 30
2246 IF PL<>2 THEN M$="ONLY SPOCK KNOWS HOW TO NERVE- PINCH PEOPLE.":GOTO 30
2247 IF OB<>49 THEN M$="DON'T BE RIDICULOUS!":GOTO 30
2248 IF L<>N(OB,1) THEN M$="HE IS NOT HERE.":GOTO 30
2249 IF G2=0 THEN M$="HE IS ALREADY STUNNED" ELSE G2=0:N$(49,1)="STUNNED GUARD":M$="HE SAID 'AAAAGH!'":GOTO 30
2250 IF OB=9 THEN M$="TRY 'EXAMINE ORDERS'":GOTO 30
2251 IF OB<>52 THEN M$="DON'T BE SILLY":GOTO 30
2252 A1=0:A2=0:FOR I=1 TO 4:IF G(PL,I)=52 THEN A1=1 ELSE IF G(PL,I)=53 THEN A2=1
2253 NEXT I:IF A1=0 THEN M$="I DON'T HAVE THE TAPE.":GOTO 30
2254 IF A2=0 THEN M$="I DON'T HAVE A TAPE READER.":GOTO 30
2255 M$="THE TAPE SAYS:"+CHR$(13)+"THE SAFE'S COMBINATION IS "+CB$:GOTO 30
2256 IF O$<>"WATER" THEN M$="I CAN'T":GOTO 30
2257 FOR I=1 TO 4:IF G(PL,I)=50 THEN ELSE NEXT I:M$="I DON'T HAVE THE WATER.":GOTO 30
2258 IF N$(50,1)="EMPTY BUCKET" THEN M$="I HAVE ALREADY USED THE WATER SOMEWHERE ELSE.":GOTO 30
2259 N$(50,1)="EMPTY BUCKET":IF L<>66 THEN M$="SPLASH! THAT WAS STUPID!":GOTO 30
2260 IF SH=0 THEN M$="SPLASH! THAT WAS STUPID!":GOTO 30
2261 SH=0:M$="SPLASH! FSSSS! THE SHIELD SHORT-CIRCUITED.":N$(47,1)="SMASHED SHIELD":GOTO 30
10001 DATAAT THE HELM OF THE ENTERPRISE. SULU IS AT THE MAIN CONTROLS,0,3,2,0,0,0
10002 DATAIN THE COMMAND SEAT. I CAN SEE A ROW OF LABELLED SWITCHES IN FRONT OF ME,1,7,0,0,0,0
10003 DATAAT PHASER CONTROL. CHEKOV IS BUSY CALIBRATING THE AIMING SYSTEM,4,5,0,1,0,0
10004 DATABY THE MAIN VIEWSCREEN,0,0,3,0,0,0
10005 DATAAT THE SCIENCE OFFICER'S POST. I CAN SEE MANY CONTROLS BUT MOST ARE BROKEN,0,0,6,3,0,0
10006 DATAAT UHURA'S POST. UHURA IS SCANNING SUB-SPACE FREQUENCIES,5,0,0,7,0,0
10007 DATAIN THE BRIDGE OF THE ENTERPRISE. UHURA IS HERE. THERE IS A COM-UNIT ON THE WALL,0,6,8,2,0,0
10008 DATAIN THE LIFT,7,0,0,0,0,9
10009 DATAIN THE LIFT,10,0,0,0,8,16
10010 DATAIN SICKBAY. THERE ARE TWO BEDS AND A COM-UNIT,12,11,9,15,0,0
10011 DATABY A BED MARKED 'I'. THERE IS A SCANNER ABOVE IT,0,0,0,10,0,0
10012 DATABY SOME SHELVES,0,13,10,14,0,0
10013 DATABY A BED MARKED 'II'. THERE IS A SCANNER ABOVE IT,0,0,0,12,0,0
10014 DATABY SOME OPEN CUPBOARDS,0,12,15,0,0,0
10015 DATABY A DRAWER FULL OF TRICORDERS. ONLY ONE IS OPERATIVE,14,10,0,0,0,0
10016 DATAIN THE LIFT,17,0,0,0,9,31
10017 DATAIN A CORRIDOR ON LEVEL 3. THERE ARE ROOMS ON EACH SIDE,18,21,16,24,0,0
10018 DATAIN A CORRIDOR. THERE ARE MORE ROOMS ,19,22,17,23,0,0
10019 DATAIN A CORRIDOR. THERE IS A SMALL SERVICE HATCH,0,20,18,25,0,0
10020 DATAIN A SERVICE HATCH. THERE IS A LADDER GOING UP,0,0,0,19,27,0
10021 DATAIN A ROOM WITH A TABLE WITH A COM-UNIT. THE DOOR HAS A SIGN READING 'C. KIRK',0,0,0,17,0,0
10022 DATAIN A ROOM WITH VULCAN FURNISHINGS,0,0,0,18,0,0
10023 DATAIN A ROOM WITH A SIGN ON THE DOOR SAYING 'DR MCCOY',0,18,0,0,0,0
10024 DATAIN A ROOM WITH A SIGN ON THE DOOR SAYING 'MR. SCOTT' A DRINKS CABINET,0,17,0,0,0,0
10025 DATAIN A CORRIDOR OUTSIDE A ROOM. THE ROOM HAS A SIGN SAYING 'BRIEFING ROOM',26,19,0,0,0,0
10026 DATAIN THE BRIEFING ROOM. A LARGE MONITOR DOMINATES THE TABLE.,0,0,25,0,0,0
10027 DATAIN A SERVICE VENT,,0,0,0,28,20
10028 DATABETWEEN TWO SERVICE LADDERS,0,29,0,0,0,27
10029 DATAON A SERVICE LADDER,0,0,0,28,0,30
10030 DATAON A SERVICE LADDER,0,0,0,0,29,70
10031 DATAIN THE LIFT,33,0,0,0,16,42
10032 DATABY A SERVICE LADDER,0,0,0,40,71,0
10033 DATAIN ENGINEERING. A MAP SHOWS THE LEVELS 1=BRIDGE 2=SICKBAY 3=QUARTERS 4=ENGINEERING,37,0,31,0,0,0
10034 DATABY THE MAIN ENGINE CONTROL PANEL,0,0,35,0,0,0
10035 DATAON SOME STAIRS,34,0,36,0,0,37
10036 DATABY THE ENGINES IT IS HOT,35,0,0,0,0,0
10037 DATABY SOME STAIRS,41,38,33,0,35,0
10038 DATABEHIND A WALL TO THE NORTH OF SOME CONTROL PANELS,0,0,39,37,0,0
10039 DATABY SOME CONTROL PANELS,38,0,0,0,0,0
10040 DATABY A SERVICE HATCH,0,32,0,41,0,0
10041 DATABY A MAP OF THE ENTERPRISE'S LOWER LEVELS 5=SHUTTLE BAY 6=TRANSPORTER,0,40,37,0,0,0
10042 DATAIN THE LIFT,43,0,0,0,31,51
10043 DATABY A SHUTTLE BAY MARKED I. THERE'S AN INTERCOM TO TALK TO THE SHUTTLECRAFT CREW,45,44,42,0,0,0
10044 DATAIN SHUTTLE BAY I. THERE ARE STEPS LEADING UP,0,0,0,43,47,0
10045 DATABY A SHUTTLE BAY MARKED II. THERE IS AN INTERCOM,0,0,43,46,0,0
10046 DATAIN SHUTTLE BAY II. THERE ARE STEPS LEADING UP,0,45,0,0,49,0
10047 DATAIN THE AIRLOCK OF SHUTTLECRAFT I,48,0,0,0,0,44
10048 DATAIN THE SHUTTLECRAFT I,0,0,47,0,0,0
10049 DATAIN THE AIRLOCK OF SHUTTLECRAFT II,50,0,0,0,0,46
10050 DATAIN THE SHUTTLECRAFT II,0,0,49,0,0,0
10051 DATAIN THE LIFT,52,0,0,0,42,0
10052 DATAIN THE TRANSPORTER ROOM. MR RECK IS MANNING THE CONTROLS. THE PLATES ARE EAST,0,53,51,54,0,0
10053 DATAON THE TRANSPORTER PLATES,0,0,0,52,0,0
10054 DATAIN A STORE ROOM,0,52,0,0,0,0
10055 DATAON THE LANDING AREA,56,0,0,0,0,0
10056 DATANEAR A POOL,0,57,55,0,0,0
10057 DATABY A POOL,0,58,59,56,0,0
10058 DATANEAR A POOL,0,61,0,57,0,0
10059 DATABY THE MAIN DOORS OF A LARGE BUILDING,57,0,0,0,0,0
10060 DATAAT THE BOTTOM OF A POOL,0,0,0,0,57,0
10061 DATABY A PILE OF ROCKS,0,0,0,58,0,0
10062 DATAJUST INSIDE THE MAIN DOORS OF THE BUILDING,0,63,66,59,0,0
10063 DATAOUTSIDE A STORE ROOM,0,64,65,62,0,0
10064 DATAIN A STORE ROOM,0,0,0,63,0,0
10065 DATABY A TABLE,63,0,0,0,0,0
10066 DATAIN A LONG PASSAGE,62,0,67,0,0,0
10067 DATAIN A WELL FURNISHED ROOM,66,0,68,0,0,0
10068 DATANEAR A LABORATORY,67,0,69,0,0,0
10069 DATAIN A LABORATORY. THERE ARE PILES OF BROKEN GLASSWARE EVERYWHERE,68,0,0,0,0,0
10070 DATANEAR THE LOWER LEVELS,0,0,0,0,30,71
10071 DATAJUST OFF THE FLOOR,0,0,0,0,70,32
11001 DATAPHAS,PHASER,2,0,TRIC,TRICORDER,0,15,MEDI,MEDICAL KIT,5,12,TRUT,TRUTH SERUM,0,-3
11005 DATASNAK,SNAKE-BITE SERUM,0,-3,TRAN,TRANQUILLISER SERUM,0,-3,LIFE,LIFE-READER,0,-3
11008 DATASCAL,SCALPEL,0,14,ORDE,ORDERS AND BRIEF,3,26,TORC,TORCH,0,23,WHIS,BOTTLE OF WHISKY,6,24,TOOL,TOOL KIT,0,34
11013 DATASPAN,SPANNER,0,-12,SCRE,SCREWDRIVER,0,-12,PLIE,PLIERS,0,-12,CUTT,WIRE CUTTERS,0,-12,COM,COM-UNIT,1,0
11018 DATASWIT,SWITCH,1,-17,BUTT,RED BUTTON,1,-20,INTE,INTERCOM,1,0,SIGN,SIGN,1,0,CAPT,CAPTAIN'S COM-UNIT,1,2
11023 DATAUHUR,UHURA,1,6,CHEK,CHEKOV,1,3,SULU,SULU,1,1,RECK,MR RECK,1,52,KIRK,KIRK,1,2,SPOC,SPOCK,1,5
11029 DATAMCCO,MCCOY,1,10,SCOT,SCOTTY,1,33,RADI,RADIATION SUIT,0,54,VACU,VACUUM SUIT,0,54,DIVI,DIVING SUIT,0,54
11034 DATATOUC,TOUCH BUTTON,1,-22,GUAR,SECURITY GUARD,1,0,BAD,BAD RYETALYN CRYSTALS,0,60,POOL,LARGE POOL,1,57
11038 DATAANTI,ANTITOXIN,0,0,HYPO,EMPTY HYPO,0,-3,ROCK,SMALL ROCK,0,61,KEY,GOLDEN KEY,0,0,DROI,GUARD DROID,1,63
11043 DATAEQUI,BAG OF EQUIPMENT,0,64,BUNS,BUNSEN BURNER,0,-43,JAR,GLASS JAR,0,-43,TUBE,LONG COLD TUBE,0,-43
11047 DATASHIE,FORCE SHIELD,1,66,SAFE,WALL SAFE,1,67,HOLB,HOLBERG,1,68,BUCK,BUCKET OF WATER,0,60,BOTT,GAS BOTTLE,0,-48
11052 DATATAPE,TICKER TAPE,0,65,READ,TAPE READER,0,5,PERF,PERFECT RYETALYN,0,69,DOOR,LOCKED DOOR,1,59
12001 DATANORTH,EAST,SOUTH,WEST,UP,DOWN,GET,TAKE,MENU,QUIT,DROP,PUSH,LOOK,INVENTORY,EXAMINE,BEAM,DIVE,FILL
12002 DATAUNLOCK,SHOOT,ASSEMBLE,DISTILL,TYPE,NERVEPINCH,READ,THROW
20000 NV=26:NO=55:NL=71:DIMV$(NV),N$(NO,1),N(NO,1),G(4,4):G(1,1)=1:G(2,1)=1:G(3,1)=1:G(4,1)=1
20002 FORI=1TONL:READN$,A,B,C,D,E,F:NEXTI:FORI=1TONO:READN$(I,0),N$(I,1),N(I,0),N(I,1):NEXTI
20004 FORI=1TONV:READV$(I):NEXTI:KN=0:AA=0:FI=0:EM$="WE'VE ONLY GOT 5 PEOPLE WORKING DOWN HERE.":UN=00:SH=1:GU=1:G2=1:
20007 SI=1:DA=0:SR$="WE'VE GOT A BAD OUTBREAK OF RIGELLIAN FEVER. WE NEED pure RYETALYN TO PROCESS INTO AN ANTITOXIN."
20008 SS=0:PL$="HOLBERG":CB$="EDDIE":FORI=1TO5:MID$(CB$,I,1)=CHR$(ASC(MID$(CB$,I,1))-16):NEXT:RETURN
