0 CLEAR3000:DIMCC,ZZ,M$:GOSUB5000:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 DIMM$(16,11),V$(20),N$(20),IV(20),D$(50):IV(2)=1:DF=0:PF=0:LG=1:WF=0:NN=14:NV=10:TF=0
11 FORI=1TO16:FORJ=1TO11:READM$(I,J):NEXT:NEXT:FORI=1TONV:READV$(I):NEXT:FORI=1TONN:READN$(I):NEXT:GOSUB203
12 DATA 04,04,04,04,04,01,04,04,04,04,04
13 DATA 04,W,W,W,W,01,W,W,W,W,04
14 DATA 04,W,2709,26,25,24,W,22,2307,W,04
15 DATA 04,W,W,28,W,0110,W,21,W,W,04
16 DATA 04,W,W,2905,W,18,19,20,W,W,04
17 DATA 04,W,W,W,W,01,W,W,W,W,04
18 DATA 04,39,W,31,30,17,W,W,W,W,04
19 DATA 04,38,W,32,W,15,161113,W,W,W,04
20 DATA 04,3712,W,33,W,01,W,W,W,W,04
21 DATA 04,36,35,34,W,01,W,12,W,W,04
22 DATA 04,40,W,W,W,0901,10,11,W,W,04
23 DATA 04,41,W,07,W,01,W,13,140309,W,04
24 DATA 04,4214,W,0608,05,02,04,W,W,W,04
25 DATA 04,43,W,08,W,03,04,W,W,W,04
26 DATA 04,04,W,W,W,04,04,04,04,04,04
27 DATA 04,04,04,04,04,04,04,04,04,04,04
30 GOSUB3000
40 D$(40)=D$(37):D$(41)=D$(37):D$(42)=D$(37)
42 X=6:Y=13
43 CLS:A$=M$(Y,X):RC$=LEFT$(A$,2):RC=VAL(RC$)
44 IFRC=7THEN166
45 IFRC=16THEN175
46 IFRC=19THEN178
47 IFRC=20THEN181
48 IFRC=23THEN184
49 IFRC=25THEN187
50 IFRC=39THEN193
51 IFRC=43THEN196
52 IFRC=24THENPF=0
53 PRINTD$(RC)
54 IFLEN(A$)<3THEN58
55 L=LEN(A$)-2:L=INT(L/2)
56 PRINT"VISIBLE ITEMS:":O$=MID$(A$,3)
57 FORI=1TOL:O1$=MID$(O$,(I-1)*2+1,2):O1=VAL(O1$):PRINTN$(O1):NEXT
58 PRINT"OBVIOUS EXITS: ";
59 IFY=1THENPRINT"N ";:GOTO61
60 IFM$(Y-1,X)<>"W"THENPRINT"N ";
61 IFX=11 THENPRINT"E ";:GOTO63
62 IFM$(Y,X+1)<>"W"THENPRINT"E ";
63 IFY=16 THENPRINT"S ";:GOTO65
64 IFM$(Y+1,X)<>"W"THENPRINT"S ";
65 IFX=1 THENPRINT"W ";:GOTO68
66 IFM$(Y,X-1)<>"W"THENPRINT"W ";
67 PRINT
68 N1$="":V1$=""
69 C$="":INPUTC$
70 IFC$=""THEN69
71 IFLEN(C$)=1THEN85
72 GOSUB2000:IFP>0THEN74
73 PRINT"PLEASE USE A NOUN AND A VERB.":GOTO68
74 V1$=LEFT$(C$,P-1)
75 N1$=MID$(C$,P+1):IFN1$=""ORV1$=""THEN73
76 FORI=1TONN:IFLEFT$(N1$,3)=LEFT$(N$(I),3)THENN=I:I=2000
77 NEXT:IFI<>2001THENM$="I DON'T KNOW WHAT A '"+N1$+"' IS.":GOSUB1:GOTO68
78 FORI=1TONV:IFLEFT$(V1$,3)=LEFT$(V$(I),3)THENV=I:I=2000
79 NEXT:IFI<>2001THENM$="I DON'T KNOW HOW TO '"+V1$+"'.":GOSUB1:GOTO68
80 ONVGOTO110,118,125,138,142,145,148,153,157,160
81 PRINT"YOU DON'T HAVE IT.":GOTO68
82 PRINT"THAT'S IMPOSSIBLE.":GOTO68
83 PRINT"YOU CAN'T GO THAT WAY.":GOTO68
84 PRINT"NOTHING HAPPENS.":GOTO68
85 IFC$<>"I"THEN93
86 PRINT"YOU HAVE:":FORI=1TONN:IFIV(I)=0THENNEXT:GOTO68
87 PRINTN$(I);:IFI<>2ANDI<>4 THENPRINT:NEXT:GOTO68
88 IFI=2THEN91
89 IFWF=0THENPRINT" (carried)":NEXT:GOTO68
90 PRINT" (worn)":NEXT:GOTO68
91 IFDF=0THENPRINT" (holstered)":NEXT:GOTO68
92 PRINT" (drawn)":NEXT:GOTO68
93 IFC$<>"N"THEN97
94 IFY=1THEN43
95 IFM$(Y-1,X)="W"THEN83
96 Y=Y-1:GOTO43
97 IFC$<>"E"THEN101
98 IFX=11THEN43
99 IFM$(Y,X+1)="W"THEN83
100 X=X+1:GOTO43
101 IFC$<>"S"THEN105
102 IFY=16THEN43
103 IFM$(Y+1,X)="W"THEN83
104 Y=Y+1:GOTO43
105 IFC$<>"W"THEN109
106 IFX=1THEN43
107 IF M$(Y,X-1)="W" THEN83
108 X=X-1:GOTO43
109 PRINT"I DON'T UNDERSTAND.":GOTO68
110 IFIV(N)=1THENPRINT"YOU ALREADY HAVE IT.":GOTO68
111 ON-(LEN(A$)<3)GOTO117:L=LEN(A$)-2:L=INT(L/2):O$=MID$(A$,3)
112 FORI=1TOL:O1$=MID$(O$,(I-1)*2+1,2):O1=VAL(O1$):IFO1=NTHENIV(N)=1:M$=LEFT$(O$,(I-1)*2)+"  "+MID$(O$,(I-1)*2+3):O$=M$
113 NEXT:IFIV(N)=0THEN117
114 T$="":FORI=1TOLEN(O$):T1$=MID$(O$,I,1):IFT1$<>" " THENT$=T$+T1$
115 NEXT:M$(Y,X)=LEFT$(A$,2)+T$
116 GOTO43
117 PRINT"IT ISN'T HERE.":GOTO68
118 IFIV(N)=0 THEN81
119 IV(N)=0:T$=MID$(STR$(N),2)
120 IFLEN(T$)=1THENT$="0"+T$
121 M$(Y,X)=M$(Y,X)+T$
122 IFN=2THENDF=0
123 IFN=4THENWF=0
124 GOTO43
125 IFIV(N)=0THEN81
126 IFN=3THEN130
127 IFN=4THEN136
128 IFN=5THEN137
129 PRINT"YOU SEE NOTHING SPECIAL.":GOTO68
130 PRINT"THE LABEL READS:"
131 PRINT"   'AVENGER' CIGARETTES"
132 PRINT"             --"
133 PRINT"     BRAND OF WINNERS"
134 PRINT"WANT TO READ MORE ? (Y/N)":GOSUB206:IFK$="N"THEN68
135 PRINT"WARNING: THE SURGEON GENERAL HASDETERMINED THAT CIGARETTE SMOK- ING IS DANGEROUS TO YOUR HEALTH.":GOTO68
136 PRINT"IT IS A DUSTY, OLD OVERCOAT     WHICH IS BIG ENOUGH TO CONCEAL  ONESELF WHEN WORN.":GOTO68
137 PRINT"IT IS A VERY GOOD BOTTLE OF     WHISKEY.":GOTO68
138 IFIV(N)=0THEN81
139 IFN<>7THEN84
140 IFRC=7THENLG=0:GOTO43
141 GOTO84
142 IFIV(N)=0THEN81
143 IFN<>4THEN82
144 WF=1:PRINT"OK":GOTO68
145 IFN<>6THEN82
146 IFRC=11THENPRINT"THE BARTENDER GIVES YOU A DRINK.THIRSTILY YOU SLUG IT DOWN...   OH NO! P O I S O N !!!":GOTO199
147 GOTO82
148 IFIV(N)=0THEN81
149 IFRC<>16THEN82
150 IFTF=1THEN82
151 IFN<>5THEN81
152 TF=1:IV(5)=0:M$(Y,X)=M$(Y,X)+"04":GOTO43
153 IFIV(N)=0THEN81
154 IFN<>2THEN82
155 IFRC<>7THEN201
156 DF=1:PRINT"OK":GOTO68
157 IFIV(N)=0THEN81
158 IFN<>2THEN82
159 DF=0:PRINT"OK":GOTO68
160 IFIV(N)=0THEN81
161 IFN<>5THEN82
162 PRINT"ARE YOU SURE YOU WANT TO DRINK  NOW? (Y/N)"
163 GOSUB206:IFK$="Y"THEN165
164 PRINT"GOOD CHOICE.":GOTO68
165 PRINT"WOW! THAT ONE SHOT REALLY HIT   THE SPOT":GOTO68
166 IFLG=1THEN174
167 PRINT"YOU ARE AT THE NORTH END OF A   SMALL CORRIDOR. THE GRATE HERE  IS UNLOCKED, LEADING TO A CHUTE BELOW."
168 PRINT"DO YOU TAKE THE CHUTE ? (Y/N)"
169 GOSUB206:IFK$="Y"THEN171
170 PRINT"AS YOU TURN AROUND, YOU SEE A   MAN WITH A GUN AT YOUR HEAD.":GOTO199
171 CLS:PRINT"YOU HAVE STUMBLED UPON THE BOSS'SECRET HIDEOUT..."
172 IF DF=0 THENPRINT"YOU ARE CAUGHT BY SURPRISE...":GOTO199
173 GOTO202
174 PRINT"YOU ARE AT THE SOUTH END OF A   SMALL CORRIDOR. THE GRATE HERE  IS LOCKED.":GOTO54
175 IFTF=1THEN177
176 PRINT"YOU ARE IN A SMALL, DARK ALLEY. THERE IS A WINO IN AN OLD OVER- COAT HERE.":GOTO54
177 PRINT"YOU ARE IN A SMALL, DARK ALLEY.":GOTO54
178 IFTF=1THEN180
179 PRINT"AS YOU APPROACH THE ARCADE, THE WINO QUICKLY RUNS INTO THE AR-  CADE.":GOTO54
180 PRINT"YOU ARE APPROACHING THE ARCADE.":GOTO54
181 IFTF=1THEN183
182 PRINT"YOU ARE IN THE ARCADE. YOU SEE  A WINO RUNNING IN THE HALLWAY   TO THE NORTH.":GOTO54
183 PRINT"YOU ARE IN THE ARCADE. THERE IS A HALLWAY TO THE NORTH.":GOTO54
184 ON-(WF=1)GOTO186:PRINT"AS YOU ENTER THE DUSTY BACK ROOMOF THE ARCADE, YOU SEE A WINO   TALKING TO TWO OTHER MEN..."
185 PRINT"THE MEN WALK TOWARDS YOU...     ONE OF THEM CLUBS YOU OVER THE  HEAD...":GOTO199
186 PRINT"YOU ARE IN A DUSTY, BACK ROOM   OF THE ARCADE. THE TWO MEN THEREDON'T SEEM TO NOTICE YOU.":GOTO54
187 IFPF=1THEN192
188 PRINT"YOU ARE AT THE TICKET WINDOW."
189 IFIV(1)=1THEN191
190 PRINT"YOU MAY NOT ENTER WITHOUT MON-  EY!!!!":SOUND100,4:FORTD=0TO2500:NEXT:X=X+1:GOTO43
191 PF=1:IV(1)=0:PRINT"THE CLERK TAKES YOUR DOLLAR AND LETS YOU PASS.":GOTO54
192 PRINT"YOU ARE AT THE TICKET WINDOW.":GOTO54
193 PRINT"YOU ARE AT THE NORTH END OF THE PATH. THERE IS A BUTTON ON THE  WALL HERE. DO YOU PRESS IT ?    (Y/N)"
194 GOSUB206:IFK$="Y"THENX=2:Y=14:GOTO43
195 GOTO54
196 PRINT"YOU ARE AT THE SOUTH END OF A   PATH. THERE IS A BUTTON ON THE  WALL HERE. DO YOU PRESS IT?     (Y/N)"
197 GOSUB206:IFK$="Y"THENX=2:Y=7:GOTO43
198 GOTO54
199 PRINT"YOU HAVE DIED. CARE TO LIVE     AGAIN ? (Y/N)":GOSUB206:IFK$="Y"THEN2
200 END
201 PRINT"SHOULDN'T DRAW YOUR GUN HERE!!!":GOTO68
202 PRINT"WITH YOUR TRUSTY WEAPON DRAWN,  YOU'VE COLLARED THE BOSS.":END
203 PRINT@484,"press "CHR$(91)"enter"CHR$(93)" to continue";
204 IFINKEY$<>CHR$(13) THEN204
205 RETURN
206 K$=INKEY$:IFK$="Y"ORK$="N"THENRETURN
207 GOTO206
2000 P=0:FORCC=1TOLEN(C$):IFMID$(C$,CC,1)=" "THENP=CC:CC=255:NEXT:RETURN
2010 NEXT:RETURN
3000 D$(1)="YOU ARE ON A NORTH/SOUTH ROAD."
3001 D$(2)="YOU ARE ON A NORTH/SOUTH ROAD,  THERE IS A HOTEL TO THE WEST."
3002 D$(3)="YOU ARE AT THE END OF A NORTH/  SOUTH STREET.":D$(4)="YOU ARE IN A FIELD."
3010 D$(5)="YOU ARE IN THE HOTEL LOBBY. THE CARPET HERE LOOKS WORN AND      SHABBY."
3011 D$(6)="YOU ARE AT THE CENTER OF A SMALLCORRIDOR.":D$(8)="YOU ARE AT THE SOUTH END OF A   SMALL CORRIDOR."
3020 D$(9)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A BAR TO THE EAST."
3021 D$(10)="YOU ARE INSIDE THE BAR, STANDINGBY THE DOOR YOU CAME IN."
3022 D$(11)="YOU ARE AT THE CENTER OF THE    BAR. THE BARTENDER ASKS YOU IF  YOU WANT TO BUY A DRINK OR JUST"
3030 D$(11)=D$(11)+" LOOK AT THE INTERIOR."
3031 D$(12)="YOU ARE AT THE NORTH END OF THE BAR. IT IS NOT VERY WELL LIT    HERE."
3032 D$(13)="YOU ARE AT THE SOUTH END OF THE BAR. THERE IS A RESTROOM TO THE EAST."
3033 D$(14)="YOU ARE IN THE RESTROOM AT THE  BAR."
3040 D$(15)="YOU ARE ON A NORTH/SOUTH ROAD.  TO THE EAST IS A DARK ALLEY."
3041 D$(17)="YOU ARE ON A NORTH/SOUTH ROAD.  TO THE WEST IS A SMALL GARDEN   PATH."
3050 D$(18)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A VIDEO ARCADE TO THE  EAST."
3051 D$(21)="YOU ARE IN A SMALL HALLWAY."
3052 D$(22)="YOU ARE AT THE NORTH END OF A   SMALL HALLWAY. THERE IS A DOOR  TO THE EAST HERE."
3060 D$(24)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A MOVIE THEATRE TO THE WEST. A SIGN SAYS:"
3061 D$(24)=D$(24)+CHR$(13)+"'SPECIAL TODAY: $1 ADMISSION'"
3062 D$(26)="YOU ARE IN THE THEATRE LOBBY.   THERE IS A RESTROOM TO THE WEST."
3070 D$(27)="YOU ARE IN THE THEATRE RESTROOM.":D$(28)="YOU ARE IN A SMALL CORRIDOR."
3071 D$(29)="YOU ARE IN THE MOVIE VIEWING    ROOM.":D$(30)="YOU ARE ON A SMALL PATH GOING   EAST AND WEST."
3080 D$(31)="HERE THE EAST/WEST PATH ENDS.   A PATH LEADS TO THE SOUTH."
3081 D$(32)="YOU ARE ON A NORTH/SOUTH PATH.":D$(33)=D$(32):D$(34)="THE PATH CONTINUES TO THE WEST."
3082 D$(35)="YOU ARE ON A VERY SMALL EAST/   WEST PATH."
3090 D$(36)="YOU ARE STANDING AT THE INTER-  SECTION BETWEEN THE END OF THE  SMALL EAST/WEST PATH AND THE"
3091 D$(36)=D$(36)+CHR$(13)+"MIDDLE OF A LONG NORTH/SOUTH    PATH.":RETURN
4000 D$(37)="YOU ARE ON A LARGE NORTH/SOUTH  PATH.":D$(38)=D$(37)
4010 DATAGET,DROP,EXAMINE,USE,WEAR,BUY,TRADE,DRAW,HOLSTER,DRINK
4020 DATADOLLAR,GUN,CIGARETTES,COAT (WINO'S),BOTTLE OF WHISKEY
4030 DATADRINK,KEY,CROW-BAR,TOILET PAPER,PENCIL,EMPTY BOTTLE,LEAD PIPE,GARBAGE CAN,ALUMINUM FOIL
5000 CLS:PRINTTAB(7)"skid row adventure":PRINT
5010 PRINT" YOU ARE A DETECTIVE FOR THE     POLICE. YOUR JOB: OUTSMART AND  CATCH 'THE BOSS', IN HIS PART   OF TOWN."
5020 PRINT" USE N,S,E,W AND I FOR NORTH,    SOUTH, EAST, WEST AND INVENT-"
5030 PRINT" ORY, RESPECTIVELY. USE TWO-WORD COMMANDS COMPOSED OF A VERB"
5040 PRINT" AND A NOUN TO DO OTHER THINGS.  IF YOU GET STUCK, TRY EXAMIN-   ING THINGS.":RETURN
6000 REM MC-10 EDITS JIM GERRIE
