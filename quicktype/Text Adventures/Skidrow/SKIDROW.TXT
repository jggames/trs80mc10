1 REM COPYRIGHT (C) MICRODEAL 1985
2 PMODE0:PCLEAR1
3 CLEAR3000
4 CLS:PRINTTAB(7)"skid row adventure":PRINT
5 PRINT" YOU ARE A DETECTIVE FOR THE     POLICE. YOUR JOB: OUTSMART AND  CATCH 'THE BOSS', IN HIS PART   OF TOWN."
6 PRINT" USE N,S,E,W AND I FOR NORTH,    SOUTH, EAST, WEST AND INVENT-   ORY, RESPECTIVELY. USE TWO-WORD COMMANDS COMPOSED OF A VERB     AND A NOUN TO DO OTHER THINGS.  IF YOU GET STUCK, TRY EXAMIN-   ING THINGS."
7 DIM M$(16,11),V$(20),N$(20),IV(20),D$(50):IV(2)=1:DF=0:PF=0:LG=1:WF=0:NN=14:NV=10:TF=0
8 FORI=1TO16:FORJ=1TO11:READM$(I,J):NEXT:NEXT
9 FORI=1 TO NV:READ V$(I):NEXT
10 FORI=1 TO NN:READ N$(I):NEXT
11 GOSUB203
12 DATA 04,04,04,04,04,01,04,04,04,04,04
13 DATA 04,W,W,W,W,01,W,W,W,W,04
14 DATA 04,W,2709,26,25,24,W,22,2307,W,04
15 DATA 04,W,W,28,W,0110,W,21,W,W,04
16 DATA 04,W,W,2905,W,18,19,20,W,W,04
17 DATA 04,W,W,W,W,01,W,W,W,W,04
18 DATA 04,39,W,31,30,17,W,W,W,W,04
19 DATA 04,38,W,32,W,15,161113,W,W,W,04
20 DATA 04,3712,W,33,W,01,W,W,W,W,04
21 DATA 04,36,35,34,W,01,W,12,W,W,04
22 DATA 04,40,W,W,W,0901,10,11,W,W,04
23 DATA 04,41,W,07,W,01,W,13,140309,W,04
24 DATA 04,4214,W,0608,05,02,04,W,W,W,04
25 DATA 04,43,W,08,W,03,04,W,W,W,04
26 DATA 04,04,W,W,W,04,04,04,04,04,04
27 DATA 04,04,04,04,04,04,04,04,04,04,04
28 DATA GET,DROP,EXAMINE,USE,WEAR,BUY,TRADE,DRAW,HOLSTER,DRINK
29 DATA DOLLAR,GUN,CIGARETTES,COAT (WINO'S),BOTTLE OF WHISKEY,DRINK,KEY,CROW-BAR,TOILET PAPER,PENCIL,EMPTY BOTTLE,LEAD PIPE,GARBAGE CAN,ALUMINUM FOIL
30 D$(1)="YOU ARE ON A NORTH/SOUTH ROAD.":D$(2)="YOU ARE ON A NORTH/SOUTH ROAD,  THERE IS A HOTEL TO THE WEST.":D$(3)="YOU ARE AT THE END OF A NORTH/  SOUTH STREET.":D$(4)="YOU ARE IN A FIELD."
31 D$(5)="YOU ARE IN THE HOTEL LOBBY. THE CARPET HERE LOOKS WORN AND      SHABBY.":D$(6)="YOU ARE AT THE CENTER OF A SMALLCORRIDOR.":D$(8)="YOU ARE AT THE SOUTH END OF A   SMALL CORRIDOR."
32 D$(9)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A BAR TO THE EAST.":D$(10)="YOU ARE INSIDE THE BAR, STANDINGBY THE DOOR YOU CAME IN.":D$(11)="YOU ARE AT THE CENTER OF THE    BAR. THE BARTENDER ASKS YOU IF  YOU WANT TO BUY A DRINK OR JUST"
33 D$(11)=D$(11)+" LOOK AT THE INTERIOR.":D$(12)="YOU ARE AT THE NORTH END OF THE BAR. IT IS NOT VERY WELL LIT    HERE.":D$(13)="YOU ARE AT THE SOUTH END OF THE BAR. THERE IS A RESTROOM TO THE EAST.":D$(14)="YOU ARE IN THE RESTROOM AT THE  BAR."
34 D$(15)="YOU ARE ON A NORTH/SOUTH ROAD.  TO THE EAST IS A DARK ALLEY.":D$(17)="YOU ARE ON A NORTH/SOUTH ROAD.  TO THE WEST IS A SMALL GARDEN   PATH."
35 D$(18)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A VIDEO ARCADE TO THE  EAST.":D$(21)="YOU ARE IN A SMALL HALLWAY.":D$(22)="YOU ARE AT THE NORTH END OF A   SMALL HALLWAY. THERE IS A DOOR  TO THE EAST HERE."
36 D$(24)="YOU ARE ON A NORTH/SOUTH ROAD.  THERE IS A MOVIE THEATRE TO THE WEST. A SIGN SAYS:               'SPECIAL TODAY: $1 ADMISSION'":D$(26)="YOU ARE IN THE THEATRE LOBBY.   THERE IS A RESTROOM TO THE WEST."
37 D$(27)="YOU ARE IN THE THEATRE RESTROOM.":D$(28)="YOU ARE IN A SMALL CORRIDOR.":D$(29)="YOU ARE IN THE MOVIE VIEWING    ROOM.":D$(30)="YOU ARE ON A SMALL PATH GOING   EAST AND WEST."
38 D$(31)="HERE THE EAST/WEST PATH ENDS.   A PATH LEADS TO THE SOUTH.":D$(32)="YOU ARE ON A NORTH/SOUTH PATH.":D$(33)=D$(32):D$(34)="THE PATH CONTINUES TO THE WEST.":D$(35)="YOU ARE ON A VERY SMALL EAST/   WEST PATH."
39 D$(36)="YOU ARE STANDING AT THE INTER-  SECTION BETWEEN THE END OF THE  SMALL EAST/WEST PATH AND THE    MIDDLE OF A LONG NORTH/SOUTH    PATH."
40 D$(37)="YOU ARE ON A LARGE NORTH/SOUTH  PATH.":D$(38)=D$(37)
41 D$(40)=D$(37):D$(41)=D$(37):D$(42)=D$(37)
42 X=6:Y=13
43 CLS:A$=M$(Y,X):RC$=LEFT$(A$,2):RC=VAL(RC$)
44 IF RC=7 THEN 166
45 IF RC=16 THEN175
46 IF RC=19 THEN178
47 IF RC=20 THEN181
48 IF RC=23 THEN184
49 IF RC=25 THEN187
50 IF RC=39 THEN193
51 IF RC=43 THEN196
52 IF RC=24 THENPF=0
53 PRINTD$(RC)
54 IF LEN(A$)<3 THEN58
55 L=LEN(A$)-2:L=INT(L/2)
56 PRINT"VISIBLE ITEMS:":O$=MID$(A$,3)
57 FORI=1 TO L:O1$=MID$(O$,(I-1)*2+1,2):O1=VAL(O1$):PRINTN$(O1):NEXT
58 PRINT"OBVIOUS EXITS: ";
59 IF Y=1 THENPRINT"N ";:GOTO61
60 IF M$(Y-1,X)<>"W" THENPRINT"N ";
61 IF X=11 THENPRINT"E ";:GOTO63
62 IF M$(Y,X+1)<>"W" THENPRINT"E ";
63 IF Y=16 THENPRINT"S ";:GOTO65
64 IF M$(Y+1,X)<>"W" THENPRINT"S ";
65 IF X=1 THENPRINT"W ";:GOTO68
66 IF M$(Y,X-1)<>"W" THENPRINT"W ";
67 PRINT
68 N1$="":V1$=""
69 C$="":LINEINPUT">";C$
70 IF C$=""THEN69
71 IF LEN(C$)=1 THEN85
72 P=INSTR(C$," "):IFP>0 THEN74
73 PRINT"PLEASE USE A NOUN AND A VERB.":GOTO68
74 V1$=LEFT$(C$,P-1)
75 N1$=MID$(C$,P+1):IF N1$="" OR V1$="" THEN73
76 FORI=1 TO NN:IF LEFT$(N1$,3)=LEFT$(N$(I),3) THENN=I:I=2000
77 NEXT:IFI<>2001 THENPRINT"I DON'T KNOW WHAT A '";N1$;"' IS.":GOTO68
78 FORI=1TO NV:IF LEFT$(V1$,3)=LEFT$(V$(I),3) THENV=I:I=2000
79 NEXT:IF I<>2001 THENPRINT"I DON'T KNOW HOW TO '";V1$;"'.":GOTO68
80 ON V GOTO110,118,125,138,142,145,148,153,157,160
81 PRINT"YOU DON'T HAVE IT.":GOTO68
82 PRINT"THAT'S IMPOSSIBLE.":GOTO68
83 PRINT"YOU CAN'T GO THAT WAY.":GOTO68
84 PRINT"NOTHING HAPPENS.":GOTO68
85 IF C$<>"I" THEN93
86 PRINT"YOU HAVE:":FORI=1TO NN:IF IV(I)=0 THEN92
87 PRINTN$(I);:IFI<>2 AND I<>4 THENPRINT:GOTO92
88 IF I=2 THEN91
89 IF WF=0 THENPRINT" (carried)" ELSEPRINT" (worn)"
90 GOTO92
91 IF DF=0 THENPRINT" (holstered)" ELSEPRINT" (drawn)"
92 NEXT:GOTO68
93 IF C$<>"N" THEN97
94 IF Y=1 THEN43
95 IF M$(Y-1,X)="W" THEN83
96 Y=Y-1:GOTO43
97 IF C$<>"E" THEN101
98 IF X=11 THEN43
99 IF M$(Y,X+1)="W" THEN83
100 X=X+1:GOTO43
101 IF C$<>"S" THEN105
102 IF Y=16 THEN43
103 IF M$(Y+1,X)="W" THEN83
104 Y=Y+1:GOTO43
105 IF C$<>"W" THEN109
106 IF X=1 THEN43
107 IF M$(Y,X-1)="W" THEN83
108 X=X-1:GOTO43
109 PRINT"I DON'T UNDERSTAND.":GOTO68
110 IFIV(N)=1 THENPRINT"YOU ALREADY HAVE IT.":GOTO68
111 IF LEN(A$)<3 THEN117
112 L=LEN(A$)-2:L=INT(L/2):O$=MID$(A$,3):FORI=1 TO L:O1$=MID$(O$,(I-1)*2+1,2):O1=VAL(O1$):IF O1=N THENIV(N)=1:MID$(O$,(I-1)*2+1,2)="  "
113 NEXT:IFIV(N)=0 THEN117
114 T$="":FORI=1TOLEN(O$):T1$=MID$(O$,I,1):IFT1$<>" " THENT$=T$+T1$
115 NEXT:M$(Y,X)=LEFT$(A$,2)+T$
116 GOTO43
117 PRINT"IT ISN'T HERE.":GOTO68
118 IFIV(N)=0 THEN81
119 IV(N)=0:T$=MID$(STR$(N),2)
120 IFLEN(T$)=1 THENT$="0"+T$
121 M$(Y,X)=M$(Y,X)+T$
122 IF N=2 THENDF=0
123 IF N=4 THENWF=0
124 GOTO43
125 IF IV(N)=0 THEN81
126 IFN=3 THEN130
127 IFN=4 THEN136
128 IFN=5 THEN137
129 PRINT"YOU SEE NOTHING SPECIAL.":GOTO68
130 PRINT"THE LABEL READS:"
131 PRINT"   'AVENGER' CIGARETTES"
132 PRINT"             --"
133 PRINT"     BRAND OF WINNERS"
134 PRINT"WANT TO READ MORE ? (Y/N)":GOSUB206:IFK$="N"THEN68
135 PRINT"WARING: THE SURGEON GENERAL HAS DETERMINED THAT CIGARETTE SMOK- ING IS DANGEROUS TO YOUR HEALTH.":GOTO68
136 PRINT"IT IS A DUSTY, OLD OVERCOAT     WHICH IS BIG ENOUGH TO CONCEAL  ONESELF WHEN WORN.":GOTO68
137 PRINT"IT IS A VERY GOOD BOTTLE OF     WHISKEY.":GOTO68
138 IFIV(N)=0 THEN81
139 IF N<>7 THEN84
140 IF RC=7 THENLG=0:GOTO43
141 GOTO84
142 IF IV(N)=0 THEN81
143 IF N<>4 THEN82
144 WF=1:PRINT"OK":GOTO68
145 IF N<>6 THEN82
146 IF RC=11 THENPRINT"THE BARTENDER GIVES YOU A DRINK.THIRSTILY YOU SLUG IT DOWN...   OH NO! P O I S O N !!!":GOTO199
147 GOTO82
148 IF IV(N)=0 THEN81
149 IF RC<>16 THEN82
150 IF TF=1 THEN82
151 IF N<>5 THEN81
152 TF=1:IV(5)=0:M$(Y,X)=M$(Y,X)+"04":GOTO43
153 IF IV(N)=0 THEN81
154 IF N<>2 THEN82
155 IF RC<>7 THEN201
156 DF=1:PRINT"OK":GOTO68
157 IF IV(N)=0 THEN81
158 IF N<>2 THEN82
159 DF=0:PRINT"OK":GOTO68
160 IF IV(N)=0 THEN81
161 IF N<>5 THEN82
162 PRINT"ARE YOU SURE YOU WANT TO DRINK  NOW? (Y/N)"
163 GOSUB206:IFK$="Y"THEN165
164 PRINT"GOOD CHOICE.":GOTO68
165 PRINT"WOW! THAT ONE SHOT REALLY HIT   THE SPOT":GOTO68
166 IF LG=1 THEN174
167 PRINT"YOU ARE AT THE NORTH END OF A   SMALL CORRIDOR. THE GRATE HERE  IS UNLOCKED, LEADING TO A CHUTE BELOW."
168 PRINT"DO YOU TAKE THE CHUTE ? (Y/N)"
169 GOSUB206:IFK$="Y" THEN171
170 PRINT"AS YOU TURN AROUND, YOU SEE A   MAN WITH A GUN AT YOUR HEAD.":GOTO199
171 CLS:PRINT"YOU HAVE STUMBLED UPON THE BOSS'SECRET HIDEOUT..."
172 IF DF=0 THENPRINT"YOU ARE CAUGHT BY SURPRISE...":GOTO199
173 GOTO202
174 PRINT"YOU ARE AT THE SOUTH END OF A   SMALL CORRIDOR. THE GRATE HERE  IS LOCKED.":GOTO54
175 IF TF=1 THEN177
176 PRINT"YOU ARE IN A SMALL, DARK ALLEY. THERE IS A WINO IN AN OLD OVER- COAT HERE.":GOTO54
177 PRINT"YOU ARE IN A SMALL, DARK ALLEY.":GOTO54
178 IF TF=1 THEN180
179 PRINT"AS YOU APPROACH THE ARCADE, THE WINO QUICKLY RUNS INTO THE AR-  CADE.":GOTO54
180 PRINT"YOU ARE APPROACHING THE ARCADE.":GOTO54
181 IF TF=1 THEN183
182 PRINT"YOU ARE IN THE ARCADE. YOU SEE  A WINO RUNNING IN THE HALLWAY   TO THE NORTH.":GOTO54
183 PRINT"YOU ARE IN THE ARCADE. THERE IS A HALLWAY TO THE NORTH.":GOTO54
184 IF WF=1 THEN186
185 PRINT"AS YOU ENTER THE DUSTY BACK ROOMOF THE ARCADE, YOU SEE A WINO   TALKING TO TWO OTHER MEN...     THE MEN WALK TOWARDS YOU...     ONE OF THEM CLUBS YOU OVER THE  HEAD...":GOTO199
186 PRINT"YOU ARE IN A DUSTY, BACK ROOM   OF THE ARCADE. THE TWO MEN THEREDON'T SEEM TO NOTICE YOU.":GOTO54
187 IF PF=1 THEN192
188 PRINT"YOU ARE AT THE TICKET WINDOW."
189 IF IV(1)=1 THEN191
190 PRINT"YOU MAY NOT ENTER WITHOUT MON-  EY!!!!":SOUND100,4:FORTD=0TO2000:NEXT:X=X+1:GOTO43
191 PF=1:IV(1)=0:PRINT"THE CLERK TAKES YOUR DOLLAR AND LETS YOU PASS.":GOTO54
192 PRINT"YOU ARE AT THE TICKET WINDOW.":GOTO54
193 PRINT"YOU ARE AT THE NORTH END OF THE PATH. THERE IS A BUTTON ON THE  WALL HERE. DO YOU PRESS IT ?    (Y/N)"
194 GOSUB206:IFK$="Y" THENX=2:Y=14:GOTO43
195 GOTO54
196 PRINT"YOU ARE AT THE SOUTH END OF A   PATH. THERE IS A BUTTON ON THE  WALL HERE. DO YOU PRESS IT?     (Y/N)"
197 GOSUB206:IFK$="Y" THENX=2:Y=7:GOTO43
198 GOTO54
199 PRINT"YOU HAVE DIED. CARE TO LIVE     AGAIN ? (Y/N)":GOSUB206:IFK$="Y"THEN2
200 END
201 PRINT"SHOULDN'T DRAW YOUR GUN HERE!!!":GOTO68
202 PRINT"WITH YOUR TRUSTY WEAPON DRAWN,  YOU'VE COLLARED THE BOSS.":END
203 PRINT@484,"press [enter] to continue";
204 IFINKEY$<>CHR$(13) THEN204
205 RETURN
206 K$=INKEY$:IFK$="Y" OR K$="N" THENRETURN ELSE206
