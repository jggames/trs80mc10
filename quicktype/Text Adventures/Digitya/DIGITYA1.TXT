330 CLS0:I=RND(-(PEEK(9)*256+PEEK(10)))
340 PRINT@96,"€€îìé€ìïìàáììàìïìàîïí€æéäâæàæé€€";
341 PRINT"€€ê€å€€ï€€ê€à€€ï€€€ï€áèäâå€áèäâ€";
342 PRINT"€€ê€å€€ï€€ê€é€€ï€€€ï€åããêå€åããê€";
350 PRINT"€€ê€åà€ï€€ê€å€€ï€€€ï€å€€êå€å€€ê€";
352 PRINT"€€ììè€ììì€äìè€ììì€€ì€ä€€èä€ä€€è€";
362 PRINT@32*11,"      PLEASE WAIT A MOMENT"
400 FORI=1TO2000
410 NEXT
1000 CLEAR1000
1100 GOSUB6090:CLS
1110 GOSUB1290
1120 P=47
1130 :
1140 :
1150 :
1160 MF=0:PRINT
1170 GOSUB1440
1180 GOSUB1560
1190 GOSUB2670
1200 IFSF=1THEN1250
1210 PRINT:INPUT"INSTRUCTIONS ";IS$
1220 GOSUB1700
1225 IFF=0THEN1210
1230 GOSUB1900
1240 IFVF=0THENPRINT"I DON'T UNDERSTAND"
1250 IFMF=1THEN1160
1260 IFMF=0THEN1210
1270 END
1290 :
1300 SN$="WELCOME TO 'DIGITAYA'"
1310 GOSUB5880
1320 PRINT
1330 SN$="AS THE MACHINE HUMS QUIETLY, YOU LOOK AROUND."
1340 SN$=SN$+" TO THE NORTH AND SOUTH STRETCHES A WIDE HIGHWAY."
1350 SN$=SN$+" YOUR MISSION IS TO FIND THE MYSTERIOUS DIGITAYA"
1360 SN$=SN$+" AND CARRY IT TO SAFETY THROUGH ONE OF THE OUTPUT"
1365 SN$=SN$+" PORTS...BUT WHICH ONE ?"
1380 GOSUB5880
1390 PRINT:PRINT"PRESS A KEY TO START";
1400 A$=INKEY$:IFA$=""THEN1400
1410 CLS:RETURN
1440 :
1450 CLS:SN$="YOU ARE "+LN$(P):GOSUB5880
1460 SN$="YOU SEE "
1470 :
1480 F=0:SP$=""
1490 FORI=1TO8
1500 IFVAL(IV$(I,2))=PTHENSN$=SN$+SP$+"A "+IV$(I,1):F=1:SP$="  "
1510 NEXTI
1520 IFF=0THENSN$=SN$+"NO OBJECTS"
1530 GOSUB5880
1540 RETURN
1560 :
1570 EX$=EX$(P)
1580 NR=VAL(LEFT$(EX$,2))
1590 EA=VAL(MID$(EX$,3,2))
1600 SO=VAL(MID$(EX$,5,2))
1610 WE=VAL(RIGHT$(EX$,2))
1620 IF(NR OR EA OR SO OR WE)=0THENRETURN
1630 PRINT:SN$="EXITS ARE TO THE "
1640 IFNR<>0THENSN$=SN$+"NORTH "
1650 IFEA<>0THENSN$=SN$+"EAST "
1660 IFSO<>0THENSN$=SN$+"SOUTH "
1670 IFWE<>0THENSN$=SN$+"WEST "
1675 GOSUB5880
1680 PRINT:RETURN
1700 :
1705 F=0
1710 IFIS$="END"ORIS$="LIST"THENVB$=IS$:F=1:RETURN
1720 IFIS$="LOOK"THENVB$=IS$:F=1:RETURN
1740 :
1750 VB$="":NN$=""
1770 LS=LEN(IS$)
1780 FORC=1TOLS
1790 A$=MID$(IS$,C,1)
1800 IFA$=" "THENVB$=LEFT$(IS$,C-1):NN$=RIGHT$(IS$,LS-C):F=1:C=LS
1810 NEXT
1830 IFF=0THENPRINT:PRINT"I NEED AT LEAST TWO WORDS"
1840 RETURN
1900 :
1910 VF=0
1920 PRINT
1930 IFVB$="GO"ORVB$="MOVE"THENVF=1:GOSUB2000
1940 IFVB$="TAKE"ORVB$="PICK"THENVF=1:GOSUB2140
1950 IFVB$="DROP"ORVB$="PUT"THENVF=1:GOSUB2360
1960 IFVB$="LIST"ORVB$="INVENTORY"THENVF=1:GOSUB2540
1965 IFVB$="LOOK"THENVF=1:MF=1:RETURN
1970 IFVB$="END"ORVB$="FINISH"THENVF=1:GOSUB2620
1980 RETURN
2000 :
2010 MF=1
2015 GOSUB8600
2020 DR$=LEFT$(NN$,1)
2030 IFDR$<>"N"ANDDR$<>"E"ANDDR$<>"S"ANDDR$<>"W"THEN2100
2040 IFDR$="N"ANDNR<>0THENP=NR:RETURN
2050 IFDR$="S"ANDSO<>0THENP=SO:RETURN
2060 IFDR$="E"ANDEA<>0THENP=EA:RETURN
2070 IFDR$="W"ANDWE<>0THENP=WE:RETURN
2080 PRINT"YOU CAN'T ";IS$
2090 MF=0:RETURN
2100 :
2110 PRINT"WHAT IS ";NN$;" ?"
2120 MF=0:RETURN
2140 :
2145 IV$(4,1)="TICKET TO TRI-STATE"
2150 GOSUB5730
2160 IFF=0THENPRINT"THERE IS NO ";W$:RETURN
2170 :
2180 OV=F:GOSUB5830
2190 IFHF=1THENSN$="YOU ALREADY HAVE THE "+IV$(F,1):GOSUB5880:RETURN
2210 :
2220 IFVAL(IV$(F,2))<>PTHENSN$=IV$(F,1)+" IS NOT HERE":GOSUB5880
2225 IFVAL(IV$(F,2))<>PTHENRETURN
2240 :
2250 AF=0:FORJ=1TO4
2260 IFIC$(J)=""THENIC$(J)=IV$(F,1):AF=1:J=4
2270 NEXTJ
2290 :
2300 IFAF=0THENPRINT"YOU ALREADY HAVE 4 OBJECTS":RETURN
2320 SN$="YOU TAKE THE "+IV$(F,1):GOSUB5880
2330 IV$(F,2)="-1"
2340 RETURN
2360 :
2370 GOSUB5730
2380 IFF=0THENPRINT"THERE IS NO ";NN$:RETURN
2400 :
2410 OV=F:GOSUB5830
2420 IFHF=0THENPRINT"YOU DO NOT HAVE THE "+IV$(F,1):RETURN
2440 :
2450 SN$="YOU DROP THE "+IV$(F,1):GOSUB5880
2460 IV$(F,2)=STR$(P)
2480 :
2490 FORJ=1TO4
2500 IFIC$(J)=IV$(F,1)THENIC$(J)="":J=4
2510 NEXT:RETURN
2540 :
2550 PRINT"OBJECTS HELD:"
2560 FORI=1TO4
2570 PRINT"  ";IC$(I)
2580 NEXT:RETURN
2620 PRINT:PRINT"ARE YOU SURE (Y/N) ?"
2630 A$=INKEY$:IFA$="N"THENRETURN
2640 IFA$<>"Y"THEN2630
2650 GOTO3360
2670 :
2680 SF=0
2690 IFP=37THEN2780
2700 IFP>7THEN2750
2702 IFP=1THENGOSUB2850
2704 IFP=2THENGOSUB2960
2706 IFP=3THENGOSUB3450
2708 IFP=4THENGOSUB3830
2710 IFP=5THENGOSUB4180
2712 IFP=6THENGOSUB4550
2714 IFP=7THENGOSUB5150
2720 RETURN
2740 :
2750 RA=RND(0)
2760 IFRA<.05THENGOSUB5420
2770 RETURN
2780 :
2790 SF=1
2800 SN$="YOU ARE MOVED AT HIGH SPEED TO A NEW LOCATION"
2805 GOSUB5880
2810 FORJ=1TO1500:NEXT
2820 P=RND(40)+7
2830 MF=1:RETURN
2850 :
2860 SF=1
2870 SN$="YOU HAVE ENTERED THE TV OUTLET AND THERE IS NO ESCAPE."
2880 SN$=SN$+" YOU ARE DOOMED FOREVER TO BE A TV CHAT SHOW HOST"
2890 GOSUB5880
2900 PRINT
2910 PRINT"WELCOME TO THE SHOW....."
2920 FORJ=1TO500:A$=INKEY$:IFA$<>""THEN3360
2925 NEXT
2930 GOTO2910
2960 :
2970 SF=1
2980 SN$="ESCAPE IS AT HAND BUT THE DDR BOOKING CLERK"
2990 SN$=SN$+" BARS YOUR WAY. HE TELLS YOU THAT HE HAS BEEN"
3000 SN$=SN$+" INSTRUCTED TO ACCEPT INPUTS ONLY. HOWEVER HE"
3010 SN$=SN$+" DOES TAKE ALL MAJOR CREDIT CARDS."
3020 GOSUB5880
3040 PRINT:INPUT"INSTRUCTIONS ";IS$
3050 GOSUB1700
3060 GOSUB1900
3070 IFMF=1THENRETURN
3080 IFVF=1THEN3040
3090 IFVB$<>"GIVE"THENPRINT"I DON'T UNDERSTAND":GOTO3040
3110 :
3120 GOSUB5730
3130 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO3040
3160 IFF<>5THENPRINT"HE ONLY ACCEPTS CREDIT CARDS":GOTO3040
3180 :
3190 OV=5:GOSUB5830
3200 IFHF=0THENPRINT"YOU DO NOT HAVE THE ";IV$(5,1):GOTO3040
3220 SN$="THE CLERK TAKES THE CARD AND SAYS 'THAT WILL DO"
3225 SN$=SN$+" NICELY, SIR'"
3230 GOSUB5880
3240 SN$="YOU ARE ALLOWED TO PASS THE BARRIER AND ENTER THE"
3245 SN$=SN$+" USER PORT"
3250 GOSUB5880
3270 SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1
3280 OV=6:GOSUB5830
3290 IFHF=1THEN3380
3320 SN$="WELL DONE YOU HAVE SUCCEEDED IN ESCAPING FROM THE"
3330 SN$=SN$+" CLUTCHES OF THE MACHINE, BUT HAVE FAILED IN YOUR"
3340 SN$=SN$+" MISSION TO BRING BACK THE MYSTERIOUS DIGITAYA"
3350 GOSUB5880
3360 PRINT:PRINT"PLAY AGAIN (Y/N) ?"
3362 A$=INKEY$:IFA$="Y"THENRUN
3364 IFA$<>"N"THEN3362
3365 END
3380 :
3390 SN$="CONGRATULATIONS, YOU HAVE SUCCEEDED IN YOUR MISSION"
3400 SN$=SN$+" TO RESCUE THE WONDEROUS DIGITAYA FROM THE"
3410 SN$=SN$+" CLUTCHES OF THE MACHINE."
3420 GOSUB5880
3430 GOTO3360
3450 :
3460 SF=1
3470 SN$="YOU FEEL AN IRRESISTIBLE FORCE PULLING YOU TOWARDS"
3480 SN$=SN$+" PERMANENT MAGNETIC SUSPENSION"
3490 GOSUB5880
3500 NS=0
3510 :
3520 NS=NS+1:IFNS>3THEN3770
3530 PRINT:INPUT"INSTRUCTIONS ";IS$
3540 GOSUB1700
3550 GOSUB1900
3560 IFMF=1THENMF=0:PRINT"YOU CAN'T MOVE...YET":GOTO3510
3570 IFVF=1THEN3510
3580 IFVB$<>"USE"THENPRINT"I DON'T UNDERSTAND":GOTO3510
3590 :
3600 GOSUB5730
3610 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO3510
3630 :
3640 IFF=8THEN3680
3650 SN$="YOUR "+IV$(F,1)+" IS USELESS, THE FORCE INCREASES"
3660 GOSUB5880:GOTO3510
3680 OV=8:GOSUB5830
3690 IFHF=0THENSN$="YOU DON'T HAVE THE "+IV$(8,1):GOSUB5880
3695 IFHF=0THEN3510
3710 :
3720 SN$="YOU USE THE BUFFER ACTIVATOR TO COUNTER THE PULL"
3730 SN$=SN$+" INTO MAGNETIC OBLIVION. THE FORCE SUBSIDES"
3740 GOSUB5880:RETURN
3770 :
3780 SN$="THE FORCE BECOMES TOO STRONG AND YOU ARE PULLED OUT"
3790 SN$=SN$+" THROUGH THE CASSETTE PORT INTO MAGNETIC NOTHING"
3800 SN$=SN$+"NESS.":GOSUB5880:GOTO3360
3830 :
3840 SF=1
3850 SN$="A USER WITH RED-RIMMED EYES ZAPS HIS LASER AT YOU"
3860 SN$=SN$+" REPEATEDLY.":GOSUB5880
3880 :
3890 RD=RND(0):IFRD>.65THEN4110
3900 PRINT:INPUT"INSTRUCTIONS ";IS$
3910 GOSUB1700:GOSUB1900
3920 IFMF=1THENMF=0:PRINT"YOU CAN'T MOVE...YET":GOTO3880
3930 IFVF=1THEN3880
3940 IFVB$<>"USE"THENPRINT"I DON'T UNDERSTAND":GOTO3880
3950 GOSUB5730
3960 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO3880
3990 IFF=3THEN4020
4000 SN$="YOUR "+IV$(F,1)+" IS NO USE":GOSUB5880:GOTO3880
4020 OV=3:GOSUB5830
4030 IFHF=0THENSN$="YOU DO NOT HAVE THE "+IV$(3,1):GOSUB5880
4035 IFHF=0THEN3880
4050 :
4060 SN$="YOU USE THE LASER SHIELD TO PROTECT YOURSELF. A BLAST"
4070 SN$=SN$+" KNOCKS YOU OUT OF THE JOYSTICK PORT AND BACK"
4080 SN$=SN$+" INTO THE MACHINE.":GOSUB5880
4085 SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1
4090 P=RND(40)+7:MF=1:RETURN
4110 :
4120 SN$="YOU ARE HIT BY THE LASER AND YOU ARE ONLY DIMLY AWARE"
4130 SN$=SN$+" THAT YOUR ATOMS HAVE BEEN DISTRIBUTED TO THE FOUR"
4140 SN$=SN$+" CORNERS OF THE UNIVERSE"
4150 GOSUB5880
4160 GOTO3360
4180 :
4190 SF=1
4200 SN$="A LARGE SIGN SAYS 'I/O THIS WAY' BUT AS YOU MOVE TO"
4210 SN$=SN$+"WARDS IT A TICKET COLLECTOR SHOUTS 'TICKETS PLEASE"
4220 SN$=SN$+"'":GOSUB5880
4240 :
4250 PRINT:INPUT"INSTRUCTIONS ";IS$
4260 GOSUB1700:GOSUB1900
4270 IFMF=1THENRETURN
4280 IFVF=1THEN4240
4290 IFVB$<>"GIVE"ANDVB$<>"OFFER"THENPRINT"I DON'T UNDERSTAND"
4295 IFVB$<>"GIVE"ANDVB$<>"OFFER"THEN4240
4300 :
4310 GOSUB5730
4320 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO4240
4350 IFF=4THEN4400
4360 SN$="THE TICKET COLLECTOR SHAKES HIS HEAD AND SAYS 'I"
4370 SN$=SN$+" CANNOT ACCEPT THIS "+IV$(F,1)
4380 GOSUB5880:GOTO4240
4400 OV=4:GOSUB5830
4410 IFHF=0THENPRINT" YOU DO NOT HAVE THE TICKET":GOTO4240
4440 SN$="THE TICKET COLLECTOR ACCEPTS YOU TICKET AND ALLOWS"
4450 SN$=SN$+" YOU TO PASS THE BARRIER."
4455 GOSUB5880
4460 SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1
4480 F=0
4490 FORJ=1TO4
4500 IFIC$(J)=IV$(4,1)THENIC$(J)="":J=4
4510 NEXT
4520 IV$(4,2)=STR$(RND(40)+8)
4530 P=15:MF=1:RETURN
4550 :
4560 SF=1
4570 RN=RND(3)
4580 IFRN=1THENCD$="AND"
4590 IFRN=2THENCD$="OR"
4600 IFRN=3THENCD$="NOT"
4610 SN$="MOUNTED ON THE WALL THERE ARE THREE BUTTONS MARKED"
4620 SN$=SN$+" 'AND', 'OR' AND 'NOT'. ACCESS CAN BE GAINED TO"
4630 SN$=SN$+" THE ACCUMULATOR BY PRESSING THE CORRECT BUTTON"
4640 GOSUB5880
4660 :
4670 PRINT:INPUT"INSTRUCTIONS ";IS$
4680 GOSUB1700:GOSUB1900
4690 IFVF=1THENRETURN
4700 IFVF=1THEN4670
4710 IFVB$="USE"ORVB$="PRESS"THEN4740
4720 PRINT"I DON'T UNDERSTAND":GOTO4670
4740 :
4750 IFVB$="PRESS"THEN4930
4770 GOSUB5730
4780 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO4670
4810 IFF=7THEN4850
4820 SN$="YOUR "+IV$(F,1)+" IS OF NO USE":GOSUB5880:GOTO4670
4850 OV=7:GOSUB5830
4860 IFHF=1THEN4900
4870 SN$="YOU DO NOT HAVE THE "+IV$(7,1):GOSUB5880:GOTO4670
4900 SN$="YOU OPEN THE CODE BOOK AND FIND THE WORT '"+CD$+"'"
4910 SN$=SN$+" WRITTEN INSIDE":GOSUB5880:GOTO4670
4930 :
4940 IFNN$="AND"ORNN$="OR"ORNN$="NOT"THEN4970
4950 SN$="THERE IS NO "+NN$:GOSUB5880:GOTO4670
4970 :
4980 IFNN$=CD$THEN5100
5020 SN$="WRONG, A TRAP DOOR OPENS AND YOU FIND YOURSELF BACK"
5030 SN$=SN$+" IN MAIN MEMORY"
5040 GOSUB5880
5050 IFRN=1THENP=39
5060 IFRN=2THENP=35
5070 IFRN=3THENP=29
5080 MF=1:RETURN
5100 SN$="THE GATEWAY TO THE ACCUMULATOR SWINGS OPEN AND YOU"
5110 SN$=SN$+" PASS THROUGH":GOSUB5880
5120 SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:P=30:MF=1:RETURN
5150 :
5160 SF=1
5170 SN$="AN USHER GREETS YOU BUT TELLS YOU THAT YOU CANNOT BE"
5180 SN$=SN$+" ADMITTED UNLESS YOU GIVE AN ADDRESS":GOSUB5880
5190 :
5200 PRINT:INPUT"INSTRUCTIONS ";IS$
5210 GOSUB1700:GOSUB1900
5220 IFMF=1THENRETURN
5230 IFVF=1THEN5200
5240 IFVB$<>"GIVE"THENPRINT"I DON'T UNDERSTAND":GOTO5200
5260 GOSUB5730
5270 IFF=0THENPRINT"THERE IS NO ";NN$:GOTO5200
5300 IFF=1THEN5330
5310 PRINT"HE NEEDS YOUR ADDRESS":GOTO5200
5330 OV=1:GOSUB5830
5340 IFHF=1THEN5380
5350 SN$="YOU DON'T HAVE THE "+IV$(1,1):GOSUB5880:GOTO5200
5380 SN$="THE USHER LOOKS AT YOUR ADDRESS AND ALLOWS YOU TO PASS"
5390 SN$=SN$+" THROUGH":GOSUB5880:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1
5400 P=40:MF=1:RETURN
5420 :
5430 SF=1
5440 SN$="A LARGE AND UGLY BUG APPEARS FROM BEHIND A CHIP AND"
5450 SN$=SN$+" LUNGES TOWARDS YOU":GOSUB5880
5470 :
5480 PRINT:INPUT"INSTRUCTIONS ";IS$
5490 GOSUB1700:GOSUB1900
5500 IFMF=1THENMF=0:PRINT"YOU CAN'T MOVE...YET":GOTO5480
5510 IFVF=1THEN5480
5520 IFVB$="KILL"ORVB$="FIGHT"THEN5550
5530 PRINT"I DON'T UNDERSTAND":GOTO5480
5550 :
5560 RA=RND(0)
5570 IFRA<.5THEN5600
5580 GOTO5670
5600 :
5610 SN$="YOU FIGHT WITH THE BUG. IT SHOWERS YOU WITH SPURIOUS"
5620 SN$=SN$+" ERRORS AND THEY EAT INTO YOUR BRAIN. FINALLY"
5630 SN$=SN$+" YOU CAN TAKE NO MORE AND YOUR HEAD EXPLODES."
5640 GOSUB5880:GOTO3360
5670 :
5680 SN$="YOU FIGHT WITH THE BUG AND THOUGH IT IS A HARD"
5690 SN$=SN$+" STRUGGLE YOU EVENTUALLY IRON IT OUT AND SURVIVE."
5700 GOTO5880
5730 :
5740 NN$=NN$+" ":LN=LEN(NN$):F=0:C=1
5745 FORK=1TOLN
5750 IFMID$(NN$,K,1)<>" "THENNEXTK:RETURN
5755 W$=MID$(NN$,C,K-C):C=K+1:LW=LEN(W$)
5760 FORJ=1TO8
5770 LI=LEN(IV$(J,1))
5780 FORI=1TOLI
5790 IFMID$(IV$(J,1),I,LW)=W$THENF=J:I=LI:J=8:K=LN
5800 NEXTI,J,K:RETURN
5830 :
5840 HF=0
5850 IFIV$(OV,2)="-1"THENHF=1
5860 RETURN
5880 :
5890 LC=0
5900 OC=1
5910 OW$=""
5920 LL=32
5930 SN$=SN$+" DUMMY "
5940 PRINT
5950 FORC=1TOLEN(SN$)
5960 LC=LC+1
5970 IFMID$(SN$,C,1)=" "THENGOSUB6020
5980 NEXTC
5990 PRINT
6000 RETURN
6020 :
6030 NW$=MID$(SN$,OC,C-OC+1)
6040 IFLC<LLTHENPRINTOW$;:GOTO6060
6050 PRINTOW$:LC=LEN(NW$)
6060 OC=C+1:OW$=NW$
6070 RETURN
6090 :
6110 DIMIV$(8,2),IC$(4),LN$(55),EX$(55)
6120 FORC=1TO8:READIV$(C,1),IV$(C,2)
6140 NEXTC
6180 C1=0:C2=0
6190 FORC=1TO54
6200 READLN$(C),EX$(C)
6210 C1=C1+VAL(LEFT$(EX$(C),4))
6220 C2=C2+VAL(RIGHT$(EX$(C),4))
6225 NEXTC
6240 READCA:IFCA<>C1THENPRINT"CHECKSUM 1  ERROR":STOP
6250 READCB:IFCB<>C2THENPRINT"CHECKSUM 2  ERROR":STOP
6260 RETURN
6280 DATAADDRESS NUMBER,45,KEY,34,LASER SHIELD,25
6282 DATATICKET TO TRI-STATE,26,DATA CREDIT CARD,28,DIGITAYA
6284 DATA30,CODE BOOK,19,BUFFER ACTIVATING DEVICE,13
6290 DATAIN THE TV OUTLET
6295 DATA00000000
6300 DATAIN THE USER PORT
6305 DATA00090100
6310 DATAIN THE CASSETTE PORT
6315 DATA00110000
6320 DATAIN THE JOYSTICK PORT
6325 DATA00130000
6330 DATAIN A TRI-STATE DEVICE
6335 DATA00170000
6340 DATAIN THE ARITHMETIC & LOGIC UNIT
6345 DATA00310016
6350 DATAAT THE GATEWAY TO MEMORY
6355 DATA00490000
6360 DATAON THE I/O HIGHWAY
6365 DATA09000001
6370 DATAON THE I/O HIGHWAY
6375 DATA10000802
6380 DATAON THE I/O HIGHWAY
6385 DATA11000900
6390 DATAON THE I/O HIGHWAY
6395 DATA12001003
6400 DATAON THE I/O HIGHWAY
6405 DATA13531100
6410 DATAON THE I/O HIGHWAY
6415 DATA14001204
6420 DATAON THE I/O HIGHWAY
6425 DATA15001300
6430 DATAON THE I/O HIGHWAY. A SIGN SAYS 'S OUT H'
6435 DATA00001400
6440 DATAIN THE DATA REGISTER
6445 DATA00061700
6450 DATAON AN 8 LANE HIGHWAY
6455 DATA16001805
6460 DATAON AN 8 LANE HIGHWAY
6465 DATA17001900
6470 DATAON AN 8 LANE HIGHWAY
6475 DATA18002000
6480 DATAON AN 8 LANE HIGHWAY
6485 DATA19292100
6490 DATAON AN 8 LANE HIGHWAY
6495 DATA20282200
6500 DATAON AN 8 LANE HIGHWAY
6505 DATA21272300
6510 DATAON AN 8 LANE HIGHWAY
6515 DATA22262400
6520 DATAON AN 8 LANE HIGHWAY
6525 DATA23250000
6530 DATAIN THE CHARACTER MATRIX
6535 DATA26360024
6540 DATAHIGH IN THE MEMORY
6545 DATA27352523
6550 DATAIN THE MIDDLE OF MEMORY
6555 DATA28342622
6560 DATAIN THE MIDDLE OF MEMORY
6565 DATA29332721
6570 DATALOW IN THE MEMORY
6575 DATA00542820
6580 DATAIN THE ACCUMULATOR'S LAIR
6585 DATA00000600
6590 DATAIN A LONG CORRIDOR
6595 DATA00420006
6600 DATAIN AN INDEX REGISTER
6605 DATA31000000
6610 DATALOW IN THE MEMORY
6615 DATA54403428
6620 DATAIN THE MIDDLE OF MEMORY
6625 DATA33393527
6630 DATAHIGH UP IN MEMORY
6635 DATA34383626
6640 DATAIN THE CHARACTER MATRIX
6645 DATA35370025
6650 DATAIN A RANDOM VECTOR TABLE
6655 DATA00000000
6660 DATAHIGH IN MEMORY OVERLOOKING A HIGHWAY
6665 DATA39003735
6670 DATAIN THE MIDDLE OF MEMORY
6675 DATA40003834
6680 DATAIN MEMORY - TO THE EAST IS A GATEWAY
6685 DATA41003933
6690 DATALOW IN MEMORY
6695 DATA00004054
6700 DATAIN A CORRIDOR
6705 DATA00430031
6710 DATAIN A CORRIDOR
6715 DATA00440042
6720 DATAIN A CORRIDOR
6725 DATA00004543
6730 DATAIN THE ADDRESS REGISTER
6735 DATA00004600
6740 DATAON A 16 LANE HIGHWAY
6745 DATA45004700
6750 DATAON A 16 LANE HIGHWAY
6755 DATA46004800
6760 DATAON A 16 LANE HIGHWAY
6765 DATA47004900
6770 DATAON A 16 LANE HIGHWAY. A LARGE GATE LOOMS TO THE WEST
6775 DATA48005007
6780 DATAON A 16 LANE HIGHWAY
6785 DATA49005100
6790 DATAON A 16 LANE HIGHWAY
6795 DATA50005200
6800 DATAON A 16 LANE HIGHWAY
6805 DATA51000000
6810 DATAIN A VECTOR TO MEMORY
6815 DATA00290012
6820 DATALOW IN MEMORY
6825 DATA00413329
6835 DATA100169,103973
8600 :
8610 NN$=NN$+" ":LN=LEN(NN$):C=1
8620 FORI=1TOLN
8630 IFMID$(NN$,I,1)<>" "THENNEXTI:RETURN
8640 W$=MID$(NN$,C,I-C):C=I+1
8650 IFW$="NORTH"ORW$="WEST"THENNN$=W$:I=LN
8660 IFW$="SOUTH"ORW$="EAST"THENNN$=W$:I=LN
8670 NEXTI:RETURN
