0 CLEAR900:DIMCC,ZZ,M$,Q$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Q$=MID$(M$,CC,1):IFQ$=" "ORQ$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFQ$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 REM LIGHTHOUSE ADVENTURE
10 REM CHRIS WILKINSON, 1982
20 CLS:PRINT:PRINT:PRINT"WELCOME TO LIGHTHOUSE ADVENTURE":PRINT:PRINT:PRINT
30 DIMD$(6),V$(22),V(22),O$(42),O(42),R$(21),R(21),D(21,6),VE(22),OB(42)
40 L=1:IN=0:C=0:F=0:T=0:R=0:FT=0:G=0
50 DATA ON A ROCKY PENINSULA-- TO THE NORTH I SEE AN OLD LIGHTHOUSE,ON A ROCKY PENINSULA
51 DATA ON THE GROUND FLOOR OF THE LIGHTHOUSE
60 DATA IN THE BEDROOM,AT THE TOP OF THE LIGHTHOUSE-- I SEE THE ROCKS DIRECTLY BELOW,IN THE GENERATOR ROOM
61 DATA IN A LONG CORRIDOR CARVED OUT OF THE ROCK
70 DATA IN A LARGE DINING HALL,IN THE GUARD ROOM,IN THE CHART ROOM,"IN THE CAPTAIN'S ROOM",IN THE STORE ROOM
71 DATA IN A METAL STORAGE CABINET,IN AN ENORMOUS VAULTED CHAMBER
80 DATA IN A SMALL PASSAGEWAY,ON A NARROW PATH-- TO THE WEST I SEE THE LAGOON,IN A LARGE ROOM,ON THE LAGOON
81 DATA IN THE MIDDLE OF THE LAGOON,ON THE LAGOON,ON A WIDE BEACH-- TO THE EAST I SEE THE LAGOON
90 DATA ROCKS,PIECE OF PAPER,GUARD-- HE WON'T LET ME PASS,ROPE CONNECTED TO THE WALL,CUP,BUTTON,FIREPLACE
91 DATA MAPS,PIRATES LOG,DEAD SMUGGLER,CLOSED COFFIN,COFFIN,SKELETON
100 DATA KNOB,LANTERN,RING (IT LOOKS MAGIC),BOTTLE,TOOL BOX,OIL,NOTE,WEAPON RACK,MUSKET,DAGGER,BULLETS
101 DATA TREASURE CHEST,FLUTE,DOOR TO THE NORTH,IT'S TOO DARK TOO SEE
110 DATA BOAT,A HUGE SEA SERPENT,TROLLEY,GENERATOR,PANEL,SWITCH,HOLE IN WALL,OPENING IN WALL,CORRIDOR LEADING DOWN
111 DATA PASSAGE LEADING UP,PULLEY SYSTEM,GOLD BULLION,FLINT,MARPLE
120 DATA GET,DROP,EXAMINE,READ,PULL,PUSH,SEARCH,OPEN,LIGHT,WEAR,DRINK,POUR,FILL,SHOOT,CUT,LOAD,PUT,PRAISE,PLAY
121 DATA SAIL,STAB,SAY
130 DATA 3,2,2,2,0,0,2,2,2,1,0,0,0,1,0,0,4,6,0,0,0,0,5,3,0,0,0,0,0,4,0,0,0,0,3,0,0,9,12,8,0,0,0,0,7,0,0,0,7,0,0,0,0,0
131 DATA 9,0,11,0,0,0,0,0,0,10,0,0
140 DATA 0,0,0,7,0,0,0,11,0,0,0,0,15,0,0,0,0,0,0,14,0,16,0,0,0,8,15,0,0,0,0,16,0,0,0,0,0,0,17,19,0,0,0,0,18,0,0,0,0,0
141 DATA 19,21,0,0,0,0,20,0,0,0
150 FORX=1TO21:READR$(X):NEXT:FORX=1TO42:READO$(X):NEXT:FORX=1TO22:READV$(X):NEXT
151 FORY=1TO21:FORX=1TO6:READD(Y,X):NEXTX:NEXTY
160 DATANORTH,SOUTH,EAST,WEST,UP,DOWN
170 FORX=1TO6:READD$(X):NEXT
180 DATA 1,4,9,9,0,0,8,10,0,8,11,0,0,0,7,13,13,12,0,0,12,12,12,0,14,15,16,17,0,0,21,6,0,0,6,5,0,0,0,0,0,0
190 FORX=1TO42:READO(X):NEXT
200 REM ******* BEGIN *******
210 PRINT:M$=R$(L):GOSUB1:PRINT
211 PRINT"OBVIOUS EXITS ARE: ":A$="":FORX=1TO6:IF D(L,X)<>0THENPRINTA$;D$(X);:A$=","
220 NEXTX:A=0
230 PRINT:PRINT:PRINT"I CAN SEE:":FORZ=1TO42:IFO(Z)=L THENPRINTO$(Z):A=1
240 NEXTZ:IFA=0THENPRINT"NOTHING OF INTEREST."
250 PRINT:INPUTI$
260 IFLEFT$(I$,3)<>"INV"THEN300
270 N$="NOTHING.":PRINT"I AM CARRYING:":FORX=1TO42:IFO(X)=-1THENPRINTO$(X):N$=""
280 NEXT:IFN$<>""THENPRINTN$
290 GOTO250
300 IFI$<>"LOOK"THEN310
301 CLS:GOTO210
310 IFLEN(I$)<>1THEN400
320 IFI$="N"ANDD(L,1)<>0THENL=D(L,1):GOTO210
330 IFI$="S"ANDD(L,2)<>0THENL=D(L,2):GOTO210
340 IFI$="E"ANDD(L,3)<>0THENL=D(L,3):GOTO210
350 IFI$="W"ANDD(L,4)<>0THENL=D(L,4):GOTO210
360 IFI$="U"ANDD(L,5)<>0THENL=D(L,5):GOTO210
370 IFI$="D"ANDD(L,6)<>0THENL=D(L,6):GOTO210
380 IFL=6ANDI$="U"THENM$="I CAN'T GET THE TROLLEY UP THE STAIRS. THE CHEST IS IN THE WAY.":GOSUB1:GOTO250
390 PRINT"YOU CAN'T GO THAT WAY.":GOTO250
400 V=0:OB=0:VE$=LEFT$(I$,3)
410 M=0:FORX=1TOLEN(I$):IFMID$(I$,X,1)=CHR$(32)THENM=X
420 NEXT
430 IFM<3ORM>200THENPRINT"I DON'T UNDERSTAND.":GOTO250
440 OB$=MID$(I$,M+1,3)
450 FORX=1TO22:IFLEFT$(V$(X),3)=VE$THENV=X
460 NEXT
470 FORX=1TO42:IFLEFT$(O$(X),3)=OB$THENOB=X
480 NEXT
490 IFOB$="SMU"THENOB=10
500 IFOB$="LOG"THENOB=9
510 IFOB$="BOX"THENOB=18
520 IFOB$="RAC"THENOB=21
530 IFOB$="CHE"THENOB=25
540 IFOB$="SER"THENOB=30
550 IFOB$="PAP"THENOB=2
560 IFOB$="NOT"THENOB=20
570 IFOB$="LIQ"THENOB=17
580 IFOB$="COF"ANDO(12)=0THENOB=11
590 IFV=0THENM$="I DON'T KNOW HOW TO "+I$:GOSUB1:GOTO250
600 ONVGOTO 610,700,740,1030,1100,1100,1210,1230,1310,1360,1400,1440,1550,1630,1740,1770,1800,1230,1850,1900,1930,1970
610 IFO(OB)<>LTHENPRINT"I DON'T SEE IT HERE.":GOTO250
620 IFIN>5THENPRINT"I CAN'T CARRY ANYMORE.":GOTO250
630 IFOB=15ANDM=1THENO(29)=17:PRINT"TAKEN.":IN=IN+1:GOTO250
640 IFOB=6OROB=14OROB=21OROB=27OROB=28THENPRINT"I CAN'T GET THAT.":GOTO250
641 IFOB=32OROB=33OROB=34OROB=35OROB=36OROB=37OROB=38OROB=39OROB=42THENPRINT"I CAN'T GET THAT.":GOTO250
650 IFOB=70OROB=11OROB=120OROB=25OROB=29THENPRINT"IT'S FAR TOO HEAVY.":GOTO250
660 IFOB=3OROB=30THENPRINT"I DON'T THINK THAT'S POSSIBLE.":GOTO250
670 IFOB=4THENM$="I CAN'T GET IT-- I THINK IT'S CONNECTED TO SOMETHING.":GOSUB1:GOTO250
680 IFOB=40THENPRINT:M$="CONGRATULATIONS! WE'VE DONE IT!":GOSUB1:GOTO2000
690 O(OB)=-1:IN=IN+1:PRINT"TAKEN.":GOTO250
700 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
710 IFOB=15THEND(17,4)=0:O(29)=0
720 IFOB=25ANDL=5THENM$="THE CHEST CRASHES DOWN TO THE ROCKS BELOW AND SMASHES OPEN!":GOSUB1:O(25)=1:O(40)=1:GOTO250
730 O(OB)=L:IN=IN-1:PRINT"DROPPED.":GOTO250
740 IFO(OB)=LORO(OB)=-1THEN750
741 PRINT"I DON'T SEE IT HERE.":GOTO250
750 IFOB=1THENM$="I SEE A FEW TYPES OF STONES-- INCLUDING FLINT.":GOSUB1:O(41)=O(1):GOTO250
760 IFOB=9OROB=20THENM$="THERE'S SOMETHING WRITTEN ON IT.":GOSUB1:GOTO250
770 IFOB=3THENM$="LOOKS LIKE A TOUGH GUY. HE SAYS, 'WHAT'S THE PASSWORD?'":GOSUB1:GOTO250
780 IFOB=4THENM$="IT LOOKS LIKE A BELL-PULL.":GOSUB1:GOTO250
790 GOTO2010
800 IFOB=8THENM$="I SEE RATHER BADLY EXECUTED PICTURES OF ISLANDS WITH LARGE X'S DRAWN ON THEM.":GOSUB1:GOTO250
810 IFOB=10THENPRINT"HE'S DEAD ALRIGHT!":GOTO250
820 IFOB=12ANDO(13)=0THENPRINT"I SEE A SKELETON IN IT.":O(13)=L:GOTO250
830 IFOB=12THENM$="I SEE A KNOB ON THE BOTTOM OF THE COFFIN.":GOSUB1:O(14)=L:GOTO250
840 IFOB=13ANDO(9)=0THENPRINT"I'VE FOUND SOMETHING.":O(9)=L:GOTO250
850 IFOB=15ANDC=0THENPRINT"THERE'S NO OIL IN IT.":GOTO250
860 IFOB=15THENM$="IT LOOKS LIKE AN ANTIQUE.":GOSUB1:GOTO250
870 IFOB=16THENM$="I SEE HIEROGLYPHICS AROUND IT.":GOSUB1:GOTO250
880 IFOB=17THENPRINT"THERE'S A LIQUID IN IT.":O$(17)="BOTTLE WITH LIQUID":GOTO250
890 IFOB=22ANDF=0THENPRINT"IT'S LOADED.":GOTO250
900 IFOB=22THENPRINT"IT'S EMPTY.":GOTO250
910 IFOB=25THENPRINT"IT LOOKS VERY UTILITARIAN.":GOTO250
920 IFOB=26THENPRINT"IT LOOKS LIKE A GOOD ONE.":GOTO250
930 IFOB=27THENPRINT"IT'S LOCKED.":GOTO250
940 IFOB=29THENPRINT"IT'S BIG ENOUGH FOR TWO OF ME":GOTO250
950 IFOB=30THENPRINT"IT LOOKS TERRIBLY FEROCIOUS!":GOTO250
960 IFOB=32ANDO(34)=0THENPRINT"I SEE SOMETHING!":O(34)=L:GOTO250
970 IFOB=32ANDO(33)=0THENPRINT"I SEE SOMETHING!":O(33)=L:GOTO250
980 IFOB=35THENPRINT"I SEE A PULLEY SYSTEM.":O(39)=L:GOTO250
990 IFOB=36THENPRINT"I CAN SEE DOWN A LONG WAY.":GOTO250
1000 IFOB=39THENPRINT"I THINK IT'S A DUMB WAITER.":GOTO250
1010 IFOB=40THENPRINT"MUST BE WORTH BILLIONS!":GOTO250
1020 PRINT"I SEE NOTHING SPECIAL.":GOTO250
1030 IFOB=2OROB=8OROB=9OROB=16OROB=20THEN1040
1031 PRINT"HOW CAN I READ THAT?":GOTO250
1040 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1050 IFOB<>2THEN1060
1051 PRINT"IT READS:"
1052 PRINT"MUSIC IS PLAYED":PRINT"BEAUTY IS WORN":PRINT"CHARM THE BEAST":PRINT"OR ELSE YOU'RE GONE.":GOTO250
1060 IFOB=8THENM$="I THINK IT'S SPANISH-- I CAN'T READ SPANISH. SORRY...":GOSUB1:GOTO250
1070 IFOB<>9THEN1080
1071 PRINT"THE LAST PAGE READS:"
1072 M$="'THE SERPENT'S TAKEN ALL BUT ME. I, CAPTAIN JAMES, ONE-TIME LEADER OF PIRATES IN THE LAND...'":GOSUB1
1073 PRINT:PRINT"THE SCRAWL FADES INTO NOTHING.":GOTO250
1080 IFOB=16THENM$="I'M SORRY BUT I CAN'T TRANSLATE.":GOSUB1:GOTO250
1090 IFOB=20THENM$="THERE'S ONE WORD WRITTEN ON IT: MARPLE":GOSUB1:GOTO250
1100 IFO(OB)=LORO(OB)=-1THEN1110
1101 PRINT"I DON'T SEE IT HERE.":GOTO250
1110 IFOB=4THENPRINT"NOTHING SEEMED TO HAPPEN.":IFR=0THEND(7,5)=0:O(38)=0:R=1:GOTO250
1120 IFOB=4THEND(7,5)=6:O(38)=7:R=0:GOTO250
1130 IFOB=6THEND(8,1)=16:M$="A SECRET PANEL SLIDES OPEN.":GOSUB1:GOTO250
1140 IFOB=14THEND(11,1)=13:M$="A HIDDEN DOOR OPENS.":GOSUB1:GOTO250
1150 IFOB=31THENPRINT"OK.":GOTO250
1160 IFOB=33THENO(38)=7:O(37)=6:D(6,6)=7:D(7,5)=6:M$="THE PANEL OPENS.":GOSUB1:GOTO250
1170 IFOB=34THENPRINT"NOTHING HAPPENS.":GOTO250
1180 IFOB<>39THEN1190
1181 M$="THE PLATFORM STARTS TO RISE.":GOSUB1:IFT=1THEN1190
1182 IFOB=39THEN250
1190 IFOB=39THENM$="THE TREASURE CHEST MOVES SLOWLY UPWARDS.":GOSUB1:O(25)=5:GOTO250
1200 PRINT"I CAN'T ";I$:GOTO250
1210 IFOB=10ANDO(10)=LTHENPRINT"I FOUND A FOLDED NOTE!":O(20)=L:GOTO250
1220 GOTO740
1230 IFO(OB)=LORO(OB)=-1THEN1240
1231 PRINT"I DON'T SEE IT HERE.":GOTO250
1240 IFOB=11THENPRINT"OK-- I'VE OPENED IT.":O(11)=0:O(12)=11:GOTO250
1250 IFOB=17THENPRINT"IT'S OPEN.":GOTO250
1260 IFOB=18THENPRINT"I SEE A CAN OF OIL.":O(19)=L:GOTO250
1270 IFOB=21THENPRINT"I SEE SOME BULLETS.":O(24)=L:GOTO250
1280 IFOB=27THENPRINT"IT'S COMPLETELY JAMMED.":GOTO250
1290 IFOB=25THENPRINT"IT WON'T OPEN.":GOTO250
1300 M$="I CAN'T "+I$:GOSUB1:GOTO250
1310 IFOB<>15THENPRINT"I CAN'T LIGHT THAT.":GOTO250
1320 IFO(15)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1330 IFC=0THENPRINT"THERE'S NO OIL IN IT.":GOTO250
1340 IFO(41)<>-1THENM$="I DON'T HAVE ANYTHING TO LIGHT IT WITH.":GOSUB1:GOTO250
1350 PRINT"LANTERN IS LIT.":M=1:O(29)=17:O(28)=0:GOTO250
1360 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1370 IFOB<>16THENPRINT"I CAN'T WEAR THAT!":GOTO250
1380 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1390 PRINT"IT FITS WELL.":G=1:GOTO250
1400 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1410 IFOB=19THENM$="YUCH-- IT TASTE'S AWFUL.":GOSUB1:GOTO250
1420 IFOB=17THENM$="AAARRRGGGHHH... I'M DEAD.":GOSUB1:GOTO2000
1430 PRINT"I CAN'T DO THAT.":GOTO250
1440 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1450 IFOB<>19THEN1490
1460 IFOB=19THENINPUT"IN OR ON WHAT";P$:IFP$="LANTERN"ANDO(15)=-1THENPRINT"LANTERN IS NOW FILLED.":C=1:GOTO250
1470 IFP$="LANTERN" AND O(15)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1480 PRINT"OK. BUT I DON'T SEE WHY.":GOTO250
1490 IFOB<>17THEN1540
1500 INPUT"IN OR ON WHAT";P$:IFP$="DOOR"ANDL=16THENPRINT"THE DOOR DISINTEGRATES.":O(27)=0:D(16,1)=17:O(17)=0:GOTO250
1510 IFP$="CUP"ANDO(5)=-1THENM$="THE CUP DISSOLVES BEFORE YOUR VERY EYES.":GOSUB1:O(5)=0:GOTO250
1520 PRINT"I DON'T HAVE IT.":GOTO250
1530 PRINT"NOTHING HAPPENS.":GOTO250
1540 IFOB=19THEN1800
1541 M$="HOW CAN I "+I$:GOSUB1:GOTO250
1550 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1560 P$="":IFOB=15THENINPUT"WITH WHAT";P$:IF P$="OIL"ANDO(19)=-1THENPRINT"THE LANTERN IS FILLED.":C=1:O(19)=0:GOTO250
1570 IFP$="BOTTLE"THENM$="THAT DOESN'T SEEM RIGHT TO ME.":GOSUB1:GOTO250
1580 IFOB=15THENPRINT"I DON'T THINK I CAN.":GOTO250
1590 IFOB=5THENINPUT"WITH WHAT";P$:IFP$="OIL"THENPRINT"OK.":O(19)=L:GOTO250
1600 IFP$="BOTTLE"THENPRINT"THE CUP SLOWLY DISSOLVES.":O(5)=0:GOTO250
1610 IFOB=19THENINPUT"INTO WHAT";P$:IFLEFT$(P$,3)="LAN"THENPRINT"OK.":C=1:GOTO250
1620 PRINT"HOW CAN I ";I$:GOTO250
1630 IFO(22)<>-1THENPRINT"I DON'T HAVE A MUSKET.":GOTO250
1640 IFOB<>27THEN1680
1650 IFF=1THENPRINT"THE MUSKET IS EMPTY.":GOTO250
1660 IFF=0ANDFT=0THENPRINT"YOU MISSED.":F=1:FT=1:GOTO250
1670 IFF=0ANDFT=1THENPRINT"YOU SHOT DOWN THE DOOR.":F=1:D(16,1)=17:O(27)=0:GOTO250
1680 IFOB<>3THEN1710
1690 IFF=1THENPRINT"THE MUSKET IS EMPTY.":GOTO250
1691 M$="THE BULLET PASSED RIGHT THROUGH HIM. I THINK HE'S A GHOST.":GOSUB1:F=1
1700 GOTO250
1710 IFOB=30THENM$="BULLETS HAVE NO EFFECT ON A SERPENT THIS SIZE!":GOSUB1:GOTO250
1720 IFF=0THENPRINT"NO EFFECT.":GOTO250
1730 PRINT"THE MUSKET IS EMPTY.":GOTO250
1740 IFO(OB)<>LTHENPRINT"I DON'T SEE IT.":GOTO250
1750 IFOB=4OROB=39THENM$="IT SEEMS TO BE A VERY STRONG ROPE. I CAN'T CUT IT.":GOSUB1:GOTO250
1760 M$="I CAN'T "+I$:GOSUB1:GOTO250
1770 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1780 IFOB=22ANDO(24)=-1THENM$="THE MUSKET IS NOW LOADED.":GOSUB1:F=0:GOTO250
1790 M$="I CAN'T "+I$:GOSUB1:GOTO250
1800 IFO(OB)=LORO(OB)=-1THEN1810
1801 PRINT"I DON'T SEE IT.":GOTO250
1810 P$="":IFOB<>25THEN1550
1811 INPUT"ON OR IN WHAT";P$
1820 IFLEFT$(P$,3)="TRO"THENPRINT"OK.":O(25)=-1:D(6,5)=0:GOTO250
1830 IFP$="HOLE"ANDL=6ANDO(25)=-1THENPRINT"OK.":D(6,5)=3:T=1:O(25)=6:GOTO250
1840 PRINT"I CAN'T.":GOTO250
1850 IFOB<>26THENPRINT"I CAN'T PLAY THAT!":GOTO250
1860 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO250
1870 IFL=18ORL=19ORL=20THEN1880
1871 PRINT"IT SOUNDS BEAUTIFUL.":GOTO250
1880 IFG=1THENM$="THE MONSTER SMILES HAPPILY AND DISAPPEARS.":GOSUB1:D(19,4)=20:O(30)=0:GOTO250
1890 M$="THE SERPENT OPEN HIS MOUTH AND... AAAAA...":GOSUB1:GOTO2000
1900 IFOB<>29THENPRINT"I CAN'T ";I$:GOTO250
1910 IFO(29)=0THENPRINT"WHAT BOAT?":GOTO250
1920 IFL=17THENL=18:O(30)=19:GOTO210
1930 IFO(23)<>-1THENM$="I DON'T HAVE A SUITABLE OBJECT TO DO THAT.":GOSUB1:GOTO250
1940 IFOB=30THENM$="I'M AFRAID HE DID'T LIKE THAT... OOOOOOOO...":GOSUB1:GOTO2000
1950 IFOB=3THENM$="HOW MURDEROUS. UNFORTUNATELY IT DID NO GOOD.":GOSUB1:GOTO250
1960 PRINT"DON'T BE RIDICULOUS!":GOTO250
1970 IFL<>9THENM$="OK: '"+MID$(I$,M+1,LEN(I$)-M)+"'":GOSUB1:GOTO250
1980 IFOB<>42THENPRINT"NOTHING HAPPENED.":GOTO250
1990 IFOB=42THENM$="HE SMILES AND SAYS, 'GOOD TO SEE YOU AGAIN, CAP'N.'... AND DISAPPEARS!":GOSUB1:O(3)=0:D(9,2)=10:GOTO250
2000 END
2010 IFO(6)<>LANDOB=7THENPRINT"I SEE SOMETHING!":GOTO2020
2011 GOTO800
2020 IFO(5)=0THENO(5)=L:GOTO250
2030 IFO(6)=0THENO(6)=L:GOTO250
2040 GOTO800
