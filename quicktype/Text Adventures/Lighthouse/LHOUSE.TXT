1 REM LIGHTHOUSE ADVENTURE
2 REM CHRIS WILKINSON, 1982
8 CLS:PRINT:PRINT:PRINT"WELCOME TO LIGHTHOUSE ADVENTURE":PRINT:PRINT:PRINT
9 DIMD$(6),V$(22),V(22),O$(42),O(42),R$(21),R(21),D(21,6),VE(22),OB(42)
10 L=1:IN=0:C=0:F=0:T=0:R%=0:FT=0:G=0
11 DATA ON A ROCKY PENINSULA - TO THE NORTH I   SEE AN OLD LIGHTHOUSE,ON A ROCKY PENINSULA,ON THE GROUND FLOOR OF THE LIGHTHOUSE
12 DATA IN THE BEDROOM,AT THE TOP OF THE LIGHTHOUSE - I SEE THEROCKS DIRECTLY BELOW,IN THE GENERATOR ROOM,IN A LONG CORRIDOR CARVED OUT OF        THE ROCK
13 DATAIN A LARGE DINING HALL,IN THE GUARD ROOM,IN THE CHART ROOM,IN THE CAPTAINS ROOM,IN THE STORE ROOM,IN A METAL STORAGE CABINET,IN AN ENORMOUS VAULTED CHAMBER
14 DATAIN A SMALL PASSAGEWAY,ON A NARROW PATH - TO THE WEST I SEE THELAGOON,IN A LARGE ROOM,ON THE LAGOON,IN THE MIDDLE OF THE LAGOON,ON THE LAGOON,ON A WIDE BEACH - TO THE EAST I SEE THE LAGOON
15 DATAROCKS,PIECE OF PAPER,GUARD - HE WON'T LET ME PASS,ROPE CONNECTED TO THE WALL,CUP,BUTTON,FIREPLACE,MAPS,PIRATES LOG,DEAD SMUGGLER,CLOSED COFFIN,COFFIN,SKELETON
16 DATAKNOB,LANTERN,RING(IT LOOKS MAGIC),BOTTLE,TOOL BOX,OIL,NOTE,WEAPON RACK,MUSKET,DAGGER,BULLETS,TREASURE CHEST,FLUTE,DOOR TO THE NORTH,IT'S TOO DARK TOO SEE
17 DATABOAT,A HUGE SEA SERPENT,TROLLEY,GENERATOR,PANEL,SWITCH,HOLE IN WALL,OPENING IN WALL,CORRIDOR LEADING DOWN,PASSAGE LEADING UP,PULLEY SYSTEM,GOLD BULLION,FLINT,MARPLE
18 DATA GET,DROP,EXAMINE,READ,PULL,PUSH,SEARCH,OPEN,LIGHT,WEAR,DRINK,POUR,FILL,SHOOT,CUT,LOAD,PUT,PRAISE,PLAY,SAIL,STAB,SAY
19 DATA 3,2,2,2,0,0,2,2,2,1,0,0,0,1,0,0,4,6,0,0,0,0,5,3,0,0,0,0,0,4,0,0,0,0,3,0,0,9,12,8,0,0,0,0,7,0,0,0,7,0,0,0,0,0,9,0,11,0,0,0,0,0,0,10,0,0
20 DATA 0,0,0,7,0,0,0,11,0,0,0,0,15,0,0,0,0,0,0,14,0,16,0,0,0,8,15,0,0,0,0,16,0,0,0,0,0,0,17,19,0,0,0,0,18,0,0,0,0,0,19,21,0,0,0,0,20,0,0,0
21 FORX=1TO21:READR$(X):NEXT:FORX=1TO42:READO$(X):NEXT:FORX=1TO22:READV$(X):NEXT:FORY=1TO21:FORX=1TO6:READD(Y,X):NEXTX:NEXTY
22 DATANORTH,SOUTH,EAST,WEST,UP,DOWN
23 FORX=1TO6:READD$(X):NEXT
24 DATA 1,4,9,9,0,0,8,10,0,8,11,0,0,0,7,13,13,12,0,0,12,12,12,0,14,15,16,17,0,0,21,6,0,0,6,5,0,0,0,0,0,0
25 FORX=1TO42:READO(X):NEXT
26 REM      ******* BEGIN *******
27 PRINT:PRINTR$(L):PRINT:PRINT"OBVIOUS EXITS ARE: ";:A$="":FORX=1TO6:IF D(L,X)<>0THENPRINTA$;D$(X);:A$=","
28 NEXTX:A=0
29 PRINT:PRINT:PRINT"I CAN SEE:":FORZ=1TO42:IFO(Z)=L THENPRINTO$(Z):A=1
30 :NEXTZ:IF A=0 THEN PRINT"NOTHING OF INTEREST."
31 PRINT:INPUT":"I$
32 IFLEFT$(I$,3)<>"INV"THEN36
33 N$="NOTHING.":PRINT"I AM CARRYING:":FORX=1TO42:IFO(X)=-1THENPRINTO$(X):N$=""
34 NEXT:IFN$<>""THENPRINTN$
35 GOTO31
36 IFI$<>"LOOK"THEN37ELSECLS:GOTO27
37 IFLEN(I$)<>1THEN46
38 IFI$="N"ANDD(L,1)<>0THENL=D(L,1):GOTO27
39 IFI$="S"ANDD(L,2)<>0THENL=D(L,2):GOTO27
40 IFI$="E"ANDD(L,3)<>0THENL=D(L,3):GOTO27
41 IFI$="W"ANDD(L,4)<>0THENL=D(L,4):GOTO27
42 IFI$="U"ANDD(L,5)<>0THENL=D(L,5):GOTO27
43 IFI$="D"ANDD(L,6)<>0THENL=D(L,6):GOTO27
44 IFL=6 AND I$="U" THENPRINT"I CAN'T GET THE TROLLEY UP THE STAIRS."''"THE CHEST IS IN THE WAY.":GOTO31
45 PRINT"YOU CAN'T GO THAT WAY.":GOTO31
46 V=0:OB=0:VE$=LEFT$(I$,3)
47 M=0:FORX=1TOLEN(I$):IFMID$(I$,X,1)=CHR$(32)THENM=X
48 NEXT
49 IFM<3ORM>200THENPRINT"I DON'T UNDERSTAND.":GOTO31
50 OB$=MID$(I$,M+1,3)
51 FORX=1TO22:IFLEFT$(V$(X),3)=VE$THENV=X
52 NEXT
53 FORX=1TO42:IFLEFT$(O$(X),3)=OB$THEN OB=X
54 NEXT
55 IFOB$="SMU" THEN OB=10
56 IFOB$="LOG" THEN OB=9
57 IFOB$="BOX" THEN OB=18
58 IFOB$="RAC" THEN OB=21
59 IFOB$="CHE" THEN OB=25
60 IFOB$="SER" THEN OB=30
61 IFOB$="PAP" THEN OB=2
62 IFOB$="NOT" THEN OB=20
63 IFOB$="LIQ" THEN OB=17
64 IFOB$="COF" AND O(12)=0 THEN OB=11
65 IFV=0THENPRINT"I DON'T KNOW HOW TO ";I$:GOTO31
66 ON V GOTO 67,76,80,109,116,116,127,129,137,142,146,150,161,169,180,183,186,129,191,196,199,203
67 IFO(OB)<>L THEN PRINT"I DON'T SEE IT HERE.":GOTO31
68 IFIN>5THENPRINT"I CAN'T CARRY ANYMORE.":GOTO31
69 IFOB=15ANDM=1THENO(29)=17:PRINT"TAKEN.":IN=IN+1:GOTO31
70 IFOB=6OROB=14OROB=21OROB=27OROB=28OROB=32OROB=33OROB=34OROB=35OROB=36OROB=37OROB=38OROB=39OROB=42THENPRINT"I CAN'T GET THAT.":GOTO31
71 IFOB=70OROB=11OROB=120OROB=25OROB=29THENPRINT"IT'S FAR TOO HEAVY.":GOTO31
72 IFOB=3OROB=30THENPRINT"I DON'T THINK THAT'S POSSIBLE.":GOTO31
73 IFOB=4THENPRINT"I CAN'T GET IT - I THINK IT'S CONNECTED TO SOMETHING.":GOTO31
74 IFOB=40THENPRINT:PRINT"CONGRATULATIONS! WE'VE DONE IT!":END
75 O(OB)=-1:IN=IN+1:PRINT"TAKEN.":GOTO31
76 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO31
77 IFOB=15THEND(17,4)=0:O(29)=0
78 IFOB=25 AND L=5 THEN PRINT"THE CHEST CRASHES DOWN TO THE ROCKS"''"BELOW AND SMASHES OPEN!":O(25)=1:O(40)=1:GOTO31
79 O(OB)=L:IN=IN-1:PRINT"DROPPED.":GOTO31
80 IFO(OB)=L OR O(OB)=-1THEN81 ELSE PRINT"I DON'T SEE IT HERE.":GOTO31
81 IFOB=1THENPRINT"I SEE A FEW TYPES OF STONES - INCLUDING FLINT.":O(41)=O(1):GOTO31
82 IFOB=9OROB=20THENPRINT"THERE'S SOMETHING WRITTEN ON IT.":GOTO31
83 IFOB=3THENPRINT"LOOKS LIKE A TOUGH GUY. HE SAYS, ""WHAT'STHE PASSWORD?""":GOTO31
84 IFOB=4THENPRINT"IT LOOKS LIKE A BELL-PULL.":GOTO31
85 GOTO207
86 IFOB=8THENPRINT"I SEE RATHER BADLY EXECUTED PICTURES OF ISLANDS WITH LARGE X'S DRAWN ON THEM.":GOTO31
87 IFOB=10THENPRINT"HE'S DEAD ALRIGHT!":GOTO31
88 IFOB=12ANDO(13)=0THENPRINT"I SEE A SKELETON IN IT.":O(13)=L:GOTO31
89 IFOB=12THENPRINT"I SEE A KNOB ON THE BOTTOM OF THE"''"COFFIN.":O(14)=L:GOTO31
90 IFOB=13ANDO(9)=0THENPRINT"I'VE FOUND SOMETHING.":O(9)=L:GOTO31
91 IFOB=15ANDC=0THENPRINT"THERE'S NO OIL IN IT.":GOTO31
92 IFOB=15THENPRINT"IT LOOKS LIKE AN ANTIQUE.":GOTO31
93 IFOB=16THENPRINT"I SEE HIEROGLYPHICS AROUND IT.":GOTO31
94 IFOB=17THENPRINT"THERE'S A LIQUID IN IT.":O$(17)="BOTTLE WITH LIQUID":GOTO31
95 IFOB=22ANDF=0THENPRINT"IT'S LOADED.":GOTO31
96 IFOB=22THENPRINT"IT'S EMPTY.":GOTO31
97 IFOB=25THENPRINT"IT LOOKS VERY UTILITARIAN.":GOTO31
98 IFOB=26THENPRINT"IT LOOKS LIKE A GOOD ONE.":GOTO31
99 IFOB=27THENPRINT"IT'S LOCKED.":GOTO31
100 IFOB=29THENPRINT"IT'S BIG ENOUGH FOR TWO OF ME":GOTO31
101 IFOB=30THENPRINT"IT LOOKS TERRIBLY FEROCIOUS!":GOTO31
102 IFOB=32ANDO(34)=0THENPRINT"I SEE SOMETHING!":O(34)=L:GOTO31
103 IFOB=32ANDO(33)=0THENPRINT"I SEE SOMETHING!":O(33)=L:GOTO31
104 IFOB=35THENPRINT"I SEE A PULLEY SYSTEM.":O(39)=L:GOTO31
105 IFOB=36THENPRINT"I CAN SEE DOWN A LONG WAY.":GOTO31
106 IFOB=39THENPRINT"I THINK IT'S A DUMB WAITER.":GOTO31
107 IFOB=40THENPRINT"MUST BE WORTH BILLIONS!":GOTO31
108 PRINT"I SEE NOTHING SPECIAL.":GOTO31
109 IFOB=2OROB=8OROB=9OROB=16OROB=20THEN110 ELSE PRINT"HOW CAN I READ THAT?":GOTO31
110 IFO(OB)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
111 IFOB=2THENPRINT"IT READS:":PRINT"MUSIC IS PLAYED":PRINT"BEAUTY IS WORN":PRINT"CHARM THE BEAST":PRINT"OR ELSE YOU'RE GONE.":GOTO31
112 IFOB=8THENPRINT"I THINK IT'S SPANISH - I CAN'T READ"''"SPANISH. SORRY...":GOTO31
113 IFOB=9THENPRINT"THE LAST PAGE READS: ""THE SERPENT'S"''"TAKEN ALL BUT ME. I, CAPTAIN JAMES, ONE-TIME LEADER OF PIRATES IN THE LAND...""":PRINT:PRINT"THE SCRAWL FADES INTO NOTHING.":GOTO31
114 IFOB=16THENPRINT"I'M SORRY BUT I CAN'T TRANSLATE.":GOTO31
115 IFOB=20THENPRINT"THERE'S ONE WORD WRITTEN ON IT: MARPLE":GOTO31
116 IFO(OB)=L ORO(OB)=-1THEN117ELSEPRINT"I DON'T SEE IT HERE.":GOTO31
117 IFOB=4THENPRINT"NOTHING SEEMED TO HAPPEN.":IFR%=0THEND(7,5)=0:O(38)=0:R%=1:GOTO31
118 IFOB=4THEND(7,5)=6:O(38)=7:R%=0:GOTO31
119 IFOB=6THENPRINT"A SECRET PANEL SLIDES OPEN.":D(8,1)=16:GOTO31
120 IFOB=14THENPRINT"A HIDDEN DOOR OPENS.":D(11,1)=13:GOTO31
121 IFOB=31THENPRINT"OK.":GOTO31
122 IFOB=33THENPRINT"THE PANEL OPENS.":O(38)=7:O(37)=6:D(6,6)=7:D(7,5)=6:GOTO31
123 IFOB=34THENPRINT"NOTHING HAPPENS.":GOTO31
124 IFOB=39THENPRINT"THE PLATFORM STARTS TO RISE.":IFT=1THEN125 ELSE IFOB=39THENGOTO31
125 IFOB=39THENPRINT"THE TREASURE CHEST MOVES SLOWLY UPWARDS.":O(25)=5:GOTO31
126 PRINT"I CAN'T ";I$:GOTO31
127 IFOB=10ANDO(10)=L THEN PRINT"I FOUND A FOLDED NOTE!":O(20)=L:GOTO31
128 GOTO80
129 IFO(OB)=L OR O(OB)=-1 THEN 130 ELSEPRINT"I DON'T SEE IT HERE.":GOTO31
130 IFOB=11THENPRINT"OK - I'VE OPENED IT.":O(11)=0:O(12)=11:GOTO31
131 IFOB=17THENPRINT"IT'S OPEN.":GOTO31
132 IFOB=18THENPRINT"I SEE A CAN OF OIL.":O(19)=L:GOTO31
133 IFOB=21THENPRINT"I SEE SOME BULLETS.":O(24)=L:GOTO31
134 IFOB=27THENPRINT"IT'S COMPLETELY JAMMED.":GOTO31
135 IFOB=25THENPRINT"IT WON'T OPEN.":GOTO31
136 PRINT"I CAN'T ";I$:GOTO31
137 IFOB<>15 THENPRINT"I CAN'T LIGHT THAT.":GOTO31
138 IFO(15)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
139 IFC=0THENPRINT"THERE'S NO OIL IN IT.":GOTO31
140 IFO(41)<>-1 THENPRINT"I DON'T HAVE ANYTHING TO LIGHT IT WITH.":GOTO31
141 PRINT"LANTERN IS LIT.":M=1:O(29)=17:O(28)=0:GOTO31
142 IFO(OB)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
143 IFOB<>16 THENPRINT"I CAN'T WEAR THAT!":GOTO31
144 IFO(OB)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
145 PRINT"IT FITS WELL.":G=1:GOTO31
146 IFO(OB)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
147 IFOB=19THENPRINT"YUCH - IT TASTE'S AWFUL.":GOTO31
148 IFOB=17THENPRINT"AAARRRGGGHHH................. I'M DEAD.":GOTO206
149 PRINT"I CAN'T DO THAT.":GOTO31
150 IF O(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO31
151 IFOB<>19 THEN 155
152 IFOB=19 THEN INPUT"IN OR ON WHAT";P$:IFP$="LANTERN" AND O(15)=-1 THENPRINT"LANTERN IS NOW FILLED.":C=1:GOTO31
153 IFP$="LANTERN" AND O(15)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
154 PRINT"OK. BUT I DON'T SEE WHY.":GOTO31
155 IFOB<>17THEN160
156 INPUT"IN OR ON WHAT";P$:IF P$="DOOR"ANDL=16THENPRINT"THE DOOR DISINTEGRATES.":O(27)=0:D(16,1)=17:O(17)=0:GOTO31
157 IFP$="CUP"AND O(5)=-1 THENPRINT"THE CUP DISSOLVES BEFORE YOUR VERY EYES.":O(5)=0:GOTO31
158 PRINT"I DON'T HAVE IT.":GOTO31
159 PRINT"NOTHING HAPPENS.":GOTO31
160 IFOB=19THEN186 ELSEPRINT"HOW CAN I ";I$:GOTO31
161 IFO(OB)<>-1 THENPRINT"I DON'T HAVE IT.":GOTO31
162 P$="":IFOB=15THEN INPUT"WITH WHAT";P$:IF P$="OIL"ANDO(19)=-1 THENPRINT"THE LANTERN IS FILLED.":C=1:O(19)=0:GOTO31
163 IFP$="BOTTLE"THENPRINT"THAT DOESN'T SEEM RIGHT TO ME.":GOTO31
164 IFOB=15THENPRINT"I DON'T THINK I CAN.":GOTO31
165 IFOB=5THENINPUT"WITH WHAT";P$:IFP$="OIL"THENPRINT"OK.":O(19)=L:GOTO31
166 IFP$="BOTTLE"THENPRINT"THE CUP SLOWLY DISSOLVES.":O(5)=0:GOTO31
167 IFOB=19THENINPUT"INTO WHAT";P$:IFLEFT$(P$,3)="LAN"THENPRINT"OK.":C=1:GOTO31
168 PRINT"HOW CAN I ";I$:GOTO31
169 IFO(22)<>-1THENPRINT"I DON'T HAVE A MUSKET.":GOTO31
170 IFOB<>27THEN174
171 IFF=1THENPRINT"THE MUSKET IS EMPTY.":GOTO31
172 IFF=0ANDFT=0THENPRINT"YOU MISSED.":F=1:FT=1:GOTO31
173 IFF=0ANDFT=1THENPRINT"YOU SHOT DOWN THE DOOR.":F=1:D(16,1)=17:O(27)=0:GOTO31
174 IFOB<>3THEN177
175 IFF=1THENPRINT"THE MUSKET IS EMPTY."ELSEPRINT"THE BULLET PASSED RIGHT THROUGH HIM. I"''"THINK HE'S A GHOST.":F=1
176 GOTO31
177 IFOB=30THENPRINT"BULLETS HAVE NO EFFECT ON A SERPENT THISSIZE!";GOTO3
178 IFF=0THENPRINT"NO EFFECT.":GOTO31
179 PRINT"THE MUSKET IS EMPTY.":GOTO31
180 IFO(OB)<>L THENPRINT"I DON'T SEE IT.":GOTO31
181 IFOB=4OROB=39 THENPRINT"IT SEEMS TO BE A VERY STRONG ROPE. I"''"CAN'T CUT IT.":GOTO31
182 PRINT"I CAN'T ";I$:GOTO31
183 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO31
184 IFOB=22 ANDO(24)=-1 THENPRINT"THE MUSKET IS NOW LOADED.":F=0:GOTO31
185 PRINT"I CAN'T ";I$:GOTO31
186 IFO(OB)=L OR O(OB)=-1 THEN187ELSEPRINT"I DON'T SEE IT.":GOTO31
187 P$="":IFOB<>25THEN161ELSEINPUT"ON OR IN WHAT";P$
188 IFLEFT$(P$,3)="TRO"THENPRINT"OK.":O(25)=-1:D(6,5)=0:GOTO31
189 IFP$="HOLE"ANDL=6ANDO(25)=-1THENPRINT"OK.":D(6,5)=3:T=1:O(25)=6:GOTO31
190 PRINT"I CAN'T.":GOTO31
191 IFOB<>26THENPRINT"I CAN'T PLAY THAT!":GOTO31
192 IFO(OB)<>-1THENPRINT"I DON'T HAVE IT.":GOTO31
193 IFL=18 OR L=19 OR L=20 THEN194 ELSE PRINT"IT SOUNDS BEAUTIFUL.":GOTO31
194 IFG=1 THENPRINT"THE MONSTER SMILES HAPPILY AND"''"DISAPPEARS.":D(19,4)=20:O(30)=0:GOTO31
195 PRINT"THE SERPENT OPEN HIS MOUTH AND..."''"AAAAA.....":GOTO206
196 IFOB<>29THENPRINT"I CAN'T ";I$:GOTO31
197 IFO(29)=0THENPRINT"WHAT BOAT?":GOTO31
198 IFL=17THENL=18:O(30)=19:GOTO27
199 IFO(23)<>-1THENPRINT"I DON'T HAVE A SUITABLE OBJECT TO DO"''"THAT.":GOTO31
200 IFOB=30THENPRINT"I'M AFRAID HE DID'T LIKE THAT..."''"OOOOOOOO...":GOTO206
201 IFOB=3THENPRINT"HOW MURDEROUS. UNFORTUNATELY IT DID NO  GOOD.":GOTO31
202 PRINT"DON'T BE RIDICULOUS!":GOTO31
203 IFL<>9THENPRINT"OK: """;MID$(I$,M+1,LEN(I$)-M);"""":GOTO31
204 IFOB<>42THENPRINT"NOTHING HAPPENED.":GOTO31
205 IFOB=42THENPRINT"HE SMILES AND SAYS, ""GOOD TO SEE YOU"''"AGAIN, CAP'N."" ... AND DISAPPEARS!":O(3)=0:D(9,2)=10:GOTO31
206 END
207 IFO(6)<>L ANDOB=7THENPRINT"I SEE SOMETHING!" ELSE GOTO86
208 IFO(5)=0THENO(5)=L:GOTO31
209 IFO(6)=0THENO(6)=L:GOTO31
210 GOTO86
