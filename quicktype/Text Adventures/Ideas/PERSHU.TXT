10 REM THE CITADEL OF PERSHU
20 RANDOMIZE VAL(RIGHT$(TIME$,2)):REM DELETE IF NOT NEEDED
30 COSUB 4510:REM INITIALISE
40 PRINT "----------------------------------":PRINT
50 GOSUB 260:REM PLAYER STATUS REPORT
60 GOSUB 440:PRINT:REM ROOM DESCRIPTION
70 IF RO=31 OR RO>43 THEN 190
80 IF A(RO,8)<>0 THEN GOSUB 3180:GOTO 110:REM MONSTER DESCRIPTION
90 IF RO=6 AND LI=0 THEN GOSUB 3340:REM TO GET TORCH
100 IF A(RO,7)<>0 AND LI=1 THEN GOSUB 3340:REM CONTENTS DESCRIPTION
110 GOSUB 4210:REM ACCEPT PLAYER COMMAND
120 IF ST*CH*DE*IN*WI*CO=0 THEN 170
130 TALLY=TALLY+1
140 IF RO=15 THEN C(2)=0
150 IF RO=20 THEN C(3)=0
160 GOTO 40
170 PRINT "YOUR COMBINED ATTRIBUTES ARE NO LONGER"
180 PRINT "ENOUGH TO SUSTAIN YOU...YOU ARE DEAD!"
190 PRINT:PRINT "YOUR FINAL SCORE, ";N$", IS";
200 PRINT 3*CASH + 30*MK + 3*(ST+CH+DE+IN+WI+CO) + TALLY
210 W=RO+TALLY/1000:IF W>99 THEN W=W-RND(1)*8:GOTO 210
220 IF RO=31 THEN W=100
230 PRINT "YOU COMPLETED";INT(W);"% OF THE QUEST"
240 END
250 REM ********************
260 REM PLAYER STATUS REPORT
270 PRINT N$
280 IF M>0 THEN PRINT "MAGIC SPELLS: YOU HAVE";M;"LEFT"
290 IF MK>0 THEN PRINT "MONSTERS KILLED:";MK
300 PRINT "YOU HAVE ";
310 FOR J=1 TO 9:PRINT T$(C(J));" ";:NEXT J
320 IF CASH>0 THEN PRINT "$";CASH
330 IF ST<0 THEN ST=0
340 IF CH<0 THEN CH=0
350 IF DE<0 THEN DE=0
360 IF IN<0 THEN IN=0
370 IF WI<0 THEN WI=0
380 IF CO<0 THEN CO=0
390 PRINT:PRINT "STRENGTH:";ST;"   CHARISMA:";CH
400 PRINT "DEXTERITY:";DE;"   INTELLIGENCE:";IN
410 PRINT "WISDOM:";WI;"   CONSTITUTION:";CO:PRINT
420 RETURN
430 *******************
440 REM ROOM DESCRIPTION
450 IF LI=0 AND RO<>6 THEN PRINT "IT IS VERY DARK":GOSUB 5430:RETURN
460 IF RO> 19 THEN 490
470 ON RO GOSUB 550,600,650,710,760,810,880,940,1000,1070,1140,1190,1260,1330,1370,1440,1530,1610,1660
480 GOSUB 5430:RETURN
490 IF RO>35 THEN 520
500 Z=RO-19:ON Z GOSUB 1710,1770,1820,1860,1900,1940,1970,2010,2050,2120,2180,2210,2260,2320,2360,2420
510 GOSUB 5430:RETURN
520 Z=RO-35:ON Z GOSUB 2460,2520,2590,2650,2720,2760,2810,2850,2920,2990,3060,3110
530 GOSUB 5430:RETURN
540 REM **************************
550 REM ROOM 1
560 PRINT "AN UNDERGROUND RIVER FLOWS SWIFTLY BY"
570 IF RND(1)>.5 THEN PRINT "THERE IS AN EXIT TO THE WEST"
580 IF RND(1)>.5 THEN PRINT "A TUNNEL LEADS TO THE SOUTH"
590 RETURN
600 REM ROOM 2
610 PRINT "YOU ARE IN THE CITADEL'S FOOD STORAGE AREA"
620 PRINT "OLD CHEESES AND BLACK LOAVES OF BREAD CAN"
630 PRINT "BE SEEN, AS WELL AS MANY SACKS OF SUPPLIES"
640 RETURN
650 REM ROOM 3
660 PRINT "YOU ARE IN THE CITADEL'S KITCHEN. A HUGE"
670 PRINT "JOINT OF MEAT TURNS SLOWLY OVER A RAGING"
680 PRINT "FIRE. DOORS LEAD INTO CUPBOARDS, AS WELL"
690 PRINT "AS TO THE WEST AND TO THE SOUTH"
700 RETURN
710 REM ROOM 4
720 PRINT "THIS IS THE CENTRAL LIBRARY. LEATHER-BOUND"
730 PRINT "VOLUMES LINE THE WALLS, RIGHT UP TO THE"
740 PRINT "ORNATELY-CARVED CEILING..."
750 RETURN
760 REM ROOM 5
770 PRINT "THIS ROOM IS AN AWFUL MESS. IT USED TO"
780 PRINT "AN ARTIST'S STUDIO. PAINT AND OLD"
790 PRINT "EASELS LIE AROUND THE FLOOR"
800 RETURN
810 REM ROOM 6
820 PRINT "THIS IS THE ENTRANCE TO THE CITADEL OF PERSHU"
830 PRINT "TURN NOW, IF YOU WISH. MANY STRONGER THAN YOU"
840 PRINT "HAVE TAKEN FRIGHT AT ITS MENACIWG TOWERS AND"
850 PRINT "DARK PORTALS. IF YOU WISH TO PROCEED, MOVE"
860 PRINT "EAST TOWARDS THE BLACK, GAPING DOORWAY..."
870 RETURN
880 REM ROOM 7
890 PRINT "A STONE ALTAR STANDS IN THE MIDDLE OF THE ROOM"
900 PRINT "WITH TWO DEAD CANDLES ON IT. AN OLD BOOK LIES"
910 PRINT "ON ONE PART OF THE ALTAR TOP, AND A FADED, RED"
920 PRINT "PARCHMENT CLOTH COVERS THE FRONT OF IT"
930 RETURN
940 REM ROOM 8
950 PRINT "YOU STAND HIGH ON THE BLACK TOWER, THE"
960 PRINT "CITADEL STRETCHES TO THE NORTH, SOUTH"
970 PRINT "AND EAST OF YOU."
980 PRINT "THERE IS ONLY ONE WAY OUT"
990 RETURN
1000 REM ROOM 9
1010 PRINT "YOU ARE IN THE NORTHERN SECTION OF THE"
1020 PRINT "CITADEL'S LARGE WINE CELLAR. HEAVY"
1030 PRINT "BARRELS LIE ALL AROUND YOU IN THIS END"
1040 PRINT "OF THE CELLAR. THERE IS A DOOR TO THE NORTH"
1050 PRINT "AND ONE TO THE SOUTH"
1060 RETURN
1070 REM ROOM 10
1080 PRINT "YOU ARE IN THE WEST WING OF THE WINE"
1090 PRINT "CELLAR. THERE IS A DOOR TO THE WEST AND"
1100 PRINT "ONE TO THE EAST. THE CENTRAL CIRCULAR"
1110 PRINT "PART OF THE CELLAR LIES BEYOND THE"
1120 PRINT "EAST DOOR."
1130 RETURN
1140 REM ROOM 11
1150 PRINT "YOU ARE IN THE CENTRAL, CIRCULAR"
1160 PRINT "AREA OF THE WINE CELLAR. THERE IS"
1170 PRINT "A DOOR AT EACH COMPASS POINT."
1180 RETURN
1190 REM ROOM 12
1200 PRINT "YOU ARE IN THE EAST SECTION OF THE"
1210 PRINT "WINE CELLAR. THERE IS A DOOR TO THE"
1220 PRINT "WEST AND ONE - WHICH YOU CANNOT USE,"
1230 PRINT "AS IT ONLY ALLOWS ENTRANCE TO WHERE"
1240 PRINT "YOU NOW STAND - TO THE EAST"
1250 RETURN
1260 REM ROOM 13
1270 PRINT "THERE ARE MANY, MANY WINE BOTTLES HERE"
1280 PRINT "LYING ON THEIR SIDES, IN THIS SOUTHERN"
1290 PRINT "SECTION OF THE WINE CELLAR. THERE IS A"
1300 PRINT "DARK, UNFRIENDLY-LOOKING HOLE TO THE WEST"
1310 PRINT "AND DOORS TO THE NORTH AND TO THE SOUTH"
1320 RETURN
1330 REM ROOM 14
1340 PRINT "THIS IS THE CITADEL'S ARMORY ROW UPON ROW"
1350 PRINT "OF SHINY SUITS OF ARMOR ARE STORED HERE..."
1360 RETURN
1370 REM ROOM 15
1380 PRINT "YOU ARE IN THE RULER'S BEDCHAMBER"
1390 PRINT "A LARGE FIRE BURNS IN THE SOUTH OF"
1400 PRINT "THE ROOM, WITH A SMALL DOOR BESIDE"
1410 PRINT "IT. OTHER EXITS ARE TO THE NORTH"
1420 PRINT "AND TO THE WEST"
1430 RETURN
1440 REM ROOM 16
1450 PRINT "THIS CURIOUS ROOM HAS A FLOOR COVERED"
1460 PRINT "IN SAND, HEAPED ALL OVER THE PLACE"
1470 PRINT "BY PEEPING OVER THE 'DUNES' YOU CAN"
1480 PRINT "SEE A GOLDEN PASSAGEWAY LEADS TO THE"
1490 PRINT "WEST, AND THERE IS A DOOR TO THE SOUTH"
1500 PRINT "YOU ARE NOT SURE WHETHER OR NOT YOU"
1510 PRINT "HAVE SEEN ALL THE EXITS":RETURN
1520 RETURN
1530 REM ROOM 17
1540 PRINT "YOU ARE IN THE PICTURE GALLERY. PORTRAITS"
1550 PRINT "OF LONG-DEAD PRINCES LINE ALL OF THE"
1560 PRINT "WALLS. THE ROOM IS DOMINATED BY A HUGE"
1570 PRINT "LANDSCAPE, HANGING ABOVE THE EXIT TO THE"
1580 PRINT "EAST WHICH LEADS, VIA THE GOLD PASSAGEWAY"
1590 PRINT "BACK TO THAT CURIOUS ROOM OF SAND"
1600 RETURN
1610 REM ROOM 18
1620 PRINT "YOU ARE ON A REMOTE TOWER BALCONY"
1630 IF RND(1)>.5 THEN PRINT "A BAT FLIES PAST YOU, SHRIEKING"
1640 PRINT "THERE ARE STAIRS HERE"
1650 RETURN
1660 REM ROOM 19
1670 PRINT "YOU WALK BENEATH A STONE ARCHWAY"
1680 PRINT "YOU CAN ONLY WALK NORTH OR SOUTH"
1690 PRINT "UNLESS YOU DECIDE TO TAKE THE STAIRS"
1700 RETURN
1710 REM ROOM 20
1720 PRINT "THIS VAST HALL HAS A MARBLE FLOOR, AND"
1730 PRINT "THE SLIGHTEST SOUND ECHOES VIOLENTLY"
1740 PRINT "THERE ARE PURPLE DRAPES CONCEALING"
1750 PRINT "THE EXITS FROM THIS HALL"
1760 RETURN
1770 REM ROOM 21
1780 PRINT "YOU ARE IN THE CLOVE STOREROOM"
1790 PRINT "THE WEST DOOR RADIATES HEAT"
1800 PRINT "ANOTHER DOOR LEADS TO THE SOUTH"
1810 RETURN
1820 REM ROOM 22
1830 PRINT "YOU ARE IN THE SILVER CROSSES STOREROOM"
1840 PRINT "THERE ARE ONLY TWO EXITS"
1850 RETURN
1860 REM ROOM 23
1870 PRINT "YOU ARE IN THE AMULET STOREROOM"
1880 PRINT "DOORS LEAD NORTH, AND SOUTH"
1890 RETURN
1900 REM ROOM 24
1910 PRINT "YOU ARE IN THE KAZOO STOREROOM"
1920 PRINT "THERE ARE TWO EXITS"
1930 RETURN
1940 REM ROOM 25
1950 PRINT "YOU ARE IN THE SATCHEL STOREROOM"
1960 RETURN
1970 REM ROOM 26
1980 PRINT "YOU ARE IN THE STOREROOM FOR WOODEN"
1990 PRINT "BOXES...THERE ARE TWO EXITS"
2000 RETURN
2010 REM ROOM 27
2020 PRINT "THIS IS WHERE PRINTED VASES ARE"
2030 PRINT "STORED...AS YOU CAN EASILY SEE"
2040 RETURN
2050 REM ROOM 28
2060 PRINT "THE HEAVY AIR OF THIS AREA SEEMS TO MAKE"
2070 PRINT "YOUR TORCH VERY DIM. YOU CAN HARDLY SEE"
2080 PRINT "AIR IS RUSHING UP FROM SOMEWHERE"
2090 PRINT "YOU CAN JUST MAKE OUT THAT THIS AREA MUST"
2100 PRINT "BE A MINE OF SOME SORT"
2110 RETURN
2120 REM ROOM 29
2130 PRINT "YOU APPEAR TO BE IN AN ENDLESS LABYRINTH,"
2140 PRINT "LINED WITH PAINTINGS........."
2150 PRINT "WHICHEVER WAY YOU TURN, THERE SEEMS TO"
2160 PRINT "BE MORE TUNNELS, ALL LINED WITH PAINTINGS"
2170 RETURN
2180 REM ROOM 30
2190 PRINT "THIS IS THE SOUTHERN TOWER OF THE CITADEL"
2200 RETURN
2210 REM ROOM 31
2220 PRINT "WELL DONE, YOU HAVE MANAGED TO FIND THE"
2230 PRINT "THE EXIT. TAKE A DEEP BREATH OF GOOD,"
2240 PRINT "CLEAN AIR..........."
2250 RETURN
2260 REM ROOM 32
2270 PRINT "THIS ROOM IS PILLED WITH SWIRLING SMOKE"
2280 PRINT "SO YOU CANNOT SEE...AIR RUSHES PAST A"
2290 PRINT "STATUE OF THE GODDESS DIANA. THIS"
2300 PRINT "MUST BE THE CITADEL'S MEDITATION CHAMBER"
2310 RETURN
2320 REM ROOM 33
2330 PRINT "A SMALL FORKED BRIDGE CROSSES A STREAM"
2340 PRINT "HERE. YOU CAN MOVE NORTH, SOUTH OR WEST"
2350 RETURN
2360 REM ROOM 34
2370 PRINT "YOU ARE IN A ROUGH STONE CAVERN, WITH"
2380 PRINT "STAIRS LEADING UP FROM IT"
2390 PRINT "THERE IS ALSO A SINGLE DOOR WHICH"
2400 PRINT "LEADS AWAY FROM THE CAVERN"
2410 RETURN
2420 REM ROOM 35
2430 PRINT "THIS IS THE FORMER CITADEL UNDERGROUND"
2440 PRINT "STABLE. IT SMELLS TERRIBLE"
2450 RETURN
2460 REM ROOM 36
2470 PRINT "YOU FIND YOURSELF IN AN UNDERGROUND"
2480 PRINT "COURTYARD, STRANGE, TWISTED TREES ARE"
2490 PRINT "AROUND YOU, AND A WIND OF INCREDIBLE"
2500 PRINT "COLDNESS BLOWS FROM THE EAST"
2510 RETURN
2520 REM ROOM 37
2530 PRINT "THIS IS THE ORACLE ROOM, ALTHOUGH THE"
2540 PRINT "MYSTIC VOICE HAS NOT SPOKEN FOR MANY"
2550 PRINT "YEARS"·
2560 IF RND(1)>.3 THEN PRINT "BUT NOW IT TELLS YOU THERE IS":PRINT "A HIDDEN STAIRWELL IN THE ROOM"
2570 IF RND(1) >.7 THEN PRINT "THE VOICE FAINTLY MURMURS OF THE DOOR TO THE SOUTH"
2580 RETURN
2590 REM ROOM 38
2600 PRINT "HORRORS. A COLD SHUDDER PASSES THROUGH AS YOU"
2610 PRINT "REALISE THIS IS THE PRIESTS' SACRIFICE ROOM"
2620 PRINT "DRIED UP BLOOD IS ON THE FLOOR, AND AN"
2630 PRINT "SKULL GRINS AT YOU, FROM HIGH ON THE WALL"
2640 RETURN
2650 REM ROOM 39
2660 PRINT "OLD STRAW MATTRESSES, AND RINGS CHAINED TO THE"
2670 PRINT "WALL TELL YOU THIS WAS THE CITADEL'S DUNGEON"
2680 IF RND(1)>.4 THEN PRINT "A SMALL DOOR LEADS TO THE NORTH":PRINT "AND ANOTHER TO THE EAST"
2690 PRINT "THE DUNGEON SEEMS TO STRETCH FOREVER, WITH MANY"
2700 PRINT "SMALL PARTITIONED AREAS...."
2710 RETURN
2720 REM ROOM 40
2730 PRINT "YOU ARE IN A SMALL ALCOVE, WITH A SOLID"
2740 PRINT "GREY GRANITE THRONE IN THE MIDDLE OF IT"
2750 RETURN
2760 REM ROOM 41
2770 PRINT "THIS IS THE ORC'S GUARDROOM, WAY BELOW"
2780 PRINT "THE GROUND. A STAIRWELL ENDS HERE, AND"
2790 PRINT "A DOOR LEADS TO THE EAST"
2800 RETURN
2810 REM ROOM 42
2820 PRINT "THERE IS A HEALING POOL HERE WITH A"
2830 PRINT "DANGEROUS, SWIRLING AREA OF WATER"
2840 RETURN
2850 REM ROOM 43
2860 PRINT "THE UNDERPRIESTS OF ODRIC USED THIS"
2870 PRINT "TINY HALL FOR THEIR FORBIDDEN WORSHIP"
2880 PRINT "EONS AGO. IT IS AN UNPLEASANT AREA,"
2890 PRINT "SO YOU ARE THRILLED TO SEE A SET OF"
2900 PRINT "STONE STAIRS"
2910 RETURN
2920 REM ROOM 44, DEATH BY DROWNING
2930 PRINT "WATER COVERS YOUR HEAD"
2940 GOSUB 5430
2950 PRINT "YOU ARE DROWNING"
2960 GOSUB 5430
2970 PRINT "GLUG...CASP..............."
2980 RETURN
2990 REM ROOM 45, DEATH BY BURNING
3000 PRINT "THE FLAMES STRIKE AT YOU..."
3010 GOSUB 5430
3020 PRINT "AS YOU SLOWLY BURN TO DEATH"
3030 GOSUB 5430
3040 IF RND(1)>.7 THEN 3000
3050 RETURN
3060 REM ROOM 46, FREEZING
3070 PRINT "YOU ARE HIT BY A FREEZING SPELL"
3080 PRINT "AND TURN INTO A BLOCK OF PERPETUAL"
3090 PRINT "LIVING STONE. THIS IS THE END"
3100 RETURN
3110 REM ROOM 47, BOTTOMLESS PIT
3120 PRINT "YOU TUMBLE DOWN A BOTTOMLESS PIT"
3130 GOSUB 5430
3140 PRINT "DOWN, DOWN, DOWN..."
3150 IF RND(1)>.4 THEN 3130
3160 RETURN
3170 REM *******************
3180 REM MONSTER DESCRIPTION
3190 G$=M$(A(RO,8))
3200 S1=(INT(RND(1)*6)+1)*3
3210 H1=(INT(RND(1)*6)+1)*3
3220 D1=(INT(RND(1)*6)+1)*3
3230 I1=(INT(RND(1)*6)+1)*3
3240 W1=(INT(RND(1)*6)+1)*3
3250 C1=(INT(RND(1)*6)+1)*3
3260 IF RND(1)>.5 THEN PRINT "YOU COME FACE TO FACE WITH A ";G$ ELSE PRINT "THE ROOM CONTAINS A ";G$
3270 PRINT "WITH ATTRIBUTES AS FOLLOWS:"
3280 PRINT "STRENGTH:";S1;"  CHARISMA:";H1
3290 PRINT "DEXTERITY:";D1;"  INTELLIGENCE:";I1
3300 PRINT "WISDOM:";W1;"  CONSTITUTION:";C1
3310 GOSUB 5430
3320 RETURN
3330 REM ********************
3340 REM CONTENTS DESCRIPTION
3350 G$=T$(A(RO,7))
3360 VALUE=INT(RND(1)*100)+56
3370 PRINT "YOU CAN SEE...."
3380 GOSUB 5430
3390 PRINT G$
3400 IF A(RO,7)>9 THEN PRINT "WORTH $";VALUE
3410 GOSUB 5430
3420 RETURN
3430 REM *********************
3440 REM PICK UP TREASURE
3450 IF LI=0 AND A(RO,7)<>1 THEN PRINT "IT IS TOO DARK TO SEE ANYTHING":GOSUB 5430:RETURN
3460 IF A(RO,7)=1 THEN LI=1:REM PLAYER IS CARRYING LIGHT
3470 IF A(RO,7)>9 THEN CASH=CASH+VALUE:GOTO 3490
3480 C(A(RO,7))=A(RO,7)
3490 A(RO,7)=0
3500 RETURN
3510 REM ********************
3520 REM GET RID OF OBJECTS
3530 FLAG=0
3540 FOR Z=1 TO 9:IF C(Z)<>0 THEN FLAG=1
3550 NEXT Z
3560 IF FLAG=0 THEN PRINT "YOU HAVE NOTHING TO GET RID OF":GOSUB 5430:RETURN
3570 IF A(RO,7)<>0 THEN PRINT "THERE IS ALREADY SOMETHING HERE":GOSUB 5430:RETURN
3580 PRINT "YOU ARE CARRYING:"
3590 FOR Z=1 TO 9
3600 IF C(Z)<>0 THEN PRINT Z;" ";T$(Z)
3610 NEXT Z
3620 PRINT:INPUT "ENTER NUMBER OF OBJECT TO DROP";K
3630 IF C(K)=0 THEN PRINT "YOU ARE NOT CARRYING ";T$(K):GOSUB 5430:GOTO 3620
3640 IF K=1 THEN LI=0:REM TURN LIGHT OFF
3650 A(RO,7)=K
3660 C(K)=0
3670 RETURN
3680 REM *********************
3690 REM THE BIG FIGHT
3700 IF A(RO,8)=0 THEN RETURN
3710 PRINT:PRINT "YOUR OPPONENT IS A ";G$
3720 MT=0:HT=0:REM MONSTER TALLY, HUMAN TALLY
3730 PRINT "WITH THE FOLLOWING ATTRIBUTES:"
3740 PRINT "1 - STRENGTH";S1;"   2 - CHARISMA";H1
3750 PRINT "3 - DEXTERITY";D1;"  4 - INTELLIGENCE";I1
3760 PRINT "5 - WISDOM";W1;"     6 - CONSTITUTION";C1
3770 PRINT:PRINT "YOUR ATTRIBUTES ARE:"
3780 PRINT "1 - STRENGTH";ST;"   2 - CHARISMA";CH
3790 PRINT "3 - DEXTERITY";DE;"  4 - INTELLIGENCE";IN
3800 PRINT "5 - WISDOM";WI;"     6 - CONSTITUTION";CO
3810 PRINT:IF C(4)<> 0 THEN PRINT "YOU HAVE A SWORD":HT=HT+1
3820 IF C(5)<>0 THEN PRINT "YOUR WAR HAMMER WILL BE OF AID":HT=HT+1
3830 IF C(6)<>0 THEN PRINT "CHAIN MAIL ARMOR GIVES YOU AN EDGE":HT=HT+1
3840 IF C(7)<>0 THEN PRINT "YOUR SHIELD WILL HELP IN THIS FIGHT":PRINT "AGAINST THE ";G$:HT=HT+1
3850 IF C(8)<>0 THEN PRINT "THE CLOAK OF PROTECTION SURROUNDS YOU":HT=HT+1
3860 IF C(9)<>0 THEN PRINT "THE WAND OF FIREBALLS ENHANCES YOUR STRENGTH":HT=HT+1
3870 IF M>0 THEN PRINT "ENTER 1 TO FIGHT WITH MAGIC":INPUT "OR 2 TO RELY ON SKILL";Q:IF Q<1 OR Q>2 THEN 3870
3880 IF M>0 AND Q=1 THEN M=M-1:PRINT "YOUR MAGIC DESTROYS IT":GOSUB 5430:MK=MK+1:GOTO 4190
3890 PRINT:INPUT "WHICH ATTRIBUTES TO FIGHT WITH (2)";Z,Q
3900 IF Z<1 OR Q<1 OR Z>6 OR Q>6 OR Z=Q THEN PRINT "NO CHEATING!":GOTO 3890
3910 IF Z=1 OR Q=1 THEN MT=MT+S1:HT=HT+ST
3920 IF Z=2 OR Q=2 THEN MT=MT+H1:HT=HT+CH
3930 IF Z=3 OR Q=3 THEN MT=MT+D1:HT=HT+DE
3940 IF Z=4 OR Q=4 THEN MT=MT+I1:HT=HT+IN
3950 IF Z=5 OR Q=5 THEN MT=MT+W1:HT=HT+WI
3960 IF Z=6 OR Q=6 THEN MT=MT+C1:HT=HT+CO
3970 PRINT:PRINT "THE FIGHT STARTS IN FAVOR OF ";
3980 IF HT>MT THEN PRINT "YOU" ELSE PRINT "THE ";G$
3990 K=INT(RND(1)*8)
4000 PRINT "THE ";G$;" -";MT
4010 PRINT N$;" -";HT:PRINT
4020 IF K=0 THEN PRINT "YOU GET IN A GLANCING BLOW":MT=MT-1
4030 IF K=1 THEN PRINT "THE ";C$;" STRIKES OUT!":HT=HT-3:ST=ST-1:CH=CH-1
4040 IF K=2 THEN PRINT "YOU DRAW THE ";G$;"'S BLOOD":MT=MT-1
4050 IF K=3 THEN PRINT "YOU ARE WOUNDED!!":HT=HT-(INT(RND(1)*3)+1):DE=DE-1
4060 IF K=4 THEN PRINT "THE ";G$;" IS TIRING":MT=MT-1
4070 IF K=5 THEN PRINT "YOU ARE BLEEDING....":HT=HT-2:WI=WI-1:CO=CO-1
4080 IF K=6 THEN PRINT "YOU WOUND THE ";G$:MT=MT-1
4090 IF K=7 THEN K=INT(RND(1)*CASH + 1):PRINT "IT KNOCKS $";K;" FROM YOUR HAND":CASH=CASH-K
4100 IF RND(1)>.25 AND HT>0 AND MT>0 THEN FOR T=1 TO 1600:NEXT T:GOTO 3990
4110 IF HT>MT THEN PRINT "YOU HAVE KILLED THE ";G$:MK=MK+1:GOTO 4190
4120 PRINT "THE ";G$;" GOT THE BETTER OF YOU THAT TIME"
4130 IF Z=1 OR Q=1 THEN ST=4*INT(ST/5)
4140 IF Z=2 OR Q=2 THEN CH=3*INT(CH/4)
4150 IF Z=3 OR Q=3 THEN DE=6*INT(DE/7)
4160 IF Z=4 OR Q=4 THEN IN=2*INT(IN/3)
4170 IF Z=5 OR Q=5 THEN WI=5*INT(WI/6)
4180 IF Z=6 OR Q=6 THEN CO=3*INT(CO/6)
4190 A(RO,8)=0:GOSUB 5430:RETURN
4200 REM *********************
4210 REM ACCEPT PLAYER COMMAND
4220 IF INKEY$<>"" THEN 4220
4230 PRINT:PRINT "WHAT DO YOU WANT TO DO?": PRINT
4240 Z$=INKEY$
4250 IF ZS="" THEN 4240
4260 IF Z$="Q" THEN PRINT "COWARD...QUITTER....TURNCOAT...":GOTO 190
4270 IF Z$="F" AND A(RO,8)=0 THEN PRINT "THERE IS NOTHING TO FIGHT":GOTO 4220
4280 IF Z$="F" THEN GOSUB 3690
4290 IF Z$="P" AND A(RO,7)=0 THEN PRINT "THERE IS NOTHING TO PICK UP":GOTO 4220
4300 IF Z$="G" THEN GOSUB 3520
4310 IF Z$="R" AND RND(1)>.4 AND A(RO,8)<>0 THEN PRINT "NO, YOU MUST STAND AND FIGHT":Z$="F"
4320 IF Z$="R" THEN INPUT "WHICH DIRECTION WILL YOU RUN";Z$
4330 IF Z$="P" THEN GOSUB 3440
4340 IF Z$="F" THEN GOSUB 3690
4350 IF Z$="N" AND A(RO,1)=0 THEN PRINT "NO EXIT THAT WAY":GOTO 4220
4360 IF Z$="S" AND A(RO,2)=0 THEN PRINT "THERE IS NO EXIT SOUTH":GOTO 4220
4370 IF Z$="E" AND A(RO,3)=0 THEN PRINT "YOU CANNOT GO IN THAT DIRECTION":GOTO 4220
4380 IF ZS="W" AND A(RO,4)=0 THEN PRINT "YOU CANNOT MOVE THROUGH SOLID STONE":GOTO 4220
4390 IF Z$="U" AND A(RO,5)=0 THEN PRINT "THERE IS NO WAY UP FROM HERE":GOTO 4220
4400 IF Z$="D" AND A(RO,6)=0 THEN PRINT "YOU CANNOT DESCEND FROM HERE":GOTO 4220
4410 IF RO=15 AND C(2)=0 AND Z$="E" THEN PRINT "YOU NEED THE SILVER KEY TO UNLOCK THE DOOR":GOTO 4220
4420 IF RO=22 AND C(3)=0 AND Z$="W" THEN PRINT "YOU NEED THE GOLD KEY TO UNLOCK THE DOOR":GOTO 4220
4430 IF Z$="N" THEN RO=A(RO,1)
4440 IF Z$="S" THEN RO=A(RO,2)
4450 IF Z$="E" THEN RO=A(RO,3)
4460 IF Z$="W" THEN RO=A(RO,4)
4470 IF Z$="U" THEN RO=A(RO,5)
4480 IF Z$="D" THEN RO=A(RO,6)
4490 RETURN
4500 REM **********
4510 REM INITIALISE
4520 CLS
4530 DIM A(43,8),C(9),T$(18),M$(19)
4540 TALLY=0:LI=0:RO=6:CASH=100:MK=0:M=3
4550 ST=3*(INT(RND(1)*6)+1)
4560 CH=3*(INT(RND(1)*6)+1)
4570 DE=3*(INT(RND(1)*6)+1)
4580 IN=3*(INT(RND(1)*6)+1)
4590 WI=3*(INT(RND(1)*6)+1)
4600 CO=3*(INT(RND(1)*6)+1)
4610 REM ****************
4620 REM ROOM DIRECTIONS
4630 FOR B=1 TO 43
4640 FOR C=1 TO 8
4650 READ A(B,C)
4660 NEXT C:NEXT B
4670 REM *****************
4680 REM ALLOT MONSTERS
4690 FOR J = 1 TO 15
4700 T=INT(RND(1)*43)+1:IF T=6 OR T=31 OR T=4 OR T=21 THEN 4700
4710 IF A(T,8)<>0 THEN 4700
4720 A(T,8)=J
4730 NEXT J
4740 REM *****************
4750 REM ALLOT TREASURE
4760 FOR J = 4 TO 18
4770 T=INT(RND(1)*43)+1:IF T=6 OR T=31 THEN 4770
4780 IF A(T,7)<>0 THEN 4770
4790 A(T,7)=J
4800 NEXT J
4810 REM ***********************
4820 INPUT "WHAT IS YOUR NAME, EXPLORER";N$
4830 CLS
4840 REM *******************
4850 REM NAME TREASURES/MONSTERS
4860 FOR J=1 TO 18
4870 READ T$(J)
4880 NEXT J
4890 FOR J=1 TO 19
4900 READ M$(J)
4910 NEXT J
4920 RETURN
4930 REM ****************
4940 DATA 1,4,1,8,0,0,0,0:REM ROOM 1
4950 DATA 0,5,3,0,0,0,0,0:REM ROOM 2
4960 DATA 3,7,3,2,0,0,0,0:REM ROOM 3
4970 DATA 1,0,5,0,0,0,2,0:REM ROOM 4
4980 DATA 2,0,0,4,0,0,0,0:REM ROOM 5
4990 DATA 0,0,7,0,0,0,1,0:REM ROOM 6, ENTRANCE
5000 DATA 3,14,15,6,0,0,0,0:REM ROOM 7
5010 DATA 1,8,8,8,0,0,0,0:REM ROOM 8
5020 DATA 10,11,0,0,0,0,0,0:REM ROOM 9
5030 DATA 0,0,11,9,0,0,0,0:REM ROOM 10
5040 DATA 9,13,12,10,0,0,0,0:REM ROOM 11 
5050 DATA 0,0,0,11,0,0,0,0:REM ROOM 12
5060 DATA 11,16,0,44,0,0,0,0:REM ROOM 13
5070 DATA 7,0,0,0,0,0,0,0:REM ROOM 14
5080 DATA 7,45,0,12,0,0,0,0:REM ROOM 15
5090 DATA 0,19,0,17,0,37,0,0:REM ROOM 16 
5100 DATA 0,0,16,0,0,0,0,0:REM ROOM 17
5110 DATA 0,30,0,0,0,34,0,0:REM ROOM 18
5120 DATA 16,28,0,0,0,43,0,0:REM ROOM 19
5130 DATA 0,31,22,0,0,0,0,0:REM ROOM 20
5140 DATA 0,23,0,45,0,0,3,0:REM ROOM 21
5150 DATA 0,24,0,20,0,0,0,0:REM ROOM 22
5160 DATA 21,25,0,0,0,0,0,0: REM ROOM 23
5170 DATA 22,0,25,0,0,0,0,0:REM ROOM 24
5180 DATA 23,27,30,24,0,0,0,0:REM ROOM 25
5190 DATA 0,0,27,0,0,0,0,0:REM ROOM 26
5200 DATA 25,0,0,26,0,0,0,0:REM ROOM 27
5210 DATA 19,28,28,28,0,47,0,0:REM ROOM 28
5220 DATA 26,29,29,29,0,0,0,0:REM ROOM 29
5230 DATA 18,0,0,25,0,0,0,0:REM ROOM 30
5240 DATA 20,0,0,0,0,0,0,0:REM ROOM 31
5250 DATA 0,0,34,0,0,47,0,0:REM ROOM 32
5260 DATA 34,36,0,35,0,0,0,0:REM ROOM 33
5270 DATA 34,33,34,32,18,0,0,0:REM ROOM 34
5280 DATA 33,38,36,0,0,0,0,0:REM ROOM 35
5290 DATA 33,39,46,35,0,0,0,0:REM ROOM 36
5300 DATA 0,40,0,0,16,0,0,0:REM ROOM 37
5310 DATA 35,0,0,0,0,41,0,0:REM ROOM 38
5320 DATA 36,39,40,39,0,0,0,0:REM BOOM 39
5330 DATA 37,0,0,39,0,0,0,0:REM ROOM 40
5340 DATA 0,0,42,0,38,0,0,0:REM ROOM 41
5350 DATA 42,43,42,41,0,47,0,0:REM ROOM 42
5360 DATA 0,0,42,0,19,0,0,0:REM ROOM 43
5370 REM ***************************
5380 REM TREASURE/MONSTERS
5390 DATA "FLAMING TORCH","SILVER KEY","GOLD KEY","SWORD","WAR HAMMER","CHAIN MAIL ARMOR","SHIELD","CLOAK OF PROTECTION","WAND OF FIREBALLS"
5400 DATA "EMERALDS","SILVER RINGS","ELVEN AMYTHESTS","DIAMOND DRAGON EYES","CRYSTAL BALL","PIECES OF EIGHT","ELEMENTAL GEMS","SHAPE-SHIFTING STONES","GOLD DUBLOONS"
5410 DATA "SWASHBUCKLER","WEREBEAR","CAECLIAE","MANTICORE","VAMPIRE","PREDEBEAST","CARGOYLE","MEDUSAE","MAGI","FIRE LIZARD","PHASE SPIDER"
5420 DATA "TROLL","HELL HOUND","FROST GIANT","NECROMANCER","HYDRA OF 10 HEADS","PATRIACH","MASTER THIEF","LIVING STATUE"
5430 FOR I=1 TO 999:NEXT:RETURN
