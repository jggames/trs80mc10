0 'SMAUG.BA - AUTHOR UNKNOWN
1 'CLUB 100 LIBRARY - 415/939-1246 BBS,    937-5039 NEWSLETTER, 932-8856 VOICE
5 REM "hom"
6 CLS:ARK=0:RING=0:LEV=1:GOLD=0:X=1:Y=1
14 PRINT @40,"**********************************************     THE TOLKIEN AFFAIR     ******";
15 PRINT"****************************************"
16 A$=CHR$(225):B$=CHR$(226):C$=CHR$(227):D$=CHR$(228):E$=CHR$(229):F$=CHR$(230):G$=CHR$(231):H$=CHR$(232):I$=CHR$(233)
17 J$=CHR$(234):K$=CHR$(235):L$=CHR$(236):P$=CHR$(237):N$=CHR$(238):O$=CHR$(239):R$=CHR$(251):S$=CHR$(252):T$=CHR$(253)
18 U$=CHR$(254)
19 GOSUB 9900
20 PRINT@205,"DO YOU WANT THE STORY (Y OR N?)"
30 Q$=INKEY$:IF Q$="Y" OR Q$="y" THEN GOSUB 9300
40 IF Q$="N" OR Q$="n" THEN 500
50 GOTO 30
500 X=1:Y=1:LEV=1
550 IF FLAG=1 THEN PRINT @DA,CHR$(239);
555 DA=(((X*2)-1)*40)+((Y*3)-1)
565 'GOSUB 9500
566 IF FLAG=0 THEN 695
570 PRINT @DA,CHR$(147);
580 FOR N=1 TO 200
590 NEXT N
605 ON 1+L(X,Y) GOSUB 5500,2000,2500,3000,3500,4000,4500,5000,5300
695 GOSUB 9500
696 IF FLAG=0 THEN GOSUB 9700
698 FLAG=1
700 Q$=INKEY$:IF Q$="" THEN 700
701 IF Q$=CHR$(29) THEN BEEP:GOTO6000
710 IF Q$=CHR$(31) THEN BEEP:GOTO6100
720 IF Q$=CHR$(30) THEN BEEP:GOTO6200
730 IF Q$=CHR$(28) THEN BEEP:GOTO6300
740 IF Q$="U" OR Q$="u" THEN BEEP:GOTO6400
750 IF Q$="D" OR Q$="d" THEN BEEP:GOTO6500
790 GOTO 700
2000  FOR N=1 TO 200
2010 NEXT N
2011 L(X,Y)=0
2020 CLS
2025 GOLD=GOLD +1000
2030 PRINT @0,"*******************************************             GOLD                ****";
2031 PRINT "****************************************";
2050 PRINT "    THERE IS A PILE OF TROGLODITE GOLD  IN  THIS ROOM.  YOU WILL BE RICH (IF YOULIVE LONG ENOUGH).
2060 FOR N=1 TO 500
2065 NEXT N
2070 IF GOLD<6000 THEN PRINT "      YOU NEED ";(6000-GOLD);"MORE GOLD BARS"
2090 GOSUB 4050
2110 RETURN
2500 FOR N=1 TO 20
2501 NEXT N
2505 CLS
2510 PRINT @0,"**********************************************            GOBLIN          ******";
2511 PRINT"****************************************";
2540 IF ARK=1 THEN 2900
2550 PRINT "     YOU HAVE JUST MET A GOBLIN. THIS   HORRIBLE LITTLE BEING AND OTHERS OF HIS KIND HATE HOBBITS."
2560 IF RING =1 THEN PRINT "     YOU HAVE THE MAGIC RING, BUT IT IS SO DARK IN THIS PARTICULAR CAVE THAT    ";
2561 PRINT "BEING INVISIBLE IS NO GOOD TO YOU."
2580 GOSUB 4050
2590 PRINT "     IN THE DARKNESS OF THE CAVE THE    GOBLIN CAN SENSE YOUR POSITION, BUT YOU CANNOT SEE HIM."
2630 GOSUB 4050
2640 CLS
2650 PRINT TAB(40);"     THE GOBLIN IS NOW CHASING YOU      AROUND THE CAVE, WILL HE CATCH YOU?"
2654 GOSUB 9800
2655 GOSUB 4050
2660 LET Q=RND(1)
2670 IF Q>.75 THEN GOTO 2950
2680 PRINT "    HA HA HA, HE HAS GOT YOU."
2690 IF Q<.25 THEN PRINT "     YOU HAVE BEEN BOILED IN OIL                 aaaaaarrrrrrrrrgggggh"
2700 IF Q>=.25 AND Q<=.5 THEN PRINT "YOU HAVE BEEN ROASTED FOR DINNER               chomp chomp chomp gulp"
2710 IF Q>.5 AND Q<=.75 THENPRINT "     YOU HAVE BEEN DROPPED INTO A LAKE OF MOLTEN LAVA";TAB(42);"aaaaaaiiiiiiiieeeeeeee"
2720 GOSUB 4050
2730 GOTO 9200
2900 PRINT "     YOU HAVE THE ARKENSTONE, WHICH IS PROTECTION YOU AGAINST THE GOBLIN."
2920 GOSUB 4050
2930 RETURN
2950 PRINT "     NO..., YOU HAVE ESCAPED HIM."
2960 GOSUB 4050
2970 RETURN
3000 FOR N=1 TO 200
3001 NEXT N
3010 CLS
3020 PRINT @0, "**********************************************             SERPENTS      *******";
3021 PRINT "****************************************";
3040 PRINT "     YOU ENTERED A CAVE WHICH HAS MANY  HIDDEN PITS CONTAINING THOUSANDS OF     SERPENTS"
3065 IF ARK=1 THEN 3400
3070 PRINT "     CAN YOU GET THROUGH WITHOUT FALLINGINTO A PIT?"
3080 GOSUB 4050
3090 GOSUB 9800
3100 Q=RND(1)
3110 IF Q>.5 THEN GOTO 3300
3120 PRINT "     YOU HAVE FALLEN DOWN A PIT AN      SINCE THE SERPENTS ARE VAMPIRES, YOU    HAVE GECOME A BLOOD DONOR.";
3130 GOSUB 4050
3140 GOTO 9200
3300 PRINT "     YOU HAVE MANAGED TO GET BY WITHOUT FALLING DOWN A PIT."
3310 GOSUB 4050
3320 RETURN
3400 LET Q=RND(1)
3410 PRINT "     YOU HAVE THE ARKENSTONE WHICH      PROTECTS YOU FROM SNAKE BITES."
3420 IF Q>.75 THEN PRINT "     YOU ARE PROTECTED FROM SNAKE BITES BUT YOU HAVE FALLEN DOWN A VERY DEEP PIT";
3421 IF Q>.75 THEN PRINT "AND DIED OF STARVATION, BECAUSE YOU CANNOT GET OUT."
3430 GOSUB 4050
3450 IF Q>.75 THEN 9200
3460 RETURN
3500 FOR N=1 TO 200
3501 NEXT N
3505 CLS
3510 PRINT @0,"*********************************************            BALROG            *****";
3511 PRINT "****************************************";
3530 IF ARK=0 AND RING =0 THEN PRINT "     YOU HAVE JUST MET THE BALROG, HE ISA ----disgusting----- CREATURE, WITH   ";
3531 PRINT" VERY BAD MANNERS-                            -HE HAS JUST SPLIT YOU IN TWO.            -aaaaaaaaiiiiiiiiieeeeeeee-"
3532 GOSUB 4050
3534 CLS
3540 IF ARK=1 THEN PRINT "     YOU HAVE MET TH BALROG, BUT THE ARKENSTONE PROTECTS YOU."
3550 IF RING=1 AND ARK=0 THEN PRINT "     YOU HAVE MET THE BALROG, BUT YOU   ARE WEARING THE MAGIC RING SO HE CANNOT SEE YOU."
3570 IF RING=0 AND ARK=0 THEN PRINT "     THIS IS ONLY A TEMPORARY           INC";ELSE3580
3571 PRINT"ONVENIENCE, MEANING THAT YOU WILL    HAVE TO START AGAIN."
3580 GOSUB 4050
3590 IF ARK=0 AND RING=0 THEN 9200
3620 RETURN
4000 FOR N=1 TO 200
4010 NEXT N
4015 CLS
4020 PRINT @0, "**************************************************     TROGLODITES   ";
4021 PRINT"**************************************************";
4040 PRINT "     THERE ARE TROGLODITES HERE WHICH ISNOT SURPRISING SINCE WE ARE IN A CAVE."
4045 GOTO 4090
4050 PRINT @287," PRESS ENTER TO CONTINUE";
4060 INPUT Q$
4061 CLS
4064 PRINT
4065 RETURN
4090 IF RING=1 THEN PRINT "     YOU HAVE THE MAGIC RING BUT IT IS  USELESS AGAINST THE TROGLODITES."
4100 IF ARK=1 THEN PRINT"     YOU HAVE TGHE ARKENSTONE BUT ITS   MAGIC DOES NOT WORK AGIANST TROGLODITES."
4110 GOSUB 4050
4115 CLS
4120 PRINT "     THE TROGLODITES ARE A CLEVER RACE, AND ARE GOING TO GIVE YOU THREE GUESSES AT THEIR SECRET NUMBER (BETW";
4121 PRINT"EEN 1 AND 5)     IF YOU GUESS CORRECTLY, THEY WILL  LET YOU GO, IF NOT THEY WILL BLAST YOU  WITH THEIR PROTOP";
4122 PRINT"LASMIC IONIZERS."
4124 GOSUB 9800
4125 LET Q=INT(RND(1)*5)+1
4130 GOSUB 4050
4135 CLS
4136 FOR F=1 TO 3
4140 PRINT "     *******--ENTER GUESS--*******"
4150 INPUT "                  ";G
4160 IF G=Q THEN 4200
4170 NEXT F
4175 PRINT "     zzziiiiippp YOU HAVE JUST BEEN     IONIZED- -bye bye-"
4180 GOSUB 4050
4190 GOTO 9200
4200 PRINT "     YOU GUESSED CORRECTLY.             THE NUMBER WAS ";Q
4220 GOSUB 4050
4230 RETURN
4500 FOR N=1 TO 200
4501 NEXT N
4505 CLS
4510 PRINT @0,"************************************************          TROLL         *********************************";
4511 PRINT"***************";
4525 IF ARK=1 OR RING=1 OR (ARK=1 AND RING=1) THEN 4600
4530 PRINT "     YOU HAVE MET A TROLL. THIS IS A    GREEDY CREATURE, WITH VERY SHARP TEETH. HE DEMANDS 2 LOTS OF YOUR GO";
4531 PRINT"LD SO HE CANBUY FOOD AT THE CANTEEN AFTER THIS GAME.";
4546 GOSUB 4050
4547 CLS
4550 IF GOLD>2000 THEN GOTO 4900
4560 IF GOLD=2000 THEN GOTO 4800
4570 PRINT @40,"     YOU HAVENT GOT ENOUGH GOLD,-OH DEAR- THAT MEANS HE WILL HAVE TO EAT YOU."
4575 GOSUB 4050
4580 PRINT @44,"crunch crunch crunch *** gulp***"
4581 PRINT @124,"LOOK ON THE BRIGHT SIDE, YOU WILL       PROBABLY GIVE HIM INDIGESTION"
4585 GOSUB 4050
4590 GOTO 9200
4600 IF ARK=1 THEN PRINT "     THE ARKENSTONE IS PROTECTING YOU   FROM THE TROLL, BUT YOU MAY LEAVE HIM   SOME";ELSE4610
4601 PRINT" GOLD IF YOU WITH."
4610 IF ARK=0 AND RING=1 THEN PRINT "    YOU ARE WEARING THE MAGIC RING, THE TROLL CANNOT SEE YOU BUT YOU ";ELSE4620
4611 PRINT"MAY LEAVE HIM SOME GOLD IF YOU WISH."
4620 GOSUB 4050
4630 RETURN
4800 PRINT "     AW WHAT A SHAME HE HAS TAKEN ALL YOUR GOLD."
4805 GOLD=0
4810 GOSUB 4050
4820 RETURN
4900 PRINT "     YOU HAVE ENOUGH GOLD TO GIVE HIM   TWO LOTS."
4910 GOLD=GOLD-2000
4920 GOSUB 4050
4930 RETURN
5000 FOR N=1 TO 200
5001 NEXT N
5005 CLS
5010 PRINT @0,"*********************************************              EXIT             *****************************";
5011 PRINT"***************";
5030 PRINT  "     YOU HAVE FOUND THE EXIT, THIS IS   ACTUALLY THE PROFESSORS PERSONALISED    MINI MATTER TRANSPORTER."
5040 GOSUB 4050
5050 IF ARK=1 THEN GOTO 5200
5070 PRINT "     BUT YOU HAVENT GOT THE ARKENSTONE, SO GO AND GET IT YOU HORRIBLE LITTLE    ADVENTURER, OR THE PROF. WIL";
5071 PRINT"L FEED YOU  TO THE TROLLS."
5080 GOSUB4050
5090 RETURN
5200 PRINT "     YOU HAVE GOT THE STONE, OH YOU ARE A CLEVER ADVENTURER (IVE GOT TO SAY THATOR YOU WOULD SWITCH ME OFF)"
5220 GOSUB 4050
5230 PRINT"FOR YOU COURAGE, THE PROF. MAY GIVE YOU A REWARD-(SOMETHING LIKE A BRAIN        PERHAPS?), THEN AGAIN, HE MIG";
5231 PRINT"HT NOT,    OH WELL."
5250 GOSUB 4050
5260 GOTO 8500
5300 FOR N=1 TO 200
5301 NEXT N
5305 CLS
5308 L(X,Y)=0
5310 PRINT @0,"**********************************************         MAGIC RING         *******************************";
5311 PRINT"***************";
5330 PRINT "     YOU HAVE FOUND THE ONE AND ONLY    MAGIC RING, WHICH MAKES YOU INVISIBLE TOALL EXCEPT TROGLODITES AND SMAUG."
5340 GOSUB 4050
5350 PRINT
5360 LET RING=1
5370 PRINT "     DAMN, THAT MEANS YOU WILL PROBABLY SURVIVE THIS GAME,.... OH WELL, I CAN   STILL TRY TO GET YOU KILLED."
5400 PRINT TAB(44);"I SUPPOSE..."
5410 GOSUB 4050
5420 GOTO 695
5500 FOR N=1 TO 200
5501 NEXT N
5505 CLS
5510 PRINT "****************************************************************************************    NOTHING HERE    ";
5511 PRINT"********************************************************************************************";
5527 FOR N=1 TO 300:NEXT N
5530 RETURN
6000 IF Y=1 THEN BEEP:GOTO695
6010 Y=Y-1
6020 GOTO 550
6100 IF X=4 THEN BEEP:GOTO695
6110 X=X+1
6120 GOTO 550
6200 IF GOLD >=6000 AND X=1 AND LEV=3 AND Y=7 THEN 9000
6205 IF LEV=3 AND X=1 AND Y=7 THEN 8000
6210 IF X=1 THEN BEEP:GOTO695
6220 X=X-1
6240 GOTO 550
6300 IF Y=7 THEN BEEP:GOTO695
6310 Y=Y+1
6320 GOTO 550
6400 IF LEV=1 THEN BEEP:GOTO695
6410 LEV=LEV-1
6415 A=X
6420 B=Y
6430 GOSUB 9900
6460 GOTO 550
6500 IF LEV=3 THEN BEEP:GOTO695
6510 LEV=LEV+1
6520 GOTO 6430
8000 FOR N=1 TO 300
8005 NEXT N
8010 CLS
8020 GOTO 8200
8040 CLS
8050 PRINTO$O$O$O$O$O$O$O$O$O$O$O$O$R$ T$R$"    "U$"  "U$"    "T$O$O$O$O$O$O$O$O$O$O$
8051 PRINTO$O$O$O$O$O$O$O$R$" "T$R$"        "O$O$O$O$U$"     "T$O$O$O$O$O$O$O$O$
8052 PRINTO$O$O$O$O$O$O$R$"           "S$O$O$L$O$L$"       "T$O$O$O$O$O$O$
8053 PRINTO$O$O$O$R$"             "O$O$O$O$O$O$O$"        "O$O$O$O$O$O$
8054 PRINTO$O$O$R$"             "O$O$O$O$O$O$O$O$U$"       "O$O$O$O$O$O$
8055 PRINTO$R$"              "O$O$O$O$O$U$T$O$O$O$"       "T$O$O$O$O$O$
8056 PRINTO$"   "S$U$" "S$U$"      "O$O$O$O$O$O$T$U$" "T$R$"        "O$O$O$O$O$
8057 PRINTR$" "S$O$O$O$O$O$O$O$U$"  "H$O$O$O$O$O$O$R$" "T$C$"         "S$O$O$O$O$O$;
8060 GOSUB 9700
8070 PRINT @168,"SMAUG"
8071 PRINT @210,"RULES"
8080 FOR N=1 TO 1000
8081 NEXT N
8085 PRINT @250, "--OK?"
8090 FOR N=1 TO 300
8091 NEXT N
8096 CLS
8100 PRINT @40,"****************************************************************************************      SMAUGS DEN";
8101 PRINT"     *******************************************************************************************"
8110 FOR N=1 TO 200
8120 NEXT N
8130 CLS
8150 RETURN
8200 GOSUB 8040
8210 PRINT @40," YOU HAVE ENTERED SMAUGS DEN BUT YOU DO NOT HAVE ENOUGH GOLD TO BRIBE THE GUARD,            SO HE KILLS YOU."
8240 GOSUB 4050
8250 IF RING=1 THEN PRINT "  THE RINGS MAGIC WONT WORK NEAR SMAUG ";
8260 IF RING=1 THEN GOSUB 4050
8270 GOTO 9200
8500 FOR N=1 TO 100 
8510 NEXT N
8520 CLS
8525 GOTO 8570
8530 PRINT "*************************************** ";
8540 FOR N=1 TO 6
8550 PRINT "****                               **** ";
8560 NEXT N
8565 PRINT "***************************************";
8568 RETURN
8570 GOSUB 8530
8575 PRINT @133, "very well done"
8580 FOR N=1 TO 500
8590 NEXT N
8600 PRINT @133, "              "
8601 PRINT @50, "you have shown,"
8602 PRINT @85,"that you are as:-"
8603 PRINT @138,"brave"
8604 PRINT @180,"cunning"
8605 PRINT @222,"and clever"
8606 PRINT @248,"lucky as the professor"
8630 FOR N=1 TO 1000
8631 NEXT N
8640 FOR N=1 TO 6
8644 PRINT @(40*N+4),SPACE$(31);
8646 NEXT N
8648 PRINT @126,"any key for another game..."
8650 IF INKEY$="" THEN 8650
8660 CLS
8665 RUN
9000 FOR N=1 TO 300
9005 NEXT N
9010 GOSUB 8040
9020 PRINT
9030 GOLD=GOLD-6000
9040 PRINT"      YOU HAVE ENTERED SMAUGS DEN"
9041 PRINT "        AND HAVE BRIBED THE GUARD"
9042 PRINT "          WITH 6000 GOLD BARS"
9060 PRINT
9070 PRINT TAB(8);"BUT SMAUG ALWAYS SLEEPS"
9071 PRINT " WITH ONE EYE OPEN, AND HAS SEEN YOU."
9090 GOSUB 4050
9095 CLS
9100 PRINT TAB(47);"YOU HAVE ONLY FIVE ARROWS"
9101 PRINT TAB(7);"WITH WHICH TO SHOOT SMAUG,"
9102 PRINT TAB(11); "PRESS "+CHR$(34)+"1"+CHR$(34)+" TO SHOOT."
9105 ARR=5
9110 IF INKEY$<>"1" THEN GOTO 9110
9115 GOSUB 9800
9120 IF RND(1)>.80 THEN GOTO 9150
9125 PRINT "   zzzzzzziiiiiiiippppp you missed"
9130 ARR=ARR-1
9131 IF ARR=0 THEN 9145 ELSE 9110
9145 GOTO 9200
9150 PRINT "    zzzzzziiiiiiiipppp you hit him"
9151 PRINT "     you have won the arkenstone"
9152 ARK=1
9160 X=1
9170 Y=7
9190 LEV=3
9191 GOSUB 4050
9194 GOSUB 9500
9195 GOTO 550
9200 FOR N=1 TO 200 
9201 NEXT N
9205 CLS
9210 GOSUB 8530
9220 PRINT @128,"you bungling idiot...."
9230 FOR N=1 TO 200
9231 NEXT N
9232 PRINT @168,"the prof will feed you"
9233 PRINT @212,"to the trolls..."
9240 GOTO 8630
9300 CLS
9310 PRINT "*********************************************        INSTRUCTIONS        ***********************************";
9311 PRINT"************";
9320 FOR N=1 TO 200
9322 NEXT N
9324 CLS
9330 PRINT "     YOU ARE AN ADVENTURER.....(THE     PROFESSORS LITTLE BROTHER)---YOU HAVE   BEEN TRANSPORTED TO THE VAST";
9331 PRINT" COMPLEX OF MIDDLE EARTH TUNNELS AND CAVES          ";
9332 PRINT "INHABITED BY SMAUG THE DRAGON AND OTHER NASTIES. THERE ARE THREE LEVELS, EACH LEVEL HAVING 28 CAVES (7 X 4) ";
9333 PRINT"OR 84 TOTAL.";
9340 GOSUB 4050
9360 PRINT @40,"     YOU MOVE ABOUT ON A LEVEL USING THECURSOR ARROWS, UP A LEVEL USING "+CHR$(34)+"U"+CHR$(34)+",    DO";
9361 PRINT"WN USING "+CHR$(34)+"D"+CHR$(34);
9370 GOSUB 4050
9400 PRINT @40,"     SMAUGS CAVE CAN ONLY BE SEEN FROM  LEVEL 3.  (YOU MUST ENTER HIS CAVE TO   BRIBE THE GUARD.)  THE E";
9401 PRINT"XIT IS ON LEVEL 1.";
9402 PRINT "  THERE IS A MAGIC RING ON LEVEL 1 TO HELP YOU.  THE ARKENSTONE HELPS PROTECT YOU.                          ";
9403 PRINT"                           --BAD LUCK--";
9410 GOSUB 4050
9439 GOTO 500
9461 PRINT ".  YOU MUST COLLECT 6 LOTSOF GOLD TO BRIBE SMAUGS GUARD THEN KILL SMAUG, STEAL THE ARKENSTONE, AND FIND   TH";
9462 PRINT"E EXIT.  (WATCH OUT FOR NASTIES.)";
9500 CLS
9501 PRINT @226,"GOLD=";GOLD
9502 PRINT @266,"LEVEL ";LEV;
9600 PRINT @0  ," "S$H$" "D$H$C$S$H$U$D$H$C$D$H$C$H$H$H$" "H$H$
9602 PRINT @40 ," " O$O$P$O$O$K$O$O$O$N$O$O$O$O$K$O$O$O$N$O$O$" "
9604 PRINT @80," " T$O$A$G$O$A$T$O$R$B$O$A$B$O$A$T$O$R$D$P$" "
9605 PRINT @120,S$O$O$P$O$O$P$O$O$P$N$O$U$S$O$P$P$O$U$O$O$O$C$
9610 PRINT @160,G$G$O$R$G$O$A$T$O$R$G$O$R$G$O$A$T$O$R$G$K$G$R$
9612 PRINT @200," "O$O$P$O$O$P$N$O$O$P$O$K$O$O$O$N$O$P$O$O$O$C$
9614 PRINT @240," " B$O$R$G$O$A$U$O$S$C$O$U$G$O$G$T$O$R$S$N$H$U$
9616 PRINT @280,T$O$O$K$O$O$K$O$O$O$K$O$O$L$O$P$N$O$K$A$O$O$K$;
9680 IF LEV=3 THEN 9681 ELSE 9690
9681 PRINT @20,S$K$G$L$U$" smaugs "
9682  PRINT @61,O$"  "T$U$"den "CHR$(153)S$O$O$U$
9684 PRINT @104,  " "T$O$O$O$O$O$O$O$O$O$U$
9686 PRINT @144,  "  "T$O$O$O$O$O$O$O$O$O$
9688 PRINT @184,  "    "T$O$O$O$O$O$O$R$
9690 PRINT @DA,CHR$(147);
9699 RETURN
9700 RESTORE 9702
9701 GOSUB 9750
9702 DATA 5
9703 DATA 9394,40 
9704 DATA 8368,10 
9705 DATA 7456,20 
9706 DATA 9394,30 
9707 DATA 5818,40 
9709 RETURN
9750 READ LN
9755 FOR N=1 TO LN
9760 READ I,J
9765 SOUND I,J
9770 NEXT N
9780 RETURN
9800 M$=RIGHT$(TIME$,1)
9810 M=VAL(M$)
9820 FOR I=1 TO M
9830 Q=RND(1)
9840 NEXT I
9850 RETURN
9900 A=X:B=Y
9905 GOSUB 9950
9906 FOR N=1 TO 12
9908 GOSUB 9990
9910 L(X,Y)=1
9912 NEXT N
9914 FOR N=1 TO 2
9916 GOSUB 9990
9918 L(X,Y)=3
9920 GOSUB 9990
9922 L(X,Y)=6
9924 GOSUB 9990
9926 L(X,Y)=2
9928 GOSUB 9990
9930 L(X,Y)=5
9932 NEXT N
9934 GOSUB 9990
9936 L(X,Y)=4
9938 GOSUB 9990
9940 IF LEV=1 AND RING=0 THEN L(X,Y)=8
9942 GOSUB 9990
9944 IF LEV=1 THEN L(X,Y)=7
9946 X=A:Y=B
9948 RETURN
9950 FOR X=1 TO 4
9952 FOR Y=1 TO 7
9954 L(X,Y)=0
9956 NEXT Y
9958 NEXT X
9960 RETURN
9990 X=4-INT(RND(1)*4)
9991 Y=7-INT(RND(1)*7)
9992 IF X=1 AND Y=1 AND FLAG=0 THEN 9990
9996 RETURN
9999 RUN
