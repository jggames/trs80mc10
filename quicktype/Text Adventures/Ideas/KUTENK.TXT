
100 REM FROM TRS 80 MICROCOMPUTER NEWS
105 REM ORIGINALLY WRITTEN FOR THE MODEL III
110 REM JULY 1983
120 REM KUTENKAMEN
130 REM BY JOHN CLEMENT
140 CLS:PRINTTAB(10)"KUTENKAMEN"
150 PRINT:PRINT"THIS IS THE GAME OF *KUTENKAMEN*";
160 PRINT"YOU ARE LEADING AN EXPEDITION TO";
170 PRINT" THE LOST TEMPLE OF THE GREAT    EGYPTIAN KING *KUTENKAMEN*"
180 PRINT"YOU GIVE ME ONE-WORD COMMANDS TODIRECT YOUR MOVEMENT THROUGH"
190 PRINT"THE TEMPLE. HERE ARE THE VALID  COMMANDS: "
200 PRINT" N, S, E, W, GET, RUB, QUIT,    LOOK, READ, SCORE, INVENT(ORY)"
204 GOSUB1200
205 CLS:PRINT" YOU CAN GO THE DIRECTIONS SHOWN FOR EXAMPLE *E*W* MEANS YOU CAN GO (E)AST OR (W)EST.
230 PRINT"THE OBJECT IS TO COLLECT ALL THETREASURES AND ARRIVE AT THE"
240 PRINT"PHAROH'S CHAMBER IN THE FEWEST"
250 PRINT" TURNS. WARNING: THE PHAROH'S   EVIL SPIRITS AND CURSES DO NOT"
260 PRINT" LIKE YOUR INTRUSIONS !!!"
270 GOSUB 1200
290 DIMD$(25),WP(25,4),TL(5),T$(5),TC$(11),TH(5)
300 RESTORE:FOR J=1 TO 25:READ D$(J):NEXT J
310 FOR J=1 TO 4:FOR K=1 TO 25:READ WP(K,J):NEXT K,J
320 FOR J=1 TO 11:READ TC$(J):NEXT J
330 FOR J=1 TO 5:READ T$(J):NEXT J
340 FOR J=1 TO 5:READ TL(J):NEXT J
350 FOR J=1 TO 5:TH(J)=0:NEXT J:L=1:T=0:Z=0:X=0:S=0:CLS
360 PRINT D$(L)
370 IF L=25 AND X=5 THEN PRINT"CONGRATULATIONS YOU SURVIVED THETEMPLE OF KUTENKAMEN.":PRINT"IT TOOK";T;"TURNS TO SCORE";S;"OUT OF 125":END
380 GOTO 410
390 PRINT"IT TOOK ";T;"TURNS"
400 PRINT"TO SCORE ";S;"OUT OF 125.":END
410 IF X=5 AND Z=0 THEN PRINT"YOU HAVE ALL THE TREASURES. NOW YOU MUST GET TO THE PHAROH'S    CHAMBER.":Z=1
420 IF L=25 THEN PRINT"YOU MADE IT. BUT YOU DON'T HAVE ALL THE TREASURES."
430 IF L=5 OR L=13 OR L=23 OR L=24 THEN GOSUB1300:GOTO 300
440 IF L=10 THEN PRINT"A BAT GRABS YOU WITH ITS CLAWS  AND FLIES YOU TO ANOTHER ROOM.":L=RND(21):GOTO360
450 TN=0:FOR J=1 TO 5:IF L=TL(J) THEN TN=J
460 NEXT J
470 IF TN<>0 THEN PRINT"THERE IS ";T$(TN);" HERE."
480 C$="":INPUT C$:IF C$="" THEN 480
485 IF C$>"" THEN T=T+1
490 IF C$="ISIS"AND L=20 THEN PRINT"THE PANEL MOVES ASIDE, REVEALING ANOTHER ROOM.":L=22:GOTO 360
500 IF C$="ISIS" THEN PRINT"I DON'T KNOW THAT WORD HERE.":GOTO 480
510 C=0:FOR J=1 TO 11:IF C$=TC$(J) THEN C=J
520 NEXT J
530 IF C=1 THEN PRINT"IT TOOK ";T;"TURNS TO SCORE ";S;"OUT OF 125.":END
540 IF C=2 AND L=3 THEN PRINT"THEY TRANSLATE 'CODE WORD ISIS'.":GOTO 480
550 IF C=2 AND L<>3 THEN PRINT"I DON'T KNOW THAT WORD HERE.":GOTO 480
560 IF C=3 AND TH(2)=1 THENPRINT" MYSTERIOUSLY, AN EVIL SPIRIT   MATERIALIZES IN FRONT OF YOU.":GOSUB1400:GOSUB1300:GOTO300
570 IF C=3 AND TH(2)<>1 THEN PRINT"YOU DO NOT HAVE THE MAGIC IDOL.":GOTO 480
580 IF C<>4 THEN 630
590 IF X=0 THEN PRINT"YOU AREN'T HOLDING ANYTHING.":GOTO 480
600 PRINT"YOU ARE HOLDING THE FOLLOWING:"
610 FOR J=1 TO 5:IF TH(J)=1 THEN PRINT T$(J)
620 NEXT J:GOTO 480
630 IF C<>5 THEN 690
640 FOR J=1 TO 5
650 IF L=TL(J) THEN TL(J)=0:TH(J)=1:GOTO 680
660 NEXT J
670 PRINT"THERE ARE NO TREASURES TO GET.":GOTO 480
680 PRINT" TAKEN.":X=X+1:S=S+25:GOTO 410
690 IF C=10 THEN 360
700 IF C=11 THEN PRINT"SO FAR YOU USED";T:GOTO720
710 GOTO 730
720 PRINT" TURNS TO SCORE ";S:GOTO 480
730 IF C=0 THEN 760
740 C=C-5:IF WP(L, C)=0 THEN PRINT" THERE IS NO WAY TO GO IN THAT  DIRECTION.":S=S-1:GOTO 480
750 IF WP(L, C)<>0 THEN L=WP(L, C):GOTO 360
760 ON RND(3) GOTO 770,780,790
770 PRINT"WHAT ?":GOTO 480
780 PRINT" I DON'T UNDERSTAND.":GOTO 480
790 PRINT" I DON'T KNOW WHAT YOU MEAN.":GOTO 480
800 DATA"   YOU ARE AT THE ENTRANCE TO    KUTENKAMENS TEMPLE. *W*"
810 DATA" YOU ARE INSIDE THE TEMPLE       ENTRANCE. *N*S*"
820 DATA" YOU ARE IN A CHAMBER WITH HEI-  ROGLYPHICS ON THE EAST WALL.    *N*S*W*"
830 DATA"YOU ARE IN A ROOM WITH TWO WALLS ,WEST AND SOUTH. *E*N*"
840 DATA"YOU ARE IN THE SERPENT ROOM.    SNAKE SKINS LINE THE WALLS.     SUDDENLY, ONE COMES TO LIFE AND EATS YOU"
850 DATA" YOU ARE IN THE MUSIC ROOM. *S*"
860 DATA" YOU ARE IN A RECTANGULAR ROOM. PICTOGRAMS LINE THE SOUTH WALL.  *N*W*"
870 DATA"YOU ARE IN A HIGH-WALLED ROOM,  WHICH IS DOMINATED BY A LARGE   STONE SCULPTURE. *N*E*"
880 DATA" YOU ARE IN THE MAGIC ROOM.      *W*S*"
890 DATA"YOU ARE INSIDE THE ENTRANCE OF A CAVE.  *S*"
900 DATA" YOU ARE AT THE EAST END OF A   EAST/WEST HALLWAY. TO THE  NORTH LOOMS THE VELVETY BLACKNESS OF A CAVE. *N*E*W*"
910 DATA"YOU ARE AT THE WEST END OF A    EAST/WEST HALLWAY. A MOANING    SOUND IS EMANATING FROM THE     SOUTH. *N*S*E*"
920 DATA"YOU ARE IN A VERY DARK ROOM.  A MUMMY COMES OUT AND STRANGLES   YOU."
930 DATA"YOU ARE IN A CHAMBER FILLED WITHSCRAPS OF POTTERY. *N*S*"
940 DATA"YOU ARE IN A RECTANGULAR COURT-  YARD. *N*S*"
950 DATA" THE CEILING DROPS DOWN SHARPLY  HERE. *S*E*"
960 DATA"YOU ARE IN A LOW CRAWL. *S*E*W*"
970 DATA"YOU ARE IN A SMALL CRYPT *N*."
980 DATA"THE CRAWL BRANCHES OFF THREE    WAYS. *N*E*W*"
990 DATA"YOU ARE IN A ROOM. THERE IS A    PANEL ON THE NORTH WALL. HEIR- OGLYPHICS ON IT SAY 'INCITE THE WORD TO PASS'"
1000 DATA"YOU ARE IN THE RICHES ROOM. *W*"
1010 DATA"YOU ARE IN A PASSAGE WITH THREE EXITS.I *E*W*N*
1020 DATA"YOU ARE AT THE EDGE OF A PIT.   A-A-A-HHH! YOU JUST FELL DOWN   THE PIT."
1030 DATA"  YOU ARE IN A ROOM WITH LONG   KNIVES ON THE EAST WALL.        SUDDENLY ONE OF THE KNIVES FLIES OVER AND PIERCES YOUR HEART."
1040 DATA"YOU ARE IN THE PHAROH'S CHAMBER."
1050 DATA 0,3,5,6,0,0,2,9,0,0,10,14,12,15,16,0,0,17,20,0,0,24,0,0,0
1060 DATA 0,1,0,3,0,0,0,7,0,0,9,11,0,0,0,17,19,0,21,0,0,23,0,0,22
1070 DATA 0,7,2,0,3,4,0,0,8,11,0,13,0,12,14,15,18,0,0,19,0,20,0,22,0
1080 DATA 2,0,4,0,0,0,8,0,11,0,12,0,0,0,0,0,16,0,17,0,19,25,22,0,0
1090 DATA "QUIT","READ","RUB","INVENT","GET","N","E","S","W","LOOK","SCORE"
1100 DATA "A GOLDEN HARP","A MAGICAL IDOL","A GOLD STATUE OF THE   PHAROH", "A BUNCH OF SILVER COINS","A BAG OF ASSORTED JEWELS"
1110 DATA 6,9,15,18,21
1200 PRINT:PRINT"   PRESS ANY KEY TO CONTINUE"
1210 IF INKEY$=""THEN 1210
1220 RETURN
1300 PRINT" YOU ARE NOW DEAD, TRY AGAIN ?    (Y/N) "
1310 A9$=INKEY$:IF A9$=""THEN 1310
1320 IF A9$="Y" THEN RETURN
1330 IF A9$<>"N" THEN CLS:GOTO 1300
1340 END
1400 PRINT" IT IMEDIATLY VANQUISHES YOU":RETURN
