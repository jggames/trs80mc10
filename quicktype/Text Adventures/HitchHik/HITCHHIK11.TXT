0 CLEAR600:CLS:DIMCC,ZZ,C3,M$,I$:M=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZZ=1TOLEN(M$):C3=ASC(MID$(M$,ZZ)):POKECC+ZZ,C3-(C3AND64):NEXT:?@CC-M+ZZ,:RETURN
8 PRINT:M$=CHR$(34)+"SPIKE HERE: "+M$+CHR$(34):GOSUB1:RETURN
9 FORZZ=1TO2500:NEXT:RETURN

REM *SPECIAL MC-10 INPUT ROUTINE. REPLACE WITH "10 INPUTM$:RETURN" FOR OTHER SYSTEMS
10 C3=0:M$="":PRINT"Œ";:FORCC=0TO1STEP0:CC=-(INKEY$=""):NEXT:CC=0
11 I$=INKEY$:IFI$=CHR$(8)THENIFC3>0THENC3=C3-1:PRINTCHR$(8)CHR$(8)"Œ";:M$=LEFT$(M$,C3)
12 IFI$>=" "ANDI$<"z"ANDC3<20THENC3=C3+1:M$=M$+I$:PRINTCHR$(8)I$"Œ";
13 CC=CC+1:IFCC=1THENPRINTCHR$(8)"Œ";
14 IFCC=12THENPRINTCHR$(8)" ";
15 IFCC=24THENCC=0
16 IFI$<>""THENIFI$=CHR$(13)ORZZ=-99THENPRINTCHR$(8);:RETURN
17 GOTO11

REM *INITIALIZATION*
100 NR=64:NT=21:NP=6:NV=29:NO=NT+NP:DIMD(NR,6),VB$(NV),OB$(NO),R1$(NR),R2$(NR),TD$(NT),VN(NV),TL(NT),TF(NT)
110 RM=1:WD=-1:IL=1:FORI=1TONR:READR1$(I),R2$(I):IFR1$(I)<>"*"THENNEXT:GOTO130
120 R1$(I)="DESERT OF GREY ASH. THE TWO SUNS OF GOMERIL BEAT DOWN MERCILESSLY AND SCAVENGER BIRDS ARE CIRCLING OVERHEAD."
130 NEXT
140 FORI=1TONV:READVB$(I),VN(I):NEXT:FORI=1TONO:READOB$(I):NEXT
150 FORI=1TONT:READTD$(I),TL(I),TF(I):NEXT:FORI=1TONR:READD(I,1),D(I,2),D(I,3),D(I,4),D(I,5),D(I,6):NEXT
160 GOSUB8900:D(24,RND(6))=-25:D(35,RND(6))=-36:ZZ=0

REM *MAIN LOOP: ROOM DESCRIPTION*
200 IFWD=0THEN350
210 WD=0:M$="I'M IN A "+R1$(RM)+" "+R2$(RM):GOSUB1
240 K=0:I$="I SEE HERE: ":FORI=1TONT
250 IFTL(I)<>RMTHEN270
260 I$=I$+TD$(I)+", ":K=-1
270 NEXT:IFKTHENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
300 K=0:I$="OBVIOUS EXITS ARE: "
310 FORI=1TO6:IFD(RM,I)>0THENI$=I$+VB$(I)+", ":K=-1
330 NEXT:IFKTHENM$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1

REM SPIKE COMMENTS
350 IFRM=37ANDS0=0THENM$="AW, REAL COOL BABY.":GOSUB8:S0=1
351 IFRM=25ANDS1=0THENM$="FANTASTIC! HE'S ON GRECIAN 2000.":GOSUB8:S1=1
352 IFRM=38ANDS2=0THENM$="YOU GOT LUCKY, MAN.":GOSUB8:S2=1
353 IFRM=28THENM$="AMAZING, MAN!":GOSUB8:SC=SC+10:GOTO2900
354 IFS5THENIFTL(12)=0ANDRND(2)=1THENM$="HE'S GETTING DRUNK!":GOSUB8:GOSUB9:GOTO2887
355 IFRM<>12THEN400
356 IFTL(20)=0ANDHT=0THENHT=-1:D(12,5)=28
357 IFSG=0THENSG=1:R2$(12)="THE SHIP'S GONE."

REM *MAIN LOOP: COMMAND INPUT AND PARSER* 
400 PRINT:PRINT"NOW WHAT? ";:GOSUB10:C$=M$:PRINT:PRINT
410 L=LEN(C$):IFL=0THEN400
420 C1$="":C2$="":C2=0:X=0 
430 FORI=1TOL:A$=MID$(C$,I,1):A=ASC(A$)
REM IFA>=97THENIFA<=122THENA$=CHR$(A-32)
450 IFA$<>" "THEN460
455 IFC2$<>""THEN500 
457 X=1:GOTO490
460 IFX=0THENC1$=C1$+A$:GOTO490
470 C2$=C2$+A$ 
490 NEXTI 
500 IFC1$=""THENPRINT"I DON'T UNDERSTAND.":GOTO400
510 C1$=LEFT$(C1$,5):C2$=LEFT$(C2$,5)
520 FORC1=1TONV 
530 IFVB$(C1)=C1$THENTM=C1:C1=NV:NEXTC1:C1=VN(TM):GOTO600
540 NEXTC1
550 M$="I DON'T KNOW THE VERB "+C1$:GOSUB1:GOTO400
600 IFC2$=""THEN700 
610 FORC2=1TONO:IFOB$(C2)=C2$THENTM=C2:C2=NO:NEXTC2:C2=TM:GOTO700
630 NEXTC2
640 M$="I DON'T KNOW THE OBJECT "+C2$:GOSUB1:GOTO400

REM *PRELIMINARY FILTERING OF COMMANDS*
700 IFC2>NT+5ANDC1<>7ANDC1<>11THEN640
710 IFC2<=NT+5ANDC2>NTTHENIFC1<>7THEN8010
720 IFC2>NTTHEN750
725 TL=ABS(TL(C2))
730 IFC1<>11ANDTL<>RMANDTL<>0THENPRINT"IT ISN'T HERE.":GOTO400
750 ONC1GOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400
760 ONC1-10GOTO1500,1800,1900,2000,2100,2200,2300,2400,2500,2900
770 ONC1-20GOTO2600,2700,2800

REM *DIRECTIONS*
1000 IFRM=36ORRM=37THENIFRND(3)=1THEN2890
1010 IFD(RM,C1)=0THENPRINT"I CAN'T GO IN THAT DIRECTION.":GOTO200
1020 RM=ABS(D(RM,C1)):WD=-1:GOTO8200

REM *GO*
1100 IFC2=0THEN8050
1105 IFC2<=NTTHEN8040
1110 C1=C2-NT:GOTO1000

REM *EAT*
1200 IFC2=0THEN8050
1210 IFC2<>16THEN8010
1220 TL(16)=-99:PRINT"IT TASTES STALE.":GOTO8200

REM *SCORE* 
1300 GOSUB1390:GOTO8200
1390 PRINT"YOUR SCORE IS"STR$(SC)"/100":RETURN

REM *WAVE*
1400 IFC2=0THEN8050
1410 IFC2<>19THEN8010
1420 IFRM<>37THEN8030
1430 RM=38:WD=-1:SC=SC+10:GOTO8000

REM *DON'T*
1500 IFC2=0THEN8050
1502 IFRM<>45ORDP<>0THEN8030
1740 IFC2=11ANDDP=0THENDP=-1:D(45,2)=41:SC=SC+10:GOTO8000
1750 GOTO8200

REM *GET*
1800 IFC2=0THEN8050
1805 IFTF(C2)=0THENPRINT"THAT'S BEYOND MY ABILITY.":GOTO8200
1810 IFTL=0THENPRINT"I ALREADY HAVE IT!":GOTO200
1820 IFIC>=ILTHENM$="I CAN'T. I'M CARRYING ENOUGH ALREADY.":GOSUB1:GOTO8200
1830 IFC2=21THENIL=4
1840 IFC2=12THENS5=1
1890 TL(C2)=0:IC=IC+1:GOTO8000

REM *DROP*
1900 IFC2=0THEN8050
1910 IFTL<>0THENPRINT"I DON'T HAVE IT.":GOTO200
1960 IFC2<>21THEN1990
1970 IL=1:FORI=1TONT:IFTL(I)=0THENTL(I)=RM:IC=IC-1
1980 NEXTI:GOTO8000
1990 TL(C2)=RM:IC=IC-1:GOTO8000

REM *INVENTORY*
2000 K=0:I$="I'M CARRYING: ":FORI=1TONT:IFTL(I)=0THENI$=I$+TD$(I)+", ":K=1
2030 NEXTI:IFK=0THENM$=I$+"NOTHING.":GOTO2060
2050 M$=LEFT$(I$,LEN(I$)-2)+"."
2060 GOSUB1:GOTO8200

REM *PULL*
2100 IFC2=0THEN8050
2110 IFC2<>8ORRM<>41THEN8010
2130 TL(20)=1:RM=42:WD=-1:R2$(12)="THE SHIP'S HERE.":CD=10:GOTO8000

REM *PUSH*
2200 IFC2=0THEN8050
2220 IFC2<>7ORRM<>36THENPRINT"NOTHING HAPPENS.":GOTO8200
2240 RM=37:WD=-1:SC=SC+10:GOTO8200

REM *SHOOT*
2300 IFC2=0THEN8050
2310 IFC2>4THEN8020
2320 IFC2=1THEN8040
2330 IFC2=2ORC2=3THEN2887
2340 IFC2=4ANDRM=40ANDSR=0ANDTL(13)=0THENSR=-1:D(40,1)=44:TD$(4)="DEAD RANGER":SC=SC+10:GOTO8000
2350 GOTO8030

REM *CHOP*
2400 IFC2=0THEN8050
2410 IFC2=5ANDRM=21ANDCT=0ANDTL(14)=0THENCT=-1:TL(10)=21:D(21,2)=17:SC=SC+10:GOTO8000
2420 IFC2=1ANDRM=18ANDKG=0ANDTL(14)=0THENKG=-1:D(18,1)=22:TD$(1)="DEAD GERBICOP":SC=SC+10:GOTO8000
2430 IFC2<>9THEN8010
2440 IFRM=32THENM$="HE'S BEEN ELECTROCUTED!":GOSUB8:GOTO2900
2450 IFRM=29ANDCW=0ANDTL(15)=0THENCW=-1:D(29,1)=33:SC=SC+10:GOTO8000
2490 GOTO8040

REM *OPEN*
2500 IFC2=0THEN8050
2505 IFC2=21THEN2000
2510 IFC2<>6ORTL(17)<>0THEN8040
2520 IFDOTHENPRINT"IT ALREADY IS.":GOTO8200
2530 D(27,2)=23:DO=-1:SC=SC+10:GOTO8000

REM *LOOK*
2600 IFC2<>0THEN8040
2610 WD=-1:GOTO8200

REM *FEED*
2700 IFC2>3THEN8010
2710 IFC2=0THEN8050
2720 IFTL(16)<>0THEN8030
2730 IFRM=20ANDF1=0THENF1=-1:TL(16)=-99:IC=IC-1
2740 IFRM=55ANDF2=0THENF2=-1:TL(16)=-99:TL(3)=-99:IC=IC-1:D(55,1)=59:SC=SC+10
2750 GOTO8000

REM *EXAMINE*
2800 IFC2=0THEN8050
2810 IFC2=20THENM$="IT'S FOR PASSAGE ON A STARLINER.":GOSUB1:GOTO8200
2820 IFC2=6ANDDO=0THENPRINT"IT'S LOCKED.":GOTO8200
2830 IFC2=21THENM$="IT'S INSCRIBED WITH 'ASSYLIOS ROVERS RULE'":GOSUB1:GOTO8200
2840 IFC2=17THENM$="IT'S LABELED 'AUTO-SAVE LIFE-RAFT'":GOSUB1:GOTO8200
2850 IFC2=14THENPRINT"IT'S FOR CHOPPING TREES.":GOTO8200
2860 IFC2<5THENPRINT"HE'S BLOCKING THE WAY NORTH.":GOTO8200
2870 IFC2=7THENPRINT"THE BUTTON IS SHINY AND RED. OH OH, THE SHIP EXPLODED!":GOTO2900
2880 PRINT"I SEE NOTHING SPECIAL.":GOTO8200

2887 M$="HE'S BEEN DEADED. HOW SAD.":GOSUB8:GOTO2900
2888 M$="AW, POOR OLD MO.":GOSUB8:GOTO2900
2890 PRINT"DEAD."

2900 GOSUB1390:PRINT"ANOTHER TRY (Y/N)? ";:ZZ=-99:GOSUB10
2920 IFI$="Y"THENRUN
2930 END

REM *COMPLETION MESSAGES*
8000 PRINT"OKAY.":GOTO8200
8010 PRINT"DON'T BE SILLY!":GOTO200
8020 PRINT"NOTHING HAPPENS.":GOTO8200
8030 PRINT"I CAN'T DO THAT NOW.":GOTO8200
8040 PRINT"A GREAT IDEA! HOW?":GOTO8200
8050 PRINT"PLEASE GIVE AN OBJECT.":GOTO200

REM *COMPLETION ROUTINES*
8200 IFRM<13THENCD=CD+1
8210 IFRM=12ORRM<3THENIFRND(3)=1THENM$="A MASSIVE EARTHQUAKE IS SHAKING THE BUILDING.":GOSUB1:GOTO8230
8220 IFRM<13ORRM=61THENIFRND(3)=1THENM$="I CAN FEEL ANOTHER EARTH TREMOR.":GOSUB1
8230 IFCD>30THENPRINT"THE PLANET EXPLODED!":GOTO2888
8240 IFRM=20ANDRND(3)=1THENTL(2)=-TL(2)
8250 IFRM=29THENTL(9)=29
8260 IFRM=32THENTL(9)=32
8310 GOTO200

8900 M$="GALACTIC HITCHHIKER":GOSUB7:PRINT@64,;
8901 M$=CHR$(34)+"BOY, I'M SICK OF THAT GUY! I.Q. APPROACHING ZERO, FROM THE NEGATIVE SIDE. "
8902 M$=M$+"VOCABULARY LIMITED, FAVOURITE EXPRESSIONS I CAN'T AND I DON'T UNDERSTAND. HE'S NUTS!":GOSUB1
8903 M$="WORD IS, YOU'LL TAKE HIM OFF MY HANDS AND MAYBE LIKE HELP HIM TO GET HOME.":GOSUB1
8904 M$="CAT SAYS HE'S MAURICE MARINA (TYPICAL!) AND HE'S FROM ASSYLIOS IX.":GOSUB1:PRINT"AND I'M SPIKE..."
8905 PRINT"LAY SOME SKIN ON ME, MAN."CHR$(34)" ";:ZZ=-99:GOSUB10:CLS
8906 M$=CHR$(34)+"OLD MAURICE THERE CAN DO TRICKS LIKE go (east, up, ETC), open, wave AND get THINGS (TRY score)...":GOSUB1
8908 M$="ALL HE'S GOT WITH HIM IS HIS RUCKSACK AND HIS TI...OH, NO! HE'S DROPPED IT! HEY MO, get ticket"+CHR$(34):GOSUB1
8909 PRINT:M$="I CAN'T. A MAN WITH A RUCKSACK INSCRIBED 'ASSYLIOS ROVERS RULE' HAS TAKEN IT.":GOSUB1:PRINT
8910 M$=CHR$(34)+"NOW AIN'T THAT JUST DANDY! OVER TO YOU PAL AND REMEMBER...":GOSUB1
8920 M$="DON'T PANIC!":GOSUB7:PRINTCHR$(34)" ";:ZZ=-99:GOSUB10:CLS:RETURN

REM 1
9010 DATADESERTED DEPARTURE LOUNGE. A RATTIUS AUTOJUKE IS PLAYING 'GERBILS BLESSED BE' AND MAKING
9011 DATAOCCASIONAL RUDE NOISES.

REM 2
9020 DATA"FOYER. A NEWSVENDOR'S PLACARD READS: 'IDI GERB PLAN FAILS."
9021 DATAEVACUATION CONTINUES'

REM 3
9030 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9031 DATAENORMOUS STORAGE TANKS. THE SNOW IS PILED HIGH HERE AND I MUST GO BACK.

REM 4
9040 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9041 DATAENORMOUS STORAGE TANKS. I'M VERY COLD.

REM 5
9050 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9051 DATAENORMOUS STORAGE TANKS. I HOPE YOU KNOW WHERE I AM.

REM 6
9060 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9061 DATAENORMOUS STORAGE TANKS. I'M LOSING ALL SENSE OF DIRECTION.

REM 7
9070 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9071 DATAENORMOUS STORAGE TANKS. FOOTPRINTS LEAD NORTH.

REM 8
9080 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9081 DATAENORMOUS STORAGE TANKS. ALL DIRECTIONS SEEM THE SAME.

REM 9
9090 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9091 DATAENORMOUS STORAGE TANKS. THE SNOW IS DRIVING INTO MY FACE.

REM 10
9100 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9101 DATAENORMOUS STORAGE TANKS. THE TERMINAL IS TO THE EAST.

REM 11
9110 DATARAGING BLIZZARD OUTSIDE THE SPACE TERMINUS BUILDINGS. THE WIND IS HOWLING
9111 DATAROUND THE FIRE ESCAPE AND THERE ARE FOOTPRINTS LEADING OFF TOWARDS THE LAUNCH-PAD.

REM 12
9120 DATAPASSENGER LIFT AT THE LAUNCH-PAD.
9121 DATATHE SHIP'S GONE! THE WHOLE PLACE IS BLOWING UP AND THE BLOODY SHIP'S GONE!

REM 13
9130 DATAWORKSHOP. CONDITIONS HERE ARE APPALLING.,A GERB OVERSEER IS EYEING ME SUSPICIOUSLY.

REM 14
9140 DATARESTAURANT.,IT'S FOR GERBILS ONLY SO I'D BETTER LEAVE.

REM 15
9150 DATATHEATRE. PLEASE GET ME OUT OF HERE,IT'S THE DES O'GERB SHOW!

REM 16
9160 DATASMALL BUILDING.,THERE ARE SOME STAIRS HERE.

REM 17
9170 DATA"BUSY THOROUGHFARE. THERE ARE LOTS OF PEOPLE,"
9171 DATABUT NOBODY SPEAKS. THEY ARE LIKE THE STRICKEN OF ZOMB.

REM 18
9180 DATASIDESTREET.,A SIREN IS WAILING AND I HAVE THE FEELING THAT THAT IS NOT GOOD...

REM 19
9190 DATALAKE OF SOFTLY SWIRLING WATER.,IT'S RATHER PLEASANT EXCEPT THAT ALL MY HAIR IS FALLING OUT.

REM 20
9200 DATAVALLEY. A QUIETLY BABBLING BROOK FLOWS WESTWARDS...,I THINK I'M BEING WATCHED.

REM 21
9210 DATA"WOOD AT THE EDGE OF A DEEP RAVINE. TO THE SOUTH I SEE THE CITY OF GERB, HOME OF"
9211 DATATHE RATTIUS CORPORATION.

REM 22
9220 DATA"POLICE TELEPHONE BOX. A NOTICE SAYS 'HELP THE SPACE RANGERS:",BEAT YOURSELF UP'

REM 23
9230 DATASMALL SPACE-CRAFT. IT APPEARS TO BE A RC101 AUTO-SAVE EMERGENCY-ONLY LIFE-RAFT.
9231 DATATHE GERBODRIVE HAS CLANKED ITSELF INTO A FRENZY AND...

REM 24
9240 DATARC101... FANTASTIC! THE PLANET HAS EXPLODED... IMPLODED AND... GONE SOMEWHERE
9241 DATAELSE! THE SHOCK WAVES ARE HURLING ME THROUGH SPACE...

REM 25
9250 DATASMALL CRATER SURROUNDED,BY BITS OF WRECKED RC101.

REM 26
9260 DATAROOFTOP,MINI-PORT.

REM 27
9270 DATALANDING AREA.,I HEAR A GERBODRIVE SOMEWHERE TO THE SOUTH.

REM 28
9280 DATASTARLINER AND I'M ON MY WAY HOME.,THANKS. IT'S BEEN FUN... BYEEEEEE...

REM 29
9290 DATACORNFIELD.,THERE IS A BARBED-WIRE FENCE TO THE NORTH.

REM 30
9300 DATACLEARING,AT THE FOOT OF A MOUNTAIN.

REM 31
9310 DATALITTLE SHACK.,THERE IS A STILL.

REM 32
9320 DATALOADING BAY. A RATTIUS BRITELOAD (THE LOADER THAT THINKS FOR ITSELF) IS BUSY STUFFING CARTONS INTO THE
9321 DATADRIVE GEAR OF A CONVEYOR BELT. BEYOND A WIRE-MESH FENCE I SEE A SPACETRAN AND I CAN HEAR A HUMMING NOISE.

REM 33
9330 DATALOADING BAY.,THERE IS A SPACETRAN HERE AND A BRITELOAD IS WORKING NEARBY.

REM 34
9340 DATASPACETRAN AND THE BRITELOAD IS APPROACHING THE CONTROLS.,I THINK WE'RE LEAVING...

REM 35
9350 DATASPACETRAN AND WE SEEM TO BE HEADING STRAIGHT FOR A BLACK HOLE! THE PRESSURE IS
9351 DATAENORMOUS....I CAN'T STAND THIS...

REM 36
9360 DATASPACETRAN.,I THINK I'M DYI...

REM 37
9370 DATAFINE MESS NOW! I'VE BEEN EJECTED,AND I'M IN FREE SPACE. I ONLY HAVE A FEW SECONDS. QUICK....

REM 38
9380 DATALABORATORY. I DON'T BELIEVE THIS! I'VE BEEN RESCUED BY A GUY IN A WHITE COAT.
9381 DATAHE SAYS WE'RE ON GOMERIL AND TO WATCH OUT FOR GHOULIBRUTES...

REM 39
9390 DATALABORATORY FULL OF MYSTIFYING GADGETRY.
9391 DATANONE OF THIS MAKES ANY SENSE.

REM 40
9400 DATAQUADRANGLE,IN FRONT OF A BLOCK OF OFFICES.

REM 41
9410 DATASMALL CHAMBER. AN EAR-SPLITTING WHINE IS GETTING LOUDER AND LOUDER.,I CAN'T STAND IT MUCH LONGER.

REM 42
9420 DATASMALL CHAMBER. THE WALLS SEEM TO BE SHIMMERING AND I SEE MY PAST LIFE DEPICTED THERE...,IT'S AWFUL...

REM 43
9430 DATACORRIDOR,LEADING ON TO A FLIGHT OF STAIRS.

REM 44
9440 DATAHALLWAY IN THE OFFICES OF THE TEMPORAL TRAVEL CORP (GOMERIL) INC.
9441 DATA"THERE IS A SIGN MARKED 'MIGGY MAGGUS, director of research'."

REM 45
9450 DATAPLUSH OFFICE. THERE IS AN EXIT TO THE SOUTH BUT MIGGY WANTS ME TO SIT IN HIS
9451 DATACHAIR. HE KEEPS ASKING FOR THE PASSWORD. WHAT PASSWORD? (I THINK HE MAY BE CRACKERS!)

REM 46
9460 DATA"LODGE, UP A MOUNTAIN. A MESSAGE IS SCRAWLED OVER THE FIREPLACE. IT SAYS:"
9461 DATA'SPIKE TOLD YOU THE PASSWORD'

REM 47
9470 DATAPASSAGEWAY AT THE BOTTOM OF A STAIRCASE.,A DOOR LEADS OFF TO THE EAST.

REM 48
9480 DATAWALLED GARDEN. A NOTICE ADVISES AGAINST PROCEEDING OUTSIDE THE AREA.,THERE ARE TWO GATES.

REM 49
9490 DATA*,A PATHWAY LEADS NORTH.

REM 50
9500 DATA*,I DON'T THINK I LIKE THIS.

REM 51
9510 DATA*,I SEE SOMETHING OVER TOWARDS THE EAST...

REM 52
9520 DATA*,ARE YOU TRYING TO WIN YOUR PATHFINDER'S BADGE?

REM 53
9530 DATA*,BLEACHED BONES ARE EVERYWHERE.

REM 54
9540 DATA*,"SORRY TO BOTHER YOU, BUT IS THIS really THE WAY TO ASSYLIOS?"

REM 55
9550 DATA*,THERE'S A SHED HERE.

REM 56
9560 DATA*,"LISTEN, BROTHER I'M GETTING TIRED!"

REM 57
9570 DATA*,I WANT MY MUM.

REM 58
9580 DATA*,YOU'VE GOT ME LOST! HAVEN'T YOU!

REM 59
9590 DATACORRUGATED IRON SHED. THE DOOR IS OFF ITS HINGES AND THE WHOLE PLACE IS PRETTY
9591 DATADELAPIDATED...

REM 60
9600 DATAPENTHOUSE. I CAN SEE A GREAT DESERT FROM THE WINDOW.,THERE'S A SMALL BUILDING FAR TO THE WEST.

REM 61
9610 DATAGIANT INDUSTRIAL COMPLEX. THERE ARE PIPES LEADING EVERYWHERE AND HUNDREDS OF
9611 DATAENORMOUS STORAGE TANKS. I CAN HEAR THE WIND WHISTLING IN THE LAUNCH-PAD GANTRY.

REM 62
9620 DATA*,AM I LOST?

REM 63
9630 DATA*,I'M GETTING FED UP WITH WANDERING AROUND OUT HERE.

REM 64
9640 DATA*,I HOPE THOSE GHOULIBRUTES DON'T COME!

9750 DATAN,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,SCORE,9,USE,10,WAVE,10,DON'T,11
9755 DATATAKE,12,GET,12,DROP,13,TOSS,13,INVEN,14,I,14,PULL,15,PUSH,16
9760 DATASHOOT,17,KILL,17,CHOP,18,CUT,18,OPEN,19,QUIT,20,LOOK,21,FEED,22,EXAMI,23

9810 DATAGERBI,GIANT,GHOUL,RANGE,TREE,DOOR,BUTTO,LEVER,FENCE,TRUNK
9830 DATAPANIC,JUG,BLAST,AXE,WIREC,JOINT,KEY,PROP,SCARF,TICKE,RUCKS
9840 DATANORTH,SOUTH,EAST,WEST,UP,DOWN

9850 DATAGERBICOP,18,,JOLLY GERB GIANT,-20,,GHOULIBRUTE,55,,SPACE RANGER,40,
9860 DATATREE,-21,,DOOR,27,,BUTTON,36,,LEVER,41,1,FENCE,-29,,TREE TRUNK ACROSS THE RAVINE,-99,
9870 DATAPANIC,-45,,JUG,31,1,BLASTER,59,1,CHOPPING AXE,46,1,WIRECUTTER,13,1,ROAST JOINT,14,1,KEY,12,1
9880 DATAPIT PROP,19,1,TATTY OLD SCARF,22,1,TICKET,-99,1,RUCKSACK,1,1

9901 DATA,2,,,,
9902 DATA1,,,11,,
9903 DATA,,-4,,,
9904 DATA-8,,,-3,,
9905 DATA-9,,-6,,,
9906 DATA-61,,,-5,,
9907 DATA-10,,-8,,,
9908 DATA-11,-4,-9,-7,,
9909 DATA,-5,,-8,,
9910 DATA,-7,,-61,,
9911 DATA,-8,-2,,-26,
9912 DATA,-61,,,,
9913 DATA,,,,16,
9914 DATA17,,,,,
9915 DATA18,,,,,
9916 DATA,,17,,32,13
9917 DATA21,14,18,16,,
9918 DATA,15,,17,,
9919 DATA,,20,,,
9920 DATA,,21,19,,
9921 DATA25,,,20,,
9922 DATA,18,,,,
9923 DATA27,,,,-24,
9924 DATA,,,,,
9925 DATA29,21,,,,
9926 DATA,,27,,,11
9927 DATA,,,26,,
9928 DATA,,,,,
9929 DATA,25,30,,,
9930 DATA,,31,29,-46,
9931 DATA,,,30,,
9932 DATA,,,,,16
9933 DATA,29,34,,,
9934 DATA,,,33,-35,
9935 DATA,,,,,
9936 DATA,,,,,
9937 DATA,,,,,
9938 DATA,,39,,,
9939 DATA43,,40,,,
9940 DATA,,,39,,
9941 DATA45,,,,,
9942 DATA,1,,,,
9943 DATA47,39,,,,
9944 DATA,40,45,,,
9945 DATA,,,44,,
9946 DATA,,,,,30
9947 DATA,43,48,,60,
9948 DATA50,,49,47,,
9949 DATA-51,,-62,-48,,
9950 DATA-53,-48,,,,
9951 DATA,-49,,-50,,
9952 DATA-56,-63,-53,,,
9953 DATA-57,-50,,-52,,
9954 DATA-58,,-55,,,
9955 DATA,,-56,-54,,
9956 DATA,-52,,-55,,
9957 DATA,-53,-58,,,
9958 DATA,-54,,-57,,
9959 DATA,55,,,,
9960 DATA,,,,,47
9961 DATA-12,-6,-10,,,
9962 DATA-64,,-63,-49,,
9963 DATA-52,,,-62,,
9964 DATA,-62,,,,
9999 REM JIM GERRIE
