0 CLS:CLEAR500:DIMC1,CC,M$,MC:MC=16384:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN:REM INPUT 2 NUMBERS N1 & N2 OUTPUT N3 (MODULUS)
5 N3=N1-(N2*INT(N1/N2)):RETURN
6 FORC1=C2+MCTOC3+MC:POKEC1,PEEK(C1)-64:NEXT:RETURN
7 C2=(PEEK(17024)AND1)*256+PEEK(17025):PRINTM$;:C3=((PEEK(17024)AND1)*256+PEEK(17025))-1:GOSUB6:RETURN
8 I$=RIGHT$("0000000"+RIGHT$(M$,LEN(M$)-1),C1):RETURN
10 CLS
20 M$="WELCOME TO MADAM FIFI'S WHORE-HOUSE ADVENTURE":GOSUB1:PRINT:PRINT
30 M$="NEED THE INSTRUCTIONS":GOSUB7:INPUTA$:A$=LEFT$(A$+" ",1)
40 IF A$="Y" OR A$= "Y" THEN GOSUB 1660
41 CLS
50 DIM D$(19),O$(31),L(25),P(19,6):L=1
60 FOR X=1 TO 19:READ D$(X):NEXT
70 DATA FRONT ROOM,WAITING ROOM,BATHROOM,OFFICE,BALCONY
80 DATA ALLEY,BAR,BRICK FIREPLACE
90 DATA HIDDEN CORRIDOR,SECRET PASSAGE
100 DATA TURKISH BATH CHAMBER,RAFT,MASSAGE ROOM
110 DATA STOREROOM,LOFT,SAUNA,FIFI'S APARTMENT,SITTING ROOM
120 DATA S&M CHAMBER
130 FOR X=1 TO 31:READ O$(X):IF X>6 THEN READ L(X-6)
140 NEXT:T2=8:R=11:WS$="MONEY"
150 DATA NORTH,SOUTH,EAST,WEST,"UP ",DOWN,SLEDGE HAMMER,5
160 DATA WATCH,1,LEATHER WHIP,9,DIRTY MAGAZINE,3,SWITCHBLADE,4,OAR,6,KEY,99
170 DATA BEER,7,BODY OIL,13,SUITCASE,14,BUCKET,14
180 DATA TORCH,8,NAILS,15,CURTAIN,16,RAFT,11,RUSTY GATE,17
190 DATA BEDROOM DOOR,18,FIRE IN THE FIREPLACE,2
200 DATA TOILET,3,SIGN,1,RAILING,5,BRICK FIREPLACE,2
210 DATA RAT,19,WINE,2,CHEDDAR CHEESE,2
220 FOR Y=1 TO 19:FOR X=1 TO 6:READ P(Y,X):NEXT X,Y
230 DATA ,,3,2,,,,,1,,,,,,4,1,,,,,5,3,,,,,,4,,,,7,,,5,,6,,,,4,
240 DATA ,2,,,,,13,,,,3,,11,8,,19,,,,10,,,,,,11,,,,,14,9,,,,
250 DATA ,13,,,2,,,,,,,16,,,,,,,,16,,,,,,17,,,,,,,10,13,,
260 W$="GO GETLOODROHITOPETIEPUSPAYOILROWCLITAKREATHRCUT"
270 N$="N  01S  02E  03W  04U  05D  06HAM07MAG10SWI11BEE14OIL15GAT22DOO23FIF23FIR28"
280 O$="NORSOUEASWESUP DOWSLEWATWHIRAISWIOARKEYBEEK-YSUIBUCTORNAICURRAFRUSBEDFIRTOISIGDIRBRIRATWINCHE"
290 M$="YOU ARE IN THE "+D$(L):GOSUB1:Y=0:L2=L
300 IF L(12)=0 OR L<10 OR (L> 12 AND L<>18)THEN 320
310 PRINT "IT'S DARK! YOU CAN'T SEE":GOTO 360
320 I$="YOU SEE: ":FOR X=1 TO 25:IF L(X)=L2 THEN I$=I$+O$(X+ 6)+", ":Y=Y+1
330 NEXT:IF Y=0 THEN I$=I$+"NOTHING INTERESTING, "
331 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
340 I$="OBVIOUS EXITS ARE: ":FOR X=1 TO 6:IF P(L,X)>0 THEN I$=I$+O$(X)+", "
350 NEXT:M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
360 S=0:F=0:PRINT:M$="DO WHAT NOW":GOSUB7:INPUTA$:PRINT
370 AA$=A$:B$="   ":FOR X=1 TO LEN(A$)
380 IF MID$(A$,X,1)=" " THEN B$=MID$(A$+"   ",X+1,3)
390 NEXT:IF LEN(A$)=1 THEN B$=A$+"  ":A$="GO "
400 GOSUB 1580:L2=L:T1=T1+1:IF T1=60 THEN T1=0:T2=T2+1
410 IF T2=12 THEN M$=" IT'S MIDNIGHT! THE WHORE-HOUSE IS CLOSED AND THE PIMP THROWS YOU OUT!":GOSUB1:GOTO 1540
420 IF A$="SWI" AND L=11 THEN M$=" YOU HAVE BEEN SCALDED TO DEATH IN THE HOT TURKISH BATHS! ":GOSUB1:GOTO 1540
430 M$=N$:I$=B$:BB=1:GOSUB9000:X=FF:N1=X:N2=5:GOSUB5:IF N3=1 THEN S=VAL(MID$(N$,X+3,2)):GOTO 450
440 M$=O$:I$=B$:BB=1:GOSUB9000:X=FF:N1=X:N2=3:GOSUB5:IF N3=1 THEN S=(X+2)/3
450 M$=W$:I$=A$:BB=1:GOSUB9000:X=FF:N1=X:N2=3:GOSUB5:IF N3=1 THEN F=(X+2)/3
460 IF F>11 THEN F=F-11
470 IF A$<>"INV" THEN 520
480 I$="YOU ARE CARRYING: ":A=0:FOR X=1 TO 25
490 IF L(X)=0 THEN I$=I$+O$(X+6)+", ":A=A+1
500 NEXT:IF A=0 THEN I$=I$+"NOTHING, "
510 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO 360
520 ON F GOTO 560,730,920,1050,1110,1190,1280,1350,1400,1470,1510
530 PRINT "I DON'T KNOW HOW TO DO THAT":GOTO 360
540 PRINT "I DON'T KNOW THAT WORD":GOTO 360
550 REM  -GO-
560 IF S<1 OR S>6 THEN 600
570 IF P(L,S)>0 THEN L=P(L,S):GOTO 290
580 PRINT "YOU CAN'T GO THAT WAY":GOTO 360
590 PRINT "YOU SEE NOTHING SPECIAL":GOTO 360
600 IF S<>28 OR L<>2 THEN 630
610 IF FI=0 THEN M$=" YOU HAVE BURNED TO DEATH! ":GOSUB7:GOTO 1540
620 L=8:GOTO 290
630 IF S<>27 OR L<>5 THEN 660
640 IF LEFT$(O$(27),1)="W" THEN L=6:PRINT "CLIMBED DOWN WHIP":PRINT:GOTO 290
650 M$=" YOU FELL AND DIED ":GOSUB7:GOTO 1540
660 IF S=21 AND L(15)=L THEN L=12:GOTO 290
670 IF L=16 AND B$="LOF" THEN 700
680 IF S<1 THEN 540
690 PRINT "YOU CAN'T GO THERE":GOTO 360
700 IF L(10)=L AND O$(16)="SUITCASE" THEN L=15:GOTO 290
710 PRINT "IT'S A LITTLE TOO HIGH":GOTO 360
720 REM  -GET-
730 PRINT S;F;X
731 IF S<7 THEN 540
740 IF C>6 THEN PRINT "YOU CAN'T CARRY ANY MORE":GOTO 360
750 IF L(S-6)<>L THEN 890
760 IF S<>14 THEN 790
770 V$="IN":GOSUB 1630:IF A$="BUC" AND L(11)=0 THEN 900
780 PRINT "YOU CAN'T DO THAT":GOTO 360
790 IF S<>20 THEN 820
800 IF TA=0 THEN PRINT "IT'S NAILED TO THE LOFT":GOTO 360
810 P(16,1)=17:PRINT "AHA! - A HOLE IN THE WALL":PRINT:GOTO 900
820 IF S<>19 THEN 850
830 IF L(1)<>0 THEN PRINT "YOU HAVE NO HAMMER":GOTO 360
840 IF L(13)=15 THEN TA=1:PRINT "THE CURTAIN IS LOOSE":GOTO 900
850 IF S<>13 THEN 880
860 L(23)=L:IF L(25)=L THEN 900
870 PRINT "THE RAT HAS IT":GOTO 360
880 IF S>20 AND S<30 THEN PRINT "YOU CAN'T GET IT":GOTO 360
881 GOTO900
890 M$="I DON'T SEE ANY "+O$(S):GOSUB1:GOTO 360
900 C=C+1:L(S-6)=0:M$="YOU GOT THE "+O$(S):GOSUB1:GOTO 360
910 REM  -LOOK-
920 IF S<7 THEN 290
930 IF S=26 AND L=1 THEN M$="'THE WHORE-HOUSE CLOSES AT MIDNIGHT'":GOSUB1:GOTO 360
940 IF S<>29 THEN 970
950 IF L(23)<>L THEN 890
960 PRINT "A KEY IS IN IT'S MOUTH!":L(7)=L:GOTO 360
970 IF S<>10 THEN 1010
980 IF L(4)=0 THEN 1000
990 PRINT "YOU DON'T HAVE IT":GOTO 360
1000 M$="THE DIRTY MAGAZINE READS: 'NOT ALL EXITS ARE OBVIOUS.'":GOSUB1:GOTO 360
1010 IF S<>8 THEN 590
1020 IF L(2)<>0 THEN 990
1030 M$=STR$(T1):C1=2:GOSUB8:M$="THE TIME IS NOW"+STR$(T2)+":"+I$:GOSUB1:GOTO 360
1040 REM  -DROP-
1050 IF S<7 THEN GOTO 540
1060 IF L(S-6)<>0 THEN 990
1070 IF S<>14 OR L<>2 THEN 1090
1080 O$(24)="SMOLDERING ASHES":FI=1:L(8)=99:C=C-1:GOTO 290
1090 M$="OKAY, THE "+O$(S)+" IS ON THE "+D$(L)+" FLOOR":GOSUB1:L(S-6)=L:C=C-1:GOTO 360
1100 REM  -HIT-
1110 IF S<7 THEN 540
1120 IF L(S-6)<>L AND (L<>8 OR S<>28) THEN 890
1130 V$="WITH":GOSUB 1630:IF A$<>"SWI" OR S<>16 OR L(5)<>0 THEN 1150
1140 O$(16)=WS$:N$=N$+"MON16":GOTO 290
1150 IF (A$<>"SLE" AND A$<>"HAM") OR S<>28 OR L(1)<>0 THEN M$="":GOTO1170
1160 O$(28)="BROKEN FIREPLACE":P(8,1)=10:D$(8)=O$(28):GOTO 290
1170 M$=M$+"NOTHING HAPPENED":GOSUB1:GOTO 360
1180 REM  -OPEN-
1190 IF S<7 THEN 540
1200 IF L(S-6)<> L THEN 890
1210 IF S<>23 THEN 1240
1220 IF L(7)<>0 THEN M$=M$+"THE BEDROOM DOOR IS LOCKED ... ":GOTO 1170
1230 O$(23)="FIFI IN HER BED":GOTO 290
1240 IF S<>22 THEN M$="":GOTO1170
1250 IF OI=0 THEN M$="TOO MUCH RUST ... ":GOTO 1170
1260 O$(22)="OPEN GATE":P(17,1)=18:GOTO 290
1270 REM  -TIE-
1280 IF S<7 THEN 540
1290 IF L(S-6)<>0 THEN 990
1300 IF S<>9 THEN 780
1310 V$="TO":GOSUB 1630:IF A$<>"RAI" THEN 780
1320 L(3)=99:C=C-1:O$(27)="WHIP TIED TO THE RAILING":N$=N$+"WHI27"
1330 M$=O$(27)+"!":GOSUB1:GOTO 360
1340 REM  -PUSH-
1350 IF S<7 THEN 540
1360 IF L(S-6)<>L THEN 890
1370 IF S<>25 THEN M$="":GOTO1170
1380 M$="AHA! - YOU HAVE REVEALED A DOORWAY":GOSUB1:PRINT:P(L,6)=9:GOTO 290
1390 REM -PAY-
1400 IF S<7 THEN 540
1410 IF S<>23 OR LEFT$(O$(23),1)<>"F" THEN 780
1420 V$="WITH":GOSUB 1630
1430 IF (A$="MON") AND L(10)=0 AND O$(16)=WS$ THEN 1450
1440 M$=" YOU FAILED! FIFI LEAVES AND YOU LOSE YOUR HARD-ON!":GOSUB1:GOTO 1540
1450 M$="CONGRATULATIONS! YOU JUST GOT SCREWED BY FIFI!":GOSUB1:GOTO 1540
1460 REM  -OIL-
1470 IF L(9)<>0 OR L<>17 OR S<>22 THEN 780
1480 PRINT "THE GATE SQUEAKS OPEN":PRINT
1490 OI=1:O$(22)="OPEN GATE":P(17,1)=18:GOTO 290
1500 REM -ROW-
1510 IF L<>12 OR L(6)<>0 THEN 780
1520 R=27-R:L=R:M$="YOU HAVE ROWED TO THE "+D$(L):GOSUB1
1530 PRINT:L(15)=L:GOTO 290
1540 PRINT:PRINT:INPUT "LIKE TO TRY AGAIN";A$:GOSUB 1580
1550 IF LEFT$(A$,1)="Y" THEN RUN
1560 IF LEFT$(A$,1)="R" THEN T1=T1-2:GOTO 360
1570 END
1580 REM  - MAKE INPUT U/C -
1590 A$=LEFT$(A$+"   ",3):FOR I=1 TO 3
1600 CH=ASC(MID$(A$,I,1)):IF CH>96 THEN MID$(A$,I,1)=CHR$(CH-32)
1610 CH=ASC(MID$(B$,I,1)):IF CH>96 THEN MID$(B$,I,1)=CHR$(CH-32)
1620 NEXT:RETURN
1630 REM - GET "WHAT" -
1640 M$="-- "+V$+" WHAT":GOSUB7:INPUT A$
1650 PRINT:GOSUB 1580:RETURN
1660 PRINT:PRINT:M$="WHORE-HOUSE HAS A CONCEALED GOAL. YOU LEARN WHAT THE GOAL IS "
1680 M$=M$+"BY EXPLORING YOUR SURROUNDINGS.  THE COMPUTER WILL ACT AS YOUR EYES "
1690 M$=M$+"AND HANDS.":GOSUB1:M$="IT WILL ACCEPT SHORT PHRASES AS COMMANDS AND ASSUMES "
1700 M$=M$+"THAT THE FIRST WORD IS A VERB AND THE LAST WORD IS THE OBJECT.":GOSUB1
1710 M$="FOR EXAMPLE: READ THE SIGN.":GOSUB1:PRINT:GOSUB1820:M$="THE COMPUTER HAS A VOCABULARY OF ABOUT "
1720 M$=M$+"70 WORDS.  SOME OF THE MORE IMPORTANT WORDS YOU SHOULD KNOW BEFORE "
1730 M$=M$+"YOU START PLAYING ARE:":GOSUB1:M$="GET <OBJECT>, DROP <OBJECT>, LOOK <OBJECT> "
1740 M$=M$+"OR JUST LOOK, GO <DIRECTION> OR <PLACE>, AND INVENTORY (TELLS WHAT "
1750 M$=M$+"YOU ARE CARRYING).":GOSUB1:M$="THE COMPUTER KNOWS THE ABBREVIATIONS: E, W, N, "
1760 M$=M$+"S, U AND D FOR GO EAST, GO WEST, ETC.":GOSUB1:GOSUB1820
1770 PRINT:PRINT:M$="THE COMPUTER'S VOCABULARY IS GOOD, BUT LIMITED.  IF YOU ARE HAVING "
1780 M$=M$+"TROUBLE DOING SOMETHING, TRY RE-PHRASING THE COMMAND OR YOU MAY "
1790 M$=M$+"NEED SOME OBJECT TO ACCOMPLISH THE TASK.":GOSUB1:M$="BY THE WAY, THE COMPUTER "
1800 M$=M$+"ONLY LOOKS AT THE FIRST 3 LETTERS OF EACH WORD.":GOSUB1:PRINT
1810 PRINT TAB(7)"--- GOOD LUCK! ---":PRINT:GOSUB1820:RETURN
1820 PRINT:M$="HIT ANY KEY":GOSUB7
1830 I$=INKEY$:IFI$=""THEN1830
1840 PRINT:RETURN
1997 REM INSTR SUBROUTINE
1998 REM INPUT VARIABLE: M$=STRING TO BE SEARCHED,I$=SEARCH TARGET,BB=BEGINING POSITION OF SEARCH
1999 REM OUTPUT VARIABLE:FF=POSITION OF SEARCH TARGET OR ZERO IF NOT FOUND
9000 FF=0:LL=LEN(I$):FORZZ=BBTOLEN(M$):ON1-(MID$(M$,ZZ,LL)=I$)GOTO9010:FF=ZZ:ZZ=255:NEXT:RETURN
9010 NEXT:RETURN
