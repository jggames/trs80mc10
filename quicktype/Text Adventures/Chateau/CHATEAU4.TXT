0 DIMN(21):MC=16384
1 C2=1:FORC1=1TO63STEP3:N(C2)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):C2=C2+1
2 NEXT:GOTO60
4 FORC2=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,C2))-64),ASC(MID$(M$,C2+1))-48:NEXT:RETURN
7 PRINT@480,;:FORC2=1TO31:PRINT" ";:NEXT:POKE16895,96:PRINT@480+INT((32-LEN(M$))/2),M$;:RETURN
8 GOSUB9
9 FORC1=0TO1200:NEXT:RETURN
60 GOSUB5140:DIMM(7,7,3),T(7,7,3),D$(12),M$(30),A(4),B(4),T$(7,7,3)
100 GOSUB210:GOSUB470:GOSUB8
140 GOSUB710:GOSUB960
160 IFINT(1+6*RND(0))=4THENGOSUB3380
170 IFST<1THENGOSUB4510
180 IFZ=0ANDX=XXANDY=YYTHENGOSUB4160
190 GOTO140
210 FORB=1TO3:FORA=1TO6:M(A,3,B)=1:M(3,A,B)=1:NEXTA,B:A=INT(1+6*RND(0)):FORB=1TO3:M(A,3,B)=4:NEXTB
260 DATA2,2,1,1,2,3,4,1,5,2,6,2,3,1,4,2,5,2,6,4,4,5,4,6,5,4,6,4,9
270 FORF=1TO3:RESTORE
280 READA:IFA=9THEN320
290 B=INT(1+A*RND(0)):FORC=1TOB:READD,E:NEXTC:M(D,E,F)=INT(2+2*RND(0))
310 IFB=ATHEN280
311 READD,E:B=B+1:GOTO310
320 NEXTF:FORC=0TO3:FORA=1TO6:FORB=1TO6:IFM(B,A,C)THEN360
350 M(B,A,C)=INT(5+8*RND(0))
360 NEXTB,A,C:FORD=1TO2
380 A=RND(6):B=RND(6):C=RND(3):IFM(A,B,C)<5ORM(A,B,C)>8THEN380
420 IFD=1THENT(A,B,1)=5
430 IFD=2THENT(A,B,C)=7
440 NEXTD
450 RETURN
470 K=0:X=3:Y=6:Z=1:L=0:TF=0
480 DATAHALL,DOOR,DOOR,STAIRCASE,DESK,CUPBOARD,CLOCK,PIANO,CHAIR,STATUE,FIREPLACE,BOOKCASE
500 FORA=1TO12:READD$(A):NEXTA:MO=0:ST=100:CL$=STR$(INT(1000+8999*RND(0))):CC=1:FORA=1TO6:FORB=1TO6
530 M(A,B,0)=INT(5+8*RND(0)):NEXTB,A
550 DATATROLL,GHOST,IMP,BAT,GIANT,ZOMBIE,GROCKLE,HOUND,DEVIL,KOBOLD,RAT,XORN,GOBLIN,ORCUS,HYDRA,SERPENT
560 DATABASILISK,PHEONIX,MEDUSA,SALAMANDER,GARGOYLE,BEHOLDER,JUBILEX,GOLEM,VAMPIRE,HELL HOUND,GAURDIAN,ORC,ELF
580 FORT=1TO29:READM$(T):NEXT:XX=RND(6):YY=RND(7):FORA=1TO6:FORB=1TO6:FORC=0TO3:IFT(A,B,C)THEN620
610 IFINT(1+7*RND(0))=5THENT(A,B,C)=8
620 IFINT(1+4*RND(0))=2ANDT(A,B,C)=0THENT(A,B,C)=RND(4)
630 NEXTC,B,A
640 FORA=1TO4
650 B=RND(6):C=RND(6):D=RND(3):IFT(B,C,D)<>0ORM(B,C,D)<5ORM(B,C,D)>8THEN650
670 T(B,C,D)=6:NEXTA:RETURN
710 GOSUB5520:GOSUB5690:PRINT@66,;
740 M=M(X,Y-1,Z):A$="NORTH":GOSUB900:PRINT@98,;
770 M=M(X-1,Y,Z):A$="WEST":GOSUB900:PRINT@130,;
800 M=M(X+1,Y,Z):A$="EAST":GOSUB900:PRINT@194,;
810 M=M(X,Y,Z):A$="HERE THERE":GOSUB900:PRINT@162,;
860 M=M(X,Y+1,Z):A$="SOUTH":GOSUB900:RETURN
900 IF((M(X,Y,Z)=1ORM(X,Y,Z)=4)ANDM>4)ORM=0THENRETURN
910 IF(M(X,Y,Z)>4ORM(X,Y,Z)=3)AND(M<5ANDM<>3)THENRETURN
920 IFM(X,Y,Z)=2ANDM>4THENRETURN
930 PRINTA$" IS A "D$(M);:RETURN
960 PRINT@258,"(M) - MOVE";:PRINT@290,"(E) - EXPLORE";:PRINT@322,"(O) - OPEN";
1000 PRINT@354,"(S) - STATUS";:PRINT@386,"(P) - PLAN";:M$="- CHOOSE M,E,O,S OR P -":GOSUB7
1030 B$=INKEY$:IFB$=""THEN1030
1050 SOUND150,1:IFB$="M"THENA=1:GOTO1130
1070 IFB$="E"THENA=2:GOTO1130
1080 IFB$="O"THENA=3:GOTO1130
1090 IFB$="P"THENA=5:AA=1:GOTO1130
1100 IFB$="S"THENA=4:GOTO1130
1110 SOUND95,2:SOUND77,2:GOTO1030
1130 ONAGOSUB1150,1470,1600,3900,5820:RETURN
1150 PRINT@264,"move";:PRINT@307,"(N) - NORTH";:PRINT@339,"(S) - SOUTH";
1200 PRINT@371,"(E) - EAST";:PRINT@403,"(W) - WEST";:M$="- CHOOSE N,S,E OR W -":GOSUB7
1230 C$=INKEY$:IFC$=""THEN1230
1250 SOUND150,1
1260 IFC$="N"ORC$="S"ORC$="W"ORC$="E"THEN1270
1261 GOTO1150
1270 IFC$="N"THENC=-1:B=0
1280 IFC$="S"THENC=1:B=0
1290 IFC$="W"THENC=0:B=-1
1300 IFC$="E"THENC=0:B=1
1310 M=M(X+B,Y+C,Z):IFM=0THENRETURN
1330 IFC$="W"THENT$(X,Y,Z)=CHR$(95)
1340 IFC$="E"THENT$(X,Y,Z)=">"
1350 IFC$="N"THENT$(X,Y,Z)="^"
1360 IFC$="S"THENT$(X,Y,Z)="V"
1370 M=M(X+B,Y+C,Z):IFM=0THENRETURN
1390 IFM(X,Y,Z)=3THEN1450
1400 IFM=1ANDM(X,Y,Z)=2THEN1450
1410 IFM(X,Y,Z)=2THENGOSUB2490:IFM>4THENRETURN
1420 IF((M(X,Y,Z)=1ORM(X,Y,Z)=4)ANDM>4)ORM=0THENRETURN
1430 IF(M(X,Y,Z)>4ORM(X,Y,Z)=3)AND(M<5ANDM<>3)THENRETURN
1440 IFM(X,Y,Z)=2ANDM>4THENRETURN
1450 X=X+B:Y=Y+C:RETURN
1470 IFM(X,Y,Z)=4THENGOSUB1690:GOTO1530
1490 IFT(X,Y,Z)=0THENGOSUB5520:GOSUB2700:T(X,Y,Z)=9:RETURN
1500 IFT(X,Y,Z)=9THEN1540
1510 ONT(X,Y,Z)GOSUB1830,1980,2160,2280,2380,450,450,4260
1520 IFT(X,Y,Z)=0THENT(X,Y,Z)=9
1530 RETURN
1540 GOSUB5520:PRINT@102,"you have searched";:PRINT@198,"here already...";:PRINT@294,"silly!!";:GOSUB8:RETURN
1600 ONM(X,Y,Z)GOSUB1530,2490,2490,1530,2610,2740,2810,2900
1620 IFM(X,Y,Z)>1ANDM(X,Y,Z)<9ANDM(X,Y,Z)<>4THEN1670
1630 GOSUB5520
1640 PRINT@100,"I'VE NEVER OPENED A";:PRINT@132,D$(M(X,Y,Z));
1650 PRINT@196,"AND I DON'T INTEND TO";:PRINT@228,"START NOW!!";
1670 GOTO8
1690 GOSUB5520
1710 PRINT@170,"stairs";:PRINT@226,"DO YOU WANT TO CLIMB UP (U)";:PRINT@258,"OR DOWN (D)?";
1750 A$=INKEY$:IFA$=""THEN1750
1760 IFA$="U"THENA=1:GOTO1790
1770 IFA$="D"THENA=2:GOTO1790
1780 GOTO1750
1790 IF(A=1ANDZ=3)OR(A=2ANDZ=1)THENPRINT@322,"YOU CAN'T!!";:GOSUB9:PRINT@322,"           ";:GOTO1750
1800 IFA=1THENZ=Z+1:GOTO1810
1801 Z=Z-1
1810 T$(X,Y,Z)="S"
1820 RETURN
1830 GOSUB5520:PRINT@104,"congratulations...";:PRINT@162,"YOU'VE JUST FOUND A TRAP.";
1870 A=INT(20+30*RND(0)):PRINT@193,A"OF YOUR STRENGTH POINTS";:PRINT@226,"ARE USED UP...";
1890 ST=ST-A
1900 IFST<1THEN1960
1910 PRINT@258,"YOU ARE LUCKY...";:PRINT@290,"YOU ARE STILL ALIVE...";
1930 IFST<30THENPRINT@322,"BUT ONLY JUST...";
1940 PRINT@356,"TREAD MORE CAREFULLY IN";:PRINT@388,"FUTURE...";
1960 GOTO5650
1980 GOSUB5520
2000 PRINT@66,"YOU'VE JUST FOUND A key";:PRINT@98,"HOWEVER IT MAY BE...";
2020 PRINT@130,"BOOBYTRAPPED!!!";:PRINT@194,"DO YOU WANT IT?";
2040 A$=INKEY$:IFA$=""THEN2040
2050 IFA$="N"THENPRINT@258,"no... OK SUIT YOURSELF";:GOTO8
2060 IFRND(9)<>3THENPRINT@258,"THE KEY'S YOURS";:K=1:T(X,Y,Z)=9:GOTO8
2070 FORAB=1TO50
2080 PRINT@264,"****BOOM****";
2090 SOUND150,1
2100 PRINT@264,"****boom****";
2110 NEXTAB:PRINT@326,"BOOBYTRAPPED!!!";:GOSUB8
2140 GOSUB1830:RETURN
2160 GOSUB5520:PRINT@66,"AH HA, ONE lamp IS YOURS";:PRINT@98,"FOR THE TAKING...";:PRINT@162,"DO YOU WANT IT?";
2210 A$=INKEY$:IFA$=""THEN2210
2220 IFA$="N"THENPRINT@180,"NO!!";:GOTO9
2230 IFRND(9)<>7THENL=1:PRINT@226,"THE LAMP IS YOURS";:T(X,Y,Z)=9:GOTO8
2240 PRINT@226,"OH DEAR THIS LAMP WASN'T FOR";:PRINT@258,"SALE! YOU'VE GOT TROUBLE!";:GOSUB8:GOSUB2280:RETURN
2280 GOSUB5520:PRINT@66,"well done... idiot!";:PRINT@130,"YOU'VE JUST DISTURBED ALL";
2310 PRINT@162,"THE 'ORRIBLE LITTLE BUGS!";
2320 PRINT@290,"BELIEVE ME...";:PRINT@322,"YOUR IN FOR SOME FUN NOW!";
2340 IFMO=1THENPRINT@386,"EVEN MORE THAN BEFORE!";
2350 M$="A1C1E1B1D1G1A1C1E1B1D1G1A1C1E1B1D1G1":GOSUB4:MO=1:GOSUB5650:RETURN
2380 GOSUB5520:PRINT@66,"WELL WELL WELL LOOK WHAT";:PRINT@98,"YOU'VE FOUND...";:PRINT@130,"YES THE cellar entrance!";
2430 PRINT@194,"DO YOU WANT TO GO IN?";
2440 A$=INKEY$:IFA$=""THEN2440
2450 IFA$="N"THENPRINT" NO!!";:GOTO9
2460 IFL=0THENPRINT@258,"TOUGH, YOU'LL NEED A LAMP!";:GOTO8
2470 Z=0:PRINT@234,"good luck";:GOSUB2320:RETURN
2490 GOSUB5520:IFM(X,Y,Z)=3THENPRINT@66,"OK THE DOOR IS OPEN";:GOTO9
2520 PRINT@66,"THE DOOR IS LOCKED";:IFK<>1THENPRINT@130,"YOU WILL NEED A KEY";
2540 IFK<>1THENPRINT@162,"TO OPEN IT...";:GOTO9
2550 PRINT@226,"DO WISH TO UNLOCK IT?";
2560 A$=INKEY$:IF A$=""THEN2560
2570 IFA$="N"THENPRINT"NO!";:PRINT@258,"SUIT YOURSELF!";:GOTO8
2580 M(X,Y,Z)=3:PRINT@258,"THE DOOR IS OPEN";
2590 IFRND(9)=3THENPRINT@290,"THE KEY IS JAMMED...";:PRINT@322,"IN THE LOCK";:K=0
2600 GOTO8

2610 GOSUB5520:IFRND(5)<>2THEN2700
2640 PRINT@66,"THE DESK IS JAMMED...";:PRINT@98,"DO YOU WISH TO FORCE IT?";
2660 A$=INKEY$:IFA$=""THEN2660
2670 IFA$="N"THEN8
2680 ST=ST-RND(10):PRINT@162,"uggggh, OPENED IT!";:GOSUB8
2700 IFT(X,Y,Z)=0THENPRINT@226,"WASTE OF TIME...";:PRINT@258,"IT'S EMPTY.";:GOTO9
2710 ONT(X,Y,Z)GOSUB450,1980,2160,2280,2380,3100,3220:GOTO8

2740 IFL=0THENGOSUB5520:PRINT@66,"IT'S TOO DARK TO SEE...";:PRINT@98,"YOU NEED A LAMP!";:GOTO2800
2770 GOSUB5520
2780 IFRND(9)=4THENPRINT@162,"YOUR LAMP'S BLOWN OUT";:L=0:GOTO2800
2790 GOSUB2700
2800 RETURN
2810 GOSUB5520:PRINT@66,"CREEEEEEK, THE DOOR IS STIFF";:PRINT@98,"AND THIS WEAKENS YOU...";
2840 ST=ST-RND(5):FORCB=1TO10:SOUND87,1:FORAB=1TO100:NEXTAB:SOUND103,1:NEXTCB:GOSUB8
2870 IFRND(9)=4THENGOSUB3380
2880 GOSUB2700:RETURN
2900 GOSUB5520:PRINT@66,"CONGRATULATIONS,";:PRINT@98,"YOU HAVE FOUND A key";
2930 IFT(X,Y,Z)<>2THENPRINT@162,"BUT THEN AGAIN MOST PIANOS";:PRINT@194,"HAVE KEYS!";
2940 GOSUB9:GOSUB2700:RETURN
2970 GOSUB5520:PRINT@66,"ha ha ha...";:PRINT@130,"the house has claimed";:PRINT@194,"another victim...";
3020 PRINT@258,"the treasure lies hidden!";:PRINT@322,"you have failed...";
3040 M$="G4G4G8G8":GOSUB4:SOUND95,4:M$="A4A4G4G4":GOSUB4:SOUND155,4:SOUND161,4:GOSUB8
3050 GOSUB5520:PRINT@66,"your final status was:";:DN=1:GOSUB8:GOSUB3930:GOTO4120
3100 GOSUB5520:PRINT@100,"YOU HAVE FOUND A CLUE...";:CC=CC+1:IFCC=5THENPRINT@162,"THE LAST ONE YOU'LL NEED!";
3140 CD=5-CC:IFCC<>5THENPRINT@162,"YOU STILL NEED"CD"CLUE";
3160 IFCC=5THEN3180
3170 IFCD=1THENPRINT"!";:GOTO3180
3171 PRINT"S!";
3180 PRINT@226,"THE NUMBER IS..."MID$(CL$,CC,1);:PRINT@270,"REMEMBER IT!";:T(X,Y,Z)=0:GOTO8
3220 GOSUB5520:PRINT@66,"YOU HAVE THE PLACE TO";:PRINT@98,"DEPOSIT YOUR CLUES";
3260 PRINT@162,"ARE YOU READY TO TRY?";
3270 A$=INKEY$:IFA$=""THEN3260
3280 IFA$="N"THENPRINT@186,"NO!";:GOTO8
3290 PRINT@258,"ENTER THE CODE";
3310 PRINT@354,;:INPUT"####";CH$:CH$=" "+CH$
3320 IFCH$<>CL$THENPRINT@370,"wrong";:PRINT@383,"µ";:PRINT@418,"YOU HAVE FAILED! NOW DIE...";:GOSUB8:GOTO2970
3330 PRINT@368,"brilliant";:PRINT@418,"YOU HAVE DONE WELL MY FRIEND";
3340 TF=1
3350 GOSUB8:GOSUB4680
3360 GOSUB3900
3370 GOTO4120
3380 IFMO=0THENRETURN
3400 A=RND(29):B=A*RND(4)
3410 GOSUB5520
3420 PRINT@98,"YOU'VE JUST BEEN CONFRONTED";
3430 PRINT@130,"BY A "M$(A);
3440 PRINT@194,"WHAT DO YOU WISH TO DO?!";
3450 PRINT@258,"   (R) - RETREAT";:PRINT@290,"   (F) - FIGHT ";:PRINT@322,"   (G) - GAMBLE";
3480 PRINT@354,"   (A) - ADVICE";:M$="ENTER R,G,F OR A...":GOSUB7
3500 D$=INKEY$:IFD$=""THEN3500
3510 IFD$="R"THENC=1:GOTO3560
3520 IFD$="F"THENC=2:GOTO3560
3530 IFD$="G"THENC=3:GOTO3560
3540 IFD$="A"THENC=4:GOTO3560
3550 GOTO3500
3560 ONCGOTO3570,3650,3780,3850
3570 GOSUB5520:IFRND(3)<>2THENPRINT@266,"you escaped!";:GOTO3840
3580 GOSUB5520
3590 PRINT@66,"A "M$(A)" DOES NOT LIKE";:PRINT@98,"CHICKENS";:PRINT@130,"HE ATTACKS, GOOD LUCK.";
3620 GOTO3660
3630 GOSUB5520
3640 PRINT@162,"THE "M$(A)" NOW HAS"B;:PRINT@194,"HIT POINTS";:GOTO3670
3650 GOSUB5520
3660 PRINT@162,"THE "M$(A)" HAS"B"HIT";:PRINT@194,"POINTS";
3670 PRINT@226,"WHAT'S YOUR PUNCH FACTOR";:INPUTU:PRINT@255,"µ";:IFU<1ORU>9THEN3670
3690 ST=ST-U
3700 U=U*RND(5):B=B-U
3710 PRINT@290,"YOUR PUNCH LEAVES THE";:PRINT@322,M$(A)" ONLY: "B"POINTS";
3730 C=RND(10):B=B-C:IFB<1THEN3755
3751 GOTO3760
3755 ST=ST-B:PRINT@354,"you win AND GAIN";-B;"POINTS";
3756 PRINT@386,"& HAVE"ST"STRENGTH POINTS!";:MK=MK+1:GOTO3840
3760 ST=ST-(C*2):PRINT@354,"HIS HIT LEAVES YOU"ST;:PRINT@386,"POINTS...";
3770 IFST<1THEN3840
3771 GOSUB8:GOSUB9:B=B+C:GOTO3630
3780 GOSUB5520
3790 A=RND(20):PRINT@66,A;"HIT POINTS ARE AT STAKE";
3800 PRINT@130,"CHOOSE A NUMBER BETWEEN...";
3810 PRINT@168,"...1 AND"STR$(20-A);:INPUTB:PRINT@191,"µ";
3820 IFB=INT(1+(20-A)*RND(0))THENST=ST+A:PRINT@226,"YOU WIN...";:GOTO3840
3830 PRINT@226,"HA...HA... YOU LOSE";:ST=ST-A
3840 GOSUB5650:RETURN
3850 GOSUB5520:PRINT@44,"advice";
3860 PRINT@98,"IF YOU WANT TO retreat YOU";:PRINT@130,"HAVE A 2 TO 1 CHANCE";
3861 PRINT@194,"IF YOU WANT TO gamble YOU";:PRINT@226,"CREATE YOUR OWN RANDOM ODDS.";
3862 PRINT@290,"IF YOU CHOOSE TO fight,";:PRINT@322,"THEN YOU HAVE TO INPUT punch";
3870 PRINT@354,"factor IN THE RANGE 1 TO 9.";
3880 PRINT@386,"THIS IS HOW HARD YOU WANT TO";:PRINT@418,"HIT YOUR FOE...";
3890 GOSUB5650:GOTO3410
3900 GOSUB5520:PRINT@66,"YOUR CURRENT STATUS IS:";
3930 PRINT@130,"STRENGTH.........."ST;:PRINT@162,"MONSTERS KILLED...";MK;
3950 PRINT@194,"TREASURE.........."TR;:PRINT@226,"KEY..............."K;
3970 PRINT@258,"LAMP.............."L"     ";:PRINT@290,"CLUES............."CC-1;
3990 PRINT@322,"LEVEL............."Z;:PRINT@354,"COORDINATES......."X;Y;:PRINT@386,;:FORC2=1TO28:PRINT" ";:NEXT
4060 SC=MK*100+TR+10*(K+L)+10*ST-VAL(LEFT$(TI$,4))
4070 IFCC=5THENSC=SC*2
4080 IFTF=1THENSC=SC*2
4090 PRINT@418,"your score is **"SC"**";
4100 IFDN=1THENRETURN
4110 GOSUB5650:RETURN
4120 M$="- DO YOU WANT ANOTHER GO? -":GOSUB7
4130 A$=INKEY$:IFA$=""THEN4130
4140 IFA$="Y"THENRUN
4150 CLS:END
4160 GOSUB5520:PRINT@66,"WOOOOOOOOOOSH!!!!!";:PRINT@98,"YOU'VE JUST FOUND A TRAVEL";:PRINT@130,"AGENT!";
4210 PRINT@194,"AND YOU HAVE BEEN KIDNAPPED";:PRINT@226,"AND RELEASED SOMEWHERE,";:PRINT@258,"BUT WHERE?";
4220 X=RND(6):Y=RND(6):Z=RND(3):GOSUB9:GOTO8
4260 A=RND(900):GOSUB5520:PRINT@66,"YOU HAVE FOUND"A"PIECES";:PRINT@98,"OF gold";:PRINT@162,"DO YOU WANT THEM?";
4320 B$=INKEY$:IFB$=""THEN4320
4330 IFB$="N"THENPRINT@182,"NO!";:GOTO8
4340 PRINT@182,"yes!!";:TR=TR+A:ST=ST-INT(A/40):T(X,Y,Z)=9
4370 IFRND(9)<>2THEN8
4380 PRINT@226,"YOU HAVE ALSO FOUND:";
4390 A=RND(8):IFA=1THENPRINT@290,"A SWORD          ";:ST=ST+RND(20)
4410 IFA=2THENPRINT@290,"BAD LUCK         ";:MO=1
4420 IFA=3THENPRINT@290,"A POTION         ";:ST=ST+RND(20)-5
4430 IFA=4THENPRINT@290,"AN AXE           ";:ST=ST+7
4440 IFA=5THENPRINT@290,"A LAMP           ";:L=1
4450 IFA=6THENPRINT@290,"A KEY            ";:K=1
4470 IFA=7THENPRINT@290,"A NUGGET         ";:TR=TR+RND(800)
4480 IFA=8THENPRINT@290,"A GOOD LUCK CHARM";:MO=0:ST=ST+10
4490 GOSUB8:GOTO4370
4510 GOSUB5520:IFTR<30THEN2970
4540 PRINT@66,"HMMM YOU SEEM TO BE OUT OF";:PRINT@98,"ENERGY.";
4560 PRINT@130,"A GRAND SCORE OF "ST;:PRINT@162,"YOU NEED HELP! SINCE YOU ";
4580 PRINT@194,"HAVE"TR"ITEMS OF TREASURE";:PRINT@226,"I'LL DO A DEAL WITH YOU";:A=8+RND(20):A=INT(TR/A)
4610 PRINT@290,"I'LL SELL YOU ONE UNIT OF";:PRINT@322,"ENERGY FOR EVERY"A"PIECES";
4630 PRINT@354,"OF TREASURE YOU GIVE ME ok";:PRINT@386,"(Y OR N)";
4650 B$=INKEY$:IFB$=""THEN4650
4660 IFB$="Y"THENST=ST+INT(TR/A):TR=TR-A*INT(TR/A):PRINT@418,"ok!";:GOTO8
4661 GOTO4670
4670 IFRND(5)=3THENPRINT@418,"***fool***";:GOSUB8:GOTO2970
4671 GOTO4560
4680 GOSUB5520:PRINT@66,"THE PREVIOUS OWNER OF THE";:PRINT@98,"TREASURE MADE SURE THAT HIS";
4720 PRINT@130,"SECRET WOULD REMAIN HIDDEN";:PRINT@162,"FOR A LONG TIME, HE ALSO";
4740 PRINT@194,"GAVE THE FINDER ONE FINAL";:PRINT@226,"TEST, IF YOU WIN...";
4760 PRINT@258,"THE TREASURE IS YOURS!";:PRINT@290,"....IF YOU LOSE death";
4780 PRINT@354,"DO YOU WANT TO TAKE IT?";
4790 A$=INKEY$:IFA$=""THEN4790
4800 IFA$="N"THENPRINT@418,"ok you have done well";:GOTO8
4810 PRINT@418,"good luck";
4820 GOSUB8:GOSUB5520
4830 PRINT@66,"THE CHEST HAS A LOCK ON IT";:PRINT@130,"TO OPEN IT YOU MUST CRACK";:PRINT@162,"THE CODE";
4860 GOSUB8
4870 FORX=1TO4:A(X)=RND(9):NEXTX
4880 FOR X=1TO4:FOR Y=1TO4:IF X=Y THEN4900
4890 IFA(X)=A(Y)THENV=1
4900 NEXTY,X:IFV=1THENV=0:GOTO4870
4910 PRINT@226,"3 POINTS WILL BE GIVEN IF";:PRINT@258,"YOU CHOOSE THE RIGHT NUMBER";
4930 PRINT@290,"IN THE RIGHT PLACE...";:PRINT@322,"1 POINT FOR THE RIGHT NUMBER";:PRINT@354,"WRONG PLACE";
4960 S=0:GOSUB8:GOSUB5520:PRINT@66,"ok TYPE IN YOUR GUESS";
5000 FORX=1TO4:PRINT@32*X+66,;:INPUTB(X):PRINT@32*X+95,"µ";:NEXTX
5010 FORX=1TO4:IFB(X)=A(X)THENS=S+3
5020 NEXTX
5030 FORX=1TO4:FORY=1TO4
5040 IFX=YTHEN5060
5050 IFB(Y)=A(X)THENS=S+1
5060 NEXTY,X:PRINT@354,;:FORX=1TO4:PRINTB(X);:NEXTX:PRINT@386,"**"S"**";
5100 PRINT@418,"try again";:GOSUB9:PRINT@418,"         ";
5110 IFS=12THENPRINT@436,"**ALL DONE**";:TR=TR+9999+RND(9999):GOTO8
5120 IFRND(11)=4THENPRINT@436,"times up";:GOSUB8:GOTO2970
5130 S=0:GOTO5000
5140 GOSUB5520
5160 X=RND(-(PEEK(9)*256+PEEK(10)))
5170 PRINT@72,"CHATEAU OF GOLD";:PRINT@104,"***************";
5190 PRINT@162,"YOU ARE IN AN OLD CHATEAU";:PRINT@194,"WHEN THE OLD COUNT WHO LIVED ";
5210 PRINT@226,"THERE DIED, HE HID ALL HIS";:PRINT@258,"WORLDLY GOODS IN THE HOUSE.";
5230 PRINT@290,"HE ALSO HID A HOARD OF";:PRINT@322,"treasure VALUED AT $2M!";:GOSUB5650:GOSUB5520
5270 PRINT@66,"TO FIND THIS TREASURE YOU";:PRINT@98,"MUST FIND VARIOUS CLUES AND,";
5290 PRINT@130,"MORE IMPORTANTLY, THE PLACE";:PRINT@162,"TO PUT THEM.";
5310 PRINT@226,"BUT be warned, HE ALSO LEFT";:PRINT@258,"TRAPS FOR THE UNWARY...";
5330 PRINT@290,"SO go carefully!";:PRINT@322,"ALSO, THINGS MAY NOT ALWAYS";:PRINT@354,"BE AS THEY SEEM.";
5370 GOSUB5650:GOSUB5520:Z=7:X=3:Y=6:GOSUB5690
5410 PRINT@98,"TO ASSIST WITH YOUR CHOICE";:PRINT@130,"YOUR CURRENT POSITION IN THE";
5430 PRINT@162,"CHATEAU WILL BE DISPLAYED IN";:PRINT@194,"THE TOP RIGHT HAND CORNER OF";
5450 PRINT@226,"THE MENU SCREEN.";:PRINT@290,"THE 1ST VALUE IS THE FLOOR";:PRINT@322,"YOU ARE ON (0 FOR CELLAR).";
5480 PRINT@354,"THE 2ND & 3RD ARE THE X & Y";:PRINT@386,"CO-ORDINATES.";:M$="PLEASE WAIT...":GOSUB7
5510 RETURN
5520 IFAA=1THENCLS3:AA=0:GOTO5540
5531 CLS
5540 PRINT"¾";:FORXA=1TO30:PRINT"¼";:NEXTXA:PRINT"½";:FORXA=1TO13:PRINT@32*XA,"º";:PRINT@32*XA+31,"µ";:NEXTXA
5580 PRINT"»";:FORXA=1TO30:PRINT"³";:NEXTXA:PRINT"·";:SOUND200,2:RETURN
5650 GOSUB6099
5660 A$=INKEY$:IFA$<>" "THEN5660
5670 SOUND200,2:RETURN
5690 PRINT@57,STR$(Z)STR$(X)STR$(Y);:PRINT@57,"º";:PRINT@59,"*";:PRINT@61,"*";:PRINT@89,"»³³³³³";:PRINT@63,"µ";:RETURN
5820 GOSUB5520:FORXA=1TO11STEP2:FORYA=1TO18STEP3:PRINT@32*XA+YA+4,"¡";:PRINT@32*XA+YA+5,"£";
5880 PRINT@32*XA+YA+6,"¢";:PRINT@32*(XA+1)+YA+4,"¥";:PRINT@32*(XA+1)+YA+6,"ª";:NEXTYA,XA
5920 FORXA=1TO18:PRINT@32*13+4+XA,"£";:NEXTXA:PRINT@89,"floor";:PRINT@122,Z;
5960 FORTY=1TO6:FORTX=1TO6:C2=64*TY+TX*3+3:M$=T$(TX,TY,Z):IFM$=""THENNEXT:NEXT:GOTO6000
5970 IFT(TX,TY,Z)=9THENPRINT@C2,M$;:NEXT:NEXT:GOTO6000
5980 POKEMC+C2,ASC(M$)AND63:NEXT:NEXT
6000 PRINT@187,"n";:PRINT@219,"^";:PRINT@249,"w"CHR$(95)"+>";"e";:PRINT@315,"s";:PRINT@283,"V";
6050 GOSUB6099
6060 PRINT@32*(Y*2)+(X*3)+3,"Ï";:C$=INKEY$:PRINT@32*(Y*2)+(X*3)+3,"#";:IFC$<>" "THEN6060
6070 GOTO5670
6099 M$="- PRESS SPACE TO CONTINUE -":GOSUB7:RETURN
