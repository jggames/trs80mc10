1 PCLEAR5:CLEAR4000:SC=2:SF=143:GOSUB10000:PRINT@38,"medieval adventure";:PRINT@97,"WITH THIS PROGRAM YOU CAN HAVE";:PRINT@130,"AN ADVENTURE WITHOUT LEAVING";:PRINT@170,"YOUR ROOM.";:PRINT@196,"TRAVEL, DISCOVER SECRET";
2 PRINT@228,"PASSAGES AND MANIPULATE";:PRINT@261,"OBJECTS YOU MAY FIND.";:PRINT@328,"DO NOT GIVE UP-";:PRINT@359,"THERE IS ALWAYS A";:PRINT@392,"PUZZLE TO SOLVE.";
5 DEF FNS(X)=(P(X)=50 OR P(X)=60):DEF FNT(X)=(P(X)=50 OR P(X)=60 OR P(X)=N)
10 DIM A$(11),B$(11),L$(26),E$(26),D$(26),O$(52),P(52),C(52),S(52)
11 PRINT@451,"press any key to continue";:KB$=INKEY$
12 KB$=INKEY$:IF KB$=""THEN12ELSESOUND210,1:SC=6:SF=175:GOSUB10000
14 MOTORON:PRINT@68,"THE CASSETTE MOTOR IS ON";:PRINT@133,"PLEASE SET UP CURRENT";:PRINT@198,"DATA TAPE AND PRESS";:PRINT@267,"ANY KEY";:KB$=INKEY$
16 KB$=INKEY$:IF KB$=""THEN16ELSESOUND210,1:MOTOROFF:SC=2:SF=239:GOSUB10000:PRINT@228,"PRESS PLAY AND ANY KEY";:KB$=INKEY$
18 KB$=INKEY$:IF KB$=""THEN18ELSESOUND210,1:SC=4:SF=207:GOSUB10000:PRINT@233,"loading data";
20 OPEN"I",#-1,"DATA"
22 FORA=1TO26:INPUT#-1,L$(A),E$(A),D$(A):NEXTA
24 FORA=1TO52:INPUT#-1,O$(A),P(A),C(A),S(A):NEXTA
26 FORA=1TO11:INPUT#-1,A$(A),B$(A):NEXTA
28 INPUT#-1,N,I,V$
30 CLOSE#-1:SC=3:SF=35:GOSUB10000
35 PRINT@168,"can your dragon";:PRINT@230,"handle double speed";:PRINT@301,"y/n?";:DS$=INKEY$
40 DS$=INKEY$:IF DS$<>"N" AND DS$<>"Y"THEN40 ELSESOUND210,1:CLS0
45 IF DS$="Y"THENPOKE&HFFD7,0
52 AA$="U6R4D3NL4D3BR2":BB$="U6R3F1D1G1NL3F1D1G1NL3BR3":CC$="NR4U6R4BD6BR2":DD$="NR3U6R3F1D4BD1BR2":EE$="NR4U3NR3U3R4BD6BR2":FF$="U3NR3U3R4BD6BR2":GG$="U6R4BD3NL1D3NL4BR2":HH$="U6BR4D3NL4D3BR2":II$="NR4BU6R2ND6R2BR2BD6":JJ$="R2U6NL2R2BD6BR2"
53 KK$="U6BR4G3R1F2D1BR2"
54 LL$="NU6R4BR2":MM$="U6F2E2D6BR2":NN$="U6F1D1F2D1F1NU6BR2":OO$="U6R4D6NL4BR2":PP$="U6R3F1D1G1NL3BD3BR3":QQ$="U6R4D6NL4NH2BR2":RR$="U3NR3U3R3F1D1G2F2BR2":SS$="R3E1U1H1L2H1U1E1R3BD6BR2":TT$="BR2U6NL2R2BD6BR2":UU$="BU1U5BR4D5G1NL2BR3"
56 VV$="BR2H2U4BR4D4G2BR4":WW$="NE2U6BR4D6NH2BR2":XX$="U1E4U1BL4D1F4D1BR2":YY$="BR2U4NH2E2BD6BR2":ZZ$="BU6R4D1G4D1R4BR4"
100 PMODE4,1:PCLS
105 FOR A=1TO16:READ X1:NEXTA
107 FOR A=1TO4:READ X1,Y1,X2,Y2:LINE(X1,Y1)-(X2,Y2),PSET:NEXTA
110 FORA=1TO12:READ X1,Y1,X2,Y2:LINE(X1+3,Y1)-(X2+3,Y2),PSET:NEXTA
112 CIRCLE(218,95),9:DX=30:DY=88:OS$="VISIBLE OBJECTS ARE":GOSUB10010:DX=2:DY=38:OS$="I AM.....":GOSUB10010:DX=2:DY=168:OS$="TELL ME WHAT TO DO...":GOSUB10010
114 DX=185:DY=41:OS$="OBVIOUS*EXITS ARE...":GOSUB10010
150 FOR Z=&H1E00 TO &H1E72:READ MC$:POKE Z,VAL("&H"+MC$):NEXTZ
155 RESTORE
200 SCREEN1,1:COLOR0,5:EXEC&H1E00:LINE(173,40)-(177,78),PSET,BF:FOR A=1TO4:READ X1,Y1,X2,Y2:LINE(X1,Y1)-(X2,Y2),PSET,BF:NEXTA:COLOR5,0:RESTORE
201 DX=2:DY=12:OS$=Q$:GOSUB10010:Q$=""
202 DX=2:DY=50:OS$=L$(N):GOSUB10010
204 DX=2:DY=100:QX=0:FORZ=1TO52:IF P(Z)=N AND S(Z)=1 AND QX<5 THEN OS$=MID$(O$(Z),4):QX=QX+1:GOSUB10010:DY=DY+10
206 NEXTZ
208 FORZ=1TO LEN(E$(N))
210 W$=MID$(E$(N),Z,1):IF W$="N"THEN DX=216:DY=68:OS$=W$:GOSUB10010
212 IF W$="E"THEN  DX=246:DY=98:OS$=W$:GOSUB10010
214 IF W$="S"THEN DX=216:DY=130:OS$=W$:GOSUB10010
216 IF W$="W"THEN DX=185:DY=98:OS$=W$:GOSUB10010
218 NEXTZ:W$=""
222 PLAY"V30O2T20ABC":GOSUB10200
224 IF LEN(J$)<3 THEN240
226 IF ASC(J$)=32 THENSOUND1,4:GOTO200
228 FOR A=1TO LEN(J$)
230 IF MID$(J$,A,1)=" "THEN242
232 NEXTA
234 IF LEFT$(J$,3)="INV"THEN3500
236 IF LEFT$(J$,3)="HEL"THEN3700
238 IF LEFT$(J$,3)="QUI"THEN7900
240 SOUND1,4:Q$="REPLY USING TWO WORDS.":GOTO200
242 R$=LEFT$(J$,A-1):IF LEN(R$)<3 THEN R$=R$+" "
244 Y$=LEFT$(R$,3)
246 S$=MID$(J$,A+1):IF LEN(S$)<3 THEN S$=S$+" "
248 IF ASC(S$)=32 THEN Q$="USE ONE SPACE BETWEEN WORDS":SOUND1,4:GOTO200
250 Z$=LEFT$(S$,3)
252 FOR A=1TO LEN(V$)STEP3
254 IF Y$<>MID$(V$,A,3)THENNEXTA:Q$="I CAN NOT "+R$+" "+S$:SOUND1,4:GOTO200
256 K=((A-1)/3)+1
258 FORA=1TO52:IF Z$<>LEFT$(O$(A),3)THENNEXTA:Q$="WHAT A SILLY IDEA!!":SOUND1,4:GOTO200
260 L=A
280 ON K GOTO 300,500,700,900,1100,1300,1500,1700,1900,2100,2300,2500,2700,2900,3100,3300,3500,3700,3900,4100,4300,4500,4700,4900,5100,5300,5500,5700,5900,6100,6300,6500,6700,6900,7100,7300,7500,7700,7900,8100,8300,8500,
290 STOP
300 IF FNT(L)=0 THEN9520
302 IF L=18 AND C(18)=-1 THEN C(18)=-2:E$(14)=E$(14)+"C":D$(14)=D$(14)+"15":GOTO9510
304 IF L=25 AND C(25)=-2 THEN C(25)=-3:E$(23)=E$(23)+"G":D$(23)=D$(23)+"24":GOTO9510
310 SOUND1,4:Q$="I CANNOT OPEN THAT.":GOTO200
500 IF FNT(L)=0 THENGOTO9520
502 IF P(11)<>50 THENSOUND1,4:Q$="NOTHING TO KILL WITH.":GOTO200
504 IF L=15 AND N=12 AND C(15)=-1 THEN C(15)=-2:C(11)=2:O$(15)="COWA DEAD COW.":O$(11)="KNIA BLOODY KNIFE.":GOTO9510
506 SOUND1,4:Q$="I CANNOT KILL THAT!":GOTO200
700 FORZ=1TO11
702 IF Z$<>A$(Z) THEN NEXTZ:Z=0:GOTO9500
704 W$=B$(Z)
706 FORZ=1 TO LEN(E$(N))
708 IF W$<>MID$(E$(N),Z,1) THENNEXTZ:Z=0:GOTO9500
710 N=VAL(MID$(D$(N),(Z*2)-1,2))
712 GOTO9510
900 GOTO700
1100 IF P(L)=60 THENSOUND1,4:Q$="I ALREADY AM.":GOTO200
1104 IF P(L)<>50 THEN9537
1108 IF L=2 AND C(L)=1 THEN P(L)=60:C(L)=2:GOTO9510
1112 IF L=28 AND C(L)=1 THEN P(L)=60:C(L)=2:GOTO9510
1114 IF L=23 AND C(L)=1 THEN P(L)=60:C(L)=2:GOTO9510
1116 IF L=31 AND C(L)=1 THEN P(L)=60:C(L)=2:GOTO9510
1117 SOUND1,4:Q$="I CANNOT WEAR THAT!":GOTO200
1300 IF FNS(L)=-1 THENSOUND1,4:Q$="I HAVE ALREADY GOT IT.":GOTO200
1301 IF P(L)<>N THEN9520
1302 IF L=22 THEN S(L)=1:GOTO1310
1305 IF L=24 THEN C(L)=1:GOTO1310
1306 IF L=23 AND C(L)=-1 AND P(2)=60 THEN C(L)=1:GOTO1310
1307 IF L=23 AND C(L)=-1 AND P(2)<>60 THENSOUND1,4:Q$="IT IS TOO HOT TO TAKE!!":GOTO200
1308 IF L=7 THEN C(L)=2:GOTO1310
1309 IF I>5 THENSOUND1,4:Q$="I AM CARRYING TOO MUCH.":GOTO200
1310 IF C(L)<0 THENSOUND1,4:Q$="THAT STAYS WHERE IT IS!!":GOTO200
1312 P(L)=50:I=I+1:GOTO9510
1500 IF FNS(L)=0 THENSOUND1,4:Q$="I HAVE NOT GOT IT.":GOTO200
1501 IF L=7 AND N=6 THEN C(L)=1:P(L)=N:I=I-1:SOUND100,2:SOUND50,2:SOUND100,2:Q$="IT IS IN ITS TORCH HOLDER.":GOTO200
1502 IF L=2 THEN C(L)=1:GOTO1510
1503 IF L=23 THEN C(L)=1:GOTO1510
1504 IF L=28 THEN C(L)=1:GOTO1510
1505 IF L=31 THEN C(L)=1
1510 P(L)=N:I=I-1:GOTO9510
1700 IF FNT(L)=0 THEN9520
1702 IF P(11)<>50 THENSOUND1,4:Q$="NOTHING TO CUT WITH.":GOTO200
1703 IF L=15 AND C(L)=-1 THENSOUND60,2:SOUND10,2:Q$="WITHOUT A CAUSE..!":GOTO200
1704 IF C(11)<>2 THENSOUND1,4:Q$="THE KNIFE WILL NOT CUT IT.":GOTO200
1705 IF L=35 AND N=26 AND C(L)=-1 THEN P(L)=N:C(L)=1:S(L)=1:GOTO200
1707 IF L=13 AND C(13)=-1 THENSOUND200,2:SOUND100,2:SOUND200,2:Q$="BE GENTLE!!":GOTO200
1709 IF L=27 AND C(27)=-1 THENPLAY"O5T20V30BBBBBBBBBB":Q$="SHE LAUGHS AT ME...":GOTO200
1730 GOTO6300
1900 IF P(L)<>N THEN9520
1910 IF L=33 THENSOUND1,4:Q$="IT CANNOT TAKE MY WEIGHT!":GOTO200
1920 IF L=32 THEN N=23:GOTO9510
1930 IF L=12 THENSOUND1,4:Q$="IT IS TOO SLIPPERY!":GOTO200
1940 IF L=45 AND N=19 THENSOUND1,4:Q$="I AM NOT GOING DOWN THERE!!":GOTO200
1950 SOUND1,4:Q$="I CANNOT CLIMB THAT.":GOTO200
2100 IF P(L)<>50 THEN9537
2110 IF L=24 AND C(L)=1 AND N=19 THEN P(L)=19:C(L)=2:I=I-1:SOUND200,2:SOUND100,2:SOUND50,2:Q$="IT JUST SPANS ACROSS THE PIT.":GOTO200
2120 GOTO1500
2300 IF P(L)<>50 THEN9537
2310 IF L=29 AND N=25 AND C(48)=-1 THEN9530
2320 IF L=29 AND N=25 AND C(48)=-2 THEN C(48)=-3:I=I-1:P(33)=N:P(L)=40:GOTO9510
2330 IF L=13 THENSOUND1,4:Q$="I AM AFRAID THEY WILL NOT GROW AGAIN.":GOTO200
2340 SOUND1,4:Q$="YOU CANNOT PLANT THAT!!":GOTO200
2500 IF P(L)<>N THEN9520
2505 IF P(14)<>50 THENSOUND1,4:Q$="I CANNOT DIG WITH MY BARE HANDS.":GOTO200
2510 IF L=46 AND C(11)=-1 THEN P(11)=9:C(11)=1:GOTO9510
2520 IF L=48 AND C(L)=-1 THEN C(L)=-2:L$(25)=L$(25)+"*WITH A SMALL HOLE.":GOTO9510
2530 SOUND1,4:Q$="I CANNOT DIG THERE!":GOTO200
2700 IF FNT(L)=0 THEN9520
2730 SOUND100,3:SOUND20,3:Q$="I AM PROBABLY WRONG,*BUT THIS SEEMS USELESS.":GOTO200
2900 IF P(L)<>50 THEN9537
2910 IF L=9 AND C(L)=1 AND N=16 THEN C(L)=2:SOUND200,3:Q$="THEY ARE FULL OF ALKALI.":GOTO200
2920 IF L=1 AND C(L)=1 AND N=13 THEN C(L)=2:SOUND255,4:Q$="IT IS FULL OF MAGIC WATER.":GOTO200
2930 IF L=9 AND N=13 THENSOUND1,4:Q$="MAGIC WATER CORRODES METAL!!":GOTO200
2940 SOUND1,4:Q$="I CANNOT FILL THAT.":GOTO200
3100 IF P(9)<>50 THENSOUND1,4:Q$="I HAVE NOT GOT ANYTHING TO BLOW WITH.":GOTO200
3110 IF FNT(L)=0 THEN9520
3120 IF L=19 AND C(19)=-1 AND N=15 AND C(9)=2 THEN C(9)=1:C(19)=-2:O$(L)="SKEPARTS OF A SKELETON.":E$(15)=E$(15)+"D":D$(15)=D$(15)+"17":GOTO9510
3125 IF L=9 AND N=15 THEN9530
3130 SOUND10,5:SOUND50,5:SOUND10,5:Q$="PUFF!!":GOTO200
3300 IF P(10)<>50 THENSOUND1,4:Q$="NOTHING TO SWEEP WITH.":GOTO200
3305 IF P(L)<>N THEN9520
3310 IF L=3 AND N=4 THENSOUND200,3:SOUND50,3:SOUND200,6:O$(L)="PARAN OLD PARCHMENT.":Q$="I CAN NOW SEE SOMETHING!!":C(3)=1:GOTO200
3320 IF L=38 AND C(L)=-1 THENSOUND210,4:C(L)=-2:L$(2)="ON A PATH WITH A TUNNEL.":E$(2)=E$(2)+"A":Q$="IT IS NOW SWEPT.":GOTO200
3330 SOUND1,4:Q$="I AM NOT THAT HOUSE PROUD!!":GOTO200
3500 IF I=0 THEN Q$="YOU HAVE NOT GOT ANYTHING!!":SOUND1,4:GOTO200
3505 CLS:PRINT@10,"inventory":PRINT@64,"I AM CARRYING :-":PRINT@128,"";
3510 FORZ=1TO52:IF P(Z)=50 THENPRINTMID$(O$(Z),4)
3520 NEXTZ
3525 GOSUB10300
3530 CLS:PRINT@10,"inventory":PRINT@64,"I AM WEARING :-":PRINT@128,"";
3540 FORZ=1TO52:IF P(Z)=60 THENPRINTMID$(O$(Z),4)
3550 NEXTZ
3555 GOSUB10300:CLS:GOTO200
3700 CLS:PRINT"helpHELPhelpHELPhelpHELPhelpHELP"
3705 IF P(13)=50 THENPRINT@224,"FLOWERS HAVE SOME AMAZING POWERS"
3710 IF P(11)=50 AND C(11)=2 THENPRINT@225,"A BLOODY KNIFE COMBATS STRIFE!"
3715 IF N=26 AND C(27)=-1 THENPRINT@192,"TO EXORCIZE EVIL AND ITS POWERS ",,"STOP, WHY NOT TRY BURNING A LOCK"
3720 IF N=6 THENPRINT@193,"THERE'S MORE TO THIS ROOM AND",,,"  ITS CONTENTS THAN YOU THINK!"
3730 GOSUB10300:CLS:GOTO200
3900 IF L=30 AND C(12)=-1 AND N=10 AND P(8)=50 THEN P(L)=50:I=I+1:C(12)=-2:C(L)=1:S(L)=1:SOUND210,1:Q$="I HAVE GOT IT!!":GOTO200
3905 IF P(L)<>N THEN9520
3910 SOUND1,4:Q$="I CANNOT LANCE THAT.":GOTO200
4100 IF P(7)<>50 THENSOUND1,4:Q$="I HAVE NOTHING TO BURN WITH.":GOTO200
4110 IF FNS(L)=0 THEN9537
4120 IF L=35 AND N=26 THEN C(27)=1:C(28)=1:O$(27)="WITA DEAD WITCH.":Q$="IT BURNS WITH A GREEN VAPOUR,*THIS GASSES THE WITCH.":FORWB=1TO10:SOUNDRND(10)+240,1:NEXTWB:P(L)=40:I=I-1:GOTO200
4130 SOUND1,4:Q$="ARSON IS NOT PERMITTED!!":GOTO200
4300 IF L=36 AND N=14 THENCLS:PRINT@9,"coded message":PRINT@128,"GL UIVV GSV KIRMXVHH, GL YV Z","HFXXVHH, BLF NFHG IVZW GSV","KHZON, DVZI Z XOLZP, XILHH ZMW Z OFXPB XSZIN, ZMW HGZY GSV LMV","DSLHV WLRMT GSV SZIN.":GOSUB10300:CLS:GOTO200
4310 IF P(L)<>50 THEN9537
4320 IF L=22 AND C(22)=1 AND N=24 THEN C(22)=2:PRINT"":FOR CT=1TO10:CLS(RND(8)):SOUNDRND(10),1:NEXTCT:Q$="I HEAR A MASSIVE CRACK OF THUNDER*ECHO THROUGH THE ROOM!!":CLS:GOTO200
4325 IF L=22 AND C(22)=1 AND N<>24 THEN9530
4330 IF L=4 THENCLS:PRINT@6,"handwritten letter":PRINT@160,"I, THE KING, LEAVE ALL MY LANDS,POSSESSIONS AND MY DAUGHTER'S","HAND IN MARRIAGE TO THE PERSON","WHO CAN FIND AND RECOVER HER","SAFELY.....GOOD LUCK.":GOSUB10300:CLS:GOTO200
4335 IF L=3 THENSOUND50,1:SOUND100,1:SOUND150,1:Q$="IF A CODED MESSAGE YOU JUST CANNOT GET,*WHY NOT REVERSE THE ALPHABET!!":GOTO200
4340 SOUND1,4:Q$="I CANNOT READ THAT.":GOTO200
4500 GOTO1300
4700 IF P(L)<>N THEN9520
4710 IF L=13 AND C(L)=-1 AND N=11 THEN C(L)=1:P(L)=50:I=I+1:GOTO9510
4720 IF L=34 AND C(L)=-1 AND N=13 THEN C(L)=1:P(L)=50:I=I+1:GOTO9510
4730 SOUND1,4:Q$="I CANNOT PICK THAT.":GOTO200
4900 IF P(30)<>50 THENSOUND1,4:Q$="I HAVE NO KEY!":GOTO200
4910 IF P(L)<>N THEN9520
4920 IF L=25 AND C(L)=-1 AND N=23 THEN C(L)=-2:GOTO9510
4930 SOUND1,4:Q$="I CANNOT UNLOCK THAT.":GOTO200
5100 IF P(L)<>N THEN9520
5105 IF L=48 AND C(L)=-3 AND P(1)=50 AND C(1)=2 THEN C(L)=-4:P(32)=25:P(33)=40:C(1)=1:FORSF=1TO200STEP10:SOUNDSF,1:NEXTSF:Q$="WOW!!!":GOTO200
5110 SOUND1,4:Q$="I CANNOT WATER THAT.":GOTO200
5300 IF FNT(L)=0 THEN9520
5310 SOUND1,4:Q$="I FEEL NOTHING SPECIAL.":GOTO200
5500 GOTO5300
5700 GOTO3300
5900 IF FNT(L)=0 THEN9520
5902 IF L=3 THEN9538
5905 SOUND1,4:Q$="IF I WERE THAT HUNGRY I WOULD HAVE BROUGHT*THE RUBBISH FROM THE CASTLE!!":GOTO200
6100 IF P(L)<>N THEN9520
6110 IF L=15 AND C(L)=-1 THENPLAY"V31O1T30ABABABABABAB":Q$="COW BASHING, TUT! TUT!":GOTO200
6120 IF L=1 THENSOUND200,4:SOUND100,4:SOUND1,4:Q$="GOOD PUN!!":GOTO200
6130 SOUND1,4:SOUND1,4:Q$="YOU MUST TRY NOT TO BE SO AGGRESSIVE!":GOTO200
6300 IF FNT(L)=0 THEN9520
6302 IF P(11)<>50 THENSOUND1,4:Q$="NOTHING TO STAB WITH!!":GOTO200
6303 IF L=15 AND C(L)=-1 THENSOUND1,4:Q$="FOR WHAT PURPOSE....":GOTO200
6305 IF L=34 AND P(L)=50 AND C(31)=2 AND C(28)=2 AND C(23)=2 AND C(22)=2 AND P(22)=50 AND N=24 THEN9600
6307 IF L=34 AND P(L)=50 AND N<>24 THEN9530
6310 IF L=19 AND C(L)=-1 THENSOUND1,4:Q$="IT GOES RIGHT THROUGH HIM!!!":GOTO200
6320 IF L=21 AND C(L)=-1 THENSOUND1,4:Q$="I CANNOT GET CLOSE ENOUGH...*IT SMELLS SO BAD.":GOTO200
6330 IF L=26 THENSOUND1,4:Q$="THE WHOLE POINT OF THIS WAS TO SAVE*HER, YOU SILLY LITTLE ADVENTURER!":GOTO200
6350 GOTO9539
6500 IF P(L)<>N THEN9520
6505 IF L=17 OR L=20 THENSOUND1,4:Q$="I AM NOT DRINKING THAT! IT MIGHT HAVE*SOME STRANGE EFFECT ON ME.":GOTO200
6510 GOTO9538
6700 IF P(L)<>N THEN9520
6710 IF L>36 THENSOUND1,4:Q$="I AM NOT BIONIC!!":GOTO200
6715 GOTO9538
6900 IF FNT(L)=0 THEN9520
6905 IF L=5 AND C(L)=1 THEN C(L)=2:P(6)=N:GOTO9510
6910 IF L=21 AND C(L)=-1 THENSOUND1,4:Q$="I CANNOT GET CLOSE ENOUGH...*IT SMELLS SO BAD.":GOTO200
6915 IF L=21 AND C(L)=-2 THEN P(31)=N:C(31)=1:C(L)=-3:GOTO9510
6916 IF L=16 AND C(L)=1 THEN P(29)=N:C(L)=2:C(29)=1:SOUND100,3:SOUND50,3:SOUND100,3:Q$="I HAVE FOUND SOMETHING!":GOTO200
6920 IF L=27 AND C(L)=-1 THENSOUND1,4:Q$="SHE MIGHT TURN ME INTO A FROG.":GOTO200
6925 IF L=27 AND C(L)=1 THENSOUND1,4:Q$="I AM NOT SEARCHING A DEAD BODY....*ESPECIALLY NOT A WITCH.":GOTO200
6930 IF L=32 THENSOUND1,4:Q$="I CAN ONLY FIND BITS OF OLD BIRD NEST*AND TWIGS...":GOTO200
6935 SOUND1,4:Q$="SORRY SHERLOCK...NOTHING SPECIAL HERE.":GOTO200
7100 IF FNT(L)=0 THEN9520
7110 IF L=7 AND C(L)=1 AND C(40)=-1 THENSOUND255,10:SOUND10,4:C(40)=-2:L$(6)=L$(6)+"*AND A SECRET ROOM!!":E$(6)=E$(6)+"B":Q$="LOOK WHAT I HAVE FOUND!!!":GOTO200
7150 SOUND1,4:Q$="I AM NOT A BELL RINGER.":GOTO200
7300 IF P(L)<>50 THEN9537
7310 IF L=13 AND C(21)=-1 AND N=18 THEN C(21)=-2:SOUND250,4:Q$="THAT IS BETTER.":O$(21)="MONA SWEET SMELLING MONSTER.":GOTO200
7320 SOUND1,4:Q$="WAVING THAT HAD NO EFFECT.":GOTO200
7500 GOTO1500
7700 IF P(L)<>N THEN9520
7710 IF L=44 AND C(L)=-1 THEN C(L)=-2:L$(21)="NOW IN A STONE CORRIDOR*WITH A SMALL DOOR.":E$(21)=E$(21)+"F":D$(21)=D$(21)+"22":SOUND100,4:SOUND10,4:Q$="WHAT IS THIS!!!":GOTO200
7720 SOUND1,4:Q$="NOTHING SPECIAL HAPPENED...":GOTO200
7900 SC=5:SF=42:GOSUB10000:PRINT@170,"do*you*wish";:PRINT@231,"to*save*this*game";:PRINT@298,"on*tape*y/n?";:KB$=INKEY$
7902 KB$=INKEY$:IF KB$<>"Y"AND KB$<>"N"THEN7902ELSESOUND210,1
7904 IF KB$="Y"THENGOSUB10400
7906 SC=6:SF=42:GOSUB10000:PRINT@170,"do*you*wish";:PRINT@232,"to*continue*with";:PRINT@297,"this*game*y/n?";:KB$=INKEY$
7908 KB$=INKEY$:IF KB$<>"Y"AND KB$<>"N"THEN7908ELSESOUND210,1
7920 IF KB$="Y"THEN200
7925 CLS:POKE&HFFD6,0:END
8100 GOTO1500
8300 IF P(L)<>N THEN9520
8310 IF L=24 AND C(L)=2 THEN N=20:GOTO9510
8320 IF L=47 THENSOUND1,4:Q$="I WOULD NOT DARE!":GOTO200
8330 SOUND1,4:Q$="I CAN NOT CROSS THAT.":GOTO200
8500 IF FNT(L)=0 THEN9520
8505 IF L=28 AND N=26 AND C(L)=-1 THENSOUND10,10:Q$="IT IS LEVITATING IN MID AIR!!":GOTO200
8510 IF L=18 AND N=14 THENSOUND200,1:SOUND100,1:SOUND200,1:Q$="I SEE A MESSAGE.":GOTO200
8520 IF L=12 AND N=10 THENSOUND10,3:SOUND30,3:SOUND50,3:Q$="AT THE VERY TOP I SEE A KEY.":GOTO200
8525 IF L=34 AND P(L)=50 THENSOUND200,3:SOUND50,3:SOUND200,3:SOUND50,3:Q$="IT LOOKS LIKE THE DEVIL HIMSELF!!":GOTO200
8550 IF L>35 THENSOUND210,1:Q$=MID$(O$,4):GOTO200
8560 SOUND1,4:Q$="I SEE NOTHING SPECIAL..":GOTO200
9005 DATA 213,60,223,68,245,90,254,100,213,122,223,131,181,90,190,100
9010 DATA 0,30,255,30,0,80,180,80,0,160,255,160,180,30,180,160,215,70,215,86,215,70,221,89,221,89,240,95,240,95,223,95,240,95,221,101,221,101,215,120,215,120,215,103,215,120,209,101,190,95,209,101,190,95,206,95,190,95,209,89,209,89,215,70
9020 DATA 8E,06,A0,BF,23,D8,86,20,B7,23,E0,8E,09,40,BF,23,E8,BD,1E,51,8E,0B,00,BF,23,D8,86,16,B7,23,E0,8E,0F,C0,BF,23,E8,BD,1E,51,8E,11,40,BF,23,D8,86,16,B7,23,E0,8E,19,C0,BF,23,E8,BD,1E,51,8E,1B,40,BF
9025 DATA 23,D8,86,20,B7,23,E0,8E,1D,20,BF,23,E8,BD,1E,51,39,C6,01,86,00,BE,23,D8,A7,80,5C,F1,23,E0,22,02,20,F6,BE,23,D8,30,88,20,BC,23,E8,22,05,BF,23,D8,20,DF,39
9500 SOUND1,4:Q$="SORRY, BUT I CAN NOT GO THERE.":GOTO200
9510 SOUND100,2:SOUND50,2:SOUND100,2:Q$="O.K. IT IS DONE...":GOTO200
9520 SOUND1,4:Q$="I CAN NOT SEE ONE OF THOSE HERE!!":GOTO200
9530 SOUND1,4:Q$="I CANNOT DO THAT YET.":GOTO200
9537 SOUND1,4:Q$="I AM NOT CARRYING IT.":GOTO200
9538 SOUND1,4:Q$="YOU MUST BE JOKING!!":GOTO200
9539 SOUND1,4:Q$="I AM JUST NOT CAPABLE OF THAT.":GOTO200
9600 SCREEN1,1:PMODE3,1:FORSF=1TO8:SCREEN1,1:PLAY"O5T100V31ABCABCABCDDDEF":SCREEN1,0:PLAY"O1GGGFFFDDDEEECCCBBBAAA":NEXTSF
9610 CLS:PRINT"":SOUND255,10:PRINT@10,"***********";:PRINT@42,"*well done*";:PRINT@74,"***********";:PRINT@128,"AS I STAB THE MANDRAKE ROOT THE","WHOLE ROOM SHUDDERS...A THICK","GREEN SMOKE FILLS THE AIR, AND"
9615 PRINT"WHEN IT CLEARS BEELZEBUB IS NO","LONGER THERE!!":PRINT"THE PRINCESS IS NOW FREED AND","I CAN CLAIM MY WEALTH AND","HAPPINESS......thank you.":END
10000 CLS(SC):FORSC=33TO449STEP32:PRINT@SC,STRING$(30,SF);:NEXT:RETURN
10010 IF OS$=""THENRETURN ELSEDRAW"BM"+STR$(DX)+","+STR$(DY)
10020 FORA=1TO LEN(OS$)
10030 SI$=MID$(OS$,A,1)
10040 IF SI$=" "THENDRAW"BR6":GOTO10095
10050 IF SI$="."THEN DRAW"U1R1D1L1BR6":GOTO10095
10060 IF SI$=","THEN DRAW"U1R1D1ND1L1BR6":GOTO10095
10070 IF SI$="!"THEN DRAW"BR1U1BU2U3BD6BR5":GOTO10095
10080 IF SI$="*"THEN DY=DY+10:DRAW"BM"+STR$(DX)+","+STR$(DY):GOTO10095
10090 DRAW"X"+SI$+SI$+"$;"
10095 NEXTA
10100 RETURN
10200 KB$=INKEY$:DX=2:DY=182:J$=""
10205 KB$=INKEY$:IF KB$=""THEN10205 ELSEIF ASC(KB$)=13 THENSOUND210,1:RETURN
10207 IF ASC(KB$)=8 THEN10225
10210 IF (ASC(KB$)<65 OR ASC(KB$)>90) AND ASC(KB$)<>32 THENSOUND1,4:GOTO10205
10215 OS$=KB$:GOSUB10010:DX=DX+6
10220 J$=J$+KB$:IF LEN(J$)<40 THEN10205
10225 IF J$=""THEN10205ELSE COLOR0,5:DX=DX-6:LINE(DX,DY-7)-(DX+6,DY),PSET,BF:COLOR5,0
10230 J$=MID$(J$,1,LEN(J$)-1):GOTO10205
10300 PRINT@483,"press any key to continue";:KB$=INKEY$
10305 KB$=INKEY$:IF KB$=""THEN10305ELSESOUND210,1:RETURN
10400 SC=6:SF=175:GOSUB10000:MOTORON:PRINT@68,"THE CASSETTE MOTOR IS ON";:PRINT@135,"PLEASE SET UP NEW";:PRINT@198,"DATA TAPE AND PRESS";:PRINT@267,"ANY KEY";:KB$=INKEY$
10405 KB$=INKEY$:IF KB$=""THEN10405ELSESOUND210,1:MOTOROFF:SC=2:SF=239:GOSUB10000:PRINT@196,"PRESS PLAY AND RECORD";:PRINT@265,"AND ANY KEY";:KB$=INKEY$
10410 KB$=INKEY$:IF KB$=""THEN10410ELSESOUND210,1:SC=4:SF=207:GOSUB10000:PRINT@233,"saving data";
10412 IF DS$="Y"THENPOKE&HFFD6,0
10415 OPEN"O",#-1,"DATA"
10420 FORA=1TO26:PRINT#-1,L$(A),E$(A),D$(A):NEXTA
10425 FORA=1TO52:PRINT#-1,O$(A),P(A),C(A),S(A):NEXTA
10430 FORA=1TO11:PRINT#-1,A$(A),B$(A):NEXTA
10435 PRINT#-1,N,I,V$
10440 CLOSE#-1
10445 IF DS$="Y"THENPOKE&HFFD7,0
10500 RETURN
