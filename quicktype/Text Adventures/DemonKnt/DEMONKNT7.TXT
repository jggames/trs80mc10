0 CLEAR500:M=16384:POKE49151,64:GOTO10001
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+M-1:FORC1=1TOLEN(M$):POKECC+C1,ASC(MID$(M$,C1))AND63:NEXT:?@CC-M+C1,:RETURN
10 DIMCC,C1,M$,A$(11),B$(11),L$(26),E$(26),D$(26),O$(52),P(52),C(52),S(52):GOSUB10300
22 FORA=1TO26:READL$(A),E$(A),D$(A):NEXT:FORA=1TO52:READO$(A),P(A),C(A),S(A):NEXT:FORA=1TO11:READA$(A),B$(A):NEXT
28 N=1:I=0:V1$="OPESACGO ENTWEATAKDROCUTCLILAYPLADIGUSEFILSPRSWEINVHELLANBURREAGETPICUNLWATTOUFEECLEEATKICSTADRIJUMSEAPUL"
29 V$=V1$+"WAVTHRTAPQUILEACROLOOEXAKIL"
100 CLS:PRINT@32*7,;:M$="VISIBLE OBJECTS ARE:            ":GOSUB7:PRINT@32*3,;:M$="I AM...                         ":GOSUB7
110 PRINT@448,"OBVIOUS EXITS:";
200 DX=2:DY=12:M$=Q$:GOSUB10010:Q$=""
202 FORCC=4TO6:PRINT@CC*32,"":NEXT:DX=2:DY=50:M$=L$(N):GOSUB10010
203 FORCC=8TO12:PRINT@CC*32,"*":NEXT
204 DX=2:DY=100:QX=0:FORZ=1TO52:IFP(Z)=NANDS(Z)=1ANDQX<5THENM$=MID$(O$(Z),4):QX=QX+1:GOSUB10010:DY=DY+10
206 NEXTZ
208 POKE32*14+M+16,43:POKE32*13+M+16,32:POKE32*14+M+17,32:POKE32*15+M+16,32:POKE32*14+M+15,32
210 FORZ=1TOLEN(E$(N)):W$=MID$(E$(N),Z,1):IFW$="N"THENPOKE32*13+M+16,14
212 IFW$="E"THENPOKE32*14+M+17,5
214 IFW$="S"THENPOKE32*15+M+16,19
216 IFW$="W"THENPOKE32*14+M+15,23
218 NEXTZ:W$=""
222 GOSUB10200
224 IFLEN(J$)<3THEN240
226 IFASC(J$)=32THENSOUND1,4:GOTO200
228 FORA=1TOLEN(J$)
230 IFMID$(J$,A,1)=" "THEN242
232 NEXTA
234 IFLEFT$(J$,3)="INV"THEN3500
236 IFLEFT$(J$,3)="HEL"THEN3700
238 IFLEFT$(J$,3)="QUI"THEN7900
240 SOUND1,4:Q$="REPLY USING TWO WORDS.":GOTO200
242 R$=LEFT$(J$,A-1):IFLEN(R$)<3THENR$=R$+" "
244 Y$=LEFT$(R$,3)
246 S$=MID$(J$,A+1):IFLEN(S$)<3THENS$=S$+" "
248 IFASC(S$)=32THENQ$="USE ONE SPACE BETWEEN WORDS":SOUND1,4:GOTO200
250 Z$=LEFT$(S$,3)
252 FORA=1TOLEN(V$)STEP3
254 IFY$<>MID$(V$,A,3)THENNEXTA:Q$="I CANNOT "+R$+" "+S$:SOUND1,4:GOTO200
256 K=((A-1)/3)+1:A=255:NEXTA
258 FORA=1TO52:IFZ$<>LEFT$(O$(A),3)THENNEXTA:Q$="WHAT A SILLY IDEA!":SOUND1,4:GOTO200
260 L=A:A=52:NEXTA:A=L
280 ONKGOTO300,500,700,900,1100,1300,1500,1700,1900,2100,2300,2500,2700,2900,3100,3300,3500,3700,3900,4100,4300,4500
281 ONK-22GOTO4700,4900,5100,5300,5500,3300,5900,6100,6300,6500,6700,6900,7100,7300,7500,7700,7900,8100,8300,8500,8500,500,
290 STOP
300 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
302 IFL=18ANDC(18)=-1THENC(18)=-2:E$(14)=E$(14)+"C":D$(14)=D$(14)+"15":GOTO9510
304 IFL=25ANDC(25)=-2THENC(25)=-3:E$(23)=E$(23)+"G":D$(23)=D$(23)+"24":GOTO9510
310 SOUND1,4:Q$="I CANNOT OPEN THAT.":GOTO200
500 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
502 IFP(11)<>50THENSOUND1,4:Q$="NOTHING TO KILL WITH.":GOTO200
504 IFL=15ANDN=12ANDC(15)=-1THENC(15)=-2:C(11)=2:O$(15)="COWA DEAD COW.":O$(11)="KNIA BLOODY KNIFE.":GOTO9510
506 SOUND1,4:Q$="I CANNOT KILL THAT!":GOTO200
700 FORZ=1TO11
702 IFZ$<>A$(Z)THENNEXTZ:Z=0:GOTO9500
704 W$=B$(Z)
706 FORZ=1TOLEN(E$(N))
708 IFW$<>MID$(E$(N),Z,1)THENNEXTZ:Z=0:GOTO9500
710 N=VAL(MID$(D$(N),(Z*2)-1,2)):TM=Z:Z=254:NEXTZ:Z=TM
712 GOTO9510
900 GOTO700
1100 IFP(L)=60THENSOUND1,4:Q$="I ALREADY AM.":GOTO200
1104 IFP(L)<>50THEN9537
1108 IFL=2ANDC(L)=1THENP(L)=60:C(L)=2:GOTO9510
1112 IFL=28ANDC(L)=1THENP(L)=60:C(L)=2:GOTO9510
1114 IFL=23ANDC(L)=1THENP(L)=60:C(L)=2:GOTO9510
1116 IFL=31ANDC(L)=1THENP(L)=60:C(L)=2:GOTO9510
1117 SOUND1,4:Q$="I CANNOT WEAR THAT!":GOTO200
1300 IF(P(L)=50ORP(L)=60)=-1THENSOUND1,4:Q$="I HAVE ALREADY GOT IT.":GOTO200
1301 IFP(L)<>NTHEN9520
1302 IFL=22THENS(L)=1:GOTO1310
1305 IFL=24THENC(L)=1:GOTO1310
1306 IFL=23ANDC(L)=-1ANDP(2)=60THENC(L)=1:GOTO1310
1307 IFL=23ANDC(L)=-1ANDP(2)<>60THENSOUND1,4:Q$="IT IS TOO HOT TO TAKE!":GOTO200
1308 IFL=7THENC(L)=2:GOTO1310
1309 IFI>5THENSOUND1,4:Q$="I AM CARRYING TOO MUCH.":GOTO200
1310 IFC(L)<0THENSOUND1,4:Q$="THAT STAYS WHERE IT IS!":GOTO200
1312 P(L)=50:I=I+1:GOTO9510
1500 IF(P(L)=50ORP(L)=60)=0THENSOUND1,4:Q$="I HAVE NOT GOT IT.":GOTO200
1501 IFL=7ANDN=6THENC(L)=1:P(L)=N:I=I-1:SOUND100,2:SOUND50,2:SOUND100,2:Q$="IT IS IN ITS TORCH HOLDER.":GOTO200
1502 IFL=2THENC(L)=1:GOTO1510
1503 IFL=23THENC(L)=1:GOTO1510
1504 IFL=28THENC(L)=1:GOTO1510
1505 IFL=31THENC(L)=1
1510 P(L)=N:I=I-1:GOTO9510
1700 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
1702 IFP(11)<>50THENSOUND1,4:Q$="NOTHING TO CUT WITH.":GOTO200
1703 IFL=15ANDC(L)=-1THENSOUND60,2:SOUND10,2:Q$="WITHOUT A CAUSE..!":GOTO200
1704 IFC(11)<>2THENSOUND1,4:Q$="THE KNIFE WILL NOT CUT IT.":GOTO200
1705 IFL=35ANDN=26ANDC(L)=-1THENP(L)=N:C(L)=1:S(L)=1:GOTO200
1707 IFL=13ANDC(13)=-1THENSOUND200,2:SOUND100,2:SOUND200,2:Q$="BE GENTLE!":GOTO200
1709 IFL=27ANDC(27)=-1THENQ$="SHE LAUGHS AT ME...":GOTO200
1730 GOTO6300
1900 IFP(L)<>NTHEN9520
1910 IFL=33THENSOUND1,4:Q$="IT CANNOT TAKE MY WEIGHT!":GOTO200
1920 IFL=32THENN=23:GOTO9510
1930 IFL=12THENSOUND1,4:Q$="IT IS TOO SLIPPERY!":GOTO200
1940 IFL=45ANDN=19THENSOUND1,4:Q$="I AM NOT GOING DOWN THERE!":GOTO200
1950 SOUND1,4:Q$="I CANNOT CLIMB THAT.":GOTO200
2100 IFP(L)<>50THEN9537
2110 IFL=24ANDC(L)=1ANDN=19THENP(L)=19:C(L)=2:I=I-1:SOUND200,2:SOUND50,2:Q$="IT JUST SPANS ACROSS THE PIT.":GOTO200
2120 GOTO1500
2300 IFP(L)<>50THEN9537
2310 IFL=29ANDN=25ANDC(48)=-1THEN9530
2320 IFL=29ANDN=25ANDC(48)=-2THEN C(48)=-3:I=I-1:P(33)=N:P(L)=40:GOTO9510
2330 IFL=13THENSOUND1,4:Q$="I AM AFRAID THEY WILL NOT GROW AGAIN.":GOTO200
2340 SOUND1,4:Q$="YOU CANNOT PLANT THAT!":GOTO200
2500 IFP(L)<>NTHEN9520
2505 IFP(14)<>50THENSOUND1,4:Q$="I CANNOT DIG WITH MY BARE HANDS.":GOTO200
2510 IFL=46ANDC(11)=-1THENP(11)=9:C(11)=1:GOTO9510
2520 IFL=48ANDC(L)=-1THENC(L)=-2:L$(25)=LEFT$(L$(25),35)+" WITH A SMALL HOLE.":GOTO9510
2530 SOUND1,4:Q$="I CANNOT DIG THERE!":GOTO200
2700 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
2730 SOUND100,3:SOUND20,3:Q$="I AM PROBABLY WRONG, BUT THIS SEEMS USELESS.":GOTO200
2900 IFP(L)<>50THEN9537
2910 IFL=9ANDC(L)=1ANDN=16THEN C(L)=2:SOUND200,3:Q$="THEY ARE FULL OF ALKALI.":GOTO200
2920 IFL=1ANDC(L)=1ANDN=13THEN C(L)=2:SOUND255,4:Q$="IT IS FULL OF MAGIC WATER.":GOTO200
2930 IFL=9ANDN=13THENSOUND1,4:Q$="MAGIC WATER CORRODES METAL!":GOTO200
2940 SOUND1,4:Q$="I CANNOT FILL THAT.":GOTO200
3100 IFP(9)<>50THENSOUND1,4:Q$="I HAVE NOT GOT ANYTHING TO SPRAY WITH.":GOTO200
3110 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
3120 IFL=19ANDC(19)=-1ANDN=15ANDC(9)=2THEN3122
3121 GOTO3125
3122 C(9)=1:C(19)=-2:O$(L)="SKEPARTS OF A SKELETON.":E$(15)=E$(15)+"D":D$(15)=D$(15)+"17":GOTO9510
3125 IFL=9ANDN=15THEN9530
3130 SOUND10,5:SOUND50,5:SOUND10,5:Q$="PUFF!":GOTO200
3300 IFP(10)<>50THENSOUND1,4:Q$="NOTHING TO SWEEP WITH.":GOTO200
3305 IFP(L)<>NTHEN9520
3310 IFL=3ANDN=4THEN3312
3311 GOTO3320
3312 SOUND200,3:SOUND50,3:SOUND200,6:O$(L)="PARAN OLD PARCHMENT.":Q$="I CAN NOW SEE SOMETHING!":C(3)=1:GOTO200
3320 IFL=38ANDC(L)=-1THENSOUND210,4:C(L)=-2:L$(2)="ON A PATH WITH A TUNNEL.":E$(2)=E$(2)+"A":Q$="IT IS NOW SWEPT.":GOTO200
3330 SOUND1,4:Q$="I AM NOT THAT HOUSE PROUD!":GOTO200
3500 IFI=0THEN Q$="YOU HAVE NOT GOT ANYTHING!":SOUND1,4:GOTO200
3505 CLS:PRINT@10,"inventory":PRINT@64,"I AM CARRYING:":PRINT@128,"";
3510 FORZ=1TO52:IFP(Z)=50THENPRINTMID$(O$(Z),4)
3520 NEXTZ
3525 GOSUB10300
3530 CLS:PRINT@10,"inventory":PRINT@64,"I AM WEARING:":PRINT@128,"";
3540 FORZ=1TO52:IFP(Z)=60THENPRINTMID$(O$(Z),4)
3550 NEXTZ
3555 GOSUB10300:GOTO100
3700 CLS:PRINT"helpHELPhelpHELPhelpHELPhelpHELP"
3705 IFP(13)=50THENPRINT@192,"FLOWERS HAVE SOME AMAZING POWERS"
3706 IFN=20THENPRINT@192,"HOW ABOUT A HOLY BOOK OF SONGS?"
3710 IFP(11)=50ANDC(11)=2THENPRINT@225,"A BLOODY KNIFE COMBATS STRIFE!"
3715 IFN=26ANDC(27)=-1THENPRINT@192,"TO EXORCIZE EVIL AND ITS POWERS ",,"STOP, WHY NOT TRY BURNING A LOCK"
3720 IFN=6THENPRINT@193,"THERE'S MORE TO THIS ROOM AND",,,"  ITS CONTENTS THAN YOU THINK!"
3730 GOSUB10300:GOTO100
3900 IFL=30ANDC(12)=-1ANDN=10ANDP(8)=50THENP(L)=50:I=I+1:C(12)=-2:C(L)=1:S(L)=1:SOUND210,1:Q$="I HAVE GOT IT!":GOTO200
3905 IFP(L)<>NTHEN9520
3910 SOUND1,4:Q$="I CANNOT LANCE THAT.":GOTO200
4100 IFP(7)<>50THENSOUND1,4:Q$="I HAVE NOTHING TO BURN WITH.":GOTO200
4110 IF(P(L)=50ORP(L)=60)=0THEN9537
4120 IFL=35ANDN=26THEN4122
4121 GOTO4130
4122 C(27)=1:C(28)=1:O$(27)="WITA DEAD WITCH.":Q$="IT BURNS WITH A GREEN VAPOUR, THIS GASSES THE WITCH."
4123 FORWB=1TO10:SOUNDRND(10)+240,1:NEXTWB:P(L)=40:I=I-1:GOTO200
4130 SOUND1,4:Q$="ARSON IS NOT PERMITTED!":GOTO200
4300 IFL=36ANDN=14THEN4302
4301 GOTO4310
4302 CLS:PRINT@9,;:M$="CODED MESSAGE":GOSUB7
4303 PRINT@128,"GL UIVV GSV KIRMXVHH, GL YV Z","HFXXVHH, BLF NFHG IVZW GSV",
4304 PRINT"KHZON, DVZI Z XOLZP, XILHH ZMW Z OFXPB XSZIN, ZMW HGZY GSV LMV","DSLHV WLRMT GSV SZIN.":GOSUB10300:GOTO100
4310 IFP(L)<>50THEN9537
4320 IFL=22ANDC(22)=1ANDN=24THEN4322
4321 GOTO4325
4322 C(22)=2:PRINT"":FORCT=1TO10:CLS(RND(8)):SOUNDRND(10),1:NEXTCT
4323 Q$="I HEAR A MASSIVE CRACK OF THUNDER ECHO THROUGH THE ROOM!":GOTO100
4325 IFL=22ANDC(22)=1ANDN<>24THEN9530
4330 IFL=4THEN4332
4331 GOTO4335
4332 CLS:PRINT@6,;:M$="HANDWRITTEN LETTER":GOSUB7:PRINT@160,"I, THE KING, LEAVE ALL MY LANDS,POSSESSIONS AND MY DAUGHTER'S",
4334 PRINT"HAND IN MARRIAGE TO THE PERSON","WHO CAN FIND AND RECOVER HER","SAFELY....GOOD LUCK.":GOSUB10300:GOTO100
4335 IFL=3THEN4337
4336 GOTO4340
4337 SOUND50,1:SOUND100,1:SOUND150,1:Q$="IF A MESSAGE YOU CANNOT GET, JUST REVERSE THE ALPHABET!":GOTO200
4340 SOUND1,4:Q$="I CANNOT READ THAT.":GOTO200
4500 GOTO1300
4700 IFP(L)<>NTHEN9520
4710 IFL=13ANDC(L)=-1ANDN=11THENC(L)=1:P(L)=50:I=I+1:GOTO9510
4720 IFL=34ANDC(L)=-1ANDN=13THENC(L)=1:P(L)=50:I=I+1:GOTO9510
4730 SOUND1,4:Q$="I CANNOT PICK THAT.":GOTO200
4900 IFP(30)<>50THENSOUND1,4:Q$="I HAVE NO KEY!":GOTO200
4910 IFP(L)<>N THEN9520
4920 IFL=25ANDC(L)=-1ANDN=23THENC(L)=-2:GOTO9510
4930 SOUND1,4:Q$="I CANNOT UNLOCK THAT.":GOTO200
5100 IFP(L)<>NTHEN9520
5105 IFL=48ANDC(L)=-3ANDP(1)=50ANDC(1)=2THEN5107
5106 SOUND1,4:Q$="I CANNOT WATER THAT.":GOTO200
5107 C(L)=-4:P(32)=25:P(33)=40:C(1)=1:FORSF=1TO200STEP10:SOUNDSF,1:NEXTSF:L$(25)=LEFT$(L$(25),35)+".":Q$="WOW!":GOTO200
5300 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
5310 SOUND1,4:Q$="I FEEL NOTHING SPECIAL.":GOTO200
5500 GOTO5300
5900 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
5902 IFL=3THEN9538
5905 SOUND1,4:Q$="IF I WERE THAT HUNGRY I WOULD HAVE BROUGHT THE RUBBISH FROM THE CASTLE!":GOTO200
6100 IFP(L)<>NTHEN9520
6110 IFL=15ANDC(L)=-1THENQ$="COW BASHING, TUT! TUT!":GOTO200
6120 IFL=1THENSOUND200,4:SOUND100,4:SOUND1,4:Q$="GOOD PUN!":GOTO200
6130 SOUND1,4:SOUND1,4:Q$="YOU MUST TRY NOT TO BE SO AGGRESSIVE!":GOTO200
6300 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
6302 IFP(11)<>50THENSOUND1,4:Q$="NOTHING TO STAB WITH!":GOTO200
6303 IFL=15ANDC(L)=-1THENSOUND1,4:Q$="FOR WHAT PURPOSE...":GOTO200
6305 IFL=34ANDP(L)=50ANDC(31)=2ANDC(28)=2ANDC(23)=2ANDC(22)=2ANDP(22)=50ANDN=24THEN9600
6307 IFL=34ANDP(L)=50ANDN<>24THEN9530
6310 IFL=19ANDC(L)=-1THENSOUND1,4:Q$="IT GOES RIGHT THROUGH HIM!":GOTO200
6320 IFL=21ANDC(L)=-1THENSOUND1,4:Q$="I CANNOT GET CLOSE ENOUGH... IT SMELLS SO BAD.":GOTO200
6330 IFL=26THENSOUND1,4:Q$="THE WHOLE POINT OF THIS WAS TO SAVE HER!":GOTO200
6350 GOTO9539
6500 IFP(L)<>NTHEN9520
6505 IFL=17ORL=20THENSOUND1,4:Q$="I'M NOT DRINKING THAT! IT MIGHT HAVE SOME STRANGE EFFECT.":GOTO200
6510 GOTO9538
6700 IFP(L)<>NTHEN9520
6710 IFL>36THENSOUND1,4:Q$="I AM NOT BIONIC!":GOTO200
6715 GOTO9538
6900 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
6905 IFL=5ANDC(L)=1THENC(L)=2:P(6)=N:GOTO9510
6910 IFL=21ANDC(L)=-1THENSOUND1,4:Q$="I CANNOT GET CLOSE ENOUGH... IT SMELLS SO BAD.":GOTO200
6915 IFL=21ANDC(L)=-2THENP(31)=N:C(31)=1:C(L)=-3:GOTO9510
6916 IFL=16ANDC(L)=1THENP(29)=N:C(L)=2:C(29)=1:SOUND100,3:SOUND50,3:SOUND100,3:Q$="I HAVE FOUND SOMETHING!":GOTO200
6920 IFL=27ANDC(L)=-1THENSOUND1,4:Q$="SHE MIGHT TURN ME INTO A FROG.":GOTO200
6925 IFL=27ANDC(L)=1THENSOUND1,4:Q$="I AM NOT SEARCHING A DEAD BODY... ESPECIALLY NOT A WITCH.":GOTO200
6930 IFL=32THENSOUND1,4:Q$="I CAN ONLY FIND BITS OF OLD BIRD NEST AND TWIGS...":GOTO200
6935 SOUND1,4:Q$="SORRY SHERLOCK...NOTHING SPECIAL HERE.":GOTO200
7100 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
7110 IFL=7ANDC(L)=1ANDC(40)=-1THEN7112
7111 GOTO7150
7112 SOUND255,10:SOUND10,4:C(40)=-2:L$(6)=L$(6)+" AND A SECRET ROOM!":E$(6)=E$(6)+"B":Q$="LOOK WHAT I HAVE FOUND!"
7113 GOTO200
7150 SOUND1,4:Q$="I AM NOT A BELL RINGER.":GOTO200
7300 IFP(L)<>50THEN9537
7310 IFL=13ANDC(21)=-1ANDN=18THENC(21)=-2:SOUND250,4:Q$="THAT IS BETTER.":O$(21)="MONA SWEET SMELLING MONSTER.":GOTO200
7320 SOUND1,4:Q$="WAVING THAT HAD NO EFFECT.":GOTO200
7500 GOTO1500
7700 IFP(L)<>NTHEN9520
7710 IFL=44ANDC(L)=-1THEN7712
7711 GOTO7720
7712 C(L)=-2:L$(21)="NOW IN A STONE CORRIDOR WITH A SMALL DOOR.":E$(21)=E$(21)+"F":D$(21)=D$(21)+"22"
7713 SOUND100,4:SOUND10,4:Q$="WHAT IS THIS!":GOTO200
7720 SOUND1,4:Q$="NOTHING SPECIAL HAPPENED...":GOTO200
7900 SC=6:SF=42:GOSUB10000:PRINT@170,"do*you*wish";:PRINT@232,"to*continue*with";:PRINT@297,"this*game*y/n?";:KB$=INKEY$
7908 KB$=INKEY$:IF KB$<>"Y"AND KB$<>"N"THEN7908
7909 SOUND210,1
7920 IFKB$="Y"THEN100
7925 END
8100 GOTO1500
8300 IFP(L)<>NTHEN9520
8310 IFL=24ANDC(L)=2THENN=20:GOTO9510
8320 IFL=47THENSOUND1,4:Q$="I WOULD NOT DARE!":GOTO200
8330 SOUND1,4:Q$="I CANNOT CROSS THAT.":GOTO200
8500 IF(P(L)=50ORP(L)=60ORP(L)=N)=0THEN9520
8505 IFL=28ANDN=26ANDC(L)=-1THENSOUND10,10:Q$="IT IS LEVITATING IN MID AIR!":GOTO200
8510 IFL=18ANDN=14THENSOUND200,1:SOUND100,1:SOUND200,1:Q$="I SEE A MESSAGE.":GOTO200
8520 IFL=12ANDN=10THENSOUND10,3:SOUND30,3:SOUND50,3:Q$="AT THE VERY TOP I SEE A KEY.":GOTO200
8525 IFL=34ANDP(L)=50THENSOUND200,3:SOUND50,3:SOUND200,3:SOUND50,3:Q$="IT LOOKS LIKE THE DEVIL HIMSELF!":GOTO200
8550 IFL>35THENSOUND210,1:Q$=MID$(O$(L),4):GOTO200
8560 SOUND1,4:Q$="I SEE NOTHING SPECIAL.":GOTO200
8600 DATA"IN FRONT OF A MASSIVE, SECURELY SHUT CAST IRON PORTCULLIS.",E,02
8601 DATA"ON A DUSTY PATH, WITH NOTHING LIVING IN SIGHT.",NW,040103
8602 DATA"IN A DAMP, ROOMY SUBTERRANEAN SECRET TUNNEL.",NW,0209
8603 DATA"IN A LARGE BUT DESERTED, DIRTY COURTYARD IN FRONT OF A MAGNIFICENT PALACE.",NS,0502
8604 DATA"IN A COLOSSAL RECEPTION ROOM OF THIS SPLENDIDLY DECORATED PALACE.",WS,0604
8605 DATA"IN A JEWEL ENCRUSTED, GOLDEN THRONE ROOM. WHAT DECOR!",E,0507
8606 DATA"IN A LARGE, CONCEALED ARMOURY. ONCE, THIS ROOM MUST HAVE BEEN FULL OF WEAPONS.",WE,0608
8607 DATA"IN FRONT OF A MASSIVE REDBRICK FIREPLACE WHICH LOOKS DISUSED.",W,07
8608 DATA"ON A LONG, DIRT ROAD. FAR, FAR IN THE DISTANCE I SEE AN EERIE BUILDING.",WE,1003
8609 DATA"NOW ON A DIRT TRACK, WITH ROADS LEADING OF IN ALL DIRECTIONS.",NESW,11091214
8610 DATA"NOW IN AN OVERGROWN, WILD FIELD WITH A VERY PLEASANT AROMA IN THE AIR.",S,10
8611 DATA"ON A SMALL, PRIMITIVE AND DIRTY MEDIEVAL FARM. PHEW, WHAT A STINK!",NE,1013
8612 DATA"AT A SMALL, NEAT, BRICKBUILT WELL. I BELIEVE THAT WATER FROM THESE WELLS IS MAGIC.",W,12
8613 DATA"NOW IN FRONT OF A LARGE, RUSTY CAST IRON GATE.",E,10
8614 DATA"IN FRONT OF A DINGY, EVIL LOOKING CASTLE.",WS,1614
8615 DATA"IN FRONT OF A SMALL, STEAMING ALKALINE POOL. A SIGN SAYS 'IT BURNS!'",E,15
8616 DATA"IN A SMALL, MUSTY KITCHEN WHICH HAS OBVIOUSLY BEEN USED AS A POTION ROOM.",NSW,211518
8617 DATA"IN A DIRTY, DINGY CAVE. OBNOXIOUS SMELLS PERVADE THE AIR.",ES,1719
8618 DATA"IN FRONT OF A VERY DEEP PIT. IN FACT IT APPEARS BOTTOMLESS!",N,18
8619 DATA"IN A HUGE, RUN DOWN, MEDIEVAL LIBRARY. IN FACT, I AM SURROUNDED BY BOOKS.",E,19
8620 DATA"NOW IN A STONE CORRIDOR, WITH STONE WALLS ON BOTH SIDES OF ME.",NS,2517
8621 DATA"NOW IN A SECRET CHAPEL. IT LOOKS LIKE THIS PLACE WAS FORGOTTEN YEARS AGO.",W,21
8622 DATA"NOW AT THE TOP OF THIS GIANT OAK TREE.",H,25
8623 DATA"IN THE PRESENCE OF THE DARK LORD, BEELZEBUB, HIMSELF. HE MAKES MY FLESH CREEP.",W,23
8624 DATA"NOW IN A BARREN AND DESOLATE GARDEN.",ES,2621
8625 DATA"IN A WITCH'S SORCERY ROOM. I CAN FEEL HER POWERS OF EVIL!",W,25
8626 DATABUCA WOODEN BUCKET,17,1,1
8627 DATAGLOA PADDED GLOVE,3,1,1
8628 DATARUBPILES OF ROTTING RUBBISH,4,-1,1
8629 DATALETHANDWRITTEN LETTER, 5,1,1
8630 DATAKINDEAD KING,6,1,1
8631 DATA"ROPA LONG, TOUGH ROPE",40,1,1
8632 DATATORA BURNING TORCH,6,1,1
8633 DATALANA JOUSTING LANCE,7,1,1
8634 DATABELSTRANGE STEEL BELLOWS,8,1,1
8635 DATABROA WOODEN BROOM,8,1,1
8636 DATAKNIA JEWELLED KNIFE,40,-1,1
8637 DATAPOSA HIGH SIGN POST,10,-1,1
8638 DATAFLOSWEET SMELLING FLOWER,11,-1,1
8639 DATASPAA STRONG SPADE,11,1,1
8640 DATACOWA FARMYARD COW,12,-1,1
8641 DATADUNA PILE OF COW DUNG,12,1,1
8642 DATAWATMAGIC WATER,13,-1,1
8643 DATAGATA RUSTING IRON GATE,14,-1,1
8644 DATASKEAN ANIMATED SKELETON,15,-1,1
8645 DATAALKA SMALL ALKALINE POOL,16,-1,1
8646 DATAMONAN EVIL SMELLING MONSTER,18,-1,1
8647 DATAPSAA BOOK OF PSALMS,20,1,2
8648 DATACROA HOLY CROSS,22,-1,1
8649 DATALADA SHORT WOODEN LADDER,25,1,1
8650 DATAWINA WINDOW WITH A KEYHOLE,23,-1,1
8651 DATAPRIA BEAUTIFUL PRINCESS,24,-1,1
8652 DATAWITAN EVIL WITCH,26,-1,1
8653 DATACLOA MAGIC CLOAK,26,-1,1
8654 DATAACOA SMALL ACORN,40,-1,1
8655 DATAKEYA RUSTY KEY,10,-1,2
8656 DATATALA MAGIC TALISMAN,40,-1,1
8657 DATATREA LARGE OAK TREE,40,-1,1
8658 DATASHOA PUNY GREEN SHOOT,40,-1,1
8659 DATAMANA MANDRAKE PLANT,13,-1,1
8660 DATA"HAIWITCH'S HAIR",26,-1,2
8661 DATAMESIT IS CODED,14,-1,2
8662 DATAPORIT IS JAMMED SHUT,1,-1,2
8663 DATAPATIT IS VERY DUSTY,2,-1,2
8664 DATATUNIT IS VERY MUSTY IN HERE,3,-1,2
8665 DATAROOI CANNOT SEE,6,-1,2
8666 DATABOOTHERE ARE SO MANY,20,-1,2
8667 DATACASIT SEEMS VERY EVIL,15,-1,2
8668 DATADOOIT LOOKS HOLY,21,-1,2
8669 DATAWALIT APPEARS SOLID,21,-1,2
8670 DATADOWIT IS A LONG WAY,23,-1,2
8671 DATAROATHE GROUND IS UNSETTLED,9,-1,2
8672 DATAPOOIT ATTACKS ALL PHOSPHATES,16,-1,2
8673 DATAGAREVERYTHING IS SHRIVELLED,25,-1,2
8674 DATAWESTHE SUN SETS THERE,23,-1,2
8675 DATAEASI CAN SEE THE PALACE,23,-1,2
8676 DATANORI CAN SEE FOR MILES,23,-1,2
8677 DATASOUBEYOND THE PIT IS...,19,-1,2
8678 DATANOR,N,EAS,E,SOU,S,WES,W,DOW,H,TUN,A,ROO,B,GAT,C,CAS,D,DOO,F,WIN,G
9500 SOUND1,4:Q$="SORRY, BUT I CANNOT GO THERE.":GOTO200
9510 SOUND100,2:SOUND50,2:SOUND100,2:Q$="O.K. IT IS DONE...":GOTO200
9520 SOUND1,4:Q$="I CANNOT SEE ONE OF THOSE HERE!":GOTO200
9530 SOUND1,4:Q$="I CANNOT DO THAT YET.":GOTO200
9537 SOUND1,4:Q$="I AM NOT CARRYING IT.":GOTO200
9538 SOUND1,4:Q$="YOU MUST BE JOKING!":GOTO200
9539 SOUND1,4:Q$="I AM JUST NOT CAPABLE OF THAT.":GOTO200
9600 CLS:PRINT"":SOUND255,10:PRINT@10,"***********";:PRINT@42,"*";:M$="WELL DONE":GOSUB7:PRINT"*";:PRINT@74,"***********";
9602 PRINT@128,"AS I STAB THE MANDRAKE ROOT THE","WHOLE ROOM SHUDDERS... A THICK","GREEN SMOKE FILLS THE AIR, AND"
9615 PRINT"WHEN IT CLEARS BEELZEBUB IS NO","LONGER THERE!"
9616 PRINT"THE PRINCESS IS NOW FREED AND","I CAN CLAIM MY WEALTH AND","HAPPINESS.... ";:M$="THANK YOU!":GOSUB7:END
10000 CLS(SC):FORSC=0TO31:POKEM+SC,SF:POKEM+SC+480,SF:NEXT:FORSC=32TO480STEP32:POKEM+SC,SF:POKEM+SC+31,SF:NEXT:RETURN
10001 SC=9:SF=32:GOSUB10000:PRINT@10,;:M$="DEMON KNIGHT":GOSUB7:PRINT@97,"WITH THIS PROGRAM YOU CAN HAVE";
10003 PRINT@130,"AN ADVENTURE WITHOUT LEAVING";:PRINT@170,"YOUR ROOM.";:PRINT@196,"TRAVEL, DISCOVER SECRET";
10004 PRINT@228,"PASSAGES AND MANIPULATE";:PRINT@261,"OBJECTS YOU MAY FIND.";:PRINT@328,"DO NOT GIVE UP-";
10005 PRINT@359,"THERE IS ALWAYS A";:PRINT@392,"PUZZLE TO SOLVE.";:GOTO10
10010 C1=1:CC=32:DX=INT(DX/8):YY=INT(DY/12+.5)
10012 IFMID$(M$,CC,1)<>""THENIFMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO10012
10013 PRINT@32*YY+DX,MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:YY=YY+1:IFC1<=LEN(M$)THEN10012
10014 RETURN
10200 PRINT@0,"":PRINT@0,"THY COMMAND";:INPUTJ$:FORSC=1TO2:PRINT@32*SC,"":NEXT:IFJ$="N"THENJ$="GO NOR"
10210 IFJ$="S"THENJ$="GO SOU"
10220 IFJ$="W"THENJ$="GO WES"
10240 IFJ$="E"THENJ$="GO EAS"
10250 IFJ$="D"THENJ$="GO DOW"
10260 RETURN
10300 PRINT@483,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7:KB$=INKEY$
10305 KB$=INKEY$:IFKB$=""THEN10305
10306 SOUND210,1:RETURN
10400 REM 2023 JIM GERRIE
