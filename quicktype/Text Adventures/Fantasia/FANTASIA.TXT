0 CLS
10 PRINT:PRINT:PRINT"           ÿ€öööþÿÿƒíîíƒÿÿ€ýû÷€ÿÿþþ€þþÿÿƒíîíƒÿÿÙ¶¶¶Íÿÿÿ¾€¾ÿÿÿƒíîíƒÿ"
20 SOUND25,9:FORW=1TO2000:NEXT:CLS
30 POKE30779,0
100 CLEAR 1900
118 OA=1
124 DIMO$(51),R(51,3),GE(44),WE(44),GE$(44),NO$(44),VE$(31)
127 O$(1)="VOR EINEM RIESIGEN TORBOGEN."
128 O$(2)="AUF EINER BLUMENWIESE."
129 O$(3)="AN EINEM SEEROSENTEICH."
130 O$(4)="IN EINER TROSTLOSEN EINOEDE."
131 O$(5)="IN EINEM NEBELIGEN MOOR."
132 O$(6)="IN EINER FEENHAFTEN GROTTE."
133 O$(7)="AN EINER ZERKLUEFTETEN FELSWAND."
134 O$(8)="IN EINEM LICHTEN AUWALD."
135 O$(9)="AUF EINER BLUEHENDEN HEIDE."
136 O$(10)="IN EINER VERDORRTEN STEPPE."
137 O$(11)="AUF DEM VERSAMMLUNGSPLATZ DER GNOME."
138 O$(12)="IN DER WAFFENKAMMER DER GNOME."
139 O$(13)="IN EINER TROFSTEINHOELE."
140 O$(14)="AM FUSS EINES BERGES."
141 O$(15)="IN DER SCHATZKAMMER DER ELFEN."
142 O$(16)="AUF EINER BERGWIESE."
143 O$(17)="AN EINER SILBERNEN QUELLE."
144 O$(18)="IN EINER SCHLUCHT MIT VIELEN FARNEN."
145 O$(19)="IN EINER BAERENHOELE."
146 O$(20)="AUF EINER SCHWANKENDEN HAENGEBRUECKE."
147 O$(21)="AN EINEM WIESENHANG."
148 O$(22)="IN EINER ROSENLAUBE."
149 O$(23)="IN EINEM KRAEUTERGARTEN."
150 O$(24)="AUF EINEM VERSCHLUNGENEM WEG."
151 O$(25)="AN EINER VERFALLENEN BURGRUINE"
152 O$(26)="IN EINEM SONNIGEN OBSTHAIN."
153 O$(27)="IN EINEM VEERWILDERTEN PARK."
154 O$(28)="IN EINER FINSTEREN HEXENKUECHE."
155 O$(29)="IM HOF EINER VERLASSENEN BURG."
156 O$(30)="IN EINEM FINSTERN BURGVERLIES."
157 O$(31)="IN DER KRONE EINES BAUMES."
158 O$(31)=O$(31)+" ICH KANN NICHT WEIT SEHEN."
159 O$(32)="IN EINEM FEUCHTEN LAUBWALD."
160 O$(33)="IN DEM REICH DER FEEN."
161 O$(34)="IN EINER GRAUEN STEINWUESTE."
162 O$(35)="AN EINEM BREITEN FLUSS."
163 O$(35)=O$(35)+" AM ANDEREN UFER BEWEGT SICH ETWAS."
164 O$(36)="AUF EINEM DRECKIGEN PFAD."
165 O$(37)="IN EINEM DORNENGESTRUEPP."
166 O$(37)=O$(37)+" ICH FINDE MICH NICHT MEHR ZURECHT."
167 O$(38)=O$(37)
168 O$(39)=O$(37)
169 O$(40)="IN EINEM HOHLWEG."
170 FORI=41TO50:O$(I)="IN EINEM UNHEIMLICHEN WALD."
171 O$(I)=O$(I)+" MAN SIEHT KAUM EINEN WEG. OB HIER EIN DRACHE"
172 O$(I)=O$(I)+" HAUST ?!":NEXT
173 O$(51)="AUF EINER UNZUGAENGLICHEN WALDLICHTUNG."
176 FORI=1TO51:FORJ=0TO3:FORJ=0TO3:READR(I,J):NEXT:NEXT
177 DATA0,0,2,0,8,0,3,1,9,0,0,2,10,0,5,0,11,0,0,4,13,0,7,0,0,8,0
178 DATA0,7,2,9,0,15,3,10,8,0,4,11,9,0,5,12,10,0,0,0,11,16,6,0,0
179 DATA17,0,15,0,0,9,0,14,0,13,17,0,0,14,18,16,24,0,19,17,25,0
180 DATA20,18,0,0,21,19,26,0,0,20,27,0,23,0,0,0,24,22,29,18,0,23
183 DATA30,19,0,0,32,21,0,0,0,22,28,0,0,0,0,27,0,0,30,0,0,25
184 DATA0,0,0,0,0,0,0,26,0,33,0,34,32,0,33,36,37,35,0,0,34,0,34
185 DATA0,5,0,37,38,39,34,37,38,38,38,40,39,39,37,42,39,0,0,43,41
186 DATA42,41,44,40,42,41,47,41,43,43,48,42,45,44
188 DATA49,45,45,44,50,51,0,0,47,43,48,47,48,44,48,47,0,45,50,0
189 DATA0,46,0,49,46,0,0,0
192 FORI=1TO44:READNO$(I):NEXT
193 DATAN,S,O,W,NORDEN,SUEDEN,OSTEN,WESTEN
194 DATAZAUBERKUCHEN,RAUPE,BUCH
195 DATAZETTEL,TARNKAPPE,RIESENFRUCHT
196 DATAFEUERSCHWERT,BAER,SCHILD
197 DATAZWERG,DRACHENEIER,STEIN
198 DATAHOELENMALEREI,STRAUCH,BEEREN
199 DATABLAETTER,DIAMANTMESSER,SILBERSCHLUESSEL
200 DATABAUM,PERGAMENT,STEINSCHLEUDER
201 DATAWASSERKRUG,ZAUBERSCHUHE,SCHRIFTTAFEL
202 DATAZWERGENDOLCH,KUGEL,KAESTCHEN,GNOM
203 DATARING,BRIEF,RUBIN,TUER,FEENHAAR,DRACHE,HOLZTAFEL,POKAL
208 FORI=1TO31:READVE$(I):NEXT
209 DATAGEHE,STICH,SIEH,INVENTAR,SAVE,LOAD,FANTASIA,ENDE,WIRF
210 DATATOETE,LIES,FUELLE,OEFFNE,BENUETZE,SIMSALABIM,SAGE,GIESSE
211 DATASCHIESSE,FUETTERE,SCHNEIDE,SPERRE,HILFE,KLETTERE
212 DATASCHLEUDERE,ISS,NIMM,VERJAGE,LEGE,DREHE,SPRINGE,TRINKE
220 A$(0)="SEI NICHT ALBERN."
221 A$(1)="ICH HABE NICHT DAS RICHTIGE WERKZEUG."
222 A$(2)="ICH WEISS NICHT WIE DAS GEHT."
223 A$(3)="LAESST MICH NICHT."
226 GE$(9)="EINEN ZAUBERKUCHEN":GE$(10)="EINE HAARIGE RAUPE"
227 GE$(11)="EIN GRUENES BUCH":GE$(12)="EINEN ZETTEL"
228 GE$(13)="EINE TARNKAPPE":GE$(14)="EINE RIESENFRUCHT"
229 GE$(15)="EIN FEUERSCHWERT":GE$(16)="EINEN GROSSEN BAEREN"
230 GE$(17)="EIN SCHILD AUS BRONZE":GE$(18)="EINEN ZWERG"
231 GE$(19)="GOLDENE DRACHENEIER":GE$(20)="EINEN STEIN"
232 GE$(21)="EINE HOELENMALEREI":GE$(22)="EINEN STRAUCH"
233 GE$(23)="VIELE ROTE BEEREN":GE$(24)="GOLDENE BLAETTER"
234 GE$(25)="EIN DIAMANTENMESSER"
235 GE$(26)="EINEN SILBERSCHLUESSEL":GE$(27)="EINEN DUERREN BAUM"
236 GE$(28)="EIN VERGILBTES PERGAMENT"
237 GE$(29)="EINE GROSSE STEINSCHLEUDER"
238 GE$(30)="EINEN LEEREN WASSERKRUG":GE$(31)="EIN PAAR ZAUBER"
239 GE$(31)=GE$(31)+"SCHUHE":GE$(32)="EINE SCHRIFTTAFEL"
240 GE$(33)="EINEN ZWERGENDOLCH":GE$(34)="EINE GROSSE KUGEL"
241 GE$(35)="EIN ROTES KAESTCHEN":GE$(36)="EINEN BOESEN GNOM"
242 GE$(37)="EINEN EISERNEN RING":GE$(38)="EINEN BRIEF"
243 GE$(39)="EINEN ROTEN RUBIN":GE$(41)="GOLDENES FEENHAAR"
244 GE$(42)="EINEN FEUERSPEIENDEN DRACHEN":GE$(44)="EINEN SILBE"
245 GE$(43)="EINE VERWITTERTE HOLZTAFEL":GE$(44)=GE$(44)+"RNEN "
246 GE$(44)=GE$(44)+"POKAL":GE(0)=-1:FORI=9TO44:READGE(I),WE(I)
247 NEXT
248 DATA28,0,29,0,8,0,24,0,2,0,26,0,20,10,19,0,19,15,18,0,18,20
249 DATA16,0,13,0,14,0,0,0,0,15,15,5,8,5,9,0,11,0,12,0,1,0,1,0,1
250 DATA0,3,7,4,0,4,0,4,0,0,0,0,0,0,12,30,0,33,10,51,0,40,0,51,44
277 OL=0
280 IFGE(13)=-2,GE(13)=-4
281 IFOA<>OLORVE=3,PRINT:OL=OA:GOSUB311
282 IFGE(13)=-2,PRINT"DIE TARNKAPPE HAT SICH IN LUFT AUFGELOEST!
283 IFGE(13)=-2,GE(13)=0:IN=IN-1
284 IFGE(13)=-4,GE(13)=-2
286 ZE=ZE+1:KO$="":INPUT"UND NUN";KO$
287 IFLEN(KO$)=0,PRINTCHR$(27);:GOTO286
288 V$="":N$="":VE=0:NO=0
289 FORI=1TOLEN(KO$)
290 IFMID$(KO$,I,1)<>" ",V$=V$+MID$(KO$,I,1):NEXT
291 IFLEN(V$)+1>=LEN(KO$),293
292 N$=RIGHT$(KO$,(LEN(KO$)-I))
293 FORI=1TO31:IFV$=VE$(I),VE=I:GOTO297
294 NEXT
295 IFN$="",N$=V$
296 GOTO298
297 IFV$=KO$,300
298 FORI=1TO44:IFN$=NO$(I),NO=I:GOTO300
299 NEXT
300 IFNO<9ANDNO<>0AND(VE=0ORVE=1),VE=1
301 IFLEN(N$)>0ANDNO=0ANDVE<>16,VE=0:FL=1
302 IFFL=1,FL=0:PRINTN$;" KENNE ICH NICHT.":GOTO280
303 IFN$=""AND(VE>8ORVE=2)ANDVE<>22ANDVE<>15ANDVE<>30,FL=1
304 IFFL=1,FL=0:PRINT"BITTE GIB EIN OBJEKT AN.":GOTO280
305 IFVE=0,PRINTV$;" KENNE ICH NICHT.":GOTO280
306 GOTO1000
311 '
312 IFOA=25,GE(40)=25:GE$(40)="EINE TUER IM NORDEN"
313 IFOA=30,GE(40)=30:GE$(40)="EINE TUER IM SUEDEN"
314 IFOA=6,P1=1
315 IFOA=31,P2=1
316 IFOA=29,P3=1
317 IFOA<>1ANDGE(31)<>-2,PRINT:FL=1
318 IFFL=1,PRINT"HILFE! ICH VERSINKE IM BODEN."
319 IFFL=1,FL=0:FORI=1TO2000:NEXT:GOTO611
320 PRINT"ICH BIN "O$(OA):FL=0:FORI=9TO44:IFGE(I)<>OA,323
321 IFFL=0,PRINT"ICH SEHE:"
322 PRINTGE$(I):FL=1
323 NEXT:FL=0
324 IFOA=31,335
325 IFIN>1ANDOA=5,327
326 GOTO331
327 FORI=9TO44:IF(GE(I)=-1ORGE(I)=-2)ANDI<>31,GE(I)=29
328 NEXT:IN=1
329 PRINT"IM MOOR IST ALLES VERSCHWUNDEN,"
330 PRINT"WAS ICH BEI MIR HATTE !"
331 FL=0:PRINT"RICHTUNGEN:":FORI=0TO3:IFR(OA,I)=0,334
332 IFFL=1,PRINT", ";
333 PRINTNO$(I+5);:FL=1
334 NEXT:FL=0
335 PRINT:FORI=1TO40:PRINT;:NEXT:RETURN
338 IFNO=0,PRINT"ICH BRAUCHE EINE RICHTUNG.":GOTO280
339 IFNO>8,504
340 IFNO>4,NO=NO-5ELSENO=NO-1
341 IFR(OA,NO)=0,PRINT"IN DIESER RICHTUNG IST KEIN WEG":GOTO280
342 IF(OA=30OR(OA=25ANDNO=0))ANDTU=0,PRINT"DIE TUR IST ZU":GOTO280
343 OA=R(OA,NO):OL=0:GOTO281
346 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
347 IFNO<>10ANDNO<>14ANDNO<>16ANDNO<>18ANDNO<>36ANDNO<>42,FL=1
348 IFFL=1,FL=0:PRINTA(2):GOTO280
349 IFNO=14,PRINT"VERSUCHE 'SCHNEIDE'":GOTO280
350 IFGE(15)<>-1ANDGE(25)<>-1ANDGE(33)<>-1,FL=1
351 IFFL=1,FL=0:PRINTA$(1):GOTO280
352 IFNO=10,PRINT"DIE RAUPE IST KITZELIG UND LACHT !":GOTO281
353 IFNO=16,PRINT"DER BAER BRUMMT UNWILLIG":GOTO281
354 IFNO=18,PRINT"DER ZWERG WIRDBOESE UND TOETET MICH":GOTO357
355 IFNO=36,PRINT"DER GNOM VERZAUBNERT MICH!":GOTO357
356 IFNO=42,PRINT"DER DRACHE VERBRENNT MICH!"
357 FORI=1TO2000:NEXT:GOTO611
360 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
361 IFGE(NO)=-1ORGE(NO)=-2,PRINT"HABE ICH DABEI.":GOTO280
362 IFNO=10ORNO=16ORNO=18ORNO=21ORNO=22ORNO=27ORNO=36ORNO=40,FL=1
363 IFFL=1ORNO=42,FL=0:PRINTA$(0):GOTO280
364 IFNO=29ORNO=14OR(NO=34ANDGE(9)<>-3),PRINT"ZU SCHWER":GOTO280
365 IFNO=17ANDGE(16)=OAANDGE(13)<>-2,PRINT"DER BAER";A$(3):GOTO281
366 IFNO=19ANDGE(18)=OAANDGE(13)<>-2,PRINT"DER ZWERG";A$(3):GOTO281
367 IFNO=35ANDGE(36)=OAANDGE(13)<>-2,PRINT"DER GNOM";A$(3):GOTO281
368 IFGE(10)=OAANDGE(13)<>-2,PRINT"DIE RAUPE";A$(3):GOTO281
369 IFNO=44ANDGE(42)=OAANDGE(13)<>-2,PRINT"DER DRACHE";A$(3):GOTO281
370 IFNO=32ORNO=43,PRINT"KANN ICH NICHT ERREICHEN":GOTO280
371 IFIN+1>7,PRINT"ICH HABE ZU VIEL ZU TRAGEN"
372 IFIN+1>7,PRINT"ICH MUESSTE ETWAS WEGLEGEN.":GOTO280
373 IN=IN+1:GE(NO)=-1:PRINT"GUT":GOTO281
376 IFIN=0,PRINT"ICH HABE NICHTS DABEI.":GOTO280
377 IFIN<>0,PRINT"IM MOMENT HABE ICH DABEI:"
378 FORI=9TO44:IFGE(I)<>-1ANDGE(I)<>-2,380
379 PRINTGE$(I)
380 NEXT:GOTO280
390 PU=0:FORI=9TO44:IFGE(I)=1,PU=PU+WE(I)
391 NEXT:PRINT"DU HAST"PU"PUNKTE VON 170."
392 PRINT"DAZU GIBT ES EINEN BONUS VON BIS";
393 PRINT"ZU 30 PUNKTEN, WENN DU AUFHOERST";
394 GOTO280
397 GOTO611
403 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
404 IFNO<>34ORGE$(34)="EINE GOLDENE KUGEL ",FL=1
405 IFFL=1,FL=0:PRINT"NICHTS PASSIERT.":IN=IN-1:GE(NO)=OA:GOTO281
406 IFGE(36)<>OA,PRINT"DIE KUGEL FAELLT ZU BODEN,":FL=1
407 IFFL=1,FL=0:PRINT"UND ZERSPLITTERT."IN=IN-1:GE(34)=0:GOTO281
408 PRINT"DIE KUGEL ZERQUETSCHT DEN GNOM "
409 PRINT"UND VERWANDELT SICH. SAG SIEH":GE$(34)="EINE GOLDENE KUGEL"
410 IN=IN-1:GE(36)=0:GE(34)=OA:WE(34)=11:GOTO281
413 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
414 PRINT"BITTE ETWAS GENAUER":GOTO280
417 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
418 IFNO=11ORNO=12ORNO=17ORNO=21ORNO=28ORNO=32ORNO=43,420
419 PRINT"DA STEHT NICHTS":GOTO280
420 PRINT"DIE SCHRIFT BESAGT:"
421 IFNO=11,PRINT"MESSER SIND ZUM STECHEN DA !"
422 IFNO=12,PRINT"DRACHEN LASSEN SICH NICHT VERJAGEN";
423 IFNO=12,PRINT" SIE SIND UNVERWUNDBAR UND UNSTERBLICH"
424 IFNO=17,PRINT"DINGE DREHEN SICH UN RINGE!"
425 IFNO=21,PRINT"EIN ZAUBERER WAR HIER"
426 IFNO=21,PRINT"SIMSALABIM"
427 IFNO=28,PRINT"VOEGEL WURDEN VOR LANGER ZEIT"
428 IFNO=28,PRINT"IN ZWERGE VERWANDELT"
430 IFNO=32,PRINT"SUCHE NACH DEN VERLORENEN SCHAETZEN !"
431 IFNO=32,PRINT"BRINGE SIE HIERHER UND LEGE SIE HIN"
432 IFNO=32,PRINT"SAG 'FANTASIA'"
433 IFNO=38,PRINT"PFLANZEN BRAUCHEN WASSER !"
434 IFNO=43,PRINT"HUETE DICH VOR DEM DRACHEN!"
435 GOTO280
438 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
439 IFNO<>30ANDNO<>35ANDNO<>44,PRINTA$(0):GOTO280
440 IFNO=35ORNO=44,PRINT"IST DAFUER NICH GEEIGNET":GOTO280
441 IFOA<>3ANDOA<>17ANDOA<>35,PRINT"WO?":GOTO280
442 IFOA=3OROA=35,PRINT"ICH KANN DAS WASSER NICHT ERREICHEN":GOTO280
443 PRINT"GUT.":GE$(30)="EINEN VOLLEN WASSERKRUG":GOTO281
446 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
447 IFNO=40,PRINT"VERSUCHE 'SPERRE'":GOTO280
448 F=1:GOSUB607:IFFL=1,FL=0:GOTO280
449 IFNO<>35,PRINTA$(2):GOTO280
450 IFGE(38)<>0,PRINT"GUT. ES IST LEER":GOTO281
451 IFGE(26)<>-1,PRINTA$(1):GOTO280
452 PRINT"ZWEI DINGE FALLEN HERAUS. SAG 'SIEH'":GE(38)=OA:GE(39)=OA
453 GOTO281
455 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
456 IFNO<>13ANDNO<>31,PRINT"BITTE ETWAS GENAUER":GOTO280
457 IFNO=13,PRINT"GUT, ICH SETZE DIE TARNKAPPE AUF":GE(13)=-2:GOTO280
458 IFNO=31,PRINT"GUT, ICH ZIEHE DIE SCHUHE AM":GE(31)=-2:GOTO281
461 IFGE(18)<>OA,PRINT"NICHTS PASSIERT":GOTO280
462 PRINT"DER ZWERG VERWANDELT SICH IN EINEN VOGEL";
463 PRINT" UND FLIEGT DAVON!":GE(18)=0:GOTO281
466 PRINT"GUT,...";N$
467 IFN$=VE$(15),461
468 PRINT"NICHTS PASSIERT":GOTO280
471 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
472 IFNO=34,PRINT"VERSUCHE 'WIRF'":GOTO280
473 IFNO=20,PRINT"VERSUCHE 'SCHLEUDERE'":GOTO280
474 PRINTA$(0):GOTO280
477 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
478 IFNO<>10ANDNO<>16ANDNO<>18ANDNO<>36ANDNO<>42,FL=1
479 IFFL=1,FL=0:PRINTA$(0):GOTO280
480 IFNO<>16,PRINT"HAT EINEN HUNGER":GOTO280
481 IFGE(23)<>-1,PRINT"DER BAER IST SEHR HUNGRIG UND ":FL=1
482 IFFL=1,FL=0:PRINT"FRISST ... MICH!":FORI=1TO2000:NEXT:GOTO611
483 PRINT"DER BAER VERSCHLINGT DIE BEEREN UND LAUFT DAVON"
484 IN=IN-1:GE(16)=0:GE(23)=0:GOTO281
487 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
488 IFNO<>14,PRINTA$(2):GOTO280
489 IFGE(33)<>-1,PRINTA$(1):GOTO280
490 IFGE(37)<>0,493
491 PRINT"ICH HABE DIE FRUCHT GETEILT UND"
492 PRINT"ETWAS FAELLT HERAUS. SAG 'SIEH'":GE(37)=OA:GOTO281
493 PRINT"DIE FRUCHT IST BEREITS GETEILT":GOTO280
496 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
497 IFNO<>40ANDNO<>35,PRINTA$(2):GOTO280
498 IFNO=35,PRINT"VERSUCHE 'OEFFNE'":GOTO280
499 IFTU=1,PRINT"IST SCHON OFFEN!":GOTO280
500 IFGE(26)<>-1,PRINT"ICH HABE KEINEN SCHLUESSEL":GOTO280
501 PRINT"GUT":TU=1:GOTO281
504 IFOA=31ANDNO=27,OA=9:GOTO281
505 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
506 IFNO<>27,PRINTA$(0):GOTO280
507 OA=31:GOTO281
510 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
511 IFNO<>20ANDNO<>34,PRINTA$(2):GOTO280
512 IFNO=34,PRINT"VERSUCHE 'WIRF'":GOTO280
513 IFGE(29)<>OA,PRINT"WOMIT?":GOTO280
514 PRINT"GUT":IN=IN-1:GE(20)=29:GE(10)=0:GOTO281
517 F=1:GOSUB605:IFFL=1,FL=0:GOTO280
518 IFVE=25ANDNO=23,FL=1:GE(23)=0:IN=IN-1
519 IFVE=31ANDNO=0ANDGE$(30)="EINEN VOLLEN WASSERKRUG",FL=2
520 IFFL=2,FL=1:GE$(30)="EINEN LEEREN WASSERKRUG"
521 IFFL=1,FL=0:PRINT"AAAAH! GUT!":GOTO281
522 IFVE=31ANDNO=30,PRINT"DER KRUG IST LEER":GOTO280
523 IFVE=25ANDNO=9,GE(9)=-3:FL=1
524 IFFL=1,PRINT"BRRRR! SCHMECKT SCHEUSSLICH!":IN=IN-1
525 IFFL=1,FL=0:PRINT"ICH FUEHLE MICH BESONDERS STARK!":GOTO281
526 PRINTA$(0):GOTO280
529 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
530 IFNO<>10ANDNO<>16ANDNO<>18ANDNO<>36ANDNO<>42,FL=1
531 IFFL=1,FL=0:PRINTA$(0):GOTO280
532 PRINT"LAESST SICH NICHT VERJAGEN":GOTO280
535 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
536 IN=IN-1:PRINT"GUT":GE(NO)=OA:GOTO280
539 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
540 IFNO<>37,PRINT"GUT. NICHTS PASSIERT":GOTO281
541 PRINT"ALLES BEGINNT SICH ZU DREHEN!"
542 PRINT"DER RING FAELLT MIR AUS DER HAND";
543 PRINT"SAG 'SIEH'":GE(37)=OA:WE(37)=16
544 GE$(37)="EINEN RING AUS GOLFAEDEN":IN=IN-1:GOTO281
547 IFOA=31AND(NO=0ORNO=27),550
548 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
549 PRINTA$(0):GOTO280
550 IFINT(4*RND(0)+1)=2,552
551 OA=9:GOTO281
552 PRINT"AUA! ICH BIN HERUNTERGEFALLEN":FORI=1TO2000:NEXT:GOTO611
555 F=0:GOSUB605:IFFL=1,FL=0:GOTO280
556 IFNO<>22ANDNO<>14ANDNO<>27,PRINTA$(0):GOTO280
557 IFGE(30)<>-1ORGE$(30)="EINEN LEEREN WASSERKRUG",FL=1
558 IFFL=1,FL=0:PRINT"ICH HABE NICHTS ZUM GIESSEN":GOTO280
559 IFNO=22,PRINT"DER STRAUCH BEGINNT ZU BLUEHEN":FL=1
560 IFFL=1,PRINT"UND ES WACHSEN ROTE BEEREN"
561 IFFL=1,PRINT"DANN VERWANDELT ER SICH. SAG 'SIEH'"
562 IFFL=1,FL=0:GE(22)=0:GE(23)=OA:GE(24)=OA:GOTO564
563 PRINT"GUT"
564 PRINT"EINEN LEEREN WASSERKRUG":GOTO281
605 IFGE(NO)<>OAANDGE(NO)<>-1ANDGE(NO)<>-2,FL=1
606 IFFL=1,PRINT"SEHE ICH HIER NICHT.":RETURN
607 IFF=1ANDGE(NO)<>-1ANDGE(NO)<>-2,PRINT"HABE ICH NICHT DABEI":FL=1
608 RETURN
611 PU=0:FORI=9TO44:IFGE(I)=1,PU=PU+WE(I)
612 NEXT:IFPU<>170,616
613 IFZE<500,PU=PU+7
614 IFZE<400,PU=PU+7
615 IFZE<300,PU=PU+7
616 IFP1=1,PU=PU+2
617 IFP2=1,PU=PU+3
618 IFP3=1,PU=PU+4
619 CLS:PRINT:PRINT
620 PRINT"DU HAST"PU"VON 200 PUNKTEN!"
621 PRINT:PRINT"- RANG ";
622 IFPU=0,PRINT"10 -":GOTO632
623 IFPU<25,PRINT"9 -":GOTO632
624 IFPU<50,PRINT"8 -":GOTO632
625 IFPU<75,PRINT"7 -":GOTO632
626 IFPU<100,PRINT"6 -":GOTO632
627 IFPU<125,PRINT"5 -":GOTO632
628 IFPU<150,PRINT"4 -":GOTO632
629 IFPU<175,PRINT"3 -":GOTO632
630 IFPU<200,PRINT"2 -":GOTO632
631 IFPU=200,PRINT"1 -"
632 PRINT:PRINT:POKE30779,32:END
1000 IFVE=1,338ELSEIFVE=2,346ELSEIFVE=3,280ELSEIFVE=4,376
1001 IFVE=5,567ELSEIFVE=6,586ELSEIFVE=7,390ELSEIFVE=8,397
1002 IFVE=9,403ELSEIFVE=10,413ELSEIFVE=11,417ELSEIFVE=12,438
1003 IFVE=13,446ELSEIFVE=14,455ELSEIFVE=15,461ELSEIFVE=16,466
1004 IFVE=17,555ELSEIFVE=18,471ELSEIFVE=19,477ELSEIFVE=20,487
1005 IFVE=21,496ELSEIFVE=22,383ELSEIFVE=23,504ELSEIFVE=24,510
1006 IFVE=25,517ELSEIFVE=26,360ELSEIFVE=27,529ELSEIFVE=28,535
1007 IFVE=29,539ELSEIFVE=30,547ELSEIFVE=31,517
