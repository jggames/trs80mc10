0 CLEAR2500:DIMCC,ZZ,M$,I$:M=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 FORX=1TO1000:NEXTX
6 FORX=1TO1500:NEXTX:RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+M-1:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
8 PRINTTAB((32-LEN(M$))/2);:RETURN
10 M$="YOU HAVE BEEN CONVICTED OF REMOVING EVIDENCE FROM THE SCENE OF A CRIME.":GOSUB1
11 M$="YOUR SENTENCE:":GOSUB355
12 E(X)=P1:T$(P)="IN JAIL FOR REMOVING EVIDENCE":GOTO1280
20 GOSUB1420
30 CLS:GOTO930
40 REM **MOVEMENT**
50 FORX=1TO25:IFE(X)=30+PTHENZZ=X:X=25:NEXTX:X=ZZ:GOTO10
60 NEXTX
70 ONNGOTO110,150,190,230,270
80 IFNO$="UP"THEN290
90 CLS:PRINT:M$="?????SAY WHAT?????":GOSUB8:PRINTM$:PRINT:GOTO1290
100 REM
110 IFP1=17THENP1=16:GOTO310
120 FORX=1TO8
130 IFDN(X)=P1THENP1=P1-3:GOTO310
140 NEXTX:GOTO1070
150 IFP1=16THENP1=17:GOTO310
160 FORX=1TO9
170 IFDS(X)=P1THENP1=P1+3:GOTO310
180 NEXTX:GOTO1070
190 IFP1=3THENP1=22:GOTO200
191 IFP1=9ORP1=12THENP1=18:GOTO310
200 FORX=1TO15
210 IFDE(X)=P1THENP1=P1+1:GOTO310
220 NEXTX:GOTO1070
230 IFP1=10THENP1=1:GOTO240
231 IFP1=18THENP1=RND(20):GOTO310
240 FORX=1TO14
250 IFDW(X)=P1THENP1=P1-1:GOTO310
260 NEXTX:GOTO1070
270 IFP1=22THENP1=3:GOTO1060
271 IFP1=14THENP1=17:GOTO1060
272 IFP1=20THENP1=2:GOTO1060
273 IFP1=24THENP1=20:GOTO1060
274 GOTO1070
290 IFP1=22THENP1=19:GOTO1060
291 IFP1=2THENP1=20:GOTO1060
292 IFP1=20THENP1=24:GOTO1060
293 IFP1=17THENP1=14:GOTO1060
294 GOTO1070
310 IFRN>10ANDE(12)=P1THENM$="A DOG HAS BITTEN YOU!":GOSUB355:T$(P)="STILL HEALING FROM DOG BITE":GOTO1280
320 IFRN>10ANDE(25)=P1THENM$="A CAT HAS JUST SCRATCHED YOU!":GOSUB355:T$(P)="TAKING CARE OF A CAT SCRATCH":GOTO1280
330 IFRN<4ANDP1=15ORP1=23THENM$="YOU HAVE JUST WALKED IN ON A YOUNG LADY USING THE BATHROOM!":GOSUB356:T=4:GOTO1280
340 IFRN>19THENM$="A FLASH OF LIGHT BLINDS YOU FOR A MOMENT.":GOSUB1:E(26)=P1:GOSUB5
350 GOTO1060
355 M$=M$+" YOU LOSE THIS AND YOUR NEXT TURN.":T(P)=1:T=4:GOTO1
356 M$=M$+" YOU LOSE THE REST OF THIS TURN OUT OF EMBARRASSMENT.":GOTO1
360 REM **PICK UP**
370 PRINT
380 IFN<5ORN>LEN(NU$)/3THENPRINT"YOU CAN'T GET THAT!!!":GOTO1290
390 N=N-5
400 IFE(N)=P1ANDN=21THENM$=R$(N):GOSUB1:GOTO1290
410 IFE(N)=P1ANDN=19THENM$="DON'T BE RIDICULOUS-- JUST EXAMINE IT!":GOSUB1:GOTO1290
420 IFE(N)=P1THENE(N)=30+P:PRINT"O.K., I'M HOLDING IT.":GOTO1290
430 M$="I DON'T KNOW WHAT YOU'RE TALKING ABOUT-- I DON'T SEE "+E$(N)+".":GOSUB1:GOTO1290
440 REM ** DROP **
450 IFNO$="GUN"THENM$="IT WOULD BE STUPID TO DROP YOUR GUN!":GOSUB1:GOTO1290
460 N=N-5:IFN>27ORN<1THEN530
470 IFN=27ANDP1=18ANDP=TITHEN475
471 GOTO490
475 CLS:M$="OK... IT SANK TO THE BOTTOM AND I CAN'T SEE IT.":GOSUB1
476 M$="CONGRATULATIONS. YOU HAVE BEEN SUCCESSFUL AT HIDING YOUR VICTIM'S BODY!":GOSUB1:GOSUB1460
477 M$="THE CASE HAS BEEN THROWN OUT OF COURT DUE TO LACK OF EVIDENCE.":GOSUB1:GOTO480
480 PRINT:M$=G$(TI)+" IS THE VICTOR!":GOSUB1:GOTO900
490 IFN=27ANDP1=18THEN495
491 GOTO500
495 M$="YOU HAVE BEEN CONVICTED AS AN ACCOMPLICE TO MURDER.":GOSUB1
496 M$="YOU HAVE BEEN SENTENCED TO THE LOSS OF THIS AND THE NEXT 3 TURNS.":GOSUB1
497 E(27)=24:T(P)=3:T=4:T$(P)="CONVICTED AS MURDER ACCOMPLICE":GOTO1280
500 IFE(N)=30+PTHENE(N)=P1:GOTO520
510 GOTO530
520 PRINT"O.K. I'VE DROPPED IT.":GOTO1290
530 PRINT"I CAN'T DROP WHAT I DON'T HAVE!":GOTO1290
540 REM *EXAMINE*
550 IFNO$="GUN"THENM$="MFG.BY COLT":GOSUB1:GOTO1290
560 PRINT:N=N-5:IFN>27ORN<1THEN610
570 IFN=19ANDE(N)=P1THENM$=R$(N):GOSUB1:GOTO1290
580 IFN=1ORN=6ORN=13ORN=17ORN=24ANDE(N)=P1THENPRINT"IT'S NOT A RABBIT":GOTO1290
590 IFN=26ANDE(N)=30+PTHENT=T-2
600 IFE(N)=30+PTHENM$=R$(N):GOSUB1:E(26)=0:GOTO1290
610 PRINT"I DON'T HAVE IT!":GOTO1290
620 M$="I'M CARRYING":GOSUB7:PRINT":":M$="A GUN,"
630 FORX=1TO27:IFE(X)=30+PTHENM$=M$+E$(X)+","
640 NEXTX:M$=LEFT$(M$,LEN(M$)-1):GOSUB1:GOTO1290
650 PRINT:FORX=1TO5
660 IFLEFT$(G$(X),3)=NO$THEN680
661 NEXTX
670 PRINT"THAT'S BEYOND DESCRIPTION.":GOTO1290
680 GOSUB699:GOSUB2330
690 GOTO1070
699 CLS:PRINT:M$=G$(X):GOSUB8:GOSUB7:PRINT:PRINT:M$="IS THE "+D$(X)+".":GOSUB1:RETURN
700 CLS:FORX=1TO5:M$=STR$(X)+"-"+G$(X)+"-- "+CL$(X,P):GOSUB1:NEXTX
710 PRINT:INPUT"ADD CLUE (Y/N)";C$:IFC$="N"THEN1290
720 IFC$<>"Y"THEN710
730 INPUT"WHO(1-5)";X
740 M$="TYPE CLUE(SHORT) FOR "+G$(X)+".":GOSUB1:INPUTCU$(X)
750 CL$(X,P)=CL$(X,P)+CU$(X)+", "
760 GOTO700
770 PRINT:FORX=1TO5
780 IFLEFT$(G$(X),3)=NO$THEN800
781 NEXTX
790 PRINT"FIND IT YOURSELF!":GOTO1290
800 M$=G$(X)+" IS IN THE "+L$(G(X)):GOSUB1:GOTO1290
810 IFNO$="COR"THENPRINT"THAT'S SICK!":GOTO1290
811 FORX=1TO5:IFLEFT$(G$(X),3)<>NO$THENNEXTX:PRINT"YOU CAN'T SHOOT THAT.":GOTO1290
820 IFG(X)<>P1THENM$=G$(X)+"IS NOT HERE!":GOSUB1:GOTO1290
830 IFX=PTHENM$="YOU HAVE INJURED YOURSELF--":GOSUB355:T$(P)="SHOULDN'T PLAY WITH GUNS":GOTO1280
840 IFG$(X)=G$(TI)THEN845
841 GOTO850
845 CLS:M$="CONGRATULATIONS ON SEEING THAT JUSTICE WAS DONE!":GOSUB1:GOSUB1490
846 M$="YOU HAVE SUCCESSFULLY SOLVED THIS MURDER MYSTERY BY SHOOTING "+G$(TI):GOSUB1
847 M$=G$(P)+" IS THE GREASTEST DETECTIVE EVER!":GOSUB1:GOTO900
850 IFRN>15THENM$="YOUR SHOT MISSED! IT RICOCHETED AND KILLED YOU!!":GOSUB1:A(P)=10:T=4:GOTO1280
860 IFRN<7THEN865
861 GOTO870
865 M$="YOU INJURED "+G$(X)+".":GOSUB1
861 M$="YOU HAVE BEEN SENTENCED TO DEATH FOR ATTEMPTED MURDER.":GOSUB1
862 T=4:T(X)=1:T$(X)="WAS INJURED BY A MANIAC KILLER":A(P)=10:GOTO1280
870 IFRN<11THEN875
871 GOTO880
875 M$="YOU HAVE SHOT AT AN INNOCENT PERSON. YOU ARE QUITE LUCKY THIS TIME--":GOSUB1
876 M$="YOU MISSED HIM BUT YOU LOSE THIS AND 4 MORE TURNS.":GOSUB1:T(P)=4
877 T$(P)="SHOULDN'T SHOOT INNOCENT PEOPLE":T=4:GOTO1280
880 M$="YOU'RE LUCKY YOUR GUN MISSFIRED. YOU SHOT AT AN INNOCENT PERSON!":GOSUB1:GOTO1280
890 CLS:M$="YOU HAVE ALL DIED! WHAT A BUNCH OF INCOMPETENT DETECTIVES!":GOSUB1:PRINT
891 M$="FOR YOUR INFORMATION THE KILLER WAS "+G$(TI)+".":GOSUB1
900 PRINT:INPUT"ANOTHER MYSTERY (Y/N)";X$:IFX$="Y"THENRUN
910 PRINT"PLEASE! "Q$"?":INPUTX$:IFLEFT$(X$,1)="N"THENQ$=Q$+"!!":GOTO910
920 PRINT"THANK YOU, I THOUGHT YOU MIGHT.":GOSUB5:RUN
930 FORP=1TO5
940 IFP>5THEN930
950 IFA(P)=10THEN955
951 GOTO960
955 FORX=1TOY:IFA(X)<>10THENZZ=X:X=Y:NEXTX:X=ZZ:NEXTP:GOTO930
956 NEXTX:GOTO890
960 IFG1(P)=1THENG(P)=G(P)+1
970 IFG1(P)=1ANDG(P)>24THENG(P)=1
980 IFG1(P)=1THENNEXTP:GOTO930
990 SOUND87,8
1000 IFT(P)<=0THEN1030
1010 IFT(P)>0THEN1015
1011 GOTO1020
1015 CLS:PRINT@160,;:M$=G$(P):GOSUB8:GOSUB7:PRINT@225,T$(P):PRINT@299,"LOSES TURN":GOSUB2330:T(P)=T(P)-1
1021 NEXTP:GOTO930
1030 CLS:POKE49151,64:PRINT@160,;:M$=G$(P):GOSUB8:GOSUB7:GOSUB2330
1050 P1=G(P):T=0
1060 T=T+1
1070 CLS:POKE49151,0:M$="I'M IN THE":GOSUB7:M$=" "+L$(P1):GOSUB1
1080 M$="I SEE":GOSUB7:PRINT":":M$=O$(P1):GOSUB1
1090 FORX=1TO5:IFG(X)<>P1THENNEXTX:GOTO1120
1100 IFX<>PTHENM$=G$(X)+" IS HERE.":GOSUB1
1110 NEXTX
1120 FORX=1TO27
1130 IFE(X)<>P1THENNEXTX:GOTO1150
1140 M$=E$(X):GOSUB1:NEXTX
1150 M$="IT LOOKS LIKE I CAN GO":GOSUB7:PRINT":"
1160 FORX=1TO8:IFDN(X)=P1THENPRINT"NORTH,";:X=99
1170 NEXTX
1180 FORX=1TO9:IFDS(X)=P1THENPRINT"SOUTH,";:X=99
1190 NEXTX
1200 FORX=1TO15:IFDE(X)=P1THENPRINT"EAST,";:X=99
1210 NEXTX
1220 FORX=1TO14:IFDW(X)=P1THENPRINT"WEST,";:X=99
1230 NEXTX
1240 FORX=1TO4:IFDD(X)=P1THENPRINT"DOWN,";:X=99
1250 NEXTX
1260 FORX=1TO4:IFDU(X)=P1THENPRINT"UP,";:X=99
1270 NEXTX:PRINTCHR$(8)
1280 G(P)=P1:IFT=4THENGOSUB2330:NEXTP:GOTO930
1290 M$="WHAT NOW "+G$(P):GOSUB7:INPUTA$
1300 RN=RND(20)
1310 IFLEFT$(A$,4)="LOOK"THEN1070
1320 IFA$="HELP"THENGOSUB2290:GOTO1290
1330 FORX=1TOLEN(A$)
1340 IFMID$(A$,X,1)<>" "THENNEXTX
1350 NO$=MID$(A$,X+1,3)
1360 FORN=1TO(LEN(NU$)/3)
1370 IFMID$(NU$,3*N-2,3)<>NO$THENNEXTN
1380 VE$=LEFT$(A$,2)
1390 FORV=1TOLEN(VR$):IFMID$(VR$,2*V-1,2)<>VE$THENNEXTV
1400 ONVGOTO40,540,360,440,650,770,810,620,700
1410 GOTO90
1420 CLS:POKE49151,64:PRINT@74,"**WHODUNIT**":PRINT@168,"A MURDER MYSTERY":PRINT@271,"BY":PRINT@330,"DAVE VINNEDGE"
1421 PRINT@459,"AUGUST 1983":GOSUB5
1430 DIML$(25),DN(8),DS(9),DE(15),DW(14),DD(4),DU(4),O$(24),E$(27),E(27),R$(27),CL$(5,5)
1440 VR$="GOEXGEDRDELOSHINCL":FORZZ=1TO27:B$=B$+CHR$(8):NEXT
1441 NU$="NORSOUEASWESDOWWHIHAI3X5ALCEMPBLAREDCHIPAGSTOSTUDOGBLOCANGARTONBROSILHAZBIRDIRBATTRONO CATANCCOR"
1450 IT=RND(5):GOTO1520
REM M1$
1460 SOUND103,1:SOUND185,1:SOUND189,1:SOUND185,1:SOUND103,1:SOUND185,1:SOUND189,1:SOUND185,1:RETURN
REM M2$
1470 SOUND200,4:SOUND206,8:SOUND206,8:SOUND206,4:SOUND206,2:SOUND200,4:SOUND200,4:SOUND200,4:SOUND200,4
1471 SOUND209,8:SOUND209,8:SOUND200,2:SOUND206,4:SOUND206,4:SOUND200,4:RETURN
REM M3$
1480 SOUND227,4:SOUND225,1:SOUND181,4:SOUND171,4:SOUND181,1:RETURN
REM M4$
1490 SOUND200,4:SOUND209,4:SOUND227,4:SOUND224,2:SOUND209,8:SOUND176,4:SOUND179,1:RETURN
REM M5$
1500 SOUND209,2:SOUND171,8:SOUND209,8:SOUND204,2:SOUND103,4:SOUND196,4:SOUND200,2:FORZZ=1TO150:NEXT:RETURN
REM M8$
1510 SOUND181,4:SOUND181,4:FORZZ=1TO150:NEXT:SOUND181,4:SOUND181,4:FORZZ=1TO150:NEXT:SOUND181,4:SOUND181,4:RETURN
1511 SOUND179,4:SOUND181,4:SOUND181,8:SOUND181,8:SOUND181,8:FORZZ=1TO75:NEXT:RETURN
1520 FORX=1TO24:READL$(X):NEXT
1530 FORX=1TO8:READDN(X):NEXT
1540 FORX=1TO9:READDS(X):NEXT
1550 FORX=1TO15:READDE(X):NEXT
1560 FORX=1TO14:READDW(X):NEXT
1570 FORX=1TO4:READDD(X):NEXT
1580 FORX=1TO4:READDU(X):NEXT
1590 FORX=1TO24:READO$(X):NEXT
1600 FORX=1TO5
1610 C=RND(5):IFC=C1ORC=C2ORC=C3ORC=C4ORC=C5THEN1610
1620 IFIT=XTHENTI=C
1630 READD$(C):READG$(C):READG1(C)
1640 ONXGOTO1650,1660,1670,1680,1690
1650 C1=C:NEXTX:GOTO1720
1660 C2=C:NEXTX:GOTO1720
1670 C3=C:NEXTX:GOTO1720
1680 C4=C:NEXTX:GOTO1720
1690 C5=C:NEXTX
1700 FORX=1TO27:READE$(X):NEXT
1710 FORX=1TO27:READR$(X):NEXT
1720 CLS:POKE49151,0
1721 M$="WELCOME LADIES AND GENTLEMEN AND THANK YOU FOR ACCEPTING THIS INVITATION TO THE GREAT WHODUNIT MYSTERY."
1722 GOSUB1:PRINT:INPUT"NEED INSTRUCTIONS (Y/N)";Y$
1730 IFY$="YES"ORY$="Y"THENGOSUB2210:GOTO1750
1740 IFY$="N"ORY$="NO"THEN1750
1741 GOTO1720
1750 CLS:INPUT"PLEASE INDICATE THE NUMBER IN   YOUR PARTY. (2-5)";Y
1760 IFY>5ORY<2THEN1750
1770 CLS:M$="THANK YOU, THAT'S FINE. NOW COULD YOU PLEASE SIGN OUR GUEST REGISTER:":GOSUB1
1780 FORX=1TOY
1790 PRINT"GUEST #"STR$(X);:INPUTG$(X)
1800 FORXY=0TOX-1:IFLEFT$(G$(X),3)=LEFT$(G$(XY),3)THEN1805
1801 GOTO1810
1805 M$=G$(X)+" AND "+G$(XY)+" SOUND ALIKE. TO ME. USE A DIFFERENT NAME FOR "+G$(X)+" (GUEST #"+STR$(X)+")"
1806 GOSUB1:XY=X:NEXTXY:GOTO1790
1810 NEXTXY
1820 T(P)=0:G1(X)=2:NEXTX
1830 CLS:M$="THE FOLLOWING ARE CHARACTER DESCRIPTIONS.":GOSUB1:GOSUB1460:GOSUB6
1840 FORX=1TO5
1850 GOSUB699:GOSUB1470:GOSUB1480:GOSUB1510:GOSUB1470:GOSUB1480:GOSUB1500:FORZZ=1TO150:NEXT
1870 NEXTX
1880 CLS:PRINT@192,;:M$="LET THE MYSTERY BEGIN...":GOSUB8:PRINTM$
1890 GOSUB1490
1900 GOSUB6
1910 FORX=0TO20STEP5:E(C1+X)=RND(24):E(C2+X)=RND(24):E(C3+X)=RND(24):NEXTX
1920 E(IT*5-5+C4)=RND(24)
1930 G=1:FORX=1TO5
1940 G(X)=RND(24)
1950 NEXTX
1960 E(27)=RND(25):IFE(27)=18THEN1960
1970 RETURN
1980 DATAKITCHEN,HALL,LIVING ROOM,DINING ROOM,HALL,DEN,LIBRARY,HALL,GAME ROOM,SERVANTS QUARTERS,HALL
1981 DATAGARAGE,MUSIC ROOM,HALL,BATHROOM,WINE CELLAR,BASEMENT,SWIMMING POOL,MASTER BEDROOM,BALCONY
1982 DATAGUEST BEDROOM,CHIMNEY,UPSTAIRS BATH,ATTIC
1990 DATA4,5,8,9,11,14,17,23,1,2,5,6,8,11,16,19,20,1,2,3,4,5,7
1991 DATA8,9,10,11,12,13,14,19,20,2,3,5,6,8,9,10,11,12,14,15,18
1992 DATA20,21,14,20,22,24,2,17,20,22
2000 DATAA LARGE POT OF CLAM CHOWDER SIMMERING ON THE STOVE. A SINK FULL OF DIRTY DISHES.
2001 DATAA SPIRAL STAIRCASE WITH CHROME RAILING LEADING UP.
2002 DATAA BRICK FIREPLACE WITH A LARGE MANTLE ON THE EAST WALL.
2010 DATAA BEAUTIFUL CRYSTAL CHANDELIER. A CHINA HUTCH ON THE SOUTH WALL.
2020 DATALOTS OF PLANTS UNDER A LARGE SKYLIGHT.
2021 DATAA LARGE OAK DESK AND MC-10 COMPUTER, LOTS OF BOOKS, A BEAUTIFUL PAINTING ON THE EAST WALL.
2022 DATAAN MC-10 COMPUTER CONNECTED TO A GIANT SCREEN T.V.
2023 DATAA COZY LITTLE ROOM,A COAT RACK,A LARGE CAR. LOTS OF TOOLS.
2030 DATAA GRAND PIANO IS IN THE CENTER OF THE ROOM-- THERE IS A GOLD CANDELABRA ON IT.
2031 DATAA DIMLY LIT CORRIDOR WITH A NARROW STAIRCASE WINDING DOWN.
2032 DATAA SUNKEN TUB,LOTS OF DUSTY BOTTLES,A DIRTY ROOM WITH LOTS OP EMPTY CRATES.
2040 DATAA LARGE KIDNEY SHAPED POOL WITH SPARKLING BLUE WATER.
2041 DATAA KINGSIZE WATERBED AND STONE FIREPLACE ON SOUTH WALL.
2042 DATAA WROUGHT IRON RAILING,A MIRRORED CEILING AND SUNKEN JACCUZI TUB.
2043 DATASOOT,SMALL BUT PRACTICAL ROOM,DUST-OLD CLOTHES-LOTS OF BOX'S
2050 DATAVICTIM'S GRANDMOTHER. SHE IS PLUMP & WHITE HAIRED (IN BUN) WITH APRON+STOCKINGS & WIRE GLASSES. LOVES COLA (+RUM)
2060 DATAGRANDMA,1
2070 DATAMAID. SHE'S VERY TALL WITH LONG BRAIDED BLACK HAIR (+RED RIBBON)-- ALWAYS CARRIES FEATHER DUSTER & DIME STORE ROMANCE
2080 DATAMAID,1
2090 DATALONG LOST DAUGHTER (CURLY BLONDE & BLUE EYED) WHO'S SHOWN UP AT THE WILL READING. DIETS BUT LOVES CANDY (& FASHION)
2100 DATADAUGHTER,1
2110 DATAFAMILY DOCTOR. MID 50'S DISTINGUISHED (WITH PIPE) SLIGHTLY GREYING BROWN HAIR (TEMPLES) & THE HINT OF A GERMAN ACCENT
2120 DATADOCTOR,1
2130 DATAGARDENER. BALD WITH A FEW GREY HAIRS. ALWAYS HAS TOOLS AND SOIL COVERED HANDS. HARD OF HEARING HE SPEAKS LOUDLY
2140 DATAGARDENER,1
2150 DATAA WHITE HAIR,A HAIRPIN,A 3X5 CARD,AN ALCOHOL BOTTLE,AN EMPTY GLASS CASE,A BLACK HAIR,A RED THREAD,A CHICKEN(?) FEATHER
2152 DATAA PAGE TORN FROM A BOOK,A STORE RECEIPT,A STUB OF AN AIRLINE TICKET,A DOG SNIFFING THE GROUND,A BLONDE HAIR
2160 DATAA CANDY WRAPPER,A GARMENT BAG,A TONGUE DEPRESSOR,A BROWN HAIR,A SILVER MEDALLION
2161 DATAA HAZE IN THE AIR,A BIRTHDAY CARD,DIRT STAINS,A BATTERY,A TROWEL,NO HAIR AT ALL,A CAT TWITCHING HER NOSE
2162 DATAAN ANCIENT LOOKING SCRIPT,A CORPSE
2170 DATA,TO PIN A BUN,A RECIPE FOR APPLE PIE,FOR INTERNAL USE ONLY,MFG. BY THE WIRE RIM GLASS CO.,,LEADS TO A RIBBON
2171 DATAI CAN'T SEE THROUGH ALL THE DUST,HER HANDS BECAME WET WITH PERSPIRATION AS THEIR EYES MET...
2180 DATATHANK YOU FOR SHOPPING AT THE TALL GIRLS SHOP,JANUARY 21ST. THE SAME DAY AS THE WILL READING
2181 DATAHE SMELLS PERFUME-VERY SEXY AND SENSUOUS,,NEW AND IMPROVED,A HALSTON ORIGINAL
2190 DATALOOKS LIKE A POPSICLE STICK,,SOME KIND OF WRITING-MAYBE GERMAN,LOOKS AND SMELLS LIKE PIPE SMOKE,HAPPY 54TH
2191 DATAGO LOOK IN YOUR GARDEN IF YOU WANT TO EXAMINE SOME DIRT,QUITE SMALL-MAYBE FROM A HEARING AID
2200 DATABRIGHT SILVER WITH WOOD HANDLE,,SHE SMELLS PESTICIDES AND MANURE,YOU ARE WELL LIKED-YOU GET TWO EXTRA MOVES THIS TURN.
2201 DATAFEELS CLAMMY-SMELLS TERRIBLE
2210 CLS:M$="WHODUNIT IS A MURDER MYSTERY GAME FOR 2-5 PLAYERS.":GOSUB1
2211 M$="THE COMPUTER WILL SELECT ONE OF THE CHARACTERS AT RANDOM AS THE MURDERER.":GOSUB1
2212 M$="IT WILL THEN PLACE CLUES THROUGHOUT THE HOUSE.":GOSUB1:GOSUB2330:CLS
2220 M$="IT'S THEN YOUR JOB TO ROAM ABOUT THE HOUSE GATHERING INFORMATION.":GOSUB1
2221 M$="BE CAREFUL NOT TO ACCUSE SOMEONE PREMATURLY BECAUSE THERE WILL BE CLUES POINTING TO EVERYONE!":GOSUB1
2222 M$="HOWEVER THERE WILL BE FOUR CLUES POINTING TO THE GUILTY PERSON WHILE EVERYONE ELSE WILL HAVE FEWER.":GOSUB1
2223 GOSUB2330:CLS
2240 M$="IF YOU ARE NOT SURE WHO A CLUE BELONGS TO YOU MAY EXAMINE IT, BUT NOT REMOVE EVIDENCE FROM A ROOM.":GOSUB1
2241 M$="ONCE YOU FEEL YOU KNOW WHO THE MURDERER IS YOU MUST FIND AND ELIMINATE HIM/HER.":GOSUB1
2250 M$="IF YOU FEEL THAT YOU ARE THE GUILTY PERSON YOU MUST THEN FIND AND HIDE THE BODY.":GOSUB1
2251 M$="(THE SWIMMING POOL IS A GOOD PLACE TO HIDE A BODY.)":GOSUB1
2252 M$="A COMPUTER SCRATCH PAD IS PROVIDED FOR KEEPING TRACK OF CLUES":GOSUB1
2260 GOSUB2330:CLS
2270 M$="THE GAME IS PLAYED BEST WHEN ONLY THE PERSON WHOSE TURN IT IS CAN SEE THE SCREEN.":GOSUB1
2271 M$="BUT THAT IS LEFT UP TO YOU TO DECIDE.":GOSUB1
2280 GOSUB2330:CLS
2290 PRINT"THE AVAILABLE VERBS ARE:":PRINT:PRINT"GO ------- TO MOVE":PRINT"GET ------ TO PICK UP OBJECTS"
2291 PRINT"EXAMINE -- TO LOOK AT EVIDENCE":PRINT"DROP ----- TO DROP OBJECTS":PRINT"DESCRIBE - DESCIBES A CHARACTER"
2300 PRINT"LOCATE --- TO FIND A CHARACTER":PRINT"SHOOT ---- TO DEAL WITH MURDERER";:PRINT"LOOK ----- GIVES YOUR LOCATION"
2301 PRINT"INV ------ LIST WHAT YOU HOLD" :PRINT"HELP ----- LIST THESE VERBS":PRINT"CLUE ----- SCRATCH PAD"
2310 GOSUB2330:CLS
2320 PRINT"EXCEPT FOR-",,,,"*LOOK",,"*INV",,"*HELP",,"*CLUE":PRINT
2321 M$="WHICH ARE USED ALONE, USE TWO WORD COMMANDS SUCH AS:":GOSUB1:PRINT:PRINT"*GO NORTH*"
2330 PRINT@484,"PRESS enter TO CONTINUE";
2340 X$=INKEY$:IFX$<>CHR$(13)THEN2340
2350 PRINTB$;:RETURN
2360 REM MC-10 EDITS JIM GERRIE
