0 CLEAR1800:DIMC1,C2:GOTO50
1 C1=1:C2=32
2 IFMID$(M$,C2,1)<>""ANDMID$(M$,C2,1)<>" "THENC2=C2-1:GOTO2
3 PRINTMID$(M$,C1,C2-C1):C1=C2+1:C2=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
9 GOSUB1:GOTO540
50 DIMOB$(52),OB(52),RM$(32),RM(40),DI$(32)
60 CLS
70 BA=0:EG=0:GG=0:AD=0:CC=0:UH=0:PS=0:MP=0:WG=0:EI=0:CL=0:DF=0:DS=0:DD=0:AI=0:PA=0:PR=0:MC=0:ME=1:AM=0:CM=0:FB=0:AA=0
80 NO=52:NR=32
90 FORI=1TO32:READRM$(I):NEXT:FORI=1TONO:READOB(I):NEXT:FORI=1TONO:READOB$(I):NEXT
150 DATA"I AM IN A DARK, DAMP AND GLOOMY PIT. RAYS OF LIGHT SHINING DOWN FROM ABOVE ARE THE ONLY SOURCE OF LIGHT."
160 DATA"I AM IN A SMALL NARROW OPENING.","I AM IN A TUNNEL.","I AM IN A LARGE UNDERGROUNG CAVERN."
190 DATA"I AM IN AN UNDERGROUND HOLLOW.","I AM IN AN OPEN FIELD.","I AM IN A YARD.","I AM IN AN OLD ABANDONED SHACK."
230 DATA"I AM IN A SMALL, DINGY BEDROOM.","I AM IN A KITCHEN.","I AM INSIDE A SMALL SENTRY POST.","I AM NEXT TO A MOAT."
270 DATA"I HAVE ENTERED A MERCHANTS TENT.","I AM ON A RIVER BANK.","I AM IN AN EERIE GRAVEYARD."
300 DATA"I AM NEAR A BOTTOMLESS CHASM.","I AM IN A SMALL SANDY CLEARING.","I HAVE COME TO A CLIFF RIDDEN AREA."
330 DATA"I AM IN A LARGE CLOSET.","I AM IN A LONG FEATURELESS HALL.","I AM INSIDE AN ANCIENT ARCHIVE."
360 DATA"I AM IN A COURTYARD OF THE CASTLE.","I AM IN THE GUARDS ROOM","I AM IN A SMALL SECRET ROOM."
390 DATA"I AM IN A WEAPON ROOM.","I AM IN AN OLD SMELLY DUNGEON.","I AM IN A BLOOD STAINED TORCHURE CHAMBER."
420 DATA"I AM IN A SECRET CAVE.","I AM ON TOP OF A LADDER.","I AM IN A DRAGONS LAIR."
430 DATA"I AM IN A ROOM WITH WALLS OF SOLID GOLD.","I AM IN A BIG HOLE, WITH WALLS OF SOLID GOLD"
470 NO$="NORTH ":SO$="SOUTH ":EA$="EAST ":WE$="WEST ":UP$="UP ":DO$="DOWN "
480 DI$(1)=NO$:DI$(2)=SO$:DI$(3)=EA$+WE$:DI$(4)=EA$+WE$:DI$(5)=EA$:DI$(6)=NO$+SO$+EA$+DO$:DI$(7)=SO$:DI$(8)=EA$+WE$+SO$
481 DI$(9)=EA$:DI$(10)=WE$:DI$(11)=SO$:DI$(12)=WE$:DI$(13)=NO$+WE$:DI$(14)=EA$:DI$(15)=SO$:DI$(16)=NO$:DI$(17)=WE$
490 DI$(18)=EA$:DI$(19)=WE$:DI$(20)=NO$:DI$(21)=WE$:DI$(22)=NO$+SO$+EA$+WE$:DI$(23)=WE$:DI$(24)=NO$:DI$(25)=EA$+SO$
491 DI$(26)=WE$+UP$:DI$(27)=EA$:DI$(28)=SO$:DI$(29)=DO$:DI$(30)=NO$+EA$:DI$(31)=WE$:DI$(32)=UP$
500 VI$="VISIBLE ITEMS: "
510 GOSUB7810
520 CLS
530 GOTO6180
540 A$="":F$="":S$="":IFRM=2THENEG=EG+1:IFEG=10ANDGG<>1THEN7620
550 IFRM=30ANDDD<>1THENFB=FB+1
560 IFFB<>3THEN610
561 SH=SH+1
570 IFOB(44)<>-1THEN PRINT"I HAVE BEEN BURNT BY A FIRE BREATHING DRAGON!":GOTO7620
580 IF OB(44)=-1 THEN PRINT"THE DRAGON SENDS A BREATH OF FIRE AT ME, BUT IT REFLECTS OFF MY SHIELD.":FB=0
590 IF SH=2 THEN PRINT"I DON'T KNOW IF I CAN HOLD THE SHIELD FOR MUCH LONGER, BECAUSE IT'S VERY HOT!":GOTO9
600 IF SH=3 THEN PRINT"THE SHIELD IS TOO HOT TO HOLD, SO I DROP IT!":OB(44)=RM:GS=1:GOSUB8090:GOTO3630
610 INPUT"COMMAND";A$
620 IFLEN(A$)>15THEN PRINT"BE SENSIBLE!":GOTO9
630 IFA$=""THEN7310
650 IFA$<>""THENA$=A$+" "
660 FOR I=1 TO LEN(A$)
670 IF MID$(A$,I,1)<>" " THEN NEXT I
680 F$=LEFT$(A$,I-1)
690 FOR I=LEN(A$) -1 TO 1 STEP-1
700 IF MID$(A$,I,1)<>" " THEN NEXT I
710 S$=MID$(A$,I+1,LEN(A$)-I-1)
720 IF F$< "A" OR F$> "Z" THEN GOTO7310
730 ZZ=ASC(A$)-64:IFZZ<1ORZZ>26THEN740
731 ONASC(A$)-64 GOTO740,750,790,1020,1270,2000,2070,3090,3100,3300,3510,3610,3740,3840,3970,4150
732 ON(ASC(A$)-64)-16 GOTO4400,4470,4690,5340,5520,5890,5900,6050,6150,6160
740 REM A
750 REM B
760 IF F$<>"BUY" THEN 7310
770 GOTO3020
790 REM C
800 IF F$<>"CLIMB" THEN 910
810 FOR I=1 TO NO:IF S$=OB$(I) THEN 830
811 NEXT I
820 GOTO7310
830 IF OB(I)=-1 THEN 7360
840 IF OB(I)<>RM THEN 7360
850 IF S$=OB$(1) AND OB(1)=1 THEN CL=1:GOTO7250
860 IF S$=OB$(1) THEN 7250
870 IF S$=OB$(49) THEN PRINT"THE WALLS ARE FAR TOO SLIPPERY.":GOTO9
880 IF F$<>"CHOP" THEN 1010
890 FOR I=1 TO NO:IF S$=OB$(I) THEN 940
900 NEXT I:GOTO7310
910 IF OB(8)=-1 AND BA=0 THEN 950
920 IF OB(8)<>-1 THEN 7360
930 IF BA=1 THEN PRINT"TRY EXAMINING AXE!":GOTO9
940 IFRM=6ANDS$=OB$(3)ANDAD<>1THENPRINT"THE VIBRATIONS LOOSEN THE APPLE, AND IT FALLS TO THE GROUND.":AD=1:BA=1:GOTO9
950 IFRM=20ANDS$=OB$(30)THENPRINT"I MAKE A HOLE BIG ENOUGH TO GET THROUGH.":CC=1:BA=1:GOTO9
960 IFRM=7ANDS$=OB$(35)THENPRINT"YOU HAVE FUN, CHOPPING THE FENCE INTO SPLINTERS":OB(35)=0:OB(48)=7:BA=1:GOTO3630
970 IFRM=6ANDS$=OB$(3)THENPRINT"THE VIBRATIONS CAUSED BY THE AXE, CAUSE THE APPLE TO FALL.":AD=1:BA=1:GOTO9
980 IFRM=1ORRM=2ANDS$=OB$(1)THENPRINT"YOU VIOLENTLY CHOP THE LADDER INTO SPLINTERS.":LD=1:BA=1:OB(1)=0:OB(50)=RM:GOTO3630
990 GOTO7580
1000 IF F$<>"CUT" THEN 7310
1010 GOTO5160
1020 REM D
1030 IF F$<>"D" THEN 1070
1040 IF RM=6 THEN 6180
1050 IF RM=29 AND SR=1 THEN 6180
1060 IF RM=29 AND SR=2 THEN 6210
1070 IF F$<>"DROP" THEN 1140
1080 FOR I=1 TO NO:IF S$=OB$(I) THEN 1100
1090 NEXT I:GOTO7310
1100 IF OB(I)<>-1 THEN 7340
1110 IF S$=OB$(31) THEN WG=0
1120 IF S$=OB$(26) THEN 4280
1130 OB(I)=RM:EI=EI-1:GOTO3630
1140 REM * DIG *
1150 IF OB(9)<>-1 THEN 7360
1160 IF F$<>"DIG" THEN 7310
1170 IF RM<>15 THEN 1220
1180 IF DF=1 THEN 7500
1190 IF SA<>1 THEN PRINT"GHOST WON'T LET ME!":GOTO9
1200 OB(13)=15: OB(15)=15: DF=1
1210 GOTO7520
1220 IF RM<>17 THEN 7500
1230 IF DS=1 THEN 7500
1240 OB(10)=17: DS=1
1250 GOTO7520
1270 REM E 
1280 IF F$<>"E" THEN 1460
1290 IF RM=18 THEN 6660
1300 IF RM=14 THEN 6540
1310 IF RM=19 THEN 6760
1320 IF RM=6 THEN 6510
1330 IF RM=22 THEN 6790
1340 IF RM=5 THEN 6270
1350 IF RM=4 THEN 6240
1360 IF RM=3 THEN 6210
1370 IF RM=25 THEN 6850
1380 IF RM=27 THEN 6940
1390 IF RM=9 THEN 6390
1400 IF RM=8 THEN 6450
1410 IF RM<>30 THEN 1440
1420 IF DD<>1 THEN PRINT"DRAGON WON'T LET ME!":GOTO9
1430 IF DD=1 THEN 7140
1440 REM * EXAM *
1450 GOTO7460
1460 IF F$<>"EXAM" THEN 1500
1470 FOR I=1 TO NO:IF S$=OB$(I) THEN 1500
1480 NEXT I
1490 GOTO7310
1500 IF OB(I)<>RM AND OB(I)<>-1 THEN 7360
1501 GOTO1510
1510 IF S$=OB$(1) THEN PRINT"IT'S FAIRLY SHORT.":GOTO9
1520 IF S$<>OB$(38) THEN 1550
1530 IF RM=16 OR RM=17 THEN PRINT"IT'S A 2-METRE WIDE BOTTOMLESS CHASM."
1531 GOTO7360
1550 IF S$=OB$(2) THEN PRINT"HE GIVES YOU A BIG GRIN, WHILST LICKING HIS CHOPS":GOTO9
1560 IF S$=OB$(18) THEN PRINT"IT'S GOT SOMETHING WRITTEN ON IT.":GOTO9
1570 IF S$=OB$(19) THEN PRINT"IT LOOKS QUITE VALUABLE.":GOTO9
1580 IF S$=OB$(44) AND SH=0 THEN PRINT"IT'S GOT A SENSE OF STRENGTH ABOUT IT!":GOTO9
1590 IF S$=OB$(44) AND SH>1 THEN PRINT"THE SHIELD IS RED HOT!":GOTO9
1600 IF S$=OB$(3) AND AA=0 THEN 1610
1601 GOTO1620
1610 PRINT"THE TREE IS PETRIFIED, YET THERE IS A RED APPLE ON ONE OF IT'S BRANCHES.": OB(4)=RM: AA=1:GOSUB8090:GOTO3630
1620 IF S$=OB$(3) AND AA=1 AND AD=0 THEN 1610
1630 IF S$=OB$(3) AND AD=1 THEN PRINT"IT'S PETRIFIED!":GOTO9
1640 IF S$="SYMBOL" THEN PRINT "IT RESEMBLES A GHOST.":GOTO9
1650 IF S$=OB$(37) THEN PRINT"IT'S RUSTY":GOTO9
1660 IF S$=OB$(4) THEN PRINT"IT'S A BIG, RED, HEATHLY, LOOKING APPLE.":GOTO9
1670 IF S$=OB$(21) THEN PRINT"IT'S STUCK TO THE WALL":GOTO9
1680 IF S$=OB$(22) THEN PRINT"IT'S A SMELLY DECAYED CORPSE.":GOTO9
1690 IF S$=OB$(51) THEN PRINT"IT'S THE TREASURE OF ELGON!!":GOTO9
1700 IF S$=OB$(36) THEN PRINT"IT'S A SHARPENING STONE.":GOTO9
1710 IF S$=OB$(24) THEN PRINT"THERE'S SOMETHING WRITTEN ON IT.":GOTO9
1720 IF S$=OB$(6) THEN PRINT"IT LOOKS DESERTED.":GOTO9
1730 IF S$=OB$(26) THEN PRINT"IT SEEMS STURDY.":GOTO9
1740 IF S$=OB$(7) AND MS=0 THEN PRINT"HE LOOKS POOR.":GOTO9
1750 IF S$=OB$(7) AND MS=1 THEN PRINT"HE'S DEAD! AND I DON'T THINK HIS CONDITION WILL GET ANY BETTER!":GOTO9
1760 IF S$<> OB$(20) THEN 1790
1770 IF AI<>1 THEN PRINT"THERE IS A SMALL ROUND HOLE IN THE MIDDLE.":GOTO9
1780 PRINT"THERE IS AN ANKH INSERTED IN THE MIDDLE OF IT.":GOTO9
1790 IF S$=OB$(8) THEN 1980
1800 IF S$=OB$(29) THEN PRINT"IT HAS A VERY STRONG CURRENT.":GOTO9
1810 IF S$<>OB$(30) THEN 1840
1820 IF CC<>1 THEN PRINT"IT'S AN OLD WOODEN CLOSET, AND IT'S LOCKED":GOTO9
1830 PRINT"IT'S GOT A HOLE CHOPPED IN IT.":GOTO9
1840 IF S$=OB$(31) THEN PRINT"THEY ARE READING GLASSES.":GOTO9
1850 IF S$=OB$(10) THEN PRINT"IT'S GOT A STRANGE SYMBOL THAT PROTRUDES FROM IT.":GOTO9
1860 IF S$=OB$(32) THEN PRINT"IT'S STEEL AND IT FEELS VERY THICK.":GOTO9
1870 IF S$=OB$(11) THEN PRINT"HE IS BIG, AND LOOKS LIKE HE COULD HAVE GREAT STRENGTH.":GOTO9
1880 IF S$<>OB$(35) THEN 1910
1890 IF PA=1 THEN 7420
1900 PRINT"IT HAS A LOOSE PLANK, NAILED ON WITH ONE NAIL.":GOTO9
1910 IF S$=OB$(12) THEN PRINT"IT IS AN OLD GRAVE COVERED WITH WEEDS.":GOTO9
1920 IF S$=OB$(34) THEN PRINT"IT HAS A CLAWED HEAD.":GOTO9
1930 IF S$=OB$(38) THEN PRINT"THE CHEST LOOKS VERY OLD AND YOU SENSE IT IS FULL OF TREASURE.":GOTO9
1940 IF S$=OB$(13) THEN PRINT"THEY LOOK LIKE THEY ARE HUMAN.":GOTO9
1950 IF S$=OB$(14) THEN PRINT"IT IS A RING WITH A SMALL THIN SCREW COMING OFF IT.":GOTO9
1960 IF S$=OB$(33) THEN PRINT"IT'S A SENTRY POST!":GOTO9
1970 GOTO7420
1980 IF BA<>1 THEN PRINT"IT'S SHARP":GOTO9
1990 IF BA=1 THEN PRINT"IT'S BLUNT.":GOTO9
2000 REM F
2010 IF F$<>"FEED" THEN 7310
2020 IF S$<>OB$(2) THEN 2060
2030 IF OB(4)<>-1 THEN 7360
2040 IF RM<>2 THEN 7360
2050 PRINT"THE GRUE HAS LIVED FOR OVER 100 YEARS AND ALONG THE WAY, HE HAS LEARNT HOW TO FEED HIMSELF!":GOTO9
2060 GOTO7580
2070 REM G
2080 IF F$<>"GET" THEN 2550
2090 IF EI>4 THEN PRINT"I'M CARRYING TOO MUCH!":GOTO9
2100 FOR I=1 TO NO:IF S$=OB$(I) THEN 2110
2101 NEXT I:GOTO7310
2110 IF OB(I)<>-1 THEN 2120
2111 GOTO2140
2120 IF S$<>OB$(26) THEN 2170
2130 IF PR=1 OR PA=0 AND RM=7 THEN PRINT "HOW?":GOTO9
2140 IF PA=1 AND OB(26)=RM THEN 2540
2150 IF RM<>7 THEN 7380
2160 IF OB(26)=-1 THEN 7480
2170 IF S$=OB$(52) THEN 2180
2171 GOTO2200
2180 IF RM=31 AND MC=1 THEN 7400
2190 GOTO7560
2200 IF OB(I)<>RM THEN 7380
2210 IF S$=OB$(36) THEN PRINT"IT'S HEAVY!":GOSUB8090:GOTO2540
2220 IF S$=OB$(1) THEN 2540
2230 IF S$=OB$(48) THEN 2540
2240 IF S$=OB$(51) THEN 7190
2250 IF S$=OB$(44) AND GS=0 THEN 2540
2260 IF S$=OB$(44) AND GS=1 THEN PRINT"TOO HOT!":GOTO9
2270 IF S$=OB$(43) THEN PRINT"I FEEL POWER WITHIN IT":GOSUB8090:GOTO2540
2280 IF S$=OB$(8) THEN 2540
2290 IF S$=OB$(9) AND MP<>1 THEN PRINT"MERCHANT WANTS SOMETHING.":GOTO9
2300 IF S$=OB$(9) AND MP=1 THEN 2540
2310 IF S$=OB$(18) OR S$=OB$(38) THEN PRINT"IT'S TOO HEAVY!":GOTO9
2330 FOR X=10 TO 11:IF S$=OB$(X) THEN 2540
2331 NEXT X
2340 FOR Y=13 TO 15:IF S$=OB$(Y) THEN 2540
2341 NEXT Y
2350 IF S$=OB$(18) THEN 7400
2360 IF S$=OB$(12) THEN 7400
2370 IF S$=OB$(19) THEN 2540
2380 IF S$=OB$(26) THEN 2540
2390 IF S$=OB$(27) THEN 2540
2400 IF S$=OB$(31) THEN 2540
2410 IF S$=OB$(7) AND MS=0 THEN PRINT"HE AVOIDS YOU SAYING 'ARE YOU SOME KIND OF WEIRDO?'":GOTO9
2420 IF S$=OB$(7) AND MS=1 THEN PRINT"I DON'T WANT TO PICK UP A BODY WITH IT'S GUTS ALL OVER THE PLACE!":GOTO9
2430 IF S$=OB$(34) THEN 2540
2440 IF S$=OB$(35) AND UH=1 THEN 2540
2450 IF S$=OB$(37) AND PS=1 THEN 2540
2460 IF S$<>OB$(22) THEN 2490
2470 PRINT "NO THANKS!":GOTO9
2490 IF S$=OB$(24) THEN PRINT"IT'S STUCK!":GOTO9
2500 IF S$=OB$(4) AND AA=0 THEN PRINT"I CAN'T SEE ANY APPLE AROUND HERE!":GOTO9
2510 IF S$=OB$(4) AND AD<>1 THEN PRINT"I CAN'T REACH IT!":GOTO9
2520 IF S$=OB$(4) AND AD=1 THEN OB(4)=-1:GOTO3630
2530 GOTO7400
2540 OB(I)=-1:EI=EI+1:GOTO3630:REM EI=ENOUGH INVENTORY
2550 REM * GO *
2560 IF F$<>"GO" THEN 2920
2570 IF S$<>OB$(5) THEN 2630
2580 IF RM<>2 THEN 7360
2590 IF GG<>1 THEN 2790
2600 IF OB(1)=-1 THEN PRINT"SOMETHING WON'T FIT!":GOTO9
2610 IF S$=OB$(5) AND RM<>2 THEN 7360
2620 GOTO6240
2630 FOR I=1 TO NO:IF S$=OB$(I) THEN 2640
2631 NEXT I:GOTO7360
2640 IF S$="PLANK" THEN 2750
2650 IF S$=OB$(52) AND RM=31 AND MC=1 THEN 7290
2660 IF OB(I)<>RM THEN 7360
2670 IF S$=OB$(6) AND RM=7 THEN 6390
2680 IF S$=OB$(25) THEN 6940
2690 IF S$=OB$(17) THEN 6880
2700 IF S$=OB$(1) THEN 830
2710 IF S$=OB$(42) THEN 6820
2720 IF S$=OB$(29) THEN PRINT "I'VE BEEN SWEPT UNDER BY THE CURRENT, AND DROWN.":GOTO7620
2730 IF S$=OB$(23) THEN 7000
2740 IF S$<>OB$(26) THEN 2830
2750 IF PR<>1 THEN 7600
2760 IF RM=14 THEN 6600
2770 IF RM=15 THEN 6570
2780 GOTO7600
2790 IF GG<> 1 THEN PRINT"GRUE WON'T LET ME!":GOTO9
2800 GOTO6240
2810 IF AI<>1 THEN PRINT"I DON'T KNOW WHAT YOU MEAN.":GOTO9
2820 GOTO7000
2830 IF S$<>OB$(33) THEN 2860
2840 IF RM<>12 THEN 7360
2850 GOTO6480
2860 IF S$<>OB$(30) THEN 2890
2870 IF CC<>1 THEN PRINT "ONLY GHOSTS CAN WALK THROUGH DOORS!":GOTO9
2880 IF CC=1 THEN 6730
2890 IF S$<>OB$(32) THEN 7310
2900 IF PS=1 AND DO=1 THEN 7060
2901 GOTO7360
2920 REM * GIVE *
2930 IF F$<>"GIVE" THEN 7310
2940 FOR I=1 TO NO:IF S$=OB$(I) THEN 2960
2950 NEXT I:GOTO7310
2960 IF S$<>OB$(4) THEN 3010
2970 IF RM=13 THEN 3060
2980 IF RM<>2 THEN 7360
2990 IF OB(4)<>-1 THEN 7360
3000 PRINT"TO WHOM?":GOTO9
3010 IF S$<>OB$(19) THEN 3050
3020 IF RM<>13 THEN 7360
3030 IF OB(19)<>-1 THEN 7360
3040 CM=1:GOTO3000
3050 IF S$<>OB$(4) THEN 7600
3060 IF OB(4)<>-1 THEN 7360
3070 PRINT"TO WHOM?":ME=1:AM=1:GOTO9
3080 GOTO7600
3090 REM H
3100 REM I
3110 IF F$<>"I" THEN 3200
3120 Z=0: FOR I=1 TO NO:IF OB(I)=-1 THEN Z=Z+1
3130 NEXT I
3140 IF Z=0 THEN PRINT"I'M NOT CARRYING ANYTHING.":GOTO9
3150 PRINT:PRINT"I AM CARRYING THE FOLLOWING:"
3160 FOR I=1 TO NO
3170 IF OB(I)=-1 THEN PRINT OB$(I)+". ";
3180 NEXT I:PRINT
3190 GOTO540
3200 IF F$<>"INSERT" THEN 7310
3210 FOR I=1 TO NO:IF S$=OB$(I) THEN  3230
3220 NEXT I:GOTO7310
3230 IF S$<>OB$(14) THEN 7580
3240 IF OB(14)<>-1 THEN 7360
3250 IF RM<>18 THEN 7360
3260 PRINT"IT FITS PERFECTLY IN THE HOLE.":GOSUB8090
3270 OB(14)=0:AI=1
3280 GOTO3630
3300 REM J
3310 IF F$<>"JUMP" THEN 7310
3320 IF RM<>29 THEN 3370
3330 IF CL=1 THEN 6330
3340 PRINT"I JUMP UP, AND LUCKILY LAND BACK ON THE LADDER JUMPING WHILST ON A LADDER IS NOT A SAFE THING TO DO!"
3350 CL=0
3360 GOTO9
3370 IF RM<>16 THEN 3410
3380 PRINT"I JUMP ACROSS THE CHASM, AND JUST MAKE IT TO THE OTHER SIDE."
3390 GOSUB8090
3400 GOTO6660
3410 IF RM<>17 THEN 3460
3420 IF OB(36)=-1 THEN PRINT"THE WEIGHT OF THE STONE, SHORTENS THE JUMP, AND I FALL DOWN THE PIT.":GOTO7620
3430 PRINT"I JUMP ACROSS THE CHASM, AND JUST MAKE IT TO THE OTHER SIDE."
3440 GOSUB8090
3450 GOTO6630
3460 IF RM<>1 THEN 3480
3470 PRINT"I JUMP AS FAR AS I COULD, BUT I END UP A FEW INCHES SHORT FROM THE TOP.":GOTO9
3480 IF RM<>2 THEN 3500
3490 PRINT"I HIT MY HEAD ON THE ROOF.":GOTO9
3500 PRINT"WHEE!":GOTO9
3510 REM K
3520 IF F$<>"KILL" THEN 7310
3530 IF S$=OB$(7) AND RM=13 AND MS=0 THEN 3570
3540 IF S$=OB$(46) AND RM=30 AND DD=0 THEN 3580
3550 IF S$=OB$(2) AND RM=2 AND GG=0 THEN 3560
3551 GOTO7600
3560 IF S$<>OB$(2) AND RM<>2 THEN 3570
3561 GOTO3590
3570 IF S$<>OB$(7) AND RM<>13 THEN 3580
3571 GOTO3590
3580 IF S$<>OB$(46) AND RM<>30 THEN 3600
3581 GOTO3590
3590 PRINT"HOW?":GOTO9
3600 GOTO7580
3610 REM L
3620 IF F$<>"LOOK" THEN 7310
3630 CLS
3640 PRINT RM$(RM):PRINTVI$
3650 PRINT
3670 FOR I=1 TO NO:IF OB(I)=RM THEN PRINT OB$(I)+". ";
3680 NEXT I
3700 PRINT:PRINT
3710 PRINT"SOME OBVIOUS EXITS ARE: "
3720 PRINT DI$(RM):GOSUB9000
3730 GOTO540
3740 REM M
3750 IF F$<>"MOVE" THEN 7310
3760 FOR I=1 TO NO:IF OB$(I)=S$ THEN 3770
3761 NEXT I:GOTO7310
3770 IF S$<>OB$(38) THEN 3810
3780 IF RM<>31 THEN 7360
3790 IF MC=1 THEN 3800
3791 PRINT"AFTER MOVING THE CHEST, YOU DISCOVER A SMALL HOLE UNDER IT.":GOSUB8090: OB(52)=31: MC=1:GOTO3630
3800 PRINT"OK, I MOVE IT BACK OVER THE HOLE.":OB(52)=0:MC=0:GOSUB8090:GOTO3630
3810 IF S$=OB$(36) THEN PRINT"OK":GOTO9
3820 FOR I=1 TO NO:IF S$=OB$(I) THEN 7580
3821 NEXT I
3830 GOTO7310
3840 REM N
3850 IF F$<>"N" THEN 7310
3860 IF RM=1 THEN 6210
3870 IF RM=16 THEN 6600
3880 IF RM=13 THEN 6330
3890 IF RM=20 THEN 6820
3900 IF RM=24 THEN 6790
3910 IF RM=6 THEN 6360
3920 IF RM<>30 THEN 3950
3930 IF DD<>1 THEN PRINT "DRAGON WON'T LET ME!":GOTO9
3940 GOTO6300
3950 IF RM=22 THEN 6910
3960 GOTO7460
3970 REM O
3980 IF F$<>"OPEN" THEN 7310
3990 FOR I=1 TO NO:IF S$=OB$(I) THEN 4010
4000 NEXT I:GOTO7310
4010 IF S$<>OB$(21) THEN 4070
4020 IF BO=1 THEN PRINT"IT'S ALREADY OPEN!":GOTO9
4030 IF RM<>11 THEN 7360
4040 IF BU<>1 THEN PRINT "IT'S LOCKED!":GOTO9
4050 OB(16)=11:BO=1
4060 GOTO3630
4070 IF S$<>OB$(32) THEN 4120
4080 IF RM<>5 THEN 7360
4090 IF PS<>1 THEN 7360
4100 PRINT"OK.":DO=1
4110 GOTO9
4120 IF S$<>OB$(38) THEN 7580
4130 IF RM<>31 THEN 7360
4140 PRINT"IT WON'T BUDGE!":GOTO9
4150 REM P
4160 IF F$<>"PAY" THEN 4220
4170 IF S$<>OB$(7) THEN 4250
4180 IF MP=1 AND OB(19)=-1 THEN PRINT"OK": OB(19)=0:GOTO9
4190 IF RM<>13 THEN 7360
4200 IF OB(19)<>-1 THEN 7360
4210 OB(19)=0:MP=1:PRINT"THE MERCHANT OFFERS YOU THE SHOVEL.":GOTO9
4220 IF F$<>"PUT" THEN 4330
4230 FOR I=1 TO NO:IF S$=OB$(I) THEN 4260
4240 NEXT I:GOTO7310
4250 GOTO7580
4260 IF S$<>OB$(26) THEN 4320
4270 IF OB(26)<>-1 THEN 7360
4280 IF RM<>14 THEN 4320
4290 PRINT"THE PLANK IS PLACED OVER THE RIVER."
4300 GOSUB8090
4310 OB(41)=15:OB(26)=RM:PR=1
4320 OB(I)=RM:GOTO3630
4330 IF F$<>"PULL" THEN 4380
4340 IF S$<>OB$(16) THEN 4390
4350 PRINT"YOU HEAR A LOUD THUD, BUT NOTHING APPEARS TO HAPPEN.":OB(42)=12:DB=1:GOTO9
4380 IF F$<>"PLACE" THEN 7310
4390 GOTO4230
4400 REM Q
4410 IF F$<>"QUIT" THEN 7310
4420 PRINT"SURE YOU WANT TO QUIT <Y/N>?"
4440 Z$=INKEY$:IFZ$="Y"THEN7630
4450 IFZ$="N"THEN4460
4451 GOTO4440
4460 PRINT"OK":GOTO9
4470 REM R 
4480 IF F$<>"READ" THEN 4600
4490 FOR I=1 TO NO:IF S$=OB$(I) THEN 4510
4500 NEXT I:GOTO7310
4510 IF S$<>OB$(24) THEN 4560
4520 IF WG<>1 THEN 7360
4530 IF RM<>1 THEN 7360
4540 PRINT:PRINT"IF YOU ARE STUMPED, THEN THE MAGIC WORD IS IN ALPHABETICAL; AND 5"
4550 PRINT"LONG; AND DON'T FORGET FIRST ONE IS ALSO THE LAST...":GOTO9
4560 IF S$<>OB$(18) THEN 4580
4570 IF RM<>21 OR WG<>1 THEN 7360
4580 PRINT"THE BOOK HAS ONE WORD WRITTEN IN IT:":PRINT:PRINT"'RACK'":PRINT:OB(17)=21:GOSUB8090
4590 GOTO3630
4600 IFF$<>"RESTORE"THEN7310
4610 IFS$<>"GAME"THEN7310
4620 OPEN "I",#1,"ELGON.SAV"
4630 FOR I=1 TO NO:INPUT #1,OB(I):NEXT I
4640 INPUT #1,RM
4650 INPUT #1,BA,GG,DD,CL,CC,AD,EI,DF,SA,DS,AI,PR,PA,MP,UH,PS,EG
4660 INPUT #1,DO,YS,BO,BU,SA,CR,WG,CM,AM,ME,SH,GS,MS,AA,MC,AD
4670 CLOSE #1
4680 GOTO3630
4690 REM S
4700 IF F$<>"S" THEN 4810
4710 IF RM=22 THEN 6760
4720 IF RM=6 THEN 6540
4730 IF RM=2 THEN 6180
4740 IF RM=7 THEN 6330
4750 IF RM=8 THEN 6360
4760 IF RM=11 THEN 6510
4770 IF RM=25 THEN 6820
4780 IF RM=15 THEN 6630
4790 IF RM=28 THEN 6700
4800 GOTO7460
4810 IF F$<>"SLAY" THEN 4900
4820 IF S$<>OB$(46) THEN 4870
4830 IF RM<>30 THEN 7360
4840 IF OB(43)<>-1 THEN 7360
4850 IF DD=1 THEN PRINT"WHAT DRAGON?":GOTO9
4860 GOTO5690
4870 IF S$<>OB$(7) THEN 7610
4880 IF OB(43)<>-1 OR RM<>13 THEN 7360
4890 MS=1:PRINT"YOU SLAY THE POOR MERCHANT TO DEATH.":GOTO9
4900 IF F$<>"SHOW" THEN 4990
4910 FOR I=1 TO NO:IF S$=OB$(I) THEN 4930
4920 NEXT I:GOTO7310
4930 IF S$<>OB$(10) THEN 5460
4940 IF OB(10)<>-1 THEN 7360
4950 IF RM<>15 THEN 7440
4960 PRINT"THE GHOST LETS OUT A LOUD SHRIEK, AS HE DISINTEGRATES.":GOSUB8090
4970 OB(11)=0: SA=1
4980 GOTO3630
4990 IF F$<>"SAY" THEN 5090
5000 IF S$<>"ACKRA" THEN 5080
5010 IF CR=1 THEN PRINT"OK":PRINT S$:GOTO9
5020 IF RM<>18 THEN PRINT"OK":PRINT S$:GOTO9
5030 IF AI<>1 THEN PRINT"OK":PRINT S$:GOTO9
5040 PRINT"A RAY OF LIGHT SHINES DOWN FROM THE SKY, REFLECTS THROUGH THE ANKH AND OPENS A SMALL CRAWLWAY IN THE CLIFF."
5050 OB(23)=18:CR=1
5060 GOSUB8090
5070 GOTO3630
5080 PRINT"OK":PRINT S$:GOTO9
5090 IF F$<>"SHARPEN" THEN 5150
5100 IF S$<>OB$(8) THEN 7580
5110 IF OB(8)<>-1 THEN 7360
5120 IF RM<>28 THEN 7360
5130 PRINT"THE AXE IS AS SHARP AS IT WAS ORIGINALLY.": BA=0:GOTO9
5140 GOTO7580
5150 IF F$<>"SAW" THEN 5210
5160 IF S$<>OB$(37) THEN 7580
5170 IF OB(27)<>-1 THEN 7360
5180 IF RM<>5 THEN 7360
5190 IF PS=1 THEN PRINT "I HAVE ALREADY DONE THAT!":GOTO9
5200 PRINT"THE PADLOCK IS SAWN EASLIY, AND IT FALLS TO THE GROUND": PS=1:GOTO9
5210 IF F$<>"SHAKE" THEN 5250
5220 IF S$<>OB$(3) THEN 7580
5230 IF RM<>6 THEN 7360
5240 PRINT"I SHAKE THE TREE AS HARD AS I CAN, BUT IT IS AS HARD AS A ROCK--THE APPLE DOESN'T EVEN BUDGE.":GOTO9
5250 IF F$<>"SAVE" THEN 7320
5260 IF S$<>"GAME" THEN 7320
5270 SV$="ELGON.SAV"
5280 OPEN "O",#1,"ELGON.SAV"
5290 FOR I=1 TO NO: WRITE #1,OB(I):NEXT I
5300 WRITE #1,RM
5310 WRITE #1,BA,GG,DD,CL,CC,AD,EI,DF,SA,DS,AI,PR,PA,MP,UH,PS,EG
5320 WRITE #1,DO,YS,BO,BU,SA,CR,WG,CM,AM,ME,SH,GS,MS,AA,MC,AD
5330 CLOSE #1:PRINT"OK":GOTO9
5340 REM T
5350 IF F$<>"TO" THEN 7310
5360 IF AM=1 THEN 5380
5370 IF CM=1 THEN 5480
5371 GOTO5440
5380 IF ME<>1 THEN 5430
5390 IF OB(4)<>-1 THEN 7360
5400 IF RM<>13 THEN 7360
5410 IF MP=1 THEN PRINT"OK":OB(4)=0:AM=0:ME=0:GOTO9
5420 IF MP<>1 THEN PRINT"THE MERCHANT OFFERS YOU THE SHOVEL.":MP=1:OB(4)=0:ME=0:AM=0:GOTO9
5430 FOR I=1 TO NO:IF S$=OB$(I) THEN 5440
5431 NEXT I:GOTO7580
5440 IF S$<>OB$(2) THEN 7310
5450 IF OB(4)<>-1 THEN 7360
5460 IF RM<>2 THEN 7360
5470 GG=1:OB(4)=0:OB(2)=0:PRINT"THE GRUE DISAPPEARS INTO THE WALL.":GOSUB8090:GOTO3630
5480 IF S$<>OB$(7) THEN 7580
5490 IF RM<>13 THEN 7360
5500 IF MP=1 THEN PRINT"OK":OB(19)=0:CM=0:GOTO9
5510 IF OB(19)<>-1 THEN 7360
5511 PRINT"THE MERCHANT OFFERS YOU THE SHOVEL":MP=1:OB(19)=0:CM=0:GOTO9
5520 REM U
5530 IFF$<>"U"THEN5580
5540 IFRM=32THEN7140
5550 IFRM<>26THEN7460
5560 GOTO6850
5580 IFF$<>"USE"THEN5760
5590 FORI=1TONO:IFS$=OB$(I)THEN5610
5600 NEXTI:GOTO7310
5610 IF S$<>OB$(34) THEN 5660
5620 IF OB(34)<>-1 THEN 7360
5630 IF PA=1 THEN 7600
5640 IF RM<>7 THEN 7440
5650 PA=1:OB(26)=7:PRINT"OK":GOTO3630
5660 GOTO5710
5670 REM
5680 REM
5690 PRINT"YOU SLAY THE DRAGON WITH MIGHTY FORCE, AND HE SHRIVELS UP INTO NOTHING."
5700 GOSUB8090: DD=1: OB(46)=0:GOTO3630
5710 IF S$<>OB$(43) THEN 5740
5720 IF RM=13 AND MS=0 THEN 4890
5730 IF RM=30 AND DD=0 THEN 5690
5740 PRINT"IT DOESN'T WORK":GOTO9
5750 GOTO7580
5760 IF F$<>"UNLOCK" THEN 5840
5770 FOR I=1 TO NO:IF S$=OB$(I) THEN 5790
5780 NEXT I:GOTO7310
5790 IF S$<>OB$(21) THEN 5880
5800 IF OB(15)<>-1 THEN 7360
5810 IF RM<>11 THEN 7360
5820 IF BU=1 THEN PRINT"IT'S ALREADY UNLOCKED!":GOTO9
5830 BU=1:PRINT"OK":GOTO9
5840 IF F$<>"UNWEAR" THEN 7310
5850 IF S$<>OB$(31) THEN PRINT"IT WOULD BE HARD EVEN PUTTING THAT ON":GOTO9
5860 IF OB(31)<>-1 AND WG<>0 THEN PRINT"I'M NOT WEARING THEM.":GOTO9
5870 PRINT"OK":WG=0:GOTO9
5880 GOTO7580
5890 REM V 
5900 REM W 
5910 IF F$<>"W" THEN 6060
5920 IF RM=3 THEN 6270
5930 IF RM=4 THEN 6300
5940 IF RM=8 THEN 6420
5950 IF RM=10 THEN 6390
5960 IF RM=26 THEN 6970
5970 IF RM=23 THEN 6910
5980 IF RM=21 THEN 6820
5990 IF RM=12 THEN 6330
6000 IF RM=13 THEN 6570
6010 IF RM=17 THEN 6700
6020 IF RM=22 THEN 6510
6030 IF RM=19 THEN 6760
6040 IF RM=31 THEN 7060
6050 GOTO7460
6060 IF F$<>"WEAR" THEN 6130
6070 FOR I=1 TO NO:IF S$=OB$(I) THEN 6090
6080 NEXT I:GOTO7310
6090 IF S$<>OB$(31) THEN 6120
6100 IF OB(31)<>-1 THEN 7360
6110 WG=1:PRINT"OK":GOTO9
6120 GOTO7600
6130 IF F$<>"WITH" THEN 7310
6140 GOTO5590
6150 REM Y 
6160 REM Z 
6170 GOTO7310
6180 RM=1:GOTO3630
6210 RM=2:GOTO3630
6240 RM=3:GOTO3630
6270 RM=4:GOTO3630
6300 RM=5:GOTO3630
6330 RM=6:GOTO3630
6360 RM=7:GOTO3630
6390 RM=8:GOTO3630
6420 RM=9:GOTO3630
6450 RM=10:GOTO3630
6480 RM=11:GOTO3630
6510 RM=12:GOTO3630
6540 RM=13:GOTO3630
6570 RM=14:GOTO3630
6600 RM=15:GOTO3630
6630 RM=16:GOTO3630
6660 RM=17:OB(47)=17:GOTO3630
6700 RM=18:GOTO3630
6730 RM=19:GOTO3630
6760 RM=20:GOTO3630
6790 RM=21:GOTO3630
6820 RM=22:GOTO3630
6850 RM=23:GOTO3630
6880 RM=24:GOTO3630
6910 RM=25:GOTO3630
6940 RM=26:GOTO3630
6970 RM=27:GOTO3630
7000 RM=28:GOTO3630
7030 RM=29:GOTO3630
7060 RM=30:IFDD=1THEN3630
7090 CLS:PRINT RM$(RM)+VI$:PRINT:FOR I=1 TO NO:IF OB(I)=RM THEN PRINT OB$(I):GOTO7100
7091 NEXT I
7100 PRINT:PRINT"OBVIOUS EXITS:";DI$(RM):GOSUB9000
7110 IFOB(44)<>-1THEN PRINT"I HAVE BEEN BURNT BY A FIRE BREATHING DRAGON!":GOSUB1:GOTO7620
7120 IFOB(44)=-1THEN PRINT"THE DRAGON SENDS A BREATH OF FIRE AT ME, BUT IT REFLECTS OFF MY SHIELD.":FB=0:GOTO9
7130 GOTO540
7140 RM=31:OB(38)=RM:GOTO3630
7190 FA$="FANTASTIC!!!! YOU SOLVED IT!":PRINT@258,"";:FORI=1TOLEN(FA$):PRINT MID$(FA$,I,1);:NEXT
7240 GOTO7630
7250 SR=RM:RM=29:GOTO3630
7290 RM=32:OB(51)=RM:GOTO3630
7310 REM ERRORS
7320 PRINT"I DON'T UNDERSTAND!":GOTO9
7340 PRINT"I'M NOT CARRYING ANYTHING!":GOTO9
7360 PRINT"I CAN'T DO THAT...YET!":GOTO9
7380 PRINT"I CAN'T SEE IT HERE.":GOTO9
7400 PRINT"IT'S BEYOND MY POWER TO DO THAT.":GOTO9
7420 PRINT"I SEE NOTHING SPECIAL.":GOTO9
7440 PRINT"NOTHING HAPPENS.":GOTO9
7460 PRINT"I CAN'T GO THAT WAY!":GOTO9
7480 PRINT"I ALREADY HAVE THAT.":GOTO9
7500 PRINT"I FOUND NOTHING!":GOTO9
7520 PRINT"I FOUND SOMETHING!"
7530 GOSUB8090:GOTO3630
7540 PRINT"HUH?":GOTO9
7560 PRINT"WHAT?":GOTO9
7580 PRINT"I CAN'T DO THAT.":GOTO9
7600 PRINT"SORRY.":GOTO9
7620 PRINT"I'M DEAD!"
7630 PA$="PLAY AGAIN (Y/N)?"
7650 FORI=1TOLEN(PA$):PRINT MID$(PA$,I,1);:FORK=1TO40:NEXT:NEXT
7690 P$=INKEY$
7700 IF P$="Y" THEN RUN
7710 IF P$="N" THEN PRINT:PRINT:PRINT"GOODBYE...":END
7720 GOTO7690
7780 DATA 2,2,6,0,2,7,13,7,13,0,15,15,0,27,0,0,0,21,4,18,11,23,0,1,23,0,19,0,14,20,9,5,12,10,7,28,5,31,16,15,0,0,25,24,0
7781 DATA 30,17,0,1,0,0,0
7790 DATA"LADDER","GRUE","TREE","APPLE","TUNNEL","SHACK","MERCHANT","AXE","SHOVEL","AMULET","GHOST","GRAVE","BONES"
7791 DATA"ANKH","KEY","LEVER","PASSAGE","BOOK","COIN","STUMP","BOX","SKELETON","CRAWLWAY","SIGN","STAIRS","PLANK"
7792 DATA"SAW","HOLE","RIVER","CLOSET"
7800 DATA"GLASSES","DOOR","POST","HAMMER","FENCE","STONE","PADLOCK","CHEST","CHASM","RIVER","PLANK","DRAWBRIDGE"
7801 DATA"SWORD","SHIELD","VAULT","DRAGON","CHASM","SPLINTERS","PIT","SPLINTERS","TREASURE","HOLE"
7810 REM HEADING 
7820 CLS:PRINT:PRINT
7825 PRINT"    THE TREASURE OF ELGON":PRINT:PRINT
7830 PRINT" èâÉçÅÜäèääÉÉäçäãÇÉäèääÉÜäÉÉ"
7840 PRINT" èÑåÖÖääçàäåèäääèäèäèääåâäåè"
7850 PRINT" èÖèÖÖäèÑÖäèèäèÇèäèäèääãçäèè"
7860 PRINT" èáèáÉáèãèãÉÉãèãèãèèÉáãèããÉÉ"
7890 PRINT"     BY: KEITH HAWTHORN."
7900 PRINT"     -------------------"
7905 PRINT"MC-10 EDITS BY JIM GERRIE"
7910 PRINT@480,"PRESS ANY KEY TO START";
7920 Z$=INKEY$:IF Z$="" THEN 7920
7930 CLS
7940 PRINT"    THE TREASURE OF ELGON."
7950 PRINT"    ----------------------"
7970 PRINT"IN THIS ADVENTURE YOU MUST"
7971 PRINT"FIND THE TREASURE OF ELGON,"
7972 PRINT"BUT ALONG YOUR WAY YOU WILL"
7973 PRINT"ENCOUNTER MANY PROBLEMS."
7980 PRINT"THIS ADVENTURE WILL ACCEPT"
7981 PRINT"TWO WORD COMMANDS, IE: EXAM"
7982 PRINT"GRUE, CLIMB PIT ETC. YOU"
7990 PRINT"CAN ALSO SAVE GAME, AND"
7991 PRINT"RESTORE GAME.":PRINT
8000 PRINT"   GOOD LUCK!"
8050 PRINT"RESTORE A SAVED GAME?"
8060 Z$=INKEY$:IF Z$="Y" OR Z$="Y" THEN 4620
8070 IF Z$="N" OR Z$="N" THEN RETURN
8080 GOTO8060
8090 REM * WAIT FOR KEY *
8100 PRINT:PRINT"PRESS A KEY TO CONTINUE."
8110 Z$=INKEY$:IF Z$="" THEN 8110
8111 RETURN
9000 PRINT"--------------------------------";:RETURN
