0 GOSUB2600:CLEAR999:OL=19:NL=27:NS=56:DIMDS(NS,5),NT$(2),NV(2),NV$(NL,2),O(OL),O$(OL):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
20 Y1$="YOU ARE IN "
30 FORZZ=1TO4000:NEXT:CLS:SOUND1,1
45 PRINT"YEAR: 1520 A.D.":PRINT
50 PRINT"ANAHUAC VALLEY. IN THE DISTANCE":PRINT"ARE HIGH MOUNTAINS COVERED IN"
60 PRINT"SNOW WITH STREAMS DESCENDING":PRINT"DOWN THEIR SIDES TO FORM FIVE"
70 PRINT"LAKES EXTENDING 80 KM IN LENGTH.I WILL BE YOUR HANDS, YOUR EYES"
75 PRINT"AND YOUR LEGS.":PRINT
80 INPUT"PRESS enter";KK$:CLS
85 PRINT"ABBREVIATIONS:"
90 PRINT"N,E,S,W=GO NORTH, EAST, SOUTH,":PRINT"AND WEST"
95 PRINT"I=INVENTORY":PRINT"L=REPEAT LOCATION DESCRIPTION"
100 PRINT"T=TAKE ITEM":PRINT"Q=QUIT":PRINT
105 PRINT"PLEASE WAIT...":PRINT
115 REM INIT
125 LN=4:P$="0000111111101101110":S=1:MX=3
135 FORX=1TONS:FORY=1TO4:READDS(X,Y):K=K+DS(X,Y)
140 NEXTY:DS(X,5)=0:NEXTX
145 IFK<>4561THENPRINT"DATA ERROR IN DIRECTIONS.":END
150 FORX=1TONL:FORY=1TO2:READNV$(X,Y):NEXTY,X
155 FORX=1TOOL:READO$(X):NEXTX
160 K=0:FORX=1TOOL:READO(X):K=K+O(X):NEXTX
165 IFK<>269THENPRINT"DATA ERROR IN ROOM OBJECTS.":END
170 INPUT"PRESS enter TO BEGIN";LW$:CLS
180 REM MANAGE INPUT
190 GOSUB340:GOSUB1455
195 PRINT:INPUT"COMMAND";C$:PRINT
200 N=N+1
205 IFC$="QUETZALCOATL"ORC$="HUITZILOPOCHTLI"THEN1490
210 IFC$="MIXCOATL"ORC$="XIPE"ORC$="COATLICUE"THEN1490
215 IFLEFT$(C$,9)="MONTEZUMA"THEN1530
220 IFLEN(C$)>1THEN265
225 IFC$="N"THENNV(1)=1:NV(2)=1:GOTO285
230 IFC$="E"THENNV(1)=1:NV(2)=2:GOTO285
235 IFC$="S"THENNV(1)=1:NV(2)=3:GOTO285
240 IFC$="W"THENNV(1)=1:NV(2)=4:GOTO285
245 IFC$="L"THEN190
250 IFC$="I"THEN1265
255 IFC$="Q"THEN2540
260 IFC$="T"THENNV(1)=5:NV(2)=0:GOTO915
265 GOSUB1640
275 REM TESTS FOR DIFFICULT SITUATIONS
285 IFNV(1)=0THENM$="I DON'T UNDERSTAND "+NT$(1):GOSUB1:GOTO195
290 IF(F(1)=1ORF(1)=2)ANDNV(1)=1THENGOSUB1460:GOTO195
295 IFF(1)>2THEN1595
300 IFF(3)=2ANDS=29THEN1605
305 IFN>45ANDF(4)<>10THENPRINT"YOU ARE HUNGRY.":GOSUB1725
306 IFN>45ANDF(4)<>10THENF(4)=F(4)+1
310 IFF(4)=9THEN1600
315 IFS=15ANDNV(1)=1ANDF(5)=0ANDNV(2)<>3THEN1605
320 GOTO790
330 REM PRINT DESCRIPTION
340 DS(S,5)=1
345 IF1+INT(S/13)=1THENGOSUB375
350 IF1+INT(S/13)=2THENGOSUB440
355 IF1+INT(S/13)=3THENGOSUB510
360 IF1+INT(S/13)=4THENGOSUB580
365 IF1+INT(S/13)=5THENGOSUB650
370 K=1:GOTO695
375 ONSGOSUB1840,1855,1860,1865,1870,1885,1900,1915,1930,1955,1970,1980
435 RETURN
440 ONS-12GOTO1995,2005,2020,2035,2040,2045,2050,2060,2065,2070,2075,2080,2085
505 RETURN
510 ONS-25GOSUB2115,2120,2140,2150,2160,2170,2180,2190,2200,2210,2220,2230,2255
575 RETURN
580 ONS-38GOSUB2265,2275,2285,2295,2310,2335,2335,2335,2365,2335,2335,2335,2335
645 RETURN
650 ONS-51GOSUB2335,1865,1865,1865,1865
675 RETURN
685 REM PRINT ITEMS AND EXITS
695 FORZ=1TOOL:IFK=1THENIFO(Z)=STHENPRINT"INTERESTING THINGS:":K=0
705 GOTO735
710 TP$=O$(Z):IFRIGHT$(TP$,1)<>"/"THENRETURN
715 FORW=LEN(TP$)-1TO1STEP-1
720 IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1):RETURN
725 NEXTW
730 RETURN
735 IFO(Z)<>STHEN745
740 GOSUB710:PRINTTP$
745 NEXTZ:PRINT
750 REM PRINT EXITS
755 K=1:FORZ=1TO4
760 IFK=1THENIFDS(S,Z)<>0THENPRINT"VISIBLE EXITS:":K=0
765 IFDS(S,Z)<>0THENPRINTO$(Z);" ";
770 NEXTZ:PRINT:RETURN
780 REM MANAGE VERBS
790 X=NV(1):IFX<2THENX=2
795 X=INT(X/2)
800 ONXGOTO885,915,985,1020,1085,1115,1145,1200,1240,1265,1310,1355,1430
865 PRINT"I DON'T UNDERSTAND WHAT YOU WANTTO DO.":GOTO195
875 REM DIRECTIONS
885 IFNV(2)<1THENPRINT"ALSO GIVE ME A DIRECTION.":GOTO195
890 IFDS(S,NV(2))=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO195
895 S=DS(S,NV(2)):GOTO1585
905 REM TAKE
915 IFNV(2)=0THEN960
920 IFO(NV(2))=-1THEN1570
925 IFO(NV(2))<>STHEN1575
930 IFMID$(P$,NV(2),1)="0"THEN1590
935 IFIV+1>MXTHEN1580
940 O(NV(2))=-1:IV=IV+1
945 IFNV(2)=9ORNV(2)=10ORNV(2)=11THENF(3)=2
950 IFNV(2)=14ANDS=19THENPRINT"THIEF!":GOTO1605
955 GOTO1560
960 FORX=5TO19:IFO(X)=STHENNV(2)=X:GOTO930
965 NEXTX:GOTO1590
975 REM PUT/PLACE/DROP
985 IFO(NV(2))<>-1THEN1590
990 O(NV(2))=S:IV=IV-1
995 IFS=30AND(O(8)=SORO(18)=S)THENF(3)=1:DS(37,4)=38
1000 GOTO1560
1010 REM EAT
1020 IFNV(2)=16THEN1045
1025 IFNV(2)<>14THEN1590
1030 IFO(14)<>-1THENPRINT"YOU DON'T HAVE THAT.":GOTO1590
1035 PRINT"YOU HAVE EATEN. YOU CAN NOW"
1040 PRINT"CONTINUE YOUR SEARCH."
1041 F(4)=10:O(14)=0:IV=IV-1:GOTO195
1045 IFO(5)=-1THENO(5)=0:IV=IV-1:GOTO1060
1050 IFO(16)=-1THENO(16)=0:IV=IV-1:GOTO1060
1055 PRINT"YOU DON'T HAVE THAT.":GOTO195
1060 PRINT"YOU ARE VERY HUNGRY, AND YOU EATBEANS?"
1065 N=38:F(4)=0:GOTO195
1075 REM SWIM/DIVE
1085 IFS>21THEN1590
1090 M$="YOU DIVED BEAUTIFULLY. IT'S A PITY YOU CAN'T SWIM.":GOSUB1:GOTO1600
1105 REM JUMP/CLIMB
1115 IFS<>9ORS<>14ORS<>27THEN1590
1120 IFS=6ORS=8THEN1090
1125 M$="YOU WERE CLIMBING, BUT YOU FELL!":GOSUB1:GOTO1600
1135 REM KILL/MURDER
1145 IFO(6)<>-1THENPRINT"WITH WHAT?":GOTO195
1150 IFS=27ORS=42THENPRINT"YOU TRIED...":GOTO1605
1155 IFNV(2)<>12THENPRINT"WHO DO YOU WANT TO KILL?":GOTO195
1160 M$="YOU KILLED THE AZTEC WARRIOR. SOME PEOPLE CARRY AWAY THE BODY.":GOSUB1
1170 O(12)=0:F(1)=0
1175 IFF(6)=0THENF(6)=1:O(16)=S:PRINT"YOU HEAR A NOISE..."
1180 GOTO195
1190 REM THROW
1200 IFNV(2)<>7ORS<>25THEN985
1205 IFINT(RND(10))+1<6THEN985
1210 M$="FANTASTIC! YOU MADE A BASKET. CONGRATULATIONS!":GOSUB1
1220 DS(27,2)=28:GOSUB1725:O(7)=S:IV=IV-1:GOTO190
1230 REM DRINK
1240 IFO(13)<>-1THENPRINT"WHAT WOULD YOU LIKE TO DRINK?"
1241 IFO(13)<>-1THEN195
1245 PRINT"YOU FEEL CHEERFUL.":GOTO195
1255 REM INVENTORY
1265 PRINT"YOU HAVE WITH YOU:"
1270 NC=0:FORZ=1TOOL
1275 IFO(Z)=-1THENNC=NC+1:GOSUB710:PRINTTP$
1280 NEXTZ
1285 IFNC=0THENPRINT"NOTHING"
1290 GOTO195
1300 REM BUY
1310 IFO(5)<>-1ANDO(16)<>-1THENPRINT"WITH WHAT?":GOTO1610
1315 IFS=15ANDF(5)<>1THENGOSUB1615:IV=IV-1:F(5)=1:GOTO1330
1320 IFS<>19THENPRINT"WHAT DO YOU WANT TO DO?":GOTO195
1321 IFO(14)<>19THENPRINT"WHAT DO YOU WANT TO DO?":GOTO195
1325 IFNV(2)=14THENPRINT"YOU HAVE THE TORTILLAS.":O(14)=-1
1330 IFO(5)=-1THENO(5)=0:GOTO195
1335 IFO(16)=-1THENO(16)=0:GOTO195
1345 REM DIG
1355 IFS<>47THEN1590
1360 INPUT"WITH WHAT? YOUR BARE HANDS";C$
1365 IFLEFT$(C$,1)<>"Y"THENGOTO200
1370 M$="YOU HAVE OPENED A HOLE IN THE WALL, WHERE WATER FLOODS IN.":GOSUB1
1375 PRINT"IT WASHES YOU OUT.":S=9
1380 FORX=1TO2500:NEXTX
1385 IFF(2)=1THENF(2)=0:DS(8,2)=0:DS(9,1)=0:DS(12,4)=0:DS(6,3)=0
1390 FORX=1TOOL
1395 IFO(X)=-1THENO(X)=10:IV=IV-1
1400 NEXTX
1405 IFF(3)=2THENF(3)=0:O(9)=30:O(10)=30:O(11)=30
1410 GOTO190
1420 REM ESCAPE/FLEE
1430 PRINT"HOW? THERE ARE NO WINDOWS, THE":PRINT"WALLS ARE DAMP AND THE DOOR IS"
1436 PRINT"MADE OF SOLID STONE.":GOTO195
1445 REM WARRIOR
1455 IFO(12)<>STHENRETURN
1460 M$="AN AZTEC WARRIOR WITH BAD INTENTION BLOCKS YOUR WAY.":GOSUB1
1470 F(1)=F(1)+1:RETURN
1480 REM PASSWORD
1490 IFC$<>"QUETZALCOATL"THEN1505
1495 IFS=9THENF(2)=1:DS(6,3)=24:DS(8,2)=23
1500 IFS=9THENF(2)=1:DS(9,1)=15:DS(12,4)=15:GOTO1510
1505 PRINT"HONOUR TO HIM!":GOTO195
1510 PRINT"THE DRAWBRIDGE LOWERS.":GOSUB1725:GOTO190
1520 REM THE ONE WHO SPEAKS
1530 IFS<>14THENPRINT"HONOUR TO HIM!":GOTO195
1535 PRINT"OUR DEITIES ARE: MIXCOATL,"
1540 PRINT"HUITZILOPOCHTLI, COATLICUE,"
1541 PRINT"QUETZALCOATL AND XIPE.":GOTO195
1550 REM MESSAGES
1560 PRINT"DONE.":GOTO195
1570 PRINT"YOU ALREADY HAVE IT.":GOTO195
1575 PRINT"WHERE DO YOU SEE IT?":GOTO195
1580 PRINT"YOUR HANDS ARE FULL.":GOTO195
1585 PRINT"DONE.":GOTO190
1590 PRINT"DON'T BE SILLY.":GOTO195
1595 M$="YOU ARE DEAD, KILLED BY AN AZTEC WARRIOR.":GOSUB1:GOTO2540
1600 PRINT"YOU ARE DEAD.":GOTO2540
1605 PRINT"TWO SOLDIERS ESCORT YOU TO      PRISON."
1610 GOSUB1725:S=47:O(6)=10:IV=IV-1:GOTO190
1615 M$="IT WILL COST YOU A BAG OF COCOA. OK, PAID.":GOSUB1
1620 RETURN
1630 REM INPUT
1640 K=1:NT$(1)="":NT$(2)=""
1645 FORX=1TOLEN(C$):K$=MID$(C$,X,1)
1650 IFK$=" "THENK=2:GOSUB1705:IFNT$(2)<>""THEN1665
1655 IFK$<>" "THENNT$(K)=NT$(K)+K$
1660 NEXTX
1665 FORX=1TO2:NV(X)=0:IFNT$(X)=""THEN1695
1670 FORY=1TONL:K$=NV$(Y,X)
1675 IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
1680 IFLEFT$(NT$(X),4)=K$THENNV(X)=Y:GOTO1690
1685 NEXTY:GOTO1695
1690 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO1690
1695 NEXTX:IF(NV(1)=5ORNV(1)=6)ANDNV(2)<1THEN1590
1700 RETURN
1705 FORY=1TONL:IFLEFT$(NT$(2),4)=NV$(Y,2)THEN1720
1710 NEXTY
1715 NT$(2)=""
1720 RETURN
1725 FORX=1TO2500:NEXTX:RETURN
1735 REM DIRECTIONS
1745 DATA2,3,4,,2,2,4,2,1,,5,4,4,4,53,1,3,,6,7,5,,,0
1750 DATA5,8,9,,,,,7
1755 DATA0,,10,7,11,,11,9,12,10,10,12,,13,11,,14,,12,0
1760 DATA1,,13,,,16,9,22,19,18,17,15,16,,,,,,,16
1765 DATA20,21,16,22
1770 DATA26,21,19,15,,,,19,23,19,15,15
1775 DATA24,25,22,8,6,26,25,23,24,,27,23,24,21,20,27,26,0
1780 DATA15,25,,29,,27,31,32,30,28,29,35,,0
1785 DATA37,36,29,,33,,34,29,,,32,36,32,,,35,,34,0
1790 DATA30,,33,,31,,,31,0
1795 DATA37,41,39,,38,40,,,,,,39,44,,,38,41,43
1800 DATA0,,,,,42
1805 DATA49,46,41,45,48,50,44,49,49,51,52,44,,,,,49
1810 DATA50,50,45,48,46,44,45
1815 DATA48,52,45,48,52,42,46,52,51,50,51,46,4,55,56,54
1820 DATA53,56,55,56,3,56,53,54,53,55,54,54
1830 REM DESCRIPTIONS
1840 PRINT"YOU ARE AT CUAUTITLAN, IN FRONT"
1845 M$="OF THE SALT LAKE ZUMPANGO. THERE ARE SWAMPS TO THE NORTH. WRITING ON THE GROUND SAYS, 'WHAT'."
1846 GOSUB1:RETURN
1855 PRINT"YOU ARE LOST IN THE SWAMPS WITH NO ESCAPE.":RETURN
1860 M$=Y1$+"ECATEPEC, IN FRONT OF THE TEXCOCO SALT LAKE.":GOSUB1:RETURN
1865 PRINT"YOU ARE LOST IN THE MIDDLE OF":PRINT"THE MOUNTAINS.":RETURN
1870 PRINTY1$"TEPEYACAC. IN THE":PRINT"DISTANCE, YOU CAN SEE THE"
1876 PRINT"TEMPLE. WRITING ON THE GROUND":PRINT"SAYS, 'WORD'."
1880 RETURN
1885 PRINT"YOU ARE ON A CAUSEWAY OVER LAKE TEXCOCO."
1890 IFF(2)=0THENPRINT"SOME PARTS HAVE BEEN REMOVED."
1895 RETURN
1900 PRINTY1$"TLACOPAN. TO THE":PRINT"NORTH YOU CAN SEE OTHER"
1906 PRINT"VILLAGES. WRITING ON THE GROUND":PRINT"SAYS, 'IS CREATOR'."
1910 RETURN
1915 PRINT"YOU ARE ON AN ELEVATED ROAD."
1920 IFF(2)=0THENPRINT"SOME THINGS HAVE BEEN REMOVED."
1925 RETURN
1930 PRINT"IN COYOACAN, FORTIFIED CITY."
1935 IFF(2)=0THENPRINT"THERE IS A RAISED DRAWBRIDGE."
1940 IFF(2)=0THENPRINT"> PASSWORD <"
1945 PRINT"WRITING ON THE GROUND SAYS,     'AND'."
1950 RETURN
1955 M$=Y1$+"MIXQUIC, IN FRONT OF CHALCO FRESHWATER LAKE. WRITING ON THE GROUND SAYS, 'GOD NAME'."
1965 GOSUB1:RETURN
1970 PRINTY1$"TLATENGO. WRITTEN ON"
1975 PRINT"THE GROUND, IT SAYS, 'WILL      OPEN'.":RETURN
1980 M$=Y1$+"MEXICALCONGO, NEAR THE TOLUHACAN TEMPLE.":GOSUB1
1985 IFF(2)=0THENPRINT"THERE IS A BROKEN BRIDGE TO THE"
1990 IFF(2)=0THENPRINT"WEST.":RETURN
1995 M$=Y1$+"THE BIG CITY OF TEXCOCO.":GOSUB1
2000 RETURN
2005 M$=Y1$+"FRONT OF THE TEMPLE OF THE SUN, IN TEOTIHUACAN.":GOSUB1
2010 M$="A SORCERER ASKS YOU FOR THE NAME OF THE EMPEROR.":GOSUB1
2015 RETURN
2020 PRINT"YOU ARE AT TENOCHTITLAN, IN"
2021 PRINT"FRONT OF THE TAX COLLECTOR."
2025 PRINT"THE CITY IS LIKE VENICE WITH"
2026 PRINT"CANALS AND WALKWAYS."
2030 RETURN
2035 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2040 PRINT"YOU ARE AT TENOCHTITLAN, IN"
2041 PRINT"FRONT OF THE LAKE.":RETURN
2045 PRINT"YOU ARE AT TENOCHTITLAN, IN"
2046 PRINT"FRONT OF THE LAKE.":RETURN
2050 PRINT"YOU ARE AT TENOCHTITLAN."
2055 PRINT"A MERCHANT OF TORTILLAS.":RETURN
2060 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2065 PRINT"YOU ARE AT TENOCHTITLAN, IN"
2066 PRINT"FRONT OF THE LAKE.":RETURN
2070 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2075 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2080 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2085 PRINTY1$"THE LARGE SQUARE. IN"
2090 PRINT"FRONT OF THE GRAND TEOCALLI IS"
2091 PRINT"THE TEMPLE OF QUETZALCOATL,"
2095 PRINT"WHILE IN FRONT OF YOU IS THE"
2096 PRINT"SACRED COURTYARD OF GAMES WITH"
2100 PRINT"TWO STONE CIRCLES HANGING"
2105 PRINT"PERPENDICULAR TO THE GROUND ON  TWO STONE WALLS."
2110 RETURN
2115 PRINT"YOU ARE AT TENOCHTITLAN.":RETURN
2120 PRINTY1$"FRONT OF MONTEZUMA"
2125 PRINT"PALACE WITH 2 WARRIORS BARRING  YOUR ENTRANCE."
2130 PRINT"THE PALACE IS IMMENSE. THE"
2131 PRINT"SQUARE IS TWO STOREYS HIGH."
2135 RETURN
2140 PRINTY1$"THE ENTRANCE OF THE  PALACE.":RETURN
2150 PRINT"YOU ARE ON THE WEST SIDE OF A   CORRIDOR.":RETURN
2160 PRINTY1$"THE TAX CHAMBER.":RETURN
2170 M$=Y1$+"A ROOM OF THE MONTEZUMA PALACE.":GOSUB1:RETURN
2180 M$="YOU ARE ON THE EAST SIDE OF A CORRIDOR.":GOSUB1:RETURN
2190 PRINTY1$"THE COURT.":RETURN
2200 M$=Y1$+"THE STAIRS OF THE ADMINISTRATORS.":GOSUB1:RETURN
2210 PRINTY1$"THE TAX WAREHOUSE.":RETURN
2220 PRINTY1$"THE GUEST BEDROOM.":RETURN
2230 M$=Y1$+"A STRANGE ROOM IN MONTEZUMA'S PALACE.":GOSUB1
2235 IFF(3)=0THENM$="ON THE LEFT THERE IS AN EMPTY TANK.":GOSUB1
2240 IFF(3)=1THENM$="THERE IS A TANK FULL OF WATER THERE. IT HAS OPENED A WEST DOOR.":GOSUB1
2250 RETURN
2255 M$="YOU ARE ON THE WEST SIDE OF A CORRIDOR.":GOSUB1:RETURN
2265 M$=Y1$+"THE HALLS OF MONTEZUMA'S 150 CONCUBINES.":GOSUB1:RETURN
2275 M$=Y1$+"THE ROOMS OF MONTEZUMA'S WIFE.":GOSUB1:RETURN
2285 M$="YOU ARE ON THE EAST SIDE OF A CORRIDOR.":GOSUB1:RETURN
2295 M$=Y1$+"THE HALL OUTSIDE MONTEZUMA'S ROOM. THERE IS A WARRIOR ON WATCH.":GOSUB1
2305 RETURN
2310 PRINTY1$"MONTEZUMA'S ROOM.":PRINT"THE GREAT EMPEROR WELCOMES YOU"
2320 PRINT"WITH HONOUR, GIVING YOU HIS":PRINT"BEAUTIFUL AND PRECIOUS HEADWEAR FOR FREE."
2330 PRINT"CONGRATULATIONS!":GOTO2540
2335 PRINTY1$"A ROOM OF THE PALACE OF MONTEZUMA.":RETURN
2365 M$=Y1$+"A PRISON. PREPARE FOR THE SACRIFICE.":GOSUB1:RETURN
2375 REM VERBS AND NOUNS
2385 DATAGO,NORT,MOVE,EAST,*RUN,SOUT,*WALK,WEST,GET,SACK,DROP,SWOR,*PUT,BALL,EAT,TRIN
2400 DATA*TAST,STON,SWIM,JEWE,*DIVE,BALE,CLIM,WARR,*JUMP,BOTT,KILL,TORT
2415 DATA*MURD,TAX,SHOO,BAG,*THRO,MOON,DRIN,BLOW,*SWIG,DISC,INVE,"",MONT,"",BUY,"",*PAY,""
2430 DATADIG,"","","",ESCA,"",*FLEE,""
2440 REM OBJECTS
2450 DATA"NORTH","EAST","SOUTH","WEST","A SACK OF COCOA BEANS/SACK/"
2460 DATA"AN OBSIDIAN SWORD/SWORD/","A RUBBER BALL/BALL/","A GOLDEN TRINKET/TRINKET/"
2475 DATA"SOME PRECIOUS STONES/STONES/","SOME GOLD JEWELRY/JEWELRY/","SOME COTTON BALES/BALES/"
2490 DATA"AN AZTEC WARRIOR/WARRIOR/","A BOTTLE OF OCTLI/BOTTLE/","SOME TORTILLAS/TORTILLAS/"
2505 DATA"TAX","A BAG OF COCOA BEANS/BAG/","AN ALL SILVER MOON/MOON/","A GOLD BLOWGUN/BLOWGUN/"
2520 DATA"A HUGE CARVED STONE DISC/DISC/"
2525 REM OBJECT ROOMS
2530 DATA0,,,,10,6,16,5,30,30,30,7,22,19,,,36,24,34
2535 REM END OF GAME ROUTINE
2540 K=0:FORX=1TONS:K=K+DS(X,5):NEXTX:K=INT(K*1000/NS)/10
2545 PRINT:PRINT"YOU HAVE SEEN"STR$(K)"% OF THE":PRINT"ADVENTURE."
2550 PRINT:INPUT"ANOTHER GAME (y/n)";K$:IFLEFT$(K$,1)="Y"THENRUN
2555 END
2600 CLS0:POKE49151,64
2603 PRINT"€€€€€€€€€‘“—œŸŸŸœŸŸž“€€€€€€€€€€€€€€““žŸ“œ“ŸŸš•›–™—ž™“€€€€€€";
2605 PRINT"€€€€‘ž™–‘Ÿœ“œŸ“—ž˜‘’”’™’€€€€€€€€–”œ˜”Ÿ”Ÿ˜‘“’˜ž”˜”˜™€€€€";
2607 PRINT"€€€‘šŸœŸŸŸ“–˜•Ÿš”™’‘—ŸœŸ•’€€€€€€•œ“œŸŸœœ˜‘—Ÿ›’”œœŸœ“œš€€€";
2609 PRINT"€€€•ŸŸŸŸž‘“““““““““’ŸŸŸ€š€€€€€€•œœŸŸšš€€‘“““’€€••Ÿœœ€š€€€";
2611 PRINT"€€€”š““ŸŸ›”œœ•ŸŸŸšœœ˜—ŸŸŸ˜•˜€€€€€€€’”œœœŸŸŸŸ”Ÿž˜ŸŸŸŸœœ˜‘ž€€€€";
2613 PRINT"€€€€€œœŸ““ŸŸ“—œœ€€€€€€€€€€€€€€€œœœœœœœœœœœœœ€€€€€€€€€";
2615 PRINT"€€€€€€€€€€€€€ßßßßß€€€€€€€€€€€€€€€€€€ßßß€€€ßßßßß€€€ÕßßÚ€€€€€€";
2617 PRINT"€€€€€€€ÜßÜ€€€€AZTEC€€€ÔÝÞØ€€€€€€€€€€€€Ü€Ü€€ADVENTURE€ÔØÔØ";:RETURN
3000 REM ROBERTO B. 1988.
