
0 'COPYRIGHT (C) T&D SOFTWARE 1984
1 POKE359,57:CLS:PRINT@39,"zector adventure":PRINT:PRINTTAB(7)"YOUR CODE IS D$8.":PRINT"  YOU HAVE JUST BEEN PULLED IN  ON THE ENEMY SHIP BY A TRACTOR  BEAM. YOU MUST FIND THE SHIPS   BATTLE PLANS AND SOMEHOW RETURN THEM TO THE REBEL FLEET.":PMODE0,1
2 PRINT"   ALL THEY TELL YOU IS THAT YOUWILL HAVE TO USE THE CODE AT    SOME POINT. YOU ARE ALSO        EQUIPPED WITH A BLASTER. YOU    HAVE 24 HOURS. GOOD LUCK!";:PRINT:PRINT"     PRESS <ENTER> TO BEGIN";
3 PCLS:SCREEN0,1:FORZ3=1TO2:PLAY"T10L5O3CP16CP16O2GP16GP16CP16CP16CP16CP16CP16CP16DP16DP16D#P16D#P16D#P16D#P16O1A#P16A#P16A#P16A#P16O2CCP1T8L2CP12D#P12GP12A#P12O3CP1":NEXT Z3
4 X5$="BR4NR4U1E4U1L4BM+7,+6NR4U3NR2U3R4BM+3,+6BM+1,-0H1U4E1R2F1BM+0,+4G1L2BM+6,0BM+2,+0U6NL2R2BM+3,+6BM+1,+0H1U4E1R2F1D4G1L2BM+6,+0U6R3F1D1G1L2NL1F3"
5 FOR Z=20 TO 30 STEP2:POKE 200,Z+20:POKE202,Z/2+20:DRAW"S16"+X5$:NEXT
6 DIM L$(24),N$(24),S$(24),E$(24),W$(24),O$(35),P(35),L1(35),I(35),D$(35):I(4)=1:N5=1:I8=130:I9=40
7 R$="BU2R3D5L5U5R2":POKE200,I8:POKE202,I9:DRAW"S6"+R$
8 L=1:FORZ=1TO24:READL$(Z),N$(Z),S$(Z),E$(Z),W$(Z):NEXT
9 FORZ=1TO29:READ O$(Z),L1(Z),P(Z),D$(Z):NEXT:O$(35)="SUIT FOR SPACE WALKS":P(35)=1:L1(35)=16:D$(35)="LOOKS LIKE IT IS IN USEABLE     CONDITION"
10 LINEINPUTZ1$:TIMER=0
11 PLAY"T155L155O2":CLS:PRINT "LOCATION: ";L$(L)
12 POKE200,I8:POKE202,I9:DRAWR$:PRINT"YOU SEE: "
13 C=0:Z1=1:FORZ=1TO35
14 IF L1(Z)=L AND I(Z)=0THEN PRINT @Z1*32+9,O$(Z):Z1=Z1+1:C=1
15 NEXT:IFC=0THENPRINT@Z1*32+9,"NOTHING OF INTEREST"
16 PRINT"EXITS: ";
17 C=0:IF N$(L)<>"0" THEN PRINT"NORTH ";:C=C+1:LINE(I8,I9)-(I8,I9-8),PSET
18 IF S$(L)<>"0" THEN PRINT"SOUTH ";:C=C+1:LINE(I8,I9)-(I8,I9+8),PSET
19 IF E$(L)<>"0" THEN PRINT"EAST ";:C=C+1:LINE(I8,I9)-(I8+8,I9),PSET
20 IF W$(L)<>"0" THEN PRINT"WEST";:C=C+1:LINE(I8,I9)-(I8-8,I9),PSET
21 IFC=0THENPRINT" ???";
22 PRINT:PRINTSTRING$(32,"=");
23 IF M5>1440 THEN PRINT:PRINT"YOU RAN OUT OF TIME!":GOTO154
24 IFL=6ANDI(35)=1 AND G=1 AND I(21)=1 THEN PRINT"YOU FLOAT SAFELY UNTIL A REBEL  SHIP PICKS YOU UP.":PRINT:PRINT" YOU HAVE SOLVED THIS ADVENTURE!":PRINT"********************************":FORZ=1TO2100:GOTO159
25 IFL=6ANDI(35)=1 AND G=1 AND I(21)=0 THEN PRINT"YOU MADE IT OUT BUT YOU FORGOT  THE PLANS STUPID!":GOTO154
26 IFL=6 ANDG=1 THEN PRINT"THE AIR LOCK WAS OPEN":PRINT"YOU WERE SUFFICATED":GOTO154
27 M5=M5+1:PRINT"NOW WHAT ? ";:LINEINPUTC$
28 IF C$="N" THEN C$="GO NORTH"
29 IF C$="S" THEN C$="GO SOUTH"
30 IF C$="E" THEN C$="GO EAST"
31 IF C$="W" THEN C$="GO WEST"
32 IF C$="GO NORTH" AND N$(L)<>"0" THEN L=VAL(N$(L)):I9=I9-16:GOTO11
33 IF C$="GO SOUTH" AND S$(L)<>"0" THEN L=VAL(S$(L)):I9=I9+16:GOTO11
34 IF C$="GO EAST" AND E$(L)<>"0" THEN L=VAL(E$(L)):I8=I8+16:GOTO11
35 IF C$="GO WEST" AND W$(L)<>"0" THEN L=VAL(W$(L)):I8=I8-16:GOTO11
36 IF LEN(C$)<3 THEN PRINT"? WHAT ?":GOTO27
37 IF C$="LOO" OR C$="LOOK" THEN 11
38 N$=LEFT$(C$,3):FORA=(LEN(C$)-2) TO1STEP-1:IFMID$(C$,A,1)=" "THEN39 ELSE NEXTA
39 O$=MID$(C$,A+1,3)
40 IF N$="INV" THEN107
41 IF N$="TIM" OR N$="SCO" THENPRINT"TIME= :";STR$(M5);" MIN.":GOTO27
42 IFN$="GET" ORN$="TAK" ORN$="STE" GOTO111
43 IFN$="DRO" ORN$="LEA" ORN$="PUT" THEN117
44 IFN$="PUS"OR N$="PRE"OR N$="MOV"THEN GOSUB126
45 IFN$="KIL"OR N$="SHO"OR N$="BLA" THEN 129
46 IFN$="FIR"OR N$="LAU" THEN156
47 IF N$="MAP" AND INKEY$="" THEN SCREEN1,1:PRESET(I8,I9):PSET(I8,I9):GOTO47 ELSE SCREEN0,1
48 IF N$<>"MAP" THEN 49 ELSE PRINT" O.K.":GOTO27
49 IFN$="TOS"OR N$="THR" THEN GOSUB132
50 IFN$="TYP"OR N$="INP"OR N$="ENT"OR N$="PRI" THEN148
51 IFN$="GIV"THEN GOTO 138
52 IFN$="LOO"OR N$="EXE"OR N$="REA" THEN143
53 PRINT" YOU CAN'T DO THAT!":GOTO27
54 DATA "SHIP DOCKING AREA",0,0,6,4
55 DATA "TERMINAL CONTROL ROOM",0,0,0,3
56 DATA "PROCESSING AREA",0,4,0,0
57 DATA "ESCAPE POD ROOM",0,0,1,0
58 DATA "COMMAND CENTER",0,7,4,0
59 DATA "WASTE DISPOSAL ROOM",0,0,0,1
60 DATA "A N/W CORRIDOR",0,12,0,0
61 DATA "A SMALL ROOM",0,0,9,0
62 DATA "BEHIND 'ZEEVIUS'",0,14,0,8
63 DATA "A N/W CORRIDOR",0,15,0,0
64 DATA "TURRET ROOM g",0,0,12,0
65 DATA "INTERSECTION g",7,17,13,11
66 DATA "MESS HALL",0,0,14,12
67 DATA "BAR AND ARCADE",0,0,15,13
68 DATA "INTERSECTION d",10,20,16,14
69 DATA "TURRET ROOM d",0,0,0,15
70 DATA "DEBREIFING ROOM",12,0,18,0
71 DATA "ENGINE ROOM",0,21,19,17
72 DATA "ENGINE ROOM",0,0,20,18
73 DATA"BUNK ROOM",15,0,0,19
74 DATA "ARMORY",18,0,22,0
75 DATA "VEIWING WINDOW",0,24,0,21
76 DATA "LARGE TERMINAL ROOM",0,0,24,0
77 DATA "TRITINAITE STEEL DOOR",22,0,0,0
78 DATA "MAINFRAME",3,0,"IT HAS DISK DRIVE SLOTS"
79 DATA "TERMINAL",2,0,IT'S SCREEN READS--               COMMAND CENTER open OR closed
80 DATA "SHIP",1,0,IT IS OUT OF FUEL
81 DATA"BLASTER",1,1,Z
82 DATA"GARBAGE",6,1,IT SMELLS
83 DATA"LASER RIFLE",21,1,IT HAS AN ALARM SYSTEM ON IT
84 DATA"SLOT IN WALL",4,0,Z
85 DATA"RED BUTTON",1,0,IT SAYS- OPEN
86 DATA"BLUE BUTTON",1,0,IT SAYS- CLOSE
87 DATA"CHAIRS",17,0,Z
88 DATA"BEDS",20,0,Z
89 DATA"QUARTER",9,1,Z
90 DATA"CONTROLS",5,0,Z
91 DATA"MEMBERS OF CREW",5,0,THEY ARE WORKING
92 DATA"GUNNER",11,0,HE IS SLEEPING
93 DATA"TORPEDO LAUNCHER",11,0,IT IS LOADED
94 DATA"TABLES",13,0,Z
95 DATA"COOK",13,0,HE LOOKS AT YOU AND TELLS YOU TOGET SOMETHING HE CAN MAKE DINNEROUT OF
96 DATA"VIDEO GAMES",14,0,THEY ARE NOT BEING PLAYED
97 DATA"COMPUTER ID TERMINAL",8,0,IT'S SCREEN SAYS -INPUT CODE ---
98 DATA"PLANS FOR BATTLE",5,1,THIS IS IT!
99 DATA"MACHINERY",18,0,HAS A LABLE THAT SAYS MADE BY-  DARRELL ULM - ADVENTURE #1         3139 CALL RD. STOW OHIO            44224  216-678-1682
100 DATA"MACHINERY",19,0,Z
101 DATA"CREW MEMBER",20,0,HE HAS A PEICE OF METAL
102 DATA"GRENADE",21,1,Z
103 DATA"REBEL FLEET IN SPACE",22,0,Z
104 DATA"TRITINITE DOOR",24,0,IT HAS A CIRCLULAR INDENTATION
105 DATA"DISKS FLOPPY",23,1,Z
106 DATA"DOOR (WOODED+LOCKED)",3,0,"A SIGN ON IT SAYS 'RESTRICTED'  AND IT LOOKS LIKE YOU COULD BLOWIT UP!"
107 C=0:PRINT"YOU ARE CARRYING":PRINT:FORZ=1TO35
108 IF I(Z)=1 THEN PRINT"> "+O$(Z):C=1:PLAY"T255L255ABCDEFG"
109 NEXT:IFC=0 THEN PRINT"** NOTHING **"
110 GOTO27
111 FORZ=1TO35
112 K$=LEFT$(O$(Z),3)
113 IF O$=K$ AND L1(Z)=L AND P(Z)=1 THEN GOSUB123:I(Z)=1:N5=N5+1:PRINT"TAKEN":PLAY"GFCBADEDEDEDEDE":GOTO115 ELSE IF O$=K$ AND L1(Z)=L AND P(Z)=0 THEN PRINT"YOU CAN'T":GOTO27
114 NEXT:PRINT"I DON'T UNDERSTAND THE NOUN":GOTO27
115 IF O$="LAS"THEN PRINT" BUT AS SOON AS YOU PICK IT UP  AN ALARM GOES OFF AND YOU ARE   CAPTURED BY THE GUARDS!":PLAY"O2DDDDDDDCCCCCCCDDDDDDDCCCCCCCDDDDDDDCCCCCCCC":GOTO154
116 GOTO27
117 FORZ=1TO35
118 K$=LEFT$(O$(Z),3):IFO$=K$ AND I(Z)=1 THEN L1(Z)=L:I(Z)=0:PRINT " DROPPED":PLAY"O1EFGCBAO2":N5=N5-1:GOTO120 ELSE IF O$=K$ THEN PRINT"YOU DON'T HAVE THAT ITEM":GOTO27
119 NEXT:PRINT"I DON'T UNDERSTAND THE NOUN":GOTO27
120 IFO$="QUA" AND L=14 THEN O$(Z)="'ZEEVIUS' IS ON":PRINT"THE LIGHTS IN THE ARCADE COME ON!!!":PRINT:P(Z)=0:N$(14)="9":PRINT"BECAUSE THE GAME WAS TURNED ON  IT LIGHTED UP THE ROOM SHOWING  YOU A NITCH BEHIND THE GAME":PLAY"O3T45L5CCGGCCCCCCDDD#C":GOTO2
121 IFO$="CAR"AND L=4 THEN PRINT" THE CARD IS GIVEN BACK TO YOU  AND A DOOR ON THE NORTH WALL IS OPENED.":N$(4)="3":I(Z)=1:PLAY"O1AA#BCC#DD#EFF#GG#":GOTO27
122 GOTO27
123 IF N5+1>4THEN PRINT "YOU ARE TOO LOADED DOWN":GOTO27 ELSE RETURN
124 FORZ=1TO35:IFL1(Z)=6 AND I(Z)=0THEN L1(Z)=22:P(Z)=0:O$=O$(Z)+" IN SPACE":O$(Z)=O$
125 NEXT:RETURN
126 IF O$="BLU"AND L=1THEN PRINT"///CLANK\\\":PLAY"O1CCO5CCCC":S$(6)="10":N$(10)="6":G=0:GOTO27
127 IFO$="RED"AND L=1 THENG=1:PRINT"///WHOOSH\\\":PLAY"V30O1AAAV25AAAV20AAAV15AAAV10AAAV5AAAV1AAAV30":GOSUB124:S$(6)="0":N$(10)="0":GOTO27
128 PRINT"IT HAD NO EFFECT":GOTO 27
129 IFL=20 AND O$="MEM" AND I(4)=1 THEN O$(24)="DEAD CREW MEMBER":O$(30)="STEEL DISK":L1(30)=20:P(30)=1:PRINT" ZAP....SIZZILE....":D$(24)="HE IS DEAD":PLAY"O5GFEDCBA":D$(30)="Z":GOTO27
130 IFI(4)=0THEN PRINT"YOU DON'T HAVE ANYTHING TO KILL HIM WITH!":GOTO27
131 PRINT" ZAP...IT HAD NO EFFECT":GOTO27
132 FORZ=1TO35
133 IFO$=LEFT$(O$(Z),3)AND I(Z)=1 THEN I(Z)=0:L1(Z)=L:N5=N5-1:GOTO 135
134 NEXT:PRINT"YOU DON'T HAVE SUCH AN ITEM":GOTO27
135 IFL=24ANDO$="STE" THEN PRINT" THE DISK STICKS TO THE DOOR AND THE DOOR OPENS":PLAY"O1V5CCCCCCCCDDDDDDO4V30GEFGEFP1P1P1P1ACEGGGGG":W$(24)="23":E$(23)="24":P(30)=0:O$(27)="OPEN TRITINITE DOOR":GOTO27
136 IFL=3ANDO$="GRE" THEN PRINT "THE DOOR BLOWS OPEN AND YOU SEE A COMPUTER ROOM TO THE EAST.":PLAY"V10GFEDBAP1P1P1P1P1O1AEDFV25AEDFV15ADEFV10ADEFV5ADEFV5ADEFV30":E$(3)="2":O$(25)="SHRAPNEL":P(25)=0:O$(29)="DOOR OPEN":GOTO27
137 PRINT"IT HAS NO EFFECT":GOTO27
138 FORZ=1TO35
139 IFO$=LEFT$(O$(Z),3)AND I(Z)=1 THEN I(Z)=0:L1(Z)=L:N5=N5-1:GOTO141
140 NEXT:PRINT"YOU DON'T HAVE SUCH AN ITEM":GOTO27
141 IFL=13ANDO$="GAR" THEN PRINT"THE COOK THANKS YOU AND GIVES   YOU A QUARTER":PLAY"CCCCCEEEEEGGGGO3CCCCCO2":P(Z)=0:I(12)=1:GOTO27
142 PRINT"IT HAD NO EFFECT":GOTO27
143 FORZ=1TO35
144 IFO$=LEFT$(O$(Z),3) AND L1(Z)=L THEN GOTO 146
145 NEXT:PRINT"THAT ITEM IS'NT AROUND":GOTO27
146 IF D$(Z)="Z" THEN PRINT"YOU SEE NOTHING OF INTEREST" ELSE PRINT D$(Z)
147 GOTO27
148 IFL=8ANDO$="D$8" THEN PRINT" ENTRY CODE ACCEPTED":PRINT"  ID CARD NOW VALIDATED":PLAY"O1AP1AP1AP1AP1AP1AP1AP1AP1AP1A":O$(32)="CARD (ID)":L1(32)=8:P(32)=1:D$(32)="IT READS- FOR MAIN COMPUTER ROOM  ONLY":GOTO 27
149 IF L1(28)=3 AND L=2ANDO$="OPE" THEN W$(4)="5":N$(7)="5":PRINT" ENTRY ACCEPTED COMMAND CENTER  NOW OPEN":PLAY"CP1CP1CP1CP1CP1CP1CP1CP1CP1C":GOTO27
150 IF L=2 AND O$="OPE" THEN PRINT"PROCESSING ROOM NEEDS COMPUTER  DISKS":GOTO27
151 IF L=2 AND O$="CLO" THEN PRINT"PROCESSING ROOM NEEDS COMPUTER  DISKS":GOTO27
152 IF L1(28)=3 AND L=2ANDO$="CLO" THEN W$(4)="0":N$(7)="0":PRINT " ENTRY ACCEPTED COMMAND CENTER  NOW CLOSED":GOTO27
153 PRINT" WRONG CODE TRY AGAIN":GOTO27
154 FORZ=1TO2100:NEXT:CLS:PRINT@292,"---MOVES=";M5;"---":PRINT@320,"  PRESS ANY KEY TO TRY AGAIN";
155 PLAY"V10O3T12L2CP8D#P8GP8A#P8O4CP8":IF INKEY$=""THEN GOTO 155 ELSE RUN
156 IF O$="GUN"OR O$="CAN"OR O$="LAS" THEN 157 ELSE PRINT"FIRE WHAT? YOU DON'T KNOW HOW":GOTO27
157 IF L=11 OR L=16 THEN158 ELSE PRINT"I DON'T SEE ONE AROUND HERE.":GOTO27
158 PRINT"THE SHOT ALERTS THE GUARDS.           YOU ARE CAUGHT!!":PLAY"T55O3V5ABCDFEV10ABCDEV20ABCDV30ABCDEF":GOTO154
159 SCREEN1,1:PCLS:FOR Z=0 TO 3:DRAW"BM128,92S8A"+STR$(Z)+X5$:NEXT:FORZ=0 TO3100:NEXT:SCREEN0:CLS:GOTO154
