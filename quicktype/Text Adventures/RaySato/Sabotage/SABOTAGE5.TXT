0 CLEAR1000:DIMI$(16),I(49),V$(17),J,P,I,CC,C1,C2,T,M$,MC,C5,C6,P$:MC=16384:GOTO2800
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:GOSUB11:IFC1<LEN(M$)THEN2
4 RETURN
5 IFP$=""THENRETURN
6 M$="":FORP=1TOLEN(P$):I=ASC(MID$(P$,P,1)):M$=M$+CHR$(ABS((C5*(I>C6))-I)):NEXT:GOSUB1:RETURN
7 V$="":IFV0$=""THENRETURN
8 FORJ=1TOLEN(V0$):I=ASC(MID$(V0$,J,1)):V$=V$+CHR$(ABS((C5*(I>64))-I)):NEXT:RETURN
9 GOSUB5:GOTO2250
10 A$="ZM ZRIOLXP. GSVIV RH Z YOFV YFGGLM SVIV":S=2:RETURN
11 C2=(PEEK(17024)AND1)*256+PEEK(17025)
12 IFC2<=448THENRETURN
13 GOSUB3040
14 I$=INKEY$:IFI$=""THEN14
15 CLS:RETURN
20 A$="Z MZIILD XLIIRWLI":N=1:S=3:RETURN
30 A$="Z MZIILD XLIIRWLI":N=2:S=4:RETURN
40 A$="Z MZIILD XLIIRWLI":N=3:S=5:RETURN
50 A$="Z HNZOO ILLN":N=4:S=6:RETURN
60 A$="Z WVXLMGZNRMZGRLM XSZNYVI":B$="GSVIV RH Z YOFV YFGGLM SVIV":N=5:S=7:RETURN
70 A$="Z HNZOO HGLIZTV XSZNYVI":N=6:S=8:W=12:RETURN
80 A$="Z HNZOO XSZNYVI":N=7:S=9:W=13:RETURN
90 A$="Z HNZOO VOVXGILMRX OZYLIZGLIB":N=8:S=10:W=14:RETURN
100 A$="Z YRLOLTRXZO OZYLIZGLIB. GSVIV RH Z IVW YFGGLM LM GSV DZOO":N=9:W=15:RETURN
110 A$="Z HGLIZTV XSZNYVI":W=16:RETURN
120 A$="Z OZITV XSZNYVI. GSVIV RH Z XZYRMVG SVIV":S=13:W=17:E=7:RETURN
130 A$="Z HGIZMTV KFIKOV ILLN. GSVIV RH Z YOFV YFGGLM SVIV":N=12:S=14:W=18:E=8:RETURN
140 A$="Z HNZOO LUURXV":N=13:S=15:E=9:RETURN
150 A$="Z HNZOO ILLN DRGS Z XZIW GZYOV RM GSV XVMGVI":N=14:S=16:W=20:E=10:RETURN
160 A$="Z OZITV LUURXV. GSVIV RH Z WVHP SVIV":N=15:W=21:E=11:RETURN
170 A$="Z LUURXV DRGS Z OZITV WVHP":S=18:E=12:RETURN
180 A$="Z HGLIZTV ILLN":N=17:S=19:E=13:RETURN
190 A$="Z OZITV SZOO":N=18:S=20:RETURN
200 A$="ZM VMGVIGZRMNVMG MLLP. Z HXIVVM IVHGH LM GSV DZOO"
201 B$="GSVIV RH Z YOFV ZMW Z IVW YFGGLM FMWVI GSV HXIVVM":N=19:S=21:E=15:RETURN
210 A$="Z WZGZ IVXLIW HGLIZTV ILLN":N=20:E=16:RETURN
220 A$="IZWZI XLMGILO. GSVIV RH Z HNZOO HXIVVM SVIV":S=23:W=27:RETURN
230 A$="Z NVWRXZO HGZGRLM. GSVIV RH Z OZITV GZYOV SVIV":N=22:S=24:W=28:RETURN
240 A$="Z HVXFIRGB HGZGRLM":N=23:RETURN
250 A$="Z IZWRL ILLN":S=26:W=30:RETURN
260 A$="Z HNZOO ILLN. GSVIV RH Z HZUV RM GSV HLFGS DZOO":N=25:W=31:RETURN
270 A$="GSV ILYLG XLMGILO XVMGVI":B$="GSVIV RH Z HNZOO XLMGILO XLNKFGVI NLFMGVW RM GSV DZOO":W=32:E=22:RETURN
280 A$="GSV DVZKLMH HGLIZTV ILLN":S=29:W=33:E=23:RETURN
290 A$="Z ORYIZIB":N=28:S=30:W=34:RETURN
300 A$="Z HVXFIRGB XSVXP ZIVZ":N=29:W=36:E=25:RETURN
310 A$="Z HNZOO ILLN DRGS Z WVHP. Z HRTM IVZWH"
311 B$="'KIVHHFIV GL IVZXGLI.' Z YOFV YFGGLM RH OLXZGVW FMWVI GSV HRTM":E=26:RETURN
320 A$="OZFMXS XLMGILO. GSVIV RH Z WVHP SVIV":S=33:W=37:E=27:RETURN
330 A$="Z HNZOO XLIIRWLI":N=32:S=34:E=28:RETURN
340 A$="Z HGIZMTV YOFV ILLN. GSVIV RH Z IVW YFGGLM SVIV":N=33:S=35:E=29:RETURN
350 A$="Z GRMB HGLIZTV ILLN":N=34:W=40:RETURN
360 A$="Z HNZOO XSZNYVI. GSVIV RH Z DVHG WLLI SVIV":E=30:RETURN
370 A$="Z HNZOO, MZIILD XLIIRWLI":N=42:E=32:RETURN
380 A$="GSV XLNKFGVI XVMGVI. GSVIV RH Z HNZOO HOLG RM GSV XLNKFGVI":S=39:RETURN
390 A$="GSV XSVNRXZO OZY":N=38:S=40:RETURN
400 A$="GSV IVZXGLI XLMGILO XVMGVI. GSVIV RH Z YOFV YFGGLM ZMW Z IVW LMV"
401 B$="Z HRTM HZBH 'IVZXGLI XLMGILO - IVW=LM, YOFV=LUU'":N=39:E=35:RETURN
410 A$="GSV MFXOVZI IVZXGLI. Z XLNKFGVI IVHGH LM GSV DZOO":E=36:RETURN
420 A$="GSV DVHG VMW LU Z OLMT XLIIRWLI":S=37:E=43:RETURN
430 A$="GSV VZHG VMW LU Z OLMT XLIIRWLI":W=42:E=44:RETURN
440 A$="Z HVXFIRGB XVMGVI":W=43:E=45:RETURN
450 A$="Z HNZOO OZFMXS TZGV":B$="GSVIV RH Z HNZOO HOLG MVCG GL GSV OZFMXS TZGV":W=44:RETURN
460 A$="Z HNZOO HKZXVXIZUG. GSVIV RH Z HNZOO HOLG RM GSV NZHGVI":B$="OZFMXS XLNKFGVI":W=45
470 IFA=10AND(D3=10 OR D3=2)THEN C$="GSV NLMHGVI XZTV RH LKVM"
480 IFA=12ANDD5=0THENC$="GSV XZYRMVG RH OLXPVW"
490 IFA=12ANDD5=1THENC$="GSV XZYRMVG RH LKVM"
500 IFA=20ANDD6=0THENC$="GSV HXIVVM RH YOZMP"
510 IFA=20ANDD6=1THENC$="Z NLERV RH YVRMT KOZBVW LM GSV HXIVVM"
520 IFA=26ANDD9=0THENC$="GSV HZUV RH OLXPVW"
530 IFA=26ANDD9=1THENC$="GSV HZUV RH LKVM"
540 IFA=27ANDE2=0THENC$="GSV XLNKFGVI RH ZXGREV"
550 IFA=27ANDE2=1THENC$="GSV XLNKFGVI RH WVHGILBVW"
560 IFA=36ANDE6=0THENC$="GSV IVZXGLI WLLI RH URINOB OLXPVW"
570 IFA=36ANDE6=1THENC$="GSV IVZXGLI WLLI RH LKVM":W=41
580 IFA=45ANDE9=0THENC$="GSV OZFMXS TZGV RH XOLHVW"
590 IFA=45ANDE9=1THENC$="GSV OZFMXS TZGV RH LKVM":E=46
600 A$=A$+".":IFLEN(B$)>3THENB$=B$+"."
610 IFLEN(C$)>3THENC$=C$+"."
650 IFN<>0THENE$="MLIGS "
660 IFS<>0THENE$=E$+"HLFGS "
670 IFW<>0THENE$=E$+"DVHG "
680 IFE<>0THENE$=E$+"VZHG "
690 IFE$<>""THENE$=LEFT$(E$,LEN(E$)-1)
700 CLS:PRINT"YOU ARE IN:":P$=A$:GOSUB5:P$=B$:GOSUB5
710 P$=C$:GOSUB5
720 PRINT:PRINT"OBJECTS YOU CAN SEE:":P$=" ":FORT=1TO16:IFI(T)=ATHENP$="- "+I$(T):GOSUB5
725 NEXTT:IFP$=" "THENP$=" MLGSRMT":GOSUB5
730 PRINT:PRINT"EXITS: ":P$=E$:GOSUB5:PRINT
740 IF(A=40ORA=35ORA=30ORA=31)ANDI(4)=0ANDF3=0THENP$="GSV HNZOO YOZXP WVERXV RH YORMPRMT":GOSUB5
750 IFA=36ANDI(4)=0ANDF3=0THENP$="GSV HNZOO YOZXP WVERXV RH UOZHSRMT YIRTSGOB":GOSUB5
760 IFF4<>0THENP$="GSV XLNKFGVI HZBH: '"+STR$(F4)+" NRMFGVH FMGRO WVHGIFXGRLM'":GOSUB5
770 IFD3=1THENP$="*** ZORVM NLMHGVI ZGGZXPRMT ***":GOSUB5
780 IFD7=1ORE0=1ORE3=1ORE7=1THENP$="* HVXFIRGB KZGILO ZGGZXPRMT *":GOSUB5
790 PRINT:INPUT"COMMAND";V0$:IFV0$="HELP"THEN7000
791 IFV0$="SAVE"THEN4000
792 IFV0$="LOAD"THEN5000
795 GOSUB7
800 FORT=1TO4:IFV$=LEFT$(V$(T),1)THENV$=V$(T)
810 NEXT
820 IFLEN(V$)<3THEN700
830 V1$=LEFT$(V$,3):V2$=RIGHT$(V$,3)
840 FORT=1TO17:IFV1$=LEFT$(V$(T),3)THENV1=T
845 NEXT
850 IFV1=0THEN P$="R WLM'G FMWVIHGZMW DSZG BLF DZMG NV GL WL":GOTO9
860 FORT=1TO16:IFV2$=RIGHT$(I$(T),3)THENV2=T
870 NEXT
880 ONV1GOTO900,940,980,1050,1130,1310,1370,1390,1560,1620,1660,1790,1860,1910,2010,2170,2240
890 GOTO2250
900 IFN=0THEN1110
910 IFD3=1THENP$="GSV NLMHGVI YOLXPH GSV VCRG":GOTO9
920 IFD7=1ORE3=1THEN1100
930 A=N:GOTO2250
940 IFS=0THEN1110
950 IFS=24ANDD8<>0ANDE2<>1THEND7=1:GOSUB1120
960 IFS=30ANDE4<>0ANDE2<>1THENE3=1:GOSUB1120
970 A=S:GOTO2250
980 IFW=0THEN1110
990 IFD3=1THENP$="GSV NLMHGVI YOLXPH GSV VCRG":GOTO9
1000 IFE0=1ORE3=1ORE7=1THEN1100
1010 IFW=41ANDF3=0THENP$="IZWRZGRLM UILN GSV IVZXGLI SRGH BLF":GOSUB5:GOTO2510
1020 IFW=30ANDE4<>0ANDE2<>1THENE3=1:GOSUB1120
1030 IFW=27ANDE1<>0ANDE2<>1THENE0=1:GOSUB1120
1040 A=W:GOTO2250
1050 IFE=0THEN1110
1060 IFE0=1ORE3=1ORE7=1THEN1100
1070 IFE=27ANDE1<>0ANDE2<>1THENE0=1:GOSUB1120
1080 IFE=44ANDE8<>0ANDE2<>1THENE7=1:GOSUB1120
1090 A=E:GOTO2250
1100 P$="GSV HVXFIRGB ZMWILRW YOLXPH GSV VCRG.":GOTO9
1110 P$="GSVIV RH ML DZB GL TL GSZG WRIVXGRLM.":GOSUB5:FORJ=1TO1400:NEXT:GOTO2250
1120 P$="Z HVXFIRGB ZMWILRW ZDZRGH BLF.":GOSUB5:RETURN
1130 IFA=1ANDV2$="LXP"THENP$="GSV ZRIOLXP LKVMH ZMW BLF ZIV YOLDM LFG RMGL GSV EZXFFN LU HKZXV.":GOSUB5:GOTO2510
1140 IFA=12ANDV2$="MVG"AND D5=0 AND I(2)<>0THENP$="BLF ZIVM'G HGILMT VMLFTS GL ULIXV RG LKVM.":GOTO9
1150 IFA=12ANDV2$="MVG"ANDD5=0ANDI(2)=0THENP$="GSV XILDYZI SVOKVW. GSV XZYRMVG RH MLD LKVM.":D5=1:I(5)=ABS(I(5)):GOTO9
1160 IFA=12ANDV2$="MVG"ANDD5=1THENP$="GSV XZYRMVG RH ZOIVZWB LKVM.":GOTO9
1170 IFA=16ANDV2$="VHP"THENP$="LP.":I(6)=ABS(I(6)):GOTO9
1180 IFA=17ANDV2$="VHP"THENP$="LP.":I(7)=ABS(I(7)):GOTO9
1190 IFA=26ANDV2$="ZUV"ANDD9=1THENP$="GSV HZUV RH ZOIVZWB LKVM.":GOTO9
1200 IFA=26ANDV2$="ZUV"ANDD9=0THENP$="R WLM'G SZEV GSV PVB GL LKVM GSV HZUV.":GOTO9
1210 IFA=31ANDV2$="VHP"THENP$="LP. BLF URMW MLGSRMT RMHRWV.":GOTO9
1220 IFA=32ANDV2$="VHP"THENP$="LP.":I(14)=ABS(I(14)):GOTO9
1230 IFA=36ANDV2$="LLI"ANDE6=1THENP$="GSV WLLI RH ZOIVZWB LKVM.":GOTO9
1240 IFA=36ANDV2$="LLI"ANDE6=0ANDI(6)<>0THENP$="BLF WLM'G SZEV GSV PVB GL GSV WLLI.":GOTO9
1250 IFA=36ANDV2$="LLI"ANDE6=0ANDI(6)=0ANDE5=0THEN3030
1260 IFA=36ANDV2$="LLI"ANDI(6)=0THENP$="GSV WLLI RH MLD LKVM.":E6=1:GOTO9
1270 IFA=41ANDV2$="MVO"THENP$="GSV KZMVO RH URINOB OLXPVW.":GOTO9
1280 IFA=45ANDV2$="LXP"ANDE9=1THENP$="GSV ZRIOLXP RH ZOIVZWB LKVM.":GOTO9
1290 IFA=45ANDV2$="LXP"ANDE9=0THENP$="GSVIV ZIVM'G ZMB ERHRYOV XLMGILOH.":GOTO9
1300 P$="R XZM'G WL GSZG.":GOTO9
1310 IFV2$="GVI"ORV2$="LRW"THENP$="WLM'G YV IRWRXFOLFH.":GOTO9
1320 IFV2=0THENP$="R XZM'G WL GSZG.":GOTO9
1330 IFI(V2)=0THENP$="BLF ZOIVZWB SZEV GSZG.":GOTO9
1340 IFI(V2)<>ATHENP$="R WLM'G HVV RG SVIV.":GOTO9
1350 IFP4>=8THENP$="HLIIB. BLF XZM'G XZIIB ZMBGSRMT NLIV.":GOTO9
1360 P4=P4+1:I(V2)=0:P$="LP.":GOTO9
1370 IFV2=0ORI(V2)<>0THENP$="BLF WLM'G SZEV GSZG.":GOTO9
1380 P4=P4-1:I(V2)=A:P$="LP.":GOTO9
1390 IFI(5)<>0THENP$="BLF WLM'G SZEV Z DVZKLM.":GOTO9
1400 IFA=1ANDV2$="LXP"THENP$="BLF ZIV YOLDM LFG LU GSV ZRIOLXP RMGL GSV EZXFFN LU HKZXV.":GOSUB5:GOTO2510
1410 IFA=27ANDV2$="GVI"THENP$="GSV XLNKFGVI RH WVHGILBVW.":E2=1:E0=0:GOTO9
1420 IFA=38ANDV2$="GVI"THENP$="GSV HSLG IVUOVXGH LUU LU GSV XLNKFGVI.":GOSUB5:GOTO2510
1430 IFA=41ANDV2$="GVI"THENP$="GSV DSLOV MFXOVZI IVZXGLI RH VCKOLWRMT.":GOSUB5:GOTO2510
1440 IFV2$="RWN" OR V2$="YLG" OR V2$="ILO" OR V2$="IWH" OR V2$="ZIW"THENV2$="LRW"
1450 IFV2$<>"GVI"ANDV2$<>"LRW"THENP$="GSV OZHVI HSLG SZH ML VUUVXG.":GOTO9
1460 IFV2$="GVI"ANDD3=0THENP$="R WLM'G HVV ZMB NLMHGVI SVIV.":GOTO9
1470 IFV2$="LRW"ANDD7=0ANDE0=0ANDE3=0ANDE7=0THENP$="R WLM'G HVV ZMB ZMWILRWH SVIV.":GOTO9
1480 T=RND(100):IFT>P2+P3+50THENP$="BLF URIV ZMW NRHH.":GOTO9
1490 IFD3=1THENP$="BLF SRG GSV NLMHGVI":GOSUB5:D4=D4-((10+P2+P3)/2):IFD4<=0THEND3=0:D4=0:P$="BLF SZEV PROOVW RG.":GOTO9
1500 IFD7=1THENP$="BLF SRG GSV ZMWILRW":GOSUB5:D8=D8-((5+P2+P3)/2):IFD8<=0THEND7=0:D8=0:P$="RG RH WVHGILBVW.":GOTO9
1510 IFE0=1THENP$="BLF SRG GSV ZMWILRW":GOSUB5:E1=E1-((5+P2+P3)/2):IFE1<=0THENE0=0:E1=0:P$="RG RH WVHGILBVW.":GOTO9
1520 IFE3=1THENP$="BLF SRG GSV ZMWILRW":GOSUB5:E4=E4-((5+P2+P3)/2):IFE4<=0THENE3=0:E4=0:P$="RG RH WVHGILBVW.":GOTO9
1530 IFE7=1THENP$="BLF SRG GSV ZMWILRW":GOSUB5:E8=E8-((5+P2+P3)/2):IFE8<=0THENE7=0:E8=0:P$="RG RH WVHGILBVW.":GOTO9
1540 IFD3=1THENP$="RG RH HGROO ZOREV.":GOTO9
1550 P$="GSV ZMWILRW RH HGROO UFMXGRLMRMT.":GOTO9
1560 IFV2=0THENP$="R XZM'G WL GSZG.":GOTO9
1570 IFI(V2)<>0THENP$="R WLM'G SZEV GSZG.":GOTO9
1580 IFV2<>9ANDV2<>14THENP$="R XZM'G WL GSZG.":GOTO9
1590 IF(V2=9ANDA=44)OR(V2=14ANDA=38)THENP$="MLGSRMT SZKKVMH.":GOTO9
1600 IFV2=9ANDA=38THEN3010
1610 IFV2=14ANDA=45THENP$="GSV TZGV LKVMH.":E9=1:GOTO9
1620 IFV2<>10THENP$="WLM'G YV IRWRXFOLFH.":GOTO9
1630 IFI(10)<>0THENP$="BLF WLM'G SZEV GSZG.":GOTO9
1640 P$="LP.":I(10)=50:P4=P4-1:P1=P1+5+P3:IFP0<P1THENP0=P1
1650 GOTO9
1660 IFA=1ANDV2$="OFV"THENP$="GSV ZRIOLXP LKVMH...":GOSUB5:P$="BLF ZIV YOLDM LFG RMGL GSV EZXFFN LU HKZXV":GOSUB5:GOTO2510
1670 IFA=6ANDV2$="OFV"THENP$="Z HGIZMTV, LIZMTV TOLD XLEVIH BLF ZMW GSVM UZWVH ZDZB.":GOTO9
1680 IFA=10ANDV2$="IVW"ANDD3=1THENP$="MLGSRMT SZKKVMH.":GOTO9
1690 IFA=10ANDV2$="IVW"THEND3=1:P$="ZM ZORVM NLMHGVI RH IVOVZHVW. RG RH ZGGZXPRMT BLF!":GOTO9
1700 IFA=13ANDV2$="OFV"THENA=34:P$="Z UOZHS LU ORTSG GVNKLIZIROB YORMWH BLF.":GOTO9
1710 IFA=20ANDV2$="IVW"ANDD6=0THENP$="MLGSRMT SZKKVMH.":GOTO9
1720 IFA=20ANDV2$="IVW"THEND6=0:P$="GSV HXIVVM TLVH YOZMP.":GOTO9
1730 IFA=20ANDV2$="OFV"THEND6=1:P$="GSV HXIVVM ORTSGH FK.":GOTO9
1740 IFA=31ANDV2$="OFV"THENE5=1:P$="LP.":GOTO9
1750 IFA=34ANDV2$="IVW"THENA=13:P$="Z UOZHS LU ORTSG GVNKLIZIROB YORMWH BLF.":GOTO9
1760 IFA=40ANDV2$="IVW"THENF3=0:P$="LP.":GOTO9
1770 IFA=40ANDV2$="OFV"THENF3=1:P$="LP.":GOTO9
1780 P$="MLGSRMT SZKKVMH.":GOTO9
1790 IFA=22ANDV2$="VVM"THENP$="BLF XZM HVV MLGSRMT LU RMGVIVHG LM GSV IZWZI.":GOTO9
1800 IFV2=0THENP$="R WLM'G SZEV GSZG.":GOTO9
1810 IFI(V2)<>0ANDI(V2)<>ATHENP$="R WLM'G SZEV GSZG.":GOTO9
1820 IFV2=3ORV2=13THENP$="R HVV MLGSRMT HKVXRZO.":GOTO9
1830 IFV2=9THENP$="HLIIB, LMOB Z XLNKFGVI XZM IVZW Z KILTIZN.":GOTO9
1840 IFV2=16THENP$="GSV KOZMH ZIV HVZOVW...LMOB XLNNZMW XZM LKVM GSVN.":GOTO9
1850 P$="R XZM'G IVZW GSZG":GOTO9
1860 CLS:P$="*** KOZBVI'H RMEVMGLIB ***":GOSUB5:PRINT
1870 FORT=1TO16:IFI(T)=0THENP$="- "+I$(T):GOSUB5
1880 NEXT
1890 GOSUB3040
1895 X$=INKEY$:IFX$=""THEN1895
1900 GOTO2400
1910 IFV2=0THENP$="R XZM'G WL GSZG.":GOTO9
1920 IFI(V2)<>0THENP$="R WLM'G SZEV GSZG.":GOTO9
1930 IFV2=1ANDA=12ANDD5=0THENP$="GSV XZYRMVG OLXP RH WVHGILBVW.":D5=1:I(1)=100:I(5)=ABS(I(5)):P4=P4-1:GOTO9
1940 IF(V2=1ORV2=15)AND(D3=1ORD7=1ORE0=1ORE3=1ORE7=1)THENI(V2)=100:P4=P4-1:GOTO1490
1950 IF(V2=1ORV2=15)ANDA=1THENP$="GSV ZRIOLXP RH WVHGILBVW...BLF ZIV YOLDM LFG RMGL GSV EZXFFN LU HKZXV!":GOSUB5:GOTO2510
1960 IF(V2=1ORV2=15)ANDA=36ANDE6=0ANDE5=0THEN3000
1970 IF(V2=1ORV2=15)ANDA=36ANDE6=0ANDF3=0THENP$="GSV WLLI RH WVHGILBVW. BLF ZIV YLNYZIWVW DRGS IZWRZGRLM.":GOSUB5:GOTO2510
1980 IF(V2=1ORV2=15)ANDA=36ANDE6=0THENP$="GSV WLLI RH WVHGILBVW.":E6=1:I(V2)=100:P4=P4-1:GOTO9
1990 IFV2=1ORV2=15THENP$="GSV "+I$(V2)+" SZH ML VUUVXG.":I(V2)=100:P4=P4-1:GOTO9
2000 GOTO1370
2010 IFV2=0THENP$="R XZM'G WL GSZG.":GOTO9
2020 IFI(V2)<>0THENP$="R WLM'G SZEV GSZG.":GOTO9
2030 IFV2=5ANDD3=1THENV2$="GVI"
2040 IFV2=5AND(D7=1ORE0=1ORE3=1ORE7=1)THENV2$="LRW"
2050 IFV2=5THEN1390
2060 IFV2=4ANDF3=0AND(A=40 OR A=30 OR A=35 OR A=31)THENP$="GSV YOZXP WVERXV RH YORMPRMT.":GOTO9
2070 IFV2=4ANDF3=0ANDA=36THENP$="GSV YOZXP WVERXV RH UOZHSRMT YIRTSGOB.":GOTO9
2080 IFV2=4THENP$="GSVIV ZIVM'G ZMB ERHRYOV XLMGILOH LM GSRH WVERXV.":GOTO9
2090 IFV2=12THENI(12)=A:P4=P4-1:GOSUB3060:F2=35:P$="GSV KSLGLM YLNY DROO VCKOLWV RM 35 NRMFGVH.":GOTO9
2100 IFV2=2ANDA=12ANDD5=0THEND5=1:P$="GSV XZYRMVG RH MLD LKVM.":I(5)=ABS(I(5)):GOTO9
2110 IFV2=2ANDA=12ANDD5=1THENP$="GSV XZYRMVG RH ZOIVZWB LKVM.":GOTO9
2120 IFV2=7ANDA=26ANDD9=0THEND9=1:I(16)=ABS(I(16)):P$="GSV HZUV LKVMH.":GOTO9
2130 IFV2<>11THENP$="DSZG WL BLF DZMG NV GL WL DRGS GSV "+I$(V2)+"?":GOTO9
2140 IFI(8)<>0THENP$="GSVIV ZIVM'G ZMB YZGGVIRVH ULI GSV IZWRL.":GOTO9
2150 IFF2<>0THENP$="Z ELRXV HZBH 'YLNY HGZGFH:"+STR$(F2)+" NRMFGVH FMGRO WVGLMZGRLM.'":GOTO9
2160 P$="GSV IZWRL RH HROVMG.":GOTO9
2170 CLS:P$="*** KOZBVI'H HGZGFH ***":GOSUB5:PRINT
2180 P$="XFIIVMG SRG KLRMGH  = "+STR$(P1):GOSUB5
2190 P$="WVCGVIRGB ZGGIRYFGV = "+STR$(P2):GOSUB5
2200 P$="OFXP ZGGIRYFGV      = "+STR$(P3):GOSUB5
2210 GOSUB3040
2220 X$=INKEY$:IFX$=""THEN2220
2230 GOTO2400
2240 CLS:PRINT "GAME OVER":GOTO2520
2250 IFF2<>0THENF2=F2-1:IFF2<=0THEN2430
2260 IFF4<>0THENF4=F4-1:IFF4<=0THEN2470
2270 IFP1<P0THENP5=P5+.5:IFP5=1THENP5=0:P1=P1+1
2280 IFD3=0ANDD7=0ANDE0=0ANDE3=0ANDE7=0THEN2390
2290 T=RND(100)
2300 IFD3=1THENP$="GSV NLMHGVI ZGGZXPH ":GOSUB5
2310 IFD3<>1THENP$="GSV HVXFIRGB ZMWILRW HSLLGH... ":GOSUB5
2320 IFT>80-(P2+P3)THENP$="RG NRHHVH.":GOSUB5:GOTO2390
2330 P1=P1-(RND(5)+RND(5)+RND(5)+15-P3)
2340 IFD3<>1THENP1=P1+5
2350 IFP1<0THEN2510
2380 P$="BLF ZIV SRG!":GOSUB5
2390 IFV1=0ORV1>4OR(D3+D7+E0+E3+E7>0)THENFORJ=1TO1400:NEXT
2400 V$="":V1$="":V2$="":V1=0:V2=0:A$="":B$="":C$="":D$="":E$="":N=0:S=0:W=0:E=0
2410 IFA<21THENONAGOSUB10,20,30,40,50,60,70,80,90,100,110,120,130,140,150,160,170,180,190,200:GOTO470
2411 IFA<41THENONA-20GOSUB210,220,230,240,250,260,270,280,290,300,310,320,330,340,350,360,370,380,390,400:GOTO470
2412 IFA<47THENONA-40GOSUB410,420,430,440,450,2550
2420 GOTO470
2430 CLS:IFA=46THENF4=-1:GOTO2550
2440 P$="GSV KSLGLM YLNY VCKOLWVH...GSV VMGRIV XLNKOVC RH WVHGILBVW":GOSUB5
2450 P$="BLF SZEV YVVM PROOVW YB GSV ULIXV LU GSV YOZHG":GOSUB5
2460 PRINT:GOTO2510
2470 CLS:IFA=46THEN2550
2480 IFA=38THENP$="GSV XLNKFGVI UOZHSVH YIRTSGOB, VNRGGRMT HKZIPH RM ZOO WRIVXGRLMH":GOSUB5
2490 P$="GSV XLNKOVC HFWWVMOB VCKOLWVH RMGL NROORLMH LU KRVXVH":GOSUB5
2500 P$="BLF ZIV PROOVW YB GSV UZOORMT WVYIRH ZILFMW BLF.":GOSUB5:PRINT
2510 P$="BLF ZIV WVZW!":GOSUB5
2520 INPUT"DO YOU WANT TO PLAY AGAIN";A$
2530 IFLEFT$(A$,1)="Y"THENRUN
2540 END
2550 V0$="THE GATE CLOSES BEHIND YOU.":GOSUB7:P$=V$:GOSUB5
2555 P$="GSV HKZXV HSRK HFWWVMOB ORUGH RMGL LIYRG ZILFMW GSV KOZMVG.":GOSUB5
2560 IF((F2=0)OR(F2<>0ANDI(12)<>41))ANDF4=0THENP$="BLF WRWM'G WVHGILB GSV YZHV. NRHHRLM UZROVW":GOSUB5:PRINT:GOTO2520
2570 P$="UILN Z WRHGZMXV, BLF XZM HVV GSV ZORVM YZHV VCKOLWV.":GOSUB5
2580 IFI(16)<>0THENP$="BLF WRWM'G IVXLEVI GSV HVXIVG KOZMH MVVWVW YB HGZI XLNNZMW.":GOSUB5:PRINT:GOTO2520
2590 P$="NRHHRLM RH Z HFXXVHH!":GOSUB5
2600 GOTO2520
2610 C5=-155:C6=64:CLS:PRINT"OPERATION: sabotage BY RAY SATO"
2640 FORT=1TO16:READ I$(T):READI(T):NEXT:FORT=1TO17:READV$(T):NEXT:FORT=1TO40:P0=P0+RND(2):NEXT
2700 P1=P0
2710 FORT=1TO10:P2=P2+RND(2):NEXT
2720 FORT=1TO10:P3=P3+RND(2):NEXT
2730 FORT=1TO50:D4=D4+RND(2):D8=D8+RND(2):E1=E1+RND(2):E4=E4+RND(2):E8=E8+RND(2):NEXT
2740 A=1:P4=1
2750 GOSUB10:GOTO470
2760 DATA KOZHGRX VCKOLHREV,0,XILDYZI,7,XZOVMWZI,8,HNZOO YOZXP WVERXV,9,OZHVI KRHGLO,-12,HVXFIRGB PVB,-16
2761 DATA VOVXGILMRX XLMGILO YZGLM,-17,YZGGVIRVH,18,XLNKFGVI WVHGIFXG KILTIZN,21,HROEVI KROO,23,KLIGZYOV IZWRL,25
2762 DATA OZITV KSLGLM YLNY,28
2770 DATA TZOZXGRX XSZIG,32,OZFMXS HBHGVN XZHHVGGV,-32,MRGILTOBXVIRM,39,HVXIVG KOZMH,-26
2780 DATA MLIGS,HLFGS,DVHG,VZHG,LKVM,TVG,WILK,HSLLG,RMHVIG,VZG,KFHS,IVZW,RMEVMGLIB,GSILD,FHV,HGZGFH,JFRG
2800 CLS:M$="IT IS THE YEAR 2101 AND WAR HAS BROKEN OUT BETWEEN EARTH AND THE DISTANT PLANET ZEKLOKE.":GOSUB1
2810 M$="THIS ALIEN POWER HAS ESTABLISHED A LARGE MILITARY COMPLEX ON MARS WHICH WILL SOON BECOME A GREAT"
2820 M$=M$+" DANGER TO EARTH.":GOSUB1
2890 M$="HIDDEN IN THE MASSIVE INSTALLATION ARE SEVERAL SECRET DOCUMENTS CONTAINING THE"
2900 M$=M$+" PLANS FOR A INCREDIBLE DEFENSE SHIELD.":GOSUB1:M$="THIS SHIELD IS STRONG ENOUGH TO STOP AN ENTIRE"
2910 M$=M$+" FLEET OF SPACECRAFT.":GOSUB1:M$="YOU ARE A SPECIAL AGENT AND HAVE JUST SUCCEEDED IN"
2920 M$=M$+" SNEAKING INTO THE ALIEN COMPLEX.":GOSUB1:M$="YOUR MISSION IS TO DESTROY THIS BASE AND"
2930 M$=M$+" RETURN WITH THE PLANS FOR THE DEFENSE SHIELD.":GOSUB1:M$="THE OUTCOME OF THE MISSION"
2940 M$=M$+" WILL DECIDE THE FATE OF MANKIND. GOOD LUCK!":GOSUB1:GOSUB3040
2960 X$=INKEY$:IFX$=""THEN2960
2970 GOTO2610
3000 P$="GSV WLLI RH WVHGILBVW...BLF ZIV HFXPVW RMGL GSV MLM-":GOSUB5:P$="KIVHHFIRAVW IVZXGLI YFROWRMT.":GOSUB5:GOTO2510
3010 F4=35:P$="GSV XLNKFGVI IVKORVH: 'YZHV WVHGIFXG HVJFVMXV HGZIGVW.":GOSUB5
3020 P$="WVHGIFXGRLM RM"+STR$(F4)+" NRMFGVH.'":P4=P4-1:I(9)=100:GOTO9
3030 P$="BLF ZIV HFXPVW RMGL GSV FMKIVHHFIRAVW IVZXGLI YFROWRMT.":GOSUB5:GOTO2510
3040 C2=(PEEK(17024)AND1)*256+PEEK(17025)+MC+2:PRINT "  **HIT ANY KEY TO CONTINUE**";:FORJ=C2TOC2+26:POKEJ,PEEK(J)-64:NEXT
3050 RETURN
3060 V0$="ARMED PHOTON BOMB":GOSUB7:I$(12)=V$:RETURN
4000 CLS:PRINT"SAVE GAME":I(17)=E0:I(18)=E1:I(19)=E2:I(20)=E3:I(21)=E4:I(22)=E5:I(23)=E6:I(24)=E7:I(25)=E8:I(26)=E9
4010 I(27)=P0:I(28)=P1:I(29)=P2:I(30)=P3:I(31)=P4:I(32)=P5
4020 I(33)=D3:I(34)=D4:I(35)=D5:I(36)=D6:I(37)=D7:I(38)=D8:I(39)=D9
4030 I(40)=F2:I(41)=F3:I(42)=F4
4040 I(43)=V1:I(44)=V2
4050 I(45)=N:I(46)=S:I(47)=E:I(48)=W:I(49)=A:GOSUB6000
4070 IFX$="Q"THEN2250
4080 PRINT"SAVING...":CSAVE*I,"SABODATA"
4090 GOTO2250
5000 CLS:PRINT"LOAD GAME":GOSUB6000
5010 IFX$="Q"THEN2250
5020 PRINT"LOADING...":CLOAD*I,"SABODATA"
5100 E0=I(17):E1=I(18):E2=I(19):E3=I(20):E4=I(21):E5=I(22):E6=I(23):E7=I(24):E8=I(25):E9=I(26)
5110 P0=I(27):P1=I(28):P2=I(29):P3=I(30):P4=I(31):P5=I(32)
5120 D3=I(33):D4=I(34):D5=I(35):D6=I(36):D7=I(37):D8=I(38):D9=I(39)
5130 F2=I(40):F3=I(41):F4=I(42):IFF2<>0THENGOSUB3060
5140 V1=I(43):V2=I(44)
5150 N=I(45):S=I(46):E=I(47):W=I(48):A=I(49):GOTO2250
6000 PRINT"hit a key (q=ABORT)":POKEMC+35,32:POKEMC+37,32
6010 X$=INKEY$:IFX$=""THEN6010
6020 CLS:RETURN
7000 PRINT"USE TWO WORD COMMANDS, N,S,E,W,"
7010 PRINT"INV, STATUS, SAVE, LOAD, OR QUIT"
7020 GOSUB3040:GOSUB6010:GOTO2250
