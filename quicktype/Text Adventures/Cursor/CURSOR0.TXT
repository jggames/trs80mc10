10 CLS:PRINT@262,"CURSOR, FOILED AGAIN!":GOSUB2170
20 DIM IN$(10)
30 X=1:TI=0:RM=1
40 REM START OF LOOP
50 ON RM GOSUB 240,590,800,990,1200,1360,1480,1650,1710,1790,1810,1860,1930,2010,2120
60 IF RM=18 THEN 190
70 TI=TI+1
80 IF TI>20 THEN 180
90 IF RM=10 THEN 210
100 REM REPORT
110 CLS
120 PRINT "YOU HAVE USED UP";TI;"MINUTES"
130 PRINT "YOU ARE CARRYING:   "
140 FOR D=1 TO X
150 PRINT IN$(D):NEXT D
160 GOSUB2170
170 GOTO 40
180 PRINT "YOU RAN OUT OF TIME AND":PRINT "DISAPPEARED"
190 PRINT "GAME OVER"
200 GOTO 230
210 PRINT "YOU DID IT! YOU REWIRED IN THE":PRINT "CORRECT ORDER"
220 PRINT "THE ENLARGING BEAM ZAPS YOU":PRINT "BACK TO FULL SIZE"
230 END
240 REM CLASSROOM
250 CLS
260 PRINT "YOU ARE IN YOUR SCHOOL'S"
270 PRINT "COMPUTER ROOM AND HAVE JUST ":PRINT "BEEN ZAPPED BY A BEAM OF LIGHT"
280 PRINT "YOU FIND YOURSELF SHRINKING"
290 PRINT "YOU MUST FIND CURSOR":PRINT "WITHIN";20-TI;"MINUTES"
300 PRINT "OR YOU WILL DISAPPEAR COMPLETELY"
310 PRINT "YOU SEE IN FRONT OF YOU:"
320 IF BP<>0 THEN 340
330 PRINT "A BUS PASS (1)"
340 IF GU<>0 THEN 360
350 PRINT "A PIECE OF GUM (2)"
360 IF RB<>0 THEN 380
370 PRINT "A RUBBER BAND (3)"
380 PRINT "CHOOSE ONE (BY NUMBER)"
390 INPUT CH
400 ON CH GOTO 420,440,460
410 GOTO 380
420 IN$(X)="BUS PASS"
430 BP=1:GOTO 480
440 IN$(X)="GUM"
450 GU=1:GOTO 480
460 IN$(X)="RUBBER BAND"
470 RB=1
480 X=X+1
490 PRINT "THERE ARE TWO COMPUTERS HERE"
500 PRINT "YOU CAN ENTER ONE OF THEM"
510 PRINT "THE LOOPINS (1) OR THE WIDGEE(2)";
520 PRINT "PICK ONE"
530 INPUT CH
540 ON CH GOTO 560,570
550 GOTO 530
560 RM=2:GOTO 580
570 RM=9
580 RETURN
590 REM LOOPINS COMPUTER
600 CLS
610 PRINT "YOU ARE IN THE LOOPINS COMPUTER"
620 IF BW=1 THEN 700
630 PRINT "YOU SEE A BLUE WIRE"
640 PRINT "YOU PICK IT UP"
650 IN$(X)="BLUE WIRE":X=X+1
660 BW=1
670 PRINT "YOU ARE HOT ON CURSOR'S TRAIL"
680 PRINT "BUT IT'S DARK IN HERE"
690 PRINT "YOU COULD USE SOME LIGHT"
700 PRINT "AHEAD YOU SEE THREE PORTS"
710 PRINT "MDM(1), CRT(2), AND KBD(3)"
720 PRINT "WHICH WAY DO YOU GO?"
730 INPUT CH
740 ON CH GOTO 760,770,780
750 GOTO 670
760 RM=12:GOTO 790
770 RM=6:GOTO 790
780 RM=7
790 RETURN
800 REM THE SCREEN
810 CLS
820 IF GW=1 THEN 880
830 PRINT "YOUR KEY TURNED ON THE SCREEN"
840 PRINT "BY ITS LIGHT YOU SEE A GREEN":PRINT "WIRE"
850 PRINT "YOU PICK IT UP"
860 IN$(X)="GREEN WIRE":X=X+1
870 GW=1
880 PRINT "ON THE SCREEN IS A RIDDLE"
890 PRINT "IT SAYS: CATCH ME AT THE":PRINT"DATABASE WHERE YOU FIND THE PIE"
900 PRINT "BELOW THE RIDDLE ARE TWO":PRINT"DATABASES TO CHOOSE FROM"
910 PRINT "Q-CIRCLE(1) AND R-SQUARE(2)"
920 PRINT "WHICH DO YOU CHOOSE?"
930 INPUT CH
940 ON CH GOTO 960,970
950 GOTO 920
960 RM=13:GOTO 980
970 RM=4
980 RETURN
990 REM R SQUARE
1000 CLS
1010 PRINT "YOU ARE IN THE R SQUARE DATABASE";
1020 PRINT "YOU MUST HAVE GOT CURSOR'S JOKE":PRINT "ABOUT PIE R SQUARED"
1030 PRINT
1040 IF YW=1 THEN 1090
1050 PRINT "IN FRONT OF YOU IS A YELLOW WIRE";
1060 PRINT "YOU PICK IT UP"
1070 IN$(X)="YELLOW WIRE":X=X+1
1080 YW=1
1090 PRINT "YOU TURN A CORNER, AND THERE IS":PRINT "CURSOR"
1100 PRINT "'I'VE HIDDEN THE ENLARGING BEAM":PRINT "IN THE CPU' HE CACKLES"
1110 PRINT:PRINT "THERE'S ONLY ONE WAY TO GET":PRINT "THERE"
1120 PRINT "BY BUS(1) OR TRAIN(2)"
1130 PRINT "CHOOSE ONE"
1140 INPUT CH
1150 ON CH GOTO 1170,1180
1160 GOTO 1110
1170 RM=14:GOTO 1190
1180 RM=8:GOTO 1190
1190 RETURN
1200 REM THE CPU
1210 CLS
1220 PRINT "YOU ARE IN THE CENTRAL":PRINT "PROCESSING UNIT"
1230 PRINT "YOU MUST REWIRE THE COMPUTER"
1240 PRINT "TO PRODUCE THE ENLARGING BEAM"
1250 PRINT "USE YOUR WIRES-- BUT YOU ONLY":PRINT "GET ONE CHANCE"
1260 PRINT "WHICH SEQUENCE DO YOU CONNECT":PRINT "THE WIRES IN?"
1270 PRINT "GREEN, BLUE, YELLOW (1)"
1280 PRINT "BLUE, GREEN, YELLOW (2)"
1290 PRINT "YELLOW, BLUE, GREEN (3)"
1300 INPUT CH
1310 ON CH GOTO 1320,1330,1340
1320 RM=11:GOTO 1350
1330 RM=10:GOTO 1350
1340 RM=11
1350 RETURN
1360 REM THE CRT
1370 CLS
1380 PRINT "YOU ARE STANDING IN FRONT OF":PRINT "A CRT"
1390 PRINT "THERE IS A KEYHOLE UNDER IT"
1400 GOSUB2170
1410 IF K=0 THEN 1430
1420 RM=3:GOTO 1470
1430 PRINT "UNFORTUNATELY, YOU DON'T HAVE":PRINT "THE KEY"
1440 PRINT "YOU GO BACK TO THE LOOPINS TO":PRINT "LOOK FOR IT"
1450 RM=2
1460 GOSUB2170
1470 RETURN
1480 REM KBD
1490 CLS
1500 PRINT "KBD STANDS FOR KEYBOARD"
1510 PRINT "THERE IS A KEY HERE--":PRINT "DO YOU TAKE IT? Y/N"
1520 INPUT CH$
1530 IF CH$="N" THEN 1560
1540 IN$(X)="KEY":X=X+1
1550 K=1
1560 PRINT "THERE ARE FINGERS TYPING ALL":PRINT "AROUND YOU"
1570 PRINT "YOU CAN STAY HERE OR GO BACK":PRINT "TO LOOPINS"
1580 PRINT "STAY HERE?(1) OR GO BACK?(2)"
1590 INPUT CH
1600 ON CH GOTO 1620,1630
1610 GOTO 1570
1620 RM=15:GOTO 1640
1630 RM=2
1640 RETURN
1650 REM TRAIN
1660 CLS
1670 PRINT "THIS TRAIN IS A LOCAL"
1680 TI=31
1690 GOSUB2170
1700 RETURN
1710 REM WIDGEE
1720 CLS
1730 PRINT "AN EVIL GUY LIKE CURSOR WOULD"
1740 PRINT "NEVER GO TO A COMPUTER NAMED"
1750 PRINT "WIDGEE.":PRINT"YOU MUST RETURN TO THE":PRINT "CLASSROOM"
1760 RM=1
1770 GOSUB2170
1780 RETURN
1790 REM YOU WIN
1800 RETURN
1810 REM WRONG SEQUENCE
1820 CLS
1830 PRINT "SORRY-- THAT WAS THE WRONG":PRINT "SEQUENCE"
1840 RM=18
1850 RETURN
1860 REM MODEM
1870 CLS
1880 PRINT "YOU ARE IN THE MDM-- THE MODEM"
1890 PRINT "YOU ARE TRANSMITTED BACK TO":PRINT "THE CLASSROOM"
1900 RM=1
1910 GOSUB2170
1920 RETURN
1930 REM Q CIRCLE
1940 CLS
1950 PRINT "YOU ARE IN THE Q CIRCLE DATABASE";
1960 PRINT "THERE'S NO PIE HERE"
1970 PRINT "YOU MUST RETURN TO THE CRT"
1980 GOSUB2170
1990 RM=3
2000 RETURN
2010 REM THE BUS
2020 CLS
2030 IF BP=1 THEN 2070
2040 PRINT "SORRY, YOU DON'T HAVE A BUS PASS";
2050 PRINT "YOU MUST GO BACK TO THE":PRINT "CLASSROOM TO GET IT"
2060 RM=1:GOTO 2100
2070 PRINT "GOOD THING YOU HAD A BUS PASS"
2080 PRINT "YOU ARE ON THE BUS GOING TO":PRINT "THE CPU"
2090 RM=5
2100 GOSUB2170
2110 RETURN
2120 REM SQUASHED
2130 CLS
2140 PRINT "YOU GOT SQUASHED BY A FINGER"
2150 RM=18
2160 RETURN
2170 FOR D=1 TO 3500:NEXT D:RETURN
2180 REM ENTER MAGAZINE
2190 REM APRIL 1985
