0 CLS:CLEAR450:DIMCC,ZZ,Z$,I$,C1:MC=16384:GOTO160
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1)
2 IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
3 C1=((PEEK(384)AND1)*256+PEEK(385))+1:IFC1>479THENGOSUB5
4 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 PRINT"hit any key";
6 IFINKEY$=""THEN6
7 CLS:PRINT:RETURN
8 Z$=" "
9 GOSUB1:RETURN
10 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC
11 FORCC=1TOLEN(Z$):ZZ=ASC(MID$(Z$,CC)):POKEC1+CC,ZZ-(ZZAND64):NEXT:RETURN

70 GOSUB15000:GOSUB14000:RETURN

80 Z$="AS YOU COME BACK ABOARD THE SHIP, THE CAPTAIN GREETS YOU. 'WELCOME BACK MATEY!'":GOTO9
90 GOSUB8:Z$="YOU CAN'T GO THAT WAY.":GOTO9
100 KW=1:GOSUB2500:GOTO190
160 CLS
170 DIMA(45,6),O1$(20),O2$(20),TR(14),IN$(14),R$(45)
171 GOSUB20000:CLS:PRINT"  PIRATE ISLAND BY DAVID MENY": PRINT" COPYRIGHT 1985, MENCO, INC.":GOSUB1
175 Z$="YOU DREAM OF VALIANT PIRATES WHO SAIL THE SEAS FINDING SECRET TREASURE HIDDEN ON DANGEROUS ISLANDS...":GOSUB1
178 Z$="OF FIGHTS TO THE DEATH OVER GOLD-LACED STATUES AND CHESTS OF GOLD...":GOSUB1
180 Z$="YOU DREAM UNTIL THE ROCKING OF THE SEAS WAKENS YOU ON BOARD THE 'THE EAGLE'S NEST', A PIRATE SHIP!":GOSUB1:GOSUB8

190 GOSUB70
210 IFST=1THENMV=MV+1:IFMV=10THENTR(2)=R
211 IFST<>1ORMV<>10THEN219
212 Z$="THE CAPTAIN WALKS UP TO YOU.":GOSUB1:IFA(24,0)=1THEN216
214 Z$="'FOOL!! WE'VE BEEN SAILING FOR DAYS AND HAVEN'T FOUND PIRATE ISLAND. I SHOULDN'T HAVE TRUSTED YOU, "
215 Z$=Z$+"EVEN THOUGH YOU ARE MY GRAND SON.' THE CAPTAIN PULLS OUT HIS CUTLASS AND DECAPITATES YOU.":GOSUB1:GOTO2000
216 Z$="'CONGRATULATIONS! WE'VE COME ASHORE ON ZE' OLD PIRATE ISLAND. NOW GO ASHORE BY THE EAST STARBOARD DECK.' "
217 Z$=Z$+"THE CAPTAIN HANDS YOU A BAG OF SILVER. 'SPEND IT WISELY. IT'S YOUR PAY.'":TR(2)=25:A(39,2)=22:A(23,0)=1:TR(14)=200
218 GOSUB1
219 GOSUB8:INPUTA$
220 IFA$=""THENGOSUB8:Z$="WHAT?":GOSUB1:GOTO210
245 IFA$="LOOK UNDER JULIUS"THENGOSUB8010:GOTO210
246 IFA$="LOOK UNDER PIRATE ISLAND" THENGOSUB8150:GOTO210
247 IFA$="LOOK UNDER DAVID MENY" THEN GOSUB8300:GOTO210
250 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO250
260 KW=0:B$=LEFT$(A$,3)
270 IFB$="INV"THENGOSUB1000:GOTO210
280 IFB$="SCO"THENGOSUB1500:GOTO210
285 IFB$="DIG"THENGOSUB2250:GOTO210
286 IFB$="KNO"THENGOSUB6500:GOTO210
290 IFB$="QUI"THENGOSUB2000
300 IFB$="LOO"THENGOSUB8:GOTO190
305 IFB$="SWI"THENGOSUB8500:GOTO210
310 IFB$="N  "THENC$=B$:GOTO100
320 IFB$="S  "THENC$=B$:GOTO100
330 IFB$="E  "THENC$=B$:GOTO100
340 IFB$="W  "THENC$=B$:GOTO100
350 IFB$="U  "THENC$=B$:GOTO100
360 IFB$="D  "THENC$=B$:GOTO100
370 N=1
380 IFMID$(A$,N,1)=" "THEN385
381 GOTO390
385 C$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN410
386 GOTO400
390 IFN<MTHENN=N+1:GOTO380
400 GOTO510
410 IFB$="GO "THEN100
420 IFB$="GET"ORB$="TAK"THENGOSUB3000:GOTO210
430 IFB$="DRO"ORB$="PUT"THENGOSUB3500:GOTO210
440 IFB$="OPE"THENGOSUB4000:GOTO210
450 IFB$="UNL"THENGOSUB4500:GOTO210
460 IFB$="BUY"THENGOSUB5000:GOTO210
480 IFB$="CLI"THENGOSUB6000:GOTO210
490 IFB$="REA"THENGOSUB7000:GOTO210
510 GOSUB8:Z$="I DON'T KNOW THAT WORD.":GOSUB1
520 GOTO210

1000 Z$="  YOU ARE CARRYING:":GOSUB1:GOSUB8:FORI=1TO14:IFTR(I)=200THENZ$="   "+IN$(I):GOSUB1:A(1,0)=1
1050 NEXTI:IFA(1,0)<>1THENZ$="   NOTHING":GOSUB1
1070 A(1,0)=0:RETURN

1500 GOSUB8
1520 Z$="OUT OF 200 POINTS, YOU GOT "+STR$(S)+" POINTS.":GOSUB1
1521 Z$="THAT GIVES YOU THE RATING OF "
1522 IFS<50THENZ$=Z$+"A DECK BOY.":GOSUB1
1523 IFS>50ANDS<100THENZ$=Z$+"A SEA MAN.":GOSUB1
1524 IFS>100ANDS<150Z$=Z$+"A BUCANEER.":GOSUB1
1525 IFS>150THENZ$=Z$+"A SEA CAPTAIN.":GOSUB1
1530 RETURN
2000 GOSUB1500:GOTO9020

2250 INPUT"WITH WHAT";F$:F$=LEFT$(F$,3)
2252 IFF$<>"SHO"THENZ$="YOU CAN'T USE THAT TO DIG WITH.":GOTO9
2260 IFR<>1ANDR<>11ANDR<>21THEN2270
2261 GOTO2290
2270 IFR>23THENZ$="IF YOU DIG ON THE SHIP, YOU MIGHT CAUSE A LEAK.":GOTO9
2280 Z$="THE GROUND IS TOO HARD HERE.":GOTO9
2290 IFR=1THEN2295
2291 GOTO2300
2295 Z$="YOU DIG INTO THE GROUND... YOU HIT SOMETHING! IT'S A COFFIN! WHEN YOU OPEN IT, A SKELETON POPS OUT, KNIFE FIRST, "
2296 Z$=Z$+"STABBING YOU! THE COFFIN WAS BOOBEYTRAPPED!":GOSUB1:GOTO2000
2300 IFR=11THENZ$="YOU DIG BUT YOU DON'T FIND ANYTHING.":GOTO9
2310 Z$="YOU DIG IN THE DAMP SOIL AND SOON YOU COME UPON A GRATING IN THE GROUND!":TR(13)=21:GOTO9

2500 C$=LEFT$(C$,1)
2511 IFC$="S"ANDR=39ANDA(23,0)=0THENZ$="ARE YOU SURE YOU WANT TO DIVE INTO THE WATER?":GOTO9
2520 IFC$="N"ANDA(R,1)=0THEN90
2530 IFC$="S"ANDA(R,2)=0THEN90
2540 IFC$="E"ANDA(R,3)=0THEN90
2550 IFC$="W"ANDA(R,4)=0THEN90
2560 IFC$="U"ANDA(R,5)=0THEN90
2570 IFC$="D"ANDA(R,6)=0THEN90
2580 IFC$="N"THENR=A(R,1)
2590 IFC$="S"THENR=A(R,2)
2600 IFC$="E"THENR=A(R,3)
2610 IFC$="W"THENR=A(R,4)
2620 IFC$="U"THENR=A(R,5)
2630 IFC$="D"THENR=A(R,6)
2640 IFR=22ANDA(2,0)=0THENS=S+20:A(2,0)=1
2650 IFR=23ANDA(3,0)=0THENS=S+20:A(3,0)=1
2660 RETURN

3000 IFC$="CHE"ANDR=23THENZ$="THE CHEST IS TOO LARGE AND HEAVY FOR YOU TO CARRY.":GOTO9
3020 IFC$="CAP"ANDR=25THENZ$="THE CAPTAIN MOVES JUST AS YOU TRY TO LIFT HIM. 'HEY, ARE YOU GETTING FRESH MATEY!?'":GOTO9
3030 IFC$="COO"ANDR=40THENZ$="THE COOK SLAPS YOUR HAND. 'NOT NOW, SOMEONE MIGHT SEE US.'":GOTO9
3040 IFC$="STE"ANDR=40THENZ$="THE COOK SLAPS YOUR HAND. 'WAIT TILL IT'S DONE.'":GOTO9
3041 IFC$="GAR"ANDA(24,0)=0THENZ$="'NO ONE MESSES WITH MY STEW MAKIN'S.' THE COOK CUTS YOUR THROAT.":GOSUB1:GOTO2000
3042 IFC$="GRA"THENZ$="THE GRATE IS SECURED TO THE GRATE.":GOTO9
3050 A(7,0)=0:A(8,0)=0:O=0
3060 FORI=1TO14
3070 IFTR(I)=200THENA(7,0)=A(7,0)+1
3080 NEXTI
3090 IFA(7,0)>6THENGOSUB8:Z$="YOU CAN'T CARRY ANYMORE.":GOTO9
3100 FORI=1TO12
3110 IFTR(I)=RTHENA(8,0)=1
3115 NEXTI
3120 IFA(8,0)=0THENGOSUB8:Z$="THERE IS NOTHING HERE TO PICK UP.":GOTO9
3130 FORI=1TO14
3140 IFC$=O1$(I)ANDTR(I)=RTHENO=I
3150 NEXT
3160 IFO=0THENGOSUB8:Z$="I DON'T SEE THAT HERE.":GOTO9
3170 TR(O)=200
3180 Z$="OK.":GOSUB1
3190 IFTR(1)=200ANDA(9,0)=0THENS=S+15:A(9,0)=1
3200 IFTR(12)=200ANDA(10,0)=0THENS=S+40:A(10,0)=1
3201 IFTR(9)=200ANDA(18,0)=0THEN S=S+60:A(18,0)=1
3210 RETURN
3500 REM DROP ROUTINE
3510 O=0:FORI=1TO14
3520 IFC$=O1$(I)THENO=I
3530 NEXTI
3540 IFO=0THENZ$="YOU DON'T HAVE THAT!":GOTO9
3550 Z$="OK.":GOSUB1
3560 TR(O)=R
3570 RETURN

4000 IFC$<>"DRA"ANDC$<>"BOX"ANDC$<>"CHE"ANDC$<>"DOO"THENGOSUB8:Z$="YOU CAN'T OPEN THAT.":GOTO9
4011 GOSUB8
4015 IFC$="DRA"ANDR<>26THENZ$="I DON'T SEE A DRESSER HERE.":GOTO9
4016 IFC$="DRA"ANDR=26ANDA(4,0)=1THENZ$="THE DRAWER IS ALREADY OPEN.":GOTO9
4020 IFC$="DRA"ANDA(4,0)=0ANDR=26THENZ$="IT'S OPEN. INSIDE YOU SEE YOUR PIRATE BOOK AND SWORD.":TR(5)=R:TR(6)=R:A(4,0)=1:GOTO9
4030 IFC$="BOX"ANDTR(11)<>200THENZ$="YOU DON'T EVEN HAVE THE BOX.":GOTO9
4040 IFC$="BOX"ANDA(5,0)=0ANDTR(11)=200THENZ$="YOU OPEN IT. YOU FIND A THIEVE'S PICK INSIDE.":TR(12)=R:A(5,0)=1:GOTO9
4050 IFC$="BOX"ANDA(5,0)=1ANDTR(11)=200THENZ$="THE BOX IS ALREADY OPEN.":GOTO9
4060 IFC$="CHE"ANDR<>23THENZ$="I DON'T SEE THE CHEST HERE.":GOTO9
4070 IFC$="CHE"ANDR=23ANDA(20,0)=0THENZ$="YOU MUST UNLOCK THE CHEST.":GOTO9
4080 IFC$="CHE"ANDR=23ANDA(20,0)=1THENZ$="THE CHEST IS ALREADY OPEN.":GOTO9
4090 IFC$="DOO"ANDR<>26ANDR<>29THENZ$="THERE IS NO DOOR HERE.":GOTO9
4100 IFC$="DOO"ANDR=26ANDA(11,0)=0THENZ$="THE DOOR SWINGS OPEN REVEALING A PASSAGE EAST.":A(26,3)=27:A(27,4)=26:A(11,0)=1:GOTO9
4110 IFC$="DOO"ANDR=26ANDA(11,0)=1THENZ$="THE DOOR IS ALREADY OPEN.":GOTO9
4120 IFC$="DOO"ANDR=29THENZ$="THE DOOR TO THE CPATAIN'S QUARTERS IS LOCKED.":GOTO9
4130 RETURN
4135 Z$="YOU DON'T NEED TO UNLOCK THAT!":GOTO9
4500 IFC$<>"DOO"ANDC$<>"CHE"ANDC$<>"GRA"ANDC$<>"BOX"THENZ$="I CAN'T UNLOCK THAT!":GOTO9
4520 IFC$="DOO"THEN4135
4530 IFC$="BOX"THEN4135
4540 INPUT"WITH WHAT";F$:F$=LEFT$(F$,3)
4550 IFC$="CHE"THEN4600
4560 IFF$="KEY"THENZ$="THE KEY DOESN'T FIT THE GRATE.":GOTO9
4570 IFF$<>"PIC"THENZ$="YOU CAN'T USE THAT TO UNLOCK THE GRATE!":GOTO9
4580 IFA(19,0)=1THENZ$="THE GRATE IS ALREADY UNLOCKED!":GOTO9
4590 Z$="THE GRATE IS NOW UNLOCKED AND OPEN! IT REVEALS A PASSAGE DOWNWARD.":A(21,6)=23:A(23,5)=21:A(19,0)=1:GOTO9
4600 IFF$="PIC"THENZ$="THE PICK DOESN'T FIT THE CHEST'S LOCK.":GOTO9
4610 IFF$<>"KEY"THENZ$="YOU CAN'T USE THAT TO UNLOCK THE CHEST!":GOTO9
4620 IFA(20,0)=1THENZ$="THE CHEST IS ALREADY UNLOCKED!":GOTO9
4630 Z$="THE CHEST IS NOW UNLOCKED AND OPEN! IT REVEALS A LARGE GOLD SPANISH COIN!":A(20,0)=1:TR(9)=23:GOTO9
5000 INPUT"WITH WHAT?";F$:F$=LEFT$(F$,3)
5020 IFC$<>"GAR"THENZ$="YOU CAN'T BUY THAT!":GOTO9
5030 IFA(17,0)=1THENZ$="YOU'VE ALREADY BOUGHT THE CLOVE OF GARLIC.":GOTO9
5040 IFR<>40ANDTR(4)<>200THENZ$="I DON'T SEE THE CLOVE OF GARLIC HERE.":GOTO9
5050 IFF$<>"BAG"ANDF$<>"SIL"THENZ$="'FOOL! YOU'D THINK I'D SETTLE FOR LESS THAN SILVER!' HE DECAPITATES YOU!":GOSUB1:GOTO2000
5060 Z$="THE COOK THINKS IT OVER THEN SAYS, 'WELL I GUESS IT'S A GOOD DEAL SO HERE.'":GOSUB1
5061 Z$="THEN THE COOKS HANDS YOU THE CLOVE AND TAKES YOUR BAG OF SILVER.":TR(4)=200:TR(14)=255:A(17,0)=1:GOTO9


6000 IFC$<>"MAS"ANDC$<>"LAD"THENZ$="YOU CAN'T CLIMB THAT!":GOTO9
6020 IFC$="MAS"ANDR=33THENZ$="YOU CLIMB UP THE MAST... SOON YOU ARE IN THE CROW'S NEST.":GOSUB1:R=35:GOTO70
6030 IFC$="MAS"ANDR=32THENZ$="YOU CLIMB UP THE MAST... SOON YOU ARE IN THE CROW'S NEST.":GOSUB1:R=34:GOTO70
6040 IFC$="MAS"THENZ$="I CAN'T SEE A MAST HERE.":GOTO9
6050 IFC$="LAD"ANDR=39THENZ$="IF YOU WANT TO GO DOWN THE LADDER JUST SAY SO.":GOTO9
6060 IFC$="LAD"THENZ$="I DON'T SEE A LADDER HERE.":GOTO9

6500 INPUT"WHAT DO YOU WANT TO KNOCK ON";F$:F$=LEFT$(F$,3)
6520 IFF$<>"DOO"THENZ$="AWESOME!!!":GOTO9
6530 IFR<>29ANDR<>26ANDR<>27THENZ$="I DON'T SEE A DOOR HERE.":GOTO9
6540 IFR=29ANDA(12,0)=0THENZ$="YOU KNOCK AND WAIT. SOON THE CAPTAIN COMES AND USHERS YOU IN.":GOSUB1:R=25:A(12,0)=1:GOTO70
6545 IFR=29ANDA(12,0)=1THENZ$="THERE IS NO ANSWER.":GOTO9
6550 Z$="OTHING HAPPENS.":GOTO9

7000 IFC$<>"BOO"ANDC$<>"NOT"THENZ$="YOU CAN'T READ THAT!":GOTO9
7015 IFC$="NOT"THEN7190
7020 IFTR(5)<>200THENZ$="YOU AREN'T EVEN CARRYING THE BOOK!":GOTO9
7060 GOSUB8:Z$="THIS IS JULIUS THE PIRATE'S BOOK ON PIRATES. INCLUDED ARE MANY ENTRIES ON OTHER PIRATES, "
7061 Z$=Z$+"SHIPS AND TREASURES.":GOSUB1
7080 Z$="YOU SKIM THROUGH THE BOOK UNTIL YOU FIND A LARGE ENTRY ON AN ANCIENT SPANISH COIN WORTH THOUSANDS OF DOLLAR":GOSUB1
7100 Z$="AND LOCATED ON AN ISLAND CLOSE BY HERE. THE LATTITUDE IS 45 DEGREES AND THE LONGITUDE IS 24 DEGREES.":GOSUB1
7120 GOSUB8:Z$="YOU NOTICE THAT AT THE BOTTOM OF THE LAST PAGE THAT THEREIS SOME TEXT READING:":GOSUB1:GOSUB8
7160 Z$="FOR OTHER REFRENCES ON JULIUS THE PIRATE, LOOK UNDER JULIUS IN A CARD CATALOG FOUND IN EVERY SHIP LIBRARY.":GOTO9

7190 IFR<>42THENGOSUB8:Z$="YOU CAN'T SEE ANY NOTE HERE.":GOTO9
7200 GOSUB8:Z$="TO FIND A BOOK IN THE MANY BOOKS IN OUR WONDOUROUS LIBRARY, "
7220 Z$=Z$+"LOOK UNDER THE SUBJECT AND THEN GO FIND THE BOOK.":GOSUB1
7230 Z$="I HOPE THE BOOKS WILL ENTERTAIN YOU AND WIDEN YOUR HORIZONS AND "
7240 Z$=Z$+"BE SURE TO PUT THE BOOKS BACK, OR ELSE!":GOSUB1:GOSUB8:Z$=" -- JONNY THE CAPTAIN":GOTO9

7250 Z$="YOU CAN'T FIND INFORMATION ON THAT HERE. TRY THE SHIP'S LIBRARY OPEN 24 HOURS A DAY!":GOTO9
8000 IFR<>42THEN7250
8010 GOSUB8:Z$="YOU LOOK THROUGH THE CARD CATALOG, FIND A BOOK ON JULIUS "
8040 Z$=Z$+"THE PIRATE, GO GET THE BOOK ALONG THE RACKS, AND FIND THIS ENTRY:":GOSUB1:GOSUB8
8060 Z$="JULIUS THE PIRATE WAS A GREAT PIRATE, KNOW FOR HIS DARING "
8070 Z$=Z$+"ADVENTURES AND FINDING RARE AND VALUABLE TREASURES.":GOSUB1
8080 Z$="HIS MOST FAMOUS TREASURE FIND WAS THE SPANISH GOLD COIN, AN ORIGINAL COIN SINCE ONLY ONE WAS MADE AND "
8090 Z$=Z$+IT WAS MINTED IN 1635.":GOSUB1:Z$="JULIUS DIED BEFORE HE COULD COLLECT THE COIN BUT "
8110 Z$=Z$+"LEFT THE DEGREES OF THE ISLAND THAT THE COIN IS ON IN HIS FAMILY'S PIRATE BOOK.":GOSUB1
8130 Z$="THE LAST KNOWN RECORDED INHERITERED OF THE BOOK WAS HUMPHRY GOODFELLOW, WHO DIED IN A FIRE AT HIS HOUSE.":GOSUB1
8132 Z$="THE WHEREABOUTS OF THE BOOK IS UNKNOWN.":GOSUB1:GOSUB8
8133 Z$="YOU READ ON TILL YOU FIND THE LAST PARAGRAPH, WHICH YOU ARE SHOCKED BY:":GOSUB1:GOSUB8
8135 Z$="JULIUS THE PIRATE WAS A GREAT PIRATE, BUT HE HAD A PROBLEM WITH HIS LEFT EYE, "
8136 Z$=Z$+"WHICH MADE HIM PUT MARKS 5 DEGREES WEST OF THE ORIGINAL ON LONGITUDE AND LATITUDE MAPS.":GOTO9
8150 IFR<>42THEN7250
8160 GOSUB8:Z$="PIRATE ISLAND IS ONE OF THE MOST SCARIEST ISLANDS IN THE WHOLE OCEAN.":GOSUB1
8170 Z$="THE NAME OF THE ISLAND HAS SENT CHILLS DOWN THE SPINES OF MANY YOUNG SEA-GOING SAILORS.":GOSUB1
8200 Z$="THE BRAVEST CAPTAINS HAVE STEERED AWAY FROM THE ISLAND JUST TO STAY "
8210 Z$=Z$+"OUT OF THE HORRORS THAT SUPERSTISION HAVE PROVIDED.":GOSUB1
8220 Z$="LEGENDS HAVE IT THAT THE VALUABLE SPANISH GOLD COIN RESTS "
8230 Z$=Z$+"ON PIRATE ISLAND BUT TO DATE, NO ONE HAS EVEN TRIED TO FIND THE COIN":GOSUB1
8240 Z$="BUT THEN AGAIN, WE DON'T RECORD ALL VOYAGES!":GOTO9

8300 IFR<>42THEN7250
8310 GOSUB8:Z$="DAVID MENY IS ONE OF THE GREATEST PIRATES ON THE SEVEN SEAS!":GOSUB1
8330 Z$="HE HAS WRITTEN HIS WAY OUT OF A JAIL CELL AND FOUND THE SPANISH "
8340 Z$=Z$+"SILVER COIN, SECOND IN VALUE TO ONLY THE FAMOUS GOLD COIN MADE BY THE SPAINARDS.":GOSUB1
8350 Z$="HE CONTINUES TO ROAM THE FREE WORLD IN SEARCH OF ADVENTURE AND INTRIGUE.":GOTO9

8500 IFR<>11ANDR<>44ANDR<>45ANDR<>22THENZ$="YOU CAN'T SWIM HERE!":GOTO9
8520 IFR=44ORR=45THENZ$="YOU SWIM AROUND FOR A WHILE THEN END UP WHERE YOU ARE NOW.":GOTO9
8530 IFR=22THENZ$="THE WATER IS TO SHALLOW TO SWIM HERE.":GOTO9
8540 Z$="YOU SWIM OUT INTO THE BAY...":GOSUB1
8550 R=44:GOTO70
8990 END

9000 Z$="YOU HAVE DIED.":GOSUB1
9020 INPUT "WOULD YOU LIKE TO START OVER";F$
9030 F$=LEFT$(F$,1)
9040 IFF$="Y"THENRUN
9050 Z$="OK. SEE YOU NEXT TIME ON PIRATE ISLAND.":GOSUB1
9080 FOR I=1 TO 100:NEXT I:CLS:END

14000 A(25,0)=0:JK=0:FORI=1TO14
14030 IFTR(I)=RTHENJK=I
14031 IFJK<>ITHEN14040
14035 IFA(25,0)=0THENZ$=O2$(JK):GOSUB1:GOTO14037
14036 Z$=O2$(JK):GOSUB1
14037 A(25,0)=1:JK=I
14040 NEXT
14050 RETURN
15000 IFR>39THEN15090
15067 GOSUB8:IFR<11THENONRGOSUB15110,15150,15150,15175,15210,15240,15280,15320,15210,15210:GOTO15071
15068 IFR<21THENONR-10GOSUB15360,15400,15210,15450,15490,15520,15560,15450,15450,15600:GOTO15071
15069 IFR<31THENONR-20GOSUB15640,15680,15720,15760,15800,15850,15890,15920,15950,16000:GOTO15071
15070 IFR<41THENONR-30GOSUB16040,16080,16120,16170,16170,16190,16230,16270,16320
15071 IFR<>25THEN15100
15072 GOSUB8:Z$="THE CAPTAIN LOOKS AT YOU THE SAYS, 'WELL WHAT'S THEN LONGITUDE OF PIRATE ISLAND?":INPUTF1
15073 INPUT"'OK, NOW WHAT'S THE LATITUDE'";F2
15074 Z$="'YOU BETTER BE RIGHT OR YOU'LL REGRET YOU EVER WOKE UP THIS MORNING! NOW LEAVE.'":GOSUB1
15075 IFF1=F4ANDF2=F3THENA(24,0)=1:GOTO16076
15075 A(24,0)=0
15076 ST=1:R=29:GOTO70
15090 ZZ=R-39:ONZZGOSUB16360,16410,16440,16480,16510,16550
15100 Z$=LEFT$("SCORE:"+STR$(S)+"   "+R$(R)+"                          ",32):C1=MC-1:GOSUB11:RETURN
15110 Z$="YOU ARE ON THE TOP OF A HIGH CLIFF OVERLOOKING THE BLUE WATERS OF THE OCEAN. A "
15130 Z$=Z$+"PATH LEADS DOWN. THE SOIL IS DAMP HERE.":GOTO9
15150 Z$="YOU ARE IN A LARGE RAIN FOREST.":GOTO9
15175 Z$="YOU ARE ON A SMALL PATH LEADING OUT OF A LARGE RAIN FOREST TO THE WEST. IN THE "
15190 Z$=Z$+"CENTER OF THE PATH THERE IS A LARGE TREE.":GOTO9
15210 Z$="THIS IS A THICK FOREST COVERED BY MANY TREES, ALL OF WHICH ARE UNCLIMBABLE.":GOTO9
15240 Z$="YOU ARE STANDING BELOW A LARGE CLIFF. A PATH LEADS UP TO THE CLIFF WHILE AN "
15260 Z$=Z$+"EAST/WEST PATH HEADS TOWARDS A COVE AND A MEADOW.":GOTO9
15280 Z$="YOU ARE NEAR A LARGE ROCK WHICH HAS PROBABLY STOOD HERE FOR MANY HUNDREDS OF "
15300 Z$=Z$+"YEARS. A SMALL PATH LEADS SOUTH.":GOTO9
15320 Z$="YOU ARE IN A LARGE TREE ABOVE A SMALL PATH. THE MANY LEAVES AND BRANCHES "
15340 Z$=Z$+"PREVENT YOU FROM GOING ANY FURTHER UP, WHILE A LARGE STUMP LEADS DOWN.":GOTO9
15360 Z$="YOU ARE STANDING ON THE SHORE OF AN INLAND COVE. THE CLEAR, BLUE WATERS OF "
15380 Z$=Z$+"THE OCEAN ARE OF TO THE EAST, WHILE A MURKY SMELL COMES FROM THE SOUTH. THE SOIL IS DAMP HERE.":GOTO9
15400 Z$="YOU ARE STANDING IN A LARGE MEADOW WHERE BIRDS SING AND DEER PLAY. THE TALL, "
15420 Z$=Z$+"LUSH GRASS BENEATH YOU IS STAMPED OUT, FORMING A PATH TO THE NORTH, SOUTH AND WEST.":GOTO9
15450 Z$="THIS IS A MURKY, DISMAL SWAMP WHERE HIDEOUS CREATURES HIDE WHEN THEY ARE NOT ON "
15470 Z$=Z$+"THE HUNT. A PATH LEADS TO THE WEST.":GOTO9
15490 Z$="YOU ARE STANDING, OR RATHER WERE STANDING, IN A LARGE POND OF QUICK SAND. "
15500 Z$=Z$+"YOU ARE SLOWLY... SINKING...":GOSUB1:GOTO9000
15520 Z$="YOU ARE WALKING ALONG A BEACH PATH WHICH SURROUNDS THE SOUTHWEST PART OF THE "
15540 Z$=Z$+"ISLAND. TO THE EAST YOU CAN SEE A BEACH, WHILE TO THE WEST COMES A MURKY SMELL.":GOTO9
15560 Z$="THIS IS A SANDY BEACHY COMING OUT OF THE WATER. PALM TRESS LINE THE SHORE AND "
15580 Z$=Z$+"A PATH LEADS TO THE WEST AND TO THE EAST. ANOTHER PATH HEADS NORTH.":GOTO9
15600 Z$="YOU ARE STANDING IN A DARK CAVE LEADING OUT OF A SWAMP. FROM THE EAST COMES "
15620 Z$=Z$+"SQUEAKY NOISES (MAYBE YOU AREN'T THE ONLY ONE ON THIS ISLAND).":GOTO9
15640 Z$="YOU ARE IN A CAVE WITH MANY BATS. THE GROUND IS DAMP HERE. "
15641 IFTR(4)<>200THENZ$=Z$+"THE BATS SEE YOU AND ATTACK. YOU HAVE MANY BAT BITES AND SLOWLY... DIE...":GOSUB1:GOTO9000
15660 Z$=Z$+"THE BATS, SEEING YOUR GARLIC, STAY FAR BACK.":GOTO9
15680 Z$="YOU ARE IN A SHALLOW COVE, SO SHALLOW THAT YOU CAN STAND UP. TO THE NORTH THERE "
15700 Z$=Z$+"IS A SANDY BEACH, WHILE TO ALL OTHER DIRECTIONS THERE IS DEEP WATER.":A(16,0)=1:GOTO9
15720 Z$="YOU ARE IN A SECRET CAVE BELOW A GRATE. FROM UP ABOVE YOU CAN HEAR SQUEAKY NOISES.":GOTO9
15760 Z$="THIS IS THE WHEELHOUSE OF THE SHIP WHERE THE SHIP IS DIRECTED. A NAVIGATOR IS "
15780 Z$=Z$+"HERE DIRECTING THE WHEEL OVER THE BLUE WATER OF THE OCEAN.":GOTO9
15800 Z$="YOU ARE IN THE CAPTAIN'S QUARTERS, A LARGE ROOM WITH SEA EQUIPMENT INCLUDING A COMPASS AND AN OLD "
15810 Z$=Z$+"RELIC MADE OUT OF GOLD. THERE IS A LARGE WOODEN DESK HERE CONTAING SECRET INFORMATION.":GOTO9
15850 Z$="YOU ARE IN YOUR CABIN, A SMALL ROOM WITH MINOR FURNISHINGS-- "
15870 Z$=Z$+"A BED & A DRESSER WITH ONE DRAWER. A WOODEN DOOR IS EAST.":GOTO9
15890 Z$="YOU ARE IN THE NORTH END OF A HALLWAY RUNNING NORTH TO SOUTH.":GOTO9
15920 Z$="YOU ARE ON THE PORT SECTION OF THE SHIP'S DECK. TO THE NORTH THERE IS THE "
15940 Z$=Z$+"SHIP'S WHEELHOUSE. ALSO THE MID DECK IS TO THE SOUTH.":GOTO9
15950 Z$="YOU ARE ON THE SHIP'S PORT DECK. WEST IS ANOTHER PORTDECK AND SOUTH, THE MID DECK. TO THE EAST THERE IS "
15960 Z$=Z$+"A PASSAGEWAY LEADING TO THE WHEELHOUSE AND TO THE NORTH A LARGE DOOR LEADS TO THE CAPTAIN'S QUARTERS.":GOTO9
16000 Z$="YOU ARE IN THE CREW'S CABIN PORTION OF THE SHIP. THERE ARE MANY BUNKS AND GEAR "
16020 Z$="HERE THAT BELONG TO OTHER PEOPLE, SO DON'T TOUCH IT.":GOTO9
16040 Z$="THIS IS A HALLWAY RUNNING NORTH TO SOUTH. TO THE WEST IS THE CREW'S CABIN WHILE "
16060 Z$=Z$+"UP THERE IS THE MID DECK.":GOTO9
16080 Z$="YOU ARE ON THE SHIP'S MID DECK. ANOTHER MID DECK IS OFF TO THE EAST WHILE THE SHIP'S PORT AND "
16090 Z$=Z$+"STARBOARD DECKS ARE NORTH AND SOUTH. A LARGE MAST IS HERE WITH A CROW'S NEST ON TOP OF IT.":GOTO9
16120 Z$="YOU ARE ON ONE OF TWO MID DECKS. A LADDER LEADS BELOW. PORT AND STARBOARD ARE NORTH AND SOUTH WHILE"
16150 Z$=Z$+"A LARGE MAST IS ABOVE YOU TOPPED WITH A CROW'S NEST.":GOTO9
16170 Z$="YOU ARE IN A CROW'S NEST, A LOOKOUT SITE FOR LAND, STORMS AND OTHER SHIPS.":GOTO9
16190 Z$="YOU ARE IN THE SHIP'S MESS HALL WHERE EVERY DAY HUNGRY CREWMEN ARE FED. TO THE "
16210 Z$=Z$+"SOUTH IS THE GALLEY AND TO THE EAST IS A HALLWAY.":GOTO9
16230 Z$="YOU ARE IN A HALLWAY RUNNING NORTH TO SOUTH. TO THE WEST IS THE SHIP'S MESS HALL.":GOTO9
16270 Z$="YOU ARE ON THE STARBOARD DECK OF THE SHIP. TO THE EAST THERE IS ANOTHER "
16290 Z$=Z$+"STARBOARD DECK, WHILE TO THE NORTH THERE IS A MID DECK. SOUTH THERE IS THE SHIP'S LIBRARY.":GOTO9
16320 Z$="THIS IS THE SHIP'S STARBOARD DECK. THERE IS A LADDER HERE, LEADING DOWN TO A "
16340 Z$="HALLWAY AND A DECK TO THE NORTH AND TO THE WEST.":GOTO9
16341 IFR<>39THEN15100
16342 IF A(16,0)<>1 THEN 15100
16343 GOSUB8
16344 IFR=39ANDA(16,0)=1ANDTR(9)=200THENGOSUB80:Z$="'YOU FOUND THE COIN! NOW WE WILL ALL BE RICH!'":GOSUB1:S=S+55:GOTO2000
16345 IFR=39ANDA(16,0)=1ANDTR(9)<>200THENGOSUB80:Z$="'WHAT? YOU DIDN'T FIND THE COIN! I'LL KILL YOU FOR THAT!'":GOSUB1
16346 Z$="THE CAPTAIN PULLS OUT HIS SWORD AND KILLS YOU!":GOSUB1:GOTO9000
16360 Z$="THIS IS THE SHIP'S GALLEY WHERE FOOD IS PREPARED. A LARGE STOVE IS AGAINST THE SOUTH "
16380 Z$=Z$+"WALL AND MANY INGREDIENTS AWAITING TO BE PREPARED IN THE STEW THE COOK IS MAKING.":GOTO9
16410 Z$="YOU ARE IN THE SOUTH END OF A HALLWAY RUNNING NORTH TO SOUTH.":GOTO9
16440 Z$="YOU ARE IN THE SHIP'S LIBRARY WITH MANY BOOKS LINING THE WALL. A CARD CATALOG IS "
16460 Z$=Z$+"HERE AND A CARD CATALOG REFERENCE NOTE IS PINNED ON TOP OF THE CARD CATALOG.":GOTO9
16480 Z$="YOU ARE UNDER WATER. YOU ARE SINKING IN THE SOFT SAND...":GOSUB1:GOTO9000
16510 Z$="YOU ARE SWIMMING ON THE TOP OF THE WATER IN AN INLAND COVE. OFF TO THE EAST IS "
16530 Z$=Z$+"THE SHORE OF THE COVE.":GOTO9
16550 Z$="YOU ARE ON THE BOTTOM OF A COVE. THE SAND HERE IS STURDY, THUS YOU DON'T SINK.":GOTO9
19990 END

REM INITIALIZE
20000 R=26:S=0
20015 F3=45:F4=29
20020 FOR I=1 TO 45
20030 FOR J=1 TO 6
20040 READ A(I,J)
20050 NEXT J
20060 NEXT I
20070 FOR I=1 TO 14
20080 READ O1$(I),O2$(I),TR(I),IN$(I)
20090 NEXT I
20100 FOR I=1 TO 45
20110 READ R$(I)
20120 NEXT I
20130 RETURN
25000 REM DATA STATEMENTS
25010 DATA 0,0,0,0,0,6
25020 DATA 2,2,3,2,0,0
25030 DATA 2,3,4,0,6,0
25040 DATA 0,0,0,3,8,0
25050 DATA 0,5,10,9,0,0
25060 DATA 0,0,12,11,1,3
25070 DATA 0,17,0,0,0,0
25080 DATA 0,0,0,0,0,4
25090 DATA 5,10,13,9,0,0
25100 DATA 5,19,10,9,0,0
25110 DATA 6,15,12,0,0,0
25120 DATA 6,16,0,11,0,0
25130 DATA 9,0,0,18,0,0
25140 DATA 0,19,14,20,0,0
25150 DATA 0,0,0,0,0,0
25160 DATA 12,0,17,15,0,0
25170 DATA 7,22,18,16,0,0
25180 DATA 13,0,0,17,0,0
25190 DATA 14,18,19,10,0,0
25200 DATA 0,0,21,14,0,0
25210 DATA 0,0,0,20,0,0
25220 DATA 17,39,43,43,43,43
25230 DATA 0,0,0,0,21,0
25240 DATA 0,0,29,28,0,0
25250 DATA 0,29,0,0,0,0
25260 DATA 0,0,0,0,0,0
25270 DATA 0,31,0,0,0,0
25280 DATA 24,32,29,0,0,0
25290 DATA 0,33,24,28,0,0
25300 DATA 0,0,31,0,0,0
25310 DATA 27,37,0,30,33,0
25320 DATA 28,38,33,0,0,0
25330 DATA 29,39,0,32,0,31
25340 DATA 0,0,0,0,0,32
25350 DATA 0,0,0,0,0,33
25360 DATA 0,40,37,0,0,0
25370 DATA 31,41,0,36,39,0
25380 DATA 32,42,39,0,0,0
25390 DATA 33,0,0,38,0,37
25400 DATA 36,0,0,0,0,0
25410 DATA 37,0,0,0,0,0
25420 DATA 38,0,0,0,0,0
25430 DATA 0,0,0,0,0,0
25431 DATA 0,0,11,0,0,45
25432 DATA 0,0,0,0,44,0
25440 DATA "SHO","THERE IS AN OLD SHOVEL HERE.",35,"A SHOVEL"
25450 DATA "CAP","THE CAPTAIN OF THE SHIP IS HERE.",25,""
25460 DATA "KEY","THERE IS A SHINY KEY LAYING NEAR YOU.",8,"A KEY"
25470 DATA "GAR","THERE IS A CLOVE OF GARLIC NEAR YOU.",40,"A CLOVE OF GARLIC"
25480 DATA "BOO","THERE IS A BOUNDED BOOK HERE.",255,"A BOOK"
25490 DATA "SWO","A SHARP SWORD IS HERE.",255,"A SHARP SWORD"
25500 DATA "CHE","THERE IS AN OLD PIRATE CHEST HERE.",23,""
25510 DATA "COO","THE SHIP'S COOK IS HERE, COOKING A STEW.",40,""
25520 DATA "COI","THERE IS A SPANISH COIN HERE.",255,"A GOLD COIN"
25530 DATA "TRA","THERE IS A TRAY HERE FULL OF DISCARDED FOOD.",36,"A TRAY"
25531 DATA "BOX","NEAR YOU, THERE IS SMALL BOX.",45,"A BOX"
25532 DATA "PIC","THERE IS A THIEVE'S PICK HERE.",255,"A THIEVE'S PICK"
25533 DATA "GRA","THERE IS A METAL GRATE HERE.",255,""
25534 DATA "BAG","A BAG OF SILVER COINS IS HERE.",255,"A BAG OF SILVER COINS"
25535 DATA HIGH CLIFF,RAIN FOREST,RAIN FOREST,SMALL PATH,THICK FOREST,NEAR CLIFF
25540 DATA ROCK,UP A TREE,THICK FOREST,THICK FOREST,INLAND COVE,MEADOW
25550 DATA THICK FOREST,SWAMP,QUICKSAND,BEACH PATH,SANDY BEACH,SWAMP,SWAMP
25560 DATA DARK CAVE,BAT CAVE,SHALLOW COVE,SECRET CAVE,WHEELHOUSE
25570 DATA CAPTAIN'S QUARTERS,YOUR CABIN,HALLWAY,PORT DECK,PORT DECK
25580 DATA CREW'S CABIN,HALLWAY,MID DECK,MID DECK,CROW'S NEST,CROW'S NEST
25590 DATA MESS HALL,HALLWAY,STARBOARD DECK,STARBOARD DECK,GALLEY,HALLWAY
25600 DATA SHIP'S LIBRARY,UNDER WATER,TOP OF THE BAY,BOTTOM OF THE BAY,HALLWAY,STARBOARD DEC
