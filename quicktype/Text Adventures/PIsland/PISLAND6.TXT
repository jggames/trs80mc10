0 CLS:CLEAR400:DIMCC,ZZ,Z$,I$,C1,A(45,6),O$(14),D$(14),I$(14):GOTO16
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1)
2 IFI$<"!"THENPRINTMID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
3 C1=((PEEK(17024)AND1)*256+PEEK(17025))+1:IFC1>479THENGOSUB5
4 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 PRINT@480,"hit any key";:POKE16867,32:POKE16871,32
6 IFINKEY$=""THEN6
7 CLS:RETURN
9 GOSUB1:RETURN
10 Z$="OK":GOSUB1:RETURN
11 INPUT"WITH WHAT";F$:F$=LEFT$(F$,3):RETURN
12 GOSUB252:GOSUB244:RETURN
14 Z$="YOU CAN'T GO THAT WAY.":GOTO9
15 GOSUB92:GOTO22
16 PRINT"  pirate island BY DAVID MENY",,,:POKE16392,32
17 R=26:FORI=1TO45:FORJ=1TO6:READA(I,J):NEXTJ,I:FORI=1TO14:READO$(I),D$(I),A(I,0),I$(I):NEXT
19 Z$="YOU DREAM OF VALIANT PIRATES WHO SAIL THE SEAS SEEKING TREASURE HIDDEN ON DANGEROUS ISLANDS...":GOSUB1
20 Z$="OF FIGHTS TO THE DEATH OVER CHESTS FILLED WITH JEWELS AND GOLD...":GOSUB1
21 Z$="YOU DREAM UNTIL THE ROCKING OF THE OCEAN WAKENS YOU ON BOARD THE 'THE EAGLE'S NEST'-- A PIRATE SHIP!":GOSUB1
22 GOSUB12
23 IFA(15,0)=1THENA(21,0)=A(21,0)+1:IFA(21,0)=10THENA(2,0)=R
24 IFA(15,0)<>1ORA(21,0)<>10THEN31
25 Z$="THE CAPTAIN WALKS UP TO YOU.":GOSUB1:IFA(24,0)=1THEN28
26 Z$="'FOOL! WE'VE BEEN SAILING FOR DAYS AND HAVEN'T FOUND PIRATE ISLAND. I SHOULDN'T HAVE TRUSTED YOU, "
27 Z$=Z$+"EVEN THOUGH YOU ARE MY GRAND SON.' THE CAPTAIN PULLS OUT HIS CUTLASS AND DECAPITATES YOU.":GOSUB1:GOTO79
28 Z$="'CONGRATULATIONS! WE'VE FOUND PIRATE ISLAND. NOW GO ASHORE BY THE EAST STARBOARD DECK.' "
29 Z$=Z$+"THE CAPTAIN HANDS YOU A BAG OF SILVER. 'SPEND IT WISELY. IT'S YOUR PAY.'":A(2,0)=25:A(39,2)=22:A(23,0)=1:A(14,0)=200
30 GOSUB1
31 INPUTA$:IFA$=""THENZ$="WHAT?":GOSUB1:GOTO23
33 Z$=LEFT$(A$,14):IFZ$="LOOK UNDER JUL"THENGOSUB206:GOTO23
34 IFZ$="LOOK UNDER PIR"THENGOSUB219:GOTO23
35 IFZ$="LOOK UNDER DAV"THENGOSUB417:GOTO23
36 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO36
37 B$=LEFT$(A$,3):IFB$="INV"ORB$="I  "THENGOSUB68:GOTO23
39 CC=0:FORZZ=1TO7:IFB$=MID$("  SCODIGKNOSWIQUILOASAV",ZZ*3,3)THENCC=ZZ
40 NEXT:IFCCTHENONCCGOSUB71,80,185,232,79,415,416:GOTO23
44 IFB$="LOO"ORB$="L  "THEN22
45 CC=0:FORZZ=1TO6:IFB$=MID$("  N  S  E  W  U  D  ",ZZ*3,3)THENCC=ZZ
46 NEXT:IFCCTHENC$=B$:GOTO15
51 N=1
52 IFMID$(A$,N,1)=" "THEN54
53 GOTO56
54 C$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN58
55 GOTO57
56 IFN<MTHENN=N+1:GOTO52
57 GOTO66
58 IFB$="GO "THEN15
59 CC=0:FORZZ=1TO9:IFB$=MID$("  GETTAKDROPUTOPEUNLBUYCLIREA",ZZ*3,3)THENCC=ZZ
60 NEXT:IFCCTHENONCCZGOSUB109,109,134,134,142,159,172,179,191:GOTO23
66 Z$="I DON'T KNOW THAT WORD.":GOSUB1:GOTO23
68 Z$="YOU ARE CARRYING:":GOSUB1:J=0:FORI=1TO14:IFA(I,0)=200THENZ$="  "+I$(I):GOSUB1:J=1
69 NEXT:IFJ=0THENZ$="  NOTHING":GOSUB1
70 RETURN
71 J=A(22,0):Z$="OUT OF 200, YOU GOT"+STR$(J)+" POINTS. THAT GIVES YOU THE RATING OF "
74 IFJ<50THENZ$=Z$+"DECK BOY.":GOSUB1
75 IFJ>=50ANDJ<100THENZ$=Z$+"SEA MAN.":GOSUB1
76 IFJ>=100ANDJ<150THENZ$=Z$+"BUCANEER.":GOSUB1
77 IFJ>=150THENZ$=Z$+" SEA CAPTAIN.":GOSUB1
78 RETURN
79 GOSUB71:GOTO239
80 GOSUB11:IFF$<>"SHO"THENZ$="YOU CAN'T USE THAT TO DIG WITH.":GOTO9
82 IFR<>1ANDR<>11ANDR<>21THEN84
83 GOTO86
84 IFR>23THENZ$="IF YOU DIG ON THE SHIP, YOU MIGHT CAUSE A LEAK.":GOTO9
85 Z$="THE GROUND IS TOO HARD HERE.":GOTO9
86 IFR=1THEN88
87 GOTO90
88 Z$="YOU DIG INTO THE GROUND... YOU HIT SOMETHING! IT'S A COFFIN! WHEN YOU OPEN IT, A SKELETON POPS OUT, KNIFE FIRST, "
89 Z$=Z$+"STABBING YOU! IT WAS A BOOBEYTRAP!":GOSUB1:GOTO79
90 IFR=11THENZ$="YOU DIG BUT YOU DON'T FIND ANYTHING.":GOTO9
91 Z$="YOU DIG IN THE DAMP SOIL AND SOON YOU COME UPON A GRATE!":A(13,0)=21:GOTO9
92 C$=LEFT$(C$,1):IFC$="S"ANDR=39ANDA(23,0)=0THENZ$="ARE YOU SURE YOU WANT TO DIVE INTO THE WATER?":GOTO9
94 CC=0:FORZZ=1TO6:IFC$=MID$("NSEWUD",ZZ,1)ANDA(R,ZZ)=0THENCC=1
95 NEXT:IFCCTHEN14
100 CC=0:FORZZ=1TO6:IFC$=MID$("NSEWUD",ZZ,1)THENR=A(R,ZZ)
101 NEXT:IFR=22ANDA(32,0)=0THENA(22,0)=A(22,0)+20:A(32,0)=1
107 IFR=23ANDA(33,0)=0THENA(22,0)=A(22,0)+20:A(33,0)=1
108 RETURN
109 IFC$="CHE"ANDR=23THENZ$="THE CHEST IS TOO HEAVY FOR YOU TO CARRY.":GOTO9
110 IFC$="CAP"ANDR=25THENZ$="THE CAPTAIN MOVES JUST AS YOU TRY TO LIFT HIM. 'HEY, ARE YOU GETTING FRESH MATEY!?'":GOTO9
111 IFC$="COO"ANDR=40THENZ$="THE COOK SLAPS YOUR HAND. 'NOT NOW, SOMEONE MIGHT SEE US.'":GOTO9
112 IFC$="STE"ANDR=40THENZ$="THE COOK SLAPS YOUR HAND. 'WAIT TILL IT'S DONE.'":GOTO9
113 IFC$="GAR"ANDR=40THENZ$="'NO ONE MESSES WITH MY STEW MAKIN'S.' THE COOK CUTS YOUR THROAT.":GOSUB1:GOTO79
114 IFC$="GRA"THENZ$="THE GRATE IS SECURED.":GOTO9
115 A(37,0)=0:A(38,0)=0:O=0
116 FORI=1TO14:IFA(I,0)=200THENA(37,0)=A(37,0)+1
118 NEXT:IFA(37,0)>6THENZ$="YOU CAN'T CARRY ANY MORE.":GOTO9
120 FORI=1TO12:IFA(I,0)=RTHENA(38,0)=1
122 NEXT:IFA(38,0)=0THENZ$="THERE IS NOTHING HERE TO PICK UP.":GOTO9
124 FORI=1TO14:IFC$=O$(I)ANDA(I,0)=RTHENO=I
126 NEXT:IFO=0THENZ$="I DON'T SEE THAT HERE.":GOTO9
128 A(O,0)=200:GOSUB10:IFA(1,0)=200ANDA(39,0)=0THENA(22,0)=A(22,0)+10:A(39,0)=1
131 IFA(12,0)=200ANDA(40,0)=0THENA(22,0)=A(22,0)+40:A(40,0)=1
132 IFA(9,0)=200ANDA(18,0)=0THENA(22,0)=A(22,0)+60:A(18,0)=1
133 RETURN
134 O=0:FORI=1TO14:IFC$=O$(I)THENO=I
137 NEXT:IFO=0THENZ$="YOU DON'T HAVE THAT!":GOTO9
139 GOSUB10:A(O,0)=R:RETURN
142 IFC$<>"DRA"ANDC$<>"BOX"ANDC$<>"CHE"ANDC$<>"DOO"THENZ$="YOU CAN'T OPEN THAT.":GOTO9
144 IFC$="DRA"ANDR<>26THENZ$="I DON'T SEE A DRESSER HERE.":GOTO9
145 IFC$="DRA"ANDR=26ANDA(34,0)=1THENZ$="THE DRAWER IS ALREADY OPEN.":GOTO9
146 IFC$="DRA"ANDA(34,0)=0ANDR=26THENZ$="IT'S OPEN. INSIDE YOU SEE A BOOK AND A SWORD.":A(5,0)=R:A(6,0)=R:A(34,0)=1:GOTO9
147 IFC$="BOX"ANDA(11,0)<>200THENZ$="YOU DON'T EVEN HAVE THE BOX.":GOTO9
148 IFC$="BOX"ANDA(35,0)=0ANDA(11,0)=200THENZ$="YOU OPEN IT. YOU FIND A THIEVE'S PICK INSIDE.":A(12,0)=R:A(35,0)=1:GOTO9
149 IFC$="BOX"ANDA(35,0)=1ANDA(11,0)=200THENZ$="THE BOX IS ALREADY OPEN.":GOTO9
150 IFC$="CHE"ANDR<>23THENZ$="I DON'T SEE THE CHEST HERE.":GOTO9
151 IFC$="CHE"ANDR=23ANDA(20,0)=0THENZ$="YOU MUST UNLOCK THE CHEST.":GOTO9
152 IFC$="CHE"ANDR=23ANDA(20,0)=1THENZ$="THE CHEST IS ALREADY OPEN.":GOTO9
153 IFC$="DOO"ANDR<>26ANDR<>29THENZ$="THERE IS NO DOOR HERE.":GOTO9
154 IFC$="DOO"ANDR=26ANDA(41,0)=0THENZ$="THE DOOR SWINGS OPEN REVEALING A PASSAGE EAST.":A(26,3)=27:A(27,4)=26:A(41,0)=1:GOTO9
155 IFC$="DOO"ANDR=26ANDA(41,0)=1THENZ$="THE DOOR IS ALREADY OPEN.":GOTO9
156 IFC$="DOO"ANDR=29THENZ$="THE DOOR TO THE CAPTAIN'S QUARTERS IS LOCKED.":GOTO9
157 RETURN
158 Z$="YOU DON'T NEED TO UNLOCK THAT!":GOTO9
159 IFC$<>"DOO"ANDC$<>"CHE"ANDC$<>"GRA"ANDC$<>"BOX"THENZ$="I CAN'T UNLOCK THAT!":GOTO9
160 IFC$="DOO"THEN158
161 IFC$="BOX"THEN158
162 GOSUB11:IFC$="CHE"THEN168
164 IFF$="KEY"THENZ$="THE KEY DOESN'T FIT THE GRATE.":GOTO9
165 IFF$<>"PIC"THENZ$="YOU CAN'T USE THAT TO UNLOCK THE GRATE!":GOTO9
166 IFA(19,0)=1THENZ$="THE GRATE IS ALREADY UNLOCKED!":GOTO9
167 Z$="THE GRATE IS NOW UNLOCKED AND OPEN! IT REVEALS A PASSAGE DOWN.":A(21,6)=23:A(23,5)=21:A(19,0)=1:GOTO9
168 IFF$="PIC"THENZ$="THE PICK DOESN'T FIT THE CHEST'S LOCK.":GOTO9
169 IFF$<>"KEY"THENZ$="YOU CAN'T USE THAT TO UNLOCK THE CHEST!":GOTO9
170 IFA(20,0)=1THENZ$="THE CHEST IS ALREADY UNLOCKED!":GOTO9
171 Z$="THE CHEST IS NOW UNLOCKED AND OPEN! IT REVEALS A LARGE GOLD SPANISH COIN!":A(20,0)=1:A(9,0)=23:GOTO9
172 GOSUB11:IFC$<>"GAR"THENZ$="YOU CAN'T BUY THAT!":GOTO9
174 IFA(17,0)=1THENZ$="YOU'VE ALREADY BOUGHT THE CLOVE OF GARLIC.":GOTO9
175 IFR<>40ANDA(4,0)<>200THENZ$="I DON'T SEE THE CLOVE OF GARLIC HERE.":GOTO9
176 IF(F$<>"BAG"ANDF$<>"SIL")ORA(14,0)<>200THENZ$="'YOU THINK I'D SETTLE FOR LESS THAN SILVER!' HE GUTS YOU!":GOSUB1:GOTO79
177 Z$="THE COOK THINKS IT OVER THEN SAYS, 'WELL I GUESS IT'S A GOOD DEAL SO HERE.'":GOSUB1
178 Z$="HE HANDS YOU THE CLOVE AND TAKES YOUR BAG OF SILVER.":A(4,0)=200:A(14,0)=255:A(17,0)=1:GOTO9
179 IFC$<>"MAS"ANDC$<>"LAD"THENZ$="YOU CAN'T CLIMB THAT!":GOTO9
180 IFC$="MAS"ANDR=33THENGOSUB414:R=35:GOTO12
181 IFC$="MAS"ANDR=32THENGOSUB414:R=34:GOTO12
182 IFC$="MAS"THENZ$="I DON'T SEE A MAST HERE.":GOTO9
183 IFC$="LAD"ANDR=39THENZ$="IF YOU WANT TO GO DOWN THE LADDER JUST SAY SO.":GOTO9
184 IFC$="LAD"THENZ$="I DON'T SEE A LADDER HERE.":GOTO9
185 Z$="WHAT DO YOU WANT TO KNOCK ON?":GOSUB1:INPUTF$:F$=LEFT$(F$,3):IFF$<>"DOO"THENZ$="AWESOME!":GOTO9
187 IFR<>29ANDR<>26ANDR<>27THENZ$="I DON'T SEE A DOOR HERE.":GOTO9
188 IFR=29ANDA(42,0)=0THENZ$="YOU KNOCK AND WAIT. SOON THE CAPTAIN COMES AND USHERS YOU IN.":GOSUB1:R=25:A(42,0)=1:GOTO12
189 IFR=29ANDA(42,0)=1THENZ$="THERE IS NO ANSWER.":GOTO9
190 Z$="NOTHING HAPPENS.":GOTO9
191 IFC$<>"BOO"ANDC$<>"NOT"THENZ$="YOU CAN'T READ THAT!":GOTO9
192 IFC$="NOT"THEN200
193 IFA(5,0)<>200THENZ$="YOU AREN'T EVEN CARRYING THE BOOK!":GOTO9
194 Z$="THIS IS JULIUS THE PIRATE'S BOOK. INCLUDED ARE MANY ENTRIES ON OTHER PIRATES, SHIPS AND TREASURES.":GOSUB1
196 Z$="YOU SKIM UNTIL YOU FIND AN ENTRY ON AN ANCIENT SPANISH COIN "
197 Z$=Z$+"LOCATED ON AN ISLAND. THE LATITUDE IS 45 AND THE LONGITUDE IS 24.":GOSUB1
198 Z$="YOU NOTICE A MESSAGE ON THE LAST PAGE:":GOSUB1
199 Z$="FOR OTHER REFRENCES ON JULIUS THE PIRATE, LOOK UNDER JULIUS IN A CARD CATALOG.":GOTO9
200 IFR<>42THENZ$="YOU DON'T SEE ANY NOTE HERE.":GOTO9
201 Z$="TO FIND A BOOK, LOOK UNDER THE SUBJECT.":GOSUB1
203 Z$="I HOPE THE BOOKS WILL ENTERTAIN YOU AND WIDEN YOUR HORIZONS. "
204 Z$=Z$+"BUT BE SURE TO PUT THEM BACK!":GOSUB1:Z$=" -- JONNY THE CAPTAIN":GOTO9
205 Z$="YOU CAN'T FIND INFORMATION ON THAT HERE. TRY THE SHIP'S LIBRARY OPEN 24/7!":GOTO9
206 IFR<>42THEN205
207 Z$="YOU LOOK THROUGH THE CATALOG, FIND A REFERENCE TO A BOOK ON JULIUS "
208 Z$=Z$+"THE PIRATE AND RETRIEVE IT. IN IT, YOU FIND THIS ENTRY:":GOSUB1
209 Z$="JULIUS THE PIRATE WAS KNOWN FOR HIS GREAT DARING AND HIS ABILITY TO FIND RARE AND VALUABLE TREASURES.":GOSUB1
211 Z$="HIS MOST FAMOUS TREASURE WAS A ONE-OF-A-KIND SPANISH COIN MINTED IN 1635.":GOSUB1
212 Z$="JULIUS DIED BEFORE HE COULD RETRIEVE IT BUT "
213 Z$=Z$+"LEFT THE ISLAND'S COORDINATES IN HIS FAMILY'S PIRATE BOOK.":GOSUB1
214 Z$="THE LAST KNOWN INHERITER WAS HUMPHRY GOODFELLOW, WHO DIED IN A FIRE. THE WHEREABOUTS OF THE BOOK IS UNKNOWN.":GOSUB1
216 Z$="YOU READ ON UNTIL YOU FIND A PARAGRAPH THAT INTRIGUES YOU:":GOSUB1
217 Z$="JULIUS THE PIRATE HAD A PROBLEM WITH HIS LEFT EYE, "
218 Z$=Z$+"WHICH MADE HIM ALWAYS MARK 5 DEGREES WEST OF ACTUAL CORRDINATES ON MAPS.":GOTO9
219 IFR<>42THEN205
220 Z$="PIRATE ISLAND IS ONE OF THE SCARIEST ISLANDS IN THE WHOLE OCEAN.":GOSUB1
221 Z$="ITS NAME SENDS CHILLS DOWN THE SPINES OF VETERAN SAILORS AND ":GOSUB1
222 Z$="EVEN THE BRAVEST CAPTAINS STEER CLEAR OF IT.":GOSUB1
224 Z$="LEGEND IS THAT A VALUABLE COIN RESTS THERE "
225 Z$=Z$+"BUT TO DATE, NO ONE HAS EVEN TRIED TO FIND IT... BUT THEN AGAIN, NOT ALL VOYAGES ARE RECORDED!":GOTO9
232 IFR<>11ANDR<>44ANDR<>45ANDR<>22THENZ$="YOU CAN'T SWIM HERE!":GOTO9
233 IFR=44ORR=45THENZ$="YOU SWIM AROUND FOR A WHILE THEN END UP HERE.":GOTO9
234 IFR=22THENZ$="THE WATER IS TOO SHALLOW TO SWIM HERE.":GOTO9
235 Z$="YOU SWIM OUT INTO THE BAY...":GOSUB1:R=44:GOTO12
238 Z$="YOU HAVE DIED.":GOSUB1
239 INPUT"WOULD YOU LIKE TO START OVER";F$:IFLEFT$(F$,1)="Y"THENRUN
243 Z$="OK. SEE YOU NEXT TIME ON PIRATE ISLAND.":GOSUB1:END
244 ZZ=0:F$="YOU CAN SEE: ":FORI=1TO14:IFA(I,0)=RTHENIFLEN(F$)<220THENF$=F$+D$(I)+", ":ZZ=1
245 NEXT:IFZZTHENZ$=LEFT$(F$,LEN(F$)-2)+".":GOSUB1
246 RETURN
252 IFR>39THEN263
253 IFR<21THENONRGOSUB265,267,267,268,270,271,273,275,270,270,277,279,270,281,283,285,287,281,281,289:GOTO257
255 IFR<41THENONR-20GOSUB291,294,296,297,299,301,303,304,306,308,310,312,314,316,316,317,319,320,322
257 IFR<>25THEN264
258 Z$="THE CAPTAIN LOOKS AT YOU AND SAYS, 'WELL WHAT'S THE LONGITUDE OF PIRATE ISLAND?'":GOSUB1:INPUTF1
259 Z$="'OK, NOW WHAT'S THE LATITUDE?'":GOSUB1:INPUTF2:IFF1=29ANDF2=45THENA(24,0)=1
260 Z$="'YOU BETTER BE RIGHT OR YOU'LL REGRET YOU EVER WOKE UP THIS MORNING! NOW GO.'":GOSUB1:A(15,0)=1:R=29:GOTO12
263 ZZ=R-39:ONZZGOSUB330,332,333,335,336,338
264 RETURN
265 Z$="YOU ARE ON THE TOP OF A HIGH CLIFF OVERLOOKING THE OCEAN. A PATH LEADS DOWN. THE SOIL IS DAMP HERE.":GOTO9
267 Z$="YOU ARE IN A VAST RAIN FOREST.":GOTO9
268 Z$="YOU ARE ON A SMALL PATH LEADING WEST OUT OF THE RAIN FOREST. IN THE "
269 Z$=Z$+"CENTER OF THE PATH THERE IS A LARGE TREE.":GOTO9
270 Z$="THIS IS A THICK FOREST... ALL THE TREES ARE UNCLIMBABLE.":GOTO9
271 Z$="YOU ARE STANDING BELOW A LARGE CLIFF. A PATH LEADS TO THE CLIFF WHILE AN "
272 Z$=Z$+"E/W PATH HEADS TOWARDS A COVE AND A MEADOW.":GOTO9
273 Z$="YOU ARE NEAR A LARGE ANCIENT ROCK. A SMALL PATH LEADS SOUTH.":GOTO9
275 Z$="YOU ARE IN A LARGE TREE ABOVE A SMALL PATH. DENSE BRANCHES "
276 Z$=Z$+"PREVENT YOU FROM GOING FURTHER UP THE TRUNK.":GOTO9
277 Z$="YOU ARE STANDING ON THE SHORE OF A COVE. THE OCEAN IS EAST, WHILE A MURKY SMELL COMES FROM THE SOUTH. "
278 Z$=Z$+"THE SOIL IS DAMP HERE.":GOTO9
279 Z$="YOU ARE STANDING IN A LARGE MEADOW WHERE BIRDS SING AND DEER PLAY. "
280 Z$=Z$+"LUSH GRASS IS STAMPED DOWN FORMING PATHS TO THE NORTH, SOUTH AND WEST.":GOTO9
281 Z$="THIS IS A DISMAL SWAMP WHERE HIDEOUS CREATURES HIDE WHEN THEY ARE NOT HUNTING. "
282 Z$=Z$+"A PATH LEADS TO THE WEST.":GOTO9
283 Z$="YOU ARE STANDING, OR RATHER WERE STANDING, IN A LARGE POND OF QUICK SAND. "
284 Z$=Z$+"YOU ARE SLOWLY... SINKING...":GOSUB1:GOTO238
285 Z$="YOU ARE WALKING ALONG A BEACH THAT STRETCHES ALONG THE SOUTHWEST PART OF THE "
286 Z$=Z$+"ISLAND. TO THE EAST IS MORE BEACH, WHILE TO THE WEST COMES A MUSTY SMELL.":GOTO9
287 Z$="THIS IS A SANDY BEACH. PALM TREES LINE THE SHORE AND "
288 Z$=Z$+"A PATH LEADS WEST AND EAST. ANOTHER PATH HEADS NORTH.":GOTO9
289 Z$="YOU ARE STANDING IN A DARK CAVE NEXT TO A SWAMP. FROM THE EAST COMES "
290 Z$=Z$+"SQUEAKING NOISES.":GOTO9
291 Z$="YOU ARE IN A CAVE WITH MANY BATS. THE GROUND IS DAMP HERE. "
292 IFA(4,0)<>200THENZ$=Z$+"THE BATS SEE YOU AND ATTACK. YOU RECEIVE MANY BITES...":GOSUB1:GOTO238
293 Z$=Z$+"THE BATS, SEEING YOUR GARLIC, STAY FAR BACK.":GOTO9
294 Z$="YOU ARE STANDING IN THE SHALLOWS OF A COVE. TO THE NORTH THERE "
295 Z$=Z$+"IS A SANDY BEACH, WHILE IN ALL OTHER DIRECTIONS THERE IS WATER.":A(16,0)=1:GOTO9
296 Z$="YOU ARE IN A CAVE BELOW A GRATE. FROM UP ABOVE YOU CAN HEAR SQUEAKS.":GOTO9
297 Z$="THIS IS THE SHIPS'S WHEELHOUSE. A NAVIGATOR IS HERE STEERING THE SHIP.":GOTO9
299 Z$="YOU ARE IN THE CAPTAIN'S QUARTERS, A LARGE ROOM WITH EQUIPMENT INCLUDING A COMPASS AND AN OLD "
300 Z$=Z$+"RELIC MADE OF GOLD. THERE IS A LARGE WOODEN DESK HERE.":GOTO9
301 Z$="YOU ARE IN YOUR CABIN, A SMALL ROOM WITH MINOR FURNISHINGS-- "
302 Z$=Z$+"A BED AND A DRESSER WITH A SINGLE DRAWER. A WOODEN DOOR IS EAST.":GOTO9
303 Z$="YOU ARE IN THE NORTH END OF A N/S HALLWAY.":GOTO9
304 Z$="YOU ARE ON THE PORT SECTION OF THE SHIP'S DECK. TO THE NORTH THERE IS THE "
305 Z$=Z$+"SHIP'S WHEELHOUSE. THE MID DECK IS SOUTH.":GOTO9
306 Z$="YOU ARE ON THE SHIP'S PORT DECK. WEST IS ANOTHER PORTDECK AND SOUTH, THE MID DECK. TO THE EAST THERE IS "
307 Z$=Z$+"A PASSAGE TO THE WHEELHOUSE AND TO THE NORTH A DOOR LEADS TO THE CAPTAIN'S QUARTERS.":GOTO9
308 Z$="YOU ARE IN THE CREW'S CABIN. THERE ARE BUNKS AND LOTS OF GEAR HERE BELONGING TO THE CREW.":GOTO9
310 Z$="THIS IS A HALLWAY RUNNING N/S. TO THE WEST IS THE CREW'S CABIN WHILE UP THERE IS THE MID DECK.":GOTO9
312 Z$="YOU ARE ON THE MID DECK. ANOTHER MID DECK IS EAST WHILE THE SHIP'S PORT AND "
313 Z$=Z$+"STARBOARD DECKS ARE NORTH AND SOUTH. A LARGE MAST IS HERE WITH A CROW'S NEST AT THE TOP.":GOTO9
314 Z$="YOU ARE ON ONE OF TWO MID DECKS. A LADDER LEADS BELOW. PORT AND STARBOARD ARE NORTH AND SOUTH WHILE "
315 Z$=Z$+"A LARGE MAST IS ABOVE TOPPED WITH A CROW'S NEST.":GOTO9
316 Z$="YOU ARE IN THE CROW'S NEST.":GOTO9
317 Z$="YOU ARE IN THE SHIP'S MESS HALL. TO THE SOUTH IS THE GALLEY AND TO THE EAST IS A HALLWAY.":GOTO9
319 Z$="YOU ARE IN A HALLWAY RUNNING N/S. TO THE WEST IS THE SHIP'S MESS.":GOTO9
320 Z$="YOU ARE ON THE STARBOARD DECK. TO THE EAST THERE IS ANOTHER "
321 Z$=Z$+"STARBOARD DECK, WHILE TO THE NORTH THERE IS A MID DECK. SOUTH THERE IS THE SHIP'S LIBRARY.":GOTO9
322 Z$="THIS IS THE SHIP'S STARBOARD DECK. THERE IS A LADDER HERE LEADING DOWN TO A "
323 Z$=Z$+"HALLWAY. OTHER DECKS ARE NORTH AND WEST.":GOSUB1:IFA(16,0)<>1THEN264
325 Z$="AS YOU COME BACK ABOARD, THE CAPTAIN GREETS YOU. 'WELCOME BACK MATEY!'":GOSUB1
326 IFA(9,0)=200THENZ$="'YOU FOUND THE COIN! NOW WE'LL ALL BE RICH!'":GOSUB1:A(22,0)=A(22,0)+50:GOTO79
328 Z$="'WHAT? YOU DIDN'T FIND THE COIN!' THE CAPTAIN PULLS OUT HIS SWORD AND KILLS YOU!":GOSUB1:GOTO238
330 Z$="THIS IS THE SHIP'S GALLEY. A LARGE STOVE IS AGAINST THE SOUTH "
331 Z$=Z$+"WALL AND MANY INGREDIENTS AWAIT TO BE PREPARED BY THE COOK.":GOTO9
332 Z$="YOU ARE IN THE SOUTH END OF A HALLWAY RUNNING N/S.":GOTO9
333 Z$="YOU ARE IN THE SHIP'S LIBRARY. A CARD CATALOG IS HERE WITH A REFERENCE NOTE PINNED TO IT.":GOTO9
335 Z$="YOU ARE UNDER WATER. YOU ARE SINKING IN THE SOFT SAND...":GOSUB1:GOTO238
336 Z$="YOU ARE SWIMMING IN AN INLAND COVE. OFF TO THE EAST IS THE SHORE.":GOTO9
338 Z$="YOU ARE ON THE BOTTOM OF A COVE. THE SAND HERE IS FIRM. YOU DON'T SINK.":GOTO9
355 DATA0,,,,,6,2,2,3,2,,,2,3,4,,6,,,,,3,8,,,5,10,9,,,,,12,11,1,3,,17,,,,,,,,,,4
363 DATA5,10,13,9,,,5,19,10,9,,,6,15,12,,,,6,16,,11,,,9,,,18,,,,19,14,20,,,,,,,,0
370 DATA12,,17,15,,,7,22,18,16,,,13,,,17,,,14,18,19,10,,,,,21,14,,,,,,20,,,17,39,43,43,43,43
377 DATA0,,,,21,,,,29,28,,,,29,,,,,,,,,,,,31,,,,,24,32,29,,,,,33,24,28,,0
384 DATA0,,31,,,,27,37,,30,33,,28,38,33,,,,29,39,,32,,31,,,,,,32,,,,,,33,,40,37,,,0
391 DATA31,41,,36,39,,32,42,39,,,,33,,,38,,37,36,,,,,,37,,,,,,38,,,,,,,,,,,0
398 DATA0,,11,,,45,,,,,44,0
400 DATA"SHO","AN OLD SHOVEL",35,"A SHOVEL","CAP","THE CAPTAIN OF THE SHIP",25,""
402 DATA"KEY","A SHINY KEY",8,"A KEY"
403 DATA"GAR","A CLOVE OF GARLIC",40,"A CLOVE OF GARLIC"
404 DATA"BOO","A LEATHER BOUND BOOK",255,"A BOOK","SWO","A SHARP SWORD",255,"A SWORD"
406 DATA"CHE","AN OLD PIRATE CHEST",23,"","COO","THE SHIP'S COOK, COOKING A STEW",40,""
408 DATA"COI","A SPANISH COIN",255,"A GOLD COIN"
409 DATA"TRA","A TRAY FULL OF DISCARDED FOOD",36,"A TRAY"
410 DATA"BOX","A SMALL BOX",45,"A BOX","PIC","A THIEVE'S PICK",255,"A THIEVE'S PICK"
412 DATA"GRA","A METAL GRATE",255,"","BAG","A BAG OF SILVER COINS",255,"A BAG OF SILVER COINS"
414 Z$="YOU CLIMB UP THE MAST...":GOSUB1:RETURN
415 PRINT"press play":CLOAD*A,"PIDAT":R=A(0,0):GOSUB12:RETURN
416 GOSUB5:A(0,0)=R:CSAVE*A,"PIDAT":RETURN
417 IFR<>42THEN205
418 Z$="DAVID MENY IS ONE OF THE GREATEST PIRATES ON THE SEVEN SEAS!":GOSUB1
419 Z$="HE CONTINUES TO ROAM THE WORLD IN SEARCH OF ADVENTURE AND INTRIGUE.":GOTO9
