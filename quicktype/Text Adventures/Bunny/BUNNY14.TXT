0 CLS0:CLEAR500:DIMCC,C1,M$:GOTO21
1 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:GOTO5
2 IFLEFT$(M$,1)="*"THENM$="YOU ARE "+MID$(M$,2)
3 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
4 CC=CC+(CC>255)*(CC-255):NEXT:RETURN
5 FORC1=1TOLEN(M$):POKECC+C1,ASC(MID$(M$,C1))AND63:NEXT:RETURN
10 GOSUB2:GOTO42
12 GOSUB2:GOSUB16:GOTO52
16 MO=MO-1:RETURN
18 M$=LEFT$("SCORE:"+STR$(S)+"            ",16)+LEFT$("MOVES:"+STR$(MO)+"           ",16):CC=16383:GOSUB5:RETURN
20 GOSUB2:RETURN
21 PRINT@46,;:M$="("+CHR$(92)+" /)":GOSUB1:PRINT@77,;:M$="(='M'=)":GOSUB1
22 PRINT@109,;:M$="("+CHR$(34)+") ("+CHR$(34)+")":GOSUB1:PRINT@192,;:M$="THE NIGHT OF THE VAMPIRE BUNNIES":GOSUB1
23 PRINT@298,;:M$="BY JASON DYER":GOSUB1:PRINT@452,;:M$="MC-10 EDITS BY JIM GERRIE":GOSUB1:FORCC=1TO200
24 IFRND(10)=1THENPOKE49151,64:FORC1=1TORND(200):NEXT:POKE49151,0
25 NEXT:NO=9:R=5:ND=6:NR=27:MO=0:NI=2:DIMR$(NR),O$(NO),O2$(NO),O(NO),MA(28,6)
26 POKE49151,0:FORI=1TONR:FORJ=0TOND-1:READMA(I,J):NEXT:NEXT:FORI=0TONO-1:READO$(I),O2$(I),O(I):NEXT
34 CLS:PRINT
36 GOSUB186
38 IFFFTHENFF=0:GOTO212
40 GOSUB182
42 IFVATHENVT=VT+1:IFVT=2THENM$="THE VAMPIRE DECIDES TO FANG YOU.":GOSUB2:GOTO210
44 IFR=25ANDNOTDBTHENRA=RA+1:GOTO48
46 RA=0
48 IFR=25ANDNOTDBTHENIFRA=3THENM$="THE VAMPIRE BUNNY NIBBLES A LITTLE ON YOUR LEG.":GOSUB2:GOTO210
50 REM MAIN
52 GOSUB18:INPUTC$:IFC$=""THENPRINT"HUH?":GOTO52
53 C1=0:FORCC=1TOLEN(C$):IFMID$(C$,CC,1)=" "THENC1=CC:CC=255
54 NEXT:IFC1=0THENN$="":V$=LEFT$(C$,5):GOTO142
55 V$=LEFT$(C$,C1-1):N$=MID$(C$,C1+1):V$=LEFT$(V$,5):N$=LEFT$(N$,5)
142 MO=MO+1
144 IFR<11ANDR<>7THEN156
146 IFO(4)=-1ORO(4)=RTHEN156
148 IFNOTLATHEN152
150 IFO(1)=-1ORO(1)=RTHEN156
152 IFC$<>"USE LAMP"ANDC$<>"TURN ON LAMP"THENM$="*IN TOTAL DARKNESS.":GOSUB2
154 GOTO52
156 IFC$="SHOUT DOWN"ORC$="SHOUT WELL"THENIFR=4THENM$="ECHO... ECHO... ECHO...":GOTO10
166 IFV$="REMOV"THENV$="UNWEA"
170 IFV$="THROW"ANDN$="WATER"THENV$="POUR"
172 IFV$="THROW"ANDN$="BUCKE"THENV$="POUR"
176 IFV$="DOFF"THENV$="UNWEA"
180 GOTO234
182 FORI=0TONO-1:IFO(I)=RTHENM$="YOU SEE "+O$(I)+" HERE.":GOSUB2
184 NEXT:RETURN
186 IFR<11ANDR<>7THEN196
188 IFO(4)=-1ORO(4)=RTHEN196
190 IFNOTLATHEN194
192 IFO(1)=-1ORO(1)=RTHEN196
194 DA=1:RETURN
196 IFR=100THENDA=0:GOTO878
198 ONRGOSUB916,934,940,946,954,960,966,972,980,990,996,1002,1006,1008,1014:IFR<=15THENRETURN
200 ONR-15GOSUB1018,1020,1022,1028,1036,1038,1040,1042,1050,1056,1066,1070:RETURN
202 IFNOTLATHEN206
204 IFO(1)=-1ORO(1)=RTHENRETURN
206 DA=1:M$="UH-OH, NOW YOU CAN'T SEE!":GOTO20
208 M$="*DEAD.":GOTO20
210 GOSUB208:GOSUB886:GOTO476
212 GOSUB208:GOSUB886:M$="NEW GAME OR UNDO (Y/N/U)?":GOSUB2
214 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"ANDI$<>"U"THEN214
216 IFI$="Y"THENRUN
218 IFI$="U"THENGOSUB16:GOTO36
220 END
222 FORI=0TONO-1:IFO2$(I)=N$THENFL=1:RO=O(I):CC=I:I=NO:NEXT:I=CC:RETURN
228 NEXT:FL=0:RETURN
234 IFV$<>"GO"ANDV$<>"RUN"ANDV$<>"CRAWL"ANDV$<>"N"ANDV$<>"S"ANDV$<>"CLIMB"ANDV$<>"E"ANDV$<>"W"ANDV$<>"U"ANDV$<>"D"THEN306
236 IFN$="NORTH"ORV$="N"THENDI=0:GOTO276
238 IFN$="SOUTH"ORV$="S"THENDI=1:GOTO276
240 IFN$="EAST"ORV$="E"THENDI=2:GOTO276
242 IFN$="WEST"ORV$="W"THENDI=3:GOTO276
244 IFN$="UP"ORV$="U"THENDI=4:GOTO276
246 IFN$="DOWN"ORV$="D"THENDI=5:GOTO276
248 IFR=1ANDN$="COFFI"THENM$="ARE YOU CRAZY?":GOTO10
250 IFR=3THENIFN$="STREA"THENV$="S":GOTO234
252 IFR=6THENIFN$="STREA"THENV$="N":GOTO234
254 IFR=9ANDN$="HOLE"THENV$="D":GOTO234
256 IFR=4ANDN$="WELL"THENV$="D":GOTO234
258 IFR=7ANDN$="CREVI"THENV$="S":GOTO234
260 IFR=8ANDN$="HOLE"THENV$="D":GOTO234
262 IFR=7ANDN$="ROPE"THENDI=4:GOTO276
264 IFR=4ANDN$="ROPE"ANDROTHENDI=5:GOTO276
266 IFR=27ANDN$="HOLE"THENV$="U":GOTO234
268 IFR=14ANDN$="HOLE"THENV$="U":GOTO234
270 IFR=2ANDN$="HOLE"THENV$="E":GOTO234
272 IFR=3ANDN$="HOLE"THENV$="W":GOTO234
274 M$="YOU CAN'T GO THAT WAY!":GOTO12
276 IFMA(R,DI)>0ANDMA(R,DI)<128THENR=MA(R,DI):GOTO36
278 IFMA(R,DI)=203THENM$="THE OOZE BLOCKS YOUR PATH.":GOTO10
280 IFMA(R,DI)=204THENM$="THE BUNNY WON'T LET YOU!":GOTO10
282 IFMA(R,DI)=200THENM$="YOU ENTER THE STREAM. A LITTLE WATER GETS IN YOUR MOUTH.":GOSUB2:GOTO212
284 IFMA(R,DI)=201ANDNOTRPTHEN274
286 IFMA(R,DI)=201ANDNOTLATHENM$="IT'S TOO DARK DOWN THERE.":GOTO10
288 IFMA(R,DI)=201THENIFO(1)<>7ANDO(1)<>-1THENM$="IT'S TOO DARK DOWN THERE.":GOTO10
290 IFMA(R,DI)=240ANDNI>1THENM$="*TOO HEAVY TO CLIMB UP.":GOTO10
292 IFMA(R,DI)=240THENR=4:M$="YOU CLIMB UP THE ROPE.":GOSUB2:GOTO36
294 IFMA(R,DI)=201ANDNI>1THENM$="THE ROPE SNAPS AND YOU PLUNGE DOWN.":GOSUB2:GOTO212
296 IFMA(R,DI)=201THENR=7:M$="YOU CLIMB DOWN THE ROPE.":GOSUB2:GOTO36
298 IFMA(R,DI)=700THENM$="THE VAMPIRE, UNFORTUNATELY, GIVES YOU A NASTY BITE.":GOSUB2:GOTO210
300 IFMA(R,DI)=314ANDNOTWCTHENM$="THE SKELETON PUSHES YOU AWAY.":GOTO10
302 IFMA(R,DI)=314THENM$="THE SKELETON ALLOWS YOU TO ENTER.":GOSUB2:R=14:GOTO36
304 GOTO274
306 IFV$<>"GET"ANDV$<>"TAKE"THEN324
308 ZH=0:GOSUB222:IFN$="WATER"THENIFO(2)=-1THENV$="FILL":N$="BUCKE":GOTO234
310 IFFL=0ORRO<>RTHENM$="YOU CAN'T GET THAT!":GOTO12
312 IFRO=-1THENM$="YOU ALREADY HAVE THAT!":GOTO12
316 IFNI=5THENM$="*CARRYING TOO MUCH!":GOTO12
318 IFNOTCRANDI=3THENCR=-1:S=S+10
320 IFNOTCAANDI=0THENCA=-1:S=S+2
322 NI=NI+1:O(I)=-1:PRINT"TAKEN.":GOTO42
324 IFV$="HINT"ORV$="HELP"THENPRINT"SORRY.":GOTO42
326 IFV$="TURN"ANDN$="OFF L"THENV$="UNLIG":N$="LAMP"
328 IFV$="TURN"ANDN$="ON LA"THENV$="LIGHT":N$="LAMP"
338 IFV$<>"DROP"THEN352
342 GOSUB222:IFFL=0THEN695
346 IFRO=-2THENRO=-1
348 IFRO<>-1THEN695
350 NI=NI-1:O(I)=R:PRINT"DROPPED.":GOTO42
352 IFV$<>"INVEN"ANDV$<>"I"THEN382
354 FL=0:M$="*CARRYING:":GOSUB2
356 IFWCTHENM$="WEARING A SILVER CROSS":GOSUB2:FL=1
358 IFBUANDO(2)=-1THENM$="A BUCKET WITH WATER":GOSUB2:FL=1
360 IFLAANDO(1)=-1THENM$="A LIT LAMP":GOSUB2:FL=1
362 IFO(4)=-1THENM$="A LIT TORCH":GOSUB2:FL=1
364 FORI=0TONO-1
366 IFWCANDI=3THEN376
368 IFBUANDI=2ANDO(I)=-1THEN376
370 IFLAANDI=1ANDO(I)=-1THEN376
372 IFI=4ANDO(I)=-1THEN376
374 IFO(I)=-1THENM$=O$(I):GOSUB2:FL=1
376 NEXT
378 IFFL=0THENPRINT"NOTHING"
380 GOTO42
382 IFV$<>"LOOK"ANDV$<>"L"THEN388
384 IFN$<>""THEN392
386 GOTO36
388 IFV$<>"EXAMI"ANDV$<>"SEARC"THEN466
392 IFN$="GROUN"ORN$="FLOOR"ORN$="FIELD"THENIFR=5ANDNOTCATHENCA=-1:O(0)=5:M$="YOU FIND A CARROT.":GOTO10
394 IFN$="GROUN"ORN$="FLOOR"THENM$="YOU SEE NOTHING UNUSUAL.":GOTO10
396 IFN$="COFFI"ANDR=1THENM$="IT IS CARVED OF MARBLE.":GOTO10
398 IFN$="SKELE"ANDR=9THENM$="IT LOOKS PRETTY BONY.":GOTO10
400 IFN$="WELL"ANDR=4THENM$="ROPE HANGS FROM A WOODEN CRANK. THE WELL IS DARK.":GOTO10
402 IFN$="ROPE"THENIFR=4ORR=7THENM$="IT DOESN'T LOOK VERY STURDY.":GOTO10
404 IF(N$="WELL"ORN$="CRACK")ANDR=7THENM$="THERE'S A LARGE CREVICE SOUTH.":GOTO10
406 IFN$="OOZE"ANDR=19ANDNOTDGTHENM$="IT IS BIG, UGLY AND SLIMY.":GOTO10
408 IFN$="COBWE"ANDNOTBCTHENM$="THEY ARE BLOCKING THE EAST PASSAGE.":GOTO10
410 IFN$="LEVER"ANDR=26THENM$="IT'S RUSTY BUT USEABLE.":GOTO10
412 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="IT LOOKS EXTREMELY VICIOUS.":GOTO10
414 IFN$="VAMPI"ANDVATHENM$="IT LOOKS AT YOU WITH AN UNNERVING GAZE.":GOTO10
416 IFN$="TOMBS"ORN$="GRAVE"THENIFR<11ANDR<>7THENM$="MANY ARE OLD AND ROTTED AND SOME ARE BLANK.":GOTO10
418 IFN$="CRANK"ANDR=4THENM$="IT IS CRACKING BUT USABLE.":GOTO10
420 IFN$="STREA"THENIFR=6ORR=3THENM$="IT LOOKS SHALLOW ENOUGH TO CROSS.":GOTO10
424 IFN$="MIRRO"ANDR=24THEN428
426 GOTO432
428 IFBRTHENM$="IT'S BROKEN. THERE'S A PASSAGE BEHIND IT.":GOTO10
430 M$="IT'S NOT WELL POLISHED.":GOTO10
432 IFN$="WALLS"ORN$="WALL"THENM$="THERE ARE ENOUGH OF THEM.":GOTO10
434 GOSUB222:IFRO<>RANDRO<>-1ANDO(I)<>(R-128)THEN629
438 IFN$="CARRO"THENM$="IT LOOKS NICE AND JUICY.":GOTO10
440 IFN$<>"LAMP"THEN446
442 IFLATHENM$="THE LAMP IS ON.":GOTO10
444 M$="THE LAMP IS OFF.":GOTO10
446 IFN$<>"BUCKE"THEN452
448 IFBUTHENM$="THE BUCKET IS FILLED.":GOTO10
450 M$="THE BUCKET IS EMPTY.":GOTO10
452 IFN$="CROSS"THENM$="IT IS ROUGH AND TARNISHED.":GOTO10
454 IFN$="PAPER"THENM$="IT HAS SOME WRITING ON IT.":GOTO10
456 IFN$="KEY"THENM$="IT IS FAIRLY SHINY.":GOTO10
458 IFN$="TORCH"THENM$="IT IS BURNING SLOWLY.":GOTO10
460 IFN$="PEN"ORN$="QUILL"THENM$="IT DOES NOT HAVE ANY INK.":GOTO10
462 IFN$="APPLE"THENM$="IT LOOKS A LITTLE WORMY.":GOTO10
464 M$="YOU SEE NOTHING UNUSUAL.":GOTO10
466 IFV$<>"QUIT"ANDV$<>"Q"ANDV$<>"END"THEN484
468 GOSUB886
470 M$="SURE YOU WANT TO QUIT (Y/N)":GOSUB2:INPUTM$:IFM$="N"THENGOSUB16:GOTO52
474 IFM$<>"Y"THEN470
476 M$="PLAY AGAIN (Y/N)?":GOSUB2
478 I$=INKEY$:IFI$<>"Y"ANDI$<>"N"THEN478
480 IFI$="Y"ORI$="Y"THENRUN
482 END
484 IFV$<>"ENTER"THEN488
486 V$="GO":GOTO234
488 IFV$<>"JUMP"THEN508
490 IFN$=""THENPRINT"WHEEEEEE!":GOTO42
492 IFN$<>"STREA"ANDN$<>"OOZE"ANDN$<>"BUNNY"THENM$="THAT WON'T DO ANY GOOD.":GOTO12
494 IFR=19ANDNOTDGANDN$="OOZE"ORN$="GREY "THENM$="THE OOZE EXPANDS AND PREVENTS YOUR JUMP.":GOTO12
496 IFR=25ANDNOTDBANDN$="BUNNY"THENM$="THE BUNNY BITES YOUR LEG.":GOSUB2:GOTO212
500 IFR<>3ANDR<>6THENM$="THAT WON'T DO ANYTHING!":GOTO10
502 IFR=3THENR=6:GOTO506
504 R=3
506 M$="YOU JUMP OVER THE STREAM.":GOSUB2:GOTO36
508 IFV$<>"READ"THEN516
510 IFN$<>"PAPER"THEN699
512 IFO(6)<>-1THEN695
514 M$="IT SAYS, 'PUSHING IS BETTER THAN PULLING'":GOTO10
516 IFV$<>"LIGHT"THEN542
518 IFN$="COBWE"THEN526
519 IFN$="OOZE"THENIFR=19ANDDG=0THEN868
520 GOSUB222:IFFL=0ORO(I)<>-1THEN695
524 IFN$<>"TORCH"ANDN$<>"LAMP"THEN699
526 IFN$="COBWE"ANDR=23ANDBC=0THENS=S+5:BC=-1:MA(23,2)=24:M$="THE COBWEBS BURN.":GOTO10
528 IFN$="COBWE"THEN699
530 IF(N$="TORCH"ANDO(4)=-1)OR(N$="LAMP"ANDO(1)=-1ANDLA)THENM$="IT'S ALREADY LIT!":GOTO10
534 IFN$="LAMP"ANDNOTLATHEN538
536 GOTO542
538 M$="THE LAMP LETS OUT A WARM GLOW.":GOSUB2:LA=-1:IFDATHEN36
540 GOTO42
542 IFV$<>"LOWER"THEN552
544 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
546 IFR<>4THEN699
548 IFRPTHENM$="IT ALREADY IS!":GOTO12
550 RP=-1:M$="THE ROPE GOES DOWN.":GOTO10
552 IFV$<>"RAISE"ANDV$<>"LIFT"THEN562
554 IFN$<>"ROPE"ANDN$<>"WELL"THEN699
556 IFR<>4THEN699
558 IFNOTRPTHENM$="IT ALREADY IS!":GOTO12
560 RP=0:M$="THE ROPE RISES.":GOTO10
562 IFV$<>"FILL"THEN570
564 IFN$<>"BUCKE"ORO(2)<>-1OR(R<>3ANDR<>6)THEN699
566 IFBUTHENM$="IT'S ALREADY FULL!":GOTO12
568 BU=-1:M$="THE BUCKET FILLS WITH WATER.":GOTO10
570 IFV$<>"UNLIG"THEN584
572 GOSUB222:IFFL=0THEN695
574 IFO(I)<>-1THEN695
576 IFN$<>"TORCH"ANDN$<>"LAMP"THEN699
578 IFN$="TORCH"ANDO(4)=-1THENM$="IT REFUSES TO GO OUT.":GOTO10
580 IFN$="LAMP"ANDO(1)=-1ANDNOTLATHENM$="IT ALREADY IS!":GOTO10
582 IFN$="LAMP"ANDO(1)=-1THENLA=0:M$="THE LAMP TURNS OFF.":GOTO10
584 IFV$<>"OPEN"THEN594
586 IFN$<>"COFFI"THEN699
588 IFR<>1THEN699
590 IFOCTHENM$="IT'S ALREADY OPEN!":GOTO12
592 OC=-1:VA=-1:MA(1,1)=700:M$="AS YOU OPEN THE COFFIN, A VAMPIRE REACHES OUT!":GOTO10
594 IFV$<>"CLOSE"THEN606
596 IFN$<>"COFFI"THEN699
598 IFR<>1THEN699
600 IFNOTOCTHENM$="IT'S NOT OPEN!":GOTO12
602 IFVATHENVT=0:VA=0:OC=0:MA(1,1)=4:M$="THE COFFIN SLAMS SHUT IN THE VAMPIRE'S FACE.":GOTO10
604 OC=0:M$="THE COFFIN CLOSES.":GOTO10
606 IFV$<>"POUR"ANDV$<>"EMPTY"ANDV$<>"SPLAS"THEN630
608 IFN$<>"WATER"ANDN$<>"BUCKE"ANDN$<>"APPLE"ANDN$<>"CARRO"ANDN$<>"SKELE"ANDN$<>"VAMPI"ANDN$<>"BUNNY"THEN699
610 IFO(2)<>-1THEN695
612 IFNOTBUTHEN699
616 IFR=19ANDDG=0THENM$="THE OOZE GROWS AND SPLATTERS YOU WITH DEADLY FORCE.":GOSUB2:GOTO212
618 IFR=24ANDDB=0THENBU=0:M$="THE BUNNY MOVES, AVOIDING THE WATER.":GOTO10
620 IFN$="APPLE"AND(O(8)=-1ORO(8)=R)THENBU=0:AP=-1:M$="THE APPLE IS SPLASHED WITH WATER.":GOTO10
622 IFN$="CARRO"AND(O(0)=-1ORO(0)=R)THENS=S+10:BU=0:CS=-1:M$="THE CARROT IS SPLASHED.":GOTO10
624 IFN$="SKELE"ANDR=9THENBU=0:M$="THE WATER FLIES THROUGH ITS SKELTAL BODY.":GOTO10
626 IFVATHENMA(1,1)=4:BU=0:VA=0:O(3)=1:M$="THE VAMPIRE SCREAMS AND DISSOLVES.":GOTO10
628 GOTO856
629 M$="THAT'S NOT HERE!":GOTO12
630 IFV$<>"WEAR"THEN640
632 IFN$<>"CROSS"THEN699
634 IFO(3)<>-1THEN695
636 IFWCTHENM$="*ALREADY WEARING IT!":GOTO12
638 WC=-1:M$="IT FITS RATHER NICE.":GOTO10
640 IFV$="PUT"ANDN$="ON CR"THENV$="WEAR":N$="CROSS":GOTO630
646 IFV$="TAKE"ANDN$="OFF C"THENV$="UNWEAR":N$="CROSS":GOTO234
648 IFV$<>"PUT"THEN716
650 IFN$="WATER"THENV$="POUR":GOTO234
652 IFN$="BUCKE"THENV$="POUR":GOTO234
654 IFN$<>"TORCH"THEN702
656 IFO(4)<>-1THEN695
662 M$="THAT PROBABLY WON'T DO MUCH.":GOTO12
695 M$="YOU DON'T HAVE THAT!":GOTO12
698 M$="THAT'S NOT HERE!":GOSUB2:GOSUB16:GOTO42
699 M$="YOU CAN'T DO THAT!":GOTO12
702 GOSUB222:IFFL=0THEN695
704 IFO(I)<>-1THEN695
706 IFN$="CARRO"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENS=S+10:CS=-1:M$="THE CARROT SOAKS UP THE WATER.":GOTO10
710 IFN$="APPLE"THENIFR=3ORR=6OR(O(2)=-1ANDBU)THENAP=-1:M$="THE APPLE SOAKS UP THE WATER.":GOTO10
714 GOTO856
716 IFV$<>"PUSH"THEN730
718 IFN$="COFFI"ANDR=1THENM$="IT IS TOO HEAVY.":GOTO10
720 IFN$="MIRRO"ANDR=24ANDNOTBRTHENM$="IT IS ATTACHED TO THE WALL.":GOTO10
722 IFN$="THRON"ANDR=25THENM$="IT IS BOLTED TO THE WALL.":GOTO10
724 IFN$<>"LEVER"ORR<>26THENM$="YOU DON'T NEED TO DO THAT.":GOTO12
726 IFPLTHENM$="NOTHING HAPPENS.":GOTO10
728 MA(8,5)=27:PL=-1:MA(27,4)=8:M$="YOU HEAR A LOUD 'CLUNK' TO THE SOUTH.":GOTO10
730 IFV$<>"PULL"THEN738
732 IFN$<>"LEVER"THEN718
734 IFR<>26THEN699
736 M$="THE CEILING CAVES IN.":GOSUB2:GOTO212
738 IFV$<>"TURN"THEN748
740 IFN$<>"CRANK"THEN699
742 IFR<>4THEN699
744 IFRPTHENRP=0:M$="THE ROPE RISES.":GOTO10
746 RP=-1:M$="THE ROPE GOES DOWN.":GOTO10
748 IFV$="WAIT"ORV$="Z"THENM$="TIME PASSES...":GOTO10
750 IFV$<>"EAT"ANDV$<>"TASTE"THEN766
752 GOSUB222:IFFL=0ORO(I)<>-1THENM$="YOU CAN'T DO THAT NOW!":GOSUB2:GOSUB16:GOTO42
756 IFI<>0ANDI<>8THENM$="YOU CAN'T EAT THAT!":GOSUB2:GOSUB16:GOTO42
758 IFI=8ANDAPTHENM$="YOU TAKE A BITE AND FALL DOWN. THE APPLE FALLS FROM YOUR LIMP HAND.":GOSUB2:GOTO212
760 IFI=0ANDCSTHENM$="YOU MUNCH A LITTLE AND COLLAPSE.":GOSUB2:GOTO212
762 IFI=8THENM$="YOU TAKE A BITE AND SPIT IT OUT.":GOTO10
764 O(0)=0:M$="MMMMM... IT IS DELICIOUS.":GOTO10
766 IFV$="SOAK"THENV$="PUT":GOTO234
770 IFV$<>"BREAK"ANDV$<>"SMASH"THEN780
772 IFN$<>"MIRRO"ORR<>24THEN629
776 IFBRTHENM$="IT'S ALREADY BROKEN!":GOTO12
778 BR=-1:S=S+8:MA(24,2)=25:M$="IT BREAKS.":GOTO10
780 IFV$<>"GIVE"THEN800
786 IFN$="BUNNY"AND(R<>25ORDB)THENM$="I DON'T SEE A BUNNY-- DO YOU?":GOTO12
788 GOSUB222:IFFL=0ORO(I)<>-1THEN695
792 IFN$<>"CARRO"THEN798
794 IFNOTCSTHENO(0)=0:M$="THE BUNNY GOBBLES UP THE CARROT BUT IS NOT SATISFIED.":GOTO10
796 IFCSTHENO(0)=0:S=S+10:DB=-1:M$="THE BUNNY GOBBLES THE CARROT UP, GASPS AND DIES.":GOTO10
798 M$="THE BUNNY REFUSES YOUR OFFER.":GOTO12
800 IFV$<>"SIT"THEN810
802 IFN$="ON TH"THENN$="THRON"
804 IF(N$<>"THRON"ANDN$<>"DOWN")ORR<>25THEN856
806 IFSTTHENM$="NOTHING HAPPENS SO YOU STAND UP AGAIN.":GOTO10
808 S=S+2:ST=-1:MA(25,2)=100:M$="AS YOU SIT, A PASSAGEWAY TO THE EAST OPENS!":GOTO10
810 IFV$="BURN"THENV$="LIGHT":GOTO234
812 IFV$="NORTH"THENV$="N":GOTO234
814 IFV$="SOUTH"THENV$="S":GOTO234
816 IFV$="EAST"THENV$="E":GOTO234
818 IFV$="WEST"THENV$="W":GOTO234
820 IFV$="UP"THENV$="U":GOTO234
822 IFV$="DOWN"THENV$="D":GOTO234
824 IFV$<>"UNWEA"THEN832
826 IFN$<>"CROSS"THEN845
828 IFNOTWCTHENM$="*NOT WEARING IT!":GOTO12
830 WC=0:M$="YOU SLIDE THE CROSS OFF YOUR NECK.":GOTO10
832 IFV$="DESTR"ANDN$="MIRRO"THENV$="BREAK":GOTO234
834 IFV$<>"DESTR"ANDV$<>"KILL"ANDV$<>"ATTAC"THEN846
836 IFN$="SKELE"ANDR=9THENM$="IT REPELS YOU.":GOTO10
838 IFN$="VAMPI"ANDVATHENM$="HE IGNORES YOUR ATTACK.":GOTO10
840 IFN$="OOZE"ANDNOTDGANDR=19THENM$="THE OOZE SPLATTERS YOU WITH KILLING FORCE.":GOSUB2:GOTO212
842 IFN$="BUNNY"ANDNOTDBANDR=25THENM$="THE BUNNY BITES.":GOSUB2:GOTO212
844 GOTO856
845 M$="I DON'T UNDERTAND.":GOTO12
846 IFV$="TALK"THEN856
848 IFV$<>"DRINK"ANDV$<>"SIP"THEN860
850 IFN$<>"WATER"ANDN$<>"STREA"THEN856
852 IFR=3ORR=6THEN858
854 IFO(2)=-1ORO(2)=RTHENIFBUTHEN858
856 M$="THAT WON'T DO MUCH.":GOTO12
858 M$="YOU SIP THE WATER AND FEEL SICK. YOU FALL TO THE FLOOR.":GOSUB2:GOTO212
860 IFV$="SCORE"THENGOSUB886:GOSUB16:GOTO50
862 M$="I DON'T KNOW THAT VERB.":GOTO12
868 DG=-1:MA(19,3)=18:O(4)=22:NI=NI-1:M$="THE OOZE IGNITES AND MELTS AWAY.":GOSUB2:GOSUB202:GOTO42
872 BC=-1:MA(23,2)=24:S=S+5:M$="THE COBWEBS LIGHT AND BURN AWAY INTO DUST.":GOTO10
878 S=S+2:M$="YOU GO THROUGH THE TUNNEL AND END UP OUTSIDE THE GRAVEYARD. YOU HURRY ALONG "
880 M$=M$+"THE PATH BACK INTO TOWN SHOUTING THE NEWS. PEOPLE RUSH OUT TO GREET YOU.":GOSUB2
884 GOSUB886:GOTO476
886 M$="YOU HAVE SCORED"+STR$(S)+" OUT OF 60 POINTS.":GOTO20
916 DA=0:M$="*AT A COFFIN. WALLS ARE NORTH, EAST AND WEST. A BLUE MIST HANGS OVER THE GRAVEYARD. "
922 IFVATHENM$=M$+"THE COFFIN IS OPEN WITH A VAMPIRE IN IT!":GOTO20
924 IFOCTHENM$=M$+"THE COFFIN IS OPEN.":GOSUB2:GOTO928
926 M$=M$+"THE COFFIN IS CLOSED.":GOSUB2
928 IFNOTOCTHENRETURN
930 FORI=0TO8:IFO(I)=.1THENM$="THERE IS "+O$(I)+" IN THE COFFIN.":GOSUB2
932 NEXT:RETURN
933 M$="*IN A GLOOMY WALLED GRAVEYARD. ":RETURN
934 DA=0:GOSUB933:M$=M$+"A HOLE IN THE EAST WALL ALLOWS EXIT.":GOTO20
940 DA=0:IFNOTJRTHENJR=-1:S=S+3
942 GOSUB933:M$=M$+"A STREAM IS TO THE SOUTH. A HOLE IN THE WEST WALL ALLOWS PASSAGE.":GOTO20
946 DA=0:GOSUB933:M$=M$+"A MOSS COVERED WALL IS WEST, A COFFIN NORTH AND A WELL IN THE CENTRE.":GOSUB2
952 IFRPTHENM$="A ROPE LEADS DOWN.":GOTO20
953 RETURN
954 DA=0:M$="*IN A MISTY FIELD. A WALL IS TO THE NORTH.":GOTO20
960 DA=0:GOSUB933:M$=M$+"A STREAM IS NORTH.":GOTO20
966 DA=0:IFNOTWETHENWE=-1:S=S+5
968 M$="*AT THE BOTTOM OF A WELL. IT'S FILLED WITH CRACKS. A ROPE HANGS DOWN.":GOTO20
972 DA=0:GOSUB933:M$=M$+"A BLACK WALL IS SOUTH. "
976 IFPLTHENM$=M$+"A GAPING HOLE LIES AMONGST THE GRAVESTONES.":GOTO20
978 M$=M$+"MANY TOMBSTONES ARE HERE.":GOTO20
980 DA=0:GOSUB933:M$=M$+"A WALL IS SOUTH. A LARGE HOLE IS HERE.":GOSUB2
986 IFWCTHENM$="A SKELETON RECOILS IN TERROR WHEN HE SEES YOUR CROSS.":GOTO20
988 M$="A SKELETON GUARDS THE HOLE.":GOTO20
990 DA=0:GOSUB933:M$=M$+"WALLS CONNECT TO THE SOUTH AND EAST.":GOTO20
996 DA=0:M$="*IN A LARGE CREVICE IN A WELL. WATER TRICKLES DOWN THE SIDES. EXIT NORTH.":GOTO20
1002 DA=0:M$="*NERVOUS ABOUT THIS PASSAGE, BUT YOU TRUDGE ON AND ARE KILLED IN A CAVE-IN."
1004 R=13:FF=-1:GOTO20
1006 DA=0:M$="*IN A DARK MAZE.":GOTO20
1008 DA=0:M$="*IN A DARK EAST/WEST TUNNEL, WITH A HOLE ABOVE.":GOTO20
1014 DA=0:M$="*IN A DARK TUNNEL WITH NORTH, SOUTH, EAST AND WEST EXITS.":GOTO20
1018 DA=0:M$="*IN A DAMP MAZE.":GOTO20
1020 DA=0:M$="*IN A DANK MAZE.":GOTO20
1022 DA=0:IFNOTSLTHENSL=-1:S=S+5
1026 M$="*IN A LONG EAST/WEST TUNNEL. YOUR FEET ACHE AS YOU WALK.":GOTO20
1028 DA=0:M$="*IN A SLIME COVERED ROOM. YOUR FEET STICK TO THE GROUND.":GOSUB2
1032 IFNOTDGTHENM$="GREY OOZE GUARDS THE WEST CORRIDOR.":GOSUB2
1034 RETURN
1036 DA=0:M$="*IN A DREARY MAZE.":GOTO20
1038 DA=0:M$="*IN A MAZE.":GOTO20
1040 DA=0:M$="*IN A DEVLISH MAZE.":GOTO20
1042 DA=0:M$="*IN A LARGE MARBLE ROOM WITH TORCH HOLDERS. MOST ARE EMPTY. " 
1046 IFBCTHENM$="EXITS NORTH, SOUTH AND EAST.":GOTO20
1048 M$="EXITS NORTH AND SOUTH, BUT THE PASSAGE EAST IS BLOCKED BY COBWEBS.":GOTO20
1050 DA=0:M$="*IN A DEAD END. ":IFBRTHENM$=M$+"IT EXITS WEST, BUT A SHATTERED MIRROR SHOWS A WAY EAST.":GOTO20
1054 M$=M$+"A LARGE MIRROR COVERS THE EAST WALL. AN EXIT IS WEST.":GOTO20
1056 DA=0:M$="*IN A LARGE CHAMBER WITH A THRONE. ":IFNOTDBTHENM$=M$+"A BUNNY IS NEXT TO IT. ":GOSUB2
1060 IFNOTDBTHENM$="THE VAMPIRE BUNNY! A PASSAGE EXITS WEST.":GOTO20
1062 IFNOTSTTHENM$=M$+"A PASSAGE EXITS WEST.":GOTO20
1064 M$=M$+"PASSAGES EXIT EAST AND WEST.":GOTO20
1066 DA=0:M$="*IN A CRAMPED CORRIDOR GOING NORTH/SOUTH. A WORN LEVER IS SET INTO THE WEST WALL.":GOTO20
1070 M$="*IN A SANDY ROOM WITH AN EXIT NORTH. ":DA=0:IFNOTPLTHENM$=M$+"THE CEILING LOOKS WEAK.":GOTO20
1074 M$=M$+"THERE IS A HOLE IN THE CEILING.":GOTO20
2100 DATA0,4,,,,,,,3,,,,,200,,2,,,1,8,5,,,201,,8,6,4,,,200,9,10,5,,,,11,,,240,,5,,9,4,,,6,,10,8,,314
2190 DATA6,,,9,,,7,,,,,,,,,,,,16,12,17,17,,,,,15,,9,,15,19,15,14,,,13,22,20,21,,,13,20,16,16,,,,,19,17,,0
2280 DATA15,,,203,,,13,17,17,16,,,16,23,13,20,,,17,21,20,16,,,21,26,,,,,,,,23,,,,,,24,,,23,27,,,,,26,,,,,0
2600 DATA"A CARROT",CARRO,,"A LAMP",LAMP,2,"A BUCKET",BUCKE,11,"A SILVER CROSS",CROSS,,"A TORCH",TORCH,14
2650 DATA"A KEY",KEY,18,"A SCRAP OF PAPER",PAPER,8,"A QUILL PEN",PEN,-1,"AN APPLE",APPLE,-1
