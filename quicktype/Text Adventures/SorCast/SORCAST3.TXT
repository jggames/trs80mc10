0 CLS:CLEAR1500:DIMC1,CC,P,M$,XX$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
100 CLS:PRINT"       SORCERER'S CASTLE":PRINT"        BY GREG HASSETT"
101 CP=49:S1$="I DON'T SEE IT HERE.":S2$="DON'T BE RIDICULOUS.":GOTO50000
110 GOTO10200
111 IFRND(85)=1THEN5000
112 IFZZ>2ANDRND(100)=1THEN5500
113 IFRND(27)=1THEN6000
120 T=T+1:GOSUB10000:IFVB$="ALA"THEN8000
121 IFVB=-1AND(NO>21ANDNO<30)THENVB=1
122 IFNO$="JES"THENPRINT"I SEE NO JESTER HERE.":GOTO120
123 IFVB$="CRO"THEN8010
124 IFVB$="TUR"THENM$="YOU HAVE USED"+STR$(T)+" TURNS SO FAR.":GOSUB1:GOTO120
125 IFVB<>30AND(VB>10ORVB=2ORVB=6)ANDNO$=""THENM$="YOU MUST SUPPLY A DIRECT OBJECT.":GOSUB1:GOTO120
126 IFVB$="HI"THEN8020
127 IFNO$="GUN"THENNO=1
128 IFVB=30THEN1000
129 IFNO$="BAG"THENNO=5
130 IFVB=-1ANDNO<>0AND(NO<22ORNO>29)THENPRINT"YOU MUST SUPPLY A VERB.":GOTO120
135 IFVB<1ANDNO=0THENM$="I DON'T KNOW HOW TO "+CHR$(34)+N1$+CHR$(34)+" ANYTHING.":GOSUB1:GOTO120
136 IFVB=6AND(NO$="EVE"ORNO$="ALL")THEN200
140 IFNO=0ANDVB>10THENM$="I DON'T KNOW WHAT A "+CHR$(34)+N1$+CHR$(34)+" IS.":GOSUB1:GOTO120
200 IFVB=30THEN1000
211 ONVBGOTO212,300,110,400,450,500,550,560,565,570,575,580,600,650,700,720,740,790,800,800,600,850,850,860,865,900,950,970,975
212 IF(NO<22ORNO>29)ANDNO$<>""THENPRINT"I DON'T KNOW HOW TO DO THAT.":GOTO120
213 IFNO$=""THENPRINT"WHERE?":GOTO120
214 IFNO>25THENNO=NO-4
215 NO=NO-22:IFP(CP,NO)=0THENM$="THERE IS NO WAY TO GO IN THAT DIRECTION.":GOSUB1:GOTO120
220 IFCP=1ANDNO=1ANDDF=0THENPRINT"THE CASTLE DOOR IS LOCKED.":GOTO120
225 IFCP=17ANDNO=1ANDCF=0THENM$="THE CRACK IS FAR TOO SMALL FOR YOU TO FOLLOW.":GOSUB1:GOTO120
299 CP=P(CP,NO):GOTO110
300 IFOB(NO,0)=-1THENPRINT"YOU'VE ALREADY GOT IT!":GOTO120
301 IFNO=0THENM$="WHAT'S A "+N1$+"?":GOSUB1:GOTO120
305 IFOB(NO,0)<>CPTHENM$=S1$:GOSUB1:GOTO120
310 IFNO=17ORNO=21ORNO=20ORNO=16THENM$=S2$:GOSUB1:GOTO120
330 IFZZ+1>7THENM$="YOU ARE UNABLE TO CARRY ANY MORE.":GOSUB1:GOTO120
331 IFNO=9ANDPF=0THEN333
332 GOTO335
333 M$="THE MOVEMENT OF THE PAINTING REVEALS A VAULT IN THE WALL. THE VAULT IS LOCKED.":GOSUB1
334 PF=1:OB(16,0)=CP:OB(9,0)=-1:ZZ=ZZ+1:GOTO120
335 PRINT"OKAY.":ZZ=ZZ+1:OB(NO,0)=-1:GOTO120
400 IFZZ=0THENPRINT"YOU ARE EMPTY HANDED.":GOTO120
410 PRINT"YOU ARE HOLDING THE FOLLOWING:":XX$="":FORI=1TOLO:IFOB(I,0)=-1THENXX$=XX$+OB$(I)+", "
420 NEXTI:M$=LEFT$(XX$,LEN(XX$)-2)+".":GOSUB1:GOTO120
450 J=0:FORI=1TOLO:IFOB(I,0)=1THENJ=J+OB(I,1)
451 NEXTI
460 GOSUB8050:GOTO120
500 IFNO<>0ANDOB(NO,0)<>-1THENPRINT"YOU'RE NOT CARRYING IT!":GOTO120
501 IFNO$<>"EVE"ANDNO$<>"ALL"THEN510
502 IFZZ=0THEN505
503 FORI=1TOLO:IFOB(I,0)=-1THENOB(I,0)=CP
504 NEXTI:ZZ=0:PRINT"OK.":GOTO120
505 PRINT"YOU'RE NOT CARRYING ANYTHING!":GOTO120
510 IFNO=0THENM$="I'VE NEVER HEARD OF A "+N1$+".":GOSUB1:GOTO120
520 OB(NO,0)=CP:PRINT"DROPPED.":ZZ=ZZ-1:GOTO120
550 M$="SORRY, THIS IS YOUR ADVENTURE, NOT MINE!":GOSUB1:GOTO120
560 GOSUB8040
561 OB(36,1)=CP:OB(37,1)=T:OB(38,1)=DF:OB(39,1)=VF:OB(40,1)=CF:OB(41,1)=PF:OB(42,1)=SF:OB(43,1)=WF:OB(44,1)=ZZ
562 CSAVE*OB,"SORDATA"
563 GOTO110
565 GOSUB8040
566 CLOAD*OB,"SORDATA"
567 CP=OB(36,1):T=OB(37,1):DF=OB(38,1):VF=OB(39,1):CF=OB(40,1):PF=OB(41,1):SF=OB(42,1):WF=OB(43,1):ZZ=OB(44,1)
568 GOTO110
570 INPUT"YOU REALLY WANT TO QUIT";QN$:IFLEFT$(QN$,1)<>"Y"THEN120
571 INPUT"ARE YOU SURE";Q2$:IFLEFT$(Q2$,1)<>"Y"THENPRINT"I DIDN'T THINK SO.":GOTO120
572 GOTO9000
575 M$="ALL RIGHT..."+N1$:GOSUB1:GOTO120
580 IFOB(NO,0)<>-1THENM$="YOU'RE NOT HOLDING IT HENCE IT IS IMPOSSIBLE TO DRINK IT.":GOSUB1:GOTO120
585 IFNO<>7THENM$=S2$:GOSUB1:GOTO120
590 M$="OKAY. GULP GULP GULK. SOMETHING SEEMS DIFFERENT NOW...":GOSUB1:ZZ=ZZ-1:OB(7,0)=0:CF=1:GOTO120
600 IFNO<>31ANDNO<>16ANDNO<>30THENM$="I KNOW NOT HOW TO OPEN SUCH A THING.":GOSUB1:GOTO120
605 IFNO=16ANDOB(16,0)<>CPTHENPRINT"WHAT VAULT?":GOTO120
610 IFNO=16ANDOB(2,0)<>-1THENPRINT"YOU DON'T HAVE THE KEY.":GOTO120
615 IFNO=16THENPRINT"THE VAULT IS OPEN.":VF=1:IFOB(15,0)=0THENPRINT"A GOLD BAR FALLS OUT!":OB(15,0)=CP
620 IFNO=16THEN120
621 IFNO=31THEN2000
625 IFCP<>1THENPRINT"WHAT DOOR?":GOTO120
630 IFOB(2,0)<>-1THENPRINT"YOU DON'T SEEM TO HAVE THE KEY.":GOTO120
635 PRINT"THE DOOR IS OPEN.":DF=1:GOTO120
650 IFOB(NO,0)<>-1THENPRINT"YOU ARE NOT CARRYING IT.":GOTO120
655 M$="I'VE KNOWN STRANGE PEOPLE, BUT ATTACKING "+OB$(NO)+"?":GOSUB1:GOTO120
700 IFNO=20THEN708
702 IFNO=17THENPRINT"WHAT KNIGHT?":GOTO120
704 IFNO=21THENPRINT"I SEE NO PIRATE HERE.":GOTO120
705 IFOB(I,0)<>-1THENM$="YOU ARE NOT CURRENTLY HOLDING THAT.":GOSUB1:GOTO120
706 PRINT"IT ISN'T ALIVE!":GOTO120
708 IFCP<>32THENPRINT"WHAT SORCERER?":GOTO120
710 IFOB(8,0)<>-1THENM$="YOU HAVE NO SWORD TO DO IT WITH. OH MY! THE SORCERER HAS JUST KILLED YOU!":GOSUB1:GOTO9000
712 M$="THE SORCERER DIED AND THEN VANISHED. HIS SCEPTER REMAINS.":GOSUB1:OB(14,0)=CP:OB(20,0)=0:GOTO120
720 IFOB(NO,0)<>-1THENPRINT"YOU DON'T HAVE THAT RIGHT NOW.":GOTO120
725 IFNO<>3THENM$="HOW DO YOU EXPECT TO READ "+OB$(NO)+"?":GOSUB1:GOTO120
730 M$="IT SAYS: 'MAGIC WORD-- ALAKAZAM.'":GOSUB1:GOTO120
740 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPTHENPRINT"I DON'T SEE THAT HERE.":GOTO120
745 IFOB(1,0)<>-1THENPRINT"YOU DON'T HAVE A GUN!":GOTO120
750 IFNO=17THENM$="THE BULLET DISSOLVES AS IT HITS THE ARMOUR.":GOSUB1:GOTO120
755 M$="THE GOLDEN BULLET FROM THE GOLDEN PISTOL VAPORIZES THAT.":GOSUB1
760 IFOB(NO,0)=-1THENZZ=ZZ-1
765 OB(NO,0)=0:GOTO120
790 IFOB(NO,0)=-1THENM$=S2$:GOSUB1:GOTO120
791 PRINT"YOU'RE NOT HOLDING IT.":GOTO120
800 IFNO=16ORNO=30ORNO=31THEN820
805 IFOB(NO,0)<>-1THENM$="YOU ARE NOT HOLDING THAT TO MY KNOWLEDGE.":GOSUB1:GOTO120
810 M$="I DON'T KNOW HOW TO CLOSE SUCH A THING.":GOSUB1:GOTO120
820 IFNO=16ANDOB(16,0)<>CPTHENPRINT"WHAT VAULT?":GOTO120
825 IFNO=16THENM$="THE VAULT IS CLOSED AND LOCKED.":GOSUB1:VF=0:GOTO120
826 IFNO=31THEN1100
830 IFCP<>1PRINT"WHAT DOOR?":GOTO120
835 PRINT"THE DOOR IS CLOSED AND LOCKED.":DF=0:GOTO120
850 IFOB(NO,0)<>-1THENPRINT"YOU'RE NOT HOLDING IT.":GOTO120
855 M$="NO, THESE ITEMS HAVE TO BE USED ON THE NEXT ADVENTURE, TOO.":GOSUB1:GOTO120
860 M$="THE VALUE OF THAT IS"+OB(NO,1)+"POINTS.":GOSUB1:GOTO120
865 IFOB(NO,0)<>-1THENPRINT"YOU DON'T SEEM TO HAVE IT.":GOTO120
870 IFNO<>8THENPRINT"WOW THIS IS FUN!":GOTO120
875 PRINT"WHOOOSH!":GOTO120
900 IFOB(NO,0)<>-1THENM$="YOU'RE NOT CURRENTLY CARRYING IT.":GOSUB1:GOTO120
910 IFNO<>8THENM$=S2$:GOSUB1:GOTO120
920 PRINT"IT IS ALREADY RAZOR SHARP.":GOTO120
950 IFOB(NO,0)<>-1THENM$="TU NE LE PORTES PAS (YOU'RE NOT CARRYING IT).":GOSUB1:GOTO120
955 IFNO<>19THENPRINT"HOW?":GOTO120
960 M$="VERY GOOD! NOW YOU MAY GO TO SECOND GRADE!":GOSUB1:GOTO120
970 PRINT"TRY DROP.":GOTO120
975 M$="I CAN ONLY TELL YOU WHAT IS IN THE ROOM WITH YOU. SORRY, I CANNOT FIND ANYTHING.":GOSUB1:GOTO120
1000 IFCP<>44THEN1002
1001 GOTO1010
1002 IFRND(2)=1THENPRINT"WHEEEE!":GOTO1010
1003 PRINT"ARE YOU ENJOYING YOURSELF?"
1010 IFCP<>44THEN120
1020 IFWF=0THENM$="THE WINDOW IS ONLY SLIGHTLY AJAR.":GOSUB1:GOTO120
1030 M$="OKAY...YOU FALL SAFELY IN THE PILE OF LEAVES.":GOSUB1:CP=48
1040 M$="YOU SEEM TO BE LOST IN A FOREST OF SPRUCE TREES.":GOSUB1:GOTO120
1100 IFCP<>44THENM$="WHAT WINDOW? I DON'T SEE A WINDOW!":GOSUB1:GOTO120
1110 IFWF=0THENPRINT"IT IS ALREADY CLOSED.":GOTO120
1120 M$="HAVING OPENED IT EARLIER, YOU FIND IT IMPOSSIBLE TO CLOSE.":GOSUB1:GOTO120
2000 IFCP<>44THENPRINT"WHAT WINDOW?":GOTO120
2010 IFWF=1THENPRINT"IT IS ALREADY OPEN.":GOTO120
2020 M$="THROUGH A GREAT EFFORT, YOU OPEN THE WINDOW. THERE IS A PILE OF LEAVES OUTSIDE THE WINDOW.":GOSUB1:WF=1:GOTO120
5000 M$="HOLY MOLY! A BLACK KNIGHT WALKED IN TO KILL YOU!":GOSUB1:GOSUB10000
5010 IFVB<>17ORNO<>17THEN5100
5020 IFOB(1,0)<>-1THENPRINT"YOU HAVE NO GUN!":GOTO5100
5030 PRINT"THE KNIGHT DISINTEGRATED.":GOTO120
5100 M$="THE KNIGHT IS RUNNING TOWARDS YOU...":GOSUB1:FORI=1TO3000:NEXTI
5110 IFRND(5)=2THENPRINT"IT KILLS YOU":GOTO9000
5120 M$="IT MISSES YOU AND CRASHES INTO THE WALL... WAIT! HE HAS DISAPPEARED!":GOSUB1:GOTO120
5500 M$="A PIRATE JUST APPEARED AND CHORTLES,":GOSUB1
5510 M$="'WELL SHIVER ME TIMBERS! NOT ANOTHER ONE! HAR HAR HAR, I'LL JUST SNATCH ALL THIS BOOTY AND HIDE IT DEEP IN THE MAZE."
5520 GOSUB1:M$="WITH THAT, HE STEALS ALL OF YOUR TREASURE!":GOSUB1
5550 FORI=1TOLO:IFOB(I,0)<>-1THEN5600
5560 IFOB(I,1)=0THEN5600
5570 OB(I,0)=RND(6)+34:ZZ=ZZ-1
5600 NEXTI:GOTO120
6000 M$="JINGLE JINGLE JINGLE! CHESTER THE JESTER POPS UP AND CHANTS,":GOSUB1
6010 M$=CHR$(34)+C$(RND(8)-1)+" TEE HEE HEE!"+CHR$(34):GOSUB1
6011 M$="AND THEN VANISHES IN A CLOUD OF PINK SMOKE.":GOSUB1
6020 GOTO120
8000 IFCP=32THENCP=17:GOTO110
8001 PRINT"NOTHING HAPPENED.":GOTO120
8010 IF(CP=52ORCP=53)THEN8013
8011 GOTO124
8013 IFCP=53THENCP=52:GOTO110
8012 CP=53:GOTO110
8020 IFRND(2)=1THENPRINT"HELLO":GOTO120
8021 M$="NICE WEATHER WE'VE BEEN HAVING LATELY.":GOSUB1:GOTO120
8040 CLS:INPUT"HIT enter WHEN TAPE IS READY";XX$:RETURN
8050 M$="OUT OF A MAXIMUM OF 225 POINTS, YOU HAVE SCORED"+STR$(J)+" POINTS.":GOSUB1
8051 IFJ>=225THENSOUND175,3:SOUND200,8
8052 RETURN
9000 J=0:FORI=1TOLO:IFOB(I,0)=1THENJ=J+OB(I,1)
9001 NEXTI
9010 GOSUB8050
9250 PRINT:INPUT"DO YOU WISH TO TRY AGAIN";AG$:AG$=LEFT$(AG$,1):IFAG$="Y"THENRUN
9251 IFAG$<>"N"THEN9250
9300 END
10000 INPUT"WHAT NOW";CM$
10010 N1$="":V1$="":NO=0:VB=0:NO$="":VB$=""
10060 CM=LEN(CM$):FORI=1TOCM:IFMID$(CM$,I,1)<>" "THENV1$=V1$+MID$(CM$,I,1):NEXTI
10070 VB$=LEFT$(V1$,3):FORI=1TONV:IFVB$(I)=VB$THENVB=I:I=NV:NEXTI:I=VB:GOTO10090
10071 NEXTI
10080 VB=-1:N1$=V1$:GOTO10100
10090 IFLEN(V1$)+1>=LEN(CM$)THENNO=0:RETURN
10091 N1$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
10100 NO$=LEFT$(N1$,3):FORI=1TONN:IFNO$(I)=NO$THENNO=I:I=NN:NEXTI:I=NO:RETURN
10101 NEXTI
10110 NO=0:RETURN
10200 CLS:M$="YOU'RE "+P$(CP):GOSUB1:FORI=1TOLO
10210 IFOB(I,0)=CPTHENM$=" *** THERE IS "+OB$(I)+" HERE.":GOSUB1
10220 NEXTI
10225 IFCP=1ANDDF=0THENPRINT"THE DOOR IS LOCKED."
10226 IFCP=18ANDVF=0ANDOB(16,0)=18THENPRINT"THE VAULT IS LOCKED."
10230 IFCP=17ANDCF=0THENM$="A SMALL CRACK LEADS SOUTHWARD.":GOSUB1
10235 IFCP=1ANDDF=1THENPRINT"THE DOOR IS OPEN."
10236 IFCP=18ANDVF=1ANDOB(16,0)=18THENPRINT"THE VAULT IS OPEN."
10237 IFCP=44ANDWF=0THENPRINT"THE WINDOW IS SLIGHTLY AJAR."
10240 IFCP=17ANDCF=1THENM$="A TOWERING HOLE LEADS SOUTHWARD.":GOSUB1
10245 IFCF=0THENP(17,1)=0
10247 IFCP=44ANDWF=1THENM$="THE WINDOW IS WIDE OPEN. THERE IS A RATHER LARGE PILE OF LEAVES OUTSIDE THE WINDOW.":GOSUB1
10250 K=0:XX$="YOU CAN GO ":FORI=0TO3:IFP(CP,I)=0THEN10280
10270 XX$=XX$+D$(I)+", ":K=1
10280 NEXTI:IFK=0THENM$=XX$+"NOWHERE!":GOSUB1:GOTO10300
10290 IFK=1THENM$=LEFT$(XX$,LEN(XX$)-2)+".":GOSUB1
10300 P(17,1)=18
10500 PRINT"--------------------------------";:GOTO111
50000 NP=53:LO=35:NN=31:NV=30:DIMP(NP,3),P$(NP),VB$(NV),NO$(NN),OB(44,1),OB$(LO)
50005 P$(1)="OUTSIDE A MEDIEVAL CASTLE.":DATA5,8,3,2,4,7,1,0,6,7,0,1
50010 P$=" SIDE OF THE CASTLE.":P$(2)="AT THE WEST"+P$:P$(3)="AT THE EAST"+P$
50015 P$(4)="IN A VAST FOREST STRETCHING OUT OF SIGHT.":DATA4,2,4,4,53,1,6,0
50020 P$(5)=P$(4):P$(6)=P$(4):DATA6,6,6,5
50025 P$(7)="AT THE SOUTH"+P$+" THERE IS A TOWER WITH A SMALL WINDOW IN THE FAR, FAR DISTANCE.":DATA2,0,0,0
50030 P$(8)="IN A SPLENDID CHAMBER 30 FEET HIGH.":DATA1,11,0,10
50040 P$(9)="IN A DAMP, MUSTY LIBRARY.":DATA0,10,0,0
50050 P$(10)="IN THE MASTER BEDROOM.":DATA9,0,8,0
50060 P$="IN A VAST CORRIDOR STRETCHING OUT OF SIGHT TO THE ":P$(11)=P$+"SOUTH.":DATA8,12,14,0
50065 P$(12)=P$+"NORTH AND SOUTH.":DATA11,13,15,0
50070 P$(13)=P$+"NORTH.":DATA12,0,16,17
50075 P$="IN A BEDROOM WITH A ":P$(14)=P$+"STONE FLOOR.":P$(15)=P$+"WOODEN FLOOR."
50076 P$(16)=P$+"DIRT FLOOR.":DATA0,0,0,11,0,0,0,12,0,33,0,13
50080 P$(17)="IN A DUSTY PANTRY. MARKINGS IN THE DUST READ, GO SOUTH TO DUNGEON.":DATA0,18,13,0
50085 P$(18)="IN A PRIMITIVE ART GALLERY.":DATA17,0,0,19
50090 P$(19)="IN A MAZE OF TWISTY LITTLE PASSAGES."
50095 P$(20)="IN A TWISTY MAZE OF LITTLE PASSAGES."
50100 P$(21)="IN A LITTLE MAZE OF TWISTY PASSAGES."
50105 P$(22)="IN A TWISTING MAZE OF LITTLE PASSAGES."
50110 P$(23)="IN A LITTLE TWISTY MAZE OF PASSAGES."
50115 DATA0,20,18,0,20,22,20,19,21,21,22,23,21,22,20,22,21,24,23,23
50120 P$(24)="IN THE KITCHEN.":DATA23,27,25,0,0,27,26,24,29,28,30,25
50125 P$(25)="IN THE DINING ROOM.":P$(26)="IN THE ALCOVE TO THE STUDY."
50126 P$(27)="IN THE OFFICE OF THE SORCERER (?)":DATA24,0,28,0
51027 P$(28)="IN THE DRAWING ROOM.":DATA0,0,0,27,0,26,0,0,0,31,0,26
51028 P$(29)="IN THE PARLOR.":P$(30)="IN THE STUDY.":P$(31)="IN THE SITTING ROOM.":DATA0,0,0,0,0,0,0,0
51029 P$(32)="IN THE DUNGEON!"
51030 FORI=1TONP:FORJ=0TO3:READP(I,J):NEXTJ,I:P(31,RND(4)-1)=32
51031 P$(33)="IN A PRIMEVAL CONFERENCE ROOM.":DATA16,0,35,0
51032 P$(34)="IN THE TOWER. IT OVERLOOKS A HUGE KINGDOM DOWN A MONSTROUS MOUNTAIN. THE GRASS IS GREENER THAN GREEN ITSELF."
51033 DATA0,0,36,0
51034 P$(35)="IN A MAZE OF TWISTY LITTLE PASSAGES, ALL ALIKE."
51035 FORI=36TO40:P$(I)=P$(35):NEXT:DATA36,35,35,33,38,34,37,35,36,0,39,0,40,39,39,36,38,37,39,38,41,38,0,0,42,40,0,0
51036 P$(41)="AT THE BRINK OF A HUGE PIT."
51037 P$(43)="IN A BARREN CUBICAL, WITH PASSAGES LEADING OFF TO THE NORTH AND SOUTH."
51038 P$(42)="IN THE PIT. A GOOD PASSAGE EXITS TO THE NORTH."
51039 P$(44)="AT THE END OF THE CASTLE. YOU CAN SEE FOREST OUT A SMALL WINDOW.":DATA43,0,41,0,44,42,0,0,0,0,0,0
51040 P$(45)="LOST IN A DARK FOREST MADE UP CHIEFLY OF SPRUCE TREES."
51041 FORI=46TO48:P$(I)=P$(45):NEXTI:DATA45,46,45,49,45,47,46,50,46,51,47,48,50,48,47,48
51042 P$(49)="ON AN OLD PATH MADE BY HORSES IN MEDIEVAL TIMES."
51043 P$(51)="IN THE MIDDLE OF A CLEARING. TO THE SOUTH IS A BRIDGE."
51044 P$(52)="ON THE NORTH SIDE OF THE BRIDGE":P$(53)="ON THE SOUTH SIDE OF THE BRIDGE."
51045 DATA0,50,45,0,49,48,46,0,47,52,0,0,51,53   
51046 DATA0,0,52,5,0,0,PIS,KEY,BOO,RUG,COI,SAP,LIQ,SWO,PAI,OPA,SIL,PEN,PIL
51047 DATA SCE,BAR,VAU,KNI,CUP,CRO,SOR,PIR,NOR,SOU,EAS,WES,N,S,E,W,DOO,WIN
51048 DATA GO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,SAY,DRI,OPE,HIT,KIL,REA
51049 DATA SHO,EAT,CLO,LOC,UNL,DES,BRE,VAL,SWI,SHA,WEA,THR,FIN,JUM
51050 FORI=1TONN:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI
51051 DATA A GOLD PISTOL,4,10,A SET OF KEYS,2,0,A BOOK ON SORCERY,9,0
51052 DATA A PERSIAN RUG,10,15,A BAG OF COINS,3,15,A PRICELESS SAPPHIRE,14,10
51053 DATA A FLASK OF LIQUID,15,0,A GOLDEN SWORD,42,10,A PRICELESS PAINTING,18,15
51054 DATA A GIANT OPAL,22,15,A SET OF SILVERWARE,24,15,A PLATINUM PEN,27,5
51055 DATA A VELVET PILLOW,43,20,THE SORCERER'S SCEPTER,0,50,A GOLD BAR,0,20
51056 DATA A VAULT IN THE WALL,0,0,A BLACK KNIGHT,0,0,A PEWTER CUP,25,15,A KING'S CROWN,30,10,XXX
51057 DATA32,0,AN EVIL PIRATE,0,0
51058 FORI=1TO21:READOB$(I),OB(I,0),OB(I,1):NEXTI
51090 DATA NORTH,SOUTH,EAST,WEST
51100 FORI=0TO3:READD$(I):NEXTI
51105 P$(19)=P$(19)+" A NOTE ON THE WALL READS: THIS IS NOT THE MAZE WHERE THE PIRATE HIDES HIS TREASURE. --GH"
51110 OB$(20)="AN EVIL SORCERER JUST WAITING TO CAST A SPELL ON YOU"
51115 P$(50)="AT THE END OF THE PATH, WITH FOREST SURROUNDING YOU IN ALL DIRECTIONS EXCEPT NORTH, WHERE YOU WERE BEFORE."
51120 P(50,3)=48:P$(47)=P$(47)+" TO THE SOUTH THERE SEEMS TO BE LIGHT."
51125 FORI=52TO53:P$(I)=P$(I)+" ACROSS THE BRIDGE IS MORE FOREST.":NEXT
51130 P(36,2)=37
51135 C$(0)="PAY ATTENTION IN THE MAZE, TO THE WORDING OF THE PHRASE! IF YOU DO YOU'LL BE UN-MAZED."
51140 C$(1)="GULP GULP GULP, DRINK IT DOWN, I'M NOT SUCH A STUPID CLOWN!"
51145 C$(2)="OUT OF A WINDOW YOU MAY FALL. JUST LOOK OUT, THAT IS ALL!"
51150 C$(3)="THE KNIGHTS CAN'T BE KILLED, AS FAR AS I KNOW. IF YOU FIND THEY CAN, PLEASE TELL ME SO!"
51155 C$(4)="DON'T BRING THE SWORD TO THE SORCERER'S PLACE. IF YOU DO, HE MIGHT CONFRONT YOU FACE TO FACE!"
51160 C$(5)="HOCUS POCUS, ALAKAZAM! DON'T READ THE BOOK OR YOU'LL BE SORRY! (YOU TRY TO RHYME ALAKAZAM)!"
51165 C$(6)="PASSAGES THAT SEEM ALL ALIKE CAN SEEM QUITE DIFFERENT, JUST HOPE YOU'RE RIGHT!"
51170 C$(7)="NOT ALL TREASURE LIES UNDER A ROOF. SOME MIGHT BE UNDER A TREE!"
60000 GOTO110
60001 REM JIM GERRIE 2013
