0 CLEAR450:DIMCC,ZZ,M$,Z$,N$,MC,A$(20),B$(50),T(50):MC=16384:GOTO9
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 ZZ=(PEEK(17024)AND1)*256+PEEK(17025)+MC-1:FORC2=1TOLEN(M$):POKEZZ+C2,ASC(MID$(M$,C2))AND63:NEXT:PRINT@ZZ-MC+C2,:RETURN
4 M$="TRY ANOTHER COMMAND.":GOTO8
5 PRINT@480,;:M$="HIT A KEY":GOSUB3
6 ON-(INKEY$="")GOTO6:CLS:RETURN
7 TM=TM+1
8 GOSUB1:GOTO462
9 CLS0:W=MC:X=MC:Y=MC+511:Z=MC+511
10 FORI=0TO15:POKEW+I,63:POKEX+32*I,63:POKEY-I,63:POKEZ-32*I,63:NEXTI:FORI=16TO31:POKEW+I,63:POKEY-I,63:NEXT
11 PRINT@32*6+10,;:M$="THE SEARCH FOR":GOSUB3
12 NV=NV+1:READA$(NV):IFA$(NV)="EV"THENNV=NV-1:GOTO14
13 GOTO12
14 PRINT@8*32+9,;:M$="SHERLOCK HOLMES!":GOSUB3
15 NM=NM+1:READB$(NM)
16 IFB$(NM)="PIPE"THENH(1)=NM
17 IFB$(NM)="TOBACCO"THENH(2)=NM
18 IFB$(NM)="MATCHES"THENH(3)=NM
19 IFB$(NM)="EM"THENNM=NM-1:GOTO21
20 GOTO15
21 CLS:GA=1:M$="YOUR NAME IS TOBY STUBBINS. IT'S THE YEAR 1893 AND YOU LIVE IN THE BACK ALLEYS OF THE "
22 M$=M$+"TEEMING METROPOLIS OF LONDON.":GOSUB1:M$="YOU'VE BEEN HOMELESS FOR ALL OF YOUR 15 YEARS, "
23 M$=M$+"BUT YOUR LIFE IS NOT AS HOPELESS AS OTHERS, FOR YOU ARE A BAKER "
24 M$=M$+"STREET IRREGULAR.":GOSUB1
25 M$="YOU ARE THE EYES AND EARS OF THE GREATEST DETECTIVE "
26 M$=M$+"WHO EVER LIVED - SHERLOCK HOLMES.":M$="AS A LIFELONG INHABITANT OF THE BLEAK SIDE, "
27 M$=M$+"YOU CAN ACCOMPLISH THINGS THAT ORDINARY LONDONERS CAN'T.":GOSUB1:GOSUB5
28 M$="YOU ARE THE CRUCIAL LINK BETWEEN THE UNDERWORLD AND THE MAN WHO HAS SOLVED "
29 M$=M$+"MORE 'UNSOLVABLE' CRIMES THAN ANYONE IN HISTORY.":GOSUB1
30 M$="BUT THIS MORNING, TRAGEDY STRUCK IN THE MOST UNLIKELY MANNER.":GOSUB1
31 M$="AS YOU APPROACHED NUMBER 221B BAKER STREET, YOU WITNESSED A COMMOTION LIKE YOU HAD "
32 M$=M$+"NEVER SEEN BEFORE. PEOPLE, HORSES, CARRIAGES, AND POLICE WERE IN A FRIGHTFUL STIR.":GOSUB1
33 GOSUB5:M$="YOU FOUGHT YOUR WAY THROUGH THE THRONG AND AT LAST SPOTTED DR. WATSON, "
34 M$=M$+"WHO WAS SPEAKING HEATEDLY WITH A YOUNG LIEUTENANT FROM THE SCOTLAND YARD.":GOSUB1
35 M$="WHEN HE SEES YOU, HE WAVES WEARILY AT THE CONFUSED OFFICER AND APPROACHES YOU.":GOSUB1
36 M$="'THANK HEAVENS IT'S YOU, TOBY. THE MOST AWFUL THING HAS HAPPENED. IT'S HOLMES - "
37 M$=M$+"HE'S BEEN KIDNAPPED!'":GOSUB1
38 GOSUB5
39 M$="'TOBY, YOU MUST FIND HIM. THE ONLY CLUE WE HAVE IS THIS SHOE. I'M SURE "
40 M$=M$+"HOLMES COULD MAKE A LOT MORE OF IT THAN I HAVE.":GOSUB1:M$="YOU SEE, IT HAS A PECULIAR TYPE "
41 M$=M$+"OF MUD CAKED AROUND THE SOLE.":GOSUB1
42 M$="I'M SURE ITS OWNER HAS SPENT TIME IN THE SEWERS RECENTLY.":GOSUB1
43 M$="I'M AFRAID THAT'S ALL I CAN MAKE OUT IN MY PRESENT STATE. TOBY, WE MUST FIND HIM - "
44 M$=M$+"AND QUICKLY.":GOSUB1:GOSUB5:M$="I HAVE REASON TO BELIEVE HE MAY HAVE BEEN DRUGGED. THE POLICE "
45 M$=M$+"WILL BE OF NO HELP - I'M AFRAID YOU'RE ON YOUR OWN. PLEASE, TOBY - FIND HOLMES!'":GOSUB1:GOSUB5
46 PRINT"AS YOU LOOK AROUND, YOU SEE:    - A PERPLEXED WATSON, HOLDING","  A SHOE"
47 PRINT"- A HANSOM CAB WAITING            PATIENTLY",,"- A CLOCK SHOWING 10 AM"
48 PRINT"- A POUND NOTE IN WATSON'S        POCKET",,"- THE LANDLADY HOLDING HOLMES'    PIPE"
49 PRINT"- A POLICEMAN SCRATCHING HIS      HEAD."
50 M$="WHAT WILL YOU DO, TOBY? YOUR POCKETS ARE EMPTY AND TIME IS RUNNING OUT.":GOSUB1:GOTO462
51 ONFWGOTO4,51,52,4,57,61,4,69,4,77,4,4,4,4,4,4,79
52 IFB$="BUTCHER"THEN92
53 IFB$="DRYGOODS"THEN95
54 IFB$="SMOKESHOP"THEN96
55 IFB$="SEWER"THEN90
56 GOTO235
57 IFB$="BUTCHER"THEN82
58 IFB$="SMOKESHOP"THEN82
59 IFB$="DRYGOODS"THEN82
60 GOTO4
61 IFB$="HAMMER"THENIT=3:GOTO84
62 IFB$="KNIFE"THENIT=3:GOTO84
63 IFB$="MATCHES"THENIT=2:GOTO84
64 IFB$="MEAT"THENIT=1:GOTO84
65 IFB$="PIPE"THENIT=2:GOTO84
66 IFB$="ROPE"THENIT=3:GOTO84
67 IFB$="TOBACCO"THENIT=2:GOTO84
68 GOTO4
69 IFB$="SEWER"THEN90
70 IFB$="221B"THEN91
71 IFB$="BUTCHER"THEN92
72 IFB$="HANSOM"THEN93
73 IFB$="DRYGOODS"THEN95
74 IFB$="SMOKESHOP"THEN96
75 IFB$="DUNGEON"THEN97
76 GOTO4
77 IFB$="AROUND"THEN98
78 GOTO4
79 IFB$="221B"THEN105
80 IFB$="SEWER"THEN90
81 GOTO4
82 IFSH=0THEN4
83 SH=0:M$="...YOU'RE BACK IN THE STREET.":GOTO7
84 IFSH<>ITTHEN240
85 IFT(SW)=3THEN234
86 IFNI=4ANDMY=5THENNI=3:GOTO89
87 IFNI>3THEN231
88 IFMY<5THEN232
89 M$="YOU NOW HAVE "+B$+".":GOSUB1:GOTO238
90 GA=3:M$="YOU'RE AT THE ENTRANCE TO THE SEWER. A TUNNEL LIES DIRECTLY AHEAD.":GOTO112
91 M$="YOU CAN EITHER WALK (SLOW) OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO8
92 M$="YOU'RE IN THE BUTCHER SHOP.":SH=1:GOTO107
93 IFMY<10THEN232
94 GA=1:PL=0:M$="YOU'RE IN HOLMES' FLAT. WATSON IS STILL THERE.":RM=1:MY=MY-10:BO=0:GOTO110
95 M$="YOU'RE IN THE DRYGOODS SHOP.":SH=3:GOTO107
96 M$="YOU'RE IN THE SMOKESHOP.":SH=2:GOTO107
97 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF THE SEWER.":GOTO8
98 IFSH>0THEN100
99 M$="YOU SEE SEVERAL SHOPS WHICH MAY PROVE USEFUL: A DRYGOODS, A BUTCHER AND A SMOKESHOP.":GOTO7
100 IFSH>1THEN102
101 M$="YOU SEE LOTS OF MEAT! 5 SHILLINGS PER PIECE.":GOTO7
102 IFSH>2THEN104
103 M$="YOU SEE PIPES, TOBACCO AND MATCHES. 5 SHILLINGS PER ITEM.":GOTO7
104 M$="THE FOLLOWING ITEMS CATCH YOUR EYE: A HAMMER, A KNIFE, AND A ROPE. THEY COST 5 SHILLINGS EACH.":GOTO7
105 GA=1:GOSUB106:GOTO113
106 M$="YOU ARE NOW IN HOLMES' FLAT. WATSON IS LYING ON THE DIVAN.":RM=1:PL=0:BO=0:RETURN
107 TM=TM+2:GOTO8
108 TM=TM+3:GOTO8
109 TM=TM+4:GOTO8
110 IFMY=0THENNI=NI-1
111 TM=TM+5:GOTO8
112 TM=TM+10:GOTO8
113 TM=TM+20:GOTO8
114 ONFWGOTO115,114,118,123,128,130,4,137,4,149,151,4,4,4,4,154,157
115 IFB$="POLICE"THEN162
116 IFB$="WATSON"THEN163
117 GOTO4
118 IFB$="ROOM"THEN165
119 IFB$="221B"THEN206
120 IFB$="DUNGEON"THEN200
121 IFB$="SEWER"THEN198
122 GOTO4
123 IFB$="BOOKCASE"THEN167
124 IFB$="CHEST"THEN170
125 IFB$="SHOE"THEN173
126 IFB$="221B"THEN206
127 GOTO4
128 IFB$="ROOM"THEN174
129 GOTO4
130 IFB$="MONEY"THEN175
131 IFB$="PIPE"THEN178
132 IFB$="SHOE"THEN183
133 IFB$="TOBACCO"THEN186
134 IFB$="LOCKET"THEN190
135 IFB$="MATCHES"THEN194
136 GOTO4
137 IFB$="SEWER"THEN198
138 IFB$="CHEAPSIDE"THEN199
139 IFB$="DUNGEON"THEN200
140 IFB$="STAIRS"THEN200
141 IFB$="HANSOM"THEN202
142 IFB$="ROOM"THEN204
143 IFB$="221B"THEN206
144 IFB$="CHEST"THEN209
145 IFB$="BOOKCASE"THEN211
146 IFB$="POLICE"THEN162
147 IFB$="WATSON"THEN214
148 GOTO4
149 IFB$="AROUND"THEN216
150 GOTO4
151 IFB$="DOOR"THEN220
152 IFB$="CHEST"THEN222
153 GOTO4
154 IFB$="MATCHES"THEN225
155 IFB$="HANSOM"THEN202
156 GOTO4
157 IFB$="CHEAPSIDE"THEN228
158 IFB$="SEWER"THEN198
159 IFB$="221B"THEN206
160 GOTO4
161 M$="LOTS OF BOOKS, SHEETS OF MUSIC, SOME MATCHES":RETURN
162 M$="THE POLICE ARE TOTALLY CONFUSED!":GOTO7
163 IFRM<>1THENM$="WATSON ISN'T MAKING MUCH SENSE.":GOTO7
164 M$="WATSON SUGGESTS THE SIDE ROOM.":GOTO7
165 IFPL<>2THEN204
166 M$="IT'S DARK IN HERE.":GOTO8
167 IFT(H(1))>2THEN169
168 GOSUB161:M$=M$+", A PIPE AND SOME TOBACCO.":GOTO7
169 GOSUB161:M$=M$+" AND SOME TOBACCO.":GOTO7
170 IFPL<4THEN240
171 IFPL=6THENM$="NOTHING MORE OF VALUE HERE.":GOTO7
172 M$="IT DOES NOT APPEAR TO BE LOCKED.":GOTO112
173 M$="YEP, IT SMELLS LIKE THE SEWER.":GOTO7
174 M$="...YOU'RE OUT.":PL=2:GOTO7
175 IFT(SW)>2THEN233
176 IFNI>3THEN231
177 M$="YOU NOW HAVE 1 POUND (20 SHILLINGS).":GOSUB1:MY=20:GOTO239
178 IFRM<>1THEN180
179 IFBO<>1THEN240
180 IFT(SW)>2THEN233
181 IFNI>3THEN231
182 M$="YOU HAVE HOLMES' PIPE.":GOTO239
183 IFT(SW)>2THEN233
184 IFNI>3THEN231
185 M$="YOU NOW HAVE A SMELLY SHOE.":GOTO239
186 IFRM<>1THEN4
187 IFBO<>1THEN240
188 IFNI>3THEN231
189 M$="YOU NOW HAVE A SLIPPER OF TOBACCO.":GOTO239
190 IFPL<>5THEN240
191 IFNI>3THEN231
192 IFT(SW)=3ORT(SW)=4THEN233
193 M$="YOU NOW HAVE THE ADLER LOCKET.":PL=6:GOTO239
194 IFBO=0THEN240
195 IFNI>3THEN231
196 IFT(SW)=3THENM$="YOU ALREADY HAVE SOME.":GOTO8
197 M$="YOU NOW HAVE HOLMES' MATCHES.":GOTO239
198 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF CHEAPSIDE.":GOTO8
199 M$="YOU CAN EITHER WALK OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO8
200 IFSP=0THENM$="YOU CAN'T GO DIRECTLY THERE.":GOTO8
201 RM=0:GA=4:PL=0:BO=0:M$="YOU'RE BACK IN THE DUNGEON.":GOTO109
202 IFMY<10THEN232
203 RM=0:BO=0:GOSUB229:MY=MY-10:GOTO110
204 IFRM<>1THEN4
205 M$="THE DOOR'S CLOSED.":PL=1:BO=0:GOTO8
206 IFSP=0THEN208
207 GOSUB106:GOTO107
208 M$="THE POLICE HAVE BARRED THE DOOR.":GOTO7
209 IFPL<>3THEN4
210 M$="YOU'RE STANDING IN FRONT OF THE CHEST.":PL=4:GOTO107
211 IFRM<>1THEN4
212 IFPL>2THEN240
213 M$="YOU'RE STANDING BEFORE THE BOOKCASE.":BO=1:GOTO7
214 IFRM<>1THEN4
215 M$="WATSON IS NAPPING FITFULLY.":GOTO7
216 IFRM<>1THEN4
217 IFPL>2THENM$="NOTHING MUCH HERE BUT THE CHEST.":GOTO7
218 M$="THE FOLLOWING THINGS CATCH YOUR EYE: WATSON NAPPING ON THE COUCH, A BOOKCASE WITH MANY INTERESTING ITEMS AND "
219 M$=M$+"THE DOOR TO A SIDE ROOM.":GOTO7
220 IFPL<>1THEN4
221 M$="IT'S DARK IN HERE.":PL=2:GOTO7
222 IFPL<4THEN240
223 IFPL>4THENM$="NOTHING MORE OF VALUE HERE.":GOTO7
224 M$="BINGO! UNDER SOME PAPERS IS A LOCKET.":PL=5:GOTO8
225 IFT(SW)<>3THEN237
226 IFPL<>2THEN4
227 M$="YOU SEE A SMALL CHEST IN THE CORNER.":PL=3:GOTO7
228 GOSUB229:GOTO113
229 GA=2:PL=0:M$="YOU'RE IN CHEAPSIDE, LONDON'S SHOPPING DISTRICT.":GOSUB1:RETURN
230 M$="YOU'VE GOT TO BE KIDDING.":GOTO8
231 M$="YOU HAVE TOO MANY ITEMS.":GOTO8
232 M$="YOU HAVEN'T THE FUNDS FOR THAT.":GOTO8
233 M$="THAT'S NO LONGER AVAILABLE.":GOTO8
234 M$="YOU ALREADY HAVE THAT ITEM.":GOTO7
235 M$="PLEASE BE MORE SPECIFIC.":GOTO8
236 M$="YOU CAN'T SEE MUCH IN THE DARK.":GOTO7
237 M$="YOU DON'T HAVE "+B$:GOTO8
238 MY=MY-5:IFMY=0THENNI=NI-1
239 T(SW)=3:NI=NI+1:TM=TM+1:GOTO8
240 M$="YOU CAN'T DO THAT... YET.":GOTO8
241 ONFWGOTO4,241,242,246,249,4,251,253,262,264,4,266,271,269,4,275,4
242 IFB$="TUNNEL"THEN280
243 IFB$="SEWER"THEN280
244 IFB$="DUNGEON"THEN284
245 GOTO4
246 IFB$="WALL"THEN286
247 IFB$="TUNNEL"THEN286
248 GOTO4
249 IFB$="TUNNEL"THEN290
250 GOTO4
251 IFB$="MEAT"THEN293
252 GOTO4
253 IFB$="DUNGEON"THEN284
254 IFB$="LEFT"THEN295
255 IFB$="RIGHT"THEN298
256 IFB$="STRAIGHT"THEN300
257 IFB$="CHEAPSIDE"THEN307
258 IFB$="BACK"THEN290
259 IFB$="TUNNEL"THEN280
260 IFB$="WALL"THEN310
261 GOTO4
262 IFB$="DOG"THEN313
263 GOTO4
264 IFB$="AROUND"THEN315
265 GOTO4
266 IFB$="WALL"THEN321
267 IFB$="DOOR"THEN321
268 GOTO4
269 IFB$="DOG"THEN324
270 GOTO4
271 IFB$="MEAT"THEN326
272 IFB$="KNIFE"THEN329
273 IFB$="HAMMER"THEN329
274 GOTO230
275 IFB$="HAMMER"THEN332
276 IFB$="MATCHES"THEN336
277 IFB$="ROPE"THEN339
278 IFB$="MEAT"THEN341
279 GOTO230
280 IFLI=1ANDJU=0THEN301
281 IFJU=0THENM$="IT'S TOO DARK TO SEE ANYTHING.":GOTO107
282 IFJU=1THENM$="WHICH ONE?":GOTO8
283 GOTO4
284 IFJU<>2ORWL<>1THEN4
285 GA=4:M$="YOU'VE ENTERED THE LONG-FORGOTTEN DUNGEON OF KING HENRY VIII.":GOTO7
286 IFJU<2THEN4
287 IFJU>2THENM$="IT'S SINGULARLY DULL.":GOTO7
288 IFDD=0THENM$="THAT DOG SURE LOOKS HUNGRY.":GOTO7
289 M$="AHA! THERE APPEARS TO BE A STONE DOOR IN THE WALL. WITH SOME EFFORT, YOU MAY BE ABLE TO GET THROUGH.":EW=1:GOTO7
290 IFJU<2THENM$="YOU'RE BACK AT THE SEWER ENTRANCE.":JU=0:GOTO110
291 IFJU>1THENM$="YOU'RE BACK AT THE FORK.":JU=1:GOTO108
292 GOTO4
293 IFJU<>2THEN4
294 M$="THE BEAST THANKS YOU, BUT HE'S STILL BLOCKING THE WAY.":T(SW)=4:GOTO7
295 IFJU<>1THEN4
296 JU=2:M$="YOU'RE WALKING DOWNHILL. THE TERRIBLE NOISE IS GETTING LOUDER BY THE MOMENT.":GOSUB1
297 M$="IT'S THE GROWLING OF A RAVENOUS DOG, WHO IS BLOCKING YOUR WAY.":GOTO107
298 IFJU<>1THEN4
299 JU=4:M$="YOU WALK FOR 2 MINUTES ONLY TO DISCOVER YOU'VE REACHED A DEAD END.":GOTO107
300 IFLI=0THEN236
301 IFJU=0THEN306
302 IFJU>2THEN4
303 IFDD=0ANDJU=2THEN340
304 IFDD=1ANDJU=2THENM$="THERE'S AN INTERSTING WALL HERE.":GOTO7
305 JU=3:M$="YOU WALK THROUGH WATER FOR 5 MINUTES BEFORE CONCLUDING THIS IS NOT THE RIGHT TUNNEL.":GOTO110
306 JU=1:M$="YOU HAVE REACHED A FORK: THERE ARE TUNNELS TO YOUR LEFT, RIGHT AND FRONT.":GOTO108
307 IFJU=0THEN309
308 IFJU>0THENM$="YOU MUST FIND YOUR WAY OUT OF THESE TUNNELS FIRST.":GOTO8
309 GA=2:LI=0:M$="YOU'RE BACK IN CHEAPSIDE.":GOTO112
310 IFJU<>2THEN4
311 IFDD=0THEN340
312 M$="YOU'RE STANDING AT THE WALL.":GOTO7
313 IFJU<>2THEN4
314 M$="THE DOG HAS BITTEN OFF THE END OF YOUR ARM. HOLMES IS DOOMED!":GOTO479
315 IFLI=0THEN236
316 IFJU=0THENM$="YOU SEE A LONG TUNNEL AHEAD.":GOTO7
317 IFJU=1THENM$="TUNNELS ARE LEFT, RIGHT AND STRAIGHT.":GOTO7
318 IFJU>2THENM$="NOTHING REVEALING HERE.":GOTO7
319 IFDD=0THENM$="THE DOG IS SURELY HIDING SOMETHING.":GOTO7
320 M$="THE WALL AT THE END OF THIS TUNNEL LOOKS AWFULLY INTERESTING.":GOTO7
321 IFEW<>1THEN4
322 M$="GOOD JOB, TOBY! YOU'RE THROUGH THE WALL. YOU SEE WHAT APPEARS TO BE A DUNGEON AND HEAR MOANING."
323 WL=1:GOTO108
324 IFJU<>2THEN4
325 M$="HOW?":GOTO8
326 IFJU<>2THEN4
327 IFT(SW)<>3THEN4
328 DD=1:M$="GOOD THINKING, TOBY! THE BEAST HAS TAKEN THE BAIT AND IS OUT OF YOUR WAY.":T(SW)=4:NI=NI-1:GOTO7
329 IFT(SW)<>3THEN4
330 IFJU<>2THEN230
331 M$="YOU PANIC AND MISS MATCH IT... HE'S ATTACKING. YOU TRY TO RUN, BUT YOU STUMBLE.":GOSUB1:GOTO340
332 IFT(SW)<>3THEN237
333 IFJU<>2THEN4
334 IFDD=0THENM$="HOW CRUEL CAN YOU GET?!":GOTO8
335 M$="TOO SMALL TO BE EFFECTIVE.":GOTO107
336 IFT(SW)<>3THEN237
337 IFLI=1THENM$="- THE MORE LIGHT THE BETTER.":GOTO7
338 LI=1:M$="OH! THAT'S BETTER. YOU MAY PROCEED.":GOTO7
339 IFT(SW)<>3THEN237
340 M$="VALIANT TRY, BUT THE DOG HAS FOUND YOUR NECK IRRESISTIBLE. YOU'RE DONE FOR.":GOTO479
341 IFJU<>2THEN4
342 GOTO235
343 ONFWGOTO344,343,347,4,350,353,355,358,366,369,373,371,379,377,381,385,4
344 IFB$="TROLL"THEN389
345 IFB$="HOLMES"THEN390
346 GOTO4
347 IFB$="CELL"THEN392
348 IFB$="SEWER"THEN409
349 GOTO4
350 IFB$="DUNGEON"THEN395
351 IFB$="STAIRS"THEN418
352 GOTO4
353 IFB$="KEYS"THEN399
354 GOTO4
355 IFB$="MONEY"THEN403
356 IFB$="LOCKET"THEN405
357 GOTO4
358 IFB$="SEWER"THEN409
359 IFB$="STAIRS"THEN411
360 IFB$="221B"THEN412
361 IFB$="CELL"THEN414
362 IFB$="TROLL"THEN417
363 IFB$="UP"THEN418
364 IFB$="HOLMES"THEN392
365 GOTO4
366 IFB$="TROLL"THEN420
367 IFB$="CHAIN"THEN422
368 GOTO4
369 IFB$="AROUND"THEN423
370 GOTO4
371 IFB$="COVER"THEN431
372 GOTO4
373 IFB$="DOOR"THEN429
374 IFB$="CELL"THEN429
375 IFB$="COVER"THEN431
376 GOTO4
377 IFB$="TROLL"THEN433
378 GOTO4
379 IFB$="KNIFE"THEN435
380 IFB$="HAMMER"THEN435
381 IFB$="CHAIN"THEN438
382 IFB$="DOOR"THEN438
383 IFB$="CELL"THEN438
384 GOTO4
385 IFB$="HAMMER"THEN440
386 IFB$="ROPE"THEN443
387 IFB$="KNIFE"THEN443
388 GOTO230
389 M$="THE TROLL HAS NO TONGUE. HIS GESTURES SADLY HAVE NO MEANING FOR YOU.":GOTO7
390 M$="HOLMES SAYS ONLY ONE THING WILL SATISFY THE TROLL: A GOLD LOCKET WITH A PICTURE OF IRENE ADLER, WHICH "
391 M$=M$+"IS SOMEWHERE IN 221B. HE IS TOO WEAK TO SAY ANY MORE.":GOTO7
392 IFSR=1THEN4
393 IFCE<2THEN240
394 M$="YOU'RE IN THE CELL. YOU RUSH TO HOLMES' SIDE AND FIND THAT HE'S STILL ALIVE.":GOSUB1:GOTO446
395 IFSR=0THEN409
396 IFSR<>2THENM$="YOU'RE ALMOST THERE.":GOTO8
397 SP=1:M$="IT'S INCREDIBLE! YOU'RE ON BAKER STREET, STANDING NEAR 221B.":GA=1:GOTO7
398 GOTO4
399 IFT(SW)=3ORT(SW)=4THEN233
400 IFTR=0THEN4
401 IFLK=0THENM$="THE TROLL'S PRICE HAS NOT BEEN MET.":GOTO8
402 M$="THE MELANCHOLY TROLL QUIETLY HANDS OVER THE KEYS.":KY=1:GOTO7
403 IFTR=0THEN4
404 M$="HE DOESN'T WANT MONEY.":GOTO7
405 IFTR=0THEN240
406 IFT(SW)<>3THEN237
407 LK=1:M$="HE LOOKS AT THE PICTURE AND TEARS COME TO HIS EYES. SHE WAS THE ONLY WOMAN WHO EVER LOVED HIM."
408 NI=NI-1:T(SW)=4:GOTO7
409 TR=0:SR=0:M$="MUCH TO YOUR DISMAY, THE WALL HAS CLOSED BACK UP AND CANNOT BE MOVED. "
410 M$=M$+"BUT NEVER FEAR - THERE IS A WAY OUT.":GOTO7
411 TR=0:SR=1:M$="YOU'RE AT THE FOOT OF THE STAIRS.":GOTO7
412 IFSP=1THEN105
413 M$="THERE'S NOT ENOUGH TIME TO GO BACK THE WAY YOU CAME. THE TROLL IS GETTING IMPATIENT.":GOTO8
414 SR=0:TR=0:IFCE=0THENM$="THAT CHAIN LOOKS PRETTY STRONG.":GOTO7
415 IFCE=1THENM$="THE CHAIN IS OFF THE CELL DOOR.":GOTO7
416 GOTO394
417 SR=0:TR=1:M$="YOU'RE STANDING BEFORE THE TROLL.":GOTO7
418 IFSR<>1THEN240
419 M$="YOU'VE REACHED THE TOP AND FIND A MANHOLE COVER. BEYOND IT, YOU HEAR STREET SOUNDS.":GOTO107
420 IFTR=0THENM$="YOU'RE TOO FAR AWAY.":GOTO8
421 M$="THERE'S NO NEED FOR VIOLENCE.":GOTO8
422 M$="WITH WHAT?":GOTO8
423 IFSR=1ORCE>0THEN427
424 M$="YOU SEE A CELL WITH A LOCKED CHAIN AROUND ITS BARS. INSIDE IS THE BARELY CONSCIOUS FIGURE OF SHERLOCK HOLMES.":GOSUB1
425 M$="IN A NEARBY CORNER SITS A MENACING TROLL. HE HAS THE KEYS TO THE CELL, BUT YOU DON'T KNOW HIS PRICE. YOU ALSO "
426 M$=M$+"SEE A SET OF STAIRS.":GOTO107
427 IFCE>0THENM$="HURRY! HOLMES IS GROANING.":GOTO7
428 M$="AS YOU LOOK UP, YOU SEE A CRACK OF LIGHT AT THE TOP. LOOKS LIKE A WAY OUT.":GOTO107
429 IFCE=0THENM$="THE CHAIN IS STILL LOCKED.":GOTO8
430 M$="THE CELL DOOR IS OPEN.":CE=2:GOTO8
431 IFSR=0THEN4
432 M$="THE COVER IS NOW OPEN AND YOU MAY EXIT.":SR=2:GOTO7
433 IFTR=0THEN4
434 M$="WITH WHAT?":GOTO8
435 IFT(SW)<>3THEN237
436 IFTR<>1THEN4
437 M$="HE'S TOO QUICK FOR YOU. HE'S THROWN THE KEYS AWAY. BAKER ST. WILL NEVER BE THE SAME.":GOTO479
438 IFKY=0THEN240
439 M$="...THE CHAIN IS OFF THE DOOR.":CE=1:GOTO7
440 IFT(SW)<>3THEN237
441 IFTR=1THENM$="HE'S REALLY NOT SUCH A BAD FELLOW.":GOTO8
442 M$="IT'S TOO STRONG FOR THAT.":GOTO7
443 IFT(SW)<>3THEN237
444 IFTR=0THEN4
445 M$="THE TROLL WILL THROW THE KEYS DOWN A BOTTOMLESS PIT IF YOU GET ANY CLOSER.":GOSUB1:GOTO8
446 SN=1:IFTM>200THEN458
447 IFTM>150THEN459
448 IFTM>100THEN461
449 M$="YOU'VE DONE A MARVELOUS JOB, TOBY. THE WORLD IS GRATEFUL TO YOU FOR RESCUING SHERLOCK HOLMES.":GOSUB1
450 IFT(H(1))=3ANDT(H(2))=3ANDT(H(3))=3THEN454
451 IFT(H(1))=3ANDT(H(2))=3THEN455
452 IFT(H(1))=3THEN456
453 M$="TOO BAD YOU FORGOT HIS BELOVED PIPE. BUT HE'LL SURELY RECOVER ANYWAY.":GOTO479
454 GOSUB457:M$=M$+", TOBACCO AND MATCHES. THESE WILL MAKE HIS DAY. JOB WELL DONE.":GOTO479
455 GOSUB457:M$=M$+" AND TOBACCO. ALAS, YOU'VE DROPPED YOUR MATCHES. PERHAPS THE TROLL HAS SOME.":GOTO479
456 GOSUB457:M$=M$+". ALAS, YOU BROUGHT NO TOBACCO, BUT AT LEAST HE HAS A FAMILIAR OBJECT TO CHEW ON.":GOTO479
457 M$="YOU EVEN REMEMBERED HIS PIPE":SN=2:RETURN
458 M$="BUT, SAD TO SAY, YOU'RE TOO LATE. HOLMES HAS SUFFERED IRREPARABLE BRAIN DAMAGE. SORRY, YOUNG FELLOW.":GOTO479
459 M$="BUT, DEAR TOBY, DESPITE YOUR BEST EFFORTS, TIME HAS BEEN YOUR ENEMY. THE GREAT DETECTIVE IS NEAR DEATH.":GOSUB1
460 M$="HE MAY EVENTUALLY RECOVER, BUT THE PROGNOSIS IS GRIM.":GOTO479
461 M$="TOBY, YOU'RE BARELY IN TIME. HOLMES' CONDITION IS PERILOUS, BUT WITH WATSON'S HELP, HE SHOULD RECOVER.":GOTO479
462 A$="":B$="":INPUT"WHAT NOW";I$:PRINT:LE=LEN(I$):IFI$="HELP"THENGOSUB480:GOTO462
463 FORK=1TOLE:IFMID$(I$,K,1)=" "THENA$=LEFT$(I$,K-1):B$=RIGHT$(I$,LE-K):K=LE
464 NEXTK:FORI=1TONV:IFA$<>A$(I)THENNEXTI
465 IFI=NV+1THEN474
466 FW=I:I=NV:NEXTI:FORJ=1TONM:IFB$<>B$(J)THENNEXTJ
467 IFJ=NM+1THEN474
468 SW=J:J=NM:NEXTJ:IFA$="DROP"THENT=T(SW):GOTO470
469 ONGAGOTO114,51,241,343
470 IFT<>3THENM$="YOU DON'T HAVE THAT ITEM.":GOSUB1:GOTO462
471 M$="YOU NO LONGER HAVE "+B$+".":GOSUB1:NI=NI-1:T(SW)=4
472 IFB$="MONEY"THENMY=0
473 GOTO462
474 PRINT"TYPE HELP":GOTO462
475 DATAASK,DROP,ENTER,EXAMINE,EXIT,GET,GIVE,GO,HIT,LOOK,OPEN,PUSH,THROW,TIE,UNLOCK,USE,WALK,EV
476 DATA221B,AROUND,BACK,BOOKCASE,BUTCHER,CELL,CHAIN,CHEAPSIDE,CHEST,COVER,DOG,DOOR
477 DATADRYGOODS,DUNGEON,HAMMER,HANSOM,HOLMES,KEYS,KNIFE,LEFT,LIST,LOCKET,MATCHES,MEAT,MONEY,PIPE,POLICE
478 DATARIGHT,ROOM,ROPE,HELP,SEWER,SHOE,SMOKESHOP,STAIRS,STRAIGHT,TOBACCO,TROLL,TUNNEL,UP,WALL,WATSON,EM
479 GOSUB1:GOSUB5:ONSNGOTO486,483:GOTO486
480 CLS:PRINTTAB(13)"VERBS":K=0:FORI=1TONV:K=K+1:PRINTA$(I)TAB(10*K);:IFK=3THENK=0:PRINT
481 NEXTI:GOSUB5:PRINTTAB(11)"MODIFIERS":K=0:FORI=1TONM:K=K+1:PRINTB$(I)TAB(10*K);:IFK=3THENK=0:PRINT
482 NEXTI:GOSUB5:RETURN
483 M$="        ":PRINTM$"èèèèèèèüüü",M$"èèèèèèüüüüü",M$"èèèèèüüüüüüü",M$"èèèèüüüüüüüüü",M$"èèèüüüüüüüüüüü"
484 PRINTM$"èèèèèüüüüüüüüü",M$"èèèèüüüüüüüüüüü",M$"èèèüüüüüüüüüü",M$"èèèèèüüüüüüüü",M$"èèèèüüüüüüüüüü"
485 PRINTM$"üüèüèüüüüüüüüü",M$"üüüüèèèèüüüüüüü",M$"èüüèèèèèüüüüüüü",M$"èèèèèèèèüüüüüüüü"
486 END
