0 CLEAR450:DIMCC,ZZ,M$,N$,MC,A$(20),B$(50),T(50):MC=16384:GOTO18
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:N$=MID$(M$,CC,1):IFN$=" "ORN$=""THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFN$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
8 M$="TRY ANOTHER COMMAND.":GOTO16
10 PRINT@480,;:M$="HIT A KEY":GOSUB7
12 ON-(INKEY$="")GOTO12:CLS:RETURN
14 TM=TM+1
16 GOSUB1:GOTO962
18 CLS0:W=MC:X=MC:Y=MC+511:Z=MC+511
20 FORI=0TO15:POKEW+I,63:POKEX+32*I,63:POKEY-I,63:POKEZ-32*I,63:NEXTI:FORI=16TO31:POKEW+I,63:POKEY-I,63:NEXT
22 PRINT@32*6+10,;:M$="THE SEARCH FOR":GOSUB7
24 NV=NV+1:READA$(NV):IFA$(NV)="EV"THENNV=NV-1:GOTO30
26 GOTO24
30 PRINT@8*32+9,;:M$="SHERLOCK  HOLMES":GOSUB7
32 NM=NM+1:READB$(NM)
34 IFB$(NM)="PIPE"THENH(1)=NM
36 IFB$(NM)="TOBACCO"THENH(2)=NM
38 IFB$(NM)="MATCHES"THENH(3)=NM
40 IFB$(NM)="EM"THENNM=NM-1:GOTO50
42 GOTO32
50 CLS:GA=1:M$="YOUR NAME IS TOBY STUBBINS. IT'S THE YEAR 1893 AND YOU LIVE IN THE BACK ALLEYS OF THE "
66 M$=M$+"TEEMING METROPOLIS OF LONDON.":GOSUB1:M$="YOU'VE BEEN HOMELESS FOR ALL OF YOUR 15 YEARS, "
68 M$=M$+"BUT YOUR LIFE IS NOT AS HOPELESS AS OTHERS, FOR YOU ARE A BAKER "
70 M$=M$+"STREET IRREGULAR.":GOSUB1
72 M$="YOU ARE THE EYES AND EARS OF THE GREATEST DETECTIVE "
74 M$=M$+"WHO EVER LIVED - SHERLOCK HOLMES.":M$="AS A LIFELONG INHABITANT OF THE BLEAK SIDE, "
76 M$=M$+"YOU CAN ACCOMPLISH THINGS THAT ORDINARY LONDONERS CAN'T.":GOSUB1:GOSUB10
78 M$="YOU ARE THE CRUCIAL LINK BETWEEN THE UNDERWORLD AND THE MAN WHO HAS SOLVED "
80 M$=M$+"MORE 'UNSOLVABLE' CRIMES THAN ANYONE IN HISTORY.":GOSUB1
82 M$="BUT THIS MORNING, TRAGEDY STRUCK IN THE MOST UNLIKELY MANNER.":GOSUB1
84 M$="AS YOU APPROACHED NUMBER 221B BAKER STREET, YOU WITNESSED A COMMOTION LIKE YOU HAD "
86 M$=M$+"NEVER SEEN BEFORE. PEOPLE, HORSES, CARRIAGES, AND POLICE WERE IN A FRIGHTFUL STIR.":GOSUB1
88 GOSUB10:M$="YOU FOUGHT YOUR WAY THROUGH THE THRONG AND AT LAST SPOTTED DR. WATSON, "
90 M$=M$+"WHO WAS SPEAKING HEATEDLY WITH A YOUNG LIEUTENANT FROM THE SCOTLAND YARD.":GOSUB1
92 M$="WHEN HE SEES YOU, HE WAVES WEARILY AT THE CONFUSED OFFICER AND APPROACHES YOU.":GOSUB1
96 M$="'THANK HEAVENS IT'S YOU, TOBY. THE MOST AWFUL THING HAS HAPPENED. IT'S HOLMES - "
98 M$=M$+"HE'S BEEN KIDNAPPED!'":GOSUB1
100 GOSUB10
102 M$="'TOBY, YOU MUST FIND HIM. THE ONLY CLUE WE HAVE IS THIS SHOE. I'M SURE "
104 M$=M$+"HOLMES COULD MAKE A LOT MORE OF IT THAN I HAVE.":GOSUB1:M$="YOU SEE, IT HAS A PECULIAR TYPE "
106 M$=M$+"OF MUD CAKED AROUND THE SOLE.":GOSUB1
108 M$="I'M SURE ITS OWNER HAS SPENT TIME IN THE SEWERS RECENTLY.":GOSUB1
110 M$="I'M AFRAID THAT'S ALL I CAN MAKE OUT IN MY PRESENT STATE. TOBY, WE MUST FIND HIM - "
112 M$=M$+"AND QUICKLY.":GOSUB1:GOSUB10:M$="I HAVE REASON TO BELIEVE HE MAY HAVE BEEN DRUGGED. THE POLICE "
114 M$=M$+"WILL BE OF NO HELP - I'M AFRAID YOU'RE ON YOUR OWN. PLEASE, TOBY - FIND HOLMES!'":GOSUB1:GOSUB10
116 PRINT"AS YOU LOOK AROUND, YOU SEE:    - A PERPLEXED WATSON, HOLDING","  A SHOE"
120 PRINT"- A HANSOM CAB WAITING            PATIENTLY",,"- A CLOCK SHOWING 10 AM"
121 PRINT"- A POUND NOTE IN WATSON'S        POCKET",,"- THE LANDLADY HOLDING HOLMES'    PIPE"
122 PRINT"- A POLICEMAN SCRATCHING HIS      HEAD."
124 M$="WHAT WILL YOU DO, TOBY? YOUR POCKETS ARE EMPTY AND TIME IS RUNNING OUT.":GOSUB1:GOTO962
126 ONFWGOTO8,126,128,8,138,146,8,162,8,178,8,8,8,8,8,8,182
128 IFB$="BUTCHER"THEN208
130 IFB$="DRYGOODS"THEN214
132 IFB$="SMOKESHOP"THEN216
134 IFB$="SEWER"THEN204
136 GOTO494
138 IFB$="BUTCHER"THEN188
140 IFB$="SMOKESHOP"THEN188
142 IFB$="DRYGOODS"THEN188
144 GOTO8
146 IFB$="HAMMER"THENIT=3:GOTO192
148 IFB$="KNIFE"THENIT=3:GOTO192
150 IFB$="MATCHES"THENIT=2:GOTO192
152 IFB$="MEAT"THENIT=1:GOTO192
154 IFB$="PIPE"THENIT=2:GOTO192
156 IFB$="ROPE"THENIT=3:GOTO192
158 IFB$="TOBACCO"THENIT=2:GOTO192
160 GOTO8
162 IFB$="SEWER"THEN204
164 IFB$="221B"THEN206
166 IFB$="BUTCHER"THEN208
168 IFB$="HANSOM"THEN210
170 IFB$="DRYGOODS"THEN214
172 IFB$="SMOKESHOP"THEN216
174 IFB$="DUNGEON"THEN218
176 GOTO8
178 IFB$="AROUND"THEN220
180 GOTO8
182 IFB$="221B"THEN236
184 IFB$="SEWER"THEN204
186 GOTO8
188 IFSH=0THEN8
190 SH=0:M$="...YOU'RE BACK IN THE STREET.":GOTO14
192 IFSH<>ITTHEN504
194 IFT(SW)=3THEN492
196 IFNI=4ANDMY=5THENNI=3:GOTO202
198 IFNI>3THEN486
200 IFMY<5THEN488
202 M$="YOU NOW HAVE "+B$+".":GOSUB1:GOTO500
204 GA=3:M$="YOU'RE AT THE ENTRANCE TO THE SEWER. A TUNNEL LIES DIRECTLY AHEAD.":GOTO250
206 M$="YOU CAN EITHER WALK (SLOW) OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO16
208 M$="YOU'RE IN THE BUTCHER SHOP.":SH=1:GOTO240
210 IFMY<10THEN488
212 GA=1:PL=0:M$="YOU'RE IN HOLMES' FLAT. WATSON IS STILL THERE.":RM=1:MY=MY-10:BO=0:GOTO246
214 M$="YOU'RE IN THE DRYGOODS SHOP.":SH=3:GOTO240
216 M$="YOU'RE IN THE SMOKESHOP.":SH=2:GOTO240
218 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF THE SEWER.":GOTO16
220 IFSH>0THEN224
222 M$="YOU SEE SEVERAL SHOPS WHICH MAY PROVE USEFUL: A DRYGOODS, A BUTCHER AND A SMOKESHOP.":GOTO14
224 IFSH>1THEN228
226 M$="YOU SEE LOTS OF MEAT! 5 SHILLINGS PER PIECE.":GOTO14
228 IFSH>2THEN232
230 M$="YOU SEE PIPES, TOBACCO AND MATCHES. 5 SHILLINGS PER ITEM.":GOTO14
232 M$="THE FOLLOWING ITEMS CATCH YOUR EYE: A HAMMER, A KNIFE, AND A ROPE. THEY COST 5 SHILLINGS EACH.":GOTO14
236 GA=1:GOSUB238:GOTO252
238 M$="YOU ARE NOW IN HOLMES' FLAT. WATSON IS LYING ON THE DIVAN.":RM=1:PL=0:BO=0:RETURN
240 TM=TM+2:GOTO16
242 TM=TM+3:GOTO16
244 TM=TM+4:GOTO16
246 IFMY=0THENNI=NI-1
248 TM=TM+5:GOTO16
250 TM=TM+10:GOTO16
252 TM=TM+20:GOTO16
254 ONFWGOTO256,254,262,272,282,286,8,300,8,324,328,8,8,8,8,334,340
256 IFB$="POLICE"THEN348
258 IFB$="WATSON"THEN350
260 GOTO8
262 IFB$="ROOM"THEN354
264 IFB$="221B"THEN436
266 IFB$="DUNGEON"THEN424
268 IFB$="SEWER"THEN420
270 GOTO8
272 IFB$="BOOKCASE"THEN358
274 IFB$="CHEST"THEN364
276 IFB$="SHOE"THEN370
278 IFB$="221B"THEN436
280 GOTO8
282 IFB$="ROOM"THEN372
284 GOTO8
286 IFB$="MONEY"THEN374
288 IFB$="PIPE"THEN380
290 IFB$="SHOE"THEN390
292 IFB$="TOBACCO"THEN396
294 IFB$="LOCKET"THEN404
296 IFB$="MATCHES"THEN412
298 GOTO8
300 IFB$="SEWER"THEN420
302 IFB$="CHEAPSIDE"THEN422
304 IFB$="DUNGEON"THEN424
306 IFB$="STAIRS"THEN424
308 IFB$="HANSOM"THEN428
310 IFB$="ROOM"THEN432
312 IFB$="221B"THEN436
314 IFB$="CHEST"THEN442
316 IFB$="BOOKCASE"THEN446
318 IFB$="POLICE"THEN348
320 IFB$="WATSON"THEN452
322 GOTO8
324 IFB$="AROUND"THEN456
326 GOTO8
328 IFB$="DOOR"THEN464
330 IFB$="CHEST"THEN468
332 GOTO8
334 IFB$="MATCHES"THEN474
336 IFB$="HANSOM"THEN428
338 GOTO8
340 IFB$="CHEAPSIDE"THEN480
342 IFB$="SEWER"THEN420
344 IFB$="221B"THEN436
346 GOTO8
347 M$="LOTS OF BOOKS, SHEETS OF MUSIC, SOME MATCHES":RETURN
348 M$="THE POLICE ARE TOTALLY CONFUSED!":GOTO14
350 IFRM<>1THENM$="WATSON ISN'T MAKING MUCH SENSE.":GOTO14
352 M$="WATSON SUGGESTS THE SIDE ROOM.":GOTO14
354 IFPL<>2THEN432
356 M$="IT'S DARK IN HERE.":GOTO16
358 IFT(H(1))>2THEN362
360 GOSUB347:M$=M$+", A PIPE AND SOME TOBACCO.":GOTO14
362 GOSUB347:M$=M$+" AND SOME TOBACCO.":GOTO14
364 IFPL<4THEN504
366 IFPL=6THENM$="NOTHING MORE OF VALUE HERE.":GOTO14
368 M$="IT DOES NOT APPEAR TO BE LOCKED.":GOTO250
370 M$="YEP, IT SMELLS LIKE THE SEWER.":GOTO14
372 M$="...YOU'RE OUT.":PL=2:GOTO14
374 IFT(SW)>2THEN490
376 IFNI>3THEN486
378 M$="YOU NOW HAVE 1 POUND (20 SHILLINGS).":GOSUB1:MY=20:GOTO502
380 IFRM<>1THEN384
382 IFBO<>1THEN504
384 IFT(SW)>2THEN490
386 IFNI>3THEN486
388 M$="YOU HAVE HOLMES' PIPE.":GOTO502
390 IFT(SW)>2THEN490
392 IFNI>3THEN486
394 M$="YOU NOW HAVE A SMELLY SHOE.":GOTO502
396 IFRM<>1THEN8
398 IFBO<>1THEN504
400 IFNI>3THEN486
402 M$="YOU NOW HAVE A SLIPPER OF TOBACCO.":GOTO502
404 IFPL<>5THEN504
406 IFNI>3THEN486
408 IFT(SW)=3ORT(SW)=4THEN490
410 M$="YOU NOW HAVE THE ADLER LOCKET.":PL=6:GOTO502
412 IFBO=0THEN504
414 IFNI>3THEN486
416 IFT(SW)=3THENM$="YOU ALREADY HAVE SOME.":GOTO16
418 M$="YOU NOW HAVE HOLMES' MATCHES.":GOTO502
420 M$="YOU CAN'T GO DIRECTLY THERE. YOU MUST GO BY WAY OF CHEAPSIDE.":GOTO16
422 M$="YOU CAN EITHER WALK OR TAKE A HANSOM CAB (10 SHILLINGS)":GOTO16
424 IFSP=0THENM$="YOU CAN'T GO DIRECTLY THERE.":GOTO16
426 RM=0:GA=4:PL=0:BO=0:M$="YOU'RE BACK IN THE DUNGEON.":GOTO244
428 IFMY<10THEN488
430 RM=0:BO=0:GOSUB483:MY=MY-10:GOTO246
432 IFRM<>1THEN8
434 M$="THE DOOR'S CLOSED.":PL=1:BO=0:GOTO16
436 IFSP=0THEN440
438 GOSUB238:GOTO240
440 M$="THE POLICE HAVE BARRED THE DOOR.":GOTO14
442 IFPL<>3THEN8
444 M$="YOU'RE STANDING IN FRONT OF THE CHEST.":PL=4:GOTO240
446 IFRM<>1THEN8
448 IFPL>2THEN504
450 M$="YOU'RE STANDING BEFORE THE BOOKCASE.":BO=1:GOTO14
452 IFRM<>1THEN8
454 M$="WATSON IS NAPPING FITFULLY.":GOTO14
456 IFRM<>1THEN8
458 IFPL>2THENM$="NOTHING MUCH HERE BUT THE CHEST.":GOTO14
460 M$="THE FOLLOWING THINGS CATCH YOUR EYE: WATSON NAPPING ON THE COUCH, A BOOKCASE WITH MANY INTERESTING ITEMS AND "
462 M$=M$+"THE DOOR TO A SIDE ROOM.":GOTO14
464 IFPL<>1THEN8
466 M$="IT'S DARK IN HERE.":PL=2:GOTO14
468 IFPL<4THEN504
470 IFPL>4THENM$="NOTHING MORE OF VALUE HERE.":GOTO14
472 M$="BINGO! UNDER SOME PAPERS IS A LOCKET.":PL=5:GOTO16
474 IFT(SW)<>3THEN498
476 IFPL<>2THEN8
478 M$="YOU SEE A SMALL CHEST IN THE CORNER.":PL=3:GOTO14
480 GOSUB483:GOTO252
483 GA=2:PL=0:M$="YOU'RE IN CHEAPSIDE, LONDON'S SHOPPING DISTRICT.":GOSUB1:RETURN
484 M$="YOU'VE GOT TO BE KIDDING.":GOTO16
486 M$="YOU HAVE TOO MANY ITEMS.":GOTO16
488 M$="YOU HAVEN'T THE FUNDS FOR THAT.":GOTO16
490 M$="THAT'S NO LONGER AVAILABLE.":GOTO16
492 M$="YOU ALREADY HAVE THAT ITEM.":GOTO14
494 M$="PLEASE BE MORE SPECIFIC.":GOTO16
496 M$="YOU CAN'T SEE MUCH IN THE DARK.":GOTO14
498 M$="YOU DON'T HAVE "+B$:GOTO16
500 MY=MY-5:IFMY=0THENNI=NI-1
502 T(SW)=3:NI=NI+1:TM=TM+1:GOTO16
504 M$="YOU CAN'T DO THAT... YET.":GOTO16
506 ONFWGOTO8,506,508,516,522,8,526,530,548,552,8,556,566,562,8,574,8
508 IFB$="TUNNEL"THEN584
510 IFB$="SEWER"THEN584
512 IFB$="DUNGEON"THEN592
514 GOTO8
516 IFB$="WALL"THEN596
518 IFB$="TUNNEL"THEN596
520 GOTO8
522 IFB$="TUNNEL"THEN604
524 GOTO8
526 IFB$="MEAT"THEN610
528 GOTO8
530 IFB$="DUNGEON"THEN592
532 IFB$="LEFT"THEN614
534 IFB$="RIGHT"THEN620
536 IFB$="STRAIGHT"THEN624
538 IFB$="CHEAPSIDE"THEN638
540 IFB$="BACK"THEN604
542 IFB$="TUNNEL"THEN584
544 IFB$="WALL"THEN644
546 GOTO8
548 IFB$="DOG"THEN650
550 GOTO8
552 IFB$="AROUND"THEN654
554 GOTO8
556 IFB$="WALL"THEN666
558 IFB$="DOOR"THEN666
560 GOTO8
562 IFB$="DOG"THEN672
564 GOTO8
566 IFB$="MEAT"THEN676
568 IFB$="KNIFE"THEN682
570 IFB$="HAMMER"THEN682
572 GOTO484
574 IFB$="HAMMER"THEN692
576 IFB$="MATCHES"THEN700
578 IFB$="ROPE"THEN706
580 IFB$="MEAT"THEN710
582 GOTO484
584 IFLI=1ANDJU=0THEN626
586 IFJU=0THENM$="IT'S TOO DARK TO SEE ANYTHING.":GOTO240
588 IFJU=1THENM$="WHICH ONE?":GOTO16
590 GOTO8
592 IFJU<>2ORWL<>1THEN8
594 GA=4:M$="YOU'VE ENTERED THE LONG-FORGOTTEN DUNGEON OF KING HENRY VIII.":GOTO14
596 IFJU<2THEN8
598 IFJU>2THENM$="IT'S SINGULARLY DULL.":GOTO14
600 IFDD=0THENM$="THAT DOG SURE LOOKS HUNGRY.":GOTO14
602 M$="AHA! THERE APPEARS TO BE A STONE DOOR IN THE WALL. WITH SOME EFFORT, YOU MAY BE ABLE TO GET THROUGH.":EW=1:GOTO14
604 IFJU<2THENM$="YOU'RE BACK AT THE SEWER ENTRANCE.":JU=0:GOTO246
606 IFJU>1THENM$="YOU'RE BACK AT THE FORK.":JU=1:GOTO242
608 GOTO8
610 IFJU<>2THEN8
612 M$="THE BEAST THANKS YOU, BUT HE'S STILL BLOCKING THE WAY.":T(SW)=4:GOTO14
614 IFJU<>1THEN8
616 JU=2:M$="YOU'RE WALKING DOWNHILL. THE TERRIBLE NOISE IS GETTING LOUDER BY THE MOMENT.":GOSUB1
618 M$="IT'S THE GROWLING OF A RAVENOUS DOG, WHO IS BLOCKING YOUR WAY.":GOTO240
620 IFJU<>1THEN8
622 JU=4:M$="YOU WALK FOR 2 MINUTES ONLY TO DISCOVER YOU'VE REACHED A DEAD END.":GOTO240
624 IFLI=0THEN496
626 IFJU=0THEN636
628 IFJU>2THEN8
630 IFDD=0ANDJU=2THEN708
632 IFDD=1ANDJU=2THENM$="THERE'S AN INTERSTING WALL HERE.":GOTO14
634 JU=3:M$="YOU WALK THROUGH WATER FOR 5 MINUTES BEFORE CONCLUDING THIS IS NOT THE RIGHT TUNNEL.":GOTO246
636 JU=1:M$="YOU HAVE REACHED A FORK: THERE ARE TUNNELS TO YOUR LEFT, RIGHT AND FRONT.":GOTO242
638 IFJU=0THEN642
640 IFJU>0THENM$="YOU MUST FIND YOUR WAY OUT OF THESE TUNNELS FIRST.":GOTO16
642 GA=2:LI=0:M$="YOU'RE BACK IN CHEAPSIDE.":GOTO250
644 IFJU<>2THEN8
646 IFDD=0THEN708
648 M$="YOU'RE STANDING AT THE WALL.":GOTO14
650 IFJU<>2THEN8
652 M$="THE DOG HAS BITTEN OFF THE END OF YOUR ARM. HOLMES IS DOOMED!":GOTO1012
654 IFLI=0THEN496
656 IFJU=0THENM$="YOU SEE A LONG TUNNEL AHEAD.":GOTO14
658 IFJU=1THENM$="TUNNELS ARE LEFT, RIGHT AND STRAIGHT.":GOTO14
660 IFJU>2THENM$="NOTHING REVEALING HERE.":GOTO14
662 IFDD=0THENM$="THE DOG IS SURELY HIDING SOMETHING.":GOTO14
664 M$="THE WALL AT THE END OF THIS TUNNEL LOOKS AWFULLY INTERESTING.":GOTO14
666 IFEW<>1THEN8
668 M$="GOOD JOB, TOBY! YOU'RE THROUGH THE WALL. YOU SEE WHAT APPEARS TO BE A DUNGEON AND HEAR MOANING."
670 WL=1:GOTO242
672 IFJU<>2THEN8
674 M$="HOW?":GOTO16
676 IFJU<>2THEN8
678 IFT(SW)<>3THEN8
680 DD=1:M$="GOOD THINKING, TOBY! THE BEAST HAS TAKEN THE BAIT AND IS OUT OF YOUR WAY.":T(SW)=4:NI=NI-1:GOTO14
682 IFT(SW)<>3THEN8
684 IFJU<>2THEN484
686 M$="YOU PANIC AND MISS MATCH IT... HE'S ATTACKING. YOU TRY TO RUN, BUT YOU STUMBLE.":GOSUB1:GOTO708
692 IFT(SW)<>3THEN498
694 IFJU<>2THEN8
696 IFDD=0THENM$="HOW CRUEL CAN YOU GET?!":GOTO16
698 M$="TOO SMALL TO BE EFFECTIVE.":GOTO240
700 IFT(SW)<>3THEN498
702 IFLI=1THENM$="- THE MORE LIGHT THE BETTER.":GOTO14
704 LI=1:M$="OH! THAT'S BETTER. YOU MAY PROCEED.":GOTO14
706 IFT(SW)<>3THEN498
708 M$="VALIANT TRY, BUT THE DOG HAS FOUND YOUR NECK IRRESISTIBLE. YOU'RE DONE FOR.":GOTO1012
710 IFJU<>2THEN8
712 GOTO494
716 ONFWGOTO720,716,726,8,732,738,742,748,764,770,778,774,790,786,794,802,8
720 IFB$="TROLL"THEN810
722 IFB$="HOLMES"THEN812
724 GOTO8
726 IFB$="CELL"THEN816
728 IFB$="SEWER"THEN850
730 GOTO8
732 IFB$="DUNGEON"THEN822
734 IFB$="STAIRS"THEN868
736 GOTO8
738 IFB$="KEYS"THEN830
740 GOTO8
742 IFB$="MONEY"THEN838
744 IFB$="LOCKET"THEN842
746 GOTO8
748 IFB$="SEWER"THEN850
750 IFB$="STAIRS"THEN854
752 IFB$="221B"THEN856
754 IFB$="CELL"THEN860
756 IFB$="TROLL"THEN866
758 IFB$="UP"THEN868
760 IFB$="HOLMES"THEN816
762 GOTO8
764 IFB$="TROLL"THEN872
766 IFB$="CHAIN"THEN876
768 GOTO8
770 IFB$="AROUND"THEN878
772 GOTO8
774 IFB$="COVER"THEN894
776 GOTO8
778 IFB$="DOOR"THEN890
780 IFB$="CELL"THEN890
782 IFB$="COVER"THEN894
784 GOTO8
786 IFB$="TROLL"THEN898
788 GOTO8
790 IFB$="KNIFE"THEN902
792 IFB$="HAMMER"THEN902
794 IFB$="CHAIN"THEN908
796 IFB$="DOOR"THEN908
798 IFB$="CELL"THEN908
800 GOTO8
802 IFB$="HAMMER"THEN912
804 IFB$="ROPE"THEN918
806 IFB$="KNIFE"THEN918
808 GOTO484
810 M$="THE TROLL HAS NO TONGUE. HIS GESTURES SADLY HAVE NO MEANING FOR YOU.":GOTO14
812 M$="HOLMES SAYS ONLY ONE THING WILL SATISFY THE TROLL: A GOLD LOCKET WITH A PICTURE OF IRENE ADLER, WHICH "
814 M$=M$+"IS SOMEWHERE IN 221B. HE IS TOO WEAK TO SAY ANY MORE.":GOTO14
816 IFSR=1THEN8
818 IFCE<2THEN504
820 M$="YOU'RE IN THE CELL. YOU RUSH TO HOLMES' SIDE AND FIND THAT HE'S STILL ALIVE.":GOSUB1:GOTO924
822 IFSR=0THEN850
824 IFSR<>2THENM$="YOU'RE ALMOST THERE.":GOTO16
826 SP=1:M$="IT'S INCREDIBLE! YOU'RE ON BAKER STREET, STANDING NEAR 221B.":GA=1:GOTO14
828 GOTO8
830 IFT(SW)=3ORT(SW)=4THEN490
832 IFTR=0THEN8
834 IFLK=0THENM$="THE TROLL'S PRICE HAS NOT BEEN MET.":GOTO16
836 M$="THE MELANCHOLY TROLL QUIETLY HANDS OVER THE KEYS.":KY=1:GOTO14
838 IFTR=0THEN8
840 M$="HE DOESN'T WANT MONEY.":GOTO14
842 IFTR=0THEN504
844 IFT(SW)<>3THEN498
846 LK=1:M$="HE LOOKS AT THE PICTURE AND TEARS COME TO HIS EYES. SHE WAS THE ONLY WOMAN WHO EVER LOVED HIM."
848 NI=NI-1:T(SW)=4:GOTO14
850 TR=0:SR=0:M$="MUCH TO YOUR DISMAY, THE WALL HAS CLOSED BACK UP AND CANNOT BE MOVED. "
852 M$=M$+"BUT NEVER FEAR - THERE IS A WAY OUT.":GOTO14
854 TR=0:SR=1:M$="YOU'RE AT THE FOOT OF THE STAIRS.":GOTO14
856 IFSP=1THEN236
858 M$="THERE'S NOT ENOUGH TIME TO GO BACK THE WAY YOU CAME. THE TROLL IS GETTING IMPATIENT.":GOTO16
860 SR=0:TR=0:IFCE=0THENM$="THAT CHAIN LOOKS PRETTY STRONG.":GOTO14
862 IFCE=1THENM$="THE CHAIN IS OFF THE CELL DOOR.":GOTO14
864 GOTO820
866 SR=0:TR=1:M$="YOU'RE STANDING BEFORE THE TROLL.":GOTO14
868 IFSR<>1THEN504
870 M$="YOU'VE REACHED THE TOP AND FIND A MANHOLE COVER. BEYOND IT, YOU HEAR STREET SOUNDS.":GOTO240
872 IFTR=0THENM$="YOU'RE TOO FAR AWAY.":GOTO16
874 M$="THERE'S NO NEED FOR VIOLENCE.":GOTO16
876 M$="WITH WHAT?":GOTO16
878 IFSR=1ORCE>0THEN886
880 M$="YOU SEE A CELL WITH A LOCKED CHAIN AROUND ITS BARS. INSIDE IS THE BARELY CONSCIOUS FIGURE OF SHERLOCK HOLMES.":GOSUB1
882 M$="IN A NEARBY CORNER SITS A MENACING TROLL. HE HAS THE KEYS TO THE CELL, BUT YOU DON'T KNOW HIS PRICE. YOU ALSO "
884 M$=M$+"SEE A SET OF STAIRS.":GOTO240
886 IFCE>0THENM$="HURRY! HOLMES IS GROANING.":GOTO14
888 M$="AS YOU LOOK UP, YOU SEE A CRACK OF LIGHT AT THE TOP. LOOKS LIKE A WAY OUT.":GOTO240
890 IFCE=0THENM$="THE CHAIN IS STILL LOCKED.":GOTO16
892 M$="THE CELL DOOR IS OPEN.":CE=2:GOTO16
894 IFSR=0THEN8
896 M$="THE COVER IS NOW OPEN AND YOU MAY EXIT.":SR=2:GOTO14
898 IFTR=0THEN8
900 M$="WITH WHAT?":GOTO16
902 IFT(SW)<>3THEN498
904 IFTR<>1THEN8
906 M$="HE'S TOO QUICK FOR YOU. HE'S THROWN THE KEYS AWAY. BAKER ST. WILL NEVER BE THE SAME.":GOTO1012
908 IFKY=0THEN504
910 M$="...THE CHAIN IS OFF THE DOOR.":CE=1:GOTO14
912 IFT(SW)<>3THEN498
914 IFTR=1THENM$="HE'S REALLY NOT SUCH A BAD FELLOW.":GOTO16
916 M$="IT'S TOO STRONG FOR THAT.":GOTO14
918 IFT(SW)<>3THEN498
920 IFTR=0THEN8
922 M$="THE TROLL WILL THROW THE KEYS DOWN A BOTTOMLESS PIT IF YOU GET ANY CLOSER.":GOSUB1:GOTO16
924 SN=1:IFTM>200THEN950
926 IFTM>150THEN954
928 IFTM>100THEN958
932 M$="YOU'VE DONE A MARVELOUS JOB, TOBY. THE WORLD IS GRATEFUL TO YOU FOR RESCUING SHERLOCK HOLMES.":GOSUB1
934 IFT(H(1))=3ANDT(H(2))=3ANDT(H(3))=3THEN942
936 IFT(H(1))=3ANDT(H(2))=3THEN944
938 IFT(H(1))=3THEN946
940 M$="TOO BAD YOU FORGOT HIS BELOVED PIPE. BUT HE'LL SURELY RECOVER ANYWAY.":GOTO1012
942 GOSUB949 M$=M$+", TOBACCO AND MATCHES. THESE WILL MAKE MAKE HIS DAY. JOB WELL DONE.":GOTO1012
944 GOSUB949:M$=M$+" AND TOBACCO. ALAS, YOU'VE DROPPED YOUR MATCHES. PERHAPS THE TROLL HAS SOME.":GOTO1012
946 GOSUB949:M$=M$+". ALAS, YOU BROUGHT NO TOBACCO, BUT AT LEAST HE HAS A FAMILIAR OBJECT TO CHEW ON.":GOTO1012
949 M$="YOU EVEN REMEMBERED HIS PIPE":SN=2:RETURN
950 M$="BUT, SAD TO SAY, YOU'RE TOO LATE. HOLMES HAS SUFFERED IRREPARABLE BRAIN DAMAGE. SORRY, YOUNG FELLOW.":GOTO1012
954 M$="BUT, DEAR TOBY, DESPITE YOUR BEST EFFORTS, TIME HAS BEEN YOUR ENEMY. THE GREAT DETECTIVE IS NEAR DEATH.":GOSUB1
956 M$="HE MAY EVENTUALLY RECOVER, BUT THE PROGNOSIS IS GRIM.":GOTO1012
958 M$="TOBY, YOU'RE BARELY IN TIME. HOLMES' CONDITION IS PERILOUS, BUT WITH WATSON'S HELP, HE SHOULD RECOVER.":GOTO1012
962 A$="":B$="":INPUT"WHAT NOW";I$:PRINT:LE=LEN(I$):IFI$="HELP"THENGOSUB2000:GOTO962
964 FORK=1TOLE:IFMID$(I$,K,1)=" "THENA$=LEFT$(I$,K-1):B$=RIGHT$(I$,LE-K):K=LE
966 NEXTK:FORI=1TONV:IFA$<>A$(I)THENNEXTI
970 IFI=NV+1THEN996
972 FW=I:I=NV:NEXTI:FORJ=1TONM:IFB$<>B$(J)THENNEXTJ
976 IFJ=NM+1THEN996
978 SW=J:J=NM:NEXTJ:IFA$="DROP"THENT=T(SW):GOTO988
986 ONGAGOTO254,126,506,716
988 IFT<>3THENM$="YOU DON'T HAVE THAT ITEM.":GOSUB1:GOTO962
990 M$="YOU NO LONGER HAVE "+B$+".":GOSUB1:NI=NI-1:T(SW)=4
992 IFB$="MONEY"THENMY=0
994 GOTO962
996 PRINT"TYPE HELP":GOTO962
1000 DATAASK,DROP,ENTER,EXAMINE,EXIT,GET,GIVE,GO,HIT,LOOK,OPEN,PUSH,THROW,TIE,UNLOCK,USE,WALK,EV
1002 DATA221B,AROUND,BACK,BOOKCASE,BUTCHER,CELL,CHAIN,CHEAPSIDE,CHEST,COVER,DOG,DOOR
1004 DATADRYGOODS,DUNGEON,HAMMER,HANSOM,HOLMES,KEYS,KNIFE,LEFT,LIST,LOCKET,MATCHES,MEAT,MONEY,PIPE,POLICE
1006 DATARIGHT,ROOM,ROPE,HELP,SEWER,SHOE,SMOKESHOP,STAIRS,STRAIGHT,TOBACCO,TROLL,TUNNEL,UP,WALL,WATSON,EM
1012 GOSUB1:GOSUB10:ONSNGOTO3040,3000:GOTO3040
2000 CLS:PRINTTAB(13)"VERBS":K=0:FORI=1TONV:K=K+1:PRINTA$(I)TAB(10*K);:IFK=3THENK=0:PRINT
2010 NEXTI:GOSUB10:PRINTTAB(11)"MODIFIERS":K=0:FORI=1TONM:K=K+1:PRINTB$(I)TAB(10*K);:IFK=3THENK=0:PRINT
2020 NEXTI:GOSUB10:RETURN
3000 PRINT"èèèèèèèüüü",,"èèèèèèüüüüü",,"èèèèèüüüüüüü",,"èèèèüüüüüüüüü",,"èèèüüüüüüüüüüü"
3010 PRINT"èèèèèüüüüüüüüü",,"èèèèüüüüüüüüüüü",,"èèèüüüüüüüüüü",,"èèèèèüüüüüüüü",,"èèèèüüüüüüüüüü"
3020 PRINT"üüèüèüüüüüüüüü",,"üüüüèèèèüüüüüüü",,"èüüèèèèèüüüüüüü",,"èèèèèèèèüüüüüüüü"
3040 END
