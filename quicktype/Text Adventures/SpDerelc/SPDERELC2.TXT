0 CLEAR700:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 CLS:CLEAR700:PRINT@6*32+12,"DERELICT":PRINT@8*32+6,"BY AARDVARK SOFTWARE"
11 DIMD$(26),O$(71),L(71),P(37,6):L=1:O1=71:O2=501:O3=31:Q=4:GOTO180
15 M$="AN ALIEN TRACTOR BEAM IS PULLING YOU IN":GOSUB1:GOTO180RUN
20 PRINT"IT'S ALREADY OPEN":GOTO370
30 PRINT"O.K."
40 GOTO370
50 IFL>30ANDL<40THENM$="YOU ARE AT: A CORRIDOR RISING UPWARD IN FRONT AND BEHIND YOU":GOSUB1
70 IFL>25ANDL<31THENM$="YOU ARE AT: "+D$(26):GOSUB1
80 RETURN
90 IFL<5ORL=11ORL=12ORL>25THEN150
100 L=L+40
110 M$="YOU ARE IN A GLASS BOOTH-- THERE IS A KEYBOARD HERE":GOSUB1:IFL>77THENL=L-40:GOTO110
120 Z$="":IFL>45ANDL<50ORL>52ANDL<65THENZ$="A SIGN:"
130 PRINT"OUTSIDE YOU SEE:":PRINT;Z$;D$(L-40):L=L-40:GOSUB1120
140 L=L+40:GOTO40
150 PRINT"THAT'S NOT HERE":GOTO40
160 L=L-40:GOTO830
170 PRINT"SHIP FLOATED AWAY":L(59)=90:P(25,5)=0:GOTO420
180 FORX=1TO26:READD$(X):NEXT
190 DATACABIN OF A ONE MAN SPACESHIP,AIRLOCK,MINE,VERTICAL SHAFT,ASTEROID SURFACE
192 DATAHGLIZTV ILLN,VMTRMV ILLN,GLLO ILLN,XLNNFMRXZGRLMH,OBSERVATION DECK,STAIRS
194 DATAMACHINERY ROOM,HRXP YZB,NVM,DLNVM,XZKGZRMH XZYRM,ORYIZIB,XIVDH JFZIGVIH
196 DATAIVXIVZGRLM
200 DATAOZYLIZGLIB,PRGXSVM,NVHH SZOO,HVXFIRGB,XLMGILO WVXP
202 DATALANDING BAY,CENTRAL SHAFT
210 FORX=1TO2500:NEXT:CLS:PRINT"BAY DOORS CLOSING BEHIND YOU"
220 FORX=1TO71:READO$(X):O$=O$+LEFT$(O$(X),2):IFX>6THENREADL(X-6)
230 NEXT
240 DATAFORWARD,AFT,SPINWARD,ANTISPINWARD,UP,DOWN,NYLON SPACESUIT-WITH FITTING,1
242 DATAOXYGEN BOTTLE,1,MAGNETIC BOOTS,1,METALLIC SPACESUIT-WITH FITTING,6,CUTTING TORCH
244 DATA8,TANK,8,LUMPS OF COAL,3,SHIELDING,7,SIGN,1,RESISTORS,9,FLASHLIGHT,12,LOG BOOK
250 DATA16,PHASER,90,RING OF KEYS,90,GRAY BOX-WITH KNOB,90,PLAYING CARDS,19,LIGHTER,16
252 DATABOTTLE,90,TITANIUM PICKAX,3,RUBY CRYSTAL,90,WIRE(SILVER),90,PAINTING,16,COINS,90
254 DATAJEWELED GAMING PIECES,19,DIAMONDS,90,GLOBE(PLATINUM),17,GOLD,90,IMMORTALITY SYRUM
256 DATA90,PORTHOLE,1,SWITCH,1,IRON DOOR,2,ATMOSPHERIC ANALYZER,2,HATCH(HULL),2
270 DATAHYPERDRIVE ENGINES,7,GAUGES,7,INSTRUMENTS,7,YELLOW SIGN,7,CHART,9,TRANSMITTER,9
272 DATARADIO,9,PRESS,12,CABINET,13,BLUE DOOR,14,PINK DOOR,15,FIXTURES(PORCELAIN),15
276 DATAFOOT HOLE,14,BUNK,16,DESK,16,SAFE,23,CLOTHPALLETS,18,OVEN,21,LARGE MACHINE,20
280 DATABENCHES AND TABLES,22,VIDEO PROJECTOR,17,SCREEN,17,PUSHBUTTONS,24,CRT SCREEN,24
282 DATABAY DOORS(CLOSED),25,SMALL SPACESHIP,25,#1 DROID,25,#2 DROID,7,#3DROID,17,#4 DROID
284 DATA13,#5 DROID,20,#6DROID,24
290 CLS:PRINT"BAY DOORS CLOSED":FORX=1TO750:NEXT:PRINT"TRACTOR BEAM OFF!
300 FORY=1TO37:FORX=1TO6:READP(Y,X):NEXTX:NEXTY
310 DATA,2,,,,,1,,,,,0,,,,,4,,,,,,5,3,,,,,,4,32,,,,,,,31,,,,,,32,,
312 DATA,,31,,,,,,,,,,,11,,,,,10,12,,,,,11,,34,,,,,
320 DATA,35,,,,,35,,,,,,33,,,,,,,,19,,,,,34,,,,,,33,,17,,,,36,,,,
322 DATA37,,,,,,,37,,,,,36,,,,,,,,,,,30,,26,,,2,,25,25,,,27,26
330 DATA,,,,28,26,32,31,,,29,27,34,33,,,30,28,36,37,,,24,29,7,9,32
332 DATA32,28,,8,6,31,31,28,,19,16,34,35,29,,18,13,35,33,29,,14,15
334 DATA33,34,,,20,23,37,37,30,,22,21,36,36,30,
340 FORX=1TO10:READC$(X):NEXT
350 DATABLACK,BROWN,RED,ORANGE,YELLOW,GREEN,BLUE,PURPLE,GRAY,WHITE
360 W$="GOLOREUNPUOPBLCUCLDRATDEPLLIWE":GOTO830
370 PRINT:PRINT"YOUR COMMAND":INPUTA$:PRINT:B$="":FORX=1TOLEN(A$)
380 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
390 NEXT:IFL=7ORL=13ORL=17ORL=20ORL=24ORL=25THEN2680
400 M=0:J1=0:R=INT(RND(0)*10)+2
410 F=0:S=0:A$=LEFT$(A$,2):IFL>2ANDB=1ANDN=0ANDL(59)=25THEN170
420 IFW=1ANDL(17)=0ANDL>6THENW=0:PRINT"LIGHTER WENT OUT"
430 IFT=1THENO2=O2-25
440 IFL(6)<>0ANDT=1ORO4=2ANDT=1THENT=0:PRINT"TORCH IS OUT"
450 IFL<6ANDO4=0THEN480
460 O=O-1:IFO<0THENPRINT"YOU DIE-- NO OXYGEN":GOTO9000
470 IFO<10THENPRINT"OXYGEN IS LOW!!"
480 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2
490 NEXT:FORX=1TO142STEP2:IFMID$(O$,X,2)=B$THENS=(X+1)/2:GOTO 500
491 NEXT
500 IFL=7ANDC2=1ANDL(4)<>0THENPRINT"YOU DIE FROM RADIATION":GOTO9000
510 Z=(S-6):IFA$="TA"ORA$="GE"THEN1000
520 IFA$="IN"ANDB$=""THEN1070
530 IFA$="LO"ANDB$=""THEN830
540 IFF=1AND(B$="BO"ORB$="IN")ANDL<40THEN90
550 IFF=1AND(B$="EX"ORB$="OU")ANDL>40THEN160
560 IFF=5ANDB$="KE"ANDL>40THEN1450
570 IFF=2ANDB$="KE"ANDL>40THENPRINT"KEYS NUMBERED 1-99":GOTO40
580 IFF=2ANDB$="BO"ANDL>40THENPRINT"EXIT":GOTO40
590 IFF=5ANDB$="BU"THEN1740
610 IFA$="TU"ANDB$="KN"THEN1650
620 IFA$="OF"THEN2470
630 IFA$="SC"THEN2570
640 IFA$="EX"ANDB$="BO"ANDL>40THEN160
650 IFF=0ORS=0THENPRINT"WHAT?":GOTO40
660 IFF>9THEN720
670 IFS<7THEN700
680 IFL(Z)<>L ANDL(Z)<>0THEN150
700 ON(F-1)GOTO1190,1260,1430,1550,1830,1950,2030,2070
710 GOTO750
720 IFS<7THEN740
730 IFL(Z)<>0THENPRINT"YOU DON'T HAVE IT":GOTO40
740 ON(F-9)GOTO2110,2230,2290,2000,2340,2630
750 IFS>6THENPRINT"CAN'T GO TO THAT":GOTO40
760 IFL>40THENPRINT"CAN'T":GOTO40
800 IFP(L,S)=0THENPRINT"CAN'T GO THAT WAY!":GOTO40
810 L=P(L,S)
820 IFL>40THEN120
830 IFL>12ANDL<25ORL>5ANDL<10THENPRINT"YOU ARE IN A ROOM"
832 IFL>12ANDL<25ORL>5ANDL<10THENM$="THE SIGN SAYS: "+D$(L):GOSUB1:GOTO910
840 GOSUB50:IFL>38THEN110
841 IFL>25ANDL<38THEN844
843 M$="YOU ARE AT: "+D$(L):GOSUB1
844 IFL>25ANDL<31ANDL(3)<>0THENM$="YOU ARE FLOATING HELPLESS IN ZERO G":GOSUB1:GOTO9000
910 IFL<5ORL>25ORL=11ORL=12GOTO1100
920 L1=INT(L/10):L2=L-10*L1:IFL=10THENM$="YOU ARE LOOKING ON AN ALIEN CITY":GOSUB1
930 IFL=5THENM$="A BLACK/GREEN ASTEROID SURFACE STRETCHES OUT BEFORE YOU":GOSUB1:GOTO950
940 M$="THE FLOOR IS "+C$(L1+1)+" AND THE WALLS ARE "+C$(L2+1):GOSUB1
950 PRINT"THERE IS A GLASS BOOTH HERE"
960 IFJ1=1ANDL<40THENPRINT"AND A SMOKING PILE OF JUNK"
970 GOTO1100
990 PRINT"CAN'T GO TO THAT":GOTO370
1000 IFS<7THENPRINT"HUH??!!":GOTO40
1010 IFL(Z)=0THENPRINT"YOU HAVE THAT":GOTO40
1020 IFL(Z)<>L THENPRINT"IT'S NOT IN SIGHT":GOTO40
1030 IFS>34THENPRINT"YOU CAN'T TAKE THAT!!":GOTO40
1040 IFS=14ANDC2=0THENM$="CAN'T-- IT'S WELDED TO THE FLOOR":GOSUB1:GOTO40
1050 IFC>12THENPRINT"YOU ARE CARRYING TOO MUCH!":GOTO40
1060 L(Z)=0:C=C+1
1070 PRINT"YOU ARE CARRYING:":FORX=1TO37:IFL(X)=0THENM$=O$(X+6):GOSUB1
1080 NEXT:IFC=0THENPRINT"NOTHING"
1090 GOTO40
1100 GOSUB1120
1110 GOTO1160
1120 Y=0:PRINT"YOU SEE HERE:":FORX=1TO65:IFL(X)=LTHENPRINTO$(X+6):Y=Y+1
1130 NEXT:IFY=0THENPRINT"NOTHING"
1140 IFL=12ORL=14ORL=15ORL=17ORL=20THENPRINT"A BUTTON(PUSH-TYPE)"
1150 RETURN
1160 I$="EXITS ARE: ":IFL>40THEN40
1170 FORX=1TO6:IFP(L,X)>0THENI$=I$+O$(X)+", "
1180 NEXT:M$=LEFT$(I$,LEN(I$)-2):GOSUB1:GOTO40
1190 IFL>40ANDS<7THEN40
1200 IFS>6THEN1220
1220 IFS=35THENPRINT"LANDING BAY"
1230 IFS=18THENPRINT"HZUV: #!!!-#!##-#!!!"
1240 IFS=28THENPRINT"A BLACK & GREEN ASTEROID"
1250 IFS=55THENPRINT"KEYPAD-NUMBERS 0-9"
1260 IFS=61ANDV=1THENPRINT"A BROWN & BLACK ALIEN CITY"
1270 IFS=38THENPRINT"HELIUM OUTSIDE"
1280 IFS=8THENPRINT"ATTACH/DETACH OXYGEN"
1290 IFS=43THENPRINT"IZWRZGRLM"
1300 IFS=49THENPRINT"NVM"
1310 IFS=12THENPRINT"DVOWRMT TZH"
1320 IFS=50THENPRINT"DLNUM"
1330 IFS=47THENPRINT"XZIYLM XIBHGZORVI FMRG"
1340 IFS=15THENM$="DROP TREASURE HERE-- TYPE SCORE":GOSUB1
1350 IFS=45ORS=57THENPRINT"A LID"
1360 IFS=63ANDP=0THENPRINT"NO POWER":GOTO40
1370 IFS=58THENPRINT"OVZW GL TLOW"
1380 IFS=24THENPRINT"XLLPRMT TZH"
1390 IFS=62THENPRINT"RED KLDVI":PRINT"GREEN GIZXGLI YVZN":PRINT"BLUE YZB WLLIH":GOTO40
1400 IFS=63THENPRINT"MY FAVORITE MARTIAN RERUNS"
1410 IFS=44THENPRINT"    # YOZXP":PRINT"    ! YILDM":PRINT"   !# IVW"
1412 IFS=44THENPRINT"   !! LIXMTV":PRINT"  !## BVOOLD":PRINT"  !#! TIVVM":PRINT" !!#YOFV"
1414 IFS=44THENPRINT"  !!! KFIKOV":PRINT" !### TIZB":PRINT"!##! DSRFV"
1420 GOTO40
1430 IFS=48ANDL(14)=0ANDK=0THENK=1:GOTO30
1440 PRINT"CAN'T":GOTO40
1450 INPUT"WHICH ONE";Z:IFZ<1ORZ>99THENPRINT"ERROR":GOTO1450
1460 IFP=0THENPRINT"NO POWER":GOTO40
1480 POKE49151,64:FORZZ=1TO500:NEXT:Z1=L:L=Z:PRINT"ZZAAPP":SOUND200,2
1490 IFZ<4ORZ=11ORZ=12ORZ>25THEN1520
1500 GOTO100
1520 M$="YOU MOLECULES ARE SCATTERED THROUGHOUT THE KNOWN UNIVERSE!!":GOSUB1:GOTO9000
1540 PRINT"MALFUNCTION":L=Z1-40:GOTO100
1550 IFB$="KE"THENPRINT"NOT HERE"
1560 IFL=24THENINPUT"WHAT COLOR";K$:K$=LEFT$(K$,2):GOTO1580
1570 GOTO1720
1580 IFK$="RE"THEN1690
1590 IFP=0THENPRINT"NOTHING HAPPENS":GOTO40
1600 IFK$="BL"THEN1630
1610 IFK$="GR"THEN1670
1620 GOTO40
1630 IFB=0THENPRINT"OPEN":B=1:O$(64)="BAY DOORS(OPEN)":GOTO40
1640 PRINT"CLOSED":B=0:O$(64)="BAY DOORS(CLOSED)":GOTO40
1650 IFL(15)=0 ORL(15)=L THEN1670
1660 PRINT"IT'S NOT HERE"
1670 IFN=0THENN=1:PRINT"BEAM ON":GOTO40
1680 N=0:PRINT"BEAM OFF":GOTO40
1690 IFP=1THENPRINT"POWER OFF":GOTO40
1700 P=1:CLS:PRINT"POWER ON":PRINT:FORX=1TO6:PRINT"INTRUDER ALERT":SOUND1,4:NEXTX
1710 FORX=24TO37:P(X,5)=0:P(X,6)=0:NEXTX:P(25,2)=0:P(25,5)=2:GOTO40
1720 IFS=36THEN2520
1730 GOTO40
1740 IFL=14 ORL=15THENPRINT"YOU HEAR WATER GURGLING":GOTO40
1750 IFP=0THENPRINT"NO POWER":GOTO40
1760 IFL=20ANDL(8)=20ANDG=1THENL(27)=20:L(8)=90:GOTO1820
1770 IFL=12ANDL(7)=L ANDD2=1THENL(7)=90:L(25)=12:GOTO1820
1780 IFL=12ORL=20THENM$="NOTHING PLACED IN THAT MACHINE":GOSUB1:GOTO40
1790 IFL=17ANDV=0THENPRINT"SCREEN IS LIT":V=1:GOTO40
1800 IFL=17THENV=0:PRINT"OFF":GOTO40
1810 PRINT"NOT HERE":GOTO40
1820 PRINT"DONE!":GOTO40
1830 IFS=48ANDK=0THENPRINT"LOCKED!":GOTO40
1840 IFS=48THENIFL(28)=90THENL(28)=13:L(21)=13:L(15)=13:GOTO30
1841 IFS=48THENIFL(28)<>90THEN20
1850 IFS=55THENIFL(23)=90THENINPUT"COMBINATION";Q$
1851 IFS=55THENIFL(23)<>90THEN20
1860 IFS=55ANDP=0THENPRINT"NO POWER":GOTO40
1865 IFS<>55THEN1880
1870 IFL(23)=90THENIFQ$="747"ORQ$="7-4-7"THENL(23)=23:L(13)=23:L(14)=23:GOTO30
1880 IFS=45THENIFL(20)=90THENL(20)=9:GOTO30
1881 IFS=45THENIFL(20)<>90THEN20
1890 IFS=37ANDD1=0ORS=39ANDD=1THEN20
1900 IFS=39ANDD1=1ANDD=0THEND=1:P(2,6)=25:GOTO30
1910 IFS=37AND1=1ANDD=0THEND1=0:P(2,1)=1:GOTO30
1920 IFS=39ORS=37THENPRINT"CAN'T YET"
1930 IFS=57THENIFL(18)=90THENL(18)=21:GOTO30
1931 IFS=57THENIFL(18)<>90THEN20
1940 GOTO40
1950 IFL(13)=0ANDS>65ANDL=L(Z)THENJ=INT((RND(0)*3)):GOTO1970
1960 PRINT"CAN'T":GOTO40
1970 IFQ=0THENPRINT"PHASER IS EMPTY!!":GOTO40
1980 IFJ<1THENSOUND225,3:PRINT"PHASER MISFIRED":GOTO40
1990 SOUND225,10:SOUND1,1:PRINT"DROID DESTROYED":Q=Q-1:L(Z)=90:GOTO40
2000 INPUT"IN WHAT";P$:P$=LEFT$(P$,2):IFP$="PR"ANDL=12ANDS=13THENL(7)=L:D2=1:GOTO30
2010 IFP$="LA"ANDL=20ANDS=14THENL(8)=L:G=1:GOTO30
2020 PRINT"CAN'T":L(Z)=L:GOTO40
2030 IFS=14ANDL<>L(Z)THEN1810
2032 IFS=14ANDL(5)=0THEN2050
2040 IFL(5)<>0THENPRINT"NO TORCH":GOTO40
2042 IFS<>14THENPRINT"CAN'T CUT THAT"
2044 GOTO40
2050 IFL(6)=0ANDT=1THENC2=1:PRINT"IT'S CUT":GOTO40
2060 PRINT"CAN'T":GOTO40
2070 IFS=37ANDD1=1ORS=39ANDD=0THENPRINT"IT'S CLOSED":GOTO40
2080 IFS=39ANDD=1THEND=0:P(2,6)=0:GOTO30
2090 IFS=37ANDD1=0THEND1=1:P(2,1)=0:GOTO30
2100 PRINT"CAN'T CLOSE THAT":GOTO40
2110 IFS<7THENPRINT"WHAT":GOTO40
2120 IFS=7THENW1=0
2130 IFS=10THENW2=0
2140 IFS=8ANDO4=1THEN2220
2150 IFS=12ANDO4=2THEN2220
2160 IFS=24ANDO4=3THEN2220
2170 L(Z)=L:C=C-1
2180 IFS=7ANDO4=1THENL(2)=L:O1=O:GOTO2210
2190 IFS=10ANDO4=2ORS=10ANDO4=3THENL(18)=L:O3=0:GOTO2210
2200 GOTO30
2210 C=C-1:O=0:O4=O:GOTO30
2220 PRINT"CAN'T-- IT'S ATTACHED":GOTO40
2230 IFO4<>0THENPRINT"CAN'T":GOTO40
2240 IFS=8ANDW1=1THENO=O1:O4=1:GOTO30
2250 IFW2=0THEN2280
2260 IFS=12THENO=O2:O4=2:GOTO30
2270 IFS=24THENO=O3:O4=3:GOTO30
2280 PRINT"NOT WEARING A SUIT":GOTO40
2290 IFS=8ANDO4=1THENO1=0:GOTO2330
2300 IFS=12ANDO4=2THENO2=O:GOTO2330
2310 IFS=24ANDO4=3THENO3=O:GOTO2330
2320 PRINT"CAN'T":GOTO40
2330 C=C-1:O=0:O4=0:GOTO30
2340 IFS=11THEN2400
2350 IFS=17ANDF1=0THENF1=1:GOTO30
2360 IFS=23ANDW=0THENW=1:GOTO30
2370 PRINT"WILL NOT LIGHT":GOTO40
2380 IFS=17ANDF1=1THEN2460
2390 GOTO40
2400 IFT=1THEN2460
2410 IFL(17)<>0THENPRINT"NO LIGHTER":GOTO40
2420 IFL(6)=0ANDO4<>2THEN2450
2430 IFL(18)=0ANDO4<>3THENPRINT"OXYGEN BOTTLE IS EMPTY":O3=0:GOTO40
2440 PRINT"CAN'T YET":GOTO40
2450 T=1:GOTO30
2460 PRINT"IT'S ALREADY LIT":GOTO40
2470 IFS=17THENF1=0:GOTO2510
2480 IFS=23THENW=0:GOTO2510
2490 IFS=11THENT=0:GOTO2510
2500 PRINT"CAN'T":GOTO40
2510 PRINT"IT'S OFF":GOTO40
2520 IFB=1ANDN=0THEN2560
2530 IFB=1THEN2550
2540 M$="CAN'T LAUNCH-- BAY DOORS CLOSED":GOSUB1:GOTO40
2550 IFN=1THENM$="TRACTOR BEAM HOLDING YOU":GOSUB1:GOTO40
2560 M$="YOU SUCESSFULLY LAUNCHED YOUR SHIP":GOSUB1:E=1
2570 S=0:FORX=19TO28:IFL(X)=1THENS=S+9
2580 NEXT:IFE=1THENS=S+10
2590 PRINT"SCORE IS"S"OUT OF 100"
2600 IFS=90THENM$="GOOD-- BUT YOU'RE STILL STUCK HERE":GOSUB1
2610 IFE=1ANDS=100THENEND
2611 IFE=1THEN9000
2620 GOTO40
2630 IFS=7ANDW2=0THENW1=1:GOTO30
2640 IFS=10ANDW1=0THENW2=1:GOTO30
2650 PRINT"YOU ALREADY ARE WEARING ONE"
2660 GOTO40
2680 IFP=0THEN410
2690 FORX=60TO65:IFL(X)=L THEN2710
2700 NEXT:J1=1:GOTO410
2710 PRINT"YOU HEAR METALIC MOVEMENT":M=M+1:PRINT
2720 IFM=R THENM$="THE DROID HAS BLASTED YOU INTO ETERNITY":GOSUB1:GOTO9000
2730 M$="A DEADLY LASER BEAM JUST MISSED YOU!!":GOSUB1:SOUND225,3:GOTO410
3010 IFP$="LA"ANDL=20ANDS=14THENL(8)=L:G=1:GOTO30
9000 FORX=1TO15:POKE49151,64:FORZZ=1TO50:NEXT:SOUND1,1:FORZZ=1TO50:NEXT:NEXTX
9001 PRINT:PRINT"PLAY AGAIN (Y/N)?"
9002 A$=INKEY$:IFA$="Y"THENRUN
9003 IFA$="N"THENEND
9004 GOTO9002
