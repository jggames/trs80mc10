0 CLS0:CLEAR400,36762:DIMC2,C3,C4,MC,CC,ZZ,VP,M$,I$,K$,W$:MC=16384:M$="O"+"K":W$=CHR$(127):GOSUB15:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENGOSUB3:ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 K$=MID$(M$,ZZ,CC-ZZ)+" ":VP=VARPTR(K$):C4=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:IFC4-MC+33=512THENGOSUB6:C4=C4-32
4 PRINT@C4-MC+33,;:FORC2=1TOLEN(K$):C3=ASC(MID$(K$,C2)):POKEC4+C2,C3+64*(C3>63):NEXT:RETURN
5 FORCC=1TO16:GOSUB6:NEXT:RETURN
6 PRINT@511,W$;:POKEMC+479,32:RETURN
15 FORCC=0TO103:POKE36762+CC,PEEK(64022+CC):NEXT:DATA49,49,60,55,54
16 FORCC=0TO4:READC2:POKE36762+CC,C2:NEXT:POKE36855,32:POKE17033,143:POKE17034,154:POKE17032,126:RETURN
60 A$=INKEY$:IFA$=""THEN60
70 RETURN
100 GOSUB5
110 M$="    THE ORIENT EXPRESS, 1923":GOSUB1
120 M$="     (C) DAVID H. AHL, 1986":GOSUB1:FORC1=1TO6:GOSUB6:NEXT
140 GOSUB1695:GOSUB5
160 M$="    THE MYSTERIOUS ARMS DEAL":GOSUB1:M$=" ":GOSUB1:GOSUB1710:
170 REM 
180 REM INITIALIZATION
190 DIMC$(25),CN(25),CP(25),DA(25),HZ(25),LA$(25),LB$(25),ME(25),N$(25)
200 DIMTA(25),TD(25),CS(25),MB$(15),MD$(26)
210 GOSUB1880
220 GOSUB2160
230 GOSUB2430
240 GOSUB2530
250 GOSUB2760
260 M$="PRESS ANY KEY TO CALL A TAXI...":GOSUB1
261 M$=INKEY$:IFM$=""THEN261
270 REM 
280 REM MAIN PROGRAM
290 FORJ=1TO24
300 M$="FEBRUARY"+STR$(DA(J)+13+HY)+", 1923":GOSUB1
310 TN=18-INT(27*RND(0)):TB=TA(J)+TN:T=TB:IFJ=1THEN450
320 GOSUB3110:M$="YOU HAVE ARRIVED AT "+LA$(J)+", "+LB$(J)+" AT":GOSUB3060:GOSUB1
330 IFTN>1THENM$="JUST"+STR$(TN)+" MINUTES LATE.":GOSUB1:GOTO360
340 IFTN<-1THENM$="ALMOST"+STR$(ABS(TN))+" MINUTES EARLY.":GOSUB1:GOTO360
350 M$="-- RIGHT ON TIME!":GOSUB1
360 IFTB>TD(J)-2THENT=TB+4:GOTO370
361 T=TD(J)
370 IFJ>23THEN620
371 IFME(J)<4THEN400
380 M$="ASLEEP IN YOUR COMPARTMENT, YOU BARELY NOTICE THAT THE DEPARTURE WAS RIGHT ON TIME AT"
390 GOSUB3060:GOSUB1:GOSUB2860:GOTO490
400 IFJ=23THENGOSUB1340
410 M$="DEPARTURE IS AT":GOSUB3060:GOSUB1
420 M$="WOULD YOU LIKE TO GET OFF AND STRETCH YOUR LEGS?":GOSUB1:GOSUB2810
430 IFA=1THENM$="OKAY, YOU STAY IN YOUR COMPARTMENT.":GOSUB1:GOTO470
440 M$="OKAY, BUT BE SURE NOT TO MISS THE TRAIN.":GOSUB1:GOTO470
450 M$="THE TAXI HAS DROPPED YOU AT VICTORIA STATION IN LONDON.":GOSUB1
460 M$="THE ORIENT EXPRESS IS STANDING MAJESTICALLY ON TRACK 14.":GOSUB1
470 SOUND50,5:GOSUB2860:SOUND50,30:M$="ALL ABOARD...":GOSUB1;
480 GOSUB2860:M$="THE TRAIN IS LEAVING.":GOSUB1:GOSUB2860
490 GOSUB2990:REM TRAIN NOISES
500 GOSUB2860:IFJ>1THEN570
510 X=3+INT(20*RND(0))
520 M$="YOU SPEAK TO SOME OF THE PASSENGERS--"+N$(X)+",":GOSUB1 
530 M$=N$(X+1)+", "+N$(X+2)+" AND OTHERS--AND ASK THEM TO KEEP " 
540 M$=M$+"THEIR EYES AND EARS OPEN AND TO PASS ANY INFORMATION--NO "
550 M$=M$+"MATTER HOW TRIVIAL--TO YOU IN COMPARTMENT 13.":GOSUB1:M$="THE CHANNEL "
560 M$=M$+"CROSSING IS PLEASANT AND THE FIRST PART OF THE TRIP UNEVENTFUL.":GOSUB1
570 IFJ=23THENGOSUB1490
580 IFME(J)>0ANDME(J)<4THENONME(J)GOSUB770,720,640
590 GOSUB860
600 IFHZ(J)>0THENONHZ(J)GOSUB970,1110
610 GOSUB1220
620 NEXTJ:GOTO3190
630 REM 
640 REM SUBROUTINE TO SERVE BREAKFAST
650 M$="BREAKFAST IS NOW BEING SERVED IN THE RESTAURANT CAR.":GOSUB1
660 M$="PRESS ANY KEY WHEN YOU'RE READY TO HAVE BREAKFAST.":GOSUB1
670 M$=INKEY$:IFM$=""THEN670
671 GOSUB5:M$="         BREAKFAST MENU":GOSUB1
680 FORI=1TO4:X=3*(I-1)+1+INT(3*RND(0))
690 M$=MB$(X):GOSUB1:NEXT
700 M$=MB$(13):GOSUB1:GOSUB2950:RETURN
710 REM 
720 REM SERVE LUNCH
730 M$="AN ENORMOUS BUFFET LUNCHEON HAS BEEN LAID OUT IN THE RESTAURANT CAR.":GOSUB1
731 M$="PRESS ANY KEY WHEN YOU HAVE FINISHED.":GOSUB1
750 M$=INKEY$:IFM$=""THEN750
751 M$=" B-U-R-P !":GOSUB1:RETURN
760 REM 
770 REM SERVE DINNER
780 M$="DINNER IS NOW BEING SERVED IN THE RESTAURANT CAR.":GOSUB1
790 M$="PRESS ANY KEY WHEN YOU'RE READY TO HAVE DINNER.":GOSUB1
800 M$=INKEY$:IFM$=""THEN800
801 GOSUB5:M$="          DINNER MENU":GOSUB1
810 FORI=1TO7:X=3*(I-1)+1+INT(3*RND(0))
820 M$=MD$(X):GOSUB1:NEXTI
830 M$=MD$(21+RND(3)):GOSUB1:M$=MD$(25):GOSUB1:GOSUB2950:RETURN
850 REM 
860 REM HAVE CONVERSATIONS
870 IFCN(J)=0THENRETURN
875 FORK=1TOCN(J)
880 GOSUB2890:CM=CM+1
890 IFCP(CS(CM))>0THENX=CP(CS(CM)):GOTO900
891 X=3+INT(23*RND(0))
900 M$="STANDING THERE IS "+N$(X)+", WHO TELLS YOU:":GOSUB1:X=CS(CM)
910 M$=C$(X):GOSUB1
950 NEXTK:RETURN
960 REM 
970 REM SNOW
980 X=RND(0):IFX>.65THENRETURN
990 M$="IT IS SNOWING HEAVILY "
1000 IFX<.01THEN1030:REM 1% CHANCE OF GETTING STUCK IN THE SNOW
1010 M$=M$+"BUT THE TRACKS HAVE BEEN CLEARED AND THE TRAIN WILL NOT BE DELAYED.":GOSUB1:RETURN
1030 M$=M$+"AND THE TRAIN IS FORCED TO SLOW DOWN.":GOSUB1
1040 M$="OH NO! THE TRAIN IS COMING TO A STOP. LET'S HOPE THIS IS "
1050 M$=M$+"NOT A REPEAT OF THE TRIP OF JANUARY 29, 1929 WHEN THE ORIENT "
1060 M$=M$+"EXPRESS WAS STUCK IN SNOWDRIFTS FOR FIVE DAYS.":GOSUB1:GOSUB2860
1070 M$="BUT IT LOOKS LIKE IT IS!":GOSUB1:GOSUB2860
1080 M$="YOU ARE STRANDED FOR TWO DAYS UNTIL A SNOWPLOW CLEARS THE TRACK.":GOSUB1
1090 M$="THE TRAIN IS NOW EXACTLY TWO DAYS BEHIND SCHEDULE.":GOSUB1:HY=HY+2:RETURN
1100 REM 
1110 REM BANDITS
1120 IFRND(0)>.04THENRETURN
1130 IFHX=1THENRETURN
1131 HX=1
1140 M$="YOU ARE RUDELY AWAKENED FROM A DEEP SLEEP BY A LOUD NOISE AS THE TRAIN JERKS TO A HALT.":GOSUB1:GOSUB2890
1160 M$="YOU ARE SHOCKED TO SEE A BANDIT WAVING A GUN IN YOUR FACE.":GOSUB1
1170 M$="HE DEMANDS THAT YOU GIVE HIM YOUR WALLET, JEWELRY, AND WATCH.":GOSUB1
1180 GOSUB2860:M$="THE BANDITS ARE OFF THE TRAIN IN A FEW MOMENTS WITH":GOSUB1
1190 M$="THEIR LOOT. THEY DISAPPEAR INTO THE FOREST. NO ONE "
1200 M$=M$+"WAS INJURED, AND THE TRAIN RESUMES ITS JOURNEY.":GOSUB1:RETURN
1210 REM 
1220 REM MISCELLANEOUS HAZARDS
1230 IFRND(0)>.02THEN RETURN:REM 2% CHANCE OF DERAILMENT
1240 IFHW=1THENRETURN
1241 HW=1
1250 M$="YOU HEAR A LOUD SCREECHING NOISE AS THE TRAIN COMES TO A "
1260 M$=M$+"CRASHING STOP. THE ENGINE, TENDER, AND FIRST COACH ARE "
1270 M$=M$+"LEANING AT A CRAZY ANGLE.":GOSUB1:M$="PEOPLE ARE SCREAMING.":GOSUB1:GOSUB2860
1280 M$="WHILE NOT AS BAD AS THE DERAILMENT AT VITRY-LE-FRANCOIS IN "
1290 M$=M$+"NOVEMBER 1911, THERE IS NO QUESTION THAT THE FRONT OF THE "
1300 M$=M$+"TRAIN HAS LEFT THE TRACK.":GOSUB1:GOSUB2860
1310 M$="YOU ARE STRANDED FOR EXACTLY ONE DAY WHILE THE TRACK IS "
1320 M$=M$+"REPAIRED AND A NEW LOCOMOTIVE OBTAINED.":GOSUB1:HY=HY+1:RETURN
1330 REM 
1340 REM SUBROUTINE TO IDENTIFY DEFECTOR AND KILLER
1350 M$=" THE TURKISH POLICE HAVE BOARDED THE TRAIN.":GOSUB1
1360 M$=" THEY HAVE BEEN ASKED TO ASSIST YOU, BUT FOR THEM TO DO SO YOU WILL HAVE TO "
1370 M$=M$+"IDENTIFY THE KILLER (THE DEALER IN MACHINE GUNS) AND THE DEFECTOR "
1380 M$=M$+"(THE SCOTCH DRINKER) TO THEM.":GOSUB1:M$="THE ARMS DEALERS ARE LINED "
1390 M$=M$+"UP AS FOLLOWS:":GOSUB1:M$=" (1) AUSTRIAN, (2) TURK, "
1400 M$=M$+"(3) POLE, (4) GREEK, (5) ROMANIAN.":GOSUB1
1410 M$="WHO IS THE DEFECTOR (A NUMBER PLEASE)?":GOSUB1:GOSUB60:A1=VAL(A$):IFA1<1ORA1>5THEN1410
1411 M$="--> "+A$:GOSUB1
1420 M$="AND WHO IS THE KILLER?":GOSUB1:GOSUB60:A2=VAL(A$):IFA2<1ORA2>5THEN1420
1421 M$="--> "+A$:GOSUB1:GOSUB2860
1430 M$="THE POLICE TAKE INTO CUSTODY THE MAN YOU IDENTIFIED AS THE "
1440 M$=M$+"KILLER AND PROVIDE A GUARD TO RIDE ON THE TRAIN WITH THE "
1450 M$=M$+"DEFECTOR.":GOSUB1:M$="YOU RETURN TO YOUR COMPARTMENT, PRAYING THAT "
1460 M$=M$+"YOU MADE THE CORRECT DEDUCTIONS AND IDENTIFIED THE RIGHT MEN.":GOSUB1
1470 GOSUB2860:RETURN
1480 REM 
1490 REM CHECK THE IDENTITIES
1500 IFA1=A3ORA2=A3THEN1600:REM DEFECTOR SAVED?
1510 M$="YOU ARE SUDDENLY AWAKENED BY WHAT SOUNDED LIKE A GUNSHOT. "
1520 M$=M$+"YOU RUSH TO THE DEFECTOR'S COMPARTMENT, BUT HE IS OKAY.":GOSUB1
1530 M$="HOWEVER, ONE OF THE OTHER ARMS DEALERS HAS BEEN SHOT.":GOSUB1:GOSUB2860
1540 M$="YOU REVIEW THE DETAILS OF THE CASE IN YOUR MIND AND REALIZE "
1550 M$=M$+"THAT YOU CAME TO THE WRONG CONCLUSION AND DUE TO YOUR MISTAKE "
1560 M$=M$+"A MAN LIES DEAD AT THE HANDS OF BANDITS.":M$="YOU RETURN TO YOUR "
1570 M$=M$+"COMPARTMENT AND ARE CONSOLED BY THE THOUGHT THAT YOU CORRECTLY "
1580 M$=M$+"IDENTIFIED THE KILLER AND THAT HE WILL HANG FOR HIS CRIMES.":GOSUB1
1590 REM 
1600 IFA1=A3ANDA2=A4THENA5=1
1601 IFA2=A4THENRETURN
1610 GOSUB2890:M$="A MAN IS STANDING OUTSIDE. HE SAYS, 'YOU MADE A "
1620 M$=M$+"MISTAKE. A BAD ONE. YOU SEE, I AM THE MACHINE-GUN DEALER.'":GOSUB1
1630 IFA2<>A3THEN1660
1640 M$="'MOREOVER, YOU INCORRECTLY IDENTIFIED THE MAN WHO WAS COOPERATING "
1650 M$=M$+"WITH YOU AS THE KILLER. SO THE STATE WILL TAKE CARE OF HIM. HA.'":GOSUB1
1660 GOSUB2860:M$="HE DRAWS A GUN. BANG. YOU ARE DEAD.":GOSUB1
1670 M$="YOU NEVER KNOW THAT THE TRAIN ARRIVED AT 12:30, RIGHT ON "
1680 M$=M$+"TIME AT CONSTANTINOPLE, TURKEY.":GOSUB1:GOSUB2860:GOSUB2860
1690 GOTO3190
1695 PRINT@480,;:M$="   PRESS ANY KEY TO CONTINUE.":GOSUB1
1696 M$=INKEY$:C1=RND(0):IFM$=""THEN1696
1697 RETURN
1700 REM 
1710 REM SUBROUTINE TO SET THE SCENARIO
1720 M$=" IT IS FEBRUARY 1923.":GOSUB1:M$=" THE FOLLOWING NOTE IS RECEIVED AT "
1730 M$=M$+"WHITEHALL: 'IF YOU WILL FURNISH ME WITH A NEW IDENTITY AND A "
1740 M$=M$+"LIFETIME SUPPLY OF SCOTCH, I WILL GIVE UP MY LIFE OF ARMS DEALING "
1750 M$=M$+"AND WILL PROVIDE YOU WITH MUCH VALUABLE INFORMATION.":GOSUB1:GOSUB1695:GOSUB5
1760 M$=" I WILL BE ON THE ORIENT EXPRESS TONIGHT.":GOSUB1:M$=" YOU MUST CONTACT ME BEFORE "
1770 M$=M$+"THE TRAIN REACHES UZUNKOPRU OR THAT SWINE DEALER OF MAXIM MACHINE "
1780 M$=M$+"GUNS WILL HAVE ME KILLED BY BANDITS LIKE HE DID TO BARON WUNSTER "
1790 M$=M$+"LAST MONTH.'":GOSUB1:M$=" THE NOTE IS NOT SIGNED.":GOSUB1:GOSUB1695:GOSUB5
1800 M$=" YOU, A BRITISH AGENT, ARE ASSIGNED TO TAKE THE TRAIN, RESCUE "
1810 M$=M$+"THE DEFECTOR, AND ARREST THE KILLER.":GOSUB1
1820 M$=" YOU KNOW THERE ARE FIVE NOTORIOUS ARMS DEALERS OF DIFFERENT "
1830 M$=M$+"NATIONALITIES OPERATING IN EUROPE UNDER AN UNEASY TRUCE AS EACH "
1840 M$=M$+"DEALS IN A DIFFERENT KIND OF WEAPON.":GOSUB1:M$="BUT IT IS OBVIOUS THAT THE "
1850 M$=M$+"TRUCE HAS ENDED.":GOSUB1:RETURN
1860 M$="PRESS ANY KEY TO CALL A TAXI...":GOSUB1
1861 M$=INKEY$:IFM$=""THEN1861
1862 RETURN
1870 REM 
1880 REM SUBROUTINE TO READ MEALS, CONVERSATIONS, HAZARDS, DAY, TIMES, LOCATION
1890 FORI=1TO24:READX,ME(I),CN(I),HZ(I),DA(I),TA(I),TD(I),LA$(I),LB$(I)
1900 NEXTI:RETURN
1910 DATA 1,0,0,0,1,0,1430,"LONDON","ENGLAND"
1920 DATA 2,1,2,0,1,1855,1919,"CALAIS","FRANCE"
1930 DATA 3,0,1,0,1,2233,2253,"PARIS (NORD)","FRANCE"
1940 DATA 4,4,0,0,1,2316,2350,"PARIS (LYON)","FRANCE"
1950 DATA 5,4,0,1,2,600,620,"VALLORBE","SWITZERLAND"
1960 DATA 6,0,1,0,2,700,707,"LAUSANNE","SWITZERLAND"
1970 DATA 7,3,1,1,2,732,734,"MONTREUX","SWITZERLAND"
1980 DATA 8,0,1,1,2,919,927,"BRIG","SWITZERLAND"
1990 DATA 9,0,3,0,2,1005,1025,"DOMODOSSOLA","ITALY"
2000 DATA 10,2,2,0,2,1223,1320,"MILAN","ITALY"
2010 DATA 11,1,2,0,2,1705,1730,"VENICE (S. LUCIA)","ITALY"
2020 DATA 12,0,1,0,2,1954,2014,"TRIESTE","(FREE STATE)"
2030 DATA 13,0,1,0,2,2044,2110,"OPICINA","ITALY"
2040 DATA 14,0,2,0,2,2119,2225,"SEZANA","SLOVENIA"
2050 DATA 15,4,0,0,3,21,107,"LJUBLJANA","SLOVENIA"
2060 DATA 16,4,0,0,3,310,330,"ZAGREB","CROATIA"
2070 DATA 17,3,2,0,3,900,956,"BELGRADE","SERBIA"
2080 DATA 18,2,1,0,3,1334,1356,"CRVENI KRST","SERBIA"
2090 DATA 19,0,2,0,3,1555,1634,"CARIBROD","SERBIA"
2100 DATA 20,1,2,0,3,1856,1935,"SOFIA","BULGARIA"
2110 DATA 21,4,0,2,4,45,120,"SVILENGRAD","BULGARIA"
2120 DATA 22,4,0,2,4,406,445,"PITHION","GREECE"
2130 DATA 23,3,0,3,4,505,545,"UZUNKOPRU","TURKEY"
2140 DATA 24,0,0,0,4,1230,0,"CONSTANTINOPLE","TURKEY"
2150 REM 
2160 REM SUBROUTINE TO READ STATEMENTS OF TRAVELERS
2170 FORI=1TO24:READCS(I),CP(I),C$(I):NEXTI:RETURN
2180 DATA1,0
2181 DATA"I'VE HEARD THEY ALL HAVE DIFFERENT COLOR CHALETS ON A NORTH-SOUTH RIDGE IN THE TYROL REGION."
2190 DATA2,0,"THE AUSTRIAN SAID HE LIKES THE LOOK OF NATURAL WOOD AND WOULD NEVER PAINT HIS CHALET."
2200 DATA3,0
2201 DATA"THEY GAVE THE WAITER A DIFFICULT TIME. THE TURK ORDERED BEER AND THE OTHER FOUR ALL ORDERED DIFFERENT DRINKS."
2210 DATA4,0
2211 DATA"THE GREEK TOLD ME HE HUNTS DEER, BUT HE NEVER HUNTS WITH ANY OF THE OTHERS BECAUSE THEY ALL HUNT DIFFERENT ANIMALS."
2220 DATA5,1
2221 DATA"MY BROTHER DELIVERED A CASE OF KIRSCH TO THE GREEN CHALET. HE RECALLS IT BEING JUST SOUTH OF THE GAUDY RED CHALET."
2230 DATA6,0
2231 DATA"THE POLE ASKED ME--CAN YOU IMAGINE THAT?--IF I WANTED TO BUY ANY HOWITZERS."
2240 DATA7,2,"ONE OF THEM ASKED ME TO COOK SOME PHEASANT THAT HE SHOT. HE SAID THAT I SHOULD COME TO THE YELLOW CHALET."
2250 DATA8,1,"ONE TIME MY BROTHER SAID HE DELIVERED A CASE OF COGNAC TO THE MIDDLE CHALET."
2260 DATA9,0,"THE ROMANIAN SAID HE HAD THE SHORTEST DISTANCE TO DRIVE FROM HIS CHALET TO THE RAILROAD STATION AT MUNICH."
2270 DATA10,0,"ONE OF THEM BRAGGED THAT HIS MILITARY RIFLES WERE SO ACCURATE THAT HE BAGGED A FOX WITH ONE OF THEM."
2280 DATA11,0
2281 DATA"THE MAN WHO HUNTS WILD BOAR SAID THAT THE PISTOL DEALER IN THE CHALET NEXT TO HIM OFTEN GIVES LOUD PARTIES."
2290 DATA12,0
2291 DATA"THE PHEASANT HUNTER COMPLAINED THAT THE ARMS DEALER IN THE CHALET NEXTDOOR MAKES TOO MUCH NOISE TESTING HIS MORTARS."
2300 DATA13,0,"THE GIN DRINKER BRAGGED THAT HE SHOT SIXTY WARTHOGS ON A SINGLE DAY LAST AUGUST."
2310 DATA14,0,"THE ROMANIAN SAID HE LOOKS OUT ON A BLUE CHALET."
2320 DATA15,0
2321 DATA"THE COGNAC DRINKER BRAGGED THAT HE IS THE BEST HUNTER AND CAN DRINK MORE THAN ALL OF THE REST OF THEM COMBINED."
2330 DATA16,0,"THE ONE CARRYING THE PISTOL SAID HE THINKS THE BOAR'S HEAD OVER HIS NEIGHBOR'S DOORWAY IS REVOLTING."
2340 DATA17,0
2341 DATA"ONE OF THEM SAID THAT ONE DAY HE'LL LOB A MORTAR SHELL AT THE STRING OF PHEASANTS DRYING IN HIS NEIGHBOR'S YARD."
2350 DATA18,0,"THE KIRSCH DRINKER SAID HE LOVED THE ROAST CHICKEN HE HAD TO EAT LAST NIGHT."
2360 DATA19,0,"THE ONE CARRYING THE PISTOL HAD A SECOND HELPING OF PIE."
2370 DATA20,0,"ONE COMMENTED THAT HIS BEEF DINNER WASN'T NEARLY AS GOOD AS THE BOAR THAT HE SHOT LAST WEEK."
2380 DATA21,0,"THE POLE ASKED FOR MORE SOUP."
2390 DATA22,0,"THE ONE EATING ALL THE CHEESE MUMBLED THAT IT WAS THE SAME COLOR AS HIS CHALET."
2400 DATA23,0,"THE ROMANIAN AND AUSTRIAN GOT COMPLETELY DRUNK LAST NIGHT."
2410 DATA24,0,"I'D LIKE TO VISIT THE BLUE CHALET. THE OWNER IS SAID TO SERVE EXCELLENT LOBSTER."
2420 REM 
2430 REM SUBROUTINE TO READ THE NAMES OF THOSE ON THE TRAIN
2440 FORI=1TO25:READN$(I):NEXTI:RETURN
2450 DATA "R. BRUNDT (A WAITER)","C. D'ARCY (A CHEF)"
2460 DATA "HERBERT HOOVER","BARON ROTHSCHILD","GUIDO FAMADOTTA","GUSTAV MAHLER"
2470 DATA "ROBERT BADEN-POWELL","FRITZ KREISLER","DAME MELBA","GERALD MURPHY"
2480 DATA "CALOUSTE GULBENKIAN","CAPTAIN G.T. WARD","SIR ERNEST CASSEL"
2490 DATA "MAJOR CUSTANCE","F. SCOTT FITZGERALD","ELSA MAXWELL","MATA HARI"
2500 DATA "CLAYTON PASHA","ARTURO TOSCANINI","MAHARAJAH BEHAR","LEON WENGER"
2510 DATA "SARAH BERNHARDT","ARTHUR VETTER","ISADORA DUNCAN","DAVID K.E. BRUCE"
2520 REM 
2530 REM SUBROUTINE TO READ MENUS
2540 FORI=1TO13:READMB$(I):NEXTI:REM BREAKFAST SELECTIONS
2550 FORI=1TO26:READMD$(I):NEXTI:REM DINNER SELECTIONS
2560 RETURN
2570 DATA "VARIETE JUS DE FRUITS","PRUNES MACEREES DANS LE VIN"
2580 DATA "DEMI PAMPLEMOUSE","TROIS OEUFS SUR LE PLAT","OEUFS POCHES"
2590 DATA "OMELETTE AUX CHAMPIGNONS","TRANCHES DE PAIN BEUREES ET CONFITUREES"
2600 DATA "GALETTES","POMMES-FRITES","PATISSERIES","CROISSANTS","YOGURT"
2610 DATA "CAFE, THE, LAIT, VIN, EAU MINERALE"
2620 DATA "HUITRES DE BEERNHAM","CANTALOUP GLACE AU MARSALE"
2630 DATA "COMPOTE DES TOMATES FRAICHES","POTAGE REINE"
2640 DATA "LA NATTE DE SOLE AU BEURRE","TRUITE DE RIVIERE MEUNIERE"
2650 DATA "POULET DE GRAIN GRILLE A DIABLE","ROTI DE VEAU A L'OSILLE"
2660 DATA "TRUITE SAUMONEE A LA CHAMBORD","CHAUD-FROID DE CANETON"
2670 DATA "CHAUDFROIX DES LANGOUSTE A LA PARISIENNE"
2680 DATA "LES NOISETTES DE CHEVREUIL RENAISSANCE","BECASSES A LA MONACO"
2690 DATA "POINTES D'ASPERGE A LA CREME","PARFAIT DE FOIES GRAS"
2700 DATA "SALADE CATALANE","TRUFFES AU CHAMPAGNE"
2710 DATA "TAGLIATELLE DE CAROTTES ET COURGETTES","SOUFFLE D'ANISETTE"
2720 DATA "CREME DE CARAMEL BLOND","SORBET AUX MURES DE FRAMBOISIER"
2730 DATA "LA SELECTION DU MAITRE FROMAGER","CORBEILLE DE FRUITS"
2740 DATA "LES MIGNARDISES","SELECTION DU VINS ET LIQUORS","POUTINE RAPAIS"
2750 REM 
2760 REM SUBROUTINE TO SHUFFLE 24 INTEGERS
2770 FORI=1TO23
2780 K=I+INT((25-I)*RND(0)):X=CS(I):CS(I)=CS(K):CS(K)=X:NEXT I
2790 RETURN
2800 REM 
2810 GOSUB60
2820 IFLEFT$(A$,1)="Y"THENA=0:RETURN
2830 IFLEFT$(A$,1)="N"THENA=1:RETURN
2840 M$="PLEASE ENTER Y FOR 'YES' OR N FOR 'NO.' WHICH IS IT?":GOSUB1:GOTO2810
2850 REM 
2860 REM SUBROUTINE CREATES A SHORT PAUSE
2870 FORX=1TO1500:NEXT:RETURN
2880 REM 
2890 REM SUBROUTINE TO RING BUZZER AND OPEN DOOR
2900 M$="YOUR COMPARTMENT BUZZER RINGS...":GOSUB1
2910 SOUND1,1:FORKA=1TO300:NEXT:SOUND1,1
2920 M$="PRESS ANY KEY TO OPEN THE DOOR.":GOSUB1
2930 M$=INKEY$:IFM$=""THEN2930
2931 RETURN
2940 REM 
2950 REM SUBROUTINE TO FINISH EATING
2960 M$="PRESS ANY KEY WHEN YOU HAVE FINISHED EATING":GOSUB1:A3=A3+5*(J+1)-(18+43):A4=A3-1
2970 M$=INKEY$:IFM$=""THEN2970
2971 GOSUB5:RETURN
2980 REM 
2990 REM SUBROUTINE TO PRODUCE TRAIN NOISES
3000 M$="CLACKETY CLACK...CLACKETY CLACK...CLACKETY CLACK":GOSUB1
3010 IFRND(0)>.5THENRETURN
3011 FORKA=6TO1STEP-1
3020 FORI=1TO4:SOUND130,1:FORK=1TO180:NEXT:NEXT
3030 IFKA=4THENGOSUB3110
3040 FORK=1TO50+KA*120:NEXT:NEXT:RETURN
3050 REM 
3060 REM SUBROUTINE TO PRINT TIME
3070 T=T+10000:T$=STR$(T):IFVAL(RIGHT$(T$,2))>59THENT=T+40
3080 T$=STR$(T):M$=M$+" "+MID$(T$,3,2)+":"+RIGHT$(T$,2)+" "
3090 RETURN
3100 REM 
3110 REM SUBROUTINE TO BLOW TRAIN WHISTLE
3120 IFRND(0)>.5THENRETURN
3130 SOUND50,5:FORK=1TO1400:NEXT:SOUND50,1
3140 FORK=1TO200:NEXT:SOUND50,5:RETURN
3150 REM 
3160 REM ERROR-HANDLING SUBROUTINE
3170 A5=ERR:RESUME NEXT
3180 REM 
3190 REM END OF JOURNEY
3200 M$="YOUR JOURNEY HAS ENDED. GEORGES NAGELMACKERS AND THE MANAGEMENT OF CIE. INTERNATIONALE DES WAGONS-LITS "
3220 M$=M$+"HOPE YOU ENJOYED YOUR TRIP ON THE ORIENT EXPRESS, THE MOST FAMOUS TRAIN IN THE WORLD.":GOSUB1
3240 IFA5<>1THEN3310
3241 SOUND1,1:SOUND1,1:SOUND1,1:X=0
3250 M$="WHITEHALL TELEGRAPHS TO CONGRATULATE YOU FOR IDENTIFYING BOTH "
3260 M$=M$+"THE KILLER AND DEFECTOR CORRECTLY.":GOSUB1:GOSUB2860:PRINT"congratulations";
3270 X=0:FORI=1TO25:FORK=1TO50:NEXTK:SOUND1,1
3280 IFX=0THENFORC1=1TO15:PRINTCHR$(8);:NEXT:PRINT"CONGRATULATIONS";:X=1:GOTO3300
3290 FORC1=1TO15:PRINTCHR$(8);:NEXT:PRINT"congratulations";:X=0:REM X$=ERASE CONGRATULATIONS
3300 NEXTI:FORC1=1TO15:PRINTCHR$(8);:NEXT:M$="CONGRATULATIONS!":GOSUB1
3310 M$="WOULD YOU LIKE TO RIDE AGAIN? ":GOSUB1:GOSUB2810
3320 IFA=0THENM$="OKAY. GOOD JOURNEY!":GOSUB1:GOSUB2860:RUN
3330 M$="OKAY. SO LONG FOR NOW.":GOSUB1:GOSUB2860:GOSUB5:END
