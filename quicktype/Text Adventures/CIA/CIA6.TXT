0 CLEAR 500:DIMC1,CC,M$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO5
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
5 GOSUB9000:DIM N1$(50),N$(50),N(50),V$(18),LO(30,4),LO$(30):FL=1:DR=0:DG=-1:T=0:EL=0:BU=0:DO=0:CA=0:BA=0:TE=0:GL=0
7 PA=0:RO=0:FOR R=1 TO 5:R1=INT(9*RND(9)):CO$=CO$+STR$(R1):NEXT:LO=1:A9$=">------------------------------<"
8 R=LEN(CO$):CO$=RIGHT$(CO$,R-1)
10 FOR R=1 TO 50:READ N1$(R),N$(R),N(R):NEXT
11 FOR R=1 TO 30:READ LO$(R),LO(R,1),LO(R,2),LO(R,3),LO(R,4):NEXT
12 FOR R=1 TO 18:READ V$(R):NEXT:GOTO20
13 Z9=0:FOR R=1 TO 50:IF V1$=N$(R) AND (N(R)=LO OR N(R)=-1) THEN Z9=1:RETURN
14 NEXT:PRINT "I DON'T SEE THAT HERE.":Z9=2:RETURN
15 FORR=1TO3500:NEXT:RETURN
20 M$="WE ARE "+LO$(LO)+".":GOSUB1
30 FOR R=1 TO 46:IF N(R)=LO THEN M$="I CAN SEE "+N1$(R)+".":GOSUB1
35 NEXT:B=0:FOR R=1 TO 4:IF LO(LO,R)>0 THEN B=1
40 NEXT:IF B=0 THEN 46
41 M$="WE COULD EASILY GO: ":IF LO(LO,1)>0 THEN M$=M$+"NORTH "
42 IF LO(LO,2)>0 THEN M$=M$+"SOUTH "
43 IF LO(LO,3)>0 THEN M$=M$+"EAST "
44 IF LO(LO,4)>0 THEN M$=M$+"WEST "
45 GOSUB1
46 PRINTA9$;
50 T=T+1:IF DG>0 THEN DG=DG-1
53 IF T=1 THEN INPUT"ENTER YOUR NAME PARTNER";NA$:CLS
55 GOTO2000
56 N=0:FOR R=1 TO 46:IF N(R)=-1 THEN N=N+1
57 NEXT
60 PRINT:INPUT"WHAT SHOULD I DO";A$
65 GOSUB3000:V$=LEFT$(A$,3):GOSUB3010:FOR R=1 TO 18:IF V$=V$(R) THEN 69
66 NEXT:IF V$="ORD" THEN 5000
67 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB1:GOTO60
69 IF V$="QUI" THEN 850
70 V=R:FOR R=1 TO LEN(A$):IF MID$(A$,R,1)=" " THEN 75
71 NEXT:IF V$="LOO" THEN 20
72 IF V$="BON" THEN 900
73 IF V$="INV" THEN 950
74 PRINT"PLEASE USE 2 WORD COMMANDS.":GOTO60
75 V1$=MID$(A$,R+1,3):FOR R=1 TO 49:IF V1$=N$(R) THEN 80
76 NEXT:IF V1$="NOR" OR V1$="SOU" OR V1$="EAS" OR V1$="WES" THEN 80
77 M$="I DON'T KNOW WHAT IT IS YOU ARE TALKING ABOUT.":GOSUB1:GOTO60
80 ON V GOTO100,150,200,250,300,350,400,450,500,550,600,650,700,750,800,850,900,950
100 IF V1$="NOR" AND LO(LO,1)>0 THEN LO=LO(LO,1):GOTO20
101 IF V1$="SOU" AND LO(LO,2)>0 THEN LO=LO(LO,2):GOTO20
102 IF V1$="EAS" AND LO(LO,3)>0 THEN LO=LO(LO,3):GOTO20
103 IF V1$="WES" AND LO(LO,4)>0 THEN LO=LO(LO,4):GOTO20
104 IF V1$="NOR" OR V1$="SOU" OR V1$="EAS" OR V1$="WES"THEN 135
105 GOSUB13:IF Z9=2 THEN 50
110 IF R=8 THEN LO=5:GOTO20
112 IF R=37 AND RO=1 AND LO=20 THEN LO=21:GOTO20
115 IF R=10 THEN LO=11:GOTO20
120 IF R=14 THEN LO=13:GOTO20
125 IF R=33 THEN LO=3:GOTO20
130 IF R=34 AND DO=1 THEN LO=9:GOTO20
135 M$="I CAN'T GO THAT WAY AT THE MOMENT.":GOSUB1:GOTO50
150 GOSUB13:IF Z9=2 THEN 50
155 IF R=2OR R=3OR R=4OR R=6OR R=15OR R=16OR R=20OR R=21OR R=22 OR R=23 OR R=25 OR R=26 OR R=27 OR R=28 OR R=30 THEN 160
156 IF R=31 OR R=37 OR R=39 OR R=40 OR R=42 OR R=44 OR R=45 OR R=46 THEN 160
157 PRINT"I CAN'T CARRY THAT!":GOTO50
160 IF N(R)=-1 THEN PRINT"I ALREADY HAVE IT.":GOTO50
163 IF N>=5 THEN PRINT"I CAN'T CARRY ANYMORE.":GOTO50
165 PRINT"O.K.":N(R)=-1
170 IF V1$="PAI" AND PA=0 THEN PRINT"SOMETHING FELL FROM THE FRAME!":N(31)=LO:PA=1
173 IF V1$="TEL" THEN TE=0
175 GOTO50
200 FOR R=1 TO 46:IF V1$=N$(R) AND N(R)=-1 THEN 205
201 NEXT:M$="I DON'T SEEM TO BE CARRYING IT.":GOSUB1:GOTO50
205 IF NOT(R=30) THEN 207 
206 M$="I DROPPED THE CUP BUT IT BROKE INTO SMALL PIECES. THE COFFEE SOAKED INTO THE GROUND.":GOSUB1:N(30)=0:DR=0:GOTO50
207 IF R=45 THEN GL=0
208 IF R=31 AND N(30)=-1 THEN M$="O.K. I DROPPED IT. BUT IT FELL IN THE COFFEE!":GOSUB1:N(31)=0:DR=1:GOTO50
210 M$="O.K. I DROPPED IT.":GOSUB1:N(R)=LO:GOTO50
250 IF V1$="BUT" AND LO=3 AND DO=0 THEN M$="THE DOORS OPEN WITH A WHOOSH!":GOSUB1:DO=1:GOTO50
251 IF N(46)=-1 AND V1$="BUT" THEN M$="I PUSH THE BUTTON ON THE BOX AND ":GOTO268
252 GOSUB13
255 IFZ9=2THEN50
256 IFR=17ANDGL<>1THENM$="THERE'S ELECTRICITY COURSING THROUGH THE SQUARE! I'M BEING ELECTROCUTED!":GOSUB1:GOTO10000
260 IFR=35AND BU=0THENM$="THE BUTTON ON THE WALL GOES IN.... CLICK! SOMETHING SEEMS DIFFFERENT NOW.":GOSUB1:BU=1:GOTO50
265 IFR=47ANDFL<>1THENLO(9,1)=3:FL=1:GOTO280
266 IFR=48ANDFL<>2THENLO(9,1)=8:FL=2:GOTO280
267 IFR=49ANDFL<>3THENLO(9,1)=10:FL=3:GOTO280
268 IFN(46)=-1AND(LO=6ORLO=29)THENM$=M$+"THERE IS A BLINDING FLASH....":GOSUB1:GOSUB15:LO=1:FL=1:LO(9,1)=3:GOTO20
270 M$=M$+"NOTHING HAPPENS.":GOSUB1:GOTO50
280 M$="THE DOORS CLOSE AND I FEEL AS IF THE ROOM IS MOVING. SUDDENLY THE DOORS OPEN AGAIN.":GOSUB1:GOSUB15:GOTO20
300 GOSUB13:IF Z9=2 THEN 50
305 IF R=18 AND GL<>1 THEN M$="THE LEVER HAS ELECTRICITY COURSING THROUGH IT! I'M BEING ELECTROCUTED!":GOSUB1:GOTO10000
310 IFR=18AND EL=0THENM$="THE LEVER GOES ALL THE WAY UP AND CLICKS. SOMETHING SEEMS DIFFERENT NOW.":GOSUB1:EL=1:GOTO50
315 PRINT"NOTHING HAPPENS.":GOTO50
350 IF V1$="DRA" AND LO=5 THEN PRINT"IT LOOKS FRAGILE.":GOTO50
353 GOSUB13:IF Z9=2 THEN 50
355 IFR=1 AND BA<>1 THEN PRINT"THERE'S NO POWER FOR IT.":GOTO50
356 IFR=1 AND TE<>1 THEN PRINT"THERE'S NO T.V. TO WATCH ON.":GOTO50
360 IFR=6 THEN PRINT"IT LOOKS HEAVY.":GOTO50
362 IFR=7 THEN PRINT"IT'S LOCKED.":GOTO50
364 IFR=13 THEN PRINT"IT'S A VERY STRONG BAG.":GOTO50
366 IFR=19 THEN M$="I CAN SEE A LOCKED DRAWER IN IT.":GOSUB1:GOTO50
368 IFR=22 OR R=27 THEN PRINT"THERE'S WRITING ON IT.":GOTO50
369 IFR=34 AND DO=1 THEN PRINT"THE DOORS ARE OPEN.":GOTO50
370 IFR=24 THEN M$="I CAN SEE A GLEAMING STONE IN IT.":GOSUB1:GOTO50
371 IFR=9 THEN M$="THERE IS A SMALL SLIT NEAR THE DOOR.":GOSUB1:GOTO50
372 IFR=34 THEN M$="THERE'S A BUTTON NEAR THE DOORS.":GOSUB1:GOTO50
373 IFR=41 AND BU=1 THEN PRINT"THE SCREEN IS DARK.":GOTO50
374 IFNOT(R=41) THEN 376
375 M$="I SEE A METAL PIT 1000'S OF FEET DEEP ON ONE MONITOR. ON THE OTHER SIDE OF THE PIT, I SEE A LARGE HOOK.":GOSUB1:GOTO50
377 IF R=41 THEN M$="ON THE OTHER SIDE OF THE PIT, I SEE A LARGE HOOK.":GOSUB1:GOTO50
376 IF R=43 AND BU=1 THEN PRINT"THE SCREEN IS DARK.":GOTO50
377 IF R=43 THEN M$="I SEE A ROOM WITH A CASE ON A PEDESTAL IN IT.":GOSUB1:GOTO50
378 IF R=44 THEN M$="I SEE A PICTURE OF A GRINNING JACKAL.":GOSUB1:GOTO50
380 M$="I SEE NOTHING OF INTEREST.":GOSUB1:GOTO50
400 GOSUB13:IF Z9=2 THEN 50
405 IFR<>3 AND R<>2 AND R<>4 AND R<>28 THEN M$="I CAN'T INSERT THAT!":GOSUB1:GOTO50
410 INPUT"INTO WHAT";A3$:A3$=LEFT$(A3$,3)
415 R1=R:V1$=A3$:GOSUB13:IF Z9=2 THEN 50
420 IFR1=3 AND R=1 THEN PRINT"O.K.":BA=1:N(3)=0:BA=1:GOTO50
423 IFR1=4 AND R=50 AND DG<=0 THEN PRINT"THE GUARD WON'T LET ME!":GOTO50
425 IFR1=2 AND R=1 THEN M$="O.K. THE TAPE IS IN THE RECORDER.":GOSUB1:N(2)=0:TA=1:GOTO50
430 IFNOT(R1=4 AND R=50)THEN435
431 M$="POP! A SECTION OF THE WALL OPENS.... REVEALING SOMETHING VERY INTERESTING.":GOSUB1:N(4)=0:N(5)=LO:GOTO50
435 IF R1=28 AND R=29 THEN M$="POP! A CUP OF COFFEE COMES OUT OF THE MACHINE.":GOSUB1:N(28)=0:N(30)=LO:GOTO50
440 PRINT"NOTHING HAPPENED.":GOTO50
450 IFV1$="DRA" AND LO=5 AND N(23)=0 THEN PRINT"IT'S STUCK.":GOTO50
454 GOSUB13:IF Z9=2 THEN 50
455 IFR=7 OR R=9 OR R=13 OR R=15 OR R=23 OR R=32 OR R=5 THEN 460
456 PRINT"I CAN'T OPEN THAT!":GOTO50
460 IFR=7 AND N(16)=-1 THEN M$="O.K. I OPENED THE DOOR.":GOSUB1:N(7)=0:N(8)=4:GOTO50
465 IFR=9 THEN M$="I CAN'T. IT DOESN'T WORK.":GOSUB1:GOTO50
470 IFR=13 AND N(16)=-1 THEN M$="O.K. THE CLOSET IS OPENED.":GOSUB1:N(13)=0:N(14)=14:GOTO50
475 IFR=15 THEN M$="I CAN'T. IT'S TOO STRONG.":GOSUB1:GOTO50
480 IFR=23 THEN PRINT"IT'S STUCK.":GOTO50
482 IFR=32AND N(28)=0AND N(4)=0AND SC=1THENM$="I OPEN THE SCULPTURE. SOMETHING FALLS OUT.":GOSUB1:N(28)=LO:N(4)=LO:GOTO50
485 IFR<>5 THEN 495
490 INPUT"WHAT'S THE COMBINATION";C$
491 IFC$=CO$ THEN PRINT"THE DOOR IS SLOWLY OPENING.":N(5)=0:N(9)=0:N(10)=10:GOTO50
493 M$="YOU MUST HAVE THE WRONG COMBINATION OR YOU ARE NOT SAYING IT RIGHT.":GOSUB1:GOTO50
495 M$="I CAN'T DO THAT.... YET!":GOSUB1:GOTO50
500 IFV1$="GLO" AND N(45)=-1 THEN M$="O.K. I'M NOW WEARING THE GLOVES.":GOSUB1:GL=1:GOTO50
505 M$="I CAN'T WEAR THAT!":GOSUB1:GOTO50
550 IFV1$<>"SIG" AND V1$<>"NOT" THEN M$="I CAN'T READ THAT.":GOSUB1:GOTO50
555 GOSUB13:IF Z9=2 THEN 50
560 IFNOT(R=22) THEN 565
561 PRINT"IT SAYS:":M$=NA$+", WE HAVE DISCOVERED ONE OF CHAOS'S SECRET WORDS.":GOSUB1
562 M$="IT IS: 'BOND007'. TO BE USED IN A 'TASTEFUL' SITUATION.":GOSUB1:GOTO50
565 IFR=27 THEN M$="IT SAYS: WATCH OUT! DANGEROUS!":GOSUB1:GOTO50
600 IFV1$<>"REC" THEN PRINT"I CAN'T START THAT.":GOTO50
605 GOSUB13:IF Z9=2 THEN 50
610 IFBA<>1 OR TE<>1 OR TA<>1 THEN 620
612 M$="THE RECORDER STARTS UP AND PRESENTS A SHORT MESSAGE:":GOSUB1
613 M$=NA$+", WE HAVE UNCOVERED A NUMBER THAT MAY HELP YOU.":GOSUB1
615 M$="THAT NUMBER IS: "+CO$+". PLEASE WATCH OUT FOR HIDDEN TRAPS.":GOSUB1
616 M$="ALSO, THERE IS SOMETHING IN THE SCULPTURE.":GOSUB1:SC=1:GOTO50
620 PRINT"NOTHING HAPPENED.":GOTO50
650 IFV1$<>"DRA" THEN M$="I'M TRYING TO BREAK IT, BUT I CAN'T.":GOSUB1:GOTO50
653 IFN(6)<>-1 THEN M$="I CAN'T DO THAT YET.":GOSUB1:GOTO50
655 IFLO=5 THEN M$="IT'S HARD.... BUT I GOT IT. TWO THINGS FELL OUT.":GOSUB1:N(3)=LO:N(22)=LO:N(23)=LO:GOTO50
660 PRINT"NOTHING HAPPENS.":GOTO50
700 GOSUB13:IF Z9=2 THEN 50
705 IFR<>15 AND R<>24 THEN M$="I'M TRYING. IT DOESN'T WORK.":GOSUB1:GOTO50
710 IFN(25)<>-1 THEN M$="I CAN'T DO THAT YET.":GOSUB1:GOTO50
715 IFR=15 THEN M$="RIP! THE BAG GOES TO PIECES, AND SOMETHING FALLS OUT!":GOSUB1:N(15)=0:N(2)=LO:GOTO50
720 IFR=24 THEN M$="I CUT THE CASE AND REACH IN TO PULL SOMETHING OUT.":GOSUB1:N(26)=-1:GOTO50
750 IFV1$<>"ROP" THEN M$="I CAN'T THROW THAT.":GOSUB1:GOTO50
752 IFN(37)<>-1 THEN M$="I CAN'T DO THAT YET.":GOSUB1:GOTO50
755 INPUT"AT WHAT";A3$:A3$=LEFT$(A3$,3)
760 IFA3$<>"HOO" THEN PRINT"O.K. I THREW IT.":N(37)=LO:GOTO50
765 IFLO<>20 THEN PRINT"I CAN'T DO THAT YET.":GOTO50
770 M$="I THREW THE ROPE AND IT SNAGGED ON THE HOOK.":GOSUB1:RO=1:N(37)=LO:GOTO50
800 IFV1$<>"TEL" THEN M$="I CAN'T CONNECT THAT.":GOSUB1:GOTO50
803 IFN(40)<>LO THEN M$="I DON'T SEE THE TELEVISION HERE.":GOSUB1:GOTO50
805 IFTE=1 THEN M$="I DID THAT ALREADY.":GOSUB1:GOTO50
810 IFLO<>2 THEN M$="I CAN'T DO THAT.... YET!":GOSUB1:GOTO50
815 M$="O.K. THE T.V. IS CONNECTED.":GOSUB1:TE=1:GOTO50
850 M$="WHAT? YOU WOULD LEAVE ME HERE TO DIE ALONE?":GOSUB1
855 M$="JUST FOR THAT, I'M GOING TO DESTROY THE GAME.":GOSUB1:PRINT:PRINT:PRINT"BOOOOOOOOOOM!":GOSUB15:GOTO22222
900 IFNOT(LO=14) THEN 905
901 M$="WHOOPS! A TRAP DOOR OPENED UNDERNEATH ME AND I FIND MYSELF FALLING.":GOSUB1:GOSUB15:LO=17:GOTO20
905 M$="NOTHING HAPPENED.":GOSUB1:GOTO50
950 Z8=0:M$="WE ARE PRESENTLY CARRYING:":GOSUB1:M$="":FORR=1TO46
955 IFN(R)<>-1 THEN 965
960 M$=M$+N1$(R):IF R=45 AND GL=1 THEN M$=M$+", WHICH I'M WEARING"
961 M$=M$+", "
963 Z8=1
965 NEXT
970 IFZ8=0 THEN M$="NOTHING  "
975 M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1:GOTO50
2000 IFLO=3AND N(39)=-1THENM$="THE DOOR MAN LOOKS AT MY BADGE AND THEN THROWS ME OUT.":GOSUB1:GOSUB15:LO=1:GOTO20
2005 IFNOT(LO=10 AND N(42)<>-1) THEN 2010
2006 M$="THE GUARD LOOKS AT ME SUSPICIOUSLY, THEN THROWS ME BACK.":GOSUB1:GOSUB15:LO=9:GOTO20
2010 IFLO=6 AND BU<>1 THEN M$="SIRENS GO OFF ALL AROUND ME! GUARDS RUN IN AND SHOOT ME TO DEATH!":GOSUB1:GOTO10000
2015 IFLO=11 AND EL<>1 THEN M$="THE FLOOR IS WIRED WITH ELECTRICITY! I'M BEING ELECTROCUTED!":GOSUB1:GOTO10000
2020 IFNOT(LO=10 AND N(30)=-1 AND DR=1) THEN 2025
2021 M$="THE GUARD TAKES MY COFFEE, DRINKS IT, AND FALLS TO SLEEP RIGHT AWAY.":GOSUB1
2022 DG=5+INT(10*RND(10)):N(11)=0:N(12)=10:DR=0:N(30)=0:GOTO56
2025 IFDG=0 THEN M$="I HEAR A NOISE LIKE SOMEONE IS YAWNING.":GOSUB1:N(11)=10:N(12)=0:GU=-2:DG=-1:GOTO56
2030 IFLO=1 AND N(26)=-1 THEN M$="HURRAY! YOU'VE RECOVERED THE RUBY! YOU WIN!":GOSUB1:GOTO10020
2035 IFT>375 THEN M$="I THINK THEY ARE ON TO ME....I HEAR NOISES.":GOSUB1
2040 IFT=400 THEN M$="OH NO! THEY CAUGHT UP TO ME! HELP! THEY'RE PULLING OUT GUNS!":GOSUB1:GOTO10000
2045 IFT=1 THEN M$="WRITING ON THE WALL SAYS: IF YOU WANT INSTRUCTIONS TYPE: ORDERS PLEASE":GOSUB1
2046 IFLO=6 AND LO(6,2)<>0 THEN M$="A SECRET DOOR SLAMS DOWN BEHIND ME!":GOSUB1:LO(6,2)=0:GOTO56
2047 IFLO=10 AND GU=-2 THEN M$="THE GUARD DRAWS HIS GUN AND SHOOTS ME!":GOSUB1:GOTO10000
2050 GOTO56
2198 PRINT"(HIT A KEY)";
2199 I$=INKEY$:ON1-(I$<>"")GOTO2199:FORT=1TO11:PRINTCHR$(8);:NEXT:RETURN
3000 IFA$="N"THEN A$="GO NORTH":RETURN
3002 IFA$="S"THEN A$="GO SOUTH":RETURN
3004 IFA$="E"THEN A$="GO EAST":RETURN
3006 IFA$="W"THEN A$="GO WEST":RETURN
3007 IFA$="I"THEN A$="INV":RETURN
3008 RETURN
3010 IF V$="WAL" OR V$="RUN" THEN V$="GO ":RETURN
3012 IF V$="TAK" OR V$="CAR" THEN V$="GET":RETURN
3014 IF V$="LEA" THEN V$="DRO":RETURN
3016 IF V$="PRE" THEN V$="PUS":RETURN
3018 IF V$="EXA" THEN V$="LOO":RETURN
3020 IF V$="PUT" THEN V$="INS":RETURN
3022 IF V$="UNL" THEN V$="OPE":RETURN
3024 IF V$="PLA" THEN V$="STA":RETURN
3026 IF V$="SMA" THEN V$="BRE":RETURN
3028 IF V$="ATT" THEN V$="CON":RETURN
3030 IF V$="LIS" THEN V$="INV":RETURN
3032 RETURN
4000 DATA A VIDEO CASSETTE RECORDER,REC,2,A VIDEO TAPE,TAP,0,A LARGE BATTERY,BAT,0,A BLANK CREDIT CARD,CAR,0
4001 DATA AN ELECTRONIC LOCK,LOC,0,AN ELABORATE PAPER WEIGHT,WEI,5,A LOCKED WOODEN DOOR,DOO,4,AN OPEN WOODEN DOOR
4002 DATA DOO,0,A SOLID LOOKING DOOR,DOO,10
4010 DATA AN OPEN DOOR,DOO,0,AN ALERT SECURITY GUARD,GUA,10,A SLEEPING SECURITY GUARD,GUA,0,A LOCKED MAINTENANCE CLOSET
4011 DATA CLO,14,A MAINTENANCE CLOSET,CLO,0,A PLASTIC BAG,BAG,13,AN OLDE FASHIONED KEY,KEY,9
4012 DATA A SMALL METAL SQUARE ON THE WALL,SQU,16
4015 DATA A LEVER ON THE SQUARE,LEV,16,AN OLD MAHOGANY DESK,DES,5,A BROOM,BRO,13,A DUSTPAN,DUS,13,A SPIRAL NOTEBOOK
4016 DATA NOT,0,A MAHOGANY DRAWER,DRA,0,A GLASS CASE ON A PEDESTAL,CAS,6,A RAZOR BLADE,BLA,27,A VERY LARGE RUBY,RUB,0
4017 DATA A SIGN ON THE SQUARE
4020 DATA SIG,16,A QUARTER,QUA,0,A COFFEE MACHINE,MAC,8,A CUP OF STEAMING HOT COFFEE,CUP,0,A SMALL CAPSULE,CAP,0
4021 DATA A LARGE SCULPTURE,SCU,3,A TALL OFFICE BUILDING,BUI,1,A PAIR OF SLIDING DOORS,DOO,3,A LARGE BUTTON ON THE WALL
4025 DATA BUT,29,A PANEL OF BUTTONS NUMBERED ONE THROUGH THREE,PAN,9,A STRONG NYLON ROPE,ROP,17
4026 DATA A LARGE HOOK WITH A ROPE HANGING FROM IT,HOO,21,A C.I.A. IDENTIFICATION BADGE,BAD,-1,A PORTABLE TELEVISION,TEL,7
4027 DATA A BANK OF MONITORS,MON,7,A CHAOS I.D. CARD
4030 DATA CAR,30,A BANK OF MONITORS,MON,19,A SMALL PAINTING,PAI,23,A PAIR OF RUBBER GLOVES,GLO,13
4031 DATA A BOX WITH A BUTTON ON IT,BOX,24,ONE,ONE,9,TWO,TWO,9,THREE,THR,9,SLIT,SLI,10,ON A BUSY STREET,0,0,0,0
4032 DATA IN A VISITOR'S ROOM,0,0,3,0
4033 DATA IN THE LOBBY OF THE BUILDING,1,0,4,2,IN A DINGY ANTE ROOM,0,0,0,3
4035 DATA IN THE COMPANY PRESIDENT'S OFFICE,0,0,0,4,IN A SMALL SOUND PROOFED CUBICLE,0,12,0,0,IN A SECURITY OFFICE,0,0,8,0
4036 DATA IN A SMALL HALLWAY,0,9,14,7,IN A SMALL ROOM,3,0,0,0,IN A SHORT CORRIDOR,15,9,0,0,IN A HALLWAY MADE OF METAL,0,0,12,10
4037 DATA IN A SMALL PLAIN ROOM,6,0,0,11
4040 DATA IN A MAINTENANCE CLOSET,0,14,0,0,IN A CAFETERIA,0,0,0,8,IN A SIDE CORRIDOR,0,10,0,16,IN A POWER GENERATOR ROOM
4041 DATA 0,0,15,0,IN A SUB-BASEMENT BELOW THE CHUTE,0,0,18,0,IN THE ENTRANCE TO THE SECRET COMPLEX,0,20,19,17
4045 DATA IN A SECRET MONITORING ROOM,0,0,0,18,ON A LEDGE IN FRONT OF A METAL PIT 1000'S OF FEET DEEP,18,0,0,0
4046 DATA ON THE OTHER SIDE OF THE PIT,0,0,22,0,IN A LONG CORRIDOR,0,25,23,21,IN A LARGE ROOM,0,26,0,22
4047 DATA IN A SECRET LABORATORY,0,0,25,0
4050 DATA IN A NARROW CROSS CORRIDOR,22,0,0,24,IN A CROSS EXAMINATION ROOM,23,28,0,0,IN A SMALL BATHROOM,0,0,28,0
4051 DATA IN THE OFFICE OF THE CHIEF OF CHAOS,26,30,0,27,IN THE CHAOS CONTROL ROOM,0,0,30,0
4052 DATA NEAR THE END OF THE COMPLEX,28,0,0,29
4055 DATA "GO ",GET,DRO,PUS,PUL,LOO,INS,OPE,WEA,REA,STA,BRE,CUT,THR,CON,QUI,BON,INV
5000 PRINT:M$=" YOUR MISSION, "+NA$+", IS TO RECOVER A RUBY THAT IS BEING "
5010 M$=M$+"USED IN TOP SECRET GOVERNMENT PROJECTS AS A PART IN A "
5020 M$=M$+"LASER PROJECTOR.":GOSUB1
5030 M$=" YOU WILL HAVE A PARTNER WHO IS NOT TOO BRIGHT AND NEEDS "
5040 M$=M$+"YOU TO TELL HIM WHAT TO DO. USE TWO WORD COMMANDS LIKE: "
5050 M$=M$+"GET NOTEBOOK GO WEST LOOK DOOR":GOSUB1:GOSUB2198
5060 M$="SOME COMMANDS USE ONLY ONE WORD. EXAMPLE: INVENTORY":GOSUB1
5070 M$=" IF YOU WANT TO SEE CHANGES IN YOUR SURROUNDINGS TYPE: LOOK "
5080 M$=M$+"THE RUBY HAS BEEN CAPTURED BY A SECRET SPY RING KNOWN AS "
5090 M$=M$+"CHAOS. WE SUSPECT THEY ARE UNDER COVER SOMEWHERE IN THIS "
5100 M$=M$+"NEIGHBORHOOD. GOOD LUCK!":GOSUB1
5110 GOTO56
9000 CLS:PRINT
9005 PRINTTAB(9)"èâÉçèèÇáèèâÉçè"
9010 PRINTTAB(9)"èÖèèèèäèèèÑåÖè"
9020 PRINTTAB(9)"èÖèçèèäèèèÖèÖè"
9030 PRINTTAB(9)"èãÉèãèÉáãèáèáã"
9040 PRINTTAB(2)" âÉçÅÜäèääÉÉäçäãÇÉäèääÉÜäÉÉ"
9050 PRINTTAB(2)" ÑåÖÖääçàäåèäääèäèäèääåâäå"
9060 PRINTTAB(2)" ÖèÖÖäèÑÖäèèäèÇèäèäèääãçä"
9070 PRINTTAB(2)" áèáÉáèãèãÉÉãèãèãèèÉáãèããÉÉ":PRINT:RETURN
10000 GOSUB15:M$="I'M DEAD!":GOSUB1
10010 M$="YOU DIDN'T WIN.":GOSUB1
10020 INPUT"TRY AGAIN (Y/N)";Z$
10030 IF LEFT$(Z$,1)="Y" THEN RUN
22222 END
22223 REM MC-10 EDITS-JIM GERRIE
