1 DIM N1$(50), N$(50), N(50), V$(18), LO(30, 4), LO$(30): CLS : GOTO 4
2 Z9 = 0: FOR R = 1 TO 50: IF V1$ = N$(R) AND (N(R) = LO OR N(R) = -1) THEN Z9 = 1: RETURN
3 NEXT: PRINT "I DON'T SEE THAT HERE.": Z9 = 2: RETURN
4 PRINT CHR$(23); "        C.I.A  ADVENTURE": PRINT "      BY   HUGH   LAMPERT"
5 FL = 1: DR = 0: DG = -1: T = 0: EL = 0: BU = 0: DX = 0: CA = 0: BA = 0: TE = 0: GL = 0
6 PA = 0: RO = 0: FOR R = 1 TO 5: R1 = RND(9): CO$ = CO$ + STR$(R1): NEXT: LO = 1: A9$ = ">" + STRING$(62, "-") + "<"
7 R = LEN(CO$): CO$ = RIGHT$(CO$, R - 1)
10 FOR R = 1 TO 50: READ N1$(R), N$(R), N(R): NEXT
11 FOR R = 1 TO 30: READ LO$(R), LO(R, 1), LO(R, 2), LO(R, 3), LO(R, 4): NEXT
12 FOR R = 1 TO 18: READ V$(R): NEXT
20 CLS
25 PRINT "WE ARE "; LO$(LO); "."
30 FOR R = 1 TO 46: IF N(R) = LO THEN PRINT "I CAN SEE "; N1$(R); "."
35 NEXT: B = 0: FOR R = 1 TO 4: IF LO(LO, R) > 0 THEN B = 1
40 NEXT: IF B = 0 THEN 45
41 PRINT "WE COULD EASILY GO: "; : IF LO(LO, 1) > 0 THEN PRINT "NORTH  ";
42 IF LO(LO, 2) > 0 THEN PRINT "SOUTH  ";
43 IF LO(LO, 3) > 0 THEN PRINT "EAST  ";
44 IF LO(LO, 4) > 0 THEN PRINT "WEST  ";
45 PRINT : PRINT A9$
50 T = T + 1: IF DG > 0 THEN DG = DG - 1
53 IF T = 1 THEN INPUT "ENTER YOUR NAME PARTNER"; NA$
55 GOTO 2000
56 N = 0: FOR R = 1 TO 46: IF N(R) = -1 THEN N = N + 1
57 NEXT
60 PRINT : INPUT "WHAT DO YOU THINK WE SHOULD DO"; A$
65 GOSUB 3000: V$ = LEFT$(A$, 3): GOSUB 3010: FOR R = 1 TO 18: IF V$ = V$(R) THEN GOTO 69: NEXT R
66 IF V$ = "ORD" THEN 5000
67 PRINT "I DON'T KNOW HOW TO DO THAT.": GOTO60
69 IF V$ = "QUI" THEN 850
70 V = R: FOR R = 1 TO LEN(A$): IF MID$(A$, R, 1) = " " THEN 75 ELSE NEXT
71 IF V$ = "LOO" THEN 20
72 IF V$ = "BON" THEN 900
73 IF V$ = "INV" THEN 950
74 PRINT "PLEASE USE 2 WORD COMMANDS SO I CAN UNDERSTAND YOU.": GOTO60
75 V1$ = MID$(A$, R + 1, 3): FOR R = 1 TO 49: IF V1$ = N$(R) THEN 80 ELSE NEXT
76 IF V1$ = "NOR" OR V1$ = "SOU" OR V1$ = "EAS" OR V1$ = "WES" THEN 80
77 PRINT "I DON'T KNOW WHAT IT IS YOU ARE TALKING ABOUT.": GOTO 60
80 ON V GOTO 100, 150, 200, 250, 300, 350, 400, 450, 500, 550, 600, 650, 700, 750, 800, 850, 900, 950
100 IF V1$ = "NOR" AND LO(LO, 1) > 0 THEN LO = LO(LO, 1): GOTO 20
101 IF V1$ = "SOU" AND LO(LO, 2) > 0 THEN LO = LO(LO, 2): GOTO 20
102 IF V1$ = "EAS" AND LO(LO, 3) > 0 THEN LO = LO(LO, 3): GOTO 20
103 IF V1$ = "WES" AND LO(LO, 4) > 0 THEN LO = LO(LO, 4): GOTO 20
104 IF V1$ = "NOR" OR V1$ = "SOU" OR V1$ = "EAS" OR V1$ = "WES" THEN 135
105 GOSUB 2: IF Z9 = 2 THEN 50
110 IF R = 8 THEN LO = 5: GOTO 20
112 IF R = 37 AND RO = 1 AND LO = 20 THEN LO = 21: GOTO 20
115 IF R = 10 THEN LO = 11: GOTO 20
120 IF R = 14 THEN LO = 13: GOTO 20
125 IF R = 33 THEN LO = 3: GOTO 20
130 IF R = 34 AND DX = 1 THEN LO = 9: GOTO 20
135 PRINT "I CAN'T GO THAT WAY AT THE MOMENT.": GOTO 50
150 GOSUB 2: IF Z9 = 2 THEN 50
155 IF R = 2 OR R = 3 OR R = 4 OR R = 6 OR R = 15 OR R = 16 OR R = 20 OR R = 21 OR R = 22 OR R = 23 OR R = 25 OR R = 26 OR R = 27 OR R = 28 OR R = 30 OR R = 31 OR R = 37 OR R = 39 OR R = 40 OR R = 42 OR R = 44 OR R = 45 OR R = 46 THEN 160
157 PRINT "I CAN'T CARRY THAT!": GOTO 50
160 IF N(R) = -1 THEN PRINT "I ALREADY HAVE IT.": GOTO 50
163 IF N >= 5 THEN PRINT "I CAN'T CARRY ANYMORE.": GOTO50
165 PRINT "O.K.": N(R) = -1
170 IF V1$ = "PAI" AND PA = 0 THEN PRINT "SOMETHING FELL FROM THE FRAME!": N(31) = LO: PA = 1
173 IF V1$ = "TEL" THEN TE = 0
175 GOTO 50
200 FOR R = 1 TO 46: IF V1$ = N$(R) AND N(R) = -1 THEN 205 ELSE NEXT
201 PRINT "I DON'T SEEM TO BE CARRYING IT.": GOTO 50
205 IF R = 30 THEN PRINT "I DROPPED THE CUP BUT IT BROKE INTO SMALL PEICES.": PRINT "THE COFFEE SOAKED INTO THE GROUND.": N(30) = 0: DR = 0: GOTO 50
207 IF R = 45 THEN GL = 0
208 IF R = 31 AND N(30) = -1 THEN PRINT "O.K. I DROPPED IT.": PRINT "BUT IT FELL IN THE COFFEE!": N(31) = 0: DR = 1: GOTO 50
210 PRINT "O.K. I DROPPED IT.": N(R) = LO: GOTO 50
250 IF V1$ = "BUT" AND LO = 3 AND DX = 0 THEN PRINT "THE DOORS OPEN WITH A WHOOSH!": DX = 1: GOTO 50
251 IF N(46) = -1 AND V1$ = "BUT" THEN PRINT "I PUSH THE BUTTON ON THE BOX AND": GOTO 268
252 GOSUB 2
255 IF Z9 = 2 THEN 50
256 IF R = 17 AND GL <> 1 THEN PRINT "THERE'S ELECTRICITY COURSING THRU THE SQUARE!": PRINT "I'M BEING ELECTROCUTED!": GOTO 10000
260 IF R = 35 AND BU = 0 THEN PRINT "THE BUTTON ON THE WALL GOES IN .....": PRINT "CLICK! SOMETHING SEEMS DIFFFERENT NOW.": BU = 1: GOTO 50
265 IF R = 47 AND FL <> 1 THEN LO(9, 1) = 3: FL = 1: GOTO 280
266 IF R = 48 AND FL <> 2 THEN LO(9, 1) = 8: FL = 2: GOTO 280
267 IF R = 49 AND FL <> 3 THEN LO(9, 1) = 10: FL = 3: GOTO 280
268 IF N(46) = -1 AND (LO = 6 OR LO = 29) THEN PRINT "THERE IS A BLINDING FLASH....": FOR R = 1 TO 3000: NEXT: LO = 1: FL = 1: LO(9, 1) = 3: GOTO 20
270 PRINT "NOTHING HAPPENS.": GOTO 50
280 PRINT "THE DOORS CLOSE AND I FEEL AS IF THE ROOM IS MOVING.": PRINT "SUDDENLY THE DOORS OPEN AGAIN.": FOR R = 1 TO 3000: NEXT: GOTO 20
300 GOSUB 2: IF Z9 = 2 THEN 50
305 IF R = 18 AND GL <> 1 THEN PRINT "THE LEVER HAS ELECTRICITY COURSING THRU IT!": PRINT "I'M BEING ELECTROCUTED!": GOTO 10000
310 IF R = 18 AND EL = 0 THEN PRINT "THE LEVER GOES ALL THE WAY UP AND CLICKS.": PRINT "SOMETHING SEEMS DIFFERENT NOW.": EL = 1: GOTO 50
315 PRINT "NOTHING HAPPENS.": GOTO 50
350 REM ---- LOOKING AT OBJECTS ----
352 IF V1$ = "DRA" AND LO = 5 THEN PRINT "IT LOOKS FRAGILE.": GOTO50
353 GOSUB2: IF Z9 = 2 THEN 50
355 IF R = 1 AND BA <> 1 THEN PRINT "THERE'S NO POWER FOR IT.": GOTO 50
356 IF R = 1 AND TE <> 1 THEN PRINT "THERE'S NO T.V. TO WATCH ON.": GOTO 50
360 IF R = 6 THEN PRINT "IT LOOKS HEAVY.": GOTO 50
362 IF R = 7 THEN PRINT "IT'S LOCKED.": GOTO 50
364 IF R = 13 THEN PRINT "IT'S A VERY STRONG BAG.": GOTO 50
366 IF R = 19 THEN PRINT "I CAN SEE A LOCKED DRAWER IN IT.": GOTO 50
368 IF R = 22 OR R = 27 THEN PRINT "THERE'S WRITING ON IT.": GOTO 50
369 IF R = 34 AND DX = 1 THEN PRINT "THE DOORS ARE OPEN.": GOTO 50
370 IF R = 24 THEN PRINT "I CAN SEE A GLEAMING STONE IN IT.": GOTO 50
371 IF R = 9 THEN PRINT "THERE IS A SMALL SLIT NEAR THE DOOR.": GOTO 50
372 IF R = 34 THEN PRINT "THERE'S A BUTTON NEAR THE DOORS.": GOTO 50
373 IF R = 41 AND BU = 1 THEN PRINT "THE SCREEN IS DARK.": GOTO50
374 IF R = 41 THEN PRINT "I SEE A METAL PIT 1000'S OF FEET DEEP ON ONE MONITOR.": PRINT "ON THE OTHER SIDE OF THE PIT,I SEE A LARGE HOOK.": GOTO 50
375 IF R = 43 AND BU = 1 THEN PRINT "THE SCREEN IS DARK.": GOTO50
376 IF R = 43 THEN PRINT "I SEE A ROOM WITH A CASE ON A PEDESTAL IN IT.": GOTO50
378 IF R = 44 THEN PRINT "I SEE A PICTURE OF A GRINNING JACKAL.": GOTO 50
380 PRINT "I SEE NOTHING OF INTEREST.": GOTO 50
400 GOSUB 2: IF Z9 = 2 THEN 50
405 IF R <> 3 AND R <> 2 AND R <> 4 AND R <> 28 THEN PRINT "I CAN'T INSERT THAT!": GOTO 50
410 INPUT "TELL ME,IN ONE WORD,INTO WHAT"; A3$: A3$ = LEFT$(A3$, 3)
415 R1 = R: V1$ = A3$: GOSUB2: IF Z9 = 2 THEN 50
420 IF R1 = 3 AND R = 1 THEN PRINT "O.K.": BA = 1: N(3) = 0: BA = 1: GOTO50
423 IF R1 = 4 AND R = 50 AND DG <= 0 THEN PRINT "THE GUARD WON'T LET ME!": GOTO50
425 IF R1 = 2 AND R = 1 THEN PRINT "O.K. THE TAPE IS IN THE RECORDER.": N(2) = 0: TA = 1: GOTO 50
430 IF R1 = 4 AND R = 50 THEN PRINT "POP! A SECTION OF THE WALL OPENS.....": PRINT "REVEALING SOMETHING VERY INTERESTING.": N(4) = 0: N(5) = LO: GOTO50
435 IF R1 = 28 AND R = 29 THEN PRINT "POP! A CUP OF COFFEE COMES OUT OF THE MACHINE.": N(28) = 0: N(30) = LO: GOTO 50
440 PRINT "NOTHING HAPPENED.": GOTO 50
450 REM ---- OPENING OBJECTS ----
452 IF V1$ = "DRA" AND LO = 5 AND N(23) = 0 THEN PRINT "IT's STUCK.": GOTO50
454 GOSUB 2: IF Z9 = 2 THEN 50
455 IF R = 7 OR R = 9 OR R = 13 OR R = 15 OR R = 23 OR R = 32 OR R = 5 THEN 460
456 PRINT "I CAN'T OPEN THAT!": GOTO 50
460 IF R = 7 AND N(16) = -1 THEN PRINT "O.K. I OPENED THE DOOR.": N(7) = 0: N(8) = 4: GOTO 50
465 IF R = 9 THEN PRINT "I CAN'T. IT DOESN'T WORK.": GOTO 50
470 IF R = 13 AND N(16) = -1 THEN PRINT "O.K. THE CLOSET IS OPENED.": N(13) = 0: N(14) = 14: GOTO 50
475 IF R = 15 THEN PRINT "I CAN'T. IT'S TOO STRONG.": GOTO 50
480 IF R = 23 THEN PRINT "IT'S STUCK.": GOTO 50
482 IF R = 32 AND N(28) = 0 AND N(4) = 0 AND SC = 1 THEN PRINT "I OPEN THE SCULPTURE.": PRINT "SOMETHING FALLS OUT.": N(28) = LO: N(4) = LO: GOTO 50
485 IF R <> 5 THEN 495
490 INPUT "WHAT'S THE COMBINATION"; C$
491 IF C$ = CO$ THEN PRINT "THE DOOR IS SLOWLY OPENING.": N(5) = 0: N(9) = 0: N(10) = 10: GOTO 50
493 PRINT "YOU MUST HAVE THE WRONG COMBINATION OR YOU ARE NOT": PRINT "SAYING IT RIGHT.": GOTO 50
495 PRINT "I CAN'T DO THAT......YET!": GOTO 50
500 IF V1$ = "GLO" AND N(45) = -1 THEN PRINT "O.K. I'M NOW WEARING THE GLOVES.": GL = 1: GOTO 50
505 PRINT "I CAN'T WEAR THAT!": GOTO 50
550 IF V1$ <> "SIG" AND V1$ <> "NOT" THEN PRINT "I CAN'T READ THAT.": GOTO50
555 GOSUB 2: IF Z9 = 2 THEN 50
560 IF R = 22 THEN PRINT "IT SAYS:": PRINT NA$; ",": PRINT "  WE HAVE DISCOVERED ONE OF CHAOSES SECRET WORDS.": PRINT "IT IS: BOND-007- .TO BE USED IN A -TASTEFUL- SITUATION.": GOTO 50
565 IF R = 27 THEN PRINT "IT SAYS: WATCH OUT! DANGEROUS!": GOTO 50
600 IF V1$ <> "REC" THEN PRINT "I CAN'T START THAT.": GOTO 50
605 GOSUB 2: IF Z9 = 2 THEN 50
610 IF BA <> 1 OR TE <> 1 OR TA <> 1 THEN 620
612 PRINT "THE RECORDER STARTS UP AND PRESENTS A SHORT MESSAGE:"
613 PRINT NA$; ","
614 PRINT "WE HAVE UNCOVERED A NUMBER THAT MAY HELP YOU."
615 PRINT "THAT NUMBER IS:"; CO$; ". PLEASE WATCH OUT FOR HIDDEN TRAPS."
616 PRINT "ALSO, THERE IS SOMETHING IN THE SCULPTURE.": SC = 1: GOTO50
620 PRINT "NOTHING HAPPENED.": GOTO50
650 IF V1$ <> "DRA" THEN PRINT "I'M TRYING TO BREAK IT, BUT I CAN'T.": GOTO 50
653 IF N(6) <> -1 THEN PRINT "I CAN'T DO THAT YET.": GOTO50
655 IF LO = 5 THEN PRINT "IT'S HARD....BUT I GOT IT. TWO THINGS FELL OUT.": N(3) = LO: N(22) = LO: N(23) = LO: GOTO 50
660 PRINT "NOTHING HAPPENS.": GOTO 50
700 GOSUB 2: IF Z9 = 2 THEN 50
705 IF R <> 15 AND R <> 24 THEN PRINT "I'M TRYING. IT DOESN'T WORK.": GOTO50
710 IF N(25) <> -1 THEN PRINT "I CAN'T DO THAT YET.": GOTO 50
715 IF R = 15 THEN PRINT "RIP! THE BAG GOES TO PIECES, AND SOMETHING FALLS OUT!": N(15) = 0: N(2) = LO: GOTO 50
720 IF R = 24 THEN PRINT "I CUT THE CASE AND REACH IN TO PULL SOMETHING OUT.": N(26) = -1: GOTO 50
750 IF V1$ <> "ROP" THEN PRINT "I CAN'T THROW THAT.": GOTO 50
752 IF N(37) <> -1 THEN PRINT "I CAN'T DO THAT YET.": GOTO 50
755 INPUT "TELL ME,IN ONE WORD,AT WHAT"; A3$: A3$ = LEFT$(A3$, 3)
760 IF A3$ <> "HOO" THEN PRINT "O.K. I THREW IT.": N(37) = LO: GOTO50
765 IF LO <> 20 THEN PRINT "I CAN'T DO THAT YET.": GOTO 50
770 PRINT "I THREW THE ROPE AND IT SNAGGED ON THE HOOK.": RO = 1: N(37) = LO: GOTO 50
800 IF V1$ <> "TEL" THEN PRINT "I CAN'T CONNECT THAT.": GOTO50
803 IF N(40) <> LO THEN PRINT "I DON'T SEE THE TELEVISION HERE.": GOTO50
805 IF TE = 1 THEN PRINT "I DID THAT ALREADY.": GOTO 50
810 IF LO <> 2 THEN PRINT "I CAN'T DO THAT....YET!": GOTO50
815 PRINT "O.K. THE T.V. IS CONNECTED.": TE = 1: GOTO50
850 PRINT "WHAT? YOU WOULD LEAVE ME HERE TO DIE ALONE?"
855 PRINT "JUST FOR THAT, I'M GOING TO DESTROY THE GAME.": PRINT : PRINT : PRINT : PRINT "BOOOOOOOOOOOOM!": FOR R = 1 TO 5000: NEXT: STOP
900 IF LO = 14 THEN PRINT "WHOOPS! A TRAP DOOR OPENED UNDERNEATH ME AND": PRINT "I FIND MYSELF FALLING.": FOR R = 1 TO 4000: NEXT: LO = 17: GOTO 20
905 PRINT "NOTHING HAPPENED.": GOTO 50
950 Z8 = 0: PRINT "WE ARE PRESENTLY CARRYING:": FOR R = 1 TO 46
955 IF N(R) <> -1 THEN 965
960 PRINT N1$(R); : IF R = 45 AND GL = 1 THEN PRINT ". WHICH I'M WEARING." ELSE PRINT " "
963 Z8 = 1
965 NEXT
970 IF Z8 = 0 THEN PRINT "NOTHING"
975 GOTO 50
2000 IF LO = 3 AND N(39) = -1 THEN PRINT "THE DOOR MAN LOOKS AT ME AND THEN THROWS ME OUT.": FOR R = 1 TO 4000: NEXT: LO = 1: GOTO20
2005 IF LO = 10 AND N(42) <> -1 THEN PRINT "THE GUARD LOOKS AT ME SUSPICIOUSLY, THEN THROWS ME BACK.": FOR R = 1 TO 4000: NEXT: LO = 9: GOTO20
2010 IF LO = 6 AND BU <> 1 THEN PRINT "SIRENS GO OFF ALL AROUND ME!": PRINT "GUARDS RUN IN AND SHOOT ME TO DEATH!": GOTO 10000
2015 IF LO = 11 AND EL <> 1 THEN PRINT "THE FLOOR IS WIRED WITH ELECTRICITY!": PRINT "I'M BEING ELECTROCUTED!": GOTO10000
2020 IF LO = 10 AND N(30) = -1 AND DR = 1 THEN PRINT "THE GUARD TAKES MY COFFEE": PRINT "AND FALLS TO SLEEP RIGHT AWAY.": DG = 5 + RND(10): N(11) = 0: N(12) = 10: DR = 0: N(30) = 0: GOTO56
2025 IF DG = 0 THEN PRINT "I HEAR A NOISE LIKE SOMEONE IS YAWNING.": N(11) = 10: N(12) = 0: GU = -2: DG = -1: GOTO56
2030 IF LO = 1 AND N(26) = -1 THEN PRINT "HURRAY! YOU'VE RECOVERED THE RUBY!": PRINT "YOU WIN!": GOTO10020
2035 IF T > 375 THEN PRINT "I THINK THEY ARE ON TO ME....I HEAR NOISES."
2040 IF T = 400 THEN PRINT "OH NO! THEY CAUGHT UP TO ME! HELP! THEY'RE PULLING OUT GUNS!": GOTO10000
2045 IF T = 1 THEN PRINT "WRITING ON THE WALL SAYS:": PRINT "IF YOU WANT INSTRUCTIONS TYPE:ORDERS PLEASE"
2046 IF LO = 6 AND LO(6, 2) <> 0 THEN PRINT "A SECRET DOOR SLAMS DOWN BEHIND ME!": LO(6, 2) = 0: GOTO56
2047 IF LO = 10 AND GU = -2 THEN PRINT "THE GUARD DRAWS HIS GUN AND SHOOTS ME!": GOTO 10000
2050 GOTO 56
3000 IF A$ = "N" THEN A$ = "GO NORTH": RETURN
3002 IF A$ = "S" THEN A$ = "GO SOUTH": RETURN
3004 IF A$ = "E" THEN A$ = "GO EAST": RETURN
3006 IF A$ = "W" THEN A$ = "GO WEST": RETURN
3007 IF A$ = "I" THEN A$ = "INV": RETURN
3008 RETURN
3010 IF V$ = "WAL" OR V$ = "RUN" THEN V$ = "GO ": RETURN
3012 IF V$ = "TAK" OR V$ = "CAR" THEN V$ = "GET": RETURN
3014 IF V$ = "LEA" THEN V$ = "DRO": RETURN
3016 IF V$ = "PRE" THEN V$ = "PUS": RETURN
3018 IF V$ = "EXA" THEN V$ = "LOO": RETURN
3020 IF V$ = "PUT" THEN V$ = "INS": RETURN
3022 IF V$ = "UNL" THEN V$ = "OPE": RETURN
3024 IF V$ = "PLA" THEN V$ = "STA": RETURN
3026 IF V$ = "SMA" THEN V$ = "BRE": RETURN
3028 IF V$ = "ATT" THEN V$ = "CON": RETURN
3030 IF V$ = "LIS" THEN V$ = "INV": RETURN
3032 RETURN
4000 DATA A VIDEO CASSETTE RECORDER,REC,2,A VIDEO TAPE,TAP,0,A LARGE BATTERY,BAT,0,A BLANK CREDIT CARD,CAR,0,AN ELECTRONIC LOCK,LOC,0,AN ELABORATE PAPER WEIGHT,WEI,5,A LOCKED WOODEN DOOR,DOO,4,AN OPEN WOODEN DOOR,DOO,0,A SOLID LOOKING DOOR,DOO,10
4010 DATA AN OPEN DOOR,DOO,0,AN ALERT SECURITY GUARD,GUA,10,A SLEEPING SECURITY GUARD,GUA,0,A LOCKED MAINTENANCE CLOSET,CLO,14,A MAINTENANCE CLOSET,CLO,0,A PLASTIC BAG,BAG,13,AN OLDE FASHIONED KEY,KEY,9,A SMALL METAL SQUARE ON THE WALL,SQU,16
4015 DATA A LEVER ON THE SQUARE,LEV,16,AN OLD MAHOGANY DESK,DES,5,A BROOM,BRO,13,A DUSTPAN,DUS,13,A SPIRAL NOTEBOOK,NOT,0,A MAHOGANY DRAWER,DRA,0,A GLASS CASE ON A PEDESTAL,CAS,6,A RAZOR BLADE,BLA,27,A VERY LARGE RUBY,RUB,0,A SIGN ON THE SQUARE
4020 DATA SIG,16,A QUARTER,QUA,0,A COFFEE MACHINE,MAC,8,A CUP OF STEAMING HOT COFFEE,CUP,0,A SMALL CAPSULE,CAP,0,A LARGE SCULPTURE,SCU,3,A TALL OFFICE BUILDING,BUI,1,A PAIR OF SLIDING DOORS,DOO,3,A LARGE BUTTON ON THE WALL
4025 DATA BUT,29,A PANEL OF BUTTONS NUMBERED ONE THRU THREE,PAN,9,A STRONG NYLON ROPE,ROP,17,A LARGE HOOK WITH A ROPE HANGING FROM IT,HOO,21,A C.I.A. IDENTIFICATION BADGE,BAD,-1,A PORTABLE TELEVISION,TEL,7
4026 DATA A BANK OF MONITORS,MON,7,A CHAOS I.D. CARD
4030 DATA CAR,30,A BANK OF MONITORS,MON,19,A SMALL PAINTING,PAI,23,A PAIR OF RUBBER GLOVES,GLO,13,A BOX WITH A BUTTON ON IT,BOX,24,ONE,ONE,9,TWO,TWO,9,THREE,THR,9,SLIT,SLI,10,ON A BUSY STREET,0,0,0,0,IN A VISITOR'S ROOM,0,0,3,0
4031 DATA IN THE LOBBY OF THE BUILDING,1,0,4,2,IN A DINGY ANTE ROOM,0,0,0,3
4035 DATA IN THE COMPANY PRESIDENT'S OFFICE,0,0,0,4,IN A SMALL SOUND PROOFED CUBICLE,0,12,0,0,IN A SECURITY OFFICE,0,0,8,0,IN A SMALL HALLWAY,0,14,9,7,IN A SMALL ROOM,3,0,0,0,IN A SHORT CORRIDOR,0,15,0,9,IN A HALLWAY MADE OF METAL,0,0,12,10
4037 DATA IN A SMALL PLAIN ROOM,6,0,0,11
4040 DATA IN A MAINTENANCE CLOSET,0,0,14,0,IN A CAFETERIA,8,0,0,0,IN A SIDE CORRIDOR,10,0,16,0,IN A POWER GENERATOR ROOM,0,0,0,15,IN A SUB-BASEMENT BELOW THE CHUTE,0,0,18,0,IN THE ENTRANCE TO THE SECRET COMPLEX,0,20,19,17
4045 DATA IN A SECRET MONITORING ROOM,0,0,0,18,ON A LEDGE IN FRONT OF A METAL PIT 1000'S OF FEET DEEP,18,0,0,0,ON THE OTHER SIDE OF THE PIT,0,0,22,0,IN A LONG CORRIDOR,0,25,23,21,IN A LARGE ROOM,0,26,0,22,IN A SECRET LABORATORY,0,0,25,0
4050 DATA IN A NARROW CROSS CORRIDOR,22,0,0,24,IN A CROSS EXAMINATION ROOM,23,28,0,0,IN A SMALL BATHROOM,0,0,28,0,IN THE OFFICE OF THE CHIEF OF CHAOS,26,30,0,27,IN THE CHAOS CONTROL ROOM,0,0,30,0,NEAR THE END OF THE COMPLEX,28,0,0,29
4055 DATA "GO ",GET,DRO,PUS,PUL,LOO,INS,OPE,WEA,REA,STA,BRE,CUT,THR,CON,QUI,BON,INV
5000 CLS : PRINT "YOUR MISSION,"; NA$; ",IS TO RECOVER A RUBY THAT IS BEING"
5010 PRINT "USED IN TOP SECRET GOVERNMENT PROJECTS AS A PART IN A"
5020 PRINT "LASER PROJECTOR."
5030 PRINT "  YOU WILL HAVE A PARTNER WHO IS NOT TOO BRIGHT AND NEEDS"
5040 PRINT "YOU TO TELL HIM WHAT TO DO. USE TWO WORD COMMANDS LIKE:"
5050 PRINT : PRINT "	       GET NOTEBOOK   GO WEST  LOOK DOOR"
5060 PRINT : PRINT "SOME COMMANDS USE ONLY ONE WORD. EXAMPLE: INVENTORY"
5070 PRINT "  IF YOU WANT TO SEE CHANGES IN YOUR SURROUNDINGS TYPE: LOOK"
5080 PRINT "THE RUBY HAS BEEN CAPTURED BY A SECRET SPY RING KNOWN AS"
5090 PRINT "CHAOS. WE SUSPECT THEY ARE UNDER COVER SOMEWHERE IN THIS"
5100 PRINT "NEIGHBORHOOD. GOOD LUCK!"
5110 GOTO 56
10000 FOR R = 1 TO 5000: NEXT: CLS : PRINT "I'M DEAD!"
10010 PRINT "YOU DIDN'T WIN."
10020 INPUT "WOULD YOU LIKE TO TRY AGAIN (Y/N)"; Z$
10030 IF Z$ = "Y" THEN RUN
10040 'COPYRIGHT (C) CLOAD 1980, HUGH LAMPERT, 110 LINDNER PL., MALVERNE, NY 11565

