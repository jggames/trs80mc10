10 CLS:GOTO3400
15 CLS3:PRINT@230,"****PLEASE WAIT****";
20 DIMR$(44),O$(59),O(59),T(4,43),V$(20),V(20),K$(3),L(43),Z$(3),B$(50)
30 DATABATHROOM,TV ROOM,GAME ROOM,OFFICE,CONFERENCE ROOM,LONG ROOM,CLOSET ROOM,BEDROOM,AVIARY,TROPHY ROOM
31 DATAPANELED ROOM,GALLERY,OFFICE,BEDROOM,LARGE ROOM
40 DATALIBRARY,FAMILY ROOM,LITTLE ROOM,COMPUTER ROOM,COLORFUL ROOM,KITCHEN,DINING ROOM,LONG HALL
41 DATALONG NARROW HALL,RECEPTION ROOM,LARGE ROOM,OFFICE,SM DINING ROOM
50 DATASMALL KITCHEN,LAUNDRY ROOM,DEN,SITTING ROOM,SMALL BEDROOM,STATE ROOM,SHORT HALL,BEDROOM,UTILITY ROOM
51 DATAGARAGE,GREEN HOUSE,SMALL ROOM,FRONT ENTRANCE,LIVING ROOM,PORCH
60 DATABATHTUB,1,SINK,1,MEDICINE CABINET,1,TV,2,VCR,2,TABLE,2,GAMES,3,COUCH,3,DESK,4,TABLE,5,BOWL OF FRUIT
61 DATA5,PIECE OF PAPER,6
70 DATA15 NUMBERED CLOSETS,7,BIRDS,9,CABINETS,10,TROPHIES,10,STUFFED ANIMALS,10,PAINTINGS TO N-S-W,12
71 DATACURTAINS TO W-N-E,13,CANOPY BED,14
80 DATADESK,15,ALCOVE,15,BOOKCASES,16,FURNITURE,17,SHELF,17,COLOR COMPUTER,19,COLORED WALLS,20,TABLE,21
81 DATACHANDELIER,22,PAINTING,22
90 DATAPAINTING,25,GLASS CASE,25,DESK,27,TABLE,28,CONTAINER,28,DURANGO DAN,29,2 CANS,29,LARGE BOWL,29,MIRROR
91 DATA31,CHAIR,31,WINDOW,31
100 DATAMONITOR,32,BUNKBEDS,33,NOTEBOOK,34,BED,36,CHEST,36,WASHER,37,WINDOW,37,PLANTS,39,WINDOW,39,CLOSET,40
101 DATAPICTURE,40,WINDOW,40,FRONT DOOR,41,CURTAINS,42,WINDOW,42,SCREEN,43,EMBROIDERED SAMPLER,43
110 DATA,4,,,,5,3,,,6,,2,1,8,-1,,2,,,4,3,1,,,,14,-1,,4,,,7,,,1,,-4,,11,9,,-1,,1,,0
111 DATA13,,,22,,,7,23,,,,24,16,,,25,,15,,-1,18,,11,,19,17,,28,2,18,,,,-1
120 DATA,3,22,,13,,,21,14,-4,,,15,33,,,16,,,,-4,35,27,,,,,-1,19,,29,,,-1,,28,21,,31,,0
121 DATA,32,-1,-1,39,33,-1,24,,,32,,,35,,26,-1,,34,,43,37,,,,38,36,29,,,37
130 DATA32,,-1,,,,,39,,,42,,35,,,41,36,,,0
140 DATATAKE,1,GET,1,EXAMINE,2,LOOK,2,READ,2,OPEN,3,KICK,4,ASK,5,USE,6,UNLOCK,7,TYPE,8,MOVE,9,PUSH,9,HELP,10
141 DATAVERB,11,SAY,12,TOUCH,13,DIAL,14,PLAY,15,INVENTORY,16,GOLD KEY,SILVER KEY,LEAD KEY
150 FORX=1TO43:READR$(X):NEXT
160 FORX=1TO58:READO$(X),O(X):NEXT
170 FORX=1TO43:READT(1,X),T(2,X),T(3,X),T(4,X):NEXT
180 FORX=1TO20:READV$(X),V(X):NEXT
190 FORX=1TO3:READK$(X):NEXT
200 T$(1)="NORTH":T$(2)="SOUTH":T$(3)="EAST":T$(4)="WEST"
210 R=25:GK=0:SK=0:LK=0:SA=0:CO=0:CC=0:C=0:WW=0:WX=0:DI=0:D=0:KK=0:LC=0
220 CLS:PRINT"YOU ARE IN THE ";R$(R)
230 PRINT:PRINT"YOU SEE:";
240 FORX=1TO58:IFO(X)=RTHENPRINTTAB(10)O$(X)
250 NEXT
260 IFR=35THENPRINT"A CARVED DOOR"
270 PRINT:PRINT"EXITS LEAD:";
280 FORX=1TO4:IFT(X,R)<>0ANDT(X,R)<>-2THENPRINTTAB(14)T$(X)
290 NEXT
300 PRINT:INPUT"WHAT WOULD YOU LIKE TO DO NOW ";C$
310 IF LEN(C$)>1THEN370
320 IFC$="N"THENC$="NORTH"
330 IFC$="S"THENC$="SOUTH"
340 IFC$="E"THENC$="EAST"
350 IFC$="W"THENC$="WEST"
360 GOTO450
370 FORX=1TOLEN(C$):IFMID$(C$,X,1)=" "THENA$=LEFT$(C$,X-1):B$=MID$(C$,X+1):GOTO405
380 NEXT
390 IFC$="NORTH"ORC$="SOUTH"ORC$="EAST"ORC$="WEST"THEN450
400 A$=C$
405 IFA$="GO"THENC$=B$:GOTO450
410 FORX=1TO20:IFV$(X)=A$THENA=V(X):GOTO440
420 NEXT
430 PRINT"I DO NOT UNDERSTAND":GOTO300
440 ONAGOTO620,720,1450,1560,1570,1610,1700,1930,2070,2170,2260,2320,2390,3110,3190,2190
450 FORX=1TO4:IFC$=T$(X)THENDR=X:GOTO480
460 NEXT
470 PRINT"NOT A DIRECTION":GOTO300
480 IFT(DR,R)>0ANDT(DR,R)<>100THENR=T(DR,R):GOTO220
490 IFT(DR,R)=0THENPRINT"YOU CAN'T GO THAT WAY":GOTO300
500 IFT(DR,R)=-1THENPRINT"A LOCKED DOOR BLOCKS EXIT"
510 IFR=32ANDDR=4THENPRINT"THERE IS A NOTE ABOVE A         COMBINATION LOCK":GOTO300
520 IFR=32ANDDR=1THENPRINT"THERE IS A SIGN ABOVE IT":GOTO300
530 IF T(DR,R)=0THENPRINT"YOU CAN'T GO THAT WAY":GOTO300
540 IFR=31THENPRINT"THERE IS SOMETHING STRANGE ABOUTTHE DOOR"
550 IFR=11ANDDR=2THENPRINT"THERE IS AN ENGRAVED PLATE ABOVETHE DOOR"
560 IFT(DR,R)<>-2THEN570
570 IFT(DR,R)=-4THENPRINT"YOU NOW FIND THE HANDLE ON THE  DOOR BROKEN AND NO WAY BACK":GOTO300
580 IFT(DR,R)=100THEN590
581 GOTO300
590 CLS:SOUND20,3:PRINT"A TRAP DOOR OPENS, YOU ONCE     AGAIN FIND YOURSELF AT THE START":GOTO3290
620 IFR=16ANDB$="BOOK"THENPRINT"THE BOOKCASE MOVED REVEALING A  DARK PASSAGE EAST":T(3,16)=100:DP=1:GOTO300
640 IFR=22ANDB$=O$(30)THEN590
650 IFR=12ANDB$="PAINTING"THENPRINT"WHICH PAINTING <N> <S> <W> ?";:GOTO2650
660 IFR=28ANDB$=O$(35)ANDCC=0THENPRINT"YOU NOW HAVE THE CONTAINER":CO=1:CC=1:O$(35)="":GOTO300
670 IFWW=1ANDRIGHT$(B$,4)="SIGN"ANDR=36THENPRINT"YOU NOW HAVE THE ";B$:WX=1:GOTO300
680 C1=0:FORZZ=1TOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
681 NEXT:IFR=15ANDC1=1ANDLK<>3THEN590
690 IFR=15ANDRIGHT$(B$,3)="KEY"ANDLK=3THENPRINT"THE KEY IS STUCK IN THE LOCK":GOTO300
700 IFR=39ANDKK=1ANDRIGHT$(B$,3)="KEY"THENPRINT"YOU NOW HAVE THE SILVER KEY":SK=1:GOTO300
710 PRINT"YOU CAN'T ";A$;" THE ";B$:GOTO300
720 IFA$=C$THEN220
740 PRINT"YOU SEE:"
750 IFR=25ANDB$=O$(31)THENPRINT"A GIRL IN A BLUE DRESS SITTING  ON A SWING":GOTO300
760 IFR=40ANDB$=O$(52)THENPRINT"IT'S A PICTURE OF A GIRL IN A   RED DRESS SITTING ON A STOOL":GOTO300
770 IFR=5ANDB$="TABLE"THEN1410
780 IFR=15ANDB$="PENS"THENPRINT"THERE ARE 11 PENS":GOTO300
785 IFR=1ANDB$="CABINET"THENPRINT"AN EMPTY CABINET":GOTO300
790 IFR=15ANDB$="PAPER"THEN792
791 GOTO795
792 PRINT"THE PAPER SAYS:":PRINT"BEHIND THE CHEST IS THE BACK    HIDDEN THERE IS A FACT"
793 PRINT"AN S-CURVED SPINE               WITH TWO NOT ONE LINE           UNLESS PERSEVERANCE YOU LACK":GOTO300
795 IFR=2ANDLEFT$(B$,4)="TAPE"THENPRINT"FOUR RECORDED VIDEO TAPES":GOTO300
800 IFR=15ANDB$="BOOK"THENPRINT"'7TH SON OF A 7TH SON'":GOTO300
810 IFR=14ANDB$=O$(45)THEN1410
820 IFR=25ANDRIGHT$(B$,4)="CASE"THENPRINT"YOU SEE 23 DOLLS":GOTO300
830 IFR=32ANDB$=O$(42)THENPRINT"THE MONITOR HAS A BUTTON":GOTO300
840 IFR=32ANDB$="NOTE"THENPRINT"R-#A'S  L-#E'S  R-#N'S":GOTO300
850 IFR=32ANDB$="SIGN"THENPRINT"SAY UNCLOSED SEED":PRINT"THEN PROCEED":PRINT"IF YOU LACK":PRINT"YOU CAN'T GO BACK":GOTO300
860 IFR=5ANDB$="FRUIT"ORB$="BOWL"ANDR=5THENPRINT"THERE ARE 6 ORANGES IN THE BOWL":GOTO300
870 IFR=40ANDB$=O$(51)THENPRINT"SOME GOWNS AND SHOES":GOTO300
880 IFR=40ANDB$="SHOES"THENPRINT"THERE ARE 8 PAIRS OF SHOES":GOTO300
890 IFR=40ANDB$="GOWNS"THENPRINT"THERE ARE 8 GOWNS":GOTO300
900 IFR=16ANDLEFT$(B$,4)="BOOK"THENPRINT"A BOOK ENTITLED '43 ADVENTURES'":GOTO300
910 IFR=31ANDB$="DOOR"THEN915
911 GOTO920
915 PRINT"THERE IS A PUSH BUTTON PAD THAT LOOKS LIKE A IT CAME OFF A PHONEAND THERE ARE INSTRUCTIONS":GOTO300
920 IFR=31ANDB$="INSTRUCTIONS"THENPRINT"TO OPEN - THE GIRL IN THE BLUE  DRESS WAS SITTING ON A #-#-#-#-#":GOTO300
930 IFR=3ANDB$=O$(7)THENPRINT"YOU SEE SOME CARD GAMES AND A   MONOPOLY BOARD":GOTO300
940 IFR=3ANDLEFT$(B$,5)="MONOP"ORR=3ANDRIGHT$(B$,5)="BOARD"THENPRINT"THREE PROPERTIES HAVE HOTELS    ON THEM":GOTO300
950 IFR=3ANDLEFT$(B$,5)="PROPE"ORR=3ANDLEFT$(B$,4)="HOTE"THEN955
951 GOTO960
955 PRINT"THE PROPERTIES  ARE KENTUCKY,   N. CAROLINA AND   ATLANTIC":GOTO300
960 IFR=16ANDB$="BOOK"THENPRINT"YOU DON'T HAVE THE BOOK":GOTO300
970 IFR=15ANDB$="DESK"ANDLK=3THENPRINT"A LEAD KEY IN THE DESK"
980 IFR=15ANDB$=O$(21)THEN1420
990 IFR=21ANDB$=O$(28)THENPRINT"IN A FINE COATING OF FLOUR YOU  SEE WRITTEN: 'CURSES, CURTAINS!'":GOTO300
1000 IFR=7ANDLEFT$(B$,4)="CLOS"THEN1005
1001 GOTO1010
1005 PRINT"A SIGN READS: TO OPEN THE EAST  DOOR THE CORRECT CLOSET MUST BE OPENED. THE NO. OF THE CLOSET IS=";
1006 PRINT" TO THE SUM OF THE SAFE COMBINATION DIVIDED BY THE NO. OF WORDSIN DGO. DAN'S AUTOGRAPH":GOTO300
1010 IFB$="WINDOW"THENPRINT"YOU SEE AN EAGER CROWD WAITING  AT THE FINISH LINE":GOTO300
1020 IFR=4ANDB$=O$(9)THENPRINT"ON THE DESK IS A PIECE OF PAPER AND AN OLD BLACK PHONE":GOTO300
1030 IFR=4ANDB$="PHONE"THEN1410
1040 IFR=4ANDB$="PAPER"THENPRINT"THE PAPER SAYS: 86 6736 843     3667 3425 1155":GOTO300
1050 IFR=6ANDRIGHT$(B$,5)="PAPER"THEN1055
1051 GOTO1060
1055 PRINT"THE BARELY LEGIBLE PAPER SAYS:  ...IS ... .NLY PLAC. .N .SA     WHER. FOU. STAT.. .EET ....":GOTO300
1060 IFR=9ANDB$=O$(14)THENPRINT"YOU SEE A SPARROW, A PARAKEET   A COCKATOO AND A PARROT":GOTO300
1070 IFR=15ANDB$=O$(22)THEN1075
1071 GOTO1080
1075 GOSUB2860:PRINT"AND BELOW IS WRITTEN :          THE KEY IS THREE!"
1076 PRINT"TINONGTGTATHKHEEELTSEHIAELDGVKOEELRYDKSKEAEYYYSSSAIAYNYKGRI":GOTO300
1080 IFR=31ANDB$=O$(39)THENPRINT"THE MIRROR IS CLOUDY":GOTO300
1090 IFR=12ANDLEFT$(B$,4)="PAIN"THENPRINT"WHICH PAINTING <N> <S> <W> ?";:GOTO2590
1100 IFR=22ANDB$=O$(30)THENPRINT"YOU SEE A PAINTING SLIGHTLY AJAR":GOTO300
1110 IFR=13ANDLEFT$(B$,4)="CURT"THENPRINT"CLOSED CURTAINS":GOTO300
1120 IFR=12ANDSA=1ANDB$="SAFE"THEN1125
1121 GOTO1130
1125 PRINT"YOU FIND A PICTURE OF DGO DAN   WITH THIS WRITTEN ON IT:"
1126 PRINT"VIY MIBLC, ABBY WRALC, TRECES   THIN, TROBHOAM LAWL             LOVE, DURANGO DAN":GOTO300
1130 IFR=12ANDB$="SAFE"ANDSA=0THENPRINT"THE SAFE IS LOCKED":GOTO300
1140 IFR=1ANDB$="WALL"THEN1145
1141 GOTO1150
1145 PRINT"ON THE WALL IS SCRIBBLED        SILVER IS THE KEY     AND FOR A GOOD TIME CALL 303-259-5880":GOTO300
1150 IFR=10ANDB$=O$(16)THENPRINT"YOU SEE 72 TROPHIES":GOTO300
1160 IFR=10ANDLEFT$(B$,4)="ANIM"ORB$=O$(17)ANDR=10THENPRINT"THERE IS A BEAR, A DEER, AN ELK,A LION, AND A TIGER":GOTO300
1170 IFR=11ANDLEFT$(B$,4)="ENGR"ORB$="PLATE"ANDR=11THENPRINT"TO UNLOCK THE DOOR ANSWER THE   ELK'S QUESTION":GOTO300
1180 IFR=19ANDB$="COMPUTER"THENPRINT"THE COCO IS ON AND THE CURSOR ISFLASHING":GOTO300
1190 IFLEFT$(B$,4)="UNDE"THENPRINT"TRY MOVING SOMETHING":GOTO300
1200 IFR=20ANDLEFT$(B$,4)="WALL"THEN1205
1201 GOTO1210
1205 PRINT"THE WALLS ARE COLORED AS FOLLOWSN-GREEN  S-BLUE  E-RED  W-YELLOW":PRINT
1206 PRINT"A VOICE SAYS-TO ESCAPE, TOUCH   ONLY THESE WALLS IN THIS ORDER  ATLANTIC,KENTUCKY,N. CAROLINA":GOTO300
1210 IFR=28ANDB$="TABLE"THEN1410
1220 IFCO=1ANDLEFT$(B$,4)="CONT"ORR=28ANDB$=O$(35)THEN?"THE CONTAINER READS-DO NOT OPEN UNTIL ABSOLUTELY NECESSARY":GOTO300
1230 IFR=29ANDB$="CANS"THENPRINT"THE TWO CANS ARE FILLED WITH    WATER":GOTO300
1240 IFR=36ANDB$="CHEST"THENPRINT"THE CHEST HAS FOUR DRAWERS":GOTO3130
1250 IFR=36ANDLEFT$(B$,4)="DRAW"THEN3130
1260 IFR=43ANDRIGHT$(B$,4)="PLER"THEN1265
1261 GOTO1270
1265 PRINT"THE SAMPLER :    ";"=======":PRINTTAB(17)": K T :":PRINTTAB(17)": C S :"
1266 ?TAB(17)": E E :":?TAB(17)": H H :":?TAB(17)": C C :":?TAB(17)":::::::":PRINTTAB(17)"! ! ! !":GOTO300
1270 IFR=17ANDB$="SHELF"THENPRINT"YOU SEE A KEYBOARD AND A DIARY":GOTO300
1280 IFR=17ANDDI=0ANDB$="DIARY"THEN1285
1281 GOTO1290
1285 PRINT"THE FIRST PAGE OF THE DIARY SAYS"
1286 PRINT"TO OPEN DOOR TYPE THE FIRST     LETTER OF EACH STATE IN         ALPHABETICAL ORDER....":DI=1:GOTO300
1290 IF DI>0ANDR=17ANDB$="DIARY"THEN1295
1291 GOTO1300
1295 PRINT".....THE DOOR IS CARVED WITH    SHAPES OF STATES. YOU ONLY NEED TO TYPE THESE STATES.":GOTO300
1300 IFR=27ANDPP=1ANDB$="PAPER"THENPRINT"TYPE DURANGO AND YOU CAN GO":GOTO300
1310 IFR=27ANDB$="DESK"THENPRINT"YOU SEE A LAMP AND A TYPEWRITER":GOTO300
1320 IFR=35ANDRIGHT$(B$,4)="DOOR"THENPRINT"THE SOUTH DOOR LOOKS LIKE THIS:":PRINT"- --- --- *--* * -* *** *- -*--*--*"
1330 IFR=34ANDB$=O$(44)THENPRINT"THE NOTEBOOK SAYS -":PRINT"REMEMBER --- CURSES, CURTAINS":GOTO300
1340 IFR=17ANDB$="DOOR"THEN2440
1350 IFR=39ANDKK=1ANDB$="DOOR"ANDSK<>1THENPRINT"A SILVER KEY IS IN THE DOOR":GOTO300
1360 IFR=2ANDB$="TABLE"THENPRINT"ON THE TABLE YOU SEE FOUR VIDEO TAPES":GOTO300
1365 IFR=17ANDLEFT$(B$,4)="TYPE"THEN1410
1370 IFR=27ANDB$="LAMP"THENPRINT"THE LAMP IS ON":GOTO300
1375 IFB$="DOOR"THEN1410
1380 FORX=1TO58:IFB$=O$(X)THENM=1:GOTO1400
1390 NEXT
1400 IFO(X)<>RANDM=1THENPRINT"I DO NOT SEE IT HERE":M=0:GOTO300
1410 PRINT"NOTHING UNUSUAL":GOTO300
1420 PRINT"THE ";B$;
1430 IFL(R)=0THENPRINT" IS LOCKED":GOTO300
1431 PRINT" IS OPEN":GOTO1890
1450 IFR=15ANDB$=O$(21)THEN1540
1460 IFR=7ANDLEFT$(B$,6)="CLOSET"THENINPUT"WHICH CLOSET NO. ";CN:GOTO2980
1470 IFR=13ANDLEFT$(B$,4)="CURT"THENPRINT"WHICH CURTAIN <E> <W> <N> ?";:GOSUB2720:GOTO2750
1480 IFR=35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDC=0ANDLC=1THEN1485
1481 GOTO1490
1485 PRINT"YOU FIND A PIECE OF PAPER -":PRINT"A:*-  E:*  L:*-**  T:-  P:*--*  S:***  O:---  N:-*  Y:-*--":GOTO300
1490 IFB$=O$(35)ANDCO=0THENPRINT"YOU DON'T HAVE THE CONTAINER":GOTO300
1495 IFLEFT$(B$,4)="CONT"ANDC=1ANDLC=1THENPRINT"YOU SEE AN EMPTY CONTAINER":GOTO300
1500 IFR<>35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDLC=1THEN1505
1501 GOTO1510
1505 PRINT"YOU SEE A PIECE OF PAPER BUT,   BEFORE YOU CAN READ IT IT BURNS IN A FLASH OF SMOKE":C=1:GOTO300
1510 IFR=36ANDLEFT$(B$,4)="DRAW"THENPRINT"WHICH DRAWER <1> <2> <3> <4> ?":GOTO3050
1520 IFR=42ANDB$=O$(55)THEN590
1530 IFR=1ANDRIGHT$(B$,4)="INET"THENPRINT"IT IS EMPTY":GOTO300
1540 PRINT"THE "B$" IS LOCKED":GOTO300
1550 PRINT"YOU CAN'T OPEN THE "B$:GOTO300
1560 PRINT"HOW WOULD YOU LIKE IT IF A":PRINTB$" WERE TO KICK YOU?":GOTO300
1570 IFR=9ANDB$="COCKATOO"THENPRINT"THE BIRD CACKLES ...FREEZE WATERFREEZE WATER....":GOTO300
1580 IFR=10ANDB$="ELK"THEN1585
1581 GOTO1590
1585 PRINT"THE ELK SAYS - ANSWER THIS AND  REMEMBER, HOW MANY CUBIC FEET OFDIRT IN A HOLE 7' X 14' X 3' ?":GOTO300
1590 IFR=29ANDRIGHT$(B$,3)="DAN"THEN1595
1591 GOTO1600
1595 PRINT"I WILL UNLOCK THE SOUTH DOOR FORYOU IF YOU CAN TELL ME HOW I CANPUT THE CONTENTS OF THE TWO CANS";
1596 PRINT"INTO THE BOWL AND STILL TELL    THEM APART":GOTO3020
1600 PRINT"THE "B$" DOESN'T ANSWER":GOTO300
1610 FORL=1TOLEN(B$)
1611 C1=0:FORZZ=LTOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
1612 NEXT:IFC1>0THEN1620
1613 NEXT
1615 IFLEFT$(B$,3)<>"KEY"THENPRINT"IT DOES NOT HELP TO USE THE     ";B$;" HERE":GOTO300
1620 INPUT"WHICH KEY ";Z$
1630 IFSK=0ANDLEFT$(Z$,4)="SILV"THEN1690
1640 IFLK=0ANDLEFT$(Z$,4)="LEAD"THEN1690
1650 IFGK=0ANDLEFT$(Z$,4)="GOLD"THEN1690
1660 IFLEFT$(Z$,4)="SILV"ANDR=41THEN3300
1665 IFLEFT$(Z$,4)="LEAD"ANDR=15THEN1880
1670 IFCO=1ANDLEFT$(Z$,4)="GOLD"ANDR<>41THENPRINT"THE CONTAINER IS NOW UNLOCKED":LC=1:GOTO300
1680 PRINT"IT DOESN'T WORK HERE":GOTO300
1690 PRINT"YOU DON'T HAVE THE ";Z$:GOTO300
1700 IFR=15ANDB$=O$(21)THEN1810
1710 IFR=39ANDB$="DOOR"THEN1810
1720 IFR=32ANDB$="DOOR"THENINPUT"WHICH DOOR <N> OR <W> ";Q$:GOTO2510
1730 IFR=4ANDB$="DOOR"THENPRINT"HOW?":GOTO300
1740 IFR=31ANDB$="DOOR"THENPRINT"HOW?":GOTO300
1750 IFR=12ANDB$="SAFE"THEN2790
1760 IFR=11ANDB$="DOOR"THEN3000
1770 IFR=41ANDB$="DOOR"THENPRINT"HOW":GOTO300
1780 IFCO<>1ANDB$="CONTAINER"THENPRINT"YOU DON'T HAVE THE ";B$:GOTO300
1790 IFCO=1ANDB$="CONTAINER"THENPRINT"HOW?":GOTO300
1800 PRINT"HOW? ORDINARY METHODS LIKE USINGKEYS WILL NOT WORK HERE":GOTO300
1810 INPUT"WITH WHAT";Z$
1820 IFGK=0ANDZ$=K$(1)THEN1910
1830 IFSK=0ANDZ$=K$(2)THEN1910
1840 IFLK=0ANDZ$=K$(3)THEN1910
1850 IFR=39ANDSK=1ANDZ$=K$(2)THENPRINT"THE DOOR OPENS":T(3,39)=40:SK=0:KK=1:GOTO300
1860 IFR=15ANDLK=1ANDZ$=K$(3)THEN1880
1870 PRINT"IT WON'T WORK HERE":GOTO300
1880 PRINT"THE DESK OPENS REVEALING SOME":L(R)=1:GOTO1900
1890 PRINT"THERE ARE SOME  ";
1900 PRINT"PENS,A PIECE OF PAPER AND A BOOK":LK=3:GOTO300
1910 PRINT"YOU DON'T HAVE THE ";Z$:GOTO300
1920 L(R)=1:GOTO300
1930 IFR=19ANDB$="RUN"THEN1935
1931 GOTO1940
1935 PRINT"ON THE SCREEN APPEARS -":PRINT"ONE THING IN COMMON":PRINT"YOU CAN SEE"
1936 PRINT"THESE LETTERS HAVE":PRINT"LOOK UNDER ME":PRINT:PRINT"    Z X C V B N M":GOTO300
1940 IFR=17THEND=0:GOTO2000
1950 IFR=27ANDB$="DURANGO"THENPRINT"THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO300
1960 IFR=27THENINPUT"TYPE WHAT ";Z$:GOTO1980
1970 PRINT"YOU CAN'T TYPE HERE":GOTO300
1980 IFZ$="DURANGO"THENPRINT"THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO300
1990 PRINT"SORRY IT DOESN'T HELP TO TYPE",Z$" HERE":GOTO300
2000 PRINT"LETTER PLEASE ";
2010 D=D+1
2020 B$(D)=INKEY$:IFB$(D)=""THEN2020
2030 PRINTB$(D)
2040 IFB$(1)="A"ANDB$(2)="C"ANDB$(3)="N"ANDB$(4)="U"THENPRINT"THE SOUTH DOOR IS NOW OPEN":T(2,17)=26:GOTO300
2050 IFD=>50THEN?"YOU DIDN'T HAVE TO TYPE ALL 50  STATES,  THE ANSWER ONLY HAS 4  STATES IN IT":FORPP=1TO3000:NEXT:GOTO590
2060 GOTO2000
2070 IFR=32ANDB$="BUTTON"THEN2075
2071 GOTO2080
2075 CLS:PRINT"A MESSAGE READS :":PRINT:PRINTTAB(15)"A":PRINTTAB(13)"PENNY"
2076 PRINTTAB(11)"SAVED IS A":PRINTTAB(9)"A PENNY EARNED":FORX=1TO3500:NEXT:CLS:GOTO220
2080 IFR=31ANDLEFT$(B$,6)="BUTTON"THEN2910
2090 IFR=31ANDB$="PAD"THEN2910
2100 IFR=22ANDB$=O$(30)THEN590
2110 IFR=12ANDLEFT$(B$,4)="PAIN"THEN650
2120 IFR=19ANDB$="COMPUTER"THENPRINT"NICE TRY, BUT NOTHING UNDER THE COMPUTER":GOTO300
2130 IFR=17ANDB$="KEYBOARD"THENPRINT"NICE TRY, BUT NOTHING HERE":GOTO300
2140 IFR=27ANDB$="TYPEWRITER"THENPRINT"YOU FIND A PIECE OF PAPER UNDER THE TYPEWRITER":PP=1:GOTO300
2150 IFR=36ANDB$="CHEST"THENPRINT"YOU FOUND A PLASTIC DOLLAR SIGN TAPED TO THE BACK OF THE CHEST":WW=1:GOTO300
2160 PRINT"YOU CAN'T ";A$;" THE ";B$:GOTO300
2170 CLS:PRINT"YOU'RE SUPPOSED TO BE A BIG TIMEADVENTURER, BUT IF YOU NEED HELPTYPE <VERB> TO GET A VERB LIST":GOTO300
2190 PRINT"YOU HAVE : ";
2200 IFLK=1THENPRINTTAB(11)"A LEAD KEY"
2210 IFSK=1THENPRINTTAB(11)"A SILVER KEY"
2220 IFGK=1THENPRINTTAB(11)"A GOLD KEY"
2230 IFCO=1THENPRINTTAB(11)"A LARGE CONTAINER"
2240 IFWW=1THENPRINTTAB(11)"$$ DOLLAR SIGN $$"
2250 GOTO300
2260 CLS:PRINT@8,"**VERB LIST**":PRINT
2280 FORX=1TO20:PRINTV$(X),:NEXT
2290 PRINT@449,"  PRESS ANY KEY TO CONTINUE"    
2300 IF INKEY$=""THEN2300
2310 GOTO220
2320 IFR=15ANDB$="RING"THENPRINT"YOU NOW HAVE THE GOLD KEY":GK=1:GOTO300
2330 IFR=15ANDB$="KING"THENPRINT"YOU NOW HAVE THE SILVER KEY":SK=1:GOTO300
2340 IFR=15ANDB$="SING"THENPRINT"YOU NOW HAVE THE LEAD KEY":LK=1:GOTO300
2350 IFR=32ANDB$="UNCLOSED SEED"THENPRINT"DO NOT TAKE THE STATEMENT WORD  FOR WORD, THIS IS A PUZZLE":GOTO300
2360 IFR=32ANDB$="OPEN SESAME"THENPRINT"THE DOOR NORTH DOOR OPENS":T(1,32)=23:GOTO300
2370 IFR=35ANDB$="PLEASE"THENPRINT"THE SOUTH DOOR IS NOW OPEN":T(2,35)=42:GOTO300
2380 PRINT"IT WON'T HELP TO SAY ";B$:GOTO300
2390 IFR<>20THENPRINT"IT DOESN'T HELP TO TOUCH THE",B$:GOTO300
2400 FORX=1TO3
2410 PRINT"NO. ";X;"  WHICH WALL":INPUT" <N> <S> <E> <W>";Z$(X)
2420 NEXT
2430 IFZ$(1)="W"ANDZ$(2)="E"ANDZ$(3)="N"THENPRINT"THE WEST DOOR OPENS":T(4,20)=19:GOTO300
2431 GOTO590
2440 CLS:PRINT"THE DOOR IS CARVED LIKE THIS -":PRINT:FORM=1TO5:PRINTTAB(16)CHR$(138):NEXT:PRINTTAB(7)"ŒŒŒŒŒŒŒŒŒˆŒŒŒŒŒŒŒŒŒ"
2480 FORM=1TO5:PRINTTAB(16)CHR$(138):NEXT:GOTO300
2510 IFQ$="W"THEN2530
2520 IFQ$="N"THEN300
2530 ?"COMBINATION LOCK":INPUT"RIGHT ";A:INPUT"LEFT ";B:INPUT"RIGHT ";C:IFA=5ANDB=5ANDC=5THEN?"THE DOOR OPENS":T(4,32)=31:GOTO300
2580 PRINT"SORRY THAT IS WRONG":GOTO300
2590 Q$=INKEY$:IFQ$=""THEN2590
2600 PRINTQ$
2610 IFQ$<>"N"ANDQ$<>"S"ANDQ$<>"W"THEN1090
2620 IFQ$="N"THENPRINT"IT'S A PAINTING OF A WINDOW WITHSOME VELVET CURATINS":GOTO300
2630 IFQ$="W"THENPRINT"THE PAINTING IS OF A BROWN DOG":GOTO300
2640 IFQ$="S"THENPRINT"THE PAINTING IS OF A SPOTTED COW":GOTO300
2650 Q$=INKEY$:IFQ$=""THEN2650
2660 IFQ$="N"THEN590
2670 IFQ$="W"THENPRINT"YOU FIND A SAFE":GOTO300
2680 GOTO710
2690 KL$=LEFT$(C$,4)
2700 KR$=RIGHT$(C$,3)
2710 RETURN
2720 Q$=INKEY$:IFQ$=""THEN2720
2730 PRINTQ$
2740 IFQ$<>"N"ANDQ$<>"S"ANDQ$<>"E"ANDQ$<>"W"THENPRINT"NOT A DIRECTION":PRINT"DIRECTION <N> <S> <E> <W> ?":GOTO2720
2741 RETURN
2750 IFQ$="S"THENPRINT"THERE IS NO CURTAIN THERE":GOTO1470
2760 IFQ$="N"THEN590
2770 IFQ$="E"THEN?"YOU SEE A WALL WITH FOLLOWING : TO OPEN SAFE :":?"L-DOLLS, R-SON X SON, L-PENS,   R-SHOES":GOTO300
2780 IFQ$="W"THENPRINT"YOU FIND A PASSAGE LEADING WEST":T(4,13)=12:GOTO300
2790 PRINT"COMBINATION LOCK"
2800 INPUT"LEFT ";A
2810 INPUT"RIGHT ";B
2820 INPUT"LEFT ";C
2830 INPUT"RIGHT ";D
2840 IFA=23ANDB=49ANDC=11ANDD=16THENPRINT"THE SAFE OPENS ":SA=1:GOTO300
2850 PRINT"SORRY WRONG COMBINATION":GOTO300
2860 IFGK=0THENPRINTK$(1);"   ";
2870 IFSK=0THENPRINTK$(2);"   ";
2880 IFLK=0THENPRINTK$(3);"   ";
2890 IFGK>0ANDSK>0ANDLK>0THENPRINT"THREE EMPTY KEY HOOKS":GOTO300
2900 PRINT:RETURN
2910 PRINT"NUMBERS PLEASE"
2920 FORX=1TO5
2930 P$(X)=INKEY$:IFP$(X)=""THEN2930
2940 PRINTP$(X);:NEXT
2950 PP$=P$(1)+P$(2)+P$(3)+P$(4)+P$(5)
2960 P=VAL(PP$):IFP=79464THENPRINT:PRINT"THE DOOR OPENS":T(4,31)=30:GOTO300
2970 PRINT:PRINT"SORRY WRONG NUMBER":GOTO300
2980 IFCN=9THEN2990
2981 GOTO590
2990 T(3,7)=8:PRINT"CORRECT, THE DOOR IS NOW OPENED":GOTO300
3000 INPUT"ENTER NUMBER PLEASE ";Z
3010 IFZ=0THENPRINT"THE SOUTH DOOR OPENS":T(2,11)=18:GOTO300
3011 PRINT"SORRY THE NUMBER DOES NOT WORK  HERE":GOTO300
3020 INPUT"IN TWO WORDS INPUT YOUR ANSWER";Z$
3030 IFZ$<>"FREEZE WATER"THENPRINT"SORRY THAT'S NOT RIGHT":GOTO300
3040 PRINT"CORRECT- DURANGO DAN PULLS A    SKELETON KEY FROM HIS JACKET ANDOPENS THE SOUTH DOOR":T(2,29)=38:GOTO300
3050 INPUTZ:IFZ=1THENPRINT"YOU SEE 6 SOCKS, 3 SHIRTS, AND  2 BELTS IN THE TOP DRAWER":CD(1)=1:GOTO300
3070 IFZ=2THENCD(2)=2:PRINT"THE DRAWER IS EMPTY":GOTO300
3080 IFZ=3THENPRINT"YOU SEE 2 BLANKETS":CD(3)=3:GOTO300
3090 IFZ=4THENPRINT"YOU SEE A NUMBER OF SPINAL      VERTABRAE":CD(4)=4:GOTO300
3100 PRINT"THERE WAS ONLY 4 DRAWERS":GOTO300
3110 IFR=4ANDB$="1155"THENPRINT"THE EAST DOOR OPENS":T(3,4)=5:GOTO300
3120 PRINT"SORRY WRONG NUMBER":GOTO300
3130 FORI=1TO4
3140 PRINT"DRAWER NO.";I;" IS ";
3150 IFCD(I)<>0THENPRINT"OPEN":GOTO3160
3151 PRINT"CLOSED"
3160 NEXT:GOTO300
3190 IFR<>2THENPRINT"IT DOESN'T HELP TO PLAY THE ":PRINTB$;" HERE":GOTO300
3200 IFLEFT$(B$,4)="TAPE"THENPRINT"WHICH TAPE ?";
3210 Q$=INKEY$:IFQ$=""THEN3210
3220 Q=VAL(Q$)
3230 PRINTQ
3240 IFQ<1ORQ>4THENPRINT"THERE AREN'T THAT MANY TAPES":PRINT"WHICH TAPE":GOTO3210
3250 IFQ=1THENPRINT"YOU SEE SEVERAL EPISODES OF     GILLIGAN'S ISLAND":GOTO300
3260 IFQ=2THEN3265
3261 GOTO3270
3265 PRINT"DGO DAN APPEARS ON THE SCREEN   AND SAYS :":PRINT"YOU ARE HALF WAY THERE"
3266 PRINT"SO DON'T DESPAIR":PRINT"IT'S ABSURD":PRINT"BUT ASK THE BIRD":GOTO300
3270 IFQ=3THENPRINT"YOU SEE THE MOVIE 'STAR WARS'":GOTO300
3280 IFQ=4THENPRINT"YOU SEE SEVERAL EPISODES OF     I LOVE LUCY":GOTO300
3290 PRINT@418,"TO START OVER PRESS <ENTER>":INPUTX:CLEAR:GOTO20
3300 IFWX=1THEN3305
3301 GOTO3310
3305 PRINT"CONGRATULATIONS YOU MADE IT     THROUGH THE SILVERTON HOUSE WITHTHE DOLLAR SIGN."
3306 PRINT"YOU LEAVE DURANGO WITH RICH     REWARDS. YOU TRULY ARE A GREAT  ADVENTURER":END
3310 PRINT"YOU MADE IT!  HOWEVER YOU HAVE  BEEN DISQUALIFIED. AS YOU DIDN'TRETURN WITH THE DOLLAR SIGN"
3311 PRINT"HIDDEN WITHIN THE HOUSE":PRINT:PRINT"PLAY AGAIN ?"
3320 Z$=INKEY$:IFZ$=""THEN3320
3330 IFZ$="Y"THENRUN
3340 IFZ$="N"THENEND
3350 GOTO3320
3400 PRINT"$$$$$ SILVERTON ADVENTURE $$$$$$";
3410 PRINT"================================"
3420 PRINT"BEING ONE OF THE TOP ADVENTURERSOF THIS CENTURY YOU HAVE BEEN"
3421 PRINT"INVITED TO THE FIRST DURANGO    PUZZLERS ADVENTURE GAME.";
3430 PRINT" YOUR   GOAL IS SIMPLE. ENTER THE       SILVERTON HOUSE, OWNED BY A"
3431 PRINT"SLIGHTLY CRAZY OLD MAN BY THE   NAME OF DURANGO DAN, FIND A     DOLLAR SIGN HIDDEN WITHIN THE"
3432 PRINT"HOUSE AND EXIT TO A WAITING     CROWD OUTSIDE THE HOUSE......"
3440 GOSUB4000
3450 CLS:PRINT:PRINT"      TOP HONORS GO TO THE MOST PERSISTENT AND PUZZLE HAPPY"
3451 PRINT"PERSON WHO CAN SOLVE THE PUZZLESAND PROCEED WITHOUT ENDING UP   BACK AT THE BEGINING."
3460 PRINT"      IN THIS GAME DIRECTIONS   CAN BE GIVEN AS: EXAMPLE <NORTH><GO NORTH> OR <N>. MOST OTHER"
3461 PRINT"COMANDS WILL BE TWO WORDS OR    LESS: EXAMPLE <LOOK> <LOOK DESK><HELP> <GET KEY>."
3470 PRINT"      GOOD LUCK!!":PRINT"P.S. A PIECE OF PAPER MIGHT COMEIN HANDY.":GOSUB4000:GOTO15
4000 PRINT@482,"<PRESS ANY KEY TO CONTINUE>";
4010 J$=INKEY$:IFJ$=""THEN4010
4020 RETURN
REM ORIGINAL TRS-80 COCO VERSION BY
REM JOHN AND PAT EVEREST
