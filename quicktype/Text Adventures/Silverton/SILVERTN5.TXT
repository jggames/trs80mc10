0 CLS:PRINT@7,"SILVERTON ADVENTURE":PRINT@64,;:GOTO346
1 ZZ=1:C1=32:FORC1=C1TOZZSTEP-1:I$=MID$(M$,C1,1):IFI$<"!"THENPRINTMID$(M$,ZZ,C1-ZZ):ZZ=C1+1:C1=ZZ+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 GOSUB1:GOTO30
4 DATABATHROOM,TV ROOM,GAME ROOM,OFFICE,CONFERENCE ROOM,LONG ROOM,CLOSET ROOM,BEDROOM,AVIARY,TROPHY ROOM
5 DATAPANELED ROOM,GALLERY,OFFICE,BEDROOM,LARGE ROOM,LIBRARY,FAMILY ROOM,LITTLE ROOM,COMPUTER ROOM,COLORFUL ROOM
6 DATAKITCHEN,DINING ROOM,LONG HALL,LONG NARROW HALL,RECEPTION ROOM,LARGE ROOM,OFFICE,SM DINING ROOM
7 DATASMALL KITCHEN,LAUNDRY ROOM,DEN,SITTING ROOM,SMALL BEDROOM,STATE ROOM,SHORT HALL,BEDROOM,UTILITY ROOM
8 DATAGARAGE,GREEN HOUSE,SMALL ROOM,FRONT ENTRANCE,LIVING ROOM,PORCH,BATHTUB,1,SINK,1,MEDICINE CABINET,1,TV,2
9 DATAVCR,2,TABLE,2,GAMES,3,COUCH,3,DESK,4,TABLE,5,BOWL OF FRUIT,5,PIECE OF PAPER,6,15 NUMBERED CLOSETS,7,BIRDS
10 DATA9,CABINETS,10,TROPHIES,10,STUFFED ANIMALS,10,PAINTINGS TO N-S-W,12,CURTAINS TO W-N-E,13,CANOPY BED,14,DESK,15
11 DATAALCOVE,15,BOOKCASES,16,FURNITURE,17,SHELF,17,COLOR COMPUTER,19,COLORED WALLS,20,TABLE,21,CHANDELIER,22,PAINTING,22
12 DATAPAINTING,25,GLASS CASE,25,DESK,27,TABLE,28,CONTAINER,28,DURANGO DAN,29,2 CANS,29,LARGE BOWL,29,MIRROR,31,CHAIR,31
13 DATAWINDOW,31,MONITOR,32,BUNKBEDS,33,NOTEBOOK,34,BED,36,CHEST,36,WASHER,37,WINDOW,37,PLANTS,39,WINDOW,39,CLOSET,40
15 DATAPICTURE,40,WINDOW,40,FRONT DOOR,41,CURTAINS,42,WINDOW,42,SCREEN,43,EMBROIDERED SAMPLER,43
16 DATA0,4,,,,5,3,,,6,,2,1,8,-1,,2,,,4,3,10,,,,14,-1,,4,,,7,,,10,,-4,,11,9,,-1,,10,,,13,,,22,,,7,23,,,,24,16,,,25,,15,,-1
17 DATA18,,11,,19,17,,28,20,18,,,,-1,,30,22,,13,,,21,14,-4,,,15,33,,,16,,,,-4,35,27,,,,,-1,19,,29,,,-1,,28,21,,31,,,,32
18 DATA-1,-1,39,33,-1,24,,,32,,,35,,26,-1,,34,,43,37,,,,38,36,29,,,37,32,,-1,,,,,39,,,42,,35,,,41,36,,,0
19 DATATAKE,1,GET,1,EXAMINE,2,LOOK,2,READ,2,OPEN,3,KICK,4,ASK,5,USE,6,UNLOCK,7,TYPE,8,MOVE,9,PUSH,9,HELP,10
20 DATAVERB,11,SAY,12,TOUCH,13,DIAL,14,PLAY,15,INVENTORY,16,GOLD KEY,SILVER KEY,LEAD KEY
21 FORM=1TO4:PRINTTAB(9)"Š":NEXT:RETURN
22 CLS:FORX=1TO43:READM$:NEXT:FORX=1TO58:READO$(X),O(X):NEXT:FORX=1TO43:READT(1,X),T(2,X),T(3,X),T(4,X):NEXT
23 FORX=1TO20:READV$(X),V(X):NEXT:FORX=1TO3:READK$(X):NEXT:T$(1)="NORTH":T$(2)="SOUTH":T$(3)="EAST":T$(4)="WEST":R=25
24 CLS:RESTORE:FORZZ=1TOR:READM$:NEXT:M$="YOU ARE IN THE "+M$:GOSUB1
25 PRINT:PRINT"YOU SEE:";:FORX=1TO58:IFO(X)=RTHENPRINTTAB(10)O$(X)
26 NEXT:IFR=35THENPRINTTAB(10)"A CARVED DOOR"
28 PRINT:PRINT"EXITS LEAD:";:FORX=1TO4:IFT(X,R)<>0ANDT(X,R)<>-2THENPRINTTAB(14)T$(X)
29 NEXT
30 PRINT:INPUT"WHAT WOULD YOU LIKE TO DO NOW ";C$:IFLEN(C$)>1THEN36
31 IFC$="N"THENC$="NORTH"
32 IFC$="S"THENC$="SOUTH"
33 IFC$="E"THENC$="EAST"
34 IFC$="W"THENC$="WEST"
35 GOTO44
36 FORX=1TOLEN(C$):IFMID$(C$,X,1)=" "THENA$=LEFT$(C$,X-1):B$=MID$(C$,X+1):GOTO40
37 NEXT
38 IFC$="NORTH"ORC$="SOUTH"ORC$="EAST"ORC$="WEST"THEN44
39 A$=C$
40 IFA$="GO"THENC$=B$:GOTO44
41 FORX=1TO20:IFV$(X)=A$THENA=V(X):GOTO43
42 NEXT:M$="I DON'T UNDERSTAND":GOTO3
43 ONAGOTO60,70,177,193,194,203,217,238,251,264,272,273,280,325,331,265
44 FORX=1TO4:IFC$=T$(X)THENDR=X:X=4:NEXT:GOTO47
45 NEXT
46 M$="YOU CAN'T GO THAT WAY":GOTO3
47 IFT(DR,R)>0ANDT(DR,R)<>100THENR=T(DR,R):GOTO24
48 IFT(DR,R)=0THEN46
49 IFT(DR,R)=-1THENPRINT"A LOCKED DOOR BLOCKS EXIT"
50 IFR=32ANDDR=4THENM$="THERE IS A NOTE ABOVE A COMBINATION LOCK":GOTO3
51 IFR=32ANDDR=1THENM$="THERE IS A SIGN ABOVE IT":GOTO3
52 IFT(DR,R)=0THEN46
53 IFR=31THENM$="THERE IS SOMETHING STRANGE ABOUT THE DOOR":GOSUB1
54 IFR=11ANDDR=2THENM$="THERE IS AN ENGRAVED PLATE ABOVE THE DOOR":GOSUB1
55 IFT(DR,R)<>-2THEN56
56 IFT(DR,R)=-4THENM$="YOU NOW FIND THE DOOR HANDLE BROKEN AND NO WAY BACK":GOTO3
57 IFT(DR,R)=100THEN59
58 GOTO30
59 SOUND20,3:M$="A TRAP DOOR OPENS. YOU FIND YOURSELF BACK AT THE START":GOSUB1:RUN349
60 IFR=16ANDB$="BOOK"THENT(3,16)=100:M$="THE BOOKCASE MOVED REVEALING A DARK PASSAGE EAST":GOTO3
61 IFR=22ANDB$=O$(30)THEN59
62 IFR=12ANDB$="PAINTING"THEN296
63 IFR=28ANDB$=O$(35)ANDCC=0THENM$="YOU NOW HAVE THE CONTAINER":CO=1:CC=1:O$(35)="":GOTO3
64 IFWW=1ANDRIGHT$(B$,4)="SIGN"ANDR=36THENM$="YOU NOW HAVE THE "+B$:WX=1:GOTO3
65 C1=0:FORZZ=1TOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
66 NEXT:IFR=15ANDC1=1ANDLK<>3THEN59
67 IFR=15ANDRIGHT$(B$,3)="KEY"ANDLK=3THENM$="THE KEY IS STUCK IN THE LOCK":GOTO3
68 IFR=39ANDKK=1ANDRIGHT$(B$,3)="KEY"THENM$="SILVER":SK=1:GOTO271
69 GOTO263
70 IFA$=C$THEN24
71 PRINT"YOU SEE:":IFR=25ANDB$=O$(31)THENM$="A GIRL IN A BLUE DRESS SITTING ON A SWING":GOTO3
73 IFR=40ANDB$=O$(52)THENM$="IT'S A PICTURE OF A GIRL IN A RED DRESS SITTING ON A STOOL":GOTO3
74 IFR=5ANDB$="TABLE"THEN173
75 IFR=15ANDB$="PENS"THENM$="THERE ARE 11 PENS":GOTO3
76 IFR=1ANDB$="CABINET"THENM$="AN EMPTY CABINET":GOTO3
77 IFR=15ANDB$="PAPER"THEN79
78 GOTO81
79 M$="THE PAPER SAYS: BEHIND THE CHEST IS THE BACK, HIDDEN THERE IS A FACT, "
80 M$=M$+"AN S-CURVED SPINE WITH TWO NOT ONE LINE, UNLESS PERSEVERANCE YOU LACK":GOTO3
81 IFR=2ANDLEFT$(B$,4)="TAPE"THENM$="FOUR RECORDED VIDEO TAPES":GOTO3
82 IFR=15ANDB$="BOOK"THENM$="'7TH SON OF A 7TH SON'":GOTO3
83 IFR=14ANDB$=O$(45)THEN173
84 IFR=25ANDRIGHT$(B$,4)="CASE"THENM$="23 DOLLS":GOTO3
85 IFR=32ANDB$=O$(42)THENM$="THE MONITOR HAS A BUTTON":GOTO3
86 IFR=32ANDB$="NOTE"THENM$="R-#A'S  L-#E'S  R-#N'S":GOTO3
87 IFR=32ANDB$="SIGN"THENM$="SAY UNCLOSED SEED, THEN PROCEED, IF YOU LACK, YOU CAN'T GO BACK":GOTO3
88 IFR=5ANDB$="FRUIT"ORB$="BOWL"ANDR=5THENM$="THERE ARE 6 ORANGES IN THE BOWL":GOTO3
89 IFR=40ANDB$=O$(51)THENM$="SOME GOWNS AND SHOES":GOTO3
90 IFR=40ANDB$="SHOES"THENM$="THERE ARE 8 PAIRS OF SHOES":GOTO3
91 IFR=40ANDB$="GOWNS"THENM$="THERE ARE 8 GOWNS":GOTO3
92 IFR=16ANDLEFT$(B$,4)="BOOK"THENM$="A BOOK ENTITLED '43 ADVENTURES'":GOTO3
93 IFR=31ANDB$="DOOR"THEN95
94 GOTO96
95 M$="THERE IS A KEYPAD LIKE ON A PHONE AND INSTRUCTIONS":GOTO3
96 IFR=31ANDB$="INSTRUCTIONS"THENM$="TO OPEN: THE GIRL IN THE BLUE DRESS WAS SITTING ON A #-#-#-#-#":GOTO3
97 IFR=3ANDB$=O$(7)THENM$="YOU SEE SOME CARD GAMES AND A MONOPOLY BOARD":GOTO3
98 IFR=3ANDLEFT$(B$,5)="MONOP"ORR=3ANDRIGHT$(B$,5)="BOARD"THENM$="3 PROPERTIES HAVE HOTELS ON THEM":GOTO3
99 IFR=3ANDLEFT$(B$,5)="PROPE"ORR=3ANDLEFT$(B$,4)="HOTE"THEN101
100 GOTO102
101 M$="THE PROPERTIES ARE KENTUCKY, N. CAROLINA AND ATLANTIC":GOTO3
102 IFR=16ANDB$="BOOK"THENM$="BOOK":GOTO215
103 IFR=15ANDB$="DESK"ANDLK=3THENPRINT"A LEAD KEY IN THE DESK"
104 IFR=15ANDB$=O$(21)THENM$=B$:GOTO174
105 IFR=21ANDB$=O$(28)THENM$="IN A FINE COATING OF FLOUR YOU SEE WRITTEN: CURSES, CURTAINS!":GOTO3
106 IFR=7ANDLEFT$(B$,4)="CLOS"THEN108
107 GOTO110
108 M$="A SIGN READS: TO OPEN THE EAST DOOR THE CORRECT CLOSET MUST BE OPENED.":GOSUB1
109 M$=M$+"THE # OF THE CLOSET= THE SUM OF THE SAFE COMBO DIVIDED BY THE # OF WORDS IN "+O$(36)+"'S AUTOGRAPH":GOTO3
110 IFB$="WINDOW"THENM$="YOU SEE AN EAGER CROWD WAITING AT THE FINISH LINE":GOTO3
111 IFR=4ANDB$=O$(9)THENM$="ON THE DESK IS A PIECE OF PAPER AND AN OLD BLACK PHONE":GOTO3
112 IFR=4ANDB$="PHONE"THEN173
113 IFR=4ANDB$="PAPER"THENM$="THE PAPER SAYS: 86 6736 843 3667 3425 1155":GOTO3
114 IFR=6ANDRIGHT$(B$,5)="PAPER"THEN116
115 GOTO117
116 M$="THE BARELY LEGIBLE PAPER READS: ...IS ... .NLY PLAC. .N .SA WHER. FOU. STAT.. .EET ....":GOTO3
117 IFR=9ANDB$=O$(14)THENM$="YOU SEE A SPARROW, A PARAKEET A COCKATOO AND A PARROT":GOTO3
118 IFR=15ANDB$=O$(22)THEN121
119 GOTO123
120 PRINT"WHICH PAINTING: N, S, W";:RETURN
121 GOSUB305:M$="AND BELOW IS WRITTEN: THE KEY IS THREE!":GOSUB1
122 PRINT"TINONGTGTATHKHEEELTSEHIAELDGVKOEELRYDKSKEAEYYYSSSAIAYNYKGRI":GOTO30
123 IFR=31ANDB$=O$(39)THENM$="THE MIRROR IS CLOUDY":GOTO3
124 IFR=12ANDLEFT$(B$,4)="PAIN"THEN292
125 IFR=22ANDB$=O$(30)THENM$="YOU SEE A PAINTING SLIGHTLY AJAR":GOTO3
126 IFR=13ANDLEFT$(B$,4)="CURT"THENM$="CLOSED CURTAINS":GOTO3
127 IFR=12ANDSA=1ANDB$="SAFE"THEN129
128 GOTO131
129 M$="YOU FIND A PICTURE OF "+O$(36)+" WITH THIS WRITTEN ON IT: "
130 M$=M$+"VIY MIBLC, ABBY WRALC, TRECES THIN, TROBHOAM LAWL LOVE, "+O$(36):GOTO3
131 IFR=12ANDB$="SAFE"ANDSA=0THENM$="THE SAFE IS LOCKED":GOTO3
132 IFR=1ANDB$="WALL"THEN134
133 GOTO135
134 M$="ON THE WALL IS SCRIBBLED: 'SILVER IS THE KEY' AND 'FOR A GOOD TIME CALL 303-259-5880'":GOTO3
135 IFR=10ANDB$=O$(16)THENM$="YOU SEE 72 TROPHIES":GOTO3
136 IFR=10ANDLEFT$(B$,4)="ANIM"ORB$=O$(17)ANDR=10THENM$="THERE IS A BEAR, DEER, ELK, LION AND TIGER":GOTO3
137 IFR=11ANDLEFT$(B$,4)="ENGR"ORB$="PLATE"ANDR=11THENM$="TO UNLOCK THE DOOR ANSWER THE ELK'S QUESTION":GOTO3
138 IFR=19ANDB$="COMPUTER"THENM$="THE MC-10 IS ON AND THE CURSOR IS FLASHING":GOTO3
139 IFLEFT$(B$,4)="UNDE"THENM$="TRY MOVING SOMETHING":GOTO3
140 IFR=20ANDLEFT$(B$,4)="WALL"THEN142
141 GOTO144
142 M$="THE WALLS ARE COLORED AS FOLLOWS: N-GREEN S-BLUE E-RED W-YELLOW":GOSUB1:PRINT
143 M$="A VOICE SAYS: TO ESCAPE TOUCH ONLY THESE WALLS IN THIS ORDER- ATLANTIC, KENTUCKY, N. CAROLINA":GOTO3
144 IFR=28ANDB$="TABLE"THEN173
145 IFCO=1ANDLEFT$(B$,4)="CONT"ORR=28ANDB$=O$(35)THENM$="THE CONTAINER READS: DO NOT OPEN UNTIL ABSOLUTELY NECESSARY":GOTO3
146 IFR=29ANDB$="CANS"THENM$="THE 2 CANS ARE FILLED WITH WATER":GOTO3
147 IFR=36ANDB$="CHEST"THENPRINT"THE CHEST HAS 4 DRAWERS":GOTO327
148 IFR=36ANDLEFT$(B$,4)="DRAW"THEN327
149 IFR=43ANDRIGHT$(B$,4)="PLER"THEN151
150 GOTO153
151 PRINT"THE SAMPLER:  =======":PRINTTAB(14)": K T :":PRINTTAB(14)": C S :"
152 PRINTTAB(14)": E E :":PRINTTAB(14)": H H :":PRINTTAB(14)": C C :":PRINTTAB(14)":::::::":PRINTTAB(14)"! ! ! !":GOTO30
153 IFR=17ANDB$="SHELF"THENM$="YOU SEE A KEYBOARD AND A DIARY":GOTO3
154 IFR=17ANDDI=0ANDB$="DIARY"THEN156
155 GOTO157
156 M$="THE FIRST PAGE OF THE DIARY SAYS: TO OPEN DOOR TYPE THE FIRST LETTER OF EACH STATE IN ALPHA ORDER...":DI=1:GOTO3
157 IFDI>0ANDR=17ANDB$="DIARY"THEN159
158 GOTO160
159 M$="...THE DOOR IS CARVED WITH SHAPES OF STATES. YOU ONLY NEED TO TYPE THESE STATES.":GOTO3
160 IFR=27ANDPP=1ANDB$="PAPER"THENM$="TYPE DURANGO AND YOU CAN GO":GOTO3
161 IFR=27ANDB$="DESK"THENM$="YOU SEE A LAMP AND A TYPEWRITER":GOTO3
162 IFR=35ANDRIGHT$(B$,4)="DOOR"THENM$="THE SOUTH DOOR LOOKS LIKE THIS: - --- --- *--* * -* *** *- -*--*--*":GOSUB1
163 IFR=34ANDB$=O$(44)THENM$="THE NOTEBOOK SAYS: REMEMBER-- CURSES, CURTAINS":GOTO3
164 IFR=17ANDB$="DOOR"THEN285
165 IFR=39ANDKK=1ANDB$="DOOR"ANDSK<>1THENM$="A SILVER KEY IS IN THE DOOR":GOTO3
166 IFR=2ANDB$="TABLE"THENM$="ON THE TABLE YOU SEE FOUR VIDEO TAPES":GOTO3
167 IFR=17ANDLEFT$(B$,4)="TYPE"THEN173
168 IFR=27ANDB$="LAMP"THENM$="THE LAMP IS ON":GOTO3
169 IFB$="DOOR"THEN173
170 FORX=1TO58:IFB$=O$(X)THENM=1:GOTO172
171 NEXT
172 IFO(X)<>RANDM=1THENM$="I DON'T SEE IT HERE":M=0:GOTO3
173 M$="NOTHING UNUSUAL":GOTO3
174 IFL(R)=0THEN192
176 M$=M$+" IS OPEN":GOSUB1:GOTO234
177 IFR=15ANDB$=O$(21)THEN192
178 IFR=7ANDLEFT$(B$,6)="CLOSET"THENINPUT"WHICH CLOSET #";CN:ON-(CN=9)GOTO314:GOTO59
179 IFR=13ANDLEFT$(B$,4)="CURT"THEN299
180 IFR=35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDC=0ANDLC=1THEN182
181 GOTO183
182 PRINT"YOU FIND A PIECE OF PAPER:":M$="A:*-  E:*  L:*-**  T:-  P:*--*  S:***  O:---  N:-*  Y:-*--":GOTO3
183 IFB$=O$(35)ANDCO=0THENM$="CONTAINER":GOTO215
184 IFLEFT$(B$,4)="CONT"ANDC=1ANDLC=1THENM$="YOU SEE AN EMPTY CONTAINER":GOTO3
185 IFR<>35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDLC=1THEN187
186 GOTO188
187 M$="YOU SEE A PIECE OF PAPER BUT, BEFORE YOU CAN READ IT IT BURNS IN A FLASH OF SMOKE":C=1:GOTO3
188 IFR=36ANDLEFT$(B$,4)="DRAW"THEN320
189 IFR=42ANDB$=O$(55)THEN59
190 IFR=1ANDRIGHT$(B$,4)="INET"THENM$="IT'S EMPTY":GOTO3
191 M$="YOU CAN'T OPEN THE "+B$:GOTO3
192 M$="THE "+B$+" IS LOCKED":GOTO3
193 M$="HOW WOULD YOU LIKE IT IF A "+B$+" WERE TO KICK YOU?":GOTO3
194 IFR=9ANDB$="COCKATOO"THENM$="THE BIRD CACKLES... FREEZE WATER FREEZE WATER...":GOTO3
195 IFR=10ANDB$="ELK"THEN197
196 GOTO198
197 M$="THE ELK SAYS: ANSWER THIS AND REMEMBER, HOW MANY CUBIC FEET OF DIRT IN A HOLE 7 X 14 X 3?":GOTO3
198 IFR=29ANDRIGHT$(B$,3)="DAN"THEN200
199 GOTO202
200 M$="I WILL UNLOCK THE SOUTH DOOR FOR YOU IF YOU TELL ME HOW TO PUT THE CONTENTS OF THE TWO CANS "
201 M$=M$+"INTO THE BOWL AND STILL BE ABLE TO TELL THEM APART":GOSUB1:GOTO317
202 M$="THE "+B$+" DOESN'T ANSWER":GOTO3
203 FORL=1TOLEN(B$):C1=0:FORZZ=LTOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
205 NEXT:IFC1>0THEN208
206 NEXT:IFLEFT$(B$,3)<>"KEY"THENM$="IT DOESN'T HELP TO USE THE "+B$+" HERE":GOTO3
208 INPUT"WHICH KEY";M$:I$=LEFT$(M$,4):IF(SK=0ANDI$="SILV")OR(LK=0ANDI$="LEAD")OR(GK=0ANDI$="GOLD")THEN215
211 IFI$="SILV"ANDR=41THEN340
212 IFI$="LEAD"ANDR=15THEN233
213 IFCO=1ANDI$="GOLD"ANDR<>41THENM$="THE CONTAINER IS NOW UNLOCKED":LC=1:GOTO3
214 M$="IT DOESN'T WORK HERE":GOTO3
215 M$="YOU DON'T HAVE THE "+M$:GOTO3
216 PRINT"HOW?":GOTO30
217 IFR=15ANDB$=O$(21)THEN228
218 IFR=39ANDB$="DOOR"THEN228
219 IFR=32ANDB$="DOOR"THEN287
220 IFR=4ANDB$="DOOR"THEN216
221 IFR=31ANDB$="DOOR"THEN216
222 IFR=12ANDB$="SAFE"THEN303
223 IFR=11ANDB$="DOOR"THENINPUT"ENTER NUMBER PLEASE";Z:ON-(Z<>0)GOTO326:M$=" SOUTH":T(2,11)=18:GOTO352
224 IFR=41ANDB$="DOOR"THEN216
225 IFCO<>1ANDB$="CONTAINER"THENM$=B$:GOTO215
226 IFCO=1ANDB$="CONTAINER"THEN216
227 M$="HOW? ORDINARY METHODS LIKE USING KEYS WILL NOT WORK HERE":GOTO3
228 INPUT"WITH WHAT";M$:IF(GK=0ANDM$=K$(1))OR(SK=0ANDM$=K$(2))OR(LK=0ANDM$=K$(3))THEN215
230 IFR=39ANDSK=1ANDM$=K$(2)THENM$="":T(3,39)=40:SK=0:KK=1:GOTO352
231 IFR=15ANDLK=1ANDM$=K$(3)THEN233
232 M$="IT WON'T WORK HERE":GOTO3
233 M$="THE DESK OPENS REVEALING SOME ":L(R)=1:GOTO235
234 M$="THERE ARE SOME "
235 M$=M$+"PENS, A PIECE OF PAPER AND A BOOK":LK=3:GOTO3
237 L(R)=1:GOTO30
238 IFR=19ANDB$="RUN"THEN240
239 GOTO242
240 PRINT"ON THE SCREEN APPEARS:":PRINT"ONE THING IN COMMON":PRINT"YOU CAN SEE"
241 PRINT"THESE LETTERS HAVE":PRINT"LOOK UNDER ME":PRINT:PRINT"    Z X C V B N M":GOTO30
242 IFR=17THEND=0:GOTO248
243 IFR=27ANDB$="DURANGO"THENM$="THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO3
244 IFR=27THEN246
245 M$="YOU CAN'T TYPE HERE":GOTO3
246 INPUT"TYPE WHAT";M$:IFM$="DURANGO"THENM$="THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO3
247 M$="SORRY IT DOESN'T HELP TO TYPE" +M$+" HERE":GOTO3
248 INPUT"LETTERS PLEASE";I$:IFI$="ACNU"THENM$="THE SOUTH DOOR IS NOW OPEN":T(2,17)=26:GOTO3
249 IFLEN(I$)>49THENPRINT"YOU DIDN'T HAVE TO TYPE ALL 50!":GOTO59
250 GOTO248
251 IFR=32ANDB$="BUTTON"THEN253
252 GOTO255
253 PRINT"A MESSAGE READS:":PRINTTAB(15)"A":PRINTTAB(13)"PENNY"
254 PRINTTAB(11)"SAVED IS A":PRINTTAB(9)"A PENNY EARNED":FORX=1TO4500:NEXT:GOTO24
255 IFR=31AND(LEFT$(B$,6)="BUTTON"ORB$="PAD")THEN310
257 IFR=22ANDB$=O$(30)THEN59
258 IFR=12ANDLEFT$(B$,4)="PAIN"THEN62
259 IFR=19ANDB$="COMPUTER"THENM$="NOTHING UNDER THE COMPUTER":GOTO3
260 IFR=17ANDB$="KEYBOARD"THENM$="NOTHING HERE":GOTO3
261 IFR=27ANDB$="TYPEWRITER"THENM$="YOU FIND A PIECE OF PAPER UNDER THE TYPEWRITER":PP=1:GOTO3
262 IFR=36ANDB$="CHEST"THENM$="YOU FOUND A PLASTIC DOLLAR SIGN TAPED TO THE BACK OF THE CHEST":WW=1:GOTO3
263 M$="YOU CAN'T "+A$+" THE "+B$:GOTO3
264 M$="TYPE 'VERB' TO GET A LIST":GOTO3
265 PRINT"YOU HAVE: ";:IFLK=1THENPRINTTAB(11)"A LEAD KEY"
266 IFSK=1THENPRINTTAB(11)"A SILVER KEY"
267 IFGK=1THENPRINTTAB(11)"A GOLD KEY"
268 IFCO=1THENPRINTTAB(11)"A LARGE CONTAINER"
269 IFWW=1THENPRINTTAB(11)"$$ DOLLAR SIGN $$"
270 GOTO30
271 M$="YOU NOW HAVE THE "+M$+" KEY":GOTO3
272 CLS:PRINT@10,"*verb list*":PRINT:FORX=1TO20:PRINTV$(X),:NEXT:GOSUB350:GOTO24
273 IFR=15ANDB$="RING"THENM$="GOLD":GK=1:GOTO271
274 IFR=15ANDB$="KING"THENM$="SILVER":SK=1:GOTO271
275 IFR=15ANDB$="SING"THENM$="LEAD":LK=1:GOTO271
276 IFR=32ANDB$="UNCLOSED SEED"THENM$="DO NOT TAKE THE STATEMENT WORD FOR WORD, THIS IS A PUZZLE":GOTO3
277 IFR=32ANDB$="OPEN SESAME"THENM$=" NORTH":T(1,32)=23:GOTO352
278 IFR=35ANDB$="PLEASE"THENM$="THE SOUTH DOOR IS NOW OPEN":T(2,35)=42:GOTO3
279 M$="IT WON'T HELP TO SAY "+B$:GOTO3
280 IFR<>20THENM$="IT DOESN'T HELP TO TOUCH THE"+B$:GOTO3
281 FORX=1TO3:PRINT"#"X" WHICH WALL?":INPUT"N, S, E, W";Z$(X):NEXT
282 IFZ$(1)="W"ANDZ$(2)="E"ANDZ$(3)="N"THENM$=" WEST":T(4,20)=19:GOTO352
283 GOTO59
284 INPUT"RIGHT";A:INPUT"LEFT";B:INPUT"RIGHT";C:RETURN
285 PRINT"THE DOOR IS CARVED LIKE THIS:":GOSUB21:PRINTTAB(2)"ŒŒŒŒŒŒŒˆŒŒŒŒŒŒŒ":GOSUB21:GOTO30
287 INPUT"WHICH DOOR: N OR W";Q$:IFQ$="N"THEN227
289 GOSUB351:GOSUB284:IFA=5ANDB=5ANDC=5THENM$="":T(4,32)=31:GOTO352
290 GOTO304
291 M$="A PAINTING OF A "+M$:GOTO3
292 GOSUB120:INPUTQ$:IFQ$<>"N"ANDQ$<>"S"ANDQ$<>"W"THEN292
293 IFQ$="N"THENM$="WINDOW WITH SOME VELVET CURTAINS":GOTO291
294 IFQ$="W"THENM$="BROWN DOG":GOTO291
295 M$="SPOTTED COW":GOTO291
296 GOSUB120:INPUTQ$:IFQ$="N"THEN59
297 IFQ$="W"THENM$="YOU FIND A SAFE":GOTO3
298 GOTO69
299 INPUT"WHICH CURTAIN: N, E, W";Q$:IFQ$<>"N"ANDQ$<>"E"ANDQ$<>"W"THENPRINT"NOT A DIRECTION":GOTO299
300 IFQ$="N"THEN59
301 IFQ$="E"THENM$="YOU SEE A SCRALLED MESSAGE: TO OPEN SAFE L-DOLLS, R-SON X SON, L-PENS, R-SHOES":GOTO3
302 M$="YOU FIND A PASSAGE LEADING WEST":T(4,13)=12:GOTO3
303 GOSUB351:INPUT"LEFT";D:GOSUB284:IFD=23ANDA=49ANDB=11ANDC=16THENM$="THE SAFE OPENS":SA=1:GOTO3
304 M$="SORRY WRONG COMBINATION":GOTO3
305 IFGK=0THENPRINTK$(1);
306 IFSK=0THENPRINTTAB(11)K$(2);
307 IFLK=0THENPRINTTAB(23)K$(3);
308 IFGK>0ANDSK>0ANDLK>0THENM$="THREE EMPTY KEY HOOKS":GOTO3
309 PRINT:RETURN
310 INPUT"NUMBERS PLEASE";M$:P=VAL(M$):IFP=79464THENM$="":T(4,31)=30:GOTO352
311 GOTO326
314 T(3,7)=8:M$="CORRECT, THE DOOR IS OPEN":GOTO3
317 INPUT"YOUR ANSWER IN TWO WORDS";M$:IFM$<>"FREEZE WATER"THENM$="SORRY THAT'S NOT RIGHT":GOTO3
319 M$="CORRECT! "+O$(36)+" PULLS A SKELETON KEY FROM HIS JACKET AND OPENS THE DOOR":T(2,29)=38:GOTO3
320 INPUT"WHICH DRAWER: 1, 2, 3, 4";Z:IFZ=1THENM$="YOU SEE 6 SOCKS, 3 SHIRTS AND 2 BELTS":CD(1)=1:GOTO3
321 IFZ=2THENCD(2)=2:M$="IT'S EMPTY":GOTO3
322 IFZ=3THENM$="YOU SEE 2 BLANKETS":CD(3)=3:GOTO3
323 IFZ=4THENM$="YOU SEE A NUMBER OF VERTABRAE":CD(4)=4:GOTO3
324 M$="THERE ARE ONLY 4 DRAWERS":GOTO3
325 IFR=4ANDB$="1155"THENM$=" EAST":T(3,4)=5:GOTO352
326 M$="SORRY WRONG NUMBER":GOTO3
327 FORI=1TO4:M$="DRAWER #"+STR$(I)+" IS ":IFCD(I)<>0THENM$=M$+"OPEN":GOTO330
329 M$=M$+"CLOSED"
330 GOSUB1:NEXT:GOTO30
331 IFR<>2THENM$="IT DOESN'T HELP TO PLAY THE "+B$+" HERE":GOTO3
333 INPUT"WHICH TAPE";Q:IFQ<1ORQ>4THEN333
334 IFQ=1THENM$="SEVERAL EPISODES OF gilligan's island":GOTO3
335 IFQ=2THEN337
336 GOTO338
337 M$=O$(36)+" APPEARS ON SCREEN AND SAYS: YOU ARE HALF WAY THERE, SO DON'T DESPAIR. IT'S ABSURD, BUT ASK THE BIRD":GOTO3
338 IFQ=3THENM$="THE MOVIE star wars":GOTO3
339 IFQ=4THENM$="SEVERAL EPISODES OF i love lucy":GOTO3
340 IFWX=1THEN342
341 GOTO344
342 M$="CONGRATULATIONS YOU MADE IT THROUGH THE SILVERTON HOUSE WITH THE DOLLAR SIGN.":GOSUB1
343 M$="YOU LEAVE DURANGO WITH RICH REWARDS. YOU TRULY ARE A GREAT ADVENTURER":GOSUB1:END
344 M$="YOU MADE IT! HOWEVER YOU HAVE BEEN DISQUALIFIED SINCE YOU DIDN'T RETURN WITH THE DOLLAR SIGN.":GOSUB1:END
346 M$="AS A TOP ADVENTURER YOU'VE BEEN INVITED TO THE FIRST DURANGO PUZZLERS ADVENTURE GAME.":GOSUB1
347 M$="YOUR GOAL IS SIMPLE-- ENTER THE SILVERTON HOUSE AND FIND A DOLLAR SIGN HIDDEN WITHIN AND EXIT.":GOSUB1
348 PRINT@320,"GOOD LUCK!":PRINT:M$="P.S. A PIECE OF PAPER MIGHT COME IN HANDY.":GOSUB1
349 CLEAR384:DIMO$(59),O(59),T(4,43),V$(20),V(20),K$(3),L(43),Z$(3):GOSUB350:GOTO22
350 PRINT@485,;:INPUT"HIT enter TO CONTINUE";M$:RETURN
351 PRINT"COMBINATION LOCK":RETURN
352 M$="THE"+M$+" DOOR OPENS":GOTO3
