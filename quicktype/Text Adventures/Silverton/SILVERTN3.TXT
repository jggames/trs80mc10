0 CLS:CLEAR256:DIMO$(59),O(59),T(4,43),V$(20),V(20),K$(3),L(43),Z$(3):GOTO3400
1 ZZ=1:C1=32:FORC1=C1TOZZSTEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,ZZ,C1-ZZ):ZZ=C1+1:C1=ZZ+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 GOSUB1:GOTO300
30 DATABATHROOM,TV ROOM,GAME ROOM,OFFICE,CONFERENCE ROOM,LONG ROOM,CLOSET ROOM,BEDROOM,AVIARY,TROPHY ROOM
31 DATAPANELED ROOM,GALLERY,OFFICE,BEDROOM,LARGE ROOM,LIBRARY,FAMILY ROOM,LITTLE ROOM,COMPUTER ROOM,COLORFUL ROOM
32 DATAKITCHEN,DINING ROOM,LONG HALL,LONG NARROW HALL,RECEPTION ROOM,LARGE ROOM,OFFICE,SM DINING ROOM
50 DATASMALL KITCHEN,LAUNDRY ROOM,DEN,SITTING ROOM,SMALL BEDROOM,STATE ROOM,SHORT HALL,BEDROOM,UTILITY ROOM
51 DATAGARAGE,GREEN HOUSE,SMALL ROOM,FRONT ENTRANCE,LIVING ROOM,PORCH,BATHTUB,1,SINK,1,MEDICINE CABINET,1,TV,2
60 DATAVCR,2,TABLE,2,GAMES,3,COUCH,3,DESK,4,TABLE,5,BOWL OF FRUIT,5,PIECE OF PAPER,6,15 NUMBERED CLOSETS,7,BIRDS
61 DATA9,CABINETS,10,TROPHIES,10,STUFFED ANIMALS,10,PAINTINGS TO N-S-W,12,CURTAINS TO W-N-E,13,CANOPY BED,14,DESK,15
80 DATAALCOVE,15,BOOKCASES,16,FURNITURE,17,SHELF,17,COLOR COMPUTER,19,COLORED WALLS,20,TABLE,21,CHANDELIER,22,PAINTING,22
90 DATAPAINTING,25,GLASS CASE,25,DESK,27,TABLE,28,CONTAINER,28,DURANGO DAN,29,2 CANS,29,LARGE BOWL,29,MIRROR
91 DATA31,CHAIR,31,WINDOW,31
100 DATAMONITOR,32,BUNKBEDS,33,NOTEBOOK,34,BED,36,CHEST,36,WASHER,37,WINDOW,37,PLANTS,39,WINDOW,39,CLOSET,40
101 DATAPICTURE,40,WINDOW,40,FRONT DOOR,41,CURTAINS,42,WINDOW,42,SCREEN,43,EMBROIDERED SAMPLER,43
110 DATA,4,,,,5,3,,,6,,2,1,8,-1,,2,,,4,3,1,,,,14,-1,,4,,,7,,,1,,-4,,11,9,,-1,,1,,0,13,,,22,,,7,23,,,,24
111 DATA16,,,25,,15,,-1,18,,11,,19,17,,28,2,18,,,,-1,,3,22,,13,,,21,14,-4,,,15,33,,,16,,,,-4,35,27,,,,,-1
112 DATA19,,29,,,-1,,28,21,,31,,0,,32,-1,-1,39,33,-1,24,,,32,,,35,,26,-1,,34,,43,37,,,,38,36,29,,,37
130 DATA32,,-1,,,,,39,,,42,,35,,,41,36,,,0
140 DATATAKE,1,GET,1,EXAMINE,2,LOOK,2,READ,2,OPEN,3,KICK,4,ASK,5,USE,6,UNLOCK,7,TYPE,8,MOVE,9,PUSH,9,HELP,10
141 DATAVERB,11,SAY,12,TOUCH,13,DIAL,14,PLAY,15,INVENTORY,16,GOLD KEY,SILVER KEY,LEAD KEY
150 CLS:FORX=1TO43:READM$:NEXT:FORX=1TO58:READO$(X),O(X):NEXT:FORX=1TO43:READT(1,X),T(2,X),T(3,X),T(4,X):NEXT
180 FORX=1TO20:READV$(X),V(X):NEXT:FORX=1TO3:READK$(X):NEXT:T$(1)="NORTH":T$(2)="SOUTH":T$(3)="EAST":T$(4)="WEST":R=25
220 CLS:RESTORE:FORZZ=1TOR:READM$:NEXT:M$="YOU ARE IN THE "+M$:GOSUB1
230 PRINT:PRINT"YOU SEE:";:FORX=1TO58:IFO(X)=RTHENPRINTTAB(10)O$(X)
250 NEXT
260 IFR=35THENPRINT"A CARVED DOOR"
270 PRINT:PRINT"EXITS LEAD:";:FORX=1TO4:IFT(X,R)<>0ANDT(X,R)<>-2THENPRINTTAB(14)T$(X)
290 NEXT
300 PRINT:INPUT"WHAT WOULD YOU LIKE TO DO NOW ";C$:IFLEN(C$)>1THEN370
320 IFC$="N"THENC$="NORTH"
330 IFC$="S"THENC$="SOUTH"
340 IFC$="E"THENC$="EAST"
350 IFC$="W"THENC$="WEST"
360 GOTO450
370 FORX=1TOLEN(C$):IFMID$(C$,X,1)=" "THENA$=LEFT$(C$,X-1):B$=MID$(C$,X+1):GOTO405
380 NEXT
390 IFC$="NORTH"ORC$="SOUTH"ORC$="EAST"ORC$="WEST"THEN450
400 A$=C$
405 IFA$="GO"THENC$=B$:GOTO450
410 FORX=1TO20:IFV$(X)=A$THENA=V(X):GOTO440
420 NEXT:M$="I DON'T UNDERSTAND":GOTO3
440 ONAGOTO620,720,1450,1560,1570,1610,1700,1930,2070,2170,2260,2320,2390,3110,3190,2190
450 FORX=1TO4:IFC$=T$(X)THENDR=X:GOTO480
460 NEXT
470 M$="YOU CAN'T GO THAT WAY":GOTO3
480 IFT(DR,R)>0ANDT(DR,R)<>100THENR=T(DR,R):GOTO220
490 IFT(DR,R)=0THEN470
500 IFT(DR,R)=-1THENPRINT"A LOCKED DOOR BLOCKS EXIT"
510 IFR=32ANDDR=4THENM$="THERE IS A NOTE ABOVE A COMBINATION LOCK":GOTO3
520 IFR=32ANDDR=1THENM$="THERE IS A SIGN ABOVE IT":GOTO3
530 IFT(DR,R)=0THEN470
540 IFR=31THENM$="THERE IS SOMETHING STRANGE ABOUT THE DOOR":GOSUB1
550 IFR=11ANDDR=2THENM$="THERE IS AN ENGRAVED PLATE ABOVE THE DOOR":GOSUB1
560 IFT(DR,R)<>-2THEN570
570 IFT(DR,R)=-4THENM$="YOU NOW FIND THE DOOR HANDLE BROKEN AND NO WAY BACK":GOTO3
580 IFT(DR,R)=100THEN590
581 GOTO300
590 SOUND20,3:M$="A TRAP DOOR OPENS. YOU FIND YOURSELF BACK AT THE START":GOSUB1:GOSUB4000:RUN
620 IFR=16ANDB$="BOOK"THENT(3,16)=100:DP=1:M$="THE BOOKCASE MOVED REVEALING A DARK PASSAGE EAST":GOTO3
640 IFR=22ANDB$=O$(30)THEN590
650 IFR=12ANDB$="PAINTING"THENGOSUB1072:GOTO2650
660 IFR=28ANDB$=O$(35)ANDCC=0THENM$="YOU NOW HAVE THE CONTAINER":CO=1:CC=1:O$(35)="":GOTO3
670 IFWW=1ANDRIGHT$(B$,4)="SIGN"ANDR=36THENM$="YOU NOW HAVE THE "+B$:WX=1:GOTO3
680 C1=0:FORZZ=1TOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
681 NEXT:IFR=15ANDC1=1ANDLK<>3THEN590
690 IFR=15ANDRIGHT$(B$,3)="KEY"ANDLK=3THENM$="THE KEY IS STUCK IN THE LOCK":GOTO3
700 IFR=39ANDKK=1ANDRIGHT$(B$,3)="KEY"THENM$="YOU NOW HAVE THE SILVER KEY":SK=1:GOTO3
710 GOTO2160
720 IFA$=C$THEN220
740 PRINT"YOU SEE:"
750 IFR=25ANDB$=O$(31)THENM$="A GIRL IN A BLUE DRESS SITTING ON A SWING":GOTO3
760 IFR=40ANDB$=O$(52)THENM$="IT'S A PICTURE OF A GIRL IN A RED DRESS SITTING ON A STOOL":GOTO3
770 IFR=5ANDB$="TABLE"THEN1410
780 IFR=15ANDB$="PENS"THENM$="THERE ARE 11 PENS":GOTO3
785 IFR=1ANDB$="CABINET"THENM$="AN EMPTY CABINET":GOTO3
790 IFR=15ANDB$="PAPER"THEN792
791 GOTO795
792 M$="THE PAPER SAYS: BEHIND THE CHEST IS THE BACK, HIDDEN THERE IS A FACT, "
793 M$=M$+"AN S-CURVED SPINE WITH TWO NOT ONE LINE, UNLESS PERSEVERANCE YOU LACK":GOTO3
795 IFR=2ANDLEFT$(B$,4)="TAPE"THENM$="FOUR RECORDED VIDEO TAPES":GOTO3
800 IFR=15ANDB$="BOOK"THENM$="'7TH SON OF A 7TH SON'":GOTO3
810 IFR=14ANDB$=O$(45)THEN1410
820 IFR=25ANDRIGHT$(B$,4)="CASE"THENM$="YOU SEE 23 DOLLS":GOTO3
830 IFR=32ANDB$=O$(42)THENM$="THE MONITOR HAS A BUTTON":GOTO3
840 IFR=32ANDB$="NOTE"THENM$="R-#A'S  L-#E'S  R-#N'S":GOTO3
850 IFR=32ANDB$="SIGN"THENM$="SAY UNCLOSED SEED- THEN PROCEED- IF YOU LACK- YOU CAN'T GO BACK":GOTO3
860 IFR=5ANDB$="FRUIT"ORB$="BOWL"ANDR=5THENM$="THERE ARE 6 ORANGES IN THE BOWL":GOTO3
870 IFR=40ANDB$=O$(51)THENM$="SOME GOWNS AND SHOES":GOTO3
880 IFR=40ANDB$="SHOES"THENM$="THERE ARE 8 PAIRS OF SHOES":GOTO3
890 IFR=40ANDB$="GOWNS"THENM$="THERE ARE 8 GOWNS":GOTO3
900 IFR=16ANDLEFT$(B$,4)="BOOK"THENM$="A BOOK ENTITLED '43 ADVENTURES'":GOTO3
910 IFR=31ANDB$="DOOR"THEN915
911 GOTO920
915 M$="THERE IS A KEYPAD OFF A PHONE AND INSTRUCTIONS":GOTO3
920 IFR=31ANDB$="INSTRUCTIONS"THENM$="TO OPEN- THE GIRL IN THE BLUE DRESS WAS SITTING ON A #-#-#-#-#":GOTO3
930 IFR=3ANDB$=O$(7)THENM$="YOU SEE SOME CARD GAMES AND A MONOPOLY BOARD":GOTO3
940 IFR=3ANDLEFT$(B$,5)="MONOP"ORR=3ANDRIGHT$(B$,5)="BOARD"THENM$="3 PROPERTIES HAVE HOTELS ON THEM":GOTO3
950 IFR=3ANDLEFT$(B$,5)="PROPE"ORR=3ANDLEFT$(B$,4)="HOTE"THEN955
951 GOTO960
955 M$="THE PROPERTIES ARE KENTUCKY, N. CAROLINA AND ATLANTIC":GOTO3
960 IFR=16ANDB$="BOOK"THENM$="BOOK":GOTO1690
970 IFR=15ANDB$="DESK"ANDLK=3THENPRINT"A LEAD KEY IN THE DESK"
980 IFR=15ANDB$=O$(21)THEN1420
990 IFR=21ANDB$=O$(28)THENM$="IN A FINE COATING OF FLOUR YOU SEE WRITTEN: CURSES, CURTAINS!":GOTO3
1000 IFR=7ANDLEFT$(B$,4)="CLOS"THEN1005
1001 GOTO1010
1005 M$="A SIGN READS: TO OPEN THE EAST DOOR THE CORRECT CLOSET MUST BE OPENED. THE NO. OF THE CLOSET= "
1006 M$=M$+"THE SUM OF THE SAFE COMBO DIVIDED BY THE # OF WORDS IN DAN'S AUTOGRAPH":GOTO3
1010 IFB$="WINDOW"THENM$="YOU SEE AN EAGER CROWD WAITING AT THE FINISH LINE":GOTO3
1020 IFR=4ANDB$=O$(9)THENM$="ON THE DESK IS A PIECE OF PAPER AND AN OLD BLACK PHONE":GOTO3
1030 IFR=4ANDB$="PHONE"THEN1410
1040 IFR=4ANDB$="PAPER"THENM$="THE PAPER SAYS: 86 6736 843 3667 3425 1155":GOTO3
1050 IFR=6ANDRIGHT$(B$,5)="PAPER"THEN1055
1051 GOTO1060
1055 M$="THE BARELY LEGIBLE PAPER SAYS: ...IS ... .NLY PLAC. .N .SA WHER. FOU. STAT.. .EET ....":GOTO3
1060 IFR=9ANDB$=O$(14)THENM$="YOU SEE A SPARROW, A PARAKEET A COCKATOO AND A PARROT":GOTO3
1070 IFR=15ANDB$=O$(22)THEN1075
1071 GOTO1080
1072 PRINT"WHICH PAINTING: N, S, W?";:RETURN
1075 GOSUB2860:M$="AND BELOW IS WRITTEN: THE KEY IS THREE!":GOSUB1
1076 PRINT"TINONGTGTATHKHEEELTSEHIAELDGVKOEELRYDKSKEAEYYYSSSAIAYNYKGRI":GOTO300
1080 IFR=31ANDB$=O$(39)THENM$="THE MIRROR IS CLOUDY":GOTO3
1090 IFR=12ANDLEFT$(B$,4)="PAIN"THEN2590
1100 IFR=22ANDB$=O$(30)THENM$="YOU SEE A PAINTING SLIGHTLY AJAR":GOTO3
1110 IFR=13ANDLEFT$(B$,4)="CURT"THENM$="CLOSED CURTAINS":GOTO3
1120 IFR=12ANDSA=1ANDB$="SAFE"THEN1125
1121 GOTO1130
1125 M$="YOU FIND A PICTURE OF DGO DAN WITH THIS WRITTEN ON IT: "
1126 M$=M$+"VIY MIBLC, ABBY WRALC, TRECES THIN, TROBHOAM LAWL LOVE, DURANGO DAN":GOTO3
1130 IFR=12ANDB$="SAFE"ANDSA=0THENM$="THE SAFE IS LOCKED":GOTO3
1140 IFR=1ANDB$="WALL"THEN1145
1141 GOTO1150
1145 M$="ON THE WALL IS SCRIBBLED: 'SILVER IS THE KEY' AND 'FOR A GOOD TIME CALL 303-259-5880'":GOTO3
1150 IFR=10ANDB$=O$(16)THENM$="YOU SEE 72 TROPHIES":GOTO3
1160 IFR=10ANDLEFT$(B$,4)="ANIM"ORB$=O$(17)ANDR=10THENM$="THERE IS A BEAR, DEER, ELK, LION AND TIGER":GOTO3
1170 IFR=11ANDLEFT$(B$,4)="ENGR"ORB$="PLATE"ANDR=11THENM$="TO UNLOCK THE DOOR ANSWER THE ELK'S QUESTION":GOTO3
1180 IFR=19ANDB$="COMPUTER"THENM$="THE COCO IS ON AND THE CURSOR IS FLASHING":GOTO3
1190 IFLEFT$(B$,4)="UNDE"THENM$="TRY MOVING SOMETHING":GOTO3
1200 IFR=20ANDLEFT$(B$,4)="WALL"THEN1205
1201 GOTO1210
1205 M$="THE WALLS ARE COLORED AS FOLLOWS: N-GREEN S-BLUE E-RED W-YELLOW":GOSUB1:PRINT
1206 M$="A VOICE SAYS- TO ESCAPE, TOUCH ONLY THESE WALLS IN THIS ORDER ATLANTIC, KENTUCKY, N. CAROLINA":GOTO3
1210 IFR=28ANDB$="TABLE"THEN1410
1220 IFCO=1ANDLEFT$(B$,4)="CONT"ORR=28ANDB$=O$(35)THENM$="THE CONTAINER READS: DO NOT OPEN UNTIL ABSOLUTELY NECESSARY":GOTO3
1230 IFR=29ANDB$="CANS"THENM$="THE TWO CANS ARE FILLED WITH WATER":GOTO3
1240 IFR=36ANDB$="CHEST"THENPRINT"THE CHEST HAS FOUR DRAWERS":GOTO3130
1250 IFR=36ANDLEFT$(B$,4)="DRAW"THEN3130
1260 IFR=43ANDRIGHT$(B$,4)="PLER"THEN1265
1261 GOTO1270
1265 PRINT"THE SAMPLER :    ";"=======":PRINTTAB(17)": K T :":PRINTTAB(17)": C S :"
1266 ?TAB(17)": E E :":?TAB(17)": H H :":?TAB(17)": C C :":?TAB(17)":::::::":PRINTTAB(17)"! ! ! !":GOTO300
1270 IFR=17ANDB$="SHELF"THENM$="YOU SEE A KEYBOARD AND A DIARY":GOTO3
1280 IFR=17ANDDI=0ANDB$="DIARY"THEN1285
1281 GOTO1290
1285 M$="THE FIRST PAGE OF THE DIARY SAYS: TO OPEN DOOR TYPE THE FIRST LETTER OF EACH STATE IN ALPHA ORDER...":DI=1:GOTO3
1290 IFDI>0ANDR=17ANDB$="DIARY"THEN1295
1291 GOTO1300
1295 M$="...THE DOOR IS CARVED WITH SHAPES OF STATES. YOU ONLY NEED TO TYPE THESE STATES.":GOTO3
1300 IFR=27ANDPP=1ANDB$="PAPER"THENM$="TYPE DURANGO AND YOU CAN GO":GOTO3
1310 IFR=27ANDB$="DESK"THENM$="YOU SEE A LAMP AND A TYPEWRITER":GOTO3
1320 IFR=35ANDRIGHT$(B$,4)="DOOR"THENM$="THE SOUTH DOOR LOOKS LIKE THIS: - --- --- *--* * -* *** *- -*--*--*":GOSUB1
1330 IFR=34ANDB$=O$(44)THENM$="THE NOTEBOOK SAYS: REMEMBER-- CURSES, CURTAINS":GOTO3
1340 IFR=17ANDB$="DOOR"THEN2440
1350 IFR=39ANDKK=1ANDB$="DOOR"ANDSK<>1THENM$="A SILVER KEY IS IN THE DOOR":GOTO3
1360 IFR=2ANDB$="TABLE"THENM$="ON THE TABLE YOU SEE FOUR VIDEO TAPES":GOTO3
1365 IFR=17ANDLEFT$(B$,4)="TYPE"THEN1410
1370 IFR=27ANDB$="LAMP"THENM$="THE LAMP IS ON":GOTO3
1375 IFB$="DOOR"THEN1410
1380 FORX=1TO58:IFB$=O$(X)THENM=1:GOTO1400
1390 NEXT
1400 IFO(X)<>RANDM=1THENM$="I DO NOT SEE IT HERE":M=0:GOTO3
1410 M$="NOTHING UNUSUAL":GOTO3
1420 PRINT"THE ";B$;
1430 IFL(R)=0THENPRINT" IS LOCKED":GOTO300
1431 PRINT" IS OPEN":GOTO1890
1450 IFR=15ANDB$=O$(21)THEN1540
1460 IFR=7ANDLEFT$(B$,6)="CLOSET"THENINPUT"WHICH CLOSET NO. ";CN:GOTO2980
1470 IFR=13ANDLEFT$(B$,4)="CURT"THEN2720
1480 IFR=35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDC=0ANDLC=1THEN1485
1481 GOTO1490
1485 PRINT"YOU FIND A PIECE OF PAPER -":PRINT"A:*-  E:*  L:*-**  T:-  P:*--*  S:***  O:---  N:-*  Y:-*--":GOTO300
1490 IFB$=O$(35)ANDCO=0THENM$="CONTAINER":GOTO1690
1495 IFLEFT$(B$,4)="CONT"ANDC=1ANDLC=1THENPRINT"YOU SEE AN EMPTY CONTAINER":GOTO300
1500 IFR<>35ANDLEFT$(B$,4)="CONT"ANDCO=1ANDLC=1THEN1505
1501 GOTO1510
1505 PRINT"YOU SEE A PIECE OF PAPER BUT,   BEFORE YOU CAN READ IT IT BURNS IN A FLASH OF SMOKE":C=1:GOTO300
1510 IFR=36ANDLEFT$(B$,4)="DRAW"THEN3050
1520 IFR=42ANDB$=O$(55)THEN590
1530 IFR=1ANDRIGHT$(B$,4)="INET"THENM$="IT'S EMPTY":GOTO3
1540 M$="THE "+B$+" IS LOCKED":GOTO3
1550 M$="YOU CAN'T OPEN THE "+B$:GOTO3
1560 M$="HOW WOULD YOU LIKE IT IF A "+B$+" WERE TO KICK YOU?":GOTO3
1570 IFR=9ANDB$="COCKATOO"THENM$="THE BIRD CACKLES... FREEZE WATER FREEZE WATER...":GOTO3
1580 IFR=10ANDB$="ELK"THEN1585
1581 GOTO1590
1585 M$="THE ELK SAYS- ANSWER THIS AND REMEMBER, HOW MANY CUBIC FEET OF DIRT IN A HOLE 7 X 14 X 3?":GOTO3
1590 IFR=29ANDRIGHT$(B$,3)="DAN"THEN1595
1591 GOTO1600
1595 M$="I WILL UNLOCK THE SOUTH DOOR FOR YOU IF YOU TELL ME HOW TO PUT THE CONTENTS OF THE TWO CANS "
1596 M$=M$+"INTO THE BOWL AND STILL BE ABLE TELL THEM APART":GOSUB1:GOTO3020
1600 M$="THE "+B$+" DOESN'T ANSWER":GOTO3
1610 FORL=1TOLEN(B$)
1611 C1=0:FORZZ=LTOLEN(B$):IFMID$(B$,ZZ,3)="KEY"THENC1=ZZ
1612 NEXT:IFC1>0THEN1620
1613 NEXT
1615 IFLEFT$(B$,3)<>"KEY"THENM$="IT DOES NOT HELP TO USE THE "+B$+" HERE":GOTO3
1620 INPUT"WHICH KEY";Z$:M$=Z$:IFSK=0ANDLEFT$(Z$,4)="SILV"THEN1690
1640 IFLK=0ANDLEFT$(Z$,4)="LEAD"THEN1690
1650 IFGK=0ANDLEFT$(Z$,4)="GOLD"THEN1690
1660 IFLEFT$(Z$,4)="SILV"ANDR=41THEN3300
1665 IFLEFT$(Z$,4)="LEAD"ANDR=15THEN1880
1670 IFCO=1ANDLEFT$(Z$,4)="GOLD"ANDR<>41THENM$="THE CONTAINER IS NOW UNLOCKED":LC=1:GOTO3
1680 M$="IT DOESN'T WORK HERE":GOTO3
1690 M$="YOU DON'T HAVE THE "+M$:GOTO3
1695 PRINT"HOW?":GOTO300
1700 IFR=15ANDB$=O$(21)THEN1810
1710 IFR=39ANDB$="DOOR"THEN1810
1720 IFR=32ANDB$="DOOR"THENINPUT"WHICH DOOR: N OR W";Q$:GOTO2510
1730 IFR=4ANDB$="DOOR"THEN1695
1740 IFR=31ANDB$="DOOR"THEN1695
1750 IFR=12ANDB$="SAFE"THEN2790
1760 IFR=11ANDB$="DOOR"THEN3000
1770 IFR=41ANDB$="DOOR"THENPRINT"HOW":GOTO300
1780 IFCO<>1ANDB$="CONTAINER"THENM$=B$:GOTO1690
1790 IFCO=1ANDB$="CONTAINER"THENPRINT"HOW?":GOTO300
1800 PRINT"HOW? ORDINARY METHODS LIKE USINGKEYS WILL NOT WORK HERE":GOTO300
1810 INPUT"WITH WHAT";Z$
1820 IF(GK=0ANDZ$=K$(1))OR(SK=0ANDZ$=K$(2))OR(LK=0ANDZ$=K$(3))THEN1910
1850 IFR=39ANDSK=1ANDZ$=K$(2)THENPRINT"THE DOOR OPENS":T(3,39)=40:SK=0:KK=1:GOTO300
1860 IFR=15ANDLK=1ANDZ$=K$(3)THEN1880
1870 M$="IT WON'T WORK HERE":GOTO3
1880 M$="THE DESK OPENS REVEALING SOME ":L(R)=1:GOTO1900
1890 M$="THERE ARE SOME "
1900 M$=M$+"PENS, A PIECE OF PAPER AND A BOOK":LK=3:GOTO3
1910 M$=Z$:GOTO1690
1920 L(R)=1:GOTO300
1930 IFR=19ANDB$="RUN"THEN1935
1931 GOTO1940
1935 PRINT"ON THE SCREEN APPEARS:":PRINT"ONE THING IN COMMON":PRINT"YOU CAN SEE"
1936 PRINT"THESE LETTERS HAVE":PRINT"LOOK UNDER ME":PRINT:PRINT"    Z X C V B N M":GOTO300
1940 IFR=17THEND=0:GOTO2000
1950 IFR=27ANDB$="DURANGO"THENM$="THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO3
1960 IFR=27THENINPUT"TYPE WHAT ";Z$:GOTO1980
1970 M$="YOU CAN'T TYPE HERE":GOTO3
1980 IFZ$="DURANGO"THENM$="THE WEST DOOR IS NOW OPEN":T(4,27)=26:GOTO3
1990 M$="SORRY IT DOESN'T HELP TO TYPE" +Z$+" HERE":GOTO3
2000 INPUT"LETTERS PLEASE";I$:IFI$="ACNU"THENM$="THE SOUTH DOOR IS NOW OPEN":T(2,17)=26:GOTO3
2050 IFLEN(I$)>49THENPRINT"YOU DIDN'T HAVE TO TYPE ALL 50!":GOTO590
2060 GOTO2000
2070 IFR=32ANDB$="BUTTON"THEN2075
2071 GOTO2080
2075 CLS:PRINT"A MESSAGE READS:":PRINT:PRINTTAB(15)"A":PRINTTAB(13)"PENNY"
2076 PRINTTAB(11)"SAVED IS A":PRINTTAB(9)"A PENNY EARNED":FORX=1TO4500:NEXT:CLS:GOTO220
2080 IFR=31ANDLEFT$(B$,6)="BUTTON"THEN2910
2090 IFR=31ANDB$="PAD"THEN2910
2100 IFR=22ANDB$=O$(30)THEN590
2110 IFR=12ANDLEFT$(B$,4)="PAIN"THEN650
2120 IFR=19ANDB$="COMPUTER"THENM$="NOTHING UNDER THE COMPUTER":GOTO3
2130 IFR=17ANDB$="KEYBOARD"THENM$="NOTHING HERE":GOTO3
2140 IFR=27ANDB$="TYPEWRITER"THENM$="YOU FIND A PIECE OF PAPER UNDER THE TYPEWRITER":PP=1:GOTO3
2150 IFR=36ANDB$="CHEST"THENM$="YOU FOUND A PLASTIC DOLLAR SIGN TAPED TO THE BACK OF THE CHEST":WW=1:GOTO3
2160 M$="YOU CAN'T "+A$+" THE "+B$:GOTO3
2170 M$="TYPE 'VERB' TO GET A LIST":GOTO3
2190 PRINT"YOU HAVE: ";:IFLK=1THENPRINTTAB(11)"A LEAD KEY"
2210 IFSK=1THENPRINTTAB(11)"A SILVER KEY"
2220 IFGK=1THENPRINTTAB(11)"A GOLD KEY"
2230 IFCO=1THENPRINTTAB(11)"A LARGE CONTAINER"
2240 IFWW=1THENPRINTTAB(11)"$$ DOLLAR SIGN $$"
2250 GOTO300
2251 M$="YOU NOW HAVE THE "+M$+" KEY":GOTO3
2260 CLS:PRINT@8,"*VERB LIST*":PRINT:FORX=1TO20:PRINTV$(X),:NEXT:GOSUB4000:GOTO220
2320 IFR=15ANDB$="RING"THENM$="GOLD":GK=1:GOTO2251
2330 IFR=15ANDB$="KING"THENM$="SILVER":SK=1:GOTO2251
2340 IFR=15ANDB$="SING"THENM$="LEAD":LK=1:GOTO2251
2350 IFR=32ANDB$="UNCLOSED SEED"THENM$="DO NOT TAKE THE STATEMENT WORD FOR WORD, THIS IS A PUZZLE":GOTO3
2360 IFR=32ANDB$="OPEN SESAME"THENPRINT"THE DOOR NORTH DOOR OPENS":T(1,32)=23:GOTO300
2370 IFR=35ANDB$="PLEASE"THENPRINT"THE SOUTH DOOR IS NOW OPEN":T(2,35)=42:GOTO300
2380 M$="IT WON'T HELP TO SAY "+B$:GOTO3
2390 IFR<>20THENM$="IT DOESN'T HELP TO TOUCH THE"+B$:GOTO3
2400 FORX=1TO3:PRINT"NO."X" WHICH WALL?":INPUT"N, S, E, W";Z$(X):NEXT
2430 IFZ$(1)="W"ANDZ$(2)="E"ANDZ$(3)="N"THENPRINT"THE WEST DOOR OPENS":T(4,20)=19:GOTO300
2431 GOTO590
2432 PRINT"COMBINATION LOCK":INPUT"RIGHT";A:INPUT"LEFT";B:INPUT"RIGHT";C:RETURN
2440 CLS:PRINT"THE DOOR IS CARVED LIKE THIS:":FORM=1TO5:PRINTTAB(16)CHR$(138):NEXT:PRINTTAB(7)"ŒŒŒŒŒŒŒŒŒˆŒŒŒŒŒŒŒŒŒ"
2480 FORM=1TO5:PRINTTAB(16)CHR$(138):NEXT:GOTO300
2510 IFQ$="W"THEN2530
2520 IFQ$="N"THEN300
2530 GOSUB2432:IFA=5ANDB=5ANDC=5THENM$="THE DOOR OPENS":T(4,32)=31:GOTO3
2580 GOTO2850
2581 M$="A PAINTING OF A "+M$:GOTO3
2590 GOSUB1072:INPUTQ$:IFQ$<>"N"ANDQ$<>"S"ANDQ$<>"W"THEN2590
2620 IFQ$="N"THENM$="WINDOW WITH SOME VELVET CURATINS":GOTO2581
2630 IFQ$="W"THENM$="BROWN DOG":GOTO2581
2640 M$="SPOTTED COW":GOTO2581
2650 GOSUB1072:INPUTQ$:IFQ$="N"THEN590
2670 IFQ$="W"THENM$="YOU FIND A SAFE":GOTO3
2680 GOTO710
2720 INPUT"WHICH CURTAIN: N, E, W";Q$:IFQ$<>"N"ANDQ$<>"E"ANDQ$<>"W"THENPRINT"NOT A DIRECTION":GOTO2720
2760 IFQ$="N"THEN590
2770 IFQ$="E"THENM$="YOU SEE A SCRALLED MESSAGE: TO OPEN SAFE L-DOLLS, R-SON X SON, L-PENS, R-SHOES":GOTO3
2780 M$="YOU FIND A PASSAGE LEADING WEST":T(4,13)=12:GOTO3
2790 GOSUB2432:INPUT"RIGHT ";D:IFA=23ANDB=49ANDC=11ANDD=16THENM$="THE SAFE OPENS ":SA=1:GOTO3
2850 M$="SORRY WRONG COMBINATION":GOTO3
2860 IFGK=0THENPRINTK$(1)"   ";
2870 IFSK=0THENPRINTK$(2)"   ";
2880 IFLK=0THENPRINTK$(3)"   ";
2890 IFGK>0ANDSK>0ANDLK>0THENM$="THREE EMPTY KEY HOOKS":GOTO3
2900 PRINT:RETURN
2910 INPUT"NUMBERS PLEASE";PP$:P=VAL(PP$):IFP=79464THENM$="THE DOOR OPENS":T(4,31)=30:GOTO3
2970 GOTO3120
2980 IFCN=9THEN2990
2981 GOTO590
2990 T(3,7)=8:M$="CORRECT, THE DOOR IS OPEN":GOTO3
3000 INPUT"ENTER NUMBER PLEASE";Z:IFZ=0THENM$="THE SOUTH DOOR OPENS":T(2,11)=18:GOTO3
3011 GOTO3120
3020 INPUT"YOUR ANSWER IN TWO WORDS";Z$
3030 IFZ$<>"FREEZE WATER"THENM$="SORRY THAT'S NOT RIGHT":GOTO3
3040 M$="CORRECT! DURANGO DAN PULLS A SKELETON KEY FROM HIS JACKET AND OPENS THE DOOR":T(2,29)=38:GOTO3
3050 INPUT"WHICH DRAWER: 1, 2, 3, 4";Z:IFZ=1THENM$="YOU SEE 6 SOCKS, 3 SHIRTS AND 2 BELTS":CD(1)=1:GOTO3
3070 IFZ=2THENCD(2)=2:M$="IT'S EMPTY":GOTO3
3080 IFZ=3THENM$="YOU SEE 2 BLANKETS":CD(3)=3:GOTO3
3090 IFZ=4THENM$="YOU SEE A NUMBER OF VERTABRAE":CD(4)=4:GOTO3
3100 M$="THERE ARE ONLY 4 DRAWERS":GOTO3
3110 IFR=4ANDB$="1155"THENM$="THE EAST DOOR OPENS":T(3,4)=5:GOTO3
3120 M$="SORRY WRONG NUMBER":GOTO3
3130 FORI=1TO4:M$="DRAWER NO."+STR$(I)+" IS "
3150 IFCD(I)<>0THENM$=M$+"OPEN":GOTO3160
3151 M$=M$+"CLOSED"
3160 GOSUB1:NEXT:GOTO300
3190 IFR<>2THENM$="IT DOESN'T HELP TO PLAY THE "+B$+" HERE":GOTO3
3200 IFLEFT$(B$,4)="TAPE"THENPRINT"WHICH TAPE? ";
3210 INPUT"WHICH TAPE";Q:IFQ<1ORQ>4THEN3210
3250 IFQ=1THENM$="SEVERAL EPISODES OF GILLIGAN'S ISLAND":GOTO3
3260 IFQ=2THEN3265
3261 GOTO3270
3265 M$="DGO DAN APPEARS ON SCREEN AND SAYS: YOU ARE HALF WAY THERE SO DON'T DESPAIR. IT'S ABSURD, BUT ASK THE BIRD":GOTO3
3270 IFQ=3THENM$="THE MOVIE STAR WARS":GOTO3
3280 IFQ=4THENM$="SEVERAL EPISODES OF I LOVE LUCY":GOTO3
3300 IFWX=1THEN3305
3301 GOTO3310
3305 M$="CONGRATULATIONS YOU MADE IT THROUGH THE SILVERTON HOUSE WITH THE DOLLAR SIGN.":GOSUB1
3306 M$="YOU LEAVE DURANGO WITH RICH REWARDS. YOU TRULY ARE A GREAT ADVENTURER":GOSUB1:END
3310 M$="YOU MADE IT! HOWEVER YOU HAVE BEEN DISQUALIFIED SINCE YOU DIDN'T RETURN WITH THE DOLLAR SIGN.":GOSUB1
3311 END
3400 PRINTTAB(7)"SILVERTON ADVENTURE":PRINT@64,;:M$="AS A TOP ADVENTURER YOU'VE BEEN INVITED TO THE FIRST DURANGO PUZZLERS "
3410 M$=M$+"ADVENTURE GAME.":GOSUB1:M$="YOUR GOAL IS SIMPLE-- ENTER THE SILVERTON HOUSE AND FIND A DOLLAR SIGN HIDDEN WITHIN "
3420 M$=M$+"AND EXIT.":GOSUB1:PRINT@352,"GOOD LUCK!":M$="P.S. A PIECE OF PAPER MIGHT COME IN HANDY.":GOSUB1:GOSUB4000:GOTO150
4000 PRINT@486,;:INPUT"HIT ENTER TO BEGIN";M$:RETURN
REM ORIGINAL TRS-80 COCO VERSION BY
REM JOHN AND PAT EVEREST
