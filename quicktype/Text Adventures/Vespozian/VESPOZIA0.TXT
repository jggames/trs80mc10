
Bloc 1:
BASIC program 
Name...:'VESPOZIAN '
Lenght.:23253 b.

Start line: 7000.
8 BORDER 0: PAPER 0: INK 7: BRIGHT 0: FLASH 0: CLS 
10 DIM L$(15,25): FOR I=1 TO 15: READ L$(I): NEXT I
11 DIM B$(15,32): FOR I=1 TO 15: READ B$(I): NEXT I
12 DIM O$(19,15): FOR I=1 TO 19: READ O$(I): NEXT I
13 DIM R$(12,14): FOR I=1 TO 12: READ R$(I): NEXT I
14 DIM E$(15,4): FOR I=1 TO 15: READ E$(I): NEXT I
15 DIM D$(15,4): FOR I=1 TO 15: READ D$(I): NEXT I
16 DIM P(19): FOR I=1 TO 19: READ P(I): NEXT I
17 DIM C(19): FOR I=1 TO 19: READ C(I): NEXT I
18 LET PQ=0: LET GR=0: DIM Q$(5,32): DIM U$(1,14)
19 LET F$="": LET G$="": LET H$="": LET I$="": LET J$=""
20 LET X$="": LET Y$="": LET A$="NOTHING": LET S$="": LET K1=1: LET K2=1
25 FOR I=1 TO 4: LET Q$(I)="": NEXT I
30 LET IN=0
50 LET JE=0: LET PN=2: LET PL=PN: LET CT=0: LET JM=-1: LET PV=6: LET CY=143: LET PF=600: LET CD=2: LET K$="A THOUGHT IS FRAMED IN YOUR MIND": LET ST=1: LET PI=999: LET SF=1
60 LET M$="TAKDROLIFLOOEXAOPEUNLLOCCLOWEAREMINSRAIPREKILSLE"
70 LET N$="PORSTAFORAFTUP DOWOUTBUNSOUNOREASWESBULDOOCRAVES"
80 LET P$="PSFAUDOBTNEWLRCV"
90 LET T$="SHUDEVPOLCRAASBSPACONCYLTONBEZGRAMAC005001002003004KEYROCDISPILDOOBUTAROSCRBUNSHE"
100 IF PN=6 THEN IF PL=10 OR P(10)=99 THEN LET P(10)=99: LET P(11)=99: GO TO 103
102 LET P(10)=6: LET P(11)=6
103 IF P(8)=10 OR (P(7)=10 AND C(7)>2) THEN LET SF=1: GO TO 105
104 LET SF=0
105 IF PV=4 AND P(3)=99 THEN LET P(3)=5
106 IF PN=3 AND C(1)=-4 AND C(5)<5 THEN GO SUB 3530: GO TO 6050
107 IF C(7)=3 AND P(7)=10 THEN LET C(7)=4: LET PF=PF+2000: LET Q$(3)="NEW FUEL LOADED"
108 LET CT=CT+1: IF ST=1 THEN LET JE=JE+1
109 IF JE<25 THEN GO TO 113
110 IF ST=1 THEN LET JE=JE+1: IF JE>24 THEN LET JE=0: LET PV=PV+JM: IF PV=CD THEN GO TO 112
111 CLS : PRINT INK 2; BRIGHT 1;"VESPOZIAN PASSING ---": PRINT : PRINT INK 3;R$(PV): PAUSE 80: GO TO 113
112 LET ST=2: LET PF=PF-100: CLS : PRINT INK 2; BRIGHT 1;"VESPOZIAN IN ---": PRINT : PRINT INK 3;R$(PV);" ORBIT": PAUSE 80
113 IF CT>200 THEN LET CT=1: LET CY=CY+1
114 IF CD=PV THEN LET ST=2
115 IF ST=2 THEN LET W$="ORBITING": GO TO 117
116 LET W$="BEARING ON"
117 IF PN=10 AND C(5)<5 THEN GO TO 6010
118 IF PN=14 AND A$="GO NORTH" THEN GO TO 6055
120 IF PV=3 AND PN=5 AND P(13)=99 THEN LET P(13)=5
125 IF P(3)=14 THEN LET P(3)=88: LET PI=CY: LET Q$(3)="POLYPS VANISH INTO ROCKS"
126 IF CY>PI+19 THEN LET P(19)=14: LET PI=CY
129 IF P(2)=PN AND (PN=9 OR PN=14) AND (P(19)<>14 OR P(7)<>14) THEN LET Q$(3)="SWITCH STARTS MOTOR. DRILL": LET Q$(4)="ENGAGES AND SPRAYS DUST!"
130 IF P(19)=14 AND P(2)=14 AND P(7)=14 AND C(7)=2 THEN LET P(19)=88: LET O$(7)="FUEL CONTAINER": LET O$(8)=" ": LET C(7)=3: LET Q$(3)="SWITCH STARTS MOTOR. DRILL": LET Q$(4)="ENGAGES AND FILLS CONTAINER"
135 IF P(18)=55 THEN LET C(4)=3
140 IF C(5)>4 AND C(5)<7 AND (PN=2 OR PN=7 OR PN=8 OR PN=11) THEN LET IG=IG+1: GO TO 145
141 LET IG=0
145 IF IG>0 THEN LET P(11)=P(1): IF IG=5 THEN GO SUB 3530: GO TO 6070
146 GO TO 170
147 IF P(11)=PN THEN IF INT (RND*7+1)=3 THEN LET Q$(1)="EVER GET THE FEELING . .?"
150 IF P(5)<>88 AND P(5)<>PN AND P(5)<>55 AND P(5)<>12 AND P(5)<>66 THEN LET P(5)=88: LET Q$(1)="STRANGE FEELING . .": GO TO 160
155 IF P(6)<>PN AND P(6)<>88 AND P(6)<>55 AND P(6)<>12 AND P(6)<>66 THEN LET P(6)=88: LET Q$(1)="STRANGE FEELING . ."
160 IF C(4)=2 AND P(18)<>55 AND P(18)<>66 AND P(18)<>99 THEN LET P(18)=88: LET Q$(1)="STRANGE, SOMETHING SEEMS MISSING": LET P(18)=3
170 IF P(11)=2 AND PN=2 AND K1=14 THEN GO SUB 3530: GO TO 6080
175 IF PN=7 OR PN=8 OR PN=11 OR PN=2 THEN IF INT (RND*3+1)=3 THEN LET P(11)=PN: GO TO 180
176 IF PN=6 AND PL<>10 THEN GO TO 180
177 LET P(11)=88
180 IF P(18)=55 AND C(18)=4 AND P(11)=11 AND (PN=12 OR PN=11) THEN GO SUB 3530: GO TO 6100
181 IF P(18)=55 AND C(18)=4 AND P(11)=11 THEN GO SUB 3530: GO TO 6100
185 IF C(1)=3 AND PN<>2 AND PN<>3 THEN GO SUB 3530: GO TO 6110
200 FOR I=1 TO LEN E$(PN)
210 IF E$(PN)(I)="P" THEN LET S$=S$+"PORT. "
220 IF E$(PN)(I)="S" THEN LET S$=S$+"S'BOARD. "
230 IF E$(PN)(I)="F" THEN LET S$=S$+"FOR'D. "
240 IF E$(PN)(I)="A" THEN LET S$=S$+"AFT. "
250 IF E$(PN)(I)="U" THEN LET S$=S$+"UP. "
260 IF E$(PN)(I)="D" THEN LET S$=S$+"DOWN. "
270 IF E$(PN)(I)="O" THEN LET S$=S$+"OUT. "
280 NEXT I
330 CLS : PRINT INK 7;"I AM": PRINT INK 6;L$(PN): PRINT B$(PN): PRINT : IF LEN S$>0 THEN PRINT INK 2;"WAYS:-- "; INK 5;S$
335 IF PN=9 OR PN=14 THEN PRINT INK 2;"WHICH WAY ?"
340 LET II=1: PRINT : PRINT INK 7;"VISIBLE:- ": IF PN=15 AND K2<>21 THEN GO TO 345
342 FOR I=1 TO 19: IF P(I)=PN THEN PRINT INK 5;O$(I)
344 NEXT I
345 IF PN=5 THEN PRINT : PRINT INK 3;"VESPOZIAN"
360 PRINT : IF A$<>"" THEN PRINT INK 7;"-----YOU SAID "; INK 6;A$: PRINT : FOR I=1 TO 4: IF Q$(I)(1)<>" " THEN PRINT INK 5;Q$(I)
365 FOR I=1 TO 4: IF Q$(I)<>"" THEN PRINT INK 5;Q$(I)
370 NEXT I
375 IF Q$(2)(1 TO 2)="OK" THEN BEEP .05,10
380 PRINT : PRINT INK 7;"-----WHAT NOW";
390 LET PL=PN: FOR I=1 TO 5: LET Q$(I)="": NEXT I: LET A$="": LET G$="": LET H$="": LET I$="": LET J$="": LET S$="": INPUT A$: IF CODE (A$(1))>90 THEN GO SUB 8050
400 LET J=1: IF LEN A$<3 THEN GO TO 4000
401 IF A$(1)<>" " THEN LET J=J+1: LET G$=G$+A$(1): LET A$=A$(2 TO ): GO TO 401+2*(A$="")
402 LET I$=A$(2 TO )
403 LET H$=(G$+"   ")(1 TO 3)
404 LET J$=(I$+"   ")(1 TO 3)
405 IF I$="" THEN LET J=0
406 LET A$=G$+" "+I$
410 IF H$="INV" THEN GO TO 460
411 IF H$="WAI" THEN GO TO 500
412 IF H$="QUI" OR H$="HEL" THEN GO TO 600
414 IF G$="GO" OR H$="GET" THEN GO TO 425
415 LET X$=M$: LET Y$=H$: GO SUB 3500: IF J=0 THEN LET Q$(2)="I DON'T KNOW HOW TO "+G$: GO TO 100
416 LET K1=INT ((J-1)/3+1)
417 LET X$=T$: LET Y$=J$: GO SUB 3500
419 IF J=0 THEN LET Q$(2)="WHAT IS A "+I$+"?": GO TO 100
420 LET K2=INT ((J-1)/3)+1
421 GO TO (K1=1)*430+(K1=2)*450+(K1=3)*700+(K1=4)*800+(K1=5)*800+(K1=6)*900+(K1=7)*1000+(K1=8)*1100+(K1=9)*1200+(K1=10)*1300+(K1=11)*1400+(K1=12)*1500+(K1=13)*700+(K1=14)*1600+(K1=15)*1700+(K1=16)*1800
425 LET X$=N$: LET Y$=J$: GO SUB 3500: IF J=0 THEN GO TO 4010
426 LET X$=E$(PN): LET Y$=P$((J-1)/3+1): GO SUB 3500: IF J=0 THEN GO TO 4010
427 LET PN=CODE (D$(PN)(J))-64: GO TO 4020
430 IF K2=20 THEN GO TO 4030
431 IF P(11)=PN THEN GO TO 6080
432 IF K2>19 THEN GO TO 4070
433 IF P(K2)=55 THEN GO TO 4040
434 IF P(K2)<>PN THEN GO TO 4050
435 IF C(K2)=-2 THEN GO TO 4000
436 IF C(K2)=-1 THEN GO TO 4060
440 IF (K2=8 OR (K2=7 AND C(7)=3)) AND P(9)<>55 THEN LET Q$(2)="TOO HOT TO HANDLE": GO TO 100
441 IF IN>3 THEN LET Q$(2)="I'M OVERLOADED ALREADY!": GO TO 100
442 LET IN=IN+1: LET P(K2)=55: GO TO 4020
450 IF K2>19 THEN GO TO 4070
451 IF P(K2)<>55 THEN GO TO 4070
452 IF K2=2 AND (PN<>9 AND PN<>14) THEN GO SUB 3530: GO TO 6060
453 LET IN=IN-1: IF PN=15 THEN LET P(K2)=66: LET Q$(3)="SLIPPED IT UNDER PILLOW": GO TO 4020
454 LET P(K2)=PN
455 GO TO 4020
460 PRINT INK 3;''"I AM CARRYING: "
461 IF IN=0 THEN LET Q$(2)="NOTHING": GO TO 100
462 LET J=1
463 FOR I=1 TO 19
464 IF P(I)=55 THEN PRINT O$(I)
470 NEXT I: PAUSE 200: GO TO 100
500 LET CT=CT+15: IF ST=1 THEN LET JE=JE+15
510 GO TO 4020
600 LET Q$(2)="LOOK AROUND AND": LET Q$(3)="EXAMINE THINGS. TRY WORDS": LET Q$(4)="LIKE INSERT, LIFT, WAIT ETC.": GO TO 100
700 IF K2<>21 OR PN=15 THEN GO TO 4000
701 LET Q$(2)="NOTHING!": FOR I=1 TO 19: IF P(I)=66 THEN LET P(I)=15: LET Q$(2)="IT'S STILL THERE!": GO TO 100
702 NEXT I: GO TO 100
800 IF K2<>24 THEN GO TO 812
801 IF PN=8 THEN LET Q$(2)="I SEE BUTTON MARKED": LET Q$(3)="-THINK CONTROL-": GO TO 100
802 IF PN=11 AND E$(11)(1 TO 3)="PD " THEN LET E$(11)(1 TO 3)="PDL": LET Q$(2)="WOW! A HIDDEN BULKHEAD": GO TO 100
805 IF PN=10 AND E$(10)="S   " THEN LET E$(10)="SC  ": LET P(4)=10: LET Q$(2)="AHA!": GO TO 100
806 IF PN=7 THEN LET Q$(2)="COURSE DATA ON SCREEN": GO TO 100
807 IF PN=3 THEN IF C(1)=-3 THEN LET Q$(3)="INNER DOOR OPEN.": LET Q$(4)="OUTER DOOR CLOSED.": GO TO 4020
808 IF PN=3 THEN IF C(1)=-4 THEN LET Q$(3)="INNER DOOR CLOSED.": LET Q$(4)="OUTER DOOR OPEN.": GO TO 4020
812 IF K2=20 THEN GO TO 4030
813 IF K2=2 THEN IF P(2)<>55 AND P(2)<>PN THEN GO TO 4030
814 IF K2=2 THEN LET Q$(2)="IT HAS HOLLOW CYLINDRICAL CUTTER": LET Q$(3)="AND SENSITIVE PRESSURE SWITCH": GO TO 100
815 IF (PN=3 OR PN=2) AND K2=23 THEN LET Q$(2)="SIGN -AIRLOCK CONTROL-": GO TO 100
820 IF K2<18 AND K2>12 THEN IF P(K2)<>55 THEN GO TO 4070
821 IF K2<18 AND K2>12 THEN GO TO (K2=13)*870+(K2=14)*871+(K2=15)*872+(K2=16)*873+(K2=17)*874
825 IF K2=20 AND PN=15 THEN LET Q$(3)="LOOKS STRANGELY LUMPY . .": GO TO 4020
826 IF K2=25 AND PN=7 THEN GO TO 4500
830 LET Q$(3)="NOTHING SPECIAL": GO TO 4020
870 LET Q$(2)="LABEL -YUSES-": GO TO 100
871 LET Q$(2)="LABEL -POLYPS-": GO TO 100
872 LET Q$(2)="LABEL -FUNUS-": GO TO 100
873 LET Q$(2)="LABEL -CONFIDENTIAL-": GO TO 100
874 LET Q$(2)="LABEL -BLOODLINE-": GO TO 100
900 IF K2<>22 THEN GO TO 4000
901 IF PN<>11 THEN GO TO 4050
902 IF P(11)=11 AND PN=11 THEN GO TO 6100
904 IF C(18)=2 THEN LET Q$(2)="LOCKED!": GO TO 100
905 IF C(18)=4 THEN LET Q$(2)="ALREADY OPEN": GO TO 100
906 IF P(8)=11 THEN GO TO 6000
910 LET C(18)=4: LET E$(11)="PD R": GO TO 4020
1000 IF K2<>22 THEN GO TO 4000
1001 IF PN<>11 THEN GO TO 4050
1002 IF C(18)>2 THEN GO TO 4090
1003 IF P(18)<>55 THEN GO TO 4060
1004 LET C(18)=3: GO TO 4020
1100 IF K2<>22 THEN GO TO 4000
1101 IF PN<>11 THEN GO TO 4050
1103 IF P(11)=11 AND PN=11 THEN GO TO 6080
1104 IF C(18)<>3 OR P(18)<>55 THEN GO TO 4060
1106 LET C(18)=3: GO TO 4020
1200 IF K2<>22 THEN GO TO 4000
1201 IF PN<>11 THEN GO TO 4050
1203 IF P(11)=11 AND PN=11 THEN GO TO 6080
1204 IF C(18)<>4 THEN GO TO 4060
1206 LET C(18)=3: LET E$(11)=E$(11)( TO 3): GO TO 4020
1300 IF K2<>5 AND K2<>6 THEN GO TO 4000
1301 IF P(K2)<>55 THEN GO TO 4070
1302 IF C(5)=K2 THEN LET Q$(2)="WHAT'S THIS I'M WEARING THEN?": GO TO 100
1303 IF C(5)>4 THEN LET Q$(2)="WEARING "+O$(K2): GO TO 100
1304 LET C(5)=K2: LET O$(K2)=O$(K2)+" (WORN)": GO TO 4020
1400 IF (K2<>5 AND K2<>6) OR C(5)=2 THEN GO TO 4080
1401 LET C(5)=2: LET O$(K2)=O$(K2): GO TO 4020
1500 IF K2=20 THEN GO TO 4030
1501 IF K2<13 OR K2>17 THEN GO TO 4000
1502 IF P(K2)<>55 THEN GO TO 4070
1503 IF PN<>7 THEN LET Q$(2)="NO COMPUTER TO TAKE IT": GO TO 100
1505 IF P(11)=7 AND K2>12 AND K2<18 THEN GO SUB 3530: GO TO 6090
1510 CLS : PRINT INK 2; BRIGHT 1;O$(K2): PRINT INK 3; BRIGHT 1;"SPINS IN ITS DRIVE . .": FOR I=1 TO 150: NEXT I: CLS : GO TO (K2=13)*1550+(K2=14)*1561+(K2=15)*1563+(K2=16)*1565+(K2=17)*1567
1550 INK 5: PRINT "-YUSES SYSTEM-": PRINT : PRINT "STAR SYSTEM ADJACENT TO FUNUS": PRINT "IT HAS 3 PLANETS.": PRINT : PRINT R$(11): PRINT "- BALL OF FIRE -": PRINT : PRINT R$(10): PRINT " - INHABITED BY MINING COLONY,": PRINT "NO FINDS.": PRINT 
1560 PRINT R$(9): PRINT " - REMOTE DEAD PLANET -": GO TO 1569
1561 PRINT "-POLYPS-": PRINT "FREQUENT GALAXY IN MILLIONS": PRINT "AROUND ENERGY RICH PLANETS.": PRINT "REGENERATE EVERY 20 YEARS.": PRINT "ACTIVE POLYPS CAN TURN DEAD": PRINT "PLANET INTO GREENHOUSE EFFECT": PRINT "PLANET, RICH IN STELLAR ENERGY"
1562 PRINT "AFTER 20 YEARS . .": GO TO 1569
1563 PRINT "-FUNUS SYSTEM-": PRINT "STAR WITH 7 PLANETS.": PRINT "IN ORDER FROM FUNUS -": PRINT : PRINT R$(2);" -MOLTEN ROCK": PRINT R$(3);" -UNINHABITED": PRINT R$(4);" -GR'NHOUSE EFFECT": PRINT R$(5);" -INHABITED"
1564 PRINT R$(6): PRINT R$(7): PRINT " - (BOTH ICY WASTES) -": PRINT R$(8): PRINT " - (LUMP OF ROCK) -": GO TO 1569
1565 PRINT "-CAPTAIN'S LOG-": PRINT "BEARING TO DOCK WITH BLOODLINE": PRINT "STARGATE IN GYRATES ORBIT.": PRINT "MISSION ACCOMPLISHED - D'TAAN'S": PRINT "EXPERIMENTS CONFIRM POLYP": PRINT "THEORY. PITY BLOODLINE WILL": PRINT "BENEFIT!"
1566 PRINT "GLAD TO GET SHOT OF THIS": PRINT "CREW!. SUSPECT GRAKTA IS": PRINT "BLOODLINE SPY. MACHEN,": PRINT "THE TIAITHEN LIZARD GIVES ME": PRINT "THE CREEPS. CAN'T GET RID OF": PRINT "HIM - THE ONLY PILOT ABOARD!": GO TO 1569
1567 PRINT "-BLOODLINE-": PRINT "TYRANNICAL EMPIRE KEEPING": PRINT "CONTROL ON ENERGY SUPPLIES": PRINT "FROM GREENHOUSE PLANETS -": PRINT "ON WHICH INTERSTELLAR TRAVEL": PRINT "DEPENDS": PRINT "STARGATE IN FUNUS SYSTEM"
1568 PRINT "CAN SUMMON CRUISERS TO": PRINT "QUELL REBELS."
1569 INK 7: FOR I=1 TO 1000: NEXT I: LET Q$(2)="YOU REMOVE THE DISK . .": GO TO 100
1600 IF K2<>23 THEN GO TO 4020
1602 IF P(11)=2 AND PN=2 THEN GO TO 6000
1603 IF PN=8 THEN IF P(11)=8 THEN GO TO 6000
1604 IF PN=8 THEN LET Q$(2)="MACHEN, THE LIZARD PILOT": LET Q$(3)="RESTRAINS YOU": GO TO 100
1606 CLS : IF PN<>13 THEN GO TO 1661
1607 IF PF<1 THEN GO TO 6020
1608 GO SUB 3515
1609 PRINT INK 5;"HEADING,(H)": PRINT : PRINT INK 3;"CRYOGEN,(C)": PRINT : PRINT INK 4;"LASER,(L)": PRINT : PRINT INK 6;"END OF INPUT,(E)"
1612 LET V$=INKEY$: IF V$="H" THEN GO TO 1618
1613 IF V$="C" THEN GO TO 1633
1614 IF V$="L" THEN GO TO 1639
1615 IF V$="E" THEN GO TO 4020
1616 GO TO 1612
1618 LET J=0: GO SUB 3515: PRINT INK 2; BRIGHT 1;"CURRENTLY:-": PRINT INK 6;W$: PRINT INK 6;R$(CD): PRINT : PRINT INK 5;"NEW DESINATION?": INPUT U$(1)
1620 FOR I=1 TO 12: IF U$(1)=R$(I) THEN LET J=I
1621 NEXT I: IF J=0 THEN PRINT INK 2; BRIGHT 1;U$(1);" NOT ON DATABASE": GO SUB 3527: GO TO 1606
1624 IF J=CD THEN GO TO 1606
1625 IF J>PV THEN LET JM=1: GO TO 1627
1626 LET JM=-1
1627 INK 6: CLS : LET ST=1: LET JE=0: LET CD=J: LET PF=PF-100: PRINT : PRINT "COURSE CORRECTION APPLIED": PRINT : PRINT "NEW BEARING:-": PRINT R$(CD): PRINT : PRINT "FUEL:- ";PF;" PICORATH": PAUSE 200
1628 INK 7: IF PF<1 THEN GO TO 6020
1629 GO TO 1606
1633 GO SUB 3515: INK 5: PRINT : PRINT "CRYOGENIC CONTROL": PRINT "ENERGY TO FREEZE CREW - 10 PR/CY": PRINT : PRINT "HOW MANY CLONE-YEARS FREEZE?": INPUT F$: IF VAL (F$)*10>PF THEN PRINT : PRINT "FUEL DEFICIENCY.": PRINT "INTERLOCK OVERRIDE": PRINT INK 2; BRIGHT 1;"* OPERATION ABORTED *": GO SUB 3525: INK 7: GO TO 1606
1636 LET PF=PF-10*VAL (F$): LET CY=CY+VAL (F$): PRINT INK 3; BRIGHT 1; FLASH 1;"STAND BY": FOR I=1 TO 100: NEXT I: GO SUB 3530: LET PN=15: LET Q$(2)="YAWN . . !": GO TO 100
1639 GO SUB 3515: PRINT INK 4;"LASER CONTROL": PRINT 
1642 PRINT INK 4;"TARGET?": INPUT U$(1): IF U$(1)="STARGATE      " THEN GO TO 1664
1643 LET J=0: FOR I=1 TO 12: IF R$(I)=U$(1) THEN LET J=1: LET I=12: NEXT I
1644 NEXT I
1645 IF J=0 THEN PRINT INK 2; BRIGHT 1;U$(1);" NOT ON DATABASE": GO SUB 3527: GO TO 1606
1646 PRINT INK 2; BRIGHT 1;"TARGETED ON ";U$(1): FOR I=1 TO 150: NEXT I: IF PF<1000 THEN PRINT INK 3; FLASH 1;"FUEL DEFICIENCY"
1647 IF U$(1)="STARGATE      " THEN GO TO 1650
1649 IF J<>PV THEN PRINT "OUT OF RANGE": GO TO 1652
1650 IF U$(1)="STARGATE      " AND PV<>3 THEN PRINT INK 6;U$(1);" OUT OF RANGE": GO TO 1652
1651 GO TO 1655
1652 PRINT : PRINT INK 2; BRIGHT 1;"COMPUTER OVERRIDE": PRINT INK 2; BRIGHT 1;"* OPERATION ABORTED *": GO SUB 3525: GO TO 1606
1655 GO SUB 3540: IF U$(1)<>"STARGATE      " THEN GO TO 6030
1656 GO TO 6040
1661 IF PN=2 THEN GO TO 1692
1664 IF PN=3 THEN GO TO 1692
1665 IF PN<>4 THEN GO TO 4050
1667 GO SUB 3515: PRINT INK 5;"THINK 'DESTINATION' OR 'END'": INPUT U$(1): IF U$(1)="VESPOZIAN    " THEN IF P(1)=3 THEN PRINT INK 3; BRIGHT 1;"SHUTTLE AT ESPOZIAN": PRINT : PRINT INK 2; BRIGHT 1;"* MISSION ABORTED *": GO SUB 3525: GO TO 100
1668 IF U$(1)="VESPOZIAN     " THEN PRINT INK 6;"IN FLIGHT VESPOZIAN": GO SUB 3520: PRINT INK 4;"DOCKING": GO SUB 3520: LET P(1)=3: LET D$(4)="C   ": GO TO 100
1672 IF U$(1)(1)="E" THEN GO TO 4020
1675 LET J=0: FOR I=1 TO 12: IF U$(1)=R$(I) THEN LET J=I
1678 NEXT I: IF J=0 THEN PRINT INK 2; BRIGHT 1;F$;"NOT ON DATABASE": GO SUB 3527: GO TO 100
1681 IF J<>PV THEN PRINT INK 6;U$(1);"OUT OF RANGE": GO SUB 3520: GO TO 100
1684 IF ST=1 THEN PRINT INK 3;"VESPOZIAN NOT IN ": PRINT INK 5;U$(1);" ORBIT": PRINT : PRINT  INK 2; BRIGHT 1;"* OPERATION ABORTED *": GO SUB 3525: GO TO 100
1687 INK 5: IF J<>9 THEN PRINT "CONDITIONS ON ";U$(1): PRINT "NOT SUITABLE FOR SHUTTLE": PRINT "LANDING": PRINT : PRINT INK 2; BRIGHT 1;"* OPERATION ABORTED *": GO SUB 3525: INK 7: GO TO 100
1690 IF C(1)<>-4 THEN PRINT INK 4;"SHUTTLE BAY DOORS CLOSED": GO SUB 3520: PRINT INK 2; BRIGHT 1;"* OPERATION ABORTED *": GO SUB 3525: GO TO 100
1691 PRINT INK 3;"HEADING - ";U$(1): GO SUB 3520: PRINT INK 5;"LANDED ON ";U$(1): LET P(1)=9: LET D$(4)="I   ": GO SUB 3520: GO TO 100
1692 IF PN=2 THEN IF C(1)=-2 THEN LET C(1)=-3: LET B$(2)="BLUE BUTTON BY OPEN BULKHEAD": LET E$(2)="PFL ": GO TO 4020
1693 IF PN=2 THEN IF C(1)=-3 THEN LET C(1)=-2: LET B$(2)="BLUE BUTTON BY CLOSED BULKHEAD": LET E$(2)="PF  ": GO TO 4020
1694 IF PN=3 AND C(1)=-3 THEN LET C(1)=-4: LET E$(3)="OT  ": LET D$(3)="ED  ": LET Q$(3)="INNER DOOR CLOSES": LET Q$(4)="OUTER DOOR OPENS": GO TO 4020
1695 IF PN=3 AND C(1)=-4 THEN LET C(1)=-3: LET E$(3)="TP  ": LET D$(3)="DB  ": LET Q$(3)="OUTER DOOR CLOSES": LET Q$(4)="INNER DOOR OPENS": GO TO 4020
1698 GO TO 4050
1700 LET Q$(2)="NO CHANCE-TOO RISKY": GO TO 100
1800 LET Q$(2)="I'M NOT TIRED": GO TO 100
3500 LET J=0: FOR I=1 TO LEN X$ STEP LEN Y$
3505 IF Y$=X$(I TO I-1+LEN Y$) THEN LET J=I: LET I=LEN X$+3: RETURN 
3510 NEXT I: RETURN 
3515 CLS : PRINT INK 6;K$: PRINT : RETURN 
3520 FOR I=1 TO 300: NEXT I: RETURN 
3525 FOR O=1 TO 6: BEEP .1,7: BEEP .1,2: NEXT O: PAUSE 150: RETURN 
3526 BEEP 3,20: RETURN 
3527 FOR O=1 TO 10: BEEP .05,6: BEEP .05,10: NEXT O: RETURN 
3530 FOR I=1 TO 15: CLS : PAPER 0: BORDER 0: PRINT : PAUSE 5: PAPER 7: BORDER 7: PRINT : NEXT I: PAPER 0: BORDER 0: INK 7: CLS : RETURN 
3540 FOR I=1 TO 10: CLS :\{17}\{0} PAPER 0: BORDER 0: PRINT : PAUSE 5: PAPER 2: BORDER 2: PRINT : NEXT I:\{17}\{0} PAPER 0: BORDER 0: INK 7: CLS : RETURN 
3550 RETURN 
3560 GO TO 4020
4000 LET Q$(2)="IMPOSSIBLE!": GO TO 100
4010 LET Q$(2)="I CAN'T GO "+I$: GO TO 100
4020 LET Q$(2)="OK": GO TO 100
4030 LET Q$(2)="REFERENCE DISC BY ITS NUMBER": GO TO 100
4040 LET Q$(2)="I'M ALREADY CARRYING IT": GO TO 100
4050 LET Q$(2)="I DON'T SEE IT HERE": GO TO 100
4060 LET Q$(2)="I CAN'T YET!": GO TO 100
4070 LET Q$(2)="I'M NOT CARRYING IT": GO TO 100
4080 LET Q$(2)="YOU MUST BE JOKING!": GO TO 100
4090 LET Q$(2)="OK-NOTHING HAPPENS": GO TO 100
4100 LET Q$(2)="CAP'N WINKS AT ME AND": LET Q$(3)="GLANCES AT GRAKTA": GO TO 100
4110 LET Q$(2)="HUH?": GO TO 100
4500 INK 5: CLS : PRINT "CLONE YEAR:";CY;"   ZONETIME:";CT: PRINT : PRINT "--------------------------------"
4501 PRINT : PRINT "VESPOZIAN ";W$: PRINT R$(CD): IF ST=1 THEN PRINT "ETA:- ";CT+ABS (CD-PV)*25-JE
4505 PRINT : PRINT "NEAREST BODY:- ";R$(PV): IF PV=2 THEN PRINT "BLOODLINE STARGATE"
4510 PRINT : PRINT "FUEL:- ";PF;" PICORATH": PRINT : PRINT "LASER:- ";: IF PF<1000 THEN PRINT "INOPERATIVE": GO TO 4520
4511 PRINT "OPERATIVE"
4520 PRINT : PRINT "--------------------------------"
4521 INK 7: PRINT : PRINT INK 4;"THINK CONTROL REPORTING": FOR I=1 TO 550: NEXT I: GO TO 4020
5000 DATA "IN THE CREWS QUARTERS","IN THE RESEARCH LAB BY","IN THE SHUTTLE BAY","IN A SHUTTLE","OUTSIDE THE HULL","IN THE CAPTAINS CABIN","IN THE NAVIGATION AND","ON THE BRIDGE","ON YUSES FAR UNDER","AS VESPOZIANS FUEL CORE"
5001 DATA "IN A METAL GANGWAY WITH A","IN A SUIT ROOM","IN A SECRET CONTROL ROOM","ON YUSES FAR","IN BY BUNK-IT HAS CLEAN"
5002 DATA "LINED WITH BUNKS","BLUE BUTTON BY A CLOSED BULKHEAD","BLUE BUTTON ON WALL","WITH-THINK CONTROL-BUTTON","OF THE VESPOZIAN","","COMPUTER ROOM","","A PURPLE SKY","I CAN FEEL HEAT"
5003 DATA "DOOR","","WITH DUPLICATE CONTROL","UNDER AN INDIGO SKY","SHEETS AND LARGE PILLOW"
5004 DATA "AUTO SHUTTLE","STRANGE DEVICE","*POLYPS*","CRAWLWAY","ASBESTOS SUIT","SPACE SUIT","METAL CONTAINER","FUEL CYLINDER","INSULATED TONGS","CAP'N BEZEL","GRAKTA","MACHEN","DISK 005","DISK 001","DISK 002","DISK 003","DISK 004","KEY","GLOWING ROCK"
5005 DATA "FUNUS","LITTLE FUNUS","GYRATES","ROTH","BULGEN","GREATER BULGEN","OUTER FUNUS","FAR FUNUS","YUSES FAR","MID YUSES","YUSES NEAR","YUSES"
5006 DATA "DSB","PF","P","O","V","OC","AFUP","AU","TE","S","PD","P","O","NEW","U"
5007 DATA "HBO","AGC","B","C","C","GJ","BHKF","GA","DN","KF","JGML","K","K","NNI","A"
5008 DATA 3,2,99,99,12,12,6,10,2,6,7,8,66,2,8,6,7,13,99
5009 DATA -2,2,2,-2,2,2,2,2,2,-2,-2,-2,2,2,2,2,2,2,-2
6000 INK 2: CLS : PRINT "GRAKTA, BLOODLINE SPY, BLASTS": PRINT "YOU WITH HIS LASER": PRINT "YOU ARE NOW A PILE OF": PRINT "YUSES DUST!": GO TO 6200
6010 INK 2: CLS : PRINT "AARGH . . !": PRINT "WENT INTO THE FUEL CORE WITHOUT": PRINT "PROTECTION - SEARING HEAT . .": GO TO 6200
6020 INK 4: CLS : PRINT "WITH NO FUEL, THE VESPOZIAN": PRINT "WANDERS AIMLESSLY OUT": PRINT "OF CONTROL, LOST IN SPACE": GO TO 6200
6030 INK 2: CLS : PRINT "TOO CLOSE PROXIMITY": PRINT "THE VESPOZIAN IS RIPPED APART": PRINT "BY THE EXPLOSION": GO TO 6200
6040 INK 5: CLS : PRINT "WHAM!": PRINT "THE BLOODLINE STARGATE IS": PRINT "VAPOURISED. YOU HAV SUCCEEDED": PRINT "IN YOUR MISSION! CONGRATULATIONS": GO TO 6202
6050 INK 5: CLS : PRINT "INNER DOOR CLOSES": PRINT "AIR EVACUATED": PRINT "OUTER DOOR OPENS - NO SPACE SUIT": PRINT "YOU PERISHED AND DRIFT INTO SPACE": GO TO 6200
6055 INK 5: CLS : PRINT "YOU HAVE STUMBLED BLINDLY ONTO": PRINT "RAZOR SHARP ROCK": PRINT : PRINT "YOUR SUIT HAS RIPPED AND YOU": PRINT "DIE FROM LACK OF OXYGEN": GO TO 6200
6060 INK 2: CLS : PRINT "PRESSURE SWITCH OPERATES": PRINT "DRILL ENGAGES AND BORES THROUGH": PRINT "CRAFT."
6061 INK 4: IF C(5)=6 THEN PRINT "YOU DRIFT OFF INTO SPACE": PRINT "AND PERISH WHEN OXYGEN RUNS OUT": GO TO 6200
6065 INK 2: PRINT "YOU BURST APART WITH THE": PRINT "DEPRESSURISATION!": GO TO 6200
6070 INK 5: CLS : PRINT "WALKING AROUND IN ";O$(K2): PRINT "HIGHLY SUSPICIOUS!": PRINT "GRAKTA, BLOODLINE SPY BLASTS YOU": GO TO 6200
6080 INK 2: CLS : PRINT "GRAKTA, BLOODLINE SPY, BLASTS": PRINT "YOU FOR TREACHERRY AGAINST THE": PRINT "BLOODLINE EMPIRE!": GO TO 6200
6090 INK 2: CLS : PRINT "GRAKTA VAPOURISES YOU ON": PRINT "SUSPICION OF SPYING BY": PRINT "RETRIEVING COMPUTERISE DATA": PRINT "YOU PERISH . . ": GO TO 6200
6100 INK 4: CLS : PRINT "GRAKTA NOTICES SUIT ROOM DOOR": PRINT "OPEN. FINDS KEY ON YOU AND": PRINT "BLASTS YOU TOWARDS FUNUS": PRINT "YOU PERISH . .": GO TO 6200
6110 INK 5: CLS : PRINT "GRAKTA NOTICES SHUTTLE BAY": PRINT "DOOR OPEN, AND SUSPECTS A PLOT": PRINT "AGAINST THE BLOODLINE EMPIRE": PRINT "YOU AR EJECTED IN THE SHUTTLE": PRINT "AND FOREVER LOST IN SPACE"
6200 FOR O=10 TO -5 STEP -1: BEEP .05,O: NEXT O: INK 7: PRINT : PRINT : PRINT "YOUR ADVENTURE IS OVER": PRINT "IF YOU WOULD LIKE ANOTHER": PRINT "GAME THEM PRESS ""Y""": GO TO 6205
6202 INK 7: GO SUB 3525: GO SUB 3526: GO SUB 3527: PRINT : PRINT : PRINT "YOUR ADVENTURE IS OVER": PRINT "IF YOU WOULD LIKE ANOTHER": PRINT "GAME THEN PRESS ""Y"""
6205 IF INKEY$="" THEN GO TO 6205
6210 IF INKEY$="Y" THEN CLS : CLEAR : RUN 8
6215 PRINT : PRINT INK 6; FLASH 1;"GOODBYE THEN": PAUSE 100: NEW 
7000 PAPER 0: BORDER 0: INK 2: CLS 
7005 PRINT AT 2,5;"                  "
7010 PRINT "                         "
7015 PRINT "                         "
7020 PRINT "                         "
7025 PRINT 
7030 PRINT "                         "
7035 PRINT "                        "
7040 PRINT "                       "
7045 PRINT "                         "
7050 PRINT : PRINT 
7055 PRINT "                               "
7060 PRINT "                               "
7065 PRINT "                              "
7070 PRINT "                               "
7075 PRINT : PRINT : PRINT INK 5;"By Keith Campbell. \* C&VG 1983."
7080 PRINT AT 21,7; INK 6;"PRESS ""P"" TO PLAY"
7085 IF INKEY$="" THEN GO TO 7085
7090 IF INKEY$="P" OR INKEY$="p" THEN GO TO 8000
7095 GO TO 7085
8000 IF PEEK 23560>64 AND PEEK 23560<91 THEN INK 2: CLS : RUN 
8007 CLS : INK 2: PRINT INK 5;"WOULD YOU PLEASE ENSURE THAT": PRINT : PRINT "YOUR SPECTRUM IS IN THE": PRINT : PRINT """CAPITAL MODE"" BEFORE PLAY"
8010 PRINT : PRINT "CHANGE TO CAPITAL MODE NOW.": PRINT : PRINT : PRINT : PRINT "PRESS ""A"" WHEN READY"
8015 INPUT Q$
8017 IF Q$="A" THEN GO TO 8020
8018 GO TO 8015
8020 INK 2: CLS : RUN 8
8050 PRINT AT 21,0;"YOU ARE NOT USING CAPITALS": PAUSE 200: PRINT AT 21,0;"                               ": RETURN 
