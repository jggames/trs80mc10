1 CLEAR500:GOTO4:DATA8,0,100,1,10,0,7,1,6,1,100,1,100,1,100,1,200,1,9,0,5,1,3,1,11,0,100,1,18,0,200,1,200,0
2 OD=PEEK(173)+256*PEEK(174):RETURN
3 POKE174,INT(OD/256):POKE173,OD-256*PEEK(174):RETURN
4 POKE17026,191:C1=RND(-(PEEK(9)*256+PEEK(10))):GOSUB200:GOTO26
5 OD=R2:GOSUB3:FORC1=1TORND(13):READR$:NEXT:PRINT:PRINTTAB((32-LEN(R$))/2)R$:PRINT:M$="":X=0
6 INPUTM$:IFM$=""THENSOUND1,1:FORC1=1TO32:PRINTCHR$(8);:NEXT:GOTO6
7 X=LEN(M$):H$="":RETURN
9 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(Z$,C1,1):IFI$<"!"THEN?MID$(Z$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
10 C1=C1+(C1>255)*(C1-255):NEXT:Z$="":RETURN

11 DATA"GLORY TO THE CSSR!","WITH THE USSR FOR THE PEOPLE!"
12 DATA"LONG LIVE SOCIALISM!"
13 DATA"WORKERS OF THE WORLD, UNITE!"
14 DATA"THE RENOWNED PARTY OF LENIN!"
15 DATA"DEATH TO IMPERIALIST DOGS!"
16 DATA"WAKE UP TO A NEW WORLD!"
17 DATA"HAIL TO GOLDEN FEBRUARY 1948!"
18 DATA"SOCIALISM-THE WAY OF TOMORROW!"
19 DATA"FORWARD! NOT ONE STEP BACK!"
20 DATA"FREEDOM FOR THE EXPLOITED!"
21 DATA"A TRUE PEOPLE'S GOVERNMENT!"
22 DATA"PERESTROIKA-PATH TO COMMUNISM!"
23 R$=R$+"   ":OD=R4:GOSUB3:READA$:FORP=1TOPP:READA$,I$,C1,C2:IFLEFT$(A$,3)=LEFT$(R$,3)THENC1=P:P=PP:NEXTP:P=C1:RETURN
24 NEXTP:P=0:R$=LEFT$(R$,LEN(R$)-3):RETURN
25 FORC2=1TO2500:NEXTC2:CLS:REM PAUSE
26 OD=R3:GOSUB3:FORC1=1TORO:READS,V,J,Z,N,D,Z$:NEXT:GOSUB90:IFHO=0ANDRO<=5THENPRINT"IT'S DARK HERE.":GOTO41
27 A$=Z$:GOSUB9:Z1$="":IFSTHENZ1$=Z1$+"N, "
28 IFVTHENZ1$=Z1$+"E, "
29 IFJTHENZ1$=Z1$+"S, "
30 IFZTHENZ1$=Z1$+"W, "
31 IFNTHENZ1$=Z1$+"U, "
32 IFDTHENZ1$=Z1$+"D, "
33 IFS+V+J+Z+N+DTHENZ$="EXITS: "+LEFT$(Z1$,LEN(Z1$)-2)+".":GOSUB9
34 Z1$="":BY=0:OD=R4:GOSUB3:READA$:FORA=1TOPP:READA$,I$,C1,C2:IFP(A)=ROTHENZ1$=Z1$+A$+", ":BY=1
35 NEXTA:IFBYTHENZ$="YOU SEE: "+LEFT$(Z1$,LEN(Z1$)-2)+".":GOSUB9
36 IFHOTHENHO=HO-1:IFHO<=0THENP(9)=200:PRINT"!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!MARX'S BOOK IS GOING OUT..."
37 IFP(16)=99THEN121
38 IFDY=0THEN41
39 DY=DY-1:IFDY=0ANDRO=18THENGOSUB900:FORC1=1TO8000:NEXTC1:RUN
40 IFDY=0ANDRO<>18THENGOSUB955:P(14)=200:P(15)=200:P(16)=18:P(17)=18:GOTO25
41 GOSUB5:R$="":FORA=XTO1STEP-1:IFMID$(M$,A,1)=" "THENA=1:NEXTA:GOTO43
42 R$=MID$(M$,A,1)+R$:NEXTA:GOTO44
43 GOSUB23:H$=LEFT$(M$,3)
44 PRINT"--------------------------------":ON-(H$="EXA")GOTO85:IFM$="N"THENIFSTHENRO=S:GOTO26
45 IFM$="E"THENIFVTHENRO=V:GOTO26
46 IFM$="S"THENIFJTHENRO=J:GOTO26
47 IFM$="W"THENIFZTHENRO=Z:GOTO26
48 IFM$="D"THENIFDTHENRO=D:GOTO26
49 IFM$="U"THENIFNTHENRO=N:GOTO26
50 IFH$="GET"THEN94
51 IFH$="DRO"THEN97
52 IFM$="END"THEN960
53 IFH$="LIG"THENIFRND(0)<.7THENPRINT"THE LIGHTER FAILS TO LIGHT...":GOTO25
54 IFH$="LIG"THENIFP=2AND(P(2)=ROORP(2)=99)THENHO=12:P(9)=99:P(2)=0:Z$="YOU HAVE SET FIRE TO DAS KAPITAL!":GOSUB9:GOTO25
55 IFH$="HIT"THENON-(P(11)<>99)GOTO993:IFUV=0ANDP(11)=99THENPRINT"YOU DON'T WANT TO MESS WITH...":GOTO25
56 IFH$="REA"THENIFP(12)=99ORP(12)=ROTHENIFUV=0THENGOSUB500:UV=1:GOTO25
57 IFM$="DIG"THENON-(UV=0)GOTO950:IFUV=1ANDP(11)=99ANDRO=4THENGOSUB600:JA=1:GOTO25
58 IFH$="LIG"THENIFP=14ANDP(14)=18ANDP(4)=99THENPRINT"YOU LIT THE WICK...":DY=2:GOTO25
59 IFH$="TYP"THENON-(RO=8ANDR$<>"1948")GOTO992:IFRO=8ANDR$="1948"THENK$="UNLOCKED":PRINT"THE LOCK CLICKED!":GOTO25
60 IFH$="OPE"THENIFRO=8ANDP=1ANDK$="UNLOCKED"THENZZ$="OPEN":PRINT"THE WAY TO THE TOILET IS CLEAR!":GOTO25
61 IFH$="LIF"THENIFRO=9ANDP(8)=99ANDP=10ANDPO=0THENPO=1:GOSUB700:GOTO25
62 IFM$="INV"THEN100
63 IFM$="HELP"THENGOSUB800:FORC1=1TO2500:NEXTC1:GOTO25
64 Z$="I CAN'T DO THAT... BUT DON'T DESPAIR, THE DIALECTIC OF HISTORY WILL GET YOU THERE EVENTUALLY!":GOSUB9:GOTO25
65 DATA 0,0,0,2,19,0,"YOU ARE AT THE END OF A LONG UNDERGROUND CORRIDOR. THERE IS A LADDER LEADING SOMEWHERE UP."
66 DATA 0,1,0,0,4,0,"YOU ARE IN A LONG UNDERGROUND CORRIDOR LEADING TO THE EAST."
67 DATA 4,0,0,0,0,0,"YOU ARE IN A SMALL SOUTHERN NICHE OF THE CAVE. THERE IS A LOT OF PAPER."
68 DATA 0,0,3,5,0,0,"YOU ARE IN A SMALL UNDERGROUND CAVE. WHEN YOU WALK, YOUR STEPS ECHO."
69 DATA 0,4,0,0,9,0,"YOU ARE IN A NARROW UNDERGROUND CORRIDOR LEADING TO THE EAST. IT'S DAMP HERE."
70 DATA 0,0,9,0,0,0,"YOU'RE IN A FORMER WAREHOUSE. THERE'S AN UNBELIEVABLE MESS."
71 DATA 0,9,0,0,0,0,"YOU ARE STANDING IN A DARK ALCOVE."
72 DATA 9,0,0,0,0,0,"YOU ARE STANDING IN FRONT OF AN UGLY STINKING PUBLIC WASHROOM."
73 DATA 6,0,8,7,0,0,"YOU ARE STANDING IN A SMALL DUSTY ROOM."
74 DATA 0,0,0,8,0,0,"YOU'RE ON A DIRTY TOILET. I'D RATHER NOT DESCRIBE IT TOO MUCH, YOU MIGHT FEEL SICK."
75 DATA 0,0,0,19,0,0,"YOU ARE STANDING IN FRONT OF A BEAUTIFULLY DECORATED ALTAR."
76 DATA 19,13,14,13,0,0,"YOU'RE IN TOWN. THERE'S A CONFUSING MAZE OF STREETS."
77 DATA 0,14,0,12,0,0,"BLOUDIS CITY."
78 DATA 12,15,16,17,0,0,"YOU'RE IN TOWN. THE STREETS ARE UNIMAGINABLY NARROW AND WINDING..."
79 DATA 0,12,0,13,0,0,"YOU'RE IN TOWN. YOU GOT LOST..."
80 DATA 14,17,12,12,0,0,"YOU WALK THROUGH THE CITY."
81 DATA 0,18,0,16,0,0,"BLOUDIS CITY. THIS WAY DOES NOT LEAD ANYWHERE..."
82 DATA 0,0,0,17,0,0,"YOU'RE IN A SMALL PARK. IT'S FULL OF GREENERY."
84 DATA 0,11,12,0,0,1,"YOU'RE IN A SMALL CHURCH. IT IS QUITE NICELY DECORATED."

85 IFP=0OR(P(P)<>RO AND P(P)<>99)THEN64
86 OD=R4:GOSUB3:READA$:FORC1=1TOP:READA$,I$,A,B:NEXTC1:Z$=I$:GOSUB9
87 IFA<=0THEN25
88 IFP(A)<>100THEN25
89 PRINT"YOU FOUND SOMETHING!":P(A)=RO:GOTO25

90 IFRO=8THENIFZZ$="OPEN"ANDK$="UNLOCKED"THENV=10
91 IFRO=4THENIFJA=1THEND=2
92 IFRO=9THENIFPO=1THEND=5
93 RETURN

94 IFP=0ORP(P)<>ROORM(P)=0THEN64
95 IFMA=5THENGOSUB990:GOTO25

96 MA=MA+1:OD=R4:GOSUB3:READA$:FORC1=1TOP:READA$,I$,A,B:NEXTC1:Z$="YOU GET THE "+A$+".":GOSUB9:P(P)=99:GOTO25

97 IFP=0THEN64
98 IFP(P)<>99THEN64
99 MA=MA-1:OD=R4:GOSUB3:READA$:FORC1=1TOP:READA$,I$,A,B:NEXTC1:Z$="YOU DROP THE "+A$+".":GOSUB9:P(P)=RO:GOTO25

100 PRINT"YOU HAVE:":OD=R4:GOSUB3:READA$:FORA=1TOPP:READA$,I$,C1,C2:IFP(A)=99THENPRINTA$
101 NEXTA
102 GOTO25

103 DATA"DON'T BE AFRAID OF THEM!"
104 DATA"DOOR","THE DOOR HAS A FOUR DIGIT KEY PAD.",-1,0
105 DATA"BOOK","IT IS MARX'S DAS KAPITAL.",-1,0
106 DATA"BOWL","IT'S COMPLETELY DRY.",2,0,"LIGHTER"
107 DATA"SURELY THERE WOULD BE SOMETHING TO LIGHT WITH IT. IT'S A QUALITY LIGHTER 'MADE IN USSR'.",-1,0
108 DATA"BOX","IT'S MADE OF WOOD.",6,0
109 DATA"BASKET","IT'S A NICE WICKER BASKET, ALTHOUGH A BIT DIRTY.",7,0
110 DATA"PACKAGE","IT'S MADE OF PAPER.",8,0
111 DATA"KEY","IT'S A SQUARE KEY TO THE HATCH.",-1,0
112 DATA"KAPITAL (BURNING)","IT SHINES AS A BEACON OF PROGRESS!",-1,0
113 DATA"HATCH","IT IS A HEAVY STEEL HATCH WITH A SQUARE KEY LOCK.",-1,0
114 DATA"PICKAXE","IT IS VERY RUSTY, BUT OTHERWISE USABLE.",-1,0
115 DATA"NEWSPAPER","IT'S AN EDITION OF 'RED RIGHT'",-1,0
116 DATA"ALTAR","THERE ARE PICTURES OF THE HOLY TRINITY: MARX, ENGELS AND LENIN ON IT...",14,0
117 DATA"DYNAMITE","IT IS A CLASSIC TUBE WITH A WICK.",-1,0
118 DATA"STATUE","LENIN...",-1,0
119 DATA"BRICK","IT'S A GOLD BRICK.",-1,0
120 DATA"PIECES OF STATUE","A PIECE OF A SKULL, AN EAR AND A HAND SHOWING THE WAY TO A BRIGHT COMMUNIST FUTURE.",-1,0

121 PRINT:PRINT"CONGRATULATIONS COMRADE!":FORC1=1TO3000:NEXTC1:GOTO400

200 REM OKAMZITE TOHO NECHTE/ TAKOVY PODVOD SE NESLUCUJE S MORALKOU/SOCIALISTICKEHOCLOVEKA!
210 CLS                                                                 
210 DIMM$:RO=9:ZZ$="CLOSED":K$="ZA":PP=17:DIMM(PP),P(PP):FORA=1TOPP:READP(A),M(A):NEXTA:GOSUB2:R2=OD
220 MA=0:HO=0:PO=0:JA=0:UV=0:DY=0:CLS
230 PRINT"  THE CENTRAL COMMITTEE ON THE"
231 PRINT"    THE 20TH ANNIVERSARY OF"
232 PRINT"       THE LIBERATION OF"
233 PRINT" CZECHOSLOVAKIA BY WARSAW PACT"
234 PRINT"   FORCES WOULD LIKE TO OFFER"
235 PRINT" A CONVERSATIONAL PUZZLE GAME:"
236 PRINT
237 PRINT"     P.E.R.E.S.T.R.O.I.K.A."
238 PRINT
239 PRINT"¿¿¿¿¶½¿¿pROGRAM FOR eXPERIMENTAL";
241 PRINT"¿¾¸°½²½¿     reVOLUTIONARY"
242 PRINT"¿²±´¿¿°¿   sOCIALIST trIPE BY
243 PRINT"¿¿¿»´¸·¿oBVIOUS iLLITERATES AND"
244 PRINT"¾¹¶¼²µ¿¿     kOOKY aUTHORS
245 PRINT"»·¿¿¿³¿¿ (C) 1988 UV SOFTWARE"
246 PRINT"AND SCENARIO (C) 1968 REAL LIFE";
247 GOSUB994:FORC2=1TO50:NEXTC2:GOSUB996:FORC1=1TO13:READZ$:NEXT:GOSUB2:R3=OD
250 FORC1=1TO19:READS,V,J,Z,N,D,Z$:NEXTC1:GOSUB2:R4=OD:CLS:RETURN
400 Z$="IT IS CLEAR THAT A SOCIALIST PERSON CAN COPE IN ANY SITUATION...":GOSUB9:FORC1=1TO3000:NEXTC1
410 Z$="CONGRATULATIONS ONCE AGAIN. WE'LL ALL MEET ON AUGUST 21 IN THE OLD TOWN SQUARE... (OR ELSEWHERE)"
420 GOSUB9:FORC1=1TO4000:NEXTC1:GOTO960
500 Z$="YOU READ 'RED RIGHT' AND IMMEDIATELY GET A TASTE FOR WORK, WHICH IS A FUNDAMENTAL HONOUR OF A SOCIALIST CITIZEN."
510 GOSUB9:RETURN
600 Z$="YOU MANAGED TO DIG INTO THE LOWER FLOOR!":GOSUB9:RETURN
700 Z$="WITH THE HELP OF THE KEY YOU MANAGE TO OPEN THE HATCH.":GOSUB9:RETURN
800 Z$="HELP: GET END LIGHT LIFT HIT TYPE READ EXAMINE OPEN INV HELP N S E W U D.":GOSUB9:RETURN
900 Z$="A HUGE EXPLOSION SHOOK THE CITY, WHICH, BEING IN THE MIDST OF IT, YOU DID NOT HEAR..."
910 GOSUB9:RETURN
950 PRINT"YOU CAN'T BE BOTHERED.":GOTO25
955 Z$="THE EARTH SHOOK WITH THE EXPLOSION.":GOSUB9
956 FORC1=1TO15:POKE49151,64:FORC2=1TO10:NEXTC2:SOUNDRND(50),1:NEXTC1:RETURN
960 CLS4:PRINT@32,;:FORC1=1TO25:I$=INKEY$:NEXTC1
961 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿Ÿ¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿";
962 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿¿¿¿";
963 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿ŸŸŸ¿¿ŸŸŸ¿¿¿¿¿¿¿¿¿¿¿¿";
964 PRINT"¿¿¿¿¿¿¿¿¿¿¿ŸŸŸŸ¿¿¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿";
965 PRINT"¿¿¿¿¿¿¿¿¿¿ŸŸŸŸ¿¿¿¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿";
966 PRINT"¿¿¿¿¿¿¿¿¿¿¿ŸŸ¿ŸŸ¿¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿";
967 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿ŸŸŸ¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿";
968 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿ŸŸŸ¿ŸŸ¿¿¿¿¿¿¿¿¿¿¿";
969 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿ŸŸŸŸŸ¿¿¿¿¿¿¿¿¿¿¿";
970 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿ŸŸ¿¿¿ŸŸŸ¿¿¿¿¿¿¿¿¿¿¿¿";
971 PRINT"¿¿¿¿¿¿¿¿¿¿¿Ÿ¿ŸŸŸŸŸŸŸŸ¿¿¿¿¿¿¿¿¿¿¿";
972 PRINT"¿¿¿¿¿¿¿¿¿¿Ÿ¿¿¿ŸŸŸŸ¿ŸŸŸ¿¿¿¿¿¿¿¿¿¿";
973 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿ŸŸ¿¿¿¿¿¿¿¿¿¿";
980 I$=INKEY$:IFI$=""THEN980
981 RUN
990 Z$="DON'T BE A HOARDER COMRADE! REMEMBER, FROM EACH ACCORDING TO THEIR ABILITY TO EACH ACCORDING TO THEIR NEED!"
991 GOSUB9:GOTO25
992 Z$="IT'S AN INAUSPICIOUS NUMBER. NOTHING HAPPENS.":GOSUB9:GOTO25
993 Z$="OUCH! SAVE YOUR VIOLENCE FOR FIGHTING IMPERIALIST RUNNING DOGS COMRADE!":GOSUB9:GOTO25
994 SOUND129,8:SOUND129,4:SOUND122.5,4:SOUND129,4:SOUND87,8:SOUND129,4:SOUND142,8:SOUND142,4:SOUND129,4:SOUND142,8
995 RETURN
996 SOUND149,8:FORC2=1TO25:NEXTC2:SOUND149,4
997 SOUND161,4:SOUND171,4:SOUND149,8:SOUND161,4:SOUND171,8:SOUND171,4:SOUND161,4:SOUND171,12:RETURN
