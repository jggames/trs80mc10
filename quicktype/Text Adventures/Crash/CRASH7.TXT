0 CLEAR500:DIMCC,ZZ,M$,I$:MC=16384:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+MC-1:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-MC+ZZ,:RETURN
10 GOSUB5700:REM INTRO
20 GOSUB5000:REM INITIALIZE
30 GOSUB1000:REM INPUT
40 GOSUB2000:REM SORT
45 GOSUB4400:REM CHECKS
50 IFWF=0ANDLF=0THEN30
60 GOSUB5500:REM END
70 END
1000 VB$="":NO$=""
1020 INPUT"WHAT DO YOU DO";R$
1030 FORI=1TOLEN(R$)
1040 IFMID$(R$,I,1)=" "THENVB$=LEFT$(R$,3):NO$=MID$(R$,I+1,3):I=LEN(R$)
1050 NEXT I
1060 IFNO$<>""THENRETURN
1070 R$=LEFT$(R$,3)
1080 IFR$="NOR"ORR$="SOU"ORR$="EAS"ORR$="WES"THENVB$="GO ":NO$=R$:RETURN
1090 IFR$="HEL"ORR$="INV"ORR$="LOO"ORR$="SLE"THENNO$="DOO":VB$=R$:RETURN
1100 PRINT"I DON'T UNDERSTAND THAT."
1110 GOTO1000
2000 VB=0:NO=0:FORI=1TOVV
2020 IFVB$=LEFT$(VB$(I),3)THENVB=I:I=VV
2030 NEXTI
2040 FORI=1TONN
2050 IFNO$=NO$(I)THENNO=I:I=NN
2060 NEXTI
2070 IFNO=0ORVB=0THENPRINT"I DON'T UNDERSTAND THAT.":RETURN
2080 ONVBGOTO3000,3100,3200,3300,3400,3500,3620,3700,3800,3900,4000,4200,4300
3000 REM  ************ GO ****************
3010 IFNO>4THENPRINT "GO WHERE ?":RETURN
3020 IFEX(CP,NO)=0THENPRINT"NO MOVEMENT POSSIBLE THAT WAY.":RETURN
3030 IFEX(CP,NO)=99THENLF=2:RETURN
3035 IF(CP=10ORCP=14ORCP=15)ANDDE<2THENDE=DE+1:GOTO4000
3036 DE=0
3037 IFDARK=1THENLF=8:RETURN
3040 CP=EX(CP,NO):GOTO4000
3100 REM  ************ GET ***************
3120 IFNO<7THENPRINT"DON'T BE SILLY.":RETURN
3130 IFOB(NO-6)=99THENPRINT"YOU'VE ALREADY GOT IT!":RETURN
3140 IFOB(1)=99ANDOB(9)<>50ANDOB(9)<>99THENOB(9)=3
3170 IFOB(NO-6)=CPTHENPRINT"O.K.":OB(NO-6)=99:IN=IN+1:RETURN
3180 PRINT"IT ISN'T HERE!":RETURN
3200 REM  ************** FEED ************
3210 IFNO<13THENPRINT"DON'T BE SILLY!":RETURN
3250 IFOB(3)<>99ANDOB(6)<>99THENM$="YOU DON'T HAVE ANYTHING TO USE!":GOSUB1:RETURN
3255 IFCP<>4ANDCP<>23THENPRINT"YOU CAN'T HERE.":RETURN
3260 IFFIRE<>1THENPRINT"IT ISN'T LIT YET!":RETURN
3265 IFCP=23ANDF2<>1THENPRINT"IT ISN'T LIT YET!":RETURN
3270 IFCP=4THENOB(3)=0:PRINT"O.K.":RETURN
3280 IFCP=23THENOB(6)=0:PRINT"O.K.":LD=LD+1:RETURN
3300 REM  ************* MELT *************
3305 IFCP<>4THENPRINT"YOU CAN'T HERE!":RETURN
3310 IFOB(4)<>99THENM$="YOU DON'T HAVE ANYTHING TO USE.":GOSUB1:RETURN
3320 IFFIRE<>1THENPRINT "YOU CAN'T... YET.":RETURN
3325 IFNO<>16THENPRINT"MELT WHAT?":RETURN
3330 PRINT"O.K.":MELT=1:RETURN
3400 REM  ************* DRINK ************
3410 IFNO<>17THENPRINT"DRINK WHAT?":RETURN
3420 IFMELT<>1THENPRINT"YOU DON'T HAVE ANY!":RETURN
3430 PRINT"THAT'S REFRESHING.":DR=1:RETURN
3500 REM  ************** EAT *************
3510 IFOB(9)<>99THENPRINT"YOU DON'T HAVE ANYTHING":RETURN
3515 IFNO<>15THENPRINT"EAT WHAT?":RETURN
3520 M$="IT'S HARD BUT TASTES MIGHTY GOOD.":GOSUB1:OB(9)=50:RETURN
3620 REM  ************* MAKE *************
3630 IFNO=13ANDCP<>4THENPRINT"YOU CAN'T HERE.":RETURN
3640 IFNO=14AND(CP=4ORCP=23)THEN3700
3650 IFOB(5)<>99THENPRINT"YOU DON'T HAVE ONE.":RETURN
3660 IFNO=13ANDOB(5)=99THENPRINT"O.K. THAT FEELS BETTER":OB$(5)="SPLINT":RETURN
3670 PRINT"MAKE WHAT?":RETURN
3700 REM  ************* LIGHT ************
3710 IFOB(2)<>99THENPRINT"YOU DON'T HAVE ANY MATCHES!":RETURN
3720 IFCP=4ANDOB(3)<>99THENPRINT"YOU DON'T HAVE ANY FUEL":RETURN
3730 IFCP=4ANDGAS<>1THENPRINT"THEY WON'T BURN... YET.":RETURN
3740 IFCP=23ANDOB(6)<>99THENPRINT"YOU DON'T HAVE ANY FUEL":RETURN
3745 IFCP<>4ANDCP<>23THENPRINT"YOU CAN'T HERE.":RETURN
3750 PRINT"O.K.":FIRE=1:IFCP=4THENOB(3)=5:OB(8)=4:RETURN
3760 IFCP=23THENOB(6)=25:F2=1:LD=LD+1:OB(8)=23:RETURN
3800 REM  ********** INVENTORY ***********
3810 PRINT:PRINT"YOU ARE CARRYING:"
3820 IFIN=0THENPRINT"NOTHING!":RETURN
3830 M$="":FORI=1TO10
3840 IFOB(I)=99THENM$=M$+"A "+OB$(I)+", "
3850 NEXTI:M$=LEFT$(M$,LEN(M$)-2):GOSUB1:PRINT:RETURN
3900 REM  ************** HELP ************
3910 PRINT:M$="THESE ARE THE VERBS YOU MAY USE:":GOSUB1
3920 M$="":FORI=1TOVV:M$=M$+VB$(I)+", ":NEXT:M$=LEFT$(M$,LEN(M$)-2):GOSUB1
3930 M$="(YOU NEED ONLY TYPE THE FIRST THREE LETTERS.)":GOSUB1:RETURN
4000 REM  ************ LOOK **************
4010 PRINT:M$=LO$(CP):GOSUB1:IFCP=22ORCP=6ORCP=11THENLF=1:RETURN
4020 FORI=1TO9
4040 IFOB(I)=CPTHENPRINT"A "OB$(I)" IS HERE"
4050 NEXTI
4060 M$="POSSIBLE MOVEMENT: "
4070 IFEX(CP,1)>0THENM$=M$+"NORTH "
4080 IFEX(CP,2)>0THENM$=M$+"SOUTH "
4090 IFEX(CP,3)>0THENM$=M$+"EAST "
4100 IFEX(CP,4)>0THENM$=M$+"WEST"
4110 GOSUB1:PRINT:RETURN
4200 REM  *********** DRAIN ***************
4210 IFNO<>18THENPRINT"DRAIN WHAT?":RETURN
4220 IFCP<>3THENPRINT"THERE'S NONE HERE...":RETURN
4230 IFOB(4)<>99THENPRINT"INTO WHAT?":RETURN
4240 PRINT"O.K.":GAS=1:RETURN
4300 REM  ************** SLEEP ************
4310 IFFIRE<>1ORCP<>4THENPRINT"IT'S TOO COLD HERE!":RETURN
4320 IFDR<>1THENPRINT"YOU'RE TOO THIRSTY TO SLEEP.":RETURN
4330 M$="YOU QUICKLY DOZE OFF BY THE FIRE.":GOSUB1:SL=1
4331 PRINT:M$="AFTER FIRST LIGHT THE SOUND OF A NEARBY PLANE AWAKENS YOU!":GOSUB1
4332 SOUND1,1:PRINT"TOO BAD THE FOG'S SO THICK.":FORCC=1TO2500:NEXT:RETURN
4400 MO=MO+1:IFOB$(5)<>"SPLINT"THENM$="YOUR LEG IS VERY PAINFUL":GOSUB7:PRINT:IFMO>6THENLF=3:RETURN
4410 IFOB(1)<>99THENM$="IT'S VERY COLD.":GOSUB7:PRINT:IFMO>8THENLF=4:RETURN
4415 IFMO<10THENRETURN
4420 IFDR<>1ANDMO/2=INT(MO/2)THENM$="YOU'RE VERY THIRSTY":GOSUB7:PRINT:IFMO>24THENLF=5:RETURN
4430 IFOB(9)<>50ANDMO/2<>INT(MO/2)THENM$="YOU'RE VERY HUNGRY.":GOSUB7:PRINT:IFMO>20THENLF=6:RETURN
4440 IFMO>18ANDSL<>1ANDMO/2<>INT(MO/2)THENM$="IT'S GETTING DARK.":GOSUB7:PRINT:IFMO>25THENDARK=1:RETURN
4445 IFMO=45THEN4470
4450 IFMO>49THENLF=7:RETURN
4460 RETURN
4470 PRINT:M$="YOU HEAR A HELICOPTER APPROACHING.":GOSUB1:FORCC=1TO16:SOUND1,1:FORZZ=1TO100:NEXT:NEXT
4480 FORI=1TO2500:NEXT:IFLD>1THENM$="THEY SEEM TO SEE YOUR LARGE FIRE...":GOSUB1:FORI=1TO2500:NEXT:WF=1:RETURN
4490 M$="BUT THEY DON'T SEE YOU...":GOSUB1:FORI=1TO2500:NEXT:RETURN
5000 CLS:PRINTTAB(8)"CAN YOU SURVIVE?":PRINT:PRINTTAB(10)"BY RUSS HALL":PRINT
5010 M$=" YOU WERE ON A BUSINESS TRIP IN A SINGLE ENGINE PLANE. BAD WEATHER CROSSING HIGH "
5020 M$=M$+"MOUNTAINS SUDDENLY CAME UP, CAUSING YOU TO CRASH INTO A ROUGH SLOPE.":GOSUB1
5030 M$="MUCH LATER YOU AWAKE IN A COLD, QUIET AND FOGGY WORLD WITH A TERRIBLE "
5040 M$=M$+"PAIN INSIDE YOUR RIGHT LEG (IF YOU NEED ASSISTANCE, TYPE 'HELP').":GOSUB1
5050 PRINT:PRINTTAB(4)"PRESS SPACE KEY TO START";
5051 M$=INKEY$:IFM$<>" "THEN5051
5052 CLS
5090 NN=18:VV=13:CP=3:IN=0:WF=0:LF=0:UF=0:CF=0
5100 DIMLO$(25),EX(25,4),NO$(NN),VB$(VV),OB$(10),OB(10)
5110 FORI=1TO25
5120 IFI=11THENLO$(I)=LO$(6):GOTO5140
5124 IFI=14ORI=15THENLO$(I)=LO$(10):GOTO5140
5126 IFI=20ORI=25THENLO$(I)=LO$(16):GOTO5140
5130 READD$:LO$(I)=D$
5140 NEXTI
5150 FORI=1TO25:FORJ=1TO4:READEX(I,J):NEXT J,I
5160 FORI=1TO9:READOB$(I):READOB(I):NEXT
5170 FORI=1TONN:READNO$(I):NEXT
5180 FORI=1TOVV:READVB$(I):NEXT
5190 GOSUB4000:RETURN:REM  LOOK
5500 SOUND1,1:IFWF=1THEN5610
5501 ONLFGOTO5510,5520,5530,5540,5550,5560,5570,5580
5510 IFCP=22THENM$="YOU SLIP ON THE SNOW AND SLIDE INTO  ROCKS BELOW.":GOSUB1:GOTO5600
5515 M$="YOU SLIP AND TUMBLE OVER THE ROCKS.":GOSUB1:GOTO5600
5520 M$="YOU HAVE FALLEN OFF THE CLIFF!":GOSUB1:GOTO5600
5530 M$="YOUR PAIN KEEPS YOU FROM MOVING FURTHER, AND YOU CANNOT KEEP WARM.":GOSUB1:GOTO5600
5540 M$="HYPOTHERMIA SETS IN AND YOU SHIVVER UNCONTROLABLY.":GOSUB1:GOTO5600
5550 M$="ONE CAN'T LAST LONG WITHOUT WATER.":GOSUB1:GOTO5600
5560 M$="WITHOUT THE ENERGY OF FOOD, HYPOTHERMIA SETS IN.":GOSUB1:GOTO5600
5570 M$="YOU HAVE BEEN TOO LONG IN THE COLD.":GOSUB1:GOTO5600
5580 M$="YOU CAN'T SEE TO MANEUVER SAFELY AND STUMBLE IN THE ROCKS, KNOCKING YOURSELF UNCONSCIOUS.":GOSUB1:GOTO5600
5600 M$="YOU HAVE FAILED TO SURVIVE.":GOSUB1:GOTO5640
5610 M$="THE RESCUE COPTER LOWERS A LADDER AND MAN TO ASSIST YOU. CONGRADULATIONS! YOU HAVE SURVIVED!":GOSUB1
5640 FORI=1TO2500:NEXT:INPUT"DO YOU WISH TO TRY AGAIN";R$
5650 IFLEFT$(R$,1)="Y"THENRUN
5660 PRINT"BYE-BYE":PRINT:END
5700 GOSUB5800
5750 PRINT@.,;:M$="CAN YOU SURVIVE?":GOSUB7:FORI=1TO2500:NEXT:PRINT@496,;:M$="BY RUSS HALL":GOSUB7
5780 R$=INKEY$:IFR$=""THEN5780
5785 CLS
5790 RETURN
5800 CLS0
5801 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€σ€€€";
5802 PRINT"€€€€€€€€€€€€€€€σ€€€€€€€€€€€€€€€€";
5803 PRINT"€€€€€€€€€€€€€σ€σσ€€€€“€€σ€€€ό€";
5804 PRINT"€€€€€€€€€€€€€€€σ€σ€€€€σό€€€€€ό€";
5805 PRINT"€€σ€σ€€€€σσ€€σ€ό€€€€€€€€€€€€€";
5806 PRINT"€€ό€€€ό€€€σ€σ€€€€€€““€€€σσ€ό“€";
5807 PRINT"€€€σ€€σό“““όσ€€ό€““€€€";
5808 PRINT"€€€ό““€€€€€“€€";
5809 PRINT"€ό“€€€“€“€€€";
5810 PRINT"σ€€€“€“““€“€σ";
5811 PRINT"σ€σ€“€€€€σ";
5812 PRINT"€€“€“€€€σ€€€€€€€";
5813 PRINT"€€€€ό€ό€€€€€σ€σ€€€€€σ€€";
5814 PRINT"€€€ό€€€ό€€σ€ό€€€€€€€€€€€€€€€";
5815 PRINT"€€€€€€€€€€€€€€σ€€€€€€€“€€€€€ό€€";:RETURN
6010 DATA"THE MOUNTAIN SIDE SLOPES DOWN STEEPLY TO THE SOUTH. THE WAY WEST IS BLOCKED BY A ROCKSLIDE."
6020 DATA"SOME CHRASH DEBRIS SHOWS HERE THROUGH THE FOG AND SNOW COVERING."
6030 DATA"YOU ARE AT THE CRASH SITE. PIECES OF THE PLANE SURROUND YOU."
6040 DATA"YOU ARE ON A GENTLE, SOUTHERN SLOPING, SNOWY MOUNTAIN SIDE."
6060 DATA"YOU CAN SEE VERY DENSE FOREST TO THE SOUTH AND EAST."
6070 DATA"THE MOUNTAIN SIDE HERE IS VERY STEEP, YOU HAVE TROUBLE MANEUVERING"
6080 DATA"HERE YOU SEE THE HILLSIDE SLOPE DOWN MORE STEEPLY ON THE WEST."
6090 DATA"THE FOG SEEMS TO BE THINNING DOWN THE MOUNTAIN, AND A ROCKY CLIFF EDGE RUNS ALONG THE SOUTH HERE."
6100 DATA"A CLIFF EDGE IS TO THE SOUTH AND DENSE FOREST ON THE EAST."
6120 DATA"YOU ARE IN THE THICK FOREST. IT'S DIFFICULT TO KNOW YOUR WAY."
6121 DATA"THIS IS A BEAUTIFUL, WIDE MORAINE FIELD WITH STEEP SLOPES WEST."
6122 DATA"YOU ARE AT THE BASE OF A ROCKY CLIFF TO THE NORTH, DENSE FOREST TO THE EAST."
6130 DATA"YOU ARE ON A BEAUTIFUL FORESTED MOUNTAINSIDE."
6140 DATA"THERE IS A LARGE, STEEP SNOWFIELD TO THE SOUTH."
6145 DATA"YOU ARE ON A ROCKY, SLOPING MOUNTAIN SIDE."
6150 DATA"THE FOREST IS THINNER HERE BUT MUCH DENSER TO THE NORTH."
6155 DATA"A STEEP, SLICK SNOWFIELD BLOCKS YOUR WAY EAST."
6160 DATA"YOU ARE ON A STEEP, SLICK SNOWFIELD."
6170 DATA"YOU'RE IN A CLEARING HERE, WITH A STEEP SNOWFIELD WEST."
6180 DATA"YOU'RE ON A SLOPING MOUNTAIN SIDE WITH CLOUDS HIGH ABOVE YOU."
6220 DATA0,6,2,,,,3,1,,,4,2,,9,5,3,,10,,4
6230 DATA1,11,7,,,12,8,6,,99,9,7,4,99,10,8,5,15,,9
6240 DATA6,16,12,,7,17,13,11,,18,14,12,,19,15,13,10,,,14
6250 DATA11,21,17,,12,22,18,16,13,23,19,17,14,24,20,18,15,,,19
6260 DATA16,,22,,17,,23,21,18,,24,22,19,,25,23,20,,,24
6310 DATACOAT,3,BUTANE LIGHTER,2,BUNCH OF STICKS,5,ALUMINUM CAN,1,STRAIGHT LIMB,4,MESS OF FALLEN TIMBER,25
6320 DATASPLINT,0,FIRE,0,CANDY BAR,0
6510 DATANOR,SOU,EAS,WES,DOO,PLA,COA,LIG,STI,CAN,LIM,TIM,SPL,FIR,BAR,SNO,WAT,GAS
6530 DATAGO ,GET,FEED,MELT,DRINK,EAT,MAKE,LIGHT,INVENTORY,HELP,LOOK,DRAIN,SLEEP
7000 REM CRASH
