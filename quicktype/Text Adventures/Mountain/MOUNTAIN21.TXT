0 CLEAR400:CLS5:SOUND200,3:GOSUB328:GOTO12
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:Z$=MID$(A$,CC,1):IFZ$<"!"THENPRINTMID$(A$,C1,CC-C1):C1=CC+1:CC=C1+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:A$="":RETURN
3 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(A$):POKEC1+C2,ASC(MID$(A$,C2))AND63:NEXT:PRINT@C1-16384+C2,:RETURN
4 PRINT"OK":GOTO87
5 GOSUB1:GOTO87
6 FORC1=1TO2500:NEXT:RETURN
7 FORC1=1TOCC:PRINT"ˇ";:NEXT:RETURN
8 PRINT@492,;:A$="HIT A KEY":GOSUB3
9 C1=RND(0):ON-(INKEY$="")GOTO9:RETURN
10 PRINT@.,;:A$=LEFT$("PLAYER IS: "+A$(Z)+"                ",32):GOTO3
11 PRINT@8,;:A$="THE HIGH MOUNTAINS":GOTO3
12 CLS:POKE49151,64:POKE17026,118:GOSUB11:PRINT," IF YOU ARE EITHER A MASTER OR ATRIPOD THEN THE OBJECT OF YOUR"
13 PRINT"GAME IS TO DESTROY ALL THE HUMANCHARACTERS WHO ARE NOT BEING","CONTROLLED BY OTHER PLAYERS."
14 PRINT" IF YOU ARE NOT A MASTER OR A","TRIPOD THEN YOU MUST COLLECT THEAPPROPRIATE OBJECTS AND CARRY"
15 PRINT"OUT THE TASKS TO ENABLE YOU TO","DESTROY THE GOLDEN CITY."," WHEN YOU THINK YOU HAVE THE"
16 PRINT"REQUIRED OBJECTS IN THEIR","CORRECT FORM, MOVE TO THE GOLDENCITY AND FIRE AWAY.":GOSUB8
17 CLS:GOSUB11
18 PRINT,"COMMANDS ARE:",,"CLIMB",,"RIDE ... DIRECTION","KILL ...","FIGHT ...","EXAMINE","LOOK","INVENTORY","GET","TAKE",
19 PRINT"SWIM","FILL ... WITH ...","DROP","UNLOCK","FIRE","LOAD","SAVE","HELP","TALK",,"AND DIRECTIONS TO MOVE.":GOSUB8
20 CLS:PRINT@10,;:A$="THE CHARACTERS":GOSUB3:FORJ=0TO29:PRINT@32+J*16,A$(J+1);:NEXT:GOSUB6
21 PRINT@.,"":PRINT@.,"NUMBER OF PLAYERS";:INPUTA$:NO=VAL(A$):IFNO<1ORNO>5THEN21
22 FORJ=1TONO
23 PRINT@0,"":PRINT@.,"PLAYER"J;:INPUTS$(J)
24 IFJ<2THEN27
25 FORI=1TOJ-1:IFLEFT$(S$(J),4)=LEFT$(A$(A(T+I)),4)THEN23
26 NEXT
27 FORK=1TO30:IFLEFT$(S$(J),4)=LEFT$(A$(K),4)THENA(T+J)=K:K=30:NEXT:GOTO29
28 NEXT:GOTO23
29 NEXT:H=2:CLS:PRINT@480,;
30 FORQ=1TONO:Z=A(T+Q):D$(1)="LOOK"
31 F=1:FORJ=1TONO:IFA(E+A(T+J))>0THENF=0
32 NEXTJ:IFFTHENGOSUB107:A$="NO-ONE HAS WON.":GOSUB1:GOTO102
33 IFA(E+Z)<=0THENNEXTQ:GOTO30
34 IFNO>1THENCC=38:GOSUB7:PRINT" NEXT PLAYER'S TURN ";:GOSUB7
35 FORR=1TOA(G+Z):IFA(E+Z)<=0THENNEXTQ:GOTO30
36 F=1:K=RND(30):FORU=1TONO:IFK=A(T+U)THENF=0
37 NEXTU:IFF=1ANDA(K)<105THENGOSUB323
38 H=1:IFD$(1)="LOOK"ORD$(1)="N"ORD$(1)="S"ORD$(1)="E"ORD$(1)="W"ORD$(1)="SWIM"ORD$(1)="RIDE"THEN41
39 IFD$(1)="CLIMB"ORD$(1)="NORTH"ORD$(1)="SOUTH"ORD$(1)="EAST"ORD$(1)="WEST"ORD$(1)="GO"THEN41
40 GOTO55
41 RESTORE:FORC1=1TOA(Z):READV$,V,W,X,Y:NEXT:A$="YOU ARE "+V$+".":GOSUB1:IFA(Z)=78ANDNM=0THENA$="THE SHED IS LOCKED.":GOSUB1
42 IFA(Z)=78ANDNM=1THENA$="THE SHED IS NOT LOCKED.":GOSUB1
43 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)ANDN(J)<M(J)THENA$="THE CANAL IS TO YOUR EAST.":GOSUB1
44 IFN(J)=A(Z)ANDM(J)<N(J)THENA$="THE CANAL IS TO YOUR WEST.":GOSUB1
45 NEXTJ
46 A$="YOU CAN MOVE: ":IFV=1THENA$=A$+"NORTH "
47 IFW=1THENA$=A$+"SOUTH "
48 IFX=1THENA$=A$+"EAST "
49 IFY=1THENA$=A$+"WEST "
50 IFA$<>""THENGOSUB1
51 FORJ=1TO15:IFA(S+J)=A(Z)THENA$="YOU CAN SEE: "+B$(J):GOSUB1
52 NEXTJ
53 O=1:FORJ=1TO30:IFA(J)=A(Z)ANDJ<>ZTHENO=O+1:A$="YOU CAN SEE: "+A$(J):GOSUB1
54 NEXTJ
55 II=RND(10-O):IFNOT(II<2)THEN58
56 FF=0:GOSUB298:IFFF=1THEN87
57 IFFF=2THEN179
58 IFFF=3THEN160
59 PRINT@511," ";:CC=63:GOSUB7:POKE16895,255:GOSUB10:PRINT@416,"command";:INPUTC$
60 PRINT@416,"COMMAND";:PRINT@448,;:FORC1=1TO63:PRINT" ";:NEXT:POKE16895,96:PRINT@448,;:IFC$=""THEN63
61 BE=1:PO=1:FORJ=1TOLEN(C$):IFMID$(C$,J,1)=" "THEND$(PO)=MID$(C$,BE,J-BE):BE=J+1:PO=PO+1:IFPO>4THENJ=255
62 NEXTJ:D$(PO)=MID$(C$,BE):IFPO<5THEND$(PO+1)=""
63 IFD$(1)="CLIMB"THEN122
64 IFD$(1)="RIDE"THEN124
65 IFD$(1)="KILL"ORD$(1)="FIGHT"THEN142
66 IFLEFT$(D$(1),4)="EXAM"THEN180
67 IFD$(1)="LOOK"THEN41
68 IFLEFT$(D$(1),3)="INV"THEN181
69 IFD$(1)="GET"ORD$(1)="TAKE"THEN184
70 IFD$(1)="SWIM"THEN197
71 IFD$(1)="FILL"THEN206
72 IFD$(1)="DROP"THEN215
73 IFD$(1)="LOAD"THEN239
74 IFD$(1)="SAVE"THEN238
75 IFD$(1)="N"ORD$(1)="NORTH"THEN224
76 IFD$(1)="S"ORD$(1)="SOUTH"THEN226
77 IFD$(1)="E"ORD$(1)="EAST"THEN228
78 IFD$(1)="W"ORD$(1)="WEST"THEN230
79 IFD$(1)="UNLOCK"THEN232
80 IFD$(1)="FIRE"THEN319
81 IFD$(1)="HELP"THENA$=H$(H)+".":GOSUB1:GOTO55
82 IFD$(1)="GO"THEND$(1)=D$(2):GOTO75
83 IFD$(1)="QUIT"THEN101
84 IFD$(1)="JUMP"ANDD$(2)="RIVER"THENPRINT"YOU CAN'T DO THAT.":H=3:GOTO55
85 IFD$(1)="TALK"THEN108
86 PRINT"I DON'T UNDERSTAND.":H=1:GOTO55
87 FF=0:FORJ=17TO30:F=0:FORU=1TONO:IFJ=A(T+U)THEN90
88 NEXTU:IFFTHEN90
89 IFA(E+J)>0THENFF=J
90 NEXTJ:IFFFTHENJ=FF:GOTO92
91 IFZ<17THENGOSUB107:A$=A$(Z)+" HAS WON!":GOSUB1:GOTO102
92 NEXTR:NEXTQ:GOTO30
93 Z=A(T+Q):D$(1)="LOOK":GOTO31
94 A$=A$+" DOES NOT MOVE BECAUSE YOU ARE NOT NEAR ANY WATER SO YOU CLIMB BACK OUT.":GOTO5
95 A$=A$+" IS NOT HERE.":GOTO5
96 FORK=1TO15:IFA(S+K)=Z+200THENA(S+K)=A(Z):GOSUB100
97 NEXTK:A(P+Z)=0:RETURN
98 FORK=1TO15:IFA(S+K)=J+200THENA(S+K)=A(J):GOSUB100
99 NEXTK:A(P+J)=0:RETURN
100 PRINTB$(K);" WAS DROPPED.":RETURN
101 ON1-(D$(2)="ALL")GOTO104:GOSUB107
102 FORC1=1TO5:SOUND30,1:SOUND20,1:SOUND10,1:POKE49151,64:FORCC=1TO35:NEXT:NEXT:GOSUB6:?:?"PRESS space TO PLAY AGAIN"
103 ON-(INKEY$<>" ")GOTO103:RUN
104 PRINT"YOU HAVE QUIT!":GOSUB96:A(Z)=999:A(E+Z)=0
105 IFNO=1THEN102
106 NEXTQ:GOTO30
107 CC=42:GOSUB7:PRINT"END OF GAME.";:GOSUB7:RETURN
108 A$="HE SAYS '"+Q$(RND(5))+"'":GOTO5
109 IFD$(3)="BOAT"THEN112
110 IFLEFT$(D$(3),4)="RIVE"THEN118
111 A$="THERE IS NOTHING TO CLIMB INTO HERE.":GOTO5
112 A$="THE BOAT":IFA(S+13)<>A(Z)THEN95
113 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN115
114 NEXTJ:GOTO94
115 J=18:NEXTJ:A(Z)=M:A(S+13)=M
116 IFA(E+Z)>4THENA$="YOU'VE CROSSED THE RIVER IN SAFETY.":GOTO5
117 PRINT"YOU CROSSED THE RIVER, BUT ARE WEAK AND HAVE LOST SOME MANEUVERABILITY.":GOTO5
118 A$="THE RIVERBOAT":IFA(S+14)<>A(Z)THEN95
119 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN121
120 NEXTJ:GOTO94
121 J=18:NEXTJ:A(Z)=M:A(S+14)=M:GOTO116
122 IFD$(2)="IN"THEN109
123 A$="THERE IS NOTHING HERE TO CLIMB.":GOTO5
124 ON1-(Z>16)GOTO129:IFD$(2)="IN"THEND$(2)=D$(3):D$(3)=D$(4)
125 IFD$(2)="HORSE"THEN130
126 IFD$(2)="CAR"THEN137
127 IFD$(2)="TRAIN"THEN139
128 IFD$(2)="BOAT"ORLEFT$(D$(2),4)="RIVE"THEND$(3)=D$(2):GOTO109
129 A$="YOU ARE NOT ABLE TO RIDE THAT OBJECT.":GOTO5
130 IFA(Z)<>A(S+9)THENA$="THE HORSE":GOTO95
131 F=9:FORJ=1TO4
132 RESTORE:FORC1=1TOA(Z):READV$,V,W,X,Y:NEXTC1:IFLEFT$(D$(3),1)="N"ANDV=1THENA(Z)=A(Z)-13
133 IFLEFT$(D$(3),1)="S"ANDW=1THENA(Z)=A(Z)+13
134 IFLEFT$(D$(3),1)="E"ANDX=1THENA(Z)=A(Z)+1
135 IFLEFT$(D$(3),1)="W"ANDY=1THENA(Z)=A(Z)-1
136 NEXTJ:A(S+F)=A(Z):GOTO87
137 IFA(Z)<>A(S+8)THENA$="THE CAR":GOTO95
138 F=8:FORJ=1TO6:GOTO132
139 IFA(Z)<>A(S+10)THENA$="THE TRAIN":GOTO95
140 IFLEFT$(D$(3),1)="E"ORLEFT$(D$(3),1)="W"THENA$="THE RAILWAY DOES NOT GO IN THAT DIRECTION.":GOTO5
141 F=10:FORJ=1TO9:GOTO132
142 D$(2)=D$(2)+" "+D$(3):FORJ=1TO30:IFA(J)=A(Z)ANDLEFT$(D$(2),4)=LEFT$(A$(J),4)THEN144
143 NEXTJ:A$=D$(2):GOTO95
144 F=J:J=30:NEXTJ:J=F:FORK=1TONO:IFA(T+K)=JTHEN163
145 NEXTK
146 GOSUB161
147 IFI$="Y"THEN149
148 IFI$="N"THENA$="NO BLOWS WERE TAKEN.":GOTO5
149 K=RND(5)-3:KK=RND(5)-3:DD=(A(E+Z)+KK)-(A(E+J)+K):IFDD>-2THEN152
150 A(E+Z)=A(E+Z)-4:PRINT"YOU WERE BADLY INJURED.":IFA(E+Z)<=0THEN160
151 GOTO146
152 IFDD<2THEN156
153 A(E+J)=A(E+J)-4:A$=A$(J)+" WAS BADLY INJURED.":GOSUB1
154 IFA(E+J)<=0THENGOSUB313:GOTO87
155 GOTO146
156 IFDD=-1THENA$="YOU WERE SLIGHTLY INJURED":A(E+Z)=A(E+Z)-1:GOTO159
157 IFDD=1THENA$=A$(J):GOSUB175:A(E+J)=A(E+J)-1:GOTO159
158 GOSUB174
159 GOSUB1:GOTO146
160 A$="IN FACT, YOU HAVE BEEN KILLED!":GOSUB1:GOSUB96:A(Z)=999:NEXTQ:GOTO30
161 PRINT"DO YOU WANT TO ATTACK? ";
162 I$=INKEY$:ON1-(I$="Y"ORI$="N")GOTO162:PRINTI$:RETURN
163 K=NO:NEXTK
164 IFA(E+J)<=0THENA$=A$(J)+" WAS KILLED!"
165 IFA(E+J)<=0THENGOSUB1:GOSUB98:A(J)=999:NEXTQ:GOTO30
166 IFA(E+Z)<=0THENJ=Z:A$=A$(J)+" WAS KILLED!":GOTO165
167 PRINTA$(J)+" DO YOU WANT TO":PRINT"DROP ALL ITEMS AND CONCEDE? ";:GOSUB318:IFI$="Y"THEN179
168 GOSUB161:ON1-(I$<>"Y")GOTO169:ON1-(I$="N")GOTO168,87
169 K=RND(10)-3:KK=RND(10)-3:DD=(A(E+Z)+KK)-(A(E+J)+K):IFDD<2THEN171
170 A(E+J)=A(E+J)-4:A$=A$(J)+" WAS BADLY INJURED.":GOSUB1:GOTO164
171 IFDD>-2THEN176
172 A(E+Z)=A(E+Z)-4:A$=A$(Z)+" WAS BADLY INJURED.":GOSUB1:IFA(E+Z)<=0THEN166
173 GOTO164
174 A$="NO ONE WAS INJURED.":RETURN
175 A$=A$+" WAS SLIGHTLY INJURED.":RETURN
176 IFDD=-1THENA$=A$(Z):GOSUB175:GOSUB1:A(E+Z)=A(E+Z)-1:GOTO164
177 IFDD=1THENA$=A$(J):GOSUB175:GOSUB1:A(E+J)=A(E+J)-1:GOTO164
178 GOSUB174:GOSUB1:GOTO164
179 A$=A$(J)+" CONCEDES. NO MORE BLOWS WERE TAKEN.":GOSUB98:GOTO5
180 A$="I DON'T SEE ANYTHING UNUSUAL ABOUT "+D$(2)+" "+D$(3)+" "+D$(4):GOTO5
181 A$="INVENTORY IS: ":IFA(P+Z)=0THENA$=A$+"EMPTY":GOTO5
182 FORJ=1TO15:IFA(S+J)=200+ZTHENA$=A$+B$(J)+" "
183 NEXTJ:GOTO5
184 I$=LEFT$(D$(2),3):IFI$="BAL"ANDNM=0THEN188
185 IFI$="GRE"ANDZ<17THENA$="BADDIES CANNOT PICK UP THE GRENADES.":GOSUB1:GOTO87
186 F=0:FORJ=1TO15:IFI$=LEFT$(B$(J),3)THENF=J
187 NEXTJ:IFFTHEN189
188 GOTO196
189 IFA(S+F)>200THEN196
190 IFF=8ORF=9ORF=10ORF=13ORF=14THENA$="IT'S TOO HEAVY.":H=5:GOTO5
191 IFF=15ANDA(S+5)<>200+ZTHENA$="YOU HAVE NOTHING TO CARRY IT IN.":H=4:GOTO5
192 IFA(P+Z)>1THENA$="YOU HAVE TOO MANY OBJECTS.":GOTO5
193 IFA(S+F)<>A(Z)THEN196
194 A(E+Z)=A(E+Z)+G(F)
195 A(P+Z)=A(P+Z)+1:A(S+F)=200+Z:GOTO4
196 A$="I DON'T SEE THAT OBJECT.":GOTO5
197 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN199
198 NEXTJ:A$="THERE IS NOWHERE TO SWIM ACROSS HERE!":GOTO5
199 J=18:NEXT:A$="YOU MADE IT ACROSS.":GOSUB1:IFA(P+Z)=0THEN203
200 A$="BUT YOU HAD TO LEAVE ALL THINGS YOU WERE CARRYING BEHIND.":GOSUB1
201 FORK=1TO15:IFA(S+K)=200+ZTHENA(S+K)=A(Z):A(E+Z)=A(E+Z)-G(K)
202 NEXTK:A(P+Z)=0
203 IFA(E+Z)<=0THENGOSUB222:NEXTR
204 A(Z)=M:IFN+1=MTHENA$="YOU SWAM EAST.":GOTO5
205 IFN-1=MTHENA$="YOU SWAM WEST.":GOTO5
206 IFD$(2)="BALLOON"THEN208
207 A$="YOU CANNOT FILL THE "+D$(2)+" WITH "+D$(4)+".":GOTO5
208 IFLEFT$(D$(4),3)="HYD"THEN210
209 A$="YOU CANNOT FILL THE BALLOON WITH THAT.":GOTO5
210 IFA(S+7)<>200+ZTHENA$="YOU DO NOT HAVE THE BALLOON":GOTO5
211 IFA(S+15)<>200+ZTHENA$="YOU DO NOT HAVE THE HYDROGEN":GOTO5
212 A$="THE BALLOON FILLS UP WITH HYDROGEN, A DANGEROUS BUT USEFUL GAS.":GOSUB1
213 A(P+Z)=1:A(S+15)=0:GOSUB214:A(171)=-1:GOTO87
214 B$(7)="INFLATED BALLOON":RETURN
215 F=0:FORJ=1TO15:IFLEFT$(D$(2),3)=LEFT$(B$(J),3)THENF=J
216 NEXTJ:IFFTHEN218
217 A$="YOU'RE NOT CARRYING THAT!":GOTO5
218 IFA(S+F)=200+ZTHENA(S+F)=A(Z):A$="OK":GOSUB1:A(E+Z)=A(E+Z)-G(F):GOTO220
219 GOTO217
220 A(P+Z)=A(P+Z)-1:IFA(E+Z)<=0THENGOSUB222:J=Z:GOTO165
221 GOTO87
222 A$="YOU DIED FROM LACK OF ENERGY.":GOSUB1:RETURN
223 A$="YOU CAN'T GO THAT WAY.":H=3:GOSUB1:GOTO46
224 IFV=0THEN223
225 A(Z)=A(Z)-13:GOTO4
226 IFW=0THEN223
227 A(Z)=A(Z)+13:GOTO4
228 IFX=0THEN223
229 A(Z)=A(Z)+1:GOTO4
230 IFY=0THEN223
231 A(Z)=A(Z)-1:GOTO4
232 IFD$(2)="SHED"THEN234
233 A$="IT'S NOT POSSIBLE TO UNLOCK THAT.":GOTO5
234 IFD$(4)="PEN"THEN236
235 A$="YOU CAN'T UNLOCK THE SHED WITH THAT!":GOTO5
236 IFA(S+3)<>200+ZTHENA$="YOU DON'T HAVE THE PEN.":GOTO5
237 A$="THE SHED IS NOW UNLOCKED.":NM=1:A(S+7)=78:A(S+3)=0:A(P+Z)=A(P+Z)-1:GOTO5
238 PRINT"SAVING":A(166)=NO:A(167)=Z:A(168)=Q:A(169)=R:A(170)=NM:CSAVE*A,"MTNDATA":GOTO55
239 PRINT"LOADING":CLOAD*A,"MTNDATA":NO=A(166):Z=A(167):Q=A(168):R=A(169):NM=A(170):IFA(171)THENGOSUB214
240 GOTO30
241 DATANEAR A SMALL HUT AND A FEW LOGS,,1,1,,ON A SMALL PATH NEXT TO SOME DARK WOODS,,1,1,1
242 DATAON A PATHWAY THAT CONTINUES INTO THE DISTANCE,,,1,1,ON A PATH. IN THE DISTANCE YOU SEE THE SHMAND FAIR,,,1,1
243 DATAAT A DERELICT RAILWAY STATION. THE RAILWAY LINE LEADS SOUTH,,1,1,1
244 DATAON A FLAT MISTY MOOR,,1,,1,ON A FLAT MISTY MOOR,,1,1,
245 DATAIN THE EASTERN PART OF WICHESTER,,1,1,1,IN A MYSTERIOUS GRAVEYARD,,1,1,1
246 DATAIN THE VILLAGE OF WICHESTER. HOUSES LINE THE SIDES OF A DUSTY STREET,,1,1,1
247 DATAON THE EAST SIDE OF WICHESTER,,1,1,1,IN A FIELD OF TURNIPS WITH A SCARECROW NEARBY,,1,,1
248 DATAAT THE BACK OF A CAVE. YOU CAN SEE THE WORDS 'YEK EHT SI NEP EHT',,1,,
249 DATAIN A TUNNEL THROUGH THE CLIFFS THAT SURROUND THE DENSE FOREST,1,1,,,AT THE EDGE OF THE DENSE FOREST,1,1,1,
250 DATA"IN A VALLEY IN A DENSE FOREST, WHICH IS SURROUNDED BY HIGH CLIFFS",,,1,1
251 DATAAT THE NORTH EAST EDGE OF THE DENSE FOREST,,1,1,1,IN A LARGE FIELD WITH A RAILWAY RUNNING THROUGH IT,1,1,1,1
252 DATAIN A FIELD OF BARLEY. YOU CAN SEE THE GOLDEN CITY IN THE DISTANCE,1,,,1
253 DATAIN THE GOLDEN CITY. THE WALLS OF THE BUILDINGS GLEAM IN THE SUNLIGHT,1,1,1,
254 DATAIN SOME FIELDS. YOU SEE A FENCE TO YOUR EAST,1,1,,1
255 DATANEAR A CHURCH,1,1,1,,IN THE SOUTHERNMOST PART OF WICHESTER,1,,1,1
256 DATANEAR WILL'S FATHER'S SAW MILL,1,1,1,1,IN A FIELD OF MAIZE,1,1,,1,JUST INSIDE A DARK CAVE,1,1,,
257 DATANEAR A PART OF THE TUNNEL THAT HAS FALLEN IN,1,,,
258 DATAIN A THICK FOREST,1,1,1,,IN THE MIDDLE OF A DENSE FOREST,,1,1,1
259 DATAIN A DENSE FOREST,1,1,,1,NEXT TO THE RAILWAY,1,1,1,,ON A FLAT MOOR,,1,,1,ON A PLAIN SOUTH OF THE GOLDEN CITY,1,1,1,
261 DATAIN A VAST FIELD OF FLOWERS,1,1,,1,IN THE VILLAGE PARK,1,,1,
262 DATAAT THE VILLAGE ENTRANCE,,1,1,1,IN THE VILLAGE FARMING AREA,1,,1,1
263 DATAAT THE EXTREME EDGE OF THE VILLAGE FARMLAND,1,,,1,AT THE ENTRANCE TO A CAVE,1,1,,
264 DATAAT A PART OF THE TUNNEL THAT HAS FALLEN IN,,1,,,APPROACHING THE HIGH CLIFFS THAT SURROUND THE FOREST,1,,1,
265 DATANEAR THE HIGH CLIFFS THAT SURROUND THE FOREST,1,,1,1,APPROACHING THE FOREST'S EDGE,1,,1,1
266 DATANEAR THE RAILWAY THAT PASSES THE FOREST HERE,1,1,1,1,NEAR AN OLD DEEP PIT,1,1,,1
267 DATANEXT TO A LARGE TRIPOD FOOTPRINT,1,1,,,NEXT TO A HERD OF GOATS,1,1,1,
268 DATANEXT TO A WALL WITH VINES GROWING ALL OVER IT,,1,,1,APPROACHING THE VILLAGE,1,1,1,
269 DATANEAR A LARGE FIELD,,1,1,1,NEAR A FIELD,,1,1,1
270 DATAIN A SMALL GLEN. YOU SEE A CAVE TO THE NORTH,1,1,,1,SOUTH OF THE LARGE CLIFFS,1,1,1,
271 DATAAPPROACHING THE LARGE CLIFFS,,1,1,1,APROACHING THE REMAINS OF A TALL PYLON,,1,1,1
272 DATAIN FRONT OF A SIGN SAYING 'HI;H =+-T.^E',,1,1,1,IN FRONT OF A LARGE ROCK RESEMBLING A PEN (OR IS IT A KEY?),1,1,,1
273 DATAON A FLAT PLAIN NEXT TO THE CANAL,1,1,,,NEXT TO SOME HOOF MARKS,1,1,,,ON A BLEAK MOOR,1,1,1,
274 DATAON A BARREN ROCK NEXT TO A SHARP DROP TO THE SOUTH,1,,1,1
275 DATASTANDING ON A WELL TRODDEN PATH,1,,1,1,VIEWING MOUNTAINS GLISTENING IN THE DISTANCE,1,,1,1
276 DATAIN THE REMAINS OF A GREAT CITY,1,1,1,1,SURROUNDED BY HIGH MOUNTAINS,1,1,,1
277 DATASTANDING ON A WHITE DOTTED LINE,1,1,1,,NEAR AN OLD WAREHOUSE,1,1,1,1
278 DATAIN A DRY RIVER BED,1,1,1,1,NEXT TO THE REMAINS OF A BRIDGE,1,1,1,1
279 DATAOUTSIDE AN OLD RAILWAY STATION,1,,1,1,VIEWING THE CANAL GLISTENING IN THE SUN,1,1,,1
280 DATAAT A PUB ALONGSIDE THE CANAL,1,1,1,,NEXT TO THE REMAINS OF A LARGE BOAT,1,1,1,1
281 DATANEAR THE REMAINS OF A TRIPOD,,1,1,1,NEAR THE REMAINS OF A DEAD SHEEP,,1,1,1
282 DATANEAR A LARGE MAN-MADE TUNNEL,,,1,1,IN A SMALL GREEN VALLEY,1,1,1,1
283 DATAVIEWING SOME GLOWING LIGHT BULBS IN AN OLD SHED. YOU WONDER WHAT POWERS THEM,1,1,,1
284 DATAIN A FLAT AND BARREN AREA,1,1,1,,IN A CITY OF THE ANCIENTS,1,,1,1
285 DATASURROUNDED BY LARGE BUILDINGS,1,1,,1,SURROUNDED BY MANY LONG CARRIAGES,1,1,1,
286 DATAVIEWING THE CITY OF THE ANCIENTS. IT LOOKS DARK AND MYSTERIOUS,,,1,1
287 DATAIN A SMALL MEADOW BY THE CANAL,1,1,,1,LISTENING TO THE CANAL HORSES,1,1,1,
288 DATAON AN OLD TRACK,1,1,1,1,IN A PLACE WHERE THE OLD TRACK DIPS A LITTLE,1,1,1,1
289 DATAIN A VILLAGE BY THE MOUNTAINS,1,1,1,1,ON TOP OF A LARGE MOUNTAIN. YOU CAN SEE A LIGHT TO THE EAST,,1,1,1
290 DATAIN THE FOOTHILLS OF THE MOUNTAINS,1,1,1,1,IN A LARGE FLAT VALLEY,1,1,,1
291 DATANEAR A TALL CHIMNEY,1,,1,,IN A LARGE FACTORY,,,1,1
292 DATASURROUNDED BY TALL SKYSCRAPERS,1,,1,1,NEAR SOME LARGE WHITE BUILDINGS,1,,1,1
293 DATASUDDENLY IN DARKNESS,,,1,1,ON A WEIRD FLAT METALLIC SURFACE,1,,,1
294 DATANEXT TO THE CANAL,1,,1,,VIEWING THE SEA DOWN A LONG DROP,1,,,1
295 DATANEXT TO A STONE WALL,1,,1,,IN A FOGGY PLACE WHERE THE AIR IS THIN,1,,1,1
296 DATASURROUNDED BY HIGH MISTY MOUNTAINS,1,,1,1
297 DATAIN A FOG SHROUDED PLACE. IT IS RAINING HARD,1,,1,1,IN AN UNBEARABLE DOWNPOUR,1,,,1
298 F=0:FORJ=30TO17STEP-1:IFA(Z)=A(J)ANDZ<>JANDZ<17THENF=J
299 NEXTJ:IFFTHENJ=F:GOTO303
300 F=0:FORJ=1TO17:IFA(Z)=A(J)ANDJ<>ZANDZ>16THENF=J
301 NEXTJ:IFFTHENJ=F:GOTO303
302 RETURN
303 F=0:FORK=1TONO:IFA(T+K)=JTHENF=1
304 NEXTK:IFFTHENRETURN
305 A$="YOU ARE BEING ATTACKED BY":GOSUB1:A$=A$(J):GOSUB1
306 K=RND(8)-5
307 DD=(A(E+Z)+5)-(A(E+J)+K):IFDD>-2THEN310
308 A$="YOU WERE BADLY INJURED.":A(E+Z)=A(E+Z)-4:GOSUB1:IFA(E+Z)<=0THENFF=3:RETURN
309 GOTO316
310 IFDD<2THEN314
311 A$=A$(J)+" WAS BADLY INJURED.":GOSUB1:A(E+J)=A(E+J)-4:IFA(E+J)<=0THENFF=1:GOTO313
312 GOTO316
313 A$="IN FACT, "+A$(J)+" IS DEAD.":GOSUB1:GOSUB98:A(J)=999:RETURN
314 IFDD=-1THENA$="YOU WERE SLIGHTLY INJURED.":GOSUB1:A(E+Z)=A(E+Z)-1:GOTO316
315 IFDD=1THENA(E+J)=A(E+J)-1:A$=A$(J)+" WAS SLIGHTLY INJURED.":GOSUB1:GOTO316
316 PRINT"DO YOU WANT TO DROP ALL ITEMS   AND CONCEDE? ";:GOSUB318:IFI$="Y"THENFF=2:RETURN
317 GOTO306
318 I$=INKEY$:ON1-(I$="Y"ORI$="N")GOTO318:PRINTI$:RETURN
319 IFA(Z)<>20THENA$="YOU CANNOT FIRE HERE TO WIN THE GAME.":GOTO5
320 IFA(S+6)<>200+ZTHEN322
321 IFZ>16ANDB$(7)="INFLATED BALLOON"ANDA(S+7)=200+ZTHENGOSUB107:A$=A$(Z)+" HAS WON!":GOSUB1:GOTO102
322 A$="YOU DO NOT HAVE THE RIGHT THING TO FIRE WITH.":GOTO5
323 RESTORE:FORC1=1TOA(K):READV$,VV,WW,XX,YY:NEXTC1:KK=RND(4):IFKK=1ANDVV=1THENA(K)=A(K)-13
324 IFKK=2ANDWW=1THENA(K)=A(K)+13
325 IFKK=3ANDXX=1THENA(K)=A(K)+1
326 IFKK=4ANDYY=1THENA(K)=A(K)-1
327 RETURN
328 PRINT@64,"ﬂﬂﬂœœœﬂﬂﬂﬂﬂﬂﬂœœﬂﬂﬂﬂﬂÿ›ﬂﬂœœﬂﬂﬂﬂﬂﬂﬂœœœﬂﬂﬂﬂﬁ›ﬂﬂﬂﬂœœœﬂﬂ¡œ ›ﬂﬂﬂﬂﬂﬂﬂœﬂﬂﬂﬂﬂﬂﬂﬂ‹«À›ﬂﬂﬂﬂﬂﬂﬁ√œŒœÀ√‹ﬂﬂﬂﬂœœœ";
329 PRINT"œﬂﬂﬂﬂﬁ√œŒ«À√‹›ﬂﬂﬁ«œŒ««œœœ√√‹ﬂﬂﬂﬂﬂﬂﬂﬂﬁÊÔÔÍÔÔÔÔÎ„‹ÁÔÓÁÔÔÔÈÊÔÔÔ„‹ﬂﬂﬁ„‚„ÁÎÏÔÔÓÔÔÔÔÔÔÓÌÔÔÔÔÈÔÔÊÌÔÔÔ‘ﬂ";
330 PRINT"ÁÔÁÔÔÍÔÔÔÂÔÔÔÏÏÏÏÈÌÔÔÔÔÔÔÔÔÔÔÈÓ„ÏÁÔÔÓÉÉÉÉÉÉÉÉèèéåãÉÉÉÉÏÏÏÏÏÔÔÔÔÊÏÏ‰ÉáèèèèèèèèèéïüÑèèèèèèèèèÉÉÉÏÏ";
331 PRINT"Ø™èèèøèèèèèèèèÉÅÅëáèèèèèèèèèèèˇˇÆ®èèøøøèèèèèèèäèÖäüèèèèèèèèèøèèˇ™èèèèèèèèèèèèéÑèÖéÑüøèèèèèèøøøèè";
332 PRINT"áèèèèèèèèèèèèèèéÑèèøøø",,,,:GOSUB11:DIMA$(30),A(171),B$(16),G(16),S$(30),D$(5),N(18),M(18),Q$(5),H$(5):E=30:G=60
333 P=90:S=150:T=120:FORJ=1TO104:READV$,V,W,X,Y:NEXT:FORX=1TO30:READA$(X):NEXT:FORJ=1TO30:READA(J):NEXT:FORJ=1TO30:READA(E+J)
334 NEXT:FORJ=1TO30:READA(G+J):NEXT:FORX=1TO15:READB$(X):NEXT:FORJ=1TO15:READA(S+J):NEXT:FORJ=1TO15:READG(J):NEXT:FORJ=1TO18
335 READN(J),M(J):NEXT:FORJ=1TO5:READQ$(J):NEXT:FORJ=1TO5:READH$(J):NEXT:PRINT@455,"BY PAUL BRAITHWAITE",:GOSUB8:RETURN
336 DATASMALL MASTER,"DREADFUL MASTER",VIOLENT MASTER,GREEDY MASTER,FAT MASTER,BIG MASTER,UGLY MASTER,TALL TRIPOD,MEAN TRIPOD
337 DATAWEAK TRIPOD,STRONG TRIPOD,SHINY TRIPOD,SLOW TRIPOD,LITTLE TRIPOD,HUGE TRIPOD,INJURED TRIPOD,LARGE VAGRANT
338 DATACLEVER VAGRANT,SCRUFFY VAGRANT,NUTTY NOMAD,OLD NOMAD,WEEDY NOMAD,HENRY,BENPOLE,WILLIAM,WISEMAN,MISTER PARKER
339 DATAOZYMANDIS,CAPTAIN CURTAS,COMTESS
340 DATA20,20,20,20,20,20,20,6,7,8,19,21,32,33,34,100,94,15,36,4,76,41,10,1,11,78,23,24,73,47,8,7,9,8,7,6,8,8,9,4,9,9,6,5,9
341 DATA6,6,8,6,7,4,2,8,6,8,7,8,7,6,4,8,5,10,7,4,8,9,8,6,4,9,9,5,6,10,5,7,8,6,7,6,6,8,7,8,5,9,7,7,4
342 DATASWORD,KNIFE,PEN,WATCH,BAG,GRENADE,BALLOON,CAR,HORSE,TRAIN,FLICKNIFE,ROPE,BOAT,RIVERBOAT,HYDROGEN
343 DATA1,93,40,81,10,80,,82,50,5,99,44,7,72,13,17,16,,,2,20,15,9,8,6,5,1,1,,9
344 DATA6,7,7,6,19,20,20,19,32,33,33,32,45,46,46,45,58,59,59,58,71,72,72,71,84,85,85,84,97,98,98,97,110,111,111,110
345 DATAHELLO,NICE WEATHER ISN'T IT,NOTHING,SHUT UP!,GO JUMP IN A LAKE,TRY OTHER WORDS
346 DATANO HELP AVAILABLE NOW,IT IS POSSIBLE TO GET ACROSS,GET SOMETHING TO CARRY IT IN,IT'S NO USE TRYING
