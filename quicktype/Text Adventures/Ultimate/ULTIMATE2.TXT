0 GOTO10
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 REM BY PHIL EDWARDSON COPYRIGHT (C) CLOAD 1982
20 X=RND(-(PEEK(9)*256+PEEK(10))):CLEAR60:CLS:DIMC1,CC,M$:MC=16384:L=1:D=250:S=250:CL=1
40 DIMI$(11),O$(11),V(11),L$(29),M$(18),M(18),T$(7),D$(4,29),D(4,29),U(11),U1(37),K$(8),Q$(3),Q(3),X(1),Y(1)
45 PRINT@227,"DO YOU WANT INSTRUCTIONS?";
46 A$=INKEY$:IFA$=""THEN46
47 PRINTA$:IFA$="Y"THENGOSUB4000
48 IFA$<>"N"THEN45
50 CLS:PRINT@165,"ULTIMATE ADVENTURE"
60 FORX=1TO11:READI$(X),O$(X),U1(11+X),V(X),U(X):U1(X)=U(X):NEXT
70 FORX=1TO15:READL$(X):NEXT
80 FORX=16TO29:L$(X)="IN A PORTHOLE":NEXT
100 GOTO150
110 FORX=1TO5
120 U1(22+X)=RND(11):IFU1(22+X)=1THEN120
130 NEXTX
140 U1(22+6)=7:U1(22+7)=12:RETURN
150 FORX=1TO7:READT$(X):NEXT
160 FORY=1TO29:FORX=1TO4:READD$(X,Y),D(X,Y):NEXTX:NEXTY
170 FORX=1TO18:READM$(X),M(X):NEXT
180 PRINT@229,"CONTINUE OLD GAME";:INPUTC$
190 IFLEFT$(C$,1)="Y"THEN1920
191 PRINT@480,"ENTER LEVEL 1-5 (1=EASIEST) ";
200 C$=INKEY$:HD=VAL(C$):IFHD=0ORHD>5THEN200
210 PRINTC$;:GOSUB2900:GOSUB110
220 GOSUB3120
230 MV=MV+1:CLS
240 IFS<1THEN3090
250 PRINT"strength";S,"money $";D
260 IFS<30THENGOSUB2700
270 PRINT:PRINT"location";"        ";"items";I:PRINTL$(L)
280 PRINT:PRINT"directions"
290 FORX=1TO4
300 IFD$(X,L)="-"THEN320
310 PRINTD$(X,L);"  ";
320 NEXTX:PRINT
330 PRINT:M$="YOU CAN SEE ":GOSUB7
340 IFL=1THENPRINT"MARKET CONTENTS":GOTO400
350 IFS>240THEN360
351 IFL=13THENPRINT"STRENGTH RECOVERED":S=250:GOSUB2830:GOTO220
360 REM 
370 FORX=1TO11:IFU1(11+X)=L THENPRINTO$(X)
380 NEXTX
390 GOTO570
400 IFL=1ANDD>999THEN3010
410 GOTO2920
420 PRINT:SOUND160,4:SOUND185,1:INPUT"NOW WHAT";C$
430 SOUND180,1
440 B$=LEFT$(C$,3)
450 IFC$="LIST CONTENTS"THEN860
460 IFB$="BUY"THEN940
470 IFC$="LIST LOAD"THENGOSUB1040:GOTO400
480 IFB$="GO "THEN1060
490 IFB$="SEA"THEN1370
500 IFC$="GET TREASURE"THEN1490
510 IFC$="CLEAR SCREEN"THENMV=MV-1:GOTO220
520 IFB$="DRO"THEN1700
530 IFB$="GET"THEN1830
540 IFB$="SWI"THENB$=MID$(C$,6):GOTO1090
550 IFC$="SAVE GAME"THEN2050
560 PRINT"DON'T UNDERSTAND":GOTO400
570 REM 
580 IFRND(20-HD)>5THEN400
590 A=0:B=0:N=0:B$="":Z=0:Q1=0:W=0
600 FORX=1TO18
610 IFM(X)=L THENB=B+1:Q$(B)=M$(X):Q(B)=X
620 NEXTX
630 IFB=0THEN400
640 AT=1:P=RND(B):Q$=Q$(P):Q1=Q(P):PRINTQ$
650 GOSUB2840
660 INPUT"USE WHAT";C$
670 IFC$="LIST LOAD"THENGOSUB1040:PRINT:GOTO660
680 IFLEFT$(C$,3)="GO "THENPRINT"CAN'T MOVE WHILE UNDER ATTACK!":GOSUB2860:GOTO660
690 FORX=1TO11
700 IFC$=I$(X)ANDU1(11+X)<>0THENPRINT"DON'T HAVE ";C$:GOTO770
710 IFC$=I$(X)ANDU1(11+X)=0THENW=X:U1(X)=U1(X)-1:GOTO730
720 NEXTX
730 Z=((W-1)*18)+Q1
740 IFZ<1OR Z>198THEN A=0:GOTO770
750 B$=MID$(A$,Z,1)
760 A=ASC(B$)-50
770 IFC$="HANDS"THENA=5
780 P=INT(RND((HD*35)/2))-A
790 IFQ1=9ANDP>0THEN2710
800 IFP<1THENP=0:PRINT"YOU ESCAPED WITHOUT INJURY!":GOSUB2910:GOSUB2900:GOTO220
810 S=S-P
820 GOSUB2850
830 PRINT"YOU LOST";P;"UNITS OF STRENGTH":PRINT"DURING THE ENCOUNTER"
840 REM 
850 AT=0:GOSUB2900:GOTO220
860 IFL<>1THENPRINT"HAVE TO BE IN MARKET":GOTO400
870 CLS:M$="MARKET CONTENTS":GOSUB7:PRINT"  YOU HAVE $";D
880 PRINT@64,"ITEM","  PRICE  USES"
890 FORX=1TO11
900 IFU1(11+X)=1THENPRINTO$(X),"  ";:PRINT"$";MID$(STR$(V(X))+"   ",2,3);:PRINT"    ";U1(X)
910 NEXTX
920 IFCL=1THENPRINT"CLUE","  ";:PRINT"$";MID$(STR$(D)+"   ",2,3);:PRINT"    ";1:IFI=0THENPRINT@478,;
930 GOTO400
940 IFL<>1THENPRINT"CAN ONLY BUY FROM MARKET":GOSUB2860:GOTO400
950 B$=MID$(C$,5)
960 IFB$="CLUE"THENGOSUB3270:GOTO400
970 IFI>7THENPRINT"ALREADY HAVE MAXIMUM LOAD":GOSUB2860:GOTO400
980 FORX=1TO11
990 IFB$=I$(X)ANDU1(11+X)=0THEN1880
1000 IFB$=I$(X)ANDU1(11+X)=1ANDD<V(X)THENPRINT"NEED MORE MONEY!":GOSUB2860:GOTO400
1010 IFB$=I$(X)ANDU1(11+X)=1ANDD=>V(X)THENU1(11+X)=0:D=D-V(X):I=I+1:S=S-2:GOTO220
1020 NEXTX
1030 GOTO560
1040 FORX=1TO11:IFU1(11+X)=0THENPRINT"U";U1(X);" ";O$(X)
1050 NEXTX:RETURN
1060 B$=MID$(C$,4)
1070 IFU1(11+5)=0THEN1090
1080 IF(L=28ANDB$="W")OR(L=26ANDB$="E")OR(L=22ANDB$="S")THENPRINT"IT'S TOO DARK":GOTO1290
1090 IFL=11ANDB$="D"ANDU1(11+8)<>0THENPRINT"NEED DIVING GEAR":GOTO1290
1100 IFL=11ANDB$="D"ANDU1(14)=0THENPRINT"CAN'T DIVE WITH RAFT":GOTO1290
1110 IFU1(11+4)=0THEN1130
1120 IF(L=19ANDB$="E")OR(L=22ANDB$="N")OR(L=23ANDB$="NW")THENPRINT"CAN'T - NO AIR":GOTO1290
1130 IFU1(14)=0ORLEFT$(C$,3)="SWI"THEN1160
1140 IF(B$="S"ANDL=10)OR(B$="W"ANDL=29)THENPRINT"NEED SOMETHING TO FLOAT ON":GOTO1290
1150 IFL=11ORL=12THENPRINT"HAVE TO SWIM":GOTO1290
1160 IFU1(11+5)<>0THEN1180
1170 IF(L=28ANDB$="W")OR(L=26ANDB$="E")OR(L=22ANDB$="S")THENU1(5)=U1(5)-1
1180 IFL=11ANDU1(11+8)=0ANDB$="D"THENU1(8)=U1(8)-1
1190 IFU1(11+4)<>0THEN1210
1200 IF(L=19ANDB$="E")OR(L=22ANDB$="N")OR(L=23ANDB$="NW")THENU1(4)=U1(4)-1
1210 IFU1(14)<>0THEN1230
1220 IF(L=10ANDB$="S")OR(L=29ANDB$="W")THENU1(3)=U1(3)-1
1230 FORX=1TO4
1240 IFLEFT$(C$,3)="SWI"THEN1260
1250 IFB$=D$(X,L)THENL=D(X,L):S=S-2:GOTO220
1260 IFB$=D$(X,L)THENL=D(X,L):S=S-7:GOTO220
1270 NEXTX
1280 GOTO1360
1290 GOSUB2860:GOTO400
1300 PRINT"RAFT DRIFTS AWAY!!":GOSUB2840:U1(14)=10
1310 FORX=1TO11
1320 IFX=3THEN1340
1330 IFU1(11+X)=11THENU1(11+X)=10
1340 NEXTX
1350 GOTO400
1360 PRINT"CAN'T GO THAT WAY":GOSUB2860:GOTO400
1370 F=0
1380 IFU1(14)<>0AND(L=11ORL=12)THENS=S-7
1390 IFU1(14)=11ANDRND(3)=1THEN1300
1400 FORE=1TO7
1410 P=RND(HD+6)
1420 IFU1(22+E)=L ANDP<5THENT$=T$(E):F=1:GOTO1440
1430 NEXTE
1440 GOTO2200
1450 IFF=1THENPRINT@167,"TREASURE FOUND!";:GOSUB2870:GOTO1480
1460 NEXTE
1470 PRINT@167,"YOU FOUND NOTHING";
1480 S=S-3:GOSUB2900:GOTO220
1490 B$=LEFT$(T$,3):IFF=1ANDL=12ANDB$="SUN"THENN=300:A=100:GOTO1530
1500 IFF=1ANDL=7ANDB$="BUR"THENN=250:A=100:GOTO1530
1510 IFF=0THENS=S-3:PRINT"YOU FORGOT WHERE IT WAS":GOSUB2860:GOTO400
1520 N=100:A=50
1530 IFL=7ANDB$="BUR"ANDU1(11+10)<>0THENPRINT"BURIED - NEED SHOVEL":GOSUB2860:GOTO400
1540 IFI>7THEN1840
1550 IFL=7ANDLEFT$(T$,3)="BUR"ANDU1(21)=0THENU1(10)=U1(10)-1
1560 P=RND(N)+A
1570 PRINT"YOU HAVE FOUND A":PRINTT$;" WORTH $";P:U1(22+E)=0:CL=1:I=I+1:GOSUB2830
1580 D=D+P:F=0:S=S-3
1590 REM 
1600 FORX=1TO7
1610 IFU1(22+X)<>0THENGOSUB2900:GOTO220
1620 NEXTX
1630 PRINT"ALL TREASURES HAVE BEEN FOUND":GOSUB2870
1640 P=RND(500)+200
1650 PRINT"YOU HAVE EARNED A $";P;"BONUS"
1660 I=I-7
1670 D=D+P:PRINT:PRINT"NEW TREASURES NOW EXIST":GOSUB2830
1680 GOSUB110
1690 GOSUB2830:GOTO220
1700 IFL=1ORL>12THENPRINT"CAN'T DROP ITEMS HERE":GOSUB2860:GOTO400
1710 B$=MID$(C$,6)
1720 FORX=1TO11
1730 IFB$=I$(X)ANDU1(11+X)=0THEN1760
1740 NEXTX
1750 PRINT"DON'T HAVE ";B$:GOSUB2860:GOTO400
1760 REM
1770 IFU1(14)=0ANDL=11THEN1810
1780 IFX<>3ANDU1(14)<>11ANDU1(14)<>0ANDL=11THEN1785
1881 GOTO1790
1785 PRINT"YOUR ";B$:PRINT"SANK TO THE BOTTOM":I=I-1:S=S-1:GOSUB2880:U1(11+X)=12:GOTO1820
1790 IF(X=5ANDL=8)OR(X=8ANDL=12)OR(X=4ANDL=6)THENPRINT"TOO DANGEROUS TO ";C$:GOSUB2860:S=S-1:GOTO1820
1800 IFL=12ANDX=3THEN1300
1810 U1(11+X)=L:S=S-1:I=I-1
1820 GOSUB2900:GOTO230
1830 IFL=1THENPRINT"CAN ONLY BUY FROM MARKET":GOSUB2860:GOTO400
1840 IFI>7THEN970
1850 IFL=6ANDRND(10)<7THENPRINT"CAN'T QUITE GRAB IT":GOSUB2860:S=S-2:GOSUB2900:GOTO220
1860 B$=MID$(C$,5)
1870 FORX=1TO11
1880 IFB$=I$(X)ANDU1(11+X)=0THENPRINT"ALREADY HAVE THE ";I$(X):GOSUB2860:GOTO400
1890 IFB$=I$(X)ANDU1(11+X)=L THENU1(11+X)=0:I=I+1:S=S-1:GOTO220
1900 NEXTX
1910 GOTO560
1920 REM
1930 CLS:PRINT"LOAD OLD GAME":PRINT
1940 PRINT"POSITION TAPE"
1950 PRINT"PRESS 'PLAY'"
1980 CLOAD*U1,"ULTDATA":L=U1(30):S=U1(31):D=U1(32):I=U1(33):MV=U1(34):F=U1(35):CL=U1(36):HD=U1(37):GOTO220
2050 CLS:PRINT"SAVE GAME TO TAPE":PRINT
2060 PRINT"POSITION TAPE"
2070 PRINT"PRESS 'PLAY' & 'RECORD'"
2080 PRINT"HIT 'ENTER' WHEN READY"
2090 IFINKEY$=""THEN2090
2100 U1(30)=L:U1(31)=S:U1(32)=D:U1(33)=I:U1(34)=MV:U1(35)=F:U1(36)=CL:U1(37)=HD:CSAVE*U1,"ULTDATA"
2190 PRINT"COMPLETED";:END
2200 REM
2220 CLS0:X(0)=32:Y(0)=16
2240 H=RND(63):V=RND(31)
2250 FORJ=1TO8
2260 G=RND(8):Z=RND(5)+2
2270 IFG=1THENH=H-RND(5)-Z:V=V-RND(5)-Z
2280 IFG=2THENV=V-RND(5)-Z
2290 IFG=3THENH=H+RND(5)+Z:V=V-RND(5)-Z
2300 IFG=4THENH=H+RND(5)+Z
2310 IFG=5THENH=H+RND(5)+Z:V=V+RND(5)+Z
2320 IFG=6THENV=V+RND(5)+Z
2330 IFG=7THENH=H-RND(5)-Z:V=V+RND(5)+Z
2340 IFG=8THENH=H-RND(5)-Z
2350 IFH<1THENH=1
2360 IFV<1THENV=1
2370 IFH>63THENH=63
2380 IFV>31THENV=31
2390 X(1)=H:Y(1)=V:C=5:GOSUB6400:X(0)=H:Y(0)=V
2400 ONJGOSUB2470,2480,2490,2500,2510,2520,2530,2540
2410 NEXTJ
2420 IFF<>1THEN1470
2430 SET(H,V,7):GOSUB2870
2440 SET(H,V,8)
2450 SET(H,V,4):GOSUB2870
2460 GOTO1450
2470 SOUNDRND(200),1:RETURN
2480 SOUNDRND(10),1:RETURN
2490 SOUNDRND(10)+10,1:RETURN
2500 SOUNDRND(100)+100,1:RETURN
2510 GOTO2470
2520 GOTO2480
2530 GOTO2490
2540 GOTO2500
2550 RETURN
2560 DATAGUN,LARGE GUN,1,80,4,BOOTS,HIP BOOTS,1,40,3,RAFT,INFLATABLE RAFT,1,150,5,SUIT,SPACE SUIT,1,200,5,FLASHLIGHT
2561 DATAFLASHLIGHT,1,10,6,COAT,FUR COAT,1,80,3,MINESWEEPER,MINESWEEPER,1,100,3
2570 DATAGEAR,DIVING GEAR,1,150,4,KNIFE,HUNTING KNIFE,1,40,5,SHOVEL,SHOVEL,1,15,4,ROPE,ROPE,1,10,3
2580 DATAIN THE MARKET,WADING THROUGH A SWAMP,IN THE ARCTIC,IN A DENSE JUNGLE,IN AN EMPTY ROOM
2581 DATAFLOATING THROUGH OUTER SPACE,CRAWLING THROUGH THE DESERT,IN A DARK CAVE,WALKING THROUGH A MINE FIELD
2582 DATAAT THE BEACH,ON THE OCEAN,SWIMMING UNDERWATER
2590 DATAIN THE INFIRMARY,AT THE SOUTH DOOR OF MARKET,AT THE EAST DOOR OF MARKET
2600 DATAVALUABLE RING,BAG OF GOLD,BOX OF JEWELS,BOX OF COLLECTABLES,RAFFLE TICKET,BURIED CHEST OF TREASURES
2601 DATASUNKEN TREASURE
2610 DATAS,14,E,15,-,1,-,1,S,16,W,15,E,17,-,2,NW,17,S,20,-,3,-,3,N,14,NE,18,SE,21,S,25,N,16,W,18,S,24,E,19,W,19
2611 DATASW,22,SE,23,-,6
2620 DATAN,24,W,21,SE,26,-,7,N,22,SW,26,SE,28,-,8,N,20,NW,23,S,27,-,9,N,25,S,11,-,10,-,10,N,10
2621 DATASE,29,D,12,-,11,U,11,-,12,-,12,-,12,NW,28,SW,29,N,27,-,13
2630 DATAN,1,S,4,-,14,-,14,E,2,W,1,-,2,-,2,N,2,S,5,-,16,-,16,W,2,E,3,-,17,-,17,W,4,E,5,-,18,-,18,W,5
2631 DATAE,6,-,19,-,19,N,3,S,9,-,20,-,20,W,4,E,7,-,21,-,21,N,6,S,8,-,22,-,22,NW,6,SE,9,-,23,-,23,N,5
2632 DATAS,7,-,24,-,24,N,4,S,10,-,25,-,25
2640 DATAW,7,E,8,-,26,-,26,N,9,S,13,-,27,-,27,W,8,E,13,-,28,-,28,W,11,E,13,-,29,-,29
2650 DATAALLIGATOR ATTACKING,2,STUCK IN QUICK SAND,2,POLAR BEAR CHARGING,3,YOU'RE GETTING COLD,3
2651 DATATIGER JUMPING,4,RHINO CHARGING,4,A PYTHON SLITHERING AROUND YOU,4,METEORS ARE BOMBARDING YOU,6
2652 DATABANDITS APPROACHING,7
2660 DATAGIANT TARANTULA LEAPING,7,YOU'RE GETTING HOT,7,LAND MINES NEARBY,9,A BARRACUDA NEARBY,12
2661 DATASHARKS ATTACKING,11,GIANT OCTOPUS GRABS YOU,12,BATS DIVING AT YOU,8,ROOF OF CAVE FALLING IN,8
2662 DATAYOU FELL IN A PIT,8
2670 REM
2680 IFS<1THEN3090
2690 IFS>20THENRETURN
2700 PRINT"STRENGTH LEVEL DANGEROUSLY LOW!"
2701 FORX=1TO3:FORY=1TO150STEP15:SOUNDY,1:NEXTY:POKE49151,64:FORY=1TO60:NEXTY:NEXTX:POKE49151,16:RETURN
2710 PRINT"YOUR ATTEMPT FAILED!"
2720 Z=0
2730 FORX=1TO11
2740 IFU1(11+X)=0THENZ=Z+1:K$(Z)=O$(X)
2750 NEXTX
2760 P=INT(RND(D))
2770 PRINT"BANDITS STOLE $";P;"FROM YOU":GOSUB2850:D=D-P
2780 P=RND(Z)
2790 IFZ>0THENPRINT"AND YOUR ";K$(P)
2800 FORX=1TO11:IFO$(X)=K$(P)THEN U1(11+X)=1:U1(X)=U(X):I=I-1
2810 NEXTX
2820 GOTO840
2830 FORX=1TO250STEP10:SOUNDX,1:NEXTX:RETURN
2840 FORX=1TO3:FORY=150TO230STEP10:SOUNDY,1:NEXT:POKE49151,64:FORZ1=1TO30:NEXT:NEXT:POKE49151,16:RETURN
2850 FORX=1TO3:SOUND1,6:FORY=1TO10:NEXTY:NEXTX:RETURN
2860 FORX=1TO3:SOUND220,4:FORY=1TO30:NEXTY:NEXTX:RETURN
2870 FORX=1TO7:SOUND(RND(40)+200),1:NEXTX:RETURN
2880 FORY=201TO1STEP-10:SOUNDY,1:NEXTY:RETURN
2890 FORX=1TO2:SOUNDRND(255),1:NEXTX:RETURN
2900 FORN=1TO1200:NEXTN:RETURN
2910 FORX=100TO210STEP15:SOUNDX,1:NEXTX:FORX=200TO100STEP-15:SOUNDX,1:NEXTX:RETURN
2920 IFL<16ORRND(25)>3THEN420
2930 PRINT"PORTHOLE MALFUNCTION!!"
2940 FORX=1TO30:SOUNDRND(250),1:NEXTX
2950 L=RND(13)
2960 IFL=12ANDU1(19)<>0THENL=11
2970 IFL=6ANDU1(15)<>0THEN2950
2980 IFL=8ANDU1(16)<>0THEN2950
2990 PRINT"YOU HAVE BEEN TELEPORTED":PRINT"TO ";L$(L)
3000 GOSUB2900:GOTO220
3010 GOSUB2900:CLS:FORX=1TO5:CLS(RND(8))
3020 PRINT@165,"YOU HAVE SUCCESSFULLY";:PRINT@201,"COMPLETED THE";:PRINT@230,"ULTIMATE ADVENTURE!";
3030 FORY=150TO200STEP5
3040 SOUNDY,1:NEXTY:NEXTX
3050 PRINT@294,"TOTAL MOVES =";MV;
3060 PRINT@480,"   >>> CONGRATULATIONS <<<      ";
3070 IFINKEY$=""THEN3070
3080 END
3090 CLS:PRINT@164,"TOO BAD, YOU GOT KILLED!";
3100 SOUND60,12:SOUND1,24
3110 GOTO3070
3120 FORZ=1TO11
3130 IF(Z>1ANDZ<5)ORZ>7THEN3200
3140 IFU1(11+Z)=12OR(L=12ANDU1(11+Z)=0)THEN3230
3150 IFU1(14)=11ANDU1(11+Z)=11THEN3200
3160 IFL=11ANDU1(11+3)=0ANDU1(11+Z)=11THEN3200
3170 IFL=11ANDU1(14)<>0ANDU1(11+Z)=0THEN3230
3180 IFU1(11+Z)=11ANDU1(14)<>11THEN3190
3181 GOTO3200
3190 PRINT"YOUR ";O$(Z):PRINT"SANK TO THE BOTTOM":GOSUB2880:U1(11+Z)=12:GOTO3130
3200 IFU1(Z)<1ANDU1(11+Z)=0THENU1(11+Z)=1:U1(Z)=U(Z):I=I-1
3210 NEXTZ
3220 RETURN
3230 PRINT"YOUR ";O$(Z):PRINT"GOT RUINED BY THE WATER!":GOSUB2840:GOSUB2900
3240 IFU1(11+Z)=0THENU1(11+Z)=1:U1(Z)=U(Z):I=I-1
3250 IFU1(11+Z)=12THENU1(11+Z)=1:U1(Z)=U(Z)
3260 GOTO3200
3270 IFCL<>1THENY=RND(7):GOTO3275
3271 IFU1(22+Y)=0THEN3270
3272 PRINT:PRINT"clue":PRINT"YOU WILL FIND TREASURE WHILE":PRINTL$(U1(22+Y)):PRINT:CL=0:D=0:RETURN
3275 PRINT"CAN'T":RETURN
4000 CLS:PRINT"   >>> ";:M$="ULTIMATE ADVENTURE":GOSUB7:PRINT" <<<":PRINT
4005 PRINT"     BEING  THE  ADVENTURER  YOUARE,   AND  HAVING  SUCCESSFULLY";
4006 PRINT"SURVIVED ALL THE  ADVENTURES YOUHAVE    ATTEMPTED,    YOU    ARE";
4007 PRINT"OBVIOUSLY  OVERQUALIFIED FOR ANYMORE    EXEDITIONS    OF     THE";
4008 PRINT"DUNGEONS,  HAUNTED HOUSES, SPACETREKS  OR THE";
4010 PRINT" LIKE. THERFORE YOUHAVE BEEN  CHOSEN  TO  UNDERTAKE(OR   OVERTAKE) THE"
4011 PRINT"       ultimate adventure":GOSUB5000
4020 PRINT"     YOU BEGIN WITH  $250 AT THETRADITIONAL  MARKET  PLACE WHERE";
4021 PRINT"YOU MAY  PURCHASE  AN  ARRAY  OFITEMS TO  ASSIST YOU IN FIGHTING";
4022 PRINT"FOES,  SURVIVING  THE  ELEMENTS,AND   FINDING  TREASURES.   YOUR";
4023 PRINT"GOAL IS TO ACCUMULATE $1,000  ORMORE   IN";
4030 PRINT"  TREASURES  AND  OTHERVALUBLE ITEMS AND RETURN  SAFELYTO  THE  ";
4031 PRINT"MARKET. IF  YOU  CAN DOTHIS, YOU WILL HAVE SUCCESSFULLYCOMPLETED";
4032 PRINT"      THE      ultimateadventure.  HOWEVER,   ACHIEVINGYOUR GOAL";
4033 PRINT" IS NOT AS  EASY AS YOUMAY   THINK.";
4040 GOSUB5000:PRINT"    THERE ARE MANY OBSTICALS YOUMUST OVERCOME. A DESCRIPTION  OF";
4041 PRINT"THE  MORE  COMMON  PROBLEMS  YOUWILL ENCOUNTER FOLLOWS:";:GOSUB5000
4060 PRINT"     strength: YOU WILL BEGIN   WITH 250 UNITS OF STRENGTH. EACH";
4061 PRINT"MOVE DIMINISHES YOUR STRENGTH TOSOME DEGREE. HOWEVER, MOST OF"
4062 PRINT"YOUR A STRENGTH WILL BE LOST    DURING ECOUNTERS, DEPENDING ON"
4070 PRINT"WHETHER YOU HAVE HAVE  OR USE   THE CORRECT ITEM (WHICH YOU"
4071 PRINT"PURCHASED FROM THE MARKET)  FOR DEFENCE. IF YOU LOSE ALL YOUR"
4072 PRINT"STRENGTH THEN YOU DIE AND THE   ADVENTURE IS UNFORTUNATELY OVER.";
4073 PRINT"BY FINDING YOUR WAY TO, AND     ENTERING THE infirmary";
4074 PRINT" YOUR     STRENGTH WILL BE REJUVINATED.";:GOSUB5000
4080 PRINT"     money:   EACH  OBJECT   YOUPURCHASE  FROM  THE MARKET  WILL";
4081 PRINT"DECREASE THE AMOUNT OF MONEY YOUHAVE. IT  IS  ALSO  POSSIBLE  TO";
4082 PRINT"HAVE  MONEY  STOLEN   FROM  YOU.OBVIOUSLY, BY FINDING TREASURES"
4090 PRINT"YOU WILL INCREASE YOUR WEALTH.  EACH OBJECT HAS IT'S OWN VALUE"
4091 PRINT"AND NUMBER OF USES. ONCE THE    ALLOCATED USES  HAVE  BEEN"
4092 PRINT"EXHAUSTED YOU WILL AUTOMATICALLYLOSE THAT OBJECT AND WILL NEED"
4093 PRINT"TO PURCHASE ANOTHER ONE FROM THEMARKET";:GOSUB5000
4100 PRINT"     load: THE OBJECTS YOU CARRYDURING YOUR ADVENTURE COMPRISE"
4101 PRINT" YOUR 'load'. YOU MAY CARRY A     MAXIMUM LOAD OF EIGHT ITEMS"
4102 PRINT" (INCLUDING TREASURES).  IF YOU  ARE CARRYING THE MAXIMUM LOAD"
4110 PRINT" WHEN YOU FIND A TREASURE,  YOU  MUST 'drop' ONE ITEM BEFORE YOU";
4111 PRINT" CAN 'get' THE TREASURE.":PRINT"TREASURES CAN NOT BE DROPPED."
4112 PRINT"HENCE, THE MORE TREASURES YOU   ACCUMULATE, THE FEWER OBJECTS"
4113 PRINT"YOU MAY HAVE IN YOUR LOAD.";:GOSUB5000
4120 PRINT"     SINCE THERE IS A TOTAL OF  SEVEN TREASURES, YOU WILL HAVE"
4121 PRINT"TO DROP ALL BUT ONE ITEM TO GET THE LAST TREASURE. ONCE YOU HAVE";
4122 PRINT"FOUND AND GOT THE LAST TREASURE SEVERAL THINGS WILL HAPPEN."
4130 PRINT"     FIRST, YOU WILL RECEIVE AN EXTRA MONETARY BONUS FOR YOUR"
4131 PRINT"ACHIEVEMENT. SECOND, ALL THE    TREASURES WILL BE GIVEN NEW"
4132 PRINT"VALUES AND LOCATIONS, AND BE    REDISTRIBUTED. THIRD, YOUR LOAD"
4140 PRINT"WILL BE DECREASED BY SEVEN ITEMS(THE SEVEN TRASURES), ALLOWING"
4141 PRINT"YOU TO CARRY MORE OBJECTS";:GOSUB5000
4150 PRINT"     portholes: THE DIFFERENT   AREAS OF YOU ADVENTURE ARE"
4151 PRINT"CONNECTED BY PORTHOLES. THERE   ARE NO TREASURES TO BE FOUND IN"
4152 PRINT"THE PORTHOLES. NORMALLY, UPON   ENTERING A PORTHOLE YOU MAY"
4160 PRINT"LOGICALLY PROGRESS TO THE NEXT  AREA OR RETREAT TO FROM WHERE"
4161 PRINT"YOU CAME. HOWEVER, THE PORTHOLESHAVE BEEN KNOWN TO MALFUNCTION."
4162 PRINT"IF THIS HAPPENS WHILE YOU ARE INONE, YOU WILL BE TELEPORTED TO A";
4163 PRINT"RANDOM LOCATION IN ADVETURELAND.";:GOSUB5000
4170 PRINT"THE FOLLOWING IS A DESCRIPTION  OF SOME OF THE COMMANDS YOU MAY"
4171 PRINT"USE DURING YOUR ADVENTURE":PRINT:PRINT "     note: ALL COMMANDS MUST BE            IN UPPER CASE";:GOSUB5000
4180 PRINT
4181 PRINT"ON THE 'now what?' CUE, ENTER A TWO-WORD COMMAND. TO MOVE NORTH"
4182 PRINT"INPUT 'go n', TO MOVE SOUTHWEST INPUT 'go sw', ETC. TO PERFORM"
4183 PRINT"OTHER FUNCTIONS, INPUT TWO-WORD COMMANDS LIKE 'get treasure' OR"
4190 PRINT"'drop (item)', OR  'get (item)'.";:GOSUB5000:PRINT
4191 PRINT"THE ONLY ONE-WORD COMMAND YOUR  COMPUTER WILL UNDERSTAND ON THE"
4192 PRINT"'now what?' CUE IS 'search' WHENYOU WISH TO SEARCH FOR TREASURE.";
4200 PRINT"     note: YOU MAY HAVE TO      SEARCH ONE AREA SEVERAL TIMES TO";
4201 PRINT"FIND A TREASURE. ALSO THERE MAY BE MORE THAN ONE TREASURE IN A  GIVEN AREA.";:GOSUB5000:PRINT
4210 PRINT"THE 'use what?' CUE APPEARS WHENYOU ARE UNDER ATTACK BY A FOE OR";
4211 PRINT"AN ELEMENT.":PRINT             "     USE ONE-WORD COMMANDS SUCH AS 'gun' OR 'rope' ON THIS CUE.":PRINT
4220 PRINT"     hint: IF YOU DON'T HAVE AN APPROPRIATE ITEM TO  USE TO"
4221 PRINT"DEFEND YOUSELF, TRY USING YOUR  'hands'";:GOSUB5000:PRINT
4230 PRINT"     INPUTTING 'list load' AT   ANY TIME WILL PRINT A LIST OF"
4231 PRINT"THE ITEMS YOU ARE CARRYING AND  THE NUMBER OF USES LEFT TO EACH ITEM.";:PRINT
4240 PRINT"     INPUTTING 'list contents'  WHILE YOU ARE IN THE MARKET WILL";
4241 PRINT"PRINT A LIST OF THE STORE'S     INVENTORY, THE NUMBER OF USES TO";
4242 PRINT"EACH ITEM AND THEIR RESPECTIVE  PRICES.";:GOSUB5000:PRINT
4250 PRINT"     INPUTTING 'clear screen' ATANY TIME WILL GIVE YOU A FRESH  STATUS REPORT.":PRINT
4260 PRINT"     IF YOU ARE UNABLE TO       FINISH YOUR ADVENTURE IN ONE"
4261 PRINT"SESSION (WHICH IS NOT UNUSUAL), YOU MAY STOP AND RECORD YOUR"
4262 PRINT"CURRENT STATUS ON TAPE BY USING THE 'save game' COMMAND. THEN   WHEN YOU BEGIN AGAIN";
4270 PRINT", YOU WILL  BE ABLE TO CONTINUE FROM WHERE  YOU LEFT OFF.";:GOSUB5000:PRINT
4280 PRINT"     BY NOW YOU SHOULD HAVE     ENOUGH KNOWLEDGE TO BEGIN YOUR"
4281 PRINT"ULTIMAT ADVENTURE AND PROCEED   THROUGH THE COURSE WITHOUT MUCH"
4282 PRINT"STUMBLING. BUT, BEWARE - THERE  ARE STILL A FEW SURPRISES AHEAD OF YOU."
4290 PRINT:PRINTTAB(6);:M$="HAPPY ADVENTURING!!!":GOSUB7:GOSUB5000
4300 RETURN
5000 PRINT@480,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7:SOUND170,2
5010 A$=INKEY$:IFA$=""THEN5010
5011 CLS:RETURN
6400 DX=X(1)-X(0):DY=Y(1)-Y(0):SX=SGN(DX):SY=SGN(DY)
6440 IFDX=0THEN6550
6450 SL=DY/DX:BB=Y(0)-SL*X(0)
6460 T1=SQR(SL*SL+1)
6470 NX=1/T1*SX
6480 FORXT=X(0)TOX(1)STEPNX:X=INT(XT+.5):Y=INT(SL*XT+BB+.5):SET(X,Y,C):NEXT
6525 RETURN
6550 X=X(0):FORY=Y(0)TOY(1)STEPSY:SET(X,Y,C):NEXT:RETURN
