0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 GOSUB1000:M$="YOU ARE JAN BONDE AGENT 999. IN THIS MISSION YOU ARE LOOKING FOR SECRET PAPERS FOR YOUR BOSS Z "
12 M$=M$+"AT THE SECRET SERVICE. GOOD LUCK!":GOSUB1
20 S$=INKEY$:IFS$=""THEN20
40 DATAIN A BIG FOREST,N02 W16,IN A GLADE,N03 E13 S01,IN FRONT OF THE HOUSE,N04 U08 S02
41 DATAIN A LONG HALLWAY,U09 E08 S03 W07 N06 D05,IN A SMALL KITCHEN,U04,IN A ROOM
42 DATAN08 S04,IN A BATHROOM,E04,IN A LIVING ROOM,S06 D03 W04,UPSTAIRS
43 DATAE10 D04 W11
50 DATAIN A SMALL ROOM,N12 W09,IN A GLOOMY ROOM,E09,IN A BEDROOM,D03 S10
51 DATAAT A RIPPLING STREAM,E14 W02,ON A ROAD,S15 W13,HOME AT Z,N14
52 DATAON A NARROW PATH,N17 E01,IN A CAVE,S16 W18,IN A DESERT,E17
70 DATASHOVEL,16,DYNAMITE,17,KEY,14,SECRET DOCUMENT,,MATCHES,7
71 DATAFOOD,,KNIFE,5,MIRROR,7,BROKEN MIRROR,,BROKEN WINDOW,
72 DATAWINDOW,3,DOOR,3,OPEN DOOR,,A HUNGRY TIGER,11,SCORPION,18
73 DATAA HOUSE,2
80 DATASTEEL DOOR,9,SMALL DOOR,9,A SAFE,10,A BLOWN SAFE,
81 DATAFRIDGE,5,OPEN FRIDGE,,AN ARMED GANGSTER,6,DEAD SCORPION
82 DATA,OPEN WINDOW,12,ELECTRIC FENCE,2,A TUNNEL UNDER THE FENCE,
90 DATAOPEN STEEL DOOR,,OPEN SMALL DOOR,
110 DIMR$(18),H$(18):FORI=1TO18:READR$(I),H$(I):NEXT
120 DIMF$(29),PL(29):FORI=1TO29:READF$(I),PL(I):NEXT
130 POKE49151,0:CLS
140 R=1:PRINT:M$="I AM "+R$(R):GOSUB1:GOTO460
160 INPUTIN$
170 IFLEN(IN$)>1THEN520
180 IFIN$="I"THEN840
190 FORI=1TOLEN(H$(R))
200 IFIN$=MID$(H$(R),I,1)THEN230
210 NEXT
220 GOTO160
230 FORJ=ITOLEN(H$(R))
240 IFMID$(H$(R),J,1)=" "THEN290
250 NEXT
260 IFR=18ANDPL(24)=0THENPRINT:PRINT"THE SCORPION STINGS YOU":GOTO880
270 IFR=PL(23)THENPRINT:PRINT"THE GANGSTER SHOOTS YOU":GOTO880
280 IFR=11ANDPL(6)<>11THENPRINT:PRINT"THE TIGER EATS YOU":GOTO880
290 IFR=3ANDIN$="N"ANDPL(13)=0ORR=4ANDIN$="S"ANDPL(13)=0THENPRINT"THE DOOR IS LOCKED":GOTO160
300 TM=0:IFR=2ANDIN$="N"ANDPL(27)=0THENTM=1
301 IFTM=1THENFORC1=1TO15:SOUND225,1:NEXT:M$="OW! I WAS SHOCKED! I CAN'T GO  NORTH.":GOSUB1:Z=Z+1:IFZ=3THEN880
302 IFTM=1ANDZ<>3THEN160
310 IFR=3ANDIN$="U"ANDPL(11)=3THENPRINT"THE WINDOW IS CLOSED":GOTO160
320 IFR=9ANDIN$="W"ANDPL(28)=0THENPRINT"THE DOOR IS CLOSED":GOTO160
330 IFR=9ANDIN$="E"ANDPL(29)=0THENPRINT"THE DOOR IS CLOSED":GOTO160
340 R=VAL(MID$(H$(R),J-2,2)):PRINT"I AM ";R$(R)
350 IFR=8ANDPL(10)=0THENPL(11)=8
355 IFR=3ANDPL(11)=0THENPL(10)=8
360 IFR=3ANDPL(10)=0THENPL(11)=3
365 IFR=3ANDPL(11)=0THENPL(10)=3
370 IFR=4ANDPL(13)=0THENPL(12)=4
375 IFR=4ANDPL(12)=0THENPL(13)=4
380 IFR=3ANDPL(13)=0THENPL(12)=3
385 IFR=3ANDPL(12)=0THENPL(13)=3
390 IFR=11THENPL(17)=0:PL(28)=11
400 IFR=9ANDPL(28)=0THENPL(17)=9
405 IFR=9ANDPL(17)=0THENPL(28)=9
410 IFR=10THENPL(18)=0:PL(29)=10
420 IFR=9ANDPL(29)=0THENPL(18)=9
425 IFR=9ANDPL(18)=0THENPL(29)=9
430 FORI=1TO29:IFR=PL(I)THENM$="I SEE "+F$(I):GOSUB1
440 NEXT
450 IFPL(19)=0ANDR<12ANDR>3THENPL(23)=R
460 PRINT"I CAN GO ";
470 FORA=1TOLEN(H$(R)):S=0:A$=MID$(H$(R),A,1):IFA$="N"ORA$="E"ORA$="S"THENS=1
475 IFS=1ORA$="W"ORA$="U"ORA$="D"THENPRINTA$;", ";
480 NEXT:PRINTCHR$(8)CHR$(8)
490 IFPL(4)=99ANDR=15THEN910
500 GOTO160
520 FORI=1TOLEN(IN$)
530 IFMID$(IN$,I,1)=" "THEN620
540 NEXT
550 IFIN$<>"HELP"THEN570
560 IFR=2THENPRINT:PRINT"<<DO AS THE MOLE>>":GOTO160
565 PRINT"YOU CAN MANAGE WITHOUT HELP.":GOTO160
570 IFIN$<>"DIG"THEN610
580 IFR=2ANDPL(1)=99THENPL(27)=2:GOTO430
590 IFPL(1)=99THENPRINT"I DON'T WANT TO."
595 IFPL(1)<>99THENPRINT"I CAN'T."
600 GOTO430
610 PRINT"I DON'T UNDERSTAND.":GOTO160
620 V1$=LEFT$(IN$,I-1)
630 F1$=RIGHT$(IN$,LEN(IN$)-I)
640 FORI=1TO9
650 IFV1$="TAKE"ANDF1$=F$(I)ANDR=PL(I)THENPRINT"I TAKE THE ";F1$:PL(I)=99:P(I)=1
660 IFV1$="DROP"ANDF1$=F$(I)ANDP(I)=1THENPRINT"I DROP THE ";F1$:PL(I)=R
670 IFV1$="OPEN"THENGOSUB730
675 IFV1$="HIT"THENGOSUB780
680 IFIN$="KILL SCORPION"ANDR=18ANDPL(7)=99THENPL(15)=0:PL(24)=18
690 IFPL(2)=99ANDPL(5)=99ANDV1$="LIGHT"THENGOSUB800
700 NEXT
710 IFF1$="MIRROR"THENM$="YOU DROP THE MIRROR! SEVEN YEARS BAD LUCK.":GOSUB1:PL(23)=7
720 GOTO430
730 IFF1$="DOOR"ANDPL(3)=99ANDR=3THENPL(12)=0:PL(13)=3
735 IFF1$="DOOR"ANDPL(3)=99ANDR=4THENPL(12)=0:PL(13)=4
740 IFF1$="STEEL DOOR"ANDR=9THENPL(17)=0:PL(28)=9
750 IFF1$="SMALL DOOR"ANDR=9THENPL(18)=0:PL(29)=9
760 IFF1$="FRIDGE"ANDR=5THENPL(21)=0:PL(22)=5:PL(6)=5
770 RETURN
780 IFF1$="WINDOW"ANDR=3THENPL(23)=R:PL(11)=0:PL(10)=3
790 RETURN
800 PL(2)=0:PL(5)=0
810 IFR=10THENPL(19)=0:PL(20)=10:PL(4)=10
815 IFR<>10THENPL(23)=R
820 RETURN
840 FORI=1TO9:IFPL(I)=99THENPRINTF$(I)
850 NEXT
860 GOTO160
880 PRINT"YOU ARE DEAD. SORRY."
890 S$=INKEY$:IFS$=""THEN890
895 RUN
910 FORW=1TO16:PRINT:SOUNDW*10,1:NEXT:PRINT"CONGRATULATIONS JAN BONDE."
915 FORW=1TO8:PRINT:SOUNDW*15,1:NEXT:PRINT"THE MISSION IS COMPLETED!"
920 S$=INKEY$:IFS$=""THEN920
930 RUN
1000 POKE49151,64:CLS:PRINT
1003 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß"
1004 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß"
1005 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß"
1006 PRINT"      ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ"
1007 PRINT"      ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ"
1009 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß"
1010 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß"
1014 PRINT"      ßßßßßßŸŸŸßßßßßßßßßßßß":PRINT:RETURN
