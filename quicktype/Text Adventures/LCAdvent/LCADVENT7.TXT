REM WORD WRAP (1), INSTR (3) AND REVERSE TEXT (7) SUBROUTINES
0 GOSUB9100:CLEAR500:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 CC=.:LL=LEN(I$):FORZZ=1TOLEN(M$):IFMID$(M$,ZZ,LL)=I$THENCC=ZZ:ZZ=255
4 NEXT:RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-16384+ZZ,:RETURN

10 GOSUB1000:REM SET UP START
20 GOSUB 2010:REM DISPLAY
30 GOSUB 2510:REM GET WORD NUNBERS
100 PRINT"THE WORD NUMBERS WERE ";W(1);"&";W(2)
990 STOP
998 REM
999 REM SORT THINGS OUT BEFORE YOU START
1000 W=0:REM THIS WILL BE THE NUNBER OF WORDS
1005 DIM W(2):REM WORD NUMBERS FOUND GO HERE
1010 RESTORE
1020 READX,X$:REM THESE VARIABLES ARE JUST FOR COUNTING
1030 IFX>0THENW=W+1:GOTO1020:RETURN:REM NOT THE END OF THE LIST?
1040 DIMWR(W),WR$(W):REM SET UP ARRAYS TO PUT WORDS IN
1045 RESTORE:REM GO BACK TO THE START OF THE LIST
1050 FORX=1TOW:READ WR(X),WR$(X):NEXT X
2000 RETURN:REM FINISHED SORTING THINGS OUT
2005 REM
2009 REM DISPLAY THINGS
2010 REM BITS GO IN HERE LATER
2200 PRINT:INPUT"WHAT NEXT";Y$:Y$=Y$+" "
2499 RETURN
2510 W(1)=0:W(2)=0:P=0:WH=1:REM THE WORD # GO HERE, P FOR POINTER, WHICH IS 1ST OR 2ND WRD FOUND
2520 P=P+1:IFP>LEN(Y$)THENRETURN:REM HAVE WE FINISHED?
2530 IFMID$(Y$,P,1)=" "THEN2520:REM FIND NEXT WORD
2540 W$="":REM NO WORD YET
2550 IFMID$(Y$,P,1)=" "THEN2600:REM END OF THE WORD?
2560 W$=W$+MID$(Y$,P,1):IFLEN(W$)=4THEN2600:REM 4 LETTERS YET?
2570 P=P+1:GOTO2550:REM GET THE NEXT LETTER
2599 REM NOW SEE IF THE WORD MATCHES
2600 FORX=1TOW
2610 IFWR$(X)=W$THENW(WH)=WR(X):WH=WH+1:REM FOUND MATCH - SAVE IT
2620 NEXT X
2630 IFWH=3THENRETURN:REM STOP IF YOU'VE GOT 2 WORDS
2640 IFMID$(Y$,P,1)<>" "THENP=P+1:GOTO2640:REM IGNORE THE REST OF THAT WORD
2650 GOTO2520:REM GET THE NEXT WORD
2699 RETURN
5000 DATA1,NORT,1,N
5010 DATA2,EAST,2,E
5020 DATA3,SOUT,3,S
5030 DATA4,WEST,4,W
5040 DATA5,UP,5,U
5050 DATA6,DOWN,6,D
5500 DATA0,X

35 IFW(1)>=1ANDW(1)<=10THENGOSUB2710:GOTO20:REM FOR > 10 ROOMS CHANGE THE 10 IN THIS LINE
990 GOTO20
1060 RM=1:Y$="START THE ADVENTURE":M1$=""
2010 RESTORE:FORCC=1TOW+1:READZZ,M$:NEXT:REM GO TO START OF THE ROOM LIST
2020 FORC=1TORM:READDI$,DE$,CO$:NEXTC:REM GET INFO FOR THAT ROOM
2025 CLS:M$="YOU ARE "+DE$+".":GOSUB1:REM PRINT WHERE YOU ARE
2030 D$="":M$=DI$:I$="N":GOSUB3:IFCC>0THEND$=D$+"NORTH "
2031 M$=DI$:I$="E":GOSUB3:IFCC>0THEND$=D$+"EAST "
2032 M$=DI$:I$="S":GOSUB3:IFCC>0THEND$=D$+"SOUTH "
2033 M$=DI$:I$="W":GOSUB3:IFCC>0THEND$=D$+"WEST "
2034 M$=DI$:I$="U":GOSUB3:IFCC>0THEND$=D$+"UP "
2035 M$=DI$:I$="D":GOSUB3:IFCC>0THEND$=D$+"DOWN ":REM D$ CONTAINS THE DIRECTION YOU CAN GO

2190 IFD$<>""THENPRINT:M$="YOU CAN GO "+D$:GOSUB1
2191 PRINT"********************************";:REM 31 STARS
2192 IFY$<>" "THENPRINT:M$="YOU WANTED TO "+Y$:GOSUB1:REM REMIND PLAYER WHAT THEY WANTED TO DO
2193 IFM1$<>""THENPRINT:M$=M1$:GOSUB1
2196 M1$="":REM PRINT ANY MESSAGES AND THEN CLEAR THEM
2710 P=1:REM TRY TO MOVE TO A NEW ROOM
2720 REM NAVIGATE
2725 IFVAL(MID$(CO$,P,2))=W(1)THENRM=VAL(MID$(CO$,P+2,2)):RETURN
2730 IFVAL(MID$(CO$,P,2))=99THENM1$="YOU CAN'T GO THAT WAY!":RETURN
2740 P=P+4:GOTO2720
5510 REM ROOMS START HERE
5520 DATA S,IN THE CONTROL ROOM,030399
5530 DATA E,IN THE WEAPONS ROOM,020399
5540 DATA NSEW,IN A CORRIDOR,010102040305040299
5550 DATA W,IN A STORE ROOM,040399
5560 DATA NS,IN A CORRIDOR,0103030799
5570 DATA ES,IN THE RECREATION ROOM,0207031099
5580 DATA NSEW,IN A CORRIDOR,010502080311040699
5590 DATA ESW,IN THE AIR LOCK,02090312040799
5600 DATA W,IN THE SHUTTLE,040899
5610 DATA NES,IN THE SLEEPING QUARTERS,01060211031399
5620 DATA NSEW,IN A CORRIDOR,010702120314041099
5630 DATA NSW,STANDING BY A CRATE IN THE HOLD,01080315041199
5640 DATA NEW,CRAWLING THROUGH AN AIR DUCT,01100214042099
5650 DATA NSEW,IN A CORRIDOR,011102150316041399
5660 DATA NW,IN THE HOLD,0112041499
5670 DATA N,IN THE ENGINE ROOM,011499
5680 DATA NEWS,CRAWLING THROUGH AN AIR DUCT,011802180319041999
5690 DATA NEWS,CRAWLING THROUGH AN AIR DUCT,011702130320041799
5700 DATA NEWS,CRAWLING THROUGH AN AIR DUCT,011702200320041799
5710 DATA NEWS,CRAWLING THROUGH AN AIR DUCT,011802130319041999
5900 DATA X,X,X

35 IFW(1)>=1ANDW(1)<=10THENGOSUB2710:REM FOR MOR THAN 10 ROOMS CHANGE THE 10 IN THIS LINE
1070 REM NOW SORT OUT THE OBJECTS - FIRST COUNT THEM
1080 RESTORE:FORCC=1TOW+1:READZZ,M$:NEXT
1081 READM$,I$:READM$:IFM$<>"X"THEN1081
1085 O=0:REM THIS WILL BE THE NUMBER OF OBJECTS
1090 READX,X$:REM THESE VARIABLES ARE JUST FOR COUNTING
1100 IFX>-1THENO=O+1:GOTO1090:REM NOT THE END OF THE LIST?
1110 DIMOB(O),OB$(O)
1120 REM NOW PUT THE OBJECTS INTO ARRAY
1130 RESTORE:FORCC=1TOW+1:READZZ,M$:NEXT:REM BACK TO THE START OF THE OBJECT LIST
1131 READM$,I$:READM$:IFM$<>"X"THEN1131
1140 FORX=1TOO:READOB(X),OB$(X):NEXT
2040 REM NOW TELL US WHAT YOU CAN SEE
2050 PRINT:M$="YOU CAN SEE: "
2060 I$="":FORX=1TOO
2070 IFOB(X)=RMTHENI$=I$+OB$(X)+", "
2080 NEXTX:IFI$<>""THENM$=M$+I$:M$=LEFT$(M$,LEN(M$)-2)+".":GOTO2082
2081 M$=M$+"NOTHING USEFUL."
2082 GOSUB1

5990 REM OBJECTS START HERE
6000 DATA 2,LAZER RIFLE
6010 DATA 4,SLEEPI-GAS BOMB
6020 DATA 1,RED BUTTON
6030 DATA 1,GREEN BUTTON
6040 DATA 0,SPACE SUIT
6050 DATA 19,SPANNER
6060 DATA 18,CROWBAR
6070 DATA 8,RED LIGHT
6080 DATA 0,GREEN LIGHT
6090 DATA 13,HUNGRY ALIEN
6100 DATA 0,SLEEPING ALIEN
6110 DATA 12,GAS MASK
6120 DATA 16,LOOSE CABLE
6130 DATA 0,FIXED CABLE
6300 DATA -1,X


40 GOSUB3000
1065 DI=1:REM SET INFORMATION DISPLAY
1150 A=0:REM THIS WILL BE THE NUMBER OF ACTIONS
1160 RESTORE:FORCC=1TOW+1:READZZ,M$:NEXT:REM BACK TO THE START OF THE OBJECT LIST
1161 READM$,I$:READM$:IFM$<>"X"THEN1161
1162 READCC,M$:IFCC<0THEN1170
1163 GOTO1162
1170 READX$,X$:REM THESE VARIALES ARE JUST FOR COUNTING
1180 IFX$<>"X"THENA=A+1:GOTO1170:REM NOT THE END OF THE LIST?
1190 REM NOW PUT THE CONDIONS AND ACTIONS INTO ARRAYS
1200 DIM CO$(A),AC$(A)
1210 RESTORE:FORCC=1TOW+1:READZZ,M$:NEXT:REM BACK TO THE START OF THE LIST
1211 READM$,I$:READM$:IFM$<>"X"THEN1211
1212 READCC,M$:IFCC<0THEN1220
1213 GOTO1212
1220 FORX=1TOA:READ CO$(X),AC$(X):NEXT
2015 ONDIGOTO2020,2191
2197 DI=1:REM RESET DISPLAY TO NORMAL
2999 REM CONDITIONS/ACTIONS
3000 AP=0:REM WHERE WE ARE IN THE CONDITION/ACTION TABLE
3015 WR=0:REM THIS MILL TELL US THAT WORDS MATCHED
3020 AP=AP+1:IF AP=A+1THENRETURN:REM WE HAVE BEEN THROUGH THE LIST?
3030 IFVAL(LEFT$(CO$(AP),2))<>W(1)THEN3020:REM DOES THE FIRST WORD MATCH
3040 IFVAL(MID$(CO$(AP),3,2))<>W(2)ANDVAL(MID$(CO$(AP),3,2))>0THEN3020:REM DOES THE 2ND WORD MATCH OR IS THE CONDITION 0
3050 WR=1
3060 GOSUB3500:REM FOUND A MATCH, GO AND DO THE ACTIONS. THIS WILL BE MORE CONPLICATED LATER
3100 RETURN
3499 REM DO ACTIONS
3500 P=0:REM POINTER IN ACTION CODE
3510 P=P+1:DO=ASC(MID$(AC$(AP),P,P))
3515 IFDO=ASC("#")THENRETURN:REM FINISHED ALL THE ACTIONS?
3520 ONDO-64GOSUB3610,3630,3650
3590 GOTO3510
3609 REM CARRY AN OBJECT
3610 OB=VAL(MID$(AC$(AP),P+1,2)):REM GET THE OBJECT NUMBER
3613 IFOB(OB)=-1THENM1$="YOU'VE ALREADY GOT IT.":GOTO3625:REM ARE WE ALREADY CARRYING IT
3615 IFOB(OB)<>RMTHENM1$="IT ISN'T HERE.":GOTO3625:REM NOT IN THIS ROOM
3620 OB(OB)=-1:M1$="YOU TAKE IT.":REM MOVE THE OBJECT TO THE INVENTORY
3625 P=P+2:DI=1:RETURN:REM MOVE THE POINTER TO THE NEXT ACTION
3629 REM DROP AN OBJECT
3630 OB=VAL(MID$(AC$(AP),P+1,2)):REM GET THE OBJECT NUMBER
3635 IFOB(OB)>-1THENM1$="YOU HAVEN'T GOT IT.":GOTO3645:REM ARE HE CARRYING IT
3640 OB(OB)=RM:M1$="YOU DROP IT.":P=P+2:RETURN:REM MOVE THE OBJECT TO THE CURRENT ROOM
3645 P=P+2:DI=1:RETURN:REM MOVE THE POINTER TO THE NEXT ACTION
3649 REM INVENTORY
3650 TE=0:REM CHECK TO SEE IF NOTHING IS CARRIED
3653 CLS:I$="YOU ARE CARRYING: ":FORX=1TOO:IFOB(X)=-1THENI$=I$+OB$(X)+", ":TE=1
3655 NEXTX
3657 IFTE=0THENI$=I$+"NOTHING USEFUL, "
3658 M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
3660 DI=2:RETURN
5060 DATA12,TAKE,12,GET,12,BRIN
5070 DATA11,DROP,11,LEAV,11,DUMP
5200 DATA50,LAZE,50,RIFL,50,GUN
5210 DATA51,SLEE,51,BOMB,51,GAS
5220 DATA52,RED
5230 DATA53,GREE
5240 DATA54,SPAC,54,SUIT
5250 DATA55,SPAN
5260 DATA56,CROW,56,BAR
5270 DATA57,ALIE
5280 DATA58,MASK
5290 DATA59,CABL
5310 DATA61,LIGH
5320 DATA62,BUTT
5490 DATA99,INVE
6399 REM CONDITIONS AND ACTIONS IN HERE
6400 DATA1250#,A01#
6410 DATA1251#,A02#
6420 DATA1254#,A05#
6430 DATA1255#,A06#
6440 DATA1256#,A07#
6450 DATA1258#,A12#
6460 DATA9900#,C#
6465 DATA1150#,B01#
6470 DATA1151#,B02#
6475 DATA1154#,B05#
6480 DATA1155#,B06#
6485 DATA1156#,B07#
6490 DATA1158#,B12#
7100 DATAX,X

3060 P=4:REM POINTER IN CONDITION CODE SET PAST WORD-NUMBERS
3065 CF=0:REM CONDITION FLAG STAYS ZERO IF CONDITIONS ARE TRUE
3070 P=P+1:DO=ASC(MID$(CO$(AP),P,P))
3075 IFDO=ASC("#") THEN GOSUB 3500:RETURN:REM IF ALL CONDITIONS TRUE, DO ACTION
3080 ONDO-64GOSUB3100
3085 IFCF=1THEN3020:REM IF CONDITION NOT TRUE: GO TO NEXT SET OF CONDITIONS
3090 GOTO3070:REM CONDITION TRUE, GOTO NEXT CONDITION
3099 REM CHECK IF OBJECT IS HERE OR CARRIED
3100 OB=VAL(MID$(CO$(AP),P+1,2)):REM GET THE OBJECT NUMBER
3105 IFOB(OB)<>RMANDOB(OB)>-1THENCF=1:REM IF NOT HERE OR CARRIED SET FLAG
3110 P=P+2:RETURN:REM MOVE POINTER TO NEXT CONDITION
3520 ONDO-64GOSUB3610,3630,3650,3670
3669 REM MOVE OBJECTnn TO ROOMnn
3670 OB=VAL(MID$(AC$(AP),P+1,2)):REM GET THE OBJECT NUMBER
3675 RO=VAL(MID$(AC$(AP),P+3,2)):REM GET THE ROOM NUMBER
3680 OB(OB)=RO:P=P+4:RETURN:REM MOVE THE OBJECT, RESET POINTER TO NEXT ACTION
5080 DATA20,FIX,20,MEND,20,REPA,USE
5085 DATA21,PUSH,21,PRES
6495 DATA2059A06A13#,D1300D1416#
6500 DATA2152A03#,D0900D0808#
6505 DATA2153A04#,D0800D0908#


REM NEW STUFF BY JIM GERRIE 2024

100 REM PRINT"THE WORD NUMBERS WERE ";W(1);"&";W(2)

REM TURNS ALIEN WILL SLEEP
1001 T1=10+RND(6)

REM ENDGAME/WIN GAME BRANCH IF REACH ROOM PAST ROOM 20
2200 ON-(RM>20)GOTO9000:PRINT:INPUT"WHAT NEXT";Y$:Y$=Y$+" "

REM CONDITIONS A-E
3080 ONDO-64GOSUB3100,3200,3300,3350,3360

REM CONDITION B: IS OBJECT NOT IN A DESIGNATED ROOM?
3200 OB=VAL(MID$(CO$(AP),P+1,2)):REM GET OBJECT NUMBER
3201 RO=VAL(MID$(CO$(AP),P+3,2)):REM GET ROOM NUMBER
3205 IFOB(OB)<>ROTHENCF=1:REM IF NOT IN DESIGNATED ROOM SET FLAG
3210 P=P+4:RETURN:REM MOVE POINTER TO NEXT CONDITION

REM CONDITION C: IS CURRENT ROOM DESIGNATED ROOM?
3300 RO=VAL(MID$(CO$(AP),P+1,2)):REM GET ROOM NUMBER
3305 IFRM<>ROTHENCF=1:REM IF CURRENT ROOM IS NOT THE DESIGNATED ROOM NUMBER SET FLAG
3310 P=P+2:RETURN:REM MOVE POINTER TO NEXT CONDITION

REM CONDITION D
3350 OB=VAL(MID$(CO$(AP),P+1,2)):REM GET THE OBJECT NUMBER
3355 IFOB(OB)=-1THENCF=1:REM IF OBJECT CARRIED SET FLAG
3360 P=P+2:RETURN:REM MOVE POINTER TO NEXT CONDITION

REM CONDITION E: IS CURRENT ROOM NOT THE DESIGNATED ROOM?
3360 RO=VAL(MID$(CO$(AP),P+1,2)):REM GET ROOM NUMBER
3365 IFRM=ROTHENCF=1:REM IF CURRENT ROOM IS NOT THE DESIGNATED ROOM NUMBER SET FLAG
3370 P=P+2:RETURN:REM MOVE POINTER TO NEXT CONDITION

REM ACTIONS A-I
3520 ONDO-64GOSUB3610,3630,3650,3670,3700,3800,3900,3950,3960

REM ACTION E
3700 SOUND225,10
3710 PRINT:M$="THE ALIENS INFESTING YOUR SHIP HEAR THE REVERBERATION OF THE LAZER BLAST AND COME GET YOU!"
3720 RM=99:GOSUB1:GOTO2200

REM ACTION F
3800 SOUND1,1
3810 PRINT:M$="YOU GAS YOURSELF TO SLEEP AND THE ALIENS INFESTING YOUR SHIP COME GET YOU!":GOSUB1:RM=99:GOTO2200

REM ACTION G: GET SENT TO SPECIFIC ROOM, SUCH AS EATEN BY ALIEN
3900 RO=VAL(MID$(AC$(AP),P+1,2)):REM GET ROOM NUMBER
3901 RM=RO:P=P+2:RETURN:REM GO TO ROOM

REM ACTION H: BE SENT BACK TO A ROOM/PREVENTED FROM ENTERING
3950 GOSUB3900:M1$="THE DOOR WON'T OPEN.":RETURN

REM ACTION I: INC SLEEP TIMER-- WAKE ALIEN IF=T1
3960 S1=S1+1:IFS1=T1THENOB(11)=0:OB(10)=13
3961 RETURN

REM SOME MODIFICATIONS TO SHORT HAND SEARCH TERMS FOR VOCAB
5070 DATA11,DROP,11,LEAV,11,DUMP,11,THRO,11,TOSS
5080 DATA20,FIX,20,MEND,20,REPA,20,USE,20,OPEN,20,RELE
5090 DATA22,SHOO,22,KILL,22,FIRE
5330 DATA63,CRAT
5331 DATA64,BLAN

5600 DATA U,IN SHUTTLE,050099
5720 DATA D,IN THE ALIEN'S BELLY,060099
5730 DATA D,IN THE VACUUM OF SPACE WITHOUT A SPACESUIT,060099
5740 DATA U,IN A SPACESUIT ESCAPING FROM YOUR ALIEN OCCUPIED SHIP,050099

6140 DATA 21,COPY OF THE MOVIE 'ALIEN' (HOW IRONIC)
6141 DATA 23,INCOMING RESCUE SHIP
6142 DATA 10,BLANKET

REM ADDED MOVE COUNT "I" ACTION
6400 DATA1250#,A01I#
6410 DATA1251#,A02I#
6420 DATA1254#,A05I#
6430 DATA1255#,A06I#
6440 DATA1256#,A07I#
6450 DATA1258#,A12I#
6460 DATA9900#,CI#
6465 DATA1150#,B01I#
6470 DATA1151#,B02I#
6475 DATA1154#,B05I#
6480 DATA1155#,B06I#
6485 DATA1156#,B07I#
6490 DATA1158#,B12I#

REM PICK UP AND DROP BLANKET (A RED HERRING)
6451 DATA1264#,A17I#
6491 DATA1164#,B17I#

REM USE GAS WITHOUT MASK
6496 DATA2051A02D12#,F#
REM USE GAS WITH MASK AND ALIEN PRESENT
6497 DATA2051A02A10A12#,D1000D0200D1113#
REM OPEN CRATE7
6498 DATA2063A07C12#,D0512#
REM GO WEST TO ROOM 20 WITH HUNGRY ALIEN IN ROOM 13
6499 DATA0400C20B1013#,G21#
REM GO EAST TO ROOM 13 WITH HUNGRY ALIEN IN ROOM 13
6501 DATA0200C13B1013#,G21#
REM GO EAST TO ROOM 9 WITHOUT GREEN LIGHT
6502 DATA0200C09B0808#,H08#
REM GO EAST TO ROOM 9 WITHOUT SUIT
6503 DATA0200C09D05#,G22#
REM GO EAST TO ROOM 9 WITHOUT SUIT
6504 DATA0200C09A05#,G23#
REM PRESS RED BUTTON
6500 DATA2152A03#,D0900D0808#
REM PRESS GREEN BUTTON
6505 DATA2153A04B1416#,D0800D0908#
REM KILL HUNGRY ALIEN
6600 DATA2257A01#,E#
REM SHOOT RIFLE
6601 DATA2250A01#,E#
REM FOR ANY MOVES CHECK IF ALIEN IS SLEEPING AND WAKE IF TIMER REACHES T1 (SET IN LINE 1001)
6602 DATA0100B1113#,I#
6603 DATA0200B1113#,I#
6604 DATA0300B1113#,I#
6605 DATA0400B1113#,I#
REM USE GAS BUT NOT IN ROOM WITH HUNGRY ALIEN
6606 DATA2051A02A12E13#,D0200#

9000 IFRM=23THENSOUND161,4:SOUND161,4:SOUND103,4:SOUND87,4:SOUND142,4:SOUND129,4:SOUND129,4:SOUND142,4:SOUND113,4
9010 PRINT:INPUT"HIT enter TO PLAY AGAIN OR q    TO QUIT";M$:IFM$=""THENRUN
9020 END

9100 CLS3
9101 PRINT"ппппппппппппппоммммппппппппппппп";
9102 PRINT"п";:M$=" LET'S ":GOSUB7:PRINT"пппппо╟╠╠╠▄дпппппппппппп";
9103 PRINT"пcomputeппппп┼╚┴аа├аеппппппппппп";
9104 PRINT"п";:M$="03-1991":GOSUB7:PRINT"ппп┴├┼аааа─аеппппппппппп";
9105 PRINT"ппппппппппк╧╧┼╔ааааазппппппппппп";
9106 PRINT"ппппппппппк╠╤┘╧╧╧╧╧┌пппппппппппп";
9107 PRINT"ппппппппппк╧╩╧╧╧╧╧╧╠днпппппппппп";
9108 PRINT"ппппппппппк╧╩╧─╔├═╩╧╧┌пппппппппп";
9109 PRINT"ппппппппппк╧╩╠╧▀╧┼╧┬гзпппппппппп";
9110 PRINT"ппппппппппк▀▀█Р╙├╧╧╦нппппппппппп";
9111 PRINT"пппппппппплг││▓╟╧╠╧╧╩ппппппппппп";
9112 PRINT"пппппппппп пп┐┴╧┌к╫▐еппппппппппп";
9113 PRINT"пппппппппппп п┼╧╦и═╧╩ппппппппппп";
9114 PRINT"ппппппппппЯпппл╠╠злгзпппspaceппп";
9115 PRINT"ппппппппппппппппппппппadventureп";
9116 PRINT"ппппппппппппппппппппппппппппппп";:RETURN
9120 REM MC-10 EDITS JIM GERRIE
