0 CLEAR500:DIMC1,C2,M$,I$:GOTO9100
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
10 C2=INT(RND(0)*(N-1))+2:RETURN
80 EN=99
90 TI=200
100 N=30
105 DIMN(N),H(3),I(6),G(N),J(6),P$(N),C(N,6),S$(30)
106 A$=INKEY$:ON-(A$="")GOTO106
107 CLS
110 X=1:Y=1
112 IFA$="Y"THENGOSUB8000
114 IFA$="N"THENM$="OH ALL RIGHT-- I'LL JUST GET ON WITH IT.":GOSUB1
120 GOSUB10:D=C2
125 C(X,Y)=D
130 FORA=1TOINT(N*1.1)
140 X1=X:X=D:IFY>3THENY=Y-6
150 Y=Y+3:D=X1:C(X,Y)=D
160 Y=INT(RND(0)*6)+1
170 IFC(X,Y)<>0THENGOSUB10:X=C2:GOTO160
180 GOSUB10:D=C2
186 Y2=Y:IFY2>3THENY2=Y2-6
187 Y2=Y2+3:IFC(D,Y2)<>0THEN170
190 C(X,Y)=D
200 NEXT
202 FORA=2TON
204 FORB=1TO6
206 IFC(A,B)<>0THEN208
207 NEXTB:C(A,INT(RND(0)*6+1))=INT(RND(0)*N)+1
208 NEXTA
210 REM setup caves
211 C(C(X,1),4)=1
215 IFA$="Y"THENGOSUB9150:GOSUB8100
220 FORA=1TON
230 READP$(A)
240 NEXT:GOTO600
250 DATA"YOU ARE UNDER A CLIFF. NEAR AN OLD HOUSE. A CAVE FACES YOU"
260 DATA"AHEAD OF YOU LIES AN UNDERGROUND STREAM, BUT YOU CAN CROSS IT"
270 DATA"YOU ARE IN A LOW CHAMBER AND MUST CRAWL ON HANDS AND KNEES" 
280 DATA"YOU ARE IN A SMALL CAVE WITH FLOURESCENT GREEN WALLS"
290 DATA"YOU COME ACROSS THE HOME OF GOR THE GIANT, BUT HE IS OUT NOW"
300 DATA"YOU ARE IN A MAN-MADE ROOM WITH WITH FLOWERED WALL PAPER"
310 DATA"YOU ARE IN A FEATURELESS CAVE & HEAR THE SOUND OF RUNNING WATER"
320 DATA"THE WALLS OPEN OUT INTO A VERY WIDE ROOM"
325 DATA"IN THIS NARROW ROOM, WITH A LOW CEILING SITS A CHUNKY WOODEN TABLE"
330 DATA"AHEAD LIES A PREHISTORIC PLACE WITH BONES STREWN EVERYWHERE"
340 DATA"YOU PASS SOME MYSTERIOUS DRAWINGS, CHALKED CENTURIES AGO"
350 DATA"YOU WALK ACROSS A ROTTING BRIDGE SPANNING A BOTTOMLESS CHASM"
360 DATA"YOU APPROACH AN OLD STONE STATUE THAT POINTS BACK THE WAY YOU CAME 
370 DATA"NOW COME INTO A SMALL CAVE, LITTERED BY HUNDEREDS MK14S"
380 DATA"ENTER A BROAD PLAIN, WITH A GLOSSY, METALLIC FLOOR"
390 DATA"YOU ARE IN A ROCKY CHAMBER-- THE SMELL OF DECAY HANGS IN THE AIR"
400 DATA"YOU PASS THROUGH A BROKEN DOOR INTO A 17TH CENTURY LIBRARY"
410 DATA"YOU PASS THROUGH A DOORWAY INTO A DISUSED LABORATORY"
420 DATA"YOU ENTER WHAT WAS A NURSERY-- A ROCKING HORSE STARES AT YOU"
430 DATA"YOU ARE IN A WINE CELLAR. ALL THE BOTTLES ARE SMASHED"
440 DATA"AHEAD LIES A SMALL OFFICE WITH PAPERS STREWN EVERYWHERE"
450 DATA"YOU ARE IN A COLD, DAMP CAVE AND SHIVER INVOLUNTARILY"
455 DATA"AHEAD LIES A ROCK FORMATION LIKE THAT OF A HONEYCOMB"
460 DATA"WATER DRIPS FROM THE CEILING OF THIS CAVE. BETTER HURRY OR GET WET"
465 DATA"NOW YOU HAVETO DODGE ROUND SOME RATHER SHARP STALAGMITES"
470 DATA"YOU WALK ACROSS SOME GRAVEL. TRYING HARD NOT TO STUMBLE"
475 DATA"YOU CROSS SOME ROUGH TERRAIN, AVOIDING THE SHARP-EDGED WALLS"
480 DATA"NOW YOU ARE IN AN OVAL CAVE WITH SMOOTH WALLS"
485 DATA"YOU'RE WALKING ON STONEY GROUND THAT MAKES WALKING UNCOMFORTABLE"
490 DATA"YOU PASS THROUGH AN OLD SCHOOL ROOM, STILL WITH DESKS"
600 FORA=0TON
610 READS$(A)
620 NEXT
625 IFA$="Y"THENGOSUB9150:GOSUB8400
635 S$(28)="THE TREASURE"
640 DATA"A SMALL RED BOX"
650 DATA"A WOODEN CUPBOARD"
660 DATA"A LARGE BRIEFCASE"
670 DATA"A WOODEN TRUNK"
680 DATA"AN ANTIQUE SIDEBOARD"
690 DATA"A LARGE, ROBUST, TREASURE CHEST"
700 DATA"A SMALL COPPER BOX"
705 DATA"A TIN OF FOOD"
710 DATA"A SKELETON OF A MAN-- PERHAPS THE LAST TREASURE HUNTER"
720 DATA"A DEAD RAT"
740 DATA"A SILVER KEY"
750 DATA"A GOLDEN KEY"
760 DATA"A BRONZE KEY"
770 DATA"A NICKEL KEY, GREEN WITH AGE"
780 DATA"A SHINEY POINTED KEY"
790 DATA"A KEY WITH THE INITIALS 'J.F' SCRATCHED ON"
800 DATA"A LENGTH OF ROPE"
810 DATA"A BOX OF UNCOMPLETED RUBIK CUBES"
820 DATA"A PILE OF OLD 78'S"
840 DATA"A TIN OPENER"
850 DATA"A FRENCH DICTIONARY"
860 DATA"A TOURIST GUIDE TO THE WEST"
870 DATA"A 1946 DAILY MIRROR"
880 DATA"A WORLD WARE 1 HELMET"
890 DATA"A LONG KNIFE"
900 DATA"A COMFY CHAIR"
910 DATA"A PAIR OF DENTURES"
920 DATA"A ROCK FROM STAR TREK"
930 DATA"A PILE OF RADIO COMPONENTS"
940 DATA"A GUN"
950 DATA"A PACKET OF PLAYING CARDS"
960 DATA"A HUGE SHELL"
1000 H(1)=0:H(2)=0:H(3)=0
1005 S$(30)="NOTHING"
1010 FORA=0TO6
1015 R1=INT(RND(0)*7)
1017 IFN(R1)<>0THEN1015
1020 I(A)=R1:N(R1)=1
1022 R1=INT(RND(0)*7)+10
1024 IFN(R1)<>0THEN1022
1030 J(A)=R1:N(R1)=1
1040 NEXT
1041 FORA=1TON:N(A)=0:NEXT
1045 J(5)=28
1050 FORA=1TON
1060 R=INT(RND(0)*(N+1))
1070 IFN(R)<>0THEN1060
1080 N(R)=1:IFR<>28THENG(A)=R
1090 NEXT
1095 CLS
2000 X=1:REM end of init
2010 IFTI>0THENM$="YOU HAVE"+STR$(TI)+" MINUTES BEFORE THE TIDE RETURNS.":GOSUB1
2012 TI=TI-INT(RND(0)*18)
2013 IFTI<0THENM$="THE TIDE IS IN.":GOSUB1
2014 IFX=1ANDTI<0THENM$="SPLOOSH! TIDE IS IN! YOU ARE CARRIED "
2015 EN=EN-(410-TI)/95:IFK=1ANDTI>0THENEN=99
2016 IFTI<-100THENTI=200
2017 IFX=1ANDTI<0THENX=INT(RND(0)*20)+1:M$=M$+"TO SOME DEEP CAVE.":GOSUB1
2018 M$=P$(X)+".":GOSUB1
2019 IFG(X)=30THEN2471
2020 IFRND(0)<.3THENM$="NEXT TO A ROCK ":GOTO2460
2030 IFRND(0)<.3THENM$="AGAINST A WALL ":GOTO2460
2040 IFRND(0)<.3THENM$="ON THE GROUND ":GOTO2460
2050 M$="REFLECTING IN YOUR TORCHLIGHT "
2460 M$=M$+"IS "
2461 FORA=1TO2500:NEXT
2464 IFRND(0)<.95THEN2469
2466 FORA=1TON:IFG(A)=30THENG(X)=7:C1=A:A=N:NEXT:A=C1:GOTO2469
2467 NEXTA
2469 REM meet ghost
2470 M$=M$+S$(G(X))+".":GOSUB1
2471 IFRND(0)<.9858THEN2475
2472 M$="YOU MEET UP WITH THE GHOST OF TANGRIN WHO CAN TAKE YOU OUT. "
2473 M$=M$+"BUT HE ONLY SPEAKS FRENCH.":GOSUB1
2474 FORA=1TO4000:NEXT:CLS:GOSUB5700
2475 M$="YOU CAN NOW GO: ":GOSUB4000
2480 M$="TYPE IN: OPEN, PICKUP, DUMP, SWAP, END, U, D, N, S, E OR W.":GOSUB1
2482 GOSUB9000
2500 INPUTG$:G$=LEFT$(G$,2):CLS
2501 IFG$="N"THENG$="NO"
2502 IFG$="S"THENG$="SO"
2503 IFG$="E"THENG$="EA"
2504 IFG$="W"THENG$="WE"
2505 IFG$="U"THENG$="UP"
2506 IFG$="D"THENG$="DO"
2510 IFG$<>"OP"THEN2800
2515 X$=RIGHT$(S$(G(X)),LEN(S$(G(X)))-2)
2520 IFG(X)>7THENM$="HOW CAN YOU OPEN "+S$(G(X)):GOSUB1:GOTO2475
2522 IFG(X)=7AND(H(1)=19ORH(3)=19)THENM$="YUM YUM!":GOSUB1:EN=EN+60:G(X)=30
2524 IFG(X)=30THEN2475
2530 IFH(1)<10ORH(1)>16THEN2550
2540 IFI(H(1)-10)=G(X)THENK=1:GOTO2610
2550 IFH(2)<10ORH(2)>16THEN2570
2560 IFI(H(2)-10)=G(X)THENK=2:GOTO2610
2570 IFH(3)<10ORH(3)>16THEN2590
2580 IFI(H(3)-10)=G(X)THENK=3:GOTO2610
2590 IFLEFT$(X$,2)="TI"THENM$="SORRY YOU DON'T HAVE A CAN OPENER.":GOSUB1:GOTO2600
2591 M$="SORRY YOU DON'T HAVE THE KEY FOR THE "+X$+".":GOSUB1
2600 GOTO2475
2610 M$="YES! YOU HAVE THE CORRECT KEY.":GOSUB1
2612 M$="THE "+S$(H(K))+" FITS.":GOSUB1
2615 M$="RIGHT, LET'S OPEN IT...":GOSUB1
2615 FORA=1TO2500:NEXT:CLS:PRINT
2620 M$="INSIDE IS "+S$(J(G(X)))+".":GOSUB1
2630 PRINT:M$="ENTER SWAP, PICKUP OR LEAVE.":GOSUB1
2640 INPUTG$:G$=LEFT$(G$,2)
2650 IFG$<>"SW"ANDG$<>"PI"THEN2475
2660 M$="YOU HOLD "
2665 FORA=1TO3:IFH(A)<>0THENM$=M$+S$(H(A))+", "
2666 NEXT:M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
2680 IFH(1)*H(2)*H(3)<>0ANDG$="PI"THENM$="YOU HOLD 3 ITEMS.":GOSUB1:GOTO2630
2690 IFH(1)+H(2)+H(3)=0ANDG$="SW"THENM$="YOU HAVE NOTHING TO SWAP.":GOSUB1:GOTO2630
2710 IFG$="SW"THEN2720
2715 IFH(3)=0THENA=3
2717 IFH(2)=0THENA=2
2716 IFH(1)=0THENA=1
2719 GOTO2730
2720 M$="WHICH ITEM TO SWAP";
2725 INPUTA
2726 IFA<0ORA>3THEN2720
2727 IFH(A)=0THEN2720
2730 IFG$="PI"THENH(A)=J(G(X)):J(G(X))=30:GOTO2475
2740 K=H(A):H(A)=J(G(X)):J(G(X))=K:GOTO2475
2800 IFG$<>"PI"THEN2900
2805 IFG(X)=30THENM$="NOTHING HERE.":GOSUB1:GOTO2475
2807 IFG(X)<8THENM$="A MAGICAL FORCE STOPS YOU FROM TAKING IT.":GOSUB1:GOTO2475
2810 IFH(1)+H(2)+H(3)<>0THENPRINT"YOU HOLD THE FOLLOWING:"
2820 FORA=1TO3:IFH(A)<>0THENM$=MID$(STR$(A),2)+". "+S$(H(A)):GOSUB1
2825 NEXT
2830 IFH(1)*H(2)*H(3)<>0THENM$="SORRY, NO HANDS OR POCKETS.":GOSUB2475
2840 IFH(3)=0THENA=3
2850 IFH(2)=0THENA=2
2860 IFH(1)=0THENA=1
2870 H(A)=G(X):G(X)=30
2875 M$="OK, I'VE PICKED UP THE "+RIGHT$(S$(H(A)),LEN(S$(H(A)))-2)+".":GOSUB1
2880 GOTO2475
2900 IFG$<>"SW"THEN3000
2910 IFG(X)<7THENM$="SORRY-- TOO HEAVY TO MOVE.":GOSUB1:GOTO2475
2920 IFH(1)+H(2)+H(3)<>0THENPRINT"YOU HOLD THE FOLLOWING:"
2930 FORA=1TO3:IFH(A)<>0THENM$=MID$(STR$(A),2)+". "+S$(H(A)):GOSUB1
2933 NEXT
2950 IFH(1)+H(2)+H(3)=0THENM$="YOU HAVE NOTHING TO SWAP.":GOSUB1:GOTO2475
2960 PRINT"PLEASE ENTER ITEM TO SWAP"
2970 INPUTA
2980 IFA<0ORA>3THEN2960
2982 IFH(A)=0THEN2960
2985 M$="OK, I'VE NOW GOT "+S$(G(X)):GOSUB1
2990 K=H(A):H(A)=G(X):G(X)=K
2992 GOTO2475
3000 IFG$="EN"THENINPUT"ARE YOU SURE?";I$:IFLEFT$(I$,1)="Y"THENEND
3100 IFG$<>"DU"THEN3300
3110 IFG(X)<>30THENM$="SORRY THERE IS ALREADY SOMETHING HERE.":GOSUB1:GOTO2475
3120 IFH(1)+H(2)+H(3)=0THENM$="YOU DON'T HAVE ANYTHING WITH YOU.":GOSUB1:GOTO2475
3130 PRINT"YOU NOW HOLD THE FOLLOWING:"
3140 FORA=1TO3:IFH(A)<>0THENM$=MID$(STR$(A),2)+". "+S$(H(A)):GOSUB1
3141 NEXT
3150 INPUT"WHICH ONE (1-3)";A
3160 IFA<0ORA>3ORH(A)=0THEN3150
3170 G(X)=H(A):H(A)=0
3180 GOTO2475
3300 REM movement
3310 IFG$="NO"THENY=1:GOTO3400
3320 IFG$="EA"THENY=2:GOTO3400
3330 IFG$="UP"THENY=3:GOTO3400
3340 IFG$="SO"THENY=4:GOTO3400
3350 IFG$="WE"THENY=5:GOTO3400
3360 IFG$="DO"THENY=6:GOTO3400
3370 M$="DON'T UNDERSTAND YOUR BANTER, SQUADRON LEADER.":GOSUB1
3380 GOTO2475
3400 X1=C(X,Y)
3410 IFX1=0THENM$="CAN'T GO THAT WAY":GOSUB1:GOTO2475
3420 X=X1
3430 IFTI<0ORX<>1THEN3500
3440 IFM(1)=28ORH(2)=28ORH(3)=28THEN3460
3450 GOTO3500
3460 M$="YOU GOT OUT WITH THE TREASURE!":GOSUB1:FORC1=1TO240STEP10:SOUNDC1,1:NEXT:SOUND240,5
3480 GOTO6000
3500 GOTO2010
4000 IFC(X,1)<>0THENM$=M$+"north, "
4010 IFC(X,2)<>0THENM$=M$+"east, "
4020 IFC(X,3)<>0THENM$=M$+"up, "
4030 IFC(X,4)<>0THENM$=M$+"south, "
4040 IFC(X,5)<>0THENM$=M$+"west, "
4050 IFC(X,6)<>0THENM$=M$+"down, "
4055 M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1:PRINT
4060 RETURN
5700 IFH(1)=20ORH(2)=20ORH(3)=20THEN5730
5710 M$="UNFORTUNATELY, YOU NE COMPRENEZ PAS.":GOSUB1:RETURN
5730 M$="AS YOU HAVE THE DICTIONARY YOU CAN TALK TO HIM.":GOSUB1
5740 M$="ENTRE VOUS 'Y' A RETURNER A LA CAVE-- IL A DIT.":GOSUB1
5750 A$=INKEY$:ON-(A$<>"")GOTO5750:IFA$="Y"THENX=1
5760 IFRND(0)>.3THENRETURN
5770 FORA=1TO3:IFH(A)=28THENC1=A:A=3:NEXT:A=C1:GOTO5780
5771 NEXT:A=INT(RND(0)*3+1)
5780 M$="HOWEVER, THE FELLOW IS UNRATBAG AND STEALS "+S$(H(A))+" FROM YOU.":GOSUB1
5790 IFH(A)=28THENG(INT(RND(0)*N+1))=28
5800 H(A)=0:FORA=1TO2500:NEXT
5810 M$=MERCI BIEN!!":GOSUB1:RETURN
6000 PRINT"HIT ANY KEY FOR NEXT GAME"
6010 A$=INKEY$:ON-(A$="")GOTO6010:RUN
8000 M$="YOU ARE ON HOLIDAY IN CORNWALL AND ARE STAYING IN YOUR AUNT'S OLD HOUSE NEAR THE SEA.":GOSUB1
8020 PRINT:M$="THE HOUSE LIES IN AN AREA KNOWN AS 'SMUGGLER'S DEN' AND THE TOWNFOLK "
8030 M$=M$+"WILL TELL STORIES OF CLEVER SMUGGLERS HIDING THEIR WARES IN THE MAZE OF CAVES AROUND THE COASTLINE."
8040 GOSUB1
8050 RETURN
8100 CLS
8110 M$="IT IS SAID THAT ONE MADMAN WHO LIVED IN THE HOUSE IN THE LAST "
8120 M$=M$+"CENTURY HAD STORED HIS TREASURE SOMEWHERE AND LOCKED IT USING "
8130 M$=M$+"KEYS WHICH WERE THEMSELVES LOCKED IN BOXES!":GOSUB1
8140 PRINT:M$="HOWEVER, MANY ATTEMPTS HAVE BEEN MADE TO RECOVER THE TREASURE "
8150 M$=M$+"AND SO THERE MAY BE KEYS LEFT BY PREVIOUS FRUSTRATED EXPLORERS-- "
8160 M$=M$+"OR SO IT IS SAID: IT IS PROBABLY MOSTLY GOSSIP!":GOSUB1
8170 RETURN
8400 CLS
8410 M$="YOU DECIDE TO DO SOME EXPLORING ON YOUR OWN. YOU GO DOWN TO "
8420 M$=M$+"THE SEA FRONT AND WALK TO THE CLIFF UNDER THE HOUSE.":GOSUB1
8430 M$="THE TIDE IS OUT, BUT IT WILL RETURN WITHIN A FEW HOURS.":GOSUB1
8440 M$="YOU ARE ABLE TO HOLD 3 THINGS IN YOUR HANDS AND POCKETS "
8450 M$=M$+"SHOULD THE NEED ARISE THAT IS.":GOSUB1
8460 GOSUB9150
8500 PRINT:M$="YOU HAVE A COMPASS AND A WATCH. AND YOU HAVE A TORCH "
8510 M$=M$+"AND A GOOD PAIR OF SHOES FOR CLIMBING.":GOSUB1
8520 M$="IF YOU GET HUNGRY OR TIRED, JUST RETURN TO THE BEACH.":GOSUB1
8530 FORA=1TO2500:NEXT
8560 RETURN
9000 IFEN<0THENEN=0
9001 ONINT(EN/10)GOTO9010,9020,9030,9040,9050,9060,9070,9080,9090
9002 IFEN>20THEN9090
9004 M$="YOU ARE EXTREMELY EXHAUSTED.":GOSUB1
9005 IFEN<2THENM$="YOU HAVE BECOME A VICTIM OF THE CAVES OF TANGRIN.":GOSUB1:END
9006 RETURN
9010 M$="YOU ARE VERY WEARY, AND MUST GET OUT FAST.":GOSUB1:RETURN
9020 M$="YOU ARE GETTING TIRED-- YOU SHOULD THINK OF RETURNING.":GOSUB1:RETURN
9030 M$="YOU ARE STILL QUITE FIT, BUT GETTING PECKISH.":GOSUB1
9035 RETURN
9040 M$="YOU ARE NOT QUITE AS STRONG NOW.":GOSUB1:RETURN
9050 REM
9060 REM
9070 M$="YOU ARE STRONG ENOUGH TO TACKLE ANYTHING.":GOSUB1:RETURN
9080 M$="YOU ARE AT 100% EFFICIENCY!":GOSUB1:RETURN
9090 M$="YOU ARE AT MAXIMUM STRENGTH-- MAKE THE MOST OF IT.":GOSUB1:RETURN
9100 CLS
9105 PRINT@32*5+5,"THE 6 KEYS OF TANGRIN"
9106 PRINT@32*6+5,"*********************"
9107 PRINT:PRINT:PRINT"   DO YOU NEED INSTRUCTIONS?":GOTO80
9150 PRINT:PRINT"PLEASE PRESS space";
9160 Z$=INKEY$:ON-(Z$<>" ")GOTO9160:CLS:RETURN
9200 REM BY G. PHILLIPS
9210 REM MC-10 EDITS BY
9220 REM JIM GERRIE
