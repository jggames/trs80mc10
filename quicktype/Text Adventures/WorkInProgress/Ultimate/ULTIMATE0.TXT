10 'COPYRIGHT (C) CLOAD 1982
20 X=RND(-TIMER):CLEAR60:CLS:L=1:D=250:S=250:CL=1
30 ' THIS IS BY PHIL EDWARDSON
40 DIM I$(11),O$(11),O(11),V(11),L$(29),M$(18),M(18),T$(7),D$(4,29),D(4,29),U(11),U1(11),K$(8),T(7),Q$(3),Q(3)
45 PRINT@227,"DO YOU WANT INSTRUCTIONS?";
46 A$=INKEY$:IF A$="" THEN 46 ELSE PRINT A$
47 IF A$="Y" THEN GOSUB 4000 ELSE IF A$<>"N" THEN 45
50 CLS:PRINT@165,"ULTIMATE ADVENTURE"'BY PHIL EDWARDSON
60 FORX=1TO11:READI$(X),O$(X),O(X),V(X),U(X):U1(X)=U(X):NEXT
70 FORX=1TO15:READL$(X):NEXT
80 FORX=16TO29:L$(X)="IN A PORTHOLE":NEXT
90 A$="F2U2UU32ZA2<2P2<222F2222222P772227222P7<772A7<<A2A2<K7<27P227<7FP<7A7F<2527477477722272U22<2<Z772<7<7<272A77A2727727FK2Z2<27227K4<22222<<7777722K2<2A<U2F427PPP222<7<7<72F<FF<AA<7P7<U727742A22272<22P"
100 GOTO150
110 FORX=1TO5
120 T(X)=RND(11):IFT(X)=1THEN120
130 NEXTX
140 T(6)=7:T(7)=12:RETURN
150 FORX=1TO7:READT$(X):NEXT
160 FORY=1TO29:FORX=1TO4:READD$(X,Y),D(X,Y):NEXTX:NEXTY
170 FORX=1TO18:READM$(X),M(X):NEXT
180 PRINT@229,"CONTINUE OLD GAME";:INPUT C$
190 IFLEFT$(C$,1)="Y"THEN1920ELSEPRINT@480,"ENTER LEVEL 1-5 (1=EASIEST) ";
200 C$=INKEY$:HD=VAL(C$):IFHD=0ORHD>5THEN200
210 PRINTC$;:GOSUB2900:GOSUB110
220 GOSUB3120
230 MV=MV+1:CLS
240 IFS<1THEN3090
250 PRINT"strength";S,"money $";D
260 IFS<30THENGOSUB2700
270 PRINT:PRINT"location";"        ";"items";I:PRINTL$(L)
280 PRINT:PRINT"directions"
290 FORX=1TO4
300 IFD$(X,L)="-"THEN320
310 PRINTD$(X,L);"  ";
320 NEXTX:PRINT
330 PRINT:PRINT"you";CHR$(128);"can";CHR$(128);"see "
340 IFL=1THENPRINT"MARKET CONTENTS":GOTO400
350 IFS>240THEN360ELSEIFL=13THENPRINT"STRENGTH RECOVERED":S=250:GOSUB2830:GOTO220
360 '
370 FORX=1TO11:IFO(X)=L THENPRINTO$(X)
380 NEXT X
390 GOTO570
400 IFL=1ANDD>999THEN3010
410 GOTO2920
420 PRINT:SOUND160,4:SOUND185,1:INPUT"NOW WHAT";C$
430 SOUND 180,1
440 B$=LEFT$(C$,3)
450 IF C$="LIST CONTENTS"THEN860
460 IFB$="BUY"THEN940
470 IFC$="LIST LOAD"THENGOSUB1040:GOTO400
480 IFB$="GO "THEN1060
490 IFB$="SEA"THEN1370
500 IFC$="GET TREASURE"THEN1490
510 IFC$="CLEAR SCREEN"THENMV=MV-1:GOTO220
520 IFB$="DRO"THEN1700
530 IFB$="GET"THEN1830
540 IFB$="SWI"THENB$=MID$(C$,6):GOTO1090
550 IFC$="SAVE GAME"THEN2050
560 PRINT"DON'T UNDERSTAND":GOTO400
570 '
580 IFRND(20-HD)>5THEN400
590 A=0:B=0:N=0:B$="":Z=0:Q1=0:W=0
600 FORX=1TO18
610 IFM(X)=L THENB=B+1:Q$(B)=M$(X):Q(B)=X
620 NEXTX
630 IFB=0THEN400
640 AT=1:P=RND(B):Q$=Q$(P):Q1=Q(P):PRINTQ$
650 GOSUB2840
660 INPUT"USE WHAT";C$
670 IFC$="LIST LOAD"THENGOSUB1040:PRINT:GOTO660
680 IFLEFT$(C$,3)="GO "THENPRINT"CAN'T MOVE WHILE UNDER ATTACK!":GOSUB2860:GOTO660
690 FORX=1TO11
700 IFC$=I$(X)ANDO(X)<>0THENPRINT"DON'T HAVE ";C$:GOTO770
710 IFC$=I$(X)ANDO(X)=0THENW=X:U1(X)=U1(X)-1:GOTO730
720 NEXTX
730 Z=((W-1)*18)+Q1
740 IFZ<1OR Z>198THEN A=0:GOTO770
750 B$=MID$(A$,Z,1)
760 A=ASC(B$)-50
770 IFC$="HANDS"THENA=5
780 P=INT(RND((HD*35)/2))-A
790 IFQ1=9ANDP>0THEN2710
800 IFP<1THENP=0:PRINT"YOU ESCAPED WITHOUT INJURY!":GOSUB2910:GOSUB2900:GOTO220
810 S=S-P
820 GOSUB2850
830 PRINT"YOU LOST";P;"UNITS OF STRENGTH":PRINT"DURING THE ENCOUNTER"
840 '
850 AT=0:GOSUB2900:GOTO220
860 IFL<>1THENPRINT"HAVE TO BE IN MARKET":GOTO400
870 CLS:PRINT"market";CHR$(128);"contents  YOU HAVE $";D
880 PRINT@64,"ITEM","  PRICE  USES"
890 FORX=1TO11
900 IFO(X)=1THENPRINTO$(X),"  ";:PRINTUSING"$###";V(X);:PRINT"    ";U1(X)
910 NEXT X
920 IFCL=1THENPRINT"CLUE","  ";:PRINTUSING"$###";D;:PRINT"    ";1:IFI=0THENPRINT@478,"";
930 GOTO400
940 IFL<>1THENPRINT"CAN ONLY BUY FROM MARKET":GOSUB2860:GOTO400
950 B$=MID$(C$,5)
960 IFB$="CLUE"THENGOSUB3270:GOTO400
970 IFI>7THENPRINT"ALREADY HAVE MAXIMUM LOAD":GOSUB2860:GOTO400
980 FORX=1TO11
990 IFB$=I$(X)ANDO(X)=0THEN1880
1000 IFB$=I$(X)ANDO(X)=1ANDD<V(X)THENPRINT"NEED MORE MONEY!":GOSUB2860:GOTO400
1010 IFB$=I$(X)ANDO(X)=1ANDD=>V(X)THENO(X)=0:D=D-V(X):I=I+1:S=S-2:GOTO220
1020 NEXTX
1030 GOTO560
1040 FORX=1TO11:IFO(X)=0THENPRINT"U";U1(X);" ";O$(X)
1050 NEXT X:RETURN
1060 B$=MID$(C$,4)
1070 IFO(5)=0THEN1090
1080 IF(L=28ANDB$="W")OR(L=26ANDB$="E")OR(L=22ANDB$="S")THENPRINT"IT'S TOO DARK":GOTO1290
1090 IFL=11ANDB$="D"ANDO(8)<>0THENPRINT"NEED DIVING GEAR":GOTO1290
1100 IFL=11ANDB$="D"ANDO(3)=0THENPRINT"CAN'T DIVE WITH RAFT":GOTO1290
1110 IFO(4)=0THEN1130
1120 IF(L=19ANDB$="E")OR(L=22ANDB$="N")OR(L=23ANDB$="NW")THENPRINT"CAN'T - NO AIR":GOTO1290
1130 IFO(3)=0ORLEFT$(C$,3)="SWI"THEN1160
1140 IF(B$="S"ANDL=10)OR(B$="W"ANDL=29)THENPRINT"NEED SOMETHING TO FLOAT ON":GOTO1290
1150 IFL=11ORL=12THENPRINT"HAVE TO SWIM":GOTO1290
1160 IFO(5)<>0THEN1180
1170 IF(L=28ANDB$="W")OR(L=26ANDB$="E")OR(L=22ANDB$="S")THENU1(5)=U1(5)-1
1180 IFL=11ANDO(8)=0ANDB$="D"THENU1(8)=U1(8)-1
1190 IFO(4)<>0THEN1210
1200 IF(L=19ANDB$="E")OR(L=22ANDB$="N")OR(L=23ANDB$="NW")THENU1(4)=U1(4)-1
1210 IFO(3)<>0THEN1230
1220 IF(L=10ANDB$="S")OR(L=29ANDB$="W")THENU1(3)=U1(3)-1
1230 FORX=1TO4
1240 IFLEFT$(C$,3)="SWI"THEN1260
1250 IFB$=D$(X,L)THENL=D(X,L):S=S-2:GOTO220
1260 IFB$=D$(X,L)THENL=D(X,L):S=S-7:GOTO220
1270 NEXTX
1280 GOTO1360
1290 GOSUB2860:GOTO400
1300 PRINT"RAFT DRIFTS AWAY!!":GOSUB2840:O(3)=10
1310 FORX=1TO11
1320 IFX=3THEN1340
1330 IFO(X)=11THENO(X)=10
1340 NEXTX
1350 GOTO400
1360 PRINT"CAN'T GO THAT WAY":GOSUB2860:GOTO400
1370 F=0
1380 IFO(3)<>0AND(L=11ORL=12)THENS=S-7
1390 IFO(3)=11ANDRND(3)=1THEN1300
1400 FORE=1TO7
1410 P=RND(HD+6)
1420 IFT(E)=L ANDP<5THENT$=T$(E):F=1:GOTO1440
1430 NEXTE
1440 GOTO2200
1450 IFF=1THENPRINT@167,"TREASURE FOUND!":GOSUB2870:GOTO1480
1460 NEXTE
1470 PRINT@167,"YOU FOUND NOTHING"
1480 S=S-3:GOSUB2900:GOTO220
1490 B$=LEFT$(T$,3):IFF=1ANDL=12ANDB$="SUN"THENN=300:A=100:GOTO1530
1500 IFF=1ANDL=7ANDB$="BUR"THENN=250:A=100:GOTO1530
1510 IFF=0THENS=S-3:PRINT"YOU FORGOT WHERE IT WAS":GOSUB2860:GOTO400
1520 N=100:A=50
1530 IFL=7ANDB$="BUR"ANDO(10)<>0THENPRINT"BURIED - NEED SHOVEL":GOSUB2860:GOTO400
1540 IFI>7THEN1840
1550 IFL=7ANDLEFT$(T$,3)="BUR"ANDO(10)=0THENU1(10)=U1(10)-1
1560 P=RND(N)+A
1570 PRINT"YOU HAVE FOUND A":PRINTT$;" WORTH $";P:T(E)=0:CL=1:I=I+1:GOSUB2830
1580 D=D+P:F=0:S=S-3
1590 '
1600 FORX=1TO7
1610 IFT(X)<>0THENGOSUB2900:GOTO220
1620 NEXTX
1630 PRINT"ALL TREASURES HAVE BEEN FOUND":GOSUB2870
1640 P=RND(500)+200
1650 PRINT"YOU HAVE EARNED A $";P;"BONUS"
1660 I=I-7
1670 D=D+P:PRINT:PRINT"NEW TREASURES NOW EXIST":GOSUB2830
1680 GOSUB110
1690 GOSUB2830:GOTO220
1700 IFL=1ORL>12THENPRINT"CAN'T DROP ITEMS HERE":GOSUB2860:GOTO400
1710 B$=MID$(C$,6)
1720 FORX=1TO11
1730 IFB$=I$(X)ANDO(X)=0THEN1760
1740 NEXTX
1750 PRINT"DON'T HAVE ";B$:GOSUB2860:GOTO400
1760 '
1770 IFO(3)=0ANDL=11THEN1810
1780 IFX<>3ANDO(3)<>11ANDO(3)<>0ANDL=11THENPRINT"YOUR ";B$:PRINT"SANK TO THE BOTTOM":I=I-1:S=S-1:GOSUB2880:O(X)=12:GOTO1820
1790 IF(X=5ANDL=8)OR(X=8ANDL=12)OR(X=4ANDL=6)THENPRINT"TOO DANGEROUS TO ";C$:GOSUB2860:S=S-1:GOTO1820
1800 IFL=12ANDX=3THEN1300
1810 O(X)=L:S=S-1:I=I-1
1820 GOSUB2900:GOTO230
1830 IFL=1THENPRINT"CAN ONLY BUY FROM MARKET":GOSUB2860:GOTO400
1840 IFI>7THEN970
1850 IFL=6ANDRND(10)<7THENPRINT"CAN'T QUITE GRAB IT":GOSUB2860:S=S-2:GOSUB2900:GOTO220
1860 B$=MID$(C$,5)
1870 FORX=1TO11
1880 IFB$=I$(X)ANDO(X)=0THENPRINT"ALREADY HAVE THE ";I$(X):GOSUB2860:GOTO400
1890 IFB$=I$(X)ANDO(X)=L THENO(X)=0:I=I+1:S=S-1:GOTO220
1900 NEXTX
1910 GOTO 560
1920 'LOAD GAME
1930 CLS:PRINT"LOAD OLD GAME":PRINT
1940 PRINT"POSITION TAPE":MOTORON
1950 PRINT"PRESS 'PLAY'"
1960 PRINT"HIT 'ENTER' WHEN READY"
1970 IFINKEY$=""THEN1970
1980 MOTOROFF
1990 OPEN"I",#-1,"GAME"
2000 INPUT#-1,L,S,D,I,MV,F,CL,HD
2010 FORX=1TO11:INPUT#-1,U1(X),O(X):NEXTX
2020 FORX=1TO7:INPUT#-1,T(X):NEXTX
2030 CLOSE#-1
2040 GOTO220
2050 CLS:PRINT"SAVE GAME TO TAPE":PRINT
2060 PRINT"POSITION TAPE":MOTORON
2070 PRINT"PRESS 'PLAY' & 'RECORD'"
2080 PRINT"HIT 'ENTER' WHEN READY"
2090 IFINKEY$=""THEN2090
2100 MOTOROFF
2110 FORY=1TO2:OPEN"O",#-1,"GAME"
2120 PRINT:PRINT"COPY";Y;"NOW RECORDING"
2130 PRINT#-1,L,S,D,I,MV,F,CL,HD
2140 FORX=1TO11
2150 PRINT#-1,U1(X),O(X):NEXTX
2160 FORX=1TO7
2170 PRINT#-1,T(X):NEXTX
2180 CLOSE#-1:NEXTY
2190 PRINT"COMPLETED";:END
2200 CLS
2210 PMODE0,1
2220 PCLS
2230 SCREEN 1,0
2240 H=RND(252):V=RND(188)
2250 FORJ=1TO8
2260 G=RND(8):Z=RND(20)+5
2270 IFG=1THENH=H-RND(20)-Z:V=V-RND(20)-Z
2280 IFG=2THENV=V-RND(20)-Z
2290 IFG=3THENH=H+RND(20)+Z:V=V-RND(20)-Z
2300 IFG=4THENH=H+RND(20)+Z
2310 IFG=5THENH=H+RND(20)+Z:V=V+RND(20)+Z
2320 IFG=6THENV=V+RND(20)+Z
2330 IFG=7THENH=H-RND(20)-Z:V=V+RND(20)+Z
2340 IFG=8THENH=H-RND(20)-Z
2350 IFH<1THENH=1
2360 IFV<1THENV=1
2370 IFH>252THENH=252
2380 IFV>188THENV=188
2390 LINE -(H,V),PSET:CIRCLE(H,V),2
2400 ON J GOSUB2470,2480,2490,2500,2510,2520,2530,2540
2410 NEXTJ
2420 IFF<>1THEN1470
2430 CIRCLE(H,V),5:GOSUB2870
2440 CIRCLE(H,V),7
2450 CIRCLE(H,V),10:CIRCLE(H,V),13:GOSUB2870
2460 GOTO1450
2470 PLAY"V31;T6;O1;L8;F;P8":RETURN
2480 PLAY"C;P8":RETURN
2490 PLAY"D;P8":RETURN
2500 PLAY"E;P8":RETURN
2510 GOTO2470
2520 GOTO2480
2530 GOTO2490
2540 GOTO2500
2550 RETURN
2560 DATA GUN,LARGE GUN,1,80,4,BOOTS,HIP BOOTS,1,40,3,RAFT,INFLATABLE RAFT,1,150,5,SUIT,SPACE SUIT,1,200,5,FLASHLIGHT,FLASHLIGHT,1,10,6,COAT,FUR COAT,1,80,3,MINESWEEPER,MINESWEEPER,1,100,3
2570 DATA GEAR,DIVING GEAR,1,150,4,KNIFE,HUNTING KNIFE,1,40,5,SHOVEL,SHOVEL,1,15,4,ROPE,ROPE,1,10,3
2580 DATA IN THE MARKET,WADING THROUGH A SWAMP,IN THE ARCTIC,IN A DENSE JUNGLE,IN AN EMPTY ROOM,FLOATING THROUGH OUTER SPACE,CRAWLING THROUGH THE DESERT,IN A DARK CAVE,WALKING THROUGH A MINE FIELD,AT THE BEACH,ON THE OCEAN,SWIMMING UNDERWATER
2590 DATA IN THE INFIRMARY,AT THE SOUTH DOOR OF MARKET,AT THE EAST DOOR OF MARKET
2600 DATA VALUABLE RING,BAG OF GOLD,BOX OF JEWELS,BOX OF COLLECTABLES,RAFFLE TICKET,BURIED CHEST OF TREASURES,SUNKEN TREASURE
2610 DATA S,14,E,15,-,1,-,1,S,16,W,15,E,17,-,2,NW,17,S,20,-,3,-,3,N,14,NE,18,SE,21,S,25,N,16,W,18,S,24,E,19,W,19,SW,22,SE,23,-,6
2620 DATA N,24,W,21,SE,26,-,7,N,22,SW,26,SE,28,-,8,N,20,NW,23,S,27,-,9,N,25,S,11,-,10,-,10,N,10,SE,29,D,12,-,11,U,11,-,12,-,12,-,12,NW,28,SW,29,N,27,-,13
2630 DATA N,1,S,4,-,14,-,14,E,2,W,1,-,2,-,2,N,2,S,5,-,16,-,16,W,2,E,3,-,17,-,17,W,4,E,5,-,18,-,18,W,5,E,6,-,19,-,19,N,3,S,9,-,20,-,20,W,4,E,7,-,21,-,21,N,6,S,8,-,22,-,22,NW,6,SE,9,-,23,-,23,N,5,S,7,-,24,-,24,N,4,S,10,-,25,-,25
2640 DATA W,7,E,8,-,26,-,26,N,9,S,13,-,27,-,27,W,8,E,13,-,28,-,28,W,11,E,13,-,29,-,29
2650 DATA ALLIGATOR ATTACKING,2,STUCK IN QUICK SAND,2,POLAR BEAR CHARGING,3,YOU'RE GETTING COLD,3,TIGER JUMPING YOU,4,RHINO CHARGING,4,A PYTHON SLITHERING AROUND YOU,4,METEORS ARE BOMBARDING YOU,6,BANDIT APPROACHING YOU,7
2660 DATA GIANT TIRANTULA LEAPS AT YOU,7,YOU'RE GETTING HOT,7,LAND MINES NEARBY,9,A BARACUDA IS NEARBY,12,SHARKS ATTACKING,11,A GIANT OCTOPUS GRABS YOU,12,BATS DIVING AT YOU,8,ROOF OF CAVE FALLING IN,8,YOU FELL IN A PIT,8
2670 '
2680 IFS<1THEN3090
2690 IFS>20THENRETURN
2700 PRINT"STRENGTH LEVEL DANGEROUSLY LOW!":FORX=1TO3:SCREEN0,1:FORY=1TO150STEP15:SOUNDY,1:NEXTY:SCREEN0,0:FORY=1TO60:NEXTY:NEXT X:RETURN
2710 PRINT"YOUR ATTEMPT FAILED!"
2720 Z=0
2730 FORX=1TO11
2740 IFO(X)=0THENZ=Z+1:K$(Z)=O$(X)
2750 NEXTX
2760 P=INT(RND(D))
2770 PRINT"BANDIT STOLE $";P;"FROM YOU":GOSUB2850:D=D-P
2780 P=RND(Z)
2790 IFZ>0THENPRINT"AND YOUR ";K$(P)
2800 FORX=1TO11:IFO$(X)=K$(P)THEN O(X)=1:U1(X)=U(X):I=I-1
2810 NEXTX
2820 GOTO840
2830 FORX=1TO250STEP10:SOUNDX,1:NEXTX:RETURN
2840 FORX=1TO3:SCREEN0,1:FORY=150TO230STEP10:SOUNDY,1:NEXT:SCREEN0,0:FORZ1=1TO30:NEXT:NEXT:RETURN
2850 FORX=1TO3:SOUND1,6:FORY=1TO10:NEXTY:NEXTX:RETURN
2860 FORX=1TO3:SOUND220,4:FORY=1TO30:NEXTY:NEXTX:RETURN
2870 FORX=1TO7:SOUND(RND(40)+200),1:NEXTX:RETURN
2880 FORY=201TO1STEP-10:SOUNDY,1:NEXTY:RETURN
2890 FORX=1TO2:PLAY"V31;O2;L8;F;P8;C;P8;D;P8;E;P8":NEXTX:RETURN
2900 FORN=1TO1000:NEXTN:RETURN
2910 FORX=100TO210STEP15:SOUNDX,1:NEXTX:FORX=200TO100STEP-15:SOUNDX,1:NEXTX:RETURN
2920 IFL<16ORRND(25)>3THEN420
2930 PRINT"PORTHOLE MALFUNCTION!!"
2940 FORX=1TO30:SOUNDRND(250),1:NEXTX
2950 L=RND(13)
2960 IFL=12ANDO(8)<>0THENL=11
2970 IFL=6ANDO(4)<>0THEN2950
2980 IFL=8ANDO(5)<>0THEN2950
2990 PRINT"YOU HAVE BEEN TELEPORTED":PRINT"TO ";L$(L)
3000 GOSUB2900:GOTO220
3010 GOSUB2900:CLS:FORX=1TO5:CLS(RND(8))
3020 PRINT@165,"YOU HAVE SUCCESSFULLY";:PRINT@201,"COMPLETED THE";:PRINT@230,"ULTIMATE ADVENTURE!";
3030 FORY=150TO200STEP5
3040 SOUNDY,1:NEXTY:NEXTX
3050 PRINT@294,"TOTAL MOVES =";MV;
3060 PRINT@480,"   >>> CONGRATULATIONS <<<      ";
3070 IFINKEY$=""THEN3070
3080 END
3090 CLS:PRINT@164,"TOO BAD, YOU GOT KILLED!";
3100 SOUND60,12:SOUND1,24
3110 GOTO3070
3120 FORZ=1TO11
3130 IF(Z>1ANDZ<5)ORZ>7THEN3200
3140 IFO(Z)=12OR(L=12ANDO(Z)=0)THEN3230
3150 IFO(3)=11ANDO(Z)=11THEN3200
3160 IFL=11ANDO(3)=0ANDO(Z)=11THEN3200
3170 IFL=11ANDO(3)<>0ANDO(Z)=0THEN3230
3180 IFO(Z)=11ANDO(3)<>11THEN3190ELSE3200
3190 PRINT"YOUR ";O$(Z):PRINT"SANK TO THE BOTTOM":GOSUB2880:O(Z)=12:GOTO3130
3200 IFU1(Z)<1ANDO(Z)=0THENO(Z)=1:U1(Z)=U(Z):I=I-1
3210 NEXTZ
3220 RETURN
3230 PRINT"YOUR ";O$(Z):PRINT"GOT RUINED BY THE WATER!":GOSUB2840:GOSUB2900
3240 IFO(Z)=0THENO(Z)=1:U1(Z)=U(Z):I=I-1
3250 IFO(Z)=12THENO(Z)=1:U1(Z)=U(Z)
3260 GOTO3200
3270 IFCL=1THENY=RND(7):IFT(Y)=0THEN3270ELSEPRINT:PRINT"clue":PRINT"YOU WILL FIND TREASURE WHILE":PRINTL$(T(Y)):PRINT:CL=0:D=0:RETURNELSEPRINT"CAN'T":RETURN
4000 CLS:PRINT"   >>> ultimate";CHR$(128);"adventure <<<":PRINT
4005 PRINT"     BEING  THE  ADVENTURER  YOUARE,   AND  HAVING  SUCCESSFULLYSURVIVED ALL THE  ADVENTURES YOUHAVE    ATTEMPTED,    YOU    AREOBVIOUSLY  OVERQUALIFIED FOR ANYMORE    EXEDITIONS    OF     THEDUNGEONS,  HAUNTED HOUSES, SPACETREKS  OR THE";
4010 PRINT" LIKE. THERFORE YOUHAVE BEEN  CHOSEN  TO  UNDERTAKE(OR   OVERTAKE) THE":PRINT"       ultimate adventure":GOSUB 5000
4020 PRINT"     YOU BEGIN WITH  $250 AT THETRADITIONAL  MARKET  PLACE WHEREYOU MAY  PURCHASE  AN  ARRAY  OFITEMS TO  ASSIST YOU IN FIGHTINGFOES,  SURVIVING  THE  ELEMENTS,AND   FINDING  TREASURES.   YOURGOAL IS TO ACCUMULATE $1,000  ORMORE   IN";
4030 PRINT"  TREASURES  AND  OTHERVALUBLE ITEMS AND RETURN  SAFELYTO  THE  MARKET. IF  YOU  CAN DOTHIS, YOU WILL HAVE SUCCESSFULLYCOMPLETED      THE      ultimateadventure.  HOWEVER,   ACHIEVINGYOUR GOAL IS NOT AS  EASY AS YOUMAY   THINK.";
4040 GOSUB 5000:PRINT"    THERE ARE MANY OBSTICALS YOUMUST OVERCOME. A DESCRIPTION  OFTHE  MORE  COMMON  PROBLEMS  YOUWILL ENCOUNTER FOLLOWS:";:GOSUB 5000
4060 PRINT"     strength: YOU WILL BEGIN   WITH 250 UNITS OF STRENGTH. EACHMOVE DIMINISHES YOUR STRENGTH TOSOME DEGREE. HOWEVER, MOST OF   YOUR A STRENGTH WILL BE LOST    DURING ECOUNTERS, DEPENDING ON  WHETHER YOU HAVE HAVE ";
4070 PRINT" OR USE   THE CORRECT ITEM (WHICH YOU     PURCHASED FROM THE MARKET)  FOR DEFENCE. IF YOU LOSE ALL YOUR   STRENGTH THEN YOU DIE AND THE   ADVENTURE IS UNFORTUNATELY OVER.BY FINDING YOUR WAY TO, AND     ENTERING THE infirmary";
4071 PRINT           " YOUR     STRENGTH WILL BE REJUVINATED.";:GOSUB 5000
4080 PRINT"     money:   EACH  OBJECT   YOUPURCHASE  FROM  THE MARKET  WILLDECREASE THE AMOUNT OF MONEY YOUHAVE. IT  IS  ALSO  POSSIBLE  TOHAVE  MONEY  STOLEN   FROM  YOU.OBVIOUSLY, BY FINDING TREASURES YOU WILL INCREASE YOUR";
4090 PRINT" WEALTH.  EACH OBJECT HAS IT'S OWN VALUE  AND NUMBER OF USES. ONCE THE    ALLOCATED USES  HAVE  BEEN      EXHAUSTED YOU WILL AUTOMATICALLYLOSE THAT OBJECT AND WILL NEED  TO PURCHASE ANOTHER ONE FROM THEMARKET";:GOSUB 5000
4100 PRINT"     load: THE OBJECTS YOU CARRYDURING YOUR ADVENTURE COMPRISE   YOUR 'load'. YOU MAY CARRY A     MAXIMUM LOAD OF EIGHT ITEMS    (INCLUDING TREASURES).  IF YOU  ARE CARRYING THE MAXIMUM LOAD   WHEN YOU FIND A";
4110 PRINT" TREASURE,  YOU  MUST 'drop' ONE ITEM BEFORE YOU CAN 'get' THE TREASURE.":PRINT"TREASURES CAN NOT BE DROPPED.   HENCE, THE MORE TREASURES YOU   ACCUMULATE, THE FEWER OBJECTS   YOU MAY HAVE IN YOUR LOAD.";:GOSUB 5000
4120 PRINT"     SINCE THERE IS A TOTAL OF  SEVEN TREASURES, YOU WILL HAVE  TO DROP ALL BUT ONE ITEM TO GET THE LAST TREASURE. ONCE YOU HAVEFOUND AND GOT THE LAST TREASURE SEVERAL THINGS WILL HAPPEN."
4130 PRINT"     FIRST, YOU WILL RECEIVE AN EXTRA MONETARY BONUS FOR YOUR   ACHIEVEMENT. SECOND, ALL THE    TREASURES WILL BE GIVEN NEW     VALUES AND LOCATIONS, AND BE    REDISTRIBUTED. THIRD, YOUR LOAD WILL BE DECREASED BY SEVEN";
4140 PRINT" ITEMS(THE SEVEN TRASURES), ALLOWING  YOU TO CARRY MORE OBJECTS";:GOSUB 5000
4150 PRINT"     portholes: THE DIFFERENT   AREAS OF YOU ADVENTURE ARE      CONNECTED BY PORTHOLES. THERE   ARE NO TREASURES TO BE FOUND IN THE PORTHOLES. NORMALLY, UPON   ENTERING A PORTHOLE YOU MAY     LOGICALLY PROGRESS TO";
4160 PRINT" THE NEXT  AREA OR RETREAT TO FROM WHERE   YOU CAME. HOWEVER, THE PORTHOLESHAVE BEEN KNOWN TO MALFUNCTION. IF THIS HAPPENS WHILE YOU ARE INONE, YOU WILL BE TELEPORTED TO ARANDOM LOCATION IN ADVETURELAND.";:GOSUB 5000
4170 PRINT"THE FOLLOWING IS A DESCRIPTION  OF SOME OF THE COMMANDS YOU MAY USE DURING YOUR ADVENTURE":PRINT:PRINT "     note: ALL COMMANDS MUST BE            IN UPPER CASE";:GOSUB 5000
4180 PRINT:PRINT"ON THE 'now what?' CUE, ENTER A TWO-WORD COMMAND. TO MOVE NORTH INPUT 'go n', TO MOVE SOUTHWEST INPUT 'go sw', ETC. TO PERFORM  OTHER FUNCTIONS, INPUT TWO-WORD COMMANDS LIKE 'get treasure' OR 'drop (item)', OR ";
4190 PRINT" 'get (item)'.";:GOSUB 5000:PRINT:PRINT"THE ONLY ONE-WORD COMMAND YOUR  COMPUTER WILL UNDERSTAND ON THE 'now what?' CUE IS 'search' WHENYOU WISH TO SEARCH FOR TREASURE.";
4200 PRINT"     note: YOU MAY HAVE TO      SEARCH ONE AREA SEVERAL TIMES TOFIND A TREASURE. ALSO THERE MAY BE MORE THAN ONE TREASURE IN A  GIVEN AREA.";:GOSUB 5000
4210 PRINT:PRINT"THE 'use what?' CUE APPEARS WHENYOU ARE UNDER ATTACK BY A FOE ORAN ELEMENT.":PRINT             "     USE ONE-WORD COMMANDS SUCH AS 'gun' OR 'rope' ON THIS CUE."
4220 PRINT:PRINT"     hint: IF YOU DON'T HAVE AN APPROPRIATE ITEM TO  USE TO     DEFEND YOUSELF, TRY USING YOUR  'hands'";:GOSUB 5000
4230 PRINT:PRINT"     INPUTTING 'list load' AT   ANY TIME WILL PRINT A LIST OF   THE ITEMS YOU ARE CARRYING AND  THE NUMBER OF USES LEFT TO EACH ITEM.";
4240 PRINT:PRINT"     INPUTTING 'list contents'  WHILE YOU ARE IN THE MARKET WILLPRINT A LIST OF THE STORE'S     INVENTORY, THE NUMBER OF USES TOEACH ITEM AND THEIR RESPECTIVE  PRICES.";:GOSUB 5000
4250 PRINT:PRINT"     INPUTTING 'clear screen' ATANY TIME WILL GIVE YOU A FRESH  STATUS REPORT.":PRINT
4260 PRINT"     IF YOU ARE UNABLE TO       FINISH YOUR ADVENTURE IN ONE    SESSION (WHICH IS NOT UNUSUAL), YOU MAY STOP AND RECORD YOUR    CURRENT STATUS ON TAPE BY USING THE 'save game' COMMAND. THEN   WHEN YOU BEGIN AGAIN";
4270 PRINT", YOU WILL  BE ABLE TO CONTINUE FROM WHERE  YOU LEFT OFF.";:GOSUB 5000
4280 PRINT:PRINT"     BY NOW YOU SHOULD HAVE     ENOUGH KNOWLEDGE TO BEGIN YOUR  ULTIMAT ADVENTURE AND PROCEED   THROUGH THE COURSE WITHOUT MUCH STUMBLING. BUT, BEWARE - THERE  ARE STILL A FEW SURPRISES AHEAD OF YOU."
4290 PRINT:PRINT"        happy adventuring!!!";:GOSUB 5000
4300 RETURN
5000 PRINT@480,"press any key to continue";:SOUND 170,2
5010 A$=INKEY$:IF A$="" THEN 5010 ELSE CLS:RETURN
