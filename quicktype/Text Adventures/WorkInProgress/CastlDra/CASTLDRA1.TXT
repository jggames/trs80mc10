0 CLEAR1500:DIMCC,ZZ,M$,Z$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$=" "ORZ$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 POKE49151,64:FORCC=1TO100:NEXT:SOUND1,1:RETURN
100 CLEAR1000:GOSUB10000
200 CLS:M$="LOCATION: "+L$(L):IFL>26THENM$=M$+" PASSAGE"
201 GOSUB1
210 PRINT:M$="YOU SEE: ":C=0:IFL=9THENM$=M$+SB$+", ":C=1
211 IFL=11THENM$=M$+SH$+", ":C=1
212 IF(L=17ANDV1=0)OR(L=24ANDV2=0)OR(L=21ANDH=0)THENM$=M$+H$+", ":C=1
213 IFL=23THENM$=M$+"BATS FLYING AROUND, ":C=1
214 IFL=26THENM$=M$+"YOUR WIFE! - ASLEEP, ":C=1
215 IFL=34THENM$=M$+"IT'S DARK HERE! ":C=1
216 IFL=25THENM$=M$+"DRACULA! - ASLEEP IN HIS COFFIN! ":C=1
220 FORK=1TO9:IFO(K)=LTHENM$=M$+OL$(K)+", ":C=1
230 NEXT:IFC=0THENM$=M$+"NOTHING OF INTEREST  "
280 I$=LEFT$(M$,LEN(M$)-2):M$=I$:GOSUB1
290 PRINT:M$="DIRECTIONS: ":IFD(L,0)<>0THENM$=M$+"NORTH "
291 IFD(L,1)<>0THENM$=M$+"SOUTH "
292 IFD(L,2)<>0THENM$=M$+"EAST "
293 IFD(L,3)<>0THENM$=M$+"WEST "
390 GOSUB1:FORK=1TO32:PRINT"=";:NEXT
400 I$="":V$=I$:O$=I$:INPUT"WHAT NOW";I$
410 IFI$="N"THENI$="GO NOR"
411 IFI$="S"THENI$="GO SOU"
420 IFI$="E"THENI$="GO EAS"
421 IFI$="W"THENI$="GO WES"
440 IFLEN(I$)<3THENPRINT"WHAT??":GOTO400
450 V$=LEFT$(I$,3):FORK=LEN(I$)-2TO1STEP-1:IFMID$(I$,K,1)<>" "THENNEXT
451 IFV$="INV"THENDA=DA-1:GOTO950
460 O$=MID$(I$,K+1,3):W=0:GOSUB2000:ON-(W=1)GOTO400:IFV$="LOO"THEN200
461 IFO$="WOO"THENO$="FIR"
462 ON-(V$="BUY")GOTO850:ON-(V$="DRO")GOTO750:IFV$="BRE"THEN5210
463 ON-(V$="SHO")GOTO700:IFV$="PUS"THEN600
464 ON-(V$="CLI")GOTO650:IFV$="WEA"THEN550
465 ON-(V$="OPE")GOTO5220:IFV$="KNO"THEN5200
466 ON-(V$="HEL")GOTO4500:IFV$="EXA"THEN4000
467 ON-(V$="THR")GOTO540:IFV$="SHA"THEN530
468 ON-(V$="DRI")GOTO500:IFV$="WAK"ORO$="WIF"THEN520
469 ON-(V$="GO ")GOTO900:IFV$="GET"ORV$="STE"ORV$="TAK"THEN800
470 PRINT"SORRY, DONT UNDERSTAND!":GOTO400
500 IFO$<>"STA"THEN555
502 IFL<>25THENM$="NOT MUCH POINT IN DOING THAT HERE!":GOSUB1:GOTO400
504 IFO(2)<>-1THENPRINT"YOU HAVEN'T GOT A MALLET!":GOTO400
506 IFO(3)<>-1THENPRINT"YOU HAVEN'T GOT A STAKE!":GOTO400
508 M$="THE STAKE DRIVES HOME - INTO THE HEART OF DRACULA!":GOSUB1
512 M$="HE SCREAMS THEN MELTS INTO NOTHINGNESS! YOU HAVE WON!":GOSUB1
514 M$="YOUR WIFE HAS AWOKEN FROM HER SPELL AND YOU ARE BOTH FREE TO LEAVE. CONGRATULATIONS!":GOSUB1
518 PRINT:INPUT"PLAY AGAIN (Y/N)";A$:IFA$="Y"THENRUN
519 END
520 IFL=26THENPRINT"SHE WON'T WAKE UP!":GOTO400
521 GOTO555
530 IFO(9)<>-1THENPRINT"YOU DON'T HAVE A KNIFE!":GOTO400
531 ON-(O$<>"FIR"ORO(7)<>-1)GOTO555:O(7)=0:O(3)=-1
532 M$="YOU HAVE CONVERTED THE FIREWOOD INTO A WOODEN STAKE!":GOSUB1:GOTO400
540 FORK=1TO9:ON-(O$(K)=O$)GOTO752:NEXT:GOTO554
550 IFO$="CLO"ANDO(8)=-2THENPRINT"YOU'RE ALREADY WEARING IT!":GOTO400
552 IFO$="CLO"ANDO(8)=-1THENPRINT"O.K - IT'S ON!":O(8)=-2:GOTO400
553 IFO$="CRO"ANDO(1)=-1THENPRINT"O.K - IT'S ON!":GOTO400
554 IFO$="CRO"ORO$="CLO"THENPRINT"YOU HAVEN'T GOT IT!":GOTO400
555 PRINT"SORRY, CAN'T":GOTO400
600 IFL<>22OR(O$<>"BOO"ANDO$<>"SHE")THEN610
602 M$="THE BOOKSHELVES SWING INTO TO THE WALL!":GOSUB1
604 M$="A DOWNWARD SLOPING PASSAGE TO THE WEST IS REVEALED!":GOSUB1:D(22,3)=23:GOTO400
610 PRINT"NOTHING HAPPENS":GOTO400
650 IFL=14AND(O$="UP"ORO$="WAL"ORO$="DOO")THENL=15:GOTO200
652 IFO$="DOW"AND(L=15ORL=16)THENL=34:GOTO200
654 PRINT"CLIMB WHERE?":GOTO400
700 IFO(5)<>-1THENM$="YOU DON'T HAVE ANYTHING TO SHOOT WITH!":GOSUB1:GOTO400
701 IFL=8THENPRINT"NOT IN A CHURCH!!":GOTO400
702 IFBU=2THENM$="YOU'VE RUN OUT OF BULLETS!":GOSUB1:GOTO400
704 BU=BU+1:IFO$<>"SHO"OR(L<>9ANDL<>11)THEN710
706 GOSUB3:M$="BANG! THE SHOPKEEPER DROPS DEAD!":GOSUB1:IFL=9THENSB$=SG$:SB=1:GOTO400
708 SH$=SG$:SH=1:GOTO400
710 IFL<>21ORH<>0THENGOSUB3:PRINT"BANG!":GOTO400
720 GOSUB3:M$="BANG - THE HUMAN DROPS DEAD!":GOSUB1:H=1:GOTO400
750 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"DROP WHAT??":GOTO400
752 IFO(K)>0THENPRINT"YOU DONT HAVE THAT ITEM!":GOTO400
754 ML=ML-1:O(K)=L:PRINT"OKAY,IT'S DONE!":GOTO400
800 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"GET WHAT??":GOTO400
801 IFO(K)=-1THENPRINT"YOU ALREADY HAVE IT!":GOTO400
802 IFO(K)<>LTHEN853
803 IF(L=11ANDSH=0)OR(L=9ANDSB=0)THENPRINT"YOU MUST BUY IN A SHOP!":GOTO400
804 IFML=5THENPRINT"YOU CAN'T CARRY ANY MORE!":GOTO400
805 IFL=8ANDO$="WAT"ANDO(1)<>-1THEN809
806 O(K)=-1:ML=ML+1:PRINT"OKAY, YOU HAVE IT!":GOTO400
809 M$="THE PRIEST SAYS - YOU CAN ONLY HAVE HOLYWATER IF YOU HAVE A CROSS!":GOSUB1:GOTO400
850 IFL<>9ANDL<>11THENPRINT"YOU CAN ONLY BUY FROM A SHOP!":GOTO400
851 IFL=9ANDO$="BIB"THENPRINT"SORRY, SOLD OUT!":GOTO400
852 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"BUY WHAT???":GOTO400
853 IFO(K)<>L THENPRINT"I CAN'T SEE IT HERE!":GOTO400
854 IF(L=9ANDSB=1)OR(L=11ANDSH=1)THENPRINT"THERE'S NO-ONE TO BUY FROM!":GOTO400
855 IFO(K)=-1THEN801
856 IFL<>9ORO(6)=-1THEN860
858 M$="SHOPKEEPER SAYS - YOU CAN ONLY HAVE THE CROSS IF YOU HAVE HOLY WATER!":GOSUB1:GOTO400
860 IFML=5THEN804
861 GOTO806
900 IFO$="NOR"ANDD(L,0)<>0THENL=D(L,0):GOTO200
901 IFL=13THENFC=FC+1:IFFC=3THEN1000
902 IFO$="SOU"ANDD(L,1)<>0THENL=D(L,1):GOTO200
904 IFO$="EAS"ANDD(L,2)<>0THENL=D(L,2):GOTO200
906 IFO$="WES"ANDD(L,3)<>0THENL=D(L,3):GOTO200
909 PRINT"YOU CAN'T GO THAT WAY!":GOTO400
950 C=0:PRINT"YOU ARE CARRYING:":FORK=1TO9:IFO(K)<0THENPRINTTAB(5);OL$(K):C=1
952 NEXT:IFC=0THENPRINTTAB(10);"NOTHING AT ALL!"
954 GOTO400
1000 M$="A HORSE-DRAWN CARRIAGE ARRIVES. THERE IS NO DRIVER!":GOSUB1:GOSUB5000
1002 PRINT:M$="YOU ENTER THE CARRIAGE AND IT DRIVES OFF":GOSUB1:GOSUB5000
1004 PRINT:M$="THE CARRIAGE ARRIVES AT THE CASTLE AND YOU GET OUT. THE CARRIAGE DRIVES OFF":GOSUB1
1006 L=14:GOSUB5000:GOSUB5000:GOTO200
2000 IFL=34ANDO(4)<>-1THENM$="YOU FELL IN A HOLE AND BROKE YOUR NECK!":GOSUB1:GOTO22000
2100 IFL=23ORL=17ORL=24ORL=21THEN3010
2101 RETURN
3010 IFL<>23THEN3100
3011 M$="THE BATS ATTACK YOU - "
3020 IFO(8)=-2THENM$=M$+"BUT ARE WARDED OFF BY YOUR CLOAK!":GOSUB1:GOSUB5000:RETURN
3030 M$=M$+"AND EAT YOU!":GOSUB1:GOTO22000
3100 ON-(L<>21)GOTO3200:IFH=1THENRETURN
3101 M$="THE HUMAN FORM ATTACKS YOU WITH A KNIFE - "
3120 IFV$="SHO"THENGOSUB1:RETURN
3121 M$=M$+"AND STABS YOU TO DEATH!":GOSUB1:GOTO22000
3200 IFL<>17THEN3300
3210 IFV1=1THENRETURN
3211 M$="THE HUMAN FORM BARES HIS TEETH AND ATTACKS YOU - ":GOSUB1
3220 IFO$<>"WAT"ORO(6)<>-1THEN3230
3221 M$="HE SCREAMS ON CONTACT WITH THE HOLY WATER, THEN DISSAPPEARS!":GOSUB1:V1=1:O(6)=0
3222 W=1:RETURN
3230 IFO$<>"CRO"ORO(1)<>-1THEN3240
3231 M$="HE TOUCHES THE CROSS AND BOTH DISSAPPEAR!":GOSUB1:V1=1:O(1)=0:GOTO3222
3240 M$="AND SINKS HIS FANGS INTO YOUR NECK!!":GOSUB1:GOTO22000
3300 IFV2=1THENRETURN
3301 M$="THE HUMAN FORM BARES HIS FANGS ATTACKING YOU - ":GOSUB1
3320 IFO$<>"WAT"ORO(6)<>-1THEN3330
3321 M$="HE SCREAMS ON CONTACT WITH THE HOLY WATER, THEN DISSAPPEARS!":GOSUB1:V2=1:O(6)=0
3322 GOTO3222
3330 IFO$<>"CRO"ORO(1)<>-1THEN3240
3331 M$="HE TOUCHES THE CROSS AND BOTH DISSAPPEAR!":GOSUB1:V2=1:O(1)=0:GOTO3222
4000 C=0:IFO$="STR"ANDL<8THENPRINT"IT'S JUST A STREET!":C=1
4002 IFO$="SHO"ANDL<8THENPRINT"IT PROBABLY SELLS THINGS!":C=1
4003 IFO$="SHO"AND(L=9ORL=11)THENPRINT"HE LOOKS MEAN!":C=1
4004 IFO$="PRI"ANDL=8THENPRINT"HE'S WEARING A DOG-COLLAR!":C=1
4006 IFL=10THENPRINT"YOUR WIFE ISN'T HERE!":C=1
4008 IFL=14ANDO$="DOO"THENPRINT"IT LOOKS VERY STRONG!":C=1
4009 IFL=26ANDO$="WIF"THENPRINT"SHE APPEARS TO BE IN A TRANCE!":C=1
4010 IF(L=14ORL=15ORL=16)ANDO$="WAL"THENPRINT"THE WALL SURROUNDS THE CASTLE!":C=1
4012 IFL=22AND(O$="SHE"ORO$="BOO"ORO$="LIB")THENM$="LOTS OF BOOKS ON LOTS OF SHELVES!":GOSUB1:C=1
4014 IFL=25AND(O$="DRA"ORO$="COF"ORO$="VAU")THENPRINT"IT'S COLD IN HERE!!":C=1
4016 IFO$="CRO"AND(O(1)=-1ORO(1)=L)THENM$="A TYPICAL VAMPIRE-DISPELLING CROSS!":GOSUB1:C=1
4018 IFO$="CLO"AND(O(8)<0ORO(8)=L)THENPRINT"IT LOOKS VERY MYSTERIOUS!":C=1
4020 IFO$="MAL"AND(O(2)<0ORO(2)=L)THENPRINT"JUST RIGHT FOR DRIVING STAKES!":C=1
4022 IFO$="STA"AND(O(3)<0ORO(3)=L)THENPRINT"IT'S VERY SHARP!!":C=1
4024 IFO$="FLA"AND(O(4)<0ORO(4)=L)THENPRINT"IT THROWS A LIGHT!!":C=1
4026 IFO$="GUN"AND(O(5)<0ORO(5)=L)THENPRINT"IT HAS ";2-BU;"BULLETS IN IT!":C=1
4028 IFO$="WAT"AND(O(6)<0ORO(6)=L)THENPRINT"LOOKS JUST LIKE ORDINARY WATER!":C=1
4030 IFO$="FIR"AND(O(7)<0ORO(7)=L)THENPRINT"LOOKS LIKE A BLUNT STAKE!!":C=1
4032 IFO$="KNI"AND(O(9)<0ORO(9)=L)THENPRINT"IT'S VERY SHARP!":C=1
4034 IFC=0THENPRINT"I CAN'T SEE IT HERE!"
4035 GOTO400
4500 C=0
4600 IFL<8THENM$="EXPLORE, EXAMINE THINGS AND THINK!!":GOSUB1:C=1
4602 IFL=9ORL=11THENPRINT"STOCK UP WISELY!":C=1
4604 IFL=13THENPRINT"WANDER!":C=1
4606 IFL=14ORL=15ORL=16THENPRINT"TRY CLIMBING!":C=1
4608 IFL>26ANDL<35THENPRINT"PAY ATTENTION TO YOUR LOCATION!":C=1
4610 IFL=18ORL=19THENM$="BEWARE! VAMPIRES LOOK LIKE HUMANS (AND SO DO HUMANS!)":GOSUB1:C=1
4612 IFL=26THENM$="YOU CANNOT WAKE HER - SHE IS UNDER DRACULA'S SPELL!":GOSUB1:C=1
4614 IFL=22THENPRINT"TRY THE OBVIOUS!":C=1
4616 IFL=35THENPRINT"USE WHAT YOU FIND!":C=1
4650 IFC=1THEN400
4688 M$="USEFUL WORDS ARE: INVENTORY, LOOK, BUY, GET, DROP, SHOOT, PUSH, CLIMB, WEAR, GO, TAKE, STEAL, THROW, SHARPEN, HELP"
4689 GOSUB1:PRINT:GOTO400
5000 FORK=1TO3000:NEXT:RETURN
5200 PRINT"THERE'S NO REPLY!":GOTO400
5210 M$="NO WAY! THE BOSS SAYS I HAVE TO PAY FOR ANYTHING I BREAK!!":GOSUB1:GOTO400
5220 PRINT"IT'S STUCK!":GOTO400
10000 CLS:PRINT"INITIALISING...":DIML$(35),D(35,3)
10010 FORK=1TO35:READL$(K),D(K,0),D(K,1),D(K,2),D(K,3)
10011 IFK<13THENL$(K)=L$(K)+" IN A LITTLE TRANSYLVANIAN VILLAGE"
10012 NEXT:L=10
10020 DATAA NARROW STREET,1,2,,,OUTSIDE A BIBLE SHOP ON A STREET,1,3,9,
10030 DATAA NARROW STREET,2,4,,,A NARROW STREET,3,5,,,OUTSIDE A HARDWARE SHOP,4,6,11,
10040 DATAA NARROW STREET,5,7,,12,IN FRONT OF A CHURCH,6,8,,,INSIDE A CHURCH,7,,,
10050 DATAIN A BIBLE SHOP,,,,2,IN A QUIET INN,,,,3,INSIDE A HARDWARE SHOP,,,,5
10060 DATAON A RUSTY PATH,,,6,13,A MAZE OF PATHS IN A FOREST,13,13,13,13
10061 DATAIN FRONT OF THE CASTLE DOOR,,,,
10070 DATAON THE CASTLE WALL,16,16,,,ON THE CASTLE WALL,,,15,15
10080 DATAIN A RED CHAMBER,,18,,,IN A HALLWAY,17,19,,,IN A LARGE HALL,18,26,20,22
10090 DATAIN THE DINING ROOM,35,21,,19,IN THE KITCHEN,20,,,,IN THE LIBRARY,,,19,0
10100 DATAIN A BASEMENT,,24,22,,IN A CRYPT,23,,25,,IN THE VAULT,,,,24
10110 DATAIN A BEDROOM,19,,,,IN A TURNY TWISTY LITTLE,27,29,30,34
10120 DATAIN A TURNY LITTLE TWISTY,34,28,30,29
10130 DATAIN A LITTLE TURNY TWISTY,29,28,33,27
10140 DATAIN A LITTLE TWISTY TURNY,27,28,31,30
10150 DATAIN A TWISTY LITTLE TWISTY,30,34,34,17
10160 DATAIN A TWISTY LITTLE TURNY,33,32,34,32
10170 DATAIN A TURNY LITTLE TURNY,33,29,33,32
10180 DATAIN A TWISTY TURNY LITTLE,27,32,28,34
10190 DATAIN AN PECULIAR SITTING ROOM AT THE END OF A,,20,,
11000 DIMO$(9),OL$(9),O(9):FORK=1TO9:READO$(K),OL$(K),O(K):NEXT
11010 DATACRO,A SILVER CROSS,9,MAL,A WOODEN MALLET,11,STA,A WOODEN STAKE,11
11020 DATAFLA,A FLASHLIGHT,11,GUN,A SMALL HAND GUN,11,WAT,A VIAL OF HOLY WATER,8
11030 DATAFIR,FIREWOOD,35,CLO,A BLACK CLOAK,35,KNI,A SMALL KITCHEN KNIFE,21
19000 SB$="THE SHOPKEEPER":SH$=SB$:SG$="A DEAD SHOPKEEPER":H$="A HUMAN FORM"
20000 RETURN
22000 PRINT:PRINT:M$="VOICES BOOM OUT - FANGS FOR THE MEMORY!":GOSUB1:PRINT
22001 PRINT:PRINT"YOU ARE DEAD!!":PRINT"PRESS <ENTER> TO START AGAIN":INPUTA$:IFA$=""THENRUN
22002 END
22003 REM MC-10 EDITS JIM GERRIE
