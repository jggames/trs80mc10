0 DIMC1,CC,M$,MC:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC1=1TOLEN(M$):POKECC+C1,ASC(MID$(M$,C1))AND63:NEXT:?@CC-MC+C1,:RETURN
10 POKE49151,16:POKE17026,15:RETURN
100 DIMO(12),C(13),P(5,3),Q(5,4),T2$(12),M1$(17)
105 CLS
110 GOSUB1540:GOSUB1640:R=3:Q(R,0)=-1:GOSUB390:GOTO210
120 PRINTTAB(3)"PRESS ANY KEY TO CONTINUE";
130 A1$=INKEY$:IFA1$=""THEN130
131 PRINT:RETURN
140 DATA"SEE",1,"BOT",2,"WAN",3,"ROC",4,"LAN",5,"WAT",6,"GOL",7
142 DATA"SIL",9,"SAN",10,"DIA",11,"VEN",15,"CHA",16,"PLA",18
170 DATA"STE",19,"E",1,"W",2,"N",3,"S",4,"GET",5,"DRO",6,"POU",6
171 DATA"CRO",12
172 DATA"CLO",11,"FOL",12,"INV",15,"HEL",16,"THR",6
173 DATA"FIL",13,"EMP",6,"PLA",6,"WAV",8
200 DATA"WAT",14,"CLI",9,"LOO",10,"PLU",11,"FIN",12,"JUM",12
210 S=0:F=0:B1$="":C1$="":T=T+1:PRINT
220 M$="WHAT DO YOU WANT TO DO?":GOSUB7:PRINT:INPUTA1$:IFA1$=""THEN220
221 PRINT
230 FORX=1TOLEN(A1$):IFMID$(A1$,X,1)<>" "THEN250
240 B1$=LEFT$(A1$,X-1):C1$=RIGHT$(A1$,LEN(A1$)-X)
250 NEXTX:IFB1$=""THENB1$=A1$:C1$=""
260 Y1$=LEFT$(A1$,3):Z1$=LEFT$(C1$,3):RESTORE
270 FORX=1TO14:READD1$,D:IFD1$=Z1$THENS=D
280 NEXTX
290 FORX=1TO23
300 READD1$,D:IFD1$=Y1$THENF=D:GOTO330
310 NEXTX
320 PRINT"I DON'T UNDERSTAND ";A1$:M=13:GOTO210
330 IFF<>16THENM=0
332 IFF=1THENGOSUB580:GOTO210
334 IFF=2THENGOSUB650:GOTO210
336 IFF=3THENGOSUB690:GOTO210
338 IFF=4THENGOSUB740:GOTO210
340 IFF=5THENGOSUB810:GOTO210
342 IFF=6THENGOSUB880:GOTO210
344 IFF=7THENGOTO210
346 IFF=8THENGOSUB1000:GOTO210
348 IFF=9THENGOSUB1070:GOTO210
350 IFF=10THENGOSUB1150:GOTO210
352 IFF=11THENGOSUB1240:GOTO210
354 IFF=12THENGOSUB1190:GOTO210
356 IFF=13THENGOSUB1220:GOTO210
358 IFF=14THENGOSUB1230:GOTO210
360 IFF=15THENGOSUB1320:GOTO210
362 IFF=16THENGOSUB1360:GOTO210
366 GOTO210
390 IFF<5THENQ(W,F)=1
400 IFR=1THEN410
401 IFR=2THEN430
402 IFR=3THEN440
403 IFR=4THEN450
404 IFR=5THEN420
410 PRINT"THIS IS THE PIRATE'S ROOM":GOTO490
420 PRINT"YOU'RE IN THE MYSTERY ROOM":GOTO1290
430 PRINT"YOU ARE AT UNDERGROUND LAKE":GOTO490
440 PRINT"AN ESCAPE HOLE IS HIGH ABOVE YOU":GOTO490
450 IFC(3)=0THEN470
460 PRINT"THIS IS A LARGE ROOM":GOTO490
470 PRINT"A VENT IS BLOWING OUT STEAM ";
480 PRINT"AND YOU CAN'T SEE ANY OBJECTS":RETURN
490 PRINT"THERE IS:"
500 FORX=1TO12:IFO(X)<>RTHEN520
510 PRINTT2$(X):L=1
520 NEXTX:IFL=0THENPRINT"NOTHING":GOTO530
521 PRINT
530 L=0:RETURN
540 IFR=3ORO(5)=6ORO(5)=RTHENQ(R,0)=-1:GOTO390
550 PRINT"IT'S PITCH BLACK IN HERE.YOU HAD BETTER GO BACK":M=1
560 INPUT"WHAT NOW";A1$:IFRND(0)>.7THENR=W:GOTO390
570 PRINT"TRY THAT AGAIN!":GOTO560
580 IFR<>1THEN600
590 W=R:R=2:GOTO540
600 IFR<>4THEN640
610 IFC(1)=1THENW=R:R=3:GOTO540
620 W=R:PRINT"A DEEP CHASM BLOCKS THE WAY":IFQ(4,0)THENM=2
630 IFF<5THENQ(W,F)=1:RETURN
631 RETURN
640 PRINT"WRONG WAY":RETURN
650 IFR=2THENW=R:R=1:GOTO540
660 IFR<>3THEN640
670 IFC(1)=1THENW=R:R=4:GOTO540
680 GOTO620
690 IFR=5THENW=R:R=1:GOTO540
700 IFR=3THENW=R:R=2:GOTO540
710 IFR<>4THEN640
720 IFC(2)=1THENPRINT"ROCKS HAVE BLOCKED THE TUNNEL":M=3:RETURN
730 W=R:R=5:GOTO540
740 IFR<>2THEN770
750 IFO(10)=2THENW=R:R=3:GOTO540
760 PRINT"IT'S TOO SLIPERY AT THE MOMENT!":M=4:Q(2,4)=1:RETURN
770 IFR=1THENW=R:R=5:GOTO540
780 IFR<>5THEN640
790 IFC(2)=0THENW=R:R=4:GOTO540
800 GOTO720
810 IFS>11THENPRINT"NO WAY":SOUND5,2:SOUND10,1:GOSUB1765:RETURN
820 IFS=0ORO(S)<>RTHENPRINT"I SEE NO ";C1$:RETURN
830 IFS=6ANDO(2)<>6THENPRINT"YOU HAVE NO BOTTLE":RETURN
840 IFO(0)>2THENPRINT"SORRY 3 IS THE LIMIT":M=5:RETURN
850 PRINT"OK":O(S)=6:O(0)=O(0)+1
860 IFO(7)=0ANDS=6THENPRINT"I SEE GOLD!":PRINTM1$(6):O(7)=R
870 RETURN
880 IFS>11THEN810
890 IFF=6ANDY1$="THR"ORY1$="PLA"THEN910
900 IFF=6ANDY1$="EMP"THENS=6:C1$="WATER"
910 IFO(S)=6THEN930
920 PRINT"YOU HAVE NO ";C1$:RETURN
930 PRINT"OK":O(S)=R:O(0)=O(0)-1:IFS=6THENO(6)=2
940 IFS=2THENPRINT"IT BREAKS!":SOUND1,4:GOSUB1765:M=7:O(2)=0
941 IFS=2ANDO(6)=6THENO(6)=2:O(0)=O(0)-1
950 IFS<>6ANDS<>1THENRETURN
960 IFS=1THENPRINT"THEY SEEM DRY!":RETURN
970 IFO(1)<>RTHENRETURN
980 SOUND16,1:SOUND18,1:SOUND20,1:SOUND21,1:SOUND23,1:SOUND25,1:SOUND27,1:SOUND28,1:GOSUB1765
982 PRINT"A HUGE PLANT GROWS TO THE ROOF"
990 O(1)=0:O(6)=2:O(12)=R:RETURN
1000 IFS<>3THENPRINTM1$(16):RETURN
1010 IFO(3)<>6THEN920
1020 IFR=3ORR=4THEN1050
1030 IFC(2)=1THENPRINT"NO GOOD!":M=8:RETURN
1040 PRINT"THERE IS A LOUD RUMBLE!!":M=9:C(2)=1:RETURN
1050 IFC(1)=0THENPRINT"A BRIDGE APPEARS":C(1)=1:RETURN
1060 PRINT"THE BRIDGE VANISHES":C(1)=0:RETURN
1070 IFO(12)=3ANDR=3THEN1090
1080 PRINT"NOWHERE TO GO":M=10:RETURN
1090 IFO(7)=6ANDO(9)=6ANDO(11)=6THEN1120
1100 PRINT"YOU DON'T HAVE ALL THE TREASURE!":M=11
1101 IFC(3)=0ANDQ(4,0)=-1THENM=12
1110 RETURN
1120 IFO(10)<>2ANDC(1)=0THEN1155
1130 PRINT"YOU DIDN'T BLOCK THE PIRATE":M=14
1132 O(7)=1:O(9)=1:O(11)=1
1140 PRINT"THE TREASURE HAS GONE!!":O(0)=O(0)=3:RETURN
1150 GOTO390
1155 SOUND16,1:SOUND16,1:SOUND20,1:SOUND23,1:SOUND18,1:SOUND20,1:SOUND23,1:SOUND25,6:GOSUB1760:P=1135-T*3
1160 PRINTTAB(5);:M$="* * * WELL DONE! * * *":GOSUB7:PRINT:PRINT
1161 PRINTTAB(5);:M$="YOU SCORED"+STR$(P)+"/1000":GOSUB7:PRINT
1162 IFHS<PTHENHS=P:HS$="?"
1163 PRINT:PRINTTAB(5);:M$="HIGH SCORE ="+STR$(HS)+" BY "+HS$:GOSUB7:PRINT:PRINT
1164 IFHS=PTHENGOSUB3000
1165 PRINT:IFP>=1000THEN1169
1166 PRINT"WHY NOT TRY FOR A PERFECT SCORE?";:PRINT"PLEASE NOTE THAT THE USE OF ALL"
1167 PRINT"COMMANDS COUNTS AS A TURN AND IS";
1168 PRINT"DEDUCTED FROM THE SCORE."
1169 IFP>=1000THENPRINT"YOU ARE NOW READY FOR ADVANCED  GAMES."
1172 PRINT
1175 INPUT"DO YOU WANT ANOTHER GAME (Y/N)";A1$
1180 IFA1$="Y"THENRUN
1181 GOSUB10:END
1190 IFR=1ORR=2ORY1$<>"CRO"ORC(1)=0THENPRINT"NICE TRY!!":RETURN
1200 IFR=4THEN580
1210 IFR=3THEN650
1220 S=6:C1$="WATER":GOTO810
1230 S=6:GOTO880
1240 IFO(4)=6THEN1260
1250 PRINT"YOU LACK WHAT IS NEEDED":M=15:RETURN
1260 IFR<>4THEN1190
1270 PRINT"THE STEAM CLEARS"
1280 C(3)=1:O(9)=4:O(11)=4:O(0)=O(0)-1:O(4)=0:GOTO490
1290 IFO(7)=6THENPRINT"THE BATS GRAB YOUR GOLD":O(0)=O(0)=1
1291 IFO(7)=6THENO(7)=2:M=17
1300 IFRND(0)>.9THENPRINT"SOMEONE IS WATCHING YOU!"
1310 GOTO490
1320 PRINT"YOU ARE CARRYING:"
1330 FORX=1TO12:IFO(X)<>6THEN1340
1331 PRINTT2$(X):L=1
1340 NEXTX:IFL=0THENPRINT"NOTHING":GOTO1350
1341 PRINT
1350 L=0:RETURN
1360 IFT<2THENGOSUB1870:RETURN
1361 PRINTM1$(M):RETURN
1540 FORX=1TO37:READD1$,D:NEXTX
1550 FORX=1TO12:READT2$(X),O(X):NEXTX
1570 FORX=0TO17:READM1$(X):NEXTX:RETURN
1590 DATA"A SEED",2,"A BOTTLE",1,"A MAGIC WAND",1,"A ROCK",3
1591 DATA"A LIT LANTERN",2,"WATER",2,"GOLD",0,"",0,"SILVER",0
1592 DATA"SAND",5,"DIAMONDS",0,"A HUGE PLANT",0
1610 DATA"BE ADVENTUROUS!","YOU NEED SOME LIGHT"
1611 DATA"YOU WILL NEED MAGIC HERE","OH DEAR!"
1612 DATA"FIND SOMETHING TO IMPROVE YOUR GRIP"
1613 DATA"YOU'LL HAVE TO DROP SOMETHING"
1615 DATA"BEWARE THE BATS!"
1620 DATA"I HOPE YOU DON'T NEED THAT AGAIN ","YOU WONT GET THRU"
1621 DATA"YOU'LL FIND OUT WHAT HAPPENED","WRONG ROOM"
1622 DATA"KEEP LOOKING"
1623 DATA"YOU'LL HAVE TO DO SOMETHING ABOUT THAT STEAM"
1630 DATA"TRY OTHER WORDS"
1631 DATA"WHATEVER BLOCKED YOU WILL BLOCK THE PIRATE"
1632 DATA"ALL OBJECTS HAVE A USE ","WAVE SOMETHING ELSE"
1635 DATA"DON'T BRING THE GOLD THIS WAY AGAIN"
1640 GOSUB10:CLS:M$="INTRODUCTION TO ADVENTURE GAMES ":GOSUB7
1650 PRINTTAB(5);:M$="WRITTEN FOR THE MC-10":GOSUB7:PRINT
1655 PRINT
1665 PRINT
1670 PRINT" ******************************"
1680 PRINT" ADVENTURE GAMES TUTORIAL   (1)"
1690 PRINT" INSTRUCT. FOR CHASM CAPERS (2)"
1700 PRINT" CHASM CAPERS               (3)"
1710 PRINT" ******************************"
1715 PRINT
1720 PRINTTAB(3);:M$="PLEASE MAKE YOUR SELECTION":GOSUB7:PRINT
1725 FORI=1TO10:NEXT
1730 GOSUB130
1740 A=INT(VAL(A1$)):IFA<1ORA>3THEN1730
1750 IFA=1THEN2000
1751 IFA=2THEN1770
1752 IFA=3THEN2640
1760 CLS
1765 POKE49151,64:POKE17026,223:RETURN
1770 CLS:M$="*INSTRUCTIONS FOR CHASM CAPERS* ":GOSUB7
1780 PRINT"   AT THE BEGINNING OF THE GAME"
1785 PRINT"YOU HAVE BEEN THROWN BY FORCES"
1790 PRINT"BEYOND YOUR CONTROL INTO A DARK"
1795 PRINT"UNDERGROUND CAVERN WITH TUNNELS"
1800 PRINT"LEADING TO OTHER ROOMS (5 TOTAL)";
1805 PRINT"THESE ROOMS CONTAIN ODD PIECES"
1810 PRINT"OF EQUIPMENT WHICH WILL CERTAIN-";
1815 PRINT"LY COME IN VERY HANDY TO YOU.THE";
1820 PRINT"OBJECT OF THE GAME IS TO ESCAPE"
1825 PRINT"FROM THE CAVES USING THE TOOLS"
1830 PRINT"YOU DISCOVER,CARRYING THE PREC-"
1835 PRINT"IOUS TREASURES HIDDEN THERE."
1838 PRINT:
1840 GOSUB120
1850 CLS:PRINT"THE FEWER MOVES IT TAKES YOU TO"
1855 PRINT"ACCOMPLISH THIS,THE CLOSER YOU"
1860 PRINT"GET TO A PERFECT SCORE."
1865 PRINT"   USE THE COMMANDS e,w,n,s, AS"
1870 PRINT"DIRECTIONS FOR MOVING FROM CAVE"
1875 PRINT"TO CAVE, OTHERWISE USE 2-WORD"
1880 PRINT"COMMANDS (A verb AND noun) TO"
1885 PRINT"ACCOMPLISH YOUR TASKS."
1890 PRINT"   TRY DIFFERENT COMMANDS AS"
1895 PRINT"THOUGH YOU WERE REALLY IN THAT"
1900 PRINT"SITUATION. YOU'LL SOON GET THE"
1905 PRINT"HANG OF IT."
1910 PRINT"   IF YOU TYPE look YOU WILL BE"
1915 PRINT"SHOWN AGAIN THE ROOM IN WHICH"
1920 PRINT"YOU RESIDE."
1925 GOSUB120
1930 CLS:PRINT"   IF YOU TYPE inventory YOU"
1935 PRINT"WILL BE SHOWN A LIST OF WHAT"
1940 PRINT"YOU ARE CARRYING."
1945 PRINT"    IF YOU TYPE help YOU MAY"
1950 PRINT"RECEIVE A USEFUL HINT."
1953 PRINT
1955 PRINTTAB(9);:M$="!!!GOOD LUCK!!!":GOSUB7:PRINT
1958 PRINT
1960 GOSUB120:GOTO1640
1990 GOSUB120:RETURN
2000 CLS:M$=" ***ADVENTURE GAMES TUTORIAL*** ":GOSUB7
2010 PRINT"   AN ADVENTURE IS A ROLE PLAY-"
2015 PRINT"ING GAME INVOLVING A COMPUTER"
2020 PRINT"SIMULATION OF AN IMAGINARY WORLD";
2025 PRINT"INTO WHICH YOU ENTER AND PERFORM";
2030 PRINT"VARIOUS TASKS AS THOUGH YOU WERE";
2035 PRINT"REALLY IN THAT SITUATION. YOU"
2040 PRINT"TAKE ACTION BY GIVING THE COMP-"
2045 PRINT"UTER A SERIES OF ONE OR TWO WORD";
2050 PRINT"COMMANDS CONSISTING OF A VERB OR";
2055 PRINT"A NOUN, SUCH AS, ";:M$="GO WEST":GOSUB7:PRINT" OR"
2060 M$="GET KEYS":GOSUB7:PRINT", ETC. YOU ARE INFORMED"
2065 PRINT"OF THE RESULTS OF ACTIONS BY"
2070 PRINT"MESSAGES OR PICTURES ON THE"
2075 PRINT"SCREEN."
2080 GOSUB120
2085 CLS:PRINT"   THE GOAL OF MOST GAMES IS FOR";
2090 PRINT"YOU TO FIND AND RESCUE SOMEONE"
2095 PRINT"OR TO DISCOVER AND TO ESCAPE WI-";
2100 PRINT"TH ITEMS OF VALUE.GENERALLY YOUR";
2105 PRINT"QUEST IS FILLED WITH PERIL BUT"
2110 PRINT"YOU CAN BE ASSISTED BY OBJECTS"
2115 PRINT"THAT YOU'LL FIND ALONG THE WAY."
2120 PRINT"USUALLY A LIMIT IS PLACED ON"
2125 PRINT"THE NUMBER OF ITEMS THAT YOU"
2130 PRINT"CAN CARRY AND HOW LONG SUCH"
2135 PRINT"THINGS AS TORCHES WILL REMAIN"
2140 PRINT"USEFUL."
2142 PRINT
2145 GOSUB120
2150 CLS:PRINT"   IF ANY OF THE PERILS CAUSE"
2155 PRINT"YOUR 'DEATH' THIS MAY BE THE END";
2160 PRINT"OF THE GAME, BUT SOMETIMES YOU"
2165 PRINT"ARE GIVEN A SECOND CHANCE."
2170 PRINT"   THE VOCABULARY OF THE GAME"
2175 PRINT"IS LIMITED TO 20 TO 100 WORDS"
2180 PRINT"DEPENDING ON THE SIZE OF THE"
2185 PRINT"PROGRAM. A COMPLETE LIST OF THE"
2190 PRINT"WORDS RECOGNIZED BY THE PROGRAM"
2195 PRINT"IS SELDOM GIVEN, AS MUCH OF THE"
2200 PRINT"FUN OF PLAYING THE GAME IS IN"
2205 PRINT"LEARNING THE LANGUAGE OF THE"
2210 PRINT"GAME. IF THE PROGRAM DOESN'T"
2215 PRINT"UNDERSTAND YOUR COMMAND, TRY"
2220 PRINT"SOMETHING ELSE."
2225 GOSUB120
2230 CLS:PRINT"   SOME TYPICAL SINGLE WORD"
2235 PRINT"COMMMANDS ARE:-"
2240 PRINT"   inventory-PROVIDES A LIST OF"
2245 PRINT"THE ITEMS THAT YOU ARE CARRYING.";
2250 PRINT"   look     -MAY REVEAL SIGNIF-"
2255 PRINT"ICANT DETAIL THAT MAY ASSIST"
2260 PRINT"YOU."
2270 PRINT"   help     -MAY RESULT IN A"
2275 PRINT"USEFUL HINT."
2278 PRINT
2280 GOSUB120
2340 CLS:PRINTTAB(7);:M$="SOME USEFUL HINTS":GOSUB7:PRINT
2350 PRINT"   MOST GAMES ALLOW THE USE OF"
2360 PRINT"SINGLE-LETTER COMMANDS FOR MOVE"
2370 PRINT"MENT SUCH AS N,S,E,W,U,D FOR"
2380 PRINT"NORTH,SOUTH,EAST,WEST,UP, AND"
2390 PRINT"DOWN."
2400 PRINT"   IN ADDITION TO LEARNING THE"
2410 PRINT"WORDS THE PROGRAM RECOGNIZES,"
2420 PRINT"YOU WILL ALSO NEED TO LEARN YOUR";
2430 PRINT"WAY AROUND THE SCENARIO, SO BE"
2440 PRINT"PREPARED TO DRAW A MAP. IF YOU"
2450 PRINT"MOVE IN A CERTAIN DIRECTION AL-"
2460 PRINT"WAYS BE SURE YOU CAN GET BACK TO";
2470 PRINT"WHERE YOU STARTED, PREPARE YOUR"
2480 PRINT"ESCAPE ROUTE."
2485 GOSUB120
2490 CLS:PRINT"   REMEMBER TO TYPE inventory"
2500 PRINT"EARLY IN THE GAME, YOU MAY BE "
2510 PRINT"CARRYING SOMETHING USEFUL. USE"
2520 PRINT"look TO CHECK YOUR SURROUNDINGS"
2530 PRINT"FOR DETAIL OR TO EXAMINE AN OB-"
2540 PRINT"JECT (I.E. ";:M$="LOOK DOOR":GOSUB7:PRINT") AND USE"
2550 PRINT"help IF YOU GET STUCK."
2560 PRINT"   FINALLY, IF YOU GET STUCK"
2570 PRINT"DO NOT BE AFRAID TO QUIT THE"
2580 PRINT"GAME AND START AGAIN IF YOU GET"
2590 PRINT"CONFUSED."
2600 PRINT
2610 PRINTTAB(8);:M$="!!!GOOD LUCK!!!":GOSUB7:PRINT
2620 PRINT
2630 GOSUB120:GOTO1640
2640 CLS6
2650 M$="                                ":GOSUB7
2655 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ";
2660 PRINT"ﬂccccﬂﬂhﬂﬂhﬂﬂaaaaﬂﬂssssﬂﬂmmmmmﬂﬂ";
2670 PRINT"ﬂcﬂﬂﬂﬂﬂhﬂﬂhﬂﬂaﬂﬂaﬂﬂsﬂﬂﬂﬂﬂmﬂmﬂmﬂﬂ";
2680 PRINT"ﬂcﬂﬂﬂﬂﬂhhhhﬂﬂaaaaﬂﬂssssﬂﬂmﬂmﬂmﬂﬂ";
2690 PRINT"ﬂcﬂﬂﬂﬂﬂhﬂﬂhﬂﬂaﬂﬂaﬂﬂﬂﬂﬂsﬂﬂmﬂmﬂmﬂﬂ";
2700 PRINT"ﬂccccﬂﬂhﬂﬂhﬂﬂaﬂﬂaﬂﬂssssﬂﬂmﬂmﬂmﬂﬂ";
2710 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ";
2720 PRINT"ﬂﬂcccﬂﬂaaaﬂﬂpppﬂﬂeeeﬂﬂrrrﬂﬂsssﬂﬂ";
2730 PRINT"ﬂﬂcﬂﬂﬂﬂaﬂaﬂﬂpﬂpﬂﬂeﬂﬂﬂﬂrﬂrﬂﬂsﬂﬂﬂﬂ";
2740 PRINT"ﬂﬂcﬂﬂﬂﬂaaaﬂﬂpppﬂﬂeeﬂﬂﬂrrﬂﬂﬂsssﬂﬂ";
2750 PRINT"ﬂﬂcﬂﬂﬂﬂaﬂaﬂﬂpﬂﬂﬂﬂeﬂﬂﬂﬂrﬂrﬂﬂﬂﬂsﬂﬂ";
2760 PRINT"ﬂﬂcccﬂﬂaﬂaﬂﬂpﬂﬂﬂﬂeeeﬂﬂrﬂrﬂﬂsssﬂﬂ";
2770 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ";
2780 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂNRC.ﬂﬂﬂ";
2790 M$="                               ":GOSUB7:POKEMC+511,32
2810 FORI=1TO3500:NEXT
2820 GOTO1760
3000 M$="PLEASE ENTER YOUR INITIALS":GOSUB7:INPUTHS$:RETURN
