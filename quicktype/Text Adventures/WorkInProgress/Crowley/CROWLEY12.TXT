0 CLS:PRINT"  THE CURSE OF CROWLEY MANOR":CLEAR512:DIMZZ,CC,M$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORCC=1TOLEN(M$)STEP2:ZZ=VAL(MID$(M$,CC,2)):IFL(ZZ)<0THENL(ZZ)=R
4 NEXT
5 WD=1:GOTO8200
6 L(CC)=R:RETURN
7 GOSUB10:WD=1:GOTO9
8 GOSUB1:GOTO200
9 GOSUB1:GOTO8200
10 FORCC=1TO10:SOUND250,1:CLSRND(8):NEXT:CLS:RETURN
11 GOSUB10:GOSUB1:GOTO3900
80 M$="OKAY.":GOTO9
81 PRINT"THAT'S SILLY!":GOTO200
82 M$="NOTHING HAPPENS.":GOTO9
84 PRINT"PLEASE GIVE AN OBJECT.":GOTO200

100 NR=43:NT=47:NP=6:NV=31:NO=NT+NP
110 DIMD(NT,6),V$(NV),O$(NO),R$(NR),I$(NT),V(NV),L(NT)
120 R=1:WD=1:C=2
130 FORI=1TONR:READR$(I):NEXT:FORI=1TONV:READV$(I),V(I):NEXT:FORI=1TONO:READO$(I):NEXT
140 FORI=1TONT:READI$(I),L(I),D(I,.):NEXT:FORI=1TONR:READD(I,1),D(I,2),D(I,3),D(I,4),D(I,5),D(I,6):NEXT:CLS
200 IFWD=0THEN400
210 WD=0:L(1)=R:PRINT:M$=R$(R)+".":IFLEFT$(M$,1)="*"THENM$="IN A"+MID$(M$,2)
220 M$="YOU ARE "+M$:GOSUB1:K=0:PRINT:M$="visible items: ":FORI=2TONT:IFL(I)=RANDL<200THENK=1:M$=M$+I$(I)+", ":L=LEN(M$)
270 NEXT:IFKTHENM$=LEFT$(M$,LEN(M$)-2)+".":IFL>=200THENM$=M$+".."
280 GOSUB1:K=0:PRINT:M$="exits: ":FORI=1TO6:IFD(R,I)<>0THENK=1:M$=M$+O$(NT+I)+", "
330 NEXT:IFKTHENM$=LEFT$(M$,LEN(M$)-2)+"."
340 GOSUB1
400 INPUTC$:L=LEN(C$):IFL=0THENPRINT"HUH?":GOTO400
410 IFC$="SEARCH"THENC$="LOOK ROOM":L=9
420 V$="":O$="":O=0:X=0
430 FORI=1TOL:A$=MID$(C$,I,1):A=ASC(A$):IFA>=97ANDA<=122THENA$=CHR$(A-32)
450 IFA$<>" "THEN460
455 IFO$<>""THENCC=I:I=L:NEXT:I=CC:GOTO500
457 X=1:GOTO490
460 IFX=0THENV$=V$+A$:GOTO490
470 O$=O$+A$
490 NEXT
500 VB$=V$:V$=LEFT$(V$,5):NN$=O$:O$=LEFT$(O$,5)
520 FORV=1TONV:IFV$(V)=V$THENCC=V:V=NV:NEXT:V=CC:V=V(V):GOTO600
540 NEXT:M$="I DON'T KNOW THE VERB "+VB$:GOTO9
600 IFO$=""THEN700
610 FORO=1TONO:IFO$(O)=O$THENCC=O:O=NO:NEXT:O=CC:GOTO700
630 NEXT
640 M$="I DON'T KNOW THE OBJECT "+NN$+".":GOTO9
700 IFO>NOANDV<>7ANDV<>21THEN640
710 IFO<NOANDO>NTTHENIFV<>7ANDV<>21THEN81
720 IFO>NTTHEN750
730 IFL(O)<>RANDL(O)<>0ANDV<>15THENM$="IT ISN'T HERE.":GOTO9
750 ONVGOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400,1500,1800,1900,2000,1700,2200,2300,2400,2500,1600
770 ONV-20GOTO2600,2700,2800,2900,3000,3900
1000 IFD(R,V)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO200
1020 R=D(R,V):WD=1:GOTO8200
1100 IFO=0THEN84
1101 IFR=3ANDL(33)=3ANDO=33THENR=4:L(33)=R:WD=1:M$="THE DRIVER ASKS, 'WHERE TO, GOVENOR?'":GOTO9
1105 IFO<=NTTHEN81
1110 V=O-NT:GOTO1000
1200 IFO=0THEN84
1201 IFO=2ORO=22THENM$="YUM!":L(O)=-1:C=C-1:GOTO9
1298 M$="YECHH!":GOTO9
1300 IFO=0THEN84
1301 IFL(8)=0ANDO=41THEND(R,1)=20:GOTO80
1302 IFL(8)=0ANDO=28THEND(R,5)=39:B1=1:GOTO80
1398 GOTO82
1400 IFR=6THENM$="'HELLO, MR BLACK. THE BODY IS IN THE KITCHEN. INSPECTOR STRADE IS INSIDE.'":GOTO9
1401 IFR=13ANDDA=0THENM$="'MY NAME IS DAVONN. NO MAN MURDERED MY MASTER. NOW IT IS LOOSE!'":DA=1:GOTO9
1402 IFR=27ANDG1=0THENM$="'I AM THE BROTHER OF THE EVIL YOU SEEK. YOU MUST PREPARE TO KILL HIM.'":G1=1:GOTO9
1403 IFR=27ANDG1=1THENM$="'TWO PATHS LEAD FROM HERE. YOU'LL NEED THE CRYSTAL. IT WILL SHOW YOU'":G1=2:D(R,1)=29:D(R,2)=28:GOTO9
1404 IFR=32ANDG2=0THENM$="HE SAYS: 'ONLY ONE COMPOSER DO I KNOW. NAME HIM AND I'LL PLAY FOR YOU.'":G2=1:GOTO9
1405 IFR=39ANDG3=0THENM$="HE SAYS: 'YOU HAVE BUT ONE MOMENT TO STRIKE. YOUR POWER WILL SAVE YOU TILL THEN.'":G3=1:GOTO9
1498 GOTO80
1500 IFO=0THEN84
1501 IFO=15ANDK1=0THENM$="SOMETHING WAS LODGED IN THE CRANK.":L(11)=R:K1=1:GOTO9
1502 IFO=15ANDK1>0THENM$="THE VICTROLA BEGINS PLAYING.":K1=2:GOTO9
1503 IFO=16ANDL(32)=0THENM$="THE SCREWS COME OUT.":GOSUB1:M$="1723":GOTO3
1598 GOTO81
1600 IFR=11ANDK1>1THENM$="THE MUSIC IS MOZART.":GOTO9
1601 IFO=41THENM$="THERE IS A NOISE FROM BEHIND THE WALL.":GOTO9
1602 IFR=33ANDN1=0THENM$="THERE'S A NOISE ABOVE YOUR HEAD.":N1=1:GOTO9
1603 IFR=36ANDN2=0THENM$="A VOICE IN THE DARK WHISPERS: 'HE HAS THE SWORD, BUT IT IS NOT YET CLEANSED.'":N2=1:GOTO7
1698 M$="YOU HEAR NOTHING INTELLIGIBLE.":GOTO9
1700 IFO=0THEN84
1701 IFR=4ANDO=35THENM$="'RIGHT AWAY SIR!'":CR=1:GOTO9
1702 IFR=26ANDO=39THENM$="THE WEST WALL CRUMBLES EXPOSING A STAIRWAY. A VOICE BOOMS, 'I AM NEAR!'":D(26,5)=27:GOTO7
1703 IFR=32ANDO=7THENM$="THE SPIRIT PLAYS BEAUTIFULLY. A STAIRWAY APPEARS ON THE NORTH WALL.":D(32,5)=33:GOTO7
1798 GOTO80
1800 IFO=0THEN84
1801 IFO=6ANDPH=1THEN8525
1802 IFC>5THENM$="YOU CAN ONLY CARRY 6 ITEMS.":GOTO9
1803 IFO=19ANDGR>1THENM$="THE GROWTH TOWERS OVER YOU... AND THEN DEVOURS YOU!":GOTO11
1804 IFO=44ORO=46THENM$="NO, THANK YOU!":GOTO9
1805 IFD(O,.)=.THENM$="THAT'S BEYOND YOUR ABILITY.":GOTO9
1810 IFL(O)=0THENM$="YOU ALREADY HAVE IT!":GOTO9
1830 L(O)=0:C=C+1:GOTO80
1900 IFO=0THEN84
1901 IFV$="THROW"THENM$="IT BOUNCES BACK AND HITS YOUR HEAD.":GOSUB1:L(O)=R:C=C-1:GOTO2300
1902 IFO=19ANDL(2)=RANDR=16THEN1999
1903 IFR=17ANDO=19THENM$="THE GROWTH SHOOTS AWAY FROM YOU AND BEGINS DEVOURING THE FOOD.":GR=9
1904 IFD(O,0)ANDL(O)=0THENL(O)=R:C=C-1:GOTO80
1998 GOTO81
1999 M$="THE GROWTH SLIDES OVER TO THE PLATE, DEVOURS THE FOOD AND GROWS.":GR=1:L(2)=-1:L(O)=R:C=C-1:GOTO9
2000 C=0:I$="you are carrying: ":FORI=1TONT:IFL(I)=0THENI$=I$+I$(I)+", ":C=C+1
2030 NEXT:IFC=0THENI$=I$+"NOTHING. "
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOTO9
2200 IFO=0THEN84
2201 IFO=13THENM$="IT READS: 'I AM DOOMED TODAY. THE DEMON IS TRAPPED IN THE HOUSE. HE'LL BE HERE LONG AFTER MY DEATH'":GOTO9
2298 GOTO2890
2300 IFR=30THENM$="THE DEMON DEVOURS YOU.":GOTO11
2301 IFR=37ANDR1=0THENM$="THE RAT DODGES YOUR BLOW AND SNEERS.":GOTO9
2302 IFR=43ANDS1=9THEN2360
2303 IFR<42THENM$="THAT WON'T WORK.":GOTO9
2304 M$="THE DEMON SHOUTS, 'YOU STRIKE TOO SOON, MAGGOT. YOU BROKE YOUR MAGIC. AND ALLOW ME THIS TREAT.'":GOTO11
2360 GOSUB10:CLS3:PRINT@224,"  YOU HAVE DEFEATED THE DEMON!",TAB(5)"THANK YOU FOR PLAYING.":FORZZ=1TO15
2361 FORCC=1TO99:PRINT@169,"congratulations";:NEXT:FORCC=1TO99:PRINT@169,"CONGRATULATIONS";:NEXT:NEXT:PRINT@288,;:GOTO3900
2400 IFO=0THEN84
2401 IFO=10ANDL(11)=0ANDL1=0THENM$="INSPECTOR STRADE'S BODY FALLS OUT ONTO THE FLOOR.":L(46)=R:L1=1:GOTO7
2402 IFO=38OR(O=21ANDR=21)THENINPUT"WHAT'S THE COMBINATION";M$:ON-(M$<>"5271")GOTO82:M$="CLICK!":D(R,1)=22:GOTO9
2498 GOTO81
2500 IFO=0THEN84
2501 IFO=14THENL(12)=R:GOTO80
2503 IFO=21ANDR=17THEND(R,3)=18:GOTO80
2598 M$="YOU CAN'T DO THAT NOW.":GOTO9
2600 IFO=0THEN2680
2601 IFO<>1THEN2650
2602 ON-(R>19)GOTO2611:IFR=1THENM$="030418":GOTO3
2603 IFR=11THENM$="31":GOTO3
2604 IFR=14THENM$="13":GOTO3
2605 IFR=8THENM$="37":GOTO3
2606 IFR=15THENM$="19":GOTO3
2607 IFR=16THENM$="02":GOTO3
2609 IFR=12THENM$="16":GOTO3
2610 IFR=19THENM$="41":GOTO3
2611 IFR=34THENM$="25":GOTO3
2612 IFR=20ANDL(9)<0THENM$="A TREMENDOUS VOICE BOOMS: 'YOU HAVE NOT THE POWER TO FACE ME YET. BE WARNED!'":L(9)=R:GOTO7
2618 IFR=35THENM$="THERE'S A CIRCULAR DEPRESSION IN THE FLOOR.":GOTO9
2620 IFR=38ANDB1=0THENM$="A NORTH STAIRWAY IS BLOCKED BY HUGE WOODEN BEAMS.":GOSUB1:M$="28":GOTO3
2621 IFR=39THENL(39)=R:D(R,3)=40:GOTO5
2650 IF(R=4ORR=5)ANDO=33THENM$="22":GOTO3
2651 IFO=30THENM$="YOU SEE A VIEW OF LONDON AT NIGHT.":GOTO9
2652 IFR=33ANDO=NT+5THENM$="A ROPE HANGS FROM THE CEILING.":D(R,5)=34:L(24)=R:GOTO9
2653 IFO>NTTHEN2898
2680 IFR=30ANDD1=1THENR=13:GOSUB8311:GOTO7
2681 IFR=30ANDL(40)<>0THENM$="IT IS HERE: 'WHERE IS YOUR WEAPON?' HE SCREAMS, 'YOU ARE SUCH A FOOL!'":D1=1:L(34)=R:GOTO7
2682 IFR=37ANDR1=0THEN8312
2683 IFR=9ANDFO=0THENM$="A TREMENDOUS FORCE THRUSTS YOU AGAINST A WALL. THERE IS A HIDEOUS SMELL.":FO=1:GOTO7
2684 IFR=22ANDW1=0THENM$="A POWERFUL FORCE SLAMS YOU AGAINST A WALL. A HORRIBLE VOICE SHRIEKS!":W1=1:GOTO7
2685 IFR=22ANDW1=1THENM$="YOU ARE THROWN FLAT ON THE FLOOR. A VOICE BELLOWS: 'SOON YOU'LL BE MINE!'":W1=2:GOTO7
2686 IFR=22ANDW1=2THENM$="THE ROOM COOLS OFF AND FILLS WITH SMOKE. YOUR CRUCIFIX IS RED HOT.":W1=3:L(43)=R:GOTO7
2698 GOTO5
2700 IFO=0THEN84
2701 IFR=40ANDL(40)=0THENM$="THE SWORD GLEAMS BRIGHTLY. YOU CAN FEEL POWER SURGING WITHIN IT.":S1=1:GOTO7
2798 GOTO80
2800 S=0:IFO=0THEN84
2801 IFO=36THENM$="THEY HAVE FIENDISH EYES. THEY DON'T LOOK HUMAN.":S=1
2802 IFO=31THENM$="IT HAS A CRANK.":L(15)=11:S=1
2803 IFO=15THENM$="THE CRANK IS STUCK.":S=1
2804 IFO=16THENM$="IT'S FASTENED WITH TWO SCREWS.":S=1
2805 IFO=10ANDL1=0ANDR=7THENM$="IT'S LOCKED.":S=1
2806 IFO=18THENM$="IT'S ONLY MONEY.":S=1
2807 IFO=38THENM$="IT HAS 4 DIALS ON IT.":S=1
2808 IFO=43THENM$="IT FORMS FRIGHTENING SHAPES.":S=1
2809 IFO=44THENM$="THEY LIE DECAYING ON THE FLOOR. ONE APPEARS TO HAVE HELD THE CLUB.":S=1
2812 IFO=39OR(O=45ANDR=27)THENM$="A GLOW SURROUNDS THE FIGURE.":S=1
2813 IFO=13THENM$="IT'S THE DIARY OF ADAM CROWLEY. THE DATE IS 1725.":S=1
2814 IFO=45ANDR=13THENM$="IT'S TOO DARK TO SEE MUCH. THE FIGURE IS ON THE FLOOR.":S=1
2815 IFO=45ANDR=32THENM$="THE FIGURE RESEMBLES THE PORTRAITS BACK IN THE MUSIC ROOM.":S=1
2816 IFO=46THENM$="HIS BODY IS TORN TO PIECES.":S=1
2817 IFO=19THENM$="IT'S INDESCRIBABLE.":S=1
2818 IFO=29THENM$="IT'S A COLT.":S=1
2819 IFO=34THENM$="HIS EYES GLOW LIKE FIRE.":S=1
2820 IFO=47THENM$="BOY! ARE YOU GRISLEY!":S=1
2821 IFO=20THENM$="IT'S 17TH CENTURY.":S=1
2822 IFO=27ANDR1=0THEN8312
2823 IFO=12THENM$="IT GLOWS FAINTLY.":S=1
2824 IFO=12ANDR=35ANDV1=1THENM$="A MAGIC SWORD APPEARS FROM A PUFF OF SMOKE.":L(40)=R:V1=2:S=1
2825 IFO=12ANDR=35ANDV1=0THENM$="A VISION FORMS OF A SWORD AND A BEAUTIFUL FOUNTAIN OF GREEN LIQUID.":V1=1:S=1
2826 IFO=22THENM$="IT SPARKLES.":S=1
2827 IFO=23THENM$="IT'S SILVER.":S=1
2828 IFO=25THENM$="IT HAS ANCIENT RUNES ON IT.":S=1
2829 IFO=40ANDS1THENM$="IT GLEAMS.":S=1
2830 IFSTHEN9
2890 IFO=3ORO=5THENM$="'INSPECTOR BLACK. SCOTLAND YARD'":GOTO9
2891 IFO=4THENM$="'APRIL 2, 1913'":GOTO9
2892 IFO=17THENM$="IT READS 5271.":GOTO9
2893 IFO=9THENM$="IT READS: 'GAFALA ALONE CAN HELP'":GOTO9
2898 M$="YOU SEE NOTHING SPECIAL.":GOTO9
2900 GOTO80
3000 IFO=0THEN84
3001 SH=SH+1
3002 IFR=37ANDL(29)=0ANDR1=0ANDSH<4THENM$="THE RAT SQUEALS, SPINS, AND DROPS DEAD ON THE FLOOR.":R1=1:I$(27)="DEAD RAT":GOTO9
3003 IFSH>3THENM$="IT'S JAMMED.":GOTO9
3090 IFL(29)<>0ORO=39ORR=13ORR=27THEN81
3098 PRINT"BANG!":GOTO8200
3900 PRINT:PRINT"HIT y TO TRY AGAIN."
3910 M$=INKEY$:ON-(M$="N")GOTO3920:ON-(M$<>"Y")GOTO3910:RUN
3920 END

8200 IFR=1AND((PH=0ANDRND(3)=1)ORPH=1)THENM$="A TELEPHONE ON THE DESK RINGS.":PH=1:GOTO8
8201 IFR=4ANDCR>0THENCR=CR+1:ONCRGOTO,8528,8529,8530
8202 IFL(34)<42THENDD=DD+1:IFDD=3THENDD=0:L(34)=42:M$="THE PRESENCE PASSES. THE DEMON HAS MOVED ON.":GOSUB1
8203 CC=21:ON-(R=6ORR=17ORR=19ORR=21)GOSUB6:CC=45:ON-(R=13ORR=27ORR=32)GOSUB6:CC=10:ON-(R=16ORR=7)GOSUB6
8204 IFR=11ORR=32THENL(42)=R
8205 IFR=13ANDDA=1ANDV=4THENGOSUB10:M$="DAVONN'S THROAT HAS BEEN RIPPED OUT.":DA=2:GOTO8
8206 IFR=16ANDL(19)=RANDGR>0THENGR=GR+1:IFGR>2THEN8300
8208 IFR=21AND(L(22)<>0ORL(23)<>0)THENM$="A MONSTROUS ICY CREATURE IS HERE! IT TEARS YOU TO PIECES.":GOTO11
8209 IFR=38ANDR1=0THENM$="THE RAT LUNGES AT YOU AND RIPS YOUR THROAT OUT.":GOTO11
8210 IFR=39AND(L(22)<>0ORL(23)<>0ORL(25)<>0ORL(40)<>0)THENGOSUB10:GOSUB8310:R=13:WD=1:GOTO8
8250 IFR=42ANDS1=0THENM$="THE DEMON HOWLS: 'YOUR SWORD HAS NO POWER YET YOU FOOL. DIE FOR ME'":GOTO11
8251 IFR<>42THEN8260
8252 S1=S1+1
8253 IFS1=3THENGOSUB10:M$="THE DEMON HURLS YOU THROUGH THE AIR. YOUR BODY STRIKES A WALL. IT SCREAMS A LAUGH.":R=43:WD=1:GOTO8
8260 IFR<>43THEN200
8261 L(34)=R:S1=S1+1
8262 IFS1=4THENM$="YOUR FACE IS HOT FROM ITS BREATH. IT HOWLS, 'THANK YOU FOR DROPPING IN. FOOL!'"
8263 IFS1=5THENM$="IT SLAMS YOU TO THE GROUND AND SCREAMS: 'YOU'VE BEEN DUPED. YOU CAN'T KILL ME.'"
8264 IFS1=6THENM$="YOUR BODY SHAKES WITH CONVULSIONS AS THE DEMON TRIES TO CRUSH YOU."
8265 IFS1=7THENM$="THE DEMON TOUCHES THE SWORD AND IT GLOWS WHITE HOT. THE EVIL SCREAMS WITH PAIN."
8266 IFS1=8THENM$="IT BELCHES A SHEET OF FLAME WHICH ENGULFS YOUR BODY."
8267 IFS1=9THENM$="THE DEMON STARES IN TO YOUR FACE. 'JOIN ME. SPAWN OF DIRT,' AND SHARE IN MY POWER.'"
8268 IFS1=10THENM$="THE DEMON ROARS, 'NO HOPE NOW, FOOL, YOU HAVE MISSED YOUR MOMENT. FARE WELL TO YOU.'":GOTO11
8269 GOSUB10:GOTO8

8300 GOSUB10:M$="THE GROWTH SHOOTS UNDER THE CHINA CABINET AND TIPS IT-- CRASH!":GOSUB1:GR=2:M$="0832":L(19)=17:GOTO3
8310 M$="YOU ARE SMASHED TO THE FLOOR LIKE AN ANT. IT IS HERE. IT SCREAMS, 'WHERE ARE YOUR SACRAMENTS?'":RETURN
8311 M$="YOU MANAGE TO ESCAPE YOUR ENCOUNTER WITH THE DEMON AND SOMEHOW END UP HERE UNHARMED.":RETURN
8312 M$="THE RAT LEAPS AND BITES A CHUNK OFF FLESH OFF YOUR ARM.":S=1:GOTO7

8525 M$="A VOICE SAYS, 'THIS IS OFFICER STRADE. COME TO CROWLEY ESTATE. THERE'S BEEN A MURDER.'":L(33)=3:PH=2:GOTO8
8528 M$="THE CAB IS PASSING THROUGH TRAFALGAR SQUARE.":GOTO8
8529 M$="YOU CAN SEE BIG BEN'S TOWER TO THE SOUTH.":GOTO8
8530 M$="THE CAB PULLS INTO A TREE LINED DRIVEWAY.":GOSUB1:R=5:WD=1:L(33)=R:IFL(18)<>0THEN8534
8531 IFL(22)<>0THENL(22)=R 
8532 M$="'HERE WE ARE, SIR.' THE CABBIE TAKES 10 SHILLINGS AND THANKS YOU.":I$(18)="40 SHILLINGS":GOTO8
8534 M$="'SORRY GOVENOR', HE SAYS, 'IF YOU CAN'T PAY, I CAN'T LET YOU OUT.'":GOSUB1:GOTO3900

9001 DATA"* LARGE OFFICE. A LARGE WINDOW IS HERE AND SOME ROSEWOOD FURNITURE"
9002 DATA"ON A LANDING. A STAIRWAY LEADS SOUTH TO THE STREET"
9003 DATA"ON THE BRICK STREET IN FRONT OF SCOTLAND YARD"
9004 DATA"* CAB TRAVELING THROUGH THE STREETS OF LONDON. IT'S A BEAUTIFUL NIGHT"
9005 DATA"* CAB. IT IS PARKED IN FRONT OF A LARGE MANOR"
9006 DATA"STANDING BY THE FRONT PORCH OF CROWLEY ESTATE. POLICE INSPECTOR HARBOUR IS ON THE PORCH"
9007 DATA"* PLUSH ENTRY HALL. THE FURNISHINGS ARE OPULENT"
9008 DATA"AT THE SOUTH END OF A LONG NORTH/SOUTH HALL"
9009 DATA"IN THE CENTRE OF A LONG NORTH/SOUTH HALL"
9010 DATA"AT THE NORTH END OF A LONG NORTH/SOUTH HALL"
9011 DATA"*N ELEGANT MUSIC ROOM"
9012 DATA"* PARLOR. IT HAS VICTORIAN FURNITURE AND PLUSH PERSIAN CARPETS"
9013 DATA"* DIMLY LIT ROOM. A SMALL FIGURE LIES HUDDLED IN THE DARKNESS"
9014 DATA"* BOOK FILLED STUDY. BOOK SHELVES LINE THE WALLS"
9015 DATA"IN THE KITCHEN. THERE IS BLOOD SPLATTERED ON THE WALLS AND A POOL ON THE FLOOR"
9016 DATA"*N EXQUISITE DINING ROOM WITH A HUGE OAK TABLE AND A CHINA CABINET"
9017 DATA"* WELL STOCKED FOOD PANTRY WITH SHELVES AND SHELVES OF FOOD. A DOOR IS EAST"
9018 DATA"* VERY SHORT EAST/WEST HALL"
9019 DATA"* SMALL STORAGE ROOM. A NAILED DOOR IS SOUTH"
9020 DATA"* DARKENED ROOM STREWN WITH SCIENTIFIC INSTRUMENTS. THERE'S A HOLE IN THE SOUTH WALL"
9021 DATA"* DINGY LOOKING ROOM. AN ANCIENT NUMERAL LOCK IS ON A DOOR ON THE NORTH WALL"
9022 DATA"* DAMP BRICK ROOM. A STAIRWAY IS EAST. THERE IS A TERRIBLE STENCH"
9023 DATA"AT THE SOUTH END OF A NORTH/SOUTH HALL"
9024 DATA"IN THE CENTRE OF A NORTH/SOUTH HALL"
9025 DATA"AT THE NORTH END OF A NORTH/SOUTH HALL"
9026 DATA"* STARK ROOM. THE WALLS, CEILING, AND FLOOR ARE ALL RED BRICK"
9027 DATA"* BRILLIANT CRYSTAL ROOM. A TOWERING FIGURE IN WHITE STANDS IN THE CENTRE"
9028 DATA"*N EAST/WEST HALL"
9029 DATA"* SHORT HALLWAY"
9030 DATA"* LOW CEILINGED CRYPT. IT IS FULL OF DUST AND COBWEBS"
9031 DATA"*N EMPTY ROOM WITH A GRAFFITI ON THE WALL 'JIM AND CHARLIE GERRIE WERE HERE'"
9032 DATA"* DIM SHIMMERING ROOM. A GHOSTLY FIGURE IS SEATED AT A CONCERT PIANO"
9033 DATA"* LARGE DESERTED ROOM. THERE IS NOTHING IN VIEW"
9034 DATA"* DAMP MUSTY PASSAGE. A ROPE HANGS THROUGH A HOLE IN THE FLOOR"
9035 DATA"* GREAT SILVER ROOM. THE WALLS ARE A SHIMMERING HAZE"
9036 DATA"* WIDE, DARK, SMELLY PIT. A LOW WAILING MOAN PERMEATES THE DARKNESS. A STAIRWAY IS NORTH"
9037 DATA"* STONE ROOM STREWN WITH ANIMAL BONES"
9038 DATA"* SMELLY PIT WITH A DIRT FLOOR. FUNGUS GROWS ON THE WALLS."
9039 DATA"* VERY EERIE CHAMBER. THE WALLS ARE PAINTED BLACK, AND ANCIENT RUNES ARE DRAWN ON THE FLOOR"
9040 DATA"* BRILLIANT GREEN ROOM. THE LIGHT MAKES YOU SQUINT. THERE IS A BEAUTIFUL FOUNTAIN HERE"
9041 DATA"* GIGANTIC CAVERN STRETCHING EAST AND WEST. THE ODOR IS SICKENING"
9042 DATA"AT THE GATES OF HELL IN A MAZE OF PASSAGES. 'IT' IS UPON YOU"
9043 DATA"ON A HIGH PINNACLE OVERLOOKING THE BURNING PITS OF HELL. THE DEMON IS OUTLINED IN FLAMES"

9050 DATAN,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,CHOP,9,TALK,10,UNSCR,11,TURN,11,TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I
9060 DATA14,SAY,15,READ,16,KILL,17,UNLOC,18,OPEN,19,LISTE,20,LOOK,21,SEARC,21,CLEAN,22,EXAMI,23,WAIT,24,SHOOT,25,QUIT,26

9100 DATAROOM,FOOD,NAMEP,CALEN,CARD,PHONE,MOZAR,AXE,BOOK,CABIN,KEY,BALL,DIARY,DESK,CRANK,CHEST,NOTE,SHILL,GROWT,TABLE
9101 DATADOOR,VIAL,CRUCI,ROPE,SHIEL,CLUB,RAT,BEAMS,REVOL,WINDO,VICTR,OPENE,CAB,DEMON,CROWL,PORTR,STATU,LOCK,GAFAL
9102 DATASWORD,WALL,PIANO,MIST,CORPS,FIGUR,BODY,BLOOD,NORTH,SOUTH,EAST,WEST,UP,DOWN

9150 DATAROOM,-1,-1,PLATE OF DELICIOUS FOOD,-1,1,NAMEPLATE,-1,1,CALENDAR,-1,1,"I.D. CARD",,1,PHONE,1,,MOZART,-1,
9151 DATAHAND AXE,-1,1,ANCIENT BOOK,-1,1,CABINET,7,,KEY,-1,1,CRYSTAL BALL,-1,1,DIARY,-1,1,DESK,14,,CRANK,-1,,CHEST,-1,
9152 DATAOLD NOTE,-1,1,50 SHILLINGS,-1,1,BROWN SLIMEY GROWTH,-1,1,TABLE,16,,DOOR,-1,,VIAL OF HOLY WATER,-1,1,CRUCIFIX,-1,1
9153 DATAROPE,-1,,GOLD SHIELD,-1,1,SILVER CLUB,30,1,HUGE UGLY RAT,37,,BEAMS,-1,,REVOLVER,,1,WINDOW,1,,VICTROLA,-1,
9154 DATALETTER OPENER,-1,1,HANSOM CAB,-1,,DEMON,42,,CROWLEY MANOR,-1,,OLD PORTRAITS,11,,STATUE OF WHITE ELEPHANT,-1,1
9155 DATALOCK,21,,GAFALA,-1,,SILVER SWORD,-1,1,PLYWOOD WALL,-1,,PIANO,11,,EVIL MIST,-1,,CORPSES,30,,FIGURE,13,
9156 DATABODY,-1,,BLOOD,15,

9201 DATA,2,,,,,1,3,,,,,2,,,,,,,,,,,,,,,6,,,7,,5,,,,,6,8,12,,,9,,,7,,,10,8,,,,,,9,,,15,,12,,,,,,13,11,7,,,,,12,14,,,
9214 DATA,,,13,,,,,,16,,10,17,,15,,,,,16,,,,,,,19,17,,,,,,18,,,,19,,21,,,,,20,,,,,21,,,,23,24,,,,22,,25,23,,,,,,24,,26,,
9226 DATA,,25,,,,,,,,,26,27,,,30,,,36,27,,,,,31,,28,32,,,,30,,,,,,,30,,,,,,,,,32,,,35,,,33,,,,34,,,,29,,,,37,,,,38,36,
9238 DATA,,37,,,,41,,,,,38,,,,39,,,42,39,,,,,,41,,,,,,,,,,
REM L(22)=0:L(23)=0:L(25)=0:L(40)=0:L(12)=0:L(8)=0:R1=1
