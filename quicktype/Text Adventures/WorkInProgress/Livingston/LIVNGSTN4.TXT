0 CLEAR500:DIMCC,ZZ,M$,I$,J1$(3),J2$(3),J3$(3),J4$(3),J5$(3),J6$(3):CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO50
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINT"DON'T BE SILLY.":GOTO1200
50 CLS:PRINT@168,"DR. LIVINGSTON":PRINT:PRINT
100 Y=1:XG=1:G6$="YOU DON'T HAVE IT.":G7$="NOTHING UNUSUAL.":R$=" 'TIS DONE. "
101 PM=35:VM=27:NM=46:IM=16:DIMV$(VM),N$(NM),I$(IM),IL(IM),IP(IM),IN(IM),P$(PM),P(PM,5):GOSUB50000:GOTO1000
490 A$="":B$=A$:C$=A$:GV$=A$:GN$=A$:INPUTA$
500 Z=LEN(A$):IFZ=0THEN490
501 IFZ-1THEN510
502 IFA$="I"THENV=3:N=0:RETURN
503 V=0:C$=A$:GOTO580
510 GOSUB600:B$=D$:GV$=G$:IFZ1=1THENC$=B$:V=0:GOTO580
511 FORI=1TOVM:IFB$<>V$(I)THENNEXT:GOTO520
512 ZZ=I:V=I:I=VM:NEXTI:I=ZZ:GOTO550
520 V=0:C$=B$:GOTO580
550 IFZ-Z1-1<1THEN590
551 A$=RIGHT$(A$,Z-Z1-1):Z=LEN(A$):C$=A$
560 IFLEFT$(C$,1)<>" "THEN570
561 C$=RIGHT$(C$,Z-1):Z=LEN(C$):IFZ=0THEN590
562 GOTO560
570 A$=C$:GOSUB600:C$=D$
580 GN$=G$:FORI=1TONM:IFC$<>N$(I)THENNEXT:GOTO590
581 N=I:ZZ=I:I=NM:NEXTI:I=ZZ:RETURN
590 N=O:RETURN
600 FORI=1TOZ:IFMID$(A$,I,1)<>" "THENNEXT:ZZ=I:GOTO620
610 ZZ=I:I=Z:NEXTI
620 I=ZZ:D$=LEFT$(A$,I-1):G$=D$:D$=LEFT$(D$,5):Z1=LEN(D$):RETURN
1000 CLS:GA$=" NEITHER ARE SEEN AGAIN.":GB$="THE NATIVE TAKES YOUR ":GC$="YOU ARE LYING IN A WARM SOFT BED."
1001 E$="SEVERAL NATIVES WAVE HELLO!":F$="THAT'S TOO DANGEROUS.":S$="IT'S NOWHERE IN SIGHT."
1002 Q$="YOU HAD BETTER LEAVE, FAST.":M$=P$(Y):GOSUB1:PRINT:D$="YOU CAN SEE ":Z=12:Z1=Y
1003 IFY=1ANDK=0THEN1005
1004 GOTO1020
1005 IFIL(1)=1THENM$=D$+I$(1):GOSUB1:GOTO1080
1006 M$=D$+G7$:GOSUB1:GOTO1080
1010 D$="YOU ARE CARRYING ":Z=16:Z1=-1
1020 A$=D$:J=1
1030 FORI=1TOIM:IFIL(I)<>Z1ANDIL(I)<>-2THEN1070
1035 B$=I$(I):IFI=5ANDIL(I)=-2THENIFZ1=YORIL(2)>0THEN1070
1040 IFJ=0THEN1050
1041 J=0:D$=D$+B$:M$=M$+D$:GOTO1070
1050 M$=M$+", "
1060 D$=B$:M$=M$+D$
1070 NEXTI:IFA$<>D$THENM$=M$+".":GOSUB1:GOTO1080
1071 M$=D$:IFZ1=YTHENM$=M$+G7$:GOSUB1:GOTO1080
1072 M$=M$+"NOTHING.":GOSUB1
1080 IFXB=1THENM$=GC$:GOSUB1
1090 IFV=3ORV=13THEN1250
1200 XD=XD+1:IFY=7THEN5000
1201 IFY=17THEN5100
1202 IFY=34THEN5200
1203 IFY=22ORY=27THEN5300
1204 IFY=12THEN5400
1210 IFY1=18ORY=18THEN1250
1211 X7=0
1220 XD=0
1250 IFTS=0THEN1255
1251 GOTO1260
1255 IFT>20ANDY>3ANDY<>7ANDY<>18THENM$="A NATIVE THROWS A SPEAR AT YOU. HE MISSES AND RUNS OFF.":GOSUB1:IL(6)=Y:TS=1
1260 IFN4=0THEN1265
1261 GOTO1270
1265 IFIL(13)=-1THEN1267
1266 GOTO1270
1267 N5=N5+1:A$="A CHARMING LITTLE FLUTE":IFN5>16THEN1269
1268 GOTO1270
1269 M$="AFTER MANY HOURS OF WHITTLING, YOUR SUGAR CANE IS NOW "+A$+".":GOSUB1:I$(13)=A$:S=S+5:N4=1:IN(13)=15
1270 IFXE=0AND(Y=2ORY=10)THENPRINT:M$="OH NO... YOUR FEET ARE BARE... TOO LATE!":GOSUB1:S=S-50:GOTO7000
1300 PRINT:GOSUB490:GOTO1500
1450 IFY=9ORY=10THENM$="A FIERCE BLACK LEOPARD BARS THE WAY.":GOSUB1:GOTO1200
1460 IFY=32THEN1465
1461 GOTO1470
1465 IFRND(5)<3THENY=34:GOTO1000
1466 Y=31:GOTO1000
1470 PRINT"THAT LEADS TO QUICKSAND.":GOTO1200
1500 T=T+1:IFVTHEN1600
1501 IFNTHEN1520
1510 I=RND(3):IFI=1THENPRINT"WHAT?":GOTO1200
1511 IFI=2THENPRINT"HUH?":GOTO1200
1512 M$="WHAT IN THE WORLD ARE YOU TALKING ABOUT?":GOSUB1:GOTO1200
1520 IFN>12THEN1550
1521 IFN<7THEN1525
1522 N=N-6
1525 Z=P(Y,N-1)
1530 IFZ<0THEN1560
1531 IFZ=98THEN1450
1532 IFZ<>0THEN1540
1533 I=RND(3):IFI=1THENPRINT"THAT DIRECTION IS SEALED OFF.":GOTO1200
1534 IFI=2THENM$="YOU WON'T GET ANYWHERE GOING THAT DIRECTION.":GOSUB1:GOTO1200
1535 PRINT"YOU CAN'T GO THAT WAY.":GOTO1200
1540 Y1=Y:Y=Z:GOTO1000
1550 M$="WHAT DO YOU WANT TO DO WITH THE "+GN+"?":GOSUB1:GOTO1200
1560 IFY=6THEN1565
1561 GOTO1570
1565 IFRND(2)=2THENZ=0:GOTO1530
1566 Y=-Z:GOTO1000
1570 IFY=12THEN1575
1571 GOTO1580
1575 IFRND(4)=1THENY=11:GOTO1000
1576 GOTO1000
1580 IFY=22THEN1582
1581 GOTO1585
1582 IFRND(2)=1THENY=21:GOTO1000
1583 Y=19:GOTO1000
1585 IFRND(8)<4THENY=-Z:I$(14)="A HUNGRY ALLIGATOR":GOTO1000
1586 Z=0:GOTO1530
1600 ONVGOTO1610,1800,1010,1900,1900,2100,2200,1000,2900,2500
1602 IFV>0THENONV-10GOTO2600,2700,1010,2400,3000,3100,3200,3300,3400,3500,2700,3600,3760,3800,3900,3700,3400
1610 IFN=0THENM$="WHO KNOWS WHAT A "+GN$+" IS?":GOSUB1:GOTO1300
1615 IFMI>4THENM$="YOUR LOAD'S TOO HEAVY.":GOSUB1:GOTO1300
1617 IFN=16ANDV5=1ANDIL(8)=YTHENI=8:GOTO1690
1620 IFV5ANDN=16THEN1630
1621 GOSUB5900:IFX1>0THEN3710
1625 IFN=11THENN=22:GOTO2740
1627 IFN=34THENON-(Y=33)GOTO3720:GOTO5960
1630 IFN=24THEN1700
1631 FORI=1TOIM:IFN=IN(I)THEN1635
1632 NEXTI:GOTO1510
1635 IFIL(I)=-1OR(IL(2)=-1ANDIL(I)=-2)THEN1637
1636 GOTO1640
1637 IFI=7THENPRINT"YOU ALREADY HAVE HIM.":GOTO1200
1638 PRINT"YOU ALREADY HAVE IT.":GOTO1200
1640 IFIL(I)<>YOR(Y=1ANDK=0ANDI<>1)THEN5960
1642 IFXBTHENIFI<>1THENPRINT"FROM BED?":GOTO1220
1645 IFN=18THEN3600
1646 IFI=2ANDIL(5)=-2THENMI=MI+1
1650 IFN=37THEN1655
1651 GOTO1660
1655 IFXFTHEN1690
1656 PRINT"IT'S NOT FREE.":GOTO1200
1660 IFN=38THENPRINT"DR. LIVINGSTON?":INPUTA$:IFA$<>"I PRESUME"THENGOSUB500:GOTO1500
1690 PRINTR$
1695 IL(I)=-1:MI=MI+1:GOTO1200
1700 IFXETHENPRINT"YOU'RE WEARING THEM.":GOTO1200
1705 Z=35:IFRND(8)<4THEN1707
1706 GOTO1710
1707 IFRND(2)=1THENZ=25:GOTO1710
1708 Z=27
1710 IFK=0THEN1720
1715 M$="YOUR FEET ARE NOW SNUG IN A PAIR OF HIKING BOOTS.":GOSUB1
1716 S=S+5:XE=1:I$(7)="DR. LIVINGSTON":IL(7)=Z:IN(7)=38:GOTO1300
1720 PRINT"WHAT BOOTS?":GOTO1300
1800 IFN=0THEN1610
1810 IFN=24THENPRINT"THEY'RE STUCK.":GOTO1200
1811 FORI=1TOIM:IFIN(I)=NTHEN1820
1812 NEXTI:GOTO1510
1820 IFIL(I)>-1THEN5950
1821 IFY=1ANDK=0ANDI>1THENPRINT"THERE'S NO ROOM.":GOTO1300
1830 IFIL(I)=-2THENON-(IL(2)=-1)GOTO3100:GOTO5950
1840 IFN=16THEN3110
1841 IFI=2ANDIL(5)=-2THENMI=MI-1
1890 IL(I)=Y:PRINTR$:GOSUB3480
1895 MI=MI-1:GOTO1200
1900 B$="IT'S ALREADY ":C$=B$+"CLOSED.":A$=B$+"OPEN.":D$="FROM BED?":IFN=0THEN1510
1901 IFV=5THEN2000
1905 IFY-1THEN1930
1906 IFN-22THEN1920
1910 IFXB=1THENM$=D$:GOSUB1:GOTO1200
1915 IFK=0THENK=1:GOTO1000
1916 M$=A$:GOSUB1:GOTO1200
1920 IFN=20THENIFIL(1)=-1THEN1925
1921 GOTO5950
1925 IFXC=0THENM$="THE BOOK FALLS OPEN TO AN UNDERLINED PASSAGE.":GOSUB1:XC=1:GOTO1200
1926 M$=A$:GOSUB1:GOTO1200
1930 IFN=13THEN1935
1931 GOTO1510
1935 IFIL(2)=-1THENM$=A$:GOSUB1:GOTO1200
1936 GOTO5950
2000 IFY-1THEN2020
2001 IFN-22THEN2010
2005 IFXB=1THENM$=D$:GOSUB1:GOTO1200
2007 IFK=1THENK=0:GOTO1000
2008 M$=C$:GOSUB1:GOTO1200
2010 IFN=20THEN2015
2011 GOTO2020
2015 IFIL(1)=-1THEN2017
2016 GOTO5950
2017 IFXC=1THENPRINTR$:XC=0:GOTO1200
2018 M$=C$:GOSUB1:GOTO1200
2020 IFN=13THEN2025
2021 GOTO1510
2025 IFIL(2)=-1THEN2027
2026 GOTO5950
2027 IFIL(5)=-2THENPRINT"THE MOUSE WON'T ESCAPE!":GOTO1200
2028 PRINT"IT'LL JUST FALL OPEN AGAIN.":GOTO1200
2100 IFY=2THEN2210
2101 IFIL(1)=-1ANDXB=1THENIL(1)=1:MI=MI-1:Y=2:XB=0:GOTO1000
2102 GOTO1510
2200 IFN>0THEN1510
2201 IFY=1THEN2205
2202 IFY<>10THEN1510
2203 IFIL(7)=-1THENIL(7)=RND(33)+2:PRINT"HELP":MI=MI-1:FORJ=1TO1500:NEXT
2204 SOUND1,10:GOTO2210
2205 Y=10:XB=0:IFIL(1)=-1THENMI=MI-1:IL(1)=1:GOTO1000
2206 GOTO1000
2210 Y=1:GOTO1000
2400 IFY=29THEN2405
2401 GOTO2410
2405 IFN=40THEN2407
2406 GOTO2420
2407 IFIL(4)=-1THENM$=GB$+"GROUNDNUTS.":GOSUB1:XF=1:IL(4)=0:MI=MI-1:S=S+10:GOTO1200
2408 GOTO5950
2410 IFY=17THEN2415
2411 GOTO2420
2415 IFN=42THEN2417
2416 GOTO2420
2417 IFXGTHEN6000
2420 PRINT"NO DICE.":GOTO1200
2500 IFY=1THENM$="THIS MAY SEEM STRANGE OR EVEN ABSURD BUT IN THE BOOK IS A MAGICAL WORD!":GOSUB1:GOTO1250
2510 IFY=23ORY=24THENM$="YOU WON'T GET ACROSS WITH A 'HOP' OR A 'SKIP'.":GOSUB1:GOTO1250
2520 IFY=25ORY=22ORY=12ORY=27THENPRINT"BE PERSISTENT.":GOTO1250
2530 IF(Y=34ORY=17)ANDIL(6)=-1THENPRINT"YOU LOOK HOSTILE.":GOTO1250
2540 IFY=34THENM$="LION TRAPS CIRCLE THE VILLAGE.":GOSUB1:GOTO1250
2550 IFY=7THENM$="SOME SNAKES JUST CAN'T BE CHARMED.":GOSUB1:GOTO1250
2560 PRINT"YOU'RE ON YOUR OWN.":GOTO1250
2600 A$="AFRICA - LAND OF THE UNKNOWN BY R. U. REDDE"
2601 B$="...PLORER NODDED OFF TO SLEEP, HIS THOUGHTS BEGAN TO DRIFT T..."
2602 IFN=36THEN2605
2603 GOTO2610
2605 IFIL(11)=-1THENPRINT"SWAMI DIAMOND":GOTO1200
2606 GOTO5950
2610 IFN=19THEN2615
2611 GOTO2620
2615 IFY=29ORY=33THENPRINT"TRADING POST":GOTO1200
2616 IFY=31THENM$="UJIJI COUNTRY - KEEP OUT NO HEED 'EM, WE EAT 'EM.":GOSUB1:GOTO1200
2617 GOTO5960
2620 IFN=20ORN=25THEN2630
2621 GOTO1510
2630 IFIL(1)=-1THEN2640
2631 GOTO5950
2640 IFXB=1THEN2650
2641 PRINT"TRY READING IN BED.":GOTO1200
2650 IFN=20THENM$=A$:GOSUB1:GOTO1200
2651 IFXC=1THENM$=B$:GOSUB1:GOTO1200
2652 GOTO5960
2700 IFN=0THEN1510
2701 IFN<13THEN1520
2702 IFN=17THEN3800
2703 IFN=21ANDY=3THENY=2:GOTO1000
2704 IFN=46ANDY=17THENY=18:GOTO1000
2740 IFN=22ANDY=1THENPRINT"YOU ARE AT THE CLOSET.":XB=0:GOTO1200
2741 IFN=23THEN2745
2742 GOTO2760
2745 IFY=1THENM$=GC$:GOSUB1:XB=1:GOTO1300
2746 PRINT"IN DARKEST AFRICA?":GOTO1200
2760 IFN=41ANDY=34THENY=35:GOTO1000
2761 IFN=29THEN2765
2762 GOTO2770
2765 IFY=16THENY=17:GOTO1000
2766 IFY=32THENN=3:GOTO1525
2770 GOTO1510
2900 IFN=35THEN2905
2901 GOTO2910
2905 IFIL(16)=-1THEN2920
2906 GOTO5950
2910 IFN=14THEN2915
2911 GOTO3
2915 IFIL(6)=-1THEN2920
2916 GOTO5950
2920 M$="IT SAILS THROUGH THE AIR... AND HITS THE ":IFY=2THENM$=M$+"WATER, QUICKLY SINKING.":GOSUB1:J=0:GOTO2940
2930 J=Y:IFY=1THENM$=M$+"FLOOR.":GOSUB1:GOTO2935
2931 M$=M$+"GROUND.":GOSUB1
2935 IFY=7THENJ=6
2940 IFN=14THENIL(6)=J:GOTO2945
2941 IL(16)=J
2945 IFY=IL(14)ANDRND(2)=2THEN2947
2946 GOTO2950
2947 FORCC=1TO999:NEXT:M$="WAIT! THAT'S NOT GROUND. IT'S ALLIGATOR.":GOSUB1:I$(14)="A DEAD ALLIGATOR"
2950 GOTO1895
3000 IF(Y=17ORY=34ORY=29)ANDN=44ANDIL(6)=-1THENPRINT"WITH YOUR PUNY SPEAR?":GOTO1200
3001 GOTO3710
3100 IFN=18THEN3105
3101 GOTO3110
3105 IFIL(5)=-2ANDIL(2)=-1THEN3107
3106 GOTO5950
3107 IFNOT(Y=9ORY=10)THENM$="THE MOUSE JUMPS OUT AND SCAMPERS OFF.":GOSUB1:IL(5)=2:GOTO1895
3108 M$="THE MOUSE JUMPS OUT AND STARTLES THE LEOPARD INTO FLIGHT."+GA$:GOSUB1
3109 P(9,4)=10:P(10,5)=9:IL(5)=0:S=S+15:GOTO1895
3110 IFN=16THEN3115
3111 GOTO1510
3115 IFIL(8)=-1THEN3117
3116 GOTO5950
3117 IFY<>22THENM$="THE VIPER SLOWLY SLITHERS AWAY.":GOSUB1:IL(8)=7:GOTO1895
3118 M$="THE VIPER WAKES UP, ATTACKS THE DOG, AND BOTH FALL INTO THE QUICKSAND."+GA$:GOSUB1:IL(8)=0:S=S+15:GOTO3120
3120 I$(15)="A DEEP-BLUE SAPPHIRE":IN(15)=45:PRINT:M$="SOMETHING SPARKLES IN THE GRASS.":GOSUB1:GOTO1895
3200 IFN=16THEN3205
3201 GOTO3710
3205 IFV5=0THEN3207
3206 PRINT"AGAIN?":GOTO1200
3207 IFY=IL(8)THEN3209
3208 PRINTS$:GOTO1200
3209 IFIL(13)=-1THENI$(8)="A LETHARGIC VIPER":V5=1:S=S+10:PRINTR$:GOTO1210
3210 M$="YOU MAY HAVE GOOD LOOKS, BUT...":GOSUB1:GOTO1200
3300 IFN=43THEN3310
3301 PRINT"OK-- HERE GOES... J U M P!":GOTO1200
3310 IFY<23ORY>24THENM$="THERE ISN'T ANY QUICKSAND HERE.":GOSUB1:GOTO1200
3320 M$="DO YOU REALLY EXPECT TO JUMP OVER 30 FEET?":GOSUB1:PRINT:INPUTA$:IFA$<>"YES"THENGOSUB500:GOTO1500
3330 CLS:PRINT"OK - HERE GOES":FORI=1TO1500:NEXT
3350 CC=1:GOSUB3390:ZZ=206:CC=2:GOSUB3380:GOSUB3390:FORCC=1TO150:NEXT:PRINT@ZZ+160,"       ";
3352 ZZ=176:CC=3:GOSUB3380:GOSUB3390:FORCC=1TO150:NEXT:PRINT@ZZ+160,"       ";:ZZ=144:CC=1:GOSUB3380
3354 FORCC=4TO43:GOSUB3390:FORZZ=1TO50:NEXT:NEXT:ZZ=176:CC=3:GOSUB3380:FORZZ=1TO150:NEXT:ZZ=209:CC=2:GOSUB3380
3362 PRINT@215,"TA DA!";:FORI=1TO1500:NEXTI:Q=0:IFY=23THENY=24:GOTO1000
3370 Y=23:GOTO1000
3380 PRINT@ZZ-32,"       ";:PRINT@ZZ,J1$(CC);:PRINT@ZZ+32,J2$(CC);:PRINT@ZZ+64,J3$(CC);
3381 PRINT@ZZ+96,J4$(CC);:PRINT@ZZ+128,J5$(CC);:PRINT@ZZ+160,J6$(CC);:RETURN
3390 PRINT@384,MID$("ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ",CC,32);:RETURN
3400 GOSUB3480
3410 M$="YOUR SCORE IS"+STR$(ST)+" POINTS OUT OF 250 IN"+STR$(T)+" TURNS.":GOSUB1:IFV=27THEN3420
3411 GOTO1250
3420 IFY=2ORY=10ORD$="QUIT"THENEND
3425 IFRETHENIFRND(5)>2THENEND
3430 PRINT"OK":FORZZ=1TO20:PRINT"Ä";:FORCC=1TO250:NEXTCC:PRINTCHR$(8);:FORCC=1TO250:NEXTCC:NEXTZZ
3431 CLS:PRINT@162,"A MIRACLE YOU HAVE RECOVERED":MI=0:FORI=2TO16:IFIL(I)=-1THENIL(I)=RND(32)+3
3440 FORCC=1TO4000:NEXTCC:Y=2:RE=1:GOTO1000
3480 ST=0:FORI=1TO16:IFIL(I)=1THENST=ST+IP(I)
3485 NEXTI:ST=ST+S:IFST<215THENRETURN
3492 B$="AN '":A$="ADVENTURER'":IFT<190THENST=ST+15:B$="A 'TALENTED "
3494 IFT<160THENST=ST+10:B$="A 'SKILLED "
3496 IFT<130THENST=ST+10:B$="A 'MASTER "
3498 M$="YOU ARE "+B$+A$:GOSUB1:D$="QUIT":V=27:GOTO3410
3500 IFN=0THEN1510
3501 IFY=1ORY>3THEN3530
3505 IFN=21THENPRINT"WHERE?":GOTO1200
3520 IFY=2OR(Y=3AND(N=4ORN=10))THEN2700
3530 IFY=23ORY=24THENPRINT"ON QUICKSAND!":GOTO1200
3540 PRINT"ROW ON LAND?":GOTO1200
3600 IFN=18THEN3605
3601 GOTO3700
3605 IFY=IL(5)THEN3607
3606 GOTO5960
3607 IFIL(2)=-1THENM$="THE MOUSE IS NOW LOST IN THE FOLDS OF YOUR KNAPSACK.":GOSUB1:I=0:IL(5)=-2:GOTO1695
3608 M$="YOU DON'T HAVE ANYTHING TO PUT IT IN.":GOSUB1:GOTO1200
3700 IFN=18ORN=34THENPRINT"IT JUST ATE.":GOTO1200
3710 IFN=0THEN1510
3711 GOSUB5900:IFX1=1THENPRINTF$:GOTO1200
3712 IFX1=2THEN5960
3720 IFN=18ORN=34THEN3725
3721 GOTO3730
3725 IFV=17THENPRINT"RODENTS ARE CHARMING ENOUGH!":GOTO1200
3726 PRINT"IT'S TOO QUICK.":GOTO1200
3730 GOTO3
3760 IFY=18THENY=17:GOTO1000
3761 GOTO1510
3800 IFY=6ORY=7THENY=7:GOTO1000
3801 GOTO1510
3900 A$="UMMMMM  GOOD.":IFN=18ANDIL(5)=-2ANDIL(2)=-1THENPRINTR$"Y U K !!":IL(5)=0:GOTO1895
3920 FORI=1TO16:IFIN(I)<>NTHENNEXT:GOTO3960
3921 IFIL(I)<>-1THEN5950
3922 IFN=42ORN=40ORN=39THENM$=R$+A$:GOSUB1:IL(I)=0:GOTO1895
3945 IFN=0THEN1510
3950 GOSUB5900:IFX1=1THENPRINTF$:GOTO1200
3960 GOTO3
5000 IFV5=0THEN5005
5001 GOTO1210
5005 IFXD=5THEN5140
5006 IFXD=8THEN7000
5007 GOTO1250
5100 IFX7=0THEN5105
5101 GOTO5110
5105 X7=1:IFIL(6)=-1THEN5130
5106 GOTO5120
5110 IFIL(6)=-1THEN5115
5111 GOTO5150
5115 IFXD>4THEN7000
5116 GOTO5140
5120 IFXGTHENM$="A FRIENDLY NATIVE APPROACHES WITH SOME TRINKETS. IT APPEARS HE WANTS TO MAKE A TRADE.":GOSUB1:GOTO1220
5121 GOTO1210
5130 M$="SUDDENLY, A VOLLEY OF SPEARS FLIES OVER YOUR HEAD; AN OBVIOUS WARNING!":GOSUB1
5140 PRINTQ$:GOTO1250
5150 PRINTE$:GOTO1220
5200 IFX7THEN5110
5201 X7=1:IFIL(6)=-1THEN5130
5202 GOTO5150
5300 IFNOT((Y=IL(15)ANDI$(15)="A WILD DOG")OR(Y=IL(14)ANDI$(14)="A HUNGRY ALLIGATOR"))THEN1210
5301 IFXD=8THEN7000
5302 IFXD=3ORXD=5THENPRINTQ$
5310 GOTO1250
5400 IFXD=3THENM$="A VOICE ECHOES FROM THE MOUTH OF THE CAVE... S W A M I":GOSUB1:GOTO1210
5401 GOTO1250
5900 X1=0:IFN=16ORN=27ORN=32ORN=33THEN5910
5901 RETURN
5910 IF(N=33ANDY=27)OR(N=32ANDY=22ANDIN(15)=32)OR(N=27AND(Y=9ORY=10)ANDP(9,4)=98)OR(N=16ANDV5=0ANDY=7)THENX1=1:RETURN
5911 X1=2:RETURN
5950 IFN=38THENM$=LEFT$(G6$,15)+"HIM.":GOSUB1:GOTO1200
5951 PRINTG6$:GOTO1200
5960 IFN=38THENPRINT"HE"+RIGHT$(S$,20):GOTO1200
5961 PRINTS$:GOTO1200
6000 IFIL(10)=-1THEN6005
6001 GOTO5950
6005 M$=GB$+"BEANS, GIVES YOU A TRINKET, THEN RUNS OFF.":GOSUB1
6006 I$(10)="A GOLDEN NOSE RING":N$(26)="RING":XG=0:S=S+15:IN(10)=26:GOTO1220
7000 PRINT:PRINT"ANOTHER EXPLORER IS LOST TO THE WILDS OF AFRICA.":PRINT:V=27:GOTO3400
50000 FORI=1TOPM:READP$(I):NEXT
50001 DATA"YOU ARE IN A BEDROOM, WEARING PJ'S. A NIGHTSTAND SITS BETWEEN THE CLOSET AND THE BED. THE BEDCOVERS ARE TURNED DOWN."
50002 DATA"YOU ARE DRIFTING IN A ROWBOAT ON A LAKE. THERE IS A BEACH ON THE EASTERN SHORELINE."
50020 DATA"YOU ARE ON THE EASTERN SHORELINE. A BOAT IS TIED TO THE DOCK.","YOU ARE ON A GRASSY PLAIN.",""
50021 DATA"A TRAIL BEGINS HERE. OFF TO ONE SIDE IS A TREE."
50060 DATA"YOU ARE PRECARIOUSLY BALANCED IN A TALL MOSS-COVERED TREE."
50061 DATA"YOU ARE ON A N/S TRAIL. CLIFFS TO THE EAST REACH UP TO A PLATEAU."
50062 DATA"YOU ARE AT THE FOOT OF A CLIFF. A TRAIL LEADS UP TO AN OPENING."
50110 DATA"YOU ARE AT THE MOUTH OF A CAVE. A TRAIL LEADS DOWNWARD."
50111 DATA"YOU ARE IN A TUNNEL. A FAINT GLOW EMANATES FROM THE WEST. A SIGN ON THE WALL SAYS: 'CAVERNS AHEAD- ENTER AT OWN RISK'"
50130 DATA"YOU ARE IN AN IMMENSE CAVERN. THE WALLS ARE COVERED WITH AN IRIDESCENT GLOW."
50131 DATA"YOU ARE IN A LONG E/W PASSAGE.",""
50132 DATA"THE TRAIL HERE IS OVERGROWN WITH GRASS.","THE TRAIL WIDENS OUT TO A NEARBY VILLAGE."
50140 DATA"YOU ARE IN A NATIVE VILLAGE. THERE ARE SEVERAL CAMPFIRES ABOUT.","YOU ARE IN A GRASS HUT."
50141 DATA"YOU ARE AT THE EDGE OF A JUNGLE. A TRAIL LEADS NORTH."
50142 DATA"YOU ARE AT THE EDGE OF A JUNGLE. GRASSLANDS EXTEND TO THE EAST AND SOUTH."
50160 DATA"YOU ARE IN A JUNGLE. TRAILS LEAD OFF IN SEVERAL DIRECTIONS.",""
50161 DATA"THERE IS A LARGE BODY OF QUICKSAND HERE, WITH A TRAIL JUST BEYOND.","",""
50162 DATA"YOU ARE ON THE GRASS PLAINS. THE TERRAIN IS BECOMING SOMEWHAT MARSHY."
50170 DATA"YOU ARE IN THE MARSHES. SOMETHING IS MOVING THE GRASS."
50171 DATA"YOU ARE IN A VALLEY. A SMALL OPENING MARKS THE HILL BESIDE YOU. ROLLING HILLS FLOW SOUTHWARD."
50180 DATA"YOU ARE AT A TRADING POST. AN EAGER NATIVE SMILES AT YOU AS HE POINTS TO HIS SIGN."
50181 DATA"YOU ARE IN A FIELD OF CORN. THE GROUND DROPS OFF TO THE NORTH."
50182 DATA"YOU ARE ON THE GRASS PLAINS. TWO SHRUNKEN HEADS DECORATE A SIGN."
50190 DATA"YOU ARE ON THE PLAINS. A VILLAGE LIES TO THE EAST."
50191 DATA"YOU ARE BEHIND A HUT. THERE IS A SIGN IN THE GROUND. A SQUIRREL JUST RAN BY."
50200 DATA"YOU ARE IN THE UJIJI VILLAGE. A NATIVE STANDS NEAR HOLDING A SPEAR. HE LOOKS EXCITED."
50201 DATA"YOU ARE IN A PIT. LIGHT STREAMS IN FROM ABOVE."
50352 J1$(1)="èèêÖéèè":J1$(2)="   äÄè ":J1$(3)=" èèäÄèè"
50354 J2$(1)="èèàåáèè":J2$(2)="   åÑç ":J2$(3)=" éååÑåå"
50356 J3$(1)="èâÄÖèèè":J3$(2)="   ÇÄãç":J3$(3)=" èèäÄèè"
50358 J4$(1)="ãèÄÑååÖ":J4$(2)="   äÄá ":J4$(3)=" èèäÄèè"
50360 J5$(1)="éåàèèèè":J5$(2)="   ääè ":J5$(3)=" èèâèÜè"
50362 J6$(1)="ãèèèèèè":J6$(2)="   äàç ":J6$(3)=" èâèèèÜ"
50900 P$(5)=P$(4):P$(14)=P$(10):P$(22)=P$(21):P$(25)=P$(21):P$(24)=P$(23):PRINTTAB(7)"IN SEARCH OF ..."
50901 FORI=1TOPM:FORJ=0TO5:READP(I,J):NEXTJ,I
52000 DATA0,0,0,0,0,0,0,0,3,0,0,0,0,5,4,2,0,0,5,6,4,3,0,0,3,0,6,4,0,0,-5,8,-4,-5,7,0,0,0,0,0,7,6,6,15,9,0,0,0,0,0,0,8,98,0,0,0
52001 DATA11,0,0,98,0,0,12,10,0,13,-11,-11,-11,-11,-11,-11,11,0,14,15,0,0,0,0,0,13,0,28,0,0,16,8,0,0,15,19,17,19,0,0
52010 DATA16,18,0,0,0,0,17,0,0,0,0,0,16,22,20,21,0,0,0,26,26,19,0,0,21,23,22,21,21,21,21,22,-1,23,22,22,98,22,98,21,23,23,24
52011 DATA98,25,98,24,24,-20,25,-20,24,25,25,20,27,30,20,0,0,-26,27,27,27,27,27,0,30,29,0,14,0,0,0,0,28,0,0
52020 DATA28,26,0,31,0,0,30,32,33,31,0,0,31,0,98,31,0,0,0,0,0,31,0,0,34,34,34,32,0,35,0,0,0,13,34,0
53000 FORI=1TOVM:READV$(I):NEXT:DATA"GET","DROP","I","OPEN","CLOSE","DRIFT","SWAMI","LOOK","THROW","HINT","READ","GO","INVEN"
53001 DATA"TRADE","KILL","FREE","CHARM","JUMP","SCORE","ROW","ENTER","CATCH","EXIT","CLIMB","EAT","FEED","QUIT"
54000 FORI=1TONM:READN$(I):NEXT:DATA"N","S","E","W","U","D","NORTH","SOUTH","EAST","WEST","UP","DOWN","KNAPS","SPEAR","FLUTE"
54001 DATA"VIPER","TREE","MOUSE","SIGN","BOOK","BOAT","CLOSE","BED","BOOTS","PASSA","","LEOPA"
54010 DATA"DIAMO","VILLA","TRINK","CANE","DOG","ALLIG","SQUIR","DAGGE","NOTE","CHAIN","LIVIN","CORN","GROUN","PIT","BEANS"
54011 DATA"QUICK","NATIV","SAPPH","HUT"
55000 FORI=1TOIM:READI$(I):NEXT:DATA"A TIMEWORN BOOK","A FADED RED KNAPSACK","EARS OF CORN","A BATCH OF GROUNDNUTS"
55001 DATA"A TINY GREY MOUSE","A POISON-TIPPED SPEAR","A PAIR OF HIKING BOOTS","A VICIOUS VIPER"
55010 DATA"A SPARKLING DIAMOND","A SACK OF COFFEE BEANS","A CRUMPLED NOTE","A SOLID SILVER CHAIN"
55011 DATA"SEVERAL STALKS OF SUGAR CANE","A HUNGRY ALLIGATOR","A WILD DOG","A PEARL-HANDLED DAGGER"
56000 FORI=1TOIM:READIL(I),IP(I),IN(I):NEXT:DATA1,0,20,1,2,13,30,5,39,33,5,40,2,7,18,0,3,14,1,50,24,7,0,16,10,15,28,15,12,42
56001 DATA18,3,36,29,15,37,24,15,31,27,0,33,22,10,32,12,10,35:RETURN
