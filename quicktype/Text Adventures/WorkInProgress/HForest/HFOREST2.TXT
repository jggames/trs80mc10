0 CLEAR1000:DIMCC,ZZ,M$,I$:MC=16384:GOTO15
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 DATA159,8,159,8,193,8,193,8,200,8,200,8,193,12,185,8,185,8,180,8,180,8,170,8,170,8,159,12,193,8,193,8
10 DATA185,8,185,8,180,8,180,8,170,12,193,8,193,8,185,8,185,8,180,8,180,8,170,12,0,2,0,2
15 GOSUB8000
20 CLS
200 GOSUB6000
205 GOSUB1000
207 R=RND(0)
210 P=RND(10)
220 REM
230 REM
240 MF=0
245 PRINT
250 GOSUB2000
255 GOSUB2300
257 GOSUB2700
258 IFSF=1THEN300
260 PRINT:INPUT"INSTRUCTIONS ";IS$
270 GOSUB2500
275 IFF=0THEN260
280 GOSUB3000
290 IFVF=0THENPRINT:PRINT"I DON'T UNDERSTAND !!"
300 IFMF=1THEN240
310 IFMF=0THEN260
451 GOSUB5500
990 END
1000 REM
1010 SN$="WELCOME TO THE HAUNTED FOREST "
1020 GOSUB5500
1040 SN$="AS YOU AWAKE FROM A DEEP SLEEP, THE "
1050 SN$=SN$+"FOREST FLOOR FEELS SOFT AND DRY. "
1060 SN$=SN$+"YOU DO NOT KNOW HOW YOU CAME TO BE HERE "
1070 SN$=SN$+"BUT KNOW THAT YOU MUST FIND THE "
1080 SN$=SN$+"VILLAGE ON THE EDGE OF THE WOOD TO REACH SAFETY."
1100 GOSUB5500:REM FORMAT
1120 SN$="YOU LOOK AROUND,TRYING TO GET YOUR BEARINGS."
1130 GOSUB5500
1140 PRINT:PRINT"     PRESS ANY KEY TO START";
1150 A$=INKEY$:IFA$=""THEN1150
1160 CLS:RETURN
1180 REM
2000 CLS
2010 SN$="YOU ARE "+LN$(P):GOSUB5500
2020 SN$="YOU SEE "
2030 REM
2040 F=0:SP$=""
2050 FORI=1TO3
2060 IFVAL(IV$(I,2))<>PTHEN2080
2070 SN$=SN$+SP$+"A "+IV$(I,1):F=1:SP$=", "
2080 NEXT
2090 IFF=0THENSN$=SN$+"NO OBJECTS"
2100 GOSUB5500
2110 RETURN
2120 REM
2299 REM
2300 REM
2310 EX$=EX$(P)
2320 NR=VAL(LEFT$(EX$,2))
2330 EA=VAL(MID$(EX$,3,2))
2340 SO=VAL(MID$(EX$,5,2))
2350 WE=VAL(RIGHT$(EX$,2))
2355 IF(NROREAORSOORWE)=0THENRETURN
2360 PRINT:SN$="EXITS ARE TO THE "
2370 IFNR<>0THENSN$=SN$+"NORTH "
2380 IFSO<>0THENSN$=SN$+"SOUTH "
2390 IFEA<>0THENSN$=SN$+"EAST "
2400 IFWE<>0THENSN$=SN$+"WEST "
2410 GOSUB5500
2420 RETURN
2500 REM
2510 IFIS$="LIST"ORIS$="END"THENVB$=IS$:F=1:RETURN
2515 IFIS$="LOOK"THENVB$=IS$:F=1:RETURN
2520 F=0
2530 LS=LEN(IS$)
2540 FORC=1TOLS
2550 A$=MID$(IS$,C,1)
2560 IFA$<>" "THEN2590
2570 VB$=LEFT$(IS$,C-1):F=1
2580 NN$=RIGHT$(IS$,LS-C):C=LS
2590 NEXT
2610 IFF=1THENRETURN
2620 PRINT:PRINT"I NEED AT LEAST TWO WORDS !!":RETURN
2700 REM
2705 SF=0
2710 IFP>4ANDRND(0)<.1THENGOSUB4290:RETURN
2720 IFP=1THENGOSUB4590
2721 IFP=2THENGOSUB4870
2722 IFP=3THENGOSUB5100
2723 IFP=4THENGOSUB4590
2730 RETURN
3000 REM
3010 VF=0
3020 IFVB$="GO"ORVB$="MOVE"THENVF=1:GOSUB3500
3030 IFVB$="TAKE"ORVB$="PICK"THENVF=1:GOSUB3700
3040 IFVB$="DROP"ORVB$="PUT"THENVF=1:GOSUB3900
3050 IFVB$="LIST"ORVB$="INVENTORY"THENVF=1:GOSUB4100
3055 IFVB$="LOOK"THENVF=1:MF=1:RETURN
3060 IFVB$="END"ORVB$="FINISH"THENVF=1:GOSUB4170
3070 RETURN
3500 REM
3505 GOSUB3630
3510 MF=1
3520 DR$=LEFT$(NN$,1)
3530 IFDR$<>"N"ANDDR$<>"E"ANDDR$<>"S"ANDDR$<>"W"GOTO3590
3540 IFDR$="N"ANDNR<>0THENP=NR:RETURN
3550 IFDR$="E"ANDEA<>0THENP=EA:RETURN
3560 IFDR$="S"ANDSO<>0THENP=SO:RETURN
3570 IFDR$="W"ANDWE<>0THENP=WE:RETURN
3580 PRINT:PRINT"YOU CAN'T ";IS$
3585 MF=0:RETURN
3590 REM
3600 PRINT"WHAT IS ";NN$;" ?":MF=0:RETURN
3630 REM
3640 NN$=NN$+" ":LN=LEN(NN$):C=1
3645 FORI=1TOLN
3650 IFMID$(NN$,I,1)<>" "THENNEXT:RETURN
3655 W$=MID$(NN$,C,I-C):C=I+1
3660 IFW$="NORTH"ORW$="EAST"THENNN$=W$:I=LN
3665 IFW$="SOUTH"ORW$="WEST"THENNN$=W$:I=LN
3670 NEXT:RETURN
3700 REM
3710 GOSUB5300
3720 IFF=0THENSN$="THERE IS NO "+W$:GOSUB5500:RETURN
3730 OV=F:GOSUB5450
3740 IFHF=1THENSN$="YOU ALREADY HAVE THE "+IV$(F,1):GOTO5500
3760 IFVAL(IV$(F,2))<>PTHENSN$=IV$(F,1)+" IS NOT HERE":GOTO5500
3790 A=0
3800 FORJ=1TO2
3810 IFIC$(J)=""THENIC$(J)=IV$(F,1):AF=1:J=2
3820 NEXT
3840 REM
3850 IFAF=0THENPRINT"YOU ALREADY HAVE TWO OBJECTS !":RETURN
3870 SN$="YOU TAKE THE "+IV$(F,1):GOSUB5500
3880 IV$(F,2)="-1":RETURN
3900 REM
3910 GOSUB5300
3920 IFF=0THENSN$="THERE IS NO "+W$:GOSUB5500:RETURN
3950 OV=F:GOSUB5450
3960 IFHF=0THENSN$="YOU DON'T HAVE THE "+IV$(F,1):GOTO5500
3990 SN$="YOU DROP THE "+IV$(F,1):GOSUB5500
4000 IV$(F,2)=STR$(P)
4030 FORJ=1TO2
4040 IFIC$(J)=IV$(F,1)THENIC$(J)="":J=2
4050 NEXT
4060 RETURN
4100 PRINT"OBJECTS HELD:":FORI=1TO2:PRINT"  ";IC$(I):NEXT:RETURN
4170 PRINT:PRINT"ARE YOU SURE (Y/N) ?"
4180 A$=INKEY$:IFA$="Y"THENEND
4190 IFA$="N"THENRETURN
4191 GOTO4180
4290 SF=1:GC=0
4300 SN$="YOU FEEL A COLD SENSATION RUNNING THE LENGTH "
4305 SN$=SN$+"OF YOUR SPINE. SUDDENLY A WHITE APPARITION"
4310 SN$=SN$+" APPEARS FROM OUT OF THE TREES AND MOVES"
4315 SN$=SN$+" TOWARDS YOU":GOSUB5500
4320 SN$="THE GHOST MOVES CLOSER":GOSUB5500
4330 GC=GC+1:IFGC>4THENGOSUB4455
4335 PRINT:INPUT"INSTRUCTIONS ";IS$
4340 GOSUB2500
4345 IFF=0THEN4320
4350 OP=P:GOSUB3000
4355 IFMF=1ANDVB$="GO"THENGOSUB4400:GOTO4320
4357 IFMF=1ANDVB$="LOOK"THENGOSUB2000:GOSUB2300:GOTO4320
4360 IFVF=1THEN4320
4370 IFVB$="KILL"ORVB$="FIGHT"THENGOSUB4425:GOTO4320
4385 IFVB$="SING"THENGOSUB4500:RETURN
4390 SN$="I DON'T UNDERSTAND !":GOSUB5500:GOTO4320
4400 SN$="YOU ARE TRANSFIXED WITH TERROR AND CANNOT"
4410 SN$=SN$+" MOVE...YET":MF=0:GOSUB5500:P=OP:RETURN
4425 REM
4430 SN$="THE GHOST IS A BEING OF THE SUPERNATURAL"
4435 SN$=SN$+" AND LAUGHS AT YOUR FEEBLE ATTEMPTS"
4440 SN$=SN$+" TO INJURE HIM":GOSUB5500:RETURN
4455 REM
4460 SN$="THE PAIN IN YOUR CHEST BECOMES UNBEARABLE"
4465 SN$=SN$+" AND YOU SLUMP ONTO THE LEAFY FOREST FLOOR."
4466 GOSUB5500
4470 SN$="YOUR SPIRIT RISES FROM YOUR INNERT BODY"
4475 SN$=SN$+" AND YOU FLOAT AWAY INTO THE MIST TO JOIN"
4480 SN$=SN$+" THE OTHER TORMENTED SOULS OF THE HAUNTED FOREST."
4490 GOSUB5500:GOTO7000
4500 REM
4505 SN$="YOU KNOW THREE SONGS. WHICH ONE WILL YOU CHOOSE ?"
4507 GOSUB5500
4510 SN$="1) THE THEME FROM 'GHOSTBUSTERS'":GOSUB5500
4515 SN$="2) 'THERE'S A GHOST IN MY HOUSE'":GOSUB5500
4520 SN$="3) 'WAY DOWN UPON THE SWANEE RIVER'":GOSUB5500
4525 PRINT:INPUT"MAKE YOUR CHOICE ";C$
4530 IFVAL(C$)>3ORVAL(C$)<1THENPRINT:PRINT"INVALID":GOTO4525
4535 CR=RND(3)
4537 IFCR<>VAL(C$)THENGOSUB4542
4540 GOSUB4565:RETURN
4542 SN$="THE GHOST HAS A PARTICULAR HATRED OF"
4550 SN$=SN$+" THAT TUNE AND LUNGES AT YOU.":GOSUB5500
4555 GOSUB4455
4560 REM
4565 SN$="THE GHOST IS APPEASED BY YOUR RENDITION OF THE TUNE"
4570 SN$=SN$+" AND VAPOURISES INTO THIN AIR":GOSUB5500:RETURN
4585 REM
4590 REM
4600 SF=1
4605 SN$="YOU HAVE ARRIVED AT THE MOUTH OF A LARGE TUNNEL"
4606 GOSUB5500
4610 SN$="YOU CAN ENTER THE TUNNEL OR RETREAT ALONG THE PATH"
4611 GOSUB5500
4620 REM
4625 PRINT:INPUT"INSTRUCTIONS ";IS$
4630 GOSUB2500
4635 IFF=0THEN4625
4637 GOSUB3000
4640 IFMF=1THENRETURN
4645 IFVF=1THEN4625
4650 REM
4655 IFVB$="ENTER"THENGOSUB4700:RETURN
4660 IFVB$="RETREAT"ANDP=4THENMF=1:P=6:RETURN
4665 IFVB$="RETREAT"ANDP=1THENMF=1:P=9:RETURN
4667 SN$="I DON'T UNDERSTAND":GOSUB5500:GOTO4625
4700 REM
4705 SN$="YOU ENTER THE TUNNEL BUT IT IS TOO DARK TO FIND"
4710 SN$=SN$+" YOUR WAY.":GOSUB5500
4725 PRINT:INPUT"INSTRUCTIONS ";IS$
4730 GOSUB2500
4732 REM
4735 IFF=0THEN4725
4740 OP=P:GOSUB3000
4745 IFMF=1THENSN$="IT IS SO DARK THAT YOU CAN ONLY SEE":P=OP
4747 IFMF=1THENSN$=SN$+" THE TUNNEL ENTRANCE":GOSUB5500:MF=0
4748 IFMF=1THEN4725
4750 IFVF=1THEN4725
4755 IFVB$="RETREAT"ANDP=4THENMF=1:P=6:RETURN
4760 IFVB$="RETREAT"ANDP=4THENMF=1:P=9:RETURN
4762 IFVB$<>"USE"ANDVB$<>"LIGHT"THENSN$="I DON'T UNDERSTAND"
4765 IFVB$<>"USE"ANDVB$<>"LIGHT"THENGOSUB5500:GOTO4725
4780 REM
4790 GOSUB5300
4795 OV=F:GOSUB5450
4797 IFF=0THENSN$="THERE IS NO "+W$:GOSUB5500:GOTO4725
4800 IFHF=0THENSN$="YOU DON'T HAVE THE "+IV$(F,1):GOSUB5500
4801 IFHF=0THEN4725
4815 IFF<>2THENSN$="THE "+IV$(F,1)+" IS NO USE":GOSUB5500
4816 IFF<>2THEN4725
4840 SN$="YOU USE THE LAMP TO LIGHT YOUR WAY THROUGH THE TUNNEL"
4845 SN$=SN$+" AND EVENTUALLY EMERGE FROM THE EXIT.":GOSUB5500
4850 IFP=1THENMF=1:P=4:RETURN
4855 IFP=4THENMF=1:P=1:RETURN
4870 REM
4875 SF=1
4880 SN$="YOU START TO SINK INTO THE SWAMP.":GOSUB5500
4885 PRINT:INPUT"INSTRUCTIONS ";IS$
4890 GOSUB2500
4895 IFF=0THEN4885
4900 GOSUB3000
4910 IFVB$="LOOK"THENGOSUB2000:GOTO4885
4915 IFVB$="DROP"THENIV$(F,2)="-2"
4917 IFVF=1THEN4885
4920 REM
4925 IFVB$<>"SWIM"THENSN$="I DON'T UNDERSTAND":GOSUB5500
4926 IFVB$<>"SWIM"THEN4885
4930 F=0
4935 FORI=1TO2
4940 IFIC$(I)<>""THENF=F+1
4950 NEXT
4955 IFF<2THENGOSUB5040:RETURN
4960 GOSUB5000:RETURN
5000 REM
5010 SN$="THE OBJECTS ARE WEIGHING YOU DOWN AND YOU ARE SINKING
5011 GOSUB5500
5012 PRINT:INPUT"INSTRUCTIONS ";IS$
5015 GOSUB2500
5020 IFVB$<>"DROP"THENGOSUB5080
5025 GOSUB3900:IV$(F,2)="-2"
5030 IFHF=0ORF=0THEN5080
5040 SN$="YOU CAN NOW SWIM THROUGH THE SWAMP. WHICH WAY WILL
5041 SN$=SN$+" YOU GO ?":GOSUB5500
5050 EX$(2)="00080605":GOSUB2300
5055 PRINT:INPUT"INSTRUCTIONS ";IS$
5060 GOSUB2500
5062 IFF=0THEN5055
5065 GOSUB3500
5067 EX$(2)="00000000"
5070 RETURN
5075 REM
5080 REM
5085 SN$="YOU SINK INTO THE SWAMP AND DROWN":GOSUB5500:GOTO7000
5100 REM
5102 SF=1
5105 SN$="THE VILLAGE IS SURROUNDED BY A TALL WALL.":GOSUB5500
5106 IFGF<>0THENGOSUB5190
5107 SN$="A GUARD IS AT THE ENTRANCE GATE TO THE VILLAGE
5108 GOSUB5500
5115 PRINT:INPUT"INSTRUCTIONS ";IS$
5120 GOSUB2500:IFF=0THEN5115
5125 GOSUB3000
5130 IFVB$="LOOK"THENGOSUB2000
5135 IFVB$="GO"ANDMF=1THENRETURN
5140 IFMF=1THEN5115
5145 IFVB$<>"KILL"THENSN$="I DON'T UNDERSTAND":GOSUB5500:GOTO5115
5150 REM
5155 SN$="WHAT WILL YOU USE TO KILL THE GUARD ?":GOSUB5500
5160 SN$="ENTER OBJECT OR <I> FOR INSTRUCTION":GOSUB5500
5162 INPUTIS$:IFIS$="I"THEN5115
5165 GOSUB2500
5167 IFF=0THEN5160
5170 GOSUB5300:IFF=0THENSN$="THERE IS NO "+W$:GOSUB5500:GOTO5160
5172 OV=F:GOSUB5450
5174 IFHF=0THENSN$="YOU DON'T HAVE THE "+IV$(F,1):GOSUB5500
5175 IFHF=0THEN5160
5176 SN$="YOU KILL THE GUARD":GOSUB5500:GF=1
5185 REM
5190 REM
5195 SN$="YOU MOVE FORWARD AND TRY TO OPEN THE GATE TO THE VILL
5200 SN$=SN$+"AGE BUT THE GATE IS LOCKED AND WILL NOT MOVE
5201 GOSUB5500
5205 PRINT:INPUT"INSTRUCTIONS ";IS$
5210 GOSUB2500:IFF=0THEN5205
5215 GOSUB3000
5220 IFVB$="LOOK"THENGOSUB2000
5225 IFVB$="GO"ANDMF=1THENRETURN
5230 IFVF=1THEN5205
5232 IFVB$="USE"THEN5240
5234 IFVB$="UNLOCK"THENSN$="HOW ?":GOSUB5500:GOTO5205
5235 SN$="I DON'T UNDERSTAND":GOSUB5500:GOTO5205
5240 GOSUB5300
5242 OV=F:GOSUB5450
5244 IFF=0THENSN$="THERE IS NO "+W$:GOSUB5500:GOTO5205
5246 IFHF=0THENSN$="YOU DON'T HAVE THE "+IV$(F,1):GOSUB5500
5247 IFHF=0THEN5205
5248 IFF<>3THENSN$="THE "+IV$(F,1)+" IS NO USE":GOSUB5500
5249 IFF<>3THEN5205
5250 SN$="YOU UNLOCK THE GATE AND, DISGUISING YOURSELF IN THE
5252 SN$=SN$+" DEAD GUARD'S CLOTHES, WALK UNNOTICED THROUGH THE
5254 SN$=SN$+" VILLAGE AND THE SAFETY OF THE OUTSIDE WORLD.
5270 GOSUB5500:GOTO7000
5300 REM
5310 NN$=NN$+" ":LN=LEN(NN$):C=1:F=0
5315 FORK=1TOLN
5320 IFMID$(NN$,K,1)<>" "THENNEXTK:RETURN
5325 W$=MID$(NN$,C,K-C):C=K+1
5330 LW=LEN(W$)
5335 FORJ=1TO3
5340 LI=LEN(IV$(J,1))
5350 FORI=1TOLI-LW+1
5360 IFMID$(IV$(J,1),I,LW)=W$THENF=J:I=LI:J=3:K=LN
5370 NEXTI,J,K:RETURN
5450 REM
5460 HF=0
5470 IFIV$(OV,2)="-1"THENHF=1
5480 RETURN
5500 REM
5510 LC=0
5520 OC=1
5530 LL=32
5540 OW$=""
5550 SN$=SN$+"  DUMMY"
5560 PRINT
5570 FORC=1TOLEN(SN$)
5580 LC=LC+1
5590 IFMID$(SN$,C,1)=" "THENGOSUB5800
5600 NEXTC
5610 PRINT:RETURN
5800 REM
5810 NW$=MID$(SN$,OC,C-OC+1)
5820 IFLC<LLTHENPRINTOW$;:GOTO5840
5830 PRINTOW$:LC=LEN(NW$)
5840 OC=C+1:OW$=NW$
5850 RETURN
6000 DIMIV$(3,2),IC$(2)
6020 FORC=1TO3:READIV$(C,1),IV$(C,2):NEXT
6030 FORC=1TO10:READLN$(C),EX$(C):CC=CC+VAL(EX$(C)):NEXT
6100 READCD:IFCD<>CCTHENPRINT"CHECKSUM ERROR":STOP
6110 RETURN
6130 DATAGUN,10,LAMP,9,KEY,5
6132 DATANEAR A TUNNEL ENTRANCE,00000900
6134 DATAIN A SWAMP,00000000
6136 DATAIN A VILLAGE,07000000
6138 DATANEAR A TUNNEL ENTRANCE,05060000
6140 DATAON A PATH,00020400
6142 DATAON A PATH,02070004
6144 DATAON A PATH,08000306
6146 DATAON A PATH,09000702
6148 DATAON A PATH,01100800
6150 DATAIN A CLEARING,00000009
6152 DATA32270015
7000 PRINT:PRINT:PRINT"PLAY AGAIN (Y/N) ??"
7010 A$=INKEY$:IFA$="Y"THENRUN
7020 IFINKEY$="N"THENEND
7021 GOTO7010
8000 CLS0
8001 PRINT"ίίίίίίίίίίίίίίίίίίίίίίίίίίίίίίίί";
8002 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
8003 PRINT"€€€€€€€€€€€€€€€…€€€€€€€€€€€€€€";
8004 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
8005 PRINT"€€€€€€€€€€€€€€…€€€€€€€€€€€€€";
8006 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€";
8007 PRINT"€€€€€€€€€€€€€…€€€€€€€€€€€€";
8008 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€";
8009 PRINT"€€€€€€€€€€€€…€€€€€€€€€€€";
8010 PRINT"€€€€€€€€€€€€€€€€€€€€€€€";
8011 PRINT"€€€€€€€€€€€…€€€€€€€€€€";
8012 PRINT"€€€€€€€€€€€€€€€€€€€€€";
8013 PRINT"€€€€€€€€€€…€€€€€€€€€";
8014 PRINT"€€€€€€€€€€€€€€€€€€€";
8015 PRINT"€€€€€€€€€…€€€€€€€€";
8016 PRINT"€€€€€€€€€€€€€€€€";
8020 CC=224:GOSUB8032:CC=280:GOSUB8032
8022 CC=32*1+6:GOSUB8032:CC=32*1+20:GOSUB8032:GOTO8052
8032 PRINT@CC+00, "€€‚€";
8034 PRINT@CC+32, "€‚";
8036 PRINT@CC+64, "€";
8038 PRINT@CC+96, "€€€Ο€€";:RETURN
8052 PRINT@236,"γγγγγγγγγ";
8054 PRINT@268,"κhauntedε";
8056 PRINT@300,"κ€forestε";
8058 PRINT@332,"μμμμμμμμμ";
8060 FORI=0TO29:PRINT@32*5,"€€€€€€€€€€€€€€€€€€€€€€€€€€€";:PRINT@32*6,"€€€€€€€€€€€€€…€€€€€€€€€€€€";
8064 PRINT@32*5+I,"ΑΝΙ";:PRINT@32*6+I,"ΕΝΝ";:READS,D:IFS=0THENFORZZ=1TO50:NEXT:GOTO8068
8065 SOUNDS,D
8068 NEXT
8070 PRINT@416,;:M$="AUTHOR UNKNOWN, ADAPTED AND EX- ":GOSUB7
8072 PRINT@448,;:M$="TENDED BY UWE SCHLENTHER, AALEN ":GOSUB7
8074 PRINT@480,;:M$=" *** PRESS ANY KEY TO START ***":GOSUB7:POKEMC+511,32
8076 A$=INKEY$:IFA$=""GOTO8076
8078 RETURN
9000 REM MC-10 EDITS JIM GERRIE
