0 CLEAR800:GOTO10
8 IFMID$(M$(C1+2),C2,1)="O"THENRETURN
9 M$=LEFT$(M$(C1+2),C2+8)+I$+MID$(M$(C1+2),C2+10):M$(C1+2)=M$:RETURN
10 CLS:PRINT@40,"zector adventure":POKE16384+46,32:PRINT:PRINTTAB(7)"YOUR CODE IS D$8."
12 PRINT"  YOU HAVE JUST BEEN PULLED IN  TO THE ENEMY SHIP BY A TRACTOR"
13 PRINT"BEAM. YOU MUST FIND THE SHIP'S  BATTLE PLANS AND SOMEHOW RETURN THEM TO THE REBEL FLEET."
20 PRINT"   ALL THEY TELL YOU IS THAT YOUWILL HAVE TO USE THE CODE AT    SOME POINT. YOU ARE ALSO"
21 PRINT"EQUIPPED WITH A BLASTER. YOU    HAVE 24 HOURS. GOOD LUCK!";:PRINT
30 FORZ3=1TO2:SOUND180,3:SOUND180,3:SOUND154,3:SOUND154,3:SOUND102,3:SOUND102,3:SOUND102,3
31 SOUND102,3:SOUND102,3:SOUND102,3:SOUND119,3:SOUND119,3:SOUND127,3:SOUND127,3:SOUND127,3:SOUND127,3
32 SOUND73,3:SOUND73,3:SOUND73,3:SOUND73,3:SOUND102,3:SOUND102,3:SOUND102,6:SOUND127,6:SOUND154,6:SOUND166,6:SOUND180,6
33 NEXTZ3
40 REM ROOM STRINGS
50 REM HIRES TITLE
60 DIML$(24),N$(24),S$(24),E$(24),W$(24),O$(35),P(35),L1(35),I(35),D$(35),M$(15):I(4)=1:N5=1:I8=130:I9=40
70 C1=INT(I9/16):C2=INT(I8/16):I$="O":GOSUB8:REM DRAW ROOM
80 L=1:FORZ=1TO24:READL$(Z),N$(Z),S$(Z),E$(Z),W$(Z):NEXT:GOSUB3000
90 FORZ=1TO29:READO$(Z),L1(Z),P(Z),D$(Z):NEXT:O$(35)="SUIT FOR SPACE WALKS":P(35)=1:L1(35)=16
91 D$(35)="LOOKS LIKE IT IS IN USEABLE     CONDITION"
100 PRINT"    PRESS <ENTER> TO BEGIN";:INPUTZ1$
110 CLS:PRINT "LOCATION: ";L$(L)
120 C1=INT(I9/16):C2=INT(I8/16):I$="O":GOSUB8:PRINT"YOU SEE: "
130 C=0:Z1=1:FORZ=1TO35
140 IFL1(Z)=LANDI(Z)=0THENPRINT@Z1*32+9,O$(Z):Z1=Z1+1:C=1
150 NEXT:IFC=0THENPRINT@Z1*32+9,"NOTHING OF INTEREST"
160 PRINT"EXITS: ";
170 C=0:IFN$(L)<>"0"THENPRINT"NORTH ";:C=C+1:C1=INT((I9-16)/16):C2=INT(I8/16):I$="!":GOSUB8
180 IFS$(L)<>"0"THENPRINT"SOUTH ";:C=C+1:C1=INT((I9+16)/16):C2=INT(I8/16):I$="!":GOSUB8
190 IFE$(L)<>"0"THENPRINT"EAST ";:C=C+1:C1=INT(I9/16):C2=INT((I8+16)/16):I$="-":GOSUB8
200 IFW$(L)<>"0"THENPRINT"WEST";:C=C+1:C1=INT(I9/16):C2=INT((I8-16)/16):I$="-":GOSUB8
210 IFC=0THENPRINT" ???";
220 PRINT:PRINT"================================";
230 IFM5>1440THENPRINT:PRINT"YOU RAN OUT OF TIME!":GOTO1540
240 IFL=6ANDI(35)=1ANDG=1ANDI(21)=1THEN245
241 GOTO250
245 PRINT"THE AIR LOCK WAS OPEN!":PRINT"YOU FLOAT SAFELY UNTIL A REBEL  SHIP PICKS YOU UP.":PRINT
246 PRINT"     YOU HAVE SOLVED THE...     ":PRINT"********************************":FORZ=1TO4500:NEXT:GOTO1590
250 IFL=6ANDI(35)=1ANDG=1ANDI(21)=0THENPRINT"YOU MADE IT OUT BUT YOU FORGOT  THE PLANS STUPID!":GOTO1540
260 IFL=6ANDG=1THENPRINT"THE AIR LOCK WAS OPEN!":PRINT"YOU WERE SUFFOCATED...":GOTO1540
270 M5=M5+1:PRINT"NOW WHAT ";:INPUTC$
280 IFC$="N"THENC$="GO NORTH"
290 IFC$="S"THENC$="GO SOUTH"
300 IFC$="E"THENC$="GO EAST"
310 IFC$="W"THENC$="GO WEST"
320 IFC$="GO NORTH"ANDN$(L)<>"0"THENL=VAL(N$(L)):I9=I9-32:GOTO110
330 IFC$="GO SOUTH"ANDS$(L)<>"0"THENL=VAL(S$(L)):I9=I9+32:GOTO110
340 IFC$="GO EAST"ANDE$(L)<>"0"THENL=VAL(E$(L)):I8=I8+32:GOTO110
350 IFC$="GO WEST"ANDW$(L)<>"0"THENL=VAL(W$(L)):I8=I8-32:GOTO110
360 IFLEN(C$)<3THENPRINT"? WHAT ?":GOTO270
370 IFC$="LOO"ORC$="LOOK"THEN110
380 N$=LEFT$(C$,3):FORA=(LEN(C$)-2)TO1STEP-1:IFMID$(C$,A,1)=" "THEN390
381 NEXTA
390 O$=MID$(C$,A+1,3)
400 IFN$="INV"THEN1070
410 IFN$="TIM"ORN$="SCO"THENPRINT"TIME= :";STR$(M5);" MIN.":GOTO270
420 IFN$="GET"ORN$="TAK"ORN$="STE"THEN1110
430 IFN$="DRO"ORN$="LEA"ORN$="PUT"THEN1170
440 IFN$="PUS"ORN$="PRE"ORN$="MOV"THENGOSUB1260
450 IFN$="KIL"ORN$="SHO"ORN$="BLA"THEN1290
460 IFN$="FIR"ORN$="LAU"THEN1560
470 IFN$="MAP"THEN485
480 IFN$<>"MAP"THEN490
481 PRINT" O.K.":GOTO270
485 CLS0:POKE49151,64:FORC1=0TO15:PRINT@32*C1,M$(C1);:NEXT
486 C1=INT(I9/16):C2=INT(I8/16):PRINT@(C1+2)*32+C2+8,"o";
487 IFINKEY$=""THEN487
488 C$="LOO":CLS0:POKE49151,0:CLS:GOTO110
490 IFN$="TOS"ORN$="THR"THENGOSUB1320
500 IFN$="TYP"ORN$="INP"ORN$="ENT"ORN$="PRI"THEN1480
510 IFN$="GIV"THEN1380
520 IFN$="LOO"ORN$="EXA"ORN$="REA"THEN1430
530 PRINT" YOU CAN'T DO THAT!":GOTO270
540 DATA"SHIP DOCKING AREA",0,0,6,4
550 DATA"TERMINAL CONTROL ROOM",0,0,0,3
560 DATA"PROCESSING AREA",0,4,0,0
570 DATA"ESCAPE POD ROOM",0,0,1,0
580 DATA"COMMAND CENTER",0,7,4,0
590 DATA"WASTE DISPOSAL ROOM",0,0,0,1
600 DATA"A N/W CORRIDOR",0,12,0,0
610 DATA"A SMALL ROOM",0,0,9,0
620 DATA"BEHIND 'ZEEVIUS'",0,14,0,8
630 DATA"A N/W CORRIDOR",0,15,0,0
640 DATA"TURRET ROOM g",0,0,12,0
650 DATA"INTERSECTION g",7,17,13,11
660 DATA"MESS HALL",0,0,14,12
670 DATA"BAR AND ARCADE",0,0,15,13
680 DATA"INTERSECTION d",10,20,16,14
690 DATA"TURRET ROOM d",0,0,0,15
700 DATA"DEBRIEFING ROOM",12,0,18,0
710 DATA"ENGINE ROOM",0,21,19,17
720 DATA"ENGINE ROOM",0,0,20,18
730 DATA"BUNK ROOM",15,0,0,19
740 DATA"ARMORY",18,0,22,0
750 DATA"VIEWING WINDOW",0,24,0,21
760 DATA"LARGE TERMINAL ROOM",0,0,24,0
770 DATA"TRITINITE STEEL DOOR",22,0,0,0
780 DATA"MAINFRAME",3,0,"IT HAS DISK DRIVE SLOTS"
790 DATA"TERMINAL",2,0,"IT'S SCREEN READS--               COMMAND CENTER open OR closed"
800 DATA"SHIP",1,0,IT IS OUT OF FUEL
810 DATA"BLASTER",1,1,Z
820 DATA"GARBAGE",6,1,IT SMELLS
830 DATA"LASER RIFLE",21,1,IT HAS AN ALARM SYSTEM ON IT
840 DATA"SLOT IN WALL",4,0,Z
850 DATA"RED BUTTON",1,0,IT SAYS- OPEN
860 DATA"BLUE BUTTON",1,0,IT SAYS- CLOSE
870 DATA"CHAIRS",17,0,Z
880 DATA"BEDS",20,0,Z
890 DATA"QUARTER",9,1,Z
900 DATA"CONTROLS",5,0,Z
910 DATA"MEMBERS OF CREW",5,0,THEY ARE WORKING
920 DATA"GUNNER",11,0,HE IS SLEEPING
930 DATA"TORPEDO LAUNCHER",11,0,IT IS LOADED
940 DATA"TABLES",13,0,Z
950 DATA"COOK",13,0,HE LOOKS AT YOU AND TELLS YOU TOGET SOMETHING HE CAN MAKE DINNEROUT OF
960 DATA"VIDEO GAMES",14,0,THEY ARE NOT BEING PLAYED
970 DATA"COMPUTER ID TERMINAL",8,0,IT'S SCREEN SAYS -INPUT CODE ---
980 DATA"PLANS FOR BATTLE",5,1,THIS IS IT!
990 DATA"MACHINERY",18,0,HAS A LABLE THAT SAYS MADE BY-  DARRELL ULM - ADVENTURE #1
1000 DATA"MACHINERY",19,0,Z
1010 DATA"CREW MEMBER",20,0,HE HAS A PIECE OF METAL
1020 DATA"GRENADE",21,1,Z
1030 DATA"REBEL FLEET IN SPACE",22,0,Z
1040 DATA"TRITINITE DOOR",24,0,IT HAS A CIRCLULAR INDENTATION
1050 DATA"DISKS FLOPPY",23,1,Z
1060 DATA"DOOR (WOODED+LOCKED)",3,0,"A SIGN ON IT SAYS 'RESTRICTED'  AND IT LOOKS LIKE YOU COULD BLOWIT UP!"
1070 C=0:PRINT"YOU ARE CARRYING":PRINT:FORZ=1TO35
1080 IFI(Z)=1THENPRINT"> "+O$(Z):C=1:SOUND73,1:SOUND93,1:SOUND102,1:SOUND119,1:SOUND134,1:SOUND141,1:SOUND154,1
1090 NEXT:IFC=0THENPRINT"** NOTHING **"
1100 GOTO270
1105 SOUND154,1:SOUND141,1:SOUND102,1:SOUND93,1:SOUND73,1:FORC1=1TO5:SOUND119,1:SOUND134,1:NEXT:RETURN
1110 FORZ=1TO35
1120 K$=LEFT$(O$(Z),3)
1130 IFO$=K$ANDL1(Z)=LANDP(Z)=1THENGOSUB1230:I(Z)=1:N5=N5+1:PRINT"TAKEN":GOSUB1105:GOTO1150 
1131 IFO$=K$ANDL1(Z)=LANDP(Z)=0THENPRINT"YOU CAN'T":GOTO270
1140 NEXT:PRINT"I DON'T UNDERSTAND THE NOUN":GOTO270
1150 IFO$="LAS"THEN1155
1151 GOTO1160
1155 PRINT" BUT AS SOON AS YOU PICK IT UP  AN ALARM GOES OFF AND YOU ARE   CAPTURED BY THE GUARDS!"
1156 FORC2=1TO3:FORC1=1TO7:SOUND119,3:NEXT:FORC1=1TO7:SOUND102,3:NEXT:NEXT:SOUND102,3:GOTO1540
1160 GOTO270
1170 FORZ=1TO35
1180 K$=LEFT$(O$(Z),3):IFO$=K$ANDI(Z)=1THEN1185
1181 IFO$=K$THENPRINT"YOU DON'T HAVE THAT ITEM":GOTO270
1182 GOTO1190
1185 L1(Z)=L:I(Z)=0:PRINT" DROPPED":SOUND134,1:SOUND141,1:SOUND154,1:SOUND102,1:SOUND93,1:SOUND73,1:N5=N5-1:GOTO1200
1190 NEXT:PRINT"I DON'T UNDERSTAND THE NOUN":GOTO270
1200 IFO$="QUA"ANDL=14THEN1205
1201 GOTO1210
1205 O$(Z)="'ZEEVIUS' IS ON":PRINT"THE LIGHTS IN THE ARCADE COME ON!!!":PRINT:P(Z)=0:N$(14)="9"
1206 PRINT"BECAUSE THE GAME WAS TURNED ON"
1207 PRINT"IT LIGHTED UP THE ROOM SHOWING  YOU A NITCH BEHIND THE GAME"
1208 SOUND102,1:SOUND102,1:SOUND154,1:SOUND154,1:FORC1=1TO6:SOUND102,1:NEXT:FORC1=1TO2:SOUND119,1:NEXT
1209 SOUND127,1:SOUND102,1:GOTO270
1210 IFO$="CAR"ANDL=4THEN1215
1211 GOTO270
1215 PRINT" THE CARD IS GIVEN BACK TO YOU  AND A DOOR ON THE NORTH WALL IS OPENED.":N$(4)="3":I(Z)=1
1216 SOUND73,1:SOUND83,1:SOUND93,1:SOUND102,1:SOUND111,1:SOUND119,1:SOUND127,1
1217 SOUND134,1:SOUND141,1:SOUND148,1:SOUND50,1:SOUND160,1:GOTO270
1230 IFN5+1>4THENPRINT"YOU ARE TOO LOADED DOWN":GOTO270
1231 RETURN
1235 Q=PEEK(9)AND128:POKE49151,0+Q:POKE49151,128-Q+0:RETURN
1240 FORZ=1TO35:IFL1(Z)=6ANDI(Z)=0THENL1(Z)=22:P(Z)=0:O$=O$(Z)+" IN SPACE":O$(Z)=O$
1250 NEXT:RETURN
1260 IFO$="BLU"ANDL=1THENPRINT"///CLANK\\\":SOUND200,1:SOUND255,1:S$(6)="10":N$(10)="6":G=0:GOTO270
1270 IFO$="RED"ANDL=1THENG=1:PRINT"///WHOOSH\\\":FORC1=1TO5:GOSUB1235:NEXT:GOSUB1240:S$(6)="0":N$(10)="0":GOTO270
1280 PRINT"IT HAD NO EFFECT":GOTO270
1290 IFL=20ANDO$="MEM"ANDI(4)=1THEN1295
1291 GOTO1300
1295 O$(24)="DEAD CREW MEMBER":O$(30)="STEEL DISK":L1(30)=20:P(30)=1:PRINT" ZAP....SIZZLE...."
1296 D$(24)="HE IS DEAD":FORC1=1TO10:GOSUB1235:NEXT:D$(30)="Z":GOTO270
1300 IFI(4)=0THENPRINT"YOU DON'T HAVE ANYTHING TO KILL HIM WITH!":GOTO270
1310 PRINT" ZAP...IT HAD NO EFFECT":GOTO270
1320 FORZ=1TO35
1330 IFO$=LEFT$(O$(Z),3)AND I(Z)=1 THEN I(Z)=0:L1(Z)=L:N5=N5-1:GOTO 1350
1340 NEXT:PRINT"YOU DON'T HAVE SUCH AN ITEM":GOTO270
1350 IFL=24ANDO$="STE"THEN1355
1351 GOTO1360
1355 PRINT" THE DISK STICKS TO THE DOOR AND THE DOOR OPENS"
1356 FORC1=1TO8:SOUND102,1:NEXT:FORC1=1TO6:SOUND119,1:NEXT
1357 FORC1=1TO2:SOUND206,1:SOUND196,1:SOUND200,1:NEXT:FORC1=1TO50:SOUND166,1:SOUND180,1:SOUND196,1:FORC1=1TO5:SOUND206,1
1358 W$(24)="23":E$(23)="24":P(30)=0:O$(27)="OPEN TRITINITE DOOR":GOTO270
1360 IFL=3ANDO$="GRE"THEN1365
1361 GOTO1370
1365 PRINT"THE DOOR BLOWS OPEN AND YOU SEE A COMPUTER ROOM TO THE EAST."
1366 SOUND206,4:SOUND200,4:SOUND196,4:SOUND189,4:SOUND176,4:SOUND166,4
1367 FORC2=1TO2500:NEXT:FORC1=1TO6:SOUND73,4:SOUND119,4:SOUND134,4:SOUND141,4:NEXT
1368 E$(3)="2":O$(25)="SHRAPNEL":P(25)=0:O$(29)="DOOR OPEN":GOTO270
1370 PRINT"IT HAS NO EFFECT":GOTO270
1380 FORZ=1TO35
1390 IFO$=LEFT$(O$(Z),3)AND I(Z)=1 THEN I(Z)=0:L1(Z)=L:N5=N5-1:GOTO1410
1400 NEXT:PRINT"YOU DON'T HAVE SUCH AN ITEM":GOTO270
1410 IFL=13ANDO$="GAR"THEN1415
1411 GOTO1420
1415 PRINT"THE COOK THANKS YOU AND GIVES   YOU A QUARTER"
1416 C2=102:GOSUB1475:C2=134:GOSUB1475:C2=154:GOSUB1475:C2=180:GOSUB1475
1417 P(Z)=0:I(12)=1:GOTO270
1420 PRINT"IT HAD NO EFFECT":GOTO270
1430 FORZ=1TO35
1440 IFO$=LEFT$(O$(Z),3)ANDL1(Z)=LTHEN1460
1450 NEXT:PRINT"THAT ITEM ISN'T AROUND":GOTO270
1460 IFD$(Z)="Z"THENPRINT"YOU SEE NOTHING OF INTEREST":GOTO270
1461 PRINTD$(Z):GOTO270
1475 FORC1=1TO5:SOUNDC2,2:NEXT:RETURN
1471 FORC1=1TO10:SOUND102,4:FORC2=1TO50:NEXT:NEXT:RETURN
1480 IFL=8ANDO$="D$8"THEN1485
1481 GOTO1490
1485 PRINT" ENTRY CODE ACCEPTED":PRINT"  ID CARD NOW VALIDATED"
1486 FORC1=1TO10:SOUND73,1:FORC2=1TO50:NEXT:NEXT
1487 O$(32)="CARD (ID)":L1(32)=8:P(32)=1:D$(32)="IT READS- FOR MAIN COMPUTER ROOM  ONLY":GOTO270
1490 IFL1(28)=3ANDL=2ANDO$="OPE"THEN1495
1491 GOTO1500
1495 W$(4)="5":N$(7)="5":PRINT" ENTRY ACCEPTED COMMAND CENTER  NOW OPEN"
1496 FORC1=1TO10:SOUND102,1:FORC2=1TO50:NEXT:NEXT:GOTO270
1500 IFL=2ANDO$="OPE"THENPRINT"PROCESSING ROOM NEEDS COMPUTER  DISKS":GOTO270
1510 IFL=2ANDO$="CLO"THENPRINT"PROCESSING ROOM NEEDS COMPUTER  DISKS":GOTO270
1520 IFL1(28)=3ANDL=2ANDO$="CLO"THENW$(4)="0":N$(7)="0":PRINT" ENTRY ACCEPTED COMMAND CENTER  NOW CLOSED":GOTO270
1530 PRINT" WRONG CODE TRY AGAIN":GOTO270
1540 FORZ=1TO4500:NEXTZ:CLS4:PRINT@40,"zector adventure";:POKE16384+46,32:PRINT@135,"MISSION INCOMPLETE";
1545 PRINT@32*11,"---MOVES=";M5;"---":PRINT@483,"PRESS ANY KEY TO TRY AGAIN";
1550 SOUND102,3:SOUND127,3:SOUND154,3:SOUND83,3:SOUND180,3:IFINKEY$=""THEN1550
1551 RUN
1560 IFO$="GUN"ORO$="CAN"ORO$="LAS"THEN1570
1561 PRINT"FIRE WHAT? YOU DON'T KNOW HOW":GOTO270
1570 IFL=11ORL=16THEN1580
1571 PRINT"I DON'T SEE ONE AROUND HERE.":GOTO270
1580 PRINT"THE SHOT ALERTS THE GUARDS.           YOU ARE CAUGHT!!"
1581 FORC1=1TO4:SOUND73,1:SOUND93,1:SOUND102,1:SOUND119,1:NEXT:GOTO1540
1590 CLS0:PRINT@32*3,;
1600 PRINT"¼¾¸º€º¾¼¸€€€¼¼º¾¼¸¶¼²¼¾¸¶¼²¾¼²°€";
1601 PRINT"€º€»³º»³€€€€€¶€»³€º€€€º€º€º»³¸€€";
1602 PRINT"€º€º€ºº€€€€€¶€€º€€º€²€º€º€ºº¹€€€";
1603 PRINT"€¸€¸€¸¼¼¸€€€¼¼¸¼¼¸´¼€€¸€´¼€¸€¸€€";
1604 PRINT"±¹€½¼²º€º¾¼¸»€º¼¾¸º€º¾¼²¾¼¸°°°°€";
1605 PRINT"º€ºµ€º¹±¸»³€º¹º€º€º€º»³¸»³€°°°°°";
1606 PRINT"¾¼ºµ€º´¶€º€€º€º€º€º€ºº¹€º€€°°°€€";
1607 PRINT"¸€¸¼¼€€¸€¼¼¸¸€¸€¸€´¼€¸€¸¼¼¸°°°€€";:GOTO1545
3000 M$(0)="€€Â€€€€€ÀÈ€ñóóóóóóóó€€€ÁÀÄ€ÀÁÀ€È"
3002 M$(1)="Â€€€ÄÀÁ€€óüø€€€€€€€€üùò€€€€€€v€€"
3003 M$(2)="À€ÄÀ€€€€ö€€€€€€€€€€€€€ôòÀ€€€È€ÀÀ"
3004 M$(3)="€€Â€€À€ö€€€€€€€€€€€€€€€ôò€È€€€ÁÀ"
3005 M$(4)="€€€€€Èõ€€€€€€€€€€€€€€€€€õ€€ÁvÀ€€"
3006 M$(5)="Â€€€€€ú€€€€€€€€€€€€€€€€€€ú€€€€€À"
3007 M$(6)="€€€Â€ñø€€€€€€€€€€€€€€€€€€ù€€ÁÀ€€"
3008 M$(7)="€€€€€õ€€€€€€€€€€€€€€€€€€€õ€€€€€€"
3009 M$(8)="Â€€€€õ€€€€€€€€€€€€€€€€€€€õ€Â€€€È"
3010 M$(9)="€€€€Âõ€€€€€€€€€€€€€€€€€€€õ€€€ÁÀ€"
3011 M$(10)="€Â€€€€ú€€€€€€€€€€€€€€€€€€ú€€Â€€€"
3012 M$(11)="€€€€€€ù€€€€€€€€€€€€€€€€€ñø€€ÁÀ€€"
3013 M$(12)="€€€Â€Âôò€€€€€€€€€€€€€€€€ö€À€ÀÀ€Á"
3014 M$(13)="€€€€€€€ôò€€€€€€€€€€€€€€ö€Â€€€Â€€"
3015 M$(14)="€Àh€€È€€ôó€€€€€€€€€€€ñö€À€€€€Á€À"
3016 M$(15)="€Ä€€€€€Â€Áüùóóóóóóóóüø€€ÈÂ€À€€":RETURN
4000 REM COPYRIGHT (C) T&D SOFTWARE 1984
4010 REM MC-10 EDITS JIM GERRIE
