1 REM *** RAINBOW ADVENTURE ***
10 CLEAR 1000
20 CLS
30 DIMRM$(85),OB$(85),OB(86),D(85,6),D$(6),F(10):MX=5:CA=0
40 FORX=1TO6:READD$(X):NEXTX
50 X=0
60 X=X+1:READ RM$(X):IF RM$(X)="END" THEN 80
70 FORY=1TO6:READD(X,Y):NEXTY:GOTO60
80 X=0
90 X=X+1:READOB$(X):IFOB$(X)="END"THENNO=X-1:GOTO100
91 READI$,OB(X):OB$=OB$+I$:GOTO90
100 X=0
110 X=X+1:READI$:IFI$="END"THEN120
111 VB$=VB$+LEFT$(I$,3):GOTO110
120 CLS
130 YOU=1
140 PRINT"I AM ";
150 PRINT RM$(YOU)
160 PRINT"I CAN SEE: ":F=0
170 FORI=1TONO
180 IFINT(OB(I))<>INT(YOU) THEN 200
190 PRINT"  "OB$(I):F=1
200 NEXT:IFF=0THENPRINT" NOTHING INTERESTING"
210 PRINT"YOU CAN GO: "
220 FORI=1TO6
230 IFD(YOU,I)<>0THENPRINT" *"D$(I);
240 NEXT
250 PRINT
260 PRINT"================================";
270 INPUT"WHAT SHOULD I DO";I$
280 IFI$=""THEN270
281 IFI$="LOOK"THENCLS:GOTO140
282 IFI$="QUIT"THENEND
284 IFI$="SAVE"THEN7010
286 IFI$="LOAD"THEN7070
290 IFLEFT$(I$,2)="GO"THENPRINT"USE SINGLE LETTERS TO INDICATE  DIRECTION (EXAMPLE, N=NORTH)":GOTO260
300 IFLEFT$(I$,3)<>"INV"THEN 360
310 PRINT"I AM CARRYING:":F=0
320 FOR I=1TO NO
330 IF OB(I)=-1THEN PRINT OB$(I):F=1
340 NEXT:IF F=0 THEN PRINT"NOTHING"
350 GOTO 260
360 IF LEFT$(I$,5)<>"SCORE"THEN 430
370 T=0:Y=0
380 FOR I=1 TO NO
390 FOR L=1TO LEN(OB$(I))
400 IF MID$(OB$(I),L,1)="*"THEN T=T+1:IF OB(I)=YOU OR OB(I)=-1THEN Y=Y+1
410 NEXT:NEXT
420 PRINT"OUT OF";T;"POINTS YOU HAVE";Y:GOTO 260
430 IF LEN(I$)=1 THEN 710
440 IF I$="HELP" THEN 800
450 SP=0:FORZZ=1TOLEN(I$):SP=SP-(MID(I$,ZZ,1)=" "):NEXT:IFSP=0THENPRINT"TRY USING TWO WORD COMMANDS.":GOTO260
460 V$=LEFT$(I$,SP-1):O$=MID$(I$,SP+1)
470 A$=LEFT$(V$,3):B$=LEFT$(O$,3)
500 IF A$="TAK" THEN A$="GET"
501 IF A$="LEA" THEN A$="DRO"
502 IF A$="LOO" THEN A$="EXA"
503 IF A$="MOV" THEN A$="PUS"
504 IF A$="TEA" THEN A$="BRE"
600 CC=0:FORZZ=1TO64:IFMID(VB$,ZZ,LEN(A$))=A$THENCC=ZZ:ZZ=64:NEXT:GOTO602
601 NEXT
602 V=(CC+2)/3:IFV<1THENCLS:PRINT"SORRY, I JUST DON'T KNOW HOW TO":PRINT V$" ANYTHING.":GOTO 140
610 CC=0:FORZZ=1TO64:IFMID(OB$,ZZ,LEN(B$))=B$THENCC=ZZ:ZZ=64:NEXT:GOTO612
611 NEXT
612 O=(CC+2)/3:IFO<1THENCLS:PRINT"SORRY, I JUST DON'T SEE ANY ":PRINT O$" HERE.":GOTO140
700 IFLEN(I$)>1THEN1000
710 V=0:CLS:IF I$="N"AND D(YOU,1)<>0 THEN YOU=D(YOU,1):GOTO 140
720 IF I$="S"AND D(YOU,2)<>.THEN YOU=D(YOU,2):GOTO 140
730 IF I$="E"AND D(YOU,3)<>.THEN YOU=D(YOU,3):GOTO 140
740 IF I$="W"AND D(YOU,4)<>.THEN YOU=D(YOU,4):GOTO 140
750 IF I$="U"AND D(YOU,5)<>.THEN YOU=D(YOU,5):GOTO 140
760 IF I$="D"AND D(YOU,6)<>.THEN YOU=D(YOU,6):GOTO 140
770 GOTO 1000
800 REM ********************
810 REM *** HELP SECTION ***
820 REM ********************
825 H=H+1
826 REM Only allow 5 hints
830 IF H=>6 THEN835
831 GOTO840
835 ?"THAT'S ENOUGH HELP FOR NOW. YOU ARE SUPPOSED TO USE YOUR BRAIN, NOT ASK FOR HELP EVERY TIME YOU ARE IN TROUBLE.":GOTO260
840 IF YOU=4 OR YOU=6 THEN PRINT"I'M PUZZLED TOO!":GOTO260
850 IF YOU=2 AND F(3)=1 THEN PRINT"MAYBE YOU NEED SOME MONEY.":GOTO260
855 IF YOU=2 AND F(3)=0 THEN PRINT"THE BUS COMPANY HAS DISCONTINUEDTHEIR BUS SERVICE TO THIS AREA.":GOTO260
860 IF YOU=9 THEN PRINT"YOU BETTER LEARN HOW TO RUN THE COLOR COMPUTER.":GOTO260
870 IF YOU=10 THEN PRINT"LOOK IN THE MIRROR.":GOTO260
880 PRINT"TOUGH LUCK!  JUST KEEP TRYING!":GOTO260
1000 F=0:IFOB(O)=YOU THENF=1:GOTO1002
1001 IFOB(O)=-1THENF=2
1002 ONVGOTO1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200
1010 IF LEN(I$)=1 THENPRINT"I CAN'T GO THAT WAY!":GOTO 140
1020 PRINT"I DON'T UNDERSTAND WHAT YOU ARE TELLING ME.":GOTO260
1100 REM *** GET ***
1102 IF F=2 THEN PRINT"I ALREADY HAVE IT!":GOTO260
1104 IF F=0 THEN PRINT"I DON'T SEE THAT HERE.":GOTO260
1106 IF CA=>MX THEN PRINT"I CAN'T CARRY ANYTHING ELSE!":GOTO260
1110 REM Don't allow objects to be picked up.
1120 IF O=1 OR O=2 OR O=5  THEN PRINT"DON'T BE RIDICULOUS!":GOTO260
1199 OB(O)=-1:PRINT"OK, I GOT IT.":CA=CA+1:GOTO260
1200 REM *** DROP ***
1201 IFF=2THENPRINT"OK, I DROPPED IT.":CA=CA-1:OB(O)=YOU:GOTO1210
1202 PRINT"I DON'T HAVE IT."
1210 REM If mirror is dropped, break it, change object and delete it from object list
1220 IF O=7 THEN PRINT"THE MIRROR BROKE INTO A MILLION PIECES!":OB$(7)="BROKEN MIRROR":GOSUB8000:GOTO260
1299 GOTO 260
1300 REM *** OPEN ***
1301 IF F=0 THEN PRINT"I DON'T SEE IT HERE.":GOTO260
1310 REM If display case in the room and key not in inventory, do not open
1320 IF OB(5)=YOU AND OB(4)<>-1 THEN PRINT"SORRY, IT'S LOCKED":GOTO260
1330 REM if display case in the room and key in inventory, then open
1340 IF OB(5)=YOU AND OB(4)=-1 THEN PRINT"OK, IT'S OPEN":OB(6)=YOU:GOTO260
1345 REM If mail box in room then open it and bring Rainbow to the room
1350 IF OB(2)=YOU THEN PRINT"OK, IT'S OPEN.":OB(3)=YOU:GOTO260
1399 PRINT"SORRY, IT DOES NOT OPEN.":GOTO260
1400 REM *** CLOSE ***
1401 IF F=0 THEN PRINT"I CAN'T CLOSE ANYTHING THAT I   CAN'T SEE OR DON'T HAVE.":GOTO260
1499 PRINT"I TRIED, BUT IT DOESN'T CLOSE.":GOTO260
1500 REM *** EXAMINE ***
1501 IF F=0 THEN PRINT"I DON'T SEE ANYTHING LIKE THAT  AROUND HERE.":GOTO260
1509 REM If shinny object, then describe
1510 IF O=4 THEN PRINT"IT'S A KEY!":GOTO260
1515 REM If Rainbow, then describe
1520 IF O=3 THEN PRINT"IT'S A COLOR COMPUTER MAGAZINE.":GOTO260
1525 REM If mirror, then describe and set flag F(1)
1530 IF O=7 THEN PRINT"I SEE MYSELF ON THE MIRROR.":PRINT"I AM WEARING A WATCH.":F(1)=1:GOTO260
1599 PRINT"NOTHING SPECIAL ABOUT IT.":GOTO260
1600 REM *** PUSH ***
1699 PRINT"NOTHING HAPPENED.":GOTO260
1700 REM *** BREAK ***
1701 IF F=0 THEN PRINT"NOTHING LIKE THAT AROUND HERE   THAT I CAN BREAK.":GOTO260
1705 REM If display case, then break it and end game
1710 IFO=5THEN1715
1711 GOTO1799
1715 PRINT"YOU BROKE THE CASE AND YOU WERE SENT TO JAIL FOR YOUR ACTIONS."
1716 PRINT"SORRY, BUT YOU HAVE TO START    THE GAME OVER.":END
1799 PRINT"I TRIED, BUT I COULDN'T BREAK IT":GOTO260
1800 REM *** ENTER ***
1801 IF F=0 THEN PRINT"I DON'T SEE ANYTHING LIKE THAT  AROUND HERE.":GOTO260
1805 REM If bus but no money, then can't get in
1810 IF O=1 AND OB(9)<>-1 THEN PRINT"THE BUS DRIVER WON'T LET ME IN.":GOTO260
1815 REM If bus and have money, then enter, switch to room 3 and delete bus from room
1820 IF O=1 AND OB(9)=-1 THEN PRINT"OK, THE BUS DRIVER LETS ME IN,  I RIDE FOR A WHILE AND THEN GET OFF THE BUS.":YOU=3:OB(9)=0:GOTO260
1899 PRINT"I TRIED TO ENTER BUT COULDN'T.":GOTO260
1900 REM *** READ ***
1901 IF F=0 THEN PRINT"I DON'T SEE IT HERE.":GOTO260
1905 REM If sign, then describe
1910 IF O=8 THEN PRINT"SIGN SAYS:":PRINT"  WE BUY AND SELL JEWELRY":GOTO 260
1915 REM IF Rainbow, then describe and end game
1920 IF O=3 THEN CLS:PRINT"YOU HAVE JUST READ THE MOST     VALUABLE PIECE OF LITERATURE    AVAILABLE FOR THE COCO."
1930 IF O=3 THEN PRINT:PRINT"YOU HAVE ALSO REACHED THE END   OF THIS ADVENTURE.":PRINT:PRINT"  CONGRATULATIONS!":END
1999 PRINT "NOTHING SPECIAL":GOTO260
2000 REM *** LOAD ***
2001 IF F<>2 THEN PRINT"I DON'T HAVE IT.":GOTO260
2005 REM If computer tape and computer not in room, then describe
2010 IF O=6 AND OB(10)<>YOU THEN PRINT"THERE IS NO COMPUTER HERE.":GOTO260
2015 REM If computer tape and computer in room, then load, drop tape and set flag F(2)
2020 IF O=6 AND OB(10)=YOU THEN PRINT "OK, THE TAPE IS NOW LOADED":OB(6)=YOU:F(2)=1:GOTO260
2099 PRINT"I TRIED, BUT NOTHING HAPPENED.":GOTO260
2100 REM *** RUN ***
2105 REM If computer and it is in room and flag F(2) was set then describe, set flag F(3) and place bus in room 2
2110 IF O=10 AND OB(10)=YOU AND F(2)=1 THEN CLS:PRINT"THE SCREEN SHOWS:":PRINT:PRINT"SPECIAL BULLETIN:":PRINT"THE BUS COMPANY HAS REINSTATED  THEIR BUS SERVICE TO THIS AREA.":F(3)=1:OB(1)=2:GOTO260
2115 REM If computer and it is not in room then describe
2120 IF O=10 AND OB(10)<>YOU THEN PRINT"THERE IS NO COMPUTER HERE.":GOTO260
2125 REM If computer and flag F(2) was not set then describe
2130 IF O=10 AND F(2)<>1 THEN PRINT"THE COMPUTER DOES NOT RUN WITH- OUT A PROGRAM.":GOTO260
2199 PRINT"DON'T BE RIDICULOUS!":GOTO260
2200 REM *** SELL ***
2205 REM If not in room 8 then describe
2210 IF YOU<>8 THEN PRINT"YOU ARE NOT AT THE RIGHT PLACE  FOR SELLING ANYTHING.":GOTO260
2215 REM If in room 8 and selling watch and flag F(1) was set then allow sale, place watch in room and money in inventory
2220 IF YOU=8 AND  O=11 AND F(1)=1 THEN PRINT"OK, I SOLD THE WATCH.":OB(11)=8:OB(9)=-1:GOTO260
2225 REM If flag F(1) not set then describe
2230 IF F(1)<>1 THEN PRINT"YOU NEED TO FIND OUT WHAT KIND  OF JEWELRY YOU HAVE BEFORE YOU  CAN SELL ANY OF IT.":GOTO260
2299 IF F<>2 THEN PRINT"I DON'T HAVE IT TO SELL.":GOTO260
3000 REM ***DIRECTIONS***
3010 DATA NORTH,SOUTH,EAST,WEST,UP,DOWN
4000 REM *** ROOMS ***
4010 DATA ON A KENTUCKY STREET,2,4,7,6,0,0
4020 DATA AT A BUS STOP,0,1,0,0,0,0
4030 DATA IN A POST OFFICE,0,0,0,0,0,0
4040 DATA ON A WINDING ROAD,1,6,4,5,0,0
4050 DATA AT A DEAD END STREET,6,4,4,6,0,0
4060 DATA ON A WINDING ROAD,4,5,1,4,0,0
4070 DATA IN A SHOPPING CENTER,8,10,9,1,0,0
4080 DATA IN A PAWN SHOP,0,7,0,0,0,0
4090 DATA IN A COMPUTER STORE,0,0,0,7,0,0
4100 DATA IN A CLOTHING STORE,7,0,0,0,0,0
4999 DATA END
5000 REM *** OBJECTS ***
5010 DATA A CITY BUS,BUS,0
5020 DATA A MAIL BOX,BOX,3
5030 DATA LATEST RAINBOW ISSUE,RAI,0
5040 DATA A SHINY OBJECT,OBJ,5
5050 DATA A DISPLAY CASE,CAS,9
5060 DATA A COMPUTER TAPE,TAP,0
5070 DATA A MIRROR,MIR,10
5080 DATA A SIGN,SIG,8
5090 DATA MONEY,MON,0
5100 DATA AN MC-10 COMPUTER,COM,9
5110 DATA WATCH,WAT,0
5999 DATA END
6000 REM *** VERBS ***
6010 DATA GET,DROP,OPEN,CLOSE,EXAMINE,PUSH,BREAK,ENTER,READ,LOAD,RUN,SELL
6999 DATA END
7000 REM *** SAVE/LOAD ***
7010 GOSUB7130:OB(86)=YOU:CSAVE*OB,"RAINDATA"
7060 CLS:GOTO 140
7070 GOSUB7130:CLOAD*OB,"RAINDATA":YOU=OB(86)
7120 CLS:GOTO140
7130 CLS:PRINT:PRINT"PRESS ANY KEY WHEN THE DEVICE   IS READY."
7180 IF INKEY$=""THEN7180
7181 RETURN
8000 REM ***ELIMINATE OBJECT FROM
8010 REM   OBJECT LIST
8020 MID$(OB$,O*3-2,3)="   "
8030 RETURN
