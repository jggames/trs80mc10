CASTLE DRACULA



READY.



100 CLR:GOSUB10000

200 PRINT".LOCATION:-  ";L$(L):IFL>26THENPRINT"PASSAGE"

210 PRINT:PRINT"YOU SEE:-";:C=0

211 IFL=9THENPRINTTAB(14);SB$:C=1:ELSEIFL=11THENPRINTTAB(14);SH$:C=1

212 IF(L=17ANDV1=0)OR(L=24ANDV2=0)OR(L=21ANDH=0)THENPRINTTAB(14);H$:C=1

213 IFL=23THENPRINTTAB(14);"BATS FLYING AROUND":C=1

214 IFL=26THENPRINTTAB(14);"YOUR WIFE - ASLEEP!":C=1

215 IFL=34THENPRINTTAB(14);"IT'S DARK HERE!":C=1

216 IFL=25THENPRINTTAB(14);"DRACULA! - ASLEEP IN HIS  COFFIN!":C=1

220 FORK=1TO9:IFO%(K)=LTHENPRINTTAB(14);OL$(K):C=1

230 NEXT

280 IFC=0THENPRINT"  NOTHING OF INTEREST"

290 PRINT:PRINT"DIRECTIONS:-  ";:IFD%(L,0)<>0THENPRINT"NORTH ";

291 IFD%(L,1)<>0THENPRINT"SOUTH ";

292 IFD%(L,2)<>0THENPRINT"EAST ";

293 IFD%(L,3)<>0THENPRINT"WEST ";

390 PRINT:FORK=1TO40:PRINT"=";:NEXT

400 I$="":V$=I$:O$=I$:INPUT"WHAT NOW";I$

410 IFI$="N"THENI$="GO NOR":ELSEIFI$="S"THENI$="GO SOU"

420 IFI$="E"THENI$="GO EAS":ELSEIFI$="W"THENI$="GO WES"

440 IFLEN(I$)<3THENPRINT"WHAT??":GOTO400

450 V$=LEFT$(I$,3):FORK=LEN(I$)-2TO1STEP-1:IFMID$(I$,K,1)<>" "THENNEXT

451 IFV$="INV"THENDA=DA-1:GOTO950

460 O$=MID$(I$,K+1,3):W=0:GOSUB2000:IFW=1THEN400:ELSEIFV$="LOO"THEN200

461 IFO$="WOO"THENO$="FIR"

462 IFV$="BUY"THEN850:ELSEIFV$="DRO"THEN750:ELSEIFV$="BRE"THEN5210

463 IFV$="SHO"THEN700:ELSEIFV$="PUS"THEN600

464 IFV$="CLI"THEN650:ELSEIFV$="WEA"THEN550

465 IFV$="OPE"THEN5220:ELSEIFV$="KNO"THEN5200

466 IFV$="HEL"THEN4500:ELSEIFV$="EXA"THEN4000

467 IFV$="THR"THEN540:ELSEIFV$="SHA"THEN530

468 IFV$="DRI"THEN500:ELSEIFV$="WAK"ORO$="WIF"THEN520

469 IFV$="GO "THEN900:ELSEIFV$="GET"ORV$="STE"ORV$="TAK"THEN800

470 PRINT"SORRY, DONT UNDERSTAND!":GOTO400

500 IFO$<>"STA"THEN555

502 IFL<>25THENPRINT"NOT MUCH POINT IN DOING THAT HERE!":GOTO400

504 IFO%(2)<>-1THENPRINT"YOU HAVEN'T GOT A MALLET!":GOTO400

506 IFO%(3)<>-1THENPRINT"YOU HAVEN'T GOT A STAKE!":GOTO400

508 PRINT"THE STAKE DRIVES HOME - INTO THE HEART"

510 PRINT"OF DRACULA!!  HE SCREAMS THEN MELTS INTO"

512 PRINT"NOTHINGNESS!  YOU HAVE WON - YOUR WIFE"

514 PRINT"HAS AWOKEN FROM HER SPELL AND YOU ARE"

516 PRINT"BOTH FREE TO LEAVE!  CONGRATULATIONS!!"

518 PRINT:INPUT"PRESS F3 IF YOU WOULD LIKE TO PLAY AGAIN";A$:IFA$<>"AGAIN"

THEN518

520 IFL=26THENPRINT"SHE WON'T WAKE UP!":GOTO400:ELSE555

530 IFO%(9)<>-1THENPRINT"YOU DON'T HAVE A KNIFE!":GOTO400

531 IFO$<>"FIR"ORO%(7)<>-1THEN555:ELSEO%(7)=0:O%(3)=-1

532 PRINT"YOU HAVE CONVERTED THE FIREWOOD INTO A  WOODEN STAKE!":GOTO400

540 FORK=1TO9:IFO$(K)=O$THEN752:ELSENEXT:GOTO554

550 IFO$="CLO"ANDO%(8)=-2THENPRINT"YOU'RE ALREADY WEARING IT!":GOTO400

552 IFO$="CLO"ANDO%(8)=-1THENPRINT"O.K - IT'S ON!":O%(8)=-2:GOTO400

553 IFO$="CRO"ANDO%(1)=-1THENPRINT"O.K - IT'S ON!":GOTO400

554 IFO$="CRO"ORO$="CLO"THENPRINT"YOU HAVEN'T GOT IT!":GOTO400

555 PRINT"SORRY, CAN'T":GOTO400

600 IFL<>22OR(O$<>"BOO"ANDO$<>"SHE")THEN610

602 PRINT"THE BOOKSHELVES SWING INTO TO THE WALL!!"

604 PRINT"A DOWNWARD SLOPING PASSAGE TO THE WEST  IS REVEALED!":D%(22,3)=2

3:GOTO400

610 PRINT"NOTHING HAPPENS":GOTO400

650 IFL=14AND(O$="UP"ORO$="WAL"ORO$="DOO")THENL=15:GOTO200

652 IFO$="DOW"AND(L=15ORL=16)THENL=34:GOTO200

654 PRINT"CLIMB WHERE??":GOTO400

700 IFO%(5)<>-1THENPRINT"YOU DON'T HAVE ANYTHING TO SHOOT WITH!":GOTO400

701 IFL=8THENPRINT"NOT IN A CHURCH!!":GOTO400

702 IFBU=2THENPRINT"YOU'VE RUN OUT OF BULLETS!":GOTO400

704 BU=BU+1:IFO$<>"SHO"OR(L<>9ANDL<>11)THEN710

706 PRINT"THE SHOPKEEPER DROPS DEAD!":IFL=9THENSB$=SG$:SB=1:GOTO400

708 SH$=SG$:SH=1:GOTO400

710 IFL<>21ORH<>0THENPRINT"BANG!":GOTO400

720 PRINT"BANG - THE HUMAN DROPS DEAD!":H=1:GOTO400

750 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"DROP WHAT??":GOTO400

752 IFO%(K)>0THENPRINT"YOU DONT HAVE THAT ITEM!":GOTO400

754 ML=ML-1:O%(K)=L:PRINT"OKAY,IT'S DONE!":GOTO400

800 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"GET WHAT??":GOTO400

801 IFO%(K)=-1THENPRINT"YOU ALREADY HAVE IT!":GOTO400:ELSEIFO%(K)<>L THEN8

53

802 IF(L=11ANDSH=0)OR(L=9ANDSB=0)THENPRINT"YOU MUST BUY IN A SHOP!":GOTO40

0

804 IFML=5THENPRINT"YOU CAN'T CARRY ANY MORE!":GOTO400

805 IFL=8ANDO$="WAT"ANDO%(1)<>-1THEN809

806 O%(K)=-1:ML=ML+1:PRINT"OKAY, YOU HAVE IT!":GOTO400

809 PRINT"THE PRIEST SAYS - YOU CAN ONLY HAVE HOLYWATER IF YOU HAVE A CROS

S!":GOTO400

850 IFL<>9ANDL<>11THENPRINT"YOU CAN ONLY BUY FROM A SHOP!":GOTO400

851 IFL=9ANDO$="BIB"THENPRINT"SORRY, SOLD OUT!":GOTO400

852 FORK=1TO9:IFO$(K)<>O$THENNEXT:PRINT"BUY WHAT???":GOTO400

853 IFO%(K)<>L THENPRINT"I CAN'T SEE IT HERE!":GOTO400

854 IF(L=9ANDSB=1)OR(L=11ANDSH=1)THENPRINT"THERE'S NO-ONE TO BUY FROM!":GO

TO400

855 IFO%(K)=-1THEN801

856 IFL<>9ORO%(6)=-1THEN860

858 PRINT"SHOPKEEPER SAYS - YOU CAN ONLY HAVE THE CROSS IF YOU HAVE HOLY W

ATER!":GOTO400

860 IFML=5THEN804:ELSEGOTO806

900 IFO$="NOR"ANDD%(L,0)<>0THENL=D%(L,0):GOTO200

901 IFL=13THENFC=FC+1:IFFC=3THEN1000

902 IFO$="SOU"ANDD%(L,1)<>0THENL=D%(L,1):GOTO200

904 IFO$="EAS"ANDD%(L,2)<>0THENL=D%(L,2):GOTO200

906 IFO$="WES"ANDD%(L,3)<>0THENL=D%(L,3):GOTO200

909 PRINT"YOU CAN'T GO THAT WAY!":GOTO400

950 C=0:PRINT"YOU ARE CARRYING:-":FORK=1TO9:IFO%(K)<0THENPRINTTAB(10);OL$(

K):C=1

952 NEXT:IFC=0THENPRINTTAB(10);"NOTHING AT ALL!"

954 GOTO400

1000 PRINT"A HORSE-DRAWN CARRIAGE ARRIVES. THERE ISNO DRIVER!":GOSUB5000

1002 PRINT:PRINT"YOU ENTER THE CARRIAGE AND IT DRIVES OFF":GOSUB5000

1004 PRINT:PRINT" THE CARRIAGE ARRIVES AT THE CASTLE AND YOU GET OUT. THE 

CARRIAGE DRIVES OFF"

1006 L=14:GOSUB5000:GOSUB5000:GOTO200

2000 IFL=34ANDO%(4)<>-1THENPRINT"YOU FELL IN A HOLE AND BROKE YOUR NECK!":

GOTO22000

2100 IFL=23ORL=17ORL=24ORL=21THEN3010:ELSERETURN

3010 IFL<>23THEN3100:ELSEPRINT"THE BATS ATTACK YOU - ";

3020 IFO%(8)=-2THENPRINT"BUT ARE WARDED OFF BY YOUR CLOAK!":GOSUB5000:RETU

RN

3030 PRINT"AND EAT YOU!":GOTO22000

3100 IFL<>21THEN3200:ELSEIFH=1THENRETURN

3101 PRINT"THE HUMAN FORM ATTACKS YOU WITH A KNIFE - ";

3120 IFV$="SHO"THENRETURN:ELSEPRINT"AND STABS YOU TO DEATH!":GOTO22000

3200 IFL<>17THEN3300

3210 IFV1=1THENRETURN:ELSEPRINT"THE HUMAN FORM BARES HIS TEETH AND":PRINT"

ATTACKS YOU - ";

3220 IFO$<>"WAT"ORO%(6)<>-1THEN3230

3221 PRINT"HE SCREAMS ON CONTACT WITHTHE HOLY WATER, THEN DISSAPPEARS!":V1

=1:O%(6)=0

3222 W=1:RETURN

3230 IFO$<>"CRO"ORO%(1)<>-1THEN3240

3231 PRINT"HE TOUCHES THE CROSS AND  BOTH DISSAPPEAR!":V1=1:O%(1)=0:GOTO32

22

3240 PRINT"AND SINKS HIS FANGS INTO  YOUR NECK!!":GOTO22000

3300 IFV2=1THENRETURN:ELSEPRINT"THE HUMAN FORM";

3301 PRINT" BARES HIS FANGS ATTACKINGYOU - ";

3320 IFO$<>"WAT"ORO%(6)<>-1THEN3330

3321 PRINT"HE SCREAMS ON CONTACT WITHTHE HOLY WATER, THEN DISSAPPEARS!":V2

=1:O%(6)=0

3322 GOTO3222

3330 IFO$<>"CRO"ORO%(1)<>-1THEN3240

3331 PRINT"HE TOUCHES THE CROSS AND BOTH     DISSAPPEAR!":V2=1:O%(1)=0:GOT

O3222

4000 C=0:IFO$="STR"ANDL<8THENPRINT"IT'S JUST A STREET!":C=1

4002 IFO$="SHO"ANDL<8THENPRINT"IT PROBABLY SELLS THINGS!":C=1

4003 IFO$="SHO"AND(L=9ORL=11)THENPRINT"HE LOOKS MEAN!":C=1

4004 IFO$="PRI"ANDL=8THENPRINT"HE'S WEARING A DOG-COLLAR!":C=1

4006 IFL=10THENPRINT"YOUR WIFE ISN'T HERE!":C=1

4008 IFL=14ANDO$="DOO"THENPRINT"IT LOOKS VERY STRONG!":C=1

4009 IFL=26ANDO$="WIF"THENPRINT"SHE APPEARS TO BE IN A TRANCE!":C=1

4010 IF(L=14ORL=15ORL=16)ANDO$="WAL"THENPRINT"THE WALL SURROUNDS THE CASTL

E!":C=1

4012 IFL=22AND(O$="SHE"ORO$="BOO"ORO$="LIB")THENPRINT"LOTS OF BOOKS ON LOT

S OF SHELVES!":C=1

4014 IFL=25AND(O$="DRA"ORO$="COF"ORO$="VAU")THENPRINT"IT'S COLD IN HERE!!"

:C=1

4016 IFO$="CRO"AND(O%(1)=-1ORO%(1)=L)THENPRINT"A TYPICAL VAMPIRE-DISPELLIN

G CROSS!":C=1

4018 IFO$="CLO"AND(O%(8)<0ORO%(8)=L)THENPRINT"IT LOOKS VERY MYSTERIOUS!":C

=1

4020 IFO$="MAL"AND(O%(2)<0ORO%(2)=L)THENPRINT"JUST RIGHT FOR DRIVING STAKE

S!":C=1

4022 IFO$="STA"AND(O%(3)<0ORO%(3)=L)THENPRINT"IT'S VERY SHARP!!":C=1

4024 IFO$="FLA"AND(O%(4)<0ORO%(4)=L)THENPRINT"IT THROWS A LIGHT!!":C=1

4026 IFO$="GUN"AND(O%(5)<0ORO%(5)=L)THENPRINT"IT HAS ";2-BU;"BULLETS IN IT

!":C=1

4028 IFO$="WAT"AND(O%(6)<0ORO%(6)=L)THENPRINT"LOOKS JUST LIKE ORDINARY WAT

ER!":C=1

4030 IFO$="FIR"AND(O%(7)<0ORO%(7)=L)THENPRINT"LOOKS LIKE A BLUNT STAKE!!":

C=1

4032 IFO$="KNI"AND(O%(9)<0ORO%(9)=L)THENPRINT"IT'S VERY SHARP!":C=1

4034 IFC=0THENPRINT"I CAN'T SEE IT HERE!"

4035 GOTO400

4500 C=0

4600 IFL<8THENPRINT"EXPLORE, EXAMINE THINGS AND THINK!!":C=1

4602 IFL=9ORL=11THENPRINT"STOCK UP WISELY!":C=1

4604 IFL=13THENPRINT"WANDER!":C=1

4606 IFL=14ORL=15ORL=16THENPRINT"TRY CLIMBING!":C=1

4608 IFL>26ANDL<35THENPRINT"PAY ATTENTION TO YOUR LOCATION!":C=1

4610 IFL=18ORL=19THENPRINT"BEWARE! VAMPIRES LOOK LIKE HUMANS (AND  SO DO H

UMANS!)":C=1

4612 IFL=26THENPRINT"YOU CANNOT WAKE HER - SHE IS UNDER      DRACULA'S SPE

LL!":C=1

4614 IFL=22THENPRINT"TRY THE OBVIOUS!":C=1

4616 IFL=35THENPRINT"USE WHAT YOU FIND!":C=1

4650 IFC=1THEN400

4688 PRINT"USEFUL WORDS ARE: -  INVENTORY (USE THE"

4690 PRINT"F1 KEY), LOOK (USE THE F2 KEY), BUY, GET"

4692 PRINT"DROP, SHOOT, PUSH, CLIMB, WEAR, GO, TAKE"

4694 PRINT"STEAL, THROW, SHARPEN, HELP-USE HELP KEY":PRINT:GOTO400

5000 FORK=1TO2500:NEXT:RETURN

5200 PRINT"THERE'S NO REPLY!":GOTO400

5210 PRINT"NO WAY! THE BOSS SAYS I HAVE TO PAY FOR ANYTHING I BREAK!!":GOT

O400

5220 PRINT"IT'S STUCK!":GOTO400

10000 COLOR0,6,3:COLOR4,6,3:PRINT"..INITIALISING--":DIML$(35),D%(35,3)

10010 FORK=1TO35:READL$(K),D%(K,0),D%(K,1),D%(K,2),D%(K,3)

10011 IFK<13THENL$(K)=L$(K)+" IN A LITTLE TRANSYLVANIAN VILLAGE"

10012 NEXT:L=10

10020 DATAA NARROW STREET,1,2,,,OUTSIDE  A  BIBLE SHOP ON A STREET,1,3,9,

10030 DATAA NARROW STREET,2,4,,,A NARROW STREET,3,5,,,OUTSIDE A HARDWARE S

HOP,4,6,11,

10040 DATAA NARROW STREET,5,7,,12,IN  FRONT  OF A CHURCH,6,8,,,INSIDE A CH

URCH,7,,,

10050 DATAIN A BIBLE SHOP,,,,2,IN A  QUIET INN,,,,3,INSIDE A HARDWARE SHOP

,,,,5

10060 DATAON A RUSTY PATH,,,6,13,A MAZE OF PATHS IN A FOREST,13,13,13,13

10061 DATAIN FRONT OF THE CASTLE DOOR,,,,

10070 DATAON THE CASTLE WALL,16,16,,,ON THE CASTLE WALL,,,15,15

10080 DATAIN A RED CHAMBER,,18,,,IN A HALLWAY,17,19,,,IN A LARGE HALL,18,2

6,20,22

10090 DATAIN THE DINING ROOM,35,21,,19,IN THE KITCHEN,20,,,,IN THE LIBRARY

,,,19,0

10100 DATAIN A BASEMENT,,24,22,,IN A CRYPT,23,,25,,IN THE VAULT,,,,24

10110 DATAIN A BEDROOM,19,,,,IN A TURNY TWISTY LITTLE,27,29,30,34

10120 DATAIN A TURNY LITTLE TWISTY,34,28,30,29

10130 DATAIN A LITTLE TURNY TWISTY,29,28,33,27

10140 DATAIN A LITTLE TWISTY TURNY,27,28,31,30

10150 DATAIN A TWISTY LITTLE TWISTY,30,34,34,17

10160 DATAIN A TWISTY LITTLE TURNY,33,32,34,32

10170 DATAIN A TURNY LITTLE TURNY,33,29,33,32

10180 DATAIN A TWISTY TURNY LITTLE,27,32,28,34

10190 DATAIN AN PECULIAR SITTING ROOM AT THE END OF A,,20,,

11000 DIMO$(9),OL$(9),O%(9):FORK=1TO9:READO$(K),OL$(K),O%(K):NEXT

11010 DATACRO,A SILVER CROSS,9,MAL,A WOODEN MALLET,11,STA,A WOODEN STAKE,1

1

11020 DATAFLA,A FLASHLIGHT,11,GUN,A SMALL HAND GUN,11,WAT,A VIAL OF HOLY W

ATER,8

11030 DATAFIR,FIREWOOD,35,CLO,A BLACK CLOAK,35,KNI,A SMALL KITCHEN KNIFE,2

1

19000 SB$="THE SHOPKEEPER":SH$=SB$:SG$="A DEAD SHOPKEEPER":H$="A HUMAN FOR

M"

19100 KEY1,"INVENTORY"+CHR$(13):KEY3,"AGAIN"+CHR$(13):KEY2,"LOOK"+CHR$(13)


20000 RETURN

22000 PRINT:PRINT:PRINT"VOICES BOOM OUT - FANGS FOR THE MEMORY!":PRINT

22001 PRINT:PRINT"YOU ARE DEAD!!  PRESS F3 TO START AGAIN":INPUTA$:IFA$<>"

AGAIN"THEN22000

22002 RUN



READY.




