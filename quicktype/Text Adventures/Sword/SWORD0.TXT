10 ' THE SWORD OF RASCHKIL VERSION 1.32 WRITTEN 8/20/81
20 ' BY MAC VAUGHN 3123 QUEENS WALK ATLANTA, GA. 30084
30 ' SOPHOMORE AT HENDERSON HIGH SCHOOL AGE 16 CLASS OF '84
35 ' PHONE (404) 491-1613 PROGRAM USES ABOUT 11K RAM
40 DIMS(19),O(19),O$(19):CLS: PRINT"QUEST #1 'THE SWORD OF RASCHKIL'  BY MAC VAUGHN TUCKER,GA.":PRINT
50 PRINT"THIS  Q U E S T  IS THE FIRST OF A SERIES.YOUR MISSION IS TO    SEEK OUT THE FAMED SWORD OF RASCHKIL. TO DO SO YOU MUST FIND THE PROPERITEMS AND USE THE CORRECT COMMANDS. YOU MUST NOT FAIL!": PRINT:FORI=1TO19:S(I)=-1:NEXTI
55 S(3)=-2:S(6)=-2:FORI=8TO12:S(I)=-2:NEXTI:S(16)=-2:S(17)=-2
60 PRINT"YOU WILL FIND THAT YOU ARE IN A WHOLE NEW WORLD.YOU CAN PICK UP THINGS (AND PUT THEM DOWN).";
61 PRINT" USE ONE- OR TWO-WORD COMMANDS ( )";
62 PRINT" TOCONTROL ME (YOUR PUPPET) IN THIS QUEST (I.E. CLIMB LADDER, SWIM)USE N,S,E OR W (OR D FOR DOWN)";
63 PRINT" TO MOVE. GOOD LUCK!!!"
65 PRINT@507, CHR$(92);
67 PRINT@640, "NOTE: TO TAKE INVENTORY TYPE 'I', TO QUIT TYPE 'Q'"
70 PRINT@704,"PRESS ANY KEY TO CONTINUE";
80 IFINKEY$=""THEN80ELSEO(1)=32:O(2)=7:O(3)=4:O(4)=9:O(5)=31
90 O(6)=8:O(7)=13:O(8)=14:O(9)=15:O(10)=13:O(11)=2:O(12)=18
100 CLS:O(13)=19:O(14)=23:O(15)=0:O(16)=25:O(17)=30:O(18)=4
110 O(19)=15:O$(1)="SWORD":O$(2)="WATER":O$(3)="TREE"
120 O$(4)="JAR":O$(5)="KEY":O$(6)="CASTLE":O$(7)="STAFF"
130 O$(8)="PIT":O$(9)="WIZARD":O$(10)="LADDER":O$(11)="HOLE"
140 O$(12)="HOUSE":O$(13)="RAT":O$(14)="PIE":O$(15)="DIAMOND"
150 O$(16)="GHOST":O$(17)="DOOR":O$(18)="LEAF":O$(19)="SIGN"
160 RO=1:GOTO2000
190 GOTO6000
200 ' INPUT COMMANDS
210 INPUTX$:IFNH=1THENGG=1:NH=0:RO=10:PRINT"YOU RUN OUT OF AIR AND SURFACE."
215 FORI=1TOLEN(X$):IFMID$(X$,I,1)=" "THEN300
220 NEXTI:IFX$="LOOK" THEN2000ELSEIFLEN(X$)=1THEN1000
230 IFX$="SWIM"THEN280ELSEIFX$="DIVE"THEN250
240 IFX$="JUMP"THEN270ELSEIFX$="SURFACE"THEN290
245 C$=X$:GOTO5020
250 IFRO<>10THEN5000
260 NH=1:RO=31:GOSUB5010:GOTO2000
270 IFRO=130RRO=9THEN5030ELSE5000
280 IFRO<>7THEN285ELSERO=10:GOTO2000
285 IFRO=10THENRO=7:GOTO2000ELSE5000
290 IFRO=31THENRO=10:GOSUB5010:GOTO2000ELSE5000
300 C$=LEFT$(X$,I-1):D$=RIGHT$(X$,(LEN(X$)-LEN(C$)-1))
310 RESTORE:FORI=1TO16:READX$:IFX$=C$THENV=I:GOTO330ELSENEXTI
320 GOTO5020
330 FORI=1TO19:IFD$=O$(I)THEN340ELSENEXTI:GOTO5080
340 ONVGOTO370,370,700,720,720,780,820,860,860,920,950,970
350 ONV-12GOTO3500,3540,3560,3600
370 ONIGOTO390,440,5090,490,510,5090,550,5090,5890,5090
380 ONI-10GOTO5090,5090,570,610,630,5090,5090,650,670
390 IFRO<>32THEN5000ELSEIFS(18)=1THEN410
400 PRINT"IT LEVITATES OUT OF YOUR REACH.":GOTO200
410 PRINT"AS YOU SEIZE THE HILT, THE SWORD BEGINS TO GLOW."
420 PRINT"YOUR QUEST IS AT AN END. YOU NOW POSSESS THE SWORD"
430 PRINT"OF RASCHKIL.":GOTO6000
440 IFRO<>7ANDRO<>10ANDRO<>31THEN5000
450 IFS(4)=1THEN470
460 PRINT"FIRST I NEED A CONTAINER.":GOTO200
470 IFS(2)=1THEN5100
480 GOSUB5010:S(2)=1:GOTO200
490 IFRO<>9THEN5000ELSEIFS(4)=1THEN5100
500 GOSUB5010:S(4)=1:O(4)=-1:GOTO200
510 IFGG=1THENGG=0:GOTO530ELSEIFRO<>31THEN5000
520 IFS(5)=1THEN5100
530 GOSUB5010
540 RO=10:S(5)=1:O(5)=-1:GOTO2000
550 IFRO<>13THEN5000ELSEIFS(7)=1THEN5100
560 S(7)=1:O(7)=-1:GOSUB5010:GOTO200
570 IFRO<>19THEN5000
580 PRINT"THE RAT BIT YOU.":KJ=RND(20):IFKJ<>1THEN200
590 PRINT"AS SOON AS HE BITES YOU A STRANGE PAIN ENVELOPS"
600 PRINT"YOUR BODY. YOU SLOWLY COLLAPSE AND DIE.":GOTO6000
610 IFRO<>23THEN5000ELSEIFS(14)=1THEN5100
620 GOSUB5010:S(14)=1:O(14)=-1:GOTO200
630 IFS(15)=-2,5000ELSEIFRO<>O(15),5000
640 IFS(15)=1THEN5100ELSES(15)=1:O(15)=-1:GOSUB5010:GOTO200
650 IFRO<>4THEN5000ELSEIFS(18)=1THEN5100
660 S(18)=1:O(18)=-1:GOSUB5010:GOTO200
670 IFRO<>15THEN5000
680 PRINT"THE WIZARD SAYS, ''LEAVE MY SHINGLE ALONE.'''"
690 GOTO200
700 IFS(I)<>1THEN5110
710 GOSUB5010:S(I)=-1:O(I)=RO:GOTO2000
720 IFRO<>13ANDRO<>4THEN5000
730 IFD$="LADDER"ANDRO=13THEN750
740 IFD$="TREE"ANDRO=4THEN770ELSE5000
750 IFC$="SCALE"THENPRINT"TRY 'CLIMB'":GOTO200
760 GOSUB5010:RO=2:GOTO2000
770 GOSUB5010:RO=9:GOTO2000
780 IFRO<>7ANDRO<>10ANDRO<>31THEN5000
790 IFD$<>"JAR"THEN5000
800 IFS(2)=1THENPRINT"IT'S ALREADY FULL.":GOTO200
810 S(2)=1:GOSUB5010:GOTO200
820 IFD$<>"WATER"THEN5000ELSES(2)=-1:IFRO=15THEN840
830 GOSUB5010:PRINT"BOY THAT REALLY HIT THE SPOT!!!":GOTO200
840 GOSUB5010:PRINT"EVERYTHING BEGINS TO SPIN AROUND AND..."
850 RO=17:GOTO2000
860 IFD$<>"PIE"ORS(14)=-1THEN5000
870 IFRO=13THENPRINT"YOU HIT THE WIZARD WITH A PIE."
880 IFRO=13THENPRINT"HE STRIKES YOU DEAD WITH HIS TOUCH."
890 IFRO=13THEN6000
900 PRINT"YOU HIT THE GHOST WITH A PIE."
910 PRINT"HE GETS MAD AND PUNCHES YOU, THE FORCE OF THE BLOW"
915 PRINT"IS SO GREAT THAT IT KILLS YOU.":GOTO6000
920 IFRO<>15ORD$<>"SIGN"THEN5000
930 PRINT"IT SAYS, 'I AM CLUTON. THROW A PIE AND YOU WILL DIE.'"
940 GOTO200
950 IFS(2)=-1ORD$<>"WATER"THEN5000ELSES(2)=-1
960 GOSUB5010:PRINT"YOU MADE A PUDDLE ON THE GROUND.":GOTO200
970 IFD$<>"DOOR"ORS(5)<>1ORRO<>30THEN5000ELSEGOSUB5010
980 PRINT"THE DOOR SWINGS OPEN.":S(17)=-3:GOTO2000
1000 '----- MOVE, QUIT OR INVENTORY?
1010 IFX$="Q"THENPRINT"OH WELL...BYE...":GOTO6000
1020 IFX$="I"THEN3700ELSEIFX$="E"ORX$="W"ORX$="N"THEN1030
1025 IFX$<>"D"ANDX$<>"S"THEN5120
1027 IFX$="D"ANDRO<>9THEN5000
1030 ON RO GOTO 1060,1080,1100,1120,1130,1150,1170,1180,1200,1210
1040 ON RO-10 GOTO1220,1240,1250,1260,1280,1300,1310,1330,1350
1050 ON RO-19 GOTO1360,1380,1390,1410,1430,1450,1470,1490,1510
1055 ON RO-28 GOTO1530,1550,1570,1570
1060 IFX$="S"THENRO=2:GOTO2000ELSEIFX$="E"THENRO=6:GOTO2000
1070 GOTO5120
1080 IFX$="W"THEN5120ELSEIFX$="E"THENRO=5:GOTO2000
1090 IFX$="N"THENRO=1:GOTO2000ELSEIFX$="S"THENRO=3:GOTO2000
1100 IFX$="N"THENRO=2:GOTO2000ELSEIFX$="E"THENRO=4:GOTO2000
1110 GOTO5120
1120 IFX$<>"W"THEN5120ELSERO=3:GOTO2000
1130 IFX$="S"THEN5120ELSEIFX$="N"THENRO=6:GOTO2000
1140 IFX$="W"THENRO=2:GOTO2000ELSERO=8:GOTO2000
1150 IFX$="N"THEN5120ELSEIFX$="S"THENRO=5:GOTO2000
1160 IFX$="E"THENRO=7:GOTO2000ELSERO=1:GOTO2000
1170 IFX$<>"W"THEN5120ELSERO=6:GOTO2000
1180 IFX$="W"THENRO=5:GOTO2000ELSEIFX$="E"THENRO=11:GOTO2000
1190 GOTO2000
1200 IFX$<>"D"THEN5120ELSERO=4:GOTO2000
1210 GOTO5120
1220 IFX$="N"THENRO=12:GOTO2000ELSEIFX$="S"THENRO=16:GOTO2000
1230 IFX$="E"THENRO=15:GOTO2000ELSERO=8:GOTO2000
1240 IFX$="E"THENRO=14:GOTO2000ELSEIFX$="S"THENRO=11:GOTO2000
1250 GOTO5120
1260 IFX$="S"THENRO=15:GOTO2000ELSEIFX$="W"THENRO=12:GOTO2000
1270 GOTO5120
1280 IFX$="N"THENRO=14:GOTO2000ELSEIFX$="W"THENRO=11:GOTO2000
1290 GOTO5120
1300 IFX$<>"N"THEN5120ELSERO=11:GOTO2000
1310 IFX$="W"THEN5120ELSEIFX$="E"THENRO=19:GOTO2000
1320 IFX$="N"THENRO=18:GOTO2000ELSERO=4:GOTO2000
1330 IFX$="S"THENRO=17:GOTO2000ELSEIFX$="E"THENRO=20:GOTO2000
1340 GOTO5120
1350 IFX$="W"THENRO=17:GOTO2000ELSE5120
1360 IFX$="S"THEN5120ELSEIFX$="N"THENRO=21:GOTO2000
1370 IFX$="E"THENRO=22:GOTO2000ELSERO=18:GOTO2000
1380 IFX$="S"THENRO=20:GOTO2000ELSE5120
1390 IFX$="S"THEN5120ELSEIFX$="N"THENRO=23:GOTO2000
1400 IFX$="E"THENRO=24:GOTO2000ELSERO=20:GOTO2000
1410 IFX$="S"THENRO=22:GOTO2000ELSEIFX$="E"THENRO=25:GOTO2000
1420 GOTO5120
1430 IFX$="N"THENRO=25:GOTO2000ELSEIFX$<>"W"THEN5120
1440 RO=22:GOTO2000
1450 IFX$="S"THENRO=24:GOTO2000ELSEIFX$="W"THENRO=23:GOTO2000
1455 IFX$="E"ANDS(16)=-3THENRO=26:GOTO2000
1457 IFX$="E"THENPRINT"THE GHOST WON'T LET YOU BY.":GOTO2000
1460 GOTO5120
1470 IFX$="N"THENRO=27:GOTO2000ELSEIFX$="S"THENRO=24:GOTO2000
1480 GOTO5120
1490 IFX$="E"THEN5120ELSEIFX$="W"THENRO=28:GOTO2000
1500 IFX$="N"THENRO=29:GOTO2000ELSERO=26:GOTO2000
1510 IFX$="N"THENRO=30:GOTO2000ELSEIFX$="E"THENRO=27:GOTO2000
1520 GOTO5120
1530 IFX$="S"THENRO=27:GOTO2000ELSEIFX$="W"THENRO=30:GOTO2000
1540 GOTO5120
1550 IFX$="S"THENRO=28:GOTO2000ELSEIFX$="E"THENRO=29:GOTO2000
1560 IFX$="W"ANDS(17)=2THENRO=32:GOTO1580
1570 GOTO5120
1580 PRINT"THE DOOR SLAMS BEHIND YOU AND...":GOTO2000
2000 '----- PRINT PRESENT LOCATION
2010 ON RO GOTO 2040,2060,2080,2090,2110,2120,2140,2160,2170,2180,2210
2020 ON RO-11 GOTO 2220,2240,2270,2290,2330,2340,2370,2380,2400,2410,2420
2030 ON RO-22 GOTO 2430,2440,2450,2460,2480,2500,2520,2540,2570,2580
2040 PRINT"YOU ARE AT THE EDGE OF A FOREST."
2050 PRINT"PATHS LEAD SOUTH AND EAST.":GOTO3000
2060 PRINT"YOU ARE IN A GROVE OF OAK TREES."
2070 PRINT"A HOLE IS NEARBY.":GOTO3000
2080 PRINT"YOU ARE IN THE MEADOW.":GOTO3000
2090 PRINT"YOU ARE ON A SMALL PATH.A LARGE OAK IS NEARBY."
2100 IFO(18)=4THENPRINT"A GOLDEN LEAF IS ON THE GROUND"
2105 GOTO3000
2110 PRINT"YOU ARE IN A BLEAK, GRASSY FIELD.":GOTO3000
2120 PRINT"YOU ARE IN A LOVELY FOREST WITH THE CALLS OF MANY"
2130 PRINT"BIRDS SOUNDING ALL AROUND YOU.":GOTO3000
2140 PRINT"YOU ARE IN AN ANCIENT ORCHARD."
2150 PRINT"A POOL OF MURKY WATER IS BEFORE YOU.":GOTO3000
2160 PRINT"YOU ARE AT THE ENTRANCE TO A SMALL CASTLE.":GOTO3000
2170 PRINT"YOU ARE AT THE TOP OF AN OAK.":GOTO3000
2180 PRINT"YOU ARE SWIMMING IN A POOL OF WATER."
2190 IFO(5)=31THENPRINT"AT THE BOTTOM OF THE POOL IS A KEY."
2200 GOTO3000
2210 PRINT"YOU ARE IN THE CASTLE ENTRY.":GOTO3000
2220 PRINT"YOU ARE IN THE NORTH WING.A DRAFT ISSUES FROM THE"
2230 PRINT"EAST EXIT.":GOTO3000
2240 PRINT"YOU ARE ON A LEDGE JUST INSIDE A BOTTOMLESS HOLE."
2250 PRINT"A LADDER LEADS UP.I ADVISE YOU NOT TO GO DOWN."
2260 GOTO3000
2270 IFS(7)=-1THENPRINT"YIIIIEEEE...YOU FELL IN A PIT!":GOTO190
2280 PRINT"YOU ARE LEVITATING ABOVE A PIT.":GOTO3000
2290 PRINT"YOU ARE IN THE WIZARD'S ROOM."
2310 PRINT"THE WIZARD IS IN THIS ROOM."
2320 PRINT"A SIGN IS NEARBY.":GOTO3000
2330 PRINT"YOU ARE IN THE SOUTH WING.":GOTO3000
2340 PRINT"YOU ARE ON THE HEATH OF ORIONE'S MANOR."
2350 PRINT"TO THE NORTH IS A HOUSE, TO THE EAST, A DUMP."
2360 GOTO 3000
2370 PRINT"YOU ARE AT THE ENTRANCE TO A HOUSE.":GOTO3000
2380 PRINT"YOU ARE IN THE DUMP.A LARGE RAT IS WATCHING YOU."
2390 GOTO3000
2400 PRINT"YOU ARE IN THE ENTRY HALL.":GOTO3000
2410 PRINT"YOU ARE IN AN ANCIENT BEDCHAMBER.":GOTO3000
2420 PRINT"YOU ARE IN THE BACK ROOM.":GOTO3000
2430 PRINT"YOU ARE IN THE KITCHEN.":GOTO3000
2440 PRINT"YOU ARE IN THE BACK PORCH.":GOTO3000
2450 PRINT"YOU ARE IN THE PANTRY.A GHOST IS HERE.":GOTO3000
2460 PRINT"YOU ARE IN THE GARDEN.A BARN IS TO THE NORTH."
2470 GOTO3000
2480 PRINT"YOU ARE IN THE BARN.TO THE NORTH ARE STABLES,"
2490 PRINT"AND A PASSAGE LEADING WEST.":GOTO3000
2500 PRINT"YOU ARE IN THE DIM FEED STORAGE ROOM.TO THE NORTH"
2510 PRINT"IS ANOTHER DIMLY LIT ROOM.":GOTO3000
2520 PRINT"YOU ARE IN THE STABLE.A PASSAGE LEADS WEST."
2530 GOTO3000
2540 PRINT"YOU ARE IN THE LEATHERSHOP.A DOOR IS TO THE WEST."
2550 IFS(17)=2THENPRINT"IT IS UNLOCKED.":GOTO3000
2560 PRINT"IT IS LOCKED.":GOTO3000
2570 PRINT"YOU ARE AT THE BOTTOM OF A POOL.":GOTO3000
2580 PRINT"YOU ARE IN A MISTY ROOM.A SWORD IS LEVITATING"
2590 PRINT"IN MIDAIR.":GOTO3000
3000 '----- PRINT VISIBLE ITEMS
3010 PRINT"VISIBLE ITEMS:":FORI=1TO19:IFO(I)=ROTHENI1=1:PRINTO$(I)
3020 NEXTI:IFI1=1THENI1=0ELSEPRINT"NOTHING"
3030 GOTO200
3500 IFD$<>"DOOR"THEN5120
3510 IFRO<>30THEN5120
3520 IFS(5)=-1THENPRINT"YOU NEED A KEY.":GOTO 200
3530 S(17)=2:GOSUB5010:GOTO200
3540 IFD$<>"DIAMOND"THEN5120ELSEIFRO<>25THEN5120
3550 PRINT"THE GHOST ACCEPTS THE DIAMOND.":S(16)=-3:GOTO200
3560 IFD$="RAT"THEN570ELSEIFD$<>"PIE"THEN5120
3570 PRINT"YOU ENJOY A DELICIOUS SARA LEE PIE."
3580 PRINT"HIDDEN INSIDE IT IS A DIAMOND.":O(15)=RO:S(15)=-1
3590 O(14)=-1:S(14)=-1:GOTO200
3600 IFD$="POOL"THENPRINT"TRY 'SWIM'.":GOTO200
3610 IFD$<>"HOLE"ORRO<>2THEN5120
3620 RO=13:GOTO2000
3700 PRINT"YOU ARE CARRYING THE FOLLOWING:":FORI=1TO19
3710 IFS(I)=1THENI1=1:PRINTO$(I)
3720 NEXTI:IFI1=0THENPRINT"NOTHING"ELSEI1=0
3730 GOTO200
5000 PRINT"I CAN'T DO THAT AT THIS TIME.":GOTO200
5010 PRINT"O.K.":RETURN
5020 PRINT"I DON'T KNOW HOW TO "C$" SOMETHING!!!!":GOTO200
5030 PRINT"YOU FELL AND BROKE EVERY BONE IN YOUR BODY!!!"
5040 PRINT"YOU'RE DEAD!!!":PRINT:GOTO6000
5050 DATA "GET","TAKE","DROP","CLIMB","SCALE","FILL","DRINK"
5060 DATA "THROW","HURL","READ","POUR","LOCK","UNLOCK","GIVE"
5070 DATA "EAT","ENTER"
5080 PRINT"I DON'T KNOW WHAT A "D$" IS!!!!!!":GOTO200
5090 PRINT"DON'T BE RIDICULOUS!!":GOTO200
5100 PRINT"I'VE GOT IT ALREADY!!!":GOTO200
5110 PRINT"I DON'T HAVE IT!!!":GOTO200
5120 PRINT"WHAT??":GOTO200
6000 INPUT "PLAY AGAIN";X$:X$=LEFT$(X$,1):IF X$<>"N" THEN RUN ELSE END



S
ENTER HOLE
TAKE STAFF
CLIMB LADDER
S
E
TAKE LEAF
CLIMB TREE
TAKE JAR
D
W
N
N
E
E
TAKE WATER
DRINK WATER
SWIM
DIVE
TAKE KEY
SWIM
W
S
E
E
E
DRINK WATER
N
E
E
N
TAKE PIE
EAT PIE
TAKE DIAMOND
E
GIVE DIAMOND
E
N
W
N
UNLOCK DOOR
W
TAKE SWORD
