0 CLS:GOSUB5000:CLEAR500:DIMCC,ZZ,M$,I$:SZ=63:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 DIMUT(4),U$(SZ),TT(21),O(33,8)
20 FORI=1TOSZ:READU$(I):NEXTI:FORI=1TO21:READTT(I):NEXTI
30 FORI=1TO33:FORJ=1TO8:READO(I,J):NEXTJ,I:FORZZ=1TO2500:NEXT:CLS
50 HE=1
90 GOSUB380
95 IFKI=1THENPRINT"THE PRINCESS IS WITH YOU."
100 L=0:N$=""
110 INPUTN$
150 N$=N$+" ":IFN$=""THEN100
160 C$="":E=0:FORI=1TOLEN(N$):IFMID$(N$,I,1)=" "THEN175
165 C$=C$+MID$(N$,I,1)
170 NEXTI:PRINT:GOTO250
175 IFC$=""THEN170
180 FORJ=1TOSZ:IFU$(J)=C$THENUT(E)=J:E=E+1:C$="":GOTO170
190 NEXTJ:M$="I DO NOT UNDERSTAND THE WORD "+CHR$(34)+C$+CHR$(34)+".":GOSUB1:GOTO100
250 IFUT(0)=0THEN100
255 IFUT(0)>0ANDUT(0)<9THEN2000
260 ONUT(0)-8GOTO300,300,340,340,600,600,630,630,650,650,730,760,770,780,800
270 ONUT(0)-23GOTO800,735,840,870,900,910,920,920,1060,930,980,980,1100,1200
280 ONUT(0)-37GOTO1210,1280,1300,1300,1350
300 IFUT(1)<43THENPRINT"WHAT?":GOTO100
310 UT(1)=UT(1)-42:IFTT(UT(1))<>HETHENM$="I CAN'T SEE THAT OBJECT.":GOSUB1:GOTO100
315 IFUT(1)=13THENPRINT"TOO DIFFICULT!":GOTO100
320 TT(UT(1))=0
330 PRINT"OK.":GOTO100
340 IFUT(1)<43THENPRINT"WHAT?":GOTO100
350 UT(1)=UT(1)-42:IFTT(UT(1))>0THENPRINT"YOU DON'T HAVE ANY!":GOTO100
360 TT(UT(1))=HE:IFHE=16ANDUT(1)=3ANDRA=0THEN370
361 IFHE=33ANDUT(1)=14ANDMM=0THEN375
362 GOTO330
370 PRINT"OK.":RA=1:SS=SS+5:M$="YOU THROW THE STONE, WHICH HITS HIM ON THE HEAD!":GOSUB1:GOTO100
375 PRINT"OK.":SS=SS+5:MM=1:M$="THE LITTLE BIRD WAKES UP. AS THANKS FOR RESCUING HIM, HE BRINGS YOU A RING.":GOSUB1
376 TT(4)=0:GOTO100
380 ONHEGOTO390,395,400,405,410,415,420,425,430,435,440,445,450,455,460,465
381 ONHE-16GOTO470,475,480,490,495,500,500,505,500,500,500,500,510,515,520,530
382 IFHE=33THEN535
390 M$="YOU'RE IN YOUR HUT.":GOSUB1:RETURN
395 M$="YOU'RE STANDING IN FRONT OF YOUR HUT.":GOSUB1:RETURN
400 PRINT"YOU'RE ON A ROCKY PATHWAY.":RETURN
405 M$="YOU'RE STANDING IN FRONT OF A DITCH.":GOSUB1:RETURN
410 PRINT"YOU ARE IN A CLEARING.":RETURN
415 M$="YOUR WAY IS BLOCKED BE A MASSIVE WEB.":GOSUB1
416 IFTT(5)=50THENM$="A GIANT SPIDER EMERGES FROM AMIDST THE WEB.":GOSUB1
417 RETURN
420 M$="YOU ARE AT A CROSSROADS.":GOSUB1:RETURN
425 M$="YOU ARE STANDING NEXT TO A TREE.":GOSUB1:RETURN
430 M$="YOU ARE IN THE CITY OF FOTEREN.":GOSUB1
431 IFTT(11)=50THENM$="A BLACKSMITH IS MAKING A SWORD.":GOSUB1
432 RETURN
435 M$="YOU'RE IN A NARROW ALLEY.":GOSUB1:RETURN
440 M$="HALF OF THE ROYAL FLOCK IS ON THE ROAD.":GOSUB1:RETURN
445 M$="YOU ARE IN FRONT OF THE CASTLE GATE. THE DRAWBRIDGE IS RAISED.":GOSUB1:RETURN
450 M$="YOU ARE IN THE ROYAL COURT. YOU STAND BEFORE THE KING.":GOSUB1
451 IFKI=1THEN4000
452 M$="HE SAYS TO YOU: "+CHR$(34)
453 M$=M$+"BOLD KNIGHT FREE MY DAUGHTER FROM THE CLUTCHES OF THE TERRIBLE DRAGON!"+CHR$(34):GOSUB1:RETURN
455 M$="YOU ARE IN A MISERABLE DIRTY HOVEL. THERE IS AN OLD WOMAN HERE.":GOSUB1
456 IFTT(2)<50THENM$="SHE SAYS TO YOU: "+CHR$(34)+"I AM STARVING, PLEASE GIVE ME FOOD!"+CHR$(34):GOSUB1
457 RETURN
460 M$="YOU ARE ASCENDING A STEEP MOUNTAIN.":GOSUB1:RETURN
465 M$="YOU ARE IN A DEEP MOUNTAIN PASS.":GOSUB1
466 IFRA=0THENM$="THERE IS A ROGUE IN THE DISTANCE.":GOSUB1
467 RETURN
470 M$="YOU ARE ON THE EDGE OF A WINDSWEPT PRECIPICE.":GOSUB1:RETURN
475 M$="YOU ARE ON THE EDGE OF A PRECIPICE NEAR THE ENTRANCE TO A CAVE.":GOSUB1
476 M$="THE PATH LEADS TO A WALL OF ROCK.":RETURN
480 M$="YOU ARE IN THE CAVERN OF THE DRAGON. IT'S GIGANTIC!":GOSUB1
481 IFSA=0THENPRINT"HERE IS THE DRAGON."
482 IFSA=1THENM$="ITS HEEL IS TRAPPED UNDER A ROCK.":GOSUB1
483 IFSA=2THENM$="YOU ATTACK ITS EXPOSED HEEL.":GOSUB1
484 IFKI=0THENM$="THE PRINCESS IS LIES DEAD IN THE CORNER.":GOSUB1
485 RETURN
490 M$="YOU ARE ON A ROCK LEDGE FROM WHICH YOU CAN REACH THE CAVE ROOF.":GOSUB1:RETURN
495 M$="YOU ARE ABOVE THE CAVE. THERE IS A ROCK BRIDGE THAT CROSSES THE CAVE.":GOSUB1:RETURN
500 M$="YOU ARE IN A DARK FOREST.":GOSUB1:RETURN
505 PRINT"YOU ARE BY A RIVER.":IFFU=0THENM$="THERE IS A MAN SPLASHING ABOUT IN THE WATER.":GOSUB1
506 RETURN
510 M$="YOU ARE IN AN ABANDONED SHED.":GOSUB1:RETURN
515 M$="YOU ARE OUTSIDE A MODEST HOMESTEAD.":GOSUB1:RETURN
520 M$="YOU ENTER THE HOME. THERE IS A TABLE HERE.":GOSUB1
521 IFTT(10)=50THENM$="THERE ARE THREE FLASKS ON THE TABLE.":GOSUB1:GOTO523
522 RETURN
523 M$="THE WITCH SAYS: "+CHR$(34)+"ONE OF THE FLASKS CONTAINS LIVING WATER. IF YOU PICK CORRECTLY "
525 M$=M$+"I WILL GIVE IT TO YOU."+CHR$(34):GOSUB1:RETURN
530 M$="THE DRAGON'S HEAD JUTS FROM THE ENTRANCE OF THE CAVE.":GOSUB1:RETURN
535 M$="YOU ARE UP A TREE. YOU SEE A NEST.":GOSUB1:RETURN
600 PRINT"YOU HAVE:":C=0:FORJ=1TO14:IFTT(J)=0THENPRINTTAB(5)U$(J+42):C=1
610 NEXTJ:IFC=0THENPRINTTAB(5)"NOTHING IMPORTANT"
620 GOTO100
630 PRINT"YOU SEE:":C=0:FORJ=1TO14:IFTT(J)=HETHENPRINTTAB(5)U$(J+42):C=1
635 NEXTJ:IFC=0THENPRINTTAB(5)"NOTHING IMPORTANT"
640 GOTO100
650 IFUT(1)=0THENPRINT"WHAT?":GOTO100
660 IFUT(1)<43THENPRINT"HUH?":GOTO100
665 IFTT(UT(1)-42)<>HEANDTT(UT(1)-42)<>0THEN675
670 ONUT(1)-42GOTO675,675,680,685,690,695,700,705,710,675,715,675,720,725
675 M$="YOU NOTICE NOTHING OF SIGNIFICANCE.":GOSUB1:GOTO100
680 M$="USEFUL FOR KONKING SOMEONE ON THE HEAD.":GOSUB1:GOTO100
685 IFHE=17THENPRINT"YOU'RE WEARING IT!":GOTO100
686 GOTO675
690 IFHE=31THENPRINT"HIT IT MAESTRO!":GOTO100
691 GOTO675
695 PRINT"IT'S FOR A LOCK.":GOTO100
700 PRINT"IT HAS A KEYHOLE.":GOTO100
705 M$="THIS AMOUNT OF GOLD WILL GET YOU A GOOD SWORD.":GOSUB1:GOTO100
710 IFHE=27THENPRINT"HERE IDIOT!":GOTO100
711 PRINT"IT IS USELESS HERE.":GOTO100
715 M$="THIS SHOULD DO THE TRICK BETTER THAN A STAFF!":GOSUB1:GOTO100
720 PRINT"TRY ROLLING IT.":GOTO100
725 M$="IT'S SLEEPING. FIND SOMEWHERE SAFE TO HIDE IT!":GOSUB1:GOTO100
730 IFUT(1)=57THEN740
735 PRINT"YOU CAN'T DO IT!":GOTO100
740 IFHE<>24ORFU=1THENM$="I DON'T SEE ANYONE DROWNING.":GOSUB1:GOTO100
745 FU=1:SS=SS+5:M$="THE MAN IS VERY GREATFUL TO YOU FOR SAVING HIS LIFE.":GOSUB1
750 M$="HE GIVES YOU A KEY.":GOSUB1:TT(6)=0:GOTO100
760 IFUT(1)=0THENPRINT"WHAT?":GOTO100
761 IFUT(1)<>44THENPRINT"DON'T BE RIDICULOUS!":GOTO100
762 IFTT(2)>0THEN766
765 TT(2)=50:GOTO330
766 PRINT"YOU DON'T HAVE ANY.":GOTO100
770 PRINT"SCORE:"STR$(SS)".":GOTO100
780 IFUT(1)<>58THEN735
785 IFHE=8THENHE=33:PRINT"OK.":GOTO90
790 PRINT"DON'T DO IT!":GOTO100
800 IFUT(1)=54THEN815
805 IFUT(1)=46THEN825
810 REM
815 IFTT(12)>0THEN766
820 PRINT"OK.":SS=SS+5:TT(12)=40:GOTO100
825 IFTT(4)>0THEN766
826 IFHE<>17THENPRINT"NOTHING HAPPENED.":GOTO100
830 PRINT"OK.":SS=SS+5:HE=18
835 M$="TWO GIANT EAGLES SWOOP DOWN AND LIFT YOU OVER THE CHASM.":GOSUB1:GOTO90
840 IFUT(1)=0THENPRINT"WHAT?":GOTO100
845 IFUT(1)<>49THEN735
850 IFTT(7)>0THEN766
851 IFLA=1THENPRINT"IT IS OPEN.":GOTO100
855 IFTT(6)>0THENPRINT"IT IS LOCKED.":GOTO100
860 PRINT"OK.":PRINT"THERE IS GOLD INSIDE!":LA=1:TT(8)=27:SS=SS+5:GOTO100
870 IFUT(1)<>49THEN735
875 IFTT(7)>0THEN766
880 IFLA=0THENPRINT"WHICH IS NOT OPEN.":GOTO100
885 PRINT"THE LID IS OPEN.":GOTO100
900 IFUT(1)-42>0THENIFTT(UT(1)-42)>0THEN766
905 PRINT"YOU LOOK CLOSER.":GOTO955
910 PRINT"DO YOU WANT TO GIVE UP (Y/N)?"
911 A$=INKEY$:IFA$="Y"THEN3010
912 IFA$<>"N"THEN911
913 GOTO100
920 M$="ACTIONS WOULD SPEAK LOUDER THAN WORDS RIGHT NOW!":GOSUB1:GOTO100
930 IFUT(1)<43THENPRINT"WHAT?":GOTO100
935 UT(1)=UT(1)-42:IFTT(UT(1))>0THEN766
940 IFUT(1)<>5THEN735
945 PRINT"OK.":M$="THE FLUTE MAKES A PLEASENT SOUND.":GOSUB1
950 IFHE=31ANDTT(10)=50THEN960
955 M$="NOTHING HAPPENS.":GOSUB1:GOTO100
960 M$="A LITTLE FLY FLITS ABOUT AND LANDS ON ONE OF THE FLASKS.":GOSUB1
970 M$="HE SHOWED YOU WHAT TO DO.":GOSUB1:TT(10)=31:SS=SS+5:GOTO100
980 IFUT(1)<59THEN735
985 UT(1)=UT(1)-58:IFHE=31ANDUT(1)=1THEN1015
990 IFHE=16ANDUT(1)=2THEN1020
995 IFHE=19ANDUT(1)=3THEN1030
1000 IFHE=6ANDUT(1)=4THEN1040
1005 IFUT(1)=5THENM$="WHAT DO YOU HAVE IN MIND?":GOSUB1:GOTO100
1010 GOTO735
1015 M$="THE WITCH CASTS SOME MAGIC...":GOSUB1:GOTO3000
1020 M$="BEFORE YOU CAN DO ANYTHING THE ROGUE SPOTS YOU AND ATTACKS!":GOSUB1:GOTO3000
1030 IFSA=2THENPRINT"YOU HAVE SLAIN THE FOUL BEAST.":GOTO100
1032 IFTT(11)>0THENM$="YOUR STUPID WEAPON HAS BECOME STUCK IN ITS SCALES...":GOSUB1:GOTO3000
1034 IFSA=0THENM$="AHHHHH! YOU NOW HAVE A MISFIT STUB OF A SWORD":GOSUB1
1035 IFSA=0THENM$="YOU CAN'T RESIST ITS FIRE...":GOSUB1:GOTO3000
1036 M$="IT BLOWS FLAME AT YOU, BUT YOU HAVE BEEN PROTECTED BY YOUR ARMOUR.":GOSUB1:SA=2
1038 SS=SS+5:GOTO100
1040 IFTT(5)<50THENPRINT"YOU ALREADY DID.":GOTO100
1041 IFTT(1)>0THENPRINT"WITH YOUR BARE HANDS?":GOTO100
1045 M$="YOU BEAT THE BEAST WITH YOUR STAFF. AS IT DIES, A FLUTE FALLS FROM ITS WEB. ":GOSUB1:SS=SS+5:TT(5)=0:GOTO100
1060 IFTT(9)>0THENPRINT"YOU DON'T HAVE ANY.":GOTO100
1065 PRINT"OK.":IFHE<>27ORTT(7)<50THEN955
1070 PRINT"YOU FIND A CHEST!":SS=SS+5:TT(7)=27:GOTO100
1100 IFUT(1)=52THEN1140
1110 IFUT(1)=51THEN1060
1120 IFUT(1)=53ANDHE=19THEN1030
1130 IFUT(1)=0THENPRINT"WHAT?":GOTO100
1135 GOTO735
1140 IFTT(10)>0THEN766
1141 IFHE<>19THENPRINT"HUH?":GOTO100
1145 IFKI=1THENPRINT"THE PRINCESS IS GONE.":GOTO100
1150 M$="WITH THE WATER OF LIFE YOU BRING THE PRINCESS BACK TO LIFE. SHE KISSES YOU IN GRATITUDE.":GOSUB1
1155 M$="NOW ALL YOU MUST DO IS ASK THE KING FOR HIS CONSENT TO WED.":GOSUB1:SS=SS+5:KI=1:GOTO100
1200 PRINT"HOW?":GOTO100
1210 IFUT(1)<43THENPRINT"WHAT?":GOTO100
1220 IFTT(UT(1)-42)>0THEN766
1230 IFUT(1)<>44ORHE<>14ORTT(12)<50THENPRINT"NO ONE NEEDS IT.":GOTO100
1240 M$="THE OLD WOMEN TOUCHES YOU GENENTLY ON THE CHEEK AND THEN GIVES YOU HER DEAD SON'S ARMOUR.":GOSUB1:SS=SS+5
1260 TT(12)=0:TT(2)=50:GOTO100
1280 IFUT(1)<>53ORHE<>9THEN735
1285 IFTT(8)>0THENPRINT"YOU HAVE NO MONEY.":GOTO100
1290 TT(8)=40:TT(11)=0:SS=SS+5:PRINT"OK.":GOTO100
1300 IFUT(1)<>55THEN735
1310 IFHE<>TT(13)THENPRINT"I DON'T SEE A ROCK.":GOTO100
1320 PRINT"OK.":M$="THE ROCK CRASHES INTO THE CAVE WITH A LOUD NOISE.":GOSUB1:TT(13)=50
1330 SA=1:SS=SS+5:GOTO100
1350 PRINT"WEEEEE!":IFHE<>4THEN955
1355 PRINT"YOU JUMPED THE GAP!":HE=5:GOTO90
2000 IFO(HE,UT(0))=0THENPRINT"YOU CAN'T GO THAT DIRECTION!":GOTO100
2005 IFHE=16ANDRA=0THEN1020
2006 IFO(HE,UT(0))=50THENPRINT"YOU FALL TO YOUR DOOM!":GOTO3000
2007 IFHE=4ANDUT(0)=3THENM$="YOUR WAY IS BLOCKED BY THE DITCH.":GOSUB1:GOTO100
2010 HE=O(HE,UT(0)):IFHE<>19ORSA=2THEN90
2020 M$="AS YOU ADVANCE, THE DRAGON HITS YOU IN THE SIDE.":GOSUB1
2025 IFTT(12)=40THENPRINT"LUCKY YOU HAVE ARMOUR!":GOTO90
2030 M$="EVEN THOUGH WEAKENED, IT BREAKS YOU IN TWO...":GOSUB1:GOTO3000
3000 FORI=1TO3000:NEXTI:PRINT"I'M SAD TO SAY, YOU'RE DEAD."
3010 PRINT"SCORE:"STR$(SS)".":PRINT"TRY AGAIN (Y/N)?"
3020 A$=INKEY$:IFA$="Y"THENRUN
3030 IFA$="N"THENEND
3040 GOTO3020
4000 M$="THE KING IS IN YOUR ETERNAL DEBT. HE GIVES YOU HIS DAUGHTER'S HAND IN MARRIAGE.":GOSUB1
4010 M$="A HUGE WEDDING FEAST IS BEING PREPARED. CONGRATULATIONS, YOU HAVE MASTERFULLY COMPLETED YOUR QUEST!":GOSUB1
4040 SS=SS+30:GOTO3010
5000 PRINT"";
5001 PRINT"";
5002 PRINT"ꀀ";
5003 PRINT"耀";
5004 PRINT"";
5005 PRINT"";
5006 PRINT"ꀀ";
5007 PRINT"ꀀ";
5008 PRINT"ꀀ";
5009 PRINT"쀀տ";
5010 PRINT"ҀҀՀ";
5011 PRINT"Ҁ";
5012 PRINT"heroڀ";
5013 PRINT"knightڀڀ";
5014 PRINT"ڀ߀";
5015 PRINT"      BY TIHOR MIKLOS 1985     ";:RETURN
10000 DATAN,S,E,W,UP,DOWN,ENTER,LEAVE,TAKE,GET,THROW,DROP,I,INVENT,L,LOOK,X,EXAMINE,PULL
10010 DATAEAT,SCORE,CLIMB,WEAR,DON,CUT,UNLOCK,LOCK,SEARCH,QUIT,SPEAK,SAY,DIG
10020 DATAPLAY,KILL,BEAT,USE,SAVE,GIVE,BUY,MOVE,PUSH,JUMP
10050 DATASTAFF,BREAD,STONE,RING,FLUTE,KEY,CHEST,GOLD,SHOVEL,FLASK,SWORD,ARMOUR,ROCK
10060 DATABIRD,MAN,TREE,WITCH,ROGUE,DRAGON,SPIDER,PRINCESS
10070 DATA1,1,3,50,50,50,50,50,29,50,50,50,21,8,50,50,60,60,60,60,60
10100 DATA0,,,,,,,2,1,,3,,,,1,,,,4,2,,,,,,,5,3,,,,0
10110 DATA6,,7,4,,,,,,5,,,,,,,,22,8,5,,,,,,,9,7,33,,,0
10120 DATA10,,15,8,,,,,,9,14,11,,,14,,12,,10,,,,,,,11,,,13,,13,0
10130 DATA0,12,,,,,,12,,,,,,,,10,,16,,9,16,,,,15,,17,,,15,,0
10135 DATA0,,50,16,,,,0
10140 DATA0,,19,,20,,19,,,,32,18,,,,18,,18,21,,,,,,,,,20,,,0
10150 DATA0,22,22,22,23,,,22,7,22,25,23,24,,,23,23,22,23,23,22,,,23,0
10160 DATA23,25,26,25,,,25,25,26,28,27,25,,,26,26,25,26,26,25,,,,0
10170 DATA26,30,28,29,,,28,22,,,26,,,,,28,25,,31,,,,31,0
10180 DATA0,,,30,,,,30,,15,,19,21,,19,,,,,,,8,,0
10200 REM MC-10 EDITS JIM GERRIE
