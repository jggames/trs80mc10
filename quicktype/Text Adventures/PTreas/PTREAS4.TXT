0 CLS:CLEAR500:DIMC1,CC,P,K:GOTO300
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
300 PRINT "********************************";
310 PRINT "        Q U E S T  V1.04"
320 PRINT "********************************";
330 M$="YOU WERE WALKING THROUGH THE WOODS ONE DAY AND YOU CAME ACROSS THE ENTRANCE OF A CAVE, COVERED WITH BRUSH.":GOSUB1
340 M$="PEOPLE SAY THAT MANY YEARS AGO A PIRATE HID HIS TREASURE IN THESE WOODS, BUT NO ONE HAS EVER FOUND IT.":GOSUB1
350 M$="IT MAY STILL BE HERE, FOR ALL I KNOW.":GOSUB1
360 PRINT:INPUT"    HIT enter TO CONTINUE";Q$
400 CLS:READ M9,T1,T2
500 DIM W(42),M(6,42)
520 FOR I=1 TO M9
530 READ N
540 IF I=N THEN 570
550 PRINT"DATABASE PROBLEM",I,N
560 STOP
570 FOR J=1 TO 6
580 READ M(J,I)
590 NEXT J
600 NEXT I
900 PRINT
905 M$="WHEN YOU ANSWER A QUESTION, I LOOK AT ONLY THE FIRST LETTER, "
906 M$=M$+"ALTHOUGH YOU CAN TYPE IN THE WHOLE WORD IF YOU WANT TOO.":GOSUB1
920 GOSUB 7500
1000 N=5:M0=0:M6=0:T=T1:P=0:P1=0:FOR J=1 TO M9:W(J)=0:NEXT J:PRINT:GOSUB 8000 
1400 REM
1420 M0=M0+1
1440 GOSUB 6000
1460 GOSUB 2000
1480 GOSUB 4000
1500 IF T>0 THEN 1400
1510 IF N<>5 THEN 1400
1710 GOSUB 3000
1720 PRINT
1730 M$="CONGRATULATIONS!! YOU GOT THE TREASURE OUT IN"+STR$(M0)+" MOVES AND YOU GOT"+STR$(S+10)+" POINTS!":GOSUB1
1760 PRINT"WANT TO HUNT AGAIN?";:A$="YN":A2=2
1780 GOSUB 5000 
1790 ON A1 GOTO 1000,9999,1760
2000 REM
2100 IF T<>N THEN RETURN
2110 IF T<0 THEN RETURN
2120 IF M6+5>M0 THEN RETURN
2200 PRINT"DO YOU WANT TO TAKE IT WITH YOU";
2210 A$="YN"
2220 A2=2
2230 GOSUB 5000 
2240 ON A1 GOTO 2300,2400
2250 PRINT"WELL?....."
2260 GOTO 2210
2300 T=-1:PRINT:M$="OK, LETS GET OUT OF HERE!":GOSUB1:RETURN
2400 PRINT
2410 M$="WE'LL LEAVE IT HERE AND YOU CAN EXPLORE SOME MORE.":GOSUB1
2420 M6=M0
2430 RETURN
3000 S=0
3010 IF T=-1 THEN S=S+5
3020 IF P=1 THEN S=S+10
3030 FOR J=2 TO M9
3040 S=S+W(J)
3050 NEXT J
3060 RETURN
4000 IF N=T2 THEN RETURN
4010 IF P=1 THEN RETURN
4020 IF T1=T2 THEN RETURN
4030 IF T<>-1 THEN RETURN
4040 IF N=16 THEN P=160
4050 IF P1>0 THEN P1=P1+1
4060 IF N=3 THEN P1=P1+1
4070 IF P1<15 THEN RETURN
4080 PRINT
4090 M$="SUDDENLY THE PIRATE LEAPS OUT OF THE GLOOM AND GRABS THE TREASURE FROM YOU!":GOSUB1
4100 M$="'HAH!', HE SHOUTS, 'YOU FOUND MY TREASURE, DID YOU?! WELL, I'LL HIDE IT BETTER THIS TIME!' "
4110 M$=M$+"AND HE DISAPPEARS INTO THE DARKNESS WITH THE TREASURE.":GOSUB1:P=1:T=T2:RETURN
5000 REM
5070 INPUT Q$
5090 Q$=LEFT$(Q$,1)
5120 FOR A1=1 TO A2
5121 IF Q$=MID$(A$,A1,1) THEN TM=A1:A1=A2:NEXT A1:A1=TM:RETURN
5140 NEXT A1
5150 A1=A2+1
5160 RETURN
6000 N9=N:N8=0
6070 GOSUB 7000
6090 IF N=1 THEN 6120
6100 N0=N
6110 A0=A1
6120 PRINT
6130 I=M(A1,N)
6200 IF I=-2 THEN I=N9
6210 IF D<>0 THEN PRINT"DEBUG";N;"TO";I
6220 IF I<500 THEN 6300
6240 I=I-500
6250 FOR J=0 TO 100
6260 NEXT J
6270 GOTO 6200
6300 ON INT(I/100) GOTO 6340,6370
6320 N=I
6330 GOTO 6400
6340 REM
6350 N=I-100
6355 IF T=-1 THEN N=N+1
6360 GOTO 6400
6370 REM
6380 N=I-200
6390 IF T=-1 THEN N=N+P
6400 IF N<>1 THEN 6500
6420 FOR J=1 TO 6
6430 M(J,N)=2
6440 NEXT J
6450 M(7-A0,N)=N0
6500 REM
6510 IF N8<>2 THEN GOSUB 8000 
6530 W(N)=1
6540 N8=N
6600 IF M(1,N)<>-2 THEN 6800
6660 I=M(6,N)
6670 J=-1
6680 IF M(4,N)>100*RND(0) THEN I=M(5,N)
6690 J=-1
6700 IF M(2,N)>100*RND(0) THEN I=M(3,N)
6710 IF D<>0 THEN PRINT" DEBUG BOUNCE TO";I
6730 GOTO 6200
6800 RETURN
7000 REM
7100 PRINT
7110 PRINT"WHICH WAY (UDNEWS PQ)";
7120 A$="NEUDWSPQ"
7130 A2=8
7140 GOSUB 5000
7145 IF A1=8 THEN INPUT"ARE YOU SURE";Q$:IF LEFT$(Q$,1)="Y" THEN END
7150 IF A1<8 THEN 7300
7160 PRINT"WHICH WAY DO YOU WANT TO GO?"
7180 GOSUB 7500
7200 GOSUB 8000 
7210 GOTO 7100
7300 IF A1<7 THEN 7400
7320 GOSUB 3000
7330 PRINT"YOU HAVE";S;"POINTS!"
7350 GOTO 7100
7400 RETURN
7500 PRINT:M$="TYPE N,S,E,W,U OR D FOR NORTH, SOUTH, EAST, WEST, UP OR DOWN. "
7510 M$=M$+"OTHERWISE TYPE P FOR SCORE, OR Q TO QUIT THE PROGRAM.":GOSUB1:RETURN
8000 I=INT(N/5)
8060 J=N-5*I+1
8100 ON I+1 GOTO 8200,8210,8220,8230,8240,8250,8260,8270,8280,8290
8200 ON J GOTO 9000,9010,9020,9030,9040
8210 ON J GOTO 9050,9060,9070,9080,9090
8220 ON J GOTO 9100,9110,9120,9130,9140
8230 ON J GOTO 9150,9160,9170,9180,9190
8240 ON J GOTO 9200,9210,9220,9230,9240
8250 ON J GOTO 9250,9260,9270,9280,9290
8260 ON J GOTO 9300,9310,9320,9330,9340
8270 ON J GOTO 9350,9360,9370,9380,9390
8280 ON J GOTO 9400,9410,9420,9430,9440
8290 ON J GOTO 9450,9460,9470,9480,9490
8400 IF T<>N THEN 8500
8410 PRINT
8420 PRINT"THE TREASURE IS HERE!!"
8500 IF T<>T2 THEN 8600
8510 IF T1=T2 THEN 8600
8520 IF T1<>N THEN 8600
8530 PRINT
8540 M$="A NOTE ON THE WALL SAYS PIRATES NEVER LEAVE THEIR TREASURE TWICE IN THE SAME PLACE!":GOSUB1
8600 RETURN
9000 REM
9003 DATA 42,23,12
9010 DATA 1,0,0,0,0,0,0
9011 M$="YOU'RE AT A DEAD END!":GOSUB1
9012 GOTO 8400
9020 DATA 2,-2,101,-2,0,0,0
9021 M$="YOU CAN'T GO IN THAT DIRECTION!":GOSUB1
9023 GOTO 8400
9030 DATA 3,33,2,1,10,106,4
9031 M$="A TUNNEL GOES NORTH-SOUTH. THERE IS AN OPENING TO THE WEST.":GOSUB1
9037 GOTO 8400
9040 DATA 4,3,30,2,11,2,1
9041 M$="YOU'RE ON THE BRINK OF A PIT.":GOSUB1
9047 GOTO 8400
9050 DATA 5,8,8,15,10,8,16
9051 M$="YOU'RE OUTSIDE THE CAVE. GO SOUTH TO ENTER.":GOSUB1
9053 GOTO 8400
9060 DATA 6,16,3,2,10,2,2
9061 M$="YOU'RE AT THE HOME OF THE GNOME-KING. FORTUNATELY, HE'S GONE FOR THE DAY.":GOSUB1
9067 GOTO 8400
9070 DATA 7,-2,101,-2,0,0,0
9071 M$="THE GNOME KING IS HERE! YOU'D BETTER GET OUT!":GOSUB1
9073 PRINT
9077 GOTO 8400
9080 DATA 8,18,18,15,10,18,9
9081 M$="YOU'RE LOST IN THE WOODS.":GOSUB1
9087 GOTO 8400
9090 DATA 9,-2,33,5,1,0,-2
9097 GOTO 8400
9100 DATA 10,-2,101,-2,0,0,0
9101 M$="YOU'RE NOT GOING TO GET FAR, DIGGING THROUGH ROCK.":GOSUB1
9103 PRINT
9104 GOTO 8400
9110 DATA 11,1,13,4,2,1,2
9111 M$="YOU'RE AT THE BOTTOM OF A PIT. A LITTLE STREAM FLOWS OVER THE ROCKS HERE.":GOSUB1
9113 GOTO 8400
9120 DATA 12,36,2,1,2,1,2
9121 M$="YOU'RE AT A DEAD END!":GOSUB1
9122 GOTO 8400
9130 DATA 13,2,37,2,1,11,14
9131 M$="YOU'RE AT A WIDE SPOT. THERE IS A SOOTY PATCH WHERE SOMEBODY HAS RESTED A TORCH AGAINST THE WALL."
9133 M$=M$+" THERE ARE JAGGED ROCKS ABOVE YOU.":GOSUB1
9134 GOTO 8400
9140 DATA 14,13,1,19,2,31,31
9142 M$="YOU'RE IN A CANYON. HIGH ON THE WALL ABOVE YOU IS SCRATCHED THE MESSAGE 'BILBO WAS HERE'":GOSUB1
9146 GOTO 8400
9150 DATA 15,-2,101,-2,0,0,0
9152 M$="YOU'RE NOT A BIRD. YOU CAN'T FLY!":GOSUB1
9154 PRINT
9156 GOTO 8400
9160 DATA 16,5,33,2,10,1,106
9162 M$="YOU'RE IN A LOW CHAMBER. A TIGHT TUNNEL GOES EAST, AND YOU CAN WALK TO THE SOUTH OR WEST.":GOSUB1
9166 M$="THERE IS LIGHT TO THE NORTH.":GOSUB1
9168 GOTO 8400
9170 DATA 17,-2,101,-2,0,0,0
9172 M$="IT'S A TIGHT SQUEEZE. YOU CAN'T GET PAST WITH THE TREASURE.":GOSUB1
9176 PRINT
9178 GOTO 8400
9180 DATA 18,-2,101,8,0,0,0
9182 M$="I DON'T THINK YOU CAN FIND THE CAVE.":GOSUB1
9184 GOTO 8400
9190 DATA 19,224,2,2,14,1,42
9192 M$="YOU'RE AT THE TOP OF A CLIMB. BELOW YOU A MESSAGE SAYS 'BILBO WAS HERE'":GOSUB1
9198 GOTO 8400
9200 DATA 20,226,1,2,2,25,2
9202 M$="YOU'RE AT THE NORTH SIDE OF A CHASM, TOO WIDE TO JUMP. RINGING ECHOES FROM BELOW ARE THE ONLY INDICATION OF DEPTH."
9208 GOSUB1:GOTO 8400
9210 DATA 21,1,226,2,2,38,25
9212 M$="YOU'RE IN XANADU. BELOW YOU ALPH, THE SACRED RIVER RUNS THROUGH CAVERNS MEASURELESS TO MAN, DOWN TO A SUNLESS SEA."
9218 GOSUB1:GOTO 8400
9220 DATA 22,-2,33,13,50,29,30
9222 GOTO 8500
9230 DATA 23,2,1,2,31,2,2
9232 M$="YOU'RE ON THE LEDGE ABOVE THE GUILLOTINE ROOM.":GOSUB1
9234 GOTO 8400
9240 DATA 24,-2,101,19,0,0,0
9242 M$="I HEAR THE GIANT THERE! YOU'D BETTER GO BACK!":GOSUB1
9246 PRINT
9248 GOTO 8400
9250 DATA 25,21,20,2,2,1,19
9252 M$="YOU'RE IN THE GIANT'S CAVERN. BETTER NOT BE HERE WHEN THE GIANT COMES!":GOSUB1
9256 GOTO 8400
9260 DATA 26,-2,65,-2,50,11,14
9262 M$="YOU'RE IN THE QUEST RESEARCH AND DEVELOPMENT AREA.":GOSUB1
9264 PRINT
9266 M$="I'M SORRY BUT VISITORS ARE NOT ALLOWED. YOU'LL HAVE TO LEAVE.":GOSUB1:PRINT:GOTO 8400
9270 DATA 27,2,40,2,2,21,20
9272 M$="YOU'RE IN THE CRYSTAL PALACE. THE WALLS RESONATE WITH AWESOME MUSIC.":GOSUB1
9273 DATA 28,-2,60,221,50,14,19
9280 GOTO 8400
9290 DATA 29,2,42,2,13,1,1
9292 M$="YOU'RE AT THE TOP OF A GIANT STALACTITE. YOU COULD SLIDE DOWN, BUT YOU COULDN'T CLIMB BACK UP.":GOSUB1
9298 GOTO 8400
9300 DATA 30,34,34,2,1,4,2
9302 M$="YOU'RE IN A LITTLE GROTTO. THERE IS A BOOK HERE CALLED JANE'S FIGHTING SHIPS, DATED 1763.":GOSUB1
9308 GOTO 8400
9310 DATA 31,14,14,23,2,1,2
9312 M$="YOU'RE IN THE GUILLOTINE ROOM. A SHARP ROCK BALANCES PRECARIOUSLY ON THE LEDGE ABOVE YOU.":GOSUB1
9318 GOTO 8400
9320 DATA 32,-2,101,516,0,0,0
9322 M$="YOU'RE IN A CHUTE, SCRAMBLING DOWN THE ROCKS! NO WAY TO STOP!":GOSUB1:FORZZ=1TO1500:NEXT:PRINT"HANG ON!"
9326 PRINT
9328 GOTO 8400
9330 DATA 33,2,1,2,1,116,3
9332 M$="THE TIGHT TUNNEL TURNS A CORNER.":GOSUB1
9334 GOTO 8400
9340 DATA 34,1,35,2,1,30,30
9342 M$="YOU'RE IN A LITTLE TWISTY MAZE.":GOSUB1
9344 GOTO 8400
9350 DATA 35,2,1,2,37,34,36
9352 M$="YOU'RE IN A LITTLE TWISTING MAZE.":GOSUB1
9354 GOTO 8400
9360 DATA 36,35,2,1,37,34,12
9362 M$="YOU'RE IN A TWISTING LITTLE MAZE.":GOSUB1
9364 GOTO 8400
9370 DATA 37,2,1,35,2,13,2
9372 M$="YOU'RE IN A TWISTY LITTLE MAZE.":GOSUB1
9374 GOTO 8400
9380 DATA 38,2,21,2,116,1,2
9381 M$="YOU'RE IN A PREHISTORIC DWELLING. ON THE WALL ARE DRAWINGS OF BISON DONE IN RED CLAY. THE FLOOR IS"
9383 M$=M$+" STREWN WITH BONES, THE REMAINS OF ANCIENT RITUALS. A SMALL TUNNEL GOES THROUGH THE FLOOR.":GOSUB1
9387 GOTO 8400
9390 DATA 39,2,40,2,32,21,26
9392 M$="YOU'RE IN A BLACK HOLE. THE FORCE OF GRAVITY IS OVERWHELMING.":GOSUB1
9396 GOTO 8400
9400 DATA 40,40,40,2,2,40,41
9402 M$="YOU'RE IN THE LABYRINTHE.":GOSUB1
9404 GOTO 8400
9410 DATA 41,40,40,40,2,40,39
9412 M$="YOU'RE IN THE LABYRINTHE. IT'S VERY DARK IN HERE.":GOSUB1
9416 GOTO 8400
9420 DATA 42,28,28,28,28,28,28
9422 M$="YOU'RE IN THE ASHRAM. INCENSE IS HEAVY IN THE AIR, AND ALL DIRECTIONS SEEM THE SAME.":GOSUB1
9428 GOTO 8400
9430 GOTO 8400
9440 GOTO 8400
9450 GOTO 8400
9460 GOTO 8400
9470 GOTO 8400
9480 GOTO 8400
9490 GOTO 8400
9999 END
10000 REM ADAPTED FROM BYTE MAG
10110 REM PROG BY ROGER CHAFFEE
10120 REM MC-10 MODS BY JGERRIE
