10 '****************************
20 'PROGRAM:WILLIAMSBURG ADVENT.
30 'AUTHOR :MIKE HUGHEY
40 '  DISTRIBUTED FOR THE DRAGON
50 '  MICROCOMPUTER BY:
60 '  microdeal LTD (CORNWALL)
70 '  ALL RIGHTS RESERVED
80 '  COPYRIGHT (C) CLOAD 1981
90 '****************************
100 GOSUB1890:GOTO1850
110 GOTO150
120 CLEAR250
130 DIML$(30),A(4,30),M$(3,30),B(3,30):MZ=1
140 GOSUB1840:GOSUB1220:LINEINPUTK$:B=1:CLS
150 CLS:PRINT"LOCATION: ";L$(B):C=0:PRINT"YOU SEE: ";:FORA=1TO30
160 IFB(3,A)=1ANDB(1,A)=B THENPRINTTAB(10)M$(2,A):C=1
170 NEXTA:IFC=0THENPRINT"NOTHING OF INTEREST"
180 C=0:PRINT"DIRECTIONS: ";:IFA(1,B)<>0THENPRINT"NOR ";:C=1
190 IFA(2,B)<>0THENPRINT"SOU ";:C=1
200 IFA(3,B)<>0THENPRINT"EAS ";:C=1
210 IFA(4,B)<>0THENPRINT"WES";:C=1
220 IFC=0THENPRINT"???";
230 PRINT:PRINTSTRING$(32,"=");
240 INPUT"WHAT NOW ";M$:PRINT" ";
250 IFM$="N"THENM$="GO NOR"ELSEIFM$="S"THENM$="GO SOU"
260 IFM$="E"THENM$="GO EAS"ELSEIFM$="W"THENM$="GO WES"
270 IFM$="LOO"ORM$="LOOK"THEN150
280 IFLEN(M$)<3THENPRINT"?? WHAT ??":GOTO240
290 N$=LEFT$(M$,3):FORA=(LEN(M$)-2)TO1STEP-1:IFMID$(M$,A,1)=" "THEN300ELSENEXTA
300 O$=MID$(M$,A+1,3):IFN$="INV"THEN390ELSEIFN$="KIL"THEN1150
310 IFO$="STU"ANDN$<>"SHO"ANDN$<>"LOO"THENPRINT"THE STUDENT DOESN'T NOTICE.     HE KEEPS STUDYING FURIOUSLY.":GOTO240
320 IFN$="HEL"THEN440ELSEIFN$="GO "ORN$="WAL"THEN530ELSEIFN$="BUY"THEN1120
330 IFN$="LOO"ORN$="EXA"THEN740ELSEIFN$="GET"ORN$="TAK"ORN$="STE"THEN790
340 IFN$="DRO"ORN$="LEA"ORN$="PUT"THEN920ELSEIFN$="SAY"THEN980
350 IFN$="GIV"THEN960ELSEIFN$="SIT"THEN1080
360 IFN$="PUS"ORN$="PUL"ORN$="MOV"THEN1000ELSEIFN$="OPE"ORN$="UNL"THEN1070ELSEIFN$="PAY"ORN$="TIP"THEN1090
370 IFN$="BRE"THEN1160ELSEIFN$="REA"THEN1180ELSEIFN$="SHO"THEN1190ELSEIFN$="EAT"THEN1100
380 PRINT"I DON'T UNDERSTAND THE VERB !":GOTO240
390 PRINT"YOU HAVE THE FOLLOWING ITEMS:"
400 C=0:FORA=1TO30
410 IFB(1,A)=30THENPRINTTAB(2)M$(2,A):C=1
420 NEXTA:IFC=0THENPRINTTAB(2)"NOTHING AT ALL"
430 GOTO240
440 IFB=1THENPRINT"TRY THINGS LIKE INVENTORY,      LOOK, HELP, ETC.":GOTO240
450 IFB=21THENPRINT"BEWARE EVIL SPIRITS!":GOTO240
460 IFB=28ANDZM=0THENPRINT"A FRIENDLY GREETING CAN WORK    WONDERS !":GOTO240
470 IFB=14THENPRINT"YOU'RE VERY TIRED!":GOTO240
480 IFB=23THENPRINT"EXAMINE THINGS!":GOTO240
490 IFB=26THENPRINT"IT PAYS TO BE HONEST.":GOTO240
500 IFB=29THENPRINT"THEY CAN'T GET YOU IF THEY      DON'T SEE YOU!":GOTO240
510 IFB=4ORB=7THENPRINT"STUDENTS GET IN FREE!":GOTO240
520 PRINT"I'M AS CONFUSED AS YOU!":GOTO240
530 IFO$="BUI"ANDB=23THENB=25:GOTO150
540 IFB=17THEN550:ELSEIFO$="NOR"ANDA(1,B)<>0THENIF(B<>26ORDI=0ORPA=1)THENB=A(1,B):GOTO150:ELSECLS:PRINT"THE CHEF SEES YOU RUNNING OUT   THE DOOR WITHOUT PAYING, GETS   ANGRY, AND THROWS HIS BUTCHER   KNIFE AT YOU.":GOTO1140
550 IFB=17THEND$="NENWSSSWNNE":IFMZ>0THENIFLEFT$(O$,1)=MID$(D$,MZ,1)THENMZ=MZ+1
560 IFMZ=12THENIFZZ=0THENZZ=1:B(1,29)=17:B(1,30)=17:GOTO150
570 IFB=17THENIFZZ=0THENGOTO150
580 IFO$="SOU"ANDA(2,B)<>0THENB=A(2,B):GOTO150
590 IFO$="EAS"ANDA(3,B)<>0THENB=A(3,B):GOTO150
600 IFO$="TAB"THENIFB=26THEN1080
610 IFMZ=12AND(O$="NOR"ORO$="SOU"ORO$="EAS"ORO$="WES")THENMZ=0:B(1,30)=0:B(1,29)=0:GOTO150
620 IFMZ=12ANDO$="PAT"THENPRINT"YOU LEAVE THE MAZE AND BOARD A  BUS WHICH TAKES YOU HOME.":FORI=1TO999:NEXT:IFB(1,29)=30THENCLS:PRINT"CONGRATULATIONS!!!!":PRINT"YOU HAVE SOLVED THIS ADVENTURE!!":END:ELSEPRINT"YOU FAILED TO GET THE TREASURE!!:END
630 IFO$="WES"ANDA(4,B)<>0THENB=A(4,B):GOTO150
640 IFO$="BED"THENPRINT"O.K.":PRINT"THE BED COLLAPSES AND REVEALS A SECRET PASSAGE!":M$(2,28)="BROKEN BED":A(1,14)=16:FORI=1TO1000:NEXT:GOTO150
650 IFO$="TAV"ANDB=6THENB=26:GOTO150
660 IFO$="WAL"ANDB=24THENCLS:PRINT"LOCATION: IN THE PALACE GARDENS YOU SEE: FLOWERS":PRINT"DIRECTIONS: NOR EAS WES":PRINTSTRING$(32,"-");:PRINT"WHAT NOW ?":FORI=1TO700:NEXT:CLS:PRINT"A POLICEMAN SEES YOU, YELLS     'THIEF!', AND SHOOTS YOU.":GOTO1140
670 IFO$="SHO"ANDB=4THENIFB(1,4)=30THENB=5:GOTO150:ELSEPRINT"LADY SAYS: I CAN'T LET YOU IN   WITHOUT A TICKET!":GOTO240
680 IFO$="SHO"ANDB=9THENB=29:GOTO150
690 IFO$="CAP"ANDB=7THENIFB(1,4)=30THENB=18:GOTO150:ELSEPRINT"YOU NEED A TICKET!"
700 IFO$="GAT"ANDB(1,13)=12THENB=13:GOTO150
710 IF(O$="GRA"ORO$="STA")ANDB=21ANDB(1,8)=21THENB=27:GOTO150
720 IFO$="SCR"THENB=15:GOTO150
730 PRINT"YOU CAN'T . .":GOTO240
740 FORA=1TO30:IFO$=M$(1,A)THEN760ELSENEXTA
750 PRINT"I DON'T UNDERSTAND THE NOUN!":GOTO240
760 IFB(1,A)<>B ANDB(1,A)<>30THENPRINT"IT ISN'T AROUND . .":GOTO240
770 IFM$(3,A)="Z"THENPRINT"YOU SEE NOTHING OF INTEREST!":GOTO240
780 PRINTM$(3,A):GOTO240
790 IFO$="UP"ANDB=26ANDA(1,26)=0THENA(1,26)=6:PRINT"O.K.":FORI=1TO500:NEXTI:GOTO150:ELSEFORA=1TO30:IFO$=M$(1,A)THEN810ELSENEXTA
800 GOTO750
810 IFB(1,A)=30THENPRINT"YOU ALREADY HAVE IT !":GOTO240
820 IFB(1,A)<>B THENPRINT"THAT ITEM ISN'T AROUND !":GOTO240
830 IFO$="GUN"THENIFB=3THENCLS:PRINT"THE ALARM SYSTEM GOES OFF;      POLICE RUSH IN AND SHOOT TO     KILL!":GOTO1140
840 IFB(2,A)=0THENPRINT"YOU CAN'T . .":GOTO240
850 C=0:FORD=1TO30:IFB(1,D)=30THENC=C+1
860 NEXTD:IFC>2THENPRINT"YOU CAN'T CARRY ANY MORE !":GOTO240
870 IFO$="STO"ANDB=21ANDB(1,2)=21THENIFB(1,20)=30THENB(1,8)=21:PRINT"THE GHOST OF BRUTON PARISH      APPEARS, BUT HE IS WARDED OFF   BY YOUR CROSS":B(1,A)=30:B(3,A)=1:FORI=1TO2000:NEXT:GOTO150:ELSECLS:PRINT"THE GHOST OF BRUTON PARISH"
880 IFO$="STO"ANDB=21ANDB(1,2)=21THENPRINT"APPEARS AND DRAINS YOUR SOUL!":GOTO1140
890 IFO$="CRO"THENIFB(1,21)<29ORBC=1THENM$(2,20)="SILVER CROSS":IFDL=0THENPRINT"THE LADY CALLS THE POLICE, WHO  SHOOT YOU DEAD!":GOTO1140:ELSEGOTO900:ELSECLS:PRINT"YOU WERE CAUGHT IN THE ACT.     POLICE COME RUSHING IN AND      SHOOT ON SIGHT":GOTO1140
900 PRINT"O.K.  . . YOU'VE GOT IT !":B(1,A)=30:B(3,A)=1
910 GOTO240
920 FORA=1TO30:IFO$=M$(1,A)THEN940ELSENEXTA
930 GOTO750
940 IFB(1,A)<>30THENPRINT"YOU DON'T HAVE THAT ITEM !":GOTO240
950 PRINT"O.K.":B(1,A)=B:GOTO240
960 IFB=28ANDO$="BOO"ANDZM=0ANDB(1,10)=30THENZM=1:PRINT"THE ORGANIST THANKS YOU AND     GIVES YOU SOMETHING":B(1,11)=30:B(1,10)=0:GOTO240
970 GOTO920
980 IF(O$="HEL"ORO$="HI")ANDB=28ANDZM=0THENPRINT"ORGANIST SAYS: I WISH I HAD MY  MUSIC.":GOTO240
990 PRINT"O.K.       NOTHING HAPPENS.":GOTO240
1000 IFO$="STO"ANDB=21ANDB(1,2)=21THENIFB(1,20)=30THENB(1,8)=21:PRINT"THE GHOST OF BRUTON PARISH      APPEARS, BUT HE IS WARDED OFF   BY YOUR CROSS":FORI=1TO2000:NEXT:GOTO110:ELSEPRINT"THE GHOST OF BRUTON PARISH      APPEARS AND KILLS YOU":GOTO1140
1010 IFO$="CHA"ANDB=13ANDCH=0THENCH=1:PRINT"THE EAST WALL VANISHES,         REVEALING A HALL TO THE EAST!":A(3,13)=14:FORI=1TO1000:NEXT:GOTO150
1020 PRINT"NOTHING HAPPENS !":GOTO240
1030 FORA=1TO36:IFO$=M$(1,A)THEN1050ELSENEXTA
1040 GOTO750
1050 IFB(1,A)<>B ANDB(1,A)<>30THENPRINT"THAT ITEM ISN'T AROUND":GOTO240
1060 PRINT"O.K. . . NOTHING HAPPENS !":GOTO240
1070 IFO$="GAT"ANDB(1,11)=30THENIFOG=0THENOG=1:PRINT"O.K.":B(1,12)=0:B(1,13)=12:FORI=1TO500:NEXT:GOTO150:ELSEPRINT"IT'S ALREADY OPEN!":GOTO240
1080 IFDI=0THENIF(O$="DOW"ORO$="TAB")ANDB=26THENPRINT"THE WAITER SERVES YOU DINNER.":B(1,17)=26:DI=1:A(1,26)=0:FORI=1TO500:NEXT:GOTO150:ELSEPRINT"YOU CAN'T.":GOTO240:ELSEPRINT"O.K.":GOTO240
1090 IFO$="WAI"ANDB=26ANDDI=1THENPRINT"THE WAITER GIVES YOU A QUARTER  IN CHANGE AND SARCASTICALLY     SAYS, 'ABOUT ALL THAT WILL BUY  YOU IS A MAP AT SCRIBNER'S!'":B(1,19)=30:B(1,18)=0:PA=1:GOTO240
1100 IFO$="DIN"ANDDI=1THENPRINT"THAT HIT THE SPOT!":B(1,17)=0:GOTO240
1110 PRINT"YOU CAN'T.":GOTO240
1120 IFO$="MAP"THENIFB(1,19)=30THENPRINT"THE CLERK TAKES YOUR QUARTER    AND GIVES YOU A MAP.":B(1,26)=30:B(1,19)=0:GOTO240:ELSEIFB(1,18)=30THENPRINT"THE CLERK SAYS SHE DOESN'T HAVE CHANGE FOR A FIFTY.":GOTO240:ELSEPRINT"THAT'S NOT FOR SALE.":GOTO240
1130 PRINT"SORRY, THAT'S NOT FOR SALE!":GOTO240
1140 PRINT:PRINT"Y O U   A R E   D E A D !":PRINT:INPUT"HIT ENTER TO RESTART";M$:RUN
1150 PRINT"HOW ???":GOTO240
1160 IFO$="CAM"AND(B(1,20)=30ORB(1,20)=B)THENPRINT"O.K. IT WAS NO PROBLEM!":M$(2,21)="BROKEN CAMERA":BC=1:GOTO240
1170 PRINT"SORRY, THAT'S NOT POSSIBLE !":GOTO240
1180 PRINT"SAY 'LOOK' TO READ THINGS.":GOTO240
1190 IFB(1,5)=30ORB(1,5)=B THEN1200:ELSEPRINT"YOU'VE NOTHING TO SHOOT WITH!":GOTO240
1200 IFO$="STU"ANDB=23ANDB(1,3)=23THENB(1,3)=0:B(1,4)=23:PRINT"YOU BLOW HIS ARM OFF.           HE PICKS THE ARM UP, LOOKS AT   HIS WATCH, AND YELLS, 'OH MY,   I'M LATE FOR CLASS.'  HE RUNS   OFF AND DROPS SOMETHING AS HE   LEAVES.":GOTO240
1210 PRINT"NOBODY NOTICES.":IFO$="LAD"ANDDL=0THENDL=1:M$(2,22)="DEAD "+LEFT$(M$(2,22),15):GOTO240:ELSEGOTO240
1220 FORA=1TO30:READL$(A),A(1,A),A(2,A),A(3,A),A(4,A):NEXTA
1230 DATA"ON DUKE-OF-GLOUCESTER STREET",10,0,2,19
1240 DATA"ON DUKE-OF-GLOUCESTER STREET",0,3,4,1
1250 DATA"IN THE STOCKADE",2,0,0,0
1260 DATA"ON DUKE-OF-GLOUCESTER STREET",0,0,6,2
1270 DATA"IN THE MUSIC TEACHER'SSHOP",4,0,0,0
1280 DATA"ON DUKE-OF GLOUCESTER STREET",8,0,7,4
1290 DATA"IN FRONT OF THE       COLONIAL CAPITOL",0,0,0,6
1300 DATA"ON A RUSTIC PATH",9,6,0,0
1310 DATA"IN FRONT OF A GIFT    SHOP",0,8,0,11
1320 DATA"ON THE PALACE GREEN",12,1,0,0
1330 DATA"ON A SIDE STREET",0,0,9,12
1340 DATA"BEFORE THE GOVERNOR'S PALACE",0,10,11,24
1350 DATA"IN THE MAIN HALL",0,12,0,0
1360 DATA"IN A SMALL BEDROOM",0,0,0,13
1370 DATA"IN SCRIBNER'S BOOK    STORE",0,22,0,0
1380 DATA"IN A SECRET PASSAGE",17,14,0,0
1390 DATA"IN THE MAZE",17,17,17,17
1400 DATA"IN THE CAPITOL        BUILDING",0,0,0,7
1410 DATA"ON DUKE-OF-GLOUCESTER STREET",20,0,1,22
1420 DATA"IN FRONT OF BRUTON    PARISH CHURCH",0,19,0,21
1430 DATA"IN A CEMETERY",0,0,20,0
1440 DATA"ON DUKE-OF-GLOUCESTER STREET",0,0,19,23
1450 DATA"AT THE COLLEGE OF     WILLIAM AND MARY",0,0,22,0
1460 DATA"NEXT TO THE WALL OF   THE PALACE",0,0,12,0
1470 DATA"IN THE WREN BUILDING",0,0,23,0
1480 DATA"IN THE KING'S ARMS    TAVERN",6,0,0,0
1490 DATA"IN A GRAVE",0,21,28,0
1500 DATA"IN THE CHURCH",0,0,0,27
1510 DATA"IN A GIFT SHOP",0,9,0,0
1520 DATA"YOU",0,0,0,0
1530 FORA=1TO30:READM$(1,A),M$(2,A),M$(3,A),B(1,A),B(2,A),B(3,A):NEXTA
1540 DATA"MUS","EERIE ORGAN MUSIC","IT SEEMS TO BE COMING FROM      INSIDE THE CHURCH",20,0,1
1550 DATA"STO","GRAVESTONE","WRITING SAYS:                      THEY RE-MOVED QUEEN ANNE        FOR HIDING SOMETHING",21,1,1
1560 DATA"STU","STUDENT","HE IS STUDYING FURIOUSLY.",23,0,1
1570 DATA"ID","STUDENT ID","Z",0,1,1
1580 DATA"CAN","CANNON","IT APPEARS TO BE LOADED",23,1,1
1590 DATA"TAV","KING'S ARMS TAVERN","SIGN SAYS: GOOD EATING",6,0,1
1600 DATA"SHO","MUSIC TEACHER'S SHOP","Z",4,0,1
1610 DATA"GRA","OPEN GRAVE WITH                 STAIRS LEADING DOWN","Z",0,0,1
1620 DATA"ORG","ORGANIST","HE IS PLAYING EERIE ORGAN       MUSIC",28,0,1
1630 DATA"BOO","BOOK OF ORGAN MUSIC","WRITING ON COVER SAYS: THIS     ADVENTURE IS A PRODUCT OF       DAWSON ADVENTURES, INC.",5,1,11640 DATA"KEY","SET OF KEYS","Z",0,1,1
1650 DATA"GAT","MAIN GATE","THERE IS A KEYHOLE",12,0,1
1660 DATA"GAT","OPEN GATE","Z",0,0,1
1670 DATA"SIG","SIGN","IT SAYS:                          PALACE CLOSED FOR RENOVATION",12,0,1
1680 DATA"TAB","TABLE","Z",26,0,1
1690 DATA"WAI","WAITER","HE SEEMS EAGER TO HELP YOU!",26,0,1
1700 DATA"DIN","DINNER","IT LOOKS GOOD.",0,1,1
1710 DATA"BIL","FIFTY DOLLAR BILL","Z",30,1,1
1720 DATA"QUA","QUARTER","Z",0,1,1
1730 DATA"CRO","CHEAP IMITATION                   SILVER CROSSES","A SIGN ABOVE THE CROSSES SAYS:  THESE PREMISES UNDER CONSTANT   SURVEILLANCE",29,1,1
1740 DATA"CAM","SURVEILLANCE CAMERA","WRITING: MANUFACTURED BY RONCO  INDUSTRIES, INC.",29,0,1
1750 DATA"LAD","LITTLE OLD LADY                   MINDING THE STORE","SHE'S VERY UGLY",29,0,1
1760 DATA"CHA","QUEEN ANNE CHAIR","Z",13,0,1
1770 DATA"GUN","GUNS OF ALL KINDS","THEY ARE PROTECTED BY AN ALARM  SYSTEM",3,0,1
1780 DATA"SCR","SCRIBNER'S","Z",22,0,1
1790 DATA"MAP","MAP OF THE MAZE","THE WAY TO THE CENTER IS N,E,N, W,S,S,S,W,N,N,E",0,1,1
1800 DATA"PLA","PLACARD","IT SAYS: MAPS ON SALE!",15,0,1
1810 DATA"BED","BED","Z",14,0,1
1820 DATA"HOR","*GOLDEN HORSESHOE*","Z",0,1,1
1830 DATA"PAT","PATH","IT LEADS AWAY FROM THE PALACE!",0,0,1
1840 RETURN
1850 PMODE0:PCLEAR1
1860 CLS:PRINT@37,"WILLIAMSBURG ADVENTURE":PRINT@99,"YOU HAVE JUST ENTERED";:PRINT@129,"COLONIAL WILLIAMSBURG.  YOUR    GOAL: TO FIND AND BRING HOME    THE FAMOUS *GOLDEN HORSESHOE*.  IT IS HIDDEN SOMEWHERE IN THE   CITY.  TELL THE COMPUTER WHAT"
1870 PRINT" YOU WANT TO DO USING TWO-WORD   COMMANDS SUCH AS 'GO NORTH'.    USE THE COMMAND 'HELP' IF YOU   GET STUCK. GOOD LUCK!"
1880 PRINT:PRINTTAB(6)"HIT =ENTER= TO START":GOTO120
1890 CLS(1):PRINT@138,CHR$(128)"microdeal" CHR$(128);
1900 PRINT@203,"PRESENTS";
1910 PRINT@294,"FOR YOUR ENJOYMENT";:PRINT@326,"AN ADVENTURE IN -";
1920 FORI=50 TO 150:SOUNDI,1:NEXTI
1930 CLS(0):PRINT@265,"williamsburg";
1940 SOUND 200,30
1950 CLS
1960 PRINT@230,"INTRODUCTION (Y/N)?"
1970 X$=INKEY$:IFX$="" THEN 1970
1980 IF X$="N" THEN RETURN
1990 IF X$<>"Y" THEN1970 
2000 CLS
2010 PRINT
2020 PRINT"     WILLIAMSBURG ADVENTURE"
2030 PRINT
2040 PRINT"WELCOME TO A microdeal ARMCHAIR"
2050 PRINT"ADVENTURE.  ADVENTURE GAMES"
2060 PRINT"TRANSPORT YOU TO FAR LANDS AND"
2070 PRINT"EXOTIC PLACES.  YOU CAN FIND"
2080 PRINT"YOURSELF FIGHTING GHOSTS,"
2090 PRINT"SEARCHING FOR HIDDEN TREASURE"
2100 PRINT"ALL FROM THE COMFORT OF YOUR"
2110 PRINT"ARMCHAIR."
2120 PRINT"
2130 PRINT"  PRESS ANY KEY TO CONTINUE:"
2140 X$=INKEY$:IFX$="" THEN 2140
2150 CLS
2160 PRINT
2170 PRINT"
2180 PRINT"USE TWO WORD COMMANDS LIKE GET"
2190 PRINT"BOOK, OPEN DOOR, SHUT WINDOW"
2200 PRINT"ETC.  IF THE WORDS ARE NOT"
2210 PRINT"CORRECT, USE OTHERS.  YOU CAN"
2220 PRINT"ALSO USE N,S,E OR W TO GO IN THE";
2230 PRINT"APPROPRIATE DIRECTION."
2240 PRINT
2250 PRINT"PRESS ANY KEY TO BEGIN YOUR"
2260 PRINT"ARMCHAIR ADVENURE."
2270 PRINT
2280 PRINT"          GOOD LUCK,"
2290 PRINT
2300 PRINT"    'COS YOU'LL NEED IT !!! "
2310 X$=INKEY$:IFX$="" THEN 2310
2320 RETURN
