0 CLEAR1000:DIMC1,CC,M$:GOTO30
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 M$=N$+", YOUR ATTRIBUTES ARE:":IFM(7)>0THENM$=M$+" MAGIC"+STR$(M(7))
11 IFS(7)>0THENM$=M$+" STRENGTH"+STR$(S(7))
12 IFW(7)>0THENM$=M$+" WISDOM"+STR$(W(7))
13 IFMO>0THENM$=M$+" WEALTH"+STR$(MO)
14 GOSUB1:RETURN
30 GOSUB4480
40 CLS:IFM(7)>0ORS(7)>0ORW(7)>0THEN100
70 M$="THE ADVENTURE HAS ENDED. YOU'VE EXHAUSTED ALL YOUR POWERS. "
80 M$=M$+"YOU FOUGHT BRAVELY AND WELL BUT COULD NOT ENDURE.":GOSUB1:PRINT"FAREWELL...":END
100 GOSUB10
110 GOSUB4990
120 GOSUB3420
130 M=0:IFZ>1THENIFRND(0)>.5THENGOSUB2740
150 GOSUB1900
170 GOTO40
180 REM
190 REM MELEE RESOLUTION
200 RO=INT(RND(0)*6)+1
210 VI=0
220 IF(DI<0ANDRO>ABS(DI))OR(DI>0ANDRO<=DI)OR(DI=0ANDRO<4)THENVI=1
230 GOSUB4990
240 RETURN
250 REM
260 REM MONSTER SUBROUTINE
270 IFQ=0THENRETURN
280 ONQGOSUB310,320,330,340,350
300 RETURN
310 M$="THERE IS AN ANGRY WARLOCK IN THE ROOM. HE HAS A MAGIC "
311 M$=M$+"RATING OF"+STR$(M(1))+", HIS STRENGTH IS"+STR$(S(1))+" AND HIS WISDOM IS"+STR$(W(1)):GOSUB1:RETURN
320 M$="THE ROOM CONTAINS A FIRE BREATHING FEARBRINGER. "
321 M$=M$+"HIS WISDOM IS"+STR$(W(2))+" WHILE HE HAS A STRENGTH RATING OF"+STR$(S(2))
322 M$=M$+" AND HIS MAGIC SKILL IS"+STR$(M(2)):GOSUB1:RETURN
330 M$="HORRORS! YOU'VE STUMBLED IN ON THE HIDING PLACE OF AN AWFUL "
331 M$=M$+"SOULTHREAT. YOU CAN SEE AT A GLANCE HIS STRENGTH IS"+STR$(S(3))
332 M$=M$+" HIS MAGIC ABILITY RATES"+STR$(M(3))+" AND HIS WISDOM IS"+STR$(W(3)):GOSUB1:RETURN
340 PRINT"YOU'VE TRIPPED IN THE DARK.":GOSUB4990:M$="SOMETHING AWAKES. OH, YOU'RE FACE "
341 M$=M$+"TO FACE WITH GRAVELPIT, THE KNEECRUSHER, WHO HAS MAGIC OF"+STR$(M(4))
342 M$=M$+" STRENGTH OF"+STR$(S(4))+" AND WISDOM OF"+STR$(W(4)):GOSUB1:RETURN
350 M$="THIS ROOM HOLDS THE DREADED ENEMY OF ALL WHO ENTER THE "
351 M$=M$+"CASTLE, WOLVLING OF WOLF GLASS WITH A STRENGTH OF"+STR$(S(5))+", "
352 M$=M$+"WISDOM OF"+STR$(W(5))+" AND MAGIC OF"+STR$(M(5)):GOSUB1:RETURN
360 REM
370 REM END OF GAME
380 PRINT"YOU HAVE STUMBLED ON TO THE"
390 PRINT"MARSHY MUD SURROUNDING THE"
400 PRINT"BLACK LAGOON UNDERNEATH THE"
410 PRINT"CASTLE. TO ESCAPE FROM THE"
420 PRINT"CASTLE YOU MUST FIGHT THE"
430 PRINT"GUARDIAN OF THE BLACK LAGOON."
450 PRINT"THE FIGHT MUST INVOLVE ALL"
460 PRINT"ATTRIBUTES...AND YOU'LL NEED"
470 PRINT"A TOTAL OF 10 TO ESCAPE..."
490 IFMO>0THENM$="YOU HAVE $"+STR$(MO)+"WORTH OF GOLD":GOSUB1
500 GOSUB4990
510 M$="THE GUARDIAN'S ATTRIBUTES: MAGIC"+STR$(M(6))+" STRENGTH"+STR$(S(6))+" WISDOM"+STR$(W(6)):GOSUB1
550 GOSUB10
590 GOSUB4990
600 IFMO<100THEN790
610 PRINT"YOU CAN BUY ATTRIBUTE POINTS"
620 PRINT"FOR $100 EACH..."
630 PRINT"IF YOU WANT TO BUY ANY, ENTER"
640 PRINT"THE INITIAL OF THE ATTRIBUTE"
650 PRINT"FOLLOWED BY THE NUMBER OF"
660 PRINT"THE ATTRIBUTE YOU WANT."
670 PRINT"ENTER 'N' WHEN YOU'VE GOT"
680 PRINT"ALL THE ATTRIBUTES YOU WANT"
690 PRINT"YOU HAVE $"MO:INPUT"ATTRIBUTE (M, S OR W)";E$
700 IFE$="N"THEN790
710 INPUT"AMOUNT TO SPEND";AM
720 IFMO-AM<1ORAM<100THEN710
730 MO=MO-AM
740 IFE$="M"THENM(7)=M(7)+INT(AM/100)
750 IFE$="S"THENS(7)=S(7)+INT(AM/100)
760 IFE$="W"THENW(7)=W(7)+INT(AM/100)
770 PRINT"MAGIC"M(7)"STRENGTH"S(7)"WISDOM"W(7)
775 GOSUB4990
780 IFMO>99THEN690
790 CLS
800 PRINT"NOW FOR THE ULTIMATE TEST..."
820 PRINT"PRESS <ENTER> WHEN YOU'RE"
830 INPUT"BRAVE ENOUGH TO FIGHT";E$
840 CLS
850 PRINT"FIRST, MAGIC..."
860 PRINT"YOU:"M(7)"GUARDIAN"M(6)
870 DI=ABS(M(7)-M(6))
880 M$="THE DIFFERENCE IS"+STR$(DI)
890 IFM(7)>M(6)THENM$=M$+" IN YOUR FAVOUR"
900 IFM(6)>M(7)THENM$=M$+" AND THE GUARDIAN HAS THE EDGE"
910 GOSUB1:GOSUB4990
920 K=M(6)+M(7)
930 CO=INT(RND(0)*K)+1
940 M$="THIS ROUND CARRIES A PENALTY OF"+STR$(CO)+" ATTRIBUTE POINTS":GOSUB1:GOSUB4990
960 GOSUB2600
970 DI=M(7)-M(6)
980 IFDI>5THENDI=DI-6:GOTO980
990 IFDI<-5THENDI=DI+6:GOTO990
1000 GOSUB190
1010 IFVI=1THENM(7)=M(7)+CO:PRINT"AND YOU'VE WON...AND SO"
1020 IFVI=0THENM(7)=M(7)-CO:PRINT"AND YOU LOST...AND SO"
1030 IFM(7)<1THENM(7)=0
1040 PRINT"NOW HAVE"M(7)"MAGIC POINTS..."
1060 PRINT"HIT <ENTER> WHEN YOU'RE READY"
1070 INPUT"TO CONTINUE THIS EPIC STRUGGLE";E$
1080 CLS
1090 PRINT"NOW IT'S TIME FOR A MATCH OF"
1100 PRINT"STRENGTH, WHERE YOUR RATING"
1110 PRINT"IS"S(7)"AND THE GUARDIAN'S"
1120 PRINT"STRENGTH RATING IS"S(6)
1130 DI=ABS(S(7)-S(6))
1140 M$="THE DIFFERENCE IS"+STR$(DI)
1150 IFS(6)>S(7)THENM$=M$+" IN THE GUARDIAN'S FAVOUR"
1160 IFS(7)>S(6)THENM$=M$+" IN YOUR FAVOUR, "+N$
1170 GOSUB1:GOSUB4990
1180 K=S(6)+S(7)
1190 CO=INT(RND(0)*K)+1
1200 M$="THIS ROUND CARRIES A PENALTY OF"+STR$(CO)+" ATTRIBUTE POINTS":GOSUB1:GOSUB4990
1220 DI=S(7)-S(6)
1230 GOSUB2600
1240 IFDI>5THENDI=DI-6:GOTO1240
1250 IFDI<-5THENDI=DI+6:GOTO1250
1260 GOSUB190
1270 IFVI=1THENS(7)=S(7)+CO:PRINT"YOU'RE THE VICTOR, AND SO"
1280 IFVI=0THENS(7)=S(7)-CO:PRINT"YOU'RE THE LOSER, AND SO"
1290 IFS(7)<0THENS(7)=0
1300 PRINT"YOU NOW HAVE"S(7)"STRENGTH POINTS"
1320 PRINT"PRESS THE <ENTER> KEY WHEN"
1330 PRINT"YOU HAVE STOPPED TREMBLING"
1340 PRINT"ENOUGH TO FACE THE THIRD, AND"
1350 INPUT"FINAL CHALLENGE..";E$
1370 CLS
1380 PRINT"NOW IT'S TIME FOR A MATCH OF"
1390 PRINT"WISDOM, WHERE YOUR RATING"
1400 PRINT"IS"W(7)"AND THE GUARDIAN'S"
1410 PRINT"WISDOM RATING IS"W(6)
1420 DI=ABS(W(7)-W(6))
1430 M$="THE DIFFERENCE IS"+STR$(DI)
1440 IFW(6)>W(7)THENM$=M$+" IN THE GUARDIAN'S FAVOUR"
1450 IFW(7)>W(6)THENM$=M$+" IN YOUR FAVOUR, "+N$
1460 GOSUB1:GOSUB4990
1470 K=W(6)+W(7)
1480 CO=INT(RND(0)*K)+1
1490 PRINT"NOW, THIS FINAL CHALLENGE"
1500 PRINT"CARRIES A HUGE PENALTY"
1510 PRINT"OF"CO"ATTRIBUTE POINTS":GOSUB4990
1520 DI=W(7)-W(6)
1530 IFDI>5THENDI=DI-6:GOTO1530
1540 IFDI<-5THENDI=DI+6:GOTO1540
1550 GOSUB190
1560 GOSUB2600
1570 IFVI=1THENW(7)=W(7)+CO:M$="AND YOU DEFEATED THE GUARDIAN!":GOSUB1
1580 IFVI=0THENW(7)=W(7)-CO:M$="BUT THE GUARDIAN GOT THE BETTER OF YOU, "+N$+"!!":GOSUB1
1590 IFW(7)<0THENW(7)=0
1600 GOSUB4990
1610 CLS
1620 M$="AND NOW, AT THE END OF THE FINAL BATTLE,":GOSUB1
1640 GOSUB10
1670 SU=M(7)+W(7)+S(7)
1680 M$="WELL, "+N$+", YOUR ATTRIBUTE TOTAL IS"+STR$(SU):GOSUB1
1700 GOSUB4990
1710 IFSU<10THEN1810
1720 M$="YOU NEEDED AT LEAST 10 POINTS TO WIN THE GAME, AND YOU'VE DONE IT, "+N$+"!":GOSUB1:GOSUB4990
1750 CLS
1760 M$="YOU'VE SUCCEEDED, O HERO OF THESE DARK AND DANGEROUS TIMES. I HEREBY DUB THEE SIR "+N$+"... ARISE...":GOSUB1
1800 END
1810 PRINT"UNFORTUNATELY, YOU DID NOT"
1820 PRINT"END UP WITH THE 10 POINTS"
1830 PRINT"YOU NEEDED, SO IT IS ALL OVER"
1840 PRINT"YOU FAIL TO ESCAPE THE CLUTCHES"
1850 PRINT"OF THE GUARDIAN....":GOSUB4990
1860 PRINT"YOU FOUGHT VALIANTLY, BUT WILL"
1870 PRINT"NOW BE CONSUMED...."
1880 GOSUB4990:GOSUB2600:GOSUB2600:END
1890 REM
1900 REM ACTION
1910 D=4:IFMID$(B$(Z),9,1)="0"ORMID$(B$(Z),9,1)=" "THEND=1:GOTO1915
1912 GOTO1920
1915 IFRND(0)>.8ANDZ>1THENGOSUB2740:GOTO1910
1920 PRINT"WHAT DO YOU WANT TO DO NOW?"
1930 INPUTZ$:IFZ$="Q"THENEND
1940 IFZ$=""THENCLS:Z$="#"
1950 IFD=4ANDLEFT$(Z$,1)<>"F"THEND=0:GOTO2090
1960 IFLEFT$(Z$,1)="F"THEN2060
1970 IFZ$="N"AND LEFT$(B$(Z),2)="00"THENPRINTTAB(12)"NO EXIT":GOTO1920
1980 IFZ$="S"ANDMID$(B$(Z),3,2)="00"THENPRINT"THERE IS NO DOOR THAT WAY":GOTO1920
1990 IFZ$="E"ANDMID$(B$(Z),5,2)="00"THENPRINT"THAT IS NOT POSSIBLE":GOTO1920
2000 IFZ$="W"ANDMID$(B$(Z),7,2)="00"THENPRINT"YOU CAN'T WALK THROUGH WALLS!":GOTO1920
2010 IFZ$="N"THENZ=VAL(LEFT$(B$(Z),2)):RETURN
2020 IFZ$="S"THENZ=VAL(MID$(B$(Z),3,2)):RETURN
2030 IFZ$="E"THENZ=VAL(MID$(B$(Z),5,2)):RETURN
2040 IFZ$="W"THENZ=VAL(MID$(B$(Z),7,2)):RETURN
2050 IFLEFT$(Z$,1)<>"F"THENRETURN
2060 IFRIGHT$(B$(Z),1)="0"THENM$="THERE IS NOTHING TO FIGHT AGAINST":GOSUB1:GOTO1920
2070 IFZ$="FL"THEND=INT(RND(0)*2)
2080 IFD=1THENPRINT"WHICH DIRECTION?":GOTO1930
2090 IFD=0THENPRINT"NO!! YOU MUST STAND AND FIGHT"
2100 M$="WHICH CHARACTERISTIC WILL YOU FIGHT WITH? "
2110 M$=M$+"YOUR MAGIC IS"+STR$(M(7))+", STRENGTH"+STR$(S(7))+" AND WISDOM"+STR$(W(7)):GOSUB1
2120 INPUTZ$:IFZ$<>"M"ANDZ$<>"S"ANDZ$<>"W"THEN2120
2130 IFZ$="M"THENHU=M(7):MN=M(Q)
2140 IFZ$="S"THENHU=S(7):MN=S(Q)
2150 IFZ$="W"THENHU=W(7):MN=W(Q)
2160 DI=HU-MN
2170 IFDI>5THENDI=DI-6:GOTO2170
2180 IFDI<-5THENDI=DI+6:GOTO2180
2190 M$="THE FIGHT TABLE FOR THIS MELEE READS "+STR$(DI):GOSUB1
2200 CO=ABS(DI)+INT(RND(0)*6)+1
2210 GOSUB4990
2220 M$="THE MELEE CARRIES A COST/REWARD OF"+STR$(CO):GOSUB1
2230 FI=INT(RND(0)*2)
2240 IFFI=0THENM$="THE MONSTER ATTACKS AND THE FIGHT IS UNDERWAY":GOSUB1
2250 IFFI=1THENM$="YOU ATTACK FIRST, AND THE BATTLE IS JOINED...":GOSUB1
2260 GOSUB4990:GOSUB2600
2270 RO=INT(RND(0)*6)+1
2280 VI=0
2290 IF(DI<0ANDRO>ABS(DI))OR(DI>0ANDRO<=DI)OR(DI=0ANDRO<4)THENVI=1
2300 IFVI=1THENGOSUB2460
2310 IFVI=0THENGOSUB2530
2320 GOSUB4990
2330 PRINT"AFTER THAT FIGHT,"
2340 GOSUB10
2390 M$="AND THOSE OF THE "+M$(Q)+" ARE: "
2400 M$=M$+"MAGIC"+STR$(M(Q))+" STRENGTH"+STR$(S(Q))+" & WISDOM"+STR$(W(Q)):GOSUB1
2430 B$(Z)=LEFT$(B$(Z),8):B$(Z)=B$(Z)+"0"
2440 GOSUB4990:CLS
2450 GOTO2740
2460 REM HUMAN VICTORY
2470 IFQ=0THEND=1
2480 M$="YOU DEFEATED THE "+M$(Q):GOSUB1
2490 IFZ$="M"THENM(7)=M(7)+CO:M(Q)=M(Q)-CO:IFM(Q)<1THENM(Q)=0
2500 IFZ$="W"THENW(7)=W(7)+CO:W(Q)=W(Q)-CO:IFW(Q)<1THENW(Q)=0
2510 IFZ$="S"THENS(7)=S(7)+CO:S(Q)=S(Q)-CO:IFS(Q)<1THENS(Q)=0
2520 RETURN
2530 REM MONSTER VICTORY
2540 M$="THE "+M$(Q)+" DEFEATED YOU":GOSUB1
2550 IFZ$="M"THENM(Q)=M(Q)+CO:M(7)=M(7)-CO:IFM(7)<1THENM(7)=0
2560 IFZ$="W"THENW(Q)=W(Q)+CO:W(7)=W(7)-CO:IFW(7)<1THENW(7)=0
2570 IFZ$="S"THENS(Q)=S(Q)+CO:S(7)=S(7)-CO:IFS(7)<1THENS(7)=0
2580 RETURN
2590 REM
2600 REM FIGHT EFFECTS
2610 FORJ=1TORND(0)*10+2
2620 ON(INT(RND(0)*6+1))GOSUB2670,2680,2690,2700,2710,2720
2630 FORP=1TO100+RND(0)*100:NEXTP
2640 PRINT
2650 NEXTJ
2660 CLS:RETURN
2670 PRINT"    BASH!!!!":FORP=1TO100:NEXTP:RETURN
2680 PRINTTAB(10)"AAAAAARGHHH!":FORP=1TO20:NEXTP:RETURN
2690 PRINT"RIP":FORP=1TO100:NEXTP:PRINTTAB(20)"TEAR!":FORP=1TO100:NEXTP:RETURN
2700 FORE=1TO24:PRINT"!!! ";:NEXTE:FORP=1TO50:NEXTP:CLS:RETURN
2710 RETURN
2720 FORE=1TO3:PRINT"!*&&*@!!   ";:NEXTE:FORP=1TO20:NEXTP:RETURN
2730 REM
2740 REM CONTENTS
2750 K=2+INT(RND(0)*8)
2760 IFK=ZTHEN2750
2770 IFRIGHT$(B$(Z),1)<>"0"THENRETURN
2780 B$(K)=LEFT$(B$(K),8):B$(K)=B$(K)+MID$(B$(Z),9,1)
2790 B$(Z)=LEFT$(B$(Z),8):B$(Z)=B$(Z)+"0"
2800 IFRND(0)>.5THENRETURN
2820 CT=INT(RND(0)*5)+1
2830 ONCTGOSUB2870,2980,3100,3210,2870
2840 GOSUB4990
2850 RETURN
2860 REM
2870 CH=CH+1:IFCH=5THENRETURN
2880 M$="IN FRONT OF YOU IS A CHEST LABELLED WITH A LARGE #"+STR$(CH)+". WILL YOU OPEN IT?":GOSUB1
2910 GOSUB3380
2920 IFZ$="N"THENRETURN
2930 J=INT(RND(0)*3)
2940 IFJ=0THENCA=100+INT(RND(0)*300):M$="IT HOLDS DRAGON'S GOLD WORTH $"+STR$(CA):GOSUB1:MO=MO+CA:GOSUB4990:RETURN
2950 IFJ=1THENM$="A GOBLIN LEAPS OUT, STABBING YOU!":GOSUB1:LO=INT(RND(0)*6)+1
2951 IFJ=1THENMO=MO-INT(RND(0)*200):IFMO<1THENMO=0:GOSUB4990:RETURN
2960 IFJ=2THENM$="A STRANGE SMOKE COMES OUT MAKING YOU SLEEPY AND SAPPING YOUR MAGIC POWER":GOSUB1
2961 IFJ=2THENLO=INT(RND(0)*8)+1:M(7)=M(7)-LO:IFM(7)<1THENM(7)=0:GOSUB4990:RETURN
2970 REM
2980 IFPO=1THEN2820
2990 PO=1
3000 M$="YOU SEE A SMALL BOTTLE ENGRAVED WITH CURIOUS, TWISTED LETTERS...":GOSUB1
3020 M$="WILL YOU DRINK THE POTION WHICH YOU CAN SEE INSIDE THE BOTTLE?":GOSUB1
3040 GOSUB3380
3050 IFZ$="N"THENRETURN
3065 TE=RND(0)
3070 IFTE>.6THENM$="IT CONTAINED A POTION TO ENHANCE YOUR WISDOM":GOSUB1:W(7)=W(7)+INT(RND(0)*6)+1:GOSUB4990:RETURN
3080 M$="IT CONTAINED A POTION WHICH WEAKENS YOU FURTHER...":GOSUB1:GOSUB4990
3081 S(7)=S(7)-(INT(RND(0)*6)+1):IFS(7)<1THENS(7)=0:RETURN
3090 REM
3100 IFSC=1THENRETURN
3110 SC=1
3120 M$="YOU SEE A PAPYRUS SCROLL. DO YOU WISH TO TRY TO READ IT?":GOSUB1
3140 GOSUB3380:IFZ$="N"THENRETURN
3150 IFRND(0)>.5THENM$="YOU CANNOT UNDERSTAND THE LANGUAGE...":GOSUB1:GOSUB4990:RETURN
3160 M$="IT CONTAINS A MAGIC SPELL. DO YOU WISH TO READ IT?":GOSUB1
3170 GOSUB3380:IFZ$="N"THENRETURN
3175 TE=RND(0)
3180 IFTE>.5THENPRINT"IT WAS A BENEFICENT SPELL":GOSUB4990:M(7)=M(7)+INT(RND(0)*6)+1:RETURN
3190 PRINT"IT WAS AN EVIL SPELL":GOSUB4990:M(7)=0:S(7)=INT(S(7)/2):RETURN
3200 REM
3210 IFSA=1THEN2820
3220 SA=1
3230 M$="ON THE WALL IS A SMALL, GILDED SAFE, AND IN FRONT OF IT IS A KEY...":GOSUB1
3240 PRINT"DO YOU WANT TO OPEN THE SAFE?"
3250 GOSUB3380
3260 IFZ$="N"THENRETURN
3270 IFRND(0)>.3THEN3330
3280 M$="A SHRIEKING HARPY FLIES OUT AND SINKS ITS TEETH INTO YOUR THROAT!":GOSUB1
3290 GOSUB4990
3300 PRINT"YOU GRAPPLE WITH IT, AND...":GOSUB4990:PRINT"...FINALLY WRING ITS NECK"
3310 S(7)=S(7)-INT(RND(0)*6)+1:IFS(7)<1THENS(7)=0
3320 GOSUB4990:RETURN
3330 M$="A CHOIR OF ANGELIC VOICES IS HEARD...":GOSUB1:GOSUB4990
3340 PRINT"YOU ARE HEALED AND REFRESHED..."
3350 M(7)=M(7)+2:S(7)=S(7)+2:W(7)=W(7)+2
3360 GOSUB4990
3370 RETURN
3380 Z$=INKEY$
3390 IFZ$<>"N"ANDZ$<>"Y"THEN3380
3400 RETURN
3410 REM
3420 REM ROOM DESCRIPTIONS
3430 ON Z GOSUB3480,3570,3630,3680,3770,3850,3950,4080,4210,4270,4350,4440
3435 Q=0:TE=RND(0)
3440 IFTE>.81ANDZ>1THENQ=INT(RND(0)*6)
3442 IFTE>.81ANDZ>1THENB$(Z)=LEFT$(B$(Z),8)+MID$(STR$(Q),2,1):GOTO260
3450 E$=MID$(B$(Z),9,1):IFE$>"0"ANDE$<"6"THENQ=VAL(E$)
3460 IFQ>0THENGOSUB260
3470 RETURN
3480 PRINT"YOU ARE AT THE ENTRANCE TO AN"
3490 PRINT"ANCIENT, FORBIDDING-LOOKING"
3500 PRINT"CASTLE. YOU ARE STANDING ON"
3510 PRINT"THE NORTH SIDE OF THE CASTLE,"
3520 PRINT"AND AS YOU LOOK SOUTH, TOWARDS"
3530 PRINT"THE TUMBLING STRUCTURE, YOU"
3540 PRINT"NOTICE THE ENTRANCE PORTAL"
3550 PRINT"IS OPEN AND UNGUARDED."
3560 RETURN
3570 PRINT"YOU ARE IN THE ENTRANCE HALL,"
3580 PRINT"WHICH IS HUNG WITH RICH"
3590 PRINT"FABRICS. DOORS LEAD TO THE"
3600 PRINT"EAST AND THE SOUTH, AND THERE"
3610 PRINT"IS AN OPEN PORTAL TO THE WEST"
3620 RETURN
3630 PRINT"THIS IS ONLY A STORE ROOM."
3640 PRINT"THERE IS A SINGLE EXIT, BACK"
3650 PRINT"THE WAY YOU CAME IN,"
3660 PRINT"TO THE WEST"
3670 RETURN
3680 PRINT"THIS SMALL ROOM, WHICH"
3690 PRINT"FEATURES AN ORNATE SCULPTURE"
3700 PRINT"OF THE MOON GODDESS ON A"
3710 PRINT"PEDESTAL IN THE NORTH-EAST"
3720 PRINT"CORNER, IS THE ROYAL"
3730 PRINT"PRESENCE CHAMBER. DOORS LEAD"
3740 PRINT"TO THE SOUTH, THE WEST AND"
3750 PRINT"TO THE EAST..."
3760 RETURN
3770 PRINT"THE HALL OF PLOTS, A"
3780 PRINT"WOODEN-PANNELLED ROOM"
3790 PRINT"REDOLENT WITH WHISPERS AND"
3800 PRINT"RUMOURS, WITH EXITS TO THE EAST"
3810 PRINT"AND TO THE SOUTH FROM WHICH"
3820 PRINT"COMES THE SMELL OF SULPHUR AND"
3830 PRINT"A WEIRD CHANTING..."
3840 RETURN
3850 PRINT"YOU HAVE ENTERED THE WIZARD'S"
3860 PRINT"DEN, WITH A CAULDRON BUBBLING"
3870 PRINT"OVER A FIRE WITH GREEN FLAMES"
3880 PRINT"IN THE SOUTH-WEST CORNER."
3890 PRINT"THIS ROOM REEKS OF BURNING"
3900 PRINT"SULPHUR, AND THE ECHO OF"
3910 PRINT"ANCIENT SPELLS. YOU CAN LEAVE"
3920 PRINT"TO THE NORTH, THE SOUTH, OR"
3930 PRINT"TO THE EAST"
3940 RETURN
3950 PRINT"YOU FIND YOURSELF IN A QUIET"
3960 PRINT"PLACE, THE CASTLE'S PICTURE"
3980 PRINT"GALLERY, WITH A LARGE PAINTING"
3990 PRINT"OF THE LEGENDARY GUARDIAN OF"
4000 PRINT"THE BLACK LAGOON TO THE LEFT OF"
4010 PRINT"THE WINDOW IN THE EAST WALL."
4020 PRINT"THROUGH IT YOU SEE THE MULLIONED";
4030 PRINT"WINDOWS OF THE GREAT HALL ACROSS";
4040 PRINT"THE GARDEN. EXITS FROM THE"
4050 PRINT"GALLERY ARE TO THE NORTH AND"
4060 PRINT"TO THE WEST...":GOSUB4990
4070 RETURN
4080 PRINT"THIS IS THE MOST MAGNIFICENT"
4090 PRINT"ROOM IN THE CASTLE, THE GREAT"
4100 PRINT"HALL, WITH A MASSIVE HAMMERBEAM"
4110 PRINT"ROOF. YOU CAN LEAVE IT BY THE"
4120 PRINT"DOUBLE DOORS TO THE NORTH OR BY"
4130 PRINT"THOSE TO THE EAST BEHIND WHICH"
4140 PRINT"YOU CAN HEAR MUSIC PLAYING."
4150 PRINT"THROUGH THE WINDOWS TO THE WEST"
4160 PRINT"YOU CAN SEE THE CONTOURED"
4170 PRINT"GARDEN AND A ROOM BEYOND IT"
4180 PRINT"WITH MANY FINE PAINTINGS":GOSUB4990
4200 RETURN
4210 PRINT"SOUNDS OF A STRING QUARTET"
4220 PRINT"FILL THIS ROOM, THE MUSICIANS'"
4230 PRINT"CHAMBER. YOU CAN LEAVE BY"
4240 PRINT"DOORS TO THE WEST OR BY ONE"
4250 PRINT"TO THE SOUTH..."
4260 RETURN
4270 PRINT"YOU ARE NOW IN THE SANCTUARY"
4280 PRINT"OF SILENCE, A ROOM WHOSE"
4290 PRINT"CALMNESS MAY BE A DECEPTION."
4300 PRINT"THE ROOM IS DAMP AND COLD. AN"
4310 PRINT"EXIT LEAVES THE ROOM TO THE"
4320 PRINT"NORTH ";
4330 IFMID$(B$(10),3,2)="12"THENPRINT"AND TO THE SOUTH";
4340 PRINT:RETURN
4350 PRINT"THIS MUST BE THE VESTIBULE"
4360 PRINT"OF SIGHS, A DANK AND CLAMMY"
4370 PRINT"ROOM WHERE THE LEGEND SAYS THE"
4380 PRINT"GUARDIAN OF THE BLACK LAGOON"
4390 PRINT"CAN SOMETIMES BE HEARD AT"
4400 PRINT"NIGHT. THERE IS A DOOR TO THE"
4410 PRINT"NORTH ";
4420 IFMID$(B$(11),3,2)="12"THENPRINT"AND TO THE SOUTH";
4430 PRINT:RETURN
4440 GOTO370
4450 IFMID$(B$(Z),9,1)<>"0"THENQ=VAL(MID$(B$(Z),9,1):GOSUB260
4460 RETURN
4470 REM
4480 REM INITIALISE
4490 C1=RND(-(PEEK(9)*256+PEEK(10)))
4500 Z=1
4510 MO=0
4520 CH=0
4530 PO=0
4540 SC=0
4550 SA=0
4560 DIMB$(12),M$(6),M(7)
4570 CLS:PRINT"THE BANNOCHBURN LEGACY":PRINT
4580 PRINT"PLEASE ENTER YOUR FIRST NAME?":INPUTN$
4590 CLS:PRINT"HI THERE, ";N$
4600 PRINT"PLEASE STAND BY..."
4610 REM FILL ROOMS
4620 FORT=1TO12
4630 READB$(T)
4640 L=RND(0)
4650 Q$=STR$(INT(RND(0)*5)+1)
4660 IFT>1ANDT<11ANDL<.63THENB$(T)=B$(T)+RIGHT$(Q$,1)
4670 IFT>10ORL>=.63THENB$(T)=B$(T)+"0"
4680 NEXTT
4690 L=RND(0)
4700 IFL<.5THENB$(11)="091200000"
4710 IFL>=.5THENB$(10)="061200000"
4720 REM CREATE MONSTERS
4730 FORT=1TO6
4740 READM$(T)
4750 S(T)=INT(RND(0)*6)+1
4760 M(T)=INT(RND(0)*6)+1
4770 W(T)=INT(RND(0)*6)+1
4780 NEXTT
4790 REM HUMAN CHARACTERISTICS
4800 S(7)=INT(RND(0)*6)+1
4810 M(7)=INT(RND(0)*6)+1
4820 W(7)=INT(RND(0)*6)+1:RETURN
4830 REM ROOM DATA
4840 DATA "000200000"
4850 DATA "00080304"
4860 DATA "00000002"
4870 DATA "00070205"
4880 DATA "00060400"
4890 DATA "05100700"
4900 DATA "04000006"
4910 DATA "02000900"
4920 DATA "00110008"
4930 DATA "06000000"
4940 DATA "09000000"
4950 DATA "00000000"
4960 REM MONSTER DATA
4970 DATA"WARLOCK","FEARBRINGER","SOULTHREAT","KNEECRUSHER","WOLVLING","GUARDIAN"
4980 REM
4990 REM PAUSE ROUTINE
5000 PRINT"hit a key";
5010 M$=INKEY$:IFM$=""THEN5010
5020 FORC1=1TO9:PRINTCHR$(8);:NEXT:RETURN
