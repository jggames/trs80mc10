10 REM THE BANNOCHBURN LEGACY
11 REM
12 REM TYPED IN AND MODIFIED FOR THE
13 REM C64, AND BUG FIXED BY RIO BAAN
14 REM FROM SOME OLD MICROSOFT BASIC
15 REM GAMES BOOK - BY TIM HARTNELL
16 REM WHICH WAS PUBLISHED WAY BACK IN
17 REM 1983. OVER 17 YEARS AGO!
20 REM
30 GOSUB4480
40 PRINT"."
50 IFM(7)=0ANDS(7)=0ANDW(7)=0THENPRINT"THE ADVENTURE HAS ENDED"
51 IFM(7)=0ANDS(7)=0ANDW(7)=0THENPRINT"YOU HAVE EXHAUSTED ALL YOUR POWERS"
52 IFM(7)=0ANDS(7)=0ANDW(7)=0THENPRINT:PRINT"YOU FOUGHT BRAVELY AND WELL"
53 IFM(7)=0ANDS(7)=0ANDW(7)=0THENPRINT"BUT COULD NOT ENDURE.":PRINT:PRINT"
FAREWELL...":END
60 PRINTN$", YOUR ATTRIBUTES ARE:"
70 IFM(7)>0THENPRINT"   MAGIC:"M(7)
80 IFS(7)>0THENPRINT"STRENGTH:"S(7)
90 IFW(7)>0THENPRINT"  WISDOM:"W(7)
100 IFMO>0THENPRINT"WEALTH: $"MO
110 GOSUB4990
120 GOSUB3420
130 M=0:IFZ>1THENIFRND(1)>.5THENGOSUB2740
140 GOSUB4990
150 GOSUB1900
160 GOSUB4990
170 GOTO40
180 REM
190 REM MELEE RESOLUTION
200 RO=INT(RND(1)*6)+1
210 VI=0
220 IF(DI<0ANDRO>ABS(DI))OR(DI>0ANDRO<=DI)OR(DI=0ANDRO<4)THENVI=1
230 GOSUB4990
240 RETURN
250 REM
260 REM MONSTER SUBROUTINE
270 IFQ=0THENRETURN
280 ONQGOSUB310,320,330,340,350
290 GOSUB4990
300 RETURN
310 PRINT"THERE IS AN ANGRY WARLOCK IN":PRINT"THE ROOM. HE HAS A MAGIC"
311 PRINT"RATING OF"M(1):PRINT"HIS STRENGTH IS"S(1)"AND":PRINT"HIS WISDOM 
IS"W(1):RETURN
320 PRINT"THE ROOM CONTAINS A FIRE-":PRINT"BREATHING FEARBRINGER. HIS"
321 PRINT"WISDOM IS"W(2)"WHILE HE":PRINT"HAS A STRENGTH RATING OF"S(2)
322 PRINT"AND HIS MAGIC SKILL":PRINT"IS"M(2):RETURN
330 PRINT"HORRORS! YOU'VE STUMBLED IN ON":PRINT"THE HIDING PLACE OF AN AWF
UL"
331 PRINT"SOULTHREAT. YOU CAN SEE AT A":PRINT"GLANCE HIS STRENGTH IS"S(3)
332 PRINT"HIS MAGIC ABILITY RATES"M(3):PRINT"AND HIS WISDOM IS"W(3):RETURN

340 PRINT"YOU'VE TRIPPED IN THE DARK.":GOSUB4990:PRINT"SOMETHING AWAKES. O
H, YOU'RE FACE"
341 PRINT"TO FACE WITH GRAVELPIT, THE":PRINT"KNEECRUSHER, WHO HAS MAGIC OF
"M(4)
342 PRINT"STRENGTH OF"S(4)"AND WISDOM":PRINT"OF"W(4):RETURN
350 PRINT"THIS ROOM HOLDS THE DREADED":PRINT"ENEMY OF ALL WHO ENTER THE"
351 PRINT"CASTLE, WOLVLING OF WOLF GLASS":PRINT"WITH STRENGTH OF"S(5)" PLU
S"
352 PRINT"WISDOM OF"W(5)"AND":PRINT"MAGIC OF"M(5):RETURN
360 REM
370 REM END OF GAME
380 PRINT"YOU HAVE STUMBLED ON TO THE"
390 PRINT"MARSHY MUD SURROUNDING THE"
400 PRINT"BLACK LAGOON UNDERNEATH THE"
410 PRINT"CASTLE. TO ESCAPE FROM THE"
420 PRINT"CASTLE YOU MUST FIGHT THE"
430 PRINT"GUARDIAN OF THE BLACK LAGOON."
440 PRINT
450 PRINT"THE FIGHT MUST INVOLVE ALL"
460 PRINT"ATTRIBUTES...AND YOU'LL NEED"
470 PRINT"A TOTAL OF 10 TO ESCAPE..."
480 GOSUB4990:GOSUB4990

490 IFMO>0THENPRINT"YOU HAVE $"MO"WORTH OF GOLD"
500 GOSUB4990
510 PRINT:PRINT"THE GUARDIAN'S ATTRIBUTES:"
520 PRINT"   MAGIC:"M(6)
530 PRINT"STRENGTH:"S(6)
540 PRINT"  WISDOM:"W(6)
550 PRINT:PRINT"YOUR ATTRIBUTES ARE:"
560 PRINT"   MAGIC:"M(7)
570 PRINT"STRENGTH:"S(7)
580 PRINT"  WISDOM:"W(7)
590 GOSUB4990:GOSUB4990
600 IFMO<100THEN790
610 PRINT:PRINT"YOU CAN BUY ATTRIBUTE POINTS"
620 PRINT"FOR $100 EACH..."
630 PRINT"IF YOU WANT TO BUY ANY, ENTER"
640 PRINT"THE INITIAL OF THE ATTRIBUTE"
650 PRINT"FOLLOWED BY THE NUMBER OF"
660 PRINT"THE ATTRIBUTE YOU WANT."
670 PRINT"ENTER 'N' WHEN YOU'VE GOT"
680 PRINT"ALL THE ATTRIBUTES YOU WNT"
690 PRINT"YOU HAVE $"MO:INPUT"ATTRIBUTE (M, S OR W)";E$
700 IFE$="N"THEN790
710 INPUT"AMOUNT TO SPEND ";AM
720 IFMO-AM<1ORAM<100THEN710
730 MO=MO-AM
740 IFE$="M"THENM(7)=M(7)+INT(AM/100)
750 IFE$="S"THENS(7)=S(7)+INT(AM/100)
760 IFE$="W"THENW(7)=W(7)+INT(AM/100)
770 PRINT"MAGIC:"M(7)"STRENGTH:"S(7)"WISDOM:"W(7)
775 GOSUB4990
780 IFMO>99THEN690
790 PRINT"."
800 PRINT"NOW FOR THE ULTIMATE TEST..."
810 GOSUB4990
820 PRINT"PRESS RETURN WHEN YOU'RE"
830 INPUT"BRAVE ENOUGH TO FIGHT";E$
840 GOSUB4990:PRINT"."
850 PRINT:PRINT"FIRST, MAGIC..."
860 PRINT:PRINT"YOU:"M(7)"GUARDIAN"M(6)
870 DI=ABS(M(7)-M(6))
880 PRINT:PRINT"THE DIFFERENCE IS"DI
890 IFM(7)>M(6)THENPRINT"IN YOUR FAVOUR"
900 IFM(6)>M(7)THENPRINT"AND THE GUARDIAN HAS THE EDGE"
910 GOSUB4990
920 K=M(6)+M(7)
930 CO=INT(RND(1)*K)+1
940 PRINT:PRINT"THIS ROUND CARRIES A PENALTY"
950 PRINT"OF"CO"ATTRIBUTE POINTS":GOSUB4990
960 GOSUB2600
970 DI=M(7)-M(6)
980 IFDI>5THENDI=DI-6:GOTO980
990 IFDI<-5THENDI=DI+6:GOTO990
1000 GOSUB190
1010 IFVI=1THENM(7)=M(7)+CO:PRINT"AND YOU'VE WON...AND SO"
1020 IFVI=0THENM(7)=M(7)-CO:PRINT"AND YOU LOST...AND SO"
1030 IFM(7)<1THENM(7)=0
1040 PRINT"NOW HAVE"M(7)"MAGIC POINTS..."
1050 GOSUB4990
1060 PRINT"PRESS RETURN WHEN YOU'RE READY"
1070 INPUT"TO CONTINUE THIS EPIC STRUGGLE";E$
1080 PRINT"."
1090 PRINT"NOW IT'S TIME FOR A MATCH OF"
1100 PRINT"STRENGTH, WHERE YOUR RATING"
1110 PRINT"IS"S(7)"AND THE GUARDIAN'S"
1120 PRINT"STRENGTH RATING IS"S(6)
1130 DI=ABS(S(7)-S(6))
1140 PRINT:PRINT"THE DIFFERENCE IS"DI
1150 IFS(6)>S(7)THENPRINT"IN YOUR GUARDIAN'S FAVOUR"
1160 IFS(7)>S(6)THENPRINT"IN YOUR FAVOUR, "N$
1170 GOSUB4990
1180 K=S(6)+S(7)
1190 CO=INT(RND(1)*K)+1
1200 PRINT:PRINT"THIS ROUND CARRIES A PENALTY"
1210 PRINT"OF"CO"ATTRIBUTE POINTS":GOSUB4990
1220 DI=S(7)-S(6)
1230 GOSUB2600
1240 IFDI>5THENDI=DI-6:GOTO1240
1250 IFDI<-5THENDI=DI+6:GOTO1250
1260 GOSUB190
1270 IFVI=1THENS(7)=S(7)+CO:PRINT"YOU'RE THE VICTOR, AND SO"
1280 IFVI=0THENS(7)=S(7)-CO:PRINT"YOU'RE THE LOSER, AND SO"
1290 IFS(7)<0THENS(7)=0
1300 PRINT"YOU NOW HAVE"S(7)"STRENGTH POINTS"
1310 GOSUB4990
1320 PRINT:PRINT"PRESS THE RETURN KEY WHEN"
1330 PRINT"YOU HAVE STOPPED TREMBLING"
1340 PRINT"ENOUGH TO FACE THE THIRD, AND"
1350 INPUT"FINAL CHALLENGE..";E$
1360 GOSUB4990
1370 PRINT"."
1380 PRINT"NOW IT'S TIME FOR A MATCH OF"
1390 PRINT"WISDOM, WHERE YOUR RATING"
1400 PRINT"IS"W(7)"AND THE GUARDIAN'S"
1410 PRINT"WISDOM RATING IS"W(6)

1420 DI=ABS(W(7)-W(6))
1430 PRINT:PRINT"THE DIFFERENCE IS"DI
1440 IFW(6)>W(7)THENPRINT"IN THE GUARDIAN'S FAVOUR"
1450 IFW(7)>W(6)THENPRINT"IN YOUR FAVOUR, "N$
1460 GOSUB4990
1470 K=W(6)+W(7)
1480 CO=INT(RND(1)*K)+1
1490 PRINT"NOW, THIS FINAL CHALLENGE"
1500 PRINT"CARRIES A HUGE PENALTY"
1510 PRINT"OF"CO"ATTRIBUTE POINTS":GOSUB4990
1520 DI=W(7)-W(6)
1530 IFDI>5THENDI=DI-6:GOTO1530
1540 IFDI<-5THENDI=DI+6:GOTO1540
1550 GOSUB190
1560 GOSUB2600
1570 IFVI=1THENW(7)=W(7)+CO:PRINT"AND YOU DEFEATED THE GUARDIAN!"
1580 IFVI=0THENW(7)=W(7)-CO:PRINT"BUT THE GUARDIAN GOT THE"
1581 IFVI=0THENW(7)=W(7)-CO:PRINT"BETTER OF YOU, "N$"!!"
1590 IFW(7)<0THENW(7)=0
1600 GOSUB4990
1610 PRINT".":PRINT:PRINT
1620 PRINT"AND NOW, AT THE END OF THE"
1630 PRINT"FINAL BATTLE, YOUR POSITION"
1640 PRINT"IS:   MAGIC..."M(7)
1650 PRINT"     WISDOM..."W(7)
1660 PRINT"   STRENGTH..."S(7)
1670 SU=M(7)+W(7)+S(7)
1680 PRINT:PRINT"WELL, "N$", YOUR"
1690 PRINT"ATTRIBUTE TOTAL IS"SU
1700 GOSUB4990
1710 IFSU<10THEN1810
1720 PRINT"YOU NEEDED AT LEAST 10 POINTS"
1730 PRINT"TO WIN THE GAME, AND YOU'VE"
1740 PRINT"DONE IT, "N$"!":FORI=1TO5:GOSUB4990:NEXT
1750 PRINT".":PRINT:PRINT:PRINT
1760 PRINT"YOU'VE SUCCEEDED, O HERO OF"
1770 PRINT"THESE DARK AND DANGEROUS"
1780 PRINT"TIMES. I HEREBY DUB THEE"
1790 PRINT"SIR "N$".... ARISE..."
1800 END
1810 PRINT"UNFORTUNATELY, YOU DID NOT"
1820 PRINT"END UP WITH THE 10 POINTS"
1830 PRINT"YOU NEEDED, SO IT IS ALL OVER"
1840 PRINT:PRINT"YOU FAIL TO ESCAPE THE CLUTCHES"
1850 PRINT"OF THE GUARDIAN....":GOSUB4990
1860 PRINT"YOU FOUGHT VALIANTLY, BUT WILL"
1870 PRINT"NOW BE CONSUMED......"
1880 GOSUB4990:GOSUB2600:GOSUB2600:END
1890 REM
1900 REM ACTION
1910 D=4:IFMID$(B$(Z),9,1)="0"ORMID$(B$(Z),9,1)=" "THEND=1:GOTO1915
1912 GOTO1920
1915 IFRND(1)>.8ANDZ>1THENGOSUB2740:GOTO1910
1920 PRINT"WHAT DO YOU WANT TO DO NOW";
1930 INPUTZ$:IFZ$="Q"THENEND
1940 IFZ$=""THENPRINT".":Z$="#"
1950 IFD=4ANDLEFT$(Z$,1)<>"F"THEND=0:GOTO2090
1960 IFLEFT$(Z$,1)="F"THEN2060
1970 IFZ$="N"AND LEFT$(B$(Z),2)="00"THENPRINT,"NO EXIT":GOTO1920
1980 IFZ$="S"ANDMID$(B$(Z),3,2)="00"THENPRINT"THERE IS NO DOOR THAT WAY":G
OTO1920
1990 IFZ$="E"ANDMID$(B$(Z),5,2)="00"THENPRINT"THAT IS NOT POSSIBLE":GOTO19
20
2000 IFZ$="W"ANDMID$(B$(Z),7,2)="00"THENPRINT"YOU CAN'T WALK THROUGH WALLS
!":GOTO1920
2010 IFZ$="N"THENZ=VAL(LEFT$(B$(Z),2)):RETURN
2020 IFZ$="S"THENZ=VAL(MID$(B$(Z),3,2)):RETURN
2030 IFZ$="E"THENZ=VAL(MID$(B$(Z),5,2)):RETURN
2040 IFZ$="W"THENZ=VAL(MID$(B$(Z),7,2)):RETURN
2050 IFLEFT$(Z$,1)<>"F"THENRETURN
2060 IFRIGHT$(B$(Z),1)="0"THENPRINT"THERE IS NOTHING TO FIGHT AGAINST":GOT
O1920
2070 IFZ$="FL"THEND=INT(RND(1)*2)
2080 IFD=1THENPRINT"WHICH DIRECTION?":GOTO1930
2090 IFD=0THENPRINT"NO!! YOU MUST STAND AND FIGHT"
2100 PRINT:PRINT"WHICH CHARACTERISTIC WILL YOU FIGHT     WITH?"
2110 PRINT"YOUR MAGIC IS"M(7)", STRENGTH IS "S(7):PRINT"AND YOUR WISDOM IS
"W(7)
2120 INPUTZ$:IFZ$<>"M"ANDZ$<>"S"ANDZ$<>"W"THEN2120
2130 IFZ$="M"THENHU=M(7):MN=M(Q)
2140 IFZ$="S"THENHU=S(7):MN=S(Q)
2150 IFZ$="W"THENHU=W(7):MN=W(Q)
2160 DI=HU-MN
2170 IFDI>5THENDI=DI-6:GOTO2170
2180 IFDI<-5THENDI=DI+6:GOTO2180
2190 PRINT"THE FIGHT TABLE FOR THIS MELEE READS "DI
2200 CO=ABS(DI)+INT(RND(1)*6)+1
2210 GOSUB4990
2220 PRINT"THE MELEE CARRIES A COST/REWARD OF"CO
2230 FI=INT(RND(1)*2):GOSUB4990
2240 IFFI=0THENPRINT"THE MONSTER ATTACKS AND THE":PRINT"FIGHT IS UNDERWAY"

2250 IFFI=1THENPRINT"YOU ATTACK FIRST, AND THE":PRINT"BATTLE IS JOINED..."

2260 GOSUB2600
2270 RO=INT(RND(1)*6)+1
2280 VI=0
2290 IF(DI<0ANDRO>ABS(DI))OR(DI>0ANDRO<=DI)OR(DI=0ANDRO<4)THENVI=1
2300 IFVI=1THENGOSUB2460
2310 IFVI=0THENGOSUB2530
2320 GOSUB4990
2330 PRINT"AFTER THAT FIGHT, YOUR"
2340 PRINT"ATTRIBUTES ARE:"
2350 PRINT"   MAGIC:"M(7)
2360 PRINT"STRENGTH:"S(7)
2370 PRINT"& WISDOM:"W(7)
2380 GOSUB4990:PRINT
2390 PRINT"AND THOSE OF THE "M$(Q)" ARE:"
2400 PRINT"   MAGIC:"M(Q)
2410 PRINT"STRENGTH:"S(Q)
2420 PRINT"& WISDOM:"W(Q)
2430 B$(Z)=LEFT$(B$(Z),8):B$(Z)=B$(Z)+"0"
2440 GOSUB4990:GOSUB4990:PRINT"."
2450 GOTO2740
2460 REM HUMAN VICTORY
2470 IFQ=0THEND=1
2480 PRINT:PRINT"YOU DEFEATED THE "M$(Q)
2490 IFZ$="M"THENM(7)=M(7)+CO:M(Q)=M(Q)-CO:IFM(Q)<1THENM(Q)=0
2500 IFZ$="W"THENW(7)=W(7)+CO:W(Q)=W(Q)-CO:IFW(Q)<1THENW(Q)=0
2510 IFZ$="S"THENS(7)=S(7)+CO:S(Q)=S(Q)-CO:IFS(Q)<1THENS(Q)=0
2520 RETURN
2530 REM MONSTER VICTORY
2540 PRINT:PRINT"THE "M$(Q)" DEFEATED YOU"
2550 IFZ$="M"THENM(Q)=M(Q)+CO:M(7)=M(7)-CO:IFM(7)<1THENM(7)=0
2560 IFZ$="W"THENW(Q)=W(Q)+CO:W(7)=W(7)-CO:IFW(7)<1THENW(7)=0
2570 IFZ$="S"THENS(Q)=S(Q)+CO:S(7)=S(7)-CO:IFS(7)<1THENS(7)=0
2580 RETURN
2590 REM
2600 REM FIGHT EFFECTS
2610 FORJ=1TORND(1)*10+2
2620 ON(INT(RND(1)*6+1))GOSUB2670,2680,2690,2700,2710,2720
2630 FORP=1TO100+RND(1)*100:NEXTP
2640 PRINT:PRINT
2650 NEXTJ
2660 PRINT".":RETURN
2670 PRINT"    BASH!!!!":FORP=1TO100:NEXTP:RETURN
2680 PRINT,"AAAAAARGHHH!":FORP=1TO20:NEXTP:RETURN
2690 PRINT"RIP":FORP=1TO100:NEXTP:PRINT,,"TEAR!":FORP=1TO100:NEXTP:RETURN
2700 FORE=1TO20:PRINT"!!! ";:NEXTE:PRINT".":RETURN
2710 RETURN

2720 FORE=1TO3:PRINT"!*&&*@!!   ";:NEXTE:FORP=1TO20:NEXTP:RETURN
2730 REM
2740 REM CONTENTS
2750 K=2+INT(RND(1)*8)
2760 IFK=ZTHEN2750
2770 IFRIGHT$(B$(Z),1)<>"0"THENRETURN
2780 B$(K)=LEFT$(B$(K),8):B$(K)=B$(K)+MID$(B$(Z),9,1)
2790 B$(Z)=LEFT$(B$(Z),8):B$(Z)=B$(Z)+"0"
2800 IFRND(1)>.5THENRETURN
2810 PRINT
2820 CT=INT(RND(1)*5)+1
2830 ONCTGOSUB2870,2980,3100,3210,2870
2840 GOSUB4990
2850 RETURN
2860 REM
2870 CH=CH+1:IFCH=5THENRETURN
2880 PRINT"IN FRONT OF YOU IS A CHEST"
2890 PRINT"LABELLED WITH A LARGE #"CH
2900 PRINT:PRINT"WILL YOU OPEN IT?"
2910 GOSUB3380
2920 IFZ$="N"THENRETURN
2930 J=INT(RND(1)*3):GOSUB4990
2940 IFJ=0THENCA=100+INT(RND(1)*300):PRINT"IT HOLDS DRAGON'S GOLD WORTH $"
CA:MO=MO+CA
2941 IFJ=0THENGOSUB4990:RETURN
2950 IFJ=1THENPRINT"A GOBLIN LEAPS OUT, STABBING YOU!":LO=INT(RND(1)*6)+1
2951 IFJ=1THENMO=MO-INT(RND(1)*200):IFMO<1THENMO=0:GOSUB4990:RETURN
2960 IFJ=2THENPRINT"A STRANGE SMOKE COMES OUT":PRINT"MAKING YOU SLEEPY AND
"
2961 IFJ=2THENPRINT"SAPPING YOUR MAGIC POWER":LO=INT(RND(1)*8)+1:M(7)=M(7)
-LO
2962 IFJ=2THEN:IFM(7)<1THENM(7)=0:GOSUB4990:RETURN
2970 REM
2980 IFPO=1THEN2820
2990 PO=1
3000 PRINT"YOU SEE A SMALL BOTTLE ENGRAVED"
3010 PRINT"WITH CURIOUS, TWISTED LETTERS..."
3020 PRINT:PRINT"WILL YOU DRINK THE POTION WHICH"
3030 PRINT"YOU CAN SEE INSIDE THE BOTTLE?"
3040 GOSUB3380
3050 IFZ$="N"THENRETURN
3060 GOSUB4990
3065 TE=RND(1)
3070 IFTE>.6THENPRINT"IT CONTAINED A POTION TO":PRINT"ENHANCE YOUR WISDOM"

3071 IFTE>.6THENW(7)=W(7)+INT(RND(1)*6)+1:GOSUB4990:RETURN
3080 PRINT"IT CONTAINED A POTION WHICH":PRINT"WEAKENS YOU FURTHER.....":GO
SUB4990
3081 S(7)=S(7)-(INT(RND(1)*6)+1):IFS(7)<1THENS(7)=0:GOSUB4990:RETURN
3090 REM
3100 IFSC=1THENRETURN
3110 SC=1
3120 PRINT"YOU SEE A PAPYRUS SCROLL."
3130 PRINT:PRINT"DO YOU WISH TO TRY TO READ IT?"
3140 GOSUB3380:IFZ$="N"THENRETURN
3150 IFRND(1)>.5THENPRINT"YOU CANNOT UNDERSTAND":PRINT"THE LANGUAGE...":GO
SUB4990:RETURN
3160 PRINT"IT CONTAINS A MAGIC SPELL. DO":PRINT"YOU WISH TO READ IT?"
3170 GOSUB3380:IFZ$="N"THENRETURN
3175 TE=RND(1)
3180 IFTE>.5THENPRINT"IT WAS A BENEFICENT SPELL":GOSUB4990
3181 IFTE>.5THENM(7)=M(7)+INT(RND(1)*6)+1:RETURN
3190 PRINT"IT WAS AN EVIL SPELL":GOSUB4990:M(7)=0:S(7)=INT(S(7)/2):RETURN
3200 REM
3210 IFSA=1THEN2820
3220 SA=1
3230 PRINT"ON THE WALL IS A SMALL, GILDED":PRINT"SAFE, AND IN FRONT OF"
3231 PRINT"IT IS A KEY..."
3240 PRINT"DO YOU WANT TO OPEN THE SAFE?"
3250 GOSUB3380
3260 IFZ$="N"THENRETURN
3270 IFRND(1)>.3THEN3330
3280 GOSUB4990:PRINT"A SHRIEKING HARPY FLIES OUT":PRINT"AND SINKS ITS TEET
H INTO"
3282 PRINT"YOUR THROAT!"
3290 GOSUB4990
3300 PRINT"YOU GRAPPLE WITH IT, AND...":GOSUB4990:PRINT"...FINALLY WRING I
TS NECK"
3310 S(7)=S(7)-INT(RND(1)*6)+1:IFS(7)<1THENS(7)=0
3320 GOSUB4990:RETURN
3330 PRINT"A CHOIR OF ANGELIC VOICES IS":PRINT"HEARD.....":GOSUB4990
3340 PRINT:PRINT"YOU ARE HEALED AND REFRESHED..."
3350 M(7)=M(7)+2:S(7)=S(7)+2:W(7)=W(7)+2
3360 GOSUB4990
3370 RETURN
3380 GETZ$
3390 IFZ$<>"N"ANDZ$<>"Y"THEN3380
3400 PRINT:RETURN
3410 REM
3420 REM ROOM DESCRIPTIONS
3430 ON Z GOSUB3480,3570,3630,3680,3770,3850,3950,4080,4210,4270,4350,4440

3435 Q=0:TE=RND(1)
3440 IFTE>.81ANDZ>1THENQ=INT(RND(1)*6)
3442 IFTE>.81ANDZ>1THENB$(Z)=LEFT$(B$(Z),8)+MID$(STR$(Q),2,1):GOTO260
3450 E$=MID$(B$(Z),9,1):IFE$>"0"ANDE$<"6"THENQ=VAL(E$)
3460 IFQ>0THENGOSUB260
3470 RETURN
3480 PRINT"YOU ARE AT THE ENTRANCE TO AN"
3490 PRINT"ANCIENT, FORBIDDING-LOOKING"
3500 PRINT"CASTLE. YOU ARE STANDING ON"
3510 PRINT"THE NORTH SIDE OF THE CASTLE,"
3520 PRINT"AND AS YOU LOOK SOUTH, TOWARDS"
3530 PRINT"THE TUMBLING STRUCTURE, YOU"
3540 PRINT"NOTICE THE ENTRANCE PORTAL"
3550 PRINT"IS OPEN AND UNGUARDED."
3560 RETURN
3570 PRINT"YOU ARE IN THE ENTRANCE HALL,"
3580 PRINT"WHICH IS HUNG WITH RICH"
3590 PRINT"FABRICS. DOORS LEAD TO THE"
3600 PRINT"EAST AND THE SOUTH, AND THERE"
3610 PRINT"IS AN OPEN PORTAL TO THE WEST"
3620 RETURN
3630 PRINT"THIS IS ONLY A STORE ROOM."
3640 PRINT"THERE IS A SINGLE EXIT, BACK"
3650 PRINT"THE WAY YOU CAME IN,"
3660 PRINT"TO THE WEST"
3670 RETURN
3680 PRINT"THIS SMALL ROOM, WHICH"
3690 PRINT"FEATURES AN ORNATE SCULPTURE"
3700 PRINT"OF THE MOON GODDESS ON A"
3710 PRINT"PEDESTAL IN THE NORTH-EAST"
3720 PRINT"CORNER, IS THE ROYAL"
3730 PRINT"PRESENCE CHAMBER. DOORS LEAD"
3740 PRINT"TO THE SOUTH, THE WEST AND"
3750 PRINT"TO THE EAST..."
3760 RETURN
3770 PRINT"THE HALL OF PLOTS, A"
3780 PRINT"WOODEN-PANNELLED ROOM"
3790 PRINT"REDOLENT WITH WHISPERS AND"
3800 PRINT"RUMOURS, WITH EXITS TO THE EAST"
3810 PRINT"AND TO THE SOUTH FROM WHICH"
3820 PRINT"COMES THE SMELL OF SULPHUR AND"
3830 PRINT"A WEIRD CHANTING..."
3840 RETURN
3850 PRINT"YOU HAVE ENTERED THE WIZARD'S"
3860 PRINT"DEN, WITH A CAULDRON BUBBLING"
3870 PRINT"OVER A FIRE WITH GREEN FLAMES"
3880 PRINT"IN THE SOUTH-WEST CORNER."
3890 PRINT"THIS ROOM REEKS OF BURNING"
3900 PRINT"SULPHUR, AND THE ECHO OF"
3910 PRINT"ANCIENT SPELLS. YOU CAN LEAVE"
3920 PRINT"TO THE NORTH, THE SOUTH, OR"
3930 PRINT"TO THE EAST"
3940 RETURN
3950 PRINT"YOU FIND YOURSELF IN A PLACE"
3960 PRINT"WHICH SEEMS QUIET AND PEACEFUL."
3970 PRINT"THIS IS THE CASTLE'S PICTURE"
3980 PRINT"GALLERY, WITH A LARGE PAINTING"
3990 PRINT"OF THE LEGENDARY GUARDIAN OF"
4000 PRINT"THE BLACK LAGOON TO THE LEFT OF"
4010 PRINT"THE WINDOW IN THE EAST WALL. THROUGH"
4020 PRINT"THE WINDOW YOU CAN SEE THE MULLIONED"
4030 PRINT"WINDOWS OF THE GREAT HALL ACROSS THE"
4040 PRINT"CONTOURED GARDEN. EXITS FROM THE"
4050 PRINT"GALLERY ARE TO THE NORTH AND"
4060 PRINT"TO THE WEST....."
4070 RETURN
4080 PRINT"THIS IS THE MOST MAGNIFICENT"
4090 PRINT"ROOM IN THE CASTLE, THE GREAT"
4100 PRINT"HALL, WITH A MASSIVE HAMMERBEAM"
4110 PRINT"ROOF. YOU CAN LEAVE IT BY THE"
4120 PRINT"DOUBLE DOORS TO THE NORTH OR BY"
4130 PRINT"THOSE TO THE EAST BEHIND WHICH"
4140 PRINT"YOU CAN HEAR MUSIC PLAYING."
4150 PRINT"THROUGH THE WINDOWS IN THE WEST"
4160 PRINT"WALL, YOU CAN SEE THE CONTOURED"
4170 PRINT"GARDEN, AND BEYOND THAT, THROUGH"
4180 PRINT"WINDOWS OF A ROOM HUNG WITH"
4190 PRINT"MANY, MANY FINE PAINTINGS"
4200 RETURN
4210 PRINT"SOUNDS OF A STRING QUARTET"
4220 PRINT"FILL THIS ROOM, THE MUSICIANS'"
4230 PRINT"CHAMBER. YOU CAN LEAVE BY"
4240 PRINT"DOORS TO THE WEST OR BY ONE"
4250 PRINT"TO THE SOUTH..."
4260 RETURN
4270 PRINT"YOU ARE NOW IN THE SANCTUARY"
4280 PRINT"OF SILENCE, A ROOM WHOSE"
4290 PRINT"CALMNESS MAY BE A DECEPTION."
4300 PRINT"THE ROOM IS DAMP AND COLD. AN"
4310 PRINT"EXIT LEAVES THE ROOM TO THE"
4320 PRINT"NORTH ";
4330 IFMID$(B$(10),3,2)="12"THENPRINT"AND ONE LEAVES TO THE SOUTH"
4340 PRINT:RETURN
4350 PRINT"THIS MUST BE THE VESTIBULE"
4360 PRINT"OF SIGHS, A DANK AND CLAMMY"
4370 PRINT"ROOM WHERE THE LEGEND SAYS THE"
4380 PRINT"GUARDIAN OF THE BLACK LAGOON"
4390 PRINT"CAN SOMETIMES BE HEARD AT"
4400 PRINT"NIGHT. THERE IS A DOOR TO THE"
4410 PRINT"NORTH ";
4420 IFMID$(B$(11),3,2)="12"THENPRINT"AND ONE LEAVES TO THE SOUTH"
4430 PRINT:RETURN
4440 GOTO370
4450 IFMID$(B$(Z),9,1)<>"0"THENQ=VAL(MID$(B$(Z),9,1):GOSUB260
4460 RETURN
4470 REM
4480 REM INITIALISE
4490 REM NO LINE HERE, THE C64 DOESN'T           NEED IT.
4500 Z=1
4510 MO=0
4520 CH=0
4530 PO=0
4540 SC=0
4550 SA=0
4560 DIMB$(12),M$(6),M(7)
4570 PRINT".THE BANNOCHBURN LEGACY":PRINT:PRINT
4580 INPUT"PLEASE ENTER YOUR FIRST NAME";N$
4590 PRINT".":PRINT"HI THERE, ";N$
4600 PRINT"PLEASE STAND BY..."
4610 REM FILL ROOMS
4620 FORT=1TO12
4630 READB$(T)
4640 L=RND(1)
4650 Q$=STR$(INT(RND(1)*5)+1)
4660 IFT>1ANDT<11ANDL<.63THENB$(T)=B$(T)+RIGHT$(Q$,1)
4670 IFT>10ORL>=.63THENB$(T)=B$(T)+"0"
4680 NEXTT
4690 L=RND(1)
4700 IFL<.5THENB$(11)="091200000"
4710 IFL>=.5THENB$(10)="061200000"
4720 REM CREATE MONSTERS
4730 FORT=1TO6
4740 READM$(T)
4750 S(T)=INT(RND(1)*6)+1
4760 M(T)=INT(RND(1)*6)+1
4770 W(T)=INT(RND(1)*6)+1
4780 NEXTT
4790 REM HUMAN CHARACTERISTICS
4800 S(7)=INT(RND(1)*6)+1
4810 M(7)=INT(RND(1)*6)+1
4820 W(7)=INT(RND(1)*6)+1
4830 REM ROOM DATA
4840 DATA "000200000"
4850 DATA "00080304"
4860 DATA "00000002"
4870 DATA "00070205"
4880 DATA "00060400"
4890 DATA "05100700"
4900 DATA "04000006"
4910 DATA "02000900"
4920 DATA "00110008"
4930 DATA "06000000"
4940 DATA "09000000"
4950 DATA "00000000"
4960 REM MONSTER DATA
4970 DATA"WARLOCK","FEARBRINGER","SOULTHREAT","KNEECRUSHER","WOLVLING","GU
ARDIAN"
4980 REM
4990 REM PAUSE ROUTINE
5000 FORP=1TO1000:NEXTP
5010 PRINT
5020 RETURN
