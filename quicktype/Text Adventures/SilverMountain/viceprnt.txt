SILVER



READY.
 
1 PRINT".":POKE53280,5:POKE53281,5:PRINT"."

10 EL=39:NO=88:NV=57:G=28

20 GOSUB3380

30 GOSUB4400

40 LL=0

50 GOSUB3310

60 P$=X$(VAL(LEFT$(D$,1)))+" "+Y$(VAL(MID$(D$,2,1)))+" "

70 J$=R$+". "+"YOU ARE "+P$+RIGHT$(D$,LEN(D$)-2)+" ":GOSUB4830

80 GOSUB3330:J$=""

90 FORI=1TOG-1

100 READO$

110 P$=Y$(VAL(LEFT$(O$,1))):GOSUB3350

120 IFF(I)=0ANDC(I)=RTHENJ$=J$+" "+P$+" "+O$+","

130 NEXTI

140 IFR=29ANDF(48)=0THENJ$=J$+" GRARGS FEASTING,"

150 IFR=29ANDF(48)=1THENJ$=J$+" A SLEEPING GRARG,"

160 IFR=12ORR=22THENJ$=J$+" A PONY,"

170 IFR=64THENJ$=J$+" A HERMIT,"

180 IFR=18ANDE$(18)="N"THENJ$=J$+" AN OAK DOOR,"

190 IFR=59ANDF(68)=1THENJ$=J+" OGBAN (DEAD),"

200 IFJ$<>""THENJ$=", YOU CAN SEE"+J$

210 J$=J$+" AND YOU CAN GO "

220 GOSUB4830:PRINT" ";

230 FORI=1TOLEN(E$(R)):PRINTMID$(E$(R),I,1);",";

240 NEXTI:PRINT:PRINT

250 R$="PARDON?":PRINT"========================================"

260 PRINT:PRINT:PRINT"WHAT WILL YOU DO NOW "

270 INPUTI$

280 IFI$="SAVE"ORI$="SAVE GAME"THENGOSUB4630

290 V$="":T$="":VB=0:B=0

300 FORI=1TOLEN(I$)

310 IFMID$(I$,I,1)=" "ANDV$=""THENV$=LEFT$(I$,I-1)

320 IFMID$(I$,I+1,1)<>" "ANDV$<>""THENT$=MID$(I$,I+1,LEN(I$)-1):I=LEN(I$)

330 NEXTI:IFT$=""THENV$=I$

340 IFLEN(V$)<3THENV$=V$+"O":GOTO340

350 IFV$="PLAY"THENV$="BLO"

360 U$=LEFT$(V$,3)

370 FORI=1TONV:IFMID$(B$,I*3-2,3)=U$THENVB=I:I=NV

380 NEXTI:F(36)=0

390 GOSUB3330

400 FORI=1TONO:READO$:IFI<=GTHENGOSUB3350

410 IFT$=O$THENB=I:I=NO

420 NEXTI

430 IFB=0ANDF(36)=0ANDT$>""THENT$=T$+"S":F(36)=1:GOTO390

440 IFVB=0THENVB=NV+1

450 IFT$=""THENR$="I NEED TWO WORDS"

460 IFVB>NVTHENR$="TRY SOMETHING ELSE"

470 IFVB>NVANDB=0THENR$="YOU CANNOT "+I$

480 IFB>G ORB=0THENGOTO510

490 IFVB=8ORVB=9ORVB=14ORVB=17ORVB=44ORVB>54THENGOTO510

500 IFVB<NVANDC(B)<>0THENR$="YOU DO NOT HAVE THE "+T$:GOTO30

510 IFR=56ANDF(35)=0ANDVB<>37ANDVB<>53THENR$=X1$+" HAS GOT YOU!":GOTO30

520 IFVB=44ORVB=47ORVB=19ORVB=57ORVB=49THENGOTO540

530 IFR=48ANDF(63)=0THENR$=X9$:GOTO30

540 H=VAL(STR$(R)+STR$(B))

550 ONINT((VB-1)/13)+1GOTO560,580,600,620,640

560 ONVBGOSUB800,800,800,800,800,800,1220,1290,1290,1470,1470,1750,1890

570 GOTO650

580 ONVB-13GOSUB1960,1980,2010,2050,2870,2120,2220,2310,2380,2420,2450,247

0,2520

590 GOTO650

600 ONVB-26GOSUB2550,2580,2610,2650,2670,2700,2720,2730,2830,2800,2870,273

0,2920

610 GOTO650

620 ONVB-39GOSUB2950,2990,3010,3050,3070,2310,2990,3070,3130,3120,3190,147

0,3100

630 GOTO650

640 ONVB-52GOSUB2870,3150,1290,1290,3170,3200

650 IFF(62)=1THENGOTO730

660 IFR=41THENF(67)=F(67)+1:IFF(67)=10THENF(56)=1:R$="YOU SANK!"

670 IFR=56ANDF(35)=0ANDC(10)<>0THENR$=X1$+" GETS YOU!":F(56)=1

680 IFF(56)=0THENGOTO30

690 GOSUB4400:PRINTR$

700 PRINT"YOU HAVE FAILED IN YOUR QUEST!"

710 PRINT:PRINT"BUT YOU ARE GRANTED ANOTHER TRY"

720 GOSUB3360:RUN

730 GOSUB4400

740 PRINT"HOOOOORRRRRAAAAAYYYYY!"

750 PRINT

760 PRINT"YOU HAVE SUCCEEDED IN YOUR"

770 PRINT"QUEST AND BROUGHT PEACE TO"

780 PRINT"THE LAND"

790 STOP

800 D=VB

810 IFD=5THEND=1

820 IFD=6THEND=3

830 IFNOT((R=75ANDD=2)OR(R=76ANDD=4))ORF(64)=1THENGOTO850

840 R$="B USPMM TUPQT ZPV DSPTTJOH":GOSUB4260:RETURN

850 IFF(64)=1THENF(64)=0

860 IFF(51)=1ORF(29)=1THENGOTO900

870 IFF(55)=1THENF(56)=1:R$="GRARGS HAVE GOT YOU!":RETURN

880 IFR=29ANDF(48)=0THENR$="GRARGS WILL SEE YOU!":RETURN

890 IFR=73ORR=42ORR=9ORR=10THENR$=X3$:F(55)=1:RETURN

900 IFC(8)=0AND((R=52ANDD=2)OR(R=31ANDD<>3))THENR$="THE BOAT IS TOO HEAVY"

:RETURN

910 IFC(8)<>0AND((R=52ANDD=4)OR(R=31ANDD=3))THENR$="YOU CANNOT SWIM":RETUR

N

920 IFR=52ANDC(8)=0ANDD=4ANDF(30)=0THENR$="NO POWER!":RETURN

930 IFR=41ANDD=3ANDF(31)=0THENR$="UIF CPBU JT TJOLJOH!":GOSUB4260:RETURN

940 IFR=33ANDD=1ANDF(32)=0THENR$="OGBAN'S BOAR BLOCKS YOUR PATH":RETURN

950 IF((R=3ANDD=2)OR(R=4ANDD=4))ANDF(45)=0THENR$=X5$:RETURN

960 IFR=35ANDC(13)<>RTHENR$="THE ICE IS BREAKING!":RETURN

970 IFR=5AND(D=2ORD=4)THENGOSUB4310

980 IFR=4ANDD=4THENR$="PASSAGE IS TOO STEEP":RETURN

990 IFR=7ANDD=2ANDF(46)=0THENR$="A HUGE HOUND BARS YOUR WAY":RETURN

1000 IF(R=38ORR=37)ANDF(50)=0THENR$="JU JT UPP EBSL":GOSUB4260:RETURN

1010 IFR=49ANDD=2ANDF(54)=0THENR$="MYSTERIOUS FORCES HOLD YOU BACK":RETURN


1020 IFR=49ANDD=3ANDF(68)=0THENR$="YOU MET OGBAN!!!":F(56)=1:RETURN

1030 IFR=38ANDF(65)=0THENR$="RATS NIBBLE YOUR ANKLES":RETURN

1040 IFR=58AND(D=1ORD=4)ANDF(66)=0THENR$="YOU GET CAUGHT IN THE WEBS!":RET

URN

1050 IFR=48ANDD=4ANDF(70)=0THENR$="THE DOOR DOES NOT OPEN":RETURN

1060 IFR=40ANDF(47)=1THENF(68)=1

1070 IFR=37ANDD=4ANDE$(37)="EW"THENR=67:R$="THE PASSAGE WAS STEEP!":RETURN


1080 IFR=29ANDD=3THENF(48)=1:F(20)=0

1090 IFR=8ANDD=2THENF(46)=0

1100 OM=R:FORI=1TOLEN(E$(R))

1110 K$=MID$(E$(OM),I,1)

1120 IF(K$="N"ORK$="U")ANDD=1THENR=R-10

1130 IFK$="E"ANDD=2THENR=R+1

1140 IF(K$="S"ORK$="D")ANDD=3THENR=R+10

1150 IFK$="W"ANDD=4THENR=R-1

1160 NEXTI:R$="OK"

1170 IFR=OMTHENR$="YOU CANNOT GO THAT WAY"

1180 IF((OM=75ANDD=2)OR(OM=76ANDD=4))THENR$="OK. YOU CROSSED"

1190 IFF(29)=1THENF(39)=F(39)+1

1200 IFF(39)>5ANDF(29)=1THENR$="CPPUT IBWF XPSO PVU":GOSUB4260:F(29)=0:C(3

)=81

1210 RETURN

1220 GOSUB3330:R$="OK":F(49)=0

1230 PRINT"YOU HAVE ";

1240 FORI=1TOG:READO$:GOSUB3350:IFI=1ANDC(1)=0ANDF(44)=1THENO$="COIN"

1250 IFI=GANDC(5)=0THENGOTO1270

1260 IFC(I)=0THENPRINTO$;",";:F(49)=1

1270 NEXTI:IFF(49)=0THENPRINT"NOTHING"

1280 PRINT:GOSUB3360:RETURN

1290 IFH=6577THENR$="HOW?":RETURN

1300 IFH=4177ORH=5177THENB=16:GOSUB2380:RETURN

1310 IFB=38THENR$="TOO HEAVY!":RETURN

1320 IFB=4ANDF(43)=0THENR$="IT IS FIRMLY NAILED ON!":RETURN

1330 CO=0:FORI=1TOG-1:IFC(I)=0THENCO=CO+1

1340 NEXTI:IFCO>13THENR$="YOU CANNOT CARRY ANY MORE":RETURN

1350 IFB>GTHENR$="YOU CANNOT GET THE "+T$:RETURN

1360 IFB=0THENRETURN

1370 IFC(B)<>RTHENR$="IT IS NOT HERE!"

1380 IFF(B)=1THENR$="WHAT "+T$+"?"

1390 IFC(B)=0THENR$="YOU ALREADY HAVE IT"

1400 IFC(B)=RANDF(B)=0THENC(B)=0:R$="YOU HAVE THE "+T$

1410 IFB=28THENC(5)=81

1420 IFB=5THENC(28)=0

1430 IFC(4)=0ANDC(12)=0ANDC(15)=0THENF(54)=1

1440 IFB=8ANDF(30)=1THENC(2)=0

1450 IFB=2THENF(30)=0

1460 RETURN

1470 R$="YOU SEE WHAT YOU MIGHT EXPECT!"

1480 IF B>0THENR$="NOTHING SPECIAL"

1490 IFB=46ORB=88THENGOSUB2550

1500 IFH=8076THENR$="IT IS EMPTY"

1510 IF H=8080THENR$="AHA!":F(1)=0

1520 IFH=7029THENR$="OK":F(2)=0

1530 IFB=20THENR$="NBUDIFT JO QPDLFU":GOSUB4260:C(26)=0

1540 IFH=1648 THENR$="THERE ARE SOME LETTERS '"+G$(2)+"'"

1550 IFH=7432THENR$="UIFZ BSF BQQMF USFFT":GOSUB4260:F(5)=0

1560 IFH=2134ORH=2187THENR$="OK":F(16)=0

1570 IFB=35THENR$="IT IS FISHY!":F(17)=0

1580 IFH=3438THENR$="OK":F(22)=0

1590 IFH=242THENR$="A FADED INSCRIPTION"

1600 IF(H=1443ORH=1485)ANDF(33)=0THENR$="B HMJNNFSJOH GSPN UIF EFQUIT":GOS

UB4260

1610 IF(H=1443ORH=1485)ANDF(33)=1THENR$="SOMETHING HERE...":F(12)=0

1620 IFH=2479ORH=2444THENR$="THERE IS A HANDLE"

1630 IFB=9THENR$="UIF MBCFM SFBET 'QPJTPO'":GOSUB4260

1640 IFH=4055THENGOSUB3290

1650 IFH=2969ANDF(48)=1THENR$="VERY UGLY!"

1660 IFH=7158ORH=7186THENR$="THERE ARE LOOSE BRICKS"

1670 IFR=49THENR$="VERY INTERESTING!"

1680 IFB=52ORB=82ORB=81THENR$="INTERESTING!"

1690 IFH=6978THENR$="THERE IS A WOODEN DOOR"

1700 IFH=6970THENR$="YOU FOUND SOMETHING":F(4)=0

1710 IFH=2066THENR$="A LARGE CUPBOARD IN THE CORNER"

1720 IFH=6865THENR$="THERE ARE NINE STONES"

1730 IFH=248THENR$="B GBEFE XPSE - 'N S I T'":GOSUB4260

1740 RETURN

1750 IFR=64THENR$="HE GIVES IT BACK!"

1760 IFH=6425THENGOSUB3210

1770 IFR=75ORR=76THENR$="HE DOES NOT WANT IT"

1780 IFB=62ANDF(44)=0THENR$="YOU HAVE RUN OUT!"

1790 IF(H=7562ORH=7662)ANDF(44)>0ANDC(1)=0THENR$="HE TAKES IT":F(64)=1

1800 IFF(64)=1THENF(44)=F(44)-1:IFF(44)=0THENC(1)=81

1810 IFB=1THENR$="HE TAKES THEM ALL!":C(1)=81:F(64)=1:F(44)=0

1820 IFH=2228ANDC(5)=81THENR$=XB$+"NORTH":C(28)=81:R=12

1830 IF(H=2228ANDC(5)=0)ORH=225THENR$=XB$+"NORTH":R=12

1840 IF(H=1228ANDC(5)=0)ORH=125THENR$=XB$+"SOUTH":R=22

1850 IFR=7ORR=33THENR$="HE EATS IT!":C(B)=81

1860 IFH=711THENF(46)=1:R$="HE IS DISTRACTED"

1870 IFH=385ORH=3824THENR$="THEY SCURRY AWAY":C(B)=81:F(65)=1

1880 RETURN

1890 R$="YOU SAID IT"

1900 IFB=84THENR$="YOU MUST SAY THEM ONE BY ONE!":RETURN

1910 IFR<>47ORB<71ORB>75ORC(27)<>0THENRETURN

1920 IFB=71ANDF(60)=0THENR$=X7$:F(60)=1:RETURN

1930 IFB=72ANDF(60)=1ANDF(61)=0THENR$=X8$:F(61)=1:RETURN

1940 IFB=(F(52)+73)ANDF(60)=1ANDF(61)=1THENF(62)=1:RETURN

1950 R$="THE WRONG SACRED WORD!":F(56)=1:RETURN

1960 IFB=5ORB=10THENGOSUB1290

1970 RETURN

1980 IFB=3THENF(29)=1:R$="ZPV BSF JOWJTJCMF":F(55)=0:GOSUB4260

1990 IFB=20THENF(51)=1:R$="ZPV BSF EJTHVJIFE":F(55)=0:GOSUB4260

2000 RETURN

2010 IFB=2ORB=14THENR$="NOTHING TO TIE IT TO!"

2020 IFH=7214THENR$="IT IS TIED":C(14)=72:F(53)=1

2030 IFH=722THENR$="OK":F(40)=1:C(2)=72

2040 RETURN

2050 IFH=1547ANDF(38)=1THENR$="ALL RIGHT":R=16

2060 IFB=14ORB=2THENR$="NOT ATTACHED TO ANYTHING!"

2070 IFH=5414ANDC(14)=54THENR$="YOU ARE AT THE TOP"

2080 IFH=7214ANDF(53)=1THENR$="GOING DOWN":R=71

2090 IFH=722ANDF(40)=1THENR=71:R$="IT IS TORN":C(2)=81:F(40)=0

2100 IFH=7114ANDF(53)=1THENC(14)=71:F(53)=0:R$="IT FALLS DOWN - BUMP!"

2110 RETURN

2120 IFH=522THENR$="OK":F(30)=1

2130 IFB=1ORB=62ORB=5ORB=28ORB=11ORB=24THENGOSUB1750

2140 IFH=416THENR$="ZPV IBWF LFQU BGMPBU":F(31)=1:GOSUB4260:RETURN

2150 IFH=4116THENR$="IT IS NOT BIG ENOUGH!":RETURN

2160 IFB=18ORB=7THENGOSUB2470

2170 IFB=13THENGOSUB2730

2180 IFB=19THENGOSUB3070

2190 IFB=10THENGOSUB2870

2200 IFB=16ORB=6THENGOSUB2380

2210 RETURN

2220 IFB=76ORB=38THENGOSUB1470

2230 IFH=2030THENF(9)=0:R$="OK"

2240 IFH=6030THENR$="OK":F(3)=0

2250 IFH=2444ORH=1870THENR$="YOU ARE NOT STRONG ENOUGH"

2260 IFH=3756THENR$="A PASSAGE!":E$(37)="EW"

2270 IFH=5960THENGOSUB3260

2280 IFH=6970THENR$="IT FALLS OFF ITS HINGES"

2290 IFH=4870THENR$="IT IS LOCKED"

2300 RETURN

2310 IFB>GTHENR$="IT DOES NOT BURN"

2320 IFB=26THENR$="YOU LIT THEM"

2330 IFH=3826THENR$="NOT BRIGHT ENOUGH"

2340 IF(B=23ORH=6970)ANDC(26)<>0THENR$="OP NBUDIFT":GOSUB4260

2350 IFB=23ANDC(26)=0THENR$="A BRIGHT "+V$:F(50)=1

2360 IFH=6970ANDC(26)=0THENF(43)=1:R$="IT HAS TURNED TO ASHES"

2370 RETURN

2380 IF(B=16ORB=6)AND(R=41ORR=51)THENR$="YOU CAPSIZED!":F(56)=1

2390 IFH=6516ANDC(16)=0THENR$="IT IS NOW FULL":F(34)=1

2400 IFH=656THENR$="IT LEAKS OUT!"

2410 RETURN

2420 IFB<>22ORR<>15THENR$="DOES NOT GROW!":RETURN

2430 R$="OK":F(37)=1

2440 RETURN

2450 IFB=22ANDF(37)=1ANDF(34)=1THENR$=X2$:F(38)=1:GOSUB4260

2460 RETURN

2470 IFB=7ORB=18THENR$="THWACK!"

2480 IFH=5818THENR$="YOU CLEARED THE WEBS":F(66)=1

2490 IFH=187THENR$="THE DOOR BROKE!":E$(18)="NS":E$(28)="NS"

2500 IFH=717THENR$="YOU BROKE THROUGH":E$(71)="N"

2510 RETURN

2520 IFB=16THENB=22:GOSUB2450

2530 IFH=499THENR$="WHERE?"

2540 RETURN

2550 IFH=4337THENVB=2:GOSUB800:RETURN

2560 IFR=36THENR$="YOU FOUND SOMETHING":F(13)=0

2570 RETURN

2580 IFR=76THENVB=4:GOSUB800:RETURN

2590 IFR=75THENVB=2:GOSUB800

2600 RETURN

2610 IF(B=3ANDF(29)=1)THENR$="TAKEN OFF":F(29)=0

2620 IF (B=20ANDF(51)=1)THENR$="OK":F(51)=0

2630 IFB=36ORB=50THENGOSUB2950

2640 RETURN

2650 IFH=3859ORH=3339ORH=1241ORH=2241ORH=751THENR$="WITH WHAT?"

2660 RETURN

2670 IFH=2340THENR$="IT GOES ROUND"

2680 IFH=2445THENR$="UIF HBUFT PQFO, UIF QPPM FNQUJFT":F(33)=1:GOSUB4260

2690 RETURN

2700 IFR=14ORR=51THENR$="YOU HAVE DROWNED":F(56)=1

2710 RETURN

2720 R$="HOW?":RETURN

2730 IFB=0ORB>GTHENRETURN

2740 C(B)=R:R$="DONE"

2750 IFH=418ORH=518THENR$="YOU DROWNED!":F(56)=1

2760 IFB=8ANDF(30)=1THENC(2)=R

2770 IFB=16ANDF(34)=1THENR$="YOU LOST THE WATER!":F(34)=0

2780 IFB=2ANDF(30)=1THENF(30)=0

2790 RETURN

2800 IFB=62ANDF(44)=0THENR$="YOU DO NOT HAVE ANY"

2810 IFH=5762ANDC(1)=0ANDF(44)>0THENGOSUB3230

2820 RETURN

2830 IFB=0ORB>GTHENRETURN

2840 R$="DID NOT GO FAR!":C(B)=R

2850 IFH=3317THENR$="ZPV DBVHIU UIF CPBS":F(32)=1:GOSUB4260

2860 RETURN

2870 IFB=10THENR$="B OJDF UVOF":GOSUB4260

2880 IFH=5233THENR$="WHAT WITH?"

2890 IFB=83THENR$="HOW, O MUSICAL ONE?"

2900 IFH=5610THENF(35)=1:R$=X1$+" IS FREE!":E$(56)="NS"

2910 RETURN

2920 IFB=0ORB>GTHENRETURN

2930 IFB=5ORB=24THENR$="YUM YUM!":C(B)=81

2940 RETURN

2950 IFR=4ANDB=50THENF(45)=1:R$="YOU REVEALED A STEEP PASSAGE"

2960 IFR=3ANDB=50THENR$="YOU CANNOT MOVE RUBBLE FROM HERE"

2970 IFH=7136THENR$="THEY ARE WEDGED IN!"

2980 RETURN

2990 IF(B=67ORB=68)ANDC(9)=0ANDR=49THENR$="OK":F(47)=1

3000 RETURN

3010 IFR<>27ORB<>63THENRETURN

3020 PRINT:PRINT"HOW MANY TIMES?":INPUT MR:IFMR=0THENPRINT"A NUMBER":GOTO3

020

3030 IFMR=F(42)THENR$="A ROCK DOOR OPENS":E$(27)="EW":RETURN

3040 R$="ZPV IBWF NJTUSFBUFE UIF CFMM!":F(56)=1:GOSUB4260:RETURN

3050 IFH=5861THENH=5818:GOSUB2470

3060 RETURN

3070 IF(H=4864ORH=4819)ANDC(19)=0THENR$=X6$:F(63)=1:GOSUB4260

3080 IFB=27THENGOSUB1290

3090 RETURN

3100 IFH=7549ORH=7649THENR$="WHAT WITH?"

3110 IFB=1ORB=62THENGOSUB1750

3120 RETURN

3130 IFH=4870ANDC(21)=0THENR$="THE KEY TURNS!":F(70)=1

3140 RETURN

3150 IFH=1870THENR$="HOW?"

3160 RETURN

3170 IFR=48THENR$="HOW?"

3180 RETURN

3190 R$="ARE YOU THIRSTY?"

3200 RETURN

3210 R$="HE TAKES IT AND SAYS '"+STR$(F(42))+" RINGS ARE NEEDED'":C(25)=81


3220 RETURN

3230 F(44)=F(44)-1:R$="A NUMBER APPEARS - "+STR$(F(41))

3240 IFF(44)=0THENC(1)=81

3250 RETURN

3260 PRINT:R$="XIBU JT UIF DPEF":GOSUB4260:PRINTR$:INPUTCN

3270 R$="WRONG!":IFCN=F(41)THENR$="IT OPENS":F(21)=0

3280 RETURN

3290 T=R:R=F(F(52)+57):GOSUB3310:R=T

3300 R$=X4$+RIGHT$(D$,LEN(D$)-2):RETURN

3310 RESTORE:FORI=1TOR:READD$:NEXTI

3320 RETURN

3330 RESTORE:FORI=1TO80:READD$:NEXTI

3340 RETURN

3350 O$=RIGHT$(O$,LEN(O$)-1):RETURN

3360 PRINT"PRESS RETURN TO CONTINUE"

3370 INPUTZ$:RETURN

3380 DIMC(G),E$(80),F(70),X$(6),Y$(6),G$(2)

3390 GOSUB3330

3400 FORI=1TONO:READT$:NEXTI

3410 FORI=1TO6:READX$(I),Y$(I):NEXTI

3420 B$="NOOEOOSOOWOOUOODOOINVGETTAKEXAREAGIVSAYPICWEATIECLIRIGUSEOPE"

3430 B$=B$+"LIGFILPLAWATSWIEMPENTCROREMFEETURDIVBAILEATHRINSBLODROEATMOV"

3440 B$=B$+"INTRINCUTHOLBURPOISHOUNLWITDRICOUPAYMAKBRESTEGATREF"

3450 X6$="ZPV SFGMFDUFE UIF XJABSET HMBSF! IF JT EFBE"

3460 X1$="THE GHOST OF THE GOBLIN GUARDIAN"

3470 X2$= "B MBSHF WJOF HSPXT JO TFDPOET!"

3480 X3$="A GRARG PATROL APPROACHES"

3490 X4$="MAGIC WORDS LIE AT THE CROSSROADS, THE FOUNTAIN AND THE "

3500 X5$="A PILE OF RUBBLE BLOCKS YOUR PATH"

3510 X7$="THE MOUNTAIN RUMBLES!"

3520 X8$="TOWERS FALL DOWN!"

3530 X9$="THE WIZARD HAS YOU IN HIS GLARE"

3540 XB$="HE LEADS YOU "

3550 GOSUB4400:PRINT"DO YOU WANT TO"

3560 PRINT:PRINT"     1. START A NEW GAME"

3570 PRINT" OR  2. CONTINUE A SAVED GAME"

3580 PRINT:PRINT:PRINT"TYPE IN EITHER 1 OR 2"

3590 INPUTC:IFC<>1ANDC<>2THENGOTO3580

3600 IFC=1THENGOSUB4450

3610 IFC=2THENGOSUB4600

3620 RETURN

3630 DATA 11HALF-DUG GRAVE,12GOBLIN GRAVEYARD

3640 DATA 11HOLLOW TOMB,23STALACTITES AND STALAGMITES

3650 DATA 11MAZE OF TUNNELS,11VAULTED CAVERN

3660 DATA 23HIGH GLASS GATES,12ENTRANCE HALL TO THE PALACE

3670 DATA 31GRARG SENTRY POST,12GUARD ROOM

3680 DATA 31MARSHY INLET,23RUSTY GATES

3690 DATA 12GAMEKEEPER'S COTTAGE,31MISTY POOL

3700 DATA 11HIGH-WALLED GARDEN,14INSCRIBED CAVERN

3710 DATA 34ORNATE FOUNTAIN,11DANK CORRIDOR

3720 DATA 12LONG GALLERY,12KITCHENS OF THE PALACE

3730 DATAL 34OLD KILN,44OVERGROWN TRACK

3740 DATA 31DISUSED WATERWHEEL,33SLUICE GATES

3750 DATA 11GAP BETWEEN SOME BOULDERS,41PERILOUS PATH

3760 DATA 31SILVER BELL IN THE ROCK,12DUNGEONS OF THE PALACE

3770 DATA 11BANQUETING HALL,42PALACE BATTLEMENTS

3780 DATA 44ISLAND SHORE,31BEACHED KETCH

3790 DATA 13BARREN COUNTRYSIDE,33SACKS ON THE UPPER FLOOR

3800 DATA 46FROZEN POND,21MOUNTAIN HUT

3810 DATA 31ROW OF CASKS,11WINE CELLAR

3820 DATA 12HALL OF TAPESTRIES,11DUSTY LIBRARY

3830 DATA 13ROUGH WATER,11PLOUGHED FIELD

3840 DATA 55OUTSIDE A WINDMILL,42LOWER FLOOR OF THE MILL

3850 DATA 44ICY PATH,41SCREE SLOPE

3860 DATA 12SILVER CHAMBER,12WIZARD'S LAIR

3870 DATA 11MOSAIC-FLOORED HALL,12SILVER THRONE ROOM

3880 DATA 12MIDDLE OF THE LAKE,42EDGE OF AN ICY LAKE

3890 DATA 41PITTED TRACK,41HIGH PINNACLE

3900 DATA 55ABOVE A GLACIER,21HUGH FALLEN OAK

3910 DATA 11TURRET ROOM WITH A SLOT MACHINE,11COBWEBBY ROOM

3920 DATA 31SAFE IN OGBAN'S CHAMBER,31CUPBOARD IN A CORNER

3930 DATA 11NARROW PASSAGE,16CAVE

3940 DATA 11WOODMAN'S HUT,42SIDE OF A WOODED VALLEY

3950 DATA 21STREAM IN A VALLEY BOTTOM,11DEEP DARK WOOD

3960 DATA 11SHADY HOLLOW,34ANCIENT STONE CIRCLE

3970 DATA 16STABLE,14ATTIC BEDROOM

3980 DATA 11DAMP WELL BOTTOM,32TOP OF A DEEP WELL

3990 DATA 31BURNT-OUT CAMPFIRE,16ORCHARD

4000 DATA 62END OF A BRIDGE,62END OF A BRIDGE

4010 DATA 61CROSSROADS,41WINDING ROAD

4020 DATA 11VILLAGE OF RUSTIC HOUSES,11WHITE COTTAGE

4030 DATA 3COINS,1SHEET,3BOOTS,1HORSESHOE,3APPLES,1BUCKET,4AXE,1BOAT,1PHIA

L

4040 DATA 3REEDS,1BONE,1SHIELD,3PLANKS,1ROPE,1RING,1JUG,1NET,1SWORD

4050 DATA 1SILVER PLATE,1UNIFORM,1KEY,3SEEDS,1LAMP,3BREAD,1BROOCH,3MATCHES


4060 DATA 2STONE OF DESTINY,4APPLE,BED,CUPBOARD,BRIDGE,TREES,SAIL,KILN

4070 DATA KETCH,BRICKS,WINDMILL,SACKS,OGBAN'S BOAR,WHEEL

4080 DATA PONY,GRAVESTONES,POOL,GATES,HANDLE,HUT,VINE,INSCRIPTIONS,TROLL,R

UBBLE

4090 DATA HOUND,FOUNTAIN,CIRCLE,MOSAICS,BOOKS,CASKS,WELL,WALLS,RATS,SAFE

4100 DATA COBWEBS,COIN,BELL,UP SILVER PLATE,STONES,KITCHENS,GOBLET,WINE

4110 DATA GRARGS,DOOR,AWAKE,GUIDE,PROTECT,LEAD,HELP,CHEST,WATER

4120 DATA STABLES,SLUICE GATES,POT,STATUE,PINNACLE,MUSIC,MAGIC WORDS

4130 DATA MISTY POOL,WELL BOTTOM,OLD KILN,MOUNTAIN HUT

4140 DATA IN,A,NEAR,THE,BY,SOME,ON,AN,"","",AT,A SMALL

4150 DATA E,ESW,WE,EW,EW,ESW,ESW,ES,EW,SW

4160 DATA S,N,ES,SW,S,NW,N,N,ES,NSW

4170 DATA NS,E,NSW,N,NES,EW,W,S,NS,N

4180 DATA NES,W,NS,D,NES,SW,E,NW,NS,S

4190 DATA NS,E,NSEW,WU,UD,NS,E,SW,NSE,NW

4200 DATA NE,EW,NSW,E,WN,S,E,NEW,NW,S

4210 DATA ES,SW,NES,EW,SW,NE,EW,ESW,SW,ND

4220 DATA " ",E,NEW,EW,NEW,EW,EW,NEW,NEW,WU

4230 DATA 80,70,60,69,74,72,63,52,20,11,1,14,36,54,61,21,32,10,50

4240 DATA 29,59,34,13,80,30,81,47,74

4250 DATA 1,2,3,4,5,9,12,13,16,17,20,21,22

4260 Z$="":FORI=1TOLEN(R$)

4270 C$=MID$(R$,I,1):IFC$<"A"THENZ$=Z$+C$:GOTO4300

4280 C=ASC(C$)-1:IFC=64THENC=90

4290 Z$=Z$+CHR$(C)

4300 NEXTI:R$=Z$:RETURN

4310 J$="SSSSSSSS":NG=0

4320 MP=D/2:GOSUB4400

4330 PRINT"YOU ARE LOST IN THE TUNNELS"

4340 PRINT"WHICH WAY? (N,S,W OR E)"

4350 IFNG>15THENPRINT "(OR G TO GIVE UP!)"

4360 PRINT:INPUTW$:J$=RIGHT$(J$+RIGHT$(W$,1),8)

4370 IFW$="G"THENF(56)=1:RETURN

4380 IFJ$<>G$(MP)THENNG=NG+1:GOTO4320

4390 RETURN

4400 PRINTCHR$(147):PRINT

4410 PRINTTAB(EL/2-9);"MYSTERY OF SILVER"

4420 PRINTTAB(EL/2-9);"     MOUNTAIN"

4430 PRINT"========================================"

4440 PRINT:PRINT:RETURN

4450 FORI=1TO80:READE$(I):NEXTI

4460 FORI=1TOG:READC(I):NEXTI

4470 FORI=1TO13:READA:F(A)=1:NEXTI

4480 F(41)=INT(RND(1)*900)+100:F(42)=INT(RND(1)*3)+2

4490 F(44)=4:F(57)=68:F(58)=54:F(59)=15:F(52)=INT(RND(1)*3)

4500 R=77:R$="GOOD LUCK ON YOUR QUEST!"

4510 G$(1)="":FORI=1TO8

4520 F$=MID$(B$,1+INT(RND(1)*4)*3,1)

4530 G$(1)=G$(1)+F$

4540 IFF$="N"THENL$="S"

4550 IFF$="S"THENL$="N"

4560 IFF$="E"THENL$="W"

4570 IFF$="W"THENL$="E"

4580 G$(2)=L$+G$(2)

4590 NEXTI:RETURN

4600 GOSUB4640:GOSUB4670

4610 R=F(69):R$="OK. CARRY ON"

4620 RETURN

4630 F(69)=R:GOSUB4640:GOSUB4760:RETURN

4640 PRINT:PRINT"PLEASE ENTER FILE NAME":INPUTFL$

4650 RETURN

4660 REM **READ DATA FILE**

4670 REM

4680 PRINT"OK. SEARCHING FOR ";FL$

4690 OPEN1,8,0,FL$:PRINT"OK. LOADING"

4700 FORI=1TO80:INPUT#1,E$(I):NEXTI

4710 FORI=1TOG:INPUT#1,C(I):NEXTI

4720 FORI=1TO70:INPUT#1,F(I):NEXTI

4730 INPUT#1,G$(1):INPUT#1,G$(2)

4740 CLOSE1:RETURN

4750 REM **SAVE DATA FILE**

4760 REM

4770 OPEN1,8,1,FL$:PRINT"OK. SAVING"

4780 FORI=1TO80:PRINT#1,E$(I):NEXTI

4790 FORI=1TOG:PRINT#1,C(I):NEXTI

4800 FORI=1TO70:PRINT#1,F(I):NEXTI

4810 PRINT#1,G$(1):PRINT#1,G$(2)

4820 CLOSE1:RETURN

4821 REM

4830 LS=1:LP=1

4840 FORI=1TOLEN(J$)

4850 IFMID$(J$,I,1)=" "ANDLL>ELTHENPRINTMID$(J$,LP,LS-LP):LL=I-LS:LP=LS+1

4860 IF MID$(J$,I,1)=" "THENLS=I

4870 LL=LL+1:NEXTI

4880 PRINTMID$(J$,LP,LEN(J$)-LP);

4890 RETURN


READY.
 
