0 CLS:CLEAR256:DIMC1,CC,A$(135),B$(10,4),B(18),O,AN$(4),MA(4):GOSUB2900:GOTO8
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
8 AN$(1)="":QR=1:GOSUB1750
10 A$(1)="ROOM ENTR.":A$(2)="DOOR":A$(3)="BLANK WALL":A$(4)="PASSAGEWAY":A$(5)="STAIRS UP":A$(6)="STEPS DOWN"
20 A$(7)="EXIT":A$(8)="TABLE":A$(9)="COMPUTER":A$(10)="GEEK"
30 A$(12)="JIM GERRIE"
36 A$(13)="JIM GERRIE"
38 A$(14)="JIM GERRIE"
40 A$(15)="JIM GERRIE"
42 A$(16)="JIM GERRIE"
44 A$(17)="JIM GERRIE"
46 A$(18)="JIM GERRIE"
48 A$(19)="JIM GERRIE"
50 A$(20)="JIM GERRIE"
52 A$(21)="JIM GERRIE"
54 A$(22)="REV KEITH KOUNOVSKY"
56 A$(23)="JASON TIMMONS"
58 A$(24)="RICHARD TUCKER"
60 A$(25)="DAN KELLER"
62 A$(26)="SANDY BAIR"
64 A$(27)="JIM O'KEEFE"
66 A$(28)="GEORGE BETHEA"
68 A$(29)="SANDY WIEMER"
70 A$(30)="DAVID KEIL"
72 A$(31)="ANDREW WEILER"
74 A$(32)="TOM SCHAEFGES"
76 A$(33)="TERRY JACKSON"
78 A$(34)="TIM FRANKLIN"
80 A$(35)="NOEMI GARCIA"
82 A$(36)="SHIRLEY STRONG"
84 A$(37)="BARB MORRIS"
86 A$(38)="ANNETTE SWOGER"
88 A$(39)="NEIL BLANCHARD"
90 A$(40)="BYRON CANFIELD"
92 A$(41)=""
94 A$(42)=""
96 A$(43)=""
98 A$(44)=""
100 A$(45)=""
102 A$(46)=""
104 A$(47)=""
106 A$(48)=""
108 A$(49)=""
110 A$(50)=""
112 A$(51)=""
114 A$(52)="MARGE CHASTEEN"
116 A$(53)="BRENDA CRISLIP"
118 A$(54)="LINDA PODRAZA"
120 A$(55)="JUSTIN WAGNER"
122 A$(56)="BRETT GORDON"
124 A$(57)="STEVE NOSKOWICZ"
126 A$(58)="CARL BOLL"
128 A$(59)="BILL ASTLE"
130 A$(60)="BILL YAKOWENKO"
132 A$(61)="KARL SEFCIK"
134 A$(62)="MARKUS BLUMRICH"
136 A$(63)="ROGELIO PEREA"
138 A$(64)="MICHAEL LEE"
140 A$(65)="DAVE BARNES"
142 A$(66)="JOHN TURNER"
144 A$(67)="WALLY GROSSMAN"
146 A$(68)="MARY KRAMER"
148 A$(69)="RICH CRISLIP"
150 A$(70)="ROB GIBONS"
152 A$(71)="BRIAN SCHUBRING"
154 A$(72)="FRANK PITTEL"
156 A$(73)="ROY JUSTUS"
158 A$(74)="NANCY HAWKS"
160 A$(75)="JOHN STRONG"
162 A$(76)="KARL SEFCIK"
164 A$(77)="ALLEN HUFFMAN"
166 A$(78)="GEORGE SCHNEEWEISS"
168 A$(79)="SALVADOR GARCIA"
170 A$(80)="BRIAN GOERS"
172 A$(81)="JOHN CHASTEEN"
174 A$(82)="BOB SWOGER"
176 A$(83)="JOHN MARK MOBLEY"
178 A$(84)="CHRIS HAWKS"
180 A$(85)="RICH BAIR"
182 A$(86)="MARK MARLETTE"
184 A$(87)="NANCY MYERS"
186 A$(88)="DAVE MYERS"
188 A$(89)="BOISY PITRE"
200 A$(90)="AARON WOLFE"
202 A$(91)="JOHN LINVILLE"
204 A$(92)="TONY PODRAZA"
206 A$(93)="MIKE SNYDER"
208 A$(94)="PAUL BURGIN"
210 A$(95)="JEFF VAVASOUR"
212 A$(96)="BROTHER JEREMY"
214 A$(97)="L. CURTIS BOYLE"
216 A$(98)="ROGER TAYLOR"
218 A$(99)="SOCK MASTER"
220 A$(100)="TONY DISTEFANO"
222 A$(101)="GREG LAW"
224 A$(102)="FRANK HOGG"
226 A$(103)="KENNETH REIGHARD"
228 A$(104)="WILLIAM BARDEN"
230 A$(105)="MARTY GOODMAN"
232 A$(106)="EDDIE KUNS"
234 A$(107)="DALE PUCKETT"
236 A$(108)="FRED SCERBO"
238 A$(109)="STEVE BJORK"
240 A$(110)="LONNIE FALK"
242 A$(111)="HOWARD LUCKEY"
244 A$(112)="MALCOLM CLEVELAND"
246 A$(113)="COCO BUCK GIFT CERTIFICATES"
248 A$(114)="+ A DISK "
250 A$(115)="DRIVE"
252 A$(116)="+ A GLENSIDE"
254 A$(117)=" IDE INTERFACE"
256 A$(118)="+ A BASIC"
258 A$(119)=" MANUAL"
260 A$(120)="+ A LWTOOLS "
262 A$(121)="ASSEMBLER"
264 A$(122)="+ AN OS-9 "
266 A$(123)="MANUAL"
268 A$(124)="+ A RAINBO"
270 A$(125)="W MAGAZINE"
272 A$(126)="+ SOME MOR"
274 A$(127)="E FLOPPIES"
276 A$(128)="+ A LOGICALL"
278 A$(129)=" V7.0 DISK"
280 A$(130)="+ A SOFTWA"
282 A$(131)="RE CD"
284 A$(132)="+ DRIVE-"
286 A$(133)="WIRE CABLE"
300 C=0
302 E=6
304 L=20
306 K=0
308 H=100
310 F=0
312 S=10
314 G=0
316 GOTO403
350 FORZZ=1TO1500:NEXT:RETURN
360 FORZZ=1TO500:NEXT:RETURN
400 IF F=1 THEN 426
402 IF INT(RND(0)*200)<5 THEN GOSUB1000
403 SOUND100,1:SOUND50,1:FORZZ=1TO18:B(ZZ)=0:NEXT:GOSUB350
404 IF L=1 THEN E=7
405 IF L<0 OR G>150 AND S>2 AND INT(RND(0)*100)<8 THEN GOSUB750
406 FOR Z=1 TO 3
408 B(Z)=INT(RND(0)*E)+1
410 IF B(Z)<3 THEN GOSUB1100
412 NEXT Z
414 IF B(1)=3 AND B(2)=3 AND B(3)=3 THEN 406
416 CLS:GOSUB2020
426 F=0
428 PRINT"WHAT DIRECTION? ";
430 Z=0
432 Z$=INKEY$:IF Z$="" THEN 432
435 IF Z$="Q" THEN 2000
436 IF Z$="L" THEN Z=1
438 IF Z$="F" THEN Z=2
440 IF Z$="R" THEN Z=3
441 IF Z$="S" THEN GOSUB550
442 IF Z<1 THEN 432
444 PRINTZ$
446 IF B(Z)=6 THEN L=L+1
448 IF B(Z)=5 THEN L=L-1
450 IF B(Z)=3 THEN GOSUB600
452 IF B(Z)=7 THEN GOSUB700
454 IF B(Z)=2 THEN GOSUB800
456 IF B(Z)=1 THEN GOSUB900
457 IF INT(RND(0)*100)>90 THEN GOSUB1300
458 GOTO400
500 TM=B(Z):B(Z)=7+RND(2):ON Z GOSUB2100,2110,2120:B(Z)=TM:RETURN
502 TM=B(Z):B(Z)=10:ON Z GOSUB2100,2110,2120:B(Z)=TM:RETURN
510 R=INT(RND(0)*101)+12
512 RETURN
516 GOSUB502:PRINT@32*7,"";:M$="IN THE ROOM YOU FIND "+A$(R):RETURN
526 GOSUB2196:RETURN
536 W=INT(RND(0)*101)+12
538 RETURN
540 W=(INT(RND(0)*16)+1)*2
542 RETURN
550 GOSUB526
552 PRINTTAB(13)"STATUS":PRINT
554 PRINT"YOU HAVE BESTED";K;"GEEKS. YOU"
558 PRINT"ARE ON FLOOR ";STR$(21-L);". YOU HAVE"
560 PRINTCHR$(8);H;"POINTS OF GEEK CRED."
562 PRINT"YOU HAVE";S;"FLOPPY DISKS AND"
564 PRINT"$";G;"FOR COCO STUFF."
570 GOSUB2198:GOSUB526
571 PRINT@32*7,"WHAT DIRECTION? ";
572 RETURN
600 GOSUB526
602 PRINT"NO-ENTRY....IT'S A WALL!",
604 F=1
606 GOSUB536
608 IF W>50 THEN RETURN
610 M$="EVEN A GEEK LOOKS STUPID WALKING INTO A WALL! YOU LOSE"+STR$(INT(W))+" POINTS OF GEEK CRED."
612 H=H-W
614 RETURN
700 GOSUB2150:CLS:PRINT"YOU GOT TO THE AUCTION.":PRINT
704 SC=G*K+S+H:PRINT"YOU BESTED";K;"GEEKS.":PRINT"YOU HAVE $";G;"OF COCO STUFF":PRINT"YOU SCORED";SC;"GEEK CRED."
712 IF SC>P THEN P=SC
714 PRINT"BEST SCORE SO FAR";P
716 PRINT:PRINT"ANOTHER GAME?";
718 Z$=INKEY$:IF Z$="" THEN 718
720 ON1-(Z$="N")GOTO8:GOTO2000
750 CLS:PRINT:PRINT"  a geek squad apprehends you   ":M$="THEY TRICK YOU OUT OF YOUR COCO BUCKS, "
755 M$=M$+"WHICH REDUCES YOUR CRED BY"+STR$(INT(H/2))+".":GOSUB1
758 M$="THEY TAKE"+STR$(INT(S/2))+" FLOPPIES AND LEAVE YOU ON FLOOR "+STR$(21-(L+1))+".":GOSUB1
759 G=0:H=H-INT(H/2):S=S-INT(S/2):L=L+1
772 GOSUB2198
776 RETURN
800 GOSUB526
801 PRINT"DOOR...O-OPEN,L-LISTEN ";
802 Z$=INKEY$:IF Z$="" THEN 802
806 IF Z$="L" THEN 811
808 IF Z$="O" THEN 898
810 GOTO802
811 PRINTZ$
812 M$="YOU HEAR "
814 IF B(Z+3)>0 THEN M$=M$+A$(B(Z+3))+" SPEAKING.":GOSUB1
816 IF B(Z+3)=0 THEN M$=M$+"NOTHING.":GOSUB1
818 PRINT"NOW WHAT?":PRINT:PRINT"G-GO IN,H-HELP,L-LEAVE ";
820 Z$=INKEY$:IF Z$="" THEN 820
824 IF Z$="G" THEN 850
826 IF Z$="H" THEN 950
828 IF Z$="L" THEN 832
830 GOTO820
832 PRINTZ$:PRINT"CHICKEN!":GOSUB360
838 F=1
840 IF INT(RND(0)*100)<10 THEN GOSUB1000
842 RETURN
850 GOSUB526
852 PRINT@32*7,"";:M$=""
854 IF B(Z+3)>0 THEN R=B(Z+3)
856 IF B(Z+3)>0 THEN GOSUB516
858 IF B(Z+3)=0 THEN M$="A GEEKLESS ROOM"
862 IF B(Z+6)>0 THEN M$=M$+" WITH"+STR$(B(Z+6))+" "+A$(113)
863 GOSUB1
864 IF B(Z+9)>0 THEN PRINTA$(B(Z+9)+100);A$(B(Z+9)+101)
866 IF B(Z+12)>0 THEN PRINTA$(B(Z+12)+100);A$(B(Z+12)+101)
868 IF B(Z+15)>0 THEN PRINTA$(B(Z+15)+100);A$(B(Z+15)+101)
869 GOSUB350
870 IF B(Z+3)>0 THEN GOSUB1314
871 IF B(Z+3)>0 AND C=1700 THEN 838
872 G=G+B(Z+6)
874 FOR X=9 TO 15 STEP 3
876 IF B(Z+X)>0 AND B(Z+X)<25 THEN H=H+30
878 IF B(Z+X)>23 THEN S=S+1
880 NEXT X
882 RETURN
898 PRINTZ$
900 GOSUB526
902 IF B(Z+3)>0 THEN R=B(Z+3)
904 IF B(Z+3)>0 THEN GOSUB516
905 W=0
906 FOR X=6 TO 15 STEP 3
908 IF B(Z+3)>0 AND B(Z+X)>0 THEN W=2
910 IF B(Z+3)=0 AND B(Z+X)>0 THEN W=1
912 NEXT X
914 IF W=2 THEN M$=M$+" AND COCO STUFF."
916 IF W=1 THEN GOSUB500:M$="THE ROOM CONTAINS COCO STUFF."
918 IF B(Z+3)=0 AND W=0 THEN M$="THE ROOM IS EMPTY."
919 PRINT@32*7,"";:GOSUB1
920 GOTO818
950 PRINTZ$
951 GOSUB526
952 IF B(Z+3)>0 THEN M$=A$(B(Z+3))+" HAS"+STR$(B(Z+3)-12)+" GEEK CRED.":GOSUB1
954 IF B(Z+3)=0 THEN PRINT"NO GEEKS!"
958 GOTO818
1000 GOSUB526
1001 F=0
1002 GOSUB536
1004 L=L+INT(W/12):M$="YOU WANDER INTO AN ELEVATOR WHICH GOES DOWN "+STR$(INT(W/12))+" FLOORS TO FLOOR "+STR$(21-L)+"."
1005 GOSUB1
1010 GOSUB536
1012 IF W>95 THEN M$="AND SLIP ON A GUM WRAPPER LOSING"+STR$(H)+" POINTS OF GEEK CRED.":GOSUB1
1014 IF W>95 THEN H=0
1016 IF W<40 THEN M$="AND BEND A FLOPPY, LOSING"+STR$(INT(H/2))+" POINTS OF GEEK CRED.":GOSUB1
1018 IF W<40 THEN H=H-INT(H/2)
1020 GOSUB2198:RETURN
1100 FOR X=3 TO 6 STEP 3
1102 GOSUB536
1104 IF W>70 THEN B(Z+X)=W
1106 GOSUB536
1108 IF B(Z+X)>1 THEN B(Z+X)=W
1109 NEXT X
1110 FOR X=9 TO 15 STEP 3
1112 GOSUB536
1113 IF W>75 THEN B(Z+X)=W
1114 GOSUB540
1116 IF B(Z+X)>0 AND W>12 THEN B(Z+X)=W
1117 IF B(Z+X)>0 AND W<13 THEN B(Z+X)=0
1118 NEXT X
1120 RETURN
1300 GOSUB526:GOSUB510:M$=A$(R)+" ARRIVES":GOSUB1
1310 PRINT"WHAT NOW?"
1314 PRINT:PRINT"B-BATTLE,T-TRADE DISKS,R-RETREAT";
1316 C=0
1318 Z$=INKEY$:IF Z$="" THEN 1318
1322 IF Z$="B" THEN C=1
1324 IF Z$="T" THEN C=2
1326 IF Z$="R" THEN C=3
1328 IF C<1 THEN 1314
1329 PRINTZ$
1330 ON C GOSUB1500,1600,1700
1332 IF C<1 THEN 1314
1334 RETURN
1500 GOSUB526
1501 SA=0:IFRND(4)=1THENGOSUB1800:GOSUB350:GOSUB2196
1502 PRINT"YOU HAD A TERRIFIC BATTLE WITH"
1506 M$=A$(R):IFSA=1THENR=H-12:M$=M$+" AND YOU JUST MANAGED TO BEST HIM/HER.":GOTO1514
1508 IF H<(R-12)THEN M$=M$+" BUT (S)HE BESTED YOU AND TOOK ALL YOUR STUFF.":GOSUB1:FORZZ=1TO10:SOUND1+RND(50),1:NEXT:GOTO712
1512 M$=M$+" AND YOU BESTED HIM/HER."
1514 GOSUB1:FORZZ=1TO10:SOUND100+RND(50),1:NEXT:GOSUB360:K=K+1:H=H+12-R:RETURN
1520 PRINT"SORRY... YOU'RE OUT OF DISKS."
1524 PRINT"NOW WHAT?"
1526 C=0
1528 RETURN
1600 GOSUB526
1602 GOSUB536
1604 IF S<1 THEN 1520
1606 S=S-1
1608 IF W>R THEN M$=A$(R)+" IS HAPPY WITH THE TRADE, AND LEAVES.":GOSUB1:GOSUB360
1610 IF W>R THEN K=K+1
1612 IF W>R THEN RETURN
1614 M$=A$(R)+" IS ANGRY. (S)HE COUNTER OFFERS...":GOSUB1
1616 IF W<27 THEN PRINT"(S)HE CHALLENGES YOU...":SA=0:GOTO1502
1620 PRINT"WHAT NOW?"
1622 C=0
1624 RETURN
1700 GOSUB536
1701 GOSUB526
1702 IF W<40 THEN PRINT"BAD LUCK. (S)HE CHALLENGES!"
1704 IF W<40 THENSA=0:GOTO1502
1706 PRINT"...CHICKEN":GOSUB360
1708 F=0
1709 L=L+1
1710 RETURN
1750 CLS:PRINT@75,"fest trek":PRINT@233,"BY JIM GERRIE":PRINT@384+2,"BASED ON A GAME BY JIM ENNES":PRINT@488,"";:GOSUB2198
1753 CLS:PRINT"YOU ARE AT THE 22ND ANNUAL LAST":PRINT"COCO FEST, WHICH HAS COCO STUFF"
1755 PRINT"BUT ALSO MANY HAZARDS. THE GOAL":PRINT"OF THE GAME IS TO MAKE YOUR WAY"
1756 PRINT"UP THE STAIRS THROUGH THE HOTEL":PRINT"TO THE EXIT ON THE 20TH FLOOR,"
1757 PRINT"WHICH WILL TAKE YOU TO THE NO":PRINT"LOWEST BID AUCTION."
1758 PRINT"PRESS 'S' TO SEE YOUR STATUS.":PRINT"IN BATTLES WITH GEEKS, YOU USE"
1759 PRINT"'GEEK CRED' POINTS TO DEFEAT":PRINT"YOUR OPPONENT. TRADING FLOPPIES"
1760 PRINT"IS ALSO OFTEN SUCCESSFUL (BUT":PRINT"NOT ALWAYS)."
1761 GOSUB2198:CLS
1762 M$="WELCOME TO HERON POINT CONVENTION CENTER! WE HOPE YOU HAVE A PLEASANT STAY!":GOSUB1
1764 RETURN
1800 IFAN$(1)="9999"THENRETURN
1810 IFQR=0THEN1830
1820 READ Q1$,AN$(1),AN$(2),AN$(3),AN$(4)
1830 FORZZ=1TO4:MA(ZZ)=0:NEXT
1840 FORT=1TO4
1850 ZZ=RND(4):IFMA(ZZ)<>0THEN1850
1860 MA(ZZ)=T:NEXT:GOSUB2196:M$=Q1$:GOSUB1
1862 M$="A. "+AN$(MA(1)):GOSUB1:M$="B. "+AN$(MA(2)):GOSUB1:M$="C. "+AN$(MA(3)):GOSUB1:M$="D. "+AN$(MA(4)):GOSUB1
1870 I$=INKEY$:IFI$=""THEN1870
1880 IFMA(ASC(I$)-64)=1THENPRINTI$" IS RIGHT!":QR=1:SA=1:RETURN
1890 PRINTI$" IS WRONG.!":QR=0:SA=0:RETURN
2000 PRINT:PRINT"GOODBYE.  SEE YOU NEXT YEAR!":END
2020 GOSUB2100:GOSUB2110:GOSUB2120:GOSUB2130:RETURN
2100 PRINT@0+O,B$(B(1),0);
2101 PRINT@32+O,B$(B(1),1);
2102 PRINT@64+O,B$(B(1),2);
2103 PRINT@96+O,B$(B(1),3);
2104 PRINT@128+O,B$(B(1),4);:RETURN
2110 PRINT@11+O,B$(B(2),0);
2111 PRINT@43+O,B$(B(2),1);
2112 PRINT@75+O,B$(B(2),2);
2113 PRINT@107+O,B$(B(2),3);
2114 PRINT@139+O,B$(B(2),4);:RETURN
2120 PRINT@22+O,B$(B(3),0);
2121 PRINT@54+O,B$(B(3),1);
2122 PRINT@86+O,B$(B(3),2);
2123 PRINT@118+O,B$(B(3),3);
2124 PRINT@150+O,B$(B(3),4);:RETURN
2130 PRINT@160+O,"   LEFT     FORWARD     RIGHT   ";:PRINTB$(1,0)B$(1,0)B$(1,0)+CHR$(128)+CHR$(128);:RETURN
2140 PRINT@0+O,B$(1,0);:PRINT@11+O,B$(1,0);:PRINT@22+O,B$(1,0);:RETURN
2150 B(1)=10:B(2)=10:B(3)=10:FORT=1TO10:O=160:GOSUB2140:O=0:GOSUB2100:GOSUB2110:GOSUB2120:SOUNDRND(50)+100,2
2160 O=0:GOSUB2140:O=32:GOSUB2100:GOSUB2110:GOSUB2120:SOUNDRND(50)+200,2:NEXT:RETURN
2196 FORT=7TO14:PRINT@32*T,BL$;:NEXT:PRINT@32*15,LEFT$(BL$,31);:PRINT@32*7,"";:RETURN
2198 PRINT" * HIT A KEY *";
2199 I$=INKEY$:ON1-(I$<>"")GOTO2199:FORT=1TO14:PRINTCHR$(8);:NEXT:RETURN
2900 BL$="                                "
3000 B$(1,0)=""
3002 B$(1,1)=""
3003 B$(1,2)=""
3004 B$(1,3)=""
3005 B$(1,4)=""
3007 B$(2,0)="Ѐ"
3008 B$(2,1)="Ѐ"
3009 B$(2,2)="Ѐ"
3010 B$(2,3)="Ѐ"
3011 B$(2,4)="ڀ"
3013 B$(4,0)="Ҁ"
3014 B$(4,1)=""
3015 B$(4,2)=""
3016 B$(4,3)=""
3017 B$(4,4)="Ѐ"
3019 B$(6,0)=""
3020 B$(6,1)=""
3021 B$(6,2)=""
3022 B$(6,3)="Ӏ"
3023 B$(6,4)=""
3025 B$(5,0)=""
3026 B$(5,1)=""
3027 B$(5,2)="䀀"
3028 B$(5,3)=""
3029 B$(5,4)=""
3031 B$(3,0)=""
3032 B$(3,1)=""
3033 B$(3,2)=""
3034 B$(3,3)=""
3035 B$(3,4)=""
3037 B$(9,0)=""
3038 B$(9,1)="󏏏"
3039 B$(9,2)=""
3040 B$(9,3)=""
3041 B$(9,4)=""
3043 B$(10,0)=""
3044 B$(10,1)="À"
3045 B$(10,2)="̀"
3046 B$(10,3)=""
3047 B$(10,4)="ڀҀ"
3049 B$(8,0)=""
3050 B$(8,1)=""
3051 B$(8,2)="o"
3052 B$(8,3)=""
3053 B$(8,4)=""
3055 B$(7,0)=""
3056 B$(7,1)=""
3057 B$(7,2)=""
3058 B$(7,3)=""
3059 B$(7,4)=""
3060 RETURN
3100 DATA THE RAINBOWFEST MASCOT WAS,COCO CAT,THE FALK FALCON,THE COCO BUNNY,RUFUS THE COCO COLLIE
3110 DATA GIME IS AN ACRONYM FOR,GRAPHICS INTERRUPT MEMORY ENHANCER
3112 DATA GRAPHICAL-INTENSIVE MULTITASKING ENABLER,GRAPHICS INTERFACE CHIP WITH MULTIPLE ELEMENTS
3114 DATA GENERAL INTEGRATED MULTIMODAL ESSENTIALS
3120 DATA WHICH OF THE FOLLOWING IS NOT AN ERROR CODE IN RS BASIC
3121 DATA DU,FC,VF,RG
3130 DATA BINARY DIGITS CAN HAVE,2 VALUES,8 VALUES,256 VALUES,1024 VALUES
9999 DATA 9999,9999,9999,9999,9999
