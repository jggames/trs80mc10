0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 DIMD(59,4),L(55),L$(30),R$(17):CH=40:MH=50:R=16
20 CLS:PRINT@199,"ONE MOMENT PLEASE.":PRINT@227,"I AM PREPARING THE DUNGEON."
30 FORX=1TO59:FORY=1TO4:READD(X,Y):NEXT Y,X
40 DATA17,,,22,4,,,,,,22,21,20,,,,38,,23,40,34,,,39,,35,,33,50,,,,,49,,,,30,,,31,,,,,10,,,,,53,,53,,,55,,,47,56
41 DATA22,59,,,18,1,,,,17,19,44,,,20,18,23,4,,19,39,,3,,45,16,1,3,36,20,24,5,25,,,23,26,24,,,27,25,,,28
42 DATA26,,,,27,,29,,,28,30,10,33,29,50,,11,52
50 DATA32,,50,31,,30,,7,,51,6,,,7,36,,37,35,23,,,,30,35,46,37,5,,,57,21,6,,41,,5,,,40,,42,47,43,41,,42,44,,51
51 DATA43,45,18,,44,22,,,,,37,47,48,42,46,15,49,47,,,9,48,50,53,,8,30,49,52,34,43,,,51,,31,,14,49,13,58,,57,,,56
52 DATA14,,55,,15,,,39,,54,32,54,,,16,59
60 DATA59,59
70 FORX=0TO29:READL(X):NEXT
80 DATA,3,3.5,1,1.5,2,8,7,11.5,5,5.5,15.5,15.5,1,4,11,5,15,13,13,10,12,7,,,,,,,9
90 FORX=0TO29:READL$(X):NEXT
100 DATA,"A LONG CANE","A JAVELIN","A PEWTER CANDLEHOLDER","A LARGE, GREEN GEM","A SILVER CROSS"
101 DATA"A ROUND SHIELD","A SET OF KEYS","A GOLD RING","A SEALED TUBE","AN ANCIENT SCROLL","A WAR HAMMER"
102 DATA"A SET OF ARMOR","AN OAK TABLE","A LARGE BOOKCASE"
110 DATA"A WOODEN CHEST","AN OAK TABLE","A NASTY LITTLE DWARF","A BRASS BED","A WOODEN CHAIR","A HUGE,IRON DOOR"
111 DATA"A RATHER LARGE, MEAN-LOOKING    VROCK.","A DUMPY OLD CARETAKER",,,,,,,"A TRANSYLVANIAN VAMPIRE"
120 FORX=1TO17:READR$(X):NEXT
130 DATA"THE TEA ROOM","A PRIVATE STUDY","THE ENTRY HALL","THE LIBRARY","THE KITCHEN","THE SITTING ROOM"
131 DATA"A SMALL CHAMBER","THE ARMORY","THE GUARD ROOM","A WELL-LIT CORRIDOR","A STORAGE CLOSET"
132 DATA"THE VROCK'S LAIR","A SMALL BEDROOM","AN EMPTY ROOM"
140 DATA"A LARGE DINING HALL","A DARK PASSAGEWAY","THE WILDERNESS"
150 L(16)=0:FW$="N   S   E   W   TAKEINVELOOKDROPGET EXAMREADOPENREMOUSE SWINTHROWEARHIT HOLDMELKUNLOKILLHELPHINTSAVELOAD"
160 SW$="CANEJAVECANDGEM CROSSHIEKEYSRINGTUBESCROHAMMARMOTABLBOOKCHESDWARBED CHAIDOORVROCCAREBOTTEND MANUHANDWORDHEADABATVAMP"
170 CLS0:PRINT@136,"tunnels";CHR$(128);"of";CHR$(128);"terror";
180 PRINT@203,"written";CHR$(128);"by";
190 PRINT@265,"miles";CHR$(128);"h";CHR$(128);"jagusch";
200 FORX=1TO1500:NEXT
210 FORX=0TO20:PRINT@70+X,"¦";:NEXT
220 FORX=0TO7:PRINT@102+32*X,"¦";:NEXT
230 FORX=0TO7:PRINT@122+32*X,"¦";:NEXT
240 FORX=0TO20:PRINT@326+X,"¦";:NEXT
250 FORX=0TO63:PRINT@X,"¦";:NEXT
260 FORX=352TO479:PRINT@X,"¦";:NEXT
270 FORY=0TO9:FORX=0TO5:PRINT@64+Y*32+X,"¦";:PRINT@90+32*Y+X,"¦";:NEXTX,Y
290 FORX=1TO2500:NEXT
300 CLS:PRINT@225,"WHAT IS YOUR CHARACTER'S NAME":INPUT" (8 CHARACTERS MAX)";CH$:CH$=(LEFT$(CH$,8))
310 CLS:PRINT@228,"DO YOU NEED INSTRUCTIONS?"
320 Z$=INKEY$:IFZ$=""THEN320
330 IFZ$="N"THEN430
340 IFZ$="Y"THEN360
350 GOTO320
360 CLS:M$="IN THIS ADVENTURE YOU WILL ENCOUNTER MANY THINGS.":GOSUB1
361 M$="THE COMPUTER WILL ASK YOU WHAT TO DO, AND YOU WILL ANSWER USING TWO-WORD SENTENCES "
362 M$=M$+"(EXCEPT FOR DIRECTIONS WHICH YOU MUST USE SINGLE LETTER ANSWERS). EXAMPLES:":GOSUB1:PRINT
370 PRINT"N":PRINT"GET KEYS":PRINT"DROP GEM":PRINT "USE HAMMER"
380 PRINT@451,"PRESS ANY KEY TO CONTINUE."
390 Z$=INKEY$:IFZ$=""THEN390
400 CLS:M$="IF YOU BECOME TOTALLY STUMPED, YOU MAY WANT TO TYPE 'HELP' OR 'HINT' TO GET A FULL LIST OF "
401 M$=M$+"POSSIBLE COMMANDS.":GOSUB1
402 M$="YOU MAY NOT WANT TO USE THIS FUNCTION IF YOU LIKE MORE OF A CHALLENGE.":GOSUB1:PRINT:M$="P.S. IT MAY BE "
410 M$=M$+"HELPFUL TO MAP THE DUNGEON.":GOSUB1:PRINT@451,"PRESS ANY KEY TO CONTINUE."
420 Z$=INKEY$:IFZ$=""THEN420
430 CLS:M$="YOU HAVE JUST DESCENDED ABOUT 1000 FEET DOWN A LONG, DARK TUNNEL.":GOSUB1:PRINT
431 M$="YOU ARE NOW AT THE ENTRANCE TO THE TUNNELS OF TERROR!!!":GOSUB1
432 PRINT@235,"GOOD LUCK":PRINT@453,"PRESS ANY KEY TO BEGIN."
440 Z$=INKEY$:IFZ$=""THEN440
450 CLS:PRINT"YOU ARE IN ";
460 IFR=59THENPRINTR$(17):GOTO490
470 IFR>15THENPRINTR$(16):GOTO490
480 PRINTR$(R)
490 PRINT"VISIBLE PASSAGES:";:GOSUB1550
500 PRINT:PRINT"VISIBLE OBJECTS:":GOSUB1600
510 IFR=15THENGOSUB1700
520 PRINT:GOSUB1630
530 F=0:S=0
540 IFR=9THENTU=TU+1
550 FORX=1TOLEN(FW$)STEP4:IFMID$(FW$,X,4)=A$THENF=(X+3)/4:GOTO570
560 NEXTX
570 FORX=1TOLEN(SW$)STEP4:IFMID$(SW$,X,4)=B$THENS=(X+3)/4:GOTO590
580 NEXTX
590 IFF<5ANDF<>0THEN1750
600 IFF=7ANDS=0THEN450
610 IFF=6ANDS=0THEN730
620 IFF=25THEN2060
621 IFF=26THEN2120
630 IF(F=23ORF=24)ANDS=0THEN1510
631 IFF<1ORS<1THENM$="I DON'T UNDERSTAND YOUR REQUEST.":GOSUB1:GOTO520
640 ONF-4GOTO680,730,810,780,680,810,970,1010,1090,1140,1140,1270,1330,1370,1380,1410,1440,1480,2060,2120
650 IFR=9THENGOSUB1670
660 IFR=12THENF9=F9+1:GOSUB1810
670 GOTO520
680 IFS<13ANDS<>7ANDW<>7ANDL(S)=R THENL(S)=100:PRINT"OK.":W=W+1
690 IFS=24ANDR=4THENM$="HEY! THE BOOKCASE MOVED REVEALING A PASSAGE TO THE SOUTH!":GOSUB1:D(4,2)=2:GOTO650
700 IFS=7ANDR=7THENPRINT"THE CARETAKER WON'T LET YOU."
710 IFS>12THENPRINT"DON'T BE RIDICULOUS."
720 GOTO650
730 CLS:PRINT"YOU ARE CARRYING:":FORX=1TO12
740 IFL(X)=100 THENPRINTL$(X)
750 NEXTX:PRINT@451,"PRESS ANY KEY TO CONTINUE."
760 Z$=INKEY$:IFZ$=""THEN760
770 GOTO450
780 IFS=4ANDL(S)=100ANDR=7THENM$="THE CARETAKER GRABS THE GEM AND HANDS YOU THE KEYS.":GOSUB1:L(7)=100:L(4)=0:GOTO650
790 IFL(S)=100THENPRINT"OK.":L(S)=R:W=W-1:GOTO650
800 PRINT"YOU DON'T HAVE IT!":GOTO650
810 IFS=4AND(L(S)=R ORL(S)=100)THENPRINT"IT LOOKS VERY EXQUISITE.":GOTO650
820 IFS=10AND(L(S)=R ORL(S)=100)THENPRINT"IT HAS WORDS WRITTEN ON IT.":GOTO650
830 IFS=9AND(L(S)=R ORL(S)=100)THENPRINT" IT HAS A LOOSE END.":GOTO650
840 IFS=4AND(L(S)=R ORL(S)=100)THEN700
850 IFS=17ANDL(17)=R ANDL$(17)="A DEAD DWARF"THENPRINT"HE IS WEARING ARMOR.":L(12)=R:GOTO650
860 IFS=1AND(L(S)=R ORL(S)=100)THENPRINT"IT HAS A LOOSE HEAD.":GOTO650
870 IFS=16ANDL$(17)<>"A DEAD DWARF"ANDL(S)=R THENPRINT" I CAN'T DO THAT NOW.":GOTO650
880 IFS=15ANDL(S)=R THENM$="IT IS SEALED SHUT WITH NO VISIBLE LOCK.":GOSUB1:GOTO650
890 IFS=14ANDL(S)=R THENM$="IT HAS ONE MANUSCRIPT THAT STICKS OUT FROM THE REST.":GOSUB1:GOTO650
900 IFS=3AND(L(S)=R ORL(S)=100)THENPRINT"IT HAS A LOOSE BOTTOM.":GOTO650
910 IFS=24ANDR=4THEN690
920 IFS=19ANDR=10THENPRINT"IT'S LOCKED!":GOTO650
930 IFS=20ANDR=12THENPRINT"YOU'RE CRAZY!":GOTO650
940 IFS=21ANDR=7THENPRINT"HE WON'T LET ME.":GOTO650
950 IF(S=2ORS=5ORS=6ORS=7ORS=8ORS=11ORS=12ORS=13ORS=17ORS=18)AND(L(S)=R ORL(S)=100)THENPRINT"I SEE NOTHING SPECIAL.":GOTO650
960 IFS<28AND(L(S)<>R ORL(S)<>100)THENPRINT"I DON'T SEE IT HERE.":GOTO650
970 IFS=10AND(L(S)=R ORL(S)=100)THEN975
971 GOTO980
975 PRINT"IT SAYS-'MELKURION ABATH'":FORZ=1TO2500:NEXTZ:CLS:M$="THE SCROLL DISAPPEARS IN A PUFF OF SMOKE!":GOSUB1:L(S)=0
980 IFS=26AND(L(10)=R ORL(10)=100)THEN985
981 GOTO990
985 PRINT"IT SAYS-'MELKURION ABATH'":FORZ=1TO2500:NEXTZ:CLS:M$="THE SCROLL DISAPPEARS IN A PUFF OF SMOKE!":GOSUB1:L(10)=0
990 IFL(10)<>R ANDL(10)<>100THENPRINT"THERE IS NOTHING HERE TO READ."
1000 GOTO650
1010 IFS=9ANDFS=0AND(L(S)=R ORL(S)=100)THENFS=0:M$="THERE APPEARS TO BE AN ANCIENT SCROLL INSIDE.":GOSUB1:L(10)=R:F12=1:GOTO520
1011 IFS=9AND(L(S)=R ORL(S)=100)THENPRINT"IT'S OPEN!":GOTO650
1020 IFS=23AND(L(9)=R ORL(9)=100)ANDFS=0THENL(10)=R:M$="THERE APPEARS TO BE AN ANCIENT SCROLL INSIDE.":GOSUB1:FS=1:GOTO520
1021 IFS=23AND(L(9)=R ORL(9)=100)THENPRINT"IT'S OPEN!":GOTO650
1030 IFS=19ANDL(7)=100THENM$="THE DOOR OPENS REVEALING A CHAMBER TO THE NORTH.":GOSUB1:D(10,1)=12:L(32)=1:GOSUB3000:GOTO650
1040 IFS=15ANDL(S)=R THENM$="IT IS SEALED SHUT WITH NO VISIBLE LOCK.":GOSUB1:GOTO650
1050 IFS=3ANDFC=0AND(L(S)=R ORL(S)=100)THENFC=1:M$="THE BOTTOM OPENS REVEALING A LARGE GEM INSIDE.":GOSUB1:L(4)=R:GOTO520
1051 IFS=3AND(L(S)=R ORL(S)=100)THENPRINT"IT'S OPEN!":GOTO650
1060 IFS=22ANDFC=0AND(L(3)=R ORL(3)=100)THENFC=1:M$="THE BOTTOM OPENS REVEALING A LARGE GEM INSIDE.":GOSUB1:L(4)=R:GOTO520
1061 IFS=22AND(L(3)=R ORL(3)=100)THENPRINT"IT'S OPEN!":GOTO650
1070 IFS=19ANDR=10ANDL(7)<100THENPRINT"IT'S LOCKED":GOTO650
1080 PRINT"I CAN'T DO THAT.":GOTO 650
1090 IFS=23AND FS=0AND(L(9)=R ORL(9)=100) THENFS=1:M$="THERE APPEARS TO BE AN ANCIENT SCROLL INSIDE.":GOSUB1:L(10)=R:GOTO520
1091 IFS=23AND(L(9)=R ORL(9)=100)THENPRINT"IT'S OPEN!":GOTO650
1100 IFS=22ANDFC=0AND(L(3)=R ORL(3)=100)THENFC=1:M$="THE BOTTOM OPENS REVEALING A LARGE GEM INSIDE.":GOSUB1:L(4)=R:GOTO520
1101 IFS=22AND(L(3)=R ORL(3)=100)THENPRINT"IT'S OPEN!":GOTO650
1110 IFS=24ANDR=4THEN690
1120 IFS=27AND(L(1)=R ORL(1)=100)THENM$="YOU DISCOVER IT'S REALLY A JAVELIN.":GOSUB1:L(2)=R:L(1)=0:GOTO650
1130 GOTO1080
1140 IFS=11ANDL(S)=100ANDR=11THEN1145
1141 GOTO1150
1145 PRINT"YOU SMASHED IT OPEN.":M$="YOU SEE A GOLD RING IN THE DEBRIS.":GOSUB1:L(33)=1:GOSUB3000:L(8)=R:GOTO650
1150 IFR=12THEN1200
1160 IFS=25THENPRINT"NO EFFECT.":GOTO650
1170 IFS<13ANDL(S)=100THENPRINT"NO EFFECT.":GOTO650
1180 IFL(S)<>100THENPRINT"YOU DON'T HAVE IT!":GOTO650
1190 GOTO1080
1200 CLS:IFS=11ANDL(S)<>100THENPRINT"YOU DON'T HAVE IT!":GOTO650
1210 IFS=25THEND=3
1220 IFS=11ANDL(S)=100THEND=6
1230 PT=RND(20):IFPT>10THEN1250
1240 PRINT"YOU MISSED HIM.":GOTO650
1250 PD=RND(D):MH=MH-PD
1260 PRINT"YOU HIT HIM FOR ";PD;" POINTS.":GOTO650
1270 IF(S=2 ORS=11)ANDL(8)=100ANDR=14ANDL$(17)<>"A DEAD DWARF"ANDL(17)=R THEN1275
1271 GOTO1280
1275 PRINT"GOOD JOB! YOU KILLED HIM":L(S)=R:L(31)=1:GOSUB3000:W=W-1:GOTO650
1280 IFS=2ANDL(S)=100ANDR=12THENW=W-1:GOTO1900
1290 IFS=11ANDL(S)=100ANDR=12THENW=W-1:GOTO1900
1300 IFS<13ANDL(S)=100THENPRINT"OK.":L(S)=R:W=W-1:GOTO650
1310 IFL(S)<>100THENPRINT"YOU DON'T HAVE IT!"
1320 GOTO650
1330 IFS=6AND(L(S)=R ORL(S)=100)ANDF5=0THENBO=BO+1:L(S)=100:F5=1:PRINT"OK.":GOTO650
1340 IFS=8AND(L(S)=R ORL(S)=100)ANDF6=0THENBO=BO+1:F6=1:PRINT"OK.":GOTO650
1350 IFS=12AND(L(S)=R ORL(S)=100)ANDF7=0THENF7=1:BO=BO+3:PRINT"OK.":GOTO650
1360 PRINT"THERE IS NOTHING HERE TO WEAR.":GOTO650
1370 PRINT"TRY 'USING' SOMETHING.":GOTO650
1380 IFR=9ANDF1=0ANDS=5THENRA=RA+1:PRINT"O.K.":GOTO650
1390 IFL(S)=100THENPRINT"OK.":GOTO650
1400 PRINT"YOU DON'T HAVE IT!":GOTO650
1410 IFR=9ANDF1=0ANDS=28THENRA=RA+1:PRINT"O.K.":GOTO650
1420 IFS=28THENPRINT"NOTHING HAPPENS.":GOTO650
1430 PRINT"WHAT?!?!":GOTO650
1440 IFS=9ANDL(7)=100ANDR=10ANDL$(19)<>"AN OPEN DOOR"THENPRINT"IT'S OPEN NOW.":L(32)=1:GOSUB3000:D(10,1)=12:GOTO650
1450 IFS=15ANDR=11THENPRINT"THERE'S NO LOCK TO UNLOCK!":GOTO650
1460 IFS=9ANDR=10ANDL$(19)="AN OPEN DOOR"THENPRINT"IT'S ALREADY OPEN.":GOTO650
1470 GOTO1080
1480 IFS=21ANDR=7THEN2030
1490 IF(S=20 ANDR=12) OR(S=29 ANDR=9)THENPRINT"TRY 'USING' SOMETHING.":GOTO650
1500 GOTO1080
1510 CLS:PRINT"COMMAND WORDS ARE-"
1511 PRINT"GET","TAKE","DROP","INVENTORY","LOOK","EXAMINE","READ","OPEN","REMOVE","SWING",
1512 PRINT"USE","THROW","WEAR","UNLOCK","KILL","HELP","HINT","SAVE","LOAD":PRINT"SECRET WORD (YOU MUST FIND IT   YOURSELF)"
1520 PRINT@451,"PRESS ANY KEY TO CONTINUE."
1530 Z$=INKEY$:IFZ$=""THEN 1530
1540 GOTO650
1550 IFD(R,1)<>0THENPRINT" N";
1560 IFD(R,2)<>0THENPRINT" S";
1570 IFD(R,3)<>0THENPRINT" E";
1580 IFD(R,4)<>0THENPRINT" W";
1590 PRINT:RETURN
1600 FORX=1TO29:IFL(X)=R THENPRINTL$(X)
1610 NEXTX
1620 RETURN
1630 A$="":B$="":INPUT"WHAT NOW";A$:IFLEN(A$)<4THENFORX=1TO(4-LEN(A$)):A$=A$+" ":NEXTX
1640 FORX=1TOLEN(A$):IFMID$(A$,X,1)=" "ANDLEN(A$)>(X+2)THEN1645
1641 GOTO1650
1645 B$=MID$(A$,X+1,4):IFLEN(B$)<4THENB$=B$+" ":GOTO1660
1646 GOTO1660
1650 NEXTX
1660 A$=LEFT$(A$,4):RETURN
1670 IFRA=2ANDF1=0THEN1675
1671 GOTO1680
1675 FORX=240TO50STEP-5:SOUNDX,1:NEXTX
1676 M$="THE VAMPIRE LETS OUT A BLOODCURDLING SHRIEK AND DISAPPEARS.":GOSUB1:CH=50:F1=1:L(29)=0:RETURN
1680 IFTU=5ANDF1=0THENM$="IT LOOKS LIKE HE GOT YOU IN THE NECK!":GOSUB1:GOTO1930
1690 RETURN
1700 IFFD=1THENRETURN
1710 M$="THE NASTY LITTLE DWARF THROWS A WAR HAMMER AT YOU.":GOSUB1:L(11)=R:GOSUB1740
1720 IFA$<>"DUCK"THENPRINT"OOOPPPSSS!":M$="HE TOOK YOUR HEAD OFF WITH IT!":GOSUB1:GOTO1930
1730 PRINT"YOU DODGED IT!":M$="THE DWARF RUNS OUT OF THE ROOM TO THE WEST.":GOSUB1:FD=1:L(17)=14:RETURN
1740 INPUT"WHAT NOW";A$:A$=LEFT$(A$,4):RETURN
1750 IFR=12THEN1800
1751 IFF=1ANDD(R,1)<>0THENR=D(R,1):GOTO450
1760 IFF=2ANDD(R,2)<>0THENR=D(R,2):GOTO450
1770 IFF=3ANDD(R,3)<>0THENR=D(R,3):GOTO450
1780 IFF=4ANDD(R,4)<>0THENR=D(R,4):GOTO450
1790 PRINT"YOU JUST RAN INTO A WALL.":GOTO520
1800 PRINT"THE VROCK WON'T LET ME!":GOTO650
1810 IFF9<2THEN1890
1820 MT=RND(20):IFMH<=0THEN2040
1830 MT=MT-BO:IFMT>6THEN1850
1840 PRINT"HE MISSED YOU.":GOTO1880
1850 MD=RND(6):CH=CH-MD
1860 M$="HE HIT YOU FOR"+STR$(MD)+" POINTS.":GOSUB1
1870 IFCH<=0THEN1930
1880 PRINT"HE HAS ";MH;" HIT POINTS.":PRINT"YOU HAVE ";CH;" HIT POINTS."
1890 RETURN
1900 CLS:IFF9>1THENPRINT"YOU ARE TOO CLOSE.":GOTO650
1901 PJ=RND(100):IFPJ>50THEN1920
1910 PRINT"YOU MISSED HIM.":GOTO630
1920 IFS=11THENDH=RND(6):M$="YOU HIT HIM FOR"+STR$(DH)+" POINTS.":GOSUB1:MH=MH-DH:L(11)=R:GOTO650
1921 DJ=RND(10):M$="YOU HIT HIM FOR"+STR$(DJ)+" POINTS.":GOSUB1:MH=MH-DJ:L(2)=R:GOTO650
1930 SOUND149,2:SOUND149,4:SOUND95,1:SOUND149,2:SOUND95,4
1931 SOUND193,2
1932 SOUND149,2:SOUND95,4
1933 SOUND193,2:SOUND203,4:SOUND176,4:SOUND196,2
1934 SOUND203,4:SOUND176,4
1935 SOUND193,2
1936 SOUND95,2
1937 SOUND193,4:SOUND203,1:SOUND196,1
1938 SOUND95,4:SOUND149,2:SOUND149,4:SOUND95,1
1940 FORX=1TO750:NEXTX
1950 CLS:PRINT"OOOPPPSSS!!!":PRINT"YOU SEEM TO HAVE GOTTEN YOURSELFKILLED!":PRINT"BETTER LUCK NEXT TIME!":FORX=1TO3500:NEXTX
1960 PRINT@450,"WOULD YOU LIKE TO TRY AGAIN?"
1970 Z$=INKEY$:IFZ$=""THEN1970
1980 IFZ$="Y"THENRUN
1990 IFZ$="N"THEN2010
2000 GOTO1970
2010 CLS:PRINT@162,"I HOPE YOU ENJOYED YOURSELF!":PRINT@230,"PLEASE COME AGAIN TO":PRINT@294,"THE TUNNELS OF TERROR!"
2020 GOTO2020
2030 CLS:PRINT"YOU FIND YOU CANNOT MOVE!":FORX=1TO2000:NEXTX
2031 PRINT"YOU HEAR SOMETHING BREAKING!":FORX=1TO1500:NEXTX
2032 PRINT"IT'S YOUR BONES!!!":FORX=1TO1500:NEXTX:SOUND113,4:SOUND129,4:SOUND142,4:SOUND149,4:SOUND161,4:SOUND87,4:SOUND103,4
2039 PRINT"YOUR NECK JUST SNAPPED!!!":FORX=1TO2000:NEXT:GOTO1930
2040 FORX=1TO1500:NEXTX
2050 CLS:SOUND149,8:SOUND95,8:SOUND203,4:SOUND193,8:SOUND203,2
2051 M$="WELL, IT LOOKS LIKE YOU'VE DONE IT! YOU'VE DEFEATED THE VROCK AND ALL HIS EVIL SERVANTS.":GOSUB1
2052 M$="YOU'VE MADE AN ABSOLUTELY EXCELLENT PERFORMANCE!":GOSUB1:GOTO1960
2060 CLS:PRINT"PLEASE POSITION TAPE,PRESS PLAY,AND HIT ANY KEY TO BEGIN SAVING"
2070 A$=INKEY$:IFA$=""THEN2070
2080 FORCC=48TO55:M$=MID$(CH$,CC-47,1):IFM$<>""THENL(CC)=ASC(M$):GOTO2082
2081 L(CC)=0
2082 NEXT
2100 L(34)=FC:L(35)=FS:L(36)=F1:L(37)=F9:L(38)=TU:L(39)=RA:L(40)=R:L(41)=W
2101 L(42)=BO:L(43)=D(10,1):L(44)=D(4,2):L(45)=CH:L(46)=MH:L(47)=FD
2110 CSAVE*L,CH$:GOTO450
2120 CLS:PRINT"PLEASE POSITION TAPE,PRESS PLAY,AND HIT ANY KEY TO BEGIN LOADING"
2130 A$=INKEY$:IFA$=""THEN2130
2140 FORCC=48TO55:M$=MID$(CH$,CC-47,1):IFM$<>""THENL(CC)=ASC(M$):GOTO2142
2141 L(CC)=0
2142 NEXT
2150 CLOAD*L,CH$
2160 FC=L(34):FS=L(35):F1=L(36):F9=L(37):TU=L(38):RA=L(39):R=L(40):W=L(41):BO=L(42)
2161 D(10,1)=L(43):D(4,2)=L(44):CH=L(45):MH=L(46):FD=L(47)
2170 GOSUB3000:GOTO450
3000 IFL(31)THENL$(17)="A DEAD DWARF"
3001 IFL(32)THENL$(19)="AN OPEN DOOR"
3002 IFL(33)THENL$(15)="A SMASHED CHEST"
3999 RETURN
4001 REM TUNNELS OF TERROR
4002 REM WRITTEN BY MILES JAGUSCH
4003 REM R.R. 1
4004 REM RED WING,MN 559066
