10 REM****************************
20 REM********THE EXPLORER********
30 REM**A CLASSIC MINI-ADVENTURE**
40 REM**FOR BEGINNER ADVENTURERS**
50 REM****************************
60 GOSUB 880
70 INK 3,6:INK 1,24:BORDER INT(RND*26)
80 CLS:L=2
90 GOTO 740
100 PRINT:PRINT">>>>>>>>>>>>>>>>>>>>>"
110 PEN 2
120 PRINT:PRINT
130 Z=Z+1:IF Z=2 THEN Z=1:PEN 3 ELSE PEN 2
140 SOUND 1,INT(RND*400)+30
150 BORDER INT(RND*26)
160 PRINT "I AM ON":PRINT L$(L)
170 PRINT "AND I AM CARRYING:";:X=0:GOSUB 690
180 PRINT "I CAN SEE:";:X=L:GOSUB 690
190 PRINT:INPUT "WHAT NEXT";A$
200 A$=UPPER$(A$)
210 K$=A$
220 IF A$="" THEN 100
230 V$=""
240 J=LEN(A$):FOR M=1 TO J
250 IF LEFT$(A$,1)=" " THEN GOSUB 680:GOTO 280:GOTO 680:GOTO 280
260 GOSUB 670
270 NEXT M
280 IF V$="GET" THEN 350
290 IF V$="DROP" THEN 450
300 IF V$="CHOP" THEN 520
310 IF V$="GO" THEN 560
320 IF V$="HELP" THEN GOTO 650
330 PRINT "I CANNOT ";K$
340 GOTO 100
350 X=0
360 FOR B=0 TO 9
370 IF BL(B)=L AND A$=B$(B) THEN X=B:BL(B)=0
380 NEXT B
390 IF X=0 THEN PRINT "IT IS NOT HERE.":GOTO 100
400 IF X=5 AND BL(6)=4 THEN BL(5)=4:PRINT "THE WITCH STOPS ME.":GOTO 100
410 IF X=1 OR X=9 THEN PRINT "DON'T BE SILLY!":BL(X)=L
420 IF X=4 AND L=2 AND BL(3)<0 THEN BL(3)=2:PEN 1:PRINT"££££ I SEE GOLD ££££":PEN 3
430 IF X<>5 THEN 100
440 PRINT "CONGRATULATIONS, YOU HAVE WON THE       PRINCESS!!!":SOUND 1,100,100:END
450 X=0
460 FOR B=1 TO 9
470 IF BL(B)=0 AND A$=B$(B) THEN X=B:BL(B)=L
480 NEXT B
490 IF X=2 AND L=2 THEN BL(2)=1:BL(8)=2
500 IF X=0 THEN PRINT "I DO NOT HAVE IT."
510 GOTO 100
520 IF A$="TREE" AND L=1 AND BL(7)=0 AND BL(1)=1 THEN DV=1
530 IF DV=1 THEN PRINT "TIMBERRRR!":BL(1)=1:BL(2)=1:GOTO 100
540 IF A$="TREE" THEN PRINT "WITH WHAT?":GOTO100
550 GOTO 330
560 IF A$="NORTH" THEN X=0
570 IF A$="SOUTH" THEN X=1:GOTO 630
580 IF A$="EAST" THEN X=2:GOTO 630
590 IF A$="WEST" THEN X=3:GOTO 630
600 IF L=2 AND A$="LOG" AND BL(8)=2 THEN L=3:GOTO 100
610 IF L=3 AND A$="SHORE" THEN L=4:LL(L,X)=4
620 IF L=4 AND BL(3)=0 THEN BL(3)=-1:BL(6)=-1:PRINT "THE WITCH TOOK THE GOLD AND RAN!":GOTO 100
630 IF LL(L,X)<0 THEN PRINT N$(-LL(L,X)):GOTO 100
640 L=LL(L,X):GOTO 100
650 IF L=2 AND (BL(7)=0 OR BL(7)=2) THEN PRINT "DROP LOG IN WATER...THEN 'GO LOG'"
660 GOTO 100
670 V$=V$+LEFT$(A$,1)
680 A$=RIGHT$(A$,LEN(A$)-1):RETURN
690 FOR B=1 TO 9
700 IF BL(B)=X THEN PRINT ", ";B$(B);
710 NEXT B
720 PRINT
730 RETURN
740 DIM L$(4),LL(4,3),B$(9),BL(9),N$(2)
750 DATA "THE EDGE OF THE FOREST","THE NORTH EDGE OF THE LAKE"
760 DATA "A LOG WHICH I PADDLED ACROSS THE LAKE"
770 DATA"THE SOUTH SIDE OF A LAKE"
780 DATA -2,2,-2,-2,1,-1,-1,-1,-1,-1,-1,-1,-1,-2,-2,-2
790 DATA TREE,LOG,GOLD,ROCK,PRINCESS,WITCH,AXE,LOG IN THE WATER,SHORE
800 DATA 1,-1,-1,2,4,4,0,-1,3
810 DATA I CANNOT SWIM, THERE IS NOTHING THAT WAY
820 FOR I=1 TO 4:READ L$(I):NEXT I
830 FOR I=1 TO 4:FOR J=0 TO 3:READ LL(I,J):NEXT J:NEXT I
840 FOR I=1 TO 9:READ B$(I):NEXT
850 FOR I=1 TO 9:READ BL(I):NEXT
860 READ N$(1):READ N$(2)
870 GOTO 100
880 REM****INSTRUCTIONS****
890 CLS
900 LOCATE 4,2:PRINT "This mini-adventure involves you"
910 PRINT "exploring the area around you to find a way to win the Princess of Light...who,"
920 PRINT "if rumours are to be believed, is       somewhere nearby."
930 PRINT:PRINT
940 PRINT "     To control your character in this  game requires you to enter the command  and the object it is to affect."
950 PRINT "For example, 'GET KNIFE'. For movement, the direction (based on the compass     points) must be preceded by the 'GO'    command."
960 PRINT:PRINT
970 PRINT "     Your final help is a list of       commands."
980 PRINT "GO NORTH; GO SOUTH; GO EAST; GO WEST;   GET; DROP; CHOP; HELP."
990 PRINT:PRINT:PRINT
1000 WHILE INKEY$="":WEND
1010 RETURN
