0 POKE17026,64:GOTO15
1 REM INSTR SUBROUTINE
7 FF=0:LL=LEN(I$):FORZZ=BBTOLEN(M$):ON1-(MID$(M$,ZZ,LL)=I$)GOTO8:FF=ZZ:ZZ=255:NEXT:RETURN
8 NEXT:RETURN
9 PRINT@384,;:FORZZ=1TO127:PRINT" ";:NEXT:RETURN
15 CLS:PRINT@232,"PHARAOH'S CURSE":GOSUB50:CLS
20 CLEAR1000:JI$="000200000005030102040000000000030600000208070500000000060009060000"
21 JI$=JI$+"11000800001100101312091100000000140011171518130016001400190015"
25 GOTO100
50 FORX=1TO1000:A$=INKEY$:IFA$=""THENNEXT
55 RETURN
100 REM ********************
110 REM * PHARAOH'S  CURSE *
120 REM ********************
130 BG=3:BX=3:FL=3:KY=3:T=3:W=3:DG=3:GR=3
150 GOSUB190:GOTO1000
189 REM entry corridor
190 D=10:D$="NYNY":GOSUB9000:PRINT@320,"YOU ARE IN THE ENTRY CORRIDOR.":P=1:RETURN
199 REM antechamber
200 D=18:D$="NYYY":GOSUB9000:PRINT@320,"YOU ARE NOW IN THE ANTECHAMBER";
201 PRINT@384,"THE ROOM APPEARS TO HAVE BEEN   RANSACKED.";:P=2:RETURN
209 REM annex
210 P=3:D=18:D$="YYNN":GOSUB9000:PRINT@320,"YOU ARE IN THE ANNEX."
211 IFBX=3THENPRINT@384,"THERE IS A BOX AT 'B'.":PRINT@135,"b";:RETURN
212 RETURN
219 REM prep. room
220 P=4:D=18:D$="NNNY":GOSUB9000:PRINT@384,"YOU ARE IN THE ROOM OF PREPAR-  ATION FOR THE AFTERLIFE. IT ALSO";
221 PRINT"APPEARS TO HAVE BEEN RANSACKED.";:IFFL=3THENPRINT@320,"THERE IS A FLUTE AT 'F'.":PRINT@180,"f";:RETURN
222 RETURN
229 REM devot. to gods
230 P=5:D=18:D$="YNNY":GOSUB9000:PRINT@384,"YOU ARE IN THE ROOM OF DEVOTION TO THE GODS.";:RETURN
239 REM boat rm.
240 P=6:D=18:D$="YYYN":GOSUB9000:PRINT@320,"THERE IS A LARGE BOAT HERE.";:PRINT@384,"THE HIEROGLYPHICS READ -- 'FOR"
241 PRINT"THE PASSAGE TO THE OTHERWORLD.  --FROM THE PHARAOH'S DEVOTED     SERVANT, RAN-SECH.";
245 PRINT@47,CHR$(193);:PRINT@77,"цц"CHR$(199)"цц";:PRINT@111,CHR$(197);
246 PRINT@135,CHR$(197)CHR$(203)CHR$(195)"─────"CHR$(197)"─────"CHR$(195)CHR$(199)CHR$(202);:PRINT@168,CHR$(205)"оооццц";
247 PRINTCHR$(199)"цццооо"CHR$(206);:PRINT@201,CHR$(196)CHR$(205)"ооооооооо"CHR$(206)CHR$(200);:PRINT@236,"ллллллл";:RETURN
249 REM minor treasury
250 P=7:D=14:D$="NNNY":GOSUB9000:PRINT@288,"YOU ARE IN THE MINOR TREASURY.  IT HAS BEEN RANSACKED.";
251 IFKY=3THENPRINT@384,"THERE IS A KEY AT 'K'.":PRINT@140,"k";:RETURN
252 RETURN
259 REM pyramid room
260 P=8:D=18:D$="NYYN":GOSUB9000:PRINT@320,"YOU ARE IN THE PYRAMID ROOM.":PRINT@384,"A LARGE PYRAMID FILLS THE ROOM.";
265 FORX=1TO7:PRINT@X*31+16,LEFT$("оооооооооооооооооооооооооооооооо",X*2);:NEXT:RETURN
269 REM passage of life
270 P=9:D=18:D$="NYNY":GOSUB9000:PRINT@320,"YOU ARE IN THE PASSAGE OF LIFE."
271 PRINT@384,"HIEROGLYPHS ON THE WALLS TELL OFTHE LIFE OF THE PHARAOH, KEMEN- RATUR.":RETURN
279 REM burial chamber
280 P=10:D=18:D$="NNYN":GOSUB9000:PRINT@320,"YOU ARE IN THE BURIAL CHAMBER."
281 PRINT@384,"A LARGE SARCOPHAGUS FILLS THE   ROOM.  HIEROGLYPHS WARN OF SURE AND SUDDEN DEATH FOR THOSE WHO  OPEN IT.";
282 RETURN
289 REM dead rm
290 P=11:D=18:D$="YYYY":GOSUB9000:PRINT@320,"THIS ROOM HAS TWO SKELETONS."
291 PRINT@384,"THE SKELETONS ARE AT THE PLACES MARKED 'S'.";:PRINT@130,"s";:PRINT@156,"s";:RETURN
299 REM major treasury
300 P=12:D=18:D$="YNNN":GOSUB9000:PRINT@320,"YOU'RE IN THE MAJOR TREASURY."
301 IFDG=3THENPRINT@384,"YOU ARE ATTACKED BY TWO HUGE    DOGS !!!":PRINT@142,"d";:PRINT@145,"d";
305 IFT=3ANDDG=1THENPRINT@210,"t";:PRINT@384,"YOU CAN SEE THE GOLD TABLETS AT 'T'.";:GOSUB307:RETURN
306 RETURN
307 PRINT"                                                            ";:RETURN
309 REM snake room
310 C=3:P=13:D=18:D$="NYNY":GOSUB9000:PRINT@320,"YOU'RE IN THE SNAKE ROOM.":PRINT@384,"THERE IS A COBRA AT 'C'."
311 FORX1=1TO100:Y=RND(8)*32:X=RND(28)+1:PRINT@X+Y,"c";:NEXT:RETURN
319 REM shrine
320 P=14:D=18:D$="YYYY":GOSUB9000:PRINT@320,"YOU ARE IN THE SHRINE."
321 PRINT@384,"THERE IS A STATUE AT 'S'.";:PRINT@175,"s";:RETURN
329 REM gate to eternal life
330 P=15:D=14:D$="NYNY":GOSUB9000:PRINT@288,"YOU ARE IN THE ROOM OF THE GATE TO ETERNAL LIFE."
331 PRINT@384,"AN INSCRIPTION ON THE EAST DOOR READS-ONLY ONE WITHOUT MALICE ORGREED MAY PASS INTO THE AFTER-  LIFE.";:RETURN
339 REM end corridor
340 P=16:D=10:D$="NYNY":GOSUB9000:PRINT@288,"YOU ARE IN THE EXIT CORRIDOR.";:RETURN
349 REM watery grave
350 P=17:FORX=0TO14:PRINT@X*32,"╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞╞":FORY=1TO10:NEXTY,X:CLS3
351 PRINT@230,"YOU WERE DROWNED !!";:GOSUB50:PRINT@480,;:CLS:END
359 REM dust bowl
360 P=18:CLS:FORX=0TO14:PRINT@X*32,"оооооооооооооооооооооооооооооооо":FORY=1TO10:NEXTY,X:CLS5
361 PRINT@224,"YOU SUFFOCATE IN A CHOKING CLOUDOF DUST !!";:POKE49151,64:GOSUB50:PRINT@480,;:SOUND1,1:CLS:END
369 REM exit sequence
370 P=19:IFBX=1ORBG=1ORFL=1ORKY=1ORW=1THENPRINT@320,;:FORZZ=1TO191:PRINT" ";:NEXT
371 PRINT@320,"A CAVE-IN ...":PRINT:PRINT"SORRY.":END
375 CLS:IFT=1THEN377
376 GOTO380
377 PRINT@224,"CONGRATULATIONS !":PRINT"YOU'VE RETRIEVED THE GOLD":PRINT"TABLETS OF THE PHARAOH"
378 PRINT"KEMEN-RATUR":PRINT:PRINT"THIS MAKES YOU A FIRST CLASS    ADVENTURER !!":PRINT:END
380 PRINT@224,"CONGRATULATIONS !!":PRINT:PRINT"YOU'VE ESCAPED THE TOMB OF THE  PHARAOH AND ELUDED THE CURSE."
381 PRINT:PRINT"HOWEVER, YOU DIDN'T RETRIEVE    THE GOLD TABLETS.":PRINT
382 PRINT"THIS QUALIFIES YOU AS A NOVICE  ADVENTURER.":PRINT:END
1000 PRINT@352,"                                ";:PRINT@352,"COMMAND";:INPUT CD$
1002 IFP=12ANDDG=3THEN1110
1004 IFP=13ANDC=3THENIFCD$<>"W"THEN1150
1010 IFLEFT$(CD$,6)="INVENT"THEN2000
1012 M$="NSEW":I$=CD$:BB=1:GOSUB7:IFFF<>0THENA$=CD$:GOTO2520
1020 IFCD$="LEAVE"THEN2500
1030 IFLEFT$(CD$,3)="GET"THEN3000
1040 IFLEFT$(CD$,4)="OPEN"THEN3500
1050 IFLEFT$(CD$,6)="SEARCH"THEN4000
1060 IFLEFT$(CD$,4)="DROP"THEN4500
1100 REM SPECIFIC VERBS
1110 IFCD$="THROW BOOMERANG" ANDP=12ANDDG=3ANDBG=1THEN1112
1111 GOTO1120
1112 PRINT@384,"THE DOGS TURN INTO STATUES AS   THE BOOMERANG DISAPPEARS IN A   FLASH OF LIGHT!!"
1113 DG=1:BG=2:GOSUB50:GOSUB305:GOTO1000
1120 IFCD$="THROW BOOMERANG"ANDBG=1THEN1122
1121 GOTO1130
1122 CLS:PRINT@320,"THE BOOMERANG FLIES AWAY FROM   YOU, THEN RETURNS AND CHOPS OFF YOUR HEAD!!":END
1130 IFP=12ANDDG=3THENPRINT@384,"THE DOGS RUSH UP AND BREAK YOUR NECK !!!":END
1140 IFCD$="DRINK WINE"THEN1142
1141 GOTO1150
1142 IFW=1THENPRINT@384,"THE WINE IS DRY AND A POWDER    AFTER 4000 YEARS !!";:FORZZ=1TO70:PRINT" ";:NEXT:W=2:GOTO1000
1143 PRINT@384,"GOOD IDEA! I COULD USE SOME.    BUT, UNFORTUNATELY, WE DON'T    HAVE ANY.";:FORZZ=1TO30:PRINT" ";:NEXT
1144 GOTO1000
1150 IFCD$="PLAY FLUTE"ANDP=13ANDC=3THEN1152
1151 GOTO1160
1152 PRINT@384,"THE COBRAS SEPARATE AND CLEAR A PATH FOR YOU TO WALK THROUGH THEROOM.";:FORZZ=1TO50:PRINT" ";:NEXT
1153 C=1:GOSUB10000:GOTO1000
1160 IFP=13ANDC=3THENPRINT@384,"YOU DIE A HORRIBLE DEATH FROM   THE COBRAS' BITES !":END
1170 IFCD$="PLAY FLUTE"THENPRINT@384,"AAH, MUSIC TO THE EARS!";:FORZZ=1TO102:PRINT" ";:NEXT:GOTO1000
1180 IFCD$="READ BOOMERANG"THEN1182
1181 GOTO1200
1182 GOSUB9
1183 PRINT@384,"IT SAYS - FOR PROTECTION AGAINSTEVIL SPIRITS IN THE PASSAGE TO  THE AFTERLIFE.";:GOTO1000
1200 IFCD$="SHOW AUTHOR"THENCLS:PRINT@228,"CONCEIVED AND WRITTEN BY:":PRINT:PRINTTAB(10)"TIM KOONCE":GOSUB50:NP=P:GOTO2550
1210 IFCD$="LOOK"THENNP=P:GOTO2550
1990 GOSUB9:PRINT@384,"I DON'T KNOW HOW TO "CD$".":GOTO1000
1999 ***INVENTORY***
2000 GOSUB9:PRINT@384,;
2010 IFBG=1THENPRINT"BOOMERANG  ";
2020 IFBX=1THEN2022
2021 GOTO2030
2022 IFKY<>1THENPRINT"LOCKED BOX  ";:GOTO2030
2023 PRINT"BOX  ";
2030 IFFL=1THENPRINT"FLUTE    ";
2040 IFKY=1THENPRINT"KEY    ";
2050 IFT=1THENPRINT"GOLD TABLETS  ";
2060 IFW=1THENPRINT"WINE BOTTLE  ";
2065 IFGR=1THENPRINT"GOLD RING";
2070 PRINT:GOTO1000
2499 ***LEAVE***
2500 PRINT@352,;FORZZ=1TO31:PRINT" ";:NEXT:PRINT@352,"WHICH DIRECTION (N,S,E,W) ?";
2510 A$=INKEY$:IFA$=""THEN2510
2520 M$="NESW":I$=A$:BB=1:GOSUB7:DN=FF:IFDN=0THEN1000
2530 AP=P*8+DN*2-9
2540 NP=VAL(MID$(JI$,AP,2))
2545 IFNP=0THEN1000
2550 P=NP:ONP GOSUB190,200,210,220,230,240,250,260,270,280,290,300,310,320,330,340,350,360,370
2560 GOTO1000
2999 REM ***GET***
3000 REM ASSUME THAT "GET" FOLLOWED BY ONE SPACE
3010 Q$=MID$(CD$,5):IFQ$="BOX"ANDP=3ANDBX=3THENBX=1:GOTO3300
3020 IFQ$="FLUTE"ANDP=4ANDFL=3THENFL=1:GOTO3300
3030 IFQ$="TABLETS"ANDP=12ANDT=3THENT=1:GOTO3300
3040 IFQ$="KEY"ANDP=7ANDKY=3THENKY=1:GOTO3300
3050 IFQ$="RING"ANDP=6ANDGR=3THENGR=1:GOTO3300
3200 GOSUB9:PRINT@384,"I CAN'T GET "Q$".";:GOTO1000
3300 GOSUB9:PRINT@384,"OK, YOU'VE GOT THE "Q$".";:GOTO1000
3499 REM ***OPEN***
3500 Q$=MID$(CD$,6)
3510 IFQ$="BOX"THEN3512
3511 GOTO3520
3512 GOSUB9
3513 IFKY=3THENPRINT@384,"THE BOX IS LOCKED.";:FORZZ=1TO109:PRINT" ";:NEXT:GOTO1000
3514 IFKY=1THEN3516
3515 GOTO3520
3516 PRINT@384,"THE BOX HOLDS A BOTTLE OF WINE  AND A CEREMONIAL BOOMERANG WITH INSCRIPTIONS ON THE SIDES.";
3517 BG=1:W=1:GOTO1000
3520 IFLEFT$(Q$,5)="SARCO"ANDP=10THEN3522
3521 GOTO3530
3522 IFOS=0THENGOSUB9:PRINT@384,"I WOULDN'T IF I WERE YOU.":OS=1:GOTO1000
3523 GOSUB9:PRINT@384,"OKAY, YOU'RE DEAD.":END
3530 IFQ$="DOOR"THENPRINT@384,"OK.";:FORZZ=1TO124:PRINT" ";:NEXT:GOTO1000
3540 GOSUB9:PRINT@384,"I CAN'T OPEN "Q$".";:GOTO1000
3999 REM ***SEARCH***
4000 Q$=MID$(CD$,8)
4010 IFQ$="STATUE"ANDP=14THEN4012
4011 GOTO4020
4012 GOSUB9:PRINT@384,"THE BASE READS-I AM THE SUN GOD.ONLY THROUGH ME CAN ANYONE FIND THE AFTERLIFE.";:GOTO1000
4020 IFLEFT$(Q$,8)="SKELETON"ANDP=11THEN4022
4021 GOTO4030
4022 GOSUB9:PRINT@384,"THEY APPEAR TO BE THE SKELETONS OF 18TH CENTURY POACHERS. THEY  DIED OF BROKEN NECKS.";:GOTO1000
4030 IFQ$="BOX"ANDBX=1THEN4032
4031 GOTO4040
4032 GOSUB9:PRINT@384,"THE BOX IS LOCKED.":GOTO1000
4040 IFQ$="PYRAMID"THEN4042
4041 GOTO4050
4042 GOSUB9:PRINT@384,"A PLAQUE READS:   CONCEIVED AND WRITTEN BY:  TIM KOONCE.";:GOTO1000
4050 IFQ$="BOAT"ANDP=6ANDGR=3THENGOSUB9:PRINT@384,"THERE IS A GOLD RING HERE.":GOTO1000
4100 GOSUB9:PRINT@384,"THERE IS NOTHING UNUSUAL ABOUT  THE "Q$".";:GOTO1000
4499 REM ***DROP***
4500 Q$=MID$(CD$,6)
4510 IFQ$="BOOMERANG"ANDBG=1THENBG=3:GOTO4700
4520 IFQ$="BOX"ANDBX=1THENBX=3:GOTO4700
4530 IFQ$="FLUTE"ANDFL=1THENFL=3:GOTO4700
4540 IFQ$="KEY"ANDKY=1THENKY=3:GOTO4700
4550 IFQ$="TABLETS"ANDT=1THENT=3:GOTO4700
4560 IFQ$="WINE"ANDW=1THENW=3:GOTO4700
4570 IFQ$="RING"ANDGR=1THENGR=3:GOTO4700
4600 GOSUB9:PRINT@384,"YOU DON'T HAVE A "Q$".";:GOTO1000
4700 GOSUB9:PRINT@384,"YOU'VE DROPPED THE "Q$".";:GOTO1000
9000 CLS:PRINT@0,CHR$(207)"лллллллллллллллллллллллллллллл"CHR$(207);
9001 FORX=1TOD/2-1:PRINTCHR$(207)"──────────────────────────────"CHR$(207);:NEXT
9002 PRINTCHR$(207)"цццццццццццццццццццццццццццццц"CHR$(207);
9010 FORX=1TO4:IFMID$(D$,X,1)="Y"THENONX GOSUB9030,9040,9050,9060
9020 NEXT:RETURN
9030 PRINT@12,"оооооооо";:RETURN
9040 FORX1=D*8-18TOD*8+78STEP32:PRINT@X1,CHR$(207);:NEXT:RETURN
9050 PRINT@D*16+12,"оооооооо";:RETURN
9060 FORX1=D*8-47TOD*8+49STEP32:PRINT@X1,CHR$(207);:NEXT:RETURN
10000 REM parting of cobras
10010 FORX=1154TO1181:A=PEEK(15360+X):B=PEEK(15360+X+32):POKE15360+X,128:POKE15360+X+32,128
10020 IFPEEK(15360+X-32)=128THENPOKE15360+X-32,A
10030 IFPEEK(15360+X+64)=128THENPOKE15360+X+64,B
10040 NEXT:RETURN
