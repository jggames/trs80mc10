0 CLEAR1200:DIMC1,CC:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
100 CLS:PRINT@129,"THE DOG-STAR MISSION ADVENTURE"
130 PRINTTAB(8)"BY LANCE MICKLUS,"
140 PRINTTAB(9)"COPYRIGHT 1979":PRINT:PRINT
145 PRINTTAB(3)"MC-10 EDITS BY JIM GERRIE"
150 PRINT@482,"PRESS ANY KEY TO START GAME!";
160 I$=INKEY$:IFI$=""THEN160
170 CLS
200 GOSUB30300
450 LC=2:SL=64:BL=4:GF=50:RV=16396
600 GOTO10300
2125 IFTC<25ORRND(GF)<>1THEN2425
2150 IFTC=300THENGF=20
2175 IFLC<3ORLC=9ORLC=26ORLC=36ORLC=37THEN2425
2200 IFLC>26ANDLC<31THEN2425
2225 M$="HOLY SMOKES. AN ARMED GUARD JUST WALKED IN.":GOSUB1
2250 GOSUB20350:IFVB<>12ORNO<>15THEN5575
2275 X=13:GOSUB21450:IFY<>-1THEN5575
2300 IFBL=0THENCLS:M$="I'M OUT OF AMMUNITION.":GOSUB1:GOTO5600
2325 POKE49151,64:M$="ZZZAP! NO MORE GUARD.":GOSUB1:SOUND240,1
2350 BL=BL-1:IFBL=0THENM$="I'M OUT OF AMMUNITION.":GOSUB1
2425 IFMD<>TCTHEN2550
2450 X=22:GOSUB21450:IFY<>-1THEN2550
2475 M$="YOUR MCDONALD'S HAMBURGER IS COLD.":GOSUB1
2550 GOSUB20350:TC=TC+1
2575 IFVB=0ANDNO=0THEN2650
2576 GOTO2800
2650 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB1:GOTO2125
2725 M$="NOTHING HAPPENED.":GOSUB1:GOTO2125
2800 IFVB>1ORNO>7THEN3175
2825 IFNO=0THEN2650
2850 IFDS(LC,NO-1)=0THENM$="I CAN'T GO THAT WAY!":GOSUB1:GOTO2125
2875 IFDRANDDS(LC,NO-1)>2ANDDS(LC,NO-1)<6THENGOSUB20000
2900 IFNO=3ANDLC=31ANDNOTDJTHENM$=M0$:GOSUB1:GOTO2125
2925 IFLC=35ANDDS(LC,NO-1)=36ANDOB(21,1)<>0THENM$="THE ROBOT WON'T LET ME THROUGH.":GOSUB1:GOTO2125
2950 IFLC=17ANDOB(13,1)=17THEN5575
2975 IFLC=9ANDOB(5,1)=9THEN5575
3000 IFLC=9ORLC=17THENHE$(LC)=""
3025 LC=DS(LC,NO-1)
3050 IFLC=34THENHE$(LC)=""
3075 IFLC=26THEN11800
3100 GOTO10300
3175 IFVB=3THEN10300
3250 IFVB<>2THEN3700
3275 IFNO=0THENM$="I DON'T KNOW WHAT A "+CHR$(34)+NO$(0)+CHR$(34)+" IS.":GOSUB1:GOTO2125
3300 IFCR>5THENM$="I CAN'T CARRY ANY MORE.":GOSUB1:M$="HINT: DROP SOMETHING.":GOSUB1:GOTO2125
3325 FORI=1TOLO:IF(OB(I,0)=NO)THEN3350
3326 NEXTI:GOTO2650
3350 IFOB(I,1)=-1THENM$="I'M ALREADY CARRYING IT.":GOSUB1:GOTO2125
3375 IFOB(I,1)<>LCTHENM$="I DON'T SEE IT.":GOSUB1:GOTO2125
3400 IFNO<>37THEN3475
3425 X=13:GOSUB21450:IFY<>-1THENM$="I DON'T HAVE A BLASTER TO PUT IT IN.":GOSUB1:GOTO2125
3450 BL=4:OB(I,1)=0:M$="MY BLASTER'S RELOADED.":GOSUB1:SOUND100,2:SOUND1,2:GOTO2125
3475 IFNO=15ORNO=25ORNO=34THENM$="HE LOOKS PRETTY MEAN TO ME.":GOSUB1:GOTO2125
3500 CR=CR+1:OB(I,1)=-1:PRINT"O.K.":FORI=1TO1:NEXTI
3525 IFNO=14ANDNOTCMTHENM$="A VOICE SAYS: "+CHR$(34)+"SESAME"+CHR$(34)+".":GOSUB1:CM=-1
3550 IFNO=22ANDMD=0THENMD=TC+50
3575 IFNO=12THENHE$(2)=""
3600 IFNO=13THENHE$(7)=""
3625 GOTO2125
3700 IFVB<>4THEN3875
3725 PRINT"I'M CARRYING:"
3750 K=0:FORI=1TOLO:IFOB(I,1)=-1THENM$=OB$(I):GOSUB1:K=1
3775 NEXTI:IFK=0THENM$="NOTHING":GOSUB1
3800 PRINT:GOTO2125
3875 IFVB<>5THEN3950
3876 GOSUB21050:GOTO2125
3950 IFVB<>6THEN4200
3975 IF(NO=0)THEN3275
4000 IFLC=2THENM$="THERE'S NO ROOM HERE.":GOSUB1:GOTO2125
4025 IC=0:FORI=1TOLO:IFOB(I,1)=LCTHENIC=IC+1
4050 NEXTI:IFIC>12THENM$="THERE NOT ENOUGH ROOM. GET RID OF SOMETHING.":GOSUB1:GOTO2125
4075 FORI=0TOLO:IF(OB(I,0)=NO)THEN4100
4076 NEXTI:GOTO2650
4100 IFOB(I,1)<>-1THENM$="I'M NOT CARRYING IT.":GOSUB1:GOTO2125
4125 CR=CR-1:OB(I,1)=LC:PRINT"O.K.":SOUND150,2:GOTO2125
4200 IFVB<>7THEN4300
4225 IFHE$(LC)=""THENM$="HOW AM I SUPPOSED TO KNOW WHAT TO DO?":GOSUB1:GOTO2125
4226 M$=HE$(LC):GOSUB1:GOTO2125
4300 IFVB<>8THEN4750
4375 M$="PRESS (ENTER) WHEN READY TO RECORD":GOSUB1
4400 GOSUB22000
4500 OB(1,3)=TB:OB(2,3)=TC:OB(3,3)=CM:OB(4,3)=DR:OB(5,3)=BL:OB(6,3)=MD:OB(7,3)=GF:OB(8,3)=DJ:OB(9,3)=CR:OB(10,3)=LC
4525 CSAVE*OB,"DSTARDAT"
4675 PRINT"O.K.":GOTO2125
4750 IFVB<>9THEN5150
4825 M$="PRESS (ENTER) WHEN READY TO READ TAPE":GOSUB1
4850 GOSUB22000:PRINT"PLAY TAPE NOW"
4950 CLOAD*OB,"DSTARDAT"
5075 TB=OB(1,3):TC=OB(2,3):CM=OB(3,3):DR=OB(4,3):BL=OB(5,3):MD=OB(6,3):GF=OB(7,3):DJ=OB(8,3):CR=OB(9,3):LC=OB(10,3)
5125 GOTO10300
5150 IFVB<>10THEN5225
5151 CLS:J=0:GOTO11200
5225 IFVB<>11THEN5725
5250 IFNO<>10THEN2650
5275 IFLC<>2ANDLC<>11THENPRINT"WHAT BUTTON,":GOTO2125
5300 IFLC=11ANDNOTTBTHENTB=-1:M$=N2$:GOSUB1:GOTO2125
5325 IFLC=11ANDTBTHENTB=0:M$=N3$:GOSUB1:GOTO2125
5375 X=12:GOSUB21450:IFY<>1THEN2725
5425 X=24:GOSUB21450:IFY<>1THEN2725
5450 IFNOTTBTHENM$=N3$:GOSUB1:GOTO2725
5475 IFNOTDRTHENM$=N4$:GOSUB1:GOTO2725
5500 GOTO11150
5575 CLS:PRINT"H E L P ! ! !"
5600 M$="ROCHE SOLDIERS ARE EVERYWHERE. I'VE BEEN CAPTURED.":GOSUB1
5625 M$="I'M NOW A PRISONER. WOE IS ME...":GOSUB1
5650 GOTO11500
5725 IFVB<>12ORNO=0THEN6025
5750 IFBL=0THENM$="BUT I DON'T HAVE ANY AMMUNITION LEFT.":GOSUB1:GOTO2125
5775 X=13:GOSUB21450:IFY<>-1THENM$="BUT I'M NOT CARRYING A BLASTER.":GOSUB1:GOTO2125
5800 X=NO:GOSUB21450:IFY=-1THENM$="I CAN'T. I'M HOLDING IT.":GOSUB1:GOTO2125
5825 IFNO=34THENPOKE49151,64:M$="ZZZAP!":GOSUB1:SOUND240,1:BL=BL-1:GOTO2125
5850 IFY<>LCTHENM$="I DON'T SEE IT.":GOSUB1:GOTO2125
5875 FORI=1TOLO:IF(OB(I,0)=NO)THEN5900
5876 NEXTI:GOTO2650
5900 OB(I,1)=0:POKE49151,64:M$="ZZZAP!!! THE "+NO$(NO)+" VAPORIZED.":GOSUB1:SOUND240,1
5925 BL=BL-1:IFBL=0THENM$="I'M OUT OF AMMUNITION.":GOSUB1
5950 GOTO2125
6025 IFVB<>13THEN6275
6050 IFNO=0THENM$="SAY WHAT?":GOSUB1:GOTO2125
6075 X=14:GOSUB21450
6100 IFY<>-1ORNO<>19THENM$="O.K. "+NO$(NO):GOSUB1:GOTO2125
6125 IFDRTHEN2725
6150 DR=-1:M$="A VOICE COMES OVER THE P.A. SYSTEM AND SAYS:":GOSUB1
6160 M$="OPENNING FLIGHT DECK DOORS":GOSUB1
6175 IFLC>2ANDLC<6THENM$="YIPS!!! THERE'S NO AIR!!! CROAK...":GOSUB1:END
6200 GOTO2125
6275 IFVB<>14THEN6750
6300 IFNO<>20ANDNO<>16ANDNO<>11ANDNO<>33THEN2650
6325 IFNO=20THEN6550
6350 IFNO<>16THEN6450
6375 IFOB(6,1)=-1THENM$="SORRY. I'M NOT A CARTOGRAPHER.":GOSUB1:GOTO2125
6400 IFOB(6,1)=LCTHENM$="TRY GET MAP.":GOSUB1:GOTO2125
6425 M$="IT'S NOT HERE.":GOSUB1:GOTO2125
6450 X=NO:GOSUB21450:IFY<>LC AND Y<>-1THEN6425
6475 IFNO=11THENM$="IT SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):GOSUB1
6500 IFNO=33THENM$="IT SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):GOSUB1
6525 GOTO2125
6550 IFLC<>13THENM$="I DON'T SEE ANY.":GOSUB1:GOTO2125
6575 M$="IT SAYS ON THE WALL,":GOSUB1
6600 M$=CHR$(34)+"YOUR MOTHER'S GOT A BIG NOSE"+CHR$(34):GOSUB1
6625 M$=CHR$(34)+"KILROY MADE IT HERE, TOO"+CHR$(34):GOSUB1
6650 M$=CHR$(34)+"SAY SECURITY"+CHR$(34):GOSUB1
6675 GOTO2125
6750 IFVB<>15THEN6975
6775 IFNO=0THENM$="WHAT'S A "+NO$(0)+"?":GOSUB1:GOTO2125
6800 IFNO<>22THENM$="DON'T BE RIDICULOUS.":GOSUB1:GOTO2125
6825 X=22:GOSUB21450:IFY<>-1THENM$="I'M NOT HOLDING IT.":GOSUB1:GOTO2125
6850 FORI=1TOLO:IFOB(I,0)=22THEN6875
6851 NEXTI:M$="I DON'T KNOW WHERE IT IS.":GOSUB1:GOTO2125
6875 OB(I,1)=0:M$="CHUMP - CHUMP. HUMMM, GOOD.":GOSUB1
6900 SOUND50,1:SOUND25,1:GOTO2125
6975 IFVB<>16ORNO<>23ORLC<>16THEN7125
7000 X=23:GOSUB21450:IFY<>-1THENM$=M1$:GOSUB1:GOTO2125
7025 OB(11,1)=0:OB(14,1)=16:CR=CR-1:M$=M2$:GOSUB1
7050 GOTO2125
7125 IFVB<>18ORNO<>36THEN7275
7150 IFNO<>36ORLC<>31THEN2650
7175 X=17:GOSUB21450:IFY<>-1THENM$=M3$:GOSUB1:GOTO2125
7200 HE$(31)="":DJ=-1:M$=M4$:GOSUB1:GOTO2125
7275 IFVB<>19ORNO=0THEN7600
7325 IFLC<>35ANDNO=34THENM$=M5$:GOSUB1:GOTO2125
7350 X=22:GOSUB21450:IFY<>-1THENM$=M6$:GOSUB1:GOTO2125
7375 X=35:GOSUB21450:IFY=1ORY=LCANDNO=35THENM$="I DON'T SEE HER.":GOSUB1:GOTO2125
7376 IFNO=35THEN9000
7400 IFNO<>34THENM$=NO$(0)+M8$:GOSUB1:GOTO2125
7425 IFTC>MDTHENM$=M9$:GOSUB1:GOTO2125
7450 FORI=1TOLO:IFOB(I,0)=34THEN7475
7451 NEXTI:GOTO2650
7475 K=I:FORI=1TOLO:IFOB(I,0)=22THEN7500
7476 NEXTI:GOTO2650
7500 M$=N0$:GOSUB1:HE$(35)=""
7525 OB(K,1)=0:OB(I,1)=0:SOUND50,1:SOUND25,1:GOTO2125
7600 IFVB<>17THEN7700
7625 M$=NO$(0):GOSUB1:GOTO2125
7700 IFVB<>20THEN7975
7725 IFNO=0THEN2650
7750 X=NO:GOSUB21450:IFY=-1THENM$="I'M CARRYING IT. THAT'S IMPOSSIBLE.":GOSUB1:GOTO2125
7775 IFY=LCTHEN7850
7800 IFNO<11ORNO=19ORNO=20ORNO=30THEN2650
7825 M$="I CAN'T HIT SOMETHING I CAN'T SEE.":GOSUB1:GOTO2125
7850 IFNO=15ORNO=25ORNO=34THENM$="I'D RATHER NOT. HE MIGHT HIT ME BACK!":GOSUB1:GOTO2125
7875 IFNO=35THENM$="THAT'S NOT NICE!":GOSUB1:GOTO2125
7900 GOTO2725
7975 IFVB<>21THEN8000
7976 M$="I'M NOT STRONG ENOUGH TO KILL ANYTHING.":GOSUB1:GOTO2125
8000 GOTO2650
9000 M$=M7$:GOSUB1:K=I:FORI=1TOLO
9010 IFOB(I,0)=22THEN7525
9020 NEXTI:GOTO2650
10300 CLS:M$=DS$(LC):GOSUB1:A$=""
10305 IFLC=35THENGF=10
10400 IFLC=7THEN10700
10450 K=0:FORI=1TOLO:IFOB(I,1)<>LCTHEN10600
10500 IFK=0THENK=1:M$="AROUND ME I SEE:":GOSUB1:A$=OB$(I):GOTO10600
10550 IFLEN(A$)+5+LEN(OB$(I))>SLTHENM$=A$:GOSUB1:A$=OB$(I):GOTO10600
10551 A$=A$+", "+OB$(I)
10600 NEXTI
10650 IFA$<>""THENM$=A$:GOSUB1
10700 M$="OBVIOUS DIRECTIONS ARE ":K=0
10750 FORI=0TO5:IFDS(LC,I)=0THEN10900
10800 IFK<>0THENM$=M$+", "
10850 M$=M$+NO$(I+1):K=1
10900 NEXTI
10950 IFK=0THENM$=M$+"UNKNOWN"
11000 M$=M$+".":GOSUB1:GOTO2125
11150 CLS:GOSUB21050
11200 IFJ=0THENM$="WE HAVE FAILED OUR MISSION. THE FORCES OF PRINCESS LEYA WILL BE CONQUERED.":GOSUB1
11250 IFJ=SCTHENGOSUB20010
11300 IFJ>0ANDJ<SCTHENM$="WE HAVE HELPED THE FORCES OF PRINCESS LEYA DEFEND THE GALAXY. LONG LIVE FREEDOM!":GOSUB1 
11500 PRINT
11550 INPUT"PLAY AGAIN (Y OR N)";A$:A$=LEFT$(A$,1)
11600 IFA$="Y"ORA$="Y"THENRUN
11601 END
11800 CLS:M$="A VOICE BOOMS OUT, "+CHR$(34)+"WHO GOES THERE"+CHR$(34):GOSUB1
11850 GOSUB20350
11900 IFVB<>13ORNO<>30THEN5575
11950 M$=N1$:GOSUB1
12000 GOSUB20350
12050 X=31:GOSUB21450:IFY<>-1THEN5575
12100 IFVB<>17ORNO<>31THEN5575
12101 GOTO10300
20000 M$="I CAN'T GO THAT WAY. FLIGHT DECK DOORS ARE OPEN. NO AIR!!!":GOSUB1:GOTO2125:RETURN
20010 M$="WE ARE HEROS. THE FORCES OF PRINCESS LEYA WILL CONQUER THE EVIL ROCHE SOLDIERS, "
20020 M$=M$+"AND FREEDOM WILL PREVAIL THROUGHOUT THE GALAXY.":GOSUB1:RETURN
20350 PRINT"WHAT SHOULD I DO?":INPUTI$:PRINT:CM$="":IFI$=""THEN20350
20360 FORIL=1TOLEN(I$):IN=ASC(MID$(I$,IL,1)):IFIN>96ANDIN<123THENIN=(IN AND223)
20365 CM$=CM$+CHR$(IN):NEXTIL
20370 IFCM$="N"THENCM$="NORTH"
20371 IFCM$="S"THENCM$="SOUTH"
20372 IFCM$="E"THENCM$="EAST" 
20375 IFCM$="W"THENCM$="WEST"
20376 IFCM$="U"THENCM$="UP"
20377 IFCM$="D"THENCM$="DOWN" 
20380 IFCM$="L"THENCM$="LOOK"
20381 IFCM$="I"THENCM$="INVENTORY"
20382 IFCM$="H"THENCM$="HELP"
20395 VB$(0)="":NO$(0)="":VB=0:NO=0:IFLEN(CM$)=0THEN20350
20400 FORZL=1TOLEN(CM$):IFMID$(CM$,ZL,1)<>" "THENVB$(0)=VB$(0)+MID$(CM$,ZL,1):NEXTZL
20500 FORZL=1TOLV:IFVB$(ZL)<>""ANDLEFT$(VB$(0),LEN(VB$(ZL)))=VB$(ZL)THENVB=ZL:GOTO20600
20501 NEXTZL
20550 VB=0:NO$(0)=VB$(0):GOTO20650
20600 IFLEN(VB$(0))+1>=LEN(CM$)THENNO=0:RETURN
20601 NO$(0)=RIGHT$(CM$,LEN(CM$)-1-LEN(VB$(0)))
20650 FORZL=1TOLN:IFNO$(ZL)<>""ANDLEFT$(NO$(0),LEN(NO$(ZL)))=NO$(ZL)THENNO=ZL:GOTO20800
20651 NEXTZL
20700 NO=0:RETURN
20800 FORZL=1TO1:NEXTZL:RETURN
21050 J=0:FORI=1TOLO:IFOB(I,1)=1THENJ=J+OB(I,2)
21100 NEXTI:M$="OUT OF A MAXIMUM OF"+STR$(SC)+" POINTS, YOU HAVE"+STR$(J)+" POINTS.":GOSUB1
21150 IFJ=0THENM$="WE'RE NOT DOING TOO GOOD.":GOSUB1
21200 RETURN
21450 FORZL=0 TO LO:IFOB(ZL,0)=XTHENY=OB(ZL,1):GOTO21500
21451 NEXTZL:Y=-99:RETURN
21500 FORZL=1TO1:NEXTZL:RETURN
22000 A$=INKEY$
22010 IFINKEY$=""THEN22010
22011 RETURN
30300 SC=215
30600 LV=21:DIMVB$(LV)
30650 VB$(1)="GO":VB$(2)="GET":VB$(3)="LOOK"
30700 VB$(4)="INVEN":VB$(5)="SCORE":VB$(6)="DROP"
30750 VB$(7)="HELP":VB$(8)="SAVE":VB$(9)="LOAD":VB$(10)="QUIT"
30800 VB$(11)="PRESS":VB$(12)="SHOOT":VB$(13)="SAY"
30850 VB$(14)="READ":VB$(15)="EAT":VB$(16)="CSAVE"
30900 VB$(17)="SHOW":VB$(18)="OPEN":VB$(19)="FEED"
30950 VB$(20)="HIT":VB$(21)="KILL"
31100 LN=37:DIMNO$(LN)
31150 NO$(1)="NORTH":NO$(2)="EAST":NO$(3)="SOUTH"
31200 NO$(4)="WEST":NO$(5)="UP":NO$(6)="DOWN"
31250 NO$(10)="BUTTON":NO$(11)="TAG":NO$(12)="FUEL"
31300 NO$(13)="BLASTER":NO$(14)="COMMUNICATOR":NO$(15)="GUARD"
31350 NO$(16)="MAP":NO$(17)="KEYS":NO$(18)="NECKLACE"
31400 NO$(19)="SESAME":NO$(20)="GRAFFITI"
31450 NO$(21)="CAPE":NO$(22)="HAMBURGER"
31500 NO$(23)="TAPE":NO$(24)="TURBO"
31550 NO$(25)="SCIENTIST":NO$(26)="PLANS"
31600 NO$(27)="SCHEMATIC":NO$(28)="DEVICE":NO$(29)="GUN"
31650 NO$(30)="SECURITY":NO$(31)="I.D.":NO$(32)="CRYSTALS"
31700 NO$(33)="SIGN":NO$(34)="ROBOT":NO$(35)="PRINCESS"
31750 NO$(36)="DOOR":NO$(37)="AMMUNITION"
31900 CL=37:DIMDS$(CL)
31950 DS$(1)="I'M IN THE PASSENGER & STORAGE COMPARTMENT OF MY SPACE SHIP. THERE'S AN EXIT HERE TO LEAVE THE SHIP."
32000 DATA2,0,0,0,0,3
32050 DS$(2)="I'M IN THE COCKPIT OF MY SPACE SHIP. A LARGE RED BUTTON SAYS "+CHR$(34)+"PRESS TO BLAST OFF"+CHR$(34)
32100 DATA0,0,1,0,0,0
32150 DS$(3)="I'M STANDING NEXT TO MY SPACE SHIP WHICH IS LOCATED ON A HUGE FLIGHT DECK."
32200 DATA18,0,4,0,1,0
32250 DS$(4)="I'M OUT ON THE FLIGHT DECK OF GENERAL DOOM'S BATTLE CRUISER."
32300 DATA3,5,4,4,0,0
32350 DS$(5)=DS$(4):DATA4,6,5,4,0,0
32400 DS$(6)="I'M IN A HALLWAY. THERE ARE DOORS ON ALL SIDES. THE DOOR TO THE NORTH SAYS: "
32401 DS$(6)=DS$(6)+CHR$(34)+"CLOSED FOR THE DAY"+CHR$(34)
32450 DATA7,0,8,5,0,0
32500 DS$(7)="I'M IN THE SUPPLY DEPOT. AROUND ME I SEE: ALL KINDS OF THINGS"
32550 DATA0,0,6,0,0,0
32600 DS$(8)="I'M AT THE END OF ONE OF THE HALLWAYS. I CAN HERE VOICES NEARBY. SOUNDS LIKE GUARDS."
32650 DATA6,10,0,9,0,12
32700 DS$(9)="I'M IN THE STRATEGY PLANNING ROOM.":DATA11,8,0,0,0,0
32750 DS$(10)="I'M IN THE DECONTAMINATION AREA.":DATA0,14,0,8,0,0
32800 DS$(11)="THIS AREA IS THE TRACTOR BEAM CONTROL ROOM. A LARGE SIGN WARNS: "+CHR$(34)+"DO NOT PRESS ANY BUTTONS"+CHR$(34)
32850 DATA0,0,9,0,0,0
32900 DS$(12)="I'M IN ANOTHER HALLWAY. TO THE EAST IS A RESTROOM."
32950 DATA15,13,0,0,8,0
33000 DS$(13)=DS$(13)+"PIPES LEAD UP THROUGH THE CEILING."
33050 DATA15,0,0,12,27,0
33100 DS$(14)="THIS APPEARS TO BE AN INTERROGATION ROOM.":DATA0,0,0,10,0,0
33150 DS$(15)="I'M IN A LOUNGE.":DATA0,0,13,12,0,0
33200 DS$(16)="THIS IS A COMPUTER ROOM. THERE'S A TRS-80 IN HERE. ON THE SCREEN IT SAYS: "+CHR$(34)+"CSAVE TAPE"+CHR$(34)
33201 DATA17,0,18,0,0,0
33250 DS$(17)="I'M IN A TESTING LABORTORY.":DATA 0,0,16,0,0,0
33300 DS$(18)="I'M IN A HALLWAY. A LARGE ARROW POINTS EAST AND SAYS: "+CHR$(34)+"TO THE VAULT"+CHR$(34)
33350 DATA16,25,3,19,0,0
33400 DS$(19)="THIS IS THE ENTRANCE TO THE DEVELOPMENT LAB SECTION":DATA20,18,21,20,22,0
33450 DS$(20)="I'M IN A LONG CORRIDOR. THERE ARE LABORTORIES ALL AROUND ME.":DATA19,23,21,20,22,24
33500 DS$(21)="I'M IN A RESEARCH LAB.":DATA20,0,0,0,0,0
33550 DS$(22)="I'M LOST!":DATA22,22,22,22,22,20
33600 DS$(23)=DS$(21):DATA0,0,0,20,0,0
33650 DS$(24)=DS$(21):DATA0,0,0,0,20,0
33700 DS$(25)="I'M NEAR THE ENTRANCE TO THE VAULT. A SIGN HERE SAYS: "+CHR$(34)+"AUTHORIZED SECURITY PERSONEL ONLY"+CHR$(34)
33750 DATA0,26,0,18,0,0
33800 DS$(26)="I'M IN THE VAULT.":DATA0,0,0,25,0,0
33850 DS$(27)="I'M IN A PIPE TUNNEL WHICH LEADS IN EVERY DIRECTION.":DATA 28,27,27,27,27,13
33900 DS$(28)=DS$(27):DATA 29,29,29,29,30,29
33950 DS$(29)="I'M LOST IN A MAZE OF PIPES.":DATA 28,29,29,29,29,27
34000 DS$(30)="I'M IN THE PIPE MAZE. BELOW ME I THINK I CAN SEE THE JAIL."
34050 DATA 29,29,28,29,29,31
34100 DS$(31)="I'M IN THE JAIL."
34150 DATA32,33,34,35,0,0
34200 DS$(32)="I'M IN A JAIL CELL.":DATA0,0,31,0,0,0
34250 DS$(33)=DS$(32):DATA0,0,0,31,0,0
34300 DS$(34)=DS$(32):DATA31,0,0,0,0,0
34350 DS$(35)="I'M AT THE SECURITY DESK. TO THE NORTH AN ELEVATOR.":DATA 36,31,0,0,0,0
34400 DS$(36)="I'M IN THE ELEVATOR.":DATA 0,0,35,0,37,0
34450 DS$(37)=DS$(36):DATA0,0,14,0,0,36
34850 LO=23:DIMOB$(LO)
34900 OB$(1)="A TAG WHICH SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):DATA 11,5,0
34950 OB$(2)="ANTI-MATTER FUEL":DATA12,5,5
35000 OB$(3)="BLASTER":DATA13,7,0
35050 OB$(4)="COMMUNICATOR":DATA14,9,0
35100 OB$(5)="A VERY SURPRISED GUARD":DATA15,9,0
35150 OB$(6)="MAP OF THE SHIP":DATA16,29,20
35200 OB$(7)="SOME KEYS":DATA17,9,0
35250 OB$(8)="A SHINESTONE NECKLACE":DATA18,10,20
35300 OB$(9)="PRINCESS LEYA'S CAPE":DATA21,14,5
35350 OB$(10)="MCDONALD'S HAMBURGER":DATA22,15,0
35400 OB$(11)="A CASSETTE TAPE":DATA23,7,0
35450 OB$(12)="A TURBOENCABULATOR":DATA24,17,5
35500 OB$(13)="AN EVIL LOOKING SCIENTIST":DATA25,17,0
35550 OB$(14)="SECRET ATTACK PLANS":DATA26,0,20
35600 OB$(15)="DEATH RAY SCHEMATIC":DATA27,9,20
35650 OB$(16)="CLOAKING DEVICE":DATA28,17,20
35700 OB$(17)="MICRO LASER GUN":DATA29,24,20
35750 OB$(18)="I.D. CARD":DATA31,17,0
35800 OB$(19)="MALIDIUM CRYSTALS (THE TREASURY!)":DATA32,26,30
35850 OB$(20)="A SIGN WHICH SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):DATA33,3,0
35900 OB$(21)="ATTACK ROBOT":DATA34,35,0
35950 OB$(22)="PRINCESS LEYA":DATA35,34,50
36000 OB$(23)="AMMUNITION":DATA37,7,0
36150 DIMDS(CL,5):RESTORE
36200 FORZL=1TOCL:FORZD=0TO5
36250 READDS(ZL,ZD):NEXTZD:NEXTZL
36300 DIMOB(LO,3)
36350 FORZL=1TOLO:READOB(ZL,0),OB(ZL,1),OB(ZL,2):NEXTZL
36500 DIMHE$(CL)
36550 HE$(1)="I THINK WE'RE SUPPOSED TO LEAVE THE STUFF HERE."
36600 HE$(2)="I WONDER IF WE HAVE ENOUGH FUEL?"
36650 HE$(7)="HOW 'BOUT A BLASTER."
36700 HE$(9)="TRY SHOOT GUARD."
36750 HE$(13)="IT MIGHT BE INTERSTING TO READ THE GRAFFITI."
36800 HE$(17)="TRY SHOOT SCIENTIST."
36850 HE$(22)="I'M AS CONFUSED AS YOU ARE.":HE$(29)=HE$(22)
36900 HE$(31)="IT MIGHT HELP IF WE HAD SOME KEYS TO OPEN ANY LOCKED DOORS."
36950 HE$(35)="DID YOU BRING ANYTHING TO EAT?"
37100 M0$="I CAN'T GO THERE. THE DOOR IS LOCKED."
37150 M1$="I'M NOT CARRYING ANY BLANK TAPE."
37200 M2$="THE TRS-80 RECORDED SOMETHING ON THE TAPE, AND THEN IT PRINTED: "+CHR$(34)+"ATTACK PLANS -- VERY SECRET"+CHR$(34)
37250 M3$="I CAN'T. I'M NOT CARRYING ANY KEYS."
37300 M4$="O.K. THE DOOR TO THE JAIL CELL IS UNLOCKED."
37350 M5$="THERE'S NO ROBOT HERE."
37400 M6$="BUT I DON'T HAVE ANY HAMBURGERS."
37450 M7$="CHUMP...CHUMP BURP! THE PRINCESS THANKS YOU FOR A DELICIOUS MEAL."
37500 M8$=" DOESN'T EAT HAMBURGER."
37550 M9$="NOTHING HAPPENED. THE HAMBURGER IS COLD YOU KNOW."
37600 N0$="THE ATTACK ROBOT EATS THE HAMBURGER AND DISAPPEARS."
37650 N1$="I'M AT THE IDENTIFICATION TERMINAL. ON THE SCREEN IT SAYS: "+CHR$(34)+"SHOW I.D."+CHR$(34)
37700 N2$="THE TRACTOR BEAM IS OFF."
37750 N3$="THE TRACTOR BEAM IS ON."
37800 N4$="YOU FORGOT TO OPEN THE FLIGHT DECK DOORS."
37850 RETURN
