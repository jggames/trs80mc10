0 CLEAR1000:GOTO100
10 C1=1:CC=32
20 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO20
30 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN20
40 RETURN
100 CLS:PRINT:PRINT:PRINT:PRINT
110 PRINTTAB(12);"DOG STAR"
120 PRINTTAB(8);"BY LANCE MICKLUS,"
130 PRINTTAB(7);"WINOOSKI, VT. 05404"
140 PRINTTAB(9);"COPYRIGHT 1979":PRINT:PRINT
145 PRINTTAB(3);"MC-10 EDITS BY JIM GERRIE"
150 PRINT:PRINT:PRINT:PRINTTAB(3);"PRESS ANY KEY TO CONTINUE"
160 I$=INKEY$:R=RND(1000):IFI$=""THEN160
170 CLS
200 GOSUB30300
450 LC=2:SL=64:BL=4:GF=50:RV=16396
600 GOTO10300
2125 IFTC<25ORRND(GF)<>1THEN2425
2150 IFTC=300THENGF=20
2175 IFLC<3ORLC=9ORLC=26ORLC=36ORLC=37THEN2425
2200 IFLC>26ANDLC<31THEN2425
2225 M$="HOLY SMOKES. AN ARMED GUARD JUST WALKED IN.":GOSUB10
2250 GOSUB20350:IFVB<>12ORNO<>15THEN5575
2275 X=13:GOSUB21450:IFY<>-1THEN5575
2300 IF BL=0 THEN CLS:M$="I'M OUT OF AMMUNITION.":GOSUB10:GOTO 5600
2325 POKE49151,64:M$="ZZZAP! NO MORE GUARD.":GOSUB10:SOUND240,1
2350 BL=BL-1:IF BL=0 THEN M$="I'M OUT OF AMMUNITION.":GOSUB10
2425 IF MD<>TC THEN 2550
2450 X=22:GOSUB 21450:IF Y<>-1 THEN 2550
2475 M$="YOUR MCDONALD'S HAMBURGER IS COLD.":GOSUB10
2550 GOSUB 20350:TC=TC+1
2575 IF VB=0 AND NO=0 THEN 2650
2576 GOTO2800
2650 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB10:GOTO 2125
2725 M$="NOTHING HAPPENED.":GOSUB10:GOTO 2125
2800 IF VB>1 OR NO>7 THEN 3175
2825 IF NO=0 THEN 2650
2850 IF DS(LC,NO-1)=0 THEN M$="I CAN'T GO THAT WAY!":GOSUB10:GOTO 2125
2875 IF DR AND DS(LC,NO-1)>2 AND DS(LC,NO-1)<6 THENGOSUB20000
2900 IF NO=3 AND LC=31 AND NOT DJ THEN M$=M0$:GOSUB10:GOTO 2125
2925 IF LC=35 AND DS(LC,NO-1)=36 AND OB(21,1)<>0 THEN M$="THE ROBOT WON'T LET ME THROUGH.":GOSUB10:GOTO 2125
2950 IF LC=17 AND OB(13,1)=17 THEN 5575
2975 IF LC=9 AND OB(5,1)=9 THEN 5575
3000 IF LC=9 OR LC=17 THEN HE$(LC)=""
3025 LC=DS(LC,NO-1)
3050 IF LC=34 THEN HE$(LC)=""
3075 IF LC=26 THEN 11800
3100 GOTO 10300
3175 IF VB=3 THEN 10300
3250 IF VB<>2 THEN 3700
3275 IF NO=0 THEN M$="I DON'T KNOW WHAT A "+CHR$(34)+NO$(0)+CHR$(34)+" IS.":GOSUB10:GOTO 2125
3300 IF CR>5 THEN M$="I CAN'T CARRY ANY MORE.":GOSUB10:M$="HINT: DROP SOMETHING.":GOSUB10:GOTO 2125
3325 FOR I=1 TO LO:IF (OB(I,0)=NO) THEN 3350
3326 NEXT I:GOTO 2650
3350 IF OB(I,1)=-1 THEN M$="I'M ALREADY CARRYING IT.":GOSUB10:GOTO 2125
3375 IF OB(I,1)<>LC THEN M$="I DON'T SEE IT.":GOSUB10:GOTO 2125
3400 IF NO<>37 THEN 3475
3425 X=13:GOSUB 21450:IF Y<>-1 THEN M$="I DON'T HAVE A BLASTER TO PUT IT IN.":GOSUB10:GOTO 2125
3450 BL=4:OB(I,1)=0:M$="MY BLASTER'S RELOADED.":GOSUB10:SOUND100,2:SOUND1,2:GOTO 2125
3475 IF NO=15 OR NO=25 OR NO=34 THEN M$="HE LOOKS PRETTY MEAN TO ME.":GOSUB10:GOTO 2125
3500 CR=CR+1:OB(I,1)=-1:PRINT"O.K.":FOR I=1 TO 1:NEXT I
3525 IF NO=14 AND NOT CM THEN M$="A VOICE SAYS: "+CHR$(34)+"SESAME"+CHR$(34)+".":GOSUB10:CM=-1
3550 IF NO=22 AND MD=0 THEN MD=TC+50
3575 IF NO=12 THEN HE$(2)=""
3600 IF NO=13 THEN HE$(7)=""
3625 GOTO 2125
3700 IF VB<>4 THEN 3875
3725 PRINT"I'M CARRYING:"
3750 K=0:FOR I=1 TO LO:IF OB(I,1)=-1 THEN M$=OB$(I):GOSUB10:K=1
3775 NEXT I:IF K=0 THEN M$="NOTHING":GOSUB10
3800 PRINT:GOTO 2125
3875 IF VB<>5 THEN 3950
3876 GOSUB 21050:GOTO 2125
3950 IF VB<>6 THEN 4200
3975 IF (NO=0) THEN 3275
4000 IF LC=2 THEN M$="THERE'S NO ROOM HERE.":GOSUB10:GOTO 2125
4025 IC=0:FOR I=1 TO LO:IF OB(I,1)=LC THEN IC=IC+1
4050 NEXT I:IF IC>12 THEN M$="THERE NOT ENOUGH ROOM. GET RID OF SOMETHING.":GOSUB10:GOTO 2125
4075 FOR I=0 TO LO:IF (OB(I,0)=NO) THEN 4100
4076 NEXT I:GOTO 2650
4100 IF OB(I,1)<>-1 THEN M$="I'M NOT CARRYING IT.":GOSUB10:GOTO 2125
4125 CR=CR-1:OB(I,1)=LC:PRINT"O.K.":SOUND150,2:GOTO 2125
4200 IF VB<>7 THEN 4300
4225 IF HE$(LC)="" THEN M$="HOW AM I SUPPOSED TO KNOW WHAT TO DO?":GOSUB10:GOTO 2125
4226 M$=HE$(LC):GOSUB10:GOTO 2125
4300 IF VB<>8 THEN 4750
4375 M$="PRESS (ENTER) WHEN READY TO RECORD":GOSUB10
4400 GOSUB22000
4500 OB(1,3)=TB:OB(2,3)=TC:OB(3,3)=CM:OB(4,3)=DR:OB(5,3)=BL:OB(6,3)=MD:OB(7,3)=GF:OB(8,3)=DJ:OB(9,3)=CR:OB(10,3)=LC
4525 CSAVE*OB,"DSTARDAT"
4675 PRINT"O.K.":GOTO 2125
4750 IF VB<>9 THEN 5150
4825 M$="PRESS (ENTER) WHEN READY TO READ TAPE":GOSUB10
4850 GOSUB22000:PRINT"PLAY TAPE NOW"
4950 CLOAD*OB,"DSTARDAT"
5075 TB=OB(1,3):TC=OB(2,3):CM=OB(3,3):DR=OB(4,3):BL=OB(5,3):MD=OB(6,3):GF=OB(7,3):DJ=OB(8,3):CR=OB(9,3):LC=OB(10,3)
5125 GOTO 10300
5150 IF VB<>10 THEN 5225
5151 CLS:J=0:GOTO 11200
5225 IF VB<>11 THEN 5725
5250 IF NO<>10 THEN 2650
5275 IF LC<>2 AND LC<>11 THEN PRINT "WHAT BUTTON,":GOTO 2125
5300 IF LC=11 AND NOT TB THEN TB=-1:M$=N2$:GOSUB10:GOTO 2125
5325 IF LC=11 AND TB THEN TB=0:M$=N3$:GOSUB10:GOTO 2125
5375 X=12:GOSUB 21450:IF Y<>1 THEN 2725
5425 X=24:GOSUB 21450:IF Y<>1 THEN 2725
5450 IF NOT TB THEN M$=N3$:GOSUB10:GOTO 2725
5475 IF NOT DR THEN M$=N4$:GOSUB10:GOTO 2725
5500 GOTO 11150
5575 CLS:PRINT"H E L P ! ! !"
5600 M$="ROCHE SOLDIERS ARE EVERYWHERE. I'VE BEEN CAPTURED.":GOSUB10
5625 M$="I'M NOW A PRISONER. WOE IS ME...":GOSUB10
5650 GOTO 11500
5725 IF VB<>12 OR NO=0 THEN 6025
5750 IF BL=0 THEN M$="BUT I DON'T HAVE ANY AMMUNITION LEFT.":GOSUB10:GOTO 2125
5775 X=13:GOSUB 21450:IF Y<>-1 THEN M$="BUT I'M NOT CARRYING A BLASTER.":GOSUB10:GOTO 2125
5800 X=NO:GOSUB 21450:IF Y=-1 THEN M$="I CAN'T. I'M HOLDING IT.":GOSUB10:GOTO 2125
5825 IF NO=34 THEN POKE49151,64:M$="ZZZAP!":GOSUB10:SOUND240,1:BL=BL-1:GOTO 2125
5850 IF Y<>LC THEN M$="I DON'T SEE IT.":GOSUB10:GOTO 2125
5875 FOR I=1 TO LO:IF (OB(I,0)=NO)THEN 5900
5876 NEXT I:GOTO 2650
5900 OB(I,1)=0:POKE 49151,64:M$="ZZZAP!!! THE "+NO$(NO)+" VAPORIZED.":GOSUB10:SOUND240,1
5925 BL=BL-1:IF BL=0 THEN M$="I'M OUT OF AMMUNITION.":GOSUB10
5950 GOTO 2125
6025 IF VB<>13 THEN 6275
6050 IF NO=0 THEN M$="SAY WHAT?":GOSUB10:GOTO 2125
6075 X=14:GOSUB 21450
6100 IF Y<>-1 OR NO<>19 THEN M$="O.K. "+NO$(NO):GOSUB10:GOTO 2125
6125 IF DR THEN 2725
6150 DR=-1:M$="A VOICE COMES OVER THE P.A. SYSTEM AND SAYS:":GOSUB10
6160 M$="OPENNING FLIGHT DECK DOORS":GOSUB10
6175 IF LC>2 AND LC<6 THEN M$="YIPS!!! THERE'S NO AIR!!! CROAK...":GOSUB10:END
6200 GOTO 2125
6275 IF VB<>14 THEN 6750
6300 IF NO<>20 AND NO<>16 AND NO<>11 AND NO<>33 THEN 2650
6325 IF NO=20 THEN 6550
6350 IF NO<>16 THEN 6450
6375 IF OB(6,1)=-1 THEN M$="SORRY. I'M NOT A CARTOGRAPHER.":GOSUB10:GOTO 2125
6400 IF OB(6,1)=LC THEN M$="TRY GET MAP.":GOSUB10:GOTO 2125
6425 M$="IT'S NOT HERE.":GOSUB10:GOTO 2125
6450 X=NO:GOSUB 21450:IF Y<>LC AND Y<>-1 THEN 6425
6475 IF NO=11 THEN M$="IT SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):GOSUB10
6500 IF NO=33 THEN M$="IT SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):GOSUB10
6525 GOTO 2125
6550 IF LC<>13 THEN M$="I DON'T SEE ANY.":GOSUB10:GOTO 2125
6575 M$="IT SAYS ON THE WALL,":GOSUB10
6600 M$=CHR$(34)+"YOUR MOTHER'S GOT A BIG NOSE"+CHR$(34):GOSUB10
6625 M$=CHR$(34)+"KILROY MADE IT HERE, TOO"+CHR$(34):GOSUB10
6650 M$=CHR$(34)+"SAY SECURITY"+CHR$(34):GOSUB10
6675 GOTO 2125
6750 IF VB<>15 THEN 6975
6775 IF NO=0 THEN M$="WHAT'S A "+NO$(0)+"?":GOSUB10:GOTO 2125
6800 IF NO<>22 THEN M$="DON'T BE REDICULOUS.":GOSUB10:GOTO 2125
6825 X=22:GOSUB 21450:IF Y<>-1 THEN M$="I'M NOT HOLDING IT.":GOSUB10:GOTO 2125
6850 FOR I=1 TO LO:IF OB(I,0)=22 THEN 6875
6851 NEXT I:M$="I DON'T KNOW WHERE IT IS.":GOSUB10:GOTO 2125
6875 OB(I,1)=0:M$="CHUMP - CHUMP. HUMMM, GOOD.":GOSUB10
6900 SOUND50,1:SOUND25,1:GOTO 2125
6975 IF VB<>16 OR NO<>23 OR LC<>16 THEN 7125
7000 X=23:GOSUB 21450:IF Y<>-1 THEN M$=M1$:GOSUB10:GOTO 2125
7025 OB(11,1)=0:OB(14,1)=16:CR=CR-1:M$=M2$:GOSUB10
7050 GOTO 2125
7125 IF VB<>18 OR NO<>36 THEN 7275
7150 IF NO<>36 OR LC<>31 THEN 2650
7175 X=17:GOSUB 21450:IF Y<>-1 THEN M$=M3$:GOSUB10:GOTO 2125
7200 HE$(31)="":DJ=-1:M$=M4$:GOSUB10:GOTO 2125
7275 IF VB<>19 OR NO=0 THEN 7600
7325 IF LC<>35 AND NO=34 THENM$=M5$:GOSUB10:GOTO 2125
7350 X=22:GOSUB 21450:IF Y<>-1 THENM$=M6$:GOSUB10:GOTO 2125
7375 X=35:GOSUB 21450:IF Y=1 OR Y=LC AND NO=35 THEN M$="I DON'T SEE HER.":GOSUB10:GOTO 2125
7376 IF NO=35 THEN9000
7400 IF NO<>34 THEN M$=NO$(0)+M8$:GOSUB10:GOTO 2125
7425 IF TC>MD THEN M$=M9$:GOSUB10:GOTO 2125
7450 FOR I=1 TO LO:IF OB(I,0)=34 THEN 7475
7451 NEXT I:GOTO 2650
7475 K=I:FOR I=1 TO LO:IF OB(I,0)=22 THEN 7500
7476 NEXT I:GOTO 2650
7500 M$=N0$:GOSUB10:HE$(35)=""
7525 OB(K,1)=0:OB(I,1)=0:SOUND50,1:SOUND25,1:GOTO 2125
7600 IF VB<>17 THEN 7700
7625 M$=NO$(0):GOSUB10:GOTO 2125
7700 IF VB<>20 THEN 7975
7725 IF NO=0 THEN 2650
7750 X=NO:GOSUB 21450:IF Y=-1 THEN M$="I'M CARRYING IT. THAT'S IMPOSSIBLE.":GOSUB10:GOTO 2125
7775 IF Y=LC THEN 7850
7800 IF NO<11 OR NO=19 OR NO=20 OR NO=30 THEN 2650
7825 M$="I CAN'T HIT SOMETHING I CAN'T SEE.":GOSUB10:GOTO 2125
7850 IF NO=15 OR NO=25 OR NO=34 THEN M$="I'D RATHER NOT. HE MIGHT HIT ME BACK!":GOSUB10:GOTO 2125
7875 IF NO=35 THEN M$="THAT'S NOT NICE!":GOSUB10:GOTO 2125
7900 GOTO 2725
7975 IF VB<>21 THEN 8000
7976 M$="I'M NOT STRONG ENOUGH TO KILL ANYTHING.":GOSUB10:GOTO 2125
8000 GOTO2650
9000 M$=M7$:GOSUB10:K=I:FOR I=1 TO LO
9010 IF OB(I,0)=22 THEN 7525
9020 NEXT I:GOTO 2650
10300 CLS:M$=DS$(LC):GOSUB10:A$=""
10305 IF LC=35 THEN GF=10
10400 IF LC=7 THEN 10700
10450 K=0:FOR I=1 TO LO:IF OB(I,1)<>LC THEN 10600
10500 IF K=0 THEN K=1:M$="AROUND ME I SEE:":GOSUB10:A$=OB$(I):GOTO 10600
10550 IF LEN(A$)+5+LEN(OB$(I))>SL THEN M$=A$:GOSUB10:A$=OB$(I):GOTO10600
10551 A$=A$+", "+OB$(I)
10600 NEXT I
10650 IF A$<>"" THEN M$=A$:GOSUB10
10700 M$="OBVIOUS DIRECTIONS ARE ":K=0
10750 FOR I=0 TO 5:IF DS(LC,I)=0 THEN 10900
10800 IF K<>0 THEN M$=M$+", "
10850 M$=M$+NO$(I+1):K=1
10900 NEXT I
10950 IF K=0 THEN M$=M$+"UNKNOWN"
11000 M$=M$+".":GOSUB10:GOTO 2125
11150 CLS:GOSUB 21050
11200 IF J=0 THEN M$="WE HAVE FAILED OUR MISSION. THE FORCES OF PRINCESS LEYA WILL BE CONQUERED.":GOSUB10
11250 IF J=SC THENGOSUB20010
11300 IFJ>0ANDJ<SCTHENM$="WE HAVE HELPED THE FORCES OF PRINCESS LEYA DEFEND THE GALAXY. LONG LIVE FREEDOM!":GOSUB10 
11500 PRINT
11550 INPUT "PLAY AGAIN (Y OR N)";A$:A$=LEFT$(A$,1)
11600 IF A$="Y" OR A$="Y" THEN RUN
11601 END
11800 CLS:M$="A VOICE BOOMS OUT, "+CHR$(34)+"WHO GOES THERE"+CHR$(34):GOSUB10
11850 GOSUB 20350
11900 IF VB<>13 OR NO<>30 THEN 5575
11950 M$=N1$:GOSUB10
12000 GOSUB 20350
12050 X=31:GOSUB 21450:IF Y<>-1 THEN 5575
12100 IF VB<>17 OR NO<>31 THEN 5575
12101 GOTO10300
20000 M$="I CAN'T GO THAT WAY. FLIGHT DECK DOORS ARE OPEN. NO AIR!!!":GOSUB10:GOTO 2125:RETURN
20010 M$="WE ARE HEROS. THE FORCES OF PRINCESS LEYA WILL CONQUER THE EVIL ROCHE SOLDIERS, "
20020 M$=M$+"AND FREEDOM WILL PREVAIL THROUGHOUT THE GALAXY.":GOSUB10:RETURN
20350 PRINT"WHAT SHOULD I DO?":INPUTI$:PRINT:CM$="":IFI$=""THEN20350
20360 FOR IL=1 TO LEN(I$):IN=ASC(MID$(I$,IL,1)):IF IN>96 AND IN<123 THEN IN=IN AND 223
20365 CM$=CM$+CHR$(IN):NEXT IL
20370 IF CM$="N" THEN CM$="NORTH"
20371 IF CM$="S" THEN CM$="SOUTH"
20372 IF CM$="E" THEN CM$="EAST" 
20375 IF CM$="W" THEN CM$="WEST"
20376 IF CM$="U" THEN CM$="UP"
20377 IF CM$="D" THEN CM$="DOWN" 
20380 IF CM$="L" THEN CM$="LOOK"
20381 IF CM$="I" THEN CM$="INVENTORY"
20382 IF CM$="H" THEN CM$="HELP"
20395 VB$(0)="":NO$(0)="":VB=0:NO=0:IF LEN(CM$)=0 THEN 20350
20400 FOR ZL=1 TO LEN(CM$):IF MID$(CM$,ZL,1)<>" " THEN VB$(0)=VB$(0)+MID$(CM$,ZL,1):NEXT ZL
20500 FOR ZL=1 TO LV:IF VB$(ZL)<>"" AND LEFT$(VB$(0),LEN(VB$(ZL)))=VB$(ZL) THEN VB=ZL:GOTO 20600
20501 NEXT ZL
20550 VB=0:NO$(0)=VB$(0):GOTO 20650
20600 IF LEN(VB$(0))+1>=LEN(CM$) THEN NO=0:RETURN
20601 NO$(0)=RIGHT$(CM$,LEN(CM$)-1-LEN(VB$(0)))
20650 FOR ZL=1 TO LN:IF NO$(ZL)<>"" AND LEFT$(NO$(0),LEN(NO$(ZL)))=NO$(ZL) THEN NO=ZL:GOTO 20800
20651 NEXT ZL
20700 NO=0:RETURN
20800 FOR ZL=1 TO 1:NEXT ZL:RETURN
21050 J=0:FOR I=1 TO LO:IF OB(I,1)=1 THEN J=J+OB(I,2)
21100 NEXT I:M$="OUT OF A MAXIMUM OF"+STR$(SC)+" POINTS, YOU HAVE"+STR$(J)+" POINTS.":GOSUB10
21150 IF J=0 THEN M$="WE'RE NOT DOING TOO GOOD.":GOSUB10
21200 RETURN
21450 FOR ZL=0 TO LO:IF OB(ZL,0)=X THEN Y=OB(ZL,1):GOTO 21500
21451 NEXT ZL:Y=-99:RETURN
21500 FOR ZL=1 TO 1:NEXT ZL:RETURN
22000 A$=INKEY$
22010 IF INKEY$="" THEN 22010
22011 RETURN
30300 SC=215
30600 LV=21:DIM VB$(LV)
30650 VB$(1)="GO":VB$(2)="GET":VB$(3)="LOOK"
30700 VB$(4)="INVEN":VB$(5)="SCORE":VB$(6)="DROP"
30750 VB$(7)="HELP":VB$(8)="SAVE":VB$(9)="LOAD":VB$(10)="QUIT"
30800 VB$(11)="PRESS":VB$(12)="SHOOT":VB$(13)="SAY"
30850 VB$(14)="READ":VB$(15)="EAT":VB$(16)="CSAVE"
30900 VB$(17)="SHOW":VB$(18)="OPEN":VB$(19)="FEED"
30950 VB$(20)="HIT":VB$(21)="KILL"
31100 LN=37:DIM NO$(LN)
31150 NO$(1)="NORTH":NO$(2)="EAST":NO$(3)="SOUTH"
31200 NO$(4)="WEST":NO$(5)="UP":NO$(6)="DOWN"
31250 NO$(10)="BUTTON":NO$(11)="TAG":NO$(12)="FUEL"
31300 NO$(13)="BLASTER":NO$(14)="COMMUNICATOR":NO$(15)="GUARD"
31350 NO$(16)="MAP":NO$(17)="KEYS":NO$(18)="NECKLACE"
31400 NO$(19)="SESAME":NO$(20)="GRAFFITI"
31450 NO$(21)="CAPE":NO$(22)="HAMBURGER"
31500 NO$(23)="TAPE":NO$(24)="TURBO"
31550 NO$(25)="SCIENTIST":NO$(26)="PLANS"
31600 NO$(27)="SCHEMATIC":NO$(28)="DEVICE":NO$(29)="GUN"
31650 NO$(30)="SECURITY":NO$(31)="I.D.":NO$(32)="CRYSTALS"
31700 NO$(33)="SIGN":NO$(34)="ROBOT":NO$(35)="PRINCESS"
31750 NO$(36)="DOOR":NO$(37)="AMMUNITION"
31900 CL=37:DIM DS$(CL)
31950 DS$(1)="I'M IN THE PASSENGER & STORAGE COMPARTMENT OF MY SPACE SHIP. THERE'S AN EXIT HERE TO LEAVE THE SHIP."
32000 DATA 2,0,0,0,0,3
32050 DS$(2)="I'M IN THE COCKPIT OF MY SPACE SHIP. A LARGE RED BUTTON SAYS "+CHR$(34)+"PRESS TO BLAST OFF"+CHR$(34)
32100 DATA 0,0,1,0,0,0
32150 DS$(3)="I'M STANDING NEXT TO MY SPACE SHIP WHICH IS LOCATED ON A HUGE FLIGHT DECK."
32200 DATA 18,0,4,0,1,0
32250 DS$(4)="I'M OUT ON THE FLIGHT DECK OF GENERAL DOOM'S BATTLE CRUISER."
32300 DATA 3,5,4,4,0,0
32350 DS$(5)=DS$(4):DATA 4,6,5,4,0,0
32400 DS$(6)="I'M IN A HALLWAY. THERE ARE DOORS ON ALL SIDES. THE DOOR TO THE NORTH SAYS: "
32401 DS$(6)=DS$(6)+CHR$(34)+"CLOSED FOR THE DAY"+CHR$(34)
32450 DATA 7,0,8,5,0,0
32500 DS$(7)="I'M IN THE SUPPLY DEPOT. AROUND ME I SEE: ALL KINDS OF THINGS"
32550 DATA 0,0,6,0,0,0
32600 DS$(8)="I'M AT THE END OF ONE OF THE HALLWAYS. I CAN HERE VOICES NEARBY. SOUNDS LIKE GUARDS."
32650 DATA 6,10,0,9,0,12
32700 DS$(9)="I'M IN THE STRATEGY PLANNING ROOM.":DATA 11,8,0,0,0,0
32750 DS$(10)="I'M IN THE DECONTAMINATION AREA.":DATA 0,14,0,8,0,0
32800 DS$(11)="THIS AREA IS THE TRACTOR BEAM CONTROL ROOM. A LARGE SIGN WARNS: "+CHR$(34)+"DO NOT PRESS ANY BUTTONS"+CHR$(34)
32850 DATA 0,0,9,0,0,0
32900 DS$(12)="I'M IN ANOTHER HALLWAY. TO THE EAST IS A RESTROOM."
32950 DATA 15,13,0,0,8,0
33000 DS$(13)=DS$(13)+"PIPES LEAD UP THROUGH THE CEILING."
33050 DATA 15,0,0,12,27,0
33100 DS$(14)="THIS APPEARS TO BE AN INTERROGATION ROOM.":DATA 0,0,0,10,0,0
33150 DS$(15)="I'M IN A LOUNGE.":DATA 0,0,13,12,0,0
33200 DS$(16)="THIS IS A COMPUTER ROOM. THERE'S A TRS-80 IN HERE. ON THE SCREEN IT SAYS: "+CHR$(34)+"CSAVE TAPE"+CHR$(34)
33201 DATA 17,0,18,0,0,0
33250 DS$(17)="I'M IN A TESTING LABORTORY.":DATA 0,0,16,0,0,0
33300 DS$(18)="I'M IN A HALLWAY. A LARGE ARROW POINTS EAST AND SAYS: "+CHR$(34)+"TO THE VAULT"+CHR$(34)
33350 DATA 16,25,3,19,0,0
33400 DS$(19)="THIS IS THE ENTRANCE TO THE DEVELOPMENT LAB SECTION":DATA 20,18,21,20,22,0
33450 DS$(20)="I'M IN A LONG CORRIDOR. THERE ARE LABORTORIES ALL AROUND ME.":DATA 19,23,21,20,22,24
33500 DS$(21)="I'M IN A RESEARCH LAB.":DATA 20,0,0,0,0,0
33550 DS$(22)="I'M LOST!":DATA 22,22,22,22,22,20
33600 DS$(23)=DS$(21):DATA 0,0,0,20,0,0
33650 DS$(24)=DS$(21):DATA 0,0,0,0,20,0
33700 DS$(25)="I'M NEAR THE ENTRANCE TO THE VAULT. A SIGN HERE SAYS: "+CHR$(34)+"AUTHORIZED SECURITY PERSONEL ONLY"+CHR$(34)
33750 DATA 0,26,0,18,0,0
33800 DS$(26)="I'M IN THE VAULT.":DATA 0,0,0,25,0,0
33850 DS$(27)="I'M IN A PIPE TUNNEL WHICH LEADS IN EVERY DIRECTION.":DATA 28,27,27,27,27,13
33900 DS$(28)=DS$(27):DATA 29,29,29,29,30,29
33950 DS$(29)="I'M LOST IN A MAZE OF PIPES.":DATA 28,29,29,29,29,27
34000 DS$(30)="I'M IN THE PIPE MAZE. BELOW ME I THINK I CAN SEE THE JAIL."
34050 DATA 29,29,28,29,29,31
34100 DS$(31)="I'M IN THE JAIL."
34150 DATA 32,33,34,35,0,0
34200 DS$(32)="I'M IN A JAIL CELL.":DATA 0,0,31,0,0,0
34250 DS$(33)=DS$(32):DATA 0,0,0,31,0,0
34300 DS$(34)=DS$(32):DATA 31,0,0,0,0,0
34350 DS$(35)="I'M AT THE SECURITY DESK. TO THE NORTH AN ELEVATOR.":DATA 36,31,0,0,0,0
34400 DS$(36)="I'M IN THE ELEVATOR.":DATA 0,0,35,0,37,0
34450 DS$(37)=DS$(36):DATA 0,0,14,0,0,36
34850 LO=23:DIM OB$(LO)
34900 OB$(1)="A TAG WHICH SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):DATA 11,5,0
34950 OB$(2)="ANTI-MATTER FUEL":DATA 12,5,5
35000 OB$(3)="BLASTER":DATA 13,7,0
35050 OB$(4)="COMMUNICATOR":DATA 14,9,0
35100 OB$(5)="A VERY SURPRISED GUARD":DATA 15,9,0
35150 OB$(6)="MAP OF THE SHIP":DATA 16,29,20
35200 OB$(7)="SOME KEYS":DATA 17,9,0
35250 OB$(8)="A SHINESTONE NECKLACE":DATA 18,10,20
35300 OB$(9)="PRINCESS LEYA'S CAPE":DATA 21,14,5
35350 OB$(10)="MCDONALD'S HAMBURGER":DATA 22,15,0
35400 OB$(11)="A CASSETTE TAPE":DATA 23,7,0
35450 OB$(12)="A TURBOENCABULATOR":DATA 24,17,5
35500 OB$(13)="AN EVIL LOOKING SCIENTIST":DATA 25,17,0
35550 OB$(14)="SECRET ATTACK PLANS":DATA 26,0,20
35600 OB$(15)="DEATH RAY SCHEMATIC":DATA 27,9,20
35650 OB$(16)="CLOAKING DEVICE":DATA 28,17,20
35700 OB$(17)="MICRO LASER GUN":DATA 29,24,20
35750 OB$(18)="I.D. CARD":DATA 31,17,0
35800 OB$(19)="MALIDIUM CRYSTALS (THE TREASURY!)":DATA 32,26,30
35850 OB$(20)="A SIGN WHICH SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):DATA 33,3,0
35900 OB$(21)="ATTACK ROBOT":DATA 34,35,0
35950 OB$(22)="PRINCESS LEYA":DATA 35,34,50
36000 OB$(23)="AMMUNITION":DATA 37,7,0
36150 DIM DS(CL,5):RESTORE
36200 FOR ZL=1 TO CL:FOR ZD=0 TO 5
36250 READ DS(ZL,ZD):NEXT ZD:NEXT ZL
36300 DIM OB(LO,3)
36350 FOR ZL=1 TO LO:READ OB(ZL,0),OB(ZL,1),OB(ZL,2):NEXT ZL
36500 DIM HE$(CL)
36550 HE$(1)="I THINK WE'RE SUPPOSED TO LEAVE THE STUFF HERE."
36600 HE$(2)="I WONDER IF WE HAVE ENOUGH FUEL?"
36650 HE$(7)="HOW 'BOUT A BLASTER."
36700 HE$(9)="TRY SHOOT GUARD."
36750 HE$(13)="IT MIGHT BE INTERSTING TO READ THE GRAFFITI."
36800 HE$(17)="TRY SHOOT SCIENTIST."
36850 HE$(22)="I'M AS CONFUSED AS YOU ARE.":HE$(29)=HE$(22)
36900 HE$(31)="IT MIGHT HELP IF WE HAD SOME KEYS TO OPEN ANY LOCKED DOORS."
36950 HE$(35)="DID YOU BRING ANYTHING TO EAT?"
37100 M0$="I CAN'T GO THERE. THE DOOR IS LOCKED."
37150 M1$="I'M NOT CARRYING ANY BLANK TAPE."
37200 M2$="THE TRS-80 RECORDED SOMETHING ON THE TAPE, AND THEN IT PRINTED: "+CHR$(34)+"ATTACK PLANS -- VERY SECRET"+CHR$(34)
37250 M3$="I CAN'T. I'M NOT CARRYING ANY KEYS."
37300 M4$="O.K. THE DOOR TO THE JAIL CELL IS UNLOCKED."
37350 M5$="THERE'S NO ROBOT HERE."
37400 M6$="BUT I DON'T HAVE ANY HAMBURGERS."
37450 M7$="CHUMP...CHUMP BURP! THE PRINCESS THANKS YOU FOR A DELICIOUS MEAL."
37500 M8$=" DOESN'T EAT HAMBURGER."
37550 M9$="NOTHING HAPPENED. THE HAMBURGER IS COLD YOU KNOW."
37600 N0$="THE ATTACK ROBOT EATS THE HAMBURGER AND DISAPPEARS."
37650 N1$="I'M AT THE IDENTIFICATION TERMINAL. ON THE SCREEN IT SAYS: "+CHR$(34)+"SHOW I.D."+CHR$(34)
37700 N2$="THE TRACTOR BEAM IS OFF."
37750 N3$="THE TRACTOR BEAM IS ON."
37800 N4$="YOU FORGOT TO OPEN THE FLIGHT DECK DOORS."
37850 RETURN
