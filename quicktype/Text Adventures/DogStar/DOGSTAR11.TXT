0 CLEAR1200:DIMCC,C1,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO6
2 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
4 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
6 CLS:GOSUB800
20 GOSUB516
22 LC=2:SL=64:BL=4:GF=50:RV=16396
24 GOTO384
26 IFTC<25ORRND(GF)<>1THEN46
28 IFTC=300THENGF=20
30 IFLC<3ORLC=9ORLC=26ORLC=36ORLC=37THEN46
32 IFLC>26ANDLC<31THEN46
34 M$="HOLY SMOKES. AN ARMED GUARD JUST WALKED IN.":GOSUB2
36 GOSUB454:IFVB<>12ORNO<>15THEN210
38 X=13:GOSUB504:IFY<>-1THEN210
40 IFBL=0THENCLS:M$="I'M OUT OF AMMUNITION.":GOSUB2:GOTO212
42 POKE49151,64:M$="ZZZAP! NO MORE GUARD.":GOSUB2:SOUND240,2
44 BL=BL-1:IFBL=0THENM$="I'M OUT OF AMMUNITION.":GOSUB2
46 IFMD<>TCTHEN52
48 X=22:GOSUB504:IFY<>-1THEN52
50 M$="YOUR MCDONALD'S HAMBURGER IS COLD.":GOSUB2
52 GOSUB454:TC=TC+1
54 IFVB=0ANDNO=0THEN58
56 GOTO62
58 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB2:GOTO26
60 M$="NOTHING HAPPENED.":GOSUB2:GOTO26
62 IFVB>1ORNO>7THEN88
64 IFNO=0THEN58
66 IFDS(LC,NO-1)=0THENM$="I CAN'T GO THAT WAY!":GOSUB2:GOTO26
68 IFDRANDDS(LC,NO-1)>2ANDDS(LC,NO-1)<6THENGOSUB448
70 IFNO=3ANDLC=31ANDNOTDJTHENM$=M0$:GOSUB2:GOTO26
72 IFLC=35ANDDS(LC,NO-1)=36ANDO(21,1)<>0THENM$="THE ROBOT WON'T LET ME THROUGH.":GOSUB2:GOTO26
74 IFLC=17ANDO(13,1)=17THEN210
76 IFLC=9ANDO(5,1)=9THEN210
78 IFLC=9ORLC=17THENHE$(LC)=""
80 LC=DS(LC,NO-1)
82 IFLC=34THENHE$(LC)=""
84 IFLC=26THEN432
86 GOTO384
88 IFVB=3THEN384
90 IFVB<>2THEN124
92 IFNO=0THENM$="I DON'T KNOW WHAT A "+CHR$(34)+N$(0)+CHR$(34)+" IS.":GOSUB2:GOTO26
94 IFCR>5THENM$="I CAN'T CARRY ANY MORE.":GOSUB2:M$="HINT: DROP SOMETHING.":GOSUB2:GOTO26
96 FORI=1TOLO:IF(O(I,0)=NO)THEN100
98 NEXTI:GOTO58
100 IFO(I,1)=-1THENM$="I'M ALREADY CARRYING IT.":GOSUB2:GOTO26
102 IFO(I,1)<>LCTHENM$="I DON'T SEE IT.":GOSUB2:GOTO26
104 IFNO<>37THEN110
106 X=13:GOSUB504:IFY<>-1THENM$="I DON'T HAVE A BLASTER TO PUT IT IN.":GOSUB2:GOTO26
108 BL=4:O(I,1)=0:M$="MY BLASTER'S RELOADED.":GOSUB2:SOUND100,2:SOUND1,2:GOTO26
110 IFNO=15ORNO=25ORNO=34THENM$="HE LOOKS PRETTY MEAN TO ME.":GOSUB2:GOTO26
112 CR=CR+1:O(I,1)=-1:PRINT"O.K.":FORI=1TO1:NEXTI
114 IFNO=14ANDNOTCMTHENM$="A VOICE SAYS: "+CHR$(34)+"SESAME"+CHR$(34)+".":GOSUB2:CM=-1
116 IFNO=22ANDMD=0THENMD=TC+50
118 IFNO=12THENHE$(2)=""
120 IFNO=13THENHE$(7)=""
122 GOTO26
124 IFVB<>4THEN134
126 PRINT"I'M CARRYING:"
128 K=0:FORI=1TOLO:IFO(I,1)=-1THENM$=O$(I):GOSUB2:K=1
130 NEXTI:IFK=0THENM$="NOTHING":GOSUB2
132 PRINT:GOTO26
134 IFVB<>5THEN138
136 GOSUB496:GOTO26
138 IFVB<>6THEN156
140 IF(NO=0)THEN92
142 IFLC=2THENM$="THERE'S NO ROOM HERE.":GOSUB2:GOTO26
144 IC=0:FORI=1TOLO:IFO(I,1)=LCTHENIC=IC+1
146 NEXTI:IFIC>12THENM$="THERE NOT ENOUGH ROOM. GET RID OF SOMETHING.":GOSUB2:GOTO26
148 FORI=0TOLO:IF(O(I,0)=NO)THEN152
150 NEXTI:GOTO58
152 IFO(I,1)<>-1THENM$="I'M NOT CARRYING IT.":GOSUB2:GOTO26
154 CR=CR-1:O(I,1)=LC:PRINT"O.K.":GOTO26
156 IFVB<>7THEN162
158 IFHE$(LC)=""THENM$="HOW AM I SUPPOSED TO KNOW WHAT TO DO?":GOSUB2:GOTO26
160 M$=HE$(LC):GOSUB2:GOTO26
162 IFVB<>8THEN174
164 M$="PRESS enter WHEN READY TO RECORD":GOSUB2
166 GOSUB510
168 O(1,3)=TB:O(2,3)=TC:O(3,3)=CM:O(4,3)=DR:O(5,3)=BL:O(6,3)=MD:O(7,3)=GF:O(8,3)=DJ:O(9,3)=CR:O(10,3)=LC
170 CSAVE*O,"DSTARDAT"
172 PRINT"O.K.":GOTO26
174 IFVB<>9THEN186
176 M$="PRESS enter WHEN READY TO READ TAPE":GOSUB2
178 GOSUB510:PRINT"PLAY TAPE NOW"
180 CLOAD*O,"DSTARDAT"
182 TB=O(1,3):TC=O(2,3):CM=O(3,3):DR=O(4,3):BL=O(5,3):MD=O(6,3):GF=O(7,3):DJ=O(8,3):CR=O(9,3):LC=O(10,3)
184 GOTO384
186 IFVB<>10THEN190
188 CLS:J=0:GOTO418
190 IFVB<>11THEN218
192 IFNO<>10THEN58
194 IFLC<>2ANDLC<>11THENPRINT"WHAT BUTTON,":GOTO26
196 IFLC=11ANDNOTTBTHENTB=-1:M$=N2$:GOSUB2:GOTO26
198 IFLC=11ANDTBTHENTB=0:M$=N3$:GOSUB2:GOTO26
200 X=12:GOSUB504:IFY<>1THEN60
202 X=24:GOSUB504:IFY<>1THEN60
204 IFNOTTBTHENM$=N3$:GOSUB2:GOTO60
206 IFNOTDRTHENM$=N4$:GOSUB2:GOTO60
208 GOTO416
210 CLS:PRINT"H E L P ! ! !"
212 M$="ROCHE SOLDIERS ARE EVERYWHERE. I'VE BEEN CAPTURED.":GOSUB2
214 M$="I'M NOW A PRISONER. WOE IS ME...":GOSUB2
216 GOTO424
218 IFVB<>12ORNO=0THEN240
220 IFBL=0THENM$="BUT I DON'T HAVE ANY AMMUNITION LEFT.":GOSUB2:GOTO26
222 X=13:GOSUB504:IFY<>-1THENM$="BUT I'M NOT CARRYING A BLASTER.":GOSUB2:GOTO26
224 X=NO:GOSUB504:IFY=-1THENM$="I CAN'T. I'M HOLDING IT.":GOSUB2:GOTO26
226 IFNO=34THENPOKE49151,64:M$="ZZZAP!":GOSUB2:SOUND240,1:BL=BL-1:GOTO26
228 IFY<>LCTHENM$="I DON'T SEE IT.":GOSUB2:GOTO26
230 FORI=1TOLO:IF(O(I,0)=NO)THEN234
232 NEXTI:GOTO58
234 O(I,1)=0:POKE49151,64:M$="ZZZAP!!! THE "+N$(NO)+" VAPORIZED.":GOSUB2:SOUND240,1
236 BL=BL-1:IFBL=0THENM$="I'M OUT OF AMMUNITION.":GOSUB2
238 GOTO26
240 IFVB<>13THEN258
242 IFNO=0THENM$="SAY WHAT?":GOSUB2:GOTO26
244 X=14:GOSUB504
246 IFY<>-1ORNO<>19THENM$="O.K. "+N$(NO):GOSUB2:GOTO26
248 IFDRTHEN60
250 DR=-1:M$="A VOICE COMES OVER THE P.A. SYSTEM AND SAYS:":GOSUB2
252 M$="OPENNING FLIGHT DECK DOORS":GOSUB2
254 IFLC>2ANDLC<6THENM$="YIPS!!! THERE'S NO AIR!!! CROAK...":GOSUB2:END
256 GOTO26
258 IFVB<>14THEN292
260 IFNO<>20ANDNO<>16ANDNO<>11ANDNO<>33THEN58
262 IFNO=20THEN280
264 IFNO<>16THEN272
266 IFO(6,1)=-1THENM$="SORRY. I'M NOT A CARTOGRAPHER.":GOSUB2:GOTO26
268 IFO(6,1)=LCTHENM$="TRY GET MAP.":GOSUB2:GOTO26
270 M$="IT'S NOT HERE.":GOSUB2:GOTO26
272 X=NO:GOSUB504:IFY<>LC ANDY<>-1THEN270
274 IFNO=11THENM$="IT SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):GOSUB2
276 IFNO=33THENM$="IT SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):GOSUB2
278 GOTO26
280 IFLC<>13THENM$="I DON'T SEE ANY.":GOSUB2:GOTO26
282 M$="IT SAYS ON THE WALL,":GOSUB2
284 M$=CHR$(34)+"YOUR MOTHER'S GOT A BIG NOSE"+CHR$(34):GOSUB2
286 M$=CHR$(34)+"KILROY MADE IT HERE, TOO"+CHR$(34):GOSUB2
288 M$=CHR$(34)+"SAY SECURITY"+CHR$(34):GOSUB2
290 GOTO26
292 IFVB<>15THEN308
294 IFNO=0THENM$="WHAT'S A "+N$(0)+"?":GOSUB2:GOTO26
296 IFNO<>22THENM$="DON'T BE RIDICULOUS.":GOSUB2:GOTO26
298 X=22:GOSUB504:IFY<>-1THENM$="I'M NOT HOLDING IT.":GOSUB2:GOTO26
300 FORI=1TOLO:IFO(I,0)=22THEN304
302 NEXTI:M$="I DON'T KNOW WHERE IT IS.":GOSUB2:GOTO26
304 O(I,1)=0:M$="CHUMP - CHUMP. HUMMM, GOOD.":GOSUB2
306 SOUND50,1:SOUND25,1:GOTO26
308 IFVB<>16ORNO<>23ORLC<>16THEN316
310 X=23:GOSUB504:IFY<>-1THENM$=M1$:GOSUB2:GOTO26
312 O(11,1)=0:O(14,1)=16:CR=CR-1:M$=M2$:GOSUB2
314 GOTO26
316 IFVB<>18ORNO<>36THEN324
318 IFNO<>36ORLC<>31THEN58
320 X=17:GOSUB504:IFY<>-1THENM$=M3$:GOSUB2:GOTO26
322 HE$(31)="":DJ=-1:M$=M4$:GOSUB2:GOTO26
324 IFVB<>19ORNO=0THEN350
326 IFLC<>35ANDNO=34THENM$=M5$:GOSUB2:GOTO26
328 X=22:GOSUB504:IFY<>-1THENM$=M6$:GOSUB2:GOTO26
330 X=35:GOSUB504:IFY=1ORY=LCANDNO=35THENM$="I DON'T SEE HER.":GOSUB2:GOTO26
332 IFNO=35THEN378
334 IFNO<>34THENM$=N$(0)+M8$:GOSUB2:GOTO26
336 IFTC>MDTHENM$=M9$:GOSUB2:GOTO26
338 FORI=1TOLO:IFO(I,0)=34THEN342
340 NEXTI:GOTO58
342 K=I:FORI=1TOLO:IFO(I,0)=22THEN346
344 NEXTI:GOTO58
346 M$=N0$:GOSUB2:HE$(35)=""
348 O(K,1)=0:O(I,1)=0:SOUND50,1:SOUND25,1:GOTO26
350 IFVB<>17THEN354
352 M$=N$(0):GOSUB2:GOTO26
354 IFVB<>20THEN372
356 IFNO=0THEN58
358 X=NO:GOSUB504:IFY=-1THENM$="I'M CARRYING IT. THAT'S IMPOSSIBLE.":GOSUB2:GOTO26
360 IFY=LCTHEN366
362 IFNO<11ORNO=19ORNO=20ORNO=30THEN58
364 M$="I CAN'T HIT SOMETHING I CAN'T SEE.":GOSUB2:GOTO26
366 IFNO=15ORNO=25ORNO=34THENM$="I'D RATHER NOT. HE MIGHT HIT ME BACK!":GOSUB2:GOTO26
368 IFNO=35THENM$="THAT'S NOT NICE!":GOSUB2:GOTO26
370 GOTO60
372 IFVB<>21THEN376
374 M$="I'M NOT STRONG ENOUGH TO KILL ANYTHING.":GOSUB2:GOTO26
376 GOTO58
378 M$=M7$:GOSUB2:K=I:FORI=1TOLO
380 IFO(I,0)=22THEN348
382 NEXTI:GOTO58
384 CLS:M$=D$(LC):GOSUB2:A$=""
386 IFLC=35THENGF=10
388 IFLC=7THEN402
390 K=0:FORI=1TOLO:IFO(I,1)<>LCTHEN398
392 IFK=0THENK=1:M$="AROUND ME I SEE:":GOSUB2:A$=O$(I):GOTO398
394 IFLEN(A$)+5+LEN(O$(I))>SLTHENM$=A$:GOSUB2:A$=O$(I):GOTO398
396 A$=A$+", "+O$(I)
398 NEXTI
400 IFA$<>""THENM$=A$:GOSUB2
402 M$="OBVIOUS DIRECTIONS ARE ":K=0
404 FORI=0TO5:IFDS(LC,I)=0THEN410
406 IFK<>0THENM$=M$+", "
408 M$=M$+N$(I+1):K=1
410 NEXTI
412 IFK=0THENM$=M$+"UNKNOWN"
414 M$=M$+".":GOSUB2:GOTO26
416 CLS:GOSUB496
418 IFJ=0THENM$="WE HAVE FAILED OUR MISSION. THE FORCES OF PRINCESS LEYA WILL BE CONQUERED.":GOSUB2
420 IFJ=SCTHENGOSUB450
422 IFJ>0ANDJ<SCTHENM$="WE HAVE HELPED THE FORCES OF PRINCESS LEYA DEFEND THE GALAXY. LONG LIVE FREEDOM!":GOSUB2 
424 PRINT
426 INPUT"PLAY AGAIN (Y OR N)";A$:A$=LEFT$(A$,1)
428 IFA$="Y"ORA$="Y"THENRUN
430 END
432 CLS:M$="A VOICE BOOMS OUT, "+CHR$(34)+"WHO GOES THERE"+CHR$(34):GOSUB2
434 GOSUB454
436 IFVB<>13ORNO<>30THEN210
438 M$=N1$:GOSUB2
440 GOSUB454
442 X=31:GOSUB504:IFY<>-1THEN210
444 IFVB<>17ORNO<>31THEN210
446 GOTO384
448 M$="I CAN'T GO THAT WAY. FLIGHT DECK DOORS ARE OPEN. NO AIR!!!":GOSUB2:GOTO26:RETURN
450 M$="WE ARE HEROES. THE FORCES OF PRINCESS LEYA WILL CONQUER THE EVIL ROCHE SOLDIERS, "
452 M$=M$+"AND FREEDOM WILL PREVAIL THROUGHOUT THE GALAXY.":GOSUB2:RETURN
454 PRINT"WHAT SHOULD I DO?":INPUTC$:PRINT:IFC$=""THEN454
456 IFC$="N"THENC$="NORTH"
458 IFC$="S"THENC$="SOUTH"
460 IFC$="E"THENC$="EAST" 
462 IFC$="W"THENC$="WEST"
464 IFC$="U"THENC$="UP"
466 IFC$="D"THENC$="DOWN"
468 IFC$="L"THENC$="LOOK"
470 IFC$="I"THENC$="INVENTORY"
472 IFC$="H"THENC$="HELP"
474 V$(0)="":N$(0)="":VB=0:NO=0:IFLEN(C$)=0THEN454
476 FORZL=1TOLEN(C$):IFMID$(C$,ZL,1)<>" "THENV$(0)=V$(0)+MID$(C$,ZL,1):NEXTZL
478 FORZL=1TOLV:IFV$(ZL)<>""ANDLEFT$(V$(0),LEN(V$(ZL)))=V$(ZL)THENVB=ZL:GOTO484
480 NEXTZL
482 VB=0:N$(0)=V$(0):GOTO488
484 IFLEN(V$(0))+1>=LEN(C$)THENNO=0:RETURN
486 N$(0)=RIGHT$(C$,LEN(C$)-1-LEN(V$(0)))
488 FORZL=1TOLN:IFN$(ZL)<>""ANDLEFT$(N$(0),LEN(N$(ZL)))=N$(ZL)THENNO=ZL:GOTO494
490 NEXTZL
492 NO=0:RETURN
494 FORZL=1TO1:NEXTZL:RETURN
496 J=0:FORI=1TOLO:IFO(I,1)=1THENJ=J+O(I,2)
498 NEXTI:M$="OUT OF A MAXIMUM OF"+STR$(SC)+" POINTS, YOU HAVE"+STR$(J)+" POINTS.":GOSUB2
500 IFJ=0THENM$="WE'RE NOT DOING TOO GOOD.":GOSUB2
502 RETURN
504 FORZL=0TOLO:IFO(ZL,0)=XTHENY=O(ZL,1):GOTO508
506 NEXTZL:Y=-99:RETURN
508 FORZL=1TO1:NEXTZL:RETURN
510 A$=INKEY$
512 IFINKEY$=""THEN512
514 RETURN
516 SC=215
518 LV=21:DIMV$(LV)
520 V$(1)="GO":V$(2)="GET":V$(3)="LOOK"
522 V$(4)="INVEN":V$(5)="SCORE":V$(6)="DROP"
524 V$(7)="HELP":V$(8)="SAVE":V$(9)="LOAD":V$(10)="QUIT"
526 V$(11)="PRESS":V$(12)="SHOOT":V$(13)="SAY"
528 V$(14)="READ":V$(15)="EAT":V$(16)="COPY"
530 V$(17)="SHOW":V$(18)="OPEN":V$(19)="FEED"
532 V$(20)="HIT":V$(21)="KILL"
534 LN=37:DIMN$(LN)
536 N$(1)="NORTH":N$(2)="EAST":N$(3)="SOUTH"
538 N$(4)="WEST":N$(5)="UP":N$(6)="DOWN"
540 N$(10)="BUTTON":N$(11)="TAG":N$(12)="FUEL"
542 N$(13)="BLASTER":N$(14)="COMMUNICATOR":N$(15)="GUARD"
544 N$(16)="MAP":N$(17)="KEYS":N$(18)="NECKLACE"
546 N$(19)="SESAME":N$(20)="GRAFFITI"
548 N$(21)="CAPE":N$(22)="HAMBURGER"
550 N$(23)="TAPE":N$(24)="TURBO"
552 N$(25)="SCIENTIST":N$(26)="PLANS"
554 N$(27)="SCHEMATIC":N$(28)="DEVICE":N$(29)="GUN"
556 N$(30)="SECURITY":N$(31)="I.D.":N$(32)="CRYSTALS"
558 N$(33)="SIGN":N$(34)="ROBOT":N$(35)="PRINCESS"
560 N$(36)="DOOR":N$(37)="AMMUNITION"
562 CL=37:DIMD$(CL)
564 D$(1)="I'M IN THE PASSENGER & STORAGE COMPARTMENT OF MY SPACE SHIP. THERE'S AN EXIT HERE TO LEAVE THE SHIP."
566 DATA2,,,,,3
568 D$(2)="I'M IN THE COCKPIT OF MY SPACE SHIP. A LARGE RED BUTTON SAYS "+CHR$(34)+"PRESS TO BLAST OFF"+CHR$(34)
570 DATA,,1,,,
572 D$(3)="I'M STANDING NEXT TO MY SPACE SHIP WHICH IS LOCATED ON A HUGE FLIGHT DECK."
574 DATA18,,4,,1,
576 D$(4)="I'M OUT ON THE FLIGHT DECK OF GENERAL DOOM'S BATTLE CRUISER."
578 DATA3,5,4,4,,
580 D$(5)=D$(4):DATA4,6,5,4,,
582 D$(6)="I'M IN A HALLWAY. THERE ARE DOORS ON ALL SIDES. THE DOOR TO THE NORTH SAYS: "
584 D$(6)=D$(6)+CHR$(34)+"CLOSED FOR THE DAY"+CHR$(34)
586 DATA7,,8,5,,
588 D$(7)="I'M IN THE SUPPLY DEPOT. AROUND ME I SEE: ALL KINDS OF THINGS"
590 DATA0,,6,,,
592 D$(8)="I'M AT THE END OF ONE OF THE HALLWAYS. I CAN HERE VOICES NEARBY. SOUNDS LIKE GUARDS."
594 DATA6,10,,9,,12
596 D$(9)="I'M IN THE STRATEGY PLANNING ROOM.":DATA11,8,,,,
598 D$(10)="I'M IN THE DECONTAMINATION AREA.":DATA0,14,,8,,
600 D$(11)="THIS AREA IS THE TRACTOR BEAM CONTROL ROOM. A LARGE SIGN WARNS: "+CHR$(34)+"DO NOT PRESS ANY BUTTONS"+CHR$(34)
602 DATA0,,9,,,
604 D$(12)="I'M IN ANOTHER HALLWAY. TO THE EAST IS A RESTROOM."
606 DATA15,13,,,8,
608 D$(13)="THIS IS WHAT ON EARTH IS CALLED THE BATHROOM. GRAFFITI IS EVERYWHERE. PIPES LEAD UP THROUGH THE CEILING."
610 DATA15,,,12,27,
612 D$(14)="THIS APPEARS TO BE AN INTERROGATION ROOM.":DATA0,,,10,,
614 D$(15)="I'M IN A LOUNGE.":DATA0,,13,12,,
616 D$(16)="THIS IS A COMPUTER ROOM. THERE'S A COMPUTER IN HERE. ON THE SCREEN IT SAYS: "+CHR$(34)+"COPY TAPE"+CHR$(34)
618 DATA17,,18,,,
620 D$(17)="I'M IN A TESTING LABORTORY.":DATA 0,,16,,,
622 D$(18)="I'M IN A HALLWAY. A LARGE ARROW POINTS EAST AND SAYS: "+CHR$(34)+"TO THE VAULT"+CHR$(34)
624 DATA16,25,3,19,,
626 D$(19)="THIS IS THE ENTRANCE TO THE DEVELOPMENT LAB SECTION":DATA20,18,21,20,22,0
628 D$(20)="I'M IN A LONG CORRIDOR. THERE ARE LABORTORIES ALL AROUND ME.":DATA19,23,21,20,22,24
630 D$(21)="I'M IN A RESEARCH LAB.":DATA20,,,,,
632 D$(22)="I'M LOST!":DATA22,22,22,22,22,20
634 D$(23)=D$(21):DATA,,,20,,
636 D$(24)=D$(21):DATA,,,,20,
638 D$(25)="I'M NEAR THE ENTRANCE TO THE VAULT. A SIGN HERE SAYS: "+CHR$(34)+"AUTHORIZED SECURITY PERSONEL ONLY"+CHR$(34)
640 DATA0,26,,18,,
642 D$(26)="I'M IN THE VAULT.":DATA0,,,25,,
644 D$(27)="I'M IN A PIPE TUNNEL WHICH LEADS IN EVERY DIRECTION.":DATA 28,27,27,27,27,13
646 D$(28)=D$(27):DATA 29,29,29,29,30,29
648 D$(29)="I'M LOST IN A MAZE OF PIPES.":DATA 28,29,29,29,29,27
650 D$(30)="I'M IN THE PIPE MAZE. BELOW ME I THINK I CAN SEE THE JAIL."
652 DATA 29,29,28,29,29,31
654 D$(31)="I'M IN THE JAIL."
656 DATA32,33,34,35,,
658 D$(32)="I'M IN A JAIL CELL.":DATA0,,31,,,
660 D$(33)=D$(32):DATA0,,,31,,
662 D$(34)=D$(32):DATA31,,,,,
664 D$(35)="I'M AT THE SECURITY DESK. TO THE NORTH IS AN ELEVATOR.":DATA 36,31,,,,
666 D$(36)="I'M IN THE ELEVATOR.":DATA0,,35,,37,
668 D$(37)=D$(36):DATA0,,14,,,36
670 LO=23:DIMO$(LO)
672 O$(1)="A TAG WHICH SAYS: "+CHR$(34)+"NEEDS TURBO"+CHR$(34):DATA 11,5,
674 O$(2)="ANTI-MATTER FUEL":DATA12,5,5
676 O$(3)="BLASTER":DATA13,7,
678 O$(4)="COMMUNICATOR":DATA14,9,
680 O$(5)="A VERY SURPRISED GUARD":DATA15,9,
682 O$(6)="MAP OF THE SHIP":DATA16,29,20
684 O$(7)="SOME KEYS":DATA17,9,
686 O$(8)="A SHINESTONE NECKLACE":DATA18,10,20
688 O$(9)="PRINCESS LEYA'S CAPE":DATA21,14,5
690 O$(10)="MCDONALD'S HAMBURGER":DATA22,15,
692 O$(11)="A CASSETTE TAPE":DATA23,7,
694 O$(12)="A TURBOENCABULATOR":DATA24,17,5
696 O$(13)="AN EVIL LOOKING SCIENTIST":DATA25,17,
698 O$(14)="SECRET ATTACK PLANS":DATA26,,20
700 O$(15)="DEATH RAY SCHEMATIC":DATA27,9,20
702 O$(16)="CLOAKING DEVICE":DATA28,17,20
704 O$(17)="MICRO LASER GUN":DATA29,24,20
706 O$(18)="I.D. CARD":DATA31,17,
708 O$(19)="MALIDIUM CRYSTALS (THE TREASURY!)":DATA32,26,30
710 O$(20)="A SIGN WHICH SAYS: "+CHR$(34)+"OUT OF ORDER"+CHR$(34):DATA33,3,
712 O$(21)="ATTACK ROBOT":DATA34,35,
714 O$(22)="PRINCESS LEYA":DATA35,34,50
716 O$(23)="AMMUNITION":DATA37,7,
718 DIMDS(CL,5):RESTORE
720 FORZL=1TOCL:FORZD=0TO5:READDS(ZL,ZD):NEXTZD:NEXTZL
722 DIMO(LO,3)
724 FORZL=1TOLO:READO(ZL,0),O(ZL,1),O(ZL,2):NEXTZL
726 DIMHE$(CL):HE$(1)="I THINK WE'RE SUPPOSED TO LEAVE THE STUFF HERE."
728 HE$(2)="I WONDER IF WE HAVE ENOUGH FUEL?":HE$(7)="HOW 'BOUT A BLASTER.":HE$(9)="TRY SHOOT GUARD."
730 HE$(13)="IT MIGHT BE INTERSTING TO READ THE GRAFFITI.":HE$(17)="TRY SHOOT SCIENTIST."
732 HE$(22)="I'M AS CONFUSED AS YOU ARE.":HE$(29)=HE$(22)
734 HE$(31)="IT MIGHT HELP IF WE HAD SOME KEYS TO OPEN ANY LOCKED DOORS.":HE$(35)="DID YOU BRING ANYTHING TO EAT?"
736 M0$="I CAN'T GO THERE. THE DOOR IS LOCKED.":M1$="I'M NOT CARRYING ANY BLANK TAPE."
738 M2$="THE COMPUTER RECORDED SOMETHING ON THE TAPE, AND THEN IT PRINTED: "+CHR$(34)+"ATTACK PLANS -- VERY SECRET"+CHR$(34)
740 M3$="I CAN'T. I'M NOT CARRYING ANY KEYS.":M4$="O.K. THE DOOR TO THE JAIL CELL IS UNLOCKED."
742 M5$="THERE'S NO ROBOT HERE.":M6$="BUT I DON'T HAVE ANY HAMBURGERS."
744 M7$="CHUMP...CHUMP BURP! THE PRINCESS THANKS YOU FOR A DELICIOUS MEAL.":M8$=" DOESN'T EAT HAMBURGER."
746 M9$="NOTHING HAPPENED. THE HAMBURGER IS COLD YOU KNOW.":N0$="THE ATTACK ROBOT EATS THE HAMBURGER AND DISAPPEARS."
748 N1$="I'M AT THE IDENTIFICATION TERMINAL. ON THE SCREEN IT SAYS: "+CHR$(34)+"SHOW I.D."+CHR$(34)
750 N2$="THE TRACTOR BEAM IS OFF.":N3$="THE TRACTOR BEAM IS ON.":N4$="YOU FORGOT TO OPEN THE FLIGHT DECK DOORS."
752 RETURN
800 PRINT" THE DOG-STAR MISSION ADVENTURE"
801 PRINT"BY LANCE MICKLUS, COPYRIGHT 1979";
802 PRINT"         ..":REM CHARLIE:)
803 PRINT"        /--"CHR$(92)"            .."
804 PRINT"       !<><>!          /--"CHR$(92)
805 PRINT"       /./"CHR$(92)"."CHR$(92)"         !<><>!"
806 PRINT"       /"CHR$(92)".../"CHR$(92)"        (./"CHR$(92)".)"
807 PRINT"      // "CHR$(91)" "CHR$(93)"!"CHR$(92)CHR$(92)"      /      "CHR$(92)
808 PRINT"     //! "CHR$(91)"."CHR$(93)"! "CHR$(92)CHR$(92)"     !!/(===*"
809 PRINT"     "CHR$(92)CHR$(92)"!    ! //     ! /  "CHR$(92)"!!"
810 PRINT"      "CHR$(92)"#====!#/      "CHR$(92)"/"CHR$(93)CHR$(91)CHR$(93)CHR$(91)CHR$(92)"/"
811 PRINT"      /!"CHR$(92)"  /I"CHR$(92)"        !"CHR$(92)"  /!"
812 PRINT"     / ! !! I "CHR$(92)"       !.!!.!"
813 PRINT"    /  ! !! !  "CHR$(92)"      "CHR$(91)" "CHR$(93)CHR$(91)" "CHR$(93)
814 PRINT"   /   ! !! !   "CHR$(92)"     ! !! !"
815 PRINT"  /    ! !! !    "CHR$(92)"    ! !! !";:RETURN
820 REM MC-10 EDITS
830 REM JIM GERRIE 2022
