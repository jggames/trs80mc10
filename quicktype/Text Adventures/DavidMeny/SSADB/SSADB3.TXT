0 CLEAR750:DIMC1,CC,M$,F$,RM$(17),A(17,10),P(5),O1$(11),O2$(11),M$(27),T(5),IN$(11),TR(11):GOTO190
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
190 CLS:PRINT@32,"ESCAPE FROM S.S.A.D.B.":PRINT"BY DAVID MENY":PRINT:GOSUB4230
230 GOSUB3240:GOSUB1900:KW=0
270 IFSI=1ANDSE=0THENMV=1:SE=1
280 IFSE=1THEN282
281 GOTO290
282 MV=MV+1
283 IFMV=3ANDRO=6ANDBOX=0THEN285
284 GOTO290
285 M$="A WORKER COMES RUNNING IN, SEES YOU AND ALERTS THE GUARDS.":GOSUB1
286 M$="THE GUARDS TAKE YOU AWAY TO BE TORTURED AND KILLED.":GOSUB1:GOTO3140
290 IFMV=3ANDRO=6ANDBOX=1THEN292
291 GOTO300
292 M$="A WORKER COMES IN AND GLANCES AROUND. NOTICING NOTHING, HE HEADS TO THE BALCONY.":GOSUB1
293 TR(11)=7
300 IFMV=3THENTR(11)=7
310 GOSUB3100:INPUTA$
320 IFA$=""THENPRINT"WHAT?":GOTO270
350 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO350
360 B$=LEFT$(A$,3)
370 IFB$="INV"ORB$="I  "THENGOSUB730:GOTO270
380 IFB$="SCO"THENGOSUB820:GOTO270
390 IFB$="QUI"ORB$="Q  "THENGOSUB850:GOTO270
400 IFB$="HEL"ORB$="CLU"THENM$="SORRY, THERE IS NO HELP FOR LOST AGENTS.":GOSUB1:GOTO270
410 IFB$="HID"THENGOSUB2360:GOTO270
420 IFB$="STA"THENGOSUB2410:GOTO270
430 IFB$="LOO"ORB$="L  "THENGOTO230
440 IFB$="N  "THEN815
450 IFB$="S  "THEN815
460 IFB$="E  "THEN815
470 IFB$="W  "THEN815
480 IFB$="U  "THEN815
490 IFB$="D  "THEN815
500 N=1
510 IFMID$(A$,N,1)=" "THENC$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN540
520 IFN<MTHENN=N+1:GOTO510
530 M$="IN THAT FORM, I DON'T KNOW THE WORD "+A$:GOSUB1:GOTO270
540 IFB$="GO "ORB$="MOV"ORB$="WAL"THENKW=1:GOSUB920:GOTO270
550 IFB$="EXA"ORB$="SEA"THENGOSUB2590:GOTO270
560 IFB$="TAK"ORB$="GET"THENKW=1:GOSUB1600:GOTO270
570 IFB$="DRO"ORB$="PUT"THENKW=1:GOSUB1140:GOTO270
580 IFB$="UNL"THENKW=1:GOSUB2110:GOTO270
590 IFB$="LOC"THENKW=1:GOSUB2440:GOTO270
600 IFB$="OPE"THENKW=1:GOSUB1280:GOTO270
610 IFB$="CLO"THENKW=1:GOSUB2510:GOTO270
620 IFB$="REA"THENKW=1:GOSUB1440:GOTO270
630 IFB$="PUS"ORB$="PRE"THENKW=1:GOSUB2260:GOTO270
640 IFB$="PUL"ORB$="UNP"THENKW=1:GOSUB2980:GOTO270
650 IFB$="TYP"THENKW=1:GOSUB3030:GOTO270
660 IFB$="POU"ORB$="SPR"THENKW=1:GOSUB2910:GOTO270
670 IFB$="INS"THENKW=1:GOSUB1830:GOTO270
680 IFB$="KIC"ORB$="HIT"THENKW=1:GOSUB1970:GOTO270
690 IFB$="MIX"ORB$="COM"THENKW=1:GOSUB2800:GOTO270
700 IFB$="THR"THENKW=1:GOSUB2190:GOTO270
710 M$="I DON'T KNOW THAT WORD.":GOSUB1:GOTO270
730 M$="YOU ARE CARRYING: ":FORI=1TO8:IFTR(I)=200THENM$=M$+IN$(I)+", ":AA=1
780 NEXT:IFAA<>1THENM$=M$+"NOTHING, "
800 AA=LEN(M$):M$=LEFT$(M$,AA-2)+".":GOSUB1:AA=0
810 RETURN
815 C$=B$:GOSUB920:GOTO270
820 M$="OUT OF 200 POINTS, YOU GOT"+STR$(SC)+" POINTS.":GOSUB1:RETURN
845 PRINT "YOU CAN'T GO THAT WAY.":RETURN
850 GOSUB820:GOSUB3100:INPUT"SURE YOU WANT TO QUIT";F$:IFF$="Y"ORF$="YES"THENEND
910 PRINT"OK.":GOTO270
920 C$=LEFT$(C$,1):IFRO=10ANDGU=1THEN942
941 GOTO950
942 M$="AS YOU TRY TO LEAVE THE GUARD SPOTS YOU AND CAPTURES YOU.":GOSUB1:GOTO286
950 IFRO=9ANDC$="S"ANDPLUG=0THEN952
951 GOTO960
952 M$="AS YOU STEP INTO THE RECORD ROOM, THE CAMERA SPIES YOU AND ALERTS THE GUARDS.":GOSUB1:GOTO286
960 IFRO=13ANDC$="S"ANDTR(11)=16THEN962
961 GOTO970
962 M$="AS YOU STEP INTO THE MISSLE SITE, A WORKER SEES YOU AND CALLS THE GUARDS.":GOSUB1:GOTO286
970 IFRO=6ANDBOX=1THEN972
971 GOTO980
972 M$=M$="YOU CAN'T GO THAT WAY WHILE YOU ARE CROUCHING IN THE BOX.":GOSUB1:RETURN
980 IFRO=7ANDTR(11)=7ANDC$="W"THEN982
981 GOTO990
982 M$="AS YOU TRY TO LEAVE, THE WORKER GRABS YOU AND CALLS THE GUARDS.":GOTO286
990 IFC$="N"ANDA(RO,1)=0THEN1135
1000 IFC$="S"ANDA(RO,2)=0THEN1135
1010 IFC$="E"ANDA(RO,3)=0THEN1135
1020 IFC$="W"ANDA(RO,4)=0THEN1135
1030 IFC$="U"ANDA(RO,5)=0THEN1135
1040 IFC$="D"ANDA(RO,6)=0THEN1135
1050 IFC$="N"THENRO=A(RO,1)
1060 IFC$="S"THENRO=A(RO,2)
1070 IFC$="E"THENRO=A(RO,3)
1080 IFC$="W"THENRO=A(RO,4)
1090 IFC$="U"THENRO=A(RO,5)
1100 IFC$="D"THENRO=A(RO,6)
1110 IFRO=3ANDS1=0THENSC=SC+10:S1=1
1120 IFRO=10ANDDS=0THENSC=SC+30:DS=1
1130 RETURN
1135 GOSUB845:RETURN
1140 FL=0:OB=0
1170 FORI=1TO8:IFTR(I)=200THENFL=1
1190 NEXTI
1200 IFFL=0THENPRINT"YOU AREN'T CARRYING ANYTHING.":RETURN
1210 FORI=1TO8
1220 IFC$=O1$(I)ANDTR(I)=200THENOB=I
1230 NEXTI
1240 IFOB=0THENPRINT"YOU'RE NOT HOLDING THAT ITEM.":RETURN
1250 TR(OB)=RO
1260 PRINT"OK."
1270 RETURN
1280 IFC$<>"DOO"ANDC$<>"CAB"ANDC$<>"VAU"THENM$="YOU CAN'T OPEN THAT.":GOSUB1:RETURN
1300 IFC$<>"DOO"THEN1360
1310 IFC$="DOO"ANDDO=0ANDRO=15THENM$="THE DOOR IS LOCKED.":GOSUB1:RETURN
1320 IFC$="DOO"ANDDO=1ANDRO=15THEN1322
1321 GOTO1330
1322 M$="THE DOOR SWINGS OPEN, REVEALING A PASSAGEWAY TO THE NORTH.":GOSUB1:DO=2:A(10,2)=15:A(15,1)=10:RETURN
1330 IFC$="DOO"ANDDO=1ANDRO=10THEN1332
1331 GOTO1340
1332 M$="THE DOOR SWINGS OPEN, REVEALING A PASSAGEWAY TO THE SOUTH.":GOSUB1:DO=2:A(10,2)=15:A(15,1)=10:RETURN
1340 IFC$="DOO"ANDDO=2ANDRO=15ORRO=10THENPRINT"THE DOOR IS ALREADY OPEN.":RETURN
1350 PRINT"YOU CAN'T SEE A DOOR HERE.":RETURN
1360 IFC$<>"VAU"THEN1400
1370 IFC$="VAUL"ANDVA=0ANDRO=1THENM$="YOU HAVE TO TYPE IN THE CORRECT CODE FOR THE VAULT TO OPEN.":GOSUB1:RETURN
1380 IFC$="VAUL"ANDVA=1ANDRO=1THENPRINT"IT'S ALREADY OPEN.":RETURN
1390 PRINT"YOU CAN'T SEE THE VAULT HERE.":RETURN
1400 IFC$="CAB"ANDCAB=0ANDRO=14THENPRINT"THE CABINET IS NOW OPEN.":CAB=1:TR(7)=14:RETURN
1410 IFC$="CAB"ANDCAB=1ANDRO=14THENPRINT"THE CABINET IS ALREADY OPEN.":RETURN
1420 PRINT"YOU CAN'T SEE A CABINET HERE.":RETURN
1430 RETURN
1440 FL=0
1470 IFC$<>"PLA"THEN1500
1480 IFRO<>6THENM$="YOU DON'T SEE THE PLAQUE HERE.":GOSUB1:RETURN
1490 M$="THE PLAQUE READS: 'PRESS IN CASE OF EMERGENCY.'":GOSUB1:RETURN
1500 FORI=1TO8
1510 IFC$=O1$(I)ANDTR(I)=200THENFL=1
1520 NEXT
1530 IFFL=0THENPRINT"YOU DON'T HAVE THAT ITEM.":RETURN
1540 HH$=LEFT$(C$,3)
1550 IFHH$<>"NOT"ANDHH$<>"NEW"ANDHH$<>"SLI"ANDHH$<>"FIL"THENM$="YOU CAN'T READ THAT!":GOSUB1:RETURN
1560 IFHH$="NEW"ANDTR(2)=200THEN1562
1561 GOTO1570
1562 M$="THE NEWSPAPER READS: 'SIGNS OF MX-13 MISSLES BEING SHIPPED TO USSR...' IT GOES ON BUT YOU CAN'T MAKE ANYMORE OUT."
1563 GOSUB1:RETURN
1570 IFHH$="SLI"ANDTR(6)=200THENM$="THE SLIP READS: 'PORCH NEEDS FIXING AND NEW CODE IS '971'.'":GOSUB1:RETURN
1580 IFHH$="FIL"ANDTR(7)=200THENM$="THE FILE READS: 'TOP-SECRET INFORMATION ON MX-13 AND DEPLOYMENT.'":GOSUB1:RETURN
1590 IFHH$="NOT"ANDTR(3)=200THEN1592
1591 GOTO1600
1592 M$="THE NOTE READS: 'NEW SCIENTIFIC BREAKTHROUGH: ANALGESIC AND LIQUID IN BEAKER MAKES GREAT ACID!'":GOSUB1:RETURN
1600 IFC$="GUA"ORC$="WOR"THENPRINT"YOU CAN'T GET THAT.":RETURN
1640 FL=0:HH=0:OB=0
1650 FORI=1TO8
1660 IFTR(I)=200THENHH=HH+1
1670 NEXT
1680 IFHH>=6THENPRINT"YOU CAN'T CARRY ANYMORE.":RETURN
1690 FORI=1TO8
1700 IFTR(I)=ROORTR(2)=220THENFL=1
1710 NEXTI
1720 IFFL=0THENPRINT"THERE IS NOTHING HERE TO GET.":RETURN
1730 IFTR(2)=220ANDC$="NEW"ANDTR(4)=220THENOB=2:IN=0:TR(4)=15:GOTO1790
1740 IFTR(2)=220ANDC$="NEW"THENOB=2:IN=0:GOTO1790
1750 FORI=1TO8:IFC$=O1$(I)ANDTR(I)=ROTHENOB=I
1770 NEXT
1780 IFOB=0THENM$="I DON'T SEE THAT OBJECT HERE.":GOSUB1:RETURN
1790 TR(OB)=200
1800 PRINT"OK.":IFTR(7)=200ANDSO=0THENSC=SC+50:SO=1
1810 IFOB=4ANDDG=0THENSC=SC+20:DG=1
1820 RETURN
1830 IFC$<>"NEW"THENPRINT"YOU CAN'T INSERT THAT.":RETURN
1850 GOSUB3100:INPUT"UNDER WHAT";F$:F$=LEFT$(F$,3):IFF$<>"DOO"THENM$="YOU CAN'T INSERT THE NEWSPAPER UNDER THAT.":GOSUB1:RETURN
1870 IFRO<>15THENPRINT"YOU CAN'T SEE A DOOR HERE.":RETURN
1880 PRINT"OK.":IN=1:TR(2)=220:RETURN
1900 FORI=1TO11:IFTR(I)=ROTHENM$=O2$(I):GOSUB1
1940 NEXTI:RETURN
1945 M$="I CAN'T USE THAT WORD HERE.":GOSUB1:RETURN
1970 IFRO<>15ANDRO<>7THEN1945
2000 IFC$<>"DOO"ANDC$<>"WOR"THENPRINT"YOU CAN'T HIT THAT.":RETURN
2010 IFC$="WOR"THEN2080
2020 IFRO=15ANDC$="DOO"ANDKE=0THENM$="YOU HEAR A SOUND FROM THE OTHER SOUND OF THE DOOR.":GOSUB1:KE=1:GOTO2040
2030 IFRO=15ANDC$="DOO"ANDKE=1THENPRINT"IT DOESN'T WORK THIS TIME.":RETURN
2040 IFIN=0THENM$="SORRY, BECAUSE OF THAT LAST ACTION, THERE IS NO WAY FOR YOU TO PROCEED.":GOSUB1:GOTO3140
2050 IFIN=1THENM$="THERE IS NOW SOMETHING ON THE NEWSPAPER.":GOSUB1
2060 TR(4)=220:RETURN
2080 IFRO=7ANDC$="WOR"ANDWO=0THEN2082
2081 GOTO2090
2082 M$="THE WORKER FALLS OVER THE PORCH, SCREAMING. SOMETHING FELL OUT OF HIS POCKET!":GOSUB1:TR(8)=7:TR(11)=255:WO=1:RETURN
2090 IFRO=7ANDC$="WOR"ANDWO=1THENM$="THE WORKER IS DEAD, BECAUSE OF YOU!":GOSUB1
2100 RETURN
2110 IFRO<>15ANDRO<>10THEN1945
2140 IFC$<>"DOO"THENPRINT"I CAN'T UNLOCK THAT.":RETURN
2150 IFTR(4)<>200THENM$="YOU HAVE NOTHING TO UNLOCK THE DOOR WITH.":GOSUB1:RETURN
2160 IFDO=0THENPRINT"THE DOOR IS NOW UNLOCKED.":DO=1:GOTO2180
2170 IFDO=1ORDO=2THENPRINT"THE DOOR IS ALREADY UNLOCKED."
2180 RETURN
2185 PRINT"THE ELEVATOR DOORS CLOSE...":FORI=1TO1500:NEXT:PRINT"THEN THEY OPEN...":RETURN
2190 IFRO<>10THEN1945
2210 IFC$<>"BRI"THENPRINT"YOU CAN'T THROW THAT.":RETURN
2220 GOSUB3100:INPUT"AT WHAT";F$:F$=LEFT$(F$,3):IFF$="GUA"ANDGU=1THEN2232
2231 GOTO2240
2232 M$="YOU HIT THE GUARD IN THE BACK OF THE HEAD AND HE BECOMES UNCONSCIOUS.":GOSUB1
2233 TR(9)=255:TR(10)=10:GU=0:TR(1)=10:SC=SC+20:RETURN
2240 IFF$="GUA"ANDGU=0THENM$="THE GUARD IS ALREADY UNCONSCIOUS.":GOSUB1:RETURN
2250 PRINT"YOU CAN'T THROW IT AT THAT.":RETURN
2260 IFRO<>6ANDRO<>11ANDRO<>12ANDRO<>13THEN1945
2280 IFC$="ONE"THENGOSUB2185:RO=11:RETURN
2300 IFC$="TWO"THENGOSUB2185:RO=12:RETURN
2310 IFC$="THR"THENGOSUB2185:RO=13:RETURN
2320 IFC$="BUT"ANDRO=6ANDGH=0THEN2322
2321 GOTO2340
2322 M$="A SIREN SOUNDS AND A WORKER WILL BE IN HERE ANY MINUTE. THE BUTTON GOES IN SO YOU CAN'T PRESS IT AGAIN."
2323 GOSUB1:SI=1:GH=1:RETURN
2330 IFC$="BUT"ANDRO=6ANDGH=1THENPRINT"YOU CAN'T PUSH IT AGAIN.":RETURN
2340 PRINT"YOU CAN'T PUSH THAT.":RETURN
2360 IFRO<>6THEN1945
2380 GOSUB3100:INPUT"IN WHAT";F$:F$=LEFT$(F$,3):IFF$="BOX"THENM$="OK, YOU ARE NOW CROUCHING IN THE BOX.":GOSUB1:BOX=1:RETURN
2400 PRINT"YOU CAN'T HIDE IN THERE.":RETURN
2410 IFBOX=0THENPRINT"YOU ARE ALREADY STANDING.":RETURN
2430 IFBOX=1THENM$="OK, YOU ARE NOW STANDING OUTSIDE THE BOX.":GOSUB1:BOX=0:RETURN
2440 IFRO<>10ANDRO<>15THEN1945
2460 IFC$<>"DOO"THENPRINT "YOU CAN'T LOCK THAT.":RETURN
2470 IFTR(4)<>200THENM$="YOU HAVE NOTHING TO LOCK THE DOOR WITH.":GOSUB1:RETURN
2480 IFDO=0THENPRINT"THE DOOR IS ALREADY LOCKED.":RETURN
2490 IFDO=1ORDO=2THENM$="OK, THE DOOR IS NOW CLOSED AND LOCKED.":GOSUB1:DO=0:A(15,1)=0:A(10,2)=0
2500 RETURN
2510 IFC$<>"DOO"ANDC$<>"CAB"THENPRINT"YOU CAN'T CLOSE THAT.":RETURN
2530 IFC$="DOO"ANDDO=2ANDRO=15THENPRINT"IT'S CLOSED NOW.":DO=1:A(10,2)=0:A(15,1)=0:RETURN
2540 IFC$="DOO"ANDDO=2ANDRO=10THENPRINT"IT'S CLOSED NOW.":DO=1:A(15,1)=0:A(10,2)=0:RETURN
2550 IFC$="DOO"ANDDO=0ORDO=1ANDRO=10ORRO=15THENPRINT "THE DOOR IS ALREADY CLOSED.":RETURN
2560 IFC$="CAB"ANDCAB=1ANDRO=14THENPRINT"THE CABINET IS NOW CLOSED.":RETURN
2570 IFC$="CAB"ANDCAB=0ANDRO=14THENPRINT"THE CABINET IS ALREADY CLOSED.":RETURN
2580 PRINT"YOU CAN'T CLOSE THAT HERE.":RETURN
2590 IFC$="DOO"AND(RO=15ORRO=10)THEN2612
2611 GOTO2620
2612 M$="THE LARGE, WOODEN DOOR HAS A KEYHOLE AND A SMALL SPACE BELOW IT. THROUGH "
2613 M$=M$+"THE KEYHOLE YOU CAN SEE A KEY, UNREACHABLE FROM THIS SIDE.":GOSUB1:RETURN
2620 IFC$="BRI"ANDTR(1)=200THENM$="IT'S JUST A NORMAL, HEAVY BRICK.":GOSUB1:RETURN
2630 IFC$="NEW"ANDTR(2)=200THENM$="IT HAS SOME READABLE WRITING ON IT.":GOSUB1:RETURN
2640 IFC$="NOT"ANDTR(3)=200THENM$="IT HAS SOME WRITING ON IT.":RETURN
2650 IF(C$="KEY"ANDTR(4)=200)OR(C$="BEA"ANDTR(5)=200)THENM$="I SEE NOTHING SPECIAL ABOUT THAT.":GOSUB1:RETURN
2670 IFC$="SLI"ANDTR(6)=200THENM$="IT HAS SOME WRITING ON IT.":RETURN
2680 IFC$="FIL"ANDTR(7)=200THENM$="THE FILE CONTAINS TOP-SECRET INFORMATION.":GOSUB1:RETURN
2690 IFC$="ASP"ANDTR(8)=200THENM$="THE ASPIRIN HAS 'BUFFERIN' ON IT.":GOSUB1:RETURN
2700 IFC$="GUA"ANDTR(9)=ROTHENM$="THE GUARD IS A HARD WORKING EMPLOYEE OF THE RUSSIANS.":GOSUB1:RETURN
2710 IFC$="GUA"ANDTR(10)=ROTHENM$="THE GUARD IS UNCONSCIOUS.":GOSUB1:RETURN
2720 IFC$="WOR"ANDTR(11)=ROTHENM$="HE HAS SOMETHING IN HIS BACK POCKET.":GOSUB1:RETURN
2730 IFC$="PLA"ANDRO=6THENM$="THE PLAQUE HAS WRITING ON IT.":GOSUB1:RETURN
2740 IFC$="BOX"ANDRO=6THENM$="IT'S BIG ENOUGH FOR YOU TO HIDE IN.":GOSUB1:RETURN
2750 IFC$="CAB"ANDRO=14ANDCAB=1THENPRINT"THE CABINET IS OPEN.":RETURN
2760 IFC$="CAB"ANDRO=14ANDCAB=0THENPRINT"THE CABINET IS CLOSED.":RETURN
2770 IFC$="PLU"ANDRO=2ANDPLUG=0THENPRINT"IT'S CONNECTED TO AN OUTLET.":RETURN
2780 IFC$="PLU"ANDRO=2ANDPLUG=1THENPRINT"IT'S NOT CONNECTED.":RETURN
2790 PRINT"I CAN'T EXAMINE THAT NOW.":RETURN
2800 IFC$<>"ASP"ANDC$<>"BEA"THENPRINT "I CAN'T MIX THAT.":RETURN
2820 IFC$="BEA"THEN2870
2830 IFC$="ASP"ANDTR(8)=200THENGOSUB3100:INPUT"IN WHAT";F$:F$=LEFT$(F$,3)
2840 IFF$="BEA"ANDTR(5)=200THEN2842
2841 GOTO2850
2842 M$="THE BEAKER BEGINS TO BUBBLE AND INSIDE THERE IS AN ACIDY SOLUTION.":GOSUB1:TR(8)=255:BE=1:RETURN
2850 IFF$="BEA"THENPRINT"YOU DON'T HAVE THE BEAKER.":RETURN
2860 M$="YOU CAN'T MIX THE ASPIRIN WITH THAT.":GOSUB1:RETURN
2870 GOSUB3100:INPUT"WITH WHAT";F$:F$=LEFT$(F$,3):IFF$="ASP"ANDTR(8)=200THEN2842
2890 IFF$="ASP"THENPRINT"YOU DON'T HAVE THE ASPIRIN.":RETURN
2900 M$="YOU CAN'T MIX THAT WITH THE BEAKER.":GOSUB1:RETURN
2910 IFRO<>8THEN1945
2940 IFC$="BEA"ORC$="LIQ"ANDBE=0THENM$="THE LIQUID IS STUCK INSIDE (FOR NOW).":GOSUB1:RETURN
2950 IFC$="BEA"ORC$="LIQ"ANDBE=1THENGOSUB3100:INPUT"ON WHAT";F$:F$=LEFT$(F$,3)
2960 IFF$="WAL"ORF$="NOR"THEN2962
2961 GOTO2970
2962 M$="THE WALL TO THE NORTH, BECOMES A GIANT HOLE, ALLOWING YOU TO GO THAT WAY.":GOSUB1:A(8,1)=3:TR(5)=255:RETURN
2970 M$="YOU CAN'T POUR THE LIQUID ON THAT.":GOSUB1:RETURN
2980 IFRO<>2THEN1945
3000 IFC$<>"PLU"THENPRINT "YOU CAN'T PULL THAT.":RETURN
3010 IFPLUG=1THENPRINT"IT'S ALREADY UNPLUGGED.":RETURN
3020 PRINT"OK, IT'S NOW UNPLUGGED.":PLUG=1:RETURN
3030 IFRO<>1THEN1945
3050 IFC$<>"971"THEN3052
3051 GOTO3060
3052 M$="YOU TYPE IN THE WRONG CODE AND A LOUD SIREN SOUNDS AND GUARDS":GOSUB1:GOTO286
3060 M$="CONGRADULATIONS! YOU GOT THE RIGHT CODE. YOU RUSH OUTSIDE TO FIND THAT YOU ARE INSIDE A LARGE SEWER. "
3061 M$=M$+"BUT, ABOVE YOU SEE A MAN HOLE, SO YOU CLIMB OUT. YOU RUSH TO TELL THE POLICE YOUR STORY.":GOSUB1
3070 IFTR(7)=200THEN3072
3071 GOTO3080
3072 M$="YOU SHOW THE POLICE THE FILE AND YOU ARE REWARDED WITH A HERO'S REWARD!":GOSUB1:SC=SC+70:GOTO3140
3080 M$="YOU HAVE NO PROOF TO SHOW THE POLICE, SO THEY LOCK YOU UP. "
3081 M$=M$+"IN EXACTLY FIVE DAYS, THE SOVIETS LAUNCH THEIR MX-13 MISSLES AT THE U.S. ALL LIFE ENDS.":GOSUB1:GOTO3140
3100 FORC1=16389TO16399:POKEC1,32:NEXT:M$="SCORE:"+STR$(SC):FORC1=1TOLEN(M$):POKE16383+C1,ASC(MID$(M$,C1,1)):NEXT
3110 FORC1=16384TO16388:POKEC1,PEEK(C1)-64:NEXT:FORC1=1TO16:POKE16399+C1,ASC(MID$(RM$(RO),C1,1)):NEXT:RETURN
3140 GOSUB820
3160 GOSUB3100:INPUT"PLAY AGAIN";F$:F$=LEFT$(F$,1):IFF$="Y"ORF$="YES"THEN3210
3180 IFF$="N"ORF$="NO"THENEND
3200 GOTO3160
3210 RO=17:SO=0:S1=0:DO=0:KE=0:SC=0:GU=1:BOX=0:BE=0:DS=0:DG=0
3220 TR(1)=17:TR(2)=17:TR(3)=8:TR(4)=255:TR(5)=8:TR(6)=5:TR(7)=255:TR(8)=16:GOTO230
3240 ONROGOSUB3320,3380,3450,3500,3550,3600,3660,3700,3750,3790,3840,3840,3840,3990,4040,4100,4160:RETURN
3320 M$="YOU ARE INSIDE A VERY LARGE ROOM WHICH IS THE EXIT TO THE OUTSIDE WORLD. BUT BEFORE ANYONE GETS OUTSIDE, "
3350 M$=M$+"THEY MUST PASS THROUGH A LARGE METAL VAULT WHICH HAS A KEYBOARD ATTACHED TO IT.":GOSUB1:RETURN
3380 M$="THIS IS A ROOM WHERE MAINTENCE EQUIPMENT IS STORED. THERE ARE MANY  BROOMS, DUSTPANS AND MOPS AROUND HERE.":GOSUB1
3410 M$="A DARK PASSAGE WAY LEADS TO THE SOUTH, WHILE A LIGHTED WAY LEADS TO THE EAST. "
3420 M$=M$+"THERE IS A PLUG ON THE EAST WALL CONNECTED TO AN OUTLET.":GOSUB1:RETURN
3450 M$="YOU ARE IN A SECRET ROOM BEHIND THE BASE LABORATORY. THE HOLE IN THE SOUTH "
3480 M$=M$+"WALL IS SMALLER ON THIS SIDE, PREVENTING YOU FROM GOING SOUTH.":GOSUB1:RETURN
3500 M$="THIS IS A NONDESCRIPT HALLWAY RUNNING NORTH AND SOUTH.":GOSUB1:RETURN
3550 M$="THIS IS A REPAIR CLOSET WHERE HAMMERS, NAILS AND SCREWDRIVERS ARE KEPT. THE ONLY EXITS IS THE WAY YOU CAME."
3560 GOSUB1:RETURN
3600 M$="YOU ARE IN A STORAGE ROOM WHERE MISCELLANOUS EQUIPMENT IS STORED.":GOSUB1
3610 M$="":IFBOX=1THENM$="YOUR ARE CROUCHING IN A BOX.":GOTO3630 
3620 M$="THERE IS A LARGE, HEAVY BOX IN THE SOUTH CORNER. "
3630 M$=M$+"TO THE EAST IS A BALCONY. ON THE NORTH WALL THERE IS A BUTTON WITH A SMALL PLAQUE BENEATH IT.":GOSUB1:RETURN
3660 M$="THIS IS AN OLD, CRUMBLING BALCONY.":GOSUB1:RETURN
3700 M$="YOU ARE IN A SCIENTIFIC LABROTORY. THE USUAL ARRAY OF SUPPLIES AND EQUIPMENT IS "
3730 M$=M$+"HERE. THE NORTH WALL IS A BIT LESS SOLID THAN THE OTHERS.":GOSUB1:RETURN
3750 M$="THIS IS A VERY NON-DESCRIPT HALLWAY RUNNING EAST TO WEST.":GOSUB1:RETURN
3790 M$="YOU ARE IN A GUARD ROOM, WHERE A GUARD IS SUPPOSED TO WATCH THE CELL TO THE "
3820 M$=M$+"SOUTH. PASSAGEWAYS RUN OFF IN THREE DIRECTIONS.":GOSUB1:RETURN
3840 M$="THIS IS AN ELEVATOR CONNECTING ALL THREE LEVELS OF THE BASE. THERE IS A PANEL "
3870 M$=M$+"HERE WITH THREE BUTTONS LABELED 1,2 AND 3.":GOSUB1:RETURN
3990 M$="THIS IS A RECORD ROOM WHERE TOP SECRET INFORMATION IS KEPT. THERE IS A FILE "
4020 M$=M$+"CABINET AND CAMERA MOUNTED ON THE WEST WALL.":GOSUB1:RETURN
4040 M$="THIS IS A NORTH CELL WHERE TOP SECURITY PRISONERS ARE KEPT. THERE IS A "
4070 M$=M$+"LARGE, WOODEN DOOR ON THE NORTH WALL. TO THE SOUTH A LIGHT CAN BE SEEN.":GOSUB1
4080 IFIN=1THENM$="UNDER THE DOOR, YOU CAN SEE A NEWSPAPER.":GOSUB1
4090 RETURN
4100 M$="THIS IS A LARGE MISSLE SILO. THE ROUNDED CEILING IS PARTLY OPEN, "
4110 M$=M$+"AWAITING THE PREPARATION OF THE MX-13 MISSILE LOCATED IN THE CENTER OF THE ROOM.":GOSUB1:RETURN
4160 M$="YOU ARE IN A CELL WHERE PRISONERS ARE KEPT. THE SECOND PART OF THIS TWO ROOM CELL IS TO THE NORTH WHILE A "
4190 M$=M$+"CRACK IN THE CEILING IS LETTING IN A LARGE AMOUNT OF LIGHT.":GOSUB1:RETURN
4230 SC=0:RO=17:GU=1:FL=0:KE=0:DO=0:A(15,7)=99:A(8,7)=100:FORX=1TO17:FORY=1TO6
4300 READA(X,Y):NEXTY:NEXTX:FORX=1TO11:READO1$(X),O2$(X),TR(X),IN$(X):NEXTX:FORI=1TO17:READM$
4310 FORC1=1TOLEN(M$):CC=ASC(MID$(M$,C1,1)):IFCC=32THENF$=F$+CHR$(32):GOTO4330
4320 F$=F$+CHR$(CC-64)
4330 NEXT:RM$(I)=RIGHT$("          "+F$,16):F$="":NEXT:RETURN
4410 DATA 0,4,0,0,0,0,0,8,3,0,0,0,0,0,0,2,0,0,1,10,5,0,0,0,0,0,0,4,0,0,0,12,7,0,0,0,0,0,0,6,0,0,0,0,9,0,0,0,0,14,10,8,0,0
4500 DATA 4,0,11,9,0,0,0,0,0,10,0,0,6,0,0,0,0,0,0,16,0,0,0,0,9,0,0,0,0,0,0,17,0,0,0,0,13,0,0,0,0,0,15,0,0,0,0,0
4580 DATA "BRI","THERE IS A BRICK ON THE FLOOR NEAR YOU.",17,"A BRICK"
4590 DATA "NEW","ON THE FLOOR, THERE IS AN OLD NEWSPAPER.",17,"A NEWSPAPER"
4600 DATA "NOT","THERE IS A NOTE LAYING NEARBY.",8,"A NOTE","KEY","THERE IS A SHINY KEY HERE.",255,"A KEY"
4620 DATA "BEA","THERE IS A SCIENTIFIC BEAKER FULL OF LIQUID NEAR YOU.",8,"A BEAKER FULL OF LIQUID"
4630 DATA "SLI","THERE IS A SLIP OF PAPER LYING HERE.",5,"A SLIP"
4640 DATA "FIL","A FILE CONTAING IMPORTANT INFORMATION IS HERE.",255,"A FILE"
4650 DATA "ASP","THERE IS A ASPIRIN HERE.",255,"AN ASPIRIN","GUA","THERE IS AN ALERT GUARD HERE.",10,""
4670 DATA "GUA","THERE IS AN UNCONSCIOUS GUARD HERE.",255,"","WOR","THERE IS A MISSLE WORKER HERE.",16,""
4690 DATA VAULT ROOM,MAINTENCE ROOM,SECRET ROOM,HALLWAY,REPAIR CLOSET,STORAGE ROOM,BALCONY,LABROTORY,HALLWAY,GUARD ROOM
4700 DATA ELEVATOR,ELEVATOR,ELEVATOR,RECORD ROOM,NORTH CELL,MISSLE SITE,SOUTH CELL
