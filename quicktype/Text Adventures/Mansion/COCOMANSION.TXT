0 REM COPYRIGHT (C) T&D SOFTWARE 1982
1 CLS:L$=CHR$(92):ZZ=8:PRINT@73,"KILLER MANSION!":PRINT:PRINTTAB(ZZ)"        /"+L$
2 PRINTTAB(ZZ)"       /()"+L$
3 PRINTTAB(ZZ)"   /"+L$+" /----"+L$+"IIII"
4 PRINTTAB(ZZ)"  /  "+L$+"! "+CHR$(91)+CHR$(93)+" !    "+L$
5 PRINTTAB(ZZ)" / "+CHR$(91)+CHR$(93)+" "+L$+"    !-----"
6 PRINTTAB(ZZ)"/-----     !    !"
7 PRINTTAB(ZZ)"  !   +--+ ! "+CHR$(91)+CHR$(93)+" !"
8 PRINTTAB(ZZ)"  !   !  ! !    !"
9 PRINTTAB(ZZ)"  !   !' ! !----!"
10 PRINTTAB(ZZ)"  !---!--!-!"
20 PRINT@480,"INSTRUCTIONS (Y/N)";:INPUT IN$:IF IN$="Y" THEN GOSUB 2170
30 CLEAR 500:DIM A(25),A$(25),B$(25)
40 L=15:W=1:DO=0:CR=0
50 CLS
60 FOR X=1 TO 25:READ A(X):NEXT X
70 FOR X=1 TO 25:READ A$(X),B$(X)
80 NEXT X
90 X=9
100 CLS:PRINT"YOU ARE IN A ";A$(X);
110 PRINT@61,"+";:PRINT @42,"YOU CAN GO ";
120 FOR N=1 TO 25:IF A(N)=L+7 THEN PRINT@29,"N";
130 IF A(N)=L-7 THEN PRINT@93,"S ";
140 IF A(N)=L+1 THEN PRINT@62,"E";
150 IF A(N)=L-1 THEN PRINT@60,"W";
160 NEXT N
170 A=3:PRINT@97,"YOU SEE:"
180 L5=LEN(B$(X))
190 FOR Q=3 TO L5 STEP 2
200 N$=MID$(B$(X),Q,2):N=VAL(N$)
210 IF N<1 THEN 230
220 PRINT"         A ";:GOSUB 460
230 NEXT Q
240 IF L=23 AND DO=0 THEN 1860
250 IF L=18 AND CR=0 THEN 2020
260 IF L=3 AND CR=0 THEN 2140
270 PRINT:PRINT"WHAT ARE YOU GOING TO DO?":INPUT M$
280 IF M$<>"N" AND M$<>"S" AND M$<>"E" AND M$<>"W" THEN 360
290 IF M$="N" THEN L1=7
300 IF M$="S" THEN L1=-7
310 IF M$="E" THEN L1=1
320 IF M$="W" THEN L1=-1
330 FOR X1=1 TO 25:MI$=MID$(B$(X1),1,2):IF VAL(MI$)=(L+L1) THEN 350
340 NEXT X1:PRINT"YOU RAN INTO A WALL!!":GOTO 270 
350 L=L+L1:X=X1:GOTO 100
360 Z$=MID$(M$,1,3)
370 IF Z$="DRO" THEN 1000
380 IF Z$="GET" THEN 1050
390 IF Z$="EXA" THEN 1140
400 IF Z$="COM" OR Z$="HEL" THEN 1300
410 IF Z$="INV" THEN 1410
420 IF Z$="MOV" THEN 1500
430 IF Z$="OPE" THEN 1540
440 IF Z$="USE" THEN 1580
450 PRINT"I DO NOT UNDERSTAND THE VERB!!":GOTO 270
460 ON1-(N>0)GOTO470:ONNGOTO470,480,490,500,510,520,530,540,550,560,570,580,590,600,610,620,630,640,650,660
465 ONN-20GOTO670,680,690,700,710,720,730,740,750,760,770,780,790,800,810,820,830,840,850,860,870,880,890,900
470 PRINT"BROKEN LIGHT FIXTURE":RETURN
480 PRINT"FEW COBWEBS":RETURN
490 PRINT"CRACKED MIRROR":RETURN
500 PRINT"DIRTY RUG":RETURN
510 PRINT"BROKEN WINDOW":RETURN
520 PRINT"SMALL BUCKET":RETURN
530 PRINT "DIRTY GLASS JAR":RETURN
540 PRINT"PAIR OF WORK CLOTHES":RETURN
550 PRINT"RUSTY OLD SHOVEL":RETURN
560 PRINT"FLATTENED OUT HAT":RETURN
570 PRINT"TRAP DOOR":RETURN
580 PRINT"CRUMPLED PAPER":RETURN
590 PRINT"KNIFE":RETURN
600 PRINT"ANTIQUE DESK":RETURN
610 PRINT"OLD CHICKEN BONE":RETURN
620 PRINT"TOWEL":RETURN
630 PRINT"RUSTY FAUCET":RETURN
640 PRINT"COUNTER":RETURN
650 PRINT"SINK":RETURN
660 PRINT"ROW OF SHELVES":RETURN
670 PRINT"TABLE":RETURN
680 PRINT"WRINKLED NEWSPAPER":RETURN
690 PRINT"SCAMPERING RAT":RETURN
700 PRINT"FEROCIOUS WATCHDOG!":RETURN
710 PRINT"FRIENDLY WATCHDOG":RETURN
720 PRINT"BED":RETURN
730 PRINT"OLD STOVE":RETURN
740 PRINT"CHEST WITH A LOCK":RETURN
750 PRINT"BOX":RETURN
760 PRINT"TORTURE CHAMBER":RETURN
770 PRINT"OLD SKELETON":RETURN
780 PRINT"ICE PICK":RETURN
790 PRINT"DIRT FLOOR":RETURN
800 PRINT"LOCKED DOOR":RETURN
810 PRINT"BLOOD STAIN!!":RETURN
820 PRINT"HALF EATEN WHOPPER!":RETURN
830 PRINT"SILVER KEY":RETURN
840 PRINT"BROOM":RETURN
850 PRINT"LOCKED DRESSER":RETURN
860 PRINT"DEAD CRIMINAL":RETURN
870 PRINT"NAIL FILE":RETURN
880 PRINT"PLUNGER":RETURN
890 PRINT"CONTROL PANEL":RETURN
900 PRINT"TRAIL OF FOOTPRINTS":RETURN
910 DATA 3,7,8,9,10,11,13,14,15,16,18,20,21,22,23,24,26,27,28,30,35,37,60,61,68
920 DATA "COAL STORAGE","03051233","LAUNDRY ROOM","070206","BASEMENT","080405"
930 DATA "FRUIT ROOM","090702","COAL ROOM","10080409","SMALL PASSAGE","11364435"
940 DATA "CLOSET","131015","DEN","141444","HALLWAY","15010203"
950 DATA "BATHROOM","16161742","HIDING ROOM","1837","STORAGE CLOSET","203839","DINING ROOM","212127"
960 DATA "LIVING ROOM","222241","SITTING ROOM","2324","SECRET ROOM","244443"
970 DATA "FOOD STORAGE","260511","PANTRY","272004","KITCHEN","28181923"
980 DATA "GUEST ROOM","301426","DEN","351420","ATTIC","372822"
990 DATA "DUNGEON","602302","TORTURE ROOM","6131","JAIL ROOM","683234"
1000 REM
1010 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXT P
1020 FOR P2=1 TO W:IF N1=VAL(V$(P2)) THEN 1040
1030 NEXT P2:PRINT"YOU DON'T HAVE THAT!!":GOTO 270
1040 B$(X)=B$(X)+" " + STR$(N1):V$(P2)="00":PRINT"OK..YOU DROPPED IT":GOTO 270
1050 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3)
1051 IF MG$="RUG" OR MG$="BUC" OR MG$="SHO" OR MG$="KNI" OR MG$="NEW" OR MG$="PIC" OR MG$="KEY" OR MG$="FIL" THEN1080
1060 IF MG$="JAR" OR MG$="WHO" OR MG$="PAP" OR MG$="BON" OR MG$="BOX" THEN 1080
1070 NEXTP:PRINT"YOU CANNOT GET THAT!!":GOTO 270
1080 GOSUB 1660
1090 L3=LEN(B$(X)):FOR C=3 TO L3 STEP 2:N1$=MID$(B$(X),C,2):IF VAL(N1$)=N1 THEN 1110
1100 NEXT C:PRINT"CANNOT FIND IT!":GOTO 270
1110 W1=0:FOR K=1 TO W:IF VAL(V$(K))>0 THEN W1=W1+1
1120 NEXT K:IF W1>3 THEN PRINT"YOU CANNOT CARRY ANY MORE!":GOTO 270
1130 V$(W)=V$(W)+MID$(B$(X),C,2):TM$=LEFT$(B$(X),C-1)+"00"+MID$(B$(X),C+2):B$(X)=TM$:PRINT"OK.. YOU GOT IT":W=W+1:GOTO270
1140 N1=0:REM
1150 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXT P
1160 IF N1<>3 THEN1170
1161 PRINT:PRINT"YOU SEE A WEIRD OBJECT IN THE   MIRROR!!!!"
1162 FOR PA=1 TO 900:NEXT PA:PRINT"            YUK! IT'S YOU!":GOTO 270
1170 IF N1=43 THEN PRINT"YOU SEE THREE LEVERS:RED, BLUE AND YELLOW":GOTO 270
1180 FOR P2=1 TO W:IF N1=VAL(V$(P2))  THEN 1200
1190 NEXT P2:PRINT"YOU HAVE TO GET THAT FIRST":GOTO 270
1200 IF N1=4 THEN PRINT"LOOKS LIKE IT HAS BEEN WALKED ONRECENTLY":GOTO 270
1210 IF N1=6 THEN PRINT"MADE IN HONG KONG":GOTO 270
1220 IFN1=29THENPRINT"YOU SEE A WARNING MESSAGE ON THETOP. LOOKS LIKE IT COULD BE     OPENED WITH SOMETHING SMALL!":GOTO270
1230 IF N1=28 THEN PRINT"IT HAS A MESSAGE SMEARED ON IT  WHICH SAYS 'DO NOT OPEN UNTIL   X-MAS!'":GOTO 270
1240 IF N1=9 THEN PRINT"IT LOOKS SOILED":GOTO 270
1250 IF N1=12 THEN PRINT"IT SAYS, 'X' MARKS THE SPOT":GOTO 270
1260 IF N1=13 THEN PRINT"IT IS SOAKED WITH BLOOD":GOTO 270
1270 IF N1=22 THEN PRINT"   HEADLINE:  CRIMINAL ESCAPES  STABS COP;RUNS OFF WITH LOOT!":GOTO270
1280 IF N1=36 THEN PRINT"AUGH!! PUT IT DOWN":GOTO 270
1290 PRINT"YOU SEE NOTHING OF INTEREST":GOTO 270
1300 REM
1310 CLS:PRINT"     COMMANDS"
1320 PRINT"  'DRO' FOR DROP" 
1330 PRINT"  'EXA' FOR EXAMINE"
1340 PRINT"  'MOV' FOR MOVE" 
1350 PRINT"  'COM' FOR COMMANDS"
1360 PRINT"  'OPE' FOR OPEN"
1370 PRINT"  'USE' FOR USING SOMETHING"
1380 PRINT"  'GET' FOR GET"
1390 PRINT"  'INV' FOR INVENTORY"
1400 GOTO 270
1410 REM
1420 CLS:PRINT"-----INVENTORY-----":PRINT
1430 FOR P1=1 TO W
1440 L4=LEN(V$(P1)):FOR L6=1 TO L4 STEP 2:IV$=MID$(V$(P1),L6,2):N=VAL(IV$)
1450 IF N<1 THEN 1470
1460 PRINT"    : ";:GOSUB 460
1470 NEXT L6
1480 NEXT P1
1490 GOTO 270
1500 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXT P
1510 IF N1=31 THEN 2010
1520 IF N1=43 THEN 1920
1530 PRINT"MOVED IT BUT NOTHING HAPPENED":GOTO 270
1540 REM
1550 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXT P
1560 IF N1=11 AND L=26 THEN 2160
1561 IF N1<11 OR N1=12 OR N1=22 OR N1=37 OR N1=15 THEN 1650
1570 CLS:PRINT@194,"THE DOOR BREAKS SETTING OFF A"
1571 PRINT"BOMB THAT BLOWS YOU OUT OF THE  MANSION!! TRY ANOTHER BODY AND  ANOTHER MANSION!":END
1580 REM
1590 B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXT P
1600 FOR P2=1 TO W:IF N1=VAL(V$(P2)) THEN 1620
1610 NEXT P2:PRINT"CANNOT FIND THAT":GOTO 270
1620 IF N1=9 AND L=3 THEN 2120
1630 IF N1=32 AND L=68 THEN 2130
1640 IF N1=37 AND L=3 THEN 2150
1650 PRINT"OK.. NOTHING HAPPENS!":GOTO 270
1660 IF MG$="MIR" THEN N1=3
1670 IF MG$="RUG" THEN N1=4
1680 IF MG$="BUC" THEN N1=6
1690 IF MG$="JAR" THEN N1=7
1700 IF MG$="SHO" THEN N1=9
1710 IF MG$="TRA" THEN N1=11
1720 IF MG$="PAP" THEN N1=12
1730 IF MG$="KNI" THEN N1=13
1740 IF MG$="BON" THEN N1=15
1750 IF MG$="NEW" THEN N1=22
1760 IF MG$="SKE" THEN N1=31
1770 IF MG$="CHE" THEN N1=28
1780 IF MG$="BOX" THEN N1=29
1790 IF MG$="PIC" THEN N1=32
1800 IF MG$="WHO" THEN N1=36
1810 IF MG$="KEY" THEN N1=37
1820 IF MG$="FIL" THEN N1=41
1830 IF MG$="LEV" THEN N1=43
1840 IF MG$="PAN" THEN N1=43
1850 RETURN
1860 N1=1:PRINT"THE DOG WILL TEAR YOU TO SHREDS UNLESS YOU DROP SOMETHING RIGHT NOW!! WHAT WILL YOU DROP?"
1861 INPUT M$:B=LEN(M$):FOR P=1 TO B-2:MG$=MID$(M$,P,3):GOSUB 1660:NEXTP
1870 FOR P2=1 TO W:IF N1=VAL(V$(P2)) THEN 1900
1880 NEXT P2
1890 CLS:PRINT@160,"YOU DON'T HAVE IT!  YOU'RE DEAD!THE CRIMINAL IS PLANNING ON     ATTENDING YOUR FUNERAL!":END
1900 IF N1=15 THEN PRINT"YOU'RE SAVED!":DO=1:B$(X)="2325":GOTO 1000
1910 IF N1<>15 THEN CLS:PRINT@160,"THAT IS THE WRONG THING! THE DOGJUMPS ON YOU AND KILLS YOU!":END
1920 PRINT"THERE ARE THREE LEVERS TO CHOOSEFROM! (RED/BLUE/YELLOW) WHICH   ONE DO YOU WANT?":INPUT LE$
1930 IF LE$="RED" OR LE$="BLUE" OR LE$="YELLOW" THEN 1950
1940 GOTO 1920
1950 CLS(0):FOR PA=1 TO 800:NEXT PA:CLS:PRINT@224,"ALL OF A SUDDEN YOU FIND YOUR-  SELF BEING SUCKED OUT OF THE    ROOM!"
1951 FOR PA=1 TO 2000:NEXT PA 
1960 CLS(0):FOR U1=13 TO 494 STEP 32:CLS(0):PRINT@U1,"AUGH!";:FOR PA=1 TO 40:NEXT PA:CLS(4)
1961 FOR PA=1 TO 50:NEXT PA:NEXT U1
1970 CLS:IFLE$<>"RED"THEN1980
1971 PRINT@224,"YOU FELL OUT OF THE WINDOW AND  CRUSHED YOUR SKULL!":PRINT"GET A NEW BODY AND TRY AGAIN!":END
1980 IFLE$<>"BLUE"THEN1990
1981 PRINT@160,"YOU'RE LUCKY, YOU LANDED IN THE GUESTROOM ON A BED BUT UNFORTU-"
1982 PRINT"NATELY YOU LOST EVERYTHING YOU  WERE CARYING":FOR P2=1 TO W:V$(P2)="00":NEXT P2:L=30:GOTO 270
1990 PRINT@192,"   YOU SEEM TO BE ALL RIGHT!!":FOR PA=1 TO 800 :NEXT PA
1991 PRINT"   OH NO!!! HERE WE GO AGAIN!":FOR PA=1 TO 900:NEXT PA:CLS(6)
1992 FOR Q1=494 TO 13 STEP -32:PRINT@Q1,"AUGH";:FOR PA=1 TO 40:NEXT PA:CLS(0):FOR PA=1 TO 30:NEXT PA:NEXT Q1
2000 L=60:X=23:GOTO 100
2010 PRINT"THE SKELETON ROLLS OVER!":FOR PA=1 TO 750:NEXT PA
2011 PRINT"   THERE IS A KNIFE CLENCHED IN HIS HAND!!":N1=13:B$(X)=B$(X)+" "+STR$(N1):GOTO 270
2020 FOR PA=1 TO 1500:NEXT PA:CLS:PRINT@192,"   YOU HEAR SOMEONE!":FOR PA=1 TO 1200:NEXT PA
2021 PRINT"   YOU'RE LOOKING AROUND!":FOR PA=1 TO 1500:NEXT PA:CLS:PRINT@169,"IT'S HIM!!!";:FOR PA=1 TO 1000:NEXT PA
2030 PRINT:PRINT"ALL OF A SUDDEN THE LIGHTS TURN OFF AND HE JUMPS ON YOU!":FOR PA=1 TO 1600:NEXT PA
2031 CLS(0):FOR PA=1 TO 750:NEXT PA:PRINT@33,"WHAM!";:FOR PA=1 TO 800:NEXT PA
2040 CLS(0):PRINT@58,"THUD!";:FOR PA=1 TO 800:NEXT PA:CLS(0):PRINT@480,"ZAP!!!";:FOR PA=1 TO 800:NEXT PA
2041 CLS(0):PRINT@505,"KAPOW!";:FOR PA=1 TO 800:NEXT PA
2050 CLS(0):FOR PP=13 TO 493 STEP 32:PRINT@PP,"RUN!";:FOR PA=1 TO 150:NEXT PA:CLS(0):FOR PA=1 TO 35:NEXT PA:NEXT PP
2060 CLS:PRINT@192,"YOUR ONLY CHANCE IS TO USE SOME-THING!! WHAT ARE YOU GOING TO   USE";:INPUT U$
2070 B=LEN(U$):FOR P=1 TO B-2:MG$=MID$(U$,P,3):GOSUB 1660:NEXT P
2080 FOR P2=1 TO W:IF N1=VAL(V$(P2)) THEN 2100
2090 NEXT P2:CLS:PRINT"YOU DON'T HAVE THAT!":PRINT"YOU DIED BUT YOU PUT UP A GOOD  FIGHT!":END
2100 IF N1<>13 THEN PRINT"YOU DO NOT HAVE THE RIGHT WEAPONTO DEFEND YOURSELF!":PRINT"YOU DIE!!!":END
2110 CLS:PRINT:PRINT:PRINT"   THE CRIMINAL FALLS BACK, HIS LAST WORDS ARE, 'THEY WILL NEVERFIND IT, IT IS TOO DEEP!"
2111 N1=40:B$(X)=B$(X)+" "+STR$(N1):CR=1:GOTO 270
2120 CLS:PRINT@192,"  YOU DIG AND DIG AND DIG UNTIL FINALLY YOU FIND A CHEST WITH A LOCK ON IT!!":N1=28
2121 B$(X)=B$(X)+" "+STR$(N1):GOTO 270
2130 PRINT:PRINT"  IT WORKED! THE DOOR OPENED!     YOU FIND YOURSELF IN A DEN":L=35:X=21:GOTO270
2140 CLS:PRINT@160," YOU WERE HIT FROM BEHIND AND      KILLED BY THE DANGEROUS CRIM-INAL!"
2141 PRINT"   BEFORE YOU CAN ENTER THAT       ROOM HE MUST BE DEAD!!!!":END
2150 CLS:PRINT@192," THE KEY FITS!! THE CHEST POPS   OPEN REVEALING THE LOST MONEY!  YOU HAVE WON!!":END
2160 CLS:PRINT@128,"   THE TRAP DOOR OPENS":FOR PA=1 TO 900:NEXT PA
2161 PRINT"   YOU FALL INTO A SHOOT AND       SLIDE TO YOUR DEATH INTO        A CEMENT FLOOR!":END
2170 CLS:PRINT@9,"KILLER MANSION";
2180 PRINT@64,"YOU ARE A DETECTIVE IN A LARGE  CITY! A INSANE MAN HAS RECENTLY ROBBED A BANK, KILLING THREE"
2181 PRINT"PEOPLE AND RUNNING OFF WITH ONE MILLION DOLLARS!"
2190 PRINT:PRINT"YOU SUSPECT THAT THE CRIMINAL ISHIDING OUT IN A OLD ABANDONED"
2191       PRINT"MANSION! YOUR MISSION IS TO FINDTHE MONEY WHILE AVOIDING THE"
2192 PRINT"THE CRIMINAL WHO MAY STILL BE   LURKING SOMEWHERE IN THE HOUSE."
2200 PRINT:PRINT"      HIT ANY KEY TO CONT";
2210 IN$=INKEY$:IF IN$="" THEN 2210
2220 CLS:PRINT:PRINT"YOU START OUT IN THE MANSION AND";
2221 PRINT"YOU ROAM AROUND USING DIFFERENT"
2222 PRINT"COMMANDS LIKE 'GET','DROP',ETC."
2223 PRINT"FOR A COMPLETE LIST OF COMMANDS TYPE 'COM' WHEN IT ASKS YOU WHATYOU ARE GOING TO DO."
2230 PRINT:PRINT"YOU MUST FIND CLUES IN ORDER TO SOLVE THE MYSTERY! GOOD LUCK!!"
2240 PRINT@485,"  HIT ANY KEY TO CONT";
2250 IN$=INKEY$:IF IN$="" THEN 2250
2260 RETURN
2270 REM COCO & DRAGON EDITS
2280 REM BY JIM GERRIE
