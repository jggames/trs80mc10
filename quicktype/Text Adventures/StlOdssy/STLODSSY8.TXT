0 CLEAR1376:DIMC1,C2,C3,C4,A,I,M$,Z$,R$(86),P(113):M=RND(-(PEEK(9)*256+PEEK(10))):M=16384:GOTO8000
1 C2=1:C1=22:FORC1=C1TOC2STEP-1:Z$=MID$(M$,C1,1):IFZ$<"!"THENGOSUB8:C2=C1+1:C1=C2+22:IFZ$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:RETURN
4 A$=""
5 B$=MID$(C$,A,1):A=A+1:IFB$<>"*"THENA$=A$+B$:GOTO5
6 V=VAL(A$):RETURN
7 PRINT@502,;:M$="HIT A KEY":GOSUB3:FORC1=0TO1STEP0:C1=-(INKEY$<>""):NEXT:PRINT@502,LEFT$(BL$,9);:GOSUB88:RETURN
8 C3=(PEEK(17024)AND1)*256+PEEK(17025):?MID$(M$,C2,C1-C2);:PRINT@C3+32*-(C3<480),;:RETURN
9 E=TU
10 PRINT@416,LEFT$(BL$,22);:F=M+416
15 FORA=1TO8:GOSUB860:IFPEEK(F)=127THENPOKEF,63:GOTO20
16 POKEF,127
20 FORB=1TO50:A$=INKEY$:IFA$=""THEN25
21 IFA$="T"ORA$="L"THEN340
22 IFA$>="A"ANDA$<"X"THEN30
25 NEXT:NEXT:TU=TU+1:PRINT@154,INT(TU):IFTU>E+3THENRETURN
26 GOTO15
30 C=ASC(A$)-64:IFV$(C)=""THEN25
31 PRINT@416,V$(C);:X=C:SOUND60,1:SOUND90,1:ONCGOTO35,35,415,35,35,350,35,9,403,9,35,9,35,9,9,35,9,400,332,9,9,410,330
35 F=M+416+10:B$=""
40 FORA=1TO7:GOSUB860:IFPEEK(F)=127THENPOKEF,63:GOTO45
41 POKEF,127
45 FORB=1TO25:GOSUB862:A$=INKEY$:IFA$=""THEN60
46 C=ASC(A$)
50 IFC=13THEN70
51 IFC=8THENGOSUB870:GOTO60
53 IFC<32THEN60
55 IFC-64<27THEN65
60 NEXT:NEXT:TU=TU+1:PRINT@154,INT(TU):IFTU>E+3THENRETURN
61 GOTO40
65 IFLEN(B$)>15THENSOUND70,1:GOTO60
66 B$=B$+A$:POKEF,CAND63:SOUND100,1:F=F+1:POKEF,127:GOTO60
70 POKEF,128:IFB$=""THEN72
71 A=ASC(LEFT$(B$,1))-64
72 ONXGOTO100,420,100,150,100,100,130,100,100,100,445,100,426,100,100,435,100,100,100,100,100,100,330

REM IN ELEVATOR?
75 GOSUB88:IFQ=22ANDP=99THENPRINT@448,;:M$="IN AN ELEVATOR SHAFT. YOU SEE BUTTONS.":GOSUB1:RETURN
77 C$=A$(Q):A=VAL(LEFT$(C$,2)):IFA=0THENPRINT@448,"YOU'RE IN A CORRIDOR";:A=-2:GOTO81
79 PRINT@448,;:M$=MID$(C$,4,A):GOSUB1

REM DISPLAY ROOM ITEMS
81 IFF>0THENRETURN
82 B=342:GOSUB89
83 FORD=A+5TO255:A$=MID$(C$,D,1):IFA$="#"THENRETURN
84 PRINT@B,Y$(ASC(A$)-64);:B=B+32
85 IFB>511THENB=342
86 NEXTD:RETURN

88 FORC3=416TO480STEP32:PRINT@C3,LEFT$(BL$,22);:NEXT:RETURN
89 FORC3=342TO470STEP32:PRINT@C3,LEFT$(BL$,10);:NEXT:PRINT@502,LEFT$(BL$,9);:POKE16895,128:RETURN

REM EXAMINE
100 IFB$=""THENF=0:GOTO75
101 A=ASC(LEFT$(B$,1))-64:A$=LEFT$(B$,1):IFA>22ORA<0THENA=22
105 IFB$<>Y$(A)THEN118
110 C=VAL(LEFT$(A$(Q),2))+3:FORB=CTO255:C$=MID$(A$(Q),B,1):IFC$="#"THEN115
111 IFC$=A$THEN120
112 NEXTB
115 FORB=1TOLEN(I$):IFMID$(I$,B,1)=A$THEN120
116 NEXTB
118 GOSUB88:PRINT@448,"YOU CAN'T";:RETURN
120 GOSUB88:PRINT@448,;:M$=O$(A)+".":GOSUB1:RETURN

REM GET
130 GOSUB88:IFB$=""ORA<1THENPRINT@448,"GET WHAT?";:RETURN
131 IFY$(A)<>B$THENPRINT@448,;:M$="I CAN'T GET "+B$:GOSUB1:RETURN
132 IFLEN(I$)>5THENPRINT@448,"ARMS FULL!";:RETURN
134 ONAGOTO135,138,135,135,135,138,138,138,135,135,135,135,135,138,138,138,135,135,138,135,138,135,138
135 PRINT@448,"IF ONLY I COULD.";:RETURN
138 A$=LEFT$(B$,1):C=VAL(LEFT$(A$(Q),2))+3:FORB=CTO99:C$=MID$(A$(Q),B,1):IFC$="#"THEN141
139 IFC$=A$THEN142
140 NEXTB
141 PRINT@448,;:M$="THERE IS NO "+B$+" HERE.":GOSUB1:RETURN
142 A$(Q)=LEFT$(A$(Q),B-1)+RIGHT$(A$(Q),LEN(A$(Q))-B):IFA$="B"THEN900
143 IFA$="O"THEN910
144 I$=I$+A$:PRINT@448,"GOT IT!";
145 F=0:C$=A$(Q):A=VAL(LEFT$(C$,2)):IFA=0THENA=-2
146 GOTO81

REM DROP
150 GOSUB88:IFB$=""ORA<1THENPRINT@448,"DROP WHAT?";:RETURN
151 IFY$(A)<>B$THENPRINT@448,;:M$="I CAN'T DROP "+B$:GOSUB1:RETURN
152 A$=LEFT$(B$,1):FORB=1TOLEN(I$):IFMID$(I$,B,1)=A$THEN155
153 NEXTB:PRINT@448,;:M$="YOU'RE NOT CARRYING IT.":GOSUB1:RETURN
155 A$(Q)=LEFT$(A$(Q),LEN(A$(Q))-1)+A$+"#":I$=LEFT$(I$,B-1)+RIGHT$(I$,LEN(I$)-B):PRINT@448,"DROPPED IT!";
156 IFVAL(LEFT$(A$(Q),2))=0THENA=-2
157 RETURN

REM SETUP SCREEN
160 CLS0:PRINT@0,H$;:DD=3*N:A1=0
162 ?@118,"DIR":?@150,"TIME":?@181,"ÐPISTOL":?@214,"WOUNDS":?@245,"ÐTIRED":?@309,"Ð* ITEMS *"
164 GOSUB838:GOSUB800:GOSUB345:F=1:GOSUB75

REM ALIEN ATTACK
170 IFQ<18THENAI=0:GOTO200
171 IFAI=1THEN180
172 IFRND(DD)<>1THEN200
173 AP=(RND(7)-1)*3+(RND(5)-1)*64+96:IFPEEK(AP+M)<>128THEN200
176 PRINT@AP+1,"Á€";:F=100:GOSUB830:PRINT@AP+1,"ÁÂ";:F=85:GOSUB830:PRINT@AP+1,"ÇÂ";
177 PRINT@AP+33,"Ä€";:F=55:GOSUB830:FORB=1TO10:PRINT@AP,"ÇÏË";:PRINT@AP+32,"ÄÌÈ";:SOUND4,1
178 GOSUB832:SOUND10,1:NEXT:F=20:GOSUB830:AI=1:GOTO200

REM ALIEN ATTACKING
180 IFRND(4+N)=1THENGOSUB880:GOSUB832:SOUND1,2:FA=FA+4:WO=WO+INT(FA/4):GOTO200
182 A=P-AP:IFA>.THEN188
183 B=-64:IFA>-61THENB=-3
184 GOTO189
188 B=64:IFA<61THENB=3
189 C1=PEEK(AP+B+M):IFC1<176THEN200
190 GOSUB832:GOSUB801:SOUND200,1:GOSUB801:PRINT@AP,"€€€";:PRINT@AP+32,"€€€";:AP=AP+B
192 GOSUB832:SOUND100,1:IFHE=1ORHE=3THENGOSUB805:GOTO195
194 GOSUB820
195 IFAP<>PTHEN200
197 FORC3=1TO10:PRINT@P,G$(RND(16)+4);:SOUNDRND(150),1:PRINT@P+32,G$(RND(16)+4);
198 SOUNDRND(150),1:NEXT:B=0-B:WO=WO+(6-N):FA=FA+(6-N):GOTO189

REM ROOM DETAILS AND END OF MAIN LOOP
200 GOSUB838:IFQ=7ANDD2=1THENPRINT@105,G$(19);:PRINT@137,G$(20);:GOTO205
201 IFQ=10ANDD1=1THENPOKEM+224,165:POKEM+256,165:GOTO205
202 IFQ=38ANDP=236THENQ=11:P=364:GOSUB800:GOSUB452
205 IFWO>99THENWO=100:GOSUB838:GOSUB840:CLS:PRINT"YOU'VE DIED FROM SEVERE WOUNDS.":GOTO855
206 IFQ=74THEN700
210 IFQ=64ANDP-INT(P/32)*32<12THENDD=1*N:GOTO214
211 IFQ=75THENDD=1*N:GOTO214
212 IFQ>57THENDD=2*N:GOTO214
213 DD=3*N
214 IFQ>17ANDPE=0ANDRND(6)=1ANDTR=0THENA$(Q)=LEFT$(A$(Q),LEN(A$(Q))-1)+"B#":F=0:C$=A$(Q):A=-2:TR=1:GOSUB81
215 IFTU>999THENPRINT@448,"OUT OF TIME!";:FORT=150TO80STEP-3:GOSUB880:SOUNDT,1:NEXT:CLS:GOTO858
216 IFQ<>74ORI$=""THEN220
218 FORT=1TOLEN(I$):IFMID$(I$,T,1)="S"THENSB=1
219 NEXTT
220 GOSUB9:GOTO170

REM WALK
300 IFFA>90THENGOSUB88:PRINT@448,"TOO TIRED";:RETURN
301 ONHEGOTO302,325,306,320

REM UP
302 IFP<128THENGOSUB815:Q=Q-1:P=P+256:GOSUB801:GOSUB800:F=1:AI=0:GOSUB75
303 GOSUB805:IFPEEK(P+M-64)<>128THENRETURN
304 PRINT@P+32,"€€";:PRINT@P-32,G$(11);:PRINT@P,G$(12);:GOSUB801
305 PRINT@P,"€€";:PRINT@P-64,G$(9);:PRINT@P-32,G$(10);:GOSUB801:P=P-64:RETURN

REM DOWN
306 IFP<320THEN312
307 GOSUB815:Q=Q+1:IFQ=12THENQ=38:P=236:GOTO311
308 P=P-256
311 GOSUB801:GOSUB800:F=1:AI=0:GOSUB75
312 GOSUB805:IFPEEK(P+M+64)<>128THENRETURN
313 PRINT@P,"€€";:PRINT@P+32,G$(15);:PRINT@P+64,G$(16);:GOSUB801
314 PRINT@P+32,"€€";:PRINT@P+64,G$(13);:PRINT@P+96,G$(14);:GOSUB801:P=P+64:RETURN

REM LEFT
320 IFP-INT(P/32)*32<2THENGOSUB810:Q=Q-A:P=P+18:GOSUB801:GOSUB800:F=1:AI=0:GOSUB75
321 GOSUB820:IFPEEK(P+M-3)<>128THENRETURN
322 PRINT@P+1,"€";:PRINT@P+33,"€";:PRINT@P-1,G$(7);:PRINT@P+31,G$(8);:GOSUB801
323 PRINT@P-1,"€€";:PRINT@P+31,"€€";:PRINT@P-3,G$(5);:PRINT@P+29,G$(6);:GOSUB801:P=P-3:RETURN

REM RIGHT
325 IFP-INT(P/32)*32>17THENGOSUB810:Q=Q+A:P=P-18:GOSUB801:GOSUB800:F=1:AI=0:GOSUB75
326 GOSUB820:IFPEEK(P+M+3)<>128THENRETURN
327 PRINT@P,"€";:PRINT@P+32,"€";:PRINT@P+1,G$(3);:PRINT@P+33,G$(4);:GOSUB801
328 PRINT@P+1,"€€";:PRINT@P+33,"€€";:PRINT@P+3,G$(1);:PRINT@P+35,G$(2);:GOSUB801:P=P+3:RETURN

REM WALK AND FATIGUE
330 E=P:GOSUB300:IFE<>PTHENTU=TU+1:GOSUB838:FA=FA+INT((WO+2)/100*20):GOSUB838
331 RETURN

REM SPRINT
332 FA=FA+3:GOSUB330:GOSUB330:RETURN

REM TURN
340 IFA$="T"THENHE=HE+1:GOTO342
341 HE=HE-1:IFHE<1THENHE=4:GOTO345
342 IFHE>4THENHE=1
345 SOUND99,1:SOUND50,1:PRINT@123,;:IFHE=1THENPRINT"NORTH";:GOTO805
346 IFHE=3THENPRINT"SOUTH";:GOTO805
347 IFHE=2THENPRINT"EAST"
348 IFHE=4THENPRINT"WEST"
349 GOTO820

REM FIRE PISTOL
350 GOSUB88:FORC1=1TOLEN(I$):IFMID$(I$,C1,1)="P"THENC1=255:NEXT:GOTO355
351 NEXT:PRINT@448,"NO PISTOL!";:RETURN
355 IFPE=0THENPRINT@448,"NO CHARGE!";:SOUND255,1:RETURN
356 ONHEGOTO362,370,366,375

REM FIRE UP
362 GOSUB378:FF=P+M+1
363 FF=FF-32:IFFF<M+96THEN392
364 IFPEEK(FF)<>128THEN380
365 POKEFF,193:SOUND6,1:POKEFF,196:SOUND4,1:POKEFF,128:SOUND2,1:GOTO363

REM FIRE DOWN
366 GOSUB378:FF=P+M
367 FF=FF+64:IFFF>M+416THEN392
368 IFPEEK(FF)<>128THEN380
369 POKEFF,200:SOUND6,1:POKEFF,194:SOUND4,1:POKEFF,128:SOUND2,1:GOTO367

REM FIRE RIGHT
370 GOSUB378:A=P+3+M:FORFF=ATOINT(A/32)*32+20:IFPEEK(FF)<>128THEN380
371 POKEFF,194:SOUND5,1:POKEFF,128:SOUND2,1:NEXTFF:GOTO392

REM FIRE LEFT
375 GOSUB378:A=P-1+M:FORFF=ATOINT(A/32)*32STEP-1:IFPEEK(FF)<>128THEN380
376 POKEFF,193:SOUND5,1:POKEFF,128:SOUND2,1:NEXTFF:GOTO392

378 FORB=15TO120STEP10:SOUNDB,1:NEXTB:RETURN

380 C1=PEEK(FF):IFC1=175ORC1<128THENFORA=161TO175:POKEFF,A:SOUNDRND(50),1:NEXT:GOTO392
381 IFQ=7THENAP=105:A$(7)=LEFT$(A$(7),30)+RIGHT$(A$(7),LEN(A$(7))-31):GOSUB89
382 FORA=1TO10:PRINT@AP,"´³¶";:PRINT@AP+32,"±¼¹";:SOUNDRND(100)+70,1:PRINT@AP,"°²²";
383 PRINT@AP+32,"€¶²";:SOUNDRND(100)+70,1:NEXT:PRINT@AP,"€€€";:PRINT@AP+32,"€€€";
384 IFQ<18THEND2=0:GOTO388
385 AI=0:IFWO>80THENC=1:GOTO388
386 IFPE<11THENC=2:GOTO388
387 C=RND(6)
388 IFC=1THENA$="O":GOTO391
389 IFC=2THENA$="B":GOTO391
390 GOTO392
391 A$(Q)=LEFT$(A$(Q),LEN(A$(Q))-1)+A$+"#":F=0:GOSUB75

392 PE=PE-10:IFPE<0THENPE=0
396 GOTO839

400 FA=FA-10+INT(WO/15):IFFA<0THENFA=0:GOTO402
401 IFFA<90THENGOSUB88
402 GOSUB838:RETURN

REM INVENTORY
403 GOSUB88:IFLEN(I$)=0THENRETURN
404 C$="":FORA=1TOLEN(I$):C=ASC(MID$(I$,A,1))-64:C$=C$+Y$(C)+", "
405 NEXT:M$=LEFT$(C$,LEN(C$)-2):PRINT@416,;:GOSUB1:GOSUB7:RETURN

REM VERB VOCABULARY
410 PRINT@416,"bOOT,eXAM,kEY,iNV,gET ";:PRINT@448,"rEST,wALK,sP,tURN,pULL";
411 PRINT@480,"fIRE,dROP,mOVE,cONVERS";:GOSUB7:RETURN

REM CONVERSE
415 IFQ<>28THENRETURN
416 GOSUB88:PRINT@416,;:M$="A VOICE SAYS 'THE ALIENS CAPTURED ME. DESTROY THIS VESSEL!":GOSUB1:GOSUB7
417 PRINT@416,;:M$="BY 999 TIME UNITS, OR ELSE THE EARTH IS DOOMED!' PULL A":GOSUB1:GOSUB7
418 PRINT@416,;:M$="REACTOR ROD. YOU'LL HAVE 10 SECONDS TO ESCAPE.":GOSUB1:GOSUB7:RETURN


REM BOOT
420 GOSUB88:IFQ<>2THENPRINT@448,;:M$="YOU CAN'T BOOT THAT HERE.":GOSUB1:RETURN
421 IFB$<>"FLOPPY"THENPRINT@448,"IN WHAT?";:RETURN
422 FORA=1TOLEN(I$):IFMID$(I$,A,1)="F"THENI$=LEFT$(I$,A-1)+RIGHT$(I$,LEN(I$)-A):GOTO425
423 NEXTA:A$="F":GOTO152
425 PRINT@448,:M$="COMPUTER: 'RED ALERT ION DRIVE DISLODGED'":GOSUB1:RETURN

REM MOVE
426 GOSUB88:IFQ<>11ORB$<>"ION DRIVE"THENPRINT@448,"IT WON'T BUDGE.";:RETURN
427 SOUND1,4:SOUND15,4:PRINT@448,;:M$="DEBRIS FALLS TO REVEAL A HOLE!":GOSUB1:GOSUB9199:GOSUB800
432 IFHE=1ORHE=3THEN805
433 GOTO820

REM PULL
435 GOSUB88:IFQ<>74ORB$<>"CARBON ROD"THENPRINT@448,"OOMPH!...WON'T BUDGE.";:RETURN
436 RB=1:RETURN

REM KEY
445 GOSUB88:IFB$="8661"ANDQ=10THEND1=0:A$(10)=LEFT$(A$(10),2)+RIGHT$(A$(10),LEN(A$(10))-3):POKEM+224,128:POKEM+256,128:GOTO452
446 IFB$="Z80"ANDQ=22ANDP=99THENFORA=1TO5:SOUND250,1:SOUND50,1:NEXT:Q=62:AI=0:GOSUB800:GOTO448
447 GOTO449
448 PRINT@448,;:M$="THE ELEVATOR LOWERS YOU TO THE NEXT LEVEL":GOSUB1:GOTO452
449 IF(Q=22ANDP=99)ORQ=10THENPRINT@448,"NO RESPONSE.";:GOTO452
450 PRINT@448,"NO BUTTONS HERE.";
452 IFHE=1ORHE=3THEN805
453 GOTO820

REM SAVE GAME
640 CLS:PRINT"sAVE, lOAD OR rESTART GAME":FORC1=1TO1000:A$=INKEY$:IFA$="S"THENC1=1000
641 IFA$="L"THENGOSUB9010:GOTO160
642 IFA$="R"THENRUN
643 NEXT:IFA$<>"S"THEN160
645 CLS:PRINT"CREATING GAME FILE":A$="*":C$="":FORA=1TO85:PRINT@24,A:IFA$(A)=""THEN664
646 B=VAL(LEFT$(A$(A),2)):IFB=0THENB=3:GOTO661
647 B=B+5
661 B$=MID$(A$(A),B):IFB$<>"#"THENC$=C$+MID$(STR$(A),2)+A$+LEFT$(B$,LEN(B$)-1)+A$
664 NEXT:C$=I$+A$+C$+"#"+A$:GOSUB9000:CLS:PRINT"PRESS enter WHEN CASSETTE IS","READY"
666 ON-(INKEY$<>CHR$(13))GOTO666:CSAVE*P,"GAMEFILE":GOTO160

700 FORA=1TOLEN(I$):IFMID$(I$,A,1)="U"THEN210
701 NEXTA:GOSUB840:CLS:PRINT"YOU'VE DIED OF RADIATION","POISONING!":GOTO855

REM DRAW ROOMS
800 GOSUB89:GOSUB9050:TR=0:RETURN

801 IFQW=0THENSOUND7,1:QW=1:GOTO803
802 SOUND8,1:QW=0
803 FORC3=1TO25:NEXT:RETURN

805 IFHE=1THENPRINT@P,G$(9);:PRINT@P+32,G$(10);:RETURN
808 PRINT@P,G$(13);:PRINT@P+32,G$(14);:RETURN

810 IFQ<18THENA=6:GOTO815
811 IFQ>57THENA=7:GOTO815
812 A=8
815 PRINT@P,"€€";:PRINT@P+32,"€€";:RETURN

820 IFHE=4THENPRINT@P,G$(5);:PRINT@P+32,G$(6);:RETURN
821 PRINT@P,G$(1);:PRINT@P+32,G$(2);:RETURN

830 FORC3=FTOF+100STEP20:SOUNDC3,1:NEXT:RETURN
832 PRINT@AP,G$(19);:PRINT@AP+32,G$(20);:RETURN

REM STATS
838 IFFA>100THENFA=100
839 ?@154,INT(TU):?@188,LEFT$(STR$(PE)+"  ",4);:?@220,LEFT$(STR$(WO)+"  ",4);:?@252,LEFT$(STR$(FA)+"  ",4);:RETURN

840 FORT=1TO200STEP5:HE=RND(4):GOSUB432:GOSUB880:SOUNDT,1:NEXT:RETURN

REM BEACON
841 PRINT@96,;:M$="BEACON TRANSMITTING"+LEFT$(STR$(BT)+" ",3):GOSUB3:SOUND30,1:SOUND60,1:BT=BT-1:IFBT>=0THENRETURN
842 FORT=1TO50:GOSUB452:SOUND30,1:GOSUB815:SOUND90,1:NEXT:Q=0:IFRB=1THENFORD=1TO99:GOSUB845:FORC2=1TO250:NEXT:NEXTD
844 GOTO851

845 PRINT@0,BL$BL$BL$;:PRINT@38,"reactor critical"RT;:GOSUB880:FORC2=1TO250:NEXT
846 SOUND1,1:RT=RT-1:PRINT@38,"REACTOR CRITICAL";:IFRT>=0THENRETURN
850 FORT=1TO50:GOSUB880:SOUND200,1:NEXT:FORT=1TO20:CLSRND(8):SOUND1,1:NEXT:CLS0

851 FORT=20TO245STEP2:SOUNDT,1:SOUNDT+10,1:NEXT:CLS
852 ON-(RT>1)GOTO857:PRINT"CONGRATULATIONS! YOU'VE SAVED","THE EARTH FROM THE ALIENS"
853 IFQ=0THENPRINT"AND SUCCESSFULLY ESCAPED.":GOTO855
854 PRINT"BUT WERE KILLED IN THE BLAST.","WHERE'S YOUR SIGNAL BEACON?"

855 A$=INKEY$:FORT=1TO2500:A$=INKEY$:IFA$=""THENNEXT
856 RUN

857 PRINT"YOU'VE SAVED YOUR OWN SKIN,","BUT THE EARTH HAS BEEN INVADED."
858 PRINT"YOU HAVE FAILED. (DIFFICULTY"STR$(4-N)")":GOTO855

860 IFSB=1THENGOSUB841
861 IFRB=1THENGOSUB845
862 IFPEEK(2)THENIF4ANDNOTPEEK(16946)THEN640
863 RETURN

REM BACKSPACE
870 SOUND200,1:IFB$=""THENRETURN
871 B$=LEFT$(B$,LEN(B$)-1):POKEF,128:F=F-1:POKEF,63:RETURN

REM FLASH SCREEN
880 POKE49151,64:FORC3=1TO50:NEXT:RETURN

REM CHARGE PISTOL
900 F=0:GOSUB75:GOSUB88:PRINT@448,"PISTOL FULLY CHARGED!";:PE=100:GOTO838

REM OINTMENT
910 F=0:GOSUB75:GOSUB88:PRINT@448,"AHHH...THAT'S BETTER.";:WO=WO-30:IFWO<0THENWO=0
911 GOTO838

REM INIT
8000 CLS0:BL$="€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€"
8035 H$="•œŸŸœœœžž™€€–™•™•••œ•œ•œ••”€š•’•€•€•—•–€™€š•••”””•˜”“—€š•“•“•“••••€™—›—›—›—Ÿ—Ÿ—Ÿ—˜€"
8044 B=16*RND(5):A=M+96:PRINT@96,BL$H$BL$;:D=SQR(2):PRINT@356,;:M$="CHOOSE DIFFICULTY (1-3)":GOSUB3
8045 M$=INKEY$:IFM$>"0"ANDM$<"4"THEN8052
8046 POKEA,PEEK(A)+B:A=A+1:IFA<M+256THEN8045
8047 A=M+96
8049 M$=INKEY$:IFM$>"0"ANDM$<"4"THEN8052
8050 POKEA,PEEK(A)-B:A=A+1:IFA<M+256THEN8049
8051 GOTO8044
8052 PRINT@96,BL$H$BL$;:FORT=60TO255STEP10:SOUNDT,1:NEXT:N=4-VAL(M$)
8053 CLS:PRINT"PLEASE WAIT":DIMG$(20),A$(86),O$(22),V$(23),Y$(22):GOSUB9100
8054 G$(1)="ÕÓ":G$(3)="ÕÓ":G$(13)="ÑÛ":G$(15)="×Ò":G$(5)="ÓÚ":G$(7)="ÓÚ":G$(9)="×Ö":G$(11)="ÙÛ"
8055 G$(2)="ÖÒ":G$(4)="Ý€":G$(14)="ÙÙ":G$(16)="ÖÖ":G$(6)="ÑÙ":G$(8)="€Þ":G$(10)="ÖÒ":G$(12)="ÑÙ"
8056 G$(17)="×Ö€":G$(18)="ÖÒ€":G$(19)="îîê":G$(20)="çíâ":GOSUB8066:GOSUB8090:GOTO160

8058 A$(1)="26#IN A DECOMPRESSION CHAMBER #":A$(2)="20#IN THE COMPUTER ROOM #":A$(4)="00#":A$(5)="00#"
8059 A$(6)="26#IN THE FLIGHT CONTROL ROOM #":A$(7)="26#AT THE NORTH END OF A HALL #":A$(8)="23#IN THE CENTRE OF A HALL #"
8060 A$(9)="26#AT THE SOUTH END OF A HALL #":A$(10)="00#":A$(11)="18#IN THE ENGINE ROOM #":A$(13)="00#"
8061 A$(14)="24#IN THE CRYOGENIC CHAMBER #":A$(16)="14#IN A STOREROOM #":A$(17)="00#":FORT=18TO85:A$(T)="00#":NEXT
8062 A$(60)="17#IN A SCIENCE ROOM #":A$(63)="24#AT THE WORKER'S QUARTERS #":A$(64)="22#YOU HEAR A NOISE AHEAD #"
8063 A$(74)="19#IN THE REACTOR ROOM #":A$(75)="27#IN THE CENTRAL CONTROL ROOM #"
8064 A$(76)="39#IN A CORRIDOR. THERE IS RED LIGHT NORTH #":A$(77)="36#AT A CROSS PASSAGE. YOU HEAR HUMMING #"
8065 A$(28)="16#YOU HEAR A VOICE #":A$(33)="00#":A$(85)="00#":RETURN

8066 O$(1)="YUCK! KING KONG LOOKED BETTER":O$(2)="IT FITS YOUR PISTOL":O$(3)="IT'S A TRS-80 WITH DISK DRIVES!"
8067 O$(5)="IT'S WELDED SHUT":O$(6)="WELL?...IT'S SQUARE":O$(7)="IT DOESN'T WORK":O$(9)="IT'S SEVERELY DAMAGED"
8068 O$(12)="IT HAS BUTTONS":O$(14)="IT READS 'Z80'":O$(16)="AN L-38 STRATOBLASTER WITH THE #8661 ON IT"
8069 O$(19)="USED TO BEAM YOURSELF TO A RESCUE CRAFT":O$(18)="A CARBON ROD IS IN A SLOT"
8070 O$(20)="1ST EMPTY, 2ND SKELETON, 3RD EMPTY":O$(15)="IT'S FOR HEALING WOUNDS"
8071 O$(21)="IT'S MADE WITH LEAD FIBRES":O$(22)="YOU SEE YOUR SHIP IN A LARGE HANGAR BAY"

8072 V$(2)="boot":V$(3)="converse":V$(4)="drop":V$(5)="examine":V$(6)="fire":V$(7)="get":V$(9)="inventory"
8073 V$(11)="key":V$(13)="move":V$(16)="pull":V$(18)="rest":V$(19)="sprint":V$(22)="verbs"
8074 V$(23)="walk":Y$(1)="ALIEN":Y$(2)="BOOSTER":Y$(3)="COMPUTER":Y$(5)="ESC HATCH"
8075 Y$(6)="FLOPPY":Y$(7)="GEIGER-C":Y$(9)="ION DRIVE":Y$(12)="LOCK+DOOR":Y$(14)="NOTE":Y$(15)="OINTMENT"
8076 Y$(16)="PISTOL":Y$(18)="REACTOR":Y$(19)="SIGNAL":Y$(20)="TUBES":Y$(21)="UNIFORM":Y$(22)="VIEWPORT":RETURN

8090 GOSUB9030
8095 GOSUB8058:A=1:GOSUB4:I$=A$:B=1:IFQ>17THENGOSUB9199
8096 GOSUB4:IFA$="#"THENRETURN
8097 B=V:GOSUB4:A$(B)=LEFT$(A$(B),LEN(A$(B))-1)+A$+"#":GOTO8096

9000 P(0)=P:P(1)=Q:P(2)=HE:P(3)=TU:P(4)=PE:P(5)=WO:P(6)=FA:P(7)=D1:P(8)=D2:P(9)=BB:P(10)=SB:P(11)=BT
9001 P(12)=RB:P(13)=RT:P(14)=AP:P(15)=AI:FORC1=1TOLEN(C$):P(15+C1)=ASC(MID$(C$,C1,1)):NEXT:RETURN

REM LOAD GAME FILE
9010 PRINT"LOADING GAMEFILE":PRINT"PRESS PLAY":CLOAD*P,"GAMEFILE"
9011 P=P(0):Q=P(1):HE=P(2):TU=P(3):PE=P(4):WO=P(5):FA=P(6):D1=P(7):D2=P(8):BB=P(9):SB=P(10):BT=P(11)
9012 RB=P(12):RT=P(13):AP=P(14):AI=P(15):C$=""
9020 FORC1=16TO113:IFP(C1)=35THENC$=C$+"#*":C1=113:GOTO9022
9021 C$=C$+CHR$(P(C1))
9022 NEXT:GOSUB8095:RETURN

9030 P=233:Q=14:HE=4:TU=0:PE=0:WO=0:FA=0:D1=1:D2=1:BB=1:SB=0:BT=30*N:RB=0:RT=10:AP=236:AI=0
9033 C$="P*1*S*2*C*6*VF*7*A*10*L*11*I*13*E*14*T*16*B*33*N*60*G*63*U*74*R*#*":RETURN

9050 FORC2=.TO4:A=C2*64+93:C1=ASC(MID$(R$(Q),C2+1,1))
9051 C3=128:FORI=.TO7:C4=INT(C1/C3):IFC4>.THENIFI>.THENA=A+3:PRINT@A,"¯¯¯";:PRINT@A+32,"¯¯¯";:GOTO9053
9052 IFI>.THENA=A+3:PRINT@A,"€€€";:PRINT@A+32,"€€€";
9053 C1=C1-C4*C3:C3=C3/2:NEXT:NEXT:RETURN

9100 W2$=CHR$(125):W3$=CHR$(95):R$(0)="ÿÿÿÿÿ":R$(1)="ÿqpqÿ":R$(2)="ÿA@Aÿ":R$(3)=R$(0):R$(4)="ÿÿxyy":R$(5)="yxxxÿ"
9107 R$(6)="ÿAAAw":R$(7)="w@"+CHR$(0)+"@A":R$(8)="AA"+CHR$(0)+"AA":R$(9)="AAAAw":R$(10)="wA"+CHR$(1)+"Aÿ"
9108 R$(11)="ÿ"+CHR$(0)+CHR$(0)+CHR$(0)+"ÿ":R$(12)=R$(0):R$(13)="ÿ"+CHR$(1)+CHR$(1)+CHR$(1)+"ÿ":R$(14)="ÿA"+CHR$(1)+"Aÿ"
9116 R$(15)=R$(0):R$(16)="ÿÿOO"+W3$:R$(17)=W3$+CHR$(15)+CHR$(15)+CHR$(15)+"ÿ"

9118 R$(18)="ÿBvB"+W3$:R$(19)=W3$+"BzB"+W3$:R$(20)=W3$+"A"+W2$+"A"+CHR$(93)
9121 R$(21)=CHR$(93)+"QWTU":R$(22)="UuG"+CHR$(92)+"Q":R$(23)="Q"+W3$+"QUU":R$(24)="UTWPU"
9125 R$(25)="U"+CHR$(93)+"EQÿ":R$(26)="ÿ"+CHR$(0)+"<"+CHR$(1)+"i":R$(27)="i(/"+CHR$(1)+CHR$(123)
9128 R$(28)=CHR$(123)+"IIIÿ":R$(29)="ÿTE"+CHR$(29)+"U":R$(30)="UUU"+CHR$(20)+"W"
9131 R$(31)="WGwP"+W3$:R$(32)=W3$+CHR$(16)+"v"+CHR$(4)+W2$:R$(33)=W2$+"@"+W3$+"@ÿ"
9134 R$(34)="ÿ"+CHR$(0)+CHR$(21)+"wA":R$(35)="A"+CHR$(20)+"WDÿ":R$(36)="ÿckho":R$(37)="o"+CHR$(0)+"ÿÿÿ":
9139 R$(38)="ÿÿ"+CHR$(123)+CHR$(0)+"ÿ":R$(39)="ÿDU"+CHR$(17)+"ÿ":R$(40)="ÿ"+CHR$(1)+"=%m":R$(41)="m-a$ÿ"
9142 R$(42)="ÿ"+CHR$(1)+W2$+"@"+CHR$(91):R$(43)=CHR$(91)+CHR$(16)+"ÿ"+CHR$(0)+W3$
9144 R$(44)=W3$+"P"+W3$+CHR$(0)+"w":R$(45)="w"+CHR$(17)+CHR$(93)+"QU":R$(46)="UTU"+CHR$(16)+"w"
9147 R$(47)="w"+CHR$(16)+"WD"+W2$:R$(48)=W2$+"@"+W3$+"QW":R$(49)="WWP"+CHR$(31)+"ÿ"

9150 R$(50)="ÿQW"+CHR$(23)+"Q":R$(51)="Q"+CHR$(5)+W2$+CHR$(1)+W2$
9151 R$(52)=W2$+CHR$(1)+W2$+CHR$(17)+"U":R$(53)="UGQ"+CHR$(93)+"Q":R$(54)="Q"+CHR$(23)+"q"+CHR$(29)+"Q"
9153 R$(55)="Q"+CHR$(23)+"u%m":R$(56)="m"+CHR$(1)+"WQ"+CHR$(93):R$(57)=CHR$(93)+"I"+CHR$(9)+"Iÿ"
9157 R$(58)="ÿ@"+W3$+W3$+W3$:R$(59)=W3$+"@ÿÿÿ":R$(60)="ÿqqpÿ":R$(61)="ÿ@"+W3$+W3$+W3$:R$(62)=W3$+"ÿ@Gÿ":R$(63)="ÿO@Oÿ"
9164 R$(64)="ÿAEDÿ":R$(65)="ÿ"+CHR$(0)+"ÿ"+CHR$(96)+"o":R$(66)="o"+CHR$(1)+"mmm"
9168 R$(67)="meu"+CHR$(5)+"u":R$(68)="u"+CHR$(21)+"UUU":R$(69)="UU"+CHR$(21)+"E"+W2$:R$(70)=W2$+W2$+CHR$(1)+"lo"
9171 R$(71)="o"+CHR$(96)+CHR$(123)+CHR$(3)+"ÿ":R$(72)="ÿ"+CHR$(1)+W2$+CHR$(1)+"m":R$(73)="mmlÿÿ":R$(74)="ÿAAAw"
9173 R$(75)="wAAAA":R$(76)="Awwww":R$(77)="www"+CHR$(0)+"w":R$(78)="w"+CHR$(3)+"ÿÿÿ":R$(79)="ÿAA"+W2$+W2$
9180 R$(80)=W2$+W2$+CHR$(1)+"kk":R$(81)="kkkkk":R$(82)="kkkoo":R$(83)="ooooo":R$(84)="ooo"+CHR$(1)+W2$
9185 R$(85)=W2$+"A"+W3$+"Aÿ":R$(86)=CHR$(5)+"uu"+CHR$(5)+"/":W2$="":W3$="":RETURN
9199 R$(11)=LEFT$(R$(11),4)+CHR$(123):RETURN
