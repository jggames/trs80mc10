0 GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINT"WHAT DO YOU WANT TO DO?":RETURN
4 GOSUB1:I=1:RETURN
5 FORZZ=1TO2500:NEXTZZ:RETURN
6 PRINTTAB((32-LEN(M$))/2);
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-16384+ZZ,:RETURN
8 PRINT:M$="-HIT A KEY-":GOSUB6:FORCC=0TO1STEP0:CC=-(INKEY$<>""):ZZ=RND(0):NEXT:CLS:RETURN
9 M$=A$:GOSUB1:GOSUB5:RETURN
10 CLS0:POKE49151,64:CLS:PRINT@32,;:M$="*PUZZLE IN THE METRO*":GOSUB6:PRINT@96,"EDICIEL, MATRA & HACHETTE 1984.":PRINT
35 A$="IT'S STILL EARLY. THE SKY IS GREY, LOW AND THE ATMOSPHERE SEEMS HEAVY.":GOSUB9
40 A$="YOUR HEAD HURTS TERRIBLY. YOU ARE TRYING TO GATHER YOUR THOUGHTS "
42 A$=A$+"BUT NOTHING HELPS, YOU HAVE FORGOTTEN EVERYTHING, EVEN YOUR OWN NAME.":GOSUB9:GOSUB8
45 A$="EVERYTHING? NO, A THOUGHT HAUNTS YOUR MIND: "
50 A$=A$+"'MEET IN FRONT OF 7 SEGUR AVENUE AT 9:10 A.M.'":GOSUB9
55 A$="YOU ARE IN THE METRO. THE ITEMS IN YOUR POSSESSION ARE A WATCH, A PAYSLIP AND 20 FRANCS.":GOSUB9:GOSUB8
60 CLS0:CLEAR350:POKE49151,16:SOUND230,5:SOUND230,5:SOUND190,4:SOUND210,10
80 CLS:N=2:AG=20:IN$="WP":D=1:V$="START":C$="UT"
100 IFDA<>DTHEN13000
110 IFT>40THENA$="IT'S AFTER 9:10.":GOSUB9:GOTO300
115 W=0:I=0:P=0:V$="":C$="":T=T+1
120 SOUND220,2:PRINT"---> ";
130 PRINT"ﬂ";
131 I$=INKEY$:IFI$=""THEN131
140 PRINTCHR$(8);:IF(ASC(I$)>64ANDASC(I$)<91)OR(ASC(I$)>44ANDASC(I$)<58)ORI$="'"THENPRINTI$;:GOTO180
150 IFI$=" "ANDP=0THENP=1:PRINT" ";:GOTO130
155 IFI$=" "THENSOUND170,2:SOUND130,4:PRINT" only two words!";:GOSUB5:FORI=1TO16:PRINTCHR$(8);:NEXT
160 IFASC(I$)=13THENPRINT:GOTO200
170 IFASC(I$)=8ANDP=0ANDV$<>""THENV$=LEFT$(V$,LEN(V$)-1):PRINTCHR$(8);:GOTO130
175 IFASC(I$)=8ANDP=1ANDC$<>""THENC$=LEFT$(C$,LEN(C$)-1):PRINTCHR$(8);:GOTO130
177 GOTO130
180 IFP=0THENV$=V$+I$:GOTO130
190 IFP=1THENC$=C$+I$:GOTO130
200 IF(V$="LOOK"ANDC$="")OR(V$=""ANDC$="")THENCLS:ONDGOSUB14000,18000,16000,17000,18000:GOSUB3:GOTO100
210 PRINT:ONNGOTO1000,2000,3000,3400,3700,4000,4300,4400,5000,7000,7500,8000,9000,9200,9500,9700,10000
230 IFV$<>""THENPRINT"I DON'T UNDERSTAND: "V$
240 PRINT:GOTO100
250 IFI=1THEN100
252 W=2:GOTO20000
255 IFI=0ANDC$<>""THENM$="I DON'T KNOW HOW TO "+V$+" "+C$:GOSUB1
260 IFI=0ANDC$=""THENPRINT"I DON'T UNDERSTAND YOU."
270 PRINT:GOTO100
300 A$="YOU NO LONGER HAVE ANY CHANCE OF ARRIVING ON TIME FOR YOUR APPOINTMENT.":GOSUB9
310 A$="YOU LOST BUT DON'T DESPAIR.":GOSUB9
320 PRINT:PRINT"WANT TO PLAY AGAIN (Y OR N)?";
330 I$=INKEY$:IFI$="Y"THEN60
350 IFI$="N"THENEND
390 GOTO330
400 IFLEFT$(IN$,1)<>"W"THENPRINT"YOU NO LONGER HAVE A WATCH.":GOTO100
410 PRINT"IT'S"STR$(INT((T+30)/60)+8)":"RIGHT$("0"+MID$(STR$(T+30-INT((T+30)/60)*60),2),2)".":GOTO100
500 M$="HERE ARE THE ITEMS YOU HAVE:":GOSUB1
520 M$="":FORI=1TOLEN(IN$):I$=MID$(IN$,I,1)
530 IFI$="W"THENM$=M$+"WATCH, "
540 IFI$="P"THENM$=M$+"PAYSLIP, "
550 IFI$="L"THENM$=M$+"LETTER, "
560 IFI$="T"THENM$=M$+"TICKET, "
580 NEXT:M$=M$+MID$(STR$(AG),2)+" FRANC":IFAG<>1THENM$=M$+"S"
585 GOSUB1:GOTO100
590 A$="NO ONE BOTHERS TO ANSWER YOU.":GOSUB9:GOTO100
600 IFLEFT$(IN$,1)<>"W"THENPRINT"YOU NO LONGER HAVE A WATCH.":GOTO100
620 IN$=RIGHT$(IN$,LEN(IN$)-1):AG=AG+16:PRINT"I'LL GIVE YOU 16F.":GOTO100
690 A$="THE DOORS OPEN AND A DENSE CROWD POURS ONTO THE PLATFORM, TRAMPLING YOU AS THEY PASS.":GOSUB9:GOTO300
700 IFXV=1THENA$="I KNOW THIS LETTER IS BEAUTIFUL BUT YOU ARE NOT GOING TO SPEND THE DAY READING IT.":GOSUB9:GOTO100
705 CLS:M$="YOU FIND 20 FRANCS IN THE ENVELOPE.":GOSUB1:AG=AG+20:PRINT"HERE IS THE TEXT OF THE LETTER:"
710 A$="I WOULD HAVE LIKED TO SEE YOU, IF ONLY FOR A BRIEF MOMENT OF TIME":GOSUB9
720 A$="A HOPE, I'M WAITING FOR YOU.":GOSUB9:A$="IN THE SHADOW OF A COBBLED ALLEY":GOSUB9:A$="I IMAGINED "
725 A$=A$+"A VENETIAN CANAL,":GOSUB9:A$="THE BRIDGES.":GOSUB9:A$="I RAISED MY EYES TO THE LAMP POST,":GOSUB9
730 A$="I WATCHED A BUS GO BY, FOR A BRIEF MOMENT":GOSUB9:A$="I HOPED. IN THE MISERY OF EXILE,"
740 GOSUB9:A$="I SUFFERED, UNBEARABLE SUFFERING,":GOSUB9:A$="FROM WHICH I CANNOT ESCAPE, AN ETERNAL PRISON"
750 GOSUB9:A$="I DON'T EXPECT ANYTHING. YET, LOOKING UP":GOSUB9:A$="I SOUGHT A FAMILIAR SHADOW,"
760 GOSUB9:A$="A HELP FOR MY DISTRESS.":GOSUB9:A$="I LOVE YOU.":GOSUB9:GOSUB3
770 XV=1:GOTO110
780 PRINT"HE ASKS FOR IDENTIFICATION.":N=16:GOTO100
800 IFZ<>1THENA$="YOU ARE LEAVING THE METRO. YOU ARE LOOKING FOR NUMBER 7 BUT YOU GET LOST.":GOSUB9:GOTO300
815 CLS:FORI=1TO15:SOUND175,1:NEXT:
820 A$="YOU GO OUT. YOU FIND NUMBER 7 WITHOUT DIFFICULTY.":GOSUB9
821 A$="YOU FIND YOURSELF FACE TO FACE WITH A VERY BEAUTIFUL YOUNG GIRL WITH LONG BLOND HAIR AND HAZEL EYES."
825 GOSUB9:A$="SHE TELLS YOU: 'YOU SPENT THE NIGHT DRINKING AGAIN WITH YOUR FRIENDS.":GOSUB9
830 A$="MY POOR DARLING, IT SEEMS AS IF YOU HAVE ARRIVED FROM ANOTHER PLANET.":GOSUB9
835 A$="A TYPICAL PAYDAY, MY LOVE?'":GOSUB9
840 A$="YOU HAVE NOW COMPLETED THE GAME. I HOPE YOU HAD THE SAME PLEASURE PLAYING IT AS I HAD CREATING IT."
845 GOSUB9:A$="IF YOU HAVE ANY COMMENTS TO MAKE, DO NOT HESITATE TO WRITE TO ME:":GOSUB9:A$="ERIC VON ASCHEBERG"
850 GOSUB9:A$="15 QUATRE VENTS STREET":GOSUB9:A$="92380 GARCHES":GOSUB9
855 A$="I WOULD LIKE TO THANK A FEW PEOPLE WITHOUT WHOM THIS GAME WOULD NEVER HAVE BECOME WHAT IT IS:":GOSUB9
860 A$="NATACHA KOTLAREVSKY FOR HER LETTER.":A$="JEAN-FRANCOIS BALAINE FOR THE LOGICAL CLARIFICATIONS.":GOSUB9
870 A$="LAURENT KNOLL FOR HIS HELP WITH THE GRAPHIC DESIGN.":GOSUB9
875 A$="AS WELL AS ALL THOSE WHO KINDLY AGREED TO TEST THE GAME.":GOSUB9:A$="SEE YOU SOON."
890 GOSUB9:IFINKEY$=""THEN890
900 GOTO320
1000 IFV$="EXAMINE"ORV$="LOOK"THEN1100
1005 IFV$="START"THENIFC$="UT"THENM$="THERE IS A MAN SITTING IN A BOOTH. YOU ALSO SEE SOME PASSERS-BY.":GOSUB1:GOTO100
1010 IFV$="EXIT"ORV$="LEAVE"THEN1200
1020 IFV$="READ"THEN1250
1030 IFV$="BUY"THEN1300
1035 IFV$="GO"THEN1400
1040 IFV$="INSERT"THEN1450
1070 IFV$="SEARCH"THEN1550
1075 IFV$="REQUEST"THEN1600
1080 IFV$="APPROACH"THEN1700
1085 IFV$="ASK"THEN1800
1095 IFV$="DONATE"THEN1900
1096 IFV$="USE"THEN3200
1097 GOTO20000
1100 IFC$="STATION"ORC$="METRO"THENM$="THIS IS THE RHINE AND DANUBE STATION, A BEAUTIFUL MODERN STATION.":GOSUB4
1110 IFC$="PASSERS-BY"ORC$="PEOPLE"THENA$="THE PASSERS-BY SEEM TO BE IN A HURRY TO GET THROUGH THE TURNSTILE.":GOSUB9:I=1
1115 IFC$="SIGN"THENM$="IT'S MADE OF WOOD AND BEARS AN INSCRIPTION.":GOSUB4
1120 IF(C$="TURNSTILE"ORC$="MACHINE")ANDL$="PD"THENPRINT"IT'S A VERY ORDINARY TURNSTILE.":I=1
1130 IF(C$="DISPENSER"ORC$="MACHINE")ANDL$="D"THEN1160
1140 IFC$="ATTENDANT"ORC$="MAN"THENPRINT"HE IS HANDSOME.":I=1
1150 GOTO250
1160 PRINT"YOU READ:":PRINT"TICKET-4F ..... BOOKLET-24F":M$="-INSERT EXACT CHANGE-":GOSUB7:PRINT:I=1:GOTO250
1200 IFC$="METRO"ORC$="STATION"THENA$="YOU ARE CONDEMNED TO REMAIN IN THE METRO. YOU WOULD GET LOST OUTSIDE.":GOSUB9:I=1
1210 GOTO250
1250 IFC$="SIGN"THENM$="IT ENCOURAGES YOU TO MAKE A DONATION.":GOSUB4
1260 IFC$="NEWSPAPER"THENPRINT"DO YOU SEE A NEWSPAPER?":I=1
1280 GOTO250
1300 IFC$="TICKET"THENPRINT"HOW?":I=1
1310 IFC$="MAP"THENPRINT"WHO WILL SELL IT TO YOU?":I=1
1320 GOTO250
1400 IF(C$="TURNSTILE"ORC$="MACHINE")ANDL$<>"PD"THENM$="YOU ARE IN FRONT OF THE TURNSTILE.":GOSUB4:L$="PD":N=6:GOTO250
1410 IF(C$="DISPENSER"ORC$="MACHINE")ANDL$<>"D"THENM$="YOU ARE IN FRONT OF THE TICKET DISPENSER.":GOSUB4:L$="D":N=4:GOTO250
1415 IFC$="MACHINE"THEN3100
1420 IFC$="WASHROOM"ORC$="BATHROOM"THENM$="YOU'RE GOING TO HAVE TO HOLD IT.":GOSUB4
1430 IFC$="MAN"THENPRINT"WHICH ONE DO YOU MEAN?":I=1
1435 IFC$="METRO"ORC$="STATION"THENM$="OK.":GOSUB4:L$="":N=1
1440 IFC$="ATTENDANT"ORC$="BOOTH"THENM$="YOU ARE IN FRONT OF THE BOOTH ATTENDANT.":GOSUB4:L$="Q":N=2
1445 GOTO250
1450 J=VAL(C$):IFAG<1THENPRINT"YOU'RE BROKE":I=1:GOTO250
1455 IFJ=4ORJ=24ORC$="MONEY"ORC$="COINS"ORC$="CHANGE"THENIFMD<>1ORAG<>24THENPRINT"YOU NEED COINS NOT BILLS.":I=1:GOTO250
1460 PRINT"THE MACHINE WILL NOT ACCEPT IT.":I=1:GOTO250
1500 I=1:IFC$="TURNSTILE"THENINPUT"HOW";I$:IFI$="FOLLOW SOMEONE"ORI$="FOLLOW PERSON"ORI$="FOLLOW MAN"THEN1870
1510 PRINT"THIS IS NOT POSSIBLE.":GOTO100
1550 IFC$="FLOOR"ORC$="GROUND"THENM$="DO YOU OFTEN FIND A VALID TICKET ON THE GROUND?":GOSUB4
1580 GOTO250
1590 PRINT"YOUR SEARCH IS FRUITLESS.":RETURN
1600 IFC$="HELP"ORC$="TICKET"THENA$="SADLY, THE PASSERS-BY IGNORE YOU.":GOSUB9:I=1
1620 GOTO250
1700 IFC$="MAN"THENA$="BE MORE SPECIFIC PLEASE.":GOSUB9:I=1
1720 IFC$="ATTENDANT"ORC$="WORKER"THENN=2:GOTO2120
1730 GOTO250
1800 IFC$="MAN"ORC$="PERSON"ORC$="PASSER-BY"ORC$="PEOPLE"ORC$="PASSERS-BY"THENPRINT"WHICH ONE?":I=1
1820 IFC$="ATTENDANT"ORC$="WORKER"THENPRINT"HE'S TOO FAR AWAY TO HEAR YOU.":I=1
1830 GOTO250
1850 IFC$="TURNSTILE"THENONRND(3)GOTO1870:PRINT"HOW?":I=1
1860 GOTO250
1870 ONRND(3)GOTO10320:GOTO4710
1900 IFC$="MONEY"THENA$="THIS IS AN EXCELLENT INITIATIVE, DO IT!":GOSUB9:I=1
1910 IFC$="PASSER-BY"ORC$="PERSON"ORC$="MAN"THENM$="THE PERSON WHO YOU GAVE THE MONEY TO THANKS YOU WARMLY.":GOSUB4:AG=0
1920 GOTO250
2000 IFV$="REPLY"ORV$="SAY"THEN2100
2010 IFV$="ASK"THEN2200
2020 IFV$="TALK"ORV$="GREET"THEN2300
2030 IFV$="REQUEST"ORV$="GET"ORV$="BUY"THEN2400
2040 IFV$="GO"THEN3100
2050 IFV$="MAKE"THEN2700
2060 IFV$="GIVE"ORV$="OFFER"THEN2800
2070 IFV$="HELP"THEN2900
2080 GOTO1000
2100 IFC$="HELLO"THEN2120
2110 GOTO250
2120 PRINT"HE SAYS 'HELLO'":I=1:IFGT=1THENPRINT"AGAIN."
2130 GT=1:GOTO250
2200 IFC$="MAN"ORC$="WORKER"ORC$="ATTENDANT"THENPRINT"HE SAYS 'HELP US IN OUR CAUSE'":I=1
2210 GOTO250
2300 IFC$="MAN"ORC$="WORKER"ORC$="ATTENDANT"THENM$="HE TELLS YOU HIS LIFE STORY. YOU WASTE 10 MINUTES.":GOSUB4:T=T+10
2310 GOTO250
2400 IFC$="DIRECTIONS"ORC$="HELP"THENM$="HE DOESN'T KNOW THE METRO WELL. SORRY!":GOSUB4
2410 IFC$="MAP"THENM$="UNFORTUNATELY HE DOES NOT HAVE A MAP.":GOSUB4
2415 IFC$="CHANGE"THENM$="THE MAN REPLIES THAT HE IS NOT AUTHORIZED TO MAKE CHANGE FOR PEOPLE.":GOSUB4
2420 IFC$="MONEY"THENM$="IT'S UP TO YOU TO GIVE SOME!":GOSUB4
2425 IFC$="TICKET"THENM$="THE MAN TELLS YOU TO GO TO THE TICKET MACHINE.":GOSUB4
2430 GOTO250
2505 PRINT"YOU'RE THERE.":N=4:L$="D":I=1:GOTO250
2530 PRINT"YOU ARE THERE.":N=6:L$="PD":I=1:GOTO250
2700 IFC$="DONATION"ANDAG=0THENM$="THAT'S NICE BUT YOU DON'T HAVE ANY MORE MONEY.":GOSUB4
2710 IFC$="DONATION"ANDAG>0THEN2730
2720 GOTO250
2730 I=1:IFAG>19THENM$="THE MAN THANKS YOU AND GIVES YOU BACK 4F IN COINS.":GOSUB1:N=3:AG=4:MD=1:GOTO250
2740 AG=0:GOTO2835
2800 J=VAL(C$):IFC$="DONATION"THENJ=20
2810 IFJ>19THEN2730
2820 IFJ<10THENM$="THE MAN REFUSES AND CALLS YOU A CHEAPSKATE.":GOSUB4:GOTO250
2830 IFJ<=AGTHENAG=AG-J:PRINT"HE THANKS YOU WARMLY.":I=1:GOTO250
2835 IFAG=0THENPRINT"YOU HAVE NO MORE MONEY.":I=1:GOTO250
2840 PRINT"YOU HAVE"AG"FRANC";:I=1:IFAG>1THENPRINT"S";
2850 PRINT".":GOTO250
2900 IFC$="MAN"ORC$="ATTENDANT"THENM$="HOW DO YOU WANT TO HELP HIM?":GOSUB4
2910 GOTO250
3000 IFV$="GO"THEN3100
3020 GOTO1000
3100 IFC$="MACHINE"THENONRND(2)GOTO2505,2530
3105 IFC$="DISPENSER"THENM$="YOU'RE NOW IN FRONT OF THE TICKET DISPENSER.":GOSUB4:L$="D":N=4:GOTO250
3110 GOTO1000
3200 IFC$="MONEY"ORC$="COINS"ORC$="CHANGE"THENM$="WHERE DO YOU WANT TO USE YOUR MONEY?":GOSUB4
3210 GOTO250
3400 IFV$="INSERT"THEN3500
3410 GOTO1000
3500 IFVAL(C$)=4ORVAL(C$)=24ORC$="MONEY"ORC$="COINS"ORC$="CHANGE"THEN3550
3507 GOTO250
3550 IFMD=1ORAG=24THEN3570
3560 GOTO1450
3570 M$="THE MACHINE ISSUES YOU A TICKET.":GOSUB1:IN$=IN$+"T":N=5:IFAG=24THENIN$=IN$+"TTTTT"
3580 AG=0:GOTO100
3700 IFV$="ENTER"ORV$="ADVANCE"THEN3800
3710 IFV$="INSERT"THEN3850
3720 IFV$="GO"THEN3900
3730 GOTO1000
3800 PRINT"HOW?":I=1
3810 GOTO250
3850 IFC$="TICKET"THENA$="YOU BREAK THE DISPENSER WHILE TRYING TO INSERT YOUR TICKET. YOU ARE ARRESTED.":GOSUB9:GOTO300
3860 GOTO250
3900 IFC$="TURNSTILE"THENPRINT"HERE YOU ARE.":N=6:L$="PD":I=1:GOTO250
3910 GOTO1400
4000 IFV$="INSERT"THEN4100
4005 IFV$="TRICK"THEN1500
4010 IFV$="JUMP"ORV$="CLIMB"THEN1850
4020 GOTO1000
4100 IFC$<>"TICKET"THEN250
4110 I=1:IFRIGHT$(IN$,1)="T"THENPRINT"THE PASSAGE IS NOW FREE.":IN$=LEFT$(IN$,LEN(IN$)-1):N=7:GOTO250
4120 PRINT"YOU DON'T HAVE ONE.":GOTO250
4300 IFV$="TAKE"ORV$="GET"ORV$="RETRIEVE"THEN4500
4400 IFV$="LOOK"ORV$="EXAMINE"THEN4600
4410 IFV$="ENTER"ORV$="GO"THEN4700
4430 GOTO1000
4500 IFC$="TICKET"THENM$="THAT'S A GREAT IDEA.":GOSUB4:N=8:IN$=IN$+"T"
4510 GOTO250
4600 IFC$="SIGN"ORC$="DIRECTORY"ORC$="SCHEDULE"THENM$="THERE IS ONLY ONE DIRECTION, NAMELY AUSTERLITZ.":GOSUB4
4610 GOTO250
4700 IFC$<>"TURNSTILE"ANDC$<>"THROUGH"ANDC$<>"IN"THEN1000
4710 A$="YOU ENTER AND GO DOWN A FLIGHT OF STAIRS. YOU FIND A LETTER IN THE LINING OF YOUR JACKET.":GOSUB9
4720 M$="YOU ARE ON THE PLATFORM.":GOSUB1:N=9:D=2:IFRIGHT$(IN$,1)<>"L"THENIN$=IN$+"L"
4730 GOTO100
5000 U=U+1:IFU=4ANDL$="R"THENA$="THE TRAIN SETS OFF AGAIN.":GOSUB9:U=0:L$="Q"
5005 IFU=4ANDL$<>"R"THENA$="A TRAIN IS COMING.":GOSUB9:I=1:L$="R"
5010 IFV$="CLIMB"THEN5650
5020 IFV$="WAIT"THEN5200
5030 IFV$="OPEN"THEN5300
5040 IFV$="TURN"ORV$="PULL"ORV$="PUSH"ORV$="PRESS"ORV$="GRAB"THEN5400
5050 IFV$="FOLLOW"THEN5500
5060 IFV$="ENTER"THEN5800
5070 IFV$="JUMP"THEN5600
5080 IFV$="MOVE"ORV$="DANCE"THEN5700
5090 IFV$="GO"THEN5900
5095 GOTO20000
5100 IFC$="SIGN"THENPRINT"THERE IS NO SIGN.":I=1
5110 IFC$="WATCH"THEN400
5120 IFC$="NAME"ORC$="ADDRESS"THENM$="CHARLES GERRIE, 118 CROMARTY ST.":GOSUB1:I=1
5130 IFC$="ENVELOPE"THENPRINT"IT'S GOT YOUR NAME AND ADDRESS.":I=1
5140 IFC$="LETTER"THEN700
5145 IFC$="MAP"THENM$="THE LINE GOES DIRECTLY TO SEGUR.":GOSUB4
5150 GOTO255
5200 IFC$=""ORC$="PATIENTLY"ORC$="TRAIN"THENIFL$<>"R"THENA$="A TRAIN ARRIVES AND STOPS.":GOSUB9:U=0:L$="R":I=1:GOTO250
5210 IFC$=""ANDL$="R"ANDKK=3THEN690
5215 IFC$=""ANDL$="R"THENA$="THE DOORS OPEN. PASSERS-BY GET OFF. THE TRAIN LEAVES.":GOSUB9:L$="Q":I=1
5230 GOTO250
5300 IFC$="DOOR"THENPRINT"HOW?":I=1
5310 GOTO250
5400 IFC$="KNOB"ANDL$="R"THENM$="THERE IS NO KNOB ON THE DOOR, BUT A SMALLER OBJECT.":GOSUB4
5410 IFC$="KNOB"ANDL$<>"R"THENPRINT"WAIT FOR A TRAIN.":I=1
5415 IFC$="HANDLE"ORC$="LEVER"THENM$="IT'S NOT A HANDLE OR LEVER ON THE DOOR.":GOSUB4
5420 IFC$="BUTTON"ANDL$="R"ANDKK=1THENA$="THE DOORS OPEN. YOU GO UP. YOU SEE A TICKET COLLECTOR.":GOSUB9:N=10:D=3:GOTO100
5430 IFC$="LATCH"ORC$="BUTTON"ANDL$="R"ANDKK<>1THEN690
5440 IFC$="LATCH"ANDL$<>"R"THENM$="THERE IS NO LATCH TO OPEN.":GOSUB4
5450 GOTO250
5500 IFC$="CROWD"ORC$="PASSENGERS"ORC$="PEOPLE"THENA$="YOU GET LOST. THIS WILL TEACH YOU TO FOLLOW STRANGERS!":GOSUB9:GOTO300
5600 IFC$="ON"ORC$="ABOARD"ORC$="TRAIN"THENA$="YOU BANG YOUR HEAD ON A BARRIER, AND FALL UNCONSCIOUS.":GOSUB9:GOTO300
5610 GOTO250
5650 IFC$="STAIRS"THENPRINT"IT'S TOO LATE TO GO BACK.":I=1
5660 GOTO5600
5700 IFC$="BODY"ORC$=""THENM$="THE YOUNG PEOPLE ON THE PLATFORM ASK YOU THE NAME OF THIS NEW DANCE.":GOSUB4
5710 GOTO250
5800 IFC$="METRO"THENM$="YOU'RE ALREADY THERE.":GOSUB4
5810 IFC$="TRAIN"ORC$="CAR"THENM$="THE DOORS ARE CLOSED.":GOSUB4
5820 GOTO250
5900 IFC$="TRAIN"ORC$="CAR"ORC$="FIRST-CLASS"THENM$="YOU ARE UNDER THE FIRST-CLASS SIGN.":GOSUB4:KK=1
5910 IFC$="DOORS"ORC$="DOOR"ORC$="ENTRYWAY"THENM$="YOU ARE IN FRONT OF THE ENTRYWAYS TO THE TRACK.":GOSUB4:KK=3
5920 IFC$="TURNSTILE"THENPRINT"YOU CANNOT RETURN.":I=1
5930 GOTO250
7000 IFV$="LEAVE"ORV$="EXIT"THEN7100
7010 IFV$="CONTINUE"ORV$="WAIT"THEN7200
7020 IFV$="SIT"THEN7300
7030 GOTO20000
7100 IFC$="TRAIN"ORC$="CAR"THENA$="THE TRAIN LEAVES. THAT'S WHEN YOU ARE ATTACKED BY THUGS.":GOSUB9:GOTO300
7110 GOTO250
7200 IFC$="JOURNEY"ORC$=""ORC$="PATIENTLY"THEN7320
7210 GOTO250
7300 IFC$="DOWN"THENPRINT"AHHH... YOU FEEL BETTER."
7320 A$="A FRIENDLY TICKET COLLECTOR ARRIVES. HE ASKS: TICKET PLEASE.":GOSUB9:N=11:D=4:I=1
7330 GOTO250
7500 IFV$="TALK"ORV$="SPEAK"THEN7600
7510 IFV$="SHOW"ORV$="GIVE"ORV$="PRESENT"THEN7700
7520 IF(V$="RUN"ORV$="WAIT"ORV$="DECLINE"ORV$="LEAVE")ANDMID$(IN$,LEN(IN$)-1,1)<>"T"THEN7710
7530 IFV$="PAY"THEN7800
7540 GOTO20000
7600 IFC$="ENGLISH"ORC$="GERMAN"ORC$="SPANISH"THENM$="THE CONTROLLER REITERATES HIS REQUEST IN EXCELLENT "+C$:GOSUB4
7610 IFC$<>"ENGLISH"ANDC$<>"GERMAN"ANDC$<>"SPANISH"THENM$="UNFORTUNATELY, YOUR "+C$+" IS NOT VERY GOOD.":GOSUB4
7620 GOTO250
7700 IFC$<>"TICKET"THEN250
7710 IFMID$(IN$,LEN(IN$)-1,1)<>"T"THENA$="YOU DO NOT HAVE A TICKET. YOU MUST PAY A FINE.":GOSUB9:GOTO780
7720 IFT>30THENA$="THE INSPECTOR SAYS THAT THE TICKET IS NOT VALID. HE OFFERS FOR YOU TO PAY A FINE.":GOSUB9:N=16:GOTO100
7730 A$="THE COLLECTOR THANKS YOU AND CONTINUES HIS WORK.":GOSUB9
7735 M$="WE HAVE ALREADY ARRIVED AT LA MOTTE-PIQUET STATION.":GOSUB4:N=12
7740 D=3:GOTO100
7800 IFC$="COLLECTOR"ORC$="FINE"THENM$="YOU DON'T HAVE ENOUGH MONEY.":GOSUB1:GOTO780
7820 GOTO250
8000 IFV$="CONTINUE"ORV$="WAIT"THEN8100
8010 IFV$="LEAVE"ORV$="EXIT"ORV$="GO"THEN8200
8020 IFV$="STAND"ORV$="GET"THEN8300
8030 GOTO20000
8100 IFC$="JOURNEY"ORC$=""ORC$="PATIENTLY"THENA$="A SHORT TIME LATER, THE TRAIN ARRIVES AT SEGUR STATION.":GOSUB9:N=13:I=1
8110 GOTO250
8200 IFC$="CAR"ORC$="METRO"ORC$="TRAIN"ORC$=""THENPRINT"THIS IS THE WRONG STATION.":GOTO300
8210 GOTO250
8300 IFC$="UP"ORC$=""THENA$="IS THE SEAT UNCOMFORTABLE?":GOSUB9:I=1
8310 GOTO250
9000 IFV$="EXIT"ORV$="LEAVE"THEN9100
9005 IFV$="GO"THEN9120
9010 IFV$="OPEN"ORV$="PULL"THEN9150
9020 GOTO20000
9030 M$="THERE ARE YOUNG PEOPLE MAKING MUSIC ON THE PLATFORM.":GOSUB4:N=14:D=5:GOTO250
9100 IFC$="TRAIN"ORC$="METRO"ORC$="CAR"THEN9030
9120 IFC$="PLATFORM"ORC$="STATION"THEN9030
9150 IFC$="DOOR"ORC$="DOORS"THENPRINT"THE DOORS ARE NOW OPEN.":I=1
9160 GOTO250
9200 IFV$="DONATE"ORV$="GIVE"ORV$="OFFER"THEN9300
9205 IFV$="MAKE"ANDC$="DONATION"THEN9250
9210 IFV$="LISTEN"THEN9350
9220 IFV$="MOVE"ORV$="DANCE"THEN5700
9230 IFV$="ASK"THEN9400
9240 GOTO9500
9250 IFAG>0THENM$="GRATEFUL, THE YOUNG PEOPLE SHOW YOU THE WAY TO GO TO 7.":GOSUB4:Z=1:N=15:AG=0
9260 GOTO1450
9300 IFC$="MONEY"ORC$="DONATION"THEN9250
9310 GOTO250
9350 IFC$="MUSIC"THENPRINT"IT'S A SONG BY SOUCHON.":I=1
9360 GOTO250
9400 IFC$="YOUTH"ORC$="GROUP"ORC$="KIDS"THENM$="WHAT DO YOU WANT TO ASK THEM?":GOSUB4
9450 IFC$="DIRECTIONS"THENA$="THEY SAY THEY ARE NOT AN INTELLIGENCE AGENCY. HOW INSOLENT!":GOSUB9:I=1
9460 GOTO250
9500 IFV$="GO"THEN9600
9510 IFV$="TAKE"ORV$="EXIT"ORV$="LEAVE"THEN9550
9520 GOTO20000
9550 IFC$="STAIRS"ORC$="METRO"ORC$="STATION"THEN800
9560 GOTO250
9600 IFC$="EXIT"THENA$="YOU ARE IN FRONT OF THE EXIT.":GOSUB9:I=1
9610 GOTO250
9700 IFV$="ASK"THEN9800
9710 IFV$="EXIT"ORV$="LEAVE"THEN9850
9720 IFV$="REQUEST"THEN9880
9730 IFV$="EXPLAIN"THEN9900
9740 IFV$="GIVE"ORV$="SHOW"THEN9950
9750 IFV$="PAY"THEN7800
9770 GOTO20000
9800 IFC$="PRICE"ORC$="COST"ORC$="AMOUNT"THENPRINT"IT'S 62F.":I=1
9810 GOTO250
9850 IFC$="CAR"ORC$="TRAIN"ORC$="METRO"THENPRINT"HOW? THE TRAIN IS ROLLING.":I=1
9860 GOTO250
9880 IFC$="STOP"THENM$="THE TICKET COLLECTOR IS BUSY.":GOSUB4
9890 GOTO250
9900 IFC$="SITUATION"ORC$="EVERYTHING"THENM$="YOU SOUND INCOHERENT. YOU DON'T REMEMBER ANYTHING.":GOSUB4
9910 GOTO250
9950 IFC$="PAYSLIP"THENM$="IT'S INSUFFICIENT. HE ALSO NEEDS SOMETHING WITH AN ADDRESS.":GOSUB4:N=17
9955 IFC$="LETTER"ORC$="ENVELOPE"THENM$="HE NEEDS SOMETHING MORE OFFICIAL.":GOSUB4
9960 GOTO250
10000 IFV$="STRANGLE"ORV$="GRAB"THEN10100
10010 IFV$="GIVE"THEN10200
10020 IFV$="BRIBE"THEN10300
10030 IFV$="TAKE"THEN10400
10040 IFV$="SHOW"ORV$="GIVE"THEN10500
10050 GOTO20000
10100 IFC$="COLLECTOR"THENA$="UNLUCKY, HE'S STRONGER THAN YOU. TOO BAD.":GOSUB9:GOTO300
10110 GOTO250
10200 IFC$="MONEY"ORC$="BRIBE"THENA$="THE COLLECTOR THANKS YOU BUT TAKES YOU TO THE STATION.":GOSUB9:GOTO300
10210 IFC$="LETTER"ORC$="ENVELOPE"THEN10510
10220 GOTO250
10300 IFC$="COLLECTOR"ORC$=""THEN10320
10310 GOTO250
10320 A$="YOU ARE VERY DISHONEST. YOU ARE TAKEN BY THE METRO POLICE TO BE CHARGED.":GOSUB9:GOTO300
10400 IFC$="HOSTAGE"THENM$="ARE YOU THREATENING HIM WITH THE PAYSLIP?":GOSUB4
10410 GOTO250
10500 IFC$<>"LETTER"ANDC$<>"ENVELOPE"THEN250
10510 A$="YOU WILL RECEIVE THE FINE BY POST.":GOSUB9:D=3:DA=3:GOTO7735
13000 U=0:FORI=1TO3:SOUND150,2:SOUND110,1:SOUND190,4:NEXT:CLS:ONDGOSUB14000,18000,16000,17000,18000
13020 DA=D:GOSUB3:IFN<>9ANDN<>17THENN=N-1:GOTO200
13030 IFN<>17ANDQ=1THEN200
13040 IFN<>17ANDQ=0THENQ=1:GOTO4710
13050 N=11:V$="SHOW":C$="TICKET":IN$=LEFT$(IN$,LEN(IN$)-1)+"T"+RIGHT$(IN$,1):GOTO200
14000 PRINT"œœœÃÃÕœœœÖèèèèèè":PRINT"œœœÄÄ≈œœœÖçéåçéå":PRINT"ÃÃÃÄÄƒÃÃÃÅéÄÅé◊ﬁ":PRINT"èèèèèèèèâéÄÅé◊ﬁ«"
14010 PRINT"èèèèèèèèÖäÃÖÅÉÇœ":PRINT"èèéèèèèèèèèèÖÇäœ":PRINT"èèÇáèèèëìììÖëìíŒ":PRINT"èèááèèèîúúúÖÉÉÉá":RETURN
16000 PRINT"èèé◊⁄èèäèèäÄÄÖèè":PRINT"èé◊ﬂ⁄èéàååàÄÄÑåå":PRINT"èäﬂﬁáèà¸¸¯èèèèÙ¸":PRINT"èäﬁáèàÙ¸¸ÇèèèàÙ¸"
16010 PRINT"èäáèâäãèäãèèèÖáè":PRINT"èèèÒÛÛÖèèèèÒÛÛÖè":PRINT"èèàÛÛÚÖèèèàÛÛÚÖè":PRINT"èâÅÅÉÖÖèèäÉÅÉÖÖâ":RETURN
17000 PRINT"èèèèèååååèèèèèèè":PRINT"èèèèè¥ºº∏åçèèèèè":PRINT"èèèèèı˜˚˙èèèèèèè"
17010 PRINT"èèèèèÙ˚˜¯èèèèèèè":PRINT"èèèååå“—åååèèèèè":PRINT"èèä◊ﬂﬂ⁄’ﬂﬂ€Öèèèè":PRINT"èèäﬂ⁄ﬂ€◊ﬂ’ﬂÖèèèè":RETURN
18000 PRINT"èèèèéå√«œÖèè≈œœœ":PRINT"èéå√«œœœ…Öèè≈œœœ":PRINT"ä«œœœœœ…≈Ñåå≈œœœ"
18010 PRINT"äœœœœœ…≈ÄÄÄÄ≈œœÃ":PRINT"äœœœœœƒÅÉÉÉÉƒÃÉé":PRINT"äœœŒÃÉèèèåâÉéå”◊":PRINT"äÃÉáèèåâÉéå”◊ﬁ‹”":RETURN
20000 IF(V$="DO"ANDC$="NOTHING")ORV$="WAIT"THENM$="THAT'S NOT VERY CONSTRUCTIVE!":GOSUB1:W=1
20005 IFRIGHT$(IN$,1)="L"THENIFV$="READ"ORV$="LOOK"ORV$="EXAMINE"THEN5100
20010 IFV$="GO"AND(C$="METRO"ORC$="NORTH"ORC$="SOUTH"ORC$="EAST"ORC$="WEST")THENM$="YOU'RE IN THE METRO!":GOSUB1:W=1
20020 IFV$="KILL"ORV$="COMMIT"THENA$="OKAY, THAT WAS SMART!":GOSUB9:GOTO300
20025 IFC$="MAP"THENPRINT"THERE IS NO MAP.":W=1
20030 IFV$="DESTROY"ORV$="SMASH"ORV$="HIT"ORV$="DEMOLISH"THENPRINT"KEEP CALM!":W=1
20035 IFC$="BOOTH"ORC$="COUNTER"THENM$="THERE IS NO TICKET COUNTER HERE.":GOSUB1:W=1
20040 IFV$="STEAL"THENPRINT"IT'S NOT NICE TO STEAL.":W=1
20045 IFV$="EAT"THENM$="YOU HAVE JUST FINISHED YOUR CROISSANT, DON'T TELL ME YOU'RE HUNGRY.":GOSUB1:W=1
20050 IFV$="SELL"ANDC$="WATCH"THEN600
20055 IF(V$="SEEK"ORV$="SEARCH")AND(C$="HELP"ORC$="MONEY"ORC$="TICKET"ORC$="AID")THENGOSUB1590:W=1
20060 IFV$="INV"THEN500
20065 IF(V$="ASK"ORV$="TALK"ORV$="GREET")AND(C$="PASSER-BY"ORC$="PERSON"ORC$="PEOPLE"ORC$="HELP")THEN590
20070 IF(V$="LOOK"ORV$="EXAMINE")ANDC$="WATCH"THEN400
20085 IFW=2THEN255
20090 IFW=0THEN230
20095 GOTO270
