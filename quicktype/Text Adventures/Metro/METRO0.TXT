10 POKE12314,0:EXEC54316:EXEC54302:POKE48937,83:POKE48936,132:CLEAR150:CLS
30 I$="...........................CASSE-TETE DANS LE METRO..........................."
35 A$="   Il est encore tèt. Le ciel est gris, bas et l'atmosphÑre semble lourde.":LI=10:GOSUB9
40 A$="  Vous avez affreusement mal â la tåte. Vous essayez de rassembler vos idçes":LI=11:GOSUB9
42 A$="  mais rien n'y fait, vous avez tout oubliç, jusqu'â votre propre nom.":LI=12:GOSUB9
45 A$="   Tout ?  Non, une penseç hante votre esprit :":LI=13:GOSUB9
50 A$=CHR$(34)+" Rendez-vous devant le 7 de l'avenue de Sçgur â 9h 10 "+CHR$(34):CO=11:LI=15:M=51:GOSUB9:M=0
55 A$="   Vous åtes dans le mçtro. Les objets en votre possession sont une montre,une    fiche de paie et":LI=18:GOSUB9
60 A$="20 francs.":LI=19:CO=19:GOSUB9:POKE12928,19:POKE12929,9:GOSUB960:PRINT"COPYRIGHT EDICIEL, Matra et Hachette  1984 ."
65 C=2:X1=0:Y1=0:X2=159:Y2=82:GOSUB915:POKE12928,15:POKE12929,28:PRINT"-TAPEZ C POUR COMMENCER-";:POKE12928,1:I=1
67 SOUND230,5:SOUND230,5:SOUND190,4:SOUND210,10
70 POKE12929,27:PRINTMID$(I$,I,27);:I=I+1:IFI=54THENI=1
75 FORJ=1TO100:NEXT:IFINKEY$<>"C"THEN70
80 POKE48937,80:POKE48936,132:FORI=1TO3000:NEXTI:CLEAR100:CLS:LI=28:N=2:AG=20:IN$="MF":D=1:V$="DEB":C$="UT"
100 IFDA<>DTHEN13000
110 IFT>40THENA$="Il est plus de 9H10.":LI=15:CO=30:M=85:GOSUB9:M=0:GOTO300
115 POKE12928,19:II=0:I=0:P=0:V$="":C$="":T=T+1
120 SOUND220,2:PRINT"---------->"TAB(60)"";:POKE12929,13
130 I$=INKEY$:IFI$=""THEN130
140 IF(ASC(I$)>64ANDASC(I$)<91)OR(ASC(I$)>47ANDASC(I$)<58)ORI$="'"THENPRINTI$;:GOTO180
150 IFI$=" "ANDP=0THENP=1:PRINT" ";:GOTO 130
155 IFI$=" "THENSOUND170,2:SOUND130,4:PRINT"  DEUX MOTS SEULEMENT SVP";:FORI=1TO3000:NEXT:FORI=1TO24:PRINTCHR$(8);:NEXT
160 IFASC(I$)=13THEN:PRINT:PRINT:PRINTTAB(239)"";:POKE12928,PEEK(12928)-3:GOTO200
170 IFASC(I$)=8ANDP=0ANDV$<>""THENV$=LEFT$(V$,LEN(V$)-1):PRINTCHR$(8);:GOTO130
175 IFASC(I$)=8ANDP=1ANDC$<>""THENC$=LEFT$(C$,LEN(C$)-1):PRINTCHR$(8);:GOTO130
177 GOTO130
180 IFP=0THENV$=V$+I$:GOTO130
190 IFP=1THENC$=C$+I$:GOTO130
200 PRINT:ONNGOTO1000,2000,3000,3400,3700,4000,4300,4400,5000,7000,7500,8000,9000,9200,9500,9700,10000
230 IFV$<>""THENPRINT"Je ne comprends pas: "V$
240 PRINT:GOTO100
250 IFI=1THEN100
252 II=2:GOTO20000
255 IFI=0ANDC$<>""THEN PRINT"Je ne sais pas "V$" un(e) "C$
260 IFI=0ANDC$=""THENPRINT"Je ne vous comprends pas."
270 PRINT:GOTO100
300 LI=29:A$="Vous n'avez plus aucune chance d'arriver â l'heure â votre rendez-vous.":GOSUB9
310 A$="Vous avez perdu mais ne dçsespçrez pas.":LI=30:GOSUB9
320 A$="Voulez-vous rejouer (O ou N) ?":LI=31:CO=25:GOSUB9
330 I$=INKEY$:IFI$=""THEN330
340 IFI$="O"THENCLS:GOTO80
350 IFI$="N"THENCLS:END
390 GOTO330
400 IFLEFT$(IN$,1)<>"M"THENPRINT"Vous n'avez plus de montre.":GOTO100
410 I=(T+30)/60:I=INT(I)+8:PRINT"Il est"I"heures ";
420 I=T+30-INT((T+30)/60)*60:IFI<>0THENPRINT"et"I"minute";:IFI<>1THENPRINT"s";
430 PRINT".":GOTO100
500 PRINT:A$="Voici les objets que vous possçdez :":GOSUB9
520 FORI=1TOLEN(IN$):I$=MID$(IN$,I,1)
530 IFI$="M"THENPRINT"Montre, ";
540 IFI$="F"THENPRINT"Fiche de paie, ";
550 IFI$="L"THENPRINT"Lettre, ";
560 IFI$="T"THENPRINT"Ticket, ";
565 IFI$="C"THENPRINT"Carnet, ";
580 NEXT:PRINTAG"Franc";:IFAG>1THENPRINT"s";
585 PRINT:GOTO100
590 A$="Personne ne daigne vous rçpondre.":GOSUB9:GOTO100
600 IFLEFT$(IN$,1)<>"M"THENPRINT"Vous n'avez plus de montre.":GOTO100
620 IN$=RIGHT$(IN$,LEN(IN$)-1):AG=AG+15:PRINT"On vous en donne 15F.":GOTO100
690 LI=27:A$="Les portes s'ouvrent et une foule compacte se dçverse sur le quai,vous piçtinantau passage.":GOSUB9:GOTO300
700 IFXV=1THENA$="Je sais que cette lettre est belle mais vous n'allez pas passer la journçe â la lire.":GOSUB9:GOTO100
705 POKE48937,37:POKE48936,130:POKE48936,140:PRINTPEEK(48937):POKE48937,64:POKE48936,132:CLS
707 PRINT"Vous trouvez vingt Francs dans l'enveloppe.":AG=AG+20:PRINT"Voici le texte de la lettre :"
710 M=17:A$="   J'aurais aimç te voir, ne serait-ce qu'une goutte lumineuse de temps":LI=10:GOSUB9
720 A$="Un espoir, je t'attends.":LI=11:GOSUB9:A$="Dans l'ombre d'une ruelle pavçe":LI=12:GOSUB9:A$="J'ai imaginç le canal "
725 A$=A$+"vçnitien,":LI=13:GOSUB9:A$="Les ponts.":LI=14:GOSUB9:A$="J'ai levç les yeux vers les péles lumiÑres,":LI=15:GOSUB9
730 A$="J'ai regardç le bus passer, un moment trop court":LI=16:GOSUB9:A$="J'ai espçrç. Dans la douleur du vlioubitsia,":LI=17
740 GOSUB9:A$="J'ai souffert, une souffrance insoutenable,":LI=18:GOSUB9:A$="Dont on ne peut se dçfaire, une prison çternelle"
750 LI=19:GOSUB9:A$="Je n'attends rien. Pourtant, en levant les yeux":LI=20:GOSUB9:A$="J'ai cherchç une ombre familiÑre,":LI=21
760 GOSUB9:A$="Une aide â mon dçsarroi.":LI=22:GOSUB9:A$="Je t'aime":LI=23:GOSUB9:M=0:A$=".Que voulez-vous faire ?":LI=25:GOSUB9
770 XV=1:DA=1:GOTO110
780 PRINT".Il demande un papier d'identite.":N=16:GOTO100
800 IFZ<>1THENA$="Vous sortez du mçtro. Vous cherchez le numçro 7 mais vous vous perdez.":GOSUB9:GOTO300
815 POKE48937,32:POKE48936,130:POKE48936,140:PRINTPEEK(48937):POKE48937,54:POKE48936,132:CLS:FORI=1TO15:SOUND175,1:NEXT:CLEAR150
820 A$="Vous sortez.Vous trouvez sans difficultç le numçro 7.Vous vous retrouvez en faced'une trÑs belle jeune fille aux "
822 LI=8:GOSUB9:CO=33
825 A$="longs cheveux blonds et aux yeux noisettes.Ellevous dit: "+CHR$(34)+"Tu as encore passç la nuit â boire":LI=9:GOSUB9
830 A$="avec tes copains.Mon pauvre chçri,on dirait que tu dçbarques d'une autre planÑte. Dire que c'est toujours la":LI=10
835 CO=46:GOSUB9:A$="måme chose le jour de la paie!"+CHR$(34):LI=11:CO=75:GOSUB9
840 A$="Vous avez maintenant terminç le jeu.J'espÑre que vous avez eu le måme plaisir eny jouant que j'ai eu en le crçant."
845 LI=15:GOSUB9:A$="Si vous avez des remarques â faire, n'hçsitez pas â m'çcrire :":LI=17:GOSUB9:A$="Eric von Ascheberg":LI=19
850 CO=28:M=85:GOSUB9:A$="15 rue des quatre vents":LI=20:CO=28:M=17:GOSUB9:A$="92380     GARCHES":LI=21:CO=28:GOSUB9
855 POKE12928,16:PRINT"J'aimerais remercier quelques personnes sans qui ce jeu ne serait jamais devenu  ce qu'il est :"
860 PRINT" Natacha Kotlarevsky pour sa lettre.":PRINT" Jean-Francois Balaine pour les mises au point logiques."
870 PRINT" Laurent Knoll pour son aide au niveau de la conception graphique."
875 A$="Ainsi que tous ceux qui ont gentiment acceptçs de jouer le rèle de candide.":LI=29:M=0:GOSUB9:A$="A bientèt."
890 LI=31:CO=45:GOSUB9:GOTO890
900 REM GRAPHIQUES
905 FORI=X1TOX2:SET(I,Y,C):NEXT:RETURN
910 FORI=Y1TOY2:SET(X,I,C):NEXT:RETURN
915 FORI=X1TOX2STEPX2-X1:FORJ=Y1TOY2:SET(I,J,C):NEXTJ,I:FORJ=Y1TOY2STEPY2-Y1:FORI=X1TOX2:SET(I,J,C):NEXTI,J:RETURN
925 R=(Y2-Y1)/(X2-X1):J=Y1:IFABS(R)>1THENFORI=X1TOX2STEPABS(1/R):SET(I,J,C):J=J+SGN(R):NEXT:RETURN
930 FORI=X1TOX2:SET(I,J,C):J=J+R:NEXT:RETURN
950 IFC$="UT"THENA$="Il y a un homme assis derriÑre le panneau. Vous voyez aussi quelques passants.":GOSUB9:GOTO100
960 C=1:X=8:Y1=94:Y2=97:GOSUB910:X=14:GOSUB910:Y=92:X1=10:X2=12:GOSUB905:Y=99:GOSUB905:SET(9,98,1):SET(13,98,1):SET(9,93,1)
965 SET(13,93,1):SET(10,96,1):SET(10,95,1):SET(11,94,1):SET(11,97,1):RETURN
980 PRINT"Vous lisez :":PRINT" Billet  - 4fr ................. Carnet  - 24fr":PRINTTAB(7)"- Introduire la somme exacte -"
985 GOTO100
990 N=10:D=3:GOTO100
1000 IFV$="OBSERVER"ORV$="REGARDER"ORV$="VOIR"THEN1100
1005 IFV$="DEB"THEN950
1010 IFV$="SORTIR"THEN1200
1020 IFV$="LIRE"THEN1250
1030 IFV$="ACHETER"THEN1300
1035 IFV$="ALLER"THEN1400
1040 IFV$="INTRODUIRE"THEN1450
1060 IFV$="FRAUDER"THEN1500
1070 IFV$="CHERCHER"THEN1550
1075 IFV$="DEMANDER"THEN1600
1080 IFV$="APPROCHER"ORV$="ABORDER"THEN1700
1085 IFV$="INTERROGER"ORV$="QUESTIONNER"ORV$="PARLER"THEN1800
1090 IFV$="FRANCHIR"ORV$="ESCALADER"THEN1850
1095 IFV$="FAIRE"THEN1900
1097 GOTO20000
1100 IFC$="STATION"THENPRINT"C'est la station Rhin et Danube, une belle station moderne.":I=1
1110 IFC$="PASSANTS"ORC$="USAGER"ORC$="GENS"ORC$="USAGER"THENA$="Les passants ont l'air trÑs pressçs.":GOSUB9:I=1
1115 IFC$="PANNEAU"THENPRINT"Le panneau est en bois, il porte une inscription.":I=1
1120 IFC$="DISTRIBUTEUR"ANDL$<>"D"THENPRINT"C'est un distributeur tout ce qu'il y a de plus quelconque.":I=1
1130 IFC$="DISTRIBUTEUR"ANDL$="D"THEN980
1150 IFC$="QUETEUR"ORC$="HOMME"THENPRINT"Il est beau.":I=1
1160 GOTO250
1200 IFC$="METRO"THENA$="Vous åtes condamnç â demeurer dans les mçandres du mçtro.Vous vous perdriez â l'extçrieur.":GOSUB9:I=1
1210 GOTO250
1250 IFC$="PANNEAU"ORC$="INSCRIPTION"THENA$="Il vous incite â faire un don.":GOSUB9:I=1
1260 IFC$="JOURNAL"THENPRINT"Vous voyez un journal, vous ?":I=1
1280 GOTO250
1300 IFC$="TICKET"ORC$="BILLET"THENPRINT"Comment ?":I=1
1310 IFC$="PLAN"THENPRINT"Qui va vous le vendre ?":I=1
1320 GOTO250
1400 IFC$="PDC"THENA$="Vous åtes devant le passage de contrèle.":GOSUB9:L$="PD":I=1
1410 IFC$="DISTRIBUTEUR"THENA$="Vous åtes devant le distributeur.":GOSUB9:L$="D":I=1
1420 IFC$="TOILETTES"ORC$="WC"THENPRINT"Il va falloir vous retenir.":I=1
1430 IFC$="HOMME"THENPRINT"Duquel voulez-vous parler ?":I=1
1440 IFC$="QUETEUR"ORC$="QUEMANDEUR"ORC$="PANNEAU"THENA$="Vous åtes devant le quåteur.":GOSUB9:L$="Q":N=2:I=1
1445 GOTO250
1450 I=0:IFC$="BILLET"ORC$="TICKET"ORC$="MONNAIE"ORC$="PIECES"THENPRINT"Il faudrait en avoir.":I=1
1460 GOTO250
1500 I=1:IFC$=""THENINPUT" Comment ";I$:IFI$="SUIVRE PASSANT"ORI$="SUIVRE USAGER"THEN4710
1510 PRINT"Ce n'est pas possible.":I=1:GOTO100
1550 IFC$="TICKET"ORC$="BILLET"THENPRINT"Vous arrive-t-il souvent de trouver un billet valable parterre ?":I=1
1560 IFC$="AIDE"ORC$="ARGENT"ORC$="AGENT"THENPRINT"Vous pouvez toujours chercher!":I=1
1580 GOTO250
1600 IFC$="AUMONE"ORC$="BILLET"ORC$="TICKET"THENA$="Moi,vous me faites pitiç,mais les passants vous ignorent.":GOSUB9:I=1
1620 GOTO250
1700 IFC$="HOMME"THENA$="Soyez plus prçcis svp.":GOSUB9:I=1
1720 IFC$="QUETEUR"ORC$="QUEMANDEUR"THENPRINT"Il vous dit bonjour.":N=2:I=1
1730 GOTO250
1800 IFC$="HOMME"THENPRINT"Lequel ?":I=1
1820 IFC$="QUETEUR"ORC$="QUEMANDEUR"THENPRINT"Il vous entend mal.":I=1
1830 GOTO250
1850 IFC$="PDC"THENPRINT"Comment ?":I=1
1860 GOTO250
1900 IFC$="MONNAIE"THENA$="Voilâ une excellente initiative, faites donc!":GOSUB9:I=1
1910 IFC$="DON"THENA$="Le passant â qui vous avez donnç votre argent vous remercie chaleureusement.":GOSUB9:AG=0:I=1
1920 GOTO250
2000 IFV$="REPONDRE"ORV$="DIRE"THEN2100
2010 IFV$="INTERROGER"ORV$="QUESTIONNER"THEN2200
2020 IFV$="PARLER"THEN2300
2030 IFV$="DEMANDER"THEN2400
2040 IFV$="ALLER"THEN2500
2050 IFV$="FAIRE"ORV$="PROPOSER"THEN2700
2060 IFV$="DONNER"ORV$="OFFRIR"THEN2800
2070 IFV$="AIDER"THEN2900
2080 GOTO20000
2100 IFC$="BONJOUR"THENPRINT"L'homme vous redit bonjour.":I=1
2110 GOTO250
2200 IFC$="HOMME"ORC$="QUETEUR"THENPRINT"Il dit: Aidez nous dans notre lutte.":I=1
2210 GOTO250
2300 IFC$="HOMME"ORC$="QUETEUR"THENPRINT"Il vous raconte sa vie. Vous perdez dix minutes.":T=T+10:I=1
2310 GOTO250
2400 IFC$="RENSEIGNEMENT"ORC$="CHEMIN"ORC$="AIDE"THENA$="Il connait assez mal le mçtro. Dçsolç!":GOSUB9:I=1
2410 IFC$="PLAN"THENA$="Il ne possÑde malheureusement pas de plan.":GOSUB9:I=1
2415 IFC$="MONNAIE"THENA$="L'homme rçpond qu'il n'est pas habilitç â vous en faire.":GOSUB9:I=1
2420 IFC$="ARGENT"THENA$="C'est â vous d'en donner !":GOSUB9:I=1
2425 IFC$="BILLET"ORC$="TICKET"THENPRINT"L'homme vous dit d'aller au distributeur.":I=1
2430 GOTO250
2500 IFC$="DISTRIBUTEUR"ANDAG<>4ANDAG<>24THENPRINT"C'est fait.":N=1:L$="D":I=1
2505 IFC$="DISTRIBUTEUR"AND(AG=24ORAG=4)THENPRINT"C'est fait.":N=4:L$="D":I=1
2510 IFC$="PDC"THENPRINT"C'est fait.":N=1:L$="PD":I=1
2520 GOTO250
2700 IFC$="DON"ANDAG=0THENPRINT"C'est gentil mais vous n'avez plus d'argent.":I=1
2710 IFC$="DON"ANDAG>0THENPRINT"L'homme prend votre argent et vous remercie chaleureusement.":AG=0:I=1
2720 GOTO250
2800 IFC$="16F"THENPRINT"L'homme vous remercie et vous rend 4F.":N=3:I=1:AG=4:GOTO250
2810 I$=LEFT$(C$,LEN(C$)-1):FORK=1TOLEN(I$):J=ASC(MID$(I$,K,1)):IFJ<48ORJ>57THEN250
2820 NEXT:J=VAL(I$):IFJ<12THENPRINT"L'homme refuse et vous traite de radin.":I=1:GOTO250
2830 IFJ<=AGTHENAG=AG-J:PRINT"Il vous remercie.":I=1:GOTO250
2835 IFAG=0THENPRINT"Vous n'avez plus d'argent.":I=1:GOTO250
2840 PRINT"Vous n'avez que"AG"Franc";:I=1:IFAG>1THENPRINT"s";
2850 PRINT".":GOTO250
2900 IFC$="HOMME"ORC$="QUETEUR"THENPRINT"Comment voulez-vous l'aider ?":I=1
2910 GOTO250
3000 IFV$="ALLER"ORV$="RETOURNER"THEN3100
3010 IFV$="INTRODUIRE"ORV$="UTILISER"THEN3200
3020 GOTO20000
3100 IFC$="DISTRIBUTEUR"THENA$="Vous åtes devant le distributeur.":GOSUB9:L$="D":N=4:I=1
3110 GOTO250
3200 IFC$="MONNAIE"ORC$="PIECES"THENPRINT"Ou voulez-vous introduire vos pieces ?":I=1
3210 GOTO250
3400 IFV$="INTRODUIRE"THEN3500
3410 GOTO20000
3500 IFC$="4F"ORC$="MONNAIE"ORC$="PIECES"THEN3550
3507 IFC$="24F"THENPRINT"Vous n'avez que 4F.":GOTO100
3510 GOTO250
3550 IFAG=4THENA$="L'appareil vous dçlivre un ticket.":GOSUB9:IN$=IN$+"T"
3570 N=5:AG=0:GOTO100
3700 IFV$="PASSER"ORV$="FRANCHIR"ORV$="AVANCER"THEN3800
3710 IFV$="INTRODUIRE"THEN3850
3720 IFV$="ALLER"THEN3900
3730 GOTO20000
3800 IFC$="PDC"THENPRINT"Comment ?":I=1
3810 GOTO250
3850 IFC$="TICKET"ORC$="BILLET"THENPRINT"Vous cassez le distibuteur en essayant d'y introduire votre ticket.On vous arrete.":GOTO3003860 GOTO250
3900 IFC$="PDC"THENA$="Vous y  åtes.":GOSUB9:N=6:L$="PD":I=1
3910 GOTO250
4000 IFV$="INTRODUIRE"THEN4100
4010 GOTO20000
4100 IFC$="TICKET"ORC$="BILLET"THENPRINT"Le passage est maintenant libre.":IN$=LEFT$(IN$,LEN(IN$)-1):N=7:I=1
4110 GOTO250
4300 IFV$="PRENDRE"ORV$="REPRENDRE"THEN4500
4400 IFV$="REGARDER"ORV$="OBSERVER"ORC$="VOIR"THEN4600
4410 IFV$="PASSER"ORV$="FRANCHIR"THEN4700
4420 IFV$="PRENDRE"ORV$="REPRENDRE"THEN4800
4430 GOTO20000
4500 IFC$="TICKET"ORC$="BILLET"THENA$="C'est une excellente idçe.":GOSUB9:N=8:IN$=IN$+"T":I=1
4510 GOTO250
4600 IFC$="PANNEAU"ORC$="DIRECTION"THENA$="Il n'y a qu'une direction, â savoir Austerlitz.":GOSUB9:I=1
4610 GOTO250
4700 IFC$<>"PDC"THEN250
4710 POKE12928,21:PRINT"Vous descendez un escalier. Vous trouvez une lettre dans la doublure de votre   veste."
4720 A$="Vous åtes sur le quai.":LI=29:CO=7:GOSUB9:N=9:D=2:IFRIGHT$(IN$,1)<>"L"THENIN$=IN$+"L"
4730 GOTO100
4800 IFC$="BILLET"ORC$="TICKET"THENA$="Vous l'avez dçja repris !:GOSUB9:I=1
4810 GOTO250
5000 U=U+1:IFU=4ANDL$="R"THENPRINT"La rame repart.":LI=29:U=0:L$="Q"
5005 IFU=4ANDL$<>"R"THENPRINT"Une rame arrive.":LI=29:U=1:L$="R"
5010 IFV$="REMONTER"THEN5650
5015 IFV$="LIRE"ORV$="REGARDER"THEN5100
5020 IFV$="ATTENDRE"THEN5200
5030 IFV$="OUVRIR"THEN5300
5040 IFV$="LEVER"ORV$="ACTIONNER"ORV$="MANOEUVRER"THEN5400
5050 IFV$="SUIVRE"THEN5500
5060 IFV$="MONTER"ORV$="ENTRER"THEN5800
5070 IFV$="RECULER"THEN5600
5080 IFV$="BOUGER"THEN5700
5090 IFV$="ALLER"THEN5900
5095 GOTO20000
5100 IFC$="PANNEAU"THENPRINT"Il n'y a pas de panneau.":I=1
5110 IFC$="PLAN"THENA$="La ligne est directe pour aller â Sçgur.":GOSUB9:I=1
5120 IFC$="NOM"THENPRINT"C'est votre nom.":I=1
5130 IFC$="ADRESSE"THENPRINT"C'est votre adresse.":I=1
5140 IFC$="LETTRE"THEN700
5150 GOTO250
5200 IFC$="METRO"ORC$="RAME"THENA$="Une rame arrive et s'arråte.":GOSUB9:U=0:L$="R":T=T+5:I=1
5210 IFC$="OUVERTURE"ANDL$<>"R"THENPRINT"L'ouverture de quoi ?":I=1
5215 IFC$="OUVERTURE"ANDL$="R"THENPRINT"Les portes s'ouvrent. Les passants descendent. La rame repart.":L$="Q":I=1
5230 GOTO250
5300 IFC$="PORTE"ORC$="PORTES"THENPRINT"Comment ?":I=1
5310 GOTO250
5400 IFC$="LOQUET"ANDL$="R"THENPRINT"Il n'y a pas de loquet sur la porte de la rame, mais un objet plus petit.":I=1
5410 IFC$="LOQUET"ANDL$<>"R"THENPRINT"Attendez donc une rame.":I=1
5415 IFC$="POIGNEE"THENA$="Ce n'est pas une poignçe sur la porte.":GOSUB9:I=1
5420 IFC$="LOQUETEAU"ANDL$="R"ANDKK=1THENA$="Les portes s'ouvrent.Vous montez. Vous apercevez un contrèleur.":GOSUB9:GOTO990
5430 IFC$="LOQUETEAU"ANDL$="R"ANDKK<>1THEN690
5440 IFC$="LOQUETEAU"ANDL$<>"R"THENA$="Il n'y a pas de loqueteau â ouvrir.":GOSUB9:I=1
5450 GOTO250
5500 IFC$="PASSANT"ORC$="USAGER"ORC$="GENS"THENA$="Vous vous perdez. Cela vous apprendra â suivre des inconnus!":GOSUB9:GOTO300
5600 IFC$=""THENA$="Vous vous cognez la tåte contre le mur, et tombez inconscient.":GOSUB9:GOTO300
5610 GOTO250
5650 IFC$="ESCALIER"THENPRINT"Il est trop tard pour reculer.":I=1
5660 GOTO250
5700 IFC$=""THENPRINT"Un groupe de jeunes vous demande le nom de cette nouvelle danse.":I=1
5710 GOTO250
5800 IFC$="METRO"THENA$="Vous y åtes deja.":GOSUB9:I=1
5810 IFC$="WAGON"ORC$="RAME"THENA$="Les portes sont fermçes.":GOSUB9:I=1
5820 GOTO250
5900 IFC$="PREMIERE"ORC$="PREMIERES"THENKK=1:A$="Vous åtes sous le panneau premiÑre classe.":GOSUB9:I=1
5910 IFC$="PORTE"ORC$="PORTES"THENA$="Vous åtes devant l'emplacement des portes de la rame.":GOSUB9:I=1
5920 IFC$="PDC"THENPRINT"Vous ne pouvez retourner.":I=1
5930 GOTO250
6000 FORII=1TOLEN(A$)
6020 AS=ASC(MID$(A$,II,1)):POKE48934,LI:POKE48935,INT(CO/2)-128*(CO/2<>INT(CO/2))
6030 I=-(4*(AS=142)+31*(AS=143)+18*(AS=141)+23*(AS=137)+25*(AS=132)+27*(AS=140))
6040 IFI<>0THENAS=I
6050 POKE48929,AS:POKE48931,M:POKE48936,81:CO=CO+1:IFCO=80THENCO=0:LI=LI+1:NEXT
6060 NEXT:LI=28:CO=0:RETURN
7000 IFV$="DESCENDRE"ORV$="QUITTER"THEN7100
7010 IFV$="CONTINUER"ORV$="ATTENDRE"THEN7200
7020 IFV$="S'ASSEOIR"THEN7300
7030 GOTO20000
7100 IFC$="RAME"ORC$="WAGON"THENPRINT"La rame repart. C'est alors que vous vous faites attaquer par des voyous.":GOTO300
7110 GOTO250
7200 IFC$="TRAJET"ORC$=""THEN:A$="Un sympathique contrèleur arrive. Il demande: Billet svp.":GOSUB9:N=11:D=4:I=1
7210 GOTO250
7300 IFC$=""THENPRINT"Ah! Vous vous sentez mieux.":I=1
7310 GOTO250
7500 IFV$="PARLER"THEN7600
7510 IFV$="MONTRER"ORV$="DONNER"ORV$="PRESENTER"THEN7700
7520 IF(V$="S'ENFUIR"ORV$="ATTENDRE"ORV$="DECLINER"ORV$="DEMANDER")AND(MID$(IN$,LEN(IN$)-1,1)<>"T")THENN=16:GOTO200
7530 IFV$="PAYER"THEN7800
7540 GOTO20000
7600 IFC$="ANGLAIS"ORC$="ALLEMAND"ORC$="ESPAGNOL"THENA$="Le contrèleur rçitÑre sa demande dans un EXCELLENT "+C$:GOSUB9:I=1
7610 IFC$<>"ANGLAIS"ANDC$<>"ALLEMAND"ANDC$<>"ESPAGNOL"THENA$="Malheureusement, votre "+C$+" n'est pas trÑs bon.":GOSUB9:I=1
7620 GOTO250
7700 IFC$<>"TICKET"ANDC$<>"BILLET"THEN250
7710 IFMID$(IN$,LEN(IN$)-1,1)<>"T"THENPRINT"Vous n'avez pas de ticket. Il vous faut payer une amende";:GOTO780
7720 IFT>30THENA$="Le contrèleur dit que le billet n'est pas valable.Il vous propose de payer une amende.":GOSUB9:N=16:GOTO100
7730 A$="Le contrèleur vous remercie et continue son travail.On arrive dçja â la station La Motte-Piquet.":GOSUB9:I=1:N=12
7740 D=3:GOTO100
7800 IFC$=""ORC$="AMENDE"THENPRINT"Vous n'avez pas assez d'argent";:GOTO780
7810 IFC$="AMANDE"THENPRINT"Essayez de payer une amende avec un E.":I=1
7820 GOTO250
8000 IFV$="CONTINUER"ORV$="ATTENDRE"THEN8100
8010 IFV$="DESCENDRE"THEN8200
8020 IFV$="SE"THEN8300
8030 GOTO20000
8100 IFC$="TRAJET"ORC$="VOYAGE"ORC$=""THENA$="Peu de temps aprÑs, la rame arrive â la station Sçgur.":GOSUB9:N=13:I=1
8110 GOTO250
8200 IFC$="RAME"ORC$="METRO"ORC$="WAGON"ORC$=""THENPRINT"Ce n'est pas la bonne station.":GOTO300
8210 GOTO250
8300 IFC$="LEVER"THENA$="Le siçge est-il inconfortable ?":GOSUB9:I=1
8310 GOTO250
9000 IFV$="SORTIR"ORV$="DESCENDRE"THEN9100
9010 IFV$="LEVER"ORV$="ACTIONNER"ORV$="MANOEUVRER"THEN9150
9020 GOTO20000
9100 IFC$="RAME"ORC$="WAGON"ORC$=""ORC$="METRO"THENPRINT"Il y a des jeunes qui font de la musique sur le quai.":N=14:D=5:I=1
9150 IFC$="LOQUETEAU"THENPRINT"Les portes sont maintenant ouvertes.":I=1
9160 GOTO250
9200 IFV$="FAIRE"ORV$="DONNER"ORV$="OFFRIR"THEN9300
9210 IFV$="ECOUTER"THEN9350
9220 IFV$="INTERROGER"ORV$="QUESTIONNER"THEN9400
9230 IFV$="DEMANDER"THEN9450
9240 GOTO9500
9300 IFC$="DON"ORC$="ARGENT"THENPRINT"Reconnaissants,les jeunes vous indiquent le chemin pour aller au 7.":Z=1:N=15:AG=0:I=1
9310 GOTO250
9350 IFC$="MUSIQUE"THENPRINT"C'est une chanson de Souchon.":I=1
9360 GOTO250
9400 IFC$="JEUNES"ORC$="GROUPE"THENPRINT"Que voulez-vous leur demander ?":I=1
9410 GOTO250
9450 IFC$="CHEMIN"THENA$="Ils rçpondent qu'ils ne sont pas une agence de renseignement.Quelle insolence !":GOSUB9:I=1
9460 GOTO250
9500 IFV$="ALLER"THEN9600
9510 IFV$="MONTER"ORV$="SORTIR"THEN9550
9520 GOTO20000
9550 IFC$=""ORC$="ESCALIER"ORC$="METRO"THEN800
9560 GOTO250
9600 IFC$="SORTIE"THENA$="Vous åtes devant la sortie.":GOSUB9:I=1
9610 GOTO250
9700 IFV$="DEMANDER"THEN9800
9710 IFV$="S'ENFUIR"THEN9850
9720 IFV$="ATTENDRE"THEN9880
9730 IFV$="DECLINER"THEN9900
9740 IFV$="DONNER"ORV$="MONTRER"THEN9950
9750 IFV$="PAYER"THEN7800
9770 GOTO20000
9800 IFC$="PRIX"ORC$="TARIF"ORC$="MONTANT"THENPRINT"C'est 62F.":I=1
9810 GOTO250
9850 IFC$="RAME"ORC$=""THENPRINT"Comment ? La rame roule.":I=1
9860 GOTO250
9880 IFC$="ARRET"ORC$=""THENA$="Le contrèleur est pressç.":GOSUB9:I=1
9890 GOTO250
9900 IFC$="IDENTITE"THENPRINT"Vous ne vous souvenez de rien.":I=1
9910 GOTO250
9950 IFC$="FDP"THENPRINT"C'est insuffisant.":N=17:I=1
9960 GOTO250
10000 IFV$="ETRANGLER"THEN10100
10010 IFV$="DONNER"THEN10200
10020 IFV$="SOUDOYER"THEN10300
10030 IFV$="PRENDRE"THEN10400
10040 IFV$="MONTRER"THEN10500
10050 GOTO20000
10100 IFC$="CONTROLEUR"THENPRINT"Manque de chance, il est plus fort que vous. Dommage.":GOTO300
10110 GOTO250
10200 IFC$="ARGENT"THENA$="Le contrèleur vous remercie mais vous emmÑne au poste.":GOSUB9:GOTO300
10210 IFC$="LETTRE"ORC$="ENVELOPPE"THEN10510
10220 GOTO250
10300 IFC$="CONTROLEUR"THENA$="Vous åtes bien malhonnåte. Il refuse et vous emmÑne au poste.":GOSUB9:GOTO300
10310 GOTO250
10400 IFC$="OTAGE"THENPRINT"Vous le menacez avec la fiche de paie ?":I=1
10410 GOTO250
10500 IFC$<>"LETTRE"ANDC$<>"ENVELOPPE"THEN250
10510 PRINT"Vous recevrez l'amende par la poste.":FORJ=1TO2000:NEXTJ:D=3:GOTO100
13000 U=0:FORI=1TO3:SOUND150,2:SOUND110,1:SOUND190,4:NEXT:ONDGOSUB14000,18000,16000,17000,18000
13020 DA=D:POKE12929,0:POKE12928,18:PRINT".Que voulez-vous faire ?":PRINT:IFN<>9ANDN<>17THENN=N-1:GOTO200
13030 IFN<>17ANDQ=1THEN200
13040 IFN<>17ANDQ=0THENQ=1:GOTO4710
13050 N=11:V$="MONTRER":C$="BILLET":IN$=LEFT$(IN$,LEN(IN$)-1)+"T"+RIGHT$(IN$,1):GOTO200
14000 POKE48937,38:POKE48936,130:POKE48936,140:PRINTPEEK(48937):POKE48937,20:POKE48936,132:CLS
14010 C=2:Y=26:X1=0:X2=37:GOSUB905:X1=57:X2=67:GOSUB905:X1=116:X2=121:GOSUB905:X1=157:X2=159:GOSUB905:Y=2:X1=37:X2=57:GOSUB905
14020 X=37:Y1=2:Y2=26:GOSUB910:X=57:GOSUB910:X=157:Y1=15:Y2=57:GOSUB910:Y=15:X1=125:X2=157:GOSUB905:X1=121:Y1=18:X2=153:Y2=59
14030 GOSUB915:X1=121:Y1=17:X2=126:Y2=15:GOSUB925:X1=153:X2=157:GOSUB925:Y1=59:Y2=57:GOSUB925:X1=38:Y1=25:X2=57:Y2=20:GOSUB925
14040 Y1=21:Y2=23:FORX=128TO143STEP3:GOSUB910:NEXT:C=1:X1=146
14070 Y1=43:X2=150:Y2=44:GOSUB915:Y=77:X1=10:X2=28:GOSUB905:Y=53:X1=8:X2=26:GOSUB905:X1=8:Y1=53:X2=10:Y2=77:GOSUB925:X1=26:X2=28
14080 GOSUB925:X1=5:Y1=75:X2=8:Y2=53:GOSUB925:Y=75:X1=5:X2=9:GOSUB905:Y1=19:Y2=37:FORX1=68TO104STEP12:X2=X1+6:GOSUB915:NEXT
14090 X=76:Y1=9:Y2=35:GOSUB910:X=79:GOSUB910:X=100:GOSUB910:X=103:GOSUB910:Y1=37:Y2=33:FORX1=74TO110STEP12:X2=X1+6:GOSUB925
14100 NEXT:X=116:Y1=14:Y2=33:GOSUB910:Y=9:X1=74:X2=76:GOSUB905:X1=80:X2=83:GOSUB905:X1=97:X2=99:GOSUB905:X1=104:X2=107:GOSUB905
14110 Y=14:FORX1=76TO122STEP12:X2=X1+3:GOSUB905:NEXT:Y1=18:Y2=15:FORX1=68TO104STEP12:X2=X1+7:GOSUB925:NEXT:X1=86:X2=93:GOSUB925
14120 X1=110:X2=117:GOSUB925:X=92:Y1=14:Y2=30:GOSUB910:Y=24:X1=89:X2=91:GOSUB905:Y=31:GOSUB905:RESET(117,15):RESET(93,15)
14130 SET(75,18,1):SET(99,18,1):POKE12928,4:POKE12929,36:PRINTCHR$(34);:POKE12929,48:PRINTCHR$(34);:RESET(77,14):RESET(78,14)
14140 RESET(77,34):RESET(78,34):RESET(101,14):RESET(102,14):RESET(101,34):RESET(102,34):RETURN
16000 POKE48937,36:POKE48936,130:POKE48936,140:PRINTPEEK(48937):POKE48937,23:POKE48936,132:CLS
16010 C=1:X=1:Y1=14:Y2=60:GOSUB910:X1=1:Y1=14:X2=15:Y2=0:GOSUB925:Y1=60:X2=39:Y2=23:GOSUB925:X1=2:Y1=83:X2=33:Y2=54:GOSUB925:C=2
16020 X1=40:Y1=7:X2=100:Y2=30:GOSUB915:X1=30:Y1=40:X2=90:Y2=53:GOSUB915:Y1=40:Y2=30:FORX1=30TO90STEP30:X2=X1+10:GOSUB925:NEXT
16030 X=30:Y1=54:Y2=62:GOSUB910:X=90:GOSUB910:X=132:GOSUB910:X=60:Y1=40:Y2=53:GOSUB910:X=70:Y1=7:Y2=30:GOSUB910:X=100:Y1=42
16040 Y2=48:GOSUB910:X=107:Y1=21:Y2=39:GOSUB910:X1=90:Y1=53:X2=107:Y2=33:GOSUB925:X=107:Y=21:X1=102:X2=107:GOSUB905:X1=102
16050 Y1=28:X2=107:Y2=21:GOSUB925:X=102:Y1=5:Y2=28:GOSUB910:Y=5:X1=42:X2=102:GOSUB905:SET(41,6,2):SET(101,6,2):C=1:X1=122:X2=123
16060 Y1=0:Y2=28:GOSUB915:C=2:X1=132:Y1=40:X2=158:Y2=53:GOSUB915:X2=142:Y2=30:GOSUB925:SET(159,53,2):SET(159,39,2):Y=30
16070 X1=143:X2=159:GOSUB905:Y=7:GOSUB905:X=143:Y1=7:Y2=29:GOSUB910:RETURN
17000 POKE48937,33:POKE48936,130:POKE48936,140:PRINTPEEK(48937):POKE48937,4:POKE48936,132:CLS
17010 C=2:X=30:Y1=53:Y2=83:GOSUB910:X=130:GOSUB910:X1=30:Y1=53:X2=36:Y2=47:GOSUB925:X1=36:Y1=47:X2=47:Y2=46:GOSUB925
17020 Y=45:X1=47:X2=68:GOSUB905:X1=93:X2=113:GOSUB905:X1=113:Y1=45:X2=124:Y2=47:GOSUB925:X1=124:Y1=47:X2=130:Y2=53:GOSUB925
17030 X=71:Y1=30:Y2=45:GOSUB910:X=90:GOSUB910:X=114:Y1=59:Y2=83:GOSUB910:X=46:GOSUB910:X1=47:X2=52:Y1=59:Y2=83:GOSUB925
17040 X1=108:Y1=83:X2=113:Y2=59:GOSUB925:X1=67:Y1=24:X2=77:Y2=37:GOSUB925:X1=77:X2=84:Y=37:GOSUB905
17050 X1=84:Y1=37:X2=94:Y2=24:GOSUB925:X=94:Y1=18:Y2=24:GOSUB910:X=67:GOSUB910:Y1=6:Y2=14:GOSUB910:X=94:GOSUB910
17060 X=95:Y1=14:Y2=18:GOSUB910:X=66:GOSUB910:C=1:Y=15:X1=74:X2=77:GOSUB905:X1=86:X2=89:GOSUB905:SET(75,16,1):SET(87,16,1)
17070 C=2:X1=79:Y1=21:X2=81:Y2=17:GOSUB925:Y1=22:X2=82:Y2=23:GOSUB925:Y=27:X1=77:X2=86:GOSUB905
17080 X1=78:Y1=27:X2=81:Y2=30:GOSUB925:X1=82:Y1=30:X2=85:Y2=27:GOSUB925:X1=77:Y1=46:X2=84:Y2=48:GOSUB915:X1=67:Y1=0:X2=94:Y2=7
17090 GOSUB915:X1=80:X2=81:C=1:FORY1=62TO82STEP4:Y2=Y1+1:GOSUB915:NEXT:C=2:X1=94:Y1=7:X2=104:Y2=11:GOSUB925
17100 X1=67:X2=76:GOSUB925:Y=11:X1=77:X2=103:GOSUB905:X=79:Y1=49:Y2=54:GOSUB910:X=82:GOSUB910:X=68:Y1=45:Y2=51:GOSUB910:X=93
17110 GOSUB910:X1=69:Y1=45:X2=80:Y2=55:GOSUB925:X1=81:Y1=55:X2=92:Y2=45:GOSUB925:X2=92:Y2=53:GOSUB925:X1=69:Y1=53:X2=80:Y2=55
17120 GOSUB925:Y=44:X1=71:X2=90:GOSUB905:X1=95:Y1=59:X2=103:Y2=65:GOSUB915:X=52:Y1=60:Y2=64:GOSUB910:X=64:GOSUB910:X=66:GOSUB910
17130 X=54:GOSUB910:RESET(54,62):Y1=62:X=56:GOSUB910:X=58:GOSUB910:SET(53,62,2):SET(57,61,2):SET(61,61,2)
17140 SET(60,62,2):SET(60,63,2):SET(61,64,2):SET(62,63,2):SET(62,62,2):RETURN
18000 POKE48937,34:POKE48936,130:POKE48936,140:PRINTPEEK(38937):POKE48937,20:POKE48936,132:CLS
18001 IFN=14THENPOKE48937,35:POKE48936,130:POKE48936,140:PRINTPEEK(38937):POKE48937,37:POKE48936,132:CLS
18010 C=2:X1=1:Y1=83:X2=78:Y2=42:GOSUB925:X1=81:X2=152:GOSUB925:X1=118:Y2=62:GOSUB925:X=153:Y1=0:Y2=62:GOSUB910:X=154
18020 GOSUB910:X1=154:Y1=62:X2=159:Y2=60:GOSUB925:Y=42:X1=135:X2=152:GOSUB905:C=1:X1=78:Y1=35:X2=111:Y2=3:GOSUB925
18030 X=108:Y1=6:Y2=12:GOSUB910:X=94:Y1=20:Y2=27:GOSUB910:C=2:X1=2:Y1=42:X2=78:Y2=2:GOSUB925:C=1:X2=135:Y1=40:Y2=41 
18040 FORX1=81TO117STEP3:GOSUB915:Y1=Y1-3:Y2=Y2-3:NEXT:C=2:X1=77:Y1=2:X2=136:Y2=42:GOSUB915::C=1:Y=42:X1=78:X2=135:GOSUB905
18500 RETURN
20000 IFV$="RIEN"ANDC$=""THENPRINT:A$="Ce n'est pas trÑs constructif !":GOSUB9:II=1
20020 IFV$="SE"ANDC$="SUICIDER"THENA$="Bon, vous åtes mort, c'est malin !":GOSUB9:GOTO300
20025 IFC$="PLAN"THENPRINT"Il n'y a pas de plan.":II=1
20030 IFV$="DETRUIRE"ORV$="TUER"ORV$="FRAPPER"ORV$="DEMOLIR"THENPRINT"Gardez votre calme!":II=1
20035 IFC$="GUICHET"THENPRINT"Il n'y a pas de guichet ici.":II=1
20040 IFV$="VOLER"THENPRINT"Ce n'est pas beau de voler.":II=1
20045 IFV$="MANGER"THENPRINT"Vous venez de finir votre croissant, ne me dites pas que vous avez faim.":II=1
20050 IFV$="VENDRE"ANDC$="MONTRE"THEN600
20060 IFV$="INV"THEN500
20065 IF(V$="INTRROGER"ORV$="DEMANDER"ORV$="PARLER"ORV$="QUESTIONNER")AND(C$="PASSANT"ORC$="USAGER"ORC$="AIDE")THEN590
20070 IFV$="REGARDER"AND(C$="MONTRE"ORC$="HEURE")THEN400
20085 IFII=2THEN255
20090 IFII=OTHEN230
20095 GOTO270
