0 CLEAR500:CC=RND(-(PEEK(9)*256+PEEK(10))):ZZ=0:M$="":I$="":MC=16384:GOTO8000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN

8 REM RANDOM EVENTS
9 IFX=1THENONRND(2)GOTO17:RETURN
10 IFRND(8)>1OR(O(17,0)=-X ANDO(17,1)=-Y)THENRETURN
11 ONRND(8)GOTO15,16,17,15,16,17,12,13
12 PRINT"YOU'RE HUNGRY.":RETURN
13 PRINT"YOU'RE THIRSTY.":RETURN
15 O(12,0)=-X:O(12,1)=-Y:RETURN
16 O(14,0)=-X:O(14,1)=-Y:RETURN
17 IFO(21,0)=0THENO(19,0)=-X:O(19,1)=-Y:RETURN
18 GOTO11

19 REM MAIN LOOP
20 FORI=1TO16:PRINT"/"CHR$(92);:NEXT:M$="LOCATION: "+MAP$(X,Y,Z)+".":GOSUB1:IFO(92,0)=1AND(X<>9ANDX<>5)THENGOSUB7040
30 IFMAP$(X,Y,Z)="FOREST"THENGOSUB9:O(7,0)=-X:O(7,1)=-Y
31 IFLEFT$(MAP$(X,Y,Z),4)="LAKE"THENO(18,0)=-X:O(18,1)=-Y
32 O(90,0)=O(90,0)+O(91,0):IFO(90,0)>35THEN7030
39 IFX=3ANDY=3ANDO(24,0)<>99THENPRINT"IT'S TOO DARK TO SEE!":GOTO50
40 I$="":FORI=1TONN:IFABS(O(I,0))=X ANDABS(O(I,1))=Y ANDZ=0 THENI$=I$+N$(I)+", "
45 NEXT:IFI$<>""THENM$="YOU SEE: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1
50 IFX=6ANDY=5ANDO(97,0)=0THENGOSUB7010
100 INPUT"WHAT NOW";A$:IFA$=""THENPRINT"WHAT?":GOTO100
120 IFLEFT$(A$,3)="GO "ANDLEN(A$)=4THENA$=RIGHT$(A$,1)
124 FORI=1TO11:W$(I)="":NEXT:N$="":N2$="":N3$="":V$="":S=1:V=0:O=0:W=1:FORI=1TOLEN(A$)+1
125 IFMID$(A$,I,1)=" "THENW$(W)=MID$(A$,S,I-S):S=I+1:W=W+1:IFW=11THENI=255
126 IFMID$(A$,I,1)=""THENW$(W)=MID$(A$,S)
127 NEXT:IFW=1THENV$=LEFT$(A$,3):W$(2)=V$:GOTO130
128 V$=LEFT$(W$(1),3):N$=LEFT$(W$(2),3):N2$=LEFT$(W$(3),3):N3$=LEFT$(W$(4),3)
130 V=0:FORI=1TO NV:IFV$=V$(I)THENV=I:I=NV
132 NEXT
134 O=0:FORI=1TO NN:IFN$=O$(I)THENO=I:I=NN:GOTO150
136 IFN2$=O$(I)THENO=I:I=NN:W$(2)=W$(3):GOTO150
138 IFN3$=O$(I)THENO=I:I=NN:W$(2)=W$(4)
140 NEXT:IFW$(2)="PEANUTS"THENW$(2)="BAG":O=20
141 IFW$(2)="MAN"THENW$(2)="DEAD":O=4
142 IFW$(2)="GUARD"THENW$(2)="SECURITY":O=17
150 REM PRINTW$(1),W$(2),V$,O$(O),V(V),O
160 ONV(V)GOTO1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300
170 IFA$="HELP"THENGOSUB7900:GOTO20
180 PRINT"I DON'T UNDERSTAND.":GOTO20

999 REM MOVE
1000 A=0:B=0:C=0:D=V-42:IFW>1THEND=O-24:IFD=-17THEND=5
1015 IFD<1ORD>6THEND=RND(4):W$(1)="RUN":W=1:PRINT"NOT SURE WHERE I'M GOING..."
1020 OND GOTO1030,1035,1040,1045,1050,1055
1030 B=-1:IFY+B<1ORZ=1THENB=0
1031 GOTO1060
1035 B=1:IFY+B>8ORZ=1THENB=0
1036 GOTO1060
1040 A=1:IFX+A>9ORZ=1THENA=0
1042 GOTO1060
1045 A=-1:IFX+A<1ORZ=1THENA=0
1047 GOTO1060
1050 C=1:IFZ+C>1ORMAP$(X,Y,Z)<>"FOREST"THENC=0
1051 O(14,0)=0:O(14,1)=0:GOTO1062
1055 C=-1:IFZ+C<0THENC=0
1056 GOTO1060
1060 IF(O(12,0)=-X ANDO(12,1)=-Y)OR(O(14,0)=-X ANDO(14,1)=-Y)THEN7050
1062 IFO(17,0)=-X ANDO(17,1)=-Y ANDW$(1)="RUN"ANDD<5THENO(92,0)=1:O(17,0)=0:O(17,1)=0:?"YOU HEAR SHOUTING BEHIND YOU."
1063 IFO(17,0)=-X ANDO(17,1)=-Y THEN7060
1065 IFX>8ANDY<>5THEN?"I'M SOAKING WET...":O(98,0)=O(98,0)+1:IFO(98,0)=4THENGOSUB7000:GOTO1098
1066 IFX+A=3ANDY+B=3 AND O(99,0)=0THENPRINT"PUMPING STATION'S LOCKED UP TIGHT.":GOTO1097
1067 IF(A=-1ANDX+A=2ANDY<>5)OR(A=1ANDX+A=3ANDY<>5)THENPRINT"PIPELINE'S IN THE WAY":GOTO1097
1068 IFA<>0OR B<>0 ORC<>0THENM$="MOVING: "+W$(2)+".":GOSUB1:X=X+A:Y=Y+B:Z=Z+C:GOTO1098
1069 IF(X=1ORY=8ORY=1)ANDZ=0THENPRINT"I THINK I'M LOST.":GOTO2900
1097 PRINT"CAN'T GO THERE."
1098 GOTO2900

1099 REM GET
1100 IFW=1THEN2500
1106 IFO=32THEN2300
1110 IFABS(O(O,0))=99 THENPRINT"YOU ALREADY HAVE IT.":GOTO2500
1120 F=0:IFABS(O(O,0))=X ANDABS(O(O,1))=Y THENF=1
1130 IFF ANDQ>4THENPRINT"YOU ARE CARRYING TOO MUCH.":GOTO2500
1140 IFF ANDO(O,0)>0THENM$=N$(O)+" TAKEN.":GOSUB1:O(O,0)=99:Q=Q+1:GOTO2500
1150 IFF THENPRINT"THAT'S IMPOSSIBLE.":GOTO2500
1160 GOTO2140

1199 REM PUT
1200 IFW=1THEN2500
1220 IFO(20,0)=99ANDO(19,0)=-X ANDO(19,1)=-Y ANDO(21,0)=0THEN1260
1230 IFO(O,0)=99THENM$="YOU "+W$(1)+" THE "+N$(O):GOSUB1:O(O,0)=X:O(O,1)=Y:Q=Q-1:GOTO2500
1240 PRINT"IT'S NOT HERE":GOTO2500
1260 M$="THE CHIPMUNK TAKES THE BAG AND RUNS.":GOSUB1:O(20,0)=9:O(20,1)=5:O(21,0)=X:O(21,1)=Y:Q=Q-1
1270 O(19,0)=0:O(19,1)=0:GOTO2500

1299 REM VIOLENCE
1300 F=0:IF(ABS(O(O,0))=X ANDABS(O(O,1))=Y)ORO(O,0)=99THENF=1
1305 IFF=0THEN7070
1310 M$="YOUR AGGRESSION PRODUCES NO USEFUL RESULT.":GOSUB1
1320 IFO(17,0)=-X ANDO(17,1)=-Y THENM$="THE SECURITY GUARD SUBDUES YOU.":GOSUB1:GOTO9900
1390 GOTO2500

1399 REM FOOD
1400 F=0:IF(ABS(O(O,0))=X ANDABS(O(O,1))=Y)ORO(O,0)=99THENF=1
1405 IFF=0THEN7070
1410 IFO(20,0)=99ANDO=20THENM$="THEY TASTE GREAT. YUM! YOU REALLY NEVER CAN EAT JUST ONE!":GOSUB1:O(20,0)=9:O(20,1)=5:GOTO2500
1420 IFO(18,0)=-X ANDO(18,1)=-Y AND O=18THEN1480
1470 PRINT"DON'T BE RIDICULOUS.":GOTO2500
1480 M$="THAT'S REFRESHING! BUT IT'LL BE BEAVER FEVER LATER!":GOSUB1:O(91,0)=O(91,0)+1:GOTO2500

1499 REM LOOK
1500 IFW=1THEN2500
1505 IFX=3ANDY=3ANDO(24,0)<>99THENPRINT"IT'S TOO DARK":GOTO2500
1510 F=0:IFABS(O(O,0))=X ANDABS(O(O,1))=Y THENF=1
1511 IFO>24THENF=1:D$(30)="DIRT, MOSS AND GRASS":IFX=9ANDY=5THEND$(30)="FLOOR"
1515 IFABS(O(O,0))=99 THENF=1
1516 IFO=9ANDX=5ANDY=7ANDO(23,0)=0THENPRINT"YOU FIND A MATCHBOOK AMONGST THE";:O(23,0)=5:O(23,1)=7
1520 IFF THENM$=D$(O)+".":GOSUB1:ONO GOSUB7570,7580:GOTO2900
1590 GOTO2140

1599 REM COMMUNICATE
1600 IFO(17,0)=-X ANDO(17,1)=-Y THENM$="THE GUARD APPREHENDS YOU AT GUNPOINT ON SUSPICION OF ECO TERRORISM.":GOSUB1:GOTO9900
1620 IFO(12,0)=-X ANDO(12,1)=-Y ANDO=12THENM$="TALKING TO A MOOSE! THAT'S A FIRST!":GOSUB1:GOTO2500
1630 IFO(14,0)=-X ANDO(14,1)=-Y ANDO=14THENM$="HE AIN'T INSTERESTED IN TALKING!":GOSUB1:GOTO2500
1640 IFO(19,0)=-X ANDO(19,1)=-Y ANDO=19ANDO(21,0)=0THENM$="HE CAN'T TALK. HE'S GOT SOMETHING IN HIS MOUTH.":GOSUB1:GOTO2500
1650 IFO(19,0)=-X ANDO(19,1)=-Y ANDO=19THENM$="HE CHITTERS AWAY HAPPILY!":GOSUB1:GOTO2500
1660 IFX=9ANDY=5THEN1680
1670 PRINT"WHO YOU TALKING TO? YOURSELF?":GOTO2500
1680 M$="HE TELLS YOU TO STAY ON THE PATH AND WATCH OUT FOR BEARS AND MOOSE.":GOSUB1
1690 M$="IF YOU SEE A BEAR, CLIMB A TREE. IF YOU SEE A MOOSE, TRY NOT TO STARTLE IT! HAVE A NICE HIKE.":GOSUB1:GOTO2500

1699 REM QUIT
1700 INPUT"ARE YOU SURE (Y/N)";M$:IFLEFT$(M$,1)="Y"THEN1750
1720 GOTO2500
1750 INPUT"SAVE GAME (Y/N)";M$:IFLEFT$(M$,1)<>"Y"THENEND
1760 INPUT"PRESS RECORD AND HIS ENTER";M$:O(50,0)=X:O(51,0)=Y:O(52,0)=Z:O(53,0)=Q:CSAVE*O,"KINDDATA"
1790 END

1799 REM USE
1800 F=0:IF(ABS(O(O,0))=X ANDABS(O(O,1))=Y)ORO(O,0)=99THENF=1
1805 IFF=0THEN7070
1810 IFO=11ANDO(11,0)=99ANDX<>9ANDY<>5THENM$="YOU DIG A NICE LITTLE HOLE.":GOSUB1:O(10,0)=-X:O(10,1)=-Y:GOTO2500
1820 IFO=3ANDO(3,0)=99THENM$="YOUR RESORT TO GUN VIOLENCE PRODUCES NO USEFUL RESULT. WHAT ARE YOU? AN AMERICAN!":GOSUB1
1821 IFO=3ANDO(3,0)=99THENM$="THE MOUNTIES SHOW UP AND CHARGE YOU WITH ILLEGAL USE OF A FIREARM.":GOSUB1:GOTO9890
1822 IFO=16ANDO(16,0)=99ANDO(23,0)=99THENM$="YOU BLOW YOURSELF TO KINGDOM COME.":GOSUB1:GOTO9890
1830 IFO=2ANDX=3ANDY=3THEN1900
1840 IFO=23ANDO(23,0)=99ANDX=3ANDY=3THENM$="THEY DON'T STAY LIT LONG ENOUGH TO SEE ANYTHING!":GOSUB1:GOTO2500
1850 IFO=23ANDO(23,0)=99THENM$="CAREFUL YOU'LL START A FIRE!":GOSUB1:GOTO2500
1860 IFO=8ANDO(23,0)=99ANDO(8,0)=99THEN1895
1890 PRINT"NOTHING HAPPENS.":GOTO2500
1895 M$="IT'S TOO BURNT. CAN'T LIGHT IT. IT CRUMBLES.":GOSUB1:O(8,0)=0:O(8,1)=0:Q=Q-1:GOTO2500

1899 REM PUSH/PULL
1900 IFW=1THENPRINT"WHAT?":GOTO2500
1905 F=0:IF(ABS(O(O,0))=X ANDABS(O(O,1))=Y)ORO(O,0)=99THENF=1
1910 IFF=0THEN7070
1920 IFO=15ANDX=3ANDY=7ANDO(16,0)=0THEN1995
1930 IFO=2ANDX=3ANDY=3THEN1960
1950 GOTO1890
1960 INPUT"WHICH DIRECTION";M$:F=-1:IFLEFT$(M$,4)="LEFT"THENF=1
1965 IFLEFT$(M$,5)="RIGHT"THENF=2
1970 IFF=O(93,0)THEN9930
1980 IFF>0THEN9870
1990 M$="YOUR OPTIONS ARE LEFT OR RIGHT.":GOSUB1:GOTO2500
1995 M$="WITH MUCH EFFORT YOU MOVE THEM. YOU FIND SOMETHING!":GOSUB1:O(16,0)=3:O(16,1)=7:GOTO2500

1999 REM OPEN/UNLOCK/CLOSE/LOCK
2000 IFX=3ANDY=4ANDO(21,0)=99ANDO(99,0)=1THENPRINT"CLICK. IT'S LOCKED.":O(99,0)=0:GOTO2500
2020 IFX=3ANDY=4ANDO(21,0)=99THENPRINT"CLICK. IT'S UNLOCKED.":O(99,0)=1:GOTO2500
2030 PRINT"CAN'T.":GOTO2500

2099 REM TURN
2100 IFW=1THENPRINT"WEEEEEEEE!!!":GOTO2500
2110 F=0:IF(ABS(O(O,0))=X ANDABS(O(O,1))=Y)ORO(O,0)=99THENF=1
2120 IFF THENIFO=1 THENONO(95,0)GOSUB7510,7520,7530,7540:GOTO2500
2130 IFF THENM$="IT LOOKS THE SAME FROM THAT DIRECTION.":GOSUB1:GOTO2500
2140 PRINT"THAT'S NOT HERE.":GOTO2500

2199 REM STAY/WAIT
2200 M$="YOU STAND PERFECTLY STILL FOR A WHILE.":GOSUB1:FORZZ=1TO2500:NEXT
2220 IFO(12,0)=-X ANDO(12,1)=-Y THENM$="THE MOOSE PASSES YOU BY.":GOSUB1:O(12,0)=0:O(12,1)=0
2290 GOTO2500

2299 REM INVENTORY
2300 I$="":FORI=1TONN:IFABS(O(I,0))=99 THENI$=I$+N$(I)+", "
2320 NEXT:IFI$<>""THENM$="YOU ARE CARRYING: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO2500
2330 PRINT"YOU ARE CARRYING NOTHING.":GOTO2500

2499 REM POST ACTION ROUTINE
2500 IF(O(12,0)=-X ANDO(12,1)=-Y)OR(O(14,0)=-X ANDO(14,1)=-Y ANDZ=0)THEN7050
2520 IFO(17,0)=-X ANDO(17,1)=-Y THEN7060
2900 GOTO20

6999 REM EVENTS AND MESSAGES

7000 PRINT"YOU FAINT FROM HYPOTHERMIA!":X=9:Y=5:Z=0:O(98,0)=0:RETURN

7010 SOUND1,4:M$="YOU HEAR A GUNSHOT TO THE WEST, WHICH CAUSES YOU TO STOP AND LISTEN.":GOSUB1:FORCC=1TO2500:NEXT
7011 M$="A MINUTE OR SO LATER A MAN COMES STUMBLING ALONG THE PATH.":GOSUB1:FORCC=1TO2500:NEXT
7012 M$="HE COLLAPSES IN FRONT OF YOU.":GOSUB1:O(97,0)=1:O(4,0)=-6:O(4,1)=-5:O(1,0)=6:O(1,1)=5
7013 O(3,0)=4:O(3,1)=3:O(5,0)=-4:O(5,1)=-3:O(10,0)=-4:O(10,1)=-3:O(17,0)=-5:O(17,1)=-5:O(11,0)=4:O(11,1)=4
7014 RETURN

7030 M$="IT'S BEAVER FEVER TIME! BACK TO THE HOTEL AND THEN TO THE HOSPITAL...":GOSUB1:GOTO9900

7040 IFRND(10)>1ORZ>0THENRETURN
7041 O(17,0)=-X:O(17,1)=-Y:M$="YOU HEAR A SOUND NEARBY.":GOSUB1:RETURN

7050 M$="IT ATTACKS. YOU ARE HORRIBLY INJURED. A FELLOW HIKER FINDS YOU AND GETS YOU TO THE HOSPITAL.":GOSUB1:GOTO9900

7060 M$="THE GUARD APPREHENDS YOU AT GUNPOINT ON SUSPICION OF ECO TERRORISM.":GOSUB1:GOTO9900

7070 PRINT"IT'S NOT HERE":GOTO2500

7510 PRINT" ооооооооооооо"
7511 PRINT" ооооонлйююооо"
7512 PRINT" ооонхцойююооо"
7513 PRINT" оооаооокюаооо"
7514 PRINT" оонеооооюеооо"
7515 PRINT" оойоооооюеооо"
7516 PRINT" онхлооооюеооо"
7517 PRINT" ообгооооюеооо"
7518 PRINT" оооооооодеооо"
7519 PRINT" ооооооооооооо":RETURN

7520 PRINT" ооооооооооооо"
7521 PRINT" ооооаеооооооо"
7522 PRINT" ооооюеоооохмо"
7523 PRINT" ооооюеооокбцо"
7524 PRINT" ооооюеоооойоо"
7525 PRINT" ооооюеооооаоо"
7526 PRINT" ооонюдооонеоо"
7527 PRINT" ооойююонхцооо"
7528 PRINT" ооокццооооооо"
7529 PRINT" ооооооооооооо":RETURN

7530 PRINT" ооооооооооооо"
7531 PRINT" оооююеооооооо"
7532 PRINT" оооююелмооооо"
7533 PRINT" оообюгоцдмооо"
7534 PRINT" ооойюооообооо"
7535 PRINT" ооойюоооокеоо"
7536 PRINT" ооойюоооооеоо"
7537 PRINT" ооойюооообюго"
7538 PRINT" ооойхооооогоо"
7539 PRINT" ооооооооооооо":RETURN

7540 PRINT" ооооооооооооо"
7541 PRINT" ооооооооаеооо"
7542 PRINT" оохмооооюеооо"
7543 PRINT" окбцооооюеооо"
7544 PRINT" оойоооооюеооо"
7545 PRINT" оокеооооюеооо"
7546 PRINT" ооодооонюдооо"
7547 PRINT" ооокблойююооо"
7548 PRINT" ооооокцйююооо"
7549 PRINT" ооооооооооооо":RETURN

7550 PRINT"    │┘"
7551 PRINT"    ─┘"
7552 PRINT"    ─┘"
7553 PRINT"    ─┘"
7554 PRINT"    ─┘"
7555 PRINT"   ▌─└"
7556 PRINT"   ┼──"
7557 PRINT"   ┼──":RETURN

7560 PRINT"   ──┘"
7561 PRINT"   ──┘"
7562 PRINT"   ┌─┤"
7563 PRINT"   ┼─ "
7564 PRINT"   ┼─ "
7565 PRINT"   ┼─ "
7566 PRINT"   ┼─ "
7567 PRINT"   ┼┬ ":RETURN

7570 IFO(96,0)=0THENGOSUB7590
7571 ONO(96,0)GOSUB7510,7520,7530,7540:RETURN

7580 IFO(96,0)=0THENGOSUB7590
7581 ONO(94,0)GOSUB7550,7560:RETURN

7590 ONRND(4)GOTO7591,7592,7593,7594
7591 O(96,0)=1:O(95,0)=2:O(94,0)=1:O(93,0)=2:RETURN
7592 O(96,0)=2:O(95,0)=1:O(94,0)=2:O(93,0)=1:RETURN
7593 O(96,0)=3:O(95,0)=4:O(94,0)=1:O(93,0)=1:RETURN
7594 O(96,0)=4:O(95,0)=3:O(94,0)=2:O(93,0)=2:RETURN

7900 M$="  YOU ARE VACATIONING IN THE BEAUTIFUL ROCKY MOUNTAINS OF CANADA. YOU DECIDE TO GO ON A HIKE THROUGH THE FOREST."
7910 M$=M$+" YOU HEAD WEST ON THE MAIN TRAIL.":GOSUB1
7930 M$="  IN THE DISTANCE YOU SEE A PIPELINE RUNNING THROUGH THE FOREST. "
7940 M$=M$+"AS YOU DRAW NEARER YOU FIND YOURSELF DRAWN INTO A SERIES OF LIFE-CHANGING EVENTS.":GOSUB1:RETURN

8000 CLS:PRINT@1*32+10,;:M$="KINDER MORGAN":GOSUB7:PRINT:PRINT:GOSUB7900
8005 NV=48:NN=32:DIMV$(NV),V(NV),N$(NN),MAP$(9,8,1),O$(NN),O(99,1),W$(11),D$(NN)
8010 FORI=1TONV:READV$(I):NEXT:FORI=1TONV:READV(I):NEXT:O$(0)="*"
8020 FORI=1TONN:READN$(I),O(I,0),O(I,1):O$(I)=LEFT$(N$(I),3):NEXT
8030 FORY=1TO8:FORX=1TO9:READMAP$(X,Y,Z):MAP$(X,Y,1)="UP A TREE":NEXT:NEXT
8040 FORI=1TONN:READD$(I):NEXT
8090 X=8:Y=5:Z=0
8100 PRINT@448,;:INPUT"CONTINUE PREVIOUS GAME (Y/N)";M$:IFLEFT$(M$,1)<>"Y"THEN8200
8110 PRINT"PRESS PLAY";:CLOAD*O,"KINDDATA":X=O(50,0):Y=O(51,0):Z=O(52,0):Q=O(53,0)
8200 CLS:GOTO20

9000 DATA GO,ENT,RUN,JUM,CLI,SWI,GET,TAK,GIV,DRO,THR,HIT,KIL,SHO,PUN,TAS,EAT,DRI,EXA,LOO,SEA,REA
9010 DATACAL,GRE,ASK,TAL,SAV,QUI,USE,DIG,PUS,PUL,MOV,OPE,UNL,CLO,LOC,TUR,STA,WAI,LIG,INV,N,S,E,W,U,D
9020 DATA  1,  1,  1,  1,  1,  1,  2,  2,  3,  3,  3,  4,  4,  4,  4,  5,  5,  5,  6,  6,  6,  6
9030 DATA  7,  7,  7,  7,  8,  8,  9,  9, 10 ,10, 10, 11, 11, 11, 11, 12, 13, 13,  9, 14,1,1,1,1,1,1

9100 DATAPAPER,0,0,LEVER,-3,-3,GUN,0,0,DEAD MAN,0,0,UNCONSCIOUS MAN,0,0,DOOR,-3,-4
9110 DATATREES,0,0,LOG,5,7,FIRE PIT,-5,-7,DIRT PILE,0,0
9120 DATASHOVEL,0,0,MOOSE,0,0,HOTEL,-9,-5,BEAR,0,0,ROCKS,-3,-7,DYNAMITE STICK,0,0
9130 DATASECURITY GUARD,0,0,WATER,0,0,CHIPMUNK,0,0,BAG OF PEANUTS,9,5,KEY,0,0,SIGN,-8,-5,MATCHES,0,0
9140 DATAFLASHLIGHT,7,7,NORTH,-10,0,SOUTH,-11,0,EAST,12,0,WEST,-13,0,UP,-14,0,DOWN,-15,0,STILL,-16,0,INV,-17,0

9200 REM ROW 1
9201 DATA FOREST
9202 DATA BY A PIPELINE
9203 DATA BY A PIPELINE
9204 DATA FOREST
9205 DATA FOREST
9206 DATA FOREST
9207 DATA FOREST
9208 DATA FOREST
9209 DATA LAKE
9210 REM ROW 2
9211 DATA FOREST
9212 DATA NORTH WEST CORNER OF PUMPING STATION
9213 DATA BEHIND THE PUMPING STATION
9214 DATA NORTH EAST CORNER OF PUMPING STATION
9215 DATA FOREST
9216 DATA FOREST
9217 DATA FOREST
9218 DATA LAKESIDE
9219 DATA LAKE
9220 REM ROW 3
9221 DATA FOREST
9222 DATA WEST SIDE OF PUMPING STATION
9223 DATA INSIDE THE PUMPING STATION
9224 DATA EAST SIDE OF PUMPING STATION
9225 DATA FOREST
9226 DATA FOREST
9227 DATA FOREST
9228 DATA LAKESIDE
9229 DATA LAKE
9230 REM ROW 4
9231 DATA FOREST
9232 DATA SOUTH WEST CORNER OF PUMPING STATION
9233 DATA FRONT OF THE PUMPING STATION
9234 DATA SOUTH EAST CORNER OF PUMPING STATION
9235 DATA HIKING TRAIL
9236 DATA HIKING TRAIL
9237 DATA HIKING TRAIL
9238 DATA LAKESIDE
9239 DATA LAKE
9240 REM ROW 5
9241 DATA HIKING TRAIL
9242 DATA WEST END OF A PEDESTRIAN TUNNEL UNDER THE PIPELINE
9243 DATA EAST END OF A PEDESTRIAN TUNNEL UNDER THE PIPELINE
9244 DATA HIKING TRAIL
9245 DATA HIKING TRAIL
9246 DATA HIKING TRAIL
9247 DATA FORK IN THE TRAIL
9248 DATA HIKING TRAIL
9249 DATA AT THE HOTEL
9250 REM ROW 6
9251 DATA FOREST
9252 DATA BY A PIPELINE
9253 DATA BY A PIPELINE
9254 DATA FOREST
9255 DATA FOREST
9256 DATA FOREST
9257 DATA FOREST
9258 DATA LAKESIDE
9259 DATA LAKE
9260 REM ROW 7
9261 DATA FOREST
9262 DATA BY A PIPELINE
9263 DATA BY A PIPELINE IN A ROCKY GULLY
9264 DATA FOREST
9265 DATA A SMALL CLEARING IN THE FOREST
9266 DATA FOREST
9267 DATA OLD CAMPSITE WITH A VIEW OF THE LAKE
9268 DATA LAKESIDE
9269 DATA LAKE
9270 REM ROW 8
9271 DATA FOREST
9272 DATA BY A PIPELINE
9273 DATA BY A PIPELINE
9274 DATA FOREST
9275 DATA FOREST
9276 DATA FOREST
9277 DATA FOREST
9278 DATA FOREST
9279 DATA LAKE

9300 DATAIT HAS A DIAGRAM,"IT'S LABELED 'SEE PROCEDURE 666'","IT'S AN AUTOMATIC"
9305 DATA"HE'S YOUNG... MAYBE NATIVE. HE'S WEARING AN 'EARTH FIRST' T-SHIRT"
9310 DATA"A SECURITY GUARD WITH A SLIGHT ABRASION ON HIS HEAD. HIS HOLSTER IS EMPTY.","IT'S STEEL AND LABLED: XP33"
9315 DATA"MANY BRANCHES AND STOUT LIMBS","IT'S PARTLY BURNT","ASH, OLD CHARCOALS AND UNBURNT STICKS"
9320 DATA"IT'S RECENTLY DUG","IT'S FROM CANADIAN TIRE. THERE'S A LITTLE BLOOD ON IT","IT LOOKS LIKE IT HASN'T NOTICED YOU"
9325 DATATHE HOTEL SITS ON A NARROW PENINSULA EXTENDING INTO THE LAKE,"IT'S LUNGING AT YOU","THEY'RE FROM BLASTING"
9330 DATA"IT HAS A WICK","HIS BADGE READS 'MEGA OIL CORP.' HE'S HOLDING A GUN AND SEEMS TO BE SEARCHING"
9335 DATA"YOU SEE YOUR REFLECTION","IT'S GOT SOMETHING SILVER IN ITS MOUTH","THEY'RE COMPLIMENTS OF THE HOTEL"
9340 DATA"IT'S COVERED IN CHIPMUNK DROOL. IT'S LABLED: XP33","IT SAYS: entrance MAIN TRAIL. FIRE WATCH LEVEL high"
9345 DATA"IT'S A HALF USED PACK BUT SEEMS OK.",IT APPEARS TO WORK,YOU SEE TREES,YOU SEE TREES
9350 DATALIGHT ON WATER SHIMMERS THROUGH TREES,YOU SEE MOUNTAINS
9355 DATA"I'VE LOOKED AT CLOUDS FROM BOTH SIDES NOW, FROM UP AND DOWN AND STILL SOME HOW, IT'S CLOUD ILLUSIONS I RECALL.."
9365 DATA"","YOU LOOK VERY SMOLDERING","YOU'VE GOT A POCKET FULL OF LINT MY GOOD CHUM"
9366 REM HEY GOOD ONE CHARLIE!

9870 M$="THAT'S THE EMEGENCY RELEASE DIRECTION! YOU'VE CAUSED A MAJOR ENVIRONMENTAL DISASTER!":GOSUB1
9880 CC=VARPTR(O$(0))+65536:ZZ=PEEK(CC+2)*256+PEEK(CC+3):POKEZZ,255

9890 M$="YOU HAVE TERRIBLY SULLIED THE ENVIRONMENTAL CAUSE...":GOSUB1:SOUND50,10:SOUND25,10:SOUND11,10
9900 M$="THE PIPELINE IS EXPANDED. TAR SANDS OIL FLOWS TO WORLD MARKETS. THE PLANET SURPASSES 2 DEGREES OF WARMING.":GOSUB1
9910 PRINT"YOU LOSE. WE ALL LOSE.":GOTO9990

9930 M$="CORRECT!":GOSUB7:IFO$(0)<>"*"THENPRINT" YOU GOT IT EVENTUALLY!":GOTO9940
9935 M$=" AND ON THE FIRST TRY!  ":GOSUB7:SOUND134,1:SOUND141,1:SOUND141,2:SOUND134,1:SOUND119,1:FORZZ=1TO2500:NEXT
9940 M$="THAT'S THE EMERGENCY SHUT-OFF DIRECTION. YOU HAVE SUCCESSFULLY MONKEYWRENCHED A MAJOR OIL CORPORATION!":GOSUB1
9950 M$="THE PIPELINE IS DISRUPTED ONE TOO MANY TIMES. THE COMPANY PULLS OUT ITS INVESTMENT.":GOSUB1
9960 M$="THE PEOPLE OF THE WORLD SLOWLY COME TO THEIR SENSES AND DECREASE THEIR USE OF FOSSIL FUELS.":GOSUB1
9970 M$="MOST OF THE TAR SANDS OIL STAYS IN THE GROUND. YOU WIN! WE ALL WIN!":GOSUB1
9990 INPUT"PLAY AGAIN (Y/N)";M$:IFLEFT$(M$,1)="Y"THENRUN
9995 END

9997 REM JIM GERRIE 2018
9998 REM WITH TESTING BY
9999 REM CHARLIE GERRIE
