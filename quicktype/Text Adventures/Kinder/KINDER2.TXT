0 CLEAR500:ZZ=0:CC=0:M$="":MC=16384:NV=48:NN=32:DIMV$(NV),V(NV),N$(NN),MAP$(9,8,1),O$(NN),O(99,1),W$(11),D$(NN):GOTO8000
1 ZZ=1:CC=32:FORCC=CC TOZZ STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORI=1TO6:PRINTCHR$(ASC(MID$(M$,I,1))-64);:NEXT:PRINT:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN

20 FORI=1TO16:PRINT"/"CHR$(92);:NEXT:M$="LOCATION: "+MAP$(X,Y,Z):GOSUB1:IFO(92,0)AND(X<>9ANDX<>5)THENGOSUB7040
30 IFMAP$(X,Y,Z)="FOREST"THENGOSUB7020
40 I$="":FORI=1TONN:IFABS(O(I,0))=X ANDABS(O(I,1))=Y THENI$=I$+N$(I)+", "
45 NEXT:IFI$<>""THENM$="YOU SEE: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1
50 IFX=6ANDY=5ANDO(97,0)=0THENGOSUB7010

123 INPUT"WHAT NOW";A$:IFA$=""THENPRINT"WHAT?":GOTO123
124 FORI=1TO11:W$(I)="":NEXT:N$="":N2$="":N3$="":V$="":S=1:V=0:O=0:W=1:FORI=1TOLEN(A$)+1
125 IFMID$(A$,I,1)=" "THENW$(W)=MID$(A$,S,I-S):S=I+1:W=W+1:IFW=11THENI=255
126 IFMID$(A$,I,1)=""THENW$(W)=MID$(A$,S)
127 NEXT:IFW=1THENV$=LEFT$(A$,3):W$(2)=V$:GOTO130
128 V$=LEFT$(W$(1),3):N$=LEFT$(W$(2),3):N2$=LEFT$(W$(3),3):N3$=LEFT$(W$(4),3)
130 V=0:FORI=1TO NV:IFV$=V$(I)THENV=I:I=NV
132 NEXT
134 O=0:FORI=1TO NN:IFN$=O$(I)THENO=I:I=NN:GOTO150
136 IFN2$=O$(I)THENO=I:I=NN:W$(2)=W$(3):GOTO150
138 IFN3$=O$(I)THENO=I:I=NN:W$(2)=W$(4)
140 NEXT
150 REM PRINTW$(1),W$(2),V$,O$(O),V(V),O
160 ONV(V)GOTO1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300
170 PRINT"I DON'T UNDERSTAND.":GOTO20

1000 REM MOVE
1010 A=0:B=0:C=0:D=V-42:IFW>1THEND=O-24
1020 OND GOTO1030,1035,1040,1045,1050,1055:STOP
1030 B=-1:IFY+B<1ORZ=1THENB=0
1031 GOTO1060
1035 B=1:IFY+B>8ORZ=1THENB=0
1036 GOTO1060
1040 A=1:IFX+A>9ORZ=1THENA=0
1041 GOTO1060
1045 A=-1:IFX+A<1ORZ=1THENA=0
1046 GOTO1060
1050 C=1:IFZ+C>1ORMAP$(X,Y,Z)<>"FOREST"THENC=0
1051 O(14,0)=0:O(14,1)=0:GOTO1062
1055 C=-1:IFZ+C<0THENC=0
1056 GOTO1060
1060 REM MORE GENERIC CHECKS
1061 IF(O(12,0)=-X ANDO(12,1)=Y)OR(O(14,0)=-X ANDO(14,1)=-Y)THENM$="IT ATTACKS AND YOU ARE HORRIBLY INJURED.":GOSUB1:GOTO9900
1062 IFO(17,0)=-X ANDO(17,1)=-Y ANDW$(1)="RUN"ANDD<5THENO(92,0)=1:O(17,0)=0:O(17,1)=0:?"YOU HEAR SHOUTING BEHIND YOU."
1063 IFO(17,0)=-X ANDO(17,1)=-Y THENM$="THE GUARD APPREHENDS YOU AT GUNPOINT ON SUSPICION OF ECO TERRORISM.":GOSUB1:GOTO9900
1065 IFX>8ANDY<>5THEN?"I'M SOAKING WET...":O(98,0)=O(98,0)+1:IFO(98,0)=4THENGOSUB7000:GOTO1098
1066 IFX+A=3ANDY+B=3 AND O(99,0)THENA=0:B=0:C=0:PRINT"IT'S LOCKED UP TIGHT."
1067 IFA<>0OR B<>0 ORC<>0THENM$="MOVING "+W$(2)+".":GOSUB1:X=X+A:Y=Y+B:Z=Z+C:GOTO1098
1068 IFX=1ORY=8ORY=1THENPRINT"I THINK I'M LOST.":GOTO1098
1097 PRINT"CAN'T GO THERE."
1098 GOTO2500

1100 REM GET
1105 IFW=1THEN2500
1110 IFABS(O(O,0))=99 THENPRINT"YOU ALREADY HAVE IT.":GOTO2500
1120 F=0:IFABS(O(O,0))=X ANDABS(O(O,1))=Y THENF=1
1130 IFF ANDQ>4THENPRINT"YOU ARE CARRYING TOO MUCH.":GOTO2500
1140 IFF ANDO(O,0)>0THENM$=N$(O)+" TAKEN.":GOSUB1:O(O,0)=99:GOTO2500
1150 IFF THENPRINT"THAT'S IMPOSSIBLE.":GOTO2500
1160 PRINT"IT'S NOT HERE":GOTO2500

1200 REM PUT
1205 IFW=1THEN2500
1220 IFO(O,0)=99ANDO(19,0)=-XANDO(19,1)=-Y THEN7060
1230 IFO(O,0)=99THENM$="YOU "+W$(1)+" THE "+N$(O):GOSUB1:O(O,0)=X:O(O,1)=Y:Q=Q-1:GOTO2500
1240 PRINT"IT'S NOT HERE":GOTO2500

1300 REM VIOLENCE
1310 F=0:INPUT"ARE YOU AN AMERICAN";M$:IFM$="Y"ORM$="YES"THENF=1
1320 GOTO2500

1400 REM FOOD
1410 IFO(20,0)=99THENM$="THEY TASTE GREAT. YUM! YUM!":O(20,0)=9:O(20,1)=5:GOTO2500
1420 PRINT"DON'T BE RIDICULOUS.":GOTO2500

1500 REM LOOK
1505 IFW=1THEN2500
1510 F=0:IFABS(O(O,0))=X ANDABS(O(O,1))=Y THENF=1
1515 IFABS(O(O,0))=99 THENF=1
1520 IFF THENM$=D$(O)+".":GOSUB1:ONO GOSUB7570,7580:GOTO2900
1530 PRINT"IT'S NOT HERE":GOTO2500

1600 REM COMMUNICATE
1690 GOTO2500

1700 REM QUIT
1790 GOTO2500

1800 REM USE
1810 IFO=11ANDO(11,0)=99THENM$="YOU DIG A NICE LITTLE HOLE.":GOSUB1:O(10,0)=X:O(10,1)=Y:GOTO2500
1820 PRINT"NOTHING HAPPENS.":GOTO2500

1900 REM PUSH/PULL
1920 IFO=15ANDX=3ANDY=7THENM$="WITH MUCH EFFORT YOU MOVE THEM. YOU FIND SOMETHING!":GOSUB1:O(16,0)=3:O(16,1)=7:GOTO2500
1930 GOTO1820

2000 REM OPEN/UNLOCK/CLOSE/LOCK
2010 IFX=3ANDY=4ANDO(21,0)=99ANDO(99,0)=1THENPRINT"CLICK. IT'S LOCKED":O(99,0)=0:GOTO2500
2020 IFX=3ANDY=4ANDO(21,0)=99THENPRINT"CLICK. IT'S UNLOCKED":O(99,0)=1:GOTO2500
2030 GOTO2500

2100 REM TURN
2105 IFW=1THENPRINT"WEEEEEEEE!!!"
2110 F=0:IFABS(O(O,0))=X ANDABS(O(O,1))=Y THENF=1
2115 IFABS(O(O,0))=99 THENF=1
2120 IFF THENIFO=1 THENONO(95,0)GOSUB7510,7520,7530,7540:GOTO2500
2130 IFF THENM$="IT LOOKS THE SAME FROM THAT DIRECTION.":GOSUB1:GOTO2500
2140 PRINT"THAT'S NOT HERE.":GOTO2500

2200 REM STAY/WAIT
2210 M$="YOU STAND PERFECTLY STILL FOR A WHILE.":GOSUB1:FORZZ=1TO2500:NEXT
2220 IFO(12,0)=-X ANDO(12,1)=-Y THENM$="THE MOOSE PASSES YOU BY.":GOSUB1:O(12,0)=0:O(12,1)=0
2290 GOTO2500

2300 REM INVENTORY
2310 I$="":FORI=1TONN:IFABS(O(I,0))=99 THENI$=I$+N$(I)+", "
2320 NEXT:IFI$<>""THENM$="YOU ARE CARRYING: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO2500
2330 PRINT"YOU ARE CARRYING NOTHING.":GOTO2500

2500 REM POST ACTION ROUTINE
2510 IF(O(12,0)=-X ANDO(12,1)=-Y)OR(O(14,0)=-X ANDO(14,1)=-Y ANDZ=0)THEN7050
2900 GOTO20

7000 PRINT"YOU FAINT FROM HYPOTHERMIA!":X=9:Y=5:Z=0:O(98,0)=0:RETURN

7010 M$="YOU HEAR A GUNSHOT TO THE WEST, WHICH CAUSES YOU TO STOP.":GOSUB1:FORCC=1TO2500:NEXT
7011 M$="YOU LISTEN. A MINUTE OR SO LATER A MAN COMES STUMBLING ALONG THE PATH.":GOSUB1:FORCC=1TO2500:NEXT
7012 M$="HE COLLAPSES IN FRONT OF YOU.":GOSUB1:O(97,0)=-1:O(4,0)=-6:O(4,1)=-5:O(1,0)=6:O(1,1)=5
7013 O(3,0)=4:O(3,1)=3:O(5,0)=-4:O(5,1)=-3:O(10,0)=-4:O(10,1)=-3:O(17,0)=5:O(17,1)=5:O(11,0)=4:O(11,1)=3
7014 RETURN

7020 IFRND(10)>9THENRETURN
7021 ONRND(3)GOTO7025,7026,7027
7025 O(12,0)=-X:O(12,1)=-Y:RETURN
7026 O(14,0)=-X:O(14,1)=-Y:RETURN
7027 IFO(20,0)<>10THENO(19,0)=-X:O(19,1)=-Y
7028 RETURN

7040 IFRND(10)>9THENRETURN
7041 O(17,0)=-X:O(17,1)=-Y:M$="YOU HEAR A SOUND BEHIND YOU.":GOSUB1:RETURN

7050 M$="IT ATTACKS. YOU ARE HORRIBLY INJURED.":GOSUB1:GOTO9900

7060 M$="THE CHIPMUNK TAKES THE BAG AND RUNS.":O(20,0)=10:O(20,1)=10:O(21,0)=X:O(21,1)=Y:GOTO2500

7510 PRINT" ооооооооооооо"
7511 PRINT" ооооонлйююооо"
7512 PRINT" ооонхцойююооо"
7513 PRINT" оооаооокюаооо"
7514 PRINT" оонеооооюеооо"
7515 PRINT" оойоооооюеооо"
7516 PRINT" онхлооооюеооо"
7517 PRINT" ообгооооюеооо"
7518 PRINT" оооооооодеооо"
7519 PRINT" ооооооооооооо":RETURN

7520 PRINT" ооооооооооооо"
7521 PRINT" ооооаеооооооо"
7522 PRINT" ооооюеоооохмо"
7523 PRINT" ооооюеооокбцо"
7524 PRINT" ооооюеоооойоо"
7525 PRINT" ооооюеооооаоо"
7526 PRINT" ооонюдооонеоо"
7527 PRINT" ооойююонхцооо"
7528 PRINT" ооокццооооооо"
7529 PRINT" ооооооооооооо":RETURN

7530 PRINT" ооооооооооооо"
7531 PRINT"▐оооююеооооооо"
7532 PRINT"▐оооююелмооооо"
7533 PRINT"▐оообюгоцдмооо"
7534 PRINT"▐ооойюооообооо"
7535 PRINT"▐ооойюоооокеоо"
7536 PRINT"▐ооойюоооооеоо"
7537 PRINT"▐ооойюооообюго"
7538 PRINT"▐ооойхооооогоо"
7539 PRINT"▐ооооооооооооо":RETURN

7540 PRINT"▐ооооооооооооо"
7541 PRINT"▐ооооооооаеооо"
7542 PRINT"▐оохмооооюеооо"
7543 PRINT"▐окбцооооюеооо"
7544 PRINT"▐оойоооооюеооо"
7545 PRINT"▐оокеооооюеооо"
7546 PRINT"▐ооодооонюдооо"
7547 PRINT"▐ооокблойююооо"
7548 PRINT"▐ооооокцйююооо"
7549 PRINT" ооооооооооооо":RETURN

7550 M$="ооооае":GOSUB3:M$="ооооюе":GOSUB3:M$="ооооюе":GOSUB3:M$="ооооюе":GOSUB3:M$="ооооюе":GOSUB3
7555 M$="ооонюд":GOSUB3:M$="ооойюю":GOSUB3:M$="ооойюю":GOSUB3:RETURN

7560 M$="оооююе":GOSUB3:M$="оооююе":GOSUB3:M$="оообюг":GOSUB3:M$="ооойюо":GOSUB3:M$="ооойюо":GOSUB3
7565 M$="ооойюо":GOSUB3:M$="ооойюо":GOSUB3:M$="ооойхо":GOSUB3:RETURN

7570 IFO(96,0)=0THENGOSUB7590
7571 ONO(96,0)GOSUB7510,7520,7530,7540:RETURN

7580 IFO(96,0)=0THENGOSUB7590
7581 ONO(94,0)GOSUB7550,7560:RETURN

7590 ONRND(4)GOTO7591,7592,7593,7594
7591 O(96,0)=1:O(95,0)=2:O(94,0)=RND(2):ONO(94,0)GOTO7599,7598
7592 O(96,0)=2:O(95,0)=1:O(94,0)=RND(2):ONO(94,0)GOTO7599,7598
7593 O(96,0)=3:O(95,0)=4:O(94,0)=RND(2):ONO(94,0)GOTO7598,7599
7594 O(96,0)=4:O(95,0)=3:O(94,0)=RND(2):ONO(94,0)GOTO7598,7599

7598 O(93,0)=1:RETURN
7599 O(93,0)=2:RETURN

8000 CLS:PRINT"PLEASE WAIT...":FORI=1TONV:READV$(I):NEXT
8010 FORI=1TONV:READV(I):NEXT
8020 FORI=1TONN:READN$(I),O(I,0),O(I,1):O$(I)=LEFT$(N$(I),3):NEXT
8030 FORY=1TO8:FORX=1TO9:READMAP$(X,Y,Z):MAP$(X,Y,1)="UP A TREE":NEXT:NEXT
8040 FORI=1TONN:READD$(I):NEXT
8090 X=8:Y=5:Z=0
8092 O(99,0)=-1:O(98,0)=0:GOTO20

9000 DATA GO,ENT,RUN,JUM,CLI,SWI,GET,TAK,GIV,DRO,THR,HIT,KIL,BRE,SHO,SMA,EAT,DRI,EXA,LOO,SEA,REA
9010 DATACAL,GRE,ASK,TAL,SAV,ABO,QUI,USE,PUS,PUL,MOV,OPE,UNL,CLO,LOC,TUR,STA,WAI,LIG,INV,N,S,E,W,U,D
9020 DATA  1,  1,  1,  1,  1,  1,  2,  2,  3,  3,  3,  4,  4,  4,  4,  4,  5,  5,  6,  6,  6,  6
9030 DATA  7,  7,  7,  7,  8,  8,  8,  9, 10 ,10, 10, 11, 11, 11, 11, 12, 13, 13,  9, 14,1,1,1,1,1,1

9100 DATAPAPER,0,0,LEVER,0,0,HANDGUN,0,0,DEAD MAN,0,0,UNCONSCIOUS MAN,0,0,DOOR,-3,-4
9110 DATATREE,0,0,LOG,0,0,FIRE PIT,-5,-7,DIRT PILE,0,0
9120 DATASHOVEL,0,0,MOOSE,0,0,HOTEL,-9,-5,BEAR,0,0,ROCKS,-3,-7
9130 DATADYNAMITE STICK,0,0,SECURITY GUARD,0,0,LAKE,0,0,CHIPMUNK,0,0,BAG OF PEANUTS,9,5,KEY,0,0,SIGN,-8,-5,MATCHES,0,0
9140 DATAFLASHLIGHT,5,7,NORTH,-10,0,SOUTH,-11,0,EAST,12,0,WEST,-13,0,UP,-14,0,DOWN,-15,0,STILL,-16,0,INV,-17,0

9200 REM ROW 1
9201 DATA FOREST
9202 DATA FOREST
9203 DATA BY A PIPELINE
9204 DATA FOREST
9205 DATA FOREST
9206 DATA FOREST
9207 DATA FOREST
9208 DATA FOREST
9209 DATA LAKE
9210 REM ROW 2
9211 DATA FOREST
9212 DATA SOUTH WEST CORNER OF PUMPING STATION
9213 DATA BEHIND THE PUMPING STATION
9214 DATA NORTH EAST CORNER OF PUMPING STATION
9215 DATA FOREST
9216 DATA FOREST
9217 DATA FOREST
9218 DATA LAKESIDE
9219 DATA LAKE
9220 REM ROW 3
9221 DATA FOREST
9222 DATA WEST SIDE OF PUMPING STATION
9223 DATA INSIDE THE PUMPING STATION
9224 DATA EAST SIDE OF PUMPING STATION
9225 DATA FOREST
9226 DATA FOREST
9227 DATA FOREST
9228 DATA LAKESIDE
9229 DATA LAKE
9230 REM ROW 4
9231 DATA FOREST
9232 DATA SOUTH WEST CORNER OF PUMPING STATION
9233 DATA FRONT OF THE PUMPING STATION
9234 DATA SOUTH EAST CORNER OF PUMPING STATION
9235 DATA HIKING TRAIL
9236 DATA HIKING TRAIL
9237 DATA HIKING TRAIL
9238 DATA LAKESIDE
9239 DATA LAKE
9240 REM ROW 5
9241 DATA HIKING TRAIL
9242 DATA PEDESTRIAN TUNNEL
9243 DATA BY A PIPELINE
9244 DATA HIKING TRAIL
9245 DATA HIKING TRAIL
9246 DATA HIKING TRAIL
9247 DATA FORK IN THE TRAIL
9248 DATA HIKING TRAIL
9249 DATA AT THE HOTEL
9250 REM ROW 6
9251 DATA FOREST
9252 DATA FOREST
9253 DATA BY A PIPELINE
9254 DATA FOREST
9255 DATA FOREST
9256 DATA FOREST
9257 DATA FOREST
9258 DATA LAKESIDE
9259 DATA LAKE
9260 REM ROW 7
9261 DATA FOREST
9262 DATA FOREST
9263 DATA BY A PIPELINE IN A ROCKY GULLY
9264 DATA FOREST
9265 DATA A SMALL CLEARING IN THE FOREST
9266 DATA FOREST
9267 DATA FOREST
9268 DATA LAKESIDE
9269 DATA LAKE
9270 REM ROW 8
9271 DATA FOREST
9272 DATA FOREST
9273 DATA BY A PIPELINE
9274 DATA FOREST
9275 DATA FOREST
9276 DATA FOREST
9277 DATA FOREST
9278 DATA FOREST
9279 DATA LAKE

9300 DATAIT HAS A DIAGRAM,IT'S LABELED EMERGENCY SHUT OFF,IT'S AN AUTOMATIC,"HE'S YOUNG... MAYBE NATIVE"
9310 DATA"A SECURITY GUARD WITH A SLIGHT ABRASION ON HIS HEAD. HIS HOLSTER IS EMPTY.","IT'S STEEL AND LABLED: 33C","MANY STOUT LIMBS"
9320 DATA"IT'S BURNT","FIRE PIT","IT'S RECENTLY DUG","IT'S FROM CANADIAN TIRE","IT LOOKS LIKE IT HASN'T NOTICED YOU"
9325 DATATHE HOTEL SITS ON A NARROW PENINSULA EXTENDING INTO THE LAKE,"IT'S LUNGING AT YOU"
9330 DATA"IT'S SMELLY","IT HAS A WICK","HIS BADGE READS 'MEGA OIL CORP.' HE'S HOLDING A GUN AND SEEMS TO BE SEARCHING"
9335 DATA"IT'S BEAUTIFUL","IT'S GOT SOMETHING SILVER IN ITS MOUTH","THEY'RE COURTESY OF THE HOTEL"
9340 DATA"IT'S COVERED IN SQUIRREL DROOL. IT'S LABLED: 33C","IT SAYS: FIRE WATCH LEVEL high","IT'S A HALF USED PACK. SEEMS OK."
9350 DATAIT APPEARS TO WORK,YOU SEE TREES,YOU SEE TREES,LIGHT ON WATER SHIMMERS THROUGH TREES
9360 DATAYOU SEE MOUNTAINS,THE SUN IS SHINING,"DIRT, MOSS AND GRASS","YOU LOOK VERY SMOLDERING","YOU'VE GOT A POCKT FULL OF LINT"

9900 M$="THE PIPELINE IS EXPANDED. TAR SANDS OIL FLOWS TO WORLD MARKETS. THE PLANET SURPASSES 2 DEGREES OF WARMING.":GOSUB1
9910 PRINT"YOU LOSE. WE ALL LOSE.":GOTO9990
9950 M$="THE PIPELINE IS DISRUPTED ONE TOO MANY TIMES. THE COMPANY PULLS OUT IT'S INVESTMENT.":GOSUB1
9960 M$="SLOWLY THE PEOPLE OF THE WORLD COMES TO THEIR SENSES AND DECREASE THEIR USE OF FOSSIL FUELS.":GOSUB1
9970 M$="MOST OF THE TAR SANDS OIL STAYS IN THE GROUND. YOU WIN! WE ALL WIN!":GOSUB1
9990 PRINT:INPUT"TRY AGAIN (Y/N)";M$:IFM$="Y"ORM$="YES"THENRUN
9995 END
