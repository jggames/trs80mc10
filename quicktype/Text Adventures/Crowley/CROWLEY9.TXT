8310
0 CLS:PRINT"  THE CURSE OF CROWLEY MANOR":CLEAR512:DIMZZ,CC,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORCC=1TOLEN(M$)STEP2:ZZ=VAL(MID$(M$,CC,2)):IFL(ZZ)=-1THENL(ZZ)=R
4 NEXT
5 WD=1:GOTO8200
8 GOSUB1:GOTO200
9 GOSUB1:GOTO8200
10 NR=44:NT=47:NP=6:NV=31:NO=NT+NP
20 DIMD(NR,6),V$(NV),O$(NO),R$(NR),I$(NT),V(NV),L(NT),C(NT)
100 R=1:WD=1
120 FORI=1TONR:READR$(I):NEXT:FORI=1TONV:READV$(I),V(I):NEXT:FORI=1TONO:READO$(I):NEXT
150 FORI=1TONT:READI$(I),L(I),C(I):NEXT:FORI=1TONR:READD(I,1),D(I,2),D(I,3),D(I,4),D(I,5),D(I,6):NEXT:CLS
200 IFWD=0THEN400
210 WD=0:L(1)=R:PRINT:M$=R$(R)+".":IFLEFT$(M$,1)="*"THENM$="IN A"+MID$(M$,2)
220 M$="YOU ARE "+M$:GOSUB1:K=0:PRINT:M$="visible items: ":FORI=2TONT:IFL(I)=RANDL<200THENK=1:M$=M$+I$(I)+", ":L=LEN(M$)
270 NEXT:IFKTHENM$=LEFT$(M$,LEN(M$)-2)+".":IFL>=200THENM$=M$+".."
280 GOSUB1:K=0:PRINT:M$="exits: ":FORI=1TO6:IFD(R,I)<>0THENK=1:M$=M$+O$(NT+I)+", "
330 NEXT:IFKTHENM$=LEFT$(M$,LEN(M$)-2)+"."
340 GOSUB1
400 INPUTC$:L=LEN(C$):IFL=0THENPRINT"EXCUSE ME?":GOTO400
410 IFC$="SEARCH"THENC$="LOOK ROOM":L=9
420 V$="":O$="":O=0:X=0
430 FORI=1TOL:A$=MID$(C$,I,1):A=ASC(A$):IFA>=97ANDA<=122THENA$=CHR$(A-32)
450 IFA$<>" "THEN460
455 IFO$<>""THENCC=I:I=L:NEXT:I=CC:GOTO500
457 X=1:GOTO490
460 IFX=0THENV$=V$+A$:GOTO490
470 O$=O$+A$
490 NEXT
500 VB$=V$:V$=LEFT$(V$,5):NN$=O$:O$=LEFT$(O$,5)
520 FORV=1TONV:IFV$(V)=V$THENCC=V:V=NV:NEXT:V=CC:V=V(V):GOTO600
540 NEXT:M$="I DON'T KNOW THE VERB "+VB$:GOTO9
600 IFO$=""THEN700
610 FORO=1TONO:IFO$(O)=O$THENCC=O:O=NO:NEXT:O=CC:GOTO700
630 NEXT
640 M$="I DON'T KNOW THE OBJECT "+NN$:GOTO9
700 IFO>NT+NPANDV<>7ANDV<>21THEN640
710 IFO<NT+NPANDO>NTTHENIFV<>7ANDV<>21THEN8010
720 IFO>NTTHEN750
730 IFL(O)<>RANDL(O)<>0ANDV<>15THENM$="IT ISN'T HERE.":GOTO9
750 ONVGOTO1000,1000,1000,1000,1000,1000,1100,1200,1300,1400,1500,1800,1900,2000,1700,2200,2300,2400,2500,1600
770 ONV-20GOTO2600,2700,2800,2900,3000,3900
1000 IFD(R,V)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO200
1020 R=D(R,V):WD=1:GOTO8200
1100 IFO=0THEN8050
1101 IFR=3ANDL(33)=3ANDO=33THENR=4:L(33)=R:WD=1:M$="THE DRIVER ASKS, 'WHERE TO, GOVENOR?'":GOTO9
1105 IFO<=NTTHEN8040
1110 V=O-NT:GOTO1000
1200 IFO=0THEN8050
1201 IFO=2THENM$="YUM!":L(2)=-1:IC=IC-1:GOTO9
1298 M$="OK YECHH!":GOTO9
1300 IFO=0THEN8050
1301 IFL(8)=0ANDO=41THEND(R,1)=20:GOTO8000
1302 IFL(8)=0ANDO=28THEND(R,5)=39:B1=1:GOTO8000
1398 GOTO8020
1400 IFR=6THENM$="'HELLO, MR BLACK. THE BODY IS IN THE KITCHEN. INSPECTOR STRADE IS INSIDE.'":GOTO9
1401 IFR=13ANDDA=0THENM$="'MY NAME IS DAVONN. NO MAN MURDERED MY MASTER. NOW IT IS LOOSE!'":DA=1:GOTO9
1402 IFR=27ANDG1=0THENM$="'I AM THE BROTHER OF THE EVIL YOU SEEK. YOU MUST PREPARE TO KILL HIM.'":G1=1:GOTO9
1403 IFR=27ANDG1=1THENM$="'TWO PATHS LEAD FROM HERE. YOU'LL NEED THE CRYSTAL. IT WILL SHOW YOU'":G1=2:D(R,1)=29:D(R,2)=28:GOTO9
1404 IFR=32ANDG2=0THENM$="HE SAYS: 'ONLY ONE COMPOSER DO I KNOW. NAME HIM AND I'LL PLAY FOR YOU.'":G2=1:GOTO9
1405 IFR=39ANDG3=0THENM$="HE SAYS: 'YOU HAVE BUT ONE MOMENT TO STRIKE. YOUR POWER WILL SAVE YOU TILL THEN.'":G3=1:GOTO9
1498 GOTO8000
1500 IFO=0THEN8050
1501 IFO=15ANDK1=0THENM$="SOMETHING WAS LODGED IN THE CRANK.":L(11)=R:K1=1:GOTO9
1502 IFO=15ANDK1>0THENM$="THE VICTROLA BEGINS PLAYING.":K1=2:GOTO9
1503 IFO=16ANDL(32)=0THENM$="THE SCREWS COME OUT.":GOSUB1:M$="1723":GOTO3
1598 GOTO8010
1600 IFR=11ANDK1>1THENM$="THE MUSIC IS MOZART.":GOTO9
1601 IFO=41THENM$="THERE IS A NOISE FROM BEHIND THE WALL.":GOTO9
1602 IFR=33ANDN1=0THENM$="THERE'S A NOISE ABOVE YOUR HEAD.":N1=1:GOTO9
1603 IFR=36ANDN2=0THENM$="A VOICE IN THE DARK WHISPERS: 'HE HAS THE SWORD, BUT IT IS NOT YET CLEANSED.'":N2=1:GOTO9
1698 M$="YOU HEAR NOTHING INTELLIGIBLE.":GOTO9
1700 IFO=0THEN8050
1701 IFR=4ANDO=35THENM$="'RIGHT AWAY SIR!'":CR=1:GOTO9
1702 IFR=26ANDO=39THENM$="THE WEST WALL CRUMBLES EXPOSING A STAIRWAY. A VOICE BOOMS, 'I AM NEAR!'":D(26,5)=27:GOTO9
1703 IFR=32ANDO=7THENM$="THE SPIRIT PLAYS BEAUTIFULLY. A STAIRWAY APPEARS ON THE NORTH WALL.":D(32,5)=33:GOTO9
1798 GOTO8000
1800 IFO=0THEN8050
1801 IFIC>5THENM$="YOU CAN ONLY CARRY 6 ITEMS.":GOTO9
1802 IFO=19ANDGR>1THENM$="THE GROWTH TOWERS OVER YOU... AND THEN DEVOURS YOU!":GOSUB1:GOTO3900
1805 IFC(O)=0THENM$="THAT'S BEYOND YOUR ABILITY.":GOTO9
1810 IFL(O)=0THENM$="YOU ALREADY HAVE IT!":GOTO9
1830 L(O)=0:IC=IC+1:GOTO8000
1900 IFO=0THEN8050
1901 IF(O=29ORO=8ORO=23ORO=25ORO=26ORO=40)ANDV$="THROW"THENM$="IT BOUNCES BACK AND HITS YOUR HEAD.":L(O)=R:IC=IC-1:GOTO2300
1902 IFR=16ANDO=19THENM$="THE GROWTH SLIDES OVER TO THE PLATE, DEVOURS THE FOOD AND GROWS.":GR=1:L(2)=-1
1903 IFR=17ANDO=19THENM$="THE GROWTH SHOOTS AWAY FROM YOU AND BEGINS DEVOURING THE FOOD.":GR=2
1998 L(O)=R:IC=IC-1:GOTO8000
2000 IC=0:I$="you are carrying: ":FORI=1TONT:IFL(I)=0THENI$=I$+I$(I)+", ":IC=IC+1
2030 NEXT:IFIC=0THENI$=I$+"NOTHING. "
2050 M$=LEFT$(I$,LEN(I$)-2)+".":GOTO9
2200 IFO=0THEN8050
2201 IFO=13THENM$="IT READS: 'I AM DOOMED TODAY. THE DEMON IS TRAPPED IN THE HOUSE. HE'LL BE HERE LONG AFTER MY DEATH'":GOTO9
2298 GOTO2890
2300 IFR=30THENM$="THE DEMON DEVOURS YOU.":GOSUB1:GOTO3900
2301 IFR=37ANDR1=0THENM$="THE RAT DODGES YOUR BLOW AND SNEERS.":GOTO9
2302 IFR=43ANDS1=8THEN2360
2303 IFR<42THENM$="THAT WON'T WORK.":GOTO9
2304 M$="THE DEMON SHOUTS, 'YOU STRIKE TOO SOON, MAGGOT. YOU BROKE YOUR MAGIC. AND ALLOW ME THIS TREAT.'":GOSUB1:GOTO3900
2360 FORCC=1TO20:CLSRND(8):NEXT:PRINT@128,; 
2361 M$="YOU MANAGED TO ESCAPE YOUR ENCOUNTER WITH THE DEMON AND SOMEHOW END UP OUTSIDE THE MANOR UNHARMED.":GOSUB1
2362 M$="congratulations! YOU HAVE DEFEATED THE DEMON! JYYM AND NORM THANK YOU FOR PLAYING.":GOSUB1:END
2400 IFO=0THEN8050
2401 IFO=10ANDL(11)=0ANDL1=0THENM$="INSPECTOR STRADE'S BODY FALLS OUT ONTO THE FLOOR.":L(46)=R:L1=1:GOTO9
2402 IFO=38OR(O=21ANDR=21)THENINPUT"WHAT'S THE COMBINATION";M$:ON-(M$<>"5271")GOTO8020:M$="CLICK!":D(R,1)=22:GOTO9
2498 GOTO8010
2500 IFO=0THEN8050
2501 IFO=14THENL(12)=R:GOTO8000
2503 IFO=21ANDR=17THEND(R,3)=18:GOTO8000
2598 GOTO8030
2600 IFO=0THEN5
2601 IFO<>1THEN2650
2602 ON-(R>19)GOTO2611:IFR=1THENM$="03040506182930":GOTO3
2603 IFR=11THENM$="31":GOTO3
2604 IFR=14THENM$="13":GOTO3
2605 IFR=8THENM$="37":GOTO3
2606 IFR=15THENM$="19":GOTO3
2607 IFR=16THENM$="02":GOTO3
2608 IFR=9ANDFO=0THENM$="A TREMENDOUS FORCE THRUSTS YOU AGAINST A WALL. THERE IS A HIDEOUS SMELL.":FO=1:GOTO9
2609 IFR=12THENM$="16":GOTO3
2610 IFR=19THENM$="41":GOTO3
2611 IFR=20ANDL(9)<0THENM$="A TREMENDOUS VOICE BOOMS: 'YOU HAVE NOT THE POWER TO FACE ME YET. BE WARNED!'":L(9)=R:GOTO9
2612 IFR=22ANDW1=0THENM$="A POWERFUL FORCE SLAMS YOU AGAINST A WALL. A HORRIBLE VOICE SHRIEKS!":W1=1:GOTO9
2613 IFR=22ANDW1=1THENM$="YOU ARE THROWN FLAT ON THE FLOOR. A VOICE BELLOWS: 'SOON YOU'LL BE MINE!'":W1=2:GOTO9
2614 IFR=22ANDW1=2THENM$="THE ROOM COOLS OFF AND FILLS WITH SMOKE. YOUR CRUCIFIX IS RED HOT.":W1=3:L(43)=R:GOTO9
2615 IFR=30ANDL(40)<>0THENM$="'IT' IS HERE: 'WHERE IS YOUR WEAPON, MAGGOT?', HE SCREAMS, 'YOU ARE SUCH A FOOL!'":D1=1:GOTO9
2616 IFR=30ANDD1=1THENR=13:GOTO5
2617 IFR=34THENM$="25":GOTO3
2618 IFR=35THENM$="THERE'S A CIRCULAR DEPRESSION IN THE FLOOR.":GOTO9
2619 IFR=37ANDR1=0THENM$="THE RAT LEAPS AND BITES A CHUNK OFF FLESH OFF YOUR ARM.":GOTO9
2620 IFR=38ANDB1=0THENM$="A NORTH STAIRWAY IS BLOCKED BY HUGE WOODEN BEAMS.":GOSUB1:M$="28":GOTO3
2621 IFR=39THENL(39)=R:D(R,3)=40:GOTO5
2650 IF(R=4ORR=5)ANDO=33THENM$="22":GOTO3
2651 IFO=30THENM$="YOU SEE A VIEW OF LONDON AT NIGHT.":GOTO9
2652 IFR=33ANDO=NT+5THENM$="A ROPE HANGS FROM THE CEILING.":D(R,5)=34:GOTO9
2653 IFO>NTTHEN2898
2698 GOTO5
2700 IFO=0THEN8050
2701 IFR=40ANDL(40)=0THENM$="THE SWORD GLEAMS BRIGHTLY. YOU CAN FEEL POWER SURGING WITHIN IT.":S1=1:GOTO9
2798 GOTO8000
2800 IFO=0THEN8050
2801 IFO=36THENM$="THEY HAVE FIENDISH EYES. THEY DON'T LOOK HUMAN.":GOTO9
2802 IFO=31THENM$="IT HAS A CRANK.":L(15)=11:GOTO9
2803 IFO=15THENM$="THE CRANK IS STUCK.":GOTO9
2804 IFO=16THENM$="IT'S FASTENED WITH TWO SCREWS.":GOTO9
2805 M$="IT'S LOCKED.":IFO=10ANDL1=0THEN9
2806 IFO=18THENM$="IT'S ONLY MONEY.":GOTO9
2807 IFO=38THENM$="THE LOCK HAS 4 DIALS ON IT.":GOTO9
2808 IFO=43THENM$="IT FORMS FRIGHTENING SHAPES.":GOTO9
2809 IFO=44THENM$="THEY LIE DECAYING ON THE FLOOR. ONE APPEARS TO HAVE HELD THE CLUB.":GOTO9
2810 IFO=12ANDR=35ANDV1=0THENM$="A VISION FORMS OF A SWORD AND A BEAUTIFUL FOUNTAIN OF GREEN LIQUID.":V1=1:GOTO9
2811 IFO=12ANDR=35ANDV1=1THENM$="A MAGIC SWORD APPEARS FROM A PUFF OF SMOKE.":L(40)=R:V1=2:GOTO9
2812 IFO=39OR(O=45ANDR=27)THENM$="A GLOW SURROUNDS THE FIGURE.":GOTO9
2813 IFO=13THENM$="IT'S THE DIARY OF ADAM CROWLEY. THE DATE IS 1742.":GOTO9
2814 IFO=45ANDR=13THENM$="IT'S TOO DARK TO SEE MUCH. THE FIGURE IS ON THE FLOOR.":GOTO9
2815 IFO=45ANDR=32THENM$="THE FIGURE RESEMBLES THE PORTRAITS BACK IN THE MUSIC ROOM.":GOTO9
2816 IFO=46THENM$="HIS BODY IS TORN TO PIECES.":GOTO9
2817 IFO=19THENM$="IT'S INDESCRIBABLE.":GOTO9
2818 IFO=29THENM$="IT'S A COLT.":GOTO9
2819 IFO=34THENM$="HIS EYES GLOW LIKE FIRE.":GOTO9
2820 IFO=47THENM$="BOY! ARE YOU GRISLEY!":GOTO9
2890 IFO=3ORO=5THENM$="'INSPECTOR BLACK. SCOTLAND YARD'":GOTO9
2891 IFO=4THENM$="'APRIL 2, 1913'":GOTO9
2892 IFO=17THENM$="IT READS 5271.":GOTO9
2893 IFO=9THENM$="THE BOOK READS: 'GAFALA ALONE CAN HELP.'":GOTO9
2898 M$="YOU SEE NOTHING SPECIAL.":GOTO9
2900 GOTO8000
3000 IFO=0THEN8050
3001 SH=SH+1
3002 IFR=37ANDL(29)=0ANDR1=0ANDSH<4THENM$="THE RAT SQUEALS, SPINS, AND DROPS DEAD ON THE FLOOR.":R1=1:I$(27)="DEAD RAT":GOTO9
3003 IFSH>3THENM$="IT'S JAMMED.":GOTO9
3090 IFL(29)<>0ORO=39ORR=13ORR=27THEN8010
3098 PRINT"BANG!":GOTO8200
3900 PRINT:PRINT"HIT y TO TRY AGAIN."
3910 M$=INKEY$:ON-(M$="N")GOTO3920:ON-(M$<>"Y")GOTO3910:RUN
3920 END

8000 M$="OKAY.":GOTO9
8010 PRINT"THAT'S SILLY!":GOTO200
8020 M$="NOTHING HAPPENS.":GOTO9
8030 M$="YOU CAN'T DO THAT NOW.":GOTO9
8040 PRINT"WHO'S YOUR ENGLISH TEACHER?":GOTO200
8050 PRINT"PLEASE GIVE AN OBJECT.":GOTO200

8200 IFR=1ANDPH=0ANDRND(3)=1THEN8521
8201 IFR=4ANDCR>0THENCR=CR+1:ONCRGOTO,8528,8529,8530
8202 ON-(R=6ORR=17ORR=19ORR=21)GOSUB8400:ON-(R=13ORR=27ORR=32)GOSUB8500
8203 IFR=11ORR=32THENL(42)=R
8204 IFR=13ANDDA=1ANDV=4THENM$="DAVONN'S THROAT HAS BEEN RIPPED OUT.":DA=2:GOTO8
8206 IFR=16ANDL(19)=RTHENGR=GR+1:IFGR>2THEN8300
8208 IFR=21AND(L(22)<>0ORL(23)<>0)THENM$="A MONSTROUS ICY CREATURE IS HERE! IT TEARS YOU TO PIECES.":GOSUB1:GOTO3900
8209 IFR=38ANDR1=0THENM$="THE RAT GRABS YOU AND RIPS YOUR THROAT OUT.":GOTO3900
8210 IFR=39AND(L(22)<>0ORL(23)<>0ORL(25)<>0)THENGOSUB10:GOTO8310
8250 ON-(R<42)GOTO200:IFR=42ANDS1=0THENM$="THE DEMON HOWLS: 'YOUR SWORD HAS NO POWER YET YOU FOOL. DIE FOR ME'":GOSUB1:GOTO3900
8251 IFR=42ANDS1=1THENM$="THE DEMON HURLS YOU THRU THE AIR. YOUR BODY STRIKES A WALL. IT SCREAMS A LAUGH.":R=43:S1=2:GOTO8
8252 IFR=43ANDS1=2THENM$="YOUR FACE IS HOT FROM ITS BREATH. IT HOWLS, 'THANK YOU FOR DROPPING IN. FOOL!'":S1=3:GOTO8
8253 IFR=43ANDS1=3THENM$="IT SLAMS YOU TO THE GROUND AND SCREAMS: 'YOU'VE BEEN DUPED. YOU CAN'T KILL ME.'":S1=4:GOTO8
8254 IFR=43ANDS1=4THENM$="YOUR BODY SHAKES WITH CONVULSIONS AS THE DEMON TRIES TO CRUSH YOU.":S1=5:GOTO8
8255 IFR=43ANDS1=5THENM$="THE DEMON TOUCHES THE SWORD AND IT GLOWS WHITE HOT. THE EVIL SCREAMS WITH PAIN.":S1=6:GOTO8
8256 IFR=43ANDS1=6THENM$="IT BELCHES A SHEET OF FLAME WHICH ENGULFS YOUR BODY.":S1=7:GOTO8
8257 IFR=43ANDS1=7THENM$="THE DEMON STARES IN TO YOUR FACE. 'JOIN ME. SPAWN OF DIRT,' AND SHARE IN MY POWER.'":S1=8:GOTO8
8258 IFR=43ANDS1=8THENM$="THE DEMON ROARS, 'NO HOPE NOW, FOOL, YOU HAVE MISSED YOUR MOMENT. FARE WELL TO YOU.'":GOSUB1:GOTO3900
8298 GOTO200

8300 M$="THE GROWTH SHOOTS UNDER A CHINA CABINET AND TIPS IT-- CRASH!":GOSUB1:GR=2:M$="0832":L(19)=17:GOTO3
8310 M$="YOU ARE SMASHED TO THE FLOOR LIKE AN ANT. IT IS HERE. IT SCREAMS: 'WHERE ARE YOUR SACRAMENTS?'":R=13:WD=1:GOTO8
8400 L(21)=R:RETURN
8500 L(45)=R:RETURN

8521 L(33)=3:PH=1:M$="A TELEPHONE ON THE DESK RINGS. A VOICE SAYS, "
8522 M$=M$+"'THIS IS OFFICER STRADE. COME TO CROWLEY ESTATE. THERE'S BEEN A MURDER.'":GOTO8
8528 M$="THE CAB IS PASSING THRU TRAFALGAR SQUARE.":GOTO8
8529 M$="YOU CAN SEE BIG BEN'S TOWER TO THE SOUTH.":GOTO8
8530 M$="THE CAB PULLS INTO A TREE LINED DRIVEWAY.":GOSUB1:R=5:WD=1:L(33)=R:IFL(18)<>0THEN8534
8531 IFL(22)<>0THENL(22)=R 
8532 M$="'HERE WE ARE, SIR.' THE CABBIE TAKES 10 SHILLINGS AND SAYS, 'THANK YOU'":I$(18)="40 SHILLINGS":GOTO8
8534 M$="'SORRY GOVENOR', HE SAYS, 'IF YOU CAN'T PAY, I CAN'T LET YOU OUT.'":GOSUB1:GOTO3900

9001 DATA"* LARGE OFFICE. A LARGE WINDOW IS HERE AND SOME ROSEWOOD FURNITURE"
9002 DATA"ON A LANDING. A STAIRWAY LEADS SOUTH TO THE STREET"
9003 DATA"ON THE BRICK STREET IN FRONT OF SCOTLAND YARD"
9004 DATA"* CAB TRAVELING THRU THE STREETS OF LONDON. IT'S A BEAUTIFUL NIGHT"
9005 DATA"* CAB. IT IS PARKED IN FRONT OF A LARGE MANOR"
9006 DATA"STANDING BY THE FRONT PORCH OF CROWLEY ESTATE. POLICE INSPECTOR HARBOUR IS ON THE PORCH"
9007 DATA"* PLUSH ENTRY HALL. THE FURNISHINGS ARE OPULENT"
9008 DATA"AT THE SOUTH END OF A LONG NORTH/SOUTH HALL"
9009 DATA"IN THE CENTER OF ALONG NORTH/SOUTH HALL"
9010 DATA"AT THE NORTH END OF A LONG NORTH/SOUTH HALL"
9011 DATA"*N ELEGANT MUSIC ROOM"
9012 DATA"* PARLOR. IT HAS VICTORIAN FURNITURE AND PLUSH PERSIAN CARPETS"
9013 DATA"* DIMLY LIT ROOM. A SMALL FIGURE LIES HUDDLED IN THE DARKNESS"
9014 DATA"* BOOK FILLED STUDY. BOOK SHELVES LINE THE WALLS"
9015 DATA"IN THE KITCHEN. THERE IS BLOOD SPLATTERED ON THE WALLS AND A POOL ON THE FLOOR"
9016 DATA"*N EXQUISITE DINING ROOM WITH A HUGE OAK TABLE AND A CRYSTAL CHANDELIER"
9017 DATA"* WELL STOCKED FOOD PANTRY WITH SHELVES AND SHELVES OF FOOD. A DOOR IS EAST"
9018 DATA"* VERY SHORT EAST/WEST HALL"
9019 DATA"* SMALL STORAGE ROOM. A NAILED DOOR IS SOUTH"
9020 DATA"* DARKENED ROOM STREWN WITH SCIENTIFIC INSTRUMENTS. THERE'S A HOLE IN THE SOUTH WALL"
9021 DATA"* DINGY LOOKING ROOM. AN ANCIENT NUMERAL LOCK IS ON A DOOR ON THE NORTH WALL"
9022 DATA"* DAMP BRICK ROOM. A STAIRWAY IS EAST. THERE IS A TERRIBLE STENCH"
9023 DATA"AT THE SOUTH END OF A NORTH/SOUTH HALL"
9024 DATA"IN THE CENTRE OF A NORTH/SOUTH HALL"
9025 DATA"AT THE NORTH END OF A NORTH/SOUTH HALL"
9026 DATA"* STARK ROOM. THE WALLS, CEILING, AND FLOOR ARE ALL RED BRICK"
9027 DATA"* BRILLIANT CRYSTAL ROOM. A TOWERING FIGURE IN WHITE STANDS IN THE CENTRE"
9028 DATA"*N EAST/WEST HALL"
9029 DATA"* SHORT HALLWAY"
9030 DATA"* LOW CEILINGED CRYPT. IT IS FULL OF DUST AND COBWEBS"
9031 DATA"*N EMPTY ROOM WITH THE GRAFFITI 'JIM AND CHARLIE GERRIE WERE HERE' ON THE WALL"
9032 DATA"* DIM SHIMMERING ROOM. A GHOSTLY FIGURE IS SEATED AT A CONCERT PIANO"
9033 DATA"* LARGE DESERTED ROOM. THERE IS NOTHING IN VIEW"
9034 DATA"* DAMP MUSTY PASSAGE. A ROPE HANGS THROUGH A HOLE IN THE FLOOR"
9035 DATA"* GREAT SILVER ROOM. THE WALLS ARE A SHIMMERING HAZE"
9036 DATA"* WIDE, DARK, SMELLY PIT. A LOW WAILING MOAN PERMEATES THE DARKNESS. A STAIRWAY IS NORTH"
9037 DATA"* STONE ROOM STREWN WITH ANIMAL BONES"
9038 DATA"* SMELLY PIT WITH A DIRT FLOOR. FUNGUS GROWS ON THE WALLS."
9039 DATA"* VERY EERIE CHAMBER. THE WALLS ARE PAINTED BLACK, AND STRANGE SYMBOLS ARE DRAWN ON THE FLOOR"
9040 DATA"* BRILLIANT GREEN ROOM. THE LIGHT MAKES YOU SQUINT. THERE IS A BEAUTIFUL FOUNTAIN HERE"
9041 DATA"* GIGANTIC CAVERN STRETCHING EAST AND WEST. THE ODOR IS SICKENING"
9042 DATA"AT THE GATES OF HELL IN A MAZE OF PASSAGES. 'IT' IS UPON YOU"
9043 DATA"ON A HIGH PINNACLE OVERLOOKING THE BURNING PITS OF HELL. THE DEMON IS OUTLINED IN FLAMES"
9044 DATA"END"

9050 DATAN,1,S,2,E,3,W,4,U,5,D,6,GO,7,EAT,8,CHOP,9,TALK,10,UNSCR,11,TURN,11,TAKE,12,GET,12,DROP,13,THROW,13,INVEN,14,I
9060 DATA14,SAY,15,READ,16,KILL,17,UNLOC,18,OPEN,19,LISTE,20,LOOK,21,SEARC,21,CLEAN,22,EXAMI,23,WAIT,24,SHOOT,25,QUIT,26

9100 DATAROOM,FOOD,NAMEP,CALEN,CARD,PHONE,MOZAR,AXE,BOOK,CABIN,KEY,BALL,DIARY,DESK,CRANK,CHEST,NOTE,SHILL,GROWT,TABLE
9101 DATADOOR,VIAL,CRUCI,ROPE,SHIELD,CLUB,RAT,BEAMS,REVOL,WINDO,VICTR,OPENE,CAB,DEMON,CROWL,PORTR,STATU,LOCK,GAFAL
9102 DATASWORD,WALL,PIANO,MIST,CORPS,FIGUR,BODY,BLOOD,NORTH,SOUTH,EAST,WEST,UP,DOWN

9150 DATAROOM,-1,-1,PLATE OF DELICIOUS FOOD,-1,,NAMEPLATE,-1,1,CALENDAR,-1,1,"I.D. CARD",-1,1,PHONE,-1,,MOZART,-1,
9151 DATAHAND AXE,-1,1,ANCIENT BOOK,-1,1,CABINET,7,,KEY,-1,1,CRYSTAL BALL,-1,1,DIARY,-1,,DESK,14,,CRANK,-1,,CHEST,-1,
9152 DATAOLD NOTE,-1,,50 SHILLINGS,-1,1,BROWN SLIMEY GROWTH,-1,1,TABLE,16,,DOOR,-1,,VIAL OF HOLY WATER,-1,1,CRUCIFIX,-1,1
9153 DATAROPE,-1,,GOLD SHIELD,-1,1,SILVER CLUB,30,1,HUGE UGLY RAT,37,,BEAMS,-1,,REVOLVER,-1,1,WINDOW,1,,VICTROLA,-1,
9154 DATALETTER OPENER,-1,1,HANSOM CAB,-1,,DEMON,43,,CROWLEY MANOR,-1,,OLD PORTRAITS,11,,STATUE OF WHITE ELEPHANT,-1,1
9155 DATALOCK,21,,GAFALA,-1,,SILVER SWORD,-1,1,PLYWOOD WALL,-1,,PIANO,11,,EVIL MIST,-1,,CORPSES,30,,FIGURE,13,
9156 DATABODY,-1,,BLOOD,15,

9201 DATA,2,,,,
9202 DATA1,3,,,,
9203 DATA2,,,,,
9204 DATA,,,,,
9205 DATA,,,6,,
9206 DATA7,,5,,,
9207 DATA,6,8,12,,
9208 DATA9,,,7,,
9209 DATA10,8,,,,
9210 DATA15,9,,,,
9211 DATA12,,,,,
9212 DATA13,11,7,,,
9213 DATA,12,14,,,
9214 DATA,,,13,,
9215 DATA,10,,16,,
9216 DATA17,,15,,,
9217 DATA,16,,,,
9218 DATA,,19,17,,
9219 DATA,,,18,,
9220 DATA,19,,21,,
9221 DATA,,20,,,
9222 DATA,21,,,,23
9223 DATA24,,,,22,
9224 DATA25,23,,,,
9225 DATA,24,,26,,
9226 DATA,,25,,,
9227 DATA,,,,,26
9228 DATA27,,,30,,
9229 DATA36,27,,,,
9230 DATA31,,28,32,,
9231 DATA,30,,,,
9232 DATA,,30,,,
9233 DATA,,,,,32
9234 DATA,,35,,,33
9235 DATA,,,34,,
9236 DATA,29,,,,37
9237 DATA,,,38,36,
9238 DATA,,37,,,
9239 DATA41,,,,,38
9240 DATA,,,39,,
9241 DATA42,39,,,,
9242 DATA,41,43,,,
9243 DATA,,,,,
9244 DATA,,,43,,
