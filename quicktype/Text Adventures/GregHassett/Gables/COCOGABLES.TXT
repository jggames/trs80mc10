0 CLEAR250:DIMC1,CC,M$:SCREEN0,1:POKE17026,70:GOTO6
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>"" AND MID$(M$,CC,1)<>" " THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$) THEN2
4 RETURN
6 CLS:PRINT"   THE HOUSE OF SEVEN GABLES":PRINT:PRINT"        BY GREG HASSETT":PRINT"COCO & DRAGON EDITS BY J. GERRIE";
7 A$="I DON'T SEE IT HERE.":R$="DON'T BE RIDICULOUS.":Y$="I DON'T KNOW THAT WORD.":CP=2:GOTO255
8 GOTO247
9 IFRND(100)=50 AND ZZ>2 THEN215
10 IFRND(40)=1 AND CP<>2 AND CP<>5 AND CP<>21 AND CP<>41 THEN223
11 GOSUB235:T=T+1:IFT=50 THENOB$(27)="A TARNISHED OLD COMPASS"
12 IFNO>40 THENNO=NO-40
13 IFVB<>25 AND VB>10 AND NO$="" THENINPUT"GIVE ME AN OBJECT";NO$:GOSUB244
14 IFNO$<>"" AND NO=0 AND VB=1 THENPRINT"I DON'T UNDERSTAND THAT!":GOTO11
15 IFT>175 THENMI=MI-10
16 IFT>225 THENMI=MI-15
17 IFNO=0 AND NO$<>"" THENPRINTY$:GOTO11
18 IFVB<>1 THEN32
19 IFVB=1 AND NO$="" THENINPUT"WHERE";NO$:GOSUB244
20 IFNO>40 THENNO=NO-40
21 IFNO<7 THEN24
22 AB=INT(OB(NO,0)):IF(AB>10 AND AB<18) OR ABS(CP-AB)>3 OR CP=2 OR AB=0 THENM$="I DON'T KNOW HOW TO APPLY THAT WORD HERE.":GOSUB1:GOTO11
23 CP=AB:GOTO8
24 IFOB(27,0)<>-1 THENM$="I CAN'T UNDERSTAND COMPASS POINTS WITHOUT A COMPASS!":GOSUB1:GOTO11
25 IFP(CP,NO-1)=0 THENM$="THERE IS NO WAY TO GO IN THAT DIRECTION.":GOSUB1:GOTO11
26 IFCP=2 AND NO=2 THENM$="THE DOOR IS LOCKED. I THINK I CAN GET BY IT, THOUGH.":GOSUB1:GOTO11
27 IFCP=6 AND NO=3 AND DF=0 THENM$="THE DOOR TO THAT WAY IS CLOSED.":GOSUB1:GOTO11
28 IFCP=9 AND OB(9,0)=9 AND NO=1 THENM$="THE VAMPIRE WON'T LET ME GO NORTH!":GOSUB1:GOTO11
29 IFCP=38 AND NO=6 AND OB(18,0)<>0 THENP(5,0)=2
30 IFCP=5 AND FF=1 AND P(5,0)=2 AND NO=1 THENCLS:PRINT"YOU MADE IT!":GOTO230
31 CP=P(CP,NO-1):GOTO8
32 IFVB<>2 THEN48
33 IFNO$="" THENINPUT"GET WHAT";NO$:GOSUB244
34 IFOB(NO,0)=-1 THENPRINT"I'M ALREADY CARRYING THAT!":GOTO11
35 IFNO=7 AND CP=2 THENPRINT"IT IS IMPOSSIBLE TO REMOVE.":GOTO11
36 IFNO=0 THENPRINTY$:GOTO11
37 IFOB(NO,0)<>CP THENPRINTA$:GOTO11
38 IFNO=31 THENPRINTR$:GOTO11
39 IFNO=9 THENPRINT"YOU CAN'T BE SERIOUS!":GOTO11
40 IFNO=40 THENM$="I CANNOT PULL IT OUT OF ITS SOCKET.":GOSUB1:GOTO11
41 IFNO=14 THENM$="MY, THAT'S HEAVY. TOO HEAVY TO CARRY, IN FACT.":GOSUB1:GOTO11
42 IFNO=16 THENM$="IT IS IN A BIG POT THAT I CAN'T LIFT.":GOSUB1:GOTO11
43 IFNO=22 THENPRINT"BE SERIOUS.":GOTO11
44 IFNO=26 THENM$="THE CAT VANISHED, SAYING "+CHR$(34)+"I WILL RETURN..."+CHR$(34):GOSUB1:OB(26,0)=RND(40):GOTO11
45 IFZZ>5 THENM$="I CAN'T CARRY ANY MORE. CLUE: DROP SOMETHING.":GOSUB1:GOTO11
46 ZZ=ZZ+1
47 OB(NO,0)=-1:PRINT"YOU GOT IT.":GOTO11
48 IFVB=3 THEN8
49 IFVB<>4 THEN53
50 ZZ=0:PRINT"CURRENTLY I AM CARRYING:":FORI=8 TOLO:IFOB(I,0)=-1 THENM$=OB$(I):GOSUB1:ZZ=ZZ+1
51 NEXTI:IFZZ=0 THENPRINT"NOTHING!"
52 GOTO11
53 IFVB<>5 THEN58
54 J=0:FORI=1 TOLO:IFOB(I,0)=5 THENJ=J+OB(I,1)
55 NEXTI:J=J+MI
56 M$="OUT OF A MAXIMUM OF 160 POINTS, YOU HAVE "+STR$(J)" POINTS.":GOSUB1
57 GOTO11
58 IFVB<>6 THEN65
59 IFNO$="" THENINPUT"DROP WHAT";NO$:GOSUB244
60 IFOB(NO,0)<>-1 THENPRINT"I AM NOT HOLDING IT.":GOTO11
61 PRINT"ALL RIGHT."
62 ZZ=ZZ-1
63 OB(NO,0)=CP
64 GOTO11
65 IFVB=7 THENM$="I CANNOT GIVE YOU HELP. IT IS AGAINST MY RELIGION.":GOSUB1:GOTO11
66 IFVB<>8 THEN73
67 CLS:INPUT"PRESS <ENTER> WHEN CASSETTE     IS READY FOR SAVING";XX$
68 OB(41,0)=CP:OB(42,0)=FF:OB(43,0)=ZZ:OB(44,0)=T:OB(45,0)=P(5,0):OB(46,0)=DF
69 OPEN "O", #1,"GABLEDAT/DAT":FORCC=0TO1:FORC1=0TO46:WRITE #-1,OB(C1,CC):NEXT:NEXT:CLOSE#1
72 GOTO11
73 IFVB<>9 THEN80
74 CLS:INPUT"PRESS <ENTER> WHEN CASSETTE     IS READY FOR LOADING";XX$
75 OPEN "I", #1,"GABLEDAT/DAT":FORCC=0TO1:FORC1=0TO46:INPUT#1,OB(C1,CC):NEXT:NEXT:CLOSE#1
76 CP=OB(41,0):FF=OB(42,0):ZZ=OB(43,0):T=OB(44,0):P(5,0)=OB(45,0):DF=OB(46,0)
79 GOTO8
80 IFVB<>10 THEN84
81 INPUT"DO YOU REALLY WANT TO QUIT NOW? ";QN$
82 QN$=LEFT$(QN$,1):IFQN$="N" THEN11
83 GOTO230
84 IFVB<>11 THEN91
85 IFOB$(NO)="" AND NO<>7 THENPRINTR$:GOTO11
86 IFNO=7 THEN89
87 IFOB(NO,0)<>-1 THENPRINTA$:GOTO11
88 IFNO<>7 THENM$="I CAN'T RING "+OB$(NO)+"!":GOSUB1:GOTO11
89 IFCP<>2 THENPRINT"I SEE NO BELL HERE!":GOTO11
90 GOTO530
91 IFVB<>12 THEN98
92 IFNO=8 AND OB(10,0)<>0 THENPRINT"IT IS ALREADY FULL.":GOTO11
93 IFOB(NO,0)<>-1 THENPRINT"I'M NOT HOLDING IT RIGHT NOW.":GOTO11
94 IFNO<>8 AND NO<>29 THENPRINT"I CANT FILL THAT!":GOTO11
95 IFCP<>3 THENM$="THERE IS NOTHING TO FILL IT WITH HERE.":GOSUB1:GOTO11
96 IFNO=29 THENM$="NO, I VALUE MY LIFE AND THAT FLUID THAT WAS IN THERE MIGHT REACT WITH THE WATER...":GOSUB1:GOTO11
97 IFNO=8 THENPRINT"YOUR BUCKET IS NOW FULL.":OB(8,0)=0:OB(10,0)=-1:GOTO11
98 IFVB<>13 THEN104
99 IFNO<>27 AND NO<>26 AND NO<>9 AND NO<>13 AND NO<>31 THENPRINT"IT ISN'T ALIVE!":GOTO11
100 IFOB(NO,0)<>CP THENPRINTA$:GOTO11
101 IFNO=9 THENM$="I DON'T THINK IT WOULD APPROVE.":GOSUB1:GOTO11
102 IFNO=27 THENM$="IT DOESN'T TAKE TOO FAVORABLY TO THE IDEA.":GOSUB1:GOTO11
103 M$="YOU'LL HAVE TO BE MORE SPECIFIC.":GOSUB1:GOTO11
104 IFVB<>14 THEN110
105 IFNO=0 THENPRINT"THROW WHAT?":GOTO11
106 IFOB(NO,0)<>-1 THENPRINT"I'M NOT HOLDING IT!":GOTO11
107 IFNO<>24 AND NO<>10 THEN60
108 IFCP=38 AND NO=10 THEN550
109 GOTO60
110 IFVB<>15 THEN116
111 IFNO=0 THENPRINT"SHOW WHAT?":GOTO11
112 IFOB(NO,0)<>-1 THENPRINT"I'M NOT CARRYING IT!":GOTO11
113 IFNO<>11 THENPRINT"NOTHING HAPPENED.":GOTO11
114 IFOB(9,0)<>25 THENPRINT"NOTHING EXCITING HAPPENED.":GOTO11
115 M$="THE VAMPIRE SCURRIES OFF SAYING: YOU HAVEN'T SEEN THE END OF ME YET!":GOSUB1:OB(9,0)=9:GOTO11
116 IFVB<>16 THEN123
117 IFNO<>9 AND NO<>13 AND NO<>31 THENPRINT"THAT IS STUPID.":GOTO11
118 IFOB(NO,0)<>CP THENPRINTA$:GOTO11
119 IFOB(23,0)<>-1 THENM$="I DON'T HAVE ANY FOOD, STUPID!":GOSUB1:GOTO11
120 IFNO=9 AND OB(23,0)<>-1 THENPRINT"HE ONLY LIKES BANANAS.":GOTO11
121 IFNO=9 THENM$="HE WHARFS DOWN YOUR BANANA AND EATS THE PEEL, TOO!":GOSUB1:OB(23,0)=0:GOTO11
122 M$="SHE GRABS THE BANANA AND THROWS IT INTO THE BREW.":GOSUB1:OB(23,0)=0:GOTO11
123 IFVB<>17 THEN129
124 IFOB(NO,0)<>-1 AND OB(NO,0)<>CP THENPRINT"I DON'T SEE IT HERE.":GOTO11
125 IFNO=11 THENM$="I DON'T WANT TO HAVE BAD BREATH.":GOSUB1:GOTO11
126 IFNO<>12 AND NO<>23 THENPRINTR$:GOTO11
127 IFOB(NO,0)<>-1 THENPRINT"I'M NOT CARRYING IT.":GOTO11
128 PRINT"MUNCH MUNCH MUNCH. GOOD.":OB(23,0)=0:OB(24,0)=-1:GOTO11
129 IFVB<>18 THEN141
130 IFNO=0 THENM$="JE NE COMPRENDS PAS. (I DON'T UNDERSTAND).":GOSUB1:GOTO11
131 IFNO<>14 AND NO<>22 AND NO<>33 THENM$="THAT IS BEYOND MY CAPABILITIES.":GOSUB1:GOTO11
132 IFCP<>25 AND NO=14 THENM$="I SEE NO COFFIN TO OPEN HERE!":GOSUB1:GOTO11
133 IFCP=25 AND OB(9,0)=CP AND NO=14 THENM$="THE VAMPIRE WON'T LET ME!":GOSUB1:GOTO11
134 IFNO=14 AND CP=25 THEN500
135 IFNO<>22 THEN139
136 IFCP=2 THENM$="IT IS LOCKED. MAYBE THESE PEOPLE(?) ARE FRIENDLY, SO TRY RINGING THE BELL.":GOSUB1:GOTO11
137 IFCP=6 THEN502
138 PRINT"I SEE NO DOOR HERE.":GOTO11
139 IFOB(33,0)=-1 THENPRINT"TRY UNFOLD AIRPLANE.":GOTO11
140 PRINT"I HAVE NO AIRPLANE.":GOTO11
141 IFVB<>20 THEN150
142 IFNO=22 AND (CP=2 OR CP=6) THEN145
143 IFNO=0 THENM$="CHOP WHAT? I DON'T UNDERSTAND!":GOSUB1:GOTO11
144 IFOB(NO,0)<>CP THENPRINT"I DON'T SEE IT HERE.":GOTO11
145 IFOB(39,0)<>-1 THENINPUT"WITH WHAT, MY BARE HANDS";BH$:IFLEFT$(BH$,1)="Y" THENPRINT"I DON'T KNOW KARATE!":GOTO11
146 IFOB(39,0)<>-1 THENPRINT"ALL RIGHT THEN!":GOTO11
147 IFNO<>22 THENPRINT"THAT IS BEYOND MY POWER.":GOTO11
148 IFCP<>2 AND CP<>6 THENPRINT"I SEE NO DOOR HERE.":GOTO11
149 M$="THE DOOR IS CHOPPED TO PIECES.":GOSUB1:DF=1:GOTO11
150 IFVB<>19 THEN158
151 IFNO<>22 AND NO<>38 AND NO<>14 THENPRINT"THAT IS RIDICULOUS.":GOTO11
152 IFNO=14 AND CP<>25 THENM$="I SEE NO COFFIN TO CLOSE HERE.":GOSUB1:GOTO11
153 IFNO=14 AND CP=25 THENPRINT"THE COFFIN IS CLOSED.":CF=0:GOTO11
154 IFNO=22 AND CP=2 THENPRINT"IT IS ALREADY CLOSED.":GOTO11
155 IFNO=22 AND CP=5 THENPRINT"THE DOOR VANISHED, REMEMBER?":GOTO11
156 IFNO=22 AND CP=6 AND DF=1 THENM$="CLOSING A BUNCH OF WOOD SCRAPS IS BEYOND BY POWER.":GOSUB1:GOTO11
157 IFNO=22 THENPRINT"I SEE NO DOOR HERE.":GOTO11
158 IFVB<>21 THEN163
159 IFNO=0 THENPRINTY$:GOTO11
160 IFNO<>15 THENM$="WHY DO YOU KEEP TELLING ME TO DO DUMB THINGS?":GOSUB1:GOTO11
161 IFOB(15,0)<>-1 THENPRINT"I'M NOT HOLDING THEM!":GOTO11
162 M$="OKAY... POOF! THE CHEMICALS THRANSFORMED THEMSELVES INTO AN ORANGE BUBBLING LIQUID.":GOSUB1:CF=1:GOTO11
163 IFVB<>22 THEN179
164 IFNO<>16 AND NO<>10 AND NO<>28 AND NO<>15 THENPRINT"YOU CAN'T BE SERIOUS.":GOTO11
165 IFNO=16 AND CP=38 THENM$="I DON'T THINK I SHOULD. (THERE'S AN OLD SHOE IN IT!)":GOSUB1:GOTO11
166 IFNO=10 AND OB(10,0)=-1 THENPRINT"OKAY! GULP GULP GULP.":OB(10,0)=0:OB(8,0)=-1:GOTO11
167 IFNO=28 AND OB(28,0)=-1 THEN504
168 IFNO<>28 OR OB(28,0)<>-1 THEN177
169 FORI=8 TOLO
170 IFOB$(I)="" THEN173
171 IFOB(I,0)<>-1 AND OB(I,O)<>5 THEN173
172 OB(I,0)=RND(38)
173 NEXTI
174 OB(28,0)=0:OB(29,0)=-1
175 ZZ=1
176 GOTO11
177 IFNO=15 THENM$="I DON'T WANT TO TRY THAT.":GOSUB1:GOTO11
178 PRINT"I CAN'T DO THAT YET.":GOTO11
179 IFVB<>23 THEN185
180 IFNO=22 AND (CP=2 OR CP=6) THEN182
181 IFOB(NO,0)<>-1 AND OB(NO,0)<>CP THENPRINT"I DON'T SEE IT HERE.":GOTO11
182 IFNO<>9 AND NO<>13 AND NO<>31 THENM$="I DON'T WANT TO HIT THE "+NO$+".":GOSUB1:GOTO11
183 IFNO=9 THENM$="HIT HIM YOURSELF! I'M SURE NOT GONNA!":GOSUB1:GOTO11
184 M$="YUK! SHE'S GOT WARTS ALL OVER HER!":GOSUB1:GOTO11
185 IFVB=24 THENM$="ALL RIGHT..."+NO$:GOSUB1:GOTO11
186 IFVB=25 THENPRINT"YOU HAVE USED"+STR$(T)+" TURNS SO FAR.":GOSUB1:GOTO11
187 IFVB<>26 THEN193
188 IFNO<>10 AND NO<>28 AND NO<>15PRINT"DON'T BE DUMB.":GOTO11
189 IFNO=10 AND OB(10,0)=-1 THENM$="YOUR BUCKET IS EMPTY AND THE GROUND IS WET.":GOSUB1:OB(10,0)=0:OB(8,0)=-1:GOTO11
190 IFNO=28 THENM$="YOU ARE STUCK WITH AN EMPTY TEST TUBE NOW.":GOSUB1:OB(28,0)=0:OB(29,0)=-1:GOTO11
191 IFNO=15 AND OB(15,0)=-1 THEN506
192 M$="I AM NOT CARRYING IT RIGHT NOW.":GOSUB1:GOTO11
193 IFVB=27 THENPRINT"I'M NOT ROCKY, YOU KNOW.":GOTO11
194 IFVB<>29 THEN198
195 IFNO<>33 THENPRINT"GIVE ME A BREAK!":GOTO11
196 IFOB(33,0)<>-1 THENM$="I'M NOT HOLDING AN AIRPLANE!":GOSUB1:GOTO11
197 PRINT"OK.":OB(33,0)=0:OB(34,0)=-1:GOTO11
198 IFVB<>28 THEN205
199 IFOB(NO,0)<>CP AND OB(NO,0)<>-1 THENM$="I DON'T SEE IT AND I'M NOT HOLDING IT.":GOSUB1:GOTO11
200 IFNO<>34 AND NO<>32 AND NO<>19 THENPRINT"THERE IS NO WRITING THERE.":GOTO11
201 IFOB(NO,0)<>-1 THENPRINT"I DON'T HAVE IT WITH ME.":GOTO11
202 IFNO=32 THENPRINT"IT SAYS: NOTTUB SSERP":GOTO11
203 IFNO=34 THENM$="IT READS: REMEMBER THE WIZARD OF OZ.":GOSUB1:GOTO11
204 M$="THEY ARE WRITTEN IN WITCHISH.":GOSUB1:GOTO11
205 IFVB=30 THENPRINT"TRY OPEN.":GOTO11
206 IFVB<>31 THEN212
207 IFOB(NO,0)<>CP THENPRINTA$:GOTO11
208 IFNO<>9 AND NO<>31 THENPRINT"BE SERIOUS.":GOTO11
209 IFOB(35,0)<>-1 THENPRINT"I DON'T HAVE A KNIFE!":GOTO11
210 IFNO=31 THENM$="THE KNIFE VANISHES WHEN IT TOUCHES HER.":GOSUB1:OB(35,0)=0:GOTO11
211 M$="THE VAMPIRE DIES AND THEN VANISHES.":GOSUB1:OB(9,0)=0:GOTO11
212 IFNO<>40 THENPRINT"THAT'S IDIOTIC.":GOTO11
213 IFCP<>37 THENPRINT"I SEE NO BUTTON HERE.":GOTO11
214 CP=38:GOTO8
215 M$="OH MY! A GHOST POPS OUT AND SAYS: THROW ME SOME TREASURE!":GOSUB1:GOSUB235
216 IFVB<>16 OR NO<>13 THEN218
217 GOTO508 
218 IFVB<>14 THENPRINT"HE IS GETTING MAD!":GOSUB235
219 IFVB<>14 THENPRINT"HE HAS KILLED ME!":GOTO230
220 IFOB(NO,0)<>-1 THENPRINT"I'M NOT HOLDING IT.":GOSUB235:GOTO218
221 IFOB(NO,1)=0 THENM$="HE GROWLS: NOT GOOD ENOUGH! AND THROWS IT BACK.":GOSUB1:GOSUB235:GOTO218
222 M$="HE SNATCHES THE TREASURE AND RUNS AWAY.":GOSUB1:ZZ=ZZ-1:OB(NO,0)=0:GOTO11
223 M$="HEAVENS! A ONE EYED GHOUL POPPED OUT!":GOSUB1
224 INPUT"WHAT AM I TO DO";CM$:GOSUB236
225 IFVB<>14 OR NO<>15 THEN229
226 IFOB(15,0)<>-1 THENPRINT"I DON'T HAVE THEM!":GOTO229
227 IFCF=0 THENPRINT"THEY WEREN'T MIXED!":GOTO229
228 PRINT"THE GHOUL MELTS TO NOTHING.":OB(15,0)=CP:GOTO11
229 PRINT"THE GHOUL KILLS ME!":GOTO230
230 J=0:FORI=8 TOLO:IFOB(I,0)=5 THENJ=J+OB(I,1)
231 NEXTI
232 J=J+MI
233 M$="OUT OF A MAXIMUM OF 160 POINTS, YOU SCORED "+STR$(J)+" POINTS, USING "+STR$(T)+" TURNS.":GOSUB1:GOTO510
235 INPUT"WHAT SHOULD I DO";CM$
236 NO$="":VB$="":VB=0:NO=0
237 LC=LEN(CM$):FORI=1 TOLC:IFMID$(CM$,I,1)<>" " THENVB$=VB$+MID$(CM$,I,1):NEXT:V1$=VB$:GOTO239
238 I=LC:NEXT:V1$=VB$
239 VB$=LEFT$(VB$,3)
240 FORI=1 TONV:IFVB$(I)=VB$ THENVB=I:I=NV:NEXT:GOTO242
241 NEXT:VB=1:NO$=VB$:GOTO244
242 IFLEN(V1$)+1>=LEN(CM$) THENNO=0:RETURN
243 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
244 NO$=LEFT$(NO$,3):FORI=1 TONN:IFNO$=NO$(I) THENNO=I:I=NN:NEXT:RETURN
245 NEXT:NO=0:RETURN
247 CLS
248 M$=P$(CP):GOSUB1
249 FORI=8 TOLO:IFOB(I,0)=CP THENM$="THERE IS "+OB$(I)+" HERE.":GOSUB1
250 NEXTI
251 FL=0:IFOB(27,0)<>-1 THEN253
252 PRINT"I CAN GO -- ";:GOSUB520
253 PRINT"--------------------------------";
254 GOTO9
255 NN=46:LO=40:P=40:NV=32:DIM NO$(NN),OB$(LO),OB(46,1),P$(P),P(P,5),VB$(NV)
256 P$(1)="I'M AT THE SECOND GABLE.":DATA0,4,0,0,0,0
257 P$(2)="I'M OUTSIDE A HOUSE WITH SEVEN GABLES. THERE IS A DOORBELL BESIDE THE DOOR.":DATA0,5,0,0,0,0
258 P$(3)="I'M IN THE KITCHEN. THERE IS A SINK WITH A WORKING SPICKET HERE.":DATA0,6,0,0,0,0
259 P$(4)="I'M IN THE GUEST ROOM. (I DON'T WANT TO BE A PERMANENT GUEST!)":DATA1,0,5,8,0,0
260 P$(5)="I'M IN THE LIVING ROOM.":DATA0,9,6,4,0,0
261 P$(6)="I'M IN THE DINING ROOM.":DATA3,0,7,5,0,0
262 P$(7)="I'M AT THE BOTTOM OF A STAIRWELL.":DATA0,0,0,6,29,0
263 P$(8)="I'M AT THE FIRST GABLE. IT OVERLOOKS A HUGE YARD OF TALL GRASS.":DATA0,0,4,0,0,0
264 P$(9)="I'M AT THE TOP OF A STAIRCASE.":DATA5,0,0,10,0,20
265 P$(10)="I'M IN A DARK STORAGE CLOSET. A HOLLOW VOICE SAYS: MIX THEM.":DATA0,0,9,0,0,0
266 P$(11)="I'M IN A MAD SCIENTIST'S LABORATORY.":DATA0,0,0,0,0,12
267 P$(12)="I'M IN A MAZE OF HALLWAYS.":DATA0,16,12,12,11,15
268 DATA0,0,0,0,0,0,0,0,0,0,0,0
269 P$(15)=P$(12):DATA15,17,15,16,0,0
270 P$(16)=P$(12):DATA15,16,12,16,16,16
271 P$(17)=P$(12):DATA15,0,18,0,0,0
272 P$(18)="I'M AT THE WEST END OF THE HALLWAY":DATA19,0,23,17,0,0
273 P$(19)="I'M IN A DUNGION. A CRUDE NOTE ON THE WALL READS: YOU CAN'T GET OUT WITHOUT KILLING ME FIRST!  --WITCHY-POO"
274 P$(20)="I'M AT THE BOTTOM OF A STAIRCASE.":DATA0,18,19,19,19,19,0,23,0,0,9,0
275 DATA0,0,0,0,0,0
276 P$(22)="I'M IN A BARREN LIBRARY.":DATA0,24,0,0,0,0
277 P$(23)="I'M IN THE CENTER OF A LONG E/W HALLWAY":DATA20,0,24,18,0,0
278 P$(24)="I'M AT THE EAST END OF THE HALLWAY.":DATA22,0,25,23,0,0
279 P$(25)="I'M IN DRACULA'S CHAMBER":DATA0,0,0,24,0,0
280 P$(26)="I'M IN A BROOM CLOSET. EXCITING.":DATA0,0,0,27,0,0
281 P$(27)="I'M AT THE FOURTH GABLE":DATA28,0,0,26,0,0
282 P$(28)="I'M AT GABLE FIVE.":DATA0,27,29,0,0,0
283 P$(29)="I'M AT THE TOP OF A STAIRCASE.":DATA0,0,0,28,0,32
284 P$(30)="I'M IN A BEDROOM.":DATA0,0,31,33,0,0
285 P$(31)="I'M IN A DUSTY OLD CHAMBER. LOOKS LIKE A DILAPIDATED BEDROOM.":DATA0,34,32,30,0,0
286 P$(32)="I'M AT THE SIXTH GABLE.":DATA0,0,0,31,0,0
287 P$(33)="I'M AT THE THIRD GABLE.":DATA0,0,34,30,0,0
288 P$(34)="I'M AT ONE END OF A HALLWAY.":DATA31,40,35,0,0,0
289 P$(35)="I'M IN THE MIDDLE OF A HALLWAY.":DATA0,0,36,34,0,0
290 P$(36)=P$(34):DATA32,0,37,35,0,0
291 P$(37)="I'M IN A ROOM WITH A PEDESTAL IN THE MIDDLE. ATOP THE PEDESTAL IS A LARGE BUTTON.":DATA0,0,0,36,0,39
292 P$(38)="I'M IN THE WITCH'S CHAMBER.":DATA0,0,0,0,0,5
293 P$(39)="I'M AT THE SEVENTH GABLE":DATA37,0,0,0,0,0
294 P$(40)="I'M IN A MAMMOTH ART GALLERY.":DATA34,0,0,0,0,0
295 OB$(8)="AN EMPTY BUCKET":OB$(9)="A MEAN LOOKING VAMPIRE":OB$(10)="A BUCKET OF WATER":DATA6,0,25,0,0,0
296 OB$(11)="SOME FRESH GARLIC":DATA3,0
297 OB$(14)="A COFFIN":OB$(15)="SOME CHEMICALS":DATA25,0,11,0
298 OB$(16)="A POT OF WITCHES' BREW":DATA38,0
299 OB$(18)="A BLACK HAT":OB$(19)="SOME VALUABLE RECIPES":DATA0,50,26,15
300 OB$(20)="SOME SILVER CANDLESTICKS":DATA6,10
301 OB$(23)="A BANANA":OB$(24)="A BANANA PEEL":DATA3,0,0,0
302 OB$(25)="A SPARKLING DIAMOND":OB$(26)="A BLACK CAT":DATA39,15,1,0
303 OB$(27)="A SHINY COMPASS":DATA2,0
304 OB$(28)="A TEST TUBE OF FLUID":OB$(29)="AN EMPTY TEST TUBE":DATA33,0,0,0
305 OB$(30)="A BEAUTIFUL ROSE":OB$(31)="AN UGLY WITCH":OB$(32)="A BOOK TITLED PRIMEVAL WITCHCRAFT":DATA29,10,38,0,22,0
306 OB$(33)="A PAPER AIRPLANE":OB$(34)="A PIECE OF PAPER WITH WRITING ON IT":DATA32,0,0,0
307 OB$(35)="SULTAN'S DAGGER":OB$(36)="A PRICELESS REMBRANDT":DATA0,35,40,20
308 OB$(39)="AN OLD RUSTY AXE":DATA15,5
309 FORI=1 TOP:FORJ=0 TO5:READP(I,J):NEXTJ,I
310 FORI=8 TOLO
311 IFOB$(I)="" THEN313
312 READOB(I,0),OB(I,1)
313 NEXTI
314 DATA NOR,SOU,EAS,WES,UP,DOW,BEL,BUC,VAM,WAT,GAR,FOO,GHO,COF,CHE,BRE,CUP,HAT,REC,CAN,KEY,DOO,BAN,PEE,DIA,CAT,COM,FLU,TUB
315 DATA ROS,WIT,BOO,AIR,PAP,DAG,REM,CAG,GAT,AXE,BUT,N,S,E,W,U,D
316 DATA GO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,RIN,FIL,KIL,THR,SHO,FEE,EAT,OPE,CLO,CHO,MIX,DRI,HIT,SAY,TUR,DUM,FIG,REA,UNF
317 DATA UNL,STA,PRE
318 FORI=1 TONN:READNO$(I):NEXTI:FORI=1 TONV:READVB$(I):NEXTI:GOTO8
500 PRINT"THE COFFIN IS OPEN":CF=1:IFOB(35,0)=0 THENOB(35,0)=25
501 GOTO11
502 M$="I CANNOT. THERE IS NO DOORKNOB AND NO KEYHOLE. HOWEVER, IT IS QUITE POSSIBLE THAT I CAN GET BY IT.":GOSUB1
503 GOTO11
504 M$="ALL RIGHT. POOF! OH MY! WHERE AM I? OH MY! ALL MY POSSESIONS WERE LOST IN THE DENSE CLOUD OF GREASY BLACK SMOKE."
505 GOSUB1:GOTO168
506 M$="THE CHEMICALS REACTED WITH THE DUST AND I GOT LOST IN A THICK CLOUD OF ORANGE SMOKE. I SUGGEST YOU 'LOOK'."
507 GOSUB1:CP=12:GOTO11
508 IFOB(23,0)=0 THENPRINT"I HAVE NO FOOD.":GOTO218
509 PRINT"HE EATS THE BANANA AND VANISHES.":OB(23,0)=0:OB(24,0)=CP:GOTO11
510 PRINT:INPUT"DO YOU WISH TO PLAY THIS        ADVENTURE AGAIN";AG$
511 AG$=LEFT$(AG$,1):IFAG$="Y" THENRUN
512 IFAG$<>"N" THEN230
513 SOUND1,1:POKE17026,0:END
520 FORI=0 TO5:IFP(CP,I)<>0 THENPRINTNO$(41+I);"  ";:FL=1
521 NEXTI
522 IFFL=0 THENPRINT"NOWHERE!"
523 PRINT:RETURN
530 M$="THE DOOR FLIES OPEN AND I AM SWEPT INSIDE, BUT THEN THE DOOR VANISHES LEAVING NO VISIBLE WAY OUT!":GOSUB1
531 FF=1:CP=5:GOTO11
550 PRINT"THE WITCH HAS VANISHED!":OB(18,0)=38:PRINT"HER HAT REMAINS.":OB(31,0)=0:OB(10,0)=0:OB(8,0)=-1:GOTO11
