2 CLS:CLEAR2000:PRINT 
5 PRINT"      *  P Y R A M I D *":PRINT:PRINT
10 DIMD$(15),P(29,6),O$(210),L(40),F(40),S(40)
30 FORX=1TO15:READ D$(X):NEXT:FOR X=1TO29:FOR Y=0TO5:READP(X,Y):NEXTY,X
40 DATAARCHEOLOGIST'S HUT,PATH,DESERT,PYRAMID STEPS
50 DATA"DEEP CAVERN - STEEP ROCK WALLS
60 L=1:K=3:DATABOAT,ANCIENT CAVE,EMPTY TREASURE ROOM,ALTAR
70 DATADUSTY CELLER,THRONE ROOM,HALL OF COLUMNS,NARROW LEDGE,CRYPT
80 DATAEMPTY ROOM-RECTANGULAR PATTERN OF LINES ON THE FLOOR
90 DATA2,,,,,,4,1,3,3,,,3,3,2,2,,
100 DATA,2,,,,,,,,,,,,,,,,5
110 DATA6,,,,8,,,,,,,7,,,,,,8
120 DATA,,,,14,,,,,,,,11,,,,,
130 DATA,,,,15,,22,,,,,,28,,,,,
140 DATA,17,28,,,,16,18,19,,,,,,17,19,,
150 DATA,18,20,17,,,,21,23,19,,,20,23,22,,,
160 DATA,,,21,,,24,21,,20,,,25,23,,,,
170 DATA26,24,,,,,27,25,,,,,,26,,28,,
180 DATA,29,27,16,,,28,,,,,
190 W$="GOLOTADRDIPUOPLIJUBUSHREGETISCSAHISMBRPR"
200 FORX=0TO39:READO$(X):O$=O$+LEFT$(O$(X),2):IFX>5THENREADL(X-6)
220 NEXTX:DATANORTH,SOUTH,EAST,WEST,UP,DOWN
230 DATAKNAPSACK,1,SHOVEL,1,AARDVARK,3,VINES,4
240 DATATINCAN,30,MATCHES,31,SWORD,8
250 DATAMONKEY,3,BANANAS,3,ROCKS,5,DAGGER - JEWELED HANDLE,31
260 DATAAMULET,31,BULLETS,31
270 DATAPISTOL,30,KEY,31,SKELETON,18,DEATHMASK OF GOLD,31
280 DATAGUNPOWDER,31,FLASHLIGHT,30,STREAM,5,SES,31,WA,31
290 DATAHEAVY DOOR,4,INSCRIPTION,17,SIGN,1,GRAFITTI,26
300 DATAMUMMY - WON'T LET ME PASS,22,HOLE-VERY SMALL-IN FLOOR,15
310 DATAKILLER ANTS,11,BOAT IN THE SAND,5,ALTAR,8
320 DATA SARCOPHAGUS,14,CHASM-WIDE AND SURROUNDING ME,13,AS,31
325 GOTO580 
330 INPUT"TELL ME WHAT TO DO";A$:B$="":S=99
332 IFA$="N"ANDLEN(A$)=1THENA$="GO":B$="NO"
333 IFA$="S"ANDLEN(A$)=1THENA$="GO":B$="SO"
334 IFA$="E"ANDLEN(A$)=1THENA$="GO":B$="EA"
335 IFA$="W"ANDLEN(A$)=1THENA$="GO":B$="WE"
337 IFA$="U"ANDLEN(A$)=1THENA$="GO":B$="UP"
338 IFA$="D"ANDLEN(A$)=1THENA$="GO":B$="DO"
340 IFS<>99THENIFLEN(A$)>1THENB$=LEFT$(A$,2)
350 FORX=1TOLEN(A$)
360 IFMID$(A$,X,1)=" "ANDLEN(A$)>X+1THENB$=MID$(A$,X+1,2)
370 NEXT:F=0
380 IFLEFT$(A$,3)="INV"THEN1230
390 IFLEN(A$)>1THENA$=LEFT$(A$,2)
400 FOR X=1 TO LEN(O$) STEP 2:IF MID$(O$,X,2)=B$ THEN S=((X+1)/2)-1
410 NEXT:RETURN
420 IFL(2)=11ANDL(28)=11THENPRINT"GOBBLE":L(28)=31:P(11,1)=12
430 IF L=4 OR L=12 THEN L(22)=L
440 IF RND(8)>2 THEN L(7)=L(8):IF L(8)=0ORL(8)=30THENL(7)=L
450 GOSUB 330:IF W>0 THENIFL=5THEN 1530
460 FORX=1TOLEN(W$)STEP2:IFMID$(W$,X,2)=A$THENF=(X+1)/2
470 NEXT:PRINT
480 IF S=32 AND F<>10 AND (LEN(O$(32))<200)THENO$(32)="VERY MAD "+O$(32):GOTO420
485 L(22)=4:IFL=12THENL(22)=L
490 ON(F)GOTO530,830,680,1160,1380,1010,1250,1190,1430,1500,520,830,680
500 IF F>13 THEN ON F-13 GOTO 790,1580,1270,1700,1700,1700,1700
510 PRINT"I DON'T KNOW HOW":GOTO420
520 PRINT"NO EFFECT":GOTO420
530 IFS<6THENIFP(L,S)>0THENL=P(L,S)
540 IFS=25THENPRINT"FLOWING TOO FAST":GOTO420
545 IF(S=37)THENIF(FA<>0)THENL=10:GOTO580
550 IF S=36 AND (L=8ORL=16)THENL=L+1:IFL=17THENL=10
560 IFS=38ANDL=13THENPRINT"TOO STEEP FOR ME"
570 IFL=5ANDS=35THENL=6:GOTO580
580 IF O=1 AND (L(18)=L) OR (L(18)=0) THEN 600
590 IFL>6THENPRINT"TOO DARK. I CAN'T SEE":GOTO420
600 IF L=26 OR L=24 THEN PRINT"FLOOR FEELS FUNNY"
610 IFL>15THENPRINT"I AM IN TWISTING CORRIDOR":GOTO630
620 PRINT"I AM IN "D$(L) :PRINT
630 Y=1:PRINT"I SEE ";
632 FOR X=0 TO33
634 IF (L(X)=L)THENPRINT(O$(X+6)):Y=0
640 NEXT:IF Y=1THENPRINT"NOTHING"
645 IFL=11 THENPRINT"CHASM"
650 IF L=25 THEN FOR X=1 TO 50:PRINT"I'M FALLING":NEXT:PRINT"I'M DEAD":END
660 PRINT:PRINT"OBVIOUS EXITS ARE ":FORX=0TO5:IFP(L,X)>0THENPRINT" * ";O$(X);
670 NEXT:PRINT:GOTO420
680 IFS=35ANDL=5THENPRINT"MUCH TO HEAVY"
690 IF(S>24)AND(S<>39)OR(S<6)THENPRINT"CAN'T":GOTO420
695 IFS=22 AND L(16)=L THEN FA=1
700 IFC>3THENPRINT"HANDS FULL!":GOTO420
710 IFS=39ANDL(11)=31ANDL=22THENL(11)=L:PRINT"TINKLE":GOTO420
730 IF(L(S-6)<>L)AND(L(S-6)<>30)THENPRINT"DON'T SEE IT HERE":GOTO420
740 IFL(S-6)=30ANDL(0)THENPRINT"NOT HERE":GOTO420
750 IFS=13THENPRINTO$(13)"WONT LET ME":GOTO420
760 IFS=21ANDL(14)=31THENPRINT"TINKLE":L(14)=18
770 IFL(S-6)=30THENK=K-1
780 L(S-6)=0:C=C+1:GOTO420
790 IFS<>9THENPRINT"CAN'T":GOTO420
800 INPUT"TO WHAT";A$:GOSUB340
810 IFS<>14ORL(8)<>0THENPRINT"NOT HERE":GOTO420
820 O$(9)=O$(9)+" TIED TO "+O$(14):B=1:GOTO420
830 IFS=99THEN580
840 PRINT" I SEE":IFL=5ANDS=4THENPRINTD$(7)" HIGH UP":GOTO420
850 IFS=33ANDL=15THENPRINT"A LOCK MECHANISM!":GOTO420
860 IFS=6ANDL(0)=0THEN990
870 IFS=3ANDL=21THENPRINTO$(14)
880 IFS<6THENIFP(L,S)>15THENPRINT"WINDING CORRIDORS":GOTO420
890 IFS<6THENIFP(L,S)<>0THENPRINTD$(P(L,S)):GOTO420
900 IFS=39ANDL=22ANDASC(O$(32))=65THENPRINT"SOMETHING GLEAMS":GOTO420
910 IFS=12ANDL(6)=0THENPRINT"PROP, OF ALI BABBA":GOTO420
920 IFS=25ANDL=5THENPRINT"FLOWS INTO HOLE IN WALL":GOTO420
930 IFS=38ANDL(10)=31ANDL=13THENPRINTO$(16);D$(11)" BEYOND":GOTO420
940 IFS=37ANDL=14THENPRINT"CAT=DBU":GOTO420
950 IFS=29ANDL=17THENPRINT"XBMMT IBWF TFDSFUT":GOTO420
960 IFS=31ANDL=26THENPRINT"UISFF USFBTVSFT IFSF":GOTO420
970 IFS=30ANDL=1THENPRINT"BRING TREASURE HERE.  TYPE 'SCORE'":GOTO420
980 PRINT"NOTHING SPECIAL":GOTO420
990 FOR X=1 TO 30:IF L(X)=30 THEN PRINT O$(X+6)
1000 NEXT:GOTO420
1010 IF S<6 OR S>40 THEN PRINT "WHAT?":GOTO 420
1020 IFS=27ANDL<>29THENPRINT"NOTHING YET":GOTO420
1030 IFS=27THENPRINT"IT SWINGS!":P(29,1)=15:GOTO420
1040 IFL(S-6)<>0THENPRINT"DON'T HAVE IT":GOTO420
1050 INPUT"WHERE";A$:IFS=6THENL(0)=L:GOTO420
1060 Y=S:GOSUB340:IFS=6ANDL(0)=0ANDK<7THENL(Y-6)=30:K=K+1:C=C-1:GOTO420
1070 IFY=15AND(S=25ORB$="HO")THENW=1:C=C-1:L(9)=31:GOTO0420
1080 IF S=10ANDL(4)=0ANDL(5)=0THENL(5)=31:C=C-1:GOTO420
1090 IF S=38 AND B=1 AND((Y=9)OR(Y=14))THENC=C-1:GOTO1560
1100 IFS<>33THENS=Y:GOTO1160
1110 IFY<>23ANDY<>11THENPRINT"WON'T FIT":GOTO420
1120 IFY=23THENP=P+1:L(17)=31
1130 IFY=11THENP=P+10:L(5)=32
1140 IFP>10THENPRINT"EXPLOSION":P(15,5)=13:O$(33)="BIG HOLE"
1150 C=C-1:GOTO420
1160 IF S<6 OR S>35 THEN PRINT"HUH?":GOTO420
1170 IFL(S-6)=0THENL(S-6)=L:C=C-1:GOTO420
1180 PRINT"DONT HAVE ONE": GOTO420
1190 IFS=24ANDL(18)=0THENO=1
1200 IFS=11ANDL(5)=0THENM=1
1210 IF S=23ANDM=1ANDP=1THENP=11:GOTO1140
1220 GOTO 420
1230 PRINT"I'M CARRYING":FORX=0TO33:IFL(X)=0THENPRINTO$(X+6)
1240 NEXT:GOTO420
1250 IFS>37ORS<5THENPRINT"HUH??":GOTO420
1260 IFS=6THENPRINT"O.K.":GOTO420
1270 IFS=26ANDL=9THENPRINT"DIZZY":L=16:GOTO420
1280 IFS=26ANDL=16THENPRINT"OOPS":L=9:GOTO420
1290 IFS=5AND(L<>12ORL(14)<>0)THENPRINT"LOCKED":GOTO420
1300 IFS=26THENPRINT"NOTHING":GOTO420
1310 IFS=5THENO$(28)="OPEN DOOR":P(12,1)=4:P(4,0)=12:GOTO420
1320 IFS=37ANDL=14ANDL(16)=31THENL(16)=14:O$(S)="OPEN "+O$(S):GOTO420
1330 IFS<38THENIFL(S-6)<>0THENPRINT"DON'T HAVE IT":GOTO420
1340 IFS=19THENPRINT"AHA!":PRINTO$(18):L(12)=L:GOTO420
1350 IFS=18THENPRINTO$(23)"!":L(17)=L:GOTO420
1360 IFS=10ANDL(4)=0ANDL(11)=31THENL(5)=L:GOTO420
1365 IFS=10ANDL(4)=0THENPRINT"EMPTY":GOTO420
1370 PRINT"CAN'T":GOTO420
1380 IFL=2ANDL(1)=0THENPRINT"FELL THROUGH":L=5:GOTO580
1390 IFL=3THENPRINT"NOTHING BUT SAND":GOTO580
1400 PRINT"TOO HARD":GOTO580
1410 FORX=1TO3O:IFL(X)=30THENPRINTO$(X+6)
1420 NEXT:GOTO420
1430 IFS<>38OR(L<>13ANDL<>11)THENPRINT"NO":GOTO420
1440 IFC+K<4THEN1480
1450 IF C<4ANDL(0)<>0THEN1480
1460 FORX=1TO30:PRINTTAB(X)"AARGG":FORY=1TO10*X:NEXTY,X
1470 PRINT"I WAS TOO HEAVY AND DIED":END
1480 PRINT"HERE GOES":L=L+2:IFL=15THENL=11
1490 GOTO580
1500 IFS<>32THENPRINT"WON'T BURN":GOTO420
1510 IFL(5)=0THENO$(32)="ASHES":P(22,1)=14
1520 GOTO420
1530 PRINT:PRINT"WATER IS RISING - I CAN'T SWIM":W=W+1
1540 IFW=4THENPRINT"DROWNED":END
1550 P(6,1)=7:P(6,5)=0:D$(6)="BOAT IN A LAKE":GOTO460
1560 PRINTO$(13)" CLIMBS INTO "O$(38)" AFTER "O$(14)
1570 PRINTO$(13)" BRING BACK "O$(16):L(10)=L:GOTO420
1580 IFL<>1THENPRINT"NOT HERE":GOTO420
1590 SC=0:IFL(11)=0OR L(11)=1  THENSC=SC+1
1600 IFL(10)=0ORL(10)=1 THENSC=SC+1
1610 IF L(16)=0ORL(16)=1THEN SC=SC+1
1620 IF L(6)=0 OR L(6)=1 THEN SC=SC+1
1630 PRINT"YOU HAVE "SC" OUT OF 4 TREASURES- SCORE="SC*25
1640 IFSC<100THEN420
1700 INPUT"WITH WHAT ";A$:IF LEN(A$)>2 THEN A$=LEFT$(A$,2)
1705 IFA$="HA"OR A$="FO"OR A$="FI"THENPRINT"YOU ARE KIDDING!!!":GOTO420
1710 IFS=5THENPRINT"THAT IS A SOLID DOOR!"
1720 IF(S=32)AND(L(26)=L)THENPRINT"HE DODGED"
1730 PRINT"NOTHING HAPPENED":GOTO 420
