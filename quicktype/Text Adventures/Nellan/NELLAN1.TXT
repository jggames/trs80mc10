0 CLS:CLEAR400:DIMC1,C2,M$,I$:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 I$=""
4 PRINT"å";:GOSUB6:PRINTCHR$(8);:ON-(C2=64)GOSUB6:ON-(M$<" ")GOTO5:PRINTM$;:I$=I$+M$:C2=.:ON-(LEN(I$)<65)GOTO4:PRINT:RETURN
5 C2=.:ON-(M$="")GOTO4:ON-(M$=CHR$(8))GOTO8:ON-(M$<>CHR$(13))GOTO4:PRINT:RETURN
6 FORC1=1TO35:M$=INKEY$:C2=ASC(M$+"@"):C1=C1-(C2<>64)*35:NEXT:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
8 IFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):PRINTCHR$(8);
9 GOTO4
10 PRINT"  éèçèèççèèèèèèèèéåéèèèèèèéèèè":PRINT"  äÜÖâçÖÖâÖÅçèáÅèèÖäåãäÜäáÇáÑÖ":PRINT"  äèÖÇçÖÖÜÖÖÖèÖàèèÖääääèéÖäèåÖ":PRINT
20 PRINTTAB(5)"WRITTEN FOR THE TRS-80":PRINTTAB(14)"BY":PRINTTAB(9)"FURMAN H. SMITH"
25 FORI=1TO2500:NEXT
100 GOSUB1010:GOSUB705
110 PRINT"ARE YOU THE ONLY PERSON PLAYING?";:GOSUB3:A$=I$
115 P=1:A$=LEFT$(A$,1):IFA$="Y"THENP9=1:GOTO140
120 IFA$<>"N"THENPRINT"PLEASE TYPE 'YES' OR 'NO'":GOTO110
130 PRINT"HOW MANY OF YOU WILL BE PLAYING?";:GOSUB3:A$=I$:P9=VAL(A$):IFP9<1THEN130
140 IFP9>5THENPRINT"SORRY, THAT IS TOO MANY.":GOTO130
145 I=1:PRINT"WHAT IS YOUR NAME?  ";:GOSUB3:P$(P)=I$:GOTO150
150 IFI=P9THEN165
155 I=I+1:PRINT"ENTER ANOTHER NAME? ";
160 GOSUB3:P$(I)=I$:GOTO150
165 IFP9=1THENT2=T9:GOTO205
199 T2=4
205 REM
210 I=VAL(MID$("091506010902091310",R*2-1,2))
211 W4(R)=1:I=1+INT((R-1)/R8):J=R-R8*(I-1)
215 ONIGOSUB220,225,230:GOTO235
220 ONJGOTO2010,2020,2030
225 ONJGOTO2040,2050,2060
230 ONJGOTO2070,2080,2090
235 FORM=1TOM9
240 IFL(M)=RTHENGOSUB265
245 NEXTM
250 IFRND(0)>T*(2-W4(3))/(7*T9)THEN260
255 PRINT:M$="A VOICE SAYS: 'TYPE help IF YOU NEED HELP.'":GOSUB1
260 GOTO305
265 PRINT:I=1+INT((M-1)/8):J=M-8*(I-1)
270 ONIGOTO275,280,285,290
275 ONJGOTO2410,2420,2430,2440,2450,2460,2470,2480
280 ONJGOTO2490,2500,2510,2520,2530,2540,2550,2560
285 ONJGOTO2570,2580,2590,2600,2610,2620,2630,2640
290 ONJGOTO2650
305 REM CHANGE COLOR
306 M$=Q$(1+Q9*RND(0)):IFRND(0)>.5THENM$=M$+", "+P$(P)
315 M$=M$+"?":GOSUB1:GOSUB799
325 FORI=1TOI9:IFMID$(A$,I,1)<>" "THENNEXT
330 V$=LEFT$(A$,I-1):IFI=I9+1THENN$=V$:GOTO345
335 FORJ=I9TOISTEP-1:IFMID$(A$,J,1)<>" "THENNEXT
340 N$=RIGHT$(A$,I9-J)
345 V=0:N=0
350 FORI=1TOV9:IFV$(I)=V$THENV=I:GOTO355
351 NEXTI
355 FORI=1TON9:IFN$(I)=N$THENN=I:GOTO357
356 NEXTI
357 IF0<NANDN<9THEN405
360 IFV*N>0OR(V+N>0ANDV$=N$)THEN380
361 GOTO3505
365 PRINT"I DON'T UNDERSTAND.":GOTO305
380 IFN<29THEN405
381 IFN=29THENN=11:GOTO405
382 N=N-7
405 IFN>4ANDN<9THENN=N-4
410 IFN>4ORN=0THEN435
411 ONNGOTO412,416,420,424
412 W1(R)=ABS(W1(R)):IFW1(R)=1THEN430
414 IFW1(R)=2THENR=R-R8:GOTO605
415 GOTO505
416 W1(R+R8)=ABS(W1(R+R8)):IFW1(R+R8)=1THEN430
418 IFW1(R+R8)=2THENR=R+R8:GOTO605
419 GOTO505
420 W2(R+1)=ABS(W2(R+1)):IFW2(R+1)=1THEN430
422 IFW2(R+1)=2THENR=R+1:GOTO605
423 GOTO505
424 W2(R)=ABS(W2(R)):IFW2(R)=1THEN430
426 IFW2(R)=2THENR=R-1:GOTO605
427 GOTO505
430 PRINT"YOU CAN'T WALK THROUGH WALLS.":GOTO305
435 IFN<>12ANDN<>22THENGOSUB4015:GOTO440
436 GOSUB4030
440 IF(V<=4ORV=13)ANDN=22THEN5410
445 IFM<>0ORV=0ORV>8THEN455
450 M$="THAT'S NOT POSSIBLE NOW, "+P$(P)+".":GOSUB1:GOTO305
455 IFM=0ORV=15ORN=9THEN505
460 IF4<VANDV<9THEN475
465 IFL(M)>-1ANDL(M)<>RTHEN470
466 GOTO475
470 M$="IT SEEMS THAT THE "+N$+" ISN'T THERE.":GOSUB1:GOTO305
475 IFV>8THEN505
476 IFV<5THENV=1:GOTO480
477 V=5
480 IFN=28THENPRINT"THE WATCH STAYS ON YOUR WRIST.":GOTO305
485 IFN=10ORN=23ORN=24THEN5980
490 ON(V+3)/4GOSUB4045,4100
495 GOTO235
505 IFN<=8ANDN>0THEN5015
506 IFN=9ANDV<9THEN5980
510 IFN=26ORN=27THENON(N-25)GOSUB5105,5155:GOTO305
512 IFV=0THEN365
515 IFV<=14THENJ=V-8:GOTO520
516 J=V-14:GOTO525
520 ONJGOTO5205,5255,5355,5355,5405,5605
525 ONJGOTO5705,5755,5780,5805,5980,205
605 T=T+1:T1=T1+1
610 IFT<T9THEN625
615 PRINT:M$="TIME IS UP... THE"+STR$(T9)+" SCOOTS HAVE PASSED.":GOSUB1
620 END
625 IFW1(R)=-1THENW1(R)=1
630 IFW1(R+R8)=-1THENW1(R+R8)=1
635 IFW2(R)=-1THENW2(R)=1
640 IFW2(R+1)=-1THENW2(R+1)=1
645 IFT1<=T2THEN205
646 T1=0:IFP<P9THENP=P+1:GOTO650
647 P=1
650 CLS:PRINT"IT'S "P$(P)"'S TURN NOW.":PRINT:FORC1=1TO2500:NEXT:GOTO210
705 CLS:M$="LET'S PRETEND THAT YOU ARE IN AN ENCHANTED HOUSE. YOU SHOULD USE ONE OR TWO WORDS TO TELL "
710 M$=M$+"ME WHAT YOU WANT TO DO.":GOSUB1:M$="I'LL SUGGEST MESSAGES AT FIRST. IF YOU HAVE A QUESTION "
720 M$=M$+"WHILE YOU'RE INSIDE THE HOUSE, TYPE help.":GOSUB1:RETURN
799 A$=""
800 GOSUB3:A$=I$:I9=LEN(A$):IFI9>0THENRETURN
805 GOTO800
1010 R6=1:R7=3:R8=3:R9=R6*R7*R8:R0=R9-R8+1:R=R0:V9=20:N9=31:M9=25
1020 DIMV$(V9),N$(N9),L(M9),N1(M9),W(M9),W4(R9),W4$(R9),W1((R6*R7+1)*R8),W2(R9+1),W1$(3),W2$(3),W3$(9)
1030 T=2:T1=0:T9=72:C=0:C9=4:Q9=4:B9=4:DIMQ$(Q9),B$(B9)
1110 FORI=1TOV9:READV$(I):NEXT
1115 DATATAKE,GET,GRAB,PICK,DROP,GIVE,OFFER,DEPOSIT,KICK,CONSULT,OPEN,UNLOCK
1125 DATAFILL,LIST,VALUE,SIT,DRINK,HELP,GO,LOOK
1210 FORI=1TON9:READN$(I):NEXT
1215 DATAN,S,E,W,NORTH,SOUTH,EAST,WEST,DOOR,CHAIR,RABBIT,BOWL,COUPON,WRAPPER,MAP
1225 DATAKEY,EGG,COIN,JEWEL,CATALOG,VASE,MILK,BANKER,CAT,WORDS
1230 DATAINVENTORY,SCORE,WATCH,CHULA,KLINKOYN,NELLAN
1320 FORJ=1TO9:W3$(J)="øø":NEXT
1420 FORI=0TO3:READW1$(I),W2$(I):NEXT
1430 DATA"øøÄÄøø","ÄÄ"
1440 DATA"øøøøøø","øø"
1450 DATA"øøåÉøø","åÉ"
1460 DATA"øøœœøø","ˇˇ"
1510 FORI=0TO9:READW4$(I):W4(I)=0:NEXT
1515 DATA"  ??  "
1520 DATA"OFFICE"," MILK ","STORE "
1525 DATA"WHITE "," GOLD "," HOT  "
1530 DATA" BANK ","GREEN "," CAT  "
1610 FORI=1TO(R6*R7+1)*R8:READW1(I):NEXT
1615 DATA1,1,1,-2.5,-2,-1,-2,-1,-2,1,1,1
1710 FORI=1TOR9+1:READW2(I):NEXT
1715 DATA1,-1,-2.5,1,-2,-2,1,1,-2,1
1810 FORM=1TOM9:READL(M),N1(M):W(M)=0:NEXT
1815 DATA1,20,6,13,0,12,0,12,0,12,2,9,3,0,0,0,7,23,1,16,9,24,0,24,0,24,0,24,0,11
1825 DATA0,11,0,11,4,11,7,15,2,14,1,0,8,18,0,17,3,19,0,21
1830 FORI=22TO25:W(I)=25:NEXT
1910 FORI=1TOQ9:READQ$(I):NEXT
1915 DATA"WHAT ARE YOU GOING TO DO NOW","WHAT NOW"
1920 DATA"WHAT DO YOU CHOOSE TO DO","WHAT IS YOUR DECISION"
1925 FORI=1TOB9:READB$(I):NEXT
1930 DATAHAPPY,"GLAD TO SEE YOU",BUSY,SLEEPY
1995 RETURN
2010 CLS:M$="WELL, "+P$(P)+", YOU ARE IN THE OFFICE. YOU ARE STANDING NEXT TO A CHAIR WHICH IS NEXT TO A DESK "
2011 M$=M$+"IN A ROOM WITH YELLOW WALLS AND BROWN CARPET. THERE IS A DOOR IN THE SOUTH WALL.":GOSUB1:RETURN
2020 CLS:M$="THIS IS THE MILK ROOM. IF YOU HAVE A BOWL, YOU CAN SAY FILL BOWL OR GET MILK AND HAVE YOUR BOWL "
2021 M$=M$+"FILLED WITH NICE COLD MILK. ":IFL(15)=-1ANDL(6)=2THENL(18)=0:L(17)=2
2025 M$=M$+"THERE ARE DOORWAYS TO THE SOUTH AND EAST.":GOSUB1:RETURN
2030 CLS:PRINT"YOU ARE IN THE STORE.":RETURN
2040 CLS:M$="YOU ARE IN THE WHITE ROOM. THE BANK IS SOUTH. THERE'S A DOOR TO THE NORTH AND A DOOR TO THE EAST.":GOSUB1:RETURN
2050 CLS:M$="ALL THE WALLS IN THIS ROOM ARE PAINTED GOLD. THERE ARE EXITS TO THE NORTH, EAST AND WEST.":GOSUB1
2052 IFL(15)=-1ANDL(23)=0THENL(15)=0:L(16)=5:L(23)=5
2053 RETURN
2060 CLS:M$="OH, IT'S HOT IN HERE! YOU ARE IN THE HOT ROOM AND IF COLD MILK WERE IN HERE, IT WOULD BE WARMED. "
2063 M$=M$+"THE MAP SHOWS THE DOORWAYS.":GOSUB1:IFL(4)=-1THENL(4)=0:L(5)=-1
2064 RETURN
2070 CLS:M$="YOU ARE IN A ROOM CALLED THE BANK. A GIANT CARPET ON THE FLOOR SAYS: 'WELCOME "+P$(P)+".":GOSUB1
2073 M$="A SCREEN SAYS THAT YOUR VISIT WILL LAST FOR"+STR$(T9)+" SCOOTS."+STR$(T)+" SCOOTS HAVE PASSED. "
2075 M$=M$+"TO LEAVE THIS ROOM, TYPE GO NORTH.":GOSUB1:RETURN
2080 CLS:M$="YOU ARE STANDING IN A ROOM WITH GREEN WALLS. A GREEN CHAIR IS IN THE CORNER.":GOSUB1
2083 IFW1(4)<0THENRETURN
2084 M$="A SIGN HERE SAYS: 'MAGIC WORKS IN THE ENCHANTED HOUSE'":GOSUB1:RETURN
2090 CLS:M$="YOU ARE IN THE ROOM WHERE NELLAN LIVES; THIS ROOM IS CALLED THE CAT ROOM.":GOSUB1:RETURN
2410 M$="THERE IS A COLLEGE CATALOG FROM THE UNIVERSITY OF TEXAS HERE.":GOSUB1:RETURN
2420 M$="ON THE FLOOR IS A COUPON. IT SAYS ON THE COUPON THAT IF IT IS DROPPED IN THE STORE WHILE THE MACHINE IS WORKING, "
2421 M$=M$+"YOU WILL RECEIVE ONE BLUE BOWL.":GOSUB1:RETURN
2430 M$="THERE IS A CLEAN EMPTY BLUE BOWL HERE.":GOSUB1:RETURN
2440 M$="THE BOWL OF COLD MILK IS HERE.":GOSUB1:RETURN
2450 M$="THERE IS A BLUE BOWL WITH WARM MILK.":GOSUB1:RETURN
2460 RETURN
2470 M$="IN A CORNER OF THE ROOM IS A GIGANTIC MACHINE. A MECHANICAL VOICE SAYS: 'DEPOSIT COUPONS HERE.'":GOSUB1:RETURN
2480 M$="THE MACHINE IN THE CORNER HAS A FLASHING SIGN ANNOUNCING: OUT OF BOWLS.":GOSUB1:RETURN
2490 M$="THE BANKER, MR. KLINKOYN, LOOKS "+B$(1+B9*RND(0))+".":GOSUB1
2491 IFL(23)=R0THENPRINT"TYPE score TO CHECK YOUR SCORE."
2494 RETURN
2500 M$="THERE IS AN OLD BRASS KEY HERE.":GOSUB1:RETURN
2510 M$="NELLAN THE CAT IS HERE. SHE NEARLY FILLS UP THE ROOM BECAUSE SHE'S AS BIG AS AN ELEPHANT (REALLY!!). "
2511 M$=M$+"IN A SOFT POLITE VOICE SHE SAYS: 'I'D LOVE TO HAVE SOME NICE COLD MILK. ":GOSUB1:GOSUB6000
2515 M$="MANY OF MY FRIENDS HAVE TRIED TO DELIVER COLD MILK HERE AND MANY HAVE FAILED. PERHAPS "
2517 M$=M$+"YOU, "+P$(P)+", WOULD BE KIND ENOUGH TO TRY. I MUST WARN YOU-- IT WILL NOT BE 'EASY.'":GOSUB1:RETURN
2520 M$="NELLAN PURRS AND SAYS: 'THANK YOU FOR THIS NICE MILK. IT'S A PITY THAT IT'S SO WARM.'":GOSUB1
2521 L(5)=0:L(3)=9:L(12)=0:L(11)=9:RETURN
2530 M$="WOW! NELLAN IS PURRING VERY, VERY LOUDLY. SHE LOOKS AT YOU AND SAYS: "
2532 M$=M$+"'"+P$(P)+", YOU'VE MADE ME SO HAPPY.":GOSUB1
2533 M$="I KNOW THAT IT WASN'T EASY TO GET THIS BOWL OF COLD MILK TO ME. YOU DID IT!"
2535 L(4)=0:L(3)=9:L(13)=0:L(14)=9:IFL(25)<>0THENGOSUB1:RETURN
2536 M$=M$+" PLEASE ACCEPT THIS SMALL PRESENT.'":GOSUB1:L(25)=9
2537 M$="SHE OFFERS YOU A SMALL "+N$(21)+".":GOSUB1:RETURN
2540 M$="NELLAN IS NAPPING. SHE'S QUITE HAPPY.":GOSUB1:RETURN
2550 M$="CHULA THE WHITE RABBIT IS HERE AND SHE SAYS: 'WELL, MY FRIEND "+P$(P)+", "
2552 M$=M$+"I HOPE THAT YOU ARE ENJOYING YOUR ADVENTURE.'":GOSUB1:RETURN
2560 M$="CHULA HOPS OUT OF YOUR ARMS AND SCURRIES OFF. SHE RETURNS AND SAYS: "
2562 M$=M$+"'PLEASE TAKE THIS GOLDEN EGG THAT I HID HERE IN THE GOLD ROOM.'":GOSUB1
2563 L(23)=5:L(15)=5:L(16)=0:C=C-1:RETURN
2570 M$="CHULA HOPS DOWN AND GOBBLES A GROWTH OF CARROTS THAT WAS BLOCKING THE EAST "
2572 M$=M$+"DOORWAY. SHE SMILES AND SAYS: 'THANKS, FRIEND "+P$(P)+".":GOSUB1
2574 L(17)=0:L(15)=2:L(6)=0:C=C-1:RETURN
2580 M$="A WHITE RABBIT IS HERE. SHE HOPS TO YOU AND SAYS: 'I AM CHULA. WELCOME TO THE ENCHANTED HOUSE, MY FRIEND, "
2583 M$=M$+P$(P)+".'":GOSUB1:M$="SHE HUGS YOU AND SAYS: 'I HAVE A PRESENT FOR YOU IN THE GOLD "
2585 M$=M$+"ROOM. IF YOU SAY TAKE RABBIT, THEN YOU CAN CARRY ME TO THE EAST.'":GOSUB1:L(18)=0:L(15)=4:RETURN
2590 M$="THERE IS A MAGIC MAP HERE. IF YOU TAKE THE MAP, YOU MAY CONSULT THE MAP.":GOSUB1:RETURN
2600 M$="AN OLD GUM WRAPPER IS ON THE FLOOR.":GOSUB1:RETURN
2610 SOUNDRND(200),1:RETURN
2620 PRINT"A HEAVY COIN IS HERE.":RETURN
2630 PRINT"A GOLDEN EGG IS ON THE FLOOR.":RETURN
2640 PRINT"ON THE FLOOR IS A JEWEL.":RETURN
2650 PRINT"A MAGNIFICENT VASE IS HERE.":RETURN
3505 IFA$="EXTEND TIME"THENT9=T9+24:N=28:GOTO5315
3995 GOTO365
4015 IFV=0ORN=0THENM=0:RETURN
4020 IFN=24THENIFL(11)=9THENM=11:RETURN
4021 IFN=24THENM=14:RETURN
4025 FORI=1TOM9:IFN1(I)=NTHENM=I:RETURN
4026 NEXTI
4030 M=0:IFN<>22ANDN<>12THENRETURN
4035 FORJ=3TO5:IFL(J)<>0THENM=J
4036 NEXT:RETURN
4045 IFL(M)=RANDC<C9THEN4075
4046 IFC>=C9THEN4055
4050 M$="YOU'VE STILL GOT THE "+N$+".":GOSUB1:GOTO4140
4055 M$="YOU CAN ONLY CARRY"+STR$(C9)+" OBJECTS (NOT COUNTING THE WATCH) AND YOU ARE ALREADY CARRYING THAT MUCH. "
4056 M$=M$+"IF YOU WANT TO GET SOMETHING ELSE, YOU WILL HAVE TO DROP SOMETHING FIRST.":GOSUB1:GOTO4140
4075 IFW(M)=0THEN4090
4076 M$="CONGRATULATIONS, "+P$(P)+", YOU'LL EARN POINTS WHEN THE "+N$+" IS DEPOSITED IN THE BANK.":GOSUB1:C=C+1:L(M)=-1:RETURN
4090 M$="OK, YOU ARE CARRYING THE "+N$+".":GOSUB1:L(M)=-1:C=C+1:RETURN
4100 IFL(M)=-1THENPRINT"OK.":L(M)=R:C=C-1:GOTO4105
4101 GOTO4135
4105 IFR=R0THENGOSUB5175
4110 IFR<>3ANDR<>9THENRETURN
4115 IFL(2)=3THENL(2)=0:L(3)=3:L(7)=0:L(8)=3
4120 IFL(4)=9THENL(4)=0:L(3)=9:L(11)=0:L(13)=9
4125 IFL(5)=9THENL(5)=0:L(3)=9:L(11)=0:L(12)=9
4130 RETURN
4135 M$="YOU DON'T HAVE THE "+N$+".":GOSUB1
4140 M$="TO SEE A LIST OF WHAT YOU'RE CARRYING, TYPE inventory.":GOSUB1:RETURN
4505 CLS:I$=W4$(R):GOSUB6010:M$="YOU ARE IN THE "+I$+" ROOM NOW.":GOSUB1
4510 PRINTTAB(25)" n":PRINTTAB(25)"w+e":PRINTTAB(25)" s"
4520 I2=R7*R8:FORI1=1TOR6
4525 IF(I1-1)*I2<RANDR<=I1*I2THEN4530
4526 NEXT
4530 K0=(I1-1)*I2:K9=I1*I2:K=K0:J=1
4535 IFK=K0ANDJ=1THENPRINTW3$(7);
4540 IFK=K0ANDJ>1ANDJ<R8+1THENPRINTW3$(8);
4545 IFK=K0ANDJ=R8+1THENPRINTW3$(9)
4550 IFK>K0ANDK<K9ANDJ=1THENPRINTW3$(4);
4555 IFK>K0ANDK<K9ANDJ>1ANDJ<R8+1THENPRINTW3$(5);
4560 IFK>K0ANDK<K9ANDJ=R8+1THENPRINTW3$(6)
4565 IFK=K9ANDJ=1THENPRINTW3$(1);
4570 IFK=K9ANDJ>1ANDJ<R8+1THENPRINTW3$(2);
4575 IFK=K9ANDJ=R8+1THENPRINTW3$(3)
4580 IFJ>R8THEN4595
4585 IFW1(K+J)>0THENPRINTW1$(W1(K+J));:GOTO4590
4586 PRINTW1$(0);
4590 J=J+1:GOTO4535
4595 IFK=K9THENRETURN
4596 J=1
4600 IFW2(K+J)>0THENPRINTW2$(W2(K+J));:GOTO4605
4601 PRINTW2$(0);
4605 IFJ>R8THENPRINT:GOTO4620
4610 IFW4(K+J)>0THENPRINTW4$(K+J);:GOTO4615
4611 PRINTW4$(0);
4615 J=J+1:GOTO4600
4620 K=K+R8:J=1:GOTO4535
5015 IFR<>1THEN5040
5016 IFL(10)=-1THEN5030
5020 M$="THE DOOR IS LOCKED AND YOU'RE NOT CARRYING THE KEY.":GOSUB1:W1(4)=3.5:GOTO305
5030 W1(4)=2.5:R=4:GOTO605
5040 IFR<>2THEN5070
5041 IFL(6)<>2THEN5060
5045 M$="YOU CANNOT ENTER... THE DOORWAY IS BLOCKED BY A THICK GROWTH OF CARROTS.":GOSUB1:W2(3)=3.5:GOTO305
5060 R=3:W2(3)=2.5:GOTO605
5070 IFR=4ANDL(10)<>-1THEN5020
5071 IFR=4THEN5080
5073 IFR>4THEN365
5075 R=2:GOTO605
5080 W1(4)=2.5:R=1:GOTO605
5105 M$="YOUR WATCH STAYS ON YOUR WRIST FOR THE WHOLE VISIT.":GOSUB1
5110 IFC=0THENM$="YOU AREN'T CARRYING ANYTHING ELSE.":GOSUB1:RETURN
5115 M$="HERE IS AN INVENTORY OF WHAT YOU'RE CARRYING:":GOSUB1:I$=""
5120 FORJ=1TOM9:IFL(J)=-1THENI$=I$+N$(N1(J))+", "
5125 NEXT:IFI$<>""THENM$=LEFT$(I$,LEN(I$)-2):GOSUB1
5126 RETURN
5155 GOSUB5175:M$="YOU HAVE"+STR$(I)+" POINTS OUT OF 100.":GOSUB1
5160 M$="YOU EARN POINTS BY DEPOSITING VALUABLE THINGS IN THE BANK.":GOSUB1:RETURN
5175 I=0:FORJ=1TOM9:IFL(J)=R0THENI=I+W(J)
5180 A$="":IFP9>1THENA$="ALL "
5185 NEXTJ:IFI<100THENRETURN
5190 PRINT:M$="MR. KLINKOYN SHOUTS: 'YOU "+A$+"WON!'":GOSUB1:GOTO620
5205 M$="KICKING "+N$+" DOES NOT HELP.":GOSUB1:GOTO305
5255 IFNOT(N=11ORN=23ORN=24)THEN5305
5260 IFL(6)=0THEN5275
5265 M$="THE "+N$(N1(M))+" LAUGHS AND SAYS: 'WELL, "+P$(P)
5266 M$=M$+", DID YOU KNOW THAT RABBITS ARE VERY FOND OF CARROTS.'":GOSUB1:GOTO305
5275 ON2-W4(1)GOTO5280,5290
5280 M$="THE "+N$(N1(M))+" SAYS: '"+P$(P)+", I THINK THAT YOU'RE DOING GREAT.'":GOSUB1:GOTO305
5290 M$="THE "+N$(N1(M))+" SMILES MYSTERIOUSLY AND SAYS: 'I RECOMMEND THAT YOU SIT IN THE CHAIR.'":GOSUB1:GOTO305
5305 IFN=15THENGOSUB4505:GOTO305
5310 IFN=20THENPRINT"IT DOESN'T HELP HERE.":GOTO305
5315 IFN=28THENM$="SCOOTS HAVE PASSED AND"+STR$(T9-T)+" SCOOTS REMAIN.":GOSUB1:GOTO305
5325 GOTO5980
5355 IFN=9THEN5015
5356 GOTO5980
5405 IFN<>12ANDN<>24THEN5980
5406 IFN=24THENV=5:N=12:GOTO435
5410 IFR<>2ORL(3)<>-1THEN5425
5411 L(3)=0:L(4)=-1
5415 M$="GLUG, GLUG, PLINK. YOUR BOWL HAS COLD MILK IN IT NOW.":GOSUB1:GOTO305
5425 IFN=22ANDM=3THENPRINT"THE BOWL IS EMPTY.":N$="BOWL"
5430 IFV<>13ANDL(M)=RTHENGOSUB4045:GOTO305
5435 IFM=4ORM=5THENPRINT"THE BOWL IS STILL FULL OF MILK."
5440 IFR<>2THENPRINT"THE MILK ROOM HAS LOTS OF MILK."
5445 IFL(M)>-1ORM<3ORM>5THENPRINT"YOU NEED A BOWL."
5450 GOTO305
5605 IFN<>25THEN5980
5606 PRINT"I KNOW THESE VERBS:":M$=""
5610 FORI=1TOV9:I$=V$(I):M$=M$+I$:GOSUB5655:NEXT
5615 PRINT"I KNOW THESE NOUNS:":M$=""
5620 FORI=1TO28:I$=N$(I):M$=M$+I$:GOSUB5655:NEXT:GOTO305
5655 IFI$=V$(V9)ORI$=N$(14)ORI$=N$(28)THENGOSUB1:M$="":I$="":RETURN
5665 M$=M$+", ":RETURN
5705 IFM=0THEN5980
5706 M$="THE "+N$+" IS WORTH"+STR$(W(M))+" POINTS.":GOSUB1:IFW(M)<>0THEN305
5710 M$="HOWEVER, IT MIGHT BE USEFUL AS A TOOL.":GOSUB1
5720 IFN=11ORN=23THENPRINT"FRIENDS ARE VALUABLE."
5730 IFN=20THENM$="A UNIVERSITY EDUCATION IS A WORTHWHILE AND STIMULATING EXPERIENCE.":GOSUB1
5735 GOTO305
5755 IFN<>10THEN5980
5756 IFR=1ORR=8THENR=9-R:GOTO605
5760 PRINT"THERE'S NO CHAIR IN HERE.":GOTO305
5780 IFN<>22THEN5980
5781 IFL(4)=-1ORL(5)=-1THEN5790
5785 PRINT"YOU ARE NOT CARRYING ANY MILK.":GOTO305
5790 PRINT"GLUG, GLUG.":L(4)=0:L(5)=0:L(3)=-1:GOTO305
5805 CLS:PRINT"LOST? consult map"
5810 PRINT"WANT ROOM DESCRIBED? look"
5820 PRINT"WANT TO MOVE? go direction"
5825 PRINT"WHAT'S A THING WORTH?":PRINT"value thing"
5835 PRINT"WANT A HINT? consult friend"
5850 PRINT"WHAT TO DO? try things":GOTO305
5980 IFN$=V$THENPRINT"YOU'LL NEED TO USE TWO WORDS.":GOTO305
5982 M$="THE "+N$+" IS HARD TO "+V$+".":GOSUB1:GOTO305
6000 PRINT"HIT enter";:FORC1=0TO1:C1=-(INKEY$=CHR$(13)):NEXT:FORC1=1TO9:PRINTCHR$(8);:NEXT:RETURN
6010 IFI$<>""THENFORC1=1TOLEN(I$):IFMID$(I$,C1,1)=" "THENNEXT
6020 I$=MID$(I$,C1):IFI$<>""THENFORC1=LEN(I$)TO1STEP-1:IFMID$(I$,C1,1)=" "THENNEXT
6030 I$=LEFT$(I$,C1):FORC1=0TO1:NEXT:RETURN
