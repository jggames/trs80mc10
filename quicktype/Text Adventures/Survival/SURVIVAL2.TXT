0 CLEAR1000:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 REM MOON SURVIVAL PROGRAM
6 REM WRITTEN BY STEWART RUSH 3/12/81
10 CLS:DIMT$(64)
20 DIMM(42,8)
40 DIMO(14)
60 REM
70 REM INITIALIZE TEXT AND MOVE MATRICES
80 REM
90 PRINT"WELCOME TO THE GAME OF SURVIVAL"
100 PRINT"WOULD YOU LIKE INSTRUCTIONS?"
110 INPUTD$
120 IFD$="Y"THENGOSUB5050
122 FORI=1TO14
124 READO(I)
126 NEXTI
130 FORI=1TO60
140 READT$(I)
150 NEXTI
160 FORI=1TO42
170 FORJ=1TO8
180 READM(I,J)
190 NEXTJ
200 NEXTI
205 RESTORE
290 REM
300 REM PROGRAM VARIABLE DEFINITION
310 REM
320 P=1
330 C=2
340 T1=0
350 GOSUB9060:REM FIX FOR EASIER GAME
360 P1=230
370 P2=230
380 V=0
390 F0=1
570 F1=0
580 F2=0
585 F3=0
590 F4=0
595 F5=0
600 F7=0
610 F9=0
620 REM
630 REM DISPLAY CURRENT STATUS AND LOCATION INFO
640 REM
650 PRINT"ELAPSED TIME:"T1"MINUTES"
660 IFO(11)=99THENPRINT"POWER UNIT:"P1"UNITS"
670 IFO(14)=99THENPRINT"POWER PACK:"P2"UNITS"
680 T1=T1+5
690 IFO(11)=99ANDP1>0THENP1=P1-5
700 IFO(14)=99ANDP2>0THENP2=P2-5
710 IFO(11)=99ANDP1=0THEN3680
720 IFO(14)=99ANDP2=0THEN3680
730 IFT1>400THEN2960
740 IFT1>350THEN3840
750 IFT1>200THEN3740
760 IFF0=1THENT2=T2-5
770 IFT2<0THENT2=0
780 IFF0=0THEN800
790 IFT2>0THEN840
800 IFF9=0THEN820
810 IFP>21THEN2900
820 IFP<18THEN2900
830 IFP=38THEN1700
840 IFP=38THEN3590
850 IFF0=1THENPRINT"OXYGEN REMAINING:"T2"MINUTES"
860 M$="PRESENT LOCATION STATUS: YOU ARE "
870 FORI=M(P,7)TOM(P,8)
880 M$=M$+T$(I)+" ":IFLEN(M$)>192THENGOSUB1
890 NEXTI:GOSUB1
900 PRINT"##"
910 REM
920 REM DISPLAY ANY OBJECTS PRESENT
930 REM
940 IFP=2THEN1920
950 FORI=1TO14
960 IFO(I)<>PTHEN990
970 GOSUB4410
980 M$="THERE IS "+B$+" HERE.":GOSUB1
990 NEXTI
1000 GOTO2000
1010 REM
1020 REM READ AND PROCESS KEYBOARD RESPONSE
1030 REM
1040 INPUTB$:
1050 I=0
1060 IFLEN(B$)<>1THEN1320
1070 IFB$="N"THENI=1
1080 IFB$="S"THENI=2
1090 IFB$="E"THENI=3
1100 IFB$="W"THENI=4
1110 IFB$="U"THENI=5
1120 IFB$="D"THENI=6
1130 IFB$="Q"THEN1570
1140 IFI=0THEN1570
1150 IFM(P,I)=0THEN1270
1160 IFM(P,I)=99THEN2940
1170 Q=M(P,I)
1180 IFP=12THEN3060
1190 IFP=13THEN3180
1200 IFP=22THEN3310
1210 IFP=23THEN3420
1220 IFP=29THEN3470
1230 R=P
1240 P=Q
1260 GOTO650
1270 M$="YOU CAN'T GO IN THAT DIRECTION.":GOSUB1
1280 GOTO1040
1290 REM
1300 REM PROCESS 2 OR MORE CHARACTER COMMANDS
1310 REM
1320 C$=LEFT$(B$,3)
1330 IFC$="LOO"THEN650
1340 IFC$="DES"THEN650
1345 IFC$="STA"THEN650
1350 IFC$="GET"THEN2190
1360 IFC$="TAK"THEN2190
1370 IFC$="KEE"THEN2190
1380 IFC$="DRO"THEN2580
1390 IFC$="LEA"THEN2580
1400 IFC$="PUT"THEN2580
1410 IFC$="INV"THEN2780
1420 IFC$="QUI"THEN9999
1430 IFC$="END"THEN9999
1440 IFC$="TRA"THEN1750
1450 IFC$="DIG"THEN1860
1470 IFC$="FUE"THEN4030
1480 IFC$="REA"THEN4250
1490 IFC$="DEA"THEN3890
1500 IFC$="BLA"THEN4110
1505 IFC$="FIR"THEN4110
1510 IFC$="UP"THEN1640
1520 IFC$="DOW"THEN1640
1530 IFC$="NOR"THEN1640
1540 IFC$="SOU"THEN1640
1550 IFC$="EAS"THEN1640
1560 IFC$="WES"THEN1640
1562 IFC$="HEL"THEN1581
1564 IFC$="WHA"THEN1581
1566 IFC$="DEB"THEN1610
1567 IFC$="ENT"THEN1670
1570 PRINT"INVALID COMMAND"
1580 GOTO1040
1590 PRINT"I CANNOT PROCESS YOUR REQUEST!"
1600 GOTO1040
1640 D$=B$
1650 B$=D$
1660 GOTO1070
1670 REM
1680 REM PROCESS SHED AND SPACE CRAFT ENTRY
1690 REM
1700 IFR<>39THEN2900
1710 GOTO840
1720 REM
1730 REM PROCESS TRANSPORT COMMAND
1740 REM
1750 IFP<>36THEN1800
1760 IFO(8)=99THEN1590
1770 P=O(8)
1780 PRINT"BEAMING IN PROGRESS."
1790 GOTO650
1800 IFP<>O(8)THEN1590
1810 P=36
1820 GOTO1780
1830 REM
1840 REM PROCESS DIG COMMAND
1850 REM
1860 IFP<>10THEN1590
1870 O(9)=10
1880 GOTO940
1890 REM
1900 REM DROP ILLUMINATOR IF AT OVERLOOK
1910 REM
1920 IFO(4)<>99THEN950
1930 O(4)=100
1940 M$="YOU DROPPED YOUR ILLUMINATOR! YOU CANNOT RETRIEVE IT.":GOSUB1
1960 GOTO950
1970 REM
1980 REM PROCESS ROBOT
1990 REM
2000 IFO(5)=28THENO(5)=35
2010 IFO(5)=42THENO(5)=28
2020 IFO(5)=41THENO(5)=42
2030 IFO(5)=27THENO(5)=41
2040 IFO(5)=25THENO(5)=27
2050 IFO(5)<>35THEN2090
2060 IFP<>28THEN1010
2070 M(28,1)=35
2080 GOTO1010
2090 IFO(5)<>32THEN1010
2100 IFP<>32THEN1010
2110 O(5)=25
2120 IFO(13)=99THEN1010
2130 M$="ROBOT FAILS TO RECOGNIZE YOU. IT FIRES A PHASOR WEAPON AT YOU!":GOSUB1
2150 GOTO2980
2160 REM
2170 REM PROCESS GET OR TAKE COMMAND
2180 REM
2190 GOSUB4590
2200 IFI>0THEN2260
2210 IFI<0THEN2240
2220 M$="I DON'T RECOGNIZE "+MID$(B$,J+1)+".":GOSUB1
2230 GOTO1040
2240 PRINT"WHAT ITEM?"
2250 GOTO1040
2260 IFO(I)<>PTHEN2370
2270 IFC>3THEN2390
2280 IFI=5THEN2410
2290 IFI=10THEN2430
2300 IFI=11THEN2460
2310 IFI=14THEN2490
2320 C=C+1
2330 O(I)=99
2340 IFI=3THENF0=1
2350 PRINT"O.K."
2360 GOTO1040
2370 M$="THERE IS NO "+MID$(B$,J+1)+" HERE!":GOSUB1
2380 GOTO1040
2390 PRINT"YOU CAN'T CARRY ANY MORE."
2400 GOTO1040
2410 PRINT"YOU CAN'T CARRY A ROBOT."
2420 GOTO1040
2430 M$="YOU CAN'T GET THE MESSAGE. IT'S ON THE TERMINAL SCREEN.":GOSUB1
2450 GOTO1040
2460 IFO(14)=99THEN2520
2470 O(11)=99
2480 GOTO2320
2490 IFO(11)=99THEN2520
2500 O(14)=99
2510 GOTO2320
2520 M$="YOU CAN'T HAVE MORE THAN ONE POWER SUPPLY.":GOSUB1
2540 GOTO1040
2550 REM
2560 REM PROCESS DROP OR LEAVE COMMAND
2570 REM
2580 GOSUB4590
2590 IFI>0THEN2620
2600 IFI<0THEN2240
2610 GOTO2220
2620 IFO(I)<>99THEN2690
2630 C=C-1
2640 O(I)=P
2650 IFI=3THENF0=0
2660 IFI=11THEN2710
2670 IFI=14THEN2710
2680 GOTO2350
2690 M$="YOU DON'T HAVE "+MID$(B$,J+1)+"!":GOSUB1
2700 GOTO1040
2710 IFP<22THEN2870
2720 IFP=38THEN2870
2730 IFF9=1THEN2870
2740 GOTO2350
2750 REM
2760 REM PROCESS INVENTORY COMMAND
2770 REM
2780 FORI=1TO14
2790 IFO(I)<>99THEN2820
2800 GOSUB4410
2810 M$="YOU HAVE "+B$+".":GOSUB1
2820 NEXTI
2830 GOTO1040
2840 REM
2850 REM PROGRAM TERMINATION PROCESSING
2860 REM
2870 M$="YOU HAVE NO POWER OR POWER PACK. YOU HAVE FROZEN TO DEATH.":GOSUB1
2890 GOTO2990
2900 M$="OXYGEN REQUIRED HERE, NONE AVAILABLE.":GOSUB1
2910 GOTO2980
2920 PRINT "A NUCLEAR DETONATION HAS JUST OCCURRED."
2930 GOTO2980
2940 PRINT"YOU HAVE FALLEN TO YOUR DEATH."
2950 GOTO2980
2952 M$="YOU HAVE BEEN ZAPPED BY THE LASER."
2954 GOTO2980
2960 M$="THE MOON BASE HAS JUST BEEN DESTROYED BY A LARGE ASTEROID.":GOSUB1
2980 PRINT"YOU HAVE FAILED TO SURVIVE."
2990 PRINT"DO YOU WISH TO TRY AGAIN?"
3000 INPUTD$
3010 IFLEFT$(D$,1)="Y"THEN122
3020 GOTO9999
3030 REM
3040 REM PROCESS METEOR SHOWER
3050 REM
3060 IFM(P,I)<>13THEN1230
3070 IFF2=1THEN1230
3080 M$="THERE IS A METEOR SHOWER. YOUR SPACE SUIT HAS DEVELOPED A LEAK!":GOSUB1
3100 GOSUB4890
3110 IFI<>2THEN2980
3120 PRINT"YOUR SUIT IS NOW SEALED."
3130 F2=1
3140 GOTO1230
3150 REM
3160 REM PROCESS LOCKED SHED
3170 REM
3180 IFM(P,I)<>22THEN1230
3190 IFF1=1THEN1230
3200 PRINT"THE SHED IS LOCKED!"
3210 GOSUB4890
3220 IFI<>1THEN3260
3230 M$="YOU ARE IN THE SHED AIR LOCK.":GOSUB1
3240 F1=1
3250 GOTO1230
3260 PRINT"YOUR ATTEMPT FAILS."
3270 GOTO1040
3280 REM
3290 REM PROCESS DARK VENTILATOR SHAFT
3300 REM
3310 IFM(P,I)<>23THEN1230
3320 IFF4=1THEN1230
3330 M$="IT IS DANGEROUS TO PROCEED IN THE DARK!":GOSUB1
3340 GOSUB4890
3350 IFI<>4THEN2940
3360 PRINT"THE SHAFT IS NOW ILLUMINATED."
3370 F4=1
3380 GOTO1230
3390 REM
3400 REM PROCESS SHAFT WITH NO ILLUMINATOR
3410 REM
3420 IFO(4)<>99THEN2940
3430 GOTO1230
3440 REM
3450 REM PROCESS LASER BEAM
3460 REM
3470 IFM(P,I)<>37THEN1230
3480 IFF3=1THEN1230
3490 M$="THERE IS A LASER BEAM HERE. PASSAGE NOT POSSIBLE WITH BEAM PRESENT.":GOSUB1
3510 GOSUB4890
3520 IFI<>12THEN2952
3530 PRINT"THE BEAM IS NOW DEFLECTED."
3540 F3=1
3550 GOTO1230
3560 REM
3570 REM PROCESS BLOWN SEAL IN SPACE STATION
3580 REM
3590 IFR<>29THEN850
3600 IFF9=1THEN850
3610 F9=1
3620 M$="YOU HAVE JUST BLOWN THE AIR SEAL IN SPACE STATION.":GOSUB1
3640 GOTO850
3650 REM
3660 REM POWER REQUIRED TESTING
3670 REM
3680 IFP<22THEN2870
3690 IFF9=1THEN2870
3700 GOTO730
3710 REM
3720 REM EXPOSE DEACTIVATOR
3730 REM
3740 IFF5=1THEN760
3750 O(6)=14
3760 M(2,8)=M(2,7)+1
3770 M(14,8)=M(14,7)
3780 M(14,4)=2
3790 F5=1
3800 GOTO760
3810 REM
3820 REM DETONATE BOMB
3830 REM
3840 GOTO760
3860 REM
3870 REM DEACTIVATE BOMB
3880 REM
3890 IFO(6)<>99THEN3940
3900 IFO(7)<>99THEN3980
3910 F7=1
3920 PRINT"BOMB IS NOW DEACTIVATED."
3930 GOTO1040
3940 PRINT"YOU HAVE NOTHING TO DO IT WITH!"
3950 GOTO1040
3960 PRINT"THERE IS NOTHING TO DO IT TO!"
3970 GOTO1040
3980 PRINT"YOU CAN'T DO IT FROM HERE!"
3990 GOTO1040
4000 REM
4010 REM FUEL ROCKET
4020 REM
4030 IFP<>19THEN3980
4040 IFO(9)<>99THEN3940
4050 C=C-1:O(9)=98
4060 PRINT"FUEL IS NOW LOADED."
4070 GOTO1040
4080 REM
4090 REM BLASTOFF PROCESSING
4100 REM
4110 IFP<>21THEN3980
4120 IFO(9)<>98THEN4200
4130 IFF7=1THEN4160
4140 PRINT"REPAIRS NOT YET COMPLETE."
4150 GOTO1040
4160 M$="CONGRATULATIONS, YOU HAVE JUST BLASTED OFF AND ARE ON YOUR WAY TO EARTH.":GOSUB1
4180 M$="YOUR ESCAPE TIME WAS"+STR$(T1)+" MINUTES.":GOSUB1
4190 GOTO2990
4200 PRINT"YOUR SPACE CRAFT HAS NO FUEL!"
4210 GOTO1040
4220 REM
4230 REM COMPUTER READOUT PROCESSING
4240 REM
4250 IFP<>35THEN3980
4260 GOSUB4590
4270 IFI<>10THEN3960
4280 IFV<>0THEN4320
4290 M$="BOMB DE-ACTIVATOR LOCATED SOMEWHERE EAST OF SPACE STATION, ON MOON'S SURFACE.":GOSUB1
4310 GOTO4360
4320 IFV<>1THEN4350
4330 M$="LOCAL FUEL SOURCE: DILITHIUM CRYSTAL.":GOSUB1
4340 GOTO4360
4350 M$="DILITHIUM FOUND IN SOFT SURFACES.":GOSUB1
4360 IFF7=1THENPRINT"SPACECRAFT REPAIRS COMPLETED."
4365 V=V+1
4370 GOTO1040
4380 REM
4390 REM SUBROUTINE TO DESCRIBE ITEMS AT LOCATION
4400 REM
4410 IFI=1THENB$="AN ELECTRONIC KEY"
4420 IFI=2THENB$="SEALANT"
4430 IFI=3THENB$="AN OXYGEN MODULE"
4440 IFI=4THENB$="AN ILLUMINATOR"
4450 IFI=5THENB$="A ROBOT"
4460 IFI=6THENB$="A DEACTIVATOR"
4470 IFI=7THENB$="A NUCLEAR BOMB"
4480 IFI=8THENB$="A TRANSPORTER UNIT"
4490 IFI=9THENB$="DILITHIUM CRYSTALS"
4500 IFI=10THENB$="A COMPUTER MESSAGE"
4510 IFI=11THENB$="A POWER UNIT"
4520 IFI=12THENB$="A MIRROR"
4530 IFI=13THENB$="A CODED BADGE"
4540 IFI=14THENB$="A POWER PACK"
4550 RETURN
4560 REM
4570 REM SUBROUTINE TO CONVERT AN ITEM TO A NUMBER
4580 REM
4590 FORJ=1TOLEN(B$)
4600 IFMID$(B$,J,1)=" "THEN4640
4610 NEXTJ
4620 I=-1
4630 RETURN
4640 C$=MID$(B$,J+1,3)
4650 I=0
4660 IFC$="ELE"THENI=1
4670 IFC$="KEY"THENI=1
4680 IFC$="SEA"THENI=2
4690 IFC$="OXY"THENI=3
4700 IFC$="MOD"THENI=3
4710 IFC$="ILL"THENI=4
4720 IFC$="ROB"THENI=5
4730 IFC$="DEA"THENI=6
4740 IFC$="NUC"THENI=7
4750 IFC$="BOM"THENI=7
4760 IFC$="TRA"THENI=8
4770 IFC$="DIL"THENI=9
4780 IFC$="CRY"THENI=9
4790 IFC$="COM"THENI=10
4800 IFC$="MES"THENI=10
4810 IFC$="UNI"THENI=11
4820 IFC$="MIR"THENI=12
4830 IFC$="BAD"THENI=13
4840 IFC$="PAC"THENI=14
4850 RETURN
4860 REM
4870 REM SUBROUTINE TO PROCESS TRY COMMAND
4880 REM
4890 INPUTB$
4900 C$=LEFT$(B$,3)
4910 IFC$="TRY"THEN4950
4920 IFC$="USE"THEN4950
4930 I=-1
4940 RETURN
4950 GOSUB4590
4960 IFI<1THEN5050
4970 IFO(I)=99THENRETURN
4980 M$="YOU DON'T HAVE "+MID$(B$,J+1)+"!":GOSUB1
4990 GOTO4930
5000 PRINT "YOUR ATTEMPT FAILS!"
5010 GOTO4930
5020 REM
5030 REM PRINT INSTRUCTIONS
5040 REM
5050 CLS:PRINT"YOU HAVE CRASH LANDED ON THE"
5060 PRINT"EARTH'S MOON. YOU HAVE LIMITED"
5070 PRINT"SUPPLIES AND TIME IN WHICH TO"
5080 PRINT"SURVIVE. TO TRAVEL, YOU MAY"
5090 PRINT"ENTER DIRECTIVES SUCH AS NORTH"
5100 PRINT"OR N, AS WELL AS S, E, W, AND"
5110 PRINT"U OR D (UP OR DOWN). YOU"
5120 PRINT"WILL ENCOUNTER VARIOUS ITEMS"
5130 PRINT"AND SITUATIONS DURING YOUR"
5140 PRINT"TRAVELS. TO COMMUNICATE, ENTER"
5150 PRINT"COMMANDS (VERBS) FOLLOWED BY"
5160 PRINT"OBJECT NAMES (IF APPLICABLE)."
5170 PRINT"FOR EXAMPLE, GET KEY, LEAVE,"
5180 PRINT"USE, AND INVENTORY.":PRINT
5190 INPUT"   PRESS enter TO CONTINUE";M$:CLS
5200 PRINT"ONCE YOU HAVE SURVIVED, THE"
5210 PRINT"OBJECT THEN IS TO ACHIEVE THE"
5220 PRINT"OPTIMAL SURVIVAL TIME."
5230 PRINTTAB(7)"*** GOOD LUCK ***"
5240 RETURN
6000 REM
6001 REM OBJECT LOCATIONS
6002 REM
6010 DATA21,19,99,6,32,,38
6020 DATA35,,35,99,33,34,37
7500 REM
7501 REM TEXT LOCATION DESCRIPTIONS
7502 REM
8001 DATA"AT MARE SERENITATIS. LONG EERIE SHADOWS"
8002 DATA"FROM DISTANT MOUNTAINS AND CRATERS CAST"
8003 DATA"THEMSELVES ACROSS THE BARREN LANDSCAPE."
8004 DATA"ON A PROMONTARY POINT ON THE RIM OF THE"
8005 DATA"CRATER POSIDONIUS, ONLY HALF VISIBLE."
8006 DATA"THERE IS TOTAL DARKNESS TO THE EAST."
8007 DATA"BETWEEN THE CRATERS OF DAWES AND PLINIUS."
8008 DATA"AT A NARROW PASS IN THE MOUNTAINS OF HAEMUS."
8009 DATA"AT A STEEP BASE OF THE CRATER OF MANILUS."
8010 DATA"AT MARE VAPORUM. THE APENNINES MOUNTAINS"
8011 DATA"RISE OMINOUSLY TO THE NORTH AND WEST."
8012 DATA"AT THE BASE OF THE AWESOME MT. EUDOXUS."
8013 DATA"INSIDE THE CRATER OF ARISTOTELES. THE"
8014 DATA"CRATER FLOOR IS LITTERED WITH ROCKS."
8015 DATA"IN LACUS SOMNORIUM, NORTH OF POSIDONIUS"
8016 DATA"AND NORTH EAST OF MARE SERENITATUS."
8017 DATA"AT THE BASE OF THE BURG CRATER IN LACUS"
8018 DATA"MORTIS. THE SURFACE IS VERY SOFT HERE."
8019 DATA"AT THE EAST SIDE OF THE VAST MARE OF"
8020 DATA"IMBRIUM. TO THE NORTH THE LOW ANGLE OF"
8021 DATA"THE SUN CASTS EERIE SHADOWS ON THE SOFT"
8022 DATA"SURFACE AND THE DISTANT MOUNTAINS TO THE"
8023 DATA"EAST. TO THE WEST THE MARE STRETCHES
8024 DATA"OUT OF SIGHT TO THE HORIZON. TO THE SOUTH"
8025 DATA"AT THE BASE OF THE CRATER OF PLATO. A"
8026 DATA"SHINEY OBJECT IS SEEN TO THE WEST."
8027 DATA"STANDING BEFORE A SMALL METAL SHED. A"
8028 DATA"SIGN READS VENTILLATOR SHAFT NUMBER 2."
8029 DATA"SOMEWHERE EAST OF MARE SERENITATUS."
8030 DATA"THERE IS TOTAL DARKNESS."
8031 DATA"AT THE CRASH SITE OF A SPACE CRAFT."
8032 DATA"THE ENTRANCE TO THE SPACE CRAFT IS BEFORE YOU."
8033 DATA"AT THE CENTER OF VAST MARE OF IMBRIUM."
8034 DATA"IN THE AIR LOCK CHAMBER OF THE SPACE CRAFT."
8035 DATA"IN THE AFT CARGO AND FUEL STORAGE COMPARTMENT."
8036 DATA"IN THE ENGINE ROOM OF THE SPACE CRAFT."
8037 DATA"IN THE CONTROL ROOM, THE SHIPS CONSOLE"
8038 DATA"IS BEFORE YOU."
8039 DATA"INSIDE A DARK SHED. A LADDER LEADS DOWN"
8040 DATA"INTO A LARGE METAL SHAFT BELOW."
8041 DATA"IN A VENTILLATOR PASSAGE."
8042 DATA"AT A VENTILLATOR OPENING. THROUGH THE"
8043 DATA"OPENING A LIT PASSAGEWAY CAN BE SEEN."
8044 DATA"IN A LIGHTED SPACE STATION CORRIDOR."
8045 DATA"IN THE SPACE STATION INFIRMARY."
8046 DATA"IN THE RECREATION ROOM AND LIBRARY."
8047 DATA"IN THE MESS HALL. ABANDONED FOOD TRAYS"
8048 DATA"ARE STILL ON THE TABLES."
8049 DATA"IN THE STORAGE ROOM AND SUPPLY AREA."
8050 DATA"IN THE SLEEPING QUARTERS."
8051 DATA"IN AN ELEVATOR AT THE SUBSURFACE LEVEL."
8052 DATA"IN AN ELEVATOR AT THE SURFACE LEVEL."
8053 DATA"IN THE SPACE STATION'S CONTROL CENTER."
8054 DATA"IN THE TRANSPORTER ROOM."
8055 DATA"IN THE SPACE STATION LABORATORY."
8056 DATA"IN THE HANGAR AREA. THE LAUNCH AREA"
8057 DATA"IS LOCATED TO THE SOUTH OF HERE."
8058 DATA"IN AN AIR LOCK CHAMBER BETWEEN THE"
8059 DATA"CHANGING AREA AND THE HANGAR."
8060 DATA"IN A SPACE SUIT CHANGING AREA."
8500 REM
8501 REM MOVEMENT AND TEXT POINTER MATRIX
8502 REM
9001 DATA7,4,2,15,,,1,3
9002 DATA9,3,14,1,,,4,6
9003 DATA2,5,14,4,,,7,7
9004 DATA1,5,3,,,,8,8
9005 DATA4,,3,6,,,9,9
9006 DATA0,,5,,,,10,11
9007 DATA8,1,9,11,,,12,12
9008 DATA,7,10,,,,13,14
9009 DATA10,2,14,7,,,15,16
9010 DATA,9,14,8,0,,17,18
9011 DATA12,15,7,16,,,19,24
9012 DATA,11,,13,,,25,26
9013 DATA,16,12,22,,,27,28
9014 DATA99,99,99,99,,,29,30
9015 DATA11,18,1,,,,31,32
9016 DATA17,16,7,16,,,33,33
9017 DATA16,17,11,17,,,33,33
9018 DATA15,19,,,,,34,34
9019 DATA18,,20,,,,35,35
9020 DATA,,,19,21,,36,36
9021 DATA,,,,,20,37,38
9022 DATA,,13,,,23,39,40
9023 DATA24,,,,22,,41,41
9024 DATA25,23,,,,,42,43
9025 DATA27,26,33,32,24,,44,44
9026 DATA25,,30,31,,,44,44
9027 DATA34,25,41,,,,44,44
9028 DATA,29,42,36,,,44,44
9029 DATA28,38,40,37,,,44,44
9030 DATA,,,26,,,45,45
9031 DATA,,26,,,,46,46
9032 DATA,,25,,,,47,48
9033 DATA,,,25,,,50,50
9034 DATA,27,,,,,49,49
9035 DATA,28,,,24,,53,53
9036 DATA,,28,,,,54,54
9037 DATA,,29,,,,55,55
9038 DATA29,,39,,,,56,57
9039 DATA40,,,38,,,58,59
9040 DATA,39,,29,,,60,60
9041 DATA,,,27,42,,51,51
9042 DATA,,,28,,41,52,52
9050 REM MC-10 EDITS JIM GERRIE
9060 T2=230:REM NEW OXYGEN LV
9070 T2=185:REM ORIGINAL OXYGEN
9080 REM DELETE LINE 9070 FOR
9090 REM EASIER GAME
9100 RETURN
9999 END
