0 CLS:CLEAR350:GOSUB9:DIMC1,CC,M$,A$:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 PRINT@256,;:GOSUB1:GOSUB6:FORZZ=8TO14:PRINT@32*ZZ,"":NEXT:RETURN
6 PRINT@448,"HIT ANY KEY TO CONTINUE"
7 QM$=INKEY$:ON1-(QM$<>"")GOTO7:RETURN
8 POKE49151,16:RETURN
9 POKE49151,64:JJ=0:RETURN
10 CLS:PRINT
20 PRINT " ******************************"
30 PRINT " *                            *"
40 PRINT " *           SORCERY          *"
50 PRINT " *                            *"
60 PRINT " ******************************":PRINT
70 M$="UNTIL 2036 THE WORLD WAS AT PEACE, THEN UNREST BEGAN TO STIR IN SOCIETY. ":GOSUB5
80 M$="22 JUNE 2037: SORCERERS REAPPEAR WITH TERRIFYING POWERS.":GOSUB5
90 M$="SMALL IN NUMBER, THEY ARE EASILY CONTROLLED. ":GOSUB5
100 M$="CHRISTMAS 2037: A WIZARD COUP--ALL ARMIES ARE DESTROYED.":GOSUB5
110 M$="THE DICTATORSHIP OF WITCHCRAFT IS PROCLAIMED. ":GOSUB5
120 M$="FEBRUARY 2038: WIZARDS ARE ABOUT TO BREAK THE WILL OF HUMAN RESISTANCE.":GOSUB5
130 M$="AWARE OF THE DANGER, SOME HAVE TRIED TO COMBAT THIS EVIL POWER, BUT IN VAIN.":GOSUB5
140 M$="AFTER 6 YEARS OF RESEARCH, YOU FINALLY FIND A CLUE: THE ULTIMATE WEAPON IS IN A CASTLE.":GOSUB5
150 M$="WHAT IS THIS ULTIMATE WEAPON? THAT IS FOR YOU TO DISCOVER!":GOSUB5:CLS
200 PRINT"SOME ELEMENTS TO HELP YOU IN":PRINT"YOUR QUEST "
210 PRINT
220 PRINT"N TO GO NORTH."
230 PRINT"S TO GO SOUTH."
240 PRINT"W TO GO WEST."
250 PRINT"E TO GO EAST."
260 PRINT"INV FOR INVENTORY."
265 PRINT"FASTEN TO TIE OR ATTACH"
270 PRINT"QUI TO EXIT."
280 GOSUB6:CLS
290 DIMLI$(24,7),OB$(30,2),VE$(32)
300 FORI=1TO24:FORJ=1TO7
310 READLI$(I,J):NEXTJ:NEXTI
320 DATA AT THE ENTRANCE OF THE CASTLE,,aEaa,,2,,
330 DATA IN THE ENTRYWAY OF THE CASTLE,,aESW,,3,7,1
340 DATA IN A LARGE LIVING ROOM,LUXURY FURNITURE,aESW,,4,8,2
350 DATA IN A MAGNIFICENT HALL,,NESW,6,5,9,3
360 DATA IN A BATHROOM,SINK,aaaW,,,,4
370 DATA IN A SUMPTUOUSLY DECORATED BEDROOM,BED,aaSa,,,4,
380 DATA IN A SMALL SALON,,NESa,2,8,11,
390 DATA IN THE KITCHEN,KITCHENWARE,NaSW,3,,13,7
400 DATA IN A SECRET LOVE NEST,"BED DOOR",aaaa,,,,
410 DATA IN A WORKSHOP,,aaaW,,,,24
420 DATA IN A CORRIDOR,,NEaa,7,12,,
430 DATA IN A LONG STONE CORRIDOR,,aEaW,,13,,11
440 DATA IN A CORRIDOR,,NaaW,8,,,12
450 DATA IN A SECRET ROOM,,NEaa,12,15,,
460 DATA IN A TORTURE CHAMBER!,INSTRUMENTS OF TORTURE,aaaW,,,,14
470 DATA IN THE ATTIC,,aEaa,,17,,
480 DATA IN A LARGE ROOM ILLUMINATED BY CANDLES,,aEaW,,18,,16
490 DATA IN A SMALL DARK ROOM,,aaaW,,,,17
500 DATA ON A LEDGE,THE VOID,aaSa,,,20,
510 DATA IN A BASEMENT ROOM,,NESa,19,21,22,
520 DATA IN AN ELEVATOR,,aaaa,,,,
530 DATA IN A DUNGEON WITH CHAINED SKELETONS,,NaSa,20,,23,
540 DATA IN AN LONG ABANDONED ROOM,,Naaa,22,,,
550 DATA IN A CAVE,,aEaW,,10,,9
560 FORI=1TO30:FORJ=1TO2:READOB$(I,J):NEXTJ:NEXTI
570 DATA BATTERY & LOW WATT BULB,-6,BULB,3,LAMP,-3,GLOVES,5,CAKE,8,PAINTING,9
580 DATA SAW,10,JAR,10,ROPE,15,PARCHMENT,17,LADDER,23,SHEETS,6,CEILING,0
590 DATA CAVE,-9,WINDOW,14,BARS,-14,STATUE,16,SLOT,18,CHANDELIER,4
600 DATA NORTH,0,SOUTH,0,EAST,0,WEST,0,BUTTON,-12,BED,0,RING,14
610 DATA DOGS,22,WALL,0,DOOR,0,PASSAGE,-12
620 FORI=1TO32:READVE$(I):NEXTI
630 DATA "GO",ENT,CLI,DES,SEA,EXA,LOO,TAK,GET,MOV,UNS,LEA,PUT,THR
640 DATA DRO,OPE,CLO,SAW,KIC,PUS,REA,EAT,GIV,FAS,LIG,INS,WEA,SCR,UNF,UNL,WAL,DRI
650 PI=0:LA=0:GA=0:AL=0:PA=0:CO=0:BA=0:FE=0:CR=0:DR=0:EM=0:NO=0
660 L=1:FI=0:LE=0
670 CLS
680 IFL=24ANDLA=0THEN3770
690 IFL=17ANDEM=0THEN3780
700 IFL=23ANDCH=0THEN3790
710 M$="YOU ARE "+LI$(L,1):GOSUB1:PRINT
720 Q1=1
730 M$="YOU SEE: "+LI$(L,2):GOSUB1:IFLI$(L,2)=""THENQ1=0
740 IFL=9ANDAC=0THENPRINT"YOU'RE TRAPPED IN THE ROOM.":GOTO760
750 IFL=9ANDAC<>0THENPRINT"RIDDLED WITH ACID. "
760 Q2=0
770 M$="":FORI=1TO30:IFVAL(OB$(I,2))<>LTHEN900
780 M$=M$+OB$(I,1)+" ":Q2=1
790 IFI=2ANDAL=0THENM$=M$+"(LIT) ":GOTO900
800 IFI=3ANDLA=0THENM$=M$+"(UNLIT) ":GOTO900
810 IFI=3ANDLA<>0THENM$=M$+"(LIT) ":GOTO900
820 IFI=8ANDFI=0THENM$=M$+"(FULL) ":GOTO900
830 IFI=8ANDFI<>0THENM$=M$+"(BROKEN) ":GOTO900
840 IFI=15ANDFE=0THENM$=M$+"(CLOSED) ":GOTO900
850 IFI=15ANDFE<>0THENM$=M$+"(OPEN) ":GOTO900
860 IFI=16ANDBA<>0THENM$=M$+"(SAWED) ":GOTO900
870 IFI=18THENM$=M$+"(IN THE WALL) ":GOTO900
880 IFI=27ANDCH=0THENM$=M$+"(HUNGRY) ":GOTO900
890 IFI=27ANDCH<>0THENM$=M$+"(DEAD) "
900 NEXTI:IFQ1=0ANDQ2=0THENPRINT"NOTHING SPECIAL.":PRINT:GOTO920
910 GOSUB1
920 M$="POSSIBLE DIRECTIONS: "
930 FORI=1TO4:DI$=MID$(LI$(L,3),I,1)
940 IFDI$="N"THENM$=M$+"NORTH "
950 IFDI$="S"THENM$=M$+"SOUTH "
960 IFDI$="E"THENM$=M$+"EAST " 
970 IFDI$="W"THENM$=M$+"WEST "
980 NEXTI:GOSUB1
990 PRINT
1000 PRINT" ------------------------------ ";
1020 IFL=21THEN3800
1040 PRINT"WHAT DO YOU DO? ";:GOSUB1200
1050 IFRIGHT$(R$,1)=" "THENR$=LEFT$(R$,LEN(R$)-1):GOTO1050
1060 IFR$=""THEN1040
1070 IFR$="W"ORR$="E"ORR$="N"ORR$="S"THENM$=R$:GOTO1490
1080 IFLEFT$(R$,3)="QUI"THENCLS:GOSUB8:END
1090 IFLEFT$(R$,3)="INV"THEN1290
1095 IFLEFT$(R$,3)="LOO"THEN670
1100 X=0:FORI=LEN(R$)TO1STEP-1
1110 IFMID$(R$,I,1)=" "ORMID$(R$,I,1)="'"THENX=I:I=1
1120 NEXTI:IFX=0THEN1190
1130 V$=LEFT$(R$,X-1):M$=MID$(R$,X+1,4)
1140 V=0:FORI=1TO32:IFLEFT$(V$,3)=VE$(I)THENV=I:I=32
1150 NEXTI
1160 M=0:FORI=1TO30:IFM$=LEFT$(OB$(I,1),4)THEN:M=I:I=30
1170 NEXTI
1180 IFV=0THEN1190
1181 IFV>23GOTO1184
1182 ONVGOTO1380,1380,1530,1640,1820,1820,670,2290,2290,2940,2460,2530,2530,2560,2560,2650,2760,2850,2910,2940,3050,3090,3140
1184 ON(V-23)GOTO3220,3310,3350,2940,3440,3500,3580,1380,3620
1190 PRINT:PRINT"TRY SOMETHING ELSE!":GOTO1040
1200 C1=0:R$=" "
1205 PRINTCHR$(128);
1210 C1=C1+1:IFC1=30THENPRINTCHR$(8)" ";
1211 IFC1=60THENPRINTCHR$(8)CHR$(128);:C1=0
1212 A$=INKEY$:IFA$=""THEN1210
1220 PRINTCHR$(8);:IFASC(A$)<>13THEN1250
1230 IFLEN(R$)>1THENR$=RIGHT$(R$,LEN(R$)-1):RETURN
1240 R$="":RETURN
1250 IFASC(A$)=8ANDLEN(R$)>1THENR$=LEFT$(R$,LEN(R$)-1):PRINTA$;:GOTO1205
1260 IFA$=" "ORA$="'"THEN1280
1270 IFASC(A$)<65ORASC(A$)>91THEN1205
1280 PRINTA$;:R$=R$+A$:GOTO1205
1290 IFNO=0THENPRINT:PRINT"YOU'RE NOT CARRYING ANYTHING.":GOTO1040
1300 PRINT:M$="YOU'RE CARRYING: ":FORI=1TO12:IFOB$(I,2)<>"100"THEN1370
1310 M$=M$+OB$(I,1)+" "
1320 IFI=3ANDLA=0THENM$=M$+"(UNLIT) ":GOTO1370
1330 IFI=3ANDLA<>0THENM$=M$+"(LIT) ":GOTO1370
1340 IFI<>8THEN1370
1350 IFFI=0THENM$=M$+"(FULL) ":GOTO1370
1360 M$=M$+"(BROKEN) "
1370 NEXTI:GOSUB1:GOTO1040
1380 IFM<>30THEN1410
1390 IFL<>12ORVAL(OB$(30,2))<0THENPRINT:M$="WHERE DO YOU SEE A "+OB$(30,1)+"?":GOSUB1:GOTO1040
1400 L=14:GOTO670
1410 IFM<>14THEN1440
1420 IFL<>9ORVAL(OB$(14,2))<0THENPRINT:M$="WHERE DO YOU SEE A "+OB$(14,1)+"?":GOSUB1:GOTO1040
1430 L=24:GOTO670
1440 IFM<>29THEN1480
1450 IFL<>9THENPRINT:PRINT"CHOOSE A DIRECTION." GOTO1040
1460 IFAC=0THENPRINT:PRINT"IMPOSSIBLE! IT'S CLOSED.":GOTO1040
1470 L=4:GOTO670
1480 IFM<20ORM>23THEN1520
1490 FORI=1TO4
1500 IFLEFT$(M$,1)=MID$(LI$(L,3),I,1)THENL=VAL(LI$(L,I+3)):GOTO670
1510 NEXTI
1520 PRINT:PRINT"YOU CAN'T GO THAT WAY!":GOTO1040
1530 IFL<>6ANDL<>19THENPRINT:PRINT"YOU CANNOT CLIMB HERE!":GOTO1040
1540 IFL<>6THEN1590
1550 IFM<>11THENPRINT:PRINT"YOU MUST BE DRUNK!":GOTO1040
1560 IFOB$(11,2)="100"THENPRINT:PRINT"FIRST DROP YOUR ";OB$(11,1):GOTO1040
1570 IFVAL(OB$(11,2))<>LTHENPRINT:PRINT"WHAT ";OB$(11,1);"?":GOTO1040
1580 L=16:GOTO670
1590 IFM=12THENL=14:GOTO670
1600 IFM<>11THENPRINT:PRINT"YOU SHOULD REST A BIT.":GOTO1040
1610 IFOB$(11,2)="100"THENPRINT:M$="PERHAPS IF YOU DROP YOUR "+OB$(11,1)+"?":GOSUB1:GOTO1040
1620 L=14:GOTO670
1640 IFL<>14ANDL<>16THENPRINT:PRINT"YOU CAN'T GO DOWN HERE.":GOTO1040
1650 IFL=14THEN1680
1660 IFM<>11THENPRINT:PRINT"NOTHING YOU SAY MAKES SENSE!":GOTO1040
1670 L=6:GOTO670
1680 IFM<>11ANDM<>9ANDM<>12THENPRINT:PRINT"YOU MUST REST A LITTLE...":GOTO1040
1690 IFFE=0THENPRINT:M$="IMPOSSIBLE! THE "+OB$(15,1)+" IS CLOSED.":GOSUB1:GOTO1040
1700 IFBA=0THENPRINT:M$=OB$(16,1)+" BLOCK YOUR WAY!":GOSUB1:OB$(16,2)="14":FORZ=1TO2000:NEXTZ:GOTO670
1710 IFM=9ORM=12THEN1750
1720 IFOB$(11,2)="100"ORVAL(OB$(11,2))=LTHENPRINT:PRINT"YOU CAN'T DO THAT.":GOTO1040
1730 IFOB$(11,2)="19"THENL=19:GOTO670
1740 PRINT:PRINT"WHAT ";OB$(11,1);"?":GOTO1040
1750 IFM=12THEN1790
1760 IFVAL(OB$(9,2))<>LANDOB$(9,2)<>"100"THENPRINT:PRINT"WHAT ";OB$(9,1);"?":GOTO1040
1770 IFCR=1THENPRINT:M$="THE "+OB$(9,1)+" BREAKS! YOU'RE FLUNG TO THE BOTTOM OF THE RAVINE.":GOSUB1:GOTO3670
1780 PRINT:M$="YOU GO THROUGH THE "+OB$(15,1)+" AND FALL TO THE BOTTOM OF THE RAVINE.":GOSUB1:GOTO3670
1790 IFVAL(OB$(12,2))<>LANDOB$(12,2)<>"100"THENPRINT:PRINT"WHAT ";OB$(12,1);"?":GOTO1040
1800 IFDR=1THENL=19:GOTO670
1810 GOTO1780
1820 IFM=0THEN2280
1830 IFVAL(OB$(M,2))=0ORVAL(OB$(M,2))=100ORVAL(OB$(M,2))=LTHEN1850
1840 PRINT:PRINT"I DO NOT SEE THAT HERE!":GOTO1040
1850 IFM>23THEN1852
1851 ONMGOTO1860,1870,1890,2280,1920,1930,2280,1940,1960,1970,2280,2280,1980,2000,2020,2070,2100,2110,2120,2280,2280,2280,2280
1852 ON(M-23)GOTO2130,2150,2180,2190,2210,2240,2270
1860 PRINT:M$="YOU'VE NEVER SEEN ANYTHING LIKE IT?":GOSUB1:GOTO1040
1870 IFAL=0THENPRINT:PRINT"IT GIVES OFF A BRIGHT LIGHT.":GOTO1040
1880 PRINT:PRINT"IT GOES OUT.":GOTO1040
1890 IFOB$(1,2)<>"100"THENPRINT:M$="IT'S MISSING A "+OB$(1,1)+".":GOSUB1:GOTO1040
1900 IFLA<>0THENPRINT:PRINT"IT IS ILLUMINATED.":GOTO1040
1910 PRINT:PRINT"IT IS NOT ON.":GOTO1040
1920 PRINT:PRINT"IT IS CHOCOLATE.":GOTO1040
1930 PRINT:PRINT"IT'S A PORTRAIT OF SATAN.":GOTO1040
1940 IFFI=0THENPRINT:PRINT"IT IS FULL OF LIQUID.":GOTO1040
1950 PRINT:PRINT"IT IS BROKEN.":GOTO1040
1960 PRINT:PRINT"IT LOOKS ANCIENT.":GOTO1040
1970 PRINT:PRINT"IT HAS A NEGLECTED LOOK.":GOTO1040
1980 IFL<>6THEN2280
1990 PRINT:M$="THERE IS A HOLE BIG ENOUGH TO PASS THROUGH.":GOSUB1:GOTO1040
2000 REM
2010 PRINT:PRINT"IT IS COMPLETELY DARK INSIDE.":GOTO1040
2020 IFFE=0THENPRINT:PRINT"IT IS CLOSED.":GOTO2040
2030 PRINT:PRINT"IT IS OPEN."
2040 IFVAL(OB$(16,2))<0THENM$="YOU ALSO SEE "+OB$(16,1)+".":GOSUB1:OB$(16,2)="14":GOTO2060
2050 M$="THERE ARE SOME "+OB$(16,1):IFBA<>0THENM$=M$+" (SAWED)."
2051 GOSUB1
2060 PRINT:M$="YOU ALSO SEE A VERY DEEP RAVINE.":GOSUB1:GOTO1040
2070 REM
2080 IFBA=0THENPRINT:PRINT"THEY LOOK STURDY.":GOTO1040
2090 PRINT:PRINT"THEY'VE BEEN SAWED.":GOTO1040
2100 PRINT:PRINT"IT'S A STATUE OF SATAN.":GOTO1040
2110 PRINT:M$="IT IS CLOSED, HORIZONTAL, AND IN THE WALL.":GOSUB1:GOTO1040
2120 PRINT:M$="IT'S GOLD AND ENRUSTED WITH DIAMONDS.":GOSUB1:GOTO1040
2130 REM
2140 PRINT:PRINT"YOU'VE NEVER SEEN THEM?":GOTO1040
2150 IFL=9THEN2280
2160 IFOB$(1,2)<>"-6"THENPRINT:PRINT"IT'S A FOUR POSTER BED.":GOTO1040
2170 PRINT:PRINT"IT'S A FOUR POSTER BED.":M$="YOU DISCOVER A "+OB$(1,1)+" HIDDEN UNDERNEATH IT!":GOSUB1:FORZ=1TO2000:NEXTZ
2171 OB$(1,2)="6":GOTO670
2180 PRINT:PRINT"IT HANGS ON THE WALL.":GOTO1040
2190 IFCH=0THENPRINT:PRINT"THEY LOOK VICIOUS.":GOTO1040
2200 PRINT:PRINT"THEY ARE DEAD.":GOTO1040
2210 IFL<>12THEN2280
2220 IFOB$(24,2)="12"THEN2280
2230 PRINT:M$="IT IS SPLIT FROM TOP TO BOTTOM.":GOSUB1:M$="YOU ALSO NOTICE A "+OB$(24,1)+".":GOSUB1:OB$(24,2)="12"
2231 FORZ=1TO2000:NEXTZ:GOTO670
2240 IFL<>9THEN2280
2250 IFAC=0THENPRINT:PRINT"IT IS MADE OF STEEL.":GOTO1040
2260 PRINT:PRINT"IT IS COVERED IN ACID.":GOTO1040
2270 REM
2280 PRINT:PRINT"YOU SEE NOTHING SPECIAL.":GOTO1040
2290 IFM=24THEN2940
2300 IFM=2THEN2400
2310 IFNO=4THENPRINT:PRINT"YOU'RE CARRY TOO MUCH!":GOTO1040
2320 IFM=0THENPRINT:PRINT"TAKE WHAT?":GOTO1040
2330 IFOB$(M,2)="100"THENPRINT:PRINT"YOU ALREADY HAVE IT!":GOTO1040
2340 IFVAL(OB$(M,2))<>LTHENPRINT:PRINT"I DO NOT SEE IT HERE!":GOTO1040
2350 IFM=9THEN2420
2360 IFM=12THEN2440
2370 IFM>12THENPRINT:PRINT"YOU CANNOT TAKE IT!":GOTO1040
2380 IFM=6THENOB$(14,2)="9"
2390 OB$(M,2)="100":NO=NO+1:GOTO670
2400 IFAL<>0THEN2390
2410 PRINT:PRINT"YOU FORGOT TO DO SOMETHING!":GOTO1040
2420 IFCR<>0THEN2410
2430 GOTO2390
2440 IFDR<>0THEN2410
2450 GOTO2390
2460 IFM<>2THENPRINT:PRINT"IT CANNOT BE UNSCREWED!":GOTO1040
2470 IFL<>3THENPRINT:PRINT"THERE IS NOTHING TO UNSCREW HERE!":GOTO1040
2480 IFAL<>0THENPRINT:PRINT"YOU ALREADY DID THAT.":GOTO1040
2490 IFOB$(4,2)<>"100"THENPRINT:M$="IMPOSSIBLE: YOU WOULD BURN YOURSELF.":GOSUB1:GOTO1040
2500 IFOB$(3,2)<>"-3"THENAL=1:GOTO670
2510 AL=1:PRINT:M$="THE LAMP IT WAS IN WAS CONNECTED TO AN ELECTRICAL OUTLET BUT NOW IT HAS BECOME DISCONNECTED.":GOSUB1
2520 OB$(3,2)="3":FORZ=1TO3000:NEXTZ:GOTO670
2530 IFM=0THENPRINT:PRINT"DROP WHAT?":GOTO1040
2540 IFOB$(M,2)<>"100"THENPRINT:PRINT"YOU DON'T HAVE IT WITH YOU.":GOTO1040
2550 OB$(M,2)=STR$(L):NO=NO-1:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2560 IFM<>8THEN2530
2570 IFL<>9THEN2530
2580 IFOB$(8,2)<>"100"THENPRINT:PRINT"YOU DON'T HAVE IT.":GOTO1040
2590 IFFI<>0THEN2530
2600 PRINT:PRINT"WHERE? ";:GOSUB1200
2610 IFLEFT$(R$,4)<>LEFT$(OB$(29,1),4)THENPRINT:M$="WHEN IT FELL IT BROKE.":GOSUB1:FI=1:OB$(8,2)="9":NO=NO-1:GOTO2645
2620 PRINT:M$="THE DOOR IS ERODED BY THE ACID IN THE JAR.":GOSUB1
2630 NO=NO-1
2640 FI=1:AC=1:OB$(8,2)="9"
2645 FORZ=1TO2000:NEXTZ:GOTO670
2650 IFM<>15 ANDM<>29THENPRINT:PRINT"IT DOES NOT OPEN!":GOTO1040
2660 IFM=29THEN2700
2670 IFL<>14THENPRINT:PRINT"WHAT ";OB$(15,1);"?":GOTO1040
2680 IFFE<>0THENPRINT:PRINT"YOU ALREADY DID THAT.":GOTO1040
2690 FE=1:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2700 IFL=24ORL=19THENPRINT:PRINT"WHAT ";OB$(29,1);"?":GOTO1040
2710 IFL<>9THENPRINT:PRINT"IT'S ALREADY OPEN!":GOTO1040
2720 IFAC<>0THEN2740
2730 PRINT:M$="IMPOSSIBLE! IT DOES NOT HAVE A HANDLE AND IT IS SEALED SHUT.":GOSUB1:GOTO1040
2740 PRINT:M$="YOU CAN'T, BUT THERE IS A BIG ENOUGH HOLE TO PASS THROUGH.":GOSUB1
2750 GOTO1040
2760 IFM<>15ANDM<>29THENPRINT:PRINT"IT DOES NOT CLOSE.":GOTO1040
2770 IFM=29THEN2810
2780 IFL<>14THENPRINT:PRINT"WHAT ";OB$(15,1);"?":GOTO1040
2790 IFFE=0THENPRINT:PRINT"YOU ALREADY DID THAT!":GOTO1040
2800 FE=0:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2810 IFL=24ORL=19THENPRINT:PRINT"WHAT ";OB$(29,1);"?":GOTO1040
2820 IFL<>9THENPRINT:M$="DON'T FORGET YOU'RE LOCKED IN...":GOSUB1:GOTO1040
2830 IFAC=0THENPRINT:M$="YOUR MENTAL STATE HAS REACHED THE BREAKING POINT I THINK...":GOSUB1:GOTO1040
2850 IFOB$(7,2)<>"100"THENPRINT:PRINT"WITH YOUR TEETH?":GOTO1040
2860 IFM<>16THENPRINT:PRINT"IT CANNOT BE CUT.":GOTO1040
2870 IFL<>14OROB$(16,2)="-14"THENPRINT:PRINT"WHAT ";OB$(16,1);"?":GOTO1040
2880 IFBA<>0THENPRINT:PRINT"YOU ALREADY DID THAT!":GOTO1040
2890 IF FE=0THENPRINT:PRINT"YOU FORGOT TO DO SOMETHING...":GOTO1040
2900 PRINT:PRINT"OK":BA=1:FORZ=1TO1000:NEXTZ:GOTO670
2910 IFM<>17THENPRINT:PRINT"FUNNY IDEA...":GOTO1040
2920 IFL<>16THENPRINT:PRINT"WHAT ";OB$(17,1);"?":GOTO1040
2930 PRINT:PRINT"OK":EM=1:GOTO1040
2940 IFM<>6ANDM<>24THENPRINT:PRINT"THAT ACCOMPLISHES NOTHING.":GOTO1040
2950 IFM=24THEN3010
2960 IFOB$(6,2)="100"OR(OB$(14,2)="9"ANDOB$(6,2)="9")THENPRINT:PRINT"NOTHING HAPPENS.":GOTO1040
2970 IFVAL(OB$(6,2))<LTHENPRINT:PRINT"WHAT ";OB$(6,1);"?":GOTO1040
2980 IFL<>9THENPRINT:PRINT"THAT ACCOMPLISHES NOTHING.":GOTO1040
2990 PRINT:M$="A "+OB$(14,1)+" WAS HIDDEN BEHIND IT!":GOSUB1
3000 FORZ=1TO2000:NEXTZ:OB$(14,2)="9":GOTO670
3010 IFL<>12OROB$(24,2)="-12"THENPRINT:PRINT"WHAT ";OB$(24,1);"?":GOTO1040
3020 IFOB$(30,2)="12"THENPRINT:PRINT"THAT ACCOMPLISHES NOTHING.":GOTO1040
3030 OB$(30,2)="12":PRINT:M$="THAT CAUSED A "+OB$(30,1)+" TO OPEN!":GOSUB1
3040 FORZ=1TO1500:NEXTZ:GOTO670
3050 IFM<>10THENPRINT:PRINT"THAT CANNOT BE READ.":GOTO1040
3060 IFOB$(10,2)<>"100"ANDVAL(OB$(10,2))<>LTHENPRINT:PRINT"WHAT ";OB$(10,1);"?":GOTO1040
3070 IFLE=0THENPRINT:M$="IT IS WRITTEN IN A STRANGE LANGUAGE THAT YOU DO NOT RECOGNIZE.":GOSUB1:GOTO1040
3080 GOTO3720
3090 IFM<>5THENPRINT:PRINT"HUMANS ARE SO FUNNY!":GOTO1040
3100 IFOB$(5,2)<>"100"ANDVAL(OB$(5,2))<>LTHENPRINT:PRINT"WHAT ";OB$(5,1);"?":GOTO1040
3110 PRINT:M$="THE POISON IN IT IS VERY TASTY...":GOSUB1
3120 M$="THAT WAS PARTICULARLY EFFECTIVE! YOU'RE AMAZED AT YOUR SUCCESS.":GOSUB1:GOTO3670
3140 IFM<>5THENPRINT:PRINT"YOU ARE VERY GENEROUS!":GOTO1040
3150 IFOB$(5,2)<>"100"THENPRINT:M$="YOU ARE NOT CARRYING IT WITH YOU.":GOSUB1:GOTO1040
3160 IFL<>22THENPRINT:M$="THAT IS VERY GENEROUS ON YOUR PART.":GOSUB1:GOTO1040
3170 PRINT:PRINT"TO WHO? ";:GOSUB1200:PRINT
3180 IFLEFT$(OB$(27,1),4)<>LEFT$(R$,4)THENPRINT:M$="THE LEAST THAT WE CAN SAY IS THAT YOU ARE GENEROUS !":GOSUB1:GOTO1040
3190 PRINT:M$="THEY EAT IT RIGHT UP! HOWEVER, IT IS THEIR LAST MEAL: THEY SLOWLY DIE FROM THE POISON IN IT!":GOSUB1
3200 OB$(5,2)="0":CH=1:NO=NO-1
3210 FORZ=1TO3000:NEXTZ:GOTO670
3220 IFM<>9ANDM<>12THENPRINT:PRINT"IT DOES NOT ATTACH.":GOTO1040
3230 IF(M=9ANDCR=1)OR(M=12ANDDR=1)THENPRINT:PRINT"IT'S ALREADY DONE.":GOTO1040
3240 IFOB$(M,2)<>"100"THENPRINT:PRINT"YOU DO NOT HAVE THEM.":GOTO1040
3250 IFL<>14THENPRINT:PRINT"YOU CANNOT FASTEN IT HERE.":GOTO1040
3260 PRINT:PRINT"WHERE? ";:GOSUB1200:PRINT
3270 IFLEFT$(R$,4)<>LEFT$(OB$(26,1),4)THENPRINT:PRINT"YOU CANNOT ATTACH IT!":GOTO1040
3280 NO=NO-1
3290 PRINT:PRINT"OK":OB$(M,2)="14":IFM=9THENCR=1:GOTO1040
3300 DR=1:GOTO1040
3310 IFM<>3THENPRINT:PRINT"IT DOES NOT LIGHT!":GOTO1040
3315 IFOB$(3,2)<>"100"THENPRINT:M$="YOU HAVE NOTHING TO LIGHT IT WITH.":GOSUB1:GOTO1040
3320 IFOB$(1,2)<>"100"THENPRINT:M$="IMPOSSIBLE: THERE IS NO "+OB$(1,1)+" HERE.":GOSUB1:GOTO1040
3330 IFLA<>0THENPRINT:PRINT"IT'S ALREADY DONE.":GOTO1040
3340 LA=1:PRINT:PRINT"OK":GOTO1040
3350 IFM<>10THENPRINT:PRINT"YOU CAN'T.":GOTO1040
3360 IFOB$(10,2)<>"100"THENPRINT:M$="YOU DON'T HAVE IT WITH YOU.":GOSUB1:GOTO1040
3370 IFL<>18THENPRINT:M$="I DO NOT SEE WHERE YOU COULD DO THAT!":GOSUB1:GOTO1040
3380 IFLE<>0THENPRINT:PRINT"YOU ALREADY DID THAT.":GOTO1040
3390 PRINT:PRINT"WHERE? ";:GOSUB1200:PRINT
3400 IFLEFT$(R$,4)<>LEFT$(OB$(18,1),4)THENPRINT:PRINT"YOU CANNOT INSERT IT THERE.":GOTO1040
3410 PRINT:M$="IT IS PULLED INSIDE, A FEW MINUTES LATER IT RE-EMERGES.":GOSUB1
3420 LE=1:NO=NO-1
3430 OB$(10,2)="18":FORZ=1TO2000:NEXTZ:GOTO670
3440 IFM<>2THENPRINT:PRINT"THAT CANNOT BE SCREWED.":GOTO1040
3450 IFL<>3THENPRINT:M$="THERE IS NOTHING HERE THAT CAN BE UNSCREWED.":GOSUB1:GOTO1040
3460 IFAL=0THENPRINT:M$="IT IS ALREADY IN THE SOCKET.":GOTO1040
3470 IFOB$(2,2)<>"100"THENPRINT:PRINT"YOU DON'T HAVE IT WITH YOU.":GOTO1040
3480 PRINT:M$="WHILE ATTEMPTING TO RE-CONNECT IT, YOU ELECTROCUTE YOURSELF!":GOSUB1
3490 GOTO3670
3500 IFM<>9ANDM<>12THENPRINT:PRINT"IT DOES NOT COME OFF.":GOTO1040
3510 IFVAL(OB$(9,2))<>LANDOB$(9,2)<>"100"ANDM=9THENPRINT:PRINT"WHAT ";OB$(9,1);"?":GOTO1040
3520 IFVAL(OB$(12,2))<>LANDOB$(12,2)<>"100"ANDM=12THENPRINT:PRINT"WHAT ";OB$(12,1);"?":GOTO1040
3530 IFM=9ANDCR=0THENPRINT:PRINT"IT IS NOT ATTACHED.":GOTO1040
3540 IFM=12ANDDR=0THENPRINT:PRINT"IT IS NOT ATTACHED.":GOTO1040
3550 IFL<>14THENPRINT:PRINT"YOU CANNOT DO THAT HERE.":GOTO1040
3560 PRINT:PRINT"OK":IFM=9THENCR=0:GOTO1040
3570 DR=0:GOTO1040
3580 IFM<>3THENPRINT:PRINT"HOW??!":GOTO1040
3590 IFOB$(3,2)<>"100"THENPRINT:PRINT"YOU DON'T HAVE THAT ON YOU.":GOTO1040
3600 IFLA=0THENPRINT:PRINT"IT IS ALREADY OFF.":GOTO1040
3610 PRINT:PRINT"OK":LA=0:GOTO1040
3620 IFM<>8THENPRINT:PRINT"SURE, WHY NOT TRY.":GOTO1040
3630 IFOB$(8,2)<>"100"ANDVAL(OB$(8,2))<>LTHENPRINT:PRINT"WHAT ";OB$(8,1);"?":GOTO1040
3640 IFFI<>0THENPRINT:M$="IT IS BROKEN: THERE IS NOTHING INSIDE.":GOSUB1:GOTO1040
3650 PRINT:PRINT"IT WAS ACID..."
3660 M$="I WILL LET YOU GUESS WHAT HAPPENED!":GOSUB1
3670 PRINT:PRINT:M$="YOU FAILED IN YOUR MISSION. YOU ARE A TOTAL LOSER.":GOSUB1
3680 PRINT:PRINT"WOULD YOU LIKE TO TRY AGAIN?"
3690 A$=INKEY$:IFA$=""THEN3690
3700 IFA$="Y"THENRUN
3710 CLS:PRINT@448,"C O W A R D !":FORZ=1TO3000:NEXTZ:GOSUB8:END
3720 CLS:PRINT"IT'S A SPELL OF SOME SORT!"
3730 PRINT:M$="IT REVEALS A WAY TO RID THE EARTH OF ALL SORCERERS!":GOSUB1
3750 PRINT:PRINT:M$="YOU HAVE EARNED THE TITLE OF THE GREATEST MAN OF THE 21ST CENTURY!":GOSUB1
3760 GOSUB8:END
3770 PRINT:M$="THIS "+OB$(14,1)+" IS SO DARK THAT YOU FALL AND BREAK YOUR NECK.":GOSUB1:GOTO3670
3780 PRINT:M$="A BOLT OF LIGHTENING COMES FROM NOWHERE AND FRIES YOU.":GOSUB1:GOTO3670
3790 PRINT:M$="THE "+OB$(27,1)+" GET A MOUTHFUL OF YOU!":GOSUB1:GOTO3670
3800 PRINT:PRINT"THIS ELEVATOR HAS NO BUTTON."
3810 PRINT"BUT IT STARTS BY ITSELF!":FORZ=1TO1500:NEXTZ
3820 M$="AFTER A FEW SECONDS, IT STOPS AND THE DOORS OPEN... IN HELL!":GOSUB1
3830 GOTO3670
