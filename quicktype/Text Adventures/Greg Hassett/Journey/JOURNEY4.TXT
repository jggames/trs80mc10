0 GOSUB9000:CLEAR2000:DIMCC,C1,M$,I$:GOTO20
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB1640:GOTO70
20 CP=1:GOSUB1850
40 GOSUB1750:GOSUB1640
50 IFCP=18AND(VB<>25ORNO<>21)THENM$="HORRORS! THE TROLL THROWS AN AXE AT ME! I AM DEAD.":GOSUB1:END
60 IFCP=18THENM$=M2$:GOSUB1:F5=1:GOSUB1640
70 IFNO<23ORVB>1ORNO>30THEN160
80 IFNO<=26THENFD=FD+1:GOTO90
81 FD=-10000
90 IFNO>26THENNO=NO-4
100 NO=NO-22
110 IFCP=10ANDNO=1THENM$="AND FALL INTO THE LAVA??? FAT CHANCE!":GOSUB1:GOTO3
120 IFCP=11ANDNO=2THENM$="DO YOU KNOW WHAT MOLTEN LAVA IS??? IT'S HOT, THAT'S WHAT!":GOSUB1:GOTO3
130 IFCP=29ANDOB(10,0)=29AND(NO=4ORNO=2)THENM$="THE HYDRA WON'T LET ME THROUGH!":GOSUB1:GOTO3
140 IFP(CP,NO)=0THENPRINT"I CAN'T GO THAT WAY!":GOTO3
150 CP=P(CP,NO):GOTO40
160 IFVB<>2ANDVB<>20THEN240
170 IFNO=10ANDOB(33,0)=CPTHENM$="IT IS SORT OF HEAVY, YOU KNOW.":GOSUB1:GOTO3
180 IFNO=0THENM$="I DON'T KNOW WHAT A "+CHR$(34)+NO$+CHR$(34)+" IS.":GOSUB1:GOTO3
190 IFOB(NO,0)<>CPTHENPRINT"I DON'T SEE IT HERE.":GOTO3
200 IFNO=10ORNO=20THEN PRINT"OH GIVE ME A BREAK!!":GOTO3
210 IFOB(NO,0)=-1THENPRINT"I'M ALREADY CARRYING IT.":GOTO3
220 IFOB(NO,0)=CPANDNT>=6THENM$="I CAN'T CARRY ANY MORE. YOU'LL HAVE TO DROP SOMETHING.":GOSUB1:GOTO3
230 IFOB(NO,0)=CPTHENPRINT"CHECK.":NT=NT+1:OB(NO,0)=-1:GOTO3
240 IFVB<>4ANDVB<>29THEN290
250 F3=0
260 PRINT"I AM CURRENTLY CARRYING:":FORI=1TOLO:IFOB(I,0)=-1THENM$=OB$(I):GOSUB1:F3=1
270 NEXTI:IFF3=0THENPRINT"NOTHING."
280 GOTO3
290 IFVB=3THEN40
300 IFNO=0ANDVB=0THENM$="THAT DOESN'T MAKE ANY SENSE TO ME.":GOSUB1:GOTO3
310 IFVB<>5THEN370
320 J=0
330 FORI=1TOLO:IFOB(I,0)=1THENJ=J+OB(I,1)
340 NEXTI:M$="OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE"+STR$(J):GOSUB1
350 IFJ=0THENPRINT"BOY, YOU STINK!"
360 GOTO3
370 IFVB<>6ANDVB<>22THEN440
380 IFNO$<>"EVERYTHING"ANDNO$<>"ALL"THEN420
390 FORI=1TOLO:IFOB(I,0)=-1THENOB(I,0)=CP
400 NEXTI:NT=0
410 PRINT"OKAY.":GOTO3
420 IFOB(NO,0)<>-1THENM$="HOW CAN I DROP WHAT I'M NOT HOLDING???":GOSUB1:GOTO3
430 OB(NO,0)=CP:PRINT"O.K.":NT=NT-1:GOTO3
440 IFVB<>7ANDVB<>28THEN480
450 IFH$(CP)=""THENM$="YOU GOT ME IN TO THIS MESS, NOW YOU GET ME OUT!":GOSUB1
460 M$=H$(CP):GOSUB1:H$(CP)=""
470 GOTO3
480 IFVB<>8THEN550
490 CLS:PRINT"SAVING...":CSAVE*OB,"JOURNSAV"
540 GOTO40
550 IFVB<>9THEN610
560 CLS:PRINT"LOADING...":CLOAD*OB,"JOURNSAV"
600 GOTO40
610 IFVB<>10THEN630
620 GOTO1410
630 IFVB<>11THEN660
640 IFNO=0THENPRINT"SAY WHAT?":GOTO3
650 M$="OKAY, IF IT IS REALLY NECESSARY..."+NO$:GOSUB1:GOTO3
660 IFVB<>12THEN700
670 IFNO<>12THENPRINT"I DON'T DO STUPID THINGS.":GOTO3
680 IFOB(12,0)<>-1THENM$="I DON'T HAVE ANY COKE TO DRINK!":GOSUB1:GOTO3
690 PRINTM3$:OB(12,0)=0:OB(13,0)=-1:GOTO3
700 IFVB<>13THEN770
710 IFOB(31,0)<>-1THENPRINT"BUT I HAVE NO FOOD!!":GOTO3
720 IFOB(NO,0)<>CPTHENPRINT"I DON'T SEE IT.":GOTO3
730 IFNO=10THEN735
731 GOTO740
735 M$="THE HYDRA WHARFS DOWN THE FOOD.":GOSUB1:OB(31,0)=0:OB$(10)="A GIANT HYDRA RUBBING HIS STOMACH":GOTO3
740 IFNO=20THENM$="THE BUGS IN THESE CAVES ONLY EAT HUMAN FLESH.":GOSUB1:GOTO3
750 IFNO<>3THENPRINT"YOU CAN'T BE SERIOUS.":GOTO3
760 M$="IT REFUSES TO EAT THE FOOD.":GOSUB1:GOTO3
770 IFVB<>14THEN850
780 IFNO=10ANDOB(33,0)=CPTHENM$="THE UNDERTAKER JUST TOOK HIM AWAY.":GOSUB1:OB(33,0)=0:GOTO3
790 IFOB(NO,0)<>CPANDOB(NO,0)<>-1THEN795
791 GOTO800
795 M$="I DON'T SEE IT AND I'M NOT HOLDING IT, SO HOW CAN I HIT IT?":GOSUB1:GOTO3
800 IFNO=3THENM$="NO!!! I KIND OF LIKE THE PARROT! POLLY WANT CWAKER?":GOSUB1
810 IFNO=10THENM$="OKAY... THE HYDRA GRUNTS "+CHR$(34)+"DARN MOSQUITOES"+CHR$(34):GOSUB1
820 IFNO=20THENM$="YUCH! I DON'T WANT TO TOUCH IT!!!":GOSUB1
830 IFNO=3ORNO=10ORNO=20THENGOTO3
840 M$="OKAY... OUCH! I HURT MY HAND!":GOSUB1:GOTO3
850 IFVB=15THENM$="I CAN'T KILL ANYTHING. I'M TOO WEAK. BUT MAYBE IF YOU WERE A BIT MORE SPECIFIC...":GOSUB1:GOTO3

860 IFVB<>16THEN950
870 IFOB(33,0)=CPANDNO=10THENM$="IT IS ALREADY BURNED TO A CRISP.":GOSUB1:GOTO3
880 IFOB(6,0)<>-1THENM$="I DON'T HAVE A LIGHTER OR MATCHES!!":GOSUB1:GOTO3
890 IFNO=3THENM$="NEVER!! YOU KNOW SOMETHING, YOU'RE A SADIST.":GOSUB1:GOTO3
900 IFNO=10ANDOB(10,0)=CPTHENOB(10,0)=0:OB(33,0)=CP:M$="THE HYDRA CRUMBLES TO THE GROUND DEAD.":GOSUB1:GOTO3
910 IFNO=20THENM$="THE BUG CANNOT BE BURNED. <GULP>":GOSUB1:GOTO3
920 IFNO=10THENOB(10,0)=0
930 PRINT"IT DOESN'T BURN.":GOTO3

950 IFVB=17THENM$=" AND JUST WHO DO YOU THINK I AM, ROCKY???":GOSUB1:GOTO3
960 IFVB=10THENM$="I DON'T KNOW HOW TO LIGHT THINGS. IF YOU WANT ME TO BURN SOMETHING, TELL ME SO!":GOSUB1:GOTO3
970 IFVB<>19THEN1060
980 IFNO$="COIN"THENNO$="QUARTER":NO=7
990 IFNO=0THENM$="I DON'T KNOW WHAT ONE IS.":GOSUB1:GOTO3
1000 IFNO=7THEN1020
1010 IFOB(NO,0)<>-1THENM$="I'M NOT HOLDING IT RIGHT NOW.":GOSUB1:GOTO3
1020 IFCP<>27THENM$="I DON'T SEE ANY COKE MACHINES HERE.":GOSUB1:GOTO3
1030 IFNO<>7THENPRINT"THAT IS RIDICULOUS.":GOTO3
1040 IFOB(NO,0)<>-1THENPRINT"I HAVE NO MONEY.":GOTO3
1050 IFOB(7,0)=-1ANDCP=27THEN1055
1051 GOTO1060
1055 M$="ALL RIGHT. CHIKCLIKCHINGGGG! A COKE POPPED OUT. THERE IS NOW A BOTTLE OF COKE HERE.":GOSUB1
1056 OB(7,0)=0:NT=NT-1:OB(12,0)=27:GOTO3
1060 IFVB<>21THEN1150
1080 IFNO<>18ANDOB(NO,0)=-1THENM$="IT'S KIND OF HEAVY, YOU KNOW.":GOSUB1:GOTO3
1090 IFOB(NO,0)<>-1THENM$="FIRST YOU HAVE TO HELP ME FIND IT, DUMMY!":GOSUB1:GOTO3
1100 IFCP=10ORCP=11THEN1120
1110 PRINT"NOTHING HAPPENED":GOTO3
1120 IFCP=10THENCP=11:GOTO1140
1130 CP=10
1140 GOTO40
1150 IFVB=23THENVB=1:GOTO70
1160 IFVB=24THENM$="TRY KILL "+NO$:GOSUB1:GOTO3
1170 IFVB<>25THEN1200
1180 IFNOC21THENPRINT"NOTHING HAPPENED":GOTO3
1190 M$="SIR WHAT??? I DON'T UNDERSTAND!":GOSUB1:GOTO3
1200 IFVB<>26THEN1270
1210 IFCP=1ANDOB(14,0)=-1ANDOB(22,0)=1THENPRINT"OKAY!!!":GOTO1400
1220 IFCP<>1THENPRINT"I CAN'T.":GOTO3
1230 IFVB<>26THEN1270
1240 IFOB(14,0)<>-1THENPRINT"I CAN'T. I HAVE NO TOOLS.":GOTO3
1250 M$="I CAN'T, I DON'T SEE THE FRIBULATING GONKULATOR AROUND ME."
1251 IFOB(22,0)=-1THENM$=M$+" I THINK I HAVE TO DROP IT FIRST."
1252 GOSUB1:GOSUB1640
1270 IFVB<>27THEN1350
1280 IFNO=0THENM$="WHAT THE DEVIL IS A "+NO$+"??"GOSUB1:GOTO3
1290 IFNO=20ORNO=10THENIFOB(NO,0)=CPTHENM$="YUCH!!! I'D NEVER EAT A "+NO$+"!!!":GOSUB1:GOTO3
1300 IFNO=10ANDOB(33,0)=CPTHENM$="WELL...IT'S COOKED NOW, BUT IT STILL LOOKS KIND OF GROSS.":GOSUB1:GOTO3
1310 IFOB(NO,0)<>-1THENPRINT"I'M NOT HOLDING IT.":GOTO3
1320 IFNO=3ANDOB(3,0)=-1THENM$="I'D RATHER NOT. I DON'T CARE FOR PARROT MEAT.":GOSUB1:GOTO3
1330 IFNO=31ANDOB(31,0)=-1THENNT=NT-1:M$="HUMM! IT WAS FILET MIGNON, MY FAVORITE DISH.":GOSUB1:OB(31,0)=0:GOTO3
1340 M$="I THINK THAT "+OB$(NO)+" WOULD GIVE ME INDIGESTION, AND I DON'T HAVE ANY PEPTO-BISMOL.":GOSUB1:GOTO3
1350 IFVB<>30THEN1390
1360 IFCP<>10ANDCP<>11THENPRINT"NOTHING HAPPENED.":GOTO3
1370 M$="NICE TRY, BUT THAT'S AN OLD, WORN OUT MAGIC WORD.":GOSUB1
1380 GOTO3
1390 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB1:GOTO3
1400 FORI=1TO1500:NEXTI
1410 M$="OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE SCORED":J=0
1420 FORI=1TOLO:IFOB(I,0)=1THENJ=J+OB(I,1)
1430 NEXTI
1440 M$=M$+STR$(J)+" POINTS.":GOSUB1
1450 IFJ=175THENM$="ALL OF ADVENTURERDOM GIVES TRIBUTE TO YOU, ADVENTURER GRAND MASTER!":GOSUB1:END
1460 IFJ>165THENM$="YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS A.":GOSUB1:END
1470 IFJ>150THEN M$="YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS B.":GOSUB1:END
1480 IFJ>140THENM$="YOU HAVE REACHED A "+CHR$(34)+"JUNIOR  MASTER"+CHR$(34)+" STATUS":GOSUB1:END
1490 IFJ>120THENM$="YOU MAY NOW CONSIDER YOURSELF A "+CHR$(34)+"SEASONED ADVENTURER"+CHR$(34):GOSUB1:END
1500 IFJ>110THENM$="YOU HAVE ACHIEVED THE RATING: "+CHR$(34)+"EXPERIENCED ADVENTURER"+CHR$(34):GOSUB1:END
1510 IFJ>100THENM$="YOUR SCORE QUALIFIES YOU AS A NOVICE CLASS ADVENTURER.":GOSUB1:END
1520 M$="YOU ARE OBVIOUSLY A RANK AMATEUR. BETTER LUCK NEXT TIME.":GOSUB1:END
1530 M$="GADZOOKS!!! A GIANT BUG JUST WALKED IN TO KILL ME!!!":GOSUB1:FF=1:GOSUB1640
1560 IFNO$="SESAME"ANDVB$="OPEN"THENM$="THE BUG HAS SHRUNK TO IT'S PROPER SIZE AND SCURRIED AWAY.":GOSUB1:GOTO1620
1570 IFNO<>20ORVB<>15THEN1575
1571 GOTO1580
1575 M$="HELP ME!!! I'M BEING DEVOURED BY A GAINT BUG!!! THE END IS NEAR! OW!! THIS HURTS!! I'M DYING...DYING...DEAD."
1576 GOSUB1:END
1580 IFOB(2,0)<>-1THENNO=2:M$="I DON'T HAVE A SWORD!!!":GOSUB1:GOTO1570
1590 IFSW=8THENM$="MY SWORD IS DULL, REMEMBER???":GOSUB1:NO=2:GOTO1570
1600 M$="DING! DONG! THE BUG IS DEAD, THE BUG IS DEAD!!!":GOSUB1
1610 SW=SW+1:IFSW=8THENM$="MY SWORD IS DULL NOW.":GOSUB1
1620 GOTO3
1640 PRINT"ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ";:INPUT"WHAT SHOULD I DO";CM$
1650 CLS:IFCM$="ABRA CADABRA"ANDF5<3ANDFF=1THENNO$="SESAME":VB$="OPEN":FF=0:F5=F5+1:RETURN
1660 VB$="":NO$="":VB=0:NO=0
1670 LC=LEN(CM$):FORI=1TOLC:IFMID$(CM$,I,1)<>" "THENVB$=VB$+MID$(CM$,I,1):NEXTI
1680 FORI=1TONV:IFVB$(I)=VB$THENVB=I:TM=I:I=NV:NEXTI:I=TM:GOTO1700
1681 NEXTI
1690 VB=0:NO$=VB$:GOTO1720
1700 IFLEN(VB$)+1>=LEN(CM$)THENNO=0:RETURN
1710 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(VB$))
1720 FORI=1TONN:IFNO$(I)=NO$THENTM=I:I=NN:NEXTI:I=TM:GOTO1740
1721 NEXTI
1730 NO=0:RETURN
1740 NO=I:RETURN
1750 M$=P$(CP):GOSUB1:PRINT 
1760 FORI=1TOLO:IFOB(I,0)=CPTHENM$="THERE IS "+OB$(I)+" HERE.":GOSUB1
1770 NEXTI
1780 A$="I CAN GO: ":F2=0
1790 FORI=1TO4:IFP(CP,I)THENA$=A$+NO$(I+22)+", ":F2=1
1800 NEXTI:IFF2=0THENA$=A$+"NOWHERE!, "
1801 M$=LEFT$(A$,LEN(A$)-2):GOSUB1
1810 IFCP=18ANDOB(3,0)=-1THENPRINT:M$=M1$:GOSUB1
1820 IFINT(RND(0)*35)+1=5ANDCP<>1ANDCP<>37ANDCP<>27THENPRINT:GOTO1530
1830 IFFD>4THEN1835
1831 RETURN
1835 M$="IF YOU'D LIKE, YOU CAN MAKE IT EASIER ON YOURSELF BY TYPING N, S, E, OR W INSTEAD OF NORTH, SOUTH, ECT.":GOSUB1
1836 FD=0:RETURN
1850 NP=37:DIMP$(NP)
1870 P$(1)="I AM IN A GIANT TUNNEL BORING MACHINE. ON A COMPUTER SCREEN IT SAYS: SHIP WILL NOT FUNCTION--"
1871 P$(1)=P$(1)+" FRIBULATING GONKULATOR IS BURNED OUT.":DATA2,6,5,4
1881 P$(2)="I'M IN A COBBLESTONE HALLWAY. I CAN HEAR CHIRPING NEARBY. SOUNDS LIKE BUGS...":DATA8,1,3,5
1890 P$(3)="I'M IN A DARK ROOM...OH NO!!! THERE ARE GIANT BUGS ALL AROUND ME!!!":DATA0,,,2
1900 P$(4)="I'M IN A SMALL ROOM. A SCRIBBLING HERE SAYS: HAR HAR HAR! YOU'LL NEVER MAKE IT! "+CHR$(34)+"ARBADAC ARBA"+CHR$(34)
1901 DATA0,7,1,0
1910 P$(5)="I'M IN A DUSTY COBBLESTONE HALLWAY.":DATA0,25,2,1
1920 P$(6)="I'M IN A DUSTY PLACE. IT IS VERY DARK IN HERE. I'M SCARED!":DATA1,24,,0
1930 P$(7)="I'M IN A LOW CRAWL SPACE. IT SEEMS AS IF WE'RE NOT SUPPOSED TO BE HERE...":DATA4,,15,0
1940 P$(8)="I'M IN A ROOM WITH ROCK WALLS. IMBEDDED IN THE WALLS ARE SCATTERED RUBIES.":DATA9,2,,13
1950 P$(9)="I'M IN A DARK ROOM. THIS ONE IS NOT SO SCARY. I THINK I HEAR GRUNTS AND GROANS NEARBY.":DATA10,8,23,0
1960 P$(10)="I'M ON THE SOUTH SIDE OF A CHASM. MOLTEN LAVA IS BENEATH ME.":DATA11,9,,0
1970 P$(11)="I'M ON THE NORTH SIDE OF THE CHASM.":DATA16,10,,0
1980 P$(12)="I'M IN A MAZE OF TWISTY LITTLE PASSAGES.":DATA12,12,13,14
1990 P$(13)=P$(12):DATA13,13,8,12
2000 P$(14)=P$(12):DATA14,15,14,12
2010 P$(15)=P$(12):DATA14,15,15,7
2020 P$(16)="I'M ON A WOOD PLATFORM. SIGN WARNS: GO BACK! BEWARE! GO SOUTH!!! THERE IS A RICKETY BRIDGE TO THE NORTH."
2021 DATA17,11,,0
2030 P$(17)="I'M IN A DUSTY PASSAGE. A SIGN POINTS WEST AND SAYS: TO TROLLS PLACE --->"
2031 P$(17)=P$(17)+" A RICKETY BRIDGE EXTENDS SOUTHWARD. MARKINGS IN THE DUST INDICATE THAT SOMEONE WAS HERE RECENTLY."
2032 DATA0,16,,18
2040 P$(18)="A VOICE BOOMS OUT: WHO DARES TO ENTER MY PALACE??? (SOUNDS LIKE A TROLL TO ME!)":DATA0,,,19
2050 P$(19)="I'M IN THE TROLL'S PALACE. IT'S BREATHTAKING!!! THE CEILING IS SO HIGH I CAN'T EVEN SEE IT!":DATA0,20,,0
2060 P$(20)="I'M AT ONE END OF A SECRET PASSAGE.":DATA19,21,,0
2070 P$(21)="I'M IN A SECRET PASSAGE.":DATA21,21,20,22
2080 P$(22)=P$(21):DATA21,22,22,23
2090 P$(23)=P$(20):DATA23,9,22,23
2100 P$(24)="I'M IN A HUGE ICE CAVERN. THE CEILING AND WALLS ARE COMPLETELY ICE.":DATA6,26,25,0
2110 P$(25)="I'M IN A ROOM WITH MANY ANIMALS... WAIT! THEY'VE ALL RAN AWAY":DATA5,,,0
2120 P$(26)="I AM IN A ROOM WITH TRACES OF SLIME ON THE FLOORS  AND  WALLS.":DATA24,29,27,28
2130 P$(27)="I'M IN...AL'S DINER??? THERE'S A COKE MACHINE HERE. IT SAYS:"
2131 P$(27)=P$(27)+" ENJOY COCA-COLA. 25 CENTS (NO CANADIAN COINS, QUARTER ONLY)":DATA0,,,26
2140 P$(28)="I'M IN A HUGE PIT.":DATA0,,26,0
2160 P$(29)="I'M IN THE HYDRA ROOM. THE WALLS ARE ALL SLIME. BOY, I'VE NEVER SEEN ANYTHING SO GROSS IN MY LIFE!"
2161 DATA26,30,31,32
2170 P$(30)="I'M IN THE DRAGON ROOM. IT SEEMS TO BE DECORATED WITH DRAGONS.":DATA29,34,,33
2180 P$(31)="I'M IN A TORTURE CHAMBER. LET'S GET OUT OF HERE QUICK!!":DATA31,29,31,31
2190 P$(32)="THIS IS THE CLAM ROOM. THE WALLS ARE SOLID CLAMSHELL EMBEDDED IN LIMESTONE.":DATA0,,29,0
2200 P$(33)="I'M IN A MAMMOTH GARDEN. THIS IS ALL PLANTS.":DATA0,,30,0
2210 P$(34)="I'M IN THE VELVET ROOM. THE WALLS  AND  CEILING ARE VELVET.":DATA30,34,35,0
2220 P$(35)="I'M IN AN ARABIAN ROOM. ARABIAN GUITARS ARE PLAYING NEARBY. THERE IS AN OIL SLICK HERE."
2221 P$(35)=P$(35)+" LOOKS LIKE MACHINE OIL, 10-W-30.":DATA36,37,,34
2230 P$(36)="I'M IN THE TREASURE ROOM. MANY DWARVES GUARD SOME PRETTY SCRAWNY TREASURE. NOTHING TO FLIP OVER, ANYWAY."
2231 DATA0,35,,0
2240 P$(37)="I'M AT MAC'S EARTHDIGGER BODY SHOP.":DATA0,,,35
2250 DIMP(37,4):FORI=1TO37:FORJ=1TO4:READP(I,J):NEXTJ,I
2260 LO=33:DIMOB$(LO),OB(LO,1)
2270 OB$(1)="A SET OF KEYS":DATA6,0
2272 OB$(2)="A SHINY SWORD":DATA10,0
2280 OB$(3)="A PARROT IN A CAGE":DATA25, 5
2282 OB$(4)="A GOLD NUGGET":DATA4,10
2290 OB$(5)="A 35,000 CARAT DIAMOND":DATA7,10
2292 OB$(6)="A CIGARETTE LIGHTER":DATA28,0
2300 OB$(7)="A QUARTER":DATA28,0
2302 OB$(8)="AN ENORMOUS EMERALD":DATA32,15
2310 OB$(9)="SOME SILVER BARS":DATA33,15
2312 OB$(10)="A GIANT HYDRA WITH NINE HEADS":DATA29,0
2320 OB$(11)="A MING VASE":DATA30,10
2322 OB$(12)="A BOTTLE OF COKE":DATA0,0
2330 OB$(13)="AN EMPTY COKE BOTTLE":DATA0,0
2332 OB$(14)="SOME TOOLS":DATA34,0
2340 OB$(15)="A LARGE RUBY":DATA8,15
2342 OB$(16)="GOLD BARS":DATA19,20
2350 OB$(17)="A DIAMOND NECKLACE":DATA19,25
2352 OB$(18)="A MAGIC WAND":DATA14,0
2360 OB$(22)="A FRIBULATING GONKULATOR":DATA37,50
2370 OB$(20)="A GIANT BUG ASLEEP":DATA31,0
2380 OB$(31)="SOME TASTY FOOD":OB$(32)="SOME HUMAN SKELETONS":OB(32,0)=31:OB(32,1)=0
2390 OB$(33)="A DEAD HYDRA ON THE GROUND"
2400 FORI=1TO20:READOB(I,0),OB(I,1):NEXTI
2410 OB(22,0)=OB(19,0):OB(22,1)=OB(19,1)
2420 OB(19,1)=0:OB(19,0)=0:OB(31,0)=27:OB(31,1)=0
2440 DATAKEYS,SWORD,PARROT,NUGGET,DIAMOND,LIGHTER,QUARTER,EMERALD,SILVER,HYDRA,VASE,COKE,BOTTLE
2441 DATATOOLS,RUBY,GOLD,NECKLACE
2442 DATAWAND,SIGN,BUG,ALEXANDER,GONKULATOR,NORTH,SOUTH,EAST,WEST
2450 DATAN,S,E,W,FOOD,SKELETONS
2460 NN=32:DIMNO$(NN):FORI=1TONN:READNO$(I):NEXTI
2480 DATAGO,GET,LOOK,INVEN,SCORE,DROP,HELP,SAVE,LOAD,QUIT,SAY,DRINK,FEED,HIT,KILL,BURN,FIGHT
2481 DATALIGHT,INSERT,TAKE,WAVE,RELEASE,CONTINUE,DESTROY,SIR,FIX,EAT,?,INVENTORY,HOCUS
2490 NV=30:DIMVB$(NV):FORI=1TONV:READVB$(I):NEXTI
2500 M1$="THE PARROT CHANTS: SIR ALEXANDER... SIR ALEXANDER!"
2510 M2$="THE TROLL SCURRIES AWAY IN FEAR."
2520 M3$="GULP GLUK GULP!  (BURP) THAT WAS REFRESHING!"
2530 DIMH$(NP):H$(25)="THIS PARROT TALKS..."
2550 H$(29)="CIGAR? CIGARETTE? TIPPARILLO?":H$(10)="YOU KNOW, THIS WORLD IS ENCHANTED..."
2570 H$(12)="YOU CAN MAKE THE PASSAGES LOOK LESS ALIKE BY DROPPING THINGS."
2580 H$(13)=H$(12):H$(14)=H$(12):H$(15)=H$(12)
2590 H$(1)="I THINK WE'RE SUPPOSED TO LEAVE WHAT WE FIND HERE."
2600 H$(35)="OFTEN TIMES OIL MEANS AN AUTO BODY SHOP IS NEARBY."
2610 H$(31)="THE BUG WILL NOT BOTHER YOU. IT IS ASLEEP. HINT: IF YOU LINGER TOO LONG IN THIS ROOM IT WILL WAKE UP!"
2620 H$(2)="YOU KNOW, IN THIS ENCHANTED WORLD, MANY THINGS THAT ARE SMALL ON EARTH ARE VERY LARGE..."
2630 H$(20)="TRY TO FIND YOUR WAY TO THE OTHER END, DUMMY!!":H$(23)=H$(20):PRINT@154,"hit";:PRINT@186,"key"
2640 C1=RND(0):ON-(INKEY$="")GOTO2640:CLS:INPUT"WHAT IS YOUR NAME";A$:NA$=A$:CLS:RETURN
9000 CLS:PRINT"    Š                      …":PRINT"    Š JOURNEY    ¿¿¿¿¿¿    …":PRINT"    Š TO THE   ¿¿ÏÏÏÏ¿¿¿¿  …"
9004 PRINT"    Š CENTER   ¿¿ÏÏ€€ÿÿÿÿ  …":PRINT"    Š OF THE   ¿¿ÏÏÏÏÿÿÿÿ  …":PRINT"    Š EARTH  ¿¿¿¿¿¿ÏÏ€€ÿÿ  …"
9007 PRINT"    Š        ¿¿¿¿¿¿¿¿      …":PRINT"    Š      ¿¿ÿÿÿÿ¿¿¿¿¿¿    …":PRINT"    Š      ÿÿÿÿÿÿÿÿ¿¿¿¿    …"
9010 PRINT"    Š    ÿÿÿÿÿÿÿÿ¿¿¿¿¿¿    …":PRINT"    Š    ¿¿¿¿¿¿¿¿¿¿¿¿      …":PRINT"    Š  ¿¿¿¿ÿÿÿÿ¿¿¿¿¿¿      …"
9013 PRINT"    Š  ÿÿÿÿ¿¿¿¿¿¿ßß        …":PRINT"    Š€€¿¿¿¿¿¿€€€€€€ßß€€€€€€…":PRINT"       ¿¿¿¿  AN ADVENTURE"
9016 PRINT"       ¿¿    BY GREG HASSETT";:RETURN
REM MC-10 EDITS JIM GERRIE
