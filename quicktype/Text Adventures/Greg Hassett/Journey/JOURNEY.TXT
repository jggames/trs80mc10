0 CLS:CLEAR2000:DIMC1,CC,M$:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 C1=RND(-(PEEK(9)*256+PEEK(10))):PRINTTAB(4)"JOURNEY TO THE CENTER OF":PRINTTAB(6)"THE EARTH ADVENTURE"
11 PRINTTAB(8)"BY GREG HASSETT":PRINTTAB(3)"MC-10 EDITS BY JIM GERRIE":PRINT
20 CP=1:GOSUB1850
40 GOSUB1750:GOSUB1640
50 IF CP=18 AND (VB<>25 OR NO<>21) THEN M$="HORRORS! THE TROLL THROWS AN AXE AT ME! I AM DEAD.":GOSUB1:END
60 IF CP=18 THEN M$=M2$:GOSUB1:F5=1:GOSUB1640
70 IF NO<23 OR VB>1 OR NO>30 THEN 160
80 IF NO<=26 THEN FD=FD+1:GOTO90
81 FD=-10000
90 IF NO>26 THEN NO=NO-4
100 NO=NO-22
110 IF CP=10 AND NO=1 THEN M$="AND FALL INTO THE LAVA??? FAT CHANCE!":GOSUB1:GOSUB1640:GOTO70
120 IF CP=11 AND NO=2 THEN M$="DO YOU KNOW WHAT MOLTEN LAVA IS??? IT'S HOT, THAT'S WHAT!":GOSUB1:GOSUB1640:GOTO70
130 IF CP=29 AND OB(10,0)=29 AND (NO=4 OR NO=2) THEN M$="THE HYDRA WON'T LET ME THROUGH!":GOSUB1:GOSUB1640:GOTO70
140 IF P(CP,NO)=0 THEN PRINT"I CAN'T GO THAT WAY!":GOSUB1640:GOTO70
150 CP=P(CP,NO):GOTO40
160 IF VB<>2 AND VB<>20 THEN 240
170 IF NO=10 AND OB(33,0)=CP THEN M$="IT IS SORT OF HEAVY, YOU KNOW.":GOSUB1:GOSUB1640:GOTO70
180 IF NO=0 THEN M$="I DON'T KNOW WHAT A "+CHR$(34)+NO$+CHR$(34)+" IS.":GOSUB1:GOSUB1640:GOTO70
190 IF OB(NO,0)<>CP THEN PRINT"I DON'T SEE IT HERE.":GOSUB1640:GOTO70
200 IF NO=10 OR NO=20 THEN PRINT"OH GIVE ME A BREAK!!":GOSUB1640:GOTO70
210 IF OB(NO,0)=-1 THEN PRINT"I'M ALREADY CARRYING IT.":GOSUB1640:GOTO70
220 IF OB(NO,0)=CP AND NT>=6 THEN M$="I CAN'T CARRY ANY MORE. YOU'LL HAVE TO DROP SOMETHING.":GOSUB1:GOSUB1640:GOTO70
230 IF OB(NO,0)=CP THEN PRINT"CHECK.":NT=NT+1:OB(NO,0)=-1:GOSUB1640:GOTO70
240 IF VB<>4 AND VB<>29 THEN 290
250 F3=0
260 PRINT"I AM CURRENTLY CARRYING:":FOR I=1 TO LO:IF OB(I,0)=-1 THEN M$=OB$(I):GOSUB1:F3=1
270 NEXT I:IF F3=0 THEN PRINT"NOTHING."
280 GOSUB1640:GOTO70
290 IF VB=3 THEN 40
300 IF NO=0 AND VB=0 THEN M$="THAT DOESN'T MAKE ANY SENSE TO ME.":GOSUB1:GOSUB1640:GOTO70
310 IF VB<>5 THEN 370
320 J=0
330 FOR I=1 TO LO:IF OB(I,0)=1 THEN J=J+OB(I,1)
340 NEXT I:M$="OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE"+STR$(J):GOSUB1
350 IF J=0 THEN PRINT"BOY, YOU STINK!"
360 GOSUB1640:GOTO70
370 IF VB<>6 AND VB<>22 THEN 440
380 IF NO$<>"EVERYTHING" AND NO$<>"ALL" THEN 420
390 FOR I=1 TO LO:IF OB(I,0)=-1 THEN OB(I,0)=CP
400 NEXT I:NT=0
410 PRINT"OKAY.":GOSUB1640:GOTO70
420 IF OB(NO,0)<>-1 THEN M$="HOW CAN I DROP WHAT I'M NOT HOLDING???":GOSUB1:GOSUB1640:GOTO70
430 OB(NO,0)=CP:PRINT"O.K.":NT=NT-1:GOSUB1640:GOTO70
440 IF VB<>7 AND VB<>28 THEN 480
450 IF H$(CP)="" THEN M$="YOU GOT ME IN TO THIS MESS, NOW YOU GET ME OUT!":GOSUB1
460 M$=H$(CP):GOSUB1:H$(CP)=""
470 GOSUB1640:GOTO70
480 IF VB<>8 THEN 550
490 CLS:PRINT"SAVING...":CSAVE*OB,"JOURNSAV"
540 GOTO40
550 IF VB<>9 THEN 610
560 CLS:PRINT"LOADING..."
580 CLOAD*OB,"JOURNSAV"
600 GOTO40
610 IF VB<>10 THEN 630
620 GOTO1410
630 IF VB<>11 THEN 660
640 IF NO=0 THEN PRINT"SAY WHAT?":GOSUB1640:GOTO70
650 M$="OKAY, IF IT IS REALLY NECESSARY..."+NO$:GOSUB1:GOSUB1640:GOTO70
660 IF VB<>12 THEN 700
670 IF NO<>12 THEN PRINT"I DON'T DO STUPID THINGS.":GOSUB1640:GOTO70
680 IF OB(12,0)<>-1 THEN M$="I DON'T HAVE ANY COKE TO DRINK!":GOSUB1:GOSUB1640:GOTO70
690 PRINT M3$:OB(12,0)=0:OB(13,0)=-1:GOSUB1640:GOTO70
700 IF VB<>13 THEN 770
710 IF OB(31,0)<>-1 THEN PRINT"BUT I HAVE NO FOOD!!":GOSUB1640:GOTO70
720 IF OB(NO,0)<>CP THEN PRINT"I DON'T SEE IT.":GOSUB1640:GOTO70
730 IF NO=10 THEN735
731 GOTO740
735 M$="THE HYDRA WHARFS DOWN THE FOOD.":GOSUB1:OB(31,0)=0:OB$(10)="A GIANT HYDRA RUBBING HIS STOMACH":GOSUB1640:GOTO70
740 IF NO=20 THEN M$="THE BUGS IN THESE CAVES ONLY EAT HUMAN FLESH.":GOSUB1:GOSUB1640:GOTO70
750 IF NO<>3 THEN PRINT"YOU CAN'T BE SERIOUS.":GOSUB1640:GOTO70
760 M$="IT REFUSES TO EAT THE FOOD.":GOSUB1:GOSUB1640:GOTO70
770 IF VB<>14 THEN 850
780 IF NO=10 AND OB(33,0)=CP THEN M$="THE UNDERTAKER JUST TOOK HIM AWAY.":GOSUB1:OB(33,0)=0:GOSUB1640:GOTO70
790 IF OB(NO,0)<>CP AND OB(NO,0)<>-1 THEN795
791 GOTO800
795 M$="I DON'T SEE IT AND I'M NOT HOLDING IT, SO HOW CAN I HIT IT?":GOSUB1:GOSUB1640:GOTO70
800 IF NO=3 THEN M$="NO!!! I KIND OF LIKE THE PARROT! POLLY WANT CWAKER?":GOSUB1
810 IF NO=10 THEN M$="OKAY... THE HYDRA GRUNTS >>DARN MOSQUITOES<<":GOSUB1
820 IF NO=20 THEN M$="YUCH! I DON'T WANT TO TOUCH IT!!!":GOSUB1
830 IF NO=3 OR NO=10 OR NO=20 THEN GOSUB1640:GOTO70
840 M$="OKAY... OUCH! I HURT MY HAND!":GOSUB1:GOSUB1640:GOTO70
850 IFVB=15THENM$="I CAN'T KILL ANYTHING. I'M TOO WEAK. BUT MAYBE IF YOU WERE A BIT MORE SPECIFIC...":GOSUB1:GOSUB1640:GOTO70
860 IF VB<>16 THEN 950
870 IF OB(33,0)=CP AND NO=10 THEN M$="IT IS ALREADY BURNED TO A CRISP.":GOSUB1:GOSUB1640:GOTO70
880 IF OB(6,0)<>-1 THEN M$="I DON'T HAVE A LIGHTER OR MATCHES!!":GOSUB1:GOSUB1640:GOTO70
890 IF NO=3 THEN M$="NEVER!! YOU KNOW SOMETHING, YOU'RE A SADIST.":GOSUB1:GOSUB1640:GOTO70
900 IFNO=10ANDOB(10,0)=CPTHEN OB(10,0)=0:OB(33,0)=CP:M$="THE HYDRA CRUMBLES TO THE GROUND DEAD.":GOSUB1:GOSUB1640:GOTO70
910 IF NO=20 THEN M$="THE BUG CANNOT BE BURNED. <GULP>":GOSUB1:GOSUB1640:GOTO70
920 IF NO=10OB(10,0)=0
930 PRINT"IT DOESN'T BURN."
940 GOSUB1640:GOTO70
950 IF VB=17 THEN M$=" AND JUST WHO DO YOU THINK I AM, ROCKY???":GOSUB1:GOSUB1640:GOTO70
960 IFVB=10THENM$="I DON'T KNOW HOW TO LIGHT THINGS. IF YOU WANT ME TO BURN SOMETHING, TELL ME SO!":GOSUB1:GOSUB1640:GOTO70
970 IF VB<>19 THEN 1060
980 IF NO$="COIN" THEN NO$="QUARTER":NO=7
990 IF NO=0 THEN M$="I DON'T KNOW WHAT ONE IS.":GOSUB1:GOSUB1640:GOTO70
1000 IF NO=7 THEN 1020
1010 IF OB(NO,0)<>-1 THEN M$="I'M NOT HOLDING IT RIGHT NOW.":GOSUB1:GOSUB1640:GOTO70
1020 IF CP<>27 THEN M$="I DON'T SEE ANY COKE MACHINES HERE.":GOSUB1:GOSUB1640:GOTO70
1030 IF NO<>7 THEN PRINT"THAT IS RIDICULOUS.":GOSUB1640:GOTO70
1040 IF OB(NO,0)<>-1 THEN PRINT"I HAVE NO MONEY.":GOSUB1640:GOTO70
1050 IFOB(7,0)=-1ANDCP=27THEN1055
1051 GOTO1060
1055 M$="ALL RIGHT. CHIKCLIKCHINGGGG! A COKE POPPED OUT. THERE IS NOW A BOTTLE OF COKE HERE.":GOSUB1
1056 OB(7,0)=0:NT=NT-1:OB(12,0)=27:GOSUB1640:GOTO70
1060 IF VB<>21 THEN 1150
1080 IF NO<>18 AND OB(NO,0)=-1 THEN M$="IT'S KIND OF HEAVY, YOU KNOW.":GOSUB1:GOSUB1640:GOTO70
1090 IF OB(NO,0)<>-1 THEN M$="FIRST YOU HAVE TO HELP ME FIND IT, DUMMY!":GOSUB1:GOSUB1640:GOTO70
1100 IF CP=10 OR CP=11 THEN 1120
1110 PRINT"NOTHING HAPPENED":GOSUB1640:GOTO70
1120 IF CP=10 THEN CP=11:GOTO1140
1130 CP=10
1140 GOTO40
1150 IF VB=23 THEN VB=1:GOTO70
1160 IF VB=24 THEN M$="TRY KILL "+NO$:GOSUB1:GOSUB1640:GOTO70
1170 IF VB<>25 THEN 1200
1180 IF NOC21 THEN PRINT"NOTHING HAPPENED":GOSUB1640:GOTO70
1190 M$="SIR WHAT??? I DON'T UNDERSTAND!":GOSUB1:GOSUB1640:GOTO70
1200 IF VB<>26 THEN 1270
1210 IF CP=1 AND OB(14,0)=-1 AND OB(22,0)=1 THEN PRINT"OKAY!!!":GOTO1400
1220 IF CP<>1 THEN PRINT"I CAN'T.":GOSUB1640:GOTO70
1230 IF VB<>26 THEN 1270
1240 IF OB(14,0)<>-1 THEN PRINT"I CAN'T. I HAVE NO TOOLS.":GOSUB1640:GOTO70
1250 M$="I CAN'T, I DON'T SEE THE FRIBULATING GONKULATOR AROUND ME."
1251 IFOB(22,0)=-1THENM$=M$+" I THINK I HAVE TO DROP IT FIRST."
1252 GOSUB1 
1260 GOSUB1640
1270 IF VB<>27 THEN 1350
1280 IF NO=0 THEN M$="WHAT THE DEVIL IS A "+NO$+"??"GOSUB1:GOSUB1640:GOTO70
1290 IF NO=20 OR NO=10 THEN IF OB(NO,0)=CP THEN M$="YUCH!!! I'D NEVER EAT A "+NO$+"!!!":GOSUB1:GOSUB1640:GOTO70
1300 IF NO=10 AND OB(33,0)=CP THEN M$="WELL...IT'S COOKED NOW, BUT IT STILL LOOKS KIND OF GROSS.":GOSUB1:GOSUB1640:GOTO70
1310 IF OB(NO,0)<>-1 THEN PRINT"I'M NOT HOLDING IT.":GOSUB1640:GOTO70
1320 IF NO=3 AND OB(3,0)=-1 THEN M$="I'D RATHER NOT. I DON'T CARE FOR PARROT MEAT.":GOSUB1:GOSUB1640:GOTO70
1330 IFNO=31ANDOB(31,0)=-1THENNT=NT-1:M$="HUMM! IT WAS FILET MIGNON, MY FAVORITE DISH.":GOSUB1:OB(31,0)=0:GOSUB1640:GOTO70
1340 M$="I THINK THAT "+OB$(NO)+" WOULD GIVE ME INDIGESTION, AND I DON'T HAVE ANY PEPTO-BISMOL.":GOSUB1:GOSUB1640:GOTO70
1350 IF VB<>30 THEN 1390
1360 IF CP<>10 AND CP<>11 THEN PRINT"NOTHING HAPPENED.":GOSUB1640:GOTO70
1370 M$="NICE TRY, BUT THAT'S AN OLD, WORN OUT MAGIC WORD.":GOSUB1
1380 GOSUB1640:GOTO70
1390 M$="I DON'T KNOW HOW TO DO THAT.":GOSUB1:GOSUB1640:GOTO70
1400 FOR I=1 TO 1500:NEXT I
1410 M$="OUT OF A MAXIMUM OF 175 POINTS, YOU HAVE SCORED":J=0
1420 FOR I=1 TO LO:IF OB(I,0)=1 THEN J=J+OB(I,1)
1430 NEXT I
1440 M$=M$+STR$(J)+" POINTS.":GOSUB1
1450 IF J=175 THEN M$="ALL OF ADVENTURERDOM GIVES TRIBUTE TO YOU, ADVENTURER GRAND MASTER!":GOSUB1:END
1460 IF J>165 THEN M$="YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS A.":GOSUB1:END
1470 IF J>150THEN M$="YOUR SCORE PUTS YOU IN MASTER ADVENTURER CLASS B.":GOSUB1:END
1480 IF J>140 THEN M$="YOU HAVE REACHED A "+CHR$(34)+"JUNIOR  MASTER"+CHR$(34)+" STATUS":GOSUB1:END
1490 IF J>120 THEN M$="YOU MAY NOW CONSIDER YOURSELF A "+CHR$(34)+"SEASONED ADVENTURER"+CHR$(34):GOSUB1:END
1500 IF J>110 THEN M$="YOU HAVE ACHIEVED THE RATING: "+CHR$(34)+"EXPERIENCED ADVENTURER"+CHR$(34)+".":GOSUB1:END
1510 IF J>100 THEN M$="YOUR SCORE QUALIFIES YOU AS A NOVICE CLASS ADVENTURER.":GOSUB1:END
1520 M$="YOU ARE OBVIOUSLY A RANK AMATEUR. BETTER LUCK NEXT TIME.":GOSUB1:END
1530 M$="GADZOOKS!!! A GIANT BUG JUST WALKED IN TO KILL ME!!!":GOSUB1
1540 FF=1
1550 GOSUB1640
1560 IF NO$="SESAME" AND VB$="OPEN" THEN M$="THE BUG HAS SHRUNK TO IT'S PROPER SIZE AND SCURRIED AWAY.":GOSUB1:GOTO1620
1570 IF NO<>20 OR VB<>15 THEN1575
1571 GOTO1580
1575 M$="HELP ME!!! I'M BEING DEVOURED BY A GAINT BUG!!! THE END IS NEAR! OW!! THIS HURTS!! I'M DYING...DYING...DEAD."
1576 GOSUB1:END
1580 IF OB(2,0)<>-1 THEN NO=2:M$="I DON'T HAVE A SWORD!!!":GOSUB1:GOTO1570
1590 IF SW=8 THEN M$="MY SWORD IS DULL, REMEMBER???":GOSUB1:NO=2:GOTO1570
1600 M$="DING! DONG! THE BUG IS DEAD, THE BUG IS DEAD!!!":GOSUB1
1610 SW=SW+1:IF SW=8 THEN M$="MY SWORD IS DULL NOW.":GOSUB1
1620 GOSUB1640:GOTO70
1630 END
1640 PRINT"ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ";:INPUT"WHAT SHOULD I DO";CM$
1645 CLS
1650 IF CM$="ABRA CADABRA" AND F5<3 AND FF=1 THEN NO$="SESAME":VB$="OPEN":FF=0:F5=F5+1:RETURN
1660 VB$="":NO$="":VB=0:NO=0
1670 LC=LEN(CM$):FOR I=1 TO LC:IF MID$(CM$,I,1)<>" " THEN VB$=VB$+MID$(CM$,I,1):NEXT I
1680 FOR I=1 TO NV:IF VB$(I)=VB$ THEN VB=I:TM=I:I=NV:NEXTI:I=TM:GOTO1700
1681 NEXT I
1690 VB=0:NO$=VB$:GOTO1720
1700 IF LEN(VB$)+1>=LEN(CM$) THEN NO=0:RETURN
1710 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(VB$))
1720 FOR I=1 TO NN:IF NO$(I)=NO$ THEN TM=I:I=NN:NEXTI:I=TM:GOTO1740
1721 NEXT I
1730 NO=0:RETURN
1740 NO=I:RETURN
1750 M$=P$(CP):GOSUB1:PRINT 
1760 FOR I=1 TO LO:IF OB(I,0)=CP THEN M$="THERE IS "+OB$(I)+" HERE.":GOSUB1
1770 NEXT I
1780 A$="I CAN GO: ":F2=0
1790 FORI=1TO4:IFP(CP,I)<>0THENA$=A$+NO$(I+22)+", ":F2=1
1800 NEXT I:IF F2=0 THENA$=A$+"NOWHERE!, "
1801 M$=LEFT$(A$,LEN(A$)-2):GOSUB1
1810 IF CP=18 AND OB(3,0)=-1 THEN PRINT:PRINT M1$
1820 IF INT(RND(1)*35)+1=5 AND CP<>1 AND CP<>37 AND CP<>27 THEN PRINT:GOTO1530
1830 IF FD>4 THEN1835
1831 RETURN
1835 M$="IF YOU'D LIKE, YOU CAN MAKE IT EASIER ON YOURSELF BY TYPING N, S, E, OR W INSTEAD OF NORTH, SOUTH, ECT.":GOSUB1
1836 FD=0:RETURN
1850 NP=37:DIM P$(NP)
1870 P$(1)="I AM IN A SHIP. ON A COMPUTER SCREEN IN HERE IT SAYS: SHIP WILL NOT FUNCTION--"
1871 P$(1)=P$(1)+" FRIBULATING GONKULATOR IS BURNED OUT. IT IS OBVIOUS THAT THIS SHIP HAS CRASHED."
1880 DATA 2,6,5,4
1881 P$(2)="I'M IN A COBBLESTONE HALLWAY. I CAN HEAR CHIRPING NEARBY. SOUNDS LIKE BUGS..."
1882 DATA 8,1,3,5
1890 P$(3)="I'M IN A DARK ROOM...OH NO!!! THERE ARE GIANT BUGS ALL AROUND ME!!!"
1891 DATA 0,0,0,2
1900 P$(4)="I'M IN A SMALL ROOM. A SCRIBBLING HERE SAYS: HAR HAR HAR! YOU'LL NEVER MAKE IT! >> ARBADAC ARBA <<"
1901 DATA 0,7,1,0
1910 P$(5)="I'M IN A DUSTY COBBLESTONE HALLWAY."
1911 DATA 0,25,2,1
1920 P$(6)="I'M IN A DUSTY PLACE. IT IS VERY DARK IN HERE. I'M SCARED!"
1921 DATA 1,24,0,0
1930 P$(7)="I'M IN A LOW CRAWL SPACE. IT SEEMS AS IF WE'RE NOT SUPPOSED TO BE HERE..."
1931 DATA 4,0,15,0
1940 P$(8)="I'M IN A ROOM WITH ROCK WALLS. IMBEDDED IN THE WALLS ARE SCATTERED RUBIES."
1941 DATA 9,2,0,13
1950 P$(9)="I'M IN A DARK ROOM. THIS ONE IS NOT SO SCARY. I THINK I HEAR GRUNTS AND GROANS NEARBY."
1951 DATA 10,8,23,0
1960 P$(10)="I'M ON THE SOUTH SIDE OF A CHASM. MOLTEN LAVA IS BENEATH ME."
1961 DATA 11,9,0,0
1970 P$(11)="I'M ON THE NORTH SIDE OF THE CHASM."
1971 DATA 16,10,0,0
1980 P$(12)="I'M IN A MAZE OF TWISTY LITTLE PASSAGES."
1981 DATA 12,12,13,14
1990 P$(13)=P$(12)
1991 DATA 13,13,8,12
2000 P$(14)=P$(12)
2001 DATA 14,15,14,12
2010 P$(15)=P$(12)
2011 DATA 14,15,15,7
2020 P$(16)="I'M ON A WOOD PLATFORM. SIGN WARNS: GO BACK! BEWARE! GO SOUTH!!! THERE IS A RICKETY BRIDGE TO THE NORTH."
2021 DATA 17,11,0,0
2030 P$(17)="I'M IN A DUSTY PASSAGE. A SIGN POINTS WEST AND SAYS: TO TROLLS PLACE --->"
2031 P$(17)=P$(17)+" A RICKETY BRIDGE EXTENDS SOUTHWARD. MARKINGS IN THE DUST INDICATE THAT SOMEONE WAS HERE RECENTLY."
2032 DATA 0,16,0,18
2040 P$(18)="A VOICE BOOMS OUT: WHO DARES TO ENTER MY PALACE??? (SOUNDS LIKE A TROLL TO ME!)"
2041 DATA 0,0,0,19
2050 P$(19)="I'M IN THE TROLL'S PALACE. IT'S BREATHTAKING!!! THE CEILING IS SO HIGH I CAN'T EVEN SEE IT!"
2051 DATA 0,20,0,0
2060 P$(20)="I'M AT ONE END OF A SECRET PASSAGE."
2061 DATA 19,21,0,0
2070 P$(21)="I'M IN A SECRET PASSAGE."
2071 DATA 21,21,20,22
2080 P$(22)=P$(21)
2081 DATA 21,22,22,23
2090 P$(23)=P$(20)
2091 DATA 23,9,22,23
2100 P$(24)="I'M IN A HUGE ICE CAVERN. THE CEILING AND WALLS ARE COMPLETELY ICE."
2101 DATA 6,26,25,0
2110 P$(25)="I'M IN A ROOM WITH MANY ANIMALS... WAIT! THEY'VE ALL RAN AWAY"
2111 DATA 5,0,0,0
2120 P$(26)="I AM IN A ROOM WITH TRACES OF SLIME ON THE FLOORS  AND  WALLS."
2121 DATA 24,29,27,28
2130 P$(27)="I'M IN...AL'S DINER??? THERE'S A COKE MACHINE HERE. IT SAYS:"
2131 P$(27)=P$(27)+" ENJOY COCA-COLA. 25 CENTS (NO CANADIAN COINS, QUARTER ONLY)"
2132 DATA 0,0,0,26
2140 P$(28)="I'M IN A HUGE PIT."
2150 DATA 0,0,26,0
2160 P$(29)="I'M IN THE HYDRA ROOM. THE WALLS ARE ALL SLIME. BOY, I'VE NEVER SEEN ANYTHING SO GROSS IN MY LIFE!"
2161 DATA 26,30,31,32
2170 P$(30)="I'M IN THE DRAGON ROOM. IT SEEMS TO BE DECORATED WITH DRAGONS."
2171 DATA 29,34,0,33
2180 P$(31)="I'M IN A TORTURE CHAMBER. LET'S GET OUT OF HERE QUICK!!"
2181 DATA 31,29,31,31
2190 P$(32)="THIS IS THE CLAM ROOM. THE WALLS ARE SOLID CLAMSHELL EMBEDDED IN LIMESTONE."
2191 DATA 0,0,29,0
2200 P$(33)="I'M IN A MAMMOTH GARDEN. THIS IS ALL PLANTS."
2201 DATA 0,0,30,0
2210 P$(34)="I'M IN THE VELVET ROOM. THE WALLS  AND  CEILING ARE VELVET."
2211 DATA 30,34,35,0
2220 P$(35)="I'M IN AN ARABIAN ROOM. ARABIAN GUITARS ARE PLAYING NEARBY. THERE IS AN OIL SLICK HERE."
2221 P$(35)=P$(35)+" LOOKS LIKE MACHINE OIL, 10-W-30."
2222 DATA 36,37,0,34
2230 P$(36)="I'M IN THE TREASURE ROOM. MANY DWARVES GUARD SOME PRETTY SCRAWNY TREASURE. NOTHING TO FLIP OVER, ANYWAY."
2231 DATA 0,35,0,0
2240 P$(37)="I'M AT MAC'S EARTHDIGGER BODY SHOP."
2241 DATA 0,0,0,35
2250 DIM P(37,4):FOR I=1 TO 37:FOR J=1 TO 4:READ P(I,J):NEXT J,I
2260 LO=33:DIM OB$(LO),OB(LO,1)
2270 OB$(1)="A SET OF KEYS"
2271 DATA 6,0
2272 OB$(2)="A SHINY SWORD"
2273 DATA 10,0
2280 OB$(3)="A PARROT IN A CAGE"
2281 DATA 25, 5
2282 OB$(4)="A GOLD NUGGET"
2283 DATA 4,10
2290 OB$(5)="A 35,000 CARAT DIAMOND"
2291 DATA 7,10
2292 OB$(6)="A CIGARETTE LIGHTER"
2293 DATA 28,0
2300 OB$(7)="A QUARTER"
2301 DATA 28,0
2302 OB$(8)="AN ENORMOUS EMERALD"
2303 DATA 32,15
2310 OB$(9)="SOME SILVER BARS"
2311 DATA 33,15
2312 OB$(10)="A GIANT HYDRA WITH NINE HEADS"
2313 DATA 29,0
2320 OB$(11)="A MING VASE"
2321 DATA 30,10
2322 OB$(12)="A BOTTLE OF COKE"
2323 DATA 0,0
2330 OB$(13)="AN EMPTY COKE BOTTLE"
2331 DATA 0,0
2332 OB$(14)="SOME TOOLS"
2333 DATA 34,0
2340 OB$(15)="A LARGE RUBY"
2341 DATA 8,15
2342 OB$(16)="GOLD BARS"
2343 DATA 19,20
2350 OB$(17)="A DIAMOND NECKLACE"
2351 DATA 19,25
2352 OB$(18)="A MAGIC WAND"
2353 DATA 14,0
2360 OB$(22)="A FRIBULATING GONKULATOR"
2361 DATA 37,50
2370 OB$(20)="A GIANT BUG ASLEEP"
2371 DATA 31,0
2380 OB$(31)="SOME TASTY FOOD":OB$(32)="SOME HUMAN SKELETONS":OB(32,0)=31:OB(32,1)=0
2390 OB$(33)="A DEAD HYDRA ON THE GROUND"
2400 FOR I=1 TO 20:READ OB(I,0),OB(I,1):NEXT I
2410 OB(22,0)=OB(19,0):OB(22,1)=OB(19,1)
2420 OB(19,1)=0:OB(19,0)=0
2430 OB(31,0)=27:OB(31,1)=0
2440 DATA KEYS,SWORD,PARROT,NUGGET,DIAMOND,LIGHTER,QUARTER,EMERALD,SILVER,HYDRA,VASE,COKE,BOTTLE
2441 DATA TOOLS,RUBY,GOLD,NECKLACE
2442 DATA WAND,SIGN,BUG,ALEXANDER,GONKULATOR,NORTH,SOUTH,EAST,WEST
2450 DATA N,S,E,W,FOOD,SKELETONS
2460 NN=32:DIM NO$(NN)
2470 FOR I=1 TO NN:READ NO$(I):NEXT I
2480 DATA GO,GET,LOOK,INVEN,SCORE,DROP,HELP,SAVE,LOAD,QUIT,SAY,DRINK,FEED,HIT,KILL,BURN,FIGHT
2481 DATA LIGHT,INSERT,TAKE,WAVE,RELEASE,CONTINUE,DESTROY,SIR,FIX,EAT,?,INVENTORY,HOCUS
2490 NV=30:DIM VB$(NV):FOR I=1 TO NV:READ VB$(I):NEXT I
2500 M1$="THE PARROT CHANTS: SIR ALEXANDER...SIR ALEXANDER!"
2510 M2$="THE TROLL SCURRIES AWAY IN FEAR."
2520 M3$="GULP GLUK GULP!  (BURP) THAT WAS REFRESHING!"
2530 DIM H$(NP)
2540 H$(25)="THIS PARROT TALKS..."
2550 H$(29)="CIGAR? CIGARETTE? TIPPARILLO?"
2560 H$(10)="YOU KNOW, THIS WORLD IS ENCHANTED..."
2570 H$(12)="YOU CAN MAKE THE PASSAGES LOOK LESS ALIKE BY DROPPING THINGS."
2580 H$(13)=H$(12):H$(14)=H$(12):H$(15)=H$(12)
2590 H$(1)="I THINK WE'RE SUPPOSED TO LEAVE WHAT WE FIND HERE."
2600 H$(35)="OFTEN TIMES OIL MEANS AN AUTO BODY SHOP IS NEARBY."
2610 H$(31)="THE BUG WILL NOT BOTHER YOU. IT IS ASLEEP. HINT: IF YOU LINGER TOO LONG IN THIS ROOM IT WILL WAKE UP!"
2620 H$(2)="YOU KNOW, IN THIS ENCHANTED WORLD, MANY THINGS THAT ARE SMALL ON EARTH ARE VERY LARGE..."
2630 H$(20)="TRY TO FIND YOUR WAY TO THE OTHER END, DUMMY!!":H$(23)=H$(20)
2640 INPUT "WHAT IS YOUR NAME";A$:NA$=A$:CLS
2650 RETURN
