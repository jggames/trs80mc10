0 CLS3:CLEAR800:DIMCC,C1,M$,I$:GOTO10
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINTTAB((32-LEN(M$))/2)M$:RETURN
4 I$="":PRINT"Ø";
5 M$=INKEY$:ON-(M$="")GOTO5:CC=LEN(I$):I$=I$+M$:C1=LEN(I$)
6 PRINTCHR$(8);:M$=RIGHT$(I$,1):IFM$=CHR$(8)THENIFC1>1THENPRINTCHR$(8)"Ø";:I$=LEFT$(I$,CC-1):GOTO5
7 IFM$=CHR$(13)THENAR$=LEFT$(I$,CC):PRINT:RETURN
8 IFM$>CHR$(31)ANDM$<"Ä"THENPRINTM$"Ø";:GOTO5
9 I$=LEFT$(I$,CC):PRINT"Ø";:GOTO5
10 GOSUB3000
20 M0$="I DON'T SEE IT HERE.":M1$="I'M NOT HOLDING IT.":M2$=" WON'T LET ME.":M3$="DON'T BE RIDICULOUS."
21 M5$="DON'T BE FUNNY.":M6$="OK.":M7$="IT'S NOT HUNGRY.":NP=51:LO=44:NN=44:NV=30
30 DIMP$(51),P(51,3),O(51,1),OB$(44),NO$(44),VB$(30),D$(4):CP=1:ZZ=0:RESTORE:GOSUB1890
40 CLS:PRINT@4*32,;:M$="VOYAGE TO ATLANTIS":GOSUB3:PRINT:M$="BY":GOSUB3:PRINT
50 M$="GREG HASSETT":GOSUB3:PRINT:M$="VERSION 6.5 (1980)":GOSUB3:PRINT
60 PRINT@448,"WOULD YOU LIKE INSTRUCTIONS? ";:GOSUB4:IFLEFT$(AR$,1)="Y"THENGOSUB2230:GOTO80
70 IFLEFT$(AR$,1)<>"N"ANDAR$<>""THEN60
80 GOTO270
90 GOSUB150
100 IFNO<>0THENIFNO<9THENIFVB=0ORVB=1ORVB=27THENVB=1
110 IFVB<>15ANDVB<>16ANDVB<>5ANDVB<>6ANDLEN(N1$)>0ANDNO=0THENM$="WHAT DOES '"+N1$+"' MEAN? ":GOSUB1:GOTO90
120 IFVB=22THEN140
130 IFN1$=""AND(VB>8ORVB=2)ANDVB<>15ANDVB<>30THENM$="PLEASE SUPPLY A DIRECT OBJECT.":GOSUB1:GOTO90
140 GOTO470
150 T=T+1:PRINT"WHAT SHOULD I DO? ";:GOSUB4:CM$=AR$
160 IFLEN(CM$)=0THEN150
170 VB$="":NO$="":VB=0:NO=0
180 LC=LEN(CM$):FORI=1TOLC:A$=MID$(CM$,I,1):IFA$<>" "THENVB$=VB$+A$:NEXTI
190 IFLEN(VB$)+1>=LEN(CM$)THENNO=0:GOTO210
200 NO$=RIGHT$(CM$,LC-I)
210 N1$=NO$:V1$=VB$:VB$=LEFT$(VB$,3):NO$=LEFT$(NO$,3):FORI=1TONV:IFVB$=VB$(I)THENVB=I:GOTO240
211 NEXTI:VB=0
220 IFN1$=""THENNO$=VB$
230 VB=0:GOTO250
240 IFVB$=CM$THENNO=0:NO$="":RETURN
250 FORI=1TONN:IFNO$=NO$(I)THENNO=I:RETURN
260 NEXTI:NO=0:NO$="":RETURN
270 CLS:IFLEFT$(P$(CP),1)="*"THENP$=RIGHT$(P$(CP),LEN(P$(CP))-1):GOTO320
280 IFCP=29THENF1=1
281 IFCP=31THENF2=1
290 IFCP=51THENF4=1
291 IFCP=6THENF3=1
300 P$="I'M IN "+P$(CP)
310 IFCP<>1ANDO(31,0)<>-1THENM$="HELP! ... NO AIR! ... I'M DEAD.":GOSUB1:GOTO1660
320 M$=P$+".":GOSUB1:K=0:FORI=1TOLO:IFO(I,0)<>CPTHEN370
330 IFK=0THENM$="VISIBLE ITEMS: "
331 IFLEN(M$)>225THEN370
340 IFK>0THENM$=M$+", "
360 M$=M$+OB$(I):K=K+1
370 NEXTI:IFLEN(M$)>225THENM$=M$+".."
371 IFK<>0THENPRINT:M$=M$+".":GOSUB1
380 IFCP=31THEN460
390 PRINT:K=0:M$="OBVIOUS EXITS: ":FORI=0TO3:IFP(CP,I)=0THEN420
400 IFK>0THENM$=M$+", "
410 M$=M$+D$(I):K=K+1
420 NEXTI:M$=M$+".":GOSUB1:IFZZ<2ORCP<>5THEN460
430 FORI=1TOLO
440 IFO(I,0)=-1ANDI<>31THENO(I,0)=29
450 NEXTI:M$="THE WHIRLPOOL SUCKED ALL MY BELONGINGS AWAY!!!":GOSUB1:ZZ=1
460 PRINT"åååååååååååååååååååååååååååååååå";:GOTO90
470 ONVBGOTO490,560,80,680,730,770,830,850,870,930
471 IFVB>0THENONVB-10GOTO940,1060,1070,1170,1180,1200,2410,1210,1250,1350,1370,2410,1430,1480,1520,560,490,1550,1590,1620
480 M$="I NEVER LEARNED HOW TO '"+V1$+"'!":GOSUB1:GOTO90
490 IFNO=0THENM$="I NEED A DIRECTION.":GOSUB1:GOTO90
500 IFNO>8THENM$="HUH? ":GOSUB1:GOTO90
510 IFNO>4THENNO=NO-4
520 NO=NO-1:IFP(CP,NO)=0THENM$="THERE IS NO WAY TO GO IN THAT DIRECTION.":GOSUB1:GOTO90
530 IFCP=30ANDGF=0THENM$="THE GATE IS LOCKED!":GOSUB1:GOTO90
540 IFCP=25ANDNO=0ANDGF=0THENM$="THERE IS A LOCKED GATE IN MY WAY!":GOSUB1:GOTO90
550 CP=P(CP,NO):GOTO80

560 IFCP<>O(NO,0)THENM$=M0$:GOSUB1:GOTO90
570 IFNO=13ORNO=14ORNO=22THENM$=M5$:GOSUB1:GOTO90
580 IFNO=27ORNO=29THENM$="IT IS TOO HEAVY!":GOSUB1:GOTO90
590 IFNO=16ANDO(16,0)=CPTHENM$="MANTA"+M2$:GOSUB1:GOTO90
600 IFNO=18ANDO(18,0)=CPTHENM$="SQUID"+M2$:GOSUB1:GOTO90
610 IFNO=36ANDO(36,0)=CPTHENM$="OCTOPUS"+M2$:GOSUB1:GOTO90
620 IFO(10,0)=CPTHENM$="EEL"+M2$:GOSUB1:GOTO90
630 IFNO=42ANDO(42,0)=CPTHENM$="MINOTAUR"+M2$:GOSUB1:GOTO90
640 IFNO=32THENM$="IT IS BEYOND MY POWER TO DO THAT.":GOSUB1:GOTO90
650 IFZZ+1>7THENM$="I CAN'T CARRY ANY MORE!":GOSUB1:GOTO90
660 IFO(NO,1)>0ANDCP=1THENTS=TS-1
670 ZZ=ZZ+1:O(NO,0)=-1:M$=M6$:GOSUB1:GOTO90

680 IFZZ=0THENM$="I DON'T HAVE ANYTHING!":GOSUB1:GOTO90
690 K=0:M$="I PRESENTLY HAVE: ":FORI=1TOLO:IFO(I,0)<>-1THEN720
700 IFK>0THENM$=M$+", "
710 M$=M$+OB$(I):K=K+1
720 NEXTI:IFK<>0THENM$=M$+"."
721 GOSUB1:GOTO90
730 CLS:INPUT"HIT <ENTER>";XX$:PRINT"SAVING..."
740 O(45,0)=CP:O(46,0)=GF:O(47,0)=ZZ:O(48,0)=F1:O(49,0)=F2:O(50,0)=F3:O(51,0)=F4:O(51,1)=T
750 CSAVE*O,"ATLANDAT"
760 GOTO80
770 CLS:INPUT"HIT <ENTER>";XX$
780 PRINT"RESTORING..."
790 CLOAD*O,"ATLANDAT"
800 CP=O(45,0):GF=O(46,0):ZZ=O(47,0):F1=O(48,0):F2=O(49,0):F3=O(50,0):F4=O(51,0):T=O(51,1)
801 IFO(45,1)=-1THENNO$(22)="'"
802 IFO(46,1)=-1THENOB$(34)="a platinum spear"
803 IFO(47,1)=-1THENOB$(37)="a gold statuette"
810 GOTO80
830 SC=0:FORI=1TOLO:IFO(I,0)=1THENSC=SC+O(I,1)
840 NEXTI:M$="YOU HAVE"+STR$(SC)+" POINTS OUT OF 170.":GOSUB1
841 M$="YOU CAN GET A 30 POINT BONUS WHEN YOU QUIT, MAKING THE ACTUAL MAXIMUM 200.":GOSUB1:GOTO90
850 M$="DO YOU REALLY WANT TO QUIT NOW?":GOSUB1:GOSUB4:X$=AR$:IFLEFT$(X$,1)="Y"THEN1660
860 M$="I DIDN'T THINK SO.":GOSUB1:GOTO90
870 IFO(NO,0)<>-1THENM$=M1$:GOSUB1:GOTO90
880 IFNO<>34THENM$="I CAN ONLY THROW THE SPEAR!":GOSUB1:GOTO90
890 IFO(9,0)<>-1OR(O(36,0)-CP AND CP-O(42,0))THENM$="THE SPEAR VANISHED IN MIDAIR.":GOSUB1:ZZ=ZZ-1:O(34,0)=0:GOTO90
900 IFCP-4THEN920
910 M$="THE SPEAR PLUNGES INTO THE OCTOPUS' HEART AND BOTH VANISH!":GOSUB1:O(36,0)=0:O(34,0)=51:ZZ=ZZ-1:GOTO90
920 M$="THE SPEAR HITS THE MINOTAUR, AND HE VANISHES!":GOSUB1:O(42,0)=0:M$="THE SPEAR THEN TURNS TO PLATINUM!":GOSUB1
921 OB$(34)="a platinum spear":O(46,1)=-1:O(34,1)=11:O(34,0)=CP:ZZ=ZZ-1:GOTO90
930 M$="YOU'LL HAVE TO BE MORE SPECIFIC!":GOSUB1:GOTO90
940 IFO(NO,0)<>-1ANDO(NO,0)<>CP THENM$=M0$:GOSUB1:GOTO90
950 IFNO<>43ANDNO<>11ANDNO<>12ANDNO<>17ANDNO<>21ANDNO<>28ANDNO<>32ANDNO<>38THENM$="THERE IS NO WRITING THERE!":GOSUB1:GOTO90
960 M$="IT SAYS:":GOSUB1
970 IFNO=11THENM$="WHAT IS THOUGHT TO BE, ISN'T WHAT YOU SEE! NORI":GOSUB1
980 IFNO=12THENM$="CAPTAIN AHAB WAS HERE.":GOSUB1
990 IFNO=17THENM$=R1$:GOSUB1:M$=R2$:GOSUB1:M$="SCRAPE IRON. NORI":GOSUB1
1000 IFNO=21THENM$="LEE SAID POW AND DIED.":GOSUB1
1010 IFNO=28THENM$="USE CRUST TOOTHPASTE FOR SHARPER TEETH!":GOSUB1:M$="SQUIDS HATE LOUD NOISES (W O P T O N).":GOSUB1
1020 IFNO=32THENM$="BRING treasures HERE AND SAY SCORE.":GOSUB1
1030 IFNO=43THENM$="BEWARE OF THE FIERCE MINOTAUR!":GOSUB1
1040 IFNO=38THENM$="PUT SHOT IN GUN.":GOSUB1
1050 GOTO90
1060 M$="USE COMPASS POINTS, PLEASE.":GOSUB1:GOTO90
1070 IFNO<>40ANDNO<>35ANDNO<>14THENM$="HOW? ":GOSUB1:GOTO90
1080 IFNO=40THENM$="TRY 'UNLOCK GATE'.":GOSUB1:GOTO90
1090 IFNO<>35THEN1140
1100 IFO(26,0)<>-1THENM$="YOU DON'T HAVE THE NECESSARY RESOURCES!":GOSUB1:GOTO90
1110 IFO(36,0)=CPTHENM$="OCTOPUS"+M2$:GOSUB1:GOTO90
1120 IFO(38,0)<>0THENM$="OK. IT'S EMPTY.":GOSUB1:GOTO90
1130 M$="OK. TWO THINGS FALL OUT!":GOSUB1:O(38,0)=CP:O(39,0)=CP:GOTO90
1140 IFO(33,0)<>-1THENM$="YOU DON'T HAVE THE RIGHT TOOLS!":GOSUB1:GOTO90
1150 IFO(37,0)<>0THENM$="THE CLAM OPENS & SHUTS. IT'S EMPTY!":GOSUB1:GOTO90
1160 M$="THE CLAM FLIES OPEN AND SOMETHING FALLS OUT, BUT IT SNAPS SHUT AGAIN!!":GOSUB1:O(37,0)=CP:GOTO90
1170 M$="I'M NOT THE DESTRUCTIVE TYPE...":GOSUB1:GOTO90
1180 IFO(18,0)<>CP THENM$="NOTHING HAPPENED.":GOSUB1:GOTO90
1190 M$="THE SQUID WAS SO STARTLED, HE SWAM AWAY!":GOSUB1:O(18,0)=0:GOTO90
1200 M$="OKAY..."+N1$:GOSUB1:GOTO90
1210 IFNO<>29THENM$="I KNOW NOT HOW TO LOAD SUCH A THING. SAY RESTORE TO START UP A SAVED GAME.":GOTO90
1220 IFCP<>12THENM$=M0$:GOSUB1:GOTO90
1230 IFO(20,0)<>-1THENM$="I HAVE NOTHING TO LOAD IT WITH!!":GOSUB1:GOTO90
1240 M$="OK.":GOSUB1:O(20,0)=-1:ZZ=ZZ-1:GOTO90
1250 IFNO<>10ANDNO<>14ANDNO<>16ANDNO<>18ANDNO<>22ANDNO<>23ANDNO<>36THENM$=M3$:GOSUB1:GOTO90
1260 IFNO<>22ANDNO<>16THENM$=M7$:GOSUB1:GOTO90
1270 IFNO=16THEN1330
1280 IFO(30,0)<>-1THENM$="I HAVE NO FOOD!":GOSUB1:GOTO90
1290 IFO(22,0)<>CP THEN1320
1300 M$="THE PIRANHAS GULP DOWN THE CUP AND, MUCH TO THEIR SURPRISE, THEIR TEETH FALL OUT!":GOSUB1
1301 O(22,0)=0:O(23,0)=CP:NO$(22)="'":O(45,1)=-1:O(30,0)=0:ZZ=ZZ-1:O(24,0)=CP:GOTO90
1320 M$="I DON'T SEE THEM.":GOSUB1:GOTO90
1330 IFO(23,0)<>-1THENM$="HE LIKES THE IDEA... AND EATS ME!!!":GOSUB1:GOTO1660
1340 M$="THE MANTA SWALLOWS THE PIRANHAS AND SWIMS AWAY.":GOSUB1:ZZ=ZZ-1:O(16,0)=0:O(23,0)=0:GOTO90
1350 IFO(25,0)<>-1THENM$="I HAVE NO SCISSORS!":GOSUB1:GOTO90
1360 M$="THE SCISSORS ARE TOO DULL TO CUT WITH!":GOSUB1:GOTO90
1370 IFNO<>40ANDNO<>35THENM$="HOW? ":GOSUB1:GOTO90
1380 IFNO=35THENM$="TRY OPEN.":GOSUB1:GOTO90
1390 IFCP<>25ANDCP<>30THENM$=M0$:GOSUB1:GOTO90
1400 IFGF=1THENM$="IT IS ALREADY OPEN!":GOSUB1:GOTO90
1410 IFO(26,0)<>-1THENM$="YOU DON'T HAVE THE KEY!":GOSUB1:GOTO90
1420 M$=M6$:GOSUB1:GF=1:GOTO90
1430 IFNO<>27THENM$="HOW? ":GOSUB1:GOTO90
1440 IFCP<>9ANDCP<>31THENM$=M0$:GOSUB1:GOTO90
1450 IFCP=9THENCP=31:GOTO80
1460 CP=9:GOTO80
1480 IFNO<>29THENM$="HOW? ":GOSUB1:GOTO90
1490 IFCP<>12THENM$=M0$:GOSUB1:GOTO90
1500 IFO(20,0)<>-1THENM$="IT'S NOT LOADED!":GOSUB1:GOTO90
1510 M$=M6$+"...POW!!!!":GOSUB1:O(20,0)=29:O(10,0)=0:GOTO90
1520 IFNO<>30THENM$=M3$:GOSUB1:GOTO90
1530 IFO(30,0)<>-1THENM$=M1$:GOSUB1:GOTO90
1540 M$="SMACK! GOOD!":GOSUB1:ZZ=ZZ-1:O(30,0)=0:GOTO90
1550 IFO(NO,0)<>-1THENM$=M1$:GOSUB1:GOTO90
1560 ZZ=ZZ-1:M$=M6$:GOSUB1:IFO(NO,0)<>-1THENM$=M1$:GOSUB1:GOTO90
1570 O(NO,0)=CP
1580 GOTO90
1590 SOUND1,1:IFO(NO,0)<>-1ANDO(NO,0)<>CP THENM$=M0$:GOSUB1:GOTO90
1600 IFNO<>37THENM$=M6$+" NOTHING HAPPENED.":GOSUB1:GOTO90
1610 M$="WAIT A MINUTE... THIS STATUETTE ISN'T IRON... IT'S GOLD!":GOSUB1
1611 OB$(37)="a gold statuette":O(47,1)=-1:O(37,1)=16:GOTO90
1620 IFCP<>31THENM$="GEE! FUN!":GOSUB1:GOTO90
1630 X=INT(4):IFX=2THEN1650
1640 CP=9:GOTO80
1650 M$="THUD. I'M DEAD...":GOSUB1
1660 PRINT:GOSUB1850:FORI=1TOLO:IFABS(O(I,0))=1THENFS=FS+O(I,1)
1670 NEXTI:IFT<500THENFS=FS+5
1680 IFT<400THENFS=FS+5
1690 IFT<310THENFS=FS+10
1700 IFT<10THENFS=FS-20
1710 M$="YOU SCORED"+STR$(FS)+" POINTS OUT OF 200.":GOSUB1
1711 IFFS=0THENM$="YOU ARE A PEE WEE CLASS ADVENTURER (THE LOWEST RANK)":GOSUB1:GOTO1810
1720 IFFS<30THENM$="ARE YOU VEGETABLE OR MINERAL? RANK 8":GOSUB1:GOTO1810
1730 IFFS<50THENM$="NOT TOO GOOD PAL! RANK 7":GOSUB1:GOTO1810
1740 IFFS<90THENM$="YOU HAVE HAD A TASTE OF ADVENTURE. RANK 6":GOSUB1:GOTO1810
1750 IFFS<110THENM$="YOU ARE A MASTER ADVENTURER CLASS C (RANK 5)":GOSUB1:GOTO1810
1760 IFFS<150THENM$="YOU ARE A MASTER ADVENTURER CLASS B (RANK 4)":GOSUB1:GOTO1810
1770 IFFS<170THENM$="YOU ARE A MASTER ADVENTURER CLASS C (RANK 3)":GOSUB1:GOTO1810
1780 IFFS<190THENM$="WOWEE! YOU ARE GREAT! YOU'LL SOON BE AN ADVENTURER GRANDMASTER! ONE RANK AWAY... RANK 2":GOSUB1:GOTO1810
1800 IFFS<201THEN1805
1801 GOTO1810
1805 M$="TUM TUM-TUM TUM... TUM TE TUM TE DUM DE TUM...":GOSUB1
1806 M$="A REAL ADVENTURER GRANDMASTER...BOY OH BOY! CAN I SHAKE YOUR HAND?":GOSUB1:REM M$="YOU SHOULD "+R1$+R2$:GOSUB1
1810 PRINT:M$="PLAY AGAIN (Y/N)?":GOSUB1:GOSUB4:A$=AR$
1820 A$=LEFT$(A$,1):IFA$="Y"THENRUN
1830 IFA$<>"N"THEN1810
1840 CLS:END
1850 IFF1THENFS=FS+2
1860 IFF2THENFS=FS+2
1861 IFF2THENFS=FS+2
1870 IFF3THENFS=FS+4
1880 RETURN
1890 FORI=1TO31:FORJ=0TO3:READP(I,J):NEXTJ,I
1900 P$(1)="MY SUBMARINE DOCKED IN THE CITY OF ATLANTIS":P$(2)="A CORRIDOR FILLED WITH WATER":P$(3)="AN ALCOVE"
1901 P$(4)="A CORAL REEF":P$(5)="A WHIRLPOOL"
1910 P$(6)="THE HALL OF SPIRITS (MORE COMMONLY CALLED A GRAVEYARD)":P$(7)="*DEAD END":P$(8)="A DINING ROOM"
1911 P$(9)="*I'M ON THE STREET"
1920 P$(10)="*I'M AT THE END OF THE STREET":P$(11)="A DARK CAVE":P$(12)="THE FORTRESS OF ATLANTIS"
1921 P$(13)="CAPTAIN AHAB'S QUARTERS":P$(14)="A BARREN CHAMBER"
1930 P$(15)="A BARBER SHOP":P$(16)="AN OLYMPIC FIELD":P$(17)="A SUPPLY ROOM":P$(18)="THE SQUID'S CHAMBER"
1931 P$(19)="THE DRAWING ROOM":P$(20)="THE ROYAL COLISEUM"
1940 P$(21)="A TEMPLE":P$(22)="HALLWAY":P$(23)="OLD LIBRARY":P$(24)="THE CLAM ROOM. THE WALLS ARE MADE OF CLAMSHELL."
1950 P$(25)="A RAIL STATION (SO >THIS< IS HOW THEY GOT AROUND!)":P$(26)=P$(24):P$(27)="THE KITCHEN."
1951 P$(28)="THE DINING HALL":P$(29)="THE ELECTRIC EEL'S ROOM"
1960 P$(30)="THE JAIL. THERE IS A GATE TO THE SOUTH"
1961 P$(31)="AT THE TOP OF A POST. LOOKING DOWN I SEE ONLY THE STREET. THE WATER IS TOO MISTY TO SEE FAR AWAY"
1970 DATA0,,2,,8,,3,1,9,,4,2,10,,5,3,11,,,4,13,,7,,,8,,,7,2,9,,15,3,10,8,,4,11,9,,5,12,10,,,,11
1971 DATA16,6,,,17,,15,,,9,,14,22,13,17,0
1980 DATA23,14,18,16,24,,19,17,25,,20,18,,,21,19,26,,,20,27,16,,,,17,24,,29,18,,23,30,19,,,,21,,0
1981 DATA0,22,28,,,,,27,,,30,,,25,,,,,,0
1990 FORI=1TO40:READNO$(I):NEXTI
2000 DATAN,S,E,W,NOR,SOU,EAS,WES,THR,EEL,BOO,PAP,TRA,CLA,MED,MAN,PLA,SQU,COI,SHO,JOU,PIR,PIR,TEE,SCI,KEY,POS,NEW
2001 DATACAN,CUP,TAN,SIG,PIC,SPE,CHE,OCT,STA,NOT,PEA,GAT
2010 DATAGO,GET,LOO,INV,SAV,RES,SCO,QUI,THR,KIL,REA,ENT,OPE,HIT,YEL,SAY,^,LOA,FEE,CUT,UNL,HEL,CLI,FIR,EAT
2011 DATATAK,SWI,DRO,SCR,JUM
2020 FORI=1TONV:READVB$(I):NEXTI
2030 DATAA SPEAR THROWER,AN ELECTRIC EEL,A SMALL BOOK,A PAPER WITH WRITING ON IT,A,A GIANT CLAM
2031 DATAjade medallion,A BLACK MANTA,a platinum plaque
2040 DATAA GIANT SQUID,many coins,AN IRON SHOTPUT,CAPTAIN AHAB'S JOURNAL,SOME FIERCE PIRANHAS
2041 DATASOME HELPLESS PIRANHAS,some valuable piranha teeth
2050 DATAa pair of gold scissors,a silver key,A TALL POST,AN OLD NEWSPAPER,AN OLD CANNON
2051 DATAA PEANUT BUTTER CUP,AN AIR TANK,A SIGN,platinum pick
2060 DATAA STEEL SPEAR,A TREASURE CHEST,A FIERCE OCTOPUS,AN IRON STATUETTE,A NOTE,a glistening pearl
2070 DATA27,,29,,23,,24,,,,26,,20,10,19,,19,15,18,,18,20,16,,13,,14,,,,,15,15,5,8,5,9,,11,,12
2071 DATA0,1,,1,,1,,3,7,3,,4,,4,,,,,,,12
2080 FORI=9TO39:READOB$(I):NEXTI:FORI=9TO39:READO(I,0),O(I,1):NEXTI
2090 D$(0)="NORTH":D$(1)="SOUTH":D$(2)="EAST":D$(3)="WEST"
2100 P$(32)="THE PILLARS OF HERCULES":P(26,0)=32:P$(33)="PROMETHEUS' CHAMBER IN OLYMPIA"
2101 P$(34)="A STRANGE ROOM WITH WALLS THAT SEEM TO COME ALIVE AS I MOVE ABOUT"
2110 P$(35)="*I'M ON THE EDGE OF A CLIFF. THE WATER BELOW ME IS TOO MISTY TO SEE FAR DOWN. THERE MIGHT BE LIFE DOWN THERE..."
2120 P$(36)="A DEEP, DARK CORRIDOR. I CAN SEE NO CEILING FROM HERE, ONLY MURKY WATER. THE WATER SEEMS TO BE MOVING EAST OF ME..
2130 P$(37)="THE HALL OR MIRRORS. ALL DIRECTIONS LOOK THE SAME.":P$(38)=P$(37):P$(39)=P$(37):P$(40)="AN AWKWARD CORRIDOR"
2140 FORI=41TO50:P$(I)="A LABYRINTH OF PASSAGES OBVIOUSLY DESIGNED BY KING MINOS. IT IS RUMORED THAT THE MINOTAUR IS NEAR..."
2141 NEXTI:P$(51)="*DEAD END"
2150 DATA0,26,,33,,34,32,,33,36,37,35,,,34,,34,,5,,37,38,39,34,37,38,38,38,40,39,39,37,42,39,,,43,41,42,41,44,40
2151 DATA42,41,47,41,43,43,48,42,45,44,49,45,45,44,50,51,,,47,43,48,47,48,44,48,47,,45,50,,,46,,49,46,,,0
2170 FORI=32TONP:FORJ=0TO3:READP(I,J):NEXTJ,I
2180 FORI=41TONN:READNO$(I),OB$(I),O(I,0),O(I,1):NEXTI
2190 DATAAPP,a golden apple,33,10,MIN,A FIERCE MINOTAUR,51,,SCR,AN ANCIENT SCROLL,40,,FLE,a golden fleece,51,44
2192 R1$="TRY SOME OTHER GREG HASSETT ADVENTURES. THEY ARE: JOURNEY TO THE CENTER OF THE EARTH, "
2193 R2$="THE HOUSE OF SEVEN GABLES, KING TUT'S TOMB, SORCERER'S CASTLE, ENCHANTED ISLAND"
2200 CLS:RETURN
2230 CLS
2290 M$="IN THIS 'ADVENTURE' YOU ARE TO EXPLORE THE LOST CITY ATLANTIS":GOSUB1
2300 M$="IN SEARCH OF TREASURES. I WILL BE YOUR EYES AND HANDS DURING":GOSUB1
2310 M$="THIS ADVENTURE. DIRECT ME WITH ORDINARY ENGLISH SENTENCES OF ONE OR TWO WORDS.":GOSUB1
2320 M$="SOME EXAMPLES:":GOSUB1
2330 M$="'GET TANK', 'INVENTORY', 'KILL MANTA', 'DROP COINS'":GOSUB1
2350 M$="USE ANY VERB/NOUN COMBINATION YOU CAN THINK OF, BECAUSE IN THIS GAME, *ANYTHING* GOES!":GOSUB1:PRINT:GOSUB2390
2370 RETURN
2380 REM PAUSE
2390 M$="PRESS <RETURN> TO CONTINUE...":GOSUB1:GOSUB4:PRINT
2400 RETURN
2410 PRINT"I CAN ONLY REPEAT THE","INSTRUCTIONS.",,"WOULD YOU LIKE THAT? ";:GOSUB4:IFLEFT$(AR$,1)="N"THENPRINT"OKAY.":GOTO90
2430 GOSUB2230:GOTO80
3000 CLS3:FORC1=0TO5:CC=C1*32+7+RND(2):GOSUB3010:FORCC=1TO1500:NEXT:NEXT
3004 FORC1=1TO10:SOUND170,1:NEXT:RETURN
3010 PRINT@CC,"ØØØØØØØØØØØØØØØØØØØØØ";:CC=CC+32
3011 PRINT@CC,"ØØØØØØ≠ØØØØatlantisØØØ";:CC=CC+32
3012 PRINT@CC,"ØØØØØØ†§ØØadventureØØ";:CC=CC+32
3013 PRINT@CC,"ØØØØØØß†ØØØbyØgregØØØ";:CC=CC+32
3014 PRINT@CC,"ØØØØØÆììì≠ØØhassettØØØ";:CC=CC+32
3015 PRINT@CC,"ØØØØØ™üüü•ØØØØØØØØØØØ";:CC=CC+32
3016 PRINT@CC,"ØØ¨ììóüüüõìì¨ØØ¨ØØØØØ";:CC=CC+32
3017 PRINT@CC,"Ø®üü…∆û√ù…∆üüíìö¨ØØØØ";:CC=CC+32
3018 PRINT@CC,"Øïüü∆…õÃó∆…üüûüûòßØØØ";:CC=CC+32
3019 PRINT@CC,"Ø´úùüüüüüüüûúß£òØØØØØ";:CC=CC+32
3020 PRINT@CC,"ØØØ´£îúúúò£ßØØØØØØØØØ";:SOUND200-5*C1,1:RETURN
3030 REM MC-10 EDITS JIM GERRIE

REM ADVENTURE VERSION 6.5
REM COPYRIGHT (C)1980
REM BY GREG HASSETT"
REM 22 ALGONQUIN RD, CHELMSFORD
REM MASSACHUSETTS, 01824
REM WELCOME TO THE WONDERFUL WORLD OF ADVENTURE!
