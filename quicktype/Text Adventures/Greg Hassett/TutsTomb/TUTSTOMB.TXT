0 CLEAR1000:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
10 CLS
15 CP=RND(-(PEEK(9)*256+PEEK(10)))
20 GOSUB3000:S1$="I DON'T SEE IT HERE."
30 S2$="DON'T BE RIDICULOUS.":CP=1:NM=35:S3$="I DON'T KNOW THAT WORD.":GOTO2170
40 GOSUB1950:IFNM<0THENNM=0
50 IFRND(80)=42ANDCP<>1THEN1580
60 IFRND(57)=20THENTG=1
70 IFTG=1THENNG=NG+1
80 IFCP=1THENTG=0:NG=0
90 IF(OB(14,0)=CP OROB(12,0)=CP)ANDTG=1THENPRINT"THE GOOBER SEES THE SNAKE,","LEAPS UP AND RUNS AWAY!":TG=0:NG=0
100 IFNG>3THEN1660
110 IFTG=1THENPRINT"THERE IS A THREATENING LITTLE","GOOBER IN THE ROOM WITH ME."
120 IFNM=0ANDMF=0THEN125
121 GOTO140
125 PRINT"YOU ARE NOW OUT OF MATCHES.","I AM PREPARED TO GIVE YOU TEN","MORE, BUT THIS WILL SUBTRACT";
126 PRINT" 25 POINTS FROM YOUR FINAL SCORE. DOYOU WANT THE EXTRA MATCHES?":INPUT" ";EM$
130 MF=1:IFLEFT$(EM$,1)="Y"THENPRINT"OKAY.":NM=10:OB(33,0)=-1:OB(34,0)=0
140 GOSUB1850:IFVB=32THENVB=6:GOTO150
141 IFVB$="I"THENVB=4
150 IFNM=0THENOB(34,0)=-1:OB(33,0)=0
160 IFVB$="L"THENVB=3
170 IFOB(12,0)=-1THENGS=GS+1:GOTO180
171 IFVB$="LIS"THENVB=4
180 T=T+1:IFBI>0THENBI=BI+1
190 IFVB$="INI"ANDT=1THENOB(35,0)=-1:OB(33,0)=-1:OB(32,0)=0:PRINT"ALL RIGHT.":NM=NM-RND(4):GOTO120
200 IFNO$="FUC"ORNO$="SHI"THENPRINT"TCH! SUCH LANGUAGE!":GOTO120
210 IFMF=1ANDM2=0THENPL(0)=PL(0)-25:M2=1
220 IFVB>10ANDVB<>20ANDNO$=""THENPRINT"WHAT DIRECT OBJECT DO YOU WANT":INPUT"ME TO DO THAT TO?";NO$:GOSUB1920
230 IFNO$<>""ANDVB=1ANDNO=0THENM$="THAT DOESN'T MAKE ANY SENSE TO ME.":GOSUB1:GOTO120
240 IFVB=20THEN1170
250 IFVB=6AND(NO$="EVE"ORNO$="ALL")THEN610
260 IFVB>10ANDNO=0THENPRINT"I DON'T KNOW WHAT A",N1$;" IS!":GOTO120
270 IFVB>19THEN1160
280 ONVB GOTO 290,380,40,500,530,550,630,650,680,710,720,840,870,910,950,1030,1090,1140,1150
290 IFNO$<>""ANDNO=0THENPRINT"S3$:GOTO110
300 IFNO$=""THENINPUT"WHERE?";NO$:GOSUB1920
310 IFNO>28ORNO<21THENPRINT"YOU MUST USE COMPASS POINTS.":GOTO120
320 IFNO>24THENNO=NO-4
330 NO=NO-21
340 IFGF=0AND((CP=18ANDNO=1)OR(CP=37ANDNO=0))THENPRINT"THE GATE IS LOCKED!":GOTO120
350 IFP(CP,NO)=0ANDCP<>1THENPRINT"THERE IS A WALL THERE.":GOTO120
360 IFP(CP,NO)=0THENPRINT"I CAN'T GO THAT WAY.":GOTO120
370 CP=P(CP,NO):GOTO40
380 IFNO$=""THENINPUT"WHAT SHOULD I GET?";NO$:GOSUB1920
390 IFNO=12ANDOB(12,0)=CP THEN480
400 IFOB(NO,0)=-1THENPRINT"I'VE ALREADY GOT IT!":GOTO120
410 IFNO=12ANDOB(14,0)=CP THENPRINT"THAT IS BOTH IMPRACTICAL","AND VERY DANGEROUS.":GOTO120
420 IFNO$="KNI"THENPRINT"THE GOOBERS' KNIVES VANISH AS","THEY HIT THE WALLS OF THE TOMB.":GOTO120
430 IFNO=0THENPRINTS3$:GOTO120
440 IFNO=29ANDTG=1THENPRINT"THAT WOULD NOT BE TOO BRIGHT.":GOTO120
450 IFNO=32ANDOB(35,0)=CP THENNO=35
460 IFOB(NO,0)<>CP THENPRINTS1$:GOTO120
470 IFNO=31THENPRINTS2$:GOTO120
480 IFZZ+1>8THENPRINT"I CAN'T CARRY ANY MORE.":GOTO120
490 ZZ=ZZ+1:PRINT"ALL RIGHT.":OB(NO,0)=-1:GOTO120
500 PRINT"I'M CURRENTLY CARRYING:":ZZ=0:FORI=1TOLO:IFOB(I,0)=-1THENPRINTOB$(I):ZZ=ZZ+1
510 NEXTI:IFZZ=0THENPRINT"NOTHING!"
520 GOTO120
530 J=0:FORI=1TOLO:IFOB(I,0)=1THENJ=J+OB(I,1)
540 NEXTI:PRINT"OUT OF A CURRENT MAXIMUM OF 175 POINTS, YOU HAVE";J;
541 PRINT"POINTS.","YOU CAN GET A BOUNS OF UP TO 32 POINTS WHEN YOU WIN OR QUIT,","MAKING THE ACTUAL MAXIMUM 207.":GOTO120
550 IFNO=0ANDNO$<>""THENPRINT"WHAT'S A ";N1$;"?":GOTO120
560 IFNO$=""THENPRINT"WHAT DO YOU WANT ME TO DROP?":INPUT" ";N1$:GOSUB1920
570 IFNO$=""ORNO=0THENPRINT"HUH??":GOTO120
580 IFNO=32ANDOB(35,0)=-1THENNO=35:GOTO600
590 IFOB(NO,0)<>-1THENPRINT"I HAVE NO ";N1$:GOTO120
600 PRINT"OKAY.":OB(NO,0)=CP:ZZ=ZZ-1:GOTO120
610 FORI=1TOLO:IFOB(I,0)=-1THENOB(I,0)=CP
620 NEXTI:PRINT"OK.":ZZ=0:GOTO120
630 IFVB=7THENPRINT"IT'S AGAINST COMPANY RULES TO","GIVE AVERAGE ADVENTURERS HELP."
631 IFVB=7THENINPUT"ARE YOU A WIZARD";A$:IFLEFT$(A$,1)<>"Y"THENPRINT"OK.":GOTO120
640 PRINT"I DON'T BELIEVE YOU!","SAY THE MAGIC WORD":INPUTA$:PRINT"BAH! YOU ARE A FOOL","TO TAMPER WITH ME...":GOTO2740
650 OB(37,0)=NM:OB(38,0)=CP:OB(39,0)=GF:OB(40,0)=ZZ:OB(41,0)=TG:OB(42,0)=NG:OB(43,0)=T:OB(44,0)=GS:OB(45,0)=BI
651 CLS:PRINT"SAVING..";NA$:CSAVE*OB,NA$:GOTO40
680 CLS:PRINT"LOADING..";NA$:CLOAD*OB,NA$
700 NM=OB(37,0):CP=OB(38,0):GF=OB(39,0):ZZ=OB(40,0):TG=OB(41,0):NG=OB(42,0):T=OB(43,0):GS=OB(44,0):BI=OB(45,0):GOTO40
710 INPUT"DO YOU REALLY WANT TO QUIT NOW";QN$:IFLEFT$(QN$,1)="Y"THEN1690
711 GOTO120
720 IFNO=31AND(CP=18ORCP=37)THEN780
730 IFNO=31THENPRINT"I SEE NO GATE HERE.":GOTO120
740 IFOB(NO,0)<>-1THENPRINT"I'M NOT HOLDING IT.":GOTO120
750 IFNO<>30ANDNO<>31ANDNO<>20THENPRINT"IT CANNOT BE OPENED.":GOTO120
760 IFNO<>31THEN790
770 IFGF=1THENPRINT"IT IS ALREADY OPEN.":GOTO120
780 IFOB(4,0)=-1THENPRINT"THE GATE IS OPEN.":GF=1:GOTO120
781 PRINT"IT WOULD HELP IF I HAD A KEY TO OPEN THE GATE.":GOTO120
790 IFNO=20THENPRINT"THE COFFIN IS OPEN.":GOTO120
800 TG=0:NG=0:PRINT"OKAY..  - - POOF ! - -","I AM ENGULFED IN A DENSE CLOUD",
801 PRINT"OF ORANGE SMOKE AND AWAKEN","COUGHING AND GASPING TO FIND.."
810 FORI=1TOLO:IFOB$(I)=""OROB(I,0)=0THEN830
820 IFABS(OB(I,0))=1 THENOB(I,0)=RND(36)+1
830 NEXTI:ZZ=0:OB(35,0)=0:CP=1:OB(33,0)=1:OB(32,0)=1:FORI=1TO3500:NEXTI:PD=0:GOTO40
840 IFNO<>31THENPRINT"I DON'T KNOW HOW TO LOCK","SUCH A THING.":GOTO120
850 IFCP<>37ANDCP<>18THENPRINT"WHAT GATE?":GOTO120
860 PRINT"OKAY. THE GATE IS LOCKED.":GF=0:GOTO120
870 IFOB(NO,0)<>-1THENPRINT"I HAVE TO BE HOLDING SOMETHING","TO READ IT.":GOTO120
880 IFNO<>2ANDNO<>16THENPRINT"S2$:GOTO110
890 IFNO=2THENPRINT"IT SAYS:":PRINT"BEST FISHES -- STEVE MARTIN","KING TUT":GOTO120
900 PRINT"IT IS WRITTEN IN EGYPTIAN","HIEROGLYPHICS.":GOTO120
910 IFOB(NO,0)<>-1THENPRINT"I DON'T THINK I'M CARRYING IT.":GOTO120
920 IFNO<>13THENPRINT"YOU CAN'T BE SERIOUS.":GOTO120
930 IFOB(13,0)<>-1THENPRINT"I HAVE NO FOOD!":GOTO120
940 ZZ=ZZ-1:PRINT"*SMACK* THAT WAS GOOD!":OB(13,0)=0:GOTO120
950 IFNO<>12ANDNO<>29THENPRINTS2$:GOTO120
960 IFNO=29ANDTG=0THENPRINT"WHAT GOOBER?":GOTO120
970 IFNO=29THENPRINT"THERE IS NOTHING HERE IT WANTS","TO EAT. (EXCEPT PERHAPS ME!)":GOTO120
980 IFOB(13,0)<>-1THENPRINT"I HAVE NO FOOD!":GOTO120
990 IFOB(12,0)<>0THENPRINT"IT IS FULL.":GOTO120
1000 IFOB(14,0)<>CP THENPRINT"WHAT SNAKE?":GOTO120
1010 ZZ=ZZ-1:PRINT"THE SNAKE WARFS DOWN YOUR FOOD  AND RUBS HIS HEAD TO MY","LEG TO SHOW AFFECTION."
1020 OB(12,0)=CP:OB(14,0)=0:OB(13,0)=0:GOTO120
1030 IFNO=29ANDTG=0THENPRINT"I SEE NO GOOBER HERE.":GOTO120
1040 IFNO=29ANDTG=1THENPRINT"THE GOOBERS ARE IMMORTAL.":GOTO120
1050 IFNO<>12THENPRINT"IT ISN'T ALIVE!":GOTO120
1060 IFOB(14,0)=CP THENPRINT"THAT IS A STUPID THING TO TRY!":GOTO120
1070 IFOB(12,0)=-1OROB(12,0)=CP THENPRINT"YOU KILLED A LITTLE SNAKE.","THE BODY VANISHES.":OB(12,0)=0:GOTO120
1080 PRINTS1$:GOTO120
1090 IFNO=29ANDTG=1THENPRINT"HE IS MAD ENOUGH AT ME ALREADY!":GOTO120
1100 IFNO=29THENPRINT"I SEE NO GOOBER HERE.":GOTO120
1110 IFOB(NO,0)<>-1THENPRINT"I'M NOT HOLDING IT.":GOTO120
1120 IFNO<>13THENPRINT"THAT'S STUPID!":GOTO120
1130 PRINT"THE SNAKE IS STARTLED,","JUMPS AWAY FROM ME","AND SCURRIES AWAY INTO THE GLOOM":OB(12,0)=0:GOTO120
1140 PRINT"OKAY..";N1$:GOTO120
1150 PRINT"TRY OPEN.":GOTO120
1160 ONVB-19 GOTO 1170,1180,1230,1270,1280,1330,1340,1410,1420,1430,1460,1570
1170 T=T-1:PRINT"YOU HAVE USED";T;"TURNS SO FAR,","AND YOU HAVE USED";35-NM;"MATCHES.":GOTO120
1180 IFNO<>20ANDNO<>31ANDNO<>30THENPRINT"I DON'T KNOW HOW TO DO THAT.":GOTO120
1190 IFNO=31AND(CP=18ORCP=37)THENPRINT"THE GATE IS CLOSED AND LOCKED.":GF=0:GOTO120
1200 IFOB(NO,0)<>-1THENPRINTS1$:GOTO120
1210 IFNO=30THENPRINT"IT IS ALREADY CLOSED.":GOTO120
1220 PRINT"OKAY. IT IS CLOSED.":GOTO120
1230 IFNO<>12THEN550
1240 IFOB(12,0)<>-1THENPRINT"I'M NOT HOLDING A SNAKE.":GOTO120
1250 IFTG=1THENPRINT"THE SNAKE ATTACKS THE GOOBER",
1251 IFTG=1THENPRINT"AND IN AS ASTOUNDING FLURRY","DRIVES THE GOOBER AWAY!":OB(12,0)=CP:NG=0:TG=0:ZZ=ZZ-1:GOTO120
1260 ZZ=ZZ-1:PRINT"THE SNAKE VANISHES","INTO THE GLOOM.":OB(12,0)=RND(35)+1:GOTO120
1270 IFOB(I,0)=-1THENPRINT"NOTHING HAPPENED.":GOTO120
1271 PRINT"I'M NOT HOLDING IT!":GOTO120
1280 IFNO<>36THENPRINTS2$:GOTO120
1290 IFOB(36,0)<>-1THENPRINT"I HAVE NO WINE.":GOTO120
1300 IFBI=0THENPRINT"OKAY. GULP GULK.":OB(36,0)=0:OB(17,0)=-1GOTO120
1310 PRINT"AHH..GOOD CURE FOR SNAKEBITE.":IFOB(12,0)=-1THENPRINT"BUT THE SNAKE RAN AWAY!!":OB(12,0)=0
1320 OB(36,0)=0:OB(17,0)=-1:BI=-32000:GOTO120
1330 PRINT"I DON'T WANT TO BREAK ANYTHING. THE BOSS SAYS I HAVE TO PAY FOR ANYTHING I BREAK ON AN ADVENTURE":GOTO120
1340 IFNM=0ANDOB(33,0)=-1THENOB(33,0)=0:OB(34,0)=-1
1350 IFNO<>33ANDNO<>32THENPRINTS2$:GOTO120
1360 IFOB(33,0)<>-1THENPRINT"I DON'T HAVE A MATCH!":GOTO120
1370 IFNO=33THENPRINT"OKAY. YOU NOW HAVE ONE","LESS MATCH.":NM=NM-1:GOTO120
1380 IFOB(35,0)<>0THENPRINT"IT IS ALREADY LIT.":GOTO120
1390 IFOB(32,0)=-1THENOB(32,0)=0:OB(35,0)=-1:NM=NM-RND(4):PD=0:GOTO40
1400 PRINT"I HAVE NO TORCH.":GOTO120
1410 PRINT"I AM NOT MOHAMMED ALI, YOU KNOW.":GOTO120
1420 PRINT"TRY HIT OR KILL.":GOTO120
1430 IFNO<>16THENPRINT"I SEE NO WRITING THERE.":GOTO120
1440 IFOB(NO,0)<>-1THENPRINT"I HAVE NO BOOK.":GOTO120
1450 PRINT"IT SAYS: GOOBERS HATE SNAKES","MOM IS FLAMMABLE.":GOTO120
1460 IFOB(33,0)<>-1THENPRINT"I HAVE NO MATCHES!":GOTO120
1470 IFOB(NO,0)<>-1THENPRINT"I AM NOT HOLDING IT RIGHT NOW.":GOTO120
1480 IFNO<>2ANDNO<>12ANDNO<>13ANDNO<>16ANDNO<>34ANDNO<>32ANDNO<>33THENPRINT"IT DOESN'T BURN.":GOTO120
1490 IFNO=32THENPRINT"YOU MIGHT TRY LIGHT TORCH","INSTEAD.":GOTO120
1500 NM=NM-1
1510 IFNO=2THENPRINT"ALL RIGHT. THE POSTER","GOES UP IN FLAMES.":ZZ=ZZ-1:OB(2,0)=0:GOTO120
1520 IFNO=12THENPRINT"THE SNAKE BIT ME IN SELF DEFENSE":BI=1:OB$(12)="A SCARED SNAKE":GOTO120
1530 IFNO=13THENPRINT"ALL RIGHT. IT IS NOW COOKED.":OB$(13)="SOME COOKED FOOD":GOTO120
1540 IFNO=16THENPRINT"THAT IS A WASTE OF","GOOD LITERATURE.":GOTO120
1550 IFNO=34THENPRINT"OKAY.":OB(34,0)=0:GOTO120
1560 IFNO=33THENPRINT"ALL RIGHT.":NM=NM-1:GOTO120
1570 PRINT"THAT IS WORTH";OB(NO,1);"POINTS.":GOTO120
1580 IFRND(2)=1THENPRINT"+=+= HOLY BANDAIDS!"
1581 PRINT"SUFFRIN' SUCCOTASH!"
1590 PRINT"AN ANCIENT MUMMY WALKED IN TO":IFRND(2)=1THENPRINT"TERMINATE ME!":GOTO1600
1591 PRINT"PUT ME TO DEATH!"
1600 GOSUB1850
1610 IFVB$="BUR"THEN1630
1615 IFVB$<>"BUR"THENPRINT"THE MUMMY IS CHARGING AT ME!!":FORI=1TO400:NEXTI:IFRND(5)=1THENPRINT"HE KILLED ME!!":GOTO2740
1620 PRINT"HE SLIPS ON A CONVENIENTLY","PLACED BANANA PEEL AND CRACKS","HIS SKULL. THE BODY VANISHES.":GOTO120
1630 IFOB(33,0)<>-1THENPRINT"I HAVE NO MATCHES!":GOTO1650
1640 PRINT"THE MUMMY BURNS TO THE GROUND.":NM=NM-1:GOTO120
1650 FORI=1TO500:NEXTI:GOTO2740
1660 PRINT"THE GOOBER THROWS A KNIFE AT ME!"
1670 IFRND(5)=1THENPRINT"IT HITS ME!":GOTO2740
1671 PRINT"IT MISSES!"
1680 NG=1:GOTO120
1690 J=0:CLS:FORI=1TOLO:IFOB$(I)=""THEN1710
1700 IFOB(I,0)=1THENJ=J+OB(I,1)
1710 NEXTI:PRINT
1720 IFT>310THENJ=J-10
1730 IFT>357THENJ=J-15
1740 J=J+INT(NM/2):FORI=0TO3:J=J+PL(I):NEXT
1750 PRINT"YOU HAVE SCORED";J;"POINTS","OUT OF A MAXIMUM OF 207",NB$;"."
1760 PRINT"A SCORE OF";J;" POINTS","QUALIFIES YOU AS A:"
1770 IFJ>182THENPRINT"ADVENTURER GRANDMASTER":END
1780 IFJ>178THENPRINT"MASTER ADVENTURER CLASS A":END
1790 IFJ>150THENPRINT"MASTER ADVENTURER CLASS B":END
1800 IFJ>140THENPRINT"EXPERIENCED ADVENTURER":END
1810 IFJ>110THENPRINT"SEASONED ADVENTURER":END
1820 IFJ>90THENPRINT"NOVICE CLASS ADVENTURER","BETTER LUCK NEXT TIME.":END
1830 IFJ>20THENPRINT"PEE WEE CLASS ADVENTURER":END
1840 PRINT"ADVENTURER WITH THE","INTELLIGENCE OF A CARROT":END
1850 CM$="":INPUT"WHAT SHOULD I DO";CM$:IFCM$=""THEN1850
1860 NO$="":VB$="":VB=0:NO=0
1870 LC=LEN(CM$):FORI=1TOLC:IFMID$(CM$,I,1)<>" "THENVB$=VB$+MID$(CM$,I,1):NEXTI
1880 V1$=VB$:VB$=LEFT$(V1$,3):FORI=1TONV:IFVB$(I)=VB$THENVB=I:GOTO1900
1881 NEXTI
1890 VB=1:NO$=VB$:GOTO1920
1900 IFLEN(V1$)+1>=LEN(CM$)THENNO=0:RETURN
1910 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
1920 N1$=NO$:NO$=LEFT$(NO$,3):FORI=1TONN:IFNO$=NO$(I)THEN1940
1921 NEXTI
1930 NO=0:RETURN
1940 NO=I:RETURN
1950 CLS
1960 IFPD>2THENPRINT"YOU FELL INTO A PIT AND BROKE","EVERY BONE IN YOUR BODY.":GOTO2740
1970 IFOB(35,0)<>-1ANDCP<>1THENPRINT"IT IS PITCH DARK.","YOU WILL PROBABLY KILL YOURSELF",
1971 IFOB(35,0)<>-1ANDCP<>1THENPRINT"IF YOU PROCEED EXPLORING.":PD=PD+1:RETURN
1980 IFT>=1THENP$(1)="I'M  OUTSIDE THE PYRAMID AGAIN."
1990 PRINTP$(CP):PD=0
2000 IFCP=32THENPL(1)=5
2010 IFCP=34THENPL(2)=5
2020 IFCP=7THENPL(3)=5
2030 FORI=1TOLO:IFOB(I,0)=CP THENM$="THERE IS "+OB$(I)+" HERE.":GOSUB1
2040 NEXTI
2050 FL=0:IFOB(35,0)<>-1THEN2100
2051 PRINT"I CAN GO --":FORI=0TO3:IFP(CP,I)<>0THENPRINTD$(I);"  ";:FL=1
2060 NEXTI:PRINT
2070 IFGS=30THENPRINT"I THINK THE SNAKE IS GETTING","HUNGRY AGAIN.":OB$(12)="A TAME HUNGRY SNAKE"
2080 IFGS=50THENPRINT"THE SNAKE JUST BIT ME!":BI=1:OB$(12)="A VERY HUNGRY SNAKE"
2090 IFBI=10THENPRINT"THAT SNAKE BITE! I'M GETTING","WEAKER..WINE! I NEED WINE!"
2100 PRINT"ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ";
2110 IFT<>0ANDOB(35,0)=-1ANDT/32=INT(T/32)THENPRINT"MY TORCH IS OUT.":OB(35,0)=0:OB(32,0)=-1
2120 IFBI=45THENPRINT"THE SNAKE BITE! OH..NO..":OB(12,0)=0:FORI=1TO600:NEXTI:GOTO2740
2130 IFCP<>18THENRETURN
2140 IFGF=1THENPRINT"THE GATE IS OPEN.":GOTO2150
2141 PRINT"THE GATE IS LOCKED."
2150 IFNM=0ANDM3=0ANDMF=1THENPRINT"I HAVE NO MORE MATCHES.":M3=1:OB(34,0)=-1:OB(33,0)=0
2160 RETURN
2170 NN=36:NV=32:P=38:LO=36:DIMP$(P),P(P,3),OB$(LO),OB(45,1),VB$(NV),NO$(NN)
2180 P$(1)="I'M OUTSIDE AN ENORMOUS PYRAMID.AN OPENING LEADS IN TO THE EAST.":DATA 0,0,5,0,2,7,3,6,3,3,8,2
2190 P$(2)="I'M IN THE ANNEX OF KING TUT'S  TOMB."
2200 P$(3)="I'M IN THE QUARTERS OF PRINCESS ANKA."
2210 P$(4)="I'M IN KARMAK'S CATHEDRAL.":DATA 0,8,12,0,0,9,0,1,2,0,0,14
2220 P$(5)="I'M IN THE ENTRY CHAMBER TO KINGTUT'S TOMB."
2230 P$(6)="I'M IN A TWISTING CORRIDOR."
2240 P$(7)="I'M IN THE JEWELRY CHAMBER.":DATA 0,7,7,2
2250 P$(8)="I'M IN THE ARTIST'S STUDIO.":DATA 4,0,0,3
2260 P$(9)="I'M CRAWLING OVER A JUMBLE OF   BROKEN ROCK.":DATA 9,13,5,0
2270 P$(10)="I'M IN A LABYRINTH OF PASSAGES  DESIGNED TO KEEP OUT TOMBROBBERS":DATA 10,15,11,10
2280 P$(11)=P$(10):DATA 11,16,11,10
2290 P$(12)="I'M IN AN EGYPTIAN LIBRARY.":DATA 0,17,4,7
2300 P$(13)="I'M IN AN AWKWARD N/S PASSAGEWAY":DATA 9,21,0,0
2310 P$(14)=P$(6):DATA 14,20,14,6
2320 P$(15)=P$(10):DATA 10,18,15,15
2330 P$(16)=P$(10):DATA 11,16,19,16
2340 P$(17)="I'M IN A WINE CLOSET. (ALL CHEAPSTUFF)":DATA 12,22,0,0
2350 P$(18)="I'M AT THE END OF THE MAZE.     THERE IS A GATE ON THE DOOR     LEADING SOUTH."
2360 DATA 18,37,15,18
2370 P$(19)=P$(10):DATA 19,23,16,19
2380 P$(20)=P$(6):DATA 14,0,0,24
2390 P$(21)="I'M IN A GAME ROOM.":DATA 13,24,0,0
2400 P$(22)="I'M IN A BEDROOM DESIGNED FOR   THE AFTERLIFE.":DATA 17,0,27,27
2410 P$(23)="I'M IN SLAVES' QUARTERS A.":DATA 19,26,0,0
2420 P$(24)="I'M AT THE WEST END OF A TEMPLE.":DATA 20,0,25,21
2430 P$(25)="I'M IN THE EAST END OF THE      TEMPLE.":DATA 25,0,0,24
2440 P$(26)="I'M IN THE WARRIORS' QUARTERS.":DATA 23,30,27,0
2450 P$(27)="I'M IN A STABLE.":DATA 0,31,22,0
2460 P$(28)="I'M IN THE HIGH PRIEST'S CHAMBER":DATA 0,35,29,0
2470 P$(29)="I'M IN THE CHAMBER OF ISIS.":DATA 30,0,29,28
2480 P$(30)="I'M IN SLAVES' QUARTERS B.":DATA 26,33,31,29
2490 P$(31)="I'M IN THE ANTECHAMBER.":DATA 27,32,0,30
2500 P$(32)="I'M AT THE ROYAL THRONE.":DATA 0,0,0,31
2510 P$(33)="I'M IN THE STORE ROOM.":DATA 0,0,36,30
2520 P$(34)="I'M IN A MIRROR ROOM.":DATA 34,35,34,34
2530 P$(35)="I'M IN THE KING'S HAREM.":DATA 28,0,35,34
2540 P$(36)="I'M IN A SNAKE PIT.":DATA 0,0,33,36
2550 P$(37)="I'M IN THE BURIAL CHAMBER.":DATA 18,0,0,0
2560 DATA 37,0,0,0
2570 FORI=1TOP:FORJ=0TO3:READP(I,J):NEXTJ,I
2580 DATA 5,5,21,0,24,10,25,5,34,10,35,10,2,15,3,5,7,10,11,10,33,5,0,0,1,0,36,0,8,10,12,0,0,0,26,0,32,10,37,55
2590 DATA"AN OPAL DRINKING CUP","A STEVE MARTIN POSTER ","A GOLD CHALICE","A GOLDEN KEY","A GOLDEN BURIAL MASK "
2591 DATA"A DIAMOND HEADRESS"
2600 DATA A PLATINUM SPEAR,A RUBY EARING,AN ALABASTER NECKLACE ,A GIANT PEARL,AN ALABASTER LAMP,A TAME SNAKE
2601 DATA A PACKAGE OF FOOD
2610 DATA A FIERCE SNAKE,A JADE STATUETTE,THE BOOK OF THE DEAD,A CRYSTAL GOBLET,A GLASS MEDALLION,AN IVORY THRONE
2611 DATA A MUMMIFORM COFFIN
2620 FORI=1TO20:READOB(I,0):READOB(I,1):NEXT
2630 FORI=1TO20:READOB$(I):NEXT
2640 DATA 28,15,0,0,1,0,1,0,0,0,0,0,17,0
2650 FORI=30TO36:READOB(I,0),OB(I,1):NEXT
2660 DATA AN IVORY SARCOPHAGOUS ,----------------,AN ANCIENT TORCH
2661 DATA SOME MATCHES,AN EMPTY MATCHBOX,A SHINING TORCH,A GOBLET OF WINE
2670 FORI=30TO36:READOB$(I):NEXT
2680 DATA CUP,POS,CHA,KEY,MAS,HEA,SPE,EAR,NEC,PEA,LAM,SNA,FOO,SNA,STA,BOO
2681 DATA GOB,MED,THR,COF,NOR,SOU,EAS,WES,N,S,E,W,GOO,SAR,GAT,TOR
2690 DATA MAT,BOX,TOR,WIN,GO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,OPE,LOC,REA,EAT,FEE,KIL,HIT,SAY,UNL,TUR,CLO,REL
2700 DATA SHO,DRI,BRE,LIG,FIG,DES,TRA,BUR,VAL,THR
2710 FORI=1TONN:READNO$(I):NEXT:FORI=1TONV:READVB$(I):NEXT
2720 DATA NORTH,SOUTH,EAST,WEST
2730 FORI=0TO3:READD$(I):NEXT:CLS:INPUT"WHAT IS YOUR NAME";NA$:NB$=NA$:NA$=LEFT$(NA$,8):GOTO40
2740 X=X+1
2750 ONX GOTO 2760,2830
2760 PRINT"OMEGOSH! YOU HAVE GOTTEN","YOURSELF KILLED!"
2770 PRINT"THE BOSS WON'T LIKE THIS..","NOT ONE BIT! MAYBE I COULD"
2780 PRINT"HELP YOU, BUT THIS WILL","BE A FIRST..."
2790 PRINT"WOULD YOU LIKE ME TO TRY AND":INPUT"REINCARNATE YOU";R$
2800 IFLEFT$(R$,1)="N"THEN1690
2810 CLS:PRINT"NOW WHERE DID I PUT MY ORANGE","SMOKE? I SEEM TO REMEMBER":PL(0)=PL(0)-20
2820 PRINT"LEAVING IT IN A SARCOPHAGOUS","SOMEWHERE. AH HERE IT IS.":GOTO800
2830 PRINT"OH BOY. YOU REALLY DID IT THIS","TIME! I CAN'T BELIEVE IT."
2840 PRINT"I'M FRESH OUT OF ORANGE SMOKE!!"
2850 PRINT"YOU DON'T EXPECT ME TO PERFORM ADECENT REINCARNATION WITH NO":INPUT"MORE SMOKE LEFT DO YOU";R2$
2860 IFLEFT$(R2$,1)="Y"THENCLS:PRINT:PRINT"HUH! WHO DO YOU THINK YOU ARE","A WIZARD!? DO IT YOURSELF!!":PRINT:END
2870 GOTO1690
3000 CLS:PRINT:PRINT
3005 PRINT"                ‰†"
3010 PRINT"               ‰  †"
3020 PRINT"              ‰    †"
3030 PRINT"             ‰      †"
3035 PRINT"            ‰        †"
3040 PRINT"           ‰  KING    †"
3045 PRINT"          ‰    TUT'S   †"
3050 PRINT"         ‰       TOMB   †"
3060 PRINT"        ‰    BY          †"
3090 PRINT"       ‰      GREG        †"
3091 PRINT"      ‰        HASSETT     †"
3092 PRINT"     ˆŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ„"
3093 PRINT"     MC-10 MODS BY JIM GERRIE
3095 RETURN
