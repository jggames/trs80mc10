0 CLS3:CLEAR800:DIMCC,C1,M$,I$:GOTO10
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 GOSUB1:GOTO26
10 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁ‹›ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂÅé◊ﬁÉ›ﬂﬂﬂENCHANTEDﬂﬂﬂ";
11 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬁ‹‹ÖèÉáààﬂﬂﬂﬂﬂISLEﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁáåçèèèà›ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ€”ﬂÿáèüáã›ﬂﬂﬂﬂﬂﬂBYﬂﬂﬂﬂﬂﬂﬂ";
12 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂ⁄áéüˇ””◊ﬂﬂﬂﬂﬂGREGﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ⁄é’ﬂˇˇﬂﬂﬂﬂﬂﬂHASSETTﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ”ﬂﬂﬂˇˇﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ";
13 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂˇˇﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁ‹‹‹ìììÙ¯ìì‹‹‹ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁììóüüüüüüüüüüüüüìììì‹ﬂﬂﬂﬂﬂﬂ";
20 M0$="I DON'T SEE IT HERE.":M1$="I'M NOT HOLDING IT.":M2$=" WON'T LET ME.":M3$="DON'T BE RIDICULOUS.":M4$="GIVE ME A BREAK!"
21 M5$="DON'T BE FUNNY.":M6$="OKAY.":M7$="IT'S NOT HUNGRY.":M8$="HOW?":M9$="A VALIANT ATTEMPT.":GOTO30000
25 GOTO100
26 GOSUB30:IFNO<9ANDNO<>0AND(VB=0ORVB=1)THENVB=1
27 IFVB-27ANDVB-28ANDLEN(N1$)>0ANDNO=0THENM$="I DON'T KNOW WHAT "+CHR$(34)+N1$+CHR$(34)+" IS.":GOTO9
28 IFVB-32ANDVB-33ANDVB>8ANDN1$=""THENPRINT"PLEASE SUPPLY A DIRECT OBJECT.":GOTO26
29 ON-((VB-27ANDVB-28ANDVB-34ANDVB-14ANDVB>8)=0)GOTO200:GOSUB250:GOTO200
30 T=T+1:INPUT"WHAT SHOULD I DO";CM$
32 IFLEN(CM$)=0THEN30
35 VB$="":NO$="":VB=0:NO=0
40 LC=LEN(CM$):FORI=1TOLC:A$=MID$(CM$,I,1):IFA$<>" "THENVB$=VB$+A$:NEXTI
43 IFLEN(VB$)+1>=LEN(CM$)THENNO=0:GOTO50
45 NO$=RIGHT$(CM$,LC-I)
50 N1$=NO$:V1$=VB$:VB$=LEFT$(VB$,3):NO$=LEFT$(NO$,3):FORI=1TONV:IFVB$=VB$(I)THENVB=I:GOTO70
51 NEXTI:VB=0
55 IFN1$=""THENNO$=VB$
60 VB=0:GOTO80
70 IFVB$=CM$THENO=0:NO$="":RETURN
80 FORI=1TONN:IFNO$=NO$(I)THENNO=I:RETURN
81 NEXTI:NO=0:NO$="":RETURN
100 II=0:IFLEFT$(P$(CP),1)="/"THENP$=RIGHT$(P$(CP),LEN(P$(CP))-1):II=1:GOTO105
104 P$="I'M "+P$(CP)
105 IFCP>28ANDCP<45ANDO(9,0)<>-1ANDO(9,0)-CPTHENCLS:PRINT"I CAN'T SEE, IT'S TOO DARK.":GOTO170
110 CLS:M$=P$+".":GOSUB1:K=0:FORI=1TOLO:IFO(I,0)<>CPORLEN(M$)>225THEN130
120 IFK=0THENPRINT:M$="VISABLE ITEMS: "
121 IFK>0THENM$=M$+", "
125 M$=M$+OB$(I):K=K+1
130 NEXTI:IFLEN(M$)>225THENM$=M$+".."
131 IFK>0THENM$=M$+".":GOSUB1
140 IFCP=45THEN170
145 PRINT:K=0:M$="OBVIOUS EXITS ARE: ":FORI=0TO3:IFP(CP,I)=0THEN156
150 IFK>0THENM$=M$+", "
155 M$=M$+D$(I):K=K+1
156 NEXTI:M$=M$+".":GOSUB1:IFCP=32ANDST=0THENM$="THE SKULL SAYS, "+CHR$(34)+"THE RUBY WAS BIMBO'S."+CHR$(34):GOSUB1:ST=1
157 IFO(I,0)=-1ANDI<>31THENO(I,0)=29
170 PRINT"åååååååååååååååååååååååååååååååå";:GOTO26
200 ONVBGOTO400,300,25,500,600,700,800,900,1000,1100,1200,1300,1400,1500,1600
201 IFVB>0THENONVB-15GOTO1620,1700,1800,1900,2000,2100,2200,2300,2400,2500,2400,2700,2800,2900,3000,3100,3200,3300
205 M$="I KNOW NOT HOW TO "+CHR$(34)+V1$+CHR$(34)+" SOMETHING.":GOTO9
210 STOP
250 X=0:IFO(NO,0)<>-1ANDO(NO,0)-CPTHENPRINTM0$:GOTO26
251 X=O(NO,0):RETURN
300 GOSUB250:IFX=-1THENPRINT"I'VE ALREADY GOT IT.":GOTO26
305 IFNO=0THENPRINT"YOU'LL HAVE TO SUPPLY A NOUN.":GOTO26
310 IFNO=14ORNO=43THENPRINTM9$:GOTO26
320 IFNO=15ORNO=19ORNO=26ORNO=28ORNO=36ORNO=40ORNO=42THENPRINTM4$:GOTO26
330 IFNO=11THENPRINT"IT IS BEYOND MY REACH.":GOTO26
340 IFNO=25ANDO(26,0)=CPTHENM$="BARBARIAN"+M2$:GOTO9
350 IFNO=27ANDO(28,0)=CPTHENM$="BEAR"+M2$:GOTO9
360 IFNO=30ANDO(42,0)=CPTHENM$="MEDICINE MAN"+M2$:GOTO9
370 IFNO=41ANDO(40,0)=CPTHENM$="TIGER"+M2$:GOTO9
375 IFNO=37ANDO(36,0)=CPTHENM$="VULTURE"+M2$:GOTO9
376 IFNO=18ANDO(19,0)=CPTHENM$="WARLOCK"+M2$:GOTO9
380 IFNO=32ANDO(21,0)<>-1THENM$="IT IS EMBEDDED IN THE ROCK WALL.":GOTO9
390 IFZZ+1>7THENM$="I'M CARRYING TOO MUCH. CLUE: DROP SOMETHING.":GOTO9
391 PRINTM6$:ZZ=ZZ+1:O(NO,0)=-1:GOTO26
400 IFNO>8THENPRINT"PLEASE USE COMPASS POINTS.":GOTO26
401 IFNO>4THENNO=NO-5:GOTO406
402 NO=NO-1
406 IFNO=-1THENPRINT"WHERE?":GOTO26
410 IFP(CP,NO)=0THENM$="THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO9
415 IFP(CP,NO)=7THENCLS:M$="I'M IN THE OCEAN! HELP! I'M DEAD.":GOSUB1:FORI=1TO1500:NEXTI:GOTO9000
420 CP=P(CP,NO):GOTO25
500 IFZZ=0THENPRINT"I'M EMPTY HANDED.":GOTO26
501 M$="I AM CARRYING: "
510 K=0:FORI=9TOLO:IFO(I,0)<>-1THEN540
520 IFK>0THENM$=M$+", "
530 M$=M$+OB$(I):K=K+1
540 NEXTI:M$=M$+".":GOTO9
600 SC=0:M$="OUT OF A MAXIMUM OF 140 POINTS, YOU HAVE":FORI=9TOLO:IFO(I,0)=1THENSC=SC+O(I,1)
610 NEXTI:M$=M$+STR$(SC)+" POINTS.":GOSUB1:IFSC=0THENPRINT"I SEE YOU'RE NEW TO THE GAME..."
620 GOTO26
700 CLS:INPUT"HIT <ENTER>";XX$:PRINT"SAVING..."
705 O(46,0)=CP:O(45,1)=ZZ:CSAVE*O,"ENCHDATA":GOTO25
800 CLS:INPUT"HIT <ENTER>";XX$:PRINT"RESTORING...":CLOAD*O,"ENCHDATA"
801 IFO(45,0)=1THENNO$(9)="LAN"
802 IFO(45,0)=2THENNO$(9)="<X>"
805 CP=O(46,0):ZZ=O(45,1):GOTO25
900 IFO(NO,0)<>-1THENPRINTM1$:GOTO26
905 IFNO=24ANDO(19,0)=CPTHENM$="THE WARLOCK SLIPS AND BREAKS HIS NECK. THEN HE VANISHES.":GOSUB1:O(19,0)=0
910 ZZ=ZZ-1:O(NO,0)=CP:PRINTM6$:GOTO26
1000 IFNO=9THENPRINT"IT'S ALREADY LIT.":GOTO26
1005 IFO(20,0)<>-1THENPRINT"I'VE NO LIGHTER!":GOTO26
1010 IFNO-10THENPRINT"IT WON'T IGNITE.":GOTO26
1015 IFO(NO,0)<>-1THENPRINTM1$:GOTO26
1020 NO$(9)="LAN":O(45,0)=1:O(10,0)=0:O(9,0)=-1:PRINTM6$:GOTO26
1100 IFNO-9THENPRINTM8$:GOTO26
1110 NO$(9)="<X>":O(45,0)=2:O(9,0)=0:O(10,0)=-1:PRINTM6$:GOTO26
1200 IFNO-11ANDNO-13ANDNO-17ANDNO-43THENPRINT"THERE IS NO WRITING THERE.":GOTO26
1210 PRINT"IT SAYS:":IFNO=11THENM$="BRING treasures HERE AND DROP THEM. SAY: SCORE":GOSUB1
1220 IFNO=13THENM$="HOLY SMOKES! A TIGER! BARBS LIKE BAN...":GOSUB1
1230 IFNO=17THENM$=R1$:GOSUB1:M$=R2$:GOSUB1:PRINT"BRE BOT MAGIC WORD: BIMBO"
1240 IFNO=43THENM$="SLIP WAR. HERACLES' THIRD LABOR...":GOSUB1
1250 GOTO26
1300 IFNO-44ANDNO-12THENPRINT"IT WON'T BREAK!":GOTO26
1301 IFNO=44THEN1305
1302 GOTO1311
1305 M$="THE BALL CLASHES INTO BITS. A HUGE PUFF OF DENSE GREY SMOKE APPEARS IN FRONT OF ME.":GOSUB1
1306 M$="OUT WALKS A TALL WIZARD AND SAYS: "+CHR$(34)+"SO YOU ARE THE EVIL MEDICINE MAN!"+CHR$(34):GOSUB1
1307 M$="WITH A PASS OF HIS HANDS, I VANISH.":GOSUB1
1311 IFNO=44THENPRINT"PRESS ANY BUTTON TO CONTINUE."
1312 IFNO=44THEN1315
1313 GOTO1320
1315 A$=INKEY$:IFA$=""THEN1315
1316 GOTO9000
1320 IFCP-51THENM$="THERE ISN'T ANYTHING HARD ENOUGH HERE.":GOTO9
1330 M$="THE BOTTLE BREAKS AND THE BROKEN GLASS VANISHES.":GOSUB1
1331 M$="SOMETHING FELL OUT! TRY 'LOOK'.":GOSUB1:IFX=-1THENZZ=ZZ-1
1340 O(12,0)=0:O(13,0)=CP:GOTO26
1400 IFNO-10ANDNO-12THENPRINTM8$:GOTO26
1405 IFNO=10THENM$="NO NEED. THE LANTERN IS ENCHANTED, AND NEVER RUNS OUT OF FUEL.":GOTO9
1410 IFCP-14THENM$="THERE IS NOTHING TO FILL IT WITH HERE!":GOTO9
1420 M$="THE WATER PASSES RIGHT THROUGH THE BOTTLE (IT MUST BE MAGIC!)":GOTO9
1500 IFNO-14THENPRINTM4$:GOTO26
1505 IFCP-3ANDCP-45THENPRINTM0$:GOTO26
1510 IFCP=3THENCP=45:GOTO1520
1511 CP=3
1520 GOTO25
1600 IFNO-15ANDNO-19ANDNO-26ANDNO-28ANDNO-36ANDNO-40ANDNO-42THENXX=0:GOTO1605
1601 XX=-1
1605 IFVB<>15THENRETURN
1610 M$="IT DOESN'T QUITE APPROVE. CLUE: BE MORE SPECIFIC.":GOTO9
1620 IFNO-44THENM$="SORRY CHARLIE. I CAN ONLY THROW THE BALL.":GOTO9
1630 IFO(44,0)<>-1THENPRINTM1$:GOTO26
1640 IFCP-29THEN1645
1641 GOTO1650
1645 M$="OKAY. POOF! A HUGE CLOUD OF GREY SMOKE EMERGES FROM THE BROKENGLASS.":GOSUB1
1646 M$="A TALL WIZARD WALKS OUT, LOOKS AROUND, AND VANISHES.":GOSUB1:O(44,0)=0:ZZ=ZZ-1:GOTO26
1650 M$="OKAY. PAVOOM! A HUGE FOG OF GREY SMOKE FILLS THE ROOM.":GOSUB1
1651 M$="A TALL FIGURE CAN BE MADE OUT. HE LOOKS AT THE MEDICINE MAN AND SAYS, "
1652 M$=M$+CHR$(34)+"AHA! I HAVE FOUND YOU, EVIL ONE!"+CHR$(34)+" THE SMOKE CLEARS AND THE TWO ARE GONE.":GOSUB1:ZZ=ZZ-1
1660 O(42,0)=0:O(44,0)=0:GOTO26
1700 GOSUB1600:IFXX=0THENPRINT"OKAY.":GOTO26
1701 IFNO-15THENM$="I'D RATHER NOT. IT MIGHT HIT ME BACK!":GOTO9
1710 IFO(29,0)<>-1THENM$="I'VE NO WEAPON, SO I'D RATHER NOT.":GOTO9
1720 O(15,0)=0:O(16,0)=CP:M$="THE DEER RUNS AWAY, BUT LEAVES SOMETHING BEHIND! TRY 'LOOK'":GOTO9
1800 IFNO-20THENPRINTM8$:GOTO1900
1801 M$="A DEEP VOICE ECHOS... "+CHR$(34)+"STOP FLICKING YOUR BIC!"+CHR$(34):GOTO9
1900 IFNO-23ANDNO-38THENPRINTM3$:GOTO26
1910 IFNO=23THENPRINT"SMAK! MMM.":O(23,0)=0:O(24,0)=-1:GOTO26
1920 PRINT"DEEEELICIOUS.":ZZ=ZZ-1:O(38,0)=0:GOTO26
2000 IFNO-23THENPRINTM8$:GOTO26
2001 M$="THE BANANA VANISHED AS I PEELED IT! (YOU MUST REMEMBER... THIS ISLAND IS enchanted)":GOSUB1
2002 O(23,0)=0:O(24,0)=-1:GOTO26
2100 GOSUB1600:IFXX=0THENPRINTM5$:GOTO26
2110 IFNO=28THEN2150
2120 M$="WE ENGAGE IN A WILD FIGHT AND WHEN THE SMOKE CLEARS I FIND MYSELF BEAT TO A PULP. I'M DEAD.":GOSUB1
2121 PRINT:PRINT"PRESS ANY KEY TO CONTINUE"
2130 IFINKEY$=""THEN2130
2131 GOTO9000
2150 M$="THE BEAR AND I ENGAGE IN A WICKED BATTLE AND AMAZINGLY, I WIN! THE BEAR IS DEAD!":GOSUB1:O(28,0)=0:GOTO26
2200 IFNO-27THENPRINTM3$:GOTO26
2210 IFO(20,0)<>-1THENPRINT"I'VE NO LIGHTER!":GOTO26
2220 PRINT"OKAY...PUFF...PUFF...PUFF":IFO(40,0)-CPTHEN26
2230 M$="THE TIGER SMELLS THE SMOKE, LOOKS AT ME WITH DISGUST, AND STROLLS AWAY.":O(40,0)=0:GOTO9
2300 IFNO-30THENPRINTM8$:GOTO26
2310 IFO(NO,0)<>-1THENPRINTM1$:GOTO26
2320 IFO(18,0)<>-1THENPRINT"I HAVE NO KEY!":GOTO26
2330 M$="OKAY. SOMETHING FALLS OUT! THE CRATE THEN DISAPPEARS! TRY 'LOOK'.":GOSUB1:IFO(30,0)=-1THENZZ=ZZ-1
2340 O(30,0)=0:O(31,0)=CP:GOTO26
2400 IFNO-30THENPRINTM8$:GOTO26
2410 IFO(NO,0)<>-1THENPRINTM1$:GOTO26
2420 PRINT"IT'S ALREADY CLOSED AND LOCKED.":GOTO26
2500 GOTO2300
2700 PRINT"OKAY..."N1$:IFLEFT$(N1$,3)<>"BIM"THEN26
2705 IFO(35,0)<>-1THENPRINT"NOTHING HAPPENED.":GOTO26
2710 M$="I AM ENGULFED IN A DENSE CLOUD OF GREY SMOKE. WHEN THE SMOKE CLEARS, I AM SOMEWHERE ELSE. TRY 'LOOK'.":GOSUB1
2720 IFCP>11ANDCP<29THENCP=31:GOTO26
2730 CP=27:GOTO26
2800 PRINT"OKAY..."N1$"!!!!!":GOTO26
2900 PRINT"I'M BEWILDERED TOO!":GOTO26
3000 GOTO300
3100 GOSUB1600:IFXX=0THENPRINTM5$:GOTO26
3101 IFNO-36ANDNO-26THENPRINTM7$:GOTO26
3110 IFNO=36THENQQ=38:GOTO3120
3111 QQ=23
3120 IFO(QQ,0)<>-1THENM$="I HAVE NOTHING IT WANTS TO EAT.":GOTO9
3130 IFNO=26THEN3160
3140 M$="THE VULTURE SWALLOWS UP THE FOOD AND FLIES AWAY.":GOSUB1:ZZ=ZZ-1:O(36,0)=0:O(38,0)=0:GOTO26
3160 M$="THE BARBARIAN EATS THE BANANA AND LEAVES. HE DROPS THE PEEL.":GOSUB1:ZZ=ZZ-1:O(23,0)=0:O(24,0)=CP:O(26,0)=0:GOTO26
3200 INPUT"DO YOU REALLY WANT TO QUIT";A$:A$=LEFT$(A$,1):IFA$="Y"THEN9000
3201 GOTO26
3300 IFCP-45THENPRINT"NOT HERE.":GOTO26
3301 CP=3:GOTO25
9000 CLS:FORI=9TOLO:IFO(I,0)=1THENFS=FS+O(I,1)
9001 NEXTI:M$="YOU SCORED"+STR$(FS)+" OUT OF A MAXIMUM OF 140.":GOSUB1
9005 IFFS=140THENM$="YOU COULD BE A WIZARD. PICK UP AN APPLICATION FORM AT YOUR LOCAL ADVENTURE MARKET.":GOSUB1:GOTO9070
9010 IFFS>130THENM$="YOU ARE AN EXPERT ADVENTURER.":GOSUB1:GOTO9070
9015 IFFS>110THENM$="AND A WISE ADVENTURER YOU ARE.":GOSUB1:GOTO9070
9020 IFFS>100THENM$="NOT BAD, IF I DO SAY SO MYSELF!":GOSUB1:GOTO9070
9025 IFFS>80THENM$="YOU COULD USE A lot OF PRACTICE.":GOSUB1:GOTO9070
9030 IFFS>60THENM$="YOU must BE NEW AT THE GAME. NO ONE COULD DO SO POORLY AFTER A FEW TIMES OF PLAYING!":GOSUB1:GOTO9070
9035 IFFS>35THENM$="YOU ARE A TERRIBLE ADVENTURE PLAYER!":GOSUB1:GOTO9070
9040 IFFS>20THENM$="HA HA HA HA HEE TEE HEE HA TA HEE HA HA HA HA!!!":GOSUB1:GOTO9070
9045 IFFS>0THENM$="YOU ACHIEVED THE LOWEST RANK NEXT TO ZERO. SUPER! SARCASTIC":GOSUB1:GOTO9070
9050 M$="A ZERO!!!!!!!! I DON'T BELIEVE IT!!!!!!!! BAAAA HAHAHAH!!!!":GOSUB1
9070 PRINT:M$=R1$:GOSUB1:M$=R2$:GOSUB1:PRINT:PRINT"PLAY AGAIN? (Y/N)";
9080 A$=INKEY$:IFA$=""THEN9090
9090 IFA$="Y"THENRUN
9100 IFA$="N"THENCLS:END
9110 GOTO9080
30000 CP=1:NP=51:LO=44:NN=44:NV=33:DIMP(NP,3),P$(NP),OB$(LO),O(46,1),NO$(NN),VB$(NV)
30010 DATA50,2,7,,,3,,1,,4,8,2,51,,,3,,6,10,,,,11,5,,,,,3,9,,,,,14,8,5,,15,,6,,,,7,13,12,12,13,13
30011 DATA18,12,9,15,,,10,,,14,16,17,16,16,17,18,19,16,13,18,18,17,17,19,20,19,20,20,23,19,,22,24,,,,25,21,,24
30012 DATA26,20,21,,,23,22,,28,,23,27,,,,28,,26,25,,,27,,,33,,,31,35,,,,36,30,,33,37,,29,34,38,32,0
30013 DATA0,39,33,30,36,,,31,,41,35,32,,,,33,,42,,34,40,,,,41,44,39,36,,,40,38,43,47,,,44,,42,40,,,43
30014 DATA0,,,0,,47,50,,42,,,46,,49,51,,,,,48,46,,1,,48,,4,0
30040 FORI=1TONP:FORJ=0TO3:READP(I,J):NEXTJ,I
30050 P$(1)="ON THE SHORE OF AN ISLAND NOT FAR FROM AUSTRALIA":P$(2)="WALKING ALONG A SANDY BEACH"
30051 P$(3)="STANDING IN AN OASIS NEAR A SANDY BEACH":P$(4)="WALKING ALONG A PATH THAT EXTENDS NORTH AND WEST"
30052 P$(5)="A PILE OF BRUSH"
30060 P$(6)="ON SPYGLASS HILL":P$(7)="THE OCEAN. I'M DEAD":P$(8)="ATOP A SAND DUNE"
30061 P$(9)="WALKING ALONG A ROCKY PATHWAY THAT EXTENDS TO THE SOUTH AND WEST":P$(10)="BESIDE A HEDGE"
30062 P$(11)="/I'M STANDING IN A SHADY SPOT NEAR A LARGE PILE OF BRUSH"
30070 P$(12)="LOST IN A DARK MARSH":P$(13)=P$(12):P$(16)=P$(12):P$(17)=P$(12):P$(18)=P$(12)
30071 P$(19)=P$(12):P$(20)=P$(12):P$(14)="BESIDE A CASCADING WATERFALL":P$(15)="ON THE EDGE OF A CLIFF"
30072 P$(21)="IN A BAMBOO FOREST. THE BAMBOO STALKS HERE ARE 6'' THICK"
30080 P$(22)="DEEP IN A BAMBOO FOREST. THE BAMBOO STALKS HERE ARE 7 OR 8'' THICK"
30081 P$(23)="IN A FOREST LOOKING SOUTH ON A GRASS HUT":P$(24)="IN A SMALL MARSH":P$(25)="WALKING ALONG A SANDY PATH"
30082 P$(26)="IN A SMALL GRASS HUT WITH EXITS TO THE NORTH AND EAST."
30090 P$(27)="IN A WET FOREST WITH A GRASS HUT TO THE WEST":P$(28)="ON THE COAST OF THE ISLAND"
30100 P$(29)="IN A STRANGE CAVE-ROOM WITH OBSCURE PATTERNS IN THE DUSTY FLOOR":P$(30)="/DEAD END"
30101 P$(31)="IN A SACRED CHAMBER DEEP WITHIN THE CAVES":P$(32)="IN AN EERIE PRISON CELL"
30102 P$(33)="IN AN ALCOVE TO A HUGE CHASM"
30110 P$(34)="CRAWLING THROUGH A TIGHT PASSAGE LEADING SOUTH AND WEST"
30111 P$(35)="IN THE PEBBLE ROOM. THE FLOOR IS MADE OF LOOSE PEBBLES"
30112 P$(36)="IN A DARK MUSTY PIT. PASSAGES LEADING OUT OF HERE ALL LOOK THE SAME, EXCEPT THE ONE GOING NORTH"
30120 P$(37)="/DEAD END":P$(38)="IN A DEEP CHASM WITH AN ALCOVE TO THE NORTH":P$(39)="IN THE ENTRY CORRIDOR"
30121 P$(40)="IN A RADIANT CAVERN. 40 FEET UP HANG OMINOUS STALACTITES. ODD, THERE ARE NO STALAGMITES"
30130 P$(40)=P$(40)+" ON THE FLOOR HERE":P$(41)="IN A WIDE VALLEY WITH A MOSSY GROWTH COVERING THE FLOOR"
30131 P$(42)="IN A MAJESTIC CHAMBER WHICH SEEMS TO SERVE AS AN ENTRANCE TO A LARGE CAVE"
30132 P$(43)="CRAWLING THROUGH A ROUGH SLOPING PASSAGE"
30140 P$(44)="IN A HIGH CAVERN. I CAN'T EVEN SEE THE CEILING. HIGH ABOVE GLOWS AN ERRIE RED LIGHT"
30141 P$(45)="AT THE TOP OF A PALM TREE. I CAN SEE A SMALL JUNGLE TO THE SOUTH. ODD, I SEE NO PASSAGE GOING TO IT"
30150 P$(46)="IN A THICKET OF TALL GRASS"
30151 P$(47)="STANDING ON A ROCKY FLAT. THERE IS A CAVE OPENING TO THE NORTH OF ME. IT LOOKS DARK IN THERE"
30152 P$(48)="IN A PATCH OF TALL REEDS":P$(49)="STANDING IN A SMALL DEPRESSION IN THE SAND"
30160 P$(50)="WALKING ALONG THE SHORE OF THE ISLAND":P$(51)="WALKING ALONG A LONG NORTH/SOUTH PATH":D$(0)="NORTH"
30162 D$(1)="EAST":D$(2)="SOUTH":D$(3)="WEST"
30170 DATA0,,49,,1,,2,,,,3,,6,,,15,8,,11,10,11,,15,,12,,17,10,18,,,,26,10,26,,28,15,28,,22,0
30171 DATA29,,,15,30,15,31,15,32,,37,10,45,,45,15,46,,46,,21,,21,10,29,,51,,16,0
30180 FORI=9TOLO:READO(I,0),O(I,1):NEXTI
30190 DATANOR,EAS,SOU,WES,N,E,S,W,<X>,LAN,SIG,BOT,NOT,TRE,DEE,ANT,BOO,KEY,WAR,LIG,PIC,COC,BAN,PEE,SPI,BAR,CIG
30191 DATABEA,POS,CRA,CHA,EME,RIN,SKU,RUB,VUL,FEA,FOO,LIQ,TIG,SKI,MAN,ROC,BAL
30200 DATAGO,GET,LOO,INV,SCO,SAV,RES,DRO,LIG,UNL,REA,BRE,FIL,CLI,KIL,THR,HIT,FLI,EAT,PEE,ATT,SMO,OPE,CLO,UNL
30201 DATALOC,SAY,YEL,HEL,TAK,FEE,QUI,JUM
30210 FORI=1TONN:READNO$(I):NEXTI:FORI=1TONV:READVB$(I):NEXTI
30220 DATAA SHINING LANTERN,AN UNLIT LANTERN,A SIGN,A GREEN BOTTLE,A SMALL NOTE,A PALM TREE,A WILD DEER
30221 DATAgolden antlers,A DUSTY BOOK,a silver key,A WICKED WARLOCK,A BIC LIGHTER,AN IRON PICK,a jewel encrusted coconut
30230 DATAA BANANA,A BANANA PEEL,rare spices,A SAVAGE BARBARIAN,cuban cigars,A WILD BEAR,A BAMBOO POST,A WOODEN CRATE
30231 DATAa golden chain,an enormous emerald,a gold ring
30240 DATAA HUMAN SKULL,a mammoth ruby,A MEAN VULTURE,a golden feather,SOME TASTY FOOD,SOME GREEN LIQUID,A FIERCE TIGER
30241 DATAa pricelss giraffe skin,AN EVIL MEDICINE MAN,A BIG ROCK WITH WRITING ON IT
30250 DATAA GLOWING GLASS BALL
30260 FORI=9TOLO:READOB$(I):NEXTI
59000 R1$="TRY SOME OTHER GREG HASSETT ADVENTURES: JOURNEY TO THE CENTER OF THE EARTH, THE HOUSE OF SEVEN GABLES"
59005 R2$="KING TUT'S TOMB, SORCERER'S CASTLE, VOYAGE TO ATLANTIS"
60000 GOTO25
60010 REM MC-10 EDITS JIM GERRIE
