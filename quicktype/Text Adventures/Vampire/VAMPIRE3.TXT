0 CLEAR1000:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 REM ** REVENGE OF THE TOOTHLESS VAMPIRE
20 REM ** AN ADVENTURE GAME FOR BBC/ELECTRON/MSX/ORIC/C64/AMSTRAD AND MC-10!
30 GOSUB 4000:GOSUB 2000
50 DIMQ$(45),S(45,4),G$(20),B(20),N$(20),N(20),V$(5):P=2
60 FORX=1TO44
63 READP$:IFP$<>"*"THENQ$(X)=Q$(X)+P$:GOTO63
65 FORY=1TO4:READS(X,Y):NEXTY,X
70 FORX=1TO20:READG$(X),B(X),N$(X):N(X)=X:NEXTX
80 CLS
90 IFP=45THEN500
100 PRINT"----------------------------------------"
101 M$="I AM:- "+Q$(P):GOSUB1
105 I$="I CAN GO:- "
110 IFS(P,1)<>.THENI$=I$+"NORTH, "
120 IFS(P,2)<>.THENI$=I$+"SOUTH, "
130 IFS(P,3)<>.THENI$=I$+"EAST, "
140 IFS(P,4)<>.THENI$=I$+"WEST, "
180 IFP=7THENI$=I$+"DOWN, "
190 M$=LEFT$(I$,LEN(I$)-2)+"":GOSUB1
200 E=.:FORT=1TO20:PP=.:IFB(T)=PTHENPP=1
210 IFPP=1THEN230
220 NEXTT:GOTO250
230 IFE=.THENM$="I CAN SEE:- "+G$(T):GOSUB1
240 E=E+1:GOTO220
250 INPUT"WHAT SHALL I DO NOW";Z$
260 CLS:B$=LEFT$(Z$,2):C$=LEFT$(Z$,3):D$=LEFT$(Z$,4)
270 IFB$="N"ANDS(P,1)>.THENP=S(P,1):GOTO90
280 IFB$="S"ANDS(P,2)>.THENP=S(P,2):GOTO90
290 IFB$="E"ANDS(P,3)>.THENP=S(P,3):GOTO90
300 IFB$="W"ANDS(P,4)>.THENP=S(P,4):GOTO90
310 IFB$="N"ORB$="S"ORB$="E"ORB$="W"THENPRINT"I CAN'T GO THAT WAY.":GOSUB1:GOTO90
320 IFC$="CLI"ORB$="UP"THENGOSUB670
330 IFC$="DOW"THENGOSUB710
340 IFC$="IN"THENGOSUB800
350 IFC$="GET"ORC$="TAK"THENGOSUB830
360 IFC$="SCO"THENM$="THIS ISN'T THAT KIND OF GAME!":GOSUB1:GOSUB1
370 IFC$="PRA"THENM$="THAT MADE ME FEEL GOOD.":GOSUB1
380 IFC$="INV"THENGOSUB1080
390 IFC$="SPR"THENGOSUB1520
400 IFC$="HEL"THENM$="I HAVEN'T A CLUE!":GOSUB1:GOSUB1
410 IFC$="GIV"ORC$="DRO"ORC$="LEA"THENGOSUB1120
420 IFC$="WAI"THENGOSUB1280
430 IFC$="PUL"THENGOSUB1330
440 IFC$="WEA"THENGOSUB1360
450 IFC$="REM"ANDAI=1THENM$="I CAN'T GET THEM OFF MY HANDS.":GOSUB1:GOTO90
460 IFC$="REM"THENM$="DON'T BE ABSURD...":GOSUB1
470 IFC$="HIT"ORC$="SMA"THENGOSUB1410
480 IFC$="UNL"THENGOSUB1470
490 IFP<>45THEN90
500 IFAD<1THENM$="I JUST REMEMBERED. I FORGOT MY TEETH. IRUN BACK!":GOSUB1:P=2:GOTO90
510 PRINT
511 PRINT"       W E L L   D O N E"
512 PRINT"YOU HAVE HELPED POOR OLD CEDRIC"
513 PRINT"    TO SOLVE HIS ADVENTURE!"
514 GOTO5000
520 END
530 DATA"IN A DENTIST'S SURGERY. THERE'S A LARGE CHAIR IN THE MIDDLE",*,,,2,.
531 DATA"IN A DINGY WAITING ROOM. A PILE OF OLD  MAGAZINES ARE PILED ON "
533 DATA"A CHAIR IN THE   CORNER",*,,9,3,1
535 DATA"A SMALL CORRIDOR",*,,,,2
540 DATA"IN A DISUSED SURGERY. A TATTY DENTISTS  CHAIR IS IN ONE CORNER",*,,,5,3
541 DATA"IN A NARROW PASSAGE",*,,6,,4
543 DATA"AT THE BOTTOM OF A STEEP FLIGHT OF STEPS. I CAN GO UP THE STEPS",*,5,,,.
545 DATA"IN A SMALL SPARSELY FURNISHED ROOM.     STEPS LEAD DOWN",*,,,,.
550 DATA"IN AN OVERGROWN GARDEN",*,,,,9,"IN A DOORWAY. A PLAQUE "
555 DATA"ON THE WALL READS- 'DR MORRIS...DENTIST'",*,2,15,8,.
560 DATA"BY A WELL STOCKED BAR",*,,,,11,"IN AN ELEGANT CAFE",*,12,13,10,.
561 DATA"BY A PILE OF TABLES",*,,11,,,"OUTSIDE A SMALL CAFE. "
563 DATA"THERE IS A BOUNCERON THE DOOR",*,,,14,.
565 DATA"ON THE PAVEMENT, AT THE SIDE OF A MAIN  ROAD",*,,,15,13
570 DATA"BY A PELICAN CROSSING. THE TRAFFIC IS   VERY HEAVY HERE",*,9,,,14
571 DATA"IN THE TOWN CENTRE. HEAVY TRAFFIC STOPS ME CROSSING THE ROAD",*,,21,17,.
573 DATA"OUTSIDE A GUNSMITHS. IT'S CLOSED",*,,22,,16
575 DATA"AT THE TOP OF A STEEP CLIFF",*,,,19,.
580 DATA"ON A BRACKEN COVERED HILLSIDE",*,,24,20,18,"ON A SMALL PLATEAU",*,,,,19
581 DATA"OUTSIDE THE VILLAGE BLACKSMITHS. IT'S   LOCKED",*,16,,22,.
583 DATA"BY THE SIDE OF A WIDE RIVER. THERE'S A  DRAWBRIDGE HERE",*,17,,,21
590 DATA"ON THE BANKS OF A RIVER. THERE'S A      DRAWBRIDGE HERE",*,,28,,22
591 DATA"IN A THICK FOG",*,19,24,25,24,"BY A MAGNIFICENT ALTAR",*,,,26,.
593 DATA"IN AN EERIE CRYPT",*,,31,27,25,"AT A MAGNIFICENT TEMPLE",*,,,28,26
600 DATA"WALKING BETWEEN TWO ROWS OF TALL PLANTS.THE PLANTS LOOK LIKE TRIFFIDS",*
601 DATA23,,,27,"ON A MISTY MOUNTAIN TOP",*,24,,30,,"OUTSIDE A STRANGE "
603 DATA"CASTLE. A SOLDIER IS  TO BE SEEN ON THE BATTLEMENTS",*,,,,29
610 DATA"AT THE TOP OF SOME STEPS",*,26,34,,,"AT THE BOTTOM OF STEPS",*,,35,,,
611 DATA"IN A GLOOMY COURTYARD.PEASANTS ARE PUSH-ING COFFINS AROUND",*,30,38,,.
613 DATA,,"IN AN ANTECHAMBER",*,31,,,.
620 DATA"IN A DARK CAVERN. BLOOD DRIPS DOWN THE  COBWEB COVERED WALLS",*,32,,,.
630 DATA"AT THE BOTTOM OF THE WEST TOWER",*,,,37,,"IN A QUADRANGLE. THERE"
631 DATA" ARE VULTURES     OVERHEAD",*,,42,38,36
633 DATA"IN A PASSAGE. THE WALLS ARE LINED WITH  ANCIENT SEPULCRES",*,33,43,39,37
635 DATA"IN A PASSAGE. THERE ARE TORCHES ON THE  WALLS",*,,,40,38
640 DATA"BY A LARGE OAK DOOR. A WREATH OF GARLIC IS FASTENED TO THE DOOR",*,,,,39
641 DATA"IN A LARGE BEDROOM.THERE'S A FOUR POSTER BED IN THE CORNER",*,,,42,.
643 DATA"IN A LIBRARY. THERE'S A COAT OF ARMS ON THE WALL",*,37,,43,41
650 DATA"BY A LARGE TOMB",*,38,,,42,"IN A SECRET CHAMBER",*,42,,,.
660 REM
670 IFP=6THENP=7:RETURN
680 IFP=32THENP=31:RETURN
690 GOTO730
700 REM
710 IFP=7THENP=6:RETURN
720 IFP=31THENP=32:RETURN
730 M$="I CAN'T DO THAT HERE!":RETURN
740 DATA"AN OLD COPY OF THE 'DENTIST'S GAZETTE'",2,GAZETTE
745 DATA"A GLASS OF 'GULL'S BLOOD' WINE",10,WINE,"A CLOVE OF GARLIC",8,GARLIC
750 DATA"A GOLDEN CANDLESTICK",25,CANDLE,"A CRUCIFIX",35,CRUCIFIX
755 DATA"A SET OF FALSE TEETH",1,TEETH
760 DATA"A PAIR OF RUBBER GLOVES",7,GLOVES,"A LARGE METAL LEVER",22,LEVER
765 DATA"A LARGE ROCK",18,ROCK,"A LARGE DANCING SKELETON WHICH I CAN'T PASS"
767 DATA40,SKELETON
770 DATA"AN ANGRY DENTIST",3,DENTIST,"A GIANT LIZARD",42,LIZARD
771 DATA"A DISCO-TICKET",5,TICKET
780 DATA"A JAR OF SUPER-LIZARD REPELLENT",12,REPELLENT
781 DATA"A LARGE SILVER KEY",4,KEY
785 DATA"A PRIEST HOLDING A GIANT CRUCIFIX",30,PRIEST
790 DATA"A LARGE BIBLE",35,BIBLE,"A JAR OF JAM",11,JAM,"A DRILL",4,DRILL
795 DATA"A PAINT POT",37,POT"A PEASANT",37,PEASANT
800 IFP=13ANDAA=1THENP=11:RETURN
810 IFP=13THENM$="THE BOUNCER ASKS 'WHERE'S YOUR TICKET?'":GOSUB1:RETURN
820 M$="I CAN'T DO THAT YET":GOSUB1:RETURN
830 GOSUB1030:IFL<1THENRETURN
840 E=.:FORH=1TO20:IFB(H)=PANDB(N(R))=PTHENE=1:PRINT"OK."
850 NEXT:IFE=.THENRETURN
860 IFR=13THENAA=1
870 IFR=1THENAB=1
880 IFR=3THENM$="VAMPIRES CAN'T CARRY GARLIC!":GOSUB1:RETURN
890 IFR=4THENM$="A SECRET PANEL MOVES ASIDE AND I WALK THROUGH IT!":GOSUB1:P=24
900 IFR=5THENM$="I CAN'T GO NEAR A CRUCIFIX!":GOSUB1:RETURN
910 IFR=8ORR=10ORR=12ORR=11ORR=16THENPRINT"DON'T BE ABSURD!":RETURN
920 IFR=1THENAC=1
930 IFR=6THENAD=1
940 IFR=7THENAE=1
950 IFR=9THENAF=1
960 IFR=14THENAG=1
970 IFR=15THENAH=1
980 IFR=17THENAJ=1
990 E=.:FORD=1TO5
1000 IFV$(D)=""THENV$(D)=G$(N(R)):E=1:D=6
1010 NEXT:IFE=.THENPRINT"MY HANDS ARE FULL!":RETURN
1020 B(N(R))=.:RETURN
1030 L$="":FORH=1TOLEN(Z$)
1040 IFMID$(Z$,H,1)=" "THENL$=RIGHT$(Z$,(LEN(Z$)-H)):H=80
1050 NEXT:R=.:L=.:IFLEN(L$)<2THENRETURN
1060 FORH=1TO20:IFLEFT$(N$(H),LEN(L$))=L$THENL=1:R=H
1070 NEXT:RETURN
1080 E=.:PRINT"I'M CARRYING:-":M$=TX$:GOSUB1:FORH=1TO5
1090 IFV$(H)<>""THENM$=V$(H):GOSUB1:E=1
1100 NEXT:IFE=.THENPRINT"NOTHING AT ALL!"
1110 RETURN
1120 GOSUB1030:IFL<1THENM$="I CAN'T SEE A "+L$:GOSUB1:PRINT:RETURN
1130 E=.:FORD=1TO5
1140 IFV$(D)=G$(N(R))THENV$(D)="":E=1:PRINT"OK."
1150 NEXT:IFE=.THENM$="I'M NOT CARRYING A "+L$:GOSUB1:RETURN
1160 B(N(R))=P
1170 IFP=3ANDR=1THENS(3,3)=4:M$="THE DENTIST THANKS ME AND LETS ME PASS.":GOSUB1
1171 B(1)=.:B(11)=.
1180 IFR=1THENAB=.
1190 IFR=2THENAC=.
1200 IFR=6THENAD=.
1210 IFR=7THENAE=.
1220 IFR=13THENAA=.
1230 IFR=14THENAG=.
1240 IFP=30ANDR=17ANDC$="GIV"THENM$="THE PRIEST THANKS ME AND LETS ME PASS.":GOSUB1
1245 IFP=30ANDR=17ANDC$="GIV"THENG$(16)="A SMILING PRIEST":G$(17)="":S(30,2)=33
1250 IFR=15THENAH=.
1260 IFR=17THENAJ=.
1270 RETURN
1280 PRINT"OK.":FORX=1TO2500:NEXT
1290 IFP=15THENM$="THE LIGHTS CHANGE AND I CROSS THE ROAD.":GOSUB1:P=16:RETURN
1300 IFP=16THENM$="THE LIGHTS CHANGE AND I CROSS THE ROAD.":GOSUB1:P=15:RETURN
1310 IFP=24THENM$="THE FOG LIFTS.":GOSUB1
1315 IFP=24THENQ$(24)="ON A MISTY HILLSIDE. A NARROW PATH LEADS SOUTH":GOSUB1
1317 IFP=24THENS(24,2)=29:S(24,3)=25:S(24,4)=.
1320 RETURN
1330 IFP<>22THENM$="NOT HERE!!":GOSUB1:RETURN
1340 IFAI<>1THENM$="I GET AN ELECTRIC SHOCK!!":GOSUB1:RETURN
1350 M$="THE DRAWBRIDGE COMES DOWN!!":GOSUB1:S(22,3)=23:RETURN
1360 IFAE<>1THENM$="I HAVEN'T GOT ANYTHING TO WEAR!!":GOSUB1:RETURN
1370 IFAI=1THENM$="I'M ALREADY WEARING THEM!!":GOSUB1:RETURN
1380 AI=1:FORX=1TO5:IFV$(X)=G$(7)THENV$(X)=""
1390 NEXTX:X$="A PAIR OF RUBBER GLOVES... WORN!!"
1400 PRINT"OK.":RETURN
1410 IFP<>40THENM$="I CAN'T DO THAT HERE!!":GOSUB1:RETURN
1420 IFAF<>1THENM$="I HAVE NOTHING TO DO THAT WITH!!":GOSUB1:RETURN
1430 M$="THE SKELETON FALLS IN A PILE OF BONES!!":GOSUB1
1440 FORX=1TO5:IFV$(X)=G$(9)THENV$(X)=""
1450 NEXT
1460 AF=3:G$(10)="A PILE OF BONES":RETURN
1470 IFP<>40THENM$="DON'T BE RIDICULOUS!!":GOSUB1:RETURN
1480 IFAF<3THENM$="I CAN'T GET PAST THE SKELETON!!":GOSUB1:RETURN
1490 IFAH<1THENM$="I HAVEN'T GOT THE KEY!":GOSUB1:RETURN
1500 M$="I OPEN THE DOOR":GOSUB1:Q$(40)="BY AN OPEN DOOR":S(40,2)=45
1510 RETURN
1520 IFAG<1THENM$="I HAVEN'T GOT ANY REPELLENT!!":GOSUB1:RETURN
1530 IFP<42THENM$="THERE'S NOT MUCH POINT IN THAT HERE!!":GOSUB1:RETURN
1540 M$="I SPRAY THE REPELLENT AND IT MOVES ASIDE.":GOSUB1:S(42,2)=44
1550 RETURN
2000 CLS:PRINT"DO YOU WANT INSTRUCTIONS?"
2010 INPUTI$
2020 IFLEFT$(I$,1)="Y"THENGOSUB3000
2030 RETURN
3000 CLS:PRINT"********************************";
3010 PRINT"revenge of the toothless vampire";
3020 PRINT"********************************";
3030 M$="YOU MUST HELP POOR OLD CEDRIC, THE TOOTHLESS VAMPIRE, COLLECT HIS FALSE "
3050 M$=M$+"TEETH AND GET OUT OF WIERDSVILLE.":GOSUB1
3060 M$="BUT HE MUST MAKE THE IRATE DENTIST HAPPY, WHO HAS LOST HIS FAVOURITE READING MATERIAL, AND AN AGITATED PRIEST WHO "
3090 M$=M$+"HAS LOST HIS BIBLE.":GOSUB1:M$="CEDRIC MAY ALSO FIND SOME USEFUL ITEMS IN THE CAFE, BUT "
3111 M$=M$+"THE BOUNCER WON'T LET HIM IN.":GOSUB1:GOSUB3200:CLS
3120 M$="COMMANDS ARE ENTERED USING THE STANDARD VERB AND NOUN ENTRIES SYSTEM.":GOSUB1
3130 M$="SOME USEFUL COMMANDS ARE CLIMB, UP, DOWN, ON, GET/TAKE, SCORE, PRAISE, "
3160 M$=M$+"INVENTORY, SPRAY, HELP, GIVE/DROP/LEAVE AND UNLOCK":GOSUB1:PRINT
3200 PRINT"   PRESS ANY KEY TO CONTINUE";
3210 I$=INKEY$:IFI$=""THEN3210
3215 RETURN
3220 PRINT"...PLEASE WAIT A MOMENT":GOSUB1
3230 RETURN
4000 CLS:PRINT"********************************";
4010 PRINT"revenge of the toothless vampire";
4020 PRINT"********************************"
4025 PRINT"      (C) JIMMY FANG 1986"
4026 PRINT"   CONVERTED FROM THE BBC BY"
4027 PRINT"       BY STEVE (C) 1985"
4028 PRINT"      (C) LEO WANKERSOFT"
4029 PRINT"FOREST HILL, VICTORIA, AUSTRALIA";
4030 PRINT"       VIC VERSION ENHANCED"
4040 PRINT"        BY DOROTHY MILLARD"
4045 PRINT
4046 PRINT"  MC-10 VERSION BY JIM GERRIE":PRINT
4050 PRINT"   PRESS ANY KEY TO CONTINUE"
4060 I$=INKEY$:IFI$=""THEN4060
4070 PRINT
4080 RETURN
5000 PRINT"WANT TO PLAY AGAIN? (Y/N)";
5010 INPUTI$
5020 IFLEFT$(I$,1)="Y"THEN RUN
5030 PRINT"...OK THEN, BYE"
5040 END
