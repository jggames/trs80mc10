0 CLEAR500:CLS:DIMCC,C1,M$:GOTO230
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
70 RESTORE:FORCC=0TOZZ:READM$:NEXTCC:GOSUB1:RETURN
80 I$=INKEY$:ON1-(I$<>"")GOTO80:RETURN
100 DATA " ",NOTHING HAPPENS,CHOP IT DOWN!,BOY THAT REALLY HIT THE SPOT!,DRAGON SMELLS SOMETHING - AWAKENS - ATTACKS ME!
105 DATA LOCK SHATTERS.,I CAN'T. IT'S LOCKED.,TIMBER... SOMETHING FELL FROM THE TREETOP AND VANISHED!
108 DATA TIMBER...,LAMP IS OFF,FLAMELESS LAMP IS ON.,I'M BIT BY A SPIDER.,MY CHIGGER BITES ARE NOW INFECTED.
110 DATA THE BITES HAVE ROTTED MY WHOLE BODY.,THE BEAR EATS THE HONEY AND FALLS ASLEEP.
111 DATA BEES STING ME,I'VE NO CONTAINER.,THE BEES ALL SUFFOCATED.
112 DATA SOMETHING I'M HOLDING VIBRATED AND...,NOTHING TO LIGHT IT WITH.
113 DATA GAS BLADDER BLEW UP.,IN MY HANDS!
114 DATA GAS NEEDS TO BE CONTAINED BEFORE IT WILL BURN.,GAS DISSIPATES.
115 DATA THAT WON'T IGNITE.,HOW?  JUMP?
116 DATA THE BEAR WON'T LET ME.,DON'T WASTE *HONEY*. GET MAD INSTEAD. DAM LAVA!
117 DATA BEES MADDEN THE BEAR. BEAR THEN ATTACKS ME!,IT SOAKS INTO THE GROUND.
118 DATA IN ONE WORD TELL ME WHAT.,OH NO...BEAR DODGES...CRASH!,IT'S HEAVY!
119 DATA SOMETHING'S TOO HEAVY. I FALL.,TO STOP GAME SAY -QUIT-
120 DATA THE MIRROR HITS THE FLOOR AND SHATTERS INTO A MILLION PIECES.
121 DATA "MIRROR LANDS SOFTLY ON THE RUG - LIGHTS UP AND SAYS:"
122 DATA YOU LOST *ALL* TREASURES.,NOT CARRYING THE AXE. TAKE INVENTORY!
123 DATA IT DOESN'T BOTHER HIM AT ALL.,THE MUD DRIED UP AND FELL OFF.
124 DATA THE BEAR GOT STARTLED AND FELL OFF THE LEDGE!
125 DATA *DRAGON STINGS* AND FADES. I DON'T GET IT - HOPE YOU DO.
126 DATA THE BEES ATTACK THE DRAGON WHICH GETS UP AND FLIES AWAY...
127 DATA MAGIC OIL ATTRACTS MAGIC LAMP. LAMP IS NOW FULL.
128 DATA I'M BIT BY CHIGGERS.
129 DATA THERE'S SOMETHING THERE ALL RIGHT!  MAYBE I SHOULD GO THERE?
130 DATA MAYBE IF I THREW SOMETHING?...,TOO DRY. FISH DIE.
131 DATA A GLOWING GENIE APPEARS - DROPS SOMETHING - THEN VANISHES.
132 DATA A GENIE APPEARS - SAYS 'BOY YOU'RE SELFISH' - TAKES SOMETHING - THEN VANISHES!
133 DATA NO! IT'S TOO HOT.,NOT HERE.,TRY THE SWAMP,USE ONE WORD.
134 DATA "TRY:  LOOK, JUMP, SWIM, CLIMB, THROW, FIND, GO, TAKE, INVENTORY, SCORE."
135 DATA ONLY 3 THINGS WILL WAKE THE DRAGON. ONE OF THEM IS DANGEROUS!
136 DATA "IF YOU NEED A HINT ON SOMETHING, TRY 'HELP'."
137 DATA READ THE SIGN IN THE MEADOW!,YOU MAY NEED MAGIC WORDS HERE.
138 DATA A VOICE BOOMS OUT ...,PLEASE LEAVE IT ALONE!,CAN ONLY THROW AXE.
139 DATA MEDICINE IS GOOD FOR BITES.,I DON'T KNOW WHERE IT IS.
140 DATA TREASURES HAVE AN * IN THEIR NAME. SAY 'SCORE',BLOW IT UP.
141 DATA FISH ESCAPE BACK TO THE LAKE.
142 DATA " "," "," "," "
230 IL=60:CL=151:NL=59:RL=33:MX=5:AR=11:TT=13:LN=3:LT=125:ML=71:TR=3
235 DIMNV(1),C(7,151):DIMNV$(59,1),IA$(60),IA(64),RS$(33),RM(33,5),NT$(1),I2(60):CC=RND(-(PEEK(9)*256+PEEK(10)))
250 X=Y=Z:K=R=V:N=LL=F:TP$=K$:W=IP=P:GOSUB290:GOSUB2210:GOTO350
290 CLS:PRINT" W E L C O M E   T O ":PRINT@64," A D V E N T U R E - 1 "
295 PRINT@128,"HAPPY ADVENTURING!":PRINT" < INITIALIZING > ":RETURN
350 R=AR:LX=LT:DF=0:SF=0:PRINT:PRINT"USE SAVED GAME (Y OR N)? ";:GOSUB80:K$=I$
360 PRINT:IFLEFT$(K$,1)<>"Y"THEN410
370 PRINT"PRESS PLAY"
380 CLOAD*IA,"ADVENDAT"
390 SF=IA(61):LX=IA(62):DF=IA(63):R=IA(64)
410 CLS:GOSUB600:GOTO490
430 INPUT"TELL ME WHAT TO DO";TP$:PRINT:GOSUB500
440 IFFTHENPRINT"YOU USE WORD(S) I DON'T KNOW!":GOTO430
450 GOSUB760:IFIA(9)=-1THENLX=LX-1:GOTO470
460 GOTO490
470 IFLX<0THENPRINT"LIGHT HAS RUN OUT!":IA(9)=0:GOTO490
480 IFLX<25THENM$="LIGHT RUNS OUT IN"+CHR$(LX)+" TURNS!":GOSUB1
490 NV(0)=0:GOSUB760:GOTO430
500 K=0:NT$(0)="":NT$(1)=""
510 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1:GOTO530
520 NT$(K)=LEFT$(NT$(K)+K$,LN)
530 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN590
540 FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
550 IFX=1ANDY<7THEN K$=LEFT$(K$,LN)
560 IFNT$(X)=K$THEN NV(X)=Y:GOTO580
570 NEXT Y:GOTO590
580 IFLEFT$(NV$(NV(X),X),1)="*" THEN NV(X)=NV(X)-1:GOTO580
590 NEXTX:F=NV(0)<1 OR LEN(NT$(1))>0 AND NV(1)<1:RETURN
600 IFDFTHENIFIA(9)<>-1ANDIA(9)<>RTHENM$="I CAN'T SEE. IT'S TOO DARK!":GOSUB1:RETURN
610 K=-1:IFLEFT$(RS$(R),1)="*" THEN M$=MID$(RS$(R),2):GOSUB1:GOTO630
620 M$="I'M IN A "+RS$(R):GOSUB1
630 FORZ=0TOIL:IFK THEN IF IA(Z)=R THENPRINT:PRINT"VISIBLE ITEMS HERE:":K=0
640 GOTO690
650 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/" THEN RETURN
660 FOR W=LEN(TP$)-1 TO 1 STEP -1:IFMID$(TP$,W,1)="/" THEN TP$=LEFT$(TP$,W-1):RETURN
670 NEXTW
680 RETURN
690 IFIA(Z)<>RTHEN720
700 GOSUB650
710 M$=TP$+". ":GOSUB1
720 NEXT:PRINT
730 K=-1:FOR Z=0 TO 5:IFK THEN IF RM(R,Z)<>0 THEN PRINT"OBVIOUS EXITS: ":K=0
740 IF RM(R,Z)<>0 THEN PRINT NV$(Z+1,1);" ";
750 NEXT:PRINT:RETURN
760 F2=-1:F=-1:F3=0:IFNV(0)=1 AND NV(1)<7 THEN 1240
770 FOR X=0 TO CL:V=INT(C(0,X)/150):IFNV(0)=0 THEN IF V<>0 THEN RETURN
780 IF NV(0)<>V THEN 830
790 N=C(0,X)-V*150
800 IFNV(0)=0THENF=0:GOTO820
810 GOTO840
820 IFINT(RND(0)*100+1)<=NTHEN850
830 NEXTX:GOTO1760
840 IF N<>NV(1)ANDN<>0THEN830
850 F2=-1:F=0:F3=-1:FORY=1TO5:ONYGOTO860,870,880,890,900
860 W=C(1,X):GOTO910
870 W=C(2,X):GOTO910
880 W=C(3,X):GOTO910
890 W=C(4,X):GOTO910
900 W=C(5,X):GOTO910
910 LL=INT(W/20):K=W-LL*20:F1=-1
920 ONK+1GOTO1110,990,1010,1030,1050,1060,1070,1080,1090,1100,950,970
930 IFK<12THEN950
940 ONK-11GOTO1000,1020,1040
950 F1=-1:FOR Z=0 TO IL:IFIA(Z)=-1THEN1110
960 NEXT:F1=0:GOTO1110
970 F1=0:FOR Z=0 TO IL:IFIA(Z)=-1THEN1110
980 NEXT:F1=-1:GOTO1110
990 F1=IA(LL)=-1:GOTO1110
1000 F1=IA(LL)<>-1 AND IA(LL)<>R:GOTO1110
1010 F1=IA(LL)=R:GOTO1110
1020 F1=IA(LL)<>0:GOTO1110
1030 F1=IA(LL)=R OR IA(LL)=-1:GOTO1110
1040 F1=IA(LL)=0:GOTO1110
1050 F1=R=LL:GOTO1110
1060 F1=IA(LL)<>R:GOTO1110
1070 F1=IA(LL)<>-1:GOTO1110
1080 F1=R<>LL:GOTO1110
1090 F1=SFANDINT(2^LL+.5):F1=F1<>0:GOTO1110
1100 F1=SFANDINT(2^LL+.5):F1=F1=0
1110 F2=F2ANDF1:IFF2THENNEXTY:GOTO1130
1120 GOTO830
1130 IP=0:FORY=1TO4:K=INT((Y-1)/2+6):ONYGOTO1140,1150,1160,1170
1140 AC=INT(C(6,X)/150):GOTO1180
1150 AC=C(6,X)-INT(C(6,X)/150)*150:GOTO1180
1160 AC=INT(C(7,X)/150):GOTO1180
1170 AC=C(7,X)-INT(C(7,X)/150)*150
1180 IFAC>101THEN1230
1190 IFAC=0THEN1720
1200 IFAC<52THENZZ=AC:GOSUB70:GOTO1720
1210 ONAC-51GOTO1340,1380,1450,1470,1480,1490,1500,1470,1520,1540,1550
1220 ONAC-62GOTO1560,1580,1590,1640,1680,1690,1700,1710,1390,1460
1230 ZZ=AC-50:GOSUB70:GOTO1720
1240 L=DF:IFLTHENL=DFANDIA(9)<>RANDIA(9)<>-1:GOTO1260
1250 GOTO1270
1260 IFLTHENM$="DANGEROUS TO MOVE IN THE DARK!":GOSUB1
1270 IFNV(1)<1THENPRINT"GIVE ME A DIRECTION TOO.":GOTO1800
1280 K=RM(R,NV(1)-1)
1290 IFK>=1 THEN 1320
1300 IFLTHENM$="I FELL DOWN AND BROKE MY NECK.":GOSUB1:K=RL:DF=0:GOTO1320
1310 M$="I CAN'T GO IN THAT DIRECTION.":GOSUB1:GOTO1800
1320 IFNOT(L)THENCLS
1330 R=K:GOSUB600:GOTO1800
1340 L=0:FORZ=1TOIL:IFIA(Z)=-1THENL=L+1
1350 NEXTZ
1360 IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO1730
1370 GOSUB1810:IA(P)=-1:GOTO1720
1380 GOSUB1810:IA(P)=R:GOTO1720
1390 PRINT"PRESS PLAY & RECORD & HIT return";:GOSUB80
1410 IA(61)=SF:IA(62)=LX:IA(63)=DF:IA(64)=R
1420 CSAVE*IA,"ADVENDAT"
1440 PRINT:GOTO1720
1450 GOSUB1810:R=P:GOTO1720
1460 GOSUB1810:L=P:GOSUB1810:Z=IA(P):IA(P)=IA(L):IA(L)=Z:GOTO1720
1470 GOSUB1810:IA(P)=0:GOTO1720
1480 DF=-1:GOTO1720
1490 DF=0:GOTO1720
1500 GOSUB1810
1510 SF=INT(.5+2^P)ORSF:GOTO1720
1520 GOSUB1810
1530 SF=SFANDNOT(INT(.5+2^P)):GOTO1720
1540 PRINT"I'M DEAD...":R=RL:DF=0:GOTO1580
1550 GOSUB1810:L=P:GOSUB1810:IA(L)=P:GOTO1720
1560 PRINT"THE GAME IS NOW OVER.":PRINT"ANOTHER GAME? ";:GOSUB80:IFLEFT$(I$,1)="N"THENEND
1570 FORX=0TOIL:IA(X)=I2(X):NEXT:GOTO350
1580 GOSUB600:GOTO1720
1590 L=0:FORZ=1TOIL:IFIA(Z)=TRTHENIFLEFT$(IA$(Z),1)="*"THENL=L+1
1600 NEXTZ:M$="I'VE STORED"+STR$(L)+" TREASURES. ON A SCALE OF 0 TO 100, THAT RATES A "+STR$(INT(L/TT*100))+".":GOSUB1
1620 IFL=TTTHENPRINT"WELL DONE. ":GOTO1560
1630 GOTO1720
1640 PRINT"I'M CARRYING:":K$="NOTHING!":FORZ=0TOIL:IFIA(Z)<>-1THEN1670
1650 GOSUB650
1660 M$=TP$+".":GOSUB1:K$=""
1670 NEXT:PRINTK$:GOTO1720
1680 P=0:GOTO1510
1690 P=0:GOTO1530
1700 LX=LT:IA(9)=-1:GOTO1720
1710 CLS:GOTO1720
1720 NEXT Y
1730 IFNV(0)<>0THEN1760
1740 GOTO830
1760 IFNV(0)=0THEN1800
1770 GOSUB1900
1780 IFFTHENM$="I DON'T UNDERSTAND.":GOSUB1:GOTO1800
1790 IFNOT(F2)THENPRINT"I CAN'T DO THAT YET.":GOTO1800
1800 RETURN
1810 IP=IP+1
1820 ONIPGOTO1830,1840,1850,1860,1870
1830 W=C(1,X):GOTO1880
1840 W=C(2,X):GOTO1880
1850 W=C(3,X):GOTO1880
1860 W=C(4,X):GOTO1880
1870 W=C(5,X):GOTO1880
1880 P=INT(W/20):M=W-P*20:IFM<>0 THEN 1810
1890 RETURN
1900 IFNV(0)<>10ANDNV(0)<>18ORF3THEN2120
1910 IFNV(1)=0THENPRINT"WHAT?":GOTO2060
1920 IFNV(0)<>10THEN1950
1930 L=0:FORZ=0TOIL:IFIA(Z)=-1THENL=L+1
1940 NEXT:IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO2060
1950 K=0:FOR X=0 TO IL:IFRIGHT$(IA$(X),1)<>"/" THEN 2070
1960 LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FOR Y=LL TO 2 STEP -1
1970 IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO2070
1980 TP$=LEFT$(MID$(TP$,Y+1),LN)
1990 IFTP$<>NV$(NV(1),1)THEN2070
2000 IFNV(0)=10THEN2030
2010 IFIA(X)<>-1THENK=1:GOTO2070
2020 IA(X)=R:K=3:GOTO2050
2030 IFIA(X)<>RTHENK=2:GOTO2070
2040 IA(X)=-1:K=3
2050 PRINT"OK, "
2060 F=0:RETURN
2070 NEXTX
2080 IFK=1THENPRINT"I'M NOT CARRYING IT!"
2090 IFK=2THENPRINT"I DON'T SEE IT HERE."
2100 IFK=0THENIFNOT(F3)THEN M$="IT'S BEYOND MY POWER TO DO THAT.":GOSUB1:F=0
2110 IFK<>0THENF=0
2120 RETURN
2210 PRINT"LOAD 'ADVENT2'":CLOAD*C,"ADVENT2":FORX=0TOML:READI$:NEXTX
2240 FORX=0TONLSTEP10:FORY=0TO1
2250 READ NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y)
2260 READ NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y):NEXT Y,X
2270 FORX=0TORL:READRM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT
2290 FORX=0TOIL:READIA$(X),IA(X):I2(X)=IA(X):NEXT X:RETURN
3840 DATA AUT,GO,*ENT,*RUN,*WAL,*CLI,JUM,BEA,CHO,*CUT
3850 DATA ANY,NORTH,SOUTH,EAST,WEST,UP,DOWN,NET,FIS,AWA
3860 DATA TAK,*GET,*PIC,*CAT,LIG,*TUR,*LAM,*BUR,DRO,*REL
3870 DATA MIR,AXE,AXE,WAT,BOT,GAM,HOL,LAM,*ON,OFF
3880 DATA *SPI,*LEA,STO,AWA,THR,TOS,QUI,SWI,RUB,LOO
3890 DATA DOO,MUD,*MED,BEE,SCO,GAS,FLI,EGG,OIL,*SLI
3900 DATA *SHO,*SEE,DRA,SCO,INV,SAV,WAK,UNL,REA,OPE
3910 DATA KEY,HEL,BUN,INV,LED,THR,CRO,BRI,BEA,DRA
3920 DATA ATT,*KIL,DRI,*GAT,BUN,FIN,*LOC,HEL,SAY,WIN
3930 DATA RUG,RUB,HON,FRU,OX,RIN,CHI,*BIT,BRA,SIG
3940 DATA DOO,SCR,*YEL,*HOL," "," "," "," "," "," "
3950 DATA BLA,WEB,*WRI,SWA,LAV,ARO,HAL,TRE,*STU,FIR
3970 DATA 0,7,10,1,0,24," "
3980 DATA 23,1,1,25,0,0,"DISMAL SWAMP."
3990 DATA 0,0,0,0,0,1,"*I'M IN THE TOP OF A TALL CYPRESS TREE."
4000 DATA 1,1,1,1,1,4,"LARGE HOLLOW DAMP STUMP IN THE SWAMP."
4010 DATA 0,0,0,0,3,5,"ROOT CHAMBER UNDER THE STUMP."
4020 DATA 0,0,0,0,4,0,"SEMI-DARK HOLE BY THE ROOT CHAMBER."
4030 DATA 0,0,0,0,5,7,"LONG DOWN-SLOPING HALL."
4040 DATA 8,9,0,27,6,12,"LARGE CAVERN."
4050 DATA 0,7,0,0,0,0,"LARGE 8-SIDED ROOM."
4060 DATA 7,0,0,0,20,0,"ROYAL ANTEROOM."
4070 DATA 11,10,0,1,0,26,"*I'M ON THE SHORE OF A LAKE."
4080 DATA 11,11,23,11,0,0,"FOREST."
4090 DATA 13,15,15,0,0,13,"MAZE OF PITS."
4100 DATA 0,0,0,14,12,0,"MAZE OF PITS."
4110 DATA 17,12,13,16,16,17,"MAZE OF PITS."
4120 DATA 12,0,13,12,13,0,"MAZE OF PITS."
4130 DATA 0,17,0,0,14,17,"MAZE OF PITS."
4140 DATA 17,12,12,15,14,18,"MAZE OF PITS."
4150 DATA 0,0,0,0,17,0
4160 DATA "BOTTOM OF A CHASM. ABOVE 2 LEDGES. ONE HAS A BRICKED UP WINDOW."
4170 DATA 0,0,0,20,0,0
4180 DATA "*I'M ON A NARROW LEDGE BY A CHASM. ACROSS THE CHASM IS A THRONE ROOM."
4190 DATA 0,0,0,0,0,9,"ROYAL CHAMBER."
4200 DATA 0,0,0,0,0,0,"*I'M ON A NARROW LEDGE BY THE THRONE ROOM-ACROSS CHASM-LEDGE."
4210 DATA 0,0,0,21,0,0,"THRONE ROOM."
4220 DATA 10,1,10,11,0,0,"SUNNY MEADOW."
4230 DATA 0,0,0,0,0,0
4240 DATA "*I THINK I'M IN REAL TROUBLE. HERE'S A GUY WITH A PITCHFORK!"
4250 DATA 11,0,1,11,0,0,"HIDDEN GROVE."
4260 DATA 0,0,0,0,0,0,"QUICK-SAND BOG."
4270 DATA 0,0,7,0,0,0,"MEMORY RAM OF AN IBM-PC. I TOOK A WRONG TURN!"
4280 DATA 0,0,0,0,0,11
4290 DATA "BRANCH ON THE TOP OF AN OLD OAK TREE. TO THE EAST I SEE A MEADOW BEYOND A LAKE."
4300 DATA 0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,24,11,24,28,24
4350 DATA "LARGE MISTY ROOM WITH STRANGE LETTERS OVER THE EXITS."
4770 DATA " ",0,"DARK HOLE",4,*POT OF RUBIES*/RUB/,4
4780 DATA "SPIDER WEB WITH WRITING ON IT.",2
4790 DATA "HOLLOW STUMP AND REMAINS OF A FELLED TREE.",0,"CYPRESS TREE",1
4800 DATA "WATER",10,"EVIL SMELLING MUD/MUD/",1,*GOLDEN FISH*/FIS/,10
4810 DATA "LIT BRASS LAMP/LAM/",0,"OLD FASHONED BRASS LAMP/LAM/",3
4820 DATA "RUSTY AXE (MAGIC WORD- BUNYON -ON IT)/AXE/",10
4830 DATA "BOTTLE OF WATER/BOT/",3,"EMPTY BOTTLE/BOT/",0
4840 DATA "RING OF SKELETON KEYS/KEY/",2
4850 DATA "SIGN- LEAVE TREASURE HERE - (SAY 'SCORE')",3
4860 DATA "LOCKED DOOR",5,"OPEN DOOR WITH A HALLWAY BEYOND",0,"SWAMP GAS",1
4870 DATA *GOLDEN NET*/NET/,18,"CHIGGER BITES",0,"INFECTED CHIGGER BITES",0
4880 DATA "FLOATING PATCH OF OILY SLIME",1,*ROYAL HONEY*/HON/,8
4890 DATA "LARGE AFRICAN BEES",8,"THIN BLACK BEAR",21,"BEES IN A BOTTLE/BOT/",0
4900 DATA "LARGE SLEEPING DRAGON",23,"FLINT AND STEEL/FLI/",13
4910 DATA *THICK PERSIAN RUG*/RUG/,17
4920 DATA "SIGN- MAGIC WORD IS AWAY. LOOK LA -(REST OF SIGN IS MISSING)",18
4930 DATA "DISTENDED GAS BLADDER/BLA/",0,"BRICKED UP WINDOW",20
4940 DATA "SIGN HERE SAYS- IN SOME CASES MUD IS GOOD, IN OTHERS...",23
4950 DATA "STREAM OF LAVA",18
4960 DATA "BRICKED UP WINDOW WITH A HOLE IN IT. BEYOND IS A LEDGE.",0
4970 DATA "LOOSE FIRE BRICKS",0,*GOLD CROWN*/CRO/,22,*MAGIC MIRROR*,21
4980 DATA "SLEEPING BEAR",0,"EMPTY WINE BLADDER/BLA/",9,"BROKEN GLASS",0
4990 DATA "CHIGGERS/CHI/",1,"DEAD BEAR",0,*DRAGON EGGS* (VERY RARE)/EGG/,0
5000 DATA "LAVA STREAM WITH A BRICK DAM",0,*JEWELED FRUIT*/FRU/,25
5010 DATA *SMALL STATUE OF A BLUE OX*/OX/,26,*DIAMOND RING*/RIN/,0
5020 DATA *DIAMOND BRACELET*/BRA/,0
5030 DATA "STRANGE SCRATCHINGS ON ROCK REVEALS- ALADDIN WAS HERE -",14
5040 DATA "SIGN SAYS- LIMBO. FIND RIGHT EXIT AND LIVE AGAIN!",33
5050 DATA "SMOKING HOLE. PIECES OF DRAGON AND GORE.",0
5060 DATA "SIGN SAYS- NO SWIMMING ALLOWED -",10,"ARROW POINTING DOWN",17
5070 DATA "DEAD FISH/FIS/",0,*FIRESTONE* (COLD NOW)/FIR/,0
5080 DATA "SIGN SAYS- PAUL'S PLACE -",25,"TREES",11," ",0," ",0
