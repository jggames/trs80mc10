0 GOTO8
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 CLS:CLEAR500:M$="WELCOME TO PIRATES ADVENTURE":GOSUB1:GOSUB183
9 R=AR:LX=LT:DF=0:SF=0:INPUT"USE SAVED GAME (Y OR N)";K$
10 IFLEFT$(K$,1)<>"Y"THEN13
11 INPUT"IS SAVED GAME TAPE POSITIONED";K$:IF LEFT$(K$,1)<>"Y"THEN9
12 INPUT#1,SF,LX,DF,R:FORX=0TOIL:INPUT#1,IA(X):NEXTX:CLOSE1
13 GOSUB31:GOTO20
14 INPUT"WHAT NOW";TP$:PRINT:GOSUB21
15 IFFTHENPRINT"YOU USE WORD(S) I DON'T KNOW!":GOTO14
16 GOSUB47:IFIA(9)=-1THENLX=LX-1:GOTO18
17 GOTO20
18 IFLX<0THENPRINT"LIGHT HAS RUN OUT!":IA(9)=0:GOTO20
19 IFLX<25THENPRINT"LIGHT RUNS OUT IN";LX;"TURNS!"
20 NV(0)=0:GOSUB47:GOTO14
21 K=0:NT$(0)="":NT$(1)=""
22 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1:GOTO24
23 NT$(K)=LEFT$(NT$(K)+K$,LN)
24 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN30
25 FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
26 IFX=1ANDY<7THENK$=LEFT$(K$,LN)
27 IFNT$(X)=K$THENNV(X)=Y:GOTO29
28 NEXTY:GOTO30
29 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO29
30 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN
31 IFDFTHENIFIA(9)<>-1ANDIA(9)<>RTHENPRINT"I CAN'T SEE! TOO DARK!":RETURN
32 K=-1:IFLEFT$(RS$(R),1)="*"THENPRINTMID$(RS$(R),2);:GOTO34
33 M$="I'M IN A "+RS$(R):GOSUB1
34 FORZ=0TOIL:IFKTHENIFIA(Z)=RTHENPRINT:PRINT"VISIBLE ITEMS HERE:":K=0
35 GOTO40
36 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/"THENRETURN
37 FORW=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1):RETURN
38 NEXTW
39 RETURN
40 IFIA(Z)<>RTHEN43
41 GOSUB36
42 M$=TP$+". ":GOSUB1
43 NEXT
44 K=-1:FORZ=0TO5:IFKTHENIFRM(R,Z)<>0THENPRINT:PRINT"OBVIOUS EXITS: ":K=0
45 IFRM(R,Z)<>0THENPRINTNV$(Z+1,1);" ";
46 NEXT:PRINT:PRINT:RETURN
47 F2=-1:F=-1:F3=0:IFNV(0)=1ANDNV(1)<7THEN95
48 FORX=0TOCL:V=INT(C0(X)/150):IFNV(0)=0THENIFV<>0THENRETURN
49 IFNV(0)<>VTHENNEXTX:GOTO140
50 N=C0(X)-V*150
51 IFNV(0)=0THENF=0:GOTO53
52 GOTO55
53 IFINT(RND(1)*100+1)<=NTHEN56
54 NEXTX:GOTO140
55 IFN<>NV(1)ANDN<>0THENNEXTX:GOTO140
56 F2=-1:F=0:F3=-1:FORY=1TO5:ON Y GOTO 57,58,59,60,61
57 W=C1(X):GOTO62
58 W=C2(X):GOTO62
59 W=C3(X):GOTO62
60 W=C4(X):GOTO62
61 W=C5(X):GOTO62
62 LL=INT(W/20):K=W-LL*20:F1=-1
63 ONK+1GOTO82,70,72,74,76,77,78,79,80,81,66,68
64 IFK<12THEN66
65 ONK-11GOTO71,73,75
66 F1=-1:FORZ=0TOIL:IFIA(Z)=-1THEN82
67 NEXT:F1=0:GOTO82
68 F1=0:FORZ=0TOIL:IFIA(Z)=-1THEN82
69 NEXT:F1=-1:GOTO82
70 F1=IA(LL)=-1:GOTO82
71 F1=IA(LL)<>-1ANDIA(LL)<>R:GOTO82
72 F1=IA(LL)=R:GOTO82
73 F1=IA(LL)<>0:GOTO82
74 F1=IA(LL)=R ORIA(LL)=-1:GOTO82
75 F1=IA(LL)=0:GOTO82
76 F1=R=LL:GOTO82
77 F1=IA(LL)<>R:GOTO82
78 F1=IA(LL)<>-1:GOTO82
79 F1=R<>LL:GOTO82
80 F1=SFANDINT(2^LL+.5):F1=F1<>0:GOTO82
81 F1=SFANDINT(2^LL+.5):F1=F1=0:GOTO82
82 F2=F2ANDF1:IFF2THENNEXTY:GOTO84
83 NEXTX:GOTO140
84 IP=0:FORY=1TO4:K=INT((Y-1)/2+6):ONYGOTO85,86,87,88
85 AC=INT(C6(X)/150):GOTO89
86 AC=C6(X)-INT(C6(X)/150)*150:GOTO89
87 AC=INT(C7(X)/150):GOTO89
88 AC=C7(X)-INT(C7(X)/150)*150
89 IFAC>101THEN94
90 IFAC=0THEN137
91 IFAC<52THENPRINTMS$(AC):GOTO137
92 ONAC-51GOTO105,109,110,112,113,114,115,112,117,119,120,121,123,124,129,133,134,135,136,178,111
94 PRINTMS$(AC-50):GOTO137
95 L=DF:IFLTHENL=DFANDIA(9)<>R ANDIA(9)<>-1:GOTO97
96 GOTO98
97 IFLTHENPRINT"DANGEROUS IN THE DARK!"
98 IFNV(1)<1THENPRINT"GIVE ME A DIRECTION TOO.":GOTO145
99 K=RM(R,NV(1)-1)
100 IFK>=1THEN103
101 IFLTHENPRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=0:GOTO103
102 PRINT"I CAN'T GO IN THAT DIRECTION!!":GOTO145
103 IF NOT L THENPRINT
104 R=K:GOSUB31:GOTO145
105 L=0:FORZ=1TOIL:IFIA(Z)=-1THENL=L+1
106 NEXTZ
107 IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO138
108 GOSUB146:IA(P)=-1:GOTO137
109 GOSUB146:IA(P)=R:GOTO137
110 GOSUB146:R=P:GOTO137
111 GOSUB146:L=P:GOSUB146:Z=IA(P):IA(P)=IA(L):IA(L)=Z:GOTO137
112 GOSUB146:IA(P)=0:GOTO137
113 DF=-1:GOTO137
114 DF=0:GOTO137
115 GOSUB146
116 SF=INT(.5+2^P)OR SF:GOTO137
117 GOSUB146
118 SF=SF ANDNOT INT(.5+2^P):GOTO137
119 PRINT"I'M DEAD...":R=RL:DF=0:GOTO123
120 GOSUB146:L=P:GOSUB146:IA(L)=P:GOTO137
121 INPUT"THE GAME IS NOW OVER.-ANOTHER GAME";K$:IFLEFT$(K$,1)="N"THENEND
122 FORX=0TOIL:IA(X)=I2(X):NEXT:PRINT"":GOTO9
123 GOSUB31:GOTO137
124 L=0:FORZ=1TOIL:IFIA(Z)=TR THENIFLEFT$(IA$(Z),1)="*"THENL=L+1
125 NEXTZ:PRINT"I'VE STORED";L;"TREASURES. ON A SCALE"
126 PRINT"OF 0 TO 100 THAT RATES A";INT(L/TT*100)
127 IFL=TTTHENPRINT"WELL DONE. ":GOTO121
128 GOTO137
129 PRINT"I'M CARRYING:":K$="NOTHING":FORZ=0TOIL:IFIA(Z)<>-1THEN132
130 GOSUB36
131 PRINT TP$;".",;:K$=""
132 NEXT:PRINTK$:GOTO137
133 P=0:GOTO116
134 P=0:GOTO118
135 LX=LT:IA(9)=-1:GOTO137
136 PRINT"":GOTO137
137 NEXTY
138 IFNV(0)<>0THEN140
139 NEXTX
140 REM
141 IFNV(0)=0THEN145
142 GOSUB155
143 IFFTHENPRINT"I DON'T UNDERSTAND YOUR COMMAND.":GOTO145
144 IF NOT F2THENPRINT"I CAN'T DO THAT YET.":GOTO145
145 RETURN
146 IP=IP+1
147 ONIPGOTO148,149,150,151,152
148 W=C1(X):GOTO153
149 W=C2(X):GOTO153
150 W=C3(X):GOTO153
151 W=C4(X):GOTO153
152 W=C5(X):GOTO153
153 P=INT(W/20):M=W-P*20:IFM<>0THEN146
154 RETURN
155 IFNV(0)<>10ANDNV(0)<>18ORF3THEN177
156 IFNV(1)=0THENPRINT"WHAT?":GOTO171
157 IFNV(0)<>10THEN160
158 L=0:FORZ=0TOIL:IFIA(Z)=-1THENL=L+1
159 NEXT:IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO171
160 K=0:FORX=0TOIL:IFRIGHT$(IA$(X),1)<>"/"THEN172
161 LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FORY=LLTO2STEP-1
162 IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO172
163 TP$=LEFT$(MID$(TP$,Y+1),LN)
164 IFTP$<>NV$(NV(1),1)THEN172
165 IFNV(0)=10THEN168
166 IFIA(X)<>-1THENK=1:GOTO172
167 IA(X)=R:K=3:GOTO170
168 IFIA(X)<>RTHENK=2:GOTO172
169 IA(X)=-1:K=3
170 PRINT"OK, ":PRINT
171 F=0:RETURN
172 NEXTX
173 IFK=1THENPRINT"I'M NOT CARRYING IT!"
174 IFK=2THENPRINT"I DON'T SEE IT HERE."
175 IFK=0THENIFNOTF3THENPRINT"IT'S BEYOND MY POWER TO DO THAT.":F=0
176 IFK<>0THENF=0
177 RETURN
178 REM SAVE GAME
179 INPUT"OUTPUT TAPE READY TO SAVE GAME";K$:IF LEFT$(K$,1)<>"Y"THEN182
180 PRINT#1,SF:PRINT#1,LX:PRINT#1,DF:PRINT#1,R:FORW=0TOIL:PRINT#1,IA(W):FORZ9=1TO10:NEXTZ9:
181 NEXTW:CLOSE1
182 GOTO137
183 IL=60:CL=151:NL=59:RL=33:MX=5:AR=1:TT=2:LN=3:LT=200:ML=71:TR=1
184 DIMNV(1),C0(CL),C1(CL),C2(CL),C3(CL),C4(CL),C5(CL),C6(CL),C7(CL)
185 DIMNV$(NL,1),IA$(IL),IA(IL),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
186 ZZ=CL:FORX=0TOZZ:READC0(X),C1(X),C2(X),C3(X),C4(X),C5(X),C6(X),C7(X):NEXT       
189 FORX=0TONLSTEP10:FORY=0TO1
190 READNV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y):NEXTY,X
192 FORX=0TORL:READRM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT
193 FORX=0TOML:READMS$(X):NEXTX
194 FORX=0TOIL:READIA$(X),IA(X):I2(X)=IA(X):NEXTX
195 MS$(2)=MS$(2)+".THERE'S A WORD ENCRAVED ON THE FLYLEAF: -YOHO- "
196 MS$(2)=MS$(2)+"AND A MESSAGE: LONG JOHN SILVER LEFT 2 TREASURES ON "
197 MS$(2)=MS$(2)+"TREASURE ISLAND!!"
198 MS$(31)=MS$(31)+",AN ANCHOR,SAILSAND A KEEL."
199 RETURN
201 DATA80,422,342,420,340,,16559,8850
202 DATA80,462,482,460,,,15712,1705
203 DATA100,521,552,540,229,220,203,8700
204 DATA3,483,,,,,15712,,100,284,,,,,8550,,100,28,663,403,40,,8700,,100,48,20,660,740,220,9055,10902
205 DATA100,28,20,,,,3810,,100,8,700,720,,,10868,,100,48,40,640,400,300,9055,8305
206 DATA25,664,,,,,4263,,40,104,886,,,,4411,,80,242,502,820,80,240,9321,10109
207 DATA100,8,140,80,500,,10262,8850
208 DATA35,421,846,420,200,,5162,,100,129,120,,,,6508,,50,242,982,820,440,240,9321,8850
209 DATA35,483,69,,,,15705,,10,483,249,,,,15706,,50,484,1073,1086,,,17661,9150
210 DATA50,204,1086,,,,16711,,10,209,1040,1060,300,1100,10872,10050
211 DATA10,208,1040,1060,89,,10867,,85,483,8,,,,15719,10200
212 DATA100,8,,,,,10200,,100,104,,,,,8550,,80,462,282,280,1160,,1422,,158,82,60,,,,8170,9600
213 DATA4510,61,,,,,300,,163,22,100,,,,8170,9600
214 DATA8100,,,,,,16200,0
215 DATA4800,104,120,61,,,10507,8164
216 DATA4800,107,100,61,89,,10507,8164
217 DATA4063,22,,,,,647,0
218 DATA5570,161,203,160,180,,10870,1264
219 DATA6170,181,180,160,,,8302,0
220 DATA6300,104,,,,,900,0
221 DATA1529,442,465,440,,,7800,0
222 DATA1529,442,462,,,,760,9150
223 DATA183,322,180,,,,8170,9600
224 DATA1538,262,242,,,,1800,0
225 DATA1538,262,245,260,,,7800,0
226 DATA5888,262,242,,,,1800,0
227 DATA5888,262,245,,,,1950,0
228 DATA6188,262,245,541,260,560,2155,7950
229 DATA5888,261,,,,,2400,0
230 DATA4088,561,,,,,2400,0
231 DATA4088,263,,,,,2713,0
232 DATA4088,562,580,109,100,249,2303,8700
233 DATA4088,249,562,108,900,240,6203,8700
234 DATA4088,248,562,,,,6600,0
235 DATA4068,103,69,,,,646,0
236 DATA4068,103,68,,,,6600,0
237 DATA5887,342,,,,,2550,0
238 DATA5887,362,,,,,2713,0
239 DATA5887,382,,,,,2100,0
240 DATA159,382,320,,,,8170,9600
241 DATA6187,342,362,,,,2550,0
242 DATA6187,345,362,541,360,380,8303,10050
243 DATA3461,503,,,,,172,0
244 DATA3750,,,,,,9900,0
245 DATA1528,,,,,,9900,0
246 DATA4108,1143,1012,,,,646,0
247 DATA6450,,,,,,2853,0
248 DATA4510,66,,,,,2720,0
249 DATA4950,,,,,,9750,0
250 DATA5114,,,,,,10650,0
251 DATA7092,592,,,,,2745,0
252 DATA185,284,140,,,,8156,10564
253 DATA4098,1054,,,,,647,17550
254 DATA4098,1053,,,,,647,17400
255 DATA4083,322,,,,,647,0
256 DATA4095,762,,,,,647,0
257 DATA195,782,921,,,,2727,0
258 DATA195,762,261,,,,2727,0
259 DATA6900,,,,,,9450,0
260 DATA1526,602,,,,,2723,0
261 DATA1541,621,602,640,520,600,7853,8250
262 DATA195,782,661,,,,2727,0
263 DATA7092,623,583,303,643,20,8700,0
264 DATA7092,,,,,,3750,0
265 DATA200,722,220,,,,10554,9600
266 DATA195,762,61,,,,2727,0
267 DATA4050,,,,,,10564,0
268 DATA1526,523,520,,,,7800,0
269 DATA195,762,340,,,,8126,8464
270 DATA195,782,360,,,,8157,10564
271 DATA7530,404,242,1053,89,,17250,0
272 DATA4800,,,,,,450,0
273 DATA5868,103,200,69,60,,4553,8700
274 DATA5868,68,,,,,494,0
275 DATA1546,146,,,,,4800,0
276 DATA1546,802,141,140,840,,8302,0
277 DATA2746,841,840,140,,,8302,4950
278 DATA3496,802,,,,,811,0
279 DATA3496,841,840,140,,,811,8302
280 DATA7366,822,820,240,400,,5305,9300
281 DATA5861,503,,,,,2100,0
282 DATA8411,501,500,140,,,5459,7833
283 DATA192,742,400,,,,8170,9600
284 DATA201,404,88,420,240,242,8170,8071
285 DATA201,404,89,120,,,8170,9600
286 DATA7530,404,245,,,,2737,0
287 DATA7530,404,912,,,,2738,0
288 DATA7530,404,89,80,740,420,5908,9300
289 DATA7530,404,88,80,740,120,5910,9300
290 DATA7671,,,,,,6000,0
291 DATA4553,903,,,,,6300,0
292 DATA1350,,,,,,6000,0
293 DATA1510,62,60,,,,7800,0
294 DATA5860,63,,,,,18000,0
295 DATA201,404,88,420,,,8170,9600
296 DATA186,284,360,,,,8170,9600
297 DATA1539,482,242,,,,1800,0
298 DATA1539,482,480,,,,7904,16800
299 DATA194,682,300,,,,8170,9600
300 DATA174,149,464,140,,,8751,0
301 DATA174,160,,,,,8751,0
302 DATA7800,444,940,921,952,,10548,8014
303 DATA7800,124,921,,,,7350,0
304 DATA7800,424,992,980,921,,10553,7264
305 DATA8250,104,,,,,10505,9600
306 DATA7800,464,148,1140,921,1152,10553,7264
307 DATA1541,643,640,,,,7800,0
308 DATA163,104,40,,,,8170,9600
309 DATA6300,44,,,,,15450,0
310 DATA4534,583,,,,,4650,0
311 DATA6187,702,541,,,,2713,16050
312 DATA5887,702,,,,,2713,0
313 DATA5887,,722,,,,2100,0
314 DATA198,1022,480,,,,8170,9600
315 DATA157,2,24,40,,,8170,9600
316 DATA1510,44,60,40,80,85,7801,10800
317 DATA1532,302,208,300,,,7800,0
318 DATA1532,302,209,,,,2813,0
319 DATA1532,305,,,,,10518,7564
320 DATA8411,841,840,140,,,8922,0
321 DATA165,1122,500,,,,8170,9600
322 DATA1392,,,,,,6000,0
323 DATA6300,284,,,,,16350,0
324 DATA8582,,,,,,17700,0
325 DATA7800,921,209,302,200,,8700,0
326 DATA7950,,,,,,2700,0
327 DATA5908,621,1143,1000,,,4553,0
328 DATA5266,,,,,,1800,0
329 DATA6300,224,,,,,17517,17850
330 DATA1200,,,,,,17100,0
331 DATA6300,124,,,,,16350,0
332 DATA1050,208,1040,1060,,,10919,0
333 DATA6300,184,242,,,,3600,0
334 DATA7800,921,160,140,,,7410,9000
335 DATA6300,,,,,,450,0
336 DATAAUT,"GO",*CLI,*WAL,"*RUN",*ENT,*PAC,"WAI",SAY,SAI
337 DATAANY,NORTH,SOUTH,EAST,WEST,UP,DOWN,STA,PAS,HAL
338 DATA"GET",*TAK,*CAT,*PIC,*REM,*WEA,*PUL,FLY,DRO,*REL
339 DATABOO,BOT,*RUM,WIN,GAM,MON,PIR,ARO,BAG,*DUF
340 DATA*THR,*LEA,*GIV,DRI,*EAT,INV,SAI,LOO,*SHO," "
341 DATA"TOR",OFF,MAT,YOH,30,LUM,RUG,KEY,INV,DOU
342 DATAREA," ",YOH,SCO,"SAV",KIL,*ATT,LIG," ",OPE
343 DATASAI,FIS,ANC,SHA,PLA,CAV,PAT,DOO,CHE,PAR
344 DATA*SMA,UNL,HEL,AWA,*BUN," ",QUI,BUI,*MAK,WAK
345 DATAHAM,NAI,BOA,*SHI,SHE,CRA,WAT,*SAL,LAG,*TID
346 DATASET,CAS,DIG,BUR,FIN,JUM,EMP,WEI," "," "
347 DATAPIT,SHO,*BEA,MAP,PAC,BON,HOL,SAN,BOX,SNE
348 DATA,,,,,," "
349 DATA,,,,,,LONDON APARTMENT
350 DATA,,,,,1,"*I'M IN AN ALCOVE"
351 DATA,,4,2,,,SECRET PASSAGEWAY
352 DATA,,,3,,,MUSTY ATTIC
353 DATA,,,,,,*I'M OUTSIDE AN OPEN WINDOW ON A LEDGE OF A TALL BUILDING
354 DATA,,8,,,,SANDY BEACH ON A TROPICAL ISLE
355 DATA,12,13,14,,11,MAZE OF CAVES
356 DATA,,14,6,,,MEADOW
357 DATA,,,8,,,GRASS SHACK
358 DATA10,24,10,10,,,*I'M IN THE OCEAN
359 DATA,,,,7,,PIT
360 DATA7,,14,13,,,MAZE OF CAVES
361 DATA7,14,12,19,,,MAZE OF CAVES
362 DATA,,,8,,,*I'M AT THE FOOT OF CAVE-RIDDEN HILL.PATHLEADS TO TOP
363 DATA17,,,,,,TOOL SHED
364 DATA,,17,,,,LONG HALLWAY
365 DATA,,,16,,,LARGE CAVERN
366 DATA,,,,,14,*I'M ON TOP OF A HILL- BELOW IS PIRATES ISLAND
367 DATA,14,14,13,,,MAZE OF CAVES
368 DATA,,,,,,*I'M ABOARD A PIRATE SHIP ANCHORED OFF- SHORE
369 DATA,22,,,,,*I'M ON THE BEACH AT TREASURE ISLAND
370 DATA21,,23,,,,SPOOKY GRAVEYARD FULL OF EMPTY & BROKEN RUM BOTTLES
371 DATA,,,22,,,LARGE BARREN FIELD
372 DATA10,6,6,6,,,SHALLOW LAGOON-TO THE NORTH IS THE OCEAN
373 DATA,,,23,,,SACKED AND DESERTED MONASTARY
374 DATA,,,,,," "
375 DATA,,,,,," "
376 DATA,,,,,," "
377 DATA,,,,,," "
378 DATA,,,,,," "
379 DATA,,,,,," "
380 DATA,,,,,," "
381 DATA,,,,,,*WELCOME TO NEVER NEVER LAND
382 DATA" "
383 DATATHERE'S A STRANGE SOUND
384 DATATHE NAME OF THE BOOK IS TREASURE ISLAND
385 DATANOTHING HAPPENS
386 DATATHERE'S SOMETHING THERE. MAYBE I SHOULD
387 DATATHAT'S NOT VERY SAFE
388 DATAYOU MAY NEED MAGIC HERE
389 DATAEVERYTHING SPINS AROUND AND SUDDENLY I'M ELSEWHERE...
390 DATATORCH IS LIT
391 DATAI WAS WRONG. I GUESS ITS NOT A MONGOOSE CAUSE THE SNAKES BIT IT.
392 DATAI'M SNAKE BIT
393 DATAPARROT ATTACKS SNAKES AND DRIVES THEM OFF
394 DATAPIRATE WON'T LET ME
395 DATAITS LOCKED
396 DATAITS OPEN
397 DATATHERE ARE A SET OF PLANS IN IT
398 DATANOT WHILE I'M CARRYING IT
399 DATACROCS STOP ME
400 DATASORRY I CAN'T
401 DATAWRONG GAME YOU SILLY GOOSE!
402 DATAI DON'T HAVE IT
403 DATAPIRATE GRABS RUM AND SCUTTLES OFF CHORT-LING
404 DATA...I THINK ITS ME. HEE HEE.
405 DATAITS NAILED TO THE FLOOR!
406 DATA-MAGIC WORD-HO AND A ... (WORK ON IT. YOU'LL GET IT)
407 DATANO. SOMETHING IS MISSING!
408 DATAIT WAS A TIGHT SQUEEZE!
409 DATASOMETHING WON'T FIT
410 DATASINCE NOTHING IS HAPPENING
411 DATAI SLIPPED AND FELL...
412 DATASOMETHING FALLS OUT
413 DATA"THEY'RE PLANS TO BUILD A SHIP-YOU'LL NEED HAMMER,NAILS,LUMBER"
414 DATAI'VE NO CONTAINER
415 DATAIT SOAKS INTO THE GROUND
416 DATATOO DRY. FISH VANISH.
417 DATAPIRATE AWAKENS. SAYS 'AYE MATEY WE BE CASTING OFF SOON-HE VANISHES
418 DATAWHAT A WASTE...
419 DATAI'VE NO CREW
420 DATAPIRATE SAYS 'AYE MATEY-WE BE NEEDING A MAP FIRST'
421 DATAAFTER A DAY AT SEA WE ANCHOR OFF A SANDYBEACH. ALL ASHORE...
422 DATATRY 'WEIGH ANCHOR'
423 DATATHERE'S A MAP IN IT
424 DATAIT'S A MAP TO TREASURE ISLAND. AT BOTTOMIT SAYS '30 PACES AND DIG!'
425 DATA" *WELCOME TO PIRATES ADVENTURE*"
426 DATAITS EMPTY,I'VE NO PLANS!,OPEN IT?,GO THERE?,I FOUND SOMETHING!
427 DATAI DIDN'T FIND ANYTHING,I DON'T SEE IT HERE,"OK,I WALKED 30 PACES"
428 DATACONGRATULATIONS!!! BUT YOUR ADVENTURE IS NOT OVER YET...
429 DATAREADING EXPANDS THE MIND,THE PARROT CRYS,'CHECK THE BAG MATEY'
430 DATA'CHECK THE CHEST MATEY',FROM THE OTHER SIDE!,OPEN THE BOOK!
431 DATATHERE'S MULTIPLE EXITS HERE!,CROCKS EAT FISH AND LEAVE
432 DATAI'M UNDERWATER.I CAN'T SWIM. BLUB BLUB..
433 DATA'PIECES OF EIGHT',ITS STUCK IN THE SAND,USE ONE WORD
434 DATAPIRATE SAYS'AYE MATEY-WE BE WAITING FOR THE TIDE TO COME IN'
435 DATATHE TIDE IS OUT,THE TIDE IS COMING IN
436 DATAABOUT 60 POUNDS. TRY 'SET SAIL','TIDES A CHANGING MATEY'
437 DATANOTE HERE 'I BE LIKING PARROTS. THEY BE SMART MATEY'
438 DATAPIRATE FOLLOWS ME ASHORE AS IF HE IS WAITING FOR SOMETHING.
439 DATAFLIGHT OF STAIRS,1
440 DATAOPEN WINDOW,2,BOOKS IN A BOOKCASE,2,LARGE LEATHER-BOUND BOOK/BOO/,0
441 DATABOOKCASE WITH SECRET PASSAGE BEHIND IT,,PIRATE'S DUFFEL BAG/BAG/,4
442 DATASIGN ON WALL 'RETURN TREASURES HERE-SAY SCORE',1
443 DATAEMPTY BOTTLE/BOT/,0
444 DATAUNLIT TORCH/TOR/,4,LIT TORCH/TOR/,,MATCHES/MAT/,0
445 DATASMALL SHIP'S KEEL AND MAST,6,WICKED LOOKING PIRATE,9
446 DATATREASURE CHEST/CHE/,9,MONGOOSE/MON/,8,RUSTY ANCHOR/ANC/,24
447 DATAGRASS SHACK,8,MEAN AND HUNGRY-LOOKING CROCODILES,11
448 DATALOCKED DOOR,11,OPEN DOOR WITH HALL BEYOND,,PILE OF SAILS/SAI/,17
449 DATAFISH/FIS/,10,*DOUBLOONS*/DOU/,25,DEADLY MAMBA SNAKES/SNA/,25
450 DATAPARROT/PAR/,9,BOTTLE OF RUM/BOT/,1,RUG/RUG/,,RING OF KEYS/KEY/,0
451 DATAOPEN TREASURE CHEST/CHE/,,SET OF PLANS/PLA/,,RUG,1
452 DATACLAW HAMMER/HAM/,15,NAILS/NAI/,,PILE OF PRECUT LUMBER/LUM/,17
453 DATATOOL SHED,17,LOCKED DOOR,16,OPEN DOOR WITH PIT BEYOND,0
454 DATAPIRATE SHIP,,ROCK WALL WITH NARROW CRACK IN IT,18
455 DATANARROW CRACK IN THE ROCK,17,SALT WATER,10
456 DATASLEEPING PIRATE,,BOTTLE OF SALT WATER/BOT/,0
457 DATAPIECES OF BROKEN RUM BOTTLES,4,NON-SKID SNEAKERS/SNE/,1,MAP/MAP/,0
458 DATASHOVEL/SHO/,15,MOULDY OLD BONES/BON/,,SAND/SAN/,6
459 DATABOTTLES OF RUM/BOT/,,*RARE OLD PRICELESS STAMPS*/STA/,,LAGOON,6
460 DATATHE TIDE IS OUT,24,THE TIDE IS COMING IN,,WATER WINGS/WIN/,15
461 DATAFLOTSAM AND JETSAM,0
462 DATAMONASTARY,23
463 DATAPLAIN WOODEN BOX/BOX/,0
464 DATADEAD WEASEL,," ",," ",0
