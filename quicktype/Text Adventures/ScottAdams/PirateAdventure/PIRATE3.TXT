0 CLS:CLEAR500:DIMC1,CC,P,K:GOTO100
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
100 X=Y=Z:K=R=V:N=LL=F:TP$=K$:W=IP=P:GOSUB2100
300 R=AR:LX=LT:DF=0:SF=0:INPUT"USE SAVED GAME (Y OR N)";K$
310 IFLEFT$(K$,1)<>"Y"THEN370
320 INPUT"IS TAPE POSITIONED";K$:IFLEFT$(K$,1)<>"Y"THEN300
340 CLOAD*I,"PIRATDAT":SF=I(61):LX=I(62):DF=I(63):R=I(64)
370 GOSUB550:GOTO440
380 PRINT:INPUT"TELL ME WHAT TO DO";TP$:GOSUB450
390 IFFTHENM$="YOU USE WORD(S) I DON'T KNOW!":GOSUB1:GOTO380
400 GOSUB710:IFI(9)=-1THENLX=LX-1:GOTO420
410 GOTO440
420 IFLX<0THENPRINT"LIGHT HAS RUN OUT!":I(9)=0:GOTO440
430 IFLX<25THENM$="LIGHT RUNS OUT IN"+STR$(LX)+" TURNS!":GOSUB1
440 NV(0)=0:GOSUB710:GOTO380
450 K=0:NT$(0)="":NT$(1)="":FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1:GOTO480
470 NT$(K)=LEFT$(NT$(K)+K$,LN)
480 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN540
490 FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
500 IFX=1ANDY<7THENK$=LEFT$(K$,LN)
510 IFNT$(X)=K$THENNV(X)=Y:GOTO530
520 NEXTY:GOTO540
530 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO530
540 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN
550 IFDFTHENIFI(9)<>-1ANDI(9)<>RTHENM$="I CAN'T SEE, IT'S TOO DARK!":GOSUB1:RETURN
560 K=-1:IFLEFT$(RS$(R),1)="*"THENM$=MID$(RS$(R),2):GOSUB1:GOTO580
570 M$="I'M IN A "+RS$(R):GOSUB1
580 FORZ=0TOIL:IFKTHENIFI(Z)=RTHENPRINT"VISIBLE ITEMS HERE:":K=0
590 GOTO640
600 TP$=I$(Z):IFRIGHT$(TP$,1)<>"/"THENRETURN
610 FORW=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1):RETURN
620 NEXTW
630 RETURN
640 IFI(Z)<>RTHEN670
650 GOSUB600
660 M$=TP$+".":GOSUB1
670 NEXT
680 K=-1:M$="":FORZ=0TO5:IFKTHENIFRM(R,Z)<>0THENM$="OBVIOUS EXITS: ":K=0
690 IFRM(R,Z)<>0THENM$=M$+NV$(Z+1,1)+" "
700 NEXT:GOSUB1:RETURN
710 F2=-1:F=-1:F3=0:IFNV(0)=1ANDNV(1)<7THEN1190
720 FORX=0TOCL:V=INT(C(0,X)/150):IFNV(0)=0THENIFV<>0THENRETURN
730 IFNV(0)<>VTHENNEXTX:GOTO1640
740 N=INT(C(0,X))-V*150
750 IFNV(0)=0THENF=0:GOTO770
760 GOTO790
770 IFINT(RND(0)*100+1)<=NTHEN800
780 NEXTX:GOTO1640
790 IFN<>NV(1)ANDN<>0THENNEXTX:GOTO1640
800 F2=-1:F=0:F3=-1:FORY=1TO5:ONYGOTO810,820,830,840,850
810 W=INT(C(1,X)):GOTO860
820 W=INT(C(2,X)):GOTO860
830 W=INT(C(3,X)):GOTO860
840 W=INT(C(4,X)):GOTO860
850 W=INT(C(5,X)):GOTO860
860 LL=INT(W/20):K=W-LL*20:F1=-1
870 ONK+1GOTO1060,940,960,980,1000,1010,1020,1030,1040,1050,900,920
880 IFK<12THEN900
890 ONK-11GOTO950,970,990
900 F1=-1:FORZ=0TOIL:IFI(Z)=-1THEN1060
910 NEXT:F1=0:GOTO1060
920 F1=0:FORZ=0TOIL:IFI(Z)=-1THEN1060
930 NEXT:F1=-1:GOTO1060
940 F1=I(LL)=-1:GOTO1060
950 F1=I(LL)<>-1ANDI(LL)<>R:GOTO1060
960 F1=I(LL)=R:GOTO1060
970 F1=I(LL)<>0:GOTO1060
980 F1=I(LL)=R ORI(LL)=-1:GOTO1060
990 F1=I(LL)=0:GOTO1060
1000 F1=R=LL:GOTO1060
1010 F1=I(LL)<>R:GOTO1060
1020 F1=I(LL)<>-1:GOTO1060
1030 F1=R<>LL:GOTO1060
1040 F1=SFANDINT(2^LL+.5):F1=F1<>0:GOTO1060
1050 F1=SFANDINT(2^LL+.5):F1=F1=0:GOTO1060
1060 F2=F2ANDF1:IFF2THENNEXTY:GOTO1080
1070 NEXTX:GOTO1640
1080 IP=0:FORY=1TO4:K=INT((Y-1)/2+6):ONYGOTO1090,1100,1110,1120
1090 AC=INT(C(6,X)/150):GOTO1130
1100 AC=INT(C(6,X))-INT(C(6,X)/150)*150:GOTO1130
1110 AC=INT(C(7,X)/150):GOTO1130
1120 AC=C(7,X)-INT(C(7,X)/150)*150
1130 IFAC>101THEN1180
1140 IFAC=0THEN1610
1150 IFAC<52THENM$=MS$(AC):GOSUB1:GOTO1610
1160 ONAC-51GOTO1290,1330,1340,1360,1370,1380,1390,1360,1410,1430,1440
1170 ONAC-62GOTO1450,1470,1480,1530,1570,1580,1590,1600,2020,1350
1180 M$=MS$(AC-50):GOSUB1:GOTO1610
1190 L=DF:IFLTHENL=DFANDI(9)<>R ANDI(9)<>-1:GOTO1210
1200 GOTO1220
1210 IFLTHENPRINT"DANGEROUS IN THE DARK!"
1220 IFNV(1)<1THENPRINT"GIVE ME A DIRECTION TOO.":GOTO1690
1230 K=RM(R,NV(1)-1)
1240 IFK>=1THEN1270
1250 IFLTHENM$="I FELL DOWN AND BROKE MY NECK.":GOSUB1:K=RL:DF=0:GOTO1270
1260 M$="I CAN'T GO IN THAT DIRECTION!":GOSUB1:GOTO1690
1270 IFNOTLTHENPRINT
1280 R=K:GOSUB550:GOTO1690
1290 L=0:FORZ=1TOIL:IFI(Z)=-1THENL=L+1
1300 NEXTZ
1310 IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO1620
1320 GOSUB1700:I(P)=-1:GOTO1610
1330 GOSUB1700:I(P)=R:GOTO1610
1340 GOSUB1700:R=P:GOTO1610
1350 GOSUB1700:L=P:GOSUB1700:Z=I(P):I(P)=I(L):I(L)=Z:GOTO1610
1360 GOSUB1700:I(P)=0:GOTO1610
1370 DF=-1:GOTO1610
1380 DF=0:GOTO1610
1390 GOSUB1700
1400 SF=INT(.5+2^P)ORSF:GOTO1610
1410 GOSUB1700
1420 SF=SFANDNOTINT(.5+2^P):GOTO1610
1430 PRINT"I'M DEAD...":R=RL:DF=0:GOTO1470
1440 GOSUB1700:L=P:GOSUB1700:I(L)=P:GOTO1610
1450 INPUT"THE GAME IS NOW OVER. ANOTHER   GAME";K$:IFLEFT$(K$,1)="N"THENEND
1460 FORX=0TOIL:I(X)=I2(X):NEXT:GOTO300
1470 GOSUB550:GOTO1610
1480 L=0:FORZ=1TOIL:IFI(Z)=TR THENIFLEFT$(I$(Z),1)="*"THENL=L+1
1490 NEXTZ:M$="I'VE STORED"+STR$(L)+" TREASURES. ON A SCALE OF 0 TO 100 THAT RATES A"+STR$(INT(L/TT*100)):GOSUB1
1510 IFL=TTTHENPRINT"WELL DONE.":GOTO1450
1520 GOTO1610
1530 I$="I'M CARRYING: ":K$="NOTHING":FORZ=0TOIL:IFI(Z)<>-1THEN1560
1540 GOSUB600
1550 I$=I$+TP$+", ":K$=""
1560 NEXT:I$=I$+K$:M$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1:GOTO1610
1570 P=0:GOTO1400
1580 P=0:GOTO1420
1590 LX=LT:I(9)=-1:GOTO1610
1600 GOTO1610
1610 NEXTY
1620 IFNV(0)<>0THEN1640
1630 NEXTX
1640 REM
1650 IFNV(0)=0THEN1690
1660 GOSUB1790
1670 IFFTHENM$="I DON'T UNDERSTAND YOUR COMMAND.":GOSUB1:GOTO1690
1680 IFNOTF2THENPRINT"I CAN'T DO THAT YET.":GOTO1690
1690 RETURN
1700 IP=IP+1
1710 ONIPGOTO1720,1730,1740,1750,1760
1720 W=INT(C(1,X)):GOTO1770
1730 W=INT(C(2,X)):GOTO1770
1740 W=INT(C(3,X)):GOTO1770
1750 W=INT(C(4,X)):GOTO1770
1760 W=INT(C(5,X)):GOTO1770
1770 P=INT(W/20):M=W-P*20:IFM<>0THEN1700
1780 RETURN
1790 IFNV(0)<>10ANDNV(0)<>18ORF3THEN2010
1800 IFNV(1)=0THENPRINT"WHAT?":GOTO1950
1810 IFNV(0)<>10THEN1840
1820 L=0:FORZ=0TOIL:IFI(Z)=-1THENL=L+1
1830 NEXT:IFL>=MXTHENPRINT"I'VE TOO MUCH TO CARRY!":GOTO1950
1840 K=0:FORX=0TOIL:IFRIGHT$(I$(X),1)<>"/"THEN1960
1850 LL=LEN(I$(X))-1:TP$=MID$(I$(X),1,LL):FORY=LLTO2STEP-1
1860 IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO1960
1870 TP$=LEFT$(MID$(TP$,Y+1),LN)
1880 IFTP$<>NV$(NV(1),1)THEN1960
1890 IFNV(0)=10THEN1920
1900 IFI(X)<>-1THENK=1:GOTO1960
1910 I(X)=R:K=3:GOTO1940
1920 IFI(X)<>RTHENK=2:GOTO1960
1930 I(X)=-1:K=3
1940 PRINT"OK,"
1950 F=0:RETURN
1960 NEXTX
1970 IFK=1THENPRINT"I'M NOT CARRYING IT!"
1980 IFK=2THENPRINT"I DON'T SEE IT HERE."
1990 IFK=0THENIFNOTF3THENM$="IT'S BEYOND MY POWER TO DO THAT.":GOSUB1:F=0
2000 IFK<>0THENF=0
2010 RETURN
2020 REM SAVE GAME
2030 INPUT"TAPE READY TO SAVE";K$:IF LEFT$(K$,1)<>"Y"THEN2090
2040 I(61)=SF:I(62)=LX:I(63)=DF:I(64)=R:CSAVE*I,"PIRATDAT"
2090 GOTO1610
2100 READIL,CL,NL,RL,MX,AR,TT,LN,LT,ML,TR:DIMNV(1),C(7,CL),NV$(NL,1),I$(IL),I(64),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
2130 ZZ=151:PRINT"load PIRATE2":CLOAD*C,"PIRATE2"
2160 FORX=0TONLSTEP10:FORY=0TO1
2170 READNV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y)
2180 READ NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y):NEXTY,X
2190 FORX=0TORL:READRM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT
2200 FORX=0TOML:READMS$(X):NEXTX
2210 FORX=0TOIL:READI$(X),I(X):I2(X)=I(X):NEXTX
2220 MS$(2)=MS$(2)+".THERE'S A WORD ENCRAVED ON THE FLYLEAF: -YOHO- "
2230 MS$(2)=MS$(2)+"AND A MESSAGE: LONG JOHN SILVER  LEFT 2 TREASURES ON "
2240 MS$(2)=MS$(2)+"TREASURE ISLAND!!"
2250 MS$(31)=MS$(31)+",AN ANCHOR,SAILSAND A KEEL."
2260 RETURN
2270 DATA60,151,59,33,5,1,2,3,200,71,1
3800 DATAAUT,"GO",*CLI,*WAL,"*RUN",*ENT,*PAC,"WAI",SAY,SAI,ANY,NORTH,SOUTH,EAST,WEST,UP,DOWN,STA,PAS,HAL
3820 DATA"GET",*TAK,*CAT,*PIC,*REM,*WEA,*PUL,FLY,DRO,*REL,BOO,BOT,*RUM,WIN,GAM,MON,PIR,ARO,BAG,*DUF
3840 DATA*THR,*LEA,*GIV,DRI,*EAT,INV,SAI,LOO,*SHO," ","TOR",OFF,MAT,YOH,30,LUM,RUG,KEY,INV,DOU
3860 DATAREA," ",YOH,SCO,"SAV",KIL,*ATT,LIG," ",OPE,SAI,FIS,ANC,SHA,PLA,CAV,PAT,DOO,CHE,PAR
3880 DATA*SMA,UNL,HEL,AWA,*BUN," ",QUI,BUI,*MAK,WAK,HAM,NAI,BOA,*SHI,SHE,CRA,WAT,*SAL,LAG,*TID
3900 DATASET,CAS,DIG,BUR,FIN,JUM,EMP,WEI," "," ",PIT,SHO,*BEA,MAP,PAC,BON,HOL,SAN,BOX,SNE
3920 DATA0,0,0,0,0,0," ",0,0,0,0,0,0,LONDON APARTMENT,0,0,0,0,0,1,"*I'M IN AN ALCOVE",0,0,4,2,0,0,SECRET PASSAGEWAY
3960 DATA0,0,0,3,0,0,MUSTY ATTIC,0,0,0,0,0,0,*I'M OUTSIDE AN OPEN WINDOW ON A LEDGE OF A TALL BUILDING
3980 DATA0,0,8,0,0,0,SANDY BEACH ON A TROPICAL ISLE,0,12,13,14,0,11,MAZE OF CAVES,0,0,14,6,0,0,MEADOW
4010 DATA0,0,0,8,0,0,GRASS SHACK,10,24,10,10,0,0,*I'M IN THE OCEAN,0,0,0,0,7,0,PIT,7,0,14,13,0,0,MAZE OF CAVES
4050 DATA7,14,12,19,0,0,MAZE OF CAVES,0,0,0,8,0,0,*I'M AT THE FOOT OF CAVE-RIDDEN HILL.PATHLEADS TO TOP
4070 DATA17,0,0,0,0,0,TOOL SHED,0,0,17,0,0,0,LONG HALLWAY,0,0,0,16,0,0,LARGE CAVERN
4100 DATA0,0,0,0,0,14,*I'M ON TOP OF A HILL- BELOW IS PIRATES ISLAND,0,14,14,13,0,0,MAZE OF CAVES
4120 DATA0,0,0,0,0,0,*I'M ABOARD A PIRATE SHIP ANCHORED OFFSHORE,0,22,0,0,0,0,*I'M ON THE BEACH AT TREASURE ISLAND
4140 DATA21,0,23,0,0,0,SPOOKY GRAVEYARD FULL OF EMPTY & BROKEN RUM BOTTLES,0,0,0,22,0,0,LARGE BARREN FIELD
4160 DATA10,6,6,6,0,0,SHALLOW LAGOON-TO THE NORTH IS THE OCEAN,0,0,0,23,0,0,SACKED AND DESERTED MONASTARY
4180 DATA0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," ",0,0,0,0,0,0," "
4250 DATA0,0,0,0,0,0,*WELCOME TO NEVER NEVER LAND," ",THERE'S A STRANGE SOUND,THE NAME OF THE BOOK IS TREASURE ISLAND
4290 DATANOTHING HAPPENS,THERE'S SOMETHING THERE. MAYBE I SHOULD,THAT'S NOT VERY SAFE,YOU MAY NEED MAGIC HERE
4330 DATAEVERYTHING SPINS AROUND AND SUDDENLY I'M ELSEWHERE...,TORCH IS LIT
4350 DATAI WAS WRONG. I GUESS ITS NOT A MONGOOSE CAUSE THE SNAKES BIT IT.,I'M SNAKE BIT
4370 DATAPARROT ATTACKS SNAKES AND DRIVES THEM OFF,PIRATE WON'T LET ME,ITS LOCKED,ITS OPEN
4410 DATATHERE ARE A SET OF PLANS IN IT,NOT WHILE I'M CARRYING IT,CROCS STOP ME,SORRY I CAN'T,WRONG GAME YOU SILLY GOOSE!
4460 DATAI DON'T HAVE IT,PIRATE GRABS RUM AND SCUTTLES OFF CHORT-LING,...I THINK ITS ME. HEE HEE.",ITS NAILED TO THE FLOOR!
4500 DATA-MAGIC WORD-HO AND A ... (WORK ON IT.  YOU'LL GET IT),NO. SOMETHING IS MISSING!,IT WAS A TIGHT SQUEEZE!
4530 DATASOMETHING WON'T FIT,SINCE NOTHING IS HAPPENING,I SLIPPED AND FELL...,SOMETHING FALLS OUT
4570 DATA"THEY'RE PLANS TO BUILD A SHIP-YOU'LL NEED HAMMER,NAILS,LUMBER",I'VE NO CONTAINER,IT SOAKS INTO THE GROUND
4600 DATATOO DRY. FISH VANISH.,PIRATE AWAKENS. SAYS 'AYE MATEY WE BE CASTING OFF SOON-HE VANISHES,WHAT A WASTE...
4630 DATAI'VE NO CREW,PIRATE SAYS 'AYE MATEY-WE BE NEEDING A MAP FIRST'
4650 DATAAFTER A DAY AT SEA WE ANCHOR OFF A SANDY BEACH. ALL ASHORE...,TRY 'WEIGH ANCHOR',THERE'S A MAP IN IT
4680 DATAIT'S A MAP TO TREASURE ISLAND. AT BOTTOM IT SAYS '30 PACES AND DIG!',*WELCOME TO PIRATES ADVENTURE*
4700 DATAITS EMPTY,I'VE NO PLANS!,OPEN IT?,GO THERE?,I FOUND SOMETHING!
4710 DATAI DIDN'T FIND ANYTHING,I DON'T SEE IT HERE,"OK,I WALKED 30 PACES"
4720 DATACONGRATULATIONS!!!  BUT YOUR ADVENTURE  IS NOT OVER YET...
4730 DATAREADING EXPANDS THE MIND,THE PARROT CRYS,'CHECK THE BAG MATEY'
4740 DATA'CHECK THE CHEST MATEY',FROM THE OTHER SIDE!,OPEN THE BOOK!,THERE'S MULTIPLE EXITS HERE!,CROCKS EAT FISH AND LEAVE
4760 DATAI'M UNDERWATER.I CAN'T SWIM. BLUB BLUB..,'PIECES OF EIGHT',ITS STUCK IN THE SAND,USE ONE WORD
4780 DATAPIRATE SAYS 'AYE MATEY-WE BE WAITING FOR THE TIDE TO COME IN',THE TIDE IS OUT,THE TIDE IS COMING IN
4800 DATAABOUT 60 POUNDS. TRY 'SET SAIL','TIDES A CHANGING MATEY',NOTE HERE 'I BE LIKING PARROTS. THEY BE SMART MATEY'
4820 DATAPIRATE FOLLOWS ME ASHORE AS IF HE IS WAITING FOR SOMETHING.,FLIGHT OF STAIRS,1
4840 DATAOPEN WINDOW,2,BOOKS IN A BOOKCASE,2,LARGE LEATHER-BOUND BOOK/BOO/,0
4850 DATABOOKCASE WITH SECRET PASSAGE BEHIND IT,0,PIRATE'S DUFFEL BAG/BAG/,4
4860 DATASIGN ON WALL 'RETURN TREASURES HERE-SAY SCORE',1,EMPTY BOTTLE/BOT/,0,UNLIT TORCH/TOR/,4,LIT TORCH/TOR/
4870 DATA0,MATCHES/MAT/,0,SMALL SHIP'S KEEL AND MAST,6,WICKED LOOKING PIRATE,9
4900 DATATREASURE CHEST/CHE/,9,MONGOOSE/MON/,8,RUSTY ANCHOR/ANC/,24,GRASS SHACK,8,MEAN AND HUNGRY-LOOKING CROCODILES,11
4920 DATALOCKED DOOR,11,OPEN DOOR WITH HALL BEYOND,0,PILE OF SAILS/SAI/,17
4930 DATAFISH/FIS/,10,*DOUBLOONS*/DOU/,25,DEADLY MAMBA SNAKES/SNA/,25
4940 DATAPARROT/PAR/,9,BOTTLE OF RUM/BOT/,1,RUG/RUG/,0,RING OF KEYS/KEY/,0
4950 DATAOPEN TREASURE CHEST/CHE/,0,SET OF PLANS/PLA/,0,RUG,1,CLAW HAMMER/HAM/,15,NAILS/NAI/,0,PILE OF PRECUT LUMBER/LUM/,17
4970 DATATOOL SHED,17,LOCKED DOOR,16,OPEN DOOR WITH PIT BEYOND,0,PIRATE SHIP,0,ROCK WALL WITH NARROW CRACK IN IT,18
4990 DATANARROW CRACK IN THE ROCK,17,SALT WATER,10,SLEEPING PIRATE,0,BOTTLE OF SALT WATER/BOT/,0
5010 DATAPIECES OF BROKEN RUM BOTTLES,4,NON-SKID SNEAKERS/SNE/,1,MAP/MAP/,0,SHOVEL/SHO/,15,MOULDY OLD BONES/BON/,0,SAND/SAN/,6
5030 DATABOTTLES OF RUM/BOT/,0,*RARE OLD PRICELESS STAMPS*/STA/,0,LAGOON,6
5040 DATATHE TIDE IS OUT,24,THE TIDE IS COMING IN,0,WATER WINGS/WIN/,15,FLOTSAM AND JETSAM,0,MONASTARY,23
5070 DATAPLAIN WOODEN BOX/BOX/,0,DEAD WEASEL,0," ",0," ",0
