0 CLEAR500:DIMCC,ZZ,Z$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$=" "ORI$=""THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
100 REM REPRODUCED WITH PERMISSION FROM CLOAD MAGAZINE
120 CLEAR250:DIML$(25),A(4,25),M$(3,36),B(3,36)
130 CLS:GOSUB1770:GOSUB1130:B=1
140 CLS:B(1,5)=1+RND(23):B(1,6)=2+RND(22):B(1,7)=2+RND(22):PRINT"LOCATION: ";L$(B):C=0:PRINT"YOU SEE: ";:FORA=1TO36
150 IF B(1,A)=B AND B(3,A)<>0 THEN PRINTTAB(10)M$(2,A):C=1
160 NEXTA:IF C=0 THEN PRINT"NOTHING OF INTEREST"
170 C=0:PRINT"DIRECTIONS: ";:IF A(1,B)<>0 THEN PRINT"NOR ";:C=1
180 IF A(2,B)<>0 THEN PRINT"SOU ";:C=1
190 IF A(3,B)<>0 THEN PRINT"EAS ";:C=1
200 IF A(4,B)<>0 THEN PRINT"WES";:C=1
210 IF C=0 THEN PRINT"? ? ?";
220 PRINT:PRINTSTRING$(30,"-")
230 INPUT"WHAT NOW . .";M$:PRINT" ";
240 IF M$="N" THEN M$="GO NOR" ELSE IF M$="S" THEN M$="GO SOU"
250 IF M$="E" THEN M$="GO EAS" ELSE IF M$="W" THEN M$="GO WES"
260 IF M$="LOO" OR M$="LOOK" THEN 140
270 IF LEN(M$)<3 THEN PRINT"?? WHAT ??":GOTO230
280 N$=LEFT$(M$,3):FOR A = (LEN(M$)-2)TO 1 STEP -1:IF MID$(M$,A,1)=" " THEN 290 ELSE NEXTA
290 O$=MID$(M$,A+1,3): IF N$="INV" THEN 370 ELSE IF N$="SCO" THEN 1090
300 IF N$="HEL" THEN 410 ELSE IF N$="GO " OR N$="WAL" THEN 470
310 IF N$="LOO" OR N$="EXA" THEN 530 ELSE IF N$="GET" OR N$="TAK" THEN 590
320 IF N$="DRO" OR N$="LEA" OR N$="PUT" THEN 740 ELSE IF N$="PRY" THEN 800
330 IF N$="INS" THEN 780 ELSE IF N$="KIC" THEN 820
340 IF N$="PUS" OR N$="PUL" OR N$="MOV" THEN 850 ELSE IF N$="OPE" OR N$="UNL" THEN 940
350 IF N$="KIL" THEN 1050 ELSE IF N$="BRE" THEN 1060 ELSE IF N$="REA" THEN 1080
360 PRINT"I DON'T UNDERSTAND THE VERB !":GOTO230
370 C=0:FOR A=1 TO 36
380 IF B(1,A)=25 THEN PRINTTAB(2)M$(2,A):C=1
390 NEXTA:IF C=0 THEN PRINT"YOU HAVE NOTHING !"
400 GOTO 230
410 IF B=1 THEN PRINT"INVENTORY AT START / SCORE AT":PRINT" THE END OF THE GAME":GOTO 230
420 IF B=7 OR B=13 OR B=18 OR B=23 OR B=24 THEN PRINT"EXAMINE THINGS . .":GOTO230
430 IF B=4 THEN PRINT"YOU DESERVE A 'BREAK' TODAY !":GOTO230
440 IF B=2 OR B=11 THEN PRINT"MOVE THINGS !":GOTO230
450 IF B=5 THEN PRINT"?? EKAT ??":GOTO230
460 PRINT"I'M AS CONFUSED AS YOU !":GOTO230
470 IF O$="DOO" AND B=1 AND B(1,2)=1 THEN B=3:GOTO140
480 IF O$="NOR" AND A(1,B)<>0 THEN B=A(1,B):GOTO140
490 IF O$="SOU" AND A(2,B)<>0 THEN B=A(2,B):GOTO140
500 IF O$="EAS" AND A(3,B)<>0 THEN B=A(3,B):GOTO140
510 IF O$="WES" AND A(4,B)<>0 THEN B=A(4,B):GOTO140
520 PRINT"YOU'RE UNABLE TO . . .":GOTO230
530 FOR A=1TO36:IFO$=M$(1,A)THEN 550 ELSE NEXTA
540 PRINT"I DON'T UNDERSTAND THE NOUN !":GOTO230
550 IF B(1,A)<>B AND B(1,A)<>25 THEN PRINT"IT ISN'T AROUND . .":GOTO230
560 IF B(3,A)=0 THEN PRINT"YOU CAN'T . . YET!":GOTO230
570 IF M$(3,A)="Z" THEN PRINT"YOU SEE NOTHING OF INTEREST!":GOTO230
580 PRINTM$(3,A):GOTO230
590 FOR A=1TO36:IF O$=M$(1,A) THEN 610 ELSE NEXTA
600 GOTO540
610 IF B(1,A)=25 THEN PRINT"YOU ALREADY HAVE IT !":GOTO230
620 IF B(1,A)<>B THEN PRINT"THAT ITEM ISN'T AROUND !":GOTO230
630 IF B(2,A)=0 THEN PRINT"YOU'RE UNABLE TO . . .":GOTO230
640 C=0:FOR D=1TO36:IF B(1,D)=25 THEN C=C+1
650 NEXTD:IF C>4 THEN PRINT"YOU CAN'T CARRY ANY MORE !":GOTO230
660 IF A=21 THEN M$(3,31)="Z"
670 IF A=25 THEN M$(3,24)="Z"
680 IF A=23 THEN M$(3,22)="Z"
690 IF A=15 THEN M$(2,35)="**THE DIAMOND**"
700 IF A=35 AND LEFT$(M$(2,35),2)="**" THEN CLS:PRINT"A TRAP DOOR OPENS . . .":PRINT"YOU FIND YOURSELF FALLING . . .":PRINT"FALLING . . .":GOTO 1010
710 PRINT"O.K.  . . YOU'VE GOT IT !":B(1,A)=25:B(3,A)=1
720 IF A=16 AND B(3,17)=0 THEN B(3,17)=1:PRINT"A TOKEN WAS UNDER THE RUG . ."
730 GOTO 230
740 FOR A=1TO36:IF O$=M$(1,A) THEN 760 ELSE NEXTA
750 GOTO 540
760 IF B(1,A)<>25 THEN PRINT"YOU DON'T HAVE THAT ITEM !":GOTO230
770 PRINT"O.K.  . .  YOU'VE DROPPED IT !":B(1,A)=B:GOTO230
780 IF B=24 AND O$="KEY" AND B(1,15)=25 THEN PRINT"O.K.":B(1,15)=24:M$(2,35)=">>THE DIAMOND<<":GOTO230
790 PRINT"SORRY, THAT'S NOT POSSIBLE !":GOTO230
800 IF O$="DOO" AND B=1 AND B(1,1)=1 THEN B(1,1)=0:M$(1,1)="!":B(1,2)=1:PRINT"THE DOOR POPS OPEN . .":GOTO230
810 PRINT"THAT'S NOT POSSIBLE . . SORRY!":GOTO230
820 IFB<>12 OR O$<>"DES" THEN PRINT"NOTHING HAPPENS . .":GOTO230
830 PRINT"  ALL OF A SUDDEN . .":PRINT"  YOU FIND YOURSELF FALLING . .":PRINT"  THRU A SECRET ENTRANCE . ."
840 FOR A=1TO1500:NEXTA:B=14:GOTO140
850 IF B<>11 THEN 870 ELSE IF O$="HOO" AND B(1,31)=0 THEN PRINT"A SMALL COMPARTMENT OPENS ! !":B(1,31)=11:B(1,21)=11:GOTO230
860 PRINT"NOTHING HAPPENS !":GOTO230
870 IF B=2 AND O$="CAB" AND B(1,12)=0 THEN PRINT"YOU SEE A SAFE !":B(1,12)=2:GOTO230
880 IF B=18 AND O$="LEV" THEN CLS:PRINT"THERE IS AN EXPLOSION . . .":GOTO1010
890 IF B=23 AND O$="BUT" THEN CLS:PRINT"THE PASSAGE BEGINS TO FLOOD . .":GOTO1010
900 FOR A=1TO36:IF O$=M$(1,A) THEN 920 ELSE NEXT A
910 GOTO 540
920 IF B(1,A)<>B AND B(1,A)<>25 THEN PRINT"THAT ITEM ISN'T AROUND":GOTO230
930 PRINT"O.K. . . NOTHING HAPPENS !":GOTO 230
940 IF B<>2 OR O$<>"SAF" OR B(1,12)<>2 THEN 1040
950 PRINT" TO  OPEN  THE SAFE  YOU  MUST":PRINT" USE AND ENTER A  THREE  NUMBER"
960 PRINT" COMBINATION.   ENTER  IT  WITH":PRINT" SLASHES BETWEEN THE  THREE #'S"
970 PRINT" YOU WISH TO USE.  FOR EXAMPLE:":PRINT" 27/14/82  . . . . IF YOU THINK"
980 PRINT" THE COMBINATION IS 27, 14, 82."
990 INPUT" YOUR ATTEMPT . .";M$: IF M$="13/82/30" THEN 1020
1000 CLS:PRINT"THERE IS AN EXPLOSION . .":PRINT:PRINT"THAT WAS THE WRONG COMBINATION!"
1010 PRINT:PRINT"Y O U   A R E   D E A D !":PRINT:INPUT"HIT ENTER TO RESTART";M$:RUN
1020 PRINT" THE SAFE POPS OPEN . . .":PRINT" TWO ITEMS FALL TO THE FLOOR !"
1030 B(1,12)=0:B(1,13)=2:B(1,14)=2:B(1,15)=2:M$(1,12)="q":GOTO230
1040 PRINT"SORRY, THAT'S NOT POSSIBLE !":GOTO 230
1050 PRINT"THOU SHALT NOT . .":GOTO230
1060 IF (B(1,30)=25 OR B(1,30)=B) AND O$="STA" THEN PRINT"IT BREAKS INTO 5,378 PIECES -":PRINT" REVEALING A MEDALLION !":B(1,30)=0:B(1,10)=B:GOTO230
1070 PRINT"SORRY, THAT'S NOT POSSIBLE !":GOTO230
1080 PRINT"TELL ME TO 'LOOK' OBJECT . .":PRINT"THEN YOU READ . . .":GOTO230
1090 IF B(1,35)=25 OR B(1,35)=1 THEN 1110
1100 PRINT"AT THE MOMENT: SCORE OF ZERO !":GOTO230
1110 IF B<>1 THEN PRINT"YOU MUST BE OUTSIDE TO SCORE !":GOTO230
1120 CLS:PRINT" C O N G R A T U L A T I O N S !":PRINT:PRINT" YOU HAVE WON !":PRINT:END
1130 FORA=1TO25:READL$(A),A(1,A),A(2,A),A(3,A),A(4,A):NEXTA
1140 DATA"OUTSIDE THE MANSION",0,0,0,0
1150 DATA"A LIBRARY",0,0,3,0
1160 DATA"AN ENTRANCE HALL",6,1,4,2
1170 DATA"A SITTING ROOM",7,0,0,3
1180 DATA"A SMALL BEDROOM",0,0,6,0
1190 DATA"A CORRIDOR",9,3,0,5
1200 DATA"A DINING ROOM",10,4,0,0
1210 DATA"A LARGE BEDROOM",11,0,9,0
1220 DATA"A CORRIDOR",12,6,10,8
1230 DATA"A KITCHEN",13,7,0,9
1240 DATA"A CLOSET",0,8,0,0
1250 DATA"A DEN",0,9,0,0
1260 DATA"A PANTRY",0,10,0,0
1270 DATA"A SECRET PASSAGE",15,12,0,0
1280 DATA"A PASSAGE",0,14,20,16
1290 DATA"IN A PASSAGE",17,0,15,0
1300 DATA"A PASSAGE",18,16,19,0
1310 DATA"A SMALL ROOM",0,17,0,0
1320 DATA"A PASSAGE TURN",0,20,21,17
1330 DATA"A MAZE-LIKE PASSAGE",19,0,0,15
1340 DATA"A TURN IN THE PASSAGE",0,22,0,19
1350 DATA"LOST IN A PASSAGE",21,23,0,20
1360 DATA"A CONFUSING PASSAGE",22,24,0,0
1370 DATA"A TREASURE ROOM",23,0,0,0
1380 DATA"YOU",0,0,0,0
1390 FORA=1TO36:READM$(1,A),M$(2,A),M$(3,A),B(1,A),B(2,A),B(3,A):NEXTA
1400 DATA"DOO","A LOCKED DOOR","IT APPEARS SOLID!",1,0,1
1410 DATA"DOO","AN OPEN DOOR","Z",0,0,1
1420 DATA"BAR","A METAL BAR","WRITING: 4.U.2. PRY",25,1,1
1430 DATA"PAP","A SHEET OF PAPER","WRITING: SAFE COMBO/ATOMIC #'S",25,1,1
1440 DATA"MOU","A MOUSE","IT IS WATCHING YOU !",3,0,1
1450 DATA"SPI","A LARGE SPIDER","IT APPEARS DANGEROUS!",10,0,1
1460 DATA"COB","COBWEBS . . .","Z",7,0,1
1470 DATA"TRE","A COAT TREE","WRITING: I WAS ONCE AN OAK!",3,1,1
1480 DATA"SOF","A SHABBY SOFA","Z",4,0,1
1490 DATA"MED","A LEAD MEDALLION","WRITING:  LEAD A# 82",0,1,1
1500 DATA"CAB","A DINGY CABINET","IT IS EMPTY . . .",2,0,1
1510 DATA"SAF","A LOCKED SAFE","WRITING: VERYSTRONG CO.",0,0,1
1520 DATA"SAF","AN OPEN SAFE","Z",0,0,1
1530 DATA"NOT","A NOTEBOOK","WRITING:  KSED KCIK",0,1,1
1540 DATA"KEY","A SMALL KEY","WRITING: INSERT",0,1,1
1550 DATA"RUG","A TATTERED RUG","Z",5,1,1
1560 DATA"TOK","A ZINC TOKEN","WRITING:  ZINC A# 30",5,1,0
1570 DATA"TAB","AN OLD TABLE","CARVED ON IT: OTKK ENTQ",7,0,1
1580 DATA"DRE","A RUN-DOWN DRESSER","IT IS EMPTY . .",8,0,1
1590 DATA"HOO","WALL HOOKS (1 TO 4)","Z",11,0,1
1600 DATA"COI","AN ALUMINUM COIN","WRITING: ALUMINUM A# 13",0,1,0
1610 DATA"COU","A COUNTER","YOU SEE A KNIFE ON IT !",10,0,1
1620 DATA"KNI","A KNIFE","WRITING: TV WONDERBLADE",10,1,0
1630 DATA"SHE","A SET OF SHELVES","YOU SEE A FLASHLIGHT !",13,0,1
1640 DATA"FLA","A FLASHLIGHT","IMPRINTED: NEVEREADY CO.",13,1,0
1650 DATA"BUC","AN EMPTY BUCKET","WRITING: EL-CHEAPO PLASTIC CO.",13,1,1
1660 DATA"WAT","A WATCH","WRITING: TIME FOR COFFEE !",15,1,1
1670 DATA"FIL","A NAIL FILE","Z",20,1,1
1680 DATA"DES","A DESK","IT IS IN POOR CONDITION !",12,0,1
1690 DATA"STA","A CHEAP STATUE","IMPRINTED: MADE IN JAPAN",4,1,1
1700 DATA"COM","A SMALL COMPARTMENT","YOU SEE: AN ALUMINUM COIN",0,0,1
1710 DATA"LEV","A BLACK LEVER","WRITING: EP OPU QVMM",18,0,1
1720 DATA"BUT","A RED BUTTON","WRITING: EFBUI !",23,0,1
1730 DATA"SLO","A KEY SIZE SLOT","WRITING: HMRDQS JDX EHQRS",24,0,1
1740 DATA"DIA","**A DIAMOND**","IT IS FLAWLESS . . HUGE !",24,1,1
1750 DATA"SIG","A SIGN","ADVENTURES ARE FUN ! ?",14,0,1
1760 INPUT" HIT ENTER TO START . . READY";M$:RETURN
1770 PRINT@40,"M A N S I O N":PRINT
1780 PRINT" SOME SAY  IT'S HAUNTED . . . .":PRINT" OTHERS SAY IT'S CURSED . . . ."
1790 PRINT" WHATEVER THE TRUTH . . . . . .":PRINT" NO ONE  HAS EVER RECOVERED THE"
1800 PRINT" FABLED MANSION DIAMOND ! !":PRINT
1810 PRINT" YOUR TASK IS TO ENTER THE MAN-":PRINT" SION,  FIND THE  DIAMOND,  AND":PRINT" THEN GET OUT SAFELY."
1820 PRINT:INPUT" HIT ENTER . . . TO CONTINUE";O$
1830 CLS:PRINT:PRINT" MAKE YOUR QUEST  USING  ONE OR":PRINT" TWO WORD COMMANDS, SUCH AS:"
1840 PRINT:PRINT" LOOK,  GO NORTH,  TAKE STATUE,":PRINT" BREAK STATUE, HELP, INVENTORY,"
1850 PRINT" LOOK CABINET, ETC.":PRINT
1860 PRINT" IF ONE COMMAND  DOESN'T  WORK,":PRINT" THEN TRY ANOTHER.":PRINT
1870 PRINT"     G O O D   L U C K ! !":PRINT:RETURN
5000 REM COPYRIGHT (C) MICRODEAL 1982
5050 REM JOHN OLSON
5060 REM HOXIE, KS  67740
5070 REM AUGUST, 1982
5080 REM MANSION ADVENTURE
5090 REM MC-10 EDITS JIM GERRIE
