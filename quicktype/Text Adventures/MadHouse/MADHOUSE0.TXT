10 REM 
12 REM MAISON DU FOU - PROGRAMME BASIC 
13 REM
14 REM Auteurs : DIEUDONNE P. NOEL H.
15 REM Magazine MICRO 7 mars 1984
16 REM Adapt. Apple II : D. RIOUAL 2017
17 REM 
10 REM
20 REM Liste des variables
21 REM *******************
22 REM AO  : Armoire Ouverte
23 REM CO  : Clef en OR
24 REM CP  : Clef Petite
25 REM LP  : Lampe de Poche
26 REM LAA : Lampe Allumee
27 REM MH  : Monte-charge Haut (remplace MCH)
28 REM MB  : Monte-charge Bas  (remplace MCB)
100 REM
110 TEXT: HOME
111 PRINT TAB(12);:INVERSE:PRINT" LA MAISON DU FOU ":NORMAL
112 GOSUB 11000
113 PRINT"Auteurs: DIEUDONNE P. NOEL H.":PRINT"         Magazine MICRO 7 mars 1984"
114 GET I$
115 FOR I=1 TO 21:PRINT:NEXT I
116 VTAB 4:PRINT"Adapte pour l'Apple II par":PRINT"Dominique RIOUAL en juin 2017"
119 DIM B$(21):A$="    D'accord"
120 FOR I=1 TO 21:READ B$(I):NEXT I
121 DATA " 1.Vous allez au nord"," 2.Vous allez au sud"," 3.Vous allez a l'est"
122 DATA " 4.Vous allez a l'ouest"," 5.Vous montez les escaliers"
123 DATA " 6.Vous descendez les escaliers"
124 DATA " 7.Vous attendez"," 8.Vous appuyez sur le bouton vert"
125 DATA " 9.Vous appuyez sur le bouton rouge","10.Vous ne faites rien"
126 DATA "11.Vous partez","12.Vous entrez dans la maison"
127 DATA "13.Vous allez au nord-est","14.Vous allez au centre-est"
128 DATA "15.Vous allez au sud-est","16.Vous allez au nord-ouest"
129 DATA "17.Vous allez au centre-ouest","18.Vous allez au sud-ouest"
130 DATA "19.Vous appuyez sur le bouton Montee"
131 DATA "20.Vous appuyez sur le bouton Descente"
132 DATA "21.Vous prenez la clef"
133 AO=0:CO=0:CP=0:LP=0:LAA=0
135 PRINT:PRINT:PRINT:PRINT:PRINT:INVERSE:PRINT "INSTRUCTIONS POUR EXECUTER UNE ACTION"
136 PRINT:NORMAL:PRINT "Entrez le nombre qui se trouve devant":PRINT"l'action."
140 PRINT:PRINT:PRINT "Appuyez sur CR pour continuer"
150 PRINT:INPUT I$
160 HOME:PRINT "Vous etes devant une vieille maison":PRINT "abandonnee.":PRINT:PRINT
165 PRINT B$(10):PRINT B$(11):PRINT B$(12):PRINT
170 GOSUB 10000
180 IF INT(I)<>I OR I<10 THEN 170
190 ON I-9 GOTO 191,191,200:GOTO 170
191 HOME:PRINT "Vous avez raison,la curiosite est un":PRINT "vilain defaut."
192 END
200 HOME:PRINT "La porte se referme derriere vous,":PRINT "impossible de l'ouvrir."
201 FOR I=1 TO 3000:NEXT I
220 HOME: PRINT "Vous etes dans l'entree, il y a une":PRINT "porte a l'est et a l'ouest."
230 PRINT:PRINT B$(3):PRINT B$(4):PRINT
231 GOSUB 10000
232 IF INT(I)<>I OR I<3 THEN 231
233 ON I-2 GOTO 250,650:GOTO 231
250 HOME:PRINT "Vous etes dans un hall."
251 PRINT "Il y a une porte au nord et a l'ouest,"
252 PRINT "et des escaliers qui montent a l'est."
260 PRINT:PRINT B$(1):PRINT B$(4):PRINT B$(5):PRINT
261 INPUT"Que faites-vous ?";I
262 IF INT(I)<>I OR I<1 THEN 261
263 ON I GOTO 300,261,261,220,350:GOTO 261
300 HOME : PRINT "Vous etes dans une penderie."
301 PRINT "Il y a une porte a l'est, au nord et au sud."
310 PRINT:PRINT B$(1):PRINT B$(2):PRINT B$(3):PRINT
311 GOSUB 10000
312 IF INT(I)<>I OR I<1 THEN 311
313 ON I GOTO 1300,250,500:GOTO 311
350 HOME : PRINT "Vous etes dans un long couloir.Il y a:"
351 PRINT "3 portes a l'est: nord,centre,sud."
352 PRINT "3 portes a l'ouest:nord,centre,sud."
353 PRINT "Une porte au nord et au sud et des escaliers qui descendent a l'ouest."
354 PRINT "Il y a aussi un monte-charges avec "
355 PRINT "un bouton Montee et un bouton Descente."
356 IF MH=0 THEN 360
357 PRINT "Le plateau du monte-charges est la."
358 IF CO=0 THEN PRINT"**Il y a une clef en OR"
359 GOTO 361
360 PRINT"Le plateau du monte-charges n'est pas la"
361 IF MH=1 AND CO=0 THEN PRINT B$(21)
362 Z=Z+1
364 PRINT B$(1):PRINT B$(2):PRINT B$(6):PRINT B$(13):PRINT B$(14):PRINT B$(15)
365 PRINT B$(16):PRINT B$(17):PRINT B$(18):PRINT B$(19):PRINT B$(20):PRINT
366 GOSUB 10000
367 IF INT(I)<>I OR I<1 THEN 366
368 ON I GOTO 600,370,366,366,366,250:IF I<13 THEN 366
369 ON I-12 GOTO 1200,1220,400,1240,1260,1280,379,377,373:GOTO 366
370 HOME:PRINT "Impossible,la porte ne s'ouvre pas"
371 IF CP=1 OR CO=1 THEN 380
372 FOR I=1 TO 2000:NEXT I:GOTO 350
373 IF MH=0 OR CO=1 THEN 366
374 PRINT:PRINT A$ : CO=1
375 FOR I=1 TO 2000:NEXT I:GOTO 350
377 PRINT:PRINT A$:MH=0:MB=1:GOTO 375
379 PRINT:PRINT"Impossible,le bouton ne s'enfonce pas":GOTO 375
380 PRINT:IF CO=1 THEN PRINT" 1.Vous ouvrez avec votre clef en OR"
381 IF CP=1 THEN PRINT" 2.Vous ouvrez avec votre petite clef"
382 PRINT ".3 Vous retournez dans le couloir"
383 PRINT:INPUT"Que faites-vous ?";I
384 IF INT(I)<>I OR I<1 OR I>3 THEN 383
385 ON I GOTO 386,390,350
386 IF CO=0 THEN GOTO 383
387 PRINT:PRINT"Impossible,elle n'entre pas dans la serrure"
388 FOR I=1 TU 2000:NEXT I:GOTO 383
390 IF CP=0 THEN GOTO 283
391 PRINT:PRINT"La clef ouvre la porte,mais vous decouvrez un mur derriere elle"
392 GOTO 388
400 HOME: PRINT "Vous etes dans une petite piece,il y a"
401 PRINT"des escaliers qui montent au nord,":PRINT"une porte a l'ouest et une a l'est"
410 PRINT:PRINT B$(3):PRINT B$(4):PRINT B$(5):PRINT
411 GOSUB 10000
412 IF INT(I)<>I OR I<3 THEN GOTO 411
413 ON I-2 GOTO 415,350,950:GOTO 411
415 HOME : PRINT "En appuyant sur la clanche,vous ouvrez"
416 PRINT"une trappe sous vos pieds et vous ":PRINT"tombez dans un trou profond."
417 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
418 INVERSE:PRINT"VOUS MOUREZ !":NORMAL
419 END
450 HOME : PRINT "Vous etes dans un grand garage."
451 PRINT"Il y a une porte a l'est et au nord."
452 PRINT"Il y a aussi un disjoncteur avec une":PRINT "inscription :":
453 PRINT TAB(8);
454 INVERSE:PRINT" LIGNE HT DU TOIT ":NORMAL
455 PRINT:PRINT"A cote,il y a un bouton vert et un":PRINT "bouton rouge."
460 PRINT:PRINT B$(1):PRINT B$(3):PRINT B$(8):PRINT B$(9):PRINT
461 GOSUB 10000
462 IF INT(I)<>I OR I<1 THEN GOTO 461
463 ON I GOTO 1050,461,700,461,461,461,461,467,469:GOTO 461
467 PRINT:PRINT A$:HT=0:GOTO 461
469 PRINT:PRINT A$:HT=1:GOTO 461
500 HOME: PRINT "Vous etes dans un petit couloir."
501 PRINT "Il y a une porte a l'est, une a l'ouest et une au nord."
510 PRINT:PRINT B$(1):PRINT B$(3):PRINT B$(4):PRINT
511 GOSUB 10000
512 IF INT(I)<>I OR I<1 THEN GOTO 511
513 ON I GOTO 1100,511,550,300:GOTO 511
550 HOME : PRINT "Vous etes dans une cuisine."
551 PRINT "Il y a une porte a l'est, une a l'ouest":PRINT "et une au nord."
552 PRINT"Il y a aussi un monte-charges, avec":PRINT "un bouton Montee et un bouton Descente."
553 IF MB=0 THEN 557
554 PRINT"Le plateau du monte-charges est la."
555 IF CO=0 THEN PRINT"**Il y a une clef en OR."
556 GOTO 558
557 PRINT"Le plateau du monte-charges n'est pas la"
558 PRINT:IF MB=1 AND CO=0 THEN PRINT B$(21)
560 PRINT B$(1):PRINT B$(3):PRINT B$(4):PRINT B$(19):PRINT B$(20):PRINT
561 GOSUB 10000
562 IF INT(I)<>I OR I<1 THEN GOTO 561
563 ON I GOTO 1300,561,1000,500:IF I<19 THEN GOTO 561
564 ON I-18 GOTO 570,573,565:GOTO 561
565 IF MB=0 OR CO=1 THEN GOTO 561
566 PRINT:PRINT A$:CO=1
567 FOR I=1 TO 2000:NEXT I:GOTO 550
570 PRINT:PRINT A$:MB=0:MH=1:GOTO 567
573 PRINT:PRINT"Impossible,le bouton ne s'enfonce pas.":GOTO 567
600 HOME : PRINT "Vous etes dans une chambre."
601 PRINT"Il y a une porte a l'ouest et au sud":PRINT"et des escaliers qui descendent a l'est."
610 PRINT:PRINT B$(2):PRINT B$(4):PRINT B$(6):PRINT
611 INPUT"Que faites-vous ?";I
612 IF INT(I)<>I OR I<2 THEN GOTO 611
613 ON I-1 GOTO 350,611,850,611,800:GOTO 611
650 HOME : PRINT "Vous etes dans une petite piece."
651 PRINT"Il y a une porte a l'est et une au nord":PRINT"et des escaliers qui descendent a"
652 PRINT"l'ouest."
660 PRINT:PRINT B$(1):PRINT B$(3):PRINT B$(6):PRINT
661 GOSUB 10000
662 IF INT(I)<>I OR I<1 THEN GOTO 661
663 ON I GOTO 750,661,220,661,661,700:GOTO 661
700 HOME : PRINT "Vous etes dans un couloir,il y a des"
701 PRINT"escaliers qui montent a l'est,une porte a l'est,une a l'ouest et une au nord."
705 Z=Z+2
710 PRINT:PRINT B$(1):PRINT B$(3):PRINT B$(4):PRINT B$(5):PRINT
711 GOSUB 10000
712 IF INT(I)<>I OR I<1 THEN GOTO 711
713 ON I GOTO 1000,711,900,450,650:GOTO 711
750 HOME:PRINT "Vous etes dans un grand salon."
751 PRINT"Il y a une porte au sud et une a l'est.":PRINT "Il y a aussi un bar dans le coin":PRINT "nord-ouest."
759 PRINT:PRINT" 1.Vous allez au bar vous desalterer"
760 PRINT B$(2):PRINT B$(3):PRINT
761 GOSUB 10000
762 IF INT(I)<>I OR I<1 THEN GOTO 761
763 ON I GOTO 764,650,1100:GOTO 761
764 IF CP=1 THEN GOTO 773
765 PRINT:PRINT"Au milieu des bouteilles, vous voyez une petite clef."
766 PRINT:PRINT"Vous la prenez(1) ou pas(0) ";
767 INPUT I
768 IF I=1 THEN GOTO 771
769 IF I<>0 THEN GOTO 767
770 PRINT:PRINT A$:GOTO 772
771 PRINT:PRINT A$:CP=1
772 FOR I=1 TO 1000:NEXT I:GOTO 750
773 PRINT:PRINT"Vous choisissez une des nombreuses"
774 PRINT"bouteilles pour vous desalterer."
775 FOR I=1 TO 2000:NEXT I:GOTO 750
800 HOME : PRINT "Vous etes dans un couloir."
801 PRINT"il y a des escaliers qui montent a":PRINT"l'ouest,une porte au sud et une a l'est."
810 PRINT:PRINT B$(2):PRINT B$(3):PRINT B$(5):PRINT
811 GOSUB 10000
812 IF INT(I)<>I OR I<2 THEN 811
813 ON I-1 GOTO 900,1300,811,600:GOTO 811
850 HOME : PRINT "Vous etes dans une salle de bains."
851 PRINT"Il y a une porte a l'ouest,une a l'est":PRINT"et une au sud."
860 PRINT:PRINT B$(2):PRINT B$(3):PRINT B$(4):PRINT
861 GOSUB 10000
862 IF INT(I)<>I OR I<2 THEN 861
863 ON I-1 GOTO 1100,600,1150:GOTO 861
900 HOME:PRINT "Vous etes dans une salle de projection."
901 PRINT"Il y a une porte au nord,une au sud et":PRINT"une a l'ouest."
910 PRINT:PRINT B$(1):PRINT B$(2):PRINT B$(4):PRINT
911 GOSUB 10000
912 IF INT(I)<>I OR I<1 THEN GOTO 911
913 ON I GOTO 800,1000,911,700:GOTO 911
950 HOME : PRINT "Vous etes sur un toit plat."
951 PRINT"Une barriere electrique H.T. en fait le tour et des escaliers en descendent":PRINT"au sud."
960 PRINT:PRINT B$(1):PRINT B$(2):PRINT B$(3):PRINT B$(4):PRINT B$(6):PRINT
961 GOSUB 10000
962 IF INT(I)<>I OR I<1 THEN GOTO 961
963 ON I GOTO 964,964,964,964,961,400:GOTO 961
964 IF HT=1 THEN GOTO 966
965 HOME:PRINT "Vous vous electrocutez.":GOTO 417
966 PRINT:PRINT"Il y a des pointes sur le mur qui":PRINT"entoure la maison."
967 PRINT:PRINT" 1.Vous sautez du toit en prenant de":PRINT"   nombreux risques."
968 PRINT B$(6):PRINT
969 GOSUB 10000
970 IF I=6 THEN GOTO 400
971 IF I<>1 THEN GOTO 969
972 IF Z=10 THEN GOTO 975
973 HOME: PRINT "Vous tombez sur les pointes qui":PRINT"entourent la maison."
974 GOTO 417
975 HOME : PRINT "Vous avez eu de la chance,votre saut"
976 PRINT"a ete assez important pour franchir les pointes."
977 PRINT"Malheureusement,vous vous etes casse la jambe en tombant mal."
978 PRINT:PRINT"Enfin,vous vous en etes sorti et c'est":PRINT"le principal..."
979 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT TAB(30);
980 INVERSE:PRINT"BRAVO !":NORMAL
981 END
1000 HOME:PRINT "Vous etes dans un auditorium."
1001 PRINT"Il y a une porte au nord,une au sud et":PRINT"une a l'ouest."
1010 PRINT:PRINT B$(1):PRINT B$(2):PRINT B$(4):PRINT
1011 GOSUB 10000
1012 IF INT(I)<I OR I<1 THEN GOTO 1011
1013 ON I GOTO 900,700,1011,550:GOTO 1011
1050 HOME:PRINT "Vous etes dans un atelier."
1051 PRINT"Il y a une porte au sud."
1060 PRINT:PRINT B$(2):PRINT
1061 GOSUB 10000
1062 IF I=2 THEN GOTO 450
1063 GOTO 1061
1100 HOME:PRINT "Vous etes sur un escalier."
1101 PRINT"Il y a une porte en haut au nord et"
1102 PRINT"2 portes en bas : au sud et a l'ouest."
1110 PRINT:PRINT B$(1):PRINT B$(2):PRINT B$(4):PRINT
1111 INPUT"Que faites-vous ?";I
1112 IF INT(I)<>I OR I<1 THEN GOTO 1111
1113 ON I GOTO 850,500,1111,750:GOTO 1111
1150 HOME:PRINT "Vous etes dans une chambre ou il y a une porte a l'est."
1152 PRINT"Il y a aussi une armoire a l'ouest."
1153 IF AO=0 THEN GOTO 1159
1154 PRINT"L'armoire est ouverte."
1155 PRINT:PRINT" 1.Vous regardez dans l'armoire"
1156 GOTO 1160
1159 PRINT:PRINT" 1.Vous essayez d'ouvrir l'armoire"
1160 PRINT B$(3):PRINT
1161 GOSUB 10000
1162 IF I=3 THEN GOTO 850
1163 IF I<>1 THEN 1161
1165 IF AO=1 THEN 1320
1166 PRINT:PRINT"Impossible de l'ouvrir"
1167 IF CO=0 AND CP=0 THEN GOTO 1161
1168 PRINT:IF CO=1 THEN PRINT" 1.Vous essayez d'ouvrir la porte avec votre clef en OR"
1169 IF CP=1 THEN PRINT" 2.Vous essayez d'ouvrir la porte avec votre petite clef"
1170 PRINT B$(3):PRINT
1171 GOSUB 10000
1172 IF INT(I)<>I OR I<1 THEN 1171
1173 ON I GOTO 1319,1174,850:GOTO 1171
1174 IF CP=0 THEN 1171
1175 HOME:PRINT "impossible de l'ouvrir,la clef ne":PRINT"convient pas..."
1176 FOR I=1 TO 2000:NEXT I
1177 GOTO 1150
1200 HOME:PRINT "Vous etes dans une chambre.":PRINT"Il y a une porte a l'ouest."
1201 PRINT"Il y a aussi un telephone sur une table."
1210 PRINT:PRINT B$(4):PRINT" 5.Vous essayez d'appeler police-secours":PRINT
1211 GOSUB 10000
1212 IF I=4 THEN 350
1213 IF I<>5 THEN 1211
1214 PRINT:PRINT"En decrochant le telephone,vous":PRINT"n'entendez aucune tonalite."
1215 PRINT"En regardant sous la table,vous voyez":PRINT"que les fils sont arraches."
1216 FOR I=1 TO 3000:NEXT I:GOTO 1200
1220 HOME:PRINT "Vous etes dans une chambre,il y a une":PRINT"porte a l'ouest."
1221 PRINT"Il y a aussi une commode avec 6 tiroirs."
1225 PRINT:PRINT B$(4):PRINT" 5.Vous cherchez dans les tiroirs":PRINT
1226 GOSUB 10000
1227 IF I=4 THEN 350
1228 IF I<>5 THEN 1226
1229 IF LP=0 THEN 1231
1230 PRINT:PRINT A$:GOTO 1226
1231 PRINT:PRINT"En ouvrant le tiroir en bas a droite,":PRINT"vous trouvez une lampe de poche."
1232 PRINT:PRINT"Vous la prenez(1) ou pas(2) ."
1233 PRINT:GOSUB 10000
1234 IF I=1 THEN GOTO 1238
1235 IF I<>2 THEN GOTO 1233
1236 PRINT:PRINT A$
1237 FOR I=1 TO 2000:NEXT I:GOTO 1220
1238 PRINT:PRINT A$:LP=1:GOTO 1237
1240 HOME:PRINT "Vous etes dans une chambre,il y a une":PRINT"porte a l'est."
1250 PRINT:PRINT B$(3):PRINT B$(7):PRINT
1251 GOSUB 10000
1252 IF I=3 THEN 350
1253 IF I<>7 THEN 1251
1254 PRINT "Le temps passe..."
1255 FOR I=1 TO 10000:NEXT I
1256 GOTO 1251
1260 HOME:PRINT "Vous etes dans une salle de bains,":PRINT"il y a une porte a l'est."
1270 PRINT:PRINT B$(3):PRINT B$(7):PRINT
1271 GOSUB 10000
1272 IF I=3 THEN 350
1273 IF I<>7 THEN 1271
1274 PRINT "On s'embete ici, non ?"
1275 FOR I=1 TO 8000:NEXT I
1276 GOTO 1271
1280 HOME:PRINT "Vous etes devant les WC,vous pouvez"
1281 PRINT"soit entrer vous soulager(1)":PRINT"ou rester dans le couloir(0)."
1285 PRINT:GOSUB 10000
1286 IF I=0 THEN 350
1287 IF I<>1 THEN 1285
1288 HOME:PRINT "En entrant dans les WC,vous pouvez lire":PRINT"ce message sur la porte:":PRINT
1289 INVERSE:PRINT"IL FAUT BEAUCOUP D'AMPOULES POUR":PRINT "ECLAIRER NEW-YORK,MAIS UNE LAMPE DE"
1290 PRINT"POCHE PERMET DE SORTIR DE CETTE MAISON.":NORMAL
1291 FOR I=1 TO 10000:NEXT I
1292 HOME:PRINT "Vous retournez dans le couloir apres":PRINT"vous etre soulage."
1293 FOR I=1 TO 2500:NEXT I:GOTO 350
1300 HOME:PRINT "Vous etes dans une salle-a-manger."
1301 PRINT"Il y a une porte a l'ouest et 2 au sud:":PRINT"une a l'est et une a l'ouest."
1310 PRINT:PRINT B$(4):PRINT B$(15):PRINT B$(18):PRINT
1311 GOSUB 10000
1312 IF I=4 THEN GOTO 800
1313 IF I=15 THEN GOTO 550
1314 IF I=18 THEN GOTO 300
1315 GOTO 1311
1318 REM Derriere l'armoire
1319 IF CO=0 THEN GOTO 1171:AO=1
1320 HOME:PRINT "Derriere le faux fond de l'armoire se":PRINT"trouve une grotte sombre."
1321 PRINT"Des bruits etranges d'animaux sauvages":PRINT"vous font tressaillir."
1322 PRINT"Mais,le jour vous apparait deja la-bas,":PRINT"tout au fond de la grotte."
1328 PRINT:PRINT" 1. Vous avancez":PRINT" 2. Vous retournez dans la chambre."
1329 IF LP=1 AND LAA=0 THEN PRINT" 3. Vous allumez la lampe de poche"
1330 PRINT:GOSUB 10000
1331 IF INT(I)<>I OR I<1 THEN 1330
1332 ON I GOTO 1333,1150,1350:GOTO 1330
1333 IF LAA=1 THEN 1335
1334 HOME:PRINT "Vous tombez dans un trou.":GOTO 417
1335 HOME:PRINT "Vous voyez apparaitre un trou devant":PRINT"vous que vous contournez..."
1336 FOR I=1 TO 3000:NEXT I
1337 PRINT:PRINT"Vous continuez ensuite d'avancer."
1338 PRINT"Les rats vous font encore quelques":PRINT"frayeurs."
1339 FOR I=1 TO 2500:NEXT I
1340 PRINT:PRINT"Mais,finalement,le jour se rapproche et vous etes bientot libre."
1341 FOR I=1 TO 2000:NEXT I
1342 PRINT:PRINT"Ca y est,vous vous en etes tout de meme sorti."
1343 PRINT:PRINT:PRINT:PRINT:PRINT TAB(10);
1344 INVERSE :PRINT"TRIPLES BRAVOS !!!!!!!!!!!":NORMAL
1345 END
1350 IF LP=0 THEN GOTO 1330
1351 PRINT:PRINT A$
1352 FOR I=1 TO 1500:NEXT I:LAA=1:GOTO 1320
9998 REM ----------------------
9999 REM Sub de saisie du choix
10000 INPUT"---> QUE FAITES VOUS ?";I
10010 RETURN
10999 REM AFFICHAGE INTRO ASCII ART
11000 PRINT "                            ===="
11001 PRINT "                            !!!!"
11002 PRINT "       =========================="
11003 PRINT "     %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
11004 PRINT "   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
11005 PRINT " %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
11006 PRINT "   ||                              ||"
11007 PRINT "   ||      _____          _____    ||"
11008 PRINT "   ||      | | |          | | |    ||"
11009 PRINT "   ||      |-|-|          |-|-|    ||"
11010 PRINT "   ||      #####          #####    ||"
11011 PRINT "   ||                              ||"
11012 PRINT "   ||                              ||"
11013 PRINT "   ||      _____   ____   _____    ||"
11014 PRINT "   ||      | | |   @@@@   | | |    ||"
11015 PRINT "   ||      |-|-|   @@@@   |-|-|    ||"
11016 PRINT "   ||      #####   @@*@   #####    ||"
11017 PRINT "   ||              @@@@            ||"
11018 PRINT " ******************____****************"
11019 PRINT " **************************************"
11030 RETURN
