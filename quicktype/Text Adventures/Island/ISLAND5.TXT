0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
10 CLS
15 GOSUB5000
20 GOSUB2820
30 D=R:IFR=20THEND=RND(80)
40 GOSUB650:GOSUB2770:PRINT"ISLAND OF SECRETS ";:M$="TIME LEFT"+LEFT$(STR$(L)+"   ",5):GOSUB7
50 PRINTG$"STRENGTH=";INT(Y),"WISDOM="X:PRINTG$;
60 M$="YOU ARE "+I$(VAL(LEFT$(A$,1)))+" ":GOSUB1:GOSUB720:N=0
70 FORI=1TOC4
80 C=0:READY$
90 IFL(I)=RANDL(M+I)<1THENC=1
100 IFN>0AND C=1THENA$=A$+","
110 IFC=1THENA$=A$+" "+Y$:N=N+1
120 NEXTI
130 IFN>0THENA$="*YOU SEE"+A$:GOSUB720
140 PRINTG$;F$
150 PRINT"WHAT WILL YOU DO";
160 INPUTE$
170 C$="":X$="":A=0:O=52:LI=LEN(E$)
180 FORI=1TOLI
190 IFMID$(E$,I,1)=" "ANDC$=""THENC$=LEFT$(E$,I-1)
200 IFMID$(E$,I+1,1)<>" "ANDC$>""THENX$=RIGHT$(E$,LI-I):I=LI
210 NEXTI
220 IFX$=""THENC$=E$
230 IFLEN(C$)<3THENC$=C$+"???"
240 FORI=1TOV
250 IFLEFT$(C$,3)=MID$(V$,3*(I-1)+1,3)THENA=I
260 NEXTI
270 GOSUB760
280 B$="":IFA=0THENA=V+1
290 IFX$="???"THENF$="MOST ACTIONS NEED TWO WORDS"
300 IFA>VORO=52THENF$=W$+C$+" "+X$
310 IFA>VANDO=52THENF$="WHAT!"
320 L=L-1:Y=Y-(E/C4+.1)
330 GOSUB4500
340 ONINT(A/10)+1GOSUB 590,600,610,620,630,630,640
350 IFR=61THENX=X-RND(2)+1
360 IFR=14ANDRND(3)=1THENY=Y-1:F$="YOU ARE BITTEN"
370 IFL(M+36)<1ANDR<>L(M+22)THENL(M+36)=L(M+36)+1:L(36)=R:Y=Y-1
380 IFR<>L(16)ANDL(16)>0THENL(16)=1+RND(4)
390 IFR<>L(39)THENL(39)=10*(RND(5)-1)+7+RND(3)
400 IFR=L(39)ANDR<>L(43)ANDL(M+13)>-1THENY=Y-2:X=X-2
410 IFR<78THENL(32)=76+RND(2)
420 IFR=33ORR=57ORR=73ANDRND(2)=1THENL(25)=R
430 IFR=L(32)ANDRND(2)=1ANDL(M+32)=0THENGOSUB1310
440 IFR=19ANDY<70ANDL(M+43)=0ANDRND(4)=1THENF$="PUSHED INTO THE PIT":L(M+W)=1
450 IFR<>L(41)THENL(41)=21+(RND(3)*10)+RND(2)
460 IFR=L(41)THENL(M+41)=L(M+41)-1:IFL(M+41)<-4THENGOSUB1230
470 IFL(M+43)=0THENL(43)=R
480 IFL(43)<18ANDR<>9ANDR<>10ANDL(M+W-2)<1THENGOSUB1330
490 IFR=18THENY=Y-1
500 IFY<50THENO=RND(9):GOSUB1530:IFL(O)=RTHENF$="YOU DROP SOMETHING"
510 IFL<900ANDR=23ANDL(M+36)>0ANDRND(3)=3THENGOSUB1360
520 IFR=47ANDL(M+8)>0THENF$=F$+" YOU CAN GO NO FURTHER"
530 IFL(M+8)+L(M+11)+L(M+13)=-3THENL(M+W)=1:GOSUB2800
540 IFL(M+W)=0ANDL>0ANDY>1ANDX>1THEN30
550 IFL<1ORY<1THENF$="YOU HAVE FAILED, THE EVIL ONE SUCCEEDS"
560 PRINT:PRINTF$:PRINT"YOUR FINAL SCORE=";INT(X+Y+(ABS(L/7*(L<640))))
570 PRINT:PRINT:PRINT"GAME OVER"
580 END
590 ONAGOSUB810,810,810,810,810,1080,1080,1390,1530:RETURN
600 ONA-9GOSUB1540,1630,1670,1710,1730,1080,1760,1760,1760,1760:RETURN
610 ONA-19GOSUB1820,1820,1820,1820,1910,2100,2210,2270,2270,1080:RETURN
620 ONA-29GOSUB2500,2500,2300,2300,2330,2350,2400,2400,2470,2540:RETURN
630 ONA-39GOSUB2600,2600,2720,640
640 RETURN
650 RESTORE
660 FORI=1TOD:READA$:NEXTI
680 D$=RIGHT$(A$,4):A$=LEFT$(A$,LEN(A$)-4)
690 IFR=39THEND$=MID$("101110100",RND(5),4)
700 IFR=20THEND$="1110"
710 GOSUB2780:RETURN
720 M$=MID$(A$,2)+".":GOSUB1:A$="":RETURN
760 IFLEN(X$)<3THENX$=X$+"???"
770 FORI=1TOW
780 IFLEFT$(X$,3)=MID$(Z$,3*(I-1)+1,3)THENO=I
790 NEXTI:IFO=0THENO=52
800 RETURN
810 D=0:C=0:IFO=52THEND=A
820 IFO>C4ANDO<WTHEND=O-C4
830 IFB$="500012"ORB$="500053"ORB$="500045"THEND=4
840 IFB$="500070"ORB$="500037"ORB$="510011"ORB$="510041"THEND=1
850 IFB$="510043"ORB$="490066"ORB$="490051"THEND=1
860 IFB$="510060"ORB$="480056"THEND=2
870 IFB$="510044"ORB$="510052"THEND=3
880 IFB$="490051"ANDL(M+29)=0THENGOSUB2110:RETURN
890 IFR=L(39)AND(X+Y<180ORR=10)THENF$=W$+"LEAVE!":RETURN
900 IFR=L(32)ANDL(M+32)<1ANDD=3THENF$="HE WILL NOT LET YOU PAST":RETURN
910 IFR=47ANDL(M+44)=0THENF$="THE ROCKS MOVE TO PREVENT YOU":RETURN
920 IFR=28ANDL(M+7)<>1THENF$="THE ARMS HOLD YOU FAST":RETURN
930 IFR=45ANDL(M+40)=0ANDD=4THENF$="HISSSS!":RETURN
940 IFR=25ANDL(M+16)+L(16)<>-1ANDD=3THENF$="TOO STEEP TO CLIMB":RETURN
950 IF R=51ANDD=3THENF$="THE DOOR IS BARRED!":RETURN
960 IFD>0THENIFMID$(D$,D,1)="0"THENR=R+VAL(MID$("-10+10+01-01",D*3-2,3)):C=1
970 F$="OK"
980 IFD<1ORC=0THENF$=W$+"GO THAT WAY"
990 IFR=33ANDL(16)=0THENL(16)=RND(4):L(M+16)=0:F$="THE BEAST RUNS AWAY!"
1000 IFR<>L(25)ORO<>25THENRETURN
1010 F$="":A$="#YOU BOARD THE CRAFT "
1020 IF X<60 THEN A$=A$+S$
1030 A$=A$+T$
1040 GOSUB2740:GOSUB2760:G2760
1050 IFX<60THENA$="#TO SERVE OMEGAN FOREVER!":L(M+W)=1
1060 IFX>59THENA$="#THE BOAT SKIMS THE DARK SILENT WATERS":R=57
1070 GOSUB2750:GOSUB2760:GOSUB2760:RETURN
1080 IF((L(M+O)>0ANDL(M+O)<9)ORL(O)<>R)ANDO<=C3THENF$="WHAT "+X$+"?":RETURN
1090 IFB$="3450050"THENY=Y-8:X=X-5:F$="THEY ARE CURSED":RETURN
1100 IFB$="3810010"THENGOSUB1370
1110 IF(A=15ANDO<>20ANDO<>1)OR(A=29ANDO<>16)ORO>C3THENF$=W$+C$+" "+X$:RETURN
1120 IFL(O)=RAND(L(M+O)<1ORL(M+O)=9)ANDO<C3THENL(O)=0:A=-1
1130 IFO=16ANDL(10)<>0THENL(O)=R:F$="IT ESCAPED":A=0
1140 IFO>C1ANDO<C2THENF=F+2:A=-1
1150 IFO>=C2ANDO<=C3THENG=G+2:A=-1
1160 IFO>C1ANDO<C3THENL(O)=-81
1170 IFA=-1THENF$="TAKEN":X=X+4:E=E+1:IFL(M+O)>1THENL(M+O)=0
1180 IFB$<>"246046"ORL(11)=0THENRETURN
1190 F$=U$:L(O)=R:IFRND(3)<3THENRETURN
1200 A$="#"=U$+R$
1210 R=63+RND(6):L(16)=1:F$=""
1220 GOSUB2740:RETURN
1230 GOSUB2770:F$="":A$="#THE LOGMEN DECIDE TO HAVE A LITTLE FUN AND "
1240 L(M+41)=0:Y=Y-4:X=X-4
1250 IFR<34THENA$=A$+"THROW YOU IN THE WATER":R=32
1260 IFR>33THENA$=A$+"TIE YOU UP IN A STOREROOM":R=51
1270 GOSUB2750:GOSUB2760
1280 FORI=3TO4
1290 IFL(I)=0THENL(I)=42
1300 NEXTI:RETURN
1310 A$="*THE SWAMPMAN TELLS HIS TALE"
1320 GOSUB2740:L(M+32)=-1:RETURN
1330 F$="MEDIAN CAN DISABLE THE EQUIPMENT"
1340 IFL(8)=0THENF$=F$+" AND ASKS YOU FOR THE PEBBLE YOU CARRY"
1350 RETURN
1360 L(M+36)=-(RND(4)+6):F$="A STORM BREAKS OVERHEAD!":RETURN
1370 FORK=1TO30:GOSUB2770:PRINT"////LIGHTNING FLASHES!":NEXTK
1380 L(39)=R:Y=Y-8:X=X-2:RETURN
1390 IF(O<>24ANDL(O)>0)ORO=52THENF$="YOU DON'T HAVE THE "+X$:RETURN
1400 PRINT"GIVE THE ";X$;" TO WHOM";:INPUT X$
1410 Q=O:GOSUB760:N=O:O=Q
1420 IFR<>L(N)THENF$="THE "+X$+" IS NOT HERE":RETURN
1430 IFB$="10045"ANDN=40THENL(O)=81:L(M+40)=1:F$="THE SNAKE UNCURLS"
1440 IFB$="2413075"ANDN=30ANDG>1THENL(M+11)=0:F$="HE OFFERS HIS STAFF":G=G-1
1450 B$=LEFT$(B$,3):F$="IT IS REFUSED"
1460 IFB$="300"ANDN=42THENX=X+10:L(O)=81
1470 IFB$="120"ANDN=42THENX=X+10:L(O)=81
1480 IFB$="40"ANDL(M+4)<0ANDN=32THENL(M+N)=1:L(O)=81
1490 IFLEFT$(B$,2)="80"AND N=43THENL(O)=81:GOSUB1560
1500 IFL(O)=81OR(O=24ANDL(11)>0ANDG>0)THENF$="IT IS ACCEPTED"
1510 IFN=41THENL(O)=51:F$="IT IS TAKEN"
1520 RETURN
1530 IFO=4ANDL(O)=0THENL(O)=81:X=X-1:F$="IT BREAKS!":RETURN
1540 IFL(O)=0ANDO<C1THENL(O)=R:F$="DONE":E=E-1
1550 RETURN
1560 A$="*HE TAKES IT ":IFR<>8THENA$=A$+"RUNS DOWN THE CORRIDOR,"
1570 GOSUB2740:A$="*AND CASTS IT INTO THE CHEMICAL VATS, PURIFYING THEM WITH"
1580 A$=A$+" A CLEAR BLUE LIGHT REACHING FAR INTO THE LAKES AND RIVERS BEYOND"
1590 L(M+8)=-1:GOSUB2750:GOSUB2760:GOSUB2760:RETURN
1600 IFL(I)<>0ANDI<C1THENI=I+1:GOTO1600
1610 IFL(I)=0THENL(I)=R:L(M+I)=0:GOSUB1450:F$="YOU DROP SOMETHING"
1620 RETURN
1630 IF(O<C1ORO>C3)ANDX$<>"???"THENF$=W$+C$+" "+X$:X=X-1:RETURN
1640 F$="YOU HAVE NO FOOD":IFF>0THENF=F-1:Y=Y+10:F$="OK"
1650 IFO=3THENX=X-5:Y=Y-2:F$="THEY MAKE YOU VERY ILL!"
1660 RETURN
1670 IFO=31THENGOSUB2380:RETURN
1680 IFX$<>"???"AND(O<21ORO>C3)THENF$=W$+C$+" "+X$:X=X-1:RETURN
1690 F$="YOU HAVE NO DRINK":IFG>0THENG=G-1:Y=Y+7:F$="OK"
1700 RETURN
1710 IFLEFT$(B$,4)="1600"THENL(M+O)=-1:F$="IT ALLOWS YOU TO RIDE"
1720 RETURN
1730 IFB$="2644044"THENF$="CHEST OPEN":L(M+6)=9:L(M+5)=9:L(M+15)=9
1740 IFB$="2951151"THENF$="THE TRAPDOOR CREAKS":L(M+29)=0:X=X+3
1750 RETURN
1760 Y=Y-2:IFB$="3577077"ANDL(9)=0THENL(M+23)=0:L(23)=R
1770 IFV>15ANDV<19AND(L(9)=0ORL(15)=0) THEN F$="OK"
1780 IFB$="1258158"ORB$"2758158"ANDL(15)=0THENL(M+12)=0:L(M+27)=0:F$="CRACK"
1790 IFLEFT$(B$,4)="1100"ANDR=10THENGOSUB1980
1800 IFA=18AND(O>29ANDO<34)OR(O>38ANDO<44)ORO=16THENGOSUB1900
1810 RETURN
1820 Y=Y-2:X=X-2:IFR<>L(O)ANDL(O)<0THEN RETURN
1830 IFO=39THENF$="HE LAUGHS DANGEROUSLY"
1840 IFO=32THENF$="THE SWAMPMAN IS UNMOVED"
1850 IFO=33THENF$=W$+"TOUCH HER!":L(23)=81
1860 IFO=41THENF$="THEY THINK THAT'S FUNNY!"
1870 IFR=46THENGOSUB1200
1880 IFLEFT$(B$,4)="1400"ANDR=L(39)THENGOSUB1980
1890 Y=Y-8:X=X-5:RETURN
1900 IFL(9)>0THENRETURN
1910 Y=Y-12:X=X-10:F$="THAT WOULD BE UNWISE"
1920 IFR<>L(O)THEN RETURN
1930 L(M+W)=1:A$="#THUNDER SPLITS THE SKY!":F$=""
1940 A$=A$+"IT IS THE TRIUMPHANT VOICE OF OMEGAN.":GOSUB2740
1950 A$="#WELL DONE ALPHAN! THE MEANS BECOME THE END.."
1960 A$=A$+"I CLAIM YOU AS MY OWN! HA HA HAH!":GOSUB2750
1970 GOSUB2760:X=0:L=0:Y=0:RETURN
1980 GOSUB2770:ON O-10 GOSUB2010,2060,2060
1990 X=X+10:L(O)=81:L(M+O)=-1:GOSUB720:GOSUB2760:GOSUB2760
2000 RETURN
2010 A$="#IT SHATTERS RELEASING A DAZZLING RAINBOW OF COLOURS!"
2020 IFL(2)<>RTHEN RETURN
2030 A$=A$+"THE EGG HATCHES INTO A BABY DAKTYL "+O$
2040 L(39)=81:L(2)=81:L(M+2)=-1:Y=Y+40
2050 RETURN
2060 IFL(13)<>R THEN RETURN
2070 A$="*THE COAL BURNS WITH A WARM RED FLAME":L(M+13)=-1
2080 IFR=10ANDR=L(39)THENA$=A$+" WHICH DISSOLVES OMEGAN'S CLOAK":Y=Y+20
2090 RETURN
2100 IFR<>51ORL(M+29)>0THENF$=W$+C$+" HERE":X=X+1
2110 X=X-1:R=RND(5):GOSUB2770:PRINT"SWIMMING IN THE POISONOUS WATERS"
2120 J=0:B$="":F$="YOU SURFACE":PRINT"YOUR STRENGTH = ";INT(Y)
2130 FORI=1TOR
2140 IFY<15THENPRINT"YOU ARE VERY WEAK"
2150 PRINT"WHICH WAY";:INPUTX$:X$=LEFT$(X$,1):B$=B$+X$:NEXTI
2160 FORI=1TOR
2170 Y=Y-(E/C4+.1)-3:IFMID$(B$,I,1)="N"THENJ=J+1
2180 NEXTI:IFR/2>JANDY>1THEN2110
2190 IFY<2THENF$="YOU GOT LOST AND DROWNED"
2200 R=30+RND(3):RETURN
2210 IFL(88)>-1THENRETURN
2220 GOSUB2770:PRINT"YOU CAN RUN TO SHELTER IN:"PRINT"1) GRANDPA'S SHACK"
2230 PRINT"2) CAVE OF SNELM":PRINT"3) LOG CABIN":PRINT"CHOOSE FROM 1-3":INPUTA$
2240 IFA$>"0"ANDA$<"4"THENR=ASC(MID$("A >",VAL(A$),1))-21:L(M+22)=-R
2250 PRINT"YOU RUN BLINDLY THROUGH THE STORM":F$="YOU REACH SHELTER"
2260 GOSUB2760:RETURN
2270 IFB$="3075075"ORB$="3371071"THENF$="HOW WILL YOU DO THAT"
2280 IFB$="3371071"ANDA=28THENL(M+3)=0:F$="SHE NODS SLOWLY":X=X+5
2290 RETURN
2300 F$="EXAMINE THE BOOK FOR CLUES"
2310 IFLEFT$(B$,3)="600"THENF$=L$
2320 RETURN
2330 IFB$="40041"THENL(M+4)=-1:F$="FILLED"
2340 RETURN
2350 F$=X$:IFX$=H$ANDR=47ANDL(M+8)=0 THENL(M+44)=1:F$=J$
2360 IFX$<>P$ORR<>L(42)ORL(3)<81ORL(12)<81THENRETURN
2370 F$="HE EATS THE FLOWERS- AND CHANGES":L(M+42)=1:L(M+43)=0:RETURN
2380 IFL(M+4)+L(4)<>-1 THENF$="YOU DON'T HAVE "+X$:RETURN
2390 GOSUB2770:PRINT"YOU TASTE A DROP AND..":GOSUB2760:F$="*OUCH!":Y=Y-4:X=X-7
2400 GOSUB2770:FORI=1TOABS(L(M+36))+3
2410 L=L-1:IFY<100ORR=L(M+22)THENY=Y+1
2420 PRINT"TIME PASSES":GOSUB2760
2430 NEXTI
2440 IFL>100ORL(M+36)<1THENX=X+2:L(M+36)=1
2450 IFA=37ORA=36THENF$="OK"
2460 RETURN
2470 IFR=L(25)THENF$="THE BOATMAN WAVES BACK"
2480 IFLEFT$(B$,3)="700"THENL(M+7)=1:F$=N$:X=X+8
2490 RETURN
2500 F$="A-DUB-DUB":IFLEFT$(B$,4)<>"2815"THEN RETURN
2510 IFL(M+O)=1THENL(M+O)=0:F$=K$:RETURN
2520 IFL(5)=0THENL(M+8)=0:GOSUB1080:F$="THE STONE UTTERS "+H$
2530 RETURN
2540 GOSUB2770:PRINT" INFO - ITEMS CARRIED":GOSUB2780
2550 PRINTG$"FOOD="F,"DRINK="G:PRINTG$;:F$="OK"
2560 FORI=1TOC4
2570 READY$:IFL(I)=0THENPRINTY$
2580 NEXTI
2590 PRINTG$;:GOSUB2730:RETURN
2600 C$="LOAD":IFA=41THENC$="SAVE"
2610 PRINT"PREPARE TO ";C$:GOSUB2730
2640 IFA=41THENL(M+50)=R:L(M+49)=Y:L(M+48)=X:L(M+47)=F:L(M+46)=G:L(M+45)=L
2660 IFA=40THENCLOAD*L,"ISDATA"
2670 IFA=41THENCSAVE*L,"ISDATA"
2700 IFA=40THENR=L(M+50):Y=L(M+49):X=L(M+48):F=L(M+47):G=L(M+46):L=L(M+45)
2710 F$="OK":RETURN
2720 L(M+W)=-1:F$="YOU RELINQUISH YOUR QUEST.":L=1:RETURN
2730 INPUT"PRESS enter";A$:RETURN
2740 GOSUB2770
2750 GOSUB720:GOSUB2760:RETURN
2760 FORD=1TO2500:NEXTD:RETURN
2770 CLS:RETURN
2780 RESTORE:FORI=1TO80:READTP$:NEXTI
2790 RETURN
2800 A$="*THE WORLD LIVES WITH NEW HOPE!":GOSUB2750
2810 F$="YOUR QUEST IS OVER":RETURN
2820 PRINT"INITIALISING"
2830 Z=21
2840 V=42:W=51:C4=43:M=52
2850 DIMI$(7),L(104)
2860 L(1)=205:L(2)=206:L(3)=71:L(4)=201:L(5)=204:L(6)=53:L(7)=59:L(8)=47:L(9)=213:L(10)=92
2861 L(11)=75:L(12)=218:L(13)=80:L(14)=67:L(15)=204:L(16)=37:L(17)=204:L(18)=202:L(19)=92:L(20)=53
2862 L(21)=204:L(22)=202:L(23)=77:L(24)=45:L(25)=193:L(26)=204:L(27)=218:L(28)=47:L(29)=211:L(30)=75
2863 L(31)=201:L(32)=78:L(33)=71:L(34)=210:L(35)=77:L(36)=55:L(37)=51:L(38)=42:L(39)=42:L(40)=205
2864 L(41)=202:L(42)=198:L(43)=198:FORCC=44TO52:L(CC)=32:NEXT
2870 DATA"4THE FURTHEST DEPTHS OF THE FOREST1001"
2880 DATA"4THE DEPTHS OF THE MUTANT FOREST1000"
2890 DATA"7A PATH OUT OF THE OVERGROWN DEPTHS1000"
2900 DATA"6A CARNIVOROUS TREE1000"
2910 DATA"4A CORRAL BENEATH THE CRIMSON CANYON1110"
2920 DATA"7THE TOP OF A STEEP CLIFF1011"
2930 DATA"4THE MARSH FACTORY1001"
2940 DATA"4THE SLUDGE FERMENTATION VATS1110"
2950 DATA"7THE UPPERMOST BATTLEMENTS1001"
2960 DATA"4OMEGAN'S SANCTUM1110"
2970 DATA"4SNELM'S LAIR0001"
2980 DATA"2A DARK CAVE0000"
2990 DATA"1BROKEN BRANCHES0100"
3000 DATA"1A THICKET OF BITING BUSHES0000"
3010 DATA"1A HUGE GLASSY STONE1110"
3020 DATA"7THE EDGE OF CRIMSON CANYON0011"
3030 DATA"4THE CLONE FACTORY0101"
3040 DATA"4A CORRIDOR OF CLONE STORAGE CASKS1100"
3050 DATA"7EDGE OF THE WELL0000"
3060 DATA"4THE ROOM OF SECRET VISIONS1110"
3070 DATA"4SNELM'S INNER CHAMBER0111"
3080 DATA"3THE SOUTHERN EDGE OF THE FOREST0101"
3090 DATA"7A LEAFY PATH1000"
3100 DATA"3A FORK IN THE PATH0100"
3110 DATA"7AN APPARENTLY UNCLIMABLE ROCKY PATH1100"
3120 DATA"7A LEDGE ATOP THE CRIMSON CANYON0010"
3130 DATA"4A TALL ENTRANCE CHAMBER1101"
3140 DATA"4A LOW PASSAGE WITH ARMS REACHIN FROM THE WALLS1010"
3150 DATA"7THE APPROACH TO THE WELL OF DESPAIR0001"
3160 DATA"4A DIM CORRIDOR DEEP IN THE CASTLE1010"
3170 DATA"4THE STAGNANT WATERS OF THE CRAWLING CREEK1001"
3180 DATA"4A SHALLOW POOL OFF THE CREEK1100"
3190 DATA"7A LOG PIER, JUTTING OUT OVER THE CREEK0000"
3200 DATA"4A STRETCH OF FEATURELESS DUNES1100"
3210 DATA"1A GROUP OF TALL TREES1010"
3220 DATA"7A NARROW LEDGE AT THE SUMMIT OF THE CANYON0011"
3230 DATA"2A MONSTEROUS PORTAL IN THE CASTLE WALL0011"
3240 DATA"4A CHAMBER INCHES DEEP WITH DUST0001"
3250 DATA"4HERE1111"
3260 DATA"2A CARVED ARCHWAY 0010"
3270 DATA"4A SMALL HUT IN THE LOG SETTLEMENT0111"
3280 DATA"1A HUGE SPLIT-LOG TABLE1001"
3290 DATA"4THE PORCH OF THE LOGMEN'S CABIN0110"
3300 DATA"4GRANDPA'S SHACK1101"
3310 DATA"3A CLEARING IN THE TREES BY A RICKETY SHACK0010"
3320 DATA"4THE NEST OF A HUGE DACTYL0111"
3330 DATA"6THE CASTLE OF DARK SECRETS BY TWO HUGE STONES0011"
3340 DATA"4A ROOM LITTERED WITH BONES0111"
3350 DATA"4THE CELL OF WHISPERED SCRETS0111"
3360 DATA"4THE LIBRARY OF WRITTEN SECRETS0111"
3370 DATA"4A REFUSE STREWN STOREROOM1111"
3380 DATA"4THE LOGMEN'S HALL0000"
3390 DATA"5A LOG BUILDING1000"
3400 DATA"7A RUTTED HILLSIDE1100"
3410 DATA"7A WINDSWEPT PLAIN AMONGST STONE MEGALITHS0100"
3420 DATA"7THE STEPS OF AN ANCIENT PYRAMID1010"
3430 DATA"7THE ISLAND OF SECRETS0111"
3440 DATA"1A BROKEN MARBLE COLUMN1001"
3450 DATA"7AN EXPANSE OF CRACKED, BAKED EARTH1100"
3460 DATA"4A DESERTED ADOBE HUT1010"
3470 DATA"4A LIVID GROWTH OF MAD ORCHIDS1011"
3480 DATA"4A CORNER STREWN WITH BROKEN CHAIRS0111"
3490 DATA"7THE BRIDGE NEAR TO A LOG SETTLEMENT0011"
3500 DATA"1A CRUMBLING MASS OF PETRIFIED TREES1011"
3510 DATA"3THE EDGE OF THE PYRAMID1101"
3520 DATA"7THE ROOF OF THE ANCIENT PYRAMID0100"
3530 DATA"3AN IMPASSABLE SPLIT IN THE PYRAMID1110"
3540 DATA"7A BARREN BLASTED WASTELAND0001"
3550 DATA"AN EXPANSE OF BLEAK, BURNT LAND1100"
3560 DATA"5A DELAPIDATED ADOBE HUT0110"
3570 DATA"4THE HEART OF THE LILIES0101"
3580 DATA"4THE MIDST OF THE LILIES1100"
3590 DATA"3A RIVER'S EDGE BY A LOG BRIDGE0100"
3600 DATA"3A PETRIFIED VILLAGE BY A RIVER CROWDED WITH LILIES0100"
3610 DATA"4THE REMAINS OF A VILLAGE1100"
3620 DATA"3THE ENTRANCE TO A PETRIFIED VILLAGE1100"
3630 DATA"4A SWAMP MATTED WITH FIBROUS ROOTS1100"
3640 DATA"2A VILLAGE OF HOLLOW STUMPS DEFYING THE SWAMP0100"
3650 DATA"4A TUNNEL INTO ONE OF THE TREE STUMPS1100"
3660 DATA"4A HOLLOW CHAMBER MANY METRES IN DIAMETER1110"
3670 DATA"A SHINY APPLE"
3680 DATA"A FOSSILISED EGG"
3690 DATA"A LILY FLOWER"
3700 DATA"AN EARTHENWARE JUG"
3710 DATA"A DIRTY OLD RAG"
3720 DATA"A RAGGED PARCHMENT"
3730 DATA"A FLICKERING TORCH"
3740 DATA"A GLISTENING PEBBLE"
3750 DATA"A WOODMAN'S AXE"
3760 DATA"A COIL OF ROPE"
3770 DATA"A RUGGED STAFF"
3780 DATA"A CHIP OF MARBLE"
3790 DATA"A POLISHED COAL"
3800 DATA"A PIECE OF FLINT"
3810 DATA"A GEOLOGIST'S HAMMER"
3820 DATA"A WILD CANYON BEAST"
3830 DATA"A GRAIN LOAF"
3840 DATA"A JUICY MELON"
3850 DATA"SOME BISCUITS"
3860 DATA"A GROWTH OF MUSHROOMS"
3870 DATA"A BOTTLE OF WATER"
3880 DATA"A FLAGON OF WINE"
3890 DATA"A FLOWING SAP"
3900 DATA"A SPARKLING FRESHWATER SPRING"
3910 DATA"THE BOATMAN"
3920 DATA"A STRAPPED OAK CHEST"
3930 DATA"A FRACTURE IN THE COLUMN"
3940 DATA"A MOUTH-LIKE OPENING"
3950 DATA"AN OPEN TRAPDOOR"
3960 DATA"A PARCHED, DESSICATED VILLAGER"
3970 DATA"A STILL OF BUBBLING GREEN LIQUOR"
3980 DATA"A TOUGH SKINNED SWAMPMAN"
3990 DATA"THE SAGE OF THE LILIES"
4000 DATA"WALL AFTER WALL OF EVIL BOOKS"
4010 DATA"A NUMBER OF SOFTER ROOTS"
4020 DATA"FIERCE LIVING STORM THAT FOLLOWS YOU"
4030 DATA"MALEVOLENT WRAITHS WHO PUSH YOU TOWARD THE WELL"
4040 DATA"HIS DREADED CLOAK OF ENTROPY"
4050 DATA"OMEGAN THE EVIL ONE"
4060 DATA"AN IMMENSE SNAKE WOUND AROUND THE HUT"
4070 DATA"A GROUP OF AGGRESSIVE LOGMEN"
4080 DATA"THE ANCIENT SCAVENGER","MEDIAN"
4090 GOSUB2780
4100 DATA"BY","FACING","AT","IN","OUTSIDE","BENEATH","ON"
4110 FORI=1TO43:READTP$:NEXT
4120 FORI=1TO7:READI$(I):NEXT
4130 R=23:B=8:L=1000:E=0
4140 C1=16:C2=21:C3=24:F=2:G=2
4150 F=2:G=2
4160 Y=100:X=35
4180 Q$="90101191001109109000901000111000000100000010000000000"
4190 G$="--------------------------------"
4200 F$="LET YOUR QUEST BEGIN"
4210 K$="REFLECTIONS STIR WITHIN"
4220 L$="REMEMBER ALADDIN IT WORKED FOR HIM"
4240 N$="THE TORCH BRIGHTENS"
4250 U$="YOU ANGER THE BIRD"
4260 W$="YOU CAN'T "
4270 P$="REMEMBER OLD TIMES"
4280 R$=" WHICH FLIES YOU TO A REMOTE PLACE"
4290 S$="FALLING UNDER THE SPELL OF THE BOATMAN "
4310 T$="AND ARE TAKEN TO THE ISLAND OF SECRETS"
4320 J$="THE STONES ARE FIXED"
4330 O$="WHICH TAKES OMEGAN IN ITS CLAWS AND FLIES AWAY"
4340 V$="N??S??E??W??GO?GETTAKGIVDROLEAEATDRIRIDOPEPICCHOCHITAPBREFIGSTRATT"
4350 V$=V$+"HITKILSWISHEHELSCRCATRUBPOLREAEXAFILSAYWAIRESWAVINFXLOXSAQUI"
4360 Z$="APPEGGFLOJUGRAGPARTORPEBAXEROPSTACHICOAFLIHAMCANLOAMELBISMUS"
4370 Z$=Z$+"BOTWINSAPWATBOACHECOLSTOTRAVILLIQSWASAGBOOROOASAWRACLOOMESNA"
4380 Z$=Z$+"LOGSCAMEDNORSOUEASWESUP?DOWIN?OUT???"
4390 FORI=1TOW+1
4400 L(I)=L(I)-32:L(M+I)=ASC(MID$(Q$,I,1))-48:IFL(I)>127THENL(I)=L(I)-96
4410 NEXTI
4420 H$="STONY WORDS"
4460 RETURN
4500 B$=STR$(O)+STR$(L(O))+STR$(L(M+O))+STR$(R)
4510 B$=STR$(VAL(B$)):B$=RIGHT$(B$,LEN(B$)-1)
4520 RETURN
5000 CLS:GOSUB6000:INPUT"DO YOU WANT INSTRUCTIONS? (Y/N)";I$
5020 IFLEFT$(I$,1)="Y"THEN6050
5030 IFLEFT$(I$,1)="N"THENRETURN
6000 PRINT"     *********************"
6010 PRINT"     *                   *"
6020 PRINT"     * ISLAND OF SECRETS *"
6030 PRINT"     *                   *"
6040 PRINT"     *********************":PRINT:RETURN
6050 CLS
6060 M$="YOU ARE ALPHAN AND MUST FIND THE OBJECTS OF POWER, THOUGH YOU DON'T KNOW WHAT"
6090 M$=M$+" THEY ARE, AND TAKE THEM TO THE ISLAND OF SECRETS.":GOSUB1
6110 M$="YOU MUST THEN WORK OUT WHAT TO DO WITH THEM TO RESTORE WARMTH AND LIFE TO THE"
6130 M$=M$+" WORLD. IF YOU DO SOMETHING WRONG YOU MAY FIND YOU HAVE LOST THE GAME.":GOSUB1
7000 M$="YOU WILL NOTICE AS YOU MOVE AROUND THAT THE TIME REMAINING, SHOWN ON THE TOP"
7020 M$=M$+" RIGHT OF THE SCREEN, CONSTANTLY DECREASES.":GOSUB1:GOSUB10000
7030 M$="THE GAME IS OVER WHEN THE TIME IS UP, NO MATTER WHAT ELSE HAS HAPPENED.":GOSUB1
7060 M$="YOU WILL ALSO SEE THAT YOUR STRENGTH AND WISDOM SCORES VARY AS THE GAME PROGRESSES.":GOSUB1
7090 M$="YOU CAN INCREASE YOUR STRENGTH BY EATING OR DRINKING, PROVIDING YOU HAVE STOCKS OF FOOD AND"
7110 M$=M$+" DRINK. YOU CAN ONLY INREASE YOUR WISDOM BY DOING THE RIGHT THINGS IN THE GAME."
7130 GOSUB1:GOSUB10000
8000 M$="YOU CAN STOP AT ANY POINT IN THE GAME AND SAVE UOUR PROGRESS BY TYPING xsave.":GOSUB1
8030 M$="LOAD IN A SAVED POSITION TYPE xload. TYPE info TO FIND OUT WHAT YOU ARE CARRYING.":GOSUB1
8060 M$="YOU START WITH TWO UNITS EACH OF FOOD AND DRINK.":GOSUB1
8080 M$="THIS CAN BE ADDED TO BY PICKING UP ANY FOOD AND DRINK YOU SPOT ALONG THE WAY.":GOSUB1
8100 GOSUB10000:RETURN
10000 PRINT:PRINT"PRESS ANY KEY TO CONTINUE";
10010 I$=INKEY$:IFI$=""THEN10010
10020 CLS:RETURN
