3 POKE R,O: DIM V(30): DIM I$(32): DIM Q$(6,5)
6 LET FI=7650: LET B=65291: LET L=2000: LET U=65: LET I2=O: LET I1=O: LET PO=O: LET D=O: LET VV=O: LET MG=O: LET LT=O: LET BD=O: LET ID=O: LET IG=O: LET FIN=O: LET IF=O: LET GG=140: LET GH=145: LET T7=O: LET T6=O: LET T5=O: LET S=O: LET R1=O: LET V5=O: LET TT=O: LET ZZ=O: LET G1=O: LET Y1=O: LET X2=O: LET NN=O: LET V1=O: LET A2=O: LET P1=O
19 GO TO L
21 POKE R,O: FOR Y=21 TO 25: BEEP .01,Y: BEEP .01,Y-10: NEXT Y: LET M$="WHAT WILL I DO NOW?             ": GO SUB 65
22 FOR Y=I TO 2: LET D$="": NEXT Y: LET T=O
23 POKE R,O: LET C$=INKEY$: PRINT AT SCR,T; FLASH I;"*": LET VV=VV+I: IF VV=1000 THEN LET VV=O: GO SUB 300: BEEP .1,20: BEEP .1,8
24 IF C$="" THEN GO TO 23
25 LET VV=O: BEEP .01,20: BEEP .01,12
26 IF C$=CHR$ 12 THEN GO TO 20
28 IF C$=CHR$ 13 THEN GO TO 34
30 PRINT AT SCR,T;C$;: LET T=T+I: LET D$=D$+C$: IF T>31 THEN GO SUB 66: LET T=O
32 FOR Y=I TO 2: LET C$="": NEXT Y: GO TO 23
34 PRINT AT SCR,T;"   <<<": LET T=O: LET C$="": IF D$="" THEN GO TO 20
35 GO SUB 66
36 RETURN 
40 POKE R,O: LET Y$(I)=D$
42 IF LEN D$=I AND D$="N" OR D$="S" OR D$="E" OR D$="W" OR D$="U" OR D$="D" THEN RETURN 
43 IF Y$(I)( TO 2)="GO" THEN RETURN 
44 IF Y$(I)( TO 3)="UNS" THEN GO TO 8500
45 RANDOMIZE USR 65197
47 IF Y$(I)( TO 3)=V$(I) THEN GO TO CODE V$(I)*100
50 LET M$="I CAN'T DO THAT": GO SUB 65: LET P1=I
52 LET NN=I: RETURN 
65 POKE R,O: PRINT AT SCR,O;M$
66 POKE R,O: LET SCR=SCR+I
67 IF SCR>21 THEN LET SCR=21: RANDOMIZE USR 65524
68 RETURN 
70 POKE R,O: RESTORE 970+(30*LE)+H: LET V=I
72 IF LE=2 AND H=17 THEN RESTORE 1022
73 IF LE=2 AND H=15 THEN RESTORE 1006
74 IF LE=2 AND H=14 THEN RESTORE 1008
75 IF (LE=I AND (H=17 OR H=18 OR H=29)) OR (LE=2 AND (H=11 OR H=6 OR H=8 OR H=13 OR H=18)) THEN RESTORE 1019
77 READ Z1,Z2,Z3,Z4,Z5: LET V=V+I: GO SUB 80
78 IF V<C THEN GO TO 77
79 RETURN 
80 POKE R,O: LET X=65480: POKE X+3,Z2: POKE X+4,Z1: POKE X+7,Z3: POKE X+6,Z4: POKE X+1,Z5: RANDOMIZE USR 65480: RETURN 
90 POKE R,O: FOR X=O TO 23: PLOT O,72+X: DRAW X,O: PLOT 175,72+X: DRAW -X,O: NEXT X: RETURN 
100 POKE R,O: LET SC=SC+I
101 CLS : LET SCR=14
102 LET A$=""
103 RESTORE (200*LE)+(H*2)
104 READ L$,F$,A$,GR,LO
105 IF LE=I AND H=11 AND T(11)>5 THEN READ L$,F$,A$,GR,LO
106 IF H=16 AND LE=I AND S(2) THEN LET A$="NS"
107 IF H=I AND ((MG AND LE=2) OR (T(H)=10 AND LE=I)) THEN LET A$="U"
108 IF LE=2 AND H=10 AND T(H)=5 THEN LET A$="S"
109 IF T(H)=5 THEN LET LO=O
110 IF LE=2 AND H=17 AND T(H)=5 THEN LET A$="ES"
112 IF NOT GR THEN GO TO 123
113 IF V(H)=I THEN GO TO 123
115 IF LE=2 AND H=15 THEN GO SUB 1118: GO TO 119
116 IF LE=2 AND H=17 THEN GO SUB 1166: GO TO 119
117 IF LE=2 AND H=14 THEN GO SUB 1124: GO TO 119
118 GO SUB 1000+(100*LE)+(H*3)
119 POKE R,O: INK O: PRINT AT I,22;L$;AT 2,22;F$
120 PRINT AT 6,22;"EXITS:"
121 LET V(H)=I: IF (LE=I AND (H=29 OR H=21)) OR (LE=2 AND (H=16 OR H=20)) THEN LET V(H)=O
122 GO TO 125
123 RANDOMIZE : LET X3=65480: POKE X3+3,O: POKE X3+4,O: POKE X3+6,32: POKE X3+7,13: POKE X3+I,64+8*(INT (RND*6)+I)+INT (RND*6)+I: RANDOMIZE USR 65480
124 PRINT AT 2,I;L$;AT 3,I;F$;AT 6,21;"EXITS:"
125 IF LE=2 THEN GO TO 169
126 IF H=11 AND NOT T(11) THEN LET M$="IT IS PITCH DARK!": GO SUB 65: BORDER O: POKE X3+I,71: RANDOMIZE USR 65480
127 IF H=27 THEN RANDOMIZE : IF INT (RND*100)<13 THEN LET H=28: GO TO 101
128 IF H=8 THEN GO SUB 146
129 IF H=3 AND BL THEN GO TO 139
130 IF H=3 THEN LET M$="YOU SEE A BLASTER!": GO SUB 65
131 IF H=21 AND SR THEN GO TO 139
132 IF H=21 THEN LET M$="HERE IS A SCREWDRIVER": GO SUB 65
133 IF H=22 AND S(8) THEN LET A$="SD"
134 IF H=15 AND E THEN GO TO 139
135 IF H=15 THEN LET M$="YOU SEE SOME FRUIT!": GO SUB 65
136 GO SUB 140: GO TO 142
137 IF H=20 THEN LET M$="YOU SEE A LARGE TOOLBOX!": GO SUB 65
138 IF H>28 THEN GO TO 150
139 GO TO 1500
140 POKE R,O: IF R(H) THEN LET M$="HERE IS THE "+R$(H): GO SUB 65: IF P(H) THEN LET M$="AND THE "+P$(H): GO SUB 65
141 RETURN 
142 IF H=19 AND SO THEN GO TO 139
143 IF H=19 THEN LET M$="HERE LIES A SONIC DEVICE!": GO SUB 65
144 IF H=28 THEN LET M$="YOU SEE A SIGN": GO SUB 65: LET P1=I
145 GO TO 137
146 POKE R,O: LET M$="YOU SEE A GUARD": GO SUB 65: IF G1 THEN LET M$="HE IS DEAD!": GO SUB 65: IF NOT I1 AND NOT I2 AND ID AND PO THEN LET M$="HERE IS THE IDENTITY CARD": GO SUB 65
147 IF S(I) THEN RETURN 
148 POKE R,O: IF NOT G1 THEN LET M$="YOU ARE CAPTURED!": GO SUB 65: GO SUB 3000: LET H=24: FOR Y=-20 TO 30: BEEP .01,Y: BEEP .05,Y: NEXT Y: GO TO 2000
149 RETURN 
150 IF H=30 THEN LET LE=2: LET X1=O: LET H=I: DIM V(30): DIM G$(5,12): DIM T(30): DIM U(30): DIM R(30): DIM R$(30,12): DIM P(30): DIM P$(30,12): LET P1=O: FOR A=I TO 9: LET S(A)=O: NEXT A: LET M$="YOU HAD TO LEAVE EVERYTHING": GO SUB 65: LET M$="BEHIND!!": GO SUB 65: FOR A=1 TO 100: NEXT A: GO TO 2000
155 IF H=29 THEN RANDOMIZE : IF INT (RND*100)>10 THEN GO TO 1500
160 LET M$="YOU HAVE BEEN BLOWN AWAY IN THE": GO SUB 65: LET M$="MAZE OF VENTILATOR SHAFTS!!": GO SUB 65: LET M$="PRESS ENTER TO PLAY AGAIN": GO SUB 65
165 IF INKEY$=CHR$ 13 THEN RUN 9600
166 BEEP .1,RND*30: GO TO 165
169 IF H=6 AND RO THEN GO TO 1500
170 IF H=6 THEN LET M$="THERE IS A ROPE HERE!": GO SUB 65
171 IF H=12 AND RE THEN GO TO 1500
172 IF H=12 THEN LET M$="RESPIRATORS!!": GO SUB 65
173 IF H=9 AND M THEN GO TO 1500
174 IF H=9 THEN LET M$="MONEY! MONEY! MONEY!": GO SUB 65
175 IF H=19 AND TO THEN GO TO 1500
176 IF H=19 THEN LET M$="YOU SEE A TORCH!": GO SUB 65
177 IF H=3 OR H=5 OR H=18 OR H=23 THEN GO SUB 179: GO TO 1500
178 GO TO 185
179 POKE R,O: LET M$="YOU SEE A GUARD!!": GO SUB 65
180 IF H=3 AND S(9) THEN LET M$="HE IS DEAD!": GO SUB 65: RETURN 
181 IF H=18 THEN LET M$="YOU ARE ARRESTED!!!": GO SUB 65: GO SUB 3000: FOR A=1 TO 50: BEEP .01,RND*30: NEXT A: LET H=17: GO TO 2000
182 IF H=3 OR H=23 THEN LET M$="HE DOES NOT SEE YOU!!": GO SUB 65
183 RETURN 
185 IF H=14 THEN LET M$="YOU SEE A DESK BEFORE YOU": GO SUB 65
186 IF H<>26 OR NOT FIN THEN GO TO 199
187 IF S(2) AND S(7) THEN GO TO 190
188 IF NOT S(2) THEN LET M$="NO RESPIRATOR-YOU ARE DEAD!": GO SUB 65: FOR A=I TO 50: BEEP .01,RND*30: NEXT A: RUN 9600
189 IF NOT S(7) THEN LET M$="RADAR TRACKS AND SHOOTS YOU!!!": GO SUB 65: FOR A=I TO 50: BEEP .1,RND*30: NEXT A: RUN 9600
190 IF NOT S(3) OR NOT TO THEN GO TO 192
191 GO TO 500
192 CLS : PRINT "YOU HAVE LEFT SOMETHING OF VITALIMPORTANCE BEHIND. YOU HAVE NO  ALTERNATIVE BUT TO TRY AGAIN!!!"''''"PRESS ANY KEY"
193 IF INKEY$="" THEN BEEP .01,RND*30: GO TO 193
195 RUN 9600
199 GO SUB 140: GO TO 1500
202 DATA "A lift. The controls are on","the side wall.","0",O,I
204 DATA "INSIDE","AIRLOCK","NS",I,I
206 DATA "The armoury. An electronic","sensor lies to one side.","NE",O,I
208 DATA "Here is an electrical unit","that prepares for action!","W",O,O
212 DATA "THE","LIFT DOOR","NS",I,I
214 DATA "A sign says 'DECONTAMINATION'","","NS",O,I
216 DATA "THE","GUARD ROOM","NS",I,I
222 DATA "This area is inky black.","","0",O,I
223 DATA "It would appear to be a large","storage area.","NSE",O,O
224 DATA "This is a changing area.","","ESW",O,O
226 DATA "This is the interrogation area","","NSEW",O,O
228 DATA "This is the off-duty room.","","EW",O,O
230 DATA "The canteen. What a smell!!","","NW",O,I
232 DATA "SOME","UNIFORMS","S",I,O
234 DATA "CORRIDOR","","NE",I,O
236 DATA "CORRIDOR","","NESW",I,O
238 DATA "CORRIDOR","","NW",I,O
240 DATA "SMALL","WORK ROOM","S",I,O
242 DATA "VENT.","CONTROL","S",I,O
248 DATA "A SMALL","CELL","S",I,I
252 DATA "THE","GROUND CAR","N",I,I
254 DATA "The lift shaft. It is quite","gloomy here.","UD",O,O
256 DATA "The top of the lift shaft.","","0",O,I
258 DATA "WINDING","PASSAGE","NSEW",I,O
300 RANDOMIZE : LET P=INT (RND*5)+I
301 GO TO 304+P
305 LET M$="WE HAVE ALMOST REACHED SOLARIS!": GO SUB 65: RETURN 
306 LET M$="HURRY ALONG THERE PLEASE!": GO SUB 65: RETURN 
307 LET M$="ZZZZZZZZZZZZZZZZZZ": GO SUB 65: RETURN 
308 LET M$="IF I DIDN'T KNOW BETTER I'D": GO SUB 65: LET M$="THINK YOU WERE SLEEPING!!": GO SUB 65: RETURN 
309 LET M$="YAAAAAWN! THIS IS BORING!": GO SUB 65: RETURN 
402 DATA "A long corridor leads you to","an overhead grille.","0",O,I
404 DATA "This room is quite empty.","","NE",O,O
406 DATA "WARNING!!!","This is the guard post!","NW",O,I
410 DATA "HANGAR","DECK","N",I,I
412 DATA "CORRIDOR","","NE",I,O
414 DATA "A dim passageway. There are","doors at either end.","WS",O,I
416 DATA "CORRIDOR","","NSE",I,O
418 DATA "This is the mess room.","And there sure is some mess!","NW",O,O
420 DATA "You find yourself within the","lift.","0",O,I
422 DATA "CORRIDOR","","NS",I,I
424 DATA "LOCKER","ROOM","E",I,I
426 DATA "CORRIDOR","","NS",I,O
428 DATA "THE LOG","ROOM","NS",I,O
430 DATA "LIFT","DOORS","NS",I,I
432 DATA "RADAR","ROOM","S",I,O
434 DATA "HOLDING","AREA","E",I,I
436 DATA "CORRIDOR","","WSE",I,I
438 DATA "You are crossing over a high","gantry. Take care not to fall!","NSEW",O,O
440 DATA "COMPUTER","ROOM","WS",I,O
444 DATA "This is Drive Control.","","E",O,O
446 DATA "BRIDGE","","EW",I,I
448 DATA "This is the navigation control","area.","SW",O,O
452 DATA "You are in the life boat.","Control panels surround you.","0",O,I
505 CLS : PRINT "ON BOARD COMPUTER FLICKERS ON -"''"LANDING COORDS:5879  (WRITE THEMDOWN)"'"SET UP TAPE TO RECORD DATA-PRESSA KEY WHEN READY."
508 POKE R,O: IF INKEY$="" THEN BEEP .01,RND*30: GO TO 508
514 LET S(10)=13: LET S(11)=3: LET S(12)=SC: LET S(I)=O: FOR Y=4 TO 9: LET S(Y)=O: NEXT Y
518 POKE R,O: SAVE "DATA 1" DATA S(): SAVE "DATA 2" DATA G$()
520 PRINT '"PRESS 'A' TO RESAVE"
522 LET A$=INKEY$: IF A$="A" THEN GO TO 518
523 IF A$="" THEN GO TO 522
525 PRINT '"TOUCHDOWN"'"PRESS A KEY TO CONTINUE"
530 POKE R,O: IF INKEY$="" THEN GO TO 530
532 PRINT ' FLASH 1;"LOAD PROGRAM TWO  -  START TAPE "
535 POKE R,O: LOAD "solaris2"
1002 DATA O,O,10,6,40,O,16,10,6,40,O,6,10,10,29,10,O,3,22,13
1006 DATA O,O,10,22,50,I,8,8,6,32,10,O,3,22,22,9,8,I,6,16
1008 DATA O,O,6,22,44,6,O,4,22,53,10,O,3,22,48,3,8,3,6,5,7,8,I,6,62,8,8,I,6,16,12,6,I,10,8
1016 DATA O,O,6,22,32,6,O,4,5,32,6,5,4,12,12,6,17,4,5,32,10,O,3,22,28
1019 DATA O,O,13,22,86,O,9,7,4,78,7,9,6,4,112
1020 DATA O,O,6,3,46,O,20,13,2,46,O,3,6,17,53,6,O,7,20,53
1021 DATA O,O,11,22,22,2,3,9,16,6,10,O,3,22,10
1024 DATA O,O,4,14,16,O,20,13,2,20,4,O,9,10,10,4,9,4,5,16,8,9,5,5,32,O,14,11,6,12,10,5,3,4,12,11,14,2,6,33,8,7,2,2,12
1026 DATA O,O,3,22,8,3,O,10,8,41,3,6,10,2,40,3,8,5,6,8,8,8,5,6,I,3,14,10,2,40,3,16,10,6,41,O,9,I,4,120
1035 DATA O,O,6,22,90,6,O,4,22,112,10,O,3,22,80,I,10,2,2,120,3,9,5,4,120,5,5,I,4,120,5,13,I,4,120,2,10,I,2,120
1042 DATA O,O,10,5,80,O,17,10,5,80,O,5,10,12,104,10,O,3,22,117
1046 DATA O,O,10,22,53,10,O,3,22,30,I,5,7,12,69,O,4,I,14,21,8,4,I,14,21,I,4,7,I,21,I,17,7,I,21
1050 DATA O,3,13,3,72,O,16,13,3,72,O,6,10,10,116,O,O,13,3,199,O,19,13,3,248,10,3,3,16,89,6,7,2,8,124,8,7,4,8,99,8,9,2,4,112,2,9,2,4,64
1053 DATA I,5,4,I,96,5,0,8,22,120,O,O,6,2,96,I,16,4,I,96,O,20,6,2,96,O,2,I,7,96,O,13,I,7,96,I,2,3,3,96,I,17,3,3,96,I,6,4,I,64,O,9,4,4,64,I,15,4,I,64,4,7,I,8,120,11,4,2,14,95
1106 LET C=5: GO SUB 70: INK 8: GO SUB 90: FOR X=O TO 23: PLOT 48,119-X: DRAW X,O: PLOT 127,119-X: DRAW -X,O: PLOT 48,152+X: DRAW X,O: PLOT 127,152+X: DRAW -X,O: NEXT X
1107 INK O: PLOT 24,175: DRAW O,-79: PLOT 151,175: DRAW O,-79
1108 FOR X=O TO I: CIRCLE 87,135,12+X: CIRCLE 87,135,2+X: PLOT 75,135+X: DRAW 24,O: PLOT 87+X,147: DRAW O,-24: NEXT X: RETURN 
1118 LET C=5: GO SUB 70: INK 8: GO SUB 90: PLOT 23,175: DRAW O,-79: PLOT 152,175: DRAW O,-79: PLOT 63,168: DRAW -7,7: DRAW O,-79: DRAW 7,7: DRAW O,64: DRAW 23,O: DRAW O,-64: DRAW -23,O
1119 PLOT 113,168: DRAW 7,7: DRAW O,-79: DRAW -7,7: DRAW O,64: DRAW -24,O: DRAW O,-64: DRAW 24,O
1120 FOR X=O TO 7: PLOT 63-X,96: DRAW O,8-X: PLOT 112+X,96: DRAW O,8-X: NEXT X: PLOT 126,134: DRAW 12,O: DRAW O,18: DRAW -12,O: DRAW O,-18: POKE 22640,143: POKE 22672,185: RETURN 
1124 LET c=8: GO SUB 70: INK 8: FOR X=O TO 31: PLOT O,96+X: DRAW X,O: PLOT 175,96+X: DRAW -X,O: NEXT X: PLOT O,94: DRAW 174,O
1125 POKE 22721,44: POKE 22722,45: POKE 22753,44: FOR x=O TO 32: PLOT 8,112+x: DRAW 23,23: NEXT x: FOR x=I TO 3: INK O: PLOT 64+x,151+x: DRAW 47-x*2,O: NEXT x: PLOT 63,151: INK 7: DRAW 47,O
1126 INK 8: FOR x=O TO 7: PLOT 64,112+x: DRAW x,O: PLOT 111,112+x: DRAW -x,O: NEXT x: INK 4: PLOT 16,112: DRAW O,7: INK O: FOR x=O TO 29 STEP 5: PLOT 75+x,115: NEXT x: PLOT 31,133: DRAW O,28: RETURN 
1129 GO TO 1157
1148 LET c=6: GO SUB 70: INK 8: GO SUB 90: PLOT 39,95: INK O: DRAW -10,-10: DRAW -2,O: DRAW 10,10: DRAW 7,-7: DRAW -2,O: DRAW -7,7: PLOT 139,95: DRAW -10,-10: DRAW -2,O: DRAW 10,10: DRAW 7,-7: DRAW -2,O: DRAW -7,7
1149 INK 7: FOR X=O TO 72 STEP 12: PLOT 51+X,120: DRAW O,-12: NEXT X: INK 4: FOR Y=O TO 84 STEP 12: FOR X=O TO 10: PLOT 40+X+Y,97: DRAW O,X: NEXT X: NEXT Y: FOR Y=O TO 84 STEP 12: FOR X=-3 TO 3: PLOT 43+Y,122: DRAW X,5: NEXT X: NEXT Y
1150 INK 8: PLOT O,175: DRAW 24,-8: DRAW 128,O: DRAW 23,8: PLOT 23,167: INK 8: DRAW O,-72: PLOT 151,167: DRAW O,-72: PLOT 39,96: INK 8: DRAW O,34: DRAW 98,O: DRAW O,-34: DRAW 2,O: DRAW O,36: DRAW -102,O: DRAW O,-36: RETURN 
1157 LET C=4: GO SUB 70: INK 8: POKE 22689,215: PLOT 144,72: DRAW O,103: PLOT 31,72: DRAW O,103: FOR X=O TO 39: PLOT 71,119: DRAW -X,-47: PLOT 104,119: DRAW X,-47: NEXT X: FOR X=O TO 31: PLOT 72,143: DRAW X,-23: NEXT X: RETURN 
1160 LET C=5: GO SUB 70: INK 8: FOR X=O TO 23: PLOT O,104+X: DRAW X,O: PLOT 159,95-X: DRAW -X,O: NEXT X: INK O: FOR Y=8 TO 88 STEP 40: PLOT Y,104: DRAW 23,23: DRAW O,-7: FOR X=O TO 31: PLOT Y+(8+X),104: DRAW 15,15: NEXT X: NEXT Y
1161 PLOT O,103: DRAW 135,O: DRAW O,-31: PLOT 136,104: DRAW 23,23: DRAW -135,O: DRAW O,48: DRAW 135,O: DRAW O,-48: DRAW -135,48: PLOT 150,127: DRAW -7,-7: RETURN 
1163 LET C=4: GO SUB 70: INK 8: GO SUB 90: INK O: PLOT O,175: DRAW 24,-16: DRAW 128,O: DRAW 23,16: IF NOT S(3) THEN FOR Y=O TO 160 STEP 32: POKE 22629+Y,132: POKE 22640+Y,132: NEXT Y
1164 IF S(3) THEN FOR Y=O TO 160 STEP 32: POKE 22629+Y,130: POKE 22640+Y,130: NEXT Y
1165 RETURN 
1172 LET C=10: GO SUB 70: INK 8: FOR X=O TO 15: PLOT 175-X,72: DRAW O,X: PLOT 112,111: DRAW O+X,-23: NEXT X: FOR X=O TO 23: PLOT 71,111: DRAW -23+X,-39: NEXT X
1173 FOR X=O TO 39: PLOT O,104+X: DRAW X,O: NEXT X: FOR X=O TO 26: INK 6: PLOT 40-X,104: DRAW 30,32: NEXT X: PLOT 65,129: INK 5: DRAW -27,O: INK 8: PLOT 40,175: DRAW O,-39: PLOT 128,175: DRAW O,-87: PLOT 40,72: INK 5: DRAW O,31: DRAW -39,O:: FOR X=I TO 2: CIRCLE 121,130,X: POKE 22599,110: RETURN 
1178 LET c=9: GO SUB 70: FOR X=O TO 39: INK 8: PLOT O,112+X: DRAW X,O: PLOT 175,112+X: DRAW -X,O: NEXT X: FOR X=O TO 15: PLOT 63,111: DRAW O-X,-39: PLOT 112,111: DRAW O+X,-39: NEXT X: PLOT O,111: DRAW 47,O: DRAW O,-39: PLOT 175,111: DRAW -47,O: DRAW O,-39: PLOT 127,111: DRAW -15,40: PLOT 47,111: DRAW 15,40
1179 INK 7: PLOT 42,175: DRAW O,-23: PLOT 135,175: DRAW O,-23: INK 8: FOR X=72 TO 103 STEP 8: PLOT X,175: DRAW O,-7: NEXT X: RETURN 
1187 GO TO 1157
1215 LET C=8: GO SUB 70: INK 6: FOR X=O TO 7: PLOT 72,119-X: DRAW X,O: PLOT 104,119-X: DRAW -X,O: NEXT X: INK 7: FOR X=O TO 15: PLOT 40,136: DRAW 31,X: PLOT 135,136: DRAW -31,X: NEXT X: INK 3: FOR X=O TO 31: PLOT O,96+X: DRAW X,O: PLOT 175,96+X: DRAW -X,O: NEXT X: INK 7
1216 FOR X=O TO 7: PLOT 40,127: DRAW 31,-X: PLOT 135,127: DRAW -31,-X: NEXT X: INK 3: FOR X=O TO 7: PLOT 84+X,117: DRAW O,-5: PLOT 80,160+X: DRAW X,O: PLOT 96,160+X: DRAW -X,O: NEXT X: INK O: PLOT 39,135: DRAW 31,-6: DRAW 31,O: DRAW 31,6: PLOT 72,130: DRAW 8,21: DRAW 16,O: DRAW 8,-21: PLOT 31,175: DRAW O,-47: PLOT 144,175: DRAW O,-47: PLOT 80,159: DRAW 2,-I: DRAW 12,O: DRAW 2,I: FOR X=O TO 21 STEP 3: PLOT 86,74+X: DRAW 4,O: DRAW O,-2: DRAW -4,O: DRAW O,2: NEXT X: RETURN 
1224 GO TO 1157
1233 GO TO 1157
1236 LET C=5: GO SUB 70: INK 2: GO SUB 90: INK 8: FOR Y=O TO 62 STEP 31: PLOT 67+Y,168: DRAW O,-73: PLOT 43+Y,175: DRAW O,-79: PLOT 49+Y,159: DRAW 8,-3: DRAW O,-8: DRAW -8,3: DRAW O,8: FOR X=O TO 7: PLOT 43+Y,175: DRAW 23,O-X: PLOT 45+Y,95: DRAW 23,O-X: NEXT X: NEXT Y
1237 PLOT 23,175: DRAW O,-79: PLOT 152,175: DRAW O,-79: RETURN 
1239 GO TO 1157
1248 LET C=8: GO SUB 70: INK 6: GO SUB 90: INK 8: PLOT 32,104: DRAW 7,7: PLOT 32,175: DRAW 7,-7: PLOT 143,175: DRAW -7,-7: PLOT 143,104: DRAW -7,7: FOR X=47 TO 143 STEP 8: PLOT X,112: DRAW O,55: NEXT X: FOR X=111 TO 167 STEP 8: PLOT 40,X: DRAW 95,O: NEXT X: PLOT 23,96: DRAW O,79: PLOT 152,96: DRAW O,79: POKE 22695,GG: POKE 22638,GH: RETURN 
1254 GO TO 1157
1260 LET C=11: GO SUB 70: INK 8: FOR X=O TO 23: PLOT 24,72+X: DRAW X,O: PLOT 151,72+X: DRAW -X,O: NEXT X: FOR X=O TO 15: PLOT 56,127-X: DRAW X,O: PLOT 119,127-X: DRAW -X,O: PLOT 56,95-X: DRAW X,O: PLOT 119,95-X: DRAW -X,O: NEXT X
1261 PLOT 73,80: DRAW O,30: PLOT 103,80: DRAW O,30: INK O: FOR X=66 TO 110 STEP 4: PLOT X,125:: NEXT X: FOR X=76 TO 100 STEP 4: PLOT X,119: NEXT X: POKE 22644,212: POKE 22868,226: POKE 22849,212: POKE 22625,226: PLOT 52,96: RETURN 
1269 LET C=15: GO SUB 70: INK O: FOR X=O TO 31: PLOT 56,136+X: DRAW 15,7: PLOT 119,136+X: DRAW -15,7: PLOT O,98+X: DRAW 39,38: PLOT 175,98+X: DRAW -39,38: NEXT X: INK 8: FOR X=O TO 15: PLOT 32,72+X: DRAW X,O: PLOT 143,72+X: DRAW -X,O: NEXT X
1270 INK 8: PLOT O,95: DRAW 31,O: DRAW 16,16: DRAW 80,O: DRAW 16,-16: DRAW 31,O: PLOT 31,95: DRAW O,-23: PLOT 144,95: DRAW O,-23: PLOT 56,120: DRAW 7,7: DRAW 48,O: DRAW 7,-7: DRAW -62,O: INK 2: FOR X=67 TO 115 STEP 8: PLOT X,123: NEXT X: INK 2: FOR X=O TO 3: CIRCLE 90,160,X: NEXT X: RETURN 
1500 FOR Y=I TO LEN A$
1505 LET B$=CHR$ CODE A$: LET A$=A$(2 TO )
1508 IF B$="0" THEN LET B$="NONE"
1510 IF B$="N" THEN LET B$="NORTH"
1511 IF B$="U" THEN LET B$="UP"
1512 IF B$="S" THEN LET B$="SOUTH"
1513 IF B$="D" THEN LET B$="DOWN"
1514 IF B$="E" THEN LET B$="EAST"
1516 IF B$="W" THEN LET B$="WEST"
1520 PRINT AT 7+Y,24;B$;" ": LET Q$(Y)=B$: LET B$="": NEXT Y
1523 RETURN 
1525 POKE R,O: IF U(H) THEN LET LO=O
1526 IF LEN D$>I THEN IF Y$(I)( TO 2)="GO" THEN LET Y$(I)=Y$(I)(4 TO )
1527 IF H=29 THEN GO TO L
1528 IF Q$(I)="NONE " THEN GO TO 1536
1529 IF LE=I AND H=24 AND T(H)<O AND Y$(I,I)="S" THEN LET M$="HE BLOCKS YOUR PATH AND CLOSES": GO SUB U: LET M$="THE DOOR!!": GO SUB U: LET T(H)=O: LET P1=I: GO TO L
1530 FOR Y=I TO 4: IF CODE Y$(I)=CODE Q$(Y) THEN GO TO 1539
1535 NEXT Y
1536 LET M$="YOU CAN'T GO THAT WAY": IF Y$(I,I)="N" AND R1 AND H=17 AND LE=I THEN LET M$=M$+" YET!"
1538 GO SUB U: LET P1=I: GO TO L
1539 IF Y$(I,I)="W" AND LE=2 AND H=7 THEN LET H=H-I: GO TO 1560
1540 IF Y$(I,I)="N" AND LE=I AND H=17 AND NOT S(6) THEN LET R1=I: GO TO 1536
1541 IF Y$(I,I)="N" AND LE=2 AND H=15 THEN LET H=20: GO TO L
1542 IF Y$(I,I)="S" AND H=2 AND LE=I AND ZZ THEN LET LO=I: GO TO 1559
1543 IF Y$(I,I)="E" AND LE=2 AND H=23 THEN LET H=H+I: GO TO 1560
1544 IF Y$(I,I)="S" AND H=2 AND LE=I AND NOT ZZ THEN LET H=H+24: GO TO 1560
1545 IF Y$(I,I)="N" AND LE=I AND H=3 THEN LET H=H+5: GO TO 1560
1546 IF LE=I AND H=22 AND S(8) AND S(3) AND Y$(I,I)="D" THEN LET H=30: FOR Y=I TO 4: LET Q$(Y)="": NEXT Y: GO TO 150
1547 IF LE=I AND H=22 AND S(8) AND NOT S(3) AND Y$(I,I)="D" THEN LET H=29: GO TO 1560
1548 IF Y$(I,I)="W" AND LE=I AND H=15 THEN LET H=H-I: GO TO 1560
1549 IF Y$(I,I)="N" AND LE=I AND H=6 THEN LET H=H+5: GO TO 1560
1550 IF Y$(I,I)="N" AND LE=I AND H=26 AND NOT LO THEN LET H=H-24: GO TO 1560
1551 IF Y$(I,I)="S" AND LE=I AND H=2 THEN LET H=H+24
1552 IF Y$(I,I)="N" AND NOT LO THEN LET H=H+5
1553 IF Y$(I,I)="U" OR Y$(I,I)="D" THEN GO TO 1580
1554 IF Y$(I,I)="S" AND NOT LO THEN LET H=H-5: GO TO 1556
1555 IF LE=2 AND H=11 AND Y$(I,I)="S" THEN LET H=H-5: GO TO 1560
1556 IF Y$(I,I)="E" AND NOT LO THEN LET H=H+I
1557 IF Y$(I,I)="S" AND LE=I AND (H+5=20) OR (H+5=22) THEN LET T(H+5)=O
1558 IF Y$(I,I)="W" AND NOT LO THEN LET H=H-I
1559 IF LO THEN LET M$="THE EXIT IS BARRED": GO SUB U: LET P1=I: GO TO L
1560 FOR Y=I TO 4: LET Q$(Y)="": NEXT Y: RETURN 
1570 BORDER O: PAPER O: INK 7: CLS : FOR Y=I TO 40: BEEP .1,Y: NEXT Y
1572 PRINT "YOU FOOL"'''"DON'T YOU KNOW WE'RE IN DEEP    SPACE!!"'''"PRESS ENTER TO TRY AGAIN.": FOR Y=40 TO I STEP -I: BEEP .1,Y: NEXT Y
1573 IF INKEY$=CHR$ 13 THEN RUN 9600
1574 BORDER RND*7: PAPER RND*7: CLS : BEEP .01,RND*40: GO TO 1573
1580 IF Y$(I,I)="U" AND H*LE=I AND T(I)>9 THEN LET H=27: GO TO L
1581 IF Y$(I,I)="U" AND LE=2 AND H=I THEN LET H=6: GO TO L
1582 IF Y$(I,I)="U" OR Y$(I)="D" AND LE=I AND H=27 THEN GO TO L
1590 GO TO 1536
1903 DATA "BLASTER"
1908 DATA "IDENT. CARD"
1915 DATA "FRUIT"
1916 DATA "UNIFORM"
1919 DATA "SONIC DEVICE"
1920 DATA "SPANNER"
1921 DATA "SCREWDRIVER"
1926 DATA "EXTINGUISHER"
1936 DATA "ROPE"
1939 DATA "MONEY"
1942 DATA "RESPIRATOR"
1944 DATA "KEY"
1949 DATA "TORCH"
2005 IF P1 THEN LET P1=O: GO TO VAL "2020"
2010 IF NOT P1 THEN GO SUB VAL "100"
2020 GO SUB VAL "20"
2030 GO SUB VAL "40"
2035 IF NN=I THEN LET NN=O: GO TO L
2040 GO SUB VAL "1525"
2050 LET P1=O
2100 GO TO L
3000 FOR Y=I TO 5: LET G$(Y)="": NEXT Y: IF LE=I THEN LET BL=O: LET I1=O: LET S(9)=O: LET S(6)=O: LET SR=O: LET S(5)=O: LET E=O: LET SO=O
3001 IF LE=2 THEN LET S(3)=O: LET RO=O: LET S(8)=O: LET M=O: LET S(2)=O: LET RE=O: LET S(I)=O: LET K=O: LET TO=O
3002 LET M$="ALL YOU HAD HAS BEEN RECLAIMED!": GO SUB U
3005 LET PO=I: RETURN 
6500 IF H=15 AND LE=I AND BD AND T(H)<14 THEN LET V$(I)="DOO": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=14: GO TO FI
6599 GO TO 7705
6604 IF LE=2 AND H=23 AND S(8) AND NOT U(H) THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET U(H)=I: GO TO FI
6697 GO TO 7705
6700 IF V$(I)="CHA" THEN GO TO 6749
6702 IF LE>I THEN GO TO 6706
6703 IF H=2 AND NOT ZZ THEN LET V$(I)="REAR": RANDOMIZE USR B: IF NOT NO THEN LET ZZ=I: LET T(H)=2: LET M$="YOU HEAR A LOUD ROAR": GO SUB U: GO TO FI
6704 IF H=26 AND NOT T5 AND NOT V5 THEN LET V$(I)="SEA": RANDOMIZE USR B: IF NOT NO THEN LET X2=O: LET T5=I: LET M$="O.K.": GO SUB U: LET T(H)=T(H)-I: LET P1=I: GO TO L
6705 IF V$(I)="CAL" AND H=24 AND T(H)>-I THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET M$="THE GUARD OPENS THE DOOR": GO SUB U: LET T(H)=-5: GO TO FI
6747 GO TO 7705
6749 IF LE>I THEN GO TO 6751
6750 IF H=4 AND BL AND T(H)=11 THEN LET V$(I)="BLA": RANDOMIZE USR B: IF NOT NO THEN LET S(4)=1: LET T(H)=O: GO TO FI
6758 GO TO 6747
6802 DIM U$(32)
6804 LET U$=Y$(I)
6805 FOR Y=I TO 5
6806 IF CODE G$(Y)=32 THEN GO TO 6810
6807 FOR A=I TO LEN U$-3
6808 IF U$(A TO A+3)=G$(Y)( TO 4) THEN GO TO 6812
6809 NEXT A
6810 NEXT Y
6811 LET M$="YOU'RE NOT CARRYING IT!": GO SUB U: LET P1=I: GO TO L
6812 LET F=6850
6813 IF (LE=I AND (H=15 OR H=16 OR H=21 OR H=20 OR H=19 OR H=26 OR H=8 OR H=3)) OR (LE=2 AND (H=6 OR H=12 OR H=19 OR H=14 OR H=9)) THEN LET M$="YOU CAN'T DROP IT HERE": GO SUB U: LET P1=I: GO TO L
6814 IF NOT R(H) THEN GO TO 6853
6815 IF LE=2 THEN GO TO 6828
6816 IF H=3 AND R$(H)( TO 3)="BLA" THEN LET R(H)=O: GO TO F
6818 IF H=8 AND R$(H)( TO 3)="IDE" THEN LET R(H)=O: GO TO F
6820 IF H=15 AND R$(H)( TO 3)="FRU" THEN LET R(H)=O: GO TO F
6822 IF H=20 AND R$(H)( TO 3)="SPA" THEN LET R(H)=O: GO TO F
6824 IF H=21 AND R$(H)( TO 3)="SCR" THEN LET R(H)=O: GO TO F
6826 IF H=26 AND R$(H)( TO 3)="EXT" THEN LET R(H)=O: GO TO F
6827 GO TO F
6828 IF H=6 AND R$(H)( TO 3)="ROP" THEN LET R(H)=O: GO TO F
6830 IF H=9 AND R$(H)( TO 3)="MON" THEN LET R(H)=O: GO TO F
6832 IF H=12 AND R$(H)( TO 3)="RES" THEN LET R(H)=O: GO TO F
6834 IF H=14 AND R$(H)( TO 3)="KEY" THEN LET R(H)=O: GO TO F
6836 IF H=19 AND R$(H)( TO 3)="TOR" THEN LET R(H)=O
6850 IF P(H) THEN LET M$="YOU CAN'T "+Y$(I)( TO VAL "20"): GO SUB U: LET P1=I: GO TO L
6852 IF R(H) THEN LET P$(H)=G$(Y): LET P(H)=I: LET G$(Y)="": LET T(H)=T(H)-I: LET P1=I: LET SC=SC-10: LET M$="O.K.": GO SUB U: GO TO L
6853 IF G$(Y)( TO 3)="ROP" THEN LET S(3)=O
6854 IF G$(Y)( TO 3)="MON" THEN LET S(8)=O
6855 IF G$(Y)( TO 3)="RES" THEN LET S(2)=O
6856 IF G$(Y)( TO 3)="KEY" THEN LET S(1)=O
6857 IF G$(Y)( TO 3)="TOR" THEN LET TO=O
6858 IF G$(Y)( TO 3)="BLA" THEN LET BD=O
6859 IF G$(Y)( TO 3)="SCR" THEN LET S(6)=O
6860 IF G$(Y)( TO 4)="UNIF" THEN LET S(I)=O: LET S(9)=O
6861 IF G$(Y)( TO 4)="EXTI" THEN LET V5=O
6862 IF G$(Y)( TO 3)="IDE" THEN LET I1=O: LET I2=I
6863 LET R$(H)=G$(Y): LET R(H)=I: LET G$(Y)="": LET T(H)=T(H)-I: LET P1=I: LET SC=SC-10: LET M$="O.K.": GO SUB U: GO TO L
6900 IF V$(I)="EAT" OR V$(I)="ENT" THEN GO TO 6980
6901 LET P1=I: IF LE>I THEN GO TO 6920
6902 IF H=26 AND T(H)>I THEN LET V$(I)="EXT": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE A PIN!": GO SUB U: GO TO L
6904 IF H=2 AND T(H)>2 THEN LET V$(I)="FLO": RANDOMIZE USR B: IF NOT NO THEN LET M$="A PRESSURE PAD IS FITTED TO": GO SUB U: LET M$="THE FLOOR!": GO SUB U: GO TO L
6906 IF H=26 AND T(H)>I THEN LET V$(I)="PIN": RANDOMIZE USR B: IF NOT NO THEN LET M$="IT COULD BE OF VALUE!": GO SUB U: GO TO L
6908 IF H=7 THEN LET V$(I)="WAL": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE A BUTTON!": GO SUB U: GO TO L
6910 IF H=I THEN LET V$(I)="LIF": RANDOMIZE USR B: IF NOT NO THEN LET M$="A TRAPDOOR ABOVE!": GO SUB U: LET T(H)=T(H)+I: GO TO L
6912 IF H=8 AND G1 AND NOT ID THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE AN IDENTITY CARD!": GO SUB U: GO TO L
6914 IF H=20 AND T(H)=I THEN LET V$(I)="BOX": RANDOMIZE USR B: IF NOT NO THEN LET M$="HERE LIES A SPANNER": GO SUB U: LET T(H)=T(H)+I: GO TO L
6916 IF H=22 THEN LET V$(I)="CEL": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE A BUNK AND...A SWITCH": GO SUB U: GO TO L
6919 GO TO 6996
6921 IF H=16 AND T(H)<>I THEN LET V$(I)="ROO": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE THE RADAR CONTROL PANEL": GO SUB U: LET T(H)=T(H)+I: GO TO L
6922 IF H=16 AND T(H)>O THEN LET V$(I)="PAN": RANDOMIZE USR B: IF NOT NO THEN LET M$="TWO BUTTONS: ONE RED, ONE GREEN": GO SUB U: GO TO L
6923 IF H=12 THEN LET V$(I)="WAL": RANDOMIZE USR B: IF NOT NO THEN LET M$="A TELEPHONE!": GO SUB U: GO TO L
6924 IF H=17 AND NOT T(H) THEN LET V$(I)="ROO": RANDOMIZE USR B: IF NOT NO THEN LET M$="A SWITCH AND A LEVER!!": GO SUB U: GO TO L
6925 IF H=22 AND NOT S(4) THEN LET V$(I)="CON": RANDOMIZE USR B: IF NOT NO THEN LET M$="DISCONNECTED COMPUTER LINKAGE!": GO SUB U: GO TO L
6926 IF H=20 AND S(4) THEN LET V$(I)="COM": RANDOMIZE USR B: IF NOT NO THEN LET M$="THE VDU HAS SOMETHING WRITTEN": GO SUB U: LET T(H)=T(H)+I: LET M$="ON IT!": GO SUB U: LET S(5)=I: PRINT AT 2,9; INK 7; PAPER O;"ZOOM": GO TO L
6930 GO TO 6996
6980 IF E AND LE=1 THEN LET V$(I)="FRU": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU FALL INTO A DRUGGED SLEEP": GO SUB U: LET M$="AND AWAKE IN A CELL!": GO SUB U: GO SUB 3000: LET H=24: FOR Y=30 TO -30 STEP -2: BEEP .01,Y: NEXT Y: GO TO L
6981 IF LE=2 AND H=5 THEN LET V$(I)="LIF": RANDOMIZE USR B: IF NOT NO THEN LET H=10: LET T(H)=O: LET U(H)=O: GO TO L
6984 GO TO 7705
6997 LET M$="I SEE NOTHING SPECIAL!": GO SUB U: LET P1=I: GO TO L
7002 IF LE>I THEN GO TO 7050
7003 IF H=15 AND BL AND S(4) AND T(H)=15 THEN LET V$(I)="BLA": RANDOMIZE USR B: IF NOT NO THEN LET S(4)=O: LET T(H)=50: GO TO FI
7004 IF BL AND S(4) THEN LET V$(I)="BLA": RANDOMIZE USR B: IF NOT NO THEN LET S(4)=O: GO TO FI
7005 IF BL AND NOT S(4) THEN LET V$(I)="BLA": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOUR BLASTER IS DEAD": GO SUB U: LET P1=I: GO TO L
7048 GO TO 7705
7050 IF LE<>2 THEN GO TO 7098
7052 IF H=17 AND NOT U(H) THEN LET V$(I)="SWI": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: LET M$="AN EXIT APPEARS TO THE SOUTH!": GO SUB U: GO TO FI
7058 GO TO 7705
7098 LET V$(I)="DDD": RANDOMIZE USR B: GO TO 7650
7100 LET I$="": LET O$="XXXX": GO SUB 7131
7101 FOR Y=I TO 5: IF CODE G$(Y)=32 THEN GO TO 7104
7102 NEXT Y
7103 LET M$="YOU CAN'T, YOU ARE CARRYING": GO SUB U: LET M$="TOO MUCH.": GO SUB U: LET P1=I: GO TO L
7104 RESTORE 1870+(30*LE)+H: READ I$: IF LE=I AND H=26 AND NOT T(H) THEN GO TO 7113
7105 LET E$=I$: IF CODE E$=32 THEN LET E$=O$
7106 FOR Y=I TO 5: IF (G$(Y)( TO 4)=I$( TO 4) AND NOT R(H)) OR (G$(Y)( TO 4)=O$( TO 4) AND R(H)) THEN GO TO 7113
7107 NEXT Y: IF K AND E$( TO 3)="KEY" THEN GO TO 7113
7108 IF R(H) THEN FOR Y=I TO LEN Y$(I)-3: IF Y$(I)(Y TO Y+3)=O$( TO 4) THEN GO TO 7114
7109 IF R(H) THEN NEXT Y: IF P(H) THEN LET TT=I: GO TO 7100
7110 FOR Y=I TO LEN Y$(I)-3: IF Y$(I)(Y TO Y+3)="EXTI" AND H=26 AND NOT X2 THEN GO TO 7113
7111 IF Y$(I)(Y TO Y+3)=I$( TO 4) THEN GO TO 7114
7112 NEXT Y
7113 LET M$="IT'S NOT HERE": GO SUB U: LET P1=I: GO TO L
7114 FOR Y=I TO 5
7115 IF CODE G$(Y)<>32 THEN GO TO 7126
7116 LET G$(Y)=E$: IF G$(Y)( TO 3)="EXT" THEN LET V5=I
7117 IF G$(Y)( TO 3)="IDE" THEN LET ID=I: LET I1=I: LET I2=O
7118 IF G$(Y)( TO 3)="BLA" THEN LET BL=I: LET BD=I
7119 IF G$(Y)( TO 3)="UNI" THEN LET S(9)=I
7120 IF G$(Y)( TO 3)="SCR" THEN LET S(6)=I: LET SR=I
7121 IF G$(Y)( TO 3)="SPA" THEN LET S(5)=I
7122 IF G$(Y)( TO 3)="FRU" THEN LET E=I
7123 IF G$(Y)( TO 3)="SON" THEN LET SO=I
7125 GO TO 7127
7126 NEXT Y
7127 IF TT THEN LET P(H)=O: LET P$(H)="": LET TT=O: GO TO 7129
7128 LET R(H)=P(H): LET R$(H)=P$(H): LET P(H)=O: LET P$(H)=""
7129 IF LE>I THEN GO SUB 7145
7130 GO TO 7650
7131 POKE R,O: IF R(H) THEN GO TO 7135
7132 IF LE=2 AND H=6 OR H=12 OR H=19 OR H=14 OR H=9 THEN RETURN 
7133 IF LE=I AND H=16 OR H=21 OR H=15 OR H=20 OR H=26 OR H=8 OR H=3 THEN RETURN 
7134 GO TO 7113
7135 FOR Y=I TO 5: IF CODE G$(Y)=32 THEN GO TO 7140
7136 NEXT Y
7137 GO TO 7103
7140 LET O$=R$(H): IF TT THEN LET O$=P$(H)
7141 GO TO 7105
7146 POKE R,O: FOR Y=I TO 5
7147 IF G$(Y)( TO 3)="ROP" THEN LET S(3)=I: LET RO=I
7148 IF G$(Y)( TO 3)="MON" THEN LET S(8)=I: LET M=I
7149 IF G$(Y)( TO 3)="RES" THEN LET S(2)=I: LET RE=I
7150 IF G$(Y)( TO 3)="KEY" THEN LET S(I)=I: LET K=I
7151 IF G$(Y)( TO 3)="TOR" THEN LET TO=I
7152 NEXT Y: RETURN 
7202 GO SUB 7180+(30*LE)+H
7203 POKE R,O: IF V1=O THEN LET M$="YOU CHOSE THE ADVENTURE...": GO SUB U: LET M$="...NOW SOLVE IT!!!"
7205 GO SUB U: LET P1=I: LET V1=O: GO TO L
7230 RETURN 
7231 LET M$="YOU'RE REALLY SWITCHED ON, MAN": LET V1=I: RETURN 
7233 RETURN 
7234 GO TO 7245
7236 LET M$="ALL IS NOT WHAT IT SEEMS!": LET V1=I: RETURN 
7243 RETURN 
7245 LET M$="WORDS ARE ALL I HAVE": LET V1=I: RETURN 
7259 RETURN 
7260 LET M$="YOU'RE THE COMPUTER FREAK!!!": LET V1=I: RETURN 
7266 RETURN 
7301 IF V$(I)="INS" THEN GO TO 7320
7302 LET M$="YOU ARE CARRYING:": GO SUB U
7304 FOR Y=I TO 5
7306 IF CODE G$(Y)=32 THEN LET A2=A2+I: GO TO 7310
7307 LET M$=G$(Y)
7308 IF G$(Y)( TO 3)="UNI" AND S(I) THEN LET M$=M$( TO 7)+" (BEING WORN)"
7309 GO SUB U
7310 NEXT Y
7312 IF A2=5 THEN LET M$="NOTHING!": GO SUB U
7314 LET A2=O: LET P1=I: GO TO L
7320 IF LE=I AND H=3 AND ID AND NOT U(H) THEN LET V$(I)="IDE": LET T(H)=4: RANDOMIZE USR B: GO TO 7650
7400 IF V$(I)="JUM" AND H=24 AND T(H)=-4 THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET LO=O: LET T(H)=O: GO TO FI
7401 IF LE=2 AND H=22 AND NOT S(4) THEN LET V$(I)="LIN": RANDOMIZE USR B: IF NOT NO THEN LET S(4)=I: GO TO FI
7402 GO TO 7705
7501 IF LE=1 AND H=24 AND T(H)<O THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET M$="HE OVERPOWERS YOU!": GO SUB U: LET P1=I: GO TO L
7502 IF LE=I AND H=8 AND NOT G1 THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=15: LET G1=I: GO TO 7650
7503 IF LE=2 AND H=3 AND S(9) THEN GO TO 7705
7504 IF LE=2 AND H=3 OR H=23 OR H=5 THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET M$="HE KILLS YOU!": GO SUB U: FOR A=I TO 50: BEEP .01,RND*40: NEXT A: RUN 9600
7598 GO TO 7705
7600 IF V$(I)="LOO" THEN LET V(H)=O: LET P1=O: GO TO L
7601 IF LE>I THEN GO TO 7606
7602 IF H=26 AND NOT X2 AND NOT V5 THEN LET V$(I)="SEA": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE A FIRE EXTINGUISHER!": GO SUB U: LET X2=I: LET T5=O: GO TO FI
7603 IF H=20 AND NOT LT THEN LET V$(I)="TRA": RANDOMIZE USR B: IF NOT NO THEN LET LT=I: GO TO FI
7604 GO TO 7705
7606 IF H=26 AND IG AND NOT FIN THEN LET V$(I)="OFF": RANDOMIZE USR B: IF NOT NO THEN LET FIN=I: FOR Y=-20 TO 30: BEEP .1,Y: NEXT Y: LET P1=O: GO TO L
7608 IF H=12 AND T(H)>-I THEN LET V$(I)="PHO": RANDOMIZE USR B: IF NOT NO THEN LET T(17)=O: LET U(17)=O: LET T(H)=-9: FOR Y=I TO 3: FOR C=I TO 2: FOR A=1 TO 8: BEEP .03,24: NEXT A: PAUSE 6: NEXT C: PAUSE 50: NEXT Y: LET M$="A VOICE SAYS 'YES?'": GO SUB U: GO TO FI
7649 GO TO 7705
7650 IF NOT NO THEN LET T(H)=T(H)+I: LET M$="O.K.": GO SUB U: LET P1=I: LET SC=SC+10
7652 IF NO THEN LET M$="YOU CAN'T "+Y$(I)( TO 20): GO SUB U: LET P1=I: GO TO L
7653 IF H=I AND T(H)>O THEN LET P1=O: LET T(H)=10: FOR A=I TO 50: NEXT A
7654 IF T(H)=16 AND H=8 AND LE=I THEN LET U(H)=I
7656 IF T(H)>50 THEN LET P1=I: LET U(H)=I: LET T(H)=O
7657 IF T(H)=9 THEN LET M$="A DOOR APPEARS TO THE NORTH!": GO SUB U: LET P1=O: LET T(H)=10: FOR Y=I TO 50: NEXT Y
7658 IF T(H)=8 AND H=11 AND LE=I AND NOT Y1 THEN LET M$="NOW YOU CAN SEE": GO SUB U: LET Y1=I: LET U(H)=I: LET P1=O: FOR Y=I TO 50: NEXT Y
7659 IF T(H)=5 THEN LET M$="THE DOOR OPENS...": GO SUB U: LET P1=I: LET U(H)=I: LET T(H)=O: IF LE=2 AND (H=17 OR H=10) THEN LET P1=O: LET T(H)=5: FOR A=I TO 70: NEXT A
7660 GO TO L
7701 IF LE>I THEN GO TO 7707
7702 IF H=16 AND NOT S(2) THEN LET V$(I)="UNI": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=8: LET S(2)=I: GO TO FI
7704 IF H=22 AND NOT S(7) THEN LET V$(I)="BUN": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=T(H)-I: LET S(7)=I: LET M$="YOU SEE A VENTILATOR GRILLE!": GO SUB U: LET P1=I: GO TO FI
7705 IF NO THEN GO TO 7650
7706 GO TO 7098
7707 IF H=I AND NOT MG THEN LET V$(I)="GRI": RANDOMIZE USR B: IF NOT NO THEN LET MG=I: GO TO 7650
7708 IF LE=2 AND H=1 THEN LET T(H)=O
7749 GO TO 7705
7901 IF LE>I THEN GO TO 7907
7903 IF H=2 AND ZZ THEN LET V$(I)="REA": RANDOMIZE USR B: IF NOT NO THEN LET ZZ=O: GO TO 1570
7904 IF H=23 AND NOT U(H) THEN LET V$(I)="DOO": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
7905 GO TO 7705
7910 IF H=14 AND NOT IF THEN LET V$(I)="DRA": RANDOMIZE USR B: IF NOT NO THEN LET IF=I: IF NOT NO AND NOT S(1) THEN LET M$="YOU SEE A KEY!": GO SUB U: LET T(H)=O: GO TO FI
7912 IF H=15 AND NOT U(H) THEN LET V$(I)="T D": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
7948 GO TO 7705
8000 IF V$(I)="PIC" THEN GO TO 8090
8002 IF V$(I)="PRE" THEN GO TO 8060
8004 IF LE=I AND H=26 AND T(H)>I AND NOT T6 THEN LET V$(I)="PIN": RANDOMIZE USR B: IF NOT NO THEN LET T6=I: GO TO FI
8005 IF LE=2 AND H=17 THEN LET V$(I)="LEV": LET T(H)=O: RANDOMIZE USR B: IF NOT NO THEN LET T(H)=20: GO TO FI
8006 IF T(17)=20 THEN FOR Y=30 TO -20 STEP -I: BEEP .01,Y: NEXT Y: LET M$="YOU ARE EJECTED INTO DEEP SPACE": GO SUB U: LET M$="YOU WILL DRIFT FOREVER...": GO SUB U: LET M$="PRESS ENTER TO TRY AGAIN": GO SUB U: PAUSE O: PAUSE O: RUN 9600
8050 GO TO 7705
8060 IF LE>I THEN GO TO 8064
8061 IF H=7 AND NOT T7 THEN LET V$(I)="BUT": RANDOMIZE USR B: IF NOT NO THEN LET T(7)=4: LET T7=I: GO TO FI
8062 IF H=6 AND NOT U(H) THEN LET V$(I)="BUT": RANDOMIZE USR B: IF NOT NO THEN LET T(6)=4: GO TO FI
8063 GO TO 7705
8065 IF H<>16 THEN GO TO 8080
8066 IF H=16 THEN LET V$(I)="BUT": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=T(H)-I: GO SUB 8068: GO TO FI
8067 GO TO 8080
8068 POKE R,O: DIM U$(32): LET U$=Y$(I): FOR Y=I TO LEN U$-3: IF U$(Y TO Y+3)=" RED" THEN LET S(7)=I: LET GG=GG+I: POKE 22695,GG
8069 IF U$(Y TO Y+3)="GREE" THEN LET S(7)=O: LET GH=GH+I: POKE 22638,GH
8070 NEXT Y: RETURN 
8080 IF H=10 AND NOT U(H) AND NOT D THEN LET V$(I)="DOW": RANDOMIZE USR B: IF NOT NO THEN LET M$="THE LIFT GLIDES DOWN.": GO SUB U: LET D=I: LET T(H)=4: GO TO FI
8081 IF H=10 AND D AND U(H) THEN LET V$(I)="DDD": RANDOMIZE USR B: LET T(H)=O
8082 IF H=26 AND NOT IG THEN LET V$(I)="IGN": RANDOMIZE USR B: IF NOT NO THEN LET IG=1: GO TO FI
8083 IF H=10 AND NOT U(H) AND D THEN LET V$(I)="P B": RANDOMIZE USR B: IF NOT NO THEN LET M$="THE LIFT DEPOSITS YOU OUTSIDE": GO SUB U: LET M$="THE DOORS!": GO SUB U: LET H=15: LET D=O: LET P1=O: FOR A=I TO 50: NEXT A: GO TO L
8089 GO TO 7705
8090 IF LE=I AND H=26 AND T(H)>2 AND NOT T(H)>4 THEN LET V$(I)="LOC": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
8098 GO TO 7705
8101 LET M$="YOUR SCORE:"+STR$ SC: GO SUB 65: FOR Y=I TO 60: NEXT Y
8102 CLS : PRINT "DO YOU WANT TO SAVE THE         ADVENTURE"
8104 GO SUB 9640
8106 IF A$="N" THEN RUN 9600
8110 GO SUB 9640
8111 LET S(12)=SC: LET S(10)=H: LET S(11)=LE: RESTORE 9680+LE: FOR Y=13 TO 17: READ Q: LET S(Y)=Q: NEXT Y
8114 POKE R,O: SAVE "DATA 1" DATA G$(): SAVE "DATA 2" DATA R$(): SAVE "DATA 3" DATA P$(): SAVE "DATA 4" DATA U(): SAVE "DATA 5" DATA S(): SAVE "DATA 6" DATA T(): SAVE "DATA 7" DATA P(): SAVE "DATA 8" DATA R()
8116 LET P1=I: GO TO L
8202 IF H=28 AND LE=I THEN LET V$(I)="SIG": RANDOMIZE USR B: IF NOT NO THEN LET M$="IT SAYS 'TOUGH LUCK-THIS IS AS": GO SUB U: LET M$="FAR AS YOU CAN GO!'": GO SUB U: LET M$="PRESS ENTER TO TRY AGAIN": GO SUB U: FOR Y=I TO 30: BEEP .1,Y: NEXT Y: GO TO 1573
8288 GO TO 8378
8300 IF V$(I)="SCO" THEN GO TO 8380
8301 IF V$(I)="SHO" THEN GO TO 8350
8302 IF V$(I)="STR" OR V$(I)="STA" THEN GO TO 8390
8303 IF V$(I)="SAY" THEN GO TO 8360
8304 IF V$(I)="SLI" THEN GO TO 8370
8305 IF LE=I AND H=11 AND T(H)<7 THEN LET V$(I)="N L": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=7: BORDER 7: GO TO FI
8306 IF LE=I AND H=21 AND NOT S(3) THEN LET V$(I)="F V": RANDOMIZE USR B: IF NOT NO THEN LET S(3)=I: LET T(H)=T(H)-1: FOR Y=O TO 160 STEP 32: POKE 22629+Y,130: POKE 22640+Y,130: NEXT Y: GO TO FI
8307 IF LE=I AND H=21 AND S(3) THEN LET V$(I)="N V": RANDOMIZE USR B: IF NOT NO THEN LET S(3)=O: LET T(H)=T(H)-1: FOR Y=O TO 160 STEP 32: POKE 22629+Y,132: POKE 22640+Y,132: NEXT Y: GO TO FI
8308 IF LE=I AND H=4 AND T(H)<9 THEN LET V$(I)="N U": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=10: GO TO FI
8349 GO TO 7705
8350 IF LE=I AND H=22 AND S AND NOT U(H) THEN LET V$(I)="WIR": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
8359 GO TO 7705
8360 IF H=5 AND S(5) THEN LET V$(I)="SSW": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU ASCEND INTO THE LIFE BOAT..": GO SUB U: FOR A=I TO 100: NEXT A: LET H=26: GO TO L
8362 IF LE=2 AND H=12 AND T(H)<O AND NOT U(H) THEN LET V$(I)="UNL": RANDOMIZE USR B: IF NOT NO THEN LET U(H)=I: GO TO FI
8369 GO TO 7705
8370 IF LE=2 AND H=7 AND NOT U(H) THEN LET V$(I)="DOO": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
8379 GO TO 7705
8380 LET M$="YOUR SCORE SO FAR IS..."+STR$ SC: GO SUB U: LET P1=I: GO TO L
8390 IF LE=I THEN GO TO 8396
8392 IF H=3 AND NOT S(9) THEN LET V$(I)="GUA": RANDOMIZE USR B: IF NOT NO THEN LET S(9)=I: LET U(H)=I: GO TO FI
8395 GO TO 7705
8396 IF H=2 AND T(H)>I THEN LET V$(I)="N P": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
8399 GO TO 7705
8501 IF V$(I)="UNL" OR Y$(I)( TO 3)="UNS" THEN GO TO 8560
8502 IF LE>I THEN GO TO 8515
8503 IF H=22 THEN GO TO 8510
8504 IF V5 THEN LET V$(I)="EXT": RANDOMIZE USR B
8506 IF NO OR NOT V5 THEN GO TO 7705
8508 BORDER O: PAPER O: INK 7: CLS : PRINT AT 10,8;"YOU ARE GASSED";AT 18,3;"PRESS ENTER TO TRY AGAIN": FOR A=I TO 30: BEEP .1,A: NEXT A: GO TO 1573
8510 IF S(7) AND S(5) AND NOT S(8) THEN LET V$(I)="SPA": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=T(H)-I: LET M$="THE GRILLE SWINGS OPEN...": GO SUB U: LET M$="YOU CAN GO DOWN!": GO SUB U: FOR Y=I TO 60: NEXT Y: LET S(8)=I: GO TO L
8549 GO TO 7705
8560 IF LE=I AND H=22 AND S(6) AND NOT S THEN LET V$(I)="SWI": RANDOMIZE USR B: IF NOT NO THEN LET M$="YOU SEE TWO WIRES!": GO SUB U: LET T(H)=O: LET S=I: GO TO FI
8562 IF LE=2 AND H=11 AND S(I) AND NOT U(H) THEN LET V$(I)="DOO": RANDOMIZE USR B: IF NOT NO THEN LET T(H)=4: GO TO FI
8598 IF NO THEN GO TO 7650
8599 GO TO 7705
8702 IF H=12 AND LE=I AND S(9) AND NOT S(I) THEN LET V$(I)="UNI": RANDOMIZE USR B: IF NOT NO THEN LET S(I)=I: GO TO FI
8749 GO TO 7705
9598 POKE 23563,0: PRINT AT 21,11; FLASH 1; INK 7;"STOP TAPE": IF INKEY$="" THEN BEEP .1,30: GO TO 9598
9599 CLEAR 65196: BORDER 7: PAPER 7: INK 0: CLS : PRINT AT 2,10; FLASH 1;"START TAPE": POKE 23563,0: LOAD ""CODE : RUN 9600
9600 DIM Y$(1,32): DIM V$(1,3): LET NO=2
9601 POKE 23658,8: LET R=23563: LET I=1: LET O=0: POKE R,O: BORDER 7: PAPER 7: INK O: CLS 
9602 DIM G$(5,12): DIM R$(30,12): DIM P$(30,12): DIM U(30): DIM S(17): DIM T(30): DIM P(30): DIM R(30)
9603 LET RE=O: LET TO=O: LET RO=O: LET M=O: LET SR=O: LET SO=O: LET E=O: LET BL=O: LET K=O
9604 LET SC=O: LET H=26: LET LE=I
9610 CLS : PRINT "DO YOU WANT TO LOAD A TAPE?(Y/N)"
9612 GO SUB 9640
9614 IF A$<>"Y" THEN GO TO 9625
9616 PRINT FLASH I;"START THE TAPE"
9618 POKE R,O: LOAD "" DATA G$(): LOAD "" DATA R$(): LOAD "" DATA P$(): LOAD "" DATA U(): LOAD "" DATA S(): LOAD "" DATA T(): LOAD "" DATA P(): LOAD "" DATA R()
9620 LET SC=S(12): LET H=S(10): LET LE=S(11): IF LE=I THEN LET V5=S(13): LET SR=S(14): LET BL=S(15): LET SO=S(16): LET G1=S(17)
9621 IF LE=2 THEN LET RO=S(13): LET RE=S(14): LET M=S(15): LET K=S(16): LET TO=S(17)
9622 GO TO I
9625 POKE R,O: PRINT "DO YOU WISH TO TRY AGAIN? (Y/N)": GO SUB 9640
9627 IF A$="Y" THEN GO TO I
9629 PRINT "ARE YOU SURE? (PRESS 'Y' TO PLAYAGAIN)": GO SUB 9640
9630 IF A$="Y" THEN GO TO I
9633 RANDOMIZE USR O
9640 POKE R,O: FOR Y=I TO 7: LET A$="": NEXT Y
9641 POKE R,O: LET A$=INKEY$: IF A$="" THEN GO TO 9640
9642 RETURN 
9681 DATA V5,SR,BL,SO,G1
9682 DATA RO,RE,M,K,TO
