0 REM ASTRONAVE FARMER (C) 1983 MARIO PETTENGHI & MCOMPUTER
1 DEFINT A-Z:CO=8:VE=11:SOG=20:OGG=10
2 DIM CO$(8),VE$(11),SOG$(20),OGG(10),OGG$(10),FL(9)
3 CL$=CHR$(26):HO$=CHR$(21):UP$=CHR$(11):DO$=CHR$(10):RI$=CHR$(12):BE$=CHR$(7):WIDTH 78
10 GOTO 500
20 LINE INPUT "COSA DEVO FARE? ";FR$:PRINT:X=0:Y=0
30 FOR J=1 TO CO:IF INSTR(1,FR$,CO$(J))<>0 THEN X=J
40 NEXT:ON X GOSUB 100,100,100,100,8000,9000,9500,9700:IF X THEN RETURN
50 FOR J=1 TO VE:IF INSTR(1,FR$,VE$(J))<>0 THEN X=J
60 NEXT:FOR J=1 TO SOG:IF INSTR(1,FR$,SOG$(J))<>0 THEN Y=J
70 NEXT:ON X GOSUB 1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,1920:IF X THEN RETURN
80 PRINT DO$;BE$;"..SARO` STUPIDO MA NON TI CAPISCO!";DO$:GOTO 20
90 N=0:E=0:S=0:O=0:PRINT BE$;
91 IF LO>20 THEN ON LO-20 GOSUB 2210,2220,2230,2240,2250,2260:GOTO 94
92 ON LO GOSUB 2000,2010,2020,2030,2040,2050,2060,2070,2080,2090,2100,2110,2120,2130,2140,2150,2160,2170,2180,2190,2200
94 PRINT:PRINT"DIREZIONI POSSIBILI: ";:IF N THEN PRINT"NORD ";
96 IF E THEN PRINT "EST ";
97 IF S THEN PRINT "SUD ";
98 IF O THEN PRINT "OVEST ";
99 RETURN
100 IF LO=2 AND X=3 AND FL(1)=0 THEN PRINT "LA PORTA DI SICUREZZA E' BLOCCATA!":RETURN
101 IF LO=10 AND X=2 AND OGG(3) THEN PRINT"SOLO GLI UFFICIALI POSSONO ENTRARE!":RETURN
102 IF LO=17 AND FL(6)=0 AND X=4 THEN PRINT "IL PORTELLONE NON SI APRE!!!":RETURN
103 IF LO=17 AND X=4 AND FL(4)=0 THEN PRINT"VUOI USCIRE DALLA NAVE MENTRE E' IN VOLO? IDIOTA.":RETURN
108 ON X GOTO 110,120,130,140
110 IF N=0 THEN 150 ELSE LO=N:RETURN
120 IF E=0 THEN 150 ELSE LO=E:RETURN
130 IF S=0 THEN 150 ELSE LO=S:RETURN
140 IF O=0 THEN 150 ELSE LO=O:RETURN
150 PRINT "NON POSSO ANDARE IN QUELLA DIREZIONE...":PRINT:RETURN
200 PRINT "OGGETTI VISIBILI:";
210 FL=0:FOR J=1 TO OGG:IF OGG(J)=LO THEN PRINT TAB(19);OGG$(J):FL=1
220 NEXT:IF FL=0 THEN PRINT TAB(19);"NESSUNO"
230 IF LO=8 AND FL(2)=0 THEN GOTO 9100
300 PRINT STRING$(78,"-"):RETURN
500 FOR J=1 TO CO:READ CO$(J):NEXT
540 FOR J=1 TO VE:READ VE$(J):NEXT
550 FOR J=1 TO SOG:READ SOG$(J):NEXT
560 FOR J=1 TO OGG:READ OGG(J),OGG$(J):NEXT
590 LO=1:PRINT CL$
600 PRINT STRING$(78,"-"):GOSUB 90
610 PRINT:GOSUB 200
620 PRINT:GOSUB 20
630 GOTO 600
1000 IF Y=0 THEN PRINT "NON CI TROVO NULLA DI INTERESSANTE!":RETURN
1010 IF LO=2 AND Y=10 THEN PRINT "E` UNA PORTA DI SICUREZZA. PUOI APRIRLA CON LA SUA SCHEDA DI RICONOSCIMENTO.":RETURN
1020 IF LO=4 AND Y=11 THEN PRINT "E` DI METALLO, MA LO SPORTELLO NON HA SERRATURE.":RETURN
1030 IF LO=6 AND Y=13 AND OGG(3)=69 THEN PRINT"ERA IL COMANDANTE C.P.USTINOV. GRAND'UOMO. NON HA SOLDI IN TASCA, MA HA UNA":PRINT"TESSERA CON FOTO SUL PETTO E UNA BRUTTA CERA.":OGG(3)=6:RETURN
1040 IF LO=18 AND Y=16 AND FL(3)=0 THEN PRINT"C'E` SCRITTO QUALCOSA IN UN VECCHIO LINGUAGGIO. UN APPASSIONATO DI COMPUTER SAPREBBE CERTO DECIFRARLO...":RETURN
1050 IF LO=18 AND Y=16 AND FL(3) THEN PRINT"LE TUE CONOSCENZE DI COMPUTER TI CONSENTONO DI LEGGERE IL CODICE DI USCITA":FL(6)=1 :RETURN
1060 IF LO=21 AND Y=18 AND OGG(8)<>0 THEN PRINT"E` DI OTTONE. C'E` UNA TORCIA SU DI ESSO!":OGG(8)=21:RETURN
1090 PRINT "MI BRUCIANO GLI OCCHI DALLO SFORZO.":RETURN
1100 FL=0:IF Y=0 THEN 1115
1102 IF LO=6 AND Y=13 THEN PRINT"NO, AMICO, CHIEDILO A FRANKENSTEIN!":RETURN
1103 IF LO=4 AND Y=2 THEN PRINT"NO, NON VALE LA PENA NON E` NEMMENO DIGITALE.":RETURN
1104 IF LO=5 AND Y=3 THEN PRINT "E'BLOCCATO CON UNA CATENELLA!":RETURN
1109 FOR J=1 TO OGG:IF INSTR(1,OGG$(J),SOG$(Y))<>0 THEN FL=J
1110 NEXT
1115 IF FL=0 OR LO<>OGG(FL) THEN PRINT "ANDIAMO, CERCA DI ESSERE SERIO!!":RETURN
1120 OGG(FL)=0:PRINT "OK, ESEGUITO.":RETURN
1200 FOR J=1 TO OGG:IF SOG$(Y)=OGG$(J) THEN FL=J
1210 NEXT
1220 IF FL=0 OR OGG(FL)<>0 THEN PRINT"COME POSSO LASCIARE QUELLO CHE NON HO, TOTANO!":RETURN
1230 OGG(FL)=LO:PRINT"OK... COME VUOI TU!":RETURN
1300 IF Y=0 THEN 1390
1310 IF LO=4 AND Y=2 THEN PRINT"E' SPENTO...":RETURN
1320 IF LO=11 AND Y=7 AND FL(5) AND OGG(7)=0 THEN LO=14:PRINT"OK, FUNZIONA, MA NON C'E` PIU` ENERGIA PER UN SECONDO UTILIZZO":RETURN
1330 IF LO=11 AND Y=7 AND FL(5)=0 THEN PRINT "PER FARLO DEVI ATTIVARE PRIMA IL MOTORE.":RETURN
1340 IF LO=11 AND Y=7 AND OGG(7) THEN PRINT "E` NECESSARIA LA CHIAVE DEL QUADRO COMANDI!":RETURN
1390 PRINT "PROVA A USARE IL CERVELLO, SE NON E` UN OPTIONAL!":RETURN
1400 IF Y=17 AND OGG(8)=0 THEN OGG(8)=-1:PRINT"OK....":RETURN
1408 IF Y<>2 THEN PRINT "ACCENDITI LA BARBA, IPPOGRIFO!":RETURN
1410 IF LO=4 AND OGG(6) THEN PRINT"DOVREI AVERE ANCHE UNA CASSETTA PER FARLO!":RETURN
1420 IF LO<>4 THEN PRINT "DOVE LO TROVO? SU AUDIO REVIEW?":RETURN
1440 PRINT"OK.. C'E` UN MESSAGGIO:";DO$:PRINT"SEI STATO RISVEGLIATO DALL'IBERNAZIONE PER UNA MISSIONE DI EMERGENZA!"
1450 PRINT"L'ASTRONAVE FARMER HA AVUTO UN'AVARIA. DEVI RIPROGRAMMARE IL COMPUTER":PRINT"DI BORDO PER UN ATTERRAGGIO DI FORTUNA."
1460 PRINT"PER FARLO TI SERVE UN FLOPPY DISC CHE CONTIENE I DATI NECESSARI. TROVALO":PRINT"E INSERISCILO NEL LETTORE DEL COMPUTER DI BORDO."
1470 PRINT"E` IN GIOCO ANCHE LA TUA VITA. AUGURI.":RETURN
1500 IF LO=13 AND Y=15 THEN PRINT "LE LUCI VACILLANO PER UN ISTANTE. TUTTO OK.":FL(5)=1:RETURN
1590 PRINT "FORSE UNA PETUNIA HA UN Q.I. SUPERIORE AL TUO...":RETURN
1600 IF Y=0 THEN PRINT "ORA TI APRIRO` IL CRANIO SE CONTINUI A DIRE SCEMATE!":RETURN
1610 IF LO=2 AND OGG(1)=0 AND Y=10 THEN PRINT"LA PORTA SI APRE LENTAMENTE...":FL(1)=1:RETURN
1620 IF LO=4 AND Y=11 THEN PRINT"C'E` UNA CASSETTA QUI DENTRO!":OGG(6)=4:RETURN
1690 PRINT "NON CI RIESCO!!!":RETURN
1700 IF LO=5 AND Y=3 THEN FL(3)=1: PRINT"ORA SAI TUTTO DI COMPUTER!":RETURN
1710 PRINT "E` MEGLIO UN BUON LIBRO DI FANTASCIENZA":RETURN
1800 IF Y<>6 THEN 1830
1810 IF OGG(5)<>0 THEN PRINT "NON MI SEMBRA DI AVERNE UNA...":RETURN
1820 FL(2)=1:PRINT "NON E` PROPRIO LA MIA TAGLIA, MA PUO` ANDARE!":RETURN
1830 PRINT"L'UNICA COSA CHE POTREI METTERE E` LA TUA TESTA IN UN TRITATUTTO!":RETURN
1900 PRINT CL$;:PRINT:RETURN
1920 IF LO<>16 OR Y<>5 THEN 1970
1930 IF OGG(4)<>0 THEN PRINT "BUONA IDEA! PECCATO CHE NON NE HO UNO...":RETURN
1940 PRINT "COMPLIMENTI! IL CALCOLATORE E` RIPROGRAMMATO. STIAMO ATTERRANDO!":PRINT "ORA DEVI USCIRE DALL'ASTRONAVE E LANCIARE L'SOS ALLA TERRA! FAI PRESTO!":FL(4)=1:RETURN
1970 PRINT"POTREI DIRE UNA VOLGARITA`... ":RETURN
2000 PRINT"ORA SONO NELLA MIA CABINA. LA MIA CAPSULA CRIOGENICA E` SPENTA.":PRINT"L'ARREDAMENTO E` RIDOTTO ALL'ESSENZIALE. POSSO USCIRE SOLO DALLA PORTA SUD."
2005 S=2:RETURN
2010 PRINT"SONO IN UNA STANZA DALLE PARETI DI METALLO. CI SONO USCITE IN OGNI DIREZIONE":PRINT"LA PORTA A SUD HA UNA SERRATURA MAGNETICA E SEMBRA MOLTO ROBUSTA."
2015 N=1:E=3:S=6:O=4:RETURN
2020 PRINT"SONO IN UN CORRIDOIO DEL SETTORE RICREATIVO. L'ILLUMINAZIONE E` SCARSA, MA":PRINT"RIESCO A ORIENTARMI. NESSUN SEGNO DI VITA, MI SENTO IN PERICOLO."
2025 N=5:E=24:O=2:RETURN
2030 PRINT"QUESTA E` LA SALA RIUNIONI. C'E` UN GRANDE TAVOLO CON MOLTE SEDIE, UN ARMADIO.":PRINT"SUL TAVOLO VEDO UN REGISTRATORE A CASSETTE, SEMBRA POSSA FUNZIONARE ANCORA."
2035 E=2:RETURN
2040 PRINT"SONO NELLA SALA DI LETTURA. CI SONO NUMEROSI VOLUMI E IN BELLA VISTA C'E` UNA":PRINT"COPIA RECENTE DI MCOMPUTER. NON C'E` ANIMA VIVA..."
2045 S=3:RETURN
2050 PRINT"MI TROVO IN UN LUNGO CORRIDOIO. PER TERRA DI FRONTE A ME C'E` UN CADAVERE.":PRINT"RIESCO A SENTIRE IL RONZIO DEI MOTORI DELL'ASTRONAVE."
2055 N=2:S=7:RETURN
2060 PRINT"ECCOMI IN UNA GRANDE STANZA. CI SONO PORTE DAPPERTUTTO. SONO VICINO AL CUORE":PRINT"DELLA NAVE. LA MOQUETTE NON E` DI MIO GUSTO MA SOPPORTO STOICAMENTE IL FATTO."
2065 N=6:E=9:S=10:O=8:RETURN
2070 PRINT"SONO NELLA CABINA DEL COMANDANTE. L'OBLO` DI FRONTE A ME E` INCRINATO. LA TEMPERATURA E` DI SETTANTA GRADI, LE RADIAZIONI SONO MORTALI."
2075 O=7:RETURN
2080 PRINT"QUESTA E` L'ARMERIA. GLI SCAFFALI SONO VUOTI, SIAMO IN MISSIONE PACIFICA.":PRINT"LA PORTA A EST E` CHIUSA E NON VEDO COME APRIRLA..."
2085 O=7:RETURN
2090 PRINT"A OVEST C'E` UN CORRIDOIO BLOCCATO DA UN ROBOT IN AVARIA. A SUD VEDO LO":PRINT"SPLENDORE DELLA GALASSIA DA UN OBLO` TRINITRON!"
2095 E=11:N=7 :RETURN
2100 PRINT"ECCO LA SALA UFFICIALI. C'E` UN ASCENSORE CON LE PORTE SPALANCATE. UN TAVOLO":PRINT"PIENO DI BICCHIERI (VUOTI). VEDO A EST UNA SALA CON PANNELLI DI CONTROLLO."
2105 E=13:O=10:RETURN
2110 RETURN: REM PARDON!
2120 PRINT"E' LA SALA CONTROLLO TRAFFICO. DI FRONTE A ME C'E` UN PULSANTE CON LA SCRITTA":PRINT"'INTERRUTTORE ASCENSORE', MI SEMBRA TROPPO BELLO..."
2125 O=11:RETURN
2130 PRINT "VEDO L'ASCENSORE, ORA INUTILIZZABILE. SONO IN UN LUNGO CORRIDOIO.":PRINT"A OVEST C'E` LA SALA DI SBARCO, A EST LA SALA DEL COMPUTER."
2135 E=15:O=17:RETURN
2140 PRINT"SONO NELLA SALA DEL COMPUTER. VEDO I GIGANTESCHI BANCHI DI MEMORIA DELLA":PRINT"SINCLAIR, E LE TASTIERE A SFIORAMENTO. SONO VICINO ALLA SALA CONTROLLI."
2145 N=18:E=16:O=14:RETURN
2150 PRINT "LA SALA CONTROLLO. DI FRONTE A ME C'E` IL DRIVE PER FLOPPY DISC, MA C'E`":PRINT"ANCHE UNA TELECAMERA DALL'ASPETTO SINISTRO..."
2155 O=15: IF OGG(3)=0 THEN 9110
2156 RETURN
2160 PRINT"ECCO LA SALA PER LO SBARCO. A OVEST C'E` IL PORTELLONE ELETTRONICO, A EST UN":PRINT"LUNGO CORRIDOIO BEN ILLUMINATO."
2165 O=19:E=14:RETURN
2170 PRINT"QUESTA E` LA SALA CONTROLLO SBARCO, C'E` UN TERMINALE ACCESO, UN VECCHIO":PRINT"QUANTUM BEEF, CON UN MONITOR HP A CRISTALLI TOSSICODIPENDENTI!"
2175 S=15:RETURN
2180 PRINT "SONO SBARCATO. IL CIELO E` VERDE CHIARO. SPIRA UN DOLCE VENTO. A OVEST VEDO":PRINT"UNA COSTRUZIONE. A NORD UNA CAVERNA. UN SENTIERO PORTA A SUD."
2185 N=20:S=22:E=17:RETURN:'O=21
2190 IF OGG(8)=-1 THEN 9200
2192 PRINT"L'INTERNO DELLA CAVERNA E` PIU` BUIO CHE IN UN ADVENTURE... I RUMORI SONO":PRINT"ALTRETTANTO RIPUGNANTI. ANIMA DI SCOTT ADAMS, AIUTAMI TU!"
2195 S=19:RETURN
2200 REM
2210 PRINT"SONO ALL'INTERNO DI UN TEMPIO ALIENO. C'E` UN GRANDE ALTARE CON UNA STATUA":PRINT"DI UNO SCARAFAGGIO A TRENTA BRACCIA, C'E` ODORE DI INSETTICIDA!"
2215 E=19:S=23:RETURN
2220 PRINT"SONO SU UNA COLLINA. IL PANORAMA SOTTO DI ME` E` ENTUSIASMANTE. VEDO VILLAGGI":PRINT"ALIENI, CARRI TRAINATI DA STRANI ANIMALI E LA RECLAME DELLA COCA-COLA."
2225 N=19:RETURN
2230 PRINT "SONO IN UNGA GRANDE SALA CON DIPINTI ALLE PARETI, GIOIELLI E ARMI. DALLA PORTA":PRINT"A EST SCORGO IL RESTO DEL VILLAGGIO, MA UNA FORZA MISTERIOSA NON MI PERMETTE DI PROSEGUIRE."
2235 N=21:RETURN
2240 PRINT"QUESTA E` LA SALA ADDESTRAMENTO COLONIZZATORI. NON RIESCO A ORIENTARMI.":PRINT"CI SONO STRANE ATTREZZATURE IN OGNI PUNTO."
2245 E=25:S=24:N=25:O=24:RETURN
2250 PRINT "SONO SEMPRE NEI LOCALI DI ADDESTRAMENTO. NON CONOSCO QUEST'AREA DELLA NAVE!"
2255 S=26:N=24:E=24:O=25:RETURN
2260 PRINT "QUESTA E` LA SALA ADDESTRAMENTO COLONIZZATORI. NON RIESCO A ORIENTARMI.":PRINT"CISONO STRANE ATTREZZATURE IN OGNI PUNTO..."
2265 N=3:S=24:O=25:E=25:RETURN
8000 PRINT:IF LO=1 THEN PRINT"SEI SOLO ALL'INIZIO.":RETURN
8010 IF LO=2 THEN PRINT"STAI IN GUARDIA E CERCA CON PAZIENZA.":RETURN
8020 IF LO<27 AND LO>23 THEN PRINT"STUDIA I PUNTI....":RETURN
8030 IF LO=17 THEN PRINT"CI VORREBBE IL CODICE COMPUTERIZZATO...":RETURN
8040 IF LO=18 AND FL(3)=0 THEN PRINT"SE TI FOSSI PREPARATO PRIMA, MAGARI LEGGENDO QUALCOSA DI UTILE...":RETURN
8050 IF LO=18 THEN PRINT PRINT"EPPURE QUALCOSA CI DEVE ESSERE...":RETURN
8060 IF LO=21 THEN PRINT"INSISTI, VEDO QUALCOSA...":RETURN
8070 IF LO=20 THEN PRINT"SE CI FOSSE MENO BUIO...":RETURN
8900 IF RND(1)>.5 THEN PRINT"SONO NEI GUAI COME TE." ELSE PRINT"MI HAI SCAMBIATO PER L'HOBBIT?"
8910 PRINT:RETURN
9000 PRINT CL$;"INVENTARIO:":PRINT"IN QUESTO MOMENTO HO CON ME:";DO$:FL=0
9010 FOR J=1 TO OGG:IF OGG(J)=0 THEN PRINT OGG$(J):FL=1
9020 NEXT:IF FL=0 THEN PRINT "UN BEL NIENTE..."
9030 RETURN
9100 PRINT:IF LO=8 THEN PRINT "LA MIA PELLE STA PRENDENDO FUOCO. HO POCHI SECONDI DI VITA E NON MI STO":PRINT"DIVERTENTO PER NIENTE. AAAARGH!":GOTO 9150
9110 PRINT DO$;DO$;"*** ATTENZIONE **** IDENTIFICAZIONE NEGATIVA. IL TUO TRACCIATO METABOLICO":PRINT"NON CORRISPONDE AL CAMPIONE. NON SEI IL COMANDANTE USTINOV!"
9112 PRINT DO$;"UN RAGGIO LASER MI TRAPASSA DA PARTE A PARTE, CON POCA DISCREZIONE. MUOIO!":GOTO 9150
9150 PRINT DO$;"HAI FALLITO MISERAMENTE. VUOI RITENTARE? (S/N)"
9160 A$=INKEY$:IF A$="" THEN 9160
9170 IF A$="S" OR A$="S" THEN RUN
9180 PRINT CL$;"ADDIO...":CLEAR:END
9200 PRINT DO$;BE$;DO$:PRINT"HAI TERMINATO QUESTA AVVENTURA...";DO$
9205 PRINT "DI FRONTE A ME C'E` UNA LASTRA DI PIETRA ALTA VENTI METRI: RECA UNA SCRITTA:";DO$
9210 READ X:IF X=255 THEN 9230
9220 PRINT CHR$(X-OGG(8)-LO);:GOTO 9210
9230 PRINT: PRINT DO$;"***COMPLIMENTI***";DO$:END
9500 PRINT CL$;BE$;"ASTRONAVE FARMER (AVVENTURA 1)********************************"
9510 PRINT DO$;"(C) 1984 MARIO PETTENGHI";DO$
9520 PRINT "COMANDI RICONOSCIUTI:";DO$
9530 FOR J=1 TO VE:PRINT VE$(J),:NEXT:FOR J=1 TO CO:PRINT CO$(J),:NEXT
9540 PRINT DO$;">PREMI UN TASTO PER RIPRENDERE IL GIOCO":A$=INKEY$
9550 A$=INKEY$:IF A$="" THEN 9550
9560 PRINT CL$:RETURN
9700 PRINT DO$;"*** SEI SICURO DI VOLER TERMINARE? ***"
9710 A$=INKEY$:IF A$="" THEN 9710
9720 IF A$="N" OR A$="N" THEN PRINT DO$;"HAI BISOGNO DI UNA LUNGA VACANZA...":RETURN
9730 IF A$="S" OR A$="S" THEN PRINT CL$:END
9740 GOTO 9710
10000 DATA NORD ,EST,SUD,OVEST,AIUTO,INVENTARIO,VOCABOLARIO,FINE:' VERBI
10010 DATA ESAMINA,PRENDI,LASCIA,USA,ACCENDI,PREMI,APRI,LEGGI,METTI,GUARDA,INSERISCI:'VERBI
10020 DATA SCHEDA,REGISTRATORE,MC,TESSERA,FLOPPY DISC,TUTA,ASCENSORE,CODICE,PORTELLO
10021 DATA PORTA,ARMADIO,CASSETTA,CADAVERE,CHIAVE,PULSANTE,MONITOR,TORCIA,ALTARE,LASER,PORTACHIAVI
10030 DATA 4,SCHEDA,5,MC,69,TESSERA,8,FLOPPY DISC,9,TUTA,32,CASSETTA,25,CHIAVE,29,TORCIA,4,LASER,7,PORTACHIAVI
10040 DATA 89,92,97,88,51,87,88,95,95,84,51,99,101,92,96,84,51,99,84,101,103,88,65,51,97,98,97,51,99,88,101,87,88,101,88,51,58,92,95,51,103,88,96,99,92,98,51,84,95,92,88,97,98,58,52,65,65,65,255
REM LE SEGUENTI LINEE RISOLVONO DUE BUG SULLA MAPPA
REM (ANTONINO PORCINO 2019)
2075 E=7:RETURN
2185 N=20:S=22:E=17:O=21:RETURN
