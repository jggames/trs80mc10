0 CLS:CLEAR500:DIMCC,ZZ,C1,M$,I$,C$(8),VE$(11),S$(20),O(10),O$(10),FL(9):CO=8:VE=11:SG=20:OG=10:GOTO500
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 CC=0:LL=LEN(I$):FORZZ=C1TOLEN(M$):IFMID$(M$,ZZ,LL)=I$THENCC=ZZ:ZZ=255
4 NEXT:RETURN
5 PRINTCHR$(91)"HIT A KEY"CHR$(93);:FORCC=0TO1STEP0:CC=-(INKEY$<>""):NEXT:FORCC=1TO11:PRINTCHR$(8);:NEXT:RETURN
9 GOSUB1:RETURN
20 INPUT"WHAT SHOULD I DO";FR$:PRINT:X=0:Y=0
30 FORJ=1TOCO:C1=1:M$=FR$:I$=C$(J):GOSUB3:IFCCTHENX=J
40 NEXT:ONXGOSUB100,100,100,100,8000,9000,9500,9700:IFXTHENRETURN
50 FORJ=1TOVE:C1=1:M$=FR$:I$=VE$(J):GOSUB3:IFCCTHENX=J
60 NEXT:FORJ=1TOSG:C1=1:M$=FR$:I$=S$(J):GOSUB3:IFCCTHENY=J
70 NEXT:ONXGOSUB1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,1920:IFXTHENRETURN
80 SOUND1,1:M$="...I MUST BE STUPID BUT I DON'T UNDERSTAND YOU!":GOSUB1:GOTO20
90 N=0:E=0:S=0:O=0
91 IFLO>20THENONLO-20GOSUB2210,2220,2230,2240,2250,2260:GOTO94
92 ONLO+0GOSUB2000,2010,2020,2030,2040,2050,2060,2070,2080,2090,2100,2110,2120,2130,2140,2150,2160,2170,2180,2190,2200
94 PRINT:M$="DIRECTIONS: ":IFNTHENM$=M$+"NORTH "
96 IFETHENM$=M$+"EAST "
97 IFSTHENM$=M$+"SOUTH "
98 IFOTHENM$=M$+"WEST "
99 GOSUB1:RETURN
100 IFLO=2ANDX=3ANDFL(1)=0THENM$="THE SECURITY DOOR IS LOCKED!":GOTO9
101 IFLO=10ANDX=2ANDO(3)THENM$="ONLY OFFICERS MAY ENTER!":GOTO9
102 IFLO=17ANDFL(6)=0ANDX=4THENM$="THE DOOR DOES NOT OPEN!!!":GOTO9
103 IFLO=17ANDX=4ANDFL(4)=0THENM$="DO YOU WANT TO EXIT THE SHIP WHILE IT'S IN FLIGHT? IDIOT.":GOTO9
108 ONXGOTO110,120,130,140
110 IFN=0THEN150
111 LO=N:RETURN
120 IFE=0THEN150
121 LO=E:RETURN
130 IFS=0THEN150
131 LO=S:RETURN
140 IFO=0THEN150
141 LO=O:RETURN
150 M$="CAN'T GO THAT DIRECTION...":GOSUB1:PRINT:RETURN
200 M$="VISIBLE ITEMS: ":FL=0:FORJ=1TOOG:IFO(J)=LOTHENIFLEN(M$)<220THENM$=M$+O$(J)+", ":FL=1
220 NEXT:IFFL=0THENM$=M$+"NONE, "
221 M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
230 IFLO=8ANDFL(2)=0THEN9100
300 PRINT"--------------------------------";:RETURN
500 FORJ=1TOCO:READC$(J):NEXT
540 FORJ=1TOVE:READVE$(J):NEXT
550 FORJ=1TOSG:READS$(J):NEXT
560 FORJ=1TOOG:READO(J),O$(J):NEXT
590 LO=1:CLS
600 GOSUB300:GOSUB90
610 PRINT:GOSUB200
620 GOSUB20
630 GOTO600
1000 IFY=0THENM$="I CAN'T FIND ANYTHING INTERESTING ABOUT IT!":GOTO9
1010 IFLO=2ANDY=10THENM$="IT IS A SECURITY DOOR. YOU CAN OPEN IT WITH A PASSCARD.":GOSUB1:GOTO5
1020 IFLO=4ANDY=11THENM$="IT IS METAL, BUT THE DOOR HAS NO LOCKS.":GOTO9
1030 IFLO=6ANDY=13ANDO(3)=69THEN1035
1031 GOTO1040
1035 M$="COMMANDER C. P. USTINOV. GREAT MAN. HE HAS NO MONEY IN HIS POCKETS, BUT HE HAS A "
1036 M$=M$+"PHOTO ID ON HIS CHEST WITH THE TYPICAL UGLY MUGSHOT.":GOSUB1:O(3)=6:GOTO5
1040 IFLO=18ANDY=16ANDFL(3)=0THEN1045
1041 GOTO1050
1045 M$="SOMETHING WRITTEN IN AN OLD COMPUTER LANGUAGE. A PROGRAMMER WOULD SURELY KNOW HOW TO DECIPHER IT..."
1046 GOSUB1:GOTO5
1050 IFLO=18ANDY=16ANDFL(3)THENM$="YOUR KNOWLEDGE OF COMPUTERS LETS YOU READ THE EXIT CODE":FL(6)=1:GOTO9
1060 IFLO=21ANDY=18ANDO(8)<>0THENM$="IT IS MADE OF BRASS. THERE IS A FLASHLIGHT ON IT!":O(8)=21:GOTO9
1090 M$="MY EYES ARE BURNING FROM THE EFFORT.":GOTO9
1100 FL=0:IFY=0THEN1115
1102 IFLO=6ANDY=13THENM$="NO, FRIEND, ASK FRANKENSTEIN!":GOTO9
1103 IFLO=4ANDY=2THENM$="NO, IT'S NOT WORTH IT'S NOT EVEN DIGITAL.":GOTO9
1104 IFLO=5ANDY=3THENM$="IT'S LOCKED WITH A CHAIN!":GOTO9
1109 FORJ=1TOOG:C1=1:M$=O$(J):I$=S$(Y):GOSUB3:IFCCTHENFL=J:J=OG
1110 NEXT
1115 IFFL=0ORLO<>O(FL)THENM$="COME ON, BE SERIOUS!!":GOTO9
1120 O(FL)=0:M$="OK, DONE.":GOTO9
1200 FORJ=1TOOG:IFS$(Y)=O$(J)THENFL=J
1210 NEXT
1220 IFFL=0ORO(FL)<>0THENM$="HOW CAN I DROP WHAT I DON'T HAVE!":GOTO9
1230 O(FL)=LO:M$="OK... AS YOU WISH!":GOTO9
1300 IFY=0THEN1390
1310 IFLO=4ANDY=2THENM$="IT'S OFF...":GOTO9
1320 IFLO=11ANDY=7ANDFL(5)ANDO(7)=0THENLO=14:M$="OK, IT WORKS, BUT THERE IS NO ENERGY FOR A SECOND USE.":GOSUB1:GOTO5
1330 IFLO=11ANDY=7ANDFL(5)=0THENM$="TO DO THIS YOU MUST ACTIVATE THE MOTOR FIRST.":GOTO9
1340 IFLO=11ANDY=7ANDO(7)THENM$="YOU NEED A KEYPAD!":GOTO9
1390 M$="TRY USING YOUR BRAIN IF IT'S NOT OPTIONAL!":GOTO9
1400 IFY=17ANDO(8)=0THENO(8)=-1:M$="OK...":GOTO9
1408 IFY<>2THENM$="LIGHT YOUR BEARD, HIPPOGRYPH!":GOTO9
1410 IFLO=4ANDO(6)THENM$="I SHOULD ALSO HAVE A CASSETTE TO DO THIS!":GOTO9
1420 IFLO<>4THENM$="WHERE DO I FIND IT? IN AUDIO REVIEW?":GOTO9
1440 M$="OK... THERE IS A MESSAGE:":GOSUB1
1441 M$="YOU HAVE BEEN AWAKENED FROM HIBERNATION FOR A SPECIAL MISSION!":GOSUB1
1450 M$="SPACESHIP FARMER HAS FAILED. YOU MUST REPROGRAM THE CENTRAL COMPUTER FOR AN EMERGENCY LANDING."
1460 GOSUB1:GOSUB5:M$="TO DO THIS, YOU NEED A FLOPPY DISC WHICH CONTAINS THE NECESSARY DATA. FIND IT "
1470 M$=M$+"AND INSERT IT IN THE CENTRAL COMPUTER READER. YOUR LIFE IS AT STAKE. GOOD LUCK!":GOSUB1:GOTO5
1500 IFLO=13ANDY=15THENM$="LIGHTS FLASH FOR AN INSTANT. EVERYTHING SEEMS OK.":FL(5)=1:GOTO9
1590 M$="MAYBE A PETUNIA HAS A HIGHER IQ THAN YOURS...":GOTO9
1600 IFY=0THENM$="NOW I WILL OPEN YOUR SKULL IF YOU KEEP SPEAKING!":GOTO9
1610 IFLO=2ANDO(1)=0ANDY=10THENM$="THE DOOR OPENS SLOWLY...":FL(1)=1:GOTO9
1620 IFLO=4ANDY=11THENM$="THERE'S A CASSETTE IN HERE!":O(6)=4:GOTO9
1690 M$="I CAN'T DO IT!!!":GOTO9
1700 IFLO=5ANDY=3THENFL(3)=1:M$="NOW YOU KNOW EVERYTHING ABOUT COMPUTERS!":GOTO9
1710 M$="A GOOD SCI-FI BOOK IS BETTER.":GOTO9
1800 IFY<>6THEN1830
1810 IFO(5)THENM$="IT DOESN'T LOOK LIKE I HAVE ONE...":GOTO9
1820 FL(2)=1:M$="IT'S JUST NOT MY SIZE, BUT IT WILL FIT!":GOTO9
1830 M$="THE ONLY THING I COULD PUT IS YOUR HEAD IN A GRINDER!":GOTO9
1900 CLS:PRINT:RETURN
1920 IFLO<>16ORY<>5THEN1970
1930 IFO(4)<>0THENM$="GOOD IDEA! pity I DON'T HAVE ONE...":GOTO9
1940 M$="CONGRATULATIONS! THE COMPUTER IS REPROGRAMMED. WE ARE LANDING!":GOSUB1:GOSUB5
1941 M$="NOW YOU MUST GET OUT OF THE SPACESHIP AND SEND AN SOS TO EARTH! HURRY!":FL(4)=1:GOTO9
1970 M$="I MIGHT SAY A PROFANITY...":GOTO9
2000 M$="I AM IN MY CABIN. MY CRYOGENIC CAPSULE IS OFF. "
2001 M$=M$+"THE FURNISHINGS ARE MINIMAL. I CAN ONLY GO OUT THROUGH THE SOUTH DOOR.":S=2:GOTO9
2010 M$="I AM IN A METAL WALLED ROOM. THERE ARE EXITS IN EVERY DIRECTION. "
2011 M$=M$+"THE SOUTH DOOR HAS A MAGNETIC LOCK AND LOOKS VERY STURDY.":N=1:E=3:S=6:O=4:GOTO9
2020 M$="I AM IN A RECREATIONAL CORRIDOR. THE LIGHT IS LOW, BUT "
2021 M$=M$+"I CAN ORIENT MYSELF. NO SIGNS OF LIFE, BUT I FEEL IN DANGER.":N=5:E=24:O=2:GOTO9
2030 M$="THIS IS THE MEETING ROOM. THERE IS A LARGE TABLE WITH MANY CHAIRS, AND A CABINET. "
2031 M$=M$+"ON THE TABLE I SEE A CASSETTE RECORDER. IT LOOKS LIKE IT MAY STILL WORK.":E=2:GOTO9
2040 M$="I AM IN THE LIBRARY. THERE ARE NUMEROUS VOLUMES AND THERE IS ONE IN PLAIN SIGHT-- "
2041 M$=M$+"A RECENT COPY OF MCOMPUTER. THERE IS NOT A SOUL IN SIGHT...":S=3:GOTO9
2050 M$="I'M STANDING IN A LONG CORRIDOR. ON THE GROUND IN FRONT OF ME IS A CORPSE. "
2051 M$=M$+"I CAN HEAR THE HUM OF THE ENGINES OF THE SPACESHIP.":N=2:S=7:GOTO9
2060 M$="HERE I AM IN A BIG ROOM. THERE ARE DOORS EVERYWHERE. I'M CLOSE TO THE HEART OF THE SHIP.":GOSUB1
2061 M$="THE CARPET IS NOT TO MY TASTE BUT I IGNORE THAT FACT.":N=6:E=9:S=10:O=8:GOTO9
2070 M$="I AM IN THE CAPTAIN'S CABIN. THE PORTHOLE IN FRONT OF ME IS CRACKED. "
2071 M$=M$+"THE TEMPERATURE IS SEVENTY DEGREES, THE RADIATION IS DEADLY.":E=7:GOTO9
2080 M$="THIS IS THE ARMORY. THE SHELVES ARE EMPTY-- WE ARE ON A PEACEFUL MISSION. "
2081 M$=M$+"THE DOOR TO THE EAST IS CLOSED AND I DON'T SEE HOW TO OPEN IT...":O=7:GOTO9
2090 M$="TO THE WEST THE CORRIDOR IS BLOCKED BY A BROKEN ROBOT. TO THE SOUTH I SEE THE "
2091 M$=M$+"SPLENDOR OF THE GALAXY ON A VIDEO SCREEN!":E=11:N=7:GOTO9
2100 M$="THIS IS THE OFFICERS' LOUNGE. THERE IS AN ELEVATOR WITH THE DOORS WIDE OPEN.":GOSUB1
2101 M$="THERE IS A TABLE FULL OF (EMPTY) GLASSES. I SEE TO THE EAST A ROOM WITH CONTROL PANELS.":E=13:O=10:GOTO9
2110 PRINT"PARDON!":RETURN
2120 M$="IT'S THE TRAFFIC CONTROL ROOM. IN FRONT OF ME IS A BUTTON LABELED "
2121 M$=M$+"'ELEVATOR SWITCH'. IT FEELS TOO EASY...":O=11:GOTO9
2130 M$="I SEE THE ELEVATOR, NOW UNUSABLE. I AM IN A LONG CORRIDOR. "
2131 M$=M$+"WEST IS THE LANDING BAY, EAST THE CENTRAL COMPUTER ROOM.":E=15:O=17:GOTO9
2140 M$="I AM IN THE COMPUTER ROOM. I SEE THE HUGE MEMORY BANK OF THE COMPUTER "
2141 M$=M$+"AND MANY KEYBOARD CONSOLES. WE ARE NEAR THE CONTROL ROOM.":N=18:E=16:O=14:GOTO9
2150 M$="THE CONTROL ROOM. IN FRONT OF ME IS THE FLOPPY DISC DRIVE, BUT THERE IS "
2151 M$=M$+"ALSO A SINISTER-LOOKING CAMERA...":GOSUB1:O=15:IFO(3)=0THEN9110
2156 RETURN
2160 M$="THIS IS THE LANDING BAY. TO THE WEST IS THE ELCTRONIC EXIT HATCH, "
2165 M$=M$+"TO THE EAST IS A LONG WELL LIT CORRIDOR.":O=19:E=14:GOTO9
2170 M$="THIS IS THE CENTRAL COMPUTER ROOM, THERE IS A TERMINAL ON A DESK WITH AN HP MONITOR!":S=15:GOTO9
2180 M$="I HAVE LANDED. THE SKY IS LIGHT GREEN. A GENTLE BREEZE IS BLOWING. TO THE WEST I SEE "
2181 M$=M$+"A BUILDING. TO THE NORTH A CAVE. A PATH LEADS SOUTH.":N=20:S=22:E=17:O=21:GOTO9
2190 IFO(8)=-1THEN9200
2192 M$="THE CAVE IS DARKER INSIDE THAN IN A TEXT ADVENTURE WHEN YOUR TORCH GOES OUT... THE NOISES ARE "
2193 M$=M$+"EQUALY dreary... THE SOUL OF SCOTT ADAMS, SAVE ME!":S=19:GOTO9
2200 REM
2210 M$="I AM INSIDE AN ALIEN TEMPLE. THERE IS A HUGE ALTAR WITH A STATUE "
2211 M$=M$+"OF A THIRTY ARMED COCKROACH, IT SMELLS OF INSECTICIDE!":E=19:S=23:GOTO9
2220 M$="I AM ON A HILL. THE VIEW BELOW ME IS THRILLING. I SEE VILLAGES "
2221 M$="ALIENS, WAGONS DRAWN BY STRANGE ANIMALS AND AN AD FOR COCA-COLA.":N=19:GOTO9
2230 M$="I AM IN A LARGE HALL WITH PAINTINGS ON THE WALLS, JEWELS AND WEAPONS.":GOSUB1
2231 M$="FROM THE DOOR I CAN SEE THE REST OF THE VILLAGE TO THE EAST, "
2232 M$=M$+"BUT A MYSTERIOUS FORCE WON'T ALLOW ME TO CONTINUE.":N=21:GOTO9
2240 M$="THIS IS THE SETTLER TRAINING ROOM. I CAN'T ORIENT MYSELF. "
2245 M$=M$+"THERE IS STRANGE EQUIPMENT EVERYWHERE.":E=25:S=24:N=25:O=24:GOTO9
2250 M$="WE ARE LOST IN THE TRAINING ROOMS. I DON'T KNOW THIS AREA OF THE SHIP!":S=26:N=24:E=24:O=25:GOTO9
2260 M$="THIS IS THE SETTLER TRAINING ROOM. I CAN'T ORIENT MYSELF. THERE'S STRANGE EQUIPMENT EVERYWHERE..."
2265 N=3:S=24:O=25:E=25:GOTO9
8000 PRINT:IFLO=1THENM$="YOU'RE JUST AT THE BEGINNING.":GOTO9
8010 IFLO=2THENM$="BEWARE AND PATIENTLY SEARCH.":GOTO9
8020 IFLO<27ANDLO>23THENM$="STUDY THE DETAILS...":GOTO9
8030 IFLO=17THENM$="WE NEED COMPUTER CODE...":GOTO9
8040 IFLO=18ANDFL(3)=0THENM$="IF YOU HAD PREPARED BEFORE, MAYBE BY READING SOMETHING USEFUL...":GOTO9
8050 IFLO=18THENM$="YET THERE MUST BE SOMETHING...":GOTO9
8060 IFLO=21THENM$="I INSIST, THERE IS SOMETHING...":GOTO9
8070 IFLO=20THENM$="IF IT WAS LESS DARK...":GOTO9
8900 IFRND(0)>.5THENM$="THEY'RE IN TROUBLE LIKE YOU.":GOSUB1:GOTO8910
8901 M$="YOU MISTAKE ME FOR THE HOBBIT?":GOSUB1
8910 PRINT:RETURN
9000 PRINT"RIGHT NOW I HAVE WITH ME:":FL=0
9010 FORJ=1TOOG:IFO(J)=0THENM$=O$(J):GOSUB1:FL=1
9020 NEXT:IFFL=0THENM$="NOTHING...":GOSUB1
9030 GOSUB5:RETURN
9100 PRINT:IFLO<>8THEN9110
9101 M$="MY SKIN IS CATCHING ON FIRE. I HAVE A FEW SECONDS TO LIVE. NOT FUNNY. AAAARGH!":GOSUB1:GOTO9150
9110 M$="*** ATTENTION **** NEGATIVE IDENTIFICATION. YOUR METABOLIC TRACE "
9111 M$=M$+"DOES NOT MATCH THE SAMPLE. YOU ARE NOT COMMANDER USTINOV!":GOSUB1:GOSUB5
9112 M$="A LASER BEAM SLICES THROUGH ME WITH EASE. I DIE!":GOSUB1
9150 PRINT"YOU FAILED MISERABLY.":PRINT"DO YOU WANT TO RETRY? (Y/N)";
9160 A$=INKEY$:IFA$=""THEN9160
9170 IFA$="Y"THENRUN
9180 PRINT"GOODBYE...":END
9200 PRINT:M$="YOU FINISHED THE ADVENTURE...":GOSUB1
9205 M$="IN FRONT OF ME IS A SLAB OF STONE TWENTY METERS HIGH: IT BEARS A MESSAGE:":GOSUB1
9210 READX:IFX=255THEN9230
9220 M$=M$+CHR$(X-O(8)-LO):GOTO9210
9230 GOSUB1:PRINT:PRINT"***CONGRATULATIONS***":END
9500 CLS:PRINT" FARMER SPACESHIP (ADVENTURE 1)":PRINT" ******************************"
9510 PRINT"    (C) 1984 MARIO PETTENGHI":PRINT:PRINT"COMMANDS RECOGNIZED:"
9530 FORJ=1TOVE:PRINTVE$(J),:NEXT:FORJ=1TOCO:PRINTC$(J),:NEXT:PRINT:GOSUB5:CLS:RETURN
9700 PRINT" *** SURE YOU WANT TO QUIT? ***"
9710 A$=INKEY$:IFA$=""THEN9710
9720 IFA$="N"THENM$="YOU NEED A LONG VACATION...":GOTO9
9730 IFA$="Y"THENCLS:END
9740 GOTO9710
10000 DATANORTH,EAST,SOUTH,WEST,HELP,INVENTORY,VOCABULARY,QUIT
10010 DATAEXAMINE,GET,DROP,USE,TURN ON,PRESS,OPEN,READ,PUT,LOOK,INSERT
10020 DATAPASSCARD,RECORDER,MAGAZINE,ID,FLOPPY DISC,SUIT,ELEVATOR,CODE,HATCH
10021 DATADOOR,CABINET,CASSETTE,CORPSE,KEY,BUTTON,MONITOR,FLASHLIGHT,ALTAR,LASER,KEYRING
10030 DATA4,PASSCARD,5,MAGAZINE,69,ID,8,FLOPPY DISC,9,SUIT,32,CASSETTE,25,KEY,29,FLASHLIGHT,4
10031 DATALASER,7,KEYRING
10040 DATA89,92,97,88,51,87,88,95,95,84,51,99,101,92,96,84,51,99,84,101,103,88,65,51,97,98,97
10041 DATA51,99,88,101,87,88,101,88,51,58,92,95,51,103,88,96,99,92,98,51,84,95,92,88,97,98,58,52,65,65,65,255
10042 REM SPACESHIP FARMER (C) 1983 MARIO PETTENGHI & MCOMPUTER
