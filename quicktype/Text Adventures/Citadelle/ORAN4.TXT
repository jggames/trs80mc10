1 CLS0:CLEAR152:DIMCC,C1,ZZ,MC,M$,A$:GOSUB1000:PRINT@0,"1-THE SHIELD OF ORAN";:PRINT@18+32*1,"LORICIELS 1984";:MC=16384
2 PRINT@10+32*15,"HIT SPACEBAR";:PRINT@0+32*12,"MC-10 VERSION BY";:PRINT@16+32*13,"JIM GERRIE 2014";:GOSUB486
3 AC=10:DO=5:PV=70:SO=5:PC=10:GOSUB470:P=1:GOSUB43:GOTO17
4 PRINTA$;:RETURN
5 M$=A$:GOSUB10:RETURN
6 REM REGULAR VIDEO HERE
7 C1=(PEEK(17024)AND1)*256+PEEK(17025):PRINTA$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1:GOSUB14:RETURN
8 C1=(PEEK(17024)AND1)*256+PEEK(17025):PRINTA$:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1:GOSUB14:RETURN
9 GOSUB485:GOTO17
10 C1=1:CC=32
11 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO11
12 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN11
13 RETURN
14 FORT=C1+MCTOCC+MC:POKET,PEEK(T)-64:NEXT:RETURN
15 FORC1=ZZTOCC:ON1-(C1<15)GOTO16:PRINT@C1*32,"":NEXT:PRINT@ZZ*32,;:RETURN
16 PRINT@15*32,"                               ";:NEXT:PRINT@ZZ*32,;:RETURN
17 ZZ=9:C1=15:GOSUB15 
18 A$="WHAT DO YOU DO":GOSUB4:INPUTC$
19 IFC$="W"THENC$="WES"
20 IFC$="E"THENC$="EAS"
21 IFC$="N"THENC$="NOR"
22 IFC$="S"THENC$="SOU"
23 LO=LEN(C$):IFLO<3THEN17
24 PRINT:MI=0:FORA=1TOLO
25 B$=MID$(C$,A,1):IFB$=" "THENMI=A
26 NEXTA:IFMI=0THENX$=C$:M=0:MI=4:GOTO31
27 W$=RIGHT$(C$,LO-MI):Y$=LEFT$(W$,3):IFY$="GRO"ORY$="ENT"ORY$="ECU"ORY$="FEN"ORY$="RES"ORY$="TOM"ORY$="COU"THENY$="SAL"
28 IFY$="CRA"ORY$="OSS"THENY$="SQU"
29 M=0:FORA=1TO51:IFY$=M$(A)THENM=A
30 NEXTA:IFM=0THENA$=U$:GOSUB6:PRINTW$;".":GOTO9
31 X$=LEFT$(C$,MI-1):Z$=LEFT$(X$,3)
32 V=0:FORA=1TO29:IFZ$=V$(A)THENV=A
33 NEXTA:IFV=0THENA$=U$:GOSUB6:PRINTX$;".":GOTO9
34 IFAR=1ANDV<>18ANDM<>1THEN491
35 IFV<10THEN41
36 IFV=13ANDM=33THEN232
37 IF(V=23ORV=24)ANDM=43THEN499
40 ONPGOTO70,80,90,100,110,120,130,140,150,160,170,180,190
41 ONVGOTO375,386,393,370,380,377,377,377,377
42 PC=P:P=T(P,V-5):IFP=0THEN488
43 CLS:PRINT@18+32*0,"L E V E L ";P
44 IF(P=11ANDLA=0)OR(P=11ANDP(14)<>11ANDP(14)<>0)THEN505
45 A$=S$+"IN "
55 ONPGOSUB400,405,410,415,420,425,430,435,440,445,450,455,460
56 PRINT:A$="OBJECTS:":GOSUB4
57 FORB=30TO51:IFP(B-29)=PTHENA$=N$(B-29)+" ":GOSUB4
58 NEXTB
59 PRINT:A$="CAN GO:":GOSUB4:IFT(P,1)>0THENA$=" NORTH":GOSUB4
60 IFT(P,2)>0THENA$=" SOUTH":GOSUB4
61 IFT(P,3)>0THENA$=" EAST":GOSUB4
62 IFT(P,4)>0THENA$=" WEST":GOSUB4
63 IFQ(1)<>PANDQ(2)<>PANDQ(3)<>PANDQ(4)<>PANDQ(5)<>PANDQ(7)<>PANDQ(8)<>PTHENMO=0:RETURN
64 PRINT:MO=0:A$="MONSTERS:":GOSUB4
65 FORA=1TO8:IFQ(A)=PTHENA$=Q$(A)+" ":GOSUB4:MO=A
66 NEXTA:RETURN
70 IFV=10ANDM=1THEN200
71 IF(V=11ORV=29)ANDM=1THEN202
72 IFV=18ANDM=1THEN204
73 IF(V=1ORV=10ORV=17)ANDM=2THEN206
74 IF(V=12ORV=19ORV=28)ANDM=2THEN208
79 GOTO490
80 IFV=13ANDM=3THEN210
81 IF(V=1ORV=17)ANDM=4THEN212
82 IF(V=12ORV=14ORV=20ORV=26ORV=27ORV=28)ANDM=4THEN214
83 IF(V=1ORV=17)ANDM=2THEN497
84 IF(V=1ORV=17ORV=10)ANDM=27THEN498
89 GOTO490
90 IFV=10ANDM=5THEN218
99 GOTO490
100 IFV=15ANDM=6ORV=27ANDM=4THEN220
101 IF(V=1ORV=17)ANDM=6THEN222
109 GOTO490
110 IFV=10ANDM=7THEN224
111 IF(V=12ORV=16)ANDM=8THEN226
112 IF(V=11ORV=29)ANDM=9THEN228
113 IFV=10ANDM=10THEN230
119 GOTO490
120 IFV=10ANDM=11THEN234
121 IFV=10ANDM=12THEN236
122 IFV=18ANDM=9THEN238
129 GOTO490
130 IF(V=1ORV=17)ANDM=13THEN240
131 IFV=17ANDM=14THEN242
132 IF(V=16ORV=18)ANDM=14THEN244
133 IF(V=15ORV=26)ANDM=15THEN248
139 GOTO490
140 IFV=20ANDM=16ANDP(22)=0THEN250
149 GOTO490
150 IFV=10ANDM=17THEN252
151 IF(V=1ORV=17ORV=10)ANDM=20THEN254
152 IF(V=15ORV=26)ANDM=6THEN256
153 IFV=1ANDM=27THEN258
159 GOTO490
160 IFV=20ANDM=18THEN260
161 IFV=21ANDM=18THEN262
162 IFV=10ANDM=11THEN264
163 IFV=20ANDM=19THEN266
164 IFV=21ANDM=19THEN268
165 IFV=17ANDM=20THEN272
166 IFV=22ANDM=39THEN274
169 GOTO490
170 IFV=1ANDM=21THEN278
171 IFV=1ANDM=41THEN280
179 GOTO490
180 IFV=25ANDM=22THEN282
181 IFV=11ANDM=42THEN296
189 GOTO490
190 IFV=10ANDM=23THEN284
191 IFV=26ANDM=24THEN286
192 IFV=17ANDM=25THEN288
193 IFV=18ANDM=25THEN290
194 IFV=10ANDM=26THEN292
199 GOTO490
200 IFP(2)<>-1THEN492
201 A$="IN THE HOLLOW OF THE TREE IS HIDDEN "+N$(2)+".":GOSUB507:P(2)=1:GOTO9
202 IFAR=1THEN495
203 A$="AS YOU PULL YOURSELF TO THE TOP YOU SEE THE ENTRANCE TO A CAVE FARTHER NORTH.":GOSUB5:AR=1:GOTO9
204 IFAR=0THEN495
205 A$="YOU'RE GOING A SLIGHT INCLINE.":GOSUB5:AR=0:GOTO9
206 IFP(1)<>-1THEN492
207 A$="SOMETHING SHINY IS STUCK HERE.":GOSUB5:GOTO9
208 IFP(1)<>-1THEN492
209 A$="YOU FIND "+N$(1)+".":GOSUB507:P(1)=1:GOTO9
210 A$="IT IS WRITTEN:":GOSUB5:A$="YOUR DESTINY ... DEATH.":GOSUB5:GOTO9
212 IFH<>1THENA$="AS YOU APPROACH, IT SEEMS TO GLOW.":GOSUB5:GOTO9
213 A$="IT FLOATS UP.":GOSUB5:GOTO9
214 IFPV<50THEN496
215 A$="YOU PASS THROUGH THE GATE WITHOUT EVEN HAVING TO SQUEEZE!":GOSUB508:P=3:GOSUB485
216 GOSUB43:GOTO9
218 IFP(3)<>-3THEN492
219 A$="IN HIS HAND, "+N$(3)+".":GOSUB507:P(3)=3:GOTO9
220 IFP(16)<>0THEN491
221 GOSUB506:T(2,1)=3:T(3,2)=2:H=1:GOTO9
222 A$="IT'S USED TO RAISE THE PORTCULLIS BUT LEVER IS BROKEN.":GOSUB5:GOTO9
224 A$="IN ONE CORNER ARE SOME BOARDS.":GOSUB5:GOTO9
226 IFP(21)<>-5THEN492
227 A$=R$+N$(21):GOSUB507:P(21)=5:GOTO9
228 A$="YOU CLIMB.":GOSUB5:P=6:GOSUB43:GOTO378
230 IFP(4)<>-5THEN492
231 A$="IN ON OF THEM, "+N$(4)+".":GOSUB507:P(4)=5:GOTO9
232 IFP(4)<>0THEN494
233 A$="IT IS WRITTEN: STATUES ARE TOKENS OF GLORY.":GOSUB5:GOTO9
234 IFP(6)<>-6THEN492
235 A$="YOU FIND "+N$(6)+".":GOSUB507:P(6)=6:GOTO9
236 IFP(7)<>-6THEN492
237 A$=R$+N$(7):P(7)=6:GOSUB507:GOTO9
238 A$="YOU DESEND.":GOSUB5:P=5:GOSUB43:GOTO378
240 A$=R$+"FULL OF STARS.":GOSUB5:GOTO9
242 A$="ONE OF THEM SOUNDS HOLLOW.":GOSUB5:GOTO9
244 IFTE=1THEN495
245 IFP(3)<>0ORP(6)<>0THEN491
246 A$="UNDER THE STAR, A HANDFUL.":GOSUB5:TE=1:GOTO9
248 IFTE<>1THEN491
249 A$="A PASSAGE OPENS TO THE WEST.":GOSUB508:T(7,4)=13:T(13,3)=7:GOTO9
250 A$="THE KEY TURNS IN THE LOCK.":GOSUB508:T(8,3)=9:T(9,4)=8:GOTO9
252 IFP(8)<>-9THEN492
253 A$=R$+N$(8):GOSUB507:P(8)=9:GOTO9
254 A$=R$+" A MECHANISM WITH A WHEEL.":GOSUB5:GOTO9
256 GOSUB506:D1=1:GOTO9
258 A$="THE GROUND IS COVERED IN DUST.":GOSUB5:GOTO9
260 IFP(9)<>-10THEN492
261 A$="THEY ARE FILLED WITH SALT.":GOSUB5:CA=1:GOTO9
262 IFP(9)<>-10ORCA<>1THEN491
263 A$="AT THE BOTTOM OF ONE OF THEM IT IS WRITTEN: AROG.":GOSUB5:P(9)=10:GOTO9
264 IFP(10)<>-10THEN492
265 A$="YOU PULL "+N$(10)+".":GOSUB507:P(10)=10:GOTO9
266 IFP(11)<>-10THEN492
267 A$="THEY'RE FILLED WITH WINE.":GOSUB5:TN=1:GOTO9
268 IFP(11)<>-10THEN492
269 IFTN<>1THEN491
270 A$="A VOICE ECHOS: TO THE NORTH LIE THE MOST PERFECT OF DEFENSES.":GOSUB5:P(11)=10:GOTO9
272 A$="IN THE MIDDLE OF THE WALL, "+R$+"A SQUARE HOLE.":GOSUB5:GOTO9
274 IFP(10)<>0THEN491
275 IFD1<>1THEN496
276 A$="Le cube disparait.":GOSUB508:T(10,1)=11:T(11,2)=10
277 A$="Au nord, un passage s'ouvre.":GOSUB508:P(10)=-1:GOTO9
278 A$="Il s'agit d'un support en metal soutenant un bouclier":GOSUB5:P(12)=11:GOTO9
280 A$="Dessus, les armes d'ORAN.":GOSUB5:GOTO9
282 A$="Le squelette se reforme, se leveet dit: vous devez me montrer":GOSUB5
283 A$="l'oeil pour franchir ce passage":GOSUB5:PRINT:OE=1:GOTO9
284 IFP(22)<>-13THEN492
285 A$=R$+N$(22):GOSUB507:P(22)=13:GOTO9
286 A$="Des que vous l'avez touche,  il forme  une  phrase :  le  cube  resoudra cette epreuve.":GOSUB5:GOTO9
288 A$="Une pierre peut etre descellee.":GOSUB5:GOTO9
290 IFP(3)<>0ORP(6)<>0THEN491
291 A$="Derriere la pierre, une cavite.":GOSUB507:DE=1:GOTO9
292 IFDE<>1THEN491
293 IFP(13)<>-13THEN492
294 A$="Vous trouvez "+N$(13)+".":GOSUB507:P(13)=13:GOTO9
296 IFP(13)<>0THEN494
297 IFOE<>1THEN496
298 IFP(12)<>0THEN496
299 A$="Le squelette prend l'oeil, le   place dans son orbite vide & dit":GOSUB5:GOSUB485
300 CLS:A$="LA PREMIERE EPREUVE EST RESOLUE.COMBATTEZ   MAINTENANT   POUR   L'ESPADON DE KRANZ.":GOSUB5
301 GOSUB485:CLS:A$="Vous  vous  enfoncez  dans  un  souterrain sous la citadelle...":GOSUB5:END
310 PM=RND(0)*40:AM=RND(0)*15:DM=RND(0)*10
311 GOSUB485:CLS:PRINT@10+32*0,;:A$="C O M B A T":GOSUB7:PRINT@11+32*1,;:A$="<A>TTACK":GOSUB7
312 PRINT@11+32*2,;:A$="<D>EFENSE":GOSUB7:PRINT@11+32*3,;:A$="<S>PELLS":GOSUB7:PRINT@11+32*4,;:A$="<R>ETREAT":GOSUB7
313 SOUND58,1:SOUND38,1:SOUND58,1:SOUND89,1:SOUND58,1:SOUND58,1:SOUND108,1
314 SOUND125,1:SOUND108,1:SOUND125,1:SOUND38,1:SOUND125,1:SOUND108,1:SOUND125,1:SOUND58,1
320 GOSUB485:PRINT@5+32*5,;:A$="WHAT DO YOU DO":GOSUB4:INPUTA$
321 IFA$="A"THEN326
322 IFA$="D"THEN328
323 IFA$="S"THEN351
324 IFA$="R"THEN357
325 GOTO320
326 A1=2:A2=4:A3=-14:GOTO330
328 A1=0:A2=0:A3=0
330 TC=INT(RND(0)*20+1):IF(18-TC)<AMTHEN336
331 A$="Vous l'avez rate.":GOSUB7:IFAM<5THENA$="Il est tres resistant.":GOSUB7
332 TC=INT(RND(0)*20+1):IF(20-TC)<AC-A2THEN341
333 A$="Le monstre vous a rate.":GOSUB7
334 IFA3<>0THEN346
335 GOTO320
336 A$="Vous l'avez touche.":GOSUB7:D=INT(RND(0)*DO-A2-A3+1)
337 IFD>8THENA$="C'est un coup de maitre.":GOSUB7
338 PM=PM-D:IFPM<0THENA$="Le monstre tombe mort.":GOSUB7:GOTO348
339 B=RND(0):IFB<.5THENA$="Vous avez l'avantage.":GOSUB7:PRINT:GOTO330
340 GOTO332
341 A$=S$+"touche.":GOSUB7:D=INT(RND(0)*DM+1)
342 IFD>8THENA$="Attention, il est tres fort.":GOSUB7
343 PV=PV-D:IFPV<0THEN358
344 B=RND(0):IFB<.5THENA$="Attention, il a l'avantage.":GOSUB7:GOTO332
345 GOTO320
346 B=RND(0):IFB<.5THEN336
347 GOTO320
348 IFPM<-3THENA$="et disparait.":GOSUB7:Q(MO)=0:GOTO350
349 Q(MO)=INT(RND(0)*13+1)
350 PR=PR+1:GOSUB1100:GOSUB485:ZZ=0:CC=14:GOSUB15:RETURN
351 IFSO=0THENA$="Vous n'avez plus aucun sort.":GOSUB7:GOTO332
352 SO=SO-1:A$="Vous lancez votre sort.":GOSUB7:B=RND(0)
353 IFB<.25THENA$="Le monstre contre le sort.":GOSUB7:PRINT:GOTO332
354 D=8:A$="Le sort atteind le monstre.":GOSUB7:PM=PM-D
355 IFPM<0THENA$="et le foudroie.":GOSUB7:PRINT:PR=PR+1:Q(MO)=0:GOSUB485:RETURN
356 GOTO320
357 P=PC:GOSUB43:GOTO9
358 CLS:PRINT@0,;:A$="Une voix retentit:":GOSUB7
359 A$="JE SUIS LE SEUL MAITRE DE CETTE CITADELLE. AINSI FINISSENT CEUX QUI OSENT M'AFFRONTER.":GOSUB7:PRINT
360 A$="HA!HA! VOS RESTES NE SERONT QUE LA MARQUE DE MON POUVOIR.":GOSUB7:GOSUB1200
361 PRINT@15+32*9,;:A$="G A M E   O V E R":PRINT@4+32*13,;:A$="PLAY AGAIN (Y/N)":GOSUB4:INPUTA$
362 IFLEFT$(A$,1)="O"THENRUN
363 CLS:END
370 ZZ=9:CC=15:GOSUB15:O=0:PRINT"Vous possedez "
371 FORB=30TO51:IFP(B-29)=0THENA$=N$(B-29)+" ":GOSUB4:O=O+1
372 NEXTB
373 IFO=0THENA$="Votre courage pour seul bien.":PRINT@1+32*11:GOSUB7
374 GOTO9
375 IFM<>0THENGOTO40
376 GOSUB43:GOTO9
377 GOSUB42
378 IFMO<>0THENGOSUB310:GOSUB43:GOTO378
379 GOTO9
380 PRINT@0+32*9:A$="EXPERIENCE :          ":GOSUB4:PRINTPR*10:A$="VIE :                 ":GOSUB4:PRINTPV
381 A$="ATTAQUE :             ":GOSUB4:PRINTDO
382 A$="DEFENSE :             ":GOSUB4:PRINTAC
383 A$="NOMBRE DE SORTS :     ":GOSUB4:PRINTSO
384 A$="TEMPS DE JEU : "+STR$(INT((TIME-TS)/21600))+" minutes":GOSUB4
385 GOTO9
386 IFNB=6ORM<30THEN493
387 IFP(M-29)<>PTHEN491
388 P(M-29)=0:NB=NB+1
389 IFM=30THENDO=15
390 IFM=31THENAC=AC-5
391 IFM=41THENAC=AC-2
392 A$="Vous prenez l'objet.":GOSUB5:GOTO9
393 IFNB=0ORM<30THEN494
394 IFP(M-29)<>0THEN491
395 P(M-29)=P:NB=NB-1
396 IFM=30THENDO=5
397 IFM=31THENAC=AC+5
398 IFM=41THENAC=AC+2
399 A$="Vous posez l'objet sur le sol.":GOSUB5:GOTO9
400 A$=A$+"une foret.":GOSUB8
401 A$="Devant vous se dressent un arbrecreux et un rocher.":GOSUB8:RETURN
405 A$=S$+"devant l'entree  d'unegrotte entouree de rochers.":GOSUB8
406 IFH<>1THENA$="Une herse bloque le passage.":GOSUB8
407 RETURN
410 A$=A$+"la grotte.":GOSUB8
411 A$="A l'est se trouve un escalier etdans un coin, un squelette.":GOSUB8:RETURN
415 A$=A$+"la salle de garde"+R$+"un mecanisme de levage.":GOSUB8:RETURN
420 A$=A$+"les ecuries.":GOSUB8
421 A$=R$+"des carcasses de chevaux et une echelle.":GOSUB8:RETURN
425 A$=S$+"sur le fenil.":GOSUB8
426 A$=R$+" du foin  en vrac,  deux sacs et une echelle.":GOSUB8:RETURN
430 A$=A$+"la salle d'arme.":GOSUB8
431 A$="Des mosaiques couvrent les murs.":GOSUB8:RETURN
435 A$=A$+"la salle d'entreeA l'est, 2 statues encadrent uneporte, le sol est fait de dalles":GOSUB8:RETURN
440 A$=A$+"un couloir sombreAu fond, a l'est, une porte.":GOSUB8:RETURN
445 A$=A$+"la reserve.":GOSUB8
446 A$="Tout autour, "+R$+"des caisses,des tonneaux et des sacs.":GOSUB8
447 A$="Au fond un mur vide.":GOSUB8:RETURN
450 A$=A$+"une salle obscureVous voyez un objet briller.":GOSUB8:RETURN
455 A$=A$+"une tombe.":GOSUB8
456 A$=R$+"des ossements et un crane":GOSUB8:RETURN
460 A$=S$+"au fond d'un puits en pierres."+R$+"de la vase et du sable.":GOSUB8:RETURN
470 U$="I DON'T UNDERSTAND:":S$="YOU ARE ":R$="THERE ARE":Q$="THERE ARE NO "
471 DATA 2,1,1,1,,1,,,8,,4,,,,,3,7,,,,,,,,,5,8,,12,3,,7,,,10,,,,,9,,,,,,8,,,,,7,
472 DIMT(13,4):FORA=1TO13:FORB=1TO4:READT(A,B):NEXTB,A
473 DATA REG,PRE,POS,INV,SCO,NOR,SUD,EST,OUE,FOU,MON,SOU,LIS,FOR,ACT,ENL,EXA,DES,TIR,OUV,VID,PLA,ALL,ETE,DIS,TOU,LEV,POU,GRI
474 DIMV$(29):FORA=1TO29:READV$(A):NEXTA
475 DATA ARB,ROC,INS,HER,SQU,MEC,SAL,PLA,ECH,CAR,SAC,FOI,MOS,ETO,POI,POR,POU,CAI,TON,MUR,OBJ,ARO,VAS,SAB,PIE,CAV,SOL,DAL,STA
476 DIMM$(51):FORA=1TO29:READM$(A):NEXTA
477 DATA EPE,une epee,-1,ARM,une armure,-1,BUR,un burin,-3,PAR,un parchemin,-5,SIL,des silex,-2,MAR,un marteau,-6
478 DATA BRA,un bracelet,-6,PER,une perle,-9,SEL,du sel,-10,CUB,un cube,-10,VIN,du vin,-10,BOU,un bouclier,-11
479 DATA OEI,un oeil,-13,LAN,une lanterne,8,TAS,une tasse,4,BAR,une barre,7,PIN,une pince,3,GAN,un gant,6,CAS,un casque,5
480 DATA CLO,des clous,7,SEQ,des sequins d'or,-5,CLE,une clef,-13
481 DIMN$(22),P(22):FORA=30TO51:READM$(A),N$(A-29),P(A-29):NEXTA
482 DATA un gnoll,2,un assassin,13,un bugbear,4,une ombre,6,une lich,7,un orc,9,une harpie,11,un guerrier,12
483 DIMQ$(8),Q(8):FORA=1TO8:READQ$(A),Q(A):NEXTA
484 RETURN
485 C$="":PRINT@10+32*15,"HIT A KEY...";
486 K$=INKEY$:IFK$=""THEN486
487 RETURN
488 A$=Q$+"pas d'issue de ce cote.":GOSUB6:P=PC:RETURN
490 IFV=1ORV=10ORV=17THEN492
491 A$="Vous ne pouvez pas faire cela.":GOSUB6:GOTO9
492 A$=Q$+"rien de special.":GOSUB6:GOTO9
493 A$=S$+"trop charge.":GOSUB6:GOTO9
494 A$="Vous ne l'avez pas.":GOSUB6:GOTO9
495 A$="Vous l'avez deja fait.":GOSUB6:GOTO9
496 A$="Il ne se passe rien.":GOSUB6:GOTO9
497 A$=R$+"une inscription.":GOSUB5:GOTO9
498 A$="Le sol est jonche de cailloux.  Parmi eux, "+N$(5)+".":GOSUB507:P(5)=2:GOTO9
499 IFP(14)<>0THEN494
500 IFV=24ANDP=11THENA$="Vous ne voulez pas faire cela ici.":GOSUB5:GOTO9
501 IFV=24THENLA=0:A$="La lanterne est eteinte.":GOSUB5:GOTO9
502 IFV=23ANDLA=1THEN495
503 IFV=23ANDP(5)<>0THENA$="Je n'ai rien pour enflammer la  meche.":GOSUB5:GOTO9
504 A$="Je frotte les silex...":GOSUB5:A$="Une etincelle allume la lanterne":GOSUB5:LA=1:GOTO9
505 A$="Vous n'y voyez rien  et  faites demi-tour.":GOSUB5:GOSUB485:P=PC:GOSUB43:GOTO9
506 A$="Un bruit sourd se fait entendre":GOSUB5:RETURN
507 GOSUB5:SOUND89,1:SOUND89,1:SOUND89,1:SOUND89,1:SOUND38,1:SOUND89,1:RETURN
508 GOSUB5:SOUND133,1:SOUND38,1:SOUND38,1:SOUND89,1:SOUND108,1:SOUND108,1:RETURN
509 IFIM=1THENRETURN
1000 PRINT@3*32,;
1010 PRINT"€€€€€€€€€€€ÑÓÐ€ÓÓ ÐÓÒ€€€€€€€€€€€";
1020 PRINT"€€€€€€€€€€€Õ¯®¬¯¯¬­¯ª€€€€€€€€€€€";
1030 PRINT"€€€€€€€€€€€Õ¯«£¯¯£§¯ª€€€€€€€€€€€";
1040 PRINT"€€€€€€€€€€ÑÓÒ¯ªÓÓ¥¯ÑÓÒ€€€€€€€€€€";
1050 PRINT"€€€€€€€€€€Õ¯«££¬¬Ó£§¯ª€€€€€€€€€€";
1060 PRINT"€€€€€€€€€€Õ¯¯¯ª ÐÕ¯¯¯ª€€€€€€€€€€";
1070 PRINT"€€€€€€€€€€Õ¯¯¯ª€ Õ¯¯¯ª€€€€€€€€€€";
1080 PRINT"€€€€€€€€€€Ô¬¬¬¨€ Ô¬¬¬¨€€€€€€€€€€";:RETURN
1100 SOUND78,2:SOUND133,2:SOUND78,2:SOUND89,2:SOUND108,2:SOUND78,2:SOUND108,2:SOUND133,2:SOUND58,2:SOUND133,2:SOUND108,4
1110 SOUND89,2:SOUND108,2:SOUND78,2:SOUND133,2:SOUND78,2:SOUND89,2:SOUND108,2:SOUND78,2:SOUND108,2:SOUND133,2:SOUND58,4
1120 SOUND133,2:SOUND108,2:SOUND89,2:SOUND108,2:SOUND78,2:SOUND58,2:SOUND58,4:RETURN
1200 SOUND133,1:SOUND89,1:SOUND38,1:SOUND89,1:SOUND38,1:SOUND133,1:SOUND108,1:SOUND133,1:SOUND89,1:SOUND38,1:SOUND89,1
1210 SOUND38,1:SOUND133,1:SOUND108,1:RETURN
