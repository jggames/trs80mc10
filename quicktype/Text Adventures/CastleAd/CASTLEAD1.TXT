0 GOTO10
6 PRINT"================================";:RETURN
7 PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";:RETURN
8 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";:RETURN
10 CLEAR300:O=66:DIML$(114),A(6,114),M$(3,O),B(4,O)
20 CLS:PRINT@167,"******************","       *CASTLE ADVENTURE*",
21 PRINT"       ******************":PRINT@329,"BY C.JACOBSON.":PRINT@480,"COPYRIGHT 1983 VIRGIN GAMES LTD."
30 GOSUB2900:B=1
40 IFB>44ANDB<56THENL$(B)="AN ALLURE"
50 IFB=999THEN1600
51 IFB=996THEN1770
60 IFB=995THEND=7:GOTO1600
61 IFB=991THEND=13:GOTO1600
70 IFB=992THEND=10:GOTO1600
80 IFB=998ORB=997ORB=994THEN1710
90 IFB=993THEN2540
91 CLS
100 S=0:PRINT"LOCATION: ";L$(B):C=0:PRINT"YOU SEE: ";:IFB>19ANDB<23ORB=18ORB=25THEN2620
110 IFB>33ANDB<37THEN2620
111 IFB=32ORB=39THEN2620
120 IFB>57ANDB<67THEN2620
130 IFB>87ANDB<97ANDB<>92THEN2620
131 IFB>74ANDB<80ANDB<>78THEN2620
140 IFB=43ORB=44ORB=4ORB=10ORB=12ORB=16ORB=15THEN2620
150 IFB=1ORL$(B)="AN ALLURE"AND B<>78THENB(1,19)=B
160 IFB=44THENS=0
170 IFB=108ORB=107ORB=28ORB=27THENPRINTTAB(10)"CLOUDS":C=1
180 FORA=1TOO
190 IFB(1,A)=B ANDB(3,A)<>0THENPRINTTAB(10)M$(2,A):C=1
200 NEXTA:IFE=1ANDB<>106THENPRINT"A LUMINOUS ARROW":GOTO210
201 IFC=0THENPRINT"NOTHING OF INTEREST"
210 IF(B>106ANDB<110)OR(B>25ANDB<29)THENGOSUB6:GOTO310
220 C=0:PRINT"VISIBLE EXITS ARE:":IFE=1THEN290
221 IFA(1,B)<>0THENPRINT"NORTH ";:C=1
230 IFA(2,B)<>0THENPRINT"SOUTH ";:C=1
240 IFA(3,B)<>0THENPRINT"EAST ";:C=1
250 IFA(4,B)<>0THENPRINT"WEST ";:C=1
260 IFA(5,B)<>0THENPRINT"UP ";:C=1
270 IFA(6,B)<>0THENPRINT"DOWN";:C=1
280 IF(B=112ORB=114)ANDSH=1THENPRINT"THROUGH A MOUSE HOLE !";
290 IFC=0THENPRINT" ? ? ?";
300 PRINT:GOSUB6
310 IFB(1,16)=31ANDA(3,7)=994THENA(3,7)=103
320 INPUT"WHAT NOW . .";M$:PRINT" ";
330 IFM$="N"THENM$="GO NOR"
331 IFM$="S"THENM$="GO SOU"
340 IFM$="E"THENM$="GO EAS"
341 IFM$="W"THENM$="GO WES"
350 IFM$="U"THENM$="GO UP"
351 IFM$="D"THENM$="GO DOW" 
360 IFM$="LOO"ORM$="LOOK"THEN40
370 IFLEN(M$)<3THENPRINT"WHAT ??":GOTO310
380 N$=LEFT$(M$,3):FORA=(LEN(M$)-2)TO1STEP-1:IFMID$(M$,A,1)=" "THEN390
381 NEXTA
390 O$=MID$(M$,A+1,3):IFB(1,18)=B ANDO$="BLO"THEND=1:GOTO1600
391 IFN$="INV"THEN540
392 IFN$="PLA"THEN1970
400 IFN$="SIN"THEN2740
410 IFT=1ANDB=112THEND=14:GOTO1600
411 IFN$="WAV"THEN2780
420 IFN$="QUI"THEN2220
421 IFN$="WEA"THEN2330
422 IFN$="CUT"THEN2370
430 IFN$="HEL"THEN580
431 IFN$="GO "THEN600
432 IFN$="SWI"THEN2630
440 IFSH=1THENPRINTY$"YOU'RE TOO SMALL !":GOTO310
450 IFN$="LOO"ORN$="EXA"THEN720
451 IFN$="GET"ORN$="TAK"THEN830
460 IFN$="DRO"ORN$="LEA"THEN980
461 IFN$="CLI"THEN1110
462 IFN$="HAR"ORN$="SHO"THEN2680
470 IFN$="INS"THEN1030
471 IFN$="AXE"ORN$="CHO"THEN1740
480 IFN$="EAT"ORN$="DRI"THEN2070
481 IFN$="PRE"THEN2120
482 IFN$="BAN"THEN2710
490 IFN$="PUS"ORN$="MOV"THEN1170
491 IFN$="OPE"ORN$="UNL"ORN$="UNT"THEN1250
500 IFN$="THR"THEN2420
501 IFN$="EXT"THEN2490
502 IFN$="GAM"ANDB=106THEN2560
510 IFN$="KIL"THEN1330
511 IFN$="REA"THEN1390
512 IFN$="SMA"ORN$="BRE"THEN2160
520 IFN$="FIL"THEN1440
521 IFN$="TUR"THEN1490
522 IFN$="OIL"THEN1560
530 PRINT"EH !",," WHAT ?",," PARDON !!",," "S$:GOTO310
540 C=0:FORA=1TOO
550 IFB(1,A)=31THENPRINTTAB(2)M$(2,A):C=1
560 NEXTA:IFC=0THENPRINTH$"ANYTHING !"
570 GOTO310
580 A=RND(2):IFA=1THENPRINT" YOUR'RE ON YOUR OWN HERE !":GOTO310
590 PRINT"I CAN'T HELP YOU !":GOTO310
600 IFB=1AND(O$="EAS"ORO$="WES")THEND=2:GOTO1600
610 IFB(1,20)=B ANDL$(B)="AN ALLURE"THEND=3:GOTO1600
620 IFB(1,20)=B THEND=4:GOTO1600
630 IFB(1,18)=B THEND=1:GOTO1600
640 IFO$="NOR"ANDA(1,B)<>0THENB=A(1,B):GOTO40
650 IFO$="SOU"ANDA(2,B)<>0THENB=A(2,B):GOTO40
660 IFO$="EAS"ANDA(3,B)<>0THENB=A(3,B):GOTO40
670 IFO$="WES"ANDA(4,B)<>0THENB=A(4,B):GOTO40
680 IFO$="UP"ANDA(5,B)<>0THENB=A(5,B):GOTO40
690 IFO$="DOW"ANDA(6,B)<>0THENB=A(6,B):GOTO40
700 IFO$="UP"THEND$="":GOTO710
701 IFO$="NOR"ORO$="SOU"THEND$="TH":GOTO710
702 IFO$="EAS"ORO$="WES"THEND$="T":GOTO710
703 IFO$="DOW"THEND$="N":GOTO710
704 PRINT"GO WHERE ??":GOTO310
710 PRINTY$"GO ";O$;D$:GOTO310
720 IFO$="ARR"ANDE=1THEN2520
721 GOSUB2880
730 IFA=7ANDS=1THENA=0:M$(3,A)="Z":GOTO800
740 IFA=7ANDB=44THENA=7:GOTO820
750 IFB(1,A)<>B ANDB(1,A)<>31THENPRINTY$"SEE",," "M$(2,A);" !!":GOTO310
760 IFA=23ORA=52THEN800
770 IFA=30ANDG=0THEND=6:GOTO1600
780 IFO$="GLA"THEN820
781 IFA=39ANDB=114THENB(3,A)=1
790 IFB(3,A)=0THENPRINTY$"EXAMINE IT YET!":GOTO310
800 IFM$(3,A)="Z"THENPRINTY$"SEE",," ANYTHING UNUSUAL!":GOTO310
810 IFA=29THENB(3,30)=1:GOTO820
811 IFA=57THENB(1,57)=0
820 PRINTM$(3,A):GOTO310
830 IFO$="INV"THEN540
831 GOSUB2880
840 IFB(1,A)=31THENPRINT"YOU'VE "A$"GOT IT !":GOTO310
850 IFA=7AND(S=1ORB=44)THENPRINT"TAKE A STAIRCASE ??":GOTO310
860 IFB(1,A)<>B THENPRINTI$:GOTO310
870 IFA=16THENA(4,59)=10:A(1,9)=11:A(4,9)=12:A(3,9)=10
880 IFA=3THENM$(2,3)="A GRAPPLING HOOK"
890 IFA=19THEN1100
900 IFA=30ANDG=0THEND=6:GOTO1600
910 IFB(2,A)=0THENPRINTY$"!!":GOTO310
920 IFA=56ANDB=112THEN925
921 GOTO930
925 T=1:PRINT"CLANG!",," A MASSIVE DOOR FALLS DOWN"," BLOCKING YOUR ONLY EXIT"," THORGAH WAKES!":B(1,A)=31:A(3,112)=0:GOTO310
930 C=0:FORD=1TOO:IFB(1,D)=31THENC=C+1
940 NEXTD:IFC>5THENPRINTY$"CARRY ANYTHING ELSE":GOTO310
950 IFA=8THENM$(3,7)="Z":A(6,44)=72:A(5,72)=44
960 PRINT"O.K",," YOU'VE GOT IT !":B(1,A)=31:B(3,A)=1
970 GOTO310
980 GOSUB2880
990 IFB(1,A)<>31THENPRINTH$"THAT OBJECT !":GOTO310
1000 IFA=31THENG=0:M$(2,A)="A SUIT OF ARMOUR"
1010 IFA=47THENAQ=0:M$(2,A)="AN AQUALUNG"
1020 PRINT"O.K",," YOU'VE DROPPED IT !":B(1,A)=B:GOTO310
1030 IFO$="CAS"THEN1060
1031 IFO$<>"KEY"THENPRINT"I BEG YOUR PARDON !!":GOTO310
1040 IFB(1,5)<>31THENPRINTH$"A KEY !":GOTO310
1050 IFB(1,4)=B THENPRINT" O.K.":M$(3,4)="Z":B(1,A)=B:B(1,5)=0:GOTO310
1051 PRINT"IN WHAT !":GOTO310
1060 IFM$(3,8)<>"IT'S EMPTY !!"THENPRINT"IT'S "A$"IN !!":GOTO310
1070 IFB(1,6)<>B ANDB(1,6)<>31THEN2010
1080 IFB(1,8)<>B ANDB(1,8)<>31THENPRINT"INTO WHAT ?":GOTO310
1090 PRINT"O.K":PRINT" IT'S IN !":M$(3,8)="THERE'S A CASSETTE IN IT !":GOTO310
1100 PRINT"IMPOSSIBLE !!":GOTO310
1110 IF(O$="POR"ORO$="GAT"ORO$="WAL")ANDB=1THEN1115
1111 GOTO1120
1115 B=56:B(1,3)=56:M$(2,3)="A GRAPPLING HOOK                HANGING OVER THE EDGE":GOTO40
1120 IFO$="OUT"THEN1125
1121 GOTO1130
1125 IFB=108ORB=107ORB=27ORB=28THEND=11:GOTO1600
1126 IFB=26THENB=23:B(1,44)=B:GOTO1130
1127 B=2:B(1,44)=2:GOTO40
1130 IFO$="IN"ANDB=2ANDB(1,44)=2THENB=109:GOTO40
1140 IFO$="IN"ANDB=23THENB=26:GOTO40
1150 IFO$="ROP"ANDCR=1THEN1155
1151 GOTO1160
1155 PRINT"O.K",," YOU ARE CLIMBING UP THE ROPE !  UHD! YOU BANGED YOUR HEAD"," "Y$"GO ANY FURTHER":CR=2:GOTO310
1160 PRINT"DON'T BE SILLY !!":GOTO310
1170 IFO$="TAP"THEN1175
1171 GOTO1180
1175 IFB<>11THENA=14:GOTO860
1176 IFB(3,15)=1THENPRINT"YOU'VE "A$"MOVED IT !!":GOTO310
1177 PRINT"THERE IS A LARGE"," DOOR BEHIND IT !!":B(3,15)=1:GOTO310
1180 GOSUB2880
1190 IFB(1,A)<>B THENPRINTM$(2,A)" ????":GOTO310
1200 IFA=27THEN2300
1210 IFA=23THEN1500
1220 IFA=38THENPRINT"O.K",," A TRAPDOOR WAS CONCEALED"," BENEATH IT !!!":B(1,39)=B:GOTO310
1230 IFB(2,A)=0THEN1160
1240 PRINTNH$:GOTO310
1250 GOSUB2880:IFB(1,A)<>31ANDB(1,A)<>B THEN860
1260 IFA=37THEND=9:GOTO1600
1270 IFA=39ANDB=114THEN1275
1271 GOTO1280
1275 IFCR<2THENPRINTY$"REACH IT !!":GOTO310
1276 PRINT"O.K",," IT'S OPEN !":A(5,114)=17:GOTO310
1280 IFA=39THENPRINT"O.K",," THE DOOR IS OPEN":A(6,104)=993:GOTO310
1290 IFA=4THEN1295
1291 GOTO1300
1295 IFM$(3,4)<>"Z"THENPRINTY$:GOTO310
1296 PRINT"THE LID POPS OPEN!"," A CASSETTE TAPE"," LIES INSIDE.":B(3,6)=1:M$(3,4)="IT IS EMPTY":B(2,6)=1:GOTO310
1300 IFA=35THEN1305
1301 GOTO1310
1305 PRINT"O.K.":PRINT" IT'S VERY BIG INSIDE !":M$(3,A)="IT'S REALLY DARK IN THERE !":IFB(1,16)=31THENA(3,7)=103:GOTO310
1306 A(3,7)=994:GOTO310
1310 IFA=38THEND=8:GOTO1600
1320 GOTO1100
1330 GOSUB2880:IFA=49ORA=48THEN2680
1340 IFB(1,A)<>B THENPRINTI$:GOTO310
1341 IFO$<>"MAD"ORMA=1THENPRINT"DON'T BE STUPID !"," IT'S "A$"DEAD !!":GOTO310
1350 SOUND50,10:FORR=31TO1STEP-1:SOUNDR,1:NEXTR
1360 FORR=10TO1STEP-1:SOUNDR,1:NEXT
1370 M$(2,27)="A DEAD ORGANIST !":M$(3,27)="HE'S SLUMPED OVER THE ORGAN     KEYBOARD !!":MA=1
1380 PRINT"THE ORGANIST IS DEAD !!":PRINT" HE LIES SLUMPED OVER THE ORGAN  KEYBOARD !":GOTO310
1390 GOSUB2880
1400 IFA=7THEN1100
1401 IFB(1,A)<>B ANDB(1,A)<>31THENPRINTI$:GOTO310
1410 IFB(4,A)=0THENPRINTY$"READ THAT !!":GOTO310
1420 IFM$(3,A)="Z"THEN800
1430 PRINTM$(3,A):GOTO310
1440 GOSUB2880
1450 IFA<>13THENPRINTS$:GOTO310
1460 IFB(1,A)<>31THENPRINTH$"A POT":GOTO310
1470 IFB<>60THENPRINTW$:GOTO310
1480 PRINT"O.K.",," THE POT IS FULL !":M$(3,A)="IT IS FULL OF OIL !":M$(3,12)="I ALWAYS EXAMINE WELL !!":GOTO310
1490 IFO$="KEY"THENA=4:GOTO1290
1500 IFO$="HAN"ANDB=67THEN1540
1510 IFO$<>"WIN"ORB<>74THENPRINT" EH??":GOTO310
1520 IFM$(3,10)="IT HAS JUST BEEN OILED !"THEN1525
1521 GOTO1530
1525 PRINT"O.K.",," YOU HAVE RAISED THE PORTCULLIS":A(1,1)=74:A(2,74)=1:M$(2,1)="A RAISED PORTCULLIS":GOTO310
1530 PRINT"IT IS TOO STIFF !":GOTO310
1540 PRINT"O.K":IFH=1THEN310
1550 H=1:PRINT" A SMALL SECTION OF THE WALL     SLIDES AWAY REVEALING A         GLASS PANEL !!":B(1,25)=67:GOTO310
1560 IFO$<>"WIN"THEN1100
1570 IFB<>74THENPRINT" PARDON ?":GOTO310
1580 IFB(1,13)<>31ANDM$(3,13)<>"IT IS FULL OF OIL !"THENPRINTW$:GOTO310
1590 PRINT"O.K",," THE WINDLASS IS NOW OILED !":M$(3,10)="IT HAS JUST BEEN OILED !":GOTO310
1600 FORR=1TO50:CLS(RND(8)):NEXTR:CLS:POKE&HFFD6,0:SOUND1,8
1610 IFD=-1THENRETURN
1620 PRINT:PRINT"        YOU ARE DEAD !!!"
1630 OND GOTO 1790,1810,1820,1830,1840,1850,1870,1890,1910,1920,1930,1940,1950,1960
1640 PRINT"    YOU FELL OVER A CASSETTE"
1650 PRINT"  RECORDER, WHICH WAS LYING ON"
1660 PRINT"         THE STAIRCASE,"
1670 PRINT"     AND BROKE YOUR NECK !!"
1680 PRINT"          HA! HA! HA!!"
1690 PRINT,,,,,,,,:PRINT"    PRESS A KEY TO RESTART!!"
1700 IFINKEY$<>""THENRUN
1701 GOTO1700
1710 PRINT"YOU CAN'T GO IN THERE !!        IT'S TOO DARK !!"
1720 IFB=994THENB=7:GOTO310
1730 IFB=998THENB=9:GOTO310
1731 IFB=997THENB=59:GOTO310
1740 IFB(1,9)<>31THENPRINTW$:GOTO310
1741 IFO$="TAP"ORO$<>"DOO"THEN1320
1750 IFB<>11THENPRINT"EH !!":GOTO310
1751 IFB(3,15)<>1ORB(1,15)=0THENPRINT"WHICH DOOR ??":GOTO310
1760 B(1,15)=0:PRINT"SMASH !!!   CRASH !!!"," THE DOOR SPLITS AND FALLS OFF   IT'S HINGES":A(4,11)=996:GOTO310
1770 CLS:PRINT,,,," YOU ARE FALLING DOWN A DARK     SHAFT !!!":FORR=1TO2000:NEXT:PRINT,," YOU LAND WITH A BUMP !!!"
1771 FORR=1TO2000:NEXT
1780 B=102:GOTO40
1790 PRINT"    YOU WERE INGURGITATED BY          THE MONSTROUS,GREASY           PULSATING,HUNGRY,WHITE"
1800 PRINT"             BLOB !":GOTO1680
1810 PRINT"      YOU FELL IN THE MOAT":PRINT"         AND DROWNED !!":GOTO1680
1820 PRINT"      YOU SLIPPED ON A BAR                  OF SOAP,                   FELL INTO THE MOAT  ";
1821 PRINT"              AND DROWNED !!":GOTO1680
1830 PRINT"      YOU SLIPPED ON A BAR                  OF SOAP,                   LYING ON THE FLOOR  ";
1831 PRINT"          AND BROKE YOUR NECK !!":GOTO1680
1840 PRINT"      YOU CHOKED TRYING TO":PRINT"   CONSUME ";M$(2,A);"!":PRINT"           AND DIED !":GOTO1680
1850 PRINT"    THE GREENY LIQIUD IN THE         FOUNTAIN WASN'T WATER,              IT WAS ACID !!"
1851 PRINT"  AND YOU KNOW WHAT HAPPENS TO"
1860 PRINT"   PEOPLE WHO PUT THEIR HANDS         IN ACID, THEY DIE !!":GOTO1680
1870 PRINT"   YOU WALKED STRAIGHT INTO A    SHEET OF GLASS WHICH WAS FIXED        ACROSS THE EXIT !!"
1880 GOTO1680
1890 PRINT" YOU WERE RENDERED SENSELESS BY    A JINN WHICH JUMPED OUT OF             THE COFFIN !      ";
1891 PRINT"       AND THEN PROCEEDED TO BORE       YOU TO DEATH WITH TRIVIA         ABOUT IT'S FAMILY !!!!"
1900 GOTO1680
1910 PRINT"     AS YOU OPENED THE SACK        A MIST ESCAPED FROM IT AND     COMPLETELY PARALIZED YOU !!!":GOTO1680
1920 PRINT"     YOU WENT TOO HIGH, AND            COULDN'T BREATH !!":GOTO1680
1930 PRINT"   NEXT TIME YOU WANT TO JUMP       OUT OF A BALLOON, TAKE A             PARACHUTE !!!!":GOTO1680
1940 PRINT" NEXT TIME WEAR THE AQUALUNG !!":GOTO1680
1950 PRINT"   YOU FELL INTO A DEEP CHASM       AND BROKE YOUR NECK !!!!":GOTO1680
1960 PRINT"      YOU WERE ANNIHILATED             BY THORGAH, KEEPER               OF THE STAFF !!!":GOTO1680
1970 IFO$="PIP"THEN2830
1971 IFO$="ORG"THEN2250
1972 IFO$="CAS"THEN2010
1973 IFO$<>"SAX"THENPRINTS$:GOTO310
1980 IFB(1,17)<>31ANDB(1,17)<>B THENPRINT"EH ??":GOTO310
1990 PRINT"O.K":IFB(1,18)=B THEN2050
2000 GOTO310
2010 IFB(1,6)<>B ANDB(1,6)<>31THENPRINTH$"A CASSETTE !":GOTO310
2020 IFB(1,8)<>B ANDB(1,8)<>31THENPRINT"IN WHAT ?":GOTO310
2030 IFM$(3,8)=" IT'S EMPTY !!"THENPRINT"INSERT THE CASSETTE INTO THE    RECORDER !":GOTO310
2040 GOTO2140
2050 PRINT" THE BLOB RUNS OUT SCREAMING     AND JUMPS OVER THE CASTLE       WALL INTO THE MOAT,";
2051 PRINT"WHERE        HE QUICKLY DISSOLVES !!"2060 B(1,18)=0:GOTO310
2070 GOSUB2880:IFB(1,A)<>31ANDB(1,A)<>B THENPRINTI$:GOTO310
2080 IFB(2,A)<2THEND=5:GOTO1600
2090 IFA=21THEND=-1:GOSUB1600
2100 PRINT:PRINT"      THE GAME IS OVER !!!":PRINT"       YOU'RE IN AN UNFIT":PRINT"      STATE TO CONTINUE !!"
2110 PRINT"     YOU SHOULDN'T DRINK SO              MUCH VODKA !!!":GOTO1680
2120 IFO$<>"PLA"OR(B(1,8)<>B ANDB(1,8)<>31)THENPRINT"EXCUSE ME ?":GOTO310
2130 IFM$(3,8)=" IT'S EMPTY !!"THENPRINT"O.K":PRINTNH$:GOTO310
2140 PRINT"O.K IT SAYS":PRINT" WAVE THE STAFF !!!"
2150 GOTO310
2160 GOSUB2880
2170 IFB(1,A)<>B ANDB(1,A)<>31THENPRINT"WHAT ??":GOTO310
2180 IFA=25THEN2200
2181 IFB(2,A)=0THEN1100
2190 PRINT"I DON'T THINK",," THAT'S VERY WISE !!":GOTO310
2200 IFB=74THENPRINTY$"IT'S TOO THICK !!":GOTO310
2210 PRINT"CRASH ! BANG ! SMASH !":PRINT" THE PANEL BREAKS INTO":PRINT" THOUSANDS OF TINY PIECES"
2211 PRINT" REVEALING A":PRINT" BLOODSTAINED AXE !!":B(2,9)=1:B(3,9)=1:B(1,25)=0:GOTO310
2220 PRINT"COWARD !!":INPUT" YOU REALLY WANT TO              GIVE UP, JUST LIKE THAT ";N$
2230 IFN$="N"ORN$="NO"THENPRINT" GOOD !":GOTO310
2240 PRINT" WELL "Y$:GOTO310
2250 IFB<>17THENPRINTI$:GOTO310
2260 IFMO=0THEN2265
2261 GOTO2270
2265 IFMA=1THENPRINTY$,," THE ORGANIST'S LYING OVER IT !":GOTO310
2266 PRINTY$,," THE ORGANIST'S PLAYING IT !!":GOTO310
2270 IFTD=1THENPRINT"O.K":PRINT" "NH$:GOTO310
2280 TD=1:B(1,27)=114
2290 PRINT"O.K",," A TRAP DOOR OPENS AND THE       ORGANIST FALLS THROUGH !!"," THUD !",," THE TRAP DOOR CLOSES !":GOTO310
2300 IFMA=0THENPRINT"I DON'T THINK HE'D"," BE TOO PLEASED !":GOTO310
2310 M$(3,27)="HE'S LYING ON THE FLOOR !"
2320 PRINT"O.K":PRINT" HE'S LYING ON THE FLOOR":MO=1:GOTO310
2330 GOSUB2880:IFA<>31ANDA<>47THEN1160
2340 IFB(1,A)<>31THENPRINTY$"WEAR IT UNTIL"," YOU'VE GOT IT !!!":GOTO310
2350 PRINT"O.K. YOU'RE WEARING IT !":IFA=31THENG=1:M$(2,A)=M$(2,A)+"                WHICH YOU ARE WEARING !!":GOTO310
2360 AQ=1:M$(2,A)=M$(2,A)+"                     WHICH YOU ARE WEARING !!":GOTO310
2370 IFO$<>"GLA"THEN1100
2380 IFB<>74THENPRINT"WHERE IS IT ??":GOTO310
2381 IFB(1,30)<>31THENPRINTW$:GOTO310
2390 IFM$(1,25)<>"GLA"THENPRINT"YOU'VE "A$"CUT IT !!":GOTO310
2400 PRINT"O.K":PRINT" THE SHEET OF GLASS HAS BEEN     CUT AWAY !!":M$(1,25)="PAN":A(1,74)=2
2410 B(3,25)=1:M$(2,25)="A GLASS PANEL":M$(3,25)="THERE'S AN AXE BEHIND IT !!":B(1,25)=0:GOTO310
2420 GOSUB2880:IFB(1,A)<>B ANDB(1,A)<>31THEN860
2430 IFB(2,A)=0THEN1100
2431 IFA=21THENPRINT"THE BOTTLE SHATTERS"," ON THE FLOOR !!":B(1,21)=0:GOTO310
2440 IFA<>33THENB(1,A)=B:PRINT"O.K":GOTO310
2450 PRINT"O.K",," YOU'VE THROWN IT !","WHAT NOW . .?":FORR=1TO800:NEXTR
2460 PRINT" WAIT IT'S COMING BACK !!"," THUD !!",," IT'S LANDED RIGHT AT"," YOUR FEET !"
2470 PRINT" AND THERE'S SOMETHING"," ATTACHED TO IT !":M$(3,33)="THERE'S A NOTE                  ATTACHED TO IT !!":B(1,34)=B
2480 GOTO310
2490 GOSUB2880:IFA<>16THEN1160
2500 IFB(1,36)<>31THENPRINTW$:GOTO310
2501 M$(2,A)="A SMOLDERING TORCH"
2510 E=1:PRINT"O.K",," I'TS GOING DARK"," A LUMINOUS ARROW APPEARS !"," ";:A(2,105)=106
2520 IFB=105ORB=83THEND$="SOUTH":GOTO2530
2521 IFB=86THEND$="NORTH":GOTO2530
2522 IFB=84ORB=85ORB=87THEND$="EAST"
2530 PRINT"IT'S POINTING "D$" !":GOTO310
2540 CLS:PRINT,,,,,,,,"  YOU ARE FALLING, FALLING,       FALLI...",,,"...THUD !!!"
2550 FORR=1TO2000:NEXTR:B=105:GOTO40
2560 E=0:FORR=1TO20:CLS5:NEXTR:CLS
2570 PRINT,,,,"  THERE IS A BLINDING FLASH !!    THEN EVERYTHING GOES DARK !!!"
2580 PRINT"  YOU LOOSE ALL",,"  CONSCIOUSNESS !!!":B(1,39)=114:B(3,39)=0
2590 FORR=1TO3000:NEXTR:FORR=0TO14:PRINT@0+(32*R),:GOSUB7
2600 FORA=1TO30:NEXTA:PRINT@0+(32*R),:GOSUB8:FORA=1TO30:NEXTA,R
2610 CLS0:FORR=1TO1400:NEXTR:B=23:GOTO40
2620 PRINTTAB(10)"A SPIRAL STAIRCASE":S=1:C=1:GOTO150
2630 IFB=19ORB=110THEN600
2631 IFB<>101ANDB<>24THEN530
2640 IFG=1THENPRINTY$"SWIM WITH THAT"," SUIT OF ARMOUR ON !!":GOTO310
2650 IFB=24ANDAQ=1THENB=110:GOTO40
2660 IFAQ=1THENB=19:GOTO40
2670 D=12:GOTO1600
2680 IFB<>19THEN530
2681 GOSUB2880:IFA<48ORA>49THEN530
2682 IFB(1,50)<>31THENPRINTW$:GOTO310
2683 IFA=48THENPRINT"O.K",," YOU MISSED !":GOTO310
2690 PRINT"THERE IS AN EAR PIERCING"," SCREAM AS THE DRAGON SINKS"," TO THE BOTTOM OF THE LAKE !!"
2691 M$(2,A)="A DEAD SEA DRAGON"
2700 M$(3,A)="THERE'S A HARPOON STUCK IN IT'S THROAT !!":GOTO310
2710 GOSUB2880:IFA<>52THEN530
2711 IFB(1,51)<>31THENPRINTW$:GOTO310
2720 IFB<>19THENPRINTI$:GOTO310
2730 B(1,51)=0:B(1,60)=31:PRINT"O.K",," THE DRAGON GIVES YOU A PIPE"," IN APPRECIATION":M$(3,49)="IT LOOKS VERY PLEASED !"
2731 M$(3,52)="ITS BANDAGED":GOTO310
2740 IFO$="LUL"THEN2760
2750 PRINT"O.K":GOTO310
2760 PRINT"O.K":IFT=0THENPRINT" "NH$:GOTO310
2761 PRINT" HE FALLS ASLEEP AT THE SOUND    OF YOUR SWEET VOICE"
2770 T=0:PRINT" A MOUSE APPEARS !":B(1,57)=B:B(1,58)=B:GOTO310
2780 GOSUB2880:IFA<>56THEN530
2781 IFB(1,56)<>31THENPRINTH$"THE STAFF":GOTO310
2782 IFB=1THENRUN284
2783 IFB<112THENPRINTNH$:GOTO310
2790 IFB=114THEN2810
2791 CLS:PRINT,,,,,," YOU ARE SHRINKING !!!":FORR=1TO1000:NEXTR:PRINT" YOU ARE ROUGHLY THE SIZE"," OF A MOUSE !!!"
2792 FORR=1TO1000:NEXTR
2800 SH=1:A(2,112)=113:GOTO40
2810 CLS:PRINT,,,,,," YOU ARE GROWING !!"," YOU ARE NOW BACK TO YOUR"," ORIGINAL SIZE !!":FORR=1TO2000:NEXTR
2820 A(1,114)=0:SH=0:GOTO40
2830 IFB(1,60)<>31THENPRINTH$"A PIPE":GOTO310
2831 PRINT"O.K":IFB<>114THENPRINT" "NH$:GOTO310
2840 B(1,61)=B:CR=1:PRINT" THE LID FALLS OFF THE BASKET    AND A ROPE BEGINS TO RISE"," OUT OF IT !!!",," IT'S STOPPED !"
2841 GOTO310
2850 SOUND200,10:CLS:PRINT,,,,
2860 PRINT" YOU ARE NOW THE MASTER OF THE              STAFF",,,"   GUARD IT WELL FOR YOU MAY","       NEED IT AGAIN !!!"
2870 FORR=1TO1500:NEXT:SOUND150,10:PRINT:END
2880 FORA=1TOO:IFM$(1,A)=O$THENRETURN
2881 NEXTA
2890 PRINT"I DIDN'T UNDERSTAND"," THE LAST BIT !":GOTO310
2900 FORA=1TO114:READL$(A),A(1,A),A(2,A),A(3,A),A(4,A),A(5,A),A(6,A):NEXTA
2910 DATAON THE DRAWBRIDGE             OUTSIDE THE MAIN GATE,,,,,,
2920 DATATHE OUTER BAILEY,4,74,,3,,
2930 DATATHE STABLES,,,2,,,
2940 DATATHE INNER GATEHOUSE,6,2,,99,61,
2950 DATAGUARD ROOM,,,20,,,
2960 DATATHE INNER BAILEY,9,4,7,8,,
2970 DATABAKE HOUSE AND                      GRANARY,,,,6,,
2980 DATASTORE,,,6,,,
2990 DATATHE ENTRANCE HALL TO                 THE KEEP,998,6,998,998,,
3000 DATAA LONG TOWER,14,,59,9,64,60
3010 DATATHE GREAT HALL,97,9,100,,,
3020 DATATHE KINGS TOWER,13,,9,,63,
3030 DATAA SMALL TREASURY,16,12,,29,,
3040 DATAA CORRIDOR,15,10,,,,
3050 DATAA KEEP TOWER,,14,,,66,
3060 DATAQUEENS TOWER,,13,,,65,
3070 DATATHE CHAPEL,,,29,,,
3080 DATAA SMALL TOWER,,,,,32,
3090 DATAUNDERWATER,110,,,,101,
3100 DATAA DRUM TOWER,,,,5,34,
3110 DATATHE SMITHY,22,,,59,35,
3120 DATAA STONE TOWER,,21,,30,36,
3130 DATAA WALLED GARDEN,,,,,,
3140 DATAA GROTTO,,,,111,,
3150 DATAA TOWER,,,,,39,
3160 DATAA HOT AIR BALLOON IN            A WALLED GARDEN,,,,,27,
3170 DATAA HOT AIR BALLOON !!,27,27,27,27,28,26
3180 DATAA HOT AIR BALLOON !!,28,28,28,107,992,27
3190 DATAA SMALL COURT YARD,,,13,17,,
3200 DATAA COURT YARD,,59,22,,,
3210 DATAYOU,,,,,,
3220 DATATHE TURRET OF A SMALL                   TOWER,46,,,45,,18
3230 DATATHE TURRET OF A TOWER,47,46,,,,
3240 DATATHE TURRET OF A DRUM                    TOWER,48,47,,,,20
3250 DATAA SMALL STOREROOM,36,48,,,,21
3260 DATATHE TURRET OF A STONE                   TOWER,49,35,,,,22
3270 DATATHE TURRET OF A SMALL                DRUM TOWER,,49,,50,,
3280 DATATHE TURRET OF A SHORT                   TOWER,,51,50,,,
3290 DATATHE TURRET OF TOWER,51,52,,,,25
3300 DATATHE TURRET OF A SHORT                DRUM TOWER,52,53,78,,,
3310 DATATHE TURRET OF A ROUND                   TOWER,53,54,,,,
3320 DATATHE TURRET OF A SMALL                ROUND TOWER,54,,55,,,
3330 DATAA SMALL ROOM,,,56,55,57,
3340 DATAA SMALL ARMOURY,,,45,56,58,999
3350 DATA,,,32,44,,
3360 DATA,33,32,,,,
3370 DATA,34,33,,,,
3380 DATA,35,34,,,,
3390 DATA,37,36,,,,
3400 DATA,,,37,38,,
3410 DATA,38,39,,,,
3420 DATA,39,40,,,,
3430 DATA,40,41,,,,
3440 DATA,41,42,,,,
3450 DATA,,,43,42,,
3460 DATA    A MACHICOLATED                      WALKWAY,,,44,43,,
3470 DATATHE WEST PARAPET OF                THE MAIN GATE,,,,,,43
3480 DATATHE EAST PARAPET OF                THE MAIN GATE,,,,,,44
3490 DATATHE ARMOURY,30,,21,997,,
3500 DATATHE KITCHEN,,,,71,10,
3510 DATATHE WINDLASS ROOM,,,,,62,4
3520 DATATOP OF GATEHOUSE,,,,,,61
3530 DATATHE KINGS TOWER,67,,,,77,12
3540 DATAA LONG TOWER,70,,,68,79,10
3550 DATAQUEENS TOWER,,,69,,76,16
3560 DATAA KEEP TOWER,,70,,69,75,15
3570 DATATHE SOLAR,,63,,,,
3580 DATAA GUEST-CHAMBER,,,64,,,
3590 DATAA CORRIDOR,,,66,65,,
3600 DATAA CORRIDOR,66,64,,,,
3610 DATAUNDERCROFT,,,60,,,
3620 DATAA GUARDROOM,,,,74,44,
3630 DATAA GARDE ROBE,,,74,,,
3640 DATATHE MAIN GATEHOUSE,995,,72,73,,
3650 DATAA KEEP TOWER,,81,,,89,66
3660 DATAQUEENS TOWER,,80,,,88,65
3670 DATATHE KINGS TOWER,,,,,91,63
3680 DATAAN ALLURE,,,,40,,
3690 DATAA LONG TOWER,81,,,,90,64
3700 DATAA CORRIDOR,76,,82,,,
3710 DATAA CORRIDOR,75,79,,82,,
3720 DATAA CORRIDOR,,,81,80,,
3730 DATAIN A MAZE,83,84,83,83,,
3740 DATAIN A MAZE,83,84,85,84,,
3750 DATAIN A MAZE,85,85,86,84,,
3760 DATAIN A MAZE,87,86,86,85,,
3770 DATAIN A MAZE,,86,105,87,,
3780 DATAQUEENS TOWER,,,92,,96,76
3790 DATAA KEEP TOWER,,92,,,93,75
3800 DATAA LONG TOWER,,,,92,95,79
3810 DATATHE KINGS TOWER,92,,,,94,77
3820 DATATHE TOP OF THE KEEP,89,91,90,88,,
3830 DATATHE TURRET OF A KEEP                    TOWER,,,,,,89
3840 DATATHE TURRET OF THE                  KINGS TOWER,,,,,,91
3850 DATATHE TURRET OF A LONG                    TOWER,,,,,,90
3860 DATATHE TURRET OF THE                 QUEENS TOWER,,,,,,88
3870 DATAA SMALL WARD,98,11,,,,
3880 DATATHE BREWHOUSE,,97,,,,
3890 DATAA GARDE ROBE,,,4,,,
3900 DATAA GARDE ROBE,,,,11,,
3910 DATAA LARGE CAVERN,,,,102,,
3920 DATATHE BOTTOM OF A SHAFT,,,101,,,
3930 DATAAN OVEN                         THE DOOR CLANGS SHUT            BEHIND YOU !!!,,,,,,104
3940 DATAA CRYPT,,,,,,
3950 DATAIN A MAZE,83,83,83,83,,
3960 DATATHE CENTRE OF THE                     MAZE,105,,,,,
3970 DATA A HOT AIR BALLOON !!,107,107,28,107,992,108
3980 DATAA HOT AIR BALLOON !!,108,108,108,108,107,109
3990 DATAA HOT AIR BALLOON IN            THE OUTER BAILEY,,,,,108,
4000 DATAUNDERWATER,,19,,,24,
4010 DATAA DOMED ROOM,991,991,24,112,,
4020 DATATHORGAH'S CHAMBER,,,111,,,
4030 DATAA MOUSE HOLE,112,114,,,,
4040 DATAA PIT,113,,,,,
4050 FORA=1TOO:READM$(1,A),M$(2,A),M$(3,A),B(1,A),B(2,A),B(3,A),B(4,A):NEXTA
4060 A$="ALREADY ":Y$="YOU CAN'T ":H$="YOU HAVEN'T GOT "
4070 W$="WITH WHAT ?":I$="IT'S NOT HERE !":NH$="NOTHING HAPPENS"
4080 S$="SORRY ?":RETURN
4090 DATAPOR,A PORTCULLIS,THERE'S NO WAY YOU'LL GET       THROUGH THAT !!,1,,1,
4100 DATA SIG,A SIGN HANGING ABOVE
4101 DATA"IT READS:                            ABANDON HOPE ALL YE WHO               ENTER HERE",1,,1,1
4110 DATAHOO,A GRAPPLING HOOK,Z,31,1,1,
4120 DATABOX,A SMALL BOX,IT'S LOCKED !,18,,1,
4130 DATAKEY,A SMALL KEY,"AN INSCRIPTION : QUO EN WRESTE",31,1,1,
4140 DATACAS,A CASSETTE,"ON THE LABEL:                    FSAB C120",18,,,1
4150 DATASPI,A SPIRAL STAIRCASE,THERE IS A CASSETTE             RECORDER LYING ON IT !!,72,,1,
4160 DATAREC,A CASSETTE RECORDER, IT'S EMPTY !!,44,1,,
4170 DATAAXE,A BLOODSTAINED AXE,"SMEARED IN BLOOD :-             BEWARE THORGAH !!",67,,,
4180 DATAWIN, A WINDLASS, THERE IS A ROPE COMING OFF IT   ATTACHED TO THE PORTCULLIS,74,,1,
4190 DATAMAC,A SCARLET MACKEREL,"IT'S ALL SLIMY, UUUGHH!",99,1,1,
4200 DATAWEL,AN OLD WELL,IT IS FULL OF OIL,60,,1,
4210 DATAPOT,A CERAMIC POT,IT LOOKS PRETTY ANCIENT !       COULD BE WORTH SOMETHING !!     AH! MADE IN TAIWAN !!!,35,1,1,
4220 DATATAP,A LARGE TAPESTRY,IT'S BEAUTIFULLY EMBROIDERED !,11,,1,
4230 DATADOO,A BULKY DOOR,IT'S LOCKED !,11,,,
4240 DATATOR,A BURNING TORCH,Z,62,1,1,
4250 DATASAX,A SAXOPHONE,IT'S GOLD PLATED,25,1,1,
4260 DATABLO,"A MONSTROUS,GREASY,             PULSATING,HUNGRY,               WHITE,BLOB !!!",,32,,1,2
4270 DATAMOA,THE CASTLE MOAT,IT'S VERY DEEP AND MURKY !!,1,,1,
4280 DATASOA,A BAR OF SOAP,"YOU CAN'T , IT KEEPS            SLIPPING OUT OF YOUR HANDS !",54,1,1,
4290 DATAVOD,A BOTTLE OF VODKA,"ON THE LABEL :-  RUSSIAN VODKA  GUARANTEED TO WARM YOUR URALS!",8,2,1,1
4300 DATAPAI,A METAL PAIL,THERE'S A HOLE IN THE BOTTOM,3,1,1,
4310 DATAHAN,,THEY'RE VERY ORNATE !,67,,,
4320 DATABED,A FOUR-POSTER BED,IT LOOKS VERY COMFORTABLE !,67,,1,
4330 DATAGLA,A SHEET OF GLASS,"YOU CAN'T SEE IT, ONLY          FEEL IT !!",74,,,
4340 DATACLO,A GRANDFATHER CLOCK,IT'S STOPPED !,67,,1,
4350 DATAMAD,A MAD ORGANIST !
4351 DATA"A REVOLTING SIGHT, GREASY       BLACK HAIR, AND BUCK TEETH,     WOULDN'T LIKE TO MEET HIM IN A  DARK ALLEY !!"
4352 DATA17,,1,
4360 DATAORG,A MASSIVE PIPE ORGAN,"A GREAT MUSTY CONTRAPTION,      EMINATING RICH, VIBRANT SOUNDS",17,,1,
4370 DATAFOU,A SMALL FOUNTAIN,THE WATER'S A BIT GREEN !!!     THERE'S A DIAMOND LYING         IN THE CENTRE !!!,30,,1,
4380 DATADIA,A DIAMOND,IT'S QUITE LARGE !!,30,1,,
4390 DATAARM,A SUIT OF ARMOUR,A SHINY SUIT COMPLETE WITH A    LARGE PAIR OF GAUNTLETS,5,1,1,
4400 DATASPE,A SPEAR,Z,44,1,1,
4410 DATABOO,A BOOMERANG,IT'S A REAL ABORIGINIE ONE !!,6,1,1,
4420 DATANOT,A NOTE,THE HANDS OF TIME MUST TURN !!,,1,1,1
4430 DATAOVE,A LARGE BREAD OVEN,IT'S SHUT !,7,,1,
4440 DATAEXT,A FIRE EXTINGUISHER,Z,84,1,1,
4450 DATASAC,A SACK,IT'S TIED WITH STRING !,71,1,1,
4460 DATACOF,A COFFIN !!,THERE'S A STENCH OF CHEAP       PERFUME DRIFTING AROUND IT !!,104,,1,
4470 DATATRA,A TRAP DOOR,Z,,,1,
4480 DATAMES,A LUMINOUS MESSAGE              WRITTEN ON THE WALL,IT SAYS GAMROTH !!,106,,1,1
4490 DATA,THE GARDEN DOWN BELOW,,27,,1,
4500 DATA,THE CASTLE DOWN BELOW,,107,,1,
4510 DATA,THE OUTER BAILEY DOWN           BELOW !!,,108,,1,
4520 DATABAL,A HOT AIR BALLOON,ITS A BEAUTIFUL RED ONE,23,,1,
4530 DATA,GREEN FIELDS BELOW,,28,,1,
4540 DATALAK,AN UNDERGROUND LAKE,THE WATERS RED !!,101,,1,
4550 DATAAQU,AN AQUALUNG,IT'S GOT 40 MINUTES BREATHING   TIME !!,23,1,1,
4560 DATAFIS,FISH,THERE'S THOUSANDS OF THEM,19,,1,
4570 DATADRA,A SEA DRAGON !,IT'S WOUNDED !!,19,,1,
4580 DATAHAR,A HARPOON GUN,Z,59,1,1,
4590 DATABAN,A BOX OF BANDAGES,THEY'RE WATERPROOF,57,1,1,
4600 DATAWOU,,IT'S NEEDS BANDAGING,19,,,
4610 DATACHE,A SUNKEN CHEST,IT'S LOCKED,110,,1,
4620 DATAPOO,A SMALL POOL,Z,24,,1,
4630 DATATHO,THORGAH !,QUICK HE'S ASLEEP !!,112,,1,
4640 DATASTA,*THE STAFF*,THERE IS A STRANGE GLOW         RADIATING FROM IT !!,112,1,1,
4650 DATAMOU,A MOUSE,IT SCAMPERS OFF INTO IT'S HOLE,,,1,
4660 DATAHOL,A MOUSE HOLE,IT'S VERY SMALL,,,1,
4670 DATABAS,A SMALL BASKET,Z,114,,1,
4680 DATAPIP,AN INDIAN PIPE,Z,,1,1,
4690 DATAROP,A ROPE,Z,,,1,
4700 DATAPOL,A FLAG POLE,THERE'S A PAIR OF KNICKERS ON   THE TOP !!,92,,1,
4710 DATAPEN,A PENDANT,ITS COVERED IN ASTROLOGICAL     SYMBOLS !,13,1,1,
4720 DATABAG,A BAG OF COINS,Z,68,1,1,
4730 DATARIN,A RING,Z,81,1,1,
4740 DATAKNI,"A CHESS PIECE                   ITS A BLACK KNIGHT !!",CARVED UNDERNEATH 'C.J.',111,1,1,
