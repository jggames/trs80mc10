0 CLS:CLEAR750:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CC TOZZ STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB1:GOTO330
10 Z$="XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":PRINT@64,Z$;:PRINT@128,Z$;:PRINT@97,"MOUNTAIN VALLEY SOFT. PRESENTS";
11 GOSUB1400:PRINT@96,;:FORA=0TO31:READU$:PRINTU$;:POKE1120+A,PEEK(1120+A)-64
12 FORPP=1TO50:NEXT
20 NEXT:FORP=1TO100:NEXT:PRINT@234,"BY B.J. BETTS":PRINT@353,"(C) 1981 MOUNTAIN VALLEY SOFT":PRINT@453,;
21 V$=" ":X$="O.K.":Y$=CHR$(34):DIMR$(18,10),S$(18,3),T$(6):FORP=1TO18:FORPP=1TO3:READU$
30 S$(P,PP)=U$:IFU$=""THENPP=3
40 NEXT:NEXT:Y1$="GO GETDROBURBRECUTCLICLODRIEATFEE":Y2$="LOOHELINVMOVLIGLOCOPEPULPUSPRE":Y3$="QUIREASMASAVSCOSLETWITURUNLTIE"
41 INPUT"HIT enter TO BEGIN";Q$:Z$=""
42 FORP=1TO18:FORPP=1TO10:READU$:R$(P,PP)=U$:IFU$=""THENPP=10
60 NEXT:NEXT:L=6:W1$="BOTTLE OF WINE."
70 CLS:M$="I AM ":ONL GOTO80,90,100,110,120,130,140,150,160,170,180,190,200,210,220,230,240,250
80 M$=M$+"IN THE LIBRARY.":GOTO260
90 M$=M$+"IN THE MAIN HALL.":GOTO260
100 M$=M$+"IN THE KITCHEN.":GOTO260
110 M$=M$+"IN THE DINING ROOM.":GOTO260
120 M$=M$+"AT THE SOUTH END OF THE HALL.":GOTO260
130 M$=M$+"STANDING AT THE ENTRANCE TO FLAGSTONE MANOR.":GOTO260
140 M$=M$+"IN A GLOOMY TUNNEL.":GOTO260
150 M$=M$+"AT THE NORTH END OF THE HALLWAY.":GOTO260
160 M$=M$+"IN THE LOUNGE ROOM.":GOTO260
170 M$=M$+"IN A HUGE GLOOMY ROOM.":GOTO260
180 M$=M$+"IN THE STUDY.":GOTO260
190 M$=M$+"IN A HIDDEN ROOM.":GOTO260
200 M$=M$+"IN A SMALL UNDERGROUND ROOM WITH A HIGH CEILING.":IFIR<2THENM$=M$+" LIGHT FILTERS DOWN FROM A CRACK IN THE ROOF."
201 GOTO260
210 M$=M$+"IN A HALLWAY AT THE TOP OF THE STAIRS.":GOTO260
220 M$=M$+"IN THE ATTIC.":GOTO260
230 M$=M$+"IN A HIDDEN CELLAR.":GOTO260
240 M$=M$+"IN THE MAIN BEDROOM.":GOTO260
250 M$=M$+"IN A WARM BED."
260 GOSUB1:M$="VISIBLE ITEMS: "
270 FORP=1TO10:IFR$(L,P)=""THEN290
280 M$=M$+R$(L,P)+V$
290 NEXT:GOSUB1:IFS$(L,1)=""THEN320
291 PRINT:M$="SOME OBVIOUS EXITS ARE: ":FORP=1TO3:IFS$(L,P)=""THEN320
292 IFS$(L,P)="N"THENM$=M$+"NORTH"
293 IFS$(L,P)="S"THENM$=M$+"SOUTH"
294 IFS$(L,P)="E"THENM$=M$+"EAST"
295 IFS$(L,P)="W"THENM$=M$+"WEST"
300 IFS$(L,P)="U"THENM$=M$+"UP"
301 IFS$(L,P)="D"THENM$=M$+"DOWN"
310 M$=M$+V$:NEXT
320 GOSUB1
330 IFL=6ANDIR=2THEN1600
331 DM=DM+1:IFDM=80THEN1540
332 IFDM>65THENPRINT"IT'S GETTING DARK."
340 Q$="":INPUT"-------TELL ME WHAT TO DO-------";Q$:A=LEN(Q$):M=M+1:IFL=10ANDCT=1THEN345
341 IFA>1THEN390
342 IFQ$="I"THEN1310
343 GOTO360
345 GOSUB1370:M$="THE GHOST, OUTRAGED AT SEEING ME WITH HIS GOLD, OVERCOMES HIS DISLIKE OF GARLIC...  AND THROTTLES ME!"
346 GOSUB1:GOTO1380
347 IFA=0THEN70
360 IFL=15ANDQ$="D"THENL=17:GOTO70
361 IFL=7ANDQ$="S"THENL=11:GOTO70
362 IFL=14ANDQ$="D"THENL=2:GOTO70
363 IFL=16ANDQ$="U"THENL=1:GOTO70
364 FORP=1TO3:IFS$(L,P)<>Q$THENNEXT:GOTO380
365 IFQ$="N"THENL=L+3:GOTO70
366 IFQ$="S"THENL=L-3:GOTO70
370 IFQ$="E"THENL=L+1:GOTO70
371 IFQ$="W"THENL=L-1:GOTO70
380 M$="I CAN'T GO IN THAT DIRECTION.":GOTO3
390 IFASC(Q$)>80THEN430
391 IFASC(Q$)>71THEN410
392 FORP=1TO11:IFLEFT$(Q$,3)<>MID$(Y1$,P*3-2,3)THEN400
393 ONP GOTO730,680,620,1090,1140,920,730,1010,930,1210,1180
400 NEXT:GOTO450
410 FORP=1TO10:IFLEFT$(Q$,3)<>MID$(Y2$,P*3-2,3)THEN420
411 ONP GOTO460,800,1310,860,1090,1020,950,1050,1050,1050
420 NEXT:GOTO450
430 FORP=1TO10:IFLEFT$(Q$,3)<>MID$(Y3$,P*3-2,3)THEN440
431 ONP GOTO830,460,1140,840,820,890,1160,1160,1040,1120
440 NEXT
450 M$="I CANNOT TRANSLATE THAT.":GOTO3
460 IFA<6THEN70
461 U$=MID$(Q$,6,3):IFU$="DIA"THEN465
462 GOTO470
465 U$="DIARY.":GOSUB1250:IFB=0THEN1430
466 PRINTX$:M$="IT SAYS FLAGSTONE HATES GARLIC BREATH.":GOTO3
470 IFU$="PAP"THEN475
471 GOTO480
475 U$="PIECE OF PAPER.":GOSUB1250:IFB=0THEN1430
476 PRINTX$:M$="IT SAYS: "+Y$+"THE FIRST IS 3."+Y$:GOTO3
480 IFU$="BOO"THEN485
481 PRINTX$:GOTO490
485 U$="BOOK.":GOSUB1250:IFB=0THEN1430
486 PRINTX$:M$="IT'S CALLED "+Y$+"GHOST STORIES"+Y$+". THERE'S SOMETHING SCRIBBLED ON THE TITLE PAGE.":GOSUB1
487 M$="IT SAYS: "+Y$+"THE THIRD IS 7"+Y$+".":GOTO3
490 IFL=6ANDU$="SIG"THENM$="SIGN SAYS: FLAGSTONE MANOR-- ENTER AT YOUR OWN RISK":GOTO3
491 IFU$="WIN"ORU$="RAC"THENIFL=4ANDLW=0THENLW=1:U$=W1$:GOTO1420
500 IFU$="SUI"ORU$="ARM"THENIFL=2THENM$="IT'S HOLDING A SHARP AXE.":GOTO3
510 IFU$="BOT"THEN515
511 GOTO520
515 U$=W1$:GOSUB1240:IFB=1THEN517
516 GOTO520
517 IFDW=0THENPRINT"AN EXCELLENT VINTAGE.":GOTO330
518 PRINT"IT'S EMPTY.":GOTO330
520 IFL=8ANDU$="CLO"THENM$="IT SAYS"+STR$(80-DM)+" MOVES UNTIL SUNDOWN.":GOTO3
521 IFL=1ANDU$="SHE"ANDLS=0THENLS=1:U$="BOOK.":GOTO1420
522 IFL=10ANDU$="DOO"THENM$="IT'S GOT A COMBINATION LOCK.":GOTO3
523 IFL=17ANDU$="WAR"THENPRINT"IT'S EMPTY.":GOTO330
530 IFL=1ANDAA=1ANDU$="OPE"THENM$="I SEE STEPS LEADING DOWN.":GOTO3
531 IFL=5ANDU$="PAI"THEN535
532 GOTO540
535 IFCP=0THENM$="IT'S A PORTRAIT OF OLD ARTHUR FLAGSTONE. THE EYES ARE WATCHING ME.":GOTO3
536 M$="IT'S CUT TO SHREDS.":GOTO3
540 IFL=7ANDU$="MOU"ANDFM=0THENM$="CUTE LITTLE FELLOW.":GOTO3
541 IFL=17ANDU$="BED"THENM$="LOOKS A WARM PLACE TO SLEEP.":GOTO3
542 IFL=15ANDU$="CHE"ANDOH=0THEN1470
543 IFL=16ANDU$="SKE"THENM$="THE HEAD'S CHOPPED OFF.":GOTO3
550 IFU$="CHE"THENU$="LUMP OF CHEESE.":GOSUB1240:IFB=1THENPRINT"LOOKS TASTY.":GOTO330
560 IFL=9ANDLF=0THENIFU$="FIR"ORU$="WOO"ORU$="PLA"THENM$="THE FIREPLACE IS FILLED WITH DRY WOOD.":GOTO3
570 IFL=3ANDU$="CUP"ANDOA=0THENOA=1:U$="MATCHES.":GOTO1420
571 IFU$="FLA"ORU$="STO"THENIFL=6ANDIR<2THENM$="THERE'S A CRACK DOWN THE MIDDLE.":GOTO3
580 IFL=11ANDU$="DES"ANDLD=0THENLD=1:U$="ASHTRAY.":GOTO1420
581 IFL=12ANDBC=1ANDU$="COB"THEN1480
582 IFL=9ANDU$="CHA"THENPRINT"IT'S BROKEN.":GOTO330
590 IFU$="CAN"THEN595
591 GOTO600
595 U$="CAN.":GOSUB1240:IFB=1THEN597
596 GOTO600
597 IFOT=0THENM$="IT'S SEALED. THE LABEL IS MISSING.":GOTO3
598 PRINT"IT'S EMPTY.":GOTO330
600 IFL=4ANDU$="TAB"ANDLT=0THENLT=1:U$="CHINA DISH.":GOTO1420
601 IFU$="GHO"ORU$="FIG"THENIFL=10THENPRINT"IT'S ARTHUR FLAGSTONE.":GOTO330
610 IFL=10ANDU$="CHA"THENPRINT"IT'S BLOODSTAINED.":GOTO330
611 IFL=13ANDU$="RIN"THENPRINT"IT'S ATTACHED TO THE WALL.":GOTO330
612 PRINT"I SEE NOTHING SPECIAL.":GOTO330
620 GOSUB1260:FORP=1TO6:IFT$(P)=U$THEN630
621 NEXT:PRINT"I'M NOT CARRYING IT.":GOTO330
630 IFU$="LUMP OF CHEESE."ANDL=7THENU$="MOU":GOTO1190
640 FORPP=1TO10:IFR$(L,PP)=""THEN650
641 NEXT:M$="SORRY, THERE'S TOO MUCH HERE ALREADY.":GOTO3
650 IFU$="BOTTLE OF WINE."ANDL=11THENPI=PI+1
660 IFASC(U$)=42THENCT=0
670 PRINTX$:T$(P)="":R$(L,PP)=U$:GOSUB1390:GOTO70
680 GOSUB1260:FORP=1TO6:IFT$(P)=U$THENM$="I'M ALREADY CARRYING IT.":GOTO3
681 NEXT:FORP=1TO10:IFR$(L,P)=U$THEN690
682 NEXT:IFU$="XXX"THEN1440
683 PRINT"I DON'T SEE IT HERE.":GOTO330
690 FORPP=1TO6:IFT$(PP)=""THEN700
691 NEXT:M$="I'M CARRYING TOO MUCH. TAKE INVENTORY.":GOTO3
700 IFU$="BOTTLE OF WINE."ANDL=11THENPI=PI-1
710 IFASC(U$)=42THENCT=1
720 PRINTX$:T$(PP)=U$:R$(L,P)="":GOSUB1390:GOTO70
730 IFASC(Q$)=71THENC=4:GOTO740
731 C=7
740 U$=MID$(Q$,C,3):IFU$="HOL"ANDL=13ANDIR=2THEN745
741 GOTO750
745 U$="LADDER.":GOSUB1250:IFB=0THEN1430
746 ES=1:PRINTX$:PRINT"I USE THE LADDER.":T$(P)="":R$(13,7)=U$:GOSUB1400:L=6:GOTO70
750 IFL=6ANDIR=2ANDU$="HOL"THENL=13:GOTO70
751 IFU$="NOR"THENQ$="N":GOTO360
752 IFU$="SOU"THENQ$="S":GOTO360
753 IFU$="EAS"THENQ$="E":GOTO360
754 IFU$="WES"THENQ$="W":GOTO360
755 IFU$="DOW"THENQ$="D":GOTO360
756 IFU$="UP"THENQ$="U":GOTO360
757 IFU$="DOO"THEN1560
760 IFL=2ANDU$="STA"THENL=14:GOTO70
761 IFL=1ANDAA=1ANDU$="OPE"THENL=16:GOTO70
762 IFL=17ANDU$="BED"THENL=18:GOTO70
763 IFU$="MAN"ORU$="HOL"THENIFL=17THENL=15:GOTO70
770 IFU$="OPE"THEN775
771 GOTO780
775 IFL=9ANDLF=1THENL=12:GOTO70
776 IFL=7ANDFM=1THENL=10:GOTO70
780 IFL=11ANDU$="OPE"ANDPL=1THENL=7:GOTO70
781 IFU$="SLE"THENQ$=U$:GOTO890
790 M$="I CAN'T "+Y$+Q$+Y$:GOTO3
800 IFK>0ANDOC=0THENPRINT"OPEN THE DOOR OF TIME.":GOTO330
801 IFK>2ANDPI=2ANDPL=0THENPRINT"PRESS ON.":GOTO330
802 IFOP=0ANDM>40THENM$="THE KEYS ARE A PRESSING PROBLEM.":GOTO3
810 IFK>2ANDCP=0THENM$="ARTHUR FLAGSTONE IS HIDING SOMETHING.":GOTO3
811 PRINT"LOOK AROUND.":GOTO330
820 M$="I'VE BEEN HERE"+STR$(M)+"MOVES.":GOTO3
830 PRINT:PRINT:M$="THE ADVENTURE IS OVER. YOU'VE BEEN HERE"+STR$(M)+" MOVES.":GOSUB1
831 INPUT"WANT TO TRY AGAIN";Q$:IFLEFT$(Q$,1)="Y"THENRUN
832 IFLEFT$(Q$,1)="N"THENEND
833 GOTO830
840 PRINT"I CAN'T SAVE YOU.":GOTO330
860 U$=MID$(Q$,6,3):IFU$="PAI"ANDL=5THENPRINT"IT'S STUCK TO THE WALL.":GOTO330
861 IFL=1ANDU$="LAM"THENPRINT"IT'S GLUED TO THE SHELF.":GOTO330
862 IFU$="FLA"ORU$="STO"THENIFL=6THENPRINT"IT WON'T BUDGE.":GOTO330
870 IFL=1ANDU$="SHE"THENPRINT"THEY'RE TOO HEAVY.":GOTO330
871 IFU$="WAL"ANDL=7THEN1230
872 PRINTX$:IFL=15ANDU$="CHE"ANDMC=0THENMC=1:U$="ROPE.":GOTO1420
873 IFL=12ANDU$="COB"ANDBC=0THENBC=1:GOTO1480
874 IFL=17ANDU$="BED"ANDMB=0THENMB=1:U$="CAN OPENER.":GOTO1420
880 PRINT"I FOUND NOTHING SPECIAL.":GOTO330
890 PRINTX$:PRINT"GOODNIGHT.":GOSUB1400:IFL<>18THENGOSUB1370:GOTO1490
891 IFDO=1THENGOSUB1370:M$="I FORGOT TO LOCK THE DOOR!":GOSUB1:GOTO1490
892 IFPI=1THENA=L:U$=W1$:L=11:GOSUB1240:W1$="EMPTY BOTTLE.":R$(L,P)=W1$:L=A:PI=2:DW=1
900 DM=0:K=K+1:PRINT:PRINT"GOOD MORNING.":IFK=1THENM$="IN THE NIGHT I HEARD THE CLANKING OF CHAINS.":GOTO3
901 IFK>1ANDPI=0THENM$="I HAD A DREAM. A VOICE SAID: "+Y$+"LEAVE THE WINE IN THE STUDY OVERNIGHT"+Y$+".":GOTO3
910 PRINT"I HAD A QUIET NIGHT.":GOTO330
920 U$="KNIFE.":GOSUB1250:IFB=0THEN1430
921 U$=MID$(Q$,5,3):IFL=5ANDU$="PAI"ANDCP=0THEN925
922 GOTO1450
925 CP=1:M$="OK. I HEAR A HORRIBLE SCREAM.":GOSUB1:GOSUB1400:U$="PIECE OF PAPER.":GOTO1420
930 U$=MID$(Q$,7,3):IFU$=""THENPRINT"DRINK WHAT?":GOTO330
931 IFU$="WIN"THEN935
932 GOTO940
935 IFDW=1THEN1460
936 U$=W1$:GOSUB1250:IFB=0THEN1430
937 PRINTX$:DW=1:W1$="EMPTY BOTTLE.":T$(P)=W1$:PRINT"HIC!":PRINT"DERLISHUSH.":GOTO330
940 PRINT"ARE YOU KIDDING?":GOTO330
950 U$=MID$(Q$,6,3):IFL=8ANDU$="CLO"ANDOC=0THENOC=1:PRINTX$:U$="KNIFE.":GOTO1420
951 IFL=4ANDU$="PAN"ANDOP=0THENPRINT"HOW?":GOTO330
952 IFL=11ANDPL=0ANDU$="PAN"THENPRINT"HOW?":GOTO330
953 IFL=2ANDU$="DOO"THENPRINT"I CAN'T-- I'M TRAPPED.":GOTO330
960 IFU$="CAN"THEN965
961 GOTO970
965 IFOT=1THEN1460
966 U$="CAN.":GOSUB1250:IFB=0THEN1430
967 U$="CAN OPENER.":GOSUB1250:IFB=0THEN1430
968 PRINTX$:PRINT"SOMETHING FALLS OUT.":GOSUB1390:U$="LUMP OF CHEESE.":OT=1:GOTO1420
970 IFL=10ANDU$="DOO"THEN975
971 GOTO980
975 IFMD=1THEN1460
976 IFEG=0THEN1530
977 INPUT"FIRST NUMBER";Q$:IFQ$<>"3"THEN1520
978 INPUT"SECOND NUMBER";Q$:IFQ$<>"8"THEN1520
979 INPUT"THIRD NUMBER";Q$:ON-(Q$<>"7")GOTO1520:GOTO1500
980 IFU$<>"DOO"ANDU$<>"CHE"THEN1000
981 Q$=U$:U$="BUNCH OF KEYS.":GOSUB1250:IFB=0THEN1430
982 IFQ$="CHE"THEN985
983 GOTO990
985 IFL=15ANDOH=0THENOH=1:PRINTX$:R$(15,1)="OPEN CHEST.":U$="DIARY.":GOTO1420
986 GOTO1000
990 IFL=17ORL=14ANDDO=0THENDO=1:R$(17,1)="OPEN DOOR.":R$(14,1)=R$(17,1):PRINTX$:GOSUB1390:GOTO70
991 IFL=8ANDDD=0THENDD=1:R$(8,1)="OPEN DOOR.":PRINTX$:GOSUB1390:GOTO70
1000 PRINT"IT'S ALREADY OPEN.":GOTO330
1010 PRINT"TRY LOCK":GOTO330
1020 U$=MID$(Q$,6,3):IFU$<>"DOO"THEN1450
1021 IFL<>14ANDL<>17THENM$="IT WON'T HELP TO LOCK THIS DOOR.":GOTO3
1022 IFDO=0THENPRINT"IT'S ALREADY LOCKED.":GOTO330
1023 U$="BUNCH OF KEYS.":GOSUB1250:IFB=0THEN1430
1024 PRINTX$:R$(17,1)="LOCKED DOOR.":R$(14,1)=R$(17,1):DO=0
1030 GOSUB1390:GOTO70
1040 IFMID$(Q$,4,1)="O"THENPRINT"TRY OPEN":GOTO330
1041 U$=MID$(Q$,9,3):PRINT"IT'S NOT ON FIRE.":GOTO330
1050 IFLEFT$(Q$,2)="PR"THENC=7:A=1:GOTO1060
1051 C=6
1060 U$=MID$(Q$,C,3):PRINTX$:IFL=4ANDA=1ANDU$="PAN"ANDOP=0THEN1065
1061 GOTO1070
1065 OP=1:PRINT"PANEL SLIDES OPEN.":GOSUB1400:R$(4,1)="OPEN PANEL.":U$="BUNCH OF KEYS.":GOTO1420
1070 IFL=11ANDU$="PAN"ANDA=1ANDPI=2ANDPL=0THEN1075
1071 IFMID$(Q$,3,1)="L"THENIFU$="ROP"ORU$="RIN"THEN1590
1072 GOTO1080
1075 PL=1:PRINT"PANEL SLIDES OPEN.":GOSUB1400:R$(11,1)="OPEN PANEL.":U$="OPENING.":GOTO1420
1080 PRINT"NOTHING HAPPENS.":GOTO330
1090 IFASC(Q$)=66THENPRINT"TRY LIGHT":GOTO330
1091 IFMID$(Q$,7,3)="LAM"THENPRINT"THERE'S NO ELECTRICITY.":GOTO330
1092 U$="MATCHES.":GOSUB1250:IFB=0THEN1430
1093 U$=MID$(Q$,7,3)
1100 IFU$="FIR"ORU$="WOO"THEN1105
1101 GOTO1110
1105 IFL=9ANDLF=0THEN1107
1106 GOTO1110
1107 LF=1:PRINTX$:M$="THE WOOD DISAPPEARS IN A PUFF OF SMOKE.":GOSUB1:GOSUB1410
1108 M$="THE BACK OF THE FIREPLACE MOVES ASIDE.":GOSUB1:GOSUB1410:U$="OPENING.":R$(9,2)="":GOTO1420
1110 IFU$="MAT"THENPRINT"OK. MATCH FLARES UP...":GOSUB1390:PRINT"AND GOES OUT.":GOTO330
1111 PRINT"IT WON'T BURN.":GOTO330
1120 IFMID$(Q$,5,3)<>"ROP"THEN1450
1121 IFL<>13THENPRINT"I CAN'T DO THAT HERE.":GOTO330
1122 IFIR=1THEN1460
1123 U$="ROPE.":GOSUB1250:IFB=0THEN1430
1124 T$(P)="":PRINTX$:M$="I TIE ONE END TO THE RING, AND THROW THE OTHER END THROUGH THE DOOR.":GOSUB1
1130 R$(13,10)="TRAIL OF ROPE.":R$(10,10)="ROPE END.":IR=1:GOSUB1410:GOTO70
1140 U$=MID$(Q$,7,3):IFU$="DOO"THENPRINT"IT'S TOO SOLID.":GOTO330
1141 IFL=12ANDU$="COB"THENPRINT"TRY MOVE":GOTO330
1142 IFU$="PAN"THENIFL=4ORL=11THENPRINT"IT'S TOO SOLID.":GOTO330
1150 GOTO1450
1160 IFLEFT$(Q$,2)="TW"THENC=7:GOTO1170
1161 C=6
1170 U$=MID$(Q$,C,3):PRINTX$:IFL=1ANDU$="LAM"ANDAA=0THEN1175
1171 PRINT"NOTHING HAPPENS.":GOTO330
1175 AA=1:PRINT"C L I C K":PRINT"THE SHELVES MOVE ASIDE.":GOSUB1400:U$="OPENING.":GOTO1420
1180 U$=MID$(Q$,6,3):IFU$="MOU"ANDFM=1THEN1460
1190 IFU$="MOU"ANDL=7THEN1195
1191 GOTO1200
1195 U$="LUMP OF CHEESE.":GOSUB1250:IFB=0THEN1430
1196 FM=1:T$(P)="":PRINTX$:M$="THE MOUSE GRABS THE CHEESE AND DISAPPEARS INTO A SMALL HOLE.":GOSUB1:GOSUB1410
1197 PRINT"I HEAR A CLICK.":GOSUB1410:PRINT"THE WALL MOVES ASIDE.":GOSUB1400:GOTO1510
1200 PRINT"IT'S NOT HUNGRY.":GOTO330
1210 U$=MID$(Q$,5,3):IFU$=""THENPRINT"EAT WHAT?":GOTO330
1211 IFU$="CHE"THEN1215
1212 GOTO1220
1215 IFEC=1THEN1460
1216 U$="LUMP OF CHEESE.":GOSUB1250:IFB=0THEN1430
1217 T$(P)="":EC=1:PRINTX$:GOTO330
1220 IFU$="GAR"THEN1225
1221 GOTO1230
1225 IFEG=1THEN1460
1226 U$="GARLIC.":GOSUB1250:IFB=0THEN1430
1227 EG=1:PRINTX$:T$(P)="":PRINT"HORRIBLE.":GOTO330
1230 PRINT"ARE YOU KIDDING?":GOTO330
1240 B=0:FORP=1TO10:IFR$(L,P)=U$THENB=1:RETURN
1241 NEXT
1250 B=0:FORP=1TO6:IFT$(P)=U$THENB=1:RETURN
1251 NEXT:RETURN
1260 IFASC(Q$)=71THENC=5:GOTO1270
1261 C=6
1270 U$=MID$(Q$,C,3):IFL=2ANDC=5ANDU$="AXE"THEN1275
1271 IFC=5ANDL=10ANDU$="CHA"THENPRINT"GHOST WON'T LET ME.":GOTO330
1272 IFU$="KNI"THENU$="KNIFE.":RETURN
1273 GOTO1280
1275 PRINTX$:GOSUB1390:GOSUB1370:M$="THE SUIT OF ARMOUR COMES TO LIFE... AND CUTS ME IN HALF!":GOSUB1:GOTO1380
1280 IFU$="CHE"THENU$="LUMP OF CHEESE.":RETURN
1281 IFU$="BOT"THENU$=W1$:RETURN
1282 IFU$="WIN"ANDW1$="BOTTLE OF WINE."THENU$=W1$:RETURN
1283 IFU$="PAP"THENU$="PIECE OF PAPER.":RETURN
1284 IFU$="ASH"ORU$="TRA"THENU$="ASHTRAY.":RETURN
1285 IFU$="MAT"THENU$="MATCHES.":RETURN
1290 IFU$="BOO"THENU$="BOOK.":RETURN
1291 IFU$="GAR"THENU$="GARLIC.":RETURN
1292 IFU$="KEY"THENU$="BUNCH OF KEYS.":RETURN
1293 IFU$="DIA"THENU$="DIARY.":RETURN
1294 IFU$="GOL"ORU$="BAR"THENU$="*GOLD BARS*":RETURN
1295 IFU$="LAD"THENU$="LADDER.":RETURN
1300 IFU$="ROP"THENU$="ROPE.":RETURN
1301 IFU$="DIS"THENU$="CHINA DISH.":RETURN
1302 IFU$="OPE"THENU$="CAN OPENER.":RETURN
1303 IFU$="CAN"THENU$="CAN.":RETURN
1304 U$="XXX":RETURN
1310 N=0:M$="I AM CARRYING: ":FORP=1TO6:IFT$(P)=""THEN1330
1320 M$=M$+T$(P)+V$:N=N+1
1330 NEXT:IFN=0THENM$=M$+"NOTHING AT ALL."
1340 GOSUB1:GOTO330
1370 CLS:PRINT"I AM IN A LOT OF TROUBLE!":PRINT:RETURN
1380 PRINT"I AM DEAD!":GOTO830
1390 FORP=1TO750:NEXT:RETURN
1400 FORP=1TO1500:NEXT:RETURN
1410 FORP=1TO2500:NEXT:RETURN
1420 PRINT"I SEE SOMETHING.":GOSUB1390:FORP=1TO10:IFR$(L,P)=""THENR$(L,P)=U$:GOTO70
1421 NEXT:GOTO70
1430 PRINT"I CAN'T DO THAT... YET.":GOTO330
1440 M$="IT'S BEYOND MY POWER TO DO THAT.":GOTO3
1450 PRINT"IT WON'T HELP TO DO THAT.":GOTO330
1460 PRINT"I ALREADY HAVE.":GOTO330
1470 PRINT"IT'S LOCKED.":GOTO330
1480 M$="THERE'S WRITING ON THE WALL BEHIND THE COBWEBS. IT SAYS: "+Y$+"THE SECOND IS 8"+Y$+".":GOTO3
1490 M$="SOMETHING CAME IN THE NIGHT... AND STRANGLED ME WITH A RUSTY CHAIN.":GOSUB1:GOTO1380
1500 MD=1:PRINT"THE DOOR SLIDES OPEN.":R$(10,1)="OPEN DOOR.":GOSUB1400:GOTO70
1510 R$(7,2)="":U$="OPENING.":GOTO1420
1520 PRINT"THAT'S WRONG!":GOSUB1390:GOSUB1370:GOTO1550
1530 GOSUB1370:M$="THE GHOST MATERIALISES... AND CHOKES ME WITH THE CHAIN.":GOSUB1:GOTO1380
1540 GOSUB1370:PRINT"IT'S DARK!":GOTO1550
1550 M$="THE SUIT OF ARMOUR APPEARS... AND CHOPS MY HEAD OFF!":GOSUB1:GOTO1380
1560 IFL=6THEN1565
1561 IFL=14THEN1563
1562 GOTO1570
1563 IFDO=0THEN1470
1564 L=17:GOTO70
1565 IFIR=0THENM$="OK. THE DOOR SLAMS SHUT BEHIND ME.":GOSUB1:GOSUB1400:L=2:GOTO70
1566 PRINT"IT WON'T OPEN.":GOTO330
1570 IFL=17THEN1575
1571 IFL=8THEN1573
1572 GOTO1580
1573 IFDD=0THEN1470
1574 L=11:GOTO70
1575 IFDO=0THEN1470
1576 L=14:GOTO70
1580 IFL=10THEN1585
1581 GOTO790
1585 IFMD=0THEN1470
1586 L=13:GOTO70
1590 IFIR<2ANDL=13THENGOSUB1390:GOSUB1370:M$="A LARGE STONE CRASHES FROM THE ROOF... AND CLOBBERS ME!":GOSUB1:GOTO1380
1591 IFIR=1THEN1595
1592 GOTO1080
1595 PRINT"CRAAA..SSH!":R$(13,9)="HOLE IN ROOF.":R$(13,8)="PIECES OF STONE."
1596 R$(6,3)="HOLE.":R$(6,1)="CLOSED DOOR.":IR=2:GOTO330
1600 IFCT=0THENM$="YOU'VE ESCAPED FROM THE HOUSE, BUT YOU LEFT THE GOLD BEHIND.":GOSUB1:GOTO340
1601 M$="congratulations! YOU'VE ESCAPED WITH THE GOLD. YOU'VE SOLVED IT ALL.":GOSUB1:GOTO830
1610 DATA" ",T,H,E," ",S,E,C,R,E,T," ",O,F," ",F,L,A,G,S,T,O,N,E," ",M,A,N,O,R," "," "
1611 DATA"E","",N,E,W,W,"",E,"",N,S,W,"",S,"",S,E,"",W,"",S,"",S,"",S,"",S,"",D,"",D,"",U,"","",W,""
1612 DATASHELVES.,LAMP.,"",CLOSED DOOR.,STAIRCASE.,SUIT OF ARMOUR.,"",CUPBOARD.,CAN.,"",SMALL PANEL.,TABLE.,WINE RACK.,""
1620 DATAPAINTING.,"",OPEN DOOR.,SIGN.,FLAGSTONE.,"",BRICK WALL.,STARTLED MOUSE.,"",LOCKED DOOR.,GRANDFATHER CLOCK.,""
1621 DATAFIREPLACE.,FIREWOOD.,ARMCHAIR.,"",METAL DOOR.,GHOSTLY FIGURE.,RUSTY CHAIN.,""
1630 DATALARGE PANEL.,DESK.,"",COBWEBS.,LADDER.,"",*GOLD BARS*,IRON RING.,"",LOCKED DOOR.,"",OLD CHEST.,""
1631 DATASKELETON.,GARLIC.,"",OPEN DOOR.,MANHOLE.,WARDROBE.,BED.,"",PILLOW.,MATTRESS.,""
