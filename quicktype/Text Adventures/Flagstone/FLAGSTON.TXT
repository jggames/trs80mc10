1 POKE16396,23:CLS:CLEAR650:DEFINTA-P:DEFSTRQ-Z:Z=STRING$(57,88):PRINT@131,Z;:PRINT@259,Z;:PRINT@204,"Mountain   Valley   Software   Presents";:GOSUB140:PRINT@195,"";:FORA=1TO29:READU:PRINTU;" ";:IFU=" "THEN2:ELSEFORPP=1TO50:NEXT
2 NEXT:FORP=1TO100:NEXT:PRINT@472,"By B.J. Betts":PRINT@713,"Copyright (C) 1981 - Mountain Valley Software":PRINT@907,"";:ONERRORGOTO7:Z=STRING$(63,45):V="  ":X="O.K.":Y=CHR$(34):DIMR(18,10),S(18,3),T(6):FORP=1TO18:FORPP=1TO3:READU
3 S(P,PP)=U:IFU=""THENPP=3
4 NEXT:NEXT:Y1="GO GETDROBURBRECUTCLICLODRIEATFEE":Y2="LOOHELINVMOVLIGLOCOPEPULPUSPRE":Y3="QUIREASMASAVSCOSLETWITURUNLTIE":INPUT"Want to restore a previously saved game";Q:IFLEFT$(Q,1)="Y"THEN135:ELSEFORP=1TO18:FORPP=1TO10:READU:R(P,PP)=U
5 IFU=""THENPP=10
6 NEXT:NEXT:L=6:W1="Bottle of wine."
7 CLS:PRINT"I am ";:ONLGOTO8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25
8 PRINT"in the library.":GOTO26
9 PRINT"in the main hall.":GOTO26
10 PRINT"in the kitchen.":GOTO26
11 PRINT"in the dining room.":GOTO26
12 PRINT"at the south end of the hall.":GOTO26
13 PRINT"standing at the entrance to Flagstone Manor.":GOTO26
14 PRINT"in a gloomy tunnel.":GOTO26
15 PRINT"at the north end of the hallway.":GOTO26
16 PRINT"in the lounge room.":GOTO26
17 PRINT"in a huge gloomy room.":GOTO26
18 PRINT"in the study.":GOTO26
19 PRINT"in a hidden room.":GOTO26
20 PRINT"in a small underground room with a high ceiling.":IFIR<2PRINT"Light filters down from a crack in the roof.":GOTO26:ELSE26
21 PRINT"in a hallway at the top of the stairs.":GOTO26
22 PRINT"in the attic.":GOTO26
23 PRINT"in a hidden cellar.":GOTO26
24 PRINT"in the main bedroom.":GOTO26
25 PRINT"in a warm bed."
26 PRINT"Visible items:  ";:IFL=8THENN=1:ELSEIFL=1THENN=-1:ELSEIFL=17THENN=-1:ELSEN=0
27 FORP=1TO10:IFR(L,P)=""THEN29:ELSEN=N+1:IFN=4THENPRINT:ELSEIFN=8THENPRINT
28 PRINTR(L,P);V;
29 NEXT:PRINT:IFS(L,1)=""THEN32:ELSEPRINT:PRINT"Some obvious exits are:  ";:FORP=1TO3:IFS(L,P)=""THEN32:ELSEIFS(L,P)="N"THENPRINT"NORTH";:ELSEIFS(L,P)="S"THENPRINT"SOUTH";:ELSEIFS(L,P)="E"THENPRINT"EAST";:ELSEIFS(L,P)="W"THENPRINT"WEST";
30 IFS(L,P)="U"THENPRINT"UP";:ELSEIFS(L,P)="D"THENPRINT"DOWN";
31 PRINTV;:NEXT
32 PRINT:PRINTZ
33 IFL=6ANDIR=2THEN160:ELSEDM=DM+1:IFDM=80THEN154:ELSEIFDM>65PRINT"It's getting dark."
34 Q="":INPUT"------Tell me what to do";Q:A=LEN(Q):M=M+1:IFL=10ANDCT=1GOSUB137:PRINT"The ghost, outraged at seeing me with his gold,":PRINT"overcomes his dislike of garlic...  and THROTTLES me!":GOTO138:ELSEIFA=0THEN7:ELSEIFA>1THEN39
35 IFQ="I"THEN131
36 IFL=15ANDQ="D"THENL=17:GOTO7:ELSEIFL=7ANDQ="S"THENL=11:GOTO7:ELSEIFL=14ANDQ="D"THENL=2:GOTO7:ELSEIFL=16ANDQ="U"THENL=1:GOTO7:ELSEFORP=1TO3:IFS(L,P)<>QTHENNEXT:GOTO38:ELSEIFQ="N"THENL=L+3:GOTO7:ELSEIFQ="S"THENL=L-3:GOTO7
37 IFQ="E"THENL=L+1:GOTO7:ELSEIFQ="W"THENL=L-1:GOTO7
38 PRINT"I can't go in THAT direction.":GOTO33
39 IFASC(Q)>80THEN43:ELSEIFASC(Q)>71THEN41:ELSEFORP=1TO11:IFLEFT$(Q,3)<>MID$(Y1,P*3-2,3)THEN40:ELSEONPGOTO73,68,62,109,114,92,73,101,93,121,118
40 NEXT:GOTO45
41 FORP=1TO10:IFLEFT$(Q,3)<>MID$(Y2,P*3-2,3)THEN42:ELSEONPGOTO46,80,131,86,109,102,95,105,105,105
42 NEXT:GOTO45
43 FORP=1TO10:IFLEFT$(Q,3)<>MID$(Y3,P*3-2,3)THEN44:ELSEONPGOTO83,46,114,84,82,89,116,116,104,112
44 NEXT
45 PRINT"I cannot translate that.":GOTO33
46 IFA<6THEN7:ELSEU=MID$(Q,6,3):IFU="DIA"THENU="Diary.":GOSUB125:IFB=0THEN143:ELSEPRINTX:PRINT"It says Flagstone hates garlic breath.":GOTO33
47 IFU="PAP"THENU="Piece of paper.":GOSUB125:IFB=0THEN143:ELSEPRINTX:PRINT"It says:":PRINTY;"The first is 3.";Y:GOTO33
48 IFU="BOO"THENU="Book.":GOSUB125:IFB=0THEN143:ELSEPRINTX:PRINT"It's called ";Y;"Ghost Stories";Y;".":PRINT"There's something scribbled on the title page.":PRINT"It says: ";Y;"The third is 7";Y;".":GOTO33:ELSEPRINTX
49 IFL=6ANDU="SIG"THENPRINT"Sign says:":PRINT"   FLAGSTONE  MANOR":PRINT"ENTER AT YOUR OWN RISK":GOTO33:ELSEIFU="WIN"ORU="RAC"THENIFL=4ANDLW=0THENLW=1:U=W1:GOTO142
50 IFU="SUI"ORU="ARM"THENIFL=2PRINT"It's holding a sharp axe.":GOTO33
51 IFU="BOT"THENU=W1:GOSUB124:IFB=1THENIFDW=0PRINT"An excellent vintage.":GOTO33:ELSEPRINT"It's empty.":GOTO33
52 IFL=8ANDU="CLO"THENPRINT"It says";80-DM;"moves until sundown.":GOTO33:ELSEIFL=1ANDU="SHE"ANDLS=0THENLS=1:U="Book.":GOTO142:ELSEIFL=10ANDU="DOO"THENPRINT"It's got a combination lock.":GOTO33:ELSEIFL=17ANDU="WAR"THENPRINT"It's empty.":GOTO33
53 IFL=1ANDAA=1ANDU="OPE"THENPRINT"I see steps leading down.":GOTO33:ELSEIFL=5ANDU="PAI"THENIFCP=0PRINT"It's a portrait of old Arthur Flagstone.":PRINT"The eyes are watching me.":GOTO33:ELSEPRINT"It's cut to shreds.":GOTO33
54 IFL=7ANDU="MOU"ANDFM=0PRINT"Cute little fellow.":GOTO33:ELSEIFL=17ANDU="BED"THENPRINT"Looks a warm place to sleep.":GOTO33:ELSEIFL=15ANDU="CHE"ANDOH=0PRINT"It's locked.":GOTO33:ELSEIFL=16ANDU="SKE"PRINT"The head's chopped off.":GOTO33
55 IFU="CHE"THENU="Lump of cheese.":GOSUB124:IFB=1PRINT"Looks tasty.":GOTO33
56 IFL=9ANDLF=0THENIFU="FIR"ORU="WOO"ORU="PLA"THENPRINT"The fireplace is filled with dry wood.":GOTO33
57 IFL=3ANDU="CUP"ANDOA=0THENOA=1:U="Matches.":GOTO142:ELSEIFU="FLA"ORU="STO"THENIFL=6ANDIR<2PRINT"There's a crack down the middle.":GOTO33
58 IFL=11ANDU="DES"ANDLD=0THENLD=1:U="Ashtray.":GOTO142:ELSEIFL=12ANDBC=1ANDU="COB"THEN148:ELSEIFL=9ANDU="CHA"THENPRINT"It's broken.":GOTO33
59 IFU="CAN"THENU="Can.":GOSUB124:IFB=1THENIFOT=0PRINT"It's sealed.":PRINT"The label is missing.":GOTO33:ELSEPRINT"It's empty.":GOTO33
60 IFL=4ANDU="TAB"ANDLT=0THENLT=1:U="China dish.":GOTO142:ELSEIFU="GHO"ORU="FIG"THENIFL=10PRINT"It's Arthur Flagstone.":GOTO33
61 IFL=10ANDU="CHA"THENPRINT"It's bloodstained.":GOTO33:ELSEIFL=13ANDU="RIN"THENPRINT"It's attached to the wall.":GOTO33:ELSEPRINT"I see nothing special.":GOTO33
62 GOSUB126:FORP=1TO6:IFT(P)=UTHEN63:ELSENEXT:PRINT"I'm not carrying it.":GOTO33
63 IFU="Lump of cheese."ANDL=7THENU="MOU":GOTO119
64 FORPP=1TO10:IFR(L,PP)=""THEN65:ELSENEXT:PRINT"Sorry, there's too much here already.":GOTO33
65 IFU="Bottle of wine."ANDL=11THENPI=PI+1
66 IFASC(U)=42THENCT=0
67 PRINTX:T(P)="":R(L,PP)=U:GOSUB139:GOTO7
68 GOSUB126:FORP=1TO6:IFT(P)=UPRINT"I'm already carrying it.":GOTO33:ELSENEXT:FORP=1TO10:IFR(L,P)=UTHEN69:ELSENEXT:IFU="XXX"THEN144:ELSEPRINT"I don't see it here.":GOTO33
69 FORPP=1TO6:IFT(PP)=""THEN70:ELSENEXT:PRINT"I'm carrying too much.  Take INVENTORY.":GOTO33
70 IFU="Bottle of wine."ANDL=11THENPI=PI-1
71 IFASC(U)=42THENCT=1
72 PRINTX:T(PP)=U:R(L,P)="":GOSUB139:GOTO7
73 IFASC(Q)=71THENC=4:ELSEC=7
74 U=MID$(Q,C,3):IFU="HOL"ANDL=13ANDIR=2THENU="Ladder.":GOSUB125:IFB=0THEN143:ELSEES=1:PRINTX:PRINT"I use the ladder.":T(P)="":R(13,7)=U:GOSUB140:L=6:GOTO7
75 IFL=6ANDIR=2ANDU="HOL"THENL=13:GOTO7:ELSEIFU="NOR"THENQ="N":GOTO36:ELSEIFU="SOU"THENQ="S":GOTO36:ELSEIFU="EAS"THENQ="E":GOTO36:ELSEIFU="WES"THENQ="W":GOTO36:ELSEIFU="DOW"THENQ="D":GOTO36:ELSEIFU="UP"THENQ="U":GOTO36:ELSEIFU="DOO"THEN156
76 IFL=2ANDU="STA"THENL=14:GOTO7:ELSEIFL=1ANDAA=1ANDU="OPE"THENL=16:GOTO7:ELSEIFL=17ANDU="BED"THENL=18:GOTO7:ELSEIFU="MAN"ORU="HOL"THENIFL=17THENL=15:GOTO7
77 IFU="OPE"THENIFL=9ANDLF=1THENL=12:GOTO7:ELSEIFL=7ANDFM=1THENL=10:GOTO7
78 IFL=11ANDU="OPE"ANDPL=1THENL=7:GOTO7:ELSEIFU="SLE"THENQ=U:GOTO89
79 PRINT"I can't ";Y;Q;Y:GOTO33
80 IFK>0ANDOC=0PRINT"Open the door of time.":GOTO33:ELSEIFK>2ANDPI=2ANDPL=0PRINT"Press on.":GOTO33:ELSEIFOP=0ANDM>40PRINT"The keys are a pressing problem.":GOTO33
81 IFK>2ANDCP=0PRINT"Arthur Flagstone is hiding something.":GOTO33:ELSEPRINT"Look around.":GOTO33
82 PRINT"I've been here";M;"moves.":GOTO33
83 PRINT:PRINT:PRINT"The Adventure is over.":PRINT"You've been here";M;"moves.":INPUT"Want to try this Adventure again";Q:IFLEFT$(Q,1)="Y"THENRUN:ELSEIFLEFT$(Q,1)="N"THENNEW:ELSE83
84 INPUT"Ready cassette - Press ENTER";Q:PRINT"Saving data...":PRINT#-1,AA,BC,CP,CT,DD,DM,DO,DW,EC,EG,ES,FM,IR,K,L,LD,LF,LS,LW,M,MC,MD,OA,OC,OH,OP,PI,PL,W1,T(1),T(2),T(3),T(4),T(5),T(6),LT,OT,MB,BD:FORP=1TO17STEP2:A=P+1
85 PRINT#-1,R(P,1),R(P,2),R(P,3),R(P,4),R(P,5),R(P,6),R(P,7),R(P,8),R(P,9),R(P,10),R(A,1),R(A,2),R(A,3),R(A,4),R(A,5),R(A,6),R(A,7),R(A,8),R(A,9),R(A,10):NEXT:PRINT#-1,L:PRINT"Save completed.":GOTO33
86 U=MID$(Q,6,3):IFU="PAI"ANDL=5PRINT"It's stuck to the wall.":GOTO33:ELSEIFL=1ANDU="LAM"THENPRINT"It's glued to the shelf.":GOTO33:ELSEIFU="FLA"ORU="STO"THENIFL=6PRINT"It won't budge.":GOTO33
87 IFL=1ANDU="SHE"THENPRINT"They're too heavy.":GOTO33:ELSEIFU="WAL"ANDL=7THEN123:ELSEPRINTX:IFL=15ANDU="CHE"ANDMC=0THENMC=1:U="Rope.":GOTO142:ELSEIFL=12ANDU="COB"ANDBC=0THENBC=1:GOTO148:ELSEIFL=17ANDU="BED"ANDMB=0THENMB=1:U="Can opener.":GOTO142
88 PRINT"I found nothing special.":GOTO33
89 PRINTX:PRINT"Goodnight.":GOSUB140:IFL<>18GOSUB137:GOTO149:ELSEIFDO=1GOSUB137:PRINT"I forgot to lock the door!  ";:GOTO149:ELSEIFPI=1THENA=L:U=W1:L=11:GOSUB124:W1="Empty bottle.":R(L,P)=W1:L=A:PI=2:DW=1
90 DM=0:K=K+1:PRINT:PRINT"Good morning.":IFK=1PRINT"In the night I heard the clanking of chains.":GOTO33:ELSEIFK>1ANDPI=0PRINT"I had a dream.":PRINT"A voice said:":PRINTY;"Leave the wine in the study overnight";Y;".":GOTO33
91 PRINT"I had a quiet night.":GOTO33
92 U="Knife.":GOSUB125:IFB=0THEN143:ELSEU=MID$(Q,5,3):IFL=5ANDU="PAI"ANDCP=0THENCP=1:PRINTX:PRINT"I hear a horrible scream.":GOSUB140:U="Piece of paper.":GOTO142:ELSE145
93 U=MID$(Q,7,3):IFU=""THENPRINT"Drink what?":GOTO33:ELSEIFU="WIN"THENIFDW=1THEN146:ELSEU=W1:GOSUB125:IFB=0THEN143:ELSEPRINTX:DW=1:W1="Empty bottle.":T(P)=W1:PRINT"Hic!":PRINT"Derlishush.":GOTO33
94 PRINT"Are you kidding?":GOTO33
95 U=MID$(Q,6,3):IFL=8ANDU="CLO"ANDOC=0THENOC=1:PRINTX:U="Knife.":GOTO142:ELSEIFL=4ANDU="PAN"ANDOP=0PRINT"How?":GOTO33:ELSEIFL=11ANDPL=0ANDU="PAN"THENPRINT"How?":GOTO33:ELSEIFL=2ANDU="DOO"THENPRINT"I can't - I'm trapped.":GOTO33
96 IFU="CAN"THENIFOT=1THEN146:ELSEU="Can.":GOSUB125:IFB=0THEN143:ELSEU="Can opener.":GOSUB125:IFB=0THEN143:ELSEPRINTX:PRINT"Something falls out.":GOSUB139:U="Lump of cheese.":OT=1:GOTO142
97 IFL=10ANDU="DOO"THENIFMD=1THEN146:ELSEIFEG=0THEN153:ELSEINPUT"First number";Q:IFQ<>"3"THEN152:ELSEINPUT"Second number";Q:IFQ<>"8"THEN152:ELSEINPUT"Third number";Q:IFQ<>"7"THEN152:ELSE150
98 IFU<>"DOO"ANDU<>"CHE"THEN100:ELSEQ=U:U="Bunch of keys.":GOSUB125:IFB=0THEN143:ELSEIFQ="CHE"THENIFL=15ANDOH=0THENOH=1:PRINTX:R(15,1)="Open chest.":U="Diary.":GOTO142:ELSE100
99 IFL=17ORL=14ANDDO=0THENDO=1:R(17,1)="Open door.":R(14,1)=R(17,1):PRINTX:GOSUB139:GOTO7:ELSEIFL=8ANDDD=0THENDD=1:R(8,1)="Open door.":PRINTX:GOSUB139:GOTO7
100 PRINT"It's already open.":GOTO33
101 PRINT"Try LOCK":GOTO33
102 U=MID$(Q,6,3):IFU<>"DOO"THEN145:ELSEIFL<>14ANDL<>17PRINT"It won't help to lock THIS door.":GOTO33:ELSEIFDO=0PRINT"It's already locked.":GOTO33:ELSEU="Bunch of keys.":GOSUB125:IFB=0THEN143:ELSEPRINTX:R(17,1)="Locked door.":R(14,1)=R(17,1):DO=0
103 GOSUB139:GOTO7
104 IFMID$(Q,4,1)="O"THENPRINT"Try OPEN":GOTO33:ELSEU=MID$(Q,9,3):PRINT"It's not on fire.":GOTO33
105 IFLEFT$(Q,2)="PR"THENC=7:A=1:ELSEC=6
106 U=MID$(Q,C,3):PRINTX:IFL=4ANDA=1ANDU="PAN"ANDOP=0THENOP=1:PRINT"Panel slides open.":GOSUB140:R(4,1)="Open panel.":U="Bunch of keys.":GOTO142
107 IFL=11ANDU="PAN"ANDA=1ANDPI=2ANDPL=0THENPL=1:PRINT"Panel slides open.":GOSUB140:R(11,1)="Open panel.":U="Opening.":GOTO142:ELSEIFMID$(Q,3,1)="L"THENIFU="ROP"ORU="RIN"THEN159
108 PRINT"Nothing happens.":GOTO33
109 IFASC(Q)=66PRINT"Try LIGHT":GOTO33:ELSEIFMID$(Q,7,3)="LAM"THENPRINT"There's no electricity.":GOTO33:ELSEU="Matches.":GOSUB125:IFB=0THEN143:ELSEU=MID$(Q,7,3)
110 IFU="FIR"ORU="WOO"THENIFL=9ANDLF=0THENLF=1:PRINTX:PRINT"The wood disappears in a puff of smoke.":GOSUB141:PRINT"The back of the fireplace moves aside.":GOSUB141:U="Opening.":R(9,2)="":GOTO142
111 IFU="MAT"THENPRINTX:PRINT"Match flares up...":GOSUB139:PRINT"and goes out.":GOTO33:ELSEPRINT"It won't burn.":GOTO33
112 IFMID$(Q,5,3)<>"ROP"THEN145:ELSEIFL<>13PRINT"I can't do that HERE.":GOTO33:ELSEIFIR=1THEN146:ELSEU="Rope.":GOSUB125:IFB=0THEN143:ELSET(P)="":PRINTX:PRINT"I tie one end to the ring,":PRINT"and throw the other end through the door."
113 R(13,10)="Trail of rope.":R(10,10)="Rope end.":IR=1:GOSUB141:GOTO7
114 U=MID$(Q,7,3):IFU="DOO"THENPRINT"It's too solid.":GOTO33:ELSEIFL=12ANDU="COB"THENPRINT"Try MOVE":GOTO33:ELSEIFU="PAN"THENIFL=4ORL=11PRINT"It's too solid.":GOTO33
115 GOTO145
116 IFLEFT$(Q,2)="TW"THENC=7:ELSEC=6
117 U=MID$(Q,C,3):PRINTX:IFL=1ANDU="LAM"ANDAA=0THENAA=1:PRINT"C L I C K":PRINT"The shelves move aside.":GOSUB140:U="Opening.":GOTO142:ELSEPRINT"Nothing happens.":GOTO33
118 U=MID$(Q,6,3):IFU="MOU"ANDFM=1THEN146
119 IFU="MOU"ANDL=7THENU="Lump of cheese.":GOSUB125:IFB=0THEN143:ELSEFM=1:T(P)="":PRINTX:PRINT"The mouse grabs the cheese":PRINT"and disappears into a small hole.":GOSUB141:PRINT"I hear a click.":GOSUB141:PRINT"The wall moves aside.":GOSUB140:GOTO151
120 PRINT"It's not hungry.":GOTO33
121 U=MID$(Q,5,3):IFU=""THENPRINT"Eat what?":GOTO33:ELSEIFU="CHE"THENIFEC=1THEN146:ELSEU="Lump of cheese.":GOSUB125:IFB=0THEN143:ELSET(P)="":EC=1:PRINTX:GOTO33
122 IFU="GAR"THENIFEG=1THEN146:ELSEU="Garlic.":GOSUB125:IFB=0THEN143:ELSEEG=1:PRINTX:T(P)="":PRINT"Horrible.":GOTO33
123 PRINT"Are you kidding?":GOTO33
124 B=0:FORP=1TO10:IFR(L,P)=UTHENB=1:RETURN:ELSENEXT
125 B=0:FORP=1TO6:IFT(P)=UTHENB=1:RETURN:ELSENEXT:RETURN
126 IFASC(Q)=71THENC=5:ELSEC=6
127 U=MID$(Q,C,3):IFL=2ANDC=5ANDU="AXE"THENPRINTX:GOSUB139:GOSUB137:PRINT"Suit of armour comes to life...  and CUTS me in HALF!":GOTO138:ELSEIFC=5ANDL=10ANDU="CHA"THENPRINT"Ghost won't let me.":GOTO33:ELSEIFU="KNI"THENU="Knife.":RETURN
128 IFU="CHE"THENU="Lump of cheese.":RETURN:ELSEIFU="BOT"THENU=W1:RETURN:ELSEIFU="WIN"ANDW1="Bottle of wine."THENU=W1:RETURN:ELSEIFU="PAP"THENU="Piece of paper.":RETURN:ELSEIFU="ASH"ORU="TRA"THENU="Ashtray.":RETURN:ELSEIFU="MAT"THENU="Matches.":RETURN
129 IFU="BOO"THENU="Book.":RETURN:ELSEIFU="GAR"THENU="Garlic.":RETURN:ELSEIFU="KEY"THENU="Bunch of keys.":RETURN:ELSEIFU="DIA"THENU="Diary.":RETURN:ELSEIFU="GOL"ORU="BAR"THENU="*GOLD BARS*":RETURN:ELSEIFU="LAD"THENU="Ladder.":RETURN
130 IFU="ROP"THENU="Rope.":RETURN:ELSEIFU="DIS"THENU="China dish.":RETURN:ELSEIFU="OPE"THENU="Can opener.":RETURN:ELSEIFU="CAN"THENU="Can.":RETURN:ELSEU="XXX":RETURN
131 PRINT"I am carrying the following:":N=0:FORP=1TO6:IFT(P)=""THEN133:ELSEN=N+1:IFN=5PRINT
132 PRINTT(P);V;
133 NEXT:IFN=0PRINT"Nothing at all.";
134 PRINT:GOTO33
135 CLS:INPUT"Ready cassette - Press ENTER";Q:PRINT"Loading data...":INPUT#-1,AA,BC,CP,CT,DD,DM,DO,DW,EC,EG,ES,FM,IR,K,L,LD,LF,LS,LW,M,MC,MD,OA,OC,OH,OP,PI,PL,W1,T(1),T(2),T(3),T(4),T(5),T(6),LT,OT,MB,BD:FORP=1TO17STEP2:A=P+1:PRINT@60,P;
136 INPUT#-1,R(P,1),R(P,2),R(P,3),R(P,4),R(P,5),R(P,6),R(P,7),R(P,8),R(P,9),R(P,10),R(A,1),R(A,2),R(A,3),R(A,4),R(A,5),R(A,6),R(A,7),R(A,8),R(A,9),R(A,10):NEXT:GOTO7
137 CLS:PRINT"I am in a LOT of TROUBLE!":PRINT:PRINTZ:RETURN
138 PRINT"I am DEAD!":GOTO83
139 FORP=1TO500:NEXT:RETURN
140 FORP=1TO1000:NEXT:RETURN
141 FORP=1TO1800:NEXT:RETURN
142 PRINT"I see something.":GOSUB139:FORP=1TO10:IFR(L,P)=""THENR(L,P)=U:GOTO7:ELSENEXT:GOTO7
143 PRINT"I can't do that... yet.":GOTO33
144 PRINT"It's beyond my power to do that.":GOTO33
145 PRINT"It won't help to do that.":GOTO33
146 PRINT"I already have.":GOTO33
147 PRINT"It's locked.":GOTO33
148 PRINT"There's writing on the wall behind the cobwebs.":PRINT"It says: ";Y;"The second is 8";Y;".":GOTO33
149 PRINT"Something came in the night...":PRINT"and STRANGLED me with a rusty chain.":GOTO138
150 MD=1:PRINT"The door slides open.":R(10,1)="Open door.":GOSUB140:GOTO7
151 R(7,2)="":U="Opening.":GOTO142
152 PRINT"That's wrong!":GOSUB139:GOSUB137:GOTO155
153 GOSUB137:PRINT"The ghost materialises...  and CHOKES me with the chain.":GOTO138
154 GOSUB137:PRINT"It's DARK!":GOTO155
155 PRINT"The suit of armour appears... and CHOPS my HEAD off!":GOTO138
156 IFL=6THENIFIR=0PRINTX:PRINT"The door slams shut behind me.":GOSUB140:L=2:GOTO7:ELSEPRINT"It won't open.":GOTO33:ELSEIFL=14THENIFDO=0THEN147:ELSEL=17:GOTO7
157 IFL=17THENIFDO=0THEN147:ELSEL=14:GOTO7:ELSEIFL=8THENIFDD=0THEN147:ELSEL=11:GOTO7
158 IFL=10THENIFMD=0THEN147:ELSEL=13:GOTO7:ELSE79
159 IFIR<2ANDL=13GOSUB139:GOSUB137:PRINT"A large stone crashes from the roof...  and CLOBBERS me!":GOTO138:ELSEIFIR=1PRINT"CRAAA..SSH!":R(13,9)="Hole in roof.":R(13,8)="Pieces of stone.":R(6,3)="Hole.":R(6,1)="Closed door.":IR=2:GOTO33:ELSE108
160 IFCT=0PRINT"You've escaped from the house,":PRINT"but you left the gold behind.":GOTO34:ELSEPRINT"CONGRATULATIONS!":PRINT"You've escaped with the gold.   You've solved it all.":GOTO83
161 DATAT,H,E," ",S,E,C,R,E,T," ",O,F," ",F,L,A,G,S,T,O,N,E," ",M,A,N,O,R,E,,N,E,W,W,,E,,N,S,W,,S,,S,E,,W,,S,,S,,S,,S,,D,,D,,U,,,W,:DATAShelves.,Lamp.,,Closed door.,Staircase.,Suit of armour.,,Cupboard.,Can.,,Small panel.,Table.,Wine rack.,
162 DATAPainting.,,Open door.,Sign.,Flagstone.,,Brick wall.,Startled mouse.,,Locked door.,Grandfather clock.,,Fireplace.,Firewood.,Armchair.,,Metal door.,Ghostly figure.,Rusty chain.,
163 DATALarge panel.,Desk.,,Cobwebs.,Ladder.,,*GOLD BARS*,Iron ring.,,Locked door.,,Old chest.,,Skeleton.,Garlic.,,Open door.,Manhole.,Wardrobe.,Bed.,,Pillow.,Mattress.,
