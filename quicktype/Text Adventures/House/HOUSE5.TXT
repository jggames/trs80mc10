0 CLEAR500:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO30
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

30 CLS:PRINT@32*7+8,"HOUSE ADVENTURE":PRINT:PRINT" REMEMBER THE IMPOSTER IS LAST"
31 Z=0:A=0:L=0:U=0:TN=0:T=0:DK=0:I=0:MI=0:S=0:SP=0
32 DIMO$(26),H$(6),R$(3,9),W$(3),R(5,26,3)
40 O$="GOLKEYCANGLOBOOKNIBOXDIMFLADIAPILPARHAICOIBANGARBATICESHOVAS":H$="BLOVAMBEALEOMONWER"
41 C$="GETTAKDROSAYKILSTALIGPLAREADIGINVQUION OFFBRUWAVUNLOPESPRSAVLOAUDNEWS"
42 FORX=0TO26:READO$(X):NEXTX:FORX=0TO6:READH$(X):NEXTX
50 FORX=0TO3:FORY=0TO9:READR$(X,Y):NEXTY,X:FORX=0TO3:READW$(X),D$(X):NEXTX
51 FORX=0TO3:FORY=0TO9:FORZ=0TO3:READA:R(X,Y,Z)=A-1:NEXTZ,Y,X
52 FORX=0TO5:FORY=0TO1:READA:R(5,X,Y)=A-1:NEXTY,X
53 FORX=0TO26:FORY=0TO1:READA:R(4,X,Y)=A-1:NEXTY,X
60 FORX=0TO1:READR(5,10,X):READR(5,11,X):NEXTX:Z=1:A=1:R(5,12,0)=INT(RND(0)*4):R(5,12,1)=INT(RND(0)*4)
61 MI=INT(RND(0)*19):R(5,6,0)=INT(RND(0)*4):R(5,6,1)=INT(RND(0)*9)

70 N$=STR$(MI):H$(6)=N$:FORX=1TOLEN(N$):H$(6)=H$(6)+CHR$(8):NEXTX:H$(6)=H$(6)+O$(MI)
80 IFR(4,7,0)=99ANDA=8THENZ=INT(RND(0)*4)
90 CLS:PRINT:M$="YOU ARE "+R$(Z,A):GOSUB1
91 IFINT(RND(0)*50)<10THEN95
92 GOTO100
95 FORX=0TO19:IFR(4,X,0)=1ANDR(4,X,1)=0THENR(4,X,0)=-1:R(4,X,1)=-1
96 NEXTX
100 IFINT(RND(0)*50)<40THEN130
101 FORX=0TO5
110 IFR(5,X,0)=-1THENR(5,X,0)=INT(RND(0)*4):R(5,X,1)=1+INT(RND(0)*8):GOTO120
111 IFR(5,X,0)<-1THENR(5,X,0)=R(5,X,0)+1:R(5,X,1)=R(5,X,1)+1
120 NEXTX
130 PRINT"I SEE:";:IFZ=0ANDL=0THEN135
131 GOTO140
135 PRINT"  NOTHING--IT'S TOO DARK!":DK=DK+1
136 IFDK>5+RND(3)THENPRINT:PRINT"AUGHHHH...":PRINT"BEWARE OF THINGS UNSEEN!!":GOTO830
137 GOTO170
140 FORX=0TO26:IFR(4,X,0)=ZANDR(4,X,1)=ATHENPRINTTAB(7);O$(X)
150 NEXTX:FORX=0TO6:IFR(5,X,0)=ZANDR(5,X,1)=ATHENPRINTTAB(7);H$(X)

160 NEXTX:PRINT:PRINT"OBVIOUS EXITS ARE:";:FORX=0TO3:REM PRINTR(Z,A,X)+1;
161 IFR(Z,A,X)=-1ORR(Z,A,X)<>INT(R(Z,A,X))THEN163
162 PRINTTAB(19);D$(X)
163 NEXTX

170 IFT=20THEN1260
171 TN=TN+1:B$="":PRINT:INPUT"WHAT TO DO";A$:A$=A$+" "
172 IFLEFT$(A$,3)="LOO"THEN80
173 GOSUB220:IFX=0THENPRINT"I DON'T UNDERSTAND":GOTO170
180 ONXGOTO270,270,360,450,520,580,600,620,670,740,800,830,840,880,920,980,1050,1110,1130,1200,1230,250,260
181 IFL=1THENU=U+1:IFU>60THENL=0
190 X=X-24:IFR(Z,A,X)=-1ORR(Z,A,X)<>INT(R(Z,A,X))THEN210
200 A=R(Z,A,X):GOTO80
210 PRINT"YOU CAN'T GO THAT WAY!":GOTO170

220 FORX=1TOLEN(A$):IFMID$(A$,X,1)=" "THENB$=RIGHT$(A$,LEN(A$)-X):ZZ=X:X=255:NEXTX:X=ZZ:GOTO230
221 NEXTX
230 FORY=1TO63STEP3:IFLEFT$(A$,3)=MID$(C$,Y,3)THENX=(Y+2)/3:Y=63:NEXTY:RETURN
240 NEXTY:FORY=64TO70
241 IFLEFT$(A$,1)=MID$(C$,Y,1)THENX=Y-42:Y=70:NEXTY:RETURN
242 NEXTY:X=0:RETURN

250 IFZ=3ORA<>9THEN210
251 Z=Z+1:GOTO90

260 IFZ=0ORA<>9THEN210
261 Z=Z-1:GOTO90

270 IFB$=""THENINPUT"TAKE WHAT";B$:B$=B$+" "
280 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN290
281 NEXTX:M$="YOU CAN'T TAKE THE "+B$:GOSUB1:GOTO170
290 IFR(4,X,0)<>ZORR(4,X,1)<>ATHEN295
291 GOTO300
295 IFVAL(H$(6))=XANDR(5,6,0)=ZANDR(5,6,1)=ATHENPRINT:PRINT"AUGHHHH...":PRINT"REMEMBER?!":PRINT:GOTO830
296 M$="I SEE NO "+B$+"HERE":GOSUB1:GOTO170
300 FORQ=0TO5:IFR(5,Q,0)=ZANDR(5,Q,1)=ATHENZZ=Q:Q=5:NEXTQ:Q=ZZ:GOTO305
301 NEXTQ:GOTO310
305 M$="THIS ROOM'S OCCUPANT SEEMS TO HAVE GROWN VERY ATTACHED TO THE "+B$+"AND WON'T LET YOU HAVE IT."
306 GOSUB1:GOTO170
310 IFX=17ANDR(4,3,0)<>99THENM$="OUCH!! THAT HURTS!!-- YOU CAN'T PICK THAT UP!":GOSUB1:GOTO170
320 IFX=13ANDR(4,6,0)<>99THENM$="DON'T BE SILLY! YOU CAN'T CARRY THAT MANY COINS!":GOSUB1:GOTO170
330 IFI>3THENM$="YOU CAN'T CARRY THAT MUCH. YOU'LL HAVE TO DROP SOMETHING.":GOSUB1:GOTO170
331 IFR(4,X,0)=1ANDR(4,X,1)=0THENT=T-1
340 R(4,X,0)=99:R(4,X,1)=99:I=I+1:M$=B$+"TAKEN.":GOSUB1:GOTO170

360 IFB$=""THENINPUT"DROP WHAT";B$:B$=B$+" "
370 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN380
371 NEXTX:M$="YOU CAN'T DROP THE "+B$:GOSUB1:GOTO170
380 IFR(4,X,0)<>99THENM$="YOU HAVE NO "+B$+"TO DROP.":GOSUB1:GOTO170
390 IFX=8ORX=16THENL=0
400 IFX=6ANDR(4,13,0)=99THENM$="TRY DROPPING THE COINS FIRST!":GOSUB1:GOTO170
410 IFX=3ANDR(4,17,0)=99THENM$="YOU'LL BURN YOURSELF IF YOU DROP THE GLOVE NOW!":GOSUB1:GOTO170
420 IFX=19AND(R(4,10,0)<>ZORR(4,10,1)<>A)THENM$="YOU CAN'T DROP THAT-- IT'LL BREAK.":GOSUB1:GOTO170
430 R(4,X,0)=Z:R(4,X,1)=A:I=I-1:M$=B$+"DROPPED.":GOSUB1:IFZ=1ANDA=0THENT=T+1
440 GOTO170

450 IFB$=""THENINPUT"SAY WHAT";B$:B$=B$+" "
460 FORX=0TO3:IFW$(X)=B$THENZZ=X:X=3:NEXTX:X=ZZ:GOTO480
461 NEXTX
470 PRINT"NOTHING HAPPENED.":GOTO170
480 FORQ=0TO1:IFZ=R(5,10,Q)ANDA=R(5,11,Q)THEN485
481 NEXTQ:GOTO470
485 IFX=R(5,12,Q)THEN500
486 Z=INT(RND(0)*4):A=INT(RND(0)*9):M$="YOU EXPERIENCE DISORIENTATION.":GOSUB1:Q=1:NEXTQ:GOTO170
500 PRINT"THE AIR SHIMMERS...":FORQ=0TO19:IFR(4,Q,0)=ZANDINT(R(4,Q,1))=ATHENR(4,Q,1)=INT(R(4,Q,1)):Q=19
510 NEXTQ:GOTO170

520 IFB$=""THENINPUT"KILL WHAT";B$:B$=B$+" "
530 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THENZZ=X:X=5:NEXTX:X=ZZ:GOTO540
531 NEXTX:PRINT"YOU CAN'T KILL THAT!":GOTO170
540 IFR(5,X,0)<>ZORR(5,X,1)<>ATHENM$="I SEE NO "+B$+"HERE!":GOSUB1:GOTO170
550 IFR(4,5,0)<>99THENM$="YOU HAVE NOTHING WITH WHICH TO KILL THE "+B$:GOSUB1:GOTO170
560 IFX<>4THENM$="THE "+B$+"WILL BECOME ANGRY IF YOU PERSIST!":GOSUB1:GOTO170
570 R(5,4,0)=-5:R(5,4,1)=-5:M$="THE MONK HAS BECOME FRIGHTENED AND RUN AWAY.":GOSUB1:GOTO170

580 IFB$=""THENINPUT"STAB WHAT";B$:B$=B$+" "
590 GOTO530

600 IFB$=""THEN605
601 GOTO610
605 IFL=0THEN840
606 GOTO880
610 IFB$="ON "THEN840
611 GOTO880

620 IFB$=""THENINPUT"PLAY WHAT";B$:B$=B$+" "
630 IFLEFT$(B$,3)<>"BAN"THENPRINT"YOU CAN'T PLAY THAT!":GOTO170
640 IFR(4,14,0)<>99THENPRINT"YOU HAVE NO BANJO TO PLAY":GOTO170
650 FORX=0TO50:SOUNDRND(200),1:NEXTX:IFR(5,2,0)=ZANDR(5,2,1)=ATHEN655
651 GOTO660
655 R(5,2,0)=-5:R(5,2,1)=-5
656 M$="MUSIC HATH CHARM TO SOOTH THE SAVAGE BEAST... THE BEAST HAS WANDERED OFF IN A STATE OF BLISS!":GOSUB1
660 GOTO170

670 IFB$=""THENINPUT"READ WHAT";B$:B$=B$+" "
680 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN690
681 NEXTX:PRINT"YOU CAN'T READ THAT!":GOTO170
690 IFR(4,X,0)<>99THENM$="YOU DON'T HAVE A "+B$+"TO READ.":GOSUB1:GOTO170
700 IFX=4THEN705
701 GOTO720

705 PRINT"THE WRITING IS BLURRY--IT READS:":PRINTTAB(11);"MAGIC WORDS"
706 PRINT:PRINT"  TO MAKE OBJECTS...":PRINT"  ONE OF THE FOLLOWING."
707 PRINT:FORY=0TO3:PRINTTAB(10);W$(Y):NEXTY:PRINT
710 PRINTTAB(2);"NOTE: BE SURE TO USE THE":PRINTTAB(8);"RIGHT WORD IN THE...":GOTO170
720 IFX=11THEN725
721 PRINT"YOU CAN'T READ THAT!":GOTO170
725 M$="THE PARCHMENT IS TORN-- IT READS: ... IS THE PLACE TO USE THEM:":GOSUB1
726 PRINTTAB(7);R$(1,8):PRINTTAB(7);R$(3,0):PRINTTAB(7);R$(1,4):GOTO170

740 IFB$=""THENINPUT"DIG WHAT";B$:B$=B$+" "
750 IFLEFT$(B$,3)<>"DIR"THENPRINT"YOU CAN'T DIG THE ";B$:GOTO170
760 IFZ<>0ORA<>0THENPRINT"I SEE NO DIRT TO DIG HERE!":GOTO170
770 IFR(4,18,0)<>99THENM$="YOU DON'T HAVE ANYTHING TO DIG WITH.":GOSUB1:GOTO170
780 PRINT"DIGGING...":FORX=0TO19:IFR(4,X,0)=ZAND(INT(R(4,X,1))=AANDR(4,X,1)<>INT(R(4,X,1)))THEN790
781 NEXTX:GOTO170
790 R(4,X,1)=INT(R(4,X,1)):PRINT"I FOUND SOMETHING!!":X=19:GOTO781

800 PRINT"YOU ARE PRESENTLY CARRYING:":IFI=0THENPRINTTAB(7);"NOTHING":GOTO170
810 FORX=0TO19:IFR(4,X,0)=99THENPRINTTAB(7);O$(X)
820 NEXTX:GOTO170

830 PRINT:M$="YOU GOT"+STR$(T)+" ITEMS OUT OF THE HOUSE IN"+STR$(TN)+" MOVES":GOSUB1:END

840 IFR(4,8,0)<>99THENPRINT"YOU HAVE NO LIGHT TO TURN ON!":GOTO170
850 IFR(4,16,0)<>99THENPRINT"IT DOESN'T WORK":GOTO170
860 IFU>60THENPRINT"THE BATTERIES ARE EXHAUSTED.":GOTO170
870 L=1:DK=0:PRINT"LIGHT ON":GOTO170

880 IFR(4,8,0)<>99THENPRINT"YOU HAVE NO LIGHT TO TURN OFF!":GOTO170
890 IFR(4,16,0)<>99THENM$="IT DOESN'T WORK ANYWAY-- SO WHY TURN IT OFF!!":GOSUB1:GOTO170
900 IFU>60THENPRINT"THE BATTERIES ARE ALREADY DEAD!":GOTO170
910 L=0:PRINT"LIGHT OFF":GOTO170

920 IFB$=""THENINPUT"BRUSH WHAT";B$:B$=B$+" "
930 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THEN940
931 NEXTX:PRINT"YOU CAN'T BRUSH THAT!":GOTO170
940 IFR(5,X,0)<>ZORR(5,X,1)<>ATHENM$="I SEE NO "+B$+"HERE!":GOSUB1:GOTO170
950 IFR(4,12,0)<>99THENM$="YOU HAVE NOTHING WITH WHICH TO BRUSH THE "+B$:GOSUB1:GOTO170
960 IFX<>3THENM$="THE "+B$+"WILL BECOME ANGRY IF YOU PERSIST!!":GOSUB1:GOTO170
970 R(5,3,0)=-5:R(5,3,1)=-5:PRINT"PURRRRRRRRR!!!!!!!!!"
971 M$="THE LEOPARD IS VERY GRATIFIED FOR THE GROOMING, AND LEAVES.":GOSUB1:GOTO170

980 IFB$=""THENINPUT"WAVE WHAT";B$:B$=B$+" "
990 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THENZZ=X:X=19:NEXTX:X=ZZ:GOTO1000
991 NEXTX:PRINT"YOU CAN'T WAVE THAT!":GOTO170
1000 IFR(4,X,0)<>99THENM$="YOU HAVE NO "+B$+"TO WAVE!":GOSUB1:GOTO170
1010 IFX=8ORX=15THEN1020
1011 M$="THAT WAS FUN!!! BUT IT HAD NO EFFECT.":GOSUB1:GOTO170
1020 IFX=8ANDR(5,1,0)=ZANDR(5,1,1)=AANDL=1THEN1025
1021 GOTO1030
1025 M$="THE VAMPIRE COVERED ITS EYES AND FLEW AWAY!":GOSUB1:R(5,1,0)=-5:R(5,1,1)=-5:GOTO170
1030 IFX=15ANDR(5,5,0)=ZANDR(5,5,1)=ATHEN1035
1031 M$="WAVING THE "+B$+"HAS NO EFFECT.":GOSUB1:GOTO170
1035 R(5,5,0)=-5:R(5,5,1)=-5:M$="THE WEREWOLF HOWLED AND RAN AWAY IN TERROR!":GOSUB1:GOTO170

1050 IFB$=""THENINPUT"UNLOCK WHAT";B$:B$=B$+" "
1060 IFLEFT$(B$,3)<>"DOO"ANDLEFT$(B$,3)<>"DRA"THEN1065
1061 GOTO1070
1065 M$="I'M SORRY, BUT I ONLY KNOW HOW TO UNLOCK DOORS AND DRAWERS.":GOSUB1:GOTO170
1070 IFLEFT$(B$,3)="DRA"THENM$="SHOW ME A DRAWER AND I'LL OPEN IT!":GOSUB1:GOTO170
1080 IFZ<>1ORA<>1ORR(4,20,0)=-1THENM$="I SEE NO DOOR THAT NEEDS OPENING!":GOSUB1:GOTO170
1090 IFR(4,1,0)<>99THENPRINT"I NEED SOMETHING FIRST!":GOTO170
1100 R(4,20,0)=-1:R(4,20,1)=-1:R(1,1,3)=INT(R(1,1,3)):PRINTTAB(7);"<<<<C-L-I-C-K>>>>"
1101 PRINT"OK, IT'S OPEN NOW":GOTO170
1110 IFB$=""THENINPUT"OPEN WHAT";B$:B$=B$+" "
1120 GOTO1050

1130 IFB$=""THENINPUT"SPRAY WHAT";B$:B$=B$+" "
1140 IFR(4,2,0)<>99THENM$="YOU HAVE NOTHING TO SPRAY THE "+B$+"WITH!":GOSUB1:GOTO170
1150 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THEN1160
1151 NEXTX:PRINT"YOU CAN'T SPRAY THAT!":GOTO170
1160 IFR(5,X,0)<>ZORR(5,X,1)<>ATHENM$="I SEE NO "+B$+"HERE.":GOSUB1:GOTO170
1170 IFSP>1THENPRINT"THE CAN IS EMPTY.":GOTO170
1180 IFX<>0THENSP=SP+1:M$="THE "+B$+"WILL BECOME ANGRY IF YOU CONTINUE TO ACT THIS WAY.":GOSUB1:GOTO170
1190 R(5,0,0)=-5:R(5,0,1)=-5
1191 M$="THROUGH THE SPRAY'S MIST, YOU CAN SEE THE BLOB DISAPPEAR DOWN A CREVICE IN THE FLOOR":GOSUB1
1192 GOTO170

1200 R(5,13,0)=Z:R(5,14,0)=A:R(5,15,0)=L:R(5,16,0)=U:R(5,17,0)=TN
1201 R(5,18,0)=T:R(5,19,0)=DK:R(5,20,0)=I:R(5,21,0)=MI:R(5,22,0)=S
1210 CSAVE*R,"HOUSEDAT"
1220 M$="THE GAME HAS BEEN SAVED UNDER UNDER FILE-NAME 'HOUSEDAT'":GOSUB1:END

1230 PRINT"PLAY 'HOUSEDAT' FROM CASSETTE":CLOAD*R,"HOUSEDAT"
1240 Z=R(5,13,0):A=R(5,14,0):L=R(5,15,0):U=R(5,16,0):TN=R(5,17,0)
1241 T=R(5,18,0):DK=R(5,19,0):I=R(5,20,0):MI=R(5,21,0):S=R(5,22,0)
1250 GOTO70

1260 SOUND100,2:FORX=0TO50:NEXT:SOUND100,2:FORX=0TO50:NEXT:SOUND150,6
1261 CLS:M$="CONGRATULATIONS-- YOU HAVE SUCCESSFULLY COMPLETED HOUSE ADVENTURE!":GOSUB1
1262 M$="YOU REMOVED ALL 20 OBJECTS IN"+STR$(TN)+" MOVES.":GOSUB1:END

1270 DATA A BAG OF GOLD,A RUSTED KEY,A CAN OF BUG SPRAY,AN OLD LEATHER GLOVE
1271 DATA A SORCERER'S HANDBOOK,A CARVING KNIFE,A WOODEN BOX,AN ALUMINUM DIME
1272 DATA A FLASHLIGHT,A SMALL DIAMOND,A SILK PILLOW,A WRINKLED PARCHMENT
1280 DATA A HAIRBRUSH,100'S OF GOLD COINS,A BANJO,A CLOVE OF GARLIC
1281 DATA A SET OF BATTERIES,A BLOCK OF DRY ICE,A SHOVEL,A MING VASE,A LOCKED DOOR
1282 DATA THE FRONT YARD,A KING SIZED BED,A BRASS BATHTUB,A SET OF STOCKS,A DUSTY MOOSE HEAD
1283 DATA A UNITRON 30/50 MAINFRAME

1290 DATA A PROTOPLASMIC BLOB,A VAMPIRE,A SAVAGE BEAST,A LEOPARD,AN INSANE MONK,A WEREWOLF,

1300 DATA IN A DIRT-FLOORED ROOM,IN THE LABORATORY,IN THE PUMPROOM,IN THE FURNACE ROOM
1301 DATA IN A DUSTY COAL BIN,IN THE TORTURE CHAMBER,IN THE WORKSHOP,IN A WALK-IN FREEZER
1302 DATA IN A TELEPHONE BOOTH,IN THE BASEMENT ELEVATOR
1310 DATA ON THE FRONT PORCH,IN THE FOYER,IN A BEDROOM,IN A COAT CLOSET,IN THE DINING ROOM
1311 DATA IN THE PANTRY,IN THE KITCHEN,IN THE FAMILY ROOM,IN A TELEPHONE BOOTH
1312 DATA IN THE FIRST FLOOR ELEVATOR
1320 DATA IN THE SEWING ROOM,IN A CLOSET,IN THE MASTER BEDROOM,IN A GUEST ROOM
1321 DATA IN A BATHROOM,IN A GUEST ROOM,IN A SITTING ROOM,IN THE DEN,IN A TELEPHONE BOOTH
1322 DATA IN THE SECOND FLOOR ELEVATOR
1330 DATA IN THE LIVING ROOM,IN THE LIBRARY,IN THE TROPHY ROOM,IN THE BARROOM,IN THE COMPUTER-ROOM
1331 DATA IN THE GAME ROOM,IN A BEDROOM,IN THE ART HALL,IN A TELEPHONE BOOTH,IN THE THIRD FLOOR ELEVATOR
1340 DATA ABRACADABRA ,NORTH,SHAZAAM ,EAST,SEERSUCKER ,WEST,UGABOOM ,SOUTH

1341 DATA 0,0,8,0, 0,3,0,0, 0,0,2,4, 3,5,0,10, 0,7,4,0, 0,0,0,7, 6,8,5,0, 0,1,7,0, 10,0,0,0, 4,0,0,9
1342 DATA 2,0,0,0, 0,8,4,1.5, 0,6,0,8, 0,2,0,0, 8,10,0,0, 9,0,3,7, 6,0,0,10, 3,0,2,5, 0,0,0,6, 7,0,5,0
1350 DATA 0,10,0,2, 1,0,0,4, 10,0,0,5, 2,5,0,0, 3,6,4,7, 0,0,5,0, 5,0,0,8, 7,0,9,0, 0,8,0,0, 0,0,1,3
1351 DATA 10,3,6,0, 0,0,10,3, 2,0,1,0, 0,6,9,0, 0,0,0,6, 5,1,4,8, 0,8,0,0, 6,0,7,0, 0,4,0,0, 0,2,0,1
1352 DATA 1,8, 2,7, 1,3, 4,2, 3,4, 4,8
1360 DATA 1,6, 1,1.5, 1,3, 1,9.5, 4,2, 2,7, 2,2, 2,5.5, 2,4, 3,2, 4,8, 1,8, 3,7, 3,9, 3,8, 1,1.5
1361 DATA 4,3, 4,4, 4,5, 3,4, 2,2, 2,1, 3,3, 3,5, 1,6, 3,8, 4,5
1362 DATA 0,8,1,4

2010 REM HOUSE.BA
2020 REM AUTHOR UNKNOWN
2030 REM CLUB 100 LIBRARY 
