0 'HOUSE.BA - AUTHOR UNKNOWN
1 'CLUB 100 LIBRARY - 415/939-1246 BBS,    937-5039 NEWSLETTER, 932-8856 VOICE
2 CLS:S=VAL(RIGHT$(TIME$,2)):FORI=1TOS:T=RND(1):NEXTI:PRINT@52,"House Adventure":PRINT:PRINT"    Remember the Imposter is last":Z=0:A=0:L=0:U=0:TN=0:T=0:DK=0:I=0:MI=0:S=0:SP=0:DIMO$(26),H$(6),R$(3,9),W$(3),O(26,1),R(3,9,3),H(6,1),Z1(1),A1(1),P(1)
3 O$="GOLKEYCANGLOBOOKNIBOXDIMFLADIAPILPARHAICOIBANGARBATICESHOVAS":H$="BLOVAMBEALEOMONWER":C$="GETTAKDROSAYKILSTALIGPLAREADIGINVQUION OFFBRUWAVUNLOPESPRSAVLOAUDNEWS":FORX=0TO26:READO$(X):NEXTX:FORX=0TO6:READH$(X):NEXTX
4 FORX=0TO3:FORY=0TO9:READR$(X,Y):NEXTY,X:FORX=0TO3:READW$(X),D$(X):NEXTX:FORX=0TO3:FORY=0TO9:FORZ=0TO3:READA:R(X,Y,Z)=A-1:NEXTZ,Y,X:FORX=0TO5:FORY=0TO1:READA:H(X,Y)=A-1:NEXTY,X:FORX=0TO26:FORY=0TO1:READA:O(X,Y)=A-1:NEXTY,X
5 FORX=0TO1:READZ1(X):READA1(X):NEXTX:Z=1:A=1:P(0)=RND(1)*4-1:P(1)=RND(1)*4-1:MI=RND(1)*19-1:H(6,0)=RND(1)*4-1:H(6,1)=RND(1)*9
6 N$=STR$(MI):H$(6)=N$:FORX=1TOLEN(N$):H$(6)=H$(6)+CHR$(8):NEXTX:H$(6)=H$(6)+O$(MI)
7 IFO(7,0)=99ANDA=8THENZ=RND(1)*4-1
8 CLS:PRINT:PRINT"YOU ARE ";R$(Z,A):IFRND(1)*50<10THENFORX=0TO19:IFO(X,0)=1ANDO(X,1)=0THENO(X,0)=-1:O(X,1)=-1:NEXTX ELSENEXTX
9 IFRND(1)*50<40THEN12ELSEFORX=0TO5
10 IFH(X,O)=-1THENH(X,0)=RND(1)*4-1:H(X,1)=RND(1)*10-1ELSEIFH(X,0)<-1THENH(X,0)=H(X,0)+1:H(X,1)=H(X,1)+1
11 NEXTX
12 PRINT"I SEE:";:IFZ=0ANDL=0THENPRINT"  NOTHING--IT'S TOO DARK!":DK=DK+1:IFDK>3THENPRINT:PRINT"AUGHHHH . . .":PRINT:PRINT"BEWARE OF THINGS UNSEEN!!":GOTO82ELSE16
13 FORX=0TO26:IFO(X,0)=Z ANDO(X,1)=A THENPRINTTAB(7);O$(X)
14 NEXTX:FORX=0TO6:IFH(X,0)=Z ANDH(X,1)=A THENPRINTTAB(7);H$(X)
15 NEXTX:PRINT:PRINT"OBVIOUS EXITS ARE:";:FORX=0TO3:IFR(Z,A,X)=-1ORR(Z,A,X)<>INT(R(Z,A,X))THENNEXTX ELSEPRINTTAB(19);D$(X):NEXTX
16 IFT=20THEN1443ELSETN=TN+1:B$="":PRINT:INPUT"WHAT TO DO";A$:A$=A$+" ":IFLEFT$(A$,3)="LOO"THEN7ELSEGOSUB21:IFX=0THENPRINT"I DON'T UNDERSTAND":GOTO16
17 ONX GOTO26,26,35,44,51,57,59,61,66,73,79,82,83,87,91,97,104,110,112,1360,1440,24,25:IFL=1THENU=U+1:IFU>40THENL=0
18 X=X-24:IFR(Z,A,X)=-1ORR(Z,A,X)<>INT(R(Z,A,X))THEN20
19 A=R(Z,A,X):GOTO7
20 PRINT"YOU CAN'T GO THAT WAY!":GOTO16
21 FORX=1TOLEN(A$):IFMID$(A$,X,1)=" "THENB$=RIGHT$(A$,LEN(A$)-X)ELSENEXTX
22 FORY=1TO63STEP3:IFLEFT$(A$,3)=MID$(C$,Y,3)THENX=(Y+2)/3:Y=63:NEXTY:RETURN
23 NEXTY:FORY=64TO70:IFLEFT$(A$,1)=MID$(C$,Y,1)THENX=Y-42:Y=70:NEXTY:RETURNELSENEXTY:X=0:RETURN
24 IFZ=3ORA<>9THEN20ELSEZ=Z+1:GOTO8
25 IFZ=0ORA<>9THEN20ELSEZ=Z-1:GOTO8
26 IFB$=""THENINPUT"TAKE WHAT";B$:B$=B$+" "
27 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN28ELSENEXTX:PRINT"YOU CAN'T TAKE THE ";B$:GOTO16
28 IFO(X,0)<>Z ORO(X,1)<>A THENIFVAL(H$(6))=X ANDH(6,0)=Z ANDH(6,1)=A THENPRINT:PRINT"AUGHHHH . . .":PRINT:PRINT"REMEMBER?!":PRINT:GOTO82ELSEPRINT"I SEE NO ";B$;"HERE":GOTO16
29 FORQ=0TO5:IFH(Q,0)=Z ANDH(Q,1)=A THENPRINT"THIS ROOM'S OCCUPANT SEEMS TO":PRINT"HAVE GROWN VERY ATTACHED TO THE":PRINTB$;"AND WON'T LET YOU HAVE IT":GOTO16ELSENEXTQ
30 IFX=17ANDO(3,0)<>99THENPRINT"OUCH!! THAT HURTS!!--YOU CAN'T":PRINT"PICK THAT UP!":GOTO16
31 IFX=13ANDO(6,0)<>99THENPRINT"DON'T BE SILLY! YOU CAN'T":PRINT"CARRY THAT MANY COINS!":GOTO16
32 IFI>3THEN34ELSEIFO(X,0)=1ANDO(X,1)=0THENT=T-1
33 O(X,0)=99:O(X,1)=99:I=I+1:PRINTB$"TAKEN":GOTO16
34 PRINT"YOU CAN'T CARRY THAT MUCH,":PRINT"YOU'LL HAVE TO DROP SOMETHING":GOTO16
35 IFB$=""THENINPUT"DROP WHAT";B$:B$=B$+" "
36 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN37ELSENEXTX:PRINT"YOU CAN'T DROP THE "B$:GOTO16
37 IFO(X,0)<>99THENPRINT"YOU HAVE NO "B$"TO DROP":GOTO16
38 IFX=8ORX=16THENL=0
39 IFX=6ANDO(13,0)=99THENPRINT"TRY DROPPING THE COINS FIRST!":GOTO16
40 IFX=3ANDO(17,0)=99THENPRINT"YOU'LL BURN YOURSELF IF YOU":PRINT"DROP THE GLOVE NOW!":GOTO16
41 IFX=19AND(O(10,0)<>Z ORO(10,1)<>A)THENPRINT"YOU CAN'T DROP THAT--IT'LL BREAK":GOTO16
42 O(X,0)=Z:O(X,1)=A:I=I-1:PRINTB$"DROPPED":IFZ=1ANDA=0THENT=T+1
43 GOTO16
44 IFB$=""THENINPUT"SAY WHAT";B$:B$=B$+" "
45 FORX=0TO3:IFW$(X)=B$THEN47ELSENEXTX
46 PRINT"NOTHING HAPPENED":GOTO16
47 FORQ=0TO1:IFZ=Z1(Q)ANDA=A1(Q)THENIFX=P(Q)THEN49ELSEZ=RND(1)*4-1:A=RND(1)*9:PRINT"YOU EXPERIENCE DISORIENTATION":GOTO16
48 NEXTQ:GOTO46
49 PRINT"THE AIR SHIMMERS . . .":FORQ=0TO19:IFO(Q,0)=Z ANDINT(O(Q,1))=A THENO(Q,1)=INT(O(Q,1))ELSENEXTQ
50 GOTO16
51 IFB$=""THENINPUT"KILL WHAT";B$:B$=B$+" "
52 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THEN53ELSENEXTX:PRINT"YOU CAN'T KILL THAT!":GOTO16
53 IFH(X,0)<>Z ORH(X,1)<>A THENPRINT"I SEE NO ";B$;"HERE!":GOTO16
54 IFO(5,0)<>99THENPRINT"YOU HAVE NOTHING WITH WHICH TO":PRINT"KILL THE "B$:GOTO16
55 IFX<>4THENPRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU PERSIST!":GOTO16
56 H(4,0)=-5:H(4,1)=-5:PRINT"THE MONK HAS BECOME FRIGHTENED":PRINT"AND RUN AWAY":GOTO16
57 IFB$=""THENINPUT"STAB WHAT";B$:B$=B$+" "
58 GOTO52
59 IFB$=""THENIFL=0THEN83ELSE87
60 IFB$="ON "THEN83ELSE87
61 IFB$=""THENINPUT"PLAY WHAT";B$:B$=B$+" "
62 IFLEFT$(B$,3)<>"BAN"THENPRINT"YOU CAN'T PLAY THAT!":GOTO16
63 IFO(14,0)<>99THENPRINT"YOU HAVE NO BANJO TO PLAY":GOTO16
64 FORX=0TO50:SOUNDRND(1)*200,1:NEXTX:IFH(2,0)=Z ANDH(2,1)=A THENH(2,0)=-5:H(2,1)=-5:PRINT"MUSIC HATH CHARM TO SOOTH THE":PRINT"SAVAGE BEAST":PRINT"THE BEAST HAS WANDARDED":PRINT"OFF IN A STATE OF BLISS!"
65 GOTO16
66 IFB$=""THENINPUT"READ WHAT";B$:B$=B$+" "
67 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN68ELSENEXTX:PRINT"YOU CAN'T READ THAT!":GOTO16
68 IFO(X,0)<>99THENPRINT"YOU DON'T HAVE A "B$"TO READ":GOTO16
69 IFX=4THENPRINT"THE WRITING IS BLURRY--IT READS:":PRINTTAB(11);"magic"CHR$(128)"words":PRINT:PRINTTAB(2);"TO MAKE OBJECTS . . . ONE OF  THE FOLLOWING.":PRINT:FORY=0TO3:PRINTTAB(10);W$(Y):NEXTY:PRINTELSE71
70 PRINTTAB(2);"NOTE: BE SURE TO USE THE":PRINTTAB(8);"RIGHT WORD IN THE . . .":GOTO16
71 IFX=11THENPRINT"THE PARCHMENT IS TORN--IT READS:":PRINT" . . . IS THE PLACE TO USE THEM:":PRINTTAB(7);R$(1,8):PRINTTAB(7);R$(3,0):PRINTTAB(7);R$(1,4):GOTO16
72 PRINT"YOU CAN'T READ THAT!":GOTO16
73 IFB$=""THENINPUT"DIG WHAT";B$:B$=B$+" "
74 IFLEFT$(B$,3)<>"DIR"THENPRINT"YOU CAN'T DIG THE ";B$:GOTO16
75 IFZ<>0ORA<>0THENPRINT"I SEE NO DIRT TO DIG HERE!":GOTO16
76 IFO(18,0)<>99THENPRINT"YOU DON'T HAVE ANYTHING TO":PRINT"DIG WITH":GOTO16
77 PRINT"DIGGING . . .":FORX=0TO19:IFO(X,0)=Z AND(INT(O(X,1))=A ANDO(X,1)<>INT(O(X,1)))THEN78ELSENEXTX:GOTO16
78 O(X,1)=INT(O(X,1)):PRINT"I FOUND SOMETHING!!":GOTO16
79 PRINT"YOU ARE PRESENTLY CARRYING:":PRINT:IFI=0THENPRINTTAB(7);"//NOTHING\\":GOTO16
80 FORX=0TO19:IFO(X,0)=99THENPRINTTAB(7);O$(X)
81 NEXTX:GOTO16
82 PRINT:PRINT"YOU GOT"T"ITEMS OUT OF THE HOUSE IN"TN"MOVES":END
83 IFO(8,0)<>99THENPRINT"YOU HAVE NO LIGHT TO TURN ON!":GOTO16
84 IFO(16,0)<>99THENPRINT"IT DOESN'T WORK":GOTO16
85 IFU>40THENPRINT"THE BATTERIES ARE EXHUSTED":GOTO16
86 L=1:DK=0:PRINT"LIGHT ON":GOTO16
87 IFO(8,0)<>99THENPRINT"YOU HAVE NO LIGHT TO TURN OFF!":GOTO16
88 IFO(16,0)<>99THENPRINT"IT DOESN'T WORK ANYWAY--SO WHY":PRINT"TURN IT OFF!!":GOTO16
89 IFU>40THENPRINT"THE BATTERIES ARE ALREADY DEAD!":GOTO16
90 L=0:PRINT"LIGHT OFF":GOTO16
91 IFB$=""THENINPUT"BRUSH WHAT";B$:B$=B$+" "
92 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THEN93ELSENEXTX:PRINT"YOU CAN'T BRUSH THAT!":GOTO16
93 IFH(X,0)<>Z ORH(X,1)<>A THENPRINT"I SEE NO "B$"HERE!":GOTO16
94 IFO(12,0)<>99THENPRINT"YOU HAVE NOTHING WITH WHICH":PRINT"TO BRUSH THE ";B$:GOTO16
95 IFX<>3THENPRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU PERSIST!!":GOTO16
96 H(3,0)=-5:H(3,1)=-5:PRINT"PURRRRRRRRR!!!!!!!!!":PRINT:PRINT"THE LEOPARD IS VERY GRATIFIED":PRINT"FOR THE GROOMING, AND LEAVES":GOTO16
97 IFB$=""THENINPUT"WAVE WHAT";B$:B$=B$+" "
98 FORX=0TO19:IFMID$(O$,X*3+1,3)=LEFT$(B$,3)THEN99ELSENEXTX:PRINT"YOU CAN'T WAVE THAT!":GOTO16
99 IFO(X,0)<>99THENPRINT"YOU HAVE NO "B$"TO WAVE!":GOTO16
100 IFX=8ORX=15THEN101ELSEPRINT"THAT WAS FUN!!!  BUT IT HAD":PRINT"NO EFFECT":GOTO16
101 IFX=8ANDH(1,0)=Z ANDH(1,1)=A ANDL=1THENPRINT"THE VAMPIRE COVERED ITS EYES":PRINT"AND FLEW AWAY!":H(1,0)=-5:H(1,1)=-5:GOTO16
102 IFX=15ANDH(5,0)=Z ANDH(5,1)=A THENH(5,0)=-5:H(5,1)=-5:PRINT"THE WEREWOLF HOWLED AND RAN":PRINT"AWAY IN TERROR!":GOTO16
103 PRINT"WAVING THE "B$"HAS NO":PRINT"EFFECT":GOTO16
104 IFB$=""THENINPUT"UNLOCK WHAT";B$:B$=B$+" "
105 IFLEFT$(B$,3)<>"DOO"ANDLEFT$(B$,3)<>"DRA"THENPRINT"I'M SORRY, BUT I ONLY KNOW HOW":PRINT"TO UNLOCK DOORS AND DRAWERS":GOTO16
106 IFLEFT$(B$,3)="DRA"THENPRINT"SHOW ME A DRAWER AND I'LL":PRINT"OPEN IT!!":GOTO16
107 IFZ<>1ORA<>1ORO(20,0)=-1THENPRINT"I SEE NO DOOR THAT NEEDS":PRINT"OPENING!":GOTO16
108 IFO(1,0)<>99THENPRINT"I NEED SOMETHING FIRST!":GOTO16
109 O(20,0)=-1:O(20,1)=-1:R(1,1,3)=INT(R(1,1,3)):PRINTTAB(7);"<<<<C-L-I-C-K>>>>":PRINT"OK, IT'S OPEN NOW":GOTO16
110 IFB$=""THENINPUT"OPEN WHAT";B$:B$=B$+" "
111 GOTO104
112 IFB$=""THENINPUT"SPRAY WHAT";B$:B$=B$+" "
113 IFO(2,0)<>99THENPRINT"YOU HAVE NOTHING TO SPRAY THE":PRINTB$"WITH!!":GOTO16
114 FORX=0TO5:IFMID$(H$,X*3+1,3)=LEFT$(B$,3)THEN115ELSENEXTX:PRINT"YOU CAN'T SPRAY THAT!":GOTO16
115 IFH(X,0)<>Z ORH(X,1)<>A THENPRINT"I SEE NO "B$"HERE":GOTO16
116 IFSP>1THENPRINT"THE CAN IS EMPTY":GOTO16
117 IFX<>0THENSP=SP+1:PRINT"THE "B$"WILL BECOME ANGRY":PRINT"IF YOU CONTINUE TO ACT THIS WAY":GOTO16
118 H(0,0)=-5:H(0,1)=-5:PRINT"THROUGH THE SPRAY'S MIST, YOU":PRINT"CAN SEE THE BLOB DISAPPEAR":PRINT"DOWN A CREVICE IN THE FLOOR":GOTO16
1360 REM DATA SAVE
1361 OPEN"HOUSE.DO"FOR OUTPUT AS 1:FORX=0TO6:FORY=0TO1:PRINT#1,H(X,Y):NEXTY,X:FORX=0TO26:FORY=0TO1:PRINT#1,O(X,Y):NEXTY,X:FORQ=0TO3:FORX=0TO9:FORY=0TO3:PRINT#1,R(Q,X,Y):NEXTY,X,Q:FORX=0TO1:PRINT#1,Z1(X),A1(X),P(X):NEXTX:PRINT#1,Z,A,L,U,TN,T,DK,I,MI,S
1362 CLOSE#1:PRINT:PRINT"THE GAME HAS BEEN SAVED UNDER UNDER FILE-NAME 'HOUSE.DO'":END
1440 REM DATA INPUT
1441 OPEN"HOUSE.DO"FOR INPUT AS 1:FORX=0TO6:FORY=0TO1:INPUT#1,H(X,Y):NEXTY,X:FORX=0TO26:FORY=0TO1:INPUT#1,O(X,Y):NEXTY,X:FORQ=0TO3:FORX=0TO9:FORY=0TO3:INPUT#1,R(Q,X,Y):NEXTY,X,Q:FORX=0TO1:INPUT#1,Z1(X),A1(X),P(X):NEXTX:INPUT#1,Z,A,L,U,TN,T,DK,I,MI,S
1442 CLOSE#1:GOTO6
1443 SOUND100,2:FORX=0TO50:NEXT:SOUND100,2:FORX=0TO50:NEXT:SOUND150,6:CLS:PRINT"CONGRATULATIONS--YOU HAVE";:PRINT"YOU HAVE SUCCESSFULLY";:PRINT"COMPLETED House Adventure":PRINT"YOU REMOVED ALL 20 OBJECTS IN";:PRINTTN"MOVES";:FORX=0TO5000:NEXTX:MENU
1444 DATA A BAG OF GOLD,A RUSTED KEY,A CAN OF BUG SPRAY,AN OLD LEATHER GLOVE,A SORCERER'S HANDBOOK,A CARVING KNIFE,A WOODEN BOX,AN ALUMINUM DIME,A FLASHLIGHT,A SMALL DIAMOND,A SILK PILLOW,A WRINKLED PARCHMENT
1445 DATA A HAIRBRUSH,100'S OF GOLD COINS,A BANJO,A CLOVE OF GARLIC,A SET OF BATTERIES,A BLOCK OF DRY ICE,A SHOVEL,A MING VASE,A LOCKED DOOR,THE FRONT YARD,A KING SIZED BED,A BRASS BATHTUB,A SET OF STOCKS,A DUSTY MOOSE HEAD,A UNITRON 30/50 MAINFRAME
1446 DATA A PROTOPLASMIC BLOB,A VAMPIRE,A SAVAGE BEAST,A LEOPARD,AN INSANE MONK,A WEREWOLF,
1447 DATA IN A DIRT-FLOORED ROOM,IN THE LABORATORY,IN THE PUMPROOM,IN THE FURNACE ROOM,IN A DUSTY COAL BIN,IN THE TORTURE CHAMBER,IN THE WORKSHOP,IN A WALK-IN FREEZER,IN A TELEPHONE BOOTH,IN THE BASEMENT ELEVATOR
1448 DATA ON THE FRONT PORCH,IN THE FOYER,IN A BEDROOM,IN A COAT CLOSET,IN THE DINING ROOM,IN THE PANTRY,IN THE KITCHEN,IN THE FAMILY ROOM,IN A TELEPHONE BOOTH,IN THE FIRST FLOOR ELEVATOR
1449 DATA IN THE SEWING ROOM,IN A CLOSET,IN THE MASTER BEDROOM,IN A GUEST ROOM,IN A BATHROOM,IN A GUEST ROOM,IN A SITTING ROOM,IN THE DEN,IN A TELEPHONE BOOTH,IN THE SECOND FLOOR ELEVATOR
1450 DATA IN THE LIVING ROOM,IN THE LIBRARY,IN THE TROPHY ROOM,IN THE BARROOM,IN THE COMPUTER-ROOM,IN THE GAME ROOM,IN A BEDROOM,IN THE ART HALL,IN A TELEPHONE BOOTH,IN THE THIRD FLOOR ELEVATOR
1451 DATA ABRACADABRA ,NORTH,SHAZAAM ,EAST,SEERSUCKER ,WEST,UGABOOM ,SOUTH:DATA ,,8,2,1,3,,,,,2,4,3,5,,10,,7,4,,9,,,7,6,8,5,,,1,7,,10,,,6,4,,,9:DATA 2,,,,,8,4,1.5,,6,,8,,2,10,,8,10,,,9,,3,7,6,,,10,3,,2,5,,,,6,7,4,5,
1452 DATA ,10,,2,1,,,4,10,,,5,2,5,6,,3,6,4,7,,4,5,,5,,,8,7,,9,,,8,,,,,1,3:DATA 10,3,,,,,10,3,2,,1,,,6,9,,9,,,6,5,,4,8,,8,,,6,,7,,,4,,5,,2,,1:DATA 1,8,2,7,1,3,4,2,3,4,4,8
1453 DATA 1,6,1,1.5,1,3,1,9.5,4,2,2,7,2,2,2,5.5,2,4,3,2,4,8,1,8,3,7,3,9,3,8,1,1.5,4,3,4,4,4,5,3,4,2,2,2,1,3,3,3,5,1,6,3,8,4,5:DATA ,8,1,4

