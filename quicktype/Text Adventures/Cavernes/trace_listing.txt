TRACE
Numéro 2 - 2e trimestre 1982
https://www.abandonware-magazines.org/affiche_mag.php?mag=205&num=4090&album=oui
Transcription by Auraes, version 1.1

Les cavernes, p. 62
Des cavernes dans le Pocket (The TRS-80 Pocket Computer)

Vous vous trouvez dans un tunnel mal éclairé, le tunnel continue vers le nord et se termine au sud, vous voyez plusieurs portes sur le côté, il y a un couteau et une vieille corde près d'une des portes. Vous voyez un lion...
Je prends la corde.
O.K.
Au nord
Vous vous trouvez dans une large pièce...

Qui ne connaît pas les programmes d'aventures?
Ce sont des sortes de labyrinthes que l'on explore À  la recherche de trésors et d'action.
II en existe un grand nombre pour le TRS-80, mais leur taille et leur complexité les interdisent aux petits systèmes. En effet, de tels programmes demandent un minimum de *4 kilo-octets de mémoire... Un peu trop pour le Pocket!
Le programme décrit ci-dessous ne fonctionne pas comme les programmes d'aventures habituels : au lieu de contenir toutes les pièces au départ, il les "invente" au fur et À  mesure, ce qui permet d'explorer aussi longtemps que l'on veut, des cavernes obscures et dangereuses.

Mode d'emploi
-------------
La première rangée des touches programmables du Pocket ont été assignées ainsi:
A : pour démarrer le jeu. Lorsque le texte "NO.=" apparaît, répondez par un nombre quelconque pour initialiser le programme.
À  la question "NIVEAU(0/5)", répondez par un nombre correspondant au niveau de difficulté souhaité dans cette aventure. Le programme vous décrira alors la première pièce, puis â€“ éventuellement â€“ le monstre qui l'occupe, ainsi que les différentes sorties dans les quatre directions (est, ouest, nord, sud).
Lorsque le texte "ORDRE" sera affiché, vous pourrez frapper votre commande (SHFT puis l'une des dix touches programmables).

S : Déplacement vers le nord. S'il y a un monstre dans la pièce, il se peut que vous ne puissiez pas passer. Dans ce cas, il vous faudra essayer une autre issue. Si vous ne pouvez plus sortir, alors vous avez perdu.
D : Mème chose pour aller a l'est.
F : Pour le sud.
G : Pour l'ouest.
H : Pour vous débarrasser des monstres qui vous gènent dans votre progression, il existe dix anneaux "magiques" ayant le pouvoir de tuer certains monstres. Ces anneaux sont dispersés dans les cavernes sauf l'anneau numéro 1 que vous possédez au départ.
Le but du jeu est, en fait, de retrouver les autres anneaux. Si vous en apercevez un, SHFT H vous permettra de le saisir.
J : Donne l'inventaire des anneaux en votre possession.

K : Pour tuer un monstre. Ceci donne un point À  votre score et vous permet de changer de pièce si le monstre vous en empèchait. Pour cela, il vous faut posséder l'anneau correspondant À  ce monstre (sinon, cela ne marche pas!).
L : Donne le score lorsque vous désirez abandonner la partie (parfois, vous ne pouvez pas faire autrement!).

= : Pour revoir la description d'une pièce.
Pour entrer le programme, il faut d'abord charger les DATA dans les mémoires correspondantes, À  l'aide par exemple d'une petite ligne de BASIC que l'on effacera ensuite:
	10 FOR G=22 TO 65:
	INPUT A$(G):
	NEXT G : END

L'ensemble DATA et programme occupent toute la mémoire. N'essayez pas d'en ajouter davantage!

Description des mémoires
------------------------
A À  U : mémoires de manæuvre.

A(12) À  A(21) : endroits on¹ se trouvent les dix anneaux. A(12) sera toujours À  0, puisqu'il est porté par le joueur.

A(22) À  A(26) : noms des différentes sorties (PORTE, CHEMIN, etc.)

A(27) À  A(46) : suite d'adjectifs divers (VIEUX, SALE, etc.)

A(47) À  A(56) : noms des pièces (VOLCAN, TEMPLE, etc.)

A(57) À  A(61) : différentes "bestioles" qui peuplent les cavernes du Pocket.

Enfin, A(62) À  A(65) contiennent les quatre directions et A(66) À  A(69) sont utilisées par le programme.

II est évidemment possible de changer le contenu des mémoires 22 À  61 (que diriez-vous d'une autre aventure À  bord d'une fusée, par exemple, en remplaÀ§ant TUNNEL par CABINE et DRAGON par ALIEN...?).

(listing au dos)_______________________Charles Feydy

----------------------------------------------------
  A    S    D   F    G     H     J     K    L    =
DEBUT NORD EST SUD OUEST PREND INVENT TUER FIN REGAR
  Z    X    C   V    B     N     M    SPC    ENTER
----------------------------------------------------
PROGRAMMES ET MÉMOIRES
----------------------

10: "A" INPUT "NO.= ";D,"NIVEAU(0/5)= ";F : F=4F

20: FOR G=13 TO 21 : E=D+15G : GOSUB "R" : A(G)=INT E : NEXT G

30: C=5 : A(67)=0 : GOSUB 180 : GOTO 100

35: A$(66)="" : IF C > 100 LET C=C-100 * INT(C/100)
 
40: G=B-INT B : FOR H=1 TO 3 : G=G*10 : A(8+H)=INT G+7+(H+1)*10 : G=G-INT G : NEXT H

50: BEEP 1 : PRINT "VOUS ÉTES DANS" : PRINT "UN "; A$(I); " "; A$(K); " "; A$(J)

60: IF INT A > 40-F LET G=57+INT B/20 : PRINT "IL Y A UN "; A$(G) : A$(66)=A$(G)

70: FOR G=1 TO 10 : IF A(11+G)=C PRINT "VOUS TROUVEZ" : PRINT "L-ANNEAU #"; G

75: NEXT G : PRINT "IL Y A:

80: FOR G=1 TO 7 STEP 2 : A$(69)="" : IF G=7 LET A$(69)=" "

85: K=G : GOSUB "T" : IF K >= 4 LET K=G+1 : J=K/2+61 : GOSUB "T" : K=22+INT (K/2) : PRINT "UN"; A$(K); " A"; A$(69); A$(J)

90: NEXT G : RETURN

100: INPUT "ORDRE",G

110: GOTO 100

120:"S" K=1 : GOTO 160

130:"D" K=3 : GOTO 160

140:"F" K=5 : GOTO 160

150:"G" K=7

160: G=K : A(68)=K : GOSUB "T" : IF K < 4 GOTO 270

170: K=G+1 : GOSUB" T" : IF K/2-INT (K/2) <> 0 IF INT A > 40-F PRINT "LE MONSTRE VOUS ARRÉTE" : GOTO 100

180: FOR G=1 TO 2 : C=C-4+A(68) : E=C+D : GOSUB "R" : A(G)=E : NEXT G : GOSUB 35 : GOTO 100

190: "H" FOR G=12 TO 21 : IF A(G)=0 PRINT "OK." : A(G)=0

200: NEXT G : GOTO 100

210: "J" I=0 : PRINT "VOUS AVEZ": FOR G=12 TO 21: IF A(G)=0 LET H=G-11 : PRINT "L-ANNEAU #"; H : I=1

220: NEXT G : IF I=0 PRINT "RIEN"

225: GOTO 100

230:"K" IF A$(66)="" GOTO 100

240: G=INT B-10*INT (B/10) : IF A(G+12)=0 PRINT "OK." : A(67)=1+A(67) : A$(66)="" : FOR H=2 TO 8 STEP 2 : K=H : GOSUB "T"

250: IF A(G+12)=0 IF K/2-INT (K/2)<>0 LET A=A+1/10^H

260: IF A(G+12)=0 NEXT H : GOTO 100

270: PRINT "NON!" : GOTO 100

280: "T" K=(INT( A*10^K))-(10*INT (A*10^(K-1))) : RETURN

290: "R" E=ABS(SIN E*100) : RETURN

300: "L" PRINT "SCORE: "; A(67) : END

400: "=" GOSUB 35 : GOTO 100

Touches programmables
---------------------
Note: faire d'abord CLEAR, puis introduire les DATA de A$(22) À  A$(65) par une ligne BASIC que l'on effacera après usage (voir plus haut). Entrer ensuite le programme. Remarquez que ce dernier a été concentré au maximum (GOTO E2 au lieu de GOTO 100, par exemple).

22. E PORTE
23. CHEMIN
24. E ROUTE
25. TUNNEL
26. E TRAPPE
27. VIEUX
28. SALE
29. GRAND
30. PETIT
31. TRISTE
32. SOMBRE
33. ÉTRANGE
34. LARGE
35. ANCIEN
36. LARGE
37. ROMAIN
38. GREC
39. PERDU
40. DÉSERT
41. HUMIDE
42. SOMBRE
43. EN BOUE
44. BRÀ›LÉ
45. MAUDIT
46. EN LAVE
47. TUNNEL
48. TEMPLE
49. TOMBEAU
50. CAVEAU
51. LOCAL
52. COULOIR
53. TUNNEL
54. PUITS
55. TUNNEL
56. VOLCAN
57. DRAGON
58. SORCIER
59. TROLL
60. LION
61. VAMPIRE
62. U NORD
63. L-EST
64. U SUD
65. L-OUEST



TRACE
Issue 2 - 2nd quarter 1982
https://www.abandonware-magazines.org/affiche_mag.php?mag=205&num=4090&album=oui
Transcription by Auraes, version 1.1

The caverns, p. 62
Caverns in the Pocket (The TRS-80 Pocket Computer)

You are in a poorly lit tunnel, the tunnel continues north and ends south, you see several doors on the side, there is a knife and an old rope near one of the doors. You see a lion...
I take the rope.
O.K.
To the north
You are in a large room...

Who does not know adventure programs?
They are a kind of labyrinths that we explore In search of treasures and action.
There are a large number of them for the TRS-80, but their size and complexity prohibit them from small systems.
Indeed, such programs require a minimum of *4 kilobytes of memory... A bit too much for the Pocket!
The program described below does not work like the usual adventure programs.  Instead of containing all the pieces at the start, it "invents" them as it goes along,
which allows you to explore dark and dangerous caverns for as long as you want.

Instructions
-------------
The first row of the Pocket's programmable keys have been assigned as follows:
A: to start the game. When the text "NO.=" appears, answer with any number to initialize the program.
To the question "LEVEL (0/5)", answer with a number corresponding to the desired difficulty level in this adventure.
The program will then describe the first room, then - possibly - the monster that occupies it, as well as the different exits in the four directions (east, west, north, south).

When the text "ORDER" is displayed, you can type your command (SHFT then one of the ten programmable keys).

N: Move north. If there is a monster in the room, you may not be able to pass. In this case, you will have to try another exit. If you can no longer exit, then you have lost.
E: Same thing to go east.
S: For the south.
W: For the west.
G: To get rid of the monsters that hinder your progress, there are ten "magic" rings that have the power to kill certain monsters.
   These rings are scattered in the caverns except for ring number 1 that you have at the start.
   The goal of the game is, in fact, to find the other rings. If you see one, H will allow you to grab it.
I: Gives the inventory of the rings in your possession.

K: To kill a monster. This gives a point to your score and allows you to change rooms if the monster prevented you from doing so.
   To do this, you must have the ring corresponding to this monster (otherwise, it doesn't work!).
L: Gives the score when you want to abandon the game (sometimes, you can't do otherwise!).

=: To review the description of a room.

To enter the program, you must first load the DATA into the corresponding memories, using for example a small line of BASIC that you will then erase:
10 FOR G=22 TO 65:
INPUT A$(G):
NEXT G : END

The DATA and program together occupy the entire memory. Do not try to add more!

ÀÇéàçêèÉæÆôöÖ£
