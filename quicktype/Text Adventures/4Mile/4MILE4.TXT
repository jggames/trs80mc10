0 CLS3:CLEAR800:DIMCC,C1,M$,Z$,RM$(23),OB$(34),OB(47),D(23,6),D$(6):MX=4:CA=1:GOTO8
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 REM C1=NUM LENGTH C2 DECIMAL LENGTH CC=NUM
4 PRINTRIGHT$("        "+STR$(INT(CC)),C1);:IFC2THENPRINTMID$(STR$((CC-INT(CC))+1.00001)+"000",3,C2+1);
5 RETURN
8 RT=190:R$="IT SAYS - ":CLS4:PRINT@64,;:M$="WELCOME TO FOUR MILE ISLAND. THE 'IMPOSSIBLE' HAS HAPPENED! "
9 M$=M$+"THIS AREA WAS DEVASTATED BY A SEVERE EARTHQUAKE. THE REACTOR HAS BEEN SEVERELY DAMAGED.":GOSUB1
10 M$="THE PLANT HAS BEEN DESERTED. ONLY YOU CAN SAVE THE AREA FROM A MAJOR DISASTER! "
11 M$=M$+"YOUR GOAL IS TO ACHIEVE A COLD SHUTDOWN OF THE REACTOR AT FOUR MILE ISLAND. ALTERNATIVE? DEATH!":GOSUB1
20 RL=4.0:FORX=1TO6:READD$(X):NEXTX:X=0
30 X=X+1:READRM$(X):IFRM$(X)="Z"THEN50
40 FORY=1TO6:READD(X,Y):NEXTY:GOTO30
50 X=0
60 X=X+1:READOB$(X):IFOB$(X)="Z"THENNO=X-1:GOTO70
61 READI$,OB(X):OB$=OB$+I$:GOTO60
70 X=0
80 X=X+1:READI$:IFI$="Z"THEN90
81 VB$=VB$+LEFT$(I$,3):GOTO80
90 PRINT@453,"HIT ANY KEY WHEN READY";:YO=1
91 ON-(INKEY$="")GOTO91:CLS
100 CLS:RT=RT+RND(6):IFRT>780THEN1760
101 PRINT"I AM ";
110 IFRT>500THENRL=RL+.1
120 IFYO=11ORYO=16ORYO=20THENOB(4)=YO
130 IFYO=2ORYO=7ORYO=4THENOB(2)=YO
140 IFYO=16ORYO=10THENOB(26)=YO
150 IFYO=9ORYO=14ORYO=21THENOB(24)=YO
160 PRINTRM$(YO):IFYO=9ANDOB(13)=-1THEND(9,1)=10:GOTO170
161 D(9,1)=0
170 IFYO=14ANDOB(17)=-1THEND(14,1)=15:GOTO180
171 D(14,1)=0
180 IFYO=13ORYO=12THENOB(25)=YO
190 PRINT"I CAN SEE: ":F=0:FORI=1TONO:IFINT(OB(I))<>INT(YO)THEN210
200 PRINT"  "OB$(I):F=1
210 NEXT:IFF=0THENPRINT"  NOTHING INTERESTING"
220 PRINT"YOU CAN GO: ":FORI=1TO6:IFD(YO,I)<>0THENPRINT" * " D$(I);
230 NEXT:PRINT
240 RT=RT+2:FORT=1TO32
241 PRINTCHR$(140);:NEXT:IFRL=4.1THENPRINT"THE RADIATION LEVEL IS CLIMBING.TIME IS GETTING SHORT!":RL=4.11
250 IFYO=21THENOB(20)=0:D(10,6)=0
260 IFRT>723ANDRT<730THEN265
261 GOTO270
265 RT=730:M$="AUTOMATIC ALARMS ARE SOUNDING! REACTOR CORE CRITICAL! RADIATION LEVEL AT MAXIMUM SAFETY LEVEL!":GOSUB1
270 INPUT"WHAT SHOULD I DO";I$:IFI$=""THEN270
271 IFI$="LOOK"THENCLS:GOTO100
280 IFI$="QUIT" ORI$="END"THEN END
290 IFI$="SAVE"THEN1700
300 IFI$="LOAD"THEN1710
310 IFLEFT$(I$,2)="GO"THENPRINT"USE SINGLE LETTERS TO INDICATE  DIRECTION (EXAMPLE, N=NORTH)":GOTO240
320 IFLEFT$(I$,3)<>"INV"THEN370
330 PRINT"I AM CARRYING:":F=0:FORI=1TONO:IFOB(I)=-1THENPRINTOB$(I):F=1
340 NEXT:IFF=0THENPRINT"NOTHING"
350 GOTO240
370 IFLEN(I$)=1THEN540
380 IFI$="HELP"THEN610
390 M$=I$:Z$=CHR$(32):BB=1:GOSUB9000:SP=FF
391 IFSP=0THENPRINT"TRY USING TWO WORD COMMANDS.":GOTO240
400 V$=LEFT$(I$,SP-1):O$=MID$(I$,SP+1):A$=LEFT$(V$,3):B$=LEFT$(O$,3):IFA$="TAK"THENA$="GET"
410 IFA$="LEA"THENA$="DRO"
420 IFA$="LOO"THENA$="EXA"
430 IFA$="PUL"ORA$="MOV"THENA$="PUS"
440 IFA$="SMA"ORA$="TEA"THENA$="BRE"
450 IFA$="KIC"THENPRINT"OUCH! I HURT MY TOE.":GOTO240
460 IFA$="FUC"ORA$="SHI"ORA$="PIS"THENPRINT"BAD LANGUAGE WON'T WORK.":GOTO240
470 IFLEFT$(O$,3)="WAL"ANDYO=10THENPRINT"I SEE A SLOT IN THE WEST WALL.":OB(20)=10:GOTO240
480 IFLEFT$(O$,3)="FLO"ANDYO=20THENPRINT"THERE IS A LOOSE TILE":OB(16)=20:GOTO240
490 IFLEFT$(O$,3)="FLO"ORLEFT$(O$,3)="CEI"ORLEFT$(O$,3)="WAL"THENPRINT"THERE IS NOTHING ON THE "O$:GOTO240
500 IFA$="CLI"THENPRINT"YOU CAN'T CLIMB THE "O$".":GOTO240
510 M$=VB$:Z$=A$:BB=1:GOSUB9000:V=(FF+2)/3
511 IFV<1THENPRINT"SORRY, I JUST DON'T KNOW HOW TO":PRINTV$" ANYTHING.":GOTO240
520 M$=OB$:Z$=B$:BB=1:GOSUB9000:O=(FF+2)/3
521 IFO<1THENPRINT"I JUST DON'T SEE ANY ":PRINTO$" HERE.":GOTO240
530 IFLEN(I$)>1THEN670
540 V=0:IFI$="N"ANDD(YO,1)<>0THENYO=D(YO,1):GOTO100
550 IFI$="S"ANDD(YO,2)<>0THENYO=D(YO,2):GOTO100
560 IFI$="E"ANDD(YO,3)<>0THENYO=D(YO,3):GOTO100
570 IFI$="W"ANDD(YO,4)<>0THENYO=D(YO,4):GOTO100
580 IFI$="U"ANDD(YO,5)<>0THENYO=D(YO,5):GOTO100
590 IFI$="D"ANDD(YO,6)<>0THENYO=D(YO,6):GOTO100
600 GOTO670
610 IFYO=8THENPRINT"GET SOME TOOLS.":GOTO240
620 IFRT>600THENPRINT"TIME IS RUNNING OUT! HURRY!":GOTO240
630 IFYO=6THENPRINT"HAVE YOU CHECKED YOUR INVENTORY?";:GOTO240
640 IFYO=9ANDOB(13)<>-1THENPRINT"YOU NEED SECURITY CLEARANCE.":GOTO240
650 IFYO>9ANDYO<16THENPRINT"YOU ARE DOING O.K. KEEP TRYING.":GOTO240
660 PRINT"I AM AS CONFUSED AS YOU.":GOTO240
670 F=0:IFOB(O)=YOTHENF=1:GOTO680
671 IFOB(O)=-1THENF=2
680 ONVGOTO700,810,830,930,970,1230,1310,1330,1350,1470,1490,1530:IFLEN(I$)=1THENPRINT"I CAN'T GO THAT WAY.":GOTO240
690 PRINT"I DON'T UNDERSTAND WHAT YOU ARE TELLING ME.":GOTO240
700 IFF=2THENPRINT "I ALREADY HAVE IT!":GOTO240
710 IFF=0THENPRINT"I DON'T SEE THAT HERE.":GOTO240
720 IFCA=>MXTHENPRINT"I CAN'T CARRY ANY MORE.":GOTO240
730 IFO=1THENPRINT"YOU KIDDING? THEY MUST WEIGH TENTONS!":GOTO240
740 IFO<12THENPRINT"YOU CAN'T TAKE THAT.":GOTO240
750 IFO=15THENPRINT"YOU GOT TO BE LEADING ME ON! THESMALLEST ONE IS 40 FEET LONG.":GOTO240
760 IFO=16THENPRINT"IT'S SLATE. I CAN'T LIFT IT.":GOTO240
770 IFO=14THENPRINT"IT IS WIRED INTO THE MAINFRAME.":GOTO240
780 IFO>23ANDO<31THENPRINT"YOU CAN'T TAKE THAT.":GOTO240
790 IFO=20THENPRINT"THAT IS SILLY! YOU CAN'T TAKE A PIECE OF THE WALL.":GOTO240
800 OB(O)=-1:PRINT"OK, I GOT IT.":CA=CA+1:GOTO240
810 IFF=2THENPRINT"OK, I DROPPED IT.":CA=CA-1:OB(O)=YO:GOTO240
811 PRINT"I DON'T HAVE IT.":GOTO240
830 IFF=0THENPRINT"I DON'T SEE IT HERE.":GOTO240
840 IFO=1THENM$="THE SECURITY LATCHES ARE ON. THE GATES WILL NOT OPEN.":GOSUB1:GOTO240
850 IFO=12ANDDO>0THENPRINT"IT'S ALREADY OPEN":GOTO240
851 IFO=12ANDDR=1THENPRINT"O.K. IT'S OPENED":OB(13)=6:DO=1:GOTO240
852 IFO=12THENPRINT"I CAN'T GET IT OPEN.":GOTO240
860 IFO=23ANDM=0THENPRINT"O.K. I OPENED IT.":M=1:GOTO240
870 IFO=23THENPRINT"IT'S ALREADY OPEN.":GOTO240
880 IFO=4ANDYO=20ANDOB(19)=-1THEN885
881 GOTO890
885 CLS4:PRINT@128,;
886 M$="THE VALVE IS OPENED. ALL RESERVE COOLING WATER HAS BEEN DUMPED IN THE RIVER. "
887 M$=M$+"THE REACTOR CANNOT BE SAVED. YOU HAVE LOST THE GAME.":GOSUB1:FORT=1TO3000:NEXT:RT=792:GOTO1760
890 IFO=4ANDYO=11ANDOB(19)=-1ANDOB(22)=-1THENPRINT"O.K. IT'S OPENED.":PV=1:CF=6801:GOTO240
891 IFO=4ANDYO=20ANDOB(19)=-1ANDOB(22)=-1THENPRINT"O.K. IT'S OPENED.":TV=1:GOTO240
900 IFO=4ANDYO=11ANDOB(19)=-1THENPRINT"IT IS REALLY STUCK! I CAN'T OPENIT.":GOTO240
910 IFYO=16ANDO=4THENPRINT"IT'S ALREADY OPEN.":GOTO240
920 PRINT"SORRY I CAN'T GET IT OPEN.":GOTO240
930 IFF=0THENPRINT"I DON'T SEE IT HERE.":GOTO240
940 IFO=23THENPRINT"O.K. I CLOSED IT.":M=0:GOTO240
950 IFO=1THENPRINT"THEY ARE CLOSED ALREADY.":GOTO240
960 PRINT"I TRIED BUT IT DOESN'T CLOSE.":GOTO240
970 IFF=0THENPRINT"I DON'T SEE ANYTHING LIKE THAT  AROUND HERE. YOU NEED GLASSES?":GOTO240
980 IFO=5THENPRINT"REACTOR TEMP"RT"DEGREES.":GOTO240
990 IFO=11ORO=23ORO=13THENPRINT"SOMETHING IS WRITTEN ON IT.":GOTO240
1000 IFO=6THENPRINT"RADIATION LEVEL =";:C1=1:C2=2:CC=RL:GOSUB4:PRINT" MREM'S/YR":GOTO240
1010 IFO=9THENOB(12)=YO:PRINT"IT HAS A DRAWER.":GOTO240
1020 IFO=14THEN1025
1021 GOTO1030
1025 PRINT"PLANT STATUS REPORT - "
1026 PRINT"REACTOR TEMP"RT"DEGREES":PRINT"RADIATION LEVEL ";
1027 C1=1:C2=2:CC=RL:GOSUB4:PRINT" MREM'S/YR":PRINT"GENERATOR OUTPUT"G"MEG/WATT":GOTO240
1030 IFO=15THENPRINT"THEY HAVE AN EERIE GREEN GLOW. IAM NERVOUS BEING IN HERE!":GOTO240
1040 IFO=16THENPRINT"IT'S A HEAVY PIECE OF SLATE.":GOTO240
1050 IFO=20THENPRINT"IT'S VERY NARROW.":GOTO240
1060 IFO=3THENPRINT"NO ONE IS THERE.":GOTO240
1070 IFO=24THENO=14:GOTO1020
1080 IFO=25ANDYO=13ANDGB=0THENPRINT"THE BREAKER IS TRIPPED.":GOTO240
1090 IFO=25ANDYO=13ANDGB=1THENPRINT"THE BREAKER IS SET.":GOTO240
1100 IFO=25ANDYO=12ANDTB=0THENPRINT"THE BREAKER IS TRIPPED.":GOTO240
1110 IFO=25ANDYO=12ANDTB=1THENPRINT"THE BREAKER IS SET.":GOTO240
1120 IFO=26THENPRINT"REACTOR COOLANT STATUS -":PRINT"AVAILABLE - "CF"GAL/MIN":PRINT"PUMP PRESSURE - 0 LBS/FT":GOTO240
1130 IFO=4ANDYO=16THENPRINT"THE VALVE IS WIDE OPEN.":GOTO240
1140 IFO=4ANDYO=11ANDPV=0THENPRINT"THE VALVE IS CLOSED.":GOTO240
1141 IFO=4ANDYO=11ANDPV=1THENPRINT"THE VALVE IS OPEN.":GOTO240
1150 IFO=4ANDYO=20ANDTV=0THENPRINT"THE VALVE IS CLOSED.":GOTO240
1151 IFO=4ANDYO=20ANDTV=1THENPRINT"THE VALVE IS OPEN.":GOTO240
1160 IFO=28THENPRINT"THE TURBINE IS MASSIVE!":GOTO240
1170 IFO=27ANDG=0THENPRINT"THE GENERATOR IS STOPPED.":GOTO240
1171 IFO=27ANDG>0THENPRINT"THE GENERATOR IS TURNING.":GOTO240
1180 IFO=29THENPRINT"IT'S GLOWING AND BUZZING! I WANTOUT OF HERE!":GOTO240
1190 IFO=30ANDOB(31)=0THENPRINT"THERE IS A SHEET OF PAPER.":OB(31)=YO:GOTO240
1200 IFO=32THENPRINT"IT IS AN ADJUSTABLE WRENCH":GOTO240
1210 IFO=33THENPRINT"WRITING ON IT SAYS -'HONG KONG'":GOTO240
1220 PRINT"NOTHING SPECIAL ABOUT IT.":GOTO240
1230 IFF=0THENPRINT"I DON'T SEE IT HERE.":GOTO240
1240 IFO=7ANDGB=0ORO=7ANDTB=0THENPRINT"NOTHING HAPPENED.":GOTO240
1241 IFO=7ANDG>1THENPRINT"I HEAR A SIREN!":GB=0:TB=0:G=0:RT=RT+68:GOTO240
1242 IFO=7THENPRINT"I HEAR A WHINING NOISE.":RT=RT-35:G=G+1:GOTO240
1250 IFO=16THEN1255
1251 GOTO1260
1255 PRINT"O.K. I MANAGED TO MOVE IT.":IFOB(17)=0THENOB(17)=YO
1256 GOTO240
1260 IFO=25ANDYO=13THEN1265
1261 GOTO1270
1265 PRINT"O.K. I PUSHED IT.":GB=GB+1:IFGB>1THENGB=0
1266 GOTO240
1270 IFO=25ANDYO=12THEN1275
1271 GOTO1280
1275 PRINT"O.K. I PUSHED IT.":TB=TB+1:IFTB>1THENTB=0
1276 GOTO240
1280 IFO=8ANDG=1ANDCF>6000ANDTB=1ANDGB=1THEN1285
1281 GOTO1290
1285 CLS3:NT=RT:PRINT"THE REACTOR IS COOLING"
1286 FORT=1TO30:RT=RT-RND(50):PRINT"REACTOR TEMP"RT:FORX=1TO100:IFRT<200THEN1740
1287 NEXTX:NEXTT
1290 IFO=8THENPRINT"I MOVED IT BUT NOTHING HAPPENED.";:GOTO240
1300 PRINT"I TRIED TO MOVE IT BUT COULDN'T":GOTO240
1310 IFF=0THENPRINT"I DON'T SEE IT HERE.":GOTO240
1320 PRINT"I TRIED TO, BUT IT WON'T BREAK.":GOTO240
1330 IFF=0THENPRINT"I DON'T SEE ANYTHING LIKE THAT  AROUND HERE.":GOTO240
1340 PRINT"I TRIED TO ENTER BUT COULDN'T.":GOTO240
1350 IFF=0THENPRINT"IT'S NOT HERE.":GOTO240
1360 IFO=5THEN980
1370 IFO=11THENPRINT"NO VISITORS BEYOND THIS POINT.":GOTO240
1380 IFO=6THEN1000
1390 IFO=13THENPRINTR$"CLASS 'B' CLEARANCE.":GOTO240
1400 IFO=7THENPRINTR$"TURBINE.":GOTO240
1410 IFO=8THENPRINTR$"COOLING PUMPS.":GOTO240
1420 IFO=17THENPRINTR$"CLASS 'A' CLEARANCE.":GOTO240
1430 IFO=23ANDM=0THENPRINTR$"4 MILE ISLAND":GOTO240
1440 IFO=23ANDM=1THEN1445
1441 GOTO1450
1445 PRINTR$:PRINT"4 MILE ISLAND SPECS.":PRINT"MAXIMUM CAPACITY 2 MEGAWATTS."
1446 PRINT"BREAKER TRIP 2.2 MEGAWATTS.":PRINT"MAXIMUM PUMP OPERATION AT 1 MEG.REACTOR CORE CAPABILITY 750 DEG.";:GOTO240
1450 IFO=31THENPRINT"IT SAY'S 'ENJOY THE ADVENTURE'.":GOTO240
1460 PRINT"THERE IS NOTHING WRITTEN ON IT.":GOTO240
1470 IFO=12ANDOB(18)=-1THENPRINT"O.K. I UNLOCKED IT.":DR=1:GOTO240
1480 PRINT"I TRIED BUT NOTHING HAPPENED.":GOTO240
1490 IFOB(O)<>-1THENPRINT"I DON'T HAVE IT.":GOTO240
1500 IFYO=10ANDO=13THENPRINT"A DOOR OPENED TO A STAIRWAY!":D(10,6)=21:GOTO240
1510 IFO=17ORO=18THENPRINT"IT DOESN'T SEEM TO FIT.":GOTO240
1520 PRINT"DON'T BE SILLY!":GOTO240
1530 IFF<>1THENPRINT"I DON'T SEE IT HERE.":GOTO240
1540 IFOB(23)=0ANDO=10ANDOB(22)=-1THENPRINT"IT POPPED RIGHT OPEN.":OB$(10)="AN OPEN CABINET":OB(23)=YO:GOTO240
1550 IFOB(23)>0THENPRINT"IT'S ALREADY OPENED.":GOTO240
1560 PRINT"I TRIED BUT NOTHING HAPPENED.":GOTO240
1570 DATANORTH,SOUTH,EAST,WEST,UP,DOWN
1580 DATAAT A MAIN GATE,0,2,0,0,0,0,IN A VISITORS LOUNGE,1,0,3,0,0,0,IN A 'C' SECURITY ROOM,0,0,4,2,0,0
1581 DATAIN A LARGE LOBBY,9,5,7,3,0,0,IN A SMALL OFFICE,4,6,0,0,0,0,IN A LARGE OFFICE,5,0,0,0,0,0
1590 DATAIN A RECEPTION ROOM,0,8,0,4,0,0,IN A LARGE OFFICE,7,0,0,0,0,0,IN A 'B' SECURITY ROOM,10,4,20,0,0,0
1591 DATAIN A REACTOR CONTROL ROOM,14,9,11,16,0,0,IN CONTROL ROOM 'P',12,0,0,10,0,0,IN CONTROL ROOM 'T',13,11,0,0,0,0
1600 DATAIN A CONTROL ROOM 'G',0,12,0,0,0,0,IN A 'A' SECURITY ROOM,15,10,0,0,0,0,IN THE REACTOR ROOM,0,14,0,0,0,19
1601 DATAIN PUMP ROOM 'A',17,0,10,0,0,0,IN TURBINE ROOM 'A',18,16,0,0,0,0
1610 DATAIN GENERATOR ROOM 'A',0,17,0,0,0,0,IN THE CONTAINMENT BLDG,0,0,0,0,15,0,IN THE COOLING TOWER,0,0,0,9,0,0
1611 DATAIN THE LOWER CONTROL ROOM,0,0,0,22,10,0,IN A TOOL ROOM,0,0,21,0,0,0
1620 DATAZ
1630 DATAHEAVY STEEL GATES,GAT,1,A VISITORS COUCH,COU,2,A SECURITY STATION,STA,3,A HUGE GATE VALVE,VAL,20
1631 DATAA LARGE GAUGE,GAU,15,A LARGE DIAL,DIA,15,A RED SWITCH,SWI,10,A YELLOW LEVER,LEV,10,A BIG DESK,DES,6
1640 DATAA STEEL CABINET,CAB,8,A SIGN,SIG,9,A DRAWER,DRA,0,A BADGE,BAD,0,A COMPUTER SCREEN,SCR,10
1641 DATAA ROW OF VATS,VAT,19,A LOOSE TILE,TIL,0,AN ID,ID ,0,A BRASS KEY,KEY,-1,A SPANNER,SPA,19
1650 DATAA NARROW SLOT IN THE WALL,SLO,0,A BENCH,BEN,22,A CROWBAR,CRO,22,AN OPERATIONS MANUAL,MAN,0
1651 DATAA MONITOR,MON,21,A CIRCUIT BREAKER,BRE,0
1660 DATAA METER,MET,16,A HUGE GENERATOR,GEN,18,A HUGE STEAM TURBINE,TUR,17,AN ATOMIC REACTOR,REA,15
1661 DATAA SECRETARY'S TABLE,TAB,5,A SHEET OF PAPER,PAP,0,A BIG WRENCH,WRE,22,A LONG STEEL ROD,ROD,22
1670 DATAZ
1680 DATAGET,DROP,OPEN,CLOSE,EXAMINE,PUSH,BREAK,ENTER,READ,UNLOCK,INSERT,PRY
1690 DATAZ
1700 GOSUB1730
1701 OB(36)=F:OB(37)=G:OB(38)=CF:OB(39)=TB:OB(40)=GB
1702 OB(41)=CA:OB(42)=RT:OB(43)=YO:OB(44)=DO:OB(45)=NT:OB(46)=DR:OB(47)=M
1705 CSAVE*OB,"4MILEDAT":CLS:GOTO100
1710 GOSUB1730:CLOAD*OB,"4MILEDAT"
1711 F=OB(36):G=OB(37):CF=OB(38):TB=OB(39):GB=OB(40)
1712 CA=OB(41):RT=OB(42):YO=OB(43):DO=OB(44):NT=OB(45):DR=OB(46):M=OB(47)
1720 CLS:GOTO100
1730 CLS:PRINT"PREPARE RECORDER.":PRINT:PRINT"HIT ANY KEY WHEN READY"
1731 ON-(INKEY$="")GOTO1731:RETURN
1740 FORT=1TO20:CLS2:PRINT@7*32+11,"ALL CLEAR!";:FORX=1TO100:NEXTX:CLS2:FORX=1TO100:NEXTX:NEXTT
1741 CLS5:PRINT@96,"CONGRATULATIONS! YOU HAVE SAFELYSHUT DOWN THE REACTOR. YOU HAVE SAVED HUNDREDS OF LIVES AND MUCH";
1742 PRINT"PROPERTY DAMAGE. THE ENTIRE AREAIS IN YOUR DEBT. MELTDOWN WOULD"
1750 PRINT"HAVE OCCURRED AT APPROXIMATELY - 780 DEGREES. THE UNIT REACHED -"NT"DEGREES.":END
1760 FORT=1TO2:CLS0:PRINT"MELTDOWN!";:FORX=1033TO1536:POKE15360+X,191:NEXT:NEXTT:CLS8
1761 PRINT@128,"THE REACTOR CORE REACHED"RT
1762 PRINT"DEGREES. YOU HAVE LOST YOUR LIFEALONG WITH THOUSANDS OF OTHERS. BETTER LUCK NEXT TIME!":END
1997 REM INSTR SUBROUTINE
1998 REM INPUT VARIABLE: M$=STRING TO BE SEARCHED,Z$=SEARCH TARGET,BB=BEGINING POSITION OF SEARCH
1999 REM OUTPUT VARIABLE:FF=POSITION OF SEARCH TARGET OR ZERO IF NOT FOUND
9000 FF=0:LL=LEN(Z$):FORZZ=BBTOLEN(M$):ON1-(MID$(M$,ZZ,LL)=Z$)GOTO9010:FF=ZZ:ZZ=255:NEXT:RETURN
9010 NEXT:RETURN
