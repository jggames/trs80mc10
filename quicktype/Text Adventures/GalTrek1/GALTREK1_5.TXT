0 CLEAR750:DIMC1,ZZ,M$,I$:GOTO20
1 ZZ=1:C1=32:FORC1=C1TOZZSTEP-1:I$=MID$(M$,C1,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,C1-ZZ):ZZ=C1+1:C1=ZZ+32:IFI$=""THENC1=0
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
8 I$=RIGHT$("        "+STR$(INT(C3)),C1):IFC2THENI$=I$+MID$(STR$((C3-INT(C3))+1.00001)+"000",3,C2+1)
9 RETURN
20 CLS:PRINT:PRINT@138,"G A L A X Y":PRINT@196,"A D V E N T U R E    # 1"
21 PRINT:PRINT@418,"COPYRIGHT 1982 HOWARD BATIE":PRINT"        HERNDON, VA 22070":GOSUB350:GOSUB1680
30 DIMDM(41,5),DS$(41),OB$(26),OB(43),DI$(5),VB$(25),NN$(26),DD$(5):WG=0:CW=0:SC=0:NM=0:CC=0:SP=0:CM$="":BP=0
40 SP$=CHR$(13)+"SPOCK SAYS-- ":CR$="THE COMPUTER RESPONDS-- ":NN$="NOT NECESSARY NOW, CAPTAIN."
41 CD$="YOU CANNOT DO THAT":TM$="YOU'RE CARRYING TOO MUCH ":BL$="A BUTTON LABELLED:"
50 DR$="YOU'LL HAVE TO DROP SOMETHING.":CP$="CAPTAIN":SH$="THE CRYSTALS SHATTER INTO DUST."
51 EN$="THE ENTERPRISE":NI$="NOTHING HAPPENED.":PF$="SORRY, YOU'RE ON YOUR OWN!"
52 FORI=1TO41:FORJ=0TO5:READDM(I,J):NEXTJ,I
60 DATA2,0,0,0,0,0,0,3,1,10,0,0,0,0,0,2,0,0,4,4,4,4,0,0,0,11,12,9,0,0,0,0,10,0,9,17,0,10,0,8,0,0,0,7,0,0,0,0,0,5
61 DATA0,0,0,6,6,2,0,7,0,0,0,0,13,5,0,0,5,13,0,0,0,0
70 DATA11,0,0,12,0,0,0,16,18,0,0,0,17,18,0,0,0,0,0,0,0,14,0,0,0,0,15,0,6,24,14,19,0,15,0,0,0,0,0,18,0,0,24,27,0
71 DATA23,0,0,0,22,0,0,0,0,0,0,23,21,0,0,22,20,0,0,0,0,0,0,20,0,17,29
80 DATA29,28,0,0,0,0,0,0,0,27,0,0,0,26,0,20,0,0,0,30,0,25,0,0,0,0,25,0,24,0,0,32,31,28,0,0,30,0,0,0,0,0,0,0,0,30
81 DATA0,0,33,33,33,33,0,0,35,38,33,4,0,0,39,40,34,0,0,0,0,0,4,0,0,0
82 DATA0,0,40,39,0,0,38,38,38,38,0,0,0,37,35,0,0,0,37,0,38,35,0,0,0,4,0,0,0,0
90 DATA32,12,13,11,5,10,15,20,25,26,7,14,27,4,8,4,16,19,21,21,21,27,31,32,1,5
91 FORI=1TO26:READOB(I):NEXT:FORJ=0TO3:DM(4,J)=4:NEXTJ
100 DS$(1)="IN THE CAPTAIN'S QUARTERS":DS$(2)="IN A CORRIDOR OF "+EN$:DS$(3)="IN THE TRANSPORTER ROOM"
101 DS$(4)="ON THE SANDY SURFACE OF THE PLANET":DS$(5)="AT THE ENTRANCE TO A TURBOLIFT":DS$(6)="IN THE TURBOLIFT"
110 DS$(7)=DS$(2):DS$(8)="IN ONE OF THE CREW'S QUARTERS":DS$(9)=DS$(6):DS$(10)=DS$(5)
111 DS$(11)="AT THE NAVIGATION CONSOLE":DS$(12)="AT THE COMMUNICATIONS STATION":DS$(13)="AT THE SCIENCE OFFICER'S STATION"
120 DS$(14)=DS$(2):DS$(15)=DS$(5):DS$(16)="IN THE SHIP'S LIBRARY":DS$(17)=DS$(6):DS$(18)=DS$(2):DS$(19)="IN THE SICK BAY"
130 DS$(20)=DS$(5):DS$(21)="IN THE SHIP'S SUPPLY WAREHOUSE":DS$(22)=DS$(2):DS$(23)=DS$(2):DS$(24)=DS$(6):DS$(25)=DS$(5)
131 DS$(26)="IN A SECURITY CELL IN THE BRIG":DS$(27)=DS$(2):DS$(28)=DS$(2):DS$(29)=DS$(6)
132 DS$(30)="IN THE WARP ENGINE DRIVE AREA"
140 DS$(31)="AT THE DILITHIUM CRYSTAL POWER STATION.":DS$(32)="AT THE AUXILIARY POWER STATION."
141 DS$(33)="IN A GREAT SEA OF MOVING SAND":DS$(34)="ON A SANDY HILLSIDE"
150 DS$(35)="AT THE WALL OF A CAMP TO THE NORTHEAST.":DS$(36)="ON A DUNE. MANY KLINGONS TO THE EAST."
160 DS$(37)="IN YOUR CREW'S CAMP-- YOU MUST LEAD THEM BACK TO WHERE YOU BEAMED DOWN."
161 DS$(38)="CAUGHT IN A ROCKSLIDE AND DIE IN AN AVALANCHE OF BOULDERS!":DS$(39)="AT A GATE OF A CAMP TO THE EAST"
170 DS$(40)="AT THE GATE OF A CAMP TO THE NORTH":DS$(41)=DS$(4):OB$(1)="AN UNARMED KLINGON COMMANDER"
171 OB$(2)=" -- SHIP'S SENSORS --":OB$(3)=" -- SHIP'S STATUS --":OB$(4)=" -- FIRE IMPULSE ENGINES --"
180 OB$(5)="A SIGN ON THE OPPOSITE WALL":FORI=6TO9:OB$(I)=OB$(5):NEXT:OB$(10)="MR. SPOCK UNCONSCIOUS ON THE FLOOR"
181 OB$(11)="A KLINGON SOLDIER":OB$(12)="A KLINGON GUARD"
190 OB$(13)="A KLINGON SENTRY":OB$(14)="A KLINGON OFFICER":OB$(15)="A FURRY LITTLE TRIBBLE"
191 OB$(16)="RAW DILITHIUM CRYSTALS":OB$(17)="THE SHIP'S TECHNICAL MANUAL"
200 OB$(18)="A HYPODERMIC ANTIDOTE":OB$(19)="A PHASER":OB$(20)="A COMMUNICATOR":OB$(21)="AN ELECTRONIC SHUNT"
201 OB$(22)="SPOCK'S TRICORDER":OB$(23)="POWER LEVELS ARE VERY LOW.":OB$(24)="A POWER CONTROL PANEL"
210 OB$(25)="A 3D CHESS SET":OB$(26)="STAR CHARTS":DI$(0)="NORTH":DI$(1)="EAST":DI$(2)="SOUTH"
211 DI$(3)="WEST":DI$(4)="UP":DI$(5)="DOWN":FORI=1TO25:READVB$(I):NEXT
220 DATANOR,EAS,SOU,WES,UP ,DOW,HEL,INV,SCO,QUI,LOO,GO ,GET,DRO,SAY,THR,FIR,PRE,REA,INS,INJ,SAV,STA,ON ,OFF
221 FORI=0TO22:READNN$(I):NEXT:PL=1:SP=0:KE=-1:CR=0:SH=0:DE=-1:TT=0:TD=1
230 DATAENER,COMM,BUTT,BUTT,BUTT,SIGN,SIGN,SIGN,SIGN,SIGN,SPOC,KLIN,KLIN
231 DATAKLIN,KLIN,TRIB,CRYS,MANU,NEED,PHAS,COMM,SHUN,TRIC
240 DD$(1)="BRIDGE":DD$(2)="PERSONNEL SECTION":DD$(3)="TECHNICAL DEPARTMENTS":DD$(4)="SECURITY SECTION"
241 DD$(5)="ENGINEERING DIVISION":NN$(25)="CHES":NN$(26)="CHAR":YY$="PRESS enter TO ":AG$="":GOSUB1700
250 CLS:PRINT:PRINT:PRINT"WANT INSTRUCTIONS ";AG$;"(y/n)?":GOSUB1720:IFZZ$="Y"THENGOSUB1780:GOTO250
260 CLS:PRINT:PRINT:PRINT"RESTORE A PREVIOUS GAME (y/n)? "
270 GOSUB1720:IFZZ$<>"Y"ANDZZ$<>"N"THENSOUND1,1:GOTO270
280 IFZZ$="N"THEN300
290 GOSUB1670
300 CLS:IFBP=1THENSOUND1,1
310 M$="YOU ":IFSP=1THENM$=M$+"AND SPOCK "
320 IFCW=1THENM$=M$+"AND THE CREW "
330 M$=M$+"ARE "+DS$(PL):GOSUB1:PRINT:IFPL=38THEN730
331 IFPL=37THENCW=1:GOTO360
332 IFPL<>33THEN360
333 GOSUB350:M$="A GIANT SAND SNAKE IS SWIMMING THROUGH THE SAND TOWARD YOU!":GOSUB1:GOSUB350:PRINT
340 PRINT"IT'S GETTING CLOSER!":GOSUB350:PRINT:PRINT"YOU CAN'T ESCAPE!":GOSUB350:PRINT
341 M$="AARRRRGHH!!! IT KILLED YOU!":GOSUB1:PRINT:GOTO730
350 FORI=1TO2500:NEXT:RETURN
360 M$="OBVIOUS EXITS ARE: ":FORJ=0TO5:IFDM(PL,J)>0THENM$=M$+DI$(J)+" "
370 NEXTJ:GOSUB1:PRINT:PRINT"YOU CAN SEE THESE OBJECTS:"
371 IFPL=21THENPRINT"ALL THE SHIP'S SUPPLIES":GOTO410
372 IFPL>10ANDPL<14THENPRINTBL$
380 K=0:FORJ=1TO26:IFOB(J)=PLTHENK=1:M$=OB$(J):GOSUB1
382 NEXTJ
383 IFPL=30THENPOKE49151,64:GOTO390
384 POKE49151,0:IFK=0THENPRINT"NOTHING"
390 IFWG=0THEN410
391 IFCW=1ANDPL=3THEN1740
400 IFPL=37THENSC=SC+250:GOTO450
410 IFOB(11)=PLOROB(12)=PLOROB(13)=PLOROB(14)=PLTHEN1490
420 IFKEANDSP=1ANDPL=28THENM$=SP$+"CAPTAIN, I SENSE KLINGONS TO THE EAST. TOO MANY FOR JUST HAND PHASERS.":GOSUB1
430 IFKEANDPL=30THENM$="YOU'RE SURROUNDED BY KLINGONS, AND HAVE TIME FOR ONE ACTION BEFORE THEY FIRE!":GOSUB1:GOTO1540
450 GOSUB1560:PRINT:CM$="":INPUT"COMMAND";CM$:IFCM$=""THEN300
451 CM$=CM$+"          "
460 VB$=LEFT$(CM$,3):NN$="":FORI=1TOLEN(CM$)-4:IFMID$(CM$,I,1)=" "THENNN$=MID$(CM$,I+1,4):I=255
470 NEXTI:VB=0:FORJ=1TO25:IFVB$=VB$(J)THENVB=J
480 NEXTJ:IFVB=0THENPRINT:PRINT"CAN YOU REPEAT THAT, "CP$"?":GOTO450
490 IFVB=22THEN1630
491 IFVB=23THEN1660
492 IFVB=24THEN770
493 IFVB=25THEN780
500 NN=-1:FORJ=0TO26:IFNN$=NN$(J)THENNN=J
510 NEXTJ:IF(NN=-1ANDVB>12)ANDVB<>15THENPRINT"CAN YOU REPEAT THAT, CAPTAIN?":GOTO450
520 ONVBGOTO530,530,530,530,530,530,560,690,720,1600,300,790,810,860,900,970,1080,1140,1230,1390,1470,770,780
530 IFTT>2ANDPL=4ANDWG=1THENM$=TM$+" FOR THIS PLANET'S GRAVITY.":GOSUB1:PRINTDR$:GOTO450
540 IFDM(PL,VB-1)>0THENPL=DM(PL,VB-1):NM=NM+1:GOTO300
550 PRINTCD$:SOUND80,8:GOTO450
560 SC=SC-10:IFPL=4THEN600
570 IFSP<>1THENPRINTPF$:GOTO450
571 PRINTSP$
572 ONPLGOSUB580,580,590,1590,580,580,610,630,580,580,640,640,640,610,580,580,580,580,580,580
573 IFPL>20THENONPL-20GOSUB650,580,580,580,580,580,610,660,580,580,680,670
574 GOTO450
580 M$="I SEE NOTHING UNUSUAL, CAPTAIN.":GOSUB1:RETURN
590 M$="SAY 'ENERGIZE', "+CP$:GOSUB1:RETURN
600 PRINTPF$:GOTO450
610 KH=0:FORJ=11TO14:IFOB(J)=PLTHENKH=1:NEXT
611 NEXT
620 IFKH=1THENM$="I SUGGEST YOU FIRE A PHASER!":GOSUB1:RETURN
621 GOTO580
630 IFOB(15)=8THENM$="DO YOU THINK THIS CREATURE IS USEFUL?":GOSUB1:RETURN
631 GOTO580
640 PRINT"SHOULD I PRESS THE BUTTON?":RETURN
650 M$="ALMOST ANY EQUIPMENT IS AVAILABLE.":GOSUB1:RETURN
660 M$="WE NEED SOMETHING TO FRIGHTEN THEM AWAY":GOSUB1:RETURN
670 IFOB(1)=32THENM$="THE KLINGON PHRASE FOR 'DO YOU SURRENDER?' IS 'TRISAETI'":GOSUB1:RETURN
680 M$="I SUGGEST WE CONSULT THE SHIP'S MANUAL.":GOSUB1:RETURN
690 CLS:PRINT"YOU ARE CARRYING:":K=0:FORJ=15TO26:IFOB(J)=0THENPRINTOB$(J):K=1
700 NEXTJ:IFK=0THENPRINT"NOTHING"
710 GOTO450
720 M$="YOUR SCORE SO FAR IS"+STR$(9*(SC-3*NM)):GOSUB1:GOTO450
730 IFPL<>4ANDPL<33ANDWG=1ANDCW=0THEN735
731 GOTO740
735 M$=SP$+"THE ENTERPRISE IS SAVED. BUT WITHOUT THE CREW, OUR MISSION IS A FAILURE.":GOSUB1
740 GOSUB350:PRINT"THIS ADVENTURE IS OVER."
750 PRINT:PRINT"TRY AGAIN? (y/n)":GOSUB1720
751 IFZZ$="Y"THENRUN
752 END
760 RETURN
770 BP=1:GOTO300
780 BP=0:GOTO300
790 VB$=LEFT$(NN$,3):VB=0:FORJ=1TO6:IFVB$=VB$(J)THENVB=J
800 NEXTJ:IFVB=0THENFORJ=1TO1:GOTO480
801 GOTO530
810 IFOB(NN)=0THENPRINT:PRINT"YOU ALREADY HAVE THAT.":GOTO450
820 IFNN<15THENPRINTCD$:GOTO450
830 IFOB(NN)=PLANDTT<5THENTT=TT+1:OB(NN)=0:GOTO300
840 IFOB(NN)=PLTHENPRINTTM$:PRINTDR$:GOTO450
850 PRINT:PRINT"I DON'T SEE THAT HERE.":GOTO450
860 IFOB(NN)<>0THENPRINT:PRINT"YOU DON'T HAVE THAT.":GOTO450
870 IFNN=16ANDOB(16)=0THENPRINTSH$:OB(16)=4:SC=SC-150:TT=TT-1:GOTO450
880 IFNN=15ANDOB(15)=0THENOB(15)=8:TT=TT-1:SC=SC-50:PRINT"O.K. BUT IT RAN AWAY!":GOTO450
890 OB(NN)=PL:TT=TT-1:GOTO300
900 IF(PL=3ANDSP=1ANDNN=0)THEN905
901 GOTO910
905 CLS:PRINT:M$=SP$+"AYE AYE, "+CP$:GOSUB1:PRINT:PRINT" ";:FORI=1TO25:PRINT"*";
907 FORJ=1TO10:NEXTJ,I:GOSUB350:PL=4:SP=0:GOTO300
910 IFNN=0ANDTT>2THENPRINTTM$:PRINTDR$:GOTO450
920 IFNN=0THENIFOB(20)=0THEN925
921 GOTO930
925 IFNN=0ANDSP=1THEN927
926 GOTO930
927 CLS:PRINT:M$=SP$+" BEAMING YOU ABOARD, CAPTAIN.":GOSUB1:PRINT:PRINT" ";:FORI=1TO25:PRINT"*";
928 FORJ=1TO10:NEXTJ,I:GOSUB350:PL=3:SP=1:GOTO300
930 IFNN=0ANDOB(20)<>0ANDSP=1THENPRINT:M$="O.K. BUT WITHOUT THE COMMUNICATOR, YOU ARE BEAMED INTO DEEP SPACE.":GOSUB1:GOTO730
940 IFPL=3THENIFNN=0ANDSP=0THENPRINT:M$="NEED SOMEONE FOR THE CONTROLS?":GOSUB1:GOTO450
950 IFPL=32THEN955
951 GOTO960
955 IFNN$="TRIS"THEN957
956 GOTO960
957 IFOB(1)<>32THEN960
958 CLS:PRINT:M$="THE COMMANDER SURRENDERS AND STATES THAT THE CREW IS UNHARMED ON THE PLANET'S SURFACE.":GOSUB1
959 M$="HE WILL SUPPLY TRANSPORTER COORDINATES AND THEN GO TO THE BRIG.":GOSUB1:OB(1)=26:SC=SC+150:GOTO450
960 M$="YOU SAID IT, BUT "+NI$:GOSUB1:GOTO450
970 PRINT:IFOB(NN)<>0THENPRINTCD$:GOTO450
980 TT=TT-1:IFNN=16THENPRINT"O.K.":PRINTSH$:SC=SC-150:OB(16)=4:GOTO450
990 IFNN>16THENPRINT"O.K.":SC=SC-50:IFSP=1THENM$=SP$+" A MOST ILLOGICAL MOVE, "+CP$:GOSUB1
1000 IFNN>16THENOB(NN)=PL:GOTO450
1010 FORI=11TO14:IFOB(I)=PLTHEN1015
1011 NEXTI:GOTO1020
1015 PRINT:M$="THE KLINGON WAS SO AFRAID OF THE TRIBBLE THAT HE RAN AWAY!":GOSUB1
1016 OB(I)=-1:OB(15)=PL:SC=SC+75:I=15
1017 NEXTI
1020 IFPL>32THENOB(15)=8:M$="BUT THE TRIBBLE RAN AWAY, TOO!":GOSUB1:GOTO450
1030 IFOB(1)=PLTHENM$="THE KLINGON COMMANDER IS VERY BRAVE AND HOLDS HIS GROUND.":GOSUB1:OB(15)=PL:GOTO450
1040 IFPL=30ANDKETHENCLS:PRINT:M$="WHEN THE KLINGONS SEE THE TRIBBLE, THEY RUN AWAY IN ALL DIRECTIONS!":GOSUB1
1050 IFPL=30THEN1055
1051 GOTO1060
1055 IFKETHENIFSP=1THENM$=SP$+"EXCELLENT, CAPTAIN! YOU REMEMBERED HOW FRIGHTENED THEY ARE OF TRIBBLES!":GOSUB1
1060 IFPL=30ANDKETHENKE=0:OB(15)=30:GOTO450
1070 OB(15)=PL:GOTO450
1080 SC=SC-20:FORI=11TO14:IFOB(I)<>PLTHEN1120
1081 PRINT:M$="THE KLINGON HAS BEEN REMOVED.":GOSUB1:SC=SC+100
1082 IFPL>32THENM$="BUT YOUR PHASER VAPORIZED!":GOSUB1:OB(19)=-1:TT=TT-1
1090 IFOB(19)<>0THENPRINT:M$=CD$+" YET.":GOSUB1:GOTO450
1100 SC=SC-20:FORI=11TO14:IFOB(I)<>PLTHEN1120
1101 PRINT:M$="THE KLINGON HAS BEEN REMOVED.":GOSUB1:SC=SC+100
1102 IFPL>32THENM$="BUT YOUR PHASER VAPORIZED!":GOSUB1:OB(19)=-1:TT=TT-1
1110 OB(I)=-1:I=15:NEXTI:GOTO450
1120 NEXTI:IFOB(1)=PLTHEN1125
1121 GOTO1130
1125 PRINT:M$="THE COMMANDER HAS BEEN REMOVED.":GOSUB1:IFSP=1THEN1127
1126 OB(1)=-1:GOTO450
1127 M$=SP$+"HIGHLY ILLOGICAL TO KILL AN UNARMED MAN, CAPTAIN.":GOSUB1:OB(1)=-1:GOTO450
1130 PRINT:PRINT"O.K.":IFSP=1THENM$="BUT WHY WASTE VALUABLE PHASER CHARGE, CAPTAIN?":GOSUB1:GOTO450
1131 GOTO450
1140 IFNN<2ORNN>4THENPRINT"DO WHAT, CAPTAIN?":GOTO450
1150 IFPL<11ORPL>13THENPRINT"WHAT BUTTON, CAPTAIN?":GOTO450
1160 SC=SC-50:IFPL=12ANDKETHEN1165
1161 GOTO1170
1165 CLS:PRINT:M$=CR$+"SENSORS SHOW ONE HUMANOID LIFE FORM ON BOARD, ONE VULCAN, AND MANY KLINGONS.":GOSUB1:GOTO450
1170 IFPL=12THENPRINT:M$=CR$+" SENSORS INDICATE ONE HUMAN AND ONE VULCAN ABOARD.":GOSUB1:PRINT:GOTO450
1180 IFPL=13THEN1185
1181 GOTO1190
1185 CLS:M$=CR$+EN$+" IS IN A DECAYING ORBIT AROUND THE CLASS 'M' PLANET TIERAS-80. "
1186 M$=M$+"IT HAS AN OXYGEN-NITROGEN ATMOSPHERE, AND IS RICH IN CRYSTALLITE MINERALS.":GOSUB1
1190 IFPL=13THENIFDETHENM$="IMPULSE ENGINES MUST BE FIRED SOON, OR WE'LL BURN UP IN THE ATMOSPHERE.":GOSUB1
1200 IFPL=13THEN450
1210 IFPL=11THEN1212
1211 PRINTNI$:GOTO450
1212 IFSH=0ORCR=0THEN1211
1215 CLS:M$="THE SHIP SHAKES VIOLENTLY... STABLE ORBIT HAS BEEN ACHIEVED!":GOSUB1
1220 DE=0:TD=1:WG=1:OB(11)=35:OB(12)=39:OB(13)=40:OB(14)=41:DM(4,0)=36:DM(4,1)=34:DM(4,2)=0:DM(4,3)=41
1221 SC=SC+200:PRINT:GOTO450
1230 SC=SC-5:IFNN=9AND(INT(PL/5)*5)=PLANDPL<26THEN1235
1231 GOTO1240
1235 CLS:PRINT:M$="IT SAYS: FEDERATION STARSHIP ENTERPRISE -- NCC 1701 --":GOSUB1
1236 PRINT"DECK";PL/5;"- ";DD$(PL/5):PRINT:GOTO450
1240 IFNN=22THEN1320
1241 IFNN=17THEN1260
1250 PRINT"READ WHAT, CAPTAIN?":GOTO450
1260 IFOB(17)<>0THEN1250
1270 IFPL=11THENM$="CHECK THE DILITHIUM CRYSTALS AND CONTROLS IN ENGINEERING.":GOSUB1:GOTO450
1280 IFPL=21ANDOB(21)=PLTHENM$="THIS SHUNT IS USED IN THE ENGINEERING CONTROL MECHANISM.":GOSUB1:GOTO450
1290 IFPL=31THENM$="INSERT CRYSTALS AT THE POWER STATION":GOSUB1:GOTO450
1300 IFPL=32THENM$="INSTALL THE SHUNT IN THE CONTROL PANEL.":GOSUB1:GOTO450
1310 M$="IT DOESN'T MENTION ANYTHING OF VALUE.":GOSUB1:GOTO450
1320 IFOB(22)<>0THEN1250
1330 IFPL=4ORPL>32THEN1360
1340 IFKE<>0OR(OB(11)>0ANDOB(11)<33)OR(OB(12)>0ANDOB(12)<33)OR(OB(13)>0ANDOB(13)<33)OR(OB(14)>0ANDOB(14)<33)THEN1345
1341 GOTO1350
1345 M$="THERE ARE STILL KLINGONS ON THE SHIP!":GOSUB1:GOTO450
1350 IFKE=0ANDOB(1)<>-1THENM$="IT INDICATES THAT ONLY ONE UNARMED KLINGON REMAINS ON THE SHIP.":GOSUB1:GOTO450
1360 M$="IT INDICATES ":IFCW=0THENM$=M$+"SEVERAL HUMANOID LIFE FORMS AND KLINGONS TO THE NORTHEAST... "
1370 IFOB(14)<>-1THENM$=M$+"ONE KLINGON TO THE WEST."
1380 GOSUB1:GOTO450
1390 IFPL<31THENPRINTCD$;" HERE":GOTO450
1400 IFPL=31THENIFOB(16)=0THENIFNN=16THENIFCRTHENPRINTNI$:GOTO450
1410 IFPL=31THEN1415
1411 GOTO1420
1415 IFOB(16)=0THEN1417
1416 GOTO1420
1417 IFNN<>16THEN1420
1418 IFCR=0THEN4000
1420 IFPL=32THENIFOB(21)=0THENIFNN=21THENIFSHTHENPRINTNI$:GOTO450
1430 IFPL=32THENIFOB(21)=0THENIFNN=21THENIFSH=0THEN4050
1440 PRINTCD$:GOTO450
1450 IFCRANDSHTHENTD=-17-RND(3)-RND(0)
1460 GOTO450
1470 IF(NN=18ORNN=10)ANDOB(10)=26ANDOB(18)=0ANDPL=26THEN1475
1471 GOTO1480
1475 SC=SC+200:M$="O.K. SPOCK AWAKENS AND SAYS-- I AM WELL ENOUGH TO HELP YOU NOW.":GOSUB1:SP=1:OB(10)=-1:GOTO450
1480 PRINT"NOT NOW, CAPTAIN.":GOTO450
1490 IFOB(15)<>0THENIFOB(19)<>0THENGOSUB350:M$="WITHOUT A PHASER, YOU MUST SURRENDER!":GOSUB1:GOTO730
1500 GOSUB1520:IFC1$=VB$(16)ANDC2$=NN$(15)ANDOB(15)=0THENTT=TT-1:GOTO1010
1510 IFC1$=VB$(17)ANDC2$=NN$(19)ANDOB(19)=0THEN1100
1511 GOTO1550
1520 PRINT:CM$="":INPUT"COMMAND";CM$:CM$=CM$+"          ":FORI=1TOLEN(CM$)
1521 IFMID$(CM$,I,1)=" "THENC1$=LEFT$(CM$,3):C2$=MID$(CM$,I+1,4):I=99
1530 NEXTI:RETURN
1540 GOSUB1520:IFC1$=VB$(16)ANDC2$=NN$(15)ANDOB(15)=0THENTT=TT-1:GOTO1010
1541 GOTO1550
1550 PRINT:M$="TOO SLOW, CAPTAIN. YOU HAVE BEEN CAPTURED AND THE ENTERPRISE IS LOST!":GOSUB1:GOTO730
1560 IFTD>0THENRETURN
1561 TD=TD+1+RND(0)
1570 IFTD>0THEN1575
1571 GOTO1580
1575 CLS:PRINT:PRINT"TOO LATE, CAPTAIN!":GOSUB350
1576 PRINT:M$="THE ENTERPRISE WILL SOON BURN UP IN THE PLANET'S ATMOSPHERE!":GOSUB1:GOSUB350
1577 PRINT:PRINT"GOODBYE, CAPTAIN!":GOSUB350:PRINT:PRINT:GOTO730
1580 C1=2:C2=2:C3=-TD:GOSUB8:IFSP=1THENM$=SP$+I$+" MINUTES UNTIL ORBIT DECAYS.":GOSUB1
1590 RETURN
1600 CLS:PRINT:M$="THIS ADVENTURE IS INTERRUPTED.":GOSUB1:PRINT
1601 PRINT"CONTINUE GAME LATER (y/n)?";:GOSUB1720:IFZZ$="N"THENEND
1610 GOSUB1640:END
1630 CLS:PRINT:PRINT:M$="SAVE THE PARAMETERS FOR THIS LOCATION (y/n)? ":GOSUB1:GOSUB1720:IFZZ$="N"THEN300
1632 GOSUB1640:GOTO300
1640 PRINT:PRINT:PRINT"WAIT...":SOUND1,1
1650 OB(27)=WG:OB(28)=CW:OB(29)=SC:OB(30)=NM:OB(31)=CC:OB(32)=SP:OB(33)=PL:OB(34)=KE:OB(35)=CR:OB(36)=SH
1651 OB(37)=DE:OB(38)=TT:OB(39)=TD:OB(40)=DM(4,0):OB(41)=DM(4,1):OB(42)=DM(4,2):OB(43)=DM(4,3)
1652 CSAVE*OB,"GAL1DATA":RETURN
1660 CLS:PRINT:PRINT
1661 M$="WANT TO START AGAIN FROM THE LAST PLACE WHERE YOU 'SAVED' LOCATION PARAMETERS (y/n)?":GOSUB1
1662 GOSUB1720:IFZZ$="N"THEN300
1663 GOSUB1670:GOTO300
1670 PRINT:PRINT"WAIT...":SOUND1,1
1671 CLOAD*OB,"GAL1DATA"
1672 WG=OB(27):CW=OB(28):SC=OB(29):NM=OB(30):CC=OB(31):SP=OB(32):PL=OB(33):KE=OB(34):CR=OB(35):SH=OB(36)
1673 DE=OB(37):TT=OB(38):TD=OB(39):DM(4,0)=OB(40):DM(4,1)=OB(41):DM(4,2)=OB(42):DM(4,3)=OB(43):RETURN
1680 CLS:PRINT"CAPTAIN'S LOG, STARDATE 4295.3":PRINT
1681 M$="YOU ARE CAPTAIN OF THE STARSHIP ENTERPRISE AND AWOKE MOMENTS AGO TO FIND AN EMPTY SHIP. "
1682 M$=M$+"NO ONE RESPONDS-- SPOCK, CHEKHOV, SULU, SCOTTY, UHURU... IS THIS THE KLINGON'S FINAL VICTORY?":GOSUB1
1690 RETURN
1700 PRINT:PRINTYY$;"BEGIN ...";:GOTO1720
1710 PRINT:PRINTYY$;"CONTINUE ...";
1720 ZZ$=INKEY$:IFZZ$=""THEN1720
1721 IFZZ$=CHR$(13)THENZZ$="Y"
1730 IFZZ$<>"Y"ANDZZ$<>"N"THENSOUND1,1:GOTO1720
1731 RETURN
1740 GOSUB350:CLS:PRINT"CONGRATULATIONS, CAPTAIN !!":M$="YOU HAVE DEFEATED THE KLINGONS, REPAIRED THE "
1741 M$=M$+"WARP DRIVE POWER, AND RETURNED THE CREW SAFELY TO THE SHIP.":GOSUB1:GOSUB1710:CLS
1750 PRINT:M$="UHURU REPORTS THAT STAR FLEET COMMAND HAS JUST SIGNALLED THAT YOU ARE TO "
1751 M$=M$+"RETURN WITH THE ENTERPRISE FOR A WELL EARNED SHORE LEAVE ON TERRA SATEY, "
1752 M$=M$+"AFTER WHICH YOU ARE TO BE PROMOTED TO COMMODORE.":GOSUB1:GOSUB1710:CLS
1760 PRINT:PRINT:PRINT:PRINT"YOU MISSION SCORE IS";9*(SC-3*NM):GOTO750
1780 CLS:M$="TO MOVE TO ANOTHER LOCATION WHICH IS NORTH, SOUTH, EAST, WEST, UP OR DOWN "
1781 M$=M$+"FROM YOUR PRESENT LOCATION, TYPE ONLY THE DIRECTION AND PRESS <ENTER>.":GOSUB1
1790 PRINT:M$="TO ACCOMPLISH AN ACTION SUCH AS READING A SIGN OR OPENING A DOOR, TYPE A VERB AND A NOUN; "
1791 M$=M$+"THEN PRESS <ENTER> LIKE:":GOSUB1:PRINT:PRINT"OPEN DOOR <ENTER>":GOSUB1710
1800 CLS:M$="THE COMPUTER WILL NOT ACCEPT AN INVALID COMMAND. IF IT DOES NOT UNDERSTAND "
1801 M$=M$+"WHAT YOU WANT IT TO DO, TRY DIFFERENT WORDS.":GOSUB1
1810 PRINT:M$="IF MR. SPOCK IS WITH YOU, HE MAY BE ABLE TO HELP. TYPE: HELP <ENTER>."
1811 M$="TO SEE WHAT YOU'RE CARRYING, TYPE: INV <ENTER>.":GOSUB1:M$="TO SEE YOUR SCORE, TYPE: SCORE <ENTER>.":GOSUB1
1812 GOSUB1710
1820 CLS:M$="TO SAVE YOUR CURRENT LOCATION PARAMETERS (WHERE YOU ARE, WHAT YOU HAVE, ETC), TYPE: "
1821 M$=M$+"SAVE <ENTER>.":GOSUB1:M$="TO STOP THE GAME, TYPE: QUIT <ENTER>.":GOSUB1
1830 M$="TO START OVER AGAIN FROM THE LAST PLACE WHERE YOU SAVED LOCATION PARAMETERS, TYPE: "
1831 M$=M$+"START <ENTER>.":GOSUB1:GOSUB1710:AG$="AGAIN ":RETURN
4000 M$="A LOW VIBRATION BEGINS... ":GOSUB1:GOSUB350
4010 C1=2:C2=2:C3=92+RND(7)+RND(0):GOSUB8
4011 M$="POWER LEVELS NOW AT "+I$+"%":GOSUB1:CR=-1:TT=TT-1:OB(16)=-1:OB(23)=-1:SC=SC+300:GOTO1450
4050 M$="A GREEN PANEL LIGHT GLOWS... ":GOSUB1:GOSUB350
4060 M$="AUXILIARY POWER IS NOW OPERATIONAL.":GOSUB1:OB(24)=-1:TT=TT-1:SH=-1:OB(21)=21:SC=SC+250:GOTO1450
