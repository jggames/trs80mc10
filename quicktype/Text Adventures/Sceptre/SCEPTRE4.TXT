0 CLS:CLEAR800:DIMZZ,CC,M$:GOTO100
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
100 GOSUB 8000
200 REM NEW ROOM
201 CLS
210 M$="YOU'RE "+R$(R):GOSUB1
220 PRINT
221 IF R=1 THEN M$="P-U!":GOSUB1
222 IF R=3 THEN M$="SSHHH!":GOSUB1
223 IF R=6 THEN M$="IF YOU LEAVE WITHOUT THE SCEPTRE PIECE YOU LOSE!":GOSUB1
224 IF R=10 AND L(13)<>-1 THEN M$="YOU HAVE LEFT WITHOUT THE SCEPTRE PIECE THEREFORE YOU LOSE.":GOSUB1:END
225 IF R=10 AND L(13)=-1 THEN M$="YOU HAVE THE PIECE! YOU WIN! LOOK SOON FOR SCEPTREQUEST II!":GOSUB1:END
240 V$="YOU SEE:  "
250 FOR I=1 TO N9:IF L(I)=R THEN V$=V$+O$(I)+", "
260 NEXT I
300 M$=LEFT$(V$,LEN(V$)-2)+".":GOSUB1
310 V$="YOU CAN GO:  "
320 FOR I=1 TO 4
330 IF R(R,I)<>0 THEN V$=V$+D$(I)+", "
340 NEXT I
341 M$=LEFT$(V$,LEN(V$)-2)+".":GOSUB1
400 PRINT"NOW WHAT";
410 INPUT A$
420 N$="":V$="":V=0:N=0:J=0
430 L=LEN(A$):FOR I=1 TO L
440 IF MID$(A$,I,1)=" " THEN V$=LEFT$(A$,I-1):J=I:I=L
450 NEXT:IF J=0 THEN V$=A$:GOTO 550
460 FOR I=L TO J STEP -1
470 IF MID$(A$,I,1)=" " THEN N$=MID$(A$,I+1):I=J
480 NEXT
500 T$=LEFT$(N$,4)
510 FOR I=1 TO N9
520 IF T$=N$(I) THEN N=I:I=N9
530 NEXT
550 T$=LEFT$(V$,3)
560 FOR I=1 TO V9
570 IF T$=V$(I) THEN V=I:I=V9
580 NEXT
600 IF V=0 THEN M$="I DON'T KNOW THAT VERB.":GOSUB1:GOTO 400
610 IF V<9 THEN 1200
620 ON V-8 GOTO 1000,1400,1600,1800,2000,2000,2000,2200,2400,2400,2600,2800
630 ON V-20 GOTO 3000,3200,3400,3600,3800,4000,4000
640 GOTO 600
900 IF N$="" THEN M$="YOU NEED TO SAY WHAT TO "+V$+".":GOSUB1:GOTO 400
910 M$="WHAT IS AN "+N$+"?":GOSUB1:GOTO 400
930 NX=0:I=L(N)
940 IF I<>R AND I<>-1 THEN M$="I DON'T SEE IT HERE.":GOSUB1:NX=1
950 RETURN
1000 IF N$="" THEN M$="GO WHERE?":GOSUB1:GOTO 400
1010 IF N=0 THEN V$=N$:GOTO 550
1020 M$="USE COMPASS DIRECTIONS.":GOSUB1:GOTO 400
1200 REM MOVEMENT
1210 V=INT((V+1)/2):I=R(R,V)
1220 IF I>0 THEN R=I:GOTO 200
1221 IF R=7 AND V=3 THEN M$="THE WALL BLOCKS THE WAY.":GOSUB1
1230 M$="YOU CAN'T GO THAT WAY.":GOSUB1
1240 GOTO400
1400 REM INV
1410 Z=0:FOR I=1 TO N9
1420 IF L(I)=-1 THEN M$="  "+O$(I):GOSUB1:Z=Z+1
1430 NEXT I
1440 IF Z=0 THEN M$="  NOTHING":GOSUB1
1450 GOTO 400
1600 REM TAKE
1610 IF N=0 THEN 900
1620 IF L(N)=-1 THEN M$="YOU HAVE IT ALREADY!":GOSUB1:GOTO 400
1630 GOSUB 930:IF NX=1 THEN 400
1640 IF A(N)=1 THEN M$="YOU CAN'T TAKE THAT.":GOSUB1:GOTO 400
1650 L(N)=-1
1660 M$="TAKEN.":GOSUB1:GOTO 400
1800 REM DROP
1810 IF N=0 THEN 900
1820 IF L(N)<>-1 THEN M$="YOU DON'T HAVE IT!":GOSUB1:GOTO 400
1822 IF N=6 THEN M$="YOU ONLY SPILLED A LITTLE BIT":GOSUB1:GOTO 400
1830 L(N)=R
1840 M$="DROPPED":GOSUB1:GOTO 400
2000 REM LOOK,EXA,SEARCH
2010 IF N=0 THEN 200
2020 GOSUB 930:IF NX=1 THEN 400
2030 M$=ED$(N):GOSUB1
2040 IF R=1 AND N=1 THEN M$="YOU SEE A SMALL COIN.":GOSUB1:L(9)=1
2050 GOTO 400
2200 REM OPEN
2210 IF N=0 THEN 900
2220 GOSUB 930:IF NX=1 THEN 400
2230 IF N=14 THEN2235
2231 GOTO2240
2235 M$="CREAK. IT OPENS, ALLOWING WAY TO THE EAST.":GOSUB1:R(6,3)=10:ED$(14)="THE GATE IS OPEN.":GOTO 400
2240 IF N=11 THEN M$="HOW? HE'LL JUST GET MAD.":GOSUB1:GOTO 400
2250 IF N=10 THEN M$="BLASPHEMY! APPOLO STRIKES YOU DOWN!":GOSUB1:END
2260 M$="YOU CAN'T OPEN THAT.":GOSUB1:GOTO 400
2400 REM EAT/DRINK
2410 IF N=0 THEN 900
2411 GOSUB 930:IF NX=1 THEN 400
2412 IF N=3 THEN10000
2413 IF N=4 THEN M$="YUM!":GOSUB1:L(4)=90:GOTO 400
2414 IF N=6 THEN M$="AK! THE WATER IS POISON, ACID, NITROGLYCERINE COMBINED! YOU'RE DEAD!":GOSUB1:END
2420 M$="YOU CAN'T DIGEST THAT.":GOSUB1:GOTO 400
2430 IF N=11 THEN M$="THAT'S DISGUSTING, AND IT WON'T WORK EITHER.":GOSUB1:GOTO 400
2600 REM VOMIT
2610 M$="BLARRRRRRGGGGGGG!!!!":GOSUB1
2620 IF R<>1 THEN M$="YOU ARE IMMEDIATELY ARRESTED FOR LITTERING. THE END.":GOSUB1:END
2630 M$="YOU ADD TO THE VOMIT ALREADY THERE.":GOSUB1:GOTO 400
2800 REM FILL(OH BOY)
2810 IF N=0 THEN 900
2820 GOSUB 930:IF NX=1 THEN 400
2830 IF N=3 AND N$(3)="GLAS" AND R=5 THEN10010
2840 IF N=3 AND N$(3)="BEER" AND R=5 THEN M$="YOU SHOULD MAYBE DRINK THE BEER FIRST.":GOSUB1:GOTO 400
2850 IF R=1 THEN M$="NO! NO WAY! FORGET IT! YOU'RE NOT MAKING A MESS OF MY CITY!":GOSUB1:GOTO 400
2860 M$="YOU CAN'T FILL ANYTHING IN THIS SITUATION.":GOSUB1:GOTO 400
3000 REM READ
3010 IF N=0 THEN 900
3020 GOSUB 930:IF NX=1 THEN 400
3030 IF N=7 THEN10020
3040 IF N=8 THEN 3045
3041 GOTO 3050
3045 M$="HI! THIS IS THE DESIGNER OF THIS GAME.":GOSUB1
3046 M$="WELCOME TO MY CITY, WHICH HAPPENS TO BE THE HIDING PLACE OF THE WIZARD'S FIRST PIECE!":GOSUB1:GOTO 3100
3050 M$="THERE'S NO WRITING":GOSUB1:GOTO 400
3100 M$="IF YOU FIND IT, LEAVE THE CITY WITH IT TO WIN. DON'T FAIL ME AND GOOD LUCK!":GOSUB1:GOTO 400
3200 REM THROW
3210 IF N=0 THEN 900
3220 GOSUB 930:IF NX=1 THEN 400
3230 IF N=3 AND BG=99 AND R=7 THENGOSUB10030
3235 IFR(7,3)=8THENM$="A SECRET ROOM TO THE EAST IS REVEALED.":GOSUB1:O$(11)="A DEAD WALL":ED$(11)="IT'S DEAD."
3236 M$="IT REBOUNDS AND LANDS HARMLESSLY WHERE  IT BEGAN.":GOSUB1:GOTO 400
3400 REM GIVE
3410 IF N=0 THEN 900
3420 GOSUB 930:IF NX=1 THEN 400
3430 IF R<>4 AND R<>7 THEN M$="TO WHO?":GOSUB1:GOTO 400
3440 IF R=7 THEN M$="THE WALL REFUSES TO BE BRIBED.":GOSUB1:GOTO 400
3450 IF R=4 AND N<>9 THEN M$="THE PRIEST INSISTS ON MONEY.":GOSUB1:GOTO 400
3460 M$="THE PRIEST SMILES AND TAKES YOUR COIN. HE POINTS WESTWARD.":GOSUB1:L(9)=90:R(4,4)=3:GOTO 400
3600 REM TALK
3610 IF N=0 THEN M$="BLAH. BLAH.":GOSUB1:GOTO 400
3620 IF R<>4 AND R<>7 THEN M$="BLAH. BLAH. PASSERBYS LOOK AT YOU ODDLY.":GOSUB1:GOTO 400
3630 IF R=4 AND N=12 THEN M$="'PAY TO PRAY, SIR.'":GOSUB1:GOTO 400
3640 IF R=7 AND N=11 THEN M$="'I AM THE GUARDIAN TO SCEPTRE PIECE ONE!'":GOSUB1:GOTO 400
3650 M$="ARE YOU POSSIBLY INSANE?":GOSUB1:GOTO 400
3800 REM PRAY
3810 IF R<>3 THEN10040
3820 M$="APPOLO SPEAKS AND GIVES YOU ENLIGHTENMENT:":GOSUB1
3821 M$="TO FIND THE SCEPTRE PIECE, THERE IS A SECRET EXIT SOUTH OF HERE!":GOSUB1:R(3,2)=7:GOTO 400
4000 REM MOVE/PUSH
4010 IF N=0 THEN 900
4020 GOSUB 930:IF NX=1 THEN 400
4030 IF N=11 THEN M$="YOU DARE LAY YOUR FILTHY HANDS ON ME?! DIE!!!!!! ZAP!!!!!!":GOSUB1:END
4040 IF A(N)=1 THEN M$="IT WON'T MOVE":GOSUB1:GOTO 400
4050 M$="IT MOVES.":GOTO 400
8000 REM 
8005 CLS
8010 ?TAB((32-14)/2)"SCEPTREQUEST I"
8020 ?
8030 M$="YOU ARE GIGLAMESHT, THE FAMOUS GREEK ADVENTURER, IN SEARCH OF THE FAMOUS ATHENA ROYAL SCEPTRE. "
8031 M$=M$+"IT IS SAID THE SCEPTRE WAS BROKEN BY AN EVIL WIZARD AND HIDDEN IN PLACES ALL OVER GREECE.":GOSUB1
8032 M$="IT IS ALSO SAID THAT WHOEVER FINDS THE SCEPTRE PIECES CAN BECOME RULER OF ALL THE GREEK EMPIRE. "
8033 M$=M$+"CAN YOU FIND THE SEVEN BROKEN PIECES? SOLVE MY SEVEN GAMES? WE SHALL SEE.":GOSUB1
8050 FOR X=1 TO 10000
8060 NEXT X
8070 CLS
9000 R9=10
9010 DIM R$(R9),R(R9,4)
9020 FOR I=1 TO R9
9030 READ R$(I)
9040 FOR J=1 TO 4
9050 READ R(I,J)
9060 NEXT J
9062 NEXT I
9070 DATA "IN THE VOMITORIUM",0,0,2,0
9080 DATA "IN A RESTAURANT",0,5,0,1
9090 DATA "IN APPOLO'S SHRINE",0,0,4,0
9100 DATA "AT APPOLO'S TEMPLE",0,0,5,0
9110 DATA "IN VATHEN'S CENTRAL SQUARE",2,9,6,4
9120 DATA "AT VATHEN'S CITY GATE",0,0,0,5
9130 DATA "IN A SECRET ROOM",3,0,0,0
9140 DATA "IN A SECRET ROOM",0,0,0,7
9150 DATA "IN THE CITY LIBRARY",5,0,0,0
9160 DATA "OUTSIDE VATHEN",0,0,0,0
9450 DIM D$(4)
9460 FOR I=1 TO 4:READ D$(I)
9461 NEXT I
9470 DATA "NORTH","SOUTH","EAST","WEST"
9500 N9=14
9510 DIM O$(N9),ED$(N9),L(N9),A(N9),N$(N9)
9520 FOR I=1 TO N9
9530 READ O$(I),ED$(I),L(I),A(I),N$(I)
9540 NEXT
9550 DATA "MUCH VOMIT","IT IS GREEN AND DISGUSTING",1,1,"VOMI"
9560 DATA "A LARGE TRENCH","THE VOMIT IS IN IT",1,1,"TREN"
9570 DATA "A GLASS OF BEER","IT LOOKS GOOD",2,2,"BEER"
9580 DATA "A ROAST CHICKEN","IT LOOKS APPETIZING",2,2,"CHIC"
9590 DATA "A PLAYING FOUNTAIN","IT OVERFLOWS WITH WATER",5,1,"FOUN"
9600 DATA "FULL OF WATER","THERE ISN'T MUCH",5,1,"WATE"
9610 DATA "A BRASS PLAQUE","IT HAS WRITING ETCHED ON IT",5,1,"PLAQ"
9620 DATA "AN OLD BOOK","IT LOOKS AGES OLD",9,2,"BOOK"
9630 DATA "A GOLD COIN","IT HAS THE SYMBOL OF APPOLO'S TEMPLE ON IT",0,2,"COIN"
9640 DATA "A MARBLE ALTAR","IT IS APPOLO'S PERSONAL ONE",3,1,"ALTA"
9650 DATA "AN ODD-LOOKING WALL"
9651 DATA"IT'S NOT MADE OF STONE-- BUT FLESH! IT SPEAKS! 'I AM THE GAURDIAN OF THE FIRST SCEPTRE PIECE! BEHOLD ME!'"
9652 DATA 7,1,"WALL"
9660 DATA "AN OLD PRIEST","HE LOOKS KINDLY  BUT GREEDY",4,1,"PRIE"
9670 DATA "THE SCEPTRE PIECE!","IT SHINES OF GOLD AND SILVER",8,2,"SCEP"
9680 DATA "THE CITY GATE","IT IS CLOSED",6,1,"GATE"
9900 V9=27
9910 DIM V$(V9)
9920 FOR I=1 TO V9
9930 READ V$(I):NEXT I
9940 DATA "N","NOR","S","SOU","E","EAS","W","WES"
9950 DATA "GO","INV","TAK","DRO","LOO","EXA","SEA","OPE","EAT","DRI","VOM","FIL","REA","THR","GIV","TAL","PRA"
9960 DATA "MOV","PUS"
9990 R=9:BG=1
9999 RETURN
10000 M$="YOU DRINK THE BEER, LEAVING AN EMPTY GLASS.":GOSUB1
10001 O$(3)="AN EMPTY GLASS":ED$(3)="IT'S EMPTY":N$(3)="GLAS":GOTO 400
10010 M$="YOU FILL THE GLASS WITH WATER FROM THE FOUNTAIN.":GOSUB1:L(6)=-1:BG=99:ED$(3)="IT'S FULL.":GOTO 400
10020 M$="THE PLAQUE READS: WARNING! THIS WATER IS POISONOUS, ACIDIC AND EVERYTHING ELSE, SO WATCH OUT!":GOSUB1
10021 GOTO 400
10030 M$="THE BOTTLE OF WATER HITS THE WALL. HE SCREAMS IN PAIN AND DISSOLVES BEFORE YOUR EYES!":GOSUB1
10031 R(7,3)=8:RETURN
10040 M$="YOU GET DOWN ON YOUR KNEES AND PRAY, BUT NOTHING HAPPENS. MAYBE YOU SHOULD GO TO A SHRINE.":GOSUB1
10041 GOTO 400
