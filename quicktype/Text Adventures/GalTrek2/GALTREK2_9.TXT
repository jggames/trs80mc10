0 CLS:CLEAR224:DIMD(59,3),S$(59),N$(28),O$(11),L$(5),L(11),R(5),D$(1):C$=" SHUTTLECRAFT":GOTO46
1 Y=1:X=32:FORX=XTOYSTEP-1:I$=MID$(M$,X,1):IFI$<"!"THEN?MID$(M$,Y,X-Y):Y=X+1:X=Y+32:IFI$=""THENX=.
2 X=X+(X>255)*(X-255):NEXT:M$="":RETURN
3 M$="YOU GOT HIM!"
4 GOSUB1:GOTO198
5 PRINT"NOTHING":RETURN
6 GOSUB7:GOTO9
7 PRINT"hit a key"
8 A$=INKEY$:ON-(A$="")GOTO8:CLS:RETURN
9 GOTO80

10 RESTORE:FORI=1TO30:READM$:NEXT:IFY<6THENM$="THE ENTRANCE TO A TURBOLIFT":RETURN
12 IFY<11THENM$="A TURBOLIFT":RETURN
14 IFY<19THENM$="A CORRIDOR":RETURN
16 IFY<23THENM$="A VENTILATION DUCT":RETURN
18 IFY<44THENFORI=23TOY:READM$:NEXT:RETURN
20 IFY<47THENFORI=23TO44:READM$:NEXT:RETURN
22 FORI=23TO44:READM$:NEXT:FORI=47TOY:READM$:NEXT:IFF9=1ANDY=50THENM$="A CORRIDOR"
24 RETURN
25 DATAREA,GET,MOV,LOO,PRE,PUS,SAY,JUM,PUL,INV,UNB,LIG,UNL,SCA,GO ,PIC,OPE,CLO,WEA,REM,FIR,DRO,QUI,HEL,ENT,LOC,GIV,INS,DEA,ACT
26 DATASCIENCE STATION,HELM,ASTROGATION CONSOLE,COMBAT CONSOLE,COMMUNICATIONS CONSOLE,TRANSPORTER ROOM,GYMNASIUM,SICK BAY
28 DATAENVIRONMENTAL CONTROL,SCIENCE LAB,SHIP'S LIBRARY,STOREROOM,ARMORY,BRIG,HANGAR BAY,AIRLOCK,MAIN ENGINE CONTROL
30 DATAAUXILIARY ENGINE CONTROL,MAINTENANCE SHOP,THE GALILEO,LOST FOREVER IN A SANDSTORM,SANDY SURFACE OF TYCHO IV
34 DATAOASIS,ENTRANCE TO A BUILDING,TRANSPORTER ROOM OF A ROMULAN BATTLE CRUISER,ROMULAN COMMANDER'S STATEROOM
38 DATAA LARGE ROOM,CORRIDOR WITH STAIRS UP AND DOWN,THIS ISN'T A BRIG--IT'S A TORTURE CELL!,CORRIDOR WITH STAIRS UP
42 DATAROMULAN SECURITY SECTION,ROMULAN ENGINE ROOM,AN AIRLOCK,SHUTTLEBAY,ROMULAN SHUTTLECRAFT
44 DATABRIDGE,PERSONNEL SECTION,TECHNICAL DEPARTMENTS,SECURITY SECTION,ENGINEERING,A SIGN ON THE WALL
46 T$="A TABLE AND AN OPEN VENTILATION DUCT":B$="A BENCH AND AN OPEN VENTILATION DUCT":D$(0)="A CLOSED DOOR TO THE ":Y=59
48 D$(1)="AN OPEN DOOR TO THE ":GOSUB10:FORI=1TO5:READL$(I):NEXT:READA$:FORI=1TO5:S$(I)=A$:NEXT:FORI=29TO59:READS$(I):NEXT
52 DATAA TABLE,,SEVERAL CHAIRS AROUND A TABLE,,MANY BOOKS ON A TABLE,NEARLY ANYTHING REQUIRED,MANY WEAPONS ON A TABLE,
54 DATATHE SHUTTLECRAFT GALILEO,,,A WORKBENCH,MANY TOOLS ON A WORKBENCH,THE ASTROGATION CONSOLE,,,,DESERT
56 DATATHOUSANDS OF FLOWERS,A SIGN ON THE WALL,SEVERAL ARMED ROMULAN GUARDS,MANY GUARDS WITH DRAWN BLASTERS
58 DATASEVERAL ARMED ROMULANS WHO BLAST YOU,AN ARMED ROMULAN GUARD,,,SEVERAL ROMULAN GUARDS
62 DATATHE ROMULAN HYPER-DRIVE ENGINES,,A ONE-MAN SHUTTLECRAFT,A SIMPLE CONTROL PANEL
63 L=25:C=15:BL=1:Z=1:PS=1:RG=1:S$(24)="'FIRE IMPULSE ENGINES'":S$(25)="'ENGAGE WARP DRIVE'":S$(26)="'FIRE PHASERS'"
64 DATAVIALS OF VACCINE,A FLASHLIGHT,A TRICORDER,,,,,,,,
72 DATAVACC,FLAS,TRIC,MANU,COMM,SWIT,PHAS,SUIT,WREN,BLAS,DEVI,WALL,FLOW,TABL,BENC,DOOR,ENTE,COVE,LEVE,PLAN,GUAR,SIGN,BUTT
74 DATADUCT,SHUT,ENER,HANG,COOR,30,30,32,-1,-1,-1,-1,38,-1,-1,56
75 FORI=1TO11:READO$(I):NEXT:FORI=1TO28:READN$(I):NEXT:FORI=1TO11:READL(I):NEXT:FORX=1TO59:FORY=0TO3:READD(X,Y):NEXTY,X
80 IFL>59THENL=L-65:SOUND50,1
82 IFL=48THENH=1
84 IFL=32THENP=1
86 IFL=37THENP=0
88 IFL=39THENS=1
90 IFL=10ORL=38THENS=0
92 IFL=30ANDH=0THENM=1
94 IFL>48THENM=0
96 IFL=44ANDH=1THEN244
98 ID=1:IFL<19ORL>22THENID=0
102 Y=L:GOSUB10:M$="LOCATION: "+M$:GOSUB1:IFPANDL<43THENM$="SPOCK, "
104 IFMTHENM$=M$+"MCCOY, "
106 IFSTHENM$=M$+"SCOTTY, "
108 IF(PANDL<43)ORMORSTHENM$="ALSO HERE: "+LEFT$(M$,LEN(M$)-2):GOSUB1
110 IFL=53THEN710
112 IFL=43THENM$="WANDERING UNTIL YOU DIE OF THIRST":GOTO708
114 W=0:PRINT"YOU SEE:":IFL<19ORL>22THEN120
116 IFID=1ANDQ=0THENGOSUB5:GOTO298
118 IFIDTHENPRINT"HANDHOLDS FOR CLIMBING":GOTO180
120 IFL=24ORL=25ORL=26THENPRINT"A BUTTON LABELLED ":W=1
122 IFS$(L)<>""THENM$=S$(L):GOSUB1:W=1
124 IFL=17THENW=1:PRINTD$(DE)"NORTH"
126 IFL=38THENW=1:PRINTD$(DE)"SOUTH"
128 IFL=54THENW=1:PRINTD$(DR)"WEST"
130 IFL=57THENW=1:PRINTD$(DR)"EAST"
132 FORI=1TO11:IFL(I)=LTHENW=1:IFO$(I)<>""THENPRINTO$(I)
134 NEXT
136 IFL=56ANDL(11)=56THENPRINT"A CLOAKING DEVICE IS INSTALLED":IFZTHENPRINT"AND ACTIVATED"
138 IFL=56ANDL(11)ANDRG=1THENM$="AND IS WATCHED BY AN ARMED GUARD":GOSUB1
140 IFL>5ORRO=0THEN150
142 IFR(L)THENPRINT"AN ARMED ROMULAN GUARD":W=1:GOTO180
144 IFR(L)=0THEN150
146 PRINT"TOO SLOW, CAPTAIN!"
148 PRINT"THE ROMULAN GUARD BLASTED YOU!":GOTO710
150 IFL<>49THEN168
152 L(8)=57:O$(8)="A SPACE SUIT"
154 IFWSTHENPRINT"YOUR SPACE SUIT IS REMOVED AND":WS=0
156 HA=0:FORI=1TO11:IFL(I)=0THENHA=1:I=11
158 NEXT:IFHA=0THEN166
162 M$="YOU ARE TOLD TO DROP ALL YOU ARE CARRYING":GOTO4
166 L=50:RC=1:M$="YOU'RE TAKEN TO THE ROMULAN COMMANDER":GOSUB1:GOTO6
168 IFL=51ORL=55OR(L=50ANDRC=2)THEN:L=53:M$="THEY OVERPOWER YOU AND TAKE YOU TO THE BRIG":GOSUB1:GOTO6
170 ON-(L<>50ORRC<>1)GOTO178:M$="THE ROMULAN COMMANDER SAYS-- WE FINALLY MEET, CAPTAIN KIRK! "
172 M$=M$+"I HAVE SENT TROOPS TO CAPTURE THE ENTERPRISE! YOUR CREW ARE NOW MY PRISONERS!":GOSUB1:GOSUB7
176 M$="GUARD! TAKE THE CAPTAIN TO THE BRIG FOR INTERROGATION":F9=1:RC=2:L=52:GOSUB1:GOTO6
178 IFW=0THENGOSUB5
180 PRINT"EXITS: ";:B=0:FORI=0TO3:A=D(L,I):IFA=0THEN196
184 IFA>127THENPRINT"D ";:B=1:GOTO196
186 IFA>63THENPRINT"U ";:B=1:GOTO196
187 IFA<=0THEN196
188 IFI+1>0THENPRINTMID$(" N E S W",(I+1)*2,2);:B=1
196 NEXT:PRINT:IFB=0THENPRINT"NONE"
198 INPUT"COMMAND";A$:PRINT:IFA$=""THEN80
202 IFL=52ANDLEFT$(S$(52),4)="AN A"THENON-(LEFT$(A$,9)<>"JUMP GUAR")GOTO146:GOTO510
204 IFRG=1ANDL=56ANDL(11)=56ANDZ=1THENON-(LEFT$(A$,3)<>"FIR")GOTO146:GOTO678
206 IFL=49ANDHA=1THEN256
208 IFLEN(A$)>1THEN256
210 IFA$="D"THENB=128:GOTO216
212 IFA$="U"THENB=64:GOTO216
214 GOTO220
216 FORI=0TO3:IFD(L,I)>BTHENL=D(L,I)-B:I=4:NEXT:GOTO9
218 NEXT:GOTO238
220 X=0:FORY=1TO4:IFA$=MID$("NESW",Y,1)THENX=Y
221 NEXT:ON-(X=0)GOTO198:B=X-1
230 IFQ=0ANDID=1ANDRND(9)=1THENM$="YOU FELL DOWN A SHAFT AND DIED!":GOTO708
232 IFQTHENC=C-1:IFC=0THENQ=0
234 A=D(L,B):IFA<1ORA>59THEN238
236 L=A:GOTO9
238 SOUND50,1:GOTO9
240 M$=M$+" GALILEO AND IT EXPLODES!":GOTO246
242 M$=M$+" GROUND ALL AROUND YOU!":GOTO246
244 M=0:M$="SUDDENLY, DISRUPTOR BEAMS STRIKE THE":ONGLGOTO240:GOTO242
246 GOSUB1:GOSUB471:M$="YOU TAKE COVER BEHIND SOME ROCKS!":GOSUB1:GOSUB471
248 M$="SUDDENLY, YOU'RE TRANSPORTED!":GOSUB1:ED=0:DE=0:GB=1:S$(46)="THE CHARRED REMAINS OF THE GALILEO"
252 PS=0:M=0:RO=5:ED=0:FORI=1TO5:R(I)=1:NEXT:L=49:GOSUB254:GOTO6
254 SOUND245,12:RETURN
256 A$=A$+"     ":V$=LEFT$(A$,3):N$="":FORI=3TOLEN(A$):IFMID$(A$,I,1)=" "THENN$=MID$(A$,I+1,4):I=255
258 NEXTI:ON-(V$="HEL")GOTO716:ON-(V$="INV")GOTO518:N=0:V=0:RESTORE:FORI=1TO30:READM$:IFV$=M$THENV=I
266 NEXT:M$="":N=0:FORI=1TO28:IFN$=N$(I)THENN=I
270 NEXT:IFN=0THEN286
272 IFL=49ANDHA=1THENON-(V$="INV")GOTO518:ON-(V$="DRO")GOTO694:GOTO146
274 IFL=56ANDRG=1THENON-(V$="FIR"ANDN$="BLAS")GOTO678:GOTO146
276 IFL>5THEN282
278 IFR(L)=0THEN282
280 ON-(V$="FIR"ANDL(7)=0)GOTO666:ON1-(V$="FIR")GOTO146:PRINT"HE BLASTS YOU!":GOTO710
282 IFV>12THENONV-12GOTO542,550,566,340,634,642,650,660,666,694,712,716,742,748,626,448,406,858
283 ONVGOTO300,340,358,754,416,412,474,506,512,518,528,534
286 M$="I CAN'T "+A$:GOTO4
288 PRINT"I DON'T HAVE IT":GOTO198
290 PRINT"I DON'T SEE ONE HERE":GOTO198
292 PRINT"I ALREADY HAVE IT":GOTO198
294 M$="THE BATTERIES ARE DEAD! ":IFIDTHEN298
296 GOTO4
298 M$=M$+"IT'S DANGEROUS TO MOVE IN THE DARK!":GOTO4
300 IFN$="SIGN"ANDL<6THENPRINT"IT SAYS-- DECK"L:PRINTTAB(10)L$(L):GOTO198
302 IFN$="SIGN"ANDL=48THENM$="IT SAYS-- WELCOME TO THE TYCHO IV HOSPITAL":GOTO4
304 IFN$<>"TRIC"THEN318
306 IFL(3)THEN288
308 PRINT"IT REGISTERS":IFL<43ANDRO>0THENPRINTRO"ROMULANS ON BOARD":GOTO198
312 IFL<43THENPRINT"ONLY THE SHIP'S CREW":GOTO198
314 IFL<49THENPRINT"PLANT LIFE"
316 GOTO198
318 IFN$<>"MANU"THEN286
320 IFL(4)THEN288
322 IFL=27THENM$="SHIP'S SENSORS ARE OPERATED FROM THE SCIENCE STATION":GOTO4
324 IFL=24THENM$="THE BUTTON IS A FIRING SWITCH":GOTO4
326 IFL=28THENM$="TO ACTIVATE THE TRANSPORTER, SAY 'ENERGIZE'":GOTO4
328 IFL=31THENM$="VENTILATION DUCTS RUN THROUGHOUT DECKS 2-5. THE BRIDGE HAS ITS OWN LIFE SUPPORT SYSTEMS":GOTO4
330 IFL=38THENM$="HANGAR DOORS PROTECT AGAINST OPEN SPACE. THEY ARE CONTROLLED BY A LEVER ON THE WALL":GOTO4
332 IFL=40THENM$="EXPERIMENTAL DEVICES ARE TO BE INSTALLED AND TESTED HERE":GOTO4
334 IFL=25THENM$="NEVER ENGAGE WARP DRIVE WHILE ORBITING A CLASS M PLANET":GOTO4
336 IFL(4)=0THENM$="THE MANUAL DOESN'T MENTION ANYTHING ABOUT THIS AREA":GOTO4
338 GOTO288
340 IFV$="PIC"THEN348
344 IFN>11THEN286
346 IFL(N)=0THEN292
348 IFN$="FLOW"THEN516
350 IFN$="DEVI"ANDZ=1THENM$="IT'S ACTIVE! YOU SUFFERED FATAL RADIATION BURNS!":GOTO708
352 IFL=56ANDN$="DEVI"THENO$(11)="THE ROMULAN CLOAKING DEVICE"
354 IFL(N)=LTHENL(N)=0:GOTO9
355 GOTO290
356 M$="THE DOORS ARE NOW ":IFXTHENM$=M$+"OPEN":GOSUB1:RETURN
357 M$=M$+"CLOSED":GOSUB1:RETURN
358 IFN$<>"LEVE"THEN390
360 IFL=37THENED=ABS(ED-1):X=ED:GOSUB356
362 IFL=58THENRD=ABS(RD-1):X=RD:GOSUB356
368 IFL=58THEN378
372 IFDETHENIFEDTHEN384
374 IFED=1ANDWS=0THEN388
376 GOTO198
378 IFDR=1ANDRD=1THEN384
380 IFRD=1ANDWS=0THEN388
382 GOTO198
384 M$="YOU LEFT THE AIRLOCK OPEN. THE ":IFL<43THENM$=M$+"ENTERPRISE DECOMPRESSED AND THE ENTIRE CREW DIED":GOTO708
386 M$=M$+"CRUISER DECOMPRESSED AND YOU WERE SUCKED INTO SPACE.":GOTO708
388 M$="UNFORTUNATELY, YOU'RE NOT WEARING YOUR SPACE SUIT! YOU'RE DEAD":GOTO708
390 IFN$="TABL"ORN$="BENC"THEN394
392 GOTO286
394 IFL=35ANDVO=0THENS$(35)="A TABLE AND A BLOCKED VENTILATION DUCT"
396 IFL=31THEND(31,2)=20:D(20,0)=31:S$(31)=T$
398 IFL=29THEND(29,1)=19:D(19,3)=29:S$(29)=T$
400 IFL=40THEND(40,1)=22:D(22,3)=40:S$(40)=B$
402 IFL=41THEND(41,3)=22:D(22,1)=41:S$(41)=B$
404 GOTO9
406 IFN$<>"DEVI"THEN286
408 IFL(11)=LTHENZ=0:M$="THE CLOAKING DEVICE IS NOW DEACTIVATED":GOTO4
410 GOTO290
412 IFN$="LEVE"THEN360
414 IFN$="TABL"ORN$="BENC"THEN396
416 IFN$<>"BUTT"THEN286
418 IFL<>24THEN438
420 IFF=0THENPRINT"NOTHING HAPPENED":GOTO198
422 IFF=1ANDL(6)=-2THENM$="YOU LEFT ORBIT ON IMPULSE ENGINES...":GOSUB1
424 IFF=1ANDL(6)=-2AND(Z=0ORL(11)<>40)THENM$="BUT THE ROMULANS SEE YOU AND BLAST YOU!":GOTO708
426 IFF=1ANDL(6)=-2ANDL(11)=40ANDZ=1THENG=1:M$="AND HAVE CAPTURED THE ROMULAN CLOAKING DEVICE INTACT!":GOSUB1
428 IFG=1ANDDV=0THENM$="BUT PARMELIAN FEVER SPREADS THROUGHOUT THE ENTERPRISE. EVERYONE DIES!":GOTO708
430 IFG=1ANDM=0THENM$="BUT YOU LEFT DR. MCCOY STRANDED ON TYCHO IV!":GOSUB1:GOTO712
432 IFG=1ANDRO>0THENM$="HOWEVER, THERE ARE STILL SOME ROMULANS HIDING ON THE ENTERPRISE":GOTO4
434 IFGTHENM$="THE VACCINE WORKS! MISSION ACCOMPLISHED!":GOSUB1:GOTO712
436 GOTO198
438 IFL=25THENM$="YOU ENGAGED THE WARP DRIVE ABOVE A CLASS M PLANET-- KILLING THE POPULATION!":GOTO708
440 IFL<>26THEN290
442 IFPLTHENPL=0:M$="YOU ZAPPED "+E$:GOTO4
444 M$="YOU DIDN'T LOCK ONTO A TARGET BEFORE YOU FIRED":GOSUB1:ONRND(5)GOTO446:PL=1:E$="EMPTY SPACE":GOTO442
446 M$="UNFORTUNATELY, YOU JUST ZAPPED THE HOSPITAL ON TYCHO IV!":GOTO708
448 IFN$="DEVI"THEN460
450 IFN$<>"SWIT"THEN286
452 IFL<>24THENPRINT"I CAN'T INSTALL IT HERE":GOTO198
454 IFFTHENPRINT"IT'S ALREADY INSTALLED":GOTO198
456 IFL(6)>0THEN288
458 F=1:L(6)=-2:PRINT"OK":GOTO198
460 IFL(11)=0AND(L=40ORL=56ORL=59)THEN464
462 GOTO198
464 M$=O$(11)+" IS NOW INSTALLED IN":CD=1:L(11)=L:IFL=40THENM$=M$+" THE ENTERPRISE":GOTO4
466 IFL=56THENM$=M$+" THE ROMULAN CRUISER":GOSUB1:GOTO4
468 M$=M$+" THE"+C$:GOSUB1
470 GOTO198
471 B=2500
472 FORI=1TOB:NEXT:RETURN
473 B=999:GOTO472
474 IFN$="ENER"THEN480
476 PRINT"OK-- ";
478 GOSUB473:PRINTRIGHT$(A$,5,LEN(A$)-4):GOSUB472:GOTO198
480 PRINT"BEAM uP OR dOWN?":GOSUB8
482 IFA$="D"THEN496
484 IFA$<>"U"THEN480
486 IFL=28ANDM=0ANDPS=0THENM$="MCCOY IS STILL DOWN THERE, BUT I DON'T HAVE HIS COORDINATES":GOTO4
488 IFL=28ANDM=0ANDCE=1THENGOSUB254:M=1:M$="MCCOY IS SAFELY BEAMED ABOARD":GOTO4
490 IFL>43THENIFL(5)THENM$="WITHOUT A COMMUNICATOR TO PROVIDE COORDINATES, YOU CAN'T BEAM UP":GOTO4
492 IFL>43ANDL<49ANDL(5)=0THENGOSUB500:L=28:P=1:GOTO9
494 PRINT"NOTHING WAS BEAMED ABOARD":GOTO198
496 IFL=28THENGOSUB500:L=46:GOTO9
498 M$="I'M ALREADY ON THE PLANET'S SURFACE":GOTO4
500 GOSUB254:IFL(1)=0THENM$="THE SPECIAL PROPERTIES OF THE VACCINE ARE DESTROYED":GOSUB1:L(1)=30:GOSUB471
502 RETURN
504 L=44:GOTO9
506 IFN$<>"GUAR"THEN286
508 IFL<>52THEN290
510 PRINT"YOU KNOCKED HIM OUT":L(10)=L:O$(10)="A ROMULAN BLASTER":S$(52)="AN UNCONSCIOUS GUARD":GOSUB471:GOTO9
512 IFN$="LEVE"THEN360
514 IFN$<>"FLOW"THEN286
516 PRINT"THE FLOWER EMITS A YELLOW GAS...";:GOSUB471:M$="YOU CHOKE TO DEATH":GOTO708
518 PRINT"YOU'RE CARRYING:":A=0:FORI=1TO11:IFL(I)=0THENPRINTO$(I):A=1
522 NEXTI:IFA=0THENGOSUB5
526 GOTO198
528 IF(N$<>"DUCT"ANDN$<>"COVE")ORL<>35THEN286
530 IFVOTHENPRINT"IT'S ALREADY OPEN":GOTO198
532 VO=1:D(35,2)=21:D(21,0)=35:PRINT"OK":S$(35)=T$:GOSUB473:GOTO9
534 IFN$<>"FLAS"THEN286
536 IFL(2)THEN288
538 IFC<1THEN294
540 Q=1:PRINT"OK. IT'S LIT":GOTO198
542 IFN$<>"FLAS"THEN286
544 IFL(2)THEN288
546 IFC<1THENPRINT"IT'S ALREADY OFF":GOTO294
548 Q=0:M$="IT'S OFF":GOTO4
550 IFN$<>"PLAN"THEN286
554 IFL<>23THENM$="I CAN'T DO THAT HERE":GOTO4
556 IFP=0THENM$="SPOCK ISN'T HERE TO OPERATE THE CONTROLS":GOTO4
558 PS=1:PRINT"SPOCK SAYS--":IFGB=1ANDM=0THENM$="CAPTAIN, WE'VE GOT DR. MCCOY'S COORDINATES":GOTO4
562 M$="MOST OF THE PLANET IS DESERT. HOWEVER, A FORM OF PLANT LIFE EXISTS IN ISOLATED COLONIES":GOSUB1
564 M$="THERE ARE NO OTHER LIFEFORMS EXCEPT FOR THE HUMANS AT THE OUTPOST":GOTO4
566 IFN$<>"SHUT"THEN574
568 IF(L=37ORL=46)ANDGB=0THENL=42:GOTO9
570 IFL=58THENL=59:GOTO9
572 GOTO286
574 IFN$<>"ENTE"THEN600
576 IFL=59THEN582
578 IFL=42THEN584
580 GOTO286
581 M$=M$+" SLAMS INTO THE HANGAR DOOR AND EXPLODES":GOTO708
582 IFRD=0THENM$="THE ROMULAN"+C$:GOTO581
584 ED=0:DE=0:M$="YOU'RE COMING UP FAST ON THE ENTERPRISE":GOSUB1:GOSUB471
586 IFGB=0THEN592
588 IFZTHENM$="THE ROMULANS HAVE NOT YET DETECTED YOUR ESCAPE":GOSUB1:GOTO592
590 M$="BUT THE"+C$+" WAS DETECTED AND DESTROYED!":GOTO708
592 ED=0:INPUT"COMMAND";A$:A$=A$+"        ":IFLEFT$(A$,9)="RADIO ENT"THENED=1:GOTO596
596 IFED=0THENM$="YOU DID NOT RADIO AHEAD! THE"+C$:GOTO581
598 RE=1:M$="ROGER, CAPTAIN. YOU ARE CLEARED TO LAND":GOSUB1:GOSUB471:PRINT"NICE TOUCHDOWN, CAPTAIN":GOTO198
600 IFN$<>"PLAN"THEN612
602 IFL<>42THENM$="I CAN'T, UNLESS I'M IN OUR"+C$:GOTO4
604 IFL=42ANDED=0THENM$="THE GALILEO SHOOTS FORWARD,":GOTO581
606 S$(46)=S$(37):S$(37)="A SHUTTLECRAFT SERVICE PLATFORM"
608 M$="YOU PROCEED TO TYCHO IV, BUT FOR UNKNOWN REASONS TOUCH DOWN A KILOMETER FROM THE HOSPITAL"
610 L=46:GL=1:ED=0:DE=0:GOSUB1:GOTO6
612 IFN$<>"HANG"THEN286
614 IFL=42ORL=59THEN618
616 GOTO286
618 IFED=1ANDWS=0THEN388
620 IFL=59ANDRE=1THENL=37:RE=0
622 IFL=42THENL=37
624 GOTO9
626 IFN$<>"VACC"THEN286
628 IFL(1)THEN288
630 IFL<>48THENM$="WAIT UNTIL WE REACH THE HOSPITAL":GOTO4
632 M$="THE COLONY'S DOCTOR THANKS YOU. THE PARMELIAN FEVER CAN NOW BE CURED":GOSUB1:GOSUB471
633 M$="YOU MUST NOW RETURN TO THE ENTERPRISE":GOSUB1:DV=1:L(1)=-1:GOTO198
634 IFN$<>"DOOR"THEN286
636 IFL=17ORL=38THENDE=1:D(17,0)=38:D(38,2)=17
638 IFL=54ORL=57THENDR=1:D(54,3)=57:D(57,1)=54
640 GOTO9
642 IFN$<>"DOOR"THEN286
644 IFL=17ORL=38THENDE=0:D(17,0)=0:D(38,2)=0
646 IFL=54ORL=57THENDR=0:D(54,3)=0:D(57,1)=0
648 GOTO9
650 IFN$<>"SUIT"THEN286
652 IFL(8)=-1THENPRINT"I ALREADY HAVE IT ON":GOTO198
654 IFL(8)=0THENPRINT"OK":L(8)=-1:WS=1:GOTO198
656 IFL(8)=LTHEN288
658 GOTO290
660 IFN$<>"SUIT"THEN286
662 IFWS=0THENPRINT"I'M NOT WEARING IT":GOTO198
664 PRINT"OK":L(8)=0:WS=0:GOTO198
666 IFN$<>"PHAS"THEN676
668 IFL(7)=0THENSOUND225,10
670 IFL<6THENIFR(L)THENR(L)=0:RO=RO-1:GOTO3
672 IFPTHENM$="SPOCK SAYS-- THAT WAS WASTE OF CHARGE":GOTO4
674 GOTO198
676 IFN$<>"BLAS"THEN286
678 IFL=56THENIFBL>0THENRG=0:BL=0:GOTO3
680 PRINT"YOUR BLASTER WAS EMPTY!":GOTO148
682 IFL>5THEN690
684 IFR(L)THEN686
686 IFL(10)<>0ORBL<1THENPRINT"HE KILLS YOU":GOTO710
688 PRINT"YOU GOT HIM!":R(L)=0:RO=RO-1
690 BL=0:PRINT"ZZZAPP!":GOSUB473:IFL=52THENS$(52)="A DEAD GUARD":GOTO9
692 GOTO198
694 IFN$="VACC"ANDL(1)=0THENL(1)=30:M$="THE VIALS ARE SHATTERED! THE VACCINE IS LOST!":GOTO4
696 IFN$="DEVI"ANDL(11)=0THENL(11)=-1:Z=1:CD=1:M$="THE CLOAKING DEVICE BROKE WHEN YOU DROPPED IT!":GOTO4
698 IFN>11THEN288
700 ON1-(L(N)=0)GOTO288:L(N)=L:IFL=49THENL(N)=-1
704 IFL=49ANDN=7THENL(7)=35
706 GOTO9
708 GOSUB1
710 PRINT"MISSION OVER"
712 END
716 IFL=28ANDP=0THENM$="YOU NEED SOMEONE TO OPERATE THE CONTROLS":GOSUB1
718 IFP=0THENM$="YOU'RE ON YOUR OWN CAPTAIN!":GOTO4
720 PRINT"SPOCK SAYS--":IFL=23THENM$="SHIP'S SENSORS CAN SCAN THE PLANET":GOTO4
722 IFL=24THENM$="THE FIRING SWITCH IS BROKEN":GOTO4
724 IFL<>34THEN732
728 M$="I SEE ":ON-(L(5)=-1ANDL(6)=-1)GOTO730:M$=M$+"ALL THE SHIP'S STORES":GOTO4
730 O$(5)="A COMMUNICATOR":O$(6)="AN ELECTRICAL SWITCH":L(5)=34:L(6)=34:M$=M$+O$(5)+" AND "+O$(6):GOTO4
732 IFL=28THENM$="READY TO ENERGIZE, CAPTAIN":GOTO4
734 IFL=38ORL=27ORL=24ORL=28ORL=31ORL=40ORL=25THENM$="I SUGGEST WE CHECK THE SHIP'S MANUAL, CAPTAIN":GOTO4
736 IFL=33THENM$="SEE WHAT'S ON THIS TABLE":GOTO4
738 IFRO>0THENM$="I SENSE DANGER, CAPTAIN":GOTO4
740 M$="I HAVE NO SUGGESTIONS, CAPTAIN":GOTO4
742 IFN$="COOR"ANDPS=1ANDM=0ANDL=23THENCE=1:M$="SPOCK SAYS-- COORDINATES ENTERED, CAPTAIN":GOTO4
744 IFN$="COOR"ANDL=23THENPRINT"OK":GOTO198
746 GOTO286
748 IFN$<>"PHAS"THEN286
750 IFL<23ORL>27THEN286
752 INPUT"ONTO WHAT (1 WORD)";E$:PL=1:GOSUB473:M$="CHEKHOV REPORTS-- PHASERS LOCKED ON, CAPTAIN":GOTO4
754 IFN$<>"DEVI"THEN764
756 IFL(11)=LORL(11)=0THEN760
758 GOTO290
760 M$="THE CLOAKING DEVICE ":IFZTHENM$=M$+"IS ACTIVATED":GOTO4
762 M$=M$+"IS NOT ACTIVATED":GOTO4
764 IFN$<>"SWIT"THEN776
766 IFL=24ANDF=0THENPRINT"THE SWITCH IS BURNED OUT":GOTO198
768 IFL=25ORL=26THEN856
770 IFL(6)=0THENPRINT"IT'S A NEW SWITCH":GOTO198
772 IFL(6)=LTHEN288
774 GOTO290
776 IFN$<>"FLAS"THEN790
778 IFL(2)=LORL(2)=0THEN782
780 GOTO786
782 PRINT"THE FLASHLIGHT IS ";:IFQTHENPRINT"ON"
784 PRINT"OFF"
786 IFL(2)=0ORL(2)=LTHENPRINT"BATTERY LIFE:"C"MINUTES":GOTO198
788 GOTO290
790 IFN$<>"BLAS"THEN798
792 IFL(10)THEN288
794 IFBLTHENPRINT"ONLY ONE CHARGE LEFT":GOTO198
796 PRINT"IT'S EMPTY!":GOTO198
798 IFN$<>"TRIC"THEN806
800 IFL(3)=0THENPRINT"YEP! IT'S THE LATEST MODEL!":GOTO198
802 IFL(3)=LTHEN288
804 GOTO290
806 IFN$="MANU"THEN322
808 IFN$<>"FLOW"THEN814
810 IFL=47THENM$="THEY'RE BLUE AND SMELL SWEET":GOTO4
812 GOTO286
814 IFN$<>"TABL"THEN828
816 IFL=35ANDL(7)=-1THENL(7)=35:O$(7)="A HAND PHASER":M$="I SEE "+O$(7):GOTO4
818 IFL=33ANDL(4)=-1THENL(4)=33:O$(4)="THE SHIP'S TECHNICAL MANUAL":M$="IT'S "+O$(4):GOTO4
820 IFL=33ORL=31ORL=29THEN856
822 IFL=35THENM$="THERE ARE MANY PHASERS":GOTO4
824 IFL=29THEN856
826 GOTO290
828 IFN$<>"BENC"THEN838
830 IFL=41ANDL(9)=-1THENL(9)=41:O$(9)="A WRENCH":M$="A WRENCH AND OTHER TOOLS ARE ON THE WORKBENCH":GOTO4
832 IFL=40THEN856
834 GOTO290
836 O$(8)="TWO SPACE SUITS":GOTO844
838 IFN$<>"WALL"THEN848
840 IFL=37ORL=58THENM$="I SEE A CONTROL LEVER FOR THE HANGAR DOORS":GOTO4
842 IFL=38THENON-(M=1)GOTO836:O$(8)="A SPACE SUIT"
844 IFL=38THENM$="I SEE "+O$(8):GOTO4
846 GOTO856
848 IFN$<>"DUCT"THEN856
850 IF(L=29ANDD(29,1)>0)OR(L=31ANDD(31,2)>0)OR(L=35ANDD(35,2)>0)THEN872
852 IF(L=40ANDD(40,1)>0)OR(L=41ANDD(41,3)>0)THEN872
854 M$="THERE IS NO OPEN VENTILATION DUCT HERE":GOTO4
856 M$="I SEE NOTHING OF INTEREST":GOTO4
858 IFN$<>"DEVI"THEN286
860 IFL(11)<>LTHEN290
862 IFCD=0THENPRINT"IT'S NOT INSTALLED YET!":GOSUB471:PRINT"LOOK OUT!":GOSUB471:PRINT"IT BLEW UP!":GOTO710
864 IFL<42THENM$="THE ENTERPRISE":GOTO870
866 IFL=59THENM$="THE"+C$:GOTO870
868 IFL>48THENM$="THE ROMULAN CRUISER"
870 Z=1:M$=M$+" NO LONGER APPEARS ON ANY SENSORS":GOTO4
872 IFQ=1ANDL(2)=0THENM$="THE DUCT RUNS VERTICALLY BETWEEN DECKS":GOTO4
874 GOTO298
900 DATA23,25,27,6,7,12,,11,30,8,14,,,34,15,9,39,18,10,17,,1,135,,,70,2,136,,71,137,3,,4,72,138,5,,73,,28,2,11,11,29,12,12,2
901 DATA32,14,13,,3,33,14,13,4,16,15,35,34,16,36,15,,5,17,41,40,18,18,5,,,148,,,83,149,,,84,150,,,,85,,,24,1,,,,25,23,24,,26,1
902 DATA25,,,27,1,26,,,,,11,,,,12,,,,3,,,32,,,,,13,31,,,,14,,,16,,,15,,,16,,,,,38,,,,,,37,,,5,,,,18,,,17,,,,,,,,,,,43,44,45,46
903 DATA44,45,47,48,,44,,,45,,,,,45,,,,50,,,,52,,49,,,,,115,53,182,50,,,,,116,55,56,,,,,,54,,,,,,58,,57,,,59,,,,
