0 CLS:CLEAR250:DIMCC,ZZ,M$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CC TO ZZ STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:M$="":RETURN
3 GOSUB1:GOTO850
100 PRINT@135,"S T A R   T R E K":PRINT@196,"A D V E N T U R E  # 2":PRINT@392,"COPYRIGHT 1983":PRINT@424,"HOWARD F. BATIE"
110 DIMS$(59),O$(11),V$(31),N$(28),L$(5),L(11),DM(59,3)
120 CL=25:LC=15:BL=1:CA=1:PS=1:RG=1
130 ZZ=59:GOSUB145:FORI=1TO5:READL$(I):NEXT:GOTO202
145 RESTORE:IFZZ<6THENM$="AT THE ENTRANCE TO A TURBOLIFT":RETURN
146 IFZZ<11THENM$="IN A TURBOLIFT":RETURN
147 IFZZ<19THENM$="IN A CORRIDOR OF THE ENTERPRISE":RETURN
165 IFZZ<23THENM$="IN A VENTILATION DUCT":RETURN
175 IFZZ<44THENFORI=23TOZZ:READM$:NEXT:RETURN
176 IFZZ<47THENFORI=23TO44:READM$:NEXT:RETURN
177 FORI=23TO44:READM$:NEXT:FORI=47TOZZ:READM$:NEXT:IFF9=1AND ZZ=50THENM$="IN A CORRIDOR"
179 RETURN
180 DATAAT THE SCIENCE OFFICER'S STATION,AT THE HELMSMAN'S CONSOLE,AT THE ASTROGATION CONSOLE,AT THE COMBAT CONSOLE
181 DATAAT THE COMMUNICATIONS CONSOLE,IN THE TRANSPORTER ROOM,IN THE GYMNASIUM,IN SICK BAY,IN ENVIRONMENTAL CONTROL
185 DATAIN THE SCIENCE LAB,IN THE SHIP'S LIBRARY,IN THE SHIP'S STOREROOM,IN THE ARMORY,IN THE SHIP'S BRIG
186 DATAIN ENTERPRISE'S HANGAR BAY,IN THE AIRLOCK,IN MAIN ENGINE CONTROL,IN AUXILIARY ENGINE CONTROL,IN THE MAINTENANCE SHOP
187 DATAIN THE GALILEO
190 DATALOST FOREVER IN A SANDSTORM,ON THE SANDY SURFACE OF TYCHO IV,IN AN OASIS,AT THE ENTRANCE TO A BUILDING
191 DATAIN THE TRANSPORTER ROOM OF A ROMULAN BATTLE CRUISER,IN THE ROMULAN COMMANDER'S STATEROOM,IN A LARGE ROOM
192 DATAIN A CORRIDOR. THERE ARE STAIRS UP AND DOWN
193 DATATOO LATE! THIS ISN'T A BRIG--IT'S A TORTURE CELL! AFTER MANY HOURS YOU'RE FINALLY PUT OUT OF YOUR MISERY
194 DATAIN A CORRIDOR. THERE ARE STAIRS UP
200 DATAIN THE ROMULAN SECURITY SECTION,IN THE ROMULAN ENGINE ROOM,IN AN AIRLOCK,IN THE SHUTTLEBAY,IN A ROMULAN SHUTTLECRAFT
201 DATABRIDGE,PERSONNEL SECTION,TECHNICAL DEPARTMENTS,SECURITY SECTION,ENGINEERING
202 AT$="A TABLE AND AN OPEN VENTILATION DUCT":AB$="A BENCH AND AN OPEN VENTILATION DUCT"
203 OD$="AN OPEN DOOR TO THE ":CD$="A CLOSED DOOR TO THE "
210 READA$:FORI=1TO5:S$(I)=A$:NEXT
215 DATAA SIGN ON THE WALL
220 FORI=6TO28:S$(I)="":NEXT:FORI=29TO59:READS$(I):NEXT
230 DATAA TABLE,,SEVERAL CHAIRS AROUND A TABLE,,MANY BOOKS ON A TABLE,NEARLY ANYTHING REQUIRED,MANY WEAPONS ON A TABLE,
235 DATATHE SHUTTLECRAFT GALILEO,,,A WORKBENCH,MANY TOOLS ON A WORKBENCH,THE ASTROGATION CONSOLE,,,,DESERT
236 DATATHOUSANDS OF FLOWERS,A SIGN ON THE WALL,SEVERAL ROMULAN GUARDS ARMED WITH BLASTERS
240 DATAMANY GUARDS WITH DRAWN BLASTERS,SEVERAL ARMED ROMULANS WHO BLAST YOU
241 DATAAN ARMED ROMULAN GUARD,,,SEVERAL ROMULAN GUARDS WITH BLASTERS DRAWN
245 DATATHE ROMULAN HYPER-DRIVE ENGINES,,A ONE-MAN SHUTTLECRAFT,A SIMPLE CONTROL PANEL
250 S$(24)=" -- FIRE IMPULSE ENGINES --":S$(25)=" -- ENGAGE WARP DRIVE --":S$(26)=" -- FIRE MAN PHASERS --"
260 FORI=1TO11:READO$(I):NEXT
265 DATAVIALS OF VACCINE, A BATTERY-OPERATED LANTERN,A TRICORDER,,,,,,,,
275 FORI=1TO30:READV$(I):NEXT
280 DATAREA,GET,MOV,LOO,PRE,PUS,SAY,JUM,PUL,INV,UNB,LIG,UNL,SCA,GO ,PIC,OPE,CLO,WEA,REM,FIR,DRO,QUI,HEL,ENT,LOC,GIV,INS,DEA,ACT
290 FORI=1TO28:READN$(I):NEXT
295 DATAVACC,LANT,TRIC,MANU,COMM,SWIT,PHAS,SUIT,WREN,BLAS,DEVI,WALL,FLOW,TABL,BENC,DOOR,ENTE,COVE,LEVE,SURF,GUAR,SIGN,BUTT
296 DATADUCT,SHUT,ENER,HANG,COOR
305 FORI=1TO11:READL(I):NEXT
310 DATA30,30,32,-1,-1,-1,-1,38,-1,-1,56
320 FORI=1TO59:FORJ=0TO3:READDM(I,J):NEXTJ,I:GOTO500
325 DATA23,25,27,6,7,12,0,11,30,8,14,0,0,34,15,9,39,18,10,17,0,1,135,0,0,70,2,136,0,71,137,3,0,4,72,138,5,0,73,0,28,2,11,11
326 DATA29,12,12,2,32,14,13,0,3,33,14,13,4,16,15,35
330 DATA34,16,36,15,0,5,17,41,40,18,18,5,0,0,148,0,0,83,149,0,0,84,150,0,0,0,85,0,0,24,1,0,0,0,25,23,24,0,26,1,25,0,0,27,1
331 DATA26,0,0,0,0,11,0,0,0,12,0,0,0,3,0
335 DATA0,32,0,0,0,0,13,31,0,0,0,14,0,0,16,0,0,15,0,0,16,0,0,0,0,38,0,0,0,0,0,37,0,0,5,0,0,0,18,0,0,17,0,0,0,0,0,0,0,0,0,0
336 DATA43,44,45,46,44,45,47,48
340 DATA0,44,0,0,45,0,0,0,0,45,0,0,0,50,0,0,0,52,0,49,0,0,0,0,115,53,182,50,0,0,0,0,116,55,56,0,0,0,0,0,54,0,0,0,0,0,58,0
341 DATA57,0,0,59,0,0,0,0
350 PRINT:PRINT"   PRESS ANY KEY TO CONTINUE";
355 A$=INKEY$:ON-(A$="")GOTO355:RETURN
500 CLS:M$="IN RESPONSE TO A CALL FOR EMERGENCY MEDICAL ASSISTANCE, THE ENTERPRISE HAS ARRIVED AT THE PLANET TYCHO IV "
511 M$=M$+"CARRYING A VACCINE FOR USE AGAINST THE DEADLY PARMELIAN FEVER.":GOSUB1
512 M$="BECAUSE OF THE VACCINE'S MAGNETIC PROPERTIES, YOU AND DR. MCCOY MUST DELIVER IT BY THE SHUTTLECRAFT GALILEO.":GOSUB1
515 GOSUB350
600 CLS:IFCL>59THENCL=CL-65:SOUND50,1
602 IFCL=48THENVH=1
605 IFCL=32THENSP=1:ELSEIFCL=37THENSP=0
610 IFCL=39THENSC=1:ELSEIFCL=10ORCL=38THENSC=0
615 IFCL=30ANDVH=0THENMC=1:ELSEIFCL>48THENMC=0
620 IFCL=44ANDVH=1THEN935
622 IFCL<19ORCL>22THENID=0ELSEID=1
630 I$="YOU ":IF(SP=1ANDCL<43)THENI$=I$+"& SPOCK "
635 IFMC=1THENI$=I$+"& MCCOY "
640 IFSC=1THENI$=I$+"& SCOTTY "
645 ZZ=CL:GOSUB145:M$=I$+"ARE "+M$:GOSUB1
646 IFCL=53THEN3700
655 IFCL=43THENM$="AND WANDER AROUND UNTIL YOU DIE OF THIRST":GOTO3699
665 W=0:PRINT:PRINT"YOU SEE":IFCL<19ORCL>22THEN674
670 IFID=1ANDLL=0THENM$="NOTHING. IT'S DANGEROUS TO MOVE IN THE DARK":GOTO3:ELSEIFID=1THENPRINT"HANDHOLDS TO USE IN CLIMBING":GOTO800
674 IFCL=24ORCL=25ORCL=26THENPRINT"A BUTTON LABELLED":W=1
675 IFS$(CL)<>""THENM$=S$(CL):GOSUB1:W=1
680 IFCL=17THENW=1:IFDE=1THENPRINTOD$;"NORTH":GOTO705:ELSEPRINTCD$;"NORTH":GOTO705
685 IFCL=38THENW=1:IFDE=1THENPRINTOD$;"SOUTH":GOTO705:ELSEPRINTCD$;"SOUTH":GOTO705
690 IFCL=54THENW=1:IFDR=1THENPRINTOD$;"WEST":GOTO705:ELSEPRINTCD$;"WEST":GOTO705
695 IFCL=57THENW=1:IFDR=1THENPRINTOD$;"EAST":GOTO705:ELSEPRINTCD$;"EAST"
705 FORI=1TO11:IFL(I)=CL THENPRINTO$(I):W=1:NEXTI:ELSENEXTI
715 IFCL=56ANDL(11)=56THENPRINT"A CLOAKING DEVICE IS INSTALLED":IFCA=1THENPRINT" AND ACTIVATED";
716 IFCL=56ANDL(11)ANDRG=1THENPRINT" AND IS WATCHED BY AN ARMED GUARD."
725 IFCL>5ORRO=0THEN750
730 IFRO(CL)=1THENPRINT"AN ARMED ROMULAN GUARD":W=1:GOTO800
732 IFRO(CL)=0THEN750
735 CLS:PRINT:PRINT"TOO SLOW, CAPTAIN!"
740 PRINT:PRINT"THE ROMULAN GUARD BLASTED YOU!":GOTO3700
750 IFCL<>49THEN780:ELSEL(8)=57:O$(8)="A SPACE SUIT"
752 IFWS=1THENPRINT"YOUR SPACE SUIT IS REMOVED AND":WS=0
755 HA=0:FORI=1TO11:IFL(I)=0THENHA=1:I=11:NEXTI:ELSENEXTI
760 IFHA=0THEN770ELSEM$="YOU ARE TOLD TO DROP ALL YOU ARE CARRYING.":GOTO3
765 CLS:PRINT:PRINT:PRINT"YOU DID NOT FOLLOW INSTRUCTIONS.":PRINT:M$="A ROMULAN WITH AN ITCHY TRIGGER FINGER BLASTED YOU!":GOTO3699
770 PRINT:M$="YOU ARE TO BE TAKEN TO THE ROMUAN COMMANDER WHO WILL DECIDE YOUR FATE.":GOSUB1:GOSUB350:CL=50:RC=1:GOTO600
780 IFCL=51ORCL=55OR(CL=50ANDRC=2)THENM$="THERE ARE JUST TOO MANY. THEY OVERPOWER YOU AND TAKE YOU TO THE BRIG.":GOSUB1:CL=53:GOSUB350:GOTO600
785 IFCL=50ANDRC=1THENPRINT:M$="THE ROMULAN COMMANDER SAYS-- WE FINALLY MEET, CAPTAIN KIRK! SINCE THE FEDERATION AND THE ROMULAN EMPIRE ARE NOW AT WAR, I HAVE SENT SOME ARMED GUARDS TO CAPTURE THE ENTERPRISE.":GOSUB1:ELSE790
787 GOSUB350:CLS:PRINT:PRINT:M$="YOUR STARSHIP IS NOW MY WAR PRIZE AND YOUR CREW ARE MY PRISONERS!":GOSUB1
788 PRINT:M$="GUARD! TAKE THE CAPTAIN TO THE BRIG FOR INTERROGATION.":PRINT:GOSUB1:GOSUB350:F9=1:RC=2:CL=52:GOTO600
790 IFW=0THENPRINT" NOTHING"
800 PRINT:PRINT"EXITS: ";:B=0:FORI=0TO3:A=DM(CL,I)
805 IFA=0THEN840
810 IFA>127THENPRINT"DOWN ";:B=1:GOTO840
815 IFA>63THENPRINT"UP ";:B=1:GOTO840
820 IFI=0ANDA>0THENPRINT"NORTH ";:B=1:GOTO840
825 IFI=1ANDA>0THENPRINT"EAST ";:B=1:GOTO840
830 IFI=2ANDA>0THENPRINT"SOUTH ";:B=1:GOTO840
835 IFI=3ANDA>0THENPRINT"WEST";:B=1
840 NEXTI:IFB=0THENPRINT"NONE":ELSEPRINT
850 PRINT:PRINT"COMMAND? ";:LINE INPUT A$
855 SOUND190,1:IFA$=""THEN600
856 IFCL=52ANDLEFT$(S$(52),4)="AN A"THENIFLEFT$(A$,9)="JUMP GUAR"THEN2210ELSE735
857 IFRG=1ANDCL=56ANDL(11)=56ANDCA=1THENIFLEFT$(A$,3)="FIR"THEN3516ELSE735
859 IFCL=49ANDHA=1THEN1000
865 IFLEN(A$)>1THEN1000
870 IFA$="D"THENB=128:GOTO880
875 IFA$="U"THENB=64:ELSE885
880 FORI=0TO3:IFDM(CL,I)>B THENCL=DM(CL,I)-B:I=4:NEXTI:GOTO600:ELSENEXT:PRINT:GOTO925
885 IFA$="N"THENB=0:GOTO905
890 IFA$="E"THENB=1:GOTO905
895 IFA$="S"THENB=2:GOTO905
900 IFA$="W"THENB=3:GOTO905
902 GOTO850
905 IFLL=0ANDID=1THENPRINT:M$="YOU FELL DOWN A HIDDEN SHAFT AND BROKE YOUR NECK!":GOTO3699
910 IFLL=1THENLC=LC-1:IFLC=0THENLL=0
915 A=DM(CL,B):IFA<1ORA>59THEN925
920 CL=A:GOTO600
925 SOUND50,1:GOTO600
935 CLS:MC=0:PRINT:M$="SUDDENLY, FROM OUT OF NOWHERE, HIGH-ENERGY BEAMS STRIKE THE":IFGL=1THENM$=M$+" GALILEO AND IT EXPLODES.":GOSUB1:ELSEM$=M$+" GROUND ALL AROUND YOU":GOSUB1
936 B=1000:GOSUB2050:PRINT:M$="YOU TAKE COVER BEHIND A PILE OF ROCKS...":GOSUB1:GOSUB2050
937 PRINT:M$="THEN SUDDENLY, YOU'RE TRANSPORTED UP FROM THE PLANET!":GOSUB1:EDO=0:DE=0:GB=1:S$(46)="THE CHARRED REMAINS OF THE GALILEO"
940 PS=0:MC=0:RO=5:EDO=0:FORI=1TO5:RO(I)=1:NEXT:CL=49:GOSUB350:PRINT:PRINT:GOSUB950:GOTO600
950 PRINT:FORI=1TO20:PRINT"*";:FORJ=1TO25:NEXTJ,I:RETURN
1000 A$=A$+"     ":V$=LEFT$(A$,3):N$="":FORI=3TOLEN(A$):IFMID$(A$,I,1)=" "THENN$=MID$(A$,I+1,4):I=255:NEXTI:ELSENEXTI
1001 IFV$="HEL"THEN3900
1002 IFV$="INV"THEN2400
1003 IFV$="QUI"THEN3700
1005 NN=0:VN=0:FORI=1TO30:IFV$=V$(I)THENVN=I:I=32:NEXTI:ELSENEXTI
1010 FORI=1TO28:IFN$=N$(I)THENNN=I:I=32:NEXTI:ELSENEXTI:GOTO1025
1015 IFCL=49ANDHA=1THENIFV$="INV"THEN2400ELSEIFV$="DRO"THEN3600ELSE735
1016 IFCL=56ANDRG=1THENIF(V$="FIR"ANDN$="BLAS")THEN3516ELSE735
1017 IFCL>5THEN1020
1018 IFRO(CL)=0THEN1020
1019 IFV$="FIR"ANDL(7)<>0THENPRINT"HE BLASTS YOU ON THE SPOT":GOTO3700:ELSEIFV$="FIR"THEN3500:ELSE735
1020 IFVN>0THEN ON VN GOTO 1500,1600,1700,4200,1910,1900,2100,2200,2300,2400,2500,2600,2700,2800,2900,1600,3100,3200,3300,3400,3500,3600,3705,3900,4000,4100,3000,2000,1800,4400
1025 PRINT:M$="I CAN'T "+A$:GOTO3
1027 PRINT:PRINT"I DON'T HAVE IT":GOTO850
1030 PRINT:PRINT"I DON'T SEE ONE HERE":GOTO850
1032 PRINT:PRINT"I ALREADY HAVE IT":GOTO850
1035 PRINT:PRINT"THE LANTERN BATTERIES ARE DEAD!":IFID=1THENM$="IT'S DANGEROUS TO MOVE IN THE DARK!":GOTO3:ELSEGOTO850
1500 IFN$="SIGN"ANDCL<6THENPRINT:PRINT"IT SAYS-- DECK"CL:PRINTTAB(10)L$(CL):GOTO850
1503 IFN$="SIGN"ANDCL=48THENPRINT:M$="IT SAYS-- WELCOME TO THE TYCHO IV HOSPITAL":GOTO3
1505 IFN$<>"TRIC"THEN1535
1510 IFL(3)=0THENPRINT"IT REGISTERS":ELSE1027
1515 IFCL<43ANDRO>0THENPRINTRO"ROMULANS ON BOARD":GOTO850
1520 IFCL<43THENPRINT" ONLY THE SHIP'S CREW":GOTO850
1525 IFCL<49THENPRINT" STRONG PLANT LIFE"
1530 GOTO850
1535 IFN$<>"MANU"THEN1025
1540 IFL(4)<>0THEN1027
1545 PRINT:IFCL=27THENM$="SHIP'S SENSORS ARE OPERATED FROM THE SCIENCE OFFICER'S STATION":GOTO3
1550 IFCL=24THENM$="THE BUTTON CONTROLS A FIRING SWITCH":GOTO3
1555 IFCL=28THENM$="TO ACTIVATE THE TRANSPORTER, SAY 'ENERGIZE'":GOTO3
1560 IFCL=31THENM$="A NETWORK OF VENTILATION DUCTS RUNS THROUGHOUT DECKS 2-5. THE BRIDGE HAS ITS OWN LIFE SUPPORT SYSTEMS.":GOTO3
1565 IFCL=38THENM$="GIANT HANGAR DOORS PROTECT AGAINST OPEN SPACE. THEY ARE CONTROLLED BY A DEVICE ON THE WALL.":GOTO3
1570 IFCL=40THENM$="EXPERIMENTAL DEVICES ARE TO BE INSTALLED AND TESTED HERE.":GOTO3
1575 IFCL=25THENM$="NEVER ENGAGE WARP DRIVE WHILE ORBITING A CLASS M PLANET.":GOTO3
1580 IFL(4)=0THENM$="THE MANUAL DOESN'T MENTION ANYTHING ABOUT THIS AREA.":GOTO3:ELSE1027
1600 IFV$="PIC"THEN1615ELSEIFV$<>"GET"THEN1025
1605 IFNN>11THEN1025
1610 IFL(NN)=0THEN1032
1615 IFN$="FLOW"THEN2310
1620 IFN$="DEVI"AND CA=1THENCLS:PRINT:M$="WHEN YOU PICKED UP THE ACTIVATED CLOAKING DEVICE, YOU SUFFERED FATAL RADIATION BURNS.":GOTO3699:ELSEIFCL=56ANDN$="DEVI"THENO$(11)="THE ROMULAN CLOAKING DEVICE"
1625 IFL(NN)=CL THENL(NN)=0:GOTO600:ELSE1030
1700 IFN$<>"LEVE"THEN1775
1705 IFCL=37THENIFEDO=0THENEDO=1:CC=1ELSEEDO=0:CC=0
1710 IFCL=58THENIFRDO=0THENRDO=1:CC=1ELSERDO=0:CC=0
1715 PRINT:M$="THE HANGAR BAY DOORS ARE NOW ":IFCC=1THENM$=M$+"OPEN":GOSUB1:ELSEM$=M$+"CLOSED":GOSUB1
1720 IFCL=58THEN1750
1730 IFDE=1ANDEDO=1THEN1765
1735 IFEDO=1ANDWS=0THEN1770
1740 GOTO850
1750 IFDR=1ANDRDO=1THEN1765
1755 IFRDO=1ANDWS=0THEN1770
1760 GOTO850
1765 PRINT:M$="UNFORTUNATELY, YOU LEFT THE AIRLOCK DOOR OPEN. THE ":IFCL<43THENM$=M$+"ENTERPRISE IS DECOMPRESSED TO SPACE, AND THE ENTIRE CREW DIES.":GOTO3699
1767 M$=M$+"CRUISER IS DECOMPRESSED TO SPACE. YOU ARE BLOWN AWAY FROM THE SHIP AND DIE WHEN YOUR OXYGEN RUNS OUT.":GOTO3699
1770 PRINT:M$="UNFORTUNATELY, YOU'RE NOT WEARING YOUR SPACE SUIT AND YOU DIE WHEN EXPOSED TO SPACE.":GOTO3699
1775 IFN$="TABL"ORN$="BENC"THEN1777ELSE1025
1777 IFCL=35ANDVO=0THENS$(35)="A TABLE AND A CLOSED VENTILATION DUCT"
1780 IFCL=31THENDM(31,2)=20:DM(20,0)=31:S$(31)=AT$
1782 IFCL=29THENDM(29,1)=19:DM(19,3)=29:S$(29)=AT$
1784 IFCL=40THENDM(40,1)=22:DM(22,3)=40:S$(40)=AB$
1786 IFCL=41THENDM(41,3)=22:DM(22,1)=41:S$(41)=AB$
1790 GOTO600
1800 IFN$<>"DEVI"THEN1025
1805 IFL(11)=CL THENCA=0:PRINT:M$="THE CLOAKING DEVICE IS NOW DEACTIVATED.":GOTO3:ELSE1030
1900 IFN$="LEVE"THEN1705
1905 IFN$="TABL"ORN$="BENC"THEN1780
1910 IFN$<>"BUTT"THEN1025
1915 IFCL<>24THEN1950
1920 IFFS=0THENPRINT:PRINT"NOTHING HAPPENED":GOTO850
1925 IFFS=1ANDL(6)=-2THENCLS:PRINT:M$="YOU LEFT THE ORBIT OF TYCHO IV ON IMPULSE ENGINES...":GOSUB1
1927 IFFS=1ANDL(6)=-2AND(CA=0ORL(11)<>40)THENM$="BUT THE ROMULANS SEE YOU AND BLAST YOU OUT OF THE SKY!":GOTO3699
1929 IFFS=1ANDL(6)=-2ANDL(11)=40ANDCA=1THENWG=1:M$="AND HAVE CAPTURED THE ROMULAN CLOAKING DEVICE INTACT!":GOSUB1
1931 IFWG=1ANDDV=0THENPRINT:M$="BUT PARMELIAN FEVER SPREADS THROUGHOUT THE ENTERPRISE AND ALL ABOARD DIE A HORRIBLE DEATH!":GOTO3699
1933 IFWG=1ANDMC=0THENM$="BUT YOU LEFT DR. MCCOY STRANDED ON TYCHO IV!":GOSUB1:GOTO3705
1935 IFWG=1ANDRO>0THENM$="HOWEVER, THERE ARE STILL SOME ROMULANS HIDING SOMEWHERE ON THE ENTERPRISE.":GOTO3
1940 IFWG=1THENPRINT:M$="DR. MCCOY'S VACCINE WORKS AND WIPES OUT THE PARMELIAN SPORES.":GOSUB1:PRINT:PRINT"YOU HAVE SOLVED THIS ADVENTURE!":GOTO3705
1945 GOTO850
1950 IFCL=25THENCLS:PRINT:PRINT:M$="YOU ENGAGED THE WARP DRIVE WHILE ORBITING A CLASS M PLANET, AND EXPOSED THE PLANET'S POPULATION TO LETHAL RADIATION LEVELS. YOU ARE HEREBY RELIEVED OF COMMAND.":GOTO3699
1955 IFCL<>26THEN1030
1960 IFPL=1THEN1965ELSECLS:PRINT:M$="YOU DIDN'T LOCK ONTO A TARGET BEFORE YOU FIRED PHASERS.":GOSUB1:IFRND(3)>1THENM$="LUCKILY, YOUR SHOT WENT HARMLESSLY INTO DEEP SPACE.":GOTO3
1962 M$="UNFORTUNATELY, YOU JUST ZAPPED THE HOSPITAL ON THE PLANET'S SURFACE!":GOTO3699
1965 PRINT:PL=0:M$="YOU ZAPPED "+PH$:GOTO3
2000 IFN$="DEVI"THEN2035
2005 IFN$<>"SWIT"THEN1025
2010 IFCL<>24THEN2025
2015 IFFS=1THEN2030
2020 IFL(6)>0THEN1027:ELSEFS=1:L(6)=-2:PRINT:PRINT"OK.":GOTO850
2025 PRINT:PRINT"I CAN'T INSTALL IT HERE":GOTO850
2030 PRINT:PRINT"IT'S ALREADY INSTALLED":GOTO850
2035 IFL(11)=0AND(CL=40ORCL=56ORCL=59)THENPRINT:M$=O$(11)+" IS SUCCESSFULLY INSTALLED IN":CD=1:L(11)=CL:IFCL=40THENM$=M$+" THE ENTERPRISE":GOSUB1:ELSEIFCL=56THENM$=M$+" THE ROMULAN CRUISER":GOSUB1:ELSEM$=M$+" THE SHUTTLECRAFT":GOSUB1
2040 GOTO850
2050 B=B:FORI=1TOB+500:NEXTI:RETURN
2100 IFN$="ENER"THEN2115
2105 PRINT:PRINT"OK-- ";
2110 B=600:GOSUB2050:PRINTRIGHT$(A$,5,LEN(A$)-4):GOSUB2050:GOTO850
2115 PRINT:PRINT"ARE YOU BEAMING":PRINT"UP (U) OR DOWN (D)?":GOSUB355
2120 IFA$="D"THEN2152
2125 IFA$<>"U"THEN2115
2130 IFCL=28ANDMC=0ANDPS=0THENPRINT:M$="MCCOY IS STILL DOWN THERE, BUT I DON'T HAVE HIS COORDINATES.":GOTO3
2135 IFCL=28ANDMC=0ANDCE=1THENGOSUB950:MC=1:PRINT:M$="MCCOY IS SAFELY BEAMED ABOARD.":GOTO3
2140 IFCL>43ANDL(5)<>0THENPRINT:M$="WITHOUT A COMMUNICATOR TO PROVIDE COORDINATES, YOU CAN'T BEAM UP.":GOTO3
2145 IFCL>43ANDCL<49ANDL(5)=0THENGOSUB2155:CL=28:SP=1:GOTO600
2150 PRINT"NOTHING WAS BEAMED ABOARD.":GOTO850
2152 IFCL=28THENGOSUB2155:CL=46:GOTO600:ELSEPRINT:M$="I AM ALREADY ON THE PLANET'S SURFACE.":GOTO3
2155 GOSUB950:CLS:PRINT:IFL(1)=0THENM$="THE MAGNETIC PROPERTIES OF THE VACCINE ARE DESTROYED AND IT IS USELESS.":GOSUB1:L(1)=30:B=2000:GOSUB2050:RETURN:ELSERETURN
2160 CL=44:GOTO600
2200 IFN$<>"GUAR"THEN1025
2205 IFCL<>52THEN1030
2210 PRINT:PRINT"OK. YOU KNOCKED HIM OUT.":L(10)=CL:O$(10)="A ROMULAN BLASTER":S$(52)="AN UNCONSCIOUS GUARD":B=1000:GOSUB2050:GOTO600
2300 IFN$="LEVE"THEN1705
2305 IFN$<>"FLOW"THEN1025
2310 PRINT:PRINT"THE FLOWER EMITS A YELLOW GAS...":B=1000:GOSUB2050:PRINT"IT'S HARD TO BREATHE!":GOSUB2050:PRINT:M$="YOU CHOKE TO DEATH ON THE POISON GAS.":GOTO3699
2400 PRINT:PRINT"YOU'RE CARRYING:":A=0
2405 FORI=1TO11:IFL(I)=0THENPRINT" ";O$(I) :A=1:NEXTI:ELSENEXTI
2410 IFA=0THENPRINT" NOTHING"
2415 GOTO850
2500 IFN$<>"COVE"ORCL<>35THEN1025
2505 PRINT:IFVO=1THENPRINT"IT'S ALREADY OPEN":GOTO850
2510 VO=1:DM(35,2)=21:DM(21,0)=35:PRINT"OK.":S$(35)=AT$:B=700:GOSUB2050:GOTO600
2600 IFN$<>"LANT"THEN1025
2605 IFL(2)<>0THEN1027
2610 IFLC<1THEN1035
2615 LL=1:PRINT:PRINT"OK. THE LANTERN IS LIT.":GOTO850
2700 IFN$<>"LANT"THEN1025
2705 IFL(2)<>0THEN1027
2710 IFLC<1THENPRINT:PRINT"IT'S ALREADY OFF.":GOTO1035
2715 LL=0:PRINT:M$="OK. THE LANTERN IS OFF.":GOTO3
2800 IFN$="SURF"ORN$="PLAN"THEN2805ELSE1025
2805 IFCL<>23THENM$="I CAN'T DO THAT HERE":GOTO3
2806 IFSP=0THENPRINT:M$="SPOCK ISN'T HERE TO OPERATE THE CONTROLS.":GOTO3
2810 PS=1:PRINT:PRINT"SPOCK SAYS--"
2815 IFGB=1ANDMC=0THENM$="CAPTAIN, WE'VE GOT DR. MCCOY'S COORDINATES.":GOTO3
2820 M$="MOST OF THE PLANET IS DESERT. HOWEVER, A UNIQUE FORM OF PLANT LIFE EXISTS IN ISOLATED COLONIES.":GOSUB1
2821 M$="THERE ARE NO OTHER LIFEFORMS EXCEPT FOR THE HUMANS IN THE OBSERVATION OUTPOST.":GOTO3
2900 IFN$<>"SHUT"THEN2915
2905 IF(CL=37ORCL=46)ANDGB=0THENCL=42:GOTO600
2910 IFCL=58THENCL=59:GOTO600
2912 GOTO1025
2915 IFN$<>"ENTE"THEN2960
2920 IFCL=59THEN2925ELSEIFCL=42THEN2930:ELSE1025
2925 IFRDO=0THENPRINT:M$="THE ROMULAN SHUTTLECRAFT SHOOTS STRAIGHT AHEAD, SLAMS INTO THE CLOSED HANGAR BAY DOOR AND EXPLODES.":GOTO3699
2930 CLS:EDO=0:DE=0:PRINT:M$="YOU'RE COMING UP FAST ON THE ENTERPRISE.":GOSUB1:PRINT:B=1000:GOSUB2050
2932 IFGB=0THEN2940
2935 IFCA=1THENM$="THE ROMULANS HAVE NOT YET DETECTED YOUR ESCAPE.":GOSUB1:ELSEM$="BUT THE SHUTTLE WAS DETECTED AND BLOWN UP BY THE ROMULANS.":GOTO3699
2940 PRINT:PRINT"COMMAND? ";:LINEINPUTA$:A$=A$+"        ":IFLEFT$(A$,9)="RADIO ENT"THENEDO=1ELSEEDO=0
2945 PRINT:IFEDO=0THENM$="YOU SLAMMED INTO THE CLOSED HANGAR BAY DOORS AND EXPLODED BECAUSE YOU DID NOT RADIO AHEAD FOR LANDING CLEARANCE.":GOTO3699
2950 RE=1:PRINT:PRINT"ROGER, CAPTAIN.":PRINT:M$="THE HANGAR BAY DOORS ARE OPEN AND YOU ARE CLEARED TO LAND.":GOSUB1:B=1500:GOSUB2050:PRINT:PRINT"NICE TOUCHDOWN, CAPTAIN.":GOTO850
2960 IFN$<>"SURF"THEN2975
2962 IFCL<>42THENPRINT:M$="I CAN'T DO THAT UNLESS I'M IN THE SHUTTLECRAFT GALILEO.":GOTO3
2965 PRINT:IFCL=42ANDEDO=0THENM$="THE GALILEO SHOOTS STRAIGHTFORWARD, SLAMS INTO THE CLOSED HANGAR BAY DOORS AND EXPLODES.":GOTO3699
2970 S$(46)=S$(37):S$(37)="A SHUTTLECRAFT SERVICE PLATFORM":CLS:M$="YOU PROCEED TO TYCHO IV, BUT FOR UNKNOWN REASONS, YOU TOUCH DOWN A KILOMETER FROM THE HOSPITAL AND HAVE TO WALK.":GOSUB1:PRINT:CL=46:GL=1:EDO=0:DE=0:GOSUB350:GOTO600
2975 IFN$<>"HANG"THEN1025
2980 IFCL=42ORCL=59THEN2985ELSE1025
2985 IFEDO=1ANDWS=0THEN1770
2987 IFCL=59ANDRE=1THENCL=37:RE=0
2990 IFCL=42THENCL=37
2992 GOTO600
3000 IFN$<>"VACC"THEN1025
3005 IFL(1)<>0THEN1027
3010 PRINT:IFCL<>48THENM$="WAIT UNTIL WE REACH THE HOSPITAL":GOTO3
3015 M$="THE COLONY'S DOCTOR THANKS YOU FOR THE PRECIOUS VACCINE. THE PARMELIAN FEVER CAN NOW BE STOPPED.":GOSUB1:B=1000:GOSUB2050:PRINT:DV=1:L(1)=-1:M$="YOU MUST NOW RETURN TO THE ENTERPRISE.":GOTO3
3100 IFN$<>"DOOR"THEN1025
3105 IFCL=17ORCL=38THENDE=1:DM(17,0)=38:DM(38,2)=17
3110 IFCL=54ORCL=57THENDR=1:DM(54,3)=57:DM(57,1)=54
3115 GOTO600
3200 IFN$<>"DOOR"THEN1025
3205 IFCL=17ORCL=38THENDE=0:DM(17,0)=0:DM(38,2)=0
3210 IFCL=54ORCL=57THENDR=0:DM(54,3)=0:DM(57,1)=0
3215 GOTO600
3300 IFN$<>"SUIT"THEN1025
3305 PRINT:IFL(8)=-1THENPRINT"I ALREADY HAVE IT ON.":GOTO850
3310 IFL(8)=0THENPRINT"OK.":L(8)=-1:WS=1:GOTO850
3315 IFL(8)=CL THEN1027ELSE1030
3400 IFN$<>"SUIT"THEN1025
3405 PRINT:IFWS=0THENPRINT"I'M NOT WEARING IT.":GOTO850
3410 PRINT"OK.":L(8)=0:WS=0:GOTO850
3500 IFN$<>"PHAS"THEN3515
3502 IFL(7)=0THENPRINT:PRINT"ZZZAPP!"
3505 IFCL<6THENIFRO(CL)=1THENRO(CL)=0:RO=RO-1:PRINT:M$="YOU GOT HIM!":GOTO3
3510 IFSP=1THENM$="SPOCK SAYS-- THAT WAS WASTE OF PHASER CHARGE.":GOTO3:ELSEGOTO850
3515 IFN$<>"BLAS"THEN1025
3516 IFCL=56THENIFBL>0THENRG=0:BL=0:PRINT:M$="YOU GOT HIM!":GOTO3:ELSECLS:PRINT:PRINT"YOUR BLASTER WAS EMPTY!":GOTO740
3519 IFCL>5THEN3525
3520 PRINT:IFRO(CL)=1THENIFL(10)<>0ORBL<1THENPRINT"HE KILLS YOU ON THE SPOT.":GOTO3700:ELSEPRINT"YOU GOT HIM!":RO(CL)=0:RO=RO-1
3525 BL=0:PRINT:PRINT"ZZZAPP!":B=600:GOSUB2050:IFCL=52THENS$(52)="A DEAD GUARD":GOTO600
3530 GOTO850
3600 PRINT:IFN$="VACC"ANDL(1)=0THENL(1)=30:M$="THE VIALS ARE SHATTERED AND THE VACCINE IS LOST!":GOTO3
3605 IFN$="DEVI"ANDL(11)=0THENL(11)=-1:CA=1:CD=1:M$="THE CLOAKING DEVICE FELL APART WHEN YOU DROPPED IT!":GOTO3
3610 IFNN>11THEN1027
3611 IFL(NN)=0THENL(NN)=CL:ELSE1027
3612 IFCL=49THENL(NN)=-1
3613 IFCL=49ANDNN=7THENL(7)=35
3615 GOTO600
3699 GOSUB1
3700 PRINT:PRINT"YOUR MISSION IS A FAILURE!"
3705 PRINT:PRINT"THIS ADVENTURE IS OVER.":PRINT"DO YOU WANT TO TRY AGAIN (Y/N)?":GOSUB355:IFA$="N"THENCLS:END:ELSEIFA$="Y"THENRUN:ELSE3705
3900 PRINT:IFCL=28ANDSP=0THENM$="YOU NEED SOMEONE TO OPERATE THE CONTROLS":GOSUB1
3905 IFSP=0THENM$="YOU'RE ON YOUR OWN, CAPTAIN!":GOTO3
3910 PRINT"SPOCK SAYS--":IFCL=23THENM$="SHIP'S SENSORS CAN SCAN THE PLANET'S SURFACE.":GOTO3
3915 IFCL=24THENM$="IT LOOKS LIKE THE FIRING SWITCH IS BROKEN.":GOTO3
3920 IFCL=34THENM$="I SEE ":IFL(5)=-1ANDL(6)=-1THENO$(5)="A COMMUNICATOR":O$(6)="AN ELECTRICAL SWITCH":L(5)=34:L(6)=34:M$=M$+O$(5)+" AND "+O$(6):GOTO3:ELSEM$=M$+"ALL THE SHIP'S STORES":GOTO3
3925 IFCL=28THENM$="READY TO ENERGIZE, CAPTAIN.":GOTO3
3930 IFCL=38ORCL=27ORCL=24ORCL=28ORCL=31ORCL=40ORCL=25THENM$="I SUGGEST WE CHECK THE SHIP'S TECHNICAL MANUAL, CAPTAIN.":GOTO3
3935 IFCL=33THENM$="SEE WHAT'S ON THIS TABLE":GOTO3
3940 IFRO>0THENM$="I SENSE DANGER, CAPTAIN.":GOTO3
3945 M$="I HAVE NO SUGGESTIONS, CAPTAIN.":GOTO3
4000 PRINT:IFN$="COOR"ANDPS=1ANDMC=0ANDCL=23THENCE=1:PRINT:M$="SPOCK SAYS-- COORDINATES ENTERED, CAPTAIN.":GOTO3
4005 IFN$="COOR"ANDCL=23THENPRINT"OK.":GOTO850
4010 GOTO1025
4100 IFN$<>"PHAS"THEN1025
4105 IFCL<23ORCL>27THEN1025
4110 PRINT:PRINT"ONTO WHAT? (ONE WORD)":PRINT" ONTO ";:LINEINPUTPH$
4115 PL=1:PRINT:B=500:GOSUB2050:M$="CHEKHOV REPORTS-- PHASERS LOCKED ON TARGET, CAPTAIN.":GOTO3
4200 PRINT:IFN$<>"DEVI"THEN4210
4205 IFL(11)=CL OR L(11)=0THENM$="THE CLOAKING DEVICE ":IFCA=1THENM$=M$+"IS ACTIVATED":GOTO3:ELSEM$=M$+"IS NOT ACTIVATED":GOTO3:ELSE1030
4210 IFN$<>"SWIT"THEN4220
4215 IFCL=24ANDFS=0THENM$="THE SWITCH IS BURNED OUT.":GOTO3:ELSEIFCL=25ORCL=26THEN4315:ELSEIFL(6)=0THENM$="IT LOOKS LIKE A NEW SWITCH.":GOTO3:ELSEIFL(6)=CL THEN1027:ELSE1030
4220 IFN$<>"LANT"THEN4235
4225 IFL(2)=CL OR L(2)=0THENPRINT"THE LANTERN IS ";:IFLL=1THENPRINT"ON":ELSEPRINT"OFF"
4230 IFL(2)=0ORL(2)=CL THENM$="THE BATTERIES WILL LAST ONLY"+STR$(LC)+" MORE MINUTES":GOTO3:ELSE1030
4235 IFN$<>"BLAS"THEN4245
4240 IFL(10)<>0THEN1027ELSEPRINT:IFBL=1THENPRINT"IT HAS ONLY ONE CHARGE LEFT":GOTO850:ELSEPRINT"IT IS EMPTY!":GOTO850
4245 IFN$<>"TRIC"THEN4255
4250 IFL(3)=0THENPRINT"YEP! IT'S THE LATEST MODEL!":GOTO850:ELSEIFL(3)=CL THEN1027ELSE1030
4255 IFN$="MANU"THEN1545
4260 IFN$<>"FLOW"THEN4270
4265 IFCL=47THENM$="THEY'RE BLUE AND SMELL SWEET.":GOTO3:ELSE1025
4270 IFN$<>"TABL"THEN4285
4273 IFCL=35ANDL(7)=-1THENL(7)=35:O$(7)="A HAND PHASER":M$="I SEE "+O$(7):GOTO3
4275 IFCL=33ANDL(4)=-1THENL(4)=33:O$(4)="THE SHIP'S TECHNICAL MANUAL":M$="IT'S "+O$(4):GOTO3
4277 IFCL=33ORCL=31ORCL=29THEN4315
4280 IFCL=35THENM$="THERE ARE MANY PHASERS":GOTO3:ELSEIFCL=29THEN4315:ELSE1030
4285 IFN$<>"BENC"THEN4295
4290 IFCL=41ANDL(9)=-1THENL(9)=41:O$(9)="A WRENCH":M$="A WRENCH AND OTHER TOOLS ARE ON THE WORKBENCH.":GOTO3:ELSEIFCL=40THEN4315:ELSE1030
4295 IFN$<>"WALL"THEN4305
4300 IFCL=37ORCL=58THENLV=1:M$="I SEE A LEVER WHICH CONTROLS THE HANGAR BAY DOORS.":GOTO3
4301 IFCL=38THENIFMC=1THENO$(8)="TWO SPACE SUITS" ELSEO$(8)="A SPACE SUIT"
4302 IFCL=38THENM$="I SEE "+O$(8):GOTO3
4304 GOTO4315
4305 IFN$<>"DUCT"THEN4315
4310 IF(CL=29ANDDM(29,1)>0)OR(CL=31ANDDM(31,2)>0)OR(CL=35ANDDM(35,2)>0)OR(CL=40ANDDM(40,1)>0)OR(CL=41ANDDM(41,3)>0)THENIF(LL=1ANDL(2)=0)THENM$="THE DUCT RUNS VERTICALLY BETWEEN DECKS.":GOTO3:ELSEM$="IT'S TOO DARK TO SEE IN THERE.":GOTO3
4312 M$="THERE IS NO OPEN VENTILATION DUCT HERE.":GOTO3
4315 M$="I SEE NOTHING OF INTEREST HERE.":GOTO3
4400 IFN$<>"DEVI"THEN1025
4405 IFL(11)<>CL THEN1030
4410 PRINT:IFCD=0THENPRINT"IT'S NOT INSTALLED YET!":B=1000:GOSUB2050:PRINT:PRINT"LOOK OUT!":GOSUB2050:PRINT:PRINT"IT BLEW UP!":GOTO3700
4415 IFCL<42THENM$="THE ENTERPRISE":ELSEIFCL=59THENM$="THE SHUTTLECRAFT":ELSEIFCL>48THENM$="THE ROMULAN CRUISER"
4420 CA=1:M$=M$+" NO LONGER APPEARS ON ANY SENSORS.":GOTO3
