0 CLS:CLEAR1000:DIMC1,CC,M$:GOTO5
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 CLS:PRINT"  T R E K   A D V E N T U R E"
8 GOSUB900:L=9:M=L:V=28:T=2:U=4:GOSUB195:GOTO365
9 PRINT:D=D-1:T=T-1:IFT<0THENU=U-1:T=59:IFU<0THEN850
50 O=O-1:IFO<6ANDO>0THENM$="OXYGEN LOW!"+STR$(O)+" MINUTES REMAINING":GOSUB1
55 IFO=0THENPRINT"OUT OF AIR!":GOTO845
60 IFL(4)>-1ANDL(5)>-1ANDZ=1THEN840
65 IF(L=11ORL=12)ANDNOTL(6)THEN830
75 INPUT"YOUR COMMAND";Z$:A$=LEFT$(Z$,2):IFLEN(Z$)=0THEN75
80 FORI=1TO22:PRINT"-";:NEXT:PRINT:IFA$="IN"THENJ=1:GOTO170
85 IFA$="FI"OR(A$="RE"ANDL=12)ORA$="PR"THENA$="DR"
86 IFA$="GE"THENA$="TA"
88 IFA$="WE"THENA$="PU"
90 IFA$="BL"THENA$="SH"
95 FORI=LEN(Z$)TO1STEP-1:IFMID$(Z$,I,1)<>" "THENNEXT:GOTO690
100 B$=MID$(Z$,I+1,2):IFB$="ID"THENB$="BA"
105 IFA$="PL"ANDB$="TA"THEN740
110 IFA$="PU"ANDB$="BU"ANDL=8THEN730
115 IFA$="CL"ANDB$="DO"THEN860
120 FORJ=1TOLEN(W$)STEP2:IFA$=MID$(W$,J,2)THENF=(J+1)/2:GOTO130
125 NEXT:GOTO690
130 FORJ=1TOLEN(O$)STEP2:IFB$=MID$(O$,J,2)THENS=(J+1)/2:GOTO140
135 NEXT:GOTO690
140 ON(F)GOTO220,150,560,300,475,585,620,620,620
145 IFA$="CH"THEN2000
150 IF(S=11)AND(L(5)=L)THENS=12
151 IF(S>V)THEN780
152 IF(S=7)AND(L(1)=L)THEN155
153 IFS<6THEN780
154 IF(L(S-7)<>L)THEN780
155 IFC=6OR(C=4AND(L(4)=-1ORL(5)=-1))THEN785
160 IFL(4)=L(5)ANDL=11THENS=12
165 L(S-7)=0:C=C+1:GOTO9
170 M$="YOU ARE CARRYING ":FORI=1TO21:IFL(I)=0THENPRINTO$(I+7):J=0
175 NEXT:IFJ<>0THENRINT" NOTHING"
180 FORI=4TO6:IFNOTL(I)THENNEXT:GOTO9
185 PRINT:M$="YOU'RE WEARING-":FORI=4TO6:IFL(I)=-1THENPRINTO$(I+7)
190 NEXT:GOTO9
195 O$="NOSOEAWEUPDOOUPHKLUNSPSPBOFLSHSEVABRPITRHASCWRBAPETAHAXX
197 O$=O$+"VAMIVIENPAPOEQLOCEBUREPASTCRDOLOARDOTUTEVEWI"
205 W$="GOTADRLOOPSHPURE":RETURN
220 X=0:IFS=47AND(L=9ORL=23ORL=16)THENN=L:L=14:GOTO276
225 IFS=7ANDL=10THENL=11:GOTO276
230 IFS=5ANDL=11THENL=12:GOTO276
235 IFS=6AND(L=11ORL=12)THENL=L-1:GOTO276
240 IFS=6ANDD=-1THENL=E:GOTO276
245 IF(S=49ANDL<9)OR(L=13ANDD<-1)THEN720
250 M=L:IFL<>14THEN280
252 IFS>7THEN780
255 IFS<5THENM$=O$(47)+" BLOCKED "+O$(S):GOSUB1:GOTO9
260 IFS=7THENL=N:M=L:GOTO365
265 IF(S=5ANDN=23)OR(S=6ANDN=9)THENN=16:GOTO275
270 N=23:IFS=5THENN=9
275 GOTO365
276 M=L:GOTO365
280 M=L:IFS>4THEN780
285 IFP(L,S)>0THENL=P(L,S):M=L:GOTO365
290 GOTO780
300 IFS=45THEN365
305 IFS=39ANDL=8THEN755
310 IFL(4)=L ANDS=11THENO$(11)=O$(0)
315 IFL(5)=L ANDS=11THENO$(12)=O$(0)
320 IFS=50ANDX<>0THENGOSUB810
325 IFS=20ANDL(13)=0THEN685
330 IFS=33ANDL=9THENL(19)=L
335 IFS=34ANDL=9THEN680
340 IF(S=10)AND(L(3)=L)THENL(21)=L
345 IFB$<>"PO"THEN385
350 IFL(3)=L THENL(17)=L:L(18)=L:L(21)=50:GOTO385
355 IF(L(4)>0)AND(L(4)<>L)AND(L(5)<>L)AND(L(5)>0)THEN9
360 O$(22)="PHILLIPS SCREWDRIVER":L(15)=L:W=1:GOTO385
365 J=0:M$="YOU ARE ":IFL>14THENM$=M$+"IN A CORRIDOR":GOTO375
370 M$=M$+D$(L)
375 GOSUB1:IFL=9ORL=23ORL=16THENL(40)=L
380 IF(L>9ANDL<13)THENL(34)=L
385 IFS=48THEN685
390 PRINT"YOU CAN SEE--":IFS=30ANDL=1THEN711
395 IF(M<>L)AND(M<15)THENM$=D$(M):GOSUB1
405 IFS=31THENM$="STARS AND A VERY CLOSE PLANET!":GOSUB1:GOTO9
410 L(39)=50:FORI=1TO4:IFP(L,I)<0THENL(39)=L
415 NEXT:IF(L>3ANDL<8)ORL=23ORL=10ORL=22ORL=8THENX=1
420 FORI=1TO40
425 IF(L(I)=M)ANDLEN(M$)<245THENM$=O$(I+7):GOSUB1:J=1:IFX ANDI=38THENPRINT"WITH WINDOW"
430 NEXT:IFL<9THENM$=O$(50):GOSUB1:M$=O$(49):GOSUB1:M$=O$(48):GOSUB1:J=1
435 M=L:IFJ=0THENPRINT"NOTHING"
440 M$="OBVIOUS EXITS ARE-":IFL=11THENM$=M$+" UP! DOWN"
450 IF(L=13ANDD=0)ORL=12THENM$=M$+" DOWN"
455 IF(L=14)THENM$=M$+" OUT, UP, DOWN"
460 FORI=1TO4:IFP(L,I)>0THENM$=M$+" *"+O$(I)
465 NEXT:GOSUB1:GOTO9
475 IFS=45ANDL=3THEN525
480 IFS=36ANDL=2THENO$(36)=D$(0):L(3)=2:L(8)=2:GOTO9
485 IFS=36ANDL=10THENO$(44)=D$(0):L(5)=L:L(6)=L:GOTO9
490 IFS=21ANDL=12THEN535
495 IFB$<>"DO"THEN690
500 GOSUB705:IFI=5THEN9
505 IFL=23ORL=10THEN695
510 IFL<>4ANDL<>5THENPRINT"IT'S JAMMED!":GOTO9
515 IFL(17)=L THENGOSUB715:GOTO9
520 GOTO790
525 IFL(17)=L THENO$(45)="OPEN ARMORY":L(1)=L:L(2)=L:GOTO9
530 GOTO520
535 INPUT"WITH WHAT";A$:IFLEN(A$)>1THENA$=LEFT$(A$,2):IFA$="PH"THEN820
540 IFA$<>"SC"THENPRINT"WON'T WORK!":GOTO9
545 IFL(15)=0ANDW=1THENO$(27)="OPEN HATCH":L(22)=L:GOTO9
550 IFL(15)=0THENPRINT"THEY'RE PHILLIPS SCREWS!":GOTO9
555 GOTO575
560 IF(S=17)AND(L=12)AND(L(22)=L)AND(L(10)=0)THENL(22)=99:GOTO570
565 IFS=17AND(L(12)<>L)THENL(10)=50:L(22)=L:GOTO795
566 IFS=11ANDL(5)=0THENS=12
570 IFS>7THENIF(L(S-7)=0)THENL(S-7)=L:C=C-1:GOTO9
575 PRINT"DON'T HAVE IT":GOTO9
585 IFL(1)<>0THENPRINT"WITH WHAT":GOTO9
590 IFS=21THEN820
595 IFB$<>"DO"THENPRINT"NO EFFECT!":GOTO9
600 GOSUB705:IFI=5THEN9
605 GOSUB715:N=L:L=P(L,I):GOSUB715:L=N
606 IFL=10ORL=23THENZ=1
610 IFL=4ORL=5THENL(22)=L(10):L(10)=50:GOTO795
611 GOTO9
620 IFS<>11ANDS<>13THEN780
622 FORI=1TO40:IFL(I)=0AND(I<4ORI>6)THEN785
625 NEXT:IFF=8THEN655
630 IFS=13ANDL(6)=0THENL(6)=-1:GOTO9
640 IFL(4)=0ANDL(5)>0ANDNOTL(6)THENL(4)=-1:O=P:GOTO9
645 IFL(5)=0ANDL(4)>0ANDNOTL(6)THENL(5)=-1:GOTO9
650 GOTO780
655 IFS=13ANDL(6)=-1THENL(6)=0:GOTO9
665 IFL(4)=-1ANDNOTL(6)ANDL(5)>0THENL(4)=0:P=O:O=0:GOTO9
670 IFL(5)=-1ANDNOTL(6)ANDL(4)>0THENL(5)=0:GOTO9
675 GOTO780
680 M$="SCREEN SAYS- ORBITAL DECAY CONTINUING":GOSUB1
685 M$="CONTACT WITH ATMOSPHERE IN"+STR$(U)+":"+RIGHT$(STR$(T),2)+" HOURS":GOSUB1:GOTO9
690 M$=Z$:GOSUB1:PRINT"DOES NOT COMPUTE":GOTO9
695 IFL(4)=-1ORL(5)=-1THEN605
700 PRINT"AREA DEPRESSURIZED":PRINT"SPACESUIT REQUIRED":GOTO9
705 FORI=1TO4:IFP(L,I)<0THENRETURN
710 NEXT:PRINT"WHAT DOOR?":RETURN
711 IFNOTL(4)ANDNOTL(5)THENPRINT"RERUTNEVDA YDRAH A"
712 IFL(4)<1ORL(5)<1THENI=0:GOSUB717
713 FORI=8TO26:IFL(I-7)=1THENGOSUB717
714 NEXT:FORI=50TO48STEP-1:GOSUB717:NEXT:GOTO9
715 FORJ=1TO4:P(L,J)=ABS(P(L,J)):NEXT:RETURN
717 M$="":FORJ=LEN(O$(I))TO1STEP-1:M$=M$+MID$(O$(I),J,1):NEXTJ:M$=M$+"  ":GOSUB1:RETURN
720 IFL(4)<1ORL(5)<1ORL(10)=0THENPRINT"CAN'T FIT NOW!":GOTO365
725 L=13:M=L:D=RND(5):E=RND(8):GOTO365
730 IFL(22)<99THEN820
735 M$="HORRAY!! YOU SAVED THE ENTERPRISE AT"+STR$(U)+":"+RIGHT$(STR$(T),2):END
740 M$="SHIP SEVERLY DAMAGED FROM FREAK ION STORM":GOSUB1
745 PRINT"ENGINES DAMAGED":PRINT"TRANSPORTER OUT"
750 PRINT"ABANDONING SHIP IN":PRINT"SHUTTLECRAFT":GOTO9
755 IFL(22)=99THENPRINT"ENGINES READY":GOTO9
760 PRINTTAB(7)"WARNING":PRINT"EXTREMELY FRAGILE":M$=O$(17):GOSUB1
765 M$=D$(12)+" IS CRACKED!":GOSUB1
770 M$="STARTING ENGINES WILL RESULT IN AN ANTI-MATTER IMPLOSION":GOSUB1:GOTO9
780 PRINT"CAN'T DO IT!":GOTO9
785 PRINT"HANDS ARE FULL!":GOTO9
790 M$="AUTHORIZED PERSONS ONLY - PRESENT ID":GOSUB1:GOTO9
795 PRINT"THE VALVE IS CRACKED!":GOTO9
800 M$="YOU ARE FLOATING AWAY IN SPACE":GOSUB1:RETURN
805 M$="YOU ARE IN THE VACUUM OF SPACE!":GOSUB1:RETURN
810 FORI=1TO40IFP(L,I)<0THENM=ABS(P(L,I))
815 RETURN
820 PRINT"THE ENGINES IMPLODED!":FORI=0TO2500:NEXT:FORI=1TO8
825 PRINTTAB(RND(50))"BOOM":NEXT:GOTO845
830 GOSUB800:IFL(6)=0THENM$="CARRYING "+O$(13):GOSUB1
835 GOTO845
840 GOSUB805:IFL(4)=0ORL(5)=0THENM$="HOLDING A "+O$(11):GOSUB1
845 PRINTTAB(10)"YOU'RE DEAD!":END
850 PRINT"THE SHIP JUST CRASHED!"
852 M$="YOU'D BE UP FOR COURTMARSHAL - IF YOU WERE STILL ALIVE.":GOTO845
860 IFL=23ORL=1THENP(23,3)=-10:P(10,4)=-23:Z=0:GOTO9
865 GOTO780
900 DIMO$(50),D$(14)
920 FORI=1TO50:READO$(I):NEXT:FORI=1TO14:READD$(I):NEXT:DIML(40)
930 FORI=1TO40:READL(I):NEXT:DIMP(23,4):FORI=1TO23:FORJ=1TO4
940 READP(I,J):NEXTJ,I:L(0)=1
950 P=13:O$(0)="SPACESUIT WITH POCKETS":D$(0)="OPEN LOCKER":RETURN
1280 DATANORTH,SOUTH,EAST,WEST,UP,DOWN,OUT
1290 DATAPHASER,KLINGAT,UNIFORMS,SPACESUIT,SPACESUIT
1300 DATAMAGNETIC BOOTS,FLASHLIGHT,SHOES,FLUX SENSOR
1310 DATAMAGNATOMIC VALVE
1320 DATASAURIAN BRANDY,PILLOW
1330 DATATRICORDER,HAMMER,ORDINARY SCREWDRIVER
1340 DATASPANNER WRENCH,ID BADGE,PENCIL,MESSAGE TAPE
1350 DATAHATCH,POCKETS,CRACKED VALVE
1360 DATAMIRROR,MAIN VIEWSCREEN,IMPULSE ENGINES
1370 DATAUHURA'S PANEL,SPOCK'S POSITION,GYM EQUIPMENT
1380 DATALOCKERS,DETENTION CELLS,ENGINE FIRING BUTTON,FLASHING READOUT
1390 DATACONTROL PANEL,STARS,DILITHIUM CRYSTALS
1400 DATAOPEN SHUTTLEBAY DOORS
1410 DATASPACESUIT LOCKER,ARMORY,CLOSED DOOR,TURBOLIFT
1420 DATACOMPUTER TERMINAL,VENILATOR,VIEWPORT
1430 DATAIN A CABIN,IN A GYM,IN SECURITY,IN ENGINEERING
1435 DATAIN PARTS STORAGE,IN A TRANSPORTER ROOM,IN A STORAGE LOCKER
1480 DATAIN AUXILIARY CONTROL,ON THE WRECKED BRIDGE OF THE STARSHIP ENTERPRISE
1485 DATAIN THE SHUTTLECRAFT BAY,ON THE STARBOARD ENGINE PYLON
1510 DATAON THE STARBOARD ENGINE,IN A VENTILATION DUCT,IN THE TURBOLIFT
1530 DATA50,50,50,7,50,50,7,50,5,5,1,1,4,4,4,4,50,50,50,12,50,50
1535 DATA1,9,4,9,9,2,2,3,8,8,6,10,4
1610 DATA10,10,3,50,50
1701 DATA,,21,
1702 DATA-21,22,15,
1703 DATA,18,,
1704 DATA-5,,,19
1705 DATA,-4,,
1706 DATA17,,,-7
1707 DATA,,-6,
1708 DATA,,-22,
1709 DATA,,,
1710 DATA,,,-23
1711 DATA,,,
1712 DATA,,,
1713 DATA13,13,13,13
1714 DATA,,,
1715 DATA20,17,-16,2
1716 DATA18,19,,-15
1717 DATA15,6,19,22
1718 DATA 3,16,,20
1719 DATA16,,4,17
1720 DATA,15,18,21
1721 DATA,-2,20,1
1722 DATA2,,17,-8
1723 DATA,,-10,
2000 REM MC-10 JIM GERRIE
