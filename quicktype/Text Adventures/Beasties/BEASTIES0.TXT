2 CLS:PRINT@472,"WELCOME TO":PRINT@530,"BASEMENTS AND BEASTIES"
3 CLEAR 20000:DEFINT A-Z:DIM D1(20,11):DIM D2$(6,42):DIM D3$(44):DIM D4$(16,2):DIM D5$(20,2)
4 DIM X$(4),DA(5),RM(20),OB(16,1),BK(10),CT(12):FOR I=1 TO 20:READ RM(I):NEXT:FOR I=1 TO 16:READ OB(I,1),OB(1,0):NEXT:FOR I=1 TO 10:READ BK(I):NEXT
6 FOR I=1 TO 20:FOR J=1 TO 11:READ D1(I,J):NEXT J:NEXT I:FOR I=1 TO 6: FORJ=1 TO 42:READ D2$(I,J):NEXT:NEXT:FOR I=1 TO 44:READ D3$(I):NEXT
8 FOR I=1 TO 16:FOR J=1 TO 2:READ D4$(I,J):NEXT J:NEXT I:FOR I=1 TO 20:FOR J=1 TO 2:READ D5$(I,J):NEXT:NEXT I
10 CT(0)=1:CT(12)=RND(10)+10:CLS
100 CT(5)=RM(CT(0)):GOSUB 1000:C=CT(6):GOSUB 1160:GOSUB 1180:IF B=0 AND C=0 THEN CT(6)=1:GOSUB 1020:RM(CT(0))=CT(5):ELSE IF B=1 THEN N=RND(100):IF N<20 THEN B=5:GOSUB 1100:GOTO 580
102 GOSUB 1140
104 GOTO 112
105 INPUT A$
106 GOSUB 1060:A$=TX$(2):GOSUB 1080
108 CT(5)=N:GOSUB 1000:IF CT(10)=0 OR N=0 THEN B=7:GOSUB 1100:GOTO 104
110 ON CT(6)+CT(7)*10 GOTO 200,220,240,260,280,300,320,340,360,380,400,420,460,480,500,520,540,560,580,600,620,640,660,680,700
112 IF OB(0,0)=0 AND CT(0)>2 THEN CT(12)=CT(12)-1:IF CT(12)<= 0 THEN CT(12)=RND(10)+10:OB(0,1)=CT(0):OB(0,0)=1:GOTO 116:ELSE 105
114 IF CT(0)<3 THEN OB(0,0)=0:GOTO 105:ELSE OB(0,1)=CT(0)
116 B=42:GOSUB 1100:B=RND(100):IF B>75 THEN 105 ELSE B=43:GOSUB 1100:B=RND(100):IF B>60 THEN B=44:GOSUB 1100:GOTO 580:ELSE 105
200 D=CT(8)+CT(9)*10-1:FOR K=1 TO 10:CT(5)=BK(K):GOSUB 1000:IF D<>CT(8) OR CT(0)<>CT(6)+CT(7)*10 THEN NEXT K:GOTO 202:ELSE IF BK(K)<0 THEN 202 ELSE B=CT(9):GOTO 206
202 D=D+1:GOSUB 1120:IF A=22 THEN B=4:GOTO 204:ELSE IF A=23 THEN B=5:GOTO 204:ELSE IF A=0 THEN B=6:GOTO 206:ELSE CT(0)=A:CT(1)=CT(1)+1:GOTO 100
204 GOSUB 1100:GOTO 580
206 GOSUB 1100:GOTO 104
220 IF TX$(3)="" THEN D=11:GOSUB 1120:N=A*100+10101:GOTO 108:ELSE A$=TX$(3):GOTO 106
240 IF CT(2)>=5 THEN B=36:GOSUB 1100:GOTO 104:ELSE A$=TX$(3):GOSUB 1080:IF N>9999 THEN B=7:GOTO 242:ELSE IF N>12 AND N<17 OR N=18 THEN B=40:GOTO 242
241 IF N=17 THEN B=8:GOTO 242:ELSE IF OB(N,1)=21 THEN B=9:GOTO 242:ELSE IF OB(N,1)<>CT(0) OR N=0 THEN B=12:GOTO 242:ELSE OB(N,1)=21:B=11:CT(2)=CT(2)+1
242 GOSUB 1100:GOTO 104
260 A$=TX$(3):GOSUB 1080:IF N>9999 THEN B=7:GOSUB 262:ELSE IF OB(N,1)<>21 THEN B=10:GOTO 262:ELSE IF N=12 THEN 540:ELSE OB(N,1)=CT(0):B=11:CT(2)=CT(2)-1
262 GOSUB 1100:GOTO 104
280 IF TX$(3)="" THEN B=7:GOTO 284:ELSE A$=TX$(3):GOSUB 1080:CT(5)=N:GOSUB 1000:A=CT(8):GOSUB 1200:IF A=0 THEN B=12:GOTO 284:ELSE IF BK(A)<0 THEN B=13:GOTO 284:ELSE IF OB(11,1)<>21 THEN B=16:GOTO 284:ELSE GOSUB 1220:B=12+CT(9)
284 GOSUB 1100:GOTO 104
300 IF TX$(3)="" THEN B=7:GOTO 304:ELSE A$=TX$(3):GOSUB 1080:CT(5)=N:GOSUB 1000:A=CT(8):GOSUB 1200:IF A=0 THEN B=12:GOTO 304:ELSE IF BK(A)>0 THEN B=13:GOTO 304:ELSE GOSUB 1220:B=17
304 GOSUB 1100:GOTO 104
320 IF OB(0,0)=1 THEN 322 ELSE FOR K=13 TO 16:IF OB(K,1)<>CT(0) THEN NEXT K:B=41:GOSUB 1100:GOTO 104
322 IF OB(10,1)<>21 THEN B=23:GOTO 326:ELSE IF K=15 OR K=16 THEN B=24:GOTO 324:ELSE X=RND(100):IF OB(0,0)=1 THEN 328 ELSE IF X>70 THEN B=26:GOTO 324:ELSE OB(K,1)=0:A=1:GOSUB 1200:GOSUB 1220:B=25:GOTO 326
324 GOSUB 1100:B=27:GOSUB 1100:X=RND(100):IF X<40 THEN B=29:GOSUB 1100:GOTO 580:ELSE B=28
326 GOSUB 1100:GOTO 105
328 IF X>70 THEN B=26:GOSUB 1100:GOTO 112:ELSE OB(0,0)=0:OB(0,1)=0:B=25:CT(4)=CT(4)+25:GOTO 326
340 B=18:GOSUB 1100:FOR J=1 TO 16:IF OB(J,1)<>21 THEN NEXT J:GOTO 104:ELSE A=4:B=J:B$=D4$(B,2):PRINT B$:NEXT J:GOTO 104
360 CT(5)=N:GOSUB 1000:B=CT(9)*10+CT(8):GOSUB 1100:GOTO 104
380 C=0:GOSUB 1160:GOSUB 1140:GOTO 104
400 IF CT(0)<>6 THEN B=32:GOTO 402:ELSE B=33
402 GOSUB 1100:GOTO 104
420 GOSUB 1240:GOTO 104
460 IF LEFT$(TX$(3),5)<>"AARDV" THEN B=34:GOSUB 1100:GOTO 104:ELSE 560
480 B=31:GOSUB 1100:GOSUB 1240:END
500 B=19:GOSUB 1100:INPUT A:FOR I=0 TO 8:PRINT#-1,OB(I,0),OB(I,1),OB(I+8,0),OB(I+8,1),RM(I),RM(I+8),RM(I+12),BK(I),BK(I+2),CT(I):NEXT:GOTO 104
520 B=19:GOSUB 1100:INPUT A:FOR I=0 TO 8:INPUT#-1,OB(I,0),OB(I,1),OB(I+8,0),OB(I+8,1),RM(I),RM(I+8),RM(I+12),BK(I),BK(I+2),CT(I):NEXT:GOTO 104
540 IF OB(12,1)<>21 THEN B=20:GOTO 544:ELSE OB(12,1)=CT(0):CT(2)=CT(2)-1:FOR K=15 TO 16:IF OB(K,1)<>CT(0) THEN NEXT K:B=21:GOTO 544:ELSE OB(K,1)=0:A=1:GOSUB 1200:GOSUB 1220:B=22
544 GOSUB 1100:GOTO 104
560 IF CT(0)=6 THEN CT(0)=1 ELSE IF CT(0)=1 THEN CT(0)=6 ELSE B=34:GOSUB 1100
562 GOTO 100
580 CT(3)=CT(3)+1:B=35:GOSUB 1100:OB(9,1)=2:FOR I=1 TO 12:IF OB(I,1)=21 THEN OB(I,1)=CT(0):NEXT:ELSE NEXT
582 CT(0)=1:CT(2)=0:FOR I=1 TO 4000:NEXT I:GOTO 100
1000 FOR Z=6 TO 10:CT(Z)=0:NEXT Z:B$=MID$(STR$(CT(5)),2):FOR Z=1 TO LEN(B$):CT(6+LEN(B$)-Z)=VAL(MID$(B$,Z,1)):NEXT Z:IF CT(5)<0 THEN CT(11)=-1:RETURN:ELSE CT(11)=1:RETURN
1020 CT(5)=CT(10)*10000+CT(9)*1000+CT(8)*100+CT(7)*10+CT(6):CT(5)=CT(5)*CT(11):RETURN
1060 FOR I=1 TO LEN(A$):IF MID$(A$,I,1)<>" " THEN NEXT I:TX$(3)="":TX$(2)=A$:RETURN:ELSE TX$(3)=MID$(A$,I+1):TX$(2)=LEFT$(A$,I-1):RETURN
1080 IF LEN(A$)>5 THEN A$=LEFT$(A$,5)
1082 A=2:B=1
1084 FOR II=1 TO 6 :FOR JJ=1 TO 42 STEP 2:B$=D2$(II,JJ):N=INT(VAL(D2$(II,JJ+1))):IF B$="." OR B$=A$ THEN RETURN ELSE NEXT:NEXT
1100 A=3:PRINT D3$(B):RETURN
1120 B=CT(0):A=1:FOR Y=1 TO D:A=D1(B,Y):NEXT Y:RETURN
1140 GOSUB 1180:IF B=1 THEN RETURN:ELSE A=4:FOR B=1 TO 16:IF CT(0)<>OB(B,1) THEN NEXT B:RETURN:ELSE TX$(4)=D4$(B,1):PRINT TX$(4):NEXT B:RETURN
1160 GOSUB 1180:IF B=1 THEN B=39:GOSUB 1100:RETURN:ELSE A=5:B=CT(0):TX$(0)=D5$(B,1):TX$(1)=D5$(B,2):IF C=0 THEN PR$=TX$(0):GOSUB 1400:RETURN:ELSE PR$=TX$(1):GOSUB 1400:RETURN
1180 IF OB(9,1)<>21 AND CT(0)<>1 AND CT(0)<>2 THEN B=1 ELSE B=0
1182 RETURN
1200 FOR Q=1 TO 10:CT(5)=BK(Q):GOSUB 1000:IF CT(6)+CT(7)*10<>CT(0) OR CT(9)<>A THEN NEXT Q:A=0:ELSE A=Q
1202 RETURN
1220 BK(A)=-BK(A):CT(5)=BK(A):GOSUB 1000:IF CT(10)=1 RETURN ELSE BK(A-1+CT(10))=-BK(A-1+CT(10)):RETURN
1240 B=30:GOSUB 1100:A=CT(4):FOR I=1 TO 20:IF RIGHT$(STR$(RM(I)),1)="1" THEN A=A+5
1242 NEXT I:FOR I=1 TO 8:IF OB(I,1)=1 THEN A=A+10
1244 NEXT I:FOR I=13 TO 16:IF OB(I,1)=0 THEN A=A+20
1246 NEXT I:A=A-CT(3)*20:PRINT A:PRINT CT(1);"STEPS":RETURN
1400 CLS:PRINT PR$:PRINT:PRINT"<";:FOR II=1 TO 62:PRINT"-";:NEXT II:PRINT">":PRINT:RETURN
2000 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
3000 DATA 4,0,7,0,20,0,11,0,5,0,19,0,7,0,6,0,2,0,3,0,10,0,12,0,4,0,18,0,14,0,6,0
4000 DATA 22902,2808,23306,3712,23404,3011,11104,11118,11714,11306
5000 DATA 1,2,2,1,1,1,1,1,0,3,9
5002 DATA 2,2,2,2,2,1,1,2,0,8,9
5004 DATA 0,0,4,10,0,0,0,0,1,0,8
5006 DATA 0,5,0,0,11,0,3,0,0,0,4
5008 DATA 0,0,0,0,0,4,0,0,0,0,5
5010 DATA 0,0,0,12,0,0,0,0,0,23,3
5012 DATA 0,0,0,14,0,0,0,0,0,0,3
5014 DATA 0,0,0,0,14,0,0,0,2,0,8
5016 DATA 9,0,16,15,9,0,0,9,0,0,7
5018 DATA 23,23,23,16,17,17,17,3,0,17,4
5020 DATA 4,0,0,0,0,0,0,0,0,0,0
5022 DATA 0,0,13,0,18,0,0,6,0,0,7
5024 DATA 0,0,0,0,0,0,12,0,0,0,6
5026 DATA 8,0,0,0,19,0,0,7,0,19,4
5028 DATA 15,0,15,0,15,16,9,0,0,0,0
5030 DATA 15,16,16,0,16,0,10,9,0,0,1
5032 DATA 18,18,18,18,18,18,18,18,18,18,0
5034 DATA 12,19,0,0,0,0,0,0,0,0,0
5036 DATA 14,0,0,0,0,18,0,0,14,20,9
5038 DATA 22,22,22,22,22,22,22,22,19,22,8
6000 DATA JEWEL,1,CROWN,1,GOLDE,2,CUBE,2,DIAMO,3,BEETL,3,SILVE,4,BELT,4,PLATI,5,RING,5,ONYX,6,COIN,7,HOURG,8,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
6001 DATA TORCH,9,AXE,10,KEY,11,GRENA,12,MANTI,13,IGUAN,14,SPIDE,15,NAMEL,16,TERRO,16,ORC,18,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
6002 DATA OOZE,17,DESKS,17,CABIN,17,BODIE,17,COKE,17,MACHI,17,COBWE,17,CASES,17,COFFI,17,DOOR,317,GRATE,217,U,10901,D,11001,N,10101,NE,10201,E,10301,SE,10401,S,10501,SW,10601,W,10701,NW,10801
6003 DATA NORTH,10101,SOUTH,10501,EAST,10301,WEST,10701,UP,10901,DOWN,11001,SCORE,10012,QUIT,10014,KILL,10007,FIGHT,10007,SLAY,10007,BLOW,10017,BOMB,10017,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
6004 DATA WAIT,13809,HELP,13709,READ,10011,SAY,10013,LOCK,10006,UNLOC,10005,OPEN,10005,SHUT,10006,CLOSE,10006,LOOK,10010,INVEN,10008,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
6005 DATA TAKE,10003,DROP,10004,THROW,10004,STEAL,10003,IN,10002,OUT,10002,GO,10002,ENTER,10002,EXIT,10002,SAVE,10015,RESTO,10016,AARDV,10018,.,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
7000 DATA"THE CREATURE WILL NOT LET YOU PASS!"
7001 DATA"THE GRATE IS CLOSED AND LOCKED!"
7002 DATA"THE DOOR IS TIGHTLY SHUT AND LOCKED."
7003 DATA"YOU BURN IN THE FLAMES!"
7004 DATA"YOU FALL TO YOUR DOOM..."
7005 DATA"YOU CAN'T GO THAT WAY"
7006 DATA"WHAT DID YOU SAY?"
7007 DATA"YOU TRY UNSUCCESSFULLY...IMMOVABLE!"
7008 DATA"YOU ALREADY HAVE IT!"
7009 DATA"YOU DON'T HAVE IT!"
7010 DATA"OKAY."
7011 DATA"I SEE NOTHING OF THE SORT HERE."
7012 DATA"YOU DON'T NEED TO."
7013 DATA"WITH A CREAK, THE GRATE FALLS OPEN."
7014 DATA"THE DOOR SWINGS OPEN WIDE."
7015 DATA"YOU HAVE NO KEY!"
7016 DATA"IT SLAMS SHUT AND THE LOCK CATCHES."
7017 DATA"YOU HAVE THE FOLLOWING:"
7018 DATA"PREPARE TAPE RECORDER AND HIT <ENTER>."
7019 DATA"YOU HAVE NO BOMB!"
7020 DATA"THE GRENADE FALLS TO THE FLOOR AND NOTHING HAPPENS."
7021 DATA"THE GRENADE EXPLODES IN A SILENT FLASH OF WEIRD BLUE LIGHT...AND THE CREATURE IS GONE!"
7022 DATA"WITH WHAT WEAPON?"
7023 DATA"YOUR AXE SWINGS ARE DYNAMIC...BUT INEFFECTIVE!"
7024 DATA"YOUR MAGIC AXE CONNECTS!  THE CREATURE VANISHES IN A PUFF OF FOUL SMOKE!"
7025 DATA"MISSED IT!  FIE!"
7026 DATA"THE HIDEOUS MONSTER LEAPS AT YOUR THROAT!"
7027 DATA"SOMEHOW YOU FEND IT OFF!"
7028 DATA"IT FINISHES YOU OFF!!"
7029 DATA"YOUR SCORE IS:"
7030 DATA"DO VISIT THE BASEMENT AGAIN!"
7031 DATA"NOTHING HERE TO READ...HOW DULL!"
7032 DATA"THE DANGER HERE IS PRETTY THICK BUT SAY <AARDVARK> YOU'LL GET OUT QUICK!"
7033 DATA"NOTHING HAPPENS."
7034 DATA"WELL, FINE ADVENTURER!  YOU ARE IN A REAL JAM!  FORTUNATELY, WE CAN BRING YOU BACK!....POOF!!..."
7035 DATA"YOUR ARMS ARE FULL...YOU CAN CARRY NO MORE."
7036 DATA"YOUR CRIES GO UNHEARD, PITIFUL WRETCH."
7037 DATA"TIME PASSES..."
7038 DATA"IT IS PITCH DARK!  YOU MAY FALL INTO A PIT!"
7039 DATA"YOU MANIFEST SOME PRETTY SUICIDAL TENDENCIES, FELLA!"
7040 DATA"SAVE YOUR STAMINA, TURKEY!  I SEE NO REAL THREAT!"
7041 DATA"THERE IS AN ANGRY ORC NEARBY!"
7042 DATA"HE SWINGS AT YOU WITH A BLACK SCIMITAR!"
7043 DATA"YOU ARE SLASHED TO PIECES."
8000 DATA"THERE IS A CROWN OF JEWELS HERE!","JEWELED CROWN"
8001 DATA"THERE IS A GOLDEN CUBE HERE!","GOLDEN CUBE"
8002 DATA"THERE IS A DIAMOND HERE CARVED LIKE A BEETLE","DIAMOND BEETLE"
8003 DATA"THERE IS A FINE SILVER BELT HERE!","SILVER BELT"
8004 DATA"THERE IS A RING HERE OF PURE PLATINUM","PLATINUM RING"
8005 DATA"THERE IS A POLISHED ONYX HERE!","ONYX"
8006 DATA"THERE IS A COIN HERE WORTH MILLIONS!","COIN"
8007 DATA"THERE IS AN ANCIENT HOURGLASS HERE!","HOURGLASS"
8008 DATA"THERE IS A BURNING TORCH HERE.",TORCH
8009 DATA"THERE IS A HEFTY MAGIC AXE HERE.",AXE
8010 DATA"THERE IS A LARGE KEY HERE.",KEY
8011 DATA"THERE IS AN ENCHANTED GRENADE HERE.",GRENADE
8012 DATA"A GIANT MANTIS CROUCHES NEARBY, READY TO POUNCE!",0
8013 DATA"A HUGE IGUANA PACES RESTLESSLY NEARBY, KEEPING AN EYE ON YOU!",0
8014 DATA"A GIANT WHITE SPIDER, MANDIBLES TWITCHING, TOWERS ABOVE YOU!",0
8015 DATA"THE NAMELESS TERROR ARISES FROM A PIT, BLOCKING YOUR RETREAT WITH SLIMY TENTACLES!!",0
9000 DATA"YOU STAND AT THE BOTTOM OF A LARGE PIT.  AT YOUR FEET IS A NARROW HOLE JUST WIDE ENOUGH TO CRAWL INTO.","BOTTOM OF PIT"
9001 DATA"HERE ARE THE RUINS OF AN ANCIENT TROLL-CASTLE.  NEARBY IS A GRATE LEADING DOWN INTO DARKNESS...","RUINS"
9002 DATA"THIS WAS APPARENTLY ONCE A WEAPONS ROOM, ALTHOUGH THE CASES ARE ALL EMPTY NOW.  THERE'S A HOLE IN THE ROOF, AN ARCHWAY TO THE EAST, AND A JAGGED HOLE IN THE SOUTHEAST WALL.","WEAPONS ROOM"
9003 DATA"THE SIGNS OF A GREAT BATTLE BETWEEN TROLLS AND TERRIBLE BEAST-MEN ARE EVIDENT...FROM THE LOOK OF IT, THE TROLLS LOST.  BODIES ARE EVERYWHERE.  THERE IS A JAGGED HOLE TO THE WEST, A HALL NORTHEAST, AND A SOUTH DOOR.","LOST BATTLE"
9004 DATA"THE WALLS ARE LINED WITH COFFIN CASES...THIS IS THE TROLL CEMETARY, IT SEEMS.  A SOUTH WEST DOOR LEADS OUT.","TOMB ROOM"
9005 DATA"THIS IS A SMALL, DARK ROOM SMELLING OF MAGIC.  THE ORACLE HAS LEFT A MESSAGE ON THE WALL.  THERE'S A SOUTHEAST DOOR AND A LARGE PIT NEAR THE DOOR.","ORACLE ROOM"
9006 DATA"AT LAST!  THE TREASURE VAULT!  WHAT A SHAME THAT SO MUCH OF THE ORIGINAL WEALTH HAS BEEN REMOVED!  THERE IS A SOUTHEAST DOOR OUT.","TREASURE VAULT"
9007 DATA"THIS WAS ONCE THE MAIN GUARDPOST TO THE UNDERGROUND KINGDOM OF THE TROLLS.  THERE IS AN ENTRANCE-GRATE SET IN THE ROOF AND A SOUTH EXIT DOOR.","GUARD POST"
9008 DATA"YOU ARE LOST IN A MAZE!","YOU ARE LOST IN A MAZE!"
9009 DATA"YOU WALK ALONG A NARROW LEDGE RUNNING NORTHWEST AND SOUTHEAST.  TO THE WEST IS A RAPID STREAM FAR BELOW, AND TO THE EAST IS A BOTTOMLESS CHASM!","NARROW LEDGE"
9010 DATA"THIS IS A SMALL PRISON CELL.  THROUGH THE BARS YOU CAN SEE A NICE OFFICE...UNREACHABLE.  THERE'S A NORTH DOOR.","CELL"
9011 DATA"HERE IS A BUSINESS OFFICE, WITH EMPTY, RANSACKED DESKS AND CABINETS.  A BARRED WINDOW IN THE WALL SHOWS A SMALL PRISON CELL OF SOME SORT.  THERE ARE TWO DOORS, TO THE NORTHWEST AND EAST, AND A ROCKY HOLE IN THE SOUTH WALL.","OFFICE"
9012 DATA"THIS IS THE LUNCH ROOM, COMPLETE WITH COKE MACHINE...EMPTY UNFORTUNATELY.  THERE IS A ROOM TO THE WEST.","LUNCH ROOM"
9013 DATA"WHAT A CREEPY PLACE!  THERE ARE COBWEBS EVERYWHERE!  A DOOR LEADS NORTH, A HALL GOES NORTHWEST AND THERE IS A HOLE IN FLOOR.","COBWEB ROOM"
9014 DATA"YOU ARE A LOST IN A MAZE!","YOU ARE LOST IN A MAZE!"
9015 DATA"YOU ARE LOST IN A MAZE!","YOU ARE LOST IN A MAZE!"
9016 DATA"YOU ARE SPLASHING ABOUT IN A COLD RUSHING STREAM!  NOTHING YOU DO SEEMS TO STOP YOUR PERILOUS RIDE TOWARDS A NEARBY STONY CAVE ENTRANCE!","RUSHING STREAM"
9017 DATA"YOU LIE ON THE SANDS OF A DARK SLIMY CAVERN BY A STREAM.  THE WALLS ARE COVERED WITH DISGUSTING OOZE.  THERE IS A HOLE IN THE NORTHERN ROCKS AND A PATH NORTHEASTWARD.","SLIMY CAVERN"
9018 DATA"SWEAT BEADS ON YOUR FACE AS YOU STAND IN A STEAMY CAVE.  SMOKE RISES FORM A HOLE IN THE FLOOR, AND THERE IS ANOTHER RAGGED HOLE IN THE ROOF WITHIN REACH.  THERE'S ALSO A PATH GOING SOUTHWEST.","STEAMY CAVE"
9019 DATA"YOU ARE CROUCHING ON A FIERY SPIRE, A PINNACLE SURROUNDED BY FLAMES!  A LOW ROOF WITH A HOLE HANGS A FOOT ABOVE YOUR HEAD  IT IS UNBEARABLY HOT!","FIERY SPIRE"
