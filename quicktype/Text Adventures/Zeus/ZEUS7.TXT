0 CLS:CLEAR500:DIMA,C1,ZZ,M$,I$:GOTO40
1 ZZ=1:C1=32:FORC1=C1TOZZSTEP-1:I$=MID$(M$,C1,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,C1-ZZ):ZZ=C1+1:C1=ZZ+32:IFI$=""THENC1=0
2 NEXT:M$="":RETURN
4 GOSUB1:GOTO200
5 FORZZ=1TO2500:NEXT:RETURN
6 RESTORE:FORC1=1TOR:READN,S,E,W,U,D:NEXT:IFR=38ANDVV THENE=45
7 IFR=1ANDWW THEND=15
9 RETURN
40 GOSUB4400:DIMW$(90),OB(20),RD(108):R=81:GOSUB6:FORA=1TO75:READW$(A):NEXT:Z$="A VOICE SAYS IN YOUR MIND: ":W$(0)=CHR$(8)
100 OB(6)=17:OB(8)=26:OB(9)=50:R=1:OB(1)=82:OB(2)=82:OB(3)=82:OB(4)=82:OB(5)=82:LD=5:MA=5:LT=1
110 PRINT@451,"PRESS ANY KEY TO BEGIN..."
120 I$=INKEY$:IFI$=""THEN120
130 CLS:GOTO780
200 REM
210 PRINT"********************************";:V$="":N$="":INPUT"WHAT NOW";A$
220 FORA=1TOLEN(A$):IFMID$(A$,A,1)=" "THENX=A-1:A=0:GOTO250
230 NEXTA
240 V$=A$:GOTO320
250 V$=LEFT$(A$,X)
260 IFRIGHT$(A$,LEN(A$)-X)=" "THENN$="":GOTO320
270 N$=RIGHT$(A$,LEN(A$)-(X+1)):X=0
280 IFN$=W$(9)ORN$=W$(10)ORN$=W$(11)ORN$=W$(12)ORN$=W$(13)ORN$=W$(14)THENV$=N$:N$="":GOTO300
281 IFN$=W$(15)ORN$=W$(16)ORN$=W$(17)ORN$=W$(18)ORN$=W$(19)ORN$=W$(20)THENV$=N$:N$=""
300 IFCC=6THEN360
320 IFV$=W$(74)THENCLS:GOTO2260
330 IFV$=W$(75)THENGOSUB2160:GOTO200
340 IFR>1ANDR<15THEN2610
350 IFR=19THEN3400
360 IFV$=W$(7)THEN2110
370 IFV$=W$(8)ORV$=W$(30)ORV$=W$(31)THEN2400
380 IFV$=W$(9)ORV$=W$(10)ORV$=W$(11)ORV$=W$(12)ORV$=W$(13)ORV$=W$(14)THEN600
381 IFV$=W$(15)ORV$=W$(16)ORV$=W$(17)ORV$=W$(18)ORV$=W$(19)ORV$=W$(20)THEN600
390 IFV$=W$(21)THENWO=1:M$="OKAY, LONG DESCRIPTIONS COMING UP.":GOTO4
400 IFV$=W$(22)THENWO=0:M$="OKAY, BACK TO SHORT DESCRIPTIONS.":GOTO4
410 IFV$=W$(23)ORV$=W$(24)ORV$=W$(25)THENX=82:PRINT"YOU ARE CARRYING:":GOSUB21000:GOTO200
420 IFR=80THEN3870
430 IFUD>0THENUD=UD+1:GOTO3800
440 IFV$=W$(26)ORV$=W$(27)THEN1200
450 IFV$=W$(28)THEN1400
460 IFV$=W$(46)ORV$=W$(47)ORV$=W$(48)THENM$="THOU ONLY NEEDEST ENTER THY WORDS.":GOTO4
470 IFV$=W$(29)ANDR=32THENGOSUB570:PL=PL+1:GOTO3200
480 IFV$=W$(29)THENGOSUB570:GOTO200
490 IFV$=W$(49)ORV$=W$(50)ORN$=W$(50)THEN3000
500 IFV$=W$(62)THENGOSUB3600
510 IFV$=W$(67)ORV$=W$(68)THEN4000
520 IFR=32THEN3200
530 IFCM=1THENCM=0:GOTO550
540 PRINT"I DON'T KNOW WHAT YOU MEAN!"
550 IFUD>0THENPRINT"DO SOMETHING QUICKLY!"
560 GOTO200
570 PRINT"HMMMM...HMMMMM...HMMMM....":RETURN
600 REM
610 IFR=32THEN650
620 IFR<15ORR>75THEN650
630 IFLT=0THEN3090
640 IFOB(4)<>82THEN3090
650 X=R
660 IFV$=W$(9)ORV$=W$(10)THENGOSUB6:R=N
670 IFV$=W$(11)ORV$=W$(12)THENGOSUB6:R=S
680 IFV$=W$(13)ORV$=W$(14)THENGOSUB6:R=E
690 IFV$=W$(15)ORV$=W$(16)THENGOSUB6:R=W
700 IFV$=W$(17)ORV$=W$(18)THENGOSUB6:R=U
710 IFV$=W$(19)ORV$=W$(20)THENGOSUB6:R=D
720 IFX=1ANDR=15THENX=0:LT=0:CLS:M$="YOU DROP DOWN AND THE PILLAR ABOVE YOU ROLLS BACK INTO PLACE.":GOSUB1:CC=0:GOTO3090
730 IFR>0THENX=R:CLS:GOTO770
740 IFR=0THENM$="YOU CAN'T MOVE THAT WAY AT THIS TIME.":GOSUB1:R=X:X=0:CM=1:IFUD>0THENUD=UD+1:GOTO3800
750 IFR=32THEN3200
760 GOTO200
770 IFWO=1THENRD(R)=0
780 IFR>54THEN840
790 IFR>27THEN820
800 IFR<15THENONRGOSUB1710,1740,1740,1740,1740,1740,1740,1740,1740,1740,1740,1740,1740,1740:GOTO850
805 ONR-14GOSUB1750,1750,1750,1750,1790,1750,1750,1750,1760,1820,1820,1820,1820:GOTO850
820 IFR<44THENONR-27GOSUB1820,1820,1820,1820,1830,1840,1840,1840,1840,1840,1840,1840,1840,1840,1850,1850:GOTO850
821 ONR-43GOSUB1850,1850,1870,1850,1850,1850,1860,1880,1880,1880,1880:GOTO850
840 IFR<73THENONR-54GOSUB1880,1880,1880,1880,1890,1900,1900,1900,1900,1910,1910,1910,1910,1910,1900,1900,1910,1910:GOTO850
841 ONR-72GOSUB1910,1910,1910,1920,1930,1940,1950,1960,2020
850 GOSUB6:PRINT"EXITS ARE: "W$(10*-(N>0))" "W$(12*-(S>0))" "W$(14*-(E>0))" "W$(16*-(W>0))" "W$(18*-(U>0))" "W$(20*-(D>0))
860 IFMH=1THENPRINT"THE MONSTER IS HERE."
900 REM
910 PRINT"OBJECTS HERE ARE:":X=R:GOSUB21000
1100 IFR=32ANDPL=0THEN3200
1120 GOTO200
1200 REM
1210 X=0:IFN$=""THENPRINT"I NEEDETH A NOUN!":GOTO1290
1220 GOSUB1500
1230 IFOB(X)=ROROB(X)=82THEN1250
1240 PRINT"YOU CAN'T AT THE MOMENT.":GOTO1290
1250 LD=LD+1:IFLD>5THENM$="YOU'VE NOT THE STRENGTH TO CARRY MORE.":GOSUB1:LD=LD-1:GOTO1290
1260 OB(X)=82
1270 IFOB(4)=82ANDR=32THENLT=1
1280 M$="OKAY, YOU'VE GOT THE "+N$+".":GOSUB1
1290 IFOB(7)=82THENKN=1
1300 X=0:GOTO200
1400 REM DROP
1410 X=0:IFN$=""THENM$="I NEEDETH A NOUN!":GOSUB1:GOTO1490
1420 GOSUB1500
1430 IFOB(X)=82THEN1450
1440 M$="YOU'RE NOT CARRYING THE "+N$+".":GOSUB1:GOTO1490
1450 OB(X)=R:LD=LD-1:M$="OKAY, DROPPED THE "+N$+".":GOSUB1
1460 IFOB(7)<>82THENKN=0
1470 IFX=4ANDR=32THEN1490
1480 IFX=4ANDLT=1THENM$="THE TORCH GOES OUT AS IT HITS THE GROUND.":GOSUB1:LT=0
1490 X=0:GOTO910
1500 REM
1510 IFN$=W$(1)THENX=1
1520 IFN$=W$(2)THENX=2
1530 IFN$=W$(3)ORN$=W$(4)THENX=3
1540 IFN$=W$(5)THENX=4
1550 IFOB(10)=82OROB(10)=RTHEN1570
1560 IFN$=W$(32)THENX=5:GOTO1580
1570 IFN$=W$(32)THENX=10
1580 IFN$=W$(56)THENX=6
1590 IFN$=W$(54)ORN$=W$(53)THENX=7
1600 IFN$=W$(58)THENX=8
1610 IFN$=W$(70)THENX=9
1630 RETURN
1700 REM
1710 M$="YOU STAND BEFORE A FALLEN PILLAR.":GOSUB1
1711 IFCC=6THENM$="THE PILLAR HAS SHIFTED AND A PASSAGE LEADS DOWN INTO DARKNESS.":GOSUB1
1720 IFRD(R)=1THENRETURN
1730 M$=Z$+CHR$(13)+"GO!!!"+CHR$(13):GOSUB1
1731 M$="BUT, ONE FAVOUR WILL I GRANT THEE; IF YOU ASK ME... IN THE PROPER PLACE, AT THE PROPER TIME. NOW GO!"
1732 GOSUB1:RD(R)=1:R=5:FORC1=1TO4:GOSUB5:NEXT:CLS
1740 M$="YOU STAND BEFORE A MIGHTY PILLAR.":GOSUB1:RETURN
1750 M$="YOU ARE IN THE CAVERNS OF HERA":GOSUB1:RETURN
1760 GOSUB1750
1770 IFRD(R)=1THENRETURN
1780 M$=Z$+"DO NOT GO DOWN BELOW. IN TWO THOUSAND YEARS ONLY ONE HAS SURVIVED.":GOSUB1
1781 M$="BEWARE THAT WHICH LIES BELOW!":GOSUB1:RD(R)=1:RETURN
1790 M$="YOU STAND BEFORE THE ALTAR OF HERA":GOSUB1
1800 IFRD(R)=1THENRETURN
1810 M$=Z$+"SACRIFICE THAT WHICH YOU HOLD DEAREST, THEN WILL I HELP YOU.":GOSUB1:RD(R)=1:RETURN
1815 M$="A GREAT WIND BLOWS FROM BELOW AND YOUR TORCH GOES OUT.":LT=0:GOSUB1:RETURN
1820 M$="YOU ARE TRAVERSING DAMP CAVERNS":GOSUB1:RETURN
1830 M$="YOU ARE IN THE POOL OF THE SYBILS":GOSUB1:RETURN
1840 M$="YOU ARE IN THE LAIR OF THE UNDEAD":GOSUB1:RETURN
1850 M$="YOU ARE IN THE STABLES OF THE CENTAURS":GOSUB1:RETURN
1860 M$="YOU ARE IN A SMITHY":GOSUB1:RETURN
1870 GOSUB1850:GOTO1815
1880 M$="YOU ARE IN THE REALM OF THE UNDERWORLD":GOSUB1:RETURN
1890 GOSUB1880:GOTO1815
1900 M$="YOU TRAVERSE THE BRIDGE OVER HADES. GREAT FIRES BURN BELOW":GOSUB1:RETURN
1910 M$="YOU ARE ON A NARROW SPIRAL STAIRCASE":GOSUB1:RETURN
1915 PRINT"YOU STAND ON THE ACROPOLIS.":RETURN
1920 GOSUB1915:M$="THE CITY OF ATHENS IS FAR BELOW":GOSUB1:RETURN
1930 GOSUB1915:M$="TO THE NORTH IS THE MUSEUM":GOSUB1:RETURN
1940 GOSUB1915:M$="TO THE EAST IS THE MUSEUM":GOSUB1:RETURN
1950 GOSUB1915:M$="YOU ARE NEXT TO THE MUSEUM. THE DOOR IS SECURELY BOLTED":GOSUB1:RETURN
1960 M$="YOU STAND BEFORE THE ALTAR OF ZEUS":GOSUB1
1970 IFML=0ANDOB(8)<>82THEN1975
1971 GOTO1980
1975 M$=Z$+"FOOLISH MORTAL! YOU ARE BEYOND HELP.":ML=1:GOSUB1:RETURN
1980 IFOB(8)=0OROB(8)<>82THENRETURN
1990 M$="THE "+W$(58)+" GLOWS BRIGHTLY: BECAUSE THOU RECOGNIZEST ME, THOU SHALL HAVE MY HELP.":GOSUB1
2000 M$=Z$+"STRIKE YOUR WORTHLESS LIGHT WITH THE TOOL OF HEPHAESTUS AND I WILL GIVE YOU THE MEANS TO DESTROY THE MONSTER. "
2010 M$=M$+"USE MY GIFT IN THE GLORY OF THE SUN.":OB(8)=0:ML=1:GOSUB1:RETURN
2020 M$="YOU ARE IN THE TEMPLE OF APOLLO":GOSUB1:RETURN
2110 INPUT"SAVE THIS ATTEMPT";A$:IFLEFT$(A$,1)="Y"THENGOSUB2160
2130 INPUT"TRY AGAIN";A$:IFLEFT$(A$,1)="Y"THENRUN
2140 M$="ALAS! THE TRIAL HAS ENDED.":GOSUB1
2150 END
2160 INPUT"PRESS PLAY & RECORD AND THEN    HIT ENTER";A$
2180 RD(83)=CC:RD(84)=KN:RD(85)=LD:RD(86)=LP:RD(87)=LT:RD(88)=MA:RD(89)=ML:RD(90)=PL:RD(91)=R
2181 RD(92)=SC:RD(93)=SW:RD(94)=UD:RD(95)=WO:RD(96)=VV:RD(97)=WW
2210 FORA=1TO10:RD(97+A)=OB(A):NEXTA:CSAVE*RD,"ZEUSDAT":RETURN
2260 PRINT"PRESS PLAY":CLOAD*RD,"ZEUSDAT"
2280 CC=RD(83):KN=RD(84):LD=RD(85):LP=RD(86):LT=RD(87):MA=RD(88):ML=RD(89):PL=RD(90):R=RD(91)
2281 SC=RD(92):SW=RD(93):UD=RD(94):WO=RD(95):VV=RD(96):WW=RD(97)
2290 FORA=1TO10:OB(A)=RD(97+A):NEXTA:GOTO600
2300 M$="ON THE PILLAR IS INSCRIBED: HERE IF IT IS THE PROPER TIME, SAYETH: '"+W$(C1)+"'":GOTO4
2310 M$="THE SKELETON CLUTCHES A KNIFE IN ITS BONY HAND... AS IF IT WAS FIGHTING OFF SOMEONE OR SOMETHING.":OB(7)=R:GOTO4
2400 IFR=3ANDN$=W$(45)THENC1=35:GOTO2300
2420 IFR=5ANDN$=W$(45)THENC1=41:GOTO2300
2430 IFR=7ANDN$=W$(45)THENC1=44:GOTO2300
2440 IFR=9ANDN$=W$(45)THENC1=38:GOTO2300
2450 IFR=11ANDN$=W$(45)THENC1=40:GOTO2300
2460 IFR=13ANDN$=W$(45)THENC1=37:GOTO2300
2470 IF(OB(6)=82OROB(6)=R)ANDN$=W$(56)ANDKN=0THEN2310
2490 IFR=32ANDSW=1ANDN$=W$(57)ANDLP=0THEN2495
2491 GOTO2500
2495 M$="THE POOL CLEARS AND YOU SEE YOU ARE STANDING BY AN ALTAR. "
2496 M$=M$+"YOU SEE A KNIFE PLUNGING TOWARDS YOUR CHEST, BUT FEEL NO FEAR. THE IMAGE FADES.":LP=1:GOTO4
2500 IFR=32ANDSW=1ANDN$=W$(57)ANDLP=1THEN2505
2501 GOTO2510
2505 M$="THE POOL CLEARS AND YOU SEE THE CREATURE FOLLOWING. YOU TURN AND HURL A THUNDERBOLT-- A STEEL THUNDERBOLT! "
2506 M$=M$+"THE IMAGE FADES.":LP=2:GOTO4
2510 IFR=32ANDSW=1ANDN$=W$(57)ANDLP=2THEN2515
2511 GOTO2520
2515 M$="THE POOL CLEARS AND YOU SEE THE CREATURE FOLLOWING WITH STEEL IN ITS CHEST. YOU ARE RACING FOR THE SUN. "
2516 M$=M$+"THE IMAGE FADES.":LP=3:GOTO4
2520 IFN$=W$(58)AND(OB(8)=ROROB(8)=82)THEN2525
2521 GOTO2530
2525 M$="THE BULL IS MAGNIFICENT, BUT DOCILE... ALMOST AS IF HE IS IN A TRANCE. "
2526 IFOB(8)=RTHENM$=M$+"HE SEEMS TO BE WAITING FOR SOMETHING TO HAPPEN.":GOTO4
2527 M$=M$+"HE LOOKS AT YOU EXPECTANTLY.":GOTO4
2530 IFN$=""AND(LT=1OR(R<15ORR>75))THEN780
2540 PRINT"I SEE NOTHING SPECIAL":GOTO200
2610 IFV$=W$(37)ANDR<>13THEN2820
2630 IFR=13ANDV$=W$(37)ANDCC=0THENCC=1:GOTO2800
2640 IFR=13ANDV$=W$(37)THENCC=0:GOTO2810
2650 IFV$=W$(40)ANDR<>11THEN2820
2660 IFR=11ANDV$=W$(40)ANDN$=W$(42)ANDCC=1THENCC=2:GOTO2800
2670 IFR=11ANDV$=W$(40)ANDN$=W$(42)ANDCC<>2THENCC=0:GOTO2810
2680 IFV$=W$(38)ANDR<>9THEN2820
2690 IFR=9ANDV$=W$(38)ANDCC=2THENCC=3:GOTO2800
2700 IFR=9ANDV$=W$(38)THENCC=0:GOTO2810
2710 IFV$=W$(44)ANDR<>7THEN2820
2720 IFR=7ANDV$=W$(44)ANDN$=W$(43)ANDCC=3THENCC=4:GOTO2800
2730 IFR=7ANDV$=W$(44)ANDN$=W$(43)THENCC=0:GOTO2810
2740 IFV$=W$(41)ANDR<>5THEN2820
2750 IFR=5ANDV$=W$(41)ANDN$=W$(36)ANDCC=4THENCC=5:GOTO2800
2760 IFR=5ANDV$=W$(41)ANDN$=W$(36)THENCC=0:GOTO2810
2770 IFV$=W$(35)ANDR<>3THEN2820
2780 IFR=3ANDV$=W$(35)ANDCC=5THENCC=6:WW=-1:GOTO2830
2790 GOTO360
2800 M$="THERE IS A FRIENDLY RUMBLE OF THUNDER FROM THE HEAVENS!":GOTO4
2810 M$=Z$+"THAT WAS NOT PROPER. THOU MUST BEGIN AGAIN!":GOTO4
2820 M$="A VOICE SAYS: BLASPHEMER... SAYETH THAT NOT HERE. NOW THOU MUST BEGIN AGAIN IF THOU WISHETH MY AID.":CC=0:GOTO4
2830 M$=Z$+"I AM GREATLY PLEASED! THE EARTH SHAKES AND A NEARBY SOUND OF SOMETHING MOVING CAN BE HEARD.":GOTO4
3000 IFMA=0THENPRINT"YOU'VE NO MATCHES"
3020 IFR<15ORR>75THENM$="NO NEED TO... THERE'S PLENTY OF LIGHT HERE.":GOTO4
3040 IFMA=0THENOB(3)=0:GOTO200
3050 IFV$=W$(49)ANDN$=W$(5)ANDOB(4)=82ANDOB(3)=82THENMA=MA-1:M$="YOUR TORCH IS LIT. YOU CAN SEE!":LT=1:GOTO4
3060 IFV$=W$(2)ORN$=W$(2)THENM$="THE FLASHLIGHT DOESN'T SEEM TO WORK.":GOTO4
3070 IFV$=W$(49)ANDN$=W$(5)ANDOB(3)<>82THENM$="YOU HAVEN'T ANY MORE MATCHES!":GOTO4
3080 IFV$=W$(49)THENMA=MA-1:M$="THE MATCH FLARES BRIEFLY AND THEN DIES.":GOTO4
3090 IFLT=0THENM$="YOU ARE IN TOTAL DARKNESS AND CANNOT MOVE.":GOTO4
3100 PRINT"NOTHING HAPPENS.":GOTO200
3200 REM
3210 IFSW=1THEN3320
3220 IFN$=W$(1)THEN3320
3230 PL=PL+1:IFPL>1THEN3235
3231 GOTO3240
3235 M$="YOU ARE BARELY STAYING AFLOAT AND ARE TIRING RAPIDLY.":GOSUB1
3236 IFPL>8THENM$="TOO LATE... YOU ARE SINKING.":GOSUB1:GOTO2130
3240 IFPL=1THEN3245
3241 GOTO3250
3245 M$="SPLASH! YOU HAVE FALLEN INTO THE POOL OF THE SYBILS. MANY MORTALS HAVE REACHED HERE BUT FEW HAVE RETURNED.":GOSUB1
3250 IFPL=1ANDOB(1)=82THENOB(1)=0:M$="THE SWAN LEAPS FROM YOUR ARMS AND BEGINS SWIMMING.":GOSUB1
3260 IFPL>1THEN3310
3270 FORA=1TO20
3280 IFOB(A)=82THENOB(A)=0
3290 NEXT:LT=0:LD=0
3300 OB(4)=32:LT=0:M$="ALL YOUR OBJECTS DROP FROM YOUR HANDS BUT ONE. "
3301 M$=M$+"MIRACULOUSLY, YOUR TORCH IS FLOATING AND CONTINUES TO BURN.":GOSUB1:GOTO910
3310 GOSUB5:CLS:GOTO820
3320 M$="YOU ARE RIDING ON THE BACK OF THE SWAN. THE LIQUID IN THE POOL IS CLOUDY.":GOSUB1:SW=1
3330 PL=PL+1
3340 IFPL>10ANDOB(4)=82THEN3345
3341 IFPL>10THENLT=0:GOTO3345
3342 GOTO850
3345 R=19:M$="THE SWAN TAKES TO THE AIR ON GIANT PINIONS. A VOICE IN YOUR MIND SAYS: I CAN HELP YOU HERE NO MORE.":GOSUB1
3346 GOSUB5:GOSUB5:GOSUB5:CLS:GOTO800
3350 M$="AS YOU KILL THE "+W$(1)+" A TERRIBLE WAILING IS HEARD. A VOICE IN YOUR MIND SAYS: "
3351 M$=M$+"YOU HAVE DESTROYED THE BELOVED OF ZEUS. YOUR PUNISHMENT IS...":GOSUB1:GOSUB5:GOTO2140
3360 M$="AS YOU ATTEMPT TO KILL THE "+W$(58)+" A LAUGH IS HEARD IN YOUR MIND: FOOLISH MORTAL... "
3361 M$=M$+"DO YOU KNOW NOTHING? WHO I AM? WHERE YOU ARE? GO!":OB(8)=0:R=5:GOTO4
3400 REM
3410 IFV$=W$(60)ORV$=W$(62)ORV$=W$(63)THEN3430
3420 GOTO360
3430 IFKN=0THENM$="ONLY WITH A SACRED KNIFE!":GOTO4
3440 IFN$=W$(61)ORN$=W$(65)OR N$=W$(66)THEN3500
3450 IFN$=W$(1)AND(OB(1)=19OROB(1)=82)THEN3350
3470 IFN$=W$(58)AND(OB(8)=82OROB(8)=19)THEN3360
3490 M$=Z$+"THINK... AND DO NOT MOCK ME WITH THAT WHICH YOU DO NOT HAVE!":GOTO4
3500 IFSC=1THENM$=Z$+"BEGONE... I CAN HELP YOU NO MORE.":GOTO4
3510 M$="YOUR HAND IS STOPPED BY A SUPERNATURAL FORCE AS IT PLUNGES TOWARD YOUR CHEST. ":GOSUB1
3520 M$=Z$+"SUCH TRUST SHALL BE REWARDED. THERE IS A SOUND AS A DOOR OPENS SOMEWHERE.":VV=-1:SC=1:GOTO4
3600 REM
3610 IFKN=0THENM$="YOU CAN ONLY PERFORM SUCH A THING WITH A SACRED KNIFE.":GOTO4
3620 IFN$=W$(1)AND(OB(1)=ROROB(1)=82)THEN3350
3640 IFN$=W$(58)AND(OB(8)=82OROB(8)=19)THEN3360
3660 IF(N$=W$(69)ANDUD=>1ANDUD<11)OR(N$=W$(64)ANDUD=>1ANDUD<11)THEN3665
3661 GOTO3680
3665 M$="YOU LOOK AROUND QUICKLY BUT DO NOT SEE ANY SIGN OF THE CREATURE. "
3666 M$=M$+"YOU ONLY HEAR MOVEMENT IN THE DARKNESS OUTSIDE THE PERIMETER OF YOUR LIGHT...":GOTO4
3680 IF(N$=W$(69)ANDUD=11)OR(N$=W$(64)ANDUD=11)THEN3685
3681 GOTO3700
3685 M$="THE KNIFE SINKS DEEPLY INTO THE CHEST OF THE MONSTER.":GOSUB1:GOSUB5
3686 M$="HE CONTINUES TO MOVE TOWARD YOU AS IF NOTHING HAS HAPPENED.":GOTO4
3700 M$="YOU ARE NOT PERMITTED TO KILL THE "+N$+" HERE.":GOTO4
3800 REM
3810 IFUD>1ANDUD<9THENPRINT"THE MONSTER DRAWS NEARER.":GOTO440
3820 IFUD=9THENM$="THE UNDEAD MONSTER CRIES OUT: ANOTHER FOOL COMES TO DESTROY ME... IF ONLY HE WOULD!":GOSUB1:GOTO440
3830 IFUD=10THEN3835
3831 GOTO3840
3835 M$="THE UNDEAD ENTERS. HIS ONCE HUMAN FEATURES ARE TWISTED IN UNIMAGINABLE AGONY. HE RAISES A "
3836 M$=M$+"FLESHLESS HAND TOWARDS YOUR THROAT.":MH=1:GOSUB1:GOTO440
3840 IFUD=11THENM$="THE BONY HAND OF THE MONSTER HOLDS YOU AS HE BENDS HIS FACE TOWARD YOUR THROAT!":GOSUB1:GOTO440
3850 IFUD=12THEN440
3860 M$="THE ULTIMATE DARKNESS CLOSES IN.":GOSUB1:GOTO2150
3870 REM
3880 IFV$=W$(70)ORV$=W$(71)ORV$=W$(72)THEN3900
3890 GOTO440
3900 IFOB(9)=82THEN3920
3910 M$=Z$+"DISBELIEVER... YOU KNOW NOT HEPHAETUS? BEGONE!":GOSUB1:GOTO440
3920 IFN$=W$(2)THEN3940
3930 M$=Z$+"STRIKE A LIGHT, THAT IS NOT A LIGHT!":GOSUB1:GOTO440
3940 IFOB(2)=82THEN3960
3950 M$=Z$+"YOU SPEAK BUT HAVE NO SUCH THING!":GOSUB1:GOTO440
3960 M$="AS YOU STRIKE, GREAT FIRES LIGHT UP THE SKY. "+Z$+"LOOK NOW WHAT YOU CARRY!":OB(2)=0:OB(9)=0:OB(10)=82:GOTO4
4000 REM
4010 IFOB(10)=82OROB(5)=82THEN4030
4020 PRINT"THAT IS OBVIOUSLY IMPOSSIBLE.":GOTO200
4030 IFR<>81THENM$="I WOULD NOT ADVISE THAT TO BE DONE HERE. SEARCH FOR THE SUN!":GOTO4
4040 IFUD<11THENM$="DO NOT WASTE THE GIFT. BE PATIENT.":GOTO4
4050 IFOB(5)=82THENM$="THE THUNDERBOLT SHATTERS AGAINST ITS CHEST. IT SHAKES ITSELF AND REACHES FOR YOU...":GOSUB1:GOTO2130
4060 M$="THE THUNDERBOLT PIERCES THE CREATURE'S CHEST. IT LIFTS ITS ARMS TOWARD YOU AS IF IN SUPPLICATION "
4061 M$=M$+"AND TURNS TO DUST.":GOSUB1:M$="YOU SLOWLY TURN AND BEGIN WALKING DOWN THE HILL.":GOSUB1:END
4070 REM
4080 IFR=81AND(OB(10)=82OROB(6)=82)ANDUD>9THEN4050
4100 M$="I CANNOT STRIKE HERE... BUT IN OTHER PLACES AT ANOTHER TIME...":CM=1:GOSUB1:GOTO440
4200 REM
4210 DATA0,8,2,,,,,9,3,1,,,,10,4,2,,,,11,5,3,,,,12,6,4,,,,13,7,5,,,,14,,6,,,1,,9,,,,2,,10
4211 DATA8,,,3,,11,9,,,4,,12,10,,,5,,13,11,,,6,,14,12,,,7,,,13,,0
4220 DATA0,18,16,,,,,19,17,15,,,,20,,16,,,15,21,19,,,,16,22,20,18,,,17,23,27,19,,,18,,22,,,,19
4221 DATA0,23,21,,,20,,,22,,33,,27,25,,,,,28,26,24,,,,29,,25,,,24,30,28,20,,,25,31,29,27,,,26,32,,28,,0
4230 DATA27,,31,,,,28,,32,30,,,,,,,,,,36,34,,23,,,37,35,33,,,,38,,34,,,33,39,37,,,,34,40
4231 DATA38,36,,,35,41,,37,,,36,,40,,,,37,,41,39,,,38,,,40,,,,45,43,,,,,46,44,42,,,,47,,43,,0
4240 DATA42,48,46,,,,43,49,47,45,,55,44,50,,46,,,45,,49,,,,46,,50,48,,,47,,,49,,,,54,52,,,,,55
4241 DATA53,51,,,,56,,52,,,51,57,55,,,,52,58,56,54,46,,53,59,,55,,,54,,58,,,,55,,59,57,,,56,,,58,,60
4250 DATA0,,,61,59,,,,60,62,,,,,61,63,,,,,62,,64,,,,,,65,63,,,,,66,64,,,,,67,65,,,,,68
4251 DATA66,,,,69,,67,,,68,70,,,,,69,,71,,,,,,72,70,,,,,73,71,,,,,74,72,,,,,75,73,,,,,76,74
4260 DATA78,,77,,,75,79,,,76,,,80,76,79,,,,,77,,78,,,,78,81,,,,,,,80,,0
4270 DATASWAN,FLASHLIGHT,MATCH,MATCHES,TORCH,SAVE,QUIT,READ,NORTH,N,SOUTH,S,EAST,E,WEST,W,UP,U,DOWN,D
4271 DATALONG,SHORT,I,INV,INVENTORY,GET,TAKE,DROP,WAIT
4280 DATALOOK,DESCRIBE,THUNDERBOLT,THUNDERBOLT (MADE OF CLAY),CHANT,DESCENT,SAFE,POWERFUL,RELENT,MORTAL,MIGHTY
4281 DATAMORTAL,GOD,THIS,ALLOW,PILLAR,SAY,SAYETH,CHANT,LIGHT,ON,STRIKE,SWORD,KNIFE,DAGGER
4290 DATABURNING TORCH,SKELETON,POOL,BULL,BULL (BEING LED BY YOU),COMMIT,SELF,KILL,SACRIFICE,MONSTER
4291 DATASUICIDE,YOURSELF,HURL,THROW,UNDEAD,HAMMER,STRIKE,HIT,THUNDERBOLT (MADE OF STEEL),RESTORE,SAVE
4400 PRINT@1*32+3,"BENEATH THE TEMPLE OF ZEUS":PRINTTAB(15)"BY":PRINTTAB(12)"KEN ROSE":PRINT:PRINT
4450 M$="USE 'SAVE' TO PRESERVE YOUR PROGRESS. DEATH IS IMMINENT AND OFTEN. "
4480 M$=M$+"USE 'RESTORE' TO RESTORE YOUR ENDING POINT. USE 'LONG' IF YOU WANT DESCRIPTIONS "
4490 M$=M$+"OF ROOMS TO REPEAT. USE 'SHORT' TO RESTORE SHORT DESCRIPTIONS.":GOSUB1:RETURN
5000 M$=M$+W$(C1)+", ":Y=1:RETURN
21000 REM
21010 IFX=OB(1)THENC1=1:GOSUB5000
21020 IFX=OB(2)THENC1=2:GOSUB5000
21030 IFX=OB(3)ANDMA=1THENM$="A MATCH, ":Y=1:GOTO21050
21040 IFX=OB(3)THENM$=M$+RIGHT$(STR$(MA),1)+" MATCHES, ":Y=1
21050 IFX=OB(4)ANDR<15THENC1=5:GOSUB5000:GOTO21090
21060 IFX=OB(4)ANDR=32THENC1=55:GOSUB5000:GOTO21090
21070 IFX=OB(4)ANDLT=0THENC1=5:GOSUB5000
21080 IFX=OB(4)ANDLT=1THENC1=55:GOSUB5000
21090 IFX=OB(5)THENC1=33:GOSUB5000
21100 IFX=OB(6)THENC1=56:GOSUB5000
21110 IFX=OB(7)THENC1=53:GOSUB5000
21120 IFOB(8)=82THENC1=59:GOSUB5000:GOTO21140
21130 IFX=OB(8)THENC1=58:GOSUB5000
21140 IFX=OB(9)THENC1=70:GOSUB5000
21150 IFX=OB(10)THENC1=73:GOSUB5000
21160 IFY=0THENM$=M$+"NOTHING, "
21170 M$=LEFT$(M$,LEN(M$)-2):GOSUB1:Y=0:X=0
21190 IFR=33ANDUD=0THENUD=1:M$="THERE IS A STIRRING AND MOANING NEARBY.":GOSUB1
21200 RETURN
21210 REM MC-10 EDITS J. GERRIE
