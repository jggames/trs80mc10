4  '****************************
5  'PROGRAM:JERUSALEM ADVENTURE
6  'AUTHOR :WARREN MELNICK   
7  '  DISTRIBUTED FOR THE DRAGON
8  '  MICRO COMPUTER  BY:
9  '  microdeal (CORNWALL)
10 '  ALL RIGHTS RESERVED
11 '  COPYRIGHT (C) CLOAD 1981
12 '****************************
15 '
16 GOSUB 60000
20 CLS:PMODE0,1:PCLEAR1:CLEAR300:PRINT" ADVENTURE IN ANCIENT JERUSALEM":GOSUB 30000:EQ=0:MI=5:DIMR$(29),R(29,5),V$(38),N$(24),N(24),O$(24),O(24),I(MI),X(7):FORX=1TO29:READR$(X):FORY=0TO5:READR(X,Y):NEXTY,X:FORX=1TO38
25 READV$(X):NEXT:FORX=1TO24:READN$(X):NEXT:OS=64
30 FORX=1TO24:READO$(X):NEXT:FORX=1TO24:READN(X):NEXT:FORX=1TO24:READO(X):NEXTX:PI=0:PR=4:GG=0:PA=0:DO=0:GL=0:RN=0:GOSUB45000:GOSUB 31000:GOSUB30030:GOSUB31000:GOSUB30065:CLS:PRINT@352,"WELCOME TO ADVENTURE IN ANCIENT":PRINTTAB(11)"JERUSALEM"
40 GOTO 115
45 FORI=0TOEQ STEP32:PRINT@I:NEXT:PRINT@0,"YOU'RE "R$(PR):PRINT"VISIBLE OBJECTS:";:FORX=1TO23:GOSUB140:NEXTX:GOSUB790:PRINT"OBVIOUS DIRECTIONS-";:FORX=1TO6:IFR(PR,X-1)=0THEN110ELSEONX GOTO50,60,70,80,90,100
50 PRINT" N";:GOTO110
60 PRINT" S";:GOTO110
70 PRINT" E";:GOTO110
80 PRINT" W";:GOTO110
90 PRINT" U";:GOTO110
100 PRINT" D";
110 NEXTX:PRINT:PRINT"================================";:RETURN
115 GOSUB 45:GOTO600
120 GOSUB510:IFPR=25THEN130ELSE IFPR< >11THEN150ELSE PRINT"YOU ARE ATTACKED BY AN ARAB.     AND HE KILLS  YOU !"
130 PRINT"YOUR ADVENTURE HAS ENDED.":PRINT"DO YOU WISH TO TRY AGAIN ? ";:GOSUB700:IF LEFT$(A$,1)="Y" THEN20ELSE IF LEFT$(A$,1)<>"N" THEN130ELSE END
140 IFO(X)=PR THEN GOSUB20000:RETURN ELSE RETURN
150 GOSUB800:PRINT@480,"WHAT NOW? ----> ";:GOSUB700:IFA$="" THEN40ELSES=0:SP=LEN(A$):FORX=1TO LEN(A$):IF MID$(A$,X,1)=" " THENS=S+1:SP=X
160 NEXTX:IFS>1THEN PRINT"ONLY USE TWO WORDS PLEASE.":GOTO40
170 IFS=0THENV$= LEFT$(A$,3):N$="":GOTO200
180 IFSP<4THENV$= LEFT$(A$,SP-1)ELSEV$= LEFT$(A$,3)
190 N$= MID$(A$,SP+1,3)
200 FORV=1TO38:IFV$< >V$(V)THEN NEXT:IFSP<1THEN52000ELSE PRINT"I DON'T KNOW HOW TO":PRINT" "CHR$(34)LEFT$(A$,SP-1)CHR$(34)" SOMETHING.":GOTO40
205 IFV=32ORV=33THEN220
210 IFN$=""THENN=0ELSE FORN=1TO24:IFN$< >N$(N)THEN NEXT:PRINT"I DON'T KNOW WHAT YOU MEAN.":GOTO40
220 ONV GOTO230,230,230,230,230,230,350,350,370,370,390,420,420,330,250,250,250,480,480,480,570,570,340,300,300,270,270,270,270,520,560,310,460,430,130,580,740,570
230 D=V-1
240 IFR(PR,D)<>0THENPR=R(PR,D):GOTO40ELSEPRINT"YOU CAN'T GO IN THAT DIRECTION.":GOTO40
250 D=N-8:IFD<0ORD>5THEN260ELSE240
260 PRINT"I SIMPLY CAN'T DO THAT!":GOTO40
270 IFPR=7ORPR=20THEN PRINT"A PUFF OF SMOKE APPEARS AND":PRINT" SUDDENLY YOU'RE ELSEWHERE.":PR=2:GOTO40
280 PRINT"OKAY.";:PRINT RIGHT$(A$,LEN(A$)-SP):PRINT"NOTHING HAPPENED.":GOTO40
290 PR=2:GOTO40
300 PRINT"YOU ARE CARRYING THE FOLLOWING:":IFPI=0THEN PRINT" NOTHING AT ALL!":GOTO40ELSE FORX=1TO23:IFO(X)<0THEN PRINT" "O$(X):IFO(X)<>-1THENPRINT"  - WHICH YOU ARE WEARING."
305 NEXT:GOTO40
310 GOSUB320:A$="":A$=A$+ CHR$(PR+OS)+ CHR$(GG+OS)+ CHR$(PA+OS)+ CHR$(DO+OS)+ CHR$(MI+OS)+ CHR$(PI+OS)+ CHR$(GL+OS)+ CHR$(RN+OS):FORX=1TO23:A$=A$+ CHR$(O(X)+OS):NEXTX:FORX=1TOPI:A$=A$+ CHR$(I(X)+OS):NEXTX
315 OPEN"O",-1,"ADVDAT":PRINT#-1,A$:CLOSE:GOTO40
320 PRINT"READY CASSETTE AND <ENTER> ";:GOSUB700:RETURN
330 IFPR=9THEN590ELSE IFPR=20ORPR=7THEN910ELSE PRINT"TRY EXAMINING THINGS.":GOTO40
340 IF(N(N)AND2)<>0THENSP=0:A$="":GOTO280ELSE PRINT"O.K.":IFN<>2THENGOTO40ELSEPA=1:R(16,5)=20:PRINT"THERE IS A HOLE IN THE FLOOR":PRINT" WHERE THE TABLE WAS!":GOTO40
350 IFO(N)=-1THENPRINT"YOU ALREADY HAVE IT!":GOTO40ELSE IFO(N)< >PR THENPRINT"I CAN'T SEE IT HERE!!":GOTO40ELSE IFPI<MI THEN360ELSE PRINT"YOU ARE CARRYING TOO MUCH. TRY   DROPPING SOMETHING.":GOTO40
360 IFN=2ORN=20THEN365ELSEPI=PI+1:I(PI)=N:O(N)= -1:PRINT"OKAY.":GOTO40
365 PRINT"IT IS BEYOND MY POWER TO DO!":GOTO40
370 IFN=0THEN550ELSE FORX=1TOPI:IFI(X)=N THEN IFO(N)= -1THEN GOSUB380:GOTO40ELSE PRINT"TRY TAKING IT OFF!":GOTO40ELSE NEXTX:PRINT"YOU'RE NOT CARRYING IT!!":GOTO40
380 IFX=MI THENPI=PI-1:O(N)=PR:RETURN ELSE FORX=X TO MI-1:I(X)=I(X+1):NEXT:PI=PI-1:O(N)=PR:RETURN
390 FORX=1TOPI:IFI(X)<>N THEN NEXT:PRINT"HOW??":GOTO40ELSE IFN=1THEN400ELSE IFN=6THEN410ELSE PRINT"YOU TRY AND WEAR ONE!!":GOTO40 
400 O(N)= -2:GL=1:R(15,2)=16:IF(PR=15)THEN GOTO120 ELSE GOTO40
410 O(N)= -2:RN=1:IF(PR=8) AND (N=20) AND (GG=0)THEN GOTO120 ELSE GOTO40
420 IFO(N)<> -2THENPRINT"YOU'RE NOT WEARING IT!!":GOTO40ELSEO(N)= -1:IFN=1THENGL=0:R(15,2)=0:GOTO40ELSERN=0:PRINT"OKAY.";:GOTO40
430 SC=0:X=3:GOSUB440:GOSUB440:GOSUB440:GOSUB440:GOSUB440:X=14:GOSUB440:X=17:GOSUB440:X=19:GOSUB440:X=21:GOSUB440:SC=(SC+ SGN(GG))*10:PRINT"YOUR PRESENT SCORE IS:"SC:IFSC<100THEN40ELSE PRINT"WELL DONE! YOU COLLECTED ALL THE TREASURES."
432 PRINT "YOU HAVE COLLECTED ALL THE           TREASURES!"
435 GOTO 130
440 IFO(X)=9THENSC=SC+1
450 X=X+1:RETURN
460 GOSUB320:OPEN"I",-1,"ADVDAT":INPUT#-1,A$:PR= ASC(LEFT$(A$,1))-OS:GG= ASC(MID$(A$,2,1))-OS:PA= ASC(MID$(A$,3,1))-OS:DO= ASC(MID$(A$,4,1))-OS:MI= ASC(MID$(A$,5,1))-OS:PI= ASC(MID$(A$,6,1))-OS:GL= ASC(MID$(A$,7,1))-OS:RN= ASC(MID$(A$,8,1))-OS
470 FORX=1TO23:O(X)= ASC(MID$(A$,X+8,1))-OS:NEXT:IFPI=0THEN40ELSE FORX=1TOPI:I(X)= ASC(MID$(A$,X+31,1))-OS:NEXT:CLOSE:GOTO40
480 IFN=21 AND O(N)=-1THEN495ELSE IFN=6THEN490ELSE IFN=24THEN500ELSE IFO(N)=-1 OR O(N)=PR THEN PRINT"I SEE NOTHING SPECIAL.":GOTO40ELSE PRINT"I CAN'T SEE IT HERE.":GOTO40
490 IFO(N)<>-1 ANDO(N)<>-2THEN40ELSE PRINT"IT SAYS:":PRINT"I AM -TOUCHED- BY YOUR INTEREST  IN THE -GATE-.":GOTO40
495 PRINT"IT SAYS:":PRINT"      microdeal (CORNWALL) ":PRINT"FOR QUALITY SOFTWARE!":GOTO40
500 IFPR<>9THENN=8:GOTO480ELSE PRINT"I SEE SOME WRITING THERE!!":PRINT"IT SAYS: LEAVE *TREASURES* HERE!";:GOTO40
510 IFPR=20 AND DO=1THENPRINT"THE DOOR IS OPEN REVEALING A":PRINT" PASSAGE TO THE WEST!":GOTO45:RETURN ELSE IFPR=8 AND GG=2THENPRINT"THE GATE IS OPEN REVEALING A":PRINT" PASSAGE TO THE WEST THAT GOES?":GOTO45:RETURN
515 IFPR=15 AND GL=1THEN PRINT"THE GLASSES REVEAL A SECRET":PRINT" PASSAGE LEADING EASTWARD!":GOSUB45:RETURN ELSE RETURN
520 IFPR=20 AND N=15THEN530ELSE IFPR=8 AND N=23THEN540ELSE PRINT"IT IS BEYOND MY POWER TO DO.":GOTO40
530 FORX=1TOPI:IFI(X)<>16THEN NEXT:GOTO550ELSEDO=1:R(20,3)=19:IF(PR=20)THEN GOTO120 ELSE GOTO40
540 IFGG< >1THEN555ELSE FORX=1TOPI:IFI(X)< >16THEN NEXT:GOTO555ELSEGG=2:R(8,3)=7:IF(PR=8)THEN GOTO120 ELSE GOTO40
550 PRINT"HOW??":GOTO40
555 PRINT"I CAN'T DO THAT...YET!!":GOTO40
560 IF(PR=8 AND N=23) OR (PR=20 AND N=15)THEN PRINT"I OPENED IT, BUT I FIND IT QUITE IMPOSSIBLE TO CLOSE.":GOTO40ELSE550
570 IFPR=8 AND N=20 AND RN=1 AND GG=0THEN PRINT"THE RUBY BUTTON DISAPPEARS AND   REVEALS A VERY BIG LOCK!":GG=1:O(20)=0:GOTO40ELSE550
580 IFPR=9THEN PRINT"A VOICE BOOMS OUT:":PRINT" "CHR$(34)"AMEN TO JERUSALEM!"CHR$(34)".":GOTO150ELSEA$=" ":SP=0:GOTO280
590 PRINT"PRAY FOR YOUR LIFE!!":GOTO40
600 EQ=USR0(0)
630 PRINT@480,"";:GOTO120
700 A$=""
710 CC=0:CN=140:GOSUB900:B=ASC(B$):IFB=21THEN760ELSEIFB=8THEN730ELSEIFB=13THENPRINTCHR$(8):RETURNELSE IFB<32ORB$>"Z"THEN710
720 A$=A$+B$:PRINTCHR$(8)B$" ";:IF LEN(A$)>20THEN PRINT CHR$(8);:GOSUB800:RETURN ELSE710
730 IFA$=""THEN710ELSEA$= LEFT$(A$,LEN(A$)-1):PRINT CHR$(8);:GOTO710
740 IFN=15ORN=23THEN PRINT"TRY OPEN.":GOTO40ELSE550
760 IF LEN(A$)=0THEN710ELSE PRINT STRING$(LEN(A$),8);:GOTO700
790 IF POS(0)>0THEN PRINT:RETURN ELSE RETURN
800 X=USR2(EQ):PRINT@EQ,STRING$(32,61);:PRINT@480," ";:RETURN
900 B$=INKEY$:IFB$=""THENCC=CC+1:IFCC>10THENCN=271-CN:CC=0:PRINTCHR$(8)CHR$(CN);:GOTO900ELSE900ELSE RETURN
910 PRINT"AMEN TO JERUSALEM!":GOTO40
20000 IF LEN(O$(X))+POS(0)>30THEN PRINT
21000 PRINT" "O$(X)" ";:RETURN
30000 PRINT@66,"IN THIS ANCIENT MIDEASTERN AD-VENTURE YOU WILL BE ABLE TO GET,WEAR, AND EXAMINE THE OBJECTS   THAT YOU FIND"
30010 PRINT"  YOU WILL COMMAND ME IN ONE OR TWO WORD ENGLISH SENTENCES.":PRINT"  YOU WILL START IN A STREET IN JERUSALEM, THE ANCIENT BIBLICAL CITY. YOU MUST EXPLORE THE CITY WHILE OUT-MANEUVERING ARABS THATWILL KILL YOU IF YOU COME INTO"
30020 PRINT"THEIR QUARTER OF THE CITY.":RETURN
30030 PRINT@32,,,"  YOU MUST ALSO AVOID BURNING   YOUR EYES OUT IN THE DEAD SEA."
30040 PRINT"  BUT AS A REWARD, YOU WILL FINDNINE TREASURES HIDDEN WITHIN    JERUSALEM AND THE OTHER PLACES  THAT YOU WILL VISIT. YOU SCORE  10 POINTS FOR EACH TREASURE THATYOU STORE, PLUS 10 POINTS FOR"
30050 PRINT"GETTING THROUGH THE GOLDEN GATE OF THE ORIGINAL TEMPLE.
30060 PRINT"  YOU CAN ALSO SAVE OR LOAD THE GAME STATUS TO TAPE BY TYPING   SAVE OR LOAD.":PRINT:RETURN
30065 PRINT"   FOR DIRECTIONS YOU CAN TYPE  N,S,E,W,U OR D FOR NORTH, SOUTH, EAST, WEST, UP OR DOWN.
30070 PRINT"TYPE 'INV' FOR INVENTORY, THAT  TELLS YOU WHAT YOU'RE CARRYING. HELP GIVES YOU SOME HELP        -SOMETIMES.
30075 PRINT"TIPS-TO GET OUT OF THAT STREET  IN JERUSALEM YOU MUST GO SOUTH  FIRST. MAKE A MAP. BE RELIGIOUS AT THE WESTERN WALL."
30080 PRINT"PRAY A LOT, SAY MAGIC WORDS   , EXAMINE THINGS. GOOD LUCK"
30085 PRINT:PRINT
31000 PRINT@484,"<ENTER> TO CONTINUE ";:GOSUB700:RETURN
40000 DATA"ON A STREET IN JERUSALEM",2,3,2,2,0,0
40010 DATA"ON A STREET IN JERUSALEM",2,2,2,1,0,0
40020 DATA"ON A STREET IN JERUSALEM",1,4,2,1,0,0
40030 DATA"ON A STREET IN JERUSALEM",3,5,3,3,0,0
40040 DATA"IN AN UNDERPASS",4,9,0,0,6,0
40050 DATA"ON TOP OF THE WESTERN     WALL",0,0,0,0,0,9
40060 DATA"IN PARADISE!",26,28,29,27,0,0
40070 DATA"IN THE GOLDEN GATE AREA",0,0,9,0,0,0
40080 DATA"IN THE WESTERN WALL AREA",0,12,10,8,0,0
40090 DATA"IN THE ZION GATE AREA",0,0,11,9,0,0
40100 DATA"IN THE ARAB QUARTER",0,0,0,0,0,0
40110 DATA"ON A STREET NEAR THE      WESTERN WALL",9,0,13,0,0,0
40120 DATA"IN THE JEWISH QUARTER",13,13,14,12,0,0
40130 DATA"IN THE JEWISH QUARTER",13,15,13,13,0,0
40140 DATA"IN A SYNAGOGUE",14,0,0,0,0,0
40150 DATA"IN A HIDDEN ROOM IN THE   SYNAGOGUE",0,0,0,15,0,0
40160 DATA"ON A MOUNTAIN OVERLOOKING JERUSALEM",0,21,0,0,0,0
40170 DATA"IN A CAVE",0,22,0,21,0,0
40180 DATA"IN AN UNDERGROUND CAVERN",0,23,20,0,0,0
40190 DATA"IN AN UNDERGROUND CAVERN",0,0,0,0,0,0
40200 DATA"ON THE SHORE OF THE DEAD  SEA",17,25,22,21,0,0
40210 DATA"ON THE SHORE OF THE DEAD  SEA",18,25,23,21,0,0
40220 DATA"ON THE SHORE OF THE DEAD  SEA",19,25,24,22,0,0
40230 DATA"ON THE SHORE OF THE DEAD  SEA",0,25,24,23,0,0
40240 DATA"IN THE DEAD SEA. Y0U HAVE BURNED YOUR EYES OUT!!",0,0,0,0,0,0
40250 DATA"IN NORTH PARADISE",0,7,0,0,0,0
40260 DATA"IN WEST PARADISE",0,0,7,0,0,0
40270 DATA"IN SOUTH PARADISE",7,0,0,0,0,0
40280 DATA"IN EAST PARADISE",0,0,0,7,0,0
41000 DATAN,S,E,W,U,D,GET,TAK,PUT,DRO,WEA,UNW,REM,HEL,RUN,WAL,GO,LOO,REA,EXA,PUS,TOU,MOV,INV,I,SAY,YEL,SCR,SHO,OPE,CLO,SAV,LOA,SCO,QUI,PRA,UNL,PRE
41100 DATAGLA,TAB,CAL,BAG,COI,RIN,SCR,NOR,SOU,EAS,WES,UP,DOW,HAL,DOO,KEY,SHE,AME,STA,BUT,BOO,LOC,GAT,WAL
41200 DATAA PAIR OF GLASSES,AN OLD WOODEN TABLE,*GOLDEN CALF*,*BRONZED BAGEL*,*GOLD COINS*,*RUBY RING*,*DEAD SEA SCROLL*,,,,,,,*SILVA HALVA*,A DOOR,A SET OF KEYS
41250 DATA*ANCIENT SHEKELS*,,*SHEPHERD'S STAFF*,RUBY COLOURED BUTTON ON THE       GOLDEN GATE,*ARCHAIC BOOK*,,,
41300 DATA1,4,1,1,1,3,1,0,0,0,0,0,0,1,0,1,1,0,1,0,3,0,0,0
41400 DATA6,16,28,14,27,17,18,,,,,,,26,20,1,29,0,10,8,21,,,
45000 FORX=2800TO2852:READQ:POKEX,Q:NEXT
45050 DEFUSR0=2826
45060 DEFUSR2=2800
45070 RETURN
45100 DATA189,139,48,142,6,0,16,142,4,0,253,8,0,166,128,167,160,252,8,0,131,0,1,38,241,57
45200 DATA142,4,0,16,142,6,0,166,132,230,128,192,125,39,4,167,160,32,244,30,1,131,4,1,126,140,55
52000 PRINT"I DON'T KNOW HOW TO":PRINT" "CHR$(34)A$CHR$(34)" SOMETHING.":GOTO150
60000 CLS(1):PRINT@138,CHR$(128) "microdeal" CHR$(128);
60010 PRINT @203,"PRESENTS";
60020 PRINT @294,"FOR YOUR ENJOYMENT";:PRINT@326,"AN ADVENTURE IN -";
60030 FOR I=50 TO 150 :SOUND I,1:NEXT I
60040 CLS(0):PRINT @267,"jerusalem";
60050 SOUND 200,30:RETURN
