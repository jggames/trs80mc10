0 CLEAR600:DIMCC,ZZ,M$,I$:MC=16384:GOTO16
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 EQ=((PEEK(17024)AND1)*256+PEEK(17025))+MC:RETURN
16 GOSUB60000
20 CLS:PRINT" ADVENTURE IN ANCIENT JERUSALEM":GOSUB30000:EQ=0:MI=5
21 DIMR$(29),R(29,5),V$(38),N$(24),N(24),O$(24),O(38),X(7):FORX=1TO29:READR$(X):FORY=0TO5:READR(X,Y):NEXTY,X:FORX=1TO38
25 READV$(X):NEXT:FORX=1TO24:READN$(X):NEXT:OS=25
30 FORX=1TO24:READO$(X):NEXT:FORX=1TO24:READN(X):NEXT:FORX=1TO24:READO(X):NEXTX
31 PI=0:PR=4:GG=0:PA=0:DO=0:GL=0:RN=0:GOSUB45000:GOSUB31000:GOSUB30030:GOSUB31000:GOSUB30065
32 CLS:PRINT@352,"WELCOME TO ADVENTURE IN ANCIENT":PRINTTAB(11)"JERUSALEM"
40 GOTO115
45 FORI=0TOEQ-MCSTEP32:PRINT@I:NEXT:PRINT@0,;:M$="YOU'RE "+R$(PR):GOSUB1:GOSUB140
46 PRINT"OBVIOUS DIRECTIONS-";:FORX=1TO6:ON-(R(PR,X-1)=0)GOTO110:ONXGOTO50,60,70,80,90,100
50 PRINT" N";:GOTO110
60 PRINT" S";:GOTO110
70 PRINT" E";:GOTO110
80 PRINT" W";:GOTO110
90 PRINT" U";:GOTO110
100 PRINT" D";
110 NEXTX:PRINT:PRINT"================================";:RETURN
115 GOSUB45:GOTO600
120 GOSUB510:IFPR=25THEN130
121 IFPR<>11THEN150
122 M$="YOU ARE ATTACKED BY AN ARAB. AND HE KILLS YOU!":GOSUB1
130 PRINT"YOUR ADVENTURE HAS ENDED.":PRINT"DO YOU WISH TO TRY AGAIN ? ";:GOSUB700:IFLEFT$(A$,1)="Y"THEN20
131 IFLEFT$(A$,1)<>"N"THEN130
132 END
140 I$="VISIBLE OBJECTS: ":C1=0:FORX=1TO23:IFO(X)=PRTHENI$=I$+O$(X)+", ":C1=1
141 NEXTX:IFC1=0THENI$=I$+" "
142 M$=LEFT$(I$,LEN(I$)-2):GOSUB1:RETURN
150 GOSUB800:PRINT@480,"WHAT NOW? ----> ";:GOSUB700:IFA$=""THEN40
151 S=0:SP=LEN(A$):FORX=1TOLEN(A$):IFMID$(A$,X,1)=" "THENS=S+1:SP=X
160 NEXTX:IFS>1THENPRINT"ONLY USE TWO WORDS PLEASE.":GOTO40
170 IFS=0THENV$=LEFT$(A$,3):N$="":GOTO200
180 IFSP<4THENV$=LEFT$(A$,SP-1):GOTO190
181 V$=LEFT$(A$,3)
190 N$=MID$(A$,SP+1,3)
200 FORV=1TO38:IFV$<>V$(V)THENNEXT:ON-(SP<1)GOTO52000:PRINT"I DON'T KNOW HOW TO":PRINT"'"LEFT$(A$,SP-1)"' SOMETHING.":GOTO40
205 IFV=32ORV=33THEN220
210 IFN$=""THENN=0:GOTO220
211 FORN=1TO24:IFN$<>N$(N)THENNEXT:PRINT"I DON'T KNOW WHAT YOU MEAN.":GOTO40
220 ONVGOTO230,230,230,230,230,230,350,350,370,370,390,420,420,330,250,250,250,480,480,480
221 IFV>0THENONV-20GOTO570,570,340,300,300,270,270,270,270,520,560,310,460,430,130,580,740,570
230 D=V-1
240 IFR(PR,D)<>0THENPR=R(PR,D):GOTO40
241 PRINT"YOU CAN'T GO IN THAT DIRECTION.":GOTO40
250 D=N-8:IFD<0ORD>5THEN260
251 GOTO240
260 PRINT"I SIMPLY CAN'T DO THAT!":GOTO40
270 IFPR=7ORPR=20THENM$="A PUFF OF SMOKE APPEARS AND SUDDENLY YOU'RE ELSEWHERE.":GOSUB1:PR=2:GOTO40
280 PRINT"OKAY.";:PRINT RIGHT$(A$,LEN(A$)-SP):PRINT"NOTHING HAPPENED.":GOTO40
290 PR=2:GOTO40
300 PRINT"YOU ARE CARRYING THE FOLLOWING:":IFPI=0THENPRINT" NOTHING AT ALL!":GOTO40
301 FORX=1TO23:IFO(X)<0THENPRINT" "O$(X):IFO(X)<>-1THENPRINT"  - WHICH YOU ARE WEARING."
305 NEXT:GOTO40
310 GOSUB320:A$="":A$=A$+CHR$(PR+OS)+CHR$(GG+OS)+CHR$(PA+OS)+CHR$(DO+OS)+CHR$(MI+OS)+CHR$(PI+OS)+CHR$(GL+OS)+CHR$(RN+OS)
311 FORX=1TO23:A$=A$+CHR$(O(X)+OS):NEXTX:FORX=1TOPI:A$=A$+ CHR$(O(OS+X)+OS):NEXTX
315 OPEN"O",-1,"ADVDAT":PRINT#-1,A$:CLOSE:GOTO40
320 PRINT"READY CASSETTE AND <ENTER> ";:GOSUB700:RETURN
330 IFPR=9THEN590
331 IFPR=20ORPR=7THEN910
332 PRINT"TRY EXAMINING THINGS.":GOTO40
340 IF(N(N)AND2)<>0THENSP=0:A$="":GOTO280
341 PRINT"O.K.":IFN<>2THEN40
342 PA=1:R(16,5)=20:M$="THERE IS A HOLE IN THE FLOOR WHERE THE TABLE WAS!":GOSUB1:GOTO40
350 IFO(N)=-1THENPRINT"YOU ALREADY HAVE IT!":GOTO40
351 IFO(N)<>PRTHENPRINT"I CAN'T SEE IT HERE!!":GOTO40
352 IFPI<MITHEN360
353 M$="YOU ARE CARRYING TOO MUCH. TRY DROPPING SOMETHING.":GOSUB1:GOTO40
360 IFN=2ORN=20THEN365
361 PI=PI+1:O(OS+PI)=N:O(N)=-1:PRINT"OKAY.":GOTO40
365 PRINT"IT IS BEYOND MY POWER TO DO!":GOTO40
370 IFN=0THEN550
371 FORX=1TOPI:IFO(OS+X)=NTHEN375
372 NEXTX:PRINT"YOU'RE NOT CARRYING IT!!":GOTO40
375 IFO(N)=-1THENGOSUB380:GOTO40
376 PRINT"TRY TAKING IT OFF!":GOTO40
380 IFX=MITHENPI=PI-1:O(N)=PR:RETURN
381 FORX=XTOMI-1:O(OS+X)=O(OS+X+1):NEXT:PI=PI-1:O(N)=PR:RETURN
390 FORX=1TOPI:IFO(OS+X)<>NTHENNEXT:PRINT"HOW??":GOTO40
391 IFN=1THEN400
392 IFN=6THEN410
393 PRINT"YOU TRY AND WEAR ONE!!":GOTO40
400 O(N)=-2:GL=1:R(15,2)=16:IF(PR=15)THEN120
401 GOTO40
410 O(N)=-2:RN=1:IFPR=8ANDN=20ANDGG=0THEN120
411 GOTO40
420 IFO(N)<>-2THENPRINT"YOU'RE NOT WEARING IT!!":GOTO40
421 O(N)=-1:IFN=1THENGL=0:R(15,2)=0:GOTO40
422 RN=0:PRINT"OKAY.";:GOTO40
430 SC=0:X=3:GOSUB440:GOSUB440:GOSUB440:GOSUB440:GOSUB440:X=14:GOSUB440:X=17:GOSUB440:X=19:GOSUB440:X=21:GOSUB440
431 SC=(SC+SGN(GG))*10:PRINT"YOUR PRESENT SCORE IS:"SC:IFSC<100THEN40
432 PRINT"WELL DONE! YOU COLLECTED ALL THETREASURES."
435 GOTO130
440 IFO(X)=9THENSC=SC+1
450 X=X+1:RETURN
460 GOSUB320:CLOAD*O,"JERUSDAT":PR=O(31):GG=O(32):PA=O(33):DO=O(34):MI=O(35):PI=O(36):GL=O(37):RN=O(38)
470 IFPI>0THEN40
471 FORX=1TOPI:O(OS+X)=0:NEXT:GOTO40
480 IFN=21ANDO(N)=-1THEN495
481 IFN=6THEN490
482 IFN=24THEN500
483 IFO(N)=-1ORO(N)=PRTHENPRINT"I SEE NOTHING SPECIAL.":GOTO40
484 PRINT"I CAN'T SEE IT HERE.":GOTO40
490 IFO(N)<>-1ANDO(N)<>-2THEN40
491 PRINT"IT SAYS:":PRINT"I AM -TOUCHED- BY YOUR INTEREST  IN THE -GATE-.":GOTO40
495 PRINT"IT SAYS:":PRINT"      microdeal (CORNWALL) ":PRINT"FOR QUALITY SOFTWARE!":GOTO40
500 IFPR<>9THENN=8:GOTO480
501 PRINT"I SEE SOME WRITING THERE!!":PRINT"IT SAYS: LEAVE *TREASURES* HERE!";:GOTO40
510 IFPR=20ANDDO=1THENM$="THE DOOR IS OPEN REVEALING A PASSAGE TO THE WEST!":GOSUB1:GOTO45
511 IFPR=8ANDGG=2THENM$="THE GATE IS OPEN REVEALING A PASSAGE TO THE WEST THAT GOES?":GOSUB1:GOTO45
515 IFPR=15ANDGL=1THENM$="THE GLASSES REVEAL A SECRET PASSAGE LEADING EASTWARD!":GOSUB1:GOSUB45
516 RETURN
520 IFPR=20ANDN=15THEN530
521 IFPR=8ANDN=23THEN540
522 PRINT"IT IS BEYOND MY POWER TO DO.":GOTO40
530 FORX=1TOPI:IFO(OS+X)<>16THENNEXT:GOTO550
531 DO=1:R(20,3)=19:IF(PR=20)THEN120
532 GOTO40
540 IFGG<>1THEN555
541 FORX=1TOPI:IFO(OS+X)<>16THENNEXT:GOTO555
542 GG=2:R(8,3)=7:IF(PR=8)THEN120
543 GOTO40
550 PRINT"HOW??":GOTO40
555 PRINT"I CAN'T DO THAT...YET!!":GOTO40
560 IF(PR=8ANDN=23)OR(PR=20ANDN=15)THENPRINT"I OPENED IT, BUT I FIND IT QUITE IMPOSSIBLE TO CLOSE.":GOTO40
561 GOTO550
570 IFPR=8ANDN=20ANDRN=1ANDGG=0THENM$="THE RUBY BUTTON DISAPPEARS AND REVEALS A VERY BIG LOCK!":GOSUB1:GG=1:O(20)=0:GOTO40
571 GOTO550
580 IFPR=9THENPRINT"A VOICE BOOMS OUT:":PRINT" 'AMEN TO JERUSALEM!'":GOTO150
581 A$=" ":SP=0:GOTO280
590 PRINT"PRAY FOR YOUR LIFE!!":GOTO40
600 GOSUB7
630 PRINT@480,;:GOTO120
700 A$=""
710 CC=0:CN=140:GOSUB900:B=ASC(B$):IFB=21THEN760
711 IFB=8THEN730
712 IFB=13THENPRINTCHR$(8):RETURN
713 IFB<32ORB$>"Z"THEN710
720 A$=A$+B$:PRINTCHR$(8)B$" ";:IFLEN(A$)>20THENPRINTCHR$(8);:GOSUB800:RETURN
721 GOTO710
730 IFA$=""THEN710
731 A$=LEFT$(A$,LEN(A$)-1):PRINTCHR$(8);:GOTO710
740 IFN=15ORN=23THENPRINT"TRY OPEN.":GOTO40
741 GOTO550
760 IFLEN(A$)=0THEN710
761 FORZ1=1TOLEN(A$):PRINTCHR$(8);:NEXT:GOTO700
800 X=EQ:PRINT@EQ-MC,"================================";
803 PRINT@480,"*";:RETURN
900 B$=INKEY$:IFB$=""THEN915
901 RETURN
915 CC=CC+1:IFCC>10THENCN=271-CN:CC=0:PRINTCHR$(8)CHR$(CN);:GOTO900
916 GOTO900
910 PRINT"AMEN TO JERUSALEM!":GOTO40
30000 PRINT@66,"IN THIS ANCIENT MIDEASTERN AD-VENTURE YOU WILL BE ABLE TO GET,WEAR, AND EXAMINE THE OBJECTS   THAT YOU FIND"
30010 PRINT"  YOU WILL COMMAND ME IN ONE OR TWO WORD ENGLISH SENTENCES.":PRINT"  YOU WILL START IN A STREET IN JERUSALEM, ";
30011 PRINT"THE ANCIENT BIBLICAL CITY. YOU MUST EXPLORE THE CITY WHILE OUT-";:PRINT"MANEUVERING ARABS WHO ";
30012 PRINT"WILL KILL YOU IF YOU COME INTO"
30020 PRINT"THEIR QUARTER OF THE CITY.":RETURN
30030 PRINT@32,,,"  YOU MUST ALSO AVOID BURNING   YOUR EYES OUT IN THE DEAD SEA."
30040 PRINT"  BUT AS A REWARD, YOU WILL FINDNINE TREASURES HIDDEN WITHIN    JERUSALEM AND THE OTHER PLACES  ";
30041 PRINT"THAT YOU WILL VISIT. ";
30041 PRINT"YOU SCORE  10 POINTS FOR EACH TREASURE THATYOU STORE, PLUS 10 POINTS FOR"
30050 PRINT"GETTING THROUGH THE GOLDEN GATE OF THE ORIGINAL TEMPLE.
30060 PRINT"  YOU CAN ALSO SAVE OR LOAD THE GAME STATUS TO TAPE BY TYPING   SAVE OR LOAD.":PRINT:RETURN
30065 PRINT"   FOR DIRECTIONS YOU CAN TYPE  N,S,E,W,U OR D FOR NORTH, SOUTH, EAST, WEST, UP OR DOWN.
30070 PRINT"TYPE 'INV' FOR INVENTORY, THAT  TELLS YOU WHAT YOU'RE CARRYING. HELP GIVES YOU SOME HELP        -SOMETIMES.
30075 PRINT"TIPS-TO GET OUT OF THAT STREET  IN JERUSALEM YOU MUST GO SOUTH  FIRST. MAKE A MAP. BE RELIGIOUS AT THE WESTERN ";
30076 PRINT"WALL."
30080 PRINT"PRAY A LOT, SAY MAGIC WORDS,    EXAMINE THINGS. GOOD LUCK"
30085 PRINT:PRINT
31000 PRINT@484,"<ENTER> TO CONTINUE ";:GOSUB700:RETURN
40000 DATA"ON A STREET IN JERUSALEM",2,3,2,2,0,0
40010 DATA"ON A STREET IN JERUSALEM",2,2,2,1,0,0
40020 DATA"ON A STREET IN JERUSALEM",1,4,2,1,0,0
40030 DATA"ON A STREET IN JERUSALEM",3,5,3,3,0,0
40040 DATA"IN AN UNDERPASS",4,9,0,0,6,0
40050 DATA"ON TOP OF THE WESTERN WALL",0,0,0,0,0,9
40060 DATA"IN PARADISE!",26,28,29,27,0,0
40070 DATA"IN THE GOLDEN GATE AREA",0,0,9,0,0,0
40080 DATA"IN THE WESTERN WALL AREA",0,12,10,8,0,0
40090 DATA"IN THE ZION GATE AREA",0,0,11,9,0,0
40100 DATA"IN THE ARAB QUARTER",0,0,0,0,0,0
40110 DATA"ON A STREET NEAR THE WESTERN WALL",9,0,13,0,0,0
40120 DATA"IN THE JEWISH QUARTER",13,13,14,12,0,0
40130 DATA"IN THE JEWISH QUARTER",13,15,13,13,0,0
40140 DATA"IN A SYNAGOGUE",14,0,0,0,0,0
40150 DATA"IN A HIDDEN ROOM IN THE SYNAGOGUE",0,0,0,15,0,0
40160 DATA"ON A MOUNTAIN OVERLOOKING JERUSALEM",0,21,0,0,0,0
40170 DATA"IN A CAVE",0,22,0,21,0,0
40180 DATA"IN AN UNDERGROUND CAVERN",0,23,20,0,0,0
40190 DATA"IN AN UNDERGROUND CAVERN",0,0,0,0,0,0
40200 DATA"ON THE SHORE OF THE DEAD SEA",17,25,22,21,0,0
40210 DATA"ON THE SHORE OF THE DEAD SEA",18,25,23,21,0,0
40220 DATA"ON THE SHORE OF THE DEAD SEA",19,25,24,22,0,0
40230 DATA"ON THE SHORE OF THE DEAD SEA",0,25,24,23,0,0
40240 DATA"IN THE DEAD SEA. YOU HAVE BURNED YOUR EYES OUT!!",0,0,0,0,0,0
40250 DATA"IN NORTH PARADISE",0,7,0,0,0,0
40260 DATA"IN WEST PARADISE",0,0,7,0,0,0
40270 DATA"IN SOUTH PARADISE",7,0,0,0,0,0
40280 DATA"IN EAST PARADISE",0,0,0,7,0,0
41000 DATAN,S,E,W,U,D,GET,TAK,PUT,DRO,WEA,UNW,REM,HEL,RUN,WAL,GO,LOO,REA,EXA,PUS,TOU,MOV,INV,I,SAY,YEL
41001 DATASCR,SHO,OPE,CLO,SAV,LOA,SCO,QUI,PRA,UNL,PRE
41100 DATAGLA,TAB,CAL,BAG,COI,RIN,SCR,NOR,SOU,EAS,WES,UP,DOW,HAL,DOO,KEY,SHE,AME,STA,BUT,BOO,LOC,GAT,WAL
41200 DATAA PAIR OF GLASSES,AN OLD WOODEN TABLE,*GOLDEN CALF*,*BRONZED BAGEL*,*GOLD COINS*,*RUBY RING*
41201 DATA*DEAD SEA SCROLL*,,,,,,,*SILVA HALVA*,A DOOR,A SET OF KEYS
41250 DATA*ANCIENT SHEKELS*,,*SHEPHERD'S STAFF*,RUBY COLOURED BUTTON ON THE GOLDEN GATE,*ARCHAIC BOOK*,,,
41300 DATA1,4,1,1,1,3,1,0,0,0,0,0,0,1,0,1,1,0,1,0,3,0,0,0
41400 DATA6,16,28,14,27,17,18,,,,,,,26,20,1,29,0,10,8,21,,,
45000 RETURN:FORX=2800TO2852:READQ:REM POKEX,Q
45001 NEXT
45050 REM DEFUSR0=2826
45060 REM DEFUSR2=2800
45070 RETURN
45100 DATA189,139,48,142,6,0,16,142,4,0,253,8,0,166,128,167,160,252,8,0,131,0,1,38,241,57
45200 DATA142,4,0,16,142,6,0,166,132,230,128,192,125,39,4,167,160,32,244,30,1,131,4,1,126,140,55
52000 PRINT"I DON'T KNOW HOW TO":PRINT" '"A$"' SOMETHING.":GOTO150
60000 CLS(1):PRINT@138,CHR$(128) "microdeal" CHR$(128);
60010 PRINT@203,"PRESENTS";
60020 PRINT@294,"FOR YOUR ENJOYMENT";:PRINT@326,"AN ADVENTURE IN -";
60030 FORI=50TO150:SOUND I,1:NEXT I
60040 CLS(0):PRINT @267,"jerusalem";
60050 SOUND 200,30:RETURN
60104 REM **************************
60105 REM    JERUSALEM ADVENTURE
60106 REM  AUTHOR :WARREN MELNICK   
60111 REM  COPYRIGHT (C) CLOAD 1981
60112 REM **************************
