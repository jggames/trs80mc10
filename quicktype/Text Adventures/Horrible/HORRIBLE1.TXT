0 CLS:CLEAR500:DIMC1,C2,M$,I$:M=16384:GOTO30
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
10 INPUT"HIT enter TO CONTINUE";M$:CLS:RETURN
20 CLS:M$=LEFT$(DE$+"                                ",32):GOTO7
30 DIMN(34),S(34),E(34),W(34),OB(7),RD(34)
40 OB(1)=5:OB(2)=22:OB(3)=25:OB(4)=31:OB(5)=27:OB(6)=33:OB(7)=32
REM READING DATA INTO THE ARRAYS
60 FORA=1TO33:READX:N(A)=X:READ X:S(A)=X:READ X:E(A)=X:READ X:W(A)=X:X=0:RD(A)=0:NEXT
90 R=1:GOSUB1500:GOTO620
REM PARSER
100 V1$="":N1$=""
120 INPUT"WHAT NOW";A$
130 REM GOSUB20
140 FORA=1TOLEN(A$)
150 IFMID$(A$,A,1)=" "THENX=A-1:A=0:GOTO180
160 NEXTA
170 V1$=A$:GOTO300
180 V1$=LEFT$(A$,X)
190 IFRIGHT$(A$,LEN(A$)-X)=" "THENN1$="":GOTO300
REM BE SURE THERE IS A SPACE BETWEEN THE FIRST PAIR OF QUOTES AND NO SPACE BETWEEN THE SECOND PAIR OF QUOTES
200 N1$=RIGHT$(A$,LEN(A$)-(X+1)):GOTO300
300 REM VERB-HANDLING ROUTINE
310 IFV1$="GO"THENV1$=N1$:GOTO500
320 IFV1$="NORTH"ORV1$="N"ORV1$="SOUTH"ORV1$="S"ORV1$="EAST"ORV1$="E"ORV1$="WEST"ORV1$="W"THEN500
330 IFV1$="INVENTORY"ORV1$="INV"ORV1$="I"THEN700
340 IFV1$="GET"ORV1$="TAKE"THEN800
350 IFV1$="PUSH"ORV1$="PRESS"THEN900
360 IFV1$="DROP"THEN1000
370 IFV1$="QUIT"THEN3700
380 IFV1$="HIT"ORV1$="STRIKE"ORV1$="BEAT"ORV1$="KILL"THEN1200
390 IFV1$="EAT"THENGOSUB1300:GOTO100
400 IFV1$="FEED"ORV1$="GIVE"THEN1400
410 IFV1$="DANCE"THEN1100
420 IFV1$="LOOK"THENPRINT@32,;:RD(R)=0:GOTO610
490 M$="I DON'T KNOW HOW TO "+V1$+".":GOSUB1:GOTO100
REM MOVING AROUND ROUTINE
500 X=R
REM STORING CURRENT ROOM NUMBER
520 IFV1$="NORTH"ORV1$="N"THENR=N(R)
530 IFV1$="SOUTH"ORV1$="S"THENR=S(R)
540 IFV1$="EAST"ORV1$="E"THENR=E(R)
550 IFV1$="WEST"ORV1$="W"THENR=W(R)
560 IFX=28ANDR=32THEN590
570 IFR=28ANDS(28)=32THEN3570
REM RETURNING TO DRAGON'S CAVE
580 IFX=28THEN3590
REM TRAPPED IN DRAGON'S CAVE
590 IFR>0THENX=R:PRINT@32,;:GOTO610
REM YOU'VE MOVED AND X WILL BE USED IN SEARCHING FOR OBJECTS
600 IFR=0THENM$="YOU CAN'T MOVE THAT WAY (I THINK).":R=X:X=0:GOSUB1:GOTO100
REM ORIGINAL ROOM VALUE SETUP
610 IFR>19THEN618
612 ONRGOSUB1500,1530,1550,1570,1590,1610,1630,1650,1670,1690,1710,1730,1750,1770,1790,1810,1830,1850,1870
615 GOTO620
618 ONR-19GOSUB1890,1910,1930,1950,1970,1990,2010,2030,2050,2070,2090,2110,2130,2150
620 M$="OBJECTS HERE ARE: "
630 X=R:GOSUB2300:M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
640 X=0
650 M$="EXITS: "
651 IFN(R)>0THENM$=M$+"NORTH, "
652 IFS(R)>0THENM$=M$+"SOUTH, "
653 IFE(R)>0THENM$=M$+"EAST, "
654 IFW(R)>0THENM$=M$+"WEST, "
690 M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1:GOTO100
REM INVENTORY ROUTINE
700 X=34
720 M$="YOU ARE CARRYING: "
730 GOSUB2300:M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
740 GOTO100
800 REM GET ROUTINE
810 IFN1$=""THENM$="DON'T BE SILLY, I NEED A NOUN!":GOSUB1:GOTO890
820 GOSUB2200:IFX=0THEN100
830 IFOB(X)=ROROB(X)=34THEN850
840 PRINT"YOU CAN'T, AT THE MOMENT.":GOTO890
850 IFX=3ANDOB(7)<34THEN3410
860 OB(X)=34
870 IFOB(3)=34THEN3400
880 M$="YOU'VE GOT THE "+N1$+".":GOSUB1
890 X=0:GOTO100
REM PRESS ROUTINE
900 IFN1$=""THENM$="DON'T BE SILLY, I NEED A NOUN!":GOSUB1:GOTO890
920 IFR=11ORR=18THEN940
930 GOTO990
940 IFR=18THEN980
950 IFOB(1)=34THEN3060
960 IFN1$="BUTTON"THEN3050
970 GOTO3040
980 IFN1$="ROCK"ORN1$="STONE"THEN3220
990 M$="I CAN'T "+V1$+" A "+N1$+".":GOSUB1:GOTO100
REM ROUTINE TO DROP OBJECTS
1000 GOSUB2200:IFX=0THEN100
1020 IFOB(X)=34THEN1040
1030 M$="I'M NOT CARRYING THE "+N1$+".":GOSUB1:GOTO1080
1040 OB(X)=R
1050 IFN1$="WAGON"ANDOB(7)=RTHEN3420
1060 M$="I'VE DROPPED THE "+N1$+".":GOSUB1
1070 IFR=28ANDOB(X)=28THENOB(X)=0:GOTO3580
REM THE DRAGON'S GONNA EAT IT
1080 X=0:GOTO100
REM DANCE ROUTINE (SO TO SPEAK)
1100 M$="YOU START TO HUM A CATCHY LITTLE TUNE. YOU GRACEFULLY LEAP UP IN THE AIR, "
1115 M$=M$+"DO A PIROUETTE, LAND, DO A CARTWHEEL, AND TAKE A BOW.":GOSUB1
1120 IFR=28THEN3530
1130 M$="FURRY LITTLE CREATURES APPEAR, APPLAUD FURRIOUSLY, AND LEAVE.":GOSUB1:GOTO100
REM BEATING AND HITTING ROUTINES
1200 IFN1$=""THENM$="OKAY, BUT YOU HAVE TO TELL ME WHAT IT IS YOU WANT TO "+V1$+".":GOSUB1:GOTO100
1220 IFR=9ANDOB(7)<34THEN3300
1230 IFR=18ANDOB(2)=34THEN3230
1240 IFR=18THEN3250
1250 IFN1$="DRUM"ORN1$="DRUMSTICK"THEN3320
1260 IFR=28ANDOB(2)=34THEN3550
1270 IFR=28THEN3560
1280 M$="YOU CAN'T "+V1$+" A "+N1$+" HERE.":GOSUB1:GOTO100
1290 M$="YUM, YUM, THAT TASTED GOOD.":OB(5)=0:GOSUB1:RETURN
REM EATING ROUTINES
1300 IFN1$=""THENM$="TSK, TSK, PLEASE SUPPLY A NOUN.":GOSUB1:RETURN
1320 IFN1$="SANDWICH"ANDOB(5)=34THEN1290
1320 IFN1$="MCRIB"ANDOB(5)=34THEN1290
1340 IFN1$="SANDWICH"ORN1$="MCRIB"THENPRINT"YOU DON'T HAVE IT.":RETURN
1350 IFN1$="DRUMSTICK"ANDOB(4)=34THENPRINT"YUCK, THAT TASTED AWFUL.":OB(4)=0:RETURN
1360 IFN1$="DRUMSTICK"THENM$="HOW CAN YOU, YOU DON'T HAVE IT.":GOSUB1:RETURN
1370 M$="DON'T BE SILLY. YOU CAN'T EAT THAT!":GOSUB1:RETURN
REM GIVING OR FEEDING ROUTINE
1400 GOSUB2200:IFX=0THEN100
1420 IFOB(X)=34THEN1440
1430 M$="YOU CAN'T "+V1$+" SOMETHING YOU DON'T HAVE!":GOSUB1:GOTO100
1440 IFR=28THENOB(X)=0:GOTO3580
REM THE DRAGON'S GONNA EAT IT
1450 M$="I DON'T KNOW HOW TO TELL YOU THIS, BUT NO ONE WANTS IT.":GOSUB1:GOTO100
REM ROOM DESCRIPTION SUBROUTINE
1500 DE$="YOU ARE IN A FOREST":GOSUB20:IFRD(R)=1THENRETURN
1520 M$="IT IS A WARM SPRING DAY IN THE FOREST PRIMEVAL. YOU ARE DRESSED IN A JERKIN. CUTE LITTLE"
1522 M$=M$+" FURRY CREATURES BOUND THROUGH THE WOODS.":RD(R)=1:GOSUB1:RETURN
1530 DE$="YOU ARE IN A LEAFY FOREST":GOSUB20:IFRD(R)=1THENRETURN
1535 M$="THE LEAVES IN THE TREES ARE QUITE UNUSUAL SINCE THIS IS SPRING. "
1540 M$=M$+"THESE ARE LEAF SPRINGS.":RD(R)=1:GOSUB1:RETURN
1550 DE$="YOU ARE IN A LEAFY GLADE":GOSUB20:IFRD(R)=1THENRETURN
1560 M$="AREN'T YOU GLAD YOU'RE IN THE GLADE?":RD(R)=1:GOSUB1:RETURN
1570 DE$="YOU ARE IN THE PINE FOREST":GOSUB20:IFRD(R)=1THENRETURN
1575 M$="THE WIND BLOWING THROUGH THE PINES IS SINGING A SONG. YOU LISTEN CLOSELY AND CAN MAKE OUT "
1577 M$=M$+"SOME OF THE WORDS.":GOSUB1
1578 M$="THEY ARE, 'I OPINE A DRAGON TO SWEETEN MAKE SURE THAT HE'S EATEN'."
1580 RD(R)=1:GOSUB1:RETURN
1590 DE$="ALL THE TREES ARE DEAD HERE":GOSUB20:IFRD(R)=1THENRETURN
1600 RD(R)=1:RETURN
REM PUT YOUR OWN CLEVER MESSAGE ON THIS LINE
1610 DE$="YOU ARE ON A PAVED ROAD":GOSUB20:IFRD(R)=1THENRETURN
1615 M$="TO THE SOUTH IS THE NORTH SIDE OF A DRAWBRIDGE. THE BRIDGE LOOKS PRETTY RICKETY."
1620 RD(R)=1:GOSUB1:RETURN
1630 DE$="YOU ARE IN THE THRONE ROOM":GOSUB20:GOSUB3100
1640 RETURN
1650 DE$="YOU ARE IN THE ANTECHAMBER":GOSUB20:IFRD(R)=1THENRETURN
1660 M$="THIS, AS YOU'LL FIND, IS NOT A VERY LARGE CASTLE. IN FACT IT HAS ONLY THREE (I THINK) "
1665 M$=M$+"ROOMS.":RD(R)=1:GOSUB1:RETURN
1670 DE$="THIS IS THE KING'S BEDROOM":GOSUB20:IFOB(3)=34THENPRINT"THE ROOM IS EMPTY.":RETURN
1680 M$="THE POOR KING HAS BEEN EXPOSED TO WEREWOLF SIMPLEX II AND IS SLOWLY TURNING INTO "
1685 M$=M$+"A WOLF.":GOSUB1
1686 M$="HE EXPLAINS THAT UNLESS CURED BY THE MAGIC SPRING HE IS DOOMED SINCE HE CAN'T "
1687 M$=M$+"LEAVE THE PALACE.":RD(R)=1:GOSUB1:RETURN
1690 DE$="MORE FOREST":GOSUB20:IFRD(R)=1THENRETURN
1700 M$="JUST MORE AND MORE FOREST":RD(R)=1:RETURN
1710 DE$="NORTH END OF BRIDGE":GOSUB20:GOSUB3000:RETURN
1730 DE$="CAUSEWAY TO CASTLE":GOSUB20:IFRD(R)=1THENRETURN
1740 M$="TO THE NORTH YOU SEE A SMALL CASTLE. SMALL DOES NOT DO IT JUSTICE. IT IS REALLY SMALL. "
1745 M$=M$+"IF YOU WANT TO SEE HOW SMALL, GO NORTH.":RD(R)=1:GOSUB1:RETURN
1750 DE$="AND YET EVEN MORE FOREST":GOSUB20:IFRD(R)=1THENRETURN
1760 M$="IF YOU THINK IT'S DULL READING ABOUT THE FOREST, YOU SHOULD TRY YOUR HAND AT WRITING ABOUT "
1765 M$=M$+"IT.":RD(R)=1:GOSUB1:RETURN
1770 DE$="DARK FOREST":GOSUB20:IFRD(R)=1THENRETURN
1780 M$="THERE IS SOMETHING VERY STRANGE HERE. THE GROUND SOUNDS HOLLOW!":RD(R)=1:GOSUB1:RETURN
1790 DE$="SOUTH SIDE OF DRAWBRIDGE.":GOSUB20:IFRD(R)=1THENRETURN
1800 GOSUB3070:RETURN
1810 DE$="GENTLY ROLLING HILLS":GOSUB20:IFRD(R)=1THENRETURN
1820 M$="THE HILLS ARE ALIVE WITH THE SOUND OF MUSIC. THEY SING:":GOSUB1:M$="DON'T PUT YOUR SHOULDER "
1825 M$=M$+"TO THE BOULDER,":GOSUB1:M$="BUT TEST YOUR METTLE, AGAINST THE KETTLE.":RD(R)=1:GOSUB1:RETURN
1830 DE$="VOLCANIC HIGHLANDS":GOSUB20:IFRD(R)=1THENRETURN
1840 M$="ALL ADVENTURE GAMES HAVE TO HAVE AT LEAST ONE VOLCANO. THIS VOLCANO IS ALL POOPED OUT AND "
1845 M$=M$+"WILL NOT ERUPT DURING THIS GAME.":RD(R)=1:GOSUB1:RETURN
1850 DE$="VOLCANO VALLEY":GOSUB20:IFN(18)=14THENRETURN
REM IF N(19)=14 THE SECRET PASSAGE IS OPEN
1860 GOSUB3200:RETURN
1870 DE$="MARSHY SWAMP":GOSUB20:IFRD(R)=1THENRETURN
1880 M$="A SMALL DINOSAUR STICKS ITS TONGUE OUT AT YOU FROM BEHIND A FERN. IT THEN DARTS AWAY."
1885 RD(R)=1:GOSUB1:RETURN
1890 DE$="MORE MARSHY SWAMP":GOSUB20:IFRD(R)=1THENRETURN
1900 M$="THE SMALL DINOSAUR REAPPEARS AND HURLS A ROCK AT YOU. THE ROCK MISSES AND THE DINOSAUR DARTS "
1905 M$=M$+"AWAY.":RD(R)=1:GOSUB1:RETURN
1910 DE$="MUSHY SWAMP":GOSUB20:IFRD(R)=1THENRETURN
1920 M$="THE SMALL DINOSAUR TAUNTS YOU BY SAYING (IN DINOSAUR LANGUAGE):":GOSUB1
1925 M$=M$+"'NYAH, NYAH, THE DRAGON'S GONNA GET YOU!'":RD(R)=1:GOSUB1:RETURN
1930 DE$="A PLAIN":GOSUB20:IFRD(R)=1THENRETURN
1940 M$="OUTSIDE OF A RUSTY SWORD, THERE IS NOTHING UNUSUAL HERE.":RD(R)=1:GOSUB1:RETURN
1950 DE$="PLAIN PLAIN":GOSUB20:IFRD(R)=1THENRETURN
1960 PRINT"NOTHING UNUSUAL HERE.":RD(R)=1:RETURN
1970 DE$="PLANE PLAIN PLAIN":GOSUB20:IFRD(R)=1THENRETURN
1980 M$="NOT ONLY IS NOTHING UNUSUAL HERE, BUT IT'S VERY FLAT HERE.":RD(R)=1:GOSUB1:RETURN
1990 DE$="THE LAND OF THE MAGIC SPRING":GOSUB20:IFRD(R)=1THENRETURN
2000 M$="A GORGEOUS RAINBOW ARCHES ACROSS THE SKY AND PINK EGRETS FLAP HAPPILY BY. A SIGN PAINTED "
2005 M$=M$+"ON THE WALL SAYS:":GOSUB1:M$="'SATISFACTION GUARANTEED OR YOUR MONEY BACK!'":RD(R)=1:GOSUB1:RETURN
2010 DE$="NONDESCRIPT LAND":GOSUB20:IFRD(R)=1THENRETURN
2020 M$="I SIMPLY CAN'T DESCRIBE A NONDESCRIPT LAND.":RD(R)=1:GOSUB1:RETURN
2030 DE$="BREAK LAND":GOSUB20:IFRD(R)=1THENRETURN
2040 M$="LOOKING AROUND YOU SEE PICNIC TABLES AND GARBAGE CANS OVERFLOWING WITH LITTER.":GOSUB1
2045 M$="HOWEVER, IT APPEARS THAT YOU FRIGHTENED SOMEONE OR SOMETHING AWAY AS THEY LEFT THEIR LUNCH ON THE "
2047 M$=M$+"TABLE.":RD(R)=1:GOSUB1:RETURN
2050 DE$="LAIR OF THE DRAGON":GOSUB20:GOTO3500
REM OFF TO THE DRAGON ROUTINES
REM DON'T NEED SECONDARY DESCRIPTIONS HERE
2070 DE$="TWISTY LITTLE MAZES":GOSUB20:IFRD(R)=1THENRETURN
2080 M$="JUST KIDDING. NO MAZES IN THIS GAME. IF YOU WANT TO SEE MY FEELING ON MAZES SEE THE LAST "
2085 M$=M$+"ISSUE OF 'SOFTLINE'.":RD(R)=1:GOSUB1:RETURN
2090 DE$="ENTRANCE TO MAGIC LAND":GOSUB20:IFRD(R)=1THENRETURN
2100 M$="MAGIC LAND IS TOO GORGEOUS FOR WORDS.":RD(R)=1:GOSUB1:RETURN
2110 DE$="COLONEL'S CAVERN":GOSUB20:IFRD(R)=1THENRETURN
2120 M$="THIS APPEARS TO HAVE BEEN SOME SORT OF QUICK FOOD PLACE AT ONE TIME. THERE IS THE SMELL OF "
2125 M$=M$+"GREASE IN THE AIR.":RD(R)=1:GOSUB1:RETURN
2130 DE$="VERY SECRET CAVE":GOSUB20:IFRD(R)=1THENRETURN
2140 M$="SOMEONE HAS SPRAY PAINTED THIS CAVE AND SCRAWLED GRAFFITI ALL OVER THE WALL. I WON'T "
2145 M$=M$+"GIVE ANY MORE DETAILS AS I'M NOT THAT SORT OF COMPUTER.":RD(R)=1:GOSUB1:RETURN
2150 DE$="BOOM BOOM ROOM":GOSUB20:IFRD(R)=1THENRETURN
2160 PRINT"ISN'T THAT RICH?":RD(R)=1:RETURN
REM SUBROUTINE TO CHECK NOUNS
2200 X=0:IFN1$="STICK"THENX=1
2220 IFN1$="SWORD"THENX=2
2230 IFN1$="SPRING"THENX=3
2240 IFN1$="DRUMSTICK"THENX=4
2250 IFN1$="MCRIB"ORN1$="SANDWICH"THENX=5
2260 IFN1$="DRUM"THENX=6
2270 IFN1$="WAGON"THENX=7
2280 IFX=0THENPRINT"BE REALISTIC"
2280 RETURN
REM SUBROUTINE TO CHECK NOUNS
2300 IFX=OB(1)THENM$=M$+"A STICK, ":Y=1
2320 IFX=OB(2)THENM$=M$+"A SWORD, ":Y=1
2330 IFX=OB(3)THENM$=M$+"A MAGIC SPRING, ":Y=1
2340 IFX=OB(4)THENM$=M$+"A DRUMSTICK, ":Y=1
2350 IFX=OB(5)THENM$=M$+"A MCRIB SANDWICH, ":Y=1
2360 IFX=OB(6)THENM$=M$+"A TOY DRUM, ":Y=1
2370 IFX=OB(7)THENM$=M$+"A DRAGON WAGON, ":Y=1
2380 IFY=0THENM$=M$+"NOTHING. "
2400 Y=0:RETURN
2500 REM THESE ARE THE ROOM DATA STATEMENTS
2510 DATA,6,2,
2520 DATA,,3,1
2530 DATA,,4,2
2540 DATA,,5,3
2550 DATA,10,,4
2560 DATA1,11,,
2570 DATA,,8,
2580 DATA,12,9,7
2590 DATA,,,8
2600 DATA5,14,,
2610 DATA6,,12,
2620 DATA8,,13,11
2630 DATA,,14,12
2640 DATA10,,,13
2650 DATA,19,16,
2660 DATA,,17,15
2670 DATA,,18,16
2680 DATA,,,17
2690 DATA15,20,,
2700 DATA19,21,,
2710 DATA20,22,,
2720 DATA21,26,23,
2730 DATA,27,24,22
2740 DATA,29,,23
2750 DATA,30,,
2760 DATA22,31,27,
2770 DATA23,,28,26
2780 DATA,,,
2790 DATA24,,30,28
2800 DATA25,33,,29
2810 DATA26,,,
2820 DATA28,29,,
2830 DATA30,,,
REM DRAWBRIDGE ROUTINES
3000 IFS(11)=15THENPRINT"THE BRIDGE IS DOWN.":GOTO3030
3020 M$="THE BRIDGE IS UP BUT THERE IS A LARGE BUTTON JUST OUT OF YOUR REACH. "
3030 M$=M$+"A SIGN UNDER THE BUTTON SAYS: 'UNDER NO CIRCUMSTANCES PUSH THIS BUTTON!'":GOSUB1:RETURN
3040 M$="WHOEVER HEARD OF PRESSING A BUTTON WITH A "+N1$+"?":GOSUB1:GOTO100
3050 M$="YOU LEAP HIGH IN THE AIR AND PRESS THE BUTTON WITH YOUR HAND. THE POISONED LIZARD LIVING "
3055 M$=M$+"IN THE BUTTONHOLE PRESSES YOUR HAND WITH ITS FANGS AND...":GOSUB1:GOTO3700
3060 M$="I'LL PRESS THE BUTTON WITH YOUR STICK SO THE BRIDGE WILL DROP.":GOSUB1
3065 M$="OOPS, I'VE DROPPED THE STICK INTO THE RIVER":OB(1)=0:S(11)=15:GOSUB1:GOTO100
3070 M$="AS YOU PASS OVER THE DRAWBRIDGE A THREE-TOED OGRE RUNS FROM UNDER THE BRIDGE CARRYING "
3075 M$=M$+"YOUR STICK. HE PRESSES THE BUTTON, CATCHES THE POISONED LIZARD FROM THE BUTTONHOLE "
3080 M$=M$+"AND EATS IT.":GOSUB1:M$="THE BRIDGE RAISES HIGH UP IN THE AIR, MAKING IT IMPOSSIBLE TO RETURN."
3085 RD(R)=1:GOSUB1:RETURN
REM THRONE ROOM ROUTINES
3100 IFOB(7)=34ANDOB(3)=34THEN3130
3120 M$="THE THRONE ROOM IS EMPTY AND FAIRLY CLEAN EXCEPT FOR A DUSTY TAPESTRY ON THE WALL. "
3125 M$=M$+"THE TAPESTRY DEPICTS A DRAGON IN A CAVE EATING A MCRIB SANDWICH.":GOSUB1:RETURN
3130 M$="THE KING JUMPS UP AND DOWN ON THE MAGIC SPRING WHICH ACTIVATES ITS CURATIVE POWERS.":GOSUB1
3140 PRINT"HE IS CURED.":GOSUB10:M$="TO SHOW HIS GRATITUDE HE GIVES YOU THE DUSTY TAPESTRY, A DEED TO THE "
3145 M$=M$+"DRAGON'S CAVE, AND THE TAX BILL THAT THE DRAGON NEVER GOT AROUND TO PAYING ON THAT PARTICULAR "
3147 M$=M$+"PIECE OF PROPERTY.":GOSUB1
3150 M$="HE ALSO GIVES YOU A COMMISSION FOR A MUCH MORE LUCRATIVE QUEST, BUT THAT IS ANOTHER ADVENTURE "
3155 M$=M$+"FOR ANOTHER TIME.":GOSUB1:GOTO3700
REM VOLCANIC ROCK ROUTINE
3200 M$="YOU ARE IN A WEIRD VALLEY. BLOCKING THE NORTH SIDE OF THE CLIFF IS A HUGE ROCK. THE ROCK "
3215 M$=M$+"IS CHIPPED AND PRETTY WELL BEATEN UP AND DENTED.":GOSUB1:M$="A DENTED WITCH'S KETTLE IS "
3218 M$=M$+"BOLTED DOWN HERE.":GOSUB1:RETURN
3220 M$="YOU PUSH AGAINST IT WITH ALL YOUR MIGHT BUT NOTHING SEEMS TO HAPPEN.":GOSUB1:GOTO100
3230 IFN1$="KETTLE"THEN3234
3231 GOTO3240
3234 M$="YOU "+V1$+" THE SWORD AGAINST THE KETTLE. THE ROCK VIBRATES IN "
3235 M$=M$+"TUNE AND ROLLS ASIDE, REVEALING A PASSAGE TO THE NORTH.":N(18)=14:S(14)=18:GOSUB1:GOTO100
3240 M$="YOU "+V1+" THE SWORD AGAINST THE "+N1$+" BUT NOTHING HAPPENS.":GOSUB1:GOTO100
3250 M$="YOU "+V1$+" IT WITH YOUR HAND. NOTHING SEEMS TO HAPPEN EXCEPT YOUR HAND HURTS.":GOSUB1:GOTO100
REM MISCELLANEOUS HITTING AND KILLING ROUTINES
3300 M$="WHAT, YOU DARE TO "+V1$+" IN THE PRESENCE OF THE KING?":GOSUB1:M$="THE GUARDS SEIZE YOU "
3315 M$=M$+"AND DRAG YOU OFF KICKING AND SCREAMING TO BE FED TO THE DRAGON.":GOSUB1:GOTO3700
REM BEATING THE DRUM
3320 IFOB(4)=34ANDOB(6)=34THEN3370
3340 IFOB(4)<>34ANDOB(6)<>34THENPRINT"YOU DON'T HAVE THAT.":GOTO100
3350 IFOB(4)=34THENPRINT"PERHAPS IF YOU HAD A DRUM...":GOTO100
3360 IFOB(6)=34THENM$="YOU HIT THE DRUM WITH YOUR HAND AND PRODUCE A MUFFLED THUD.":GOSUB1:GOTO100
3370 IFR=28THEN3620
3380 M$="KABOOM, KABOOM. HITTING THE DRUM WITH THE DRUMSTICK PRODUCES A NICE BEAT. YOU'RE NO GENE "
3385 M$=M$+"KRUPA (WHO?), BUT YOU'LL DO IN A PINCH.":GOSUB1:GOTO100
REM GETTING THE SPRING
3400 M$="YOU PUSH AND TUG THE MAGIC METAL SPRING INTO THE WAGON AND FIND THAT YOU CAN NOW "
3405 M$=M$+"TRANSPORT IT. AREN'T YOU CLEVER, YOU RASCAL YOU.":GOSUB1:GOTO890
3410 M$="YOU CAN'T SEEM TO MOVE THE HEAVY SPRING.":GOSUB1:GOTO890
3420 OB(3)=R:M$="OH, OH, WITHOUT HAVING THE WAGON THE HEAVY METAL SPRING TUMBLES TO THE FLOOR.":GOSUB1
3425 GOTO1070
REM DRAGON ROUTINES
3500 M$="A HUGE, FIERCE, HUNGRY RED DRAGON GETS UP FROM ITS NEST IN THE MIDDLE OF THE CAVE.":GOSUB1
3515 M$="IT SNORTS FIRE FROM ITS NOSTRILS, BURPS, DOES A BIT OF THE OLD SOFT SHOE, AND SAYS TO YOU:":GOSUB1
3520 M$="'BOY I'M GLAD YOU MADE IT. NOT ONLY AM I BORED, BUT I'M STARVING. SHALL WE DANCE, OR "
3525 M$=M$+"SHALL YOU FEED ME FIRST?'":GOSUB1:GOTO100
3530 M$="THE DRAGON LOOKS AT YOU IN DISGUST. HE GETS UP AND SAYS, 'THAT'S AWFUL. THIS IS "
3535 M$=M$+"HOW YOU DO IT.'":GOSUB1:M$="HE IS EXTREMELY CLUMSY AND SAYS, 'I JUST CAN'T SEEM TO GET THE "
3537 M$=M$+"TEMPO RIGHT.'":GOSUB1:M$="HE IS GETTING MADDER AND MADDER. HE LOOKS AT YOU, SNARLS, AND SAYS, "
3540 M$=M$+"'WELL, IF YOU WON'T HELP ME... 'CHOMP!!":GOSUB1:GOTO3700
3550 M$="YOU WHIP OUT YOUR TRUSTY SWORD AND LUNGE. THE DRAGON LOOKS AT YOU AND SAYS: "
3555 M$=M$+"HOW THOUGHTFUL, HE BROUGHT HIS OWN TOOTHPICK.' ...CHOMP!!":GOSUB1:GOTO3700
3560 M$="YOU ATTACK THE DRAGON FURIOUSLY WITH YOUR BARE HANDS. THE DRAGON CHUCKLES AT YOU...":GOSUB1
3565 M$="'IF YOU HAD A MAGIC SWORD YOU MIGHT HAVE HAD A CHANCE.' ...CHOMP!!":GOSUB1:GOTO3700
3570 M$="THE DRAGON NOTICES YOU REENTER. 'MY, THAT WAS A NICE DANCE. NOW, I'M HUNGRY!' "
3575 M$=M$+" ...CHOMP!!":GOSUB1:GOTO3700
3580 M$="THE DRAGON GOBBLES UP THE "+N1$+". 'YUMMY, GOOD! WHAT (OR WHO) IS NEXT?'":GOSUB1:GOTO100
3590 X=0:R=28:M$="YOU'RE TRAPPED IN THE DRAGON'S CAVE. IF YOU DON'T DO SOMETHING SOON YOU'RE IN BIG "
3595 M$=M$+"TROUBLE.":GOSUB1:TC=TC+1:M$="YOU'VE ONLY GOT"+STR$(5-TC)+" MINUTES LEFT.":GOSUB1
3600 IFTC<5THEN100
3610 M$="OH, NO...THE DRAGON'S GETTING UP. HE'S GOING TO... CHOMP!!":GOSUB1:GOTO3700
3620 M$="WOW!!! THE DRAGON IS FASCINATED. HE GETS UP, TAKES THE DRUM AND DRUMSTICK, "
3625 M$=M$+"SNIFFS IT, AND BEGINS TO DANCE AND BEAT THE DRUMP IN 3/4 TIME.":GOSUB1
3630 M$="WHILE HE'S SO OCCUPIED, YOU NOTICE HE HAS MOVED AWAY FROM A PASSAGE TO THE SOUTH.":S(28)=32
3635 GOSUB1:GOTO100
3700 PRINT"AUF WIEDERSEHEN (GAME OVER)":END
3710 REM THE HORRIBLE ROTTEN
3720 REM DANCING DRAGON STRIKES
3730 REM SOFTLINE BY KEN ROSE
3740 REM MC-10 EDITS JIM GERRIE
