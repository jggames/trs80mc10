0 CLEAR400:DIMCC,ZZ,M$,Z$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 CLS:ME=19526-MEM:PRINTTAB(7)"THE WIZARD OF CHAOS":PRINT" A";ME;"BYTES ADVENTURE GAME BY"
22 PRINTTAB(4)"JOHN C LONNINGDAL - 2010":PRINT
23 M$="THE GOLD MASK OF POWER HAS BEEN STOLEN BY THE EVIL WIZARD OF CHAOS. YOU HAVE "
25 M$=M$+"BEEN GIVEN THE TASK TO GET THE MASK BACK BEFORE HE WIELDS ITS POWERS!":GOSUB1
30 R=1:GL=0:SW=0:BL=0:BO=0
50 GOSUB200
100 PRINT:INPUT"WHAT NOW";I$:OK=0:GOSUB600:IFI$="QUIT"THEN210
105 IFI$="INV"THENGOSUB250
106 IFI$="LOOK"THENGOSUB200
107 IFOK=1THEN100
108 IFOK=2THENGOSUB200:GOTO100
109 IFOK=3THENPRINT"OK":GOTO100
110 M$="I AM SORRY, I DO NOT UNDERSTAND.":GOSUB1
120 GOTO100
200 GOSUB300:GOSUB500:GOSUB400:OK=1:RETURN
210 M$="I GUESS WE WILL HAVE TO RELY ON SOMEONE ELSE TO SAVE THE WORLD.":GOSUB1:PRINT"BYE!":END
250 M$="YOU ARE CARRYING: ":CA=0:IFGL=1THENM$=M$+"GOLD COIN, ":CA=1
254 IFSW=1THENM$=M$+"A SWORD, ":CA=1
255 OK=1:IFCA=0THENM$=M$+"NOTHING":GOSUB1:RETURN
259 M$=LEFT$(M$,LEN(M$)-2):GOSUB1:RETURN
300 M$="YOU ARE ":ONRGOTO310,320,330,340,350
310 M$=M$+"IN YOUR OLD CABIN":GOSUB1:RETURN
320 M$=M$+"ON A DIRTY ROAD":GOSUB1:RETURN
330 M$=M$+"IN A SMALL CHURCH":GOSUB1:RETURN
340 M$="ON A NARROW PATH GOING UP A MOUNTAIN. A CASTLE LOOMS AT THE TOP":GOSUB1:RETURN
350 PRINT"IN THE CASTLE OF CHAOS":GOSUB1:RETURN
400 M$="YOU CAN SEE: ":OB=0:ONRGOSUB410,420,430,440,450
404 IFOB=0THENM$=M$+"NOTHING SPECIAL"
405 GOSUB1:RETURN
410 IFGL=0THENM$=M$+"GOLD COIN":OB=1
411 RETURN
420 M$=M$+"A MERCHANT":OB=1:RETURN
430 M$=M$+"AN ALTAR":OB=1:RETURN
440 IFBO=0THENM$=M$+"LARGE BOULDER":OB=1
441 RETURN
450 M$=M$+"WIZARD OF CHAOS":OB=1:RETURN
500 M$="FROM HERE YOU CAN GO: "
502 ONRGOTO510,520,530,540,550
510 M$=M$+"NORTH":RETURN
520 M$=M$+"SOUTH, WEST, EAST":RETURN
530 M$=M$+"EAST":RETURN
540 M$=M$+"WEST"
541 IFBO=1THENM$=M$+", NORTH":RETURN
542 RETURN
550 M$=M$+"SOUTH":RETURN
600 ONRGOTO610,620,630,640,650
610 IFI$="N"THENR=2:OK=2:RETURN
611 IFGL=0ANDI$="TAKE COIN"THENGL=1:OK=3:RETURN
612 RETURN
620 IFI$="S"THENR=1:OK=2:RETURN
621 IFI$="W"THENR=3:OK=2:RETURN
622 IFI$="E"THENR=4:OK=2:RETURN
623 IFI$="EXAMINE MERCHANT"ANDSW=0THENPRINT"HE HAS A SWORD FOR SALE":OK=1
624 IFI$="EXAMINE MERCHANT"ANDSW=1THENPRINT"NOTHING FOR SALE":OK=1
625 IFI$="BUY SWORD"ANDGL=1THENSW=1:GL=2:OK=3:RETURN
626 IFI$="BUY SWORD"ANDGL=0THENPRINT"MY SWORD IS NOT FOR FREE!":OK=1
629 RETURN
630 IFI$="E"THENR=2:OK=2:RETURN
631 IFI$="EXAMINE ALTAR" THEN PRINT"IT IS THE ALTAR OF THE GOLD MASK":OK=1
632 IFI$="PRAY"THEN634
633 RETURN
634 M$="A VOICE ECHOES FROM THE ALTAR: ONLY A TRUE BELIEVER WILL FEEL THE POWER OF THE GOLDEN MASK!":GOSUB1
636 INPUT"ARE YOU A BELIEVER";B$
637 IFB$="NO"THENBL=1
638 IFB$="YES"THENBL=2
639 PRINT:PRINT"AND SO BE IT!":OK=1:RETURN
640 IFI$="W"THENR=2:OK=2:RETURN
641 IFI$="N"ANDBO=1THENR=5:OK=2:RETURN
642 IFI$="EXAMINE BOULDER" THEN PRINT"IT LOOKS VERY HEAVY!":OK=1:RETURN
643 IFI$="PUSH BOULDER"ANDBL=2THEN646
644 IFI$="PUSH BOULDER"ANDBL<>2THENPRINT"IT WONT BUDGE!":OK=1
645 RETURN
646 M$="THE BOULDER FEELS LIGHT AND CRASHES DOWN THE MONTAIN SIDE!":GOSUB1
647 OK=1:BO=1:RETURN
650 IFI$="S"THENR=4:OK=2:RETURN
651 IFI$="EXAMINE CHAOS"THENPRINT"HE IS WEARING THE MASK":OK=1
652 IFI$="KILL CHAOS"ANDSW=0ANDBL<>1THEN700
653 IFI$="KILL CHAOS"ANDSW=0ANDBL=1THEN710
654 IFI$="KILL CHAOS"ANDSW=1ANDBL<>1THEN720
655 IFI$="KILL CHAOS"ANDSW=1ANDBL=1THEN730
659 RETURN
700 M$="YOU LASH OUT YOUR FISTS TOWARDS THE WIZARD, BUT WITH NO LUCK.":GOSUB1
701 M$="A POWERFUL RED BEAM OF LIGHT STREAMS FROM THE MASK AND CONSUMES YOU.":GOSUB1:GOTO800
710 M$="WITH ONLY YOUR FISTS, YOU CHARGE TOWARDS THE WIZARD. BUT BEFORE YOU "
711 M$=M$+"REACH HIM A BLINDING RED LIGHT EMITS FROM THE MASK AND ENGULFS YOU--":GOSUB1
712 M$="THE WIZARD, BAFFLED OVER THE FACT THAT IT HAS NO EFFECT ON YOU, "
713 M$=M$+"STILL MANAGES TO KNOCK YOU UNCONCIOUS WITH HIS STAFF BEFORE YOU REACH HIM.":GOSUB1:GOTO800
720 M$="YOU SWING YOUR SWORD QUICKLY TOWARDS THE WIZARD, BUT BEFORE IT HITS "
721 M$=M$+"HIM A RED BEAM OF LIGHT FROM THE MASK EVAPORATES YOU!":GOSUB1:GOTO800
730 M$="AS YOU LIFT YOUR SWORD THE WIZARD HAS UTTERED SOME WORDS THAT TRIGGERED THE "
732 M$=M$+"MASKS DESTRUCTIVE POWERS.":GOSUB1:M$="AS THE RED BEAM OF LIGHT FLOWS OUT OF IT TOWARDS "
734 M$=M$+"YOU, HE LAUGHS OUT WITH A MENACING GRIN.":GOSUB1:GOSUB900
735 M$="HIS FACE TWITCHES AS THE LIGHT PASSES THROUGH YOU WITH NO APPARENT EFFECT.":GOSUB1
737 M$="A SCREAM OF TERROR EMITS FROM HIS LIPS AS YOUR SWORD IS FIRMLY PLANTED IN HIS "
739 M$=M$+"SIDE.":GOSUB1:M$="DROPPING SLOWLY TO THE GROUND HIS BODY TURNS TO DUST POURING OUT OF "
741 M$=M$+"HIS HOODED CLOAK.":GOSUB1:GOSUB900
742 M$="BACK IN THE VILLAGE, THE PEOPLE ARE CHEERING AS YOU COME BACK AND THE "
744 M$=M$+"KING OFFERS YOU A NEW LARGE FARM WITH A NEWLY BUILT CASTLE.":GOSUB1
746 M$="UNFORTUNATLY HE WAS ALL OUT OF PRINCESSES AT THE MOMENT...":GOSUB1
800 PRINT"IT SEEMS YOUR QUEST IS OVER..."
801 INPUT"DO YOU WANT TO TRY AGAIN";T$
802 IF T$="YES" OR T$="Y"THEN10
803 END
900 INPUT"HIT enter TO CONTINUE";M$:CLS:RETURN
