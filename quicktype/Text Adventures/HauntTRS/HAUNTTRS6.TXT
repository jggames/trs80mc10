0 CLS0:CLEAR450:DIMCC,ZZ,M$,I$:K$="KILL GHOST":Z$="ATTACK GHOST":R$="READ SIGN":GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 INPUTT$:FL=0:M$=RIGHT$(T$,4):IFLEN(T$)>7THEN6
4 IFM$="GET"ORM$="READ"ORM$="OPEN"ORM$="DROP"ORM$="POUR"ORM$="RINK"ORM$="MASH"THENGOSUB3010:FL=0
5 IFM$="APER"ORM$="KEY"ORM$="NIFE"ORM$="ROPE"ORM$="LUGH"ORM$="ROLL"ORM$="WORD"ORM$="SIGN"THENGOSUB3110:FL=0
6 M$=LEFT$(T$,4):IFM$="BREA"ORM$="SMAS"ORM$="HIT "ORM$="KICK"THENPRINT"OUCH! IT DOESN'T WORK.":FL=1
7 IFT$="SAY PLUGH"ANDPL=1THENM$="SORRY, ONLY ONE PLUGH PER CUSTOMER.":GOSUB1:FL=1
8 IF(T$="READ BADGE"ORT$="EXAMINE BADGE")ANDPA=1THENM$="ON ITS BACK IS ENGRAVED A SINGLE WORD: 'PLUGH'":GOSUB1:FL=1
9 IFT$="READ BADGE"ANDPA=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
10 IFT$="READ PAPER"ANDSC=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
11 IFT$="READ PAPER"ANDSC=1THENM$="IT SAYS, 'THERE IS ESCAPE FROM THE SECOND FLOOR!'":GOSUB1:FL=1
12 IFLEFT$(T$,3)="INV"ORT$="I"THENGOSUB2910:FL=1
13 IFT$="QUIT"THENFL=1:INPUT"ARE YOU SURE";T$:IFLEFT$(T$,1)="Y"THEN362
14 IFT$="LOOK"ORT$="EXAMINE ROOM"ORT$="SEARCH ROOM"THENFL=2
16 IFBU<>1THEN19
17 M$=LEFT$(T$,4):IFM$<>"POUR"ANDM$<>"DUMP"ANDM$<>"EMPT"ANDM$<>"TIP "THEN19
18 M$="THE GROUND IS WET. THE BUCKET MAGICALLY REFILLS.":GOSUB1:FL=1
19 IFT$="DRINK WATER"ANDBU=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
20 IF(T$="DRINK WATER"ORT$="DRINK BUCKET")ANDBU=1THENM$="YOU FEEL SICK. IN FACT, YOU JUST DIED! IT WAS POSION.":GOSUB1:END
21 IF(T$="DROP BUCKET"ORT$="THROW BUCKET")ANDBU=1THENBU=-1:M$="THE BUCKET BREAKS AND THE WATER DRAINS AWAY.":GOSUB1:FL=1
23 IF(T$="BREAK SWORD"ORT$="TOUCH BLADE")ANDSW=1THENM$="OUCH! YOU HURT YOUR HAND.":GOSUB1:FL=1
24 IFT$="DROP SWORD"ANDSW=1THENSW=0:PRINT"OK.":FL=1:T$=""
25 IFT$="DROP SWORD"ANDSW=0THENM$="YOU'RE NOT CARRYING IT.":GOSUB1:FL=1
26 IF(T$="READ SWORD"ORT$="EXAMINE SWORD")ANDSW=1THENM$="THERE IS A FIGURE OF JUSTICE HOLDING A SWORD ON IT.":GOSUB1:FL=1
27 IFT$="DROP KEY"ANDKY=1THENKY=2:PRINT"OK. IT DISAPPEARS!":FL=1
28 IFT$="DROP KNIFE"ANDKN=1THENPRINT"OK. IT FLOATS AWAY.":KN=0:FL=1
29 IFT$="FLICK SWITCH"ORT$="LIGHT ON"THENPRINT"THERE'S NO POWER.":FL=1
30 IFT$="EXAMINE WINDOW"ORT$="EXAMINE WINDOWS"THENPRINT"THEY'RE ALL BOARDED SHUT.":FL=1
31 IFT$="RUN"THENPRINT"WHERE?":FL=1
32 IFT$="HELP"THENM$="THERE IS NO HELP. IT'S UP TO YOU!":GOSUB1:FL=1
33 IFT$<>"SAY PLUGH"AND(LEFT$(T$,3)="SAY"ORLEFT$(T$,5)="SHOUT"ORLEFT$(T$,6)="SCREAM")THENPRINT"THERE'S NOBODY HERE.":FL=1
34 IFT$="KNOCK"ORT$="KNOCK WALL"ORT$="KNOCK DOOR"THENPRINT"SOMETHING KNOCKS BACK.":FL=1
36 IF(T$="DROP SILVER"ORT$="DROP COINS")ANDMO=1THENPRINT"OK":MO=0:FL=1
37 IFT$="DROP SIGN"ANDSI=1THENSI=0:PRINT"OK":FL=1
38 IFT$="DROP PAPER"ANDSC=1THENSC=2:PRINT"OK":FL=1
39 RETURN

40 PRINT"ÄÄÄÄÄÄÄÄÄÄÄÄÄ–—ÄÄÄÄÄÄÄÄÄhauntedÄ";
41 PRINT"ÄÄÄÄÄ—”””””—÷‹◊€””””””““ÄÄhouseÄ";
42 PRINT"ÄÄÄÄ—ﬂ“–ÄÄÄ“—ﬂﬂﬂ€Ä—––—◊ﬂ“ÄÄÄÄÄÄÄ";
43 PRINT"ÄÄÄ—ﬂﬂﬂ“–ÄÄÄ‹ﬂﬂﬂﬁ‹ÄÄ”ﬂﬂﬂﬂ“ÄÄÄÄÄÄ";
44 PRINT"ÄÄ—ﬂﬂﬂﬂﬂ“–ÄÄ–ﬂ’Ä⁄ÄÄÄ—ﬂ’ü⁄ÄÄÄÄÄÄÄ";
45 PRINT"—◊ﬂﬂﬂﬂﬂﬂﬂ—–Ä‘ﬂ–⁄⁄–––’ﬂﬁ”⁄ÄÄÄÄÄÄÄ";
46 PRINT"ÄÄﬂﬂ◊Äﬂﬂ€ÿ”””◊ﬂﬂ⁄ﬁﬁﬁﬂﬂﬂﬂﬂ“––ÄÄÄÄ";
47 PRINT"ÄÄﬂﬂ’—ﬂﬂﬂ◊ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“ÄÄÄÄÄ";
48 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“ÄÄÄÄ";
49 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ”–ÄÄ";
50 PRINT"ÄÄﬂﬂﬂﬂﬂﬂ‹‹ﬂ‹›ﬂ‹‹›ﬂﬂ‹‹‹›ﬂ‹›‹›‹‹ÄÄ";
51 PRINT"ÄÄﬂﬂﬂﬂﬂﬂÄÄ⁄Ä’ﬂ––’ﬂ⁄“›‹›ﬂÄ’–’ÄÄÄÄ";
52 PRINT"ÄÄﬂﬂÄŸ◊ﬂÄÄ⁄Ä’ﬂ––‘ﬂﬁÄ’Ä’ﬂÄ’–’ÄÄÄÄ";
53 PRINT"ÄÄﬂﬂÄ⁄’ﬂﬂﬂﬂﬂﬂﬂ–Äêﬂﬂﬂﬂﬂﬂﬂﬂﬂ”◊“ÄÄÄ";
54 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ”””◊”ÄÄ";
55 PRINT"ÄÄ‹‹‹Ä‘ÄÄÄÄÄÄ‘–ÄÄ‘‹‹‹‹‹‹‹‹–––‘ÿ";
60 G1=1:G2=1:G3=1:G4=1:G5=0:G6=0:G7=0:G8=0:G9=1:G0=1:PA=2
70 GOSUB130:GOTO100

80 IFT$="DROP ROPE"ANDRP=1THENRP=0:PRINT"IT SLITHERS AWAY!":RETURN
81 IFT$="WEAR BADGE"THENPRINT"OK":WB=1:RETURN
82 IFT$="DROP BADGE"THENPA=2:M$="OK. SOMETHING MAKES IT SLIDE INTO A MOUSE HOLE.":GOSUB1:RETURN
86 M$=RIGHT$(T$,5):IFLEFT$(T$,4)<>"DROP"THEN90
87 IFM$<>"KNIFE"ANDM$<>"SWORD"ANDM$<>"UCKET"ANDM$<>"P KEY"ANDM$<>" ROPE"ANDM$<>"CROLL"ANDM$<>"BADGE"ANDM$<>" SIGN"THEN89
88 GOTO99
89 PRINT"YOU DON'T HAVE THAT.":RETURN
90 IFLEFT$(T$,3)<>"GET"THEN99
91 IF(M$="KNIFE"ANDKN=0)OR(M$="SWORD"ANDSW=0)OR(M$="UCKET"ANDBU<1)OR(M$="T KEY"ANDKY=0)THEN94
92 IF(M$=" ROPE"ANDRP=0)OR(M$="CROLL"ANDSC=0)OR(M$="BADGE"ANDPA=0)OR(M$=" SIGN"ANDSI=0)THEN94
93 IFM$="OTTLE"ORM$="TTLES"THENM$="THEY ALL FLY OFF THE TABLE AND SMASH.":GOSUB1:BO=1:RETURN
94 PRINT"THERE'S NOT ONE HERE.":RETURN
99 M$="I DON'T UNDERSTAND.":GOSUB1:RETURN

100 PRINTTAB(9)"HAUNTED HOUSE":M$="HAUNTED HOUSE IS A VERY SIMPLE GAME. THERE ARE NO TREASURES TO FIND. "
110 M$=M$+"THERE ARE NO SCORES TO KEEP. THERE IS NO TIME LIMIT.":GOSUB1
120 M$="YOU ONLY HAVE ONE TASK-- GET OUT OF THE HOUSE ALIVE!!":GOSUB1:GOTO180

130 REM PRESS A KEY
140 A$=INKEY$:IFA$=""THEN140
141 CLS:RETURN

180 PRINT
190 M$="GENERATIONS HAVE PASSED SINCE THE MCDANIEL FAMILY MYSTERIOUSLY DISAPPEARED.":GOSUB1
200 M$="IT IS SAID THAT A STRANGER CAME TO VISIT ON THAT COLD, "
210 M$=M$+"AUTUMN DAY MANY YEARS AGO, BUT NO ONE KNOWS FOR SURE.":GOSUB1
220 GOSUB140
230 M$="THEIR HOUSE HAS BEEN VACANT FOR DECADES NOW.":GOSUB1:M$="ITS TWO STORY IMAGE IS "
240 M$=M$+"FORLORN AND LOOMING, VISIBLE ONLY FROM THE NARROW, WINDING ROAD THAT "
250 M$=M$+"IS DISTORTED BY VEGETATION FROM THE SURROUNDING FOREST.":GOSUB1
260 M$="THE STONE WALL THAT ENCOMPASSES THE HOUSE IS DISCOLORED AND BROKEN FROM "
270 M$=M$+"YEARS OF NEGLECT, ITS IRON GATE RUSTY AND WORN BY ANGRY SEASONS.":GOSUB1:GOSUB140
280 M$="THE WINDOWS ARE BOARDED-- THE HOUSE IS QUIET AND NOT "
290 M$=M$+"ACCUSTOMED TO VISITORS.":GOSUB1:M$="THE WIND IS RESTLESS TODAY, BLOWING FALLEN "
300 M$=M$+"LEAVES IN ALL DIRECTIONS.":GOSUB1:M$="AS YOU WALK TOWARDS THE ENTRANCE OF THE HOUSE, "
310 M$=M$+"THE WIND GROWS DISTANT AND WEAK.":GOSUB1:M$="SUDDENLY, THE CALM AND SILENCE IS "
320 M$=M$+"BROKEN BY SOUNDS FROM WITHIN THE HOUSE!":GOSUB1
330 PRINT:M$="DO YOU HAVE THE COURAGE TO ENTER?":GOSUB1

350 REM GETYINPUT:
360 A$=INKEY$:IFA$="Y"THEN370
361 IFA$<>"N"THEN360
362 M$="THE SPIRITS WILL REMAIN RESTLESS...":GOSUB1:END

365 PRINT"THE MAT HAS 'MCDANIEL' ON IT.":IFPA=2THENPA=0:M$="THERE'S SOMETHING UNDERNEATH IT!":GOSUB1
366 GOTO420

370 CLS
380 PRINT"HAUNTED HOUSE!!"

400 REM OUTSIDE OF HOUSE
410 M$="YOU ARE AT THE OUTSIDE OF THE HOUSE.":GOSUB1
411 IFPA=0THENM$="THERE IS A FBI BADGE ON THE GROUND.":GOSUB1
412 PRINT"THE FRONT DOOR IS CLOSED."
420 GOSUB3:ONFLGOTO420,410
421 IFT$="EXAMINE DOOR"THENPRINT"IT'S VERY SOLID.":GOTO420
422 IFT$="EXAMINE MAT"ORT$="MOVE MAT"ORT$="LIFT MAT"ORT$="SEARCH MAT"ORT$="GET MAT"THEN365
423 IFT$="RING DOORBELL"ORT$="PRESS DOORBELL"ORT$="PRESS BUTTON"THENFORZ=1TO6:SOUND225,1:NEXT:GOTO420
430 IFT$="PLUGH"ORT$="SAY PLUGH"THENPL=1:M$="YOU MATERIALIZE INSIDE THE DOOR.":GOSUB1:GOTO510
460 IFT$="GET BADGE"ANDPA=0THENPA=1:PRINT"OK":GOTO420
481 IFT$="OPEN DOOR"THENM$="THE DOOR CAN'T BE OPENED.":GOSUB1:GOTO420
483 IFT$="DOOR"ORT$="BADGE"ORT$="EXAMINE BADGE"THENM$="WHAT SHOULD I DO WITH IT?":GOSUB1:GOTO420
486 IFT$="N"ORT$="S"ORT$="E"ORT$="W"THEN410
492 GOSUB80:GOTO420

500 REM FOYER
510 M$="YOU ARE AT THE FOYER.":GOSUB1
520 GOSUB3:ONFLGOTO520,510
534 IFT$="E"THEN610
535 IFT$="S"THEN1210
536 IFT$="W"THEN1310
537 IFT$="EXAMINE DOOR"THENM$="IT'S JUST AS SOLID ON THE INSIDE.":GOSUB1:GOTO520
538 IFT$="N"THEN510
550 GOSUB80:GOTO520

600 REM LIVING ROOM
610 M$="YOU ARE AT THE LIVING ROOM.":GOSUB1
620 IFKN=0THENM$="A KNIFE IS LEVITATING IN THE MIDDLE OF THE ROOM.":GOSUB1
630 IFSC=0THENM$="THERE IS A SCRAP OF PAPER ON THE GROUND.":GOSUB1
640 GOSUB3:ONFLGOTO640,610
641 IFT$="EXAMINE COUCH"ORT$="SEARCH COUCH"THENPRINT"NOTHING BUT LINT.":GOTO640
642 IFT$="GET PAPER"AND(KN=0ORPA=0)THEN662
653 IFT$="E"ANDKN>0THEN710
654 IFT$="W"THEN510
655 IFT$="GET PAPER"THENSC=1:PRINT"OK":GOTO640
656 IFT$="READ PAPER"ANDSC=1ANDKN=0THEN662
658 IFT$="GET KNIFE"ANDPA<>1ANDKN=0THENM$="THE KNIFE FLOATS OUT OF YOUR REACH.":GOSUB1:GOTO640
659 IFT$="GET KNIFE"ANDPA=1ANDKN=0THENKN=1:PRINT"OK":GOTO640
660 IFKN<>0THEN670
661 IFT$<>"N"ANDT$<>"S"ANDT$<>"E"THEN680
662 M$="SUDDENLY THE KNIFE WHOOSHES DOWN AND SLITS YOUR THROAT! YOU ARE DEAD.":GOSUB1:END
670 IFT$="N"ORT$="S"THEN610
680 GOSUB80:GOTO640

700 REM DINING ROOM
710 M$="YOU ARE AT THE DINING ROOM.":GOSUB1
720 GOSUB3:ONFLGOTO720,710
721 IFT$="EXAMINE TABLE"ORT$="SEARCH TABLE"THENPRINT"THERE'S A STAIN ON IT.":GOTO720
722 IFT$="EXAMINE BLOOD"ORT$="EXAMINE STAIN"THENPRINT"IT SPELLS OUT THE WORD 'HELP'":GOTO720
733 IFT$="S"THEN810
734 IFT$="W"THEN610
735 IFT$="N"ORT$="E"THEN710
738 GOSUB80:GOTO720

800 REM KITCHEN
810 M$="YOU ARE AT THE KITCHEN.":GOSUB1
820 IFBU=0THENM$="A BUCKET OF WATER IS ON THE FLOOR.":GOSUB1
830 GOSUB3:ONFLGOTO830,810
831 IFT$="SEARCH CUPBOARDS"ORT$="EXAMINE CUPBOARDS"ORT$="EXAMINE STOVE"THENPRINT"YOU FIND NOTHING.":GOTO830
843 IFT$="N"THEN710
844 IFT$="W"THEN1210
845 IFT$="S"ANDKN>0THENM$="A SPECTRAL HOUND HERE HOWLS AND FLEES WHEN IT SPOTS YOUR KNIFE.":GOSUB1:GOTO910
846 IFT$="S"ANDKN=0THENM$="YOU ARE IN THE BREAKFAST ROOM. A VICIOUS SPECTRAL HOUND DRIVES YOU OUT!":GOSUB1:GOTO810
847 IFT$="GET BUCKET"ANDBU=0THENBU=1:PRINT"OK":GOTO820
851 IFT$="E"THEN810
854 GOSUB80:GOTO830

900 REM BREAKFAST ROOM
910 M$="YOU ARE AT THE BREAKFAST ROOM.":GOSUB1
920 GOSUB3:ONFLGOTO920,910
921 IF(T$="EXAMINE TABLE"ORT$="SEARCH TABLE")ANDBO=0THENM$="IT'S COVERED WITH GIN BOTTLES.":GOSUB1:GOTO920
922 IF(T$="EXAMINE BOTTLE"ORT$="EXAMINE BOTTLES")ANDBO=0THENPRINT"THEY'RE LABELED 'XXX'":GOTO920
930 IFT$="N"THEN810
931 IFT$="E"THEN1010
932 IFT$="S"ORT$="W"THEN910
935 GOSUB80:GOTO920

1000 REM SERVANTS QUARTERS1
1010 M$="YOU ARE AT THE SERVANTS QUARTERS. THERE IS A CABINET ON ONE WALL.":GOSUB1
1020 GOSUB3:ONFLGOTO1020,1010
1021 IFT$="CABINET"THENGOSUB3110
1022 IFT$="SEARCH CABINET"ORT$="EXAMINE CABINET"THENT$="OPEN CABINET"
1041 IFT$="N"THEN1110
1042 IFT$="W"THEN910
1043 IFT$="OPEN CABINET"THENM$="IT'S EMPTY.":GOSUB1:GOTO1020
1044 IFT$="GET CABINET"THENM$="DON'T BE RIDICULOUS!":GOSUB1:GOTO1020
1045 IFT$="S"ORT$="E"THEN1010
1048 GOSUB80:GOTO1020

1100 REM SERVANTS QUARTERS2
1110 M$="YOU ARE AT THE SERVANTS QUARTERS. THERE IS A CABINET ON ONE WALL.":GOSUB1
1120 GOSUB3:ONFLGOTO1120,1110
1121 IFT$="CABINET"THENGOSUB3110
1122 IFT$="SEARCH CABINET"ORT$="EXAMINE CABINET"THENT$="OPEN CABINET"
1141 IFT$="S"THEN1010
1142 IFT$="OPEN CABINET"THENCA=1:PRINT"OK":ON-(KY<>0)GOTO1120:M$="THERE'S A KEY IN IT.":GOSUB1:GOTO1120
1143 IFT$="GET CABINET"THENM$="DON'T BE RIDICULOUS!":GOSUB1:GOTO1120
1144 IFT$="GET KEY"ANDCA=1THENKY=1:PRINT"OK":GOTO1120
1145 IFT$="N"ORT$="E"ORT$="W"THEN1110
1147 GOSUB80:GOTO1120

1200 REM DEN
1210 M$="YOU ARE AT THE DEN.":GOSUB1
1220 GOSUB3:ONFLGOTO1220,1210
1230 IFT$="SEARCH DESK"ORT$="EXAMINE DESK"THENM$="RECEIPTS FOR POTATOES, JUNIPER BERRIES AND TOMMY GUNS.":GOSUB1:GOTO1220
1242 IFT$="N"THEN510
1243 IFT$="E"THEN810
1244 IFT$="S"ORT$="W"THEN1210
1247 GOSUB80:GOTO1220

1300 REM EAST END HALL
1310 M$="YOU ARE AT THE EAST END OF THE HALL.":GOSUB1
1320 GOSUB3:ONFLGOTO1320,1310
1340 IFT$="N"THEN1510
1341 IFT$="E"THEN510
1342 IFT$="W"THEN1410
1343 IFT$="S"THEN1310
1347 GOSUB80:GOTO1320

1400 REM WEST END HALL
1410 M$="YOU ARE AT THE WEST END OF THE HALL. A LOCKED DOOR BARS THE WAY SOUTH.":GOSUB1
1420 GOSUB3:ONFLGOTO1420,1410
1421 IFT$="EXAMINE DOOR"THENPRINT"IT'S LOCKED.":GOTO1420
1430 IFT$="N"THEN1710
1431 IFT$="E"THEN1310
1432 IF(T$="S"ORT$="OPEN DOOR"ORT$="UNLOCK DOOR")ANDKY=1THEN1810
1433 IF(T$="S"ORT$="OPEN DOOR")ANDKY<>1THENM$="YOU'LL NEED A KEY TO GET THROUGH THAT DOOR.":GOSUB1:GOTO1420
1434 IFT$="W"THEN1410
1490 GOSUB80:GOTO1420

1500 REM GREEN BEDROOM
1510 M$="YOU ARE AT THE GREEN BEDROOM. THERE'S A PANEL ON THE WEST WALL.":GOSUB1
1520 GOSUB3:ONFLGOTO1520,1510
1521 IFT$="PANEL"THENGOSUB3110
1530 IFT$="SEARCH BED"ORT$="EXAMINE BED"THENPRINT"NOTHING BUT COBWEBS.":GOTO1520
1531 IFT$="SEARCH PANEL"ORT$="EXAMINE PANEL"THENPRINT"IT'S MADE OF WOOD.":GOTO1520
1540 IFT$="S"THEN1310
1541 IFT$="OPEN PANEL"ORT$="GO PANEL"ORT$="MOVE PANEL"THEN1610
1542 IFT$="N"ORT$="E"ORT$="W"THEN1510
1545 GOSUB80:GOTO1520

1600 REM SECRET PASSAGE
1610 M$="YOU ARE AT THE SECRET PASSAGE.":GOSUB1
1620 IFRP=0THENM$="A ROPE IS NEARBY.":GOSUB1
1630 GOSUB3:ONFLGOTO1630,1610
1640 IFT$="E"THEN1510
1641 IFT$="W"THEN1710
1642 IFT$="GET ROPE"THENRP=1:PRINT"OK":GOTO1630
1643 IFT$="N"ORT$="S"THEN1610
1646 GOSUB80:GOTO1630

1700 REM BLUE BEDROOM
1710 M$="YOU ARE AT THE BLUE BEDROOM. THERE'S A PANEL ON THE EAST WALL.":GOSUB1
1720 GOSUB3:ONFLGOTO1720,1710
1721 IFT$="PANEL"THENGOSUB3110
1725 IFT$="SEARCH BED"ORT$="EXAMINE BED"THENPRINT"NOTHING BUT BEDBUGS.":GOTO1720
1726 IFT$="SEARCH PANEL"ORT$="EXAMINE PANEL"THENPRINT"IT'S MADE OF WOOD.":GOTO1720
1730 IFT$="S"THEN1410
1731 IFT$="OPEN PANEL"ORT$="GO PANEL"ORT$="MOVE PANEL"THEN1610
1732 IFT$="N"ORT$="E"ORT$="W"THEN1710
1736 GOSUB80:GOTO1720

1800 REM MASTER BEDROOM
1810 M$="YOU ARE IN THE MASTER BEDROOM. A WALL OF RAGING FIRE BLOCKS THE WAY EAST.":GOSUB1
1820 GOSUB3:ONFLGOTO1820,1810
1821 IFT$="SEARCH BED"ORT$="EXAMINE BED"THENPRINT"NOTHING BUT OLD IOUS.":GOTO1820
1830 IFT$="N"THEN1410
1831 IFT$="E"THENM$="ARE YOU JUST GOING TO WALK RIGHT THROUGH THAT RANGING FIRE?":GOSUB1:GOTO1820
1832 IFT$="YES"THENPRINT"OK":GOTO1910
1833 IFT$="NO"THENPRINT"A WISE DECISION.":GOTO1820
1834 IFT$="W"ORT$="S"THEN1810
1840 IF(T$="DOUSE FIRE"ORT$="DOUSE FLAMES"ORT$="DROWN FIRE")ANDBU=1THENPRINT"IT DOESN'T GO OUT.":GOTO1820
1841 IFT$="THROW WATER"ORT$="SWING BUCKET"THENM$="THE WATER WON'T LEAVE THE BUCKET.":GOSUB1:GOTO1820
1890 GOSUB80:GOTO1820

1900 REM LIBRARY
1910 M$="YOU ARE AT THE LIBRARY. THERE IS A HOLE IN THE CEILING.":GOSUB1
1920 GOSUB3:ONFLGOTO1920,1910
1921 IFT$="USE ROPE"ORT$="TIE ROPE"THENPRINT"HOW?":GOTO1920
1930 IFT$="W"THEN1810
1931 IF(T$="DROP ROPE"ORT$="THROW ROPE")ANDRP=1THEN1935
1932 GOTO1940
1935 RP=0:RC=1:PRINT"OK"
1936 M$="INSTANTLY THE ROPE UNWINDS AND LEVITATES TO THE HOLE IN THE CEILING!":GOSUB1:GOTO1920
1940 IFT$="CLIMB ROPE"ORT$="JUMP ROPE"ORT$="CLIMB UP"ORT$="CLIMB"THEN1942
1941 GOTO1950
1942 IFRC<>1THENPRINT"NO.":GOTO1920
1943 M$="YOU DROP EVERYTHING YOU WERE CARRYING TO CLIMB THE ROPE. YOU REACH THE SECOND FLOOR.":GOSUB1
1944 PA=0:KY=0:KN=0:RP=0:BU=0:CA=0:SC=0:IFWB=1THENPA=1
1945 GOTO2000
1950 IFT$="N"ORT$="E"ORT$="S"THEN1910
1960 IFT$="READ BOOKS"ORT$="EXAMINE BOOKS"THENM$="NONE ARE INTERESTING, EXCEPT ONE BY CHARLIE GERRIE.":GOSUB1:GOTO1920
1990 GOSUB80:GOTO1920

1999 REM DIMLY ROOM SWORD
2000 M$="YOU ARE IN A DIMLY LIT ROOM. THERE IS A HOLE IN THE FLOOR.":GOSUB1
2010 IFSW=0THENM$="THERE IS A GLOWING SWORD ON THE FLOOR.":GOSUB1
2020 GOSUB3:ONFLGOTO2020,2000
2030 IFT$="E"THEN2110
2040 IFT$="W"THEN2310
2041 IFT$="S"THEN2210
2042 IFT$="GET SWORD"THENSW=1:PRINT"OK":GOTO2020
2046 IFT$="CLIMB ROPE"ORT$="CLIMB DOWN"ORT$="GO HOLE"THEN2055
2047 IFT$="N"THEN2000
2050 GOSUB80:GOTO2020

2055 M$="YOU FALL THROUGH THE HOLE AND BREAK YOUR NECK! YOU ARE DEAD.":GOSUB1:END

2060 M$="THE GHOST FLIES AT YOU AND KILLS YOU.":GOSUB1:END
2061 M$="THE GHOST IS IMMUNE TO YOUR ATTACK!":GOSUB1:RETURN

2070 M$="YOUR MAGIC SWORD ENABLES YOU TO KILL THE GHOST!":GOSUB1:RETURN
2080 M$="THE GHOST WILL NOT LET YOU PASS!":GOSUB1:RETURN
2090 M$="THE 'BODY' OF A 'DEAD' GHOST WRITHES ON THE FLOOR.":GOSUB1:RETURN
2091 GR=0:IFT$="EXAMINE GHOST"THENM$="IT LOOKS VICIOUS.":GOSUB1:GR=1
2092 RETURN
2093 GR=0:IFT$="DROP BADGE"THENM$="THE GHOST HOVERS OVER IT. THE BADGE DISAPPEARS.":GOSUB1:PA=2:GR=1
2095 IFT$="EXAMINE GHOST"THENM$="HE'S SOMEHOW NOT AS TERRIFYING AS THE OTHERS.":GOSUB1:GR=1
2096 RETURN
2098 PRINT"YOU ARE IN A DIMLY LIT ROOM.":RETURN
2099 PRINT"THERE IS A GHOST HERE.":RETURN

2100 REM DIMLY ROOM GHOST1
2110 GOSUB2098
2120 IFG1=1THENGOSUB2099
2130 IFG5=1THENGOSUB2090
2140 GR=0:GOSUB3:ONFLGOTO2140,2110:GOSUB2091:ONGRGOTO2140
2150 IF(T$=K$ORT$=Z$)ANDG1=1ANDSW=1THENG1=0:G5=1:GOSUB2070:GOTO2140
2151 IF(T$=K$ORT$=Z$)ANDG1=1ANDSW=0THEN2060
2152 IF(T$=K$ORT$=Z$)ANDG5=1THENM$="THE THING IS ALREADY DEAD.":GOSUB1:GOTO2140
2156 IFT$="W"THEN2000
2157 IF(T$="N"ORT$="E"ORT$="S")AND(G5=1ORSW=1)THEN2110
2158 IF(T$="N"ORT$="E"ORT$="S")ANDG1=1THEN2060
2162 GOSUB80:GOTO2140

2200 REM DIMLY ROOM GHOST2
2210 GOSUB2098
2220 IFG2=1THENGOSUB2099
2230 IFG6=1THENGOSUB2090
2240 GR=0:GOSUB3:ONFLGOTO2240,2210:GOSUB2091:ONGRGOTO2240
2250 IF(T$=K$ORT$=Z$)ANDG2=1ANDSW=1THENG2=0:G6=1:GOSUB2070:GOTO2240
2251 IF(T$=K$ORT$=Z$)ANDG2=1ANDSW=0THEN2060
2252 IF(T$=K$ORT$=Z$)ANDG6=1THENM$="THE THING IS ALREADY DEAD.":GOSUB1:GOTO2240
2256 IFT$="N"THEN2000
2257 IF(T$="W"ORT$="E"ORT$="S")AND(G6=1ORSW=1)THEN2210
2258 IF(T$="W"ORT$="E"ORT$="S")ANDG2=1THEN2060
2262 GOSUB80:GOTO2240

2300 REM DIMLY ROOM GHOST3
2310 GOSUB2098
2320 IFG3=1THENGOSUB2099
2330 IFG7=1THENGOSUB2090
2340 GR=0:GOSUB3:ONFLGOTO2340,2310:GOSUB2091:ONGRGOTO2340
2350 IF(T$=K$ORT$=Z$)ANDG3=1ANDSW=1THENG3=0:G7=1:GOSUB2070:GOTO2340
2351 IF(T$=K$ORT$=Z$)ANDG3=1ANDSW=0THEN2060
2352 IF(T$=K$ORT$=Z$)ANDG7=1THENM$="THE THING IS ALREADY DEAD.":GOSUB1:GOTO2340
2356 IFT$="E"THEN2000
2357 IFT$="W"ANDG7=1THEN2410
2358 IF(T$="N"ORT$="W"ORT$="S")AND(G7=1ORSW=1)THEN2310
2359 IF(T$="N"ORT$="W"ORT$="S")ANDG3=1THEN2060
2363 GOSUB80:GOTO2340

2400 REM DIMLY ROOM UNKILL1
2410 GOSUB2098:GOSUB2099
2420 GR=0:GOSUB3:ONFLGOTO2420,2410:GOSUB2093:ONGRGOTO2420
2430 IF(T$=K$ORT$=Z$)THENGOSUB2061:GOTO2420
2444 IFT$="E"THEN2310
2445 IFT$="N"ANDSW=0ANDG1+G2+G3=0ANDSO=1THEN2510
2446 IFT$="S"ANDSW=0THENSO=1:GOTO2410
2447 IF(T$="N"ORT$="S"ORT$="W")AND(SW=1ORG1+G2+G3>0)THENGOSUB2080:GOTO2420
2449 IFT$="W"THEN2410
2451 GOSUB80:GOTO2420

2500 REM DIMLY ROOM UNKILL2
2510 GOSUB2098:GOSUB2099
2520 GR=0:GOSUB3:ONFLGOTO2520,2510:GOSUB2093:ONGRGOTO2520
2532 IF(T$=K$ORT$=Z$)ANDSW=0THENGOSUB2061:GOTO2520
2533 IFT$="E"THEN2310
2534 IFT$="W"THEN2610
2535 IFT$="S"THEN2410
2536 IFT$="N"THEN2510
2539 GOSUB80:GOTO2520

2600 REM DIMLY ROOM UNKILL3
2610 GOSUB2098:GOSUB2099
2620 GR=0:GOSUB3:ONFLGOTO2620,2610:GOSUB2093:ONGRGOTO2620
2632 IF(T$=K$ORT$=Z$)ANDSW=0THENGOSUB2061:GOTO2620
2633 IFT$="E"THEN2510
2634 IFT$="S"THEN2710
2635 IFT$="N"ORT$="W"THEN2610
2638 GOSUB80:GOTO2620

2700 REM DIMLY ROOM ENDGAME
2710 GOSUB2098
2715 IFSI=0THENM$="THERE IS A RUSTY OLD SIGN LAYING ON THE GROUND WITH SOMETHING SCRATCHED ON IT.":GOSUB1
2716 IFMO=0THENM$="THERE'S A HUGE PILE OF SILVER COINS HERE.":GOSUB1
2720 GOSUB3:ONFLGOTO2720,2710
2730 IFT$="GET SIGN"THENSI=1:PRINT"OK":GOTO2720
2731 IF(T$=R$ORT$="EXAMINE")ANDSI=0THENM$="YOU ARE NOT CARRYING IT.":GOTO2720
2732 IFT$="GET SILVER"ORT$="GET COINS"THENMO=1:PRINT"OK":GOTO2720
2733 IF(T$="EXAMINE COIN"ORT$="EXAMINE COINS")ANDMO=0THENPRINT"YOU ARE NOT CARRYING THEM.":GOTO2720
2733 IF(T$="EXAMINE COIN"ORT$="EXAMINE COINS")ANDMO=1THENPRINT"THEY WERE MINTED IN 1932.":GOTO2720
2740 IF(T$=R$ORT$="EXAMINE SIGN")ANDSI=1THEN2742
2741 GOTO2746
2742 M$="THE MESSAGE ON THE SIGN SAYS, 'THERE ARE THREE EXITS FROM THIS ROOM.":GOSUB1
2743 M$="ONLY ONE IS TRUE... YOU MUST KNOW, BUT NOT BE BURDENED BY THIS CLUE!'":GOSUB1:GOTO2720
2746 IFT$="N"THEN2610
2747 IFT$="E"ORT$="W"ORT$="S"THEN2750
2749 GOTO2760
2750 IFSI=1ORMO=1THENM$="YOU FALL THROUGH A TRAP DOOR TO YOUR DEATH!":GOSUB1:END
2751 M$="YOU GO THROUGH A DOOR AND FIND YOURSELF ON A BALCONY.":GOSUB1
2752 M$="YOU CLIMB DOWN A TREE AND ESCAPE TO SAFETY! CONGRATULATIONS! YOU MADE IT!":GOSUB1
2753 M$="AS YOU LOOK BACK AT THE HOUSE BEFORE LEAVING YOU SEE A LIGHT IN A SECOND FLOOR WINDOW SLOWLY FADE.":GOSUB1:END
2760 GOSUB80:GOTO2720

2900 REM INVENTORY
2910 I$="":IFPA=1THENI$=I$+"FBI BADGE, "
2911 IFKY=1THENI$=I$+"KEY, "
2912 IFKN>0THENI$=I$+"KNIFE, "
2913 IFRP=1THENI$=I$+"ROPE, "
2914 IFBU=1THENI$=I$+"WATER BUCKET, "
2915 IFSC=1THENI$=I$+"PAPER, "
2916 IFSW=1THENI$=I$+"MAGIC SWORD, "
2917 IFSI=1THENI$=I$+"RUSTY SIGN, "
2918 IFMO=1THENI$=I$+" SILVER COINS, "
2920 IFI$=""THENI$="NOTHING, "
2930 M$="YOU ARE CARRYING: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1
2950 RETURN

3000 REM GET NOUN
3010 V$=T$:INPUT"WHAT";N$:T$=V$+" "+N$:RETURN

3100 REM GET VERB
3110 N$=T$:INPUT"WHAT DO YOU WANT ME TO DO WITH  IT";V$:T$=V$+" "+N$:RETURN

4000 REM CONVERSION TO BASIC
4010 REM DONALD FOSTER
4020 REM MC-10 EDITS JIM GERRIE
