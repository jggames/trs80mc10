0 CLS0:CLEAR450:DIMCC,ZZ,M$,I$:K$="KILL GHOST":R$="READ SIGN":GOTO30
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 INPUTT$:FL=0:IFT$="GET"ORT$="READ"ORT$="OPEN"ORT$="DROP"ORT$="POUR"ORT$="DRINK"ORT$="SMASH"THENGOSUB3010:FL=0
4 IFT$="PAPER"ORT$="KEY"ORT$="KNIFE"ORT$="ROPE"ORT$="ARMOR"ORT$="CABINET"ORT$="SCROLL"ORT$="SWORD"ORT$="SIGN"THENGOSUB3110:FL=0
5 IF(T$="PLUGH"ORT$="SAY PLUGH")ANDPL=1THENM$="SORRY, ONLY ONE PLUGH PER CUSTOMER.":GOSUB1:FL=1
6 IF(T$="READ PAPER"ORT$="LOOK PAPER")ANDPA=1THENM$="IT SAYS, "+CHR$(34)+"MAGIC WORD-- PLUGH."+CHR$(34):GOSUB1:FL=1
7 IFT$="READ PAPER"ANDPA=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
10 IFT$="READ SCROLL"ANDSC=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
11 IFT$="READ SCROLL"ANDSC=1THENM$="IT SAYS, "+CHR$(34)+"THERE IS ESCAPE FROM THE SECOND FLOOR!"+CHR$(34):GOSUB1:FL=1
12 IFLEFT$(T$,3)="INV"ORT$="I"THENGOSUB2910:FL=1
13 IFT$="QUIT"THENFL=1:INPUT"ARE YOU SURE";T$:IFLEFT$(T$,1)="Y"THENEND
14 IFT$="LOOK"THENFL=2
15 IFT$="DROP PAPER"THENPA=0:M$="OK. A DRAFT BLOWS IT INTO A MOUSE HOLE.":GOSUB1:FL=1
16 IFBU<>1THEN19
17 IFLEFT$(T$,4)<>"POUR"ANDLEFT$(T$,4)<>"DUMP"ANDLEFT$(T$,5)<>"EMPTY"THEN19
18 M$="THE GROUND IS WET. THE BUCKET MAGICALLY REFILLS.":GOSUB1:FL=1
19 IFT$="DRINK WATER"ANDBU=0THENM$="YOU AREN'T CARRYING IT.":GOSUB1:FL=1
20 IFT$="DRINK WATER"ANDBU=1THENM$="YOU FEEL SICK. IN FACT, YOU JUST DIED! IT WAS POSION.":GOSUB1:END
23 IF(T$="BREAK SWORD"ORT$="TOUCH BLADE")ANDSW=1THENM$="OUCH! YOU HURT YOUR HAND.":GOSUB1:FL=1
24 IFT$="DROP SWORD"ANDSW=1THENSW=0:PRINT"OK.":FL=1:T$=""
25 IFT$="DROP SWORD"ANDSW=0THENM$="YOU'RE NOT CARRYING IT.":GOSUB1:FL=1
26 IFT$="READ SWORD"ANDSW=1THENM$="AN INSCRIPTION READS, "+CHR$(34)+"GHOST KILLER."+CHR$(34):GOSUB1:FL=1
27 IFT$="DROP KEY"ANDKY=1THENKY=0:PRINT"OK. IT DISAPPEARS!":FL=1
28 IFT$="DROP KNIFE"ANDKN=1THENPRINT"OK. IT FLOATS OUT OF THE ROOM.":KN=0:FL=1
29 RETURN

30 PRINT"ÄÄÄÄÄÄÄÄÄÄÄÄÄ–—ÄÄÄÄÄÄÄÄÄhauntedÄ";
31 PRINT"ÄÄÄÄÄ—”””””—÷‹◊€””””””““ÄÄhouseÄ";
32 PRINT"ÄÄÄÄ—ﬂ“–ÄÄÄ“—ﬂﬂﬂ€Ä—––—◊ﬂ“ÄÄÄÄÄÄÄ";
33 PRINT"ÄÄÄ—ﬂﬂﬂ“–ÄÄÄ‹ﬂﬂﬂﬁ‹ÄÄ”ﬂﬂﬂﬂ“ÄÄÄÄÄÄ";
34 PRINT"ÄÄ—ﬂﬂﬂﬂﬂ“–ÄÄ–ﬂ’Ä⁄ÄÄÄ—ﬂ’ü⁄ÄÄÄÄÄÄÄ";
35 PRINT"—◊ﬂﬂﬂﬂﬂﬂﬂ—–Ä‘ﬂ–⁄⁄–––’ﬂﬁ”⁄ÄÄÄÄÄÄÄ";
36 PRINT"ÄÄﬂﬂ◊Äﬂﬂ€ÿ”””◊ﬂﬂ⁄ﬁﬁﬁﬂﬂﬂﬂﬂ“––ÄÄÄÄ";
37 PRINT"ÄÄﬂﬂ’—ﬂﬂﬂ◊ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“ÄÄÄÄÄ";
38 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“ÄÄÄÄ";
39 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ”–ÄÄ";
40 PRINT"ÄÄﬂﬂﬂﬂﬂﬂ‹‹ﬂ‹›ﬂ‹‹›ﬂﬂ‹‹‹›ﬂ‹›‹›‹‹ÄÄ";
41 PRINT"ÄÄﬂﬂﬂﬂﬂﬂÄÄ⁄Ä’ﬂ––’ﬂ⁄“›‹›ﬂÄ’–’ÄÄÄÄ";
42 PRINT"ÄÄﬂﬂÄŸ◊ﬂÄÄ⁄Ä’ﬂ––‘ﬂﬁÄ’Ä’ﬂÄ’–’ÄÄÄÄ";
43 PRINT"ÄÄﬂﬂÄ⁄’ﬂﬂﬂﬂﬂﬂﬂ–Äêﬂﬂﬂﬂﬂﬂﬂﬂﬂ”◊“ÄÄÄ";
44 PRINT"ÄÄﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ”””◊”ÄÄ";
45 PRINT"ÄÄ‹‹‹Ä‘ÄÄÄÄÄÄ‘–ÄÄ‘‹‹‹‹‹‹‹‹–––‘ÿ";
50 PA=0:KY=0:KN=0:RP=0:AR=0:CA=0:SC=0:SW=0:SI=0:BU=0:PL=0
60 G1=1:G2=1:G3=1:G4=1:G5=0:G6=0:G7=0:G8=0:G9=1:G0=1:SW=0:SI=0
70 GOSUB130:GOTO100

80 IFT$="DROP ROPE"ANDRP=1THENRP=0:PRINT"IT SLITHERS AWAY!":RETURN
90 M$="I DON'T UNDERSTAND.":GOSUB1:RETURN

100 PRINTTAB(9)"HAUNTED HOUSE":M$="HAUNTED HOUSE IS A VERY SIMPLE GAME. THERE ARE NO TREASURES TO FIND. "
110 M$=M$+"THERE ARE NO SCORES TO KEEP. THERE IS NO TIME LIMIT.":GOSUB1
120 M$="YOU ONLY HAVE ONE TASK-- GET OUT OF THE HOUSE ALIVE!!":GOSUB1:GOTO180

130 REM PRESS A KEY
140 A$=INKEY$:IFA$=""THEN140
141 CLS:RETURN

180 PRINT
190 M$="GENERATIONS HAVE PASSED SINCE THE MCDANIEL FAMILY MYSTERIOUSLY DISAPPEARED.":GOSUB1
200 M$="IT IS SAID THAT A STRANGER CAME TO VISIT ON THAT COLD, "
210 M$=M$+"AUTUMN DAY MANY YEARS AGO, BUT NO ONE KNOWS FOR SURE.":GOSUB1
220 GOSUB140
230 M$="THEIR HOUSE HAS BEEN VACANT FOR DECADES NOW.":GOSUB1:M$="ITS TWO STORY IMAGE IS "
240 M$=M$+"FORLORN AND LOOMING, VISIBLE ONLY FROM THE NARROW, WINDING ROAD THAT "
250 M$=M$+"IS DISTORTED BY VEGETATION FROM THE SURROUNDING FOREST.":GOSUB1
260 M$="THE STONE WALL THAT ENCOMPASSES THE HOUSE IS DISCOLORED AND BROKEN FROM "
270 M$=M$+"YEARS OF NEGLECT, ITS IRON GATE RUSTY AND WORN BY ANGRY SEASONS.":GOSUB1:GOSUB140
280 M$="THE WINDOWS ARE BOARDED-- THE HOUSE IS QUIET AND CONTENTED, NOT "
290 M$=M$+"ACCUSTOMED TO VISITORS.":GOSUB1:M$="THE WIND IS RESTLESS TODAY, BLOWING FALLEN "
300 M$=M$+"LEAVES IN ALL DIRECTIONS.":GOSUB1:M$="AS YOU WALK TOWARDS THE ENTRANCE OF THE HOUSE, "
310 M$=M$+"THE WIND GROWS DISTANT AND WEAK.":GOSUB1:M$="SUDDENLY, THE CALM AND SILENCE IS "
320 M$=M$+"BROKEN BY SOUNDS FROM WITHIN THE HOUSE!":GOSUB1
330 PRINT:M$="DO YOU HAVE THE COURAGE TO ENTER?":GOSUB1

350 REM GETYINPUT:
360 A$=INKEY$:IFA$<>"Y"THEN360

370 CLS
380 PRINT"HAUNTED HOUSE!!"

400 REM OUTSIDE OF HOUSE
410 M$="YOU ARE AT THE OUTSIDE OF THE HOUSE. THERE IS A CRUMPLED PIECE OF PAPER ON THE GROUND.":GOSUB1
411 PRINT"THE FRONT DOOR IS CLOSED."
420 GOSUB3:ONFLGOTO420,410
430 IFT$="PLUGH"ORT$="SAY PLUGH"THENPL=1:M$="YOU MATERIALIZE INSIDE THE DOOR.":GOSUB1:GOTO510
460 IFT$="GET PAPER"THENPA=1:PRINT"OK":GOTO420
481 IFT$="OPEN DOOR"THENM$="THE DOOR CAN'T BE OPENED.":GOSUB1:GOTO420
483 IFT$="DOOR"ORT$="PAPER"ORT$="EXAMINE PAPER"THENM$="WHAT SHOULD I DO WITH IT?":GOSUB1:GOTO420
486 IFT$="N"ORT$="S"ORT$="E"ORT$="W"THEN410
492 GOSUB80:GOTO420

500 REM FOYER
510 M$="YOU ARE AT THE FOYER.":GOSUB1
520 GOSUB3:ONFLGOTO520,510
534 IFT$="E"THEN610
535 IFT$="S"THEN1210
536 IFT$="W"THEN1310
537 IFT$="DOOR"ORT$="PAPER"THENM$="WHAT SHOULD I DO WITH IT?":GOSUB1:GOTO520
538 IFT$="N"THEN510
550 GOSUB80:GOTO520

600 REM LIVING ROOM
610 M$="YOU ARE AT THE LIVING ROOM.":GOSUB1
620 IFKN=0THENM$="A KNIFE IS LEVITATING IN THE MIDDLE OF THE ROOM.":GOSUB1
630 IFSC=0THENM$="THERE IS A MYSTERIOUS SCROLL ON THE GROUND.":GOSUB1
640 GOSUB3:ONFLGOTO640,610
653 IFT$="E"ANDKN=1THEN710
654 IFT$="W"ANDKN=1THEN510
655 IFT$="GET SCROLL"THENSC=1:PRINT"OK":GOTO640
658 IFT$="GET KNIFE"ANDPA=0THENKN=0:M$="THE KNIFE FLOATS OUT OF YOUR REACH.":GOSUB1:GOTO640
659 IFT$="GET KNIFE"ANDPA=1THENKN=1:PRINT"OK":GOTO640
660 IFKN<>0THEN670
661 IFT$<>"N"ANDT$<>"S"ANDT$<>"E"ANDT$<>"W"THEN670
662 M$="SUDDENLY THE KNIFE WHOOSHES DOWN AND SLITS YOUR THROAT! YOU ARE DEAD.":GOSUB1:END
670 IFT$="N"ORT$="S"THEN610
680 GOSUB80:GOTO640

700 REM DINING ROOM
710 M$="YOU ARE AT THE DINING ROOM.":GOSUB1
720 GOSUB3:ONFLGOTO720,710
733 IFT$="S"THEN810
734 IFT$="W"THEN610
735 IFT$="N"ORT$="E"THEN710
738 GOSUB80:GOTO720

800 REM KITCHEN
810 M$="YOU ARE AT THE KITCHEN.":GOSUB1
820 IFBU=0THENM$="A BUCKET OF WATER IS ON THE FLOOR.":GOSUB1
830 GOSUB3:ONFLGOTO830,810
843 IFT$="N"THEN710
844 IFT$="W"THEN1210
845 IFT$="S"ANDKN=1THENM$="A SUIT OF ARMOUR HERE FLEES WHEN IT SPOTS YOUR KNIFE.":GOSUB1:GOTO910
846 IFT$="S"ANDKN=0THENM$="YOU ARE IN THE BREAKFAST ROOM. AN ANIMATED ARMOUR SUIT THROWS YOU OUT!":GOSUB1:GOTO810
847 IFT$="GET BUCKET"THENBU=1:PRINT"OK":GOTO820
851 IFT$="E"THEN810
854 GOSUB80:GOTO820

900 REM BREAKFAST ROOM
910 M$="YOU ARE AT THE BREAKFAST ROOM.":GOSUB1
920 GOSUB3:ONFLGOTO920,910
930 IFT$="N"THEN810
931 IFT$="E"THEN1010
932 IFT$="S"ORT$="W"THEN910
935 GOSUB80:GOTO920

1000 REM SERVANTS QUARTERS1
1010 M$="YOU ARE AT THE SERVANTS QUARTERS. THERE IS A CABINET ON ONE WALL.":GOSUB1
1020 GOSUB3:ONFLGOTO1020,1010
1041 IFT$="N"THEN1110
1042 IFT$="W"THEN910
1043 IFT$="OPEN CABINET"THENM$="IT'S EMPTY.":GOSUB1:GOTO1020
1044 IFT$="GET CABINET"THENM$="DON'T BE RIDICULOUS!":GOSUB1:GOTO1020
1045 IFT$="S"ORT$="E"THEN1010
1048 GOSUB80:GOTO1020

1100 REM SERVANTS QUARTERS2
1110 M$="YOU ARE AT THE SERVANTS QUARTERS. THERE IS A CABINET ON ONE WALL.":GOSUB1
1120 GOSUB3:ONFLGOTO1120,1110
1141 IFT$="S"THEN1010
1142 IFT$="OPEN CABINET"THENPRINT"OK":M$="THERE IS A KEY IN IT.":GOSUB1:GOTO1120
1143 IFT$="GET CABINET"THENM$="DON'T BE RIDICULOUS!":GOSUB1:GOTO1120
1144 IFT$="GET KEY"THENKY=1:PRINT"OK":GOTO1120
1145 IFT$="N"ORT$="E"ORT$="W"THEN1110
1147 GOSUB80:GOTO1120

1200 REM DEN
1210 M$="YOU ARE AT THE DEN.":GOSUB1
1220 GOSUB3:ONFLGOTO1220,1210
1230 IFT$="SEARCH DESK"THENPRINT"YOU FIND NOTHING.":GOTO1220
1242 IFT$="N"THEN510
1243 IFT$="E"THEN810
1244 IFT$="S"ORT$="W"THEN1210
1247 GOSUB80:GOTO1220

1300 REM EAST END HALL
1310 M$="YOU ARE AT THE EAST END OF THE HALL.":GOSUB1
1320 GOSUB3:ONFLGOTO1320,1310
1340 IFT$="N"THEN1510
1341 IFT$="E"THEN510
1342 IFT$="W"THEN1410
1343 IFT$="S"THEN1310
1347 GOSUB80:GOTO1320

1400 REM WEST END HALL
1410 M$="YOU ARE AT THE WEST END OF THE HALL. A LOCKED DOOR BARS THE WAY SOUTH.":GOSUB1
1420 GOSUB3:ONFLGOTO1420,1410
1430 IFT$="N"THEN1710
1431 IFT$="E"THEN1310
1432 IF(T$="S"ORT$="OPEN DOOR")ANDKY=1THEN1810
1433 IF(T$="S"ORT$="OPEN DOOR")ANDKY=0THENM$="YOU'LL NEED A KEY TO GET THROUGH THAT DOOR.":GOSUB1:GOTO1420
1434 IFT$="W"THEN1410
1480 IFT$="KNOCK"ORT$="KNOCK DOOR"ORT$="HIT DOOR"ORT$="SMASH DOOR"THENPRINT"SOMETHING KNOCKS FROM THE OTHER SIDE":THEN1420
1490 GOSUB80:GOTO1420

1500 REM GREEN BEDROOM
1510 M$="YOU ARE AT THE GREEN BEDROOM. THERE'S A PANEL ON THE WEST WALL.":GOSUB1
1520 GOSUB3:ONFLGOTO1520,1510
1530 IFT$="SEARCH BED"ORT$="EXAMINE BED"THENPRINT"NOTHING BUT COBWEBS.":GOTO1520
1540 IFT$="S"THEN1310
1541 IFT$="PANEL"ORT$="OPEN PANEL"ORT$="GO PANEL"ORT$="MOVE PANEL"THEN1610
1542 IFT$="N"ORT$="E"ORT$="W"THEN1510
1545 GOSUB80:GOTO1520

1600 REM SECRET PASSAGE
1610 M$="YOU ARE AT THE SECRET PASSAGE.":GOSUB1
1620 IFRP=0THENM$="A ROPE IS NEARBY.":GOSUB1
1630 GOSUB3:ONFLGOTO1630,1610
1640 IFT$="E"THEN1510
1641 IFT$="W"THEN1710
1642 IFT$="GET ROPE"THENRP=1:PRINT"OK":GOTO1630
1643 IFT$="N"ORT$="S"THEN1610
1646 GOSUB80:GOTO1630

1700 REM BLUE BEDROOM
1710 M$="YOU ARE AT THE BLUE BEDROOM. THERE'S A PANEL ON THE EAST WALL.":GOSUB1
1720 GOSUB3:ONFLGOTO1720,1710
1725 IFT$="SEARCH BED"ORT$="EXAMINE BED"THENPRINT"NOTHING BUT BEDBUGS.":GOTO1720
1730 IFT$="S"THEN1410
1731 IFT$="PANEL"ORT$="OPEN PANEL"ORT$="GO PANEL"ORT$="MOVE PANEL"THEN1610
1732 IFT$="N"ORT$="E"ORT$="W"THEN1710
1736 GOSUB80:GOTO1720

1800 REM MASTER BEDROOM
1810 M$="YOU ARE IN THE MASTER BEDROOM. A WALL OF RAGING FIRE BLOCKS THE WAY EAST.":GOSUB1
1820 GOSUB3:ONFLGOTO1820,1810
1830 IFT$="N"THEN1510
1831 IFT$="E"THENM$="ARE YOU JUST GOING TO WALK RIGHT THROUGH THAT RANGING FIRE?":GOSUB1:GOTO1820
1832 IFT$="YES"THENPRINT"OK":GOTO1910
1833 IFT$="NO"THENM$="A WISE DECISION.":GOSUB1:GOTO1820
1834 IFT$="W"ORT$="S"THEN1810
1840 IF(T$="DOUSE FIRE"ORT$="DOUSE FLAMES")ANDBU=1THENPRINT"IT DOESN'T GO OUT.":GOTO1820
1890 GOSUB80:GOTO1820

1900 REM LIBRARY
1910 M$="YOU ARE AT THE LIBRARY. THERE IS A HOLE IN THE CEILING.":GOSUB1
1920 GOSUB3:ONFLGOTO1920,1910
1930 IFT$="W"THEN1810
1931 IFT$="DROP ROPE"THEN1935
1932 GOTO1940
1935 RP=0:RC=1:PRINT"OK"
1936 M$="INSTANTLY THE ROPE UNWINDS AND LEVITATES TO THE HOLE IN THE CEILING!":GOSUB1:GOTO1920
1940 IF(T$="CLIMB ROPE"ORT$="JUMP ROPE")ANDRC=1THEN1945
1941 GOTO1950
1945 M$="YOU DROP EVERYTHING YOU HAD TO CLIMB THE ROPE TO THE SECOND FLOOR.":GOSUB1
1946 PA=0:KY=0:KN=0:RP=0:AR=0:CA=0:SC=0:GOTO2000
1950 IFT$="N"ORT$="E"ORT$="S"THEN1910
1960 IFT$="READ BOOK"ORT$="READ BOOKS"ORT$="EXAMINE BOOKS"THENPRINT"NONE ARE INTERESTING, EXCEPT ONEBY C. GERRIE.":GOTO1920
1990 GOSUB80:GOTO1920

1999 REM DIMLY ROOM SWORD 
2000 M$="YOU ARE IN A DIMLY LIT ROOM. THERE IS A HOLE IN THE FLOOR.":GOSUB1
2010 IFSW=0THENM$="THERE IS A MAGIC SWORD ON THE FLOOR.":GOSUB1
2020 GOSUB3:ONFLGOTO2020,2000
2030 IFT$="E"THEN2110
2040 IFT$="W"THEN2310
2041 IFT$="S"THEN2210
2042 IFT$="GET SWORD"THENSW=1:PRINT"OK":GOTO2020
2046 IFT$="CLIMB ROPE"ORT$="CLIMB DOWN"THENM$="YOU FALL THROUGH THE HOLE AND BREAK YOUR NECK! YOU ARE DEAD.":GOSUB1:END
2047 IFT$="N"THEN2000
2050 GOSUB80:GOTO2020

2060 M$="YOU CAN'T KILL A GHOST WITH YOUR BARE HANDS.":GOSUB1:RETURN
2070 M$="YOUR MAGIC SWORD ENABLES YOU TO KILL THE GHOST!":GOSUB1:RETURN
2080 M$="THE GHOST WILL NOT LET YOU PASS!":GOSUB1:RETURN
2090 M$="THE BODY OF A DEAD GHOST IS ON THE FLOOR.":GOSUB1:RETURN

2100 REM DIMLY ROOM GHOST1
2110 M$="YOU ARE IN A DIMLY LIT ROOM.":GOSUB1
2120 IFG1=1THENPRINT"THERE IS A GHOST HERE."
2130 IFG5=1THENGOSUB2090
2140 GOSUB3:ONFLGOTO2140,2110
2150 IFT$=K$ANDG1=1ANDSW=1THENG1=0:G5=1:GOSUB2070:GOTO2140
2151 IFT$=K$ANDG1=1ANDSW=0THENGOSUB2060:GOTO2140
2152 IFT$=K$ANDG5=1THENM$="THE POOR THING'S ALREADY DEAD.":GOSUB1:GOTO2140
2156 IFT$="W"THEN2000
2157 IF(T$="N"ORT$="E"ORT$="S")ANDG5=1THEN2110
2158 IF(T$="N"ORT$="E"ORT$="S")ANDG1=1THENGOSUB2080:GOTO2140
2162 GOSUB80:GOTO2140

2200 REM DIMLY ROOM GHOST2
2210 M$="YOU ARE IN A DIMLY LIT ROOM.":GOSUB1
2220 IFG2=1THENPRINT"THERE IS A GHOST HERE."
2230 IFG6=1THENGOSUB2090
2240 GOSUB3:ONFLGOTO2240,2210
2250 IFT$=K$ANDG2=1ANDSW=1THENG2=0:G6=1:GOSUB2070:GOTO2240
2251 IFT$=K$ANDG2=1ANDSW=0THENGOSUB2060:GOTO2240
2252 IFT$=K$ANDG6=1THENM$="THE POOR THING'S ALREADY DEAD.":GOSUB1:GOTO2240
2256 IFT$="N"THEN2000
2257 IF(T$="W"ORT$="E"ORT$="S")ANDG6=1THEN2210
2258 IF(T$="W"ORT$="E"ORT$="S")ANDG2=1THENGOSUB2080:GOTO2240
2262 GOSUB80:GOTO2240

2300 REM DIMLY ROOM GHOST3
2310 M$="YOU ARE IN A DIMLY LIT ROOM.":GOSUB1
2320 IFG3=1THENPRINT"THERE IS A GHOST HERE."
2330 IFG7=1THENGOSUB2090
2340 GOSUB3:ONFLGOTO2340,2310
2350 IFT$=K$ANDG3=1ANDSW=1THENG3=0:G7=1:GOSUB2070:GOTO2340
2351 IFT$=K$ANDG3=1ANDSW=0THENGOSUB2060: GOTO INPUT19
2352 IFT$=K$ANDG7=1THENM$="THE POOR THING'S ALREADY DEAD.":GOSUB1:GOTO2340
2356 IFT$="E"THEN2000
2357 IFT$="W"ANDG7=1THEN2410
2358 IF(T$="N"ORT$="W"ORT$="S")ANDG7=1THEN2310
2359 IF(T$="N"ORT$="W"ORT$="S")ANDG3=1THENGOSUB2080:GOTO2340
2363 GOSUB80:GOTO2340

2400 REM DIMLY ROOM UNKILL1
2410 M$="YOU ARE IN A DIMLY LIT ROOM. THERE IS A GHOST HERE.":GOSUB1
2420 GOSUB3:ONFLGOTO2420,2410
2430 IFT$=K$ANDSW=1THENG4=0:G8=1:M$="THE GHOST IS IMMUNE TO YOUR ATTACK!":GOSUB1:GOTO2420
2440 IFT$=K$ANDSW=0THENGOSUB2060:GOTO2420
2444 IFT$="E"THEN2310
2445 IFT$="N"ANDSW=0ANDSO=1THEN2510
2446 IFT$="S"ANDSW=0THENSO=1:GOTO2410
2447 IF(T$="N"ORT$="S"ORT$="W")ANDSW=1THENGOSUB2080:GOTO2420
2448 IFT$="N"ANDSW=0ANDSO=0THEN2410
2451 GOSUB80:GOTO2420

2500 REM DIMLY ROOM UNKILL2
2510 M$="YOU ARE IN A DIMLY LIT ROOM. THERE IS A GHOST HERE.":GOSUB1
2520 GOSUB3:ONFLGOTO2520,2510
2532 IFT$=K$ANDSW=0THENGOSUB2060:GOTO2520
2533 IFT$="E"THEN2310
2534 IFT$="W"THEN2610
2535 IFT$="S"THEN2410
2536 IFT$="N"THEN2510
2539 GOSUB80:GOTO2520

2600 REM DIMLY ROOM UNKILL3
2610 M$="YOU ARE IN A DIMLY LIT ROOM. THERE IS A GHOST HERE.":GOSUB1
2620 GOSUB3:ONFLGOTO2620,2610
2632 IFT$=K$ANDSW=0THENGOSUB2060:GOTO2620
2633 IFT$="E"THEN2510
2634 IFT$="S"THEN2710
2635 IFT$="N"ORT$="W"THEN2610
2638 GOSUB80:GOTO2620

2700 REM DIMLY ROOM ENDGAME
2710 M$="YOU ARE IN A DIMLY LIT ROOM.":GOSUB1
2715 IFSI=0THENM$="THERE IS A RUSTY OLD SIGN LAYING ON THE GROUND.":GOSUB1
2720 GOSUB3:ONFLGOTO2720,2710
2730 IFT$="GET SIGN"THENSI=1:PRINT"OK":GOTO2720
2731 IFT$=R$ANDSI=0THENM$="YOU ARE NOT CARRYING IT.":GOTO2720
2740 IFT$=R$ANDSI=1THEN2742
2741 GOTO2745 
2742 RS=1:M$="THE SIGN SAYS, "+CHR$(34)+"THERE ARE THREE EXITS FROM THIS ROOM.":GOSUB1
2743 M$="ONLY ONE IS TRUE... YOU MUST KNOW, BUT NOT BE BURDENED BY THIS CLUE!"+CHR$(34):GOSUB1:GOTO2720
2745 IFT$="DROP SIGN"THENSI=0:PRINT"OK":GOTO2720
2746 IFT$="N"THEN2610
2747 IFT$="E"ORT$="W"ORT$="S"THEN2750
2749 GOTO2760
2750 IFSI=1ORRS=0THEN2755
2751 M$="YOU WALK THROUGH A DOOR AND FIND YOURSELF ON A BALCONY.":GOSUB1
2752 M$="YOU CLIMB DOWN A TREE AND ESCAPE TO SAFETY! CONGRATULATIONS! YOU MADE IT!":GOSUB1:END
2755 M$="YOU FALL THROUGH A TRAP DOOR TO YOUR DEATH!":GOSUB1:END
2760 GOSUB80:GOTO2720

2900 REM INVENTORY
2910 I$="":IFPA=1THENI$=I$+"CRUMPLED PAPER, "
2911 IFKY=1THENI$=I$+"KEY, "
2912 IFKN=1THENI$=I$+"KNIFE, "
2913 IFRP=1THENI$=I$+"ROPE, "
2914 IFBU=1THENI$=I$+"WATER BUCKET, "
2915 IFSC=1THENI$=I$+"SCROLL, "
2916 IFSW=1THENI$=I$+"MAGIC SWORD, "
2917 IFSI=1THENI$=I$+"RUSTY SIGN, "
2920 IFI$=""THENI$="NOTHING, "
2930 M$="YOU ARE CARRYING: "+LEFT$(I$,LEN(I$)-2)+".":GOSUB1
2950 RETURN

3000 REM GET NOUN
3010 V$=T$:INPUT"WHAT";N$:T$=V$+" "+N$:RETURN

3100 REM GET VERB
3110 N$=T$:INPUT"WHAT DO YOU WANT ME TO DO WITH  IT";V$:T$=V$+" "+N$:RETURN

4000 REM CONVERSION TO BASIC
4010 REM DONALD FOSTER
4020 REM MC-10 EDITS JIM GERRIE
