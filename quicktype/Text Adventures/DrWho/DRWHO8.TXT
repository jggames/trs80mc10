0 CLEAR894:DIMCC,C1,M$,SO$(17),D(86,6),SI$(6),V(2),SW$(5),L(6),PI(7):O=70:F$=" "+CHR$(8):FORCC=1TO5:F$=F$+CHR$(0):NEXT:GOTO660
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
20 M$=U$+"DEEP PIT.":GOSUB1:GOSUB580:RETURN
30 M$=U$+"SMALL CHAMBER.":GOSUB1:RETURN
40 M$=Y$+"FOLLOWING A TRAIL AROUND A STEEP CLIFF.":GOSUB1:RETURN
50 M$="THE PATH FORKS HERE.":GOSUB1:RETURN
60 M$=O$+"TOP":GOTO590
70 M$=O$+"BASE":GOTO590
80 M$=Y$+"LOST IN THE MAZE.":GOSUB1:RETURN
90 M$="THE PATH WIDENS FOR A BIT.":GOSUB1:RETURN
100 M$=U$+"SECRET ROOM.":GOSUB1:RETURN
110 M$=U$+"PASSAGE NEAR SOME STAIRS.":GOSUB1:RETURN
120 M$=O$+"JUNCTION OF FOUR UNDERGROUND PASSAGES.":GOSUB1:RETURN
130 M$=U$+"BIG VALLEY. TO THE SOUTH STANDS A VAST BLACK MONOLITH. "+T$+"A THICK HAZE TO THE NORTH.":GOSUB1:RETURN
140 GOSUB570:M$="A MESSAGE SCRAWLED ON IT SAYS: 'DIG HERE'":GOSUB1:RETURN
150 M$=U$+"SMALL CAVE OF BROKEN ROCKS.":GOSUB1:RETURN
160 M$=O$+"BASE OF THE MONOLITH. "+T$+"A SMALL GRATE AT YOUR FEET.":GOSUB1:RETURN
170 GOSUB120:M$="UNDER A GRATE.":GOSUB1:RETURN
180 M$=R$+"DALEKS. PASSAGES LEAD OFF IN ALL DIRECTIONS. I WOULD NOT TRY TO GET PAST THE DALEKS, HOWEVER.":GOSUB1:RETURN
190 M$=O$+"TOP OF A HILL LOOKING DOWN UPON A DOMED CITY.":GOSUB1:RETURN
200 M$=Y$+"IN THE CITY'S DRAINS. TUNNELS LEAD OFF IN ALL DIRECTIONS..":GOSUB1:GOSUB580:RETURN
210 M$=O$+"GATES OF THE CITY. AT YOUR FEET IS A MANHOLE COVER.":GOSUB1:RETURN
220 M$=Y$+"IN THE DALEK MASTER STRATEGY ROOM. RED LIGHTS ARE FLASHING AND SIRENS SCREAMING. I WOULD LEAVE.":GOSUB1:RETURN
230 M$=Y$+"IN AN ALCOVE THAT HAD BEEN HIDDEN BEHIND A DEACTIVATED DALEK.":GOSUB1:RETURN
240 M$=R$+"BROKEN MACHINERY.":GOSUB1:RETURN
250 M$=R$+"BUBBLING BEAKERS, BURNING BUNSENS, FILTER FUNNELS, COLOURFUL CHEMICALS, ETC.":GOSUB1:RETURN
260 M$=Y$+"INSIDE THE DALEK EXPERIMENTATION ROOM. I WOULDN'T LINGER, AS THE EXPERIMENTS LOOK HUNGRY.":GOSUB1:GOSUB580:RETURN
270 M$=O$+"BOTTOM OF A DEEP DUST-FILLED HOLE.":GOSUB1:RETURN
280 M$=Y$+"JUST INSIDE A VAST CRATER. "+T$+"A SHIP TO YOUR NORTH, A DEEP HOLE TO YOUR SOUTH, AND A SET OF ROUGH STONE STEPS"
281 M$=M$+" LEADING UP THE CRATER WALL NEAR A CAVE ENTRANCE.":GOSUB1:RETURN
290 M$=Y$+"INSIDE THE SHIP NEAR THE AIRLOCK.":GOSUB1:RETURN
300 M$=H$+"JUST ENTERED A ROOM FULL OF CYBERMEN. BACK OUT VERY SLOWLY AND THEY MIGHT NOT SEE YOU. BE CAREFULL!":GOSUB1:RETURN
310 M$=O$+"BASE OF THE CYBERSHIP.":GOSUB1:RETURN
320 M$=U$+"CAVE IN THE CLIFF FACE.":GOSUB1:RETURN
330 M$=Y$+"BENEATH THE CYBERSHIP.":GOSUB1:RETURN
340 M$=O$+"TOP OF A CLIFF OVERLOOKING A SPACESHIP.":GOSUB1:RETURN
350 M$=R$+"HEAVY MACHINERY.":GOSUB1:RETURN
360 M$=R$+"TELEVISION SCREENS SHOWING VIEWS OF DIFFERENT AREAS. FOR EXAMPLE, ONE SHOWS A WHITE CRYSTAL LYING BESIDE A"
361 M$=M$+" MOUNTAIN PATH.":GOSUB1:RETURN
370 M$=Y$+"LOST IN THE SLIME. "+T$+"A TREE IN THE DISTANCE.":GOSUB1:RETURN
380 M$=Y$+"STANDING AT THE HIGH POINT ON A VAST PLAIN OF SLIME. "+T$+"A LONE TREE TO YOUR NORTH.":GOSUB1:RETURN
390 M$=O$+"TOP OF A TALL TREE. THE TARDIS CAN BE SEEN TO THE SOUTH.":GOSUB1:RETURN
400 M$=Y$+"AT A CLEARING IN THE SLIME. "+T$+"A TALL TREE HERE.":GOSUB1:RETURN
410 M$=Y$+"FOLLOWING A NARROW PATH ALONG THE EDGE OF THE CLIFF. ":GOSUB1:RETURN
420 M$=Y$+"IN THE MIDST OF A THICK FOG.":GOSUB1:RETURN
430 M$=U$+"VALLEY BETWEEN TWO MOUNTAINS. A THICK FOG IS ROLLING IN FROM THE NORTH.":GOSUB1:RETURN
440 GOSUB610:M$=M$+" UPON A VAST EXPANSE OF OCEAN.":GOSUB1:RETURN
450 GOSUB610:M$=M$+" INTO A BOTTOMLESS CHASM FILLED WITH WRITHING YELLOW FOG.":GOSUB1:RETURN
460 M$=Y$+"ON THE BRIM OF A DEEP VOLCANO.":GOSUB1:RETURN
470 M$=O$+"SUMMIT OF A VERY TALL MOUNTAIN. "+T$+"A THICK FOG HERE.":GOSUB1:RETURN
480 GOSUB620:M$=M$+" 4' CHIMNEY FROM WHICH IS POURING A VERY DENSE THICK YELLOW SMOG.":GOSUB1:RETURN
490 GOSUB620:M$=M$+" SMALL HOLE INTO WHICH THE FOG IS FLOWING.":GOSUB1:RETURN
500 GOSUB620:M$=M$+" FAST FLOWING RIVER. THE WATER IS A DULL YELLOW SHADE.":GOSUB1:RETURN
510 M$=O$+"INTERSECTION OF TWO PASSAGES. ":GOSUB1:RETURN
520 GOSUB510:GOSUB580:RETURN
530 M$=R$+"BROKEN DOWN TARDISES.":GOSUB1:RETURN
540 GOSUB510:M$="A SIGN NEARBY SAYS:":GOSUB1:GOSUB1640:RETURN
550 M$=U$+"MAGNIFICENT CHAMBER. "+T$+"A LARGE THRONE HERE.":GOSUB1:RETURN
560 GOSUB630:M$=M$+", WITH A HOLE IN IT.":GOSUB1:RETURN
570 GOSUB630:M$=M$+".":GOSUB1:RETURN
580 M$="THERE ARE SOME STAIRS HERE.":GOSUB1:RETURN
590 M$=M$+" OF A STEEP INCLINE.":GOSUB1:RETURN
600 PRINT"OKAY":GOTO780
610 M$=O$+"TOP OF A MOUNTAIN LOOKING DOWN":RETURN
620 M$=U$+"VALLEY BESIDE A":RETURN
630 M$=O$+"BASE OF AN UNSCALABLE WALL OF ROCK":RETURN
660 DV=-1:Y$="YOU ARE ":H$="YOU HAVE ":U$="YOU ARE IN A ":O$="YOU ARE AT THE ":T$="THERE IS "
661 R$="YOU ARE IN A ROOM FULL OF ":CLS:PRINT@203,"DOCTOR WHO"
670 DATAAGGADOR,A PICK,A DESIONATING RENTICULATOR,A SONIC SCREWDRIVER,A GIANT BLUE SPIDER!,A BAG OF JELLY BABIES
671 DATAA DALEK RAY-GUN,A COPY OF 'PLAYDALEK',A LARGE ROCK,A LONG SCARF,A STRANGE LOOKING OBJECT,A BUNCH OF BANANAS
680 DATAA DEAD SPIDER,A WHITE CRYSTAL,A BLUE CRYSTAL,A SIONATED CUMQUAT,A SKULL
690 DATAGROUP OF PELADONIANS,TRIO OF UGLY MUTANTS,GROUP OF DALEKS,CYBERMAN,SINGLE LOATHESOME HORRIBLE CREATURE,TROGLODYTE
691 DATATIMELORD,49,56,57,58,27,59,60,2,3,4,6,5,99,1
700 SF$="1211132112131223122212221222122211221124112112211111112111311221111135111113133312111311121312231111131111131113"
710 SC$="AGGBABBAGBANBLUBUNCLOCOMCOPCRECRYCUMCYBD  DALDESDIEDIGDOWDROE  EASEATENTEXIFIRGALGETGIAGIVGRAGUNHIDIN INVJELLARLISLON"
711 SC$=SC$+"LOOMAGMOOMUTN  NOROBJOFFOPEOUTPELPICPLAQUIRAYREARENRESROCS  SCASCRSEASIOSKASKUSONSOUSPISTRTALTARTIMTROU"
712 SC$=SC$+"  UP W  WESWHI"
720 PRINT,,TAB(10)"LOAD 'DRWHO2'":CLOAD*D,"DRWHO2"
750 RESTORE:FORI=1TO17:READSO$(I):NEXT:FORI=0TO6:READSI$(I):NEXT:FORI=0TO6:READL(I):NEXT:FORI=1TO7:READPI(I):NEXT
755 S1$="":FORI=1TO78:READX:S1$=S1$+CHR$(X):NEXTI:S1$=LEFT$(S1$,60)+CHR$(34)+MID$(S1$,62,4)+CHR$(34)+RIGHT$(S1$,12)
760 L=RND(-(PEEK(9)*256+PEEK(10))):Q9=0:C=0:WT=0:TI=0:L=6:PO=1:P=61:TL=20+RND(20):CLS:D(14,0)=13:D(39,0)=34:GOTO1070
780 IFPO=99THENP=99:GOTO790
781 P=L*10+PO
790 IFP=6ORP=8ORP=25ORP=44ORP=99ORL=4ANDQ9THEN840
800 IFC>0THENC=C+RND(0)
810 IF(P=66ORP=7)ANDC=0ANDRND(0)>.4ORRND(0)>.9ANDC=0THENC=1
820 IFP=19ANDD(75,2)=5THEN825
821 GOTO830
825 IFC<4THEN840
826 M$="THE SPIDER HAS EATEN YOU.":GOSUB1:GOTO1420
830 IFC=1THENM$="OH OH, HERE COMES A "+SI$(L)+"!":GOSUB1:GOTO840
831 IFCTHENM$=T$+"A "+SI$(L)+" HERE!":GOSUB1
840 INPUTA$:IFA$=""THEN840
850 V(0)=100:V(1)=100:V(2)=100:FORI=1TO5:SW$(I)="":NEXTI
860 J=0:K=-1:FORI=1TOLEN(A$):X$=MID$(A$,I,1):IFX$=" "THEN865
861 IFNOTKTHENSW$(J)=SW$(J)+X$:GOTO869
862 IFJ<5THENK=0:J=J+1:SW$(J)=X$
863 GOTO869
865 IFKTHEN863
866 K=-1
869 NEXTI
870 FORI=1TOJ:SW$(I)=LEFT$(SW$(I)+"   ",3):NEXTI:K=0:FORI=1TOJ:A1=1:AN=78:FORI1=1TO78:AI=INT((A1+AN)/2)
871 X$=MID$(SC$,AI*3-2,3):IFSW$(I)>X$THENA1=AI+1:GOTO880
872 AN=AI-1
880 IFX$<>SW$(I)ANDA1<=ANTHENNEXTI1:GOTO890
881 IFX$=SW$(I)THEN885
882 GOTO886
885 V(K)=ASC(MID$(S1$,AI,1)):K=K+1:IFK>2THENGOSUB1840:GOTO890
886 NEXTI:GOSUB1840
890 IFWTTHENIFRND(0)>.998ANDPS<>99THEN895
891 GOTO900
895 M$=Y$+"HIT OVER THE BACK OF THE HEAD AND EVERYTHING GOES BLACK... "
896 M$=M$+"YOU AWAKE TO FIND THAT EVERYTHING YOU WERE HOLDING IS GONE.":GOSUB1
897 FORI=2TO16:IFD(O+I,1)=90THEND(O+I,1)=6:NEXTI:GOTO840
898 NEXTI:GOTO840
900 IFV(0)=22ANDV(2)=100ORV(1)=100AND(V(0)>17ANDV(0)<27ANDV(0)<>22)THENM$="PLEASE BE MORE SPECIFIC.":GOSUB1:GOTO840
910 IFV(0)=33ANDV(1)=46THEN915
911 GOTO920
915 IFD(73,1)=90ANDD(86,1)=90THEND(86,1)=91:M$=H$+"DESIONATED THE CUMQUAT INTO ":GOSUB1:GOTO1410
916 GOTO1500
920 ONV(0)GOTO1110,1110,1110,1110,1110,1110,1190,1170,1020,1200
921 ONV(0)-10GOTO1240,1440,840,1060,1370,1280,970,840,1310,1310,1270,1350,1340,840,930,950,1040:PRINT"WHAT?":GOTO840
930 IFV(1)<31ORV(1)>46THEN1450
931 X1=V(1)-30:IFD(O+X1,1)=90THEN1460
932 IFD(O+X1,1)<>PTHEN1480
933 IF(D(O+X1,4)AND8)=0THEN1450
934 IFD(O+X1,3)+WT>20THEN1490
940 D(O+X1,1)=90:WT=WT+D(O+X1,3):D(O+X1,4)=(D(O+X1,4)OR1)-1:GOTO600
950 IFV(1)<31ORV(1)>46THEN1450
951 X1=V(1)-30:IFD(O+X1,1)<>90THEN955
952 IFV(1)=31THEN1510
953 GOTO960
955 IFD(O+X1,1)<>PORD(O+X1,1)=91THEN1480
955 GOTO1500
960 WT=WT-D(O+X1,3):D(O+X1,1)=P:GOTO600
970 IFC=0THEN1520
971 IFP=7THEN1570
972 IFL=4ANDC<0THEN1620
973 IFP=66THENGOSUB1000:GOTO975
974 GOTO980
975 IFNO<6THEN1750
976 GOTO1430
980 ONL+1GOTO1580,1520,1590,1600,1610,990,1630
990 ONRND(3)GOTO1650,1660,1670
1000 QC=0:NO=0:FORI=1TO16:IF(D(O+I,4)AND2)ANDD(O+I,1)=90THENNO=NO+1:QC=QC+(D(O+I,4)AND240)/16
1010 NEXTI:RETURN
1020 M$=Y$+"PRESENTLY HOLDING:":GOSUB1:NO=0:FORI=1TO16:IFD(O+I,1)=90THENPRINTSO$(D(O+I,2)):NO=NO+1
1030 NEXTI:IFNO=0THENPRINT"NOTHING"
1031 GOTO780
1040 IFL=6ORC=0THENM$=T$+"NO NEED TO HIDE!":GOSUB1:GOTO780
1041 IFP=19ANDD(19,1)=15THEN1450
1042 IFRND(0)>.95THEN1045
1043 C=0:GOTO1050
1045 M$="THE CREATURE THAT YOU BUMPED AS YOU RUSHED PAST WAS FASTER THAN YOU. HE CAUGHT AND KILLED YOU.":GOSUB1:GOTO1420
1050 IFL=5THENPO=1:P=51:GOTO1060
1051 IFL=4THENPO=4:P=44:GOTO1060
1052 PO=6:P=L*10+PO
1060 IFPO=99THENM$=Y$+"INSIDE THE TARDIS.":GOSUB1:GOTO1090
1070 IFD(P,0)<20THENOND(P,0)+1GOSUB20,30,40,50,60,70,80,90,100,110,120,130,140,150,160,170,180,190,200,210:GOTO1080
1072 IFD(P,0)<40THENOND(P,0)+1-20GOSUB220,230,240,250,260,270,280,290,300,310,320,330,340,350,360,370,380,390,400,410:GOTO1080
1074 OND(P,0)+1-40GOSUB420,430,440,450,460,470,480,490,500,510,520,530,540,550,560,570,580,590,600
1080 IFPO=1THENM$="THE TARDIS IS SITTING OFF TO ONE SIDE.":GOSUB1
1090 GOSUB1820:NO=0:FORI=1TO16:IFP=D(O+I,1)AND(D(O+I,4)AND1)=0THEN1095
1091 GOTO1100
1095 NO=NO+1:IFNO=1THENPRINT"AROUND YOU YOU SEE:"
1096 M$=SO$(D(O+I,2)):GOSUB1
1100 NEXTI:GOTO780
1110 IFPO=99THENM$=Y$+"STILL IN THE TARDIS.":GOSUB1:GOTO780
1111 Y=D(P,V(0)):IFP=19ANDD(19,1)=15THEN1680
1112 IFY=15THEN1530
1113 IFY=13THENONLGOTO1700,1110,1110,1740:GOTO1120
1114 IFY=14THENONL-2GOTO1710,1720,1730
1120 IFL<>6ANDC>2.5ANDRND(0)>.5THEN1125
1121 GOTO1130
1125 IFL>2THENM$="HE HAS ":GOTO1690
1126 M$="THEY HAVE":GOTO1690
1130 IFP=36ANDRND(0)>.3THEN1135
1131 GOTO1140
1135 M$="THE SHIP JUST BLASTED OFF. AS YOU WERE UNDERNEATH IT AT THE TIME, "+Y$+"NOW SLIGHTLY INCINERATED.":GOSUB1:GOTO1420
1140 IFP=62ANDRND(0)>.3THENM$=H$+"BEEN HIT BY A LOW FLYING TARDIS.":GOSUB1:GOTO1420
1150 IFP=6ANDRND(0)<.9THENY=PO
1160 PO=Y:P=L*10+PO:C=-(P=20ORP=33ORP=66):GOTO1060
1170 F=0:IFPO<>99THENS$="ALREADY OUTSIDE":GOTO1180
1171 S$="NOW OUTSIDE":PO=1:F=-1:P=1+L*10
1180 M$=Y$+S$+" THE TARDIS.":GOSUB1:IFFTHEN1060
1181 GOTO780
1190 F=0:C=0:IFPO=99THENS$="ALREADY INSIDE":GOTO1180
1191 IFPO<>1THEN1550
1192 S$="NOW INSIDE":PO=99:P=PO:GOTO1180
1200 IFPO<>99THEN1540
1201 TI=TI+1:IFTI>TLTHENM$="YOUR TARDIS HAS RUN OUT OF POWER... "+Y$+"'LOST IN SPACE'!":GOSUB1:GOTO1420
1210 FORI=0TO6:IFV(1)=L(I)THEND=I:I=6:NEXTI:GOTO1220
1211 NEXTI:D=RND(7)-1
1220 IFRND(2)=1THEND=RND(10)-1:IFD>6THENM$=H$+"MATERIALISED IN SPACE. I WILL RESET THE TARDIS FOR YOU.":GOSUB1:D=RND(7)-1
1230 L=D:M$=H$+"MATERIALISED ON ":IFL=3THENM$=M$+"THE DARK SIDE OF THE MOON":GOSUB1:GOTO780
1231 M$=M$+MID$("PELADON     MUTOS"+F$+"SKARO"+F$+"            HIDAOUS     DIETHYLAMIDEGALAFRY",L*12+1,12):GOSUB1:GOTO780
1240 PRINTH$"FOUND:":F=0:IFP=6ANDRND(0)>.3ORL=4ANDPO<>6ANDPO<>4ANDPO<>1ANDRND(0)>.7THEN1260
1241 FORI=1TO16:IFD(O+I,1)=PTHENF=F+1:PRINTSO$(D(O+I,2)):D(O+I,4)=(D(O+I,4)OR5)-1
1250 NEXTI:IFP=26THENF=F+1:M$="A SECRET PASSAGE HIDDEN BEHIND A DEACTIVATED DALEK.":GOSUB1:D(26,3)=5
1260 IFFTHEN780
1261 PRINT"NOTHING":GOTO780
1270 IFV(1)=36ORV(1)=42ORV(1)=46THEN1275
1271 GOTO1450
1275 IFD(O+V(1)-30,1)=90THEND(O+V(1)-30,1)=91:PRINT"DELICIOUS!":GOTO780
1276 GOTO1500
1280 IFD(O+2,1)<>90THENM$=H$+"NOTHING TO DIG WITH.":GOSUB1:GOTO780
1281 IFP=12ANDD(12,4)=15THEND(12,4)=6:D(12,0)=54:GOTO1300
1282 IFP=13ANDD(13,3)=15THEND(13,3)=4:D(13,0)=54:GOTO1300
1290 M$="YOU HAVEN'T DUG UP ANYTHING USEFUL.":GOSUB1:GOTO780
1300 M$=H$+"BROKEN A HOLE IN THE WALL WITH YOUR PICK. YOU CAN SEE A CHAMBER BEHIND IT.":GOSUB1:GOTO780
1310 IFV(1)<>55ANDV(2)<>55THEN1450
1311 IFL<>1ORPO<5ORPO>6THEN1480
1312 IFD(74,1)<>90THEN1450
1313 IFV(0)=20THEN1330
1320 D(15,6)=6:D(16,5)=5:GOTO600
1330 D(15,6)=13:D(16,5)=13:GOTO600
1340 IFV(1)=38THEN1345
1341 IFV(1)=33THEN1347
1342 GOTO1450
1345 IFD(78,1)=90THENM$="THIS DOES NOTHING FOR ME, YOU KNOW.":GOSUB1:GOTO780
1346 GOTO1500
1347 IFD(73,1)=90THENGOSUB1000:PRINT"IT SAYS";ABS(NO+RND(3)-2+RND(0)):GOTO780
1348 GOTO1500
1350 IFV(1)=35ANDV(2)=45ANDD(85,1)=90ANDP=19ANDD(19,1)=15THENC=0:D(19,1)=7:D(75,1)=14:D(75,4)=D(75,4)OR8:D(75,2)=13:GOTO1760
1360 IFV(1)<31ORV(1)>46ORV(2)<49ORV(2)>56THEN1450
1361 Y=PI(V(2)-49):IFY=99ORY<>LORC=0ORD(O+V(1)-30,1)<>90THEN1450
1362 Y=VAL(MID$(SF$,(V(1)-30)+16*Y,1)):ONYGOTO1770,1780,1790,1800,1810
1370 IFV(1)<>37THEN1450
1371 IFD(77,1)<>90THEN1500
1372 IFC=0THENM$=T$+"NOTHING HERE TO FIRE AT.":GOSUB1:GOTO780
1373 C=0
1380 IFL<>6THEN1390
1385 M$=T$+"A LOUD EXPLOSION AND... HEY THAT WAS A TIMELORD! JUST WHOSE SIDE ARE YOU ON?":GOSUB1
1386 M$="THAT'S IT FOR YOU, BUSTER!":GOSUB1:GOTO1420
1390 IFP=19THEND(19,1)=7:D(75,1)=91
1400 M$+T$+"A LOUD EXPLOSION AND A BRILLIANT FLASH, AND YOU FIND YOURSELF LOOKING AT":IFL=4THENQ9=1
1410 M$=M$+" A SMALL PILE OF GREY POWDER WHICH SLOWLY DISSIPATES.":GOSUB1:GOTO780
1420 M$=H$+"FAILED... THE UNIVERSE WILL BE DESTROYED... AND ALL THE BLAME LIES ON YOUR SHOULDERS...":GOSUB1:GOTO1440
1430 M$=H$+"FOUND ALL SIX PARTS! WE'RE SAVED!!":GOSUB1
1440 GOSUB1000:QC=INT((QC*NO*100)*TL/(TI+1)):M$="YOU SCORED"+STR$(QC):GOSUB1
1441 INPUT"WANT TO TRY AGAIN";X$:IFLEFT$(X$,1)="N"THENEND
1442 IFLEFT$(X$,1)="Y"THEN720
1443 GOTO1441
1450 M$="I CAN'T QUITE SEE HOW TO DO THAT.":GOSUB1:GOTO780
1460 PRINT"YOU'RE ALREADY CARRYING IT.":GOTO780
1470 PRINT"YOU CAN'T DO THAT!":GOTO780
1480 PRINT"I CAN'T SEE THAT HERE.":GOTO780
1490 M$="YOU CAN'T CARRY ALL THIS AND THAT TOO. YOU'LL HAVE TO DROP SOMETHING FIRST.":GOSUB1:GOTO780
1500 PRINT"YOU AREN'T CARRYING IT.":GOTO780
1510 M$="HE DOESN'T SEEM TO WANT TO GO AWAY.":GOSUB1:GOTO780
1520 M$=T$+"NOBODY ELSE HERE WHO CAN TALK.":GOSUB1:GOTO780
1530 M$=T$+"NO WAY TO GO IN THAT DIRECTION.":GOSUB1:GOTO780
1540 M$=H$+"TO BE IN THE TARDIS TO USE IT.":GOSUB1:GOTO780
1550 M$=T$+"NO TARDIS HERE.":GOSUB1:GOTO780
1560 M$="YOU CAN'T GET THROUGH THE GRATE WITHOUT OPENING IT FIRST.":GOSUB1:GOTO780
1570 M$="THEY HAVE SPOKEN OF A SECRET DOORWAY BEHIND A NEARBY CURTAIN.":GOSUB1:D(7,3)=8:GOTO780
1580 M$="ONE OF THEM POKED YOU WITH A SPEAR. BUT YOU WERE NOT KILLED, JUST MORTALLY WOUNDED.":GOSUB1:GOTO780
1590 M$="'EXTERMINATE'... IT ZAPPED YOU. "+Y$+"NOW A SLOWLY EVAPORATING PILE OF GREY POWDER.":GOSUB1:GOTO1420
1600 M$="'HAVE YOU GOT A SPOT OF OIL ON YOU?'":GOSUB1:GOTO780
1610 M$="'CREATURE HUNGRY... CAN CREATURE EAT YOU?'":GOSUB1:GOTO780
1620 M$="'I SAY, THESE SWEETIES ARE SIMPLY SPIFFING. RATHER.":GOSUB1
1621 M$="NOT AT ALL LIKE THOSE BANANAS. CRUNCHY BANANAS ARE SIMPLY NOT ON, WHAT?'":C=.01:GOTO780
1630 GOSUB1640:GOTO780
1640 PRINT"'BEWARE OF LOW FLYING TARDISES'":RETURN
1650 PRINT"'EEEEEEEEE...' (UNINTELLIGABLE)":GOTO780
1660 M$="THE FOG MUFFLES YOUR VOICE SO THAT HE CAN'T HEAR YOU.":GOSUB1:GOTO780
1670 M$="HE HAS SPOKEN OF A STRANGE CRYSTAL ON A MOUNTAIN MANY MILES AWAY...":GOTO780
1680 M$="AS YOU APPROACHED THE SPIDER, IT TURNED RED AND STARTED SPITTING POISON. YIKES!":GOSUB1:GOTO780
1690 M$=M$+" DEVELOPED AN INTENSE DISLIKE OF YOU AND TORN YOU TO PIECES.":GOSUB1:GOTO1420
1700 M$=H$+"TO OPEN THE GRATE FIRST.":GOSUB1:GOTO780
1710 M$=H$+"BEEN STRANGLED BY A CYBERMAN.":GOSUB1:GOTO1420
1720 M$="YOU JUMPED FROM THE TREE AND BROKE YOUR NECK. SILLY YOU.":GOSUB1:GOTO1420
1730 M$=H$+"JUMPED TO YOUR DOOM.":GOSUB1:GOTO1420
1740 M$=H$+"TAKEN A MOUTH FULL OF SLIME. (UGGH!)":GOSUB1:GOTO780
1750 M$="'THESE ARE NOT THE RIGHT PARTS. TAKE THEM FROM MY SIGHT!'":GOSUB1:GOTO780
1760 M$="THE SPIDER LOOKED DEEP INTO THE CRYSTAL, TURNED GREEN AND ROSE SLOWLY UP THROUGH THE ROOF.":GOSUB1
1761 PRINT"I THINK IT WAS DEAD!":GOTO780
1770 M$="HE DOESN'T SEEM INTERESTED IN IT.":GOSUB1:GOTO780
1780 PRINT"HE TAKES IT AND SCARPERS.":C=0:D(O+V(1)-30,1)=L*10+RND(10)-1:GOTO780
1790 PRINT"HE EATS IT.":D(O+V(1)-30,1)=91:C=.01:GOTO780
1800 M$="THE DALEKS TAKE ONE LOOK AT YOUR COPY OF 'PLAYDALEK' AND FREAK OUT.":GOSUB1
1805 M$="THEY HEAD DOWN SEPARATE TUNNELS DOING CARTWHEELS AND BLASTING THE WALLS. THIS MUST BE HOT STUFF!":GOSUB1:C=0:GOTO780
1810 M$="THE CREATURE HAS CALMED DOWN A LITTLE AND LOOKS FRIENDLY (UGLY, BUT FRIENDLY)... EVEN TALKATIVE!":C=-1:GOTO780
1820 IFP=99THEN4
1821 M$="YOU CAN GO":FORI=1TO6:IFD(P,I)<>15THEN1825
1822 GOTO1830
1825 IFI<3THENM$=M$+MID$(" NORTH, SOUTH,",I*7-6,7):GOTO1830
1826 IFI=5THENM$=M$+" UP,":GOTO1830
1827 M$=M$+MID$(" EAST, WEST,     , DOWN,",I*6-17,6)
1830 NEXT:M$=M$+CHR$(8)+".":GOSUB1:RETURN
1840 IFV(0)>V(1)ORV(0)>V(2)THENT=-((V(1)<=V(2))*1+(V(1)>V(2))*2):T1=V(0):V(0)=V(T):V(T)=T1
1850 IFV(1)>V(2)THENT=V(2):V(2)=V(1):V(1)=T
1851 RETURN
2150 DATA31,36,36,42,45,42,20,17,38,52,45,46,51,6,50,33,59,16,6,26,3,3,21,7,8,15,60,25,35,22,55,37,27,7,9,36,39,9,40,14
2160 DATA38,58,56,1,1,41,22,19,8,49,32,38,12,37,23,33,10,39,2,40,99,11,46,57,43,99,2,35,41,17,48,53,54,5,5,4,4,44
