10 REM ADVENTURE
20 REM BY SCOTT LE BRUN
30 CLEAR100:O=6:V=15:CO=0:GOSUB60000
40 CLS:PRINT"ADVENTURE":GOSUB4000
100 PRINT"---------":GOSUB4000
110 PRINT"YOUR LOCATION"
120 PRINTD$(R)
130 PRINT"EXITS:";
140 FORI=1TOLEN(R$(R))
150 PRINTMID$(R$(R),I,1);",";
160 NEXTI
170 PRINT
180 FORI=1TOO
190 IFL(I)=RANDF(I)=0,PRINT"YOU CAN SEE :";O$(I);" HERE"
200 NEXTI
210 PRINT"================================";
220 PRINTM$:M$="WHAT?"
230 PRINT"WHAT WILL YOU DO NOW":INPUTQ$
240 V$="":W$="":VB=0:OB=0
250 FORI=1TOLEN(Q$):QW=0
260 IFMID$(Q$,I,1)=" "ANDV$="",V$=LEFT$(Q$,I-1)
270 IFMID$(Q$,I+1)<>" "ANDV$<>"",W$=MID$(Q$,I+1,LEN(Q$)-1):QW=1
275 IFQW=1,I=LEN(Q$)
280 NEXTI
290 IFW$="",V$=Q$
300 FORI=1TOV
310 IFV$=V$(I),VB=I
320 NEXTI
330 FORI=1TOO
340 IFW$=O$(I),OB=I
350 NEXTI
360 IFW$>""ANDOB=0,M$="THAT'S SILLY"
370 IFVB=0,VB=V+1
380 IFW$="",M$="I NEED TWO WORDS"
390 IFVB>VANDOB>0,M$="YOU CAN'T '"+Q$+"'"
400 IFVB>VANDOB=0,M$="YOU DON'T MAKE SENSE!"
410 IFVB<VANDOB>0ANDC(OB)=0,M$="YOU DON'T HAVE '"+Q$+"'"
500 IFVB=1ORVB=2ORVB=3ORVB=4ORVB=5ORVB=6,GOSUB1000
510 IFVB=7,GOSUB1300
515 IFVB=8,GOSUB1400
520 IFVB=9,GOSUB1500
525 IFVB=10,GOSUB1600
530 IFVB=11,GOSUB1700
535 IFVB=12,GOSUB1800
540 IFVB=13,GOSUB1900
545 IFVB=14,GOSUB2000
550 IFVB=15,GOSUB2100
600 GOTO40
1000 REM MOVE
1010 IFC(6)=0,M$="TOO DARK TO MOVE!":RETURN
1020 IFC(2)=0ANDC(3)=0ANDR=41ANDVB=3,M$=H$(3):RETURN
1100 F(35)=0:RL=LEN(R$(R))
1110 FORI=1TORL
1120 U$=MID$(R$(R),I,1)
1130 IFU$="N"ANDVB=1ANDF(35)=0,R=R-3:F(35)=1
1140 IFU$="S"ANDVB=2ANDF(35)=0,R=R+3:F(35)=1
1150 IFU$="W"ANDVB=3ANDF(35)=0,R=R-1:F(35)=1
1160 IFU$="E"ANDVB=4ANDF(35)=0,R=R+1:F(35)=1
1170 IFU$="U"ANDVB=5ANDF(35)=0ANDR=17,R=12:F(35)=1
1180 IFU$="U"ANDVB=5ANDF(35)=0ANDR=48,R=25:F(35)=1
1190 IFU$="D"ANDVB=6ANDF(35)=0ANDR=12,R=17:F(35)=1
1200 IFU$="D"ANDVB=6ANDF(35)=0ANDR=25,R=48:F(35)=1
1210 NEXTI
1220 M$="OK":IFF(35)=0,M$="YOU CAN'T GO THAT WAY!"
1230 RETURN
1300 REM EXAMINE
1310 M$="ITS JUST AN ORDINARY "+W$
1399 RETURN
1400 REM WAVE
1410 IFOB<>5,M$="WAVING A "+W$+" PROVES NOTHING":RETURN
1415 IFC(5)=0,RETURN
1420 IFR<>24ANDR<>36,M$="NOTHING HAPPENS!":RETURN
1430 M$="MAGIC OCCURS...YOU ARE MAGICALLYTRANSPORTED"
1440 IFR=24,R=36:RETURN
1450 IFR=36,R=24:RETURN
1500 REM GET
1510 M$="I CAN'T GET '"+W$+"'"
1520 IFL(OB)<>R,M$="IT ISN'T HERE":RETURN
1530 IFF(OB)=1,M$="WHAT "+W$+"?":RETURN
1540 IFC(OB)=1,M$="YOU ALREADY HAVE IT":RETURN
1545 RT=0
1550 IFOB>0ANDL(OB)=RANDF(OB)=0,RT=1
1560 IFRT=1,CO=CO+1:IFCO>3,CO=3:M$=H$(2):RETURN
1570 IFRT=1,C(OB)=1:L(OB)=49:M$=H$(1)+W$
1599 RETURN
1600 REM DROP
1605 IFC(OB)=0,M$="YOU DON'T HAVE THE "+W$:RETURN
1610 IFC(OB)=1,C(OB)=0:L(OB)=R:M$="YOU HAVE DROPPED THE "+W$
1620 CO=CO-1:IFCO<0,CO=0
1699 RETURN
1700 REM HELP
1710 M$="IF YOU REALLY NEED HELP THEN CONTACT ME ON (03)754-4460
1799 RETURN
1800 REM VOCABULARY
1810 CLS:PRINT"VOCABULARY":GOSUB4000
1820 PRINT"----------"
1830 FORI=0TOV
1840 PRINTV$(I);",";
1850 NEXTI
1860 M$="":PRINT
1870 GOSUB3000
1899 RETURN
1900 REM EAT
1910 IFC(4)=0ANDOB=4,M$="WHAT BUN?":RETURN
1920 IFOB<>4,M$="EAT WHAT?":RETURN
1930 M$="MUNCH.MUNCH..<BURP>..THE CREAM  BUN WAS DELICIOUS!"
1940 C(OB)=0:CO=CO-1
1999 RETURN
2000 REM INVENTORY
2010 CLS:PRINT"INVENTORY":GOSUB4000
2020 PRINT"---------"
2030 PRINT:PRINT:PRINT"YOU ARE CARRYING...":PRINT
2040 FORI=1TOO
2050 IFC(I)=1,PRINTO$(I);",";
2060 NEXTI:M$="":GOSUB3000
2099 RETURN
2100 CLS:PRINT"SCORE":GOSUB4000
2110 PRINT"-----"
2120 PRINT:PRINT:PRINT
2130 IFC(1)=1ANDC(2)=1ANDC(3)=1ANDR=1,S=60ELSE2200
2140 PRINT" "S:PRINT:PRINT"CONGRATULATIONS,YOU HAVE"
2150 PRINT"COMPLETED THE ADVENTURE!!":FORI=1TO100000:NEXT:CLS:END
2200 IFC(1)=1ORC(2)=1ORC(3)=1,S=20
2240 IFC(1)=1ANDC(2)=1,S=40
2250 IFC(1)=1ANDC(3)=1,S=40
2260 IFC(2)=1ANDC(3)=1,S=40
2270 PRINT:PRINT:PRINT" YOUR SCORE SO FAR IS :-"S:M$=""
2280 GOSUB3000:RETURN
3000 PRINT@480,"PRESS <RETURN> TO CONTINUE";
3010 R$=INKEY$:T$=INKEY$:IFT$<>CHR$(13),3010ELSERETURN
4000 PRINT@16,"BY SCOTT LE BRUN";:RETURN
60000 REM INITIALISATION
60010 DIM O$(O),V$(V),D$(48),R$(48),C(O),L(O),F(35)
60020 DATA"AT A PLATEAU NEAR A CLIFF,A ROCKY PATH LEADS SOUTH"
60030 DATA" "," ","ON A ROCKY PATH LEADING NORTH & CURVING EAST"
60040 DATA"ON A PATH THAT LEADS WEST AND WINDS EAST"
60050 DATA"AT THE ENTRANCE TO A DARK CAVE"
60060 DATA" "," "
60070 DATA"INSIDE A NORTH-SOUTH TUNNEL"," "," "
60080 DATA"IN AN OVAL CAVERN.THERE IS A STAIRCASE LEADING DOWN"
60090 DATA" ","IN A COLD,SMALL ROOM"
60100 DATA" ","IN ROOM WITH COBWEB COVERED WALLS"
60110 DATA"IN AN IMMENSE CAVERN WITH TALL OMINOUS WALLS"
60120 DATA"IN A TRIANGULAR SUB-CHAMBER"
60130 DATA" ","IN AN ENORMOUS CAVE WITH A STONE PILLAR"
60140 DATA" ","IN AN OCTAGONAL ROOM","STALAGTITE LITTERED ROOM"
60150 DATAIN A ROOM WHICH THE ONLY WAY OUT IS THE WAY YOU CAME IN
60160 DATA IN A ROUND ROOM WITH A SPIRAL STAIRCASE LEADING DOWN
60170 DATA"IN A LOW-HUGGING TUNNEL"," "," "
60180 DATA"IN A STRANGE TWISTED ROOM WITH WALLS OF ICE"
60185 DATA"IN A ROOM WITH STALAGMITES EMINATING EVERWHERE"
60190 DATA" "," ","IN A ROOM WITH INCHES OF UNDISTURBED DUST"," "
60200 DATA" ","IN A ROOM ONLY REACHED BY MAGICAL MEANS"," "," "
60210 DATA" ","YOU ARE IN THE THRONE ROOM OF THE EVIL OVERLORD
60220 DATA"IN COLD DANK TUNNEL WITH ROCKS STREWN EVERYWHERE"
60225 DATA A MAGICAL ROOM
60230 DATA" "," ","IN A VAST UNDERGROUND CAVERN"," "," "
60240 DATA"IN A ROOM WITH A SPIRAL STAIRCASE LEADING UP"
60250 FORI=1TO48:READD$(I):NEXT
62000 REM OBJECTS
62010 DATA CROWN,RING,GOBLET,BUN,ROD,LAMP
62020 FORI=1TOO:READO$(I):NEXT
62030 REM VERBS
62040 DATA N,S,W,E,U,D,EXAMINE,WAVE,GET,DROP,HELP
62050 DATA VOCABULARY,EAT,INVENTORY,SCORE
62060 FORI=1TOV:READV$(I):NEXT
62100 REM ROUTES
62110 DATA S," "," ",NE,WE,SW," "," ",NS," "," ",ND
62120 DATA " ",S," ",E,NSWEU,W," ",NS," ",E,NWE,W
62130 DATA DE,SW," "," ",NE,SW," "," ",NS," "," ",N
62140 DATA " "," "," ",E,WE,WS," "," ",NS," "," ",NU
62150 FORI=1TO48:READR$(I):NEXT
62160 R=1:M$="OK"
62170 DATA 40,16,22,12,14,1
62180 FORI=1TOO:READL(I):NEXT
62190 H$(1)="YOU HAVE THE "
62195 H$(2)="YOU WILL HAVE TO DROP SOMETHING"
62200 H$(3)="A MAGICAL BARRIER BLOCKS THE WAY!"
65529 RETURN
