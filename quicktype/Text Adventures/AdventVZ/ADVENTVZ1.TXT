0 CLS:CLEAR500:DIMCC,ZZ,Z$,I$:GOTO30
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
30 O=6:V=15:CO=0:GOSUB6000
40 CLS:PRINT"adventure":GOSUB4000
100 PRINT"---------":GOSUB4000
110 PRINT"YOUR LOCATION:"
120 Z$=D$(R):GOSUB1
130 I$="EXITS: "
140 FORI=1TOLEN(R$(R))
150 I$=I$+MID$(R$(R),I,1)+", "
160 NEXTI:Z$=LEFT$(I$,LEN(I$)-2):GOSUB1
180 PRINT"YOU CAN SEE: ":I$="":FORI=1TOO
190 IFL(I)=RANDF(I)=0THENI$=I$+O$(I)+", "
200 NEXTI:IFI$<>""THENZ$=LEFT$(I$,LEN(I$)-2):IFZ$<>""THENGOSUB1:GOTO210
205 PRINT"NOTHING OF INTEREST"
210 PRINT"================================";
220 Z$=M$:IFM$<>""THENGOSUB1
221 M$="WHAT?"
230 PRINT"WHAT WILL YOU DO NOW":INPUTQ$
240 V$="":W$="":VB=0:OB=0
250 FORI=1TOLEN(Q$):QW=0
260 IFMID$(Q$,I,1)=" "ANDV$=""THENV$=LEFT$(Q$,I-1)
270 IFMID$(Q$,I+1)<>" "ANDV$<>""THENW$=MID$(Q$,I+1,LEN(Q$)-1):QW=1
275 IFQW=1THENI=LEN(Q$)
280 NEXTI
290 IFW$=""THENV$=Q$
300 FORI=1TOV
310 IFV$=V$(I)THENVB=I
320 NEXTI
330 FORI=1TOO
340 IFW$=O$(I)THENOB=I
350 NEXTI
360 IFW$>""ANDOB=0THENM$="THAT'S SILLY"
370 IFVB=0THENVB=V+1
380 IFW$=""THENM$="I NEED TWO WORDS"
390 IFVB>VANDOB>0THENM$="YOU CAN'T '"+Q$+"'"
400 IFVB>VANDOB=0THENM$="YOU DON'T MAKE SENSE!"
410 IFVB<VANDOB>0ANDC(OB)=0THENM$="YOU DON'T HAVE '"+Q$+"'"
500 IFVB=1ORVB=2ORVB=3ORVB=4ORVB=5ORVB=6THENGOSUB1000
510 IFVB=7THENGOSUB1300
515 IFVB=8THENGOSUB1400
520 IFVB=9THENGOSUB1500
525 IFVB=10THENGOSUB1600
530 IFVB=11THENGOSUB1700
535 IFVB=12THENGOSUB1800
540 IFVB=13THENGOSUB1900
545 IFVB=14THENGOSUB2000
550 IFVB=15THENGOSUB2100
600 GOTO40
1000 REM MOVE
1010 IFC(6)=0THENM$="TOO DARK TO MOVE!":RETURN
1020 IFC(2)=0ANDC(3)=0ANDR=41ANDVB=3THENM$=H$(3):RETURN
1100 F(35)=0:RL=LEN(R$(R))
1110 FORI=1TORL
1120 U$=MID$(R$(R),I,1)
1130 IFU$="N"ANDVB=1ANDF(35)=0THENR=R-3:F(35)=1
1140 IFU$="S"ANDVB=2ANDF(35)=0THENR=R+3:F(35)=1
1150 IFU$="W"ANDVB=3ANDF(35)=0THENR=R-1:F(35)=1
1160 IFU$="E"ANDVB=4ANDF(35)=0THENR=R+1:F(35)=1
1170 IFU$="U"ANDVB=5ANDF(35)=0ANDR=17THENR=12:F(35)=1
1180 IFU$="U"ANDVB=5ANDF(35)=0ANDR=48THENR=25:F(35)=1
1190 IFU$="D"ANDVB=6ANDF(35)=0ANDR=12THENR=17:F(35)=1
1200 IFU$="D"ANDVB=6ANDF(35)=0ANDR=25THENR=48:F(35)=1
1210 NEXTI
1220 M$="OK":IFF(35)=0THENM$="YOU CAN'T GO THAT WAY!"
1230 RETURN
1300 REM EXAMINE
1310 M$="IT'S JUST AN ORDINARY "+W$
1399 RETURN
1400 REM WAVE
1410 IFOB<>5THENM$="WAVING A "+W$+" PROVES NOTHING.":RETURN
1415 IFC(5)=0THENRETURN
1420 IFR<>24ANDR<>36THENM$="NOTHING HAPPENS!":RETURN
1430 M$="MAGIC OCCURS... YOU ARE MAGICALLY TRANSPORTED."
1440 IFR=24THENR=36:RETURN
1450 IFR=36THENR=24:RETURN
1500 REM GET
1510 M$="I CAN'T GET '"+W$+"'"
1520 IFL(OB)<>RTHENM$="IT ISN'T HERE.":RETURN
1530 IFF(OB)=1THENM$="WHAT "+W$+"?":RETURN
1540 IFC(OB)=1THENM$="YOU ALREADY HAVE IT.":RETURN
1545 RT=0
1550 IFOB>0ANDL(OB)=RANDF(OB)=0THENRT=1
1560 IFRT=1THENCO=CO+1:IFCO>3THENCO=3:M$=H$(2):RETURN
1570 IFRT=1THENC(OB)=1:L(OB)=49:M$=H$(1)+W$
1599 RETURN
1600 REM DROP
1605 IFC(OB)=0THENM$="YOU DON'T HAVE THE "+W$+".":RETURN
1610 IFC(OB)=1THENC(OB)=0:L(OB)=R:M$="YOU HAVE DROPPED THE "+W$+"."
1620 CO=CO-1:IFCO<0THENCO=0
1699 RETURN
1700 REM HELP
1710 M$="IF YOU REALLY NEED HELP THEN CONTACT ME AT (03)754-4460"
1799 RETURN
1800 REM VOCABULARY
1810 CLS:PRINT"VOCABULARY":GOSUB4000
1820 PRINT"----------"
1830 I$="":FORI=0TOV
1840 I$=I$+V$(I)+", "
1850 NEXTI:Z$=LEFT$(I$,LEN(I$)-2)+".":GOSUB1
1860 M$="":PRINT
1870 GOSUB3000
1899 RETURN
1900 REM EAT
1910 IFC(4)=0ANDOB=4THENM$="WHAT BUN?":RETURN
1920 IFOB<>4THENM$="EAT WHAT?":RETURN
1930 M$="MUNCH... MUNCH... <BURP>... THE CREAM BUN WAS DELICIOUS!"
1940 C(OB)=0:CO=CO-1
1999 RETURN
2000 REM INVENTORY
2010 CLS:PRINT"INVENTORY":GOSUB4000
2020 PRINT"---------"
2030 PRINT:PRINT:PRINT"YOU ARE CARRYING...":PRINT
2040 I$="":FORI=1TOO
2050 IFC(I)=1THENI$=I$+O$(I)+", "
2060 NEXTI:IFI$<>""THENZ$=LEFT$(I$,LEN(I$)-2):GOSUB1
2065 M$="":GOSUB3000
2099 RETURN
2100 CLS:PRINT"SCORE":GOSUB4000
2110 PRINT"-----"
2120 PRINT:PRINT:PRINT
2130 IFC(1)=1ANDC(2)=1ANDC(3)=1ANDR=1THENS=60:GOTO2140
2131 GOTO2200
2140 PRINTS:PRINT:Z$="CONGRATULATIONS! YOU HAVE COMPLETED THE ADVENTURE.":GOSUB1
2150 SOUND200,4:END
2200 IFC(1)=1ORC(2)=1ORC(3)=1THENS=20
2240 IFC(1)=1ANDC(2)=1THENS=40
2250 IFC(1)=1ANDC(3)=1THENS=40
2260 IFC(2)=1ANDC(3)=1THENS=40
2270 PRINT:PRINT:Z$="YOUR SCORE SO FAR IS:"+STR$(S):GOSUB1:M$=""
2280 GOSUB3000:RETURN
3000 PRINT@480,"PRESS <ENTER> TO CONTINUE";:R$=INKEY$
3010 T$=INKEY$:IFT$<>CHR$(13)THEN3010
3011 SOUND1,1:RETURN
4000 PRINT@16,"BY SCOTT LE BRUN";:RETURN
6000 REM INITIALISATION
6010 DIM O$(O),V$(V),D$(48),R$(48),C(O),L(O),F(35)
6020 DATA"AT A PLATEAU NEAR A CLIFF, A ROCKY PATH LEADS SOUTH"
6030 DATA" "," ","ON A ROCKY PATH LEADING NORTH & CURVING EAST"
6040 DATA"ON A PATH THAT LEADS WEST AND WINDS EAST"
6050 DATA"AT THE ENTRANCE TO A DARK CAVE"
6060 DATA" "," "
6070 DATA"INSIDE A NORTH-SOUTH TUNNEL"," "," "
6080 DATA"IN AN OVAL CAVERN.THERE IS A STAIRCASE LEADING DOWN"
6090 DATA" ","IN A COLD,SMALL ROOM"
6100 DATA" ","IN ROOM WITH COBWEB COVERED WALLS"
6110 DATA"IN AN IMMENSE CAVERN WITH TALL OMINOUS WALLS"
6120 DATA"IN A TRIANGULAR SUB-CHAMBER"
6130 DATA" ","IN AN ENORMOUS CAVE WITH A STONE PILLAR"
6140 DATA" ","IN AN OCTAGONAL ROOM","STALAGTITE LITTERED ROOM"
6150 DATAIN A ROOM WHICH THE ONLY WAY OUT IS THE WAY YOU CAME IN
6160 DATA IN A ROUND ROOM WITH A SPIRAL STAIRCASE LEADING DOWN
6170 DATA"IN A LOW-HUGGING TUNNEL"," "," "
6180 DATA"IN A STRANGE TWISTED ROOM WITH WALLS OF ICE"
6185 DATA"IN A ROOM WITH STALAGMITES EMINATING EVERWHERE"
6190 DATA" "," ","IN A ROOM WITH INCHES OF UNDISTURBED DUST"," "
6200 DATA" ","IN A ROOM ONLY REACHED BY MAGICAL MEANS"," "," "
6210 DATA" ","YOU ARE IN THE THRONE ROOM OF THE EVIL OVERLORD
6220 DATA"IN COLD DANK TUNNEL WITH ROCKS STREWN EVERYWHERE"
6225 DATA A MAGICAL ROOM
6230 DATA" "," ","IN A VAST UNDERGROUND CAVERN"," "," "
6240 DATA"IN A ROOM WITH A SPIRAL STAIRCASE LEADING UP"
6250 FORI=1TO48:READD$(I):NEXT
6300 REM OBJECTS
6310 DATA CROWN,RING,GOBLET,BUN,ROD,LAMP
6320 FORI=1TOO:READO$(I):NEXT
6330 REM VERBS
6340 DATA N,S,W,E,U,D,EXAMINE,WAVE,GET,DROP,HELP
6350 DATA VOCABULARY,EAT,INVENTORY,SCORE
6360 FORI=1TOV:READV$(I):NEXT
6400 REM ROUTES
6410 DATA S," "," ",NE,WE,SW," "," ",NS," "," ",ND
6420 DATA " ",S," ",E,NSWEU,W," ",NS," ",E,NWE,W
6430 DATA DE,SW," "," ",NE,SW," "," ",NS," "," ",N
6440 DATA " "," "," ",E,WE,WS," "," ",NS," "," ",NU
6450 FORI=1TO48:READR$(I):NEXT
6460 R=1:M$="OK"
6470 DATA 40,16,22,12,14,1
6480 FORI=1TOO:READL(I):NEXT
6490 H$(1)="YOU HAVE THE "
6495 H$(2)="YOU WILL HAVE TO DROP SOMETHING"
6500 H$(3)="A MAGICAL BARRIER BLOCKS THE WAY!":RETURN
6530 REM ADVENTURE
6531 REM BY SCOTT LE BRUN
