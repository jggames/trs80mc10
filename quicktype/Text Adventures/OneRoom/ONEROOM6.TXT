0 CLS:CLEAR900:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Q$=MID$(M$,CC,1):IFQ$=" "ORQ$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFQ$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 CC=0:LL=LEN(Q$):FORZZ=1TOLEN(M$):IFMID$(M$,ZZ,LL)=Q$THENCC=ZZ:ZZ=255
4 NEXT:RETURN
8 FORCC=1TO32:PRINT"Œ";:NEXT:RETURN
9 GOSUB1:GOTO320
10 PRINT"    * A ONE ROOM ADVENTURE *":GOSUB8:PRINT:PRINT"YOU HAVE JUST AWAKENED.":PRINT
20 M$="YOU DON'T HAVE THE SLIGHTEST IDEA WHERE YOU ARE OR EVEN WHO YOU ARE!":GOSUB1
30 PRINT:M$="YOU SEEM TO HAVE AMNESIA FROM A BLOW TO YOUR HEAD.":GOSUB1
40 MX=8:CA=0:XT=0:BT=0:GOSUB3000:FORZZ=1TO3500:NEXT
200 Y=1
210 CLS:IFY<0THENY=2
230 M$="I AM "+R$(Y):GOSUB1:IFF(2)=0THENPRINT"I AM BLINDFOLDED."
240 IFF(3)=0THENPRINT"I AM SITTING ON A CHAIR."
250 IFF(2)=0THEN320
270 PRINT"I CAN SEE: ":F=0
280 FORI=1TONO
290 IFINT(O(I))<>INT(Y)THEN310
300 M$=" "+O$(I):GOSUB1:F=1
310 NEXT:IFF=0THENPRINT"  NOTHING INTERESTING"
320 IFBT>6THENF(1)=0
330 IFF(1)=1THENBT=BT+1:GOSUB2830
340 IFF(13)=1ANDO(49)<>0THEN345
341 GOTO350
345 M$="SOMETHING JUST CAME THROUGH THE BROKEN WINDOW.":GOSUB1
346 F(13)=0:O(54)=6:F(7)=1:E$(37)="THERE IS A PIECE OF PAPER TIED TO ONE OF ITS LEGS."
350 GOSUB8:INPUT"WHAT SHOULD I DO";I$
360 IFLEN(I$)=1THENM$="NSEWUD":Q$=I$:GOSUB3:X=CC:IFX>0ANDX<7THENY=X+1:GOTO210
370 IFI$=""THEN350
371 IFI$="LOOK"THENCLS:GOTO210
380 IFI$="QUIT"THEN1850
400 IFI$="HELP"THEN750
420 IFI$="RADIO OFF"THENF(1)=0:M$="OK, RADIO IS OFF.":GOTO9
430 IFI$<>"PLAY RADIO"ANDI$<>"RADIO ON"THEN440
435 IFO(14)=0THENF(1)=1:GOTO320
436 M$="IT NEEDS A BATTERY.":GOTO9
440 IFLEFT$(I$,3)<>"INV"THEN500
450 CLS:PRINT"I AM CARRYING:":CA=0
460 FORI=1TONO
470 IFO(I)=-1THENM$="  "+O$(I):GOSUB1:CA=CA+1
480 NEXT:IFCA=0THENPRINT"NOTHING"
490 GOTO320
500 M$=I$:Q$=" ":GOSUB3:SP=CC:IFSP=0THENM$="TRY USING TWO-WORD COMMANDS.":GOTO9
520 V$=LEFT$(I$,SP-1):O$=MID$(I$,SP+1)
530 A$=LEFT$(V$,3):B$=LEFT$(O$,3)
540 IFA$="TAK"THENA$="GET"
550 IFA$="LEA"THENA$="DRO"
560 IFA$="LOO"THENA$="FAC"
570 IFA$="MOV"THENA$="PUS"
580 IFA$="TEA"THENA$="BRE"
590 IFA$="TOU"THENA$="FEE"
600 IFB$="FLO"THENB$="DOW"
610 IFB$="CEI"THENB$="UP "
620 IFA$="SHR"THENA$="CUT"
630 IFA$="UNL"THENA$="OPE"
640 IFB$="CB"THENB$="RIG"
650 IFB$="BIR"THENB$="DOV"
660 M$=VB$:Q$=A$:GOSUB3:V=(CC+2)/3:IFV<1THENM$="SORRY, I JUST DON'T KNOW HOW TO "+V$+" ANYTHING.":GOTO9
670 M$=OB$:Q$=B$:GOSUB3:O=(CC+2)/3:IFO>1THEN980
680 IFA$<>"EXA"ORB$<>"POC"THEN690
685 IFO(35)=-1ANDO(24)=0THENPRINT"I SEE A PAIR OF PLIERS.":GOTO320
686 IFO(35)=-1ANDO(24)<>0THENPRINT"POCKETS ARE EMPTY.":GOTO320
687 PRINT"I DON'T HAVE ANY POCKETS.":GOTO320
690 IFA$="EXA"ANDB$="ROO"THENM$="IT'S A LARGE ROOM WITH A WINDOW, A DOOR AND LOTS OF THINGS LYING AROUND.":GOTO9
700 IFA$="EXA"ANDB$="GAT"THENM$="THE ONLY WAY TO OPEN IT IS TO CUT THE WIRE.":GOTO9
710 GOTO980
750 M$="TELL ME WHICH OBJECT HAS YOU A LITTLE PUZZLED: ":GOSUB1:INPUTI$:PRINT
760 IFI$="ROPE"THENM$="HAVE YOU TRIED RUBBING IT ON SOMETHING SHARP?":GOTO9
770 IFI$="RADIO"THENM$="TRY PLAYING IT.":GOTO9
780 IFI$="CB"ORI$="SET"THENM$="READ A BOOK ON IT.":GOTO9
790 IFI$="DOVE"ORI$="BIRD"THENM$="LET IT FLY AWAY AND SEE WHAT HAPPENS.":GOTO9
800 IFI$="MOUSE"ORI$="RAT"THENM$="YOU CAN'T CATCH IT, SO DON'T WASTE YOUR TIME.":GOTO9
810 IFI$="STOVE"THENM$="THERE MAY BE SOMETHING IN IT.":GOTO9
820 IFI$="ANTENNA"THENM$="YOU CAN MAKE AN ANTENNA OUT OF WIRE.":GOTO9
830 IFI$="WINDOW"THENM$="IT CAN'T BE OPENED, SO DON'T WASTE YOUR TIME.":GOTO9
840 IFI$="CORD"THENM$="IT WON'T DO ANY GOOD UNLESS IT IS PLUGGED IN.":GOTO9
850 IFI$="ROOM"THENM$="IN CASE YOU DIDN'T KNOW IT, YOU CAN ALSOLOOK UP OR DOWN.":GOTO9
860 IFI$="ME"ORI$="ALL" OR I$="" OR I$="ANYTHING" OR I$="EVERYTHING"THENM$="TRY MOVING THINGS AROUND.":GOTO9
870 IFI$="BUTTON"THENM$="IF YOU PUSH IT, YOU MAY BE ABLE TO TRANSMIT ON THE CB.":GOTO9
880 IFI$="CHAIR"THENM$="HAVE YOU TRIED FEELING IT?":GOTO9
890 IFI$="DOOR"THENM$="IT CAN ONLY BE OPENED FROM THE OUTSIDE.":GOTO9
900 IFI$="GLASS"THENM$="IT CAN BE USED FOR CUTTING.":GOTO9
910 IFI$="HANGER"THENM$="MAYBE IT CAN BE USED AS A CB ANTENNA.":GOTO9
920 IFI$="HOLE"THENM$="TRY COVERING THE HOLE WITH SOMETHING.":GOTO9
930 IFI$="TOOLBOX"THENM$="YOU NEED SOMETHING WITH WHICH TO OPEN IT.":GOTO9
940 IFI$="PILLOW"THENM$="TRY GETTING WHAT'S INSIDE.":GOTO9
950 IFI$="PAPER"THENM$="YOU NEED TO HAVE SOMETHING IN YOUR HANDS BEFORE YOU CAN READ IT.":GOTO9
960 IFI$="BLINDFOLD"THEN M$="YOU NEED TO UNTIE YOUR HANDS BEFORE YOU CAN REMOVE IT.":GOTO9
970 M$="SORRY, I CAN'T HELP YOU WITH THAT ITEM.":GOTO9
980 F=0:IFO(O)=Y THENF=1:GOTO1000
981 IFO(O)=-1THENF=2
1000 ONVGOTO1040,1300,1380,1510,1560,1650,1890,1960,1990,2060,2110,2130,2190,2230,2260,2300,2370,2410,2450,2500,2590
1010 IFLEN(I$)=1THENPRINT"I CAN'T GO THAT WAY!":GOTO210
1020 M$="I DON'T UNDERSTAND.":GOTO9
1040 IFF(2)=0THENM$="I CAN'T GET ANYTHING NOW!":GOTO9
1050 IFF=2THENM$="I ALREADY HAVE IT!":GOTO9
1060 IFCA>=MXTHENM$="I CAN'T CARRY ANYTHING ELSE!":GOTO9
1070 IFO<1THENM$="WHY DO YOU WANT TO DO THAT FOR?":GOTO9
1080 IFO<7ORO=15ORO=16ORO=17ORO=26ORO=39ORO=40ORO=41ORO=46THENPRINT"DON'T BE SILLY!":GOTO320
1090 IFO=11ORO=27ORO=32ORO=50THENM$="I CAN'T, IT'S TOO HEAVY!":GOTO9
1100 IFO=12THENIFF(6)=0THENM$="I CAN'T, DESK IS LOCKED.":GOTO9
1101 IFO=12THENE$(O-6)="I SEE A CB SET.":O(29)=2:GOTO1490
1110 IFO=43ANDO(54)=0THENIFF(5)=0THENM$="I CAN'T, THE CAGE IS NOT OPEN.":GOTO9
1120 IFO<>43ORO(49)<>-1THEN1130
1125 IFO(54)=6THENO(49)=0:O(54)=0:GOTO1270
1126 M$="IT WON'T COME TO ME.":GOTO9
1130 IFO=43ANDO(49)<>-1THENM$="IT WON'T COME TO ME.":GOTO9
1140 IFO=45THEN1145
1141 GOTO1150
1145 IFLEFT$(E$(34),2)="I "THENE$(34)="THERE IS NOTHING IN THERE.":GOTO1270
1146 IFO=45THEN1280
1150 IFO=31THENM$="I CAN'T DO THAT.":GOTO9
1160 IFO=30THENO(31)=Y
1170 IFO=54ANDF(7)=1THENF(7)=0:O(54)=0:O(49)=0:O=43:GOTO1270
1180 IFO=55ANDO(55)=0THENM$="I CAN'T, THE WIRE IS WRAPPED TOO TIGHT.":GOTO9
1190 IFO=35THENO$(O)="A COAT":O(36)=5:GOTO1270
1200 IFO=36THENO$(O)="A HANGER":GOTO1270
1210 IFO=33THENE$(26)="I SEE A CAN OPENER.":GOTO1270
1220 IFO=18THENO(40)=3
1230 IFO=29THEN1235
1231 GOTO1240
1235 IFF(6)=0THEN1280
1236 IFO=29THENE$(6)="IT'S NOW EMPTY.":GOTO1270
1240 IFO=44ANDLEFT$(E$(37),1)<>"T"THENM$="I DON'T SEE ANY PAPER HERE.":GOTO9
1241 E$(37)="IT'S A SPECIAL TYPE DOVE."
1250 IFO=56THENE$(26)="THERE IS NOTHING THERE.":GOTO1270
1260 IFO=49THENIFO(49)=0THEN1280
1270 O(O)=-1:CA=CA+1:M$="OK, I GOT IT.":GOTO9
1280 M$="I CAN'T GET THAT NOW.":GOTO9
1300 IFF<>2THEN1360
1310 IFO=43ANDF(4)=1THENM$="THE DOVE FLEW OUT THE BROKEN WINDOW.":GOSUB1:F(13)=1:O(43)=0:E$(36)="IT'S EMPTY.":GOTO1350
1320 IFO=43THENO(43)=0:O(54)=6:PRINT"OK, I LET THE DOVE GO.":GOTO1350
1330 IFY=6THENY=7
1340 O(O)=Y:PRINT"OK, I DROPPED IT."
1350 CA=CA-1:GOTO320
1360 PRINT"I DON'T HAVE IT.":GOTO320
1380 IFO=13THENO(14)=7:M$="THE BACK COVER POPPED OPEN AND SOMETHING FELL TO THE FLOOR.":GOTO9
1390 IFO=11THEN1395
1391 GOTO1400
1395 IFO(45)=-1THENF(6)=1:GOTO1490
1396 IFO=11THENM$="I DON'T HAVE ANY KEYS.":GOTO9
1400 IFO<>42THEN1420
1401 IFO(55)=0THENM$="I CAN'T, THE GATE IS WIRED SHUT.":GOTO9
1410 E$(36)="IT'S EMPTY.":F(5)=1:IFO(43)=-1ORF(13)THEN1490
1411 IFF(4)ANDF(13)=0ANDO(43)<>-1ANDF(7)=0THENF(13)=1:M$="THE DOVE JUST FLEW OUT THROUGH THE BROKEN WINDOW...":GOTO9
1412 IFF(4)=0THENO(54)=6:F(7)=1
1413 GOTO1490
1420 IFO=12THEN1100
1430 IFO=40THENINPUT" THE COMBINATION";I$:IFI$="4R8L12R"THENPRINT"CLICK!":E$(O-6)="I SEE A SET OF KEYS.":GOTO1490
1440 IFO=21AND(F=1ORF=2)THEN1445
1441 GOTO1450
1445 IFO(56)=-1THENO(22)=Y:O(23)=Y:O(25)=Y:GOTO1490
1446 IFO=21AND(F=1ORF=2)THENM$="THE TOP IS RUSTED AND CANNOT BE OPENED.":GOTO9
1450 IFO=50THEN1455
1451 GOTO1460
1455 IFO(37)=0THENM$="I OPENED THE STOVE AND SAW AN ELECTRIC CORD INSIDE.":GOTO9
1456 IFO=50THENM$="THERE IS NOTHING IN IT.":GOTO9
1460 IFF=0THEN2780
1461 IFO=50ORO=42THEN1490
1470 M$="SORRY, IT DOES NOT OPEN.":GOTO9
1480 M$="IT'S ALREADY OPEN.":GOTO9
1490 M$="OK, IT'S NOW OPEN.":GOTO9
1510 IFO=42THEN1515
1511 GOTO1520
1515 IFF(5)=1THEN1540
1516 IFO=42THENM$="IT'S ALREADY CLOSED.":GOTO9
1520 IFF=0THEN2780
1530 M$="I TRIED, BUT IT DOESN'T CLOSE.":GOTO9
1540 M$="OK, IT'S NOW CLOSED.":GOTO9
1560 IFO<1THENM$=I$:Q$=" ":GOSUB3:M$="DID ANYBODY MENTION ANYTHING ABOUT ANY"+MID$(I$,CC)+"?":GOTO9
1570 IFF(2)=0THENM$="I CAN'T, I'M BLINDFOLDED.":GOTO9
1580 IFO<7THENY=O+1:GOTO210
1590 IFB$="POC"THEN1595
1591 GOTO1600
1595 IFO(24)=0ANDO(35)=-1THENM$="I SEE A PAIR OF PLIERS.":GOTO9
1596 IFB$="POC"THENM$="THE POCKETS ARE EMPTY.":GOTO9
1600 IFO=44ANDF<>2THENM$="I DON'T HAVE IT.":GOTO9
1610 IFO=44ANDO(51)<>-1THENM$="THERE IS PRINTING ON IT.":GOTO9
1620 M$=E$(O-6):GOTO9
1630 M$="NOTHING SPECIAL ABOUT IT.":GOTO9
1650 IFF<>1THEN1700
1660 IFO=11THENO(15)=2:GOTO1870
1670 IFO=27THEN1675
1671 GOTO1680
1675 IFO(39)<>-2THENO(39)=Y:GOTO1870
1676 IFO=27THEN1900
1680 IFO=30THENO(31)=Y:GOTO1870
1690 IFO<>31THEN1700
1695 IFO(22)<>-1THENM$="I CAN'T WITH JUST MY HANDS.":GOTO9
1696 IFO(20)=0THENO(20)=Y
1697 GOTO1870
1700 IFO=17THEN1940
1701 IFO<>46THEN1860
1710 IFF(16)=0THENM$="IT WILL NOT WORK AT ALL WITHOUT AN ANTENNA!":GOTO9
1720 IFO(29)=0THEN1860
1730 IFF(9)=0ORF(10)=0THENM$="THE CB RIG DOES NOT SEEM TO BE WORKING AT ALL.":GOTO9
1740 M$="THE CB SWITCHED TO TRANSMIT. WHAT DO YOU WANT TO SAY?":GOSUB1:INPUTI$
1750 IFF(14)=1THEN1755
1751 GOTO1760
1755 BT=BT+1:IFBT>=10THENE$(8)="THEY ARE DEAD.":F(14)=0:M$="I CAN'T HEAR YOU ANYMORE. YOUR BATTERIES MUST BE LOW.":GOTO9
1756 IFF(14)=1THENBT=BT+1:M$="I HEAR YOU OLD BUDDY, KEEP ON TALKING SO WE CAN FIND YOU.":GOTO9
1760 IFF(15)=1ANDXT=0THENXT=XT+1:M$="I HEAR YOU OLD BUDDY, KEEP ON TALKING SO WE CAN FIND YOU.":GOTO9
1761 XT=XT+1
1770 IFF(15)<>2THEN1860
1771 ONXTGOTO1790,1800,1810,1820,1830
1780 M$="'WE HEAR YOU OLD BUDDY! KEEP YAKKING ON THAT RIG!'":GOTO9
1790 M$="'WE ARE GETTING SOME MORE HELP. JUST STAY PUT!'":GOTO9
1800 M$="'WE GOT YOUR SIGNAL TRACKED DOWN AND WE THINK WE KNOW WHERE YOU ARE!'":GOTO9
1810 M$="'KEEP THOSE LIPS GOING, BUDDY, SO WE CAN FIND YOU QUICKER.'":GOTO9
1820 M$="'WE SEE A BUILDING IN THE FIELD. THAT MUST BE IT! WE'RE COMING IN!'":GOSUB1
1830 M$="THE TRUCK DRIVERS CRASHED THE DOOR AND GOT YOU OUT SAFELY.":GOSUB1
1840 M$="NEXT TIME BE MORE CAREFUL WHEN YOU SEE A ROBBERY TAKING PLACE!":GOSUB1
1850 PRINT:INPUT"PLAY AGAIN";M$:IFLEFT$(M$,1)="Y"THENRUN
1851 END
1860 M$="NOTHING HAPPENED.":GOTO9
1870 M$="OK, IT MOVED.":GOTO9
1890 IFF=0THEN2780
1900 INPUT" WITH WHAT";I$:GOSUB2800:IFX>0THEN1900
1910 IFO=26THEN1915
1911 GOTO1920
1915 IFI$="CROWBAR"ORI$="HAMMER"ORI$="RACK"THEN1919
1916 IFO=26THEN1940
1917 GOTO1920
1919 F(4)=1:O(19)=7:E$(O-6)="THE WINDOW IS BROKEN.":GOSUB2960:M$="THE GLASS WINDOW PANES BROKE.":GOTO9
1920 IFO=26ANDF(7)=1THENF(13)=1:F(7)=0:O(54)=0:O(43)=0:M$="THE DOVE FLEW OUT THE BROKEN WINDOW.":GOTO9
1930 IFO=33ANDO(19)=-1THENO(51)=7:E$(27)="IT'S TORN AND THERE IS NOTHING IN IT.":M$="SOMETHING FELL OUT OF IT.":GOTO9
1940 M$="I TRIED, BUT NOTHING HAPPENED.":GOTO9
1960 IFF=0THEN2780
1970 M$="I TRIED TO ENTER, BUT COULDN'T.":GOTO9
1990 IFF<>2THENM$="I DON'T HAVE IT.":GOTO9
2000 IFO=28THENM$="THE BOOK SAYS: IN ORDER TO OPERATE CB SET YOU NEED SOME POWER, AN ANTENNA AND A MICROPHONE.":GOTO9
2010 IFO=44ANDO(51)<>-1THENM$="THE PRINT IS TOO SMALL TO READ WITH THE NAKED EYE.":GOTO9
2011 M$=E$(O-6):GOTO9
2020 IFF<>1THEN1960
2030 IFF=0THEN2780
2040 M$="NOTHING SPECIAL NOTED.":GOTO9
2060 IFO=17THENF(10)=1:GOTO2090
2070 M$="I TRIED, BUT NOTHING HAPPENED.":GOTO9
2080 IFF=0THEN2780
2090 M$="OK, I TURNED IT.":GOTO9
2110 M$="DON'T BE RIDICULOUS!":GOTO9
2130 IFF<>2THENM$="I DON'T HAVE IT TO SHAKE.":GOTO9
2140 IFO=48THENM$="SOME SEEDS FELL TO THE FLOOR.":GOSUB1:GOTO2150
2141 M$="NOTHING HAPPENED.":GOTO9
2150 IFF(17)=0THENM$="A MOUSE ATE THE SEEDS AND TOOK OFF BEFORE I COULD CATCH IT!":GOTO9
2151 O(49)=7:GOTO320
2190 IFF(3)=0THEN2790
2200 IFO=9THEN2380
2210 GOTO320
2230 IFO=7THENM$="I FEEL SOMETHING LIKE A NAIL.":GOTO9
2240 M$="I DON'T FEEL ANYTHING UNUSUAL.":GOTO9
2260 INPUT" ON WHAT";I$:GOSUB2800:IFX>0THEN2260
2270 IFO=8ANDI$="NAIL"THENF(3)=1:O(8)=7:M$="OK, THE ROPE WAS TORN BY THE NAIL WHEN I RUBBED IT.":GOTO9
2280 M$="NOTHING HAPPENED.":GOTO9
2300 IFF(3)=0THEN2790
2310 INPUT" WITH WHAT";I$:GOSUB2800:IFX>0THEN2310
2320 IFO=33THEN1930
2330 IFO=55ANDI$="PLIERS"ANDO(24)=-1THENO(55)=7:E$(49)="THE WIRE HAS BEEN CUT.":M$="OK, THE WIRE IS NOW CUT.":GOTO9
2331 M$="I CAN'T.":GOTO9
2340 IFO<>33ORF<>0OR(I$<>"GLASS"ANDI$<>"BROKEN GLASS")THEN2350
2345 IFO(19)<>-1THENM$="I DON'T HAVE THE GLASS WITH ME.":GOTO9
2346 IFO(19)=-1THENO(51)=7:E$(27)="IT IS TORN.":M$="SOMETHING CAME OUT OF THE TORN PILLOW.":GOTO9
2350 M$="I CAN'T CUT IT.":GOTO9
2370 IFF(3)=0THEN2790
2380 IFO=9ANDF(2)=0THENF(2)=1:Y=2:M$="OK, I DID.":GOTO9
2390 M$="IT CAN'T BE REMOVED.":GOTO9
2410 IFO<1THEN1560
2420 IFO<7THENY=O+1:GOTO210
2430 GOTO1560
2450 IFF(2)=0ORF(3)=0THEN2640
2460 IFO(O)=0THENM$="I DON'T KNOW WHAT YOU MEAN!":GOTO9
2470 IFO(O)=-1THENM$="I CAN'T, I'M CARRYING IT!":GOTO9
2480 Y=O(O):M$="OK, I DID.":GOTO9
2500 IFF=0THENM$="I DON'T SEE THAT HERE.":GOTO9
2510 INPUT" WHERE";I$:GOSUB2800:IFX>0THEN2510
2520 IFO<>37ORI$<>"OUTLET"THEN2530
2525 M$="OK, CORD IS NOW PLUGGED INTO THE OUTLET.":GOSUB1:E$(O-6)="IT'S PLUGGED IN."
2526 IFLEFT$(E$(23),2)="CB"THENF(9)=1:E$(23)="CB IS READY TO GO!":GOTO2540
2530 IFO=37AND(I$="RIG"ORI$="CB")THEN2535
2531 GOTO2540
2535 M$="OK, CB RIG NOW HAS THE CORD CONNECTED.":GOSUB1
2536 IFLEFT$(E$(23),4)="THER"THENF(9)=1:E$(23)="CB NOW HAS A CORD AND MIKE.":GOTO320
2540 IFF(15)=0THENF(15)=1:GOTO2550
2541 IFF(15)=1THENF(15)=2:F(9)=1
2550 IFO=55THENM$="THAT WIRE WON'T DO ANY GOOD, IT HAS BEEN CUT INTO SMALL PIECES.":GOTO9
2560 IFO=36THENF(16)=1:M$="OK, THE WIRE HANGER FIT NICELY INTO THE ANTENNA JACK!":GOTO9
2570 IFO=37THEN320
2571 M$="I CAN'T CONNECT THAT.":GOTO9
2590 IFF<>2THENM$="I DON'T HAVE IT.":GOTO9
2591 INPUT" WHERE";I$:GOSUB2800:IF X>0THEN2590
2600 IFO=43ANDI$="CAGE"THENE$(36)="I SEE A DOVE.":O(O)=0:F(5)=0:F(7)=0:GOTO2630
2610 IFO=20ANDI$="HOLE"THENF(17)=1:O(39)=-2:O(20)=-2:M$="OK, THE HOLE IS NOW PLUGGED AND THE MOUSE CAN'T GET OUT.":GOTO9
2620 M$="I CAN'T.":GOTO9
2630 M$="OK, I DID.":GOTO9
2640 M$="I CAN'T DO THAT.":GOTO9
2780 M$="I DON'T SEE ANYTHING LIKE THAT AROUND HERE.":GOTO9
2790 M$="I CAN'T, MY HANDS ARE TIED WITH A ROPE.":GOTO9
2800 M$=I$:Q$=" ":GOSUB3:X=CC:IFX>0THENPRINT:M$="PLEASE ANSWER WITH ONE WORD.":GOSUB1:PRINT
2801 RETURN
2830 IFBT<>3THEN2900
2840 FORCC=1TO5:SOUNDRND(100),1:NEXT
2850 M$="RADIO ANNOUNCER INTERRUPTS THE PROGRAM!":GOSUB1
2860 M$="'A TRUCK STOP HAS BEEN HELD UP. ONE OF THE TRUCK DRIVERS HAS BEEN KIDNAPPED.'":GOSUB1
2870 M$="'THERE ARE HUNDREDS OF TRUCK DRIVERS SEARCHING AROUND FOR THEIR FRIEND.'":GOSUB1
2880 M$="'IF YOU HAVE A CB SET, TURN IT ON AND LISTEN TO THE ACTION!'":GOSUB1
2890 RETURN
2900 IFBT<>5THENM$="I HEAR MUSIC COMING FROM THE RADIO.":GOSUB1:RETURN
2910 SOUND100,1:SOUND200,1:SOUND50,1:SOUND200,1:SOUND90,1
2920 M$="RADIO ANNOUNCER INTERRUPTS THE PROGRAM.":GOSUB1
2930 M$="'THE SEARCH FOR THE KIDNAPPED TRUCK DRIVER CONTINUES.'":GOSUB1
2940 M$="'HIS SEMI TRUCK WAS FOUND IN AN ABANDONED ROAD. THERE IS SPECULATION HE MAY NOT BE ALIVE.'":GOSUB1
2950 RETURN
2960 IFO(54)<>0ORO(43)<>0THENM$="THE DOVE FLEW AWAY THROUGH THE BROKEN WINDOW.":GOSUB1:O(54)=0:O(43)=0:F(13)=1
2970 RETURN
3000 DIMCC,ZZ,M$,Q$,R$(7),O$(56),E$(50),O(57),D$(6),F(18):FORX=1TO6:READD$(X):NEXTX:X=0
3100 X=X+1:READI$:IFI$="END"THEN3190
3111 R$(X)=I$:FORY=1TO6:READD:NEXT:GOTO3100
3190 X=0
3200 X=X+1:READI$:IFI$="END"THENNO=X-1:GOTO3250
3211 O$(X)=I$:READI$,O(X):OB$=OB$+I$:IFX>6THENREADE$(X-6)
3240 GOTO3200
3250 X=0
3280 X=X+1:READI$:IFI$="END"THENRETURN
3291 VB$=VB$+LEFT$(I$,3):GOTO3280
3400 DATANORTH,SOUTH,EAST,WEST,UP,DOWN
3420 DATAIN THE MIDDLE OF A ROOM,,,,,,,FACING THE NORTH WALL,,,,,,,FACING THE SOUTH WALL,,,,,,0
3450 DATAFACING THE EAST WALL,,,,,,,FACING THE WEST WALL,,,,,,,LOOKING AT THE CEILING,,,,,,0
3480 DATALOOKING AT THE FLOOR,,,,,,,END
3510 DATANORTH,NOR,0,SOUTH,SOU,0,EAST,EAS,0,WEST,WES,0,UP ,UP ,0,DOWN,DOW,0
3520 DATAA CHAIR,CHA,2,THE CHAIR LOOKS RATHER FRAGILE.,A PIECE OF ROPE,ROP,7,IT'S JUST A REGULAR ROPE.
3540 DATAA BLINDFOLD,BLI,7,IT'S JUST A PIECE OF CLOTH.,A NAIL,NAI,0,THE NAIL IS QUITE SHARP.
3560 DATAA DESK,DES,2,IT'S THE TYPE WITH A ROLLOVER TOP AND HAS A LARGE DRAWER ON THE SIDE.
3570 DATAA DRAWER,DRA,0,THE DRAWER IS LOCKED AND CAN'T BE OPENED.,A RADIO,RAD,2,IT'S A PORTABLE RADIO.
3590 DATAA BATTERY,BAT,0,IT'S A 12 VOLT BATTERY.,AN ELECTRIC OUTLET,OUT,0,IT'S A 120 VOLT OUTLET.
3610 DATAA DOOR,DOO,3,IT'S LOCKED FROM THE OUTSIDE.,A SWITCH ON THE WALL,SWI,3,IT'S A LIGHT SWITCH.
3630 DATAA PICTURE,PIC,3,IT'S A PICTURE OF A BOAT IN A NICE WOODEN FRAME.
3640 DATABROKEN GLASS,GLA,0,THE BROKEN GLASS IS VERY SHARP.,A ROUND BOX,BOX,0,THE BOX IS EMPTY.
3660 DATAA TOOLBOX,TOO,4,IT'S MADE OF METAL.,A CROWBAR,CRO,0,JUST A REGULAR CROWBAR.
3680 DATAA SCREWDRIVER,SCR,0,IT'S THE REGULAR KIND.,A PAIR OF PLIERS,PLI,0,IT'S THE LONG-NOSE TYPE.
3700 DATAA HAMMER,HAM,0,NOTHING SPECIAL ABOUT IT.,A WINDOW,WIN,5,THE PANES HAVE BEEN PAINTED.
3720 DATAANTIQUE FURNITURE,FUR,5,IT'S A BOOKCASE.,A BOOK,BOO,0,IT'S A BOOK ON CB RADIOS.
3740 DATAA CB RIG,RIG,0,THERE IS A MICROPHONE ALREADY ATTACHED TO IT.,A SMALL RUG,RUG,7,IT'S AN ORIENTAL RUG.
3760 DATAA LOOSE BOARD,BOA,0,IT SEEMS TO GIVE A LITTLE.,A COUCH,COU,4,THERE ARE SOME PILLOWS ON IT.
3780 DATAPILLOW,PIL,0,IT FEELS RATHER HEAVY.,A COAT RACK,RAC,5,IT'S MADE OF METAL.
3800 DATAA COAT ON THE RACK,COA,5,IT'S A SPORTS COAT.,A HANGER ON THE RACK,HAN,0,JUST A REGULAR WIRE HANGER.
3820 DATAAN ELECTRIC CORD,COR,0,ONE END IS BARE AND THE OTHER END HAS A PLUG ON IT.
3830 DATAA MICROPHONE,MIC,0,IT IS ATTACHED TO THE CB SET AND HAS A BUTTON ON IT.
3840 DATAA HOLE ON THE WALL,HOL,0,LOOKS LIKE IT WAS MADE BY A LARGE RAT.
3850 DATAA VAULT ON THE WALL,VAU,0,IT HAS A COMBINATION LOCK ON IT.,WALL,WAL,0,NOTHING SPECIAL ABOUT IT.
3870 DATAA BIG BIRD CAGE,CAG,4,IT HAS A WHITE DOVE IN IT.,A WHITE DOVE,DOV,0,IT'S A SPECIAL TYPE DOVE.
3890 DATAA PIECE OF PAPER,PAP,0,THE CHARACTERS '4R8L12R' ARE WRITTEN ON IT.,A SET OF KEYS,KEY,0,I SEE SEVERAL SMALL KEYS.
3910 DATAA RED BUTTON,BUT,0,JUST A REGULAR BUTTON.,A FLOWER POT,POT,3,SUNFLOWERS ARE GROWING IN IT.
3930 DATASUNFLOWERS,SUN,0,THEY ARE KIND OF DRY.,SUNFLOWER SEEDS,SEE,0,NOTHING SPECIAL ABOUT THEM.
3950 DATAA WOODBURNING STOVE,STO,4,IT'S A FRANKLIN STOVE.,A MAGNIFYING LENS,LEN,0,IT'S A SHERLOCK HOLMES FAN CLUB LENS!
3970 DATAPIECES OF WOOD,WOO,0,NOTHING SPECIAL.,PIECES OF CARDBOARD,CAR,0,NOTHING SPECIAL.
3990 DATAA BIRD FLYING AROUND,BIR,0,IT'S THE DOVE I SAW BEFORE.,WIRE,WIR,0,IT'S HOLDING THE DOOR CLOSED.
4010 DATACAN OPENER,OPE,0,IT'S THE HEAVY-DUTY TYPE.,END
4040 DATAGET,DROP,OPEN,CLOSE,EXAMINE,PUSH,BREAK,ENTER,READ,TURN,RUN,SHAKE
4050 DATAUNTIE,FEEL,RUB,CUT,REMOVE,FACE,GO ,CONNECT,PUT,END
4080 DATARADIO OFF OR ON, BLINDFOLD ON OR OFF, HANDS TIED OR UNTIED, WINDOW OK OR BROKEN
4090 DATABIRD CAGE CLOSED OR OPEN,DESK LOCKED OR UNLOCKED,BIRD IN ROOM OR NOT IN
4100 DATASTOVE OFF OR ON,CB OFF OR ON,SWITCH OFF OR ON,ANTENNA OFF OR ON,TOOL BOX LOCKED OR UNLOCKED
4110 DATADOVE DID NOT LEAVE ROOM,CB RUNNING ON BATTERIES,CB RUNNING ON ELECTRICITY,ANTENNA OFF OR ON
4120 DATAHOLE NOT PLUGGED OR PLUGGED,CORD NOT CONNECTED OR CONNECTED,END
