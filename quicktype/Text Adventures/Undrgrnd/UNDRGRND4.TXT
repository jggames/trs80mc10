0 CLEAR200:CLS:DF=1:GF=1:GOSUB272:CP=1:GOTO9
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 GOTO12
8 FORI=1TO2500:NEXT:RETURN
9 GOSUB423
10 IFTB<>0THENO(9)=CP
11 IFTB=1THENPRINT"THERE'S A TAME BEAR FOLLOWING   YOU!"
12 GOSUB45
13 IFV=34THEN260
14 IFV>9ANDNO$=""THENPRINT"YOU NEED A DIRECT OBJECT.":GOTO7
15 IFNO$<>""ANDV=1ANDN=0THENPRINT"THAT DOESN'T MAKE SENSE TO ME.":GOTO7
16 ONVGOTO20,29,9,57,62,64,87,249,91,29,95,104,111,116,120,125,140,148,149,160,172,185
18 ONV-22GOTO189,192,195,200,206,212,220,227,236,240,254,260,262,266,415,419
20 IFNO$<>""ANDN=0THENPRINT"I DON'T KNOW THAT WORD.":GOTO7
21 IFN>28ORN<21THENPRINT"I DON'T UNDERSTAND.":GOTO7
22 IFN>24THENN=N-4
23 N=N-21
24 IFN<>0ANDCP=3ANDGF=1THEN442
25 IFN<>0ANDPD=1THENPRINT"YOU'VE FALLEN IN A PIT!":GOTO81
26 IFP(CP,N)=0THENPRINT"YOU CAN'T GO THAT WAY.":GOTO7
27 IFCP=53ANDN=1THEN471
28 CP=P(CP,N):GOTO9
29 IFN=0THEN254
30 GOSUB447
31 IFO(N)=-1THENPRINT"YOU'VE ALREADY GOT IT!":GOTO7
32 IFO(N)<>CPTHENPRINT"I CAN'T SEE IT HERE.":GOTO7
33 IF(N=18ORN=39)ANDO(19)<>-1THENPRINT"YOU HAVE NO CONTAINER.":GOTO7
35 IFN=39ANDO(19)=-1THENO(39)=0:N=52:O(19)=0:Z=Z-1:GOTO44
36 IFN=18ANDO(19)=-1THENO(18)=0:O(19)=0:N=51:Z=Z-1:GOTO44
37 IFN=1ORN=3ORN=6ORN=9ORN=11ORN=17THENPRINT"DON'T BE SILLY!":GOTO7
38 IFN=20ORN=29ORN=30ORN=31ORN=32ORN=35ORN=36THENPRINT"I CAN'T DO THAT!":GOTO7
39 IFN=40ORN=41ORN=43ORN=49THENPRINT"IT CAN'T BE DONE.":GOTO7
40 IFN=8ORN=50THENPRINT"THERE'S NO POINT,SO I WON'T!":GOTO7
41 IFZ>4THENPRINT"YOU'RE CARRYING TOO MUCH!":GOTO7
42 IFN=12ANDCP=10THENP(10,3)=0:P$(10)="FACED BY A VAST CHASM."
43 IFN=15ANDSC=0THENPRINT"YOU CAN'T GET IT YET.":GOTO7
44 Z=Z+1:PRINT"OK.":O(N)=-1:GOTO7
45 PRINT:INPUT"WHAT NOW";CM$:PRINT
46 NO$="":V$="":V=0:N=0
47 LC=LEN(CM$):FORI=1TOLC:IFMID$(CM$,I,1)<>" "THENV$=V$+MID$(CM$,I,1):NEXT
48 V1$=V$:V$=LEFT$(V1$,3):FORI=1TONV:IFV$(I)=V$ THENV=I:GOTO51
49 NEXT
50 V=1:NO$=V$:GOTO53
51 IFLEN(V1$)+1>=LEN(CM$)THENN=0:RETURN
52 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
53 N1$=NO$:NO$=LEFT$(N1$,3):FORI=1TONN:IFNO$=NO$(I)THENN=I:RETURN
54 NEXTI
55 N=0:RETURN
57 PRINT"YOU ARE CARRYING:":GS=0:Z=0
58 FORI=1TOLO:IFO(I)=-1THENPRINTO$(I):GS=GS+1:Z=Z+1
59 NEXT
60 IFGS=0THENPRINT"NOTHING."
61 GOTO12
62 PRINT"NO POINTS... JUST ESCAPE!":GOTO7
64 IFN=0THEN254
65 GOSUB447
66 IFO(N)<>-1THENPRINT"YOU HAVEN'T GOT IT!":GOTO7
67 IFN=19THENPRINT"SMASH!":O(19)=0:O(50)=CP:Z=Z-1:GOTO7
68 IFN=51THENPRINT"SMASH!":O(51)=0:O(50)=CP:Z=Z-1:GOTO7
69 IFN=52THENPRINT"SMASH!":O(52)=0:O(50)=CP:Z=Z-1:GOTO7
70 IFN=16THENM$="IT VANISHES IN A SPARKLE OF SHATTERED GLASS!":GOSUB1
71 IFN=16THENO(16)=0:Z=Z-1:GOTO7
72 IFN=46THENO(N)=0:O(45)=CP:Z=Z-1:PRINT"OK.":GOTO7
73 IFN<>12THEN77
74 IFCP<>10THEN77
75 M$="BRILLIANT! NOW YOU CAN WALK ACROSS THE PLANK.":GOSUB1
76 O(12)=CP:P(10,3)=14:P$(10)="WALKING ACROSS THE PLANK.":Z=Z-1:GOTO7
77 PRINT"OK.":Z=Z-1:O(N)=CP:IFTB=1ANDN<>13THEN79
78 GOTO12
79 PRINT"THE BEAR GLARES AT YOU AND RUNS AWAY!":TB=0:O(9)=INT(RND(41))
80 Z=Z-1:GOTO7
81 PRINT"YOU'RE DEAD!":END
87 M$="YOU WON'T GET MUCH HELP FROM ME IN THIS GAME.":GOSUB1:GOTO7
91 IFN=0THENPRINT"CROSS WHAT?":GOTO7
92 IF(CP<>15)AND(CP<>10)THENPRINT"THERE'S NOTHING HERE TO CROSS!":GOTO7
93 IFN<>1ANDN<>6ANDN<>12THENPRINT"MMM... WHAT A STRANGE IDEA!":GOTO7
94 PRINT"YOU'LL JUST HAVE TO TYPE IN A   DIRECTION INSTEAD!":GOTO7
95 IFN=0THENPRINT"OPEN WHAT?":GOTO7
96 IFCP<>60ANDCP<>3THENPRINT"THERE'S NOTHING HERE TO OPEN.":GOTO7
97 IFCP=60THEN101
98 IFGF=1THENPRINT"BUT IT IS OPEN!":GOTO7
99 IFO(42)<>-1THENPRINT"BUT YOU HAVEN'T GOT THE KEY!":GOTO7
100 PRINT"THE GATE SWINGS SLOWLY OPEN.":GF=1:P(3,0)=2:GOTO414
101 IFDF=1THENPRINT"BUT IT ALREADY IS OPEN.":GOTO7
102 IFO(33)<>-1THENPRINT"BUT YOU'VE NOTHING STRONG ENOUGHTO OPEN IT WITH!":GOTO7
103 PRINT"YOU'LL JUST HAVE TO TRY AND DO  THIS SOME OTHER WAY!":GOTO7
104 IFN=0THENPRINT"CLOSE WHAT?":GOTO7
105 IFN<>32ANDN<>35THENPRINT"HUH?":GOTO7
106 IFCP=3THEN109
107 IFDF=0THENPRINT"IT'S ALREADY CLOSED.":GOTO7
108 PRINT"OK.":P(60,1)=0:DF=0:P$(60)="FACED WITH A CLOSED DOOR.":GOTO7
109 IFGF=0THENPRINT"IT'S ALREADY CLOSED.":GOTO7
110 M$="THE GATE IS MAGICAL & CANNOT BE CLOSED AGAIN!":GOSUB1:GOTO7
111 IFN=0THEN254
112 GOSUB447
113 IFO(N)<>-1ANDO(N)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO7
114 IFN<>10THENPRINT"I DON'T THINK SO SOMEHOW.":GOTO7
115 PRINT"MMM-MMM! DELICIOUS!":Z=Z-1:O(19)=0:GOTO7
116 IFN=0THENPRINT"I DON'T UNDERSTAND.":GOTO7
117 IFN<>9THENPRINT"IT ISN'T HUNGRY!":GOTO7
118 IFO(10)<>-1THENPRINT"YOU'VE NOTHING TO FEED IT WITH!":GOTO7
119 GOTO136
120 IFN=0THEN254
121 GOSUB447
122 IFN<>51ANDN<>52THENPRINT"YOU MUST BE JOKING!":GOTO7
123 IFN=51THENPRINT"URGGHH!!":O(51)=0:O(19)=-1:GOTO7
124 PRINT"GLUG-GLUG-GLUG ... HIC!!":O(52)=0:O(19)=-1:GOTO7
125 IFN=0THENPRINT"OFFER WHAT?":GOTO7
126 GOSUB447
127 IFO(N)<>-1THENM$="YOU'VE GOT TO HAVE IT BEFORE YOU CAN OFFER IT!":GOSUB1:GOTO7
128 IFN=10THEN135
129 IFN<>52THENPRINT"NO-ONE'S INTERESTED.":GOTO7
130 IFCP<>50THENPRINT"THERE'S NO-ONE HERE WHO WANTS IT(EXCEPT YOU, PERHAPS).":GOTO7
131 PRINT"THE DENIZEN OF THE CAVES DOWNS  IT IN ONE DRAUGHT,AND GRATEFULLYSHOWS YOU A NEW TUNNEL."
132 PRINT"HE THEN WOBBLES SLOWLY AWAY TO  SLEEP OFF HIS HANGOVER."
133 O(52)=0:O(19)=-1:P(50,3)=55:P$(50)="WALKING PAST OLD SPIRITS."
134 O(29)=0:GOTO7
135 IFCP<>27THENPRINT"THERE'S NO ONE HERE WHO WANTS   IT.":GOTO7
136 PRINT"THE BEAR GRATEFULLY ACCEPTS THE BUN, AND STANDS ASIDE TO REVEAL A NEW PATH!"
137 PRINT"IN A SHOW OF GRATITUDE, HE      ATTACHES HIMSELF TO YOU LIKE A  LIMPET."
138 P(27,0)=28:P$(27)="WALKING PAST THE SCENT OF OLD BEAR."
139 O(10)=0:Z=Z-1:TB=1:GOTO7
140 IFN=0THEN254
141 GOSUB447
142 IFO(N)<>-1ANDO(N)<>CPTHENPRINT"YOU HAVEN'T GOT IT.":GOTO7
143 IFN<>2THENPRINT"WAVE,WAVE,WAVE, BUT NOTHING     HAPPENS.":GOTO7
144 IFCP<>15THENPRINT"NOTHING HAPPENS.":GOTO7
145 IFBR=1THENPRINT"DON'T CHANCE YOUR LUCK!":GOTO7
146 PRINT"A CRYSTAL BRIDGE NOW SPANS THE  CHASM (AREN'T YOU THE LUCKY ONE)":O(6)=CP:P(15,1)=17:BR=1
147 P(15,2)=16:P$(15)="WALKING ACROSS THE CHASM.":GOTO7
148 GOTO149
149 IFN=0THEN254
150 GOSUB447
151 IFO(N)<>-1ANDO(N)<>CPTHENPRINT"I CAN'T SEE IT HERE.":GOTO7
152 IFN<>3ANDN<>15ANDN<>5ANDN<>12ANDN<>32THENPRINT"NOTHING DOING!":GOTO7
153 IFO(4)<>-1THENPRINT"YOU'VE NOTHING TO CUT IT WITH.":GOTO7
154 IFN<>3ANDN<>12THENPRINT"YOUR AXE IS NOT STRONG ENOUGH.":GOTO7
155 IFN=3THEN158
156 M$="THE PLANK IS NOW CUT, BUT YOU'LL NEED SOMETHING ELSE TO FINISH THE LADDER":GOSUB1
157 O(12)=0:O(53)=-1:GOTO7
158 PRINT"TIMBERRRRR! THE TREE CRASHES TO THE GROUND."
159 P(21,2)=22:P$(21)="WALKING PAST A DEAD TREE.":O$(3)="AN EX-TREE":GOTO7
160 IFN<>3ANDN<>5ANDN<>13THENPRINT"I BEG YOUR PARDON?!":GOTO7
161 IFN=3THENPRINT"WHAT? AND RUIN YOUR NICE NEW    SUIT?  SORRY.":GOTO7
162 IFN=5THEN168
163 IFO(13)<>CPTHENPRINT"I CAN'T SEE IT ANYWHERE.":GOTO7
164 IFCP<>45ANDCP<>47THENPRINT"THERE'S NO POINT CLIMBING IT    HERE.":GOTO7
165 IFTB<>0THENPRINT"THE LADDER SNAPS IN TWO!!":O(13)=0:P(45,1)=0:GOSUB8:GOTO9
166 IFCP=45THENO(13)=47:CP=47:GOTO9
167 CP=45:O(13)=45:GOTO9
168 IFO(5)<>CP THENPRINT"I DON'T SEE IT ON THE GROUND    ANYWHERE.":GOTO7
169 IFCP<>35ANDCP<>36THENPRINT"THERE'S NO POINT CLIMBING THE   ROPE HERE.":GOTO7
170 IFCP=35THENO(5)=36:CP=36:GOTO9
171 O(5)=35:CP=35:GOTO9
172 IFN=0THEN254
173 GOSUB447
174 IFO(N)<>CPANDO(N)<>-1THENPRINT"IT ISN'T HERE.":GOTO7
175 IFO(44)<>-1THENPRINT"YOU'VE NOTHING TO LIGHT IT WITH.":GOTO7
176 IFN<>45ANDN<>7THENPRINT"IT WON'T LIGHT.":GOTO7
177 IFN=7THEN180
178 IFO(46)=-1THENPRINT"IT'S ALREADY LIT!":GOTO7
179 PRINT"OK.":O(46)=-1:O(45)=0:PD=0:GOTO7
180 IFO(7)=-1THENPRINT"KABOOOM!! YOU'VE JUST BLOWN     YOURSELF UP (STUPID)!":GOTO81
181 IFCP<>4THENPRINT"KABOOOM!! THE DUST CLEARS TO    REVEAL THAT NOTHING'S CHANGED.":O(7)=0
182 IFCP<>4THENZ=Z-1:GOTO7
183 SOUND1,10:M$="THE WALL IS BLASTED TO SMITHEREENS!":GOSUB1
184 O(7)=0:Z=Z-1:P(4,3)=5:P$(4)="WALKING ALONG A DUSTY TRACK.":GOTO7
185 IFN=0THENPRINT"ATTACK WHAT?":GOTO7
186 GOSUB447:IFO(N)<>CP ANDO(N)<>-1THENPRINT"WHERE IS IT?":GOTO7
187 IFN<>9ANDN<>11ANDN<>29ANDN<>30ANDN<>31THENPRINT"WHAT A STRANGE IDEA!":GOTO7
188 PRINT"THIS IS NOT ONE OF YOUR BETTER  SUGGESTIONS!":GOTO7
189 IFN=0THENPRINT"KILL WHAT?":GOTO7
190 GOSUB447:IFO(N)<>CP ANDO(N)<>-1THENPRINT"IT'S NOT HERE.":GOTO7
191 GOTO187
192 IFN=0THENPRINT"HIT WHAT?":GOTO7
193 IFO(N)<>CP ANDO(N)<>-1THENPRINT"IT ISN'T HERE.":GOTO7
194 PRINT"APART FROM A GRAZED HAND,NOTHINGHAPPENS.":GOTO7
195 IFN=0THENPRINT"MAKE WHAT?":GOTO7
196 IFN<>13THENPRINT"I DON'T UNDERSTAND.":GOTO7
197 IFO(53)<>-1ORO(14)<>-1ORO(4)<>-1THENPRINT"YOU HAVEN'T GOT ALL THE RIGHT   MATERIALS.":GOTO7
198 M$="YOU'VE GOT A BRAND NEW LADDER-- SMART WORK.":GOSUB1:O(13)=-1:O(14)=0:O(53)=0:Z=Z-1:GOTO7
200 IFN=0THENPRINT"REFLECT WHAT?":GOTO7
201 IFN<>47THENPRINT"WHAT ARE YOU ON ABOUT?":GOTO7
202 IFCP<>93THENPRINT"NOTHING HAPPENS.":GOTO7
203 IFSC=1THENPRINT"YOU'VE ALREADY DONE THAT.":GOTO7
204 PRINT:PRINT"THE LIGHT IS REFLECTED BACK, ANDTHE CURTAIN FALLS ASIDE (COR)."
205 P(93,0)=95:O(15)=CP:P$(93)="WALKING PAST A SHIMMERING LIGHT.":SC=1:GOTO7
206 IFN=0THENPRINT"OIL WHAT?":GOTO7
207 IFO(51)<>-1THENPRINT"YOU HAVEN'T GOT ANY OIL.":GOTO7
208 IFCP<>79 THENPRINT"NOTHING WORTH OILING AROUND HERE":GOTO7
209 IFN<>17THENPRINT"YOU'VE JUST WASTED A LOT OF OIL.":O(51)=0:O(19)=-1:GOTO7
210 PRINT"THE TRACK SLIDES AWAY, TO REVEALMORE TUNNELS!"
211 O(51)=0:O(19)=-1:P(79,2)=80:P(79,3)=81:P$(79)="WALKING PAST A SMOOTH    TRACK.":O(17)=0:GOTO7
212 IFN=0THEN254
213 GOSUB447
214 IFO(N)<>-1ANDO(N)<>CPTHENPRINT"BUT IT ISN'T HERE!":GOTO7
215 IFO(38)<>-1THENPRINT"YOU'VE NOTHING TO STAB IT WITH.":GOTO7
216 IFN<>30THENPRINT"YOU'RE WASTING YOUR TIME THERE.":GOTO7
217 M$="THE SPIDER DIES IN A GLORIOUS DISPLAY OF BIT-ACTING, AND REVEALS: A NEW PASSAGEWAY (IT WAS VERY LARGE).":GOSUB1
218 P(84,2)=86:P(84,3)=85:O$(30)="A DEAD SPIDER!":P$(84)="WALKING PAST A DEAD SPIDER.":GOTO7
220 IFN=0THEN254
221 GOSUB447
222 IFO(N)<>CPANDO(N)<>-1THENPRINT"I CAN'T SEE IT HERE.":GOTO7
223 IFO(34)<>-1THENPRINT"YOU'RE NOT CARRYING ANY SPRAY.":GOTO7
224 IFN<>31THENPRINT"COUGH-COUGH SPLUTTER-SPLUTTER!":GOTO7
225 PRINT"THE FLY COUGHS ITS LAST, AND    REVEALS A HIDDEN TUNNEL."
226 P(74,3)=75:P$(74)="WALKING PAST A DEAD FLY.":O$(31)="A DEAD FLY!":GOTO7
227 IFN=0THENPRINT"THROW WHAT?":GOTO7
228 IFN<>33ANDN<>4THEN65
229 IFN=4ANDNP=0THEN65
230 IFN=4ANDNP=1THEN458
231 IFO(33)<>-1THENPRINT"YOU'RE NOT CARRYING IT!":GOTO7
232 IFCP<>60THENPRINT"OK! IT VANISHES IN A CLOUD OF   DUST.":O(33)=0:Z=Z-1:GOTO7
233 M$="THE DOOR FLIES OPEN AND REVEALS A NEW PASSAGEWAY!":GOSUB1
234 P(60,0)=61:P(60,3)=65:P$(60)="WALKING PAST THE DOOR.":O(33)=0
235 Z=Z-1:GOTO7
236 IFN=0THEN254
237 GOSUB447
238 IFO(N)<>CP ANDO(N)<>-1THENPRINT"THERE'S NOTHING HERE TO RUB!":GOTO7
239 PRINT"INTERESTING, BUT UNREWARDING.":GOTO7
240 IFN=0THEN254
241 GOSUB447
242 IFN<>48THENPRINT"THERE'S NOTHING ON IT TO READ.":GOTO7
243 IFO(48)<>-1THENPRINT"YOU'RE NOT HOLDING IT.":GOTO7
244 M$="THERE'S MATERIALS IN HERE TO BUILD A LADDER. YOU'LL NEED STUFF LIKE NAILS, A PLANK AND AN AXE.":GOSUB1
246 M$="(AND PERHAPS ONE OR TWO OTHER THINGS!) THERE'S ALSO A TOUCH OF MAGIC IN THE AIR. (SO BE CAREFUL!)":GOSUB1:GOTO7
249 PRINT"OK.":INPUT"SAVE YOUR PROGRESS";M$:IFM$="Y"THEN415
253 END
254 IFN=0THENPRINT"WHAT'S A ";N1$;"!":GOTO7
255 IFN=43ORN=1ORN=6THENPRINT"THERE'S NOTHING INTERESTING HERE":GOTO7
256 GOSUB447
257 IFO(N)<>-1ANDO(N)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO7
258 IFN=2ORN=16ORN=33ORN=37ORN=38THENPRINT"THIS HAS USEFUL POWERS.":GOTO7
259 PRINT"IT'S NOTHING MORE THAN:":PRINTO$(N):GOTO7
260 IFCP=15ORCP=35ORCP=10THENPRINT"I WARNED YOU ....":PRINT"DOWN ... DOWN ... DOWN !!":GOTO81
261 PRINT"WHEEE!":GOTO7
262 IFN=0THENPRINT"BREAK WHAT?":GOTO7
263 GOSUB447
264 IFO(N)<>CP ANDO(N)<>-1THENPRINT"I CAN'T! IT ISN'T HERE.":GOTO7
265 PRINT"YOU COULDN'T BREAK YOUR WAY OUT OF A PAPER BAG!":GOTO7
266 IFN=0THENPRINT"PUSH WHAT?":GOTO7
267 GOSUB447
268 IFO(N)<>CP ANDO(N)<>-1THENPRINT"I CAN'T! IT ISN'T HERE.":GOTO7
269 IFCP<>79THENPRINT"YOU CAN'T!":GOTO7
270 PRINT"TRY DOING THIS ANOTHER WAY (LIKEUSING SOMETHING ELSE!).":GOTO7
271 END
272 NV=38:NN=53:P=100:LO=53:DIMP$(P),P(P,3),O$(LO),O(LO),V$(NV),NO$(NN)
273 P$(1)="ON AN OLD TRACK HEADING TOWARDS THE CAVES.":P$(2)="GETTING EVER NEARER THE CAVES."
275 P$(3)="AT THE ENTRANCE OF THE CAVES, WITH PATHS LEADING OFF EVERYWHERE."
276 P$(4)="IN FRONT OF A SOLID ROCK WALL. YOUR PROGRESS IS HALTED.":P$(5)="IN A SUBTERRANEAN TOMB WITH CREVICES ALL OVER THE PLACE."
278 P$(6)="WALKING AROUND THE SIDE OF THE CREVICE ROOM.":P$(7)="SURROUNDED BY BRICKED UP WALLS."
281 P$(8)="NEAR A GREAT CHASM WHICH PLUNGES DOWN HUNDREDS OF FEET."
282 P$(9)="IN THE HEART OF THE CREVICE ROOM WITH TUNNELS GOING EVERYWHERE."
284 P$(10)="IN FRONT OF A GREAT CREVICE WHICH IS TOO WIDE TO JUMP."
286 P$(11)="ON THE SOUTHERN RIM OF THE CHASM IN A JUMBLE OF ROCKS."
287 P$(12)="LOST IN CHASM COUNTRY.":P$(13)="IN A ROOM FULL OF ROCKS."
289 P$(14)="ON THE WEST SIDE OF THE CHASM. THERE'S NOT MUCH OF A VIEW."
290 P$(15)="FACED WITH A CRACK IN THE FLOOR TOO WIDE TO JUMP."
291 P$(16)="IN AN EAST SIDE CHAMBER.":P$(17)="ON THE MAIN TRACK THROUGH THE CAVES, SURROUNDED CHAMBERS."
293 P$(18)="AWAY FROM THE CENTRE TRACK, WITH A CHOICE OF ROUTES."
294 P$(19)="IN A SHARPLY TWISTING CORRIDOR.":P$(20)="ON A LONG EAST-WEST TRACK INTO THE DEPTHS."
296 P$(21)="FORCED TO A HALT BY A LARGE UNDERGROUND TREE."
297 P$(22)="HEADING DOWN A TWISTY PATH INTO AN ANIMAL'S LAIR."
298 P$(22)="ON THE TWISTY PATH YOU SEE PAW PRINTS THAT INDICATE SOMETHING BIG LIVES HERE!"
299 P$(23)="SURROUNDED BY ROCK IN A MIX OF CORRIDORS."
300 P$(24)="WALKING ALONG AN OLD TUNNEL.":P$(25)="FORCED TO TURN SHARPLY."
302 P$(26)="WALKING ALONG A LARGE CORRIDOR.":P$(27)="FACE TO FACE WITH A BEAR!"
304 P$(28)="AT A T-JUNCTION BEHIND THE BEAR'S DEN.":P$(29)="AT A DEAD END."
306 P$(30)="NEAR THE HEART OF THE BEAR'S DEN.":P$(31)="IN A BEAR'S DEN."
308 P$(32)="HEADING DOWN AN OFF-SHOOT FROM THE MAIN MINE."
309 P$(33)="FACED WITH A CHOICE OF ROUTES SHORED UP BY TIMBERS."
311 P$(34)="HEADING DOWN A LONG, LOW CORRIDOR.":P$(35)="FACED WITH A DROP TOO DEEP TO JUMP OR CLIMB."
314 P$(36)="AT THE FOOT OF THE DROP.":P$(37)="IN ANOTHER TWISTY PART OF THE CAVES."
315 P$(38)="IN A TWISTY PART OF THE CAVES.":P$(39)="ON A LOW EAST-WEST PATH."
317 P$(40)="IN A DEAD END.":P$(41)="TO THE WEST OF THE CAVES."
320 P$(42)="CONFRONTED BY AN ANGRY PANTHER.":P$(43)="AT AN MEETING OF PATHS.":P$(44)="NEAR A GREAT INCLINE."
323 P$(45)="AT THE FOOT OF A SLOPE TOO STEEP TO JUMP OR CLIMB.":P$(46)="IN A DEAD END."
325 P$(47)="AT THE TOP OF THE SLOPE.":P$(48)="NEAR A PART OF THE CAVES, REPUTEDLY HAUNTED."
327 P$(49)="NEAR A MAGIC PART OF THE CAVES.":P$(50)="CONFRONTED BY THE GHOSTLY SPIRIT OF THE CAVES!"
330 P$(51)="ON AN OFF-SHOOT FROM THE MAIN PATH.":P$(52)="ON AN OLD NORTH-SOUTH PATH."
332 P$(53)="IN FRONT OF A VERY NARROW CRACK.":P$(54)="NEAR THE MAGICAL CAVERNS.":P$(55)="IN A WIDE, OPEN TUNNEL."
336 P$(56)="IN A DEAD END.":P$(57)="IN A DEAD END.":P$(58)="ON A WELL-TROD EAST-WEST PATH."
338 P$(59)="AT A TURNING POINT.":P$(60)="FACED WITH A DOOR MARKED WITH THE WORDS 'BEGONE STRANGER'"
341 P$(61)="IN A MAZE OF TWISTY PATHS":P$(62)="IN A MAZE OF TWISTY PATHS":P$(63)="IN A MAZE OF TWISTY PATHS"
344 P$(64)="IN A MAZE OF TWISTY PATHS":P$(65)="IN A MAZE OF TWISTY PATHS":P$(66)="WALKING ALONG A ROUGH PATH."
348 P$(67)="IN A LOW, DAMP CORRIDOR.":P$(68)="IN A LOW, DAMP CORRIDOR."
350 P$(69)="STOPPED BY A WALL OF MIST THAT OBSCURES ALL LIGHT.":P$(70)="ON THE WEST SIDE OF THE MIST."
352 P$(71)="HEADING ALONG A PATH CUT FROM ROCK.":P$(72)="IN A SHARPLY TWISTING TUNNEL."
354 P$(73)="IN INSECT COUNTRY!":P$(74)="FACE TO FACE WITH A GIANT FLY THAT BLOCKS YOUR PATH."
357 P$(75)="ON A LOW EAST-WEST PATH THAT IS FREE OF INSECTS!":P$(76)="IN A DEAD END."
360 P$(77)="STILL HEADING NORTH-SOUTH":P$(78)="AT THE BOTTOM OF A LOW NORTH-SOUTH PASSAGE."
362 P$(79)="PREVENTED FROM GOING ANY FURTHER BY A SEIZED-UP MINING TRACK.":P$(80)="IN A ROOM THAT'S FULL OF OLD COBWEBS."
364 P$(81)="ON THE WEST SIDE OF THE TRACK.":P$(82)="IN THE SALVAGE ROOM.":P$(83)="IN A DARK PASSAGEWAY."
366 P$(85)="IN THE SPIDERS' TOMB!":P$(84)="THE LARGE SPIDER HERE WON'T LET YOU PASS."
368 P$(86)="IN A DARK PASSAGEWAY.":P$(87)="IN A DEAD END.":P$(88)="IN THE HEART OF THE MAGICAL CAVERNS."
373 P$(89)="ON A NORTHERN OFF-SHOOT FROM THE MAIN PATH.":P$(90)="WALKING ALONG A MAGICAL PATH."
375 P$(91)="IN A DIMLY-LIT CORRIDOR.":P$(92)="NEAR THE SOURCE OF THE MAGIC."
377 P$(93)="HALTED BY A SHIMMERING CURTAIN.":P$(94)="IN A DEAD END."
380 P$(95)="ON THE NORTH SIDE OF THE CURTAIN.":P$(96)="IN A LOW CORRIDOR."
382 P$(97)="TREADING OVER DIMLY LIT ROCKS AND RUBBLE.":P$(98)="IN A DEAD END. THE WALL IS BRICKED UP."
384 P$(99)="ON A NORTHERN OFF-SHOOT FROM THE MAIN PATH.":P$(100)="IN AN OLD WAREHOUSE ONCE USED BY TOOLMAKERS."
386 PRINT"LOAD UNDRDATA":CLOAD*P,"UNDRDATA"
387 DATA15,20,21,34,24,0,40,0,27,7,42,46,0,14,93,67,79,48,98,69
388 O$(1)="A VAST CHASM":O$(2)="AN IRON STAFF":O$(3)="A VAST TREE":O$(4)="A STOUT AXE":O$(5)="A THICK COIL OF ROPE"
389 O$(6)="AN ENCHANTED BRIDGE":O$(7)="SOME DYNAMITE":O$(8)="A PILE OF RUBBLE":O$(9)="A GOLDEN BEAR"
390 O$(10)="A BUN":O$(11)="A LARGE BLACK PANTHER":O$(12)="A LONG WOODEN PLANK":O$(13)="A TALL LADDER":O$(14)="SOME NAILS"
391 O$(15)="A SHIMMERING CURTAIN":O$(16)="A POLISHED MIRROR":O$(17)="A BLOCKED TRACK"
392 O$(18)="A POOL OF OIL":O$(19)="AN EMPTY BOTTLE":O$(20)="A SOLID WALL OF MIST"
393 FORI=1TO20:READO(I):NEXT
394 DATA50,84,74,60,76,87,3,53,63,31,73,0,0,100,0,3,1,0,0,39,0,0,0,0,0
395 DATATHE GHOSTLY DENIZEN OF THE CAVES,A HUGE SPIDER,A MASSIVE FLY,AN OLD DOOR,A LUMP OF MORTAR,A FLY SPRAY
397 DATAA SOLID GATE,A NARROW CRACK,A SHINING STONE,A TRUSTY SWORD,WHISKY
398 DATAA LIVING GARGOYLE,AN EVIL KNIFE,A KEY,A WALL,MATCHES,AN OLD TORCH
399 DATAA BLAZING TORCH,A GLOWING LIGHT,AN OLD PARCHMENT,PROGRAM,A PILE OF BROKEN GLASS
400 DATAA BOTTLE OF OIL,A BOTTLE OF WHISKY,SOME NICELY SAWN TIMBER
401 FORI=29TOLO:READO(I):NEXT:FORI=29TOLO:READO$(I):NEXT
403 DATACHA,STA,TRE,AXE,ROP,BRI,DYN,RUB,BEA,BUN,PAN,PLA,LAD,NAI,CUR,MIR,TRA,OIL,BOT,MIS,NOR,SOU,EAS,WES,N,S,E,W,GHO,SPI,FLY
405 DATADOO,MOR,SPR,GAT,CRA,STO,SWO,WHI,GAR,KNI,KEY,WAL,MAT,TOR,TOR,LIG,PAR,PRO,GLA,BOT,BOT,TIM,GO,GET,LOO,INV,SCO,DRO
406 DATAHEL,QUI,CRO,TAK,OPE,CLO,EAT,FEE,DRI,OFF,WAV,CUT,CHO,CLI,LIG,ATT,KIL,HIT,MAK,REF,OIL,STA,SPR,THR,RUB,REA,EXA,JUM
408 DATABRE,PUS,SAV,LOA
409 FORI=1TONN:READNO$(I):NEXT:FORI=1TONV:READV$(I):NEXT
411 DATANORTH,SOUTH,EAST,WEST
412 FORI=0TO3:READDD$(I):NEXT:RETURN
414 PRINT:M$="AND NOW YOU CAN LEAVE THESE NASTY OLD CAVES. CONGRATULATIONS!":GOSUB1:END
415 CLS:PRINT"INSERT TAPE AND HIT 'SPACE'"
416 PP$=INKEY$:IFPP$=""THEN416
417 PRINT"OK.":STOP
418 GOTO82
419 CLS:PRINT"INSERT TAPE AND HIT 'SPACE'"
420 PP$=INKEY$:IFPP$=""THEN420
421 PRINT"OK.":STOP
422 GOTO9
423 CLS:IFO(46)<>-1AND(CP>4ANDCP<100)THENPRINT"IT IS NOW PITCH DARK.  CARRY ON AND YOU'LL FALL INTO A PIT!":PD=1:RETURN
425 M$="YOU'RE "+P$(CP):GOSUB1:PD=0:IFCP=42ANDTB=1ANDP(42,1)=0THEN469
427 PRINT:PRINT"YOU CAN SEE:":FL=0:FORI=1TOLO:IFO(I)=CPTHENPRINTO$(I):FL=1
429 NEXTI:IFFL=0THENPRINT"NOTHING SPECIAL"
430 IFCP=3ANDGF=0THENPRINT:PRINT"THE GATE IS NOW SHUT."
431 FL=0:PRINT:PRINT"YOU CAN GO:":FORI=0TO3:IFP(CP,I)<>0THENPRINTDD$(I);" ";:FL=1
433 NEXT:IFFL=0THENPRINT"NOWHERE!"
434 PRINT:IFNP=1THEN453
435 IF(CP>20ANDCP<88)AND(INT(RND(10))>9)THENNP=1:GOTO453
436 IFCP<>69THENRETURN
437 IFP(69,3)=70THENRETURN
438 IFO(15)<>-1THENPRINT"YOU CAN'T PASS YET.":RETURN
439 M$="THE SHIMMERING CURTAIN WASHES AWAY THE MIST AND REVEALS A NEW TUNNEL!":GOSUB1
440 P(69,3)=70:O(15)=0:Z=Z-1:P$(69)="WALKING PAST AN ICY COLD SPOT.":O(20)=0:RETURN
442 CLS:M$="OH DEAR! THE GATE TO THE CAVE SLAMS SHUT! YOU'LL HAVE TO FIND THE KEY NOW BEFORE YOU CAN GET OUT!":GOSUB1
444 M$="DON'T WORRY. IT'S IN HERE SOMEWHERE.":GOSUB1:P(3,0)=0:GF=0:GOTO7
447 IFN=45ANDO(46)=-1THENN=46:RETURN
448 IFN=19ANDO(51)=-1THENN=51:RETURN
449 IFN=19ANDO(52)=-1THENN=52:RETURN
450 IFN=18ANDO(51)=-1THENN=51:RETURN
451 IFN=39ANDO(52)=-1THENN=52:RETURN
452 RETURN
453 M$="THERE'S A HOSTILE GARGOYLE WATCHING YOU FROM THE SHADOWS!":GOSUB1
454 IFINT(RND(100))>99THEN463
455 M$="HE THROWS A KNIFE AT YOU!":GOSUB1:O(40)=CP
456 IFINT(RND(100))>99THENPRINT"HE'S KILLED YOU!":GOTO81
457 PRINT"IT MISSED!":RETURN
458 IFINT(RND(10))>1THENPRINT"YOU KILLED HIM!":O(40)=0:GOTO462
459 PRINT"YOU MISSED HIM!":O(40)=CP
460 O(4)=CP:Z=Z-1:GOSUB8:NEXT:GOTO9
462 NP=0:GOTO460
463 PRINT"HE APPEARS FROM THE SHADOWS AND STEALS:":GS=0
464 C=2:GOSUB467:C=7:GOSUB467:C=14:GOSUB467:C=16:GOSUB467:C=19:GOSUB467:C=33:GOSUB467:C=34:GOSUB467
465 C=38:GOSUB467:C=44:GOSUB467:IFGS=0THENPRINT"NOTHING! YOU WERE LUCKY!"
466 RETURN
467 IFO(C)=-1THENO(C)=63:PRINTO$(C):GS=1
468 RETURN
469 PRINT"THE PANTHER FLEES AT THE SIGHT  OF THE BEAR!":P(42,1)=43:O(11)=0
470 P$(42)="WALKING PAST THE SCENT OFOLD PANTHER!":GOTO427
471 OC=0:FORI=1TOLO:IFO(I)=-1THENOC=OC+1
472 NEXT
473 IFOC>1THENPRINT"SOMETHING WON'T FIT THROUGH.":GOTO7
474 IFO(37)<>-1THENPRINT"SORRY, I CAN'T FIT THROUGH.":GOTO7
475 CP=100:M$="THE STONE GLOWS AND LETS YOU THROUGH.":GOSUB1:GOTO7
