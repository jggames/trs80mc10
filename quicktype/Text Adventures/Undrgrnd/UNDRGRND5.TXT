0 CLEAR150:CLS:DF=1:GF=1:GOSUB272:C=1:GOTO9
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 M$="IT'S ALREADY CLOSED.":GOTO6
4 PRINT"OK.":GOTO12
5 M$="THERE'S NO-ONE HERE WHO WANTS IT."
6 GOSUB1:GOTO12
7 M$="THERE'S NO POINT CLIMBING HERE.":GOTO6
8 FORI=1TO2500:NEXT:RETURN
9 GOSUB423
10 IFTB<>0THENO(9)=C
11 IFTB=1THENM$="A TAME BEAR IS FOLLOWING YOU!":GOSUB1
12 GOSUB45
13 IFV=34THEN260
14 IFV>9ANDN$=""THENM$="YOU NEED NOUN.":GOTO6
15 IFN$<>""ANDV=1ANDN=0THENM$="THAT DOESN'T MAKE SENSE TO ME.":GOTO6
16 ONVGOTO20,29,9,57,62,64,87,249,91,29,95,104,111,116,120,125,140,149,149,160,172,185
18 ONV-22GOTO189,192,195,200,206,212,220,227,236,240,254,260,262,266,415,419
20 IFN$<>""ANDN=0THENM$="I DON'T KNOW THAT WORD.":GOTO6
21 IFN>28ORN<21THENM$="I DON'T UNDERSTAND.":GOTO6
22 IFN>24THENN=N-4
23 N=N-21
24 IFN<>0ANDC=3ANDGF=1THEN442
25 IFN<>0ANDPD=1THENPRINT"YOU'VE FALLEN IN A PIT!":GOTO81
26 IFP(C,N)=0THENM$="YOU CAN'T GO THAT WAY.":GOTO6
27 IFC=53ANDN=1THEN471
28 C=P(C,N):GOTO9
29 IFN=0THEN254
30 GOSUB447:IFO(N)=-1THENM$="YOU'VE ALREADY GOT IT!":GOTO6
32 IFO(N)<>CTHENM$="I CAN'T SEE IT HERE.":GOTO6
33 IF(N=18ORN=39)ANDO(19)<>-1THENM$="YOU HAVE NO CONTAINER.":GOTO6
35 IFN=39ANDO(19)=-1THENO(39)=0:N=52:O(19)=0:Z=Z-1:GOTO44
36 IFN=18ANDO(19)=-1THENO(18)=0:O(19)=0:N=51:Z=Z-1:GOTO44
37 IFN=1ORN=3ORN=6ORN=9ORN=11ORN=17THENM$="DON'T BE SILLY!":GOTO6
38 IFN=20ORN=29ORN=30ORN=31ORN=32ORN=35ORN=36THENM$="I CAN'T DO THAT!":GOTO6
39 IFN=40ORN=41ORN=43ORN=49THENM$="IT CAN'T BE DONE.":GOTO6
40 IFN=8ORN=50THENM$="THERE'S NO POINT!":GOTO6
41 IFZ>4THENM$="YOU'RE CARRYING TOO MUCH!":GOTO6
42 IFN=12ANDC=10THENP(10,3)=0
43 IFN=15ANDSC=0THENM$="YOU CAN'T GET IT YET.":GOTO6
44 Z=Z+1:O(N)=-1:GOTO4
45 PRINT:INPUT"WHAT NOW";CM$:PRINT
46 N$="":V$="":V=0:N=0
47 C1=LEN(CM$):FORI=1TOC1:IFMID$(CM$,I,1)<>" "THENV$=V$+MID$(CM$,I,1):NEXT
48 V1$=V$:V$=LEFT$(V1$,3):POKE174,INT(VV/256):POKE173,VV-256*PEEK(174):FORI=1TONV:READM$:IFM$=V$THENV=I:GOTO51
49 NEXT:V=1:N$=V$:GOTO53
51 IFLEN(V1$)+1>=LEN(CM$)THENN=0:RETURN
52 N$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
53 N1$=N$:N$=LEFT$(N1$,3):POKE174,INT(VN/256):POKE173,VN-256*PEEK(174):FORI=1TONN:READM$:IFN$=M$THENN=I:RETURN
54 NEXTI:N=0:RETURN
57 PRINT"YOU ARE CARRYING:":GS=0:Z=0:FORI=1TOLO:IFO(I)=-1THENPRINTO$(I):GS=GS+1:Z=Z+1
59 NEXT:IFGS=0THENPRINT"NOTHING."
60 GOTO12
61 O(N)=0:M$="SMASH!":O(50)=C:Z=Z-1:GOTO6
62 M$="NO POINTS... JUST ESCAPE!":GOTO6
64 IFN=0THEN254
65 GOSUB447:IFO(N)<>-1THENM$="YOU HAVEN'T GOT IT!":GOTO6
67 IFN=19ORN=51ORN=52THEN61
70 IFN=16THENM$="IT VANISHES IN A SPRAY OF SHATTERED GLASS!":GOSUB1
71 IFN=16THENO(16)=0:Z=Z-1:GOTO12
72 IFN=46THENO(N)=0:O(45)=C:Z=Z-1:GOTO4
73 IFN<>12THEN77
74 IFC<>10THEN77
75 M$="BRILLIANT! NOW YOU CAN CROSS.":O(12)=C:P(10,3)=14:Z=Z-1:GOTO6
77 Z=Z-1:O(N)=C:IFTB=1ANDN<>13THENM$="THE BEAR RUNS AWAY!":TB=0:O(9)=RND(41):Z=Z-1:GOTO6
78 GOTO4
81 PRINT"YOU'RE DEAD!":END
87 M$="YOU WON'T GET MUCH HELP FROM ME IN THIS GAME.":GOTO6
91 IFN=0THENM$="CROSS WHAT?":GOTO6
92 IF(C<>15)AND(C<>10)THENM$="THERE'S NOTHING HERE TO CROSS!":GOTO6
93 IFN<>1ANDN<>6ANDN<>12THENM$="MMM... WHAT A STRANGE IDEA!":GOTO6
94 M$="YOU'LL JUST HAVE TO TYPE IN A DIRECTION INSTEAD!":GOTO6
95 IFN=0THENM$="OPEN WHAT?":GOTO6
96 IFC<>60ANDC<>3THENM$="THERE'S NOTHING HERE TO OPEN.":GOTO6
97 IFC=60THEN101
98 IFGF=1THENM$="IT IS OPEN!":GOTO6
99 IFO(42)<>-1THENM$="BUT YOU HAVEN'T GOT THE KEY!":GOTO6
100 PRINT"THE GATE SWINGS SLOWLY OPEN.":GF=1:P(3,0)=2:GOTO414
101 IFDF=1THENM$="BUT IT ALREADY IS OPEN.":GOTO6
102 IFO(33)<>-1THENM$="BUT YOU'VE NOTHING STRONG ENOUGH TO OPEN IT WITH!":GOTO6
103 M$="YOU'LL JUST HAVE TO TRY SOME OTHER WAY!":GOTO6
104 IFN=0THENM$="CLOSE WHAT?":GOTO6
105 IFN<>32ANDN<>35THENM$="HUH?":GOTO6
106 IFC=3THEN109
107 IFDF=0THEN3
108 P(60,1)=0:DF=0:GOTO4
109 IFGF=0THEN3
110 M$="THE GATE IS MAGICAL & CANNOT BE CLOSED AGAIN!":GOTO6
111 IFN=0THEN254
112 GOSUB447:IFO(N)<>-1ANDO(N)<>C THENM$="I CAN'T SEE IT HERE.":GOTO6
114 IFN<>10THENM$="I DON'T THINK SO.":GOTO6
115 Z=Z-1:O(19)=0:M$="MMM-DELICIOUS!":GOTO6
116 IFN=0THENM$="I DON'T UNDERSTAND.":GOTO6
117 IFN<>9THENM$="IT ISN'T HUNGRY!":GOTO6
118 IFO(10)<>-1THENM$="YOU'VE NOTHING TO FEED IT!":GOTO6
119 GOTO136
120 IFN=0THEN254
121 GOSUB447:IFN<>51ANDN<>52THENM$="YOU MUST BE JOKING!":GOTO6
123 IFN=51THENO(51)=0:O(19)=-1:M$="URGGHH!":GOTO6
124 O(52)=0:O(19)=-1:M$="GLUG-GLUG... HIC!":GOTO6
125 IFN=0THENM$="OFFER WHAT?":GOTO6
126 GOSUB447:IFO(N)<>-1THENM$="YOU'VE DON'T HAVE IT!":GOTO6
128 IFN=10THEN135
129 IFN<>52THENM$="NO-ONE'S INTERESTED.":GOTO6
130 IFC<>50THEN5
131 M$="HE DOWNS IT IN ONE AND GRATEFULLY SHOWS YOU A NEW TUNNEL.":GOSUB1
132 O(52)=0:O(19)=-1:P(50,3)=55:O(29)=0:M$="AND THEN WOBBLES AWAY.":GOTO6
135 IFC<>27THEN5
136 M$="THE BEAR ACCEPTS THE BUN & STANDS ASIDE TO REVEAL A NEW PATH!":GOSUB1
137 P(27,0)=28:O(10)=0:Z=Z-1:TB=1:M$="IN GRATITUDE, HE ATTACHES HIMSELF TO YOU LIKE A LIMPET.":GOTO6
140 IFN=0THEN254
141 GOSUB447:IFO(N)<>-1ANDO(N)<>CTHENM$="YOU HAVEN'T GOT IT.":GOTO6
143 IFN<>2THENM$="WAVE, WAVE, WAVE, BUT NOTHING HAPPENS.":GOTO6
144 IFC<>15THENM$="NOTHING HAPPENS.":GOTO6
145 IFBR=1THENM$="DON'T PUSH YOUR LUCK!":GOTO6
146 O(6)=C:P(15,1)=17:BR=1:P(15,2)=16:M$="A CRYSTAL BRIDGE NOW SPANS THE CHASM.":GOTO6
149 IFN=0THEN254
150 GOSUB447:IFO(N)<>-1ANDO(N)<>CTHENM$="I CAN'T SEE IT HERE.":GOTO6
152 IFN<>3ANDN<>15ANDN<>5ANDN<>12ANDN<>32THENM$="NOTHING DOING!":GOTO6
153 IFO(4)<>-1THENM$="YOU'VE NOTHING TO CUT IT WITH.":GOTO6
154 IFN<>3ANDN<>12THENM$="YOUR AXE IS NOT STRONG ENOUGH.":GOTO6
155 IFN=3THEN158
156 O(12)=0:O(53)=-1:M$="THE PLANK IS NOW CUT, BUT YOU'LL NEED SOMETHING ELSE":GOTO6
158 P(21,2)=22:O$(3)="AN EX-TREE":M$="TIMBER! THE TREE CRASHES TO THE GROUND.":GOTO6
160 IFN<>3ANDN<>5ANDN<>13THENM$="I BEG YOUR PARDON?":GOTO6
161 IFN=3THENM$="WHAT? AND RUIN YOUR NICE NEW SUIT? SORRY.":GOTO6
162 IFN=5THEN168
163 IFO(13)<>CTHENM$="I CAN'T SEE IT.":GOTO6
164 IFC<>45ANDC<>47THEN7
165 IFTB<>0THENPRINT"THE LADDER SNAPS IN TWO!":O(13)=0:P(45,1)=0:GOSUB8:GOTO9
166 IFC=45THENO(13)=47:C=47:GOTO9
167 C=45:O(13)=45:GOTO9
168 IFO(5)<>CTHENM$="I DON'T SEE IT ANYWHERE.":GOTO6
169 IFC<>35ANDC<>36THEN7
170 IFC=35THENO(5)=36:C=36:GOTO9
171 O(5)=35:C=35:GOTO9
172 IFN=0THEN254
173 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="IT ISN'T HERE.":GOTO6
175 IFO(44)<>-1THENM$="YOU'VE NOTHING TO LIGHT IT WITH.":GOTO6
176 IFN<>45ANDN<>7THENM$="IT WON'T LIGHT.":GOTO6
177 IFN=7THEN180
178 IFO(46)=-1THENM$="IT'S ALREADY LIT!":GOTO6
179 O(46)=-1:O(45)=0:PD=0:GOTO4
180 SOUND1,10:IFO(7)=-1THENM$="YOU'VE JUST BLOWN YOURSELF UP!":GOSUB1:GOTO81
181 IFC<>4THENZ=Z-1:O(7)=0:M$="THE DUST CLEARS TO REVEAL THAT NOTHING'S CHANGED.":GOTO6
183 O(7)=0:Z=Z-1:P(4,3)=5:M$="THE WALL IS BLASTED TO SMITHEREENS!":GOTO6
185 IFN=0THENM$="ATTACK WHAT?":GOTO6
186 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="WHERE IS IT?":GOTO6
187 IFN<>9ANDN<>11ANDN<>29ANDN<>30ANDN<>31THENM$="WHAT A STRANGE IDEA!":GOTO6
188 M$="NOT ONE OF YOUR BETTER SUGGESTIONS!":GOTO6
189 IFN=0THENM$="KILL WHAT?":GOTO6
190 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="IT'S NOT HERE.":GOTO6
191 GOTO187
192 IFN=0THENM$="HIT WHAT?":GOTO6
193 IFO(N)<>CANDO(N)<>-1THENM$="IT ISN'T HERE.":GOTO6
194 M$="APART FROM A GRAZED HAND, NOTHING HAPPENS.":GOTO6
195 IFN=0THENM$="MAKE WHAT?":GOTO6
196 IFN<>13THENM$="I DON'T UNDERSTAND.":GOTO6
197 IFO(53)<>-1ORO(14)<>-1ORO(4)<>-1THENM$="YOU HAVEN'T GOT ALL THE RIGHT MATERIALS.":GOTO6
198 O(13)=-1:O(14)=0:O(53)=0:Z=Z-1:M$="YOU'VE GOT A BRAND NEW LADDER!":GOTO6
200 IFN=0THENM$="REFLECT WHAT?":GOTO6
201 IFN<>47THENM$="WHAT ARE YOU ON ABOUT?":GOTO6
202 IFC<>93THENM$="NOTHING HAPPENS.":GOTO6
203 IFSC=1THENM$="YOU'VE ALREADY DONE THAT.":GOTO6
204 P(93,0)=95:O(15)=C:SC=1:M$="THE LIGHT IS REFLECTED BACK AND THE CURTAIN FALLS ASIDE (COR).":GOTO6
206 IFN=0THENM$="OIL WHAT?":GOTO6
207 IFO(51)<>-1THENM$="YOU HAVEN'T GOT ANY OIL.":GOTO6
208 IFC<>79THENM$="NOTHING WORTH OILING AROUND HERE":GOTO6
209 IFN<>17THENO(51)=0:O(19)=-1:M$="YOU'VE JUST WASTED A LOT OF OIL.":GOTO6
210 O(51)=0:O(19)=-1:P(79,2)=80:P(79,3)=81:O(17)=0:M$="THE TRACK SLIDES AWAY TO REVEAL MORE TUNNELS!":GOTO6
212 IFN=0THEN254
213 GOSUB447:IFO(N)<>-1ANDO(N)<>CTHENM$="BUT IT ISN'T HERE!":GOTO6
215 IFO(38)<>-1THENM$="YOU'VE NOTHING TO STAB IT WITH.":GOTO6
216 IFN<>30THENM$="YOU'RE WASTING YOUR TIME.":GOTO6
217 P(84,2)=86:P(84,3)=85:O$(30)="A DEAD SPIDER!"
218 M$="THE SPIDER DIES AND REVEALS A NEW PASSAGEWAY (IT WAS VERY LARGE)!":GOTO6
220 IFN=0THEN254
221 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="I CAN'T SEE IT HERE.":GOTO6
223 IFO(34)<>-1THENM$="YOU'RE NOT CARRYING ANY SPRAY.":GOTO6
224 IFN<>31THENM$="COUGH-COUGH SPLUTTER-SPLUTTER!":GOTO6
225 P(74,3)=75:O$(31)="A DEAD FLY!":M$="THE FLY COUGHS AND COLLAPSES REVEALING A HIDDEN TUNNEL.":GOTO6
227 IFN=0THENM$="THROW WHAT?":GOTO6
228 IFN<>33ANDN<>4THEN65
229 IFN=4ANDNP=0THEN65
230 IFN=4ANDNP=1THEN458
231 IFO(33)<>-1THENM$="YOU'RE NOT CARRYING IT!":GOTO6
232 IFC<>60THEN:O(33)=0:Z=Z-1:M$="IT VANISHES IN A CLOUD OF DUST.":GOTO6
233 P(60,0)=61:P(60,3)=65:O(33)=0:Z=Z-1:M$="THE DOOR OPENS AND REVEALS A PASSAGE!":GOTO6
236 IFN=0THEN254
237 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="THERE'S NOTHING HERE TO RUB!":GOTO6
239 M$="INTERESTING, BUT UNREWARDING.":GOTO6
240 IFN=0THEN254
241 GOSUB447:IFN<>48THENM$="THERE'S NOTHING ON IT TO READ.":GOTO6
243 IFO(48)<>-1THENM$="YOU'RE NOT HOLDING IT.":GOTO6
244 M$="THERE'S MATERIALS HERE TO BUILD A LADDER. YOU'LL NEED NAILS, A PLANK, AN AXE AND PERHAPS OTHER THINGS!":GOSUB1
246 M$="THERE'S ALSO A TOUCH OF MAGIC IN THE AIR.":GOTO6
249 INPUT"SAVE YOUR PROGRESS";M$:IFM$="Y"THEN415
253 END
254 IFN=0THENM$="WHAT'S A "+N1$+"!":GOTO6
255 IFN=43ORN=1ORN=6THENM$="THERE'S NOTHING INTERESTING HERE":GOTO6
256 GOSUB447:IFO(N)<>-1ANDO(N)<>CTHENM$="I CAN'T SEE IT HERE.":GOTO6
258 IFN=2ORN=16ORN=33ORN=37ORN=38THENM$="THIS HAS USEFUL POWERS.":GOTO6
259 M$="IT'S NOTHING MORE THAN: "+O$(N):GOTO6
260 IFC=15ORC=35ORC=10THENPRINT"DOWN...":FORC1=25TO1:SOUNDC1,1:NEXT:GOTO81
261 M$="WHEEE!":GOTO6
262 IFN=0THENM$="BREAK WHAT?":GOTO6
263 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="I CAN'T! IT ISN'T HERE.":GOTO6
265 M$="YOU COULDN'T BREAK YOUR WAY OUT OF A PAPER BAG!":GOTO6
266 IFN=0THENM$="PUSH WHAT?":GOTO6
267 GOSUB447:IFO(N)<>CANDO(N)<>-1THENM$="I CAN'T! IT ISN'T HERE.":GOTO6
269 IFC<>79THENM$="YOU CAN'T!":GOTO6
270 M$="TRY DOING THIS ANOTHER WAY.":GOTO6
271 END
272 NV=38:NN=53:P=100:LO=53:DIMP(P,3),O$(LO),O(LO),N$(NN)
273 DATAON AN OLD TRACK HEADING TOWARDS THE CAVES,GETTING EVER NEARER THE CAVES
275 DATAAT THE ENTRANCE OF THE CAVES,IN FRONT OF A SOLID ROCK WALL,IN A SUBTERRANEAN TOMB WITH CREVICES ALL OVER
278 DATAWALKING AROUND THE SIDE OF THE CREVICE ROOM,SURROUNDED BY BRICKED UP WALLS
281 DATANEAR A GREAT CHASM WHICH PLUNGES HUNDREDS OF FEET,IN THE HEART OF THE CREVICE ROOM
284 DATAIN FRONT OF A GREAT CREVICE,ON THE SOUTHERN RIM OF THE CHASM IN A JUMBLE OF ROCKS
287 DATALOST IN CHASM COUNTRY,IN A ROOM FULL OF ROCKS,ON THE WEST SIDE OF THE CHASM--NOT MUCH OF A VIEW
290 DATAFACED WITH A WIDE CRACK IN THE FLOOR
291 DATAIN AN EAST SIDE CHAMBER,ON THE MAIN TRACK SURROUNDED CHAMBERS,AWAY FROM THE CENTRE TRACK
294 DATAIN A SHARPLY TWISTING CORRIDOR,ON A LONG EAST-WEST TRACK,FORCED TO A HALT BY A LARGE UNDERGROUND TREE
297 DATAHEADING DOWN A TWISTY PATH. PAW PRINTS THAT INDICATE SOMETHING BIG LIVES HERE
299 DATASURROUNDED BY A MIX OF CORRIDORS,WALKING ALONG AN OLD TUNNEL,FORCED TO TURN SHARPLY
302 DATAWALKING ALONG A LARGE CORRIDOR,FACE TO FACE WITH A BEAR,AT A T-JUNCTION BEHIND THE BEAR'S DEN,AT A DEAD END
306 DATANEAR THE HEART OF THE BEAR'S DEN,IN A BEAR'S DEN,HEADING DOWN AN OFF-SHOOT FROM THE MAIN MINE
309 DATAFACED WITH A CHOICE OF ROUTES SHORED UP BY TIMBERS
311 DATAHEADING DOWN A LONG CORRIDOR,FACED WITH A DROP,AT THE FOOT OF THE DROP,IN ANOTHER TWISTY PART OF THE CAVES
315 DATAIN A TWISTY PART OF THE CAVES,ON A LOW EAST-WEST PATH,IN A DEAD END,TO THE WEST OF THE CAVES
320 DATACONFRONTED BY AN ANGRY PANTHER,AT AN MEETING OF PATHS,NEAR A GREAT INCLINE
323 DATAAT THE FOOT OF A STEEP SLOPE,IN A DEAD END,AT THE TOP OF THE SLOPE,NEAR A REPUTEDLY HAUNTED PART OF THE CAVES
327 DATANEAR A MAGIC PART OF THE CAVES,CONFRONTED BY A GHOSTLY SPIRIT,ON AN OFF-SHOOT FROM THE MAIN PATH
328 DATAON AN OLD NORTH-SOUTH PATH,IN FRONT OF A VERY NARROW CRACK,NEAR THE MAGICAL CAVERNS,IN A WIDE TUNNEL
336 DATAIN A DEAD END,IN A DEAD END,ON A WELL-TROD EAST-WEST PATH
338 DATAAT A TURNING POINT,FACED WITH A DOOR WITH A SIGN 'BEGONE STRANGER'
341 DATAIN A MAZE OF TWISTY PATHS,IN A MAZE OF TWISTY PATHS,IN A MAZE OF TWISTY PATHS
344 DATAIN A MAZE OF TWISTY PATHS,IN A MAZE OF TWISTY PATHS,WALKING ALONG A ROUGH PATH
348 DATAIN A DAMP CORRIDOR,IN A DAMP CORRIDOR,STOPPED BY A WALL OF MIST THAT OBSCURES ALL LIGHT,ON THE WEST SIDE OF THE MIST
352 DATAON A PATH CUT FROM ROCK,IN A SHARPLY TWISTING TUNNEL,IN INSECT COUNTRY,FACE TO FACE WITH A GIANT FLY
357 DATAON A LOW EAST-WEST PATH FREE OF INSECTS,IN A DEAD END,STILL HEADING NORTH-SOUTH,AT THE BOTTOM OF A LOW NORTH-SOUTH PASSAGE
362 DATABLOCKED BY A SEIZED-UP MINING TRACK,IN A ROOM FULL OF COBWEBS,ON THE WEST SIDE OF THE TRACK
364 DATAIN THE SALVAGE ROOM,IN A DARK PASSAGEWAY,IN THE SPIDERS' LAIR,THE HUGE SPIDER HERE WON'T LET YOU PASS
368 DATAIN A DARK PASSAGEWAY,IN A DEAD END,IN THE HEART OF THE MAGICAL CAVERNS
373 DATAON A NORTHERN OFF-SHOOT FROM THE MAIN PATH,WALKING ALONG A MAGICAL PATH
375 DATAIN A DIMLY-LIT CORRIDOR,NEAR THE SOURCE OF THE MAGIC,HALTED BY A SHIMMERING CURTAIN,IN A DEAD END
380 DATAON THE NORTH SIDE OF THE CURTAIN,IN A LOW CORRIDOR,TREADING OVER ROCKS AND RUBBLE,FACING A BRICKED-UP WALL
384 DATAON A NORTHERN OFF-SHOOT FROM THE MAIN PATH,IN AN OLD WAREHOUSE ONCE USED BY TOOLMAKERS
386 PRINT"LOAD UNDRDATA":CLOAD*P,"UNDRDATA":FORI=1TOP:READM$:NEXT
387 DATA15,20,21,34,24,,40,,27,7,42,46,,14,93,67,79,48,98,69
388 O$(1)="A VAST CHASM":O$(2)="AN IRON STAFF":O$(3)="A VAST TREE":O$(4)="A STOUT AXE":O$(5)="A THICK COIL OF ROPE"
389 O$(6)="AN ENCHANTED BRIDGE":O$(7)="SOME DYNAMITE":O$(8)="A PILE OF RUBBLE":O$(9)="A GOLDEN BEAR"
390 O$(10)="A BUN":O$(11)="A LARGE BLACK PANTHER":O$(12)="A LONG WOODEN PLANK":O$(13)="A TALL LADDER":O$(14)="SOME NAILS"
391 O$(15)="A SHIMMERING CURTAIN":O$(16)="A POLISHED MIRROR":O$(17)="A BLOCKED TRACK"
392 O$(18)="A POOL OF OIL":O$(19)="AN EMPTY BOTTLE":O$(20)="A SOLID WALL OF MIST"
393 FORI=1TO20:READO(I):NEXT
394 DATA50,84,74,60,76,87,3,53,63,31,73,,,100,0,3,1,,,39,,,,,0
395 DATATHE GHOSTLY DENIZEN OF THE CAVES,A HUGE SPIDER,A MASSIVE FLY,AN OLD DOOR,A LUMP OF MORTAR,A FLY SPRAY
397 DATAA SOLID GATE,A NARROW CRACK,A SHINING STONE,A TRUSTY SWORD,WHISKY
398 DATAA LIVING GARGOYLE,AN EVIL KNIFE,A KEY,A WALL,MATCHES,AN OLD TORCH
399 DATAA BLAZING TORCH,A GLOWING LIGHT,AN OLD PARCHMENT,PROGRAM,A PILE OF BROKEN GLASS
400 DATAA BOTTLE OF OIL,A BOTTLE OF WHISKY,SOME NICELY SAWN TIMBER
401 FORI=29TOLO:READO(I):NEXT:FORI=29TOLO:READO$(I):NEXT
403 DATACHA,STA,TRE,AXE,ROP,BRI,DYN,RUB,BEA,BUN,PAN,PLA,LAD,NAI,CUR,MIR,TRA,OIL,BOT,MIS,NOR,SOU,EAS,WES,N,S,E,W,GHO,SPI,FLY
405 DATADOO,MOR,SPR,GAT,CRA,STO,SWO,WHI,GAR,KNI,KEY,WAL,MAT,TOR,TOR,LIG,PAR,PRO,GLA,BOT,BOT,TIM,GO,GET,LOO,INV,SCO,DRO
406 DATAHEL,QUI,CRO,TAK,OPE,CLO,EAT,FEE,DRI,OFF,WAV,CUT,CHO,CLI,LIG,ATT,KIL,HIT,MAK,REF,OIL,STA,SPR,THR,RUB,REA,EXA,JUM
408 DATABRE,PUS,SAV,LOA,NORTH,SOUTH,EAST,WEST
409 VN=PEEK(173)+256*PEEK(174):FORI=1TONN:READM$:NEXT
410 VV=PEEK(173)+256*PEEK(174):FORI=1TONV:READM$:NEXT:FORI=0TO3:READDD$(I):NEXT:RETURN
414 PRINT:M$="AND NOW YOU CAN LEAVE THESE NASTY OLD CAVES. CONGRATULATIONS!":GOSUB1:END
415 CLS:INPUT"HIT enter";M$:CSAVE*P,"UNDRGAME":END
419 CLS:CLOAD*P,"UNDRGAME":GOTO9
423 CLS:IFO(46)<>-1AND(C>4ANDC<100)THENPRINT"IT IS NOW PITCH DARK!":PD=1:RETURN
425 RESTORE:FORC1=1TOC:READM$:NEXT:GOSUB500:M$="YOU'RE "+M$+".":GOSUB1:PD=0:IFC=42ANDTB=1ANDP(42,1)=0THEN469
427 PRINT:PRINT"YOU CAN SEE:":FL=0:FORI=1TOLO:IFO(I)=CTHENPRINTO$(I):FL=1
429 NEXTI:IFFL=0THENPRINT"NOTHING SPECIAL"
430 IFC=3ANDGF=0THENPRINT:PRINT"THE GATE IS NOW SHUT."
431 FL=0:PRINT:PRINT"YOU CAN GO:":FORI=0TO3:IFP(C,I)<>0THENPRINTDD$(I)" ";:FL=1
433 NEXT:IFFL=0THENPRINT"NOWHERE!"
434 PRINT:IFNP=1THEN453
435 IFC>20ANDC<88ANDRND(9)=1THENNP=1:GOTO453
436 IFC<>69THENRETURN
437 IFP(69,3)=70THENRETURN
438 IFO(15)<>-1THENPRINT"YOU CAN'T PASS YET.":RETURN
439 M$="THE CURTAIN WASHES AWAY THE MIST AND REVEALS A NEW TUNNEL!":GOSUB1:P(69,3)=70:O(15)=0:Z=Z-1:O(20)=0:RETURN
442 CLS:M$="THE GATE TO THE CAVE SLAMS SHUT! YOU'LL HAVE TO FIND THE KEY TO GET OUT NOW!":P(3,0)=0:GF=0:GOTO6
447 IFN=45ANDO(46)=-1THENN=46:RETURN
448 IFN=19ANDO(51)=-1THENN=51:RETURN
449 IFN=19ANDO(52)=-1THENN=52:RETURN
450 IFN=18ANDO(51)=-1THENN=51:RETURN
451 IFN=39ANDO(52)=-1THENN=52:RETURN
452 RETURN
453 M$="THERE'S A HOSTILE GARGOYLE WATCHING YOU FROM THE SHADOWS!":GOSUB1
454 IFRND(99)=1THEN463
455 M$="HE THROWS A KNIFE AT YOU!":GOSUB1:O(40)=C
456 IFRND(99)=1THENPRINT"HE'S KILLED YOU!":GOTO81
457 PRINT"IT MISSED!":RETURN
458 IFRND(9)=1THENPRINT"YOU KILLED HIM!":O(40)=0:GOTO462
459 PRINT"YOU MISSED HIM!":O(40)=C
460 O(4)=C:Z=Z-1:GOSUB8:NEXT:GOTO9
462 NP=0:GOTO460
463 PRINT"HE APPEARS FROM THE SHADOWS AND STEALS:":GS=0
464 CC=2:GOSUB467:CC=7:GOSUB467:CC=14:GOSUB467:CC=16:GOSUB467:CC=19:GOSUB467:CC=33:GOSUB467:CC=34:GOSUB467
465 CC=38:GOSUB467:CC=44:GOSUB467:IFGS=0THENPRINT"NOTHING! YOU WERE LUCKY!"
466 RETURN
467 IFO(CC)=-1THENO(CC)=63:PRINTO$(C):GS=1
468 RETURN
469 PRINT"THE PANTHER FLEES AT THE SIGHT  OF THE BEAR!":P(42,1)=43:O(11)=0:GOTO427
471 OC=0:FORI=1TOLO:IFO(I)=-1THENOC=OC+1
472 NEXT
473 IFOC>1THENM$="SOMETHING WON'T FIT THROUGH.":GOTO6
474 IFO(37)<>-1THENM$="SORRY, I CAN'T FIT THROUGH.":GOTO6
475 C=100:M$="THE STONE GLOWS AND LETS YOU THROUGH.":GOTO6
500 IFC=10ANDP(C,3)=0THENM$="FACING A VAST CHASM"
501 IFC=10ANDP(C,3)=14THENM$="WALKING THE PLANK"
502 IFC=60ANDP(C,1)=0THENM$="FACING A CLOSED DOOR"
503 IFC=50ANDP(C,3)=55THENM$="PASSING OLD SPIRITS"
504 IFC=27ANDP(C,0)=28THENM$="SNIFFING THE SCENT OF OLD BEAR"
505 IFC=15ANDP(C,2)=16THENM$="CROSSING THE CHASM"
506 IFC=21ANDP(C,2)=22THENM$="PASSING A DEAD TREE"
507 IFC=4ANDP(C,3)=5THENM$="WALKING A DUSTY TRACK"
508 IFC=93ANDP(C,0)=95THENM$="BY A SHIMMERING LIGHT"
509 IFC=79ANDP(C,3)=81THENM$="PASSING A SMOOTH TRACK"
510 IFC=84ANDP(C,3)=85THENM$="BY A DEAD SPIDER"
511 IFC=74ANDP(C,3)=75THENM$="BY A DEAD FLY"
512 IFC=60ANDP(C,3)=65THENM$="BY THE DOOR"
513 IFC=69ANDP(C,3)=70THENM$="A VERY COLD SPOT"
514 IFC=42ANDP(C,1)=43THENM$="SNIFFING THE SCENT OF OLD PANTHER"
515 RETURN
