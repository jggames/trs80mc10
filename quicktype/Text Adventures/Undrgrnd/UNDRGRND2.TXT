2 DF=1:GF=1
5 CR$=CHR$(13)
10 GOSUB10000:GOSUB2000:CP=1
15 PD$="IT IS NOW PITCH DARK.  CARRY ON AND YOU'LL FALL INTO A PIT!"
20 IM$="YOU CAN'T GO THAT WAY."
25 GF$="THE GATE IS NOW SHUT."
26 D$="DOWN ... DOWN ... DOWN !!"
27 DF$="THE DOOR IS SHUT."
200 GOSUB5000
206 IFTB<>0THENOB(9)=CP
208 IFTB=1THENPRINT"THERE'S A TAME BEAR FOLLOWING   YOU!"
210 GOSUB390
220 IFVB=34 THEN1950
225 IFVB>9ANDNO$=""THENPRINT"YOU NEED A DIRECT OBJECT.":GOTO210
227 IFNO$<>""ANDVB=1ANDNO=0THENPRINT"THAT DOESN'T MAKE SENSE TO ME.":GOTO210
240 ONVB GOTO270,300,200,500,540,560,650,1890,690,300,780,880,900,950
250 ONVB-14GOTO1000,1050,1100,1150,1200,1250,1300,1350
260 ONVB-22GOTO1400,1450,1500,1550,1600,1650,1700,1750,1800,1850,1900,1950
262 ONVB-34GOTO1960,1970,3000,3200
270 IFNO$<>""ANDNO=0THENPRINT"I DON'T KNOW THAT WORD.":GOTO210
272 IFNO>28ORNO<21THENPRINT"I DON'T UNDERSTAND.":GOTO210
274 IFNO>24THENNO=NO-4
276 NO=NO-21
277 IFNO<>0ANDCP=3ANDGF=1THEN5200
278 IFNO<>0ANDPD=1THENPRINT"YOU'VE FALLEN INTO A PIT, YOU   CLUMSY OAF!":GOTO612
288 IFP(CP,NO)=0THENPRINTIM$:GOTO210
289 IFCP=53ANDNO=1THEN6300
290 CP=P(CP,NO):GOTO200
300 IFNO=0THEN1900
302 GOSUB5300
304 IFOB(NO)=-1THENPRINT"YOU'VE ALREADY GOT IT!":GOTO210
306 IFOB(NO)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO210
308 IFNO=18ANDOB(19)<>-1THENPRINT"YOU HAVE NO CONTAINER.":GOTO210
310 IFNO=39ANDOB(19)<>-1THENPRINT"YOU HAVE NO CONTAINER.":GOTO210
312 IFNO=39ANDOB(19)=-1THENOB(39)=0:NO=52:OB(19)=0:ZZ=ZZ-1:GOTO389
314 IFNO=18ANDOB(19)=-1THENOB(18)=0:OB(19)=0:NO=51:ZZ=ZZ-1:GOTO389
315 IFNO=1ORNO=3ORNO=6ORNO=9ORNO=11ORNO=17THENPRINT"DON'T BE SILLY!":GOTO210
316 IFNO=20ORNO=29ORNO=30ORNO=31ORNO=32ORNO=35ORNO=36THENPRINT"I CAN'T DO THAT!":GOTO210
318 IFNO=40ORNO=41ORNO=43ORNO=49THENPRINT"IT CAN'T BE DONE.":GOTO210
320 IFNO=8ORNO=50THENPRINT"THERE'S NO POINT,SO I WON'T!":GOTO210
322 IFZZ>4THENPRINT"YOU'RE CARRYING TOO MUCH!":GOTO210
324 IFNO=12ANDCP=10THENP(10,3)=0:P$(10)="FACED BY A VAST CHASM."
326 IFNO=15ANDSC=0THENPRINT"YOU CAN'T GET IT YET.":GOTO210
389 ZZ=ZZ+1:PRINT"OK.":OB(NO)=-1:GOTO210
390 PRINT:PRINT"WHAT NOW? ";:GOSUB60000:PRINT
392 NO$="":VB$="":VB=0:NO=0
394 LC=LEN(CM$):FORI=1TOLC:IFMID$(CM$,I,1)<>" "THENVB$=VB$+MID$(CM$,I,1):NEXT
396 V1$=VB$:VB$=LEFT$(V1$,3):FORI=1TONV:IFVB$(I)=VB$ THENVB=I:GOTO402
398 NEXT
400 VB=1:NO$=VB$:GOTO406
402 IFLEN(V1$)+1>=LEN(CM$)THENNO=0:RETURN
404 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
406 N1$=NO$:NO$=LEFT$(N1$,3):FORI=1TONN:IFNO$=NO$(I)THEN412
408 NEXTI
410 NO=0:RETURN
412 NO=I:RETURN
500 PRINT"YOU ARE CARRYING :-":GS=0:ZZ=0
502 FORI=1TOLO:IFOB(I)=-1THENPRINTOB$(I):GS=GS+1:ZZ=ZZ+1
504 NEXT
506 IFGS=0THENPRINT"NOTHING."
508 GOTO210
540 PRINT"THERE ARE NO POINTS TO BE SCOREDIN THIS GAME.  YOU'VE JUST GOT  TO ESCAPE!"
542 GOTO210
560 IFNO=0THEN1900
562 GOSUB5300
564 IFOB(NO)<>-1THENPRINT"YOU HAVEN'T GOT IT!":GOTO210
566 IFNO=19THENPRINT"SMASH!":OB(19)=0:OB(50)=CP:ZZ=ZZ-1:GOTO210
568 IFNO=51THENPRINT"SMASH!":OB(51)=0:OB(50)=CP:ZZ=ZZ-1:GOTO210
570 IFNO=52THENPRINT"SMASH!":OB(52)=0:OB(50)=CP:ZZ=ZZ-1:GOTO210
572 IFNO=16THENPRINT"OH DEAR!  IT VANISHES IN A      SPARKLE OF SHATTERED GLASS!!"
573 IFNO=16THENOB(16)=0:ZZ=ZZ-1:GOTO210
574 IFNO=46THENOB(NO)=0:OB(45)=CP:ZZ=ZZ-1:PRINT"OK.":GOTO210
575 IFNO<>12THEN580
576 IFCP<>10THEN580
578 PRINT"BRILLIANT! NOW YOU CAN WALK     ACROSS THE PLANK."
579 OB(12)=CP:P(10,3)=14:P$(10)="WALKING ACROSS THE PLANK.":ZZ=ZZ-1:GOTO210
580 PRINT"OK.":ZZ=ZZ-1:OB(NO)=CP:IFTB=1ANDNO<>13THEN584
582 GOTO210
584 PRINT"THE BEAR GLARES AT YOU AND RUNS AWAY!":TB=0:OB(9)=INT(RND(41))
586 ZZ=ZZ-1:GOTO210
612 PRINT"YOU'RE DEAD!!"
614 PRINT"DO YOU WANT TO PLAY AGAIN       (Y OR N)?"
616 PA$=INKEY$:IFPA$=""THEN616
617 IFPA$="Y"THENRUN
618 IFPA$="N"THENPRINT"OKAY, BYE.":END
620 GOTO616
650 PRINT"I'M AFRAID THAT YOU WON'T GET   MUCH HELP FROM ME IN THIS GAME."
652 PRINT:PRINT"SO JUST KEEP ON TRYING THINGS!":PRINT"IF NOTHING'S HAPPENING, TRY     USING DIFFERENT WORDS INSTEAD."
654 PRINT"ABOVE ALL, DON'T DESPAIR!"
656 GOTO210
690 IFNO=0THENPRINT"CROSS WHAT?":GOTO210
692 IF(CP<>15)AND(CP<>10)THENPRINT"THERE'S NOTHING HERE TO CROSS!":GOTO210
694 IFNO<>1ANDNO<>6ANDNO<>12THENPRINT"MMM... WHAT A STRANGE IDEA!":GOTO210
696 PRINT"YOU'LL JUST HAVE TO TYPE IN A   DIRECTION INSTEAD!":GOTO210
780 IFNO=0THENPRINT"OPEN WHAT?":GOTO210
782 IFCP<>60ANDCP<>3THENPRINT"THERE'S NOTHING HERE TO OPEN.":GOTO210
784 IFCP=60THEN790
786 IFGF=1THENPRINT"BUT IT IS OPEN!":GOTO210
788 IFOB(42)<>-1THENPRINT"BUT YOU HAVEN'T GOT THE KEY!":GOTO210
789 PRINT"THE GATE SWINGS SLOWLY OPEN.":GF=1:P(3,0)=2:GOTO2510
790 IFDF=1THENPRINT"BUT IT ALREADY IS OPEN.":GOTO210
792 IFOB(33)<>-1THENPRINT"BUT YOU'VE NOTHING STRONG ENOUGHTO OPEN IT WITH!":GOTO210
794 PRINT"YOU'LL JUST HAVE TO TRY AND DO  THIS SOME OTHER WAY!":GOTO210
880 IFNO=0THENPRINT"CLOSE WHAT?":GOTO210
881 IFNO<>32ANDNO<>35THENPRINT"HUH?":GOTO210
882 IFCP=3THEN890
884 IFDF=0THENPRINT"IT'S ALREADY CLOSED.":GOTO210
886 PRINT"OK.":P(60,1)=0:DF=0:P$(60)="FACED WITH A CLOSED DOOR.":GOTO210
890 IFGF=0THENPRINT"IT'S ALREADY CLOSED.":GOTO210
892 PRINT"THE GATE IS A MAGICAL ONE, AND  ONCE OPENED, CANNOT BE CLOSED BYTHE LIKES OF YOU!":GOTO210
900 IFNO=0THEN1900
902 GOSUB5300
904 IFOB(NO)<>-1ANDOB(NO)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO210
906 IFNO<>10THENPRINT"I DON'T THINK SO SOMEHOW.":GOTO210
908 PRINT"MMM-MMM! DELICIOUS!":ZZ=ZZ-1:OB(19)=0:GOTO210
950 IFNO=0THENPRINT"I DON'T UNDERSTAND.":GOTO210
952 IFNO<>9THENPRINT"IT ISN'T HUNGRY!":GOTO210
954 IFOB(10)<>-1THENPRINT"YOU'VE NOTHING TO FEED IT WITH!":GOTO210
956 GOTO1072
1000 IFNO=0THEN1900
1002 GOSUB5300
1004 IFNO<>51ANDNO<>52THENPRINT"YOU MUST BE JOKING!":GOTO210
1006 IFNO=51THENPRINT"URGGHH!!":OB(51)=0:OB(19)=-1:GOTO210
1008 PRINT"GLUG-GLUG-GLUG ... HIC!!":OB(52)=0:OB(19)=-1:GOTO210
1050 IFNO=0THENPRINT"OFFER WHAT?":GOTO210
1052 GOSUB5300
1054 IFOB(NO)<>-1THENPRINT"YOU'VE GOT TO HAVE IT BEFORE YOUCAN OFFER IT!":GOTO210
1056 IFNO=10THEN1070
1058 IFNO<>52THENPRINT"NO-ONE'S INTERESTED.":GOTO210
1060 IFCP<>50THENPRINT"THERE'S NO-ONE HERE WHO WANTS IT(EXCEPT YOU, PERHAPS).":GOTO210
1062 PRINT"THE DENIZEN OF THE CAVES DOWNS  IT IN ONE DRAUGHT,AND GRATEFULLYSHOWS YOU A NEW TUNNEL."
1064 PRINT"HE THEN WOBBLES SLOWLY AWAY TO  SLEEP OFF HIS HANGOVER."
1066 OB(52)=0:OB(19)=-1:P(50,3)=55:P$(50)="WALKING PAST OLD SPIRITS."
1067 OB(29)=0:GOTO210
1070 IFCP<>27THENPRINT"THERE'S NO ONE HERE WHO WANTS   IT.":GOTO210
1072 PRINT"THE BEAR GRATEFULLY ACCEPTS THE BUN, AND STANDS ASIDE TO REVEAL A NEW PATH!"
1074 PRINT"IN A SHOW OF GRATITUDE, HE      ATTACHES HIMSELF TO YOU LIKE A  LIMPET."
1075 P(27,0)=28:P$(27)="WALKING PAST THE SCENT OFOLD BEAR."
1076 OB(10)=0:ZZ=ZZ-1:TB=1:GOTO210
1100 IFNO=0THEN1900
1102 GOSUB5300
1104 IFOB(NO)<>-1ANDOB(NO)<>CP THENPRINT"YOU HAVEN'T GOT IT.":GOTO210
1106 IFNO<>2THENPRINT"WAVE,WAVE,WAVE, BUT NOTHING     HAPPENS.":GOTO210
1108 IFCP<>15THENPRINT"NOTHING HAPPENS.":GOTO210
1110 IFBR=1THENPRINT"DON'T CHANCE YOUR LUCK!":GOTO210
1112 PRINT"A CRYSTAL BRIDGE NOW SPANS THE  CHASM (AREN'T YOU THE LUCKY ONE)":OB(6)=CP:P(15,1)=17:BR=1
1114 P(15,2)=16:P$(15)="WALKING ACROSS THE CHASM.":GOTO210
1150 GOTO1200
1200 IFNO=0THEN1900
1202 GOSUB5300
1204 IFOB(NO)<>-1ANDOB(NO)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO210
1205 IFNO<>3ANDNO<>15ANDNO<>5ANDNO<>12ANDNO<>32THENPRINT"NOTHING DOING!":GOTO210
1206 IFOB(4)<>-1THENPRINT"YOU'VE NOTHING TO CUT IT WITH.":GOTO210
1208 IFNO<>3ANDNO<>12THENPRINT"YOUR AXE IS NOT STRONG ENOUGH.":GOTO210
1210 IFNO=3THEN1220
1212 PRINT"THE PLANK IS NOW NICELY CUT, BUTYOU'LL NEED SOMETHING ELSE      BEFORE YOU CAN FINISH THE LADDER"
1214 OB(12)=0:OB(53)=-1:GOTO210
1220 PRINT"TIMBERRRRR! THE TREE CRASHES TO THE GROUND."
1222 P(21,2)=22:P$(21)="WALKING PAST A DEAD TREE.":OB$(3)="AN EX-TREE":GOTO210
1250 IFNO<>3ANDNO<>5ANDNO<>13THENPRINT"I BEG YOUR PARDON?!":GOTO210
1252 IFNO=3THENPRINT"WHAT? AND RUIN YOUR NICE NEW    SUIT?  SORRY.":GOTO210
1254 IFNO=5THEN1266
1256 IFOB(13)<>CP THENPRINT"I CAN'T SEE IT ON THE GROUND    ANYWHERE.":GOTO210
1257 IFCP<>45ANDCP<>47THENPRINT"THERE'S NO POINT CLIMBING IT    HERE.":GOTO210
1258 IFTB<>0THENPRINT"THE LADDER SNAPS IN TWO!!":OB(13)=0:P(45,1)=0:FORI=1TO2000:NEXT:GOTO200
1259 IFCP=45THENOB(13)=47:CP=47:GOTO200
1260 CP=45:OB(13)=45:GOTO200
1266 IFOB(5)<>CP THENPRINT"I DON'T SEE IT ON THE GROUND    ANYWHERE.":GOTO210
1267 IFCP<>35ANDCP<>36THENPRINT"THERE'S NO POINT CLIMBING THE   ROPE HERE.":GOTO210
1268 IFCP=35THENOB(5)=36:CP=36:GOTO200
1270 OB(5)=35:CP=35:GOTO200
1300 IFNO=0THEN1900
1302 GOSUB5300
1304 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"IT ISN'T HERE.":GOTO210
1306 IFOB(44)<>-1THENPRINT"YOU'VE NOTHING TO LIGHT IT WITH.":GOTO210
1308 IFNO<>45ANDNO<>7THENPRINT"IT WON'T LIGHT.":GOTO210
1310 IFNO=7THEN1320
1312 IFOB(46)=-1THENPRINT"IT'S ALREADY LIT!":GOTO210
1314 PRINT"OK.":OB(46)=-1:OB(45)=0:PD=0:GOTO210
1320 IFOB(7)=-1THENPRINT"KABOOOM!! YOU'VE JUST BLOWN     YOURSELF UP (STUPID)!":GOTO612
1322 IFCP<>4THENPRINT"KABOOOM!! THE DUST CLEARS TO    REVEAL THAT NOTHING'S CHANGED.":OB(7)=0
1323 IFCP<>4THENZZ=ZZ-1:GOTO210
1324 PRINT"BOOOOOMMMMM!! THE WALL IS       BLASTED TO SMITHEREENS!"
1326 OB(7)=0:ZZ=ZZ-1:P(4,3)=5:P$(4)="WALKING ALONG A DUSTY    TRACK.":GOTO210
1350 IFNO=0THENPRINT"ATTACK WHAT?":GOTO210
1351 GOSUB5300:IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"WHERE IS IT?":GOTO210
1352 IFNO<>9ANDNO<>11ANDNO<>29ANDNO<>30ANDNO<>31THENPRINT"WHAT A STRANGE IDEA!":GOTO210
1354 PRINT"THIS IS NOT ONE OF YOUR BETTER  SUGGESTIONS!":GOTO210
1400 IFNO=0THENPRINT"KILL WHAT?":GOTO210
1401 GOSUB5300:IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"IT'S NOT HERE.":GOTO210
1402 GOTO1352
1450 IFNO=0THENPRINT"HIT WHAT?":GOTO210
1452 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"IT ISN'T HERE.":GOTO210
1454 PRINT"APART FROM A GRAZED HAND,NOTHINGHAPPENS.":GOTO210
1500 IFNO=0THENPRINT"MAKE WHAT?":GOTO210
1502 IFNO<>13THENPRINT"I DON'T UNDERSTAND YOU AT TIMES.":GOTO210
1504 IFOB(53)<>-1OROB(14)<>-1OROB(4)<>-1THENPRINT"YOU HAVEN'T GOT ALL THE RIGHT   MATERIALS.":GOTO210
1506 PRINT"GOOD LORD, YOU'VE GOT A BRAND   NEW LADDER - SMART WORK.":OB(13)=-1:OB(14)=0:OB(53)=0:ZZ=ZZ-1
1508 GOTO210
1550 IFNO=0THENPRINT"REFLECT WHAT?":GOTO210
1552 IFNO<>47THENPRINT"WHAT ARE YOU ON ABOUT?":GOTO210
1554 IFCP<>93THENPRINT"NOTHING HAPPENS.":GOTO210
1556 IFSC=1THENPRINT"YOU'VE ALREADY DONE THAT.":GOTO210
1558 PRINT:PRINT"THE LIGHT IS REFLECTED BACK, ANDTHE CURTAIN FALLS ASIDE (COR)."
1560 P(93,0)=95:OB(15)=CP:P$(93)="WALKING PAST A SHIMMERINGLIGHT.":SC=1:GOTO210
1600 IFNO=0THENPRINT"OIL WHAT?":GOTO210
1602 IFOB(51)<>-1THENPRINT"YOU HAVEN'T GOT ANY OIL.":GOTO210
1604 IFCP<>79 THENPRINT"NOTHING WORTH OILING AROUND HERE":GOTO210
1605 IFNO<>17THENPRINT"YOU'VE JUST WASTED A LOT OF OIL.":OB(51)=0:OB(19)=-1:GOTO210
1606 PRINT"THE TRACK SLIDES AWAY, TO REVEALMORE TUNNELS!"
1610 OB(51)=0:OB(19)=-1:P(79,2)=80:P(79,3)=81:P$(79)="WALKING PAST A SMOOTH    TRACK.":OB(17)=0:GOTO210
1650 IFNO=0THEN1900
1651 GOSUB5300
1652 IFOB(NO)<>-1ANDOB(NO)<>CP THENPRINT"BUT IT ISN'T HERE!":GOTO210
1653 IFOB(38)<>-1THENPRINT"YOU'VE NOTHING TO STAB IT WITH.":GOTO210
1654 IFNO<>30THENPRINT"YOU'RE WASTING YOUR TIME THERE.":GOTO210
1656 PRINT"THE SPIDER DIES IN A GLORIOUS   DISPLAY OF BIT-ACTING, AND      REVEALS :-"
1658 PRINT"A NEW PASSAGEWAY (IT WAS A VERY LARGE SPIDER).":P(84,2)=86:P(84,3)=85:OB$(30)="A DEAD SPIDER!"
1660 P$(84)="WALKING PAST A DEAD      SPIDER.":GOTO210
1700 IFNO=0THEN1900
1701 GOSUB5300
1702 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"I CAN'T SEE IT HERE.":GOTO210
1703 IFOB(34)<>-1THENPRINT"YOU'RE NOT CARRYING ANY SPRAY.":GOTO210
1704 IFNO<>31THENPRINT"COUGH-COUGH SPLUTTER-SPLUTTER!":GOTO210
1706 PRINT"THE FLY COUGHS ITS LAST, AND    REVEALS A HIDDEN TUNNEL."
1708 P(74,3)=75:P$(74)="WALKING PAST A DEAD FLY.":OB$(31)="A DEAD FLY!":GOTO210
1750 IFNO=0THENPRINT"THROW WHAT?":GOTO210
1751 IFNO<>33ANDNO<>4THEN562
1752 IFNO=4ANDNP=0THEN562
1753 IFNO=4ANDNP=1THEN6010
1754 IFOB(33)<>-1THENPRINT"YOU'RE NOT CARRYING IT!":GOTO210
1755 IFCP<>60THENPRINT"OK! IT VANISHES IN A CLOUD OF   DUST.":OB(33)=0:ZZ=ZZ-1:GOTO210
1756 PRINT"THE DOOR FLIES OPEN UNDER THE   FORCE OF THE BLOW, AND REVEALS ANEW PASSAGEWAY!"
1758 P(60,0)=61:P(60,3)=65:P$(60)="WALKING PAST THE DOOR.":OB(33)=0
1760 ZZ=ZZ-1:GOTO210
1800 IFNO=0THEN1900
1801 GOSUB5300
1802 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"THERE'S NOTHING HERE TO RUB!":GOTO210
1804 PRINT"INTERESTING, BUT UNREWARDING.":GOTO210
1850 IFNO=0THEN1900
1851 GOSUB5300
1852 IFNO<>48THENPRINT"THERE'S NOTHING ON IT TO READ.":GOTO210
1854 IFOB(48)<>-1THENPRINT"YOU'RE NOT HOLDING IT.":GOTO210
1856 PRINT"THERE'S MATERIALS IN HERE TO    BUILD A LADDER."
1858 PRINT"YOU'LL NEED STUFF LIKE NAILS,   A PLANK AND AN AXE."
1859 PRINT"(AND PERHAPS ONE OR TWO OTHER   THINGS!)"
1860 PRINT:PRINT"THERE'S ALSO MORE THAN A TOUCH  OF MAGIC IN THE AIR."
1862 PRINT"(SO BE CAREFUL!)":GOTO210
1890 PRINT"OK.":PRINT"DO YOU WANT TO SAVE YOUR        PROGRESS ONTO TAPE (Y OR N)?"
1892 PR$=INKEY$:IFPR$=""THEN1892
1893 IFPR$="Y"THEN3000
1894 IFPR$="N"THEN614
1896 GOTO 1892
1900 IFNO=0THENPRINT"WHAT'S A ";N1$;"!":GOTO210
1901 IFNO=43ORNO=1ORNO=6THENPRINT"THERE'S NOTHING INTERESTING HERE":GOTO210
1903 GOSUB5300
1904 IFOB(NO)<>-1ANDOB(NO)<>CP THENPRINT"I CAN'T SEE IT HERE.":GOTO210
1905 IFNO=2ORNO=16ORNO=33ORNO=37ORNO=38THENPRINT"THIS HAS USEFUL POWERS.":GOTO210
1906 PRINT"IT'S NOTHING MORE THAN :-":PRINTOB$(NO):GOTO210
1950 IFCP=15ORCP=35ORCP=10THENPRINT"I WARNED YOU ....":PRINTD$:GOTO612
1952 PRINT"WHEEE!":GOTO210
1960 IFNO=0THENPRINT"BREAK WHAT?":GOTO210
1961 GOSUB5300
1962 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"I CAN'T! IT ISN'T HERE.":GOTO210
1964 PRINT"YOU COULDN'T BREAK YOUR WAY OUT OF A PAPER BAG!":GOTO210
1970 IFNO=0THENPRINT"PUSH WHAT?":GOTO210
1971 GOSUB5300
1972 IFOB(NO)<>CP ANDOB(NO)<>-1THENPRINT"I CAN'T! IT ISN'T HERE.":GOTO210
1974 IFCP<>79THENPRINT"YOU CAN'T!":GOTO210
1976 PRINT"TRY DOING THIS ANOTHER WAY (LIKEUSING SOMETHING ELSE!).":GOTO210
1999 END
2000 NV=38:NN=53:P=100:LO=53:DIMP$(P),P(P,3),OB$(LO),OB(LO),VB$(NV),NO$(NN)
2001 P$(1)="ON AN OLD TRACK HEADING  TOWARDS THE CAVES.":DATA 0,2,0,0
2002 P$(2)="GETTING EVER NEARER THE  CAVES.":DATA 1,3,0,0
2004 P$(3)="AT THE ENTRANCE OF THE   CAVES, WITH PATHS LEADING OFF   EVERYWHERE."
2006 P$(4)="IN FRONT OF A SOLID WALL OF ROCK AND YOUR PROGRESS IS    HALTED."
2008 P$(5)="IN A SUBTERRANEAN TOMB,  WITH CRACKS AND CREVICES ALL    OVER THE PLACE."
2010 P$(6)="WALKING AROUND THE SIDE  OF THE CREVICE ROOM."
2012 P$(7)="SURROUNDED BY BRICKED UP WALLS."
2013 DATA2,15,20,4,0,0,3,0,6,13,4,0,0,5,0,7,0,9,6,8,0,10,7,0,7,12,5,10
2014 P$(8)="NEAR A GREAT CHASM IN THEROCKS WHICH PLUNGES DOWN OVER A HUNDRED FEET."
2016 P$(9)="IN THE HEART OF THE      CREVICE ROOM WITH TUNNELS GOING OFF EVERYWHERE."
2017 DATA 8,11,9,0,10,0,12,0,9,0,13,11,5,0,0,12,0,0,10,0,3,0,0,0,0,18,0,15
2018 P$(10)="IN FRONT OF A GREAT      CREVICE WHICH IS MUCH TOO DEEP  AND WIDE TO JUMP ACROSS."
2019 DATA 15,33,18,19,16,34,0,17,0,32,17,0,0,0,21,3,0,0,0,20,23,0,0,21
2020 P$(11)="ON THE SOUTHERN RIM OF   THE CHASM IN A JUMBLE OF ROCKS  AND BOULDERS."
2022 P$(12)="LOST IN CHASM COUNTRY."
2024 P$(13)="IN A ROOM FULL OF ROCKS, ROCKS, ROCKS AND ROCKS."
2026 P$(14)="ON THE WEST SIDE OF THE  CHASM, BUT THERE ISN'T MUCH OF AVIEW."
2028 P$(15)="FACED WITH A CRACK IN THEFLOOR THAT IS MUCH TOO WIDE TO  JUMP."
2030 P$(16)="IN AN EAST-SIDE CHAMBER."
2032 P$(17)="ON THE MAIN TRACK THROUGHTHE CAVES, SURROUNDED BY SIDE   CHAMBERS."
2034 P$(18)="AWAY FROM THE CENTRE     TRACK, WITH A CHOICE OF ROUTES  AS THE TUNNELS WIDEN OUT."
2037 P$(19)="IN A SHARPLY TWISTING    CORRIDOR."
2038 P$(20)="ON A LONG EAST-WEST TRACKINTO THE DEPTHS OF THE MINES."
2040 P$(21)="FORCED TO A HALT BY A    LARGE UNDERGROUND TREE."
2042 P$(22)="HEADING DOWN A TWISTY    PATH INTO AN OLD ANIMAL'S LAIR."
2043 P$(22)=P$(22)+" YOU CAN SEE FROM THE OLD PAW    PRINTS THAT SOMETHING MIGHTY BIGMUST LIVE HERE!"
2044 P$(23)="SURROUNDED BY ROCK IN A  MIXTURE OF CORRIDORS.":DATA25,22,24,0
2045 P$(24)="WALKING ALONG AN OLD     TUNNEL CARVED OUT OF THE ROCKS."
2046 P$(25)="FORCED TO TURN SHARPLY.":DATA26,0,0,23,0,23,26,0,27,24,0,25
2048 P$(26)="WALKING ALONG A FAIRLY   LARGE CORRIDOR."
2050 P$(27)="FACE TO FACE WITH A VERY LARGE BEAR!":DATA0,26,0,0,0,27,30,29
2052 P$(28)="AT A T-JUNCTION BEHIND   THE BEAR'S LAIR."
2054 P$(29)="AT A DEAD END.":DATA0,0,28,0,31,0,0,28,0,30,0,0
2056 P$(30)="NEAR THE HEART OF THE    BEAR'S HIDING PLACE."
2057 P$(31)="IN AN OLD CAVE, USED AS ARESTING PLACE BY THE BEAR."
2058 P$(32)="HEADING DOWN AN OFF-SHOOTFROM THE MAIN MINE.":DATA 19,42,33,41
2060 P$(33)="FACED WITH A CHOICE OF   ROUTES AS YOU STAND HERE SURR-  OUNDED BY SHORED UP TIMBERS AND WALLS."
2061 DATA 17,0,34,32
2062 P$(34)="HEADING DOWN A LONG, LOW CORRIDOR."
2064 P$(35)="FACED WITH A VERY DEEP   DROP THAT IS TOO DEEP TO JUMP ORCLIMB."
2065 DATA 18,0,35,33,0,0,0,34,0,0,38,0,0,38,39,0,37,0,0,36,0,0,40,37,0,0,0,39
2066 P$(36)="AT THE FOOT OF THE DROP.":P$(37)="IN ANOTHER TWISTY PART OFTHE CAVES."
2070 P$(38)="IN A TWISTY PART OF THE  CAVES."
2072 P$(39)="ON A LOW EAST-WEST PATH."
2074 P$(40)="IN A DEAD END."
2075 DATA 0,0,32,0,32,0,0,0,42,0,44,46,0,45,0,43,44,0,0,0,0,0,43,0,0,52,49,48
2076 P$(41)="TO THE WEST OF THE CAVES."
2078 P$(42)="CONFRONTED BY A VERY     ANGRY PANTHER."
2080 P$(43)="AT AN MEETING OF PATHS."
2082 P$(44)="NEAR A GREAT INCLINE."
2084 P$(45)="AT THE FOOT OF A GREAT   SLOPE THAT IS MUCH TOO STEEP TO JUMP OR CLIMB."
2086 P$(46)="IN A DEAD END."
2088 P$(47)="AT THE TOP OF THE SLOPE."
2090 P$(48)="NEAR AN OLD SCARY PART OFTHE CAVES, REPUTEDLY HAUNTED."
2092 P$(49)="NEAR AN OLD MAGIC PART OFTHE CAVES."
2093 DATA 0,51,47,50,0,53,54,47,0,0,48,0,48,66,0,0,47,77,0,0,49,100,0,0,0,0,88,49
2094 P$(50)="CONFRONTED BY THE GHOSTLYSPIRIT OF THE CAVES!"
2096 P$(51)="ON AN OFF-SHOOT FROM THE MAIN PATH."
2097 P$(52)="ON AN OLD NORTH-SOUTH    PATH."
2098 P$(53)="IN FRONT OF A VERY NARROWCRACK."
2099 P$(54)="NEAR THE MAGICAL CAVERNS."
2100 DATA 56,57,50,58,0,55,0,0,55,0,0,0,0,0,55,59,60,0,58,0,0,59,0,0
2102 P$(55)="IN A WIDE, OPEN TUNNEL."
2104 P$(56)=P$(46):P$(57)=P$(46)
2108 P$(58)="ON A WELL-TROD EAST-WEST PATH."
2110 P$(59)="AT A TURNING POINT."
2112 P$(60)="FACED WITH A DOOR MARKED WITH THE WORDS 'BEGONE STRANGER'"
2114 DATA 61,61,61,62,61,61,61,63,61,64,61,61,61,61,65,61,61,61,60,61
2115 P$(61)="IN A MAZE OF TWISTY PATHS"
2116 P$(62)=P$(61)
2117 P$(63)=P$(61)
2118 P$(64)=P$(61)
2119 P$(65)=P$(61)
2120 DATA 51,67,0,68,66,0,0,69,0,69,66,0,68,0,67,0,0,71,69,0,70,72,0,74,71,0,0,73
2122 P$(66)="WALKING ALONG A ROUGH    PATH."
2124 P$(67)="IN A LOW, DAMP CORRIDOR."
2126 P$(68)=P$(67)
2128 P$(69)="STOPPED BY A WALL OF HAZYMIST THAT OBSCURES ALL LIGHT."
2130 P$(70)="ON THE WEST SIDE OF THE  MIST, IN CLEARER AIR."
2132 P$(71)="HEADING ALONG A CLEAR    PATH CUT FROM THE LIVING ROCK."
2134 P$(72)="IN A SHARPLY TWISTING    TUNNEL."
2136 P$(73)="IN INSECT COUNTRY!"
2138 P$(74)="FACE TO FACE WITH A GIANTFLY THAT COMPLETELY BLOCKS YOUR PATH."
2140 DATA 74,0,72,0,0,73,71,0,0,0,74,76,0,0,75,0
2142 P$(75)="ON A LOW EAST-WEST PATH  THAT IS TOTALLY FREE OF INSECTS!"
2144 P$(76)=P$(56)
2146 DATA 52,78,0,0,77,79,0,0,78,0,0,0,0,83,0,79,0,82,79,0,81,0,0,0,80,84,0,0
2148 P$(77)="STILL HEADING NORTH-SOUTH"
2159 P$(78)="AT THE BOTTOM OF A LONG, LOW NORTH-SOUTH PASSAGE."
2160 P$(79)="PREVENTED FROM GOING ANY FURTHER BY AN OLD SEIZED-UP     MINING TRACK."
2161 P$(80)="IN A ROOM THAT'S FULL OF OLD COBWEBS."
2162 P$(81)="ON THE WEST SIDE OF THE  TRACK."
2163 P$(82)="IN THE SALVAGE ROOM."
2164 P$(85)="IN THE SPIDERS' TOMB!"
2165 P$(84)="IN SPIDER COUNTRY AND THELARGE SPIDER HERE WON'T LET YOU PASS."
2166 P$(83)="IN A DARK PASSAGEWAY.":P$(86)=P$(83)
2168 P$(87)="IN A TOTAL DEAD END."
2169 DATA 83,0,0,0,0,0,84,0,0,87,0,84,86,0,0,0
2170 DATA 89,90,92,54,0,88,91,0,88,94,0,0,0,92,93,89,91,0,97,88,0,97,0,91
2172 P$(88)="IN THE HEART OF THE      MAGICAL CAVERNS."
2173 P$(89)="ON A NORTHERN OFF-SHOOT  FROM THE MAIN PATH."
2174 P$(90)="WALKING ALONG A MAGICAL  PATH."
2176 P$(91)="IN A DIMLY-LIT CORRIDOR."
2178 P$(92)="NEAR THE SOURCE OF THE   MAGIC."
2180 P$(93)="HALTED BY A SHIMMERING   CURTAIN THAT YOU CAN'T GET PAST."
2182 DATA 90,0,0,0,0,93,96,0,99,0,98,95,93,0,0,92,0,0,0,96,0,96,0,0,53,0,0,0
2184 P$(94)=P$(56)
2186 P$(95)="ON THE NORTH SIDE OF THE SHIMMERING CURTAIN."
2188 P$(96)="IN A LOW CORRIDOR."
2189 P$(97)="TREADING OVER DIMLY LIT  ROCKS AND RUBBLE."
2190 P$(98)="IN A DEAD END.  THE WALL IS ALL BRICKED UP HERE, AND YOU CAN'T GO ANYWHERE."
2191 P$(99)=P$(89)
2192 P$(100)="IN AN OLD WAREHOUSE ONCEUSED BY THE MINERS' TOOLMAKERS."
2200 FORI=1TOP:FORJ=0TO3:READP(I,J):NEXTJ,I
2210 DATA 15,20,21,34,24,0,40,0,27,7,42,46,0,14,93,67,79,48,98,69
2212 DATA A VAST CHASM,AN IRON STAFF,A VAST TREE,A STOUT AXE,A THICK COIL OF ROPE
2214 DATA AN ENCHANTED BRIDGE,SOME DYNAMITE,A PILE OF RUBBLE,A GOLDEN BEAR
2216 DATA A BUN,A LARGE BLACK PANTHER,A LONG WOODEN PLANK,A TALL LADDER,SOME NAILS
2218 DATA A SHIMMERING CURTAIN,A POLISHED MIRROR,A BLOCKED TRACK
2220 DATA A POOL OF OIL,AN EMPTY BOTTLE,A SOLID WALL OF MIST
2222 FORI=1TO20:READOB(I):NEXT:FORI=1TO20:READOB$(I):NEXT
2224 DATA 50,84,74,60,76,87,3,53,63,31,73,0,0,100,0,3,1,0,0,39,0,0,0,0,0
2226 DATA THE GHOSTLY DENIZEN OF THE CAVES,A HUGE SPIDER,A MASSIVE FLY
2228 DATA AN OLD DOOR,A LUMP OF MORTAR,A FLY SPRAY
2230 DATA A SOLID GATE,A NARROW CRACK,A SHINING STONE,A TRUSTY SWORD,SOME WHISKY
2232 DATA A LIVING GARGOYLE,AN EVIL KNIFE,A KEY,A WALL,SOME MATCHES,AN OLD TORCH
2234 DATA A BLAZING TORCH,A GLOWING LIGHT,AN OLD PARCHMENT,PROGRAM,A PILE OF BROKEN GLASS
2236 DATA A BOTTLE OF OIL,A BOTTLE OF WHISKY,SOME NICELY SAWN TIMBER
2238 FORI=29TOLO:READOB(I):NEXT
2240 FORI=29TOLO:READOB$(I):NEXT
2250 DATA CHA,STA,TRE,AXE,ROP,BRI,DYN,RUB,BEA,BUN,PAN,PLA,LAD,NAI,CUR,MIR,TRA
2252 DATAOIL,BOT,MIS,NOR,SOU,EAS,WES,N,S,E,W,GHO,SPI,FLY,DOO,MOR,SPR,GAT,CRA
2254 DATASTO,SWO,WHI,GAR,KNI,KEY,WAL,MAT,TOR,TOR,LIG,PAR,PRO,GLA,BOT,BOT,TIM
2256 DATAGO,GET,LOO,INV,SCO,DRO,HEL,QUI,CRO,TAK,OPE,CLO,EAT,FEE,DRI,OFF,WAV
2258 DATACUT,CHO,CLI,LIG,ATT,KIL,HIT,MAK,REF,OIL,STA,SPR,THR,RUB,REA,EXA,JUM
2260 DATABRE,PUS,SAV,LOA
2262 FORI=1TONN:READNO$(I):NEXT
2264 FORI=1TONV:READVB$(I):NEXT
2266 DATA NORTH,SOUTH,EAST,WEST
2268 FORI=0TO3:READDD$(I):NEXT
2270 RETURN
2510 PRINT:PRINT"AND NOW YOU CAN LEAVE THESE     NASTY OLD CAVES.":PRINT:PRINT"CONGRATULATIONS!!!":END
3000 CLS:PRINT"INSERT TAPE AND PRESS 'SPACE'   WHEN READY."
3002 PP$=INKEY$:IFPP$=""THEN3002
3004 PRINT"OK.":STOP
3026 GOTO614
3200 CLS:PRINT"INSERT TAPE AND PRESS 'SPACE'   WHEN READY."
3202 PP$=INKEY$:IFPP$=""THEN3202
3204 PRINT"OK.":STOP
3226 GOTO200
5000 CLS
5001 IFOB(46)<>-1AND(CP>4ANDCP<100)THENPRINTPD$:PD=1:RETURN
5002 PRINT"YOU'RE ";P$(CP):PD=0
5003 IFCP=42ANDTB=1ANDP(42,1)=0THEN6054
5004 PRINT:PRINT"YOU CAN SEE :-"
5006 FL=0:FORI=1TOLO:IFOB(I)=CP THENPRINTOB$(I):FL=1
5008 NEXTI:IFFL=0THENPRINT"NOTHING SPECIAL"
5009 IFCP=3ANDGF=0THENPRINT:PRINTGF$
5010 FL=0
5012 PRINT:PRINT"YOU CAN GO :-":FORI=0TO3:IFP(CP,I)<>0THENPRINTDD$(I);" ";:FL=1
5013 NEXT:IFFL=0THENPRINT"NOWHERE!"
5015 PRINT:IFNP=1THEN6000
5016 IF(CP>20ANDCP<88)AND(INT(RND(10))>9)THENNP=1:GOTO6000
5018 IFCP<>69 THEN RETURN
5020 IFP(69,3)=70THENRETURN
5022 IFOB(15)<>-1THENPRINT"YOU CAN'T PASS YET.":RETURN
5024 PRINT"THE SHIMMERING CURTAIN WASHES   AWAY THE MIST AND REVEALS A NEW TUNNEL!"
5025 P(69,3)=70:OB(15)=0:ZZ=ZZ-1:P$(69)="WALKING PAST AN ICY COLD SPOT."
5026 OB(20)=0:RETURN
5200 CLS:PRINT"OH DEAR!! THE GATE TO THE CAVE  APPEARS TO HAVE SLAMMED SHUT!!"
5202 PRINT:PRINT"THAT'S TORN IT!  YOU'LL HAVE TO FIND THE KEY NOW BEFORE YOU CAN GET OUT!!"
5204 PRINT:PRINT"BUT DON'T WORRY.  IT'S IN HERE  SOMEWHERE."
5205 P(3,0)=0
5206 GF=0:GOTO210
5300 IFNO=45ANDOB(46)=-1THENNO=46:RETURN
5302 IFNO=19ANDOB(51)=-1THENNO=51:RETURN
5304 IFNO=19ANDOB(52)=-1THENNO=52:RETURN
5306 IFNO=18ANDOB(51)=-1THENNO=51:RETURN
5308 IFNO=39ANDOB(52)=-1THENNO=52:RETURN
5310 RETURN
6000 PRINT:PRINT"THERE IS A HOSTILE GARGOYLE     WATCHING YOU FROM THE SHADOWS!"
6001 IFINT(RND(100))>99THEN6020
6002 PRINT"HE HAS A KNIFE : HE THROWS IT ATYOU!!":OB(40)=CP
6004 IFINT(RND(100))>99THENPRINT"HE'S KILLED YOU!!":GOTO612
6006 PRINT"IT MISSED!":RETURN
6010 IF INT(RND(10))>1THENPRINT"YOU'VE JUST KILLED A GARGOYLE!":OB(40)=0:GOTO6014
6011 PRINT"YOU MISSED HIM!":OB(40)=CP
6012 OB(4)=CP:ZZ=ZZ-1
6013 FORI=1TO2000:NEXT:GOTO200
6014 NP=0:GOTO6012
6020 PRINT"HE APPEARS FROM THE SHADOWS AND STEALS :-":GS=0
6022 IFOB(2)=-1THENOB(2)=63:PRINTOB$(2):GS=1
6024 IFOB(7)=-1THENOB(7)=63:PRINTOB$(7):GS=1
6026 IFOB(14)=-1THENOB(14)=63:PRINTOB$(14):GS=1
6028 IFOB(16)=-1THENOB(16)=63:PRINTOB$(16):GS=1
6030 IFOB(19)=-1THENOB(19)=63:PRINTOB$(19):GS=1
6031 IFOB(33)=-1THENOB(33)=63:PRINTOB$(33):GS=1
6032 IFOB(34)=-1THENOB(34)=63:PRINTOB$(34):GS=1
6034 IFOB(38)=-1THENOB(38)=63:PRINTOB$(38):GS=1
6036 IFOB(44)=-1THENOB(44)=63:PRINTOB$(44):GS=1
6038 IFGS=0THENPRINT"NOTHING! YOU WERE LUCKY!"
6040 RETURN
6054 PRINT"THE PANTHER FLEES AT THE SIGHT  OF THE BEAR!":P(42,1)=43:OB(11)=0
6055 P$(42)="WALKING PAST THE SCENT OFOLD PANTHER!":GOTO5004
6300 OC=0:FORI=1TOLO:IFOB(I)=-1THENOC=OC+1
6302 NEXT
6304 IFOC>1THENPRINT"SOMETHING WON'T FIT THROUGH.":GOTO210
6306 IFOB(37)<>-1THENPRINT"SORRY, I CAN'T FIT THROUGH.":GOTO210
6308 CP=100:PRINT"THE STONE GLOWS WITH A SHINY    LIGHT AND LETS YOU THROUGH."
6310 GOTO210
10000 CLS
10002 PRINT:PRINT"HERE YOU ARE, MILES AWAY FROM   HOME, TRYING TO DECIDE HOW TO   SPEND YOUR AFTERNOONS."
10003 PRINT:PRINT"DO YOU LOOK FOR PEACE AND QUIET,OR DO YOU LOOK FOR DANGER AND   ADVENTURE ?"
10004 PRINT:PRINT"OF COURSE,YOU LOOK FOR ADVENTURE":PRINT"LIFE'S TOO SHORT!"
10005 PRINT:PRINT"JUST HANG ON A BIT."
10014 RETURN
60000 CM$=""
60002 PRINT"*";CHR$(8);
60005 Z$=INKEY$:IFZ$=""THEN60005
60010 Z=ASC(Z$):IF(Z>97ORZ<65)ANDZ<>32ANDZ<>13ANDZ<>8THEN60005
60080 ZL=LEN(CM$):IFZL>19THEN60110
60100 IFZ>31THENCM$=CM$+Z$:PRINTZ$;:GOTO60002
60110 IFZ=13ANDZL>0THENPRINT" ":RETURN
60120 IFZ=8ANDZL>0THENCM$=LEFT$(CM$,ZL-1):PRINTZ$;
60140 GOTO60002
