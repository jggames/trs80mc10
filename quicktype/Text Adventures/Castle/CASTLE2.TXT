0 GOSUB9800:CLEAR500:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORZZ=1TO3:SOUND180,ZZ:SOUND190,ZZ:SOUND200,ZZ:SOUND200,ZZ+1:SOUND200,ZZ+2:NEXT:RETURN
5 PRINT"  WELCOME TO CASTLE ADVENTURE,":M$="THE OBJECT OF THE GAME IS TO ENTER THE CASTLE AND RESCUE THE FAIR MAIDEN "
6 M$=M$+"FROM THE CLUTCHES OF THE BLACK KNIGHT AND RETURN WITH HER TO THE FRONT OF THE CASTLE.":GOSUB1
10 M$="I RECOGNIZE SIMPLE TWO WORD COMMANDS LIKE TAKE BUCKET, AND ONLY NEED ONE LETTER TO MOVE IN A DIRECTION.":GOSUB1
12 M$="OTHER WORDS THAT I KNOW ARE LOOK AND INV (INVENTORY)":GOSUB1:PRINT"--GOOD LUCK!":GOTO40
20 DIMV$(33),P$(26,5),N$(35,1),M$(12):FORN=0TO33:READV$(N):NEXT:FORN=0TO26:FORX=0TO5:READP$(N,X):NEXT:NEXT
30 FORN=0TO35:FORX=0TO1:READN$(N,X):NEXT:NEXT:FORN=0TO12:READM$(N):NEXT:CLS0:SOUND1,1:CLS:GOTO5
40 INPUT"PLEASE TELL ME YOUR NAME";Z$
45 P=0:N2=0:N3=0:N5=0:N8=0:F=0
46 G=5
47 CLS
48 GOTO2050
49 GOTO55
50 FORZZ=3TO0STEP-1:PRINT@32*ZZ,"":NEXT
55 GOSUB2000
60 B=RND(20):IFB=6THENPRINT@256,;:M$="A HEART STOPPING FEMALE SCREAM IS HEARD FROM SOMEWHERE":GOSUB1
62 F=F+1
65 PRINT@480,"                               ";:PRINT@448,"":PRINT@448,"---NOW WHAT";:INPUTQM$
70 X1=LEN(QM$):IFX1<3THEN100
71 X3=0:FORX2=1TOX1:M$=MID$(QM$,X2,1):IFM$=" "THENX3=X2
74 NEXTX2
80 IFX3=0THEN90
85 X5=X1-X3:R$=RIGHT$(QM$,X5):R1$=LEFT$(R$,3)
90 L$=LEFT$(QM$,3):IFL$="GET"THENL$="TAK"
91 GOTO112
100 L$=LEFT$(QM$,1)
112 FORI=0TO33
113 IFL$=V$(I)THEN180
114 NEXTI:G=2:GOTO2050
116 GOSUB2100
117 IFJJ<0THEN130
120 ONSGOTO3000,3010,3020,3030,3100,3200,3300,6700,7000,3500,3700,3800,3900,4000,4100,4200,4300,4400,4500,4600
122 ONS-20GOTO4700,4800,4900,3400,9700,8100,8200,8500,3300,4300,8550,8550,8550,8550
130 G=2:GOTO2050
180 S=I+1:IFS<8ORS=22ORS=19ORS=27ORS=14ORS=29THEN120
181 GOTO116
2000 IFP=6THEN9000
2001 FORZZ=3TO0STEP-1:PRINT@0,;:I$="YOU SEE: "+P$(P,0)+", ":R=0
2005 FORN=0TO35:IFP=VAL(N$(N,1))THEN2010
2006 NEXT:GOTO2040
2010 I$=I$+N$(N,0)+", ":R=R+1
2020 NEXT
2040 M$=LEFT$(I$,LEN(I$)-2):GOSUB1:PRINT@128,"─▄▄──▄▄──▄▄──▄▄──▄▄──▄▄──▄▄──▄▄─";:RETURN
2050 GOSUB6850:PRINT@160,;:M$=M$(G):GOSUB1:GOTO55
2100 JJ=-1:FORJ=0TO34
2110 L1$=LEFT$(N$(J,0),3)
2111 IFL1$=R1$THEN2120
2112 NEXTJ:RETURN
2120 JJ=J:RETURN
3000 P1$=P$(P,1):GOTO3040
3010 P1$=P$(P,2):GOTO3040
3020 P1$=P$(P,3):GOTO3040
3030 P1$=P$(P,4)
3040 IFVAL(P1$)<0THEN3600
3045 IFVAL(P1$)=40THEN3070
3050 P=VAL(P1$):GOTO46
3060 G=0:GOTO2050
3070 G=3:GOTO2050
3100 IFP=7THEN3110
3105 G=4:GOTO2050
3110 IFR1$="NOR"ORR1$="BAN"ORR1$="CAS"THEN3130
3120 IFR1$="SOU"ORR1$="MOA"THEN3140
3125 G=4:GOTO2050
3130 P=8:GOTO46
3140 P=6:GOTO46
3200 IFP=8ORP=9ORP=11ORP=24THEN3210
3205 G=1:GOTO2050
3210 IFP<10THEN3270
3220 IFP<22THEN3230
3222 GOTO3290
3225 G=4:GOTO2050
3230 IFP=11THEN3232
3231 GOTO3205
3232 IFR$="UP"THEN3235
3233 IFR$="DOWN"THEN3240
3234 GOSUB6850:PRINT@160,;:M$="YOU CAN ONLY GO UP OR DOWN ON STAIRS":GOSUB1:GOTO60
3235 P=10:GOTO46
3240 P=12:GOTO46
3250 IFJJ=28THEN3270
3251 G=9
3260 GOTO2050
3270 IFP=8THEN3280
3271 IFP=9THEN3275
3272 G=0:GOTO2050
3275 P=8:GOTO46
3280 IFN8=0THEN3285
3282 P=9:GOTO46
3285 GOSUB6850:PRINT@160,;:M$="THE VINES ONLY REACH ABOUT HALF WAY UP THE WALL":GOSUB1:GOTO60
3290 IFP<>24THEN3293
3291 IFR$="DOWN"THEN3294
3292 IFR$="UP"THEN3295
3293 GOTO3234
3294 P=23:GOTO46
3295 P=25:GOTO46
3300 GOSUB2000:IFJJ=34ANDP=9ANDVAL(P$(9,5))=1THEN3330
3303 IFP$(15,5)="1"ANDP=15ANDR1$="PIC"THEN3340
3305 IFVAL(P$(P,5))=0THEN3320
3310 G=8:GOTO2050
3320 G=7:GOTO2050
3330 P$(9,5)="0":N$(6,1)="9":GOTO3310
3340 P$(15,5)="0":N$(9,1)="15":GOTO3310
3400 GOSUB8000:IFN4=0THEN60
3410 IFJJ=15THEN3420
3415 G=1:GOTO2050
3420 IFN$(0,1)="30"ANDP=19THEN3440
3430 G=0:GOTO2050
3440 IFN$(14,1)="6"ANDN$(25,1)="22"THEN3460
3450 GOSUB6850:PRINT@160,"DING... NOTHING HAPPENS":GOSUB3:GOTO60
3460 N$(13,1)="40":N$(14,1)="40":P$(19,3)="23"
3470 GOSUB6850:PRINT@160,;:M$="DING... THE WHITE KNIGHT COMES CHARGING OVER THE DRAWBRIDGE AND ENGAGES IN A "
3471 M$=M$+"FIERCE BATTLE WITH THE BLACK KNIGHT, WHERE UPON THEY BOTH DISAPPEAR IN A PUFF OF SMOKE":GOSUB1
3480 GOSUB3:GOTO50
3500 IFJJ=18THEN3540
3502 GOSUB8000:IFN4=0THEN60
3503 IFJJ=3THEN3525
3505 IFJJ=18ORJJ=19THEN3550
3515 G=12:GOTO2050
3525 GOSUB6850:IFVAL(N$(2,1))=30THEN3535
3530 PRINT@160,;:M$="THE DUCKS TRY TO BITE YOUR FINGERS":GOSUB1:GOTO50
3535 PRINT@160,;:M$="THE DUCKS EAT YOUR GRAIN AND BECOME FRIENDLY":GOSUB1:N2=1:N$(2,1)="40":GOTO50
3540 IFN$(18,1)="-6"THENJJ=19
3543 GOTO3502
3550 GOSUB6850:IFVAL(N$(3,1))=30THEN3560
3555 PRINT@160,;:M$="OK, THE CROC'S RUN AFTER YOU TRYING TO EAT YOU, BUT YOU'RE TOO FAST SO THEY GIVE UP":GOSUB1:GOTO50
3560 N$(18,1)="-6":N$(19,1)="6":P$(6,4)="7":PRINT@160,;
3561 M$="THE CROCS TAKE YOUR DUCKS AND DESPITE THEIR QUACKING EAT THEM FEATHERS AND ALL":GOSUB1:N$(3,1)="-5"
3565 GOTO50
3600 FORZZ=13TO5STEP-1:PRINT@32*ZZ,"":NEXT:G=0
3601 IFP=6ANDL$="W"THENPRINT@160,;:M$="THE CROCS ATTEMPT TO BITE YOU AS YOU APPROACH IHE MOAT SO YOU RETREAT":GOSUB1:GOTO50
3605 IFP=16THENPRINT@160,"THE ARMOUR WON'T LET YOU BY":GOTO50
3610 IFP=7THENPRINT@160,;:M$="I DON'T THINK YOU CAN WALK ON WATER":GOSUB1:GOTO50
3615 IFP=8ANDL$="N"THENPRINT@160,;:M$="YOU JUST WALKED INTO A CASTLE WALL":GOSUB1:GOTO50
3617 IFP=12ANDL$="S"THENPRINT@160,;:M$="YOU JUST BUMPED INTO A WALL BUT IT SOUNDS HOLLOW":GOSUB1:GOTO50
3620 IFP=9THENPRINT@160,;:M$="IT'S A LONG WAY DOWN TO THE BANK, BUT THE VINES OVER THE EDGE LOOK PRETTY STURDY":GOSUB1:GOTO50
3625 IFP=11ORP=12THENPRINT@160,;:M$="DON'T YOU KNOW THAT YOU HAVE TO LIFT YOUR FEET UP WHEN YOU CLIMB STAIRS?":GOSUB1:GOTO50
3635 IFP=17THENPRINT@160,;:M$="THE STONE WALLS LOOK SOLID BUT YOU FEEL A DRAFT COMING FROM ONE OF THEM":GOSUB1:GOTO50
3640 IFP=19THENPRINT@160,;:M$="THE BLACK KNIGHT WON'T LET YOU BY":GOSUB1:GOTO50
3645 IFP=20THENPRINT@160,"THE GATE SEEMS TO BE LOCKED":GOTO50
3650 IFP=22THENPRINT@160,;:M$="YOU HAVE TO LET DOWN THE DRAWBRIDGE BEFORE YOU CAN WALK ACROSS IT":GOSUB1:GOTO50
3655 IFP=23THENPRINT@160,;:M$="DIDN'T YOU EVER LEARN TO OPEN A DOOR BEFORE YOU WALK THROUGH IT":GOSUB1:GOTO50
3660 IFP=24THENPRINT@160,;:M$="DON'T YOU EVER LEARN, YOU HAVE TO LIFT YOUR FEET WHEN YOU CLIMB STAIRS!":GOSUB1:GOTO50
3665 IFP=6THENPRINT@160,;:M$="THE DRAWBRIDGE IS RAISED SO YOU CAN'T CROSS THE MOAT ON IT":GOSUB1:GOTO50
3670 G=3:GOTO2050
3700 IFJJ=11THEN3725
3705 IFJJ=26THEN3735
3715 GOSUB6850:PRINT@160,"YOU CAN'T BE SERIOUS":GOTO60
3725 GOSUB8050:IFN4=1ANDP=26ANDN$(10,1)="-26"THEN3750
3726 IFN4=1THEN3790
3727 JJ=31:GOSUB8050:IFN4=1THEN3790
3728 GOTO6720
3735 GOSUB8050:IFN4=1ANDP=22ANDN$(24,1)="22"THEN3780
3736 IFN4=1THEN3790
3737 JJ=17:GOSUB8050:IFN4=1THEN3790
3738 GOTO6720
3750 N5=1:N$(16,1)="26":G=5:GOTO47
3780 P$(22,2)="6":P$(6,1)="22":N$(32,1)="-6":N$(33,1)="6":P$(22,3)="7":P$(22,4)="7"
3785 P$(22,5)="0":N$(24,1)="-22":N$(25,1)="22":GOSUB6850:PRINT@160,;:M$="YOU HEAR THE SOUND OF MACHINERY":GOSUB1:GOTO50
3790 G=4:GOTO2050
3800 IFJJ=11ANDP=25THEN3805
3801 GOSUB8000:IFN4=0THEN60
3802 IFJJ=11THEN3810
3803 GOTO3850
3805 IFVAL(N$(6,1))=30ANDVAL(N$(31,1))=25THEN3860
3810 G=0:GOTO2050
3820 G=5:N$(31,1)="-25":N$(5,1)="25":GOTO55
3850 G=12:GOTO2050
3860 GOSUB6850:PRINT@160,"THE BLADE SEEMS TO BE TOO DULL":GOTO60
3900 GOSUB8000:IFN4=0THEN50
3905 IFJJ=3THEN6700
3910 IFJJ=18ORJJ=19THEN3550
3915 IFJJ=13ORJJ=14ORJJ=15ORJJ=17THEN3930
3920 G=4:GOTO2050
3930 GOSUB6850:PRINT@160,;:M$="I WOULDN'T DO THAT IF I WERE YOU HE'S LIABLE TO CHOP YOUR HEAD OFF":GOSUB1:GOTO50
4000 IFR1$="CHA"THEN4003
4001 IFP=20ANDN$(9,1)="30"THEN4050
4002 GOSUB8000:IFN4=0THEN60
4003 IFP=17THEN4010
4005 G=1:GOTO2050
4010 IFN$(17,1)="17"ANDN$(9,1)="30"ANDR1$="CHA"THEN4020
4015 G=0:GOTO2050
4020 P$(17,5)="0":N$(17,1)="40":P$(17,2)="18":GOSUB6850:N$(35,1)="17"
4025 PRINT@160,;:M$="THE SKELETON SAYS 'THANKS, NOW I'LL SHOW YOU THE WAY OUT.' IT PUSHES A STONE IN THE WALL "
4026 M$=M$+"AND DISAPPEARS CHAINS AND ALL":GOSUB1:GOTO50
4030 GOTO50
4050 GOSUB6850:PRINT@160,;:M$="THE GATE IS SO HARD IT WEARS THE TEETH OFF THE SAW":GOSUB1:GOTO60
4100 GOSUB8000:IFN4=0THEN60
4103 IFJJ=34THEN4110
4105 G=1:GOTO2050
4110 IFVAL(N$(8,1))<8THEN4120
4115 GOSUB6850:PRINT@160,"IT'S NOT LOADED":GOTO60
4120 IFN$(7,1)="30"ANDN$(6,1)<>"-9"THEN4130
4125 G=0:GOTO2050
4130 IFN$(14,1)="-6"THEN4140
4135 GOSUB6850:PRINT@160,;:M$="YOU CAN'T. YOU DON'T HAVE ANY MORE GUNPOWDER":GOSUB1:GOTO60
4140 N$(14,1)="6":GOSUB6850:PRINT@160,;:M$="THE CANNON EXPLODES WITH A FLASH AND A BOOM, AND YOU HEAR THE SOUND "
4141 M$=M$+"OF HOOF BEATS FROM SOMEWHERE NEAR THE MOAT":GOSUB1:GOTO50
4200 GOSUB8000:IFN4=0THEN60
4203 IFJJ=34THEN4210
4205 G=1:GOTO2050
4210 IFVAL(N$(8,1))>0THEN4220
4215 GOSUB6850:PRINT@160,"THE CANNON IS ALREADY LOADED":GOTO60
4220 IFN$(8,1)="30"ANDP=9THEN4230
4225 G=0:GOTO2050
4230 N$(8,1)="-14":G=5:GOTO2050
4300 IFP=13ORP=16THEN4305
4302 IFP=23ANDJJ=29THEN4330
4303 IFP=20THEN4360
4304 GOTO4306
4305 IFJJ=29THEN4335
4306 G=4:GOTO2050
4310 IFJJ=22THEN4360
4320 IFJJ=24THEN4350
4330 IFP$(23,3)="-24"THEN4340
4335 GOSUB6850:PRINT@160,"IT'S ALREADY OPEN":GOTO60
4340 P$(23,3)="24":G=5:GOTO2050
4350 G=0:GOTO2050
4360 IFN$(4,1)="30"THEN4380
4370 GOSUB6850:PRINT@160,"YOU CAN'T IT'S LOCKED":GOTO60
4380 IFP$(20,2)="21"THEN4335
4383 IFP<>20THEN4350
4385 P$(20,2)="21":N$(22,1)="-20":N$(23,1)="20":GOTO46
4400 IFJJ=12ANDP=26ANDN$(16,1)="26"THEN4460
4401 IFJJ=12ANDP=26ANDN$(16,1)="-26"THEN4480
4403 IFJJ>12THEN4485
4405 IFJJ=3ORJJ=4ORJJ=5THEN4470
4406 GOSUB8000:IFN4=0THEN60
4410 IFJJ=12THEN4430
4420 N3=1:GOTO7000
4430 GOSUB6850:PRINT@160,;:INPUT"AT WHAT";AZ$
4440 IFLEFT$(AZ$,3)="ARM"THEN4447
4445 IFAZ$="VINES"THEN8100
4447 IFP=16THEN4450
4448 GOTO4720
4450 N$(12,1)="7":N$(20,1)="-16":N$(21,1)="16":P$(16,4)="17":GOTO46
4460 P$(26,5)="0":N$(16,1)="-26":N$(10,1)="26":N$(4,1)="26":GOSUB6850
4461 PRINT@160,;:M$="THE WATER GETS ABSORBED INTO THE GROUND":GOSUB1:P$(26,5)="0":GOTO50
4470 G=1:GOTO2050
4480 GOSUB6850:PRINT@160,"THE BUCKET IS EMPTY":GOTO60
4485 G=9:GOTO2050
4500 FORZZ=13TO10STEP-1:PRINT@32*ZZ,"":NEXT:PRINT@320,"YOU ARE CARRYING THE FOLLOWING:"
4510 I$="":PRINT@352,;:V1=0:FORH=0TO12:V=VAL(N$(H,1)):IFV=30THEN4540
4520 NEXTH:IFV1<>30THENPRINT"NOTHING AT ALL":GOTO60
4530 M$=LEFT$(I$,LEN(I$)-2)::GOSUB1:GOTO60
4540 V1=V:I$=I$+N$(H,0)+", ":GOTO4520
4600 GOTO4650
4602 GOSUB8000:IFN4=0THEN60
4610 IFJJ=11ORJJ=26ORJJ=28ORJJ=1THEN4620
4614 G=1:GOTO2050
4620 GOSUB6850:PRINT@160,"WITH WHAT? YOUR BARE HANDS";:INPUTGN$
4622 IFGN$="YES"THEN4630
4625 GOTO60
4630 IFN$(31,1)="25"ANDP=25THEN4660
4631 PRINT"SORRY... YOU'RE TOO PUNY!":GOTO60
4650 IFP=17ANDJJ=26ANDN$(17,1)="17"THEN4620
4652 IFP=25ANDN$(31,1)="25"THEN4620
4655 GOTO4602
4660 GOSUB6850:PRINT@160,;:M$="I DON'T BELIEVE IT BUT YOU JUST BROKE THAT ROPE WITH YOUR BEAR HANDS-- "
4661 M$=M$+"I GUESS YOU'RE NOT SO PUNY AFTER ALL":GOSUB1:GOTO3820
4700 REM
4702 IFJJ=5ORJJ=10ORJJ=11ORJJ=13ORJJ=18ORJJ=19ORJJ=20ORJJ=22ORJJ=24ORJJ=26ORJJ=29THEN4720
4705 GOSUB8000:IFN4=0THEN60
4710 IFJJ=15THEN3400
4720 B=JJ+1:GOSUB6850
4730 ONBGOTO4750,4620,4750,3530,4750,4805,4750,4750,4760,4750,4815,4825,4750,4835,3930,3400,4750,3930,4845,3555
4731 ONB-20GOTO4855,3930,4865,4750,4875,4750,4755,4750,4755,4885,4755,4750,4755,4750,4760,4750
4750 G=12:GOTO2050
4755 G=4:GOTO2050
4760 PRINT@160,;:M$="I WOULDN'T DO THAT IF I WERE YOU, IT MIGHT EXPLODE":GOSUB1:GOTO60
4800 G=6:GOTO2050
4805 GOSUB8050:IFN4=1THEN4750
4807 JJ=31:GOSUB8050:IFN4=0THEN6720
4810 GOTO4750
4815 GOSUB8050:IFN4=1THEN4750
4817 JJ=16:GOSUB8050:IFN4=0THEN6720
4820 GOTO4750
4825 GOSUB8050:IFN4=1THEN4750
4827 JJ=31:GOSUB8050:IF N4=0THEN6720
4830 GOTO4750
4835 GOSUB8050:IFN4=1THEN3930
4837 JJ=14:GOSUB8050:IFN4=0THEN6720
4840 GOTO3930
4845 GOSUB8050:IFN4=1THEN3555
4847 JJ=19:GOSUB8050:IFN4=0THEN6720
4850 GOTO3555
4855 GOSUB8050:IFN4=1THEN3930
4857 JJ=21:GOSUB8050:IFN4=0THEN6720
4860 GOTO3930
4865 GOSUB8050:IFN4=1THEN4755
4867 JJ=25:GOSUB8050:IFN4=0THEN6720
4870 GOTO4755
4875 GOSUB8050:IFN4=1THEN4755
4877 JJ=25:IFN4=1THEN4755
4880 JJ=32:GOSUB8050:IFN4=1THEN4755
4882 JJ=33:GOSUB8050:IFN4=1THEN4755
4884 GOTO6720
4885 GOSUB8050:IFN4=1THEN4755
4887 JJ=30:GOSUB8050:IFN4=1THEN4755
4890 IFP=13THEN4755
4892 GOTO6720
4900 IFJJ=1ANDN$(1,1)="30"THEN4910
4905 G=0:GOTO2050
4910 IFVAL(N$(2,1))=PTHENJJ=12
4915 IFP=7THENJJ=12
4920 IFJJ<>12ORJJ<>2THEN4940
4925 GOSUB6850:PRINT@160,N$(JJ,0)" TAKEN"
4930 N$(JJ,1)="30":GOTO50
4940 PRINT@160,;:INPUT"WITH WHAT";ZN$
4945 JJ=40:IFZN$="GRAIN"THEN4970
4950 IFZN$="WATER"THEN4980
4965 GOSUB6850:PRINT@160,;:M$="SORRY "+ZN$+" WON'T FIT IN THE JAR":GOSUB1:GOTO60
4970 JJ=2:GOSUB8000:IFN4=0THEN60
4975 GOTO4925
4980 JJ=12:GOSUB8000:IFN4=0THEN60
4985 GOTO4925
5000 DATAN,S,E,W,SWI,CLI,LOO,TAK,DRO,FEE,PUL,CUT,CAT,SAW,FIR,LOA,OPE,THR,INV,BRE,HIT,SAY,FIL,RIN,KIL,POU,FUC,CLO
5005 DATAEXA,UNL,TOU,FEE,SME,TAS
5010 DATAFOREST,0,2,1,0,0,FOREST,1,3,1,0,0,FOREST & BUSHES,0,4,3,5,1,FOREST,1,3,3,2,0,BUSHES,2,4,4,4,0
5015 DATAFOREST,5,5,2,6,0,CASTLE ENCIRCLED BY MOAT,-22,6,5,-2,0,MOAT,-8,-6,-7,-7,0
5020 DATABANK & CASTLE,-9,7,8,8,0,GUARDS WALK,9,-8,10,9,1,GUARDS WALK NEAR TOP OF SOME STEPS,11,10,10,9,0
5025 DATASTAIRS,40,-11,40,-11,0,HALLWAY NEAR FOOT OF SOME STEPS,40,-13,11,15,1,SECRET DOOR,12,14,40,40,0
5027 DATAARMOURY,13,40,40,40,0
5030 DATAHALLWAY WITH PICTURES OF FIERCE LOOKING BEARDED KNIGHTS,40,40,12,16,1,HALLWAY,40,40,15,-17,0
5032 DATADUNGEON,40,-18,16,40,1,SECRET PASSAGEWAY,17,40,19,40,0,COURT YARD & WELL,26,20,-23,18,0,CASTLE ENTRANCE
5035 DATA19,-21,19,19,0,TUNNEL,20,22,40,40,0,GATEHOUSE,21,-6,-7,-7,1
5040 DATADOOR NEAR FOOT OF SOME STEPS,40,40,-24,19,0,STAIRS,-24,-24,-24,-24,0,WATCH TOWER,40,40,24,40,0
5042 DATAWELL,40,19,40,40,1,MACE,17,JAR,4,GRAIN,1,DUCKS,5,KEY,-26,MAIDEN,-25,DAGGER,-9,TORCH,15
5045 DATAGUNPOWDER,14,SAW,-15,BUCKET,-26,ROPE,26,WATER,7
5050 DATAKNIGHT DRESSED IN BLACK ARMOUR,19,KNIGHT DRESSED IN WHITE ARMOUR,-6,BELL,19
5052 DATABUCKET OF WATER,-26,SKELETON CHAINED TO WALL,17,CROCS WITH EMPTY BELLYS,6,CROCS WITH FULL BELLYS,-6
5054 DATAARMOUR,16,RUSTY SUIT OF ARMOUR,-16,GATE,20
5055 DATAOPEN GATE,-20
5060 DATADRAWBRIDGE,22,LOWERED DRAWBRIDGE,-22,CHAIN,22,WALL,40,VINES,8,DOOR,16,DOOR,19
5065 DATAMAIDEN TIED UP WITH ROPE,25,DRAWBRIDGE,6,LOWERED DRAWBRIDGE,-6,CANNON,9,SECRET PASSAGE,-17
5070 DATA"SORRY, BUT YOU CAN'T DO THAT... YET!",I DON'T KNOW WHAT YOU MEAN,WHAT?,SORRY YOU CAN'T GO THERE
5072 DATANOTHING HAPPENS,OK,SORRY SIR LANCELOT... WRONG GAME,YOU SEE NOTHING SPECIAL,THERE'S SOMETHING THERE ALL RIGHT
5075 DATAIT'S BEYOND YOUR POWER!,TAKEN
5080 DATADROPPED,YOU MUST BE WEIRD
6010 IFVAL(N$(12,1))=PTHENN$(12,1)="30"
6015 IFVAL(N$(2,1))=PTHENN$(2,1)="30"
6020 GOTO6740
6025 P$(2,5)="0":GOTO6010
6100 IFVAL(N$(1,1))=30THEN6740
6110 PRINT@160,;:M$="THE GRAIN SLIPS THROUGH YOUR FINGERS AS YOU TRY TO TAKE IT":GOSUB1:GOTO50
6200 IFN2=1THEN6740
6210 PRINT@160,;:M$="THE DUCKS FLY AWAY FROM YOU AS YOU APPROACH THEM":GOSUB1:GOTO50
6300 P$(12,2)="13":P$(12,5)="0":PRINT@160,;:M$="YOU HEAR THE SOUNDS OF GEARS AND CHAINS CLANKING COMING FROM "
6301 M$=M$+"SOMEWHERE DOWN THE HALLWAY":GOSUB1:N$(JJ,1)="30":GOTO50
6400 IFVAL(N$(1,1))=30THEN6430
6410 PRINT@160,"YOU'VE NOTHING TO CARRY IT IN":GOTO50
6430 G=10:GOTO6740
6500 IFN5=0THEN6520
6510 GOTO6740
6520 PRINT@160,;:M$="YOU CAN'T TAKE THE ROPE YET IT SEEMS TO BE ATTACHED TO SOMETHING":GOSUB1:GOTO50
6700 GOSUB6850:C=JJ+1:IFC>13THEN6770
6705 G=10:IFVAL(N$(JJ,1))=30THEN6750
6707 IFJJ=10THEN6900
6709 IFJJ=5THEN8300
6710 IFVAL(N$(JJ,1))=ABS(P)THEN6730
6720 G=0:PRINT@160,"I DON'T SEE IT HERE":GOTO60
6730 IFVAL(N$(JJ,1))=PTHEN6760
6735 G=0
6740 IFG=0THEN2050
6743 N$(JJ,1)="30":PRINT@160,"TAKEN":GOTO50
6750 GOSUB6850:PRINT@160,"YOU'RE ALREADY CARRYING IT!":GOTO60
6760 ONCGOTO6740,6025,6100,6200,6740,6740,6740,6300,6740,6740,6900,6500,6400
6770 G=9:GOTO2050
6850 FORZZ=9TO5STEP-1:PRINT@32*ZZ,"":NEXT:FORZZ=13TO9STEP-1:PRINT@32*ZZ,"":RETURN
6900 IFN$(16,1)="26"ANDN$(10,1)="-26"ANDP=26THEN6940
6910 IFVAL(N$(10,1))>0ANDVAL(N$(10,1))<>30THEN6740
6920 IFVAL(N$(10,1))<0ANDVAL(N$(16,1))<0THEN6720
6930 G=0:GOTO2050
6940 GOSUB6850:PRINT@160,"THE BUCKET'S TOO HEAVY":GOTO60
7000 IFVAL(N$(JJ,1))=30THEN7020
7005 GOSUB6850:PRINT@160,;:M$="YOU'RE NOT CARRYING THE "+N$(JJ,0):GOSUB1:GOTO60
7020 IFJJ=12THEN7050
7021 IFJJ=1THEN7060
7022 IFN3=0THEN7024
7023 G=5:N3=0:GOTO7025
7024 G=11
7025 N$(JJ,1)=STR$(P):GOTO2050
7050 N$(12,1)="7":GOSUB6850:PRINT@160,;:M$="THE WATER SPLASHES OUT AND GETS ABSORBED INTO THE GROUND":GOSUB1:GOTO50
7060 IFVAL(N$(2,1))=30THENN$(2,1)=STR$(P)
7061 IFN3=0THEN7064
7062 N3=0:G=5:GOTO7065
7064 G=11
7065 N$(JJ,1)=STR$(P):IFVAL(N$(12,1))=30THEN7050
7070 GOTO7025
7450 P$(15,5)="0":GOTO7040
8000 N4=1:IFVAL(N$(JJ,1))=30ORVAL(N$(JJ,1))=PTHENRETURN
8010 GOSUB6850:PRINT@160,;:M$="YOU CAN'T DO THAT YET BECAUSE IT'S NOT HERE":GOSUB1
8020 N4=0:RETURN
8050 N4=1:IFVAL(N$(JJ,1))=30ORVAL(N$(JJ,1))=PTHENRETURN
8051 N4=0:RETURN
8100 IFP=8ANDN$(12,1)="30"ANDJJ=12ANDN8=0THEN8120
8103 IFP=26ANDR1$="WAT"THEN4400
8105 GOTO7000
8120 GOSUB6850:PRINT@160,;:M$="THE GROUND RUMBLES AROUND YOU AND THE VINE NOW SPROUTS ALL THE WAY UP THE CASTLE WALL"
8121 GOSUB1:N8=1:N$(12,1)="7":GOTO50
8200 GOSUB6850:PRINT@160,"HEY, WATCH YOUR LANGUAGE":GOTO60
8300 IFP=25ANDN$(31,1)="25"THEN8340
8305 IFN$(5,1)="30"ORVAL(N$(5,1))=PTHEN8350
8310 GOSUB6850:PRINT@160,"SHE'S NOT HERE":GOTO60
8340 GOSUB6850:PRINT@160,;:M$="SHE'S NOT READY TO GO YET, SHE'S TIED UP AT THE MOMENT":GOSUB1:GOTO60
8350 GOTO6740
8500 G=9:GOTO2050
8550 GOSUB6850:PRINT@160,"STOP THAT YOU WEIRDO":GOTO60
9000 IFVAL(N$(5,1))=30THEN9050
9010 GOTO2001
9050 CLS:PRINT@160,"   CONGRATULATIONS YOU WON!"
9055 PRINT@8*32,"YOU USED";F;"TURNS"
9056 IFF>350THEN9080
9057 IFF>185THEN9090
9060 PRINT@32*10,;:M$="EXCELLENT-- I HEREBY KNIGHT YOU SIR "+Z$:GOSUB1
9070 PRINT:END
9080 PRINT@32*10,;:M$="POOR-- "+Z$+" SHOULD HAVE EATEN THE DUCKS INSTEAD":GOSUB1
9081 GOTO9070
9090 PRINT@32*10,;:M$="FAIR-- BUT "+Z$+" DOESN'T GET TO KEEP THE MAIDEN":GOSUB1
9091 GOTO9070
9700 G=9:GOTO2050
9800 CLS5:POKE49151,64:PRINT"оооооооооооооооолллооооооооооооо";
9802 PRINT"ооCASTLEоооооооо╣©аооооооDAVEооо";
9803 PRINT"оADVENTUREоооооо═цоооооTRAPASSOо";
9804 PRINT"оооооооооойЁеоонСмоооо╠╡оооооооо";
9805 PRINT"оооооооооойцоонВЪШмоооагоооооооо";
9806 PRINT"ооооооооонСдонВЪЪЪШмохСмоооооооо";
9807 PRINT"оооооооонВЪШдкясссргхВЪШмооооооо";
9808 PRINT"оооооооокяссаоуэъэзобссргооооооо";
9809 PRINT"ооооооооотччейшъсъвейщщьоооооооо";
9810 PRINT"оооооооойъввзоясссроушшъеооооооо";
9811 PRINT"ооооооооояссднуэъэзмхссроооооооо";
9812 PRINT"оооооооооуыщувшъсъвшзчжзоооооооо";
9813 PRINT"оооооооооутууъчэээщъззьзоооооооо";
9814 PRINT"оооооооооуъъуъзЭЭЭуъзъъзоооооооо";
9815 PRINT"ооJULYоооуъъуъзЭЭЭуъзъъзоо1981оо";
9816 PRINT"оооооооооуъъуъзЭЭЭуъзъъз";:FORZZ=1TO2500:NEXTZZ:RETURN
9900 REM MC-10 EDITS JIM GERRIE
