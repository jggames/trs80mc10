0 GOTO6010
9 IFFTHENT=T+.5:IFT=100THENCLS:FORI=1TO8:PRINT:NEXT:GOSUB925:PRINT:GOSUB750:FORI=0TO80:CLSRND(8):FORJ=1TO15:NEXTJ,I:GOTO930
10 IFF THENIFT/10=INT(T/10)THENGOTO740
15 IFU AND O(6)THEN945
20 IFO(12)=0 AND O(4)THEN900
25 IFO(13)=0 AND O(4)THEN910
30 Z=Z+1:IF(Z>0 AND O(6))AND L=6THENGOSUB990:GOTO945
40 PRINT:PRINT"YOUR COMMAND";
58 INPUTZ$:IFZ$=""THEN40
59 A$=LEFT$(Z$,2):FORI=1TO31:PRINT"-";:NEXT:PRINT
60 FORI=1TOLEN(W$)STEP2:IFA$=MID$(W$,I,2)THENJ=(I+1)/2:GOTO70
65 NEXT:GOTO725
70 B$="":GOSUB100
75 IFJ=15THENJ=0:GOTO375
76 IFB$<>""THEN90
80 IFJ=2THENS=5:GOTO265
85 GOTO725
90 I=-2:ONJ GOTO200,230,340,395,405,490,490,540,540,605,615,640,665,230
100 FORI=LEN(Z$)TO1STEP-1
105 IFMID$(Z$,I,1)=" "THENB$=MID$(Z$,I+1,2):GOTO114
106 NEXT
114 FORI=1TOLEN(O$)STEP2:IFB$=MID$(O$,I,2)THENS=(I+1)/2-1:RETURN
115 NEXT:RETURN
117 DATATHE CONTROL ROOM,THE CONNING TOWER,THE ENGINE ROOM,THE BATTERY ROOM,THE GALLEY, A FLOODED COMPARTMENT,A TOOL ROOM
118 DATATHE MISSILE ROOM,THE TORPEDO ROOM,THE CREW'S QUARTERS,THE REACTOR ROOM
120 DATAFORWARD,AFT,UP,DOWN,HUNGRY MORAY eel,POTS,PLATES,RUBBER gloves,MATCHES,scuba GEAR,BOX OF washers,UNDERWATER lantern
121 DATAWRENCH,SLEDGEHAMMER,oxyacetylene TORCH,STORAGE batteries,electric EEL,FISH
130 DATACOMPUTER CIRCUIT board,HEAVY METAL chair
140 DATAREACTOR TEMPERATURE dial,DEPTH gauge,MAIN computer,PERISCOPE,STORAGE cabinet,WARNING notice,SINK,LOCKER,MISSILE tubes
141 DATATORPEDO tubes
150 DATAESCAPE hatch,STEAM turbines,ENGINES,hatch IN DECK,CREWMEN'S EMPTY bunks,NUCLEAR reactor,SMASHED CONTROL rods
151 DATApuddle ON FLOOR,SEA chest,hatch IN BULKHEAD,hatch TO MISSILE ROOM,JAGGED hole IN HULL
175 W$="GOLOTADROPPUPOHIBRHOLISHFIREIN":OP$="OPEN ":Y$="YOU CAN SEE---"
180 O$="FOAFUPDOEEPOPLGLMASCWALAWRSLOXBAELFIBOCHDIGACOCAPENOSILOMITOESTUENHABUREROPUCHHAHAHOPIBRACARHI":RETURN
190 GOSUB198:GOSUB6000:IFA$="l"THEN195
191 OPEN"O",#-1,:FORI=24TO40:PRINT#-1,O$(I):NEXT:FORI=4TO19:PRINT#-1,O(I):NEXT:
192 PRINT#-1,L(10,3):PRINT#-1,C:PRINT#-1,D:PRINT#-1,E:PRINT#-1,F:PRINT#-1,L:PRINT#-1,MH:PRINT#-1,LO:PRINT#-1,Q:PRINT#-1,T
193 PRINT#-1,U:PRINT#-1,Z:CLOSE#-1:GOTO196
195 OPEN"I",#-1,:FORI=24TO40:INPUT#-1,O$(I):NEXT:FORI=4TO19:INPUT#-1,O(I):NEXT
196 INPUT#-1,L(10,3):INPUT#-1,C:INPUT#-1,D:INPUT#-1,E:INPUT#-1,F:INPUT#-1,L:INPUT#-1,MH:INPUT#-1,LO:INPUT#-1,Q:INPUT#-1,T
197 INPUT#-1,U:INPUT#-1,Z:CLOSE#-1:GOSUB5000:CLS:PRINT"BE SURE TO PRESS SHIFT 0!":S=45:GOTO222
198 PRINT:PRINT:PRINT"START TAPE, PRESS ENTER"
199 B$=INKEY$:ON-(B$="")GOTO199:RETURN
200 IFS=41ANDL=6THENPRINT"YOU'RE TOO big TO FIT!":GOTO9
205 IFL=9ANDE=1AND(S=29ORS=31)THEN995
210 IFS>3THEN895
215 IFL=10 AND O(1)=L AND S<>1THEN960
220 IFL(L,S)<=0THENPRINT"YOU CAN'T GO THAT WAY!":GOTO9
221 L=L(L,S):S=5:I=0
222 REM SOUND
223 IFL=11THENSOUND10,1:GOTO230
224 IFL=3THENSOUND20,1:GOTO230
228 IFF=0THENSOUND30,1
230 IFJ=1THENPRINT"YOU ARE IN---":PRINTD$(L)
231 IFS=15THENPRINT:PRINT"ACID!":GOTO9
235 IFS=31ANDL=8THENS=28
240 IFS=31ANDL=9THENS=29
245 IFS=19ANDL=6THEN855
250 IFS<20ORS>29THEN265
255 IFO(S-3)<>LTHEN880
260 ONS-19GOTO730,755,765,775,790,800,805,825,850,870
265 IFNOTU OR (Q AND(O(8)=L OR O(8)=0))THEN275
270 PRINT:PRINT"theres no light-you cant see!":GOTO9
275 PRINT:PRINTY$
276 IFI=-2 AND S<>45 ANDS>3THEN875
280 IFL=11 AND S=2THEN915
285 IFS<4THENIFL(L,S)>0THENPRINTD$(L(L,S)):GOTO9
290 J=0:FORI=1TO38:IFO(I)=L THENPRINTO$(I+3):J=1
295 NEXT:IFJ=0THENPRINT"NOTHING
300 IFL=10 AND H AND NOT U THENPRINT:PRINT:PRINT"SOUND OF WATER SLOSHING!
305 PRINT:PRINT"OBVIOUS EXITS ARE---":FORI=0TO3
310 IFL=10 AND H THEN L(L,3)=6
312 IFL=1 AND R THENL(L,1)=8
315 IFL=5 AND MH THENL(L,0)=10
320 IFL(L,I)>0THENPRINTO$(I)"--";
325 NEXT:PRINT
330 IFL=6 AND O(6)THENPRINT"WATER IS COLD --GASP-- HURRY!
335 GOTO9
340 IFS=44THENPRINT"not A GOOD IDEA!":GOTO9
345 IFS<4 OR S>19 THEN895
350 IFO(S-3)<>L AND O(S-3)<>L+50THEN880
355 IFC>3THENPRINT"YOU'RE CARRYING TOO MUCH!":GOTO9
360 O(S-3)=0:C=C+1
365 IFS=4THEN960
370 GOTO905
375 PRINT"YOU'RE CARRYING---":FORI=1TO16
380 IFO(I)=0THENPRINTO$(I+3):J=1
385 NEXT:IFJ=0THENPRINT"NOTHING
390 GOTO9
395 IFO(S-3)THEN940
400 O(S-3)=L:C=C-1:GOTO905
405 I=0:IF(S=31 OR S=33) AND L=8THENS=28
410 IFS=31 AND L=9THENS=29
415 IFS=19 AND L=6THENS=38
420 IFS=35 AND L=11THENFORI=1TO80:CLS(RND(8)):FORJ=1TO15:NEXTJ,I:GOTO935
425 IFS=23 AND L=2THENO$(24)=OP$+O$(24):O(15)=52:GOTO265
430 IFS=27 AND L=6THENO$(27)=OP$+O$(27):LO=-1:GOTO265
435 IFS=38 AND L=6THENO$(38)=OP$+O$(38):D=-1:GOTO265
440 IFS<>33THEN470
445 IFL=2THENPRINT"THERE'S SOMETHING ON THE OUTSIDEHOLDING IT CLOSED!":GOTO9
450 IFNOT F THEN885
452 IFL=1THENPRINT"IT OPENED AN INCH, THEN JAMMED!":GOTO9
455 IFL=5 AND F THENMH=-1:GOTO965
460 IFL<>10THEN895
465 IFNOT H THENPRINT"***SCREECH***":SOUND200,5:FORI=1TO15:SOUNDRND(3)*9),1:NEXT
466 PRINT"THE HINGES ARE CORRODED!":PRINT"IT WON'T OPEN!":GOSUB990:GOTO222
470 IFS=28 OR (S=29 AND NOT E)THENPRINT"***WARNING***":PRINT"TUBE LOADED!":PRINT"CANNOT BE OPENED!":GOTO9
475 IFS<>29THEN895
480 IFNOT(U)THENPRINT"HIGH WATER PRESSURE ON DOORS":PRINT"CAN'T BE OPENED":GOTO9
485 E=1:O$(29)=OP$+O$(29):GOTO265
490 IF(S<19ANDO(S-3))OR(S=44ANDO(12))THEN940
495 J=S:S=0:INPUT"WHERE";Z$:Z$=" "+Z$:GOSUB100
500 IFL=10ANDJ=44 AND(S=33 OR S=46)THENH=-1:O$(33)=OP$+O$(33):GOSUB966:GOTO222
501 IFJ=44AND(S=33ORS=46)THENPRINT"**sizzle**":PRINT"IT RAN OFF TOO FAST TO WORK!":GOTO920
505 IFJ=44THEN955
510 IFJ<>18THEN920
515 IFS<>22 OR U THEN920
520 F=-1:O(15)=50:FORI=2TO5:FORS=1TO12:SOUNDRND(200),1:NEXTS,I:CLS
521 FORI=1TO8:PRINT@163,"++++computer on-line++++";:FORJ=1TO12:SOUND1,1:NEXT
522 PRINT@163,"****COMPUTER ON-LINE****";:FORJ=1TO12:SOUND200,1:NEXTJ,I:PRINT
523 SOUND1,10:FORI=1TO4:PRINT@232,">>>>danger<<<<";:POKE49151,64:PRINT@232,"<<<<DANGER>>>>";:FORZZ=1TO100:NEXT:SOUND100,1:NEXT
524 PRINT
525 PRINT:PRINT:PRINT:PRINT"REACTOR CONTROL RODS DESTROYED":GOSUB990:PRINT"REACTOR CORE TEMPERATURE RISING"
526 FORI=1TO8:SOUNDI*10,1:PRINT@409,"rising";:PRINT@409,"RISING":NEXTI
535 CLS:C=C-1:GOTO222
540 J=S:INPUT"WITH WHAT";Z$:Z$=" "+Z$:GOSUB100:IFS<>13 AND S<>19 THEN920
545 IF O(S-3)THEN940
550 IFS=19THENPRINT:PRINT"*****CLANG!*****":PRINT:SOUND200,4:FORI=15TO1STEP-1:SOUNDI,1:NEXT:IFJ=33 AND L=1THEN602
560 IFJ<>42 OR S<>13 ORL<>11 THEN920
565 CLS:PRINT:PRINT:PRINTTAB(5)"THE SHIP IS FLOODING!":GOSUB990
566 POKE49151,64:CLS:PRINT:PRINT:PRINTTAB(3)"CORE TEMPERATURE DROPPING":FORZZ=1TO750:NEXT:FORI=150TO50STEP-2:SOUNDI,1:NEXT
570 FORI=1TO8:PRINT@132,"                     ";:SOUND50,2:PRINT@132,"SAFETY LEVEL ATTAINED";:FORJ=0TO50:NEXTJ,I:GOSUB990
575 SOUND250,3:FORI=1TO30:PRINT@198,"                  ";:SOUND245,1:PRINT@198,"ELECTRICAL FAILURE";:SOUND1,1:NEXT
580 FORI=0TO500:NEXT:PRINT@229,"SYSTEMS SHORTING OUT!":FORI=0TO500:NEXT:FORI=1TO5:PRINT@293,"                    ";
581 SOUND100,5:PRINT@293,"COMPUTER MALFUNCTION";:FORJ=0TO80:NEXTJ,I
581 FORI=5TO1STEP-1:SOUNDI,1:FORS=12TO1STEP-1:SOUNDS,1:NEXTS,I:FORI=15TO1STEP-1:SOUNDI,1:NEXT
585 FORI=1TO30:PRINT@422,"                   ";:FORJ=0TORND(150):NEXT
586 PRINT@422,"COMPLETE POWER LOSS";:FORJ=0TORND(150):NEXTJ,I:FORI=0TO100:NEXT:CLS0
595 GOSUB990:F=0:U=-1:O(14)=8:O(1)=10:O(13)=4:O(37)=50:SOUND50,1
600 O(34)=50:O(38)=6:GOTO9
602 O$(40)=OP$+O$(40):R=-1:GOSUB966:GOTO222
605 IFS=43 AND L<>6THENZ=-3:GOTO905
610 GOTO895
615 IFS=11 AND O(8)=0THENQ=-1:S=45:GOTO222
625 IFS<>8THEN895
630 FORI=0TO7:SOUND1,1:PRINTTAB(RND(24))"SPUTTER":PRINT:NEXT
635 GOSUB990:PRINTTAB(10)"THEY'RE WET!":GOSUB990:GOTO222
640 IFS=28 OR S=29THEN665
645 IFO(13) OR O(1)<>10 OR L<>10THEN920
650 O(1)=50
655 PRINT"THE MORAY EEL HAS FLED":PRINT"DOWN INTO "D$(6)
660 PRINT"AND OUT A JAGGED HOLE":PRINT"IN THE SIDE!":GOTO9
665 IFNOT F THEN885
670 IFS<>28 AND S<>29 THEN920
675 IFS=28THEN690
680 E=-1:FORI=12TO1STEP-1:SOUNDI,1:NEXT:A$="FFFFFFFFFTTT*** ":FORI=1TO15:PRINT@510-I,LEFT$(A$,I);:GOSUB689:NEXT
681 FORI=14TO0STEP-1:PRINT@480+I,A$;:GOSUB689:NEXT:FORI=15TO1STEP-1:PRINT@480,RIGHT$(A$,I);:NEXT
685 GOSUB990:S=45:GOTO222
689 FORJ=0TO25:NEXT:RETURN
690 A$="WHOOOSSHH":FORI=0TO16:PRINT:NEXT:FORI=1TO10:PRINTTAB(14)MID$(A$,I,1):SOUND200,1:NEXTI:FORI=0TO16:PRINT:NEXT:FORJ=2TO5
691 FORI=1TO12:SOUNDI,1:NEXTI,J
700 PRINT"YOU JUST STARTED WORLD WAR III!":PRINT:PRINT"THERE'S NOTHING FOR YOU TO GO":PRINTTAB(9)"BACK TO NOW!":GOSUB990
701 GOTO930
725 PRINT:PRINT"I DON'T UNDERSTAND "Z$:PRINT:GOTO40
730 IFNOT(F)THEN885
731 PRINT:PRINT"  CORE TEMP."T"% OF MELTDOWN":PRINT:GOTO9
740 CLS:FORI=1TO8:PRINT:NEXT:PRINTTAB(7)"CORE TEMPERATURE":PRINTTAB(13)T"%":PRINTTAB(3)"OF SECONDARY CRITICALITY":GOSUB750
741 PRINT:S=44:PRINT@480,;:GOTO222
750 FORI=1TO16:PRINT@193,"alert ALERT alert ALERT alert";:SOUND50,1:PRINT@193,"ALERT alert ALERT alert ALERT";
751 POKE49151,64:FORZZ=1TO100:NEXT:NEXT:RETURN
755 IFF THENPRINT"DEPTH READING: SEA BOTTOM":GOTO9
760 GOTO885
765 IFF THEN875
770 PRINT"ACCESS PANEL OPEN--":PRINT"ONE CIRCUIT BOARD IS MISSING":GOTO9
775 J=0:IFO(15)=52THENPRINTY$:PRINTO$(18):J=1
780 IFJ=0THEN875
781 GOTO9
790 PRINT"THERE'S FISH, A HUNGRY LOOKING  MORAY EEL AND A LOT OF WATER    OUT THERE!":GOTO9
800 PRINT"***danger***":PRINT"HIGHLY CORROSIVE BATTERY ACID!":GOTO9
805 J=0:PRINTY$:IFO(4)>49THENO(4)=55:PRINTO$(7):J=1
810 IFO(5)>49THENO(5)=55:PRINTO$(8):J=1
815 IFJ=0THEN875
816 GOTO9
825 J=0:PRINTY$:IFLO=0THEN875
830 IFO(7)>49THENO(7)=56:PRINTO$(10):J=1
835 IFO(8)>49THENO(8)=56:PRINTO$(11):J=1
840 IFJ=0THEN875
841 GOTO9
850 PRINTY$:PRINT"TRIDENT NUCLEAR MISSILES":PRINT"THROUGH A WINDOW IN A LOADING   HATCH":GOTO9
855 IF NOT D THEN875
860 IFO(6)>49THENO(6)=56:PRINTO$(9):GOTO9
865 GOTO875
870 IFNOT E THENPRINTY$:PRINT"TORPEDOES":GOTO9
875 PRINT"NOTHING SPECIAL":GOTO9
880 PRINT"I DON'T SEE THAT HERE!":GOTO9
885 PRINT"NO RESPONSE--":PRINT"SHIP'S COMPUTER NOT FUNCTIONING":GOTO9
895 PRINT"YOU CAN'T DO THAT!":GOTO9
900 PRINT"OH NO! YOU SPILLED":PRINT"   ***acid***":PRINT"ALL OVER YOURSELF!":GOTO935
905 S=45:GOTO222
910 CLS:SOUND240,5:FORI=1TO15:PRINT@RND(470),"-ZAP-";:SOUND200,1:NEXT:GOTO935
915 PRINT"LEAKY PIPES DRIPPING":PRINT"COLD SEA-WATER":GOTO9
920 PRINT"NO EFFECT!":GOTO9
925 PRINTTAB(3)"CORE TEMPERATURE CRITICAL!":PRINTTAB(7)"MELTDOWN IMINENT!":RETURN
926 RETURN
930 SOUND1,1:CLS:FORI=0TO25:PRINT@(RND(470)),"BOOM";:SOUND1,10:GOSUB689:NEXT
935 SOUND30,1:PRINT@362,"YOU'RE DEAD!";:SOUND1,1:FORI=0TO20:NEXT:PRINT@362,"you're dead!";
936 FORI=0TO20:NEXT:GOTO935
940 PRINT:PRINT"YOU DON'T HAVE THAT!":GOTO9
945 PRINT"OH NO! YOU'RE DROWNING!":GOSUB990:GOSUB990
950 CLS:FORI=1TO25:SOUND26-I,1:PRINT@RND(470),"-GLUB-";:NEXT:GOTO935
955 PRINT"SIZZLE.....":PRINT"THAT WON'T HELP!":GOTO9
960 CLS:FORI=1TO10:PRINT@RND(470),"*CHOMP*";:SOUND20,3:SOUND1,3:GOTO935
965 O$(39)=OP$+"HATCH":GOSUB966:GOTO222
966 :FORI=1TO12:SOUND200,1:NEXT:RETURN
970 A=1:A$="F9CEAAABLAAHAABA7BAAAAKAAADABLAAAKAAJFA;IDAA":FORI=1TO11:FORJ=0TO3:L(I,J)=ASC(MID$(A$,A,1))-65:A=A+1:NEXTJ,I
975 A$="sFFsssssHHHEsGsKBBBCCEFGIJCDDKKLLLGFBs":FORI=1TO38:O(I)=ASC(MID$(A$,I,1))-65:NEXT:RETURN
990 FORI=0TO1500:NEXT:RETURN
995 CLS:PRINT@262,"DON'T GET THE BENDS";:PRINT@297,"ON THE WAY UP!";
996 FORI=1TO12:SOUNDI,1:NEXT:PRINT@201,"YOU MADE IT!":FORI=12TO1STEP-1:SOUNDI,1:NEXT:PRINT@201,"you made it!":GOTO996
6000 RETURN
6010 CLS:PRINT@64,"**NUCLEAR SUBMARINE  ADVENTURE**";:FORI=1TO32:PRINT"-";:NEXT
6011 PRINT@168,"COPYRIGHT  1980";:PRINT@199,"ROBERT J. RETELLE";:PRINT@291,"PRODUCED UNDER LICENSE BY"
6012 PRINT@322,"AARDVARK TECHNICAL SERVICES"
6013 PRINT@365,"FOR":PRINT@389,"TRS-80 COLOR COMPUTER":POKE49151,64:GOSUB990
6014 CLEAR250:DIMD$(11),O$(41),L(11,3),O(46):GOSUB970:FORI=1TO11:READD$(I):NEXT:FORI=0TO41:READO$(I):NEXT
6015 W=32:GOSUB175:S=5:L=1:J=1:CLS:GOTO222
