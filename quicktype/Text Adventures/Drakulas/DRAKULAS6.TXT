0 CLEAR500:DIMC1,C2,MC,M$,I:MC=16384:GOTO4000
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 FORI=MC+384TO16895:POKEI,32:NEXT:PRINT@384,;:RETURN
5 M$=INKEY$:IFM$=""THEN5
6 RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,;:RETURN
8 I$="":C2=(PEEK(17024)AND1)*256+PEEK(17025)+MC:POKEC2,96:C1=C2-M
9 GOSUB5:IFM$=CHR$(13)THENPOKEC2,32:RETURN
10 IFM$=CHR$(8)THENIFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):POKEC2,32:C2=C2-1:POKEC2,96
11 IFM$<" "ORM$>"^"ORC2>MC+510THEN9
12 POKEC2,ASC(M$)AND63:C2=C2+1:POKEC2,96:I$=I$+M$:GOTO9
20 CLS0
30 IFDU<>1THEN7000
60 A$="IT'S NIGHT"
70 GOSUB3:M$=A$:GOSUB7:PRINT@416,;:M$="? ":GOSUB7:A$="":GOSUB8:E$=I$:IFLEN(E$)=1THEN1300
90 FORI=1TOLEN(E$):IFMID$(E$,I,1)=" "THEN110
100 NEXT:A$="I DON'T UNDERSTAND":GOTO2000
110 V$=LEFT$(E$,I-1):O$=MID$(E$,I+1):IFV$="GET"THENV$="TAKE"
111 IFV$="LOOK"THENV$="EXAM"
120 V$=LEFT$(V$,4):O$=LEFT$(O$,4)

200 IFV$<>"EXAM"THEN410
201 IFDU=1THENA$="TOO DARK":GOTO70
202 IFR<>1THEN260
210 IFO$="PICT"THENA$="WELL, A BIT STRANGE"
220 IF(O$="CARP"ORO$="RUG")ANDA(6)=0THENA$="OH, A KEY!":A(6)=1
230 IFO$="PAIN"THENA$="IT'S COLOURFUL"
240 IFO$="TABL"ANDA(2)=0THENA$="THERE'S A PIECE OF PAPER":A(2)=1
250 IFO$="SAFE"ANDA(27)=2THENA$="THERE'S A BOARD":A(7)=1:A(27)=3
260 IFR<>2THEN290
261 IFO$="SHEL"ANDA(10)<2THENA$="CHISEL":A(10)=1
270 IFO$="SHEL"ANDA(9)<2THENA$=A$+" & BLOOD PRESERVES":A(9)=1
280 IFO$="FLOO"ANDA(14)<2THENA$="THERE'S A NOTE":A(14)=1
285 IFO$="COFF"ANDA(13)<2THENA$="A CROSS":A(13)=1
290 IFR<>3THEN310
291 IFO$="TOIL"ANDA(18)<2THENA$="A HOOK":A(18)=1
300 IFO$="SINK"THENA$="'IN BLOOD LIES THE KEY'"
305 IF(O$="VENT"ORO$="FAN")ANDA(7)=4THEN6000
310 IFR<>4THEN350
311 IF(O$="FRID"ORO$="REFR")ANDA(28)=1THENA$="LAMB LEG":A(20)=1
320 IF(O$="CUPB"ORO$="CABI")ANDA(30)=1THENA$="OH, PIPES AND MATCHES":A(19)=1
330 IF(O$="OVEN"ORO$="STOV")ANDA(21)<2THENA$="A KNIFE":A(21)=1
340 IF(O$="PAPE"ORO$="NOTE")ANDA(30)=0THENA$="NOTE: 'DRINK BLOOD'"

350 IFO$="FLOO"ANDA(24)<2ANDR=5THENA$="A PIECE OF PAPER":A(24)=1
360 IFO$="COFF"ANDA(22)=1ANDR=5THENA$="PLIERS":A(26)=1
370 IFO$="HOLE"ANDA(29)=1ANDR=5THENA$="SCREWDRIVER":A(25)=1
380 IFO$="WIND"THENA$="IT'S RAINING"
390 IFO$="TOWE"ANDA(16)=2THENA$="A MONOGRAM: C.D."
395 IFA$=""THENA$="NOTHING SPECIAL"
396 GOTO2000

410 IFV$<>"TAKE"THEN700

411 IFR<>1THEN500
420 IFO$<>"PAIN"ORA(1)<>0THEN450
421 PRINT@448,;:M$="IT'S NAILED!!!":GOSUB7
425 PRINT@480,;:M$="REMOVE IT WITH WHAT? ":GOSUB7:GOSUB8:E$=I$
430 IFE$<>"PLIERS"ORA(26)<>2THEN1200
440 A$="AAH!! A SAFE!":A(27)=1:A(1)=1
450 IF(O$="PAPE"ORO$="NOTE")ANDA(2)=1THENA$="YOU'LL NEVER FIND THE STONES":A(2)=2
460 IFO$="CAND"ANDA(3)=1THENA$=OK$:A(3)=2
470 IFO$="KEY"ANDA(6)=1THENA$=OK$:A(6)=2
480 IFO$="BOAR"ANDA(7)=1THENA$=OK$:A(7)=2
490 IFO$="VACU"ANDA(8)=1THENA$=OK$:A(8)=2
495 GOTO689

500 IFR<>2THEN610
501 IFO$="BLOO"ANDA(9)=1THENA$=OK$:A(9)=2
510 IFO$="CHIS"ANDA(10)=1THENA$=OK$:A(10)=2
520 IFO$="LOCK"ANDA(12)=1THENA$=OK$:A(12)=2: REM MOVED FROM 685
530 IF(O$="PAPE"ORO$="NOTE")ANDA(14)=1THENA$="1431":A(14)=2
540 IFO$<>"CROS"ORA(13)<>1THEN689
545 GOSUB3:M$="YOU FELL INTO THE COFFIN.":GOSUB7
550 PRINT@416,;:M$="THE DUST IS POISONOUS!":GOSUB7
555 PRINT@448,;:M$="HOW WILL YOU PROTECT YOURSELF?":GOSUB7:PRINT@480,;:M$="? ":GOSUB7
560 GOSUB8:E$=I$:IFRIGHT$(E$,5)<>"TOWEL"ORA(16)<>2THENA$="POISON DUST":GOTO3000
570 GOSUB3:M$="THE COFFIN IS CLOSED. THERE":GOSUB7
575 PRINT@416,;:M$="ARE 4 SCREWS. HOW WILL YOU FREE":GOSUB7:PRINT@448,;:M$="YOURSELF? ":GOSUB7
580 GOSUB8:E$=I$:IFRIGHT$(E$,11)<>"SCREWDRIVER"ORA(25)<>2THENA$="CHOKED":GOTO3000
590 A(13)=2:A$=OK$:GOTO30

610 IFR<>3THEN630
611 IFO$="TOWE"THENA(16)=2:A$=OK$
620 IFO$="HOOK"ANDA(18)=1THENA$=OK$:A(18)=2
621 GOTO689

630 IFR<>4THEN660
631 IFO$="MATC"ANDA(19)=1THENA$=OK$:A(19)=2:A(30)=2
640 IFO$="LAMB"ANDA(20)=1THENA$=OK$:A(20)=2:A(28)=2
650 IFO$="KNIF"ANDA(21)=1THENA$=OK$:A(21)=2
651 GOTO689

660 IF(O$="PAPE"ORO$="NOTE")ANDA(24)=1THENA$="'PAINTINGS OFTEN HIDE VALUABLES'":A(24)=2
670 IFO$="SCRE"ANDA(25)=1ANDR=5THENA$=OK$:A(25)=2:A(29)=2
680 IFO$="PLIE"ANDA(26)=1ANDR=5THENA$=OK$:A(26)=2:A(22)=2
685 IFO$="LOCK"ANDA(12)=1THENA$=OK$:A(12)=2

689 IFA$=""THENA$="YOU CAN'T"
690 GOTO2000

700 IFV$<>"CLIM"ANDV$<>"STAN"THEN780
701 IFR<>1THEN740
710 IFO$<>"TABL"THEN1200
711 IFA(2)<>2THEN730
720 A$=OK$:A(3)=1:GOTO70
730 A$="SLIPPED":GOTO3000
740 IFR<>3THEN1200
745 IFO$<>"TOIL"ANDO$<>"BOAR"THEN1200
750 IFA(7)<>3THENA$="FALLEN INTO TOILET":GOTO3000
751 IFA(11)=1ANDA(15)=1THENA$="DECAPITATION":GOTO3000
760 A(7)=4:A$=OK$:GOTO70

780 IFV$<>"PRES"THEN800
781 IFO$<>"BUTT"ANDO$<>"SWIT"THEN1200
782 IFR<2ORR>3THEN1200
790 IFR=2THENA(11)=-A(11)+1:GOTO795
791 IFR=3THENIFA(11)THENA(15)=-A(15)+1:IFA(15)THENA$="THE STENCH HAS GONE":GOTO2000
795 A$="CLICK":GOTO2000

800 IFV$<>"OPEN"THEN1020
801 IFR<>1THEN820
802 IF(O$="CURT"ORO$="DRAP")ANDA(2)>1ANDR=1ANDA(4)=0THENA$=OK$:A(4)=1:GOTO2000
805 IF(O$="CABI"ORO$="CUPB")ANDA(5)=0THENA(5)=1:A(8)=1:A$="AH, A VACUUM CLEANER":GOTO2000
810 IFO$<>"SAFE"ORA(27)<>1THEN1200
811 PRINT@448,;:M$="WHICH COMBINATION? ":GOSUB7:GOSUB8:E$=I$
815 IFE$="1431"THENA$=OK$:A(27)=2:GOTO820
816 A$="WRONG!"
820 IFR<>4THEN840
821 IF(O$="CUPB"ORO$="CABI")ANDA(30)=0THENA$=OK$:A(30)=1
830 IF(O$="FRID"ORO$="REFR")ANDA(28)=0THENA$=OK$:A(28)=1
840 IFR<>5ORO$<>"COFF"THEN950
850 IFA(22)<>0THEN1200
860 PRINT@448,;:M$="LOCKED! HOW DO YOU OPEN IT? ":GOSUB7:PRINT@480,;:M$="? ":GOSUB7
870 GOSUB8:E$=I$:IFE$<>"LOCKPICK"ORA(12)<>2THEN1200
880 PRINT@72,"ÒÚ";:PRINT@104,"˙qqı";:PRINT@136,"˙ÒÚı";
890 PRINT@168,"˜Ò˙";:PRINT@199,"Ã¿¿√";:PRINT@229,"Ã¿¿√";
900 GOSUB3:M$="COUNT D. SLEEPS HERE. YOU'RE":GOSUB7
910 PRINT@416,;:M$="TRAPPED! HE'S GOING TO BITE YOU!":GOSUB7
920 PRINT@448,;:M$="WHAT WILL YOU DO? ":GOSUB7:GOSUB8:E$=I$
930 IFRIGHT$(E$,5)="CROSS"ANDA(13)=2THENA$=OK$:A(26)=1:A(22)=1:GOTO30
940 A$="SLURP...":GOTO3000
950 IFO$<>"BLOO"ORA(9)<>2THEN1020
960 GOSUB3:M$="OPEN WITH WHAT? ":GOSUB7
980 GOSUB8:E$=I$:IFE$<>"CHISEL"ORA(10)<>2THEN1200
990 PRINT@416,;:M$="AND AS HAMMER? ":GOSUB7:GOSUB8:E$=I$
1000 IFE$<>"LAMB LEG"ORA(20)<>2THEN1200
1010 A$=OK$:A(9)=2:GOTO70
1020 IFV$<>"DRIN"ORO$<>"BLOO"ORA(9)<>2THEN1050
1030 A$="GAG, BUT THERE'S A LOCKPICK!"
1040 A(9)=4:A(12)=1:GOTO70
1050 IF(V$="PLAC"ORV$="DROP")ANDO$="BOAR"ANDR=3ANDA(7)=2THENA$=OK$:A(7)=3
1060 IFV$="LIGH"ANDR=4ANDA(19)=2THENA$="EXPLOSION":GOTO3000
1070 IFV$="LIGH"ANDO$="CAND"ANDA(3)=2ANDA(19)=2THENA$=OK$:DU=2:GOTO20
1080 IFA$=""THENIFV$<>"MOVE"ANDV$<>"PULL"ANDV$<>"REMO"THENA$="I DON'T UNDERSTAND":GOTO2000
1090 IF(O$="CURT"ORO$="DRAP")ANDA(2)>1ANDR=1ANDA(4)=0THENA$=OK$:A(4)=1
1095 IFA$<>OK$THENA$="DOESN'T WORK"
1100 IFO$<>"STON"ORR<>5ORA(29)<>0THEN2000
1110 GOSUB3:M$="LOOSEN IT WITH WHAT? ":GOSUB7:GOSUB8:E$=I$
1140 IFE$<>"KNIFE"ORA(21)<>2THEN1200
1150 A(29)=1:A$="CRUNCH... AH, A HOLE!":GOTO2000
1200 A$="DOESN'T WORK":GOTO2000
1300 IFR<>1THEN1340
1301 IFE$="W"ANDA(4)=1THENR=4:GOTO20
1310 IFE$<>"N"THEN1332
1311 IFA(31)=1THENR=3:GOTO20
1320 PRINT@448,;:M$="HOW SHOULD I OPEN IT? ":GOSUB7:GOSUB8:E$=I$
1330 IFRIGHT$(E$,4)="KNOB"THENA(31)=1:R=3:GOTO20
1331 GOTO1580
1332 IFE$<>"D"THEN1580
1333 IFA(18)=3THENR=2:GOTO20
1334 PRINT@448,;:M$="HOW DO YOU OPEN THE TRAP DOOR?  ":GOSUB7:GOSUB8:E$=I$
1336 IFE$<>"HOOK"ORA(18)<>2THEN1580
1338 A(18)=3:R=2:GOTO20
1340 IFR=2ANDE$="U"THENR=1:GOTO20
1550 IFR=3ANDE$="S"THENR=1:GOTO20
1560 IFR=5ANDE$="E"THENR=4:GOTO20
1570 IFR=4ANDE$="E"THENR=1:GOTO20
1572 IFR<>4ORE$<>"W"THEN1580
1573 IFA(6)=3THENR=5:GOTO20
1574 PRINT@448,;:M$="OPEN IT WITH WHAT? ":GOSUB7:GOSUB8:E$=I$:IFE$<>"KEY"ORA(6)<>2THEN1580
1576 A(6)=3:R=5:GOTO20
1580 A$="NO WAY THROUGH"
2000 IFDU=1THEN2290
2001 IFR<>1THEN2120
2002 IFA(5)=1THENPRINT@142,"öêêï";
2010 IFA(3)=2THENPRINT@15," ";
2020 IFA(2)=2THENPRINT@272,"ˇ";
2030 IFA(6)=1THENPRINT@328,"≤≥∞";:GOTO2040
2031 PRINT@328,"øøø";
2040 IFA(27)>0THENPRINT@148,"ﬁ‹‹ﬂ";:PRINT@180,"⁄––ﬂ";:PRINT@212,"€””ﬂ";
2050 IFA(18)=3THENPRINT@246,"ÅÉÉÉ";:PRINT@278,"äÄÄÖ";:PRINT@310,"äÄÄÖ";:PRINT@342,"äÄÄÄÑ";:PRINT@375,"ÄÄÄÄÑ";
2070 IFA(31)=1THENPRINT@136,"éååç";:PRINT@168,"äÄÄÖ";:PRINT@200,"äÄÄÖ";
2090 IFA(4)=1THENPRINT@97,"ÁÌ‚";:PRINT@129,"Í‡Í";:PRINT@161,"Í‡Í";
2100 IFA(4)=1THENPRINT@193,"Í‡Í";:PRINT@225,"Í‡Í";:PRINT@257,"Í‡È";:PRINT@289,"Í‡·";:PRINT@321,"È";
2120 IFR<>2THEN2160
2121 IFA(11)=1THENPRINT@242,;:M$="*ON ":GOSUB7:GOTO2130
2122 PRINT@242,;:M$="*OFF":GOSUB7
2130 IFA(14)>1THENPRINT@371,"∞";
2135 IFA(13)>1THENPRINT@330,"£";
2140 IFA(10)>1THENPRINT@146,"∞∞∞∞";
2150 IFA(9)>1THENPRINT@137,"∞∞∞∞∞∞";
2160 IFR<>3THEN2200
2161 IFA(15)=1THENPRINT@82,">‹‹‹‹‹‹‹<";
2170 IFA(15)=0THENPRINT@82," ‘‹‹‹‹‹ÿ ";
2180 IFA(16)>1THENPRINT@161,"èå";:PRINT@192,"åÉè";:PRINT@225,"èè";:PRINT@257,"èè";:PRINT@289,"èè";
2200 IFR<>4THEN2260
2210 IFA(30)>0THENPRINT@85,"üúúö";:PRINT@117,"ü≥æö";:PRINT@149,"üììö";
2220 IFA(28)>0THENPRINT@202,"œ¿¿";:PRINT@234,"œ√√";
2230 IFA(28)>0THENPRINT@266,"œ¿¿";:PRINT@298,"œ√√";
2240 IFA(6)>2THENPRINT@98,"∞";:PRINT@130,"∞∞";:PRINT@162,"∞∞";:PRINT@194,"∞∞";
2250 IFA(6)>2THENPRINT@226,"∞∞";:PRINT@258,"∞∞";:PRINT@290,"∞∞";:PRINT@322,"∞";
2260 IFR<>5ORA(29)=0THEN2290
2270 PRINT@155," ";:PRINT@187," ";
2280 IFA(25)<2THENPRINT@155,"I";:PRINT@187,"!";
2290 M=M+5:IFM=60THENM=0:H=H+1
2300 MI$=MID$(STR$(M),2):IFM=5THENMI$="05":GOTO2310
2301 IFM=0THENMI$="00"
2310 H$=STR$(H):PRINT@18,H$":"MI$;
2320 IFH=3THENA$="TIME'S UP":GOTO3000
2330 IFH=18ANDM=0THENGOSUB3:M$="IT'S GETTING DARK!":GOSUB7:FORI=0TO1500:NEXT
2340 IFH=20ANDM=0THENDU=1
2350 IFH<>24ORM<>0THEN70
2360 M=5
2370 PRINT@139,"œœ";:PRINT@170,"≈OO ";:PRINT@202,"≈œœ ";
2380 PRINT@234,"œœœœ";:PRINT@266,"œœœœ";
2390 GOSUB3:M$="AHHH, A GHOST! WHAT DO YOU":GOSUB7
2400 PRINT@416,;:M$="DEFEND YOURSELF WITH? ":GOSUB7
2410 GOSUB8:E$=I$
2420 IFLEFT$(E$,6)<>"VACUUM"ORA(8)<>2THEN2500
2430 A$="SAVED":GOTO20
2500 A$="GHOST"
3000 CLS0:GOSUB3:IFA$="TIME'S UP"THENPRINT@170,"YOU FAILED!";:GOTO3035
3020 PRINT@168,"√À¬";:PRINT@181,"√À¬";:PRINT@200,"¿ ";:PRINT@213,"¿ ";
3030 PRINT@171,"YOU DIED.";
3035 PRINT@384,;:M$="REASON:":GOSUB7:PRINT@416,;:M$=A$:GOSUB7:PRINT@448,;:M$="(HEHE...)":GOSUB7
3040 PRINT@480,;:M$="TRY AGAIN (Y/N)? ":GOSUB7
3050 M$=INKEY$:IFM$="N"THENEND
3060 IFM$<>"Y"THEN3050
3061 RUN4300
4000 GOSUB5000
4010 PRINT:PRINT:PRINTTAB(7)"(C) 1983 AND 1985":PRINT
4020 PRINT:PRINT" C. SEIBT AND WOLFGANG WESEMANN"
4040 FORI=0TO2E3:NEXT:CLS
4050 M$="ALL HELL'S BREAKING LOOSE AT INTERPOL: THE VALUABLE DIAMONDS BELONGING TO BILLIONAIRE NJ SIRMEL'S WIFE "
4065 M$=M$+"DISAPPEARED THREE DAYS AGO.":GOSUB1
4070 M$="MRS. SIRMEL WAS FOUND DEAD ON MONDAY MORNING, ROBBED OF BOTH HER BLOOD AND HER JEWELRY. "
4090 M$=M$+"SHE WAS LYING ON HER PERSIAN RUG, THE WINDOW OPEN AND THE PERPETRATOR GONE.":GOSUB1
4100 M$="SUSPICION HAS IMMEDIATELY FALLEN ON THE YOUNG COUNT D. FOR THE FOLLOWING REASONS:":GOSUB1:GOSUB4990
4110 CLS:PRINT:M$="1. HE HAS NEITHER AN ALIBI, NOR AN EXPLANATION FOR HIS RECENT DISAPPEARANCE.":GOSUB1:PRINT
4125 M$="2. SINCE HE BECAME HEIR TO HIS ESTATE IN TRANSYLVANIA, HE'S HAD TO PAY A "
4130 M$=M$+"HORRENDOUS INHERITANCE TAX AND IS NOW IN VERY SERIOUS DEBT.":GOSUB1:GOSUB4990
4160 CLS:PRINT:M$="TODAY THE COUNT LIVES IN A 4.5-ROOM APARTMENT IN A petty-bourgeois area.":GOSUB1:PRINT
4175 M$="IT IS SUSPECTED THAT HE HAS THE LOOT THERE. "
4185 M$=M$+"YOU ARE ASSIGNED TO SEARCH THE APARTMENT.":GOSUB1:PRINT
4190 M$="COMMANDS ARE MADE IN 2 WORDS:":GOSUB1
4195 PRINT"NOUN (SPACE) VERB"
4200 PRINT:PRINT"E.G. EXAMINE WINDOW":PRINT:GOSUB4990
4210 CLS:PRINT:M$="NOUN AND VERB CAN BE SHORTENED TO 4 LETTERS EACH WITH A SPACE "
4225 M$=M$+"BETWEEN THEM.":GOSUB1:PRINT
4230 M$="IF THE COMPUTER ASKS FOR AN OBJECT, THE NAME MUST ALWAYS BE ENTERED IN FULL.":GOSUB1:PRINT
4245 M$="COMMANDS SUCH AS THE FOLLOWING ARE AVAILABLE:":GOSUB1:PRINT:GOSUB4990
4250 CLS:PRINT:PRINT"-EXAMINE","-TAKE":PRINT
4252 PRINT"-CLIMB (ON)","-DRINK":PRINT
4254 PRINT"-OPEN","-LIGHT":PRINT
4256 PRINT"-MOVE","-PRESS":PRINT
4258 PRINT"-PLACE","-N,S,E,W,U,D":PRINT:PRINT:GOSUB4990
4300 CLEAR500:DIMC1,C2,MC,M$,I,A(31):MC=16384:R=1:OK$="OK":H=16:H$="16:00":GOTO20
4990 PRINT:PRINTTAB(10)"PRESS <enter>";
4995 IFINKEY$<>CHR$(13)THEN4995
4996 RETURN
5000 CLS:M$="          THE DIAMONDS          ":GOSUB7:PRINT
5010 PRINTTAB(11)"OF COUNT D.":RETURN
6000 GOSUB5000:FORC1=MC+480TOMC+511:POKEC1,32:NEXT
6020 PRINT:PRINT"CONGRATULATIONS!!! IT'S DONE!"
6030 PRINT"YOU SUCCEEDED IN FINDING THE"
6040 PRINT"DIAMONDS.":GOTO3040
7000 CLS0:IFR<>1THEN8000
7010 PRINT"ÉååèèèèèèèèIè∞èIèèèèèèèèèèèèèååÉ";
7020 PRINT"èèèÉååèèèèèÉÉÉÉÉèèèèèèèèèèååÉèèè";
7030 PRINT"éåèèèèÅÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÇèéåÉÇè";
7040 PRINT"äÔ„·çèÖèèèèèèèììììèèèèèèèäèÖäèäè";
7050 PRINT"äèèÖäèÖèéååçèèöïöïèè—”„“èäèÖäèàè";
7060 PRINT"äÔÔÂÍèÖèäwcÖèèúúúúèè’◊Ø⁄èäèÑÇÉäè";
7070 PRINT"äèèÖäèÖèä∞ëÖèèèèèèèèîúúòèäèÖäèäè";
7080 PRINT"äÔÔÂÍèÖèä∞∞ÖååååååèèèèèèèäèÖäèäè";
7090 PRINT"äèèÖäèÑåà∞∞ÑÙˇˇˇœˇÑååååååàèãÉåàè";
7100 PRINT"äÔÔÂËÇ∑øøøøø±±≥≥≥≤≤øªçèèèèÉåèèèè";
7110 PRINT"äèåÉè±øøøøøøø∑øøøøªøøªäÉÉÉÜèÉåèè";
7120 PRINT"àÉèèäºººººººººººººººººèÖèèèÜèèÉå";
7190 PRINT:A$="LIVING ROOM":GOTO2000
8000 IFR<>2THEN9000
8010 PRINT"º≥∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞µ∞∞∞∞∞∞≥º";
8020 PRINT"∞∞º≥∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∫∞∞∞∞≥º∞∞";
8030 PRINT"∞∞∞∞º≥∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞µ∞∞∞≥º∞∞∞∞";
8040 PRINT"∞∞∞∞∞∞æººººººººººººººººª≥∑∞∞∞∞∞∞";
8050 PRINT"∞∞∞∞∞∞∫∞∞º∞º∞º∞∞∞∞””mm∞ª≥∑∞∞∞∞∞∞";
8060 PRINT"∞∞∞∞∞∞∫∞¸¸¸¸¸¸¸∞∞¸¸¸¸¸∞ª≥∑∞∞∞∞∞∞";
8070 PRINT"∞∞∞∞∞∞∫∞££££££∞∞∞°££££¢ª≥∑∞∞∞∞∞∞";
8080 PRINT"∞∞∞∞∞∞∫•∞∞∞∞∞∞™∞∞•jAUS™ª≥∑∞∞∞∞∞∞";
8090 PRINT"∞∞∞∞∞∞∫∞´£££££ß∞ê§¨¨¨¨®ª≥∑∞∞∞∞∞∞";
8100 PRINT"∞∞∞∞∞∞∫ß££££££Ø≥≥≥≥≥≥≥≥∫∞µ∞∞∞∞∞∞";
8110 PRINT"∞∞∞≥ºº∞´££+££ß®∞∞∞∞∞∞∞∞∞∞∞ºº≥∞∞∞";
8120 PRINT"≥ºº∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞º∞∞∞∞∞∞∞∞∞ºº≥";
8140 A$="CELLAR":GOTO2000
9000 IFR<>3THEN10000
9010 PRINT"ÉååèèèèèèèèèèèèèèèèèèèèèèèèèèååÉ";
9020 PRINT"èèèÉååèèèèèèèèèèèèèèèåﬂåèèååÉèèè";
9030 PRINT"èèèèèèÅÉÉÉÉÉÉÉÉÉÉÉÉ‘‹∞‹–‹ÿèèèèèè";
9040 PRINT"èèèèèèÖèèèèèèèèäìììÖèèèèèäèèèèèè";
9050 PRINT"èèèèèèÖèèè≈ÕèèèäüüüÖèèèèèäèèèèèè";
9060 PRINT"èå¡ÅèèÖè√√«√√√¬äúúúÖèèèèèäèèéåèè";
9070 PRINT"à«œÖèèÖè≈∞∞∞∞≈èèèèèèèèèèèäèèäèèè";
9080 PRINT"äœœÖèèÖèƒÀ√√√Œèèèèèèè√ŒÃÃÕ√èäèèè";
9090 PRINT"äCDÖèèÑååååååååååååå≈∞∞∞∞∞≈èäœèè";
9100 PRINT"ä»Éèå”ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂÕÃ√√Ã åàèèè";
9110 PRINT"èèå”ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ…∞∞«ﬂﬂﬂåèè";
9120 PRINT"å”ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂÃÃﬂﬂﬂﬂﬂ”å";
9170 A$="BATHROOM":GOTO2000
10000 IFR<>4THEN11000
10010 PRINT"ÉååèèèèèèèèèèèèèèèèèèèèèèèèèèååÉ";
10020 PRINT"èèèÉååèèèèèèèèèèèèèèèèèèèèååÉèèè";
10030 PRINT"èèèèèäÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉûúúöÇèèèèèè";
10040 PRINT"èäÛåçäèèèèèèèèèèèèèèèûœúöäèèåÄÖè";
10050 PRINT"èäˇˇÖäèèèè¡√√¬èèèèèèèõììöäèèÄÄÖè";
10060 PRINT"èäˇˇÖäèèèäœœœ≈èèèéåååååååàèèÄÄÖè";
10070 PRINT"èäˇˇÖäèèèä√√¬œèèèä∆ÀÕ∆ÀçèãçèÄÄÖè";
10080 PRINT"èäˇˇÖäåååäœI œèèèäœ¡√√√ÅÉÉÇèÄÄÖè";
10090 PRINT"èäˇˇÖäåÑààœœ ŒåååàÕ≈¿¿≈ÖèèäèÄÄÖè";
10100 PRINT"èäˇˇÑÉèèèäÃÃ»áèèèèÇƒÃÃÃÑååàåÄÄÖè";
10110 PRINT"èä¸ÉèèèèèèèèèèèèèèèèèèèèèèèèãÇÖè";
10120 PRINT"åÉèèèèèèèèèèèèèèèèèèèèèèèèèèèèÉå";
10140 A$="KITCHEN":GOTO2000
11000 PRINT"∞∞’∞∞∞∞∞⁄∞∞∞’”””””⁄∞∞∞∞⁄∞∞∞⁄∞∞⁄∞";
11010 PRINT"∞∞’∞∞∞””€”‹‹‹’∞∞∞∞›‹‹”◊∞∞∞∞⁄∞’∞∞";
11020 PRINT"∞∞’”ﬁ‹⁄’∞∞∞∞∞⁄∞∞∞∞ﬁ∞∞’∞‹‹”◊∞∞’∞∞";
11030 PRINT"∞”‹∞€”€◊∞∞∞∞∞€””””⁄∞∞’∞∞∞’∞‹”∞⁄∞";
11040 PRINT"÷’∞∞⁄∞⁄’””‹‹ﬁ∞∞∞⁄∞‹‹›◊”∞∞’∞Ø∞ﬁ€∞";
11050 PRINT"∞∞⁄∞€”‹‹√√√∞’∞∞’∞∞∞∞’∞∞‹›◊∞Ø∞⁄∞Ÿ";
11060 PRINT"∞∞€‹∞∞√Ã∞∞√ ∞⁄∞⁄∞∞∞∞∞⁄∞∞’∞‹”∞⁄∞∞";
11070 PRINT"—›∞∞√Ã∞∞√Ã∞ ∞€”€””∞∞’∞∞∞⁄∞∞’‹€∞∞";
11080 PRINT"ÿ’∞≈ÃÃÃŒ∞∞¡»‹∞∞∞∞∞‹‹‹””∞⁄∞∞’∞∞Ÿ∞";
11090 PRINT"∞∞⁄≈∞x∞ ¡∆»∞∞∞∞∞∞∞∞∞∞∞∞‹‹”∞’∞∞∞Ÿ";
11100 PRINT"∞∞€≈√√√Œ»∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞‹◊∞∞∞∞";
11110 PRINT"∞÷∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞∞Ÿ∞∞∞";
11130 A$="TOWER ROOM":GOTO2000
11140 REM MC-EDITS J. GERRIE
