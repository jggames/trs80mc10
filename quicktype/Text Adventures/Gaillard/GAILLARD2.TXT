0 CLS:DIMZ,CC,P,M$:GOTO10
1 Z=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,Z,CC-Z):Z=CC+1:CC=Z+31:IFZ<=LEN(M$)THEN2
4 RETURN
10 CLS:PRINT"CHATEAU GAILLARD"
20 GOSUB6800
40 GOSUB50:GOTO60
50 FORZ=1TO7500:NEXTZ:RETURN
51 FORZ=1TO1500:NEXTZ:RETURN
60 CLS
70 GOSUB3300
80 IFA(RO,7)<>0ORA(RO,9)<>0ORA(RO,10)<>0THENGOSUB6660
90 IFA(RO,7)>98THENPRINT"ONE OF THE DOORS IS LOCKED":PRINT"PREVENTING YOU FROM EXPLORING":PRINT"FURTHER"
100 IFA(RO,8)=0THEN110
101 PRINT"LOOK OUT!"
102 PRINT"THERE IS A ";M$(A(RO,8));" HERE!"
103 IFRND(0)>.7ANDA(RO,8)<>1THENPRINT"THE ";M$(A(RO,8));" ATTACKS!":KW=1:GOSUB1400:GOTO40
110 IFST<1 THEN ST=0:GOTO120
111 IFRND(0)>.84THENST=ST-1
120 IFCH<1THENCH=0:GOTO130
121 IFRND(0)>.84THENCH=CH-1
130 IFDE<1THENDE=0:GOTO140
131 IFRND(0)>.84THENDE=DE-1
140 IFIT<1THENIT=0:GOTO150
141 IFRND(0)>.84THENIT=IT-1
150 IFWI<1THENWI=0:GOTO160
151 IFRND(0)>.84THEN WI=WI-1
160 IFCO<1THENCO=0:GOTO170
161 IFRND(0)>.84THENCO=CO-1
170 IFRND(0)>.84ANDRO=16ANDA(RO,8)=1THEN175
171 GOTO180
175 PRINT:PRINT"YOU HEAR A WHISPERED WARNING:":PRINT"'DO SOMETHING ABOUT THE DWARF'"
180 PRINT:PRINT"YOUR ATTRIBUTES ARE:"
190 PRINT"STR"ST,"CHA"CH
200 PRINT"DEX"DE,"INT"IT
210 PRINT"WIS"WI,"CON"CO
220 IFST*CH*DE*IT*WI*CO=0THENPRINT"YOU ARE EXHAUSTED...":PRINT"SO THIS ADVENTURE MUST END":QU=2:GOTO2590   
230 FL=0
240 FORJ=1TO5
250 IFP(J)<>0THENFL=1
260 NEXTJ
270 IFFL=0THEN340
280 CA=0
290 PRINT:PRINT"YOU ARE CARRYING:"
300 FORJ=1TO5
310 IFP(J)<>0THENPRINTO$(P(J)):CA=CA+V(P(J))
320 NEXT J
330 IFCA>0THENPRINT"TOTAL VALUE $";STR$(CA)
340 PRINT
370 KW=0
380 GOSUB51
390 INPUT"WHAT DO YOU DO";A$
400 IFA$=""THEN390
410 AS=ASC(A$)
430 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO430
440 B$=LEFT$(A$,3)
450 IF B$="HEL" THEN 455
451 GOTO460
455 PRINT"NO HELP FOR THE MORTALS HERE!":PRINT"ALTHOUGH READING & DRINKING":PRINT"MAY HELP...":GOTO40
460 IFB$="QUI"THENQU=4:GOTO2530
470 N=1
480 IFMID$(A$,N,1)=" "THEN485
481 GOTO490
485 C$=MID$(A$,N+1,3):IF LEFT$(C$,1)<>" " THEN 530
486 GOTO500
490 IFN<M THEN N=N+1:GOTO480
500 IFRND(0)>.5 THEN 520
510 PRINT"BY ITSELF,";A$:PRINT"CAN'T BE ACTED ON":GOTO390
520 PRINT A$;" IS JUST ONE WORD":PRINT"I NEED TWO":GOTO390
530 IFRO<>8 AND RO<>34 THEN 560
540 IFB$="STA" OR B$="KIL" OR B$="FIG" OR B$="KIC" OR B$="PUN" OR B$="SLA" OR B$="ATT" THEN KW=1:GOSUB1400
550 IFA(RO,7)>98 AND B$<>"UNL" THEN PRINT"** THE DOORS ARE LOCKED **":GOTO40
560 IFB$="STA"OR B$="KIL" OR B$="FIG" OR B$="KIC" OR B$="PUN" OR B$="SLA" OR B$="ATT" THEN KW=1:GOSUB1400
570 IFB$="GO "OR B$="MOV" OR B$="CLI" OR B$="RUN" OR B$="WAL" THEN KW=1:GOSUB740
580 IFB$="TAK"OR B$="GET" OR B$="STE" OR B$="LIF" THEN KW=1:GOSUB950
590 IFB$="DRO"OR B$="PUT" OR B$="THR" OR B$="BRE" THEN KW=1:GOSUB1170
600 IFB$="UNL"THEN KW=1:GOSUB2360
610 IFB$="OPE"THEN KW=1:GOSUB2660
620 IFB$="REA"THEN KW=1:GOSUB2750
630 IFB$="DRI"OR B$="SWA" THEN KW=1:GOSUB2870
640 IFB$="BRI"OR B$="PAY" THEN KW=1:GOSUB3000
650 IFKW=1THEN700
660 R=INT(RND(0)*3)
670 IFR=0THENPRINT"IT WOULD NOT BE WISE TO":PRINTA$
680 IFR=1THENPRINT"ONLY A FOOL WOULD TRY TO":PRINTA$
690 IFR=2THENPRINT"I DON'T UNDERSTAND:":PRINTA$
700 GOSUB50
710 GOTO40
740 IFRO<>16ORA(16,8)<>1THEN800
760 PRINT:PRINT"THE DWARF REFUSES TO LET" 
770 PRINT"YOU PROCEED..."
780 GOSUB50
790 RETURN
800 C$=LEFT$(C$,1)
810 IFC$="N"ANDA(RO,1)=0THENPRINT"YOU CANNOT GO THAT WAY":RETURN
820 IFC$="S"ANDA(RO,2)=0THENPRINT"THERE IS NO EXIT SOUTH":RETURN
830 IFC$="E"ANDA(RO,3)=0THENPRINT"I SEE NOWHERE TO THE EAST TO GO":RETURN
840 IFC$="W"ANDA(RO,4)=0THENPRINT"YOU CANNOT WALK THROUGH WALLS":RETURN
850 IFC$="U"ANDA(RO,5)=0THENPRINT"THERE IS NO WAY TO MOVE UP":RETURN
860 IFC$="D"ANDA(RO,6)=0THENPRINT"YOU CANNOT DESCEND FROM HERE":RETURN
870 IFC$="N"THENRO=A(RO,1)
880 IFC$="S"THENRO=A(RO,2)
890 IFC$="E"THENRO=A(RO,3)
900 IFC$="W"THENRO=A(RO,4)
910 IFC$="U"THENRO=A(RO,5)
920 IFC$="D"THENRO=A(RO,6)
930 RETURN
960 FL=0
970 FORJ=1TO5
980 IFP(J)<>0THENFL=FL+1
990 NEXTJ
1000 IFFL=5 THEN PRINT"YOU ARE ALREADY CARRYING YOUR":PRINT"MAXIMUM OF FIVE OBJECTS":RETURN
1010 IFC$="CHE" THEN PRINT"IT IS FAR TOO HEAVY TO LIFT":RETURN
1020 IFA(RO,7)=0 AND A(RO,9)=0 AND A(RO,10)=0 THEN PRINT"I SEE NOTHING TO PICK UP":RETURN
1030 D$="":E$="":F$="":N=0
1040 D$=LEFT$(O$(A(RO,7)),3)
1050 E$=LEFT$(O$(A(RO,9)),3)
1060 F$=LEFT$(O$(A(RO,10)),3)
1070 IFC$=D$THENN=A(RO,7):A(RO,7)=0
1080 IFC$=E$THENN=A(RO,9):A(RO,9)=0
1090 IFC$=F$THENN=A(RO,10):A(RO,10)=0
1100 J=1
1110 IFP(J)=0THENP(J)=N:GOTO1130
1120 IFJ<5THENJ=J+1:GOTO1110
1130 IFN>0THENPRINT"YOU NOW HAVE THE ";O$(N)
1140 RETURN
1170 FL=0
1180 FOR J=1 TO 5
1190 IF P(J)<>0 THEN FL=1
1200 NEXT J
1210 IF FL=0 THEN PRINT"YOU ARE NOT CARRYING ANYTHING":RETURN
1220 IF A(RO,7)<>0 AND A(RO,9)<>0 AND A(RO,10)<>0 THEN1225
1221 GOTO1230
1225 PRINT"THIS ROOM ALREADY HOLDS ITS":PRINT"MAXIMUM # OF OBJECTS":RETURN
1230 D$="":D=0
1240 FOR J=1 TO 18
1250 IF LEFT$(O$(J),3)=C$ THEN D$=O$(J):D=J
1260 NEXT J
1270 IF D$="" THEN PRINT"HOW CAN YOU WHEN YOU'RE":PRINT"NOT HOLDING IT?":RETURN
1280 FOR J=1 TO 5
1290 IF P(J)=D THEN P(J)=0
1300 NEXT J
1310 IFA(RO,7)=0 THEN A(RO,7)=D:GOTO1340
1320 IFA(RO,9)=0 THEN A(RO,9)=D:GOTO1340
1330 IFA(RO,10)=0 THEN A(RO,10)=D
1340 IFB$="DRO" THEN PRINT"YOU HAVE DROPPED THE ";D$
1350 IFB$="PUT" THEN PRINT"YOU HAVE PUT THE ";D8;" DOWN"
1360 IFB$="THR" THEN PRINT"WITH A MIGHTY HEAVE, YOU":PRINT"THROW THE ";D$;" AWAY"
1370 IFB$="BRE" THEN PRINT"YOU HAVE BROKEN THE ";D$
1380 RETURN
1400 IFA(RO,8)=1 THEN PRINT"THE DWARF REFUSES TO FIGHT":PRINT"AND HIS MAGIC PROTECTS HIM":RETURN
1420 IFA(RO,8)<>0 THEN 1470
1430 R=RND(0)
1440 IFR<.5 THEN PRINT"THERE IS NOTHING TO FIGHT HERE"
1450 IFR>=.5 THEN PRINT"YOU CAN'T FIGHT EMPTY AIR!"
1460 RETURN
1470 G$=M$(A(RO,8))
1480 S1=RND(16)+2:H1=RND(16)+2:D1=RND(16)+2:I1=RND(16)+2:W1=RND(16)+2:C1=RND(16)+2
1540 PRINT"--------------------------------";
1550 PRINT"YOUR OPPONENT IS A ":PRINTG$
1560 MT=0:HT=0
1570 FF=S1*(INT(RND(0)*6)+1)
1580 PRINT"--------------------------------";
1590 PRINT"THE ";G$;"'S DANGER":PRINT"LEVEL IS"FF
1600 GOSUB51
1610 FORJ=1TO5
1620 T(J)=0
1630 IFP(J)=1THENPRINT"YOUR AXE COULD BE HANDY":T(J)=1
1640 IFP(J)=2THENPRINT"YOUR SKILL WITH THE SWORD":PRINT"MAY STAND YOU IN GOOD STEAD":T(J)=2
1650 IFP(J)=3THENPRINT"YOUR DAGGER IS USEFUL AGAINST ":PRINTG$;"S":T(J)=3
1660 IFP(J)=4THENPRINT"THE MACE WILL MAKE SHORT WORK":PRINT"OF IT":T(J)=4
1670 IFP(J)=5THENPRINT"YOUR QUARTERSTAFF WILL GIVE":PRINT"IT NO QUARTER...":T(J)=5
1680 IFP(J)=6THENPRINT"YOUR MORNING STAR MAY INFLICT":PRINT"HEAVY WOUNDS ON THE":PRINT G$:T(J)=6
1690 IFP(J)=7THENPRINT"A FALCHION IS A USEFUL WEAPON":T(J)=7
1700 NEXTJ
1710 FL=0
1720 FORJ=1TO5
1730 IFT(J)<>0THENFL=FL+1
1740 NEXTJ
1750 IFFL=0THENPRINT"YOU MUST FIGHT THE ";G$:PRINT"WITH YOUR BARE HANDS":GOTO1910
1760 IFFL>1THEN1830
1770 FORJ=1TO5
1780 IFT(J)<>0THENFL=T(J)
1790 NEXTJ
1800 PRINT"YOU MUST FIGHT WITH YOUR ":PRINTO$(FL)
1810 FF=INT(FF*2/FL)
1820 GOTO1910
1830 PRINT"CHOOSE YOUR WEAPON:"
1840 FORJ=1TO5
1850 IFP(J)<>0THENPRINTJ;" - ";O$(J)
1860 NEXTJ
1870 INPUT"ENTER THE NUMBER TO CHOOSE";J
1880 IFP(J)=0 THEN PRINT"YOU DO NOT HAVE THE ";O$(J):GOTO1870
1890 PRINT"RIGHT, SO YOU CHOOSE TO FIGHT":PRINT"WITH THE ";O$(J)
1900 FF=INT(FF*2/J)
1910 GOSUB51
1920 PRINT"THE ";G$;" HAS":PRINT"THE FOLLOWING ATTRIBUTES:"
1930 PRINT"1 STR"S1,"2 CHA";H1
1940 PRINT"3 DEX"D1,"4 INT"I1
1950 PRINT"5 WIS"W1,"6 CON"C1
1960 PRINT:PRINT"YOUR ATTRIBUTES ARE:"
1970 PRINT"1 STR"ST,"2 CHA"CH
1980 PRINT"3 DEX"DE,"4 INT"IT
1990 PRINT"5 WIS"WI,"6 CON"CO
2000 PRINT:PRINT"WHICH ATTRIBUTES WILL YOU":PRINT"FIGHT WITH (2)";
2010 INPUT Z,Q
2020 IFZ<1ORQ<1ORZ>6ORQ>6ORZ=QTHEN PRINT"DO NOT FOOL AROUND WHEN A ";G$:PRINT"IS IN THE ROOM WITH YOU!":GOTO2010
2030 IFZ=1ORQ=1THENMT=MT+S1:HT=HT+ST
2040 IFZ=2ORQ=2THENMT=MT+H1:HT=HT+CH
2050 IFZ=3ORQ=3THENMT=MT+D1:HT=HT+DE
2060 IFZ=4ORQ=4THENMT=MT+I1:HT=HT+IT
2070 IFZ=5ORQ=5THENMT=MT+W1:HT=HT+WI
2080 IFZ=6ORQ=6THENMT=MT+C1:HT=HT+CO
2090 IFHT=MTTHEN PRINT"YOU ARE EVENLY MATCHED":GOTO2130
2100 PRINT"IT LOOKS LIKE THE ODDS"
2110 PRINT"ARE IN FAVOUR OF ";
2120 IF HT>MT THEN PRINT"YOU":GOTO2130
2121 PRINT"THE ";G$
2130 K=INT(RND(0)*7)
2140 GOSUB51
2150 PRINT"THE ";G$;" -"MT
2160 PRINT"YOU -"HT:PRINT
2170 IFK=0THENPRINT"YOU STRUCK A SPLENDID BLOW!":MT=MT-1
2180 IFK=1THENPRINT"THE "G$" STRIKES OUT":HT=HT-3:ST=ST-1:CH=CH-1
2190 IFK=2THENPRINT"YOU DRAW THE ";G$;"'S BLOOD":MT=MT-1
2200 IFK=3THENPRINT"YOU ARE WOUNDED!":HT=HT-INT(RND(0)*3)-1:DE=DE-1
2210 IFK=4THENPRINT"THE "G$" IS TIRING":MT=MT-1
2220 IFK=5THENPRINT"YOU ARE BLEEDING...":HT=HT-2:WI=WI-1:CO=CO-1
2230 IFK=6THENPRINT"YOU WOUND THE ";G$:MT=MT-1
2240 IFRND(0)>.25ANDHT>0ANDMT>0THENGOSUB51:GOTO2130
2250 IFHT>MTTHENPRINT"YOU HAVE SLAIN THE ";G$:MK=MK+1:GOTO2330
2260 PRINT"THE "G$" GOT THE BETTER OF":PRINT"YOU THAT TIME..."
2270 IFZ=1ORQ=1THENST=4*INT(ST/5)
2280 IFZ=2ORQ=2THENCH=3*INT(CH/4)
2290 IFZ=3ORQ=3THENDE=6*INT(DE/7)
2300 IFZ=4ORQ=4THENIT=2*INT(IT/3)
2310 IFZ=5ORQ=5THENWI=5*INT(WI/6)
2320 IFZ=6ORQ=6THENCO=INT(CO/2)
2330 A(RO,8)=0
2340 RETURN
2360 IFRO<>8ANDRO<>34ORA(RO,7)<99THENPRINT"THERE IS NO LOCKED DOOR IN":PRINT"THIS ROOM":RETURN
2380 FL=0
2390 T=0
2400 FORJ=1TO5
2410 IFP(J)=17THENFL=1:T=J
2420 IFP(J)=18THENFL=2:T=J
2430 NEXT J
2440 IFFL=0THENPRINT"YOU DO NOT HAVE A KEY!":RETURN
2450 IFFL=2ANDRO=8ORFL=1 AND RO=34 THEN PRINT"THAT KEY DOES NOT FIT THIS DOOR":RETURN
2460 PRINT"THERE IS A CREAK AS THE KEY":PRINT"TURNS"
2470 GOSUB51
2480 PRINT"...THE DOOR IS NOW UNLOCKED."
2490 A(RO,7)=0
2500 P(T)=0
2510 RETURN
2530 SC=0
2550 IFQU=4THENPRINT"I DID NOT IMAGINE YOU WOULD":PRINT"TURN OUT TO BE A QUITTER!":GOTO2590
2560 PRINT:PRINT"CONGRATULATIONS! YOU HAVE COMPLETED":PRINT"THE ADVENTURE"
2570 SC=100
2580 PRINT:PRINT
2590 SC=(SC+20*CA+47*MK+ST+2*CH+3*DE+4+IT+5*WI+6*CO)/QU
2600 IFMK>0THENPRINT"YOU KILLED"MK"MONSTERS"
2610 IFMK>0ANDCA>0THENPRINT"AND ";
2620 PRINT:PRINT"YOU FOUND $"STR$(CA)" WORTH":PRINT"OF TREASURE"
2630 PRINT:PRINT"YOUR SCORE WAS"SC
2640 END
2660 FL=0
2680 IFC$<>"CHE" THEN PRINT"THAT WOULD NOT BE WISE":RETURN
2690 IFRO<>13ANDRO<>40 THEN PRINT"I CANNOT SEE ANYTHING TO OPEN":PRINT"HERE.":RETURN
2700 IFBOX=1THENIFRND(0)>.6THENPRINT"IT HOLDS NOTHING BUT DUST...":RETURN
2710 IFRO=13ORBOX=1THENPRINT"IT IS EMPTY":RETURN
2720 IFBOX=0THEN2725
2721 GOTO2730
2725 PRINT"INSIDE YOU FIND A PARCHMENT,":PRINT"WITH THE FOLLOWING MESSAGE:"
2726 PRINT"'A LITTLE MAN CAN BE BOUND BY":PRINT"GOLD'":BOX=1
2730 RETURN
2750 FL=0
2770 FORJ=1TO5
2780 IFP(J)=12THENFL=1
2790 NEXTJ
2800 IFFL=0THENPRINT"YOU ARE NOT HOLDING ANYTHING":PRINT"WHICH YOU CAN READ":RETURN
2810 R=INT(RND(0)*3)
2820 IFR=0THENPRINT"IT SAYS:":PRINT"'THE LOCKS NEED SPECIAL KEYS'"
2830 IFR=1THENPRINT"THE SCROLL READS:":PRINT"'CHESTS CAN CONTAIN AID'"
2840 IFR=2THENPRINT"IT SAYS:":PRINT"'THE AMULET IS IMPORTANT'"
2850 RETURN
2870 FL=0
2890 FORJ=1TO5
2900 IFP(J)=13THENFL=J
2910 NEXTJ
2920 IFFL=0THENPRINT"YOU CAN'T DRINK ";C$:RETURN
2930 PRINT"YOU ARE INSTANTLY FILLED WITH":PRINT"HEALING, AND YOUR STRENGTH IS RESTORED"
2940 PRINT"THE BOTTLE HOLDING THE POTION"
2950 PRINT"MAGICALLY FADES FROM VIEW..."
2960 ST=20
2970 P(FL)=0
2980 RETURN
3000 IFA(RO,8)<>1THENPRINT"YOU SHOULDN'T TRY THAT":PRINT"WITH A ";M$(A(RO,8)):RETURN
3020 PRINT"HE DEMANDS THE AMULET!"
3030 FL=0
3040 FORJ=1TO5
3050 IFP(J)=9THENFL=J
3060 NEXTJ
3070 IFFL<>0THEN3250
3080 PRINT"YOU DO NOT HAVE IT..."
3090 GOSUB51
3100 IFRND(0)>.5THENPRINT"HE WOULD ACCEPT ANYTHIHG":PRINT"THAT HE REALLY WANTS":GOTO3210
3110 PRINT"HE DECIDES, HOWEVER, TO ACCEPT"
3120 PRINT"A 'GIFT' OF ";
3130 FORJ=1TO5
3140 IFP(J)<>0THENFL=J
3150 NEXTJ
3160 IFFL=0THEN3200
3170 PRINT"THE ";O$(FL)
3180 P(FL)=0
3190 GOTO3270
3200 PRINT"ANYTHING VALUABLE"
3210 PRINT:PRINT"BUT YOU HAVE NOTHING"
3220 PRINT"AND SO HE KILLS YOU!!"
3230 QU=3
3240 GOTO2570
3250 PRINT"LUCKY FOR YOU THAT YOU HAD IT!"
3260 P(FL)=0
3270 A(RO,8)=0
3280 RETURN
3300 IFRO>11THEN4140
3320 ONROGOSUB3340,3380,3450,3530,3620,3720,3790,3840,3920,3990,4060
3330 RETURN
3340 PRINT"YOU ARE OUT ON THE BATTLEMENTS"
3360 PRINT"OF THE CHATEAU. THERE IS ONLY":PRINT"ONE WAY BACK."
3370 RETURN
3380 PRINT"THIS IS AN EERIE ROOM, WHERE"
3400 PRINT"MAGICIANS COVORTED WITH EVIL"
3410 PRINT"SPRITES AND WEREBEASTS..."
3420 PRINT"EXITS LEAD IN 3 DIRECTIONS."
3430 PRINT"AN EVIL SMELL COMES FROM THE":PRINT"SOUTH."
3440 RETURN
3450 PRINT"AN OLD STRAW MATTRESS LIES IN"
3470 PRINT"ONE CORNER...IT HAS BEEN RIPPED"
3480 PRINT"APART TO FIND ANY TREASURE":PRINT"WHICH WAS HIDDEN IN IT. LIGHT"
3490 PRINT"COMES FITFULLY FROM A WINDOW TO"
3500 PRINT"THE NORTH, AND AROUND THE DOORS"
3510 PRINT"TO THE SOUTH, EAST AND WEST."
3520 RETURN
3530 PRINT"THIS WOODEN-PANELLED ROOM MAKES"
3550 PRINT"YOU FEEL DAMP AND UNCOMFORTABLE"
3560 IFRND(0)>.5THENPRINT"A MOUSE SCAMPERS ACROSS THE":PRINT"FLOOR.":GOTO3570
3561 PRINT"A BAT FLITS ACROSS THE CEILING"
3570 PRINT"THERE ARE THREE DOORS LEADING"
3580 PRINT"FROM THIS ROOM, ONE MADE OF IRON"
3590 PRINT"YOUR SIXTH SENSE WARNS YOU TO"
3600 PRINT"CHOOSE CAREFULLY..."
3610 RETURN
3620 PRINT"YOU IGNORE YOUR INTUITION..."
3640 PRINT"A SPELL OF LIVING STONE, PRIMED"
3650 PRINT"TO TRAP THE FIRST INTRUDER HAS"
3660 PRINT"BEEN SET ON YOU. WITH YOUR LAST"
3670 PRINT"SECONDS OF LIFE YOU HAVE TIME"
3680 PRINT"ONLY TO FEEL PROFOUND REGRET..."
3700 SC=50
3710 GOTO2590
3720 PRINT"YOU ARE IN AN L-SHAPED ROOM"
3740 PRINT"HEAVY PARCHMENT LINES THE WALLS"
3750 PRINT"YOU CAN SEE THROUGH AN ARCHWAY"
3760 PRINT"TO THE EAST...BUT THAT IS NOT"
3770 PRINT"THE ONLY EXIT FROM THIS ROOM"
3780 RETURN
3790 PRINT"THERE IS AN ARCHWAY TO THE WEST,"
3810 PRINT"LEADING TO AN L-SHAPED ROOM"
3820 PRINT"A DOOR LEADS IN THE OPPOSITE":PRINT"DIRECTION."
3830 RETURN
3840 PRINT"THIS MUST BE THE CHATEAU'S MAIN"
3860 PRINT"KITCHEN BUT ANY FOOD LEFT HERE"
3870 PRINT"HAS LONG ROTTED AWAY..."
3880 PRINT
3890 PRINT"A DOOR LEADS TO THE NORTH, AND"
3900 PRINT"THERE IS ONE TO THE WEST"
3910 RETURN
3920 PRINT"YOU FIND YOURSELF IN A SMALL,"
3940 PRINT"ROOM...WHICH MAKES YOU FEEL"
3950 PRINT"CLAUSTROPHOBIC...":PRINT
3960 PRINT"THERE IS A PICTURE OF A BLACK"
3970 PRINT"DRAGON PAINTED ON THE NORTH"
3980 PRINT"WALL, ABOVE THE DOOR..."
3990 PRINT"A STAIRWELL ENDS IN THIS ROOM, WHICH"
4010 PRINT"MORE OF A LANDING THAN A REAL"
4020 PRINT"ROOM. THE DOOR TO THE NORTH IS"
4030 PRINT"MADE OF IRON, WHICH HAS RUSTED"
4040 PRINT"OVER THE CENTURIES..."
4050 RETURN
4060 PRINT"THERE IS A STONE ARCHWAY TO THE NORTH,"
4080 PRINT"YOU ARE IN A VERY LONG ROOM."
4090 PRINT:PRINT"FRESH AIR BLOWS DOWN SOME STAIRS"
4100 PRINT"AND RICH RED DRAPES COVER
4110 PRINT"THE WALLS...YOU CAN SEE DOORS"
4120 PRINT"TO THE SOUTH AND EAST"
4130 RETURN
4140 IFRO>22THEN5030
4150 ONRO-11GOSUB4170,4240,4330,4390,4460,4530,4610,4710,4760,4890,4970
4160 RETURN
4170 PRINT"YOU HAVE ENTERED A ROOM FILLED"
4190 PRINT"WITH SWIRLING, CHOKING SMOKE.":PRINT
4200 PRINT"YOU MUST LEAVE QUICKLY TO REMAIN"
4210 PRINT"HEALTHY ENOUGH TO CONTINUE"
4220 PRINT"YOUR CHOSEN QUEST..."
4230 RETURN
4240 PRINT"THERE IS A MIRROR IN THE CORNER"
4260 PRINT"YOU GLANCE AT IT, AND FEEL"
4270 PRINT"SUDDENLY VERY ILL.":PRINT
4280 PRINT"YOU REALISE THE LOOKING-GLASS HAS"
4290 PRINT"BEEN ENFUSED WITH A SPELL OF CHARISMA"
4300 PRINT"REDUCTION...OH DEAR..."
4310 CH=CH-1
4320 RETURN
4330 PRINT"THIS ROOM IS RICHLY FINISHED, WITH"
4350 PRINT"A WHITE MARBLE FLOOR. STRANGE"
4360 PRINT"FOOTPRINTS LEAD TO THE TWO DOORS"
4370 PRINT"FROM THIS ROOM...DARE YOU FOLLOW THEM?"
4380 RETURN
4390 PRINT"YOU ARE IN A LONG, LONG"
4410 PRINT"HALLWAY, LINED ON EACH SIDE"
4420 PRINT"WITH RICH, RED DRAPES..."
4430 PRINT:PRINT"THEY ARE PARTED HALFWAY DOWN"
4440 PRINT"THE EAST WALL WHERE THERE IS A DOOR"
4450 RETURN
4460 PRINT"SOMEONE HAS SPENT A LONG TIME"
4480 PRINT"PAINTING THIS ROOM A BRIGHT:PRINT"YELLOW..."
4490 PRINT:PRINT"YOU REMEMBER READING THAT"
4500 PRINT"YELLOW IS THE ANCIENT ORACLE'S"
4510 PRINT"COLOR OF WARNING..."
4520 RETURN
4530 PRINT"AS YOU DESCEND THE LADDER YOU"
4550 PRINT"FALL INTO THE ROOM. THE LADDER"
4560 PRINT"CRASHES DOWN BEHIND YOU...THERE"
4570 PRINT"IS NOW NO WAY BACK..."
4580 PRINT:PRINT"A SMALL DOOR LEADS EAST FROM"
4590 PRINT"THIS VERY CRAMPED ROOM..."
4600 RETURN
4610 PRINT"YOU FIND YOURSELF IN A HALL OF"
4630 PRINT"MIRRORS...AND SEE YOURSELF"
4640 PRINT"REFLECTED A HUNDRED TIMES OR"
4650 PRINT"MORE...THROUGH THE BRIGHT GLARE"
4660 PRINT"YOU CAN MAKE OUT DOORS IN ALL"
4670 PRINT"DIRECTIONS...YOU NOTICE THE"
4680 PRINT"MIRRORS AROUND THE EAST DOOR"
4690 PRINT"ARE HEAVILY TARNISHED..."
4700 RETURN
4710 PRINT"YOU FIND YOURSELF IN A LONG":PRINT"CORRIDOR."
4730 GOSUB51
4740 PRINT"YOUR FOOTSTEPS ECHO AS YOU WALK."
4750 RETURN
4760 PRINT"YOU FEEL AS YOU'VE BEEN WANDERING"
4780 PRINT"AROUND THIS CHATEAU FOR EVER..."
4790 PRINT"AND YOU BEGIN TO DESPAIR OF EVER"
4800 PRINT"ESCAPING..."
4810 PRINT:PRINT"STILL, YOU CAN'T GET TOO DEPRESSED, BUT"
4820 PRINT"MUST STRUGGLE ON. LOOKING AROUND, YOU"
4830 PRINT"SEE YOU ARE IN A ROOM WHICH HAS A"
4840 PRINT"HEAVY TIMBERED CEILING, AND WHITE"
4850 PRINT"ROUGHLY-FINISHED WALLS..."
4860 PRINT:PRINT"THERE ARE TWO DOORS..."
4870 GOSUB51
4880 RETURN
4890 PRINT"YOU ARE IN A SMALL ALCOVE. YOU"
4910 PRINT"LOOK AROUND...BUT CAN'T SEE"
4920 PRINT"IN THE GLOOM...PERHAPS IF YOU"
4930 PRINT"WAIT A WHILE YOUR EYES WILL"
4940 PRINT"ADJUST TO THE DARK..."
4950 GOSUB51:RETURN
4960 RETURN
4970 PRINT"A DRIED-UP FOUNTAIN STANDS IN"
4990 PRINT"THE CENTER OF THIS COURTYARD,"
5000 PRINT"WHICH ONCE HELD BEAUTIFUL"
5010 PRINT"FLOWERS--NOW LONG DEAD..."
5020 RETURN
5030 IFRO>33THEN 5890
5040 ONRO-22GOSUB5060,5110,5160,5230,5280,5380,5450,5530,5590,5680,5840
5050 RETURN
5060 PRINT"THE SCENT OF DRIED FLOWERS"
5080 PRINT"FILLS THIS BRIGHT ROOM..."
5090 PRINT:PRINT"THERE ARE TWO EXITS FROM IT."
5100 RETURN
5110 PRINT"THIS IS A ROUND STONE CAVERN"
5130 PRINT"OFF THE SIDE OF THE ALCOVE TO"
5140 PRINT"YOUR NORTH..."
5150 RETURN
5160 PRINT"YOU ARE IN A LARGE CIRCULAR"
5180 PRINT"ROOM, WHICH SEEMS TO HAVE BEEN"
5190 PRINT"USED FOR GAMES. RUBBLE COVERS"
5200 PRINT"THE FLOOR, PARTIALLY BLOCKING"
5210 PRINT"THE ONLY EXIT..."
5220 RETURN
5230 PRINT"THROUGH THE DIM MUSTINESS OF"
5250 PRINT"THIS SMALL POTTING SHED YOU"
5260 PRINT"CAN SEE A STAIRWELL..."
5270 RETURN
5280 PRINT"YOU BEGIN THIS ADVENTURE IN A"
5300 PRINT"WOOD OUTSIDE THE CHATEAU..."
5310 GOSUB50:GOSUB50
5320 PRINT:PRINT"WHILE OUT WALKING, YOU COME"
5330 PRINT"ACROSS A RAMSHACKLE SHED."
5340 PRINT"ENTERING IT, YOU SEE A HOLE"
5350 PRINT"IN ONE CORNER...AN OLD LADDER"
5360 PRINT"LEADS DOWN..."
5370 RETURN
5380 PRINT"HOW WONDERFUL! FRESH AIR..."
5400 GOSUB51
5410 PRINT:PRINT"BIRDS ARE SINGING..."
5420 PRINT"YOU ARE FREE AT LAST...."
5430 PRINT:PRINT
5440 GOTO2560
5450 PRINT"THE SMELL CAME FROM BODIES"
5470 PRINT"ROTTING IN HUGE TRAPS..."
5480 GOSUB51
5490 PRINT"ONE SPRINGS SHUT ON YOU,"
5500 PRINT"TRAPPING YOU FOREVER."
5510 QU=3.5
5520 GOTO2570
5530 PRINT"YOU FALL INTO A FLAMING PIT..."
5550 IFRND(0)>.7THEN5530
5560 SC=10
5570 QU=3.4
5580 GOTO2590
5590 PRINT"AAAAHHH...YOU HAVE FALLEN INTO"
5610 GOSUB50:GOSUB50
5620 PRINT"A POOL OF ACID...NOW YOU KNOW"
5630 PRINT"WHY THE MIRRORS WERE SO BADLY"
5640 PRINT"TARNISHED..."
5650 SC=20
5660 QU=3
5670 GOTO2590
5680 PRINT"TOO BAD YOU CHOSE THAT EXIT"
5700 PRINT"FROM THE ALCOVE..."
5710 FOR Z=1 TO 2000:NEXT Z
5720 PRINT"A GIANT FUNNEL-WEB SPIDER LEAPS"
5730 PRINT"ON YOU...AND BEFORE YOU CAN"
5740 PRINT"REACT BITES YOU ON THE NECK..."
5750 PRINT"YOU HAVE 10 SECONDS TO LIVE..."
5760 FORT=10TO1STEP-1
5770 PRINTTAB(T);T
5780 FORZ=1TO500:NEXTZ
5790 PRINT
5800 NEXTT
5810 SC=3
5820 QU=5
5830 GOTO2590
5840 PRINT"A STAIRWELL LEADS INTO THIS"
5860 PRINT"ROOM, A POOR AND COMMON HOVEL"
5870 PRINT"WITH MANY DOORS AND EXITS..."
5880 RETURN
5890 ONRO-33GOSUB5920,5970,6030,6100,6240,6310,6360,6430,6510,6560,6600
5900 RETURN
5920 PRINT"IT IS HARD TO SEE IN THIS"
5940 PRINT"ROOM, AND YOU SLIP SLIGHTLY"
5950 PRINT"ON THE UNEVEN, ROCKY FLOOR..."
5960 RETURN
5970 PRINT"HORRORS! THIS ROOM WAS ONCE"
5990 PRINT"THE TORTURE CHAMBER..."
6000 PRINT:PRINT"SKELETONS LIE ON THE FLOOR,"
6010 PRINT"STILL IN CHAINS..."
6020 RETURN
6030 PRINT"ANOTHER ROOM WITH VERY"
6050 PRINT"UNPLEASANT MEMORIES..."
6060 GOSUB51
6070 PRINT:PRINT"THIS FOUL HOLE WAS USED AS"
6080 PRINT"THE CHATEAU DUNGEON...."
6090 RETURN
6100 PRINT"OH NO, THIS IS A GARGOYLE'S LAIR..."
6120 GOSUB51
6130 PRINT"IT HAS BEEN HELD PRISONER HERE"
6140 PRINT"FOR THREE HUNDRED YEARS..."
6150 GOSUB51
6160 PRINT:PRINT"IN HIS FRENZY HE LASHES OUT..."
6170 GOSUB51
6180 PRINT"AND..."
6190 GOSUB51
6200 PRINT"...AND BREAKS YOUR NECK!"
6210 SC=0
6220 QU=3
6230 GOTO2590
6240 PRINT"THIS WAS THE LOWER DANCING HALL"
6260 PRINT"WITH DOORS TO THE NORTH, EAST"
6270 PRINT"AND WEST, YOU WOULD SEEM TO BE"
6280 PRINT"ABLE TO FLEE ANY DANGER..."
6290 GOSUB51
6300 RETURN
6310 PRINT"THIS IS A DINGY PIT AT THE FOOT"
6330 PRINT"OF SOME DUBIOUS-LOOKING STAIRS."
6340 PRINT"A DOOR LEADS TO THE EAST..."
6350 RETURN
6360 PRINT"THE TROPHY ROOM OF THE CHATEAU."
6380 PRINT"DOORS OPEN TO EACH DIRECTION."
6390 PRINT:PRINT"THE HEADS OF STRANGE CREATURES"
6400 PRINT"SHOT BY THE OWNERS ARE MOUNTED"
6410 PRINT"HIGH UP ON EACH WALL..."
6420 RETURN
6430 PRINT"YOU HAVE FOUND A SECRET ROOM..."
6450 GOSUB51
6460 PRINT:PRINT"DOWN HERE THE ANCIENT"
6470 PRINT"NECROMANCERS OF THORIN PLIED"
6480 PRINT"THEIR EVIL CRAFT... THE SCENT"
6490 PRINT"OF VILE POTIONS REMAINS..."
6500 RETURN
6510 PRINT"COBWEBS BRUSH YOUR FACE AS YOU"
6530 PRINT"MAKE YOUR WAY THROUGH THE GLOOM"
6540 PRINT"OD THIS ROOM OF SHADOWS..."
6550 RETURN
6560 PRINT"THIS GLOOMY PASSAGE LIES AT THE"
6580 PRINT"INTERSECTION OF THREE ROOMS..."
6590 RETURN
6600 PRINT"YOU ARE IN THE REAR TURRET ROOM,";
6620 PRINT"BELOW THE WESTERN WALL OF THE"
6630 PRINT"ANCIENT CHATEAU..."
6640 RETURN
6660 PRINT
6680 IFA(RO,7)>98ANDA(RO,9)=0ANDA(RO,10)=0THENRETURN
6690 PRINT"YOU CAN SEE:"
6700 IFA(RO,7)>98THEN6720
6710 IFA(RO,7)<>0THENPRINTO$(A(RO,7))
6720 IFA(RO,9)>98THEN6740
6730 IFA(RO,9)<>0THENPRINTO$(A(RO,9))
6740 IFA(RO,10)>98THEN6760
6750 IFA(RO,10)<>0THENPRINTO$(A(RO,10))
6760 GOSUB50
6770 PRINT
6780 RETURN
6800 N=RND(-(PEEK(9)*256+PEEK(10))):N=0
6900 DIMA(44,10),P(5),O$(20),V(20),M$(20),T(5)
6910 ST=RND(16)+2:CH=RND(16)+2:DE=RND(16)+2:IT=RND(16)+2:WI=RND(16)+2:CO=RND(16)+2:CA=0:RO=27:QU=1:MK=0:BOX=0
7020 PRINT:PRINT"LOAD 'CHATEAU2'":CLOAD*A,"CHATEAU2"
7150 FORZ=1TO20
7160 READO$(Z),V(Z)
7170 NEXTZ
7180 FORQ=1TO16
7190 Z=INT(RND(0)*44)+1
7200 IFZ=5ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7190
7210 IFA(Z,7)<>0 THEN 7190
7220 A(Z,7)=Q
7230 NEXTQ
7240 PRINT
7250 PRINT"JUST A FEW MOMENTS MORE..."
7280 FORJ=1TO20
7290 READM$(J)
7300 IFJ=1THEN7350
7310 Z=INT(RND(0)*44)+1
7320 IFZ=5ORZ=16ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7310
7330 IFA(Z,8)<>0THEN7310
7340 A(Z,8)=J
7350 NEXT J
7360 RETURN
7830 DATA "AXE",0,"SWORD",0,"DAGGER",0,"MACE",0
7840 DATA "QUARTERSTAFF",0,"MORNING STAR",0,"FALCHION",0
7850 DATA "CRYSTAL RALL",99,"AMULET",247," EBONY RING",166,"GEMS",462,"MYSTIC SCROLL",195,"HEALING POTION",231
7851 DATA "DILITHIUM CRYSTALS",162,"COPPER PIECES",27,"DIADEM",141,"SILVER KEY",0,"GOLDEN KEY",0,"CHEST OF STONE",0
7852 DATA "CHEST MADE OF IRON",0
7870 DATA "DWARF","MONOCEROS","PARADRUS","VAMPYRE","WRNACH","GIOLLA DACKER","KRAKEN","FENRIS WOLF","CALOPUS","BASILISK"
7880 DATA "GRIMOIRE","FLYING BUFFALO","BERSERKOID","WYRM","CROWTHERWOOD","GYGAX","RAGNAROK","FOMORINE","HAFGYGR","GRENDEL"
