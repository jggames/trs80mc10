0 CLS:CLEAR500:DIMZ,CC,P,M$:CLS:PRINT"CHATEAU GAILLARD":GOSUB6800:GOTO60
1 Z=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,Z,CC-Z):Z=CC+1:CC=Z+31:IFZ<=LEN(M$)THEN2
4 RETURN
50 FORZ=1TO500:NEXTZ:RETURN
51 FORZ=1TO1500:NEXTZ:RETURN
60 GOSUB51:CLS:GOSUB3300:IFA(RO,7)<>0ORA(RO,9)<>0ORA(RO,10)<>0THENGOSUB6660
90 IFA(RO,7)>98THENM$="ONE OF THE DOORS IS LOCKED PREVENTING YOU FROM EXPLORING FURTHER":GOSUB1
100 IFA(RO,8)=0THEN110
101 M$="LOOK OUT! THERE IS A "+M$(A(RO,8))+" HERE!":GOSUB1
103 IFRND(0)>.7ANDA(RO,8)<>1THENM$="THE "+M$(A(RO,8))+" ATTACKS!":GOSUB1:KW=1:GOSUB1400:GOTO60
110 IFST<1THENST=0:GOTO120
111 IFRND(0)>.84THENST=ST-1
120 IFCH<1THENCH=0:GOTO130
121 IFRND(0)>.84THENCH=CH-1
130 IFDE<1THENDE=0:GOTO140
131 IFRND(0)>.84THENDE=DE-1
140 IFIT<1THENIT=0:GOTO150
141 IFRND(0)>.84THENIT=IT-1
150 IFWI<1THENWI=0:GOTO160
151 IFRND(0)>.84THEN WI=WI-1
160 IFCO<1THENCO=0:GOTO170
161 IFRND(0)>.84THENCO=CO-1
170 IFRND(0)>.84ANDRO=16ANDA(RO,8)=1THEN175
171 GOTO180
172 PRINT:PRINT"YOUR ATTRIBUTES ARE:":PRINT"1 STR"ST;TAB(11);"2 CHA"CH;TAB(22)"3 DEX"DE
173 PRINT"4 INT"IT;TAB(11)"5 WIS"WI;TAB(22)"6 CON"CO:RETURN
175 PRINT:M$="YOU HEAR A WHISPERED WARNING: 'DO SOMETHING ABOUT THE DWARF'":GOSUB1
180 GOSUB172:IFST*CH*DE*IT*WI*CO=0THENM$="YOU ARE EXHAUSTED... SO THIS ADVENTURE MUST END":GOSUB1:QU=2:GOTO2590   
230 FL=0:FORJ=1TO5:IFP(J)<>0THENFL=1
260 NEXTJ
270 IFFL=0THEN340
280 CA=0:PRINT:PRINT"YOU ARE CARRYING:":FORJ=1TO5:IFP(J)<>0THENPRINTO$(P(J)):CA=CA+V(P(J))
320 NEXTJ
330 IFCA>0THENPRINT"TOTAL VALUE $";STR$(CA)
340 PRINT
370 KW=0
380 GOSUB51
390 INPUT"WHAT DO YOU DO";A$
400 IFA$=""THEN390
410 AS=ASC(A$)
430 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO430
440 B$=LEFT$(A$,3)
450 IFB$="HEL"THENM$="NO HELP FOR THE MORTALS HERE! ALTHOUGH READING & DRINKING MAY HELP...":GOSUB1:GOSUB51:GOTO60
460 IFB$="QUI"THENQU=4:GOTO2530
470 N=1
480 IFMID$(A$,N,1)=" "THEN485
481 GOTO490
485 C$=MID$(A$,N+1,3):IF LEFT$(C$,1)<>" "THEN530
486 GOTO500
490 IFN<MTHENN=N+1:GOTO480
500 IFRND(0)>.5THEN520
510 M$="BY ITSELF, "+A$+" CAN'T BE ACTED ON":GOSUB1:GOTO390
520 M$=A$+" IS JUST ONE WORD. I NEED TWO.":GOSUB1:GOTO390
530 IFRO<>8 AND RO<>34 THEN 560
540 IFB$="STA"ORB$="KIL"ORB$="FIG"ORB$="KIC"ORB$="PUN"ORB$="SLA"ORB$="ATT"THEN KW=1:GOSUB1400
550 IFA(RO,7)>98ANDB$<>"UNL"THENPRINT"** THE DOORS ARE LOCKED **":GOTO60
560 IFB$="STA"ORB$="KIL"ORB$="FIG"ORB$="KIC"ORB$="PUN"ORB$="SLA"ORB$="ATT"THENKW=1:GOSUB1400
570 IFB$="GO "ORB$="MOV"ORB$="CLI"ORB$="RUN"ORB$="WAL"THENKW=1:GOSUB740
580 IFB$="TAK"ORB$="GET"ORB$="STE"ORB$="LIF"THENKW=1:GOSUB950
590 IFB$="DRO"ORB$="PUT"ORB$="THR"ORB$="BRE"THENKW=1:GOSUB1170
600 IFB$="UNL"THEN KW=1:GOSUB2360
610 IFB$="OPE"THEN KW=1:GOSUB2660
620 IFB$="REA"THEN KW=1:GOSUB2750
630 IFB$="DRI"OR B$="SWA" THEN KW=1:GOSUB2870
640 IFB$="BRI"OR B$="PAY" THEN KW=1:GOSUB3000
650 IFKW=1THEN700
660 R=INT(RND(0)*3)
670 IFR=0THENM$="IT WOULD NOT BE WISE TO "+A$:GOSUB1
680 IFR=1THENM$="ONLY A FOOL WOULD TRY TO "+A$:GOSUB1
690 IFR=2THENM$="I DON'T UNDERSTAND "+A$:GOSUB1
700 GOTO60
740 IFRO<>16ORA(16,8)<>1THEN800
760 PRINT:M$="THE DWARF REFUSES TO LET YOU PROCEED...":GOSUB1
780 GOSUB50
790 RETURN
800 C$=LEFT$(C$,1)
810 IFC$="N"ANDA(RO,1)=0THENPRINT"YOU CANNOT GO THAT WAY":RETURN
820 IFC$="S"ANDA(RO,2)=0THENPRINT"THERE IS NO EXIT SOUTH":RETURN
830 IFC$="E"ANDA(RO,3)=0THENPRINT"I SEE NOWHERE TO THE EAST TO GO":RETURN
840 IFC$="W"ANDA(RO,4)=0THENPRINT"YOU CANNOT WALK THROUGH WALLS":RETURN
850 IFC$="U"ANDA(RO,5)=0THENPRINT"THERE IS NO WAY TO MOVE UP":RETURN
860 IFC$="D"ANDA(RO,6)=0THENPRINT"YOU CANNOT DESCEND FROM HERE":RETURN
870 IFC$="N"THENRO=A(RO,1)
880 IFC$="S"THENRO=A(RO,2)
890 IFC$="E"THENRO=A(RO,3)
900 IFC$="W"THENRO=A(RO,4)
910 IFC$="U"THENRO=A(RO,5)
920 IFC$="D"THENRO=A(RO,6)
930 RETURN
950 FL=0:FORJ=1TO5:IFP(J)<>0THENFL=FL+1
990 NEXTJ
1000 IFFL=5THENM$="YOU ARE ALREADY CARRYING YOUR MAXIMUM OF # OF OBJECTS":GOSUB1:RETURN
1010 IFC$="CHE"THENPRINT"IT IS FAR TOO HEAVY TO LIFT":RETURN
1020 IFA(RO,7)=0ANDA(RO,9)=0ANDA(RO,10)=0THENPRINT"I SEE NOTHING TO PICK UP":RETURN
1030 D$="":E$="":F$="":N=0
1040 D$=LEFT$(O$(A(RO,7)),3)
1050 E$=LEFT$(O$(A(RO,9)),3)
1060 F$=LEFT$(O$(A(RO,10)),3)
1070 IFC$=D$THENN=A(RO,7):A(RO,7)=0
1080 IFC$=E$THENN=A(RO,9):A(RO,9)=0
1090 IFC$=F$THENN=A(RO,10):A(RO,10)=0
1100 J=1
1110 IFP(J)=0THENP(J)=N:GOTO1130
1120 IFJ<5THENJ=J+1:GOTO1110
1130 IFN>0THENM$="YOU NOW HAVE THE "+O$(N):GOSUB1
1140 RETURN
1170 FL=0
1180 FORJ=1TO5
1190 IFP(J)<>0THENFL=1
1200 NEXTJ
1210 IFFL=0THENPRINT"YOU ARE NOT CARRYING ANYTHING":RETURN
1220 IFA(RO,7)<>0ANDA(RO,9)<>0ANDA(RO,10)<>0THEN1225
1221 GOTO1230
1225 M$="THIS ROOM ALREADY HOLDS ITS MAXIMUM # OF OBJECTS":GOSUB1:RETURN
1230 D$="":D=0
1240 FORJ=1TO18
1250 IFLEFT$(O$(J),3)=C$THEND$=O$(J):D=J
1260 NEXTJ
1270 IFD$=""THENPRINT"HOW CAN YOU WHEN YOU'RE":PRINT"NOT HOLDING IT?":RETURN
1280 FORJ=1TO5
1290 IFP(J)=DTHENP(J)=0
1300 NEXTJ
1310 IFA(RO,7)=0THENA(RO,7)=D:GOTO1340
1320 IFA(RO,9)=0THENA(RO,9)=D:GOTO1340
1330 IFA(RO,10)=0THENA(RO,10)=D
1340 IFB$="DRO"THENM$="YOU HAVE DROPPED THE "+D$:GOSUB1
1350 IFB$="PUT"THENM$="YOU HAVE PUT THE "+D8+" DOWN":GOSUB1
1360 IFB$="THR"THENM$="WITH A MIGHTY HEAVE, YOU THROW THE "+D$+" AWAY":GOSUB1
1370 IFB$="BRE"THENM$="YOU HAVE BROKEN THE "+D$:GOSUB1
1380 RETURN
1400 IFA(RO,8)=1THENM$="THE DWARF REFUSES TO FIGHT AND HIS MAGIC PROTECTS HIM":GOSUB1:RETURN
1420 IFA(RO,8)<>0THEN1470
1430 R=RND(0)
1440 IFR<.5THENPRINT"THERE IS NOTHING TO FIGHT HERE"
1450 IFR>=.5THENPRINT"YOU CAN'T FIGHT EMPTY AIR!"
1460 RETURN
1470 G$=M$(A(RO,8))
1480 S1=RND(16)+2:H1=RND(16)+2:D1=RND(16)+2:I1=RND(16)+2:W1=RND(16)+2:C1=RND(16)+2
1550 M$="YOUR OPPONENT IS A "+G$:GOSUB1
1560 MT=0:HT=0
1570 FF=S1*(INT(RND(0)*6)+1)
1590 M$="ITS DANGER LEVEL IS"+STR$(FF):GOSUB1
1600 GOSUB51
1610 FORJ=1TO5
1620 T(J)=0
1630 IFP(J)=1THENPRINT"YOUR AXE COULD BE HANDY":T(J)=1
1640 IFP(J)=2THENM$="YOUR SKILL WITH THE SWORD MAY STAND YOU IN GOOD STEAD":GOSUB1:T(J)=2
1650 IFP(J)=3THENM$="YOUR DAGGER IS USEFUL AGAINST "+G$+"S":GOSUB1:T(J)=3
1660 IFP(J)=4THENM$="THE MACE WILL MAKE SHORT WORK OF IT":GOSUB1:T(J)=4
1670 IFP(J)=5THENM$="YOUR QUARTERSTAFF WILL GIVE IT NO QUARTER...":GOSUB1:T(J)=5
1680 IFP(J)=6THENM$="YOUR MORNING STAR MAY INFLICT HEAVY WOUNDS ON THE"+G$:GOSUB1:T(J)=6
1690 IFP(J)=7THENM$="A FALCHION IS A USEFUL WEAPON":GOSUB1:T(J)=7
1700 NEXTJ
1710 FL=0
1720 FORJ=1TO5
1730 IFT(J)<>0THENFL=FL+1
1740 NEXTJ
1750 IFFL=0THENM$="YOU MUST FIGHT THE "+G$+" WITH YOUR BARE HANDS":GOSUB1:GOTO1910
1760 IFFL>1THEN1830
1770 FORJ=1TO5
1780 IFT(J)<>0THENFL=T(J)
1790 NEXTJ
1800 M$="YOU MUST FIGHT WITH YOUR "+O$(FL):GOSUB1
1810 FF=INT(FF*2/FL)
1820 GOTO1910
1830 PRINT"CHOOSE YOUR WEAPON:"
1840 FORJ=1TO5
1850 IFP(J)<>0THENPRINTJ;" - ";O$(J)
1860 NEXTJ
1870 INPUT"ENTER THE NUMBER TO CHOOSE";J
1880 IFP(J)=0THENM$="YOU DO NOT HAVE THE "+O$(J):GOSUB1:GOTO1870
1890 M$="RIGHT, SO YOU CHOOSE TO FIGHT WITH THE "+O$(J):GOSUB1
1900 FF=INT(FF*2/J)
1910 GOSUB51
1920 PRINT"IT HAS THE FOLLOWING ATTRIBUTES:";
1930 PRINT"1 STR"S1;TAB(11)"2 CHA"H1;TAB(22)"3 DEX"D1:PRINT"4 INT"I1;TAB(11)"5 WIS"W1;TAB(22)"6 CON"C1
1960 GOSUB172
2000 PRINT:PRINT"WHICH ATTRIBUTES WILL YOU":PRINT"FIGHT WITH (2)";
2010 INPUTZ,Q
2020 IFZ<1ORQ<1ORZ>6ORQ>6ORZ=QTHENM$="DO NOT FOOL AROUND WHEN A "+G$+" IS IN THE ROOM WITH YOU!":GOSUB1:GOTO2010
2030 IFZ=1ORQ=1THENMT=MT+S1:HT=HT+ST
2040 IFZ=2ORQ=2THENMT=MT+H1:HT=HT+CH
2050 IFZ=3ORQ=3THENMT=MT+D1:HT=HT+DE
2060 IFZ=4ORQ=4THENMT=MT+I1:HT=HT+IT
2070 IFZ=5ORQ=5THENMT=MT+W1:HT=HT+WI
2080 IFZ=6ORQ=6THENMT=MT+C1:HT=HT+CO
2090 IFHT=MTTHENPRINT"YOU ARE EVENLY MATCHED":GOTO2130
2100 PRINT"IT LOOKS LIKE THE ODDS ARE IN   FAVOUR OF ";
2120 IFHT>MTTHENPRINT"YOU":GOTO2130
2121 PRINT"THE ";G$
2130 K=INT(RND(0)*7)
2140 GOSUB51
2150 PRINT"THE ";G$;" -"MT
2160 PRINT"YOU -"HT:PRINT
2170 IFK=0THENPRINT"YOU STRUCK A SPLENDID BLOW!":MT=MT-1
2180 IFK=1THENM$="THE "+G$+" STRIKES OUT":GOSUB1:HT=HT-3:ST=ST-1:CH=CH-1
2190 IFK=2THENM$="YOU DRAW THE "+G$+"'S BLOOD":GOSUB1:MT=MT-1
2200 IFK=3THENPRINT"YOU ARE WOUNDED!":HT=HT-INT(RND(0)*3)-1:DE=DE-1
2210 IFK=4THENM$="THE "+G$+" IS TIRING":GOSUB1:MT=MT-1
2220 IFK=5THENPRINT"YOU ARE BLEEDING...":HT=HT-2:WI=WI-1:CO=CO-1
2230 IFK=6THENM$="YOU WOUND THE "+G$:GOSUB1:MT=MT-1
2240 IFRND(0)>.25ANDHT>0ANDMT>0THENGOSUB51:GOTO2130
2250 IFHT>MTTHENM$="YOU HAVE SLAIN THE "+G$:GOSUB1:MK=MK+1:GOTO2330
2260 M$="THE "+G$+" GOT THE BETTER OF YOU THAT TIME...":GOSUB1
2270 IFZ=1ORQ=1THENST=4*INT(ST/5)
2280 IFZ=2ORQ=2THENCH=3*INT(CH/4)
2290 IFZ=3ORQ=3THENDE=6*INT(DE/7)
2300 IFZ=4ORQ=4THENIT=2*INT(IT/3)
2310 IFZ=5ORQ=5THENWI=5*INT(WI/6)
2320 IFZ=6ORQ=6THENCO=INT(CO/2)
2330 A(RO,8)=0:GOSUB51:RETURN
2360 IFRO<>8ANDRO<>34ORA(RO,7)<99THENM$="THERE IS NO LOCKED DOOR IN THIS ROOM":GOSUB1:RETURN
2380 FL=0
2390 T=0
2400 FORJ=1TO5
2410 IFP(J)=17THENFL=1:T=J
2420 IFP(J)=18THENFL=2:T=J
2430 NEXT J
2440 IFFL=0THENPRINT"YOU DO NOT HAVE A KEY!":RETURN
2450 IFFL=2ANDRO=8ORFL=1ANDRO=34THENM$="THAT KEY DOES NOT FIT THIS DOOR":GOSUB1:RETURN
2460 M$="THERE IS A CREAK AS THE KEY TURNS":GOSUB1
2470 GOSUB51
2480 PRINT"...THE DOOR IS NOW UNLOCKED."
2490 A(RO,7)=0
2500 P(T)=0
2510 RETURN
2530 SC=0
2550 IFQU=4THENM$="I DID NOT IMAGINE YOU WOULD TURN OUT TO BE A QUITTER!":GOSUB1:GOTO2590
2560 PRINT:M$="CONGRATULATIONS! YOU HAVE COMPLETED THE ADVENTURE":GOSUB1
2570 SC=100
2580 PRINT:PRINT
2590 SC=(SC+20*CA+47*MK+ST+2*CH+3*DE+4+IT+5*WI+6*CO)/QU
2600 IFMK>0THENPRINT"YOU KILLED"MK"MONSTERS"
2610 IFMK>0ANDCA>0THENPRINT"AND ";
2620 PRINT:M$="YOU FOUND $"+STR$(CA)+" WORTH OF TREASURE":GOSUB1
2630 PRINT:PRINT"YOUR SCORE WAS"SC
2640 END
2660 FL=0
2680 IFC$<>"CHE"THENPRINT"THAT WOULD NOT BE WISE":RETURN
2690 IFRO<>13ANDRO<>40THENM$="I CANNOT SEE ANYTHING TO OPEN HERE.":GOSUB1:RETURN
2700 IFBOX=1THENIFRND(0)>.6THENPRINT"IT HOLDS NOTHING BUT DUST...":RETURN
2710 IFRO=13ORBOX=1THENPRINT"IT IS EMPTY":RETURN
2720 IFBOX=0THEN2725
2721 GOTO2730
2725 M$="INSIDE YOU FIND A PARCHMENT, WITH THE FOLLOWING MESSAGE: 'A LITTLE MAN CAN BE BOUND BY GOLD'":GOSUB1:BOX=1
2730 RETURN
2750 FL=0
2770 FORJ=1TO5
2780 IFP(J)=12THENFL=1
2790 NEXTJ
2800 IFFL=0THENM$="YOU ARE NOT HOLDING ANYTHING WHICH YOU CAN READ":GOSUB1:RETURN
2810 R=INT(RND(0)*3)
2820 IFR=0THENM$="IT SAYS: 'THE LOCKS NEED SPECIAL KEYS'":GOSUB1
2830 IFR=1THENM$="THE SCROLL READS: 'CHESTS CAN CONTAIN AID'":GOSUB1
2840 IFR=2THENM$="IT SAYS: 'THE AMULET IS IMPORTANT'":GOSUB1
2850 RETURN
2870 FL=0
2890 FORJ=1TO5
2900 IFP(J)=13THENFL=J
2910 NEXTJ
2920 IFFL=0THENM$="YOU CAN'T DRINK "+C$:GOSUB1:RETURN
2930 M$="YOU ARE INSTANTLY FILLED WITH HEALING, AND YOUR STRENGTH IS RESTORED":GOSUB1
2940 M$="THE BOTTLE HOLDING THE POTION MAGICALLY FADES FROM VIEW...":GOSUB1
2960 ST=20
2970 P(FL)=0
2980 RETURN
3000 IFA(RO,8)<>1THENM$="YOU SHOULDN'T TRY THAT WITH A "+M$(A(RO,8)):GOSUB1:RETURN
3020 PRINT"HE DEMANDS THE AMULET!"
3030 FL=0
3040 FORJ=1TO5
3050 IFP(J)=9THENFL=J
3060 NEXTJ
3070 IFFL<>0THEN3250
3080 PRINT"YOU DO NOT HAVE IT..."
3090 GOSUB51
3100 IFRND(0)>.5THENM$="HE WOULD ACCEPT ANYTHING THAT HE REALLY WANTS":GOSUB1:GOTO3210
3110 PRINT"HE DECIDES, HOWEVER, TO ACCEPT"
3120 PRINT"A 'GIFT' OF ";
3130 FORJ=1TO5
3140 IFP(J)<>0THENFL=J
3150 NEXTJ
3160 IFFL=0THEN3200
3170 PRINT"THE ";O$(FL)
3180 P(FL)=0
3190 GOTO3270
3200 PRINT"ANYTHING VALUABLE"
3210 PRINT:PRINT"BUT YOU HAVE NOTHING"
3220 PRINT"AND SO HE KILLS YOU!!"
3230 QU=3
3240 GOTO2570
3250 PRINT"LUCKY FOR YOU THAT YOU HAD IT!"
3260 P(FL)=0
3270 A(RO,8)=0
3280 RETURN
3300 IFRO>11THEN4140
3320 ONROGOSUB3340,3380,3450,3530,3620,3720,3790,3840,3920,3990,4060
3330 RETURN
3340 M$="YOU ARE OUT ON THE BATTLEMENTS OF THE CHATEAU. THERE IS ONLY ONE WAY BACK.":GOSUB1
3370 RETURN
3380 M$="THIS IS AN EERIE ROOM, WHERE MAGICIANS COVORTED WITH EVIL SPRITES AND WEREBEASTS...":GOSUB1
3420 M$="EXITS LEAD IN 3 DIRECTIONS. AN EVIL SMELL COMES FROM THE":PRINT"SOUTH.":GOSUB1:RETURN
3450 M$="AN OLD STRAW MATTRESS LIES IN ONE CORNER...IT HAS BEEN RIPPED APART TO FIND ANY TREASURE HIDDEN IN IT. LIGHT "
3460 M$=M$+"COMES FAINTLY FROM A WINDOW TO THE NORTH, AND AROUND THE DOORS TO THE SOUTH, EAST AND WEST.":GOSUB1:RETURN
3530 M$="THIS WOODEN-PANELLED ROOM MAKES YOU FEEL DAMP AND UNCOMFORTABLE":GOSUB1
3560 IFRND(0)>.5THENPRINT"A MOUSE SCAMPERS ACROSS THE":PRINT"FLOOR.":GOTO3570
3561 PRINT"A BAT FLITS ACROSS THE CEILING"
3570 M$="THERE ARE THREE DOORS LEADING FROM THIS ROOM, ONE MADE OF IRON":GOSUB1
3590 M$="YOUR SIXTH SENSE WARNS YOU TO CHOOSE CAREFULLY...":GOSUB1
3610 RETURN
3620 M$="YOU IGNORE YOUR INTUITION... A SPELL OF LIVING STONE, PRIMED TO TRAP THE FIRST INTRUDER HAS "
3660 M$=M$+"BEEN SET ON YOU. WITH YOUR LAST SECONDS OF LIFE YOU HAVE TIME ONLY TO FEEL PROFOUND REGRET...":GOSUB1
3700 SC=50
3710 GOTO2590
3720 M$="YOU ARE IN AN L-SHAPED ROOM HEAVY PARCHMENT LINES THE WALLS YOU CAN SEE THROUGH AN ARCHWAY "
3760 M$=M$+"TO THE EAST...BUT THAT IS NOT THE ONLY EXIT FROM THIS ROOM":GOSUB1:RETURN
3790 M$="THERE IS AN ARCHWAY TO THE WEST,LEADING TO AN L-SHAPED ROOM A DOOR LEADS IN THE OPPOSITE DIRECTION.":GOSUB1:RETURN
3840 M$="THIS MUST BE THE CHATEAU'S MAIN KITCHEN BUT ANY FOOD LEFT HERE HAS LONG ROTTED AWAY...":GOSUB1:PRINT
3890 M$="A DOOR LEADS TO THE NORTH, AND THERE IS ONE TO THE WEST.":GOSUB1:RETURN
3920 M$="YOU FIND YOURSELF IN A SMALL, ROOM...WHICH MAKES YOU FEEL CLAUSTROPHOBIC...":GOSUB1
3960 M$="THERE IS A PICTURE OF A BLACK DRAGON PAINTED ON THE NORTH WALL, ABOVE THE DOOR... A STAIRWELL ENDS"
3990 M$=M$+"IN THIS ROOM, WHICH IS REALLY MORE OF A LANDING. THE DOOR TO THE NORTH IS MADE OF IRON, WHICH HAS RUSTED "
4040 M$=M$+"OVER THE CENTURIES...":GOSUB1:RETURN
4060 M$="THERE IS A STONE ARCHWAY TO THE NORTH, YOU ARE IN A VERY LONG ROOM.":GOSUB1
4090 M$="FRESH AIR BLOWS DOWN SOME STAIRSAND RICH RED DRAPES COVER THE WALLS...YOU CAN SEE DOORS TO THE SOUTH AND EAST"
4130 GOSUB1:RETURN
4140 IFRO>22THEN5030
4150 ONRO-11GOSUB4170,4240,4330,4390,4460,4530,4610,4710,4760,4890,4970
4160 RETURN
4170 M$="YOU HAVE ENTERED A ROOM FILLED WITH SWIRLING, CHOKING SMOKE. YOU MUST LEAVE QUICKLY TO REMAIN "
4210 M$=M$+"HEALTHY ENOUGH TO CONTINUE YOUR CHOSEN QUEST...":GOSUB1:RETURN
4240 M$="THERE IS A MIRROR IN THE CORNER YOU GLANCE AT IT, AND FEEL SUDDENLY VERY ILL. YOU REALISE THE LOOKING-GLASS HAS "
4290 M$=M$+"BEEN ENFUSED WITH A SPELL OF CHARISMA REDUCTION...OH DEAR...":GOSUB1:CH=CH-1:RETURN
4330 M$="THIS ROOM IS RICHLY FINISHED, WITH A WHITE MARBLE FLOOR. STRANGE FOOTPRINTS LEAD TO THE TWO DOORS "
4370 M$=M$+"FROM THIS ROOM...DARE YOU FOLLOW THEM?":GOSUB1:RETURN
4390 M$="YOU ARE IN A LONG, LONG HALLWAY, LINED ON EACH SIDE WITH RICH, RED DRAPES... THEY ARE PARTED HALFWAY DOWN "
4440 M$=M$+"THE EAST WALL WHERE THERE IS A DOOR":GOSUB1:RETURN
4460 M$="SOMEONE HAS SPENT A LONG TIME PAINTING THIS ROOM A BRIGHT:PRINT"YELLOW... YOU REMEMBER READING THAT "
4500 M$=M$+"YELLOW IS THE ANCIENT ORACLE'S COLOR OF WARNING...":GOSUB1:RETURN
4530 M$="AS YOU DESCEND THE LADDER YOU FALL INTO THE ROOM. THE LADDER CRASHES DOWN BEHIND YOU...THERE "
4570 M$=M$+"IS NOW NO WAY BACK... A SMALL DOOR LEADS EAST FROM THIS VERY CRAMPED ROOM...":GOSUB1:RETURN
4610 M$="YOU FIND YOURSELF IN A HALL OF MIRRORS...AND SEE YOURSELF REFLECTED A HUNDRED TIMES OR MORE... "
4650 M$=M$+"THROUGH THE BRIGHT GLARE YOU CAN MAKE OUT DOORS IN ALL DIRECTIONS...":GOSUB1
4660 M$="YOU NOTICE THE MIRRORS AROUND THE EAST DOOR ARE HEAVILY TARNISHED...":GOSUB1:RETURN
4710 M$="YOU FIND YOURSELF IN A LONG CORRIDOR.":GOSUB1:GOSUB51:PRINT"YOUR FOOTSTEPS ECHO AS YOU WALK.":RETURN
4760 M$="YOU FEEL AS YOU'VE BEEN WANDERING AROUND THIS CHATEAU FOR EVER... AND YOU BEGIN TO DESPAIR OF EVER  ESCAPING..."
4810 GOSUB1:M$="STILL, YOU CAN'T GET TOO DEPRESSED, BUT MUST STRUGGLE ON. LOOKING AROUND, YOU SEE YOU ARE IN A ROOM "
4820 M$=M$+"WHICH HAS A HEAVY TIMBERED CEILING, AND WHITE ROUGHLY-FINISHED WALLS... THERE ARE TWO DOORS...":GOSUB1
4870 GOSUB51:RETURN
4890 M$="YOU ARE IN A SMALL ALCOVE. YOU LOOK AROUND...BUT CAN'T SEE IN THE GLOOM...PERHAPS IF YOU "
4930 M$=M$+"WAIT A WHILE YOUR EYES WILL ADJUST TO THE DARK...":GOSUB1:GOSUB51:RETURN
4970 M$="A DRIED-UP FOUNTAIN STANDS IN THE CENTER OF THIS COURTYARD, WHICH ONCE HELD BEAUTIFUL FLOWERS--NOW LONG DEAD..."
5020 GOSUB1:RETURN
5030 IFRO>33THEN5890
5040 ONRO-22GOSUB5060,5110,5160,5230,5280,5380,5450,5530,5590,5680,5840
5050 RETURN
5060 M$="THE SCENT OF DRIED FLOWERS FILLS THIS BRIGHT ROOM... THERE ARE TWO EXITS FROM IT.":GOSUB1:RETURN
5110 M$="THIS IS A ROUND STONE CAVERN OFF THE SIDE OF THE ALCOVE TO YOUR NORTH...":GOSUB1:RETURN
5160 M$="YOU ARE IN A LARGE CIRCULAR ROOM, WHICH SEEMS TO HAVE BEEN USED FOR GAMES. RUBBLE COVERS "
5200 M$=M$+"THE FLOOR, PARTIALLY BLOCKING THE ONLY EXIT...":GOSUB1:RETURN
5230 M$="THROUGH THE DIM MUSTINESS OF THIS SMALL POTTING SHED YOU CAN SEE A STAIRWELL...":GOSUB1:RETURN
5280 M$="YOU BEGIN THIS ADVENTURE IN A WOOD OUTSIDE THE CHATEAU...":GOSUB1:GOSUB50:GOSUB50
5320 PRINT:M$="WHILE OUT WALKING, YOU COME ACROSS A RAMSHACKLE SHED. ENTERING IT, YOU SEE A HOLE "
5350 M$=M$+"IN ONE CORNER...AN OLD LADDER LEADS DOWN...":GOSUB1:RETURN
5380 PRINT"HOW WONDERFUL! FRESH AIR...":GOSUB51:PRINT:M$="BIRDS ARE SINGING... YOU ARE FREE AT LAST....":GOSUB1
5430 PRINT:PRINT:GOTO2560
5450 M$="THE SMELL CAME FROM BODIES ROTTING IN HUGE TRAPS...":GOSUB1:GOSUB51
5490 M$="ONE SPRINGS SHUT ON YOU, TRAPPING YOU FOREVER.":GOSUB1:QU=3.5:GOTO2570
5530 PRINT"YOU FALL INTO A FLAMING PIT..."
5550 IFRND(0)>.7THEN5530
5560 SC=10
5570 QU=3.4
5580 GOTO2590
5590 PRINT"AAAAHHH...YOU HAVE FALLEN INTO"
5610 GOSUB50:GOSUB50
5620 M$="A POOL OF ACID...NOW YOU KNOW WHY THE MIRRORS WERE SO BADLY TARNISHED...":GOSUB1
5650 SC=20
5660 QU=3
5670 GOTO2590
5680 M$="TOO BAD YOU CHOSE THAT EXIT FROM THE ALCOVE...":GOSUB1
5710 GOSUB51
5720 M$="A GIANT FUNNEL-WEB SPIDER LEAPS ON YOU...AND BEFORE YOU CAN REACT, BITES YOU ON THE NECK...":GOSUB1
5750 PRINT"YOU HAVE 10 SECONDS TO LIVE..."
5760 FORT=10TO1STEP-1
5770 PRINTTAB(T);T
5780 GOSUB50
5790 PRINT
5800 NEXTT
5810 SC=3:QU=5:GOTO2590
5840 M$="A STAIRWELL LEADS INTO THIS ROOM, A POOR AND COMMON HOVEL WITH MANY DOORS AND EXITS...":GOSUB1:RETURN
5890 ONRO-33GOSUB5920,5970,6030,6100,6240,6310,6360,6430,6510,6560,6600
5900 RETURN
5920 M$="IT IS HARD TO SEE IN THIS ROOM, AND YOU SLIP SLIGHTLY ON THE UNEVEN, ROCKY FLOOR...":GOSUB1:RETURN
5970 M$="HORRORS! THIS ROOM WAS ONCE THE TORTURE CHAMBER... SKELETONS LIE ON THE FLOOR, STILL IN CHAINS...":GOSUB1:RETURN
6030 M$="ANOTHER ROOM WITH VERY UNPLEASANT MEMORIES...":GOSUB1:GOSUB51
6070 M$="THIS FOUL HOLE WAS USED AS THE CHATEAU DUNGEON....":GOSUB1:RETURN
6100 M$="OH NO, THIS IS A GARGOYLE'S LAIR...":GOSUB1:GOSUB51
6130 M$="IT HAS BEEN HELD PRISONER HERE FOR THREE HUNDRED YEARS...":GOSUB1:GOSUB51
6160 PRINT:PRINT"IN HIS FRENZY HE LASHES OUT...":GOSUB51:PRINT"AND...":GOSUB51:PRINT"...AND BREAKS YOUR NECK!"
6210 SC=0:QU=3:GOTO2590
6240 M$="THIS WAS THE LOWER DANCING HALL WITH DOORS TO THE NORTH, EAST AND WEST, YOU WOULD SEEM TO BE "
6280 M$=M$+"ABLE TO FLEE ANY DANGER...":GOSUB1:GOSUB51:RETURN
6310 M$="THIS IS A DINGY PIT AT THE FOOT OF SOME DUBIOUS-LOOKING STAIRS. A DOOR LEADS TO THE EAST...":GOSUB1:RETURN
6360 M$="THE TROPHY ROOM OF THE CHATEAU. DOORS OPEN TO EACH DIRECTION. THE HEADS OF STRANGE CREATURES "
6400 M$=M$+"SHOT BY THE OWNERS ARE MOUNTED HIGH UP ON EACH WALL...":GOSUB1:RETURN
6430 M$="YOU HAVE FOUND A SECRET ROOM...":GOSUB1:GOSUB51
6460 M$="DOWN IN THIS ROOM THE ANCIENT NECROMANCERS OF THORIN PLIED THEIR EVIL CRAFT... THE SCENT "
6490 M$=M$+"OF VILE POTIONS REMAINS...":GOSUB1:RETURN
6510 M$="COBWEBS BRUSH YOUR FACE AS YOU MAKE YOUR WAY THROUGH THIS ROOM OF SHADOWS...":GOSUB1:RETURN
6560 M$="THIS GLOOMY PASSAGE LIES AT THE INTERSECTION OF THREE ROOMS...":GOSUB1:RETURN
6600 M$="YOU ARE IN THE REAR TURRET ROOM, BELOW THE FURTHEST WESTERN WALL OF THE ANCIENT CHATEAU...":GOSUB1:RETURN
6660 PRINT
6680 IFA(RO,7)>98ANDA(RO,9)=0ANDA(RO,10)=0THENRETURN
6690 M$="YOU CAN SEE: ":IFA(RO,7)>98THEN6720
6710 IFA(RO,7)<>0THENM$=M$+O$(A(RO,7))
6720 IFA(RO,9)>98THEN6740
6730 IFA(RO,9)<>0THENM$=M$+O$(A(RO,9))
6740 IFA(RO,10)>98THEN6760
6750 IFA(RO,10)<>0THENM$=M$+O$(A(RO,10))
6760 GOSUB1:GOSUB50:RETURN
6800 N=RND(-(PEEK(9)*256+PEEK(10))):N=0
6900 DIMA(44,10),P(5),O$(20),V(20),M$(20),T(5)
6910 ST=RND(16)+2:CH=RND(16)+2:DE=RND(16)+2:IT=RND(16)+2:WI=RND(16)+2:CO=RND(16)+2:CA=0:RO=27:QU=1:MK=0:BOX=0
7020 PRINT:PRINT"LOAD 'CHATEAU2'":CLOAD*A,"CHATEAU2":FORZ=1TO20:READO$(Z),V(Z):NEXTZ:FORQ=1TO16
7190 Z=INT(RND(0)*44)+1:IFZ=5ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7190
7210 IFA(Z,7)<>0THEN7190
7220 A(Z,7)=Q:NEXTQ:PRINT:PRINT"JUST A FEW MOMENTS MORE...":FORJ=1TO20:READM$(J):IFJ=1THEN7350
7310 Z=INT(RND(0)*44)+1:IFZ=5ORZ=16ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7310
7330 IFA(Z,8)<>0THEN7310
7340 A(Z,8)=J
7350 NEXTJ:RETURN
7830 DATA"AXE",0,"SWORD",0,"DAGGER",0,"MACE",0,"QUARTERSTAFF",0,"MORNING STAR",0,"FALCHION",0
7850 DATA"CRYSTAL RALL",99,"AMULET",247,"EBONY RING",166,"GEMS",462,"MYSTIC SCROLL",195,"HEALING POTION",231
7851 DATA"DILITHIUM CRYSTALS",162,"COPPER PIECES",27,"DIADEM",141,"SILVER KEY",0,"GOLDEN KEY",0,"CHEST OF STONE",0
7852 DATA"CHEST MADE OF IRON",0
7870 DATA"DWARF","MONOCEROS","PARADRUS","VAMPYRE","WRNACH","GIOLLA DACKER","KRAKEN","FENRIS WOLF","CALOPUS","BASILISK"
7880 DATA"GRIMOIRE","FLYING BUFFALO","BERSERKOID","WYRM","CROWTHERWOOD","GYGAX","RAGNAROK","FOMORINE","HAFGYGR","GRENDEL"
