0 CLS:CLEAR450:DIMZZ,CC,M$,A(44,10),P(5),O$(20),V(20),M$(20),T(5),D$(6),F(6):GOSUB6800:GOTO60
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
5 FORCC=1TO32:PRINT"-";:NEXT:POKE49151,64:POKE17026,48:RETURN
10 GOSUB1:GOSUB51:RETURN
30 IFA(RO,J)>98THEN40
35 IFA(RO,J)<>0THENM$=M$+O$(A(RO,J))
40 RETURN
50 GOSUB51
51 FORZZ=1TO1500:NEXTZZ:RETURN
60 CLS:GOSUB3300:IFA(RO,7)<>0ORA(RO,9)<>0ORA(RO,10)<>0THENGOSUB6660
90 IFA(RO,7)>98THENM$="ONE OF THE DOORS IS LOCKED PREVENTING YOU FROM EXPLORING FURTHER":GOSUB1
100 IFA(RO,8)=0THEN110
101 M$="LOOK OUT! THERE IS A "+M$(A(RO,8))+" HERE!":GOSUB1
103 IFRND(0)>.7ANDA(RO,8)<>1THENM$="THE "+M$(A(RO,8))+" ATTACKS!":GOSUB1:KW=1:GOSUB1400:GOTO60
110 FORJ=1TO6:IFF(J)<1THENF(J)=0:GOTO120
111 IFRND(0)>.84THENF(J)=F(J)-1
120 NEXTJ:IFRND(0)>.84ANDRO=16ANDA(RO,8)=1THEN175
171 GOTO180
172 PRINT:PRINT"YOUR ATTRIBUTES ARE:":PRINT"1 STR"F(1);TAB(11);"2 CHA"F(2);TAB(22)"3 DEX"F(3)
173 PRINT"4 INT"F(4);TAB(11)"5 WIS"F(5);TAB(22)"6 CON"F(6):RETURN
175 PRINT:M$="YOU HEAR A WHISPERED WARNING: 'DO SOMETHING ABOUT THE DWARF'":GOSUB10
180 GOSUB172:IFF(1)*F(2)*F(3)*F(4)*F(5)*F(6)=0THENM$="YOU ARE EXHAUSTED, SO THIS ADVENTURE MUST END":GOSUB1:QU=2:GOTO2590   
230 F=0:FORJ=1TO5:IFP(J)<>0THENF=1
260 NEXTJ:GOTO340
270 IFF=0THEN340
280 CA=0:PRINT:PRINT"YOU ARE CARRYING:":FORJ=1TO5:IFP(J)<>0THENPRINTO$(P(J)):CA=CA+V(P(J))
320 NEXTJ
330 IFCA>0THENPRINT"TOTAL VALUE";CA
340 PRINT:KW=0
390 INPUT"WHAT DO YOU DO";A$:IFA$=""THEN390
400 IFLEFT$(A$,3)="INV"THEN280
410 AS=ASC(A$)
430 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO430
440 B$=LEFT$(A$,3)
450 IFB$="HEL"THENM$="READING & DRINKING MAY HELP.":GOSUB10:GOTO60
460 IFB$="QUI"THENQU=4:GOTO2530
470 N=1
480 IFMID$(A$,N,1)=" "THEN485
481 GOTO490
485 C$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN530
486 GOTO500
490 IFN<MTHENN=N+1:GOTO480
500 PRINT"I NEED TWO WORDS.":GOTO390
530 IFRO<>8ANDRO<>34THEN560
540 IFB$="KIL"ORB$="FIG"ORB$="ATT"THENKW=1:GOSUB1400
550 IFA(RO,7)>98ANDB$<>"UNL"THENPRINT"** THE DOORS ARE LOCKED **":GOSUB51:GOTO60
560 IFB$="KIL"ORB$="FIG"ORB$="ATT"THENKW=1:GOSUB1400
570 IFB$="GO "ORB$="MOV"ORB$="CLI"ORB$="RUN"ORB$="WAL"THENKW=1:GOSUB740
580 IFB$="TAK"ORB$="GET"ORB$="STE"ORB$="LIF"THENKW=1:GOSUB950
590 IFB$="DRO"ORB$="PUT"ORB$="THR"ORB$="BRE"THENKW=1:GOSUB1170
600 IFB$="UNL"THENKW=1:GOSUB2360
610 IFB$="OPE"THENKW=1:GOSUB2660
620 IFB$="REA"THENKW=1:GOSUB2750
630 IFB$="DRI"ORB$="SWA"THENKW=1:GOSUB2870
640 IFB$="BRI"ORB$="PAY"THENKW=1:GOSUB3000
650 IFKW=1THEN60
660 M$="I DON'T UNDERSTAND "+A$:GOSUB10:GOTO60
730 M$="YOU CANNOT GO THAT WAY":GOTO10
740 IFRO=16ANDA(16,8)=1THENPRINT:M$="THE DWARF REFUSES TO LET YOU PROCEED.":GOTO10
800 C$=LEFT$(C$,1):FORJ=1TO6:ON1-(C$=D$(J))GOTO810:ON1-(A(RO,J)<>0)GOTO730:RO=A(RO,J)
810 NEXTJ:RETURN
950 F=0:FORJ=1TO5:IFP(J)<>0THENF=F+1
990 NEXTJ:IFF=5THENM$="YOU ARE CARRYING YOUR MAXIMUM":GOTO10
1010 IFC$="CHE"THENM$="IT IS TOO HEAVY TO LIFT":GOTO10
1020 IFA(RO,7)=0ANDA(RO,9)=0ANDA(RO,10)=0THENM$="I SEE NOTHING TO PICK UP":GOTO10
1030 D$="":E$="":F$="":N=0:D$=LEFT$(O$(A(RO,7)),3):E$=LEFT$(O$(A(RO,9)),3):F$=LEFT$(O$(A(RO,10)),3)
1070 IFC$=D$THENN=A(RO,7):A(RO,7)=0
1080 IFC$=E$THENN=A(RO,9):A(RO,9)=0
1090 IFC$=F$THENN=A(RO,10):A(RO,10)=0
1100 J=1
1110 IFP(J)=0THENP(J)=N:GOTO1130
1120 IFJ<5THENJ=J+1:GOTO1110
1130 IFN>0THENM$="YOU NOW HAVE THE "+O$(N):GOSUB1
1140 GOTO51
1170 F=0:FORJ=1TO5:IFP(J)<>0THENF=1
1200 NEXTJ
1210 IFF=0THENM$="YOU ARE NOT CARRYING ANYTHING":GOTO10
1220 IFA(RO,7)<>0ANDA(RO,9)<>0ANDA(RO,10)<>0THENM$="THIS ROOM IS FULL":GOTO10
1230 D$="":D=0
1240 FORJ=1TO18
1250 IFLEFT$(O$(J),3)=C$THEND$=O$(J):D=J
1260 NEXTJ
1270 IFD$=""THENM$="YOU'RE NOT HOLDING IT!":GOTO10
1280 FORJ=1TO5
1290 IFP(J)=DTHENP(J)=0
1300 NEXTJ:M$="":IFA(RO,7)=0THENA(RO,7)=D:GOTO1340
1320 IFA(RO,9)=0THENA(RO,9)=D:GOTO1340
1330 IFA(RO,10)=0THENA(RO,10)=D
1340 IFB$="DRO"THENM$="YOU HAVE DROPPED THE "+D$
1350 IFB$="PUT"THENM$="YOU HAVE PUT THE "+D$+" DOWN"
1360 IFB$="THR"THENM$="WITH A MIGHTY HEAVE, YOU THROW THE "+D$+" AWAY"
1370 IFB$="BRE"THENM$="YOU HAVE BROKEN THE "+D$
1380 GOTO10
1400 IFA(RO,8)=1THENM$="THE DWARF REFUSES TO FIGHT AND HIS MAGIC PROTECTS HIM":GOTO10
1420 IFA(RO,8)=0THENM$="YOU CAN'T FIGHT EMPTY AIR!":GOTO10
1470 G$=M$(A(RO,8)):S1=RND(16)+2:H1=RND(16)+2:D1=RND(16)+2:I1=RND(16)+2:W1=RND(16)+2:C1=RND(16)+2:GOSUB5
1550 M$="YOUR OPPONENT IS A "+G$:GOSUB1:GOSUB5:MT=0:HT=0:FF=S1*(INT(RND(0)*6)+1):M$="ITS DANGER LEVEL IS"+STR$(FF)
1610 F=0:FORJ=1TO5:T(J)=0:FORZ=1TO7:ON1-(P(J)=Z)GOTO1620:F=F+1:T(F)=Z
1620 NEXTZ:NEXTJ:IFF=0THENM$="YOU MUST FIGHT THE "+G$+" WITH YOUR BARE HANDS":GOSUB1:GOTO1910
1760 IFF>1THEN1830
1770 F=T(1)
1800 M$="YOU WILL FIGHT WITH YOUR "+O$(F):GOSUB1:FF=INT(FF*2/F):GOTO1910
1830 PRINT"CHOOSE YOUR WEAPON:":FORJ=1TO5:IFT(J)<>0THENPRINTJ;" - ";O$(T(J))
1860 NEXTJ
1870 INPUT"ENTER THE NUMBER";J:IFJ<1ORJ>7THEN1870
1880 IFT(J)=0THENPRINT"YOU DON'T HAVE THAT":GOTO1870
1890 F=T(J):GOTO1800
1910 GOSUB51:PRINT"IT HAS THE FOLLOWING ATTRIBUTES:";
1930 PRINT"1 STR"S1;TAB(11)"2 CHA"H1;TAB(22)"3 DEX"D1:PRINT"4 INT"I1;TAB(11)"5 WIS"W1;TAB(22)"6 CON"C1:GOSUB172:PRINT
2000 PRINT"WHICH ATTRIBUTES WILL YOU","FIGHT WITH (2)";:INPUTZ,Q:IFZ<1ORQ<1ORZ>6ORQ>6ORZ=QTHEN2000
2030 IFZ=1ORQ=1THENMT=MT+S1:HT=HT+F(1)
2040 IFZ=2ORQ=2THENMT=MT+H1:HT=HT+F(2)
2050 IFZ=3ORQ=3THENMT=MT+D1:HT=HT+F(3)
2060 IFZ=4ORQ=4THENMT=MT+I1:HT=HT+F(4)
2070 IFZ=5ORQ=5THENMT=MT+W1:HT=HT+F(5)
2080 IFZ=6ORQ=6THENMT=MT+C1:HT=HT+F(6)
2090 IFHT=MTTHENM$="YOU ARE EVENLY MATCHED":GOTO2120
2100 M$="IT LOOKS LIKE THE ODDS ARE IN FAVOR OF ":IFHT>MTTHENM$=M$+"YOU":GOTO2120
2110 M$=M$+"THE "+G$
2120 GOSUB1
2130 GOSUB50:K=INT(RND(0)*7):PRINT"THE "G$" -"MT:PRINT"YOU -"HT:PRINT
2170 IFK=0THENPRINT"YOU STRUCK A SPLENDID BLOW!":MT=MT-1
2180 IFK=1THENM$="THE "+G$+" LASHES OUT":GOSUB1:HT=HT-3:F(1)=F(1)-1:F(2)=F(2)-1
2190 IFK=2THENM$="YOU DRAW THE "+G$+"'S BLOOD":GOSUB1:MT=MT-1
2200 IFK=3THENPRINT"YOU ARE WOUNDED!":HT=HT-INT(RND(0)*3)-1:F(3)=F(3)-1
2210 IFK=4THENM$="THE "+G$+" IS TIRING":GOSUB1:MT=MT-1
2220 IFK=5THENPRINT"YOU ARE BLEEDING.":HT=HT-2:F(5)=F(5)-1:F(6)=F(6)-1
2230 IFK=6THENM$="YOU WOUND THE "+G$:GOSUB1:MT=MT-1
2240 IFRND(0)>.25ANDHT>0ANDMT>0THEN2130
2250 IFHT>MTTHENM$="YOU HAVE SLAIN THE "+G$:MK=MK+1:GOTO2267
2260 M$="THE "+G$+" GOT THE BETTER OF YOU THAT TIME"
2265 FORJ=1TO6:IFZ=JORQ=JTHENONJGOSUB2270,2280,2290,2300,2310,2320
2266 NEXTJ
2267 A(RO,8)=0:SOUND1,1:POKE17026,0:GOSUB1:GOSUB50:RETURN
2270 F(1)=4*INT(F(1)/5):RETURN
2280 F(2)=3*INT(F(2)/4):RETURN
2290 F(3)=6*INT(F(3)/7):RETURN
2300 F(4)=2*INT(F(4)/3):RETURN
2310 F(5)=5*INT(F(5)/6):RETURN
2320 F(6)=INT(F(6)/2):RETURN
2360 IFRO<>8ANDRO<>34ORA(RO,7)<99THENM$="THERE IS NO LOCKED DOOR HERE":GOTO10
2380 F=0:T=0:FORJ=1TO5:IFP(J)=17THENF=1:T=J
2420 IFP(J)=18THENF=2:T=J
2430 NEXTJ:IFF=0THENM$="YOU DO NOT HAVE A KEY!":GOTO10
2450 IFF=2ANDRO=8ORF=1ANDRO=34THENM$="THAT KEY DOES NOT FIT THIS DOOR":GOTO10
2460 M$="THERE IS A CLICK AS THE KEY TURNS...":GOSUB10:PRINT"THE DOOR IS NOW UNLOCKED.":A(RO,7)=0:P(T)=0:GOTO10
2530 SC=0:IFQU=4THEN2590
2560 PRINT:M$="CONGRATULATIONS! YOU HAVE COMPLETED THE ADVENTURE":GOSUB1
2570 SC=100
2580 PRINT:PRINT
2590 SC=(SC+20*CA+47*MK+F(1)+2*F(2)+3*F(3)+4+F(4)+5*F(5)+6*F(6))/QU
2600 IFMK>0THENPRINT"YOU KILLED"MK"MONSTERS"
2610 IFMK>0ANDCA>0THENPRINT"AND ";
2620 PRINT:M$="YOU FOUND"+STR$(CA)+" WORTH OF TREASURE":GOSUB1
2630 PRINT:PRINT"YOUR SCORE WAS"SC
2640 END
2660 F=0:IFC$<>"CHE"THENM$="THAT WOULD NOT BE WISE":GOTO10
2690 IFRO<>13ANDRO<>40THENM$="I CANNOT SEE ANYTHING TO OPEN HERE.":GOTO10
2700 IFBX=1THENIFRND(0)>.6THENM$="IT HOLDS NOTHING BUT DUST.":GOTO10
2710 IFRO=13ORBX=1THENM$="IT IS EMPTY":GOTO10
2720 IFBX=0THENM$="INSIDE YOU FIND A PARCHMENT, WITH THE FOLLOWING MESSAGE: 'A LITTLE MAN CAN BE BOUND BY GOLD'":BX=1:GOTO10
2730 RETURN
2750 F=0:FORJ=1TO5:IFP(J)=12THENF=1
2790 NEXTJ
2800 IFF=0THENM$="YOU ARE NOT HOLDING ANYTHING YOU CAN READ":GOTO10
2810 R=INT(RND(0)*3)
2820 IFR=0THENM$="IT SAYS: 'THE LOCKS NEED SPECIAL KEYS'"
2830 IFR=1THENM$="THE SCROLL READS: 'CHESTS CAN CONTAIN AID'"
2840 IFR=2THENM$="IT SAYS: 'THE AMULET IS IMPORTANT'"
2850 GOTO10
2870 F=0:FORJ=1TO5:IFP(J)=13THENF=J
2910 NEXTJ
2920 IFF=0THENM$="YOU CAN'T DRINK "+C$:GOTO10
2930 M$="YOU INSTANTLY FEEL YOUR STRENGTH BEING RESTORED. THE POTION BOTTLE MAGICALLY DISAPPEARS.":F(1)=20:P(F)=0:GOTO10
3000 IFA(RO,8)<>1THENM$="YOU SHOULDN'T TRY THAT WITH A "+M$(A(RO,8)):GOTO10
3020 PRINT"HE DEMANDS THE AMULET!"
3030 F=0:FORJ=1TO5:IFP(J)=9THENF=J
3060 NEXTJ:IFF<>0THEN3250
3080 PRINT"YOU DO NOT HAVE IT...":GOSUB51:IFRND(0)>.5THENM$="HE WOULD ACCEPT ANYTHING THAT HE REALLY WANTS":GOSUB1:GOTO3210
3110 PRINT"HE DECIDES, HOWEVER, TO ACCEPT","A 'GIFT' OF ";
3130 FORJ=1TO5:IFP(J)<>0THENF=J
3150 NEXTJ:IFF=0THEN3200
3170 PRINT"THE ";O$(F):P(F)=0:GOTO3270
3200 PRINT"ANYTHING VALUABLE"
3210 PRINT:PRINT"BUT YOU HAVE NOTHING"
3220 PRINT"AND SO HE KILLS YOU!":QU=3:GOTO2570
3250 PRINT"LUCKY FOR YOU THAT YOU HAD IT!":P(F)=0
3270 A(RO,8)=0:GOTO50
3300 IFRO>11THEN4140
3320 ONROGOSUB3340,3380,3450,3530,3620,3720,3790,3840,3920,3990,4060:RETURN
3340 M$="YOU ARE OUT ON THE BATTLEMENTS OF THE CHATEAU. THERE IS ONLY ONE WAY BACK.":GOSUB1:RETURN
3380 M$="THIS IS AN EERIE ROOM, WHERE MAGICIANS COVORTED WITH SPRITES AND WEREBEASTS. "
3420 M$=M$+"EXITS LEAD IN 3 DIRECTIONS. AN FOUL SMELL COMES FROM THE SOUTH.":GOSUB1:RETURN
3450 M$="AN OLD STRAW MATTRESS LIES IN ONE CORNER. IT HAS BEEN RIPPED APART TO FIND ANY TREASURE HIDDEN IN IT. LIGHT "
3460 M$=M$+"COMES FAINTLY FROM A WINDOW TO THE NORTH AND AROUND THE DOORS TO THE SOUTH, EAST AND WEST.":GOSUB1:RETURN
3530 M$="THIS WOODEN-PANELLED ROOM IS DAMP AND UNCOMFORTABLE.":GOSUB1
3560 IFRND(0)>.5THENPRINT"A MOUSE RUNS ACROSS THE FLOOR.":GOTO3570
3561 PRINT"A BAT FLITS ACROSS THE CEILING."
3570 M$="THERE ARE 3 DOORS, ONE MADE OF IRON. YOUR 6TH SENSE WARNS YOU TO CHOOSE CAREFULLY.":GOSUB1:RETURN
3620 M$="YOU IGNORE YOUR INTUITION. A SPELL OF LIVING STONE, PRIMED TO TRAP THE FIRST INTRUDER HAS BEEN SET "
3660 M$=M$+"ON YOU. WITH YOUR LAST SECONDS OF LIFE YOU HAVE TIME ONLY TO FEEL PROFOUND REGRET.":GOSUB1:SC=50:GOTO2590
3720 M$="YOU ARE IN AN L-SHAPED ROOM. HEAVY PARCHMENT LINES THE WALLS. YOU CAN SEE THROUGH AN ARCHWAY "
3760 M$=M$+"TO THE EAST, BUT THAT IS NOT THE ONLY EXIT FROM THIS ROOM.":GOSUB1:RETURN
3790 M$="THERE IS AN ARCHWAY TO THE WEST, LEADING TO AN L-SHAPED ROOM. A DOOR LEADS IN THE OPPOSITE DIRECTION.":GOSUB1:RETURN
3840 M$="THIS MUST BE THE CHATEAU'S KITCHEN BUT ANY FOOD LEFT HERE HAS LONG ROTTED AWAY. "
3890 M$=M$+"A DOOR LEADS TO THE NORTH, AND THERE IS ONE TO THE WEST.":GOSUB1:RETURN
3920 M$="YOU FIND YOURSELF IN A SMALL, ROOM WHICH MAKES YOU FEEL CLAUSTROPHOBIC. "
3960 M$="THERE IS A PICTURE OF A BLACK DRAGON PAINTED ON THE NORTH WALL, ABOVE THE DOOR.":GOSUB1:RETURN
3990 M$="A STAIRWELL ENDS AT THIS LANDING. THERE IS AN OLD RUSTY IRON DOOR TO THE NORTH.":GOSUB1:RETURN
4060 M$="THERE IS A STONE ARCHWAY TO THE NORTH, YOU ARE IN A VERY LONG ROOM.":GOSUB1
4090 M$="FRESH AIR BLOWS DOWN A STAIRWELL. RICH RED DRAPES COVER THE WALLS. YOU CAN SEE DOORS TO THE SOUTH AND EAST."
4130 GOSUB1:RETURN
4140 IFRO>22THEN5030
4150 ONRO-11GOSUB4170,4240,4330,4390,4460,4530,4610,4710,4760,4890,4970:RETURN
4170 M$="YOU HAVE ENTERED A ROOM FILLED WITH SWIRLING, CHOKING SMOKE. YOU MUST LEAVE QUICKLY TO REMAIN "
4210 M$=M$+"HEALTHY ENOUGH TO CONTINUE YOUR QUEST.":GOSUB1:RETURN
4240 M$="THERE IS A MIRROR IN THE CORNER YOU GLANCE AT IT, AND FEEL SUDDENLY VERY ILL. YOU REALISE THE LOOKING-GLASS HAS "
4290 M$=M$+"BEEN ENFUSED WITH A SPELL OF CHARISMA REDUCTION. OH DEAR!":GOSUB1:F(2)=F(2)-1:RETURN
4330 M$="THIS ROOM IS RICHLY FINISHED, WITH A WHITE MARBLE FLOOR. STRANGE FOOTPRINTS LEAD TO THE TWO DOORS "
4370 M$=M$+"FROM THIS ROOM. DARE YOU FOLLOW THEM?":GOSUB1:RETURN
4390 M$="YOU ARE IN A VERY LONG HALLWAY, LINED ON EACH SIDE WITH RICH, RED DRAPES. THEY ARE PARTED HALFWAY DOWN "
4440 M$=M$+"THE EAST WALL, WHERE THERE IS A DOOR.":GOSUB1:RETURN
4460 M$="SOMEONE HAS SPENT A LONG TIME PAINTING THIS ROOM A BRIGHT YELLOW. YOU REMEMBER READING THAT "
4500 M$=M$+"YELLOW IS THE ANCIENT ORACLE'S COLOR OF WARNING!":GOSUB1:RETURN
4530 M$="AS YOU DESCEND THE LADDER YOU FALL AND THE LADDER CRASHES DOWN INTO THE ROOM! NOW THERE IS NO "
4570 M$=M$+"WAY BACK! A SMALL DOOR LEADS EAST FROM THIS VERY CRAMPED ROOM.":GOSUB1:RETURN
4610 M$="YOU FIND YOURSELF IN A HALL OF MIRRORS AND SEE YOURSELF REFLECTED A HUNDRED TIMES OR MORE. "
4650 M$=M$+"THROUGH THE BRIGHT GLARE YOU CAN MAKE OUT DOORS IN ALL DIRECTIONS.":GOSUB1
4660 M$="YOU NOTICE THE MIRRORS AROUND THE EAST DOOR ARE HEAVILY TARNISHED.":GOSUB1:RETURN
4710 M$="YOU FIND YOURSELF IN A LONG CORRIDOR. YOUR FOOTSTEPS ECHO AS YOU WALK.":GOSUB1:RETURN
4760 M$="YOU FEEL AS YOU'VE BEEN WANDERING AROUND THIS CHATEAU FOR EVER AND BEGIN TO DESPAIR OF EVER ESCAPING.":GOSUB1
4810 M$="STILL, YOU CAN'T GET TOO DEPRESSED, BUT MUST STRUGGLE ON. LOOKING AROUND, YOU SEE A ROOM "
4820 M$=M$+"WITH A TIMBERED CEILING AND WHITE ROUGHLY-FINISHED WALLS. THERE ARE TWO DOORS.":GOSUB1
4870 GOSUB51:RETURN
4890 M$="YOU ARE IN A SMALL ALCOVE. YOU LOOK AROUND, BUT CAN'T SEE IN THE GLOOM. PERHAPS IF YOU "
4930 M$=M$+"WAIT A WHILE, YOUR EYES WILL ADJUST TO THE DARK.":GOTO10
4970 M$="A DRIED-UP FOUNTAIN STANDS IN THE CENTER OF THIS COURTYARD, WHICH ONCE HELD BEAUTIFUL FLOWERS--NOW LONG DEAD."
5020 GOSUB1:RETURN
5030 IFRO>33THEN5890
5040 ONRO-22GOSUB5060,5110,5160,5230,5280,5380,5450,5530,5590,5680,5840:RETURN
5060 M$="THE SCENT OF DRIED FLOWERS FILLS THIS BRIGHT ROOM. THERE ARE TWO EXITS FROM IT.":GOSUB1:RETURN
5110 M$="THIS IS A ROUND STONE CAVERN OFF THE SIDE OF THE ALCOVE TO YOUR NORTH.":GOSUB1:RETURN
5160 M$="YOU ARE IN A LARGE CIRCULAR ROOM, WHICH SEEMS TO HAVE BEEN USED FOR GAMES. RUBBLE COVERS "
5200 M$=M$+"THE FLOOR, PARTIALLY BLOCKING THE ONLY EXIT.":GOSUB1:RETURN
5230 M$="THROUGH THE DIM MUSTINESS OF THIS SMALL POTTING SHED YOU CAN SEE A STAIRWELL.":GOSUB1:RETURN
5280 M$="YOU BEGIN THIS ADVENTURE IN A WOOD OUTSIDE THE CHATEAU.":GOSUB1
5320 PRINT:M$="WHILE OUT WALKING, YOU COME ACROSS A RAMSHACKLE SHED. ENTERING IT, YOU SEE A HOLE "
5350 M$=M$+"IN ONE CORNER. AN OLD LADDER LEADS DOWN...":GOSUB1:RETURN
5380 PRINT"HOW WONDERFUL! FRESH AIR... BIRDS ARE SINGING... YOU ARE FREE AT LAST!":GOSUB1
5430 PRINT:PRINT:GOTO2560
5450 M$="THE SMELL CAME FROM BODIES ROTTING IN HUGE TRAPS...":GOSUB50
5490 M$="ONE SPRINGS SHUT ON YOU, TRAPPING YOU FOREVER.":GOSUB1:QU=3.5:GOTO2570
5530 PRINT"YOU FALL INTO A FLAMING PIT...":IFRND(0)>.7THEN5530
5560 SC=10:QU=3.4:GOTO2590
5590 PRINT"AAAAHHH...YOU HAVE FALLEN INTO":GOSUB51
5620 M$="A POOL OF ACID... NOW YOU KNOW WHY THE MIRRORS WERE SO BADLY TARNISHED!":GOSUB1:SC=20:QU=3:GOTO2590
5680 M$="TOO BAD YOU CHOSE THAT EXIT FROM THE ALCOVE...":GOSUB10
5720 M$="A GIANT FUNNEL-WEB SPIDER LEAPS ON YOU! BEFORE YOU CAN REACT, IT BITES...":GOSUB1
5750 PRINT"YOU HAVE 10 SECONDS TO LIVE..."
5760 FORT=10TO1STEP-1:PRINTTAB(T);T:GOSUB51:PRINT:NEXTT:SC=3:QU=5:GOTO2590
5840 M$="A STAIRWELL LEADS INTO THIS ROOM, A POOR AND COMMON HOVEL WITH MANY DOORS AND EXITS.":GOSUB1:RETURN
5890 ONRO-33GOSUB5920,5970,6030,6100,6240,6310,6360,6430,6510,6560,6600:RETURN
5920 M$="IT IS HARD TO SEE IN THIS ROOM, AND YOU SLIP SLIGHTLY ON THE UNEVEN, ROCKY FLOOR.":GOSUB1:RETURN
5970 M$="HORRORS! THIS ROOM WAS ONCE THE TORTURE CHAMBER. SKELETONS LIE ON THE FLOOR, STILL IN CHAINS.":GOSUB1:RETURN
6030 M$="ANOTHER ROOM WITH VERY UNPLEASANT MEMORIES.":GOSUB10
6070 M$="THIS FOUL HOLE WAS USED AS THE CHATEAU DUNGEON.":GOSUB1:RETURN
6100 M$="OH NO, THIS IS A GARGOYLE'S LAIR!":GOSUB10
6130 M$="IT HAS BEEN HELD PRISONER HERE FOR THREE HUNDRED YEARS.":GOSUB10
6160 PRINT:PRINT"IN HIS FRENZY HE LASHES OUT...":GOSUB51:PRINT"AND...":GOSUB51:PRINT"...AND BREAKS YOUR NECK!"
6210 SC=0:QU=3:GOTO2590
6240 M$="THIS WAS THE LOWER DANCING HALL WITH DOORS TO THE NORTH, EAST AND WEST. YOU WOULD SEEM TO BE "
6280 M$=M$+"ABLE TO FLEE ANY DANGER.":GOTO10
6310 M$="THIS IS A DINGY PIT AT THE FOOT OF SOME DUBIOUS-LOOKING STAIRS. A DOOR LEADS TO THE EAST.":GOSUB1:RETURN
6360 M$="THE TROPHY ROOM OF THE CHATEAU. DOORS OPEN TO EACH DIRECTION. THE HEADS OF STRANGE CREATURES "
6400 M$=M$+"SHOT BY THE OWNERS ARE MOUNTED HIGH UP ON EACH WALL.":GOSUB1:RETURN
6430 PRINT"YOU HAVE FOUND A SECRET ROOM.":GOSUB51:M$="IN THIS ROOM THE ANCIENT NECROMANCERS OF THORIN PLIED THEIR "
6440 M$=M$+"EVIL CRAFT. THE SCENT OF THEIR VILE POTIONS REMAINS.":GOSUB1:RETURN
6510 M$="COBWEBS BRUSH YOUR FACE AS YOU MAKE YOUR WAY THROUGH THIS ROOM OF SHADOWS.":GOSUB1:RETURN
6560 M$="THIS GLOOMY PASSAGE LIES AT THE INTERSECTION OF THREE ROOMS.":GOSUB1:RETURN
6600 M$="YOU ARE IN THE REAR TURRET ROOM, BELOW THE WESTERN WALL OF THE CHATEAU.":GOSUB1:RETURN
6660 PRINT:IFA(RO,7)>98ANDA(RO,9)=0ANDA(RO,10)=0THENRETURN
6690 M$="YOU CAN SEE: ":J=7:GOSUB30:J=9:GOSUB30:J=10:GOSUB30:GOTO10
6800 Z=RND(-(PEEK(9)*256+PEEK(10))):N=0
6910 F(1)=RND(16)+2:F(2)=RND(16)+2:F(3)=RND(16)+2:F(4)=RND(16)+2:F(5)=RND(16)+2:F(6)=RND(16)+2:CA=0:RO=27:QU=1:MK=0:BX=0
7020 PRINT"LOAD 'CHATEAU2'":CLOAD*A,"CHATEAU2":FORZ=1TO20:READO$(Z),V(Z):NEXTZ:FORQ=1TO16
7190 Z=INT(RND(0)*44)+1:IFZ=5ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7190
7210 IFA(Z,7)<>0THEN7190
7220 A(Z,7)=Q:NEXTQ:PRINT:PRINT"JUST A FEW MOMENTS MORE...":FORJ=1TO20:READM$(J):IFJ=1THEN7350
7310 Z=INT(RND(0)*44)+1:IFZ=5ORZ=16ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN7310
7330 IFA(Z,8)<>0THEN7310
7340 A(Z,8)=J
7350 NEXTJ:FORJ=1TO6:READD$(J):NEXT:RETURN
7830 DATA"AXE",0,"SWORD",0,"DAGGER",0,"MACE",0,"QUARTERSTAFF",0,"MORNING STAR",0,"FALCHION",0
7850 DATA"CRYSTAL RALL",99,"AMULET",247,"EBONY RING",166,"GEMS",462,"MYSTIC SCROLL",195,"HEALING POTION",231
7851 DATA"DILITHIUM CRYSTALS",162,"COPPER PIECES",27,"DIADEM",141,"SILVER KEY",0,"GOLDEN KEY",0,"CHEST OF STONE",0
7852 DATA"CHEST MADE OF IRON",0,"DWARF","MONOCEROS","PARADRUS","VAMPYRE","WRNACH","GIOLLA DACKER","KRAKEN","FENRIS WOLF"
7870 DATA"CALOPUS","BASILISK","GRIMOIRE","FLYING BUFFALO","BERSERKOID","WYRM","CROWTHERWOOD","GYGAX","RAGNAROK"
7880 DATA"FOMORINE","HAFGYGR","GRENDEL",N,S,E,W,U,D
