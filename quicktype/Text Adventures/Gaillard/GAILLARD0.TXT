0 CLS:PRINT"JUST A FEW MOMENTS...":CLEAR350:DIMZZ,CC,M$,A(44,10),P(5),O$(20),V(20),M$(20),T(5),D$(6),F(6):GOSUB282:GOTO14
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 FORCC=1TO32:PRINT"-";:NEXT:RETURN
6 GOSUB1:GOSUB13:RETURN
7 PRINT:PRINT"YOUR ATTRIBUTES ARE:":PRINT"1 STR"F(1)TAB(11)"2 CHA"F(2)TAB(22)"3 DEX"F(3)
8 PRINT"4 INT"F(4)TAB(11)"5 WIS"F(5)TAB(22)"6 CON"F(6):RETURN
9 IFA(C,J)>98THEN11
10 IFA(C,J)THENM$=M$+O$(A(C,J))+" "
11 RETURN
12 GOSUB13
13 FORZZ=1TO1500:NEXT:RETURN
14 CLS:GOSUB189:IFA(C,7)<>0ORA(C,9)<>0ORA(C,10)<>0THENGOSUB280
15 IFA(C,7)>98THENM$="ONE OF THE DOORS IS LOCKED PREVENTING YOU FROM EXPLORING FURTHER":GOSUB1
16 IFA(C,8)=0THEN19
17 M$="LOOK OUT! THERE IS A "+M$(A(C,8))+" HERE!":GOSUB1
18 IFRND(0)>.7ANDA(C,8)<>1THENM$="THE "+M$(A(C,8))+" ATTACKS!":GOSUB1:KW=1:GOSUB94:GOTO14
19 FORJ=1TO6:IFF(J)<1THENF(J)=0
20 NEXTJ:IFRND(0)>.84THENF(1)=F(1)-1
21 IFRND(0)>.84ANDC=16ANDA(C,8)=1THENPRINT:M$="YOU HEAR A WHISPERED WARNING: 'DO SOMETHING ABOUT THE DWARF'":GOSUB6
22 GOSUB7:IFF(1)*F(2)*F(3)*F(4)*F(5)*F(6)=0THENM$="YOU ARE EXHAUSTED, SO THIS ADVENTURE MUST END":GOSUB1:QU=2:GOTO151   
23 F=0:FORJ=1TO5:IFP(J)THENF=1
24 NEXTJ:GOTO29
25 IFF=0THEN29
26 CA=0:PRINT:PRINT"YOU ARE CARRYING:":FORJ=1TO5:IFP(J)THENPRINTO$(P(J)):CA=CA+V(P(J))
27 NEXTJ
28 IFCA>0THENPRINT"TOTAL VALUE"CA
29 PRINT:KW=0
30 INPUT"WHAT DO YOU DO";A$:IFA$=""THEN30
31 IFLEFT$(A$,3)="INV"THEN26
32 AS=ASC(A$)
33 M=LEN(A$):IFM<7THENA$=A$+" ":GOTO33
34 B$=LEFT$(A$,3):IFB$="HEL"THENM$="READING & DRINKING MAY HELP.":GOSUB6:GOTO14
37 N=1
38 IFMID$(A$,N,1)=" "THEN40
39 GOTO42
40 C$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN44
41 GOTO43
42 IFN<MTHENN=N+1:GOTO38
43 PRINT"I NEED 2 WORDS.":GOTO30
44 IFC<>8ANDC<>34THEN47
45 IFB$="KIL"ORB$="FIG"ORB$="ATT"THENKW=1:GOSUB94
46 IFA(C,7)>98ANDB$<>"UNL"THENPRINT"THE DOORS ARE LOCKED.":GOSUB13:GOTO14
47 IFB$="KIL"ORB$="FIG"ORB$="ATT"THENKW=1:GOSUB94
48 IFB$="GO "ORB$="MOV"ORB$="CLI"ORB$="RUN"THENKW=1:GOSUB59
49 IFB$="TAK"ORB$="GET"ORB$="STE"ORB$="LIF"THENKW=1:GOSUB62
50 IFB$="DRO"ORB$="PUT"ORB$="THR"ORB$="BRE"THENKW=1:GOSUB75
51 IFB$="UNL"THENKW=1:GOSUB141
52 IFB$="OPE"THENKW=1:GOSUB157
53 IFB$="REA"THENKW=1:GOSUB163
54 IFB$="DRI"ORB$="SWA"THENKW=1:GOSUB170
55 IFB$="BRI"ORB$="PAY"THENKW=1:GOSUB175
56 IFKW=1THEN14
57 M$="I DON'T UNDERSTAND "+A$:GOSUB6:GOTO14
58 M$="YOU CANNOT GO THAT WAY":GOTO6
59 IFC=16ANDA(16,8)=1THENPRINT:M$="THE DWARF REFUSES TO LET YOU PROCEED.":GOTO6
60 C$=LEFT$(C$,1):FORJ=1TO6:ON1-(C$=D$(J))GOTO61:ON1-(A(C,J)<>0)GOTO58:C=A(C,J)
61 NEXTJ:RETURN
62 F=0:FORJ=1TO5:IFP(J)THENF=F+1
63 NEXTJ:IFF=5THENM$="YOU ARE CARRYING YOUR MAXIMUM":GOTO6
64 IFC$="CHE"THENM$="IT IS TOO HEAVY TO LIFT":GOTO6
65 IFA(C,7)=0ANDA(C,9)=0ANDA(C,10)=0THENM$="I SEE NOTHING TO PICK UP":GOTO6
66 D$="":E$="":F$="":N=0:D$=LEFT$(O$(A(C,7)),3):E$=LEFT$(O$(A(C,9)),3):F$=LEFT$(O$(A(C,10)),3)
67 IFC$=D$THENN=A(C,7):A(C,7)=0
68 IFC$=E$THENN=A(C,9):A(C,9)=0
69 IFC$=F$THENN=A(C,10):A(C,10)=0
70 J=1
71 IFP(J)=0THENP(J)=N:GOTO73
72 IFJ<5THENJ=J+1:GOTO71
73 IFN>0THENM$="YOU NOW HAVE THE "+O$(N):GOSUB1
74 GOTO13
75 F=0:FORJ=1TO5:IFP(J)THENF=1
76 NEXTJ:IFF=0THENM$="YOU ARE NOT CARRYING ANYTHING":GOTO6
78 IFA(C,7)THENIFA(C,9)THENIFA(C,10)THENM$="THIS ROOM IS FULL":GOTO6
79 D$="":D=0
80 FORJ=1TO18
81 IFLEFT$(O$(J),3)=C$THEND$=O$(J):D=J
82 NEXTJ
83 IFD$=""THENM$="YOU'RE NOT HOLDING IT!":GOTO6
84 FORJ=1TO5
85 IFP(J)=DTHENP(J)=0
86 NEXTJ:M$="":IFA(C,7)=0THENA(C,7)=D:GOTO89
87 IFA(C,9)=0THENA(C,9)=D:GOTO89
88 IFA(C,10)=0THENA(C,10)=D
89 IFB$="DRO"THENM$="YOU HAVE DROPPED THE "+D$
90 IFB$="PUT"THENM$="YOU HAVE PUT THE "+D$+" DOWN"
91 IFB$="THR"THENM$="WITH A MIGHTY HEAVE, YOU THROW THE "+D$+" AWAY"
92 IFB$="BRE"THENM$="YOU HAVE BROKEN THE "+D$
93 GOTO6
94 IFA(C,8)=1THENM$="THE DWARF REFUSES TO FIGHT AND HIS MAGIC PROTECTS HIM":GOTO6
95 IFA(C,8)=0THENM$="YOU CAN'T FIGHT EMPTY AIR!":GOTO6
96 G$=M$(A(C,8)):S1=RND(16)+2:H1=RND(16)+2:D1=RND(16)+2:I1=RND(16)+2:W1=RND(16)+2:C1=RND(16)+2:GOSUB5
97 M$="YOUR OPPONENT IS A "+G$:GOSUB1:GOSUB5:MT=0:HT=0
98 F=0:FORJ=1TO5:T(J)=0:FORZ=1TO7:ON1-(P(J)=Z)GOTO99:F=F+1:T(F)=Z
99 NEXTZ:NEXTJ:IFF=0THENM$="YOU MUST FIGHT THE "+G$+" WITH YOUR BARE HANDS":GOSUB1:FF=1:GOTO108
100 IFF>1THEN103
101 F=T(1)
102 M$="YOU WILL FIGHT WITH YOUR "+O$(F):GOSUB1:FF=(F+2)/2:GOTO108
103 PRINT"CHOOSE YOUR WEAPON:":FORJ=1TO5:IFT(J)THENPRINTJ" - "O$(T(J))
104 NEXTJ
105 INPUT"ENTER THE NUMBER";J:IFJ<1ORJ>7THEN105
106 IFT(J)=0THENPRINT"YOU DON'T HAVE THAT":GOTO105
107 F=T(J):GOTO102
108 GOSUB13:PRINT"IT HAS THE FOLLOWING ATTRIBUTES:";
109 PRINT"1 STR"S1;TAB(11)"2 CHA"H1;TAB(22)"3 DEX"D1:PRINT"4 INT"I1;TAB(11)"5 WIS"W1;TAB(22)"6 CON"C1:GOSUB7:PRINT
110 PRINT"WHICH 2 ATTRIBUTES WILL YOU","FIGHT WITH";:INPUTZ,Q:IFZ<1ORQ<1ORZ>6ORQ>6ORZ=QTHEN110
111 IFZ=1ORQ=1THENMT=MT+S1:HT=HT+F(1)
112 IFZ=2ORQ=2THENMT=MT+H1:HT=HT+F(2)
113 IFZ=3ORQ=3THENMT=MT+D1:HT=HT+F(3)
114 IFZ=4ORQ=4THENMT=MT+I1:HT=HT+F(4)
115 IFZ=5ORQ=5THENMT=MT+W1:HT=HT+F(5)
116 IFZ=6ORQ=6THENMT=MT+C1:HT=HT+F(6)
117 IFHT=MTTHENM$="YOU ARE EVENLY MATCHED":GOTO120
118 M$="IT LOOKS LIKE THE ODDS ":IFHT>MTTHENM$=M$+"ARE IN YOUR FAVOR":GOTO120
119 M$=M$+"FAVOR OF THE "+G$
120 GOSUB1
121 GOSUB12:K=RND(7):PRINT"THE "G$" -"INT(MT):PRINT"YOU -"HT:PRINT
122 IFK=1THENPRINT"YOU STRUCK A SPLENDID BLOW!":MT=MT-FF
123 IFK=2THENM$="THE "+G$+" LASHES OUT":GOSUB1:HT=HT-3:F(Z)=F(Z)-1:F(Q)=F(Q)-1
124 IFK=3THENM$="YOU DRAW THE "+G$+"'S BLOOD":GOSUB1:MT=MT-FF
125 IFK=4THENPRINT"YOU ARE WOUNDED!":HT=HT-RND(3):IFRND(0)>.5THENR=RND(6):F(R)=F(R)-1
126 IFK=5THENM$="THE "+G$+" IS TIRING":GOSUB1:MT=MT-FF
127 IFK=6THENPRINT"YOU ARE BLEEDING.":HT=HT-2:R=RND(6):F(R)=F(R)-1
128 IFK=7THENM$="YOU WOUND THE "+G$:GOSUB1:MT=MT-FF
129 IFRND(0)>.25ANDHT>0ANDINT(MT)>0THEN121
130 IFHT>MTTHENM$="YOU HAVE SLAIN THE "+G$:MK=MK+1:GOTO134
131 M$="THE "+G$+" GOT THE BETTER OF YOU THAT TIME"
132 FORJ=1TO6:IFZ=JORQ=JTHENONJGOSUB135,136,137,138,139,140
133 NEXTJ
134 A(C,8)=0:SOUND1,1:GOSUB1:GOSUB12:RETURN
135 F(1)=4*INT(F(1)/5):RETURN
136 F(2)=3*INT(F(2)/4):RETURN
137 F(3)=6*INT(F(3)/7):RETURN
138 F(4)=2*INT(F(4)/3):RETURN
139 F(5)=5*INT(F(5)/6):RETURN
140 F(6)=INT(F(6)/2):RETURN
141 IFC<>8ANDC<>34ORA(C,7)<99THENM$="THERE IS NO LOCKED DOOR HERE":GOTO6
142 F=0:T=0:FORJ=1TO5:IFP(J)=17THENF=1:T=J
143 IFP(J)=18THENF=2:T=J
144 NEXTJ:IFF=0THENM$="YOU DO NOT HAVE A KEY!":GOTO6
145 IFF=2ANDC=8ORF=1ANDC=34THENM$="THAT KEY DOES NOT FIT THIS DOOR":GOTO6
146 M$="THERE IS A CLICK AS THE KEY TURNS...":GOSUB6:M$="THE DOOR IS NOW UNLOCKED.":A(C,7)=0:P(T)=0:GOTO6
148 PRINT:M$="CONGRATULATIONS! YOU HAVE COMPLETED THE ADVENTURE.":GOSUB1
149 SC=100:PRINT
151 SC=(SC+20*CA+47*MK+F(1)+2*F(2)+3*F(3)+4+F(4)+5*F(5)+6*F(6))/QU
152 M$="":IFMK>0THENM$="YOU KILLED"+STR$(MK)+" MONSTERS AND "
154 M$=M$+"YOU FOUND"+STR$(CA)+" WORTH OF TREASURE.":GOSUB1
155 PRINT"YOUR SCORE WAS"INT(SC):END
157 F=0:IFC$<>"CHE"THENM$="THAT WOULD NOT BE WISE":GOTO6
158 IFC<>13ANDC<>40THENM$="I CANNOT SEE ANYTHING TO OPEN HERE.":GOTO6
159 IFBX=1THENIFRND(0)>.6THENM$="IT HOLDS NOTHING BUT DUST.":GOTO6
160 IFC=13ORBX=1THENM$="IT IS EMPTY":GOTO6
161 IFBX=0THENM$="INSIDE YOU FIND A PARCHMENT, WITH THE FOLLOWING MESSAGE: 'A LITTLE MAN CAN BE BOUND BY GOLD'":BX=1:GOSUB6
162 GOSUB12:RETURN
163 F=0:FORJ=1TO5:IFP(J)=12THENF=1
164 NEXTJ
165 IFF=0THENM$="YOU ARE NOT HOLDING ANYTHING YOU CAN READ":GOTO6
166 R=RND(3):IFR=1THENM$="IT SAYS: 'THE LOCKS NEED SPECIAL KEYS'"
167 IFR=2THENM$="THE SCROLL READS: 'CHESTS CAN CONTAIN AID'"
168 IFR=3THENM$="IT SAYS: 'THE AMULET IS IMPORTANT'"
169 GOTO6
170 F=0:FORJ=1TO5:IFP(J)=13THENF=J
171 NEXTJ
172 IFF=0THENM$="YOU CAN'T DRINK "+C$:GOTO6
173 M$="YOU INSTANTLY FEEL YOUR STRENGTH BEING RESTORED. THE BOTTLE MAGICALLY DISAPPEARS.":GOSUB6
174 F(1)=20:P(F)=0:GOTO12
175 IFA(C,8)<>1THENM$="YOU SHOULDN'T TRY THAT WITH A "+M$(A(C,8)):GOTO6
176 PRINT"HE DEMANDS THE AMULET!"
177 F=0:FORJ=1TO5:IFP(J)=9THENF=J
178 NEXTJ:IFFTHEN187
179 PRINT"YOU DO NOT HAVE IT...":GOSUB13:IFRND(0)>.5THENM$="HE WOULD ACCEPT ANYTHING THAT HE REALLY WANTS":GOSUB1:GOTO185
180 M$="HE DECIDES, HOWEVER, TO ACCEPT A 'GIFT' OF ":F=0:FORJ=1TO5:IFP(J)THENF=J
182 NEXTJ:IFF=0THEN184
183 M$=M$+"THE "+O$(P(F)):P(F)=0:GOSUB1:GOTO188
184 M$=M$+"ANYTHING VALUABLE":GOSUB1
185 PRINT:PRINT"BUT YOU HAVE NOTHING"
186 PRINT"AND SO HE KILLS YOU!":QU=3:GOTO149
187 PRINT"LUCKY FOR YOU THAT YOU HAD IT!":P(F)=0
188 A(C,8)=0:GOTO12
189 IFC>11THEN212
190 ONCGOSUB191,192,194,196,200,202,204,205,207,209,210:RETURN
191 M$="YOU ARE OUT ON THE BATTLEMENTS. THERE IS ONLY ONE WAY BACK.":GOTO1
192 M$="THIS IS AN EERIE ROOM, WHERE MAGICIANS COMMUNED WITH SPRITES AND WEREBEASTS. "
193 M$=M$+"EXITS LEAD IN 3 DIRECTIONS. A FOUL SMELL COMES FROM THE SOUTH.":GOTO1
194 M$="AN OLD STRAW MATTRESS LIES IN ONE CORNER. IT HAS BEEN RIPPED APART (SEARCHED?). LIGHT "
195 M$=M$+"COMES FAINTLY FROM A WINDOW TO THE NORTH AND AROUND THE DOORS TO THE SOUTH, EAST AND WEST.":GOTO1
196 M$="THIS WOODEN-PANELLED ROOM IS DAMP AND UNCOMFORTABLE.":GOSUB1
197 IFRND(0)>.5THENPRINT"A MOUSE RUNS ACROSS THE FLOOR.":GOTO199
198 PRINT"A BAT FLITS ACROSS THE CEILING."
199 M$="THERE ARE 3 DOORS, ONE MADE OF IRON. YOUR 6TH SENSE WARNS YOU TO CHOOSE CAREFULLY.":GOTO1
200 M$="YOU IGNORE YOUR INTUITION. A SPELL OF LIVING STONE, PRIMED TO TRAP THE FIRST INTRUDER HAS BEEN SET "
201 M$=M$+"ON YOU. WITH YOUR LAST SECONDS OF LIFE YOU HAVE TIME ONLY TO FEEL PROFOUND REGRET.":GOSUB1:SC=50:GOTO151
202 M$="YOU ARE IN AN L-SHAPED ROOM. HEAVY PARCHMENT LINES THE WALLS. YOU CAN SEE THROUGH AN ARCHWAY "
203 M$=M$+"TO THE EAST, BUT THAT IS NOT THE ONLY EXIT FROM THIS ROOM.":GOTO1
204 M$="THERE IS AN ARCHWAY TO THE WEST, LEADING TO AN L-SHAPED ROOM. A DOOR LEADS IN THE OPPOSITE DIRECTION.":GOTO1
205 M$="THIS MUST BE THE CHATEAU'S KITCHEN BUT ANY FOOD LEFT HERE HAS LONG ROTTED AWAY. THERE ARE DOORS NORTH AND WEST.":GOTO1
207 M$="YOU ARE IN A TINY ROOM THAT MAKES YOU FEEL CLAUSTROPHOBIC. "
208 M$=M$+"A BLACK DRAGON IS PAINTED ON THE NORTH WALL, ABOVE THE DOOR.":GOTO1
209 M$="A STAIRWELL ENDS AT THIS LANDING. THERE IS AN OLD RUSTY IRON DOOR TO THE NORTH.":GOTO1
210 M$="THERE IS A STONE ARCHWAY TO THE NORTH OF THIS VERY LONG ROOM. "
211 M$=M$+"FRESH AIR BLOWS DOWN A STAIRWELL. RICH RED DRAPES COVER THE WALLS.":GOTO1
212 IFC>22THEN239
213 ONC-11GOSUB214,216,218,220,222,224,227,230,231,235,237:RETURN
214 M$="YOU HAVE ENTERED A ROOM FILLED WITH SWIRLING, CHOKING SMOKE. YOU MUST LEAVE QUICKLY TO REMAIN "
215 M$=M$+"HEALTHY ENOUGH TO CONTINUE YOUR QUEST.":GOTO1
216 M$="THERE IS A MIRROR IN THE CORNER YOU GLANCE AT IT, AND FEEL SUDDENLY VERY ILL. YOU REALISE THE LOOKING-GLASS HAS "
217 M$=M$+"BEEN ENFUSED WITH A SPELL OF CHARISMA REDUCTION. OH DEAR!":GOSUB1:F(2)=F(2)-1:RETURN
218 M$="THIS ROOM IS RICHLY FINISHED, WITH A WHITE MARBLE FLOOR. STRANGE FOOTPRINTS LEAD TO THE TWO DOORS "
219 M$=M$+"FROM THIS ROOM. DARE YOU FOLLOW THEM?":GOTO1
220 M$="YOU ARE IN A VERY LONG HALLWAY, LINED ON EACH SIDE WITH RICH, RED DRAPES. THEY ARE PARTED HALFWAY DOWN "
221 M$=M$+"THE EAST WALL, WHERE THERE IS A DOOR.":GOTO1
222 M$="SOMEONE HAS SPENT A LONG TIME PAINTING THIS ROOM A BRIGHT YELLOW. YOU REMEMBER READING THAT "
223 M$=M$+"YELLOW IS THE ANCIENT ORACLE'S COLOR OF WARNING!":GOTO1
224 IFA(17,5)=0THEN226
225 M$="AS YOU DESCEND THE LADDER IT BREAKS AND YOU FALL! NOW THERE IS NO WAY BACK!":GOSUB1:A(17,5)=0
226 M$="A SMALL DOOR LEADS EAST FROM THIS VERY CRAMPED ROOM.":GOTO1
227 M$="YOU FIND YOURSELF IN A HALL OF MIRRORS AND SEE YOURSELF REFLECTED HUNDREDS OF TIMES. "
228 M$=M$+"THROUGH THE DAZZLING GLARE YOU CAN MAKE OUT DOORS IN ALL DIRECTIONS.":GOSUB1
229 M$="YOU NOTICE THE MIRRORS AROUND THE EAST DOOR ARE HEAVILY TARNISHED.":GOTO1
230 M$="YOU FIND YOURSELF IN A LONG CORRIDOR. YOUR FOOTSTEPS ECHO AS YOU WALK.":GOTO1
231 M$="YOU FEEL AS YOU'VE BEEN WANDERING AROUND FOR EVER AND BEGIN TO DESPAIR OF EVER ESCAPING.":GOSUB1
232 M$="STILL, YOU CAN'T GET TOO DEPRESSED, BUT MUST STRUGGLE ON. LOOKING AROUND, YOU SEE A ROOM "
233 M$=M$+"WITH A TIMBERED CEILING AND WHITE ROUGHLY-FINISHED WALLS. THERE ARE TWO DOORS.":GOSUB1
234 GOSUB13:RETURN
235 M$="YOU ARE IN A SMALL ALCOVE. YOU CAN'T SEE IN THE GLOOM. PERHAPS IF YOU "
236 M$=M$+"WAIT A WHILE, YOUR EYES WILL ADJUST TO THE DARK.":GOTO6
237 M$="A DRIED-UP FOUNTAIN STANDS IN THE CENTER OF THIS COURTYARD. ONCE BEAUTIFUL FLOWERS NOW LIE DEAD.":GOSUB1:RETURN
239 IFC>33THEN261
240 ONC-22GOSUB241,242,243,245,246,249,250,252,254,256,260:RETURN
241 M$="THE SCENT OF DRIED FLOWERS FILLS THIS BRIGHT ROOM. THERE ARE TWO EXITS FROM IT.":GOTO1
242 M$="THIS IS A ROUND STONE CAVERN OFF THE SIDE OF THE ALCOVE TO YOUR NORTH.":GOTO1
243 M$="YOU ARE IN A LARGE CIRCULAR ROOM, WHICH SEEMS TO HAVE BEEN USED FOR GAMES. RUBBLE COVERS "
244 M$=M$+"THE FLOOR, PARTIALLY BLOCKING THE ONLY EXIT.":GOTO1
245 M$="THROUGH THE DIM MUSTINESS OF THIS SMALL POTTING SHED YOU CAN SEE A STAIRWELL.":GOTO1
246 M$="YOU BEGIN THIS ADVENTURE IN A WOOD OUTSIDE THE CHATEAU.":GOSUB1
247 PRINT:M$="WHILE WALKING, YOU COME ACROSS A RAMSHACKLE SHED. ENTERING IT, YOU SEE A HOLE "
248 M$=M$+"IN ONE CORNER. AN OLD LADDER LEADS DOWN...":GOTO1
249 M$="HOW WONDERFUL! FRESH AIR... BIRDS ARE SINGING... YOU ARE FREE AT LAST!":GOSUB1:GOTO148
250 M$="THE SMELL CAME FROM BODIES ROTTING IN HUGE TRAPS...":GOSUB6
251 M$="ONE SPRINGS SHUT ON YOU, TRAPPING YOU FOREVER.":QU=3.5:GOSUB1:GOTO149
252 PRINT"YOU FALL INTO A FLAMING PIT...":GOSUB13:IFRND(0)>.5THEN252
253 SC=10:QU=3.4:GOTO151
254 PRINT"AAAAHHH...YOU HAVE FALLEN INTO":GOSUB13
255 M$="A POOL OF ACID... NOW YOU KNOW WHY THE MIRRORS WERE SO BADLY TARNISHED!":GOSUB1:SC=20:QU=3:GOTO151
256 M$="TOO BAD YOU CHOSE THAT EXIT FROM THE ALCOVE...":GOSUB6
257 M$="A GIANT FUNNEL-WEB SPIDER LEAPS ON YOU! BEFORE YOU CAN REACT, IT BITES...":GOSUB1
258 PRINT"YOU HAVE 10 SECONDS TO LIVE..."
259 FORT=10TO1STEP-1:PRINTTAB(T)T:GOSUB13:PRINT:NEXTT:SC=3:QU=5:GOTO151
260 M$="A STAIRWELL LEADS INTO THIS ROOM, A POOR AND COMMON HOVEL WITH MANY DOORS AND EXITS.":GOTO1
261 ONC-33GOSUB262,263,264,266,270,272,273,275,277,278,279:RETURN
262 M$="IT IS HARD TO SEE IN THIS ROOM, AND YOU SLIP SLIGHTLY ON THE UNEVEN, ROCKY FLOOR.":GOTO1
263 M$="HORRORS! THIS ROOM WAS ONCE THE TORTURE CHAMBER. SKELETONS LIE ON THE FLOOR, STILL IN CHAINS.":GOTO1
264 M$="ANOTHER ROOM WITH VERY UNPLEASANT MEMORIES.":GOSUB6
265 M$="THIS FOUL HOLE WAS USED AS THE CHATEAU DUNGEON.":GOTO1
266 M$="OH NO, THIS IS A GARGOYLE'S LAIR!":GOSUB6
267 M$="IT HAS BEEN HELD PRISONER HERE FOR 300 YEARS.":GOSUB6
268 PRINT:PRINT"IN HIS FRENZY HE LASHES OUT...":GOSUB13:PRINT"AND...":GOSUB13:PRINT"... BREAKS YOUR NECK!"
269 SC=0:QU=3:GOTO151
270 M$="THIS WAS THE LOWER DANCING HALL WITH DOORS TO THE NORTH, EAST AND WEST. YOU WOULD SEEM TO BE "
271 M$=M$+"ABLE TO FLEE ANY DANGER.":GOTO6
272 M$="THIS IS A DINGY PIT AT THE FOOT OF SOME DUBIOUS-LOOKING STAIRS. A DOOR LEADS TO THE EAST.":GOTO1
273 M$="THE TROPHY ROOM OF THE CHATEAU. DOORS OPEN TO EACH DIRECTION. THE HEADS OF STRANGE CREATURES "
274 M$=M$+"SHOT BY THE OWNERS ARE MOUNTED HIGH UP ON EACH WALL.":GOTO1
275 PRINT"YOU HAVE FOUND A SECRET ROOM!":GOSUB13:M$="IN THIS ROOM THE ANCIENT NECROMANCERS OF THORIN PLIED THEIR "
276 M$=M$+"EVIL CRAFT. THE SCENT OF THEIR VILE POTIONS REMAINS.":GOTO1
277 M$="COBWEBS BRUSH YOUR FACE AS YOU MAKE YOUR WAY THROUGH THIS ROOM OF SHADOWS.":GOTO1
278 M$="THIS GLOOMY PASSAGE LIES AT THE INTERSECTION OF 3 ROOMS.":GOTO1
279 M$="YOU ARE IN THE REAR TURRET ROOM, BELOW THE WESTERN WALL OF THE CHATEAU.":GOTO1
280 PRINT:IFA(C,7)>98ANDA(C,9)=0ANDA(C,10)=0THENRETURN
281 M$="YOU CAN SEE: ":J=7:GOSUB9:J=9:GOSUB9:J=10:GOSUB9:GOTO6
282 Z=RND(-(PEEK(9)*256+PEEK(10))):N=0:CA=0:C=27:QU=1:MK=0:BX=0
284 FORX=1TO44:FORY=1TO10:READA(X,Y):NEXTY,X:FORZ=1TO20:READO$(Z),V(Z):NEXTZ:FORQ=1TO16
285 Z=INT(RND(0)*44)+1:IFZ=5ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN285
286 IFA(Z,7)THEN285
287 A(Z,7)=Q:NEXTQ:FORJ=1TO20:READM$(J):IFJ=1THEN291
288 Z=RND(44):IFZ=5ORZ=16ORZ=17ORZ=27ORZ=29ORZ=30ORZ=31ORZ=32ORZ=37THEN288
289 IFA(Z,8)THEN288
290 A(Z,8)=J
291 NEXTJ:FORJ=1TO6:READD$(J):NEXT
292 FORJ=1TO6:F(J)=RND(16)+2:NEXT
293 PRINT@256,;:GOSUB7:PRINT:PRINT"ROLL AGAIN (Y/N)?"
294 A$=INKEY$:ON-(A$="")GOTO294:ON-(A$="Y")GOTO292:RETURN
300 DATA1,1,2,1,1,1,,,,,,29,3,1,,,17,,,,,8,4,2,,,,,,,,9,5,3,,,,,,,5,5,5,5,5,5,,,,
301 DATA,11,7,30,,,,,,,,,8,6,,,,,,,3,,,7,,,99,,,,4,10,,,,,,,,,9,,,,,39,,,,
302 DATA6,,,,28,,,,,,,16,13,,,,,,,,,,14,12,,,19,,,,,18,,13,,,,,,,,21,16,,,,,,,
303 DATA12,20,19,15,,,,1,,,,,18,,27,,,,,,14,19,31,17,,,,,,,18,23,,16,,,,,,
304 DATA16,25,,,,,,,,,15,24,,32,,,,,,,,26,23,,,,,,,,19,,,22,,,,,,,21,,,,,,,,,
305 DATA20,25,25,25,25,25,,,,,22,,,,,33,,,,,,,,,,17,,,,,,,,,,11,,,,
306 DATA29,29,29,29,29,29,,,,,30,30,30,30,30,30,,,,,31,31,31,31,31,31,,,,,32,32,32,32,32,32,,,,
307 DATA43,42,40,,26,,,,,,,38,35,,,,100,,,,,43,36,34,,,,,,,,40,37,35,,,,,,
308 DATA37,37,37,37,37,37,,,,,34,,43,39,,,,,,,,,38,,10,,,,,,36,41,44,33,,,20,,,
309 DATA40,41,41,42,41,41,,,,,33,42,41,42,42,42,,,,,35,33,,38,,,,,,,,,,40,,,18,,,
396 DATA"AXE",,"SWORD",,"DAGGER",,"MACE",,"QUARTERSTAFF",,"MORNING STAR",,"FALCHION",
397 DATA"CRYSTAL RALL",99,"AMULET",247,"EBONY RING",166,"GEMS",462,"MYSTIC SCROLL",195,"HEALING POTION",231
398 DATA"DILITHIUM CRYSTALS",162,"COPPER PIECES",27,"DIADEM",141,"SILVER KEY",,"GOLDEN KEY",,"CHEST OF STONE",
399 DATA"CHEST MADE OF IRON",,"DWARF","MONOCEROS","PARADRUS","VAMPYRE","WRNACH","GIOLLA DACKER","KRAKEN","FENRIS WOLF"
400 DATA"CALOPUS","BASILISK","GRIMOIRE","FLYING BUFFALO","BERSERKOID","WYRM","CROWTHERWOOD","GYGAX","RAGNAROK"
401 DATA"FOMORINE","HAFGYGR","GRENDEL",N,S,E,W,U,D
