0 CLS:CLEAR350:GOSUB9600:DIMZZ,M$,A$:GOSUB8000:GOTO500
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:A$=MID$(M$,CC,1):IFA$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFA$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 GOSUB1
9 FORZZ=1TO2500:NEXT:RETURN
10 IFRO>18ANDRO<30THENFORF=1TO12:IFP(F)=(RO-1)THENP(F)=RO
11 IFRO>18ANDRO<30THENNEXTF
12 PRINT:PRINT"you can see:":C=0:FORF=1TO12:IFP(F)=ROTHENPRINTP$(F):C=1
20 NEXTF:IFC=0THENPRINT"NO ITEMS OF INTEREST"
100 FORF=1TO5:IFC(F)=ROTHEN140
120 NEXTF:GOTO150
140 PRINTC$(F)
150 IFE(1)=1ORE(1)=2THENM$="YOU NOTICE SOME MOVEMENT IN THE SHADOWS":GOSUB1
160 IFE(1)=3THENM$="A MEAN LOOKING HOTEL HENCHMAN ENTERS":GOSUB1:C(3)=RO
170 IFE(1)=4THENM$="THE HOTEL HENCHMAN CLUBS YOU FROM BEHIND":GOSUB1:GOTO7500
180 IFE(1)>0THENE(1)=E(1)+1
190 IFRO=6ANDE(1)=0THENE(1)=1
191 IFRO<>7THEN201
195 IFE(2)=1THENE(2)=3:M$="STEVE PULLS SOMETHING FROM HIS POCKET 'YOU MAY NEED THIS'":GOSUB8:P(6)=RO:GOTO500
198 IFRND(3)=1ANDE(2)>0THENPRINT"STEVE SAYS 'I'M NOT GORMLESS'"
200 IFE(2)=0THENGOSUB9900
201 ON-(C(5)<>RO)GOTO215:PRINT"DAVE SAYS ";:ONRND(10)GOTO203,204,205,206,207,208,209,210,211,212
203 PRINT"'HI THERE MATE!'":GOTO215
204 PRINT"'BEEBUDEEBUDEEBU!'":GOTO215
205 PRINT"'GIZ A JOB!'":GOTO215
206 PRINT"'BLEEP!BLOP!BLIP!'":GOTO215
207 PRINT"'WASAPENIN?'":GOTO215
208 PRINT"'WADUYAWANT MATE?'":GOTO215
209 PRINT"'BEWARE THE ALLEWELL!'";:GOTO215
210 PRINT"'LIZY WIZY LET'S GET  BUSY!'":GOTO215
211 PRINT"'WANA FIGHT'":GOTO215
212 PRINT"'HOWZ THINGS?'":GOTO215
215 IFRO=19ANDE(3)=0THEN9910
220 IFC(1)<>0ORC(3)<>0THENIFRO=19THENM$="BELOW YOU YOU SEE SOMEONE FIRING AT THE HOPPER. THE HOPPER EXPLODES!":GOSUB1:GOTO7500
222 IFRO=29THENE(4)=0
225 IFE(4)=1THENM$="THE MISSILES LOCK ON AND BLOW YOU TO SMITHEREENS":GOSUB1:GOTO7500
230 IFRO=28THENE(4)=1
240 IFRO=32ANDE(5)=0THENM$="BUT YOU ARE UNABLE TO KEEP YOUR GRIP AND SLIDE BACK DOWN":GOSUB8:RO=31:GOTO500
250 IFE(6)=0ANDC(4)=0ANDRO=38THEN9930
260 IFE(6)=1ANDC(4)=0ANDRO=38THENGOSUB9950
270 IFE(7)>0THENE(7)=E(7)+1:IFE(7)=4THENPRINT"EXPLOSIONS ROCK THE SHIP"
280 IFE(7)=5THENM$="THE SHIP EXPLODES WITH YOU ON IT":GOSUB1:GOTO7500
285 IFRND(6)=1ANDRO<19THENM$="A SMALL TREMOR SHAKES THE BUILDINGS AROUND YOU":GOSUB1
290 IFE(8)=1THENM$="THE HOTEL RECEPTIONIST SMASHES YOUR BRAINS OUT FROM BEHIND":GOSUB8:GOTO7500
300 GOTO520
500 GOSUB9150:PRINT:M$="YOU ARE "+R$(RO):GOSUB1:PRINT"visible exits:":IF R(RO,5)<>0THENPRINT"UP ";
512 IFR(RO,1)THENPRINT"NORTH ";
513 IFR(RO,2)THENPRINT"SOUTH ";
514 IFR(RO,3)THENPRINT"EAST ";
515 IFR(RO,4)THENPRINT"WEST ";
516 IFR(RO,6)THENPRINT"DOWN ";
517 GOSUB9:GOTO10
520 INPUT"what now";X$:II=1:Z$="":I=1:I$="":O$="":FORF=1TOLEN(X$):IFI=1ANDII<4THENI$=I$+MID$(X$,F,1):II=II+1
545 IFI=2ANDII<4THENO$=O$+MID$(X$,F,1):II=II+1
546 Z$=MID$(X$,F,1):IFZ$=" "THENI=I+1:II=1
550 NEXTF:IFI$="N"THENI$="NOR"
575 IFI$="I"THENI$="INV"
580 IFI$="S"THENI$="SOU"
590 IFI$="E"THENI$="EAS"
600 IFI$="W"THENI$="WES"
603 IFI$="R"THENI$="RED"
605 IFI$="U"THENI$="UP "
608 IFI$="D"THENI$="DOW"
609 IFLEN(I$)=2THENI$=I$+" "
610 IFLEN(O$)=2THENO$=O$+" "
615 FORF=1TO77STEP+3:IFMID$(V$,F,3)=I$THENCC=F:F=77:NEXTF:F=CC:GOTO632
630 NEXTF:GOTO640
632 IFO$=""THEN650
633 FORX=1TO97STEP3:IFMID$(N$,X,3)=O$THENCC=X:X=97:NEXTX:X=CC:GOTO650
635 NEXTX:IFO$="GRE"THENX=100:GOTO650
638 IFO$="DAV"THEN650
639 IFO$="SLO"THENX=103:GOTO650
640 PRINT"I DON'T UNDERSTAND":GOTO520

650 IFF=1ORF=4ORF=7ORF=10THENIFRND(3)=1THENXX=RND(4):IFR(C(5),XX)<>0THENC(5)=R(C(5),XX)
660 IFF=1THENIFR(RO,1)<>0THENRO=R(RO,1):GOTO500
661 IFF=25THENPRINT"YOU ARE CARRYING:":FORX=1TOGT:PRINT"THE "P$((G(X)+2)/3):NEXTX:IFGT=0THENPRINT"NOTHING"
662 IFF=19THEN750
663 IFF=28THEN950
664 IFF=31THEN500
665 IFF=34THEN1500
666 IFF=37THEN1600
667 IFF=40THEN1700
668 IFF=43THEN1800
669 IFF=46THEN1900
670 IFF=4THENIFR(RO,2)<>0THENRO=R(RO,2):GOTO500
671 IFF=49THEN2000
672 IFF=25ANDE(5)=1THEN2160
674 IFF=58THEN2500
675 IFF=22THEN850
676 IFF=4THENPRINT"YOU CAN'T GO THAT WAY":GOTO520
677 IFF=61THENPRINT"I CAN'T SEE ANYTHING TO OPEN":GOTO520
678 IFF=64THEN500
679 IFF=67THENPRINT"YOU MUST BE JOKING":GOTO520
680 IFF=7THENIFR(RO,3)THENRO=R(RO,3):GOTO500
681 IFF>67THENPRINT"WATCH THE LANGUAGE MATEY!":GOTO520
685 IFF=16THENIFR(RO,6)THENRO=R(RO,6):GOTO500
686 IFF=25THEN520
690 IFF=10THENIFR(RO,4)THENRO=R(RO,4):GOTO500
695 IFF=13THENIFR(RO,5)THENRO=R(RO,5):GOTO500
700 IFF=1ORF=4ORF=7ORF=10ORF=13ORF=16THENPRINT"YOU CAN'T GO THAT WAY":GOTO520
701 IFF=55THEN2100
749 GOTO500

750 FORF=1TO10:IFG(F)=XTHENPRINT"YOU ALREADY HAVE IT":GOTO520
760 NEXTF:IFX<19THENIFX<>1THENIFP((X+2)/3)<>ROTHEN9999
792 IFX>54ANDX<92THENIFP(((X-54)+2)/3)<>ROTHEN9999
795 IFX=1THENIFP(1)<>ROTHEN9999
800 IFX<19ORX>54THENIFGT=5THENPRINT"YOU'RE CARRYING TOO MUCH":GOTO520
810 IFX<19THENGT=GT+1:G(GT)=X:P((X+2)/3)=0:GOTO830
815 IFX>54ANDX<92THENGT=GT+1:G(GT)=X-54:P(((X-54)+2)/3)=0:GOTO830
820 PRINT"I CAN'T":GOTO520
830 PRINT"OK":GOTO520

850 FORF=1TO10:IFG(F)<18THENIFG(F)=XTHEN890
865 IFG(F)>18THENIFG(F)=X-54THEN890
870 NEXTF:PRINT"YOU DON'T HAVE IT":GOTO520
890 IFX<19THENG(F)=0:P((X+2)/3)=RO
895 IFX>19THENG(F)=0:P(((X-54)+2)/3)=RO
900 FORX=FTOGT:G(X)=G(X+1):NEXTX:GT=GT-1:PRINT"OK":GOTO520

950 W=0:WW=0:IFX=19AND(RO=8ORRO=4)THENPRINT"WHICH BUTTON?":GOTO520
970 IFX=22ANDRO=8ANDR(8,4)<>13THENGOSUB9998:GOSUB8:R(8,4)=R(8,4)+1:GOTO500
990 IFX=25ANDRO=8ANDR(8,4)<>9THENGOSUB9998:R(8,4)=R(8,4)-1:GOTO500
1000 IFX=25ANDRO=8ORX=22ANDRO=8THENPRINT"IT DOESN'T SEEM TO WORK":GOTO520
1001 IFX<>100THEN1010
1002 FORF=1TO10:IFG(F)=34THENF=10:NEXTF:GOTO1004
1003 NEXTF:PRINT"YOU DON'T HAVE THE BOX":GOTO520
1004 IFRO<>C(1)THENIFC(1)THENPRINT"THE HOTEL RECEPTIONIST ARRIVES":E(8)=1:C(1)=RO:GOTO520
1005 IFC(1)THENM$="THE RECEPTIONIST TURNS TO FACE YOU":GOSUB1:E(8)=1:GOTO520
1010 IFX=34ANDRO=4THENM$="THE ENGINES START AND THE HOPPER RISES INTO THE AIR":GOSUB8:FORF=1TO12:IFP(F)=4THENP(F)=19
1015 IFX=34ANDRO=4THENNEXTF:RO=19:GOTO500
1020 IFX=37ANDRO<>4ANDRO>18ANDRO<29THEN9996
1033 IFX=100THENPRINT"YOU DON'T HAVE THE BOX":GOTO520
1040 IFX=40ANDRO=28THENM$="THE ENTIRE COCKPIT RIPS OFF OF THE BODY OF THE HOPPER":GOSUB8:RO=29:GOTO500

1050 IFX<>43THEN1080
1051 FORF=1TO10:IFG(F)=22THENWW=1
1052 IFG(F)=7THENW=1
1054 NEXTF
1057 IFW=0THENPRINT"YOU DON'T HAVE THE COMLINK":GOTO520
1060 IFWW=0THENM$="A VOICE COMES FROM THE COMLINK, 'GARGLE GLOOP GLEEP GLOP ZING'":GOSUB1:GOTO520
1070 M$="A VOICE COMES FROM THE COMLINK. THE TRANSLATOR TRANSLATES IT: 'CALLING AGENT 29326'":GOSUB1:GOTO520

1080 IFX<>97THEN1400
1081 IFRO=40ANDE(7)=0THENPRINT"NOTHING HAPPENS":GOTO520
1090 IFRO=40THEN9940
1400 PRINT"YOU CAN'T DO THAT HERE":GOTO520

1500 FORF=1TO10:IFG(F)=4THEN1530
1522 NEXTF:PRINT"YOU DON'T HAVE THE ROPE":GOTO520
1530 IFX=4ANDRO=13THENM$="YOU TIE THE ROPE TO ONE OF THE POSTS":GOSUB8:R(13,6)=16
1540 FORX=FTOGT:G(X)=G(X+1):NEXTX:GT=GT-1:GOTO500

1600 FORF=1TOGT:IFG(F)=13THEN1650
1630 NEXTF:PRINT"YOU DON'T HAVE THE GLASS CUTTER":GOTO520
1650 IFRO=16ORRO=17THENM$="USING THE GLASS CUTTER YOU CAREFULLY CUT AWAY PART OF THE WINDOW":GOSUB8:R(RO,3)=RO-2:GOTO500
1665 IFRO=18THENGOSUB9970:GOTO3000
1670 GOTO1400

1700 IFX=31ANDRO=10THENM$="THE DOOR TO THE APARTMENT OPENS":GOSUB8:R(10,4)=7:GOTO500
1720 PRINT"IT HAS NO EFFECT":GOTO520

1800 IFX=91ANDRO=37THENGOSUB9997:GOTO3000
1806 IFX=91ANDRO=41ANDC(4)=41THEN9980
1807 IFX=91ANDRO=41THENGOSUB9997:GOTO4000
1810 FORF=1TOGT:IFG(F)<18THENIFG(F)=XTHEN1850
1825 IFG(F)>18THENIFG(F)=X-54THEN1850
1830 NEXTF:PRINT"YOU DON'T HAVE IT":GOTO520
1850 IFG(F)>18THENCC=INT(((X-54)+2)/3)
1855 IFG(F)<18THENCC=INT((X+2)/3)
1860 M$=D$(CC):GOSUB1:GOTO520

1900 FORF=1TO10:IFG(F)=10THEN1910
1907 NEXTF:PRINT"YOU HAVEN'T GOT THE GUN":GOTO520
1910 IFX=94ANDC(4)=ROORX=46ANDC(1)=ROORX=49ANDC(2)=ROORC(3)=ROANDX=52THEN1915
1912 IFX=46ORX=49ORX=52THENPRINT"HE IS NOT HERE":GOTO520
1915 IFX=46ORX=49ORX=52THENM$="PULLING OUT YOUR GUN YOU SHOOT...":GOSUB8
1920 IFC(1)=ROTHENIFX=46THEN9960
1930 IFC(2)=ROTHENIFX=49THENM$="STEVE FALLS DEAD TO THE GROUND":GOSUB8:P(10)=RO:C(2)=0:GOTO500
1940 IFC(3)=ROTHENIFX=52THEN9990
1945 IFX=94ANDRO=41THEN9993
1950 PRINT"YOU CAN'T SHOOT THAT":GOTO520

2000 IFX<>16THENPRINT"YOU CAN'T READ THAT":GOTO520
2020 FORF=1TO10:IFG(F)=16THENF=10:NEXTF:GOTO2060
2040 NEXTF:PRINT"YOU DON'T HAVE THE MANUAL":GOTO520
2060 PRINT"...VERY INTERESTING":E(3)=1:GOTO520

2100 IFE(5)=1THENPRINT"YOU'RE ALREADY WEARING THEM":GOTO520
2110 IF(X-54)<>19THENPRINT"YOU CAN'T WEAR THAT":GOTO520
2120 FORF=1TO10:IFG(F)=19THEN2150
2140 NEXTF:PRINT"YOU HAVEN'T GOT THE CRAMPONS":GOTO520
2150 FORX=FTOGT:G(X)=G(X+1):NEXTX:GT=GT-1:E(5)=1
2160 M$="YOU'RE WEARING THE MAGNETIC CRAMPONS":GOSUB1:GOTO520

2500 IFX<>1THENM$="YOU CAN'T INSERT THAT INTO ANYTHING":GOSUB1:GOTO520
2501 IFRO<>36THEN1400
2520 FORF=1TO10:IFG(F)=1THENF=10:NEXTF:GOTO2560
2540 NEXTF:PRINT"YOU DON'T HAVE THE CARD":GOTO520
2560 M$="YOU INSERT THE SECURITY CARD INTO THE SLOT. THE DOOR TO THE NORTH OPENS":GOSUB8:R(36,1)=37:GOTO500

3000 CLS:PRINT"please enter your code number:"
3002 INPUTQ$:IFQ$<>"29326"THENCLS:M$="YOU HAVE BEEN DISCONNECTED":GOSUB8:GOTO3160
3090 CLS:PRINT"1. LOG":PRINT"2. SAFETY PRECAUTIONS":PRINT"3. EMERGENCY DRILL":PRINT"4. IN THE EVENT OF FAILURE"
3094 PRINT"5. EXIT THIS DATABASE":PRINT"6. REDISPLAY"
3100 M$=INKEY$:IFM$="1"THENGOSUB3200
3110 IFM$="2"THENGOSUB3300
3120 IFM$="3"THENGOSUB3400
3130 IFM$="4"THENGOSUB3500
3140 IFM$="5"THEN3160
3141 IFM$="6"THEN3090
3150 GOTO3100
3160 CLS:PRINT"THE SCREEN FADES":GOTO520
3200 CLS:PRINT"private log STARDATE 20/54-21"
3201 M$="BY TOMORROW OUR DRILLING SHOULD BE DEEP ENOUGH THAT WE CAN SUCK THIS PLANET DRY":GOSUB8
3202 RETURN
3300 CLS:PRINT"safety precautions"
3301 M$="AS YOU KNOW OUR EYES CANNOT STAND BRIGHT LIGHTS":GOSUB8
3302 M$="IN CASE OF MUTINY, POWER TO THE SHIP'S LIGHTING CAN BE INCREASED TO AN UNBEARABLE LEVEL":GOSUB8
3303 M$="USE THE CODE 242 ON THE CONTROL ROOM COMPUTER":GOSUB8:RETURN
3400 CLS:PRINT"emergency drill"
3401 M$="IN THE EVENT OF AN EMERGENCY THE ESCAPE PODS MUST BE USED":GOSUB8
3402 M$="THEY WILL ONLY OPERATE IN THE EVENT OF AN EMERGENCY":GOSUB8:RETURN
3500 CLS:PRINT"in the event of failure"
3501 M$="IN THE EVENT OF FAILURE THE PERSONS INVOLVED CAN NEVER RETURN":GOSUB8
3502 M$="SO THEIR ONLY OPTION IS SELF-DESTRUCTION":GOSUB8
3503 M$="TO DO THIS THE CODE 691 MUST BE USED ON THE CONTROL ROOM COMPUTER":GOSUB8:RETURN

4000 CLS:PRINT "  please enter a control code"
4001 INPUT"          ";Q$
4002 IFQ$="242"THENCLS:PRINT"CONTROL CODE OK        LIGHTS OK":GOSUB9:CLS:E(6)=1:GOTO4000
4060 IFQ$="691"THENCLS:PRINT"CONTROL CODE OK SELF DESTRUCT OK":GOSUB9:CLS:E(7)=1:GOTO4000
4070 CLS:PRINT"CONTROL CODE INVALID":GOSUB9:CLS:PRINT"THE SCREEN FADES":GOTO520

7500 PRINT"YOU ARE DEAD"
7520 PRINT:PRINT"DO YOU WANT ANOTHER GO?"
7530 M$=INKEY$:IFM$="Y"THENRUN
7540 IFM$="N"THENEND 
7550 GOTO7530

8000 DIMM(12),E(8),C$(5),C(5),G(10),P(13),R(41,6),P$(13),R$(51),D$(12)
8006 FORF=1TO41:FORX=1TO6:READR(F,X):NEXTX:NEXTF:XX=0:RO=1:GT=0
8007 V$="NORSOUEASWESUP DOWGETDROINVPREREDTIECUTSAYEXASHOREAQUIWEAINSOPELOOHELFUCSHICRA"
8008 N$="CARROPCOMPISCUTMANBUTUP DOWGLAJASTAKLANEJEBLARECSTEHENSECHIGRADLAZCUTINSCRATRARECSTEHENBOXTERALIRED"
8009 FORF=1TO13:READP$(F):NEXTF:FORF=1TO13:READP(F):NEXTF:FORF=1TO5:READC(F):NEXTF:FORF=1TO5:READC$(F):NEXTF
8010 FORF=1TO12:READA:M(F)=A:NEXTF:FORF=1TO41:READR$(F):NEXTF:FORF=1TO12:READD$(F):NEXTF:RETURN

8020 DATA0,2,,,,,1,,5,3,,,,,2,4,,,,,3,4,,,,6,9,3,,,5,,,,,,,,10,,,0
8021 DATA0,,,9,,,,,8,5,,,,,8,,,,,,8,,,,,,8,,,,,,8,,,,,,12,16,0
8022 DATA0,,,11,15,,,,,,,13,17,,,,,16,18,,,,,17,,20,,,,,,21,19,,,,0
8023 DATA22,20,,,,,,21,,,,23,,,,,22,24,,,,,23,25,,,26,27,24,,,,,25,,28
8024 DATA0,,25,,,28,,,25,25,25,0
8025 DATA0,,30,,,,,,31,29,,,,,,30,32,,,,,,33,31,,,,,34,32,,,,,35,33,0
8026 DATA0,,36,,34,,,35,38,,,,36,,,,,,39,36,41,,,38,40,,,,,39,,,,,,,0
8027 DATA38,,,0

9003 DATA"ALIEN SECURITY CARD","HIGH TENSILE ROPE","TWO WAY COMLINK","LAZER PISTOL","LAZER GLASS CUTTER","INSTRUCTION MANUAL"
9004 DATA"MAGNETIC CRAMPONS","TRANSLATOR","DEAD RECEPTIONIST","DEAD BODY OF STEVE","DEAD HENCHMAN","SMALL BOX","DEAD ALIEN"
9005 DATA0,4,15,1,6,,14,,,,,,,6,7,,41,2
9007 DATA"THE HOTEL RECEPTIONIST","STEVE ADAMS","THE HOTEL HENCHMAN","THE DISGUSTING ALIEN","DAVE THE DROID"
9008 DATA49185,,53000,55000,60000,,,,,,,56000

9011 DATA"ON THE BRIDGE OF YOUR SHIP. THERE IS AN AIRLOCK TO THE SOUTH"
9012 DATA"ON A LANDING PAD ON THE PLANET BETHSELAMINE. TO THE WEST IS A SMALLER PAD. STEPS LEAD DOWN TO THE EAST"
9013 DATA"ON A SMALL LANDING PAD. A PLANET HOPPER IS HERE WITH ITS HATCH OPEN. TO THE EAST IS A LARGER PAD"
9014 DATA"SITTING INSIDE THE PLANET HOPPER. THERE ARE THREE BUTTONS MARKED take off, land AND eject. AN AIRLOCK IS EAST"
9015 DATA"AT THE FESTIVAL SITE. THERE ARE NO TOURISTS, JUST LOCALS. SOUTH IS THE HOTEL. EAST IS AN APARTMENT BUILDING"
9016 DATA"IN THE HOTEL. TO THE NORTH IS THE FESTIVAL SQUARE","IN STEVE'S APARTMENT"
9018 DATA"INSIDE A LIFT. YOU CAN SEE BUTTONS MARKED up AND down"
9019 DATA"IN THE APARTMENT BUILDING. TO THE EAST ARE SOME SLIDING DOORS"
9020 DATA"ON FIRST FLOOR. YOU CAN SEE A DOOR WITH SPEECH GRILL SET IN THE WALL BY IT. BEHIND YOU IS THE LIFT"
9021 DATA"ON SECOND FLOOR. YOU CAN SEE A DOOR WITH A SPEECH GRILL SET IN THE WALL BY IT. BEHIND YOU IS THE LIFT"
9022 DATA"ON THIRD FLOOR. YOU CAN SEE A DOOR WITH A SPEECH GRILL SET IN THE WALL BY IT. BEHIND IS THE LIFT"
9023 DATA"ON THE ROOF. IT IS FLAT APART FROM A SERIES OF POSTS ALONG THE EDGE"
9024 DATA"IN A DESERTED APARTMENT","IN THE APARTMENT OF THE HOTEL RECEPTIONIST"
9026 DATA"HANGING ON A ROPE OUTSIDE THE WINDOW OF A THIRD FLOOR APARTMENT"
9027 DATA"HANGING ON A ROPE OUTSIDE THE WINDOW OF A SECOND FLOOR APARTMENT"
9028 DATA"HANGING ON THE END OF A ROPE OUTSIDE STEVE'S APARTMENT ON THE FIRST FLOOR"
9029 DATA"HIGH ABOVE THE LANDING PAD IN THE PLANET HOPPER"
9030 DATA"FLYING OVER ROCKY LAND, AHEAD IS A MOUNTAIN RANGE","FLYING AMONG THE MOUNTAINS"
9032 DATA"OVER A HUGE CRATER. IT MUST BE AT LEAST A MILE WIDE"
9033 DATA"DECENDING INTO A VERTICAL TUNNEL AT THE BOTTOM OF THE CRATER"
9034 DATA"AT THE TOP OF AN IMMENSE CAVERN. BELOW YOU SEE A SILVER BALL ON 4 SQUAT LEGS"
9035 DATA"ABOVE THE SILVER BALL. IT IS A CRAFT OF SOME SORT"
9036 DATA"AT THE EAST SIDE OF THE CAVERN","AT THE WEST SIDE OF THE CAVERN"
9038 DATA"ON A COLLISION COURSE WITH THE CRAFT. TWO WHITE OBJECTS LAUNCH FROM IT AND STREAK TOWARDS YOU"
9039 DATA"IN THE EJECTED COCKPIT OF THE HOPPER. NEARBY YOU SEE ONE OF THE LEGS OF THE ALIEN CRAFT"
9040 DATA"BY ONE OF THE CRAFT'S LEGS. YOU CAN SEE A SMALL HATCH LEADING INWARDS"
9041 DATA"INSIDE THE ALIEN CRAFT'S LEG. THERE IS A SMOOTH POLE LEADING UPWARDS","CLIMBING A POLE"
9043 DATA"CLIMBING A POLE. YOU ARE NEARING THE TOP","AT THE TOP OF THE POLE. ABOVE YOU IS A HATCH"
9045 DATA"INSIDE THE ALIEN CRAFT, THE INTERIOR IS ENTIRELY BLACK. THE ONLY LIGHT COMES FROM THE CEILING"
9046 DATA"IN A LONG EAST/WEST CORRIDOR. NORTH IS A DOOR. ALONG SIDE IT IS A SLOT"
9047 DATA"IN A SMALL COMPUTER ROOM. THERE IS A COMPUTER TERMINAL"
9048 DATA"AT THE END OF THE CORRIDOR. WEST IS A RED DOOR. SOUTH IS A PASSAGE"
9049 DATA"IN A PASSAGE. THERE IS A HATCH TO THE SOUTH"
9050 DATA"INSIDE A WELL PADDED ESCAPE POD. THE ONLY CONTROLS ARE A RED BUTTON"
9051 DATA"IN THE CONTROL ROOM WITH A COMPUTER TERMINAL ON THE WALL"

9101 DATA"SMALL AND PLASTIC. IT IS COVERED WITH ALIEN SYMBOLS. AT ONE END THERE'S AN ARROW DRAWN ON IT"
9102 DATA"IT'S NOT VERY THICK BUT INCREDIBLY STRONG. IT COULD EASILY HOLD YOUR WEIGHT"
9103 DATA"IT'S A SMALL TWO-WAY TRANSEVER WITH A SMALL BLACK BUTTON. IT IS COVERED IN ALIEN SYMBOLS"
9104 DATA"VERY POWERFUL","USED FOR CUTTING GLASS"
9106 DATA"IT LOOKS LIKE AN INSTRUCTION MANUAL FOR A PLANET HOPPER"
9107 DATA"FOR CLIMBING UP METALIC SURFACES","FOR TRANSLATING ALIEN LANGUAGES INTO YOUR OWN"
9109 DATA"LOOKS NASTY","LOOKS EXTREMELY NASTY","LOOKS VERY NASTY"
9112 DATA"IT LOOKS LIKE A SHORT RANGE TRANSMITTER. IT BLEEPS. IT IS COVERED IN ALIEN SYMBOLS AND HAS A GREEN BUTTON"

9150 CLS:IFRO=4THEN9400
9151 IFRO<16ORRO>29THEN9153
9152 IFRO>15ANDRO<19THEN9300
9153 IFRO>18ANDRO<30THEN9400
9154 IFRO=37ORRO=41THEN9500
9200 PRINT@47,"Ž„";:PRINT@79,"Ž†":RETURN
9300 PRINT@47,"Ž„Ê";:PRINT@79,"Ž†Ê":RETURN
9400 PRINT@46,"Žáâ";:PRINT@78,"‚‚":RETURN
9500 PRINT@46,"ÁÃÃÂ";:PRINT@78,"ÄÌÌÈ";:PRINT@110,"ŠÙÙÙ…";:RETURN

9600 M$="YOU ARE JASON JETBLASTER, REPORTER AND CLOSE FRIEND OF STEVE ADAMS--":GOSUB1
9602 M$="THE MAN IN CHARGE OF MINING ON THE PLANET BETHSELAMINE.":GOSUB1
9603 M$="BETHSELAMINE IS A PLANET WITH AN UNUSUAL ORBIT THAT MEANS IT IS IN DARKNESS FOR TWENTY YEARS.":GOSUB1
9604 M$="WHEN THE SUN DOES RISE THE SIGHT BRINGS TOURISTS FROM ACROSS THE GALAXY TO ATTEND A FESTIVAL OF THE SUNRISE."
9605 GOSUB1:GOSUB9650
9610 M$="YOU ARRIVE WHEN THE FESTIVAL SHOULD BE AT ITS PEAK BUT THE PLACE IS DESERTED.":GOSUB1
9611 M$="ALL YOU KNOW IS THAT YOUR FRIEND STEVE IS STAYING ON THE FIRST FLOOR OF THE HOTEL.":GOSUB1
9612 M$="YOU MUST SAVE THE PLANET FROM DESTRUCTION.":GOSUB1:GOSUB9650:RETURN
9650 PRINT:PRINT"hit any key to continue";:FORX=0TO1STEP0:CC=RND(0):X=-(INKEY$<>""):NEXT:CLS:RETURN

9900 E(2)=1:M$="AS YOU ENTER STEVE JUMPS UP, 'JASON, YOU MADE IT":GOSUB8
9901 M$="TERRIBLE THINGS HAVE BEEN HAPPENING. A FEW DAYS AGO A LARGE SPACECRAFT CRASHED BEHIND THE HILLS":GOSUB8
9902 M$="THERE WAS A LARGE EARTH TREMOR, WHICH SCARED ALL THE TOURISTS AWAY.":GOSUB8
9903 M$="SINCE THEN, THERE HAVE BEEN MORE TREMORS":GOSUB8
9904 M$="THE OBJECT IS TUNNELING INTO THE PLANET'S CORE. YOU HAVE TO STOP IT!'":GOSUB8:RETURN

9910 M$="UNFORTUNATELY YOU DON'T HAVE THE SLIGHTEST IDEA HOW TO CONTROL IT":GOSUB8
9911 M$="THE HOPPER PLUMITS TO THE GROUND AND EXPLODES":GOSUB8:GOTO7500

9930 GOSUB9955:M$="YOU RUN OUT OF THE ROOM AND COME FACE TO FACE WITH A BAND OF ALIENS CARRYING RIFLES":GOSUB8
9931 M$="YOU DON'T STAND A CHANCE...":GOSUB8:GOTO7500

9940 M$="THE ESCAPE POD ROCKETS CLEAR OF THE ALIEN SHIP":GOSUB8
9941 M$="THE SHIP EXPLODES IN A BALL OF SUPER HOT GASES TAKING ALL OF THE ALIENS WITH IT":GOSUB8
9942 M$="WELL DONE! YOU HAVE COMPLETED THE ADVENTURE!":GOSUB8:GOTO7520

9950 GOSUB9955:M$="YOU RUN OUT THE DOOR AND SEE SEVERAL ALIENS WRITHING ON THE FLOOR IN AGONY FROM THE LIGHTS":GOSUB8
9951 M$="THEY WERE OBVIOUSLY PLANNING TO AMBUSH YOU":GOSUB8:RETURN
9955 FORCC=1TO3:FORZZ=1TO16:SOUNDZZ,1:NEXTZZ,CC:RETURN

9960 E(8)=0:M$="THE HOTEL RECEPTIONIST FALLS TO THE FLOOR.":GOSUB8
9961 M$="SOME THINGS FALL OUT OF HIS POCKETS":GOSUB8:C(1)=0:P(1)=RO:P(8)=RO:P(9)=RO:GOTO500

9970 M$="AS YOU ATTEMPT TO CUT THE GLASS. A FRIGHTENED STEVE THROWS SOMETHING AT YOU":GOSUB8
9971 M$="YOU LOSE YOUR GRIP ON THE ROPE AND CRASH TO THE GROUND":GOSUB8:GOTO7500

9980 M$="AS YOU TURN YOUR BACK ON THE ALIEN IT SENDS A JET OF VENOM TOWARDS YOU!":GOSUB8
9981 M$="THE EFFECTS ARE INSTANT":GOSUB8:GOTO7500

9990 M$="THE HOTEL HENCHMAN FALLS TO THE GROUND DEAD":GOSUB8
9991 M$="A SMALL OBJECT FALLS FROM HIS POCKET":GOSUB8:P(12)=RO:C(3)=0:E(1)=5:P(11)=RO:GOTO500

9993 M$="THE ALIEN SCREAMS AND FLOPS TO THE DECK, SLIME OOZING FROM EVERY PORE OF ITS BODY":GOSUB8
9994 C(4)=0:P(13)=RO:GOTO520

9996 M$="YOU HEAR A SYNTHESIZED VOICE 'I COMPUTE THAT IT IS NOT SAFE TO LAND HERE'":GOSUB1:GOTO520

9997 M$="AS YOU SIT DOWN AT THE TERMINAL THE SCREEN LIGHTS...":GOSUB8:RETURN
9998 M$="THE DOORS CLOSE, YOU SENSE MOTION, THEY OPEN AGAIN":RETURN
9999 PRINT"YOU CAN'T SEE THAT HERE":GOTO520

10000 REM MC-10 EDITS JIM GERRIE
