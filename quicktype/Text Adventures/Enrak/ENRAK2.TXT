0 CLS:CLEAR120:DIMN(21)
1 CC=1:FORC1=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):CC=CC+1
2 NEXT:GOTO10
4 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),ASC(MID$(M$,CC+1,1))-48:NEXT:RETURN
10 LT=500:DIMA$(50),RM(50,4),IT$(53),IT(35),JT$(35):O=35
11 PRINT@230,"ENRAK":PRINT@262,"BY KEN OSTRER":PRINT@294,"MIKE SMITH":PRINT@326,"(C) MARCH 1983":PRINT
12 DATANORTH,SOUTH,EAST,WEST
20 RT$(3)="THE DUKE OF 'BONE'              BORN 786 - DIED 835":RT$(4)="IQVQ QRGP EQHHKP CPF UOGNN VJG  TQUG"
21 RT$(5)="SILENCE IS NICE."
30 DATA"NORTH OF A FOREST, THERE IS A   CAVE TO THE EAST"
40 DATAIN A CAVE,ON A WINDING MOUNTAIN PASS,ON A FOREST TRAIL,AT A THREE-WAY INTERSECTION OF  PASSAGES
41 DATAAT A TURN IN THE PASSAGE
42 DATAIN A LARGE CAVERN,IN A SMALL CAVERN,AT A BLOCKED PASSAGE,AT THE BRINK OF A DEEP PIT,ON A FOREST TRAIL
50 DATAIN A SMALL CLEARING,IN THE HEART OF A DARK FOREST,AT A RIVER'S ESTUARY,IN A FOREST,ON A FOREST PATH,ON A FOREST PATH
51 DATAON A JAGGED ROAD,ON A DARK ROAD,NORTH OF A MINING TOWN,IN A MINING TOWN
60 DATAAT A TOWN MARKETPLACE,IN A SALOON,AT A VERY QUIET GRAVEYARD,AT AN ENTRANCE WAY
70 DATAIN A VERY LONG CORRIDOR,IN THE MAIN LIBRARY,IN A VERY LONG CORRIDOR,IN A GIANT LABORATORY,IN A TORTURE ROOM
71 DATAIN A TIME ROOM.,IN A DINING ROOM,IN A SOUND PROOF ROOM,IN THE DUNGEON,IN AN ANIMAL CAGE.
80 DATAIN A BEDROOM,IN THE MAIN KITCHEN,IN A CIRCULAR ROOM,IN A STOREROOM,IN A ROOM THAT RESEMBLES A      MORTUARY
81 DATAAT A VERY LONG AND NARROW FOYER,C
90 DATAOUTSIDE OF A STOREROOM,IN A TRIANGULAR-SHAPED ROOM.,B,AT A LEDGE. THE MOON IS FULL    TONIGHT
91 DATAAT A VAMPIRE BAT CLUB HOUSE,IN FRONT OF A GLOWING FIREPLACE
100 DATA"IN A MUSTY, DARK ROOM.",IN LUTARN'S DEN. HINT+ LUTARN   EXISTS IN A GASEOUS FORM.
110 DATA0,4,2,3,,5,,1,1,11,15,,1,13,,,2,10,6,,,7,,5,6,9,8,,,9,,7,8,,,7,5,,,,3,19,12,0
120 DATA0,14,,11,4,14,,,13,,16,12,,,,3,,17,,14,16,,,18,,,17,20,11,20,,,19,21,18,,20,23,22,,,24,,21
130 DATA21,,24,,22,,,23,26,,,24,,25,27,29,,,,28,,30,26,25,,,26,,28,,31,40,39,32,33,30,31,,,,38,34,35,31
140 DATA33,,,,37,36,,33,35,,,,,35,,,,33,,,,31,,,,41,30,42,40,46,48,,,43,40,,42,45,,44,,47,43,0
150 DATA43,,46,,41,,,45,44,50,,,,49,,41,48,,,,47,,,0
160 D$="NSEW":R=1:FORX=1TO4:READD$(X):NEXTX:FORX=1TO50:READA$(X):NEXTX:FORX=1TO50:FORY=1TO4:READRM(X,Y):NEXTY,X
161 FORX=1TO35:READ IT$(X),JT$(X),IT(X):NEXTX:PRINT"DO YOU WANT TO LOAD AN OLD GAME FROM TAPE?"
170 A$=INKEY$:IFA$="Y"THEN1800
171 IFA$<>"N"THEN170
180 A$(45)="NEAR A CLOSED COFFIN"
190 A$(42)="IN FRONT OF A LOCKED DOOR."
200 IFR>=26ANDAA=0THENCLSRND(8):PRINT"A COLORFUL DARK MIST COVERS THE ROOM.":GOTO340
210 IFFG=-1THENIT$(29)="AN EMPTY BEAKER"
220 CLS:PRINT"YOU ARE:":PRINTA$(R)
230 PRINT:PRINT"OBVIOUS DIRECTIONS ARE:"
240 FORY=1TO4:IFRM(R,Y)<>0THENPRINTD$(Y);"-> ";
241 NEXTY
250 PRINT:I=0:PRINT:PRINT"YOU SEE: ":IFR=2THENPRINT"OLD HERMIT":I=1
260 IFR=21ANDZZ<>-1THENPRINT"GUNFIGHTER WITH THE MAGIC GLOVE":I=1
270 FORX=1TO35
280 IFIT(X)=RTHENPRINTIT$(X):I=1
290 NEXT
300 IFI=0THENPRINT"NOTHING"
310 IFYD=-1THENPRINT:PRINT"THE POISON OF LUTARN WAS FATAL.":GOTO1730
320 IFZZ<>-1ANDR=21THENGOSUB1860
330 X=RND(35):IFRND(40)=1ANDIT(X)=-1ANDX<>19THEN341
340 PRINT:INPUTI$:IFAA=-1THEN345
341 GOTO350
345 LT=LT-1:IFLT=0THENPRINT"YOUR LIGHT HAS RAN OUT.":GOTO1730
346 M$="C2C2C2D2D2D2E2E2E2F2F2F2":GOSUB4:PRINT:PRINT"A GIANT VULTURE HAS CARRIED OFF YOUR..??":IT(X)=RND(24):J=J-1
350 IFLEFT$(I$,5)="MOVE "THEN1340
351 IFLEFT$(I$,4)="HELP"THEN1880
352 IFLEFT$(I$,4)="QUIT"THENEND
353 IFLEFT$(I$,4)="SING"THEN1920
354 IFLEFT$(I$,6)="SHOOT "THEN1820
355 IFLEFT$(I$,6)="CRACK "THEN1750
356 IFLEFT$(I$,5)="WEAR "THEN1620
360 IFLEFT$(I$,6)="TOUCH "THEN1650
361 IFLEFT$(I$,4)="DIP "THEN1480
362 IFLEFT$(I$,4)="ASK "THEN1230
363 IFLEFT$(I$,4)="SAVE"THEN1780
364 IFLEFT$(I$,5)="CALL "THEN1690
365 IFLEFT$(I$,5)="GIVE "THEN1310
366 IFLEFT$(I$,5)="LOAD "THEN1940
370 IFLEFT$(I$,5)="SHOW "THEN1080
371 IFLEFT$(I$,5)="POUR "THEN1580
372 IFLEFT$(I$,7)="UNLOCK "THEN1370
373 IFLEFT$(I$,5)="OPEN "THEN1180
374 IFLEFT$(I$,6)="THROW "THEN1270
375 IFLEFT$(I$,3)="DIG"THEN1110
380 IFLEFT$(I$,5)="FEED "THEN1520
381 IFLEFT$(I$,5)="SCORE"THEN1060
382 IFLEFT$(I$,7)="INSERT "THEN1550
383 IFLEFT$(I$,6)="SMELL "THEN1150
384 IFLEFT$(I$,6)="LIGHT "THEN950
390 IFLEFT$(I$,5)="DROP "THEN750
391 IFLEFT$(I$,1)="L"THEN200
392 IFLEFT$(I$,1)="I"THEN650
393 IFLEFT$(I$,4)="GET "THEN480
394 IFLEFT$(I$,4)="SAY "THEN1020
395 IFLEFT$(I$,4)="BUY "THEN910
396 IFLEFT$(I$,5)="READ "THEN860
400 IFRM(25,3)=0ANDI$="N"ANDR=25THENPRINT"THE COUNT IS IN YOUR WAY.":GOTO340
410 FORX=1TO4
420 IFMID$(D$,X,1)=I$THEN450
430 NEXTX
440 PRINT"W-H-A-T ? ? ?":GOTO340
450 IFRM(R,X)=0 THEN PRINT"YOU CAN'T GO THAT WAY":GOTO340
460 R=RM(R,X)
470 GOTO200
480 G$=RIGHT$(I$,LEN(I$)-4):G$=LEFT$(G$,3)
490 IFR=49ANDG$=LEFT$(JT$(25),3)ORR=49ANDG$=LEFT$(JT$(26),3)THENPRINT"IT CANNOT BE TAKEN OFF OF THE   WALL.":GOTO340
500 IFG$=LEFT$(JT$(19),3)ANDR=22ANDBU<>1THENPRINT"THE LANTERN COSTS MONEY.":GOTO340
510 IFG$=LEFT$(JT$(31),3)ANDR=50ANDIT(31)=50ORG$=LEFT$(JT$(22),3)ANDR=20THENPRINT"THAT IS NOT WITHIN YOUR POWER.":GOTO340
520 IFUP<>-1ANDG$=LEFT$(JT$(8),3)ANDR=10THENPRINT"AN INVISIBLE FORCE IS GUARDING  THE ROSE.":GOTO340
530 IFNF<>-1ANDG$=LEFT$(JT$(6),3)ANDR=48THENPRINT"IT'S TOO HOT!":GOTO340
540 IFG$=LEFT$(JT$(34),3)ANDR=35ANDAG<>-1THENPRINT"THE GORILLA WON'T LET YOU TAKE  IT.":GOTO340
550 FORX=3TO34
560 IFX>8ANDX<19ANDIT(X)=33ANDR=33ANDG$=LEFT$(JT$(X),3)THENMS=MS-10
570 IFJ=6THEN850
571 IFLEFT$(JT$(X),3)=G$ANDIT(X)=RTHENJ=J+1:IT(X)=-1:PRINT"OK":M$="C1F1E1":GOSUB4:GOTO340
580 NEXT
590 PRINT"I DON'T FEEL LIKE IT.":GOTO340
600 DATACOUNT DRACULA,DRACULA,25,THE TOWN DRUNK,DRUNK,23,BROKEN TOMBSTONE,TOMBSTONE,24,A FADED SIGN,SIGN,8,STONE TABLET
601 DATATABLET,0,GLOWING EMBER,EMBER,48,WOODEN SHOVEL,SHOVEL,15,ROSE BUD,ROSE,10
610 DATA**MAGIC OYSTER**,OYSTER,46,**SILVER COMPASS**,COMPASS,0,**DISK OF THE GODS**,DISK,38,**TURQUOISE CROSS**,CROSS,7
611 DATA**ANTIQUE CLOCK**,CLOCK,0,**SOME GLOWING RUBIES**,RUBIES,0,**PERUVIAN EMERALDS**,EMERALDS,0,**SPARKLING PEARL**,PEARL
620 DATA0,**CROWN JEWELS OF BAVARIA**,JEWELS,0,**SILVER BRACELET**,BRACELET,0
630 DATALANTERN AND OIL,LANTERN,22,GOLD NUGGETS,NUGGETS,0,DIAMOND KEY,KEY,0,STRANGE LOOKING BOX,BOX,20,HOLLOW STICK,STICK,24
631 DATAWHISKEY BOTTLE,BOTTLE,0,EINSTEIN'S PORTRAIT,PORTRAIT,49,NEWTON'S PICTURE,PICTURE,49,METAL STRAINER,STRAINER,17
640 DATASOME ALGAE,ALGAE,14,BEAKER OF WATER,BEAKER,29,MAGIC GLOVE,GLOVE,0,THE HIDEOUS LUTARN,LUTARN,0,LARGE EGG,EGG,0,REVOLVER
641 DATAREVOLVER,0,A BULLET,BULLET,35,WALL SAFE,SAFE,0
650 PRINT"YOU ARE CARRYING:"
660 IFAA=-1THENLA$="LIT ":GOTO670
661 LA$="UNLIT "
670 IFIT(19)=-1THENPRINTLA$;IT$(19):I=1
680 I=0:FORX=3TO34
690 IFIT(19)=-1THENI=1
700 IFX=19THENX=20
710 IFIT(X)=-1THENPRINTIT$(X):I=1
720 NEXT
730 IFI=0THENPRINT"NOTHING"
740 GOTO340
750 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
760 IFG$=LEFT$(JT$(19),3)ANDIT(18)=-1THENAA=0
770 IFG$=LEFT$(JT$(30),3)ANDIT(30)=-1THENPRINT"IT IS A PART OF YOU NOW.":GOTO340
780 FORX=3TO34
790 IFX>8ANDX<19ANDIT(X)=-1ANDR=33ANDG$=LEFT$(JT$(X),3)THENMS=MS+10
800 IFIT(X)=-1ANDLEFT$(JT$(X),3)=G$THEN830
810 NEXT
820 GOTO590
830 J=J-1:PRINT"OK":IT(X)=R
840 IFMS=100THENPRINT"YOU SOLVED THE ADVENTURE!":M$="A2B3C2D2E2F2G2N2M2L2K2C2J2A2H8":GOSUB4:GOTO1780
841 GOTO340
850 PRINT"YOU CAN'T CARRY ANYTHING ELSE.":GOTO340
860 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
870 FORX=3TO5
880 IFIT(X)=-1ANDG$=LEFT$(JT$(X),3)THENPRINTCHR$(34)RT$(X)CHR$(34):GOTO340
890 NEXTX
900 GOTO590
910 G$=RIGHT$(I$,LEN(I$)-4):G$=LEFT$(G$,3)
920 IFR=22ANDBU=1THENPRINT"WE'RE ALL OUT BUDDY.":GOTO340
930 IFG$=LEFT$(JT$(19),3)ANDR=22ANDIT(20)=-1ANDJ<>6THENBU=1:GOTO550
940 GOTO590
950 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
960 IFIT(19)=-1ANDG$="ON"THEN980
961 IFIT(19)=-1ANDG$="OFF"THEN1000
970 GOTO590
980 IFAA=-1THENPRINT"IT'S ALREADY ON.":GOTO340
990 AA=-1:GOTO200
1000 IFAA=0THENPRINT"IT'S ALREADY OFF.":GOTO340
1010 AA=0:GOTO200
1020 G$=RIGHT$(I$,LEN(I$)-4)
1030 IFRM(24,4)=0THEN1050
1040 IFR=24ANDG$="BONE"THENRM(24,4)=0:RM(24,3)=25:GOTO220
1050 PRINT"OK":PRINTCHR$(34);G$;CHR$(34):GOTO340
1060 PRINT"OUT OF 100 POINTS YOU HAVE      SCORED";MS;"POINTS."
1070 PRINT"LIGHT LEFT=";LT:GOTO340
1080 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1090 IFRM(25,3)=0ANDG$=LEFT$(JT$(12),3)ANDIT(12)=-1ANDR=25THEN1101
1100 GOTO590
1101 PRINT"THE COUNT FLIES AWAY IN PAIN.":IT(1)=0:RM(25,3)=28:GOTO340
1110 IFIT(7)<>-1THENPRINT"NO SHOVEL":GOTO340
1120 IFR=10ANDIT(31)=-2ANDIT(32)=0THENIT(32)=10:GOTO220
1130 IFR=12ANDIT(21)=0THENIT(21)=12:GOTO220
1140 PRINT"NOTHING HERE FRIEND.":GOTO340
1150 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
1160 IFIT(5)=0 AND G$=LEFT$(JT$(8),3) AND R=45 AND OP=-1 AND IT(8)=-1 THEN IT(5)=45:IT(10)=16:GOTO200
1170 IFG$=LEFT$(JT$(8),3)ANDIT(8)=-1THENPRINT"AHHH!! WHAT BEAUTY...":GOTO340
1171 PRINT"MY NOSE IS STUFFED UP.":GOTO340
1180 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1190 IFIT(13)=0ANDG$=LEFT$(JT$(22),3)ANDIT(23)=0ANDR=20THENIT(24)=20:IT(13)=31:IT(33)=20:GOTO200
1200 IFIT(23)<>0ANDG$=LEFT$(JT$(22),3)ANDR=20THENPRINT"AN ANGRY DOG IS GUARDING THE    STRANGE BOX...":GOTO340
1210 IFOP<>-1ANDG$=LEFT$("COFFIN",3)ANDR=45ANDIT(21)=-1THENGOSUB2020:GOTO200
1220 GOTO590
1230 G$=RIGHT$(I$,LEN(I$)-4):G$=LEFT$(G$,3)
1240 IFDS=-1ANDG$=LEFT$(JT$(2),3)ANDR=23THENPRINTCHR$(34);"ADD ONE LETTER TO EACH LETTER   IN THE PHRASE...";CHR$(34):GOTO340
1250 IFG$=LEFT$(JT$(2),3)ANDR=23THENPRINTCHR$(34);"FOR A BOTTLE OF WHISKEY I COULD BECOME VERY TALKATIVE...";CHR$(34):GOTO340
1260 GOTO590
1270 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
1280 IFIT(31)=50ANDR=50ANDIT(6)=-1 AND G$=LEFT$(JT$(6),3) AND IT(31)=50 THEN1301
1290 IFIT(23)=-1ANDG$=LEFT$(JT$(23),3)ANDR=20THENPRINT"THE DOG RUNS FOOLISHLY AFTER    THE STICK...":IT(23)=0:J=J-1:GOTO300
1300 GOTO760
1301 PRINT:PRINT"LUTARN BURNS FIERCELY. A VOICE  SAYS"CHR$(34)"GID TA TIP"CHR$(34)
1302 FORI=1TO2500:NEXTI:IT(6)=50:IT(31)=-2:J=J-1:GOTO220
1310 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1320 IFDS<>-1ANDR=23ANDG$=LEFT$(JT$(24),3)ANDIT(24)=-1THEN1331
1330 GOTO760
1331 PRINT"THE DRUNK TAKES THE BOTTLE      IMMEDIATELY...":IT(24)=0:GOSUB2010:J=J-1:GOTO340
1340 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1350 IFIT(35)=0ANDG$=LEFT$(JT$(25),3)ANDR=49THENIT(35)=49:GOTO200
1360 GOTO590
1370 G$=RIGHT$(I$,LEN(I$)-7):G$=LEFT$(G$,3)
1380 IFIT(15)=0ANDG$=LEFT$(JT$(35),3)ANDIT(35)=49ANDR=49THEN1400
1390 GOTO590
1400 PRINTCHR$(34);"REFER TO BIRTH DIGITS";CHR$(34);" SAYS AN ECHOING VOICE..."
1410 FORI=1TO1000:NEXTI:CLS:FORX=1TO3
1420 B$(1)="LEFT":B$(2)="RIGHT":B$(3)="LEFT"
1430 PRINT@70,B$(X);" VALUE":INPUTC(X)
1440 IFC(X)<=0 THEN 1430
1450 NEXTX
1460 IFC(1)=7ANDC(2)=8ANDC(3)=6THENPRINT"THE SAFE CREAKS OPEN...":FORH=1TO2500:NEXT:IT(14)=49:IT(15)=49:GOTO200
1470 PRINT"THE SAFE DIDN'T OPEN.":FORH=1TO2500:NEXTH:GOTO200
1480 G$=RIGHT$(I$,LEN(I$)-4):G$=LEFT$(G$,3)
1490 IFIT(20)<>0ANDR=14ANDG$=LEFT$(JT$(27),3)ANDIT(27)=-1THENGOTO1140
1500 IFG$=LEFT$(JT$(27),3)ANDR=14ANDIT(27)=-1THENIT(20)=14:GOTO200
1510 GOTO590
1520 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1530 IFIT(16)=0ANDG$=LEFT$(JT$(9),3)ANDIT(9)=-1ANDIT(28)=-1THEN1541
1540 GOTO590
1541 PRINT"THE OYSTER EATS THE ALGAE.":PRINT"AND SOMETHING IN IT DISAPPEARS"
1542 FORI=1TO2500:NEXTI:IT(16)=2:IT(28)=0:J=J-1:GOTO200
1550 G$=RIGHT$(I$,LEN(I$)-7):G$=LEFT$(G$,3)
1560 IFIT(17)=0ANDG$=LEFT$(JT$(16),3)ANDR=42ANDIT(16)=-1THENGOSUB2000:GOTO200
1570 GOTO590
1580 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1590 IFFG<>-1ANDR<>10ANDG$=LEFT$("WATER",3)ANDIT(29)=-1THENPRINT"YOU'RE ARE IN TROUBLE NOW...":FORI=1TO2500:NEXT:FG=-1:GOTO200
1600 IFFG<>-1ANDUP<>-1ANDIT(29)=-1ANDG$=LEFT$("WATER",3)ANDR=10THEN1611
1610 GOTO590
1611 UP=-1:FG=-1:PRINT"THE FORCE FIELD IS DESTROYED":FORI=1TO2500:NEXT:GOTO200
1620 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1630 IFNF<>-1ANDG$=LEFT$(JT$(30),3)ANDIT(30)=-1THENPRINT"NICE FIT..":NF=-1:GOTO340
1640 GOTO590
1650 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
1660 IFG$=LEFT$("HERMIT",3)ANDR=2ANDNF=-1THENPRINT"YOU FEEL DIZZY FOR A MOMENT":PH=-1:GOTO340
1670 IFG$=LEFT$("HERMIT",3)ANDR=2ANDNF<>-1THENPRINT"YOU HAVE JUST DIED...":GOTO1730
1680 GOTO590
1690 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1700 IFIT(31)<>-2ANDG$=LEFT$(JT$(31),3)ANDR=50ANDPH=-1THENIT(31)=50:GOTO220
1710 IFG$=LEFT$(JT$(31),3)ANDR=50ANDPH<>-1THENYD=-1:IT(31)=50:GOTO220
1720 GOTO590
1730 PRINT"DO YOU WANT TO PLAY AGAIN?"
1740 A$=INKEY$:IFA$="Y"THEN0
1741 IFA$="N"THENEND
1742 GOTO1740
1750 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
1760 IFIT(18)=0ANDG$=LEFT$(JT$(32),3)ANDIT(32)=-1THEN1771
1770 GOTO590
1771 IT(18)=5:PRINT"SOMETHING FALLS OUT...":PRINT"THEN DISAPPEARS":FORI=1TO2500:NEXTI:GOTO220
1780 INPUT"FILENAME";FI$:IFFI$=""THEN340
1785 IT(O+1)=J:IT(O+2)=R:IT(O+3)=MS:IT(O+4)=DS:IT(O+5)=OP:IT(O+6)=RM(24,3):IT(O+6)=RM(24,4)
1786 IT(O+8)=RM(25,3):IT(O+9)=LT:IT(O+10)=AA:IT(O+11)=FG:IT(O+12)=UP:IT(O+13)=NF:IT(O+14)=BU
1787 IT(O+15)=PH:IT(O+16)=ZZ:IT(O+17)=AG:IT(O+18)=LO
1790 CSAVE*IT,FI$:PRINT"OK":IFMS=100THEN1730
1795 GOTO340
1800 INPUT"FILENAME";FI$:IFFI$=""THEN200
1810 CLOAD*IT(X),FI$
1811 J=IT(O+1):R=IT(O+2):MS=IT(O+3):DS=IT(O+4):OP=IT(O+5):RM(24,3)=IT(O+6):RM(24,4)=IT(O+6)
1812 RM(25,3)=IT(O+8):LT=IT(O+9):AA=IT(O+10):FG=IT(O+11):UP=IT(O+12):NF=IT(O+13):BU=IT(O+14)
1813 PH=IT(O+15):ZZ=IT(O+16):AG=IT(O+17):LO=IT(O+18):GOSUB3000:GOTO200
1820 G$=RIGHT$(I$,LEN(I$)-6):G$=LEFT$(G$,3)
1830 IFIT(33)=-1ANDLO=-1ANDZZ<>-1ANDG$=LEFT$("GUNFIGHTER",3)ANDR=21THENPRINT"YOU KILLED THE GUNFIGHTER."
1831 IT(30)=21:FORI=1TO2500:NEXTI:GOSUB2040:GOTO200
1840 GOTO590
1850 END
1860 IFRND(10)=1THENPRINT:PRINT"THE GUNFIGHTER FILLED YOU FULL  OF LEAD!":GOTO1730
1870 GOTO340
1880 IFR=50THENPRINT"WITHOUT THE TOUCH, THE SIGHT OF LUTARN WILL KILL YOU.":GOTO340
1890 IFR=2THENPRINT"TOUCH THE HERMIT ONLY WHILE THE GLOVE IS ON.":GOTO340
1900 IFR=20THENPRINT"LUTARN LOVES TO HEAR HIS NAME.":GOTO340
1910 PRINT"NOT NOW...":GOTO340
1920 IFAG<>-1ANDR=35THENPRINT"THE GORILLA FALLS ASLEEP.":AG=-1:GOTO340
1930 PRINT"YOU'RE A LITTLE OFF TUNE...":GOTO340
1940 G$=RIGHT$(I$,LEN(I$)-5):G$=LEFT$(G$,3)
1950 IFLO<>-1ANDG$=LEFT$(JT$(33),3)ANDIT(33)=-1ANDIT(34)=-1THEN1970
1960 GOTO590
1970 PRINT"OK":GOSUB2030:IT(34)=0:J=J-1:GOTO340
2000 IT(17)=42:A$(42)="IN FRONT OF AN UNLOCKED DOOR.":RETURN
2010 DS=-1:IT$(2)="THE HAPPY TOWN DRUNK":DS=-1:RETURN
2020 OP=-1:A$(45)="NEAR AN OPEN COFFIN":RETURN
2030 LO=-1:IT$(33)="LOADED REVOLVER":RETURN
2040 ZZ=-1:IT$(33)="EMPTY REVOLVER":RETURN
3000 IFIT(17)=42THENGOSUB2000
3010 IFDS=-1THENGOSUB2010
3020 IFOP=-1THENGOSUB2020
3030 IFLO=-1THENGOSUB2030
3040 IFZZ=-1THENGOSUB2040
3050 RETURN
