0 CLEAR500:DIMCC,ZZ,M$,I$,N(21):MC=16384:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOTO330
4 GOTO410
6 CC=(PEEK(17024)AND1)*256+PEEK(17025)+MC-1:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-MC+ZZ,:RETURN
7 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC))-64),ASC(MID$(M$,CC+1))-48:NEXT:RETURN
10 FORT=1TO2500:NEXT:RETURN
11 FORC1=1TO250:NEXT:RETURN
15 G1=RND(28)+2:IFG1=LORG1=28ORG1=7THEN15
16 RETURN
20 CC=1:FORC1=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):CC=CC+1
22 NEXT
30 CLS:PRINT@7,;:M$="THE BIANCO MANSION":GOSUB6:PRINT:GOSUB3000:GOSUB1470:PRINT@486,;:M$="HIT ANY KEY TO BEGIN":GOSUB6
35 IFINKEY$=""THEN35
40 REM MAIN LOOP
50 ST=ST-1:IFST=0THEN1340
60 CLS:PRINT:M$="YOU ARE IN "+L$(L)+".":GOSUB1
70 IFL=7ANDL(17)=0THEN1200
80 IFL=7THENIFL(17)THEN1320
90 FORM=1TO4:IFP(L,M)=-1THENM$="THERE IS LOCKED DOOR TO THE "+MID$(D$,(5*M)-4,5+(M>2))+".":GOSUB1
91 NEXT
100 S=0:PRINT:PRINT"YOU HAVE:";
110 FORD=1TO18:IFD=6ORD=9ORD=10ORD>14THENA$=" ":GOTO120
111 A$=" A "
120 IFS>0ANDL(D)=0THENPRINTTAB(9);
130 IFL(D)=0THENPRINTA$;O$(D):S=S+1
140 NEXTD:IFS=0THENPRINT" NOTHING"
150 PRINT
160 Z=0:PRINT"YOU CAN SEE:";
170 FORD=1TO18:IFD=6ORD=9ORD=10ORD>14THENA$=" ":GOTO180
171 A$=" A "
180 IFL(D)=LANDZ>0THENPRINTTAB(12);
190 IFL(D)=LTHENPRINTA$;O$(D):Z=Z+1
200 NEXTD
210 IFZ=0THENPRINT" NOTHING MUCH."
220 IFG1=LTHENPRINT:PRINTTAB(8);:M$="A SECURITY GUARD!":GOSUB6:PRINT:DC=DC-1:IFDC=0THEN1340
230 PRINT:INPUT"WHAT NOW";N$
231 IFLEFT$(N$,3)="USE"THENN$="PUT"+MID$(N$,4)
232 IFN$="N"THENN$="GO NORTH"
233 IFN$="S"THENN$="GO SOUTH"
234 IFN$="E"THENN$="GO EAST"
235 IFN$="W"THENN$="GO WEST"
240 GOSUB1400
250 IFG1=LANDT<>13ANDT<>14ANDT<24THEN290
260 IFT>96ORT<1THENPRINT:PRINTTAB(12)MID$(P$,((RND(4)-1)*8)+1,8):GOTO280
270 ONTGOSUB3,3,4,4,4,500,560,620,640,670,680,680,810,810,900,910,940,940,1000,1040,1040,1050,1080,1110,1110,1110,1110
280 FORTT=1TO500:NEXT:IFT<3THEN310
290 IFG1=LTHENPRINT"THE GUARD FIGHTS BACK...":SOUND1,1
300 FORTT=1TO850:IFINKEY$=""THENNEXT:GOTO40
301 TT=1000:NEXT
310 GOTO40
320 REM COMMANDS
330 REM
340 IFLEFT$(CT$,1)="N"THENK=1
350 IFLEFT$(CT$,1)="S"THENK=2
360 IFLEFT$(CT$,1)="E"THENK=3
370 IFLEFT$(CT$,1)="W"THENK=4
380 N=L:L=P(L,K)
390 IFL<1THENPRINT"YOU CAN'T GO THAT WAY!":L=N
400 RETURN
405 M$="C1J1C1J1C1J1C1J1":GOSUB7:RETURN
410 DV=0:FORCT=1TO54STEP3:IFMID$(O$,CT,3)=LEFT$(CT$,3)THENDV=CT
420 NEXT:CT=(DV+2)/3:IFDV=0THENT=100:RETURN
430 IFS>4OR((L(8)=0ORL(13)=0)ANDS>2)THENPRINT"YOU MUST DROP SOMETHING FIRST.":RETURN
440 IF(CT=8ORCT=13)ANDS>2THENM$="TO CARRY THE "+O$(CT)+", YOU CAN ONLY CARRY 2 OTHER ITEMS.":GOSUB1:RETURN
441 IFL=21THENIFCT$="MON"THEN:M$="AN ALARM SOUNDS. A GUARD ARRIVES!":GOSUB1:G1=L:GOTO405
450 IFL=3ANDCT=17THENIFL(17)=34THENPRINT"IT IS OUT OF REACH.":RETURN
451 IFL(CT)<>LTHENPRINT"IT IS NOT HERE.":RETURN
460 IFCT=3ORCT=11THENM$="DON' T BE SILLY, IT'S TOO HEAVY.":GOSUB1:RETURN
470 IFL(17)=3THENIFMF=0THENST=25:M$="THE ALARM HAS SOUNDED. YOU DON'T HAVE MUCH TIME!":GOSUB1:L(17)=0:MF=1:GOTO405
480 IFL(CT)=LTHENL(CT)=0:M$="YOU ARE NOW CARRYING THE "+O$(CT)+".":GOSUB1:RETURN
490 RETURN
500 REM DROP
510 DV=0:FORCT=1TO54STEP3:IFLEFT$(CT$,3)=MID$(O$,CT,3)THENDV=CT
520 NEXT:CT=(DV+2)/3:IFDV=0THENT=100:RETURN
521 IFL(CT)>35THENL(CT)=0
530 IFS=0THENM$="YOU HAVE NOTHING TO DROP.":GOSUB1:RETURN
540 IFL(CT)THENM$="YOU DON'T HAVE IT TO DROP.":GOSUB1:RETURN
550 L(CT)=L:M$="YOU HAVE DROPPED THE "+O$(CT)+".":GOSUB1:RETURN
560 IFL(2)THENPRINT"IT'S TOO DARK TO READ!":RETURN
570 IFL(4)THENIFL(17)THENIFL(18)THENM$="YOU'RE NOT HOLDING ANY READING MATERIALS!":GOSUB1:RETURN
580 IFL(4)=0ANDCT$="LET"THENM$="IT READS: WHAT YOU NEED IS IN THE STUDY.":GOSUB1:GOTO610
590 IFL(18)=0ANDCT$="ATL"THENM$="I'M SURE YOU HAVE SEEN AN ATLAS BEFORE.":GOSUB1:RETURN
600 IFL(17)=0ANDCT$="MAP"THENM$="YOUR TASK IS TO STEAL'EM NOT READ'EM.":GOSUB1:RETURN
601 IFL=14THENM$="THERE'S TOO MUCH HERE TO READ!":GOSUB1:RETURN
610 PRINT:INPUT"PRESS ENTER TO CONTINUE";M$:RETURN
615 IFL(17)=34THENM$="YOU CAN REACH THE MAP NOW.":GOSUB1:L(17)=3
616 RETURN
620 IFL=3THENIF(L(8)=0ORL(8)=L)ANDCT$="STO"THENL(8)=3:GOTO615
621 IFL=3THENIF(L(13)=0ORL(13)=L)ANDCT$="LAD"THENL(13)=3:GOTO615
630 IFL=6THENM$="YOU CANNOT CLIMB OVER THE FENCE.":GOSUB1:RETURN
640 IFL=6ANDL(6)=36ANDL(9)=0ANDCT$="FEN"THEN645
641 GOTO650
645 M$="THE FENCE HAS BEEN CUT. YOU CAN GET OUT BY GOING NORTH.":GOSUB1:P(6,1)=7:RETURN
650 IFL=6ANDL(6)<>0ANDL(9)=0ANDCT$="FEN"THENM$="YOU CUT THE FENCE BUT FORGOT IT WAS electrified!":GOSUB1:SOUND245,25:GOTO1340
651 IFL=6ANDL(9)<>0ANDCT$="FEN"THEN660
651 IFL(9)=0ORL(5)=0THENPRINT"CUT WHAT? WHY?":RETURN
660 M$="YOU HAVE NOTHING TO CUT IT WITH.":GOSUB1:RETURN
670 M$="O.K. I CHANGED YOU INTO A FROG...":GOSUB1:GOSUB10:GOTO1340
680 DDV=0:FORM=1TO4:IFP(L,M)=-1THENDDV=1
690 NEXTM
691 IFDDV=1ANDL(14)=0THEN695
692 GOTO700
695 KY=KY+1:IFKY=3THENKY=0:L(14)=35:M$="THE KEY WAS OLD AND YOU USED IT TOO MUCH, IT HAS CRUMBLED INTO DUST.":GOSUB1:RETURN
700 IFDDV<>1ORL(14)<>0ORCT$<>"DOO"THEN770
710 M$="WITH A CREAK, THE DOOR OPENS...":GOSUB1:M$="C1D1C1":GOSUB7
720 IFL=19THENP(19,2)=18:P(18,1)=19
730 IFL=2ORL=27THENP(2,1)=27:P(27,2)=2
740 IFL=16ORL=28THENP(28,2)=16:P(16,1)=28
750 IFL=21ORL=22THENP(22,3)=21:P(21,4)=22
760 RETURN
770 IFL=19THENM$="YOU ARE TRAPPED INSIDE THE VAULT.":GOSUB1:IFL(14)THENGOSUB10:GOTO1340
771 IFCT$="LET"THENIFL(4)=0ORL(4)=LTHENPRINT"IT DOESN'T NEED TO BE OPENED.":RETURN
780 IFDDV<>1THENM$="THERE IS NO DOOR TO UNLOCK FOOL!":GOSUB1:RETURN
790 IFL(14)THENPRINT"YOU DON'T HAVE THE KEY.":RETURN
800 T=100:RETURN
810 IFL(12)=37ANDG1=LANDL(1)=0ANDLEFT$(CT$,2)="GU"THEN870
820 IFG1<>LTHENM$="THERE IS NO GUARD HERE!":GOSUB1
830 IFL(1)THENM$="YOU HAVE NOTHING TO FIRE WITH.":GOSUB1
840 IFLEFT$(CT$,2)<>"GU"THENPRINT"SHOOT WHAT?"
850 IFL(1)=0ANDG1=LANDLEFT$(CT$,2)="GU"ANDL(12)<>37THEN855
851 RETURN
855 M$="YOU KILLED HIM BUT THE GUNSHOTS HAVE ACTIVATED THE SONIC ALARM. YOU HAVE LITTLE TIME TO ESCAPE!":GOSUB1
856 ST=15:M$="D2B2C2B2C2B2C2B2C2B2C2":GOSUB7:G1=RND(28)+2:IFG1=LORG1=28THENG1=G1-1
860 RETURN
870 IFRND(3)<2THENPRINT"YOU MISSED HIM.":RETURN
880 PRINT"YOU KILLED HIM.":GOSUB15
890 RETURN
900 PRINT"TIME PASSES...":FORTT=1TO2500:NEXT:RETURN
910 IFLEFT$(CT$,3)<>"WIN"ORL<>4THENPRINT"DRINK WHAT?":RETURN
920 IFL=4THENIFL(7)THENM$="YOU HAVE NOTHING TO DRINK FROM.":GOSUB1:RETURN
930 M$="YUK! IT TASTES AWFUL, BUT IT WAS ONLY COOKING WINE!":GOSUB1:RETURN
940 IFL(2)THENM$="IT'S TOO DARK TO LOOK AROUND OR SEARCH.":GOSUB1:RETURN
950 IFL=3THENM$="THE MAP IS ON THE TOP SHELF.":GOSUB1:RETURN
951 IFL=18THENM$="THE VAULT DOOR HAS AN AUTOMATIC TIME LOCK. IT IS OPEN AT THIS TIME.":GOSUB1:RETURN
960 IFL=21THENM$="YES, THAT IS A MONET ON THE FAR WALL. HOW OBSERVENT OF YOU...":GOSUB1:RETURN
970 IFL=2THENM$="THERE IS A LOCKED DOOR WESTWARDS. IT CAN ONLY BE OPENED BY THE LETTER KNIFE.":GOSUB1:RETURN
980 IFL=30THEN985
981 GOTO990
985 M$="THERE IS A NOTE ON THE TABLE ADDRESSED TO THE CONTESSA. "
986 M$=M$+"IT SAYS THAT THE ELECTRICIAN LEFT HIS GLOVES IN THE GALLERY AND COULD HE HAVE THEM BACK PLEASE.":GOSUB1
987 GOTO610
990 M$="I'M LOOKING, BUT THERE'S LITTLE HERE TO SEE.":GOSUB1:RETURN
1000 IFL(6)=0THENM$="THEY FIT YOU PERFECTLY! YOU ARE NOW WEARING GLOVES.":GOSUB1:L(6)=36:RETURN
1010 IFL(15)=0THENM$="VERY NICE AND VERY VALUABLE. IT IS YOUR SIZE...":GOSUB1:L(15)=36:RETURN
1020 M$="YOU ARE WEARING ALL THAT YOU OWN.":GOSUB1:RETURN
1030 RETURN
1040 M$="HELP YOURSELF, NO CLUES FROM ME.":GOSUB1:RETURN
1050 PRINT:INPUT"HAVE YOU REALLY HAD ENOUGH";A$
1060 IFLEFT$(A$,1)="Y"THENPRINT"OK YOU HAVE GIVEN UP...coward!":SOUND1,16:END
1070 RETURN
1080 IFLEFT$(CT$,3)="GUN"ANDMID$(N$,5,8)="SILENCER"ANDL(12)=0ANDL(1)=0THEN1085
1081 IFLEFT$(CT$,3)="SIL"ANDL(12)=0ANDL(1)=0THENINPUT"ON WHAT";W$:IFLEFT$(W$,3)="GUN"THEN1085
1082 GOTO1090
1085 M$="SILENCER ATTACHED. YOU CAN NOW FIRE QUIETLY.":GOSUB1:L(12)=37:RETURN
1090 IFLEFT$(CT$,3)="LOC"ANDMID$(N$,5,3)="KNI"ANDL=2ANDL(5)=0THEN1095
1091 IFLEFT$(CT$,3)="KNI"ANDL=2ANDL(5)=0THENINPUT"IN WHAT";W$:IFLEFT$(W$,3)="LOC"THEN1095
1092 GOTO1100
1095 M$="THE DOOR QUIETLY OPENS. JUST ENTER go west.":GOSUB1:P(2,4)=3:P(3,3)=2:RETURN
1100 M$="YOU CAN'T PUT THAT THERE!":GOSUB1:RETURN
1110 IFG1=LTHEN1130
1120 M$="THERE'S NO ONE NEAR YOU.":GOSUB1:RETURN
1130 INPUT"WITH WHAT";W$:IFLEFT$(W$,3)="GUN"THENCT$="GUN":GOTO830
1131 IFLEFT$(W$,4)="WIRE"ANDL(9)=0ANDRND(2)=1THENPRINT"YOU KNOCK HIM OUT COLD!":G1=0
1132 IFLEFT$(W$,4)="KNIF"ANDL(5)=0ANDRND(4)=1THENM$="BLUNT, BUT EFFECTIVE. HE'S DEAD!":GOSUB1:G1=0
1140 IFW$="ROPE"ANDL(16)=0ANDRND(2)=1THENM$="YOU MANAGE TO SUBDUE HIM AND THEN TIE HIM UP.":GOSUB1:L(16)=35:G1=0
1150 IFLEFT$(W$,4)<>"WIRE"ANDLEFT$(W$,4)<>"ROPE"ANDLEFT$(W$,4)<>"KNIF"THEN1180
1160 IFG1=0THENGOSUB15
1170 RETURN
1180 M$="YOU FIGHT WITH YOUR BARE HANDS...":GOSUB1:IFRND(5)=1THENPRINT"HE'S KNOCKED OUT!":G1=0:GOTO1160
1190 RETURN
1200 REM SUCCESS
1210 GOSUB10:CLS:PRINT:PRINT
1211 M$="WELL DONE! YOU ESCAPED WITH THE MAP. YOUR TASK IS COMPLETED. YOU CAN NOW GO A FREE MAN.":GOSUB1
1220 M$="C2E2D2F2G2A2B4A4B4":GOSUB7:GOSUB10

1230 CLS0:T=0:FORCC=1TO4:PRINT@T,"æìâê€êæìâæìâîìèæìâæìâê";
1240 PRINT@T+32,"éã€ê€êê€€ê€€ëã€éã€éã€ê";
1250 PRINT@T+64,"â€êê€êê€âê€âê€€â€êâ€êè";
1260 PRINT@T+96,"äì€äì€äì€äì€ììèäì€äì€è";:T=T+128+3:NEXT
1300 FORT=1TO5000:NEXT:PRINT@480,;:M$="HIT R":GOSUB6
1310 IFINKEY$<>"R"THEN1310
1315 RUN
1320 REM ESCAPED BUT WITHOUT MAP
1330 GOSUB10:CLS0:PRINT@224,;:M$="YOU ESCAPED BUT WITHOUT THE MAP. THEREFORE, YOU MUST ENTER AGAIN.":GOSUB1
1331 GOTO1300
1340 CLS0:T=0:FORCC=1TO4:PRINT@T,"¾¼¸±¹€´¾€º€€º€º¾¼²¾¼¸º";
1350 PRINT@T+32,"»²€º€º€º€º€€º€º»³¸»³€º";
1360 PRINT@T+64,"º€€¾¼º€º€º€€º€ºº¹€º€€¸";
1370 PRINT@T+96,"¸€€¸€¸´¼€¼¼¸´¼€¸€¸¼¼¸¸";:T=T+128+3:NEXT
1390 M$="N4K4A4E4C4":GOSUB7:GOTO1300
1400 REM COMMAND HANDLING
1410 T=1:IFLEN(N$)<3THENT=100:RETURN
1420 B$=MID$(C$,T,3):IFB$=LEFT$(N$,3)THENT=(T+2)/3:GOTO1450
1430 IFT>97THEN1460
1440 T=T+3:GOTO1420
1450 FORCT=LEN(N$)TO1STEP-1:IFMID$(N$,CT,1)=" "THENCT$=MID$(N$,CT+1,3):CC=CT:CT=1:NEXT:CT=CC:RETURN
1451 NEXT
1460 RETURN
1470 REM INITIALISATION
1480 DIMO$(18),L$(30),P(30,4),L(18),A(18):FORT=1TO18:READO$(T):NEXT:FORT=1TO30:READL$(T):NEXT
1510 FORT=1TO18:READL(T):NEXT
1520 DATAGUN,TORCH,"WINE-CASK","PERFUMED LETTER","LETTER-KNIFE",GLOVES,CUP,STOOL,"WIRECUTTERS"
1530 DATAMONEY,STATUE,SILENCER,LADDER,KEY,RING,ROPE,MAP,ATLAS
1540 DATA"THE MAIN ENTRANCE. THE COUNT AND CONTESSA ARE AWAY. YOU CAN ENTER"
1541 DATA"THE RECEPTION AREA","THE STUDY","THE KITCHENS","THE COURTYARD"
1542 DATA"THE SHADOW OF THE ELECTRIFIED FENCE","SAFETY. FREE AT LAST","A GREAT HALLWAY","THE BOILER ROOM"
1550 DATA"THE WINE CELLAR","AN OAKEN CORRIDOR","AN UNKEMPT GARDEN","THE BILLIARD ROOM"
1551 DATA"THE LIBRARY","A DIMLY-LIT STOREROOM","THE MAGNIFICENT DINING ROOM","A SPACIOUS STUDIO"
1560 DATA"REACH OF THE COUNT'S VAULT","THE COUNT'S VAULT","THE SMOKING ROOM","A ROOM FILLED WITH PAINTINGS"
1561 DATA"A DARKENED BEDROOM","THE SERVANTS QUARTERS","THE CARD ROOM","THE MASTER BEDROOM","A LARGE CUPBOARD"
1570 DATA"AN AUSTERE LIVING ROOM","THE NURSERY","THE DOORWAY LEADING TO THE CONTESSA'S ROOM","THE CONTESSA'S ROOM"
1580 DATA0,2,4,27,8,21,25,13,9,19,20,30,5,10,13,11,34,11
1590 D$="NORTHSOUTHEAST WEST "
1600 C$="GO MOVTAKGETSTEDROREACLICUTCHAOPEUNLFIRSHOWAIDRISEALOOWEAHELCLUQUIPUTHITFIGATTKIL"
1610 O$="GUNTORWINLETKNIGLOCUPSTOWIRMONSTASILLADKEYRINROPMAPATL"
1620 P$="PARDON? WHAT?   RUBBISH!REPHRASE"
1630 L=1:L(1)=0:K=0:ST=10000:KY=0
1640 GOSUB15
1650 DC=RND(6)+4
1660 FORX=1TO30:FORY=1TO4:READP(X,Y):NEXT:NEXT
1670 DATA2,,,,-1,1,4,,,,2,,,10,9,2,,15,6,,,,5,,,,,,15,24,,21,20,8,13,9,4,,9,1
1680 DATA0,27,14,12,17,,11,16,16,,,9,26,,18,11,5,8,,21,-1,23,12,26,22,12,15,,19,20,18,14,,-1,,,18,9,,0
1690 DATA2,17,8,-1,,17,-1,28,16,23,,17,8,,26,12,,,,27,6,14,,24,11,-1,25,13,29,16,,,,28,,30,,,29,0
1710 RETURN 
3000 M$="FOR MANY YEARS YOU HAVE LIVED THE LIFE OF A SOUL TORMENTED WITH A NEED TO FIND A PARTICULAR ANCIENT MAP.":GOSUB1
3010 M$="YOU HAVE AT LAST DISCOVERED THE BUILDING WHERE IT LIES, THE PICTURESQUE BIANCO MANSION, "
3020 M$=M$+"HOME OF COUNT PHILLIPE AND CONTESSA CATHERINE DE BIANCO.":GOSUB1
3030 M$="THEY ARE AWAY AT PRESENT AND YOU HAVE ENTERED THE BUILDING. "
3040 M$=M$+"YOU MUST FIND THE MAP AND LEAVE VIA THE COURTYARD. WATCH OUT FOR SECURITY GUARDS!":GOSUB1:RETURN
4010 REM THE BIANCO MANSION
4020 REM    CLIVE GIFFORD
4030 REM  VIRGIN BOOKS 1984
4040 REM MC-10 VER. JIM GERRIE
