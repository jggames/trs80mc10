0 REM (C) 1985 BY R.P.WILLERT
10 CLEAR2000
20 DIMA$(22)
30 FORA=1TO22:READA$(A):NEXT
40 DATACHEST,DOOR,KEY,DOG,PISTOL,GUARD,DIAMOND,LADDER,ROOM
50 DATATAKE,LOOK,KILL,GO,UP,DOWN,WEST,NORTH,EAST,SOUTH,GIVE,OUT
60 DATAOPEN
70 DIMR(3,9),R$(3,9,15),T$(3,9)
80 A=RND(3):B=RND(9)
90 R(A,B)=R(A,B)+1
100 R$(A,B,R(A,B))="CHEST"
110 FORA=1TO3
120 FORB=1TO2
130 C=RND(9)
140 R(A,C)=R(A,C)+1
150 R$(A,C,R(A,C))="DOG"
160 NEXT:NEXT
170 FORA=1TO3
180 FORB=1TO2
190 C=RND(9)
200 R(A,C)=R(A,C)+1
210 R$(A,C,R(A,C))="PISTOL"
220 NEXT:NEXT
230 A=RND(3):B=RND(9)
240 R(A,B)=R(A,B)+2
250 R$(A,B,R(A,B)-1)="GUARD"
270 FORA=1TO2
280 B=RND(3):C=RND(9)
290 R(B,C)=R(B,C)+1
300 R$(B,C,R(B,C))="DIAMOND"
310 NEXT
312 R(2,4)=R(2,4)+1
314 R$(2,4,R(2,4))="DOOR"
320 FORA=1TO2
330 B=RND(9)
340 FORC=1TO3
350 R(C,B)=R(C,B)+1
360 R$(C,B,R(C,B))="LADDER"
390 NEXT:NEXT
400 FORA=1TO3
410 FORB=1TO9
420 READT$(A,B)
430 NEXT:NEXT
440 DATAES,WS,S,ES,NWES,NW,N,NE,W
450 DATAS,ES,W,NE,NWS,S,E,NWE,NW
460 DATAES,WE,WS,NES,WS,N,N,NE,W
470 REM
480 REM
490 E=RND(3):R=RND(9)
500 CLS
510 PRINT@0,"STICK :"E,"ROOM  :"R"      ";
530 PRINT"EXIT  : "T$(E,R),"DANGER:";
540 FORA=1TOR(E,R)
550 IFR$(E,R,A)="DOG"THEN570
560 NEXT:GOTO580
570 PRINT" POSITIVE";:GOTO590
580 PRINT" NEGATIVE";
590 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
600 REM
605 INPUT"WHAT NOW  ";W$
610 FORA=1TOLEN(W$)
620 IFMID$(W$,A,1)=" "THEN650
630 NEXT
640 PRINT"I DON'T UNDERSTAND !":GOTO600
650 W1$=LEFT$(W$,A-1)
660 W2$=RIGHT$(W$,LEN(W$)-A)
670 FORA=1TO22
680 IFA$(A)=W1$THEN720
690 NEXT
700 PRINT"I DON'T KNOW THE WORD "CHR$(34)W1$CHR$(34)" !"
710 GOTO600
720 FORA=1TO22
730 IFA$(A)=W2$THEN770
740 NEXT
750 PRINT"I DON'T KNOW THE WORD "CHR$(34)W2$CHR$(34)" !"
760 GOTO600
770 REM
780 IFW1$<>"TAKE"THEN950
790 IFW2$<>"KEY"THEN840
800 FORA=1TOR(E,R)
810 IFR$(E,R,A)="KEY"THENBE=BE+1:B$(BE)="KEY":R$(E,R,A)="":H=1
815 IFH=1THENH=0:PRINT"OK":T=1:GOTO5000
820 NEXT
830 PRINT"I CAN'T SEE ANY KEYS !":GOTO600
840 IFW2$<>"PISTOL"THEN890
850 FORA=1TOR(E,R)
860 IFR$(E,R,A)="PISTOL"THENBE=BE+1:B$(BE)="PISTOL":H=1
865 IFH=1THENH=0:R$(E,R,A)="":PRINT"OK":GOTO5000
870 NEXT
880 PRINT"I CAN'T SEE ANY PISTOLS !":GOTO600
890 IFW2$<>"DIAMOND"THEN940
900 FORA=1TOR(E,R)
910 IFR$(E,R,A)="DIAMOND"THENBE=BE+1:B$(BE)="DIAMOND":H=1
915 IFH=1THENH=0:R$(E,R,A)="":PRINT"OK":GOTO5000
920 NEXT
930 PRINT"I CAN'T SEE ANY DIAMONDS !":GOTO600
940 PRINT"I CAN'T DO THAT !":GOTO600
950 IFW1$<>"LOOK"THEN1130
960 IFW2$<>"CHEST"THEN1050
970 FORA=1TOR(E,R)
980 IFR$(E,R,A)="OPEN CHEST"ANDT=0THENPRINT"I SEE A KEY !":H=1
990 IFH=1THENH=0:R(E,R)=R(E,R)+1:R$(E,R,R(E,R))="KEY":GOTO5000
1000 IFR$(E,R,A)="CHEST"THENPRINT"THE CHEST IS SHUT !":GOTO600
1010 IFR$(E,R,A)="OPEN CHEST"ANDT=1THENH=1
1020 IFH=1THENH=0:PRINT"I SEE NOTHING SPECIAL !":GOTO600
1030 NEXT
1040 PRINT"I CAN'T SEE A CHEST !":GOTO600
1050 IFW2$<>"ROOM"THEN1120
1060 IFR(E,R)=0THENH=1:GOTO1020
1070 PRINT"I SEE :"
1080 FORA=1TOR(E,R)
1090 PRINTR$(E,R,A)
1100 NEXT
1110 GOTO5000
1120 H=1:GOTO1020
1130 IFW1$<>"KILL"THEN1250
1140 IFW2$<>"DOG"THEN1240
1150 FORA=1TOR(E,R)
1160 IFR$(E,R,A)="DOG"THEN1190
1170 NEXT
1180 PRINT"I CAN'T SEE ANY DOGS !":GOTO600
1190 FORC=1TOBE
1200 IFB$(C)="PISTOL"THENB$(C)="":R$(E,R,A)="DEATH DOG":H=1
1210 IFH=1THENH=0:PRINT"OK":GOTO500
1220 NEXT
1230 PRINT"I HAVEN'T GOT A PISTOL !":GOTO600
1240 PRINT" I CAN'T DO THAT !":GOTO600
1250 IFW1$<>"GO"THEN1600
1255 IFW2$="OUT"THEN1890
1260 IFW2$<>"UP"THEN1330
1270 IFE=1THEN1320
1280 FORA=1TOR(E,R)
1290 IFR$(E,R,A)="LADDER"THENH=1
1300 IFH=1THENH=0:PRINT"OK":E=E-1:GOTO500
1310 NEXT
1320 PRINT"I CAN'T GO THAT WAY !":GOTO600
1330 IFW2$<>"DOWN"THEN1400
1340 IFE=3THEN1320
1350 FORA=1TOR(E,R)
1360 IFR$(E,R,A)="LADDER"THENH=1
1370 IFH=1THENH=0:PRINT"OK":E=E+1:GOTO500
1380 NEXT
1390 GOTO1320
1400 IFW2$<>"WEST"THEN1450
1410 FORA=1TOLEN(T$(E,R))
1420 IFMID$(T$(E,R),A,1)="W"THENR=R-1:GOTO500
1430 NEXT
1440 GOTO1320
1450 IFW2$<>"NORTH"THEN1500
1460 FORA=1TOLEN(T$(E,R))
1470 IFMID$(T$(E,R),A,1)="N"THENR=R-3:GOTO500
1480 NEXT
1490 GOTO1320
1500 IFW2$<>"EAST"THEN1550
1510 FORA=1TOLEN(T$(E,R))
1520 IFMID$(T$(E,R),A,1)="E"THENR=R+1:GOTO500
1530 NEXT
1540 GOTO1320
1550 IFW2$<>"SOUTH"THEN1590
1560 FORA=1TOLEN(T$(E,R))
1570 IFMID$(T$(E,R),A,1)="S"THENR=R+3:GOTO500
1580 NEXT
1590 GOTO1320
1600 IFW1$<>"GIVE"THEN1730
1610 IFW2$<>"DIAMOND"THEN1240
1620 FORA=1TOBE
1630 IFB$(A)="DIAMOND"THEN1660
1640 NEXT
1650 PRINT"I HAVEN'T GOT ANY DIAMONDS !":GOTO600
1660 FORB=1TOR(E,R)
1670 IFR$(E,R,B)="GUARD"THEN1695
1680 NEXT
1690 PRINT"I CAN'T SEE A GUARD !":GOTO600
1695 PRINT"OK":B$(A)="":R$(E,R,B)=""
1700 PRINT"THE GUARD GIVE YOU A KEY AND    RUN AWAY !"
1710 B$(A)="KEY"
1720 GOTO5000
1730 IFW1$<>"OPEN"THEN640
1740 IFW2$<>"CHEST"THEN1240
1750 FORA=1TOR(E,R)
1760 IFR$(E,R,A)="CHEST"THEN1830
1770 NEXT
1780 FORA=1TOR(E,R)
1790 IFR$(E,R,A)="OPEN CHEST"THEN1820
1800 NEXT
1810 GOTO1040
1820 PRINT"THE CHEST IS OPEN NOW !":GOTO600
1830 FORB=1TOBE
1840 IFB$(B)="KEY"THEN1870
1850 NEXT
1860 PRINT"I HAVEN'T GOT ANY KEYS !":GOTO600
1870 B$(B)="":R$(E,R,A)="OPEN CHEST":PRINT"OK"
1880 GOTO5000
1890 IFT=1ANDR=4ANDE=2THEN5080
1895 IFR=4ANDE=2THEN1860
1900 GOTO1320
1910 CLS
5000 FORA=1TOR(E,R)
5010 IFR$(E,R,A)="DOG"THEN5040
5020 NEXT
5030 GOTO600
5040 PRINT"THE DOG HAD KILLED YOU !!!"
5050 INPUT"NEW GAME  ";E$
5060 IFLEFT$(E$,1)="Y"THENRUN
5070 END
5080 CLS
5090 PRINT:PRINT
5100 PRINT" YOU ARE IN THE GARDEN AND"
5110 PRINT
5120 PRINT" AN APLE FELL ON YOUR HEAD."
5130 PRINT
5140 PRINT" YOU ARE DEAD !!!"
5150 PRINT:PRINT
5160 INPUT"NEW GAME ";GE$
5170 IFLEFT$(GE$,1)="Y"THENRUN
5180 END
