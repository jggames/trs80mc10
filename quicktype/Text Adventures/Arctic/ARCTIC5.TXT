0 CLEAR2000:DIMCC,ZZ,M$,Z$,C1,C2:M=16384:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN

10 FE=3:DIMO$(43,3),R$(20,2),M$(42),O(50):GOSUB1370
30 R=20:FORA=1TOR:READR$(A,1),R$(A,2):NEXT
40 O=41:FORA=1TOO:READO$(A,1),O$(A,2),O$(A,3):NEXT
50 V1$="GETTAKGRAGO RUNCLIENTDROTHRINVWEADIGEXASEALOOHELUSEQUIENDEATOPEMELUNLSCOSAVLOASHOFIRSAYYELSHO"
51 V2$="001001001002002002002003003004005006007007007008009010010011012013014015016017018018019019019"
60 N1$="SHOCOANORSOUEASWESOUTUP IGLSMAFLOOCEWAT***CHELARDIVSUITWORADCAVDOWICEBRITRAPOSHOMESKCABSUPSIG"
61 N1$=N1$+"POLBEAFLARATFOODOOBOOWEIKEYHOLFIRBROMEDRUMHARSLEDOGGUNOCTWRESHIMUSYOH"
63 N2$="001002003004005006007008009009010011011012013013014014015015016017018018019019020021022022023"
64 N2$=N2$+"024024025026026027028028029030031032032033034035035036037038038039040"
65 NT$="001001003003003003003003003003003003003003001001001001001001003003001001003003003003003003003"
66 NT$=NT$+"003003001001001003001001001001003003002002001003003003003003003002002"
70 FORA=1TO42:READM$(A):NEXT
80 PRINT@482,"PRESS enter TO CONTINUE... Œ";
90 A$=INKEY$:IFA$<>CHR$(13)THEN90
100 DS=1
120 L=1:CLS
140 M$="WELCOME TO "+CHR$(34)+"ARCTIC ADVENTURE"+CHR$(34)+" BY HARRY MCCRACKEN":GOSUB1050
150 IFL=5ORL=9ORL=15ORL=16THEN155
151 GOTO160
155 IFF2<>1THENM$=M$(21):GOSUB1050:GOTO180
156 IFF5<>1THENM$=M$(22):GOSUB1050:L=5
160 IFL>1THENIFL<12THENIFF1=0THENM$=M$(6):GOSUB1050:GC=GC+1:IFGC>10THENM$=M$(7):GOSUB1050:GOTO180

170 PRINT"YOUR COMMAND-->Œ";:I$="":GOSUB210
180 M$="THE ADVENTURE IS OVER.":GOSUB1050:PRINT"DO YOU WANT TO PLAY AGAIN? Œ";
190 A$=INKEY$:IFA$=""THEN190
191 IFA$="N"THENCLS:END
192 IFA$="Y"THENRUN
193 GOTO190
210 A$=INKEY$:IFA$=""THEN210
220 IFASC(A$)<32THENIFASC(A$)<>8ANDASC(A$)<>13THEN210
230 IFA$=CHR$(8)THEN235
231 GOTO240
235 IFLEN(I$)>0THENPRINTCHR$(8)CHR$(8)"Œ";:I$=LEFT$(I$,LEN(I$)-1):GOTO210
236 GOTO210
240 IFA$=CHR$(13)THENPRINTCHR$(8):GOTO270
250 IFLEN(I$)<15THENPRINTCHR$(8);A$;"Œ";:I$=I$+A$:GOTO210
260 GOTO210

270 IFI$=""THENM$="WHAT?":GOSUB1050:GOTO150
275 I$=I$+" "
280 FORA=1TOLEN(I$):IFMID$(I$,A,1)=" "THENQ=A:A=255:NEXTA:GOTO290
281 NEXTA:Q=A
290 A=Q:V$=LEFT$(I$,3):N$=MID$(I$,A+1,3):FORA=1TOLEN(V1$)STEP3
300 IFMID$(V1$,A,3)=V$THEN310 
301 NEXT:M$="I DON'T KNOW HOW TO "+CHR$(34)+MID$(I$,1,Q-1)+"."+CHR$(34):GOSUB1050:GOTO150
310 IFN$=""THENI$=I$+"   ":N$="***"
320 V=VAL(MID$(V2$,A,3)):FORA=1TOLEN(N1$)STEP3
330 IFMID$(N1$,A,3)=N$THEN340
331 NEXT:I$=RIGHT$(I$,LEN(I$)-Q):IFV=19THEN350
332 M$="I DON'T KNOW WHAT "+CHR$(34)+LEFT$(I$,LEN(I$)-1)+CHR$(34)+" MEANS.":GOSUB1050:GOTO150
340 N=VAL(MID$(N2$,A,3)):IFN=36THENM$=M$(35):GOSUB1050:M$=M$(36):GOSUB1050:GOTO150
350 NT=VAL(MID$(NT$,A,3)):ONVGOTO360,400,460,510,550,570,590,650,660,670,680,690,710,730,750,760,770,920,990
360 GOSUB1060:IFNT=3THENM$="THAT ISN'T POSSIBLE.":GOSUB1050:GOTO150
361 GOSUB370:O$(N,2)="99":NO=NO+1:M$=M$(5):GOSUB1050:GOTO150
370 IFNO=5THENM$=M$(25):GOSUB1050:GOTO150
380 IF(L=11ANDVAL(O$(24,2))=11)OR(L=16ANDVAL(O$(37,2))=16)THENM$=M$(26):GOSUB1050:GOTO150
390 RETURN

400 FORA=1TOLEN(R$(L,2))/2STEP2:IFMID$(R$(L,2),A,2)=LEFT$(N$,2)THENC1=A:A=255:NEXTA:GOTO410
402 NEXTA:M$="YOU CAN'T GO THAT DIRECTION.":GOSUB1050:GOTO150
410 IFN=10THEN415
412 GOTO420
415 IFRND(10)=1THENM$=M$(12):GOSUB1050:GOTO180
416 M$=M$(13):GOSUB1050:IFFE=3THENFE=7:GOTO420
417 FE=3
420 IFL=6THENL=FE:O$(10,2)=STR$(FE):O$(11,2)=STR$(FE):GOSUB790:GOTO150
430 IF(VAL(O$(30,2))=0ANDL=17)AND(N=27ORN=30)THENM$=M$(30):GOSUB1050:GOTO150
440 IFN=19THENM$=M$(24):GOSUB1050
450 L5=L:L=VAL(MID$(R$(L,2),C1+LEN(R$(L,2))/2,2)):GOTO970

460 GOSUB1080:GOSUB470:O$(N,2)=STR$(L):NO=NO-1:M$=M$(5):GOSUB1050:GOTO150
470 IFN=2THENF1=0:GOTO480
471 IFN=14THENF2=0:GOTO480
472 IFN=28THENF5=0
480 IFL=11THENIFVAL(O$(24,2))=11THENIFN=26THENO$(26,2)="0":O$(24,2)="0":NO=NO-1:M$=M$(31):GOSUB1050:GOTO150
490 IFL=12ANDNT=2THENO$(N+2,2)="12":O$(N,2)="0":NO=NO-1:M$=M$(34):GOSUB1050:GOTO150
500 RETURN
510 NH=1:M$="YOU ARE HOLDING: ":FORA=1TOO:IFVAL(O$(A,2))=99THEN515
511 GOTO530
515 NH=0:M$=M$+O$(A,1):IF(F1=1ANDA=2)OR(F2=1ANDA=14)OR(F5=1ANDA=28)THENM$=M$+" (WEARING), ":GOTO530 
521 M$=M$+", "
530 NEXT:IFNH=1THENM$=M$+"NOTHING SPECIAL":GOTO540
531 M$=LEFT$(M$,LEN(M$)-2)
540 GOSUB1050:GOTO150
550 GOSUB1080:IFN<>2ANDN<>14ANDN<>28THENM$=M$(8):GOSUB1050:GOTO150
551 IFN=2THENF1=1:GOTO560
552 IFN=14THENF2=1:GOTO560
553 IFN=28THENF5=1
560 M$=M$(5):GOSUB1050:GOTO150
570 IFVAL(O$(1,2))<>99THENM$=M$(11):GOSUB1050:GOTO150
580 IFL<>2ORF3=1THENM$=M$(9):GOSUB1050:GOTO150
581 F3=1:O$(13,2)="2":M$=M$(10):GOSUB1050:GOTO150
590 GOSUB1100:GOSUB600:IFSFTHEN150
591 IFO$(N,3)=""THENM$="NOTHING SPECIAL.":GOSUB1050:GOTO150
592 M$=O$(N,3):GOSUB1050:GOTO150
600 SF=0:IFN=13ANDVAL(O$(14,2))=0THENO$(14,2)=STR$(L):O$(15,2)=STR$(L):M$=M$(10):GOSUB1050:SF=1:RETURN
610 IFN=9THENO$(18,2)="2":M$=M$(23):GOSUB1050:SF=1:RETURN
620 IFN=23THEN625
621 GOTO630
625 M$="THE INUIT COMMENTS:":GOSUB1050
626 M$=CHR$(34)+"DROP YOUR TREASURES HERE AND I'LL GIVE YOU SUPPLIES."+CHR$(34):GOSUB1050:SF=1:RETURN
630 IFN=2THENIFVAL(O$(26,2))=0THENO$(26,2)=STR$(L):M$=M$(10):GOSUB1050:SF=1
640 RETURN
650 M$="USE THE RADIO AND MAKE CONTACT WITH THE BASE.":GOSUB1050:GOTO150
660 GOSUB1080:IFN<>15THENM$="HUH?":GOSUB1050:GOTO150
661 M$=M$(14):GOSUB1050:M$=CHR$(34)+M$(RND(6)+14)+CHR$(34):GOSUB1050:GOTO150
670 GOTO180
680 GOSUB1080:IFN<>26THENM$=M$(27):GOSUB1050:GOTO150
681 O$(N,2)="0":M$=M$(28):GOSUB1050:GOTO150
690 GOSUB1100:IFSFTHEN150
691 IFN<>13ANDN<>27THENM$="HUH?":GOSUB1050:GOTO150
700 IFN=13THENM$=M$(29):GOSUB1050:GOTO150
701 IFVAL(O$(30,2))=0THENM$=M$(30):GOSUB1050:GOTO150
710 GOSUB1080:IFN<>18THENM$="HUH?":GOSUB1050:GOTO150
720 IFL<>13THENM$=M$(11):GOSUB1050:GOTO150
721 O$(29,2)="13":O$(18,2)="0":NO=NO-1:M$=M$(10):GOSUB1050:M$=M$(32):GOSUB1050:GOTO150
730 GOSUB1100:IFSFTHEN150
731 IFO$(29,2)<>"99"THENM$=M$(11):GOSUB1050:GOTO150
740 IFVAL(O$(30,2))<>0THENM$=M$(29):GOSUB1050:GOTO150
741 O$(30,2)="17":O$(27,2)="0":M$=M$(5):GOSUB1050:GOTO150
750 M$="THIS ADVENTURE HAS NO SCORE.":GOSUB1050:M$="THE OBJECT IS TO GET BACK TO YOUR BASE.":GOSUB1050:GOTO150
760 GOSUB900:FORA=1TO43:O(A)=VAL(O$(A,2)):IFO$(A,2)=""THENO(A)=-1
761 NEXT:O(44)=L:O(45)=F1:O(46)=F2:O(47)=F3:O(48)=FE:CSAVE*O,"ARCDATA"
762 M$="DONE.":GOSUB1050:GOTO150
770 GOSUB900:M$="LOADING...":GOSUB1050:CLOAD*O,"ARCDATA"
771 FORA=1TO43:O$(A,2)=STR$(O(A)):IFO(A)=-1THENO$(A,2)=""
772 NEXT:L=O(44):F1=O(45):F2=O(46):F3=O(47):FE=O(48):M$="DONE.":GOSUB1050:GOTO150
780 GOTO150

790 PRINT@.,;:FORC2=1TO7:PRINT:NEXT:PRINT"--------------------------------";
791 PRINT@.,;:M$="YOU ARE "+R$(L,1)+". OBVIOUS EXITS: "
800 FORC2=1TOLEN(R$(L,2))/2STEP2:D$=MID$(R$(L,2),C2,2):IFD$="WE"THENM$=M$+"WEST, "
810 IFD$="NO"THENM$=M$+"NORTH, ":GOTO830
811 IFD$="SO"THENM$=M$+"SOUTH, ":GOTO830
812 IFD$="EA"THENM$=M$+"EAST, ":GOTO830
820 IFD$="OU"THENM$=M$+"OUT, ":GOTO830
821 IFD$="UP"THENM$=M$+"UP, ":GOTO830
822 IFD$="DO"THENM$=M$+"DOWN, "
830 NEXT:M$=LEFT$(M$,LEN(M$)-2):GOSUB1
840 NH=1
841 PRINT@64,;:M$="YOU SEE: ":FORC2=1TOO:IFVAL(O$(C2,2))=LTHENNH=0:M$=M$+O$(C2,1)+", "
850 NEXT:IFNH=1THENM$=M$+"NOTHING SPECIAL":GOTO860
851 M$=LEFT$(M$,LEN(M$)-2)
860 GOSUB1:PRINT@480,;:RETURN

900 PRINT"READY TAPE. PRESS ENTER... Œ";
910 A$=INKEY$:IFA$<>CHR$(13)THEN910
911 PRINTCHR$(8):RETURN
920 GOSUB1080:GOSUB950:IFN=34THEN960
930 M$="BANG!":GOSUB1050:IFL<>2THENM$="NOTHING HAPPENS.":GOSUB1050:GOTO150
931 GOSUB940:O$(28,2)="2":M$=M$(37):GOSUB1050:GOTO150
940 IFVAL(O$(28, 2))<>0THENM$="NOTHING HAPPENS.":GOSUB1050:GOTO150
941 RETURN
950 IFN<>34ANDN<>25THENM$="YOU CAN'T FIRE THAT!":GOSUB1050:GOTO150
951 RETURN
960 M$="WHOOSH!":GOSUB1050:IFL=11THENM$=M$(38):GOSUB1050:GOTO150
961 IFL=16THENO$(37,2)="0":M$=M$(39):GOSUB1050:GOTO150

970 IFL=19ANDVAL(O$(35,2))=0THENM$="YOU CAN'T GO THAT WAY.":GOSUB1050:GOTO150
980 IFL=16ANDL5=15THENM$=M$(40):GOSUB1050:GOTO180
981 M$="OKAY.":GOSUB1050:GOTO150

990 IFLEFT$(I$,3)="SAY"THENI$=MID$(I$,5):GOTO992
991 I$="!@#$&! "
992 M$=CHR$(34)+LEFT$(I$,LEN(I$)-1)+CHR$(34):GOSUB1050:IFN<>39THEN150
1000 IFN=40THENM$="WHO DO YOU THINK YOU ARE, SCOTT ADAMS?":GOSUB1050:GOTO150
1010 IFN=39THENIFL<>19THENM$="NOTHING HAPPENS.":GOSUB1050:GOTO150
1020 M$=M$(41):GOSUB1050:L=20:M$=M$(42):GOSUB1050:GOTO180

1050 PRINT@480,;:GOSUB1:GOSUB790:RETURN
1060 IFVAL(O$(N, 2))<>LTHENM$="THAT ISN'T HERE.":GOSUB1050:GOTO150
1070 RETURN
1080 IFVAL(O$(N,2))<>99THENM$="YOU DON'T HAVE THAT.":GOSUB1050:GOTO150
1090 RETURN

1100 SF=0:IFVAL(O$(N,2))<>99ANDVAL(O$(N,2))<>LTHENSF=1:M$="THAT ISN'T HERE.":GOSUB1050:REM *!*
1110 RETURN

1130 DATA IN AN IGLOO,OU02,ON A SNOWY PLAIN,WESOIG030401,BY AN OCEAN,OCWAFLEA05050602,NEAR A SNOW-COVERED CAVE
1131 DATA NOCA0208,IN THE OCEAN,DOEA0903,ON AN ICE FLOE,WE01,ON THE BANK OF AN OCEAN,WEFL1006
1140 DATA IN THE NECK OF A CAVE,OUNO0411,ON THE FLOOR OF THE OCEAN,UPEA0515,IN AN INUIT VILLAGE,EATRPOESHO0712121313
1150 DATA IN A CAVE,SODO0814,IN A TRADING POST,OUSL1019,IN AN INUIT'S HOME,OU10,IN A DOWNWARD TUNNEL
1160 DATA UPDO1117,IN THE OCEAN AT A WRECKED SHIP,WESHWR091616,ON THE DECK OF A WRECKED SHIP,SOHA1518
1161 DATA AT THE BOTTOM OF A TUNNEL,UPHODO141818,IN A WRECKED SHIP,UPSO1716
1170 DATA IN A DOG SLED,WE12,AT THE BASE,NOEASOWE01020304

1180 DATA SHOVEL,1,,WARM COAT,1,,NORTH,,,SOUTH,,,EAST,,,WEST,,,OUT,,,UP,,,SMALL IGLOO,2,
1181 DATA ICE FLOE,3,IT'S IN THE OCEAN,OCEAN,3,,,,,LARGE CHEST,0,,DIVING SUIT,0,YOUR SIZE,RADIO,0,
1190 DATA CAVE,4,,DOWN,,,ICE BRICK,0,,TRADING POST,10,,INUIT HOME,10,,INUIT,12,,CABINETS FULL OF SUPPLIES
1191 DATA 12,,SIGN,12,"THE SIGN READS: WE TRADE TREASURES FOR SUPPLIES.",POLAR BEAR,11,,FLARE GUN,11,
1200 DATA RATIONS,0,,DOOR IN FLOOR,17,IT'S LOCKED,WEIGHTED BOOTS,0,, KEY,0,,HOLE IN FLOOR,0,,COZY FIRE
1210 DATA 13,,*BRONZE MEDALLION*,18,,*ANCIENT RUM*,16,,HARPOON GUN,0,,DOGSLED,0,,,,
1211 DATA ANGRY OCTOPUS,16,,WRECKED SHIP,15,
1220 DATA MUSH,0,,YOHO,0,,HATCH,16,

1280 DATA IT'S EMBEDDED IN THE ICE.,THAT ISN'T HERE.,YOU DON'T HAVE THAT.,THAT'S IMPOSSIBLE!,OKAY.
1281 DATA IT'S VERY COLD HERE!,YOU DIE OF EXPOSURE...,YOU CAN'T WEAR THAT., YOU FIND NOTHING.,YOU FIND SOMETHING.
1290 DATA YOU'RE MISSING SOMETHING.,THE ICE FLOE BREAKS! YOU FALL THROUGH TO YOUR DEATH.,THE ICE FLOW FLOATS...
1291 DATA A CRACKLING MESSAGE FROM THE BASE COMES THROUGH...
1300 DATA EXAMINE EVERYTHING CLOSELY!,SAY MUSH TO GET TO THE BASE.,SHOOT A FLARE IF THERE'S AN EMERGENCY.
1301 DATA WILD ANIMALS ARE *VERY* HUNGRY!,WEIGHT YOURSELF DOWN.,COME BACK HERE WHEN YOU'RE DONE.
1310 DATA YOU CAN'T BREATHE! YOU DROWN., YOU FLOAT TO THE SURFACE..., AN ICE-BRICK CAME LOOSE.
1311 DATA THE INUIT SAYS HOWDY!,YOUR ARMS ARE FULL,THE BEAST WON'T LET YOU.
1320 DATA YECHH!,"COULD USE A LITTLE SALT, BUT NOT BAD...",IT'S ALREADY OPEN.,IT'S LOCKED.
1321 DATA THE BEAR GOBBLES IT AND WALKS OFF.,THE FIRE MELTS IT.
1330 DATA THERE'S NOTHING HERE TO UNLOCK.,THE INUIT TAKES IT AND BRINGS YOU SOMETHING.
1340 DATA THERE'S MORE THAN ONE GUN IN THIS GAME. SAY SOMETHING LIKE, SHOOT FLARE GUN OR GET HARPOON GUN.
1341 DATA A PLANE FLIES BY AND DROPS SOMETHING.
1350 DATA IT DOESN'T BOTHER THE CREATURE A BIT.,THE CREATURE MOANS IN PAIN AND SLITHERS AWAY TO SUFFER.
1360 DATA THE OCTOPUS SQUEEZES YOU TO DEATH!,THE DOGS PULL YOUR SLED ACROSS MANY KILOMETRES...
1361 DATA TO THE BASE! YOU'VE SOLVED THE ADVENTURE!

1370 CLS:PRINTTAB(8);:M$="ARCTIC ADVENTURE":GOSUB7:PRINT:PRINTTAB(7);:M$="BY HARRY MCCRACKEN":GOSUB7:PRINT
1371 M$="   I WILL DESCRIBE YOUR SURROUNDINGS. "
1372 M$=M$+"TELL ME WHAT YOU WANT TO DO WITH MOSTLY TWO-WORD COMMANDS SUCH AS "
1373 M$=M$+CHR$(34)+"GET BRICK"+CHR$(34)+" AND "+CHR$(34)+"MELT ICE."+CHR$(34):GOSUB1
1374 M$="   THERE ARE ALSO SOME ONE-WORD COMMANDS, SUCH AS "+CHR$(34)+"INVENTORY"+CHR$(34)
1381 M$=M$+" AND "+CHR$(34)+"HELP."+CHR$(34):GOSUB1
1390 M$="   USE "+CHR$(34)+"SAVE"+CHR$(34)+" AND "+CHR$(34)+"LOAD"+CHR$(34)+" TO STORE GAME PROGRESS TO TAPE."
1391 GOSUB1:M$="   REMEMBER TO EXAMINE THINGS, AND TO THINK LOGICALLY.":GOSUB1:PRINTTAB(11)"GOOD LUCK!"
1410 RETURN
1420 REM MC-10 EDITS JIM GERRIE
