0 CLEAR800:DIMCC,ZZ,M$,Z$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

10 FE=3:DIMO$(43,3),R$(20,2),M$(42):GOSUB1370
30 R=20:FORA=1TOR:READR$(A,1),R$(A,2):NEXT
40 O=41:FORA=1TOO:READO$(A,1),O$(A,2),O$(A,3):NEXT
50 V1$="GETTAKGRAGO RUNCLIENTDROTHRINVWEADIGEXASEALOOHELUSEQUIENDEATOPEMELUNLSCOSAVLOASHOFIRSAYYELSHO"
51 V2$="001001001002002002002003003004005006007007007008009010010011012013014015016017018018019019019"
60 N1$="SHOCOANORSOUEASWESOUTUP IGLSMAFLOOCEWAT***CHELARDIVSUITWORADCAVDOWICEBRITRAPOSHOMESKCABSUPSIG"
61 N1$=N1$+"POLBEAFLARATFOODOOBOOWEIKEYHOLFIRBROMEDRUMHARSLEDOGGUNOCTWRESHIMUSYOH"
63 N2$="001002003004005006007008009009010011011012013013014014015015016017018018019019020021022022023"
64 N2$=N2$+"024024025026026027028028029030031032032033034035035036037038038039040"
65 NT$="001001003003003003003003003003003003003003001001001001001001003003001001003003003003003003003"
66 NT$=NT$+"003003001001001003001001001001003003002002001003003003003003003002002"
70 M=42:FORA=1TOM:READM$(A):NEXT
80 PRINT@485,"PRESS enter TO CONTINUE.";
90 A$=INKEY$:IFA$<>CHR$(13)THEN90
100 DS=1:GOTO120
110 REM 
120 L=1:CLS
130 REM GOSUB790
140 PRINT@480,"WELCOME TO "CHR$(34)"ARCTIC ADVENTURE"CHR$(34);:GOSUB1050:PRINT"BY HARRY MCCRACKEN";:GOSUB1050
150 IFL=5ORL=9ORL=15ORL=16THEN155
151 GOTO160
155 IFF2<>1THENPRINTM$(21);:GOTO180
156 IFF5<>1THENPRINTM$(22);:GOSUB1050:L=5
160 IFL>1THENIFL<12THENIFF1=0THENPRINTM$(6);:GOSUB1050:GC=GC+1:IFGC>10THENPRINTM$(7);:GOSUB1050:GOTO180

170 PRINT"YOUR COMMAND-->";CHR$(207);:I$="":GOSUB210
180 GOSUB1050:PRINT"THE ADVENTURE IS OVER.";:GOSUB1050:PRINT "DO YOU WANT TO PLAY AGAIN?";
190 A$=INKEY$:IFA$=""THEN190
191 IFA$="N"THENEND
192 IFA$="Y"THENRUN
193 GOTO190
210 A$=INKEY$:IFA$=""THEN210
220 IFASC(A$)<32THENIFASC(A$)<>8ANDASC(A$)<>13THEN210
230 IFA$=CHR$(8)THEN235
231 GOTO240
235 IFLEN(I$)>0THENPRINTCHR$(8)CHR$(8)CHR$(207);:I$=LEFT$(I$,LEN(I$)-1):GOTO210
236 GOTO210
240 IFA$=CHR$(13)THENPRINTCHR$(8);:GOTO270
250 IFLEN(I$)<15THENPRINTCHR$(8);A$;CHR$(207);:I$=I$+A$:GOTO210
260 GOTO210

270 GOSUB1050:IFI$=""THENPRINT"WHAT?";:GOSUB1050:GOTO150
275 I$=I$+" "
280 FORA=1TOLEN(I$):IFMID$(I$,A,1)=" "THEN290
281 NEXT
290 Q=A:V$=LEFT$(I$,3):N$=MID$(I$,A+1,3):FORA=1TOLEN(V1$)STEP3
300 IFMID$(V1$,A,3)=V$THEN310 
301 NEXT:M$="I DON'T KNOW HOW TO "+CHR$(34)+LEFT$(I$,Q)+"."+CHR$(34):GOSUB1:GOSUB1050:GOTO150
310 IFN$=""THENI$=I$+"   ":N$="***"
320 V=VAL(MID$(V2$, A, 3)) : FOR A=1 TO LEN(N1$) STEP 3
330 IFMID$(N1$,A,3)=N$THEN340
331 NEXT:PRINT"I DON'T KNOW WHAT ";CHR$(34);RIGHT$(I$,LEN(I$)-Q);CHR$(8);CHR$(34);" MEANS.";:GOSUB1050:GOTO150
340 N=VAL(MID$(N2$,A,3)):IFN=36THENPRINTM$(35);:GOSUB1050:PRINTM$(36);:GOSUB1050:GOTO150
350 NT=VAL(MID$(NT$,A,3)):ONVGOTO360,400,460,510,550,570,590,650,660,670,680,690,710,730,750,760,770,920,990
360 GOSUB1060:IFNT=3THENPRINT"THAT ISN'T POSSIBLE.";:GOSUB1050:GOTO150
361 GOSUB370:O$(N,2)="99":NO=NO+1:PRINTM$(5);:GOSUB1050:GOTO150:REM GOSUB790
370 IFNO=5THENPRINTM$(25);:GOSUB1050:GOTO150
380 IF(L=11ANDVAL(O$(24,2))=11)OR(L=16ANDVAL(O$(37,2))=16)THENPRINTM$(26);:GOSUB1050:GOTO150
390 RETURN
400 FORA=1TOLEN(R$(L,2))/2STEP2:IFMID$(R$(L,2),A,2)=LEFT$(N$,2)THEN410
401 NEXT:PRINT"YOU CAN'T GO THAT DIRECTION.";:GOSUB1050:GOTO150
410 IFN=10THEN415
411 GOTO420
415 IFRND(10)=1THENPRINTM$(12);:GOSUB1050:GOTO180
416 PRINTM$(13);:GOSUB1050:IFFE=3THENFE=7:GOTO420
417 FE=3
420 IF L=6 THEN L=FE : O$(10, 2)=STR$(FE) : O$(11, 2)=STR$(FE) : GOSUB 790 : GOTO 150
430 IF (VAL(O$(30, 2))=0 AND L=17) AND (N=27 OR N=30) THEN PRINTM$(30); :GOSUB1050: GOTO 150
440 IF N=19 THEN PRINTM$(24); : GOSUB 1050
450 L5=L:L=VAL(MID$(R$(L,2),A+LEN(R$(L,2))/2,2)):GOTO970
460 GOSUB1080:GOSUB470:O$(N,2)=STR$(L):PRINTM$(5);:NO=NO-1:GOSUB1050:GOTO150
470 IFN=2THENF1=0:GOTO480
471 IFN=14THENF2=0:GOTO480
472 IFN=28THENF5=0
480 IFL=11THENIFVAL(O$(24,2))=11THENIFN=26THENPRINTM$(31);:GOSUB1050:O$(26,2)="0":O$(24,2)="0":NO=NO-1:GOTO150
490 IFL=12ANDNT=2THENPRINTM$(34);:O$(N+2,2)="12":O$(N,2)="0":GOSUB1050:NO=NO-1:GOTO150
500 RETURN
510 NH=1:M$="YOU ARE HOLDING: ":FORA=1TOO:IFVAL(O$(A,2))=99THEN515
511 GOTO530
515 NH=0:M$=M$+O$(A, 1):IF(F1=1ANDA=2)OR(F2=1ANDA=14)OR(F5=1ANDA=28)THENM$=M$+"WHICH YOU ARE WEARING, ":GOTO530 
521 M$=M$+", "
530 NEXT:IFNH=1THENPRINTM$=M$+"NOTHING SPECIAL.":GOSUB1:GOTO540
531 M$=LEFT$(M$,LEN(M$)-2):GOSUB1
540 GOSUB1050:GOTO150
550 GOSUB1080:IFN<>2ANDN<>14ANDN<>28THENPRINTM$(8);:GOSUB1050:GOTO150
551 IFN=2THENF1=1:GOTO560
552 IFN=14THENF2=1:GOTO560
553 IFN=28THENF5=1
560 PRINTM$(5);:GOSUB1050:GOTO150
570 IFVAL(O$(1, 2))<>99THENPRINTM$(11);:GOSUB1050:GOTO150
580 IFL<>2ORF3=1THENPRINTM$(9);:GOSUB1050:GOTO150
581 F3=1:PRINTM$(10);:O$(13,2)="2":GOSUB1050:GOTO150
590 GOSUB1100:GOSUB600:IFO$(N,3)=""THENPRINT "NOTHING SPECIAL.";:GOSUB1050:GOTO150
591 PRINTO$(N,3);:GOSUB1050:GOTO150
600 IF N=13 AND VAL(O$(14, 2))=0 THEN PRINTM$(10); :GOSUB1050: O$(14, 2)=STR$(L) : O$(15, 2)=STR$(L) : GOSUB 790 : RETURN
610 IF N=9 THEN PRINTM$(23); :GOSUB1050: O$(18, 2)="2" : GOSUB 790 : RETURN
620 IF N=23 THEN625
621 GOTO630
625 PRINT"THE ESKIMO COMMENTS:";:GOSUB1050:
626 PRINTCHR$(34);"DROP YOUR TREASURES HERE AND I'LL GIVE YOU SUPPLIES.";CHR$(34);:GOSUB1050:RETURN
630 IF N=2 THEN IF VAL(O$(26, 2))=0 THEN PRINTM$(10); :GOSUB1050: O$(26, 2)=STR$(L) : GOSUB 790 : GOTO 150
640 RETURN
650 PRINT "USE THE RADIO AND MAKE CONTACT WITH THE BASE."; :GOSUB1050: GOTO 150
660 GOSUB1080:IFN<>15THENPRINT"HUH?";:GOSUB1050:GOTO150
661 PRINTM$(14);:GOSUB1050:PRINTCHR$(34)M$(RND(6)+14);CHR$(34);:GOSUB1050:GOTO150
670 GOTO 180
680 GOSUB1080:IFN<>26THENPRINTM$(27);:GOSUB1050:GOTO150
681 PRINTM$(28);:GOSUB1050:O$(N,2)="0":GOTO150
690 GOSUB 1100 : IF N <> 13 AND N <> 27 THEN PRINT "HUH?"; :GOSUB1050: GOTO 150
700 IFN=13THENPRINTM$(29);:GOSUB1050:GOTO150
701 IFVAL(O$(30,2))=0THENPRINTM$(30);:GOSUB1050:GOTO150
710 GOSUB 1080 : IF N <> 18 THEN PRINT "HUH?"; :GOSUB1050: GOTO 150
720 IFL<>13THENPRINTM$(11);:GOSUB1050:GOTO150
721 PRINTM$(10);:GOSUB1050:PRINTM$(32);:O$(29,2)="13":O$(18,2)="0":NO=NO-1:GOSUB1050:GOTO150
730 GOSUB 1100 : IF O$(29, 2) <> "99" THEN PRINTM$(11); :GOSUB1050: GOTO 150
740 IFVAL(O$(30, 2))<>0THENPRINTM$(29);:GOSUB1050:GOTO150
741 PRINTM$(5);:O$(30,2)="17":O$(27,2)="0":GOSUB1050:GOTO150
750 PRINT "THIS ADVENTURE HAS NO SCORE.";:GOSUB1050:PRINT"THE OBJECT IS TO GET BACK TO YOUR BASE.";:GOSUB1050:GOTO150
760 GOSUB900:FORA=1TOO:PRINT#-1,O$(A,2);:NEXT:PRINT#-1,L,F1,F2,F3,FE:PRINT "DONE.";:GOSUB1050:GOTO150
770 GOSUB900:FORA=1TOO:INPUT#-1,O$(A,2);:NEXT:INPUT#-1,L,F1,F2,F3,FE:PRINT "DONE.";:GOSUB1050:GOTO150
780 GOTO150

790 PRINT@0,;:M$="YOU ARE "+R$(L,1)+". OBVIOUS EXITS: "
800 FORA=1TOLEN(R$(L,2))/2STEP2:D$=MID$(R$(L,2),A,2):IFD$="WE"THENM$=M$+"WEST, "
810 IFD$="NO"THENM$=M$+"NORTH, ":GOTO830
811 IFD$="SO"THENM$=M$+"SOUTH, ":GOTO830
812 IFD$="EA"THENM$=M$+"EAST, ":GOTO830
820 IFD$="OU"THENM$=M$+"OUT, ":GOTO830
821 IFD$="UP"THENM$=M$+"UP, ":GOTO830
822 IFD$="DO"THENM$=M$+"DOWN, "
830 NEXT:M$=LEFT$(M$,LEN(M$)-2):GOSUB1
840 NH=1:FORA=1TO5:PRINTA:NEXT:PRINT"--------------------------------";
841 PRINT@96,;:M$="YOU SEE: ":FORA=1TOO:IFVAL(O$(A,2))=LTHENGOSUB870
850 NEXT:IFNH=1THENM$=M$+"NOTHING SPECIAL":GOTO860
851 M$=LEFT$(M$,LEN(M$)-2)
860 GOSUB1:PRINT@480,;:RETURN

870 NH=0:M$=M$+O$(A,1)+", ":RETURN
900 PRINT "READY TAPE. PRESS ENTER...";
910 A$=INKEY$:IFA$<>CHR$(13)THEN920
911 GOSUB1050:RETURN
920 GOSUB 1080 : GOSUB 950 : IF N=34 THEN960
930 PRINT"BANG!";:GOSUB1050:IFL<>2THENPRINT"NOTHING HAPPENS.";:GOSUB1050:GOTO150
931 GOSUB940:PRINTM$(37);:O$(28,2)="2":GOSUB1050:GOTO150
940 IFVAL(O$(28, 2))<>0THENPRINT"NOTHING HAPPENS.";:GOSUB1050:GOTO150
941 RETURN
950 IFN<>34ANDN<>25THENPRINT "YOU CAN'T FIRE THAT!";:GOSUB1050:GOTO150
951 RETURN
960 PRINT"WHOOSH!";:GOSUB1050:IFL=11THENPRINTM$(38);:GOSUB1050:GOTO150
961 IFL=16THENPRINTM$(39);:O$(37,2)="0":GOSUB1050:GOTO150
970 IFL=19ANDVAL(O$(35,2))=0THENPRINT"YOU CAN'T GO THAT WAY.";:GOSUB1050:GOTO150
980 IFL=16ANDL5=15THENPRINTM$(40);:GOSUB1050:GOTO180
981 GOSUB790:GOTO150
990 PRINT CHR$(34); RIGHT$(I$, LEN(I$) - Q); CHR$(8); CHR$(34); : GOSUB 1050
1000 IF N=40 THEN PRINT "WHO DO YOU THINK YOU ARE, SCOTT ADAMS?"; :GOSUB1050: GOTO 150
1010 IF N=39 THEN IF L <> 19 THEN PRINT "NOTHING HAPPENS."; :GOSUB1050: GOTO 150
1020 PRINTM$(41); :GOSUB1050: L=20 : GOSUB 790 : PRINTM$(42); : GOTO 180
1030 GOTO 150
1040 GOTO 1040
1050 PRINT:GOSUB790:RETURN
1060 IF VAL(O$(N, 2))<>LTHENPRINT"THAT ISN'T HERE.";:GOSUB1050:GOTO 150
1070 RETURN
1080 IF VAL(O$(N, 2)) <> 99 THEN PRINT "YOU DON'T HAVE THAT."; :GOSUB1050: GOTO 150
1090 RETURN
1100 IF VAL(O$(N, 2)) <> 99 AND VAL(O$(N, 2)) <> L THEN PRINT "THAT ISN'T HERE."; :GOSUB1050: GOTO 150
1110 RETURN

1130 DATA "IN AN IGLOO", OU02, ON A SNOWY PLAIN, WESOIG030401, BY AN OCEAN, OCWAFLEA05050602, NEAR A SNOW-COVERED CAVE
1131 DATA NOCA0208, IN THE OCEAN, DOEA0903, ON AN ICE FLOE, WE01, ON THE BANK OF AN OCEAN, WEFL1006
1140 DATA IN THE NECK OF A CAVE, OUNO0411, ON THE FLOOR OF THE OCEAN, UPEA0515, IN AN ESKIMO VILLAGE, EATRPOESHO0712121313
1150 DATA IN A CAVE, SODO0814, IN A TRADING POST, OUSL1019, IN AN ESKIMO'S HOME, OU10, IN A DOWNWARD TUNNEL
1160 DATA UPDO1117, IN THE OCEAN AT A WRECKED SHIP, WESHWR091616, ON THE DECK OF A WRECKED SHIP, SOHA1518
1161 DATA AT THE BOTTOM OF A TUNNEL, UPHODO141818, IN A WRECKED SHIP, UPSO1716
1170 DATA IN A DOG SLED, WE12, AT THE BASE, NOEASOWE01020304
1180 DATA SHOVEL, 1, , WARM COAT, 1, , NORTH, , , SOUTH, , , EAST, , , WEST, , , OUT, , , UP, , , SMALL IGLOO, 2, 
1181 DATA ICE FLOE, 3, IT'S IN THE OCEAN, OCEAN, 3, , , , , LARGE CHEST, 0, , DIVING SUIT, 0, YOUR SIZE, RADIO, 0,
1190 DATA CAVE, 4, , DOWN, , , ICE BRICK, 0, , TRADING POST, 10, , ESKIMO HOME, 10, , ESKIMO, 12, , CABINETS FULL OF SUPPLIES
1191 DATA 12, , SIGN, 12,"THE SIGN READS: WE TRADE TREASURES FOR SUPPLIES.", POLAR BEAR, 11, , FLARE GUN, 11,
1200 DATA RATIONS, 0, , DOOR IN FLOOR, 17, IT'S LOCKED, WEIGHTED BOOTS, 0, , KEY, 0, , HOLE IN FLOOR, 0, , COZY FIRE
1210 DATA 13, , *BRONZE MEDALLION*, 18, , *ANCIENT RUM*, 16, , HARPOON GUN, 0, , DOGSLED, 0, , , , 
1211 DATA ANGRY OCTOPUS, 16, , WRECKED SHIP, 15,
1220 DATA MUSH, 0, , YOHO, 0, , HATCH, 16,

1280 DATA IT'S EMBEDDED IN THE ICE., THAT ISN'T HERE., YOU DON'T HAVE THAT., THAT'S IMPOSSIBLE!, OKAY.
1281 DATA IT'S VERY COLD HERE!, YOU DIE OF EXPOSURE..., YOU CAN'T WEAR THAT., YOU FIND NOTHING., YOU FIND SOMETHING.
1290 DATA YOU'RE MISSING SOMETHING., THE ICE FLOE BREAKS! YOU FALL THROUGH TO YOUR DEATH., THE ICE FLOW FLOATS...
1291 DATA A CRACKLING MESSAGE FROM THE BASE COMES THROUGH...
1300 DATA EXAMINE EVERYTHING CLOSELY!, SAY MUSH TO GET TO THE BASE., SHOOT A FLARE IF THERE'S AN EMERGENCY.
1301 DATA WILD ANIMALS ARE *VERY* HUNGRY!, WEIGHT YOURSELF DOWN., COME BACK HERE WHEN YOU'RE DONE.
1310 DATA YOU CAN'T BREATHE! YOU DROWN., YOU FLOAT TO THE SURFACE..., AN ICE-BRICK CAME LOOSE.
1311 DATA THE ESKIMO SAYS HOWDY!, YOUR ARMS ARE FULL, THE BEAST WON'T LET YOU.
1320 DATA YECHH!, "COULD USE A LITTLE SALT, BUT NOT BAD...", IT'S ALREADY OPEN., IT'S LOCKED.
1321 DATA THE BEAR GOBBLES IT AND WALKS OFF., THE FIRE MELTS IT.
1330 DATA THERE'S NOTHING HERE TO UNLOCK., THE ESKIMO TAKES IT AND BRINGS YOU SOMETHING.
1340 DATA THERE'S MORE THAN ONE GUN IN THIS GAME. SAY SOMETHING LIKE, SHOOT FLARE GUN OR GET HARPOON GUN.
1341 DATA A PLANE FLIES BY AND DROPS SOMETHING.
1350 DATA IT DOESN'T BOTHER THE CREATURE A BIT., THE CREATURE MOANS IN PAIN AND SLITHERS AWAY TO SUFFER.
1360 DATA THE OCTOPUS SQUEEZES YOU TO DEATH!, THE DOGS PULL YOUR SLED ACROSS MANY MILES...
1361 DATA TO THE BASE! YOU'VE SOLVED THE ADVENTURE!

1370 CLS:PRINT"ARCTIC ADVENTURE":PRINT"----------------":PRINT"BY HARRY MCCRACKEN"
1371 M$="I WILL DESCRIBE YOUR SURROUNDINGS AT THE TOP OF THE SCREEN.":GOSUB1
1372 M$="TELL ME WHAT YOU WANT TO DO WITH MOSTLY TWO-WORD COMMANDS SUCH AS ":
1373 M$=M$+CHR$(34)+"GET BRICK"+CHR$(34)+", "+CHR$(34)+"INVENTORY"+CHR$(34)
1381 M$=M$+", "+CHR$(34)+"HELP"+CHR$(34)+", AND "+CHR$(34)+"MELT ICE"+CHR$(34)+".":GOSUB1
1390 PRINT"    USE " CHR$(34)"SAVE" CHR$(34)" AND " CHR$(34)"LOAD" CHR$(34);
1391 PRINT" TO STORE AND RECOVER A GAME IN        PROGRESS TO TAPE."
1400 PRINT"    REMEMBER TO EXAMINE THINGS, AND TO THINK LOGICALLY.";
1401 PRINT"         GOOD LUCK ON YOUR ADVENTURE!"
1410 RETURN
