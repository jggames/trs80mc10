0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:MC=16384:GOTO15
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
15 P=448:W=160:W$="WHAT SHOULD HE DO?":H=500:H$="HIT '/' KEY":GOTO60
20 IFINKEY$<>"/"THEN20
21 CLS:RETURN
50 FORT=1TO2500:NEXT:RETURN
60 POKE49151,64:PRINT@201,;:M$="ASSIGNMENT 45":GOSUB7:GOSUB50:CLS
70 POKE49151,0:PRINT@356,"A HARRY FLYNN ADVENTURE":GOSUB50:CLS
90 PRINT@298,"YEAR: 2154":GOSUB50
100 PRINT@353,"PLACE: ABODE OF HARRY A. FLYNN          SPECIAL AGENT             UNITED EARTH COMMAND (UEC)"
110 GOSUB50:GOSUB50
120 PRINT@106,"TIME: 5:31 AM":GOSUB50:CLS
130 FORB=1TO6
140 PRINT@200,"PRIORITY MESSAGE"
150 FORT=1TO250:NEXT:CLS:FORC=1TO75:NEXT:SOUND100,1:NEXT:FORY=1TO500:NEXT
160 CLS:PRINT@198,"FOR COMMANDER FLYNN":GOSUB50:CLS
180 PRINT@452,;:M$="PRESS ANY KEY WHEN READY":GOSUB7:PRINT@484,;:M$="TO RECEIVE COMMUNICATION":GOSUB7:PRINT
190 IFINKEY$=""THEN190
191 GOTO200
200 CLS:M$="GOOD MORNING HARRY! THIS IS SECTOR CHIEF CHRONOS.":GOSUB1
240 PRINT:M$="THERE'S BEEN SOME TROUBLE IN THE SIRAN SYSTEM.":GOSUB1
250 PRINT:M$="ABAR CALLEASE, RULER OF SIRA, HAS KEPT PEACE WITH EARTH OVER THE LAST SEVEN YEARS.":GOSUB1
251 M$="NOW HIS DAUGHTER, KURVI CALLEASE, AGE 19, HAS BEEN KIDNAPPED.":GOSUB1
260 M$="HIS POLITICAL ENEMIES ARE BLAMING EARTH AND DEMAND WAR. CALLEASE HAS ASKED FOR HELP.":GOSUB1
270 PRINT@H,H$;
280 GOSUB20
290 PRINT@P,;
291 M$="YOUR MISSION IS TO FIND THE GIRL AND RETURN HER BEFORE IT'S TOO LATE. MAJOR ORR WILL GIVE YOU THE DETAILS.":GOSUB1
300 PRINT@H,H$;
310 GOSUB20
440 PRINT@288,;:M$="HELLO COMMANDER. THIS IS MAJOR ORR.":GOSUB1:GOSUB50:CLS
460 PRINT:M$="YOU'LL BE USING A MAKO JET SLED FOR SURFACE TRAVEL. IT'S ALREADY BEEN LOADED ABOARD YOUR SHIP.":GOSUB1
480 M$="THE MAKO IS VERY FAST, HAS TWIN LASER CANNON, SHIELD GENERATOR, AND FORWARD SCANNERS."::GOSUB1
481 M$="THE ENGINES HAVE BEEN CALIBRATED TO BURN THE AIR ON ZAGAR.":GOSUB1
485 PRINT@H,H$;
486 GOSUB20
500 M$="THAT'S WHERE THE PRINCESS IS, OR, MORE PRECISELY, WHERE SHE WAS WHEN HER PERSONAL BEACON STOPPED TRANSMITTING.":GOSUB1
520 M$="YOU'LL ALSO WEAR A MEAD CELL (MATTER ENERGY AMPLFICATION DIODE) ON YOUR WRISTBAND.":GOSUB1
530 M$="IT BRINGS THINGS INTO THE VISIBLE SPECTRUM THAT NORMALLY COULDN'T BE SEEN-- A KIND OF SUPER MICROSCOPE.":GOSUB1
531 M$="IT CAN ALSO PICK UP YOUR THOUGHT WAVES AND MAY BE DIRECTED INTO A SMALL FORCE BEAM.":GOSUB1
540 PRINT@H,H$;
550 GOSUB20
560 PRINT@352,"THAT'S ALL COMMANDER. GOOD LUCK.":GOSUB50
570 FORN=1TO4:PRINT@P,"PRIORITY TRANSMISSION COMPLETED":FORT=1TO500:NEXT:CLS:FORC=1TO75:NEXT:SOUND90,1:NEXT
571 GOSUB50:CLS
580 POKE49151,64:PRINT@288,;
581 M$="THE TAKE-OFF AND TRANSIT THROUGH THE STAR-GATE WERE ROUTINE. BEFORE LONG ZAGAR APPEARED ON THE NAV SCREEN.":GOSUB1
590 M$="HARRY LANDED AT SOME DISTANCE FROM THE LOCATION WHERE THE BEACON WAS LAST DECTECTED.":GOSUB1
591 M$="AS HE LEFT THE SHIP HE SAW A YELLOW, DESERT LANDSCAPE WITH A RED SKY.":GOSUB1
600 PRINT@H,H$;
610 GOSUB20
620 M$="THE CARGO DOOR OPENED DEPOSITING THE MAKO ON THE SANDY GROUND.":GOSUB1
621 M$="HARRY GOT IN. THE ENGINES WHINED, THEN SCREAMED AS THE SLED LIFTED A METER OFF THE LAND AND SHOT FORWARD.":GOSUB1
625 PRINT@H,H$;:GOSUB20
630 PRINT@P,;:M$="AFTER 30 SECONDS THE SCANNER PICKED UP A CLUSTER OF HUTS... AND A POWER FIELD.":GOSUB1
650 PRINT:M$="SECONDS LATER THE FIELD WAS CONFIRMED AS AN ELECTRONIC BARRIER SURROUNDING THE HUTS.":GOSUB1
655 PRINT@H,H$;:GOSUB20
670 POKE49151,.:PRINT@W,W$
680 M$="1) ATTEMPT TO GAIN ACCESS TO THE HUTS":GOSUB1
700 M$="2) HIDE THE SLED AND HIKE UP TO BARRIER UNNOTICED":GOSUB1
710 M$="3) USING SLED'S SHIELDS BREAK THROUGH BARRIER":GOSUB1
720 M$="4) FIRE SLED'S LASERS INTO FIELD":GOSUB1
730 M$="5) USE MEAD CELL":GOSUB1
740 M$="6) CALL UEC FOR ASSISTANCE":GOSUB1
750 M$="7) SCOUT AROUND SOME MORE IN THE SLED BEFORE DOING ANYTHING":GOSUB1
760 M$="(ENTER THE NUMBER OF THE ACTION YOU SELECT)":GOSUB1:INPUTN
765 CLS
770 IFN<1ORN>7THEN760
780 ONNGOTO790,810,1040,1300,1060,1130,1220
790 PRINT@P,;
791 M$="SEEMS HARRY FORGOT ABOUT THE BARRIER. HE WALKED RIGHT INTO IT AND WAS IMMEDIATELY STUNNED.":GOSUB1:PRINT@H,H$;
795 IFINKEY$<>"/"THEN795
796 CLS
800 PRINT@P,;
801 M$="HE HAS SET OFF AN ALARM. ARMOURED SENTRY ROBOTS DRAG HIM AWAY. HE HAS FAILED HIS MISSION!":GOSUB1:PRINT@H,H$;
805 IFINKEY$<>"/"THEN805
806 GOTO2540
810 PRINT@P,;
811 M$="HARRY HIDES THE SLED BEHIND SOME ROCKS AND HIKES TO THE BARRIER.":GOSUB1:PRINT@H,H$;:GOSUB20
820 PRINT@W,W$
840 M$="1) ATTEMPT TO GAIN ACCESS TO HUTS":GOSUB1
850 M$="2) USE MEAD CELL":GOSUB1
860 M$="3) USE HAND LASER":GOSUB1
870 INPUTN:CLS
880 IFN<1ORN>3THEN820
890 ONNGOTO790,920,900
900 PRINT@P,;:M$="HARRY DRAWS HIS HAND LASER AND BLASTS THE BARRIER.":GOSUB1
910 M$="IT HAS A NEGLIGIBLE EFFECT BUT SETS OFF AN ALARM THAT BRINGS ARMOURED SENTRY ROBOTS.":GOSUB1
911 M$="THE HAND LASER CANNOT STOP THEM. HARRY GETS VAPORIZED!":GOSUB1:PRINT@H,H$;
915 IFINKEY$<>"/"THEN915
916 GOTO2540
920 PRINT@P,;:M$="AS HE APPROACHES THE BARRIER HARRY HOLDS UP THE MEAD CELL.":GOSUB1
921 M$="HE STUDIES THE COMPOSITION OF THE FORMERLY INVISIBLE FIELD. IT MAY BREAK DOWN UNDER LASER FIRE.":GOSUB1:PRINT@H,H$;
925 IFINKEY$<>"/"THEN925
926 CLS
930 PRINT@W,W$
940 M$="1) RETURN TO SLED":GOSUB1
950 M$="2) CONTINUE TO EXPLORE":GOSUB1
960 M$="3) USE HAND LASER":GOSUB1
970 INPUTN:CLS
980 IFN<1ORN>3THEN930
990 ONNGOTO1000,1020,900
1000 PRINT@P,;:M$="HARRY GETS BACK INTO THE SLED.":GOSUB1:PRINT@H,H$;:GOSUB20
1010 GOTO670
1020 PRINT@P,;:M$="AS HE LOOKS AROUND, HARRY IS PICKED UP ON SECURITY SENSORS.":GOSUB1:PRINT@H,H$;:GOSUB20
1030 GOTO800
1040 PRINT@P,;:M$="HARRY SETS THE SLED'S SHIELDS AT MAXMUM INTENSITY AND CHARGES INTO THE BARRIER!":GOSUB1
1050 M$="UNFORTUNATELY HE UNDERESTIMATED THE STRENGTH OF THE FORCE FIELD.":GOSUB1
1051 M$="HARRY IS KNOCKED OUT AS THE SLED CRASHES INTO THE BARRIER.":GOSUB1:PRINT@H,H$;
1055 IFINKEY$<>"/"THEN1055
1056 CLS:GOTO800
1060 PRINT@P,;
1061 M$="HARRY DECIDES TO USE HIS MEAD CELL TO EXAMINE THE BARRIER. BUT HE HAS TO GET CLOSER FOR THAT.":GOSUB1
1062 PRINT@H,H$;:GOSUB20
1070 PRINT@W,W$
1080 M$="1) ATTEMPT TO GAIN ACCESS TO THE HUTS":GOSUB1
1090 M$="2) HIDE THE SLED AND HIKE UP TO BARRIER UNNOTICED":GOSUB1
1100 M$="3) USING SLED'S SHIELDS BREAK THROUGH BARRIER":GOSUB1
1110 INPUTN:CLS:IFN<1ORN>3THEN1070
1120 ONNGOTO790,810,1040
1130 PRINT@P,;
1131 M$="HARRY DECIDES TO CALL UEC FOR ASSISTANCE. AFTER HEARING THE PROBLEM, "
1132 M$=M$+"UEC SAYS IT WILL PRESENT THE PROBLEM TO COMPU-CEN AND GET BACK TO HIM WITHIN THE HOUR.":GOSUB1
1140 PRINT:M$="AFTER AN HOUR HARRY STILL HAS NOT RECEIVED A REPLY.":GOSUB1:PRINT@H,H$;:GOSUB20
1150 PRINT@W,W$
1160 PRINT"1) CONTINUE WAITING"
1170 PRINT"2) ACT ON HIS OWN"
1180 INPUTN:CLS:IFN<1ORN>2THEN1150
1190 ONNGOTO1200,1210
1200 PRINT@P,;:M$="WHILE WAITING HARRY DECIDES TO EXPLORE ON FOOT.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1020
1210 PRINT@P,;:M$="HARRY FIGURES THAT HE HAS WAITED LONG ENOUGH FOR THE WHIZ KIDS AT COMPU-CEN "
1211 M$=M$+"TO COME UP WITH SOMETHING.":GOSUB1:PRINTH,H$;
1215 IFINKEY$<>"/"THEN1215
1216 CLS:GOTO670
1220 PRINT@P,;:M$="HARRY DRIVES ON UNTIL DARKNESS APPROACHES BUT FINDS NOTHING.":GOSUB1:PRINT@H,H$;:GOSUB20
1230 PRINT@W,W$
1240 PRINT"1) RETURN TO HUTS"
1250 PRINT"2) KEEP LOOKING"
1260 INPUTN:CLS:IFN<1ORN>2THEN1230
1270 ONNGOTO1280,1290
1280 PRINT@P,;:M$="HARRY ARRIVES BACK AT THE HUTS.":GOSUB1:GOTO670
1290 PRINT@P,;
1291 M$="HARRY DRIVES ON IN THE DARKNESS. SUDDENLY HIS SCANNER DETECTS A MAMMOTH FORM DECENDING FROM THE SKY. "
1292 M$=M$+"ATTRACTED BY THE LIGHTS, A GIANT MANTA MOTH SWALLOWS THE SLED.":GOSUB1:PRINT@H,H$;
1295 IFINKEY$<>"/"THEN1295
1296 CLS:GOTO1375
1300 PRINT@P,;
1301 M$="HARRY FIRES THE POWERFUL TWIN LASER CANNONS POINT BLANK INTO THE BARRIER. "
1302 M$=M$+"THE BARRIER HAS BEEN WEAKENED SUBSTANTIALL, BUT SENTRY ROBOTS OPEN FIRE ON HIM.":GOSUB1:PRINT@H,H$;:GOSUB20
1310 PRINT@W,W$
1320 PRINT"1) TURN ON SHIELDS"
1330 PRINT"2) USE LASER CANNONS"
1340 PRINT"3) GET OUT OF THERE"
1350 INPUTN:CLS:IFN<1ORN>3THEN1310
1360 ONNGOTO1380,1370,1370
1370 PRINT@P,;:M$="THE SKY IS FILLED WITH RED LASER BOLTS. THE SLED IS HIT.":GOSUB1:PRINT@H,H$;:GOSUB20
1375 CLS:PRINT@400,"GOOD BYE HARRY!!":PRINT@H,H$;
1376 IFINKEY$<>"/"THEN1376
1377 CLS:GOTO2540
1380 POKE49151,64:PRINT@P,;:M$="LASER BOLTS FLASH AGAINST THE SLED'S SHIELDS.":GOSUB1:PRINT@H,H$;
1381 IFINKEY$<>"/"THEN1381
1382 CLS
1390 POKE49151,.:PRINT@W,W$
1400 PRINT"1) GET OUT OF THERE"
1410 PRINT"2) RAM THE BARRIER"
1420 INPUTN:CLS:IFN<1ORN>2THEN1390
1430 ONNGOTO1440,1450
1440 PRINT@P,;:M$="HARRY ESCAPES BUT HAS FAILED HIS MISSION. HE IS EXECUTED FOR COWARDICE!":GOSUB1:PRINT@H,H$;
1441 IFINKEY$<>"/"THEN1441
1442 CLS:GOTO1375
1450 PRINT@P,;
1451 M$="WITH FULL POWER TO FRONT SHIELDS, HARRY CHARGES THE WEAKENED BARRIER.":GOSUB1
1452 GOSUB50:PRINT:M$="HE BREAKS THROUGH AND ZOOMS PAST THE SLOW MOVING ROBOTS.":GOSUB1
1460 M$="HARRY QUICKLY HIDES THE SLED AMONG THE CLUSTER OF HUTS.":GOSUB1
1470 M$="WITH LASER GUN DRAWN, HE CHARGES INTO THE NEAREST STRUCTURE. "
1471 M$=M$+"IT TURNS OUT TO BE AN ELEVATOR THAT LUNGES DOWNWARD.":GOSUB1:PRINT@H,H$;:GOSUB20
1480 PRINT@P,;
1481 M$="HE IS IN A HUGE COMPLEX OF SILVER GLASS PANES. EACH PANE IS SCORED INTO 5 CM SQUARES, "
1482 M$=M$+"AND EACH SQUARE IS PERFORATED BY A SMALL HOLE.":GOSUB1:PRINT@H,H$;:GOSUB20
1490 PRINT@W,W$
1500 PRINT"1) EXPLORE MORE"
1510 PRINT"2) EXAMINE PANE"
1520 INPUTN:CLS:IFN<1ORN>2THEN1490
1530 ONNGOTO1540,1610
1540 PRINT@P,;
1541 M$="HARRY GOES BACK UP THE ELEVATOR. ONCE OUTSIDE HE GOES INTO ANOTHER HUT, BUT FINDS NOTHING.":GOSUB1
1542 PRINT@H,H$;:GOSUB20
1550 PRINT@W,W$
1560 PRINT"1) CONTINUE EXPLORING"
1570 M$="2) GO BACK TO GLASS PANE COMPLEX":GOSUB1
1580 INPUTN:CLS:IFN<1ORN>2THEN1550
1590 ONNGOTO1600,1480
1600 PRINT@P,;:M$="HE RUNS FOR ANOTHER HUT, BUT THIS TIME HE MEETS THE ROBOTS INSTEAD.":GOSUB1:PRINT@H,H$;
1601 IFINKEY$<>"/"THEN1601
1602 GOTO1375
1610 PRINT@P,;
1611 M$="AS HE TOUCHES THE PANE, HE IS STUNNED UNCONSCIOUS. WHEN HE AWAKENS, HE IS SURROUNDED BY A SECURITY TEAM. "
1612 M$=M$+"A MAN WITH GOLD TEETH IS SMILING. THE MAN SPEAKS.":GOSUB1:PRINT@H,H$;:GOSUB20
1620 PRINT@P,;:M$="'THE GREAT HARRY FLYNN. I NEVER THOUGHT I'D MEET YOU IN PERSON.'":GOSUB1:PRINT@H,H$;:GOSUB20
1630 PRINT@P,;:M$="(STANDING UP, HARRY RETORTS) 'THE INFAMOUS DR. NON FROM KRUEL.'":GOSUB1:PRINT@H,H$;:GOSUB20
1640 PRINT@P,;:M$="'YES, KRUEL. KIDNAPING, REVOLT, USURPATION, EXTORTION LEAGUE. "
1641 M$=M$+"YOU, MR. FLYNN, HAVE INVADED OUR HOSTAGE STORAGE CENTER.'":GOSUB1:PRINT@H,H$;:GOSUB20
1650 PRINT@P,;:M$="(HARRY PROBES) 'TELL ME MORE.'":GOSUB1:PRINT@H,H$;:GOSUB20
1660 M$="'OF COURSE. EACH HOSTAGE IS KEPT FOR FUTURE POLITICAL USE. "
1661 M$=M$+"THEY ARE DATA PATTERNED AND LASER TAMPED ON THOSE SLIDES YOU CARELESSLY TOUCHED. "
1662 M$=M$+"INSERT A PELLET LIKE THIS, AND THEY ARE RECONSTRUCTED--UNHARMED.":GOSUB1
1670 PRINT:M$="'WITHOUT A PELLET, A NASTY SHOCK IS APPLIED. YOU MAY HAVE THIS ONE AS A SOUVENIR.' "
1671 M$=M$+"(DR. NON SLIPS A PELLET INTO HARRY'S SHIRT POCKET)":GOSUB1
1675 M$="'TOO BAD YOU MUST LEAVE US NOW, MR. FLYNN... PERMANENTLY.'":GOSUB1:PRINT@H,H$;:GOSUB20
1680 PRINT@P,;
1681 M$="HARRY IS TAKEN TO THE SOLAR MIRROR. ONE OF THE GUARDS EXPLAINS THAT DR. NON "
1682 M$=M$+"RESERVES THIS FORM OF EXECUTION OF SPECIAL GUESTS.":GOSUB1
1690 M$="HARRY IS STRAPPED TO THE MIRROR. AT SUNRISE HE WILL BE INSTANTLY TOASTED. "
1691 M$=M$+"THE GUARDS LEAVE.":GOSUB1:PRINT@H,H$;:GOSUB20
1700 PRINT@W,W$
1710 PRINT"1) BREAK BONDS"
1720 PRINT"2) ROTATE MIRROR FROM SUN"
1730 PRINT"3) USE MEAD CELL"
1740 PRINT"4) PRAY"
1750 INPUTN:CLS:IFN<1ORN>4THEN1700
1760 ONNGOTO1770,1810,1850,1840
1770 PRINT@P,;:M$="THE BONDS LOOSEN BUT DO NOT BREAK. SHOULD HARRY: 1) KEEP TRYING 2) TRY SOMETHING ELSE.":GOSUB1
1780 INPUTN:CLS:ONNGOTO1790,1800
1790 PRINT@P,;:M$="HARRY CAN'T LOOSEN THEM ENOUGH BEFORE SUNRISE.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1375
1800 GOTO1700
1810 PRINT@P,;:M$="MIRROR BUDGES A LITTLE AS BONDS STRETCH. SHOULD HARRY: 1) KEEP TRYING 2) TRY SOMETHING ELSE.":GOSUB1
1820 INPUTN:CLS:ONNGOTO1830,1700
1830 PRINT@P,;:M$="THE MIRROR DOESN'T TURN FAR ENOUGH.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1375
1840 PRINT@P,;:M$="AS HE PRAYS, HE GETS THE IDEA OF USING THE MEAD CELL.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1700
1850 PRINT@P,;:M$="HARRY REMEMBERS WHAT ORR SAID ABOUT THE AMPLIFYING THOUGHT WAVES. "
1851 M$=M$+"HE CONCENTRATES ON MOVING THE MIRROR'S POSITION CONTROL LEVER.":GOSUB1:PRINT
1860 M$="NOTHING HAPPENS. SHOULD HARRY: 1) KEEP TRYING 2) TRY SOMETHING ELSE.":GOSUB1
1870 INPUTN:CLS:ONNGOTO1890,1880
1880 GOTO1700
1890 PRINT@P,;:M$="FINALLY THE LEVER MOVES AND ROTATES THE MIRROR AWAY FROM THE SUN. "
1891 M$=M$+"THIS GIVES HARRY THE TIME HE NEEDS TO BREAK FREE.":GOSUB1:PRINT@H,H$;:GOSUB20
1900 PRINT@W,W$
1910 M$="1) SNEAK BACK TO HOSTAGE STORAGE AND USE PELLET":GOSUB1
1920 M$="2) ESCAPE AND RETURN WITH REINFORCEMENTS":GOSUB1
1930 M$="3) CAPTURE DR. NON AND USE HIM TO BARGAIN FOR THE GIRL AND FREEDOM":GOSUB1
1940 M$="4) FAKE HIS DEATH BY TURNING MIRROR BACK INTO THE SUN":GOSUB1
1950 M$="5) USE MIRROR AS WEAPON":GOSUB1
1960 INPUTN:CLS:IFN<1ORN>5THEN1900
1970 ONNGOTO1980,1990,2000,2010,2050
1980 PRINT@P,;:M$="AS HARRY SNEAKS BACK HE IS SURPRISED BY A SECURITY TEAM.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1375
1990 PRINT@P,;:M$="HARRY MAKES IT OUT OF THE COMPLEX, BUT WITHOUT THE SLED. "
1991 M$=M$+"HE PERISHES IN THE DESERT.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2540
2000 PRINT@P,;:M$="FOR THIS PLAN HARRY NEEDS A WEAPON. HE SNEAKS BACK TO THE SLED AND REMOVES A LASER CANNON. "
2002 M$=M$+"HE THEN CONFRONTS DR. NON AND A SECURITY TEAM. UNFORTUNATELY, HARRY'S LASER FAILS TO OPERATE.":GOSUB1
2003 PRINT@H,H$;:GOSUB20:GOTO1375
2010 PRINT@P,;:M$="NO ONE WILL BE LOOKING FOR HIM NOW. SHOULD HE: 1) ESCAPE 2) TRY TO CAPTURE DR. NON "
2011 M$=M$+"3) GET THE GIRL":GOSUB1
2020 INPUTN:CLS:ONNGOTO2030,2040,2030
2030 PRINT@P,;:M$="HARRY DASHES THROUGH THE CLUSTER OF HUTS, BUT STEPS ON A SNIC ANTI-PERSONNEL MINE.":GOSUB1
2031 PRINT@H,H$;:GOSUB20:GOTO1375
2040 GOTO2000
2050 PRINT@P,;:M$="HARRY TURNS THE MIRROR ON THE INSTALLATION AND DESTROYS IT. "
2051 M$=M$+"DR. NON, HOWEVER, ESCAPES IN HIS PERSONAL SHUTTLE. SHOULD HARRY: 1) GO AFTER DR. NON 2) GET THE GIRL":GOSUB1
2052 INPUTN:CLS:ONNGOTO 2060,2070
2060 PRINT@P,;:M$="WITH HIS HEAD START, DR. NON EASILY ESCAPES PURSUIT. "
2061 M$=M$+"HARRY WASTED VALUABLE HOURS. BY THE TIME HE RESCUES THE GIRL, WAR HAS BROKEN OUT WITH SIRA.":GOSUB1
2062 PRINT"HARRY FAILED.":PRINT@H,H$;:GOSUB20:GOTO2540
2070 PRINT@P,;:M$="HARRY MAKES HIS WAY TO THE HOSTAGE STORAGE AREA. BY NOW DR. NON HAS RADIOED FOR HELP. "
2071 M$=M$+"KRUEL REINFORCEMENTS ARE ON THEIR WAY.":GOSUB1:PRINT@H,H$;:GOSUB20
2080 PRINT@W,W$
2090 M$="1) STUDY THE STORAGE SYSTEM":GOSUB1
2100 M$="2) FREE EVERYONE BY DISABLING LOCK MECHANISM":GOSUB1
2110 M$="3) EXAMINE THE FILES":GOSUB1
2120 M$="4) CALL UEC ON SLED RADIO":GOSUB1
2130 M$="5) GET MECHANISM TO PRODUCE MORE PELLET KEYS":GOSUB1
2140 INPUTN:CLS:IFN<1ORN>5THEN2080
2150 ONNGOTO2160,2170,2220,2230,2270
2160 PRINT@P,"THE SYSTEM HAS 3 PARTS:"
2161 PRINT" FILES":PRINT" PELLET PRODUCER":PRINT" LOCKING MECHANISM":PRINT@H,H$;:GOSUB20:GOTO2080
2170 PRINT@P,;:M$="TAMPERING WITH THE LOCK STARTED A BASE DESTRUCTION SEQUENCE. "
2171 M$=M$+"HARRY HAS 60 SECONDS TO DECIDE WHETHER TO: 1) ABANDON THE BASE 2) TRY TO ABORT SEQUENCE":GOSUB1
2180 INPUTN:CLS:IFN<1ORN>2THEN2170
2190 ONNGOTO2200,2210
2200 PRINT@P,;:M$="HARRY LIVES, BUT THE MISSION IS LOST.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2540
2210 PRINT@P,;:M$="HE ALMOST HAD IT, THEN *BOOM!!*":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1375
2220 PRINT@P,;:M$="THERE ARE MANY FILES. SINCE THE GIRL WAS JUST TAKEN, SHE SHOULD BE AMONG THE LAST ENTRIES. "
2221 M$=M$+"BUT HARRY HAS ONLY 1 PELLET. WHICH FILE IS THE RIGHT ONE?":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2280
2230 PRINT@P,;:M$="HARRY FINDS THE SLED RADIO BROKEN. SHOULD HE: 1) TRY TO FIX IT  2) GO BACK TO STORAGE AREA":GOSUB1
2240 INPUTN:CLS:ONNGOTO2250,2260
2250 PRINT@P,;:M$="HARRY GETS IT TO RECEIVE, BUT HE CAN'T TRANSMIT.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2080
2260 GOTO2080
2270 PRINT@P,;:M$="BY ATTEMPTING TO BYPASS THE PELLET TIMER, HARRY SETS OFF A SIGNAL WHICH ERASES ALL OF THE FILES! "
2271 M$=M$+"MISSION IS FAILURE":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2540
2280 PRINT@W,W$
2290 M$="1) LOAD THE FILES UP AND BRING THEM TO UEC":GOSUB1
2300 M$="2) DISABLE LOCK MECHANISM":GOSUB1
2310 M$="3) TAKE A GUESS":GOSUB1
2320 M$="4) GET MECHANISM TO PRODUCE MORE PELLETS":GOSUB1
2330 M$="5) CUT OFF POWER TO STORAGE COMPUTER":GOSUB1
2340 M$="6) GO BACK TO SHIP AND USE RADIO":GOSUB1
2350 INPUTN:CLS:IFN<1ORN>6THEN2280
2360 ONNGOTO2370,2170,2380,2270,2390,2430
2370 PRINT@P,;:M$="WHEN HARRY ATTEMPTS TO REMOVE A PANE FROM ITS HOLDER, THE FILE DISSOLVES. "
2371 M$=M$+"THE GIRL AND ALL OTHER HOSTAGES ON THE PANE ARE DESTROYED. MISSION FAILED.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO2540
2380 PRINT@P,;:M$="BAD ODDS! HARRY'S PELLET FREES A HUNGRY GORGON WOLF MAN.":GOSUB1:PRINT@H,H$;:GOSUB20:GOTO1375
2390 PRINT@P,;:M$="WITH THE POWER OFF THE SYSTEM BECOMES TOTALLY INOPERATIVE. "
2391 M$=M$+"SHOULD HARRY: 1) TURN POWER BACK ON  2) EXAM SYSTEM WITH POWER OFF":GOSUB1
2400 INPUTN:CLS:ONNGOTO2410,2420
2410 GOTO2280
2420 PRINT@P,;:M$="A SECONDARY POWER CIRCUIT SNAPS ON WHILE HARRY I STUDYING CONNECTIONS. HE IS ELECTROCUTED!":GOSUB1
2421 PRINT@H,H$;:GOSUB20:GOTO1375
2430 PRINT@P,;:M$="HARRY DRIVES BACK TO THE SHIP AND CALLS UEC. COMPU-CEN SUGGESTS TAKING MICRO-PHOTOS USING "
2431 M$=M$+"THE MEAD CELL AND ANALYZING THEM ON THE SHIP'S COMPUTER.":GOSUB1
2440 M$="IN ADDITION, UEC IS SENDING A TASK FORCE TO SECURE THE BASE.":GOSUB1:PRINT@H,H$;:GOSUB20
2450 PRINT@P,;:M$="HARRY RETURNS TO THE STORAGE AREA, AND FOLLOWING COMPU-CEN'S ADVICE IS ABLE "
2451 M$=M$+"TO IDENTIFY THE GIRL'S FILE.":GOSUB1
2460 M$="HE SLIPS THE PELLET IN AND A HOLOGRAPHIC IMAGE APPEARS. IS THIS KURVI CALLEASE. "
2461 M$=M$+"SHE IS ALMOST MAGICALLY BEAUTIFUL. THE IMAGE FILLS AND SHE COMES TO LIFE, BUT IMMEDIATELY FAINTS.":GOSUB1
2462 PRINT@H,H$;:GOSUB20
2470 POKE49151,64:PRINT@P,;:M$="HARRY CATCHES HER AND CARRIES HER TO THE SLED. THEY SPEED OVER THE YELLOW SANDS TO "
2471 M$=M$+"HIS SHIP. ABOVE, THE SKY FLASHES WITH RED AND BLUE LASER STROBES AS UEC AND KRUEL FORCES BATTLE IT OUT.":GOSUB1
2480 M$="THE GIRL AWAKENS. A TEAR IN HER EYE REFLECTS THE FLASHES OVERHEAD.":GOSUB1:PRINT@H,H$;:GOSUB20
2490 POKE49151,0:PRINT@P,"'WHO ARE YOU?'":PRINT@H,H$;:GOSUB20
2500 PRINT@P,;:M$="'HARRY FLYNN FROM UEC AT YOUR SERVICE.'":GOSUB1:PRINT@H,H$;:GOSUB20
2510 PRINT@P,;:M$="SHE PRESSED CLOSE TO HIM, HER HEAD ON HIS SHOULDER. 'TAKE ME HOME, PLEASE, HARRY.'":GOSUB1
2511 PRINT@H,H$;:GOSUB20
2520 PRINT@P,"'YOU'RE AS GOOD AS THERE.'":PRINT@H,H$;:GOSUB20
2530 PRINT@104,;:M$="CONGRATULATIONS,":GOSUB7:PRINT@133,;:M$="MISSION ACCOMPLISHED!!":GOSUB7:FORT=1TO3000:NEXT:END
2540 CLS:PRINT@387,"WANT TO TRY AGAIN (Y/N)";
2550 INPUTD$:IFD$="Y"THEN580
2560 IFD$="N"THEN2580
2570 IFD$<>"Y"ORD$<>"N"THENCLS:PRINT@464,"ANSWER ONLY WITH A Y OR N";:GOTO2550
2580 CLS:PRINT@448,;:M$="YOU BETTER GO BACK TO UEC OFFICER ACADEMY FOR A REFRESHER COURSE":GOSUB1:FORT=1TO3000:NEXT:END
