0 GOSUB500:CLEAR350:DIMG,Z,M$,I$,D(6),S(4),T(4),W(8),A$(20),A(20,9),M$(12),M(12,15),C$(31),V(26),FD(3),TD(3):GOTO154
1 Z=1:G=32:FORG=GTOZSTEP-1:I$=MID$(M$,G,1):IFI$<"!"THENPRINTMID$(M$,Z,G-Z):Z=G+1:G=Z+32:IFI$=""THENG=.
2 G=G+(G>255)*(G-255):NEXT:M$="":RETURN
3 GOSUB1:GOTO140
4 GOSUB1:GOTO116
5 INPUT"hit enter";A$:RETURN
7 MM=PEEK(173)+256*PEEK(174):RETURN
8 POKE174,INT(MM/256):POKE173,MM-256*PEEK(174)
9 FORG=1TOZ3:READA$:NEXT:RETURN
10 Z1=31
11 RESTORE:FORG=1TOZ1:READM$:NEXT:RETURN
12 DATANORTH,SOUTH,EAST,WEST,UP,DOWN,GET,TAKE,DROP,LOOK,EXAMINE,ATTACK,FLEE,GIVE,INVENTORY
13 DATABLAST,HEAL,POWER,SPEED,SMILE,WAVE,SAY,READ,READY,LIGHT,OPEN,PUT,DRINK,STATS,ENTER,TROLLSFIRE
14 Z2=20
15 GOSUB10:FORG=1TOZ2:READM$:FORC1=1TO8:READZ:NEXT:NEXT:RETURN
16 DATADIAMONDS,200,1,3,3,,,,,AXE,25,2,4,,,1,1,6,BOTTLE,50,6,2,5,100,6,2,,SWORD,25,2,4,,-10,5,1,8
20 DATAGOLD COINS,50,,2,8,,,,,RING,150,1,5,,,,,,MACE,40,2,4,,10,3,1,4,SPICES,100,1,3,18,,,,0
24 DATABOOK,250,7,3,19,7,2,1,,TROLLSFIRE,100,3,3,,25,5,1,10,JEWELS,1000,1,5,26,,,,0
27 DATACHEST,,1,300,14,,,,,EMPTY BOTTLE,1,1,1,,,,,,RUBY,250,1,1,37,,,,,IVORY SWORD,200,3,5,11,15,5,1,8
31 DATALONG BOW,35,3,5,,10,2,1,8,OLD STAFF,30,3,3,-1,10,3,1,4,BROADSWORD,90,3,11,,5,5,2,5,BROKEN BOAT,,,999,26,,,,
35 DATACLUB,1,3,5,,10,3,1,4
36 Z3=35
37 GOSUB14:GOSUB7:GOTO9
38 DATA"LOOKING S AT THE ENTRANCE OF A CAVE OVER WHICH IS WRITTEN 'BEGINNERS ONLY.' TO THE N THE ROAD LEADS BACK TO TOWN."
39 DATA"IN A DARK N/S TUNNEL. YOU SEE A FLICKERING LIGHT S.","IN A SMALL COLD SIDE CHAMBER. THE ONLY LIGHT COMES FROM THE E."
41 DATA"IN A LARGE N/S CHAMBER. BURNING TORCHES LINE THE WALLS. SMALL CHAMBERS ARE E & W."
42 DATA"IN A SMALL COLD SIDE CHAMBER. THE ONLY LIGHT COMES FROM THE W.","IN A SMALL, STARK CELL WITH A DOOR TO THE E."
44 DATA"AT THE N END OF A LONG N/S HALL. BOLTED DOORS ARE E & W."
45 DATA"IN A SMALL, STARK CELL WITH A DOOR TO THE W.","IN A SMALL, STARK CELL WITH A DOOR E."
47 DATA"IN THE MIDDLE OF THE LONG N/S HALL. BOLTED DOORS E & W."
48 DATA"IN A SMALL, STARK CELL WITH A DOOR W.","IN A SMALL, STARK CELL WITH A DOOR E."
50 DATA"AT THE S END OF THE LONG N/S HALL. SHOCKINGLY THERE ARE DOORS E & W. A BROKEN PASSAGE GOES S."
51 DATA"IN A STARK CELL WITH A DOOR W.","IN A N/S TUNNEL. THE SIDES ARE ROUGH & BROKEN. YOU SEE TORCHLIGHT S."
53 DATA"IN A SECRET E/W PASSAGE. YOU HEAR A FAINT MELODY TO THE E."
54 DATA"IN AN E/W PASSAGE. RELIGIOUS MUSIC & INCENSE WAFT FROM THE E."
55 DATA"IN A TEMPLE. PICTURES OF HEROES COVER THE WALLS. THERE ARE TWO ALTARS, ONE GOLD, ONE BLOODY. THE ONLY EXIT IS W."
56 DATA"IN WHAT WAS ONCE A LIBRARY. SCRAPS OF PAPER COVER THE FLOOR. A BROKEN DOOR E IS THE ONLY EXIT."
57 DATA"IN A T-INTERSECTION WITH TORCHES. TUNNELS LEAD N & E. A BROKEN DOOR IS W."
58 DATA"IN A DARK E/W TUNNEL. YOU CAN SEE TORCHLIGHT BOTH DIRECTIONS."
59 DATA"AT THE TOP OF SOME STAIRS WITH TORCHES EITHER SIDE. IT LOOKS DARK DOWN THERE. A TUNNEL HEADS W."
60 DATA"AT THE BOTTOM OF SOME STAIRS. LIGHT CAN BE SEEN AT THE TOP. A DIM LIGHT CAN BE SEEN E."
61 DATA"IN AN E/W TUNNEL WITH LIGHT AT EACH END.","IN AN E/W TUNNEL. YOU SEE LIGHT E. A SALTY BREEZE COMES FROM THAT WAY."
63 DATA"IN A SMALL BAY SURROUNDED BY HIGH CLIFFS. THE ONLY EXIT IS BACK W. TO THE E IS THE SEA."
64 DATA"IT SLOWLY COMES ALIVE AND TWO TENTACLES COME OUT OF THE SIDE, HOLDING YOU FAST. ANOTHER TENTACLE FROM INSIDE ATTACKS!"
65 DATA"A HUMAN FIGURE YOU CAN'T MAKE OUT... BRIGHT GREEN FLAME SURROUNDS THE BLADE OF HIS SWORD."
66 DATA"THE FLAME ON HIS SWORD DIES ALSO.","FIRE DANCES FROM THE SWORD'S BLADE."
68 DATA"'TROLLSFIRE' IS WRITTEN ON THE SWORD'S BLADE."
69 DATA"YOU RECEIVE SOME BURN DAMAGE UNTIL THE SWORD (HEARING YOUR CRIES OF PAIN?) EXTINGUISHES ITS FLAME."
70 DATA"'MAGICAL BOAT REPAIR' BY J. GERRIE (WARNING: BOAT NOT INCLUDED)","YOU REPAIR THE BOAT.","THE BOOK BITES YOUR HEAD OFF."
73 Z4=12
74 GOSUB36:FORG=1TOZ4:READM$:FORC1=1TO12:READZ:NEXT:NEXT:RETURN
75 DATABLACK RAT,2,10,,10,3,5,,,,30,1,1,BROWN RAT,2,10,,10,3,5,,,,30,1,1,TAN RAT,2,10,,10,3,5,,,,30,1,1
78 DATAHERMIT,13,22,50,75,5,75,,,2,55,1,6,GORILLA,12,15,,60,8,200,,,,45,1,4,HEINRICH,15,5,65,100,6,100,,1,4,12,3,8
81 DATAMIMIC,20,20,,100,,300,,,,40,1,3,PRIEST,10,6,,100,18,75,,1,7,25,1,4,CYNTHIA,3,20,95,5,18,50,,,-1,,,
84 DATAPIRATE,5,20,,10,26,300,,,10,60,1,10,WOLF,8,8,5,60,12,65,,,,40,1,4,CAVEMAN,10,10,25,40,11,155,,,20,55,1,6
87 Z5=26
88 GOSUB73:FORG=1TOZ5:READD(1),D(2),D(3),D(4),D(5),D(6):NEXT:RETURN
89 DATA1,2,,,,,1,4,,,,,,,4,,,,2,7,5,3,,,,,,4,,,,,7,,,,4,10,8,6,,,,,,7,,,,,10,,,
98 DATA7,13,11,9,,,,,,10,,,,,13,,,,10,15,14,12,,,,,,13,,,13,20,-16,,,,,,17,15,,
105 DATA,,18,16,,,,,,17,,,,,20,,,,15,,21,19,,,,,22,20,,,,,,21,,23,,,24,,22,,,,25,23,,
113 DATA-37,,26,24,,,,,,25,,
115 M$="YOUR SPEED SPELL HAS JUST EXPIRED!":GOTO1
116 IFS1THENS1=S1-1:IFS1=.THENM(0,2)=INT(M(0,2)/2):M(0,10)=M(0,10)-2*M(0,2):IFRND(0)>.8THENGOSUB115
117 Z3=L:GOSUB8:M$="YOU ARE "+A$:GOSUB1:V(L)=V(L)+1
118 FORM=1TONM:IFM(M,5)<>L THEN121
119 IFM(M,15)THENM$=M$(M)+" IS HERE.":GOSUB1:GOTO121
120 M(M,15)=1:IFM=10THENR=2:GOSUB416
121 NEXT:FORA=1TONA:IFA(A,4)<>L THEN124
122 IFA(A,9)THENM$="YOU SEE "+A$(A)+".":GOSUB1:GOTO124
123 A(A,9)=1
124 NEXTA
125 PRINT:INPUT" YOUR COMMAND";A$
126 IFLEFT$(A$,1)=" "THENA$=MID$(A$,2):GOTO126
127 IFA$=""THENA$=CZ$:ON-(A$="")GOTO135:PRINT"("A$")"
128 CZ$=A$:FORA=2TOLEN(A$):IFMID$(A$,A,1)<>" "THENNEXT
129 V$=LEFT$(A$,A-1):S$=MID$(A$,A+1)
130 IFLEFT$(S$,1)=" "THENS$=MID$(S$,2):GOTO130
131 FORC=1TO6:IFLEFT$(C$(C),LEN(V$))=V$THEN138
132 NEXT:F=0:FORA=7TONC:IFC$(A)=V$THENC=A:GOTO138
133 IFLEFT$(C$(A),LEN(V$))=V$THENF=F+1:C=A:IFF>1THEN135
134 NEXT:IFF THEN138
135 PRINT"*COMMANDS--":FORC=1TONC:PRINTC$(C),:IFC=8THENGOSUB5
136 NEXT:PRINT:GOTO125
137 IFC$(C)=S$THENS$=V$:V$=C$(C)
138 ONC GOTO176,176,176,176,176,176,196,196,221,230,230,243,299,306,325:IFC<1ORC>31THEN140
139 ONC-15GOTO328,335,339,350,353,353,359,407,366,371,373,197,398,417,424,427
140 IFNB=.THEN116
141 FORM=1TONM:IFM(M,5)<>L ORM(M,15)=2THEN152
142 Q=M(M,14):X=RND(40)-20:X=X-(X=0):MR=FD(Q)/TD(Q)+X
143 IFM(M,4)<MR THENPRINTM$(M)" FLEES.":M(M,5)=RND(26):Q=M(M,14):TD(Q)=TD(Q)-M(M,1):FD(Q)=FD(Q)-M(M,13):GOTO152
144 IFM(M,14)=2THEN152
146 OF=M:DF=0:IFTD(3)=M(0,1)THEN150
147 FORQ=1TONM:IFM(OF,14)=3THENIFQ<>OF ANDM(Q,14)<>3ANDM(Q,5)=L ANDRND(4)=1ANDM(Q,15)<2THENDF=Q:Q=NM:NEXTQ:GOTO150
148 IFM(OF,14)=1THENIFM(Q,14)=3THENIFM(Q,5)=L ANDRND(4)=1ANDM(Q,15)<2THENDF=Q:Q=NM:NEXTQ:GOTO150
149 NEXTQ:IFM(OF,14)=3THEN152
150 GOSUB264
152 NB=-(FD(1)<TD(1)):IFNB THENNEXTM
153 GOTO116
154 NC=31:FORC=1TONC:READC$(C):NEXT:NA=20:GOSUB10:FORA=1TONA:READA$(A):FORA2=1TO8:READA(A,A2):NEXT
155 NEXTA:GOSUB36:NM=12:FORA=1TONM:READM$(A):FORA2=1TO12:READM(A,A2):NEXTA2,A:RE=1:INPUT"NAME";M$(0)
156 FORC=1TO2:GOSUB169:M(0,C)=Z:NEXT:GOSUB169:CH=Z:GP=RND(100)
157 FORS=1TO4:S(S)=24+RND(51):T(S)=S(S):NEXT:PRINT@320,;:GOSUB418
159 PRINT@492,"ACCEPT?";:M$=INKEY$:G=RND(0):IFM$=""THEN159
160 IFM$<>"Y"THEN156
161 W(1)=5:W(2)=-10:W(3)=20:W(4)=10
165 CLS:AC=3:A2=INT(AC/2):IFA2*2<>AC THENM(0,8)=1:EA=-5
166 IFA2THENM(0,8)=M(0,8)+A2:EA=EA-A2*10:IFA2=3THENM(0,8)=M(0,8)+2:EA=EA-30
167 A=17:GOSUB520:WT=A(A,3):IFRND(4)=1THENA(13,4)=9:IFRND(8)=1THENA(18,4)=9
168 NR=26:L=1:ND=6:R2=1:MW=2*M(0,1):GOTO185
169 Z=RND(8)+RND(8)+RND(8):RETURN
170 IFDI=1THENPRINT"YOU ARE DEAD."
171 IFDI=2THENPRINT"YOU SAIL OFF INTO THE SUNSET.":SZ=SZ+999:IFM(9,5)=26THENPRINT"YOU AND YOUR TRUE LOVE! YOU WIN!":SZ=SZ+200
172 IFDI=3THENPRINT"YOU RIDE OFF BACK TO TOWN.":SZ=SZ+100:IFM(9,5)=1THENPRINT"YOU AND YOUR TRUE LOVE!":SZ=SZ+200
173 IFDI>1THENPRINT"GOLD="SZ+GP
174 INPUT"TRY AGAIN";M$:IFLEFT$(M$,1)="Y"THENRUN
175 END
176 IFNB THENM$="YOU CAN'T DO THAT WITH UNFRIENDLIES ABOUT!":GOTO4
177 FORD=1TO6:IFLEFT$(V$,1)<>MID$("NSEWUD",D,1)THENNEXT
178 R2=D(D):IFL=1ANDD=1THENDI=3:GOTO170
179 IFR2 THEN185
184 PRINT"YOU CAN'T GO THAT WAY!":V(L)=0:GOTO116
185 JD=0:R3=L:L=R2:GOSUB189:IFL<0THENL=R3:GOTO184
186 Z5=L:GOSUB88:R2=D(D):NX=0:FORX=1TOND:IFV(L)>1THEND(X)=ABS(D(X))
187 IFD(X)>0ANDD(X)<=NR THENNX=NX+1
188 NEXT:GOTO116
189 TD(1)=0:TD(3)=M(0,1):FD(1)=0:FD(2)=M(0,13)
190 FORM=1TONM:IFM(M,5)=R3THENIFM(M,14)=3ORRND(200)<M(M,4)THENM(M,5)=L:GOTO194
191 IFM(M,5)<>L THEN195
192 FR=M(M,3):IFFR THENIFFR<>100THENFR=FR+INT((CH-10)/2)
193 M(M,14)=1:IFFR>(RND(100))THENM(M,14)=2:IFFR>(RND(100))THENM(M,14)=3
194 A=M(M,14):FD(A)=FD(A)+M(M,13):TD(A)=TD(A)+M(M,1)
195 NEXTM:NB=-(TD(1)>0):RETURN
196 GOSUB219
197 IFS$="ALL"THEN204
198 IFS$="TORCH"THENM$="ALL TORCHES ARE BOLTED TO THE WALL AND CAN'T BE REMOVED.":GOTO3
199 WH=L:HA=L:GOSUB215:IFFO=.THENM$="I SEE NO "+S$+" HERE!":GOTO4
200 GOSUB209:IFGT=.THEN116
201 IFWT+A(A,3)>MW THENM$=A$(A)+" IS TOO HEAVY":GOTO4
202 PRINT"GOT IT.":A(A,4)=-1:SZ=SZ+A(A,0):WT=WT+A(A,3):IFA(A,2)<2ORM(0,9)<>-1THEN140
203 GOTO366
204 FORA=1TONA:IFA(A,4)<>L THEN208
205 GOSUB209:IFGT=.THEN208
206 IFWT+A(A,3)>MW THENM$=A$(A)+" IS TOO HEAVY":GOSUB1:GOTO208
207 M$=A$(A)+" TAKEN.":GOSUB1:A(A,4)=-1:WT=WT+A(A,3)
208 NEXT:GOTO140
209 GT=1:RETURN
210 WH=L:HA=-1
211 FO=0:FORX=1TONM:IFS$<>M$(X)ANDLEFT$(M$(X),LEN(S$))<>S$ANDRIGHT$(M$(X),LEN(S$))<>S$THEN213
212 IFM(X,5)=WH ORM(X,5)=HA THENFO=1:RETURN
213 NEXT:RETURN
214 WH=L:HA=-1
215 FO=0:FORA=1TONA:IFA$(A)<>S$ANDLEFT$(A$(A),LEN(S$))<>S$ANDRIGHT$(A$(A),LEN(S$))<>S$THEN218
216 IFA(A,4)<>WH ANDA(A,4)<>HA THEN218
217 FO=1:G=A:A=NA:NEXTA:A=G:RETURN
218 NEXTA:RETURN
219 IFS$=""THENPRINTC$(C)" WHAT";:INPUTS$:GOTO219
220 RETURN
221 GOSUB219:IFS$="ALL"THEN228
222 FORA=1TONA:IF(A$(A)<>S$ANDLEFT$(A$(A),LEN(S$))<>S$ANDRIGHT$(A$(A),LEN(S$))<>S$)ORA(A,4)<>-1THENNEXT:GOTO318
224 WT=WT-A(A,3):A(A,4)=L:M$=A$(A)+" DROPPED.":GOSUB1
225 IFWT>MW THENM$="TOO MUCH! YOU DROP ALL YOUR ITEMS.":GOSUB1:GOTO228
226 IFM(0,9)>0THENIFA(M(0,9),4)<>-1THENM(0,9)=-1
227 GOTO140
228 FORA=1TONA:IFA(A,4)=-1THENM$=A$(A)+" DROPPED.":GOSUB1:A(A,4)=L
229 NEXTA:WT=0:GOTO226
230 LK=0:IFS$=""THEN235
231 FORA=1TONA:IFA$(A)=S$AND(A(A,4)=L ORA(A,4)=-1)THENGOSUB241:LK=1:A(A,9)=1
232 IFA$(A)=S$ANDA(A,4)=L THENLK=1:A(A,4)=L
233 NEXTA:IFLK THEN140
234 GOTO240
235 Z5=L:GOSUB88:FORX=1TOND
236 IFD(X)<0ANDD(X)>=-NR THENM$="YOU FOUND A SECRET PASSAGE "+C$(X)+"!":GOSUB1:D(X)=ABS(D(X)):V(L)=2
237 NEXT
238 FORA=1TONA:IFA(A,4)=L THENA(A,9)=L:LK=1
239 NEXT:IFLK THENPRINT"YOU FOUND SOMETHING."
240 V(L)=V(L)+1:GOTO140
241 IFA=3ORA=13THENM$="THE BOTTLE SAYS, 'HEALING POTION'":GOTO1
242 R=7:ON-(A=9)GOTO416:M$="NOTHING UNUSUAL.":GOTO1
243 GOSUB219:GOSUB210:IFFO=.THEN245
244 M=X:GOTO251
245 WH=L:HA=L:GOSUB215:IFFO=.THENPRINT"ATTACK WHO?":GOTO116
246 PRINT"THAT WOULD BE STUPID.":GOTO116
251 IFM(0,9)=-1THENM$="YOU HAVE NO WEAPON READY!":GOTO4
252 OF=0:DF=M:GOSUB264:IFHI=.THEN258
253 IFI0THENG=A(M(0,9),6):W(G)=W(G)+2
254 IF-EA<=AETHEN140
255 IFRND(100)<=M(0,10)THEN258
256 A=2:IFEA+AE=-1THENA=1
257 AE=AE+A:M(0,10)=M(0,10)+A
258 IFM(DF,14)=1ORDF=0THEN140
259 M(DF,3)=INT(M(DF,3)/2):FORM=1TONM:IFM(M,5)=L THENIFM(M,14)>1THENM(M,14)=0
260 NEXTM:R3=L:GOSUB189:GOTO140
261 IFM(OF,10)=0THENRETURN
262 FORA=1TONA:IFA(A,4)<>L ORA(A,2)<2ORA(A,2)>3THENNEXT:RETURN
263 M$=M$(OF)+" PICKS UP "+A$(A)+".":GOSUB1:A(A,4)=0:M(OF,9)=A:M(OF,10)=M(OF,10)+A(A,5):M(OF,4)=M(OF,4)*2:RETURN
264 HI=0:IFM(OF,9)=-1THEN261
265 M$=M$(OF)+" ATTACKS "+M$(DF)+"--":GOSUB1
266 RL=RND(100):HI=0:IFRL<5ORRL<M(OF,10)THENHI=1
267 IFHI THEN276
268 IFRL<97THENPRINT"A MISS.":RETURN
269 PRINT"A FUMBLE!":FR=RND(100):IFFR<=35+40*-(M(OF,9)=.)THENPRINT" FUMBLE RECOVERED.":RETURN
270 IFFR<76THENPRINT"WEAPON DROPPED!":A(M(OF,9),4)=L:M(OF,9)=-1:RETURN
271 IFFR>95THEN274
272 PRINT" WEAPON BROKEN!":A(M(OF,9),4)=0:M(OF,10)=M(OF,10)-A(M(OF,9),5):M(OF,9)=-1:M(OF,4)=INT(M(OF,4)/2):IFRND(0)>.5THEN220
273 PRINT" BROKEN WEAPON HURTS USER!"
274 IFFR=100THEND=M(OF,11)*2:S=M(OF,12):DF=OF:A=0:GOTO283
275 D=M(OF,11):S=M(OF,12):DF=OF:A=1:GOTO283
276 I0=-(RND(100)>M(OF,10)):M(OF,10)=M(OF,10)+2*I0
277 D=M(OF,11):S=M(OF,12):A=1:IFRL>5THENPRINT"A HIT!":GOTO283
278 PRINT"A CRITICAL HIT!":RL=RND(100):IFRL<51THENA=0:GOTO283
279 IFRL<86THENS=1.5*S:GOTO283
280 IFRL<96THEND=2*D:GOTO283
281 IFRL<100THEND=3*D:GOTO283
282 GOTO292
283 D2=0:FORD3=1TOD:D2=D2+INT(S*RND(0)+1):NEXT:D2=D2-A*M(DF,8):IFD2<1THENPRINT" BLOW BOUNCES OFF ARMOUR":RETURN
284 M(DF,13)=M(DF,13)+D2:IFM(DF,13)>=M(DF,1)THEN292
285 A=M(DF,14):FD(A)=FD(A)+D2
286 M$=M$(DF)+" IS ":ONABS(INT(M(DF,13)*5/M(DF,1)+1))GOTO287,288,289,290,291
287 M$=M$+"STILL IN GOOD SHAPE.":GOTO1
288 M$=M$+"HURTING.":GOTO1
289 M$=M$+"IN PAIN.":GOTO1
290 M$=M$+"VERY BADLY INJURED.":GOTO1
291 M$=M$+"AT DEATH'S DOOR, KNOCKING LOUDLY.":GOTO1
292 M$=M$(DF)+" IS DEAD!":GOSUB1:A=M(DF,14):FD(A)=FD(A)+M(DF,1)-M(DF,13)+D2:JD=DF:M(DF,5)=0:IFM(DF,9)>0THENA(M(DF,9),4)=L
294 IFFD(1)>TD(1)THENNB=0
295 IFDF=0THENDI=1:GOTO170
296 IFDF=10THENR=3:GOSUB416:A(10,8)=6:V(L)=0:A(10,4)=L
297 IFDF=7THENM$="AS THE MIMIC DIES, IT ROLLS OVER AND YOU FIND A RING UNDERNEATH IT.":GOSUB1:A(6,4)=L
298 RETURN
299 IFNB=.THENM$="THERE'S NOTHING TO FLEE FROM!":GOTO4
300 IFM(7,5)=L THENM$="*HELD FAST BY THE MIMIC AND CANNOT FLEE!":GOTO3
303 IFNX=0THENM$="THERE'S NO PLACE TO GO!":GOTO3
304 D=RND(6):R2=D(D):IFR2<1ORR2>NR THENNX=0:GOTO303
305 GOTO185
306 A2=1:C3=0
307 FORM=1TONM:IFLEFT$(S$,LEN(M$(M)))=M$(M)ANDM(M,5)=L THEN310
308 NEXT:IFA2=1THENA2=2:INPUT"WHO TO GIVE TO";S$:GOTO307
309 M$="NOBODY HERE BY THAT NAME!":GOTO4
310 S$=MID$(S$,LEN(M$(M))+1)
311 C3=VAL(S$):IFLEFT$(S$,1)=" "THENS$=MID$(S$,2):GOTO311
312 IFC3>0THEN323
313 IFS$=""THEN317
314 FORA=1TONA:IFA$(A)=""THEN316
315 IFLEFT$(S$,LEN(A$(A)))=A$(A)ANDA(A,4)=-1THEN319
316 NEXT
317 IFA2>2THENA2=3:INPUT"WHAT TO GIVE";S$:GOTO311
318 M$="YOU AREN'T CARRYING A "+S$+"!":GOTO4
319 IFA=M(0,9)THENM(0,9)=-1
320 PRINT"OKAY.":A(A,4)=-M-1:M(M,14)=0:M(M,3)=M(M,3)*INT(1+A(A,1)/100)
321 IFM(M,10)<>0ANDM(M,9)=-1THENM(M,9)=A:M(M,10)=M(M,9)+A(A,5):M(M,4)=2*M(M,4)
322 R3=L:GOSUB189:GOTO225
323 C=INT(VAL(S$)):IFC>GP THENM$="YOU AREN'T CARRYING THAT MUCH GOLD!":GOTO3
324 PRINT"OK.":GP=INT(GP-C):M(M,0)=M(M,0)+C:M(M,14)=0:M(M,3)=M(M,3)*INT(1+C/100):GOTO322
325 PRINT"*CARRYING--":A0=0:FORA=1TONA
326 IFA(A,4)=-1THENM$=A$(A)+LEFT$(" (READY WEAPON)",1+20*-(A=M(0,9))):GOSUB1:A0=A0+1:IFA0=14THENA0=0:GOSUB5
327 NEXT:GOTO140
328 GOSUB219:FORM=1TONM:IFS$<>M$(M)ORM(M,5)<>L THENNEXT:M$="YOU CAN'T BLAST "+S$+"!":GOTO4
329 S=1:GOSUB331:IFSU=.THEN140
330 PRINT"DIRECT HIT!":S=6:D=1:DF=M:A=0:GOSUB283:GOTO140
331 SU=0:IFS(S)THENRL=RND(100):SU=-(((RL<=T(S))OR(RL<5))AND(RL<95))
332 IFSU=.THENPRINT"NOTHING HAPPENED.":RETURN
333 RL=RND(100):IFRL>T(S)THENT(S)=T(S)+2:S(S)=S(S)+2
334 T(S)=INT(T(S)/2):RETURN
335 S=2:GOSUB331:IFSU=.THEN140
336 IFM(0,13)THENM$="SOME OF YOUR WOUNDS CLEAR UP.":GOSUB1
337 M(0,13)=M(0,13)-RND(10):IFM(0,13)<0THENM(0,13)=0
338 DF=0:GOSUB286:GOTO140
339 S=4:GOSUB331:IFSU=.THEN140
340 RA=0:FORM=1TONM:IFM=JD THENRA=1:M$=M$(M)+" COMES ALIVE!":GOSUB1:M(M,13)=0:M(M,5)=L:JD=0
341 NEXTM:IFRA THENR3=L:GOSUB189:GOTO140
342 FORA=1TONA:IFA(A,4)=L THENRA=1:PRINTA$(A)" VANISHES!":A(A,4)=RND(26)
343 NEXTA:IFRA THEN140
344 RR=RND(100):IFRR<11THENM$="THE SECTION OF THE CEILING YOU ARE UNDER COLLAPSES.":GOSUB1:DI=1:GOTO170
345 IFRR<86THENM$="YOU HEAR A VERY LOUD BOOM THAT ECHOES THROUGH THE TUNNELS.":GOTO3
346 IFRR>95THENPRINT"ALL OF YOUR HITS ARE HEALED.":M(0,13)=0:GOTO140
347 GOTO351
350 S=3:GOSUB331:IFSU=.THEN140
351 IFS1=.THENM(0,10)=M(0,10)+2*M(0,2):M(0,2)=2*M(0,2)
352 S1=S1+9+RND(25):M$="YOU FEEL NEW AGILITY FLOWING THROUGH YOU!":GOTO3
353 FORM=1TONM:IFM(M,5)<>L THEN358
354 ONM(M,14)GOTO355,356,357
355 M$=M$(M)+" GROWLS AT YOU!":GOSUB1:GOTO358
356 M$=M$(M)+" IGNORES YOU.":GOSUB1:GOTO358
357 M$=M$(M)+" "+V$+"S BACK.":GOSUB1:GOTO358
358 NEXTM:GOTO140
359 GOSUB219
360 IFS$="BLAST"THENV$=S$:S$="":GOTO328
361 IFS$="HEAL"THEN335
362 IFS$="POWER"THEN339
363 IFS$="SPEED"THEN350
364 IFS$="TROLLSFIRE"THEN427
365 M$="OKAY, '"+S$+"'":GOTO3
366 GOSUB219:FORA=1TONA:IFRIGHT$(A$(A),LEN(S$))<>S$ORA(A,4)<>-1THENNEXT:GOTO318
368 IFA(A,2)<2ORA(A,2)>3THENPRINT"THAT ISN'T A WEAPON!":GOTO116
369 GOSUB520:PRINT"READIED.":GOTO140
371 GOSUB219:GOSUB214:IFFO=.THEN423
372 M$="IT WON'T LIGHT!":GOTO4
373 GOSUB219:WH=L:HA=-1:GOSUB215:IFFO=.THEN384
374 IFA=12THENR=1:GOSUB416:NB=1:A(12,4)=0:M(7,5)=L:R3=L:GOSUB189:GOTO140
375 IFA(A,2)=4THEN380
376 IFA(A,2)=7THENPRINT"TRY READING IT.":GOTO116
377 IFA(A,2)=1THENM$="YOU'LL MAKE A MESS. WAIT TILL YOU LEAVE.":GOTO4
379 M$="THAT'S NOT WHAT YOU DO WITH IT.":GOTO4
380 IFA(A,5)THENIFA(A(A,5),4)<>-1THENPRINT"IT'S LOCKED!":GOTO116
381 LK=0:FORX=1TONA:IFA(X,4)=ATHENA(X,4)=L:LK=1
382 NEXT:IFLK THENPRINT"YOU FOUND SOMETHING INSIDE!"
383 GOTO140
384 IFS$="DOOR"ORS$="GATE"THENM$="IT WILL OPEN WHEN YOU APPROACH.":GOTO4
385 M$="I DON'T KNOW HOW TO OPEN "+S$+".":GOTO4
398 GOSUB219:IFS$="POTION"THENS$="BOTTLE"
399 GOSUB214:IFFO=.THEN423
400 IFA(A,2)<>6THENPRINT"YOU CAN'T DO THAT!":GOTO116
401 IFA(A,7)<=0THENPRINT"THERE ISN'T ANY LEFT.":GOTO140
402 IFA(A,5)>RND(99)THEN404
403 PRINT"NOTHING SEEMS TO HAPPEN.":GOTO140
404 A(A,7)=A(A,7)-1:M(0,13)=M(0,13)-A(A,6)
405 M$="YOUR WOUNDS SEEM TO "+MID$("OPENHEAL",(A(A,6)>0)*-4+1,4)+"!":GOSUB1:GOTO140
407 GOSUB219:WH=L:HA=-1:GOSUB215:IFFO=.THENPRINT"THERE IS NOTHING TO READ.":GOTO116
408 IFA(A,2)=7THEN410
409 PRINT"YOU CAN'T READ THAT!":GOTO140
410 IFA=9THEN413
411 FORX=1TOA(A,6):R=A(A,5)-1+X:GOSUB416:NEXT:GOSUB5
412 GOTO140
413 R=7:GOSUB416
414 IFL=26ANDNB=0THENR=8:GOSUB416:DI=2:GOTO170
415 R=9:GOSUB416:DI=1:GOTO170
416 Z3=26+R:GOSUB8:M$=A$:GOTO1
417 GOSUB418:GOTO116
418 PRINT"HARDINESS"M(0,1),"AGILITY"M(0,2),"CHARISMA "CH,"GOLD   "GP,
419 FORG=1TO4:PRINTC$(15+G)STR$(S(G))+"%",:NEXT:RETURN
423 PRINT"I DON'T UNDERSTAND.":GOTO116
424 IFS$="WATER"ORS$="SEA"THENIFL=26THENM$="I'M AFRAID TO GO INTO THE WATER WITHOUT MY LIFE PRESERVER!":GOTO4
425 IFS$="CAVE"ANDL=1THENV$="S":GOTO176
426 ON-(S$="BOAT"ANDL=26)GOTO530:M$="HUH?":GOTO4
427 IFA(10,4)<>-1THENPRINT"NOTHING HAPPENS.":GOTO140
428 IFTR=.THEN431
429 R=6:GOSUB416:TR=0:A(10,8)=6:IFM(0,9)=10THENM(0,12)=6
430 GOTO140
431 R=4:GOSUB416:IFM(0,9)<>10THENR=5:GOSUB416:D=1:S=5:DF=0:GOSUB283:GOTO140
432 TR=1:A(10,8)=10:M(0,12)=10:GOTO140
500 CLS:G=132:PRINT@G,"WELCOME TO THE":PRINT@G+36,"WORLD OF":PRINT@G+69,"eamon!"
510 FORG=0TO7:PRINT@G*32+52,MID$("Ž²Œ³…Š³¿¿¿²µ¿¿µ¿µ·¿¿¿‹‚³·¿…Œ±¿¿¿»ºµ¿µµ¾¸ƒƒƒƒƒ",G*7+1,7):NEXT:RETURN
520 M(0,10)=(EA+AE)*-(-EA>AE)+A(A,5)+W(A(A,6))+2*M(0,2):M(0,9)=A:M(0,11)=A(A,7):M(0,12)=A(A,8):RETURN
530 M$="TOO DANGEROUS! IT'S BEEN DAMAGED.":GOTO4
