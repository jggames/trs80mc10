0 CLS:CLEAR350:DIMCC,ZZ,M$,I$,RD(6):GOTO1000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 GOSUB1:GOTO800
9 GOSUB1:GOTO600
10 INPUT"(HIT enter TO CONTINUE)";A$
11 FORCC=1TO32:PRINTCHR$(8);:NEXT:RETURN

12 MM=PEEK(173)+256*PEEK(174):RETURN
13 POKE174,INT(MM/256):POKE173,MM-256*PEEK(174)
14 FORCC=1TOZ3:READA$:NEXT:RETURN

89 Z1=31
90 RESTORE:FORCC=1TOZ1:READM$:NEXT:RETURN
91 DATANORTH,SOUTH,EAST,WEST,UP,DOWN,GET,TAKE,DROP,LOOK,EXAMINE,ATTACK,FLEE,GIVE,INVENTORY
92 DATABLAST,HEAL,POWER,SPEED,SMILE,WAVE,SAY,READ,READY,LIGHT,OPEN,PUT,DRINK,FREE,ENTER,TROLLSFIRE

REM ARTIFACTS
99 Z2=20
100 GOSUB89:FORCC=1TOZ2:READM$:FORC1=1TO8:READZZ:NEXT:NEXT:RETURN
101 DATADIAMONDS,200,1,3,3,,,,0
102 DATAAXE,25,2,4,,,1,1,6
103 DATABOTTLE,50,6,2,5,100,6,2,0
104 DATASWORD,25,2,4,,-10,5,1,8
105 DATAGOLD COINS,50,,2,8,,,,0
106 DATARING,150,1,5,,,,,0
107 DATAMACE,40,2,4,,10,3,1,4
108 DATASPICES,100,1,3,18,,,,0
109 DATABOOK,250,7,3,19,7,2,1,0
110 DATATROLLSFIRE,100,3,3,,25,5,1,10
111 DATAJEWELS,1000,1,5,26,,,,0
112 DATACHEST,,1,300,14,,,,0
113 DATAEMPTY BOTTLE,1,1,1,,,,,0
114 DATARUBY,250,1,1,37,,,,0
115 DATAIVORY SWORD,200,3,5,11,15,5,1,8
116 DATALONG BOW,35,3,5,,10,2,1,8
117 DATAOLD STAFF,30,3,3,,10,3,1,4
118 DATABROADSWORD,90,3,11,,5,5,2,5
119 DATACHEST,1250,1,50,29,,,,0
120 DATACLUB,1,3,5,,,3,1,4

REM DESC 37,44,9,20
199 Z3=35
200 GOSUB99:GOSUB12:GOTO14
201 DATA"LOOKING s AT THE ENTRANCE OF A CAVE. TO THE n THE ROAD LEADS BACK TO TOWN."
202 DATA"IN A DARK n/s TUNNEL. NORTH YOU SEE OUTSIDE. TO THE s YOU SEE A FLICKERING LIGHT."
203 DATA"IN A SMALL SIDE CHAMBER. IT IS COLD HERE, AND THE ONLY LIGHT COMES FROM THE LARGE CHAMBER."
204 DATA"IN A HUGE CHAMBER. BURNING TORCHES LINE THE WALLS. TUNNELS LEAD n AND s, AND SMALL CHAMBERS ARE e & w."
205 DATA"IN A SMALL CHAMBER. IT IS VERY COLD. THE ONLY LIGHT COMES FROM THE LARGE CHAMBER."
206 DATA"IN A SMALL, STARK CELL WITH A DOOR TO THE e."
207 DATA"AT THE n END OF A LONG HALL. A TUNNEL GOES n. BOLTED DOORS ARE e & w. THE HALL GOES s."
208 DATA"IN A SMALL, STARK CELL WITH A DOOR w."
209 DATA"IN A SMALL, STARK CELL WITH A DOOR e."
210 DATA"IN THE MIDDLE OF THE LONG n/s HALL. BOLTED DOORS e & w."
211 DATA"IN A SMALL, STARK CELL WITH A DOOR w."
212 DATA"IN A SMALL, STARK CELL WITH A DOOR e."
213 DATA"AT THE s END OF THE LONG HALL. SHOCKINGLY THERE ARE TWO DOORS TO THE e & w. A BROKEN PASSAGE GOES s."
214 DATA"IN A STARK CELL WITH A DOOR w."
215 DATA"IN A n/s TUNNEL. THE SIDES ARE ROUGH & BROKEN. YOU SEE TORCHLIGHT s."
216 DATA"IN A SECRET e/w PASSAGE. YOU HEAR A FAINT MELODY TO THE e."
217 DATA"IN AN e/w PASSAGE. YOU CAN HEAR RELIGIOUS MUSIC AND SMELL INCENSE FROM THE e."
218 DATA"IN THE TEMPLE. PICTURES OF HEROES COVER THE WALLS. THERE ARE TWO ALTARS, ONE GOLD, ONE BLOODY. THE ONLY EXIT IS w."
219 DATA"IN WHAT WAS ONCE A LIBRARY. SCRAPS OF PAPER COVER THE FLOOR. A BROKEN DOOR LIES ON THE GROUND. THE EXIT IS e."
220 DATA"IN A T-INTERSECTION. A TORCH IS s. TUNNELS LEAD n & e. A BROKEN DOOR IS w."
221 DATA"IN A DARK e/w TUNNEL. YOU CAN SEE TORCHLIGHT BOTH DIRECTIONS."
222 DATA"AT THE TOP OF SOME STAIRS. A TORCH IS ON THE WALL. IT LOOKS DARK DOWN THERE. A TUNNEL HEADS w."
223 DATA"AT THE BOTTOM OF SOME STAIRS. A LIGHT CAN BE SEEN AT THE TOP. A DIM LIGHT CAN BE SEEN e."
224 DATA"IN AN e/w TUNNEL. DIM LIGHT e & w. A NARROWER TUNNEL LEADS s."
225 DATA"IN AN e/w TUNNEL. YOU SEE LIGHT e. A SALTY BREEZE COMES FROM THAT DIRECTION."
226 DATA"IN A SMALL BAY. HIGH WALLS SURROUND YOU. THE ONLY EXIT IS BACK w. TO THE e IS A BROKEN BOAT."
227 DATA"IT SLOWLY COMES ALIVE AND TWO TENTACLES COME OUT OF THE SIDE, HOLDING YOU FAST. ANOTHER TENTACLE FROM INSIDE ATTACKS YOU."
228 DATA"A HUMAN FIGURE YOU CAN'T MAKE OUT... BRIGHT GREEN FLAME SURROUNDS THE BLADE OF HIS SWORD."
229 DATA"THE FLAME ON HIS SWORD DIES ALSO."
230 DATA"FIRE DANCES FROM THE SWORD'S BLADE."
231 DATA"'TROLLSFIRE' IS WRITTEN ON THE SWORD'S BLADE."
232 DATA"YOU RECEIVE SOME BURN DAMAGE UNTIL THE SWORD (HEARING YOUR CRIES OF PAIN?) EXTINGUISHES ITS FLAME."
233 DATA"'BOAT REPAIR' BY JIM GERRIE"
234 DATA"YOU REPAIR THE BOAT."
235 DATA"THE BOOK BITES YOUR HEAD OFF."

REM MONSTERS
299 Z4=12
300 GOSUB199:FORCC=1TOZ4:READM$:FORC1=1TO12:READZZ:NEXT:NEXT:RETURN
301 DATABLACK RAT,2,10,,10,3,5,,,,30,1,1
302 DATABROWN RAT,2,10,,10,3,5,,,,30,1,1
303 DATATAN RAT,2,10,,10,3,5,,,,30,1,1
304 DATAHERMIT,13,22,50,75,5,75,,,2,55,1,6
305 DATAGORILLA,12,15,,60,8,200,,,,45,1,4
306 DATAHEINRICH,15,5,65,100,6,100,,1,4,12,1,8
307 DATAMIMIC,5,5,,100,,300,,,,40,1,3
308 DATAPRIEST,10,6,,100,18,75,,1,7,25,1,4
309 DATACYNTHIA,3,20,95,5,18,50,,,-1,,,0
310 DATAPIRATE,5,20,,10,26,300,,,10,60,1,10
311 DATAWOLF,8,8,5,60,12,65,,,,40,1,4
312 DATACAVEMAN,10,10,25,40,11,155,,,24,55,1,6

REM ROOMS 26
399 Z5=26
400 GOSUB299:FORCC=1TOZ5:READRD(1),RD(2),RD(3),RD(4),RD(5),RD(6):NEXT:RETURN
401 DATA1,2,,,,
402 DATA1,4,,,,
403 DATA,,4,,,
404 DATA2,7,5,3,,
405 DATA,,,4,,
406 DATA,,7,,,
407 DATA4,10,8,6,,
408 DATA,,,7,,
409 DATA,,10,,,
410 DATA7,13,11,9,,
411 DATA,,,10,,
412 DATA,,13,,,
413 DATA10,15,14,12,,
414 DATA,,,13,,
415 DATA13,20,-16,,,
416 DATA,,17,15,,
417 DATA,,18,16,,
418 DATA,,,17,,
419 DATA,,20,,,
420 DATA15,,21,19,,
421 DATA,,22,20,,
422 DATA,,,21,,23
423 DATA,,24,,22,
424 DATA,27,25,23,,
425 DATA-37,,26,24,,
426 DATA,,,25,,

500 M$="YOUR SPEED SPELL HAS JUST EXPIRED!":GOTO1

REM MAIN LOOP
600 IFS1THENS1=S1-1:IFS1=.THENM(0,2)=M(0,2)/2:M(0,10)=M(0,10)-2*M(0,2):IFRND(0)>.8THENGOSUB500
640 Z3=L:GOSUB13:M$="YOU ARE "+A$:GOSUB1:V(L)=V(L)+1
650 FORM=1TONM:IFM(M,5)<>L THEN670
660 IFM(M,15)THENM$=M$(M)+" IS HERE.":GOSUB1:GOTO670
661 M(M,15)=1:IFM=10THENR=2:GOSUB23800
670 NEXT:FORA=1TONA:IFA(A,4)<>L THEN690
680 A(A,9)=1
671 IFA(A,9)THENM$="YOU SEE "+A$(A)+".":GOSUB1:GOTO690
690 NEXTA

REM COMMAND ENTRY
710 PRINT:INPUT" YOUR COMMAND";A$
720 IFLEFT$(A$,1)=" "THENA$=MID$(A$,2):GOTO720
730 IFA$=""THENA$=CZ$:ON-(A$="")GOTO775:PRINT"("A$")"
740 CZ$=A$:FORA=2TOLEN(A$):IFMID$(A$,A,1)<>" "THENNEXT
750 V$=LEFT$(A$,A-1):S$=MID$(A$,A+1)
760 IFLEFT$(S$,1)=" "THENS$=MID$(S$,2):GOTO760
770 FORC=1TO6:IFLEFT$(C$(C),LEN(V$))=V$THEN790
772 NEXT:F=0:FORA=7TONC:IFC$(A)=V$THENC=A:GOTO790
773 IFLEFT$(C$(A),LEN(V$))=V$THENF=F+1:C=A:IFF>1THEN775
774 NEXT:IFF THEN790
775 PRINT"*COMMANDS--":SP=225:FORC=1TONC:PRINTC$(C),:IFC=8THENGOSUB10
776 NEXT:SP=255:PRINT:GOTO710
780 IFC$(C)=S$THENS$=V$:V$=C$(C)
790 ONC GOTO3000,3000,3000,3000,3000,3000,4000,4000,5000,6000,6000,7000,8000,9000,10000:IFC<1ORC>31THEN800
791 ONC-15GOTO11000,12000,13000,14000,15000,15000,16000,23000,17000,19000,20000,21000,22000,24000,25000,26000

REM COMMANDS RETURN TO HERE
800 IFNB=.THEN600
820 FORM=1TONM:IFM(M,5)<>L ORM(M,15)=2THEN930
830 M2=M(M,14)-(M(M,14)=2):X=RND(40)-20:X=X-(X=0):MR=FD(M2)/TD(M2)+X
840 IFM(M,4)<MR THENPRINTM$(M)" FLEES.":M(M,5)=R2:M2=M(M,14):TD(M2)=TD(M2)-M(M,1):FD(M2)=FD(M2)-M(M,13):GOTO930
845 IFM(M,14)=2THEN930
850 IFM(M,14)=3THEN890
860 OF=M:IFTD(3)=M(0,1)THENDF=0:GOSUB7500:GOTO930
870 FORM2=1TONM:IFM(M2,5)=L ANDM(M2,14)=3ANDRND(0)<.25ANDM(M2,15)<2THENDF=M2:GOSUB7500:M2=200:NEXTM2:GOTO930
880 NEXTM2:DF=0:GOSUB7500:GOTO930
890 OF=M
900 FORM2=1TONM:IFM(M2,5)=L THENIFM(M2,14)=.THENDF=M2:GOSUB7500:M2=200:NEXTM2:GOTO930
910 NEXTM2:GOTO900
930 NB=-(FD(0)<TD(0)):IFNB THENNEXTM
950 GOTO600

REM INIT
1000 NA=20:DIMA$(NA),A(NA,9):GOSUB89:FORA=1TONA:READA$(A):FORA2=1TO8:READA(A,A2):NEXT
1030 NEXTA:GOSUB199:NM=12:DIMM$(NM),M(NM,15):FORA=1TONM:READM$(A):FORA2=1TO12:READM(A,A2):NEXTA2,A
1040 RE=1:INPUT"NAME";M$(0)
1041 M(0,1)=10+RND(8):M(0,2)=10+RND(8):CH=10+RND(8):GP=10+RND(8)
1042 PRINT@32,"HARDINESS"M(0,1),"AGILITY"M(0,2),"CHARISMA "CH,"GOLD   "GP
1043 PRINT@128,"ACCEPT?":M$=INKEY$:CC=RND(0):IFM$="N"THEN1041
1044 IFM$<>"Y"THEN1042
1045 GOSUB11:DIMSA(4),WA(8):WA(1)=5:WA(2)=-10:WA(3)=20:WA(4)=10:SA(1)=150:AC=3
1050 NW=4:DIMWN$(NW),WT(NW),WO(NW),WD(NW),WP(NW),WS(NW):WN$(1)="CLUB":WT(1)=3:WO(1)=10:WD(1)=1:WS(1)=4
1060 FORW2=2TO4:WN$(W2)="NONE":NEXT:W2=1
1070 A$(NA)=WN$(1):A(20,2)=2:A(20,3)=2:A(20,4)=-1:A(20,5)=WO(W2):A(20,6)=WT(W2):A(20,7)=WD(W2):A(20,8)=WS(W2)
1080 WT=A(20,3)
1110 IFA2THENM(0,8)=M(0,8)+A2:EA=EA-A2*10:IFA2=3THENM(0,8)=M(0,8)+2:EA=EA-30
1130 NC=31:DIMC$(NC):RESTORE:FORC=1TONC:READC$(C):NEXT:R2=1
1140 FORS=1TO4:S2(S)=SA(S):NEXT:NR=26:DIMV(NR):L=1:ND=6:R2=1
1210 GOTO3500

REM GAME OVER
2000 IFDI=1THENPRINT"YOU ARE DEAD."
2010 IFDI=2THENM$="YOU SAIL OFF INTO THE SUNSET. YOU WIN!":GOSUB1
2020 IFDI=3THENPRINT"YOU RIDE OFF BACK TO TOWN."
2030 IFDI>1THENPRINT"GOLD="SZ+GP
2040 INPUT"TRY AGAIN";M$:IFLEFT$(M$,1)="Y"THENRUN
2050 END

REM MOVE COMMAND
3000 IFNB THENIFS$<>"FLEE"THENM$="YOU CAN'T DO THAT WITH UNFRIENDLIES ABOUT!":GOTO9
3020 FORD=1TO6:IFLEFT$(V$,1)<>MID$("NSEWUD",D,1)THENNEXT
3030 R2=RD(D):IFL=1ANDD=1THENDI=3:GOTO2000
3050 IFR2 THEN3500
3060 IFR2=.THEN3490
3300 IFA(R2-100,7)=0THENR2=A(R2-100,5):GOTO3050
3310 IFA(R2-100,6)=0THENR2=A(R2-100,5):GOTO3050
3320 M$="THE "+A$(R2-100)+" BLOCKS THE WAY!":GOTO9
3490 PRINT"YOU CAN'T GO THAT WAY!":V(L)=0:GOTO600

REM MOVE IT
3500 R3=L:L=R2:GOSUB3600:IFL<0THENL=R3:GOTO3490
3540 Z5=L:GOSUB400:R2=RD(D):NX=0:FORX=1TOND:IFV(L)>1THENRD(X)=ABS(RD(X))
3550 IFRD(X)>0ANDRD(X)<=NR THENNX=NX+1
3560 NEXT:GOTO600

3600 TD(0)=0:TD(1)=M(0,1):FD(0)=0:FD(1)=M(0,13)
3610 FORM=1TONM:IFM(M,5)=R3THENIFM(M,14)OR200*RND(0)<M(M,4)THENM(M,5)=L:GOTO3650
3620 IFM(M,5)<>L THEN3660
3630 FR=M(M,3):IFFR THENIFFR<>100THENFR=FR+INT((CH-10)/2)
3640 M(M,14)=-(FR>100*RND(0)):IFM(M,14)=.THENNB=1
3650 A=M(M,14):FD(A)=FD(A)+M(M,13):TD(A)=TD(A)+M(M,1)
3660 NEXTM:NB=-(TD(0)>0):RETURN

REM GET
4000 GOSUB4900
4020 IFS$="ALL"THEN4160
4030 IFS$="TORCH"THENM$="ALL TORCHES ARE BOLTED TO THE WALL AND CAN'T BE REMOVED.":GOTO8
4120 WH=L:HA=L:GOSUB4810:IFFO=.THENM$="I SEE NO "+S$+" HERE!":GOTO9
4130 GOSUB4200:IFGT=.THEN600
4135 IFWT+A(A,3)>10*M(0,1)THENPRINT"IT IS TOO HEAVY.":GOTO600
4140 PRINT"GOT IT.":A(A,4)=-1:SZ=SZ+A(A,0):WT=WT+A(A,3):IFA(A,2)<2ORM(0,9)<>-1THEN800
4150 GOTO17000
4160 FORA=1TONA:IFA(A,4)<>L THEN4190
4170 GOSUB4200:IFGT=.THEN4190
4175 IFWT+A(A,3)>10*M(0,1)THENM$=A$(A)+" IS TOO HEAVY":GOSUB1:GOTO4190
4180 M$=A$(A)+" TAKEN.":GOSUB1:A(A,4)=-1:WT=WT+A(A,3)
4190 NEXT:GOTO800

REM SPECIAL EFFECTS OF PICKING SOMETHING UP
4200 GT=1:RETURN

REM MONST SEARCH
4700 WH=L:HA=-1
4710 FO=0:FORX=1TONM:IFS$<>M$(X)ANDLEFT$(M$(X),LEN(S$))<>S$ANDRIGHT$(M$(X),LEN(S$))<>S$THEN4750
4720 IFM(X,5)=WH ORM(X,5)=HA THENFO=1:RETURN
4750 NEXT:RETURN

REM ARTIF SEARCH
4800 WH=L:HA=-1
4810 FO=0:FORA=1TONA:IFA$(A)<>S$ANDLEFT$(A$(A),LEN(S$))<>S$ANDRIGHT$(A$(A),LEN(S$))<>S$THEN4850
4820 IFA(A,4)<>WH ANDA(A,4)<>HA THEN4850
4830 FO=1:CC=A:A=NA:NEXTA:A=CC:RETURN
4850 NEXTA:RETURN

REM ROUTINE TO GET S$ IF NOT SPECIFIED.
4900 IFS$=""THENPRINTC$(C);" WHAT";:INPUTS$:GOTO4900
4910 RETURN

REM DROP COMMAND
5000 GOSUB4900:IFS$="ALL"THEN5100
5030 FORA=1TONA
5031 IF(A$(A)<>S$ANDLEFT$(A$(A),LEN(S$))<>S$ANDRIGHT$(A$(A),LEN(S$))<>S$)ORA(A,4)<>-1THENNEXT:GOTO9090
5040 WT=WT-A(A,3):A(A,4)=L:M$=A$(A)+" DROPPED.":GOSUB1
5050 IFWT>10*M(0,1)THENM$="YOU SUDDENLY FIND YOU CANNOT CARRY ALL YOUR ITEMS AND THEY FALL TO THE GROUND.":GOSUB1:GOTO5100
5060 IFM(0,9)>0THENIFA(M(0,9),4)<>-1THENM(0,9)=-1
5070 GOTO800
5100 FORA=1TONA:IFA(A,4)=-1THENM$=A$(A)+" DROPPED.":GOSUB1:A(A,4)=L
5110 NEXTA:WT=0:GOTO5060

REM LOOK COMMAND 4000
6000 LK=0:IFS$=""THEN6070
6020 FORA=1TONA:IFA$(A)=S$AND(A(A,4)=L ORA(A,4)=-1)THENGOSUB6200:LK=1:A(A,9)=1
6025 IFA$(A)=S$ANDA(A,4)=L THENLK=1:A(A,4)=L
6030 NEXTA:IFLK THEN800
6051 GOTO6150
6070 Z5=L:GOSUB400:FORX=1TOND
6071 IFRD(X)<0ANDRD(X)>=-NR THENM$="YOU FOUND A SECRET PASSAGE "+C$(X)+"!":GOSUB1:RD(X)=ABS(RD(X)):V(L)=2
6080 NEXT
6090 FORA=1TONA:IFA(A,4)=L THENA(A,9)=L:LK=1
6100 NEXT:IFLK THENPRINT"YOU FOUND SOMETHING."
6150 V(L)=V(L)+1:GOTO800

REM READ ART DESC
6200 IFA=3ORA=13THENM$="THE BOTTLE SAYS, 'HEALING POTION'":GOTO1
6310 M$="NOTHING UNUSUAL.":GOTO1

REM ATTACK
7000 GOSUB4900:GOSUB4700:IFFO=.THEN7150
7120 M=X:GOTO7290
7150 WH=L:HA=L:GOSUB4810:IFFO=.THENPRINT"ATTACK WHO?":GOTO600
7160 IFA(A,2)<>8ANDA(A,2)<>4THENPRINT"THAT WOULD BE STUPID.":GOTO600
7170 S=7:K=6:IFA(A,2)=4THENS=S-1:K=K-1
7180 D2=0:FORX=1TOM(0,11):D2=D2+INT(RND(0)*M(0,12)+1):NEXT
7181 A(A,S)=A(A,S)-D2:IFA(A,S)<=0THENA(A,S)=0:A(A,K)=0:M$="THE "+A$(A)+" SHATTERS!":GOTO8
7190 M$="WHAM!! YOU HIT THE "+A$(A)+"!":GOTO8
7290 IFM(0,9)=-1THENM$="YOU HAVE NO WEAPON READY!":GOTO9
7300 OF=0:DF=M:GOSUB7500:IFHI=.THEN7360
7310 IFI0THENW2=A(M(0,9),6):WA(W2)=WA(W2)+2
7320 IF-EA<=AETHEN800
7330 IFINT(100*RND(0)+1)<=M(0,10)THEN7360
7340 A=2:IFEA+AE=-1THENA=1
7350 AE=AE+A:M(0,10)=M(0,10)+A
7360 IFM(DF,14)=1ORDF=0THEN800
7370 M(DF,3)=M(DF,3)/2:FORM=1TONM:IFM(M,5)=L THENIFM(M,14)>1THENM(M,14)=0
7380 NEXTM:R3=L:GOSUB3600:GOTO800

REM MONSTER TRIES TO PICK UP WEAPON
7400 IFM(OF,10)=0THENRETURN
7420 FORA=1TONA:IFA(A,4)<>L ORA(A,2)<2ORA(A,2)>3THENNEXT:RETURN
7430 M$=M$(OF)+" PICKS UP "+A$(A)+".":GOSUB1:A(A,4)=0:M(OF,9)=A:M(OF,10)=M(OF,10)+A(A,5):M(OF,4)=M(OF,4)*2:RETURN
7500 HI=0:IFM(OF,9)=-1THENRETURN
7505 M$=M$(OF)+" ATTACKS "+M$(DF)+"--":GOSUB1
7510 RL=RND(100):HI=0:IFRL<5ORRL<M(OF,10)THENHI=1
7515 IFHI THEN7600
7520 IFRL<97THENPRINT"A MISS.":RETURN
7525 PRINT"A FUMBLE!":FR=RND(100):IFFR<=35+40*-(M(OF,9)=.)THENPRINT" FUMBLE RECOVERED.":RETURN
7530 IFFR<76THENPRINT"WEAPON DROPPED!":A(M(OF,9),4)=L:M(OF,9)=-1:RETURN
7535 IFFR>95THEN7550
7540 PRINT" WEAPON BROKEN!":A(M(OF,9),4)=0:M(OF,10)=M(OF,10)-A(M(OF,9),5):M(OF,9)=-1:M(OF,4)=M(OF,4)/2:IFRND(0)>.5THENRETURN
7545 PRINT" BROKEN WEAPON HURTS USER!"
7550 IFFR=100THEND=M(OF,11)*2:S=M(OF,12):DF=OF:A=0:GOTO7635
7555 D=M(OF,11):S=M(OF,12):DF=OF:A=1:GOTO7635
7600 I0=-(RND(100)>M(OF,10)):M(OF,10)=M(OF,10)+2*I0
7605 D=M(OF,11):S=M(OF,12):A=1:IFRL>5THENPRINT"A HIT!":GOTO7635
7610 PRINT"A CRITICAL HIT!":RL=RND(100):IFRL<51THENA=0:GOTO7635
7615 IFRL<86THENS=1.5*S:GOTO7635
7620 IFRL<96THEND=2*D:GOTO7635
7625 IFRL<100THEND=3*D:GOTO7635
7630 GOTO7700
7635 D2=0:FORD3=1TOD:D2=D2+INT(S*RND(0)+1):NEXT:D2=D2-A*M(DF,8):IFD2<1THENPRINT" BLOW BOUNCES OFF ARMOUR":RETURN
7640 M(DF,13)=M(DF,13)+D2:IFM(DF,13)>=M(DF,1)THEN7700
7645 A=M(DF,14):FD(A)=FD(A)+D2
7650 M$=M$(DF)+" IS ":ONINT(M(DF,13)*5/M(DF,1)+1)GOTO7655,7660,7665,7670,7675
7655 M$=M$+"STILL IN GOOD SHAPE.":GOTO1
7660 M$=M$+"HURTING.":GOTO1
7665 M$=M$+"IN PAIN.":GOTO1
7670 M$=M$+"VERY BADLY INJURED.":GOTO1
7675 M$=M$+"AT DEATH'S DOOR, KNOCKING LOUDLY.":GOTO1

REM MONSTERS DYING EFFECTS
7700 M$=M$(DF)+" IS DEAD!":GOSUB1:A=M(DF,14):FD(A)=FD(A)+M(DF,1)-M(DF,13)+D2
7701 M(DF,5)=0:IFM(OF,9)>0THENA(M(OF,9),4)=L
7710 IFFD(0)>TD(0)THENNB=0
7720 IFDF=0THENDI=1:GOTO2000
7760 IFDF=10THENR=3:GOSUB23800:A(10,8)=6:V(L)=0
7770 IFDF=7THENM$="AS THE MIMIC DIES, IT ROLLS OVER AND YOU FIND A RING UNDERNEATH IT.":GOSUB1:A(6,4)=L
7900 RETURN

REM FLEE
8000 IFNB=.THENM$="THERE'S NOTHING TO FLEE FROM!":GOTO9
8015 IFM(7,5)=L THENM$="*HELD FAST BY THE MIMIC AND CANNOT FLEE!":GOTO8
8020 FORD=1TO6:IFS$=C$(D)THENV$=S$:S$="FLEE":GOTO3000
8030 NEXT
8040 IFNX=0THENM$="THERE'S NO PLACE TO GO!":GOTO9
8520 D=RND(6):R2=RD(D):IFR2<1ORR2>NR THENNX=0:GOTO8040
8530 GOTO3500

REM GIVE
9000 A2=1:C3=0:IFVAL(S$)>0THENC3=VAL(S$)
9020 FORM=1TONM:IFLEFT$(S$,LEN(M$(M)))=M$(M)ANDM(M,5)=L THEN9050
9030 NEXT:IFA2=1THENA2=2:INPUT"WHO TO GIVE TO";S$:GOTO9020
9040 M$="NOBODY HERE BY THAT NAME!":GOTO9
9050 S$=MID$(S$,LEN(M$(M))+1)
9060 IFLEFT$(S$,1)=" "THENS$=MID$(S$,2):GOTO9060
9065 IFC3>0THEN9500
9068 IFS$=""THEN9085
9070 FORA=1TONA:IFA$(A)=""THEN9080
9075 IFLEFT$(S$,LEN(A$(A)))=A$(A)ANDA(A,4)=-1THEN9095
9080 NEXT
9085 IFA2>2THENA2=3:INPUT"WHAT TO GIVE";S$:GOTO9065
9090 M$="YOU AREN'T CARRYING A "+S$+"!":GOTO9
9095 IFA=M(0,9)THENM(0,9)=-1
9100 PRINT"OKAY.":A(A,4)=-M-1:M(M,14)=0:M(M,3)=M(M,3)*(1+A(A,1)/100)
9110 IFM(M,10)<>0ANDM(M,9)=-1THENM(M,9)=A:M(M,10)=M(M,9)+A(A,5):M(M,4)=2*M(M,4)
9120 R3=L:GOSUB3600:GOTO5050
9500 IFVAL(S$)>GP THENM$="YOU AREN'T CARRYING THAT MUCH GOLD!":GOTO8
9510 GP=INT(GP-VAL(S$)):M(M,0)=M(M,0)+INT(VAL(S$)):M(M,14)=0:M(M,3)=M(M,3)*(1+VAL(S$)/100):GOTO9120

REM INVENTORY
10000 PRINT"*CARRYING--":A0=0:FORA=1TONA
10010 IFA(A,4)=-1THENM$=A$(A)+LEFT$(" (READY WEAPON)",1+20*-(A=M(0,9))):GOSUB1:A0=A0+1:IFA0=14THENA0=0:GOSUB10
10020 NEXT:GOTO800

REM BLAST SPELL
11000 GOSUB4900:FORM=1TONM:IFS$<>M$(M)ORM(M,5)<>L THENNEXT:M$="YOU CAN'T BLAST "+S$+"!":GOTO9
11480 S=1:GOSUB11500:IFSU=.THEN800
11490 PRINT"DIRECT HIT!":S=6:D=1:DF=M:A=0:GOSUB7635:GOTO800

11500 SU=0:IFSA(S)THENRL=RND(100):SU=-(((RL<=S2(S))OR(RL<5))AND(RL<95))
11510 IFSU=.THENPRINT"NOTHING HAPPENED.":RETURN
11520 RL=RND(100):IFRL>S2(S)THENS2(S)=S2(S)+2:SA(S)=SA(S)+2
11530 S2(S)=S2(S)/2:RETURN

REM HEAL SPELL
12000 S=2:GOSUB11500:IFSU=.THEN800
12020 IFM(0,13)THENM$="SOME OF YOUR WOUNDS SEEM TO CLEAR UP.":GOSUB1
12030 M(0,13)=M(0,13)-INT(10*RND(0)+1):IFM(0,13)<0THENM(0,13)=0
12040 DF=0:GOSUB7650:GOTO800

REM POWER SPELL
13000 S=4:GOSUB11500:IFSU=.THEN800
13020 RA=0:FORM=1TONM:IFA(M,4)=L ORA(M,4)=-1THENRA=1:GOSUB13200
13030 NEXTM:IFRA THENR3=L:GOSUB3600:GOTO800
13040 FORA=1TONA:IFA(A,4)=L THENRA=1:PRINTA$(A)" VANISHES!":A(A,4)=0
13050 NEXTA:IFRA THEN800
13060 RR=RND(100):IFRR<11THENM$="THE SECTION OF THE CEILING YOU ARE UNDER COLLAPSES.":GOSUB1:DI=1:GOTO2000
13080 IFRR<86THENM$="YOU HEAR A VERY LOUD BOOM THAT ECHOES THROUGH THE TUNNELS.":GOTO8
13090 IFRR>95THENPRINT"ALL OF YOUR HITS ARE HEALED.":M(0,13)=0:GOTO800
13100 GOTO14020

REM RAISE DEAD
13200 M$=M$(M)+" COMES ALIVE!":GOSUB1:A(M2,4)=0:M(M,5)=L:M(M,13)=0:IFA(M2,4)=-1THENWT=WT-A(M2,3)
13290 RETURN

REM SPEED SPELL
14000 S=3:GOSUB11500:IFSU=.THEN800
14020 IFS1=.THENM(0,10)=M(0,10)+2*M(0,2):M(0,2)=2*M(0,2)
14030 S1=S1+INT(25*RND(0)+10):M$="YOU CAN FEEL THE NEW AGILITY FLOWING THROUGH YOU!":GOTO8

REM SMILE COMMAND
15000 FORM=1TONM:IFM(M,5)<>L THEN15100
15020 ONM(M,14)GOTO15030,15040,15050
15030 M$=M$(M)+" GROWLS AT YOU!":GOSUB1:GOTO15100
15040 M$=M$(M)+" IGNORES YOU.":GOSUB1:GOTO15100
15050 M$=M$(M)+" "+V$+"S BACK.":GOSUB1:GOTO15100
15100 NEXTM:GOTO800

REM SAY COMMAND
16000 GOSUB4900
16030 IFS$="BLAST"THENV$=S$:S$="":GOTO11000
16040 IFS$="HEAL"THEN12000
16050 IFS$="POWER"THEN13000
16060 IFS$="SPEED"THEN14000
16070 IFS$="TROLLSFIRE"THEN26000
16900 M$="OKAY, '"+S$+"'":GOTO8

REM READY A WEAPON.
17000 GOSUB4900
17020 FORA=1TONA:IFRIGHT$(A$(A),LEN(S$))<>S$ORA(A,4)<>-1THENNEXT:GOTO9090
17030 IFA(A,2)<2ORA(A,2)>3THENPRINT"THAT ISN'T A WEAPON!":GOTO600
17980 M(0,10)=(EA+AE)*-(-EA>AE)+A(A,5)+WA(A(A,6))+2*M(0,2):M(0,9)=A:M(0,11)=A(A,7):M(0,12)=A(A,8)
17990 PRINT"READIED.":GOTO800

REM LIGHT
19000 GOSUB4900:GOSUB4800:IFFO=.THEN24160
19040 M$="IT WON'T LIGHT!":GOTO9

REM OPEN
20000 GOSUB4900:WH=L:HA=-1:GOSUB4810:IFFO=.THEN20200
20015 IFA=12THENR=1:GOSUB23800:A(12,4)=0:M(7,5)=L:R3=L:GOSUB3600:GOTO600
20020 IFA(A,2)=4THEN20100
20030 IFA(A,2)=7THENPRINT"TRY READING IT.":GOTO600
20040 IFA(A,2)=1THENM$="YOU'LL MAKE A MESS. WAIT TILL YOU LEAVE.":GOTO9
20050 IFA(A,2)=8THEN20300
20060 M$="THAT'S NOT WHAT YOU DO WITH IT.":GOTO9

REM OPEN CONTAINER
20100 IFA(A,5)THENIFA(A(A,5),4)<>-1THENPRINT"IT'S LOCKED!":GOTO600
20120 LK=0:FORX=1TONA:IFA(X,4)=ATHENA(X,4)=L:LK=1
20130 NEXT:IFLK THENPRINT"YOU FOUND SOMETHING INSIDE!"
20140 GOTO800
20200 IFS$="DOOR"ORS$="GATE"THENM$="IT WILL OPEN WHEN YOU APPROACH.":GOTO9
20210 M$="I DON'T KNOW HOW TO OPEN "+S$+".":GOTO9

REM OPEN DOOR/GATE
20300 IFA(A,6)THENIFA(A(A,6),4)<>-1THENPRINT"IT'S LOCKED!":GOTO600
20330 A(A,6)=0:M$="YOU'VE OPENED THE "+A$(A)+".":GOSUB1:FORX=1TOND:IFRD(X)=ATHENRD(X)=A(A,5)
20340 NEXT:GOTO800

REM PUT
21000 IFS$=""THEN21040
21015 FORX=1TOLEN(S$):IFMID$(S$,X,5)=" INTO"ANDX>2THENA$=LEFT$(S$,X-1):B$=MID$(S$,X+6):GOTO21100
21020 IFMID$(S$,X,3)=" IN"ANDX>2THENA$=LEFT$(S$,X-1):B$=MID$(S$,X+3):GOTO21100
21030 NEXT
21040 INPUT"PUT WHAT?";A$:INPUT"PUT INTO WHAT?";B$
21100 S$=A$:GOSUB4800:IFFO=.THEN24160
21110 X=A:S$=B$:GOSUB4800:IFFO=.ORA=X THEN21150
21120 A(X,4)=A:PRINT"OKAY.":GOTO800
21150 PRINT"DON'T BE ABSURD.":GOTO600

REM HEALING POTION
22000 GOSUB4900:IFS$="POTION"THENS$="BOTTLE"
22015 GOSUB4800:IFFO=.THEN24160
22020 IFA(A,2)<>6THENPRINT"YOU CAN'T DO THAT!":GOTO600
22030 IFA(A,7)<=0THENPRINT"THERE ISN'T ANY LEFT.":GOTO800
22040 IFA(A,5)>RND(99)THEN22100
22050 PRINT"NOTHING SEEMS TO HAPPEN.":GOTO800

REM HEAL
22100 A(A,7)=A(A,7)-1:M(0,13)=M(0,13)-A(A,6)
22120 M$="YOUR WOUNDS SEEM TO "+MID$("OPENHEAL",(A(A,6)>0)*-4+1,4)+"!":GOSUB1
22130 GOTO800

REM READ
23000 GOSUB4900:WH=L:HA=-1:GOSUB4810:IFFO=.THENPRINT"THERE IS NOTHING TO READ.":GOTO600
23020 IFA(A,2)=7THEN23200
23090 PRINT"YOU CAN'T READ THAT!":GOTO800

REM READ ARTIF
23200 IFA=9THEN23300
23250 FORX=1TOA(A,6):R=A(A,5)-1+X:GOSUB23800:NEXT:GOSUB10
23290 GOTO800

REM READ BOOK
23300 R=7:GOSUB23800
23320 IFL=26THENR=8:GOSUB23800:DI=2:GOTO2000
23330 R=9:GOSUB23800:DI=1:GOTO2000
23800 Z3=26+R:GOSUB13:M$=A$:GOTO1

REM FREE
24000 GOSUB4900:FORA=1TONA:IFS$<>A$(A)ANDS$<>RIGHT$(A$(A),LEN(S$))THENNEXT:GOTO24160
24020 IFA(A,2)<>10THEN24160
24030 M=A(A,5):K=A(A,6):G=A(A,7):IFA(A,4)<>L THEN24160
24120 IFM(G,5)=L THENM$=M$(G)+" WON'T LET YOU DO THAT!":GOTO8
24130 IFA(K,4)<>-1ANDK<>0THENPRINT"YOU DON'T HAVE THE KEY!":GOTO600
24150 M$="YOU HAVE FREED "+M$(M)+".":GOSUB1:M(M,5)=L:A(A,4)=0:R3=L:GOSUB3600:GOTO800
24160 PRINT"I DON'T UNDERSTAND.":GOTO600

REM ENTER
25000 IFS$="WATER"ORS$="SEA"THENIFL=26THENM$="I'M AFRAID TO GO INTO THE WATER WITHOUT MY LIFE PRESERVER!":GOTO9
25020 IFS$="CAVE"ANDL=1THENV$="S":GOTO3000
25030 M$="HUH? TRY GIVING A PLACE TO GO.":GOTO9

REM TROLLSFIRE
26000 IFA(10,4)<>-1THENPRINT"NOTHING HAPPENS.":GOTO800
26020 IFTR=.THEN26500
26030 R=6:GOSUB23800:TR=0:A(10,8)=6:IFM(0,9)=10THENM(0,12)=6
26040 GOTO800
26500 R=4:GOSUB23800:IFM(0,9)<>10THENR=5:GOSUB23800:D=1:S=5:DF=0:GOSUB7635:GOTO800
26510 TR=1:A(10,8)=10:M(0,12)=10:GOTO800
