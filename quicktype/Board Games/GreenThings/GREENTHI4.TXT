0 CLS:CLEAR200:DIMG,B,I,N,Z,MC,M$,A$,S(26):MC=16384:V=10:M$=" AWFUL GREEN THINGS ":PRINT@6,;:GOSUB1:GOTO15
1 B=1:N=32
2 IFMID$(M$,N,1)<>""ANDMID$(M$,N,1)<>" "THENN=N-1:GOTO2
3 A$=MID$(M$,B,N-B):PRINTA$;:Z=((PEEK(17024)AND1)*256+PEEK(17025))+16383:GOSUB7:B=N+1:N=B+31:IFB<=LEN(M$)THEN2
4 RETURN
7 FORG=Z-LEN(A$)+1TOZ:POKEG,PEEK(G)-64:NEXT:RETURN
8 I$="":Z=((PEEK(17024)AND1)*256+PEEK(17025))+MC:POKEZ,255
9 M$=INKEY$:IFM$=""THEN9
10 IFM$=CHR$(13)THEN14
11 IFM$=CHR$(8)THENIFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):PRINTCHR$(8);:POKEZ,32:Z=Z-1:POKEZ,255
12 IFM$<" "ORM$>"Z"THEN9
13 PRINTM$;:POKEZ,PEEK(Z)-64:POKEZ+1,255:Z=Z+1:I$=I$+M$:GOTO9
14 M$=" ":GOSUB1:RETURN
15 POKE17026,255:GOSUB8100:DIMA(26),B(26),E(26),N$,Z(10),W(17),H(2),X(26),WW$(16),LL$(26),K(3)
20 FORI=1TO26:A(I)=0:B(I)=0:E(I)=0:X(I)=0:NEXT
30 N$="B   AcNUbdEPcRW CFkYEgiTfHQSG   fJQZI   eOPTM   LO  B   KM  CK  GI  DV  G   FK  BV  RUW DV  Z   E   IX  "
60 FORI=1TO3:J=RND(26):A(J)=A(J)+1:J=RND(26):B(J)=B(J)+1:J=RND(26):E(J)=E(J)+1:NEXT
70 L=RND(26):IFA(L)+B(L)+E(L)=0THEN70
80 W$="UUUHHHNNOOWWAAAA":FORI=1TO16:W(I)=ASC(MID$(W$,I,1)):NEXT:W(0)=32
81 FORI=1TO3:IFRND(0)<.5THENW(I)=90
90 NEXT:FORI=4TO8:IFRND(0)<.5THENW(I)=W(I)+2
100 NEXT:FORI=13TO14:IFRND(0)<.5THENW(I)=W(I)+23
110 NEXT:FORI=15TO16:W(I)=W(I)+RND(26)-1:NEXT:H(1)=0:H(2)=0:H(0)=0
120 WW$(3)="GAS BOMB":WW$(5)="FUEL CAN":WW$(9)="STUN GUN":WW$(11)="WELDING TORCH"
130 WW$(13)="COMM. BEAMER":WW$(15)="EXTINGUISHER"
140 WW$(1)=WW$(3):WW$(2)=WW$(3):WW$(4)=WW$(5):WW$(6)=WW$(5):FORI=7TO15STEP2:WW$(I+1)=WW$(I):NEXT
150 WW$(7)="GIN AND TONIC"
160 LL$(11)="CORRIDORS":FORI=2TO9:LL$(I)=LL$(11):NEXT
170 LL$(10)="FUEL POD":LL$(12)="COMPUTER ROOM"
180 LL$(13)="CABIN":LL$(15)="BRIDGE":LL$(16)="GALLEY":LL$(14)=LL$(13)
190 LL$(17)="ENGINE ROOM":LL$(18)="HANGAR DECK":LL$(19)="CARGO HOLD"
200 LL$(20)="SCIENCE LAB.":LL$(21)="PROBE CONTROL":LL$(25)="SICK BAY"
210 LL$(22)="PROBE HANGAR":LL$(23)="MACHINE ROOM"
220 LL$(24)="SENSOR ROOM":LL$(26)="LIFE SUPPORT":LL$(1)=LL$(24):LL$(8)=LL$(10)
490 FORI=1TO99:NEXT:GOTO600
500 I=I+1
501 IFPEEK(16895)=128THENPRINT@480,;:M$="MOVE? ":GOSUB1:GOSUB8:GOTO505
502 IFPEEK(16895)=96THENPRINT:INPUT"MOVE";I$
505 IFI$=""ORI$=CHR$(13)THENGOSUB8000:GOTO560
510 IFPEEK(16895)=128THENPRINT@511,"";
512 IFLEFT$(I$,1)<>"Y"THENI=3:GOTO560
513 PRINT"YOU CAN MOVE TO:":FORJ=0TO3:Z=ASC(MID$(N$,1+4*(L-1)+J,1)):K(J)=((Z/32)-INT(Z/32))*32
514 IFK(J)>0THENPRINT"  "LL$(K(J)),"(";MID$(STR$(K(J)),2);")"
517 NEXT
520 INPUT"WHERE TO";J
530 FORK=0TO3:IFK(K)=JANDJ>0THENL=J
540 NEXT:IFJ<>LTHENPRINT"YOU CAN'T MOVE THERE":GOTO520
550 IFA(L)+B(L)+E(L)>0THENI=3
560 IFI<>3THEN500
600 CLS:PRINT"YOU'RE IN THE "LL$(L)" (";MID$(STR$(L),2);")"
602 Y=0:FORI=1TO26:Y=Y+5*A(I)+2*B(I)+E(I):NEXT
604 PRINT:PRINT"ALIEN LIFE ENERGY ON BOARD IS":PRINTMID$(STR$(Y),2)" UNITS"
608 Y=A(L)+B(L)+E(L):IFY=0THEN630
610 PRINT:PRINT"THERE ARE"A(L)"BIG GREEN THINGS,":PRINTMID$(STR$(B(L)),2)" LITTLE GREEN THINGS, AND"
615 PRINTMID$(STR$(E(L)),2)" EGGS HERE"
630 PRINT:INPUT"DO YOU WISH TO ATTACK";Z$:IFLEFT$(Z$,1)<>"Y"THEN800
635 IFH(1)+H(2)=0THENI=0:GOTO680
640 PRINT"ENTER 0 FOR HAND-TO-HAND"
650 FORI=1TO2:IFH(I)>0THENPRINT"     "I"FOR "WW$(H(I))
660 NEXT:INPUT"WHICH";I:IFI>0ANDH(I)=0ORI>2THENPRINT"NO !":GOTO630
670 IFI<1ORI>2THENI=0:H=0
680 I=H(I):IFI=0THENIFY>0THEN770
690 G=I:IFI<7THENIFI>0THEN1300
700 I=(I+1)AND30:IFX(I)=0THENX(I)=RND(5)
705 J=X(I):IFY=0THENPRINT"NOTHING TO ATTACK":GOTO800
710 IFJ=1THENF=L:FORC=1TOY:GOSUB1100:NEXT:PRINT"THEY DON'T LIKE IT!":GOTO762
720 IFJ=2THENPRINT"NOTHING HAPPENS":GOTO762
730 IFJ=3THENI=L:GOSUB1000:PRINT"THEY GREW!":GOTO762
740 IFJ=4THENE(L)=B(L):B(L)=A(L):A(L)=0:PRINT"THEY SHRUNK!":GOTO762
750 A(L)=0:B(L)=0:E(L)=Y:FORI=1TOY:E(L)=E(L)+RND(6)-1:NEXT
760 PRINT"THEY BROKE INTO BITS!"
762 IFG>8THEN800
764 I=9:GOTO1420
770 IFY>3THENY=INT(Y*2/3)
780 F=L:FORC=1TOY:GOSUB1100:NEXT:PRINT:PRINT"YOU'RE HURTING THEM!"
800 Y=A(L)*16+B(L)*11+E(L)*5:IFY>0THENIFRND(Y)-1>80THEN1200
810 Y=0:FORI=1TO26:Y=Y+A(I)+B(I)+E(I):GOSUB1000:NEXT:IFY=0THEN1600
820 IFA(L)+B(L)+E(L)=0THENPRINT"NO AWFUL GREEN THINGS HERE"
900 FORI=1TO16:IFW(I)<>L+64THEN990
910 PRINT:PRINTWW$(I)" IS HERE.":INPUT"TAKE";Z$:IFLEFT$(Z$,1)<>"Y"THEN990
920 IFH(1)=0THENH(1)=I:W(I)=0:GOTO990
930 IFH(2)=0THENH(2)=I:W(I)=0:GOTO990
940 PRINT"YOU CAN'T HOLD IT. YOU ARE":PRINT"HOLDING A "WW$(H(1))
950 PRINT"AND A "WW$(H(2)):INPUT"DROP WHICH (0/1/2)";J
960 IFJ=1ORJ=2THENW(H(J))=L+64:H(J)=0:GOTO920
990 NEXT:GOTO500
1000 Q=RND(26):IFRND(V*2-1)-V>4THENE(I)=E(I)+A(I):RETURN
1010 IFRND(0)>.5THENB(I)=B(I)+E(I):E(I)=0:RETURN
1020 IFRND(V*2-1)-V>3THENA(Q)=A(Q)+B(I):B(I)=0:RETURN
1030 RETURN
1100 K=RND(35)-1:IFA(F)>0THENIFK>16THENA(F)=A(F)-1
1110 IFB(F)>0THENIFK>11THENB(F)=B(F)-1:RETURN
1120 IFE(F)>0THENIFK>4THENE(F)=E(F)-1
1130 RETURN
1200 PRINT:PRINT"THEY KILLED YOU.":PRINT"HARD LUCK.":PRINT
1210 INPUT"ANOTHER GAME";Z$
1220 IFLEFT$(Z$,1)="Y"THEN20
1230 END
1300 G=I:I=I+2:I=I-((I/3)-INT(I/3))*3:IFX(I)=0THENI=RND(5)
1310 J=X(I):INPUT"THROW INTO WHICH LOCATION";D
1320 IFD<1ORD>26THENPRINT"TRY AGAIN":I=G:GOTO1300
1330 D=D+96
1340 IFD=ASC(MID$(N$,1+4*(L-1)+0,1))THEN1500
1341 IFD=ASC(MID$(N$,1+4*(L-1)+1,1))THEN1500
1342 IFD=ASC(MID$(N$,1+4*(L-1)+2,1))THEN1500
1343 IFD=ASC(MID$(N$,1+4*(L-1)+3,1))THEN1500
1345 D=D-32:IFD=ASC(MID$(N$,1+4*(L-1)+0,1))THEN1355
1346 IFD=ASC(MID$(N$,1+4*(L-1)+1,1))THEN1355
1347 IFD=ASC(MID$(N$,1+4*(L-1)+2,1))THEN1355
1348 IFD=ASC(MID$(N$,1+4*(L-1)+3,1))THEN1355
1350 I=G:GOTO1300
1355 D=D-64:Y=A
1360 IFJ=1THENF=D:FORC=1TOY:GOSUB1100:NEXT:PRINT"THEY DON'T LIKE IT!":GOTO1420
1370 IFJ=2THENPRINT"NOTHING HAPPENS"GOTO1420
1380 IFJ=3THENI=D:GOSUB1000:PRINT"THEY GREW!"GOTO1420
1390 IFJ=4:E(D)=B(D):B(D)=A(D):A(D)=0:PRINT"THEY SHRUNK!":GOTO1420
1400 A(D)=0:B(D)=0:E(D)=Y:FORI=1TOY:E(D)=E(D)+RND(6)-1:NEXT
1410 PRINT"THEY BROKE INTO BITS!"
1420 IFH(1)=GTHENH(1)=0
1430 IFH(2)=GTHENH(2)=0
1440 IFI=3THENW(G)=85+5*(RND(2)-1)
1450 IFI=6THENW(G)=72+2*(RND(2)-1)
1455 IFI=9THENW(G)=78+2*(RND(2)-1)
1460 GOTO800
1500 IFI=3THENPRINT:PRINT"hiss"
1510 IFI<>3THENPRINT:PRINT"boom"
1520 PRINT:PRINT"YOU FOOL-- YOU'VE KILLED":PRINT"YOURSELF !":PRINT:PRINT:GOTO1210
1600 PRINT:PRINT:PRINT"WELL DONE! THEY'RE ALL DEAD.":PRINT:GOTO1210
8000 CLS0
8001 PRINT"";
8002 PRINT"";
8003 PRINT"";
8004 PRINT"";
8005 PRINT"";
8006 PRINT"";
8007 PRINT"";
8008 PRINT"1";
8009 PRINT"";
8010 PRINT"";
8011 PRINT"";
8012 PRINT"";
8013 PRINT"";
8014 PRINT"";
8015 PRINT"";
8016 FORI=1TO26:PRINT@S(I),;:M$=MID$(STR$(I),2):GOSUB1:NEXT
8017 PRINT@S(L),;:M$=MID$(STR$(L),2):PRINTM$;:PRINT@480,;:RETURN
8100 S(1)=225:S(2)=231:S(3)=239:S(4)=337:S(5)=244:S(6)=248:S(7)=123:S(8)=61
8110 S(9)=379:S(10)=445:S(11)=147:S(12)=165:S(13)=104:S(14)=170:S(15)=47
8120 S(16)=175:S(17)=252:S(18)=432:S(19)=55:S(20)=150:S(21)=293:S(22)=393
8130 S(23)=299:S(24)=438:S(25)=341:S(26)=441:RETURN
