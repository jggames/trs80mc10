0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO7000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINT@CC,"°Ø¢";:PRINT@CC+32,"À¨√";:RETURN
4 PRINT@CC,"±ø≤";:PRINT@CC+32,"Àº√";:RETURN
5 PRINT@CC," ¿¿";:PRINT@CC+32,"À√√";:RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:RETURN
10 DIMM(4),MO(7),KN(2):GOSUB1000
15 DX=1:DY=0:XX=DX:YY=DY:PT=2:GOSUB200
18 KN(0)=12:KN(1)=12:M(0)=0
30 FORI=0TO1:FL=0:VL=-(I=0)*5-(I=1)*10
35 PRINT@24,"":PRINT@24,PL$(I);
36 IFCFTHEN45
38 IFM(0)=0THEN?@1*32+24,"":?@2*32+24,"":GOTO45
40 ?@1*32+24,CHR$(95)" ^ / "CHR$(92);
41 ?@2*32+24,MID$(STR$(M(0))+" ",2,2)MID$(STR$(M(1))+" ",2,2)MID$(STR$(M(2))+" ",2,2)MID$(STR$(M(3))+" ",2,2);
45 PRINT@XX*3+2+32*((YY*2)+1),"Ô";
50 M$=INKEY$:IFM$=""THEN50
51 DX=XX:DY=YY
52 IFM$=CHR$(13)THENIFFL<>1THENFL=1:KN=XX+1+10*YY:GOSUB300:GOTO35
53 IFM$=CHR$(13)THENIFFL=1THEN115
54 IFM$="W"THENDY=YY-1
55 IFM$="S"THENDY=YY+1
56 IFM$="A"THENDX=XX-1
57 IFM$="D"THENDX=XX+1
60 IFDX<0ORDX>7THEN50
65 IFDY<0ORDY>7THEN50
85 PRINT@XX*3+2+32*((YY*2)+1),"√";
88 XX=DX:YY=DY
90 GOTO45
115 GOSUB550:GOTO320
120 SOUND1,1
135 ?@4*32+24,"YOU MUST";:?@5*32+24,"MOVE!";:GOSUB520:FORZZ=1TO2500:NEXT:?@4*32+24,"":?@5*32+24,""
140 FL=0:RETURN
195 NEXTI:GOTO30
200 IFASC(MID$(R$,PT,1))=0THENFORQ=0TO4:M(Q)=0:NEXTQ:RETURN
203 TP=INT(PT/10):UP=PT-TP*10
205 CN=0:K3=TP*10+1:K4=TP*10+8:K5=1:GOSUB250:M(0)=CN
210 CN=0:K3=UP:K4=UP+70:K5=10:GOSUB250:M(1)=CN
215 T1=TP:UT=UP:IFTP+UP>9THENT1=8-UP:UT=8-TP
220 CN=0:K3=PT-T1*9:K4=PT+(UT-1)*9:K5=9:GOSUB250:M(2)=CN
225 T1=TP:UT=UP:IFUP>TPTHENUT=TP+1:T1=UP-1
230 CN=0:K3=PT-(UT-1)*11:K4=PT+(7-T1)*11:K5=11:GOSUB250:M(3)=CN
235 RETURN
250 FORK2=K3TOK4STEPK5:IFASC(MID$(R$,K2,1))<>0THENCN=CN+1
260 NEXTK2
270 RETURN
300 GOSUB500
302 IFI=0ANDASC(MID$(R$,KN,1))<>5THEN135
303 IFI=1ANDASC(MID$(R$,KN,1))<>10THEN135
305 PT=XX+1+10*YY:GOTO200
320 K2=XX+1+10*YY:IFASC(MID$(R$,K2,1))=VLANDKN=K2THENGOSUB520:FL=0:GOTO35
322 IFABS(K2-KN)=M(0)THEN340
324 IFABS(K2-KN)=M(1)*10THEN340
326 IFABS(K2-KN)=M(2)*9THEN340
328 IFABS(K2-KN)=M(3)*11THEN340
338 ?@4*32+24,"ILLEGAL";:?@5*32+24,"MOVE!";:GOSUB520:GOSUB600:FL=0:FORZZ=1TO2500:NEXT
339 ?@4*32+24,"":?@5*32+24,"":GOTO35
340 VL=-(I=0)*5-(I=1)*10:K4=K2-KN
342 IFK4/11=INT(K4/11)THENK5=11:GOTO350
344 IFK4/10=INT(K4/10)THENK5=10:GOTO350
346 IFK4/9=INT(K4/9)THENK5=9:GOTO350
348 K5=1
350 IFK2<KNTHENK5=-K5
353 FORQ=KNTOK2-K5STEPK5:IFASC(MID$(R$,Q,1))<>VLANDASC(MID$(R$,Q,1))<>0THENQ=K2-K5:NEXTQ:GOTO390
355 NEXTQ
358 IFASC(MID$(R$,K2,1))=VLTHEN391
360 IFASC(MID$(R$,K2,1))<>0THENKN(-(I=0))=KN(-(I=0))-1
363 ZZ=I:P=INT(K2/10):X=(K2-P*10):Y=P:GOSUB2300
365 ZZ=2:P=INT(KN/10):X=(KN-P*10):Y=P:GOSUB2300
368 R$=LEFT$(R$,KN-1)+CHR$(0)+MID$(R$,KN+1):R$=LEFT$(R$,K2-1)+CHR$(VL)+MID$(R$,K2+1)
370 M1=0:VL=5:GOSUB400:M1=1:VL=10:GOSUB400:M(0)=0:GOTO195
390 ?@4*32+24,"CANNOT";:?@5*32+24,"JUMP";:?@6*32+24,"OPPONENT";:GOSUB520:GOSUB600:FL=0:GOSUB395:GOTO35
391 ?@4*32+24,"CANNOT";:?@5*32+24,"LAND ON";:?@6*32+24,"OWN MAN";:GOSUB520:GOSUB600:FL=0:GOSUB395:GOTO35
395 FORZZ=1TO2500:NEXT:?@4*32+24,"":?@5*32+24,"":?@6*32+24,"":RETURN

400 J=1:S$=R$:CN=1
403 IFASC(MID$(S$,J,1))<>VLTHENJ=J+1:GOTO403
405 K=0
412 P1=J+MV(K):IFP1<1THEN450
415 K2=ASC(MID$(S$,P1,1)):IFK2<>VLTHEN450
420 S$=LEFT$(S$,J-1)+CHR$(VL+K*16)+MID$(S$,J+1):CN=CN+1:J=P1:GOTO405
450 K=K+1:IFK<8THEN412
455 S$=LEFT$(S$,J-1)+CHR$(VL+112)+MID$(S$,J+1):IFCN>=KN(M1)THENONM1+1GOTO700,800
460 FORK=0TO7:P1=J+MV(K):IFP1<1THEN480
463 K2=ASC(MID$(S$,P1,1)):IFK2=0THEN480
465 IFK2=VL+112THEN480
468 IFK2<11THEN480
470 K=INT(K2/16)+1:J=P1:GOTO412
480 NEXTK:RETURN

500 SOUND100,1:FORI1=1TO188:NEXTI1:SOUND200,1
503 IFASC(MID$(R$,KN,1))<>VLTHENFL=0:RETURN
510 PRINT@XX*3+2+32*((YY*2)+1)-2,"*";:RETURN
520 SOUND50,2
550 SOUND220,2
600 P=INT(KN/10):X=(KN-P*10):Y=P:PRINT@(X-1)*3+32*((Y*2)+1),"À";
610 PRINT@XX*3+2+32*((YY*2)+1)-2,"À";:RETURN

700 ?@0*32+24,PL$(1);:?@1*32+24,"IS THE";:?@2*32+24,"WINNER!":SOUND200,5
710 GOTO805
800 ?@0*32+24,PL$(2);:?@1*32+24,"IS THE";:?@2*32+24,"WINNER!":SOUND200,5
805 FORZZ=1TO2500:NEXT
810 ?@4*32+24,"PLAY":?@5*32+24,"AGAIN":?@6*32+24,"(Y/N)?"
820 M$=INKEY$:IFM$="Y"THENRUN
830 IFM$="N"THENEND
840 GOTO820

1000 GOSUB4000
1005 RESTORE
1010 FORI=0TO7:READA:MV(I)=A:NEXT I
1020 DATA-11,-1,9,10,11,1,-9,-10
1200 ? "BLUE PLAYER, ENTER YOUP NAME":INPUTM$:IFLEN(M$)>8THEN1200
1205 IFM$<>""THENPL$(0)=M$
1210 ? "RED  PLAYER, ENTER YOUR NAME":INPUTM$:IFLEN(M$)>8THEN1210
1215 IFM$<>""THENPL$(1)=M$
1250 ? "ADVANCED PLAY (Y,N)";:INPUTM$
1260 IFLEFT$(M$,1)="Y"THENCF=1
1300 GOSUB6000:GOSUB4045:RETURN
2300 CC=(X-1)*3+32*(Y*2):ONZZ+1GOTO3,4,5:STOP
3000 CLS:PRINTTAB(9);:M$="LINES OF ACTION":GOSUB7:PRINT@1*32,;
3015 M$="THIS IS A VERY INTRICATE GAME. THE OBJECT IS TO POSITION ALL YOUR PIECES "
3020 M$=M$+"IN SUCH A MANNER THAT THEY TOUCH ONE ANOTHER, REGARDLESS OF THE ACTUAL "
3025 M$=M$+"FORMATION OR THE NUMBER OF MEN LEFT ON THE BOARD.":GOSUB1
3030 M$="THINK OF YOUR MEN AS PIECES OF A CHAIN. YOUR JOB IS TO CONNECT ALL "
3035 M$=M$+"THE LINKS.":GOSUB1:GOSUB3055:M$="THE LENGTH OR SHAPE OF THE CHAIN IS IRRELEVANT.":GOSUB1
3040 M$="IN FACT, THE MORE LINKS YOU HAVE, THE MORE DIFFICULT YOUR JOB BECOMES.":GOSUB1
3045 M$="FOR EXAMPLE, IF YOU ONLY HAVE ONE PIECE LEFT, YOU AUTOMATICALLY WIN!":GOSUB1
3050 M$="YOUR MOVEMENTS ARE GOVERNED BY CERTAIN RULES.":GOSUB1:GOSUB3055:GOTO3070
3055 PRINT@480,;:M$="HIT ANY KEY":GOSUB7
3060 M$=INKEY$:IFM$=""THEN3060
3065 CLS:RETURN
3070 M$="ALTHOUGH YOU MAY MOVE VERTICALLY, HORIZONTALLY, DIAGONALLY, FORWARD "
3080 M$=M$+"AND IN REVERSE, YOUR LIMITS ARE AS FOLLOWS:":GOSUB1
3085 M$="  1. THE NUMBER OF SQUARES YOUR PIECE CAN MOVE IS DETERMINED BY ITSELF "
3090 M$=M$+"ADDED TO HOWEVER MANY OTHER PIECES ARE IN THE SAME ROW OR COLUMN.":GOSUB1
3092 M$="AFTER YOU INDICATE WHICH PIECE YOU INTEND TO MOVE, THE COMPUTER WILL "
3094 M$=M$+"DISPLAY ITS VARIOUS AVAILABLE MOVES.":GOSUB1:GOSUB3055
3095 M$="  2. YOU MAY jump OVER YOUR MEN, BUT NOT OVER YOUR OPPONENT'S.":GOSUB1
3100 M$="  3. YOU ARE ALLOWED TO land ON YOUR OPPONENT'S PIECE (NOT YOURS). THIS ELIMINATES THAT PIECE "
3105 M$=M$+" FROM THE GAME.":GOSUB1
3110 GOSUB3055
3125 M$="IN THE ADVANCED-PLAY MODE, THE ALLOWABLE MOVES FOR THE VARIOUS PIECES WILL BE SHOWN.":GOSUB1
3130 M$="POSITION THE CURSOR ABOVE THE MAN YOU WISH TO MOVE AND PRESS THE enter.":GOSUB1
3131 M$="A GREEN ASTERISK WILL APPEAR AT YOUR CURRENT POSITION.":GOSUB1
3135 M$="THEN MOVE THE CURSOR TO THE SQUARE WHICH YOU WANT TO LAND ON AND "
3140 M$=M$+"PRESS enter AGAIN.":GOSUB1:GOSUB3055:M$="IF YOU CHANGE YOUR MIND, SIMPLY LAND ON "
3142 M$=M$+"THE SAME MAN YOU STARTED OUT WITH AND PRESS enter.":GOSUB1
3143 M$="YOU MAY THEN MAKE A DIFFERENT MOVE.":GOSUB1
3144 M$="IF YOUR MOVE IS ILLEGAL, THE COMPUTER WILL GIVE YOU THE REASON WHY.":GOSUB1
3145 M$="USE wasd KEYS TO MOVE THE PURPLE CURSOR.":GOSUB1
3148 M$="THINK AHEAD BEFORE MOVING.":GOSUB1:PRINT"GOOD LUCK!"
3150 GOSUB3055:RETURN
4000 CLS0:POKE49151,64:CLS:POKE17026,207:RETURN
4045 GOSUB8000
4100 FORX=2TO7:R$=LEFT$(R$,X-1)+CHR$(5)+MID$(R$,X+1):NEXTX
4110 FORX=72TO77:R$=LEFT$(R$,X-1)+CHR$(5)+MID$(R$,X+1):NEXTX
4120 FORX=11TO61STEP10:R$=LEFT$(R$,X-1)+CHR$(10)+MID$(R$,X+1):NEXTX
4130 FORX=18TO68STEP10:R$=LEFT$(R$,X-1)+CHR$(10)+MID$(R$,X+1):NEXTX
4300 RETURN
6000 CLS0:POKE49151,16:CLS:RETURN
7000 AG=0
7003 DIMP(8)
7004 M$=CHR$(0):FORCC=1TO120:R$=R$+M$:NEXT
7005 DIMPL$(1):PL$(0)="PLAYER 1":PL$(1)="PLAYER 2"
7006 IFAG<>0THENRETURN
7007 GOSUB4000:PRINTTAB(9);:M$="LINES OF ACTION":GOSUB7:PRINT@1*32,;
7008 PRINT:PRINTTAB(10)"BY SOL GUBER":PRINT:PRINT"BASED ON A GAME BY CLAUDE SOUCIE"
7009 PRINT"DO YOU NEED INSTRUCTIONS";:INPUTM$:IFLEFT$(M$,1)="Y"THENGOSUB3000
7010 GOTO10
8000 CLS
8001 PRINT" Ä¿°Ø¢°Ø¢°Ø¢°Ø¢°Ø¢°Ø¢ ÄÄèèèèèèèè";
8002 PRINT"À√√À¨√À¨√À¨√À¨√À¨√À¨√À√√èèèèèèèè";
8003 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èèèèèèèè";
8004 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèèèèèè";
8005 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èèèèèèèè";
8006 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèèèèèè";
8007 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èèèèèèèè";
8008 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèèèèèè";
8009 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èLINESèè";
8010 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèOFèèè";
8011 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èACTIONè";
8012 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèèèèèè";
8013 PRINT"±ø≤ Ä¿ ¿¿ ¿¿ ¿¿ ÄÄ ¿¿±ø≤èèèèèèèè";
8014 PRINT"Àº√À√√À√√À√√À√√À√√À√√Àº√èèèèèèèè";
8015 PRINT" Ä¿°Ø¢°Ø¢°Ø¢°Ø¢°Ø¢°Ø¢ ÄÄèèèèèèèè";
8016 PRINT"À√√À¨√À¨√À¨√À¨√À¨√À¨√À√√";:RETURN
8020 REM MC-10 EDITS JIM GERRIE
