0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO5
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 GOSUB2000
10 REM SNAKES AND LADDERS
20 DIM BD(100)
30 CLS1:PRINT TAB(7);"SNAKES AND LADDERS"
40 PRINT:PRINT"YOU MUST START ON A SIX"
50 PRINT"YOU GET AN EXTRA GO FOR A SIX"
60 PRINT"YOU MUST END EXACTLY ON 100"
70 PRINT
80 INPUT"FIRST PLAYER";PL$(0)
90 INPUT"SECOND PLAYER";PL$(1)
100 REM SET UP THE BOARD
110 FOR Z=1 TO 100:BD(H)=0:NEXT
120 FOR Z=1 TO 25
130 H=RND(99):IF BD(H)<>0 THEN 130
140 H2=80-RND(160):IF H2+H<1 OR H2+H>99 THEN 140
150 IF BD(H2+H)<>0 THEN 140
160 BD(H)=H2:BD(H+H2)=999
170 NEXT:CLS1
180 FOR Z=0 TO 3:PP(Z)=1:NEXT
190 FOR Z=1 TO 100:IF BD(Z)=999 THEN BD(Z)=0
200 NEXT
210 S(0)=0:S(1)=0
220 GOSUB 640
230 G=2-RND(2)
240 REM MAIN LOOP
250 GOSUB 810
260 PRINT PL$(G);" IS ON";PP(G);:PRINT"THROWS ":GOSUB 460
270 T=RND(6):PRINTMID$(STR$(T),2)" ";:GOSUB 460
280 IF S(G)<-1 THEN 480
290 IF S(G)=0 AND T<>6 THEN PRINT"NOT STARTED YET":GOTO 480
291 S(G)=-1
300 PP(G)=PP(G)+T
310 PRINT "AND MOVES TO";PP(G)
320 GOSUB 850
330 IF PP(G)=100 THEN 520
340 IF PP(G)<100 THEN 370
350 PRINT"OH DEAR TOO FAR-- MOVING BACK":GOSUB 460
360 PRINT"TO";:PP(G)=200-PP(G):PRINT PP(G):GOSUB 850
370 REM CHECK HAZARDS
380 IF BD(PP(G))=0 THEN 440
390 IF BD(PP(G))<0 THEN 395
391 GOTO 400
395 PRINT "HISS-- HISS ":GOSUB 570:PRINT"WHEE-- DOWN WE GO TO";
396 GOSUB 460:PP(G)=PP(G)+BD(PP(G)):PRINTPP(G):GOSUB850:GOTO380
400 PRINT"OH LOOK! HERE'S A LADDER"
410 PRINT"UP WE GO TO";:GOSUB 460:PP(G)=PP(G)+BD(PP(G)):PRINTPP(G):GOSUB 850
420 IF RND(6)=1 THEN 425
421 GOTO 430
425 PRINT"OH DEAR-- YOU FELL OFF ":PRINT "YOU MISS TWO TURNS AND GO TO 1"
426 S(G)=-3:PP(G)=1:GOSUB 850:GOTO 480
430 GOTO 380
440 IF T=6 THEN GOSUB 820:PRINT "ANOTHER GO" :GOTO 240
450 GOTO 480
460 FOR DL=1 TO 350:NEXT DL
470 RETURN
480 REM CHANGE OVER PLAYERS
490 G=1-G
500 IF S(G)<-1 THEN S(G)=S(G)+1:GOSUB 820:PRINT PL$(G);" MISSES A TURN ":GOSUB 460:GOTO 490
510 GOTO 240
520 REM PLAYER WINS
530 PRINT PL$(G);" WINS"
540 PRINT:INPUT "ANOTHER GAME";A$
550 IF LEFT$(A$,1)="Y" THEN 100
560 END 
570 REM TEST FOR SNAKE BITE
580 IF RND(10)<>1 THEN RETURN
590 PRINT "AARGH!-- SNAKE GOT YOU"
600 PRINT "SNAKE SERUM IS AT THE HOSPITAL"
610 PRINT"AT SQUARE 43-- MISS THREE TURNS"
620 S(G)=-4:PP(G)=43
630 RETURN
640 REM SR TO DRAW BOARD
650 PP=0
660 FOR Y2=1 TO 3 STEP 2
670 FOR Y3=1 TO 25
680 DY=-1:GOSUB 760
690 NEXT Y3
700 FOR Y3=25 TO 1 STEP -1
710 DY=0:GOSUB 760
720 NEXT Y3
730 NEXT Y2
740 PRINT@Y2*32,"";
750 RETURN
760 PP=PP+1:IF INT (PP/2)<>PP/2 THEN CS=16:GOTO770
761 CS=32
770 IF BD(PP)<0 THEN CS=112
780 IF BD(PP)>0 THEN CS=48
790 PRINT@(Y2+DY)*32+Y3+3,CHR$(143+CS);
800 RETURN
810 PRINT@448,"";:INPUT"HIT enter TO THROW";A$
820 FOR Y=4 TO 14
830 PRINT@Y*32,""
840 NEXT:PRINT@128,"";:RETURN
850 REM SR TO UPDATE BOARD
860 REM PP(G)=NEW PP(G+2)=OLD
870 IF PP(G+2)=<PP(G) THEN DY=1:GOTO 880
871 DY=-1
880 FOR Y2=PP(G+2)+DY TO PP(G) STEP DY
890 BP=Y2-DY:GOSUB 1010
900 IF Y2>100 OR Y2-DY>100 THEN PC=0:GOTO 990
910 IF Y2-DY=PP(1-G) THEN PRINT@BP,MID$(PL$(1-G),1,1);:GOTO 960
920 IF INT(Y2/2)<>Y2/2 THEN PC=32:GOTO 930
921 PC=16
930 IF BD(Y2-DY)<0 THEN PC=112
940 IF BD(Y2-DY)>0 THEN PC=48
950 PRINT@BP,CHR$(143+PC);
960 BP=Y2:GOSUB 1010
970 PRINT@BP,MID$(PL$(G),1,1);
980 SOUND Y2,1
990 NEXT Y2:PRINT@128,"";
1000 PP(G+2)=PP(G):RETURN
1010 BP=BP-1:B2=INT(BP/25):B1=BP-B2*25
1020 IF BP<0 THEN B1=0:B2=0
1030 IF B2/2<>INT(B2/2) THEN B1=24-B1
1040 BP=B2*32+B1+4
1050 RETURN

2000 CLS1:PRINT TAB(7);"SNAKES AND LADDERS":PRINT
2010 M$="A TRADITIONAL GAME FOR TWO PLAYERS.":GOSUB1
2020 M$="THE NON-TRADITIONAL TWIST IS THAT THE SNAKES CAN BITE AND LAND YOU IN HOSPITAL FOR 3 TURNS.":GOSUB1
2030 M$="AND YOU CAN ALSO FALL OFF THE LADDER AND HAVE TO GO BACK TO SQUARE ONE FOR 2 TURNS!":GOSUB1
2035 GOSUB3000
2040 M$="THE PROGRAM DRAWS THE BOARD USING BLUE AND YELLOW CHEQUERED SQUARES.":GOSUB1
2050 M$="SQUARE ONE IS TOP LEFT AND SQUARE 100 IS BOTTOM LEFT.":GOSUB1
2060 M$="NUMBERING IS IN THE TRADITIONAL SEQUENCE OF BACKWARDS ON ALTERNATE ROWS.":GOSUB1
2070 M$="THE LADDERS ARE MARKED IN RED AND THE SNAKES IN ORANGE.":GOSUB1
2080 M$="YOU HAVE TO THROW A 6 TO START AND YOU MUST END EXACTLY ON 100.":GOSUB1
3000 PRINT:PRINT"press any key"
3010 M$=INKEY$:IFM$=""THEN3010
3020 CLS:RETURN
