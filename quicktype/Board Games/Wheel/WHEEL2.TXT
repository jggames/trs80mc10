0 CLEAR500:DIMC1,C2,M$,I$:M=16384:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+M-1:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
10 CLS3:PRINT@32,"ппппппппппппппомммппппппппппппппппппппппппппоаААААадпппппппппппп";
11 PRINT"пппппппппппоАБЗПЕПГАдпппппппппппппппппппппоААПППЕППКАдппппппппппппппппппппаБПЖППЕПОЗЛАеппппппппп";
12 PRINT"пппппппппоАЗП WHEEL ПВдппппппппппппппппппкАПП       ПКаппппппппппппппппппкАГГГ OFПГГГВаппппппппп";
13 PRINT"пппппппппкАПП       ПКапппппппппппппппппппаЕПFORTUNEПаепппппппппппппппппппвАМЗППЕППЖИазппппппппп";
14 PRINT"пппппппппппвАНППЕППИазппппппппппппппппппппппвАДМДМАазппппппппппппппппппппппппгвааагзпппппппппппп";
30 DIMWH$(20),A$(40),H$(40),PL$(8),PM(8),PS(8),C$(6),P$(10),FT(8),BL$(8),U(20),ZG$(25)
40 FORI=1TO20:READ WH$(I):NEXT I:RESTORE:VV=1:ZW=0:TN=0:GOSUB1000
50 C$(1)="TITLE":C$(2)="FICTIONAL CHARACTER":C$(3)="PERSON":C$(4)="EVENT":C$(5)="PLACE":C$(6)="THING"
60 CLSRND(7)+1:M$="NOW WE ARE GOING TO PLAY 'WHEEL OF FORTUNE'. HOW MANY PEOPLE  ARE PLAYING?":GOSUB1
70 GOSUB1090:NP=A:PN=RND(NP)
71 IFNP<1ORNP>8THENPRINT:M$="I'M SORRY, ONLY 1 TO 8 PLAYERSCAN PLAY.":GOSUB1:PRINT"HOW MANY PLAYERS?":GOTO70
80 FORI=1TONP:PRINT"WHAT IS THE NAME OF PLAYER #"STR$(I);:INPUTPL$(I):NEXT
90 M$="EITHER THE COMPUTER CAN CHOOSE THE PHRASE THAT YOU HAVE TO GUESS, OR SOMEONE WHO IS NOT "
91 M$=M$+"PLAYING CAN CHOOSE IT.":GOSUB1:M$="DO YOU WANT THE COMPUTER TO CHOOSE IT?":GOSUB1:GOSUB1110
100 IFB$="Y"THENCC=2:GOSUB210:GOTO170
101 CC=1
110 CLSRND(7)+1:M$="ANYONE WHO IS GOING TO PLAY THE GAME SHOULD NOT LOOK AT THE SCREEN.":GOSUB1
120 M$="ENTER A WORD OR PHRASE, CHOSEN IN ONE OF THE FOLLOWING CATEGORIES:":GOSUB1
121 FORI=1TO6:M$=STR$(I)+"."+C$(I):GOSUB1:NEXTI
130 M$="DO NOT USE ANY PUNCTUATION MARKS OR NUMERALS.":GOSUB1
131 INPUT"WHAT IS YOUR PHRASE";S$:W$=S$:IFLEN(S$)>30THENCLS:PRINT"YOUR PHRASE IS TOO LONG.":GOTO120
140 M$="WHAT IS THE NUMBER OF THE CATEGORY THIS IS IN?":GOSUB1:GOSUB1090
141 CN=A:IFCN<1ORCN>6THENPRINT:PRINT"YOUR CATEGORY MUST HAVE NUMBERFROM 1 TO 6."
142 FORI=1TO6:M$=STR$(I)+"."+C$(I):GOSUB1:NEXTI:GOTO140
150 GOSUB 240
160 REM TAKE TURN
170 GOSUB1030:PRINT@352,;:PRINT"IT IS "PL$(PN)"'S TURN.";
171 PRINT@384,;:M$="HIT ANY KEY TO":GOSUB7:PRINT@416,;:M$="SPIN THE WHEEL":GOSUB7:A$=INKEY$
180 A$=INKEY$:A=RND(0):IFA$=""THEN180
190 GOSUB820:GOSUB980:PRINT@352,BL$(CO);:PRINT@384,BL$(CO);:PRINT@416,BL$(CO);:ONRXGOTO390,400,430,440
200 REM CHOOSE PHRASE
210 TN=TN+1:RESTORE:FOR I=1TO 20:READ A$:NEXT:W$="":N=RND(100)
220 FORJ=1TOTN-1:IFU(J)=NTHENC1=J:J=TN-1:NEXTJ:TN=TN-1:J=C1:GOTO210
221 NEXTJ:U(TN)=N
230 FORI=1TON:READ S$,CN:NEXT:W$=S$
240 NV=0:TL=LEN(W$):SP=0:FOR I=1 TO 30:A$(I)="":NEXT
250 FORI=1TOTL:A$(I)=MID$(W$,I,1):IFA$(I)=" "THENSP=SP+1:SL(SP)=I:GOTO260
251 IF A$(I)="A" OR A$(I)="E" OR A$(I)="I" OR A$(I)="O" OR A$(I)="U" THEN NV=NV+1
260 NEXTI:SL(0)=0:SL(SP+1)=TL+1:W$="":FORI=1TO7:P$(I)="":NEXTI
261 FORI=0TO7:P$(I)="":FOR J=SL(I)+1TOSL(I+1)-1:P$(I)=P$(I)+A$(J):NEXTJ,I
262 FORI=0TOSP+1:W$=W$+P$(I):NEXTI
270 NL=LEN(W$):FORI=1TONL:A$(I)=MID$(W$,I,1):H$(I)="-":NEXTI:C=0:RETURN
280 REM CHECK GUESS
290 IFASC(LG$)>64ANDASC(LG$)<91THEN310
291 PRINT@0,BL$(CO);:PRINT@2,"LETTERS ONLY!!";:PRINT@480,"WHAT IS YOUR GUESS?";:LG$=INKEY$
300 LG$=INKEY$:IFLG$=""THEN300
301 GOTO290
310 PRINT@448,BL$;:IFVV=0THEN320
311 IFLG$="A"ORLG$="E"ORLG$="I"ORLG$="O"ORLG$="U"THENPRINT@416,"NO VOWELS!";:LG$=".":GOTO290
320 D=0:FOR I=1 TO NL:IFA$(I)=LG$THENIFH$(I)<>LG$THENC=C+VV:H$(I)=A$(I):SOUND170,8:GOTO330
321 D=D+1
330 NEXTI:GOSUB1030:RETURN
340 FORI=1TONL
350 H$(I)=A$(I)
360 NEXTI
370 GOSUB1030
380 RETURN
390 PRINT@352,"YOU ARE BANKRUPT!";:SOUND 20,5:SOUND 1,15:PM(PN)=0:FT(PN)=0:GOSUB 780:GOTO 650
400 PRINT@352,"YOU LOSE YOUR TURN!";:SOUND 20,5:SOUND 1,15:IF FT(PN)=0 THEN GOSUB 780:GOTO 650
410 PRINT@384,"YOU HAVE"FT(PN)"FREE TURN";:IFFT(PN)=1THENPRINT".";:GOTO420
411 PRINT"S.";
420 PRINT@416,"DO YOU WANT TO SPIN";:PRINT@448,"AGAIN?";:GOSUB1110
421 IFB$="Y"THENFT(PN)=FT(PN)-1:GOTO680
422 GOTO650
430 PRINT@352,"YOU WIN A FREE SPIN!";:SOUND170,8:FT(PN)=FT(PN)+1:GOSUB780
431 PRINT@480,BL$(CO);:PRINT@483,BL$(CO);:GOTO 190
440 PRINT@352,"GUESS A LETTER.";:GOSUB1070:LG$=INKEY$
450 LG$=INKEY$:IF LG$=""THEN450
451 IFLG$="A"ORLG$="E"ORLG$="I"ORLG$="O"ORLG$="U"THENPRINT@384,"DO NOT GUESS A VOWEL.";:GOTO450
460 GOSUB290:IFD=NLTHENPRINT@352,"THERE ARE NO "LG$"'S.";:SOUND20,5:SOUND1,15
461 ZW=ZW+1:ZG$(ZW)=LG$:IFFT(PN)>0THEN410
462 GOSUB780:GOTO650
470 X=WH*(NL-D):PRINT@352,"YOU WIN $"X".";:PM(PN)=PM(PN)+X:PRINT@384,"YOU NOW HAVE $"PM(PN)".";
480 GOSUB 780:GOSUB 1030:IF NL-C<=NV THEN 710
490 PRINT@352,"DO YOU WANT TO:";:PRINT@384,"   1. GUESS THE ANSWER.";
491 PRINT@416,"   2. BUY A VOWEL.     ";:PRINT@448,"   3. SPIN AGAIN.     ?";:GOSUB1090
500 IF A<1 OR A>3 THEN PRINT@320,"CHOOSE A NUMBER FROM 1 TO 3.":GOTO 490
510 ON A GOTO 520,540,670
520 GOSUB 1030:PRINT@352,"WHAT IS YOUR GUESS";:INPUT GS$:IF GS$=S$ THEN 910
530 PRINT@416,"I'M SORRY, BUT YOUR ANSWER IS   INCORRECT.":GOSUB 780:GOTO 650
540 GOSUB 1030:IF PM(PN)<250 THEN PRINT@320,"YOU DO NOT HAVE ENOUGH MONEY TO BUY A VOWEL.":GOTO 610
550 PRINT@320,"A VOWEL COSTS $250.":PM(PN)=PM(PN)-250:PRINT"YOU HAVE $"PM(PN)"LEFT."
560 PRINT@384,"WHAT VOWEL DO YOU WANT TO BUY?";:LG$=INKEY$
570 LG$=INKEY$:IF LG$=""THEN 570
580 IFLG$<>"A"ANDLG$<>"E"ANDLG$<>"I"ANDLG$<>"O"ANDLG$<>"U"THENGOSUB1030:?@320,"YOU MUST CHOOSE A,E,I,O,OR U.":GOTO560
590 IFASC(LG$)<65ORASC(LG$)>90THENGOSUB1030:?@3,BL$(CO);:?@3,"LETTERS ONLY!!";:?@480"WHAT IS YOUR GUESS?";:GOTO570
600 VV=0:GOSUB 290:VV=1
610 PRINT@384,"DO YOU WANT TO:";:PRINT@416,"  1. GUESS THE ANSWER.";:PRINT@448,"  2. SPIN AGAIN.     ?";:GOSUB 1090
620 IF A<1 OR A>2 THEN GOSUB 1020:PRINT@320"PLEASE ANSWER 1 OR 2.":GOTO 610
630 ON A GOTO 520,680
640 REM NEXT PLAYER
650 PN=PN+1:IF PN>NP THEN PN=1
660 GOTO 170
670 REM SPIN AGAIN
680 GOSUB 1030:PRINT@352,;:M$="HIT ANY KEY TO SPIN THE WHEEL!  ":GOSUB7
690 GOTO 180
700 REM NO MORE CONSONANTS
710 D=0:FOR II=1 TO NL:IF H$(II)="-"THEN D=1
720 NEXT II:IF D=0 THEN 910
730 PRINT@320,;:M$="THERE ARE NO MORE CONSONANTS IN THE PUZZLE. EACH PLAYER WILL NOW GET ONE CHANCE TO GUESS THE "
731 M$=M$+"ANSWER. "+PL$(PN)+" GOES FIRST.":GOSUB1:INPUT"WHAT IS YOUR GUESS";GS$:IFGS$=S$THEN910
740 IFNP=1THEN760
741 FORII=1TONP-1:A=PN+II:IFPN+II>NPTHENA=A-NP
750 GOSUB1030:PRINT@320,"IT IS NOW "PL$(A)"'S":PRINT"TURN TO GUESS."
751 INPUT"WHAT IS YOUR GUESS";GS$:IFGS$=S$THENPN=A:C1=II:II=NP-1:NEXTII:II=C1:GOTO910
752 NEXTII
760 GOSUB340
770 PRINT@2,"THE ANSWER WAS:";:FOR I=1 TO NP:PM(I)=0:NEXT I:GOTO 930
780 PRINT@480,;:M$="HIT ANY KEY TO CONTINUE.":GOSUB7:A$=INKEY$
790 A$=INKEY$:A=RND(0):IFA$=""THEN790
791 RETURN
810 REM SPIN WHEEL
820 PRINT@244,;:M$="=>":GOSUB7:ST=K-14:IFST<1THENST=ST+20
830 FORI=-1+STTOST+6+RND(20):SOUND200,1:FORJ=1TO15:K=J+I
840 IFK>20THENK=K-20:GOTO840
850 PRINT@22+(J-1)*32,WH$(K):NEXTJ,I
860 IFK<8THENK=K+20
870 WH$=WH$(K-7)
880 IFWH$="BANKRUPT"THENRX=1:RETURN
881 IFWH$="LOSE TURN"THENRX=2:RETURN
882 IFWH$="FREE SPIN"THENRX=3:RETURN
883 RX=4
890 WH=VAL(RIGHT$(WH$,3)):RETURN
900 REM WINNER!
910 FOR I=0 TO 8:CLS I:SOUND 170,6:NEXT I:CLS RND(9)-1:IF PM(PN)<100 THEN PM(PN)=100
920 PRINT@169,"YOU'RE RIGHT!!!";:PRINT@264,"YOU WIN $"PM(PN)"!";:PS(PN)=PS(PN)+PM(PN):FOR I=1 TO NP:PM(I)=0:NEXTI
930 IFTN>=20THENB$="N":GOTO940
931 PRINT@388,"DO YOU WANT TO CONTINUE?";:GOSUB1110
940 CLSRND(9)-1:PRINT@200,"SCORES:";:FORI=1TONP:PRINT@202+32*I,PL$(I)TAB(23)"$"PS(I);:NEXTI
950 GOSUB780:CLS RND(9)-1:IF B$<>"Y" THEN END
960 ZW=0:VV=1:PN=PN+1:IF PN>NP THEN PN=1
970 CLSRND(7)+1:IFCC=2THENGOSUB210:GOTO170
971 GOTO110
980 CO=ABS(POINT(60,31)):RETURN
990 REM SET ERASING CHARACTERS
1000 BL$(0)=CHR$(128):FOR I=1TO21:BL$(0)=BL$(0)+CHR$(128):NEXT I
1010 FOR CO=1TO8:BL$(CO)=CHR$(127+16*CO):FOR I=1TO21:BL$(CO)=BL$(CO)+CHR$(127+16*CO):NEXT I:NEXT CO:RETURN
1020 REM DISPLAY PHRASE SUBROUTINE
1030 FOR I=0 TO 7:D$(I)="":NEXT I:FOR I=1 TO LEN(P$(0)):D$(0)=D$(0)+H$(I):NEXT I:IF SP=0 THEN 1050
1040 LG=LEN(P$(0)):FOR J=1 TO SP:FOR I=LG+1 TO LG+LEN(P$(J)):D$(J)=D$(J)+H$(I):NEXT I:LG=LG+LEN(P$(J)):NEXT J
1050 CLS RND(7)+1:PRINT@2,C$(CN);:FOR I=0 TO SP:PRINT@65+32*I,D$(I);:NEXT I:RETURN
1060 REM DISPLAY WRONG GUESSES
1070 IFZW=0THENRETURN
1071 PRINT@416,"INCORRECT GUESSES:";:PRINT@448,"";:FORZI=1TOZW:PRINTZG$(ZI);:IFZI<>ZWTHEN PRINT",";
1080 NEXTZI:RETURN
1090 A$=INKEY$
1100 A$=INKEY$:IFA$=""THEN1100
1101 A=VAL(A$):SOUND200,1:RETURN
1110 B$=INKEY$
1120 B$=INKEY$:IFB$<>"Y"ANDB$<>"N"THEN1120
1121 RETURN
1130 DATA $150,$200,$300,LOSE TURN,$300,$500,$150,FREE SPIN,$100,$250,$400
1131 DATA BANKRUPT,$500,$100,$200,LOSE TURN,$400,$100,$350,FREE SPIN
1140 DATA ALICE IN WONDERLAND,1,WHEEL OF FORTUNE,1,DIFFERENT STROKES,1
1141 DATA ALL IN THE FAMILY,1,HAPPY DAYS,1,THE SWORD IN THE STONE,1
1142 DATA THE FOX AND THE HOUND,1,MY FAIR LADY,1,THE MUSIC MAN,1,FAMILY FEUD,1
1150 DATA MARY POPPINS,2,MICKEY MOUSE,2,OLIVER TWIST,2,LITTLE ORPHAN ANNIE,2
1151 DATA DENNIS THE MENACE,2,DICK TRACY,2,DADDY WARBUCKS,2,DOCTOR DOLITTLE,2
1152 DATA CLARK KENT,2,SNOW WHITE,2
1160 DATA WALT DISNEY,3,MARK TWAIN,3,QUEEN ELIZABETH,3,ANN LANDERS,3
1161 DATA JOHNNY CARSON,3,BOB HOPE,3,JOHN PAUL JONES,3,ADAM AND EVE,3
1162 DATA BENJAMIN FRANKLIN,3,RONALD REAGAN,3
1170 DATA SINGING A SONG,4,PRESIDENTIAL ELECTION,4,DISCOVERY OF AMERICA,4
1171 DATA CALIFORNIA GOLD RUSH,4,COLLEGE GRADUATION,4,SPELLING BEE,4
1172 DATA RECEIVING THE TEN COMMANDMENTS,4,INVENTING THE TELEPHONE,4
1173 DATA FALLING ASLEEP,4,GOING TO SCHOOL,4
1180 DATA NEW YORK CITY,5,UNITED STATES OF AMERICA,5,STATE OF ILLINOIS,5
1181 DATA WALT DISNEY WORLD,5,THE ENGLISH CHANNEL,5,CITY OF CHICAGO,5
1182 DATA ATLANTIC OCEAN,5,MOUNT RUSHMORE,5,SMITHSONIAN INSTITUTION,5,YELLOWSTONE NATIONAL PARK,5
1190 DATA PHONOGRAPH RECORD,6,COLOR COMPUTER,6,TELEVISION SET,6,BARBIE DOLL,6
1191 DATA THEATER TICKET,6,LIGHTNING BUG,6,MAGAZINE SUBSCRIPTION,6,DECK OF CARDS
1192 DATA 6,LIBRARY BOOK,6,AIRPLANE TICKET,6
1200 DATA HUCKLEBERRY FINN,2,WOODY WOODPECKER,2,ANNE FRANK,3,THE THREE MUSKETEERS,1
1201 DATA THE PRINCE  AND THE PAUPER,1,SCOOBY DOO,2,SONG OF THE SOUTH,1,SEWING BASKET,6
1202 DATA THE PHANTOM TOLLBOOTH,1,THE THREE LITTLE PIGS,2
1210 DATA COUNT DRACULA,2,MOTHER GOOSE,2,LITTLE RED RIDING HOOD,2,HANS CHRISTIAN ANDERSON,3
1211 DATA THOMAS ELVA EDISON,3,GEORGE WASHINGTON,3,PAUL REVERE,3,CHRISTOPHER COLUMBUS,3
1212 DATA CHARLIE BROWN,2,BEETLE BAILEY,2
1220 DATA BEDKNOBS AND BROOMSTICKS,1,ESCAPE TO WITCH MOUNTAIN,1,HAUNTED MANSION,5
1221 DATA THE LOVE BUG,1,SING A SONG OF SIXPENCE,1,A CHRISTMAS CAROL,1,DONALD DUCK,2
1222 DATA HALLOWEEN,4,NANCY DREW,2,PECOS BILL,2                   
1230 DATA JACK AND THE BEANSTALK,1,SPACE MOUNTAIN,5,NIAGARA FALLS,5,JACK AND JILL,1
1231 DATA THE WIZARD OF OZ,1,WHEN YOU WISH UPON A STAR,1,SEVENTY SIX TROMBONES,1
1232 DATA RAGGEDY ANN,2,STRAWBERRY SHORTCAKE,2,ICE CREAM CONE,6
1300 REM WHEEL OF FORTUNE
1310 REM BY HAROLD SCHNEIDER
1320 REM MC-10 EDITS JIM GERRIE

