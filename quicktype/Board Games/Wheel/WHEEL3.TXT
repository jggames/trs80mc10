0 CLEAR500:DIMC1,C2,M$,I$,I,J,K:M=16384:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+M-1:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
10 CLS3:PRINT@32,"ппппппппппппппомммппппппппппппппппппппппппппоаААААадпппппппппппп";
11 PRINT"пппппппппппоАБЗПЕПГАдпппппппппппппппппппппоААПППЕППКАдппппппппппппппппппппаБПЖППЕПОЗЛАеппппппппп";
12 PRINT"пппппппппоАЗП WHEEL ПВдппппппппппппппппппкАПП       ПКаппппппппппппппппппкАГГГ OFПГГГВаппппппппп";
13 PRINT"пппппппппкАПП       ПКапппппппппппппппппппаЕПFORTUNEПаепппппппппппппппппппвАМЗППЕППЖИазппппппппп";
14 PRINT"пппппппппппвАНППЕППИазппппппппппппппппппппппвАДМДМАазппппппппппппппппппппппппгвааагзпппппппппппп";
30 DIMW$(20),A$(40),H$(40),PL$(8),PM(8),PS(8),C$(6),P$(10),FT(8),BL$(8),U(20),ZG$(25)
40 FORI=1TO20:READW$(I):NEXTI:RESTORE:VV=1:ZW=0:TN=0:GOSUB1000
50 C$(1)="TITLE":C$(2)="FICTIONAL CHARACTER":C$(3)="PERSON":C$(4)="EVENT":C$(5)="PLACE":C$(6)="THING"
60 CLSRND(7)+1:M$="LET'S PLAY wheel of fortune! HOW MANY PEOPLE ARE PLAYING?":GOSUB1
70 GOSUB1090:NP=A:PN=RND(NP)
71 IFNP<1ORNP>8THENPRINT:M$="I'M SORRY, ONLY 1 TO 8 PLAYERSCAN PLAY.":GOSUB1:PRINT"HOW MANY PLAYERS?":GOTO70
80 FORI=1TONP:PRINT"WHAT IS THE NAME OF PLAYER #"STR$(I);:INPUTPL$(I):NEXT
90 M$="EITHER THE COMPUTER CAN CHOOSE THE PHRASE THAT YOU HAVE TO GUESS, OR SOMEONE WHO IS NOT "
91 M$=M$+"PLAYING CAN CHOOSE IT.":GOSUB1:M$="DO YOU WANT THE COMPUTER TO CHOOSE IT?":GOSUB1:GOSUB1110
100 IFB$="Y"THENCC=2:GOSUB210:GOTO170
101 CC=1
110 CLSRND(7)+1:M$="ANYONE WHO IS GOING TO PLAY THE GAME SHOULD NOT LOOK AT THE SCREEN.":GOSUB1
120 M$="ENTER A WORD OR PHRASE, CHOSEN IN ONE OF THE FOLLOWING CATEGORIES:":GOSUB1
121 FORI=1TO6:M$=STR$(I)+"."+C$(I):GOSUB1:NEXTI
130 M$="DO NOT USE ANY PUNCTUATION MARKS OR NUMERALS.":GOSUB1
131 PRINT"WHAT IS YOUR PHRASE?":INPUTS$:W$=S$:IFLEN(S$)>30THENCLS:PRINT"YOUR PHRASE IS TOO LONG.":GOTO120
140 M$="WHAT IS THE NUMBER OF THE CATEGORY THIS IS IN?":GOSUB1:GOSUB1090:CN=A
141 IFCN<1ORCN>6THEN145
142 GOTO150
145 PRINT:M$="YOUR CATEGORY MUST HAVE NUMBER FROM 1 TO 6.":GOSUB1:FORI=1TO6:M$=STR$(I)+"."+C$(I):GOSUB1:NEXT:GOTO140
150 GOSUB240
160 REM TAKE TURN
170 GOSUB1030:PRINT@352,;:PRINT"IT IS "PL$(PN)"'S TURN.";
171 PRINT@384,;:M$="HIT ANY KEY TO":GOSUB7:PRINT@416,;:M$="SPIN THE WHEEL":GOSUB7:A$=INKEY$
180 A$=INKEY$:A=RND(0):IFA$=""THEN180
190 GOSUB980:PRINT@352,BL$(CO);:PRINT@384,BL$(CO);:PRINT@416,BL$(CO);:GOSUB820:ONRXGOTO390,400,430,440
200 REM CHOOSE PHRASE
210 TN=TN+1:RESTORE:FORI=1TO20:READA$:NEXT:W$="":N=RND(100)
220 FORJ=1TOTN-1:IFU(J)=NTHENC1=J:J=TN-1:NEXTJ:TN=TN-1:J=C1:GOTO210
221 NEXTJ:U(TN)=N
230 FORI=1TON:READ S$,CN:NEXT:W$=S$
240 NV=0:TL=LEN(W$):SP=0:FORI=1TO30:A$(I)="":NEXT
250 FORI=1TOTL:A$(I)=MID$(W$,I,1):IFA$(I)=" "THENSP=SP+1:SL(SP)=I:GOTO260
251 IFA$(I)="A"ORA$(I)="E"ORA$(I)="I"ORA$(I)="O"ORA$(I)="U"THENNV=NV+1
260 NEXTI:SL(0)=0:SL(SP+1)=TL+1:W$="":FORI=1TO7:P$(I)="":NEXTI
261 FORI=0TO7:P$(I)="":FOR J=SL(I)+1TOSL(I+1)-1:P$(I)=P$(I)+A$(J):NEXTJ,I
262 FORI=0TOSP+1:W$=W$+P$(I):NEXTI
270 NL=LEN(W$):FORI=1TONL:A$(I)=MID$(W$,I,1):H$(I)="-":NEXTI:C=0:RETURN
280 REM CHECK GUESS
290 IFASC(LG$)>64ANDASC(LG$)<91THEN310
291 PRINT@0,BL$(CO);:PRINT@2,"LETTERS ONLY!!";:PRINT@480,"WHAT IS YOUR GUESS?";:LG$=INKEY$
300 LG$=INKEY$:IFLG$=""THEN300
301 GOTO290
310 PRINT@448,BL$;:IFVV=0THEN320
311 IFLG$="A"ORLG$="E"ORLG$="I"ORLG$="O"ORLG$="U"THENPRINT@416,"NO VOWELS!";:LG$=".":GOTO290
320 D=0:FORI=1TONL:IFA$(I)=LG$ANDH$(I)<>LG$THENC=C+VV:H$(I)=A$(I):SOUND170,8:GOTO330
321 D=D+1
330 NEXTI:GOSUB1030:RETURN
340 FORI=1TONL
350 H$(I)=A$(I)
360 NEXTI
370 GOSUB1030
380 RETURN
390 PRINT@352,"YOU ARE BANKRUPT!";:SOUND20,5:SOUND1,15:PM(PN)=0:FT(PN)=0:GOSUB780:GOTO650
400 PRINT@352,"YOU LOSE YOUR TURN!";:SOUND20,5:SOUND 1,15:IFFT(PN)=0THENGOSUB780:GOTO650
410 PRINT@384,"YOU HAVE"FT(PN)"FREE TURN";:IFFT(PN)=1THENPRINT".";:GOTO420
411 PRINT"S.";
420 PRINT@416,;:M$="DO YOU WANT TO":GOSUB7:PRINT@448,;:M$="SPIN AGAIN?   ":GOSUB7:GOSUB1110
421 IFB$="Y"THENFT(PN)=FT(PN)-1:GOTO680
422 GOTO650
430 PRINT@352,"YOU WIN A FREE SPIN!";:SOUND170,8:FT(PN)=FT(PN)+1:GOSUB780
431 PRINT@480,BL$(CO);:PRINT@483,BL$(CO);:GOTO190
440 PRINT@352,;:M$="GUESS A LETTER.":GOSUB7:GOSUB1070:LG$=INKEY$
450 LG$=INKEY$:IFLG$=""THEN450
451 IFLG$="A"ORLG$="E"ORLG$="I"ORLG$="O"ORLG$="U"THENPRINT@384,;:M$="DO NOT GUESS A VOWEL.":GOSUB7:GOTO450
460 GOSUB290:IFD=NLTHEN465
461 GOTO470
465 PRINT@352,"THERE ARE NO "LG$"'S.";:SOUND20,5:SOUND1,15:FORC1=1TOZW:IFLG$=ZG$(C1)THENC1=ZW:NEXT:GOTO468
466 NEXT:ZW=ZW+1:ZG$(ZW)=LG$
468 IFFT(PN)>0THEN410
469 GOSUB780:GOTO650
470 X=WH*(NL-D):PRINT@352,"YOU WIN $"STR$(X)".";:PM(PN)=PM(PN)+X:PRINT@384,"YOU NOW HAVE $"STR$(PM(PN))".";
480 GOSUB780:GOSUB1030:IFNL-C<=NVTHEN710
490 PRINT@352,"DO YOU WANT TO:";:PRINT@384,"   1. GUESS THE ANSWER";
491 PRINT@416,"   2. BUY A VOWEL     ";:PRINT@448,"   3. SPIN AGAIN?     ";:GOSUB1090
500 IFA<1ORA>3THENPRINT@320,;:M$="CHOOSE A NUMBER FROM 1 TO 3.":GOSUB7:GOTO490
510 ONAGOTO520,540,670
520 GOSUB1030:PRINT@352,"WHAT IS YOUR GUESS":INPUTGS$:IFGS$=S$THEN910
530 PRINT@416,;:M$="I'M SORRY, BUT YOUR ANSWER IS INCORRECT.":GOSUB1:GOSUB780:GOTO650
540 GOSUB1030:IFPM(PN)<250THENPRINT@320,;:M$="YOU DO NOT HAVE ENOUGH MONEY TO BUY A VOWEL.":GOSUB1:GOTO610
550 PRINT@320,"A VOWEL COSTS $ 250.":PM(PN)=PM(PN)-250:PRINT"YOU HAVE $"PM(PN)"LEFT."
560 PRINT@384,;:M$="WHAT VOWEL DO YOU WANT TO BUY?":GOSUB7:LG$=INKEY$
570 LG$=INKEY$:IFLG$=""THEN570
580 IFLG$<>"A"ANDLG$<>"E"ANDLG$<>"I"ANDLG$<>"O"ANDLG$<>"U"THENGOSUB1030:?@320,"YOU MUST CHOOSE A,E,I,O, OR U.":GOTO560
590 IFASC(LG$)<65ORASC(LG$)>90THENGOSUB1030:?@3,BL$(CO);:?@3,"LETTERS ONLY!!";:?@480,"WHAT IS YOUR GUESS?";:GOTO570
600 VV=0:GOSUB290:VV=1
610 PRINT@384,"DO YOU WANT TO:";:PRINT@416,"  1. GUESS THE ANSWER";:PRINT@448,"  2. SPIN AGAIN?     ";:GOSUB1090
620 IFA<1ORA>2THENGOSUB1020:PRINT@320,;:M$="PLEASE ANSWER 1 OR 2.":GOSUB7:GOTO610
630 ONAGOTO520,680
640 REM NEXT PLAYER
650 PN=PN+1:IFPN>NPTHENPN=1
660 GOTO170
670 REM SPIN AGAIN
680 GOSUB1030:GOTO171
690 GOTO180
700 REM NO MORE CONSONANTS
710 D=0:FORII=1TONL:IFH$(II)="-"THEND=1
720 NEXTII:IFD=0THEN910
730 PRINT@320,;:M$="THERE ARE NO MORE CONSONANTS IN THE PUZZLE. EACH PLAYER WILL NOW GET ONE CHANCE TO GUESS THE "
731 M$=M$+"ANSWER. "+PL$(PN)+" GOES FIRST.":GOSUB1:INPUT"WHAT IS YOUR GUESS";GS$:IFGS$=S$THEN910
740 IFNP=1THEN760
741 FORII=1TONP-1:A=PN+II:IFPN+II>NPTHENA=A-NP
750 GOSUB1030:PRINT@320,"IT IS NOW "PL$(A)"'S":PRINT"TURN TO GUESS."
751 INPUT"WHAT IS YOUR GUESS";GS$:IFGS$=S$THENPN=A:C1=II:II=NP-1:NEXTII:II=C1:GOTO910
752 NEXTII
760 GOSUB340
770 PRINT@2,"THE ANSWER WAS:";:FORI=1TONP:PM(I)=0:NEXTI:GOTO930
780 PRINT@480,;:M$="HIT ANY KEY TO CONTINUE.":GOSUB7:A$=INKEY$
790 A$=INKEY$:A=RND(0):IFA$=""THEN790
791 RETURN
810 REM SPIN WHEEL
820 PRINT@244,;:M$="=>":GOSUB7:ST=K-14:IFST<1THENST=ST+20
830 C1=-10:C2=32:FORI=-1+STTOST+6+RND(20):SOUND200,1:FORJ=1TO15:K=J+I
840 IFK>20THENK=K-20:GOTO840
850 PRINT@C1+J*C2,W$(K):NEXT:NEXT
860 IFK<8THENK=K+20
870 WH$=W$(K-7)
880 IFWH$="BANKRUPT"THENRX=1:RETURN
881 IFWH$="LOSE TURN"THENRX=2:RETURN
882 IFWH$="FREE SPIN"THENRX=3:RETURN
883 RX=4
890 WH=VAL(RIGHT$(WH$,3)):RETURN
900 REM WINNER!
910 FORI=0TO8:CLSI:SOUND170,6:NEXTI:IFPN=1THENCLS0:GOTO912
911 CLSPN
912 IFPM(PN)<100THENPM(PN)=100
920 PRINT@168,"YOU'RE RIGHT!";:PRINT@264,"YOU WIN $"STR$(PM(PN))"!";:PS(PN)=PS(PN)+PM(PN):FORI=1TONP:PM(I)=0:NEXTI
930 IFTN>=20THENB$="N":GOTO940
931 PRINT@388,;:M$="DO YOU WANT TO CONTINUE?":GOSUB7:GOSUB1110
940 CLSRND(9)-1:PRINT@64,"SCORES:":FORI=1TONP:PRINT@96+32*I,PL$(I)TAB(23)"$"PS(I):NEXTI
950 GOSUB780:CLS RND(9)-1:IFB$<>"Y"THENEND
960 ZW=0:VV=1:PN=PN+1:IFPN>NPTHENPN=1
970 CLSRND(7)+1:IFCC=2THENGOSUB210:GOTO170
971 GOTO110
980 CO=ABS(POINT(60,31)):RETURN
990 REM SET ERASING CHARACTERS
1000 BL$(0)="А":FORI=1TO21:BL$(0)=BL$(0)+"А":NEXTI
1010 FORCO=1TO8:BL$(CO)=CHR$(127+16*CO):FORI=1TO21:BL$(CO)=BL$(CO)+CHR$(127+16*CO):NEXTI:NEXTCO:RETURN
1020 REM DISPLAY PHRASE SUBROUTINE
1030 FORI=0TO7:D$(I)="":NEXTI:FORI=1TOLEN(P$(0)):D$(0)=D$(0)+H$(I):NEXTI:IFSP=0THEN1050
1040 LG=LEN(P$(0)):FORJ=1TOSP:FORI=LG+1TOLG+LEN(P$(J)):D$(J)=D$(J)+H$(I):NEXTI:LG=LG+LEN(P$(J)):NEXTJ
1050 IFPN=1THENCLS0:GOTO1052
1051 CLSPN
1052 PRINT@2,C$(CN);:C1=32:C2=65:FORI=0TOSP:PRINT@C2+C1*I,D$(I);:NEXT:RETURN
1060 REM DISPLAY WRONG GUESSES
1070 IFZW=0THENRETURN
1071 PRINT@416,"INCORRECT GUESSES:";:PRINT@448,;:FORZI=1TOZW:PRINTZG$(ZI);:IFZI<>ZWTHENPRINT",";
1072 IFZI=10THENPRINT@480,;
1080 NEXTZI:RETURN
1090 A$=INKEY$
1100 A$=INKEY$:IFA$=""THEN1100
1101 A=VAL(A$):SOUND160,1:RETURN
1110 B$=INKEY$
1120 B$=INKEY$:IFB$<>"Y"ANDB$<>"N"THEN1120
1121 SOUND160,1:RETURN
1130 DATA $150,$200,$300,LOSE TURN,$300,$500,$150,FREE SPIN,$100,$250,$400
1131 DATA BANKRUPT,$500,$100,$200,LOSE TURN,$400,$100,$350,FREE SPIN
1140 DATA ALICE IN WONDERLAND,1,WHEEL OF FORTUNE,1,DIFFERENT STROKES,1
1141 DATA ALL IN THE FAMILY,1,HAPPY DAYS,1,THE SWORD IN THE STONE,1
1142 DATA THE FOX AND THE HOUND,1,MY FAIR LADY,1,THE MUSIC MAN,1,FAMILY FEUD,1
1150 DATA MARY POPPINS,2,MICKEY MOUSE,2,OLIVER TWIST,2,LITTLE ORPHAN ANNIE,2
1151 DATA DENNIS THE MENACE,2,DICK TRACY,2,DADDY WARBUCKS,2,DOCTOR DOLITTLE,2
1152 DATA CLARK KENT,2,SNOW WHITE,2
1160 DATA WALT DISNEY,3,MARK TWAIN,3,QUEEN ELIZABETH,3,ANN LANDERS,3
1161 DATA JOHNNY CARSON,3,BOB HOPE,3,JOHN PAUL JONES,3,ADAM AND EVE,3
1162 DATA BENJAMIN FRANKLIN,3,RONALD REAGAN,3
1170 DATA SINGING A SONG,4,PRESIDENTIAL ELECTION,4,DISCOVERY OF AMERICA,4
1171 DATA CALIFORNIA GOLD RUSH,4,COLLEGE GRADUATION,4,SPELLING BEE,4
1172 DATA RECEIVING THE TEN COMMANDMENTS,4,INVENTING THE TELEPHONE,4
1173 DATA FALLING ASLEEP,4,GOING TO SCHOOL,4
1180 DATA NEW YORK CITY,5,UNITED STATES OF AMERICA,5,STATE OF ILLINOIS,5
1181 DATA WALT DISNEY WORLD,5,THE ENGLISH CHANNEL,5,CITY OF CHICAGO,5
1182 DATA ATLANTIC OCEAN,5,MOUNT RUSHMORE,5,SMITHSONIAN INSTITUTION,5,YELLOWSTONE NATIONAL PARK,5
1190 DATA PHONOGRAPH RECORD,6,COLOR COMPUTER,6,TELEVISION SET,6,BARBIE DOLL,6
1191 DATA THEATER TICKET,6,LIGHTNING BUG,6,MAGAZINE SUBSCRIPTION,6,DECK OF CARDS
1192 DATA 6,LIBRARY BOOK,6,AIRPLANE TICKET,6
1200 DATA HUCKLEBERRY FINN,2,WOODY WOODPECKER,2,ANNE FRANK,3,THE THREE MUSKETEERS,1
1201 DATA THE PRINCE AND THE PAUPER,1,SCOOBY DOO,2,SONG OF THE SOUTH,1,SEWING BASKET,6
1202 DATA THE PHANTOM TOLLBOOTH,1,THE THREE LITTLE PIGS,2
1210 DATA COUNT DRACULA,2,MOTHER GOOSE,2,LITTLE RED RIDING HOOD,2,HANS CHRISTIAN ANDERSON,3
1211 DATA THOMAS ELVA EDISON,3,GEORGE WASHINGTON,3,PAUL REVERE,3,CHRISTOPHER COLUMBUS,3
1212 DATA CHARLIE BROWN,2,BEETLE BAILEY,2
1220 DATA BEDKNOBS AND BROOMSTICKS,1,ESCAPE TO WITCH MOUNTAIN,1,HAUNTED MANSION,5
1221 DATA THE LOVE BUG,1,SING A SONG OF SIXPENCE,1,A CHRISTMAS CAROL,1,DONALD DUCK,2
1222 DATA HALLOWEEN,4,NANCY DREW,2,PECOS BILL,2
1230 DATA JACK AND THE BEANSTALK,1,SPACE MOUNTAIN,5,NIAGARA FALLS,5,JACK AND JILL,1
1231 DATA THE WIZARD OF OZ,1,WHEN YOU WISH UPON A STAR,1,SEVENTY SIX TROMBONES,1
1232 DATA RAGGEDY ANN,2,STRAWBERRY SHORTCAKE,2,ICE CREAM CONE,6
1300 REM WHEEL OF FORTUNE
1310 REM BY HAROLD SCHNEIDER
1320 REM MC-10 EDITS JIM GERRIE
