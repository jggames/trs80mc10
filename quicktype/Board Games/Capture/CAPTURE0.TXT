0 CLS:CLEAR2000:DIMCC$:GOTO100
9 FORZZ=1TO2200:NEXTZZ:RETURN
100 PRINT"            capture"
105 PRINT
110 PRINT"     (C) MAC OGLESBY 2/20/78"
112 PRINT
115 PRINT"     LAST REVISION: 3/30/80"
117 PRINT
120 PRINT" A GAME FOR 1 OR 2 PLAYERS."
130 PRINT" COPYRIGHT 1978 BY MAC OGLESBY"
140 PRINT" PERMISSION GRANTED TO USE,"
150 PRINT" BUT NOT TO SELL."
160 PRINT
1000 REM INITIALIZATION
1040 PRINT"    WANT INSTRUCTIONS (Y/N)?":GOSUB10000
1050 IFXA$<>"Y"THEN1100
1060 X=-1
1070 PRINT:PRINT"I'LL PRINT INSTRUCTIONS AFTER I"
1081 PRINT"MAKE THE PLAYING BOARD...";:GOSUB9
1100 CLS1:DIMD$(16),E$(16),L(26),M(26),R(25),C(25),P$(2)
1110 P$(1)="#":P$(2)="@"
1140 FORJ=1TO25:READ R(J),C(J):NEXTJ
1150 DATA-3,0,-2,-1,-2,0,-2,1
1155 DATA-1,-2,-1,-1,-1,0,-1,1,-1,2 
1160 DATA0,-3,0,-2,0,-1,0,0,0,1,0,2,0,3 
1165 DATA1,-2,1,-1,1,0,1,1,1,2 
1170 DATA2,-1,2,0,2,1,3,0 
1180 A$="":FORJ=1TO32:A$=A$+CHR$(143):NEXT
1190 FORJ=1TO16:D$(J)=A$:NEXTJ
1200 CLS1:FORJ=1TO200
1210 R8=RND(16):C8=RND(32) 
1220 P8=16383+C8+32*(R8-1):IFPEEK(P8)<>143THEN1210
1230 C$="€":IFJ>26THENPOKEP8,128:GOTO1250
1240 L(J)=100*R8+C8:C$=CHR$(J+64):POKEP8,J+64:M(J)=L(J)
1250 GOSUB6000
1260 NEXTJ
1300 FORJ=1TO16:E$(J)=D$(J):NEXTJ 
1400 IFX<.THENGOSUB9000
2000 T=2 
2100 GOSUB10600
2110 PRINT"HOW MANY HUMAN PLAYERS (1-2)?  ";
2120 GOSUB10000 
2130 IFXA$<>"1"ANDXA$<>"2"THEN2110
2140 PRINT@480,CC$;:PL=VAL(XA$)
2150 IFPL=2THEN3000 
2200 PRINT@480,"OK. I'LL PLAY THE #'S          ";:GOSUB9
2210 PRINT@480,CC$;:PRINT@480,"WHO GOES FIRST (1=COM 2=YOU)? ";
2220 GOSUB10000 
2230 IFXA$<>"1"ANDXA$<>"2"THEN2220
2240 PRINT@480,CC$;:T=3-VAL(XA$)
3000 REM *** COMPUTER'S MOVE 
3100 T=3-T
3110 IFPL=2ORT=2THEN4000
3130 GOSUB10600:PRINT"PLEASE WAIT. I'M THINKING...   ";:GOSUB9:PRINT@480,CC$;
3140 E=0 
3160 FORJ=1TO26
3200 IFL(J)=.THEN3900
3210 POKE16895,J+64
3220 R=INT(L(J)/100):C=L(J)-R*100
3240 Q=0 
3250 FORK=1TO25
3260 R1=R+R(K):IFR1>16ORR1<1THEN3400
3270 C1=C+C(K):IFC1>32ORC1<1THEN3400
3280 X$=MID$(D$(R1),C1,1):IFX$=CHR$(143)ORX$="#"THEN3400
3290 Q=Q+1
3400 NEXTK 
3500 IFQ>ETHENM$=CHR$(J+64):E=Q:GOTO3900
3510 IFQ=ETHENM$=M$+CHR$(J+64)
3900 GOSUB7110:NEXTJ
3910 X=1+INT(LEN(M$)*RND(0))
3920 Z$=MID$(M$,X,1):X=ASC(Z$)-64
3930 GOSUB10600
3940 PRINT"I CAPTURE "Z$"                    ";:GOSUB9:PRINT@480,CC$;:GOTO4300
4000 REM *** HUMAN'S MOVE
4110 GOSUB10600:PRINT"THE "P$(T)"'S CAPTURE...             ";:GOSUB9:PRINT@480,CC$;
4200 GOSUB10000
4210 IFXA$="?"THENGOSUB9000:GOTO4110
4240 X=ASC(XA$)-64:IFX<1ORX>26THEN4260
4250 IFL(X)<>0THEN4300
4255 GOSUB10600:GOSUB10700:PRINT"THAT LETTER HAS BEEN CAPTURED. ";:GOSUB9:PRINT@480,CC$;:GOTO4110
4260 GOSUB10600:GOSUB10700:PRINT"YOU CONFUSE ME. TYPE ? FOR HELP";:GOSUB9:PRINT@480,CC$;:GOTO4110
4300 R=INT(L(X)/100):C=L(X)-R*100
4390 C$=P$(T)
4400 FORJ=1TO25
4410 R8=R+R(J):IFR8<1ORR8>16THEN4500
4420 C8=C+C(J):IFC8<1ORC8>32THEN4500
4430 Z$=MID$(D$(R8),C8,1):IFZ$=CHR$(143)THEN4500
4440 X=ASC(Z$):IFX>64ANDX<91THENL(X-64)=0
4450 GOSUB6000
4500 NEXTJ
4520 GOSUB7000:X=0
4600 FORJ=1TO26:X=X+L(J):NEXTJ
4610 IFX=0THEN8000
4700 GOTO3000
6000 REM *** UPDATE BOARD 
6100 IFC8=1THEND$(R8)=C$+RIGHT$(D$(R8),31):RETURN
6200 IFC8=32THEND$(R8)=LEFT$(D$(R8),31)+C$:RETURN
6300 D$(R8)=LEFT$(D$(R8),C8-1)+C$+MID$(D$(R8),C8+1):RETURN
7000 REM *** PRINT BOARD 
7100 PRINT@.,;:FORJ=1TO15:PRINTD$(J);:NEXT:
7110 J=ASC(MID$(D$(16),32,1)):IFJ=35THENJ=J+64
7120 POKE16895,J:RETURN
8000 REM *** END 
8100 GOSUB10600:PRINT"NOW, LET'S SEE WHO WON...      ";:GOSUB9:PRINT@480,CC$;
8200 H1=0:H2=0
8300 FORJ=1TO16 
8320 FORK=1TO32 
8330 A$=MID$(D$(J),K,1) 
8340 IFA$="#"THENH1=H1+1:GOTO8400
8350 IFA$="@"THENH2=H2+1
8400 NEXTK
8500 NEXTJ
8600 IFH1<>H2THEN8700 
8620 GOSUB10600:PRINT"TIE GAME!!!                   ";:GOSUB9:PRINT@480,CC$;:GOTO8710
8700 GOSUB10600:PRINT"THE "P$((SGN(H2-H1)+3)/2)"'S WIN!!!                 ";:GOSUB9:PRINT@480,CC$;
8710 PRINT@480,"#'S"H1" @'S"H2"            ";
8720 PRINT@500,"PLAY AGAIN?";:POKE16895,96
8740 GOSUB10000:IFXA$<>"Y"ANDXA$<>"N"THEN8740
8750 PRINT@480,CC$;:IFXA$="Y"THEN8760
8752 PRINT" BYE FOR NOW...":END
8754 REM LINES 8760-8840 ALLOW FOR REPLAY WITH SAME BOARD SETUP IF THE CODE 
8756 REM FOLLOWING THE REMS IN LINE 1240 AND LINE 1300 IS RESTORED.
8758 REM MY 8K PET RUNS OUT OF MEMORY 
8760 GOSUB10600:PRINT"1=SAME SETUP OR 2=NEW BOARD?   ";
8790 GOSUB10000:PRINT@480,CC$;
8800 IFXA$="2"THENRUN
8810 IFXA$<>"1"THEN8760
8820 FORJ=1TO16:D$(J)=E$(J):NEXTJ
8830 FORJ=1TO26:L(J)=M(J):NEXTJ
8840 GOSUB7000:GOTO2000
9000 REM *** RULES
9010 CLS
9100 PRINT"THE PLAYERS # AND @ TAKE TURNS"
9110 PRINT"CAPTURING LETTERS ON THE BOARD."
9120 PRINT"ALL CHARACTERS EXCEPT SPACES"
9125 PRINT"WITHIN THIS SHAPE FIELD:"
9130 PRINT"               *"
9131 PRINT"              ***"
9132 PRINT"             *****"
9133 PRINT"            ***A***"
9134 PRINT"             *****"
9135 PRINT"              ***"
9136 PRINT"               *"
9140 PRINT"ARE ALSO CAPTURED AND CHANGE"
9150 PRINT"TO THAT PLAYER'S SYMBOL. WHEN"
9170 PRINT"ALL THE LETTERS ARE GONE THE"
9180 PRINT"PLAYER WITH THE MOST CAPTIVES"
9300 PRINT"WINS. PRESS enter WHEN READY.";
9400 GOSUB10000:IFXA$<>CHR$(13)THEN9400
9410 CLS1:GOSUB7000:RETURN
10000 REM GET 
10300 XA$=INKEY$:IFXA$<>""THEN10300
10310 XA$=INKEY$:IFXA$=""THEN10310
10500 RETURN
10600 CC$=LEFT$(D$(16),31):PRINT@480,;:RETURN
10700 SOUND1,1 
10710 RETURN
