0 CLS:PRINT@20,"LASER CHESS":GOTO900
9 PRINT@Z,P$(B(X,Y,0),B(X,Y,1),B(X,Y,2));:PRINT@Z+V,Q$(B(X,Y,0),B(X,Y,1),B(X,Y,2));:RETURN

20 I=ASC(INKEY$+"@"):IFI>64THENGOSUB9:X=X+(I=65ANDX>0)-(I=83ANDX<9):Y=Y+(I=87ANDY>0)-(I=90ANDY<7):Z=(Y*2)*V+X*2:C=9
21 C=C+1:IFC=1THENGOSUB9
22 IFC=10THENPRINT@Z,P$(0,2,0);:PRINT@Z+V,Q$(0,2,0);
23 IFC=20THENC=0
24 IFI>=64THEN20
25 IFI=44THENIFP=B(X,Y,2)THEN30
26 IFI=46THENIFP=B(X,Y,2)THEN35
27 IFI=13THEN48
29 GOTO20

30 IFB(X,Y,0)<>5THENB(X,Y,1)=B(X,Y,1)-1:GOTO33
31 IFP=1THENB(X,Y,1)=B(X,Y,1)-3:IFB(X,Y,1)<1THENB(X,Y,1)=4
32 IFP=2THENB(X,Y,1)=B(X,Y,1)-1:IFB(X,Y,1)<2THENB(X,Y,1)=3
33 IFB(X,Y,1)<1THENB(X,Y,1)=4
34 GOTO39

35 IFB(X,Y,0)<>5THENB(X,Y,1)=B(X,Y,1)+1:GOTO38
36 IFP=1THENB(X,Y,1)=B(X,Y,1)+3:IFB(X,Y,1)>4THENB(X,Y,1)=1
37 IFP=2THENB(X,Y,1)=B(X,Y,1)+1:IFB(X,Y,1)>3THENB(X,Y,1)=2
38 IFB(X,Y,1)>4THENB(X,Y,1)=1
39 SOUND99,1:GOSUB9:C=9
40 IFP=1THENX=9:Y=7
41 IFP=2THENX=0:Y=0
42 GOTO70

48 IFB(X,Y,2)<>PORB(X,Y,0)=5THENSOUND1,1:GOTO20
49 SOUND99,1:D=X:E=Y:F=B(X,Y,0):G=B(X,Y,1):H=B(X,Y,2):B(X,Y,0)=0:B(X,Y,1)=0:B(X,Y,2)=0
50 I=ASC(INKEY$+"@"):IFI>64THENGOSUB9:X=X+(I=65ANDX>0)-(I=83ANDX<9):Y=Y+(I=87ANDY>0)-(I=90ANDY<7):Z=(Y*2)*V+X*2:C=9
51 C=C+1:IFC=1THENGOSUB9
52 IFC=10THENPRINT@Z,P$(F,G,H);:PRINT@Z+V,Q$(F,G,H);
53 IFC=20THENC=0
54 IFI<>13THEN50
55 IFABS(X-D)<2ANDABS(Y-E)<2ANDB(X,Y,0)=0THENB(X,Y,0)=F:B(X,Y,1)=G:B(X,Y,2)=H:GOTO39
69 SOUND1,1:GOTO50

70 Z=(Y*2)*V+X*2:C=9:GOSUB9:A=X*4+1:B=Y*4+1:I=B(X,Y,1)
71 IFI=1THENC=.:D=-1:E=-1:F=.:G=1:H=.:L=4:R=2
72 IFI=2THENC=1:D=.:E=.:F=-1:G=.:H=1:L=1:R=3
73 IFI=3THENC=.:D=1:E=-1:F=.:G=1:H=.:L=4:R=2
74 IFI=4THENC=-1:D=.:E=.:F=1:G=.:H=-1:L=3:R=1

81 I=POINT(A+C,B+D):IFI=.THENA=A+C:B=B+D:SET(A,B,.):IFA>.ANDB>.ANDA<40ANDB<31THEN81
82 IFA=.ORB<1ORA>38ORB>30THEN89
83 IFA/2-INT(A/2)=.ORB/2-INT(B/2)=.THEN90
84 A=A+C:B=B+D:X=INT(A/4):Y=INT(B/4):Z=(Y*2)*V+X*2:C=9:IFB(X,Y,.)=5THEN99
85 FORI=1TO10:PRINT@Z,P$(0,1,0);:PRINT@Z+V,Q$(0,1,0);:SOUND1,1
86 PRINT@Z,P$(0,3,0);:PRINT@Z+V,Q$(0,3,0);:SOUND1,1:NEXT:PRINT@Z,P$(0,0,0);:PRINT@Z+V,Q$(0,0,0);
87 IFB(X,Y,0)=4THEN200
88 B(X,Y,0)=.:B(X,Y,1)=.:B(X,Y,2)=.:PRINT@Z,P$(0,0,0);
89 GOTO99

90 Z=.:IFPOINT(A+E,B+F)=.THENI=L:Z=Z+1
91 IFPOINT(A+G,B+H)=.THENI=R:Z=Z+1
92 A=A+C:B=B+D:IFZ=1THENSOUND200,3:GOTO71
93 IFPOINT(A,B)=1THEN81
94 GOTO99

99 FORY=0TO7:FORX=0TO9:Z=(Y*2)*V+X*2:?@Z,P$(B(X,Y,0),B(X,Y,1),B(X,Y,2));:?@Z+V,Q$(B(X,Y,0),B(X,Y,1),B(X,Y,2));:NEXT:NEXT
110 P=P+1:IFP=3THENP=1
120 IFP=1THENX=9:Y=7
130 IFP=2THENX=0:Y=0
140 Z=(Y*2)*V+X*2:PRINT@V*2+20,"PLAYER"P;:SOUND50,1:SOUND75,1:GOTO20

200 PRINT@V*8+20,;
210 IFB(X,Y,2)=1THENPRINT"BLUE LOSES";:GOTO230
220 PRINT"RED LOSES";
230 PRINT@V*10+20,"PLAY AGAIN?";
250 I$=INKEY$:IFI$=""THEN250
260 IFI$="Y"THENRUN
270 IFI$="N"THENEND
280 GOTO250

900 DIMP$(5,4,2),B(9,7,2),X,Y,Z:V=32
910 FORZ=0TO2:FORY=0TO7:FORX=0TO9:READB(X,Y,Z):NEXT:NEXT:NEXT

REM BLUE

1001 P$(1,1,1)="¯¨":P$(1,2,1)= "­ª":P$(1,3,1)="¡ª":P$(1,4,1)="«€"
1002 Q$(1,1,1)="¨€":Q$(1,2,1)="€¨":Q$(1,3,1)="¬¨":Q$(1,4,1)="¬¨"

1003 P$(2,1,1)="¡¨":P$(2,2,1)="©€":P$(2,3,1)="¡¨":P$(2,4,1)="©€"
1004 Q$(2,1,1)="¨€":Q$(2,2,1)="€¨":Q$(2,3,1)="¨€":Q$(2,4,1)="€¨"

1005 P$(3,1,1)="¯¨":P$(3,2,1)="¯ª":P$(3,3,1)="­ª":P$(3,4,1)="«ª"
1006 Q$(3,1,1)="¬¨":Q$(3,2,1)="¨¨":Q$(3,3,1)="¬¨":Q$(3,4,1)="¬¨"

1007 P$(4,1,1)="§¢":P$(4,2,1)="§¢":P$(4,3,1)="§¢":P$(4,4,1)="§¢"
1008 Q$(4,1,1)="¤€":Q$(4,2,1)="¤€":Q$(4,3,1)="¤€":Q$(4,4,1)="¤€"

1009 P$(5,1,1)="¡€":P$(5,2,1)="«€":P$(5,3,1)="­¨":P$(5,4,1)="¡ª"
1010 Q$(5,1,1)="¬¨":Q$(5,2,1)="¨€":Q$(5,3,1)="€€":Q$(5,4,1)="€¨"

REM RED

2001 P$(1,1,2)="¿¸":P$(1,2,2)="½º":P$(1,3,2)="±º":P$(1,4,2)="»€"
2002 Q$(1,1,2)="¸€":Q$(1,2,2)="€¸":Q$(1,3,2)="¼¸":Q$(1,4,2)="¼¸"

2003 P$(2,1,2)="±¸":P$(2,2,2)="¹€":P$(2,3,2)="±¸":P$(2,4,2)="¹€"
2004 Q$(2,1,2)="¸€":Q$(2,2,2)="€¸":Q$(2,3,2)="¸€":Q$(2,4,2)="€¸"

2005 P$(3,1,2)="¿¸":P$(3,2,2)="¿º":P$(3,3,2)="½º":P$(3,4,2)="»º"
2006 Q$(3,1,2)="¼¸":Q$(3,2,2)="¸¸":Q$(3,3,2)="¼¸":Q$(3,4,2)="¼¸"

2007 P$(4,1,2)="·²":P$(4,2,2)="·²":P$(4,3,2)="·²":P$(4,4,2)="·²"
2008 Q$(4,1,2)="´€":Q$(4,2,2)="´€":Q$(4,3,2)="´€":Q$(4,4,2)="´€"

2009 P$(5,1,2)="±€":P$(5,2,2)="»€":P$(5,3,2)="½¸":P$(5,4,2)="±º"
2010 Q$(5,1,2)="¼¸":Q$(5,2,2)="¸€":Q$(5,3,2)="€€":Q$(5,4,2)="€¸"

REM EXTRA GRAPHICS

2011 P$(0,0,0)="€€":P$(0,1,0)="ùù":P$(0,2,0)="ÏÏ":P$(0,3,0)="öö"
2012 Q$(0,0,0)="€€":Q$(0,1,0)="ùù":Q$(0,2,0)="ÏÏ":Q$(0,3,0)="öö"
2099 GOTO99

3001 DATA 5,0,0,0,3,4,3,1,0,0
3002 DATA 0,0,1,0,0,0,0,0,0,0
3003 DATA 0,0,0,1,0,0,0,0,0,0
3004 DATA 1,0,1,0,2,2,0,1,0,1
3005 DATA 1,0,1,0,2,2,0,1,0,1
3006 DATA 0,0,0,0,0,0,1,0,0,0
3007 DATA 0,0,0,0,0,0,0,1,0,0
3008 DATA 0,0,1,3,4,3,0,0,0,5

3011 DATA 2,0,0,0,2,1,2,1,0,0
3012 DATA 0,0,2,0,0,0,0,0,0,0
3013 DATA 0,0,0,3,0,0,0,0,0,0
3014 DATA 4,0,2,0,2,1,0,1,0,3
3015 DATA 1,0,3,0,1,2,0,4,0,2
3016 DATA 0,0,0,0,0,0,1,0,0,0
3017 DATA 0,0,0,0,0,0,0,4,0,0
3018 DATA 0,0,3,4,1,4,0,0,0,4

3021 DATA 2,0,0,0,2,2,2,2,0,0
3022 DATA 0,0,2,0,0,0,0,0,0,0
3023 DATA 0,0,0,1,0,0,0,0,0,0
3024 DATA 2,0,1,0,2,2,0,2,0,1
3025 DATA 2,0,1,0,1,1,0,2,0,1
3026 DATA 0,0,0,0,0,0,2,0,0,0
3027 DATA 0,0,0,0,0,0,0,1,0,0
3028 DATA 0,0,1,1,1,1,0,0,0,1
