0 CLS:CLEAR650:DIMCC,ZZ,Z$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1)
2 IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
3 C1=((PEEK(17024)AND1)*256+PEEK(17025))+1:IFC1>479THENGOSUB5
4 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 PRINT@480,"hit any key";:POKE16867,32:POKE16871,32
6 IFINKEY$=""THEN6
7 CLS:RETURN
10 CLS:DIM B(19,9),M(19,4),S(9),P$(3)
11 Z$="HEXAPAWN":GOSUB14
12 Z$="CREATIVE COMPUTING":GOSUB14:Z$="MORRISTOWN, NEW JERSEY":GOSUB14
13 Z$=" ":GOSUB1:Z$=" ":GOSUB1:GOTO15
14 FORCC=1TO2STEP-1:PRINTTAB((32-LEN(Z$))/2)Z$:GOTO3
15 W=0:L=0:GOTO35
20 ZZ=-3*(CC=1)-(CC=3)-4*(CC=6)-6*(CC=4)-7*(CC=9)-9*(CC=7)-CC*(CC=2 OR CC=5 OR CC=8):RETURN:REM FNR
30 ZZ=CC-INT(CC/10)*10:RETURN:REM FNM
35 P$="X.O"
40 FOR I=1 TO 19:FOR J=1 TO 9:READ B(I,J):NEXT J:NEXT I
45 FOR I=1 TO 19:FOR J=1 TO 4:READ M(I,J):NEXT J:NEXT I
50 Z$="INSTRUCTIONS (Y/N)":GOSUB1
60 INPUT A$
70 A$=LEFT$(A$,1)
80 IF A$="Y" THEN 2000
90 IF A$<>"N" THEN 50
100 X=0:Y=0
111 S(4)=0:S(5)=0:S(6)=0
112 S(1)=-1:S(2)=-1:S(3)=-1
113 S(7)=1:S(8)=1:S(9)=1
115 GOSUB 1000
120 PRINT "YOUR MOVE";
121 INPUT M1,M2
122 IF M1=INT(M1) AND M2=INT(M2) AND M1>0 AND M1<10 AND M2>0 AND M2<10 THEN 130
123 PRINT "ILLEGAL CO-ORDINATES."
124 GOTO 120
130 IF S(M1)=1 THEN 150
140 PRINT "ILLEGAL MOVE.":GOTO 120
150 IF S(M2)=1 THEN 140
160 IF M2-M1<>-3 AND S(M2)<>-1 THEN 140
170 IF M2>M1 THEN 140
180 IF M2-M1=-3 AND (S(M2)<>0) THEN 140
185 IF M2-M1<-4 THEN 140
186 IF M1=7 AND M2=3 THEN 140
190 S(M1)=0
200 S(M2)=1
205 GOSUB 1000
210 IF S(1)=1 OR S(2)=1 OR S(3)=1 THEN 820
220 FOR I=1 TO 9
221 IF S(I)=-1 THEN 230
222 NEXT I
223 GOTO 820
230 FOR I=1 TO 9
240 IF S(I)<>-1 THEN 330
250 IF S(I+3)=0 THEN 350
260 CC=I:GOSUB20:IF ZZ=I THEN 320
270 IF I>3 THEN 300
280 IF S(5)=1 THEN 350
290 GOTO 330
300 IF S(8)=1 THEN 350
310 GOTO 330
320 IF S(I+2)=1 OR S(I+4)=1 THEN 350
330 NEXT I
340 GOTO 820
350 FOR I=1 TO 19
360 FOR J=1 TO 3
370 FOR K=3 TO 1 STEP -1
380 T((J-1)*3+K)=B(I,(J-1)*3+4-K)
390 NEXT K
400 NEXT J
410 FOR J=1 TO 9
420 IF S(J)<>B(I,J) THEN 460
430 NEXT J
440 R=0
450 GOTO 540
460 FOR J=1 TO 9
470 IF S(J)<>T(J) THEN 510
480 NEXT J
490 R=1
500 GOTO 540
510 NEXT I
511 REMEMBER THE TERMINATION OF THIS LOOP IS IMPOSSIBLE
512 PRINT "ILLEGAL BOARD PATTERN."
530 STOP
540 X=I
550 FOR I=1 TO 4
560 IF M(X,I)<>0 THEN 600
570 NEXT I
580 PRINT "I RESIGN."
590 GOTO 820
600 Y=INT(RND(0)*4+1)
601 IF M(X,Y)=0 THEN 600
610 IF R<>0 THEN 630
620 CC=M(X,Y):GOSUB30:PRINT "I MOVE FROM";STR$(INT(M(X,Y)/10));" TO";STR$(ZZ)
622 S(INT(M(X,Y)/10))=0
623 CC=M(X,Y):GOSUB30:S(ZZ)=-1
624 GOTO 640
630 CC=INT(M(X,Y)/10):GOSUB20:PRINT "I MOVE FROM";STR$(ZZ);" TO";
631 CC=M(X,Y):GOSUB30:CC=ZZ:GOSUB20:PRINT STR$(ZZ)
632 CC=INT(M(X,Y)/10):GOSUB20:S(ZZ)=0
633 CC=M(X,Y):GOSUB30:CC=ZZ:GOSUB20:S(ZZ)=-1
640 GOSUB 1000
641 IF S(7)=-1 OR S(8)=-1 OR S(9)=-1 THEN 870
650 FOR I=1 TO 9
660 IF S(I)=1 THEN 690
670 NEXT I
680 GOTO 870
690 FOR I=1 TO 9
700 IF S(I)<>1 THEN 790
710 IF S(I-3)=0 THEN 120
720 CC=I:GOSUB20:IF ZZ=I THEN 780
730 IF I<7 THEN 760
740 IF S(5)=-1 THEN 120
750 GOTO 790
760 IF S(2)=-1 THEN 120
770 GOTO 790
780 IF S(I-2)=-1 OR S(I-4)=-1 THEN 120
790 NEXT I
800 Z$="YOU CAN'T MOVE, SO "
810 GOTO 870
820 PRINT "YOU WIN."
830 M(X,Y)=0
840 L=L+1
850 PRINT"I HAVE WON"+STR$(W):PRINT"AND YOU"+STR$(L):PRINT"OUT OF"+STR$(L+W)+" GAMES."
851 PRINT
860 GOTO 100
870 Z$=Z$+"I WIN.":GOSUB1
880 W=W+1
890 GOTO 850
900 DATA -1,-1,-1,1,0,0,0,1,1,-1,-1,-1,0,1,0,1,0,1
905 DATA -1,0,-1,-1,1,0,0,0,1,0,-1,-1,1,-1,0,0,0,1
910 DATA -1,0,-1,1,1,0,0,1,0,-1,-1,0,1,0,1,0,0,1
915 DATA 0,-1,-1,0,-1,1,1,0,0,0,-1,-1,-1,1,1,1,0,0
920 DATA -1,0,-1,-1,0,1,0,1,0,0,-1,-1,0,1,0,0,0,1
925 DATA 0,-1,-1,0,1,0,1,0,0,-1,0,-1,1,0,0,0,0,1
930 DATA 0,0,-1,-1,-1,1,0,0,0,-1,0,0,1,1,1,0,0,0
935 DATA 0,-1,0,-1,1,1,0,0,0,-1,0,0,-1,-1,1,0,0,0
940 DATA 0,0,-1,-1,1,0,0,0,0,0,-1,0,1,-1,0,0,0,0
945 DATA -1,0,0,-1,1,0,0,0,0
950 DATA 24,25,36,0,14,15,36,0,15,35,36,47,36,58,59,0
955 DATA 15,35,36,0,24,25,26,0,26,57,58,0
960 DATA 26,35,0,0,47,48,0,0,35,36,0,0,35,36,0,0
965 DATA 36,0,0,0,47,58,0,0,15,0,0,0
970 DATA 26,47,0,0,47,58,0,0,35,36,47,0,28,58,0,0,15,47,0,0
1000 PRINT
1010 FOR I=1 TO 3
1020 FOR J=1 TO 3
1030 PRINT MID$(P$,S((I-1)*3+J)+2,1);
1040 NEXT J
1050 PRINT
1060 NEXT I
1070 PRINT
1080 RETURN
2000 Z$=" ":GOSUB1:Z$="THIS PROGRAM PLAYS THE GAME OF HEXAPAWN.":GOSUB1
2010 Z$="HEXAPAWN IS PLAYED WITH CHESS PAWNS ON A 3 BY 3 BOARD.":GOSUB1
2020 Z$="THE PAWNS ARE MOVED AS IN CHESS-- ONE SPACE FORWARD TO "
2030 Z$=Z$+"AN EMPTY SPACE OR ONE SPACE FORWARD AND DIAGONALLY TO "
2040 Z$=Z$+"CAPTURE AN OPPOSING MAN.":GOSUB1
2050 Z$="ON THE BOARD, YOUR PAWNS ARE 'O', THE COMPUTER'S PAWNS ARE 'X', AND EMPTY "
2060 Z$="":GOSUB1:Z$="SQUARES ARE '.'.  TO ENTER A MOVE, TYPE THE NUMBER OF "
2070 Z$=Z$+"THE SQUARE YOU ARE MOVING FROM, FOLLOWED BY THE NUMBER "
2080 Z$=Z$+"OF THE SQUARE YOU WILL MOVE TO.":GOSUB1
2090 Z$="THE NUMBERS MUST BE SEPERATED BY A COMMA.":Z$="":GOSUB1
2100 Z$="THE COMPUTER STARTS A SERIES OF GAMES KNOWING ONLY WHEN "
2105 Z$=Z$+"THE GAME IS WON (A DRAW IS IMPOSSIBLE) AND HOW TO MOVE.":GOSUB1
2110 Z$="IT HAS NO STRATEGY AT FIRST AND JUST MOVES RANDOMLY.":GOSUB1
2120 Z$="HOWEVER, IT LEARNS FROM EACH GAME.  THUS, WINNING BECOMES "
2130 Z$=Z$+"MORE AND MORE DIFFICULT.":GOSUB1
2140 Z$="ALSO, TO HELP OFFSET YOUR INITIAL ADVANTAGE, YOU WILL NOT BE TOLD HOW TO WIN THE "
2150 Z$=Z$+"GAME BUT MUST LEARN THIS BY PLAYING.":GOSUB1
2160 Z$="":GOSUB1:Z$="THE NUMBERING OF THE BOARD IS AS FOLLOWS:":GOSUB1
2170 Z$="123":GOSUB14:Z$="456":GOSUB14:Z$="789":GOSUB14
2180 Z$="":GOSUB1Z$="FOR EXAMPLE, TO MOVE YOUR RIGHTMOST PAWN FORWARD, "
2190 Z$=Z$+"YOU WOULD TYPE 9,6 IN RESPONSE TO THE QUESTION 'YOUR MOVE?'.":GOSUB1
2210 Z$="SINCE I'M A GOOD SPORT, YOU'LL ALWAYS GO FIRST.":GOSUB1:Z$=" ":GOSUB1
2220 GOTO 100
8884 REM HEXAPAWN: INTERPRETATION OF HEXAPAWN GAME AS PRESENTED IN
8885 REM MARTIN GARDNER'S "THE UNEXPECTED HANGING AND OTHER MATHEMATIC-
8886 REM AL DIVERSIONS", CHAPTER EIGHT:  A MATCHBOX GAME-LEARNING MACHINE
8887 REM ORIGINAL VERSION FOR H-P TIMESHARE SYSTEM BY R.A. KAAPKE 5/5/76
8888 REM INSTRUCTIONS BY JEFF DALTON
8889 REM CONVERSION TO MITS BASIC BY STEVE NORTH
8890 REM MC-10 EDITS JIM GERRIE
9999 END
