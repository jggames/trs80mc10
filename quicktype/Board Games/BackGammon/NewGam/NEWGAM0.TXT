10O=0:B=0:A(0)=0:A(25)=0:Q=4
20DATA 2,0,0,0,0,-5,0,-3,0,0,0,5,-5,0,0,0,3,0,5,0,0,0,0,-2
30FOR X=1 TO 24:READ A(X):NEXT X
40GOSUB 1000
60P=RND(6):C=RND(6)
70PRINT AT 0;"ROLL: ";:COLOR 12:PRINT"PLAYER";P;:PRINT AT 14;", ";
75COLOR 9:PRINT"COMPUTER";C;:PRINT AT 26;". ";
80COLOR 15:IF P=C THEN 60
90IF P>C THEN PRINT"PLAYER";:GOTO 110
100PRINT"COMPUTER";
110INPUT" GOES FIRST.  PRESS ENTER";A$
120IF P>C THEN 600
130REM COMPUTER'S TURN
140T=0:GOSUB 1300
150GOSUB 1350:PRINT".  THINKING...";
155REM ARE ALL PIECES ON INNER BOARD?
160C=1:FOR Z=0 TO 18:IF A(Z)>0 THEN C=0
170NEXT Z
171REM PLAYER HASN'T PASSED COMPUTER?
172V=0:P=0:FOR Z=1 TO 24:IF (A(Z)<0)*P THEN V=1
174IF A(Z)>0 THEN P=1
176NEXT Z
178H=0:L=0:R=0:FOR Y=0 TO 3:IF A(Y+26)=0 THEN 315
180FOR X=0 TO 25-A(Y+26):E=0:IF A(X)<=0 THEN 310
190IF (X=0)*(A(X)>0) THEN E=E+128
200IF X+A(26+Y)=25 THEN E=E+64
210IF (A(X)=1)*(A(X+A(26+Y)))*V THEN E=E+16
220IF (A(X)<>2)*(A(X+A(26+Y)))*V THEN E=E+8
230IF (A(X+A(26+Y))=-1)+(X<7) THEN E=E+32
240IF X<19 THEN E=E+4
250IF (A(X+A(26+Y))<-1)+((C=0)*(X+A(26+Y)>24)) THEN 310
260E=E+1:IF (X+A(26+Y)=25)+(Y=3) THEN 300 
270FOR Z=0 TO 3:D=X+A(26+Y)-A(26-Z):IF (Y=Z)+(A(26+Z)=0)+(D<0)+(D>24) THEN 290
280IF (A(D)=1)+(A(D)>2) THEN E=E+2:GOTO 300
290NEXT Z
300IF (H<E)+(H=E)*((X<L)+(X=L)*(R<A(26+Y))) THEN H=E:R=A(26+Y):L=X
310NEXT X
315NEXT Y
320IF (H<128)*(A(0)>0) THEN 580
330IF H<>0 THEN 390
340R=7:FOR Y=0 TO 3:IF (A(26+Y)<R)*(A(26+Y)>1) THEN R=A(26+Y):L=Y
350NEXT Y:FOR X=0 TO 25-R:IF A(X)>0 THEN 580
360NEXT X:FOR X=26-R TO 24:IF A(X)>0 THEN A(26+L)=25-X:GOTO 178
370NEXT X:GOTO 580
390IF L>0 THEN C=A(L):P=L:GOSUB 1600
400A(L)=A(L)-1:IF L=0 THEN GOSUB 1550
440IF A(L+R)=-1 THEN B=B+1:A(L+R)=0:C=1:P=L+R:GOSUB 1600:GOSUB 1550
450Y=A(L+R)+1:A(L+R)=Y:IF L+R<25 THEN C=A(L+R):P=L+R:GOSUB 2000:GOTO 453
452GOSUB 1550
453FOR Z=0 TO 3:IF A(26+Z)<>R THEN NEXT Z
454GOSUB 1350:PRINT". MOVE FROM";:X=L:GOSUB 1850:PRINT" TO";:X=L+R:GOSUB 1850:A(26+Z)=0
455COLOR 15:INPUT". PRESS ENTER";A$
460IF A(25)=15 THEN 560
470FOR X=0 TO 3:IF A(26+X)>0 THEN 150
480NEXT X:GOTO 600
560GOSUB 1500:PRINT AT 0;"I WIN.  ANOTHER GAME (Y/N)";:GOTO 860
580GOSUB 1350:PRINT". CANNOT MOVE";
590INPUT". PRESS ENTER";A$
600REM PLAYER'S TURN
605IF CPOINT(0,6)<>Q THEN GOSUB 1000
610T=1:GOSUB 1300
620GOSUB 1350:IF B THEN F=25:PRINT". MOVE FROM BAR";:GOTO 640
630R=999:N=99:INPUT". MOVE FROM";F:F=INT(F)
635IF F=N THEN 130
637IF CPOINT(0,6)<>Q THEN GOSUB 1000:GOTO 620
640IF F=R THEN GOSUB 1000:GOTO 620
650IF ((F=25)*(B=0))+(F<1)+(F>25) THEN 980
660IF (F<25)*(A(F)>=0) THEN 980
670PRINT AT 41;:X=O:O=0:N=99:R=999:INPUT"TO";D:O=X:D=INT(D)
671IF CPOINT(0,6)<>Q THEN GOSUB 1000:GOTO 620
672IF D=N THEN 130
673IF D=R THEN GOSUB 1000:GOTO 620
675IF (D<0)+(D>24) THEN 980
680IF A(D)>1 THEN 980
682M=0:G=0:FOR W=0 TO 3:IF A(26+W)=F-D THEN 690
683IF M<A(26+W) THEN M=A(26+W):G=W
684NEXT W:IF (D<>0)+(B<>0) THEN 980
685REM DICE EXCEED WHAT'S NEEDED TO MOVE OFF?
686W=G:IF F<M THEN M=F+1
687FOR X=M TO 6:IF A(X)<0 THEN 980
688NEXT X:GOTO 706
689REM PUT COMPUTER ON BAR?
690IF (D>0)*(A(D)=1) THEN A(0)=A(0)+1:GOSUB 1550:A(D)=0
695REM FROM BAR?
700IF F=25 THEN B=B-1:GOSUB 1550:GOTO 730
702REM FROM NORMAL
704IF D<>0 THEN 710
706FOR X=7 TO 24:IF A(X)<0 THEN 980
708NEXT X
710C=A(F):P=F:GOSUB 1600:A(F)=A(F)+1
720IF D=0 THEN 800
730A(D)=A(D)-1:P=D:C=A(D):GOSUB 2000:A(26+W)=0
740REM ANY MORE DICE LEFT?
750FOR W=0 TO 3:IF A(26+W)<>0 THEN 620
760NEXT W:GOTO 130
800REM JUST MOVED OFF
840O=O+1:GOSUB 1550:A(26+W)=0:IF O<15 THEN 750
850GOSUB 1500:PRINT AT 0;"YOU WIN!  ANOTHER GAME (Y/N)";
860Y=1:N=0:INPUT A:IF (A<>Y)*(A<>N) THEN GOTO 820
870IF A=Y THEN GOTO 10
880END
970REM ILLEGAL MOVE
980COLOR 14:GOSUB 1500:PRINT AT 0;
990INPUT"ILLEGAL MOVE.  PRESS ENTER";A$:GOTO 620
1000REM REDRAW SCREEN
1002COLOR 2:INVERSE 1:CLS:INVERSE 0:COLOR 15
1003FOR X=0 TO 64:PRINT" ";:NEXT X
1004PRINT AT 64;"  BAR    24   23  22   21  20   19  18   17  16   15  14   13  ";
1005PRINT AT 15*64;"  OFF    01   02  03   04  05   06  07   08  09   10  11   12  ";
1010COLOR Q:FOR X=0 TO 127:SET(X,6):SET(X,44):NEXT X
1020FOR Y=6 TO 44:SET(0,Y):SET(1,Y):SET(12,Y):SET(13,Y)
1030SET(126,Y):SET(127,Y):NEXT Y
1040FOR X=2 TO 11:SET(X,25):NEXT X
1050COLOR 15:FOR X=18 TO 108 STEP 18
1060FOR Y=7 TO 12:RESET(X,Y):RESET(X+1,Y):RESET(X+2,Y)
1070RESET(X+3,Y):RESET(X+4,Y):SET(X+9,Y):SET(X+10,Y)
1080SET(X+11,Y):SET(X+12,Y):SET(X+13,Y):NEXT Y
1090FOR Y=13 TO 18:RESET(X+1,Y):RESET(X+2,Y):RESET(X+3,Y)
1100SET(X+10,Y):SET(X+11,Y):SET(X+12,Y):NEXT Y
1110FOR Y=19 TO 23:RESET(X+2,Y):SET(X+11,Y):NEXT Y
1120FOR Y=43 TO 38 STEP -1:SET(X,Y):SET(X+1,Y):SET(X+2,Y)
1130SET(X+3,Y):SET(X+4,Y):RESET(X+9,Y):RESET(X+10,Y)
1140RESET(X+11,Y):RESET(X+12,Y):RESET(X+13,Y):NEXT Y
1150FOR Y=37 TO 32 STEP -1:SET(X+1,Y):SET(X+2,Y):SET(X+3,Y)
1160RESET(X+10,Y):RESET(X+11,Y):RESET(X+12,Y):NEXT Y
1170FOR Y=31 TO 27 STEP -1:SET(X+2,Y):RESET(X+11,Y):NEXT Y
1180NEXT X:COLOR 15:INVERSE 0
1190GOSUB 1550
1200PRINT AT 322;:COLOR 2:INVERSE 1
1210PRINT "BAR";:PRINT AT 706;"OFF";
1220FOR X=4 TO 9:COLOR 12:SET(X,12):SET(X,30)
1230COLOR 1:SET(X,19):SET(X,37):NEXT X
1240FOR P=1 TO 24:S=1:IF A(P)<0 THEN S=-1
1250FOR C=0 TO A(P) STEP S:GOSUB 2000:NEXT C:NEXT P
1260COLOR 2:PRINT AT 0;:COLOR 15:INVERSE 0
1270RETURN
1300REM ROLL DICE
1310A(26)=RND(6):A(27)=RND(6):A(28)=0:A(29)=0
1320IF A(26)=A(27) THEN A(28)=A(26):A(29)=A(26)
1325FOR X=0 TO 3:A(30+X)=A(26+X):NEXT X
1330RETURN
1350REM PRINT DICE
1360GOSUB 1500:IF T=0 THEN COLOR 9:PRINT AT 4;"COMPUTER";:GOTO 1370
1365COLOR 12:PRINT AT 4;"PLAYER";
1370FOR X=0 TO 6:SET(X,1):NEXT X
1375COLOR 15:PRINT" ROLLED";:FOR X=26 TO 29
1380IF A(X)<>0 THEN ON A(X+4) GOSUB 1400, 1410, 1420, 1430, 1440, 1450
1390NEXT X:RETURN
1400PRINT" 1";:RETURN
1410PRINT" 2";:RETURN
1420PRINT" 3";:RETURN
1430PRINT" 4";:RETURN
1440PRINT" 5";:RETURN
1450PRINT" 6";:RETURN
1500REM CLEAR TOP LINE
1510PRINT AT 0;"                                                                ";
1520RETURN
1550REM UPDATE BAR/OFF COUNTERS
1560PRINT AT 193;:COLOR 2:INVERSE 1:PRINT B;:PRINT AT 449;A(0);
1570PRINT AT 577;O;:PRINT AT 833;A(25);
1580PRINT AT 0;:COLOR 15:INVERSE 0:RETURN
1600REM ERASE A PIECE
1610IF C=0 THEN RETURN
1620C=ABS(C):X=9*P+9:Y=44-C*2
1630IF P>12 THEN X=9*(26-P):Y=6+C*2
1640IF C>9 THEN RETURN
1650IF P-INT(P/2)*2 THEN 1750
1660IF C=9 THEN 1740
1670ON INT((C-1)/3)+1 GOTO 1680, 1700, 1720
1680COLOR 2:SET(X-1,Y):RESET(X,Y):RESET(X+1,Y):RESET(X+2,Y)
1690RESET(X+3,Y):RESET(X+4,Y):SET(X+5,Y):RETURN
1700COLOR 2:SET(X-1,Y):SET(X,Y):RESET(X+1,Y):RESET(X+2,Y)
1710RESET(X+3,Y):SET(X+4,Y):SET(X+5,Y):RETURN
1720COLOR 2:SET(X-1,Y):SET(X,Y):SET(X+1,Y):RESET(X+2,Y)
1730SET(X+3,Y):SET(X+4,Y):SET(X+5,Y):RETURN
1740COLOR 2:FOR A=X-1 TO X+5:SET(A,Y):NEXT A:RETURN
1750IF C=9 THEN 1820
1755ON INT((C-1)/3)+1 GOTO 1760, 1780, 1800
1760COLOR 2:SET(X-1,Y):COLOR 15:SET(X,Y):SET(X+1,Y):SET(X+2,Y)
1770SET(X+3,Y):SET(X+4,Y):COLOR 2:SET(X+5,Y):RETURN
1780COLOR 2:SET(X-1,Y):SET(X,Y):COLOR 15:SET(X+1,Y):SET(X+2,Y)
1790SET(X+3,Y):COLOR 2:SET(X+4,Y):SET(X+5,Y):RETURN
1800COLOR 2:SET(X-1,Y):SET(X,Y):SET(X+1,Y):COLOR 15:SET(X+2,Y)
1810COLOR 2:SET(X+3,Y):SET(X+4,Y):SET(X+5,Y):RETURN
1820COLOR 2:FOR A=X-1 TO X+5:SET(A,Y):NEXT A:RETURN
1850REM PRINT TWO DIGIT NUMBER
1860ON X+1 GOTO 1870,1880,1885,1890,1895,1900,1905,1910,1915,1920,1925,1930,1935,1940,1945,1950,1955,1960,1965,1970,1975,1980,1985,1990,1995,1997
1870PRINT" BAR";:RETURN
1880PRINT" 01";:RETURN
1885PRINT" 02";:RETURN
1890PRINT" 03";:RETURN
1895PRINT" 04";:RETURN
1900PRINT" 05";:RETURN
1905PRINT" 06";:RETURN
1910PRINT" 07";:RETURN
1915PRINT" 08";:RETURN
1920PRINT" 09";:RETURN
1925PRINT" 10";:RETURN
1930PRINT" 11";:RETURN
1935PRINT" 12";:RETURN
1940PRINT" 13";:RETURN
1945PRINT" 14";:RETURN
1950PRINT" 15";:RETURN
1955PRINT" 16";:RETURN
1960PRINT" 17";:RETURN
1965PRINT" 18";:RETURN
1970PRINT" 19";:RETURN
1975PRINT" 20";:RETURN
1980PRINT" 21";:RETURN
1985PRINT" 22";:RETURN
1990PRINT" 23";:RETURN
1995PRINT" 24";:RETURN
1997PRINT" OFF";:RETURN
2000REM DRAW A PIECE
2010IF C=0 THEN RETURN
2030X=9*P+9:Y=44-ABS(C)*2
2040IF P>12 THEN X=9*(26-P):Y=6+ABS(C)*2
2050IF ABS(C)>9 THEN RETURN
2060COLOR 1:IF C<0 THEN COLOR 12
2070FOR A=X-1 TO X+5:SET(A,Y):NEXT A
2080RETURN
