0 CLEAR3000:CLS:DIMC1,CC,ZZ,A$(26),B$(26),H(40),N$(2),M(2),I(2),G$(40),L(40),P(40),R(40),C$(10),Z$(10),M$(20):GOTO8
3 FN=(H(I(Z))*R(I(Z))*10):RETURN
4 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=.
5 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
8 M(1)=1500:M(2)=1500:I(1)=0:I(2)=0:FN=0:P$="HOW MANY HOUSES AT $":O$=" EACH DO YOU WANT ON "
9 FORI=1TO40:L(I)=0:H(I)=0:NEXTI:FORI=1TO8:U(I)=0:NEXTI
10 FORI=1TO40:READP(I),R(I):NEXTI
11 FORI=1TO40:READG$(I):NEXTI
12 FORI=1TO20:READM$(I):NEXTI
13 FORI=1TO10:READC$(I):NEXTI
14 FORI=1TO10:READZ$(I):NEXTI:RESTORE
15 M$=M$(1):GOSUB4:PRINT:PRINT:PRINT"** RULES **":M$=M$(2):GOSUB4
16 M$=M$(3)+" "+M$(4):GOSUB4:M$=M$(5):GOSUB4:PRINT
17 FORI=1TO40:READP(I),R(I):NEXTI
18 DATA60,2,,,60,4,,,200,25,100,6,,,100,6,120,8,,0
19 DATA140,10,150,35,140,10,160,12,200,25,180,14,,0
20 DATA180,14,200,16,,,220,18,,,220,18,240,20,200,25
21 DATA260,22,260,22,150,35,280,24,,,300,26,300,26,,0
22 DATA320,28,200,25,,,350,35,,,400,50,,0
23 RESTORE
26 N$(1)="":N$(2)=""
30 PRINT"WHO IS PLAYER #1";:INPUT N$(1)
40 PRINT"WHO IS PLAYER #2";:INPUT N$(2)
100 PRINT:M$="***** "+N$(1)+"'S TURN *****"::GOSUB4:F=0
102 Z=1:GOSUB1000
103 GOSUB2000
115 IFD=D1THENF=F+1:IFF<3THENM$=M$(9):GOSUB4:GOTO102
117 IFF=3THENM$=M$(10):GOSUB4:I(Z)=10:M(Z)=M(Z)-50
200 PRINT:M$="***** "+N$(2)+"'S TURN *****":GOSUB4:F=0
202 Z=2:GOSUB1000
203 GOSUB2000
215 IFD=D1THENF=F+1:IFF<3THENM$=M$(9):GOSUB4:GOTO202
217 IFF=3THENM$=M$(10):GOSUB4:I(Z)=10:M(Z)=M(Z)-50
225 M$=M$(11):GOSUB4:INPUTF$:IFLEFT$(F$,1)="Q"THEN30000
227 IFLEFT$(F$,1)="B"THENGOSUB6400:GOTO225
228 GOTO100
1000 M$=M$(6):GOSUB4
1002 I$=INKEY$:X=RND(0):Y=RND(0):IFI$<>"R"THEN1002
1004 D=INT(X*6+1):D1=INT(Y*6+1)
1015 PRINT"YOU ROLLED A"D"AND A"D1:I(Z)=I(Z)+(D+D1)
1016 IFI(Z)>40THENI(Z)=I(Z)-40:M(Z)=M(Z)+200
1017 RETURN
2000 M$="YOU ARE ON "+G$(I(Z)):GOSUB4
2002 IFI(Z)=2ORI(Z)=17ORI(Z)=33THEN4100
2004 IFI(Z)=4THENM(Z)=M(Z)-200:RETURN
2006 IFI(Z)=7ORI(Z)=22ORI(Z)=36THEN4400
2008 IFI(Z)=10ORI(Z)=20THEN2015
2009 IFI(Z)=30THENM(Z)=M(Z)-50:I(Z)=10:RETURN
2011 IFI(Z)=38THENM(Z)=M(Z)-75:RETURN
2013 IFI(Z)<>40THEN2017
2014 M(Z)=M(Z)+200:I(Z)=0:RETURN
2015 RETURN
2017 IFL(I(Z))=ZTHENM$=M$(12):GOSUB4:GOTO3100
2018 IFH(I(Z))<>0ORL(I(Z))=<0THEN2020
2019 IFL(I(Z))<>ZTHEN2125
2020 IFH(I(Z))=<0ORL(I(Z))=<0THEN2022
2021 IFL(I(Z))<>ZTHEN2100
2022 M$=M$(7)+STR$(P(I(Z))):GOSUB4
2024 M$=M$(8):GOSUB4:INPUTF$:IFLEFT$(F$,1)<>"B"THEN3100
2026 M(Z)=M(Z)-P(I(Z)):M$="YOU NOW HAVE $"+STR$(M(Z)):GOSUB4
2027 L(I(Z))=Z:GOTO3100
2100 IFZ=1THENGOSUB3:M(Z)=M(Z)-FN:GOSUB3:M(2)=M(2)+FN
2101 GOSUB3:M$="YOU OWE $"+STR$(FN)+" RENT":GOSUB4:RETURN
2102 IFZ=2THENGOSUB3:M(Z)=M(Z)-FN:GOSUB3:M(1)=M(1)+FN
2103 GOSUB3:M$="YOU OWE $"+STR$(FN)+" RENT":GOSUB4:RETURN
2125 IFZ=1THENM(Z)=M(Z)-R(I(Z)):M(2)=M(2)+R(I(Z))
2126 M$="YOU OWE $"+STR$(R(I(Z)))+" RENT":GOSUB4:RETURN
2127 IFZ=2THEN M(Z)=M(Z)-R(I(Z)):M(1)=M(1)+R(I(Z))
2128 M$="YOU OWE $"+STR$(R(I(Z)))+" RENT":GOSUB4:RETURN
3100 M$=M$(13)+" "+M$(14):GOSUB4:INPUTF$:IFLEFT$(F$,1)="S"THEN3102
3101 GOTO3105
3102 GOSUB3150
3105 M$=M$(19):GOSUB4:INPUTH$:IFLEFT$(H$,1)="H"THEN3190
3106 RETURN
3150 M$=M$(15):GOSUB4:INPUTF$:IFLEFT$(F$,1)<>"L"THEN3152
3151 GOSUB3170
3152 M$=M$(17):GOSUB4:INPUTV$:IFV$=""THENRETURN
3153 M$="HOW MUCH DO YOU WANT FOR IT":GOSUB4:INPUTV
3154 FORI=1TO39:IFV$<>G$(I)THEN3162
3155 IFL(I)=0THENI=39:GOTO3162
3157 IFL(I)=1THENM(1)=M(1)+V:M(2)=M(2)-V:L(I)=2:I=99:GOTO3162
3160 IFL(I)=2THENM(2)=M(2)+V:M(1)=M(1)-V:L(I)=1:I=99
3162 NEXT:IFI=40THENM$=M$(16):GOSUB4
3165 M$=M$(18):GOSUB4:INPUTF$:IFLEFT$(F$,1)="Y"THEN3152
3166 RETURN
3170 C=0:D=0:A$(1)="":B$(1)="":FORI=1TO39
3171 IFL(I)=1THENC=C+1:A$(C)=G$(I):GOTO3176
3173 IFL(I)=2THEND=D+1:B$(D)=G$(I):GOTO3176
3176 NEXT
3180 LN=0:M$=N$(1)+"'S PROPERTY:":GOSUB4:LN=LN+1
3181 FORI=1TOC:M$=A$(I):GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
3182 NEXT
3185 M$=N$(2)+"'S PROPERTY:":GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
3186 FORI=1TOD:M$=B$(I):GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
3187 NEXT:RETURN
3190 IFL(1)<>ZORL(3)<>ZTHEN3195
3191 M$=P$+"50"+O$+G$(1)+" AND "+G$(3):GOSUB4:INPUTU:IFU>0THENGOSUB3250
3195 IFL(6)<>ZORL(8)<>ZORL(9)<>ZTHEN3199
3196 M$=P$+"50"+O$+G$(6)+", "+G$(8)+" AND "+G$(9):GOSUB4:INPUTU:IFU>0THENGOSUB3254
3199 IFL(11)<>ZORL(13)<>ZORL(14)<>ZTHEN3203
3200 M$=P$+"100"+O$+G$(11)+", "+G$(13)+" AND "+G$(14):GOSUB4:INPUTU:IFU>0THENGOSUB3258
3203 IFL(16)<>ZORL(18)<>ZORL(19)<>ZTHEN3207
3206 M$=P$+"100"+O$+G$(16)+", "+G$(18)+" AND "+G$(19):GOSUB4:INPUTU:IFU>0THENGOSUB3262
3207 IFL(21)<>ZORL(23)<>ZORL(24)<>ZTHEN3210
3209 M$=P$+"150"+O$+G$(21)+", "+G$(23)+" AND "+G$(24):GOSUB4:INPUTU:IFU>0THENGOSUB3266
3210 IFL(26)<>ZORL(27)<>ZORL(29)<>ZTHEN3212
3211 M$=P$+"150"+O$+G$(26)+", "+G$(27)+" AND "+G$(29):GOSUB4:INPUTU:IFU>0THENGOSUB3270
3212 IFL(31)<>ZORL(32)<>ZORL(34)<>ZTHEN3214
3213 M$=P$+"200"+O$+G$(31)+", "+G$(32)+" AND "+G$(34):GOSUB4:INPUTU:IFU>0THENGOSUB3274
3214 IFL(37)<>ZORL(39)<>ZTHEN3220
3215 M$=P$+"200"+O$+G$(37)+" AND "+G$(39):GOSUB4:INPUTU:IFU>0THENGOSUB3278
3220 RETURN
3250 U(1)=U(1)+U:M(Z)=M(Z)-(U*100):IFM(Z)=>0THEN3252
3251 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*100):U(1)=U(1)-U:RETURN
3252 H(1)=U(1):H(3)=U(1):RETURN
3254 U(2)=U(2)+U:M(Z)=M(Z)-(U*150):IFM(Z)=>0THEN3256
3255 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*150):U(2)=U(2)-U:RETURN
3256 H(6)=U(2):H(8)=U(2):H(9)=U(2):RETURN
3258 U(3)=U(3)+U:M(Z)=M(Z)-(U*150):IFM(Z)=>0THEN3260
3259 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*150):U(3)=U(3)-U:RETURN
3260 H(11)=U(3):H(13)=U(3):H(14)=U(3):RETURN
3262 U(4)=U(4)+U:M(Z)=M(Z)-(U*300):IFM(Z)=>0THEN3264
3263 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*300):U(4)=U(4)-U:RETURN
3264 H(16)=U(4):H(18)=U(4):H(19)=U(4):RETURN
3266 U(5)=U(5)+U:M(Z)=M(Z)-(U*450):IFM(Z)=>0THEN3268
3267 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*450):U(5)=U(5)-U:RETURN
3268 H(21)=U(5):H(23)=U(5):H(24)=U(5):RETURN
3270 U(6)=U(6)+U:M(Z)=M(Z)-(U*450):IFM(Z)=>0THEN3272
3271 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*450):U(6)=U(6)-U:RETURN
3272 H(26)=U(6):H(27)=U(6):H(29)=U(6):RETURN
3274 U(7)=U(7)+U:M(Z)=M(Z)-(U*600):IFM(Z)=>0THEN3276
3275 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*600):U(7)=U(7)-U:RETURN
3276 H(31)=U(7):H(32)=U(7):H(34)=U(7):RETURN
3278 U(8)=U(8)+U:M(Z)=M(Z)-(U*400):IFM(Z)=>0THEN3280
3279 M$=M$(20):GOSUB4:M(Z)=M(Z)+(U*400):U(8)=U(8)-U:RETURN
3280 H(37)=U(8):H(39)=U(8):RETURN
4100 GOSUB9000
4102 IFL>.1THEN4104
4103 M$=Z$(1):GOSUB4:M(Z)=M(Z)+100:RETURN
4104 IFL>.2THEN4106
4105 M$=Z$(2):GOSUB4:M(Z)=M(Z)+100:RETURN
4106 IFL>.3THEN4108
4107 M$=Z$(3):GOSUB4:M(Z)=M(Z)+20:RETURN
4108 IFL>.4THEN4110
4109 M$=Z$(4):GOSUB4:M(Z)=M(Z)-50:RETURN
4110 IFL>.5THEN4112
4111 M$=Z$(5):GOSUB4:M(Z)=M(Z)+100:RETURN
4112 IFL>.6THEN4114
4113 M$=Z$(6):GOSUB4:M(Z)=M(Z)+200:I(Z)=0:RETURN
4114 IFL>.7THEN4116
4115 M$=Z$(7):GOSUB4:M(Z)=M(Z)-100:RETURN
4116 IFL>.8THEN4118
4117 M$=Z$(8):GOSUB4:M(Z)=M(Z)-50:I(Z)=10:RETURN
4118 IFL>.9THEN4120
4119 M$=Z$(9):GOSUB4:M(Z)=M(Z)-50:RETURN
4120 M$=Z$(10):GOSUB4:M(Z)=M(Z)+200:RETURN
4400 GOSUB9000
4402 IFL>.1THEN4404
4403 M$=C$(1):GOSUB4:M(Z)=M(Z)-15:RETURN
4404 IFL>.2THEN4406
4405 M$=C$(2):GOSUB4:I(Z)=24:GOSUB2000:RETURN
4406 IFL>.3THEN4408
4407 M$=C$(3):GOSUB4:I(Z)=I(Z)-3:GOSUB 2000:RETURN
4408 IFL>.4THEN4410
4409 M$=C$(4):GOSUB4:I(Z)=0:M(Z)=M(Z)+200:RETURN
4410 IFL>.5THEN4412
4411 M$=C$(5):GOSUB4:M(Z)=M(Z)+50:RETURN
4412 IFL>.6THEN4414
4413 M$=C$(6):GOSUB4:I(Z)=5:M(Z)=M(Z)+200:GOSUB2000:RETURN
4414 IFL>.7THEN4416
4415 M$=C$(7):GOSUB4:I(Z)=10:M(Z)=M(Z)-50:RETURN
4416 IFL>.8THEN4418
4417 M$=C$(8):GOSUB4:M(Z)=M(Z)+150:RETURN
4418 IFL>.9THEN4420
4419 M$=C$(9):GOSUB4:I(Z)=39:GOSUB 2000:RETURN
4420 M$=C$(10):GOSUB4:I(Z)=11:GOSUB 2000:RETURN
5001 DATA"MEDITERRANEAN AVENUE","COMMUNITY CHEST","BALTIC AVENUE","INCOME TAX","READING RAILROAD"
5002 DATA"ORIENTAL AVENUE","CHANCE ?","VERMONT AVENUE","CONNECTICUT AVENUE","VISITING IN JAIL"
5003 DATA"ST.CHARLES PLACE","ELECTRIC COMPANY","STATES AVENUE","VIRGINIA AVENUE","PENNSYLVANIA RAILROAD"
5004 DATA"ST.JAMES PLACE","COMMUNITY CHEST","TENNESSEE AVENUE","NEW YORK AVENUE","FREE PARKING"
5005 DATA"KENTUCKY AVENUE","CHANCE ?","INDIANA AVENUE","ILLINOIS AVENUE","B.& O. RAILROAD"
5006 DATA"ATLANTIC AVENUE","VENTNOR AVENUE","WATER WORKS","MARVIN GARDENS","GO TO JAIL"
5007 DATA"PACIFIC AVENUE","NORTH CAROLINA AVENUE","COMMUNITY CHEST","PENNSYLVANIA AVENUE","SHORT LINE"
5008 DATA"CHANCE ?","PARK PLACE","LUXURY TAX","BOARDWALK","GO"
6000 DATA"* MATILDA'S MONOPOLY FOR TWO *"
6010 DATA"EACH PLAYER HAS $1500"
6020 DATA"WHEN YOU BUY HOUSES YOU HAVE TO PUT THE SAME"
6030 DATA"NUMBER ON EACH MEMBER OF A COLOR GROUP"
6040 DATA"WHEN YOU BUY HOUSES INPUT THE NUMBER YOU WANT PER LOT"
6050 DATA"TO ROLL DICE PRESS R KEY"
6060 DATA"IT WILL ONLY COST YOU $"
6070 DATA"IF YOU WISH TO BUY IT TYPE BUY"
6080 DATA"YOU ROLLED DOUBLES**ROLL AGAIN"
6090 DATA"YOU ROLLED 3 SETS OF DOUBLES**GO TO JAIL"
6100 DATA"TYPE QUIT TO QUIT OR BOARD TO SEE BOARD"
6110 DATA"YOU ALREADY OWN THIS PIECE OF PROPERTY"
6120 DATA"IF THERE IS ANY PROPERTY YOU WISH TO SELL AND"
6130 DATA"YOUR OPPONENT WANTS TO BUY TYPE SELL"
6140 DATA"IF YOU NEED A LIST OF PROPERTY TYPE LIST"
6150 DATA"YOU CAN'T SELL THIS**YOU DON'T OWN IT"
6160 DATA"TYPE THE NAME OF THE PROPERTY YOU WANT TO SELL"
6170 DATA"IF THERE IS ANY MORE PROPERTY YOU WISH TO SELL TYPE YES"
6180 DATA"IF YOU WANT TO IMPROVE YOUR PROPERTY TYPE HOUSE"
6190 DATA"YOU DON'T HAVE ENOUGH MONEY FOR THAT MANY HOUSES"
6200 DATA"PAY POOR TAX OF $15"
6210 DATA"ADVANCE TOKEN TO ILLINOIS AVENUE"
6220 DATA"GO BACK THREE SPACES"
6230 DATA"ADVANCE TO GO"
6240 DATA"BANK PAYS YOU DIVIDEND OF $50"
6250 DATA"TAKE A RIDE ON THE READING"
6260 DATA"GO TO JAIL"
6270 DATA"BUILDING AND LOAN MATURES**COLLECT $150"
6280 DATA"ADVANCE TOKEN TO BOARDWALK"
6290 DATA"ADVANCE TOKEN TO ST.CHARLES PLACE"
6300 DATA"XMAS FUND MATURES**COLLECT $100"
6310 DATA"LIFE INSURANCE MATURES**COLLECT $100"
6320 DATA"INCOME TAX REFUND**COLLECT $20"
6330 DATA"DOCTOR'S FEE**PAY $50"
6340 DATA"YOU INHERIT $100"
6350 DATA"ADVANCE TO GO"
6360 DATA"PAY HOSPITAL $100"
6370 DATA"GO TO JAIL"
6380 DATA"PAY SCHOOL TAX OF $50"
6390 DATA"BANK ERROR IN YOUR FAVOUR**COLLECT $200"

6400 CLS
6401 PRINT"INCOME":PRINT"TAX":PRINT"":PRINT"BALT"
6405 PRINT"AVE.":PRINT"":PRINT"COMMUNI":PRINT"CHEST"
6409 PRINT"":PRINT"MED.":PRINT"AVE.":PRINT"";
6413 PRINT"GOBRDLUXPK";
6415 PRINT"WLKTAXPLC$200SRR";:GOSUB7080

7001 PRINT"VIRG":PRINT"AVE.":PRINT"":PRINT"STAT"
7005 PRINT"AVE.":PRINT"":PRINT"ELECT":PRINT"COMP."
7009 PRINT"":PRINT"ST.":PRINT"CHAR":PRINT"ӁӁ";
7013 PRINT"JIN ߅߅UJAIL CONVERMORIE";
7015 PRINT"SNECONTNTALTVISITAVEAVE.AVE.R RR";:GOSUB7080

7021 PRINT"ILLI":PRINT"AVE.":PRINT"":PRINT"INDI"
7025 PRINT"AVE.":PRINT"":PRINT"CHANCE":PRINT"??????"
7029 PRINT"":PRINT"KENT":PRINT"AVE.":PRINT"󁃃󁃃";
7033 PRINT"FREEPARK-NYTENNST.";
7035 PRINT"INGAVEAVEJAMPLCP RR";:GOSUB7080

7041 PRINT"PENN":PRINT"AVE.":PRINT"":PRINT"COMMUNI"
7045 PRINT"CHEST":PRINT"":PRINT"NC":PRINT"AVE."
7049 PRINT"":PRINT"PACI":PRINT"AVE.":PRINT"";
7053 PRINT"GOWAT-TOMARERVENATL";
7055 PRINT"JAILGARCO.AVEAVEB&O R";:POKE16895,82

7080 PRINT@15,"HIT ENTER";:INPUTI$:CLS:RETURN

8000 PRINT@24,"   PRESS ANY KEY TO CONTINUE";
8010 I$=INKEY$:CC=RND(0):IFI$=""THEN8010
8020 FORZZ=1TO28:PRINTCHR$(8);:NEXT:RETURN
9000 PRINT"PRESS ANY KEY TO PICK A CARD";:GOTO8010
30000 C=0:D=0
30002 FORI=1TO39
30004 IFL(I)=1THENC=C+1:A$(C)=G$(I)
30006 IFL(I)=2THEND=D+1:B$(D)=G$(I)
30007 NEXTI:PRINT:PRINT:LN=2
30008 M$=N$(1)+" OWNED THE FOLLOWING PROPERTY:":GOSUB4:LN=LN+1
30009 FORI=1TOC:M$=A$(I):GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
30010 NEXTI
30011 M$=N$(2)+" OWNED THE FOLLOWING PROPERTY:":GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
30012 FORI=1TOD:M$=B$(I):GOSUB4:LN=LN+1:IFLN=12ORLN=24ORLN=36THENGOSUB8000
30013 NEXTI:GOSUB8000
30014 M$="AT THE END OF THE GAME "+N$(1)+" HAD"+STR$(M(1))+" DOLLARS AND "+N$(2)+" HAD"+STR$(M(2))+" DOLLARS":GOSUB4
30020 END
30030 REM DAVID BARKER
30040 REM MC-10 JIM GERRIE
