0 REM COMPILED USING MCBASIC
10 CLEAR500:DIMA(9,9),C(64),A,B,X,Y,A0,A1,A2,A3,A4,A5,A6,A7,A8,B1,B2,B3,B4,B5,B6,B7,B8,H,M,N,P,I,J,K,T,W,Q,S
20 A=0:B=0:X=0:Y=0:S=0:A0=0:T=0:A8=0:A2=0:A3=0:A4=0:B1=0:B6=0:H=0:M=0:N=0:P=0:A5=0:I=1:B7=RND(-(PEEK(9)*256+PEEK(10)))
30 CLS:INPUT"DO YOU WANT INSTRUCTIONS";IN$:IN$=LEFT$(IN$,1):IFIN$="Y"THENGOSUB2220
50 INPUT"YOUR NAME IS";B$:IFB$=""THENB$=" HUMAN ":GOTO60
55 IFLEN(B$)<6THENB$=LEFT$("      ",6-LEN(B$))+B$
57 B$=LEFT$(B$,7)
60 INPUT"LEVEL OF PLAY";B8:IFB8<1THENB8=1:GOTO70
61 IFB8>24THENB8=24
70 B7=(B8+1+RND(0))/2:PRINT"DO YOU WANT WHITE ";B$;:INPUTIN$:GOSUB790:IFLEFT$(IN$,1)<>"N"THENGOSUB900:GOTO190
80 GOSUB890
90 REM COMPUTER MOVE
100 F=-99:A0=0:FORJ=1TO8:FORK=1TO8:IFA(J,K)=99THENA6=J:A7=K:GOTO120
101 NEXT:NEXT
120 FORX=1TO8:FORY=1TO8:IFA(X,Y)<0THENGOSUB270:IFF>=B7 THEN150
130 NEXT:NEXT:IFF>=-9THEN150
140 GOTO2200
150 A(R,U)=A(E,Q):A(E,Q)=1:IFA(R,U)=-2ANDU=1THENA(R,U)=-9
160 X=R:Y=U:A0=4:GOSUB270:PRINT@89,"MY MOVE";:PRINT@121,"   IS  ";:PRINT@155,CHR$(E+64);CHR$(Q+48);CHR$(R+64);CHR$(U+48);
180 GOSUB910:X=E:Y=Q:GOSUB910:IFC=1THENPRINT@185," check ";:C=0:GOTO190
181 PRINT@185,"       ";
185 REM PLAYER MOVE
190 IFX$="S"THENB7=3*RND(0):GOSUB2480:GOTO100
191 GOSUB1590:IFX$="S"THENPRINT@25," SELF- ";:PRINT@57,"PLAYING";:GOSUB1830:GOTO190
192 D=0:B4=A:B9=B:B5=0:IFA(X,Y)=2ANDY=5ANDB=6ANDA(A,B)=1ANDABS(A-X)=1THENB5=1:GOTO220
200 IFX$="K"ORX$="Q"THEN100
210 A0=3:GOSUB250:IFD=0THENPRINT@25,"illegal";:PRINT@57," move  ";:FORJ=1TO1500:NEXT:GOTO190
220 PRINT@345,"   OK  ";:A=B4:B=B9:A(A,B)=A(X,Y):A(X,Y)=1:GOSUB910:X=A:Y=B:GOSUB910
230 IFA(A,B)=2ANDB=8THENGOSUB2100:GOSUB910
240 IFB5=1THENA(A,B-1)=1:X=A:Y=B-1:GOSUB910:GOTO100
241 GOTO100
245 REM COMPUTER PIECE MOVE
250 PRINT@31,"$";:ONA(X,Y)GOTO0,480,0,380,330,0,280,0,330
255 REM KING MOVE
260 FORA=X-1TOX+1:FORB=Y-1TOY+1:IFA(A,B)<>0THENGOSUB650:NEXT:NEXT:RETURN
261 NEXT:NEXT:RETURN
265 REM PLAYER PIECE MOVE
270 ON-A(X,Y)GOTO0,530,0,380,330,0,280,0,330:GOTO260
275 REM ROOK/QUEEN MOVE
280 B=Y:FORA=X+1TO8:GOSUB640:IFS=0THENNEXT
290 FORA=X-1TO1STEP-1:GOSUB640:IFS=0THENNEXT
300 A=X:FORB=Y+1TO8:GOSUB640:IFS=0THENNEXT
310 FORB=Y-1TO1STEP-1:GOSUB640:IFS=0THENNEXT:RETURN
311 RETURN
320 REM BISHOP/QUEEN MOVE
330 B=Y:FORA=X+1TO8:B=B+1:GOSUB640:IFS=0THENNEXT
340 B=Y:FORA=X-1TO1STEP-1:B=B-1:GOSUB640:IFS=0THENNEXT
350 B=Y:FORA=X-1TO1STEP-1:B=B+1:GOSUB640:IFS=0THENNEXT
360 B=Y:FORA=X+1TO8:B=B-1:GOSUB640:IFS=0THENNEXT
370 IFABS(A(X,Y))=9THEN280
371 RETURN
375 REM KNIGHT MOVE
380 A=X+2:B=Y+1:IFA<9ANDB<9THENGOSUB650
390 B=B-2:IFB>0ANDA<9THENGOSUB650
400 A=A-4:IFA>0ANDB>0THENGOSUB650
410 B=B+2:IFB<9ANDA>0THENGOSUB650
420 A=A+1:B=B+1:IFA>0ANDA<9ANDB<9THENGOSUB650
430 B=B-4:IFB>0ANDA>0ANDA<9THENGOSUB650
440 A=A+2:IFA>0ANDA<9ANDB>0THENGOSUB650
450 B=B+4:IFB<9ANDA>0ANDA<9THENGOSUB650:RETURN
451 RETURN
475 REM PLAYER PAWN MOVE
480 A=X:IFY>2THEN500
490 B=Y+1:IFA(A,B)=1THENGOSUB660:B=B+1:IFA(A,B)=1THENGOSUB660:GOTO510
491 GOTO510
500 B=Y+1:IFA(A,B)=1THENGOSUB660
510 A=X+1:B=Y+1:IFA(A,B)<0THENGOSUB660
520 A=A-2:IFA(A,B)<0THENGOSUB660:RETURN
521 RETURN
525 REM COMPUTER PAWN MOVE
530 A=X:IFY<7THEN550
540 B=Y-1:IFA(A,B)=1THENGOSUB660:B=B-1:IFA(A,B)=1THENGOSUB660:GOTO560
541 GOTO560
550 B=Y-1:IFA(A,B)=1THENGOSUB660
560 A=X-1:B=Y-1:IFA(A,B)>1THENGOSUB660
570 A=A+2:IFA(A,B)>1THENGOSUB660:RETURN
571 RETURN
575 REM PROTECT ROUTINE
580 T=A(A,B):IFT=-99THENB1=T:RETURN
590 A5=S:IFABS(T)<=A(X,Y)THENA(A,B)=A(X,Y):A(X,Y)=1:GOTO610
600 IFT<B1 THENB1=T:S=A5:RETURN
601 S=A5:RETURN
610 A1=X:A2=Y:A3=A:A4=B:A8=T:A0=2:FORX=1TO8:FORY=1TO8:IFA(X,Y)<0THENGOSUB270:IFT=0THEN630
620 NEXT:NEXT
630 X=A1:Y=A2:A=A3:B=A4:A0=5:A(X,Y)=A(A,B):A(A,B)=A8:GOTO600
635 REM LEGAL MOVE CHECK
640 S=0:IFA(A,B)=1THEN660
641 IFA(A,B)=0THENS=1:RETURN
642 IFSGN(A(A,B))=SGN(A(X,Y))THENS=1:RETURN
643 S=1:GOTO660
650 IFA(A,B)<>1THENIFSGN(A(A,B))=SGN(A(X,Y))THENRETURN
652 REM SPECIAL CONDITION CHECKS
653 REM A0=1  ADJUST MOVE VALUE IF PLAYER IN CHECK
654 REM A0=2  FROM PROTECT ROUTINE T=0, COMPUTER PIECE PROTECTED
655 REM A0=3  CHECK FOR LEGAL MOVE D=1, ILLEGAL MOVE
656 REM A0=4  C=1, PLAYER IN CHECK
657 REM A0=5  GOTO PROTECT ROUTINE IF UNDER ATTACK
658 REM A0=0  GOTO LOOK-AHEAD ROUTINE
660 PRINT@31," ";:ONA0 GOTO670,680,690,700,710:GOTO720
670 IFA6=A ANDA7=B THENB1=B1+1:RETURN
671 RETURN
680 IFA3=A ANDA4=B THENT=0:RETURN
681 RETURN
690 IFB4=A ANDB9=B THEND=1:RETURN
691 RETURN
700 IFA6=A ANDA7=B THENC=1:RETURN
701 RETURN
710 IFA(A,B)<0THEN580
711 RETURN
715 REM LOOK-AHEAD ROUTINE
720 B3=S:W=X:M=Y:N=A:H=B:P=A(A,B):A(A,B)=A(X,Y):A(X,Y)=1:B1=0
730 A0=5:FORX=1TO8:FORY=1TO8:IFA(X,Y)>1THENGOSUB250
740 NEXT:NEXT:X=N:Y=H:A0=1:GOSUB270:A0=0:S=B3:X=W:Y=M:A=N:B=H:A(X,Y)=A(A,B):A(A,B)=P
745 REM EVALUATE MOVE
750 B6=1/(ABS(4.5-A)+ABS(4.5-B)+1):IFA(X,Y)<-2ANDA(X,Y)>-9THENB6=B6+1/(ABS(A6-A)+ABS(A7-B)+5)+RND(0)/15
760 G=P+B1+B6:IFP=99THENGOSUB1830:PRINT@89,"       ";:PRINT@121," mate! ";:PRINT@153,B$;:PRINT@185,"       ";:GOTO2205
770 IFG<=F THENRETURN
780 F=G:E=X:Q=Y:R=A:U=B:RETURN
785 REM INITIALIZATION ROUTINE
790 CLS:FORY=8TO1STEP-1:FORX=1TO8:READA(X,Y):NEXT:NEXT
800 FORJ=1TO4:SB$=SB$+CHR$(140)+CHR$(140)+CHR$(140)+CHR$(131)+CHR$(131)+CHR$(131)
801 SW$=SW$+CHR$(131)+CHR$(131)+CHR$(131)+CHR$(140)+CHR$(140)+CHR$(140):NEXT
810 FORJ=32TO416STEP128:PRINT@J,SB$;:PRINT@J+64,SW$;:NEXT:M$=" A  B  C  D  E  F  G  H  "
811 FORJ=0TO24:C3=ASC(MID$(M$,J+1,1)):POKE16864+J,C3-(C3AND64):NEXT
812 M$="8 7 6 5 4 3 2 1 ":FORJ=0TO15:C3=ASC(MID$(M$,J+1,1)):POKE16408+J*32,C3-(C3AND64):NEXT
813 RETURN
820 DATA-7,-4,-5,-9,-99,-5,-4,-7
830 DATA-2,-2,-2,-2,-2,-2,-2,-2
840 DATA1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
850 DATA2,2,2,2,2,2,2,2
860 DATA7,4,5,9,99,5,4,7
885 REM PLAYER REQUESTS BLACK
890 I=-I:A(4,1)=99:A(5,1)=9:A(4,8)=-99:A(5,8)=-9
895 REM GRAPHIC DRIVER-FULL SCREEN
900 FORY=1TO8:FORX=1TO8:GOSUB910:NEXT:NEXT:RETURN
905 REM GRAPHIC DRIVER-ONE PIECE
910 L=509+X*3-Y*64:IFX+Y-INT((X+Y)/2)*2=0THEN940
920 IFI*A(X,Y)>0THENONABS(A(X,Y))GOTO960,970,0,980,990,0,1000,0,1010:PRINT@L," K ";:RETURN
921 ONABS(A(X,Y))GOTO960,1040,0,1050,1060,0,1070,0,1080:PRINT@L," k ";:RETURN
940 IFI*A(X,Y)>0THENONABS(A(X,Y))GOTO1100,1110,0,1120,1130,0,1140,0,1150:PRINT@L,CHR$(128);"K";CHR$(128);:RETURN
941 ONABS(A(X,Y))GOTO1100,1180,0,1190,1200,0,1210,0,1220:PRINT@L,CHR$(128);"k";CHR$(128);:RETURN
960 PRINT@L,"   ";:RETURN
970 PRINT@L," P ";:RETURN
980 PRINT@L," N ";:RETURN
990 PRINT@L," B ";:RETURN
1000 PRINT@L," R ";:RETURN
1010 PRINT@L," Q ";:RETURN
1040 PRINT@L," p ";:RETURN
1050 PRINT@L," n ";:RETURN
1060 PRINT@L," b ";:RETURN
1070 PRINT@L," r ";:RETURN
1080 PRINT@L," q ";:RETURN
1100 PRINT@L,CHR$(128);CHR$(128);CHR$(128);:RETURN
1110 PRINT@L,CHR$(128);"P";CHR$(128);:RETURN
1120 PRINT@L,CHR$(128);"N";CHR$(128);:RETURN
1130 PRINT@L,CHR$(128);"B";CHR$(128);:RETURN
1140 PRINT@L,CHR$(128);"R";CHR$(128);:RETURN
1150 PRINT@L,CHR$(128);"Q";CHR$(128);:RETURN
1180 PRINT@L,CHR$(128);"p";CHR$(128);:RETURN
1190 PRINT@L,CHR$(128);"n";CHR$(128);:RETURN
1200 PRINT@L,CHR$(128);"b";CHR$(128);:RETURN
1210 PRINT@L,CHR$(128);"r";CHR$(128);:RETURN
1220 PRINT@L,CHR$(128);"q";CHR$(128);:RETURN
1565 REM INPUT ROUTINE
1570 PRINT@25,"cannot ";:PRINT@57,"castle ";:FORJ=1TO1500:NEXT:GOTO1590
1580 PRINT@25," entry ";:PRINT@57," error ";:FORJ=1TO1500:NEXT
1590 GOSUB1790:GOSUB1830:PRINT@217,"  your ";:PRINT@249,"  move ";:PRINT@281,B$;:PRINT@313,"";
1591 INPUTIN$:X$=LEFT$(IN$,1):IFX$>="A"ANDX$<="H"THEN1730
1630 IFX$="K"THENIFA(5,1)=99ANDA(8,1)=7ANDA(6,1)<2ANDA(7,1)<2THENA(5,1)=1:A(6,1)=7:A(7,1)=99:A(8,1)=1:GOTO1800
1640 IFX$="Q"THENIFA(5,1)=99ANDA(1,1)=7ANDA(4,1)<2ANDA(3,1)<2THENA(5,1)=1:A(4,1)=7:A(3,1)=99:A(1,1)=1:GOTO1800
1650 IFX$="K"THENIFA(4,1)=99ANDA(1,1)=7ANDA(3,1)<2ANDA(2,1)<2THENA(4,1)=1:A(3,1)=7:A(2,1)=99:A(1,1)=1:GOTO1800
1660 IFX$="Q"THENIFA(4,1)=99ANDA(8,1)=7ANDA(5,1)<2ANDA(6,1)<2THENA(4,1)=1:A(5,1)=7:A(6,1)=99:A(8,1)=1:GOTO1800
1665 REM DECODE INPUT
1670 IFX$="X"THENGOSUB1830:PRINT@25,"  EX-  ";:PRINT@57,"CHANGE ";:GOSUB2480:GOSUB1790:GOTO100
1680 IFX$="S"THENRETURN
1690 IFX$="M"THEN1840
1700 IFX$="I"THENGOSUB2220:GOSUB810:GOSUB900:GOTO1590
1710 IFX$="L"THENGOSUB2040:GOTO1590
1720 IFX$="P"THENRUN
1721 GOTO1580
1730 IFLEN(IN$)=4THENX=ASC(X$)-64:Y=ASC(MID$(IN$,2,1))-48:A=ASC(MID$(IN$,3,1))-64:B=ASC(MID$(IN$,4,1))-48:GOTO1740
1731 GOTO1580
1740 IFY<1ORY>8ORA<1ORA>8ORB<1ORB>8THEN1580
1741 IFA(X,Y)<2ORA(A,B)>1THEN1580
1742 GOSUB1790:RETURN
1790 PRINT@25,"       ";:PRINT@57,"       ";:RETURN
1800 PRINT@313,"CASTLE ";:PRINT@345,X$;"-SIDE ";:GOSUB1790:GOSUB900:RETURN
1820 REM BLANK MESSAGE AREA
1830 FORJ=217TO345STEP32:PRINT@J,"       ";:NEXT:RETURN
1834 REM MODIFY ROUTINE
1835 PRINT@25,"modify ";:PRINT@57," error ";:FORJ=1TO1500:NEXT
1840 GOSUB1830:GOSUB1790:PRINT@25,"modify?";:PRINT@57,;:INPUTIN$:M$=LEFT$(IN$,1):IFM$="Z"THENGOSUB1790:GOTO1590
1850 IFLEN(IN$)<>4THEN1835
1851 X=ASC(MID$(IN$,1,1))-64:Y=ASC(MID$(IN$,2,1))-48:IFX<1ORX>8ORY<1ORY>8THEN1835
1890 M$=MID$(IN$,3,1):IFM$="C"ORM$="P"ORM$="E"THEN1900
1891 GOTO1835
1900 V$=MID$(IN$,4,1):IFV$="S"ORV$="P"ORV$="N"ORV$="B"ORV$="R"ORV$="Q"ORV$="K"THEN1905
1901 GOTO1835
1905 IFABS(A(X,Y))=99THEN1835
1910 IFV$="P"THENA(X,Y)=2:GOTO1980
1920 IFV$="N"THENA(X,Y)=4:GOTO1980
1930 IFV$="B"THENA(X,Y)=5:GOTO1980
1940 IFV$="R"THENA(X,Y)=7:GOTO1980
1950 IFV$="Q"THENA(X,Y)=9:GOTO1980
1960 IFV$="K"THENA(X,Y)=99
1961 IFM$+V$="ES"THENA(X,Y)=1:GOTO1990
1962 GOTO1835
1980 IFM$="C"THENA(X,Y)=-A(X,Y)
1990 GOSUB910:IFV$<>"K"THEN1840
1991 FORJ=1TO8:FORK=1TO8:IFA(J,K)=A(X,Y)AND(X<>J ORY<>K)THENA(J,K)=1:X=J:Y=K:GOSUB910:GOTO1840
1992 NEXT:NEXT:GOTO1840
2035 REM CHANGE LEVEL OF PLAY
2040 GOSUB1830:PRINT@217," LEVEL ";:PRINT@251,B8;:PRINT@281,"change ";:PRINT@313,"levels ";:PRINT@345,;:INPUTIN$:IFLEN(IN$)=0THEN2040
2041 IFLEFT$(IN$,1)="N"THENRETURN
2042 IFLEFT$(IN$,1)="Y"THEN2040
2043 B8=VAL(IN$):IFB8<1ORB8>24THEN2040
2044 B7=B8/2:RETURN
2090 REM PROMOTE PLAYER PAWN
2100 GOSUB1830:PRINT@217,"  what ";:PRINT@249," piece ";:PRINT@281," PNBRQ ";:
2110 PRINT@313,;:INPUTIN$
2120 IFIN$="P"THENA(A,B)=2:RETURN
2130 IFIN$="N"THENA(A,B)=4:RETURN
2140 IFIN$="B"THENA(A,B)=5:RETURN
2150 IFIN$="R"THENA(A,B)=7:RETURN
2160 IFIN$="Q"THENA(A,B)=9:RETURN
2161 GOTO2110
2190 REM END OF GAME
2200 GOSUB1830:PRINT@89,"       ";:PRINT@121,"  YOU  ";:PRINT@153,"  WIN! ";:PRINT@185,B$;
2205 PRINT@377,"HIT 'P'";:PRINT@409,"TO PLAY";:PRINT@441,"OR 'B' ";:PRINT@473,"  FOR  ";:PRINT@505," BASIC";:
2210 A$=INKEY$:IFA$="B"THENCLS:END
2211 IFA$="P"THENRUN
2212 GOTO2210
2215 REM INSTRUCTIONS
2220 GOSUB2330:PRINT"***COMMANDS DURING PLAYER'S MOVE";:PRINT"  K - TO CASTLE KING SIDE"
2221 PRINT"  Q - TO CASTLE QUEEN SIDE":PRINT"  X - TO EXCHANGE BLACK/WHITE":PRINT"  S - TO LET COMPUTER SELF-PLAY"
2230 PRINT"  M - TO modify THE BOARD, ENTER";:PRINT"      THE SQUARE FOLLOWED BY:"
2231 PRINT"      -C, P OR E FOR COMPUTER,":PRINT"       PLAYER OR EMPTY":PRINT"      -S, P, N, B, Q OR K FOR":PRINT"       SQUARE, PAWN, ETC.":
2240 PRINT"      (E.G.'D4PP', 'A8ES', ECT.)";:PRINT"  Z - TO ESCAPE modify":GOSUB2310
2250 GOSUB2330:PRINT"***COMMANDS DURING PLAYER'S MOVE";:PRINT"   I - TO GET INSTRUCTIONS"
2251 PRINT"   L - TO CHANGE LEVEL OF PLAY":PRINT"   P - TO START NEW GAME":PRINT
2260 PRINT"***TO PROMOTE TO P, N, B, R OR Q";:PRINT"   ENTER LETTER WHEN PROMPTED":PRINT
2261 PRINT"***TO MOVE, CAPTURE OR CAPTURE":PRINT"   'EN PASSANT', SPECIFY FROM/TO";:PRINT"   SQUARES(E.G. 'B1C3')"
2270 PRINT:PRINT"***LEVELS OF PLAY ARE 1 THRU 24":GOSUB2310
2275 CLS:PRINT"        ***C.4 CHESS***":PRINT:PRINT"***COMPUTER ASSUMES MATE IF KING";:PRINT"   IS LEFT IN check"
2276 PRINT:PRINT"***IF entry error OR illegal"
2277 PRINT"   move APPEAR, ENTER CORRECTED":PRINT"   MOVE.  A MOVE MAY BE STARTED"
2280 PRINT"   OVER BY FORCING AN ERROR"
2290 PRINT:PRINT"***YOU HAVE A CHOICE OF BLACK":PRINT"   OR WHITE.  YOU ARE ALWAYS"
2291 PRINT"   AT THE BOTTOM OF THE SCREEN":PRINT:GOSUB2310:RETURN
2300 IFINKEY$=""THEN2300
2301 RETURN
2310 PRINT"HIT ANY KEY TO CONTINUE";:
2320 IFINKEY$=""THEN2320
2321 CLS:RETURN
2330 CLS:PRINT"        ***C.4 CHESS***":PRINT:RETURN
2470 REM EXCHANGE COMPUTER/PLAYER  PIECES
2480 I=-I:FORJ=1TO4:FORK=1TO8:L=A(J,K):IFA(9-J,9-K)=1THENA(J,K)=1:GOTO2490
2481 A(J,K)=-A(9-J,9-K)
2490 IFL=1THENA(9-J,9-K)=1:GOTO2500
2491 A(9-J,9-K)=-L
2500 NEXT:NEXT:GOSUB900:RETURN
