0 CLS:CLEAR900:DIMC1,C2,P(37),O(37),C(1),M,D1,D2,S:M=16384:C(1)=-48:GOSUB9900:GOTO1020

1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN

10 PRINT@P(S),MID$(T1$,2*D1+1,2);:PRINT@P(S)+32,MID$(T2$,2*D1+1,2);
20 PRINT@P(S)+O(S),MID$(T1$,2*D2+1,2);:PRINT@P(S)+32+O(S),MID$(T2$,2*D2+1,2);
30 C1=P(S)+M:C2=C(SAND1):POKEC1,PEEK(C1)+C2:POKEC1+1,PEEK(C1+1)+C2:POKEC1+32,PEEK(C1+32)+C2:POKEC1+33,PEEK(C1+33)+C2
32 C1=C1+O(S):POKEC1,PEEK(C1)+C2:POKEC1+1,PEEK(C1+1)+C2:POKEC1+32,PEEK(C1+32)+C2:POKEC1+33,PEEK(C1+33)+C2:RETURN

90 F$=F$+RIGHT$(STR$(X),1)+RIGHT$(STR$(Y),1):RETURN

1020 DIMD(6,6)
1030 REM MACHINE CODE LOADING AND DATA
1070 REM INSTRUCTIONS
1080 CLS:DE=INT(100*RND(0)):REM RANDOMIZE?
1090 PRINT@5*32+12,"DOMINOES":PRINT@7*32+9,"BY D.N. SMITH.";
1100 PRINT@15*32+2,"DO YOU REQUIRE INSTRUCTIONS?";
1110 GOSUB2390:IFA$="N"THEN1350
1120 CLS:PRINT"instructions:":PRINT
1130 M$="THIS IS PLAYED LIKE ORDINARY DOMINOES EXCEPT THAT POINTS MAY BE SCORED.":GOSUB1
1140 M$="YOU PLAY THE COMPUTER, AND WIN IF YOU GET EXACTLY 72 POINTS BEFORE THE COMPUTER DOES.":GOSUB1
1150 M$="TO SCORE POINTS THE END DOMINOES MUST TOTAL A MULTIPLE OF 3 OR 5.":GOSUB1
1160 M$="EACH MULTIPLE SCORES 1 POINT.":GOSUB1
1170 M$="PRESS ANY KEY AND I WILL SHOW YOU SOME EXAMPLES OF SCORING POINTS.":GOSUB1:GOSUB2390
1230 SOUND1,1:CLS
1240 L=INT(7*RND(0)):R=INT(7*RND(0))
1250 F$=""
1260 X=L:Y=6:GOSUB90:X=6:Y=2:GOSUB90
1270 X=2:Y=5:GOSUB90:X=5:Y=R:GOSUB90:GOSUB3070:IFP=0THEN1240
1275 E$=F$:GOSUB2470
1280 PRINT@32*3,;:M$="REMEMBER THAT ANY DOUBLES COUNT FOR DOUBLE, THE ENDS TOTAL FOR ABOVE="+STR$(T)+".":GOSUB1
1300 M$=MID$(STR$(T),2)+" MAKES"+STR$(M3)+" MULTIPLES OF 3 AND"+STR$(M5)+" MULTIPLES OF 5":GOSUB1
1320 M$="TOTAL POINTS SCORED="+STR$(M3)+" +"+STR$(M5)+" ="+STR$(M3+M5):GOSUB1
1330 M$="FOR A FURTHER EXAMPLE PRESS a KEY.":GOSUB1
1340 M$="TO PLAY THE GAME PRESS p KEY.":GOSUB1:GOSUB2390:T=0:X=0:Y=0:F$="":L=0:R=0:M3=0:M5=0:IFA$<>"P"THEN1230

1350 REM-SETTING OUT SCREEN
1360 CLS
1370 PRINT@32*2+4,"écomputerådominoesågameç";:PRINT@32*3+4,"ãÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉá";
1380 PRINT@32*6+8,"éstateåofåplayååéscoresç";
1400 PRINT@32*7+8,"äèèèèèèèèèèèèèèääYOU "RIGHT$(STR$(PS),2)"Ö";
1410 PRINT@32*8+8,"äDEALING...èèèèääMEè "RIGHT$(STR$(CS),2)"Ö";
1420 PRINT@32*9+8,"äèèèèèèèèèèèèèèääWIN=72Ö";
1440 PRINT@32*10+8,"ãÉÉÉÉÉÉÉÉÉÉÉÉÉÉÉãÉÉÉÉÉÉá";
1490 REM RENEW DOMINOES
1520 REM OWNERSHIP OF DOMINOES
1530 E$="":CD=9:C=0
1540 FORI=0TO6
1550 FORJ=1TO6:D(I,J)=0:NEXTJ
1560 NEXTI
1570 REM DEAL
1580 FORI=1TO18
1590 Y=INT(7*RND(0)):X=INT((Y+1)*RND(0)):IFD(X,Y)<>0THEN1590
1600 IFINT(I/2)=I/2THEND(X,Y)=1:GOTO1620
1610 D(X,Y)=2
1620 NEXTI
1630 REM PRINT DOMINOES
1640 GOSUB4200
1650 REM WHOSE TURN
1660 DE=DE+1:IFINT(DE/2)=DE/2THEN3190
1670 REM PLAYER
1680 REM 1ST DECISION
1690 IFE$=""THEN1930
1700 M1$="YOUR CHOICES"
1710 M2$="1=LEFT 2=RIGHT"
1720 M3$="3=CAN'T Q=QUIT"
1730 GOSUB2430:GOSUB2390:C$=A$
1740 IFC$="Q"THEN4150
1750 C=VAL(C$):IFC<1ORC>3THEN1700
1760 IFC<>3THEN1930
1770 REM CAN PLAYER GO?
1780 M1$="OK.":M2$="":M3$="":GOSUB2430
1790 L=VAL(LEFT$(E$,1)):R=VAL(RIGHT$(E$,1))
1800 FORI=0TO6
1810 FORJ=ITO6
1820 IFD(I,J)<>1THEN1850
1830 IFI=LORJ=LTHEN1920
1840 IFI=RORJ=RTHEN1920
1850 NEXTJ:NEXTI
1860 FORI=0TO6
1870 IFD(I,L)=0THEND(I,L)=3
1880 IFD(L,I)=0THEND(L,I)=3
1890 IFD(I,R)=0THEND(I,R)=3
1900 IFD(R,I)=0THEND(R,I)=3
1910 NEXT:GOTO3170
1920 M1$="NO CHEATING!":M2$="YOU CAN PLAY!":M3$="":GOSUB2430:GOTO2130
1930 REM PLAYER'S DOMINO
1940 M1$="CHOOSE DOMINO":M2$="?":M3$="":GOSUB2430
1950 GOSUB2390:X$=A$
1960 IFX$="0"THEN1980
1970 IFVAL(X$)<1ORVAL(X$)>6THEN1950
1980 M2$="? "+X$:GOSUB2450
1990 GOSUB2410:Y$=A$:IFY$="0"THEN2010
2000 IFVAL(Y$)<1ORVAL(Y$)>6THEN1990
2010 M2$="? "+X$+" "+Y$:GOSUB2450
2020 IFX$>Y$THENA$=X$:X$=Y$:Y$=A$
2030 X=VAL(X$):Y=VAL(Y$)
2040 IFD(X,Y)=1THEN2060
2050 M1$="NOT YOURS.":M2$="TRY AGAIN!":M3$="":GOSUB2430:GOTO2130
2060 M1$="OK.":M2$="":M3$="":GOSUB2430:FORC1=1TO1500:NEXT
2070 IFE$=""THENE$=X$+Y$:GOTO2210
2080 REM FIT PLAYER'S DOMINO
2090 IFC=2THEN2160
2100 REM FIT LEFT
2110 IFX$=LEFT$(E$,1)ORY$=LEFT$(E$,1)THEN2140
2120 M1$="DOESN'T FIT,":M2$="TRY AGAIN!":M3$="":GOSUB2430
2130 FORI=1TO2500:NEXT:GOTO1670
2140 IFX$=LEFT$(E$,1)THENE$=Y$+X$+E$:GOTO2210
2150 E$=X$+Y$+E$:GOTO2210
2160 REM FIT RIGHT
2170 IFX$=RIGHT$(E$,1)ORY$=RIGHT$(E$,1)THEN2190
2180 GOTO2120
2190 IFX$=RIGHT$(E$,1)THENE$=E$+X$+Y$:GOTO2210
2200 E$=E$+Y$+X$:GOTO2210
2210 REM DISPLAY PLAYER'S DOMINO
2220 GOSUB2470
2230 REM SCORE FOR PLAYER, PS
2240 GOSUB3050
2250 IFP+PS>72THEN2280
2260 IFP+PS=72ANDLEN(E$)=2THEN2280
2270 PS=PS+P
2280 PRINT@32*7+29,RIGHT$(STR$(PS),2);
2290 REM CHECK FOR WIN
2300 IFPS<>72THEN2360
2310 M1$="HUMAN WINS,":M2$=""
2320 IFCS>=65THENM2$"CLOSE GAME!"
2330 IFCS<65THENM2$"YOU'RE GOOD!"
2340 M3$="PRESS A KEY."
2350 GOSUB2430:GOSUB2390:RUN
2360 REM USING UP THE PLAYER'S DOMINO
2370 D(X,Y)=3:D(Y,X)=3:GOSUB4200
2380 GOTO3170

2390 REM KEY INPUT
2410 A$=INKEY$:IFA$=""THEN2410
2420 RETURN

2430 REM MESSAGES
2440 PRINT@32*7+9,LEFT$(M1$+"              ",14);
2450 PRINT@32*8+9,LEFT$(M2$+"              ",14);
2460 PRINT@32*9+9,LEFT$(M3$+"              ",14);:RETURN

2470 PRINT@0,"":PRINT@1*32,"":PRINT@2*32+28,"":PRINT@3*32+28,"":PRINT@4*32,""
2480 PRINT@5*32,"":FORC1=6TO10:PRINT@C1*32,"        ";:NEXT
2490 S=0
2510 FORCP=1TOLEN(E$)STEP2:S=S+1
2520 D1=VAL(MID$(E$,CP,1)):D2=VAL(MID$(E$,CP+1,1)):GOSUB10
2530 NEXTCP:RETURN

2830 REM RIGHT SIDE DOMINO
2840 D1=X:D2=Y:GOSUB10:RETURN

3050 REM-SCORE SUBROUTINE
3060 F$=E$
3070 M3=0:M5=0
3080 A=VAL(LEFT$(F$,1))
3090 B=VAL(RIGHT$(F$,1))
3100 IFLEN(F$)=2THEN3130
3110 IFRIGHT$(F$,1)=MID$(F$,LEN(F$)-1,1)THENB=2*B
3120 IFLEFT$(F$,1)=MID$(F$,2,1)THENA=2*A
3130 T=A+B
3140 IFT/3=INT(T/3)THENM3=T/3
3150 IFT/5=INT(T/5)THENM5=T/5
3160 P=M3+M5:RETURN

3170 REM COMPUTER'S TURN
3180 M1$="COMPUTING":M2$="REPLY...":M3$="":GOSUB2430:IFE$<>""THEN3310
3190 REM SELECT 1ST
3200 FORI=0TO6:FORJ=ITO6
3210 IFD(I,J)=2THENX=I:Y=J:GOTO3230
3220 NEXTJ:NEXTI
3230 IFD(0,6)=2THENX=0:Y=6
3240 IFD(3,3)=2THENX=3:Y=3
3250 IFD(4,6)=2THENX=4:Y=6
3260 IFD(1,5)=2THENX=1:Y=5
3270 IFD(2,4)=2THENX=2:Y=4
3280 IFD(4,5)=2THENX=4:Y=5
3290 IFD(6,6)=2ANDD(3,6)=2THENX=6:Y=6
3300 C=1:GOTO3930

3310 REM COMPUTER'S STRATEGY
3320 REM NULLIFY PREVIOUS CHOICES
3330 CO=0:MA=0:F$=E$:L=VAL(LEFT$(E$,1)):R=VAL(RIGHT$(E$,1)):PM=50
3340 E1=7:E2=7:E3=7:E4=7:E5=7:E6=7:E7=7:E8=7:E9=7
3350 REM TEST EACH COMPUTER DOMINO FOR FIT TO CHAIN
3360 FORI=6TO0STEP-1
3370 FORJ=6TOISTEP-1
3380 IFD(I,J)<>2THEN3490
3390 IFI<>LTHENIFJ<>LTHENIFI<>RTHENIFJ<>RTHEN3490
3400 CO=CO+1:J$=RIGHT$(STR$(J),1):I$=RIGHT$(STR$(I),1)
3410 IFI=LTHENC=1:F$=J$+I$+F$:GOTO3440
3420 IFJ=LTHENC=1:F$=I$+J$+F$:GOTO3440
3430 GOTO3450
3440 GOSUB3580
3450 IFI=RTHENC=2:F$=F$+I$+J$:GOTO3480
3460 IFJ=RTHENC=2:F$=F$+J$+I$:GOTO3480
3470 GOTO3490
3480 GOSUB3580
3490 NEXTJ:NEXTI
3500 REM SELECT SUITABLE DOMINO
3510 IFCO<>0THEN3550
3520 M1$="COMPUTER":M2$="CAN'T GO.":M3$="":GOSUB2430:FORI=1TO2500:NEXT
3530 IFC=3THENM1$="NO PLAY":M2$="POSSIBLE.":GOTO3980
3540 GOTO1670
3550 IFE4=7THEN3570
3560 IFPS=64ORPS=66ORPS>67ORMA=0THENC=E7:X=E8:Y=E9:GOTO3930
3570 C=E1:X=E2:Y=E3:GOTO3930
3580 REM CALCULATING SUBROUTINE
3590 GOSUB3070
3600 IFP+CS=72THENX=I:Y=J:GOTO3930
3610 IFP+CS>72THENP=0
3620 IFP<MATHEN3640
3630 MA=P:E1=C:E2=I:E3=J
3640 IFPS=64ORPS=66ORPS>67ORMA=0THEN3660
3650 GOTO3810
3660 G$=F$:PQ=0
3670 FORK=0TO6
3680 FORZ=KTO6
3690 IFD(K,Z)=3ORD(K,Z)=2THEN3790
3700 K$=RIGHT$(STR$(K),1):Z$=RIGHT$(STR$(Z),1)
3710 IFK$=LEFT$(G$,1)THENG$=Z$+K$+G$:GOTO3740
3720 IFZ$=LEFT$(G$,1)THENG$=K$+Z$+G$:GOTO3740
3730 GOTO3750
3740 GOSUB3820:IFP>=PQTHENPQ=P:E4=C:E5=I:E6=J
3750 IFK$=RIGHT$(G$,1)THENG$=G$+K$+Z$
3760 IFZ$=RIGHT$(G$,1)THENG$=G$+Z$+K$
3770 GOTO3790
3780 GOSUB3820:IFP>=PQTHENPQ=P:E4=C:E5=I:E6=J
3790 NEXTZ:NEXTK
3800 IFPQ<=PMTHENE7=E4:E8=E5:E9=E6:PM=PQ
3810 F$=E$:RETURN
3820 REM-SCORE G$
3830 M3=0:M5=0
3840 A=VAL(LEFT$(G$,1))
3850 B=VAL(RIGHT$(G$,1))
3860 IFLEN(G$)=2THEN3890
3870 IFRIGHT$(G$,1)=MID$(G$,LEN(G$)-1,1)THENB=2*B
3880 IFLEFT$(G$,1)=MID$(G$,2,1)THENA=2*A
3890 T=A+B:IFT/3=INT(T/3)THENM3=T/3
3900 IFT/5=INT(T/5)THENM5=T/5
3910 P=M3+M5:IFPS+P>72THENP=0
3920 G$=F$:RETURN
3930 REM-COMPUTER PLAYING ITS CHOICE
3940 M1$="COMPUTER":M2$="PLAYING"+STR$(X)+STR$(Y):M3$="":GOSUB2430:FORC1=1TO1500:NEXT
3950 D(X,Y)=3:D(Y,X)=3:CD=CD-1
3960 IFCD<>0THEN3990
3970 M1$="OUT OF":M2$="DOMINOES"
3980 M3$="DEAL AGAIN.":GOSUB2430:FORI=1TO2500:NEXT:GOTO1350
3990 X$=RIGHT$(STR$(X),1)
4000 Y$=RIGHT$(STR$(Y),1)
4010 IFC=2THEN4040
4020 IFX$=LEFT$(E$,1)THENE$=Y$+X$+E$:GOTO4060
4030 E$=X$+Y$+E$:GOTO4060
4040 IFX$=RIGHT$(E$,1)THENE$=E$+X$+Y$:GOTO4060
4050 E$=E$+Y$+X$
4060 GOSUB2470
4070 REM SCORE FOR COMPUTER
4080 GOSUB3050
4090 IFCS+P>72THEN4190
4100 IFP+CS=72ANDLEN(E$)=2THEN4120
4110 CS=CS+P
4120 PRINT@32*8+29,RIGHT$(STR$(CS),2);
4130 REM CHECK FOR WIN
4140 IFCS<>72THEN4190
4150 M1$="COMPUTER WINS!":M2$="":M$=""
4160 IFPS<66THENM2$="MORE PRACTICE!"
4170 IFPS>65THENM2$="GOOD GAME!"
4180 GOTO2340
4190 D(X,Y)=3:D(Y,X)=3:GOTO1670

4200 REM PRINT DOMINOES
4210 CP=0:PRINT@32*11,;
4220 FORI=1TO4:PRINT"":NEXT
4230 FORI=0TO6:FORJ=ITO6:IFD(I,J)=1THENCP=CP+1:X=I:Y=J:S=28+CP:IFCP<10THENGOSUB2840
4260 NEXTJ:NEXTI
4270 RETURN

9900 T1$="œœœœŒœŒœŒŒŒŒŒÃ":T2$="œœœ«œŒœ∆ŒŒŒ∆ŒÃ"
9911 P(1)=0:P(2)=4:P(3)=8:P(4)=12:P(5)=16:P(6)=20:P(7)=24:P(8)=28
9912 P(9)=94:P(10)=156:P(11)=152:P(12)=148:P(13)=144:P(14)=140:P(15)=136:P(16)=132:P(17)=194
9913 P(18)=260:P(19)=264:P(20)=268:P(21)=272:P(22)=276:P(23)=280:P(24)=284:P(25)=350:P(26)=412
9914 P(27)=408:P(28)=404
9915 FORC1=0TO8:P(29+C1)=11*32+C1*3:O(29+C1)=64:NEXT

9921 O(1)=2:O(2)=2:O(3)=2:O(4)=2:O(5)=2:O(6)=2:O(7)=2:O(8)=2
9922 O(9)=64:O(10)=-2:O(11)=-2:O(12)=-2:O(13)=-2:O(14)=-2:O(15)=-2:O(16)=-2:O(17)=64
9923 O(18)=2:O(19)=2:O(20)=2:O(21)=2:O(22)=2:O(23)=2:O(24)=2:O(25)=64:O(26)=-2
9924 O(27)=-2:O(28)=-2:RETURN
9930 REM MC-10 EDITS JIM GERRIE
