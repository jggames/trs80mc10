PROCEDURE chess
SHELL "display 1b24 1b20 1 0 0 28 18 0 2 2 1b21 </1"
BASE 0
DIM Z(10,10):INTEGER
DIM B6,B7,F,G:REAL
DIM IN$,X$,X1$,X2$,X3$,B$,C$,N$,P$,R$,S$:STRING[3]
DIM Q$:STRING[10]; EV$,OD$,BL$:STRING[50]
DIM A,B,C,D,E,H,I,J,K,M,N,P,Q,R,S,T,U,V,W,X,Y:INTEGER
DIM A0,A1,A2,A3,A4,A5,A6,A7,A8,A9,B1,B2,B3,B4,B5,B9:INTEGER

70 GOSUB 1490

PRINT C$; R$; "***** GRAND MASTER 0.9 *****"; N$
PRINT 
PRINT "(C) 1992 by Matthew Thompson"
PRINT 

PRINT "Do you need help (y/n)? "; 
90 GOSUB 1500
IF IN$<>"Y" AND IN$<>"N" THEN 90
PRINT 
IF IN$="Y" THEN 
PRINT 
GOSUB 1470
ENDIF 

PRINT 
PRINT "EASY <---------> HARD"
PRINT " 1  2  3  4  5  6  7"
PRINT 
PRINT "Difficulty level (1-7)? "; 
100 GOSUB 1500
IF IN$<"1" OR IN$>"7" THEN 100
I=VAL(IN$)-1
B7=FLOAT(I*2)
IF B7=.0 THEN 
B7=1.
ENDIF 

PRINT 
PRINT 
PRINT "Do you want white (y/n)? "; 
110 GOSUB 1500
IF IN$<>"Y" AND IN$<>"N" THEN 110
IF IN$="N" THEN 
Z(4,1)=99
Z(5,1)=9
Z(4,8)=-99
Z(5,8)=-9
V=-(V)
ENDIF 
GOSUB 880
IF IN$="Y" THEN 260

210 PRINT P$; " 2"; BL$
F=-99.
A0=0
J=1
K=1
215 IF Z(J,K)=99 THEN 
A6=J
A7=K
GOTO 220
ENDIF 
K=K+1
IF K=9 THEN 
K=1
J=J+1
ENDIF 
IF J<9 THEN 215

220 X=1
Y=1
225 IF Z(X,Y)<0 THEN 
PRINT P$; CHR$(32+X*3); CHR$(49-Y*2); 
GOSUB 320
IF F>=B7 THEN 240
ENDIF 
230 Y=Y+1
IF Y=9 THEN 
Y=1
X=X+1
ENDIF 
IF X<9 THEN 225
IF F<-9. THEN 1440

240 Z(R,U)=Z(E,Q)
Z(E,Q)=1
IF Z(R,U)=-2 AND U=1 THEN 
Z(R,U)=-9
ENDIF 

250 A0=4
X=R
Y=U
GOSUB 320
PRINT B$; P$; " 2My move is "; 
PRINT CHR$(E+64); CHR$(Q+48); CHR$(R+64); CHR$(U+48)
PRINT P$; " 4"; BL$; 
GOSUB 890
X=E
Y=Q
GOSUB 890
IF C=1 THEN 
PRINT B$; R$; P$; "02** CHECK **"; N$
C=0
ENDIF 

260 GOSUB 1160
D=0
B4=A
B9=B
B5=0
IF X$="K" OR X$="Q" THEN 210

270 A0=3
GOSUB 300
IF D=0 THEN 
PRINT B$; R$; P$; " 4ILLEGAL MOVE"; N$
GOTO 260
ENDIF 

280 A=B4
B=B9
Z(A,B)=Z(X,Y)
Z(X,Y)=1
GOSUB 890
X=A
Y=B
GOSUB 890
IF Z(A,B)=2 AND B=8 THEN 
GOSUB 1360
GOSUB 890
ENDIF 

290 IF B5=1 THEN 
Z(A,B-1)=1
X=A
Y=B-1
GOSUB 890
ENDIF 

GOTO 210

300 ON Z(X,Y) GOTO 70,500,70,420,370,70,330,70,370

310 FOR A=X-1 TO X+1
FOR B=Y-1 TO Y+1
IF Z(A,B)<>0 THEN 
GOSUB 670
ENDIF 
NEXT B
NEXT A
RETURN 

320 ON -(Z(X,Y)) GOTO 70,550,70,420,370,70,330,70,370
GOTO 310

330 B=Y
A=X+1
335 GOSUB 660
IF S=0 THEN 
A=A+1
IF A<9 THEN 335
ENDIF 
340 A=X-1
345 GOSUB 660
IF S=0 THEN 
A=A-1
IF A>0 THEN 345
ENDIF 
350 A=X
B=Y+1
355 GOSUB 660
IF S=0 THEN 
B=B+1
IF B<9 THEN 355
ENDIF 
360 B=Y-1
365 GOSUB 660
IF S=0 THEN 
B=B-1
IF B>0 THEN 365
ENDIF 
RETURN 

370 B=Y
A=X+1
375 B=B+1
GOSUB 660
IF S=0 THEN 
A=A+1
IF A<9 THEN 375
ENDIF 
380 B=Y
A=X-1
385 B=B-1
GOSUB 660
IF S=0 THEN 
A=A-1
IF A>0 THEN 385
ENDIF 
390 B=Y
A=X-1
395 B=B+1
GOSUB 660
IF S=0 THEN 
A=A-1
IF A>0 THEN 395
ENDIF 
400 B=Y
A=X+1
405 B=B-1
GOSUB 660
IF S=0 THEN 
A=A+1
IF A<9 THEN 405
ENDIF 
410 IF ABS(Z(X,Y))=9 THEN 330
RETURN 

420 A=X+2
B=Y+1
IF A<9 AND B<9 THEN 
GOSUB 670
ENDIF 
430 B=B-2
IF B>0 AND A<9 THEN 
GOSUB 670
ENDIF 
440 A=A-4
IF A>0 AND B>0 THEN 
GOSUB 670
ENDIF 
450 B=B+2
IF B<9 AND A>0 THEN 
GOSUB 670
ENDIF 
460 A=A+1
B=B+1
IF A>0 AND A<9 AND B<9 THEN 
GOSUB 670
ENDIF 
470 B=B-4
IF B>0 AND A>0 AND A<9 THEN 
GOSUB 670
ENDIF 
480 A=A+2
IF A>0 AND A<9 AND B>0 THEN 
GOSUB 670
ENDIF 
490 B=B+4
IF B<9 AND A>0 AND A<9 THEN 
GOSUB 670
ENDIF 
RETURN 

500 A=X
IF Y>2 THEN 520
510 B=Y+1
IF Z(A,B)=1 THEN 
GOSUB 680
B=B+1
IF Z(A,B)=1 THEN 
GOSUB 680
ENDIF 
ENDIF 
GOTO 530
520 B=Y+1
IF Z(A,B)=1 THEN 
GOSUB 680
ENDIF 
530 A=X+1
B=Y+1
IF Z(A,B)<0 THEN 
GOSUB 680
ENDIF 
540 A=A-2
IF Z(A,B)<0 THEN 
GOSUB 680
ENDIF 
RETURN 

550 A=X
IF Y<7 THEN 570
560 B=Y-1
IF Z(A,B)=1 THEN 
GOSUB 680
B=B-1
IF Z(A,B)=1 THEN 
GOSUB 680
ENDIF 
ENDIF 
GOTO 580
570 B=Y-1
IF Z(A,B)=1 THEN 
GOSUB 680
ENDIF 
580 A=X-1
B=Y-1
IF Z(A,B)>1 THEN 
GOSUB 680
ENDIF 
590 A=A+2
IF Z(A,B)>1 THEN 
GOSUB 680
ENDIF 
RETURN 

600 T=Z(A,B)
IF T=-99 THEN 
B1=T
RETURN 
ENDIF 
IF ABS(T)<=Z(X,Y) THEN 
Z(A,B)=Z(X,Y)
Z(X,Y)=1
GOTO 630
ENDIF 
620 IF T<B1 THEN 
B1=T
ENDIF 
S=A5
RETURN 
630 A1=X
A2=Y
A3=A
A4=B
A8=T
A0=2
X=1
Y=1
635 IF Z(X,Y)<0 THEN 
GOSUB 320
IF T=0 THEN 650
ENDIF 
Y=Y+1
IF Y=9 THEN 
Y=1
X=X+1
ENDIF 
IF X<9 THEN 635
650 X=A1
Y=A2
A=A3
B=A4
A0=5
Z(X,Y)=Z(A,B)
Z(A,B)=A8
GOTO 620
660 S=0
IF Z(A,B)=1 THEN 680
S=1
IF Z(A,B)=0 THEN 
RETURN 
ENDIF 
IF SGN(Z(A,B))=SGN(Z(X,Y)) THEN 
RETURN 
ENDIF 
GOTO 680
670 IF Z(A,B)<>1 THEN 
IF SGN(Z(A,B))=SGN(Z(X,Y)) THEN 
RETURN 
ENDIF 
ENDIF 
680 ON A0 GOTO 690,700,710,720,730
GOTO 740
690 IF A6=A AND A7=B THEN 
B1=B1+1
ENDIF 
RETURN 
700 IF A3=A AND A4=B THEN 
T=0
ENDIF 
RETURN 
710 IF B4=A AND B9=B THEN 
D=1
ENDIF 
RETURN 
720 IF A6=A AND A7=B THEN 
C=1
ENDIF 
RETURN 
730 IF Z(A,B)<0 THEN 600
RETURN 
740 B3=S
W=X
M=Y
N=A
H=B
P=Z(A,B)
Z(A,B)=Z(X,Y)
Z(X,Y)=1
B1=0
A0=5
FOR X=1 TO 8
FOR Y=1 TO 8
IF Z(X,Y)>1 THEN 
GOSUB 300
ENDIF 
NEXT Y
NEXT X
750 X=N
Y=H
A0=1
GOSUB 320
A0=0
S=B3
X=W
Y=M
A=N
B=H
Z(X,Y)=Z(A,B)
Z(A,B)=P
760 B6=1./(ABS(4.5-FLOAT(A))+ABS(4.5-FLOAT(B))+1.)
IF Z(X,Y)<-2 AND Z(X,Y)>-9 THEN 
B6=B6+1./(ABS(FLOAT(A6-A))+ABS(FLOAT(A7-B))+5.)+RND(0)/15.
ENDIF 
770 G=FLOAT(P)+FLOAT(B1)+B6
IF P=99 THEN 
PRINT B$; B$; B$; R$; P$; " 2** CHECKMATE **"
PRINT "** YOU LOSE! **"; N$
GOTO 2000
ENDIF 
780 IF G<=F THEN 
RETURN 
ENDIF 
790 F=G
E=X
Q=Y
R=A
U=B
RETURN 

880 PRINT C$; 
FOR I=8 TO 2 STEP -2
PRINT STR$(I); " "; EV$; N$
PRINT "  "; EV$; N$
PRINT STR$(I-1); " "; OD$; N$
PRINT "  "; OD$; N$
NEXT I
PRINT "   A  B  C  D  E  F  G  H"
FOR Y=8 TO 1 STEP -1
FOR X=1 TO 8
GOSUB 890
NEXT X
NEXT Y
RETURN 

890 I=Z(X,Y)
PRINT P$; CHR$(32+X*3); CHR$(48-Y*2); 
IF I=1 THEN 
IF MOD(X+Y,2)=0 THEN 895
ELSE 
IF V*SGN(I)=-1 THEN 895
ENDIF 
PRINT R$; 
895 I=ABS(I)
IF I=99 THEN 
PRINT "K"; 
ELSE 
PRINT MID$(Q$,I,1); 
ENDIF 
PRINT N$; 
RETURN 

1140 PRINT B$; R$; P$; " 4CANNOT CASTLE"; N$
GOTO 1160
1150 PRINT B$; R$; P$; " 4ENTRY ERROR"; N$
1160 PRINT P$; " 3Your move?     "; P$; "+3"; 
GOSUB 1500
X$=IN$
GOSUB 1500
X1$=IN$
GOSUB 1500
X2$=IN$
GOSUB 1500
X3$=IN$
PRINT P$; " 3"; BL$; P$; " 4"; BL$; 
IF X$>="A" AND X$<="H" THEN 1310
IF X$<>X1$ OR X$<>X2$ OR X$<>X3$ THEN 1150

1200 IF V=1 AND Z(5,1)=99 THEN 
IF X$="K" AND Z(8,1)=7 AND Z(6,1)<2 AND Z(7,1)<2 THEN 
Z(5,1)=1
Z(6,1)=7
Z(7,1)=99
Z(8,1)=1
GOTO 1340
ENDIF 
IF X$="Q" AND Z(1,1)=7 AND Z(4,1)<2 AND Z(3,1)<2 THEN 
Z(5,1)=1
Z(4,1)=7
Z(3,1)=99
Z(1,1)=1
GOTO 1340
ENDIF 
ENDIF 
IF V=-1 AND Z(4,1)=99 THEN 
IF X$="K" AND Z(1,1)=7 AND Z(3,1)<2 AND Z(2,1)<2 THEN 
Z(4,1)=1
Z(3,1)=7
Z(2,1)=99
Z(1,1)=1
GOTO 1340
ENDIF 
IF X$="Q" AND Z(8,1)=7 AND Z(5,1)<2 AND Z(6,1)<2 THEN 
Z(4,1)=1
Z(5,1)=7
Z(6,1)=99
Z(8,1)=1
GOTO 1340
ENDIF 
ENDIF 
IF X$="K" OR X$="Q" THEN 1140

IF X$="I" THEN 
PRINT C$; 
GOSUB 1470
PRINT 
PRINT "Hit any key to continue... "; 
GOSUB 1500
GOSUB 880
GOTO 1160
ENDIF 

IF X$="P" THEN 70
IF X$="T" THEN 5000

GOTO 1150

1310 X=ASC(X$)-64
Y=ASC(X1$)-48
A=ASC(X2$)-64
B=ASC(X3$)-48
IF Y<1 OR Y>8 OR A<1 OR A>8 OR B<1 OR B>8 THEN 1150
IF Z(X,Y)<2 OR Z(A,B)>1 THEN 1150
RETURN 

1340 A9=X
B2=Y
Y=1
FOR X=1 TO 8
GOSUB 890
NEXT X
X=A9
Y=B2
RETURN 

1360 PRINT P$; " 4Promote pawn to (NBRQ)? "; 
1370 GOSUB 1500
I=SUBSTR(IN$,Q$)
IF I=0 OR IN$=" " OR IN$="P" THEN 1370
Z(A,B)=I
PRINT P$; " 4"; BL$
RETURN 

1440 PRINT B$; B$; B$; R$; P$; " 2** CHECKMATE **"
PRINT "** YOU WIN!! **"; N$
GOTO 2000

1470 PRINT "To move or capture, specify"
PRINT "from/to (eg. 'B1C3').  Enter"
PRINT "KKKK to castle king side, QQQQ"
PRINT "to castle queen side, IIII for"
PRINT "help, PPPP to restart, or TTTT"
PRINT "to terminate program."
RETURN 

1490 RESTORE 1492
FOR Y=8 TO 1 STEP -1
FOR X=1 TO 8
READ Z(X,Y)
NEXT X
NEXT Y
FOR I=0 TO 9
Z(I,0)=0
Z(I,9)=0
Z(0,I)=0
Z(9,I)=0
NEXT I
1492 DATA -7,-4,-5,-9,-99,-5,-4,-7
DATA -2,-2,-2,-2,-2,-2,-2,-2
DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
DATA 2,2,2,2,2,2,2,2
DATA 7,4,5,9,99,5,4,7
1495 C=0
D=0
F=.0
G=.0
H=0
M=0
N=0
P=0
S=0
T=0
(* White flag 1=human, -1=computer
V=1
W=0
A1=0
A2=0
A3=0
A4=0
A5=0
A8=0
B1=0
B3=0
B4=0
B5=0
B9=0
Q$=" P NB R Q"
B$=CHR$(7)+CHR$(7)+CHR$(7)
C$=CHR$(12)
N$=CHR$(31)+CHR$(33)
P$=CHR$(2)
R$=CHR$(31)+CHR$(32)
S$="   "
EV$=R$+S$+N$+S$+R$+S$+N$+S$+R$+S$+N$+S$+R$+S$+N$+S$
OD$=N$+S$+R$+S$+N$+S$+R$+S$+N$+S$+R$+S$+N$+S$+R$+S$+N$
BL$="                                     "
RETURN 

1500 RUN INKEY(IN$)
IF IN$="" THEN 1500
I=ASC(IN$)
IF I>96 AND I<123 THEN 
IN$=CHR$(I-32)
ENDIF 
RETURN 

2000 PRINT "Play again (y/n)? "; 
GOSUB 1500
IF IN$="Y" THEN 70

5000 PRINT CHR$(27); CHR$(36); 
END 
