0 DIMC1,CC,M$,MC:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
20 DIMZ(10,10),B6,B7,F,G,A,B,C,D,E,H,I,J,K,M,N,P,Q,R,S,T,U,V,W,X,Y
40 DIMA0,A1,A2,A3,A4,A5,A6,A7,A8,A9,B1,B2,B3,B4,B5,B9,IN$,X$,X1$,X2$,X3$,B$,C$,N$,P$,R$,S$,Q$,OL
70 GOSUB1490
80 CLS:M$="******* GRAND MASTER 0.9 *******":GOSUB7:PRINT:PRINT "(C) 1992 BY MATTHEW THOMPSON":PRINT
85 PRINT"DO YOU NEED HELP (Y/N)? ";
90 GOSUB1500:IFIN$<>"Y"ANDIN$<>"N"THEN90
95 PRINT:IFIN$="Y"THENPRINT:GOSUB1470
96 PRINT:PRINT "EASY <---------> HARD":PRINT " 1  2  3  4  5  6  7":PRINT:PRINT"DIFFICULTY LEVEL (1-7)? ";
100 GOSUB1500:IFIN$<"1"ORIN$>"7"THEN100
101 I=VAL(IN$)-1:B7=INT(I*2):IF B7=0THENB7=1
102 PRINT:PRINT:PRINT "DO YOU WANT WHITE (Y/N)? ";
110 GOSUB1500:IFIN$<>"Y"ANDIN$<>"N"THEN110
111 IFIN$="N"THENZ(4,1)=99:Z(5,1)=9:Z(4,8)=-99:Z(5,8)=-9:V=-(V) 
112 GOSUB880
113 IFIN$="Y"THEN 260
210 FORC1=0TO2:PRINT@26+C1*32,"":NEXTC1:PRINT@26,;:F=-99:A0=0:J=1:K=1
215 IFZ(J,K)=99THENA6=J:A7=K:GOTO220
216 K=K+1:IFK=9THENK=1:J=J+1
217 IFJ<9THEN215
220 X=1:Y=1
225 IFZ(X,Y)<0THENOL=PEEK(MC+X*3+(17-Y*2)*32):?@X*3+(17-Y*2)*32,CHR$(191);:GOSUB320:POKEMC+X*3+(17-Y*2)*32,OL:IFF>=B7THEN240
230 Y=Y+1:IFY=9THENY=1:X=X+1
231 IFX<9THEN225
232 IFF<-9THEN1440
240 Z(R,U)=Z(E,Q):Z(E,Q)=1:IFZ(R,U)=-2ANDU=1THENZ(R,U)=-9
250 A0=4:X=R:Y=U:GOSUB320:SOUND100,1:PRINT@26,"MY";:PRINT@26+32*1,"MOVE";
251 PRINT@26+32*2,CHR$(E+64);CHR$(Q+48);CHR$(R+64);CHR$(U+48)
252 PRINT@26+32*3,"":PRINT@26+32*4,"":PRINT@26+32*5,"":GOSUB890:X=E:Y=Q:GOSUB890
253 IFC=1THENPRINT@26+32*6+1,;:M$="CHECK":GOSUB7:C=0
260 GOSUB1160:D=0:B4=A:B9=B:B5=0:IFX$="K"ORX$="Q"THEN210
270 A0=3:GOSUB300:IFD=0THENPRINT@26+6*32+1,;:M$="CAN'T":GOSUB7:GOTO260
280 A=B4:B=B9:Z(A,B)=Z(X,Y):Z(X,Y)=1:GOSUB890:X=A:Y=B:GOSUB890:IFZ(A,B)=2ANDB=8THENGOSUB1360:GOSUB890 
290 IFB5=1THENZ(A,B-1)=1:X=A:Y=B-1:GOSUB890
291 GOTO210
300 ONZ(X,Y)GOTO70,500,70,420,370,70,330,70,370
310 FORA=X-1TOX+1:FORB=Y-1TOY+1:IFZ(A,B)<>0THENGOSUB670 
311 NEXTB:NEXTA:RETURN 
320 ON-(Z(X,Y))GOTO70,550,70,420,370,70,330,70,370:GOTO310
330 B=Y:A=X+1
335 GOSUB660:IFS=0THENA=A+1:IFA<9THEN335
340 A=X-1
345 GOSUB660:IFS=0THENA=A-1:IFA>0THEN345 
350 A=X:B=Y+1
355 GOSUB660:IFS=0THENB=B+1:IFB<9THEN355 
360 B=Y-1
365 GOSUB660:IFS=0THENB=B-1:IFB>0THEN365
366 RETURN
370 B=Y:A=X+1
375 B=B+1:GOSUB660:IFS=0THENA=A+1:IFA<9THEN375 
380 B=Y:A=X-1
385 B=B-1:GOSUB660:IFS=0THENA=A-1:IFA>0THEN385 
390 B=Y:A=X-1
395 B=B+1:GOSUB660:IFS=0THENA=A-1:IFA>0THEN395 
400 B=Y:A=X+1
405 B=B-1:GOSUB660:IFS=0THENA=A+1:IFA<9THEN405
410 IFABS(Z(X,Y))=9THEN330
411 RETURN 
420 A=X+2:B=Y+1:IFA<9ANDB<9THENGOSUB670
430 B=B-2:IFB>0ANDA<9THENGOSUB670
440 A=A-4:IFA>0ANDB>0THENGOSUB670
450 B=B+2:IFB<9ANDA>0THENGOSUB670
460 A=A+1:B=B+1:IFA>0ANDA<9ANDB<9THENGOSUB670
470 B=B-4:IFB>0ANDA>0ANDA<9THENGOSUB670
480 A=A+2:IFA>0ANDA<9ANDB>0THENGOSUB670
490 B=B+4:IFB<9ANDA>0ANDA<9THENGOSUB670
491 RETURN 
500 A=X:IFY>2THEN520
510 B=Y+1:IFZ(A,B)=1THENGOSUB680:B=B+1:IFZ(A,B)=1THENGOSUB680
511 GOTO530
520 B=Y+1:IFZ(A,B)=1THENGOSUB680 
530 A=X+1:B=Y+1:IFZ(A,B)<0THENGOSUB680 
540 A=A-2:IFZ(A,B)<0THENGOSUB680
541 RETURN 
550 A=X:IFY<7THEN570
560 B=Y-1:IFZ(A,B)=1THENGOSUB680:B=B-1:IFZ(A,B)=1THENGOSUB680
561 GOTO580
570 B=Y-1:IFZ(A,B)=1THENGOSUB680 
580 A=X-1:B=Y-1:IFZ(A,B)>1THENGOSUB680
590 A=A+2:IFZ(A,B)>1THENGOSUB680
591 RETURN 
600 T=Z(A,B):IFT=-99THENB1=T:RETURN 
601 IFABS(T)<=Z(X,Y)THENZ(A,B)=Z(X,Y):Z(X,Y)=1:GOTO630
620 IFT<B1THENB1=T
621 S=A5:RETURN 
630 A1=X:A2=Y:A3=A:A4=B:A8=T:A0=2:X=1:Y=1
635 IFZ(X,Y)<0THENGOSUB320:IFT=0THEN650
636 Y=Y+1:IFY=9THENY=1:X=X+1 
637 IFX<9THEN635
650 X=A1:Y=A2:A=A3:B=A4:A0=5:Z(X,Y)=Z(A,B):Z(A,B)=A8:GOTO620
660 S=0:IFZ(A,B)=1THEN680
661 S=1:IFZ(A,B)=0THENRETURN
662 IFSGN(Z(A,B))=SGN(Z(X,Y))THENRETURN 
663 GOTO680
670 IFZ(A,B)<>1THENIFSGN(Z(A,B))=SGN(Z(X,Y))THENRETURN
680 ONA0GOTO690,700,710,720,730:GOTO740
690 IFA6=AANDA7=BTHENB1=B1+1 
691 RETURN 
700 IFA3=AANDA4=BTHENT=0
701 RETURN 
710 IFB4=AANDB9=BTHEND=1
711 RETURN 
720 IFA6=AANDA7=BTHENC=1
721 RETURN 
730 IFZ(A,B)<0THEN600
731 RETURN 
740 B3=S:W=X:M=Y:N=A:H=B:P=Z(A,B):Z(A,B)=Z(X,Y):Z(X,Y)=1:B1=0:A0=5:FORX=1TO8:FORY=1TO8:IFZ(X,Y)>1THENGOSUB300 
741 NEXTY:NEXTX
750 X=N:Y=H:A0=1:GOSUB320:A0=0:S=B3:X=W:Y=M:A=N:B=H:Z(X,Y)=Z(A,B):Z(A,B)=P
760 B6=1/(ABS(4.5-INT(A))+ABS(4.5-INT(B))+1):IFZ(X,Y)<-2ANDZ(X,Y)>-9THENB6=B6+1/(ABS(INT(A6-A))+ABS(INT(A7-B))+5)+RND(0)/15
770 G=INT(P)+INT(B1)+B6:IFP=99THEN?@26,"CHECK";:?@26+1*32,"& MATE";:?@26+2*32,"YOU";:PRINT@26+3*32,"LOSE!";:GOTO2000
780 IFG<=FTHENRETURN
790 F=G:E=X:Q=Y:R=A:U=B:RETURN
880 CLS:FORI=8TO2STEP-2:PRINTSTR$(I);:GOSUB1498:PRINT:PRINT"  ";:GOSUB1498:PRINT
881 PRINTSTR$(I-1);:GOSUB1497:PRINT:PRINT"  ";:GOSUB1497:IFI<>2THENPRINT
882 NEXTI
883 PRINT@480,"  A  ";:M$="B  ":GOSUB7:PRINT"C  ";:M$="D  ":GOSUB7:PRINT"E  ";:M$="F  ":GOSUB7:PRINT"G  ";:M$="H  ":GOSUB7
884 FORY=8TO1STEP-1:FORX=1TO8:GOSUB890:NEXTX:NEXTY:RETURN 
890 I=Z(X,Y):PRINT@X*3+(16-Y*2)*32,;:IFI=1THENIF(X+Y)-(2*INT((X+Y)/2))=0THEN895
891 IFI=1THENIF(X+Y)-(2*INT((X+Y)/2))<>0THEN897
892 IFV*SGN(I)=-1THEN897
895 I=ABS(I):IFI=99THENPRINT"K";
896 PRINTMID$(Q$,I,1);:RETURN
897 I=ABS(I):IFI=99THENM$="K":GOSUB7:RETURN
898 M$=MID$(Q$,I,1):GOSUB7:RETURN
1140 PRINT@26+6*32,"CAN'T";:PRINT@26+7*32,"CASTLE";:GOTO1160
1150 PRINT@26+6*32+1,;:M$="ERROR":GOSUB7
1160 PRINT@26+3*32,"YOUR";:PRINT@26+4*32,"MOVE?";:PRINT@26+5*32,;
1161 GOSUB1500:X$=IN$:GOSUB1500:X1$=IN$:GOSUB1500:X2$=IN$:GOSUB1500:X3$=IN$
1162 FORC1=3TO14:PRINT@26+C1*32,"":NEXTC1
1163 IFX$>="A"ANDX$<="H"THEN1310
1164 IFX$<>X1$ORX$<>X2$ORX$<>X3$THEN1150
1200 IFV=1ANDZ(5,1)=99THEN1202
1201 GOTO1205
1202 IFX$="K"ANDZ(8,1)=7ANDZ(6,1)<2ANDZ(7,1)<2THENZ(5,1)=1:Z(6,1)=7:Z(7,1)=99:Z(8,1)=1:GOTO1340
1203 IFX$="Q"ANDZ(1,1)=7ANDZ(4,1)<2ANDZ(3,1)<2THENZ(5,1)=1:Z(4,1)=7:Z(3,1)=99:Z(1,1)=1:GOTO1340
1205 IFV=-1ANDZ(4,1)=99THEN1210
1206 GOTO1220
1210 IFX$="K"ANDZ(1,1)=7ANDZ(3,1)<2ANDZ(2,1)<2THENZ(4,1)=1:Z(3,1)=7:Z(2,1)=99:Z(1,1)=1:GOTO1340
1211 IFX$="Q"ANDZ(8,1)=7ANDZ(5,1)<2ANDZ(6,1)<2THENZ(4,1)=1:Z(5,1)=7:Z(6,1)=99:Z(8,1)=1:GOTO1340
1220 IFX$="K"ORX$="Q"THEN1140
1221 IFX$="I"THENCLS:GOSUB1470:PRINT:PRINT"HIT ANY KEY TO CONTINUE...";:GOSUB1500:GOSUB880:GOTO1160
1222 IFX$="P"THEN70
1223 IFX$="T"THEN5000
1224 GOTO1150
1310 X=ASC(X$)-64:Y=ASC(X1$)-48:A=ASC(X2$)-64:B=ASC(X3$)-48
1311 IFY<1ORY>8ORA<1ORA>8ORB<1ORB>8THEN1150
1312 IFZ(X,Y)<2ORZ(A,B)>1THEN1150
1313 RETURN 
1340 A9=X:B2=Y:Y=1:FORX=1TO8:GOSUB890:NEXTX:X=A9:Y=B2:RETURN 
1360 PRINT@26+6*32,"PROM.":PRINT@26*7*32,"PAWN":PRINT@26+8*32,"NBRQ?";
1370 GOSUB1500
1371 I=0:IFIN$="P"THENI=2
1372 IFIN$="N"THENI=4
1373 IFIN$="B"THENI=5
1374 IFIN$="R"THENI=7
1375 IFIN$="Q"THENI=9
1376 IFI=0ORIN$=" "ORIN$="P"THEN1370
1377 Z(A,B)=I:FORC1=7TO14:PRINT@26+C1*32,"":NEXTC1:RETURN
1440 PRINT@26,"CHECK";:PRINT@26+1*32,"& MATE";:PRINT@26+2*32,"YOU";:PRINT@26+3*32,"WIN!";:GOTO2000
1470 PRINT "TO MOVE OR CAPTURE, SPECIFY"
1471 PRINT "FROM/TO (EG. '";:M$="B1C3":GOSUB7:PRINT"').  ENTER"
1472 PRINT "kkkk TO CASTLE KING SIDE, qqqq"
1473 PRINT "TO CASTLE QUEEN SIDE, iiii FOR"
1474 PRINT "HELP, pppp TO RESTART, OR tttt"
1475 PRINT "TO TERMINATE PROGRAM."
1476 RETURN
1490 RESTORE:FORY=8TO1STEP-1:FORX=1TO8:READZ(X,Y):NEXTX:NEXTY:FORI=0TO9:Z(I,0)=0:Z(I,9)=0:Z(0,I)=0:Z(9,I)=0:NEXTI
1492 DATA-7,-4,-5,-9,-99,-5,-4,-7,-2,-2,-2,-2,-2,-2,-2,-2
1493 DATA1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
1494 DATA2,2,2,2,2,2,2,2,7,4,5,9,99,5,4,7
1495 C=0:D=0:F=0:G=0:H=0:M=0:N=0:P=0:S=0:T=0:V=1:W=0:A1=0:A2=0:A3=0:A4=0:A5=0:A8=0:B1=0:B3=0:B4=0:B5=0:B9=0:Q$=" P NB R Q"
1496 S$="   ":RETURN 
1497 PRINTS$;:M$=S$:GOSUB7:PRINTS$;:GOSUB7:PRINTS$;:GOSUB7:PRINTS$;:GOSUB7:RETURN
1498 M$=S$:GOSUB7:PRINTS$;:GOSUB7:PRINTS$;:GOSUB7:PRINTS$;:GOSUB7:PRINTS$;:RETURN
1500 IN$=INKEY$:IFIN$=""THEN1500
1510 I=ASC(IN$):IFI>96ANDI<123THENIN$=CHR$(I-32)
1520 PRINTIN$;:RETURN
2000 PRINT@26+6*32,"PLAY";:PRINT@26+7*32,"AGAIN";:PRINT@26+8*32,"(Y/N)?";:PRINT@26+9*32,;:GOSUB1500:IFIN$="Y"THEN70
5000 END
5010 REM MC-10 EDITS JIM GERRIE
