0 CLS:CLEAR500:DIMC1,C2,C3,Z,M$,I$:Z=16384:GOTO200
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:RETURN

200 REM *MAIN PROGRAM*
210 C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
220 REM   
230 M$="YOU ARE NOW GOING TO PLAY A COMPUTERIZED VERSION OF BINGO--":GOSUB1
240 F=1
250 DIM B(10,10)
260 DIM L(75),A$(5)
270 FOR C1=1 TO 5:READ A$(C1):NEXT
280 DATA B ,I ,N ,G ,O 
290 REM  THIS SEQUENCE GENERATES THE CARD NUMBERS (LINES 240-430)
299 CLS
300 FOR K1=1 TO 75
310 L(K1)=0
320 NEXT K1
330 REM 
340 IF F>1 THEN 370
350 LL=0:PRINT@LL,"   YOUR CARD    ";:PRINT@LL+32,"éåbååiåånåågååoç";:LL=LL+64
360 GOTO 380
370 LL=16:PRINT@LL,"    MY CARD     ";:PRINT@LL+32,"éåbååiåånåågååoç";:LL=LL+64
380 M=16
390 G=F+4
400 FOR Y=F TO G
410 FOR X=F TO G
420 R=INT(M*RND(0))
430 IF R<M-15 THEN 420
440 IF L(R)<>0 THEN 420
450 B(X,Y)=R
460 L(R)=1
470 NEXT X
480 M=M+15
490 NEXT Y

500 REM  THIS SEQUENCE PRINTS THE CARD (LINES 440-670)
520 RR=0
530 FOR X=F TO G
570 FOR Y=F TO G
580 IF B(X,Y)=B(F+2,F+2) THEN 610
590 PRINT@LL+RR,"ä"MID$(STR$(B(X,Y))+" ",2,2)"Ö";:RR=RR+3
600 GOTO 620
610 PRINT@LL+RR,"ä  ä";:RR=RR+3
620 NEXT Y:LL=LL+32:RR=0
630 NEXT X
670 PRINT@LL,"ãÉÉÉÉÉÉÉÉÉÉÉÉÉÉá";
720 IF F=6 THEN 750
730 F=6
740 GOTO 300

750 REM CONTINUE
760 REM M$="NOW WE'RE ALL SET TO PLAY THE GAME. USE A PENCIL TO MARK OFF THE NUMBERS ON YOUR CARD AS THEY ARE CALLED."
761 REM GOSUB1
780 REM M$="(PLEASE PLAY MY CARD AS WELL AS YOUR OWN) ***DON'T GET SMART. I'LL BE PLAYING BOTH YOUR CARD AND MINE"
800 REM M$=M$+" UP HERE IN DARTMOUTH. (SO, THERE--)":GOSUB1
810 REM
820 PRINT "ARE YOU READY";
830 INPUT R$
840 IF R$<>"NO" THEN 880
850 PRINT
860 PRINT"*********:HURRY UP:*********"
870 GOTO 810
880 REM
890 FOR K1=1 TO 75
900 L(K1)=0
910 NEXT K1
930 B(3,3)=0
940 B(8,8)=0
950 REM  THIS SEQUENCE GENERATES THE BINGO NUMBERS (LINES 880-1100)
960 U=INT(75*RND(0))+1
970 IF L(U)<>0 THEN 960
980 L(U)=1
990 PRINT
1000 IF RND(0)>.5 THEN 1030
1010 PRINT@256,"THE NUMBER COMES UP:"
1020 GOTO 1040
1030 PRINT@256,"THE NEXT ONE IS:"
1040 PRINT A$(INT((U-1)/15)+1);U
1050 REM  THIS IS THE "NUMBER ON CARD?" SEQUENCE (LINES 1110-1260)
1060 FOR Y=1 TO 10
1070 FOR X=1 TO 10
1080 IF B(X,Y)=U THEN 1200
1090 NEXT X
1100 NEXT Y
1110 F=1
1120 GOSUB 1250
1130 F=6
1140 GOSUB 1250
1150 IF V=0 THEN 1180
1160 IF W=1 THEN 1900
1170 GOTO 2000
1180 IF W=0 THEN 960
1190 GOTO 2080
1200 C1=32*(X-1)+(Y-1)*3+65:PRINT@416,;:INPUTM$
1201 IFY>5THENC1=C1+1
1202 IFX>5THENC1=C1-160
1205 PRINT@C1,;:M$=MID$(STR$(B(X,Y))+" ",2,2):GOSUB7:B(X,Y)=0:PRINT@320,;
1210 GOTO 1090
1220 REM  THIS IS THE BINGO DETERMINING SEQUENCE (LINES 1270-1950)
1230   
1240 REM  THIS IS THE VERTICAL CHECK FOR BINGO*** (LINES 1270-1430)
1250 G=F+4
1260 FOR Y=F TO G
1270 FOR X=F TO G
1280 IF B(X,Y)<>0 THEN 1400
1290 IF X<F+4 THEN 1390
1300 IF F=6 THEN 1350
1310 PRINT"YOU'VE GOT A   B I N G O * * *"
1330 W=1
1340 RETURN
1350 PRINT"I'VE GOT A   B I N G O * * * * *"
1370 V=1
1380 GOTO 1410
1390 NEXT X
1400 NEXT Y
1410 REM  THIS IS THE HORIZONTAL CHECK FOR BINGO*** (LINES 1450-1610)
1420 G=F+4
1430 FOR X=F TO G
1440 FOR Y=F TO G
1450 IF B(X,Y)<>0 THEN 1570
1460 IF Y<F+4 THEN 1560
1470 IF F=6 THEN 1520
1480 PRINT"YOU'VE GOT A   B I N G O * * *"
1500 W=1
1510 RETURN
1520 PRINT"I'VE GOT A   B I N G O * * * * *"
1540 V=1
1550 GOTO 1590
1560 NEXT Y
1570 NEXT X
1580 REM  THIS IS THE SLANT CHECK (M=-1) FOR BINGO*** (LINES 1620-1770)
1590 X=F
1600 Y=F
1610 IF B(X,Y)<>0 THEN 1720
1620 X=X+1
1630 Y=Y+1
1640 IF Y<F+5 THEN 1610
1650 IF Y=11 THEN 1700
1660 W=1
1670 PRINT"YOU'VE GOT A  B I N G O * * *"
1690 RETURN
1700 PRINT"I'VE GOT A  B I N G O * * * * *"
1710 V=1 
1720 RETURN
1730 REM  THIS IS THE SLANT CHECK (M=1) FOR BINGO*** (LINES 1780-1950)
1740 X=F+4
1750 Y=X
1760 IF B(X,Y)<>0 THEN 1880
1770 X=X-1
1780 Y=Y+1
1790 IF Y<F+5 THEN 1760
1800 IF Y=11 THEN 1850
1810 PRINT"YOU'VE GOT A  B I N G O * * *"
1830 W=1
1840 RETURN
1850 PRINT"I'VE GOT A  B I N G O * * * * *"
1870 V=1
1880 RETURN
1890 REM  THIS THE TIE PRINTOUT SEQUENCE
1900 PRINT
1910 PRINT"  *********** IT'S A TIE ***********"
1920 REM  THIS IS THE "PLAY AGAIN?" SEQUENCE
1930 PRINT
1940 PRINT@448,"DO YOU WANT TO PLAY AGAIN";
1950 INPUT E$
1960 PRINT
1970 IF E$="YES" OR E$="Y" THEN 240
1980 END
1990 REM  THIS IS THE "I WIN" SEQUENCE
2000 FOR S=1 TO 6
2020 PRINT"I WIN.  ";
2030 NEXT S
2040 PRINT
2050 PRINT
2060 GOTO 1930
2070 REM  THIS IS THE "YOU WIN" SEQUENCE
2080 PRINT
2090 PRINT"  YOU WIN..... "
2100 GOTO 1930
2110 REM  THIS IS THE "END" OF THE LIST OF PROGRAM ENTITLED "BINGO"
2120 END
3100 REM NAME--BINGO
3110 REM
3120 REM DESCRIPTION--
3130 REM GAME OF BINGO
3140 REM SOURCE--
3150 REM LARRY BETHURUM
3151 REM PHILLIPS EXETER
3152 REM ACADEMY. 1/23/66
