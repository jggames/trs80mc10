0 CLEAR400:DIMC1,C2,M,ZZ,CC,M$:MC=16384:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,:RETURN
10 CC=.5:ZZ=.66666666
30 DIMQ(53),S(53),I(12),X(52),Y(52)
40 GOSUB1700:GOTO560
100 C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
110 REM USERS CARDS
120 REM VARIABLES
130 A=10:X=0:U=19:YX=(U*3)-A:P=12
140 FORM=XTOUSTEP2:C1=M*CC:SET(C1,1*ZZ,5):SET(C1,9*ZZ,5):SET(C1,13*ZZ,5):SET(C1,21*ZZ,5)
150 SET(C1,25*ZZ,5):SET(C1,33*ZZ,5):SET(C1,37*ZZ,5):SET(C1,45*ZZ,5):NEXT:GOSUB200
170 X=U+4:U=(X+(A*2))
180 IFX<>YXTHEN140
190 IFR=1THENR=0:GOTO300
191 GOTO260
200 REM VERTICAL FOR CARDS
210 Y=2:N=8
220 FORM=YTONSTEP1.5:SET(X*CC,M*ZZ,5):SET(U*CC,M*ZZ,5):NEXT
230 Y=N+6:N=Y+6
240 IFY<>(A*5)THEN220
250 RETURN
260 REM COMPUTER'S HAND
270 X=(X*2)-A:U=(U*2)-31:YX=(YX*3)-A
280 R=1:GOTO140
300 REM DRAW CARD
310 FORX=54TO73:C1=X*CC:SET(C1,19*ZZ,5):SET(C1,27*ZZ,5):NEXT
320 FORY=20TO26:C1=Y*ZZ:SET(54*CC,C1,5):SET(73*CC,C1,5):NEXT
330 PRINT@331,;:M$="DRAW CARD ":GOSUB7
340 REM CARD AREA FOR USER
350 U=67:W=130:J=1:L=1091
360 U(J)=U:W(J)=W:J=J+1
370 U=U+256:W=W+256:IFJ=9THEN420
380 IFU<>LTHEN360
390 U=79:W=142:L=1103
400 GOTO360
410 REM CARD AREA FOR COMPUTER
420 C=109:V=172:J=1:L=1133
430 C(J)=C:V(J)=V:J=J+1
440 C=C+256:V=V+256:IFJ=9THEN490
450 IFC<>LTHEN430
460 C=121:V=184:L=1207
470 GOTO430
480 REM ONE DECK OF CARDS
490 FORK=1TO52:Q(K)=K:NEXT:E=INT(K/4):S=K:S1=1:CU=0:RETURN
500 Z=RND(S)
510 IFQ(Z)=0THEN500
520 T=INT((Q(Z)-1)/E):REM SUIT
530 J=INT(Q(Z)-T*E):REM VALUE
540 J1=T:J2=J
550 Q(Z)=0:REM CARD USED
555 CU=CU+1:IFCU+1=52THENGOSUB580:M$="LAST CARD ":GOSUB7:GOSUB596
556 IFCU+1=53THENGOSUB580:M$="GAME OVER ":GOSUB7:GOTO1300
558 RETURN
560 GOSUB100
570 GOTO600
580 PRINT@23*CC,"€€€€€€€€€€";
590 PRINT@23*CC,;:RETURN
595 FORTI=1TO3000:NEXT:RETURN
596 FORTY=1TO1500:NEXT:RETURN
600 REM USERS CARD PLACEMENT
610 GOSUB580
620 PRINT"YOUR CARDS";
630 FORI=1TO8:PRINT@U(I)*CC,I;:NEXT:GOSUB595:GOSUB580
640 FORI=1TO8:PRINT@U(I)*CC,"€€€";:NEXT
650 FORI=1TO8:A(I)=U(I):Z(I)=W(I):NEXT:I=1:V=1:II=1
670 GOSUB500
680 IFJ2=1THENPRINT@A(I)*CC,"a";:J2=1:GOTO720
690 IFJ2=11THENPRINT@A(I)*CC,"j";:J2=11:GOTO720
700 IFJ2=12THENPRINT@A(I)*CC,"q";:J2=12:GOTO720
701 IFJ2=10THENPRINT@A(I)*CC,"t";:GOTO720
710 IFJ2=13THENPRINT@A(I)*CC,"k";:J2=13:GOTO720
711 PRINT@A(I)*CC,;:M$=MID$(STR$(J2),2,1):GOSUB7
720 IFR=3THENX(II)=J2:GOTO730
721 Y(II)=J2
730 IFJ1=0THENPRINT@Z(I)*CC-31,"ÔÞ";:GOTO765
740 IFJ1=1THENPRINT@Z(I)*CC-31,"ÑÛ";:GOTO765
750 IFJ1=2THENPRINT@Z(I)*CC-31,"±»";:GOTO765
760 IFJ1=3THENPRINT@Z(I)*CC-31,"´¶";
765 IFR=2THENRETURN
766 IFR=1THENR=2:GOTO820
770 I=I+1:II=II+1:V=V+1:IFI<>9THEN670
780 IFV=17THEN810
790 FORI=1TO8:A(I)=C(I):Z(I)=V(I):NEXT:I=1:II=1:R=3
800 GOTO670
810 A(I)=476:Z(I)=541:R=1:II=I:GOTO670:REM DRAW CARD
820 REM WHO GOES FIRST?
825 M=8
830 GOSUB580
840 IFY(M)=X(M)THEN870
850 IFY(M)>X(M)THEN880
860 IFY(M)<X(M)THEN890
870 M=M-1:GOTO830
880 M$="YOU GO 1ST":GOSUB7:GOTO1000
890 M$="I'M FIRST ":GOSUB7:GOSUB596:GOTO1200
900 PRINT@A(II)*CC,"€€€€";
910 RETURN
1000 REM USER GOES FIRST
1010 GOSUB596
1020 LL=0:GOSUB580
1030 M$="PLAY Y/N? ":GOSUB7
1040 P$=INKEY$:IFP$="Y"THEN1050
1045 IFP$="N"THEN1047
1046 GOTO1040
1047 B=B+1:IFB=2THENB=0:GOSUB900:GOSUB500:GOSUB680:GOTO1200
1048 GOTO1200
1050 B=0:GOSUB580
1060 M$="#OF CARDS?":GOSUB7
1070 N$=INKEY$:IFN$=""THEN1070
1080 N=VAL(N$):GOSUB580
1090 M$="CARD #S   ":GOSUB7
1100 K=1:T1=0
1110 M$=INKEY$:IFM$=""THEN1110
1120 T(K)=VAL(M$):SOUND100,1:T1=Y(T(K))+T1:K=K+1
1130 IFK<=NTHEN1110
1140 IFT1+J2<>15THEN1150
1145 FORI=1TO8:A(I)=U(I):Z(I)=W(I):NEXT:T2=T1+J2+T2:GOSUB1500:GOTO1400
1150 GOSUB580:M$="BAD TOTAL ":GOSUB7:GOSUB596
1155 GOSUB580
1200 REM COMPUTER'S TURN
1205 GOSUB580:GOSUB595
1210 B=0:K=1:T3=0:H=0:KK=1
1220 IFJ2+X(K)=15THENM1=1:GOSUB1560:GOTO1650
1225 IFJ2+X(H)=15THENM1=1:GOSUB1580:GOTO1650
1230 IFJ2+X(K)+X(H)=15THENM1=2:GOSUB1590:GOTO1650
1235 IFJ2+X(H+2)+X(H+4)=15THENM1=2:GOSUB1600:GOTO1650
1240 K=K+1:IFK=9ANDH=9THEN1270
1250 IFK=9THENH=H+1:K=KK
1260 GOTO1220
1270 B=B+1:IFB=2THENB=0:GOSUB900:GOSUB500:GOSUB680:GOTO1020
1271 GOTO1020
1300 REM WIN - LOSE
1305 FORKL=1TO2500:NEXT
1310 REM CLS:PRINT
1315 IFT6=T2THEN1330
1320 IFT6<T2THEN1340
1325 IFT6>T2THEN1355
1330 PRINT@480,;:M$="TIE!  YOU"+STR$(T2)+"  ME"+STR$(T6):GOSUB7:GOTO1365
1340 PRINT@480,;:M$="YOU WIN.  YOU"+STR$(T2)+"  ME"+STR$(T6):GOSUB7:GOTO1365
1355 PRINT@480,;:M$="YOU LOSE!  YOU"+STR$(T2)+"  ME"+STR$(T6):GOSUB7:GOTO1365
1365 FORKL=1TO8000:NEXT
1370 PRINT@480,;:M$="TRY ANOTHER GAME OF IT? (Y/N)":GOSUB7
1375 I$=INKEY$:IFI$="N"THEN1385
1380 IFI$="Y"THENT2=0:T6=0:CLS0:GOSUB100:GOTO600
1381 GOTO1375
1385 CLS:PRINT"END OF RUN..."
1390 END
1400 REM DRAW CARD(S)
1405 I=1
1410 A(I)=A(T(I)):Z(I)=Z(T(I)):GOSUB500
1415 Y(T(I))=J2:GOSUB680
1420 I=I+1:IFI<=NTHEN1410
1425 GOSUB500:II=9
1430 A(I)=A(II):Z(I)=Z(II):GOSUB680
1435 IFLL=1THEN1020
1436 GOTO1200
1500 REM CLEAR CARD USED
1510 I=1
1520 PRINT@A(T(I))*CC,"€€€";
1530 I=I+1
1540 IFI<=NTHEN1520
1550 GOSUB900:RETURN
1560 REM CLEAR COMPUTER CARD
1570 PRINT@C(K)*CC,"€€€";:T6=J2+X(K)+T6:GOTO1610
1580 PRINT@C(H)*CC,"€€€";:T6=J2+X(H)+T6:GOTO1610
1590 PRINT@C(K)*CC,"€€€";:PRINT@C(H)*CC,"€€€";
1591 T6=J2+X(K)+X(H)+T6:GOTO1610
1600 PRINT@C(H+2)*CC,"€€€";:PRINT@C(H+4)*CC,"€€€";:T6=J2+X(H+2)+X(H+4)+T6:UU=1
1610 REM
1620 PRINT@A(II)*CC,"€€€";
1630 FORI=1TO8:A(I)=C(I):Z(I)=V(I):NEXT
1640 RETURN
1650 REM DRAW FOR COMPUTER
1660 I=K:M2=1
1665 IFUU=1THENUU=0:K=H+2:H=H+4
1670 A(I)=C(K):Z(I)=V(K):GOSUB500
1675 X(K)=J2:GOSUB680:IFM2=M1THEN1685
1680 M2=M2+1:K=H:GOTO1670
1685 LL=1:M1=0
1690 GOTO1425
1700 CLS
1710 PRINTTAB(7)"<SOLITAIRE FOR TWO>":PRINT
1730 M$="SOLITAIRE FOR TWO IS DESIGNED FOR YOU AND YOUR COMPUTER. "
1740 M$=M$+"EACH OF YOU WILL DRAW EIGHT CARDS.":GOSUB1
1750 M$="THE OBJECT OF THE GAME IS TO USE THE DRAW CARD WITH A COMINATION OF YOUR CARDS TO TOTAL 15.":GOSUB1
1760 M$="ACES COUNT AS 1, KINGS AS 13, QUEENS AS 12 AND JACKS AS 11. ALL OTHERS ARE FACE VALUE.":GOSUB1
1770 M$="THE CARDS YOU USE DETERMINE YOUR POINTS WHEN THE DECK RUNS OUT.":GOSUB1
1840 PRINT"PRESS enter";:INPUTXX:CLS
1850 M$="WHEN ASKED '#OF CARDS' ENTER THE NUMBER OF CARDS FROM YOUR HAND YOU WILL BE USING (I.E. DON'T INCLUDE THE DRAW CARD)."
1851 GOSUB1
1860 M$="YOUR ENTRY WILL NOT REQUIRE THE USE OF 'ENTER.' "
1870 M$=M$+"THE VALUE OF THE 8TH CARD WILL DETERMINE WHICH OF YOU WILL GO FIRST. "
1880 M$=M$+"IN CASE OF A TIE THE 7TH CARD WILL BE USED, ETC., ETC.":GOSUB1
1890 M$="THE COMPUTER WILL PRINT YOUR CARD POSITIONS AT THE BEGINNING OF THE RUN, REMEMBER THESE.":GOSUB1
1900 M$="YOU ONLY GET ONE CHANCE TO ENTER THE CORRECT CARD(S).":GOSUB1
1950 PRINT"PRESS enter TO BEGIN";:INPUTXX
1960 CLS0
1970 RETURN
