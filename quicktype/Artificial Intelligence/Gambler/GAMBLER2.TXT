0 CLS:CLEAR1000:DIMC1,C2,M$,I$,G$(2,11),D$(6),E$(6),H$(3),I$(3):C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$=" "ORI$=""THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=0
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 M$=M$+" $"+MID$(STR$(C1),2):RETURN
4 PRINTTAB((32-(LEN(M$)))/2)M$:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:RETURN
10 PRINT"WELCOME TO *GAMBLER*":PRINT:PRINT:FORY=0TO11:FORX=0TO2:READG$(X,Y):NEXTX,Y
20 A1$="          Ö          Ö":GOSUB430
30 PRINT"HOW MANY PLAYERS (UP TO 4)? ";
40 B$=INKEY$:IFB$=""THEN40
41 N=VAL(B$):IFN<1ORN>4THEN40
42 PRINTN
50 FORI=1TON:PRINT"WHO IS PLAYER #"STR$(I);:INPUTM$:N$(I)=LEFT$(M$,10)
51 M(I)=100:IO(I)=0:L$(I,0)="":NEXTI:N$(I)="TRS-80":M(I)=100:IO=0:N=N+1
52 PRINT:PRINT"DO YOU NEED INSTRUCTIONS?"
60 B$=INKEY$:IFB$=""THEN60
61 IF(ASC(B$)AND95)=89THENGOSUB1370
70 FORIP=1TON
80 CLS:PRINT"PRESENT BANKROLLS           IOUS--------------------------------";:FORJ=1TON
81 M$=LEFT$(N$(J)+" ..............",15)+"$":IFM(J)<0THENM$=M$+MID$(STR$(M(J))+"        ",1,9):GOTO83
82 M$=M$+MID$(STR$(M(J))+"         ",2,9)
83 M$=M$+RIGHT$("            "+STR$(IO(J)),7):PRINTM$;:NEXTJ
84 GOSUB200:PRINT:PRINT"IT IS ";N$(IP);"'S TURN.":IFM(IP)>=0THEN100
90 IO(IP)=IO(IP)+1:M(IP)=M(IP)+100:IFM(IP)<0THEN90
91 M$="YOU MUST BORROW MONEY TO CONTINUE "
92 M$=M$+"TO PLAY AND PAY IT BACK TO THE BANK... PLUS INTEREST!":GOSUB1:FORI=1TO3000:NEXTI:GOTO80
100 IFIO(IP)>0ANDM(IP)>=110THENPB=INT(M(IP)/110):GOTO110
101 GOTO130
110 IFPB>IO(IP)THENPB=IO(IP)
120 IO(IP)=IO(IP)-PB:M(IP)=M(IP)-110*PB
121 M$="YOU CAN PAY BACK"+STR$(PB)+" IOU NOTE AT $110 EACH.":GOSUB1:FORTI=1TO3000:NEXTTI:GOTO80
130 IFL$(IP,0)="XXX"THEN160
131 PRINT"WOULD YOU LIKE TO BUY A LOTTERY":PRINT"TICKET FOR $10? ";
140 IFIP=NTHENPRINT"Y":GOSUB260:L$(IP,0)=L$(IP,0)+"X":L$(IP,LEN(L$(IP,0)))=T$:M(IP)=M(IP)-10:FORTI=1TO1000:NEXTTI:GOTO80
150 B$=INKEY$:IFB$=""THEN150
151 B$=CHR$(ASC(B$)AND95):IFB$="N"THENPRINT"N":GOTO160
152 IFB$<>"Y"THEN150
153 PRINT"Y":GOSUB260:L$(IP,0)=L$(IP,0)+"X":L$(IP,LEN(L$(IP,0)))=T$:M(IP)=M(IP)-10:FORTI=1TO1000:NEXTTI:GOTO80
160 PRINT@485,"HIT ANY KEY TO CONTINUE";
170 B$=INKEY$:IFB$=""THEN170
171 GOSUB290

180 REM GOSUB1200:GOTO190:REM FOR TESTING
181 IFGN<16THENONGNGOSUB1020,810,460,1130,1170,1190,460,1200,1230,1240,460,750,1300,940,1020:GOTO190
182 ONGN-15GOSUB710,810,1310,1320,750,460,1330,710,810,1340,1350,1330,810,460,940,1310,710,750,1360,810,710
190 NEXTIP:GOTO70

200 HM=M(1):HN=1:FORJ=2TON:IFM(J)>HMTHENHM=M(J):HN=J
201 NEXTJ
210 IFHM<1000THENRETURN
220 K=0:FORJ=1TON:IFM(J)=HMTHENK=K+1:NEXTJ:GOTO230
221 NEXTJ
230 IFK>1THENPRINT"SINCE THERE'S A TIE, KEEP GOING";:RETURN
240 PRINT:M$=N$(HN)+" WINS THIS GAME WITH":C1=HM:GOSUB3:M$=M$+"!":GOSUB1
241 FORI=0TO90STEP2:SOUND141-I,1:NEXTI:PRINT"WOULD YOU LIKE TO PLAY AGAIN?"
250 B$=INKEY$:IFB$=""THEN250
251 B$=CHR$(ASC(B$)AND95):IFB$="Y"THENRUN
252 END
260 T$=CHR$(48+RND(5)):T1$=CHR$(48+RND(6)):IFT1$<T$THEN260
261 T$=T$+"-"+T1$:FORI=1TON:IFL$(I,0)=""THEN280
270 FORJ=1TOLEN(L$(I,0)):IFT$=L$(I,J)THEN260
271 NEXTJ
280 NEXTI:PRINT"YOU RECEIVE TICKET NUMBER ";T$:FORJ=1TO6:SOUND238+J,1:NEXT:RETURN

290 CLS:PRINT:FORJ=1TO12:PRINTA1$:NEXTJ:PRINT
300 FORY=0TO11:FORX=0TO2:PRINT@32+32*Y+X*11,G$(X,Y);:NEXTX,Y
310 X=0:Y=0:B$=INKEY$:PRINT@480,N$(IP)", HIT A KEY TO SELECT";
320 PRINT@32+32*Y+X*11,;:M$=G$(X,Y):GOSUB7:SOUNDRND(49),1:FORC1=1TO50:NEXT:PRINT@32+32*Y+X*11,G$(X,Y);
321 X=X+RND(2):Y=Y+RND(2)-1:IFX>2THENY=Y+1:X=RND(3)-1
322 IFY*32+X*11>374THENY=0:X=RND(3)-1
330 IFIP=NTHENON-(RND(30)>25)GOTO360:GOTO320
340 B$=INKEY$:IFB$<>""THEN360
350 GOTO320
360 JJ=100:KK=50:FORI=1TO5+RND(4):PRINT@32+32*Y+X*11,;:M$=G$(X,Y):GOSUB7:SOUNDKK,1:KK=KK+20:FORJ=1TOJJ:NEXTJ:JJ=JJ+100
361 PRINT@32+32*Y+X*11,G$(X,Y);
370 X=X+1:IFX>2THENY=Y+1:X=0
372 IFY*32+X*11>374THENY=0:X=0
373 NEXTI
380 PRINT@32+32*Y+X*11,;:M$=G$(X,Y):GOSUB7:SOUNDKK,1:FORTI=1TO500:NEXTTI:GN=Y*3+X+1
381 FORI=1TO5:PRINT@32+32*Y+X*11,;:M$=G$(X,Y):GOSUB7:SOUND25,2:PRINT@32+32*Y+X*11,G$(X,Y);
382 FORTI=1TO99:NEXTTI:NEXTI:RETURN

390 DATAPOKER,*FORTUNE!*,SWEEPSTAKE,LOVE THY,EASY COME,WIN A FEW,SWEEPSTAKE,UNLUCKY 7,LOSE A FEW
400 DATAEVEN STEVE,SWEEPSTAKE,DAILY DBL,POT LUCK,HI-ROLLER,POKER,LOTTERY,*FORTUNE!*,JACKPOT,$100 BONUS
401 DATATHE RACES,SWEEPSTAKE,LOSE TURN
410 DATALOTTERY,*FORTUNE!*,TAX TIME,BONANZA,LOSE TURN,*FORTUNE!*,SWEEPSTAKE,HI-ROLLER,JACKPOT,LOTTERY
411 DATAHORSE RACE,MAD MONEY,*FORTUNE!*,LOTTERY

430 D$(1)="œœœ":D$(2)="Œœœ":D$(3)="Œœœ":D$(4)="ŒœÕ":D$(5)="ŒœŒ":D$(6)="ŒŒŒ"
440 E$(1)="œÀœ":E$(2)="œœÕ":E$(3)="œÀŒ":E$(4)="ŒœÕ":E$(5)="ŒÀŒ":E$(6)="ŒŒŒ"
450 H$(1)=" èèå":H$(2)=" èéç":H$(3)=" èéç":I$(1)=" éÇá":I$(2)=" ãÅç":I$(3)=" äÇè":RETURN

460 CLS:PRINT@11,"SWEEPSTAKES!"
461 PRINT"  äÉÉÉÉgÉÉÉÉÅÉÉÉÉhÉÉÉÉÖ"
462 PRINT"  a         c         e" 
463 PRINT"  äèèèèèèèèèÖèèèèèèèèèÖ"
464 PRINT
465 PRINT
466 PRINT"       äèèèèèèèèèÖèèèèèèèèèÖ"
467 PRINT"       b         d         f"
468 PRINT"       äååååiååååÑååååjååååÖ"
480 FORK=1TO6:X=249+K*10:GOSUB1465:NEXTK
490 PRINT@323,"DO YOU NEED INSTRUCTIONS?":B$=INKEY$
500 B$=INKEY$:IFB$<>""THEN510
501 IFRND(9)=1THENSOUNDRND(99),1:GOTO500
502 GOTO500
510 B$=CHR$(ASC(B$)AND95):IFB$="N"THEN540
511 PRINT@320,;:M$="IN SWEEPSTAKES EVERYONE ANTES $10. EACH PLAYER BETS ON THE OUTCOME OF THE ROLL OF 6 DICE. "
512 M$=M$+"THERE ARE THREE TYPES OF BETS.":GOSUB1:GOSUB1470:C1=320:GOSUB705
520 M$="ONE WAY IS TO BET ON WHERE THE HIGHEST NUMBER ROLLED WILL APPEAR IF THE HIGH NUMBER APPEARS "
521 M$=M$+"IN A,B,C,D,E OR F YOU WIN $300.":GOSUB1:GOSUB1470:M$="LETTERS G,H,I, AND J COVER TWO SPOTS. "
522 M$=M$+"IF YOU BET G, YOU COVER SPOTS A AND C AND CAN WIN $150.":C1=320:GOSUB705:PRINT@320,;:GOSUB1
530 GOSUB1470:C1=320:GOSUB705:PRINT@320,;:M$="ANOTHER BET IS <P> FOR PAIRS. IF TWO DICE NEXT TO ONE ANOTHER "
531 M$=M$+"MATCH, YOU WIN $200. THE THIRD BET IF <S> FOR A STRAIGHT.":GOSUB1:GOSUB1470:C1=320:GOSUB705
532 PRINT@320,;:M$="IF 3 CONSECUTIVE DICE APPEAR IN NUMERICAL ORDER, YOU WIN $450.":GOSUB1
540 GOSUB1470:C1=320:GOSUB705:PRINT@344,"A-F $300":PRINT@376,"G-J $150":PRINT@409,"P $200":PRINT@441,"S $450"
541 FORI=1TON:M(I)=M(I)-10:S$(I)="":NEXTI:X=320:PRINT@488,"PLACE YOUR BETS";
550 FORJ=IPTON:PRINT@X,N$(J)": ";:X=X+32
560 IFJ=NTHEN565
561 GOTO570
565 B$=CHR$(64+RND(11)):IFB$="K"THENB$="P"
566 GOTO580
570 B$=INKEY$:IFB$=""THEN570
571 B$=CHR$(ASC(B$)AND95):IF(B$>"@"ANDB$<"K")ORB$="P"ORB$="S"THEN580
572 GOTO570
580 FORK=1TON:IFB$=S$(K)THEN560
581 NEXTK:PRINTB$;:S$(J)=B$:NEXTJ
590 IFIP=1THENHN=1:GOTO620
591 FORJ=1TOIP-1:PRINT@X,N$(J)": ";:X=X+32
600 B$=INKEY$:IFB$=""THEN600
601 B$=CHR$(ASC(B$)AND95):IF(B$>"@"ANDB$<"K")ORB$="P"ORB$="S"THEN610
602 GOTO600
610 FORK=1TON:IFB$=S$(K)THEN600
611 NEXTK:PRINTB$;:S$(J)=B$:NEXTJ:HN=1
620 GOSUB1470:PRINT@128,"":PRINT@160,"":FORX=1TO6:FORL=1TO5
621 D(X)=RND(6):C1=INT((249+X*10)/2):K=D(X):GOSUB1466:NEXTL:IFD(X)>HNTHENHN=D(X)
622 NEXTX
630 FORJ=1TON:IFS$(J)="P"THENS$(J)="SORRY, NO PAIRS!":GOTO635
631 GOTO640
635 FORK=1TO5:IFD(K)=D(K+1)THENS$(J)="$200 FOR PAIR":M(J)=M(J)+200:K=5
636 NEXTK
640 IFS$(J)="S"THEN645
641 GOTO650
645 FORK=1TO4:IFD(K)=D(K+1)-1ANDD(K)=D(K+2)-2THENS$(J)="$450 FOR STRAIGHT":M(J)=M(J)+450:K=4
646 NEXTK
650 IFS$(J)="S"THENS$(J)="NO STRAIGHT!":GOTO655
651 GOTO660
655 FORK=1TO4:IFD(K)=D(K+1)+1ANDD(K)=D(K+2)+2THENS$(J)="$450 FOR STRAIGHT":M(J)=M(J)+450:K=4:REM D=4?
656 NEXTK
660 IF(S$(J)="G"AND(D(1)=HNORD(3)=HN))THEN669
661 IF(S$(J)="H"AND(D(3)=HNORD(5)=HN))THEN669
662 IF(S$(J)="I"AND(D(2)=HNORD(4)=HN))THEN669
663 IF(S$(J)="J"AND(D(4)=HNORD(6)=HN))THEN669
664 GOTO670
669 S$(J)="$150 FOR BET ON "+S$(J):M(J)=M(J)+150
670 IFS$(J)>"@"ANDS$(J)<"G"THENK=ASC(S$(J))-64:GOTO675
671 GOTO680
675 IFD(K)=HNTHENS$(J)="$300 ON SPOT "+S$(J):M(J)=M(J)+300:GOTO680
676 S$(J)="YOU LOSE WITH "+S$(J)
680 IFLEN(S$(J))=1THENS$(J)="MAYBE NEXT TIME!"
690 NEXTJ:C1=288:GOSUB705:X=320:FORJ=IPTON:PRINT@X,N$(J)": "S$(J):X=X+32:NEXTJ
691 IFIP=1THEN700
692 FORJ=1TOIP-1:PRINT@X,N$(J)": "S$(J):X=X+32:NEXTJ
700 GOSUB1470:RETURN

705 FORC2=C1TO480STEP32:PRINT@C2,"                               ";:NEXTC2:PRINT@C1,;:RETURN

710 CLS:PRINT@0,;:M$="lottery IF BOTH THE #S ON YOUR TICKET ARE AMONG THE SIX #S ROLLED, "
711 M$=M$+"YOU WILL RECEIVE $50 FOR THAT TICKET. HERE WE GO...":GOSUB1
712 FORJ=1TON:PRINT@288+J*32,N$(J)TAB(12)L$(J,1)TAB(19)L$(J,2)TAB(26)L$(J,3):NEXTJ:GOSUB1470
720 FORJ=1TO6:D(J)=0:NEXTJ:FORJ=1TO6:K=RND(6):X=313+J*10:GOSUB1465:D(K)=1:SOUND25*(J+1),1:NEXTJ
730 FORJ=1TON:FORK=1TOLEN(L$(J,0)):FORL=1TO300:NEXTL
731 IFD(VAL(LEFT$(L$(J,K),1)))=1ANDD(VAL(RIGHT$(L$(J,K),1)))=1THEN735
732 FORL=32TO64:SOUNDL,1:NEXT:PRINT@293+J*32+K*7,"---";:NEXTK,J:GOTO740
735 M(J)=M(J)+50:SOUND240,2:PRINT@293+J*32+K*7,"50$";:NEXTK,J:GOTO740
740 GOSUB1470:FORJ=1TON:L$(J,0)="":FORK=1TO3:L$(J,K)="":NEXTK,J:RETURN

750 CLS1:FORJ=1TO6:D(J)=10+J*64:PRINT@J*64,J;:PRINT@J*64+10,H$(3);:PRINT@J*64+42,I$(3);:NEXTJ
751 FORJ=1TON:PRINT@J*64,N$(J);:M(J)=M(J)-20:NEXTJ
752 PRINT@10,"HORSE RACES!";:FORY=4TO28STEP2:RESET(60,Y):NEXTY
760 PRINT@32,"EVERYONE HAS BET $20":PRINT@448,"THE WINNER WILL RECEIVE $100.":GOSUB1470
770 FORJ=1TO3:SOUND200,1:FORK=1TO20:NEXTK,J
780 FORI=1TO3:K=RND(6):D(K)=D(K)+1:C1=RND(2):?@D(K),H$(C1);:?@D(K)+32,I$(C1);:IF(D(K)/32-INT(D(K)/32))*32>=27THEN790
781 NEXT:GOTO770
790 I=3:NEXT:PRINT@32,"":PRINT@32,;:IFK<=NTHENM$=N$(K)+" WINS!":GOSUB4:M(K)=M(K)+100:GOSUB1470:RETURN
791 M$="HORSE #"+STR$(K)+" WINS!":GOSUB4:GOSUB1470:RETURN

810 M$="":CLS:PRINT@131,"THE FORTUNE TELLER SAYS:":K=1:M=10*RND(5)+50
820 IFRND(5)=5THENM$="HOLD A SWEEPSTAKES":K=2:GOTO890
830 IFRND(5)=5THENM$="HOLD A LOTTERY":K=3:GOTO890
840 IFRND(2)=2THENM$="COLLECT FROM ":K=-1:GOTO850
841 M$="PAY TO "
850 J=RND(N+1):IFJ<>IPTHEN860
851 M$=M$+"EVERYONE":M(IP)=M(IP)-K*N*M:FORJ=1TON:M(J)=M(J)+K*M:NEXTJ:GOTO880
860 IFJ=N+1THENM$=M$+"THE BANK":M(IP)=M(IP)-K*M:GOTO880
870 M$=M$+N$(J):M(IP)=M(IP)-K*M:M(J)=M(J)+K*M
880 C1=M:GOSUB3
890 C1=0:D(0)=50:D(1)=40:D(2)=20:D(3)=7:SOUND150,1
900 M$="                                "+N$(IP)+", "+M$+"                                "
901 FORJ=1TOLEN(M$)-31:PRINT@256,MID$(M$,J,31);:SOUNDD(C1AND3),1:C1=C1+1:FORTI=1TO55:NEXTTI,J:M$=""
910 PRINT@484,"HIT ANY KEY TO CONTINUE";:B$=INKEY$
920 IFINKEY$=""THEN920
930 IFK=2THEN460
931 IFK=3THEN710
932 RETURN

940 CLS:M$="high roller-- EVERYONE ANTES $20 AND ROLLS 2 DICE. HIGHEST TAKES THE $100 PRIZE.":GOSUB1
941 GOSUB1470:GOSUB1015
950 FORJ=1TON:M(J)=M(J)-20:X=64+J*128:PRINT@X/2,N$(J):X=X+22:GOSUB1450:D(J)=K:X=X+12:GOSUB1450
951 D(J)=D(J)+K:X=X+8:PRINT@X/2,"TOTAL:"D(J);:NEXTJ
960 HN=D(1):HM=1:FORJ=2TON:IFD(J)>HNTHENHN=D(J):HM=J
961 NEXTJ
970 K=0:FORJ=1TON:IFD(J)=HNTHENK=K+1
971 NEXTJ
980 IFK=1THENPRINT@448,;:M$=N$(HM)+" WINS THE POT!":GOSUB4:M(HM)=M(HM)+100:GOSUB1470:RETURN
990 PRINT@448,;:M$="TIE! HIGH ROLLERS ROLL AGAIN!":GOSUB4
991 GOSUB1470:GOSUB1015:X=192
1000 FORJ=1TON:IFD(J)<HNTHEND(J)=0:NEXTJ:GOTO960
1010 X=64+J*128:PRINT@X/2,N$(J):PRINT@X/2+32,"":X=X+22:GOSUB1450:D(J)=K:X=X+12:GOSUB1450
1011 D(J)=D(J)+K:X=X+8:PRINT@X/2,"TOTAL:"D(J);:NEXTJ
1012 GOTO960

1015 PRINT@484,"HIT A KEY TO ROLL DICE";:RETURN

1020 CLS:M$="poker party-- EACH PLAYER PAYS $20 AND ROLLS THREE DICE. "
1021 M$=M$+"THE BEST POKER HAND (3 OF A KIND > STRAIGHT > PAIR) WINS $100.":GOSUB1
1022 GOSUB1470:GOSUB1015:XX=256:FORI=1TON:M(I)=M(I)-20:NEXTI:HM=0
1030 FORI=1TON:PRINT@XX/2,N$(I):FORM=1TO3:J=I:X=XX+14+8*M:B$=INKEY$:GOSUB1450:D(M)=K:NEXTM:P(I)=0
REM 1 TO I IN ABOVE
1040 IFD(1)=D(2)ANDD(1)=D(3)THENPRINT@(XX+46)/2,"3 OF KIND";:P(I)=30+D(1):GOTO1100
1050 IFD(1)=D(2)THENP(I)=10+D(1):GOTO1060
1051 IFD(1)=D(3)THENP(I)=10+D(1):GOTO1060
1052 IFD(2)=D(3)THENP(I)=10+D(2)
1060 IFP(I)>0THENPRINT@(XX+46)/2,"PAIR";:GOTO1100
1070 FORJ=1TO3:IF(D(1)=D(2)+1ANDD(1)=D(3)+2ORD(1)=D(2)-1ANDD(1)=D(3)-2)THENP(I)=20:GOTO1072
1071 HN=D(1):D(1)=D(2):D(2)=D(3):D(3)=HN
1072 NEXTJ
1080 HN=D(1):FORJ=2TO3:IFD(J)>HNTHENHN=D(J)
1081 NEXTJ
1090 P(I)=P(I)+HN:IFP(I)>20THENPRINT@(XX+46)/2,"STRAIGHT";:GOTO1100
1091 PRINT@(XX+46)/2,"HIGH"STR$(P(I));
1100 XX=XX+128:IFP(I)>HMTHENHM=P(I)
1101 NEXTI
1110 K=0:FORI=1TON:IFP(I)=HMTHENK=K+1:J=I
1111 NEXTI
1120 IFK=1THENPRINT@448,;:M$=N$(J)+" WINS $100 POT":GOSUB4:M(J)=M(J)+100:GOSUB1470:RETURN
1121 PRINT@448,;:M$="TIE... PLAY ANOTHER HAND!":GOSUB4:GOSUB1470:C1=128:GOSUB705:XX=256:HM=0:GOSUB1015:GOTO1030

1130 CLS:PRINT@128,;:K=0:M=RND(5)*10+50
1140 J=RND(N):IFJ=IPTHEN1140
1150 M$="SHOW THAT YOU'RE A GOOD NEIGHBOR AND GIVE "+N$(J):C1=M:GOSUB3
1160 M(IP)=M(IP)-M:M(J)=M(J)+M:GOTO890

1170 CLS:M$="easy come easy go-- THE BANK WILL PAY YOU 10 TIMES THE ROLL OF 2 DICE.":GOSUB1
1172 GOSUB1470:GOSUB1015:J=IP:X=279:GOSUB1450:M=K:X=289:GOSUB1450:M=(M+K)*10:M(IP)=M(IP)+M:
1173 PRINT@235,;:M$="YOU WIN":C1=M:GOSUB3:PRINTM$
1180 GOSUB1470:RETURN

1190 CLS:M$="win a few-- THE BANK WILL PAY YOU TEN TIMES THE ROLL OF ONE DIE.":GOSUB1:GOSUB1470:GOSUB1015
1192 J=IP:X=284:GOSUB1450:M=K*10:M(IP)=M(IP)+M:PRINT@235,;:M$="YOU WIN":C1=M:GOSUB3:PRINTM$:GOSUB1470:RETURN

1200 CLS:M$="unlucky 7-- ROLL TWO DICE. IF THE TOTAL IS 7 YOU LOSE $100. ANY OTHER AND YOU WIN $100.":GOSUB1
1201 GOSUB1470:GOSUB1015:J=IP:X=279:GOSUB1450:M=K:X=289:GOSUB1450:M=M+K
1210 IFM=7THENPRINT@224,;:M$="YOU LOSE $100!":GOSUB4:M(IP)=M(IP)-100:GOTO1220
1211 PRINT@224,;:M$="YOU WIN $100!":GOSUB4:M(IP)=M(IP)+100
1220 GOSUB1470:RETURN

1230 CLS:M$="lose a few-- YOU MUST PAY THE BANK 10X THE ROLL OF 1 DIE.":GOSUB1
1231 GOSUB1470:GOSUB1015:J=IP:X=284:GOSUB1450:M=K*10:M(IP)=M(IP)-M
1232 PRINT@235,;:M$="YOU LOST":C1=M:GOSUB3:PRINTM$:GOSUB1470:RETURN

1240 CLS:M$="even steven-- YOU MAY BET UP TO $90 AND ROLL 2 DICE. IF THE TOTAL IS EVEN, "
1241 M$=M$+"YOU COLLECT 2X YOUR BET. HOW MUCH WILL YOU BET? ":GOSUB1:FORC1=0TO1:C1=-(INKEY$=""):NEXTC1
1242 IFIP=NTHENB$=STR$(RND(4)+5):GOTO1260
1250 B$=INKEY$:IFB$=""ORVAL(B$)<1THEN1250
1260 M=VAL(B$)*10:M$="":C1=M:GOSUB3:PRINT@132,M$:GOSUB1470:GOSUB1015
1270 X=406:J=IP:GOSUB1450:P=K:X=418:GOSUB1450:P=P+K
1280 IFINT(P/2)*2=PTHENPRINT@288,;:M$="YOU WIN":C1=2*M:GOSUB3:GOSUB4:M(IP)=M(IP)+2*M:GOTO1290
1281 PRINT@288,;:M$="YOU LOSE":C1=M:GOSUB3:GOSUB4:M(IP)=M(IP)-M
1290 GOSUB1470:RETURN

1300 CLS:M=10*RND(5)+10:M(IP)=M(IP)-M:M$="WHY DON'T YOU SWEETEN THE POT BY GIVING THE BANK":C1=M:GOSUB3:M$=M$+"?":K=0:GOTO890
1310 CLS:M=RND(5)*10+50:M$="YOU HIT THE JACKPOT! COLLECT":C1=M:GOSUB3:K=0:M(IP)=M(IP)+M:GOTO890
1320 CLS:M(IP)=M(IP)+100:M$="PLEASE ACCEPT THIS $100 BONUS!":K=0:GOTO890
1330 CLS:M$="YOU JUST LOST THIS TURN!":K=0:GOTO890
1340 CLS:M$="PAY $100 TAX TO THE BANK!":M(IP)=M(IP)-100:K=0:GOTO890
1350 CLS:M=RND(5)*10:M(IP)=M(IP)+M*N:FORJ=1TON:M(J)=M(J)-M:NEXTJ
1351 M$="WHAT A BONANZA! EVERYONE PAYS YOU":C1=M:GOSUB3:K=0:GOTO890
1360 CLS:M=RND(5)*10:M(IP)=M(IP)-M*N:FORJ=1TON:M(J)=M(J)+M:NEXTJ
1361 M$="THIS OUGHT TO MAKE YOU MAD. GIVE EVERYBODY":C1=M:GOSUB3:K=0:GOTO890

1370 CLS:PRINT"THIS IS THE GAME OF GAMBLER!":PRINT:PRINT"YOU AND THE TRS-80 ARE IN A CON-";
1371 PRINT"TEST TO SEE WHO WILL BUILD HIS  $100 BANKROLL INTO $1000 FIRST. MONEY IS MADE AND LOST THROUGH A";
1372 PRINT"SERIES OF GAMES OF CHANCE--":PRINT"FROM HORSE RACING AND DICE GAMES";
1380 PRINT"TO LOTTERIES AND SWEEPSTAKES! IFYOU SHOULD LOSE ALL YOUR MONEY"
1381 PRINT"YOUR IOU WILL BE ACCEPTED (AS":PRINT"LONG AS YOU PAY IT BACK WITH":PRINT"INTEREST). GOOD LUCK!":K=0:GOTO910

1450 B$=INKEY$:IFJ=NTHENFORL=1TORND(45):K=RND(6):GOSUB1465:NEXTL:RETURN
1460 K=RND(6):GOSUB1465:IFINKEY$=""THEN1460
1461 RETURN

1465 C1=INT(X/2)
1466 PRINT@C1,D$(K);:PRINT@C1+32,E$(K);:SOUND50+RND(99),1:RETURN
1470 M$=INKEY$:PRINT@484,"HIT ANY KEY TO CONTINUE";
1480 IFINKEY$<>""THENC1=480:GOSUB705:RETURN
1481 IFRND(9)=1THENSOUNDRND(99),1:GOTO1480
1482 GOTO1480
