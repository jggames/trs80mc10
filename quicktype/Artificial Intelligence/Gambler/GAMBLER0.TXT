10 CLS:PRINTCHR$(23)"Welcome to *GAMBLER*
(with sound effects)":PRINT
20 CLEAR1000:DEFINTA-Z:A1$=CHR$(191)+"  "+CHR$(149)+STRING$(17,32):A1$=A1$+A1$+A1$+CHR$(191):A2$=CHR$(188)+STRING$(2,140)+CHR$(156)+STRING$(17,140):A2$=A2$+A2$+A2$+CHR$(188):GOSUB420:GOSUB1390
30 PRINT"How many players (up to 4) ? ";
40 B$=INKEY$:IFB$=""THEN40 ELSEN=VAL(B$):IFN<1ORN>4THEN40ELSEPRINTN
50 FORI=1TON:PRINT"Who is player #";I;:INPUTN$(I):M(I)=100:IO(I)=0:L$(I,0)="":NEXTI:N$(I)="Trs-80":M(I)=100:IO=0:N=N+1:PRINT"

Do you need instructions?"
60 B$=INKEY$:IFB$=""THEN60ELSEIF(ASC(B$)AND95)=89THENGOSUB1370
70 FORIP=1TON
80 CLS:PRINTCHR$(23);"Present Bankrolls           IOUs";STRING$(32,45);:FORJ=1TON:PRINTUSINGN$(J)+" "+STRING$(15-LEN(N$(J)),46)+"$$###";M(J);:PRINT@122+J*64,IO(J);:NEXTJ:GOSUB200:PRINT"
It is ";N$(IP);"'s turn.":IFM(IP)>=0THEN100
90 IO(IP)=IO(IP)+1:M(IP)=M(IP)+100:IFM(IP)<0THEN90ELSEPRINT"You must borrow money to cont-
inue to play and pay it back to
the bank ... plus interest!":FORI=1TO2000:NEXTI:GOTO80
100 IFIO(IP)>0ANDM(IP)>=110THENPB=INT(M(IP)/110)ELSE130
110 IFPB>IO(IP)THENPB=IO(IP)
120 IO(IP)=IO(IP)-PB:M(IP)=M(IP)-110*PB:PRINT"You can pay back";PB;"IOU note
at 110 Dollars each.":FORTI=1TO2000:NEXTTI:GOTO80
130 IFL$(IP,0)="XXX"THEN160ELSEPRINT"Would you like to buy a lottery
ticket for $10 ?";
140 IFIP=NTHENPRINT"Y":GOSUB260:L$(IP,0)=L$(IP,0)+"X":L$(IP,LEN(L$(IP,0)))=T$:M(IP)=M(IP)-10:FORTI=1TO1000:NEXTTI:GOTO80
150 B$=INKEY$:IFB$=""THEN150ELSEB$=CHR$(ASC(B$)AND95):IFB$="N"THENPRINT"N":GOTO160ELSEIFB$<>"Y"THEN150ELSEPRINT"Y":GOSUB260:L$(IP,0)=L$(IP,0)+"X":L$(IP,LEN(L$(IP,0)))=T$:M(IP)=M(IP)-10:FORTI=1TO1000:NEXTTI:GOTO80
160 PRINT@962,"Touch any key to continue";
170 B$=INKEY$:IFB$=""THEN170ELSEGOSUB290
180 ONGNGOSUB1020,810,460,1130,1170,1190,460,1200,1230,1240,460,750,1300,940,1020,710,810,1310,1320,750,460,1330,710,810,1340,1350,1330,810,460,940,1310,710,750,1360,810,710
190 NEXTIP:GOTO70
200 HM=M(1):HN=1:FORJ=2TON:IFM(J)>HMTHENHM=M(J):HN=J:NEXTJELSENEXTJ
210 IFHM<1000THENRETURN
220 K=0:FORJ=1TON:IFM(J)=HMTHENK=K+1:NEXTJELSENEXTJ
230 IFK>1THENPRINT"Since there's a tie, keep going";:RETURN
240 PRINT:PRINT" ";N$(HN);" wins this game with
";HM;"dollars!":FORI=0TO90STEP2:Q=USR(11141-I):NEXTI:PRINTCHR$(23):PRINT"Would you like to play again ?"
250 B$=INKEY$:IFB$=""THEN250ELSEB$=CHR$(ASC(B$)AND95):IFB$="Y"THENRUNELSEEND
260 T$=CHR$(48+RND(5)):T1$=CHR$(48+RND(6)):IFT1$<T$THEN260ELSET$=T$+"-"+T1$:FORI=1TON:IFL$(I,0)=""THEN280
270 FORJ=1TOLEN(L$(I,0)):IFT$=L$(I,J)THEN260ELSENEXTJ
280 NEXTI:PRINT"You receive ticket number ";T$:FORJ=1 TO 20:OUT255,9:OUT255,8:NEXTJ:RETURN
290 RESTORE:CLS:PRINTA2$;:FORJ=1TO12:PRINTA1$;:NEXTJ:FORI=1TO3:PRINTCHR$(143)STRING$(2,140)CHR$(141)STRING$(17,140);:NEXT:PRINTCHR$(143)
300 FORX=68TO772STEP64:READA$:PRINT@X,A$;:READA$:PRINT@X+21,A$;:READA$:PRINT@X+42,A$;:NEXTX
310 X=65:B$=INKEY$:PRINT@970,N$(IP);", touch any key for your selection";
320 Y=X:PRINT@X,STRING$(2,143);:Q=USR(5000+RND(128)):X=RND(12)*64+RND(3)*21-20
330 IFIP=NTHENIFRND(30)=25THEN360ELSE350
340 B$=INKEY$:IFB$<>""THEN360
350 PRINT@Y,"  ";:GOTO360
360 JJ=100:KK=5160:FORI=1TO5+RND(4):Q=USR(KK):KK=KK+20:FORJ=1TOJJ:NEXTJ:JJ=JJ+100:PRINT@Y,"  ";:Y=X:PRINT@X,STRING$(2,143);:X=X+21:IFPOS(0)>40THENX=X+1
370 IFX>830THENX=65:NEXTIELSENEXTI
380 Q=USR(KK):FORTI=1TO300:NEXTTI:GN=INT(Y/21)-2:PRINT@Y,"  ";:FORI=1TO5:Q=USR(0):PRINT@Y,STRING$(2,143);:FORTI=1TO99:NEXTTI:PRINT@Y,"  ";:NEXTI:RETURN
390 DATA Poker Party,F O R T U N E !,Sweepstakes!,Love thy Neighbor,Easy Come Easy Go,Win a Few,Sweepstakes!,Unlucky Seven,Lose a Few
400 DATA Even Steven,Sweepstakes!,Daily Double,Pot Luck,High Roller,Poker Party,Lottery,F O R T U N E !,Jackpot,$100 Bonus,Off to the Races,Sweepstakes!,Lose This Turn
410 DATA Lottery,F O R T U N E !,Tax Time,Bonanza,Lose This Turn,F O R T U N E !,Sweepstakes!,High Roller,Jackpot,Lottery,Horse Race,Mad Money,F O R T U N E !,Lottery
420 R$=CHR$(26)+STRING$(7,24):D$(1)=STRING$(3,191)+CHR$(143)+STRING$(3,191)+R$+STRING$(7,143):D$(2)=CHR$(191)+CHR$(179)+STRING$(5,191)+R$+STRING$(5,143)+CHR$(140)+CHR$(143)
430 D$(3)=CHR$(191)+CHR$(179)+CHR$(191)+CHR$(143)+STRING$(3,191)+R$+STRING$(5,143)+CHR$(140)+CHR$(143):D$(4)=CHR$(191)+CHR$(179)+STRING$(3,191)+CHR$(179)+CHR$(191)+R$+CHR$(143)+CHR$(140)+STRING$(3,143)+CHR$(140)+CHR$(143)
440 D$(5)=CHR$(191)+CHR$(179)+CHR$(191)+CHR$(143)+CHR$(191)+CHR$(179)+CHR$(191)+R$+CHR$(143)+CHR$(140)+STRING$(3,143)+CHR$(140)+CHR$(143):D$(6)=CHR$(191)+CHR$(179)+CHR$(191)+CHR$(179)+CHR$(191)+CHR$(179)+CHR$(191)+R$+CHR$(143)+CHR$(140)+CHR$(143)
450 D$(6)=D$(6)+CHR$(140)+CHR$(143)+CHR$(140)+CHR$(143):RETURN
460 CLS:PRINT@19,"S W E E P S T A K E S !":PRINT@70,CHR$(176)STRING$(8,140)" G "STRING$(8,140)CHR$(176)STRING$(8,140)" H "STRING$(8,140)CHR$(176):PRINT@134,"A":PRINT@154,"C":PRINT@174,"E":PRINT@198,CHR$(143)STRING$(19,32)CHR$(143)STRING$(19,32)CHR$(143)
470 PRINT@400,CHR$(191)STRING$(19,32)CHR$(191)STRING$(19,32)CHR$(191):PRINT@464,"B":PRINT@484,"D":PRINT@504,"F":PRINT@528,CHR$(131)STRING$(8,140)" I "STRING$(8,140)CHR$(131)STRING$(8,140)" J "STRING$(8,140)CHR$(131):PRINT@448,"P=PAIR";
480 PRINT@512,"S=STRAIGHT":FORX=1TO6:PRINT@249+X*10,D$(X);:NEXTX
490 PRINT@650,"Do you need Sweepstakes instructions?":B$=INKEY$
500 B$=INKEY$:IF B$<>""THEN510ELSEIFRND(9)=1THENQ=USR(1E4+RND(99)):GOTO500ELSE500
510 B$=CHR$(ASC(B$)AND95):IFB$="N"THEN540ELSEPRINT@640,CHR$(31);"In Sweepstakes everyone antes $10.  Each player bets on the out-come of the roll of 6 dice.  There are three types of bets.":GOSUB1470
520 PRINT@640,CHR$(31);"One way is to bet on where the highest number rolled will appearIf the high number appears in A,B,C,D,E or F you win $300.  Let-ters G,H,I, and J cover two spots. If you bet G, you cover spotsA and C and can win $150."
530 GOSUB1470:PRINT@640,CHR$(31);"Another bet is <<P>> for pairs.  If two dice next to one anothermatch, you win $200.  The third bet if <<S>> for a straight.   If 3 consecutive dice appear in numerical order, you win $450.":GOSUB1470
540 PRINT@640,CHR$(31):PRINT@690,"A-F $300":PRINT@754,"G-J $150":PRINT@820,"P $200":PRINT@884,"S $450":FORI=1TON:M(I)=M(I)-10:S$(I)="":NEXTI:X=640:PRINT@980,"Place your bets.:";
550 FORJ=IPTON:PRINT@X,N$(J);" - ";:X=X+64
560 IFJ=NTHENB$=CHR$(64+RND(11)):IFB$="K"THENB$="P":GOTO580ELSE580
570 B$=INKEY$:IFB$=""THEN570ELSEB$=CHR$(ASC(B$)AND95):IF(B$>"@"ANDB$<"K")ORB$="P"ORB$="S"THEN580ELSE570
580 FORK=1TON:IFB$=S$(K)THEN560ELSENEXTK:PRINTB$;:S$(J)=B$:NEXTJ
590 IFIP=1THENHN=1:GOTO620ELSEFORJ=1TOIP-1:PRINT@X,N$(J);" - ";:X=X+64
600 B$=INKEY$:IFB$=""THEN600ELSEB$=CHR$(ASC(B$)AND95):IF(B$>"@"ANDB$<"K")ORB$="P"ORB$="S"THEN610ELSE600
610 FORK=1TON:IFB$=S$(K)THEN600ELSENEXTK:PRINTB$;:S$(J)=B$:NEXTJ:HN=1
620 GOSUB1470:PRINT@192,:PRINT@256,:FORX=1TO6:FORL=1TO5:Q=USR(5E3+RND(128)):D(X)=RND(6):PRINT@249+X*10,D$(D(X));:NEXTL:IFD(X)>HNTHENHN=D(X):NEXTXELSENEXTX
630 FORJ=1TON:IFS$(J)="P"THENS$(J)="Sorry, no pairs!":FORK=1TO5:IFD(K)=D(K+1)THENS$(J)=" $200 for a pair":M(J)=M(J)+200:K=5:NEXTKELSENEXTK
640 IFS$(J)="S"THENFORK=1TO4:IFD(K)=D(K+1)-1ANDD(K)=D(K+2)-2THENS$(J)=" $450 for the straight":M(J)=M(J)+450:K=4:NEXTKELSENEXTK
650 IFS$(J)="S"THENS$(J)="No straight!":FORK=1TO4:IFD(K)=D(K+1)+1ANDD(K)=D(K+2)+2THENS$(J)=" $450 for the straight":M(J)=M(J)+450:D=4:NEXTKELSENEXTK
660 IF(S$(J)="G"AND(D(1)=HNORD(3)=HN))OR(S$(J)="H"AND(D(3)=HNORD(5)=HN))OR(S$(J)="I"AND(D(2)=HNORD(4)=HN))OR(S$(J)="J"AND(D(4)=HNORD(6)=HN))THENS$(J)=" $150 for bet on "+S$(J):M(J)=M(J)+150
670 IFS$(J)>"@"ANDS$(J)<"G"THENK=ASC(S$(J))-64:IFD(K)=HNTHENS$(J)=" $300 on spot "+S$(J):M(J)=M(J)+300ELSES$(J)=" You lose with spot "+S$(J)
680 IFLEN(S$(J))=1THENS$(J)=" Maybe next time!"
690 NEXTJ:PRINT@576,CHR$(30):X=640:FORJ=IPTON:PRINT@X,N$(J);" - ";S$(J);CHR$(31):X=X+64:NEXTJ:IFIP=1THEN700ELSEFORJ=1TOIP-1:PRINT@X,N$(J);" - ";S$(J);:X=X+64:NEXTJ
700 GOSUB1470:RETURN
710 CLS:PRINT@24,"L O T T E R Y
If both the numbers on your ticket are among the six numbers    rolled, you will receive $50 for that ticket.  Here we go ...":FORJ=1TON:PRINT@576+J*64,N$(J)TAB(20)L$(J,1)TAB(30)L$(J,2)TAB(40)L$(J,3):NEXTJ:GOSUB1470
720 FORJ=1TO6:D(J)=0:NEXTJ:FORJ=1TO6:K=RND(6):PRINT@313+J*10,D$(K);:D(K)=1:Q=USR(1000*(J+1)):NEXTJ
730 FORJ=1TON:FORK=1TOLEN(L$(J,0)):FORL=1TO300:NEXTL:IFD(VAL(LEFT$(L$(J,K),1)))=1ANDD(VAL(RIGHT$(L$(J,K),1)))=1THENM(J)=M(J)+50:Q=USR(0):PRINT@586+J*64+K*10,"50$";:NEXTK,JELSEFORL=2032TO2080:Q=USR(L):NEXTL:PRINT@586+J*64+K*10,"---";:NEXTK,J
740 GOSUB1470:FORJ=1TON:L$(J,0)="":FORK=1TO3:L$(J,K)="":NEXTK,J:RETURN
750 CLS:H$=CHR$(32)+"-"+CHR$(156)+CHR$(140)+CHR$(150)+CHR$(129):FORJ=1TO6:D(J)=10+J*128:PRINT@J*128,J;TAB(10);H$;:NEXTJ:FORJ=1TON:PRINT@J*128,N$(J);:M(J)=M(J)-20:NEXTJ:PRINT@19,"H O R S E   R A C E S !";:FORY=7TO37STEP3:SET(120,Y):NEXTY
760 PRINT@900,"Everyone has bet $20, the winner will receive $100.":GOSUB1470
770 FORJ=1TO3:Q=USR(266):FORK=1TO20:NEXTK,J
780 FORI=1TO3:K=RND(6):D(K)=D(K)+1:PRINT@D(K),H$;:IFPOS(0)>61THENI=3:NEXTELSENEXT:GOTO770
790 PRINT@896,CHR$(31);:IFK<=NTHENPRINTTAB(23-LEN(N$(K))/2);N$(K);" ";:M(K)=M(K)+100ELSEPRINTTAB(16);"Horse Number";K;
800 PRINT"wins this race!":GOSUB1470:RETURN
810 M$="":CLS:PRINT@262,"T h e   F o r t u n e   T e l l e r   S a y s :":K=1:M=10*RND(5)+50
820 IFRND(5)=5THENM$="hold a sweepstakes.":K=2:GOTO890
830 IFRND(5)=5THENM$="hold a lottery.":K=3:GOTO890
840 IFRND(2)=2THENM$="collect from ":K=-1ELSEM$="pay to "
850 J=RND(N+1):IFJ<>IPTHEN860ELSEM$=M$+"everyone":M(IP)=M(IP)-K*N*M:FORJ=1TON:M(J)=M(J)+K*M:NEXTJ:GOTO880
860 IFJ=N+1THENM$=M$+"the bank":M(IP)=M(IP)-K*M:GOTO880
870 M$=M$+N$(J):M(IP)=M(IP)-K*M:M(J)=M(J)+K*M
880 M$=M$+STR$(M)+" dollars."
890 QQ=0:D(0)=5000:D(1)=5040:D(2)=5020:D(3)=5007:PRINTCHR$(23):X=USR(266)
900 M$=STRING$(32,32)+N$(IP)+", "+M$+STRING$(32,32):FORJ=1TOLEN(M$)-31:PRINT@512,MID$(M$,J,31);:Q=USR(D(QQAND3)):QQ=QQ+1:FORTI=1TO55:NEXTTI,J:M$=""
910 PRINT@966,"Touch any key to continue";:B$=INKEY$
920 IFINKEY$=""THEN920
930 IFK=2THEN460ELSEIFK=3THEN710ELSERETURN
940 CLS:PRINT" H I G H   R O L L E R -- Everyone antes $20 and rolls two dice. The highest total takes the $100 prize.  To roll dice, touch":PRINT" any key on your turn.":GOSUB1470:PRINT@192,CHR$(31)
950 FORJ=1TON:M(J)=M(J)-20:X=64+J*128:PRINT@X,N$(J):X=X+10:GOSUB1450:D(J)=K:X=X+15:GOSUB1450:D(J)=D(J)+K:X=X-25:PRINT@X+35,"Your total is";D(J);:NEXTJ
960 HN=D(1):HM=1:FORJ=2TON:IFD(J)>HNTHENHN=D(J):HM=J:NEXTJELSENEXTJ
970 K=0:FORJ=1TON:IFD(J)=HNTHENK=K+1:NEXTJELSENEXTJ
980 IFK=1THENPRINT@896,TAB(19+LEN(N$(HM))/2);N$(HM);" wins the pot!":M(HM)=M(HM)+100:B$=INKEY$:GOSUB1470:RETURN
990 PRINT@900,"We've got  tie!  Those high rollers will roll again!":B$=INKEY$:GOSUB1470:PRINT@192,CHR$(31);:X=192
1000 FORJ=1TON:IFD(J)<HNTHEND(J)=0:NEXTJELSEPRINT@X,N$(J):X=X+10:GOSUB1450:D(J)=K:X=X+15:GOSUB1450:D(J)=D(J)+K:X=X-25:PRINT@X+35,"Your totl is";D(J);:X=X+128:NEXTJ
1010 GOTO960
1020 CLS:PRINT"P O K E R   P A R T Y --  Each player pays $20 and rolls three dice.  The best poker hand (three of a kind > straight > pair) wins $100.   Touch any key to roll dice.":GOSUB1470:PRINT@192,CHR$(31):XX=256:FORI=1TON:M(I)=M(I)-20:NEXTI:HM=0
1030 FORI=1TON:PRINT@XX,N$(I):FORM=1TO3:J=1:X=XX+10*M:B$=INKEY$:GOSUB1450:D(M)=K:NEXTM:P(I)=0
1040 IFD(1)=D(2)ANDD(1)=D(3)THENPRINT@XX+40,"Three of a kind!";:P(I)=30+D(1):GOTO1100
1050 IFD(1)=D(2)THENP(I)=10+D(1)ELSEIFD(1)=D(3)THENP(I)=10+D(1)ELSEIFD(2)=D(3)THENP(I)=10+D(2)
1060 IFP(I)>0THENPRINT@XX+40,"A pair!";:GOTO1100
1070 FORJ=1TO3:IF(D(1)=D(2)+1ANDD(1)=D(3)+2ORD(1)=D(2)-1ANDD(1)=D(3)-2)THENP(I)=20:NEXTJELSEHN=D(1):D(1)=D(2):D(2)=D(3):D(3)=HN:NEXTJ
1080 HN=D(1):FORJ=2TO3:IFD(J)>HNTHENHN=D(J):NEXTJELSENEXTJ
1090 P(I)=P(I)+HN:IFP(I)>20THENPRINT@XX+40,"A straight!";ELSEPRINT@XX+40,"Highest roll is a";P(I);
1100 XX=XX+128:IFP(I)>HMTHENHM=P(I):NEXTIELSENEXTI
1110 K=0:FORI=1TON:IFP(I)=HMTHENK=K+1:J=I:NEXTIELSENEXTI
1120 IFK=1THENPRINT@914,N$(J);" wins the pot of $100":M(J)=M(J)+100:GOSUB1470:RETURNELSEPRINT@905,"We have a tie ... so let's all play another hand!":GOSUB1470:PRINT@196,CHR$(31):XX=256:HM=0:GOTO1030
1130 CLS:PRINT@256,CHR$(23):K=0:M=RND(5)*10+50
1140 J=RND(N):IFJ=IPTHEN1140
1150 M$="show that you're a good neighbor and give "+N$(J)+STR$(M)+" dollars."
1160 M(IP)=M(IP)-M:M(J)=M(J)+M:GOTO890
1170 CLS:PRINT"E A S Y   C O M E   E A S Y   G O  --  The bank will pay you 10 times roll of 2 dice.  Touch any key to roll dice," ;N$(IP);".":GOSUB1470:J=IP:X=279:GOSUB1450:M=K:X=289:GOSUB1450:M=(M+K)*10:M(IP)=M(IP)+M:PRINT@470,"You win"M"dollars."
1180 GOSUB1470:RETURN
1190 CLS:PRINT"W I N   A   F E W  -- The bank will pay you ten times
the roll of one die.  Touch any key to roll die, ";N$(IP);".":GOSUB1470:J=IP:X=284:GOSUB1450:M=K*10:M(IP)=M(IP)+M:PRINT@470,"You win";M;"dollars.":GOSUB1470:RETURN
1200 CLS:PRINT"U N L U C K Y   S E V E N  --  Roll two dice.  If the total is
seven you lose $100.  For any other total, you win $100.
Touch any key to roll dice, ";N$(IP);".":GOSUB1470:J=IP:X=279:GOSUB1450:M=K:X=289:GOSUB1450:M=M+K
1210 IFM=7THENPRINT@470,"You lose 100 dollars!":M(IP)=M(IP)-100ELSEPRINT@470,"You win 100 dollars!":M(IP)=M(IP)+100
1220 GOSUB1470:RETURN
1230 CLS:PRINT"L O S E   A   F E W  --  You must pay the bank ten times the    roll of one die.   Touch any key to roll die, ";N$(IP);".":GOSUB1470:J=IP:X=284:GOSUB1450:M=K*10:M(IP)=M(IP)-M:PRINT@470,"You lost";M;"dollars.":GOSUB1470:RETURN
1240 CLS:PRINT"E V E N   S T E V E N  --  You may bet up to $90 and roll two   dice.  If the total is even, you collect twice you bet.  Touch any key to roll dice.
";N$(IP);", how much will you bet ? ";:IFIP=NTHENB$=STR$(RND(4)+5):GOTO1260
1250 B$=INKEY$:IFB$=""ORVAL(B$)<1THEN1250
1260 M=VAL(B$)*10:PRINTM;"dollars":GOSUB1470
1270 X=407:J=IP:GOSUB1450:P=K:X=417:GOSUB1450:P=P+K
1280 IFINT(P/2)*2=PTHENPRINT@598,"You win";2*M;"dollars.":M(IP)=M(IP)+2*MELSEPRINT@598,"You lose";M;"dollars":M(IP)=M(IP)-M
1290 GOSUB1470:RETURN
1300 CLS:PRINTCHR$(23):M=10*RND(5)+10:M(IP)=M(IP)-M:M$="why don't you sweeten the pot by giving the bank"+STR$(M)+"dollars.":K=0:GOTO890
1310 CLS:PRINTCHR$(23):M=RND(5)*10+50:M$="you hit the jackpot! Collect"+STR$(M)+"dollars.":K=0:M(IP)=M(IP)+M:GOTO890
1320 CLS:PRINTCHR$(23):M(IP)=M(IP)+100:M$="please accept this $100 bonus!":K=0:GOTO890
1330 CLS:PRINTCHR$(23):M$="you just lost this turn!":K=0:GOTO890
1340 CLS:PRINTCHR$(23):M$="pay 100$ tax to the bank!":M(IP)=M(IP)-100:K=0:GOTO890
1350 CLS:PRINTCHR$(23):M=RND(5)*10:M(IP)=M(IP)+M*N:FORJ=1TON:M(J)=M(J)-M:NEXTJ:M$="WHAT A BONANZA! EVERYONE PAYS YOU"+STR$(M)+"DOLLARS.":K=0:GOTO890
1360 CLS:PRINTCHR$(23):M=RND(5)*10:M(IP)=M(IP)-M*N:FORJ=1TON:M(J)=M(J)+M:NEXTJ:M$="this ought to make you mad.  Give everybody"+STR$(M)+" dollars.":K=0:GOTO890
1370 CLS:PRINTCHR$(23)"This is the game of GAMBLER!":PRINT:PRINT"You and the TRS-80 are in a con-test to see who will build his  $100 bankroll into $1000 first. Money is made and lost through aseries of games of chance --
from horse racing and dice games";
1380 PRINT"to lotteries and sweepstakes! Ifyou should lose all your money
your IOU will be accepted ( as
long as you pay it back with
interest ). Good luck!":K=0:GOTO910
1390 SOUND$="/Í
MD>iÓÿ- ý<iÓÿ- ý
î¯ÓÿÉ":'28 SLASHES
1400 I=VARPTR(SOUND$):J!=PEEK(I+1)+256*PEEK(I+2):J=J!+65535*(J!>32767)
1410 FORK=1TO36:READA$:NEXTK:FORK=JTOJ+26:READX:POKEK,X:NEXTK
1420 IFPEEK(16396)=201THENPOKE16526,PEEK(I+1):POKE16527,PEEK(I+2)ELSECMD"T":DEFUSR0=J:POKE14308,0
1430 RETURN
1440 DATA 205,127,10,77,68,62,1,105,211,255,45,32,253,60,105,211,255,45,32,253,13,16,238,175,211,255,201
1450 B$=INKEY$:IFJ=NTHENFORL=1TORND(50):K=RND(6):PRINT@X,D$(K);:Q=USR(1E4+RND(99)):NEXTL:RETURN
1460 K=RND(6):PRINT@X,D$(K);:Q=USR(1E4+RND(99)):IFINKEY$=""THEN1460ELSERETURN
1470 PRINT@978,"Touch any key to continue";
1480 IFINKEY$<>""THENPRINT@978,CHR$(30);:RETURNELSEIFRND(9)=1THENQ=USR(1E4+RND(99)):GOTO1480ELSE1480

