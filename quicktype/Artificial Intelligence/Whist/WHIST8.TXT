0 CLEAR500:DIMC1,C2,C3,M$,MC,Z$,V(21),I,J,K,N,I$,J$,H$(2),II$,JJ$,H$,R,S,C$,SU$(4),R$,Z$,N(2),M,NT,T(2):MC=16384
1 C2=1:FORC1=1TO63STEP3:V(C2)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):C2=C2+1
2 NEXT:SU$(1)="¯":SU$(2)="¿":SU$(3)="ß":SU$(4)="ï":GOTO20
4 FORC1=1TOLEN(M$)STEP2:SOUNDV(ASC(MID$(M$,C1,1))-64),ASC(MID$(M$,C1+1,1))-48:NEXT:RETURN
5 C1=VARPTR(M$):C2=C2+PEEK(C1+2)*256+PEEK(C1+3)-1:POKEC2,ASC(Z$):POKEC2+1,ASC(MID$(Z$,2)):RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-MC+C2,:RETURN
8 C2=.:C1=LEN(I$):FORC3=1TOLEN(M$):IFMID$(M$,C3,C1)=I$THENC2=C3:C3=255
9 NEXT:RETURN

11 IFI$=":"THENJ$="T":RETURN
12 IFI$=";"THENJ$="J":RETURN
13 IFI$="<"THENJ$="Q":RETURN
14 IFI$="="THENJ$="K":RETURN
15 IFI$=">"THENJ$="A":RETURN
16 J$=I$:RETURN

20 CLS0:PRINT@137,;:M$="DEVIL'S WHIST":GOSUB7:PRINT@201,"BOB DELBOURGO";:PRINT@361,"DIRECTIONS Y/N";
21 I$=INKEY$:IFI$="Y"THEN25
22 IFI$="N"THEN100
23 GOTO21
25 CLS0:PRINT@10,;:M$="DEVIL'S WHIST":GOSUB7:PRINT@64,"WHIST IS THE POOR MAN'S VERSION OF CONTRACT BRIDGE. DEVIL"
26 PRINT"WHIST IS AN ABSORBING VARIATION OF IT.":PRINT"YOU PLAY AGAINST MC10 AND AT THE";
27 PRINT"START OF THE GAME CAN CHOOSE HOWMANY ROUNDS TO PLAY. THE NUMBER OF ROUNDS EQUALS THE NUMBER OF"
28 PRINT"CARDS DEALT AT THE BEGINNING.":PRINT"CARDS COME IN FOUR SUITS: ";:FORC1=1TO4:PRINTSU$(C1);:NEXT:PRINT
29 PRINT"AND THE TRUMP SUIT IS CHOSEN AT RANDOM BY THE COMPUTER AT THE   START OF EVERY ROUND.":GOSUB1800
30 CLS:PRINT"YOU MUST BID FOR THE NUMBER OF  TRICKS THAT YOU HOPE TO GET AT  THE START OF EACH ROUND. MC10"
31 PRINT"LIKEWISE. IF YOU ACHIEVE YOUR   BID, YOU RECEIVE YOUR BID # PLUSA BONUS OF 10 POINTS. THE SAME"
32 PRINT"APPLIES TO MC10.":PRINT:PRINT"IF YOU UNDERACHIEVE,YOU SCORE 0.IF YOU OVERACHIEVE YOU SCORE THE";
33 PRINT"NUMBER OF TRICKS YOU'VE MADE.":PRINT"THE COMPUTER DOES NOT KNOW YOUR HAND AND PLAYS HONESTLY. YOU TOO";
34 PRINT"MUST FOLLOW A SUIT IF YOU HAVE ACARD IN A MATCHING SUIT!!!":GOSUB1800
35 CLS:PRINT"IN THE NEXT ROUND, ONE LESS CARDIS DEALT TO YOU AND MC10. THUS  BIDDING GETS PROGRESSIVELY MORE TRICKY!! ";
36 PRINT"IN THE FINAL ROUND IT'SANYONE'S GUESS WHO WILL WIN.":PRINT:M$="IMPORTANT POINT:":GOSUB7:PRINT" WHEN BIDDING"
37 PRINT"THE TOTAL NUMBER OF TRICKS BID  BY YOU AND MC10 CANNOT EQUAL    THE TOTAL NUMBER OF CARDS DEALT.";
38 PRINT"THIS IS TO ENSURE THAT ONE OF   YOU WILL BE DISAPPOINTED!!!":PRINT"THE PLAYER TO BID FIRST IS THE"
39 PRINT"PLAYER WHO WON THE LAST TRICK OFTHE LAST ROUND.";:M$="GOOD LUCK...":GOSUB7:GOSUB1800
100 CLS:TW$="C1G1E1C1G1E1C1E1C1G1E1C1G1E1":TL$="B1B1B1A1A1A1B1B1B1A1A1A1":TB$="C1J1Q1Q1J1C1":C$=""
101 FORI=2TO14:FORJ=1TO4:C$=C$+CHR$(48+I)+SU$(J):NEXT:NEXT
103 TT=0
105 RD=0:SC(1)=0:SC(2)=0:CLS:PRINT@10,;:M$="DEVIL'S WHIST":GOSUB7:PRINT:PRINT
106 INPUT"HOW MANY ROUNDS (8 MIN. 16 MAX.)TO PLAY";NN:NN=INT(NN):IFNN<8ORNN>16THENSOUND1,1:GOTO105
110 N=NN:PRINT"SHUFFLING AND DEALING THE CARDS.PLEASE WAIT...":GOSUB1000:GOSUB1050:IFTTTHEN120
115 TT=1:INPUT"DO YOU WISH TO START? (Y/N)";Q$:IFQ$="Y"THENSS=1:GOTO120
116 SS=2
120 RD=RD+1:T$=MID$(H$,2,1):H$="":N(1)=0:N(2)=0:PRINT"SORTING THE HANDS.":PRINT"PLEASE WAIT...":GOSUB1100:GOSUB1200:GOSUB1250
125 S=SS:IFS=1THENPRINT@185," ";:PRINT@166,"YOUR TRICKS BID ";:GOTO127
126 GOTO130
127 INPUTT(1):PRINT@186,"ßÞÔßßß";:PRINT@294,"THINKING...         ";:GOSUB1150:IFT(1)+T(2)<>NTHEN130
128 IFT(2)=0THENT(2)=1:GOTO130
129 T(2)=T(2)-1
130 IFS=1THENM$=TB$:GOSUB4:PRINT@294,"MC10 TRICKS BID = "MID$(STR$(T(2))+" ",2,2);
135 IFS=2THENPRINT@294,"THINKING...         ";:GOSUB1150:PRINT@294,"MC10 TRICKS BID = "MID$(STR$(T(2))+" ",2,2);
140 IFS=2THEN142
141 GOTO150
142 PRINT@185," ";:PRINT@166,"YOUR TRICKS BID ";:INPUTT(1):PRINT@186,"ßÞÔßßß";
143 IFT(1)+T(2)=NTHENPRINT@166,"NUMBER NOT ALLOWED. ":PRINT@186,"ßÞÔßßß";:SOUND1,10:GOTO140
150 PRINT@481,;:M$="PICK CARD VIA A OR S AND ENTER":GOSUB7:NC=1:IFS=2THEN300
160 PRINT@96+2*NC-1,CHR$(195);:FORDL=1TO25:NEXT:PRINT@96+2*NC-1,"€";:FORDL=1TO25:NEXT:I$=INKEY$:IFI$=""THEN160
165 IFI$=CHR$(13)THEN200
170 IFI$="S"ORI$="D"THENNC=NC+1:IFNC>N THENNC=N:SOUND1,5
175 IFI$="A"THENNC=NC-1:IFNC=0THENNC=1:SOUND1,5
180 GOTO160
200 PRINT@96+2*NC-1,CHR$(195);:CC$(1)=MID$(H$(1),2*NC-1,2)
201 M$=H$(1):I$=KK$:GOSUB8:QQ=C2:M$=CC$(1):I$=KK$:GOSUB8
202 IFQQTHENIFS=2THENIFC2=.THENSOUND10,10:GOTO160
205 IFN(2)<>T(2)ANDS=2THENGOSUB2000
210 IFN(2)<>T(2)ANDS=1THENGOSUB2200
215 IFN(2)=T(2)ANDS=2THENGOSUB2100
220 IFN(2)=T(2)ANDS=1THENGOSUB2300
225 IFS=2THEN235
226 GOSUB1700
230 PRINT@416+2*N-1,J$;:PRINT@448+2*N-1,RIGHT$(CC$(2),1);:M$="E1D1C1F1E1D1C1F1E1D1C1F1":GOSUB4
235 GOSUB1500:PRINT@344,MID$(STR$(N(2))+" ",2,2);:PRINT@216,MID$(STR$(N(1))+" ",2,2);
236 H$(1)=LEFT$(H$(1),2*NC-2)+MID$(H$(1),2*NC+1):H$(2)=LEFT$(H$(2),2*K-2)+MID$(H$(2),2*K+1)
237 IFS=2THENM$=TL$:GOSUB4:GOTO239
238 M$=TW$:GOSUB4
239 N=N-1:IFN=0THEN500
240 FORDL=1TO100:NEXT:PRINT@358,"ANY KEY - NEXT TRICK";:FORDL=1TO150:NEXT:PRINT@358,"€€€€€€€€€€€€€€€€€€€€";:IFINKEY$=""THEN240
250 GOSUB1250:NC=1:IFS=2THEN300
251 GOTO160
300 IFN(2)<>T(2)THENGOSUB2000:GOTO305
301 GOSUB2100
305 GOSUB1700
310 PRINT@416+2*N-1,J$;:PRINT@448+2*N-1,RIGHT$(CC$(2),1);:M$="B1D1C1F1B1D1C1F1B1D1C1F1":GOSUB4:GOTO160
500 GOSUB2500:NN=NN-1:N=NN:IFN=0THEN600
505 CLS:M$="END OF ROUND"+STR$(RD):GOSUB7:PRINT:PRINT:SS=S
510 PRINT"YOUR SCORE ="SC(1):PRINT"MC10 SCORE ="SC(2):PRINT
515 GOTO110

600 CLS:PRINT@10,;:M$="DEVIL'S WHIST":GOSUB7:PRINT:PRINT:FORK=1TO5:M$=TW$:GOSUB4:M$=L$:NEXT:PRINT"  YOUR FINAL SCORE ="SC(1)
601 PRINT"  MC10 FINAL SCORE ="SC(2):PRINT
605 PRINT"  PRESS <ENTER> FOR A NEW GAME"
610 I$=INKEY$:IFI$=""THEN610
615 IFI$=CHR$(13)THENRUN
620 END

1000 FORI=1TO100
1005 R=RND(52):S=RND(52):IFR=S THEN1005
1010 R$=MID$(C$,2*R-1,2):S$=MID$(C$,2*S-1,2):M$=C$:C2=2*R-1:Z$=S$:GOSUB5:C2=2*S-1:Z$=R$:GOSUB5:C$=M$:NEXT:H$=C$:RETURN

1050 H$(1)=LEFT$(H$,2*N):H$(2)=MID$(H$,2*N+1,2*N):H$=MID$(H$,4*N+1):RETURN

1100 FORJ=1TON:FORI=1TON-1:FORK=1TO2:I$=MID$(H$(K),2*I,1):II$=MID$(H$(K),2*I+2,1)
1101 J$=MID$(H$(K),2*I-1,1):JJ$=MID$(H$(K),2*I+1,1)
1105 IFII$>I$OR(II$=I$ANDJJ$>J$)THENM$=H$(K):C2=2*I-1:Z$=JJ$+II$:GOSUB5:C2=2*I+1:Z$=J$+I$:GOSUB5:H$(K)=M$
1110 NEXT:NEXT:NEXT:RETURN

1150 GOSUB1180:T(2)=0:M=0:FORI=1TON:IFMID$(H$(2),2*I-1,1)>":"THENT(2)=T(2)+1:M=1
1152 IFI=1THEN1165
1155 IFMID$(H$(2),2*I,1)=MID$(H$(2),2*I-2,1)AND(M=1ORN=<8)ANDMID$(H$(2),2*I-1,1)>"7"ANDMID$(H$(2),2*I-1,1)<";"THENT(2)=T(2)+1
1160 IFMID$(H$(2),2*I-1,1)<"8"THENM=0
1165 IFNT>N/4+1ANDMID$(H$(2),2*I,1)=T$ANDMID$(H$(2),2*I-1,1)<"8"ANDMID$(H$(2),2*I-1,1)>"4"THENT(2)=T(2)+1
1170 NEXT:RETURN

1180 NT=0:FORJ=1TON:IFMID$(H$(2),2*J,1)=T$THENNT=NT+1
1185 NEXT:RETURN

1200 CLS0:PRINT@160,"¯¯¨ ­¯                    ßÞÔßßß¯¯£¡§¯¯                   ßÓÑ×ßß";
1201 PRINT@288,"¿¿¸¸½¿                    ïèàíïï¿¿»±¿¿                    ïëáïïï";
1202 PRINT@224,"ROUND :"RD:PRINT@256,"TRUMPS: "T$:PRINT@240,"YOUR TOTAL ="SC(1);:PRINT@272,"MC10 TOTAL ="SC(2);
1203 PRINT@326,"MC10 TRICKS WON = "MID$(STR$(N(1))+" ",2,2);:PRINT@198,"YOUR TRICKS WON = "MID$(STR$(N(2))+" ",2,2);:RETURN

1250 PRINT@32,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
1251 PRINT@416,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
1252 FORI=1TON:PRINT@64+2*I-1,MID$(H$(1),2*I,1);
1255 I$=MID$(H$(1),2*I-1,1):GOSUB11
1260 PRINT@32+2*I-1,J$;:NEXT
1265 FORI=1TON:PRINT@416+2*I-1,CHR$(255);:PRINT@448+2*I-1,CHR$(255);:NEXT:RETURN

1500 IFRIGHT$(CC$(2),1)=RIGHT$(CC$(1),1)ANDLEFT$(CC$(2),1)>LEFT$(CC$(1),1)THENN(2)=N(2)+1:S=2:RETURN
1505 IFRIGHT$(CC$(2),1)=RIGHT$(CC$(1),1)ANDLEFT$(CC$(2),1)<LEFT$(CC$(1),1)THENN(1)=N(1)+1:S=1:RETURN
1510 IFRIGHT$(CC$(2),1)=T$ANDRIGHT$(CC$(1),1)<>T$THENN(2)=N(2)+1:S=2:RETURN
1515 IFRIGHT$(CC$(1),1)=T$ANDRIGHT$(CC$(2),1)<>T$THENN(1)=N(1)+1:S=1:RETURN
1520 IFS=1ANDRIGHT$(CC$(1),1)<>RIGHT$(CC$(2),1)THENN(1)=N(1)+1:S=1:RETURN
1525 IFS=2ANDRIGHT$(CC$(1),1)<>RIGHT$(CC$(2),1)THENN(2)=N(2)+1:S=2:RETURN

1600 K$=MID$(H$(2),2*K-1,1):KK$=MID$(H$(2),2*K,1):RETURN

1700 I$=LEFT$(CC$(2),1):GOTO11:RETURN

1800 PRINT@486,;:C1=RND(.):M$="ANY KEY TO CONTINUE":GOSUB7
1805 ON-(INKEY$="")GOTO1805:RETURN

2000 K=0
2005 K=K+1:IFK>NTHENK=0:GOTO2020
2010 GOSUB1600:IFKK$=T$ANDK$>":"THENCC$(2)=K$+KK$:RETURN
2015 GOTO2005
2020 K=K+1:IFK>NTHENK=0:GOTO2035
2025 GOSUB1600:IFK$>":"THENCC$(2)=K$+KK$:RETURN
2030 GOTO2020
2035 K=K+1:IFK>NTHENK=0:GOTO2050
2040 GOSUB1600:IFKK$=T$ANDK$<";"ANDK$>"7"THENCC$(2)=K$+KK$:RETURN
2045 GOTO2035
2050 K=K+1:IFK>N THENK=N+1:GOTO2065
2055 GOSUB1600:IFK$<";"ANDK$>"7"THENCC$(2)=K$+KK$:RETURN
2060 GOTO2050
2065 K=K-1:IFK=0THENK=N+1:GOTO2080
2070 GOSUB1600:IFKK$<>T$ANDK$<"8"THENCC$(2)=K$+KK$:RETURN
2075 GOTO2065
2080 K=K-1:GOSUB1600:IFK$<"8"ORK=1THENCC$(2)=K$+KK$:RETURN
2085 GOTO2080
2100 K=N+1
2105 K=K-1:IFK=0THENK=N+1:GOTO2120
2110 GOSUB1600:IFKK$<>T$ANDK$<"8"THENCC$(2)=K$+KK$:RETURN
2115 GOTO2105
2120 K=K-1:IFK=0THENK=N+1:GOTO2135
2125 GOSUB1600:IFKK$=T$ANDK$<"8"THENCC$(2)=K$+KK$:RETURN
2130 GOTO2120
2135 K=K-1:IFK=0THENK=N+1:GOTO2150
2140 GOSUB1600:IFKK$<>T$ANDK$>"7"THENCC$(2)=K$+KK$:RETURN
2145 GOTO2135
2150 K=K-1:GOSUB1600:IFK$=T$ORK=1THENCC$(2)=K$+KK$:RETURN
2155 GOTO2150
2200 K=N+1
2205 K=K-1:IFK=0THENK=N+1:GOTO2220
2210 GOSUB1600:IFKK$=RIGHT$(CC$(1),1)ANDK$>LEFT$(CC$(1),1)THENCC$(2)=K$+KK$:RETURN
2220 K=K-1:IFK=0THENK=N+1:GOTO2235
2225 GOSUB1600:IFKK$=RIGHT$(CC$(1),1)THENCC$(2)=K$+KK$:RETURN
2230 GOTO2220
2235 K=K-1:IFK=0THENK=N+1:GOTO2250
2240 GOSUB1600:IFKK$=T$THENCC$(2)=K$+KK$:RETURN
2245 GOTO2235
2250 K=K-1:GOSUB1600:IFKK$<>T$THENCC$(2)=K$+KK$:RETURN
2255 GOTO2250
2300 K=N+1
2305 K=K-1:IFK=0THENK=N+1:GOTO2320
2310 GOSUB1600:IFKK$=RIGHT$(CC$(1),1)THENCC$(2)=K$+KK$:RETURN
2315 GOTO2305
2320 K=K-1:IFK=0THENK=N+1:GOTO2335
2325 GOSUB1600:IFKK$<>T$THENCC$(2)=K$+KK$:RETURN
2330 GOTO2320
2335 K=K-1:GOSUB1600:IFKK$=T$ORK=1THENCC$(2)=K$+KK$:RETURN
2340 GOTO2335
2500 FORK=1TO2:IFN(K)=T(K)THENSC(K)=SC(K)+T(K)+10
2505 IFN(K)>T(K)THENSC(K)=SC(K)+N(K)
2510 NEXT:RETURN
2520 REM MC-10 EDITS JIM GERRIE
