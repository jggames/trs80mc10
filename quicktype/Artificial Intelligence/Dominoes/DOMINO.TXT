10 REM "DOMINOES"
12 LET Z=0
14 LET P=0
20 GOSUB 9000
25 CLS
30 GOSUB 9500
40 GOSUB 8500
45 CLS
50 GOSUB 8000
55 CLS
100 FOR R=1 TO 7
110 LET P$(R)=A$(R)
112 LET P$(R+7,1)=P$(R,3)
114 LET P$(R+7,3)=P$(R,1)
116 LET P$(R+7,2)=P$(R,2)
120 LET Z$(R)=A$(R+7)
122 LET Z$(R+7,1)=Z$(R,3)
124 LET Z$(R+7,3)=Z$(R,1)
126 LET Z$(R+7,2)=Z$(R,2)
130 LET D=D+2
140 NEXT R
150 GOSUB 7500
160 IF G=0 THEN GOTO 2000
170 GOTO 1000
200 REM END ROUTINE
250 IF G=0 THEN PRINT AT 21,0;"I,VE USED ALL MY DOMINOES......."
260 IF G=1 THEN PRINT AT 21,0;"OH....YOU,RE OUT OF DOMINOES...."
350 FOR R=1 TO 125
400 NEXT R
900 PRINT AT 21,0;"DO YOU WANT TO PLAY ON?....(Y/N)"
910 INPUT C$
920 IF C$="Y" THEN GOTO 25
930 PRINT AT 21,0;" "
940 IF Z>P THEN PRINT AT 21,0;"I HAVE WON BY ";Z-P;" POINTS."
950 IF Z THEN PRINT AT 21,0;"YOU WIN BY ";P-Z;" POINTS."
960 IF Z=P THEN PRINT AT 21,0;"ITS A DRAW,";Z;" POINTS EACH."
999 STOP
1000 REM PLAYERS MOVE
1005 LET B=0
1006 LET C=0
1010 PRINT AT 21,0;"NEXT DOMINO ? (1 TO 7 OR 0=PASS)"
1020 INPUT N
1025 IF N=0 THEN GOTO 8350
1030 IF N<1 OR N>7 THEN GOTO 1020
1035 PRINT AT 21,0;" "
1040 IF T=0 THEN GOTO 4500
1050 IF ((B$(2,1)=B$(2,3*T)) AND (B$(2,1)<>" ")) OR ((B$(1,2)=B$(2,3*T)) AND (B$(1,2)<>" ")) OR ((B$(2,1)=B$(1,3*T-1)) AND (B$(2,1)<>" ")) THEN GOTO 3500
1060 IF ((P$(N,1)=B$(2,3*T)) AND (P$(N,3)=B$(2,1))) OR ((P$(N+7,1)=B$(2,3*T)) AND (P$(N+7,3)=B$(2,1))) OR ((P$(N,1)=B$(1,3*T-1)) AND (P$(N,3)=B$(1,2))) OR ((P$(N+7,1)=B$(1,3*T-1)) AND (P$(N+7,3)=B$(1,2))) OR ((P$(N,1)=B$(1,3*T-1)) AND (P$(N,3)=B$(2,1))) OR ((P$(N+7,1)=B$(1,3*T-1)) AND (P$(N+7,3)=B$(2,1))) OR ((P$(N,1)=B$(2,3*T)) AND (P$(N,3)=B$(1,2))) OR ((P$(N+7,1)=B$(2,3*T)) AND (P$(N+7,3)=B$(1,2))) THEN GOTO 3500
1070 IF (P$(N,3)=B$(2,1)) OR (P$(N,3)=B$(1,2)) THEN GOTO 3000
1080 IF (P$(N+7,3)=B$(2,1)) OR (P$(N+7,3)=B$(1,2)) THEN GOTO 3150
1090 IF (P$(N,1)=B$(2,3*T)) OR (P$(N+7,1)=B$(1,3*T-1)) THEN GOTO 3250
1100 IF (P$(N+7,1)=B$(2,3*T)) OR (P$(N+7,1)=B$(1,3*T-1)) THEN GOTO 3350
1101 LET P=P-(P>0)
1102 IF A=1 THEN GOTO 8350
1105 PRINT AT 21,0;"ILLEGAL....YOU FORFEIT YOUR TURN"
1110 GOTO 8370
2000 REM ZX81 MOVE
2005 LET C=0
2010 LET A=0
2020 LET N=0
2030 LET HS=0
2040 LET N1=0
2045 IF T=0 THEN GOTO 4000
2050 FOR R=1 TO 7
2060 IF ((Z$(R,1)=B$(2,3*T)) OR (Z$(R,1)=B$(1,3*T-1))) THEN GOSUB 2250
2070 IF ((Z$(R+7,1)=B$(2,3*T)) OR (Z$(R+7,1)=B$(1,3*T-1))) THEN GOSUB 2300
2080 IF ((Z$(R,3)=B$(2,1)) OR (Z$(R,3)=B$(1,2))) THEN GOSUB 2350
2090 IF ((Z$(R+7,3)=B$(2,1)) OR (Z$(R+7,3)=B$(1,2))) THEN GOSUB 2400
2100 NEXT R
2110 IF N=0 AND N1=0 THEN GOTO 8250
2112 IF N=0 THEN LET V=V1
2115 IF N=0 THEN LET N=N1
2118 GOTO V
2120 IF (Z$(N,3)=B$(2,1)) OR (Z$(N,3)=B$(1,2)) THEN GOTO 6000
2130 IF (Z$(N+3,3)=B$(2,1)) OR (Z$(N+7,3)=B$(1,2)) THEN GOTO 6150
2140 IF (Z$(N,1)=B$(2,3*T)) OR (Z$(N,1)=B$(1,3*T-1)) THEN GOTO 6250
2150 IF (Z$(N+7,1)=B$(2,3*T)) OR (Z$(N+7,1)=B$(1,3*T-1)) THEN GOTO 6350
2160 GOTO 8250
2250 LET S3=VAL Z$(R,3)+VAL Z$(R,3)*(Z$(R,1)=Z$(R,3))
2255 LET V1=2140
2260 IF B$(2,1)=" " THEN LET S1=(VAL B$(1,2)*2+S3)/L
2270 IF B$(2,1)<>" " THEN LET S1=(VAL B$(2,1)+S3)/L
2280 GOTO 2900
2300 LET S3=VAL Z$(R+7,3)+VAL Z$(R+7,3)*(Z$(R+7,1)=Z$(R+7,3))
2305 LET V1=2150
2310 GOTO 2260
2350 LET S3=VAL Z$(R,1)+VAL Z$(R,1)*(Z$(R,3)=Z$(R,1))
2355 LET V1=2120
2360 IF B$(2,3*T)=" " THEN LET S1=(VAL B$(1,3*T-1)*2+S3)/L
2370 IF B$(2,3*T)<>" " THEN LET S1=(VAL B$(2,3*T)+S3)/L
2380 GOTO 2900
2400 LET S3=VAL Z$(R+7,1)+VAL Z$(R+7,1)*(Z$(R+7,3)=Z$(R+7,1))
2405 LET V1=2130
2410 GOTO 2360
2900 LET S2=S1*L/K
2910 LET S=S1*(S1=INT (S1))+S2*(S2=INT (S2))
2920 IF S>HS THEN LET N=R
2925 IF S>HS THEN LET V=V1
2930 IF S>HS THEN LET HS=S
2940 LET N1=R
2950 RETURN
3000 REM SET UP NEW B$
3010 FOR R=1 TO 3
3020 LET B$(R,4 TO 3*(T+1))=B$(R,1 TO 3*T)
3030 LET B$(R,1 TO 3)=" "
3040 NEXT R
3050 IF P$(N,1)<>P$(N,3) THEN LET B$(2,1 TO 3)=P$(N)
3060 IF P$(N,1)=P$(N,3) THEN GOSUB 3100
3070 GOTO 6400
3100 FOR R=1 TO 3
3110 LET B$(R,2)=P$(N,R)
3120 NEXT R
3130 RETURN
3150 LET N=N+7
3160 GOTO 3000
3250 IF P$(N,1)<>P$(N,3) THEN LET B$(2,3*T+1 TO 3*T+3)=P$(N)
3252 LET C=1
3260 IF P$(N,1)=P$(N,3) THEN GOSUB 3300
3270 GOTO 6400
3300 FOR R=1 TO 3
3310 LET B$(R,3*(T+1)-1)=P$(N,R)
3320 NEXT R
3330 RETURN
3350 LET N=N+7
3360 GOTO 3250
3500 REM CHOICE OF ENDS
3510 PRINT AT 21,0;"WHICH END? (BOTTOM LEFT? Y/N)"
3520 INPUT C$
3525 PRINT AT 21,0;" "
3530 IF C$="Y" THEN GOTO 1070
3540 IF C$="N" THEN GOTO 1090
3550 GOTO 3500
4000 REM ZX81 FIRST DOMINO ROUTINE
4010 FOR R=1 TO 7
4020 LET S1=VAL Z$(R,1)
4030 LET S2=VAL Z$(R,3)
4040 LET S3=S1+S2
4050 LET S4=S3/L
4060 LET S5=S3/K
4070 LET S=S4*(S4=INT (S4))+S5*(S5=INT (S5))
4080 IF S>HS THEN LET N=R
4090 IF S>HS THEN LET HS=S
4100 NEXT R
4110 IF N=0 THEN LET N=1
4120 IF Z$(N,1)<>Z$(N,3) THEN LET B$(2,1 TO 3)=Z$(N)
4130 IF Z$(N,1)=Z$(N,3) THEN GOSUB 4150
4140 GOTO 6400
4150 FOR R=1 TO 3
4160 LET B$(R,2)=Z$(N,R)
4170 NEXT R
4180 RETURN
4500 REM PLAYER 1ST DOMINO ROUTINE
4510 IF (P$(N,1)<>P$(N,3)) THEN LET B$(2,1 TO 3)=P$(N)
4520 IF (P$(N,1)=P$(N,3)) THEN GOSUB 4950
4540 GOTO 6400
4950 REM ENTER DOUBLES INTO B$
4960 FOR R=1 TO 3
4970 LET B$(R,T+2)=P$(N,R)
4980 NEXT R
4990 RETURN
5000 REM PRINT STARTING DOMINO
5002 FOR R=1 TO 3
5004 IF C=0 THEN PRINT AT 16+R,13;B$(R,1 TO 3)
5006 NEXT R
5008 REM TAKE NEW DOMINO ROUTINE
5010 LET D=D+1
5020 LET N=N-7*(N>7)
5030 IF G=1 THEN GOTO 5100
5040 LET Z$(N)=" "
5050 LET Z$(N+7)=" "
5060 IF D THEN GOSUB 5400
5070 GOTO 5500
5100 LET P$(N)=" "
5110 LET P$(N+7)=" "
5130 IF D THEN GOSUB 5300
5140 GOTO 5600
5300 LET P$(N)=A$(D)
5310 FOR R=1 TO 3
5320 LET P$(N+7,R)=A$(D,4-R)
5330 NEXT R
5340 PRINT AT 12+N,4;P$(N)
5350 RETURN
5400 LET Z$(N)=A$(D)
5410 FOR R=1 TO 3
5420 LET Z$(N+7,R)=A$(D,4-R)
5430 NEXT R
5440 RETURN
5500 REM KEEP SCORE ROUTINE
5510 LET Z=Z+HS
5520 PRINT AT 8,4;Z
5530 GOTO 5750
5600 IF B$(2,1)=" " THEN LET S1=(1+(T<>1))*VAL B$(1,2)
5610 IF B$(1,2)=" " THEN LET S1=VAL B$(2,1)
5620 IF B$(2,3*T)=" " THEN LET S2=(1+(T<>1))*VAL B$(1,3*T-1)
5630 IF B$(1,3*T-1)=" " THEN LET S2=VAL B$(2,3*T)
5640 LET S3=S1+S2
5650 LET S4=S3/L
5660 LET S5=S3/K
5670 LET S=S4*(S4=INT (S4))+S5*(S5=INT (S5))
5680 LET P=P+S
5690 PRINT AT 4,4;P
5750 REM WHO GOES NEXT ROUTINE
5760 IF A+B=2 THEN GOTO 900
5765 IF G=0 THEN GOTO 5800
5770 FOR R=1 TO 7
5780 IF P$(R)<>" " THEN GOTO 5980
5790 NEXT R
5792 LET P=P+1
5793 PRINT AT 4,4;P
5795 GOTO 200
5800 FOR R=1 TO 7
5810 IF Z$(R)<>" " THEN GOTO 5980
5820 NEXT R
5824 LET Z=Z+1
5828 PRINT AT 8,4;Z
5830 GOTO 200
5980 LET G=(G=0)
5985 IF G=0 THEN PRINT AT 21,0;"MY TURN NOW....................."
5990 GOTO 1000*(2-G)
6010 FOR R=1 TO 3
6020 LET B$(R,4 TO 3*(T+1))=B$(R,1 TO 3*T)
6030 LET B$(R,1 TO 3)=" "
6040 NEXT R
6050 IF Z$(N,1)<>Z$(N,3) THEN LET B$(2,1 TO 3)=Z$(N)
6060 IF Z$(N,1)=Z$(N,3) THEN GOSUB 6100
6070 GOTO 6400
6100 FOR R=1 TO 3
6110 LET B$(R,2)=Z$(N,R)
6120 NEXT R
6130 RETURN
6150 LET N=N+7
6160 GOTO 6000
6250 IF Z$(N,1)<>Z$(N,3) THEN LET B$(2,3*T+1 TO 3*T+3)=Z$(N)
6252 LET C=1
6260 IF Z$(N,1)=Z$(N,3) THEN GOSUB 6300
6270 GOTO 6400
6300 FOR R=1 TO 3
6310 LET B$(R,3*(T+1)-1)=Z$(N,R)
6320 NEXT R
6330 RETURN
6350 LET N=N+7
6363 GOTO 6250
6400 REM SHOW DOMINOES ON TABLE
6405 LET T=T+1
6408 IF G=1 THEN PRINT AT 12+N-(7*(N>7)),4;" "
6410 IF C=1 THEN GOTO 6500+100*(2*INT (T/6)-(INT (T/6)=T/6))
6420 IF C=0 AND T>5 THEN PRINT AT 21,0;"I,M JUST MOVING THE PIECES ROUND"
6500 REM DOMINOES 1 TO 5
6510 FOR R=1 TO 3
6520 IF C=1 THEN PRINT AT 16+R,13;B$(R,1 TO 15)
6525 IF C=0 THEN PRINT AT 16+R,13;" ";B$(R,4 TO 15)
6530 NEXT R
6540 IF T<6 THEN GOTO 5000
6600 REM DOMINO NO.6
6610 FOR R=1 TO 3
6620 FOR I=1 TO 3
6630 PRINT AT 18-I,26+R;B$(R,15+I)
6640 NEXT I
6650 NEXT R
6660 IF T=6 THEN GOTO 5000
6700 REM DOMINOES 7 TO 11
6710 FOR R=1 TO 3
6720 FOR I=1 TO 15
6730 LET S$(R,16-I)=B$(R,18+I)
6740 NEXT I
6745 PRINT AT 12+R,13;S$(R)
6750 NEXT R
6760 IF T<12 THEN GOTO 5000
6800 REM DOMINO NO.12
6810 FOR R=1 TO 3
6820 FOR I=1 TO 3
6830 PRINT AT 14-I,10+R;B$(R,33+I)
6840 NEXT I
6850 NEXT R
6860 IF T=12 THEN GOTO 5000
6900 REM DOMINOES 13 TO 17
6910 FOR R=1 TO 3
6920 PRINT AT 8+R,13;B$(R,37 TO 51)
6930 NEXT R
6940 IF T<18 THEN GOTO 5000
7000 REM DOMINO NO.18
7015 FOR R=1 TO 3
7025 FOR I=1 TO 3
7035 PRINT AT 10-I,26+R;B$(R,51+I)
7045 NEXT I
7055 NEXT R
7065 IF T=18 THEN GOTO 5000
7100 REM DOMINOES 19 TO 23
7115 FOR R=1 TO 3
7125 FOR I=1 TO 15
7135 LET S$(R,16-I)=B$(R,54+I)
7145 NEXT I
7150 PRINT AT 4+R,13;S$(R)
7155 NEXT R
7165 IF T<24 THEN GOTO 5000
7200 REM DOMINO NO.24
7215 FOR R=1 TO 3
7225 FOR I=1 TO 3
7235 PRINT AT 6-I,10+R;B$(R,69+I)
7245 NEXT I
7255 NEXT R
7265 IF T=24 THEN GOTO 5000
7300 REM DOMINOES 25 TO 27
7315 FOR R=1 TO 3
7325 PRINT AT R,13;B$(R,73 TO )
7335 NEXT R
7340 GOTO 5000
7500 REM DRAW BOARD
7510 PRINT " SCORE "
7515 FOR R=1 TO 19
7520 PRINT " "
7530 NEXT R
7540 PRINT " "
7550 PRINT AT 11,0;" PLAYERS "
7560 PRINT AT 12,0;" PIECES "
7570 PRINT AT 2,23;"DOMINOES"
7580 PRINT AT 2,2;"PLAYER"
7585 PRINT AT 4,4;P
7590 PRINT AT 6,3;"ZX81"
7595 PRINT AT 8,4;Z
7600 FOR R=1 TO 7
7610 PRINT AT 12+R,2;R
7620 NEXT R
7630 FOR R=1 TO 7
7640 PRINT AT 12+R,4;P$(R)
7650 NEXT R
7990 RETURN
8000 REM WHO GOES FIRST ROUTINE
8010 PRINT "TO DECIDE WHO GOES FIRST I WILL THROW A DICE...THE HIGHEST TROW CAN START."
8015 FOR A=1 TO 25
8016 NEXT A
8020 PRINT ,,"PLAYERS DICE = ";
8030 LET D1=INT (RND*6)+1
8040 PRINT D1
8045 FOR A=1 TO 10
8046 NEXT A
8050 PRINT ,," ZX81 DICE = ";
8060 LET D2=INT (RND*6)+1
8070 IF D2=D1 THEN GOTO 8060
8080 PRINT D2
8085 FOR A=1 TO 10
8086 NEXT A
8090 IF D2=D1 THEN PRINT ,,"OK..YOU GO FIRST."
8100 IF D1>D2 THEN LET G=1
8110 IF D2>D1 THEN PRINT ,,"OK...I WILL START."
8120 PRINT ,,"PUSH ANY KEY TO CONTINUE......."
8130 IF INKEY$="" THEN GOTO 8130
8140 RETURN
8250 REM ZX81 CANT GO ROUTINE
8260 PRINT AT 21,0;"OH DEAR I CAN,T GO.............."
8270 LET A=1
8272 FOR R=1 TO 25
8274 NEXT R
8280 GOTO 5750
8350 REM PLAYER CANT GO ROUTINE
8360 PRINT AT 21,0;"OH...YOU CAN,T GO..............."
8370 LET B=1
8372 FOR R=1 TO 25
8374 NEXT R
8380 GOTO 5750
8500 REM SHUFFLE DOMINOES
8510 SLOW
8511 PRINT ,,"I,M JUST SHUFFLING THE DOMINOES."
8512 FOR A=1 TO 10
8513 NEXT A
8514 FAST
8520 RAND
8540 DIM A$(28,3)
8550 FOR R=1 TO 28
8560 LET N=INT (RND*28)+1
8570 IF D$(N*3)="7" THEN GOTO 8560
8580 LET A$(R)=D$((N*3-2) TO (N*3))
8590 LET D$(N*3)="7"
8595 NEXT R
8596 SLOW
8599 RETURN
9000 REM INSTRUCTIONS
9005 PRINT TAB 5;" DOMINOES DOOR L.MOLL."
9010 PRINT ,,"INSTRUCTIONS:-"
9015 PRINT ,,"DE 28 PIECES IN THE SET ARE:-"
9020 PRINT ,," 0:0 1:1 2:2 3:3 4:4 5:5 6:6"
9021 PRINT " 0:1 1:2 2:3 3:4 4:5 5:6"
9022 PRINT " 0:2 1:3 2:4 3:5 4:6"
9023 PRINT " 0:3 1:4 2:5 3:6"
9024 PRINT " 0:4 1:5 2:6"
9025 PRINT " 0:5 1:6"
9026 PRINT " 0:6"
9028 PRINT TAB 7;"THE DOMINOES ARE SHUFFLEDAND EACH PLAYER IS GIVEN SEVEN TO START THE GAME."
9030 PRINT ,," A NEW DOMINO IS TAKEN FROM THE UNUSED PACK AFTER EACH GO UNTIL ALL DOMINOES HAVE BEEN USED."
9040 PRINT ,,"PUSH ANY KEY TO CONTINUE........"
9042 IF INKEY$="" THEN GOTO 9042
9044 CLS
9046 PRINT TAB 5;"DOMINOES"
9048 PRINT ,,"YOU AND THE ZX81 LAY ALTERNATELY"
9050 PRINT ,," A DOMINO CAN ONLY BE USED IF ITHAS A NUMBER MATCHING AN END NO.ON THE TABLE. ";
9060 PRINT "DOUBLES (EG 3:3) ARE LAID CROSSWISE AND BOTH NOS.ARE USED IN SCORING."
9070 PRINT ,,"SCORING:-"
9080 PRINT ,,TAB 10;"ADD THE END NUMBERS ONTHE TABLE AND DIVIDE BY 5 OR 3 OR BOTH.";
9090 PRINT "(EG. 4:33:5 SCORES 3 PTS.)"
9095 PRINT ,,"YOU CAN SCORE USING 6 AND 4 IF YOU PREFER......"
9096 PRINT ,,"ENTER OPTION REQD:- ""A""= 5 AND 3"
9097 PRINT TAB 20;"""B""= 6 AND 4"
9100 INPUT C$
9102 IF C$="A" THEN LET K=5
9104 IF C$="A" THEN LET L=3
9106 IF C$="B" THEN LET K=6
9108 IF C$="B" THEN LET L=4
9110 IF C$<>"A" AND C$<>"B" THEN GOTO 9100
9115 CLS
9120 PRINT TAB 5;" DOMINOES DOOR L.MOLL."
9130 PRINT ,,"YOU HAVE CHOSEN THE ";K;"/";L;" METHOD OF SCORING."
9200 PRINT ,,"IF A PLAYER USES ALL DOMINOES INTHEIR HAND THEN THEY GAIN AN EXTRA POINT."
9205 PRINT ,,"IF YOU ENTER AN ILLEGAL MOVE YOULOSE A POINT."
9208 PRINT ,,"YOU CAN LEAVE 2 RANDOM DOMINOES OUT OF THE GAME IF YOU WISH.....PUSH ""Y"" IF YOU WANT THIS OPTION"
9210 INPUT C$
9212 IF C$="Y" THEN LET E=27
9214 IF C$<>"Y" THEN LET E=29
9215 CLS
9218 PRINT ,,"FINALLY, YOU MAY TERMINATE THE GAME AT THE END OF A ROUND OR CONTINUE THE SCORES INTO ANOTHERGAME."
9220 PRINT TAB 11;"GOOD LUCK"
9450 PRINT ,,"PUSH ANY KEY TO CONTINUE........"
9460 IF INKEY$="" THEN GOTO 9460
9490 RETURN
9500 REM SET UP VARIABLES
9505 PRINT "PLEASE WAIT......."
9506 FOR A=1 TO 15
9507 NEXT A
9508 FAST
9530 LET D$="0:00:10:20:30:40:50:61:11:21:31:41:51:62:22:32:42:52:63:33:43:53:64:44:54:65:55:66:6"
9540 LET G=0
9550 DIM Z$(14,3)
9560 DIM P$(14,3)
9570 DIM B$(3,81)
9580 LET R$=" "
9590 FOR R=1 TO 27
9600 LET R$=R$+" "
9610 NEXT R
9620 FOR R=1 TO 3
9630 LET B$(R)=R$
9640 NEXT R
9650 LET D=0
9660 LET T=0
9670 LET A=0
9680 LET B=0
9690 LET V=2120
9700 LET V1=0
9710 LET C=0
9720 DIM S$(3,15)
9980 RETURN
9990 REM MEMORY USED
9995 PRINT PEEK 16396+256*PEEK 16397-16509
9996 PRINT USR 8192;"SAVE:P:DOMINOES:"
9997 GOTO 1
9998 PRINT USR 8192;"SAVE:P:DOMINOES:"
9999 GOTO 1
