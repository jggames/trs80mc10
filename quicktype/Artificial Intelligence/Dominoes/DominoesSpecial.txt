1 CLS:PRINTCHR$(23);:PRINT@192,"   DOMINOES    VERSION  2.1
2 PRINT@830," Adapted by Geza A Dujmovich 1985";
3 CLEAR1000:RANDOM:GT=100:G=140:P=15360:DIMA$(28),D(20),P$(20),T$(20),P1$(20)
4 G$(1)="BLOCK GAME":G$(2)="DRAW GAME":G$(3)="BERGEN    ":X$=STRING$(3,131):X1$="   ":CH=-1:PB=7:TB=7:BN=14:DB=1
5 C=1:FORA=0TO6:FORB=0+ATO6:A$(C)=STR$(A)+STR$(B):C=C+1:NEXT:NEXT
6 IFGM=1THEN36ELSEGM=1
7 FORA=1TO16:D(A)=896+(A*3):NEXT
8 GOSUB154
9 PRINT@330,"DOMINOES";:PRINT@342,"DOMINOES";
10 PRINT@512,"CHOOSE GAME";:FORA=1TO3:PRINT@530+A*64,ACHR$(24)". "G$(A);:NEXT
11 A$=INKEY$:FORB=1TO125:NEXT:POKE16007,63:FORA=1TO125:NEXT:POKE16007,32:IFA$=""THEN11
12 D=VAL(A$):IFD<1ORD>3THEN11ELSEIFD=3THENGT=15ELSEIFD=1THENGT=50
13 IFD=2GS=1
14 PRINT@512,G$(D)"  ";:PRINT@595,"DO YOU WANT    ";:PRINT@659,"   RULES?   ";:PRINT@723,"   (Y/N)           ";:PRINT@787,"            ";
15 A$=INKEY$:IFA$=""THEN15ELSEIFA$="N"ORA$="n"THEN30ELSEIFA$="Y"OR A$="y"THEN16ELSE15
16 CLS:PRINT"DOMINOES ARE PLAYED WITH A DOUBLE-SIX PACK OF 28 TILES (CALLED":PRINT"BONES) WITH PIPS (OR SPOTS) RANGING FROM 0/0 TO 6/6. PLAYERS":PRINT"ARE DEALT 6 OR 7 BONES (DEPENDING ON GAME). THE REST OF THE"
17 PRINT"PACK IS CALLED THE BONEYARD (OR JUST YARD) AND IS SET ASIDE FOR":PRINT"LATER. THE PRIMARY OBJECT OF DOMINOES IS TO BE THE FIRST TO":PRINT"PLAY ALL HIS TILES AND GO OUT, OR HAVE THE LOWEST PIP COUNT"
18 PRINT"PLAY AND IT WILL BE PLACED FOR YOU. YOU NEED NOT PRESS ENTER.":PRINT"YOUR DOMINOES WILL BE DISPLAYED ALONG THE BOTTOM OF THE SCREEN.":PRINT:PRINT"STARTING (SETTING) AND SCORING DIFFERS WITH GAME PLAYED.":GOSUB156
19 CLS:PRINTG$(D):PRINT:ONDGOTO24,26,20
20 PRINT"PLAYERS ARE DEALT 6 BONES INSTEAD OF 7. GAME IS PLAYED TO 15":PRINT"POINTS. THE LOWEST DOUBLE MUST BE SET AND THE HOLDER SCORES 2":PRINT"POINTS. THE OBJECT OF THE GAME IS TO MATCH BOTH ENDS OF THE"
21 PRINT"CHAIN, MAKING BOTH ENDS ALIKE. HE SCORES 2 POINTS FOR A":PRINT"DOUBLE-HEADER. IF ONE END OF THE CHAIN HAS A DOUBLE OR IF THE":PRINT"PLAYER PLAYS A DOUBLE, HE SCORES 3 POINTS FOR A TRIPLE-HEADER."
22 PRINT"A SMALL 'D' INSIDE A CUBICLE ON THE DOMINO ON THE DISPLAY":PRINT"INDICATES A DOUBLE ON THAT END OF THE CHAIN. IT'S JUST A":PRINT"REMINDER. IF A PLAYER GOES OUT, HE GETS 1 ADDITIONAL POINT. ALL"
23 PRINT"BONES IN THE YARD ARE PLAYED. IF YOU CAN NOT PLAY YOU MUST":PRINT"DRAW ONE BONE AND ONLY ONE. IF YOU STILL CAN NOT PLAY, YOU MUST":PRINT"PASS. IF THE YARD IS USED UP, 2 CONSECUTIVE PASSES ENDS PLAY.":GOSUB156:GOSUB154:GOTO30
24 PRINT"PLAYER HAVING THE HIGHEST DOUBLE MUST START AND SET IT. THERE":PRINT"IS NO DRAW FROM THE YARD. PLAYERS PLAY ONLY WITH TILES DEALT.":PRINT"WHEN YOU CAN'T PLAY, YOU MUST PASS. TWO CONSECUTIVE PASSES"
25 PRINT"ENDS PLAY. GAME IS PLAYED TO 50 POINTS.":GOSUB28:GOSUB154:GOTO30
26 PRINT"PLAYERS DRAW BONES TO SEE WHO SETS FIRST, HIGHEST TILE (TOTAL":PRINT"PIPS) STARTING AND PLACING ANY HE OWNS. IF A PLAYER CAN NOT":PRINT"PLAY A TILE, HE MUST DRAW ONE OR MORE FROM THE YARD UNTIL HE"
27 PRINT"CAN PLAY AND PLAY IT. THE LAST 2 BONES IN THE YARD ARE FROZEN":PRINT"AND MAY NOT BE PICKED. A PLAYER MAY NOT PASS UNTIL THEN. TWO":PRINT"CONSECUTIVE PASSES ENDS PLAY. GAME IS PLAYED TO 100 POINTS."
28 PRINT:PRINT"SCORING: IF ONE PLAYER GOES OUT, HE SCORES THE SUM OF THE PIPS":PRINT"ON HIS OPPONENT'S UNPLAYED TILES. IF NO ONE GOES OUT, THE":PRINT"PLAYER WITH THE FEWEST REMAINING PIPS WINS AND SCORES THE"
29 PRINT"DIFFERENCE BETWEEN HIS AND HIS OPPONENT'S.":GOSUB156:IFD=1THENRETURNELSEGOSUB154
30 IFD<>2THEN36ELSEPRINT@512,"         ";:GOSUB35:PRINT@583,"WM'LL DRAW TO SEE WHO STARTS";:FORA=1TO2000:NEXT:CH=0
31 A=RND(28):B=RND(28):IFA=BTHEN31ELSEA1=VAL(MID$(A$(A),2,1))+VAL(RIGHT$(A$(A),1)):B1=VAL(MID$(A$(B),2,1))+VAL(RIGHT$(A$(B),1)):PRINT@583,"I DREW A"A$(A)", YOU DREW A"A$(B);
32 IFA1=B1PRINT@647,"WE BOTH HAVE"A1"PIPS";:PRINT@711,"WE'LL REDRAW";:FORA=1TO2000:NEXT:GOSUB35:FORA=1TO999:NEXT:GOTO31
33 IFA1>B1PRINT@647,"I HAVE"A1"AND YOU HAVE"B1"PIPS";:PRINT@711,"I START";:FORA=1TO3000:NEXT:GOSUB35:Z2=2:PL=1:GOTO36
34 IFB1>A1PRINT@647,"YOU HAVE"B1"AND I HAVE"A1"PIPS";:PRINT@711,"YOU START";:FORA=1TO3000:NEXT:Z2=1:PL=2:GOSUB35:GOTO36
35 PRINT@583,STRING$(30,32);:PRINT@647,STRING$(30,32);:PRINT@711,"                    ";:RETURN
36 IFD=3THENTB=6:PB=6:BN=16
37 '
38 ' ARRAYS: A$(X) = BONEYARD, P$(X) = PLAYER'S TILES, T$(X) = COMPUTER'S TILES, C(X) = STORES QUANTITY OF EACH SET OF PIPS COMPUTER CURRENTLY HOLDS, NEEDED FOR BEST PLAY OF COMPUTER
39 '
40 FORA=1TOPB
41 C=RND(28):IFA$(C)=""THEN41ELSEP$(A)=A$(C):A$(C)="":NEXT:FORA=1TOTB
42 C=RND(28):IFA$(C)=""THEN42ELSET$(A)=A$(C):A$(C)="":GOSUB155:NEXT:PRINT@43,G$(D);:PRINT@0,"SCORE:";:PRINT@14,"TRS-80:";:PRINT@27,"YOU:";:PRINT@172,"TILES:";:PRINT@243,"TRS-80:";:PRINT@307,"YOU:";:IFD>1PRINT@371,"YARD:";
43 PRINT@492,"LAST PLAYED:";:PRINT@563,"TRS-80:";:PRINT@627,"YOU:";:PRINT@748,"CHAIN LENGTH:";:PRINT@812,"GAME: "GT"PTS";
44 GOSUB126:FORA=1TOPB:PRINT@D(A),MID$(P$(A),2,1);:PRINT@D(A)+64,RIGHT$(P$(A),1);:NEXT:PRINT@951,"< BONES";:PRINT@330,"        ";:PRINT@342,"        ";:PRINT@512,STRING$(16,32);:PRINT@662,"       ";:PRINT@726,"     ";:ONDGOTO47,45,48
45 IFZ2=1PRINT@586,"YOUR TURN TO SET    ";:FORA=1TO1500:NEXT:PL=2:GOTO57
46 IFZ2=2PRINT@586,"MY TURN TO SET     ";:FORA=1TO1500:NEXT:PL=1:GOTO57
47 A3=7:PRINT@586,"HIGHEST DOUBLE STARTS";:GOTO49
48 A3=1:PRINT@586,"LOWEST DOUBLE STARTS";
49 A3=A3-1:FORA=1TOPB:A2$=STR$(ABS(A3)):A2$=MID$(A2$,2,1):IFMID$(P$(A),2,1)=A2$ANDRIGHT$(P$(A),1)=A2$H=ABS(A3):HM=1:GOTO51
50 IF((A3=0)*(D=1))+((A3=-6)*(D=3))THEN51ELSENEXTA:GOTO49
51 IFD=1THENA3=7ELSEIFD=3A3=1
52 A3=A3-1:FORW=1TOTB:A2$=STR$(ABS(A3)):A2$=MID$(A2$,2,1):IFMID$(T$(W),2,1)=A2$ANDRIGHT$(T$(W),1)=A2$T=ABS(A3):TR=1:GOTO54
53 IF((A3=0)*(D=1))+((A3=-6)*(D=3))THEN157ELSENEXT:GOTO52
54 GOSUB159:PRINT@586,"";:IFD=1IF(H>T)+(TR=0)THENL=H:PRINT"YOU'RE HIGH WITH "H"/"H;:PRINT@636,"SET";:PL=2:GOTO104ELSEL=T:PRINT"I HAVE A "T"/"T"       ";:PRINT@572,"SET";:PL=1:C(T)=C(T)-2:GOTO104
55 IFD=3IF((H>T)*(TR=1))+((HM=0)*(TR=1))THENL=T:PRINT"I HAVE A "T"/"T"      ";:TT=TT+2:C(T)=C(T)-2:GOSUB146:PRINT@572,"SET";:PL=1:GOTO104ELSEIFHM=1L=H:PRINT"YOU'RE LOW WITH A "H"/"H;:TP=TP+2:GOSUB146:PRINT@636,"SET";:PL=2:GOTO104
56 REM PRINT@128,"";:FORJ=1TOTB:PRINTT$(J)" ";:NEXT:PRINT"    ";'TO SEE COMPUTER PLAY ITS DOMINOES FACE UP, REMOVE REM
57 Q1=0:DB=0:GOSUB126:GOSUB143:IF((TB=0)+(PB=0))+((Q6=1)*(Q7=1))THEN130ELSEPRINT@586,STRING$(23,32);:IFPL=1THENPRINT@80,X$;:PRINT@91,X1$;:GOTO77ELSEPRINT@91,X$;:PRINT@80,X1$;:PRINT@704,"OPTIONS:";
58 PRINT@715,"PLAY (##)  OR";:PRINT@730,"PASS (P)       ";:IF(D>1)*((BN>2)+(D=3))*(BE=0)PRINT@730,"DRAW (D)       ";
59 Q6=0:PRINT@320,"PLAYED ";:PRINT@384,"? / ?";
60 A$=INKEY$:IFA$=""THEN60ELSEIFA$="P"THENIFD>1ANDBN>2ANDBE=0PRINT@586,"YOU MUST DRAW";:GOTO60ELSE73
61 IFA$="D"IF(BN=0)+(BE=1)PRINT@586,"SORRY, YOU CAN'T";:GOTO60
62 IFA$="D"IFD=1THENPRINT@586,"NOT IN THIS GAME";:GOTO60ELSEIF(D=2)*(BN<3)PRINT@586,"* YARD IS FROZEN *";:GOTO60ELSE75
63 IFASC(A$)<48ORASC(A$)>54THEN60
64 PRINT@384,A$;
65 B$=INKEY$:IFB$=""THEN65ELSEIFASC(B$)<48ORASC(B$)>54THEN65
66 C$=A$+B$:D$=RIGHT$(C$,1)+LEFT$(C$,1):FORA=1TOPB:Y$=MID$(P$(A),2,1):F$=RIGHT$(P$(A),1):E$=Y$+F$:IF(E$=C$)+(E$=D$)THEN67ELSENEXT:PRINT@586,"YOU DON'T HAVE THAT ONE";:GOTO59
67 PRINT@636,A$"-"B$" ";:PRINT@388,B$;:IF(RT=LT)+(GS=1)THEN69ELSEIF((VAL(A$)=LT)*(VAL(B$)=RT))+((VAL(A$)=RT)*(VAL(B$)=LT))THENPRINT@704,"     AT WHICH END, PLEASE, L OR R ?      ";:POKE15885,76:POKE15899,82ELSE69
68 Z$=INKEY$:IFZ$=""THEN68ELSEPOKE15885,32:POKE15899,32:IFZ$="L"ORZ$="l"THENL=RT:GOTO104ELSEIFZ$="R"ORZ$="r"THENL=LT:GOTO109ELSE68
69 PRINT@388,B$;:IFD=2IFPL=2IFZ2=1IFGS=1GS=0:L=VAL(A$):CH=-1:HA=1:Z2=2:GOTO104
70 IFVAL(A$)=LTTHENL=VAL(B$):GOTO104ELSEIFVAL(A$)=RTTHENL=VAL(B$):GOTO109
71 IFVAL(B$)=LTTHENL=VAL(A$):GOTO104ELSEIFVAL(B$)=RTTHENL=VAL(A$):GOTO109
72 PRINT@586,"YOU CAN'T MATCH IT       ";:GOTO59
73 Q6=1:PRINT@636,"PASS";:PL=1:BE=0:IFD=3IFBN>0THENQ6=0
74 GOTO57
75 BN=BN-1:PB=PB+1:PRINT@636,"DRAW";:IFD=3BE=1
76 C=RND(28):IFA$(C)=""THEN76ELSEP$(PB)=A$(C):A$(C)="":A=PB:PRINT@D(A),MID$(P$(A),2,1);:PRINT@D(A)+64,RIGHT$(P$(A),1);:GOTO57
77 Q7=0:PRINT@320,"      ";:PRINT@384,"     ";:PRINT@704,STRING$(35,32);:IFD=2IFGS=1IFZ2=2Z2=1:GS=0:HA=1:W=RND(6)+1:L=VAL(MID$(T$(W),2,1)):S1=VAL(RIGHT$(T$(W),1)):PL=1:GOSUB114:GOTO104
78 IF(D=3)*(DB=0)THEN100
79 FORW=1TOTB:S=VAL(RIGHT$(T$(W),1))
80 IFVAL(MID$(T$(W),2,1))=LTIFC(S)>1THENL=S:S1=LT:GOSUB114:GOTO104ELSEL=S:Q1=1:S1=LT:W1=W
81 NEXT
82 FORW=1TOTB
83 S=VAL(RIGHT$(T$(W),1))
84 IFVAL(MID$(T$(W),2,1))=RTIFC(S)>1THENL=S:S1=RT:GOSUB114:GOTO109ELSEL=S:Q1=2:S1=RT:W1=W
85 NEXT
86 FORW=1TOTB
87 S=VAL(MID$(T$(W),2,1))
88 IFVAL(RIGHT$(T$(W),1))=LTIFC(S)>1THENL=S:S1=LT:GOSUB114:GOTO104ELSEL=S:Q1=1:S1=LT:W1=W
89 NEXT
90 FORW=1TOTB
91 S=VAL(MID$(T$(W),2,1))
92 IFVAL(RIGHT$(T$(W),1))=RTIFC(S)>1THENL=S:S1=RT:GOSUB114:GOTO109ELSEL=S:Q1=2:S1=RT:W1=W
93 NEXT
94 IFQ1<>0THENW=W1:GOSUB114:IFQ1=1THEN104ELSE109
95 IF((BN<3)*(D<>3))+(D=1)+(BE=1)+(BN=0)THEN98ELSEPRINT@572,"DRAW";:PRINT@586,"I'LL DRAW FROM THE YARD";:BN=BN-1:TB=TB+1:IFD=3BE=1
96 C=RND(28):IFA$(C)=""THEN96ELSEFORA=1TOTB:IFT$(A)=""THENT$(A)=A$(C)ELSENEXT:T$(TB)=A$(C):A=TB
97 A$(C)="":GOSUB155:FORA=1TO2000:NEXT:GOTO56
98 Q7=1:PRINT@572,"PASS";:PRINT@586,"I'LL HAVE TO PASS";:BE=0:PL=2:FORA=1TO200:NEXT:IFD=3IFBN>0THENQ7=0
99 GOTO57
100 DM$=STR$(LT)+STR$(RT):MD$=STR$(RT)+STR$(LT):FORW=1TOTB:IFT$(W)=DM$THEN102ELSENEXT
101 FORW=1TOTB:IFT$(W)=MD$THEN103ELSENEXT:GOTO79
102 L=VAL(RIGHT$(DM$,1)):S1=LT:GOSUB114:GOTO104
103 L=VAL(MID$(MD$,2,1)):S1=LT:GOSUB114:GOTO104
104 BE=0:POKE15690,32:CH=CH+1:LT=L:PRINT@266,"          ";:POKE15694,32:PRINT@394,"          ";:IFL=1ORL=3ORL=5THENGOSUB118ELSEIFL<>0GOSUB119
105 GOSUB149:LD=0
106 IFHA=1HA=0:L=VAL(B$):S1=L:GOTO109
107 IFDB=1IFPL=1GOSUB115
108 IFDB=1THEN109ELSE111
109 BE=0:POKE15710,32:CH=CH+1:RT=L:PRINT@278,"          ";:POKE15706,32:PRINT@406,"          ";:IFL=1ORL=3ORL=5THENGOSUB122ELSEIFL<>0GOSUB123
110 GOSUB149:RD=0
111 GOSUB147:A4=A:IFPL=1THENPL=2:FORA1=1TO200:NEXT:TB=TB-1:GOTO56ELSEIFPL=2FORA2=1TO1:FORA1=1TO125:NEXT:PRINT@D(A4),MID$(P$(A4),2,1);:PRINT@D(A4)+64,RIGHT$(P$(A4),1);:FORA1=1TO125:NEXT:PRINT@D(A4)," ";:PRINT@D(A4)+64," ";:NEXT:P$(A4)=""
112 A3=0:FORA=1TOPB:IFP$(A)=""THENNEXT:PB=PB-1:PL=1:GOTO57ELSEA3=A3+1:P1$(A3)=P$(A):NEXT:P$(PB)=""
113 FORA=1TOPB:P$(A)=P1$(A):NEXT:FORA=1TOPB:PRINT@D(A),MID$(P$(A),2,1);:PRINT@D(A)+64,RIGHT$(P$(A),1);:NEXT:PRINT@D(A-1),"     ";:PRINT@D(A-1)+64,"     ";:PL=1:PB=PB-1:GOTO57
114 A$=MID$(T$(W),2,1):B$=RIGHT$(T$(W),1):PRINT@586,"I'LL PLAY THE "A$" / "B$;:PRINT@572,A$"-"B$" ";
115 T$(W)="":FORA=1TOTB:IFT$(A)=""THENFORA3=ATOTB:T$(A3)=T$(A3+1):NEXT:T$(A3+1)=""ELSENEXT
116 IFDB=0IFA$=B$THENC(L)=C(L)-2ELSEC(L)=C(L)-1:C(S1)=C(S1)-1
117 RETURN
118 POKE15694,G:IFL=1RETURN
119 POKE15626,G:POKE15762,G:IFL<4RETURN
120 POKE15634,G:POKE15754,G:IFL<>6RETURN
121 POKE15630,G:POKE15758,G:POKE15694,32:RETURN
122 POKE15706,G:IFL=1RETURN
123 POKE15638,G:POKE15774,G:IFL<4RETURN
124 POKE15646,G:POKE15766,G:IFL<>6RETURN
125 POKE15642,G:POKE15770,G:POKE15706,32:RETURN
126 PRINT@21,TT;:PRINT@31,TP;:PRINT@251,TB;:PRINT@315,PB;:PRINT@764,CH;:IFD>1PRINT@379,BN;
127 IFD=2IFBN<3POKE15738,42:POKE15742,42
128 IF(DB=1)+(GS=1)PRINT@764,ZZ;
129 RETURN
130 ST=0:SP=0:FORA=0TOTB:ST=ST+VAL(MID$(T$(A),2,1))+VAL(RIGHT$(T$(A),1)):NEXT:FORA=1TOPB:SP=SP+VAL(MID$(P$(A),2,1))+VAL(RIGHT$(P$(A),1)):NEXT
131 PRINT@80,X1$;:PRINT@91,X1$;:POKE15738,32:POKE15742,32:PRINT@586,"** HANDPLAY OVER **";:PRINT@704,STRING$(40,32);:PRINT@320,"      ";:PRINT@384,"     ";:FORA=1TO25:NEXT
132 IFD=3IFTB=0THENPRINT@586,"I GET 1 POINT FOR GOING OUT";:TT=TT+1ELSEIFPB=0THENPRINT@586,"YOU GET 1 POINT FOR GOING OUT";:TP=TP+1
133 IFD=3THEN139ELSEIFTB=0THENTT=TT+SP:GO=1ELSEIFPB=0THENTP=TP+ST:GO=2ELSEIFST>SPTHENTP=TP+(ST-SP):GO=3ELSEIFSP>STTHENTT=TT+(SP-ST):GO=4ELSEPRINT@580,"DRAWN HAND -- WE BOTH HAVE"SP"PIPS";:GOTO139
134 GOSUB126:PRINT@580,"";:ONGOGOTO135,136,137,138
135 PRINT"I WIN THE HAND   ";:GOTO139
136 PRINT"I GUESS YOU WIN THE HAND  ";:PRINT@644,"I HAVE"ST"PIPS";:GOTO139
137 PRINT"YOU HAVE"SP"PIPS AND I HAVE"ST"PIPS";:PRINT@644,"YOUR COUNT IS LOWER THAN MINE";:PRINT@708,"YOU WIN THE HAND";:GOTO139
138 PRINT"I HAVE"ST"PIPS AND YOU HAVE"SP"PIPS";:PRINT@644,"MY COUNT IS LOWER THAN YOURS";:PRINT@708,"I WIN THE HAND";
139 GOSUB126:FORA=1TO999:NEXT:GOSUB143:PRINT@320,"TO DEAL";:PRINT@384,"PRESS";:PRINT@448,"ENTER";:PRINT@636,"    ";:PRINT@572,"    ";:PRINT@266,"          ";:PRINT@330,"          ";:PRINT@394,"          ";:IFD=2GS=1
140 PRINT@278,"          ";:PRINT@342,"          ";:PRINT@406,"          ";
141 A$=INKEY$:IFA$=""THEN141ELSEIFASC(A$)=13THENST=0:FORA=896TO936:PRINT@A," ";:PRINT@A+64," ";:NEXT:SP=0:Q7=0:Q6=0:PRINT@580,STRING$(37,32);:PRINT@644,STRING$(35,32);:PRINT@708,STRING$(35,32);:PRINT@320,"       ";:PRINT@448,"     ";:PRINT@384,"     ";
142 IFASC(A$)<>13THEN141ELSEPRINT@590,"* SHUFFLING *";:FORA=1TO20:P$(A)="":T$(A)="":NEXT:FORA=0TO6:C(A)=0:NEXT:POKE15738,32:POKE15742,32:GOTO4
143 IFTT>=GTTHENH$="I":GOTO145ELSEIFTP>=GTTHENH$="YOU":GOTO145
144 RETURN
145 FORA=1TO7:FORB=1TO300:NEXT:PRINT@708,"                 ";:FORB=1TO300:NEXT:PRINT@708,H$" WIN THE MATCH";:NEXT:PRINT@256,"TO PLAY";:PRINT@320,"AGAIN ";:PRINT@384,"PRESS";:PRINT@448,"ENTER";:INPUTA$:RUN
146 FORK=1TO99:NEXT:PRINT@586,"2 POINT STARTER               ";:RETURN
147 IFD<>3THENRETURNELSEIFA$=B$IFLT=VAL(A$)THENLD=1:POKE15690,68ELSEIFRT=VAL(B$)RD=1:POKE15710,68
148 RETURN
149 IFD<>3ORDB=1THEN150ELSEIFRT=LTTHEN151
150 RETURN
151 IFPL=1IFLD=1ORRD=1ORA$=B$THENTT=TT+3:H$=" * * * TRIPLE":GOTO153ELSETT=TT+2:H$=" * * * DOUBLE":GOTO153
152 IFPL=2IFLD=1ORRD=1ORA$=B$THENTP=TP+3:H$=" * * * TRIPLE":GOTO153ELSETP=TP+2:H$=" * * * DOUBLE":GOTO153
153 PRINT@704,STRING$(35,32);:FORA5=1TO1:FORA6=1TO30:NEXT:PRINT@579,H$" HEADER * * *      ";:FORA6=1TO500:NEXT:PRINT@579,"                           ";:NEXT:RETURN
154 CLS:FORA=0TO768STEP64:POKEA+P+41,191:NEXT:FORA=0TO63:POKEA+P+832,131:NEXT:FORA=8TO32:POKEA+P+192,176:POKEA+P+448,131:NEXT:FORA=256TO384STEP64:POKEA+P+8,191:POKEA+P+20,191:POKEA+P+32,191:NEXT:RETURN
155 C(VAL(MID$(T$(A),2,1)))=C(VAL(MID$(T$(A),2,1)))+1:C(VAL(RIGHT$(T$(A),1)))=C(VAL(RIGHT$(T$(A),1)))+1:RETURN
156 PRINT@960,"* HIT ENTER * ";:INPUTA$:RETURN
157 IFD<>2IFTR=0ANDHM=0PRINT@586,"NO DOUBLE IN PLAY - REDEAL";:FORA=1TO2000:NEXT:GOSUB35:PRINT@896,STRING$(30,32);:PRINT@960,STRING$(30,32);:GOTO4
158 GOTO54
159 IFD=1IFH>TTHENIFMID$(P$(A),2,1)<>RIGHT$(P$(A),1)GOTO164
160 IFD=1IFT>HIFMID$(T$(W),2,1)<>RIGHT$(T$(W),1)THEN164
161 IFD=3IFH<TIFMID$(P$(A),2,1)<>RIGHT$(P$(A),1)GOTO164
162 IFD=3IFT<HIFMID$(T$(W),2,1)<>RIGHT$(T$(W),1)GOTO164
163 RETURN
164 PRINT@586,"COMPUTER ERROR -- REDEAL";:FORA=1TO2000:NEXT:GOSUB35:PRINT@896,STRING$(30,32);:PRINT@960,STRING$(30,32);:GOTO4  ' POSSIBLE ONLY ON SET PLAY AND IS RARE

