1 '*** From the Software Library of
2 '    The San Gabriel Valley TRS-80 Users Group.
3 '
4 'Downloaded from The Green Machine, Riverside, CA
5 '                   714-354-8004
6 '
10 CLEAR:DEFINTA-Z:I=1:DIMC(64):RR!=SGN(RND(0)-.5):ONERRORGOTO2500
20 CLS:PRINTCHR$(23):INPUT"Do you want instructions ";IN$:IFLEFT$(IN$,1)="Y"THENGOSUB2180
30 CL$="Y":'IF YOU DON'T HAVE THE CLOCK, CHANGE THIS TO 'CL$="N"'
40 CLS:PRINTCHR$(23):INPUT"Your name is ";B$:IFB$=""THENB$=" human "ELSEB$=LEFT$(B$,9)
50 CLS:PRINTCHR$(23)"Level of play";:INPUTB8!:IFB8!=0THENB8!=1:B7!=1+RND(0)/2ELSEB7!=(B8!+1)/2+RND(0)/2
60 CLS:PRINTCHR$(23);"Do you want white ";B$;" ";:INPUTC$:GOSUB750:IFLEFT$(C$,1)<>"N"THENGOSUB860:GOTO170
70 GOSUB850:IFCL$="Y"THENPRINT@306,"START ";RIGHT$(TIME$,8);:PRINT@370,STRING$(15,CHR$(128));
80 F!=-99:A0=0:FORJ=1TO8:FORK=1TO8:IFA(J,K)=99THENA6=J:A7=K
90 NEXT:NEXT
100 FORX=1TO8:FORY=1TO8:PRINT@114," X Y = ";CHR$(X+64);Y;:IFA(X,Y)<0THENGOSUB240:IFF!>B7!THENGOTO130
110 NEXT:NEXT:IFF!>=-9THENGOTO130ELSEGOSUB860
120 PRINT@242,"I CONCEDE    ";:GOTO2150
130 A(R,U)=A(E,Q):A(E,Q)=1:IFA(R,U)=-2ANDU=1THENA(R,U)=-9
140 X=R:Y=U:A0=4:GOSUB240:PRINT@242,"IS MY MOVE    ";:PRINT@178,CHR$(E+64);Q;"- ";CHR$(R+64);U;
150 IFCL$="Y"THENPRINT@370,"STOP ";RIGHT$(TIME$,8);
160 X=E:Y=Q:GOSUB870:X=R:Y=U:GOSUB870:IFC=1THENPRINT@242,"CHECK ";B$;:C=0
170 IFINKEY$=CHR$(17)THENX$="":GOTO170ELSEIFX$="S"THENGOSUB1790:PRINT@50,"SELF PLAYING";:GOSUB2400:GOTO80:ELSEPRINT@114,"MOVE ";B$;:GOSUB1530:D=0:B4=A:B9=B:B5=0:IFA(X,Y)=2ANDY=5ANDB=6ANDA(A,B)=1ANDABS(A-X)=1THENB5=1:GOTO200
180 IFX$="K"ORX$="Q"THENGOSUB860:PRINT@114,STRING$(14," ");:GOTO80
190 IFX$="S"THENCL$="N":GOTO170ELSEA0=3:GOSUB230:IFD=0THENPRINT@242,"ILLEGAL       ";:GOTO170
200 PRINT@114,"OK            ";:A=B4:B=B9:A(A,B)=A(X,Y):A(X,Y)=1:GOSUB870:X=A:Y=B:GOSUB870:IFN$="N"THENGOSUB860:N$=" "
210 IFA(A,B)=2ANDB=8THENPRINT@114,"WHAT PIECE";:GOSUB2060:X=A:Y=B:GOSUB870:PRINT@114,STRING$(14," ");:PRINT@178,STRING$(14," ");
220 IFB5=1THENA(A,B-1)=1:X=A:Y=B-1:GOSUB870:GOTO80:ELSEGOTO80
230 PRINT@62,"+ ";:ONA(X,Y)GOTO,440,0,340,290,0,250,0,290:GOTO420
240 ON(-A(X,Y))GOTO0,490,0,340,290,0,250,0,290:GOTO420
250 B=Y:FORA=X+1TO8:GOSUB600:IFS=0THENNEXT
260 FORA=X-1TO1STEP-1:GOSUB600:IFS=0THENNEXT
270 A=X:FORB=Y+1TO8:GOSUB600:IFS=0THENNEXT
280 FORB=Y-1TO1STEP-1:GOSUB600:IFS=0THENNEXT:RETURNELSERETURN
290 B=Y:FORA=X+1TO8:B=B+1:GOSUB600:IFS=0THENNEXT
300 B=Y:FORA=X-1TO1STEP-1:B=B-1:GOSUB600:IFS=0THENNEXT
310 B=Y:FORA=X-1TO1STEP-1:B=B+1:GOSUB600:IFS=0THENNEXT
320 B=Y:FORA=X+1TO8:B=B-1:GOSUB600:IFS=0THENNEXT
330 IFABS(A(X,Y))=9THENGOTO250ELSERETURN
340 A=X+2:B=Y+1:IFA<9ANDB<9THENGOSUB610
350 B=B-2:IFB>0ANDA<9THENGOSUB610
360 A=A-4:IFA>0ANDB>0THENGOSUB610
370 B=B+2:IFB<9ANDA>0THENGOSUB610
380 A=A+1:B=B+1:IFA>0ANDA<9ANDB<9THENGOSUB610
390 B=B-4:IFB>0ANDA>0ANDA<9GOSUB610
400 A=A+2:IFA>0ANDA<9ANDB>0GOSUB610
410 B=B+4:IFB<9ANDA>0ANDA<9GOSUB610:RETURNELSERETURN
420 FORA=X-1TOX+1:FORB=Y-1TOY+1:IFA(A,B)<>0THENGOSUB610
430 NEXT:NEXT:RETURN
440 A=X:IFY>2THENGOTO460
450 B=Y+1:IFA(A,B)=1THENGOSUB620:B=B+1:IFA(A,B)=1THENGOSUB620:GOTO470ELSEGOTO470ELSEGOTO470
460 B=Y+1:IFA(A,B)=1THENGOSUB620
470 A=X+1:B=Y+1:IFA(A,B)<0THENGOSUB620
480 A=A-2:IFA(A,B)<0THENGOSUB620:RETURNELSERETURN
490 A=X:IFY<7THENGOTO510
500 B=Y-1:IFA(A,B)=1THENGOSUB620:B=B-1:IFA(A,B)=1THENGOSUB620:GOTO520ELSEGOTO520ELSEGOTO520
510 B=Y-1:IFA(A,B)=1THENGOSUB620
520 A=X-1:B=Y-1:IFA(A,B)>1THENGOSUB620
530 A=A+2:IFA(A,B)>1THENGOSUB620:RETURNELSERETURN
540 T=A(A,B):IFT=-99THENB1=T:RETURN
550 A5=S:IFABS(T)<=A(X,Y)THENA(A,B)=A(X,Y):A(X,Y)=1:GOTO570
560 IFT<B1THENB1=T:S=A5:RETURNELSES=A5:RETURN
570 A1=X:A2=Y:A3=A:A4=B:A8=T:A0=2:FORX=1TO8:FORY=1TO8:IFA(X,Y)<0THENGOSUB240:IFT=0THENGOTO590
580 NEXT:NEXT
590 X=A1:Y=A2:A=A3:B=A4:A0=5:A(X,Y)=A(A,B):A(A,B)=A8:GOTO560
600 S=0:IFA(A,B)=1THENGOTO620ELSEIFA(A,B)=0THENS=1:RETURNELSEIFSGN(A(A,B))=SGN(A(X,Y))THENS=1:RETURN:ELSES=1:GOTO620
610 IFA(A,B)=1THENGOTO620ELSEIFSGN(A(A,B))=SGN(A(X,Y))THENRETURN
620 PRINT@62," +";:ONA0GOTO630,640,650,660,670:GOTO680
630 IFA6=AANDA7=BTHENB1=B1+1:RETURNELSERETURN
640 IFA3=AANDA4=BTHENT=0:RETURNELSERETURN
650 IFB4=AANDB9=BTHEND=1:RETURNELSERETURN
660 IFA6=AANDA7=BTHENC=1:RETURNELSERETURN
670 IFA(A,B)<0THENGOTO540ELSERETURN
680 B3=S:W=X:M=Y:N=A:H=B:P=A(A,B):A(A,B)=A(X,Y):A(X,Y)=1:B1=0
690 A0=5:FORX=1TO8:FORY=1TO8:IFA(X,Y)>1THENGOSUB230
700 NEXT:NEXT:X=N:Y=H:A0=1:GOSUB240:A0=0:S=B3:X=W:Y=M:A=N:B=H:A(X,Y)=A(A,B):A(A,B)=P
710 B6!=1/(ABS(4.5-A)+ABS(4.5-B)+1):IFA(X,Y)<-2ANDA(X,Y)>-9THENB6!=B6!+1/(ABS(A6-A)+ABS(A7-B)+5)+RND(0)/15
720 G!=P+B1+B6!:IFP=99THENGOSUB860:PRINT@242,"MATE ";B$;:GOTO2150
730 IFG!<=F!THENRETURN
740 F!=G!:E=X:Q=Y:R=A:U=B:RETURN
750 CLS
760 Y=0:FORJ=896TO0STEP-128:Y=Y+1:X=0:FORK=JTO42+JSTEP6:X=X+1:T(X,Y)=K:NEXT:NEXT
770 Y=0:FORJ=960TO64STEP-128:Y=Y+1:X=0:FORK=JTO42+JSTEP6:X=X+1:B(X,Y)=K:NEXT:NEXT
780 DATA-7,-4,-5,-9,-99,-5,-4,-7
790 DATA-2,-2,-2,-2,-2,-2,-2,-2
800 DATA1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
810 DATA2,2,2,2,2,2,2,2
820 DATA7,4,5,9,99,5,4,7
830 FORY=8TO1STEP-1:FORX=1TO7STEP2:S(X,Y)=I:S(X+1,Y)=-I:NEXT:I=-I:NEXT
840 FORY=8TO1STEP-1:FORX=1TO8:READA(X,Y):NEXT:NEXT:RETURN
850 I=-I:A(4,1)=99:A(5,1)=9:A(4,8)=-99:A(5,8)=-9
860 FORY=1TO8:FORX=1TO8:GOSUB870:NEXT:NEXT:RETURN
870 IFS(X,Y)>0THENGOTO890
880 ONABS(A(X,Y))GOSUB920,980,,1280,1380,,1480,,1080:IFABS(A(X,Y))=99THENGOSUB1180:RETURNELSERETURN
890 ONABS(A(X,Y))GOSUB900,930,,1230,1330,,1430,,1030:IFABS(A(X,Y))=99THENGOSUB1130:RETURNELSERETURN
900 IFS(X,Y)<0THENGOTO920
910 PRINT@T(X,Y),STRING$(6,CHR$(191));:PRINT@B(X,Y),STRING$(6,CHR$(191));:RETURN
920 PRINT@T(X,Y),STRING$(6," ");:PRINT@B(X,Y),STRING$(6," ");:RETURN
930 IFI*A(X,Y)<0GOTO960
940 PRINT@T(X,Y),STRING$(6,CHR$(191));
950 PRINT@B(X,Y),CHR$(191);CHR$(173);CHR$(187);CHR$(185);CHR$(175);CHR$(191);:RETURN
960 PRINT@T(X,Y),STRING$(6,CHR$(191));
970 PRINT@B(X,Y),CHR$(191);CHR$(159);CHR$(132);CHR$(142);CHR$(191);CHR$(191);:RETURN
980 IFI*A(X,Y)<0GOTO1010
990 PRINT@T(X,Y),STRING$(6,CHR$(128));
1000 PRINT@B(X,Y),CHR$(128);CHR$(160);CHR$(187);CHR$(177);CHR$(128);CHR$(128);:RETURN
1010 PRINT@T(X,Y),STRING$(6,CHR$(128));
1020 PRINT@B(X,Y),CHR$(128);CHR$(146);CHR$(132);CHR$(134);CHR$(144);CHR$(128);:RETURN
1030 IFI*A(X,Y)<0GOTO1060
1040 PRINT@T(X,Y),CHR$(191);CHR$(183);CHR$(175);CHR$(167);CHR$(191);CHR$(191);
1050 PRINT@B(X,Y),CHR$(191);CHR$(173);CHR$(187);CHR$(185);CHR$(175);CHR$(191);:RETURN
1060 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(147);CHR$(187);CHR$(191);CHR$(191);
1070 PRINT@B(X,Y),CHR$(191);CHR$(159);CHR$(132);CHR$(142);CHR$(191);CHR$(191);:RETURN
1080 IFI*A(X,Y)<0GOTO1110
1090 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(172);CHR$(132);CHR$(128);CHR$(128);1100 PRINT@B(X,Y),CHR$(128);CHR$(160);CHR$(187);CHR$(177);CHR$(128);CHR$(128);:RETURN
1110 PRINT@T(X,Y),CHR$(128);CHR$(136);CHR$(144);CHR$(152);CHR$(128);CHR$(128);
1120 PRINT@B(X,Y),CHR$(128);CHR$(146);CHR$(132);CHR$(134);CHR$(144);CHR$(128);:RETURN
1130 IFI*A(X,Y)<0GOTO1160
1140 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(153);CHR$(157);CHR$(187);CHR$(191);
1150 PRINT@B(X,Y),CHR$(191);CHR$(159);CHR$(186);CHR$(191);CHR$(154);CHR$(191);:RETURN
1160 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(183);CHR$(162);CHR$(191);CHR$(191);
1170 PRINT@B(X,Y),CHR$(191);CHR$(191);CHR$(133);CHR$(128);CHR$(175);CHR$(191);:RETURN
1180 IFI*A(X,Y)<0GOTO1210
1190 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(136);CHR$(157);CHR$(128);CHR$(128);
1200 PRINT@B(X,Y),CHR$(128);CHR$(128);CHR$(186);CHR$(191);CHR$(144);CHR$(128);:RETURN
1210 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(166);CHR$(162);CHR$(132);CHR$(128);
1220 PRINT@B(X,Y),CHR$(128);CHR$(160);CHR$(133);CHR$(128);CHR$(165);CHR$(128);:RETURN
1230 IFI*A(X,Y)<0GOTO1260
1240 PRINT@T(X,Y),CHR$(191);CHR$(159);CHR$(179);CHR$(179);CHR$(175);CHR$(191);
1250 PRINT@B(X,Y),CHR$(191);CHR$(157);CHR$(185);CHR$(191);CHR$(154);CHR$(191);:RETURN
1260 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(143);CHR$(143);CHR$(191);CHR$(191);
1270 PRINT@B(X,Y),CHR$(191);CHR$(191);CHR$(134);CHR$(128);CHR$(175);CHR$(191);:RETURN
1280 IFI*A(X,Y)<0GOTO1310
1290 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(176);CHR$(176);CHR$(128);CHR$(128);
1300 PRINT@B(X,Y),CHR$(128);CHR$(128);CHR$(185);CHR$(191);CHR$(144);CHR$(128);:RETURN
1310 PRINT@T(X,Y),CHR$(128);CHR$(160);CHR$(140);CHR$(140);CHR$(144);CHR$(128);
1320 PRINT@B(X,Y),CHR$(128);CHR$(162);CHR$(134);CHR$(128);CHR$(165);CHR$(128);:RETURN
1330 IFI*A(X,Y)<0GOTO1360
1340 PRINT@T(X,Y),CHR$(191);CHR$(159);CHR$(187);CHR$(155);CHR$(191);CHR$(191);
1350 PRINT@B(X,Y),CHR$(191);CHR$(173);CHR$(187);CHR$(185);CHR$(175);CHR$(191);:RETURN
1360 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(135);CHR$(175);CHR$(191);CHR$(191);
1370 PRINT@B(X,Y),CHR$(191);CHR$(159);CHR$(132);CHR$(142);CHR$(191);CHR$(191);:RETURN
1380 IFI*A(X,Y)<0GOTO1410
1390 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(184);CHR$(144);CHR$(128);CHR$(128);
1400 PRINT@B(X,Y),CHR$(128);CHR$(160);CHR$(187);CHR$(177);CHR$(128);CHR$(128);:RETURN
1410 PRINT@T(X,Y),CHR$(128);CHR$(160);CHR$(132);CHR$(164);CHR$(128);CHR$(128);
1420 PRINT@B(X,Y),CHR$(128);CHR$(146);CHR$(132);CHR$(134);CHR$(144);CHR$(128);:RETURN
1430 IFI*A(X,Y)<0GOTO1460
1440 PRINT@T(X,Y),CHR$(191);CHR$(159);CHR$(159);CHR$(159);CHR$(159);CHR$(191);
1450 PRINT@B(X,Y),CHR$(191);CHR$(157);CHR$(187);CHR$(191);CHR$(153);CHR$(191);:RETURN
1460 PRINT@T(X,Y),CHR$(191);CHR$(191);CHR$(175);CHR$(175);CHR$(175);CHR$(191);
1470 PRINT@B(X,Y),CHR$(191);CHR$(191);CHR$(132);CHR$(128);CHR$(174);CHR$(191);:RETURN
1480 IFI*A(X,Y)=0GOTO1510
1490 PRINT@T(X,Y),CHR$(128);CHR$(160);CHR$(160);CHR$(160);CHR$(160);CHR$(128);
1500 PRINT@B(X,Y),CHR$(128);CHR$(162);CHR$(132);CHR$(128);CHR$(166);CHR$(128);:RETURN
1510 PRINT@T(X,Y),CHR$(128);CHR$(128);CHR$(144);CHR$(144);CHR$(144);CHR$(128);
1520 PRINT@B(X,Y),CHR$(128);CHR$(128);CHR$(187);CHR$(191);CHR$(145);CHR$(128);:RETURN
1530 PRINT@50,STRING$(14," ");
1540 X$=INKEY$:IFX$="N"THENN$="N"
1550 IFX$>"@"ANDX$<"I"THENX=VAL(CHR$(ASC(X$)-16)):GOTO1690
1560 IFX$="N"THENGOSUB1990
1570 IFX$="K"ANDA(5,1)=99ANDA(6,1)=1ANDA(7,1)=1ANDA(8,1)=7THENA(5,1)=1:A(6,1)=7:A(7,1)=99:A(8,1)=1:GOTO1760
1580 IFX$="Q"ANDA(5,1)=99ANDA(4,1)=1ANDA(3,1)=1ANDA(2,1)=1ANDA(1,1)=7THENA(5,1)=1:A(4,1)=7:A(3,1)=99:A(1,1)=1:GOTO1760
1590 IFX$="K"ANDA(4,1)=99ANDA(3,1)=1ANDA(2,1)=1ANDA(1,1)=7THENA(4,1)=1:A(3,1)=7:A(2,1)=99:A(1,1)=1:GOTO1760
1600 IFX$="Q"ANDA(4,1)=99ANDA(5,1)=1ANDA(6,1)=1ANDA(7,1)=1ANDA(8,1)=7THENA(4,1)=1:A(5,1)=7:A(6,1)=99:A(8,1)=1:GOTO1760
1610 IFX$="X"THENGOSUB1790:PRINT@50,"EXCHANGING   ";:GOSUB2410:IFCL$="Y"THENPRINT@306,"START ";RIGHT$(TIME$,8);:GOTO80ELSEGOTO80
1620 IFX$="S"THENGOTO1780
1630 IFX$="M"THENGOSUB1800
1640 IFX$="I"THENGOSUB2180:CLS:GOSUB860:PRINT@114,"MOVE ";B$;
1650 IFX$=CHR$(17)THENRUN
1660 IFX$="L"THENGOSUB2000
1670 IFX$="P"THENGOSUB1790:GOSUB2460:CLS:GOSUB860:PRINT@114,"MOVE ";B$;
1680 GOTO1540
1690 PRINT@51,X$;
1700 Y=VAL(INKEY$):IFY=0GOTO1700
1710 PRINT@52,Y;:PRINT@55,"-";
1720 IFA(X,Y)<2ORA(X,Y)>99THENPRINT@242,"ENTRY ERROR  ";:GOTO1530
1730 A$=INKEY$:IFA$<"A"ORA$>"H"THEN1730ELSEA=VAL(CHR$(ASC(A$)-16))
1740 PRINT@57,A$;
1750 B=VAL(INKEY$):IFB=0GOTO1750ELSEPRINT@58,B;
1760 IFX$="K"ORX$="Q"THENPRINT@50,"CSTL. ";X$;"-SIDE";:GOSUB1790ELSEGOSUB1790
1770 IFCL$="Y"THENPRINT@306,"START ";RIGHT$(TIME$,8);
1780 RETURN
1790 FORJ=114TO562STEP64:PRINT@J,STRING$(14," ");:NEXT:RETURN
1800 GOSUB1790:PRINT@50,"MODIFYING     ";
1810 GOSUB1970:IFM$<"A"ORM$>"H"THENGOTO1810
1820 X=VAL(CHR$(ASC(M$)-16)):PRINT@114,M$;
1830 M$=INKEY$:IFM$<"1"ORM$>"8"THENGOTO1830
1840 Y=VAL(M$):PRINT@116,M$;" = ";
1850 M$=INKEY$:IFM$="C"ORM$="P"ORM$="E"THENS1$=M$:PRINT@120,M$;ELSEGOTO1850
1860 M$=INKEY$:IFM$="S"ORM$="P"ORM$="N"ORM$="B"ORM$="R"ORM$="Q"ORM$="K"THENV$=M$:PRINT@122,M$;ELSEGOTO1860
1870 IFV$="P"THENV1=2
1880 IFV$="N"THENV1=4
1890 IFV$="B"THENV1=5
1900 IFV$="R"THENV1=7
1910 IFV$="Q"THENV1=9
1920 IFV$="K"THENV1=99
1930 IFV$="S"THENV1=1
1940 IFS1$="C"ANDV1<>1THENV1=-V1
1950 A(X,Y)=V1:IFS1$="E"THENA(X,Y)=1
1960 GOSUB870:GOTO1800
1970 M$=INKEY$:IFM$="N"THENGOSUB1990:GOTO1970ELSEIFM$="Z"THENGOSUB1790:PRINT@50,STRING$(14," ");:GOSUB860ELSERETURN
1980 IFCL$="Y"THENPRINT@306,"START ";RIGHT$(TIME$,8);:GOTO80ELSEGOTO80
1990 FORJ=1TO8:FORK=1TO8:K$=STR$(K):PRINT@T(J,K),CHR$(J+64);B(J,K),RIGHT$(K$,1);:NEXT:NEXT:RETURN
2000 GOSUB1790:PRINT@50,"LEVEL=";B8!;"   ";:PRINT@114,"CHANGE LEVELS?";
2010 C$=INKEY$:IFC$="N"THENGOTO2050ELSEIFC$="Y"THENPRINT@57,STRING$(7," ");:GOSUB1790ELSEGOTO2010
2020 L1$=INKEY$:IFL1$=""THENGOTO2020ELSEIFL1$>="0"ANDL1$<="9"THENPRINT@57,L1$;ELSEGOTO2020
2030 L2$=INKEY$:IFL2$=""THENGOTO2030ELSEIFL2$<"0"ORL2$>"9"THENL2$=L1$:L1$="0"
2040 B8!=VAL(L1$)*10+VAL(L2$):B7!=B8!/2:PRINT@57,L1$;L2$;
2050 GOSUB1790:PRINT@50,STRING$(14," ");:PRINT@114,"MOVE ";B$;:RETURN
2060 REM
2070 PRINT@178,"P, N, B, R, Q";
2080 Z$=INKEY$:IFZ$="P"THENA(A,B)=2:GOTO2140
2090 IFZ$="N"THENA(A,B)=4:GOTO2140
2100 IFZ$="B"THENA(A,B)=5:GOTO2140
2110 IFZ$="R"THENA(A,B)=5:GOTO2140
2120 IFZ$="Q"THENA(A,B)=9:GOTO2140
2130 GOTO2080
2140 RETURN
2150 IFCL$="Y"THENPRINT@370,"STOP ";RIGHT$(TIME$,8);
2160 PRINT@560,"HIT 'P' TO PLAY";:PRINT@624,"OR 'B' FOR BASIC";
2170 A$=INKEY$:IFA$=""THENGOTO2170ELSEIFA$="B"NEWELSEIFA$="P"THENRUNELSEGOTO2170
2180 CLS:PRINT"1.  USE LIST TO INSURE PGM LOADED RIGHT, THE LAST LINE IS 2510."
2190 PRINT"2.  ALSO NOTE MEMORY SIZE AS A DOUBLE CHECK. "
2200 PRINT"3.  SPECIAL COMMANDS DURING PLAYER'S MOVE ARE:"
2210 PRINT"    TYPE 'N' - TO NUMBER THE BOARD."
2220 PRINT"         'K' - TO CASTLE KING SIDE."
2230 PRINT"         'Q' - TO CASTLE QUEEN SIDE."
2240 PRINT"         'X' - TO EXCHANGE PIECES AND LET THE COMPUTER PLAY."
2250 PRINT"         'S' - TO LET THE COMPUTER PLAY THE GAME BY ITSELF."
2260 PRINT"         'M' - TO MODIFY THE BOARD ENTER THE SQUARE FOLLOWED BY"
2270 PRINT"                  C, P, OR E FOR COMPUTER, PLAYER, OR EMPTY"
2280 PRINT"                  AND S,P,N,B,R,Q,K  FOR SQUARE,PAWN, ETC."
2290 PRINT"         'Z' - TO ESCAPE MODIFY AND LET THE COMPUTER MOVE."
2300 PRINT"         'I' - TO GET INSTRUCTIONS AGAIN."
2310 PRINT"         'L' - TO LOOK AT OR CHANGE LEVEL OF PLAY."
2320 PRINT"         'P' - TO SAVE OR LOAD PREVIOUS GAME."
2330 INPUT"HIT ENTER TO CONTINUE";EN$:PRINT
2340 PRINT"4.  TO PROMOTE TO P,N,B,R,Q  ENTER THE LETTER WHEN PROMOTED."
2350 PRINT"5.  TO CAPTURE 'EN PASSANT' SPECIFY THE 'FROM' - 'TO' SQUARES."
2360 PRINT"6.  LEVELS OF PLAY ARE FROM 01 TO 24."
2370 PRINT"7.  COMPUTER ASSUMES MATE IF KING IS LEFT IN CHECK, TO BE SURE    TO WATCH FOR THE CHECK MSG."
2380 PRINT"8.  IF A MOVE IS ILLEGAL OR AN ENTRY ERROR IS MADE, SIMPLY TYPE    IN A NEW MOVE.  A MOVE MAY BE STARTED OVER BY FORCING AN     ERROR.    (E.G. ''9'')
2390 PRINT"9.  YOU HAVE A CHOICE OF WHITE OF BLACK, THE COMPUTER WILL    ALWAYS BE AT THE TOP OF THE SCREEN.10. TO RESTART THE GAME, PRESS <CONTROL> <Q>.HIT ENTER TO CONTINUE.";:INPUTEN$:RETURN
2400 B7!=B7!+I*RR!
2410 I=-I:GOSUB2420:GOSUB860:RETURN
2420 L=0:FORJ=1TO8:FORK=1TO8:L=L+1:IFA(J,K)<>1THENC(L)=-A(J,K)ELSEC(L)=A(J,K)
2430 NEXT:NEXT
2440 L=65:FORJ=1TO8:FORK=1TO8:L=L-1:A(J,K)=C(L):NEXT:NEXT
2450 RETURN
2460 PRINT@48,"<S>AVE OR <L>OAD";
2470 EN$=INKEY$:IFEN$=""THENGOTO2470ELSEIFEN$="L"THENPRINT@114,"LOADING";:GOTO2490ELSEIFEN$<>"S"THENRETURN
2480 PRINT@114,"SAVING";:OPEN"O",1,"CHESGAME/CUR":FORX=1TO8:PRINT#1,A(X,1),A(X,2),A(X,3),A(X,4),A(X,5),A(X,6),A(X,7),A(X,8):NEXT:PRINT#1,I:CLOSE1:RETURN
2490 OPEN"I",1,"CHESGAME/CUR":FORX=1TO8:INPUT#1,A(X,1),A(X,2),A(X,3),A(X,4),A(X,5),A(X,6),A(X,7),A(X,8):NEXT:INPUT#1,I:CLOSE1:KILL"CHESGAME/CUR":RETURN
2500 ONERRORGOTO0'RESUMENEXT
