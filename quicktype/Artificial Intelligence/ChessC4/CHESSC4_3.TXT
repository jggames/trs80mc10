1 CLS:X=32768:GOSUB3000:Z=0:X=0:Y=0:I=1:DIMC(64),A(9,9),A,B,M(4),U(4):RR=SGN(RND(0)-.5):GOSUB3100:GOTO20
5 TI$="":SS=TT:FORJ=0TO4
6 IFINT(SS/M(J))THENTI$=TI$+MID$(":",1,ABS(LEN(TI$)>0))+RIGHT$("00"+MID$(STR$(INT(SS/M(J))),2),2)
7 SS=SS-INT(SS/M(J))*M(J):NEXT:RETURN
10 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
11 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
20 CLS:INPUT"DO YOU WANT INSTRUCTIONS";IN$:IFLEFT$(IN$,1)="Y"THENGOSUB2180
30 CL$="Y":REM IF YOU DON'T HAVE THE CLOCK, CHANGE THIS TO 'CL$="N"'
40 CLS:INPUT"YOUR NAME IS";B$:IFB$=""THENB$="HUMAN":GOTO50
41 B$=LEFT$(B$,7)
50 CLS:PRINT"LEVEL OF PLAY";:INPUTB8:IFB8=0THENB8=1:B7=1+RND(0)/2:GOTO60
51 B7=(B8+1)/2+RND(0)/2
60 CLS:PRINT;"DO YOU WANT WHITE ";B$;:INPUTC$:J=USR(0):TT=0:GOSUB750:IFLEFT$(C$,1)<>"N"THENGOSUB860:GOTO170
70 GOSUB850:IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@152,TI$;:J=USR(0):PRINT@184,"--------";
80 F=-99:A0=0:FORJ=1TO8:FORK=1TO8:IFA(J,K)=99THENA6=J:A7=K
90 NEXT:NEXT
100 FORX=1TO8:FORY=1TO8:PRINT@56,"XY=";CHR$(X+64);MID$(STR$(Y),2);:IFA(X,Y)<0THENGOSUB240:IFF>B7THEN130
110 NEXT:NEXT:IFF>=-9THEN130
111 GOSUB860
120 PRINT@120,"I LOSE.":GOTO2150
130 A(R,U)=A(E,Q):A(E,Q)=1:IFA(R,U)=-2ANDU=1THENA(R,U)=-9
140 X=R:Y=U:A0=4:GOSUB240
141 PRINT@120,"COMPUTER";:PRINT@88,CHR$(E+64);MID$(STR$(Q),2);"-";CHR$(R+64);MID$(STR$(U),2)
150 IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@184,TI$;:J=USR(0)
160 X=E:Y=Q:GOSUB870:X=R:Y=U:GOSUB870:IFC=1THENPRINT@120,"CHECK":C=0
170 IFINKEY$=CHR$(21)THENX$="":GOTO170
171 IFX$="S"THENGOSUB1790:PRINT@24,"AUTO":GOSUB2400:GOTO80
172 PRINT@56,B$:GOSUB1530:D=0:B4=A:B9=B:B5=0:IFA(X,Y)=2ANDY=5ANDB=6ANDA(A,B)=1ANDABS(A-X)=1THENB5=1:GOTO200
180 IFX$="K"ORX$="Q"THENGOSUB860:PRINT@56,"":GOTO80
190 IFX$="S"THENCL$="N":GOTO170
191 A0=3:GOSUB230:IFD=0THENPRINT@120,"ILLEGAL":GOTO170
200 PRINT@56,"OK":A=B4:B=B9:A(A,B)=A(X,Y):A(X,Y)=1:GOSUB870:X=A:Y=B:GOSUB870:IFN$="N"THENGOSUB860:N$=" "
210 IFA(A,B)=2ANDB=8THEN?@56,"PIECE?  ";:GOSUB2060:X=A:Y=B:GOSUB870:?@56,"        ";:?@88,"        ";
220 IFB5=1THENA(A,B-1)=1:X=A:Y=B-1:GOSUB870
221 GOTO80
230 PRINT@30,"+ ";:ONA(X,Y)GOTO,440,,340,290,,250,,290:GOTO420
240 ON(-A(X,Y))GOTO,490,,340,290,,250,,290:GOTO420
250 B=Y:FORA=X+1TO8:GOSUB600:IFS=0THENNEXT
260 FORA=X-1TO1STEP-1:GOSUB600:IFS=0THENNEXT
270 A=X:FORB=Y+1TO8:GOSUB600:IFS=0THENNEXT
280 FORB=Y-1TO1STEP-1:GOSUB600:IFS=0THENNEXT
281 RETURN
290 B=Y:FORA=X+1TO8:B=B+1:GOSUB600:IFS=0THENNEXT
300 B=Y:FORA=X-1TO1STEP-1:B=B-1:GOSUB600:IFS=0THENNEXT
310 B=Y:FORA=X-1TO1STEP-1:B=B+1:GOSUB600:IFS=0THENNEXT
320 B=Y:FORA=X+1TO8:B=B-1:GOSUB600:IFS=0THENNEXT
330 IFABS(A(X,Y))=9THEN250
331 RETURN
340 A=X+2:B=Y+1:IFA<9ANDB<9THENGOSUB610
350 B=B-2:IFB>0ANDA<9THENGOSUB610
360 A=A-4:IFA>0ANDB>0THENGOSUB610
370 B=B+2:IFB<9ANDA>0THENGOSUB610
380 A=A+1:B=B+1:IFA>0ANDA<9ANDB<9THENGOSUB610
390 B=B-4:IFB>0ANDA>0ANDA<9THENGOSUB610
400 A=A+2:IFA>0ANDA<9ANDB>0THENGOSUB610
410 B=B+4:IFB<9ANDA>0ANDA<9THENGOSUB610
411 RETURN
420 FORA=X-1TOX+1:FORB=Y-1TOY+1:IFA(A,B)<>0THENGOSUB610
430 NEXT:NEXT:RETURN
440 A=X:IFY>2THEN460
450 B=Y+1:IFA(A,B)=1THEN455
451 GOTO470
455 GOSUB620:B=B+1:IFA(A,B)=1THENGOSUB620
456 GOTO470
460 B=Y+1:IFA(A,B)=1THENGOSUB620
470 A=X+1:B=Y+1:IFA(A,B)<0THENGOSUB620
480 A=A-2:IFA(A,B)<0THENGOSUB620
481 RETURN
490 A=X:IFY<7THEN510
500 B=Y-1:IFA(A,B)=1THEN505
501 GOTO520 
505 GOSUB620:B=B-1:IFA(A,B)=1THENGOSUB620
506 GOTO520
510 B=Y-1:IFA(A,B)=1THENGOSUB620
520 A=X-1:B=Y-1:IFA(A,B)>1THENGOSUB620
530 A=A+2:IFA(A,B)>1THENGOSUB620
531 RETURN
540 T=A(A,B):IFT=-99THENB1=T:RETURN
550 A5=S:IFABS(T)<=A(X,Y)THENA(A,B)=A(X,Y):A(X,Y)=1:GOTO570
560 IFT<B1THENB1=T:S=A5:RETURN
561 S=A5:RETURN
570 A1=X:A2=Y:A3=A:A4=B:A8=T:A0=2:FORX=1TO8:FORY=1TO8:IFA(X,Y)<0THENGOSUB240:IFT=0THEN590
580 NEXT:NEXT
590 X=A1:Y=A2:A=A3:B=A4:A0=5:A(X,Y)=A(A,B):A(A,B)=A8:GOTO560
600 S=0:IFA(A,B)=1THEN620
601 IFA(A,B)=0THENS=1:RETURN
602 IFSGN(A(A,B))=SGN(A(X,Y))THENS=1:RETURN
603 S=1:GOTO620
610 IFA(A,B)=1THEN620
611 IFSGN(A(A,B))=SGN(A(X,Y))THENRETURN
620 PRINT@30," +";:ONA0GOTO630,640,650,660,670:GOTO680
630 IFA6=AANDA7=BTHENB1=B1+1
631 RETURN
640 IFA3=AANDA4=BTHENT=0
641 RETURN
650 IFB4=AANDB9=BTHEND=1
651 RETURN
660 IFA6=AANDA7=BTHENC=1
661 RETURN
670 IFA(A,B)<0THEN540
671 RETURN
680 B3=S:W=X:M=Y:N=A:H=B:P=A(A,B):A(A,B)=A(X,Y):A(X,Y)=1:B1=0
690 A0=5:FORX=1TO8:FORY=1TO8:IFA(X,Y)>1THENGOSUB230
700 NEXT:NEXT:X=N:Y=H:A0=1:GOSUB240:A0=0:S=B3:X=W:Y=M:A=N:B=H:A(X,Y)=A(A,B):A(A,B)=P
710 B6=1/(ABS(4.5-A)+ABS(4.5-B)+1):IFA(X,Y)<-2ANDA(X,Y)>-9THENB6=B6+1/(ABS(A6-A)+ABS(A7-B)+5)+RND(0)/15
720 G=P+B1+B6:IFP=99THENGOSUB860:PRINT@120,"MATE":GOTO2150
730 IFG<=FTHENRETURN
740 F=G:E=X:Q=Y:R=A:U=B:RETURN
750 CLS
760 Y=0:FORJ=448TO0STEP-64:Y=Y+1:X=0:FORK=JTO21+JSTEP3:X=X+1:T(X,Y)=K:NEXT:NEXT
770 Y=0:FORJ=480TO32STEP-64:Y=Y+1:X=0:FORK=JTO21+JSTEP3:X=X+1:B(X,Y)=K:NEXT:NEXT
780 DATA-7,-4,-5,-9,-99,-5,-4,-7
790 DATA-2,-2,-2,-2,-2,-2,-2,-2
800 DATA1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1
810 DATA2,2,2,2,2,2,2,2
820 DATA7,4,5,9,99,5,4,7
830 FORY=8TO1STEP-1:FORX=1TO7STEP2:S(X,Y)=I:S(X+1,Y)=-I:NEXT:I=-I:NEXT
840 FORY=8TO1STEP-1:FORX=1TO8:READA(X,Y):NEXT:NEXT:RETURN
850 I=-I:A(4,1)=99:A(5,1)=9:A(4,8)=-99:A(5,8)=-9
860 FORY=1TO8:FORX=1TO8:GOSUB870:NEXT:NEXT:RETURN
870 IFS(X,Y)>0THEN890
880 ONABS(A(X,Y))GOSUB920,980,,1280,1380,,1480,,1080:IFABS(A(X,Y))=99THENGOSUB1180
881 RETURN
890 ONABS(A(X,Y))GOSUB900,930,,1230,1330,,1430,,1030:IFABS(A(X,Y))=99THENGOSUB1130
891 RETURN
900 IFS(X,Y)<0THEN920
910 PRINT@T(X,Y),"ÏÏÏ";:PRINT@B(X,Y),"ÏÏÏ";:RETURN
920 PRINT@T(X,Y),"ÀÀÀ";:PRINT@B(X,Y),"ÀÀÀ";:RETURN
930 IFI*A(X,Y)<0THEN960:REM PAWN ON WHITE
940 PRINT@T(X,Y),"Ïñò";
950 PRINT@B(X,Y),"Ï÷û";:RETURN
960 PRINT@T(X,Y),"Ï¡¢";
970 PRINT@B(X,Y),"Ï§«";:RETURN
980 IFI*A(X,Y)<0THEN1010:REM PAWN ON BLACK
990 PRINT@T(X,Y),"Àñò";
1000 PRINT@B(X,Y),"À÷û";:RETURN
1010 PRINT@T(X,Y),"À¡¢";
1020 PRINT@B(X,Y),"À§«";:RETURN
1030 IFI*A(X,Y)<0THEN1060:REM QUEEN ON WHITE
1040 PRINT@T(X,Y),"Ïùö";
1050 PRINT@B(X,Y),"Ïÿÿ";:RETURN
1060 PRINT@T(X,Y),"Ï©¦";
1070 PRINT@B(X,Y),"Ï¯¯";:RETURN
1080 IFI*A(X,Y)<0THEN1110:REM QUEEN ON BLACK
1090 PRINT@T(X,Y),"Àùö";
1100 PRINT@B(X,Y),"Àÿÿ";:RETURN
1110 PRINT@T(X,Y),"À©¦";
1120 PRINT@B(X,Y),"À¯¯";:RETURN
1130 IFI*A(X,Y)<0THEN1160:REM KING ON WHITE
1140 PRINT@T(X,Y),"Ïû÷";
1150 PRINT@B(X,Y),"Ïû÷";:RETURN
1160 PRINT@T(X,Y),"Ï«§";
1170 PRINT@B(X,Y),"Ï«§";:RETURN
1180 IFI*A(X,Y)<0THEN1210:REM KING ON BLACK
1190 PRINT@T(X,Y),"Àû÷";
1200 PRINT@B(X,Y),"Àû÷";:RETURN
1210 PRINT@T(X,Y),"À«§";
1220 PRINT@B(X,Y),"À«§";:RETURN
1230 IFI*A(X,Y)<0THEN1260:REM KNIGHT ON WHITE
1240 PRINT@T(X,Y),"Ïöû";
1250 PRINT@B(X,Y),"Ïóÿ";:RETURN
1260 PRINT@T(X,Y),"Ï¦«";
1270 PRINT@B(X,Y),"Ï£¯";:RETURN
1280 IFI*A(X,Y)<0THEN1310:REM KNIGHT ON BLACK
1290 PRINT@T(X,Y),"Àöû";
1300 PRINT@B(X,Y),"Àóÿ";:RETURN
1310 PRINT@T(X,Y),"À¦«";
1320 PRINT@B(X,Y),"À£¯";:RETURN
1330 IFI*A(X,Y)<0THEN1360:REM BISHOP ON WHITE
1340 PRINT@T(X,Y),"Ïöù";
1350 PRINT@B(X,Y),"Ï÷û";:RETURN
1360 PRINT@T(X,Y),"Ï¦©";
1370 PRINT@B(X,Y),"Ï§«";:RETURN
1380 IFI*A(X,Y)<0THEN1410:REM BISHOP ON BLACK
1390 PRINT@T(X,Y),"Àöù";
1400 PRINT@B(X,Y),"À÷û";:RETURN
1410 PRINT@T(X,Y),"À¦©";
1420 PRINT@B(X,Y),"À§«";:RETURN
1430 IFI*A(X,Y)<0THEN1460:REM ROOK ON WHITE
1440 PRINT@T(X,Y),"Ïû÷";
1450 PRINT@B(X,Y),"Ïÿÿ";:RETURN
1460 PRINT@T(X,Y),"Ï«§";
1470 PRINT@B(X,Y),"Ï¯¯";:RETURN
1480 IFI*A(X,Y)<0THEN1510:REM ROOK ON BLACK
1490 PRINT@T(X,Y),"Àû÷";
1500 PRINT@B(X,Y),"Àÿÿ";:RETURN
1510 PRINT@T(X,Y),"À«§";
1520 PRINT@B(X,Y),"À¯¯";:RETURN
1530 PRINT@24,"        ";
1540 X$=INKEY$:IFX$="N"THENN$="N"
1550 IFX$>"@"ANDX$<"I"THENX=VAL(CHR$(ASC(X$)-16)):GOTO1690
1560 IFX$="N"THENGOSUB1990
1570 IFX$="K"ANDA(5,1)=99ANDA(6,1)=1ANDA(7,1)=1ANDA(8,1)=7THENA(5,1)=1:A(6,1)=7:A(7,1)=99:A(8,1)=1:GOTO1760
1580 IFX$="Q"ANDA(5,1)=99ANDA(4,1)=1ANDA(3,1)=1ANDA(2,1)=1ANDA(1,1)=7THENA(5,1)=1:A(4,1)=7:A(3,1)=99:A(1,1)=1:GOTO1760
1590 IFX$="K"ANDA(4,1)=99ANDA(3,1)=1ANDA(2,1)=1ANDA(1,1)=7THENA(4,1)=1:A(3,1)=7:A(2,1)=99:A(1,1)=1:GOTO1760
1600 IFX$="Q"ANDA(4,1)=99ANDA(5,1)=1ANDA(6,1)=1ANDA(7,1)=1ANDA(8,1)=7THENA(4,1)=1:A(5,1)=7:A(6,1)=99:A(8,1)=1:GOTO1760
1610 IFX$="X"THEN1615
1611 GOTO1620
1615 GOSUB1790:PRINT@24,"EXCHANGE";:GOSUB2410:IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@152,TI$;:J=USR(0)
1616 GOTO80
1620 IFX$="S"THEN1780
1630 IFX$="M"THENGOSUB1800
1640 IFX$="I"THENGOSUB2180:CLS:GOSUB860:PRINT@56,B$
1650 IFX$=CHR$(21)THENRUN
1660 IFX$="L"THENGOSUB2000
1670 IFX$="P"THENGOSUB1790:GOSUB2460:CLS:GOSUB860:PRINT@56,B$
1680 GOTO1540
1690 PRINT@24,X$
1700 Y=VAL(INKEY$):IFY=0THEN1700
1710 PRINT@25,MID$(STR$(Y),2);:PRINT@26,"-";
1720 IFA(X,Y)<2ORA(X,Y)>99THENPRINT@120,"ERROR":GOTO1530
1730 A$=INKEY$:IFA$<"A"ORA$>"H"THEN1730
1731 A=VAL(CHR$(ASC(A$)-16))
1740 PRINT@27,A$;
1750 B=VAL(INKEY$):IFB=0THEN1750
1751 PRINT@28,MID$(STR$(B),2);
1760 IFX$="K"ORX$="Q"THENPRINT@24,"CASTLE ";X$;
1761 GOSUB1790
1770 IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@152,TI$;:J=USR(0)
1780 RETURN
1790 FORJ=56TO280STEP32:PRINT@J,"":NEXT:RETURN
1800 GOSUB1790:PRINT@24,"MODIFY"
1810 GOSUB1970:IFM$<"A"ORM$>"H"THEN1810
1820 X=VAL(CHR$(ASC(M$)-16)):PRINT@56,M$;
1830 M$=INKEY$:IFM$<"1"ORM$>"8"THEN1830
1840 Y=VAL(M$):PRINT@57,M$;"=";
1850 M$=INKEY$:IFM$="C"ORM$="P"ORM$="E"THENS1$=M$:PRINT@59,M$;:GOTO1860
1851 GOTO1850
1860 M$=INKEY$:IFM$="S"ORM$="P"ORM$="N"ORM$="B"ORM$="R"ORM$="Q"ORM$="K"THENV$=M$:PRINT@60,M$;:GOTO1870
1861 GOTO1860
1870 IFV$="P"THENV1=2
1880 IFV$="N"THENV1=4
1890 IFV$="B"THENV1=5
1900 IFV$="R"THENV1=7
1910 IFV$="Q"THENV1=9
1920 IFV$="K"THENV1=99
1930 IFV$="S"THENV1=1
1940 IFS1$="C"ANDV1<>1THENV1=-V1
1950 A(X,Y)=V1:IFS1$="E"THENA(X,Y)=1
1960 GOSUB870:GOTO1800
1970 M$=INKEY$:IFM$="N"THENGOSUB1990:GOTO1970
1971 IFM$="Z"THENGOSUB1790:PRINT@24,"":GOSUB860:GOTO1980
1972 RETURN
1980 IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@152,TI$;:J=USR(0)
1981 GOTO80
1990 FORJ=1TO8:FORK=1TO8:PRINT@T(J,K),CHR$(J+64);:K$=MID$(STR$(K),2):PRINT@T(J,K)+32,K$;:NEXT:NEXT:RETURN
2000 GOSUB1790:PRINT@24,"LV";B8;:PRINT@56,"CHANGE?";
2010 C$=INKEY$:IFC$="N"THEN2050
2011 IFC$="Y"THENPRINT@27,"":GOSUB1790:GOTO2020
2012 GOTO2010
2020 L1$=INKEY$:IFL1$=""THEN2020
2021 IFL1$>="0"ANDL1$<="9"THENPRINT@27,L1$;:GOTO2030
2022 GOTO2020
2030 L2$=INKEY$:IFL2$=""THEN2030
2031 IFL2$<"0"ORL2$>"9"THENL2$=L1$:L1$="0"
2040 B8=VAL(L1$)*10+VAL(L2$):B7=B8/2:PRINT@27,L1$;L2$;
2050 GOSUB1790:PRINT@24,"":PRINT@56,B$:RETURN
2060 REM
2070 PRINT@88,"PnBrQ?";
2080 Z$=INKEY$:IFZ$="P"THENA(A,B)=2:GOTO2140
2090 IFZ$="N"THENA(A,B)=4:GOTO2140
2100 IFZ$="B"THENA(A,B)=5:GOTO2140
2110 IFZ$="R"THENA(A,B)=5:GOTO2140
2120 IFZ$="Q"THENA(A,B)=9:GOTO2140
2130 GOTO2080
2140 RETURN
2150 IFCL$="Y"THENTT=TT+INT(USR(1)/60):GOSUB5:PRINT@184,TI$;:J=USR(0)
2160 PRINT@280,"P=PLAY";:PRINT@312,"B=BASIC";
2170 A$=INKEY$:IFA$=""THEN2170
2171 IFA$="B"THENEND
2172 IFA$="P"THENRUN
2173 GOTO2170
2180 CLS
2200 PRINT"COMMANDS DURING PLAYER'S MOVE:"
2210 PRINT"N-TO NUMBER THE BOARD."
2220 PRINT"K-TO CASTLE KING SIDE."
2230 PRINT"Q-TO CASTLE QUEEN SIDE."
2240 PRINT"X-EXCHANGE PIECES."
2250 PRINT"S-COMPUTER SELF-PLAY MODE."
2260 PRINT"M-MODIFY BOARD. ENTER SQUARE &"
2270 PRINT"C,P,E FOR COMP,PLAYER,EMPTY &"
2280 PRINT"S,P,N,B,R,Q,K FOR SQUARE,PAWN...";
2290 PRINT"Z-TO ESCAPE MODIFY FUNCTION."
2300 PRINT"I-GET INSTRUCTIONS AGAIN."
2310 PRINT"L-SEE/CHANGE LEVEL OF PLAY."
2320 PRINT"P-SAVE OR LOAD PREVIOUS GAME."
2330 INPUT"HIT ENTER TO CONTINUE";EN$:CLS
2340 PRINT"TO PROMOTE TO P,N,B,R,Q"
2341 PRINT"ENTER THE LETTER WHEN PROMOTED."
2350 PRINT"TO CAPTURE 'EN PASSANT' SPECIFY"
2351 PRINT"THE 'FROM' - 'TO' SQUARES."
2360 PRINT"LEVELS OF PLAY ARE FROM 1-24."
2370 PRINT"COMPUTER ASSUMES MATE IF KING"
2371 PRINT"IS LEFT IN CHECK, TO BE SURE"
2372 PRINT"TO WATCH FOR THE CHECK MESSAGE."
2380 PRINT"FORCE AN ERROR TO REDO MOVE."
2390 PRINT"THE COMPUTER WILL ALWAYS BE AT"
2391 PRINT"THE TOP OF THE SCREEN."
2392 PRINT"TO RESTART THE GAME, PRESS"
2393 PRINT"<CONTROL>-<Q>.":PRINT"HIT ENTER TO CONTINUE";:INPUTEN$:RETURN
2400 B7=B7+I*RR
2410 I=-I:GOSUB2420:GOSUB860:RETURN
2420 L=0:FORJ=1TO8:FORK=1TO8:L=L+1:IFA(J,K)<>1THENC(L)=-A(J,K):GOTO2430
2421 C(L)=A(J,K)
2430 NEXT:NEXT
2440 L=65:FORJ=1TO8:FORK=1TO8:L=L-1:A(J,K)=C(L):NEXT:NEXT
2450 RETURN
2460 PRINT@23,"sAV/lOAD";
2470 EN$=INKEY$:IFEN$=""THEN2470
2471 IFEN$="L"THENPRINT@56,"LOADING" :GOTO2490
2472 IFEN$<>"S"THENRETURN
2480 PRINT@56,"SAVING":CSAVE*A,"CHESGAME":RETURN
2490 CLOAD*A,"CHESGAME":RETURN
2999 END
3000 Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:RETURN
3001 READU$:FORY=1TOLEN(U$)STEP2:POKEZ,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3010 REM MC-10 EDITS JIM GERRIE
3100 M(0)=604800:M(1)=86400:M(2)=3600:M(3)=60:M(4)=1:RETURN
