5 GOSUB500
10 CLEAR100:DIM D(10,4),B(10),C(10),G(10):A=RND(-(PEEK(9)*256+PEEK(10)))
20 A=0:GOTO 200
30 PRINT:J=0
35 INPUT"YOUR GUESS";N:N=(N+.1)/100000
40 FORI=0TO4:G(I)=INT(10*N):N=10*N-INT(10*N):IFI=0THENK=0:GOTO44
41 FORK=0TOI-1:REM IF I=0 GOTO 44
42 IFG(I)=G(K)THEN170
43 NEXTK
44 NEXTI
45 P=4:A=0:GOSUB 300
50 IF V=1 THEN B$="BULL":GOTO 52
51 B$="BULLS"
52 IF W-V=1 THEN C$="COW":GOTO60
53 C$="COWS"
60 IF V=5 THEN PRINT"5 BULLS, YOU WIN!":GOTO 110
61 PRINTV;B$;W-V;C$
65 IF J=0 THEN A=1:GOTO 200
68 GOSUB400
70 PRINT"MY GUESS IS ";
75 FORI=0TO4:PRINTCHR$(D(J,I)+48);:NEXT I:PRINT
80 INPUT"MY SCORE #,#";B(J),C(J):C(J)=C(J)+B(J)
81 IFB(J)>-1THENIFB(J)<6THENIFC(J)<6THENIFC(J)-B(J)>-1THEN83
82 PRINT" - RIDICULOUS!!":GOTO 70
83 IFB(J)=4THENIFC(J)=5THEN82
85 IFB(J)=5THENPRINT " - I WIN -":PRINT"MY NUMBER WAS ";:GOTO 100
90 GOTO35
100 FORI=0TO4:PRINTCHR$(D(0,I)+48);:NEXTI:PRINT
110 PRINT
120 SOUND100,1:INPUT"TRY AGAIN (Y/N)";M$
130 IF LEFT$(M$,1)="N" THEN END
140 GOTO 10
150 PRINT:PRINT "YOU HAVE GIVEN ME IMPOSSIBLE":PRINT"SCORES-- GAME SPOILED.":GOTO 110
170 PRINT "REPEATED DIGITS NOT ALLOWED":GOTO 35
200 FOR P=0 TO 4
210 D(A,P)=INT(10*RND(0))
219 IFP=0THENI=0:GOTO240
220 FORI=0TOP-1:REM IF P=0 GOTO 230
222 IFD(A,I)=D(A,P)THENTM=I:I=P-1:NEXTI:I=TM:GOTO210
230 NEXTI
240 NEXTP
250 IF A=0 THEN 30
260 J=1:GOTO 70
300 V=0:W=0
310 FORI=0TOP:IFD(A,I)=G(I)THENV=V+1
320 FORK=0TO4:IFD(A,K)=G(I)THENW=W+1
330 NEXTK
340 NEXTI
350 RETURN
400 P=0
405 G(P)=D(J,P)
409 IFP=0THENI=0:GOTO420
410 FORI=0TOP-1:REM IF P=0 GOTO 420
412 IFG(I)=G(P)THEN430
415 NEXTI
420 FORA=1TOJ:GOSUB300
425 IFV<=B(A)THENIFW<=C(A)THENIF4-P>=C(A)-WTHENIF4-P>=B(A)-VTHEN448
430 G(P)=G(P)+3:IFG(P)>9THENG(P)=G(P)-10
432 IFP=0THENIFG(P)=D(1,0)THEN150
435 IFG(P)<>D(J,P)THEN409
440 P=P-1:IFP<0THEN150
445 GOTO430
448 NEXTA
450 P=P+1:IFP<5THEN405
455 J=J+1
460 FORI=0TO4:D(J,I)=G(I):NEXTI
465 RETURN
500 CLS:PRINT TAB(6)"bulls and cows game"
601 PRINT"BEAT THE COMPUTER AT GUESSING"
602 PRINT"A FIVE DIGIT RANDOM NUMBER"
603 PRINT"WITHOUT DUPLICATION."
604 PRINT
605 PRINT"THE PROGRAM WILL:"
606 PRINT
607 PRINT" - ASK FOR GUESSES AND ITS SCORE";
608 PRINT" - REJECT GUESSES"
609 PRINT"   THAT ARE MALFORMED"
610 PRINT" - SCORE YOUR GUESSES"
611 PRINT
612 PRINT"SCORES ARE COMPUTED AS:
613 PRINT"1 BULL FOR EACH CORRECT DIGIT"
614 PRINT"1 COW FOR EACH IN WRONG POSITION";
615 INPUT"      HIT enter TO BEGIN";M$:CLS:GOTO10
700 REM BRADFORD UNIVERSITY
710 REM CN NOV 1976
720 REM MC-10 EDITS JIM GERRIE
999 END
