0 CLS:GOTO100:REM C1=NUM LENGTH C2 DECIMAL LENGTH CC=NUM
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
8 PRINTRIGHT$("        "+STR$(INT(CC)),C1);:IFC2THENPRINTMID$(STR$((CC-INT(CC))+1.00001)+"000",3,C2+1);
9 RETURN
100 CLEAR 1000:DIMC1,C2,CC,M$,MC:MC=16384
110 DIM C(13,32),P(8,12),P$(2,12),H(6),PC$(2) 
120 M$=CHR$(RND(7)*16+137):FORC1=1TO10:CB$=CB$+M$:NEXT:REM ***** CARD BACK DESIGN 
130 BB$="          " 
140 REM U$="$####":UC$="###.##:US$="$$######,.##"
150 REM
160 REM
170 CLS
180 PRINT@224,"";
190 INPUT"HOW MANY PLAYERS (1-12)";N
200 IF N<1 OR N>12 THEN 190
210 REM
220 REM
230 FOR Y=1 TO N
240 CLS
250 PRINT@64,"WHAT IS YOUR NAME PLAYER #";Y
260 INPUT P$(1,Y) 
270 IF LEN(P$(1,Y))<=8 THEN 300
280 PRINT:PRINT "SHORTEN YOUR NAME TO 8 CHAR'S" 
290 GOTO 260
300 PRINT:PRINT"HOW MUCH IS YOUR STAKE "P$(1,Y)
310 INPUT P(1,Y)
320 IF P(1,Y)=>100 THEN 360
330 PRINT:PRINT"*YOU NEED AT LEAST $100 TO PLAY*"
340 PRINTTAB(9)"AT THIS TABLE."
350 GOTO 310
360 P(8,Y)=P(1,Y):REM **** SET CURRENT STAKE TO START STAKE 
370 NEXT Y
380 REM
390 REM
400 REM *************************
410 REM *  START OF GAME LOOP   *
420 REM *************************
430 FOR Q=1 TO 70:REM **** 8 DECKS /POSSIBLE 6 CARDS PER HAND
440 FOR Y=1 TO 6
450 H(Y)=0:REM **** HAND ARRAY
460 NEXT Y
470 BK=0:REM **** BROKE PLAYER COUNTER
480 FOR Y=1 TO N
490 CLS
500 PRINT@70,"**PLACE YOUR BETS**"
510 P(2,Y)=0:P$(2,Y)=""
520 IF P(8,Y)=>20 THEN 640
530 PRINT:PRINTTAB(3)P$(1,Y)" HAS LESS THAN THE"
540 PRINTTAB(7)"MINIMUM BET OF $20"
550 BK=BK+1
560 FOR T=1 TO 500
570 NEXT T
580 IF BK<>N THEN 820
590 PRINT:PRINT" *ALL PLAYERS ARE OUT OF FUNDS*"
600 PRINTTAB(6)"SO THE GAME IS OVER"
610 FOR T=1 TO 3000
620 NEXT T
630 GOTO 1780
640 PRINT:PRINT"WHAT IS YOUR BET "P$(1,Y)
650 INPUT P(2,Y)
660 IF P(2,Y)<=P(8,Y) THEN 690
670 PRINT"   *YOU ONLY HAVE $";:C1=3:C2=2:CC=P(8,Y):GOSUB8:PRINT" LEFT*"
680 GOTO 640
690 IF P(2,Y)=>20 THEN 720
700 PRINTTAB(6)"*MINIMUM BET IS $20*"
710 GOTO 640
720 IF P(2,Y)<=2000 THEN 750
730 PRINTTAB(5)"*MAXIMUM BET IS $2000*"
740 GOTO 640
750 J=INT(P(2,Y)/5):CH=P(2,Y)-5*J
760 IF CH=0 THEN 790
770 PRINT"   *THE SMALLEST CHIP IS $5*"
780 GOTO 640
790 INPUT "FOR BANK OR PLAYER (B OR P)";P$(2,Y)
800 IF P$(2,Y)="B" OR P$(2,Y)="P" THEN 820
810 GOTO 790
820 NEXT Y
830 REM
840 REM
850 REM *************************
860 REM *  CARD DISPLAY BEGINS  *
670 REM *************************
880 CLS0
890 PP=32:REM ****** PRINT POSITION
900 PRINT@0,"player";
910 PRINT@256,"bank";
920 FOR DR=1 TO 2:REM ***** DRAW CARD BACKS
930 FOR DC=1 TO 5
940 PRINT@PP,CB$;
950 PRINT@PP+11,CB$;
960 PP=PP+32
970 NEXT DC
980 PP=288
990 NEXT DR
1000 FOR D=1 TO 4
1010 GOSUB 2730:REM **** PICK A CARD ****
1020 FOR T=1 TO 300
1030 NEXT T
1040 H(D)=L
1050 IF H(D)=>10 THEN H(D)=0:REM **** FACE CARDS COUNT ZERO
1060 IF D=1 THEN PP=32
1070 IF D=2 THEN PP=288
1080 IF D=3 THEN PP=43
1090 IF D=4 THEN PP=299
1100 GOSUB 3020:REM ***** DISPLAY CARD
1110 NEXT D
1120 REM
1130 REM
1140 REM *************************
1150 REM * THE RULES OF BACCARAT *
1160 REM *************************
1170 PS=0:BS=0:REM **** FLAGS
1180 PT=H(1)+H(3):REM **** PLAYERS TOTAL
1190 IF PT=>10 THEN PT-PT-10
1200 BT=H(2)+H(4):REM **** BANKS TOTAL
1210 IF BT=>10 THEN BT=BT-10
1220 IF PT=>8 THEN PRINT@119,"NATURAL";
1230 IF BT=>8 THEN PRINT@376,"NATURAL";
1240 IF PT=>8 OR BT=>8 THEN 1360
1250 IF PT=6 OR PT=7 THEN PS=1:PRINT@192,"PLAYER STANDS";
1260 IF BT=7 THEN BS=1:PRINT@448,"BANK STANDS";
1270 IF BT=6 AND PS=1 THEN BS=1:PRINT@448,"BANK STANDS";
1280 IF PS=1 AND BS=1 THEN 1360
1290 IF PS=0 THEN GOSUB 2050:REM **** PLAYERS THIRD CARD
1300 IF BS=1 THEN 1360
1310 IF BT<=2 THEN 2380:REM **** BANK'S 3RD CARD
1320 IF BT=3 AND (H(5)=9 OR H(5)<=7) THEN 2380
1330 IF BT=4 AND H(5)=>2 AND H(5)<=7 THEN 2380
1340 IF BT=5 AND H(5)=>4 AND H(5)<=7 THEN 2380
1350 IF BT=6 AND (H(5)=6 OR H(5)=7) THEN 2380
1360 PRINT@207,;:M$="PLAYER'S TOTAL"+STR$(PT):GOSUB7
1370 PRINT@465,;:M$="BANK'S TOTAL  "+STR$(BT):GOSUB7
1380 PRINT@483,"PRESS ANY KEY TO CONTINUE";
1390 I$=INKEY$:IF I$="" THEN 1390
1400 REM
1410 REM
1420 REM **************************
1430 REM *  PRINT THE WINNER AND  *
1440 REM *     CALCULATE MONEY    *
L450 REM **************************
1460 CLS
1470 IF PT>BT THEN GOSUB 2180:REM **** PLAYER WINS
1480 IF BT>PT THEN BOSUB 2510:REM **** BANK WINS
1490 IF BT=PT THEN PRINT@196,"*DRAW* ALL BETS ARE OFF"
1500 PRINT@483,"PRESS ANY KEY TO CONTINUE";
1510 I$=INKEY$:IF I$="" THEN 1510
1520 CLS
1530 PRINT@70,"*THE PRESENT STAKES*":PRINT
1540 FOR X=1 TO N
1550 PRINT P$(1,X);
1560 PRINTTAB(9)"";
1570 PRINT"$";:C1=6:C2=2:CC=P(8,X):GOSUB8:PRINT
1580 NEXT X
1590 PRINT@484,"DO YOU WANT ANOTHER HAND";
1600 I$=INKEY$:IF I$="" THEN 1600
1610 IF I$="N" THEN 1790
1620 NEXT Q:REM **** END OF GAME LOOP
1630 REM
1640 REM
1650 CLS
1660 PRINT@196,"I'M ALMOST OUT OF CARDS"
1670 PRINT@262,"SO I WILL RESHUFFLE"
1680 FOR X=1 TO 13
1690 FOR Y=1 TO 32
1700 C(X,Y)=0
1710 NEXT Y
1720 NEXT X
1730 GOTO 430
1740 REM
1750 REM
1760 REM ************************** 
1770 REM *      THE SUMMARY       * 
1780 REM ************************** 
1790 FOR X=1 TO N
1800 CLS
1810 PRINT@64+((32-LEN(P$(1,X)))/2),P$(1,X)
1820 PRINT:PRINT" WON  $";:CC=P(3,X):C1=6:C2=2:GOSUB8:PRINT" IN"P(5,X)"HANDS"
1830 PRINT:PRINT" LOST $";:CC=P(4,X):C1=6:C2=2:GOSUB8:PRINT" IN"P(6,X)"HANDS"
1840 PRINT:PRINT"CASINO'S COMMISSION $";:C1=6:C2=2:CC=P(7,X):GOSUB8:PRINT
1850 PRINT:PRINT"YOU STARTED WITH    $";:C1=6:C2=2:CC=P(1,X):GOSUB8:PRINT
1860 PRINT:PRINT"YOU NOW HAVE        $";:C1=6:C2=2:CC=P(8,X):GOSUB8:PRINT
1870 IF X=N THEN PRINT@488,"SUMMARY COMPLETE";:GOTO 1890
1880 PRINT@489,"PRESS ANY KEY";
1890 I$=INKEY$:IFI$=""THEN1890
1900 NEXT X
1910 REM
1920 REM
1930 CLS
1940 END
1950 REM
1960 REM
1970 REM ***************
1980 REM THE SUBROUTINES
1990 REM ***************
2000 REM
2010 REM
2020 REM ************************ 
2030 REM *  PLAYERS THIRD CARD  * 
2040 REM ************************ 
2050 GOSUB 2730:REM **** PICK CARD
2060 PP=54
2070 GOSUB 3020:REM **** PRINT CARD
2080 H(5)=L
2090 IF H(5)=>10 THEN H(5)=0
2100 PT=PT+H(5)
2110 IF PT=>10 THEN PT=PT-10
2120 RETURN
2130 REM
2140 REM
2150 REM ***************** 
2160 REM *  PLAYER WINS  * 
2170 REM *****************
2180 PRINT@73,"*PLAYER WINS*":PRINT
2190 FOR X=1 TO N
2200 IF P$(2,X)="" THEN 2310
2210 IF P$(2,X)="B" THEN 2270:REM **** BET ON BANK
2220 P(3,X)=P(3,X)+P(2,X)
2230 P(5,X)=P(5,X)+1
2240 P(8,X)=P(8,X)+P(2,X)
2250 PRINT P$(1,X);:PRINTTAB(9)"WON ";:PRINT"$";:C1=4:C2=0:CC=P(2,X):GOSUB8:PRINT
2260 GOTO 2310
2270 P(4,X)=P(4,X)+P(2,X)
2280 P(6,X)=P(6,X)+1
2290 P(8,X)=P(8,X)-P(2,X)
2300 PRINT P$(1,X);:PRINTTAB(9)"LOST";:PRINT"$";:C1=4:C2=0:CC=P(2,X):GOSUB8:PRINT
2310 NEXT X
2320 RETURN
2330 REM
2340 REM
2350 REM ************************* 
2360 REM *   BANKS THIRD CARD    * 
2370 REM ************************* 
2380 GOSUB 2730:REM **** PICK CARD 
2390 PP=310
2400 GOSUB 3020:REM **** PRINT CARD
2410 H(6)=L
2420 IF H(6)=>10 THEN H(6)=0
2430 BT=BT+H(6)
2440 IF BT=>10 THEN BT=BT-10
2450 GOTO 1360
2460 REM
2470 REM
2480 REM ************************* 
2490 REM *       BANK WINS       * 
2500 REM ************************* 
2510 PRINT@74,"*BANK WINS*":PRINT
2520 FOR X=1 TO N
2530 IF P$(2,X)="" THEN 2660
2540 IF P$(2,X)="P" THEN 2620:REM **** BET ON PLAYER 
2550 P(3,X)=P(3,X)+P(2,X)
2560 P(7,X)=P(7,X)+P(2,X)*5/100
2570 P(5,X)=P(5,X)+1
2580 P(8,X)=P(8,X)+(P(2,X)-P(2,X)*5/100)
2590 PRINT P$(1,X);:PRINTTAB(9)"WON ";:PRINT"$";:C1=4:C2=0:CC=P(2,X):GOSUB8:PRINT
2600 PRINT" COM";:PRINT"$";:C1=4:C2=2:CC=P(2,X)*5/100:GOSUB8:PRINT
2610 GOTO 2660
2620 P(4,X)=P(4,X)+P(2,X)
2630 P(6,X)=P(6,X)+1
2640 P(8,X)=P(8,X)-P(2,X)
2650 PRINT P$(1,X);:PRINTTAB(9)"LOST";:PRINT"$";:C1=4:C2=0:CC=P(2,X):GOSUB8:PRINT
2660 NEXT X
2670 RETURN
2680 REM
2690 REM
2700 REM ************************* 
2710 REM * ONE CARD FROM 8 DECKS * 
2720 REM ************************* 
2730 L=RND(13):S=RND(32) 
2740 IF C(L,S)=-1 THEN 2730
2750 C(L,S)=-1
2760 ON L GOTO 2770,2780,2790,2800,2810,2820,2830,2840,2850,2860,2870,2880,2890
2770 PC$(1)=" ACE":GOTO 2900
2780 PC$(1)="TWO":GOTO 2900
2790 PC$(1)="THREE ":GOTO 2900
2800 PC$(1)="FOUR":GOTO 2900
2810 PC$(1)="FIVE":GOTO 2900
2820 PC$(1)="SIX":GOTO 2900
2830 PC$(1)="SEVEN":GOTO 2900
2840 PC$(1)="EIGHT":GOTO 2900
2850 PC$(1)="NINE":GOTO 2900
2860 PC$(1)="TEN":GOTO 2900
2870 PC$(1)="JACK":GOTO 2900
2880 PC$(1)="QUEEN":GOTO 2900
2890 PC$(1)="KING"
2900 IF S<9 THEN 2940
2910 IF S>8 AND S<17 THEN 2950
2920 IF S>16 AND S<25 THEN 2960
2930 PC$(2)="CLUBS":RETURN
2940 PC$(2)="SPADES":RETURN
2950 PC$(2)="HEARTS":RETURN
2960 PC$(2)="DIAMONDS":RETURN
2970 REM
2980 REM
2990 REM ************************* 
3000 REM PRINT CARD INSIDE BLANKS* 
3010 REM ************************* 
3020 T=INT((10-(LEN(PC$(1))))/2)
3030 U=INT((10-(LEN(PC$(2))))/2) 
3040 PRINT@PP,BB$;
3050 PRINT@PP+32,BB$;
3060 PRINT@PP+32+T,PC$(1);
3070 PRINT@PP+64,"    OF    ";
3080 PRINT@PP+96,BB$;
3090 PRINT@PP+96+U,PC$(2);
3100 PRINT@PP+128,BB$;
3110 RETURN
4000 REM ****************************
4020 REM *  BACCARAT---VEGAS RULES  *
4030 REM *COPYRIGHT 1982 GEOFF WELLS*
4040 REM *     21-12 EAST AVE.N.    * 
4050 REM *    HAMILTON, ONTARIO     *
4060 REM *     CANADA. 18K 5H2      *
4070 REM **************************** 
