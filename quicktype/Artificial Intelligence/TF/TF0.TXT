1 CLS:PRINT@202,"tf, VER. 2.2":PRINT@264,"JOHN KRUTCH 1981"
100 REM SUPERVISOR
110 GOSUB 190:REM INIT
120 GOSUB 250:REM FIRST LE INPUTS FROM PLAYER
130 GOSUB 310:REM INPUT
140 GOSUB 550:REM PRINTING AND SCOREKEEPING
150 GOSUB 380:REM MATCH-PATTERN UPDATE
160 GOSUB 490:REM LOCATION CALCULATION
170 GOSUB 420
180 GOTO 130
190 REM INITIALIZATION
200 PRINT@484,"HIT ANY KEY TO CONTINUE";:REM RANDOMIZE
201 LE=RND(0):IFINKEY$=""THEN201
210 DIM Q(1111,2)
220 LE=4
230 CLS
240 RETURN
250 REM FIRST LE INPUTS FROM PLAYER
260 FOR I=1 TO LE
270 GOSUB 310
280 MP(I)=CD
290 NEXT I
300 RETURN
310 REM INPUT
320 PRINT@506,"ready";:POKE16895,ASC("?")
330 IP$=INKEY$:IF IP$="T" OR IP$="F" THEN 340
331 GOTO 330
340 IF IP$="T" THEN CD=1:CLS6:GOTO 360
350 IF IP$="F" THEN CD=0:CLS4:GOTO 360
360 SOUND100,1
370 RETURN
380 REM LOCATION UPDATE
390 IF CD=1 THEN Q(LO,1)=Q(LO,1)+1:GOTO 410
400 IF CD=0 THEN Q(LO,2)=Q(LO,2)+1
410 RETURN
420 REM LOCATION CALCULATION
430 A$=""
440 FOR I=1 TO LE
450 A$=A$+MID$(STR$(MP(I)),2)
460 NEXT I
470 LO=VAL(A$)
480 RETURN
490 REM MATCH-PATTERN UPDATE
500 FOR I=1 TO LE-1
510 MP(I)=MP(I+1)
520 NEXT I
530 MP(LE)=CD
540 RETURN
550 REM PRINTING AND SCOREKEEPING
560 PRINT@192,"YOU TYPED: ";IP$
570 PRINT@224,"COMPUTER WAS PREDICTING: ";
580 IF Q(LO,1)>Q(LO,2) THEN TF$="T":T=T+1:PRINTTF$:GOTO 630
590 IF Q(LO,2)>Q(LO,1) THEN TF$="F":F=F+1:PRINTTF$:GOTO 630
600 RAN=RND(2)
610 IF RAN=1 THEN TF$="T":T=T+1:PRINTTF$
620 IF RAN=2 THEN TF$="F":F=F+1:PRINTTF$
630 IF TF$=IP$ THEN NC=NC+1
640 PRINT@256,"TOTAL ENTRIES:"T+F
650 PRINT@288,"CORRECT PREDICTIONS:"NC
670 PRINT@320,"% CORRECT:"INT(((NC/(T+F))*100)*10+.5)/10
690 RETURN
700 REM MC-10 EDITS JIM GERRIE
