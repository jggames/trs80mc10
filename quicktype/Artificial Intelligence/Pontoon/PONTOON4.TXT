0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 PRINT "DO YOU WANT INSTRUCTIONS?":INPUT"(1=YES,0=NO) ";K
25 IF K<>1 THEN 85
30 CLS
50 M$="THIS IS THE GAME OF BLACKJACK, LAS VAGAS STYLE"
55 M$="HERE ARE THE RULES OF THE HOUSE.":GOSUB1
60 M$="THE DEALER MUST HIT ON 16 OR LESS AND WILL STICK ON 17 OR MORE.":GOSUB1
65 M$="YOU MAY SPLIT TWO CARDS IF THEY ARE THE SAME "
70 M$=M$+"AND PLAY ONE HAND WITH EACH OF THEM.":GOSUB1
75 M$="ALSO, YOU MAY DOUBLE YOUR BET AND RECEIVE EXACTLY ONE "
80 M$=M$+"MORE CARD ANY TIME ON YOUR FIRST HIT.":GOSUB1
85 M$="THE COMMANDS ARE 1=HIT 2=DOUBLE 3=SPLIT AND 0=NO HIT":GOSUB1:INPUT"HIT enter TO CONTINUE";M$:CLS
95 IF K<>1 THEN 155
100 M$="WHEN THE DEALER HAS EXPOSED AN ACE HE WILL ASK"
105 M$="YOU FOR AN INSURANCE BET.":GOSUB1:M$="AN INSURANCE BET WILL "
110 M$=M$+"RISK HALF YOUR BET FOR AN AMOUNT EQUAL TO YOUR BET "
115 M$=M$+"IF YOU WIN.":GOSUB1:M$="YOU WIN IF THE DEALER HAS A BLACK JACK "
120 M$=M$+"AND LOSE IF HE DOESN'T.":GOSUB1:M$="THE HOUSE LIMIT IS $500.00":GOSUB1
125 M$="GOOD LUCK. OH ! BY THE WAY, THE DEALER IS NOTED FOR "
130 M$=M$+"DEALING OFF THE BOTTOM OF THE PACK. WATCH HIM VERY CLOSELY.":GOSUB1
140 INPUT"HIT enter TO CONTINUE";M$:CLS
155 K=0:W1=0
165 N=RND(-(PEEK(9)*256+PEEK(10))):N=INT(1945*RND(0)+1)
175 M$="ANY TIME YOU WANT ME TO RESHUFFLE THE CARDS SIMPLY "
185 M$=M$+"TYPE 7777 WHEN I ASK FOR YOUR WAGER AND I'LL BE VERY "
190 M$=M$+"HAPPY TO OBLIGE.":GOSUB1
200 X=INT(10*RND(0)):DIM D(52),E(5),V(5),T(5),W(5)
215 FOR A=1 TO 52:D(A)=0:NEXT A
230 DIM Q(52)
235 FOR A=0 TO 39 STEP 13
240 FOR C=1 TO 13:Q(A+C)=C:NEXT C
250 NEXT A
265 K=K+1
270 FOR P=1 TO 5
275 E(P)=0:V(P)=0:T(P)=0
290 NEXT P
295 V(3)=1
300 PRINT
315 PRINT "WAGER ";:P=1:INPUT W
330 W(2)=W
335 IF W<=0 THEN 1685
340 IF W<=500 THEN 370
345 IF W<>7777 THEN 360
350 GOSUB 1645
355 GOTO 315
360 M$="THAT'S TOO MUCH - THE HOUSE LIMIT IS $500":GOSUB1:GOTO 300
370 M$="I SHOW "
380 GOSUB 865
385 IF E(1)=0 THEN 395
390 V(4)=1
395 V(5)=1
400 GOSUB 865
405 M=X
410 P=2
415 M$="YOUR FIRST CARD IS "
420 GOSUB 865
425 G=X
430 M$="NEXT CARD IS "
435 GOSUB 865
440 IF V(2)>0 THEN 605
445 S=X
450 IF V(3)<>1 THEN 605
455 IF T(P)<>21 THEN 505
460 PRINT "       *****BLACKJACK*****"
465 PRINT "THAT'S GETTING RIDICULOUS !!!"
475 PRINT "MY HOLE CARD WAS",
485 X=M
490 GOSUB 1035
495 W1=W1+1.5*W
500 GOSUB 1335
505 IF V(4)=0 THEN 570
510 PRINT "INSURANCE ANYONE":INPUT"(1=YES,0=NO) ";I
520 IF I=0 THEN 570
530 IF T(1)<>21 THEN 555
535 W1=W1+W
540 M$="YOU WIN $"+STR$(W/2)+" ON YOUR INSURANCE BET":GOSUB1:GOTO 570
555 W1=W1-W/2
560 M$="YOU LOST $"+STR$(W/2)+" ON YOUR INSURANCE BET - I DON'T HAVE A BLACK JACK":GOSUB1
570 IF T(1)<>21 THEN 605
575 PRINT "*****I HAVE BLACKJACK*****"
585 M$="MY HOLE CARD WAS "
590 X=M
595 GOSUB 1035
600 GOTO 1300
605 IF T(P)<=21 THEN 650
610 IF E(P)>0 THEN 640
615 PRINT "YOU BUSTED. ";
620 PRINT "YOUR TOTAL IS";T(P)
625 C1=T(P)-5*(INT(T(P)/5))
630 IF V(2)=1 THEN 775
635 GOTO 1175
640 E(P)=E(P)-1
645 T(P)=T(P)-10
650 IF V(1)=2 THEN 620
655 V(3)=V(3)+1
660 PRINT "HIT ";
665 INPUT V(1)
670 IF V(1)<>3 THEN 830
675 IF V(2)>0 THEN 820
680 IF V(3)<>2 THEN 820
685 IF Q(G)=Q(S) THEN 700
690 PRINT "NOW IS THAT A PAIR ?":GOTO 660
700 V(2)=1
705 IF Q(G)<>1 THEN 715
710 V(1)=2
715 P=3
720 PRINT"PLAY ONE HAND NOW"
725 M$="THE FIRST CARD IS "
730 W(3)=W
735 X=G
740 GOSUB 750
745 GOTO 430
750 GOSUB 990
755 GOSUB 1035
760 V(3)=1
765 T(P)=C
770 RETURN
775 P=2
780 V(2)=2
785 PRINT "PLAY THE SECOND HAND NOW"
790 M$="THE FIRST CARD IS "
795 X=S
800 GOSUB 750
805 IF Q(G)=1 THEN 815
810 V(1)=0
815 GOTO 430
820 PRINT "NO SPLITS -- TRY AGAIN"
825 GOTO 660
830 IF V(1)<>2 THEN 855
835 IF V(3)=2 THEN 850
840 PRINT "TOO LATE TO DOUBLE, CHARLIE":GOTO 660
850 W(P)=2*W(P)
855 IF V(1)>0 THEN 430
860 GOTO 620
865 GOSUB 900
870 T(P)=T(P)+C
875 IF V(5)=0 THEN 890
880 V(5)=0
885 RETURN
890 GOSUB 1035
895 RETURN
900 IF R>=50 THEN 945
905 N=10*(1+ABS(COS(N+W1)))
910 FOR A=1 TO N
915 X=INT(52.999999*RND(Y))
920 IF X=0 THEN 915
925 NEXT A
930 IF D(X)=0 THEN 980
935 R=R+1
940 IF R<50 THEN 900
945 FOR A=1 TO 52
950 IF D(A)=K THEN 960
955 D(A)=0
960 NEXT A
965 R=0
970 PRINT "I RESHUFFLED   ";
975 GOTO 900
980 R=0
985 D(X)=K
990 IF Q(X)<>1 THEN 1010
995 C=11
1000 E(P)=E(P)+1
1005 RETURN
1010 IF Q(X)>10 THEN 1025
1015 C=Q(X)
1020 RETURN
1025 C=10
1030 RETURN
1035 GOSUB 1050
1040 GOSUB 1120
1045 RETURN
1050 IF Q(X)<>1 THEN 1065
1055 M$=M$+"ACE "
1060 RETURN
1065 IF Q(X)>10 THEN 1080
1070 M$=M$+MID$(STR$(Q(X)),2)+" "
1075 RETURN
1080 IF Q(X)>11 THEN 1095
1085 M$=M$+"JACK "
1090 RETURN
1095 IF Q(X)>12 THEN 1110
1100 M$=M$+"QUEEN "
1105 RETURN
1110 M$=M$+"KING "
1115 RETURN
1120 IF X>39 THEN 1145
1125 IF X>26 THEN 1155
1130 IF X>13 THEN 1165
1135 M$=M$+"OF SPADES":GOSUB1
1140 RETURN
1145 M$=M$+"OF CLUBS":GOSUB1
1150 RETURN
1155 M$=M$+"OF HEARTS":GOSUB1
1160 RETURN
1165 M$=M$+"OF DIAMONDS":GOSUB1
1170 RETURN
1175 P=2
1180 M$="MY HOLE CARD WAS "
1185 X=M
1190 GOSUB 1035
1195 IF T(2)<22 THEN 1210
1200 IF V(2)=0 THEN 1300
1205 IF T(3)>21 THEN 1300
1210 P=1
1215 IF T(1)<17 THEN 1360
1220 IF T(1)>17 THEN 1230
1225 IF E(1)>0 THEN 1360
1230 IF T(1)>21 THEN 1375
1235 P=2
1240 PRINT "MY TOTAL IS";T(1)
1245 IF T(P)>21 THEN 1300
1250 IF T(1)>21 THEN 1265
1255 IF T(1)>T(P) THEN 1300
1260 IF T(1)=T(P) THEN 1330
1265 W1=W1+W(P)
1270 IF C1>3 THEN 1445
1275 IF C1>2 THEN 1465
1280 IF C1>1 THEN 1485
1285 IF C1>0 THEN 1505
1290 GOTO 1525
1295 GOTO 1330
1300 W1=W1-W(P)
1305 IF C1>3 THEN 1545
1310 IF C1>2 THEN 1565
1315 IF C1>1 THEN 1585
1320 IF C1>0 THEN 1605
1325 GOTO 1625
1330 IF V(2)>0 THEN 1345
1335 GOSUB 1405
1340 GOTO 265
1345 P=3
1350 V(2)=0
1355 GOTO 1245
1360 M$="I DRAW "
1365 GOSUB 865
1370 GOTO 1210
1375 IF E(1)=0 THEN 1395
1380 E(1)=E(1)-1:T(1)=T(1)-10:GOTO 1210
1395 PRINT "I BUSTED .......":GOTO 1235
1405 IF W1<0 THEN 1425
1410 IF W1=0 THEN 1435
1415 PRINT "YOU'RE AHEAD $";W1:RETURN
1425 PRINT "YOU'RE BEHIND $";INT(-W1*100)/100:RETURN
1435 PRINT "YOU'RE EVEN":RETURN
1445 M$="I MUST HAVE DEALT WRONG":GOSUB1:GOTO 1330
1465 M$="YOU LUCKED OUT AGAIN":GOSUB1:GOTO 1330
1485 M$="YOU MUST HAVE BEEN PEEKING":GOSUB1:GOTO 1330
1505 M$="I COULD LOSE MY JOB THIS WAY":GOSUB1:GOTO 1330
1525 M$="THE CARDS HAVE TURNED AGAINST ME":GOSUB1:GOTO 1330
1545 M$="THE BOTTOM OF THE DECK STRIKES AGAIN":GOSUB1:GOTO 1330
1565 M$="A VICTORY FOR THE GOOD GUYS":GOSUB1:GOTO 1330
1585 M$="YOU CAN'T BEAT SKILL":GOSUB1:GOTO 1330
1605 M$="YOU CAN'T BEAT THEM ALL":GOSUB1:GOTO 1330
1625 M$="BABY GETS A NEW PAIR OF SHOES":GOSUB1:GOTO 1330
1645 FOR M9=1 TO 52:D(M9)=0:NEXT M9
1660 R=0
1665 PRINT
1670 PRINT "I RESHUFFLED"
1675 PRINT
1680 RETURN
1685 PRINT "QUIT? (1=YES,0=NO) ";:INPUT K:IF K=1 THEN END
1686 GOTO 315
1690 REM PONTOON V002A SJD 10-OCT-77
