0 CLS0:CLEAR150:DIMC1,C2,M$,S$(5),N$(13),D(52),P(5),C(5),AC(5),C$(3):M=16384:L1=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
8 PRINT@L1,"     ";
9 L1=L1+32:RETURN
10 DATA "&","*","#","^","-"
20 FORX=1TO5:READS$(X):NEXTX:FORL1=1TO31:B$=B$+CHR$(128):NEXT
30 GOSUB7000
45 CLS0:NG=0:PT=0:CT=0
50 GOSUB8000
75 REM PRINT PLAYER'S HAND
80 L=257
90 FORC1=1TO2:GOSUB1000:P(C1)=Z:C=P(C1):GOSUB500:PT=PT+T:NEXT
100 L=1:FORC1=1TO2:S=5:GOSUB2000:NEXT
105 REM PRINT COMPUTER'S HAND
110 L=1
120 GOSUB1000:C(1)=Z:C=C(1):GOSUB500:CT=CT+T
130 S=5:GOSUB2000
150 PRINT@0,;:M$="COMPUTER'S HAND":GOSUB7
160 PRINT@256,;:M$="YOUR HAND":GOSUB7
170 GOSUB6000
200 L=269:K=3
205 PRINT@480,B$;:PRINT@480,;:M$="ANOTHER CARD (Y/N)?":GOSUB7
210 R$=INKEY$:R=RND(10):IFR$=""THEN210
220 IFR$="N"THENGOSUB7050:GOTO242
225 IFR$<>"Y"THEN210
230 GOSUB1000:P(K)=Z:C=P(K):GOSUB500
240 PT=PT+T
242 FORX=1TOK
244 IFPT>21AND(P(X)-1)/13=INT((P(X)-1)/13)ANDAC(X)<>1THENPT=PT-10:AC(X)=1
246 NEXTX
247 IFPT>21THENPRINT@480,B$;:PRINT@480,;:M$="YOU BUSTED!!!":GOSUB7:SOUND1,10:GOTO385
250 K=K+1:IFK<6AND R$<>"N"THEN205
255 L=7:K=2:FORX=1TO5:AC(X)=0:NEXTX
260 GOSUB1000:C(K)=Z:C=C(K):GOSUB500
270 CT=CT+T
272 FORX=1TOK
274 IFCT>21AND(C(X)-1)/13=INT((C(X)-1)/13)ANDAC(X)<>1THENCT=CT-10:AC(X)=1
276 NEXTX
277 IF CT>=PT THEN 360
280 IFCT<17ANDK<5THENK=K+1:GOTO260
360 IF CT<22 AND PT<=CT THEN 380
370 PRINT@480,B$;:PRINT@480,;:M$="CONGRATULATIONS WINNER!":GOSUB7
375 GW=GW+1:GOTO390
380 PRINT@480,B$;:PRINT@480,;:M$="TOUGH LUCK, KID.":GOSUB7:SOUND10,5:SOUND1,5
385 GL=GL+1
390 GOSUB6000
400 PRINT@224,B$;:PRINT@224,;:M$="ANOTHER GAME (Y/N)?":GOSUB7
410 R$=INKEY$:IFR$=""THEN410
420 IFR$="Y"THEN45
430 IFR$="N"THEN900
440 GOTO410
500 GOSUB4000:GOSUB2000
520 RETURN
900 PRINT@224,B$;:PRINT@224,;:M$="ARE YOU SURE YOU WANT TO QUIT?":GOSUB7
910 R$=INKEY$:IFR$=""THEN910
920 IFR$="Y"THENEND
930 IFR$="N"THEN45
940 GOTO910
1000 REM PICK A CARD
1002 IFKC=0THENGOSUB7000
1005 Z=RND(52)
1010 IFD(Z)=0THEN1005
1020 D(Z)=0
1025 KC=KC-1
1030 RETURN
1999 REM PRINT SUITS
2000 S$=S$(S):L1=L+32:IFS=5THENGOSUB9141:GOTO2070
2005 ONNGOSUB9001,9021,9031,9041,9051,9061,9071,9081,9091,9101,9111,9121,9131
2070 L1=0:L=L+6:RETURN
4000 REM COMPUTE NUMBER AND SUIT
4005 S=INT((C-1)/13)+1
4010 N=C-(S*13-13)
4015 REM COMPUTE POINT VALUE
4020 IFN=11ORN=12ORN=13THENT=10:GOTO4030
4025 T=N
4030 IFN=1THENT=11
4040 RETURN
6000 PRINT@21,;:M$="WINS:"+STR$(GL):GOSUB7:PRINT@277,;:M$="WINS:"+STR$(GW):GOSUB7
6010 RETURN
7000 PRINT@235,;:M$="SHUFFLING...":GOSUB7
7005 FORDE=1TO52:D(DE)=DE:IFRND(5)=1THENSOUND200+RND(25),1
7010 NEXTDE:KC=52
7015 FORDE=1TO5:IFC(DE)<>0THEND(C(DE))=0:KC=KC-1
7020 IFP(DE)<>0THEND(P(DE))=0:KC=KC-1
7025 NEXTDE
7030 PRINT@224,B$;
7040 RETURN
7050 PRINT@480,B$;
7060 RETURN
8000 FORDE=1TO5:P(DE)=0:C(DE)=0:AC(DE)=0:NEXTDE
8010 RETURN

9001 PRINT@L1,"A    ";:GOSUB9
9002 GOSUB8
9003 PRINT@L1,"  "S$"  ";:GOSUB9
9004 GOSUB8
9005 GOSUB8
9006 PRINT@L1,"    A";:RETURN

9021 PRINT@L1,"2    ";:GOSUB9
9022 PRINT@L1,"  "S$"  ";:GOSUB9
9023 GOSUB8
9024 GOSUB8
9025 PRINT@L1,"  "S$"  ";:GOSUB9
9026 PRINT@L1,"    2";:RETURN

9031 PRINT@L1,"3    ";:GOSUB9
9032 PRINT@L1,"  "S$"  ";:GOSUB9
9033 GOSUB8
9034 PRINT@L1,"  "S$"  ";:GOSUB9
9035 GOSUB8
9036 PRINT@L1,"  "S$" 3";:RETURN

9041 PRINT@L1,"4    ";:GOSUB9
9042 PRINT@L1," "S$" "S$" ";:GOSUB9
9043 GOSUB8
9044 GOSUB8
9045 PRINT@L1," "S$" "S$" ";:GOSUB9
9046 PRINT@L1,"    4";:RETURN

9051 PRINT@L1,"5    ";:GOSUB9
9052 PRINT@L1," "S$" "S$" ";:GOSUB9
9053 GOSUB8
9054 PRINT@L1,"  "S$"  ";:GOSUB9
9055 GOSUB8
9056 PRINT@L1," "S$" "S$"5";:RETURN

9061 PRINT@L1,"6    ";:GOSUB9
9062 PRINT@L1," "S$" "S$" ";:GOSUB9
9063 GOSUB8
9064 PRINT@L1," "S$" "S$" ";:GOSUB9
9065 GOSUB8
9066 PRINT@L1," "S$" "S$"6";:RETURN

9071 PRINT@L1,"7    ";:GOSUB9
9072 PRINT@L1," "S$" "S$" ";:GOSUB9
9073 PRINT@L1,"  "S$"  ";:GOSUB9
9074 PRINT@L1," "S$" "S$" ";:GOSUB9
9075 GOSUB8
9076 PRINT@L1," "S$" "S$"7";:RETURN

9081 PRINT@L1,"8    ";:GOSUB9
9082 PRINT@L1," "S$" "S$" ";:GOSUB9
9083 PRINT@L1,"  "S$"  ";:GOSUB9
9084 PRINT@L1," "S$" "S$" ";:GOSUB9
9085 PRINT@L1,"  "S$"  ";:GOSUB9
9086 PRINT@L1," "S$" "S$"8";:RETURN

9091 PRINT@L1,"9    ";:GOSUB9
9092 PRINT@L1," "S$" "S$" ";:GOSUB9
9093 PRINT@L1,"  "S$"  ";:GOSUB9
9094 PRINT@L1," "S$" "S$" ";:GOSUB9
9095 PRINT@L1," "S$" "S$" ";:GOSUB9
9096 PRINT@L1," "S$" "S$"9";:RETURN

9101 PRINT@L1,"1"S$" "S$" ";:GOSUB9
9102 PRINT@L1,"0 "S$"  ";:GOSUB9
9103 PRINT@L1," "S$" "S$" ";:GOSUB9
9104 PRINT@L1,"  "S$"  ";:GOSUB9
9105 PRINT@L1," "S$" "S$"1";:GOSUB9
9106 PRINT@L1," "S$" "S$"0";:RETURN

9111 PRINT@L1,"J    ";:GOSUB9
9112 PRINT@L1,S$"/-"CHR$(92)" ";:GOSUB9
9113 PRINT@L1," !"CHR$(34)"! ";:GOSUB9
9114 PRINT@L1," "CHR$(92)"Q/ ";:GOSUB9
9115 PRINT@L1," +-+"S$;:GOSUB9
9116 PRINT@L1," !?!J";;:RETURN

9121 PRINT@L1,"Q    ";:GOSUB9
9122 PRINT@L1,S$">=< ";:GOSUB9
9123 PRINT@L1," !"CHR$(34)"! ";:GOSUB9
9124 PRINT@L1," (.) ";:GOSUB9
9125 PRINT@L1,"  H "S$;:GOSUB9
9126 PRINT@L1," ! !Q";:RETURN

9131 PRINT@L1,"K    ";:GOSUB9
9132 PRINT@L1,S$"iii ";:GOSUB9
9133 PRINT@L1," I"CHR$(34)"I ";:GOSUB9
9134 PRINT@L1," "CHR$(92)"M/ ";:GOSUB9
9135 PRINT@L1," +$+"S$;:GOSUB9
9136 PRINT@L1," ! !K";:RETURN

9141 PRINT@L1,"+---+";:GOSUB9
9142 PRINT@L1,"!   !";:GOSUB9
9143 PRINT@L1,"!   !";:GOSUB9
9144 PRINT@L1,"!   !";:GOSUB9
9145 PRINT@L1,"!   !";:GOSUB9
9146 PRINT@L1,"+---+";:RETURN

