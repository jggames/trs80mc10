0 CLEAR500:DIMCC,ZZ,M$,Z$:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 REM FUZZY RITA
20 GOSUB 1380:REM INITIALIZE
30 GOSUB 1160:REM OUTPUT OPTIONS
40 GOSUB 1250:REM DISCRIMINATION OPTIONS
50 GOSUB 140:REM QUESTION USER
60 GOSUB 480:REM MAKE DECISION AND UPDATE KNOWLEDGE BASE
70 PRINT "PRESS <ENTER> TO CONTINUE"
80 IF X$<>"" THEN INPUT I$:GOTO 50
90 M$="TRAINING OR ANY KEY THEN <ENTER> TO USE RITA":GOSUB1
110 INPUT X$:GOTO 50
120 END
130 REM **************************
140 REM QUESTION USER
150 CLS
160 PRINT "HERE ARE THE THE SUBJECTS I":PRINT"CAN DISCRIMINATE BETWEEN"
170 PRINT
180 FOR J=1 TO TT
190 M$=" >  "+A$(J):GOSUB1
200 NEXT J
210 GOSUB 1500
220 IF X$="" THEN M$="THINK OF ONE THEN PRESS <ENTER>":GOSUB1
230 IF X$<>"" THEN M$="I AM READY NOW TO DETERMINE WHICH ONE YOU HAVE":GOSUB1
240 IF X$="" THEN INPUT J$
250 ADD=.5
260 FOR J=1 TO DQ
270 ADD=ADD+ADD
280 GOSUB 1500
290 IF X$<>"" AND TT>2 THEN 390:REM CHECK IF QUESTION CAN BE JUMPED
300 M$="ENTER A NUMBER FROM 1 (TRUE) TO 0 (FALSE) ($ TO END RUN)":GOSUB1
320 PRINT:M$=E$(J):GOSUB1
330 INPUT H$:IF H$="$" THEN PRINT:PRINT "THANK YOU":PRINT:END
340 C(J)=VAL(H$)
350 C(J)=ADD*C(J)
360 NEXT J
370 RETURN
380 REM *******************************
390 REM CHECK IF QUESTION CAN BE JUMPED
400 JUMP=1
410 FOR W=1 TO TT
420 IF ABS(B(W,J)-B(1,J))>.7 THEN JUMP=0
430 NEXT W
440 IF JUMP=0 THEN 300
450 C(JUMP)=B(W,J)
460 GOTO 360
470 REM *************
480 REM MAKE DECISION
490 FOR J=1 TO TT
500 D(J)=0:E(J)=0:F(J)=0
510 NEXT J
520 ADD=.5
530 FOR J=1 TO TT
540 ADD=ADD+ADD
550 FOR X=1 TO DQ
560 REM PLAY WITH VALUES IN NEXT THREE LINES FOR MOST EFFICIENT RESULTS
570 IF C(X)=B(J,X) THEN D(J)=D(J)+1
580 IF ABS(C(X)-B(J,X))<.6*ADD THEN E(J)=E(J)+.4
590 IF ABS(C(X)-B(J,X))<1.2*ADD THEN F(J)=F(J)+.1
600 NEXT X
610 NEXT J
620 A1=1:A2=1:A3=1
630 F1=1:F2=1:F3=1
640 FOR J=1 TO TT
650 IF D(J)>F1 THEN F1=D(J):A1=J
660 IF E(J)>F2 THEN F2=E(J):A2=J
670 IF F(J)>F3 THEN F3=F(J):A3=J
680 NEXT J
690 REM ** ANNOUNCE RESULT **
700 PRINT
710 CFLG=0
720 M$="THE MOST LIKELY RESULT IS "+A$(A1):GOSUB1
730 IF A2<>A1 THEN M$="THE NEXT MOST LIKELY IS "+A$(A2):GOSUB1:CFLG=1
740 IF A3<>A2 AND A3<>A1 THEN M$="THE NEXT MOST LIKELY IS "+A$(A3):GOSUB1:CFLG=2
750 PRINT
760 M$="IS THE MOST LIKELY RESULT CORRECT (Y OR N)?":GOSUB1
770 INPUT F$
780 IF F$<>"Y" AND F$<>"N" THEN 770
790 IF F$="Y" AND X$<>"" THEN RETURN
800 IF F$="Y" THEN 980
810 IF TT=2 AND A1=1 THEN A1=2:GOTO 980
820 IF TT=2 THEN A1=1:GOTO 980
830 IF CFLG=0 THEN 890
840 M$="IS MY SECOND CHOICE CORRECT (Y OR N)?":GOSUB1
850 INPUT F$
860 IF F$="N" THEN 890
870 IF CFLG=1 THEN A1=A2:GOTO 980
880 IF CFLG=2 THEN A1=A3:GOTO 980
890 GOSUB 1500
900 FOR J=1 TO TT
910 M$=MID$(STR$(J),2)+"- "+A$(J):GOSUB1
920 NEXT J
930 PRINT
940 M$="WHICH IS THE CORRECT ONE?":GOSUB1
950 INPUT A1
960 IF A1<1 OR A1>TT THEN 950
970 REM ** EDUCATING RITA ** (UPDATE KNOWLEDGE BASE)
980 FOR J=1 TO DQ
990 IF B(A1,J)<>0 THEN B(A1,J)=(C(J)+5*B(A1,J))/6
1000 IF B(A1,J)=0 THEN B(A1,J)=C(J)
1010 B(A1,J)=INT(10*B(A1,J))/10
1020 NEXT J
1030 PRINT
1040 IF U$="" THEN RETURN
1050 RO=1:FOR J=1 TO TT
1060 GOSUB 1500:GOSUB1600
1070 M$=A$(J):GOSUB1:GOSUB1600
1080 PRINT:GOSUB1600
1090 FOR K=1 TO DQ
1100 M$=E$(K)+STR$(B(J,K)):GOSUB1:GOSUB1600
1110 NEXT K
1120 NEXT J
1130 PRINT
1140 RETURN
1150 REM **************
1160 REM OUTPUT OPTIONS
1170 TT=0
1180 TT=TT+1
1190 GOSUB 1500
1200 M$="ENTER OUTPUT OPTION NUMBER"+STR$(TT)+" (PRESS <ENTER> TO END)":GOSUB1
1210 INPUT A$(TT)
1220 IF A$(TT)="" OR TT=51 THEN TT=TT-1:RETURN
1230 GOTO 1180
1240 REM ************************
1250 REM DISCRIMINATION QUESTIONS
1260 CLS
1270 FOR J=1 TO TT
1280 M$=A$(J):GOSUB1
1290 NEXT J
1300 DQ=0
1310 DQ=DQ+1
1320 GOSUB 1500
1330 M$="ENTER QUESTION NUMBER"+STR$(DQ)+" (PRESS <ENTER> TO END)":GOSUB1
1340 INPUT E$(DQ)
1350 IF E$(DQ)="" OR DQ=51 THEN DQ=DQ-1:RETURN
1360 GOTO 1310
1370 REM **************
1380 REM INITIALIZATION
1390 CLS
1400 REM REDUCE ARRAYS IN NEXT LINE IN ACCORDANCE WITH YOUR NEEDS
1410 DIM A$(50),B(50,50),C(50),D(50),E$(50),F(50),E(50)
1420 X$=""
1430 M$="PRESS ANY KEY, THEN <ENTER> IF YOU WANT TO SEE THE UPDATED KNOWLEDGE BASE "
1450 M$=M$+"AFTER EACH RUN;":GOSUB1
1460 M$="JUST PRESS <ENTER> IF YOU DON'T.":GOSUB1
1470 INPUT U$
1480 CLS
1490 RETURN
1500 PRINT "--------------------------------";
1510 RETURN
1550 PRINT"HIT ANY KEY TO CONTINUE";
1560 M$=INKEY$:IFM$=""THEN1560
1570 FORCC=1TO23:PRINTCHR$(8);:NEXT:RETURN
1600 RO=RO+1:IFRO>=15THENRO=1:GOSUB1550
1610 RETURN
