10 REM     TOWERS OF HANOI
20 REM   BY KIMBALL M RUDEEN
30 CLEAR 1000
40 REM USE ONLY INT VARIABLES IF POSSIBLE
50 INPUT "NUMBER OF DISKS";N
60 DIM ND(N),X$(N),Y$(N),Z$(N)
70 X$(1)="A"
80 Y$(1)="C"
90 Z$(1)="B"
100 LV=1
110 ND(1)=N
120 NM=0
130 GOSUB 1000
140 PRINT"PROBLEM COMPLETE"
150 END
1000 REM RECUSIVE SUBROUTINE
1010 LV=LV+1
1020 ND(LV)=ND(LV-1)-1
1030 REM SECTION 1
1040 X$(LV)=X$(LV-1)
1050 Y$(LV)=Z$(LV-1)
1060 Z$(LV)=Y$(LV-1)
1070 IF ND(LV)=1 THEN NM=NM+1:PRINT "MOVE";NM;":"X$(LV);" TO ";Y$(LV):GOTO 1080
1071 GOSUB1000
1080 REM SECTION 2
1090 NM=NM+1:PRINT "MOVE";NM;":";X$(LV-1);" TO ";Y$(LV-1)
1100 REM SECTION 3
1110 X$(LV)=Z$(LV-1)
1120 Y$(LV)=Y$(LV-1)
1130 Z$(LV)=X$(LV-1)
1140 IF ND(LV)=1 THEN NM=NM+1:PRINT "MOVE";NM;":"X$(LV);" TO ";Y$(LV):GOTO 1150
1141 GOSUB1000
1150 LV=LV-1
1160 RETURN
1170 REM CREATIVE COMPUTING
1180 REM NOVEMBER 1982
1180 REM MC-10 EDITS JIM GERRIE
1190 REM     TOWERS OF HANOI
1200 REM   BY KIMBALL M RUDEEN
