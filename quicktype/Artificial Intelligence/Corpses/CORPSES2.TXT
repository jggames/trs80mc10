10 CLS
20 CLEAR 200
25 N=16
30 CLS
40 PRINT TAB((32-11)/2)"THE GAME OF"
41 PRINT TAB((32-17)/2)"EXQUISITE CORPSES"
45 GOSUB 500
50 A$="                               SURREALIST SENTENCE CONSTRUCTION"
60 B=LEN(A$)-1
70 FOR I=1 TO 63
80     PRINT@352,LEFT$(A$,32)
90     FOR J=1 TO 5:NEXT J
100    A$=RIGHT$(A$,B)+LEFT$(A$,1)
110 NEXT I
115 CLS:IFRND(2)=1THENPOKE49151,64:GOTO118
116 POKE49151,0
118 C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
120 C=0                  :REM LETTER-PER-LINE COUNTER
125 C1=C1+1              :REM SENTENCE COUNTER
130 FOR I=1 TO 6
135     HA=RND(N)
138     WO$=WO$(I,HA):IF RIGHT$(WO$,1)="*" THEN GOSUB 470:GOTO140
139 AS=0
140     ON I GOSUB 200,300,400,450,200,300
145     WO$=" "+WO$ : L=LEN(WO$)
150     C=C+LEN(WO$)
160     IF C>31 THEN C=LEN(WO$) : PRINT
165     FOR J=1 TO L
170         PRINT MID$(WO$,J,1);
175         FOR K=1 TO 20
180 NEXT K,J,I
185 PRINT:PRINT
190 B$=INKEY$:IF B$="" THEN 190
195 IF C1<3 THEN 120
196 C1=0:GOTO 115
200 REM ''' ADJECTIVE SUBROUTINE '''
205 DE=RND(5) : GE=RND(1) : NO=RND(2)
215 DET$=DE$(DE,GE,NO)
220 IF DET$="A" AND AS=1 THEN DET$="AN"
230 WO$=DET$+" "+WO$
290 RETURN
300 REM '''''' NOUN SUBROUTINE '''
310 FI$=EN$(1,NO)
320 IF AS=1 THEN IF NO=2 THEN FI$="ES"
350 WO$=WO$+FI$
390 RETURN
400 REM '''' VERB SUBROUTINE ''''
405 TE=RND(3)     :REM TENSE
410 IF AS=1 THEN P=0:GOTO 415
411 P=1           :REM TRANSITIVE
415 IF TE=3 THEN WO$=VE$(TE,NO)+" "+WO$:GOTO420
416 WO$=WO$+VE$(TE,NO)
420 IF RIGHT$(WO$,3)="EED" THEN WO$=LEFT$(WO$,LEN(WO$)-2)+"D"
425 RETURN
450 REM ''' ADVERB SUBROUTINE '''
460 IF P=1 THEN P1=RND(10) : WO$=WO$+" "+PR$(P1)
465 RETURN
470 REM --- TREATMENT OF PARTICULAR CASES (*) ---
472 AS=1                   :REM IDENTIFICATION
475 D=LEN(WO$)-1           :REM AND REMOVAL OF
480 WO$=LEFT$(WO$,D)       :REM THE ASTERISK (*)
490 RETURN
500 REM ***************************
510 REM  FABRICATION DE TABLEAUX
520 REM ***************************
600 REM ** TABLE OF DETERMINERS ***
610 DIM DE$(5,2,2)
620 FOR I=0 TO 5 : FOR J=1 TO 2 : FOR K=1 TO 2
630     READ DE$(I,J,K)
640 NEXT K,J,I
700 REM *** TABLE OF ENDINGS ***
710 DIM EN$(2,2)
720 FOR I=1 TO 2: FOR J=1 TO 2
730     READ EN$(I,J)
740 NEXT J,I
800 REM *** TABLE OF VERBS ***
810 DIM VE$(3,2)
820 FOR I=1 TO 3: FOR J=1 TO 2
830     READ VE$(I,J)
840 NEXT J,I
850 REM *** TABLE OF PREPOSITIONS ***
860 DIM PR$(10)
870 FOR I=1 TO 10
880     READ PR$(I)
890 NEXT I
900 REM *** TABLE OF WORDS ***
910 DIM WO$(6,N)
920 FOR I=1 TO 6: FOR J=1 TO N
930     READ WO$(I,J)
940 NEXT J,I
950 RETURN
960 REM *************
980 REM --- LISTS ---
990 REM TO INTRODUCE ADDITIONAL WORDS,
991 REM DON'T FORGET TO MODIFY THE
992 REM THE VALUE OF THE VARIABLE N.
995 REM--------------------
1000 REM --- DETERMINERS --
1010 DATA THEIR,THEIR,THEIR,THEIR,HIS,HIS,HER,HER
1011 DATA THE,THE,THE,THE,A,SOME,A,SOME
1012 DATA THIS,THESE,THIS,THESE
1013 DATA EACH,MANY,ANY,SOME
1020 REM---- ENDINGS -----
1030 DATA "","S","","S"
1040 REM---- VERBS -------
1050 DATA "S","","ED","ED","WILL","WILL"
1060 REM-------- PREPOSITIONS ----
1070 DATA BEFORE,WITH,AGAINST,TO,BEHIND
1080 DATA IN FRONT OF,FOR,UNDER,OVER,TOWARD
1090 REM -------------------
1095 REM ------ WORDS ------
1097 REM EACH SECTION HAS THE SAME NUMBER OF WORDS.
1100 REM ---ADJECTIVES (I=1) ------
1110 DATA CLAUSTROPHOBIC,CZECHOSLOVAKIAN,FLAT,WET,ELECTRONIC*
1120 DATA SNOBBISH,CHUBBY,FLAKY,HYSTERICAL,PETTY,RED
1130 DATA POLYGAMOUS,BALD,FUNNY,FUGITIVE,DISOBEDIENT
1200 REM --- NOUNS (I=2) ---
1205 REM "*" = PLURALIZE WITH "ES" ...
1210 DATA THUG,KLEPTOMANIAC,HOT AIR BALLOON,ASS*,SKELETON
1220 DATA SOCK,CAP,SACRISTAN,GIRAFFE,PENDULUM,FIREFIGHTER
1230 DATA GHOST,CHIMPANZEE,STRAINER,BALLERINA,CIGARETTE
1300 REM --- VERBS (I=3) ---
1305 REM "*" = TRANSITIVE ...
1310 DATA SPUTTER,ENTANGLE*,MEOW,COO,CARESS*
1320 DATA UNROLL*,GRAZE,SNIFF*,PHONE*,EXAMINE*
1330 DATA CHATTER,CONSUME*,CLEAVE*,BURN*,GROPE*,CLEAN*
1400 REM --- ADVERBS (I=4) ---
1410 DATA THIS VERY DAY,NOISILY,QUICKLY,CHEERFULLY
1420 DATA (SO THEY SAY),SURREPTITIOUSLY,ONCE AGAIN,BEFORE MIDNIGHT
1430 DATA - IT'S SCANDALOUS -,FROM NOW ON,EXTRAVAGANTLY
1440 DATA DISDAINFULLY,SNEAKILY,OFTEN
1450 DATA PARSIMONIOUSLY,HALF-HEARTEDLY
1500 REM -- ADJECTIVES (I=5) --
1510 DATA CLEAN,NYMPHOMANIAC,DRIPPING,BLOODTHIRSTY
1520 DATA CANDY-PINK,BULGARIAN,DEMONIC,BEATNIK
1530 DATA LACY,AQUATIC,PORCELAIN,PERPLEXED
1540 DATA GRIM,SMOKING,SUBLIME,HAIRY
1600 REM--- NOUNS (I=6) -------
1610 DATA PERCOLATOR,BICYCLE,GRAPEFRUIT,NAIL,CORPSE
1620 DATA CONCIERGE,SKIRT,DANDELION,PRESIDENT,COFFEE MACHINE
1630 DATA PIG,CAT,SAUCEPAN,MAIDEN,MOUSTACHE,KILT
1700 REM-----------------------
1710 REM FROM HEBDOGICIEL 42,
1711 REM 27 JULY 1984,
1712 REM BY PHILIPPE HENRI
1713 REM TRANLATED FROM FRENCH
1714 REM BY NICK MONTFORT AND
1715 REM ARIANE SAVOIE
1716 REM MC-10 EDITS JIM GERRIE
