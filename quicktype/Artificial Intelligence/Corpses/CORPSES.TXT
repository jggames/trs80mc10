5 ' FROM HEBDOGICIEL 42, 27 JULY 1984, BY PHILIPPE HENRI
6 ' TRANLATED FROM FRENCH BY NICK MONTFORT AND ARIANE SAVOIE
10 CLS
20 CLEAR 200
25 N=16
30 PRINT CHR$(23)
40 PRINT "        THE GAME OF"
41 PRINT "      EXQUISITE CORPSE"
45 GOSUB 500
50 A$=STRING$(31," ")+"SURREALIST SENTENCE CONSTRUCTION"
60 B=LEN(A$)-1
70 FOR I=1 TO 63
80     PRINT@704, LEFT$(A$,32)
90     FOR J=1 TO 5 : NEXT J
100    A$=RIGHT$(A$,B)+LEFT$(A$,1)
110 NEXT I
115 CLS
117 PRINT CHR$(23)
118 RANDOM
120 C=0                      ' LETTER-PER-LINE COUNTER
125 C1=C1+1                  ' SENTENCE COUNTER
130 FOR I=1 TO 6
135     HA=RND(N)
138     WO$=WO$(I,HA)
139     IF RIGHT$(WO$,1)="*" GOSUB 470 ELSE AS=0
140     ON I GOSUB 200,300,400,450,200,300
145     WO$=" "+WO$ : L=LEN(WO$)
150     C=C+LEN(WO$)
160     IF C>31 THEN C=LEN(WO$) : PRINT
165     FOR J=1 TO L
170         PRINT MID$(WO$,J,1);
175         FOR K=1 TO 20
180 NEXT K,J,I
185 PRINT : PRINT
190 B$=INKEY$ : IF B$="" THEN 190
195 IF C1<3 THEN 120 ELSE C1=0 : GOTO 115
200 ''''''''''''''' ADJECTIVE SUBROUTINE '''''''''''''''''''''
205 DE=RND(5) : GE=RND(1) : NO=RND(2)
215 DET$=DE$(DE,GE,NO)
220 IF DET$="A" AND AS=1 THEN DET$="AN"
230 WO$=DET$+" "+WO$
290 RETURN
300 ''''''''''''''' NOUN SUBROUTINE ''''''''''''''''''''''''''
310 FI$=EN$(1,NO)
320 IF AS=1 THEN IF NO=2 THEN FI$="ES"
350 WO$=WO$+FI$
390 RETURN
400 ''''''''''''''' VERB SUBROUTINE '''''''''''''''''''''''''''
405 TE=RND(3)                            ' TENSE
410 IF AS=1 THEN P=0 ELSE P=1            ' TRANSITIVE
415 IF TE=3 THEN WO$=VE$(TE,NO)+" "+WO$ ELSE WO$=WO$+VE$(TE,NO)
420 IF RIGHT$(WO$,3)="EED" THEN WO$=LEFT$(WO$,LEN(WO$)-2)+"D"
425 RETURN
450 ''''''''''''''' ADVERB SUBROUTINE '''''''''''''''''''''''''
460 IF P=1 THEN P1=RND(10) : WO$=WO$+" "+PR$(P1)
465 RETURN
470 '------------ TREATMENT OF PARTICULAR CASES (*) -----------
472 AS=1                                 ' IDENTIFICATION
475 D=LEN(WO$)-1                         ' AND REMOVAL OF
480 WO$=LEFT$(WO$,D)                     ' THE ASTERISK (*)
490 RETURN
500 '**********************************************************
510 '                 FABRICATION DE TABLEAUX
520 '**********************************************************
600 '**************** TABLE OF DETERMINERS ********************
610 DIM DE$(5,2,2)
620 FOR I=0 TO 5 : FOR J=1 TO 2 : FOR K=1 TO 2
630     READ DE$(I,J,K)
640 NEXT K,J,I
700 '**************** TABLE OF ENDINGS ************************
710 DIM EN$(2,2)
720 FOR I=1 TO 2: FOR J=1 TO 2
730     READ EN$(I,J)
740 NEXT J,I
800 '**************** TABLE OF VERBS **************************
810 DIM VE$(3,2)
820 FOR I=1 TO 3: FOR J=1 TO 2
830     READ VE$(I,J)
840 NEXT J,I
850 '**************** TABLE OF PREPOSITIONS *******************
860 DIM PR$(10)
870 FOR I=1 TO 10
880     READ PR$(I)
890 NEXT I
900 '**************** TABLE OF WORDS **************************
910 DIM WO$(6,N)
920 FOR I=1 TO 6: FOR J=1 TO N
930     READ WO$(I,J)
940 NEXT J,I
950 RETURN
960 '*********************************************************
980 '------------------------ LISTS --------------------------
990 'TO INTRODUCE ADDITIONAL WORDS, DON'T FORGET TO MODIFY THE
991 'THE VALUE OF THE VARIABLE N.
995 '---------------------------------------------------------
1000 '---------- DETERMINERS ---------------------------------
1010 DATA THEIR,THEIR,THEIR,THEIR,HIS,HIS,HER,HER
1011 DATA THE,THE,THE,THE,A,SOME,A,SOME
1012 DATA THIS,THESE,THIS,THESE
1013 DATA EACH,MANY,ANY,SOME
1020 '---------- ENDINGS -------------------------------------
1030 DATA "","S","","S"
1040 '---------- VERBS ---------------------------------------
1050 DATA "S","","ED","ED","WILL","WILL"
1060 '---------- PREPOSITIONS --------------------------------
1070 DATA BEFORE,WITH,AGAINST,TO,BEHIND
1080 DATA IN FRONT OF,FOR,UNDER,OVER,TOWARD
1090 '--------------------------------------------------------
1095 '------------------- WORDS ------------------------------
1097 'EACH SECTION HAS THE SAME NUMBER OF WORDS.
1100 '----------- ADJECTIVES (I=1) ---------------------------
1110 DATA CLAUSTROPHOBIC,CZECHOSLOVAKIAN,FLAT,WET,ELECTRONIC*
1120 DATA SNOBBISH,CHUBBY,FLAKY,HYSTERICAL,PETTY,RED
1130 DATA POLYGAMOUS,BALD,FUNNY,FUGITIVE,DISOBEDIENT
1200 '----------- NOUNS (I=2) --------------------------------
1205 ' "*" = PLURALIZE WITH "ES" ...
1210 DATA THUG,KLEPTOMANIAC,HOT AIR BALLOON,ASS*,SKELETON
1220 DATA SOCK,CAP,SACRISTAN,GIRAFFE,PENDULUM,FIREFIGHTER
1230 DATA GHOST,CHIMPANZEE,STRAINER,BALLERINA,CIGARETTE
1300 '----------- VERBS (I=3) --------------------------------
1305 ' "*" = TRANSITIVE ...
1310 DATA SPUTTER,ENTANGLE*,MEOW,COO,CARESS*
1320 DATA UNROLL*,GRAZE,SNIFF*,PHONE*,EXAMINE*
1330 DATA CHATTER,CONSUME*,CLEAVE*,BURN*,GROPE*,CLEAN*
1400 '----------- ADVERBS (I=4) ------------------------------
1410 DATA THIS VERY DAY,NOISILY,QUICKLY,CHEERFULLY
1420 DATA (SO THEY SAY),SURREPTITIOUSLY,ONCE AGAIN,BEFORE MIDNIGHT
1430 DATA - IT'S SCANDALOUS -,FROM NOW ON,EXTRAVAGANTLY
1440 DATA DISDAINFULLY,SNEAKILY,OFTEN
1450 DATA PARSIMONIOUSLY,HALF-HEARTEDLY
1500 '---------- ADJECTIVES (I=5) ----------------------------
1510 DATA CLEAN,NYMPHOMANIAC,DRIPPING,BLOODTHIRSTY
1520 DATA CANDY-PINK,BULGARIAN,DEMONIC,BEATNIK
1530 DATA LACY,AQUATIC,PORCELAIN,PERPLEXED
1540 DATA GRIM,SMOKING,SUBLIME,HAIRY
1600 '---------- NOUNS (I=6) ---------------------------------
1610 DATA PERCOLATOR,BICYCLE,GRAPEFRUIT,NAIL,CORPSE
1620 DATA CONCIERGE,SKIRT,DANDELION,PRESIDENT,COFFEE MACHINE
1630 DATA PIG,CAT,SAUCEPAN,MAIDEN,MOUSTACHE,KILT
1700 '--------------------------------------------------------