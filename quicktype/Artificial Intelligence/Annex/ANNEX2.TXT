2 CLEAR:CLS0
3 DIMT(9,9):DIMT4(9,9):DIMT6(9,9):DIMT2(50):DIMT3(50):DIMT5(50)
5 PRINT"°°°°°°°°ediciel°presents";
8 H$="€c€€ediciel€matra€hachette€"
9 PRINT@448,H$;
10 POKE16832,40:POKE16834,41:POKE16859,49:POKE16860,57:POKE16861,56:POKE16862,52
15 W$="ßÜßÐßÒÐßÐßÒÐßÐßÜÐßÒÑßÐßÐßÜßÐßÒÐß"
17 X$="ßÓßÐßÜÛßÐßÜÛßÐßÓÐÐßßÐÐßÐßÐßÐßÜÛß"
19 C$="ßÐßÐßÐÔßÐßÐÔßÐßÓÐßØÔßÐßÐßÓßÐßÐÔß"
20 PRINT@128,W$:PRINT@160,X$:PRINT@192,C$
34 PRINT@224,"                                 ¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬"
36 PRINT@288," ££££££££££££££££££££££££££££££"
37 FORG=-255TO255STEP8:SOUNDABS(G),2:NEXTG
39 CLS:POS=1
41 A$="CHOOSE YOUR DESIRED COLOUR":B$="YOUR PIECES WILL BE":C$="GREEN  (NUMBER 1)":D$="YELLOW (NUMBER 2)"
42 E$="RED    (NUMBER 3)":F$="CYAN   (NUMBER 4)":G$="ENTER THE NUMBER OF YOUR CHOICE:"
43 PRINT@98,A$:PRINT@162,B$
44 PRINT@198,C$:PRINT@230,D$:PRINT@262,E$:PRINT@294,F$
45 PRINT@352,G$:PRINT:INPUT"         NUMBER";A$:IFA$=""THEN45
46 CO=ASC(A$)-48:IFCO<1ORCO>4ORLEN(A$)>1THEN39
47 CLS:PRINT@299,"(FROM 1-7)":PRINT@200," ":INPUT"  ENTER LEVEL OF DIFFICULTY";A$:IFA$=""THEN47
48 N2=ASC(A$)-48:IFN2<1ORN2>7ORLEN(A$)>1THEN47
50 IFCO=4THENCO=6
51 IFCO=3THENCO=4
54 CLS
55 FORZ=0TO3
56 FORE=0TO14STEP2
60 PRINT@33+96*Z+E,"ÁÃ";:PRINT@65+96*Z+E,"ÄÌ";:PRINT@97+96*Z+E,"ÅÏ";
75 O=Z*(E+1)+1:P=1:SOUNDO,P
80 NEXTE:NEXTZ
83 FORZ=0TO12
84 PRINT@49+32*Z,"…";
85 NEXTZ
87 M$="€€a€b€c€d€e€f€g€h…"
88 PRINT@416,M$
90 PRINT@452," LEVEL ":PRINT@459,N2
100 FORZ=0TO3
110 PRINT@32+96*Z,"€";:POKE16448+96*Z,2*Z+49:POKE16480+96*Z,2*Z+50
120 NEXTZ
150 POS=1
210 FORX=0TO9:T(0,X)=-2:T(X,0)=-2:T(X,9)=-2:T(9,X)=-2
211 NEXTX
213 FORX=1TO8:FORY=1TO8:
215 T(X,Y)=0
218 NEXTY:NEXTX
220 T(5,5)=-1:T(4,4)=-1:T(4,5)=1:T(5,4)=1
221 E=5:D=5:BL=1:GOSUB3000
224 E=4:D=4:GOSUB3000
226 E=4:D=5:BL=-1:GOSUB3000
228 E=5:D=4:GOSUB3000
229 S$=""
230 A$="  ARE YOU":B$=" OKAY WITH  ":C$="THIS INITIAL":D$=" ARRANGEMENT? ":E$="(Y/N) ?"
231 PRINT@115,A$:PRINT@147,B$:PRINT@179,C$:PRINT@210,D$;
232 PRINT@277,E$;
234 S$=INKEY$:IFS$=""THEN232
236 PRINT@115," ":PRINT@147," ":PRINT@179," ":PRINT@210," ":PRINT@277," "
238 IFS$="N"THENS=-1
240 IFS$<>"N"THEN260
255 E=5:D=5:BL=-1:GOSUB3000
256 T(5,5)=-S:T(4,4)=-S:T(4,5)=S:T(5,4)=S
257 E=4:D=4:GOSUB3000
258 E=4:D=5:BL=1:GOSUB3000
259 E=5:D=4:GOSUB3000
260 FORG=1TO1000:NEXTG:PRINT@212," "
261 PRINT@179," ":PRINT@51,"YOUR TURN"
262 PRINT@115," LINE?":A$=INKEY$:IFA$=""THEN262
263 I=ASC(A$)-48:IFA$="P"THENI=0:PRINT@115,"YOU GIVE UP":PRINT@179,"YOUR TURN.":PRINT@51," ":BL=-1:GOTO275
264 IFI>8ORI<1THENPRINT@51," P=PASS  ":FORZZ=1TO2500:NEXT:GOTO261
265 PRINT@124,A$
266 PRINT@179," COLUMN? ":B$=INKEY$:IFB$=""THEN266
267 J=ASC(B$)-64:IFJ>8ORJ<1THEN266
268 PRINT@188,B$:FORG=1TO120:NEXT
269 PRINT@212,"           ":SOUND60,2:SOUND12,4:SOUND1,3:BL=-1
270 PRINT@116,"YOUR MOVE ":PRINT@180,"        ";:PRINT@183,A$:PRINT@185,B$
271 PRINT@51,"        ":FORG=1TO300:NEXTG
272 IFT(I,J)<>0THENSOUND200,3:PRINT@212,"NOT VALID ":GOTO260
275 FIN=1:IFC1<>0ANDI=0ANDBL=-1THENVE=1:GOTO300
276 IFI=0ANDC1=0THENPRINT@274,"MC-10'S TURN:"
278 C1=1
295 IFI<>0THEN950
298 BL=1
300 FORV=0TO50:T2(V)=0:NEXTV
305 V=0:RES=0
310 FORI=1TO8:FORJ=1TO8
320 RES=0
330 IFT(I,J)<>0GOTO750
350 IFT(I+1,J)=BLTHENX=I+1:M=1:Y=J:N=0:GOSUB2000
360 RES=RES+CPT:CPT=0
380 IFT(I,J+1)=BLTHENX=I:M=0:Y=J+1:N=1:GOSUB2000
390 RES=RES+CPT:CPT=0
410 IFT(I-1,J)=BLTHENX=I-1:M=-1:Y=J:N=0:GOSUB2000
420 RES=RES+CPT:CPT=0
440 IFT(I,J-1)=BLTHENX=I:M=0:Y=J-1:N=-1:GOSUB2000
450 RES=RES+CPT:CPT=0
470 IFT(I+1,J+1)=BLTHENX=I+1:M=1:Y=J+1:N=1:GOSUB2000
480 RES=RES+CPT:CPT=0
500 IFT(I-1,J-1)=BLTHENX=I-1:M=-1:Y=J-1:N=-1:GOSUB2000
510 RES=RES+CPT:CPT=0
530 IFT(I+1,J-1)=BLTHENX=I+1:M=1:Y=J-1:N=-1:GOSUB2000
540 RES=RES+CPT:CPT=0
560 IFT(I-1,J+1)=BLTHENX=I-1:M=-1:Y=J+1:N=1:GOSUB2000
570 RES=RES+CPT:CPT=0
690 IFRES=0GOTO750
700 T2(V)=(I*10+J)*10000+RES+5000
720 IFN2>1THENGOSUB1155
730 V=V+1
750 NEXTJ:NEXTI
760 IFVE=1ANDT2(0)<>0THENPRINT@212,"CHEATER!":VE=0:SOUND2,10:SOUND120,6:SOUND180,4:SOUND50,12:SOUND1,20:GOTO260
762 IFVE=1ANDT2(0)=0THENVE=2:GOTO298
766 IFVE=2THENVE=0:IFT2(0)=0THEN60000
780 SOUNDRND(255),1
800 Z=0:Z1=T2(0)-INT(T2(0)/10000)*10000
819 X=V
820 FORV=1TOX
830 V1=T2(V)-INT(T2(V)/10000)*10000
840 IFZ1>V1GOTO880
845 H=RND(2)
850 IF((Z1=V1)AND(H=2))THEN880
860 Z1=V1:Z=V
880 NEXTV
885 IF(C1=1)AND(N2>2)THEN1210
886 W1=Z1
887 IFFIN=0ANDN2>2ANDZ1=0THENZ1=5000:T3(ZA)=T3(ZA)-150*BL
888 IFFIN=0ANDC1=4THENZ1=5000:C1=3
890 IF(FIN=0)AND(N2>2)THEN1216
900 I=INT(T2(Z)/100000)
905 J=INT(T2(Z)/10000)-I*10
910 V=X
930 IFABS(T(I,J))<>0ANDBL=-1THENSOUND200,3:PRINT@212,"NOT VALID ":POS=0:GOTO260
935 POS=0
940 IFI=0ANDJ=0ANDBL=1ANDFIN=1THEN1100
950 POS=0
1000 IFT(I-1,J)=(1*BL)THENX=I-1:M=-1:Y=J:N=0:GOSUB2000
1005 IFCPT<>0THEND=I-1:E=J:GOSUB2045
1010 IFT(I+1,J)=(1*BL)THENX=I+1:M=1:Y=J:N=0:GOSUB2000
1015 IFCPT<>0THEND=I+1:E=J:GOSUB2045
1020 IFT(I,J-1)=BLTHENX=I:Y=J-1:M=0:N=-1:GOSUB2000
1025 IFCPT<>0THEND=I:E=J-1:GOSUB2045
1030 IFT(I,J+1)=BLTHENX=I:M=0:Y=J+1:N=1:GOSUB2000
1035 IFCPT<>0THEND=I:E=J+1:GOSUB2045
1040 IFT(I+1,J+1)=BLTHENX=I+1:Y=J+1:M=1:N=1:GOSUB2000
1045 IFCPT<>0THEND=I+1:E=J+1:GOSUB2045
1050 IFT(I-1,J-1)=(1*BL)THENX=I-1:M=-1:Y=J-1:N=-1:GOSUB2000
1055 IFCPT<>0THEND=I-1:E=J-1:GOSUB2045
1060 IFT(I+1,J-1)=BLTHENX=I+1:M=1:Y=J-1:N=-1:GOSUB2000
1065 IFCPT<>0THEND=I+1:E=J-1:GOSUB2045
1070 IFT(I-1,J+1)=BLTHENX=I-1:M=-1:Y=J+1:N=1:GOSUB2000
1075 IFCPT<>0THEND=I-1:E=J+1:GOSUB2045
1079 IFPOS<>0THENT(I,J)=-BL
1080 IF(N2>2)AND(FIN=0)THEN1230
1090 IFPOS=0ANDBL=-1THENSOUND200,3:PRINT@212,"IMPOSSIBLE"
1100 IFPOS=0ANDBL=1THENPRINT@274,"    MC-10":PRINT@307,"HAS NO VALID":PRINT@339,"   MOVE.":GOTO260
1103 IFPOS<>0ANDBL=-1THENPRINT@274,"MC-10'S TURN:":PRINT@307," ":PRINT@339," "
1105 IFPOS<>0THENT(I,J)=-BL:D=I:E=J:GOSUB3000
1106 IFBL=-1THEN1110
1107 PRINT@274,"MC-10'S MOVE:":PRINT@311,I
1108 PRINT@310,I:PRINT@313,CHR$(64+J)
1109 SOUND3,3:SOUND90,1
1110 IF(POS=0ANDBL=-1)OR(BL=1)THEN260
1150 BL=1:GOTO300
1151 END
1155 IF(J=2ANDI=2ANDT(1,1)=-BL)OR(J=2ANDI=7ANDT(8,1)=-BL)THENT2(V)=T2(V)+110
1157 IF(J=7ANDI=2ANDT(1,8)=-BL)OR(J=7ANDI=7ANDT(8,8)=-BL)THENT2(V)=T2(V)+110
1158 IF(N2=4ORN2=6)AND(J=2ORJ=7ORI=2ORI=7)THENT2(V)=T2(V)-0.5:GOTO1165
1160 IF(J=2ORJ=7)OR(I=2ORI=7)THENT2(V)=T2(V)-10
1165 IF(J=2ORJ=7)AND(I=2ORI=7)THENT2(V)=T2(V)-70
1170 IF(I=1ORI=8)AND(J=1ORJ=8)THENT2(V)=T2(V)+120
1180 IF((J=3ORJ=6)AND(I=3ORJ=6))THENT2(V)=T2(V)+0.6
1200 RETURN
1210 FORG=0TOV:T3(G)=T2(G):NEXTG
1212 FIN=0:ZA=0
1213 FORI=0TO9:FORJ=0TO9:T4(I,J)=T(I,J):NEXTJ:NEXTI
1214 C1=3
1215 GOTO1222
1216 T3(ZA)=T3(ZA)-Z1+5000
1217 IF(N2>4)AND(C1=3)THEN1240
1218 FORI=0TO9:FORJ=0TO9:T(I,J)=T4(I,J):NEXTJ:NEXTI
1219 BL=-BL:G4=0
1220 ZA=ZA+1:C1=3
1222 I=INT(T3(ZA)/100000)
1224 J=INT(T3(ZA)/10000)-I*10
1226 IFT3(ZA)<>0THEN950
1227 FORG=0TOZA:T2(G)=T3(G):NEXTG
1229 FIN=1:V=ZA-1:GOTO800
1230 BL=-BL
1232 IFN2>4ANDC1=3THENZA=ZA:FORG=0TO9:FORG1=0TO9:T6(G,G1)=T(G,G1):NEXTG1:NEXTG
1235 GOTO300
1240 IFG4=0THENGOSUB1300
1241 IFG4=1THEN1350
1242 I=INT(T2(Z)/100000)
1243 J=INT(T2(Z)/10000)-I*10
1245 C1=4:TG=0:G4=1
1246 IFW1<>0THEN950
1250 BL=-1:GOTO1218
1300 TG=0
1310 FORG=0TO50:T5(G)=0:NEXTG
1320 FORG=0TOX
1325 IFN2=6ORN2=7THENT5(TG)=T2(TG):TG=TG+1
1328 IFN2=5AND((T2(G)-(INT(T2(G)/10000))*10000)=W1)AND(Z<>G)THENT5(TG)=T2(TG):TG=TG+1
1330 NEXTG
1332 G4=2:W0=100000
1340 RETURN
1350 IFW1<W0ANDW1<>0THENW0=W1
1355 Z=T5(TG)
1360 IFZ=0ANDW0=100000THENW0=5000
1362 TG=TG+1
1365 IFZ=0THENT3(ZA)=T3(ZA)+W0-5000:G4=0:BL=-1:GOTO1218
1370 FORG=0TO9:FORG1=0TO9:T(G,G1)=T6(G,G1):NEXTG1:NEXTG
1380 I=INT(Z/100000)
1382 J=INT(Z/10000)-10*I
1385 C1=4
1386 BL=-1
1390 GOTO950
2000 CPT=0
2010 CPT=CPT+1:Y=Y+N:X=X+M
2012 IF(N2=1)OR(CPT>1)THEN2020
2014 IF(I=1ORI=8)AND(T(I,J+1)*BL<=0)AND(T(I,J-1)*BL<=0)THENCPT=CPT+20
2016 IF((ABS(M)=1)AND(N=0)AND(J=1ORJ=8))OR((ABS(N)=1)AND(M=0)AND(I=1ORI=8))THENCPT=CPT+19
2018 IF(J=1ORJ=8)AND(T(I+1,J)*BL<=0)AND(T(I-1,J)*BL<=0)THENCPT=CPT+20
2020 G=T(X,Y)
2025 IF(G=0)OR(G=-2)THENCPT=0:RETURN
2030 IFG=-BLTHENRETURN
2035 GOTO2010
2039 REM RETOURNEMENT DE PIONS
2040 D=D-M:E=E-N
2045 POS=1
2046 CPT=0
2050 T(D,E)=-1*BL
2051 IFFIN=0GOTO2060
2053 D1=D:E1=E
2055 GOSUB3000
2058 D=D1:E=E1
2060 D=D+M:E=E+N
2070 IF(D=X)AND(E=Y)THENRETURN
2080 GOTO2050
2999 REM COLORIAGE D'UN PION
3000 Z=INT((D-1)/2)
3002 E=E*2-2
3003 IFINT((D-1)/2)=(D-1)/2THEN3020
3004 SOUND90,1
3005 PRINT@97+96*Z+E,CHR$(133+(BL+CO)*16);
3010 PRINT@98+96*Z+E,CHR$(143+(BL+CO)*16);:RETURN
3020 PRINT@33+96*Z+E,CHR$(129+(BL+CO)*16);
3022 PRINT@34+96*Z+E,CHR$(131+(BL+CO)*16);
3024 PRINT@65+96*Z+E,CHR$(132+(BL+CO)*16);
3026 PRINT@66+96*Z+E,CHR$(140+(BL+CO)*16);
3030 SOUND3,1:RETURN
60000 CLS
60002 FORI=1TO30:PRINT@I,"Ÿ";:NEXTI
60003 FORI=32 TO448STEP32:PRINT@I,"Ÿ";:NEXTI
60004 FORI=63TO479STEP32:PRINT@I,"Ÿ";:NEXTI
60005 PRINT@481,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
60006 PRINT@41,"ÿÿÿÿÿÿÿÿÿÿÿÿ"
60007 PRINT@457,"ÿÿÿÿÿÿÿÿÿÿÿÿ"
60008 PRINT@428,"¿¿¿¿¿¿":PRINT@447,"Ÿ";:PRINT@479,"Ÿ";
60009 PRINT@76,"¿¿¿¿¿¿":PRINT@63,"Ÿ";:PRINT@95,"Ÿ";
60010 PRINT@137," GAME OVER!"
60015 PRINT@159,"Ÿ";
60020 PRINT@198,"HERE ARE YOUR SCORES:"
60021 PRINT@223,"Ÿ";
60030 FORI=1TO8:FORJ=1TO8
60040 IFT(I,J)=1THENS0=S0+1
60050 IFT(I,J)=-1THENS1=S1+1
60055 SOUND(I*J*3),1
60060 NEXTJ:NEXTI
60080 C$="YOU HAVE":D$="MC-10 HAS"
60085 PRINT@265,C$:PRINT@275,S0:PRINT@297,D$:PRINT@307,S1
60087 PRINT@287,"Ÿ";:PRINT@319,"Ÿ";
60090 IFS0>S1THENPRINT@358,"YOU ARE THE VICTOR!!"
60091 PRINT@383,"Ÿ";
60092 IFS0>S1THENSO=80:FORG=1TO80:SO=SO+(-2)^RND(4):IFSO<160ANDSO>1THENSOUNDSO,1:NEXTG
60095 IFS0<S1THENPRINT@358,"MC-10 IS THE VICTOR!"
60096 PRINT@383,"Ÿ";
60097 IFS0<S1THENSO=80:FORG=1TO140:SOUNDRND(255),RND(5):NEXTG
60098 FORG=1TO2000:NEXTG
60099 CLS
60100 PRINT@232,"PLAY ANOTHER GAME ?"
60105 PRINT@302,"(Y/N)"
60110 Z$=INKEY$:IFZ$=""THEN60100
60115 S0=0:S1=0:C1=0
60120 IFZ$="Y"THEN39
60130 CLS:END
