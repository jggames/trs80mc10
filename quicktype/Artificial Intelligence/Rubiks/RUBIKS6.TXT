REM MC-10 VERSION
0 GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(WW$,CC,1):IFZ$<"!"THEN?MID$(WW$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXTCC:WW$="":RETURN

4 GOSUB9
5 PRINT@32*4,"ﬂﬂﬂHITﬂﬂ";:PRINT@32*5,"ﬂﬂENTERﬂ";:IFINKEY$<>CHR$(13)THEN5
6 GOSUB9:RETURN

7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(Z$):POKECC+ZZ,ASC(MID$(Z$,ZZ))AND63:NEXTZZ:?@CC-M+ZZ,:RETURN

9 FORCC=4TO11:PRINT@32*CC,"ﬂﬂﬂﬂﬂﬂﬂﬂ";:NEXT:RETURN
10 RESTORE:FORCC=1TO120:READZZ:NEXT:RETURN:RESTORE310
20 RESTORE:FORCC=1TO144:READZZ:NEXT:RETURN:RESTORE320

100 CLS0:CLEAR1000:DIMCC,ZZ,Z$,X1,Y1,WW$:M=16384:GOTO1000
REM ----- SUBROUTINE TO DRAW CUBES
104 GOSUB4
105 PRINT@448,"":PRINT@480,"                               ";:POKE16895,96:PRINT@448,;:RETURN
110 CLS6:QQ=-1:GOTO120
115 QQ=1:PRINT@.,;
120 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁ¡‘upﬂﬂbackÿ≤¢›leftﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁÄÕ»‘ﬂﬂﬂﬂﬂﬂÿµ∏≠†›ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁ¡ÀÄ¡À‘ﬂﬂﬂﬂÿ∑∞≤¢•¢›ﬂﬂ";
123 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬁ¡Ä»¡Ä»¡‘ﬂﬂÿ≤∏µ∏≠†®¢›ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ–Õ»ÄÕ»ÄÕ»’ﬂµ∏∑∞≤¢•¢≠’ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂıÚ¡ÀÄ¡ÀÄá’ﬂ∞≤∏µ∏≠†®¢’ﬂ";
126 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ¯Ú»¡Ä»Çà’ﬂµ∏∑Äóí•¢≠’ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂıÚ˝ÄÕ»Öàá’ﬂ∞≤∏íîÄí®¢’ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ¯ÚıÚáêÇà’ﬂµ∏îûÄîûÄ≠’ﬂ";
129 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂıÚ˝¯àÖàá’ﬂÄóíÄóíÄóí’ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“¯ÚıÚáÄÇà◊ﬂ€îÄíîÄíî—ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“˝¯àÖà◊ﬂﬂﬂ€îûÄîû—ﬂﬂﬂ";
132 PRINT"ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ“ıÚáÄ◊ﬂﬂﬂﬂﬂ€†óí—ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂfront“¯à◊rightﬂﬂ€î—downﬂ";:IFQQ=1THENGOSUB1810:RETURN
133 RETURN
REM ---- ROTATIONAL DATA
155 DATA9,33,45,21,44,43,10,24,15,51
160 DATA39,3,50,49,4,42,38,37,16,6
165 DATA7,31,43,19,35,34,33,32,1,13
170 DATA49,37,50,53,52,51,14,17,16,15
175 DATA32,44,20,8,19,9,48,47,2,14
180 DATA50,38,1,15,42,41,3,54,53,37
185 DATA11,35,47,23,46,7,8,36,5,17
190 DATA53,41,40,13,14,54,18,52,1,2
195 DATA6,18,54,42,12,36,48,24,22,23
200 DATA20,21,2,3,4,5,38,39,40,41
205 DATA4,16,52,40,10,34,46,22,11,12
210 DATA45,31,5,6,51,13,17,18,39,49
REM ------- ROTATION SUBROUTINE
215 FORI=1TO5:READP,Q,R,S:ONUGOTO220,230,225
220 T=C(S):C(S)=C(R):C(R)=C(Q):C(Q)=C(P):C(P)=T:GOTO235
225 T=C(P):C(P)=C(Q):C(Q)=C(R):C(R)=C(S):C(S)=T:GOTO235
230 T=C(S):C(S)=C(Q):C(Q)=T:T=C(P):C(P)=C(R):C(R)=T
235 NEXTI:RETURN
240 GOSUB115:REM FORI=1TO1000:NEXTI
245 IFLEFT$(D$,1)="Q"THEND$="QUIT"
246 D=LEN(D$)-2:IFD<0THENRETURN
250 M$=LEFT$(D$,1):U=VAL(MID$(D$,2,1)):D$=MID$(D$,3,D)
255 IFM$="D"THENRESTORE:FORCC=1TO80:READZZ:NEXT:GOTO290
260 IFM$="U"THENRESTORE:FORCC=1TO20:READZZ:NEXT:GOTO290
265 IFM$="R"THENRESTORE:GOTO290
270 IFM$="L"THENRESTORE:FORCC=1TO60:READZZ:NEXT:GOTO290
275 IFM$="F"THENRESTORE:FORCC=1TO40:READZZ:NEXT:GOTO290
280 IFM$="B"THENRESTORE:FORCC=1TO100:READZZ:NEXT:GOTO290
283 IFM$="Q"THEN1050
285 GOTO295
290 IFU>.THENIFU<4THENGOSUB215:ONFGOTO245,240
295 GOSUB105:PRINTM$;RIGHT$(STR$(U),1);
300 PRINT" IS AN INVALID MOVE";
305 FORI=1TO2500:NEXTI:RETURN
REM ---------------- CORNER IDENT. DATA
310 DATA6,39,4,18,40,5,42,38,3,54,41,2
315 DATA1,14,53,13,17,52,49,16,51,37,15,50
REM ---------------- EDGIE IDENT. DATA
320 DATA8,47,9,44,10,45,11,46,36,23,12,22
325 DATA24,21,48,20,7,35,31,34,43,33,19,32
REM ---------------- MOVE BUILD SUBROUTINES TO LINE 905
330 D$="D2"
335 D$=D$+I$+"1D2"+I$+"3"
340 D$=D$+"D2"
345 D$=D$+F$+"3D1"+F$+"1":RETURN
350 D$=F$+"3D3"+F$+"1"
355 D$=D$+"D2"
360 D$=D$+I$+"1D3"+I$+"3":RETURN
365 D$="D3"
370 D$=D$+I$+"1"
375 D$=D$+"U1"+H$+"3U3":RETURN
380 D$="D3"
385 D$=D$+I$+"2":RETURN
390 D$="D1":GOTO360
395 D$="D3":GOTO360
400 D$=F$+"3"
405 D$=D$+I$+"3":RETURN
410 D$="U3"+F$+"3U1":RETURN
415 D$=G$+"1"
420 D$=D$+"U2"+G$+"3U2":RETURN
425 D$="D1":GOTO385
430 D$=H$+"1D2"+H$+"3":GOTO360
435 D$=G$+"3D2"+G$+"1":GOTO355
440 D$=H$+"3D3"+H$+"1":GOTO360
445 D$=I$+"3D2"+I$+"1":GOTO360
450 D$="D1":GOTO335
455 D$=F$+"3"
460 D$=D$+"U3"+F$+"1U1":RETURN
465 D$="D2":GOTO370
470 D$="D1":GOTO370
475 D$="D2":GOTO385
480 D$=H$+"1":GOTO375
485 D$=G$+"1"
490 D$=D$+"U1"+H$+"1U3":RETURN
495 D$=H$+"1"
500 D$=D$+I$+"1":RETURN
505 D$=I$+"3":GOTO375
510 D$=G$+"3D2"+G$+"1":GOTO345
515 D$="D1":GOTO345
520 D$="D3":GOTO345
525 D$="D3":GOTO335
530 D$="U2"+G$+"1U2"
535 RETURN
540 D$=G$+"1D1"+G$+"3":GOTO355
545 D$=F$+"1D2"+F$+"3":GOTO345
550 D$=G$+"1D1"+G$+"3":GOTO345
555 D$=H$+"1D2"+H$+"3":GOTO340
560 D$=I$+"1D1"+I$+"3":GOTO340
565 D$="":ONCMGOTO535,390,360,395,355,330,535,405,410,420
570 ONCM-10GOTO375,425,430,350,435,440,445,450,455,365,465
575 ONCM-21GOTO470,370,475,535,535,535,535,535,535,480,400
580 ONCM-32GOTO485,495,505,385,510,340,515,345,520,525,415
585 ONCM-43GOTO530,490,500,460,380,540,545,550,555,560,335
590 D$="":ONCMGOTO430,355,390,360,395,525,480,375,405,410
595 ONCM-10GOTO420,475,540,445,350,435,440,330,535,370,365
600 ONCM-21GOTO465,470,380,535,535,535,535,535,535,415,505
605 ONCM-32GOTO400,485,495,425,535,520,340,515,345,335,455
610 ONCM-43GOTO460,530,490,500,385,510,560,545,550,555,450
615 D$="":ONCMGOTO540,395,355,390,360,335,415,420,375,405
620 ONCM-10GOTO410,380,510,440,445,350,435,525,480,470,370
625 ONCM-21GOTO365,465,385,535,535,535,535,535,535,455,495
630 ONCM-32GOTO505,400,485,475,430,345,520,340,515,450,535
635 ONCM-43GOTO500,460,530,490,425,535,555,560,545,550,330
640 D$="":ONCMGOTO510,360,395,355,390,450,455,410,420,375
645 ONCM-10GOTO405,385,535,435,440,445,350,335,415,465,470
650 ONCM-21GOTO370,365,425,535,535,535,535,535,535,535,485
655 ONCM-32GOTO495,505,400,380,540,515,345,520,340,330,480
660 ONCM-43GOTO490,500,460,530,475,430,550,555,560,545,525
665 D$="":ONSGOTO690,690,695,685,695,695,695,685
670 ONS-8GOTO685,695,685,690,680,690,695,695,695
675 ONS-17GOTO685,685,695,695,685,685,690,695,680
680 F$="B":G$="L":GOTO700
685 F$="L":G$="F":GOTO700
690 F$="F":G$="R":GOTO700
695 F$="R":G$="B"
700 D$=F$+"1D3"
705 D$=D$+F$+"3"+G$+"1"+F$+"3"+G$+"3"+F$+"1"
710 RETURN
715 D$=F$+"1D2"+F$+"3D3"+F$+"1D2":GOTO705
720 D$="":R=2:ONCM-7GOTO765,770,775,780,750
725 R=1:ONCM-19GOTO750,755,760,745
730 R=2:IFCM=24THEN755
735 IFCM=36THEN745
740 ONCM-43GOTO785,790,795,800,750
745 D$="L1U1D3B1U1D3R3U2D2L3":ONRGOTO775,790
750 D$="F1U1D3L1U1D3B3U2D2F3":ONRGOTO780,795
755 D$="R1U1D3F1U1D3L3U2D2R3":ONRGOTO765,800
760 D$="B1U1D3R1U1D3F3U2D2B3":ONRGOTO770,785
765 D$=D$+J$+"F1U1D3L1U1D3B3U2D2F3"+K$:RETURN
770 D$=D$+J$+"D1R1U1D3F1U1D3L3U2D2R3D3"+K$:RETURN
775 D$=D$+J$+"F3U2D2B1U1D3R3U1D3F1"+K$:RETURN
780 D$=D$+J$+"F1U1D3L2U1D3B2U2D2F3"+K$:RETURN
785 D$=D$+J$+"F1U1D3L3U1D3B1U2D2F3"+K$:RETURN
790 D$=D$+J$+"F1U3D1B2U3D1L2U2D2F3"+K$:RETURN
795 D$=D$+J$+"F1U2D2B1U3D1L3U3D1F3"+K$:RETURN
800 D$=D$+J$+"D3L1U1D3B3U1D3R1U2D2L3D1"+K$:RETURN
805 ONR-1GOTO810,815,820,825,830,835,840,845,850,855,860
810 D$="R2U3D1B2U1D3":RETURN
815 D$="B2U1D3R2U3D1":RETURN
820 D$="U2D2F2U2D2B2":RETURN
825 D$="F2U3D1R2U1D3":RETURN
830 D$="L2U3D1F2U1D3":RETURN
835 D$="R2U1D3F2U3D1":RETURN
840 D$="B2U3D1L2U1D3":RETURN
845 D$="R2L2U1D3F2B2U3D1":RETURN
850 D$="F2U1D3L2U3D1":RETURN
855 D$="L2U1D3B2U3D1":RETURN
860 D$="R2U2D2L2U2D2":RETURN
865 D$="R3L1U1R3L1F1R3L1D1R2L2U1R3L1F1R3L1D1R3L1B2":RETURN
870 D$="R1L1U1R3L1F1R3L1D1R2L2U1R3L1F1R3L1D1R3L1B2R2":RETURN
875 D$="B3F1D1B3F1L1B3F1U1B2F2D1B3F1L1B3F1U1B3F1R2":RETURN
880 D$="B3F1U1B3F1R1B3F1D1B2F2U1B3F1R1B3F1D1B3F1L2":RETURN
885 D$="R3L3U1R3L1F1R3L1D1R2L2U1R3L1F1R3L1D1R3L1B2L2":RETURN
890 D$=""
895 D$=D$+"R3L1D1R3L1B1R3L1U1R2L2D1R3L1B1"
900 D$=D$+"R3L1U1R3L1F2":RETURN
905 GOSUB865:GOSUB104:WW=WW+1:PRINTD$;:GOTO895
REM ---------------- INITIALIZATION (START)
1000 DIMC(54),W(8),B(8)
REM W(1)=4:W(2)=1:W(3)=2:W(4)=3:W(5)=8:W(6)=0:W(7)=5
REM B(4)=1:B(1)=2:B(2)=3:B(3)=4:B(8)=5:B(0)=6:B(5)=7
1005 W(1)=8:W(2)=2:W(3)=5:W(4)=3:W(5)=4:W(6)=0:W(7)=1
1010 B(8)=1:B(2)=2:B(5)=3:B(3)=4:B(4)=5:B(0)=6:B(1)=7

1020 FORZ=1TO49STEP6:C(Z)=3
1025 C(Z+1)=1:C(Z+2)=7:C(Z+3)=5:C(Z+4)=4
1030 C(Z+5)=2:NEXTZ
1035 GOSUB110
1050 PRINT@33,;:Z$="RUBIK'S":GOSUB7
1055 PRINT@65,;:Z$="   CUBE":GOSUB7
1060 PRINT@32*4,"ﬂﬂrogerﬂ";:PRINT@32*5,"ﬂsafford";:PRINT@32*7,"ﬂ1-HELP ";
1065 PRINT@32*8,"ﬂ2-MIX  ";:PRINT@32*9,"ﬂ& PLAY ";:PRINT@32*10,"ﬂ3-MATCH";:PRINT@32*11,"ﬂ& SOLVE";
1070 GOSUB105:INPUT"SELECT";D$
1075 IFD$="1"THEN2200
1080 IFD$<>"2"ANDD$<>"3"THEN1035
1085 GOSUB105
1090 INPUT"DISPLAY EACH MOVE? (Y/N)";E$
1095 E=1:IFE$="Y"THENE=2
1100 IFD$="3"THEN1300
REM ----------------MIX AND PLAY ROUTINE
1200 GOSUB105:INPUT"HOW MANY MIXES";U
1205 M$="RUBLDF":D$="":F=1:IFU>31THENU=31
1210 FORJ=1TOU:D=INT(RND(0)*6+1)
1215 D$=D$+MID$(M$,D,1)
1220 D$=D$+RIGHT$(STR$(INT(RND(0)*3+1)),1)
1225 NEXTJ:GOSUB245:GOSUB115
1230 GOSUB105
1235 INPUT"ENTER MOVE OR 'Q' TO END";D$
1240 F=E:GOSUB245:IFF=1THENGOSUB115
1245 GOTO1230
REM ---------------- MATCH AND SOLVE ROUTINE
1300 GOSUB1805:WW=16
REM ---------------- SOLVE TOP EDGIES
1305 F=E:F$="R":G$="B":H$="L":I$="F":FOREG=1TO4
1310 GOSUB20:EX=C(25+EG)*C(25):FOREY=1TO12:READR,S
1315 IFEX<>C(R)*C(S)THENNEXTEY:EY=C(25):GOTO2000
1320 EY=12:NEXTEY:CM=R:IFC(S)=C(25)THENCM=S
1325 ONEGGOSUB590,615,640,565:IFD$=""THEN1335
1330 GOSUB104:PRINTD$;:GOSUB245:GOSUB115
1335 D$=F$:F$=G$:G$=H$:H$=I$:I$=D$:NEXTEG
REM ---------------- SOLVE TOP CORNERS
1340 FOREG=1TO4:GOSUB10:EX=C(25)*C(26)*C(29)
1345 IFEG<4THENEX=C(25)*C(25+EG)*C(26+EG)
1350 FOREY=1TO8:READR,S,T:Q=C(R)*C(S)*C(T)
1355 IFEX<>QTHENNEXTEY:EY=C(25):GOTO2045
1360 EY=8:NEXTEY:CM=T:IFC(R)=C(25)THENCM=R
1365 IFC(S)=C(25)THENCM=S
1370 ONEGGOSUB590,615,640,565:IFD$=""THEN1380
1375 GOSUB104:PRINTD$;:GOSUB245:GOSUB115
1380 IFC(EG+25)<>C(EG+49)THENEY=C(25):GOTO2040
1385 D$=F$:F$=G$:G$=H$:H$=I$:I$=D$:NEXTEG
REM ---------------- CHECK FOR VALID CORNER COLORS
1390 FOREG=1TO4:GOSUB10:EX=C(30)*C(26)*C(29)
1395 IFEG<4THENEX=C(30)*C(25+EG)*C(26+EG)
1400 FOREY=1TO4:READR,S,T:Q=C(R)*C(S)*C(T)
1405 IFEX<>QTHENNEXTEY:EY=C(30):GOTO2025
1410 EY=4:NEXTEY,EG
REM ---------------- PATTERN TO FLIP BOTTOM CORNERS
1415 S=0:GOSUB10:FORI=0TO2:FORJ=0TO2
1420 READR:IFC(R)=C(30)THENS=S+3^I*J
1425 NEXTJ,I:IFS=0THEN1445
1430 GOSUB104:WW=WW+1:GOSUB665:PRINTD$;
1435 IFWW>20THENGOSUB104:PRINT"NO SOLUTION":GOTO2060
1440 GOSUB245:GOSUB115:GOTO1415
1445 FORCM=2TO5:IFC(CM)=C(CM+36)THEN1460
1450 NEXTCM:GOSUB715:GOSUB104:WW=WW+1:PRINTD$;
1455 GOSUB245:GOSUB115:GOTO1445
1460 FORJ=26TO29:IFC(CM)<>C(J)THENNEXTJ
1465 EG=J-CM-24:IFEG<0THENEG=EG+4
1470 ONCM-2GOTO1485,1490,1495
1475 IFC(4)=C(40)THEN1510
1480 F$="B":G$="L":GOTO1500
1485 F$="L":G$="F":GOTO1500
1490 F$="F":G$="R":GOTO1500
1495 F$="R":G$="B"
1500 GOSUB715:GOSUB104:WW=WW+1:PRINTD$;
1505 GOSUB245:GOSUB115
1510 IFEG=0THEN1525
1515 D$="D"+RIGHT$(STR$(EG),1):GOSUB104:WW=WW+1
1520 PRINTD$;:GOSUB245:GOSUB115
REM ---------------- FINAL CORNER CUBIE CHECK
1525 FOREG=1TO4:IFC(EG+37)=C(25+EG)THENNEXTEG:GOTO1540
1530 EY=C(26+EG):IFEG=4THENEY=C(26)
1535 EX=C(30)+C(25+EG)*EY:EY=C(30):GOTO2020
REM ---------------- SOLVE BOTTOM EDGIES
1540 FOREG=1TO4:GOSUB20:EX=C(25+EG)*C(30)
1545 FOREY=1TO8:READR,S
1550 IFEX<>C(R)*C(S)THENNEXTEY:EY=C(30):GOTO2000
1555 EY=8:NEXTEY:CM=R:IFC(S)=C(30)THENCM=S
1560 R=INT(EG*EG*EG/3-EG*EG*1.5+EG/6+5.1)
1565 IFCM=12*RTHEN1590
1570 J$="":IFEG>1THENJ$="D"+RIGHT$(STR$(5-EG),1)
1575 K$="":IFEG>1THENK$="D"+RIGHT$(STR$(EG-1),1)
1580 GOSUB720:IFD$=""THEN1590
1585 GOSUB104:WW=WW+1:PRINTD$;:GOSUB245:GOSUB115
1590 NEXTEG
REM ---------------- CHECK FOR VALID EDGIE COLORS
1595 GOSUB20:Q=C(26)*C(29):FORI=0TO3:READR,S
1600 IFQ<>C(R)*C(S)THENNEXTI:EY=C(29):EG=1:GOTO2000
1605 Q=I:I=3:NEXTI:FOREG=1TO3:GOSUB20
1610 P=C(EG+25)*C(EG+26):FOREY=1TO4:READR,S
1615 IFP<>C(R)*C(S)THENNEXTEY:EY=C(EG+26):GOTO2000
1620 EY=4:NEXTEY,EG
REM ---------------- POSITION CENTER EDGIES
1625 GOSUB20:P=C(26)*C(27):FORI=0TO3:READR,S
1630 IFP<>C(R)*C(S)THENNEXTI:END
1635 R=Q*3+I:IFI<QTHENR=R+1
1640 I=3:NEXTI:IFR=1THEN1655
1645 GOSUB805
1650 GOSUB104:WW=WW+1:PRINTD$;:GOSUB245:GOSUB115
REM ---------------- FLIP CENTER EDGIES
1655 R=1:IFC(8)<>C(26)THENR=R+4
1660 IFC(9)<>C(27)THENR=R+2
1665 IFC(10)<>C(28)THENR=R+1
1670 IFR=1THEN1695
1675 ONR-1GOSUB865,870,875,880,885,890,905
1680 IFLEN(D$)>60THENPRINTLEFT$(D$,60);:GOSUB104:PRINTMID$(D$,61);:GOTO1690
1685 GOSUB104:PRINTD$;
1690 GOSUB245:GOSUB115
1695 FOREG=26TO29:IFC(EG)<>C(EG+18)THEN2050
1700 NEXTEG
1705 GOSUB104:PRINT"ALL DONE"
1800 GOSUB4
1802 RUN
1804 REM ---------------- SUBROUTINE TO SET CUBIE COLORS
1805 GOSUB9:GOSUB105:PRINT@448,"1=ˇ, 2=ü, 3=œ, 4=Ø, 5=ø, 7=g,":PRINT@480,"ENTER COLORS";:QQ=0
1810 FORS=0TO5:FORT=0TO2:FORU=0TO2:Z=S+U*6+T*18
1815 ONSGOTO1825,1830,1835,1845,1855
1820 X=(T+U)*3+24:Y=32+(U-T)*3:GOTO1860
1825 X=T*3+23:Y=21+U*4-T*3:GOTO1860
1830 X=T*3+32:Y=15+U*4+T*3:GOTO1860
1835 X=T*3+45:Y=24+U*4+T*3:GOTO1860
1845 X=T*3+54:Y=30+U*4-T*3:GOTO1860
1855 X=(T+U)*3+46:Y=21+(U-T)*3
1860 WW=C(Z+1):GOSUB1870
1865 NEXTU,T,S:IFQQTHENRETURN
1866 GOTO1900
1870 X1=X-1:Y1=40-Y:SET(X1,Y1,W(WW))
1871 CC=POINT(X1,Y1-1):SET(X1,Y1-1,W(WW)):IFCC=.THENRESET(X1,Y1-1)
1872 CC=POINT(X1+1,Y1):IFPOINT(X1+2,Y1)<>6THENSET(X1+1,Y1,W(WW)):IFCC=.THENRESET(X1+1,Y1)
1873 CC=POINT(X1,Y1+1):SET(X1,Y1+1,W(WW)):IFCC=.THENRESET(X1,Y1+1)
1874 CC=POINT(X1-1,Y1):SET(X1-1,Y1,W(WW)):IFCC=.THENRESET(X1-1,Y1)
1875 IFQQTHENRETURN
1879 Z$=INKEY$:RESET(X1,Y1):IFZ$=""THEN1870
1880 IFZ$=CHR$(13)THENIFWW>0ANDWW<8THENSET(X1,Y1,W(WW)):RETURN
1881 CC=VAL(Z$):IFCC<1ORCC>7ORCC=6THENSOUND1,1:GOTO1870
1885 WW=VAL(Z$):C(Z+1)=WW:GOTO1870
1900 GOSUB105:INPUT"ANY CHANGES (Y/N)";WW$
1905 IFLEFT$(WW$,1)="Y"THENGOSUB105:GOTO1805
1910 GOSUB105:FOREG=7TO15:NEXTEG
1915 FOREG=16TO31:NEXTEG:RETURN
REM ---------------- ERROR MESSAGES FOR INVALID CUBES
2000 GOSUB105:PRINT"CAN'T FIND";
2005 GOSUB2070:EY=C(25+EG):GOSUB2070:PRINT" EDGIE";
2010 GOSUB104:PRINT"SOMEONE SWITCHED THE STICKERS";
2015 GOTO1800
2020 GOSUB105:PRINT"SWAPPED COLORS AT";:GOTO2030
2025 GOSUB105:PRINT"CAN'T FIND";:EY=C(30)
2030 GOSUB2070:EY=C(25+EG):GOSUB2070
2035 EY=EX/C(30)/EY+.1:GOSUB2070:PRINT" CORNER";:GOTO2010
2040 GOSUB105:PRINT"SWAPPED COLORS AT";:GOTO2050
2045 GOSUB105:PRINT"CANNOT FIND";:EY=C(25)
2050 GOSUB2070:EY=C(25+EG):GOSUB2070
2055 EY=EX/C(25)/EY+.1:GOSUB2070:PRINT" CORNER";:GOTO2010
2060 GOSUB104:PRINT"YOUR CUBE MUST BE DISASSEMBLED"
2065 PRINT"AND PUT TOGETHER CORRECTLY";:GOTO1800
2070 ONEY-1GOTO2080,2085,2090,2095,2100,2105
2075 PRINT" ORANGE";:RETURN
2080 PRINT" YELLOW";:RETURN
2085 PRINT" WHITE";:RETURN
2090 PRINT" BLUE";:RETURN
2095 PRINT" RED";:RETURN
2100 PRINT" INVALID";:RETURN
2105 PRINT" GREEN";:RETURN
REM ---------------- INSTRUCTIONS
2200 CLS:Z$="MODES OF PLAY":GOSUB7:PRINT
2210 WW$="SELECT MIX & PLAY AND I'LL MIX THE CUBE FOR YOU TO SOLVE.":GOSUB1
2220 WW$="SELECT MATCH & SOLVE AND YOU CAN MATCH MY DISPLAYS TO YOUR HAND HELD CUBE THEN I'LL GIVE YOU A LIST OF MOVES "
2230 GOSUB1
2240 WW$=WW$+"WHICH WILL SOLVE THE CUBE FROM THAT POSITION OR LET YOU KNOW YOU HAVE AN INVALID CUBE!":GOSUB1
2260 INPUT"HIT enter TO CONTINUE ";WW$
2270 CLS:Z$="HOW TO ENTER MOVES":GOSUB7:PRINT
2280 WW$="MOVES ARE ENCODED BY USING THE FIRST LETTER OF THE FACE TO BE "
2290 WW$=WW$+"ROTATED. THIS IS FOLLOWED BY A NUMBER FROM 1 TO 3 TO INDICATE THE NUMBER OF QUARTER TURNS IN A "
2310 WW$=WW$+"CLOCKWISE DIRECTION AS YOU FACE THAT SIDE.":GOSUB1
2320 PRINT
2330 WW$="FOR EXAMPLE: F3 TURNS THE FRONT FACE 3 QUARTER TURNS. (WHICH EQUALS ONE CCW TURN)":GOSUB1
2350 WW$="MULTIPLE MOVES MAY BE WRITTEN AS FOLLOWS:":GOSUB1
2360 PRINT" D1U3R2D3U1 (NO SPACES)"
2370 INPUT"HIT enter TO CONTINUE";WW$
2380 CLS
2390 Z$="MISC. FUNCTIONS:":GOSUB7:PRINT
2400 WW$="A 'Y' RESPONSE TO THE DISPLAY EACH MOVE QUERY WILL DO "
2410 WW$=WW$+"JUST THAT AND ALLOW YOU TO SEE EACH MOVE.":GOSUB1
2420 WW$="HOWEVER, WHEN DONE IN MATCH AND SOLVE MODE, MY SOLUTION SPEED IS GREATLY REDUCED.":GOSUB1
2440 WW$="IN MATCH & SOLVE COLORS ARE SET BY PRESSING THE COLOR NUMBER FOLLOWED BY 'ENTER'.":GOSUB1
2450 WW$="THE FLASHING CURSOR WILL INDICATE THE LOCATION.":GOSUB1
2470 INPUT "HIT enter TO START ";WW$:RUN
