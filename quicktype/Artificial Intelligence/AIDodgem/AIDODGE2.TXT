0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 PRINT@480,"PRESS enter TO CONTINUE";
20 M$=INKEY$:IFM$<>CHR$(13)THEN20
30 FORCC=1TO23:PRINTCHR$(8);:NEXT:RETURN
100 REM
110 REM
120 REM BY: MAC OGLESBY ON 18 OCT 75.
130 REM
140 REM DESCRIPTION: TWO SETS OF PIECES RACE AT RIGHT ANGLES ACROSS A 
150 REM SQUARE BOARD. FOR ONE OR TWO PLAYERS.
160 REM
170 REM INSTRUCTIONS: TYPE "RUN" FOR COMPLETE INSTRUCTIONS.
180 REM
190 REM REMARKS: THE GAME OF DODGEM IS DESCRIBED BY MARTIN GARDNER
200 REM IN SCIENTIFIC AMERICAN OF JUNE 1975
210 REM
220 REM
230 CC=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
240 PRINT "WANT INSTRUCTIONS FOR DODGEM";
250 INPUT A$
260 GOSUB 2950:REM PROCESS INPUT
270 IF MID$(A$,1,1)<>"Y" THEN 290
280 GOSUB 3130:REM INSTRUCTIONS
290 PRINT "BOARD SIZE (3-6)";
300 INPUT A
310 A=INT(A)
320 IF (6-A)*(A-3)=>0 THEN 340
330 GOTO 290
340 P(1,0)=A-1:P(2,0)=A-1:REM HOW MANY PIECES AT START
350 REM
360 REM
370 REM MATRIX P(,) KEEPS TRACK OF PIECES
380 FOR J=1 TO A-1
390 P(1,J)=10*J+1:REM LOCATE DIGITS
400 NEXT J
410 REM
420 FOR J=1 TO A-1
430 P(2,J)=10*A+J+1:REM LOCATE LETTERS
440 NEXT J
450 REM
460 F=1
470 M$(1)="NES":REM LEGAL MOVES FOR THE DIGITS
480 M$(2)="NE W":REM LEGAL MOVES FOR THE LETTERS
490 C$(1)="DIGITS"
500 C$(2)="LETTERS"
510 A$(1)="1234567"
520 A$(2)="ABCDEFG"
530 REM
540 REM
550 REM SET UP BOARD
560 FOR J=1 TO A
570 IF J=A THEN 630
580 D$(J,1)=CHR$(48+J)
590 FOR K=2 TO A
600 D$(J,K)="."
610 NEXT K
620 GOTO 670
630 D$(J,1)="."
640 FOR K=2 TO A
650 D$(J,K)=CHR$(63+K)
660 NEXT K
670 NEXT J
680 REM
690 PRINT "HOW MANY PLAYERS (1 OR 2)";
700 INPUT B
710 IF B=2 THEN 800
720 IF B=1 THEN 740
730 GOTO 690
740 PRINT "OK, THE COMPUTER WILL MOVE THE":PRINT"DIGITS. WHO MOVES FIRST"
750 PRINT "(1=COMPUTER 2=YOU)";
760 INPUT F
770 IF (2-F)*(1-F)=0 THEN 800
780 PRINT "PLEASE TYPE 1 OR 2."
790 GOTO 750
800 PRINT
810 PRINT "HERE WE GO..."
820 REM
830 REM PRINT THE DISPLAY PRINT
840 PRINT
850 FOR J=1 TO A
860 FOR K=1 TO A
870 PRINT " ";D$(J,K);
880 NEXT K
890 PRINT
900 NEXT J
910 PRINT
920 REM
930 REM
940 REM MAIN MOVE LOOP
950 FOR J=F TO 3-F STEP 3-2*F
960 REM
970 REM CHECK IF PLAYER J HAS A LEGAL MOVE LEFT
980 FOR J1=1 TO A-1
990 R=INT(P(J,J1)/10):REM ROW OF J1TH PIECE
1000 C=P(J,J1)-10*R:REM COLUMN OF J1TH PIECE
1010 REM
1020 ON J GOTO 1040,1100
1030 REM
1040 IF C=A THEN 1080
1050 IF C>A THEN 1070
1060 GOTO 1160
1070 GOTO 1250
1080 GOTO 1300
1090 REM
1100 IF R=1 THEN 1140:REM AT EDGE OF BOARD?
1110 IF R=0 THEN 1130:REM OFF BOARD?
1120 GOTO 1160:REM SEE IF PIECE CAN MOVE
1130 GOTO 1250:REM TRY NEXT PIECE
1140 GOTO 1300:REM LEGAL MOVE EXISTS
1150 REM
1160 IF D$(R-1,C)="." THEN 1240:REM SEE IF PIECE CAN GO NORTH
1170 IF D$(R,C+1 )="." THEN 1240:REM SEE IF PIECE CAN GO EAST
1180 IF J=2 THEN 1220
1190 IF D$(R+1,C)="." THEN 1210:REM CAN IT GO SOUTH
1200 GOTO 1250:REM TRY NEXT PIECE
1210 GOTO 1300:REM LEGAL MOVE EXISTS
1220 IF D$(R,C-1)="." THEN 1240:REM SEE IF PIECE CAN GO WEST
1230 GOTO 1250:REM TRY NEXT PIECE
1240 GOTO 1300:REM LEGAL MOVE EXISTS
1250 NEXT J1
1260 M$="THE "+C$(3-J)+" HAVE LEFT NO LEGAL MOVE FOR THE "+C$(J)+"!":GOSUB1
1270 M$="*** THE "+C$(J)+" WIN!!!":GOSUB1
1280 END
1290 REM
1300 IF B=2 THEN 1750
1310 IF J=2 THEN 1750
1320 REM
1330 REM GENERATE COMPUTER'S MOVE
1340 L1=2
1350 FOR L0=1 TO 3
1360 REM
1370 ON L0 GOTO 1450,1380,1430
1380 IF RND(0)<.5 THEN 1410
1390 L1=1
1400 GOTO 1450
1410 L1=3
1420 GOTO 1450
1430 L1=4-L1
1440 REM
1450 P1=INT(RND(0)*A)
1460 FOR L2=1 TO A-1
1470 P1=P1+1
1480 IF P1<=A-1 THEN 1500
1490 P1=P1-(A-1)
1500 R=INT(P(J,P1)/10):REM ROW
1510 C=P(J,P1)-10*R:REM COLUMN
1520 IF C>A THEN 1720
1530 REM
1540 ON L1 GOTO 1570,1620,1690
1550 REM
1560 REM NORTH
1570 IF D$(R-1,C)= "." THEN 1590
1580 GOTO 1720
1590 GOTO 2090
1600 REM
1610 REM EAST
1620 IF D$(R,C+1)="." THEN 1660
1630 IF C=A THEN 1650
1640 GOTO 1720
1650 P(J,0)=P(J,0)-1
1660 GOTO 2160
1670 REM
1680 REM SOUTH
1690 IF D$(R+1,C)="." THEN 1710
1700 GOTO 1720
1710 GOTO 2230
1720 NEXT L2
1730 NEXT L0
1740 REM
1750 PRINT C$(J);" MOVE";
1760 INPUT A$
1770 GOSUB 2950:REM PROCESS INPUT
1780 REM
1790 IF MID$(A$,1,1)="R" THEN 2860
1800 IF MID$(A$,1,1)="H" THEN 2460
1810 A$=MID$(A$,1,2):REM LOOK ONLY AT 1ST 2 CHARACTERS
1811 IFLEN(A$)<2THENA$="@0"
1820 P1=0:FOR CC=1 TO 7:IF MID$(A$,1,1)=MID$(A$(J),CC,1) THEN P1=CC:REM WHAT PIECE
1821 NEXT CC
1830 IF P1=0 THEN 2430
1840 P2=0:FOR CC=1 TO 7:IF MID$(A$,2,1)=MID$(M$(J),CC,1) THEN P2=CC:REM WHAT DIR
1841 NEXT CC
1850 IF P2=0 THEN 2430
1860 R=INT(P(J,P1)/10):REM ROW OF PIECE
1870 C=P(J,P1)-10*R:REM COLUMN OF PIECE
1880 IF R=0 THEN 2430
1890 IF C>A THEN 2430
1900 REM
1910 ON J GOTO 1930,1990
1920 REM
1930 IF C<A THEN 1970:REM PIECE NOT AT EAST EDGE
1940 IF P2<>2 THEN 1970:REM DIRECTION NOT EAST
1950 P(1,0)=P(1,0)-1:REM REDUCE # OF PIECES
1960 GOTO 2160:REM UPDATE PIECE LOCATION
1970 GOTO 2040:REM CHECK MOVE FURTHER
1980 REM
1990 IF R>1 THEN 2040:REM PIECE NOT AT NORTH EDGE
2000 IF P2<>1 THEN 2040:REM DIRECTION NOT NORTH
2010 P(2,0)=P(2,0)-1:REM REDUCE # OF PIECES
2020 GOTO 2090:REM UPDATE LOCATIONS
2030 REM
2040 ON P2 GOTO 2070,2140,2210,2280
2050 REM
2060 REM SEE IF PIECE CAN MOVE NORTH
2070 IF D$(R-1,C)="." THEN 2090
2080 GOTO 2430:REM ERROR MESSAGE
2090 D$(R-1,C)=MID$(A$(J),P1,1)
2100 P(J,P1)=P(J,P1)-10
2110 GOTO 2330
2120 REM
2130 REM EAST
2140 IF D$(R,C+1)="." THEN 2160
2150 GOTO 2430:REM ERROR MESSAGE
2160 D$(R,C+1)=MID$(A$(J),P1,1)
2170 P(J,P1)=P(J,P1)+1
2180 GOTO 2330
2190 REM
2200 REM SOUTH
2210 IF D$(R+1,C)="." THEN 2230
2220 GOTO 2430:REM ERROR MESSAGE
2230 D$(R+1,C)=MID$(A$(J),P1,1)
2240 P(J,P1)=P(J,P1)+10 
2250 GOTO 2330
2260 REM
2270 REM WEST
2280 IF D$(R,C-1)="." THEN 2300
2290 GOTO 2430:REM ERROR MESSAGE
2300 D$(R,C-1)=MID$((A$(J),P1,1)
2310 P(J,P1)-P(J,P1)-1
2320 REM
2330 D$(R,C)=".":REM FINISH UPDATING DISPLAY
2340 IF B=2 THEN 2380
2350 IF J=2 THEN 2380
2360 M$="THE DIGITS MOVE: "+MID$(A$(J),P1,1)+MID$(M$(J),L1,1):GOSUB1
2380 IF P(J,0)<>0 THEN 2420:REM CHECK FOR WIN
2390 PRINT
2400 M$="*** THE "+C$(J)+" WIN!!!":GOSUB1
2410 END
2420 GOTO 2900:REM GET NEXT PLAYER'S MOVE
2430 PRINT "ILLEGAL MOVE OR BAD INPUT."
2440 PRINT "INPUT IGNORED. TYPE H FOR HELP."
2450 GOTO 1750
2460 M$="THE "+C$(J)+" HAVE THESE LEGAL MOVES:":GOSUB1
2470 REM HELP! -- PRINT LEGAL MOVES
2480 FOR J3=1 TO A-1
2490 P$=MID$(A$(J),J3,1):REM NAME OF PIECE
2500 R=INT(P(J,J3)/10):REM ROW
2510 C=P(J,J3)-10*R:REM COLUMN
2520 REM
2530 ON J GOTO 2550,2620
2540 REM
2550 IF C=A THEN 2590:REM PIECE AY EAST SIDE
2560 IF C>A THEN 2580
2570 GOTO 2690
2580 GOTO 2830
2590 PRINT"  ";P$;"E";
2600 GOTO 2690
2610 REM
2620 IF R=1 THEN 2660
2630 IF R=0 THEN 2650
2640 GOTO 2690
2650 GOTO 2830
2660 GOTO 2700
2670 REM
2680 REM NORTH
2690 IF D$(R-1,C)<>"." THEN 2730
2700 PRINT "  ";P$;"N";
2710 REM
2720 REM EAST
2730 IF D$(R,C+1)<>"." THEN 2770
2740 PRINT "  ";P$;"E";
2750 REM
2760 REM SOUTH AND WEST
2770 IF J=2 THEN 2810
2780 IF D$(R+1,C)<>"." THEN 2800
2790 PRINT "  ";P$;"S";
2800 GOTO 2830
2810 IF D$(R,C-1)<>"." THEN 2830
2820 PRINT "  ";P$;"W";
2830 NEXT J3
2840 PRINT
2850 GOTO 1750
2860 M$="THE "+C$(J)+" GIVE UP!!":GOSUB1
2870 M$="*** THE "+C$(3-J)+" WIN!!!":GOSUB1
2880 END
2890 REM
2900 NEXT J
2910 GOTO 840:REM PRINT DISPLAY
2920 REM
2930 REM
2940 REM PROCESS INPUTT;IGNORE COMMAS AND SPACES
2950 IF LEN(A$)>10 THEN 3090
2960 M$=""
2970 FOR J2=1 TO LEN (A$)
2980 IF MID$(A$,J2,1)=" " OR MID$(A$,J2,1)="," OR MID$(A$,J2,1)="" THEN 3000
2990 M$=M$+MID$(A$,J2,1):A(J2)=ASC(MID$(A$,J2,1))
3000 NEXT J2
3010 A$=M$
3090 RETURN
3120 REM INSTRUCTIONS
3130 CLS
3140 PRINT "HERE'S A SAMPLE PLAYING BOARD:"
3150 PRINT
3160 PRINT "1 . . . ."
3170 PRINT "2 . . . ."
3180 PRINT "3 . . . ."
3190 PRINT "4 . . . ."
3200 PRINT ". A B C D"
3210 PRINT
3220 M$="TWO SETS OF PIECES (DIGITS AND LETTERS) RACE AT RIGHT ANGLES "
3230 M$=M$+"ACROSS A SQUARE BOARD. VACANT LOCATIONS ARE SHOWN AS PERIODS.":GOSUB1
3240 M$="YOU GET TO CHOOSE THE BOARD SIZE. (THE ONE ABOVE IS SIZE 5.)":GOSUB1
3250 GOSUB10:CLS
3260 PRINT "  N"
3270 PRINT "  !"
3280 PRINT "W-+-E"
3290 PRINT "  !"
3300 PRINT "  S"
3310 PRINT
3320 M$="THE OBJECT IS TO MOVE ALL OF YOUR PIECES ACROSS THE BOARD "
3330 M$=M$+"AND OFF THE OPPOSITE EDGE.":GOSUB1:M$="DIGITS LEAVE THE BOARD ONLY AT "
3340 M$=M$+"THE EASTERN EDGE; LETTERS ONLY AT THE NORTHERN.":GOSUB1
3350 M$="THE WINNER IS THE PLAYER WHOSE PIECES HAVE ALL LEFT THE BOARD.":GOSUB1
3360 GOSUB10:CLS
3370 M$="THE PLAYERS GO IN TURN, MOVING ONE OF THEIR PIECES TO AN "
3380 M$=M$+"ADJACENT LOCATION WHICH IS EITHER OFF THE BOARD OR CURRENTLY VACANT.":GOSUB1
3390 M$="THERE ARE NO DIAGONAL MOVES, NO JUMPS AND NO CAPTURES.":GOSUB1
3400 M$="DIGITS CANNOT MOVE WEST, NOR LETTERS MOVE SOUTH.":GOSUB1
3410 GOSUB10:CLS
3420 M$="TO MOVE A PIECE, TYPE ITS NAME AND THE FIRST LETTER OF THE DESIRED DIRECTION.":GOSUB1
3430 PRINT"EXAMPLES:"
3440 M$="  2E  MEANS PIECE 2 WANTS TO GO EAST":GOSUB1
3450 M$="  BW  MEANS PIECE B WANTS TO GO WEST":GOSUB1
3460 GOSUB10:CLS
3470 M$="NOTE: YOU FORFEIT THE GAME IF YOUR MOVE LEAVES YOUR OPPONENT WITHOUT ANY LEGAL MOVE."
3490 GOSUB1:PRINT
3500 M$="LASTLY, YOU MAY TYPE R TO RESIGN OR H FOR HELP.":GOSUB1
3510 GOSUB10:CLS
3520 RETURN
3530 END
