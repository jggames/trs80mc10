0 CLS:GOTO620:REM BY HLO
30 IFX<20THENX=8:GOTO32
31 X=22
32 IFY=2THENY=1:X=0:GOTO50
33 IFY>2ANDY<7THENY=Y-3:GOTO50
34 IFY>6ANDY<10THENY=Y-7:X=22:GOTO50
35 IFY=10ORY=11THENY=4:X=8:GOTO50
36 IFY>11THENY=Y-7:GOTO50
50 ZZ=32*Y+X:IFZZ>495THENPRINT@ZZ,LEFT$(A$,9);:RETURN
51 IFX<21THENPRINT@ZZ,A$;:RETURN
52 PRINT@ZZ,LEFT$(A$,10);:RETURN
100 PRINT"CARDS(1)---------- (5)----------";
101 PRINT"     (2)---------- (6)----------";
102 PRINT"     (3)---------- (7)----------";
103 PRINT"     (4)----------              ";
104 PRINT"LAST   :----------              ";
105 PRINT"PLAYER :----------    ----------";
106 PRINT"       :----------    ----------";
107 PRINT"STATUS :----------    ----------";
108 PRINT"       :----------    ----------";
109 PRINT"HAZARDS:----------    ----------";
110 PRINT"       :----------    ----------";
111 PRINT"SAFETYS:----------    ----------";
112 PRINT"you    :----------    ----------";
113 PRINT" 0     :----------    ----------";
114 PRINT"com    :----------    ----------";
115 PRINT" 0     :----------    ---------";:RETURN
620 PRINT:PRINT:PRINT"MC-10 MILLE BORNES V2":PRINT
650 PRINT"WANT TO PLAY:":PRINT"1) LONG GAME 1000/5000":PRINT"2) SHORT GAME 700/3000"
655 INPUTI:IFI<1ORI>2THENCLS:GOTO650
660 IFI=1THENMS=1000:WS=5000:GOTO680
661 MS=700:WS=3000
680 CLS
700 PRINT"MC-10 MILLE BORNES V2":PRINT"A CARD GAME OF AUTO RACING"
800 DIMCR$(115),HZ$(2),YR$(2,7),ST$(2),SC(2),SF$(2,7),CT(2),SL$(2)
900 DIMSB(2),SF(2),WR(2),MX(2),CC(2),SU(2),TL(2)
950 DIMSI$(2),CF(2)
1000 TL(1)=0:TL(2)=0:TD=0:G8=0
1050 REM RESTART POINT
1100 FORI=1TO2:HZ$(I)="":ST$(I)="":SC(I)=0:CT(I)=0:SL$(I)=""
1111 CF(1)=0:CF(2)=0
1200 SB(I)=0:SF(I)=0:WR(I)=0:MX(I)=0:CC(I)=0:SU(I)=0
1300 FORJ=1TO7:YR$(I,J)="":SF$(I,J)="":NEXTJ:NEXTI
1400 DS=0:YN$="":NC=15:T$="":OW=0:RO=0:ZH=0:ST=0:SL=0
1500 R=0:D=0:Q=0:T1$="":T2$="":Z=0 
1550 RR=0:I=0:X=0:Y=0:A$="":PR=0
1600 VL=0:NS=0:S9=0:H$="":SW=0:KP=0:D$="" 
1650 P=0:C=0:PO=0:WN=0:HZ=0:SP=0:SL=0
1700 HI=0:I=0
1950 PRINT"READING AND SHUFFLING CARDS":I=0
1960 RESTORE
2000 READD$:IFD$="END-DATA"THEN2200
2001 I=I+1:CR$(I)=D$:GOTO2000
2200 Q=0:P=0:TD=I
2410 FORZ=1TO200:A=RND(100):B=RND(100) 
2500 T1$=CR$(A):CR$(A)=CR$(B):CR$(B)=T1$:NEXTZ
3000 C=1
3100 FORI=1TO14STEP2
3200 YR$(1,C)=CR$(I):YR$(2,C)=CR$(I+1)
3300 C=C+1
3400 NEXTI
3600 CLS
3670 REM DISPLAY INTERFACE
3700 GOSUB100
4700 PR=1:PO=2
4750 FORI=1TO7
4770 Y=I+2:X=5:A$=YR$(PR,I):GOSUB30:NEXTI
4780 X=9:A$="STOPPED       STOPPED":Y=14:GOSUB30
4800 REM ***MAIN LOOP***
4900 REM LOOP BACK
5000 X=9:A$="YOU       ":Z=20:PR=1:PO=2:DS=0:C=0
5100 Y=12:GOSUB30:Y=12:X=Z:A$="          ":GOSUB30
5200 A$=INKEY$:IFA$=""THEN5200
5201 K=ASC(A$)
5300 IF(K>48ANDK<56)THEN5500
5301 GOTO5200
5500 KP=(5-(53-K)):T$=YR$(PR,KP):IFT$=""THEN5200
5600 REM CHK KEY PRESS
5700 IFT$="25"ORT$="50"ORT$="75"ORT$="100"ORT$="200"THENGOSUB19600
5800 IFT$="REPAIRS   "ORT$="GASOLINE  "ORT$="SPARE TIRE"ORT$="END LIMIT "ORT$="ROLL !"THENGOSUB20700
5900 IFT$="FLAT TIRE "ORT$="OUT OF GAS"ORT$="ACCIDENT  "ORT$="SPEED LIM "ORT$="STOP      "THENGOSUB22200
6000 IFT$="PUN PROOF "ORT$="EXTRA TANK"ORT$="DRIV ACE  "ORT$="RITE O WAY"THENGOSUB23800
6100 IFDS=1THEN5000
6200 GOSUB26300:REM GET NEW CARD
6300 IFSC(PR)>=MSTHENOW=1:WN=PR:GOTO26700
6400 GOSUB18200:REM REDRAW SCREEN
6500 REM COMPUTER PLAY
6600 X=20:A$="COMPUTER  ":Z=9
6700 Y=12:GOSUB30:Y=12:X=Z:A$="          ":GOSUB30
6800 ZH=0:ST=0:SL=0:HZ=0:SP=0:SJ=0
6900 IFLEN(ST$(1))>1THENSP=1
7000 IFLEN(HZ$(1))>1THENHZ=1
7100 IFLEN(HZ$(2))>1THENZH=1
7200 IFLEN(ST$(2))>1THENST=1
7300 IFLEN(SL$(1))>1THENSL=1
7400 IFLEN(SL$(2))>1THENSJ=1
7500 S9=0:H$="":NS=0:VL=0:T$="":PR=2:PO=1:C=0
7600 REM C PLAY ROLL&CHK SAFES
7700 DS=0:IF(SC(1)>(MS-200))OR(SC(2)>(MS-200))THEN DS=1
7800 FORI=1TO7:IFS9=1THEN8300
7900 T$=YR$(PR,I):H$=MID$(HZ$(2),1,3)
8000 T2$=MID$(T$,1,3):IF(T2$="PUN"ORT2$="EXT"ORT2$="DRI"ORT2$="RIT")ANDDS=1THENS9=1:KP=I:GOSUB23800
8100 IFT$="ROLL !"ANDST=0ANDZH=0THENS9=1:KP=I:GOSUB20700
8200 IFT$="ROLL !"ANDH$="STO"THENS9=1:KP=I:GOSUB20700
8300 NEXTI
8400 IFS9=1THEN17600
8500 REM C HAS HAZARD PLAY SAFETY
8600 IFZH=0THEN11000
8700 H$=MID$(HZ$(PR),1,2)
8800 FORI=1TO7:IFS9=1THEN9600
8900 T$=YR$(PR,I):T2$=MID$(T$,1,3)
9000 IF(T2$="PUN"ANDH$="FL")OR(T2$="EXT"ANDH$="OU")THENS9=1:KP=I:GOTO9500
9100 IF(T2$="DRI"ANDH$="AC")OR(T2$="RIT"ANDH$="ST")THENS9=1:KP=I:GOTO9500
9400 GOTO9600
9500 GOSUB23800
9600 NEXTI
9700 IFS9=1THEN17600
9800 REM C PLAY REMEDYS ON HAZARD
9900 FORI=1TO7:T$=YR$(PR,I):T2$=MID$(T$,1,3)
10000 IFS9=1THEN10700
10100 IF(T2$="GAS"ANDH$="OU")OR(T2$="REP"ANDH$="AC")THENS9=1:KP=I:GOTO10600
10300 IF(T2$="ROL"ANDH$="ST")OR(T2$="SPA"ANDH$="FL")THENS9=1:KP=I:GOTO10600
10500 GOTO10700
10600 GOSUB20800
10700 NEXTI
10800 IFS9=1THEN17600
10900 REM C PLAY MILES
11000 IFST=0THEN12400
11100 HI=0
11200 FORI=1TO7
11300 T$=YR$(PR,I)
11400 IFT$="200"ORT$="100"ORT$="75"ORT$="50"ORT$="25"THENVL=VAL(T$):GOTO11500
11401 GOTO12000
11500 IF(SJ=1ANDVL>50)OR(CT(2)>1ANDVL=200)OR((SC(2)+VL)>MS)THEN12000
11800 IFVL>HITHENHI=VL:GOTO11900
11801 GOTO12000
11900 KP=I:S9=1:H$=T$
12000 NEXT I
12100 IFS9=1THENVL=HI:T$=H$:GOSUB19700
12200 IFS9=1THEN17600
12300 REM C REMEDY SPEED LIM
12400 IFSJ=0THEN13100
12500 FORI=1TO7:IFS9=1THEN12800
12600 T$=YR$(PR,I):T2$=MID$(T$,1,3)
12700 IFT2$="END"THENS9=1:KP=I:GOSUB20700
12800 NEXTI
12900 IFS9=1THEN17600
13000 REM C PLAYS HAZARD
13100 IFHZ=1THEN14100
13200 IFSP=0THEN14100
13300 FORI=1TO7:IFS9=1THEN13900
13400 T$=YR$(PR,I):T2$=MID$(T$,1,3)
13500 IF(T2$="FLA"ANDLEN(SF$(PO,1))<2)OR(T2$="ACC"ANDLEN(SF$(PO,4))<2)THENKP=I:S9=1:GOSUB22200
13700 IF T2$="STO"ANDLEN(SF$(PO,3))<2THENKP=I:S9=1:GOSUB22200
13750 IF(T2$="SPE"ANDLEN(SF$(PO,3))<2)OR(T2$="OUT"ANDLEN(SF$(PO,2))<2)THENKP=I:S9=1:GOSUB22200
13900 NEXTI:IFS9=1THEN17600
14000 REM USE SPEED LIM
14040 GOTO14700
14100 IFSL=1THEN14700
14200 FORI=1TO7:IFS9=1THEN14400
14300 T$=YR$(PR,I):T2$=MID$(T$,1,3):IFT2$="SPE"THENKP=I:S9=1:GOSUB22200
14400 NEXTI
14500 IFS9=1THEN17600
14600 REM C PLAY SAFETY
14700 FORI=1TO7:IFS9=1THEN15300
14800 T$=YR$(PR,I):T2$=MID$(T$,1,3)
14900 IFT2$="EXT"ORT2$="DRI"ORT2$="PUN"ORT2$="RIT"THENS9=1:KP=I:GOSUB23800
15300 NEXTI
15400 IFS9=1THEN17600
15500 REM C TO DISCARD
15600 FORI=1TO7:IFS9=1THEN16300
15700 H$=MID$(T$,1,3):REM T$=YR$(PR,I)
15800 IF(H$="FLA" AND LEN(SF$(PO,1))<2)OR(H$="OUT" AND LEN(SF$(PO,2))<2)THEN KP=I:S9=1:GOTO 16300
16000 IF(H$="ACC" AND LEN(SF$(PO,4))<2)OR(H$="STO" AND LEN(SF$(PO,3))<2)THEN KP=I:S9=1:GOTO 16300
16200 IFRND(0)>.5AND(H$="ACC"ORH$="STO"ORH$="SPE"ORH$="OUT"ORH$="FLA")THENKP=I:S9=1
16300 NEXTI
16400 IFS9=1THEN17500
16500 C=0
16600 C=C+1:X=INT(RND(0)*7+1)
16700 T$=YR$(PR,X)
16800 H$=MID$(T$,1,3)
16900 IF C>5 THEN 17400
17000 IF H$="RIT" OR H$="DRI" OR H$="EXT" OR H$="PUN" THEN 16600
17050 J=RND(255)
17100 IF J>204 AND H$="ROL" AND ST=1 THEN 16600
17200 IF(J>126 AND H$="200")OR(J>102 AND H$="100")THEN 16600
17400 KP=X
17500 C=7:T$=YR$(PR,KP)
17520 IFT$=""THENH$="":GOTO17530
17521 GOTO17595
17530 FOR I=1 TO 7:IF YR$(2,I)<>"" THEN H$=YR$(2,I):KP=I
17540 NEXTI:IFH$=""THEN17900
17541 T$=H$
17595 GOSUB25900
17600 IF C<>7 THEN T$=YR$(PR,KP)
17700 GOSUB 18300:REM REDRAW SCREEN
17800 GOSUB 26300:REM GET A NEW CARD
17900 IF SC(PR)>= MS THEN OW=1:WN=PR:GOTO 26700
17920 C=0:FOR I=1 TO 7:IF YR$(2,I)="" THEN C=C+1
17940 NEXT I
17960 IF C=5 THEN 26700
18000 GOTO 4900
18100 REM DISPLAY CARDS
18200 REM CLEAR CARDS
18250 Y=KP+2:X=5:A$="          ":GOSUB30:Y=KP+2:X=5:A$=YR$(PR,KP):GOSUB30
18255 REM Y=I+2:X=5:A$=YR$(PR,I):GOSUB30:NEXT I
18300 Y=10:X=11:A$=T$+"          ":GOSUB30
18320 IF TD-NC<0 THEN 18400
18350 Y=2:X=25:A$=STR$(TD-NC):GOSUB30
18400 FORI=1TO2:IFI=1THENX=9:GOTO18450
18401 X=20
18450 IFST$(I)=""THENSI$(I)="STOPPED":GOTO18500
18451 SI$(I)="ROLL !"
18500 Y=14:A$=SI$(I):GOSUB30
18550 IFHZ$(I)=""THENA$="          ":GOTO18600
18551 A$=HZ$(I)
18600 Y=16:GOSUB30
18700 Y=18:A$=SF$(I,1):GOSUB30
18800 Y=19:A$=SF$(I,2):GOSUB30
18900 Y=20:A$=SF$(I,3):GOSUB30
19000 Y=21:A$=SF$(I,4):GOSUB30
19050 IFSL$(I)=""THENA$="          ":GOTO19100
19051 A$=SL$(I)
19100 Y=14:GOSUB30
19200 IFX<16THENPRINT@32*13,;:GOTO19202
19201 PRINT@32*15,;
19202 A$=STR$(SC(I)):PRINTA$;
19300 NEXTI
19400 RETURN
19500 REM SCORING
19600 IFST$(PR)=""THEN20500
19700 VL=VAL(T$)
19800 IFVL+SC(PR)>MSTHEN20500
19900 IFVL<>200THEN20100
20000 CT(PR)=1+CT(PR):IF CT(PR)>2 THEN 20500
20100 IF SL$(PR)="SPEED LIM " AND VL>50 THEN 20500
20200 SC(PR)=SC(PR)+VL
20400 RETURN
20500 GOSUB 25700
20600 RETURN
20700 REM REMEDYS
20800 IFHZ$(PR)="ACCIDENT  " AND T$="REPAIRS   " THEN 21700
20900 IFHZ$(PR)="OUT OF GAS" AND T$="GASOLINE  " THEN 21700
21000 IFHZ$(PR)="FLAT TIRE " AND T$="SPARE TIRE" THEN 21700
21100 IFSL$(PR)="SPEED LIM "  AND T$="END LIMIT " THEN SL$(PR)="":GOTO 22100
21200 IFT$<>"ROLL !" THEN 22000
21300 IFST$(PR)="ROLL !" THEN 22000
21400 IFHZ$(PR)="STOP      " THEN ST$(PR)="ROLL !":GOTO 21700
21500 IFHZ$(PR)<>"" THEN 22000
21600 ST$(PR)="ROLL !":GOTO 22100
21700 HZ$(PR)=""
21800 IFSF$(PR,3)<>"" THEN ST$(PR)="ROLL !"
21900 RETURN
22000 GOSUB 25700
22100 RETURN
22200 REM @@@HAZARDS
22300 IF T$="SPEED LIM " THEN 22800
22400 IF ST$(PO)="" THEN 23500
22500 IF SF$(PO,1)<>"" AND T$="FLAT TIRE " THEN 23500
22600 IF SF$(PO,2)<>"" AND T$="OUT OF GAS" THEN 23500
22700 IF SF$(PO,3)<>"" AND T$="STOP      " THEN 23500
22800 IF SF$(PO,3)<>"" AND T$="SPEED LIM " THEN 23500
22900 IF SF$(PO,4)<>"" AND T$="ACCIDENT  " THEN 23500
23000 IF SL$(PO)="" AND T$="SPEED LIM " THEN SL$(PO)="SPEED LIM ":GOTO 23400
23100 IF SL$(PO)<>"" AND T$="SPEED LIM " THEN 23500
23200 HZ$(PO)=T$
23300 ST$(PO)=""
23400 RETURN
23500 GOSUB 25700
23600 RETURN
23700 REM SAFETYS
23800 IF T$<>"PUN PROOF " THEN 24200
23900 SF$(PR,1)=T$:IF HZ$(PR)<>"FLAT TIRE " THEN 25500
24000 CF(PR)=CF(PR)+200:HZ$(PR)=""
24100 GOSUB 25555:GOTO 25500
24200 IF T$<>"EXTRA TANK" THEN 24600
24300 SF$(PR,2)=T$:IF HZ$(PR)<>"OUT OF GAS" THEN 25500
24400 CF(PR)=CF(PR)+200:HZ$(PR)=""
24500 GOSUB 25555:GOTO 25500
24600 IF T$<>"RITE O WAY" THEN 25200
24810 SF$(PR,3)=T$:IF HZ$(PR)<>"STOP      " THEN 24900
24820 HZ$(PR)="":CF(PR)=CF(PR)+200
24830 GOSUB 25555
24900 IF SL$(PR)<>"SPEED LIM " THEN 25500
24920 SL$(PR)=""
25100 ST$(PR)="ROLL !":GOTO 25500
25200 SF$(PR,4)=T$
25300 IF HZ$(PR)<>"ACCIDENT  " THEN 25500
25400 CF(PR)=CF(PR)+200:HZ$(PR)=""
25410 GOSUB 25555
25500 IF SF$(PR,3)<>"" AND HZ$(PR)="" THEN ST$(PR)="ROLL !"
25550 RETURN
25555 Y=1:X=6:A$="COUP FOURRE !!!   ":GOSUB30
25556 SOUND 89,5:SOUND 5,5
25558 Y=1:X=6:A$="COLOR MILLE BORNES":GOSUB30:RETURN
25600 REM DISCARD
25700 IF PR<>1 THEN 26100
25720 Y=10:X=1:A$="DISCARD? (Y/N)":GOSUB30:INPUTYN$
25750 Y=10:X=1:A$="                        ":GOSUB30
25800 IFYN$="N"THENDS=1:Y=10:X=1:A$="LAST CARD:"+T$:GOSUB30:GOTO 26100
25900 T$=T$+" D"
26000 Y=10:X=1:A$="LAST CARD:          ":GOSUB30
26050 Y=10:X=1:A$="                    ":GOSUB30
26100 RETURN
26200 REM NEW CARD
26300 NC=NC+1:IF NC=TD THEN YR$(PR,KP)="":GOTO 2650
26400 YR$(PR,KP)=CR$(NC)
26500 RETURN
26600 REM END GAME
26700 A$=STR$(SC(1)):PRINT@32*13,A$;
26710 A$=STR$(SC(2)):PRINT@32*15,A$;
26720 FORI=1TO100:A=SIN(I):NEXTI:REM DELAY
26800 FORI=1TO2
26900 IF SF$(I,1)<>"" THEN SB(I)=SB(I)+100
27000 IF SF$(I,2)<>"" THEN SB(I)=SB(I)+100
27100 IF SF$(I,3)<>"" THEN SB(I)=SB(I)+100
27200 IF SF$(I,4)<>"" THEN SB(I)=SB(I)+100
27300 IF SB(I)=400 THEN SF(I)=300
27400 IF I=WN THEN WR(I)=400
27500 IF TD-NC<1 AND I=WN THEN MX(I)=400
27600 IF CT(I)=0 AND I=WN THEN CC(I)=300
27700 PO=1:IFI=1THENPO=2
27800 IFSC(PO)=0THENSU(I)=500
27820 REM
27850 REM CF(COUFRE),CC(NOUSE200),SF(NOSAFET),
27860 REM SC(MILES),SU(SUOUT),SB(SAFETS),
27870 REM WR(WN)
27880 REM
27900 SC(I)=SC(I)+SB(I)+SF(I)+WR(I)+CC(I)+SU(I)+CF(I)
28000 TL(I)=TL(I)+SC(I)
28100 NEXT I
28200 CLS:Y=1:X=1:A$="BONUS POINTS AND SCORING":GOSUB30
28300 Y=4:X=10:A$="SAFETYS":GOSUB30 
28340 Y=7:X=9:A$="ALL SAFETYS":GOSUB30
28360 Y=10:X=9:A$= STR$(MS)+" MILES":GOSUB30
28400 Y=13:X=10:A$="SAFE TRIP":GOSUB30
28450 Y=17:X=10:A$="SHUT OUT":GOSUB30
28500 Y=21:X=2:A$="TOTAL SCORE THIS ROUND":GOSUB30
28600 Y=3:X=1:A$="YOU                 COMPUTER":GOSUB30
28700 FORI=1TO2:IFI=1THENX=1:GOTO28800
28701 X=21
28800 Y=5:A$=STR$(SB(I)):GOSUB30:Y=8:A$=STR$(SF(I)):GOSUB30
28820 Y=11:A$=STR$(WR(I)):GOSUB30:Y=15:A$=STR$(CC(I)):GOSUB30
28900 Y=18:A$=STR$(SU(I)):GOSUB30
29000 Y=22:A$=STR$(SC(I)):GOSUB30:Y=22:A$=STR$(SC(I)):GOSUB30
29100 NEXTI
29200 PRINT32*4,"HIT enter TO CONTINUE";:INPUTYN$
29300 CLS:Y=1:X=1:A$="ENDING SCORE:":GOSUB30
29320 Y=3:X=1:A$="    YOUR SC:":GOSUB30
29340 Y=5:X=1:A$="COMPUTER SC:":GOSUB30
29400 Y=3:X=16:A$=STR$(TL(1)):GOSUB30:Y=5:X=16:A$=STR$(TL(2)):GOSUB30
29500 IFTL(1)<WSANDTL(2)<WSTHEN29700
29501 Y=10:X=1:A$="GAME OVER":GOSUB30:GOTO29900
29700 Y=10:X=1:A$="NEXT ROUND":GOSUB30
29720 INPUTYN$:CLS:Y=2:X=8:G8=1:A$="MC-10 MILLE BORNES":GOSUB30:GOTO1100
29800 REM 
29900 END
30000 REM
30200 REM CARD DATA
30300 DATA"OUT OF GAS","FLAT TIRE ","ACCIDENT  ","SPEED LIM ","STOP      "
30400 DATA"OUT OF GAS","FLAT TIRE ","ACCIDENT  ","SPEED LIM ","STOP      "
30500 DATA"GASOLINE  ","SPARE TIRE","END LIMIT ","ROLL !","EXTRA TANK"
30501 DATA"PUN PROOF ","DRIV ACE  ","RITE O WAY","200","100","75","50","25"
30600 DATA"GASOLINE  ","SPARE TIRE","END LIMIT ","ROLL !"
30700 DATA"GASOLINE  ","SPARE TIRE","END LIMIT ","ROLL !"
30800 DATA"REPAIRS   ","REPAIRS   ","REPAIRS   ","REPAIRS   ","REPAIRS   ","REPAIRS   "
30900 DATA"STOP      ","STOP      ","ROLL !","ROLL !","ROLL !","ROLL !","ROLL !","ROLL !"
30901 DATA"ROLL !","ROLL !","ROLL !","ROLL !","ROLL !"
31000 DATA"GASOLINE  ","SPARE TIRE","END LIMIT "
31100 DATA"GASOLINE  ","SPARE TIRE","END LIMIT "
31200 DATA"GASOLINE  ","SPARE TIRE","END LIMIT "
31300 DATA"200","200","200"
31400 DATA"100","100","100","100","100","100","100","100","100","100","100"
31500 DATA"75","75","75","75","75","75","75","75","75"
31600 DATA"50","50","50","50","50","50","50","50","50"
31700 DATA"25","25","25","25","25","25","25","25","25"
31800 DATA"END-DATA"
