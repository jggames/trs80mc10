0 CLS:CLEAR1000:DIMC1,CC,M$,I$,MC:MC=16384:GOTO9
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 PRINT TAB(14);"AWARI":PRINT TAB(7)"CREATIVE COMPUTING":PRINTTAB(5)"MORRISTOWN, NEW JERSEY"
10 PRINT:INPUT"DO YOU NEED INSTRUCTIONS";M$:CLS
11 IFLEFT$(M$,1)="Y"THENGOSUB1000
15 DIM B(13),G(13),F(50):N=0
20 PRINT:PRINT:E=0
25 FOR I=0 TO 12:B(I)=3:NEXT I
30 C=0:F(N)=0:B(13)=0:B(6)=0
35 M$=" 1   2   3   4   5   6 ":GOSUB 500
40 PRINT "YOUR MOVE";:GOSUB 110
45 IF E=0 THEN 80
50 IF M=H THEN GOSUB 100
55 IF E=0 THEN 80
60 PRINT "MY MOVE IS ";:GOSUB 800
65 IF E=0 THEN 80
70 IF M=H THEN PRINT ",";:GOSUB 800
75 PRINT:IF E>0 THEN 35
80 PRINT:PRINT"GAME OVER"
85 D=B(6)-B(13):IF D<0 THEN PRINT "I WIN BY";-D;"POINTS":GOTO 20
90 N=N+1:IF D=0 THEN PRINT "DRAWN GAME":GOTO 20
95 PRINT "YOU WIN BY";D;"POINTS":GOTO 20
100 PRINT "AGAIN";
110 INPUT M:IF M<7 THEN IF M>0 THEN M=M-1:GOTO 130
120 PRINT "ILLEGAL MOVE":GOTO 100
130 IF B(M)=0 THEN 120
140 H=6:GOSUB 200
150 M$=" 6   5   4   3   2   1 ":GOTO 500
200 K=M:GOSUB 600
205 E=0:IF K>6 THEN K=K-7
210 C=C+1:IF C<9 THEN F(N)=F(N)*6+K
215 FOR I=0 TO 5:IF B(I)<>0 THEN 230
220 NEXT I
225 RETURN
230 FOR I=7 TO 12:IF B(I)<>0 THEN E=1:RETURN
235 GOTO 220
500 PRINT:PRINT"   ";
505 FOR I=12 TO 7 STEP -1:GOSUB 580
510 NEXT I
515 PRINT:I=13:GOSUB 580
520 GOSUB7:PRINT B(6):PRINT "   ";
525 FOR I=0 TO 5:GOSUB 580
530 NEXT I
535 PRINT:PRINT:RETURN
580 IF B(I)<10 THEN PRINT " ";
585 PRINT B(I);:RETURN
600 P=B(M):B(M)=0
605 FOR P=P TO 1 STEP -1:M=M+1:IF M>13 THEN M=M-14
610 B(M)=B(M)+1:NEXT P
615 IF B(M)=1 THEN IF M<>6 THEN IF M<>13 THEN IF B(12-M)<>0 THEN 625
620 RETURN
625 B(H)=B(H)+B(12-M)+1:B(M)=0:B(12-M)=0:RETURN
800 D=-99:H=13
805 FOR I=0 TO 13:G(I)=B(I):NEXT I
810 FOR J=7 TO 12:IF B(J)=0 THEN 885
815 G=0:M=J:GOSUB 600
820 FOR I=0 TO 5:IF B(I)=0 THEN 845
825 L=B(I)+I:R=0
830 IF L>13 THEN L=L-14:R=1:GOTO 830
835 IF B(L)=0 THEN IF L<>6 THEN IF L<>13 THEN R=B(12-L)+R
840 IF R>Q THEN Q=R
845 NEXT I
850 Q=B(13)-B(6)-Q:IF C>8 THEN 875
855 K=J:IF K>6 THEN K=K-7
860 FOR I=0 TO N-1:IF F(N)*6+K=INT(F(I)/6^(7-C)+.1) THEN Q=Q-2
870 NEXT I
875 FOR I=0 TO 13:B(I)=G(I):NEXT I
880 IF Q>=D THEN A=J:D=Q
885 NEXT J
890 M=A:PRINT CHR$(42+M);:GOTO 200
900 FOR I=0 TO N-1:PRINT B(I):NEXT I
999 END
1000 M$="AWARI IS AN ANCIENT AFRICAN GAME.":GOSUB1
1001 M$="THE BOARD IS DIVIDED INTO SIX COMPARTMENTS OR PITS ON EACH SIDE (NUMBERED AS SHOWN BELOW).":GOSUB1
1002 M$="IN ADDITION, THERE ARE TWO SPECIAL HOME PITS AT THE ENDS:":GOSUB1
1003 PRINT"         MY SIDE"
1004 PRINT"       6 5 4 3 2 1"
1005 PRINT"MY                    YOUR"
1006 PRINT"HOME                  HOME"
1007 PRINT"       1 2 3 4 5 6"
1008 PRINT"        YOUR SIDE":GOSUB2000
1009 M$="A MOVE IS MADE BY TAKING ALL OF THE BEANS FROM ANY (NON-EMPTY) PIT ON YOUR OWN SIDE.":GOSUB1
1010 M$="STARTING FROM THE PIT TO THE RIGHT OF THIS ONE, THESE BEANS ARE 'SOWN' ONE IN EACH PIT "
1011 M$=M$+"WORKING AROUND THE BOARD ANTICLOCKWISE.":GOSUB1
1012 M$="A TURN CONSISTS OF ONE OR TWO MOVES.":GOSUB1:GOSUB2000
1013 M$="IF THE LAST BEAN OF YOUR MOVE IS SOWN IN YOUR OWN HOME YOU MAY TAKE A SECOND MOVE.":GOSUB1
1014 M$="IF THE LAST BEAN SOWN IN A MOVE LANDS IN AN EMPTY PIT, PROVIDED THAT THE OPPOSITE PIT IS NOT EMPTY, "
1015 M$=M$+"ALL THE BEANS IN THE OPPOSITE PIT, TOGETHER WITH THE LAST BEAN SOWN, ARE 'CAPTURED' AND MOVED TO THE PLAYER'S HOME."
1016 GOSUB1:GOSUB2000
1017 M$="WHEN EITHER SIDE IS EMPTY, THE GAME IS FINISHED. THE PLAYER WITH THE MOST BEANS IN HIS HOME HAS WON.":GOSUB1
1018 M$="IN THE COMPUTER VERSION, THE BOARD IS PRINTED AS 14 NUMBERS REPRESENTING THE 14 PITS.":GOSUB1
1019 PRINT"  3 3 3 3 3 3"
1020 PRINT"0             0"
1021 PRINT"  3 3 3 3 3 3":GOSUB2000
1022 M$="THE PITS ON YOUR (LOWER) SIDE ARE NUMBERED 1-6 FROM LEFT TO RIGHT.":GOSUB1
1023 M$="THE PITS ON MY (THE COMPUTER'S) SIDE ARE NUMBERED FROM MY LEFT (YOUR RIGHT).":GOSUB1
1024 M$="TO MAKE A MOVE YOU TYPE IN THE NUMBER OF A PIT IF THE LAST BEAN LANDS IN YOUR HOME, "
1025 M$=M$+"THE COMPUTER TYPES 'AGAIN?' AND YOU THEN TYPE IN YOUR SECOND MOVE.":GOSUB1:GOSUB2000
1026 M$="THE COMPUTER'S MOVE IS TYPED, FOLLOWED BY A DIAGRAM OF THE BOARD IN ITS NEW STATE.":GOSUB1
1027 M$="THE COMPUTER ALWAYS OFFERS YOU THE FIRST MOVE.":GOSUB1
1028 M$="THIS IS CONSIDERED TO BE A SLIGHT ADVANTAGE.":GOSUB1:GOSUB2000
1029 M$="THERE IS A LEARNING MECHANISM IN THE PROGRAM THAT CAUSES THE PLAY OF THE COMPUTER TO IMPROVE AS IT PLAYS MORE GAMES."
1030 GOSUB1
1031 M$="THIS VERSION OF AWARI IS ADOPTED FROM ONE ORIGINALLY WRITTEN BY GEOFF WYVILL OF BRADFORD, YORKSHIRE, ENGLAND."
1032 GOSUB1:GOSUB2000:RETURN
2000 PRINT:PRINT"press a key"
2010 M$=INKEY$:IFM$=""THEN2010
2020 CLS:RETURN
