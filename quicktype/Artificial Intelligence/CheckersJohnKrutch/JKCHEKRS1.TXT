0 CLEAR1500:M=16384
1 REM CHECKERS, VER 1.1
10 REM
20 GOSUB 130:REM INIT
30 GOSUB 230
40 GOSUB 970:REM SET ARRAY TO INIT PIECE CONFIG
50 GOSUB 450
60 GOSUB 570
70 GOSUB 780
80 GOSUB 840:REM CHANGE ARRAY TO REFLECT NEW PSN OF PIECES 
81 IF C1 THEN SOUND 1,4:GOTO 70: REM ERROR TRAPPING ON INPUT
90 GOSUB 570
100 IF M$="X" THEN 70:DOUBLE AND TRIPLE JUMPS
110 GOSUB 1040
120 GOTO 60

130 REM INITIALIZATION
140 CLS
150 C1=RND(-(PEEK(9)*256+PEEK(10)))
160 DIM T$(11,11)
170 BL$="€ÓÓ€"
180 WH$="€³³€"
200 KW$="€»·€"
210 X$="€€€€"
220 RETURN

230 REM SET ARRAY TO INITIAL PIECE CONFIGURATION
240 FOR I=2 TO 9
250 FOR J=2 TO 9
260 T$(I,J)=X$
270 NEXT J,I
280 FOR I=2 TO 4 STEP 2
290 FOR J=2 TO 8 STEP 2
300 T$(I,J)=WH$
310 NEXT J,I
320 FOR I=3 TO 3
330 FOR J=3 TO 9 STEP 2
340 T$(I,J)=WH$
350 NEXT J,I
360 FOR I=7 TO 9 STEP 2
370 FOR J=3 TO 9 STEP 2
380 T$(I,J)=BL$
390 NEXT J,I
400 FOR I=8 TO 8 
410 FOR J=2 TO 8 STEP 2
420 T$(I,J)=BL$
430 NEXT J,I
440 RETURN

450 REM DRAW CHECKERBOARD
460 REM DEFINE STRINGS A-D
470 B$="ÏÏÏÏ"
480 C$="€€€€"
490 A$=B$+C$+B$+C$+B$+C$+B$+C$
500 D$=C$+B$+C$+B$+C$+B$+C$+B$
510 PRINTA$A$D$D$A$A$D$D$A$A$D$D$A$A$D$C$B$C$B$C$B$C$;
520 FOR I=16892 TO 16895
530 POKE I,207
540 NEXT I
550 REM DEF SNG A-D
560 RETURN

570 REM PRINT NUMBERS ON CHECKERBOARD SQUARES AND
580 REM PRINT CURRENT ARRANGEMENT OF PIECES
590 L=6
600 FOR I=9 TO 3 STEP -2
610 FOR J=3 TO 9 STEP 2
620 POKEM+L,48+I:POKEM+L+1,48+J
630 PRINT@L+30,T$(I,J);
640 L=L+8
650 NEXT J
660 L=L+96
670 NEXT I
680 L=66
690 FOR I=8 TO 2 STEP -2
700 FOR J=2 TO 8 STEP 2
710 POKEM+L,48+I:POKEM+L+1,48+J
720 PRINT@L+30,T$(I,J);
730 L=L+8
740 NEXT J
750 L=L+96
760 NEXT I
770 RETURN

780 REM INPUT
790 C1=0:B$="":SOUND56,2:SOUND98,1
800 A$=INKEY$:IF A$="" THEN 800
810 B$=B$+A$:IFLEN(B$)=3 THEN 820
811 SOUND 200,1
820 IF LEN(B$)=5 THEN 830
821 GOTO 800
830 RETURN

840 REM CHANGE THE ARRAY TO REFLECT NEW BOARD POSITION
850 I=VAL(LEFT$(B$,1)):J=VAL(MID$(B$,2,1)):IF I<2 OR J<2 THEN C1=1:RETURN
860 K=VAL(MID$(B$,4,1)):L=VAL(RIGHT$(B$,1)):IF K<2 OR L<2 THEN C1=1:RETURN
870 M$=MID$(B$,3,1):IF M$="X" OR M$="," OR M$=" " OR M$="-" THEN 880
871 C1=1:RETURN
880 IF K=I-2 AND L=J-2 THEN T$(K,L)=KW$:T$(I,J)=X$:T$(I-1,J-1)=X$:GOTO960
890 IF K=I-2 AND L=J+2 THEN T$(K,L)=KW$:T$(I,J)=X$:T$(I-1,J+1)=X$:GOTO960
900 IF K=9 THEN T$(K,L)=KW$:GOTO 930
910 IF T$(I,J)=KW$ THEN T$(K,L)=KW$:GOTO 930
920 T$(K,L)=WH$
930 T$(I,J)=X$
940 IF K=I+2 AND L=J+2 THEN T$(K-1,L-1)=X$:GOTO 960
950 IF K=I+2 AND L=J-2 THEN T$(K-1,L+1)=X$
960 RETURN

970 REM COMPUTER'S FIRST MOVE
980 RA=RND(4)
990 IF RA=1 THEN T$(7,3)=X$:T$(6,4)=BL$
1000 IF RA=2 THEN T$(7,5)=X$:T$(6,4)=BL$
1010 IF RA=3 THEN T$(7,5)=X$:T$(6,6)=BL$
1020 IF RA=4 THEN T$(7,7)=X$:T$(6,6)=BL$
1030 RETURN

1040 REM EVALUATION ROUTINE
1050 REM A. BLACK LOOKING FOR JUMP POSSIBILITIES
1060 FOR I=2 TO 9
1070 FOR J=2 TO 9
1080 IF T$(I,J)=BL$ THEN 1090
1081 GOTO 1110
1090 IF T$(I-1,J+1)=WH$ AND T$(I-2,J+2)=X$ THEN T$(I-1,J+1)=X$:T$(I-2,J+2)=BL$:T$(I,J)=X$:GOTO 1360
1100 IF T$(I-1,J-1)=WH$ AND T$(I-2,J-2)=X$ THEN T$(I-1,J-1)=X$:T$(I-2,J-2)=BL$:T$(I,J)=X$:GOTO 1360
1110 NEXT J,I
1120 REM B. BLACK GUARDING ITSELF AGAINST A JUMP
1130 FOR I=2 TO 9
1140 FOR J=2 TO 9
1150 IF T$(I,J)=BL$ THEN 1160
1151 GOTO 1220
1160 IF T$(I-1,J-1)=WH$ AND T$(I+1,J+1)=X$ THEN 1180
1170 IF T$(I-1,J+1)=WH$ AND T$(I+1,J-1)=X$ THEN 1200
1171 GOTO 1220
1180 IF T$(I+2,J)=BL$ THEN T$(I+2,J)=X$:T$(I+1,J+1)=BL$:GOTO 1360
1190 IF T$(I+2,J+2)=BL$ THEN T$(I+2,J+2)=X$:T$(I+1,J+1)=BL$:GOTO 1360
1200 IF T$(I+2,J-2)=BL$ THEN T$(I+2,J-2)=X$:T$(I+1,J-1)=BL$:GOTO 1360
1210 IF T$(I+2,J)=BL$ THEN T$(I+2,J)=X$:T$(I+1,J-1)=BL$:GOTO 1360
1220 NEXT J,I
1230 REM C. BLACK LOOKING FOR A MOVE
1240 REM WHICH WON'T RESULT IN A CAPTURE
1250 FOR I=2 TO 9
1260 FOR J=2 TO 9
1270 IF T$(I,J)=BL$ THEN 1280
1271 GOTO 1340
1280 IF T$(I-2,J)=WH$ AND T$(I,J-2)=X$ THEN 1310
1290 IF T$(I-2,J-2)=WH$ THEN 1310
1300 IF T$(I-1,J-1)=X$ THEN T$(I,J)=X$:T$(I-1,J-1)=BL$:GOTO 1360
1310 IF T$(I-2,J)=WH$ AND T$(I,J+2)=X$ THEN 1340
1320 IF T$(I-2,J+2)=WH$ THEN 1340
1330 IF T$(I-1,J+1)=X$ THEN T$(I,J)=X$:T$(I-1,J+1)=BL$:GOTO 1360
1340 NEXT J,I
1350 REM RESET EDGES OF ARRAY TO THE EMPTY STRING
1360 FOR I=0 TO 1:FOR J=0 TO 1:T$(I, J)="":NEXT J,I
1370 FOR I=10 TO 11:FOR J=10 TO 11:T$(I,J)="":NEXT J,I
1380 RETURN


REM MC-10 PIECES AND BOARD
REM ÏÏÏÏ€€€€
REM ÏÏÏÏ€€€€
REM €€€€ÏÏÏÏ
REM €€€€ÏÏÏÏ
REM €ÐÐ€€°°€
REM €Û×€€»·€
REM €€€€€€€€
REM €ÓÓ€€³³€
REM MC-10 EDITS JIM GERRIE
