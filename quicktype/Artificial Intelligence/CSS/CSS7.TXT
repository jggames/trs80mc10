0 CLS
1 PRINT"  ****************************"
2 PRINT"  **    CSS CHESS PROGRAM   **"
3 PRINT"  ****************************"
4 PRINT"      by dieter steinwender"
5 PRINT
6 DIMB(119),S(10,4)
7 DIMM(10),A$(10),U(10),F$(10)
8 DIMR1(4),R2(4),R3(4),R4(4),R5(4),R6(4),R7(4),G1(10)
9 DIMW(10),P(10)
10 DIMO(15),OA(6),OE(6),L(6),Z(200,6)
11 DIMZT(9,8),BV(8),BL(2,9),TL(2,9),I,J,CL,K,Z1,V,I2,C,P,R,S,T,W,PT,Y1,X1,Z0,Z1,Z2,MZ,MT,II,FA,T7,T0,W$,B$,E$
12 B$="€€€€"
13 W$=CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207):C(0)=32:C(2)=48
14 DIMT7(2),BA(2),KR(2),KL(2)
15 RESTORE
16 FORI=0TO119
17 B(I)=100
18 NEXT
19 FORI=9TO2STEP-1
20 FORJ=1TO8
21 READB(I*10+J)
22 NEXT
23 NEXT
24 DATA-2,-4,-3,-5,-6,-3,-4,-2
25 DATA-1,-1,-1,-1,-1,-1,-1,-1
26 DATA,,,,,,,
27 DATA,,,,,,,
28 DATA,,,,,,,
29 DATA,,,,,,,
30 DATA1,1,1,1,1,1,1,1
31 DATA2,4,3,5,6,3,4,2
32 F=1
33 FORI=1TO4
34 S(.,I)=1
35 NEXT
36 S(.,.)=.
37 M(.)=.
38 FORI=.TO6
39 READA$(I),U(I)
40 NEXT
41 DATA".",0,"P",100,"R",500,"B",350
42 DATA"N",325,"Q",900,"K",20000
43 M0=48100
44 F$(0)="."
45 F$(1)="."
46 F$(2)="*"
47 FORI=.TO15
48 READO(I)
49 NEXT
50 DATA-9,-11,9,11,-1,10,1,-10,19,21,12,-8,-19,-21,-12,8
51 FORI=1TO6
52 READOA(I),OE(I),L(I)
53 NEXT
54 DATA,3,,4,7,1,,3,1,8,15,,,7,1,,7,
55 FORI=1TO4
56 READR1(I),R2(I),R3(I),R4(I),R5(I),R6(I),R7(I)
57 NEXT
58 DATA96,97,95,97,95,97,98
59 DATA92,94,93,95,95,93,91
60 DATA26,27,25,27,25,27,28
61 DATA22,24,23,25,25,23,21
62 FORJ=1TO8
63 FORI=2TO9
64 ZT(I,J)=INT(12-4*(ABS(5.5-I)+ABS(4.5-J)))
65 NEXT
66 READBV(J)
67 NEXT
68 DATA,,4,6,7,2,,
69 MZ=0
70 G1(0)=1
71 T0=1:GOTO98
72 CL=1:FORY1=0TO7:CL=CL*-1:FORX1=0TO7
73 P=((Y1*2)*32)+(X1*4):CL=CL*-1
74 IFCL=-1THENPRINT@P,B$;:PRINT@P+32,B$;:GOTO75
75 IFCL=1THENIFP<476THENPRINT@P,W$;:PRINT@P+32,W$;
76 NEXT:NEXT:PRINT@477,LEFT$(W$,2);:PRINT@477+32,LEFT$(W$,2);
77 POKE16384+511,207:POKE16384+511-3,207:POKE16384+511-32,207:POKE16384+511-32-3,207
78 RETURN
79 P=(32*(R*2))+(S*4)+1:ONPTGOSUB80,81,82,83,84,85:RETURN
80 PRINT@P+32,CHR$(135+C);CHR$(130+C);:RETURN
81 PRINT@P,CHR$(130+C);CHR$(129+C);:PRINT@P+32,CHR$(133+C);CHR$(138+C);:RETURN
82 PRINT@P,CHR$(129+C);CHR$(128+C);:PRINT@P+32,CHR$(141+C);CHR$(136+C);:RETURN
83 PRINT@P,CHR$(129+C);CHR$(130+C);:PRINT@P+32,CHR$(135+C);CHR$(130+C);:RETURN
84 PRINT@P,CHR$(131+C);CHR$(130+C);:PRINT@P+32,CHR$(135+C);CHR$(130+C);:RETURN
85 PRINT@P,CHR$(130+C);CHR$(130+C);:PRINT@P+32,CHR$(141+C);CHR$(136+C);:RETURN
86 PRINT@P,CHR$(143);CHR$(143);:PRINT@P+32,CHR$(143);CHR$(143);
87 C=PEEK(16384+P-1):PRINT@P,CHR$(C);CHR$(C);:PRINT@P+32,CHR$(C);CHR$(C);:RETURN
88 PRINT"--HELP--"
89 PRINT"rE=RESTART"
90 PRINT"qQ=QUIT"
91 PRINT"vB=VIEW BOARD (ENTER)"
92 PRINT"nM=NEXT MOVE"
93 PRINT"mM=MULTIPLE MOVES"
94 PRINT"lM=LIST LEGAL MOVES"
95 PRINT"tB=TAKE BACK MOVE"
96 PRINT"dL=DIFFICULTY LEVEL"
97 PRINT"sB=SETUP BOARD":RETURN
98 T=0
99 IFI$<>""THENE$=I$:I$="":GOTO102
101 PRINT"YOUR MOVE";:INPUTE$
102 IFE$<>"RE"ANDE$<>"R"THEN104
103 GOTO15
104 IFE$<>"QQ"ANDE$<>"Q"THEN106
105 END
106 IFE$<>"VB"ANDE$<>""ANDE$<>"V"THEN109
107 GOSUB177
108 GOTO99
109 IFE$<>"SB"ANDE$<>"S"THEN112
110 GOSUB205
111 GOTO99
112 IFE$<>"NM"ANDE$<>"N"THEN114
113 GOTO159
114 IFE$<>"MM"ANDE$<>"M"THEN118
115 PRINT"MULTIPLE MOVES: ";
116 IFMZ=0THENMZ=1:PRINT"BEGIN":GOTO99
117 MZ=0:PRINT"END":GOTO99
118 IFE$<>"LM"ANDE$<>"L"THEN123
119 Z0=Z1
120 GOSUB265
121 Z1=Z0
122 GOTO99
123 IFE$<>"TB"ANDE$<>"T"THEN130
124 IFT=1THEN127
125 PRINT"SORRY: NOT POSSIBLE"
126 GOTO99
127 GOSUB429
128 PRINT"OK"
129 GOTO99
130 IFE$<>"DL"ANDE$<>"D"THEN135
131 PRINT"LEVEL OF ANALYSIS=";T0;
132 INPUT T0
133 T0=ABS(T0)
134 GOTO99
135 IFLEN(E$)<4THENGOSUB88:GOTO99
137 V1=ASC(E$)-64+10*(ASC(MID$(E$,2,1))-47)
138 N1=ASC(MID$(E$,3,1))-64+10*(ASC(MID$(E$,4,1))-47)
139 GOSUB373
140 GOSUB282
141 FORZ1=1TOG-1
142 IFZ(Z1,1)<>V1THEN144
143 IFZ(Z1,2)=N1THEN147
144 NEXT
145 PRINT"ILLEGAL MOVE"
146 GOTO99
147 IFZ(Z1,4)=0 THEN 151
148 IFRIGHT$(E$,1)="N"THENZ1=Z1+1
149 IFRIGHT$(E$,1)="B"THENZ1=Z1+2
150 IFRIGHT$(E$,1)="R"THENZ1=Z1+3
151 PRINT"YOU MOVE: ";
152 GOSUB254
153 GOSUB381
154 GOSUB282
155 IFMT=0THEN158
156 GOSUB429
157 GOTO145
158 IFMZ=1THEN99
159 I$=""
160 GOSUB373
161 GOSUB454
162 IFZ2=0THEN173
163 IFW=1THEN174
164 IFW=-32766THEN171
165 Z1=Z2
166 PRINT"MY MOVE: ";
167 GOSUB254
168 GOSUB381
169 IFW=-2THEN174
170 IFW<32765THEN175
171 PRINT"CHECK MATE. YOU WIN!"
172 GOTO175
173 IFT0=0THEN175
174 PRINT"STALEMATE: TIE GAME!"
175 PRINT"VALUE=";W:PRINT"POSITIONS ANALYSED=";C1
176 GOTO99
177 GOSUB72:FORI=9TO2STEP-1:FORJ=1TO8:GOSUB199:NEXT:NEXT:SOUND1,1:I$="":J=1:I=2:GOSUB199
184 E$=INKEY$:GOSUB86
185 IFE$="A"THENIFJ>1THENGOSUB199:J=J-1:GOSUB199
186 IFE$="S"THENIFJ<8THENGOSUB199:J=J+1:GOSUB199
187 IFE$="Z"THENIFI>2THENGOSUB199:I=I-1:GOSUB199
188 IFE$="W"THENIFI<9THENGOSUB199:I=I+1:GOSUB199
189 IFE$=CHR$(13)THEN I$=I$+CHR$(64+J)+RIGHT$(STR$(I-1),1):SOUND100,1:IFLEN(I$)=4THENCLS:RETURN
190 IFE$<>" "THENGOSUB199:GOTO184
191 PRINT@511,CHR$(207);:PRINT
192 PRINT"MATERIAL VALUE = ";M(T)
193 PRINT"EN PASSANT SQUARE = ";:GOSUB275
194 PRINT"CASTLE STATUS = ";S(T,1);S(T,2);S(T,3);S(T,4)
195 PRINT"SIDE TO MOVE=";
196 IFF=1THENPRINT" WHITE/RED":GOTO198
197 PRINT" BLACK/BLUE"
198 RETURN
199 R=9-I
200 A1=B(I*10+J)
201 F1=SGN(A1)
202 A1=ABS(A1)
203 S=J-1:PT=A1:C=C(F1+1):GOSUB79
204 RETURN
205 T=0
207 INPUT"CLEAR BOARD (Y/N)";E$
209 IFE$="N"THEN217
210 IFE$<>"Y"THEN207
211 FORI=2TO9:FORJ=1TO8:B(I*10+J)=.:NEXT:NEXT:M(.)=.
217 PRINT"WHITE/RED ";
218 F=1
219 GOSUB240
220 PRINT"BLACK/BLUE ";
221 F=-1
222 GOSUB240
223 PRINT"SIDE TO MOVE (W/B) ";
224 INPUTE$
225 IFE$="B"THEN228
226 IFE$<>"W"THEN223
227 F=1
228 PRINT"CHANGE EN PASSANT/CASTLE STATUS (Y/N)";
229 INPUTE$
230 IFE$="N"THEN237
231 IFE$<>"Y"THEN228
232 PRINT"PIECE COLUMN";:INPUTE$
233 IFE$="0"THENS(.,.)=.:GOTO235
234 S(.,.)=F*15+ASC(E$)-9
235 PRINT"CASTLE STATUS ";
236 INPUTS(0,1),S(0,2),S(0,3),S(0,4)
237 GOSUB177
238 RETURN
240 INPUTE$:IFE$="."THEN253
242 IFLEN(E$)<>3THEN246
243 FORI=.TO6
244 IFLEFT$(E$,1)=A$(I)THENA=I:GOTO248
245 NEXT
246 PRINT"ENTRY ERROR (EG. RA1 OR .=END)":GOTO240
248 N=ASC(MID$(E$,2,1))-64+10*(ASC(MID$(E$,3,1))-47)
249 M(.)=M(.)-SGN(B(N))*U(ABS(B(N)))
250 B(N)=F*A
251 M(.)=M(.)+F*U(A)
252 GOTO240
253 RETURN
254 A$=A$(ABS(B(Z(Z1,1))))
256 FORJ=1TO2:RE=INT(Z(Z1,J)/10):LI=Z(Z1,J)-10*RE:A$=A$+CHR$(64+LI)+CHR$(47+RE):NEXT
261 IFZ(Z1,4)=.THEN263
262 A$=A$+"="+A$(Z(Z1,4))
263 PRINTA$
264 RETURN
265 GOSUB282:IFMT=.THEN270
268 PRINT"KING COULD BE TAKEN":RETURN
270 PRINTG-G1(T);"POSSIBLE LEGAL MOVES"
271 FORZ1=G1(T)TOG-1
272 GOSUB254
273 NEXT
274 RETURN
275 IFS(T,0)=.THENA$="0":GOTO280
277 RE=INT(S(T,.)/10):LI=S(T,0)-10*RE:A$=CHR$(64+LI)+CHR$(47+RE)
280 PRINTA$:RETURN
282 MT=.:G=G1(T):FORV=21TO98:A=B(V):IFA=100THEN326
288 IFSGN(A)<>FTHEN326
289 A=ABS(A)
290 IFA<>1THEN313
291 N=V+(F*10)
292 IFB(N)<>.THEN300
293 GOSUB356
294 IF(INT(V/10)-5.5)*F<>-2.5THEN300
295 N=V+(F*20)
296 IFB(N)<>.THEN300
297 GOSUB363
298 Z(G-1,6)=INT((V+N)*.5)
300 FORI=1TO2:N=V+O(F+I):IFB(N)=100THEN311
303 IFN=S(T,.)THEN308
304 IFSGN(B(N))<>-FTHEN311
305 IFB(N)=-F*6THENMT=1:GOTO355
306 GOSUB356:GOTO311
308 GOSUB363
309 Z(G-1,3)=1
310 Z(G-1,6)=N-10*F
311 NEXT
312 GOTO326
313 FORI=OA(A)TOOE(A):LA=L(A):N=V
317 N=N+O(I)
318 IFB(N)=100THEN325
319 IFSGN(B(N))=FTHEN325
320 IFB(N)=.THEN323
321 IFB(N)=(-1*F)*6THENMT=1:GOTO355
322 LA=0
323 GOSUB363
324 IFLA=1THEN317
325 NEXT
326 NEXT
327 FORI=F+2TOF+3
328 IFS(T,I)=.THEN353
329 FORJ=R1(I)TOR2(I)
330 IFB(J)<>.THEN353
331 NEXT
332 FORJ=R3(I)TOR4(I)
333 FORK=.TO7
334 N=J
335 N=N+O(K)
336 IFB(N)=100THEN344
337 IFSGN(B(N))=FTHEN344
338 IFB(N)=0THEN335
339 IFOA(ABS(B(N)))>KTHEN344
340 IFOA(ABS(B(N)))<KTHEN344
341 IFN=J+O(K)THEN353
342 IFL(ABS(B(N)))<>1THEN344
343 GOTO353
344 NEXT
345 FORK=8TO15
346 IFB(J+O(K))=-F*4THEN353
347 NEXT
348 NEXT
349 V=R5(I)
350 N=R6(I)
351 GOSUB363
352 Z(G-1,5)=I
353 NEXT
354 G1(T+1)=G
355 RETURN
356 IF(INT(N/10)-5.5)*F<>3.5THENGOSUB363:GOTO362
358 FORI2=5TO2STEP-1
359 GOSUB363
360 Z(G-1,4)=I2
361 NEXT
362 RETURN
363 Z(G,1)=V:Z(G,2)=N:Z(G,3)=ABS(B(N))
367 FORI1=4TO6
368 Z(G,I1)=0
369 NEXT
370 IFG=200THEN372
371 G=G+1
372 RETURN
373 IFT=.THEN380
375 FORI=.TO4
376 S(.,I)=S(1,I)
377 NEXT
378 M(0)=M(1)
379 T=0
380 RETURN
381 T=T+1
383 S(T,.)=.
384 FORI=1TO4
385 S(T,I)=S(T-1,I)
386 NEXT
387 M(T)=M(T-1)
388 V=Z(Z1,1)
389 N=Z(Z1,2)
390 IFZ(Z1,6)=.THEN396
391 IFZ(Z1,3)=.THEN394
392 B(Z(Z1,6))=.
393 GOTO423
394 S(T,0)=Z(Z1,6)
395 GOTO423
396 IFV<>R5(F+2)THEN401
398 S(T,F+2)=.
399 S(T,F+3)=.
400 GOTO416
401 IFV<>R7(F+2)THEN404
402 S(T,F+2)=.
403 GOTO406
404 IFV<>R7(F+3)THEN406
405 S(T,F+3)=0
406 IFN<>R7((-1*F)+2)THEN409
407 S(T,-F+2)=.
408 GOTO411
409 IFN<>R7((-1*F)+3)THEN411
410 S(T,(-1*F)+3)=.
411 IFZ(Z1,4)=.THEN423
413 B(V)=Z(Z1,4)*F
414 M(T)=M(T)+F*(U(Z(Z1,4))-100)
415 GOTO423
416 RO=Z(Z1,5)
418 IFRO=.THEN423
419 VO=R7(RO)
420 B(VO)=.
421 NA=INT((R5(RO)+R6(RO))*.5)
422 B(NA)=2*F
423 B(N)=B(V)
425 B(V)=0
426 M(T)=M(T)+F*U(Z(Z1,3))
427 F=-F
428 RETURN
429 F=(-1*F)
431 V=Z(Z1,1)
432 N=Z(Z1,2)
433 IFZ(Z1,6)=.THEN439
434 IFZ(Z1,3)=.THEN450
435 B(Z(Z1,6))=(-1*F)
436 B(V)=F
437 B(N)=0
438 GOTO452
439 RO=Z(Z1,5)
441 IFRO=.THEN447
442 NA=INT((R5(RO)+R6(RO))*.5)
443 B(NA)=0
444 VO=R7(RO)
445 B(VO)=2*F
446 GOTO450
447 IFZ(Z1,4)=.THEN450
449 B(N)=F
450 B(V)=B(N)
451 B(N)=-F*Z(Z1,3)
452 T=T-1
453 RETURN
454 Z2=0
456 C1=0
457 W(0)=-32767
458 W(1)=-32767
459 IFT<T0THEN463
460 GOSUB499
461 W(T+2)=W*F
462 GOTO493
463 GOSUB282
464 IFMT=.THEN467
465 W(T+2)=32767-T
466 GOTO493
467 IFG>G1(T)THEN471
468 W(T+2)=.
469 GOTO493
471 P(T)=G1(T)
472 W(T+2)=W(T)
473 Z1=P(T)
474 IFT<>.THEN476
475 GOSUB254
476 GOSUB381
477 C1=C1+1
478 GOTO459
479 IF-1*(W(T+3))<=W(T+2)THEN485
480 W(T+2)=(-1*W(T+3))
481 IFT>.THEN484
482 Z2=P(T)
483 PRINT"NEW BEST MOVE - VALUE=";W(2)
484 IFW(T+2)>=(-1*W(T+1))THEN493
485 P(T)=P(T)+1
486 IFP(T)<G1(T+1)THEN473
487 IFW(T+2)<>-32766+TTHEN493
488 F=(-1*F)
489 GOSUB282
490 F=(-1*F)
491 IFMT=1THEN493
492 W(T+2)=1+T
493 IFT=.THEN497
494 Z1=P(T-1)
495 GOSUB429
496 GOTO479
497 W=W(2)
498 RETURN
499 M=0
502 W=0
503 FORI=.TO2
504 T7(I)=.
505 BA(I)=.
506 FORJ=.TO9
507 BL(I,J)=.
508 TL(I,J)=.
509 NEXT
510 NEXT
511 FORI=2TO9
512 FORJ=1TO8
513 V=I*10+J
514 A=ABS(B(V))
515 IFA=.THEN539
516 FA=SGN(B(V))
517 M=M+U(A)
518 ONAGOTO519,524,529,533,539,536
519 BA(FA+1)=BA(FA+1)+1
521 BL(FA+1,J)=BL(FA+1,J)+1
522 W=INT(W+FA*BV(J)*(3.5-FA*(5.5-I)))
523 GOTO539
524 IF(I-5.5)*FA<>2.5THEN527
526 T7(FA+1)=T7(FA+1)+1
527 TL(FA+1,J)=TL(FA+1,J)+1
528 GOTO539
529 IF(I-5.5)*FA<>-3.5THEN539
531 W=INT(W-FA*10)
532 GOTO539
533 W=INT(W+FA*ZT(I,J))
535 GOTO539
536 KR(FA+1)=I
538 KL(FA+1)=J
539 NEXT
540 NEXT
541 FA=SGN(M(T))
543 IFFA=.THEN545
544 W=INT(W+M(T)+INT(M(T)*BA(FA+1)/(BA(FA+1)+1)*(M0-M)*.0001))
545 W=INT(W+INT(ZT(KR(2),KL(2))*(43000-M+M(T))*.001))
547 W=INT(W-INT(ZT(KR(0),KL(0))*(43000-M-M(T))*.001))
548 W=INT(W+T7(2)*T7(2)*12)
549 W=INT(W-T7(.)*T7(.)*12)
550 FORI=1TO8
551 FORJ=.TO2
552 FA=J-1
553 IFFA=.THEN575
554 IFBL(J,I)=.THEN571
555 W=INT(W-FA*(BL(J,I)-1)*8)
556 II=.
557 IFBL(J,I-1)>.THEN561
558 IFBL(J,I+1)>.THEN561
559 W=INT(W-FA*20)
560 II=1
561 IFBL(2-J,I)>.THEN575
562 W=INT(W-FA*TL(2-J,I)*TL(2-J,I)*3)
563 IFBL(2-J,I-1)>.THEN567
564 IFBL(2-J,I+1)>.THEN567
565 W=INT(W+FA*18)
566 GOTO576
567 IFII=.THEN576
569 W=INT(W-FA*10)
570 GOTO576
571 IFBL(2-J,I)>.THEN575
572 W=INT(W+TL(2,I)*TL(2,I)*8)
573 W=INT(W-TL(.,I)*TL(.,I)*8)
574 GOTO576
575 NEXT
576 NEXT
577 RETURN
