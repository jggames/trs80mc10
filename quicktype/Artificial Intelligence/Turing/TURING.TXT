0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 CLS:PRINTTAB(9)"turing tumult":PRINT
20 PRINT "THIS PROGRAM DEMONSTRATES A"
30 PRINT "SIMPLE TURING MACHINE FOR"
40 PRINT "MULTIPLICATION, YOU INPUT TWO"
50 PRINT "NUMBERS AND IT DISPLAYS THEM"
60 PRINT "AS A STRING OF ONES. THEN IT"
70 PRINT "SHOWS YOU HOW IT FINDS THE"
80 PRINT "PRODUCT"
85 PRINT "DO YOU WANT MORE COMPLETE"
86 INPUT"INSTRUCTIONS (y/n)";M$:IF LEFT$(M$,1)="Y"THEN 400
90 PRINT "PLEASE TYPE TWO NON-ZERO NUMBERS";
100 INPUT "WITH PRODUCT <= 100 #,#";N1,N2:N1=INT(N1):N2=INT(N2)
110 IF N1*N2<1 OR N1*N2>100 THEN 90
120 CLS:PRINT@96,"X";
130 FOR M1=1 TO N1
140 PRINT "1";:NEXT
150 PRINT "X X";
160 FOR M2=1 TO N2
170 PRINT "1";:NEXT
180 PRINT "X"
190 P=16384-1024+1123+N1+N2:D=1
200 Q=P+32:O=PEEK(Q)
210 POKE Q,O:Q=P+32:O=PEEK(Q):POKE Q,94
220 FOR G=1 TO 115:NEXT
230 C=PEEK(P)
240 ON D GOTO 250,260,270,280,290,300,310,320,330,340,350,360,370,380
250 P=P-1:IF C=88 THEN D=2
255 GOTO 210
260 IF C=96 THEN P=P-1:GOTO 265
261 POKE P,96:D=3
265 GOTO 210
270 IF C=96 THEN P=P-1:GOTO 275
271 POKE P,96:IF C=99 THEN D=12:GOTO 275
272 D=4
275 GOTO 210
280 P=P+1:IF C=88 THEN D=5
285 GOTO 210
290 POKE P,96:D=6:GOTO 210
300 P=P+1:IF C=88 THEN D=7
305 GOTO 210
310 IF C=96 THEN POKE P,113:D=8:GOTO 315
311 P=P+1
315 GOTO 210
320 IF C=96 THEN P=P+1:D=9:GOTO 325
321 P=P-1
325 GOTO 210
330 IF C=96 THEN P=P+1:GOTO 335
331 IF C=88 THEN P=P-1:D=10:GOTO 335
332 GOTO 290
335 GOTO 210
340 IF C=96 THEN POKE P,113:GOTO 345
341 P=P-1:IF C=88 THEN D=11
345 GOTO 210
350 IF C=96 THEN P=P-1:GOTO 355
351 POKE P,96:IF C=88 THEN D=12:GOTO 355
352 D=4
355 GOTO 210
360 IF C=96 THEN P=P+1:GOTO 365
361 POKE P,96:D=13
365 GOTO 210
370 IF C=113 THEN POKE P,96:GOTO 375
371 P=P+1:IF C=88 THEN D=14
375 GOTO 210
380 IF C=96 THEN POKE P,88:PRINT@0,"DONE":END
381 P=P+1:GOTO 210
400 CLS:READ M$:IF M$="999" THEN 90
410 GOSUB1:PRINT:PRINTTAB(10)"hit any key"
420 IFINKEY$=""THEN420
430 GOTO400
500 DATA"COMPUTERS CAN'T THINK-- AT LEAST NOT YET."
501 DATA"BUT THEY CAN DO SOME THINGS, LIKE ARITHMETIC CALCULATIONS SO QUICKLY THT THEY SEEM TO HAVE INCREDIBLE INTELLIGENCE."
502 DATA"CALCULATING DOESN'T REQUIRE INTELLIGENCE, THOUGH-- OR EVEN A COMPUTER."
503 DATA"BRITISH MATHEMATICIAN ALAN TURING DESIGNED A VERY SIMPLE MACHINE THAT COULD IMITATE THE MOST COMPLEX COMPUTER."
504 DATA"THIS PROGRAM WAS WRITTEN FOR THE TRS-80 COMPUTER BY 19-YEAR OLD DAVID LEWIS."
505 DATA"IT CREATES A TURING MACHINE THAT DOES MULTIPLICATION ON YOUR TV SCREEN."
506 DATA"THE MACHINE CAN ONLY DO FIVE THINGS-- AND ALL THE ACTION TAKES PLACE RIGHT IN FRONT OF YOU."
507 DATA"IT CAN MOVE TO THE LEFT, MOVE TO THE RIGHT, ERASE THE SQUARE IT IS ON, OR WRITE A '1' OR AN 'X'."
508 DATA"EVEN SO, IT CAN FIND THE PRODUCT OF TWO NUMBERS."
509 DATA"HOW DOES IT DO IT?"
510 DATA"FIRST IT TRANSLATES THE TWO NUMBERS YOU GIVE IT INTO STRINGS OF 1'S"
511 DATA"FOR EXAMPLE, 5 WOULD BE REPRESENTED AS 11111."
512 DATA"THEN IT BUILDS A STRING OF 1'S FOR THE PRODUCT BY A PROCESS THAT IS PRETTY MUCH LIKE COUNTING ON YOUR FINGERS."
513 DATA"TO SEE CLEARLY HOW SIMPLE THE MACHINE IS, TRY GIVING IT THE NUMBERS 2 AND 5; THEN TRY 5 AND 2."
514 DATA"THE DIFFERENCE IN THE WAY IT HANDLES THE TWO PROBLEMS SHOULD HELP YOU UNDERSTAND HOW IT WORKS."
515 DATA"999"
600 REM DAVID LEWIS
610 REM ENTER MAGAZINE
620 REM MAY 1984
630 REM MC-10 EDITS JIM GERRIE
