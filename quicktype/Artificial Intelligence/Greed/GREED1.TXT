10 CLS
20 N=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
30 PRINT "DO YOU NEED INSTRUCTIONS";
40 INPUT N$
50 IF LEFT$(N$,1)="N" THEN 100
60 PRINT"greed IS A GAME FOR 2 PLAYERS"
61 PRINT"ON EACH OF THEIR 10 TURNS, THE"
62 PRINT"THE PLAYERS THROW THE DICE AND"
63 PRINT"ADD THE RESULT TO THEIR SCORES."
64 PRINT"PLAYERS MAY THROW THE DICE AS"
65 PRINT"MANY TIMES AS THEY LIKE BUT IF"
66 PRINT"THE FIRST RESULT OF THE TURN IS"
67 PRINT"REPEATED, THEIR SCORE FOR THAT"
68 PRINT"TURN IS ZERO."
70 PRINT"THIS PROGRAM PLAYS AS THE 2ND
71 PRINT"PLAYER AT VARYING SKILL LEVELS."
72 PRINT"IT PLAYS THE BEST AT LEVEL 100"
73 PRINT"(AVERAGE SCORE ABOVE 150)."
80 PRINT"GOOD LUCK!"
100 PRINT "WHAT SKILL LEVEL WOULD YOU"
105 INPUT"LIKE";N:PRINT
122 IF N<100 THEN 124
124 N=100*N
125 I=RND(0)
130 IF I<.5 THEN 140
135 N=-N
140 S(2)=0:S(1)=S(2)
141 I=1
145 PRINT "YOUR TURN #"I
150 GOSUB 400
160 R1=R9:T=R1
162 PRINT "FIRST THROW IS"STR$(R9)". ";
164 PRINT "AGAIN";
166 INPUT N$
168 IF LEFT$(N$,1)="N" THEN 200
180 GOSUB 400
185 PRINT "ROLL="R9;
190 IF R1=R9 THEN 195
191 T=T+R9
192 PRINT "TOTAL="STR$(T)". ";
193 GOTO 164
195 PRINT:T=0
200 S(1)=S(1)+T
203 PRINT "THIS TURN:"T:PRINT"YOUR TOTAL IS"S(1)
204 INPUT"HIT <ENTER> TO CONTINUE";M$:FORC1=1TO32:PRINTCHR$(8);:NEXT:PRINT

206 PRINT "MY TURN #"I
210 GOSUB 400
215 PRINT "FIRST THROW IS"STR$(R9)"."
220 T=R9:R1=T
230 N1=R1-1
231 IF N1<6 THEN 240
235 N1=12-N1
240 G=252/N1-R1
241 G=(N*G)/100+G
242 IF (S(2)-S(1))<((10-I)*50) THEN 244
243 G=G/2
244 IF (S(1)-S(2))<((10-I)*50) THEN 250
245 IF G<50 THEN 248
246 IF N<60 THEN 249
247 GOTO 250
248 IF N<60 THEN 250
249 G=50
250 IF T>=INT(G) THEN 285
255 IF I<>10 THEN 257
256 IF S(2)+T>S(1) THEN 290
257 IF T<100 THEN 261
258 IF (T+S(2)-S(1))<100 THEN 261
259 IF 2=R1 THEN 261
260 IF R1<12 THEN 290
261 IF T<250 THEN 265
262 IF (T+S(2)-S(1))>100 THEN 290
265 GOSUB 400
267 PRINT "ROLL="R9;
270 IF R9=R1 THEN 280
271 T=T+R9
273 PRINT "TOTAL="STR$(T)". "
275 GOTO 250
280 PRINT:T=0
281 GOTO 290
285 IF I<>10 THEN 290
286 IF S(2)+T<S(1) THEN 255
290 S(2)=S(2)+T
295 PRINT "THIS TURN:"STR$(T):PRINT"MY TOTAL IS"STR$(S(2))".":PRINT
300 I=I+1
301 IF I=11 THEN 310
302 GOTO 145
310 IF S(1)<=S(2) THEN 320
315 PRINT "YOU WIN"
316 GOTO 330
320 IF S(1)<S(2) THEN 324
321 PRINT "WE TIED."
322 GOTO 330
324 PRINT "SORRY, YOU LOST."
330 PRINT "PLAY AGAIN";
332 INPUT N$
334 IF LEFT$(N$,1)="N" THEN 999
336 GOTO 100
400 D3=INT(6*RND(0))+1
410 D2=INT(6*RND(0))+1
420 R9=D3+D2
421 RETURN
999 END
1000 REM MC-10 EDITS JIM GERRIE
