1 'VERSION 9/13/78 1433
2 CLS:PRINT@282,"BACKGAMMON"
3 PRINT@409,"COPYRIGHT BY"
4 PRINT@470,"CREATIVE COMPUTING"
5 PRINT@536,"P.O. BOX 789-M"
6 PRINT@593,"MORRISTOWN, NEW JERSEY 07960"
7 FORI=1TO2000:NEXTI
10 ' BACKGAMMON BY SCOTT ADAMS 8/25/78
11 CLEAR 100
12 DEFINT A-Z
20 DB=0'DEBUG 1=YES, 0=NO
25 DIM B(35) :L=0 :M=0 :V=0 :I=0 :J=0 :J1=0 :K=0 :J2=0 :J3=0
30 DIM HD(27),DI(4),MV(2,4),BS(2,4),Q7(2,16),DH(4)
32 CLS :PRINT :PRINTTAB(4)"WELCOME TO  ** ADAMS-COMPUTER-BACKGAMMON **" :PRINT
33 PRINT"SPECIAL NOTES FOR *-ADAMS BACKGAMMON-*":PRINT"  1) IF YOU HAVE A BAR MOVE, ENTER IT FIRST.":PRINT"  2) IF YOU SHOULD BE TRAPPED WITH NO LEGAL MOVE, HIT ENTER." :PRINT"  3) YOU'LL NOT BE WARNED IF YOU FORGET TO BEAR OFF.":PRINT
35 VH=11 :VT=5 :VP=-7 :VM=5 :VL=-2 :VF=10 :VA=-1 :VO=-2
36 PRINT"I TAKE 40 SECONDS OR LESS FOR A MOVE." :PRINT"BUT ONCE IN AWHILE I'LL TAKE UP TO 5 MINUTES. YOU":PRINT"MAY TAKE AS LONG AS YOU LIKE!" :PRINT :PRINT :INPUT"HAVE YOU PLAYED WITH ME BEFORE";A$ :IF LEFT$(A$,1)="N" GOSUB20000
40 DATA 2,66,130,194,3,67,131,195,1,65,129,193,0,64,128,192:DATA194,130,66,2,195,131,67,3,193,129,65,1,192,128,64,0
50 FORL=1TO2:FORM=1TO16:READQ7(L,M):NEXTM,L
60 FOR L=0TO27 :B(L)=0 :NEXT :B(24)=-2 :B(1)=2 :B(19)=5 :B(6)=-5   :B(12)=5 : B(13)=-5  :B(17)=3 :B(8)=-3 :LV=1 'START VERSION 1  LOGIC FOR SPEED.
80 GOSUB10000
90 GOSUB11111:IF DI(1)=DI(2) THEN 90 ELSE PRINT@896,"MY ROLL IS"  ;DI(1);" YOUR ROLL IS";DI(2);" ";
100 IF DI(1)<DI(2) THEN PRINT" YOU"; ELSE PRINT" I";
110 PRINT" GO FIRST."; :FOR L=1TO2000 :NEXT:IF DI(1)>DI(2) THEN 135 ELSE 120
115 GOSUB11111
120 GOSUB1000 :PRINT@896,TAB(63);:PRINT :GOSUB2000
130 GOSUB11111 :PRINT@960,TAB(63);
135 PRINT@896,TAB(63);:PRINT@896,"... ONE MINUTE PLEASE WHILE I COGITATE ..."
140 PRINT@960,"MY ROLL IS";DI(1);DI(2);" ";
150 GOSUB5400 :GOSUB2000
160 GOTO115
1000 'HUMAN MOVE
1010 IF NOT POINT(4,19) GOSUB 10000 'BOARD GONE?
1020 PRINT@832,TAB(63);:PRINT@832," YOUR ROLL IS";DI(1);DI(2);". MOVE ('X')";
1025 IF B(26)<>0 AND B(25-DI(1))>1 AND B(25-DI(2))>1 THEN PRINT" YOU CAN'T MOVE THOUGH!!!!!";:FOR L=1TO2000 :NEXT :RETURN
1030 A$="" :INPUT A$ :IF LEFT$(A$,1)<>"B" THEN 1040 ELSE GOSUB10000			  :GOTO1020
1040 GOSUB3000 :IF Q<>0 THEN 1095
1060 FOR L=1TO MS :FOR M=1TO2 'REPLC 0 WITH HOME OR OFF
1070 IF MV(M,L)=0 THEN MV(M,L)=25+M
1080 NEXTM,L
1090 GOSUB4000
1095 IF Q=0 THEN 1110
1097 PRINT@896,"* ERROR - ";
1100 IF Q=2 PRINT"MOVE MAN ON BAR";
1101 IF Q=1 PRINT"TRY AGAIN";
1102 IF Q=3 PRINT"TOO FEW MOVES";
1103 IF Q=4PRINT"CHECK YOUR DICE!";
1104 IF Q=5 PRINT"TOO MANY MOVES";
1109 PRINT" *  YOUR MOVE WAS: ";A$; :PRINT@832,TAB(63);:GOTO1020
1110 IF MS<1 THEN RETURN ELSE FOR I=1TO MS' DO MOVE
1120 IF B(MV(2,I))=1 THEN QF=MV(2,I) :QT=0 :GOSUB9000
1130 QF=MV(1,I) :QT=MV(2,I) :GOSUB9000
1140 NEXTI :RETURN
1245 .455
2000 'SEE IF ANY WINNERS
2010 IF B(25)=15 THEN LO=LO+1 :A$="I" :GOTO2030
2020 IF B(27)<>-15 THEN RETURN ELSE WI=WI+1 :A$="YOU"
2030 PRINT@896,TAB(63); :PRINT@896,A$;" WIN!! YOU HAVE WON";WI;"AND LOST";LO;"GAMES. ";
2040 INPUT"ANOTHER GAME";C$ :IF LEFT$(C$,1)="Y" THEN 60 ELSE IF LEFT$(C$,1)="N" THEN CLS :GOTO 30000:ELSE 2030
3000 'CONVERT MOVES IN A$ TO MV ARRAY
3010 'LEGAL FORMAT 1-2-3/3-4 ETC.
3020 IF A$="" THEN MS=0 :Q=0 :RETURN
3030 L=0 : M=0 'L=CHAR CNTR, M=MV INDX
3035 IF M>3 THEN Q=1 :RETURN
3040 L=L+1 :M=M+1 :GOSUB 3300 :MV(1,M)=Q :L=L+1
3050 GOSUB 3300 :MV(2,M)=Q
3055 IF L>LEN(A$) THEN 3100
3060 IF MID$(A$,L,1)<>"-" THEN 3090
3070 L=L+1 :GOSUB 3300 :IF M+Q-1>DN THEN Q=5 :RETURN 'ERROR TOO MANY MOVES
3080 N=M :FOR M=N+1 TO N+Q-1 :MV(1,M)=MV(1,N) :MV(2,M)=MV(2,N):NEXT :M=N+Q-1
3090 IF L<LEN(A$) THEN IF MID$(A$,L,1)="."THEN 3035 ELSE Q=1 :RETURN 'ERROR REDO MOVE
3100 Q=0 :MS=M :RETURN
3300 'REMOVE NEXT # FROM A$
3310 B$=MID$(A$,L,1) :C$=MID$(A$,L+1,1)
3320 IF C$>="0" AND C$<="9" THEN L=L+1 :B$=B$+C$
3330 Q=VAL(B$) :L=L+1 :RETURN 'L IS NEXT CHAR POINTER
4000 'CHECK HUMAN MOVE, Q=0 NO ERROR
4010 FOR L=0TO27 :HD(L)=B(L) :NEXT :Q=1 'SAVE BOARD
4015 IF MS=0 THEN 4110 'NO MOVE?
4020 FOR L=1TO MS :FOR M=1TO2 : N=MV(M,L)
4030 IF N<1 OR N>27 OR N=25 THEN 4200
4040 NEXT M,L
4050 FOR L=1TO MS :QT=MV(2,L) :QF=MV(1,L)
4060 IF B(QT)>1 OR B(QF)>-1 THEN 4200
4062 IF B(26)<>0 AND (QF<>26 OR QT<19) THEN Q=2 :GOTO 4200
4070 B(QF)=B(QF)+1 :B(QT)=B(QT)-1 :IF B(QT)=0 THEN B(QT)=-1
4090 IF QT=27 THEN N=B(26) :FOR M=7TO24 :N=N+(B(M)<0) :NEXT:IF N<>0 THEN 4200
4100 NEXT L
4110 GOSUB 6500 'ALL MOVES USED UP?
4120 FOR L=0TO27 :B(L)=HD(L) :NEXT :RETURN
4200 GOTO4120
5000 'INPUT J,J1,D1,D2 DO DUMMY MOVE. EVALUATE BOARD. IF NO MOVE FOOR A DIE THEN RANK MOVE -100
5010 FOR L=0TO27 :HD(L)=B(L) :NEXT L:ZZ=Z
5020 V=0 :FOR L=1TO DN :ON L GOTO 5025,5030,5035,5040
5025 M=J :M1=J+D :GOTO5041
5030 M=J1 :M1=J1+D1 :GOTO5041
5035 M=J2 :M1=J2+D :GOTO5041
5040 M=J3 :M1=J3+D
5041 '
5044 IF Z=1 AND M1>25 AND M1<999 THEN FOR M9=18 TO M-1 :IF B(M9)<1 THEN NEXT M9:M1=25 'IF BEARING OFF USE HIGHEST AVAILABLE MAN
5045 IF M1>24+Z IF M1<999 THEN V=-10000 :GOTO5070 'DON'T TRY TO BEAR OFF IF STUCK AND NOT IN END GAME!
5046 IF M>24 OR M1>24+Z THEN V=V-100 :GOTO5060
5048 B(M)=B(M)-1 
5050 IF B(M)>-1 THEN B(M1)=B(M1)+1 :IF B(M1)=0 THEN B(M1)=1:V=V+VT :B(26)=B(26)-1 :IF M1<11 THEN V=V+VT'WE HIT EM
5055 IF B(M)<0 IF B(25)>14 THEN V=10000 :GOTO5070 :ELSE V=-10000 :GOTO5070'ERROR NO MEN LEFT TO MOVE
5057 IF Z=1 AND M1=25 THEN V=V+VF 'END GAME BORNE OFF
5058 IF Z=0 FOR LL=0 TO 18 :IF B(LL)<1 NEXT LL :Z=1 'END GAME?
5060 NEXT L :GOSUB8000
5070 FOR L=0TO27 : B(L)=HD(L) :NEXTL :Z=ZZ :RETURN
5200 ' MOVE FROM BAR
5201 IF DB=1 PRINT@832,"BAR MOVE";
5210 IF B(0)=0 OR DN<1 THEN RETURN
5220 FOR I=1TODN : J=DI(I) :IF B(J)<>-1 THEN 5240 ELSE QF=J :QT=26 :GOSUB9000:QF=0 :QT=J :GOSUB9000 'HIT EM
5230 DI(I)=DI(DN) :DN=DN-1 :GOTO5210 'COMPACT DICE
5240 NEXT I :FOR I=1TODN : J=DI(I) :IF B(J)=1 THEN QF=0 :QT=J :GOSUB9000 :GOTO5230 'HELP SINGLE POINT
5250 NEXT I :FOR I=1TODN :J=DI(I) :IF B(J)>-1 THEN QF=0 :QT=J :GOSUB9000 :GOTO5230 'MOVE ANYWHERE
5260 NEXT I :PRINT@CR," I CANN'T MOVE!"; :RETURN
5400 ' DO COMPY MOVE
5410 PRINT". MOVE: "; :CR=960+POS(0) :Z=0 'END GAME FLAG
5411 IF DB=1 PRINT@832,TAB(63);
5420 L=0 :FOR I=0TO18 :L=L+(B(I)>0) :NEXT I'END MOVE?
5430 IF L=0 THEN Z=1 :LV=2 'END DO LVL 2 LOGIC
5440 GOSUB5200 'BAR MOVE
5450 IF DN=0 OR B(0)<>0 THEN 5470 'NO MORE MOVE!
5452 IF Z=0 THEN 5460
5455 IF B(26)=0 FOR L=1 TO DN:FOR M=25-DI(L) TO 24 :IF B(M)>-1 NEXT M:IF B(25-DI(L))<1 THEN NEXT L ELSE QF=25-DI(L) :QT=25 :GOSUB9000 :DI(L)=0 :NEXT L
5456 M=0 :FOR L=1TO DN :IF DI(L)<>0 THEN M=M+1 :DH(M)=DI(L)
5457 NEXT L :IF DN<>M :FOR L=1 TO DN :DI(L)=DH(L) :NEXT L :DN=M
5458 IF DN<1 THEN 5450
5459 'THE PRECEDING MOVES REMOVED ANY MEN DURING AN END GAME AS LONG AS THERE WERE NO HUMAN MEN IN FRONT. IT THEN COMPACTED THE DICE ARRAY.
5460 IF DN<3 THEN GOSUB7000 ELSE GOSUB6000
5470 PRINT@896,TAB(63); :RETURN
5700 'FLAG WE STILL HERE
5705 IF DB=1 RETURN
5710 IF FG=0 THEN FG=1
5720 FG=-FG :PT=896+30
5730 IF FG<0 THEN PRINT@PT,"COGITATE"; ELSE PRINT@PT,"-THINK?-";
5740 RETURN
6000 'COMPY HAD DOUBLES
6001 IF DB=1 PRINT@832,"DOUBLES MOVE";
6010 MX=-9999 : D=DI(1) :D1=D :E=24 :NM=0:J=999 :J1=999 :J2=999 :J3=999 'END AT POINT 24 IF END GAME ELSE 25-DICE
6012 BG=2*VL :IF LV=2 THEN 6019 'SEE IF ANY 1 HUMAN CAN BE HIT BY A SAFE DOUBLE. IF SO STOP WHEN MX>=VT ELSE IF MX>=2*VL. IF END GAME LV=2
6014 FOR L=1TO24 :IF B(L)<>-1 THEN NEXT L ELSE: K=L:FOR M=1TO DN/2 :K=K-D :IF K<1 THEN NEXT L ELSE IF B(K)<-1 THEN NEXT L ELSE IF B(K)>1 THEN BG=1 ELSE NEXT M,L
6019 IF Z=1 THEN BG=VF*DN/2+VM
6020 FOR J=1TOE :K=B(J) '1ST MOVE
6030 IF K>0 THEN K=B(J+D) :IF K>-2 THEN 6050
6035 IF Z=1 IF J+D>25 THEN 6050
6040 NEXT J :J=999 :IF MX>-50 THEN 6150 ELSE 6140
6050 FOR J1=J TO E :K=B(J1) :IF J1=J+D THEN K=1 '2ND MOVE
6055 IF J1=J IF K<2 THEN K=0 'TOO MANY SAME SPOT
6060 IF K>0 THEN K=B(J1+D) :IF K>-2 THEN 6080 'OK TRY IT
6065 IF Z=1 IF J1+D1>25 THEN 6080
6070 NEXT J1: J1=999 :IF MX>-50 THEN 6040 ELSE 6140 'GOT 1 MOVE AT LEAST WHICH USES ALL DICE. SO DON'T BOTHER WITH MOVES     THAT DONT'T EVEN MOVE
6080 GOSUB5700 :FOR J2=J1 TO E:K=B(J2):IF J2=J1+D OR J2=J+D THEN K=1 '3RD MOVE
6085 IF J2=J1 IF J1=J IF K<3 THEN K=0 'TOO MANY FROM SAME SPOT
6090 IF K>0 THEN K=B(J2+D) :IF K>-2 THEN 6110
6095 IF Z=1 IF J2+D>25 THEN 6110
6100 NEXT J2 :J2=999 :IF MX>-50 THEN 6070 ELSE 6140 
6110 GOSUB5700:IF DN<4 THEN 6140 ELSE FOR J3=J2 TO E :K=B(J3):IF J3=J1+D OR J3=J2+D OR J3=J+D THEN K=1'FINAL MOVE
6120 IF K>0 THEN K=B(J3+D) :IF K>-2 THEN 6140
6125 IF Z=1 IF J3+D>25 THEN 6140
6130 NEXT J3 :J3=999 :IF MX>-50 THEN 6100
6140 GOSUB 5000 :IF V>MX THEN MX=V: NM=1:BS(1,1)=J :BS(2,1)=J+D :BS(1,2)=J1 :BS(2,2)=J1+D:BS(1,3)=J2 : BS(2,3)=J2+D : BS(1,4)=J3 :BS(2,4)=J3+D:IF DB=1THEN PRINT"*";V;J;J1;J2;J3;
6141 IF MX>=BG THEN 6160
6145 IF J3<>999 THEN6130 ELSE IF J2<>999 THEN 6100 ELSE IF J1<>999 THEN 6070 ELSE IF J<>999 THEN 6040
6150 'DO BEST MOVE
6160 GOTO7120
6500 'HAS HUMAN USED ALL MOVES?
6510 FOR L=1TO4 :DH(L)=DI(L) :NEXT L'COPY DICE
6515 IF MS=0 THEN 6570 'NO MOVE?
6520 FOR L=1TO MS 'SEE WHICH ROLLS USED UP
6530 QF=MV(1,L) :IF QF=26 LET QF=25 'BAR
6540 QT=MV(2,L) :IF QT=27 LET QT=0 'HOME
6550 FOR M=1 TO DN :IF QF-QT=DH(M) THEN DH(M)=0 :ELSE NEXT M 'FLAG IT USED
6560 NEXT L
6570 FOR L=1 TO DN 'SEE IF ANY DICE UNUSED
6580 IF DH(L)=0 THEN 6700 'NO
6590 'CAN HE MOVE THE MISSING ROLL?
6600 D=DH(L) :IF B(26)<>0 IF B(25-D)>0 THEN 6700 ELSE Q=2 :GOTO6710 'MAN LEFT ON BAR!
6605 Q=3 :IF MS=DN THEN Q=4 'NO TOO FEW BU BAD COUNT
6610 FOR M=1 TO 24 'SEE IF ANY REGULAR MOVES AVAILABLE
6620 IF B(M)>-1 THEN 6650 'NO MAN HERE
6630 N=M-D :IF N<1 THEN 6650
6640 IF B(N)<1 THEN 6710 'HE HAS A MOVE!
6650 NEXT M
6700 NEXT L :Q=0 'NO ERROR, ALL ROLLS USED
6710 RETURN
7000 'DO ALL POSSIBLE REGULAR MOVES, FIND BEST
7001 IF DB=1 PRINT@832,"REGULAR";
7010 MX=-9999 : NM=0 
7020 FOR I=1TO DN :D=DI(I) :D1=DI(3-I) :J1=999
7030 FOR J=1TO 25-D+Z*(D-1) :K=B(J) 'FIRST MOVE
7040 IF K>0 THEN K=B(J+D) :IF K>-2 THEN 7060
7045 IF Z=1 IF J+D>25 THEN 7060
7050 NEXT J :J=999 :IF MX>-50 THEN 7110 ELSE 7100
7060 IF DN=1 THEN 7100 '1 MOVE ONLY
7065 K=J :IF K<24 IF I=2 OR B(K)<2 LET K=K+1
7070 GOSUB5700 :FOR J1=K TO 24 :K=B(J1):IF J1=J+D THEN K=1'SECOND MOVE
7080 IF K>0 THEN K=B(J1+D1) :IF K>-2 THEN 7100
7085 IF Z=1 IF J1+D1>25 THEN 7100
7090 NEXT J1 :J1=999 :IF MX>-50 THEN 7050 'UNMOVABLE BUT GOT 1.
7100 GOSUB 5000 :IF V>MX THEN MX=V: NM=1:BS(1,1)=J :BS(2,1)=J+D:BS(1,2)=J1:BS(2,2)=J1+D1:IF DB=1 PRINT"*";MX;J;J+D;J1;J1+D1;
7105 IF J1<>999 THEN 7090 ELSE IF J<>999 THEN 7050
7110 NEXT I
7120 IF NM=0 PRINT@CR,"I CANNOT MOVE"; :RETURN 'DO BEST MOVE
7130 NM=0 :FOR L=1 TO DN :FOR M=1 TO2 
7133 IF BS(M,L)>25 AND BS(M,L)<>999 THEN BS(M,L)=25
7135 IF BS(M,L)<0 OR BS(M,L)>=999 THEN 7160 ELSE NEXT M
7140 IF B(BS(2,L))=-1 THEN QF=BS(2,L) :QT=26 :GOSUB9000
7150 QF=BS(1,L) :QT=BS(2,L) :GOSUB9000 :NM=1
7160 NEXT L :IF NM=0 THEN 7120 ELSE RETURN
8000 'EVALUATE MOVE V=MEN HIT ALREADY AND MEN BORNE OFF
8010 IF Z=1 THEN 8040 'END GAME
8020 FOR L=0TO18 :IF B(L)>0 THEN 8030 ELSE NEXT L:V=V+VM 'ALL MEN HOME?
8030 FOR L=1TO6 :IF B(L)>0 THEN V=V+B(L)*VL 'MEN IN HUMAN HOME
8035 NEXT L
8040 M=26 :IF B(26)<>0 THEN 8070 'NOT PAST HUMANS YET
8050 M=0 :FOR L=24 TO 0 STEP-1 :IF M=0 IF B(L)<0 THEN M=L 'M=FIRST HUMAN
8060 IF M=0 OR B(L)<=0 THEN NEXT :V=V+VH :GOTO8100 'PAST HUMANS
8065 'LOOK FOR SINGLES ONLY IF WE AREN'T PAST ALL HUMANS
8070 FOR L=1TO24 'LOOK FOR SINGLES, WORST IS SINGLE WHERE HUMAN CAN HIT THEM, NEXT IS SINGLE IN HUMAN HOME, NEXT IS SINGLE PAST ALL HUMANS
8075 IF B(L)<>1 THEN 8090
8080 IF L<7 THEN V=V+VP-VL :GOTO8090 'SINGLE IN HUMAN HOME
8085 IF L>M THEN V=V+VA ELSE V=V+VP
8087 IF L>18 THEN V=V-VO*B(26) 'MEN OPEN IN HOME CAN BE HIT
8090 NEXT L
8095 IF B(26)=0 THEN 8100 ELSE M=0 :FOR L=19 TO 24:IF B(L)=1 THEN M=0 ELSE IF B(L)>1 THEN M=M+1 :NEXT L :ELSE NEXT L
8096 IF M<>6 THEN V=V+(M*3-1)/2 ELSE V=V+35 'SHUT HIM OUT!
8100 RETURN
9000 'MOVE QT TO QF
9010 S=SGN(B(QF)) :IF S=0 RETURN
9020 Q4$="O" :IF S=-1 THEN Q4$="X"
9030 Q=ABS(B(QF)) :B(QF)=B(QF)-S :Q2=QF
9040 GOSUB9100 :PRINT@Q1," "; 'REMOVE FROM
9050 B(QT)=B(QT)+S :Q=ABS(B(QT)) :Q2=QT
9060 GOSUB9100 :PRINT@Q1,Q4$;
9065 B$=MID$("##",1,2+(QF<10)):C$=MID$("##",1,2+((QT<10)OR(QT=25)))
9070 IF S=1 PRINT@CR,""; :PRINTUSING B$;QF; :PRINT"-";:PRINTUSING C$;-QT*(QT<25); :PRINT".";:CR=960+POS(0)
9080 RETURN
9100 'COMPUTE SCREEN POSITION Q=CHIP#, Q2=POINT#
9110 Q3=1 :IF Q2>0 AND Q2<25 THEN Q3=CINT(Q2/13)+1 'TOP/BOTTOM
9120 IF Q2>25 THEN Q3=2
9125 Q3=3-Q3
9130 IF Q2=0 OR Q2=26 THEN Q4=155+320*(3-Q3-1) ELSE IF Q2=25 OR Q2=27 THEN Q4=187+320*(3-Q3-1) ELSE IF Q2>18 THEN Q4=128+33+(Q2-19)*4 ELSE IF Q2>12 THEN Q4=128+(Q2-13)*4 ELSE IF Q2>6 THEN Q4=448+(12-Q2)*4 ELSE Q4=448+33+(6-Q2)*4
9140 Q1=Q7(Q3,Q)+Q4 :RETURN
10000 'DRAW BOARD
10010 CLS :PRINT@768,; :FOR L=12TO1 STEP-1 :PRINTUSING"  ##";L;:IF L=7 THEN PRINT"   BAR   "; ELSE IF L=1 THEN PRINT"   OFF";
10020 NEXT :PRINT@0,;:FOR L=13TO24 :PRINT USING"  ##";L;:IF L=18 THEN PRINT"   BAR   "; ELSE IF L=24 PRINT"   OFF";
10025 NEXT
10030 PRINT@384,; :FOR L=0TO56 STEP2 :PRINT CHR$(140);CHR$(032); :NEXT :PRINT@384," ";
10040 FOR L=65TO118 STEP4 :FOR M=L TO L+640 STEP 640
10042 IF L>86 AND L<97 THEN 10049
10043 N=M :IF L>90 LET N=N+1
10045 K=184 :K1=180 :IF M=L THEN K=139 :K1=135
10048 PRINT@N,CHR$(K);CHR$(191);CHR$(K1);
10049 NEXTM,L
10050 FOR L=49TO65 STEP16 :FOR M=0TO37 :SET(L,M) :NEXT M,L
10060 'PUT MEN ON BOARD
10070 FOR Q2=0TO27 :IF B(Q2)=0 THEN 10110
10080 FOR Q=1TO ABS(B(Q2)) :GOSUB9100
10090 IF B(Q2)<0 THEN PRINT@Q1,"X"; ELSE PRINT@Q1,"O";
10100 NEXT
10110 NEXT
10120 RETURN
11111 FOR RR=1 TO RND(100) :DI(1)=RND(6) :NEXT :RANDOM
11121 FOR RR=1 TO RND(100) :DI(2)=RND(6) :NEXT :RANDOM
11130 IF DI(1)=DI(2) THEN DN=4 :DI(3)=DI(1) :DI(4)=DI(1):RETURN
11140 DN=2 :RETURN
20000 'INSTRUCTIONS
20010 CLS :PRINT"RULES ARE THE SAME AS IN BACKGAMMON. TO ENTER A MOVE"
20020 PRINT"INPUT FROM-TO-TIMES. SEPERATE EACH MOVE BY A PERIOD(.)!"
20030 PRINT : PRINT"EXAMPLE TO MOVE FROM POINT 2 TO POINT 3 TWICE:"
20040 PRINT"    2-3-2"
20050 PRINT :PRINT"EXAMPLE: TO MOVE FROM POINT 1 TO POINT 2 AND FROM POINT 3 TO 4"
20060 PRINT"     1-2.3-4
20070 PRINT :PRINT"EXAMPLE: TO MOVE FROM POINT 1 TO POINT 2 AND FROM POINT 3 TO 4" :PRINT"  AND FROM POINT 8 TO HOME TWICE..."
20080 PRINT"      1-2.3-4.8-0-2"
20090 PRINT :PRINT"NOTE THAT BOTH HOME AND BAR ARE SIGNIFIED BY A 0." 
20100 PRINT"INPUT 'BOARD' TO REDRAW BOARD AS NEEDED."
20400 INPUT"HIT ENTER TO START";A$ :RETURN
30000 PRINT"THANKS FOR PLAYING!":PRINT:PRINT"TO PLAY AGAIN, TYPE 'RUN'."
30001 PRINT:PRINT"TO LOAD ANOTHER PROGRAM, TYPE 'CLOAD'.":PRINT:END
