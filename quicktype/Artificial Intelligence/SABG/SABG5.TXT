0 CLS:SOUND1,1:POKE17026,0:CLEAR400:DIMC1,C2,M$,I$:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
3 PRINT@480,"                               ";:RETURN
4 PRINT"€";:GOSUB6:PRINTCHR$(8);:ON-(C2=64)GOSUB6:ON-(M$<" ")GOTO5:PRINTM$;:I$=I$+M$:C2=.:ON-(LEN(I$)<31)GOTO4:PRINT:RETURN
5 C2=.:ON-(M$="")GOTO4:ON-(M$=CHR$(8))GOTO8:ON-(M$<>CHR$(13))GOTO4:PRINT:RETURN
6 FORC1=1TO35:M$=INKEY$:C2=ASC(M$+"@"):C1=C1-(C2<>64)*35:NEXT:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,:RETURN
8 IFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):PRINTCHR$(8);
9 GOTO4
10 GOSUB8900
20 DB=0:REM DEBUG 1=YES, 0=NO
25 DIMB(35):L=0:M=0:V=0:I=0:J=0:J1=0:K=0:J2=0:J3=0
30 DIMHD(27),DI(4),MV(2,4),BS(2,4),Q7(2,16),DH(4)
32 CLS:PRINT"** ADAMS-COMPUTER-BACKGAMMON **" :PRINT
33 PRINT"SPECIAL NOTES 1) IF YOU HAVE A"
34 PRINT"BAR MOVE, ENTER IT FIRST.":PRINT"2) IF YOU ARE TRAPPED WITH NO":PRINT"LEGAL MOVE, HIT ENTER."
35 PRINT"3) YOU'LL NOT BE WARNED IF":PRINT"YOU FORGET TO BEAR OFF.":PRINT:PRINT"YOU ARE x, THE COMPUTER IS o."
36 VH=11:VT=5:VP=-7:VM=5:VL=-2:VF=10:VA=-1:VO=-2
37 PRINT:PRINT"I TAKE 40 SECONDS OR LESS FOR"
38 PRINT"A MOVE, BUT SOMETIMES UP TO 5"
39 PRINT"MINUTES.":PRINT:INPUT"HAVE YOU PLAYED BEFORE";A$:CLS:IFLEFT$(A$,1)="N"THENGOSUB20000
40 DATA2,66,130,194,3,67,131,195,1,65,129,193,0,64,128,192
41 DATA194,130,66,2,195,131,67,3,193,129,65,1,192,128,64,0
50 FORL=1TO2:FORM=1TO16:READQ7(L,M):NEXTM,L
60 FORL=0TO27:B(L)=0:NEXT:B(24)=-2:B(1)=2:B(19)=5:B(6)=-5:B(12)=5:B(13)=-5:B(17)=3:B(8)=-3:LV=1
80 GOSUB10000
90 GOSUB11111:IFDI(1)=DI(2)THEN90
91 PRINT@448,"ME";STR$(DI(1))" YOU"STR$(DI(2))".";
100 IFDI(1)<DI(2)THENPRINT"  YOU";:GOTO110
101 PRINT"  I";
110 PRINT" GO FIRST.";:FORL=1TO3000:NEXT:IFDI(1)>DI(2)THEN135
111 GOTO120
115 GOSUB11111
120 GOSUB1000:PRINT@448,"":GOSUB2000
130 GOSUB11111:PRINT@448,""
135 PRINT@416,"PLEASE WAIT WHILE I COGITATE...":PRINT@448,"":GOSUB3
140 PRINT@448,"ME"STR$(DI(1))","MID$(STR$(DI(2)),2);
150 GOSUB5400:GOSUB2000
160 GOTO115
999 REM HUMAN MOVE
1000 PRINT@416,"":PRINT@416,"YOU"STR$(DI(1))","MID$(STR$(DI(2)),2);
1020 IFB(26)<>0ANDB(25-DI(1))>1ANDB(25-DI(2))>1THENPRINT@448,"YOU CAN'T MOVE THROUGH.":FORL=1TO4000:NEXT:RETURN
1030 PRINT": ";:I$="":M$="":GOSUB4:A$=I$:IFLEFT$(A$,1)<>"B"THEN1040
1031 GOSUB10000:GOTO1000
1040 GOSUB3000:IFQ<>0THEN1095
1060 FORL=1TOMS:FORM=1TO2:REM HOME OR OFF
1070 IFMV(M,L)=0THENMV(M,L)=25+M
1080 NEXTM,L
1090 GOSUB4000
1095 IFQ=0THEN1110
1097 PRINT@448,"error-- ";
1100 IFQ=2THENPRINT"MOVE FROM BAR"
1101 IFQ=1THENPRINT"TRY AGAIN"
1102 IFQ=3THENPRINT"TOO FEW MOVES"
1103 IFQ=4THENPRINT"CHECK YOUR DICE!"
1104 IFQ=5THENPRINT"TOO MANY MOVES"
1109 GOSUB3:PRINT@480,A$;:PRINT@416,"":GOTO1000
1110 IFMS<1THENRETURN
1111 FORI=1TOMS
1120 IFB(MV(2,I))=1THENQF=MV(2,I):QT=0:GOSUB9000
1130 QF=MV(1,I):QT=MV(2,I):GOSUB9000
1140 NEXTI:RETURN
1999 REM SEE IF ANY WINNERS
2000 IFB(25)=15THENLO=LO+1:A$="I":GOTO2030
2020 IFB(27)<>-15THENRETURN
2021 WI=WI+1:A$="YOU"
2030 PRINT@448,"":PRINT@448,A$+" WIN! YOU"STR$(WI)", ME"STR$(LO)" GAMES."
2040 I$="":M$="":PRINT"ANOTHER GAME? ";:GOSUB4:C$=I$:CLS:IFLEFT$(C$,1)="Y"THEN60
2050 IFLEFT$(C$,1)="N"THENCLS:GOTO30000
2051 GOTO2030
2998 REM CONVERT MOVES IN A$ TO MV ARRAY
3999 REM LEGAL FORMAT 1-2-3/3-4 ETC.
3000 IFA$=""THENMS=0:Q=0:RETURN
3030 L=0:M=0:REM L=CHAR CNTR, M=MV INDX
3035 IFM>3THENQ=1:RETURN
3040 L=L+1:M=M+1:GOSUB3300:MV(1,M)=Q:L=L+1
3050 GOSUB3300:MV(2,M)=Q
3055 IFL>LEN(A$)THEN3100
3060 IFMID$(A$,L,1)<>"-"THEN3090
3070 L=L+1:GOSUB3300:IFM+Q-1>DNTHENQ=5:RETURN:REM ERROR TOO MANY MOVES
3080 N=M:FORM=N+1TON+Q-1:MV(1,M)=MV(1,N):MV(2,M)=MV(2,N):NEXT:M=N+Q-1
3090 IFL<LEN(A$)THEN3095
3091 GOTO3100
3095 IFMID$(A$,L,1)="."THEN3035
3096 Q=1:RETURN:REM ERROR REDO MOVE
3100 Q=0:MS=M:RETURN
3300 REM REMOVE NEXT # FROM A$
3310 B$=MID$(A$,L,1):C$=MID$(A$,L+1,1)
3320 IFC$>="0"ANDC$<="9"THENL=L+1:B$=B$+C$
3330 Q=VAL(B$):L=L+1:RETURN:REM L IS NEXT CHAR POINTER
3999 REM CHECK HUMAN MOVE, Q=0 NO ERROR
4000 FORL=0TO27:HD(L)=B(L):NEXT:Q=1:REM SAVE BOARD
4015 IFMS=0THEN4110:REM NO MOVE?
4020 FORL=1TOMS:FORM=1TO2:N=MV(M,L)
4030 IFN<1ORN>27ORN=25THEN4200
4040 NEXTM,L
4050 FORL=1TOMS:QT=MV(2,L):QF=MV(1,L)
4060 IFB(QT)>1ORB(QF)>-1THEN4200
4062 IFB(26)<>0AND(QF<>26ORQT<19)THENQ=2:GOTO4200
4070 B(QF)=B(QF)+1:B(QT)=B(QT)-1:IFB(QT)=0THENB(QT)=-1
4090 IFQT=27THENN=B(26):FORM=7TO24:N=N+(B(M)<0):NEXT:IFN<>0THEN4200
4100 NEXTL
4110 GOSUB6500:REM ALL MOVES USED UP?
4120 FORL=0TO27:B(L)=HD(L):NEXT:RETURN
4200 GOTO4120
4999 REM INPUT J,J1,D1,D2 DO DUMMY MOVE. EVALUATE BOARD. IF NO MOVE FOOR A DIE THEN RANK MOVE -100
5000 FORL=0TO27:HD(L)=B(L):NEXTL:ZZ=Z
5020 V=0:FORL=1TODN:ONLGOTO5025,5030,5035,5040
5025 M=J:M1=J+D:GOTO5044
5030 M=J1:M1=J1+D1:GOTO5044
5035 M=J2:M1=J2+D:GOTO5044
5040 M=J3:M1=J3+D
5041 REM IF BEARING OFF USE HIGHEST AVAILABLE MAN
5044 IFZ=1ANDM1>25ANDM1<999THENFORM9=18TOM-1:IFB(M9)<1THENNEXTM9:M1=25
5045 IFM1>24+ZTHENIFM1<999THENV=-10000:GOTO5070:REM DON'T TRY TO BEAR OFF IF STUCK AND NOT IN END GAME!
5046 IFM>24ORM1>24+ZTHENV=V-100:GOTO5060
5048 B(M)=B(M)-1:IFB(M)>-1THENB(M1)=B(M1)+1:IFB(M1)=0THENB(M1)=1:V=V+VT:B(26)=B(26)-1:IFM1<11THENV=V+VT
5050 IFB(M)<0THEN5052
5051 GOTO5057
5052 IFB(25)>14THENV=10000:GOTO5070
5053 V=-10000:GOTO5070
5057 IFZ=1ANDM1=25THENV=V+VF:REM END GAME BORNE OFF
5058 IFZ=0THENFORLL=0TO18:IFB(LL)<1THENNEXTLL:Z=1:REM END GAME?
5060 NEXTL:GOSUB8000
5070 FORL=0TO27:B(L)=HD(L):NEXTL:Z=ZZ:RETURN
5200 REM MOVE FROM BAR
5201 IFDB=1THENPRINT@416,"BAR MOVE";
5210 IFB(0)=0ORDN<1THENRETURN
5220 FORI=1TODN:J=DI(I):IFB(J)<>-1THEN5240
5221 QF=J:QT=26:GOSUB9000:QF=0:QT=J:GOSUB9000:REM HIT EM
5230 DI(I)=DI(DN):DN=DN-1:GOTO5210:REM COMPACT DICE
5240 NEXTI:FORI=1TODN:J=DI(I):IFB(J)=1THENQF=0:QT=J:GOSUB9000:GOTO5230:REM HELP SINGLE POINT
5250 NEXTI:FORI=1TODN:J=DI(I):IFB(J)>-1THENQF=0:QT=J:GOSUB9000:GOTO5230:REM MOVE ANYWHERE
5260 NEXTI:PRINT@CR," I CAN'T MOVE!";:RETURN
5400 REM DO COMPY MOVE
5410 PRINT": ";:CR=(PEEK(17024)AND1)*256+PEEK(17025)-1:Z=0:REM END GAME FLAG
5411 IFDB=1THENPRINT@416,""
5420 L=0:FORI=0TO18:L=L+(B(I)>0):NEXTI
5430 IFL=0THENZ=1:LV=2:REM END DO LVL 2 LOGIC
5440 GOSUB5200:REM BAR MOVE
5450 IFDN=0ORB(0)<>0THEN5470:REM NO MORE MOVE!
5451 IFZ=0THEN5460
5452 GOTO5500

5458 REM THE PRECEDING MOVES REMOVED ANY MEN DURING AN END GAME AS LONG AS THERE WERE NO HUMAN MEN IN FRONT.
5459 REM IT THEN COMPACTED THE DICE ARRAY.
5460 IFDN<3THENGOSUB7000:GOTO5470
5461 GOSUB6000
5470 PRINT@416,"":RETURN
5700 REM FLAG WE STILL HERE
5705 IFDB=1THENRETURN
5710 IFFG=0THENFG=1
5720 FG=-FG:PT=436
5730 IFFG<0THENPRINT@PT,"cogitate";:GOTO5740
5731 PRINT@PT,"COGITATE";
5740 RETURN

5500 IFB(26)=0THEN5505
5501 GOTO5520 
5505 FORL=1TODN:FORM=25-DI(L)TO24:IFB(M)>-1THEN5510
5506 GOTO5520
5510 NEXTM:IFB(25-DI(L))<1THENNEXTL:GOTO5520
5511 QF=25-DI(L):QT=25:GOSUB9000:DI(L)=0:NEXTL
5520 M=0:FORL=1TO DN:IFDI(L)THENM=M+1:DH(M)=DI(L)
5530 NEXTL:IFDN<>MTHENFORL=1TODN:DI(L)=DH(L):NEXTL:DN=M
5540 IFDN<1THEN5450
5550 GOTO5460

6000 REM COMPY HAD DOUBLES
6001 IFDB=1THENPRINT@416,"DOUBLES MOVE";
6002 MX=-9999:D=DI(1):D1=D:E=24:NM=0:J=999:J1=999:J2=999:J3=999
6003 BG=2*VL:IFLV=2THEN6019
6004 FORL=1TO24:IFB(L)<>-1THENNEXTL:GOTO6019
6005 K=L:FORM=1TOINT(DN/2):K=K-D:IFK<1THENNEXTL:GOTO6019
6006 IFB(K)<-1THENNEXTL:GOTO6019
6007 IFB(K)>1THENBG=1:GOTO6019
6008 NEXTM,L
6019 IFZ=1THENBG=VF*INT(DN/2)+VM
6020 FORJ=1TOE:K=B(J):REM 1ST MOVE
6030 IFK>0THENK=B(J+D):IFK>-2THEN6050
6035 IFZ=1THENIFJ+D>25THEN6050
6040 NEXTJ:J=999:IFMX>-50THEN6160
6041 GOTO6140
6050 FORJ1=JTOE:K=B(J1):IFJ1=J+DTHENK=1:REM 2ND MOVE
6055 IFJ1=JTHENIFK<2THENK=0:REM TOO MANY SAME SPOT
6060 IFK>0THENK=B(J1+D):IFK>-2THEN6080:REM OK TRY IT
6065 IFZ=1THENIFJ1+D1>25THEN6080
6070 NEXTJ1:J1=999:IFMX>-50THEN6040
6071 GOTO6140:REM GOT 1 MOVE AT LEAST WHICH USES ALL DICE. SO DON'T BOTHER WITH MOVES THAT DONT'T EVEN MOVE
6080 GOSUB5700:FORJ2=J1TOE:K=B(J2):IFJ2=J1+DORJ2=J+DTHENK=1:REM 3RD MOVE
6085 IFJ2=J1THENIFJ1=JTHENIFK<3THENK=0:REM TOO MANY FROM SAME SPOT
6090 IFK>0THENK=B(J2+D):IFK>-2THEN6110
6095 IFZ=1THENIFJ2+D>25THEN6110
6100 NEXTJ2:J2=999:IFMX>-50THEN6070
6101 GOTO6140 
6110 GOSUB5700:IFDN<4THEN6140
6111 FORJ3=J2TOE:K=B(J3):IFJ3=J1+DORJ3=J2+DORJ3=J+DTHENK=1
6120 IFK>0THENK=B(J3+D):IFK>-2THEN6140
6125 IFZ=1THENIFJ3+D>25THEN6140
6130 NEXTJ3:J3=999:IFMX>-50THEN6100
6140 GOSUB5000:IFV>MXTHEN6143
6141 GOTO6144
6143 MX=V:NM=1:BS(1,1)=J:BS(2,1)=J+D:BS(1,2)=J1:BS(2,2)=J1+D:BS(1,3)=J2:BS(2,3)=J2+D:BS(1,4)=J3:BS(2,4)=J3+D
6144 IFMX>=BGTHEN6160
6145 IFJ3<>999THEN6130
6150 IFJ2<>999THEN6100
6151 IFJ1<>999THEN6070
6152 IFJ<>999THEN6040
6160 GOTO7120
6500 REM HAS HUMAN USED ALL MOVES?
6510 FORL=1TO4:DH(L)=DI(L):NEXTL
6515 IFMS=0THEN6570:REM NO MOVE?
6520 FORL=1TOMS:REM SEE WHICH ROLLS USED UP
6530 QF=MV(1,L):IFQF=26THENQF=25:REM BAR
6540 QT=MV(2,L):IFQT=27THENQT=0:REM HOME
6550 FORM=1TODN:IFQF-QT=DH(M)THENDH(M)=0:GOTO6560
6551 NEXTM:REM FLAG IT USED
6560 NEXTL
6570 FORL=1TODN:REM SEE IF ANY DICE UNUSED
6580 IFDH(L)=0THEN6700:REM NO
6590 REM CAN HE MOVE THE MISSING ROLL?
6600 D=DH(L):IFB(26)THEN6602
6601 GOTO6605
6602 IFB(25-D)>0THEN6700
6603 Q=2:GOTO6710:REM MAN LEFT ON BAR!
6605 Q=3:IFMS=DNTHENQ=4:REM NO TOO FEW BU BAD COUNT
6610 FORM=1TO24:REM SEE IF ANY REGULAR MOVES AVAILABLE
6620 IFB(M)>-1THEN6650:REM NO MAN HERE
6630 N=M-D:IFN<1THEN6650
6640 IFB(N)<1THEN6710:REM HE HAS A MOVE!
6650 NEXTM
6700 NEXTL:Q=0:REM NO ERROR, ALL ROLLS USED
6710 RETURN
7000 REM DO ALL POSSIBLE REGULAR MOVES, FIND BEST
7001 IFDB=1THENPRINT@416,"REGULAR";
7010 MX=-9999:NM=0 
7020 FORI=1TODN:D=DI(I):D1=DI(3-I):J1=999
7030 FORJ=1TO25-D+Z*(D-1):K=B(J):REM FIRST MOVE
7040 IFK>0THENK=B(J+D):IFK>-2THEN7060
7045 IFZ=1THENIFJ+D>25THEN7060
7050 NEXTJ:J=999:IFMX>-50THEN7110
7051 GOTO7100
7060 IFDN=1THEN7100:REM 1 MOVE ONLY
7065 K=J:IFK<24THENIFI=2ORB(K)<2THENK=K+1
7070 GOSUB5700:FORJ1=KTO24:K=B(J1):IFJ1=J+DTHENK=1:REM SECOND MOVE
7080 IFK>0THENK=B(J1+D1):IFK>-2THEN7100
7085 IFZ=1THENIFJ1+D1>25THEN7100
7090 NEXTJ1:J1=999:IFMX>-50THEN7050:REM UNMOVABLE BUT GOT 1.
7100 GOSUB5000:IFV>MXTHENMX=V:NM=1:BS(1,1)=J:BS(2,1)=J+D:BS(1,2)=J1:BS(2,2)=J1+D1
7105 IFJ1<>999THEN7090
7106 IFJ<>999THEN7050
7110 NEXTI
7120 IFNM=0THENPRINT@CR," I CAN'T MOVE";:RETURN:REM DO BEST MOVE
7130 NM=0:FORL=1TODN:FORM=1TO2
7133 IFBS(M,L)>25ANDBS(M,L)<>999THENBS(M,L)=25
7135 IFBS(M,L)<0ORBS(M,L)>=999THEN7160
7136 NEXTM
7140 IFB(BS(2,L))=-1THENQF=BS(2,L):QT=26:GOSUB9000
7150 QF=BS(1,L):QT=BS(2,L):GOSUB9000:NM=1
7160 NEXTL:IFNM=0THEN7120
7161 RETURN
8000 REM EVALUATE MOVE V=MEN HIT ALREADY AND MEN BORNE OFF
8010 IFZ=1THEN8040:REM END GAME
8020 FORL=0TO18:IFB(L)>0THEN8030
8021 NEXTL:V=V+VM:REM ALL MEN HOME?
8030 FORL=1TO6:IFB(L)>0THENV=V+B(L)*VL:REM MEN IN HUMAN HOME
8035 NEXTL
8040 M=26:IFB(26)<>0THEN8070:REM NOT PAST HUMANS YET
8050 M=0:FORL=24TO0STEP-1:IFM=0THENIFB(L)<0THENM=L:REM M=FIRST HUMAN
8060 IFM=0ORB(L)<=0THENNEXT:V=V+VH:RETURN:REM PAST HUMANS
8065 REM LOOK FOR SINGLES ONLY IF WE AREN'T PAST ALL HUMANS
8070 FORL=1TO24:REM LOOK FOR SINGLES, WORST IS SINGLE WHERE HUMAN CAN HIT THEM, NEXT IS SINGLE IN HUMAN HOME, NEXT IS SINGLE PAST ALL HUMANS
8075 IFB(L)<>1THEN8090
8080 IFL<7THENV=V+VP-VL:GOTO8090:REM SINGLE IN HUMAN HOME
8085 IFL>MTHENV=V+VA:GOTO8087
8086 V=V+VP
8087 IFL>18THENV=V-VO*B(26):REM MEN OPEN IN HOME CAN BE HIT
8090 NEXTL
8095 IFB(26)=0THENRETURN
8096 M=0:FORL=19TO24:IFB(L)=1THENM=0:GOTO8099
8097 IFB(L)>1THENM=M+1:NEXTL:GOTO8099
8098 NEXTL
8099 IFM<>6THENV=V+INT((M*3-1)/2):RETURN
8100 V=V+35:RETURN:REM SHUT HIM OUT!
8900 CLS:PRINT@139,"BACKGAMMON"
8903 PRINT@201,"COPYRIGHT 1978"
8904 PRINT@231,"CREATIVE COMPUTING"
8906 PRINT@265,"MORRISTOWN, NJ"
8907 PRINT@293,"BY SCOTT ADAMS 8/25/78"
8908 FORI=1TO2500:NEXTI:RETURN
9000 REM MOVE QT TO QF
9010 S=SGN(B(QF)):IFS=0THENRETURN
9020 Q4$="o":IFS=-1THENQ4$="x"
9030 Q=ABS(B(QF)):B(QF)=B(QF)-S:Q2=QF
9040 GOSUB9100:C1=PEEK(Q1+MC):IFC1>64ANDC1<92THENPOKEQ1+MC,C1-64:GOTO9050
9041 PRINT@Q1,"€";:REM REMOVE FROM
9050 B(QT)=B(QT)+S:Q=ABS(B(QT)):Q2=QT:GOSUB9100:C1=PEEK(Q1+MC):IFC1>.ANDC1<27THENQ4$=CHR$(ASC(Q4$)-32)
9051 PRINT@Q1,Q4$;:B$=MID$("##",1,2+(QF<10)):C$=MID$("##",1,2+((QT<10)OR(QT=25))):IFS=1THEN9075
9071 RETURN
9075 PRINT@CR,;:PRINTSTR$(QF)"-"MID$(STR$(-QT*(QT<25)),2)".";:CR=(PEEK(17024)AND1)*256+PEEK(17025)-1:RETURN
9100 REM COMPUTE SCREEN POSITION Q=CHIP#, Q2=POINT#
9110 Q3=1:IFQ2>0ANDQ2<25THENQ3=INT(Q2/13)+1:REM TOP/BOTTOM
9120 IFQ2>25THENQ3=2
9125 Q3=3-Q3
9130 IFQ2=0ORQ2=26THENQ4=155+320*(3-Q3-1):GOTO9140
9131 IFQ2=25ORQ2=27THENQ4=187+320*(3-Q3-1):GOTO9140
9132 IFQ2>18THENQ4=128+33+(Q2-19)*4:GOTO9140
9133 IFQ2>12THENQ4=127.5+(Q2-13)*4:GOTO9140
9134 IFQ2>6THENQ4=447.5+(12-Q2)*4:GOTO9140
9135 Q4=448+33+(6-Q2)*4
9140 Q1=(Q7(Q3,Q)+Q4)*.5:RETURN
10000 REM DRAW BOARD
10020 CLS:POKE49151,64:POKE17026,112
10021 PRINT"131415161718õbarú192021222324off";
10022 PRINT"½¾ýþ½¾ýþ½¾ýþõ€€€ú½¾ýþ½¾ýþ½¾ýþ   ";
10023 FORC1=1TO9:PRINT"€€€€€€€€€€€€õ€€€ú€€€€€€€€€€€€   ";:NEXT
10032 PRINT"÷û·»÷û·»÷û·»õ€€€ú÷û·»÷û·»÷û·»   ";
10033 PRINT"121110 9 8 7õbarú 6 5 4 3 2 1OFF";:FORC1=0TO56STEP4:SET(C1,13,8):SET(C1+1,13,8):NEXT
10034 FORCC=1TO3:M$=MID$(" 131517",CC*2,2):PRINT@0+(CC-1)*4,;:GOSUB7:NEXT
10035 FORCC=1TO3:M$=MID$(" 192123",CC*2,2):PRINT@17+(CC-1)*4,;:GOSUB7:NEXT
10036 FORCC=1TO3:M$=MID$(" 11 9 7",CC*2,2):PRINT@386+(CC-1)*4,;:GOSUB7:NEXT
10037 FORCC=1TO3:M$=MID$("  5 3 1",CC*2,2):PRINT@403+(CC-1)*4,;:GOSUB7:NEXT
10060 REM PUT MEN ON BOARD
10070 FORQ2=0TO27:IFB(Q2)=0THEN10110
10080 FORQ=1TOABS(B(Q2)):GOSUB9100
10090 IFB(Q2)<0THENPRINT@Q1,"x";:GOTO10100
10091 PRINT@Q1,"o";
10100 NEXT
10110 NEXT
10120 RETURN
11111 DI(1)=RND(6)
11121 DI(2)=RND(6)
11130 IFDI(1)=DI(2)THENDN=4:DI(3)=DI(1):DI(4)=DI(1):RETURN
11140 DN=2:RETURN
20000 REM INSTRUCTIONS
20010 CLS:PRINT"TO ENTER A MOVE INPUT:"
20011 PRINT"    FROM-TO-TIMES."
20012 PRINT"SEPERATE EACH MOVE BY A PERIOD."
20030 PRINT"E.G. TO MOVE FROM 2 TO 3 TWICE:"
20040 PRINT"    2-3-2"
20050 PRINT"E.G. TO MOVE FROM 1 TO 2 AND
20051 PRINT"FROM 3 TO 4"
20060 PRINT"    1-2.3-4
20070 PRINT"E.G. TO MOVE FROM 1 TO 2 AND"
20071 PRINT"FROM 3 TO 4":PRINT"AND FROM 8 TO HOME TWICE..."
20080 PRINT"    1-2.3-4.8-0-2"
20090 PRINT"NOTE: HOME AND BAR ARE":PRINT"DESIGNATED BY 0."
20100 PRINT"INPUT 'BOARD' TO REDRAW BOARD."
20400 INPUT"HIT ENTER TO START";A$:CLS:RETURN
30000 SOUND1,1:POKE17026,0:PRINT"THANKS FOR PLAYING!":PRINT:PRINT"TO PLAY AGAIN, TYPE 'RUN'."
30001 END
30002 REM VERSION 9/13/78 1433
