5 GOSUB2000
30 WS=143+16*RND(6):IS=42:MS=25:CS=3:SD=WS:NS=46:ES=255
40 TL=16384:TR=TL+14:LL=32
50 DT=TR-TL:BL=TL+DT*LL:BR=BL+DT
60 CP=BR-LL-1:MP=TL+LL+1:CM=-LL:MM=1
70 HT=INT(DT/2):HL=INT((BL-TL)/2):BC=TL+HT-HL
71 BC=TL+HT+HL
75 YC=INT((BC-TL)/LL):XC=BC-(TL+(LL*YC)):HT=HT-1:HL=HL-LL
80 DC=2*(LL+1):TN=0:TS=0:SP=BL+DC+HT-2
85 NV=NV+10:NH=0:HV=NV/2:LT=TL:LB=BL:RT=TR:RB=BR
90 FORA=LTTORT:GOSUB1000:NEXT
100 FORA=LBTORB:GOSUB1000:NEXT
110 FORA=LTTOLBSTEPLL:GOSUB1000:NEXT
120 FORA=RTTORBSTEPLL:GOSUB1000:NEXT
180 LT=LT+LL+1:RT=RT+LL-1:LB=LB-LL+1:RB=RB-LL-1
190 IFLT=BC-DCTHENSD=IS:GOTO230
200 IFLT=BCTHEN260
210 IFSD=WSTHENSD=IS:GOTO90
220 IFSD=ISTHENSD=WS:GOTO90
230 FORA=BC-HTTOBC+HT:IFPEEK(A)=ISTHENTN=TN-1
235 POKEA,32:NEXT
240 FORA=BC-HLTOBC+HLSTEPLL:IFPEEK(A)=ISTHENTN=TN-1
245 POKEA,32:NEXT
250 GOTO90
260 IFPEEK(CP)=ISTHENTN=TN-1
270 IFPEEK(MP)=ISTHENTN=TN-1
280 POKECP,CS:POKEMP,MS:POKEBC,WS
299 REM PLAYER'S MOVE
300 MY=YC-INT((MP-TL)/LL):MX=MP-BC+(LL*MY)
310 TM=.:IFPEEK(2)THENIFMX=.ORMY=.THEN390
340 NP=PEEK(MP+MM)
350 IFNP<>WSTHEN450
360 MM=-LL/MM:IFMX=MYTHENMM=-MM
380 GOTO340
390 IF1ANDNOTPEEK(16946)THENTM=-LL/ABS(MM):GOTO430
410 IF1ANDNOTPEEK(16949)THENTM=LL/ABS(MM)
430 NP=PEEK(MP+TM):IFNP=WSTHENTM=.:GOTO340
445 TM=TM*2:NP=PEEK(MP+TM)
450 IFNP=CSTHEN950
460 IFNP=ISTHENTS=TS+NV:NH=NH+1
470 IFNP=NSTHENTS=TS+HV:NH=NH+1
480 POKEMP,32
490 PM=MM:IFTMTHENPM=TM
500 MP=MP+PM:POKEMP,MS
699 REM COMPUTER'S MOVE
700 CY=YC-INT((CP-TL)/LL):CX=CP-BC+(LL*CY):PM=CM
710 TM=.:CA=PEEK(CP+CM)
720 IFCA<>WSTHEN760
730 CM=-LL/CM:IFCX=CYTHENCM=-CM
750 PM=CM:GOTO850
760 IFCA=MSTHEN950
770 IFCXTHENIFCYTHENZM=.:GOTO850
780 A=MM*MM:IFA=1THENB=ABS(MY):GOTO800
790 B=ABS(MX)
800 IFRND(0)>SLTHEN850
805 IFABS(CY)=BORABS(CX)=BTHENZM=.:GOTO850
810 TM=LL/ABS(CM)
815 IFPEEK(CP+TM)=WSTHENTM=-TM:ZM=TM
820 IFZM=.THEN830
825 IFZM<>TMTHENTM=-TM:GOTO815
830 TM=2*TM
840 IFTMTHENPM=TM
850 NC=PEEK(CP+PM):RS=32:IFNC=32THEN870
860 IFNC=MSTHEN950
870 IFF=1THENRS=NS:F=.
880 IFNC=ISTHENTS=TS-NV
890 IFNC=ISORNC=NSTHENF=1
900 POKECP,RS:CP=CP+PM:POKECP,CS
905 REM POKE SCORE
910 PRINT@84,TS
930 IFNH=TNTHENTS=TS+(NV*10):GOTO85
940 GOTO300
950 POKEMP,32:POKECP,32:POKEMP,ES:SOUND1,1
955 PRINT@20,"GAME OVER.":PRINT@52,"YOU SCORED":PRINT@84,TS
970 PRINT@20+128,"TRY AGAIN?"
980 M$=INKEY$:IFM$="Y"THENRUN
981 IFM$="N"THENEND
982 GOTO980
990 REM SUBROUTINE TO POKE BOARD
1000 IFPEEK(A)=ISORSD=WSTHEN1020
1010 TN=TN+1
1020 POKEA,SD
1030 RETURN
2000 CLS:A=RND(-(PEEK(9)*256+PEEK(10))):PRINTTAB(7)"HEAD-ON COLLISION!":PRINT
2001 PRINTTAB(3)"BY WYNFORD AND JANE JAMES":PRINT
2005 PRINT"YOU SCORE BY HITTING THE SYMBOLS";
2010 PRINT"IN THE LANES. ORIGINAL SYMBOLS"
2020 PRINT"ARE WORTH TWICE THE VALUE OF"
2030 PRINT"SYMBOLS CREATED BY THE PATH OF"
2035 PRINT"THE COMPUTER'S PIECE. YOUR PIECE";
2040 PRINT"ONLY MOVES CLOCKWISE.":PRINT
2050 PRINT"a CHANGES LANE UP/LEFT,"
2070 PRINT"d CHANGES LANE DOWN/RIGHT.":PRINT
2075 PRINT"SKILL LEVEL: 1 TO 5";:INPUTSL
2080 A=INT(SL):IFA<1ORA>5THEN2075
2090 NV=A*2:SL=.5+SL/10
2100 CLS0:RETURN
2200 REM PRACTICAL COMPUTING
2210 REM NOVEMBER 1981
2220 MC-10 EDITS JIM GERRIE
