0 CLEAR1000
10 REM ** FUZZY MATCHING USING SOUNDEX CODES** 
20 REM **BOB CHAPPELL 17/4/82**
30 DIMSC$(26),N$(200,2)
40 REM **SOUNDEX CODES FOR ALPHABET**
50 FORJ=1TO26:READSC$(J):NEXT
60 REM READ IN NAMES**
70 CLS:PRINTTAB(4)"SOUNDEX CODING THE DATA":PRINT
80 TN=130:FORJ=1TOTN:READN$:GOSUB110
90 N$(J,1)=N$:N$(J,2)=CD$:PRINTN$;" = ";N$(J,2):NEXT:GOTO180
100 REM **TRANSFORM TO SOUNDEX  CODE**
110 CD$=LEFT$(N$,1):L=LEN(N$):LC$=SC$(ASC(CD$)-64):IFL=1THEN160
120 FORK=2TOL:A=ASC(MID$(N$,K,1))-64:IFA<1ORA>26THEN150
130 A$=SC$(A):IFA$="0"ORA$=LC$THEN150
140 CD$=CD$+A$:IFLEN(CD$)=4THENK=L
150 LC$=A$:NEXTK:IFLEN(CD$)=4THENRETURN
160 FORK=1TO(4-LEN(CD$)):CD$=CD$+"0":NEXTK:RETURN
170 REM **SHELL-METZNER SORT**
180 CLS:PRINT"SORTING INTO":PRINT"NAME WITHIN SOUNDEX CODE ORDER":N=116:M=116 
190 M=INT(M/2):IFM=0THEN290
200 J=1:K=N-M
210 I=J
220 L=I+M:IFN$(I,2)<N$(L,2)THEN260
230 IFN$(I,2)=N$(L,2)ANDN$(I,1)<N$(L,1)THEN260
240 F$=N$(I,2):N$(I,2)=N$(L,2):N$(L,2)=F$
250 F$=N$(I,1):N$(I,1)=N$(L,1):N$(L,1)=F$:I=I-M:IFI>0THEN220
260 J=J+1:IFJ>KTHEN190
270 GOTO210
280 REM **OBTAIN INPUT FROM USER**
290 CLS
300 PRINT"DO YOU WISH TO ENTER A NAME (N)"
310 INPUT"OR A CODE (C)";A$:PRINT
320 IFA$="N"THEN440
330 IFA$<>"C"THENPRINT"INVALID ENTRY.":GOTO300
340 PRINT"CODE MAY BE A LETTER FOLLOWED":PRINT"BY A MAXIMUM OF THREE NUMBERS."
350 PRINT:INPUT"ENTER THE CODE";CD$:PRINT
360 A=ASC(LEFT$(CD$,1))-64
370 IFA<1ORA>26THENPRINT"FIRST CHARACTER MUST BE A":PRINT"LETTER." :GOTO350
380 L=LEN(CD$):IFL>4THENPRINT"CODE IS TOO LONG.":GOTO350
390 J=2:IFL=1THEN430
400 K=ASC(MID$(CD$,J,1))-48
410 IFK<0ORK>9THENPRINT"LETTER MUST BE FOLLOWED BY":PRINT"NUMBER(S).":GOTO350
420 J=J+1:IFJ<=LTHEN400
430 A=L:GOTO510
440 CLS:INPUT"ENTER THE NAME";N$ 
450 GOSUB110:PRINT:PRINT"THE SOUNDEX CODE IS ";CD$:L=4
470 FORJ=1TOL:PRINT:PRINTTAB(10);J;MID$(CD$,1,J):NEXT
480 PRINT:PRINT"WHICH NUMBER (1-";MID$(STR$(L),2);")":INPUTY$
490 A=VAL(Y$):IFA<1ORA>LTHENPRINT"INVALID NUMBER.":GOTO480
500 CD$=MID$(CD$,1,A)
510 CLS:PRINT"MATCHING AGAINST ";CD$
520 PRINT"================================";
530 TT=0:FORJ=1TOTN:N$=MID$(N$(J,2),1,A)
540 IFCD$>N$THEN570
550 IFCD$<N$THENJ=TN:GOTO570
560 PRINTN$(J,1):TT=1
570 NEXTJ:PRINT:IFTT=0THENPRINT"NO MATCHES FOUND."
580 PRINT@484,"PRESS SPACE TO CONTINUE.";
590 A$=INKEY$:IFA$<>" "THEN590
600 GOTO290
610 REM**SOUNDEX CODES FOR ALPHABET
620 DATA0,1,2,3,0,1,2,0,0,2,2,4,5,5,0,1,0,6,2,3,0,1,0,2,0,2 
630 REM**NAMES**
640 DATAABEL,ABEL,ADAMS,ADAMSON,ALDISS,ALDOUS,ALLAN,ALLEN,ALLENBURY,ALLENBY
650 DATAANDERSEN,ANDERSON
660 DATABAILEY,BAILY,BAILLIE,BROWN,BROWNE,BROWNIG,BROWNLEE
670 DATACLARK,CLARKE,CLARKSON,COLLINGS,COLLINS,COOK,COOKE,COOKSON
680 DATAD'EATH,DE'ATH,DE ATH,DEATH,DAVIES,DAVIESON,DAVIS,DAVISON,DAVY,DAVYS
690 DATAEDMONDS,EDMONDSON,EDMUNDS,EDMUNDSON,FORSTER,FOSTER
700 DATAFEATHERSTONEHAUGH,FEDROSS,FITHERSTON
710 DATAGARDENER,GARDNER,HAIG,HAIGH,HAIN,HAINES,HAYNES
720 DATAHEWES,HEWETT,HEWITT,HEWISON,HEWSON,HUGHES,HUGHS,HUGHSON
730 DATAIRVINE,IRVING,JOHNSON,JOHNSTON,JOHNSTONE
740 DATAKNIGHT,KNIGHTLEY,KNIGHTON,KNIGHTS,LEA,LEE
750 DATALLOYD,LOYD,MACINTOSH,MACKINTOSH,MCINTOSH,MATHEWS,MATTHEWS
760 DATAMARQUAYS,MARS,MARWAYS,MARRYS,MCINTYRE,MACKINDER,MCANTRY
770 DATAMOORE,MORE,NICHOLAS,NICHOLLS,NICHOLS,NICHOLSON
780 DATAOSBORN,OSBORNE,OSBOURNE,PHILIPS,PHILIPSON,PHILLIPS,PHILLIPSON
790 DATAQUIN,QUINN,READ,REED,REID,ROBERTS,ROBERTSON,ROBBINS,ROBINSON
800 DATATHOMAS,THOMPSON,THOMSON,UNDERHILL,UNDERWOOD,SMITH,SMYTH,SMYTHE 
810 DATAVINE,VYNE,WALLACE,WALLIS,WALLS,WILLIAMS,WILLIAMSON
820 DATAXERXES,XERKSES,YATES,YEATS,ZACKS,ZAKS,ZAX 
830 REM MICROCOMPUTER PRINTOUT
835 REM AUGUST 1982
840 REM MC-10 EDITS JIM GERRIE
