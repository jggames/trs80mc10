REM MC-10 VERSION
0 DIMC1,C2,M$:M=16384:GOTO99
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
99 DIMA(100)
100 GOSUB3000:GOSUB2500
230 FORN=1TO64:GOSUB3500:NEXTN:N=1:PRINT@192/2,"WANT TO GO FIRST (Y/N)?";
235 A$=INKEY$:IF A$="" THEN 235
240 PRINT@96,"ÄÄÄÄèèèèÄÄÄÄèèèèÄÄÄÄèèèèÄÄÄÄèèè ";:GOSUB6000:IFA$="N"THENGOSUB5000
300 GOSUB1000:GOSUB5000:GOTO300
1000 PRINT@92,"FROM";
1001 A$=INKEY$:ON-(A$="")GOTO1001:PRINT@124,A$;
1002 B$=INKEY$:ON-(B$="")GOTO1002:PRINT@125,B$;
1003 F$=A$+B$:F=VAL(F$):Q=1
1005 IF(F<1)+(F>64)THENGOSUB6000:GOTO1000
1006 PRINT@92," TO ";
1007 A$=INKEY$:ON-(A$="")GOTO1007:PRINT@124,A$;
1008 B$=INKEY$:ON-(B$="")GOTO1008:PRINT@125,B$;
1009 T$=A$+B$:T=VAL(T$):IF(T=0)AND(Q=0)THENRETURN
1010 IF(T<1)+(T>64)THENGOSUB6000:GOTO1006
1015 FORX=112TO127:FORY=6TO11:SET(X*.5,Y*.667,1):SET(X*.5,(Y+12)*.667):NEXTY:NEXTX
1020 GOSUB1500:IFQ=0THENPRINT@220,"BAD ";:PRINT@252,"MOVE";:GOTO1000
1100 A(T)=A(F):A(F)=0:N=F:GOSUB3500:N=T:GOSUB3500
1150 IFT<9THENA(T)=-2:N=T:GOSUB3500
1200 IF(ABS(T-F)=7)OR(ABS(T-F)=9)THENRETURN
1250 N=(T+F)/2:A(N)=0:GOSUB3500
1255 IFA(T)=-1THEN1270
1260 FORB=7TO9STEP2:G=T+B:IF(G>64)OR(G+B>64)THEN1266
1265 IF((A(G)=1)OR(A(G)=2))AND(A(G+B)=0)THENF=T:Q=0:GOTO1006
1266 NEXTB
1270 FORB=-9TO-7STEP2:G=T+B:IF(G<0)+(G+B<0)THEN1290
1280 IF((A(G)=1)+(A(G)=2))*(A(G+B)=0)THENF=T:Q=0:GOTO1006
1290 NEXTB:RETURN
1500 Q=0:IF(A(F)>-1)OR(A(T)<>0)THENRETURN
1520 IFA(F)=-2THENFORB=-9TO-7STEP2:GOSUB1560:NEXTB
1540 FORB=7TO9STEP2:GOSUB1560:NEXTB:RETURN
1560 IF(F-T=B)OR((F-T=2*B)AND((A((F+T)/2)=1)OR(A((F+T)/2)=2)))THENQ=1
1580 RETURN
2000 IFU=1THENH=8:GOTO2002
2001 H=6
2002 FORX=UTOHSTEP2:W=16*X
2005 FORY=UTOHSTEP2:Z=6*Y
2010 FORI=WTOW+15:FORJ=ZTOZ+5:SET(I,J):NEXTJ:NEXTI:NEXTY:NEXTX
2020 RETURN
2500 FORN=1TO64:READA(N):NEXTN:RETURN
2511 DATA99,1,99,1,99,1,99,1,1,99,1,99,1,99,1,99,99,1,99,1,99,1
2512 DATA99,1,0,99,0,99,0,99,0,99,99,0,99,0,99,0,99,0,-1,99,-1,99
2513 DATA-1,99,-1,99,99,-1,99,-1,99,-1,99,-1,-1,99,-1,99,-1,99,-1,99
3000 CLS0:FORX=0TO7:FORY=0TO7:PRINT@((128*X)+(8*Y))*.5,;:M$=STR$(8*X+Y+1):GOSUB7:NEXTY:NEXTX
3005 A$=CHR$(143)+CHR$(143)+CHR$(143)+CHR$(143)
3010 FORX=0TO6STEP2:FORY=0TO6STEP2:Z=((128*X)+(8*Y)):PRINT@Z*.5,;:M$=STR$(8*X+Y+1):GOSUB7
3011 PRINT@Z*.5,A$;:PRINT@(Z+64)*.5,A$;:PRINT@(Z+136)*.5,A$;:IFZ<>816THENPRINT@(Z+200)*.5,A$;:NEXTY:NEXTX
3012 FORX=112TO127:FORY=45TO47:SET(X*.5,Y*.667,1):NEXTY,X
3015 PRINT@33,;:RETURN
3500 Y=(INT((N-1)/8)+1):X=N-(Y-1)*8:I=(X-1)*16:J=(Y-1)*6
3510 ON(A(N)+3)GOSUB3600,3700,3800,3900,4000
3520 RETURN
3600 FORZ=2TO13:SET((I+Z)*.5,(J+3)*.667,1):SET((I+Z)*.5,(J+4)*.667,1):NEXTZ:RETURN
3700 FORZ=2TO13:SET((I+Z)*.5,(J+4)*.667,1):NEXTZ:RETURN
3800 FORZ=2TO13:RESET((I+Z)*.5,(J+3)*.667):RESET((I+Z)*.5,(J+4)*.667):NEXTZ:RETURN
3900 GOSUB3700:FORZ=4TO13STEP3:RESET((I+Z)*.5,(J+4)*.667):NEXTZ:RETURN
4000 GOSUB3600:FORZ=4TO14STEP3:RESET((I+Z)*.5,(J+3)*.667):RESET((I+Z)*.5,(J+4)*.667):NEXTZ:RETURN
5000 Q=-99:FORI=1TO64:IF(A(I)<1)OR(A(I)>2)THEN5050
5010 IFA(I)=2THENFORB=-9TO-7STEP2:GOSUB5100:NEXTB
5020 FORB=7TO9STEP2:GOSUB5100:NEXTB
5050 NEXTI:IFQ=-99THEN9900
5070 A(T)=A(F):A(F)=0:N=F:GOSUB3500:N=T:GOSUB3500
5075 IFT>56THENA(T)=2:N=T:GOSUB3500
5080 IF(ABS(F-T)<>14)AND(ABS(F-T)<>18)THENRETURN
5085 N=(T+F)/2:A(N)=0:GOSUB3500:IFA(T)=1THEN5090
5087 FORB=-9TO-7STEP2:C=T+B:IF(C<0)OR(C+B<0)THEN5089
5088 IF(A(C)<0)AND(A(C+B)=0)THENF=T:T=C+B:GOTO5070
5089 NEXTB
5090 FORB=7TO9STEP2:C=T+B:IF(C>64)OR(C+B>64)THEN5098
5091 IF(A(C)<0) AND (A(C+B)=0)THENF=T:T=C+B:GOTO5070
5098 NEXTB:RETURN
5100 R=-99:J=I+B:K=0:IF(J<0)OR(J>64)THEN5200
5105 IFA(J)=0THENR=0:L=J
5108 IF(J+B<0)OR(J+B>64)THEN5115
5110 IF(A(J+B)=0)AND(A(J)<0)THENR=5:K=1:L=J+B
5115 IFR=-99THEN5200
5116 IFA(I)=2THENR=R-1:GOTO5126
5120 IFI<9THENR=R-2
5123 IFL>47THENR=R+3
5125 IFL>56THENR=R+3
5126 FORA=7TO9STEP2:GOSUB5250:NEXTA
5190 C=RND(0):IF(R>Q)OR((R=Q)AND(C>.4))THENQ=R:F=I:T=L
5200 RETURN
5250 G=I+A:H=I-A:IF(G>64)AND(G+A>64)THEN5270
5255 IF((A(G)=1)OR(A(G)=2))AND(A(G+A)<0)THENR=R-8
5256 IFH<0THEN5300
5260 IF(A(G)<0)AND(A(H)=0)THENR=R+4
5265 IF(A(H)=-2)AND(A(G)=0)THENR=R+4
5270 IF(H<0)OR(H-A<0)THEN5300
5280 IF((A(H)=1)OR(A(H)=2))AND(A(H-A)=-2)THENR=R-8
5300 S=R:G=L+A:H=L-A:IFG>64THEN5335
5302 IFH<0THEN5307
5305 IF(A(G)<0)AND((A(H)=0)+(I=H))THENR=R-10:GOTO5307
5306 IF(A(H)=-2)AND((A(G)=0)OR(I=G))THENR=R-10
5307 IFG+A>64THEN5335
5310 IF(A(G)<0)AND(A(G+A)=0)THENR=R+5+K*10
5330 IF((A(G)=1)OR(A(G)=2))AND(A(G+A)<0)THENR=R+4:S=S+4
5335 IF(H<0)OR(H-A<0)THEN5350
5340 IF((A(H)=1)OR(A(H)=2))AND(A(H-A)=-2)THENR=R+4:S=S+4
5350 IFR>STHENR=S
5500 RETURN
6000 FORY=6TO11:FORX=1TO7STEP2:FORZ=X*16TOX*16+15:SET(Z*.5,Y*.667,1):NEXTZ:NEXTX:NEXTY
6200 FORN=9TO15STEP2:GOSUB3500:NEXTN:FORX=0TO1:FORY=12TO14:SET(X*.5,Y*.667,1):NEXTY:NEXTX:RETURN
9900 PRINT@0,"YOU WIN";:GOTO9900
REM https://willus.com/trs80/
REM FROM PLANET EMU
