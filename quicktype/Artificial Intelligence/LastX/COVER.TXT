10 CLS:PRINT"GAME OF last x WHERE COMPUTER":PRINT"MODIFIES ITSELF"
30 DEFINT A-Z
40 DIM A$(2),BD(15,3),CR(10),CP(10),HR(10),HP(10)
50 A$(1)="---I ":A$(2)="---YOU "
60 FOR J=1 TO 3:FOR I=1 TO 15:REM READ IN
70 READ BD(I,J):REM ***STRATEGY
80 NEXT I,J:REM *******VALUES
90 DATA50,50,50,50,50,50,50,50,50,50,50,50,50,50,50
91 DATA50,50,50,50,50,50,50,50,50,50,50,50,50,50,50
92 DATA50,50,50,50,50,50,50,50,50,50,50,50,50,50,50
100 PC=15:REM PIP COUNTER
104 NM=0:REM MOVE COUNTER
108 A$="C":REM SET TO ME
110 INPUT"WHO'S FIRST, <H> OR <C>";A$
120 GOSUB 5010:REM DISPLAY BOARD
130 IF A$="H" THEN 2010:REM IF HUMAN, SKIP
1000 PL=1:REM COMPUTER PLAYER
1010 MX=0:REM MAXIMUM MOVE
1011 FORI=1 TO 3:REM CYCLE THROUGH MOVES
1012 IF MX>BD(PC,I) THEN 1015:REM THIS ONE BETTER?
1013 MX=BD(PC,I):REM NO, THIS ONE IS
1014 PP=I:REM SET PIPS TO TAKE
1015 NEXT I:REM ALL DONE
1016 IF MX=50 THEN PP=RND(3):REM WELL, JUST PICK ONE
1020 GOSUB 4000:REM LEGAL CHECK?
1025 IF LF<>0 THEN 1000:REM SORRY, DO AGAIN
1030 PRINT:PRINT"I'LL TAKE ";PP
1032 IF A$="C" THEN NM=NM+1
1034 CR(NM)=PC:REM SAVE PIP COUNTER
1036 CP(NM)=PP:REM SAVE THIS MOVE
1040 FOR I=1 TO 2000:REM SLOW DOWN FOR HUMAN
1050 GOSUB 5000:REM ADJUST BOARD
2000 PL=2:REM HUMAN PLAYER
2010 PRINT:INPUT"YOUR MOVE HUMAN, ENTER PIPS";PP
2020 GOSUB 4000:REM LEGAL CHECK?
2030 IF LF=0 THEN 2060
2040 PRINT"CHEATER"
2050 GOTO 2010
2060 GOSUB 5000:REM ADJUST BOARD
2062 IF A$="H" THEN NM=NM+1:REM INCREASE MOVE COUNTER
2064 HR(NM)=PC+PP:REM SAVE PIP COUNTER
2066 HP(NM)=PP:REM SAVE THIS MOVE
2070 GOTO 1000:REM COMPUTER'S MOVE
4000 LF=0:REM LEGAL CHECK
4010 IF PP>3 THEN LF=1:REM GREATER THAN 3
4020 IF PP<1 THEN LF=1:REM LESS THAN 1
4030 IF PP>PC THEN LF=1:REM NOT ENOUGH LEFT
4040 RETURN
5000 PC=PC-PP:REM ADJUST BOARD
5010 IF PC=0 THEN 6000:REM EMPTY?
5020 FOR I=1 TO PC
5030 PRINT"X ";
5040 NEXT I
6000 REM WIN CHECK
6010 IF PC>0 THEN RETURN
6020 PRINT A$(3-PL);"WIN"
7000 REM LEARNING
7010 PRINT"JUST A SEC WHILE I LEARN FROM":PRINT"THIS VALUABLE EXPERIENCE"
7020 FOR I=1 TO NM:REM REPLAY GAME
7030 BD(HR(I),HP(I))=BD(HR(I),HP(I))+(3-2*PL):REM HUMAN WINS
7040 BD(CR(I),CP(I))=BD(CR(I),CP(I))-(3-2*PL):REM COMPY WINS
7050 NEXT I
8000 AD=PEEK(16639)+256*PEEK(16640):REM LAST DATA READ
8010 AD=AD-144:REM FIRST 50'S
8020 FOR I=1 TO NM:REM DECODE
8030 XR=HR(I):REM THE
8040 XP=HP(I):REM HUMAN
8050 GOSUB 9010
8060 XR=CR(I):REM AND
8070 XP=CP(I):REM COMPUTER
8080 GOSUB 9010
8090 NEXT I:REM MOVES
8100 GOTO 100
9000 IF XR*XP=0 THEN RETURN:REM ODD GAME PROTECTION
9010 XB=BD(XR,XP):REM POKE ROUTINE
9020 PK=AD+50*(XP-1)+3*(XR-1)
9030 LB=INT(XB/10):REM LEFT BYTE
9040 RB=XB-LB*10:REM RIGHT BYTE
9050 POKE PK,48+LB:REM POKE LB
9060 POKE PK+1,48+RB:REM POKE RB
9070 RETURN
9900 REM A.W. MADDOX
9910 REM BASIC COMPUTING
9920 REM FEB 1984
9930 REM MC-10 JIM GERRIE
