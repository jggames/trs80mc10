100 CLS:X=RND(-(PEEK(9)*256+PEEK(10)))
110 PRINTTAB(11)"J-CHECKERS"
120 PRINTTAB(5)"BY CREATIVE COMPUTING"
125 PRINTTAB(9)"AND J.F. WHITE"
130 DIMR(4),T(4),S(8,8)
140 FORX=1TO8:FORY=1TO8:READJ:IFJ=15THEN160
150 S(X,Y)=J:GOTO170
160 RESTORE:READS(X,Y)
170 NEXTY:NEXTX:READX
171 X=32768:GOSUB3000:X=USR(0):REM INSTALL TIMER
180 DATA1,0,1,0,0,0,-1,0,0,1,0,0,0,-1,0,-1,15
190 CLS
200 PRINT"THIS IS THE GAME OF CHECKERS.":PRINT"BLACK IS X AND WHITE IS O."
210 PRINT"SQUARES APE REFERRED TO BY A":PRINT"COORDINATE SYSTEM"
220 PRINT"(1,1) IS THE LOWER LEFT CORNER"
230 PRINT"(1,8) IS THE UPPER LEFT CORNER"
240 PRINT"(8,1) IS THE LOWER RIGHT CORNER"
250 PRINT"(8,8) IS THE UPPER RIGHT CORNER"
260 PRINT"THE COMPUTER WILL TYPE '+ TO'":PRINT"AFTER YOU MAKE A JUMP. ENTER"
270 PRINT"0,0 IF YOU CANNOT JUMP AGAIN."
280 PRINT"YOU WILL BE ASKED WHICH TYPE":PRINT"OF COMPUTER SEARCH YOU WANT."
290 PRINT"PRUNED SEARCH IS FASTER."
300 INPUT"SEARCH LEVEL (1-2)";L:CLS
310 IFL=1THEN330
320 GOTO360
330 INPUT "DO YOU WANT ITERATIVE DEEPENING (Y/N)";ID$
340 IFID$="Y"THENINPUT"SET TIME FOR SEARCH (SECS)";ST:AB$="Y":GOTO410
350 GOTO410
360 INPUT"DO YOU WANT PRUNED SEARCH       (Y/N)";FA$
370 IFFA$="Y"THEN410
380 INPUT"DO YOU WANT ALPHA-BETA SEARCH   (Y/N)";AB$
390 IFAB$="Y"THEN410
400 PRINT"DEFAULT TO MINIMAX SEARCH."
410 INPUT"DO YOU WANT TO GO FIRST         (Y/N)";C$
420 GOSUB2000:TB=TA
430 R(0)=-99:G=-1:P1=12:P2=12
440 IFC$="Y"THENC1$="X":C2$="O":C3$="o":C4$="x":M=M+1:GOTO910
450 C1$="O":C2$="X":C3$="x":C4$="o"
460 M=M+1
470 IFID$="Y"ANDL=2THENX=R(1):Y=R(2):B=G:FORA=-1TO1STEP2:GOSUB570:NEXT
480 IFM=2ANDS(2,6)=0THENR(1)=3:R(2)=7:R(3)=2:R(4)=6:GOTO790
490 FORX=1TO8:FORY=1TO8:IFS(X,Y)>-1THEN520
500 IFS(X,Y)=-1THENB=G:FORA=-1TO1STEP2:GOSUB570:NEXT
510 IFS(X,Y)=-2THENFORB=-1TO1STEP2:FORA=-1TO1STEP2:GOSUB570:NEXT:NEXT
520 NEXTY:NEXTX
530 IFID$="Y"ANDID=0ANDL<2THENL=L+1:GOTO470
540 ID=0:IFID$="Y"THENL=1
550 GOTO780
560 REM FIRST MOVE GENERATOR
570 IFID=1THENRETURN
580 U=X+A:V=Y+B:IFU<1ORU>8ORV<1ORV>8THENRETURN
590 IFS(U,V)=0THENGOSUB640:RETURN
600 IFS(U,V)<0THENRETURN
610 U=U+A:V=V+B:IFU<1ORU>8ORV<1ORV>8THENRETURN
620 IFS(U,V)=0THENGOSUB640
630 RETURN
640 UA=U:VA=V:XA=X:YA=Y:ED=1:GA=G
650 GOSUB2040:Q0=Q
660 IFID$="Y"THEN700
670 IFQ0>8THENGOSUB1480:GOTO720
680 IFFA$="Y"ANDQ0>0THENGOSUB1480:GOTO720
690 IFFA$="Y"THEN720
700 IFAB$="Y"ANDL=2THENGOSUB1480:GOTO720
710 IFL=2THENGOSUB1480:GOTO720
720 IFQ0>R(0)THENR(0)=Q0:R(1)=X:R(2)=Y:R(3)=U:R(4)=V:GOTO750
730 IFQ0=R(0)ANDRND(0)>.5THENR(0)=Q0:R(1)=X:R(2)=Y:R(3)=U:R(4)=V:GOTO750
740 GOTO770 
750 IFID$="Y"THENPRINT"I AM CONSIDERING"STR$(R(1))","MID$(STR$(R(2)),2)" TO"STR$(R(3))","MID$(STR$(R(4)),2)
760 T1=T(1):T2=T(2):T3=T(3):T4=T(4)
770 Q0=0:RETURN
780 IFR(0)=-99THEN1420
790 CLS:PRINT"FROM"STR$(R(1))","MID$(STR$(R(2)),2)" TO"STR$(R(3))","MID$(STR$(R(4)),2):R(0)=-99
800 IFR(4)=1THENS(R(3),R(4))=-2:GOTO820
810 S(R(3),R(4))=S(R(1),R(2))
820 S(R(1),R(2))=0:IFABS(R(1)-R(3))<>2THEN910
830 S((R(1)+R(3))/2,(R(2)+R(4))/2)=0
840 X=R(3):Y=R(4):IFS(X,Y)=-1THENB=-2:FORA=-2TO2STEP4:GOSUB880
850 IFS(X,Y)=-2THENFORA=-2TO2STEP4:FORB=-2TO2STEP4:GOSUB880:NEXTB
860 NEXTA:IFR(0)<>-99THENPRINT" TO"STR$(R(3))","MID$(STR$(R(4)),2):R(0)=-99:GOTO800
870 GOTO910
880 U=X+A:V=Y+B:IFU<1ORU>8ORV<1ORV>8THENRETURN
890 IFS(U,V)=0ANDS(X+A/2,Y+B/2)>0THENID=0:GOSUB640
900 RETURN
910 PRINT:GOSUB2000:TD=TA:TF=TF+INT((TD-TB)/60*100+.00001)/100
920 P1=0:P2=0
930 FORY=9TO0STEP-1:FORX=0TO9
940 IFY=9AND(X=0ORX=9)THEN1030
950 IFY=0AND(X=0ORX=9)THEN1030
960 IFY=0ORY=9THENPRINTTAB(2*X-1)STR$(X);:GOTO1030
970 IFX=0THENPRINTMID$(STR$(Y),2,1);:GOTO1030
971 IFX=9THENPRINTSTR$(Y);:GOTO1030
980 IFS(X,Y)=0THENPRINTTAB(2*X)".";:GOTO1030
990 IFS(X,Y)=1THENPRINTTAB(2*X)C1$;:P1=P1+1:GOTO1030
1000 IFS(X,Y)=-1THENPRINTTAB(2*X)C2$;:P2=P2+1:GOTO1030
1010 IFS(X,Y)=-2THENPRINTTAB(2*X)C3$;:P2=P2+3:GOTO1030
1020 IFS(X,Y)=2THENPRINTTAB(2*X)C4$;:P1=P1+3
1030 NEXTX
1040 IFY=7THENPRINTTAB(21)"MOVE #"M;
1050 IFY=5THENPRINTTAB(21)"MY TIME:";
1051 IFY=4THENPRINTTAB(21)LEFT$(STR$(TF),9);
1060 IFY=3THENPRINTTAB(21)"YOUR TIME:";
1061 IFY=2THENPRINTTAB(21)LEFT$(STR$(TG),9);
1070 PRINT:NEXTY
1080 IFP1=0THEN1410
1090 IFP2=0THEN1420
1100 IFP1>P2THENP3=(P1-P2)*P1/P2
1110 IFP2>P1THENP3=(P1-P2)*P2/P1
1120 IFP1<7ORP2<7THENFORZZ=1TO11:PRINTCHR$(8);:NEXT:PRINT"end game"
1130 IFL=2ANDAB$<>"Y"ANDFA$<>"Y"THEN1150
1140 GOTO1170
1150 PRINT"YOUR BEST MOVE IS:";
1160 PRINTSTR$(T1)","MID$(STR$(T2),2)" TO"STR$(T3)","MID$(STR$(T4),2)
1170 GOSUB1390
1180 GOSUB2000:TE=TA:PRINT
1190 INPUT"FROM";E,H:E=INT(E):H=INT(H)
1200 GOSUB1390
1210 IFE<1ORE>8ORH<1ORH>8THENZ=2:GOSUB1440:GOTO1190
1220 IFS(E,H)<=0THENZ=2:GOSUB1440:GOTO1190
1230 INPUT" TO ";A,B:A=INT(A):B=INT(B)
1240 GOSUB1390
1250 IFA<0ORA>8ORB<0ORB>8THENZ=1:GOSUB1440:GOTO1230
1260 IFS(A,B)=0ANDABS(A-E)<=2ANDABS(A-E)=ABS(B-H)THEN1280
1270 Z=2:GOSUB1440:GOTO1190
1280 IFABS(A-E)=2ANDS((A+E)/2,(B+H)/2)>-1THENZ=2:GOSUB1440:GOTO1190
1290 S(A,B)=S(E,H):S(E,H)=0:IFABS(E-A)<>2THEN1350
1300 S((E+A)/2,(H+B)/2)=0
1310 INPUT"+TO ";A1,B1:GOSUB1390:IFA1<1THEN1350
1311 IFA1<0ORA1>8ORB1<0ORB1>8THENZ=1:GOSUB1440:GOTO1310
1320 IFS(A1,B1)<>0ORABS(A1-A)<>2ORABS(B1-B)<>2THENZ=1:GOSUB1440:GOTO1310
1330 E=A:H=B:A=A1:B=B1:IFB=8THENS(A,B)=2
1340 GOTO1290
1350 IFB=8THENS(A,B)=2
1360 GOSUB2000:TB=TA:TG=TG+INT((TB-TE)/60*100+.0001)/100
1370 ID=0:GOTO460
1380 REM BEEP-BEEP
1390 POKE8,0:SOUND100,1:SOUND200,1:POKE8,8
1400 RETURN
1410 PRINT:PRINT" I WIN !":FORZZ=1TO12:GOSUB1390:NEXT:END
1420 PRINT:PRINT" YOU WIN !":FORZZ=1TO12:GOSUB1390:NEXT:END
1430 REM REJECT MOVE
1440 POKE8,0:SOUND1,1:POKE8,8
1450 PRINT"ILLEGAL MOVE-- TRY AGAIN";:FORZZ=1TO2500:NEXT:FORZZ=1TO32*Z+24:PRINTCHR$(8);:NEXT
1460 RETURN
1470 REM SECOND LEVEL
1480 IFID$="Y"THENGOSUB2000:IFTA-TB>ST-5THENID=1:RETURN
1490 T(0)=-98:G1=1
1500 A7=S(X,Y):A8=S(U,V):S(U,V)=S(X,Y):S(X,Y)=0
1510 IFABS(X-U)=2THENA3=S((X+U)/2,(Y+V)/2):S((X+U)/2,(Y+V)/2)=0
1520 FORX1=1TO8:FORY1=1TO8:IFS(X1,Y1)<1THEN1550
1530 IFS(X1,Y1)=1THENB1=G1:FORA1=-1TO1STEP2:GOSUB1620:NEXT
1540 IFS(X1,Y1)=2THENFORB1=-1TO1STEP2:FORA1=-1TO1STEP2:GOSUB1620:NEXT:NEXT
1550 NEXTY1:NEXTX1
1560 S(X,Y)=A7:S(U,V)=A8
1570 IFABS(X-U)=2THENS((X+U)/2,(Y+V)/2)=A3
1580 Q0=Q0-T(0):IFFA$="Y"THENQ0=Q0+3
1590 IFID$="Y"THENQ0=Q0+3
1600 AB=0
1610 RETURN
1620 IFID=1ORAB=1THENRETURN
1630 U1=X1+A1:V1=Y1+B1:IFU1<1ORU1>8ORV1<1ORV1>8THENRETURN
1640 IFS(U1,V1)=0THENGOSUB1700:RETURN
1650 IFS(U1,V1)>0THENRETURN
1660 U1=U1+A1:V1=V1+B1:IFU1<1ORU1>8ORV1<1ORV1>8THENRETURN
1670 IFS(U1,V1)=0THENGOSUB1700
1680 RETURN
1690 REM SECOND MOVE GENERATOR
1700 UA=U1:VA=V1:XA=X1:YA=Y1:ED=8:GA=G1
1710 GOSUB2040
1720 Q1=Q
1730 IFID$<>"Y"ANDQ1>8THENGOSUB1800
1740 IFAB$="Y"ANDQ0-Q1<R(0)THENAB=1
1750 IFQ1>T(0)THENT(0)=Q1:T(1)=X1:T(2)=Y1:T(3)=U1:T(4)=V1
1760 IFQ1=T(0)ANDRND(0)>.5THENT(0)=Q1:T(1)=X1:T(2)=Y1:T(3)=U1:T(4)=V1
1770 Q1=0
1780 RETURN
1790 REM THIRD LEVEL
1800 IFID$="Y"THENGOSUB2000:IFTA-TB>ST-5THENID=1:RETURN
1810 A4=S(X1,Y1):A5=S(U1,V1):S(U1,V1)=S(X1,Y1):S(X1,Y1)=0
1820 IFABS(X1-U1)=2THENA6=S((X1+U1)/2,(Y1+V1)/2):S((X1+U1)/2,(Y1+V1)/2)=0
1830 FORX2=1TO8:FORY2=1TO8
1840 IFS(X2,Y2)>-1THEN1870
1850 IFS(X2,Y2)=-1THENB2=G:FORA2=-1TO1STEP2:GOSUB1920:NEXT
1860 IFS(X2,Y2)=-2THENFORB2=-1TO1STEP2:FORA2=-1TO1STEP2:GOSUB1920:NEXT:NEXT
1870 NEXTY2:NEXTX2
1880 S(X1,Y1)=A4:S(U1,V1)=A5
1890 IFABS(X1-U1)=2THENS((X1+U1)/2,(Y1+V1)/2)=A6
1900 RETURN
1910 REM THIRD MOVE GENERATOR
1920 U2=X2+A2:V2=Y2+B2:IFU2<1ORU2>8ORV2<1ORV2>8THENRETURN
1930 IFS(U2,V2)<=0THENRETURN
1940 U2=U2+A2:V2=V2+B2:IFU2<1ORU2>8ORV2<1ORV2>8THENRETURN
1950 IFS(U2,V2)=0THENGOSUB1970
1960 RETURN
1970 Q1=Q1+P3-9
1980 RETURN
1990 REM TIMER
2000 TT=TT+USR(1)/60:ZZ=USR(0):TA=INT(TT)
2020 RETURN
2030 REM EVALUATION
2040 Q=0:IFID$="Y"THENGOSUB2000:IFTA-TB>ST-5THENID=1:RETURN
2050 IFP1<7ORP2<7THENGOSUB2530
2060 CP=0:IFABS(YA-VA)=2THENQ=Q+10:CP=1:IFGA=-1THENQ=Q+20
2070 IFGA=-1ANDXA=3ANDYA=5THENQ=Q-.2
2080 IFGA=1ANDXA=6ANDYA=4THENQ=Q-.2
2090 FORC=-1TO1STEP2:IFUA+C<1ORUA+C>8ORVA+GA<1ORVA+GA>8THEN2250
2100 IFS(UA+C,VA+GA)=GAORS(UA+C,VA+GA)=2*GATHENQ=Q+1
2110 IFUA-C<1ORUA-C>8ORVA-GA<1ORVA-GA>8THEN2250
2120 IFS(XA,YA)=2*GATHEN2140
2130 GOTO2170
2140 IFS(UA+C,VA-C)=0ANDS(UA-C,VA+C)=0ANDS(UA-1,VA+C)=-2*GATHEN2160
2150 GOTO2170
2160 IFS(UA+1,VA-C)=-2*GATHENQ=Q+3
2170 IF(S(UA+C,VA+GA)=-GAORS(UA+C,VA+GA)=-2*GA)THEN2190
2180 GOTO2200
2190 IFS(UA-C,VA-GA)=0OR(UA-C=XAANDVA-GA=YA)THENQ=Q-3
2200 IFS(UA+C,VA-GA)=-2*GAAND(S(UA-C,VA+GA)=0OR(UA-C=XANDVA+GA=YA))THENQ=Q-3
2210 IFS(UA+C,VA+GA)=-2*GAAND(UA-C=XAANDVA-GA=YA)THENQ=Q-3
2220 IFUA+2*C<1ORUA+2*C>8ORVA+2*GA<1ORVA+2*GA>8THEN2250
2230 IFS(UA+C,VA+GA)=0ANDS(UA+2*C,VA+2*GA)=0ANDS(UA,VA+2*GA)=-GATHENQ=Q+.5
2240 IFS(UA+C,VA+GA)=0ANDS(UA+2*C,VA+2*GA)=0ANDS(UA,VA+2*GA)=-2*GATHENQ=Q+.5
2250 NEXTC
2260 IFS(XA,YA)=2*GATHENQ=Q+.2:GOTO2380
2270 REM MAN ONLY
2280 IFVA=EDTHENQ=Q+2
2290 IFCP=1THENFC=-2:FORFC=-2TO2STEP4:GOSUB2470:NEXT
2300 IFYA=ED-GA*2THENQ=Q+.2
2310 IFYA=ED-GATHENQ=Q+.3
2320 IFYA=ED-7*GATHENQ=Q-2.2
2330 IFXA=1ORXA=8THEN2360
2340 IFXA+1>8ORXA-1<1ORYA-GA<1ORYA-GA>8THEN2360
2350 IFS(XA+1,YA-GA)=2*GAORS(XA-1,YA-GA)=-2*GATHENQ=Q+2
2360 IFUA=1ORUA=8THENQ=Q+1:IFVA=EDTHENQ=Q-1.2
2370 RETURN
2380 REM KING ONLY
2390 IFCP=1THENFORFB=-2TO2STEP4:FORFC=-2TO2STEP4:GOSUB2470:NEXT:NEXT
2400 IFVA=5AND(UA=3ORUA=4ORUA=5)THENQ=Q+.25
2410 IFVA=4AND(UA=3ORUA=4ORUA=5)THENQ=Q+.25
2420 IFUA-1<1ORUA+1>8ORVA-GA<1ORVA-GA>8THEN2440
2430 IFS(UA-1,VA-GA)=-GAANDS(UA+1,VA-GA)=-GATHENQ=Q+1
2440 IFUA=1ORUA=8THENQ=Q-.5
2450 IFVA=1ORVA=8THENQ=Q-.5
2460 RETURN
2470 UC=UA+FB:VC=VA+FC
2480 IFUC<1ORUC>8ORVC<1ORVC>8THENRETURN
2490 IFS(UC,VC)=0ANDS((UC+UA)/2,(VC+VA)/2)=-GATHENQ=Q+10
2500 IFS(UC,VC)=0ANDS((UC+UA)/2,(VC+VA)/2)=-2*GATHENQ=Q+11
2510 RETURN
2520 REM ENDINGS
2530 IFGA=1THENRETURN
2540 IFP2<6ANDP1>P2THEN2610
2550 IFV=3ANDS(U,1)>0THENQ=Q+2
2560 IFS(X,Y)<>-2THEN2650
2570 IFU=3ANDS(1,V)>0THENQ=Q+2
2580 IFU=6ANDS(8,V)>0THENQ=Q+2
2590 IFV=6ANDS(U,8)>0THENQ=Q+2
2600 GOTO2650
2610 IF(U=7ANDV=1)OR(U=8ANDV=2)OR(U=1ANDV=7)OR(U=2ANDV=8)THENQ=Q+1.5
2620 IF8-V>V-1ANDU-1<X-1AND8-V<8-YTHENQ=Q+.3:GOTO2650
2630 IF8-U<X-1ANDV-1<Y-1THENQ=Q+.3
2640 IFU=1ORU=8ORV=1ORV=8THENQ=Q-.35
2650 IFS(X,Y)<>-1THEN2740
2660 FORI=VTO1STEP-1
2670 IFS(U,I)>0THEN2740
2680 IFU=8ANDS(U-1,I)>0THEN2740
2690 IFU+1>8THEN2740
2700 IFU=1ANDS(U+1,I)>0THEN2740
2710 IFS(U-1,I)>0ORS(U+1,I)>0THEN2740
2720 NEXTI
2730 Q=Q+1
2740 RETURN
3000 Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:RETURN
3001 READU$:FORY=1TOLEN(U$)STEP2:POKEZ,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATAHPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATAINCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
4000 REM JOHN WHITE
4010 REM YOUR COMPUTER
4020 REM MAGAZINE OCT 1981
4030 REM MC-10 JIM GERRIE
