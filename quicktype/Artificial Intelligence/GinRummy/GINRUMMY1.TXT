15 CLS0:CC=RND(-(PEEK(9)*256+PEEK(10))):PRINT@198," * GIN RUMMY 2.0 * ";:PRINT@230,"MANHATTAN SOFT 1980";
20 FORX=1TO2500:NEXT
30 CLS:CLEAR800:PRINT@204,"STARTING"
40 DIMC(55),SC(57),CH(12),PH(12),I(55),R(13),V(58),V$(13),C$(57),M(57),DC(15)
50 GOSUB2180:WD=2
60 CK=0:PK=0:YT=0:FC=0:RL=0:T=0:G1=0:IFW$="YOUR"THENWD=1:GOTO70
61 WD=2
70 CLS:CL=1:Z=1:U=1:GOSUB350  
80 CLS:M=11:N=0:PRINTTAB(3)"YOUR HAND":PRINT
90 FORX=1TO9:PRINTX" - "C$(PH(X)):NEXT:PRINT10"- "C$(PH(10))
100 IFWD=2THENPH(11)=UC:GOTO140 
110 IFWD=1THENCH(11)=UC:PRINT@332,"ARRANGING MY HAND";:GOTO520  
120 IFPK=1THEN940  
130 IFYT=0THEN260 
140 PRINT11"- "C$(PH(11)):PRINT@239,"0=DISCARD";:PRINT@271,"99=KNOCK";:PRINT@.5*837," ";
150 YT=1:WD=0:INPUT"MOVE WHICH CARD";M:IFM>11ANDM<>99THEN150
160 IFM>11ANDM<>99THEN150
170 IFM=99THENPK=1:FC=1:GOTO220 
180 IFM=0THEN220
190 S=PH(M):INPUT"TO WHICH LOCATION";N:IFN<1ORN>11THEN190
200 IFN>MTHENFORX=MTON-1:PH(X)=PH(X+1):NEXT:PH(N)=S:GOTO80 
210 FORX=MTON+1STEP-1:PH(X)=PH(X-1):NEXT:PH(N)=S:GOTO80 
220 P=351
230 PRINT@.5*P,"DISCARD";:INPUTD:IFD<1ORD>11THENP=P+64:GOTO230
240 IFPH(D)=UCANDZ<>1THEN245
241 GOTO250
245 P=670:GOSUB3550:PRINT@364,"CAN'T DISCARD UP-CARD";
246 PRINT@402,"YOU PICKED UP";:P=862:GOSUB3550:FORX=1TO4000:NEXT:GOTO80 
250 T=T+1:DC(T)=PH(D):UC=PH(D):WD=0:YT=0:FORX=DTO10:PH(X)=PH(X+1):NEXT:GOTO80 
260 PRINT@14,53-K"CARDS LEFT";
270 X=365:Y=18:GOSUB1470:PRINT@175,"UP CARD: "C$(UC);:WD=0:IFYT=0THEN440
280 PRINT@239,"tAKE UP CARD   ";:PRINT@271,"dRAW FROM DECK";
290 A$=INKEY$:IFA$=""THEN290
300 IFA$<>"T"ANDA$<>"D"THEN290
310 IFA$="T"THENPH(11)=UC:PU(U)=UC:U=U+1:GOTO80 
320 PH(11)=C(K):K=K+1:IFK=52THEN3520
321 GOTO80 
330 A$=INKEY$:IFA$=""THEN330
340 IFA$="Y"ORA$="N"THENRETURN
341 GOTO330 
350 PRINT@204,"SHUFFLING":FORI=1TO55:I(I)=0:NEXT:I(14)=1:I(28)=1:I(42)=1
360 FORI=1TO52
370 J=RND(55):IFI(J)<>0THEN370
380 C(I)=J:I(J)=1:NEXT
390 IFWD=2THENPRINT@204," I DEAL":GOTO400
391 PRINT@204,"YOU DEAL"
400 FORX=1TO500:NEXT
410 FORX=1TO10:PH(X)=C((X*2)-1):NEXT
420 FORX=1TO10:CH(X)=C(X*2):NEXT:UC=C(21):K=22:RETURN
430 DATAA,2,3,4,5,6,7,8,9,T,J,Q,K
440 PRINT@332,"CONSIDERING UP CARD";:IFZ=1THENCH(11)=57:GOSUB1030
450 GOTO780
460 FORA=1TO10:IFUC=CH(A)+1ORUC=CH(A)-1THEN480
470 NEXTA:GOTO920
480 FORB=1TO10
490 IFM(UC)=M(CH(B))THEN510
500 NEXTB:GOTO920
510 PRINT@332,"I TAKE THE UP CARD":CH(11)=UC
520 Z=Z+1:GOSUB1030 
530 FORX=1TO11:CH(X)=R(X):NEXT
540 IFTS=0THENPRINT@430,"11-CARD GIN!";:G1=1:GOTO970 
550 IFTS<21THENGOSUB1130 
560 L=RND(3):IFL<>2ORZ>3THEN590
570 FORD=1TO11:IFV(CH(D))=0ANDM(CH(D))>2THEN740
580 NEXTD
590 REM ERROR TRAP
610 RJ=0
620 FORY=13TO4STEP-1
630 FORD=11TO1STEP-1
640 IFV(CH(D))=0ANDM(CH(D))=YTHEN2350
650 NEXTD
660 NEXTY
670 FORY=13TO4STEP-1
680 FORD=11TO1STEP-1
690 IFV(CH(D))=1ANDM(CH(D))=YTHEN2350
700 NEXTD
710 NEXTY
720 RJ=1:GOTO620
730 IFD=0THENRJ=1:GOTO620
740 UC=CH(D):TS=TS-SC(UC)
750 FORX=DTO11:CH(X)=CH(X+1):NEXT
760 PRINT@364,"I DISCARD "C$(UC)
770 YT=1:PRINT@113,"YOUR TURN";:GOTO270 
780 FORA=1TO9
790 IFV(CH(A))=4THEN840
800 FORB=A+1TO10
810 IFV(CH(B))=4THEN830
820 IFM(UC)=M(CH(A))ANDM(UC)=M(CH(B))THEN510
830 NEXTB
840 NEXTA
850 FORA=1TO9
860 IFV(CH(A))=3ORV(CH(A+1))=3THEN890
870 IFCH(A)=CH(A+1)-1THENIFUC=CH(A)-1ORUC=CH(A)+2THEN510
880 IFCH(A)=CH(A+1)-2ANDUC=CH(A)+1THEN510
890 NEXTA
900 IFTS<21ANDSC(UC)<4THEN510
910 IFZ<4THEN460
920 PRINT@332,"I DRAW ":CH(11)=C(K):K=K+1:IFK=52THEN3520
930 PRINT@14,"   ";:PRINT@14,53-K;:GOTO520
940 PRINT@175,"KNOCK PTS";:INPUTPP
950 PRINT@192," 5  - "C$(PH(5));
960 IFPP>10THEN965
961 GOTO970
965 PRINT@239,"YOU CAN'T KNOCK";:PRINT@303,"WITH"STR$(PP)"-- RETRY";
966 FORX=1TO3000:NEXT:PH(11)=UC:PK=0:YT=1:GOTO80
970 P=161:PRINT@79,"WAIT WHILE";:PRINT@111,"I CHECK HANDS";:FC=1
980 AB=TS:FORX=1TO10:TH(X)=V(CH(X)):V(CH(X))=0:NEXT
990 FORX=1TO10:R(X)=PH(X):NEXT
1000 R(11)=57:GOSUB1040 
1010 IFZ=1THENCH(11)=57:GOSUB1030:FORX=1TO10:CH(X)=R(X):NEXT:AB=TS
1020 GOTO1210 
1030 FORX=1TO11:R(X)=CH(X):NEXT
1040 FORI=1TO10
1050 FORJ=I+1TO11
1060 IFR(I)<R(J)THEN1080
1070 A=R(I):R(I)=R(J):R(J)=A
1080 NEXTJ
1090 NEXTI
1100 IFZ=1THENFORX=1TO10:CH(X)=R(X):NEXT:RETURN
1110 GOTO2420 
1120 RETURN
1130 FORY=10TO1STEP-1
1140 FORD=1TO11
1150 IFSC(CH(D))=YANDV(CH(D))<3ANDTS-SC(CH(D))<11THENTS=TS-SC(CH(D)):GOTO1180 
1160 NEXTD,Y
1170 RETURN
1180 IFTS=0THENPRINT@432,"GIN!";:GOTO1200 
1190 PRINT@429,"I KNOCK WITH "TS;:CK=1:FC=1
1200 FORX=DTO10:CH(X)=CH(X+1):NEXT:GOTO970 
1210 FORX=1TO10:V(CH(X))=TH(X):NEXT
1220 CLS:PRINTTAB(4)"YOUR HAND":PRINT:FORX=1TO9:IFV(PH(X))>1THENPRINT"*";
1230 PRINTTAB(1)X" - "C$(PH(X)):NEXT
1240 IFV(PH(10))>1THENPRINT"*";
1250 PRINTTAB(1)10"- "C$(PH(10))
1260 PRINT"UNMATCHED"PS
1270 IFPK=1ANDPS>10THEN1275
1271 GOTO1280
1275 Z=1:PRINT@206,"YOU KNOCKED WITH";:PRINT@238,"MORE THAN 10 PTS";
1276 PRINT@270,"CHECK YOUR HAND";
1277 PRINT@302,"AND HIT enter";:INPUTA$:PK=0:YT=1:PH(11)=UC:FC=0:GOTO80
1280 PRINT@19,"MY HAND";:P=163:FORX=1TO10:IFV(CH(X))>1THENPRINT@.5*P,"*";
1290 PRINT@.5*P+3,C$(CH(X));:P=P+64:NEXT:TS=AB:PRINT@.5*P,"UNMATCHED"TS
1300 IFG1=1THENPRINT@.5*99,"*  "C$(CH(11));
1310 IFPS=0ORTS=0THENGOSUB1430:GOTO1350 
1320 IFCK=1THEN1700
1330 PRINT@417,"CHECKING LAYOFFS...";
1340 ONCLGOTO1480,1550,1590 
1350 IFPS>TSTHENNS=PS-TS:GOTO1360
1351 NS=TS-PS
1360 IFPK=1THENX=834:Y=60:GOSUB1470:PRINT@.5*848,"CHECK DONE";
1370 IFPK=1ANDPS=>ABTHENFORX=1TO2500:NEXT:PRINT@420,"I'VE UNDERCUT YOU -25 PTS";:NS=NS+25:W$="MY":GOTO1400 
1380 IFCK=1ANDAB=>PSTHENFORX=1TO2500:NEXT:PRINT@420,"YOU UNDERCUT ME -25 PTS";:NS=NS+25:W$="YOUR":GOTO1400 
1390 IFPS>TSTHENW$="MY":GOTO1400
1391 W$="YOUR"
1400 PRINT@455,W$" NET SCORE"NS;
1410 PRINT@485,"HIT enter FOR SCORES";:INPUTA$:GOTO1920 
1420 GOTO1690 
1430 REM GIN
1440 IFPS=0THENG$="YOU":NS=TS+25:GOTO1460 
1450 G$="I":NS=PS+25
1460 PRINT@420,G$": +25 POINT GIN BONUS";:GOTO1390 
1470 PRINT@.5*X,;:FORCC=1TOY*.5:PRINT" ";:NEXT:RETURN
1480 FORY=1TO10
1490 IFV(CH(Y))>1THEN1540
1500 FORA=1TO10
1510 IFV(PH(A))<4THEN1530
1520 IFCH(Y)+1=PH(A)ORCH(Y)-1=PH(A)THENV(CH(Y))=6:GOTO1670 
1530 NEXTA
1540 NEXTY
1550 FORY=1TO10
1560 IFV(CH(Y))>1THEN1580
1570 IFCH(Y)=CH(Y+1)-1ANDV(CH(Y+1))=6ORCH(Y)=CH(Y-1)+1ANDV(CH(Y-1))=6THENV(CH(Y))=6:CL=2:GOTO1670 
1580 NEXTY
1590 FORX=1TO10
1600 IFV(PH(X))<>3THEN1650
1610 FORY=1TO10
1620 IFV(CH(Y))>1THEN1640
1630 IFM(CH(Y))=M(PH(X))THENV(CH(Y))=6:CL=3:GOTO1670 
1640 NEXTY
1650 NEXTX
1660 CL=1:GOTO1350 
1670 REM PROGRAM LAYOFF
1680 AB=AB-SC(CH(Y)):PRINT@449,"I LAY OFF "C$(CH(Y))"-- NEW TOTAL:"AB;
1690 PRINT@486," ";:INPUT"HIT enter";A$:GOTO1220 
1700 REM PLAYER LAYOFF
1710 PRINT@420,"CLAIM A LAYOFF (Y/N)?":GOSUB330 
1720 IFA$="N"THEN1350 
1730 PRINT@417,"NUMBER OF CARD";:INPUTQ:IFV(PH(Q))>1THEN 1830 
1740 X=834:Y=55:GOSUB1470 
1750 PRINT@420,"LAYOFF ON mELD OR rUN?"
1760 A$=INKEY$:IFA$=""THEN1760
1770 IFA$<>"M"ANDA$<>"R"THEN1760
1780 IFA$="R"THEN1840
1790 FORX=1TO10
1800 IFV(CH(X))<>3THEN1820
1810 IFM(PH(Q))=M(CH(X))THENV(PH(Q))=3:GOTO1900
1820 NEXTX
1830 PRINT@450,C$(PH(Q))" DOESN'T LAY OFF";:GOTO1910
1840 FORX=1TO10:IFPH(Q)=PH(X)+1ORPH(Q)=PH(X)-1THENIFV(PH(X))=7THENV(PH(Q))=7:GOTO1900 
1850 NEXTX
1860 FORX=1TO10
1870 IFV(CH(X))<4THEN1890
1880 IFPH(Q)=CH(X)+1ORPH(Q)=CH(X)-1THENV(PH(Q))=7:GOTO1900
1890 NEXTX:GOTO1830
1900 PS=PS-SC(PH(Q)):PRINT@450,"LAY OFF "C$(PH(Q))"NEW TOTAL"PS;
1910 PRINT@487,"HIT enter";:INPUTA$:GOTO1220 
1920 REM ** SCORE SHEET **
1930 CLS:Y=16384:Z=170
1940 FORX=1TOU:PU(X)=0:NEXT
1950 PRINTTAB(20)"SCORE SHEET";
1960 PRINT@.5*107,"YOURS";:PRINT@.5*119,"MINE";
1980 PRINT@.5*799,"BOXES";
1990 PRINT@.5*863,"GAME ";
2000 PRINT@.5*927,"SCHNE";
2010 PRINT@.5*991,"TOTAL";
2020 PRINT@.5*192,W$" SCORE:"NS;:P1=173:P2=184
2030 IFW$="MY"THENF=F+1:CS(F)=CS(F-1)+NS:BF=BF+25:GOTO2050 
2040 E=E+1:HS(E)=HS(E-1)+NS:BE=BE+25
2050 IFE>0THENFORX=1TOE:PRINT@.5*P1,HS(X);:P1=P1+64:NEXT
2060 IFF>0THENFORX=1TOF:PRINT@.5*P2,CS(X);:P2=P2+64:NEXT
2070 IFHS(E)=>100THENGOSUB2100:GOTO2170 
2080 IFCS(F)=>100THENGOSUB2120:GOTO2170 
2090 GOTO2160
2100 PRINT@407,BE;:PRINT@438,100;:IFCS(F)=0THENSN=(HS(E)+BE+100):PRINT@470,SN;
2110 HT=HS(E)+BE+100+SN:PRINT@502,HT;:PRINT@412,BF;:PRINT@508,LEFT$(STR$(CS(F)+BF),3);:NH=HT-CS(F)-BF:GOSUB2140
2111 IFCS(F)+BF>99THENPOKE16384+511,ASC(RIGHT$(STR$(CS(F)+BF),1))+64
2112 RETURN
2120 PRINT@412,BF;:PRINT@443,100;:IFHS(E)=0THENSN=(CS(F)+BF+100):PRINT@475,SN;
2130 CT=CS(F)+BF+100+SN:PRINT@508,LEFT$(STR$(CT),3);:PRINT@407,BE;:PRINT@502,HS(E)+BE;:NC=CT-HS(E)-BE:GOSUB2140
2131 IFCT>99THENPOKE16384+511,ASC(RIGHT$(STR$(CT),1))+64
2132 RETURN
2140 PRINT@160,W$" GAME SCORE IS";:IFNH>0THENPRINTNH;:RETURN
2141 PRINTNC;:RETURN
2160 PRINT@192,"enter=NEXT HAND";:INPUTA$:GOTO60 
2170 PRINT@323,"enter=NEW GAME";:INPUTA$:GOTO30 
2180 FORX=1TO55:C(X)=X:NEXT
2190 FORX=1TO13:M(C(X))=X:M(C(X+14))=X:M(C(X+28))=X:M(C(X+42))=X:NEXT
2200 FORX=1TO10:SC(C(X))=X:NEXT
2210 FORX=15TO24:SC(C(X))=X-14:NEXT
2220 FORX=29TO38:SC(C(X))=X-28:NEXT
2230 FORX=43TO52:SC(C(X))=X-42:NEXT
2240 FORX=11TO13:SC(C(X))=10:SC(C(X+14))=10:SC(C(X+28))=10:SC(C(X+42))=10:NEXT
2250 RESTORE:FORI=1TO13:READV$(I):NEXT
2260 S1$="°C"
2270 S2$="¿D"
2280 S3$="¿H"
2290 S4$="°S"
2300 FORX=1TO13:C$(X)=V$(X)+S1$:NEXT
2310 FORX=15TO27:C$(X)=V$(X-14)+S2$:NEXT
2320 FORX=29TO41:C$(X)=V$(X-28)+S3$:NEXT
2330 FORX=43TO55:C$(X)=V$(X-42)+S4$:NEXT
2340 RETURN
2350 IFRJ=1THEN740
2360 FORX=1TOU
2370 IFM(CH(D))=M(PU(X))THEN2390
2380 NEXTX:GOTO740
2390 FORX=1TOT:IFM(DC(X))=M(CH(D))THEN740
2400 NEXTX
2410 IFV(CH(D))=0THEN650
2411 GOTO700
2420 FORX=1TO11:V(R(X))=0:NEXT
2430 RV=0:MV=0:LM=0:LR=0
2440 FORA=1TO9
2450 IFR(A)=R(A+1)-1ANDR(A)=R(A+2)-2THENV(R(A))=4:V(R(A+1))=5:V(R(A+2))=6:RV=RV+1
2460 NEXT
2470 IFFC=0THENPRINT@364,"I'M THINKING... ";
2480 FORA=1TO8
2490 IFR(A)=R(A+3)-3THENV(R(A))=5:V(R(A+1))=4:V(R(A+2))=5:V(R(A+3))=6:RV=RV-1:LR=1
2500 IFR(A)=R(A+4)-4THENV(R(A+1))=5:V(R(A+2))=4:V(R(A+3))=5:V(R(A+4))=6
2510 IFR(A)=R(A+5)-5THENV(R(A+3))=4:V(R(A+4))=5:V(R(A+5))=6
2520 NEXT
2530 FORA=1TO9
2540 IFV(R(A))=4THEN2620
2550 FORB=A+1TO10
2560 IFV(R(B))=4THEN2610
2570 FORC=B+1TO11
2580 IFV(R(C))=4THEN2600
2590 IFM(R(A))=M(R(B))ANDM(R(A))=M(R(C))THEN2595
2591 GOTO2600
2595 IFV(R(A))=3THENV(R(C))=3:LM=LM+1:GOTO2600
2596 V(R(A))=3:V(R(B))=3:V(R(C))=3:MV=MV+1
2600 NEXTC
2610 NEXTB
2620 NEXTA
2630 IFMV=1ANDRV=2ANDLM>0THEN3260 
2640 IFMV=1ANDRV=2THEN2950
2650 IFMV=2ANDRV=1ANDLM>0THEN3530 
2660 IFMV=2ANDRV=2THEN3150 
2670 FORX=1TO11:IFV(R(X))>3THENV(R(X))=0
2680 NEXTX
2690 IFMV=2ANDRV=1ANDLM=0THEN3240
2700 IFFC=1THENPRINT@141,"HERE ARE THE HANDS";
2710 IFRV=3THEN3490
2720 IFMV=1ANDRV=1THEN3240
2730 IFMV=2ANDRV=1THEN3050
2740 IFMV=0ANDRV=2THEN3240
2750 FORX=1TO9:IFV(R(X))=3ORV(R(X+1))=3ORV(R(X+2))=3THEN2770 
2760 IFR(X)=R(X+1)-1ANDR(X)=R(X+2)-2THENV(R(X))=4:V(R(X+1))=5:V(R(X+2))=5
2770 NEXTX
2780 IFFC=0THENPRINT@364,"JUST A SECOND...";
2790 IFFC=1THEN2910
2800 FORA=1TO10
2810 IFV(R(A))>1ORV(R(A+1))>1THEN2830
2820 IFR(A)=R(A+1)-1THENV(R(A))=1:V(R(A+1))=1
2830 NEXTA
2840 FORA=1TO10
2850 IFV(R(A))>1THEN2900
2860 FORB=A+1TO11
2870 IFV(R(B))>1THEN2890
2880 IFM(R(A))=M(R(B))THENV(R(A))=1:V(R(B))=1
2890 NEXTB
2900 NEXTA
2910 TS=0
2920 FORX=1TO11:IFV(R(X))>1THEN2940
2930 TS=TS+SC(R(X))
2940 NEXT:PS=TS:GOTO1120
2950 REMMV1/RV2
2960 IFLM>0THEN3380
2970 FORX=2TO11:IFV(R(X))=3ANDV(R(X)-1)>3THEN2990
2980 NEXTX:GOTO3030 
2990 FORY=X+1TO11:IFV(R(Y))=3ANDV(R(Y)-1)>3THEN3010
3000 NEXTY:GOTO3030
3010 FORX=1TO11:V(R(X))=0:NEXT
3020 GOTO2750
3030 FORX=1TO11:IFV(R(X))>3THENV(R(X))=0
3040 NEXTX:GOTO2750
3050 REM ** SPECIAL MV2/RV1 CHECKS **
3060 FORX=1TO8
3070 IFV(R(X))=3ANDV(R(X)+3)=3THEN3100
3080 NEXTX
3090 GOTO2750
3100 IFR(X)=R(X+1)-1ANDR(X)=R(X+2)-2THEN3110
3101 GOTO3080
3110 FORY=1TO11
3120 IFM(R(Y))=M(R(X))THENV(R(Y))=0
3130 NEXTY
3140 GOTO2750
3150 REM MV2/RV2
3160 IFLM>0THEN3420
3170 FORX=2TO11
3180 IFV(R(X))=3ANDV(R(X)+1)=3THEN3230
3190 NEXTX
3200 FORX=3TO11
3210 IFV(R(X))=3ANDR(X)=R(X-1)+1THEN3110
3220 NEXTX
3230 IFLR=0THEN2670
3231 GOTO3010 
3240 REM HIDDEN MELD
3250 IFLM=3THEN3380
3260 FORA=1TO9
3270 IFR(A)=R(A+1)-1ANDR(A)=R(A+2)-2THENIFV(R(A+1))=3ORV(R(A+2))=3THEN3290
3280 NEXTA:GOTO2750 
3290 FORB=1TO11
3300 IFM(R(B))=M(R(A))ANDR(B)<>R(A)THEN3320
3310 NEXTB:GOTO2750 
3320 FORC=1TO11
3330 IFM(R(C))=M(R(B))ANDR(C)<>R(B)ANDR(C)<>R(A)THENV(R(C))=3:V(R(B))=3:V(R(A))=3:MV=2:IFRV<>3THEN3350
3340 NEXTC:GOTO2750 
3350 IFLM>0THEN3360
3351 GOTO2720 
3360 FORX=(C+1)TOO111:IFM(R(X))=M(R(C))THENV(R(X))=3:GOTO2670 
3370 NEXTX:GOTO2670 
3380 REM LM IN MV1/RV2 & MV1/RV1
3390 FORX=2TO11
3400 IFV(R(X))=3ANDR(X)=R(X-1)+1AND(R(X)=R(X-2)+2ORR(X)=R(X+1)-1)THENV(R(X))=0:GOTO2750 
3410 NEXTX:GOTO2750 
3420 REM 2 LM IN MV2/RV2
3430 FORX=3TO11
3440 IFV(R(X))=0ANDR(X)=R(X-1)+1THENV(R(X-1))=0:GOTO2750 
3450 NEXTX
3460 FORX=3TO11
3470 IFV(R(X))=3ANDR(X)=R(X-1)+1ANDR(X)=R(X-2)+2THENV(R(X))=0:V(R(X-1))=0:GOTO2750 
3480 NEXTX:GOTO2750 
3490 REM RV3
3500 IFMV=2ANDLM>0THEN3260
3510 GOTO3010 
3520 PRINT@78,"ONLY 2 CARDS LEFT";:PRINT@111,"IN DECK-- DRAW";:FORX=1TOU:PU(X)=0:NEXT:FORX=1TO4000:NEXT:GOTO60 
3530 FORX=1TO9:IFV(R(X))=3ANDV(R(X+1))>3ANDV(R(X+2))>3THENV(R(X))=0:GOTO2670 
3540 NEXTX:GOTO2670 
3550 PRINT@.5*P,"---------------";:RETURN
4000 REM COPYRIGHT 1980
4010 REM MANHATTAN SOFTWARE INC.
4020 REM P.O.BOX 5200
4030 REM GRAND CENT. STA.
4040 REM NYC NY 10017
