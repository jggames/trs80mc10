0 REM COPYRIGHT (C) T&D SOFTWARE 1985
2 REM  *********************
4 REM  * FOUR IN A ROW     *
6 REM  *   BY:             *
8 REM  * HAROLD SNYDER     *
10 REM * P.O. BOX 3330     *
12 REM * CHEYENNE,  WY     *
14 REM *            82003  *
16 REM *********************
18 REM
20 CLEAR500:CLS(0):PRINT@192,"    **** FOUR IN A ROW ****":PRINT@256,"        BY HAROLD SNYDER":FORI=1TO999:NEXTI
22 E$="":FORZ1=1TO13:E$=E$+CHR$(128):NEXT:C$="                               ":D$=CHR$(128)+CHR$(128)
23 X$=CHR$(156):O$=CHR$(188):H$=CHR$(140):DIMB$(8,9),L(8),S(49),F(4),V(16),N(4):MC=15360
24 DATA1,100,500,7712,1,800,4000,7712
26 DATA1,75,900,7704,1,450,3000,7704
28 FORZ1=1TO16:READV(Z1):NEXTZ1:CLS:PRINT:PRINT"  DO YOU WANT INSTRUCTIONS Y/N"
30 A$=INKEY$:IFA$="N"THEN42
32 IFA$="Y"THEN36
34 GOTO30
36 CLS:PRINT:PRINT:PRINT"THIS GAME CONSISTS OF STACKING  MARKERS(THE COMPUTER PLAYS RED) ON THE BOARD UNTIL ONE OF THE"
37 PRINT"PLAYERS GETS FOUR IN A ROW.     THIS CAN BE DONE HORIZONTALLY,  VERTICALLY OR DIAGONALLY."
38 PRINT:PRINT:PRINT:PRINT"      HIT ANY KEY TO START"
40 A$=INKEY$:IFA$=""THEN40
42 CLS:FORI=1TO8:FORJ=1TO8:B$(I,J)=H$:NEXTJ:NEXTI:FORZ1=1TO8:L(Z1)=0:NEXTZ1:PRINT:PRINT"  DO YOU WANT TO GO FIRST Y/N"
44 A$=INKEY$:IFA$="N"THENM9=RND(2)+4:GOTO102
46 IFA$<>"Y"THEN44
48 CLS:GOSUB50:GOTO54
50 PRINT@0,"";:PRINT:PRINT:PRINTTAB(3):PRINTE$;E$:FORI=8TO1STEP-1:PRINTTAB(3):FORJ=1TO9:PRINTD$;B$(I,J);:NEXTJ:PRINT:NEXTI
51 GOSUB200:RETURN
54 PRINT@416,"        ** YOUR MOVE **"
56 M$=INKEY$:IFM$=""THEN56
58 SOUND200,1:M=VAL(M$):IFM<1ORM>8THENPRINT@416,"     ILLEGAL MOVE,TRY AGAIN":PRINT@448,C$:SOUND40,2:GOTO56
60 L=L(M):IFL>7THENPRINT@416,"     ILLEGAL MOVE,TRY AGAIN":PRINT@448,C$:SOUND40,2:GOTO56
62 L(M)=L+1:L=L+1:B$(L,M)=X$:GOSUB50:PRINT@416,"         ** THINKING **":PRINT@448,C$:P$=X$:GOSUB110:FORZ=1TO4
63 IFS(Z)<4THEN66
64 PRINT@416,"         ** YOU WIN **":PRINT@448,C$:GOTO134
66 NEXTZ:M9=0:V1=0:N1=1:FORM4=1TO8:L=L(M4)+1:IFL>8THEN98
68 V=1:P$=O$:W=0:M=M4
70 GOSUB110:FORZ1=1TO4:N(Z1)=0:NEXTZ1:FORZ=1TO4:S=S(Z):IFS-W>3THEN104
72 T=S+F(Z):IFT<4THEN76
74 V=V+4:N(S)=N(S)+1
76 NEXTZ:FORI=1TO4:N=N(I)-1:IFN=-1THEN80
78 I1=8*W+(4*SGN(N)+I):V=V+V(I1)+(N*V(8*W+1))
80 NEXTI:IFW=1THEN84
82 W=1:P$=X$:GOTO70
84 L=L+1:IFL>8THEN90
86 GOSUB110:FORZ=1TO4:IFS(Z)>3THENV=2
88 NEXTZ
90 IFV<V1 THEN98
92 IFV>V1 THENN1=1:GOTO96
94 N1=N1+1
96 V1=V:M9=M4
98 NEXTM4:IFM<>0THEN102
100 PRINT@416,"         ** TIE GAME **":GOTO134
102 M=M9
104 PRINT@448,"         MY MOVE WAS";M:L=L(M)+1:L(M)=L(M)+1:B$(L,M)=O$:P$=O$:GOSUB50:GOSUB110:FORZ=1TO4:IFS(Z)<4THEN108
106 PRINT@416,"      ** COMPUTER WINS **":GOTO134
108 NEXTZ:GOTO54
110 Q$=X$:IFP$=X$ THENQ$=O$
112 D2=1:D1=0:Z=0:GOSUB114:D1=1:D2=1:GOSUB114:D2=0:D1=1:GOSUB114:D2=-1:D1=1:GOSUB114:RETURN
114 D=1:S=1:T=0:Z=Z+1
116 C=0:FORK=1TO3:M5=M+(K*D1):L1=L+(K*D2):IFM5<1ORL1<1ORM5>8ORL1>8THEN128
118 B$=B$(L1,M5):IFC=0THEN124
120 IFB$=Q$ THENK=3:GOTO128
122 T=T+1:GOTO128
124 IFB$=P$ THENS=S+1:GOTO128
126 C=1:GOTO120
128 NEXTK:IFD=0THEN132
130 D=0:D1=-D1:D2=-D2:GOTO116
132 S(Z)=S:F(Z)=T:RETURN
134 END
200 PRINT"   ";:FORI=1TO2:PRINTE$;:SOUNDI*75+50,1:NEXTI
201 POKEMC+1379,32:POKEMC+1380,32:POKEMC+1381,49:POKEMC+1382,32:POKEMC+1383,32:POKEMC+1384,50:POKEMC+1385,32
204 POKEMC+1386,32:POKEMC+1387,51
205 POKEMC+1388,32:POKEMC+1389,32:POKEMC+1390,52:POKEMC+1391,32:POKEMC+1392,32:POKEMC+1393,53:POKEMC+1394,32
206 POKEMC+1395,32:POKEMC+1396,54:POKEMC+1397,32:POKEMC+1398,32:POKEMC+1399,55:POKEMC+1400,32:POKEMC+1401,32
207 POKEMC+1402,56:POKEMC+1403,32:POKEMC+1404,32:PRINT:RETURN
