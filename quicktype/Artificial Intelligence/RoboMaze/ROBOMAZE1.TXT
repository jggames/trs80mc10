0 CLEAR1000:DIMC1,C2,MC:MC=16384:GOTO20
3 PRINT@(C1-1)*32+C2-1,:RETURN
REM ************************
REM * LISTING 5.1          *
REM * ROBOT MAZE LEARNER   * 
REM ************************
20 SW=32:SH=15:REM WIDTH & HEIGHT
30 DIMM$(SH+1,SW+1),V(SH+1,SW+1),R((SW+1)*(SH+1),1)
40 CLS0:R$=CHR$(96):W$=CHR$(143+RND(6)*16):W=ASC(W$):BL$=CHR$(128):REM TRS-SPECIAL CHARS.
50 C1=1:FORI=3TOSW:C2=I:GOSUB3:PRINTW$;:NEXT
60 FORJ=2TOSH:C1=J:C2=SW:GOSUB3:PRINTW$;:NEXT 
70 FORI=SW-2TO1STEP-1:C1=SH:C2=I:GOSUB3:PRINTW$;:NEXT 
80 C2=1:FORJ=SH-1TO1STEP-1:C1=J:GOSUB3:PRINTW$;:NEXT
90 C1=RND(-(PEEK(9)*256+PEEK(10))):FORI=2TOSW-1:REM RANDOMIZE TIMER
100 FORJ=2TOSH-1
110 IFRND(0)<.25THENC1=J:C2=I:GOSUB3:PRINTW$;
120 NEXT:NEXT
130 FORI=2TO4:FORJ=2TO4:C1=I:C2=J:GOSUB3:PRINTBL$;:C1=SH-I+1:C2=SW-J+1:GOSUB3:PRINTBL$;:NEXT:NEXT
140 REM SEARCH MAZE USING SESNORS
150 PRINT@480,"EXPLORING...€€€€€€€€€€€€€€€€€€€";
155 I=1:J=2
160 IFI=SHORJ=SWTHEN280
170 GOSUB390:ME=ME+1:R(ME,0)=I:R(ME,1)=J:V(I,J)=V(I,J)+1:IFM$(I,J)=""THENGOSUB480
REM STORE ROUTE; HOW OFTEN VISIT EACH CELL?; USE SENSORS IF NOT THERE BEFORE
180 B$=M$(I,J)
190 IFV(I,J)=1THEN230
200 FORK=ME-1TO1STEP-1:IFR(K,0)=ITHENIFR(K,1)=JTHENME=K:K=1
REM IF YOU'VE BEEN HERE BEFORE FORGET THE ROUTE THAT BROUGHT YOU HERE.
210 NEXT
220 ONV(I,J)-INT((V(I,J)-1)/4)*4GOTO230,240,250,260
230 IFMID$(B$,1,1)="0"THENGOSUB430:I=I+1:GOTO160
240 IFMID$(B$,2,1)="0"THENGOSUB430:J=J+1:GOTO160
250 IFMID$(B$,3,1)="0"THENGOSUB430:I=I-1:GOTO160
260 IFMID$(B$,4,1)="0"THENGOSUB430:J=J-1
270 GOTO160
280 GOSUB390:ME=ME+1:R(ME,0)=I:R(ME,1)=J
290 PRINT@480,"GOT THERE!€€€€€€€€€€€€€€€€€€€€€";
300 REM RE-RUN MAZE
310 GOSUB430
320 FORK=1TOME
330 I=R(K,0):J=R(K,1) 
340 GOSUB390
350 FORQ=1TO25:X=X+1-1:NEXT:REM DELAY
360 GOSUB430 
370 NEXT:GOSUB390
380 PRINT@480,"HOW'S THAT!€€€€€€€€€€€€€€€€€€€€";:PRINT@0,;:GOTO600
390 REM POSITION ROBOT I,J
410 C1=I:C2=J:GOSUB3:PRINTR$;
420 RETURN
430 REM DELETE ROBOT
450 C1=I:C2=J:GOSUB3:PRINTBL$;
460 RETURN
REM USE SENSORS TO BUILD MAP
480 V=I:H=J
500 IFPEEK((V-1)*32+H-2+MC)=WTHENM$(I,J)="1":GOTO510
501 M$(I,J)="0"
510 IFME=1THENM$(I,J)="1"+M$(I,J):GOTO540
520 IFPEEK((V-2)*32+H-1+MC)=WTHENM$(I,J)="1"+M$(I,J):GOTO540
521 M$(I,J)="0"+M$(I,J)
540 IFPEEK((V-1)*32+H+MC)=WTHENM$(I,J)="1"+M$(I,J):GOTO560
541 M$(I,J)="0"+M$(I,J)
560 IFPEEK(V*32+H-1+MC)=WTHENM$(I,J)="1"+M$(I,J):RETURN
561 M$(I,J)="0"+M$(I,J)
580 RETURN
600 PRINT@10,"AGAIN? (Y/N)";:M$=INKEY$:IFM$="Y"THENRUN
610 IFM$<>"N"THEN600
620 END
630 REM R. FORSYTH & C. NAYLOR
640 REM HH GUIDE TO AI 1986
640 REM MC-10 EDITS JIM GERRIE
