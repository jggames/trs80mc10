0 CLEAR500:DIMX,ZZ,M$,X$:GOTO20
1 ZZ=1:X=32:FORX=XTOZZSTEP-1:X$=MID$(M$,X,1):IFX$<"!"THEN?MID$(M$,ZZ,X-ZZ):ZZ=X+1:X=ZZ+32:IFX$=""THENX=.
2 X=X+(X>255)*(X-255):NEXT:M$="":RETURN
9 FORZZ=1TO2500:NEXT:RETURN
10 REM PROGRAM TITLE: ALL TOGETHER
20 CLS
30 M$="all together IS A RACE GAME THAT PUTS YOU ON A WILD RACE COURSE. "
40 M$=M$+"JUST LIKE THE REAL THING YOU ARE ONLY VULNERABLE TO:":GOSUB1
60 M$="RUNNING OUT OF FUEL, BLOWN ENGINES, AND CRASHING.":GOSUB1
105 PRINT
110 M$="YOUR CAR IS RED, THE COMPUTER'S IS MIDDLE AND THE PACE CAR IS IN THE RIGHT LANE.":GOSUB1
120 PRINT
130 M$="FIRST TO COMPLETE THE COURSE IS THE WINNER.":GOSUB1
140 PRINT
150 INPUT"HOW MANY LAPS (DEFAULT=50)";N:IFN=0THENN=50
160 CLS:N=N-1
170 M$="THE RACE WILL BE MOVING FROM BOTTOM TO TOP OF SCREEN FOR"+STR$(N+1)+" LAPS.":GOSUB1
180 M$="w WILL BE THE ACCELERATOR (GAS PEDAL).":GOSUB1
190 M$="a TO MOVE THE CAR LEFT.":GOSUB1
200 M$="d TO MOVE THE CAR RIGHT.":GOSUB1
210 M$="s KEY TO STOP CAR.":GOSUB1
220 PRINT
225 M$="IF YOU WANT TO CHANGE LANES, LET OFF THE GAS PEDAL!!":GOSUB1
230 M$="YOU MUST REMEMBER ALL THE ABOVE IF YOU PLAN TO WIN.":GOSUB1:PRINT
240 INPUT"PRESS enter TO START RACE";X
250 CLS0:REM DRAW TRACK
255 V=32:A=489:B=300:C=498:N$="€€€"
256 YL=0:BL=0:CL=0:YF=(N+1)*5+5:BF=YF:CF=YF
257 FORX=0TO96STEP32:PRINT@X,"      ";:PRINT@X+24,"        ";:NEXT:PRINT@0,"FUEL ";:PRINT@24,"LAPS    ";
260 FORX=0TO31:SET(15,X,5):SET(45,X,5):NEXT
280 A$="ÂÊÂ":B$="€€€"
285 X=RND(-(PEEK(9)*256+PEEK(10)))
290 IFYF>.THENPRINT@A,"²º²";
300 IFBF>.THENPRINT@B,A$;
310 IFCF>.THENPRINT@C,A$;
320 FORZZ=1TO100:NEXT:PRINT@A,N$;:PRINT@C,N$;:IFBFTHENPRINT@B,N$;
325 IFBF>.THENIFB>6ANDB<23THENB=B+480:BL=BL+1:PRINT@88,"COMP"STR$(BL);:BF=BF-5:IFBF<1THENBF=.:GOTO330
330 IFC>6ANDC<23THENC=498:CL=CL+1:PRINT@120,"PACE"STR$(CL);:CF=CF-5:IFCF<1THENCF=.:GOTO335
335 IFBF<1THEN340
336 IFB>=103THENB=B-BS:GOTO340
337 B=B-V
340 IFC>=103THENC=C-CS:GOTO345
341 C=C-V
345 IFPEEK(2)THENIF1ANDNOTPEEK(16949)THENA=A+3
350 IFPEEK(2)THENIF1ANDNOTPEEK(16946)THENA=A-3
370 IFA>6THENIFA<23THENA=A+480:YL=YL+1:PRINT@56,"YOU "STR$(YL);:YF=YF-5:IFYF<1THENYF=.
400 IFB>.THENIFB<=7THENGOSUB701
410 IFB>=23THENIFB<=31THENGOSUB701
440 IFA>=.THENIFA<=7THEN700
450 IFA>=23THENIFA<=31THEN700
460 BB=RND(3):IFBB=1THENBS=64:GOTO465
461 IFBB=3THENBS=96:GOTO465
462 BS=V
465 IFRND(3)=1THENBS=SGN(((B/V)-INT(B/V))*V-((A/V)-INT(A/V))*V)*3
466 IFH=2THENBS=3
470 X=RND(3):IFX=2THENCS=64:GOTO480
471 IFX=1THENCS=96:GOTO480
472 CS=V
480 IFA>=103THENIFPEEK(2)THENIF4ANDNOTPEEK(16952)THENA=A-64:BE=RND(30)
485 IFBE=3THENIFYL=INT((N+1)/2)THEN780
520 PRINT@32,YF;:PRINT@64,BF;:PRINT@96,CF;
560 IFA=BORA=CTHEN700
590 IFYF<1THEN1100
600 IFYL>NTHEN1200
610 IFBF<1THEN1120
620 IFBL>NTHEN1220
640 IFCL>NTHEN1240
670 IFPEEK(2)THENIF4ANDNOTPEEK(16948)THEN290
680 A=A-V:GOTO290
700 PRINT@A,"ùùù";:GOSUB800:GOTO250
701 PRINT@B,"™™™";:BF=0:B=0:YF=YF-(RND(2)-1)*5:RETURN
780 PRINT@160,"ENGINE";:PRINT@192,"BLOWN!";:PRINT@224,"      ";:PRINT@256,"      ";
790 GOSUB9:CLS:GOTO860
800 GOSUB9:CLS
810 PRINT"SORRY..."
820 FORZZ=1TO1000:NEXT
830 M$="IN LAP"+STR$(YL)+" THERE WAS AN ACCIDENT...":GOSUB1
850 GOSUB9
860 PRINT"SHALL WE START THE RACE"
870 PRINT"AGAIN";
880 INPUTX$
890 IFLEFT$(X$,1)<>"N"THEN250
900 PRINT:PRINT"CHICKEN..."
910 PRINT"AREN'T YOU!"
920 END
1100 PRINT@A,N$;:PRINT@160,"YOU'RE";:PRINT@192,"OUT OF";:PRINT@224,"GAS!  ";:PRINT@256,"      ";
1110 GOSUB9:GOTO860
1120 PRINT@160,"COMP. ";:PRINT@192,"CAR IS";:PRINT@224,"OUT OF";:PRINT@256,"RACE! ";
1130 GOTO620
1160 FORZZ=1TO15:SOUND160,1:NEXT:CLS:SOUND114,1:RETURN
1200 GOSUB1160
1210 M$="THAT'S THE"+STR$(N+1)+" LAP RACE! YOU'VE WON!!":GOSUB1:GOTO850
1220 GOSUB1160
1225 M$="THAT'S THE RACE....THE COMPUTER CAR HAS WON!!":GOSUB1:GOTO850
1230 GOTO850
1240 GOSUB1160
1250 PRINT"WOULD YOU BELIEVE IT?!"
1260 M$="THE PACE CAR HAS WON THIS RACE!!":GOSUB1:GOTO850
1270 GOTO850
1280 REM DAVID CHANCE
1290 REM COMPUTER GRAPHICS 1981
1300 REM TAB BOOKS
1310 REM MC-10 EDITS JIM GERRIE
