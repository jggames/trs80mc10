0 GOTO100
16 C1=(PEEK(LC+32)<>96)+(PEEK(LC+1)<>96)+(PEEK(LC-32)<>96)+(PEEK(LC-1)<>96):RETURN:REM FNBX(LC)
17 C1=(LC<>2*INT(LC/2)):RETURN:REM FNVH(LC)
100 CLS7:PRINT@236, "d o t s";
110 PRINT@357,"SKILL LEVEL (0-10)";:INPUT SK:IFSK<0ORSK>10THEN110
120 CLS:SK=(10-SK)/10:TS=200-200*SK:DT=TS+25:SC=16384
130 PRINT:FOR I=1 TO 7:PRINT" O O O O O O O O O O":PRINT:NEXTI 
140 YS=0:CS=0:PRINT@87,"YOU:":PRINT@278,"COMPUTER:"
150 PRINT@152,YS:PRINT@344,CS
180 SL=SC+202:X=10:Y=7:CC=PEEK(SL) 
190 POKESL,128+16*(2-1)+15:F=0 
200 GOSUB910
210 X=X+J:Y=Y+K:IFX<1ORX>19ORY<2ORY>14THENX=X-J:Y=Y-K:GOTO200 
220 IFPC=99THEN250
230 POKESL,CC:SL=SL+PC:CC=PEEK(SL):POKESL,128+16*(2-1)+15
240 GOTO200
250 L=(PEEK(SL+1)=79)+(PEEK(SL-1)=79)+(PEEK(SL+32)=79)+(PEEK(SL-32)=79)
260 IFL+(CC=96)=-3THEN280 
270 I=62:GOSUB760:GOTO200 
280 WH=1:ML=SL:GOSUB700:LC=SL:GOSUB17:IFNOT(C1)THEN330
290 LC=SL-1:GOSUB16:IFX>1ANDC1=-4THENBX=SL-1:GOSUB770:F=-1
300 LC=SL+1:GOSUB16:IFX<19ANDC1=-4THENBX=SL+1:GOSUB770:GOTO180 
310 IFFTHEN180
320 GOTO360
330 LC=SL-32:GOSUB16:IFY>1ANDC1=-4THENBX=SL-32:GOSUB770:F=-1
340 LC=SL+32:GOSUB16:IFY<13ANDC1=-4THENBX=SL+32:GOSUB770:GOTO180 
350 IFFTHEN180
360 WH=2:F=0:CN=0:IFRND(0)<SKTHEN440
370 FORI=64TO384STEP64:FORJ=2TO12STEP2:K=SC+I+J
380 LC=K:GOSUB16:IFPEEK(K)=96ANDC1=-3THEN400
390 NEXTJ,I:GOTO440
400 I=K:IFPEEK(I-32)=96THENI=I-32:GOTO590
410 IFPEEK(I+32)=96THENI=I+32:GOTO590
420 IFPEEK(I-1)<>96THENI=I+1:GOTO650
430 IFPEEK(I+1)<>96THENI=I-1:GOTO650
440 I=RND(19)+RND(13)*32+33+SC:CN=CN+1:IFPEEK(I)<>96THEN440
450 IFNOT((PEEK(I+1)=79ANDPEEK(I-1)=79)OR(PEEK(I+32)=79ANDPEEK(I-32)=79))THEN440
460 IFSK>.6ORCN>TSTHEN520 
470 LC=I:GOSUB17:IFC1THEN500
480 LC=I-32:GOSUB16:IFC1=-2THEN440
481 LC=I+32:GOSUB16:IFC1=-2THEN440
490 GOTO590
500 LC=I-1:GOSUB16:IFC1=-2THEN440
501 LC=I+1:GOSUB16:IFC1=-2THEN440
510 GOTO650
520 LC=I:GOSUB17:IFC1THEN560
530 IFSK>.6ORCN>DTTHEN590
540 LC=I+32:GOSUB16:IFC1=-2THENLC=I-32:IFC1=-2THEN440
550 GOTO590
560 IFSK>.6ORCN>DTTHEN650
570 LC=I+1:GOSUB16:IFC1=-2THENLC=I-1:IFC1=-2THEN440
580 GOTO650
590 ML=I:GOSUB700
600 LC=ML-32:GOSUB16:IFC1=-4THENBX=ML-32:GOSUB770:F=-1
610 LC=ML+32:GOSUB16:IFC1=-4THENBX=ML+32:GOSUB770:GOTO360
620 IFFTHEN360 
630 GOTO180
640 IFNOT(PEEK(I-32)=79ANDPEEK(I+32)=79)THEN440
650 ML=I:GOSUB700
660 LC=ML-1:GOSUB16:IFC1=-4THENBX=ML-1:GOSUB770:F=-1
670 LC=ML+1:GOSUB16:IFC1=-4THENBX=ML+1:GOSUB770:GOTO360
680 IFFTHEN360 
690 GOTO180
700 REM
710 POKEML,109
720 LC=ML:GOSUB17:IFC1THENPOKE ML,73
730 I=185:IFWH=2THENI=150 
740 FORJ=1TOWH:GOSUB760 
750 FORL=1TO200:NEXT:NEXT 
760 SOUNDI,2:RETURN:REM NOISE 
770 YS=YS+1:J=8:I=200:IFWH=2THENJ=4:I=150:YS=YS-1:CS=CS+1
780 POKEBX,128+16*(5-1)+15
790 FORL=1TO3:POKEBX,128+16*(J-1)+15:GOSUB760:POKEBX,128+16*(J-1)+15:FORI=1TO200:NEXT:I=I+18:POKEBX,128+16*(J-1)+15:NEXT
800 PRINT@152,YS:PRINT@344,CS 
810 IFYS+CS<54THENRETURN 
820 IFYS>CSTHEN850
830 PRINT@487,"SORRY, YOU LOST."; 
840 SOUND128,10:FORI=1TO1500:NEXTI:GOTO870
850 PRINT@490,"YOU WIN!!!!!";
860 FORI=128TO255:SOUNDI,1:NEXT 
870 PRINT@487,"PLAY AGAIN (y/n) ?"; 
880 A$=INKEY$:IFA$=""THEN880 
890 IFLEFT$(A$,1)="Y"THEN100 
900 END
910 J=0:K=0:PC=0
920 A$=INKEY$:IFA$=""THEN920 
930 IFA$="W"THENPC=-32:K=-1:RETURN
940 IFA$="S"THENJ=1:PC=1:RETURN
950 IFA$="Z"THENK=1:PC=32:RETURN
960 IFA$="A"THENJ=-1:PC=-1:RETURN
970 IFASC(A$)=13THENPC=99 
980 RETURN
