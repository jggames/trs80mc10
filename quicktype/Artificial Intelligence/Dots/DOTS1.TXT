0 GOTO 100
1 PRINT@X+Y*32,;:RETURN : REM LOCATE Y,X
2 I$=INKEY$:RETURN
100 GOSUB 9000
110 GOSUB 8000
120 FOR Q=0 TO 1
130 ON Q + 1 GOSUB 1000,2000
140 GOSUB 3000
150 IF FJ THEN 7000
160 IF JA THEN 130
170 IF Q=1 THEN  FOR N=1 TO 100: NEXT N
180 NEXT Q
190 GOTO 120
1000 REM PLAYER MOVE
1010 Y=15:X=0:GOSUB 1: PRINT "<W/A/S/D> MOVE CURSOR, <ENTER>";
1020 Y=Y0+YC-1:X=X0+XC-1:GOSUB1
1030 GOSUB 2
1040 IF I$="W" THEN  IF YC > 1 THEN YC=YC - 1: GOTO 1020
1050 IF I$="A" THEN  IF XC > 1 THEN XC=XC - 1: GOTO 1020
1060 IF I$="S" THEN  IF YC < TY THEN YC=YC + 1: GOTO 1020
1070 IF I$="D" THEN  IF XC < TX THEN XC=XC + 1: GOTO 1020
1080 IFI$=CR$THENN=T(YC,XC):IFN=H0ORN=V0THENI=YC:J=XC:Y=15:X=0:GOSUB 1:PRINT "                               ";:RETURN
1090 IF I$ <> CC$ THEN  GOTO 1030
1100 STOP
2000 REM COMPUTER MOVE
2010 Y=10:X=23:GOSUB 1
2020 IF RND(0)< NH THEN 2200
2100 REM TAKE BLOCK
2110 FOR I=2 TO TY - 1 STEP 2: FOR J=2 TO TX - 1 STEP 2
2120 REM Y=Y0 + I - 1:X=X0 + J - 1:GOSUB 1
2130 IF T(I,J) <> L3 THEN  NEXT J,I: GOTO 2200
2140 IF T(I-1,J)=H0 THEN I=I - 1: RETURN
2150 IF T(I+1,J)=H0 THEN I=I + 1: RETURN
2160 IF T(I,J-1)=V0 THEN J=J - 1: RETURN
2170 J=J + 1: RETURN
2200 REM 
2210 QT=0
2220 I=INT(RND(0)*TY)+1:J=(INT(RND(0)*TX)ANDNOT1)+1+(IAND1)
2230 REM Y=Y0 + I - 1:X=X0+J-1:GOSUB 1
2240 QT=QT + 1
2250 N=T(I,J): IF N=H0 THEN DI=1:DJ=0: GOTO 2300
2260 IF N=V0 THEN DI=0:DJ=1:GOTO 2300
2270 GOTO 2220
2300 IF NH>.6 OR QT>I2 THEN RETURN
2310 REM CALCULATE RISK
2320 R= ABS (T(I - DI,J - DJ)=L2) +  ABS (T(I + DI,J + DJ)=L2)
2330 IF R=2 THEN 2220
2340 IF R=1 THEN  IF  NOT (QT>I1) THEN 2220
2350 RETURN
3000 REM COLOCA LINHA
3010 IF T(I,J)=H0 THEN N=H1:DI=1:DJ=0: GOTO 3030
3020 N=V1:DI=0:DJ=1
3030 T(I,J)=N
3040 Y=Y0+I-1:X=X0+J-1:GOSUB1: PRINT C$(N);BS$;
3100 REM CHECA SE FECHOU CASAS
3110 JA=NAO
3120 II=I - DI:JJ=J - DJ:GOSUB 3200
3130 II=I + DI:JJ=J + DJ:GOSUB 3200
3140 RETURN
3200 N=T(II,JJ): IF N=0 THEN  RETURN
3210 N=N + 1: IF N <> L4 THEN T(II,JJ)=N: RETURN
3220 T(II,JJ)=M(Q)
3230 Y=Y0 + II - 1:X=X0 + JJ - 1:GOSUB 1: PRINT C$(M(Q));BS$;
3240 REM 
3250 P(Q)=P(Q)+1:GOSUB 4000
3260 JA=SIM: REM GANHA JOGADA ADICIONAL
3270 FJ=(P(0)+P(1)=QC)
3280 RETURN
4000 REM 
4010 Y=4:X=26:GOSUB 1: PRINT P(0);
4020 Y=10:X=26:GOSUB 1: PRINT P(1);
4030 RETURN
7000 REM FIM DE JOGO
7010 IF P(0) > P(1) THEN 7100
7020 Y=15:X=5:GOSUB 1: PRINT "SORRY, YOU LOST.";
7030 REM DEFEAT SOUND EFFECT
7040 GOTO 7200
7100 Y=15:X=4:GOSUB 1: PRINT "CONGRATUALTIONS, YOU WON!";
7110 REM
7200 FOR N=1 TO 1500: NEXT N
7210 Y=15:X=0:GOSUB 1: PRINT  "                               ";
7220 Y=15:X=6:GOSUB 1: PRINT "PLAY AGAIN (Y/N)?";
7230 GOSUB 2
7240 IF I$="Y" THEN RUN
7250 IF I$="N" THEN END
7260 GOTO 7230
8000 REM APRESENTACAO
8010 CLS
8020 Y=7:X=10:GOSUB 1: PRINT "dots"
8030 Y=11:X=1:GOSUB 1: INPUT "SKILL LEVEL (0-10)";N
8040 IF N<0 OR N>10 THEN 8030
8050 NH=(10-N)/10
8060 I1=200-200*NH
8070 I2=I1+25
8500 REM SCREEN GAME
8510 CLS
8520 FOR I=1 TO TY: FOR J=1 TO TX
8530 Y=Y0+I-1:X=X0+J-1:GOSUB1:PRINT C$(T(I,J));
8540 NEXT J,I
8600 Y=2:X=24:GOSUB1:PRINT "YOU:";
8610 Y=4:X=25:GOSUB1:PRINT C$(M0);
8620 Y=8:X=23:GOSUB1:PRINT "MC-10:";
8630 Y=10:X=25:GOSUB1:PRINT C$(M1);
8620 GOSUB 4000
8999 RETURN
9000 REM INIT
9010 SIM=-1:NAO=0
9020 CX=10:CY=6
9030 QC=CX*CY
9040 TX=2*CX+1:TY=2*CY+1
9050 X0=1:Y0=1
9060 XC=1+CX:YC=1+CY
9070 DIM P(1)
9080 FJ=NAO
9100 DIM C$(11)
9110 PT=0:C$(PT)="*"
9110 H0=1:C$(H0)=" "
9120 H1=2:C$(H1)="-"
9130 V0=3:C$(V0)=" "
9140 V1=4:C$(V1)="!"
9150 L0=5:C$(L0)=" "
9160 L1=6
9170 L2=7
9180 L3=8
9190 L4=9
9190 M0=10:C$(M0)=CHR$(255)
9200 M1=11:C$(M1)=CHR$(191)
9210 DIM M(1):M(0)=M0:M(1)=M1
9300 DIM T(TY + 1,TX + 1)
9310 T(1,1)=PT:T(1,2)=H0
9320 T(2,1)=V0:T(2,2)=L0
9330 FOR I=1 TO 2: FOR J=3 TO TX
9340 T(I,J)=T(I,J - 2)
9350 NEXT J,I
9360 FOR I=3 TO TY: FOR J=1 TO TX
9370 T(I,J)=T(I-2,J)
9380 NEXT J,I
9400 CR$=CHR$ (13)
9410 CC$="Q"
9420 BS$=CHR$ (8): REM BACKSPACE
9900 REM AUXILIARES
9910 AT$ = CHR $ (27) + "=": REM ESCAPE FOR POSITION CURSOR
9920 CP = 910: REM ADDRESS OF curpos
9930 K0 = 283: REM ENDERECO DE KEY0
9999 RETURN
10000  REM "LIGA-PONTOS"
10001  REM EMERSON JOSE SILVEIRA DA COSTA (ENSJO)
10002  REM AGOSTO DE 2014
10003  REM TOTALMENTE REFORMULADO A PARTIR DE:
10004  REM "DOTS"
10005  REM ERIC K. EVANS
10005  REM PUBLICADO NA REVISTA COMPUTE!
10006  REM EDICAO 40, SETEMBRO DE 1983
10007  REM PARA TRS-80 COLOR COMPUTER
