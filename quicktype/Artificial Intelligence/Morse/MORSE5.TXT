0 CLS:GOSUB9000:POKE36397,2:POKE36398,128:POKE16925,0:POKE16926,1:V=.15:REM V=ADJUSTMENT FOR COMPUTER
10 CLS:DIMM$(43),D(43),Z$(64),V(15),W(15):ZZ=RND(-(PEEK(9)*256+PEEK(10)))
12 B$="ETINAMSDRGUKWOHBLZFCP?VX?Q?YJ?56?7???8???????94???????3???2?10??"
14 FORI=1TO64:Z$(I)=MID$(B$,I,1):NEXT
20 POKE8,8
40 FORI=1TO43:READM$(I):NEXT:POKE8,0
50 FORI=1TO43:D(I)=1:NEXTI
100 CLS
110 PRINTTAB(11)"MORSE TUTOR":PRINTTAB(11)"-----------":PRINT
120 PRINT"THESE ARE YOUR OPTIONS:"
130 PRINT:PRINT"COMPUTER GENERATED MORSE":PRINT"------------------------"
150 PRINT"1. LEARNING THE CODE (1 LETTER)"
160 PRINT"2. IMPROVING SPEED"
170 PRINT"3. MORSE TEST SIMULATOR (RANDOM)"
180 PRINT"COMPUTER ANALYSED MORSE":PRINT"-----------------------"
190 PRINT"4. LEARNING TO TRANSMIT":PRINT"ENTER NUMBER OF SELECTION"
220 A$=INKEY$:IFA$=""THEN220
230 ONABS(ASC(A$)-48)GOSUB1000,2000,3000,4000
240 GOTO100
300 REM SUBROUTINE TO GENERATE MORSE
310 FORJ=1TOLEN(M$(C))
320 IFMID$(M$(C),J,1)="S"THENT=DL
330 IFMID$(M$(C),J,1)="L"THENT=3*DL
340 GOSUB410
350 FOR K=1 TO DL:NEXT K
360 NEXTJ
370 FOR K=1 TO CS:NEXT K
380 RETURN
400 REM TONE GENERATION
410 POKE8,8
420 FOR K=1 TO T:NEXT K
430 POKE8,0
440 RETURN
1000 REM SINGLE LETTERS
1005 CLS
1010 PRINT"ENTER REQUIRED SPEED"
1011 INPUT"(NUMBER OF 5 LETTER WORDS/MIN)  ";S:IFS=0THENS=12
1015 PRINT"PRESS ANY LETTER OR NUMBER.","(*=EXIT)"
1020 GR=INT(EXP(5.28-V*5))
1022 IC=3.10470588*GR
1024 DL=INT(IC-GR*LOG(S))
1025 CS=2*DL
1030 A$=INKEY$:IFA$=""THEN1030
1031 IF(A$<"0"ORA$>"Z")ANDA$<>" "ANDA$<>"*"THEN1030
1035 IFA$="*"THENRETURN
1040 PRINTA$;
1050 IFA$=" "THENFORK=ITODL*4:NEXT:GOTO1030
1060 C=ASC(A$)-47
1070 GOSUB 310
1080 GOTO 1030
2000 REM IMPROVING SPEED
2005 CLS
2006 SY=1
2007 INPUT"RANDOM WORDS OR NUMBERS W/N";A$
2008 IFLEFT$(A$,1)="N"THENSY=0
2009 L5=10+SY*16:DT=L5
2010 PRINT"ENTER REQUIRED SPEED"
2012 INPUT"(NUMBER OF 5 LETTER WORDS/MIN)  ";S:IFS=0THENS=12
2014 PRINT"HOW MUCH TIME DO YOU WANT TO":PRINT"REPLY? 0 GIVES LEAST TIME,"
2015 INPUT"HIGHER NUMBERS MORE TIME";MAX:MAX=MAX*50
2018 PRINT"(*=EXIT, #=LETER PROBABILITY)","PRESS ANY KEY TO START PROGRAM."
2020 GR=INT(EXP(5.28-V*5))
2022 IC=3.10470588*GR
2024 DL=INT(IC-GR*LOG(S))
2025 CS=2*DL
2026 A$=INKEY$:IFA$=""THEN2026
2027 IFA$="*"THENRETURN
2028 FORK=1TOCS:NEXTK
2270 DT=L5/DT:FORK=SY*17+1TOL5+SY*17:D(K)=D(K)*DT:NEXTK:DT=L5
2280 K=RND(0)*L5:C1=0:C=0+SY*17
2282 C=C+1
2284 C1=C1+D(C)
2286 IF C1=<K THEN2282
2290 DU=0:GOSUB310
2300 A$=INKEY$:IFA$=""THENDU=DU+1:GOTO2300
2310 IFA$="*"THENRETURN
2312 IFA$<>"#"THEN2320
2314 FORI=SY*17+1TOL5+SY*17STEP2
2316 PRINTCHR$(47+I);D(I)/L5;TAB(15);CHR$(47+I+1);D(I+1)/L5
2318 NEXT
2319 GOTO 2300
2320 IFDU<=MAXTHENDT=DT-D(C)/4:D(C)=D(C)-D(C)/4:GOTO2350
2340 IF DU>MAXTHENPRINT"TOO SLOW  ";
2350 PRINTCHR$(C+47);:IFA$=CHR$(C+47)THENPRINT"  CORRECT":GOTO2270
2400 DT=DT+D(C)*2:D(C)=D(C)+D(C)*2:PRINT:GOTO 2290
2410 GOSUB 310
2420 A$=INKEY$:IFA$=""THEN 2420
2430 IF A$=CHR$(C+47)THEN 2300
2440 GOTO 2410
3000 REM RANDOM WORDS
3005 CLS:SY=1:INPUT"RANDOM WORDS OR NUMS: W/N";A$:IFLEFT$(A$,1)="N"THEN SY=0
3010 INPUT"NUMBER OF LETTERS, PER WORD";NL:IFNL>20THENNL=5
3020 INPUT"ENTER SPEED TO WHICH YOU ASPIRE ";S:IFS=0THENS=12
3030 GR=INT(EXP(5.28-V*NL)):IC=3.01470588*GR:DL=INT(IC-GR*LOG(S))
3040 PRINT"HOW MUCH DO YOU WANT TO SLOW":PRINT"DOWN THE INTER LETTER INTERVAL?"
3045 PRINT"ENTER 0 IF YOU DON'T WANT A":PRINT"CHANGE"
3046 PRINT"ENTER A NUMBER GREATER THAN 0":PRINT"TO SLOW IT DOWN PROPORTIONATELY"
3047 INPUTSL
3050 CS=2*DL+100*SL
3100 FORI=1TONL
3120 C=INT(RND(0)*(10+SY*16)+1)+SY*17
3140 PRINTCHR$(C+47);
3160 GOSUB 310
3180 NEXTI
3200 FORK=1TO2*CS:NEXTK
3220 PRINT
3240 A$=INKEY$:IFA$="*"THENRETURN
3250 GOTO 3100
4000 REM DECODE MORSE
4005 CLS:E=0
4040 PRINT"WOULD YOU LIKE THE MACHINE TO":INPUT"ESTIMATE YOUR DIT LENGTH (Y/N)";A$
4050 IF A$="N"THENINPUT"LENGTH OF DIT";DIT:GOTO4250
4060 PRINT"TAP IN V, AT LEAST 3 TIMES":A$=INKEY$
4070 ZZ=PEEK(2)ANDPEEK(17023):IFZZ=0THEN4070
4080 FORI=1TO12:SI=0
4090 IFE=0THENPOKE8,8:E=1
4130 ZZ=PEEK(2)ANDPEEK(17023):IFZZ>0THENSI=SI+1:GOTO4130
4141 IFZZ=42THENRETURN
4140 POKE8,0:E=0
4150 V(I)=SI
4160 NS=0
4170 IFI=12THEN4210
4180 ZZ=PEEK(2)ANDPEEK(17023):IFZZ=0THENNS=NS+1:GOTO4180
4181 IFZZ=42THENRETURN
4190 W(I)=NS
4200 NEXT
4210 DIT=V(4)+V(8)+V(12)
4220 DIT=INT((DIT/3)/2)+1
4240 PRINT"YOUR DIT LENGTH";DIT
4250 DL=40:E=0
4260 C=28:GOSUB310
4300 NS=0
4310 I=0
4320 ZZ=PEEK(2)ANDPEEK(17023):IFZZ=0THENNS=NS+1:GOTO4320
4321 IFZZ=42THENRETURN
4330 IFNS>4*DIT THENPRINT" "
4340 T1=0:I2=1
4350 FORI=1TO5
4360 I2=I2*2
4370 IFE=0THENPOKE8,8:E=1
4380 SI=0
4390 ZZ=PEEK(2)ANDPEEK(17023):IFZZ>0THENSI=SI+1:GOTO4390
4400 POKE8,0:E=0
4410 IFSI>DIT THENT1=T1+I2:GOTO4430
4420 T1=T1+I2/2
4430 NS=0
4440 ZZ=PEEK(2)ANDPEEK(17023):IFZZ>0THEN4470
4450 NS=NS+1:IFNS>DIT THENPRINTZ$(T1);:GOTO4300
4460 IFZZ<>42THEN4440
4465 I=5
4470 NEXTI
4480 IFZZ<>42THEN4300
4490 RETURN

6000 DATA204,8,0,253,142,45,134,126,183,66,6,206,142,23,255,66
6010 DATA7,14,134,0,151,8,57,182,142,44,136,128,183,142,44,183
6020 DATA191,255,252,142,45,211,9,149,8,221,11,59,0,0,0,-1

7000 DATA LLLLL,SLLLL,SSLLL,SSSLL,SSSSL,SSSSS
7010 DATA LSSSS,LLSSS,LLLSS,LLLLS
7020 DATA SLSLSL,LLSSL,SSSLS,LLSLLSL
7030 DATA LSLSLS,LLLSSS
7040 DATA SSSLLL
7070 DATA SL,LSSS,LSLS,LSS,S
7080 DATA SSLS,LLS,SSSS,SS,SLLL
7090 DATA LSL,SLSS,LL,LS,LLL
7100 DATA SLLS,LLSL,SLS,SSS,L
7110 DATA SSL,SSSL,SLL,LSSL,LSLL,LLSS

9000 A=36352:I=0
9005 READV:IFV=-1THENEXECA:RETURN
9010 POKEA+I,V:I=I+1:GOTO9005

9997 REM CHRIS DRACUP & 
9998 REM DEREK WAKELIN
9999 REM SOUND GREG HAMILTON
10000 REM JIM G
