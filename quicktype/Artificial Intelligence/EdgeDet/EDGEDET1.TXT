100 REM INTIALIZATION
105 CLS:PRINTTAB(7)"PHIL KEGELMEYER'S":PRINTTAB(5)"EDGE DETECTION PROGRAM":DIMI,J,O:O=11
500 IE=20
510 JE=20
520 DIM I(IE,JE),T(IE,JE)
530 DIM C(9)
540 GOSUB3000
550 GOSUB6080:GOSUB6070
551 PRINT@0,"1-VERTICAL EDGE DETECTION MASK":PRINT"2-A CORNER DETECTOR":PRINT"3-A GENERAL EDGE DETECTION MASK"
554 INPUT"CHOICE";CC:IFCC<0ORCC>3THEN551
555 ONCCGOSUB2000,2100,2200
580 GOSUB4000
590 INPUT"SHOW PROCESSED IMAGE (Y/N)";M$:IFLEFT$(M$,1)="Y"THENGOSUB1000
600 INPUT"ALTER TEST IMAGE (Y/N)";M$:IFLEFT$(M$,1)="Y"THENGOSUB7000:CLS:GOTO550
700 INPUT"RESTART PROGRAM (Y/N)";M$:IFLEFT$(M$,1)="Y"THENCLS:GOTO550
800 END
1000 REM PERFORM CONVOLUTION AND DISPLAY RESULT
1010 FORJ=2TOJE-1
1020 FORI=2TOIE-1
1030 RE=I(I-1,J-1)*C(1)+I(I,J-1)*C(2)+I(I+1,J-1)*C(3)
1040 RE=RE+I(I-1,J)*C(4)+I(I,J)*C(5)+I(I+1,J)*C(6)
1050 RE=RE+I(I-1,J+1)*C(7)+I(I,J+1)*C(8)
1060 RE=RE+I(I+1,J+1)*C(9)
1070 RE=ABS(RE)
1080 IFRE>=TH THENSET(I-1,O+J,5)
1090 IFRE<TH THENRESET(I-1,O+J)
1100 NEXTI
1110 NEXTJ
1120 RETURN
2000 REM A VERTICAL EDGE DETECTION MASK
2001 C(1)=-1
2002 C(2)=2
2003 C(3)=-1
2004 C(4)=-1
2005 C(5)=2
2006 C(6)=-1
2007 C(7)=-1
2008 C(8)=2
2009 C(9)=-1
2010 TH=3
2015 GOTO5000
2020 REM
2100 REM A CORNER DETECTOR
2101 C(1)=1
2102 C(2)=-2
2103 C(3)=1
2104 C(4)=-2
2105 C(5)=4
2106 C(6)=-2
2107 C(7)=1
2108 C(8)=-2
2109 C(9)=1
2110 TH=1
2115 GOTO5000
2200 REM A GENERAL EDGE DETECTION MASK
2201 C(1)=-1
2202 C(2)=-1
2203 C(3)=-1
2204 C(4)=-1
2205 C(5)=8
2206 C(6)=-1
2207 C(7)=-1
2208 C(8)=-1
2209 C(9)=-1
2210 TH=3
2215 GOTO5000
2220 REM
3000 REM A TEST IMAGE
3002 GOSUB6000
3004 REM MAKE A SQUARE
3005 FORJ=8TO12
3010 FORI=8TO12
3015 T(I,J)=1
3020 NEXTI
3025 NEXTJ
3026 REM MAKE A VERTICAL LINE
3030 FORJ=6TO14
3035 T(4,J)=1
3040 NEXTJ
3042 REM MAKE A HORIZONTAL LINE
3045 FORI=6TO14
3050 T(I,4)=1
3055 NEXTI
3057 REM MAKE A SINGLE POINT
3060 T(15,15)=1
3065 RETURN
3066 REM
4000 REM PRINT THE TEST IMAGE
4010 REM CLS0
4020 FORJ=1TOJE
4030 FORI=1TOIE
4040 IFI(I,J)=1THENSET(I-1,O+J,5)
4050 NEXTI
4060 NEXTJ
4070 RETURN
5000 REM PRINT CURRENT MASK
5010 PRINT@0,;
5020 PRINTTAB(5);C(1);TAB(10);C(2);TAB(15);C(3)
5030 PRINTTAB(5);C(4);TAB(10);C(5);TAB(15);C(6)
5040 PRINTTAB(5);C(7);TAB(10);C(8);TAB(15);C(9)
5050 RETURN
5060 REM
6000 REM ZERO OUT THE TEST IMAGE
6010 FORJ=1TOJE:FORI=1TOIE:I(I,J)=.:NEXT:NEXT:RETURN
6020 FORJ=1TOJE:FORI=1TOIE:RESET(I-1,O+J):NEXT:NEXT:RETURN
6070 FORJ=1TOJE:FORI=1TOIE:RESET(I-1,O+J):IFI(I,J)THENSET(I-1,O+J,5)
6071 NEXT:NEXT:RETURN
6080 FORJ=1TOJE:FORI=1TOIE:I(I,J)=T(I,J):NEXT:NEXT:RETURN
6090 FORJ=1TOJE:FORI=1TOIE:T(I,J)=.:NEXT:NEXT:RETURN
7000 CLS:GOSUB6090:GOSUB6010:GOSUB6070:J=1:I=1
7010 SET(I-1,O+J,5):M$=INKEY$:RESET(I-1,O+J):IFM$=""THEN7010
7020 IFT(I,J)=0THENRESET(I-1,O+J)
7030 IFT(I,J)=1THENSET(I-1,O+J,5)
7031 IFM$="S"ANDI<20THENI=I+1
7032 IFM$="A"ANDI>1THENI=I-1
7033 IFM$="Z"ANDJ<20THENJ=J+1
7034 IFM$="W"ANDJ>1THENJ=J-1
7035 IFM$=" "THENT(I,J)=1:SET(I-1,O+J,5)
7036 IFM$="B"THENT(I,J)=0:RESET(I-1,O+J)
7037 IFM$="Q"THENRETURN
7038 GOTO7010
7050 REM MC-10 EDITS JIM GERRIE
