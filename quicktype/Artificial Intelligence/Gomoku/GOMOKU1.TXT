0 CLS:GOSUB200:GOTO6
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

5 PRINT@480,"                               ";:POKE49151+511,96:PRINT@480,;:RETURN

6 CLEAR100:T=0:I=T:D=T:H=T:J=T:K=T:B1=T:E=T:W=T:F=T:B=T:RL=9:RH=1:CL=9:CH=1:V=16384:R=5:C=5:P=544:L$=CHR$(159)
7 DIMF(4),G(4),B(81),V(81)

8 F(0)=4:F(1)=12:F(2)=30:F(3)=90:F(4)=484:G(0)=5:G(1)=15:G(2)=35:G(3)=100:G(4)=485:FORI=1TO81:B(I)=0:NEXT:GOSUB52
9 CLS:GOSUB48:IFM=0THENB(41)=2:PRINT@448,"I MOVE TO: 5,5";:S$="C":RL=R:RH=R:CL=C:CH=C:GOSUB50
10 SOUND96,1:PRINT@416,"YOUR MOVE:":PRINT@480,"(USE =ASWZ= KEYS- THEN =ENTER=)";:GOSUB74:T=R*9-9+C:IFB(T)THEN10
12 GOSUB58:S$="Y":GOSUB50:B(T)=1:IFB(41)=0THENH=41:GOTO44
13 FORI=1TO81:V(I)=0:NEXT:PRINT@448,"":PRINT@448,"THINKING...";:FORR=RLTORH:FORC=CLTOCH:Q=70+32*R+2*C
14 U=PEEK(V+Q):PRINT@Q,L$;:I=9*R-9+C:D=0:IFC>2ANDC<8THEND=1:H=1:GOSUB20
15 IFR>2ANDR<8THEND=D+1:H=9:GOSUB20
16 IFD>1THENH=10:GOSUB20:H=8:GOSUB20
18 POKEV+Q,U:NEXT:NEXT:GOTO40
20 N(0)=0:N(1)=0:N(2)=0:FORJ=-2TO2:K=B(I+J*H):N(K)=N(K)+1:NEXT:IFN(1)=0THEN24
22 IFN(2)THENRETURN
24 IFN(1)>4THENPRINT@Q,CHR$(U);:PRINT@448,"":PRINT@448,"YOU WIN!":GOSUB5:GOTO92
26 IFN(2)=4THENB1=1
28 IFN(1)=0THENE=G(N(2)):GOTO30
29 E=F(N(1))
30 T=I-H-H:IFB(T)=0THENV(T)=V(T)+E
32 T=I-H:IFB(T)=0THENV(T)=V(T)+E
34 IFB(I)=0THENV(I)=V(I)+E
36 T=I+H:IFB(T)=0THENV(T)=V(T)+E
38 T=T+H:IFB(T)=0THENV(T)=V(T)+E
39 RETURN
40 H=1:W=V(1):FORJ=2TO81:IFW<=V(J)THENH=J:W=V(J)
42 NEXT:IFW<4THENPRINT@416,"GAME IS DRAWN":PRINT@448,"":PRINT@448,;:GOTO92
44 B(H)=2:R=INT((H-1)/9+1):C=H-9*R+9:PRINT@448,"I MOVE TO:";:GOSUB90:S$="C":GOSUB50
46 GOSUB58:IFB1THENPRINT@416,"":PRINT@416,"I WIN!":PRINT@448,"":PRINT@448,;:GOTO92
47 GOTO10
48 PRINT@9,"* G O M O K U *":PRINT@72,"1 2 3 4 5 6 7 8 9":FORZ=1TO9:PRINTTAB(5)Z"+ + + + + + + + +"Z:NEXT
49 PRINTTAB(8)"1 2 3 4 5 6 7 8 9":RETURN
50 PRINT@70+32*R+2*C,S$;:RETURN
52 PRINT"DO YOU WISH TO MOVE FIRST?";
54 S$=INKEY$:ON-(S$="")GOTO54:M=1:ON-(S$="Y")GOTO56:M=0:ON-(S$="N")GOTO56:GOTO54
56 RETURN
58 IFR>RHTHENRH=R
60 IFR<RLTHENRL=R
62 IFC>CHTHENCH=C
64 IFC<CLTHENCL=C
66 IFRH<3THENRH=3
68 IFRL>7THENRL=7
70 IFRH=RLTHENRH=RH+1
72 RETURN
74 P=70+32*R+2*C:O=PEEK(V+P)
76 PRINT@P,L$;:S$=INKEY$:POKEP+V,O:IFS$=""THEN76
77 X=ASC(S$):IFX=82THENB1=1:GOTO46
80 IFX=65THENIFC>1THENC=C-1:P=P-2:GOTO88
81 IFX=65THENC=9:P=P+32:GOTO88
82 IFX=83THENIFC<9THENC=C+1:P=P+2:GOTO88
83 IFX=83THENC=1:P=P-32:GOTO88
84 IFX=90THENIFR<9THENR=R+1:P=P+32:GOTO88
85 IFX=90THENR=1:P=P-256:GOTO88
86 IFX=87THENIFR>1THENR=R-1:P=P-32:GOTO88
87 IFX=87THENR=9:P=P+256:GOTO88
88 PRINT@426,;:IFX-13THENGOSUB90:GOTO74
90 PRINTMID$(STR$(R),1)","MID$(STR$(C),2);:RETURN
92 PRINT"PRESS =ENTER=";:FORI=0TO1STEP0:IFINKEY$<>CHR$(13)THENNEXT
93 CLS:GOTO6

200 CLS:M$="gomoku, THE JAPANESE GAME OF FIVE IN A ROW.":GOSUB1
210 M$="TRY TO GET FIVE IN A ROW HORIZONTALLY, VERTICALLY OR DIAGONALLY.":GOSUB1
220 M$="PLACE YOUR PIECE (Y) BY USING THE ARROW KEYS - THEN PRESS =ENTER=.":GOSUB1
230 M$="THE COMPUTER WILL THINK & THEN MAKE ITS MOVE (C).":GOSUB1:PRINT
240 M$="PRESS <R> TO RESIGN...":GOSUB1:PRINT:RETURN


1000 REM BY JAMES L FULLER
1010 REM FULLER'S ELECTRONICS
1020 REM SANTA ANA  CALIF
1030 REM COPYRIGHT (C)
1040 REM CLOAD 1980
1050 REM MC-10 EDITS BY
1060 REM JIM GERRIE
