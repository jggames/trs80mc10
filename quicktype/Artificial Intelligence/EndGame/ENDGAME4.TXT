99 REM**END-GAME BY J.F.WHITE.
100 CLS:GOTO1050
110 GOSUB230
120 GOSUB510:REM**CAN PAWN ADVANCE?
130 FORP3=-1TO1
140 IFZ(X+P3,Y-AO)=-CZ*AOTHENQ=Q-AO*CG
150 IFP3=0THEN170
160 IFZ(X+P3,Y-AO)=-AOTHENQ=Q-AO*CG
170 NEXT
180 N=N+1
190 GOSUB630
200 GOSUB680
210 GOSUB1480
220 RETURN
230 REM**EVALUATION
240 RX=Z(X,Y):Z(X,Y)=Z(I,J):Z(I,J)=0
250 Q=0
260 FORIZ=1TO8
270 IFZ(1,IZ)=AO OR Z(8,IZ)=AOTHENQ=Q-AO/CH
280 FORJZ=1TO8
290 Q=Q+Z(IZ,JZ)
300 IFZ(IZ,JZ)=0THEN340
310 IFZ(IZ,JZ)=CDTHENQ=Q+(9-JZ)*(9-JZ)*CC:MA=MA+1:GOTO340
320 IFZ(IZ,JZ)=AO*CZTHENGOSUB410:GOTO340
330 IFZ(IZ,JZ)=-CDTHENQ=Q-JZ*JZ*CC:MB=MB+1
340 NEXT
350 Q=Q-((MA-CD)*CF*MA)
360 Q=Q+((MB-CD)*CF*MB)
370 MA=0:MB=0
380 NEXT
390 Z(I,J)=Z(X,Y):Z(X,Y)=RX
400 RETURN
410 FORP3=-2TO2STEP4
420 IFIZ+P3<1ORIZ+P3>8ORJZ+P3<1ORJZ+P3>8THEN440
430 IFZ(IZ+P3,JZ)=-CZ*AOORZ(IZ,JZ+P3)=CZ*AOTHENQ=Q+AO*CE:GOTO440
440 NEXT
450 FORI7=-1TO1:FORJ7=-1TO1
460 IFZ(IZ+I7,JZ+J7)=0THEN480
470 Q=Q-((Z(IZ+I7,JZ+J7)-2*AO)*CD/CK)
480 NEXT:NEXT:Q=Q+3*AO/CK
490 IFZ(IZ+CD,JZ-AO)=-AOORZ(IZ-CD,JZ-AO)=-AOTHENQ=Q-5*AO
500 Q=Q+((IZ<3)+(IZ>6)+(JZ<3)+(JZ>6))*AO/CJ:RETURN:REM TRUE BOOLEEN VALUES FOR MZ-80 HANDLED AS -1
510 FORP2=-1TO1:IFP2+X>8THEN580
520 FORQ2=Y-AOTO(4-4*AO)STEP-AO
530 QP=QP+Z(P2+X,Q2)
540 NEXT
550 FORQ2=1TO8
560 GQ=GQ+Z(P2+X,Q2)
570 NEXT
580 NEXT
590 IFQP=0THENQ=Q+CG*AO
600 IFY=1THENQ=Q+.5
610 Q=Q+QP*CD/CH+GQ/CM:QP=0:QG=0
620 RETURN
630 REM**MOVE STORAGE
640 AA(N)=X:BB(N)=Y
650 AB(N)=I:BC(N)=J
660 Q(N)=Q
670 RETURN
680 REM**EVALUATE CAPTURES
690 IFQA(K)-Q(N)>.80ANDFL=1THEN710
700 GOTO790
710 REM**3RD LEVEL
720 FORI5=-1TO1:FORJ5=-1TO1
730 IFI5=0ANDJ5=0THEN750
740 IFZ(AA(N)+I5,BB(N)+J5)=CZ*-AOTHENQ(N)=Q(N)-Z(AB(N),BC(N)):GOSUB30:NG=1
750 NEXT:NEXT
760 IFNG=1THENNG=0:GOTO790
770 IFZ(AA(N)+AO,BB(N)-AO)=-AOTHENQ(N)=Q(N)-Z(AB(N),BC(N)):GOSUB30:GOTO790
780 IFZ(AA(N)-AO,BB(N)-AO)=-AOTHENQ(N)=Q(N)-Z(AB(N),BC(N)):GOSUB30
790 IFQ(N)<S0THENS0=Q(N):S1=AA(N):S2=BB(N):S3=AB(N):S4=BC(N)
800 IFQA(K)+S0<=R(0)THENAB=1
810 RETURN
820 REM FOURTH LEVEL
830 FORI4=-1TO1:FORJ4=-1TO1
840 X2=AA(N)+I4:Y2=BB(N)+J4
850 IFX2=AB(N)ORY2=BC(N)THEN870
860 IFZ(X2,Y2)=CZ*AOTHENQ(N)=Q(N)+AO:NG=1
870 NEXT:NEXT:IFNG=1THENNG=0:RETURN
880 IFAA(N)-1=AB(N)THEN910
890 IFZ(AA(N)-1,BB(N)-1)=AOTHENQ(N)=Q(N)+AO:RETURN
900 IFAA(N)+1=AB(N)THEN920
910 IFZ(AA(N)+1,BB(N)-1)=AOTHENQ(N)=Q(N)+AO
920 RETURN
930 DATA4,5,3,2,6,1,3,6,1,4,7,1,5,6,1,6,6,1
940 DATA 6,4,-3,2,3,-1,3,3,-1,4,2,-1,5,3,-1,6,3,-1
950 DATA 4,6,3,1,7,1,2,7,1,3,7,1,7,7,1,8,7,1
960 DATA7,3,-3,1,2,-1,2,2,-1,3,2,-1,6,2,-1,8,3,-1
970 DATA4,6,3,1,5,1,2,7,1,8,7,1,0,0,0,0,0,0
980 DATA4,4,-3,2,3,-1,7,3,-1,8,3,-1,0,0,0,0,0,0
990 DATA4,8,3,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
1000 DATA4,4,-3,4,2,-1,0,0,0,0,0,0,0,0,0,0,0,0
1010 DATA7,6,3,8,6,1,0,0,0,0,0,0,0,0,0,0,0,0
1020 DATA6,3,-3,7,3,-1,8,3,-1,0,0,0,0,0,0,0,0,0
1030 DATA4,6,3,2,6,1,3,5,1,5,6,1,7,7,1,8,6,1
1040 DATA5,2,-3,1,2,-1,3,4,-1,4,2,-1,7,3,-1,8,3,-1
1050 DIMAA(25),BB(25),Q(25),A(20),B(20)
1060 DIMC(20),D(20)
1070 DIMZ(9,9)
1080 DIMAB(25),BC(25),QA(25)
1090 DIMR(4),S(4),R$(4),S$(4)
1100 C1=INT(LOG(25)/LOG(2)+1):DIMW(C1,2)
1110 CC=.012:CD=1:CE=.10:CF=.2:CG=.3:CH=10:REM VARIABLES IN EVALUATION
1120 CI=10:CJ=30:CK=15:CL=050:CM=10:CZ=3
1130 PRINT:PRINT:PRINT
1140 PRINT"SELECT POSITION(1-6) OR RND"
1150 INPUTA$:IFA$="RND"THENRN=INT(RND(0)*6)+1:GOTO1170
1160 RN=VAL(A$):IFRN<1ORRN>6THEN1140
1170 CLS:PRINT@260,"end - game BY J.F.WHITE."
1180 FORIA=1TORN
1190 FORI=1TO8:FORJ=1TO8:Z(I,J)=0:NEXT:NEXT
1200 FORI=1TO16:IFI<11ANDI>6THEN1220
1210 READA(I),B(I):READZ(A(I),B(I))
1220 NEXT
1230 NEXT
1240 RESTORE
1250 GOSUB2880
1260 PRINT"WANT TO GO FIRST(Y/N)?"
1270 INPUTA$:IFA$="Y"THEN1830
1280 IFA$<>"N"THEN1260
1290 CLS:PRINT"   FROM    ,    TO    , "
1300 T0=-100
1310 TI$="000000"
1320 R(0)=-100
1330 AO=1:S0=99:AB=0
1340 FL=0
1350 GOTO1390
1360 I=S(3):J=S(4):S0=99:AB=0
1365 IFK=1THEN1390
1370 IFZ(I,J)=AOTHENGOSUB2330
1380 IFZ(I,J)=CZ*AOTHENGOSUB2090
1390 FORI=1TO8:FORJ=1TO8
1400 IFAB=1THEN1430
1410 IFZ(I,J)=CZ*AOTHENGOSUB2090
1420 IFZ(I,J)=AOTHENGOSUB2330
1430 NEXT:NEXT
1440 IFN<>0THEN1540
1450 PRINT:PRINTTAB(7);"STALEMATE!":END
1480 IFFL=1ORN=0THEN1530
1490 IFQ(N)>T0THENT0=Q(N):T1=AA(N):T2=BB(N):T3=AB(N):T4=BC(N):GOTO1510
1500 GOTO1530
1510 T1$=CHR$(ASC(RIGHT$(STR$(T1),1))+16):T3$=CHR$(ASC(RIGHT$(STR$(T3),1))+16)
1530 PRINT@9,T3$;:PRINT@12,T4;:PRINT@20,T1$;:PRINT@23,T2;
1530 RETURN
1540 IFFL=1THENRETURN
1550 REM**SORT
1560 M=N:FL=1:AO=-1
1570 GOSUB2560
1580 REM**2ND LEVEL
1590 R(0)=-99
1600 FORK=1TOM
1610 A(M-K+1)=AA(K):B(M-K+1)=BB(K):C(M-K+1)=AB(K):D(M-K+1)=BC(K)
1620 QA(M-K+1)=Q(K):Q(K)=0
1630 AA(K)=0:AB(K)=0:BC(K)=0:BB(K)=0
1640 NEXT
1650 FORK=1TOM
1660 N=0
1670 RY=Z(A(K),B(K)):Z(A(K),B(K))=Z(C(K),D(K)):Z(C(K),D(K))=0
1680 GOSUB1360
1690 QZ=S0+QA(K)
1700 IFQZ>R(0)THENR(0)=QZ:R(1)=A(K):R(2)=B(K):R(3)=C(K):R(4)=D(K):GOTO1720
1710 GOTO1750
1720 S(1)=S1:S(2)=S2:S(3)=S3:S(4)=S4
1730 GOSUB2840
1740 PRINT@9,R$(3);:PRINT@12,R(4);:PRINT@20,R$(1);:PRINT@23,R(2);
1750 Z(C(K),D(K))=Z(A(K),B(K)):Z(A(K),B(K))=RY
1760 NEXT
1770 PRINT:PRINT"MY TIME FOR THIS MOVE =":
1772 PRINTMID$(TI$,3,2);" MINS ";RIGHT$(TI$,2);" SECS."
1780 GOSUB2840
1790 REM PRINT"FROM ";R$(3);" ,";R(4);" TO ";R$(1);" ,";R(2)
1800 IFR(2)=1 ANDZ(R(3),R(4))=1THENPRINT":PRINT"I WON!":END
1810 IFABS(R(1)=R(3))=1ANDR(2)=3ANDA2=R(1)ANDB2=R(2)+1THENZ(A2,B2)=0
1820 Z(R(1),R(2))=Z(R(3),R(4)):Z(R(3),R(4))=0
1825 GOSUB2900
1830 PRINT"YOUR MOVE? ":FL=0:N=0:T0=-100
1840 INPUT"FROM (COL,ROW)";A1$,B1$:B1=VAL(B1$)
1842 IFA1$="Y"THENA1$=S$(3):B1=S(4):A2$=S$(1):B2=S(2):GOTO1900
1843 IFA1$="Y"THENSOUND1,1:GOTO1830
1850 IFA1$="P"THENGOSUB2880:GOTO1830
1860 IFA1$<>"Q"THEN1890
1870 PRINT"YOUR BEST REPLY IS:"
1872 PRINTS$(3);" ,";S(4);" TO ";S$(1);" ,";S(2)
1880 GOTO1830
1890 INPUT"TO (COL,ROW)  ";A2$,B2$:B2=VAL(B2$)
1900 IFA1$=""ORA2$=""THENSOUND1,1:GOTO1830
1905 A1=VAL(CHR$(ASC(A1$)-16))
1910 A2=VAL(CHR$(ASC(A2$)-16))
1920 IFZ(A1,B1)>-1THENPRINT"ERROR-- TRY AGAIN":GOTO1830
1930 IFB2=BANDZ(A1,B1)=-1THENPRINT:PRINT"YOU WON!":END
1940 CLS:PRINT"   FROM    ,    TO    , "
1950 Z(A2,B2)=Z(A1,B1):Z(A1,B1)=0
1960 IFZ(A2,B2)=-1ANDB1=5THEN1980
1970 GOTO1990
1980 IFABS(A1-A2)=1ANDR(2)-R(4)=-2ANDR(1)-A2=0THENZ(A2,B2-1)=0
1990 IFB1=2ANDB2=4THENAO=1:GOTO2010
2000 GOTO2080
2010 IFZ(A2+1,B2)=1THENI=A2+1:J=B2:N=1:GOSUB2040
2020 IFZ(A2-1,B2)=1THENI=A2-1:J=B2:N=1:GOSUB2040
2030 GOTO2080
2040 Z(A2,B2-1)=Z(A2,B2):Z(A2,B2)=0:GOSUB2470:Z(A2,B2)=Z(A2,B2-1)
2050 Z(A2,B2-1)=0
2060 T0=Q(N)
2070 RETURN
2080 GOTO1310
2090 REM**KING MOVE
2100 FORII=-1TO1:FORIJ=-1TO1
2110 IFAB=1THEN2300
2120 IFII=0ANDIJ=0THEN2300
2130 X=I+II:Y=J+IJ
2140 IFX<1ORX>8ORY<1ORY>8THEN2300
2150 IFZ(X,Y)=AOTHEN2300
2160 FORP4=-1TO1:FORQ4=-1TO1
2170 IFZ(X+P4,Y+Q4)=CZ*AOTHENNG=1
2180 IFZ(X-1,Y-AO)=-AOTHENNG=1
2190 IFZ(X+1,Y-AO)=-AOTHENNG=1
2200 NEXT:NEXT:IFNG=1THENNG=0:GOTO2300
2210 GOSUB230
2220 N=N+1
2230 GOSUB630
2240 GOSUB680
2245 IFFL=1THEN2300
2250 QG=0:CQ=2
2252 FORP3=-CQTOCQ
2254 IFX+P3<1ORX+P3>8THEN2264
2256 IFY+CQ>8THEN2260
2258 QG=QG+ABS(Z(X+P3,Y+CQ))
2260 IFY-CQ<1THEN2264
2262 QG=QG+ABS(Z(X+P3,Y-CQ))
2264 IFY+P3<1ORY+P3>8THEN2274
2266 IFX-CQ<1THEN2270
2268 QG=QG+ABS(Z(X-CQ,Y+P3))
2270 IFX+CQ>8THEN2274
2272 QG=QG+ABS(Z(X+CQ,Y+P3))
2274 NEXT
2276 IFQG>0THENQ(N)=Q(N)+QG/(CQ*CL):GOTO2290
2278 CQ=CQ+1:IFCQ>8THEN2290
2280 GOTO2252
2290 GOSUB1480
2300 NEXT:NEXT
2310 RETURN
2320 REM**PAWN 1-MOVE
2330 X=I:Y=J-AO
2340 IFZ(X,Y)<>0THEN2460
2350 GOSUB110
2360 REM**PAWN 2-MOVE
2370 IF(AO=1)AND(J=2)THEN2400
2380 IF(AO=-1)AND(J=2)THEN2400
2390 GOTO2460
2400 X=I:Y=J-2*AO
2410 IFZ(X,Y)<>0THEN2460
2420 GOSUB110
2425 REM**EN PASSANT THREAT
2430 IFZ(X+1,5+(AO=-CD))=-AOTHENQ(N)=Q(N)-AO*.8
2430 IFZ(X-1,5-(AO=-CD))=-AOTHENQ(N)=Q(N)-AO*.8
2450 GOSUB1480
2460 REM** PAWN CAPTURE
2470 X=I-1:Y=J-AO
2480 IF(AO=1)AND(Z(X,Y)>=0)THEN2510
2490 IF(AO=-1)AND(Z(X,Y)<=0)THEN2510
2500 GOSUB110
2510 X=I+1:Y=J-AO
2520 IF(AO=1)AND(Z(X,Y)>=0)THEN2550
2530 IF(AO=-1)AND(Z(X,Y)<=0)THEN2550
2540 GOSUB110
2550 RETURN
2560 REM**QUICK SORT
2570 SS=1
2580 W(1,1)=1:W(1,2)=M
2590 LL=W(SS,1):RR=W(SS,2):SS=SS-1
2600 II=LL:JJ=RR:XX=Q(INT(RND(0)*(RR-LL)+.5)+LL)
2610 IFQ(II)>=XXTHEN2630
2620 II=II+1:GOTO2610
2630 IFXX>=Q(JJ)THEN2650
2640 JJ=JJ-1:GOTO2630
2650 IFII>JJTHEN2720
2660 WW=Q(II):Q(II)=Q(JJ):Q(JJ)=WW
2670 WW=AA(II):AA(II)=AA(JJ):AA(JJ)=WW
2680 WW=BB(II):BB(II)=BB(JJ):BB(JJ)=WW
2690 WW=AB(II):AB(JJ)=AB(JJ):AB(JJ)=WW
2700 WW=BC(II):BC(II)=BC(JJ):BC(JJ)=WW
2710 II=II+1:JJ=JJ-1
2720 IFII<=JJTHEN2610
2730 IFJJ-LL>=RR-IITHEN2770
2740 IFII>=RRTHEN2760
2750 SS=SS+1:W(SS,1)=II:W(SS,2)=RR
2760 RR=JJ:GOTO2800
2770 IFLL>=JJTHEN2790
2780 SS=SS+1:W(SS,1)=LL:W(SS,2)=JJ
2790 LL=II
2800 IFLL<RRTHEN2600
2810 IFSS>0THEN2590
2820 PRINT"SORT COMPLETE"
2830 RETURN
2840 FORKI=1TO3STEP2
2850 R$(KI)=CHR$(ASC(RIGHT$(STR$(R(KI)),1))+16):S$(KI)=CHR$(ASC(RIGHT$(STR$(S(KI)),1))+16)
2860 NEXT
2870 RETURN
2880 REM**SCREEN DISPLAY OF POSITION
2890 CLS
2900 FORJ=8TO1STEP-1:FORI=1TO8
2910 PRINTTAB(3*I-3);
2920 IFZ(I,J)=0THENPRINT".";
2930 IFZ(I,J)=CDTHENPRINT"P";
2940 IFZ(I,J)=-CDTHENPRINT"p";
2950 IFZ(I,J)=CZTHENPRINT"K";
2960 IFZ(I,J)=-CZTHENPRINT"k";
2970 NEXT:PRINT J :NEXT
2980 PRINT"A  B  C  D  E  F  G  H"
2990 PRINT:PRINT"PRESS ANY KEY TO CONTINUE";
3000 A$=INKEY$:IFA$=""THEN3000
3010 FORC1=1TO25:PRINTCHR$(8);:NEXT:RETURN
