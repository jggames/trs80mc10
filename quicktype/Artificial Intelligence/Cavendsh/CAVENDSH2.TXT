10 CLEAR 500:PMODE 3,1:COLOR 2,1:PCLS:SCREEN1,0:DU=RND(-TIMER)
30 GOSUB 190
40 GOSUB 470:GOSUB 3130
50 GOSUB 860
60 REM
70 FOR I=1 TO 8
80 IF T(I,1)<4 THEN GOSUB 1380:IF Y>2 THEN GOSUB 1900
90 IF T(I,1)<5 THEN COLOR 3:DRAW"BM"+STR$(T(I,9)*8)+","+STR$(T(I,8)*8):UU=VAL(MID$(U$,I,1)):A$=UC$(UU):GOSUB 3000
100 NEXT I
110 FOR E=9 TO 16
120 IF T(E,1)<4 THEN GOSUB 2140
130 NEXT E
140 GOSUB 1020
150 GOSUB 2290
160 IF VC<>1 AND DE<>1 THEN 60
170 GOSUB 2380
180 GOSUB 2410:CLS:END
190 REM INITIALIZATION
200 VC=0:DE=0
210 FOR K=1 TO 9
220 READ A$
230 UC$(K)=A$
290 NEXT K

310 REM
330 COLOR 4,1
340 PCLS
350 DIM M(16,30)
355 DIM T(16,9) 
360 DIM T$(8),O$(5),W$(5),M$(5),A$(4),R$(4)
370 I$="NWSE"
380 FOR J=1 TO 5:READ O$(J),W$(J),M$(J):NEXT J
390 FOR J=1 TO 8:READ T$(J):NEXT J
400 FOR J=1 TO 4:READ A$(J),R$(J):NEXT
410 U$="65778899":U$=U$+U$
415 X$="NnYy"
420 RETURN
470 REM CREATE
480 FOR I=1 TO 16:GOSUB 800:M(I,1)=R:NEXT I
490 FOR I=1 TO 16
500 FOR J=2 TO 30
510 S=RND(10)
520 IF S<8 THEN GOSUB 800
525 IF S>=8 THEN R=M(I,J-1)
530 M(I,J)=R
540 IF R=3 AND J<30 THEN M(I,J+1)=4
550 IF M(I,J)<>0 AND M(I,J)<>3 THEN LINE(J*8,I*8)-(J*8+7,I*8+7),PRESET,BF:DRAW"BM"+STR$(J*8)+","+STR$(I*8)+UC$(M(I,J))
555 IF M(I,J)=3 AND J<>30 THEN DRAW"BM"+STR$(J*8)+","+STR$(I*8)+UC$(3)+"BM"+STR$((J+1)*8)+","+STR$(I*8)+UC$(4)
560 NEXT J,I
580 GOSUB 720
590 FOR I=1 TO 8
600 FOR J=1 TO 2:T(I,J)=2:T(I+8,J)=2:NEXT J 
610 FOR J=3 TO 4:READ T(I,J):T(I+8,J)=T(I,J):NEXT J
620 READ MR
630 FOR J=0 TO 8 STEP 8
640 T(I+J,5)=MR+RND(2)
650 T(I+J,6)=(RND(100)*10)+10
660 T(I+J,7)=T(I+J,6)
670 NEXT J
680 T(I,8)=15
690 T(I+8,8)=1
700 NEXT I
710 RETURN
720 REM BORDER
730 LINE(0,128)-(255,135),PRESET,BF
740 LINE(248,0)-(255,191),PRESET,BF:LINE(4,4)-(252,132),PSET,B
790 RETURN
800 REM CHOOSE TERRAIN
810 R=RND(50)
820 IF R>5 THEN R=0
830 IF R>4 THEN R=3:RETURN
840 IF R>1 THEN R=2
850 RETURN
860 REM DISPOSE TROOPS
870 COLOR 2
880 FOR M=1 TO 2
890 S=1:R=1
900 FOR K=1 TO 8
910 REM
920 S=RND(8*M)
930 IF T(S,9)<>0 THEN 910
940 R=RND(4)+R
950 R=R-INT(R/30)
960 T(S,9)=R
970 COLOR M:IF M=1 THEN COLOR 3
980 DRAW"BM"+STR$(T(S,9)*8)+","+STR$(T(S,8)*8):UU=VAL(MID$(U$,S,1)):A$=UC$(UU):GOSUB 3000
990 NEXT K
1000 NEXT M
1010 RETURN
1020 REM EFFECT OF ORDERS
1030 FOR I=1 TO 16
1032 IF T(I,1)>3 THEN 1140
1035 COLOR 4:GOSUB 2540:DRAW"BM0,144":A$="UNIT"+STR$(1)+" ACTS":GOSUB 3190
1040 CL=3:IF I>8 THEN CL=4:COLOR CL
1050 IF T(I,1)=3 THEN B=I:GOSUB 1160
1055 IF T(I,1)=2 THEN 1140
1060 IF T(I,1)=1 THEN SH=I:GOSUB 1710:GOTO 1140
1070 FOR F=-1 TO 1
1080 FOR G=-1 TO 1
1090 FOR E=1 TO 16
1100 IF (T(I,8)+F=T(E,8)) AND (T(I,9)+G=T(E,9)) AND T(E,1)<>5 THEN US=I:TH=E:GOSUB 1510
1110 NEXT E,G,F
1140 NEXT I
1150 RETURN
1160 REM MOVE UNIT
1170 OX=T(B,8):OY=T(B,9)
1175 ZZ=0
1180 IF M(T(B,8),T(B,9))<>0 THEN ZZ=M(T(B,8),T(B,9))
1190 D=5-T(B,4)
1200 IF B<3 OR B=9 OR B=10 THEN D=D+2
1210 V=T(B,2)-1
1215 UP=0:AL=V-2
1220 IF (V/2)-INT(V/2)=0 THEN UP=V-1:AL=0
1230 REM
1240 NL=T(B,9)+AL:NP=T(B,8)+UP
1250 IF NP<1 THEN NP=1
1260 IF NP>15 THEN NP=15
1270 IF NL<1 THEN NL=1
1280 IF NL>30 THEN NL=30
1290 IF M(NP,NL)>0 THEN D=D-1
1300 FOR K=1 TO 8
1310 IF (T(K,9)=NL AND T(K,8)=NP AND K<>B) THEN D=0
1315 IF (T(K+8,9)=NL AND T(K+8,8)=NP AND K+8<>B) THEN D=0
1320 NEXT K
1330 IF D>0 THEN T(B,9)=NL:T(B,8)=NP:D=D-1
1340 IF D<>0 THEN 1230
1350 X9=OY*8:Y9=OX*8:IF ZZ<>0 THEN COLOR 4:LINE(X9,Y9)-(X9+7,Y9+7),PRESET,BF:DRAW"BM"+STR$(X9)+","+STR$(Y9)+UC$(ZZ) ELSE LINE(X9,Y9)-(X9+7,Y9+7),PRESET,BF
1360 COLOR CL:DRAW"BM"+STR$(T(B,9)*8)+","+STR$(T(B,8)*8):UU=VAL(MID$(U$,I,1)):A$=UC$(UU):GOSUB 3000
1370 RETURN
1380 REM SELECT UNIT FOR ORDERS
1390 GOSUB 2540
1400 COLOR 4
1410 DRAW"BM"+STR$(T(I,9)*8)+","+STR$(T(I,8)*8):UU=VAL(MID$(U$,I,1)):A$=UC$(UU):GOSUB 3030
1420 DRAW"BM0,144":A$="UNIT "+STR$(I)+" "+T$(I)+"       ":GOSUB 3190
1430 DRAW"BM0,152":A$="ORDERS ARE TO   "+O$(T(I,1))+" ":GOSUB 3190
1440 IF T(I,1)=3 THEN DRAW"BM160,152":A$=MID$(I$,T(I,2),1):GOSUB 3190
1450 REM
1460 DRAW"BM0,160":A$="CHANGE ORDERS  Y OR N":GOSUB 3190
1465 Y=0:Y$=INKEY$
1466 IF Y$="" THEN 1465
1470 Y=INSTR(1,X$,Y$)
1490 IF Y=0 THEN 1450
1500 RETURN
1510 REM COMBAT
1520 IF (US<9 AND TH<9) OR (US>8 AND TH>8) THEN RETURN
1530 IF T(US,1)=5 OR T(TH,1)=5 THEN RETURN
1540 GOSUB 2540
1550 DRAW"BM0,144":A$="COMBAT   ":GOSUB 3190
1560 AT=INT((T(US,7)-T(TH,7))/50)
1570 AT=AT+T(US,3)-T(TH,4)+T(US,5)+RND(5)
1580 IF ABS(T(US,2)-T(TH,2))<>2 THEN AT=AT+2
1590 IF US<3 OR US=9 OR US=10 THEN AT=AT+1
1600 DF=INT((T(TH,7)-T(US,7))/60)
1610 DF=DF+T(TH,3)-T(US,4)+T(TH,5)+M(T(TH,8),T(TH,9))+RND(3)+2
1615 WN=TH:LO=US
1620 IF AT>DF THEN WN=US:LO=TH
1630 WC=INT(T(WN,7)/10):IF WC<1 THEN WC=1
1640 T(WN,7)=T(WN,7)-WC
1650 LC=INT(T(LO,7)/5):IF LC<1 THEN LC=1
1660 T(LO,7)=T(LO,7)-LC
1670 DRAW"BM0,160":A$=STR$(WN)+" LOSES"+STR$(WC)+" "+STR$(LO)+" LOSES"+STR$(LC):GOSUB 3190
1680 GOSUB 2410
1690 UN=LO:GOSUB 2200
1700 RETURN
1710 REM MISSILE ROUTINE
1720 GOSUB 2540
1730 DRAW"BM0,152":A$="UNIT"+STR$(SH)+" FIRES":GOSUB 3190
1740 FX=5:FY=5:GP=-1
1745 ST=9
1750 IF SH>8 THEN ST=1
1770 FOR M=ST TO (ST+7)
1780 TM=ABS(T(M,8)-T(SH,8)):TY=ABS(T(M,9)-T(SH,9))
1785 IF TM<FX AND T(M,1)<5 AND TY<FY THEN FX=TM:FY=TY:GP=M
1790 NEXT M
1800 IF GP=-1 THEN DRAW"BM0,160":A$="NOTHING IN RANGE":GOSUB 3190:GOSUB 2410:RETURN
1810 C=8-T(GP,4)-ABS(FX-FY)
1820 IF GP<3 OR GP=9 OR GP=10 THEN C=C+1
1830 IF M(T(GP,8),T(GP,9))=2 THEN C=C-2
1840 IF T(GP,1)<>2 THEN C=C+1
1850 C=(C+(INT(T(SH,7)/40))+RND(3))*10
1860 T(GP,7)=T(GP,7)-C
1870 DRAW"BM0,160":A$="CAUSES"+STR$(C)+" CASUALTIES ON UNIT"+STR$(GP):GOSUB 3190
1875 GOSUB 2410
1880 UN=GP:GOSUB 2200
1890 RETURN
1900 REM SELECT ACTION
1910 GOSUB 2540
1920 DRAW"BM0,152":A$="OPTIONS    ":GOSUB 3190
1930 FOR J=1 TO 4
1940 DRAW"BM104,"+STR$(144+J*8):A$=LEFT$(O$(J),1)+"  "+O$(J):GOSUB 3190
1950 NEXT J
1960 REM
1962 A=0
1965 F$="FHMS"
1970 G$=INKEY$:IF G$="" THEN 1970
1980 A=INSTR(1,F$,G$)
1990 IF A<=0 THEN 1960
2000 IF I<>6 AND I<>5 AND A=1 THEN GOSUB 2540:DRAW"BM64,152":A$="NO BOWS":GOSUB 3190:GOSUB 2410:GOTO 1910
2010 IF A=4 THEN GOSUB 2440:RETURN
2020 T(I,1)=A
2030 IF A=3 THEN GOSUB 2050
2040 RETURN
2050 REM DECIDE MOVEMENT DIRECTION
2055 GOSUB 2540
2060 DRAW"BM0,144":A$="WHICH WAY  N S E W":GOSUB 3190
2065 G=0
2070 REM
2080 G$=INKEY$:IF G$="" THEN 2080
2100 G=INSTR(1,I$,G$)
2110 IF G<=0 THEN 2070
2120 T(I,2)=G
2130 RETURN
2140 REM ENEMY SELECTS ACTION
2150 T(E,2)=3
2160 T(E,1)=RND(3)
2170 IF T(E,1)=1 AND T(E,3)<>2 THEN 2160
2180 IF T(E,1)=3 AND RND(2)=1 THEN T(E,2)=RND(4)
2190 RETURN
2200 REM UNIT MORALE TEST
2210 IF T(UN,6)-T(UN,7)<((T(UN,6)/100)*((T(UN,5)+2)*10)) THEN RETURN
2220 GOSUB 2540
2230 DRAW"BM0,152":A$="LOSSES ARE TOO GREAT":GOSUB 3190
2240 DRAW"BM0,160":A$="UNIT"+STR$(UN)+" DISINTEGRATES":GOSUB 3190
2250 GOSUB 2410
2260 T(UN,1)=5
2270 X9=T(UN,9)*8:Y9=T(UN,8)*8:LINE(X9,Y9)-(X9+7,Y9+7),PRESET,BF
2280 RETURN
2290 REM TEST FOR VICTORY
2300 GD=0:BD=0
2310 FOR M=1 TO8
2320 IF T(M,1)<>5 THEN GD=GD+1
2330 IF T(M+8,1)<>5 THEN BD=BD+1
2340 NEXT M
2350 IF GD>BD*2 OR (BD<2 AND GD>2) THEN VC=1
2360 IF BD>GD*2 OR (GD<2 AND BD>2) THEN DE=1
2370 RETURN
2380 REM END MESSAGE
2390 IF VC=1 THEN DRAW"BM0,176":A$="VICTORY":GOSUB 3190
2395 IF DE=1 THEN DRAW"BM0,176":A$="A CRUSHING DEFEAT":GOSUB 3190
2400 RETURN
2410 REM DELAY
2420 DRAW"BM80,176":A$="PRESS ANY KEY":GOSUB 3190
2425 G$=INKEY$:IF G$=" THEN 2425
2426 LINE(0,176)-(255,183),PRESET,BF
2430 RETURN
2440 REM UNIT STATUS
2450 GOSUB 2540
2460 DRAW"BM0,144":A$="UNIT"+STR$(I)+"    TYPE "+T$(I):GOSUB 3190
2470 DRAW"BM0,152":A$="WEAPON "+W$(T(I,3)):GOSUB 3190
2480 DRAW"BM120,152":A$="ARMOUR "+A$(T(I,4)):GOSUB 3190
2490 DRAW"BM0,160":A$="STRENGTH"+STR$(T(I,7)):GOSUB 3190
2500 DRAW"BM120,160":A$="ATTITUDE "+M$(T(I,5)):GOSUB 3190
2510 DRAW"BM0,168":A$="LOCATION "+R$(M(T(I,8),T(I,9))+1):GOSUB 3190
2520 GOSUB 2410
2530 RETURN

2540 REM CLEAR TEXT WINDOW
2550 PMODE0,4:PCLS0:PMODE3,1
2560 RETURN

2570 DATA BR2D2L2D5RU2R3D2R3U4L2UL2D2
2590 DATA BR2R3DRDLDL2ND4L3U2R3DL3
2610 DATA BD3E2R2UD2RFGLG3
2630 DATA BD3E2RF3
2650 DATA ND7FR2FD5UNR3L2
2670 DATA ND7FR4DNR2DNFL3UR2
2690 DATA R5ND6DL5D5R3DL
2710 DATA NR3DRF6U4LHE2DL
2730 DATA BR7G7E2UNF3H2DR

2760 DATA FIRE,NONE,COWARDLY,HALT,BOW,UNWILLING
2770 DATA MOVE,SWORD,WILLING,STATUS,AXE,BRAVE
2780 DATA ROUT,LANCE,VALIANT
2790 DATA KNIGHTS,SERGEANTS,MEN AT ARMS,MEN AT ARMS,ARCHERS,ARCHERS,PEASANTS,PEASANTS
2800 DATA NONE,PLAINS,JERKIN,VILLAGE,CHAINMAIL,WOODS,PLATE,HILLS
2810 DATA 5,4,3,5,3,3,4,3,2,3,3,1,2,2,1,2,3,2,3,2,0,3,1,0
3000 X9=PEEK(200):Y9=PEEK(202):LINE(X9,Y9)-(X9+7,Y9+7),PRESET,BF
3010 REM POKE 200,X9:POKE 202,Y9:DRAW A$
3020 RETURN
3030 X9=PEEK(200):Y9=PEEK(202):C9=PEEK(178):COLOR 2:LINE(X9,Y9)-(X9+7,Y9+7),PSET,BF
3040 REM POKE 178,C9:GOTO 3010
3050 REM DATA FOR LETTERS & DIGITS
3060 DATA BR4,BDD5RU3NR2U2ERFND5BR4BU,D6R3EUHEUHBR5,NR5D6R5BR3BU6,NR3D6R3EU4BUBR4,NR5D3NR3D3R5BR3BU6,NR5D3NR3D3BR8BU6,NR5D6R5U2BU4BR3
3070 DATA ND6D3R5D3U6BR3,R5L2D6L3R5BR3BU6,BD5RFREU5L2BR6,ND6D3R3FD2BU4U2BR4,D6R5BR3BU6,ND6DR5ND5UBR3,ND6DR2D3R3D2U6BR3
3080 DATA D6R5U6NL5BR3,D6U3R5U3NL5BR3,D6UR3FRBU2U4NL5BR3,D6U2R3FDUBU2NL2U3NL4BR4,NR5D3R5D3NL5BU6BR3,R5L2ND6BR5,D6R5U6BR3
3090 DATA D4RFDRUEU4BR4,D6UR5DU6BR3,D2RFGND2ERFND2HEU2BR4,D2RFD3RU3EU2BR4,R4D2GLGD2R4BU6BR3
3100 DATA BR3LGD4FREU4BR4BU,BR2DNL2D5L2R5BU6BR3,BDRERFDGL2D3R4BU6BR3,BDRERFDGFDGLHLBU5BR8,D4R5UD3BU6BR3,NR5D2R3FD2GLHLBU5BR8,BDBR5LHLGD2NR2D2FREUBU4BR4
3110 DATA R5D2LGLGD2BR7BU6,BR3LGDFGDFREUHEUBUBR4,BR3LGDFR2D2GLHLBU2BR4U2BUBR4
3120 REM SET UP CHAR ARRAYS
3130 DIM LE$(26)
3140 FOR K9=0 TO 26:READ LE$(K9):NEXT
3150 FOR K9=0 TO 9:READ NU$(K9):NEXT
3170 RETURN
3180 REM ROUTINE TO PRINT A$
3190 FOR K9=1 TO LEN(A$)
3200 B$=MID$(A$,K9,1)
3210 IF B$>="0" AND B$<="9" THEN DRAW NU$(VAL(B$)):GOTO 3240
3220 IF B$=" " THEN N9=0 ELSE N9=ASC(B$)-64
3230 DRAW LE$(N9)
3240 NEXT
3250 RETURN
