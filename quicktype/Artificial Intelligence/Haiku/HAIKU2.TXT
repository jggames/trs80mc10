0 GOSUB3000:CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):PRINT" ";:ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 IFLEN(LI$)>30THENM$=LI$:GOSUB1:RETURN
8 PRINTTAB((32-LEN(LI$))/2)LI$:RETURN
10 REM supervisor
20 GOSUB120:REM init
30 GOSUB200:GOTO50
40 GOSUB200:REM input
50 CLS:GOSUB260
60 IFRA=1THENGOSUB450:REM 1ST PATTERN
70 IFRA=2THENGOSUB670:REM 2ND PATTERN
80 IFRA=3THENGOSUB890:REM 3RD PATTERN
90 IFRA=4THENGOSUB1110:REM 4TH PATTERN
100 GOTO40
120 REM INIT
130 I=RND(-(PEEK(9)*256+PEEK(10))):REM randomize
140 AR=4
150 AD=50
160 NO=50
170 VE=14
180 PR=6
190 RETURN
200 REM input
210 LI$=""
220 RESTORE
221 A$=INKEY$:IFA$<>""THEN221
230 A$=INKEY$:IFA$=""THEN230
240 CLS
250 RETURN
260 REM determine haiku pattern 
270 RA=RND(4)
280 RETURN
300 REM check articles 
305 FORI=1TOLEN(LI$)-2
310 IFMID$(LI$,I,3)<>" A "THEN320
311 B$=MID$(LI$,I+3,1):IFB$="A"ORB$="E"ORB$="I"ORB$="O"ORB$="U"THENLI$=LEFT$(LI$,I+1)+"N"+MID$(LI$,I+2)
320 NEXTI
330 FORI=1TOLEN(LI$)-2
340 IFMID$(LI$,I,4)=" AN "THEN345
341 GOTO350
345 B$=MID$(LI$,I+4,1):IFB$="A"ORB$="E"ORB$="I"ORE$="O"ORB$="U"THEN350
346 LI$=LEFT$(LI$,I+1)+MID$(LI$,I+3)
350 NEXTI
360 RETURN
380 REM choose words of poem
385 RA=RND(N)+P
390 FORI=1TORA
400 READWR$
410 NEXTI
420 LI$=LI$+" "+WR$
430 RESTORE
440 RETURN
450 REM 1st haiku pattern
460 N=AR:P=0:GOSUB380
470 N=AD:P=AR:GOSUB380
480 N=NO:P=AR+AD:GOSUB380
490 GOSUB300
500 LI$=LI$+"..."
510 PRINT@32*5,;:GOSUB7
520 LI$=""
530 N=AR:P=0:GOSUB380
540 N=NO:P=AR+AD:GOSUB380
550 N=VE:P=AR+AD+NO:GOSUB380
560 N=PR:P=AR+AD+NO+VE:GOSUB380
570 N=AR:P=0:GOSUB380
580 N=NO:P=AR+AD:GOSUB380
590 GOSUB300
600 GOSUB7:REM 6
610 LI$=""
620 N=AD:P=AR:GOSUB380
630 GOSUB380
640 N=NO:P=AR+AD:GOSUB380
650 GOSUB7:REM 7
660 RETURN
670 REM 2nd haiku pattern
680 N=NO:P=AR+AD:GOSUB380
690 N=PR:P=AR+AD+NO+VE:GOSUB380
700 N=AR:P=0:GOSUB380
710 N=NO:P=AR+AD:GOSUB380
720 GOSUB300
730 LI$=LI$+";"
740 PRINT@32*5,;:GOSUB7
750 LI$=""
760 N=AR:P=0:GOSUB380
770 N=AD:P=AR:GOSUB380
780 N=NO:P=AR+AD:GOSUB380
790 N=PR:P=AR+AD+NO+VE:GOSUB380
800 N=AR:P=0:GOSUB380
810 N=NO:P=AR+AD:GOSUB380
820 GOSUB300
830 GOSUB7:REM 6
840 LI$=""
850 N=AD:P=AR:GOSUB380
860 N=NO:P=AR+AD:GOSUB380
870 GOSUB7:REM 7
880 RETURN
890 REM 3rd haiku pattern
900 N=AR:P=0:GOSUB380
910 N=AD:P=AR:GOSUB380
920 GOSUB380
930 N=NO:P=AR+AD:GOSUB380
940 GOSUB300
950 LI$=LI$+";" 
960 PRINT@32*5,;:GOSUB7
970 LI$=""
980 N=PR:P=AR+AD+NO+VE:GOSUB380
990 N=AR:P=0:GOSUB380
1000 N=AD:P=AR:GOSUB380
1010 N=NO:P=AR+AD:GOSUB380
1020 GOSUB300
1030 GOSUB7:REM 6
1040 LI$=""
1050 N=AR:P=0:GOSUB380
1060 N=NO:P=AR+AD:GOSUB380
1070 N=VE:P=AR+AD+NO:GOSUB380
1080 GOSUB300
1090 GOSUB7:REM 7
1100 RETURN
1110 REM 4th haiku pattern
1120 N=AR:P=0:GOSUB380
1130 N=AD:P=AR:GOSUB380
1140 N=NO:P=AR+AD:GOSUB380
1150 N=VE:P=AR+AD+NO:GOSUB380
1160 GOSUB300
1170 LI$=LI$+""
1180 PRINT@32*5,;:GOSUB7
1190 LI$=""
1200 N=AR:P=0:GOSUB380
1210 N=AD:P=AR:GOSUB380
1220 GOSUB380
1230 N=NO:P=AR+AD:GOSUB380
1240 GOSUB300
1250 GOSUB7:REM 6
1260 LI$=""
1270 N=PR:P=AR+AD+NO+VE:GOSUB380
1280 N=AR:P=0:GOSUB380
1290 N=AD:P=AR:GOSUB380
1300 N=NO:P=AR+AD:GOSUB380
1310 GOSUB300
1320 GOSUB7:REM 7
1330 RETURN
2000 REM vocab
2010 REM articles
2020 DATA A,THE,AN,THE
2040 DATA AUTUMN,HIDDEN,BITTER,MISTY,SILENT,EMPTY
2050 DATA DRY,DARK,SUMMER,ICY,DELICATE,QUIET
2060 DATA WHITE,COOL,SPRING,WINTER,DAPPLED
2070 DATA TWILIGHT,DAWN,CRIMSON,WISPY,AZURE
2080 DATA BLUE,BILLOWING,BROKEN,COLD,DAMP,FALLING
2090 DATA FROSTY,GREEN,LONG,LATE,LINGERING,LIMPID
2100 DATA LITTLE,MORNING,MUDDY,OLD,RED,ROUGH
2110 DATA STILL,SMALL,SPARKLING,THROBBING,VERMILION
2120 DATA WANDERING,WITHERED,WILD,BLACK,YOUNG
2130 REM nouns
2140 DATA WATERFALL,RIVER,BREEZE,MOON
2150 DATA RAIN,WIND,SEA,MORNING,SNOW,LAKE,SUNSET
2160 DATA PINE,SHADOW,LEAF,DAWN,GLITTER,FOREST
2170 DATA HILL,CLOUD,MEADOW,SUN,GLADE,BIRD,BROOK
2180 DATA BUTTERFLY,BUSH,DEW,DUST,FIELD,FIR
2190 DATA FLOWER,FIREFLY,FEATHER,GRASS,HAZE,MOUNTAIN
2200 DATA NIGHT,POND,SHADE,SNOWFLAKE
2210 DATA SILENCE,SOUND,SKY,SHAPE,SURF,THUNDER
2220 DATA VIOLET,WATER,WILDFLOWER,WAVE
2230 REM d. verbs
2240 DATA SHAKES,DRIFTS,HAS STOPPED,STRUGGLES
2250 DATA HAS FALLEN,HAS PASSED,SLEEPS,CREEPS
2260 DATA FLUTTERS,HAS RISEN,IS FALLING,IS TRICKLING
2270 DATA MURMURS,IS FLOATING
2280 REM e. prepositions
2290 DATA ON,IN,OF,UNDER,OVER,NEAR
3000 CLS:PRINT@6*32+8,"HAIKU, VERS. 1.2"
3010 PRINT@480,"   HIT ANY KEY FOR NEXT POEM";:RETURN
3020 REM JOHN KRUTCH 1981
3030 REM MC-10 EDITS JIM GERRIE
