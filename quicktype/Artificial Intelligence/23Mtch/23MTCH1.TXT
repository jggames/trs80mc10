5   REM *** WRITTEN BY BOB ALBRECHR, PEOPLES COMPUTER CO
10  REM *** CONVERTED TO RSTS/E BY DAVID AHL, DIGITAL
20  REM ***23 MATCHES
100 CLS
110 PRINT "LET'S PLAY 23 MATCHES. WE START WITH 23 MATCHES."
115 PRINT "YOU MOVE FIRST. YOU MAY TAKE 1, 2 OR 3 MATCHES."
120 PRINT "THEN I MOVE...I MAY TAKE 1,2 OR 3 MATCHES."
125 PRINT "YOU MOVE, I MOVE AND SO ON.":PRINT"THE ONE WHO HAS TO"
130 PRINT "TAKE THE LAST MATCH LOSES."
135 PRINT "GOOD LUCK AND MAY THE BEST":PRINT"COMPUTER (HA HA) WIN."
140 PRINT 
150 M=23
200 REM ***THE HUMAN MOVES
205 PRINT 
210 PRINT "THERE ARE NOW";M;"MATCHES."
215 PRINT 
220 PRINT "HOW MANY DO YOU TAKE";
230 INPUT H
240 IF H>M THEN 510 
250 IF H<>INT(H) THEN 510 
260 IF H<=0 THEN 510 
270 IF H>=4 THEN 510 
280 M=M-H
290 IF M=0 THEN 410 
300 REM ***THE COMPUTER MOVES
305 IF M=1 THEN 440 
310 R=M-4*INT(M/4)
320 IF R<>1 THEN 350 
330 C=INT(3*RND(0))+1
340 GOTO 360 
350 C=(R+3)-4*INT((R+3)/4)
360 M=M-C
370 IF M=0 THEN 440 
375 PRINT
380 PRINT "I TOOK";C;"..."
390 GOTO 210
400 REM ***SOMEBODY WON (SEE LINES 290,305,370)
410 PRINT 
420 PRINT "I WON!!!":PRINT"BETTER LUCK NEXT TIME."
430 GOTO 140
440 PRINT 
450 PRINT "O.K. SO YOU WON.":PRINT"LET'S PLAY AGAIN."
460 GOTO 140
500 REM ***THE HUMAN CHEATED! (SEE LINES 240 THRU 270)
510 PRINT "YOU CHEATED!::PRINT"BUT I'LL GIVE YOU ANOTHER CHANCE."
520 GOTO 215
999 END
