0 DIMC1,C2,C3,M$,MC,R(2,6):MC=16384:GOTO10
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,:RETURN
8 FORC1=1TO2200:NEXT:RETURN
10 GOTO600
20 CLS
21 FORC1=0TO2:FORC2=0TO6:R(C1,C2)=.:NEXT:NEXT
25 L=1:Y=0:E=1:T=0
30 FORI=1TO6
40 R(2,I)=4
50 R(1,I)=4
60 NEXTI
69 GOSUB1000
70 A=1:B=0
80 S=0:X=0
87 PRINT@13*32+5,"   ";:PRINT@13*32+5,STR$(T);:PRINT@13*32+23,"   ";:PRINT@13*32+23,STR$(Y);
89 PRINT@6*32+3,"   ";:PRINT@6*32+25,"   ";:PRINT@10*32+3,"   ";:PRINT@10*32+25,"   ";
90 P=INT(((13*RND(0))+9)/2)*2+1
95 PRINT@2*32+14,"   ";:PRINT@2*32+14,STR$(P);
100 PRINT@6*32+14,"   ";:PRINT@6*32+14,STR$(P);
110 IF6<=10*RND(0)THEN225
115 PRINT@10*32+11," MY TURN ";:GOSUB8
120 E1=E
130 L1=L
140 E=1+(A/2-INT(A/2))*2
150 L=1+INT((P/6-INT(P/6))*6+.5)
160 IFR(E,L)>=PTHEN320
170 M=R(E,L)
180 IFM<=0THEN370
190 P=P-M:X=X+M
200 PRINT@6*32+3,"   ";:PRINT@6*32+3,STR$(M);
205 PRINT@6*32+14,"   ";:PRINT@6*32+14,STR$(P);:PRINT@10*32+3,"   ";:PRINT@10*32+3,STR$(X);
210 PRINT@10*32+11,"YOUR MOVE";
220 B=B+M
225 PRINT@10*32+11,"YOUR TURN";
230 PRINT@448+15,;:M$=INKEY$:IFM$=""THEN230
231 IFM$<"0"ORM$>"9"THENSOUND1,1:GOTO230
235 PRINT@6*32+25,"   ";:PRINT@6*32+25,STR$(M);
240 M=INT(VAL(M$))
245 S=S+M
247 PRINT@10*32+25,"   ";:PRINT@10*32+25,STR$(S);
250 IFM<1THEN450
260 IFM>4THEN460
270 IFM>PTHEN460
280 IFM=PTHEN360
290 P=P-M
295 PRINT@6*32+14,"   ";:PRINT@6*32+14,STR$(P);
300 A=A+M
310 GOTO115
320 PRINT@6*32+3,"   ";:PRINT@6*32+3,STR$(P);
330 PRINT@10*32+3,"   ";:PRINT@10*32+3,STR$(P+X);
340 R(E,L)=P
350 B=B+P
360 IFB/2=INT(B/2)THEN420
370 PRINT@10*32+11," ";:M$="YOU WIN":GOSUB7:PRINT" ";:GOSUB8
375 Y=Y+1
390 IFR(E,L)=1THEN480
400 R(E,L)=R(E,L)-1
410 GOTO70
420 PRINT@10*32+11,"  ";:M$="I WIN":GOSUB7:PRINT"  ";:GOSUB8
425 T=T+1
430 GOTO70
450 IFM=0THEN570
460 PRINT@10*32+11,;:M$="  WRONG  ":GOSUB7:GOSUB8
463 S=S-M
465 PRINT@10*32+25,"   ";:PRINT@10*32+25,STR$(S);
466 FORN=0TO10
467 NEXTN
468 PRINT@10*32+11,"TRY AGAIN";:GOSUB8
470 GOTO230
480 IFR(E1,L1)=1THEN70
490 R(E1,L1)=R(E1,L1)-1
500 GOTO70
570 PRINT@488,"PLAY AGAIN (Y/N)?";:M$=INKEY$:IFM$=""THEN570
580 IFM$="Y"THEN10
590 IFM$="N"THENEND
595 GOTO570
600 CLS:PRINT@5,;:M$="WELCOME TO EVEN STEVEN":GOSUB7:PRINT
610 PRINT
620 PRINT"THIS GAME HAS ARTIFICIAL INTEL-"
621 PRINT"LIGENCE AS YOU PLAY, IT GETS"
622 PRINT"SMARTER UNTIL, AFTER ABOUT 15"
623 PRINT"GAMES, IT WILL BE HARD TO BEAT"
624 PRINT:PRINT:PRINT"     HIT ANY KEY TO CONTINUE";
625 M$=INKEY$:C1=RND(0):IFM$=""THEN625
630 CLS:PRINTTAB(12);:M$="TO PLAY":GOSUB7:PRINT:PRINT
635 PRINT"THE GAME STARTS WITH AN ODD"
636 PRINT"NUMBER OF CHIPS AND EITHER GOES"
637 PRINT"ITSELF OR LETS YOU GO."
645 PRINT"THE OBJECT OF THE GAME IS TO END";
646 PRINT"UP WITH AN EVEN AMOUNT OF CHIPS.";
647 PRINT"YOU ARE ALLOWED TO TAKE 1 TO 4"
648 PRINT"CHIPS PER TURN."
650 PRINT"YOU TAKE THE CHIPS BY ENTERING"
651 PRINT"YOUR NUMBER AT THE 'YOUR MOVE'"
652 PRINT"PROMPT."
660 PRINT" TO START THE GAME PRESS g"
680 PRINT" TO STOP PLAYING PRESS ";:M$="0":GOSUB7:PRINT
690 M$=INKEY$:IFM$=""THEN690
700 IFM$="G"THEN20
711 IFM$="0"THEN570
720 GOTO690
1000 CLS:?"         STARTING TOTAL         ";
1001 PRINT"     ";
1002 PRINT"       ";:REM 4,13
1003 PRINT"                           ";
1004 PRINT" I TAKE BOARD TOTAL YOU TAKE ";
1005 PRINT"        ";
1006 PRINT"        ";:REM 10,2
1007 PRINT"        ";
1008 PRINT"MY TOTALYOUR TOTAL";
1009 PRINT"      ";
1010 PRINT"               ";:REM 17,2
1011 PRINT"     ";
1012 PRINT
1013 PRINT"I WON 0  GAMES  YOU WON 0  GAMES";:REM 21,0
1014 REM   01234567890123456789012345678901
1080 RETURN
