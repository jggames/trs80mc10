0 CLS0:CLEAR500,36857:DIMB,C,M,M$:M=16384:GOTO15
5 FORC=1TO10:SOUND113,1:NEXT:RETURN
6 I$="":C=(PEEK(17024)AND1)*256+PEEK(17025)+M:POKEC,96:B=C-M:GOTO8
7 B=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC=1TOLEN(M$):POKEB+C,ASC(MID$(M$,C))AND63:NEXT:?@B-M+C,;:RETURN
8 M$=INKEY$:IFM$=""THEN8
9 IFM$=CHR$(13)THEN13
10 IFM$=CHR$(8)THENIFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):POKEC,32:C=C-1:POKEC,96
11 IFM$<"0"ORM$>"9"ORLEN(I$)>2THEN8
12 POKEC,ASC(M$)AND63:C=C+1:POKEC,96:I$=I$+M$:GOTO8
13 POKEC,32:PRINT@B+32,;:RETURN
15 DIMC$(6,3),C1$(5),C2$(5),C3$(5),C4$(5),DE$(52),BE(2),C(6,3),GA(2),PA(2),HA,X,A,C,L,T,DE,RC,RE,ND,Z
20 REM CHANGE M=1024 FOR COCO
30 GOSUB5000:GOSUB7000
100 RESTORE:FORC=0TO51:READA$:NEXTC
110 ND=2:BE(1)=5:BE(2)=5
130 DE=1:RC=1:T=0
150 DATA AC,2C,3C,4C,5C,6C,7C,8C,9C,TC,JC,QC,KC
160 DATA AS,2S,3S,4S,5S,6S,7S,8S,9S,TS,JS,QS,KS
170 DATA AH,2H,3H,4H,5H,6H,7H,8H,9H,TH,JH,QH,KH
180 DATA AD,2D,3D,4D,5D,6D,7D,8D,9D,TD,JD,QD,KD
190 SU$="23456789TJQKA"
200 FORX=1TO5:C$(X,3)="œ":NEXTX
220 GOSUB9100
230 PO(1)=500:PO(2)=500
240 REM MAIN CODE
250 GOSUB570
260 GOSUB640
270 GOSUB1890:GOSUB1950
280 GOSUB700
290 GOSUB810
310 PRINT@485,;:M$="ANOTHER HAND (Y OR N)?":GOSUB7
320 A$=INKEY$:IFA$=""THEN320
330 IFA$="N"THEN370
340 IFA$="Y"THENRC=0:GOTO240
350 GOTO320
360 CLS:PRINT"THANKS FOR THE GAME.":PRINT"I WANNA KILL SHEEP NOW.":END
370 PRINT@485,;:M$="ANOTHER GAME (Y OR N)?":GOSUB7
371 A$=INKEY$:IFA$=""THEN371
372 IFA$="N"THEN360
373 IFA$="Y"THENRUN
380 GOTO371
390 REM SHUFFLE
400 RESTORE
410 FORX=0TO51
420 READDE$(X)
430 NEXTX
470 RETURN
480 REM
500 FORX=1TO5
510 GOSUB9300:GOSUB9400
520 PRINT@CL,CD$"œœœœ";
530 PRINT@CL+32,C1$(SU);
540 PRINT@CL+64,C2$(SU);
550 PRINT@CL+96,C3$(SU);
555 PRINT@CL+128,C4$(SU)CD$;
560 IF LEN(CD$)>2ANDSU=4THENPRINT@CL+128,"  " CHR$(131)" "; LEFT$(CD$,1);
562 CL=CL+6:NEXTX:IFRC=999THEN565
563 IFHA=2THENZ=49:FORT=M+387TOM+412STEP6:POKET,Z:Z=Z+1:NEXT
565 RETURN
570 REM DEAL HANDS
580 GOSUB390:EXEC36858
590 FORX=1TO5
600 HA=1:CL=33:GOSUB2010
610 HA=2:CL=225:GOSUB2010
620 NEXTX
630 RETURN
640 EXEC36858
650 GOSUB9600
660 IFDE=1THENHA=3:CL=33:GOSUB480
670 IFDE=0THENHA=1:CL=33:GOSUB1070
680 HA=2:CL=225:GOSUB1070:GOSUB480
690 RETURN
700 REM HANDLE DRAW
710 FORK=1TOND
730 PRINT@449,;:M$="HIT CARD NO. TO TRASH OR ENTER ":GOSUB7
735 I$=INKEY$:IFI$=""THEN735
740 A=VAL(I$)
750 IFA>5THENSOUND1,5:GOTO735
755 SOUND200,1
760 IFI$=CHR$(13)THEN790
765 IFA=0THENSOUND1,5:GOTO735
770 HA=2:X=A:GOSUB2010
780 GOSUB640
790 NEXTK
800 PRINT@449,;:M$="     PRESS C TO SEE HANDS      ":GOSUB7:CH=99
801 I$=INKEY$:IFI$<>"C"THEN801
805 RETURN
810 EXEC36858
820 GOSUB9600
830 GOSUB1630
840 HA=1:GOSUB1070:CL=33:GOSUB480
850 GOSUB900:RE=M+193:PRINT@32*6+1,;:GOSUB9500
860 HA=2:GOSUB1070:CL=225:RC=999:GOSUB480
870 GOSUB900:RE=M+385:PRINT@32*12+1,;:GOSUB9500
880 GOSUB1490
890 RETURN
900 REM HAND ANALYSER
910 HI(HA)=0
920 GOSUB1070
930 GOSUB1160:GOSUB1220:GOSUB1280:GOSUB1340:GOSUB1400
940 RE$="GARBAGE":GA(HA)=0
950 IFPA(HA)>.THENRE$=MID$(STR$(PA(HA)),2)+" PAIR":GA(HA)=PA(HA)
960 IFTH(HA)=1THENRE$="THREE OF A KIND":GA(HA)=3
970 IFSR(HA)=1THENRE$="STRAIGHT":GA(HA)=4
980 IFFL(HA)=1THENRE$="FLUSH":GA(HA)=5
990 IFPA(HA)=1ANDTH(HA)=1THENRE$="FULL HOUSE":GA(HA)=6
1000 IFFO(HA)=1THENRE$="FOUR OF A KIND":GA(HA)=7
1010 IFFL(HA)=1ANDSR(HA)=1THENRE$="STRAIGHT FLUSH":GA(HA)=8
1020 IFFL(HA)=1ANDSR(HA)=1ANDHI(HA)=13THENRE$="ROYAL FLUSH":GA(HA)=9
1030 GOSUB4050
1035 IFHI(HA)=0THENGOSUB4000
1040 REM FIND HIGH CARD
1050 RE$=RE$+" "+HI$+" HIGH"
1060 RETURN
1070 REM SORT HAND
1080 FORX=1TO5
1090 GOSUB3000
1100 NEXTX
1110 Z=0:FORX=1TO4
1120 IF C(X,HA)>C(X+1,HA) THEN HS=C(X,HA):C(X,HA)=C(X+1,HA):C(X+1,HA)=HS:HS$=C$(X,HA):C$(X,HA)=C$(X+1,HA):C$(X+1,HA)=HS$:Z=1
1130 NEXTX
1140 IFZ=1THEN1110
1150 RETURN
1160 REM LOOK FOR PAIRS
1170 PA(HA)=0
1180 FORX=1TO4
1190 IFC(X,HA)<>C(X-1,HA)ANDC(X,HA)=C(X+1,HA)ANDC(X,HA)<>C(X+2,HA)THENPA(HA)=PA(HA)+1:IFHI(HA)<C(X,HA)THENHI(HA)=C(X,HA)
1200 NEXTX
1210 RETURN
1220 REM LOOK FOR THREE OF A KIND
1230 TH(HA)=0
1240 FORX=1TO3
1250 IFC(X,HA)<>C(X-1,HA)ANDC(X,HA)=C(X+1,HA)ANDC(X,HA)=C(X+2,HA)ANDC(X,HA)<>C(X+3,HA)THEN1270
1260 NEXT X:RETURN
1270 TH(HA)=1:IFHI(HA)<C(X,HA)THENHI(HA)=C(X,HA):GOTO1260
1276 RETURN
1280 REM LOOK FOR FOUR OF A KIND
1290 FO(HA)=0
1300 FORX=1TO2
1310 IFC(X,HA)=C(X+1,HA)ANDC(X,HA)=C(X+2,HA)ANDC(X,HA)=C(X+3,HA)THENFO(HA)=1:IFHI(HA)<C(X,HA)THEN HI(HA)=C(X,HA)
1320 NEXTX
1330 RETURN
1340 REM LOOK FOR STRAIGHT
1350 SR(HA)=1
1360 FORX=1TO4
1370 IFC(X,HA)+1<>C(X+1,HA)THENSR(HA)=0
1380 NEXTX
1390 RETURN
1400 REM LOOK FOR FLUSH
1410 FL(HA)=1
1420 FORX=2TO5
1430 IFRIGHT$(C$(1,HA),1)<>RIGHT$(C$(X,HA),1)THENFL(HA)=0
1440 NEXTX
1450 RETURN
1490 REM WHO WON?
1500 WI=0
1510 IFGA(1)>GA(2)THENWI=1
1520 IFGA(2)>GA(1)THENWI=2
1530 IFWI<>0THEN1560
1540 IFHI(1)>HI(2)THENWI=1
1550 IFHI(2)>HI(1)THENWI=2
1560 IFWI=1THENWI$="I WIN!!!!":PO(1)=PO(1)+BE(2):PO(2)=PO(2)-BE(2)
1570 IFWI=2THENWI$="YOU WIN... ":PO(2)=PO(2)+BET(1):PO(1)=PO(1)-BET(1)
1580 IFWI=0THENWI$="DRAW. ALL BETS CARRIED..."
1590 PRINT@417,;:M$=WI$:GOSUB7
1595 PRINT@449,;:M$="YOUR POT: $"+MID$(STR$(PO(2)),2+(PO(2)<0))+"   MY POT: $"+MID$(STR$(PO(1)),2+(PO(1)<0)):GOSUB7
1596 IFPO(2)<1THENGOSUB5:PRINT@428,;:M$="YOU'RE BUSTED.":GOSUB7:GOTO370
1597 IFPO(1)<1THENGOSUB5:PRINT@428,;:M$="I'LL PAY YA LATER.":GOSUB7:GOTO370
1610 BE(1)=0:BE(2)=0
1620 RETURN
1630 REM COMPUTER ANALYSES ITS HAND
1640 DR=0
1650 DR=DR+1:IFDR>ND THEN1880
1660 HA=1
1670 GOSUB1070
1680 GOSUB1400:IFFL(HA)=1THEN1880
1690 GOSUB1340:IFSR(HA)=1THEN1880
1700 GOSUB1280:IFFO(HA)=1THEN1880
1710 HI(HA)=0:GOSUB1220
1720 IFTH(HA)=0THEN1780
1730 GOSUB1160:IFPA(HA)=1THEN1880
1740 X=1
1750 IFC(X,HA)=HI(HA)THENX=X+1:GOTO1750
1760 GOSUB2010
1770 GOSUB1070:GOTO1650
1780 REM WE HAVE, AT BEST, TWO PAIR
1790 GOSUB1160
1800 IFPA(HA)=0THENX=1:GOSUB2010:GOTO1650
1810 IFPA=2THEN1850
1820 X=1
1830 IFC(X,HA)=HI(HA)THENX=X+1:GOTO1830
1840 GOSUB2010:GOTO1650
1850 IFC(1,HA)<>C(2,HA)THENX=1:GOSUB2010:GOTO1650
1860 IFC(5,HA)<>C(4,HA)THENX=5:GOSUB2010:GOTO1650
1870 X=3:GOSUB2010:GOTO1650
1880 RETURN
1890 REM COMPUTER DETERMINES ITS BET
1900 HA=1
1910 GOSUB900:REM SEE WHAT IT'S GOT
1920 BE(HA)=BE(HA)+7+(10*GA(HA))+INT(RND(0)*9)
1930 PRINT@449,;:M$="I BET $"+MID$(STR$(BE(HA)),2):GOSUB7
1940 RETURN
1950 REM GET PLAYER'S BET
1960 HA=2:PRINT@472,;:M$="        ":GOSUB7
1970 PRINT@462,;:M$="YOUR BET? ":GOSUB7:GOSUB6
1975 IFVAL(I$)>100THENPRINT@481,;:M$="*******SORRY $100 LIMIT*******":GOSUB7:GOTO1960
1980 BE(HA)=VAL(I$)
1990 IFBE(2)<BE(1)THENPRINT@481,;:M$="*YOU MUST SEE OR RAISE MY BET*":GOSUB7:GOTO1960
2000 BE(1)=BE(2)
2010 REM DEAL ONE CARD
2020 J=RND(52)-1:IF DE$(J)="XX" THEN 2020
2030 C$(X,HA)=DE$(J):DE$(J)="XX"
2040 RETURN
3000 REM REPLACEMENT FOR INSTR
3010 M$=LEFT$(C$(X,HA),1):IFM$="2"THENC(X,HA)=2
3020 IFM$="3"THENC(X,HA)=3
3030 IFM$="4"THENC(X,HA)=4
3040 IFM$="5"THENC(X,HA)=5
3050 IFM$="6"THENC(X,HA)=6
3060 IFM$="7"THENC(X,HA)=7
3070 IFM$="8"THENC(X,HA)=8
3080 IFM$="9"THENC(X,HA)=9
3090 IFM$="T"THENC(X,HA)=10
3100 IFM$="J"THENC(X,HA)=11
3110 IFM$="Q"THENC(X,HA)=12
3120 IFM$="K"THENC(X,HA)=13
3130 IFM$="A"THENC(X,HA)=14
3140 RETURN
4000 REM WHAT'S HIGH?
4010 FORX=1TO5
4020 IFC(X,HA)>C(X+1,HA)THENHI(HA)=C(X,HA):GOTO4040
4030 HI(HA)=C(X+1,HA)
4040 NEXTX
4050 IFHI(HA)=14THENHI$="ACES"
4060 IFHI(HA)=13THENHI$="KINGS"
4070 IFHI(HA)=12THENHI$="QUEENS"
4080 IFHI(HA)=11THENHI$="JACKS"
4090 IFHI(HA)<11ANDHI(HA)>0THENHI$=MID$(STR$(HI(HA)),2)+"S"
4095 RETURN
5000 GOSUB390:A=36858:FORI=0TO4:READB:POKEA+I,B:NEXT:RETURN
5010 DATA198,32,126,251,214
7000 POKE49151,64:CLS
7010 PRINTTAB(12)"CN! POKER":PRINT
7020 PRINT" PLAYING POKER WITH A COMPUTER   HAS ITS ADVANTAGES. IT DOESN'T  CHEAT, IT DOESN'T TRY TO READ"
7030 PRINT" YOUR FACE AND IF YOU FIND       YOURSELF IN THE HOLE TO IT,YOU  CAN SHUT IT OFF. HERE'S A BYTE"
7040 PRINT" OF SOFTWARE TO DEAL YOUR        SYSTEM. YOU START WITH $500.00"
7050 PRINT:PRINT"        ---------------":PRINT"        BY STEVE RIMMER"
7060 PRINT"        ---------------"
7070 PRINT@480,"     PRESS ANY KEY TO BEGIN";
7080 I$=INKEY$:T=RND(0):IFI$=""THEN7080
7090 CLS0:SOUND113,1:POKE49151,0:EXEC36858:RETURN
9100 C1$(1)="Œ≥Ã≥Õ":C1$(2)="œŒ¿Õœ":C1$(3)="œ ¿≈œ":C1$(4)="œ»≥ƒœ":C1$(5)="œœœœœ"
9110 C2$(1)=" øøø≈":C2$(2)="Œ¿¿¿Õ":C2$(3)=" ¿¿¿≈":C2$(4)=" ∑øª≈":C2$(5)="œœœœœ"
9120 C3$(1)="À¥ø∏«":C3$(2)="À√¿√«":C3$(3)="À√¿√«":C3$(4)="À¥ø∏«":C3$(5)="œœœœœ"
9130 C4$(1)="œÀ√«":C4$(2)="œœ√œ":C4$(3)="œœ√œ":C4$(4)="œÀ√«":C4$(5)="œœœœ"
9300 REM
9310 IFRIGHT$(C$(X,HA),1)="H"THENSU=1
9320 IFRIGHT$(C$(X,HA),1)="S"THENSU=2
9330 IFRIGHT$(C$(X,HA),1)="C"THENSU=3
9340 IFRIGHT$(C$(X,HA),1)="D"THENSU=4
9350 IFHA=3THENSU=5
9355 RETURN
9400 CD$=LEFT$(C$(X,HA),1)
9460 RETURN
9500 M$=RE$:GOSUB7:RETURN
9600 PRINT@12,;:M$="CN! POKER":GOSUB7:RETURN
9710 REM POKER!!!
9720 REM COPYRIGHT 1984 (C) STEVE RIMMER
9730 REM MC-10 EDITS JIM GERRIE
