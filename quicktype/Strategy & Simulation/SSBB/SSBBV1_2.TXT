1 REM SSBB FOR TS 2068
9 CLS 
10 LET DEBUG=0
11 165 9700:CLS
12 ING=1
15 165 9800
16 RANDOMIZE 
18 OUTS=0:GO SUB 165
20 GOSUB1000
25 IF ING>=9 AND HV=2 AND T(1)>T(2) THEN60
30 IF OUTS<3 THEN20
31 IF L(HV,9)<300 THEN : LET HP=HVP: LET HVP=HV :GOSUB3200: LET HVP=HP 
33 FOR I=1 TO 3: LET R(I)=0: NEXT I
35 IF HV=1 THEN LET ING=ING+1  
38 IF HV=2 AND ING > 9 AND T(1)<>T(2) THEN60
39 IF ING=9 AND HV=2 AND T(1)>T(2) THEN60
40 IF ING>9 AND HV=1 AND T(1)<>T(2) THEN60
45 IF HV=2 THEN LET HV=1: LET HVP=2:GOSUB2030:GOTO18
47 IF HV=1 THEN LET HV=2: LET HVP=1:GOSUB2030
49 PAPER 1: INK 7: PRINT AT 3,14;ING: PAPER 7: INK 0
50 GOTO18
60 IF AL=1 THEN LET Q$="(AL)": LET V$="(NL)"
61 IF AL=2 THEN LET Q$="(NL)": LET V$="(AL)"
62 LET M$="          FINAL SCORE:"+F$
63 LET M$=M$+"          HOME    "+Q$+" : "+STR$ T(1)+F$
64 LET M$=M$+"          VISITOR "+V$+" : "+STR$ T(2)
65 LET M$=M$+F$+F$+"     PRESS ENTER TO QUIT"
70 GOSUB170: INPUT P$
99 STOP 
100 REM  DICE
101 LET D1=RND*6: LET D1=INT D1 +1
102 LET D2=RND*6: LET D2=INT D2 +1
103 LET D3=RND*6: LET D3=INT D3 +1
104 LET ROLL=10*X(D1)+Y(D2)+Z(D3)
105 RETURN 
110 REM  UDBASE
112 PAPER 0 : INK 7
114 LET PNUM=R(1):GOSUBGTNAME: PRINT AT 6,9;Z$(1 TO 23): PRINT AT 6,9;N$
116 LET PNUM=R(2):GOSUBGTNAME: PRINT AT 7,9;Z$(1 TO 23): PRINT AT 7,9;N$
118 LET PNUM=R(3):GOSUBGTNAME: PRINT AT 8,9;Z$(1 TO 23): PRINT AT 8,9;N$
120 INK 7-(R(1)>0): PRINT AT 7,2;"\B";
122 INK 7-(R(2)>0): PRINT AT 6,1;"\B";
124 INK 7-(R(3)>0): PRINT AT 7,0;"\B";
126 RETURN 
130 REM  ADD1
131 LET S(HV,ING)=S(HV,ING)+1: LET T(HV)=T(HV)+1: RETURN 
135 REM  GTNAME\#020\#000
136 IF PNUM=0 THEN LET N$="": RETURN 
137 RESTORE 9000+PNUM: READ N$,H$(1)
139 RETURN 
140 REM  UDSCORE
141 PAPER 1: INK 7
143 IF HV=2 THEN PRINT AT 1,(2+(3*(ING-1)));S(2,ING)
145 IF HV=1 THEN PRINT AT 2,(2+(3*(ING-1)));S(1,ING)
147 REM LET HS=0: LET VS=0: FOR I = 1 TO ING: LET HS=HS+S(1,I): LET VS=VS+S(2,I) : NEXT I
148 PRINT AT 1,29;T(2): PRINT AT 2,29;T(1)
149 RETURN 
160 REM  UDHV
161 PAPER 1: INK 7
162 IF HV=1 THEN PRINT AT 1,0;"V": PAPER 7: INK 0: PRINT AT 2,0;"H"
163 IF HV=2 THEN PRINT AT 2,0;"H": PAPER 7: INK 0: PRINT AT 1,0;"V"
164 RETURN 
165 REM UDOUT
166 PAPER 1: INK 7
167 PRINT AT 3,23;OUTS;
169 RETURN 
170 REM  DSPMSG
171 PAPER 7: INK 0
172 FOR I=13 TO 21: PRINT AT I,0;Z$: NEXT I
173 PRINT AT 14,0;M$
179 RETURN 
180 REM  GTPNAME
185 IF PNUM=0 THEN LET N$="": RETURN 
187 RESTORE 9300+PNUM: READ N$, H$(1)
189 RETURN 
200 REM  STEAL 2
202 IF R(1)=0 OR R(2)>0 THEN LET M$="CAN NOT STEAL SECOND":GOSUB170: RETURN 
204 LET PNUM=R(1):GOSUBGTNAME
206 RESTORE 9250+VAL (H$(1,2)): READ S$
208 GOSUB100: LET S$=S$(ROLL-9)
210 LET BASE=2
212 IF S$="X" OR S$="P" THENGOSUB300:GOSUB170: RETURN 
214 IF S$=" " OR S$="=" OR S$="E" THENGOSUB305:GOSUB170: RETURN 
216 IF S$="B" THENGOSUBBALK:GOSUB170: RETURN 
229 INPUT P$: RETURN 
230 REM  STEAL 3
232 IF R(2)=0 OR R(3)>0 THEN LET M$="CAN NOT STEAL THIRD":GOSUB170: RETURN 
234 LET PNUM=R(2):GOSUBGTNAME
236 RESTORE 9260+VAL (H$(1,2)): READ S$
238 GOSUB100: LET S$=S$(ROLL-9)
240 LET BASE=3
242 IF S$="X" OR S$="P" THENGOSUB300:GOSUB170: RETURN 
244 IF S$=" " OR S$="=" OR S$="E" THENGOSUB305:GOSUB170: RETURN 
246 IF S$="B" THENGOSUBBALK:GOSUB170: RETURN 
249 INPUT P$: RETURN 
250 REM  STL H
252 IF R(3)=0 THEN LET M$="CAN NOT STEAL HOME":GOSUB170: RETURN 
254 LET PNUM=R(3):GOSUBGTNAME
256 RESTORE 9270+VAL (H$(1,2)): READ S$
258GOSUB100: LET S$=S$(ROLL-9)
260 LET BASE=4
262 IF S$="X" OR S$="P" THENGOSUB300:GOSUB170: RETURN 
264 IF S$=" " OR S$="=" OR S$="E" THENGOSUB305:GOSUB170: RETURN 
266 IF S$="B" THENGOSUBBALK:GOSUB170: RETURN 
269 INPUT P$: RETURN 
300 REM  STLX\#020\#000
301 IF S$="X" THEN LET M$="RUNNER IS TRYING TO STEAL"+F$+"HERE'S THE THROW, RUNNER IS"+F$+"OUT"
302 IF S$="P" THEN LET M$="RUNNER TAKES OFF, AND IS PICKED OFF" 
303 LET OUTS=OUTS+1: LET R(BASE-1)=0 :GOSUBUDBASE :GOSUB165: IF OUTS>=3 THEN LET M$=M$+F$+F$+"3 OUT, THAT ENDS THE INNING"
304 RETURN 
305 REM  STEAL SAFE\#020\#000
306 IF BASE<4 THEN LET R(BASE)=R(BASE-1)
307 LET R(BASE-1)=0 :GOSUBUDBASE: IF BASE=4 THENGOSUBADD1:GOSUBUDSCORE
308 LET M$="RUNNER IS TRYING TO STEAL"+F$+"HERE'S THE THROW, RUNNER IS"+F$+"SAFE"
310 IF S$<>"=" THEN RETURN 
312 LET M$=M$+F$+F$+"AND THE BALL BOUNDS AWAY,"+F$+"RUNNERS ADVANCE AN EXTRA BASE"
314 IF R(3)>0 THENGOSUBADD1:GOSUBUDSCORE: LET R(3)=0
316 LET R(3)=R(2): LET R(2)=R(1)
317 PAUSE 60:GOSUBUDBASE
329 RETURN 
330 REM  BALK
331 IF R(3)>0 THENGOSUBADD1:GOSUBUDSCORE
332 LET R(3)=R(2): LET R(2)=R(1): LET R(1)=0:GOSUBUDBASE
333 LET M$="RUNNER ON 1ST IS TRYING TO STEAL, THE PITCHER BALKS, ALL RUNNERS ADVANCE 1 BASE" 
334 RETURN 
1000 REM  PLAY
1005 LET OK=1: LET TK=0
1010 LET B(HV)=B(HV)+1: IF B(HV)>9 THEN LET B(HV)=1
1015 GOSUB8060:GOSUB8070
1020 IF OUTS>=3 THEN RETURN 
1022 IF ING>=9 AND HV=1 AND T(1)>T(2) THEN RETURN 
1025 INPUT "S,B,2,3,H,P,I,A,U,D,M,Q:";A$: LET M$="":GOSUB170
1026 IF A$="" THEN1025
1028 IF A$="Q" OR A$="Q" THEN STOP 
1029 IF A$="B" OR A$="B" THENGOSUB3000:GOTO1020
1030 IF A$="S" OR A$="S" THENGOSUB2000
1031 IF A$="2" THENGOSUB200:GOTO1020
1032 IF A$="3" THENGOSUB230:GOTO1020
1033 IF A$="H" OR A$="H" THENGOSUB250:GOTO1020
1034 IF A$="P" OR A$="P" THEN LET HVP=0:GOSUB3200:GOTO1020
1036 IF A$="I" OR A$="I" THEN LET R$="B":GOSUB8420:GOSUB170:GOSUB2030
1037 IF A$="A" OR A$="A" OR A$="U" OR A$="U" THENGOSUB4000: IF OK=1 THENGOSUB170:GOSUB2030
1038 IF (A$="A" OR A$="A" OR A$="U" OR A$="U") AND OK=0 THEN1020
1039 IF A$="D" OR A$="D" THENGOSUB1100:GOSUB3000 : LET HV=HH :GOTO1020
1070 IF A$="M" OR A$="M" THENGOSUB6000:GOTO1020
1080 IF OUTS>=3 THEN INPUT P$: LET M$="":GOSUB170
1099 RETURN 
1100 REM  TEMP SWAP HV
1105 LET HH=HV
1110 IF HV=1 THEN LET HV=2: RETURN 
1115 IF HV=2 THEN LET HV=1: RETURN 
1119 RETURN 
2000 REM  PITCH BAT
2001 LET R$=""
2002 GOSUB100
2005 LET R$=K$(ROLL-9): IF DEBUG=1 THEN PRINT AT 20,25;ROLL;" ";R$;
2006 IF TK=1 AND (ROLL=35 OR ROLL=36) THEN LET R$="K"
2007 IF R$="#" THENGOSUB4500
2008 IF R$="X" THENGOSUB100: LET R$=X$(ROLL-9)
2009 IF R$<>" " THEN2025
2010 GOSUB100
2020 LET R$=B$(PH,ROLL-9): IF DEBUG=1 THEN PRINT AT 21,27;ROLL;: PRINT AT 21,29;R$;
2025 GOSUB8100
2030 GOSUB165
2031 GOSUBUDBASE  
2032 GOSUBUDHV
2045 GOSUBUDSCORE
2055 PAPER 7: INK 0
2199 RETURN 
3000 REM  LINE UP
3005 LET MIN=10: IF ING=0 THEN LET MIN=1
3006 LET CK=0
3010 PAPER 1: INK 7: CLS : PAPER 7: INK 0: PRINT AT 1,0;Z$: PRINT AT 12,0;Z$;
3012 LET T$="HOME" : IF HV=2 THEN LET T$="VISTOR"
3015 PRINT AT 1,4;T$;" LINE UP": PRINT AT 1,20;"DEF RATE: ";D(HV,10);
3016 PRINT AT 12,4;"BENCH": PAPER 1: INK 7
3017 LET T$=""
3020 FOR I = 1 TO 16 
3025 LET PN=L(HV,I)
3030 PRINT AT 1+I+((I>9)*2),0;I;" ";
3035 GOSUB7000
3038 NEXT I
3040 PAPER 7: INK 0: PRINT AT 1,30;D(HV,10);: INK 1: PAPER 1
3045 INPUT "# FROM LINEUP (QUIT=0) : ";Z1 : IF Z1 <0 OR Z1 > 9 THEN3045
3046 IF Z1=0 THENGOSUBCKPOS: IF OK=1 THEN3096
3047 IF Z1=0 THEN3040
3049 PAPER 1: PRINT AT 21,0;Z$;
3050 INK 0: PAPER 7: PRINT AT Z1+1,0;">>";
3052 INPUT "REPLACE WITH #(99=CHANGE POS)";Z2: IF Z2=0 THEN3096
3054 IF Z2=99 THENGOSUBSWPOS:GOTO3040
3056 IF Z2<MIN OR Z2>16 THEN3055
3057 IF L(HV,Z2)<0 THEN PRINT AT 20,0;"PLAYER NOT AVAILABLE":GOTO3055
3060 PRINT AT Z2+1+((Z2>9)*2),0;">>";
3065 INPUT "CONFIRM SWICTH (Y/N): ";P$
3068 IF P$="N" OR P$="N" THENGOSUB3100:GOTO3040
3070 IF P$="Y" OR P$="Y" THEN LET CK=1:GOSUB7100 :GOTO3040
3096 IF A$="D" OR A$="D" THEN LET HV=HH
3097 IF ING>0 THENGOSUB9800:GOSUBUDHV:GOSUB8020:GOSUBUDBASE:GOSUB8060 :GOSUB8070
3099 RETURN 
3100 REM  NO SWAP
3105 INK 7: PAPER 1
3110 PRINT AT Z1+1,0;Z1;" ";
3115 PRINT AT Z2+1+(Z2>9)*2,0;Z2;" ";
3119 RETURN 
3200 REM  PITCHER SELECTION
3201 IF HVP=0 AND HV=1 THEN LET HVP=2
3202 IF HVP=0 THEN LET HVP=1
3205 LET MIN=6: LET MAX=9: IF ING=0 THEN LET MIN=1: LET MAX=5
3210 PAPER 1: CLS : PAPER 7 : INK 0
3212 PRINT AT 1,0;Z$: PRINT AT 1,3;"STARTERS": PRINT AT 8,0;Z$: PRINT AT 8,3;"RELIEVERS"
3215 PAPER 1: INK 7
3220 FOR I=1 TO 9
3225 LET PN =P(HVP,I)
3228 IF PN=B(HVP+2) THEN LET Z1=I
3230 PRINT AT 1+I+((I>5)*2),0;I;" ";
3235 GOSUB7300
3238 NEXT I
3245 INPUT "NEW PITCHER? ("+STR$ (MIN)+"-"+STR$ (MAX)+" QUIT=0):";Z2
3246 PRINT AT 20,0;Z$
3248 IF Z2=0 THENGOSUB3096: RETURN 
3249 IF Z2<MIN OR Z2>MAX THEN3245
3250 IF P(HVP,Z2)<0 OR Z1=Z2 THEN PRINT AT 20,0;"PITCHER NOT AVAILABLE";:GOTO3245
3255 INK 0: PAPER 7: PRINT AT (Z2+1)+(Z2>5)*2,0;">>";
3260 INPUT "CONFIRM CHANGE (Y/N) : ";P$
3270 IF P$="Y" OR P$="Y" THENGOSUB7150
3375 GOTO3245
4000 REM  BUNT\#020\#000
4001 LET OK=1: LET TK=0
4002 IF A$="A" OR A$="A" THENGOSUB4225
4002 IF A$="U" OR A$="U" AND R(3)=0 THEN LET OK=0
4003 IF OK=0 AND (A$="A" OR A$="A") THEN LET M$="YOU CAN'T SAC BUNT":GOSUB170: RETURN 
4004 IF OK=0 AND (A$="U" OR A$="U") THEN LET M$="YOU CAN'T SUICIDE SQUEEZE":GOSUB170: RETURN 
4010 IF A$="A" OR A$="A" THEN LET BUNT=9231
4015 IF A$="U" OR A$="U" THEN LET BUNT=9236
4020 LET PNUM=L(HV,B(HV)):GOSUBGTNAME
4025 IF H$(1,3)="B" THEN LET BUNT=BUNT+1
4030 RESTORE BUNT : READ Q$
4035 GOSUB100 
4037 LET R$=Q$(ROLL-9)
4038 IF R$<>"2" AND R$<>" " THEN LET TK=0
4040 IF R$="S" THENGOSUB4200
4041 IF R$="1" THENGOSUB8500
4042 IF R$="E" THENGOSUB8500
4043 IF R$="F" THENGOSUB4210
4044 IF R$="G" THEN LET OUTS=OUTS+1: LET M$=M$+" AND GROUNDS OUT":GOSUB8610
4045 IF R$="2" THENGOSUB4300 : IF I$="Y" THEN4035
4046 IF R$="P" THENGOSUB8600
4047 IF R$=" " THENGOSUB4250 : IF I$="Y" THEN4035
4048 IF TK=1 THEN LET OK=0: RETURN 
4050 LET M$="THE BATTER BUNTS,"+F$+M$
4099 RETURN 
4200 REM  S BUNT
4202 LET OUTS=OUTS+1
4204 IF OUTS>=3 THEN LET M$="AND IS OUT AT FIRST"+F$+"3 OUTS, ENDS THE INNING": RETURN 
4205 IF R(3)=0 THEN LET R(3)=R(2): LET R(2)=0
4206 LET R(2)=R(1): LET R(1)=0
4206 LET M$="OUT AT FIRST RUNNERS ON"+F$+"FIRST & SECOND ADVANCE"
4209 RETURN 
4210 REM  F BUNT
4211 LET OUTS=OUTS+1
4212 LET M$="HE POPS OUT, RUNNERS HOLD"
4213 IF OUTS>=3 THEN LET M$=M$+F$+"3 OUTS, THAT ENDS THE INNING"
4214 RETURN 
4225 REM  VER SACBUNT
4226 IF R(1)=0 AND R(2)=0 THEN LET OK=0: RETURN 
4227 IF R(1)=0 AND R(2)>0 AND R(3)>0 THEN LET OK=0: RETURN 
4228 IF R(1)>0 AND R(2)>0 AND R(3)>0 THEN LET OK=0: RETURN 
4229 RETURN 
4250 REM  BLANK
4252 LET I$="N"
4253 IF TK=1 THENGOSUB8400: LET OUTS=OUTS+1: IF OUTS>=3 THEN LET M$=M$+F$+"3 OUTS, THAT ENDS THE INNING":GOSUB165: RETURN 
4254 IF TK=1 THENGOSUB170 :GOSUB165: INPUT O$
4260 LET M$="MISSED BUNT ON SQUEEZE":GOSUB170 : INPUT "PRESS ENTER";O$:GOSUB250
4268 INPUT O$
4270 IF OUTS>=3 THEN LET M$=M$+F$+"3 OUTS, THAT ENDS THE INNING": RETURN 
4275 GOSUB4310
4299 RETURN 
4300 REM  2K\#020\#000
4302 LET I$="N"
4305 IF TK=1 THENGOSUB8400: LET OUTS=OUTS+1: IF OUTS>=3 THEN LET M$=M$+F$+"3 OUTS, THAT ENDS THE INNING"
4306 IF TK=1 THEN LET TK=0: RETURN 
4310 LET TK=1
4320 INPUT "TWO FOULS, BUNT AGAIN? (Y/N)";I$
4330 IF I$="Y" THEN LET I$="Y"
4499 RETURN 
4500 REM  DEF OUT
4505 LET DF=D(1,10): LET R=ROLL
4510 IF HV=1 THEN LET DF=D(2,10)
4515 LET R$=" "
4520 IF (DF>=0 AND DF<=9) AND R=10 THEN LET R$="G"
4521 IF (DF>=10 AND DF<=13) AND R=11 THEN LET R$="G"
4522 IF (DF>=14 AND DF<=18) AND R=12 THEN LET R$="G"
4523 IF (DF>=19 AND DF<=22) AND R=13 THEN LET R$="G"
4524 IF (DF>=23 AND DF<=27) AND R=14 THEN LET R$="G"
4525 IF (DF>=28 AND DF<=31) AND (R=11 OR R=12) THEN LET R$="G"
4526 IF (DF>=32 AND DF<=36) AND (R=11 OR R=13) THEN LET R$="G"
4527 IF (DF>=37 AND DF<=40) AND (R=12 OR R=13) THEN LET R$="G"
4528 IF (DF>=41 AND DF<=45) AND (R=12 OR R=14) THEN LET R$="G"
4529 IF (DF>=46 AND DF<=49) AND (R=13 OR R=14) THEN LET R$="G"
4530 IF (DF>=50 AND DF<=54) AND (R=11 OR R=12 OR R=13) THEN LET R$="G"
4531 IF (DF>=55 AND DF<=58) AND (R=11 OR R=12 OR R=14) THEN LET R$="G"
4532 IF (DF>=59 AND DF<=63) AND (R=11 OR R=13 OR R=14) THEN LET R$="G"
4533 IF (DF>=64 AND DF<=67) AND (R=12 OR R=13 OR R=14) THEN LET R$="G"
4534 IF (DF>=68 AND DF<=72) AND (R=12 OR R=14 OR R=15) THEN LET R$="G"
4535 IF (DF>=73 AND DF<=76) AND (R=13 OR R=14 OR R=15) THEN LET R$="G"
4536 IF (DF>=77 AND DF<=81) AND (R=10 OR R=12 OR R=14 OR R=15) THEN LET R$="G"
4537 IF (DF>=82 AND DF<=85) AND (R=11 OR R=12 OR R=14 OR R=15) THEN LET R$="G"
4539 RETURN 
6000 REM  MENU
6005 LET M$="S: BATTER SWINGS   I: INT. WALK"+F$
6008 LET M$=M$+"B: BATTING LINEUP  D: DEF LINEUP"+F$
6010 LET M$=M$+"2: STEAL 2ND       M: MENU"+F$
6015 LET M$=M$+"3: STEAL 3RD       Q: QUIT"+F$
6020 LET M$=M$+"H: STEAL HOME"+F$
6023 LET M$=M$+"P: PITCHER CHANGE"+F$
6025 LET M$=M$+"A: SAC. BUNT"+F$
6030 LET M$=M$+"U: SUCICDE SQUEEZE"
6080GOSUB170
6090 RETURN   
7000 REM  LINE UP PASS PN
7005 IF PN<0 THEN INK 0: LET PN=PN*-1
7010 LET PNUM=PN:GOSUBGTNAME
7011 IF I<10 THEN PRINT TAB 2;L$(HV,I);
7012 IF ING=0 OR A$="D" OR A$="D" THEN7015
7013 IF L(HV,B(HV))=PN THEN PRINT TAB 3;"*";
7015 PRINT TAB 4; N$;
7016 IF PNUM>300 THEN PRINT TAB 26;"P";:GOTO7055
7017 PRINT TAB 22; H$(1,1);
7018 PRINT TAB 23; H$(1,2);
7020 PRINT TAB 24; H$(1,3);
7030 FOR J=4 TO 20 STEP 4 :
7031 IF H$(1,J TO J+1)="CF" THEN PRINT TAB 25+(J/4);"\C"; :GOTO7050
7032 IF H$(1,J TO J+1)="OF" THEN PRINT TAB 25+(J/4);"\O"; :GOTO7050
7040 PRINT TAB 25+(J/4);H$(1,J);
7050 NEXT J
7055 PRINT : INK 7 
7059 RETURN 
7060 REM  SWAP BASES
7061 FOR C=1 TO 3
7062 IF R(C)=HOLD THEN LET R(C)=PNUM
7063 NEXT C
7064 RETURN 
7100 REM  SWAP PLRS
7103 LET OK=1
7105 IF Z2>9 THEN LET T$=L$(HV,Z1): LET PNUM=L(HV,Z2):GOSUBGTNAME:GOSUBPLPOS
7106 IF OK=0 THEN7120
7110 LET HOLD=L(HV,Z1): LET L(HV,Z1)=L(HV,Z2): IF Z1=9 THEN LET HOLD=L(HV,Z2)
7111 IF Z2<10 THEN LET U$=L$(HV,Z1): LET L$(HV,Z1)=L$(HV,Z2): LET L$(HV,Z2)=U$
7112 IF ING>0 THEN LET L(HV,Z2)=HOLD*-1:GOSUB7060
7114 IF ING=0 THEN LET L(HV,Z2)=HOLD
7118 IF Z2>9 THEN LET HOLD=D(HV,Z1): LET HOLD=DFRAT-HOLD: LET D(HV,10)=D(HV,10)+HOLD: LET D(HV,Z1)=DFRAT
7120 PAPER 1: INK 7
7130 PRINT AT Z1+1,0;Z1;: LET PN=L(HV,Z1): LET I=Z1:GOSUB7000
7140 PRINT AT Z2+1+((Z2>9)*2),0;Z2;: LET PN=L(HV,Z2): LET I=Z2:GOSUB7000
7149 RETURN 
7150 REM  SWAP PITCH\#020\#000 
7160 IF ING>0 THEN LET P(HVP,Z1)=-1*P(HVP,Z1)
7170 LET B(HVP+2)=P(HVP,Z2)
7172 LET L(HVP,9)=P(HVP,Z2)+300
7175 PAPER 1: INK 7
7180 PRINT AT Z2+1+(Z2>5)*2,0;Z2;" *";
7190 PRINT AT (Z1+1)+(Z1>5)*2,0;Z1;"  ";
7192 IF ING>0 THEN LET PN=P(HVP,Z1):GOSUB7300
7195 LET Z1=Z2: INK 7
7199 RETURN 
7200 REM  SWPOS
7201 LET C$=L$(HV,Z1): PAPER 1: INK 7
7202 INPUT "ENTER NEW POSITION:             (C,1,2,S,3,L,CF,R,P,PH) : ";P$
7203 LET PNUM=L(HV,Z1):GOSUBGTNAME
7204 PRINT AT 21,0;Z$;
7210 LET T$=P$:GOSUBPLPOS
7218 IF OK=0 THEN7202 
7220 IF P$="R" THEN LET L$(HV,Z1)="\R"
7221 IF P$="CF" THEN LET L$(HV,Z1)="\C"
7222 IF P$="L" THEN LET L$(HV,Z1)="\L"
7223 IF P$="C" THEN LET L$(HV,Z1)="C"
7224 IF P$="S" THEN LET L$(HV,Z1)="S"
7225 IF P$="P" THEN LET L$(HV,Z1)="P"
7226 IF CODE (P$(1)) < 52 THEN LET L$(HV,Z1)=P$
7230 PRINT AT Z1+1+((Z1>9)*2),0;Z1;" ";L$(HV,Z1);" ";
7235 LET HOLD=D(HV,Z1): LET HOLD=DFRAT-HOLD: LET D(HV,10)=D(HV,10)+HOLD
7237 LET D(HV,Z1)=DFRAT
7299 RETURN 
7300 REM  PRINT PITCHER
7302 INK 7
7305 IF PN<0 THEN INK 0: LET PN=PN*-1
7310 LET PNUM=PN :GOSUBGTPNAME
7323 IF B(HVP+2)=PN THEN PRINT TAB 2;"*";
7325 PRINT TAB 3;N$;
7330 PRINT TAB 22;H$(1,1);
7335 INK 7
7399 RETURN 
7400 REM  CKPOS
7405 LET OK=1: IF CK=0 THEN RETURN 
7410 LET J=0
7411 LET J=J+1: LET K=J+1
7412 IF J>8 THEN RETURN 
7415 LET C$=L$(HV,J)
7420 IF C$=L$(HV,K) THEN LET OK=0: PAPER 1: INK 7: PRINT AT 21,0;"TWO PLAYERS HAVE SAVE POSITION";: RETURN 
7422 LET K=K+1 : IF K>9 THEN7411
7425GOTO7415
7450 REM  PLPOS PASS IN T$
7455 LET OK=0
7460 IF T$="\C" THEN LET T$="CF"
7461 IF T$="\R" THEN LET T$="R"
7462 IF T$="\L" THEN LET T$="L"
7465 FOR J=4 TO 20 STEP 4
7468 IF (T$="CF" OR T$="CF") AND H$(1,J TO J+1)="CF" THEN LET OK=J
7470 IF (T$="C" OR T$="C") AND H$(1,J TO J+1)="C " THEN LET OK=J
7471 IF (T$(1)<>"C" AND T$<>"C") AND T$(1)=H$(1,J) THEN LET OK=J
7472 IF (T$(1)="L" OR T$="R" OR T$="L" OR T$="R") AND H$(1,J)="O" THEN LET OK=J
7473 IF (T$="S" OR T$="S") AND H$(1,J)="S" THEN LET OK=J
7480 NEXT J
7485 IF OK>0 THEN LET DFRAT=VAL H$(1,OK+2 TO OK+3): LET OK=1
7490 IF OK=0 AND Z1<>9 THEN PRINT AT 20,0;"NOT RATED FOR THAT POSITION    ": PRINT AT 21,0;"DEFENSIVE RATING SET TO -5     "
7495 IF OK=0 THEN LET OK=-1 : LET DFRAT=0: IF Z1<>9 THEN LET DFRAT=-5
7499 RETURN 
8020 REM  LISTING SCORES
8021 PAPER 1: INK 7: LET HS=0: LET VS=0
8022 FOR I= 1 TO ING 
8023 PRINT AT 1,(2+(3*(I-1)));S(2,I)
8024 IF I<ING OR HV=1 THEN  PRINT AT 2,(2+(3*(I-1)));S(1,I)
8025 LET HS=HS+S(1,I): LET VS=VS+S(2,I) 
8026 NEXT I
8027 PRINT AT 1,29;VS: PRINT AT 2,29;HS
8029 RETURN 
8060 REM  NOW BATG
8061 PAPER 0: INK 6
8062 LET PNUM=L(HV,B(HV)):GOSUBGTNAME
8063 IF PNUM<299 THEN RESTORE 9100+L(HV,B(HV))
8064 IF PNUM>299 THEN RESTORE 9199
8065 READ B$(1),B$(2)
8066 PRINT AT 10,9;Z$(1 TO 23): PRINT AT 10,9;N$; 
8069 RETURN 
8070 REM  NOW PITCH
8075 LET PHV=3 : IF HV=1 THEN LET PHV=4
8085 LET PNUM=B(PHV):GOSUBGTPNAME: RESTORE 9400+PNUM: READ K$
8086 LET K$="######"+K$
8090 PRINT AT 12,9;Z$(1 TO 23): PRINT AT 12,9;N$;
8093 LET PH=2
8095 IF H$(1,1)="L" THEN LET PH=1
8099 RETURN 
8100 REM  RSLT OF BATR\#020\#000
8101 LET M$=""
8102 IF DEBUG=1 THEN INPUT R$: REM TESTING
8105 IF R$="F" OR R$="K" OR R$="G" OR R$="P" OR R$="S" OR R$="V" OR R$="/" OR R$="N" OR R$="V" OR R$="N" THEN LET OUTS=OUTS+1
8108 IF R$="F" OR R$="S" OR R$="N" OR R$="V" THENGOSUB8405
8109 IF R$="K" THENGOSUB8400
8110 IF R$="G" OR R$="P" OR R$="/" THEN LET M$="FIELDERS CHOICE BATTER OUT"
8120 IF OUTS>=3 THEN LET M$=M$ + F$+"3 OUTS, THAT ENDS THE INNING":GOSUB170: RETURN 
8121 IF R$="G" THENGOSUB8610
8122 IF R$="P" THENGOSUB8600
8123 IF R$="/" THENGOSUB8620
8124 IF R$="S" THENGOSUB8625
8130 IF R$="1" OR R$="E" THENGOSUB8500
8131 IF R$="+" THENGOSUB8510
8132 IF R$="A" THENGOSUB8520
8133 IF R$="2" OR R$="*" THENGOSUB8530
8134 IF R$="3" OR R$="4" THENGOSUB8540
8135 IF R$="B" OR R$="H" THENGOSUB8420
8136 IF R$="W" OR R$="Q" OR R$="L" THENGOSUB8440
8137 IF R$="$" THENGOSUB8730
8138 IF R$="D" THENGOSUB8740
8139 IF R$="V" THENGOSUB8750
8140 IF R$="C" THENGOSUB8760
8142 IF R$="N" THENGOSUB8770
8145 IF OUTS>= 3 THEN LET M$=M$+F$+"3 OUTS, THAT ENDS THE INNING"
8150 GOSUB170
8299 RETURN 
8400 REM  K\#020\#000
8402 LET M$="STRIKE THREE!!!, BATTER IS OUT"
8404 RETURN 
8405 REM  F OUT
8407 LET M$="FLY BALL CAUGHT, BATTER IS OUT "
8409 RETURN 
8420 REM  BB/HBB
8421 IF R$="B" THEN LET M$="BALL FOUR, HE WALKED HIM"
8423 IF R$="H" THEN LET M$="OUCH, HE HIT HIM WITH THE PITCH"
8425 IF R(3)>0 AND R(2)>0 AND R(1)>0 THENGOSUBADD1: LET R(3)=R(2): LET R(2)=R(1):GOTO8430
8427 IF R(2)>0 AND R(1)>0 THEN LET R(3)=R(2): LET R(2)=R(1):GOTO8430
8429 IF R(1)>0 THEN LET R(2)=R(1)
8430 LET R(1)=L(HV,B(HV))
8430 RETURN 
8440 REM  XCHART
8442 LET R$=X$(ROLL-9)
8443 IF R$="W" THEN LET M$="WILD PITCH"
8444 IF R$="Q" THEN LET M$="PASSED BALL"
8445 IF R$="L" THEN LET M$="PITCHER BALKS"
8448 LET R$=R$+", ALL RUNNER ADVANCE"
8450 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8455 LET R(3)=R(2): LET R(2)=0
8460 LET R(2)=R(1): LET R(1)=0
8470 LET B(HV)=B(HV)-1 
8499 RETURN 
8500 REM  1
8501 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8502 IF R(2)>0 THEN LET R(3)=R(2): LET R(2)=0
8503 IF R(1)>0 THEN LET R(2)=R(1) 
8504 LET R(1)=L(HV,B(HV)): LET M$="SINGLE, ALL RUNNERS ADVANCE ONE BASE"
8505 IF R$="E" THEN LET M$="SAFE ON ERROR, RUNNERS ADVANCE"
8509 RETURN 
8510 REM  +
8511 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8512 IF R(2)>0 THENGOSUBADD1: LET R(2)=0
8513 IF R(1)>0 THEN LET R(2)=R(1) 
8514 LET R(1)=L(HV,B(HV)): LET M$="SINGLE RUNNERS ON 2B, 3B SCORE, RUNNER ON 1B GOES TO 2B"
8519 RETURN 
8520 REM  A
8521 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8522 IF R(2)>0 THENGOSUBADD1: LET R(2)=0
8523 IF R(1)>0 THEN LET R(3)=R(1) 
8524 LET R(1)=L(HV,B(HV)): LET M$="SINGLE ALL RUNNERS ADVANCE 2    BASES"
8525 RETURN 
8530 REM  2\#020\#000
8531 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8532 IF R(2)>0 THENGOSUBADD1: LET R(2)=0
8533 IF R(1)>0 AND R$<>"*" THEN LET R(3)=R(1)
8534 IF R(1)>0 AND R$="*" THENGOSUBADD1 
8535 LET R(2)=L(HV,B(HV))
8536 LET R(1)=0
8537 IF R$="2" THEN LET M$="DOUBLE ALL RUNNERS ADVANCE"+F$+"2 BASES"
8538 IF R$="*" THEN LET M$="DOUBLE ALL RUNNER SCORE"
8539 RETURN 
8540 REM  3,4\#020\#000
8541 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8542 IF R(2)>0 THENGOSUBADD1: LET R(2)=0
8543 IF R(1)>0 THENGOSUBADD1: LET R(1)=0
8544 IF R$="3" THEN LET R(3)=L(HV,B(HV)): LET M$="TRIPLE ALL RUNNERS SCORE"
8545 IF R$="4" THENGOSUBADD1: LET M$="HOME RUN!!! ALL RUNNERS SCORE"
8546 RETURN 
8600 REM  DP\#020\#000
8601 IF R(1)=0 THEN LET M$="GROUND BALL, OUT AT FIRST": RETURN 
8602 IF R(3)=0 AND R(2)=0 AND R(1)>0 THEN LET R(1)=0: LET OUTS=OUTS+1 : LET M$="DOUBLE PLAY": RETURN 
8603 IF R(3)=0 AND R(2)>0 AND R(1)>0 THEN LET R(3)=R(2) : LET R(2)=0: LET R(1)=0:: LET OUTS=OUTS+1: LET M$="DOUBLE PLAY": RETURN 
8604 IF OUTS=2 THEN LET OUTS=OUTS+1 : RETURN 
8605 IF R(1)>0 AND R(2)>0 AND R(3)>0 THENGOSUB8700 : RETURN 
8606 IF R(1)>0 AND R(2)=0 AND R(3)>0 THENGOSUB8700: RETURN 
8609 RETURN 
8610 REM  OUT G\#020\#000
8611 IF R(1)=0 THEN LET M$="GROUND OUT": RETURN 
8612 IF R(2)=0 AND R(3)=0 THEN LET R(1)=L(HV,B(HV)) : RETURN 
8613 IF R(3)=0 AND R(2)>0 THEN LET R(3)=R(2): LET R(2)=0: LET R(1)=L(HV,B(HV)) : RETURN 
8614 IF R(3)>0 AND R(2)=0 AND R(1)>0 THEN LET R(2)=R(1):GOSUB8720: RETURN 
8615 IF R(3)>0 AND R(2)>0 THEN LET R(3)=R(2): LET R(2)=R(1):  LET R(1)=L(HV,B(HV)) : RETURN 
8616 RETURN 
8620 REM  /\#020\#000
8621 IF R(3)>0 THENGOSUBADD1: LET R(3)=0
8622 IF R(2)>0 THEN LET R(3)=R(2): LET R(2)=0
8623 IF R(1)>0 THEN LET R(2)=R(1)
8623 LET R(1)=0
8624 RETURN 
8625 REM  S\#020\#000
8626 IF R(3)=0 THEN RETURN 
8627GOSUBADD1: LET R(3)=0
8628 LET M$=M$+F$+"RUNNER ON THIRD SCORES"
8629 RETURN 
8700 REM  DP LOADED\#020\#000
8705 PRINT AT 19,0;"<1>DOUBLEPLAY <2>FORCE/HOME" : INPUT Q$
8706 IF Q$<>"1" AND Q$<>"2" THEN : BEEP 1,1:GOTO8705
8707 IF Q$="1" THEN LET OUTS=OUTS+1 : LET  R(1)=0 : LET R(3)=0:GOSUBADD1 : RETURN 
8708 LET R(3)=R(2): LET R(2)=R(1): LET R(1)=L(HV,B(HV))
8709 RETURN 
8720 REM  G 1&3\#020\#000
8723 LET M$="<1> FORCE AT SECOND, RUN SCORES,"+F$+"    BATTER SAFE AT 1ST"+F$+F$+"<2> BATTER OUT "+F$+"    RUNNER ON 1ST ADVANCES"+F$+"    RUNNER ON 3RD HOLDS"
8724GOSUB170 : INPUT "1 OR 2? ";Q$: LET M$="" :GOSUB170
8726 IF Q$<>"1" AND Q$<>"2" THEN : BEEP 1,1:GOTO8723
8727 IF Q$="1" THEN LET R(3)=0 : LET R(2)=0: LET R(1)=L(HV,B(HV)) :GOSUBADD1 : RETURN 
8728 LET R(2)=R(1): LET R(1)=0
8729 RETURN 
8730 REM  $\#020\#000
8732GOSUB8530
8731 LET M$="DOUBLE RUNNERS ADVANCE 2 BASES"
8734 IF R(3)=0 THEN RETURN 
8735 LET BASE=3: LET SBASE=1
8736GOSUBMDC
8739 RETURN 
8740 REM  D\#020\#000
8741GOSUB8510
8742 LET M$="SINGLE RUNNERS ON 2ND, 3RD SCORE"
8743 IF R(2)=0 THEN RETURN 
8745 LET BASE=2: LET SBASE=1
8746GOSUBMDC
8747 RETURN 
8750 REM  V\#020\#000
8751 LET M$="DEEP FLY BALL, BATTER IS OUT"
8752 IF R(3)=0 THEN RETURN 
8754 LET BASE=3: LET SBASE=3
8755GOSUBMDC
8757 RETURN 
8760 REM  C\#020\#000
8761GOSUB8500
8762 IF R(3)=0 AND R(2)=0 THEN RETURN 
8764 IF R(3)<>0 THEN LET BASE=3: LET SBASE=2:GOSUBMDC
8765 IF OUTS>=3 THEN RETURN 
8766 IF R(2)=0 OR R(3)<>0 THEN RETURN 
8767 LET BASE=2: LET SBASE=1:GOSUBMDC
8769 RETURN 
8770 REM  N\#020\#000
8771GOSUB8625
8772 IF R(2)=0 AND R(1)=0 THEN RETURN 
8773 IF R(2)<>0 THEN LET BASE=2: LET SBASE=2:GOSUBMDC
8774 IF OUTS>=3 THEN RETURN 
8775 IF R(1)=0 OR R(2)<>0 THEN RETURN 
8776 LET BASE=1: LET SBASE=1:GOSUBMDC
8779 RETURN 
8790 REM  UDPLAY\#020\#000
8791GOSUBUDBASE:GOSUBUDSCORE:GOSUB165
8792 RETURN 
8800 REM  MDC\#020\#000
8801 IF BASE=3 THEN LET Q$="HOME"
8802 IF BASE<3 THEN LET Q$=STR$ (BASE+1)+E$(BASE+1)
8804 LET M$=M$+F$+"ATTEMPT TO ADVANCE RUNNER"+F$+"STARTING ON "+STR$ SBASE + E$(SBASE)+ "?"+F$+F$+"   <Y> ATTEMPT ADVANCE TO "+Q$+F$+"   <N> HOLD AT "+STR$ BASE+E$(BASE)
8805GOSUB170
8806 INPUT "Y OR N? ";Q$ 
8807 IF Q$<>"Y" AND Q$<>"N" AND Q$<>"Y" AND Q$<>"N" THEN BEEP 1,1:GOTO8806
8808 IF Q$="N" OR Q$="N" THEN LET M$="":GOSUB170: RETURN 
8810 LET PNUM=R(SBASE):GOSUBGTNAME
8815GOSUB100
8820 RESTORE 9240+VAL H$(1,2): READ S$: LET S$=S$(ROLL-9)
8825 IF DEBUG=1 THEN INPUT S$:
8826 IF DEBUG=1 THEN PRINT AT 20,1;ROLL;" ";"<";S$;">"
8830 IF S$="X" THEN LET OUTS=OUTS+1: LET R(BASE)=0: LET M$="RUNNER IS OUT TRYING TO ADVANCE"
8832 IF S$=" " OR S$="E" THENGOSUB8900
8834 IF S$="+" OR S$="=" THENGOSUB8915
8835 IF S$="*" THEN LET OUTS=OUTS+1:GOSUB8930
8899 RETURN 
8900 REM  ADV " "/E\#020\#000
8902 LET M$="RUNNER HAS ADVANCED":
8903 IF S$="E" THEN LET M$=M$+" ON ERROR"+F$+"OTHER RUNNERS HOLD"
8905 IF BASE=3 THENGOSUBADD1: LET R(3)=0
8907 IF BASE=2 THEN LET R(3)=R(2): LET R(2)=0
8908 IF BASE=1 THEN LET R(2)=R(1): LET R(1)=0
8909 RETURN 
8915 REM  +/=\#020\#000
8920 LET M$="RUNNER HAS ADVANCED AND"+F$+"RUNNERS BEHIND ADVANCE"
8921 IF S$="=" THEN LET M$=M$+" ON ERROR": PRINT AT 21,0;BASE
8923 IF BASE=3 THENGOSUBADD1: LET R(3)=0: LET R(3)=R(2): LET R(2)=R(1)
8925 IF BASE=2 THEN LET R(3)=R(2): LET R(2)=R(1): LET R(1)=0
8927 IF BASE=1 THEN LET R(2)=R(1): LET R(1)=0
8929 LET R(1)=0: RETURN 
8930 REM  *\#020\#000
8933 LET M$="RUNNER OUT TRYING TO ADVANCE"
8935 IF OUTS>=3 THEN RETURN 
8936 LET M$=M$+F$+"RUNNERS BEHIND ADVANCE ON THROW"
8937 IF BASE=3 THEN LET R(3)=R(2): LET R(2)=R(1)
8938 IF BASE=2 THEN LET R(2)=R(1)
8939 LET R(1)=0: RETURN 
9000 REM  NAMEDATA\#020\#000
9001 DATA "JACKIE ROBINSON","R4A2B+53B+21B+2OF+0    "
9002 DATA "CHARLIE GEHRINGER","L2A2B14                "
9004 DATA "HONUS WAGNER","R5ASS173B+72B+41B+4OF+4"
9007 DATA "PIE TRAYNOR","R2A3B13                "
9008 DATA "BILL TERRY","L2B1B+7                "
9011 DATA "STAN MUSIAL","L2BOF+21B+1            "
9012 DATA "LOU GEHRIG","L1B1B+0                "
9015 DATA "GEORGE SISLER","L4A1B+8                "
9018 DATA "EDDIE COLLINS","L4A2B13                "
9020 DATA "LOU BOUDREAU","R1ASS103B+2            "
9022 DATA "JOE CRONIN","R1ASS+53B+31B+0        "
9024 DATA "GEORGE KELL","R2A3B+61B+0            "
9028 DATA "STUFFY MCINNIS","R3A1B+6                "
9030 DATA "ROGERS HORNSBY","R2A2B+53B+2SS+2        "
9033 DATA "ARKY VAUGHAN","L1ASS+33B+1OF+0        "
9043 DATA "ROBERTO CLEMENTE","R2BOF+9                "
9047 DATA "HANK AARON","R2BOF+6CF+21B+0        "
9057 DATA "NAP LAJOIE","R3A2B121B+4SS+0        "
9059 DATA "WILLIE MAYS","R3BCF13OF101B+0        "
9065 DATA "TED WILLIAMS","L1BOF+0                "
9069 DATA "TY COBB","L5AOF+2CF+1            "
9070 DATA "TRIS SPEAKER","L3ACF14OF10            "
9071 DATA "BABE RUTH","L2BOF+4                "
9072 DATA "MICKEY MANTLE","B3BCF+40F+21B-1        "
9078 DATA "MICKEY COCHRANE","L2AC 14                "
9079 DATA "JIMMIE FOXX","R1B1B+13B-1C -1        "
9080 DATA "ROY CAMPANELLA","R0BC 14                "
9082 DATA "TED SIMMONS","B1BC +51B+0OF-2        "
9084 DATA "YOGI BERRA","L0BC +8OF-2            "
9088 DATA "LOU BROCK","L5AOF+1CF-1            "
9100 REM  HIT DATA\#020\#000
9101 DATA "*3E$4/K/S4VV2CS/1PD+PG+AGFNFPP","*3$4GD22G4EGKVA+1GPSPCPN/FSFG/"
9102 DATA "3P4PP2$P*2PGGVG/GFESD+SNAFKC1F","3PP$4VVP*KFFG+PGG/ESD2SNAF/C1/"
9104 DATA "KK4$3PE*K311F2C/PD/PGVGA+NSFFG","*4$3///SN$EG2CSN1ND+PP+AGFKVFP"
9107 DATA "*N$32NGEE4DG1CGGVFFV++PAFGPSSG","*/$32SSEE4FF1CPPN/D/AA/+FGGSSK"
9108 DATA "$3P42VVP*PSDP+S//KEP1CNGAFGFKP","*P34D1$PPKEK2V+NFPPPGAASF/GCGG"
9111 DATA "E43DP2$P*2KK4PGGCF1EF+FAS/NVNF","N*3$NVNV4KKD+FGSC1ENN2PFA/G4PS"
9112 DATA "*$3/2KK1GKVVC/VG+KDPAAP4SFPFNE","*$3DGV$EGV1G2CVA+KAK//P4SFPFNK"
9115 DATA "K432ASK$E*P+V+N/FCCP1P/AGFGSGE","*G4E3G$GGKG12+CNVDKPFPSGAF//AP"
9118 DATA "*3GEGP2$G4KNGVGPG//S+C/A1FSFD/","PPPE+2$3*4ADPCA/GGGKNW/G1FSVFG"
9120 DATA "4$PD2PEP*GGP1CPVAAK3KVS+/FFGGS","4PE$DKKP3GGG2CA+1PP*PNVS/FFGPP"
9122 DATA "KKE$F4S3*PDC1AV+2KF///SNFGPPPK","*3E$4CDKKKFF2A+V1P/KPK/SFGNPPN"
9124 DATA "43E$PPPP*PDP2NGV1GPPSSCA/FF++G","2GD2GP$3*4EP1CGPPNN/SSVA/FF++K"
9128 DATA "*K32NFFN4EEVD1CPSSV$++PA/GGFF/","4G$EDP2G3GKAAF//1NVNNGC+FPGSSG"
9130 DATA "FE1$3K1/*KPP2CGA4DPAFP/+FNSVSG","/*3$KKKEVPPG1C+CGDPVF4/SFNP2V4"
9133 DATA "4P23PPPP$*VVGF/GGKEN1CNSFA+/D+","*K43N2$GG2FFV+GGN1EVDCPFAF//PP"
9143 DATA "*K3K42EPA4KD1CAGPFK$SSG+F/NVPP","*GK234KK$K/AAC/K1DEVVPN+FGSPFG"
9147 DATA "*3$P2SKDG4VVSF4AC1SPNN+/PFGKNE","*3SP2GK$P1D1GCA4VFFVNN+/FPSKNE"
9157 DATA "2*4P3$/PNP//1CNPV/EVDFASFF+2GG","*43D$NNNAKEP1VAGFGKSPGC+F/S2PP"
9159 DATA "SK$32SD4*KAAS4+GFKEF1CPFN/GVPK","*$K32FFDKKE1/D4A/KSKGV+FNGSPPP"
9165 DATA "*$GGV2VG32KD+C4FF1PPFNNSAPK//E","N3N$NVV*FKS1CSF4+FD4F2G/APKNPE"
9169 DATA "*GPE23P4$GPKVCNS+DKG/GF/A1FGGG","*4$E3NNGVGKV2CFSDFF1K+PGA1/G/G"
9170 DATA "*3GE$G224KGD+G//NNPS1CPGAFSFVV","443EP$P*/$PD+FNG/1///C2FAGSVGK"
9171 DATA "*P$2PPE134/N+NKKCKD2KFFA4GS/VK","*1P3PNE$$PD+CNGK//4GG2SA4KFFVP"
9172 DATA "*3N+K2V$KGVD+C4A1FE4KKSPGFN//K","3$2E1KF44*CNNK4PVKDNG+SAFK/GG/"
9178 DATA "*GE$1KPK43KD+CN/PNNWP2GSAFGF//","*3D4GGE$$/1G2CP/+WKW/GVSAFNFPP"
9179 DATA "*D3KP2$EK241CK4A//KKFV+SFNGPPK","*34KK2$DK2K1CA4G//KSFV+PFNSKPE"
9180 DATA "*$P2NPKK23DNPV4+P1/C/C+GSFNF/E","*K/K21PE$3DPCA4NNVVNP+/GSFKFPP"
9182 DATA "E*K42/1$V3DKSCFFPVNPS+P/AGNGFV","3NF4/1NN*VP2KC/F+D$ENGFSAGPVP/"
9184 DATA "3$E2KP1FF*AG4GAP+1D1/PVFSN/FNF","3$12//EGK*ND+4PNN/GSPCVSAFFPGG"
9188 DATA "3$2ENK/4K*NCV+SFK/DKK/FGGAP1K/","33$EDCK4*NK/+1SPVKF/GNGGAFK/2/"
9199 DATA "4+GG   2$3GP+SKKKKKKKKKKKKKKKK","KKKKKKKKKKKKKKK1CVNVFAP/FPKGGE" 
9200 REM  DATA/LINEUP\#020\#000
9210 DATA 69,18,15,65,71,24,78,20,343,2,72,70,12,84,79,22
9220 DATA 4,30,57,47,59,43,11,80,311,82,28,8,1,33,7,88
9221 DATA "\C21\L\R3CSP",1,13,8,0,4,6,14,10
9222 DATA "S32\L\C\R1CP",17,2,12,6,13,9,1,14
9224 REM  PITCHRS/TEAM\#020\#000
9225 DATA 43,3,24,4,22,9,44,7,6
9226 DATA 11,12,23,20,13,18,16,45,15
9230 REM  SAC BUNT\#020\#000
9231 DATA "SSSS1SSSSSSSS1SSSSPS222ESGFSGS"
9232 DATA "SSSSSSSSSSSS1SGGEPEG2FFG222SSS"
9235 REM  SU BUNT\#020\#000
9236 DATA "SSS11SSSSSS1S    SSSESEF22SGG "
9237 DATA "S11SSSSSSSS FF    S SEG222  G1"
9239 REM  MGR DEC +E='=' \#020\#000
9240 DATA "XXEXXXXXXXXX*****X=XXXXX+   XX"
9241 DATA "X*XEXXXXXXXXX*** =+XXX++   X X"
9242 DATA " *XXEXXXXXXXX **=  +     +++  "
9243 DATA "*EE X**XXXXXX=X         +++   "
9244 DATA "XE E**XX  XX   = +      +++   "
9245 DATA "*E *EXXXX       ==      ++++  "
9249 REM  STL 2\#020\#000
9250 DATA "XXXPEXXXXBXXXXXXXX =XXX   X  ="
9251 DATA "BX PEXXXX XXXXXXXX=XXX       ="
9252 DATA "XB PEXX XXXXXXXX     =        "
9253 DATA "XX PEX XXXXXXXX    = =       B"
9254 DATA "XB PEXXXXBXXXX    =  =        "
9255 DATA "XXXPE XXX XXX     B    =      "
9259 REM  STL 3\#020\#000
9260 DATA "XEXXXXXPXBXXXXXXXXXXXXX   X  ="
9261 DATA "XEEEEEEPXBXXXXXXXXX=XX       ="
9262 DATA "BEXXXXXPX XXXXXXX =          ="
9263 DATA "BEXXXXXP XXXXXX      =        "
9264 DATA "XEX XXXPXXXXXX     = =       B"
9265 DATA " EBXXXXPXXXXX     =  =       X"
9269 REM  STL HOME\#020\#000
9270 DATA "PXX XXXXBEXXXXXXXXXXXXXXXXXXX="
9271 DATA "P XXX   BEXXXXXXXXXXXXXXXXXXX="
9272 DATA "PXXX    BXXXXXXX  =EXXXXXXXXXX"
9273 DATA "PB XXXXXEXXXXXXXXX =  XXXX   ="
9274 DATA "PB XXXXE XXXXXXXXXXXXXXX    =X"
9275 DATA "PEB=XXXX XXXXXXXXXX=  XX    XX"
9279 REM  XCHART\#020\#000
9280 DATA "WWWWWWWWWWWWWQQQWHHHHHHWWWLQWW"
9300 REM  PITCH NAMES\#020\#000
9303 DATA "WALTER JOHNSTON", "R0B"
9304 DATA "RON GUIDRY", "L0B"
9306 DATA "SPARKY LYLE", "L0B" 
9307 DATA "LEFTY GROVE", "L0B"
9309 DATA "ROLLIE FINGERS", "R0B"
9311 DATA "CHRISTY MATHEWSON", "R0B"
9312 DATA "JUAN MARICHAL", "R0B"
9313 DATA "SANDY KOUFAX", "L0B"
9315 DATA "TUG MCGRAW", "L0B"
9316 DATA "GROVER ALEXANDER", "R0B"
9318 DATA "MORDECAI BROWN", "R0B"
9320 DATA "BOB GIBSON", "R0B"
9322 DATA "CY YOUNG", "R0B"
9323 DATA "HIPPO VAUGHN", "L0B"
9324 DATA "RUBE WADDELL", "L0B"
9343 DATA "ED WALSH", "R0B"
9344 DATA "HOYT WILHELM", "R0B"
9345 DATA "CARL HUBBELL", "L0B"
9400 REM  PITCH DATA\#020\#000
9403 DATA "      F   K  BGG KF  BX "
9404 DATA "   K   F   K     BK  GX "
9406 DATA "  FB   G    K  K B    X "
9407 DATA "      G  BK  F G K F FX "
9409 DATA "F        B   B    K  GX "
9411 DATA "       B    K    K GFFX "
9412 DATA "      BKK  GG     F   X "
9413 DATA "        B F K  F  KK  X "
9415 DATA " B    G   K   FK  B   X "
9416 DATA "       FFGK    BG    KX "
9418 DATA "     FFBK      GG     X "
9420 DATA "          BBG FFKK    X "
9422 DATA "KK  B  G   KF  F     GXB"
9423 DATA "       G    B  B K   FX "
9424 DATA "  F         K BBKK    X "
9443 DATA "F      B KK    F     GX "
9444 DATA " BG    KF   B    K   BXG"
9445 DATA "    GGGGF BF     K    X "
9500 REM  SPECIAL CHARS HOME, BASE, CF,RF,LF\#020\#000
9511 DATA 255,255,255,255,126,60,24,0
9512 DATA 24,60,126,255,255,126,60,24
9513 DATA 0,119,68,70,68,68,116,0
9514 DATA 0,119,84,118,100,84,84,0
9515 DATA 0,71,68,70,68,68,116,0
9600 REM  BASES\#020\#000
9610 PAPER 0: INK 7
9620 FOR I=5 TO 12
9630 PRINT AT I,0;Z$;
9640 NEXT I
9645 PRINT AT 8,1;"\H";
9646 PRINT AT 7,2;"\B";: PRINT AT 6,1;"\B";: PRINT AT 7,0;"\B";
9648 PRINT AT 6,5;"1B:";: PRINT AT 7,5;"2B:";: PRINT AT 8,5;"3B:";
9650 PRINT AT 10,0;"AT BAT :"
9652 PRINT AT 12,0;"PITCHER:"
9653 PAPER 7: INK 0
9654 FOR I = 13 TO 20: PRINT AT I,0;Z$;: NEXT I
9699 RETURN 
9700 REM SPLASH
9703 LET AL=1: LET NL=2
9705 PAPER 1: INK 7 : BORDER 0: CLS 
9710 PRINT AT 3,5;"SUPERSTAR BASEBALL V 1.0"
9715 PRINT AT 5,8;" PRESENTED BY..."
9716 PRINT AT 7,7;"ANDIAR COMPUTER LABS"
9717 PRINT AT 9,6;"SPECIAL EDITION FOR": PRINT AT 10,5;"\@RETROCHALLENGE 7/2015"
9718 INPUT "HOME TEAM? (A)MER LEAGUE                   (N)AT LEAGUE: ";P$
9719 IF P$<>"A" AND P$<>"A" AND P$<>"N" AND P$<>"N" THEN9718
9720 IF P$="N" OR P$="N" THEN LET NL=1: LET AL=2
9724 GOSUB9900
9734 CLS : PRINT AT 4,12;"MENU"
9735 PRINT AT 6,7;"(1) HOME LINE UP": PRINT AT 7,7;"(2) HOME PITCERS"
9736 PRINT AT 9,7;"(3) VISITOR LINEUP": PRINT AT 10,7;"(4) VISITOR PITCHERS"
9740 PRINT AT 12,7;"(P) PLAY BALL"
9741 INPUT P$
9742 IF P$="Q" THEN STOP 
9745 IF P$="P" OR P$="P" THEN LET HV=2: LET HVP=1 : RETURN 
9750 IF P$="1" THEN LET HV=1 :GOSUB3000
9752 IF P$="2" THEN LET HVP=1 :GOSUB3200
9755 IF P$="3" THEN LET HV=2 :GOSUB3000
9756 IF P$="4" THEN LET HVP=2 :GOSUB3200
9760 PAPER 1: INK 7: CLS :GOTO9734
9800 REM  SCREEN SETUP\#020\#000
9805 PAPER 7: CLS 
9806 PAPER 1: INK 7
9830 FOR I=0 TO 3: PRINT AT I,0;Z$: NEXT I
9831 PRINT AT 0,2;"1  2  3  4  5  6  7  8  9  R"
9833 PRINT AT 1,0;"V": PRINT AT 2,0;"H"
9835 PRINT AT 3,0;"       INNING:";ING; : PRINT AT 3,18;"OUTS:";OUTS;"        ";
9836 GOSUB9600
9837 PAPER 7: INK 0
9839 RETURN 
9900 REM  SETUP\#020\#000
9903 FLASH 1: PRINT AT 20,10;"INITIALIZING": FLASH 0
9905 DIM X(6): DIM Y(6): DIM Z(6): DIM H$(1,23) : DIM L(2,16): DIM B(4): DIM E$(3,2)
9906 DIM B$(2,30) : DIM R(3): DIM S(2,20): DIM P(2,9): DIM L$(2,9): DIM D(2,10): DIM T(2)
9910 LET X(1)=1: LET X(2)=2: LET X(3)=2: LET X(4)=3: LET X(5)=3: LET X(6)=3
9920 LET Y(1)=0: LET Y(2)=0: LET Y(3)=1: LET Y(4)=2: LET Y(5)=3: LET Y(6)=4
9930 LET Z(1)=0: LET Z(2)=1: LET Z(3)=2: LET Z(4)=3: LET Z(5)=4: LET Z(6)=5
9935 LET T(1)=0: LET T(2)=0
9940 LET Z$="                                "
9945 LET F$ =CHR$ (13): LET A$="": LET E$(1)="ST": LET E$(2)="ND": LET E$(3)="RD"
9950 RESTORE 9210: FOR I=1 TO 16: READ L(AL,I): NEXT I
9960 RESTORE 9220: FOR I=1 TO 16: READ L(NL,I): NEXT I
9961 READ L$(AL): FOR I=1 TO 8: READ D(AL,I): LET D(AL,10)=D(AL,10)+D(AL,I): NEXT I
9962 READ L$(NL): FOR I=1 TO 8: READ D(NL,I): LET D(NL,10)=D(NL,10)+D(NL,I): NEXT I
9964 RESTORE 9225: FOR I=1 TO 9: READ P(AL,I): NEXT I
9965 FOR I=1 TO 9: READ P(NL,I): NEXT I
9970 LET HB=0: LET VB=0: LET HV=2 : LET B(1)=0: LET B(2)=0: LET ING=0: LET OUTS=0
9975 LET B(AL+2)=L(AL,9)-300: LET B(NL+2)=L(NL,9)-300
9977 RESTORE 9280: READ X$
9980 RESTORE 9511: FOR I=0 TO 7: READ D: POKE USR ("H")+I,D: NEXT I
9981 FOR I=0 TO 7: READ D: POKE USR ("B")+I,D: NEXT I
9982 FOR I=0 TO 7: READ D: POKE USR ("C")+I,D: NEXT I
9983 FOR I=0 TO 7: READ D: POKE USR ("R")+I,D: NEXT I
9984 FOR I=0 TO 7: READ D: POKE USR ("L")+I,D: NEXT I
9990 LET ADD1=130: LET MDC=8800:LET UDHV=160
9991 LET BALK=330: LET UDBASE=110: LET GTNAME=135
9992 LET UDSCORE=140: LET UDPLAY=8790
9993 LET DSPMSG=170: LET GTPNAME=180: LET SWPOS=7200
9994 LET CKPOS=7400: LET PLPOS=7450
9999 RETURN 

