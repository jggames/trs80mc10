
ZX81 LIST utility        V051292
(C) Jack Raats, Steenbergen 1992
Email:     2:285/751@fidonet.org
          27:1331/304@signet.org

FILE: grafspee.p

----- SYSTEM VARIABLES -----

PROG  : 16509
D_FILE: 30781
VARS  : 31574
E_LINE: 31575
STKBOT: 31575
STKEND: 31575

----- LEGENDA -----

[A] means  INVERSE A
{A} means GRAPHICS A

----- START OF LISTING -----

   1 REM 0E#RND{T}*F7SAVE TAN LEN [ ]?/PAUSE 0000
   2 REM [S][C][R][E][E][N][ ][I][N][V][E][R][T]
   3 LET SI=16515
   5 SLOW 
   6 GOSUB 8100
  15 RAND 
 460 GOSUB 9000
 500 DIM X(10)
 510 DIM Y(10)
 520 DIM Z(10)
 530 DIM W(10)
 790 LET GS=8E3
 795 LET SCORE=PI-PI
 800 FOR N=1 TO 10
 801 CLS 
 802 GOSUB 8110
 805 PRINT AT 4,12;"CAPTAIN";TAB 12;"{7}{7}{7}{7}{7}{7}{7}";TAB 4;"YOU HAVE CONTACTED YOUR";TAB 4;"SUPPLY SHIP THE ALTMARK";TAB 10;TAB 4;"YOUR SUPPLIES HAVE BEEN";
 806 PRINT TAB 5;"REPLENISHED AND SOME";TAB 5;"REPAIRS TO YOUR SHIP";TAB 5;"HAVE BEEN CARRIED OUT.";AT 13,4;"YOUR SCORE SO FAR IS ";INT SCORE
 807 PRINT AT 15,12;" {4}/ V  V";TAB 12;"{Q}[ ]{6}{6}[ ]{6}{6}[ ]{4}"
 808 LET GS=GS+INT (RND*4000)
 809 IF GS>8E3 THEN LET GS=8E3
 810 FOR S=1 TO 10
 820 LET Z(S)=INT (RND*4+N)
 825 IF Z(S)>5 THEN LET Z(S)=5
 830 LET W(S)=(4000*(Z(S)=1))+(6000*(Z(S)=2 OR Z(S)=4))+(8E3*(Z(S)=3))+(1E4*(Z(S)=5))+INT (RND*3000)
 840 LET Y(S)=(INT (RND*3800))+250
 850 LET X(S)=INT (RND*((2E3*(Y(S)<=2500))+(4E3*(Y(S)>3500))+(3E3*(Y(S)>2500 AND Y(S)<=3500))))
 851 LET X(S)=X(S)+500+(750*(Y(S)>1E3))+(250*(Y(S)>1250))-(500*(Y(S)>2500))-(250*(Y(S)>3500))
 853 NEXT S
 854 GOSUB 8400
 855 CLS 
 860 PRINT AT 0,20;" GRAF SPEE [ ]";TAB 20;" {7}{7}{7}{7}{7}{7}{7}{7}{7} [ ]";TAB 20;"[ ][S][P][E][E][D][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][F][U][E][L][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][H][E][A][D][I][N][G][ ][ ][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]" 
 870 PRINT TAB 20;"[ ][S][H][E][L][L][S][ ][ ][ ][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][T][O][R][P][E][D][O][E][S][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][P][O][S][I][T][I][O][N][ ][ ][ ]";TAB 20;"[ ][N][/][S][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][E][/][W][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";TAB 20;"[ ][T][G][T][ ][R][A][N][G][E][ ][ ]";TAB 20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]"
 872 LET FU=FU+INT (RND*3000)+2000
 873 LET TO=TO+INT (RND*20)+5
 874 LET SH=SH+INT (RND*200)+20
 875 IF FU>=8E3 THEN LET FU=8E3
 876 IF SH>200 THEN LET SH=200
 877 IF TO>20 THEN LET TO=20
 878 LET HE=6
 880 PRINT AT 7,20;B$(HE);AT 9,23;SH;AT 11,23;TO
 890 LET JJ=1
 970 FOR I=1 TO 10+RND*5
 980 IF JJ=1 THEN GOTO 1000
 990 GOTO 1230
1000 PRINT AT 17,20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";Z$;Z$;Z$;Z$
1001 PRINT AT 0,0;"{E}{7}{7}{7}{7}{7}{7}{7}{7}{7}{7}[ ][ ]N[ ][ ][ ][ ][ ][ ]";TAB 0;"{5}{2}        {8}[ ][1][2][3][ ][ ][A][ ][ ]";TAB 0;"{5}         {8}W[8]*[4]E[ ][F][ ][ ]";TAB 0;"{5}          [ ][7][6][5][ ][ ][R][ ][ ]";TAB 0;"[ ][ ]{W}{6}       {2}[ ]S[ ][ ][ ][I][ ][ ]";TAB 0;"[S][ ][A][ ]           {2}[ ][C][ ][ ]";TAB 0;"[O][ ][M][ ]{W}          {8}[ ][A][ ][ ]"
1005 LET CL=1
1010 PRINT "[U][ ][E][ ][ ]{1}          {R}[ ][ ][ ]";TAB 0;"[T][ ][R][ ]{E}           {8}[ ][ ][ ]";TAB 0;"[H][ ][I][ ]{1}           {Q}[ ][ ][ ]";TAB 0;"[ ][ ][C]{E}            {R}[ ][ ][ ]";TAB 0;"[ ][ ][A]{1}            {2}[ ][ ][ ]"
1020 PRINT "[ ][ ]{E}              [ ]{E}{R}";TAB 0;"[ ][ ]{5}                {8}";TAB 0;"[ ]{E}                 {8}";TAB 0;"{E}                  {8}";TAB 0;"{5}                  {8}";TAB 0;"{W}{6}{Q}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{6}{Q}"
1180 LET SP=18
1185 LET JJ=0
1190 LET LL=0
1200 PRINT AT 19,0;"PLOT COURSE USING THE DIRECTION"," CONTROLS (1 TO 8).ON ARRIVING","AT THE POINT TO SEARCH PRESS ""S"""
1220 PRINT AT 3,23;"[ ][ ]KTS";AT 3,23;SP
1230 FOR I=1 TO 10+RND*5
1240 LET PX=INT (EW/120)
1250 LET PY=43-INT (NS/120)
1260 LET CX=INT (PX/2)+1
1270 LET CY=INT (NS/240)
1280 LET KK=PEEK ((PEEK 16396+256*PEEK 16397)+CX+CY*33)
1290 IF KK<>0 AND KK<>2 AND KK<>1 AND KK<>4 AND KK<>135 THEN GOTO 7400
1300 IF RND>.9 THEN GOSUB 6400
1310 UNPLOT PX,PY
1320 PLOT PX,PY
1325 PRINT AT 18,1;"[I][N][P][U][T][ ][D][I][R][E][C][T][I][O][N] OR ""M""=SCUTTLE"
1330 IF INKEY$="" THEN GOTO 1300
1340 LET VV=HE
1341 LET HE=CODE INKEY$-28
1345 PRINT AT 18,0;Z$(1 TO 19)
1346 IF HE=22 THEN GOTO 7500
1350 IF (HE<1 OR HE>8) AND HE<>28 THEN GOTO 1300
1360 IF HE=28 THEN GOTO 1440
1370 LET SA=120
1380 PRINT AT 7,20;B$(HE)
1390 GOSUB 8010
1395 IF FU<=0 THEN GOTO 7500
1400 GOSUB 8005
1410 UNPLOT PX,PY
1420 NEXT I
1440 LET TR=1E7
1445 UNPLOT PX,PY
1450 FOR S=1 TO 10
1460 IF X(S)>EW+240 OR X(S)<EW-240 OR Y(S)>NS+240 OR Y(S)<NS-240 THEN GOTO 1510
1470 LET TX=X(S)
1480 LET TY=Y(S)
1490 GOSUB 8050
1500 IF TR<24E4 THEN GOTO 4800
1510 UNPLOT PX,PY
1515 PLOT PX,PY
1520 NEXT S
1522 IF HE=28 THEN GOTO 1300
1525 LET HE=VV
1530 NEXT N
1540 GOTO 8600
4800 LET TT=W(S)
4805 PRINT AT 19,0;Z$;Z$;Z$
4810 LET TX=X(S)
4820 LET TY=Y(S)
4830 LET K=Z(S)
4831 LET SS=S
4835 PRINT AT 0,0;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]"
4840 FOR S=1 TO 9
4845 PRINT "[ ]                  [ ]"
4850 NEXT S
4855 FOR S=1 TO 7
4860 PRINT "[ ]{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}[ ]"
4865 NEXT S
4870 PRINT "[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]"
4880 PRINT AT 1,16;"N";TAB 15;"[1][2][3]";TAB 14;"W[8]*[4]E";TAB 15;"[7][6][5]";TAB 16;"S"
4989 LET CL=0
5001 IF SM<1 THEN LET SM=0
5002 IF SP>SM THEN LET SP=SM
5003 PRINT AT 3,23;"[ ][ ]";AT 3,23;SP
5004 GOSUB 8000
5008 IF GS<1 OR FU<1 THEN GOTO 7500
5010 GOSUB 8010
5060 IF TR>=24E4 OR TT<1 THEN GOTO 1000
5070 PRINT AT 17,22;"[ ][ ][ ][ ][ ][ ]";AT 17,22;INT TR
5080 LET D=(TR<4E4)+(TR<3E4)+(TR<2E4)+(TR<1E4)
5088 GOSUB 8500
5090 IF K>3 AND RND>(.6*(K>3))+(.1*(K>4)) AND TR<26000 THEN GOSUB 7000
5100 IF TH<>HE OR D=0 THEN PRINT AT 7,5;Z$(1 TO 11);TAB 5;Z$(1 TO 11);TAB 5;Z$(1 TO 11)
5110 IF TH=HE AND D<>0 THEN GOSUB 8700
5120 GOSUB 6500
5130 IF A>0 THEN GOTO 5100+A*200
5140 PRINT AT 19,10;"[I][N][P][U][T][ ][C][H][O][I][C][E]";TAB 0;"[F][I][R][E][(][0][=][T][O][R][P][E][D][O][E][S][ ][1][=][G][U][N][S][)][ ][2][=][T][U][R][N]";TAB 2;"[S][P][E][E][D][(][3][=][I][N][C][ ][4][=][D][E][C][)][ ][M][=][S][C][U][T][T][L][E]"
5150 FOR F=1 TO 20
5170 IF INKEY$<>"" THEN GOTO 5190
5180 NEXT F
5190 LET A=CODE INKEY$-28
5192 IF A=22 OR GS<1 THEN GOTO 7500
5200 PRINT AT 19,0;Z$
5201 IF A>=0 AND A<=4 THEN PRINT Z$;Z$
5204 LET B=(2*(A=0))+(1*(A=1))
5205 IF A=0 THEN LET A=1
5210 IF A<1 OR A>4 THEN LET A=0
5220 IF A=4 THEN LET C=1
5230 IF A=4 THEN LET A=3
5250 GOTO 5000-(3605*(CL=1))
5300 IF D=0 OR TH<>HE THEN LET A=0
5302 IF D=0 OR HE<>TH THEN GOTO 5140
5305 GOTO 6000
5500 PRINT AT 19,10;"[I][N][P][U][T][ ][D][I][R][E][C][T][I][O][N]";TAB 1;"[1][=][N][/][W][ ][2][=][N][ ][3][=][N][/][E][ ][4][=][E][ ][5][=][S][/][E][ ][6][=][S]";TAB 11;"[7][=][S][/][W][ ][8][=][W]"
5510 FOR F=1 TO 20
5520 IF INKEY$="" THEN GOTO 5540
5530 IF INKEY$<>"" THEN GOTO 5550
5540 NEXT F
5550 LET E=CODE INKEY$-28
5560 PRINT AT 19,0;Z$
5570 IF E>0 AND E<9 THEN GOTO 5590
5575 IF CL=1 THEN GOTO 1395
5580 GOTO 5000
5590 LET HE=E
5595 PRINT AT 20,0;Z$;Z$
5600 PRINT AT 7,20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";AT 7,20;B$(HE)
5610 LET A=0
5620 GOTO 5575
5700 PRINT AT 20,0;Z$;Z$;AT 20,13;"[I][N][P][U][T]"
5710 IF C=0 THEN PRINT TAB 6;"[S][P][E][E][D][ ][I][N][C][R][E][A][S][E][ ][(][0][-][9][)]"
5715 IF C=1 THEN PRINT TAB 6;"[S][P][E][E][D][ ][D][E][C][R][E][A][S][E][ ][(][0][-][9][)]"
5720 FOR F=1 TO 20
5730 IF INKEY$="" THEN GOTO 5750
5740 IF INKEY$<>"" THEN GOTO 5760
5750 NEXT F
5751 PRINT AT 20,0;Z$
5752 IF CL=1 THEN GOTO 1395
5753 GOTO 5000
5760 LET E=CODE INKEY$-28
5765 IF E<0 OR E>9 THEN GOTO 5720
5770 PRINT AT 18,0;Z$;Z$;Z$
5780 IF C=0 THEN LET SP=SP+E
5790 IF C=1 THEN LET SP=SP-E
5800 IF SP>SM THEN LET SP=SM
5810 IF SP<0 THEN LET SP=0
5820 PRINT AT 20,0;Z$;Z$
5830 LET A=0
5840 LET C=0
5850 GOTO 5752
5880 PRINT AT 3,23;"[ ][ ]";AT 3,23;SP;AT 20,0;Z$;Z$
6000 IF B=1 AND SH<1 OR B=2 AND TO<1 THEN GOTO 6160
6001 PRINT AT 20,8;"[I][N][P][U][T][ ][N][U][M][B][E][R][ ][O][F]"
6010 IF B=2 THEN PRINT AT 21,6;"[T][O][R][P][E][D][O][E][S][ ][F][I][R][I][N][G][(][1][-][2][)]"
6020 IF B=1 THEN PRINT AT 21,8;"[G][U][N][S][ ][F][I][R][I][N][G][(][1][-][6][)]"
6030 FOR F=1 TO 20
6050 IF INKEY$<>"" THEN GOTO 6070
6060 NEXT F
6070 LET V=CODE INKEY$-28
6075 PRINT AT 20,0;Z$
6080 IF ((V<1 OR V>2) AND B=2) OR (B=1 AND (V<1 OR V>6)) THEN GOTO 5000
6085 LET W=0
6090 FOR F=1 TO V
6095 IF B=1 AND TR>26000 THEN GOTO 6140
6100 IF (RND*TR)<1000 THEN GOTO 6210
6110 IF B=1 THEN GOTO 6140
6120 GOSUB 8900
6130 GOTO 6150
6140 PRINT AT 11,7+F;"[+]";AT 11,7+F;"{A}"
6150 NEXT F
6152 IF TH=HE AND D<>0 THEN GOSUB 8700
6155 IF B=1 THEN LET SH=SH-V
6157 IF B=2 THEN LET TO=TO-V
6158 PRINT AT 9,23;"[ ][ ][ ]";AT 9,23;SH;AT 11,23;"[ ][ ][ ]";AT 11,23;TO
6160 LET A=0
6170 LET B=0
6180 LET V=0
6190 PRINT AT 20,0;Z$;Z$
6200 GOTO 5000
6210 IF B=1 THEN GOTO 6250
6220 GOSUB 8900
6230 LET R=RND*10000
6240 GOTO 6270
6250 PRINT AT 9,7+F;"[+]";AT 9,7+F;"{A}";AT 9,7+F;"{D}";AT 9,7+F;" "
6260 LET R=RND*1000
6270 LET TT=TT-R
6273 LET SCORE=SCORE+R
6275 IF TT<1 THEN GOTO 6290
6280 GOTO 6150
6290 PRINT AT 7,5;"  {Y}{4} {A}{A} {6}  ";TAB 5;"   {R}{E}{A}{A}{Q}{1}  ";TAB 5;"    {R}[ ][ ]{1}   "
6291 FOR F=1 TO 15
6292 NEXT F
6293 PRINT AT 7,5;Z$(1 TO 11);TAB 5;Z$(1 TO 11);TAB 5;Z$(1 TO 11)
6294 LET Z(SS)=0
6295 LET Y(SS)=0
6296 LET X(SS)=0
6297 LET W(SS)=0
6300 GOTO 6155
6460 LET BB=INT (RND*10)+1
6465 IF Y(BB)<2 OR X(BB)<2 THEN RETURN 
6470 LET SY=43-INT (Y(BB)/120)
6480 LET SX=INT (X(BB)/120)
6482 FOR S=1 TO 5
6485 PLOT SX,SY
6486 UNPLOT SX,SY
6487 NEXT S
6496 RETURN 
6500 PRINT AT 18,0;Z$;AT 18,20-(2*(TH=2 OR TH=6))-(3*(TH=4 OR TH=8));B$(TH);"  ";Z$;AT 18,2;"CAPTAIN TARGET TO "
6510 RETURN 
7000 LET G=0
7010 FOR F=1 TO (3*(K=4))+(4*(K=5))
7020 IF TH=HE THEN PRINT AT 9,7;"[+]";TAB 11;"[+]"
7040 IF TH=HE THEN PRINT AT 9,7;"[ ]";TAB 11;"[ ]"
7050 IF RND*TR>800 THEN GOTO 7110
7060 LET G=G+1
7065 PRINT AT 0,0;
7070 FOR I=1 TO 4
7080 LET L=USR SI
7090 NEXT I
7100 GOTO 7130
7110 PRINT AT 15,6;"[ ][ ]";TAB 13;"[ ][ ]";AT 14,5;"{8}[ ][ ]{5}";TAB 12;"{8}[ ][ ]{5}";AT 13,5;"[ ][ ][ ][ ]";TAB 12;"[ ][ ][ ][ ]";AT 12,4;"{8}[ ][ ][ ][ ]{5}";TAB 11;"{8}[ ][ ][ ][ ]{5}"
7120 PRINT AT 15,6;"{A}{A}";TAB 13;"{A}{A}";AT 14,5;"{A}{A}{A}{A}";TAB 12;"{A}{A}{A}{A}";AT 13,5;"{A}{A}{A}{A}";TAB 12;"{A}{A}{A}{A}";AT 12,4;"{A}{A}{A}{A}{A}{A}";TAB 11;"{A}{A}{A}{A}{A}{A}"
7130 NEXT F
7135 IF TH=HE THEN GOSUB 8700
7140 IF G=0 THEN RETURN 
7150 FOR F=1 TO G
7160 IF K=4 THEN LET GS=GS-(RND*500)
7165 IF K=5 THEN LET GS=GS-(RND*1200)
7170 NEXT F
7180 PRINT AT 18,0;Z$;Z$;AT 18,4;"WE HAVE BEEN HIT ";(G AND G>1);(" TIMES" AND G>1);("NCE" AND G=1);TAB 2;"OUR DAMAGE STANDS AT ";(100-(INT ((GS/8E3)*100)));"{2}/{4}"
7185 PRINT Z$;Z$
7190 LET SM=INT (.26*(GS/120))
7192 FOR F=1 TO 40
7193 NEXT F
7200 RETURN 
7400 CLS 
7405 GOSUB 8110
7410 PRINT AT 6,4;"YOU HAVE RUN AGROUND"
7490 GOTO 7506
7500 CLS 
7505 GOSUB 8110
7506 PRINT AT 3,11;"[G][R][A][F][ ][S][P][E][E]"
7510 PRINT AT 9,2;"HARD LUCK CAPTAIN YOUR SHIP";TAB 2;"HAS SUNK WITH THE LOSS OF ALL";TAB 2;"YOUR CREW"
7520 PRINT TAB 2;"SCORE=";INT SCORE
7530 PRINT AT 21,6;"[P][R][E][S][S][ ][Y][ ][F][O][R][ ][N][E][W][ ][G][A][M][E]"
7540 IF INKEY$="" THEN GOTO 7540
7550 IF INKEY$="N" THEN STOP 
7555 IF INKEY$="Y" THEN RUN 
7560 GOTO 7540
8004 LET SA=((SP/15)*(TR<3E4))+((SP/5)*(TR>=3E4 AND TR<6E4))+(SP*(TR>=6E4))
8005 LET FU=FU-(SA*(SP>0))-(.5*SA*(SP>15))-(.5*SA*(SP>20))
8006 IF FU<1 THEN LET FU=0
8007 IF FU=0 THEN LET SP=0
8008 PRINT AT 5,20;"[ ][ ][ ][ ][ ][ ][ ][ ][ ][ ]";AT 5,20;INT FU;" TONS"
8009 RETURN 
8010 LET NS=NS+(SA*(HE=6))-(SA*(HE=2))-(SA*.7*(HE=1 OR HE=3))+(SA*.7*(HE=7 OR HE=5))
8020 PRINT AT 13,24;"[ ][ ][ ][ ][ ]";AT 13,24;INT NS
8030 LET EW=EW+(SA*(HE=4))-(SA*(HE=8))-(SA*.7*(HE=1 OR HE=7))+(SA*.7*(HE=3 OR HE=5))
8040 PRINT AT 14,24;"[ ][ ][ ][ ][ ]";AT 14,24;INT EW
8045 IF CL=1 THEN RETURN 
8050 LET TR=(SQR (((ABS (TX-EW))**2)+((ABS (TY-NS))**2)))*2000
8060 RETURN 
8100 CLS 
8102 LET Z$="                                "
8103 LET A$="{A}                              {A}"
8104 LET N=0
8105 GOTO 8120
8115 PRINT AT 0,0;"{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}"
8116 PRINT A$;A$;A$;A$;A$;A$;A$;A$;A$;A$
8117 PRINT A$;A$;A$;A$;A$;A$;A$;A$;A$;A$
8118 PRINT "{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}{A}"
8119 RETURN 
8120 GOSUB 8110
8130 PRINT AT 1,11;"GRAF SPEE";TAB 11;"{7}{7}{7}{7}{7}{7}{7}{7}{7}";TAB 2;"[C][O][P][Y][R][I][G][H][T][ ][S][.][R][.][M][A][N][S][F][I][E][L][D][ ][1][9][8][2]"
8150 PRINT AT 6,2;"THE YEAR IS 1940,AND YOU ARE";TAB 2;"IN COMMAND OF THE VESSEL,THE"
8170 PRINT "{A} ADMIRAL GRAF SPEE A POCKET-"
8175 PRINT "{A} BATTLESHIP."
8180 PRINT AT 11,0;"{A} YOUR SHIP IS ARMED WITH SIX"
8190 PRINT "{A} 11 INCH GUNS AND TWO TORPEDO"
8200 PRINT "{A} TUBES."
8210 PRINT AT 15,0;"{A} YOUR OBJECTIVE IS TO SINK AS"
8220 PRINT "{A} MANY ENEMY SHIPS AS POSSIBLE."
8260 GOSUB 8400
8280 GOSUB 8110
8285 PRINT AT 1,11;"[G][R][A][F][ ][S][P][E][E]"
8290 PRINT AT 5,1;"THE DISPLAY WILL SHOW A MAP OF";TAB 2;"THE ATLANTIC." 
8300 PRINT AT 8,2;"A TEMPORARY FLASHING PIXEL";TAB 3;"WILL SHOW THE APPROXIMATE";TAB 2;"POSITION OF AN ALLIED SHIP."
8310 PRINT AT 12,2;"THE MAIN FLASHING PIXEL SHOWS";TAB 2;"YOUR POSITION."
8330 PRINT AT 15,1;"TO DESTROY THE TARGET YOU MUST";TAB 2;"GET WITHIN VISUAL RANGE,AND";TAB 2;"THEN FIRE."
8370 GOSUB 8400
8390 RETURN 
8400 PRINT AT 20,3;"[P][R][E][S][S][ ][N][E][W][L][I][N][E][ ][T][O][ ][C][O][N][T][I][N][U][E]"
8410 IF INKEY$="" THEN GOTO 8410
8415 PRINT AT 20,3;Z$(1 TO 25)
8420 RETURN 
8500 LET IX=((TY-NS)+(TY=NS))/((TX-EW)+(TX=EW))
8510 LET TH=(IX>.5 AND IX<2)+(2*(IX>2 OR IX<-2))+(3*(IX>-2 AND IX<-.5))+(4*(TY>NS))
8520 IF (IX>-.5 AND IX<.5) THEN LET TH=1+(3*(TX>EW))+(7*(TX<EW))
8530 RETURN 
8600 CLS 
8605 GOSUB 8110
8610 PRINT AT 4,11;"[G][R][A][F][ ][S][P][E][E]";AT 9,4;"CONGRATULATIONS CAPTAIN";AT 11,2;" YOU HAVE BEEN PROMOTED TO","{A} ADMIRAL YOUR SCORE=";SCORE
8620 GOTO 7530
8700 IF KKK<>K+D THEN PRINT AT 7,5;Z$( TO 10),TAB 5;Z$( TO 10),TAB 5;Z$( TO 10)
8710 IF D=1 THEN PRINT AT 8,5;"{D} {D}";TAB 6;"   {S}"
8720 IF D=2 THEN PRINT AT 7,5;"{S} {S}{D}";TAB 8;" {D}";TAB 5;"     {4}     "
8730 IF K=1 AND D=3 THEN PRINT AT 7,5;"{S}{D}",TAB 7;"{S}",TAB 7;"{Q}{4} {E} {E}"
8740 IF K=1 AND D=4 THEN PRINT AT 7,6;"{D}",TAB 7;"{4}/ V  V",TAB 6;"{Q}[ ]{6}{6}[ ]{6}{6}[ ]{6}"
8750 IF K=2 AND D=3 THEN PRINT AT 7,6;"{S} {D}",TAB 9;"{S}",TAB 10;"{4}{4}"
8760 IF K=2 AND D=4 THEN PRINT AT 7,5;"{S} {D}",TAB 9;"{S}{4}{4}";TAB 6;"{E}{W}{6}{Q}[ ]{W}{6}{Q}{4}"
8770 IF K=3 AND D=3 THEN PRINT AT 7,5;"{S}{D}",TAB 8;"{S}",TAB 5;"     {Q}{4}     "
8780 IF K=3 AND D=4 THEN PRINT AT 7,5;"{S}{D}",TAB 8;"{4}",TAB 7;"{Q}[ ]{6}{6}{6}{Q}{4}"
8790 IF K=4 AND D=3 THEN PRINT AT 8,5;" {S} {D}",TAB 5;"     {S}{4}{4}    "
8800 IF K=4 AND D=4 THEN PRINT AT 7,5;"{S} {D}",TAB 8;"{S}{4}{4}",TAB 6;"{E}[ ]{Q}[ ]{W}[ ]{4}"
8810 IF K=5 AND D=3 THEN PRINT AT 7,5;"{S} {D}{S}",TAB 8;"{S}{4}{4}",TAB 5;"    {W}[ ]    "
8820 IF K=5 AND D=4 THEN PRINT AT 7,5;"{S} {S}{E}{E}",TAB 7;"{6}{8}{Q}{5}{6}",TAB 5;"{Q}{W}[ ][ ][ ][ ][ ]{W}[ ]{6}"
8825 LET KKK=K+D
8830 RETURN 
8900 FOR G=1 TO 7
8910 PRINT AT 17-G,3+G;"[/]";AT 17-G,3+G;"{A}"
8920 NEXT G
8930 RETURN 
9005 LET PX=10
9006 LET PY=22
9050 LET NS=1000
9060 LET EW=1700
9100 LET HE=6
9110 LET TH=10
9130 LET A=0
9140 DIM B$(8,10)
9150 LET B$(1)="NORTH/WEST"
9160 LET B$(2)="  NORTH"
9170 LET B$(3)="NORTH/EAST"
9180 LET B$(4)="   EAST"
9190 LET B$(5)="SOUTH/EAST"
9200 LET B$(6)="  SOUTH"
9210 LET B$(7)="SOUTH/WEST"
9220 LET B$(8)="   WEST"
9230 LET C=0
9240 LET D=-1
9250 LET B=0
9280 LET SM=26
9290 LET KKK=0
9291 LET SH=200
9292 LET TO=20
9293 LET FU=8E3
9300 RETURN 
9997 CLEAR 
9998 SAVE "G[S]"
9999 RUN 

----- END OF LISTING -----
