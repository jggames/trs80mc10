0 DIM W(4,25),IW(3,25),N(3),P(25)
10 CLS:PRINT"ENIGMA SIMULATOR":PRINT"WRITTEN BY JOHN A. SHONDER":PRINT"PRESS ANY KEY TO BEGIN."
130 Q$=INKEY$:IF Q$="" THEN 130
140 CLS
150 PRINT"ROTOR SELECTION"
160 PRINT
170 PRINT"THE ENIGMA USES THREE ROTORS,":PRINT"ONE EACH IN POSITIONS 1-3."
180 PRINT"THE BASKET CONTAINS 10 ROTORS":PRINT"LABELED 0-9. PICK ANY THREE."
190 PRINT:PRINT
200 FOR I=1 TO 3
210 REM GET THE CONNECTIONS FOR EACH ROTOR
220 PRINT"ROTOR TO BE USED IN POSITION";I
230 INPUT X
240 IF X<0 OR X>9 THEN PRINT"ENTER A NUMBER FROM 0 TO 9.":GOTO 210
250 RESTORE:LOL=26*X:IF LOL>0 THEN FOR FF=1 TO LOL:READ Z:NEXT FF
260 FOR J=0 TO 25:READW(I,J):PRINTW(I,J);:NEXT J:PRINT
270 RESTORE:FOR FF=1 TO 260:READ Z:NEXT FF:NEXT I
280 PRINT"PRESS ANY KEY TO CONTINUE"
310 Q$=INKEY$:IF Q$="" THEN 310
320 CLS
330 PRINT"REFLECTOR SELECTION"
340 PRINT
350 PRINT"THE BASKET ALSO CONTAINS THREE":PRINT"DIFFERENT REFLECTORS LABELED"
360 PRINT"1-3.  CHOOSE ANY ONE."
370 PRINT:REM GET THE CONNECTIONS FOR THE REFLECTOR
380 INPUT "WHICH REFLECTOR";X
390 IF X<1 OR X>3 THEN PRINT"ENTER A NUMBER FROM 1 TO 3.":GOTO 380
400 RESTORE:LOL=26*(X+9):IF LOL>0 THENFOR FF=1 TO LOL:READ Z:NEXT FF
410 FOR I=0 TO 25:READW(4,I):PRINTW(4,I);:NEXT I:PRINT
430 PRINT"PRESS ANY KEY TO CONTINUE"
470 Q$=INKEY$:IF Q$="" THEN 470
480 CLS
490 PRINT"RING SETTINGS"
500 PRINT
510 PRINT"THE RING ON EACH ROTOR IS":PRINT"INITIALLY SET TO POSITION 26.":PRINT"ENTER THE SETTING"
520 PRINT"FOR EACH RING. ENTER AN ":PRINT"NUMBER BETWEEN 1 AND 26."
530 PRINT
540 FOR I=1 TO 3
550 PRINT"INITIAL SETTTING OF ROTOR";I
560 INPUT X
570 IF X<1 OR X>26 THEN PRINT"ENTER A # BETWEEN 1 AND 26.":GOTO 550
580 FOR J=0 TO 25:Z1=(W(I,J)+X)/26:Z2=INT(Z1):W(I,J)=(Z1-Z2)*26:PRINT W(I,J):NEXT J
590 NEXT I
600 PRINT"PRESS ANY KEY TO CONTINUE"
640 Q$=INKEY$:IF Q$="" THEN 640
650 CLS
660 PRINT"INITIAL ROTOR SETTINGS"
670 PRINT
680 PRINT"ENTER THE INITIAL SETTING FOR":PRINT"EACH ROTOR. ENTER A LETTER."
690 PRINT:PRINT
700 FOR I=1 TO 3
710 PRINT"INITIAL POSITION OF ROTOR";I
720 INPUT Q$:N(I)=ASC(Q$)-65:IF ((N(I)<0) OR (N(I)>25)) THEN PRINT"ENTER A CAPITAL LETTER.":GOTO 710
730 IF W(I,0)=N(I) THEN 800
740 T=W(I,25):FOR J=25 TO 1 STEP -1:W(I,J)=W(I,J-1):NEXT J:W(I,0)=T
750 GOTO 730
800 FOR J=0 TO 25:IW(I,W(I,J))=J:NEXT J
810 NEXT I
820 PRINT"PRESS ANY KEY TO CONTINUE"
860 Q$=INKEY$:IF Q$="" THEN 860
870 CLS
880 PRINT"PLUG CONNECTIONS"
890 PRINT
900 PRINT"NOW SET THE PLUG CONNECTIONS.":PRINT"THE ENIGMA CAN BE SET WITH"
910 PRINT"0-13 PLUGS. AT THE PROMPT,":PRINT"ENTER THE PAIR OF LETTERS":PRINT"TO BE CONNECTED, EG. 'AX'"
920 PRINT"HIT ENTER WHEN YOU ARE DONE."
930 PRINT
940 FOR I=0 TO 25:P(I)=-1:NEXT I
950 FOR I=1 TO 13
960 PRINT"PLUG CONNECTION ";I;:INPUT A$
970 IF A$="" THEN 1080
980 P1$=LEFT$(A$,1):P1=ASC(P1$)-65
990 P2$=RIGHT$(A$,1):P2=ASC(P2$)-65
1000 IF ((P1<0) OR (P1>25) OR (P2<0) OR (P2>25)) THEN PRINT"ENTER A PAIR OF CAPITAL LETTERS.":GOTO 960
1010 IF P1=P2 THEN PRINT"THE TWO LETTERS MUST BE DISTINCT.":GOTO 960
1020 IF (P(P1)<>-1) OR (P(P2)<>-1) THEN PRINT"LETTERS ALREADY BEEN USED.":PRINT"ENTER A DIFFERENT PAIR.":GOTO 960
1030 P(P1)=P2
1040 P(P2)=P1
1050 NEXT I
1080 FOR I=0 TO 25
1090 IF P(I)=-1 THEN P(I)=I
1100 NEXT I
1110 PRINT"PRESS ANY KEY TO CONTINUE"
1120 Q$=INKEY$:IF Q$="" THEN 1120
1180 X=0
1190 CLS
1200 O1=TIMER
1220 INPUT TX$:IF TX$="" THEN 1290
1240 GOSUB 1310:REM PROCESS A LINE OF TEXT
1250 PRINT:GOTO 1220:REM READ ANOTHER LINE
1290 OEL=O2-O1
1300 PRINTX;"CHARACTERS PROCESSED.":END
1310 REM THIS SUBROUTINE PROCESSES ONE LINE OF TEXT
1320 FOR I=1 TO LEN(TX$)
1330 W=ASC(MID$(TX$,I,1))-65:REM PICK OFF A CHARACTER AND CONVERT TO INTEGER
1340 IF (W<0) OR (W>25) THEN 1470:REM IF NOT A CAPITAL LETTER, GET NEXT CHARACTER
1350 U=P(W):REM SEND CHARACTER THROUGH PLUG CONNECTION
1360 FOR J=1 TO 4:U=W(J,U):NEXT J:REM SEND CHARACTER THROUGH ROTORS AND REFLECTOR
1370 FOR J=3 TO 1 STEP -1:U=IW(J,U):NEXT J:REM SEND CHARACTER BACK THROUGH ROTORS
1380 U=P(U)+65:C$=CHR$(U):REM SEND CHARACTER THROUGH PLUG CONNECTION
1400 X=X+1:REM COUNT CHARACTERS FOR REFERENCE
1410 PRINTC$;:REM PRINT TRANSFORMED CHARACTER TO OUTPUT
1420 IF ((X/5)-INT(X/5))*5=0 THEN PRINT" ";:REM PRINT IN BLOCKS OF FIVE
1430 IF ((X/65)-INT(X/65))*65=0 THEN PRINT:REM PRINT 13 BLOCKS PER LINE
1440 R=3:GOSUB 1490:REM INCREMENT ROTOR 3
1450 IF W(3,0)<>N(3) THEN 1470:REM CHECK IF ROTOR 3 HAS MADE A COMPLETE REVOLUTION
1460 R=2:GOSUB 1490:REM IF SO,THEN INCREMENT ROTOR 2
1462 IF W(2,0)<>N(2) THEN 1470:REM CHECK IF ROTOR 2 HAS MADE A COMPLETE REVOLUTION
1464 R=1:GOSUB 1490:REM IF SO, THEN INCREMENT ROTOR 1
1470 NEXT I
1480 RETURN
1490 REM THIS SUBROUTINE INCREMENTS THE ROTORS
1500 T=W(R,25):FOR J=25 TO 1 STEP -1:W(R,J)=W(R,J-1):NEXT J:W(R,0)=T
1510 FOR J=0 TO 25:Z1=(IW(R,J)+1)/26:Z2=INT(Z1):IW(R,J)=(Z1-Z2)*26:NEXT J
1520 RETURN
5000 REM ROTOR 0
5005 DATA 2,6,16,17,1,19,8,25,24,20,22,13,0,14,11,10,9,7,5,12,23,18,15,21,4,3
5010 REM ROTOR 1
5015 DATA 5,22,0,25,20,7,10,9,1,11,24,15,16,19,14,18,3,13,6,8,17,23,4,21,2,12
5020 REM ROTOR 2
5025 DATA 23,11,1,18,10,6,21,15,0,16,24,14,9,8,5,2,17,25,7,12,4,20,3,13,22,19
5030 REM ROTOR 3
5035 DATA 1,9,4,18,6,0,10,24,14,20,16,13,19,5,15,17,8,22,21,11,23,12,25,7,3,2
5040 REM ROTOR 4
5045 DATA 18,25,16,9,2,3,10,8,11,7,4,13,21,23,19,5,0,20,1,14,6,12,15,24,22,17
5050 REM ROTOR 5
5055 DATA 23,24,6,25,19,16,11,17,4,1,2,22,21,14,20,13,10,12,0,3,18,7,15,9,8,5
5060 REM ROTOR 6
5065 DATA 11,10,17,6,7,2,20,5,16,14,3,1,22,4,12,13,23,15,24,19,21,0,25,9,18,8
5070 REM ROTOR 7
5075 DATA 24,3,19,10,2,22,21,9,8,16,4,7,6,14,20,12,0,15,25,13,18,23,5,17,11,1
5080 REM ROTOR 8
5085 DATA 17,12,5,9,15,3,23,7,1,24,19,25,6,21,0,11,13,18,2,16,10,20,8,22,4,14
5090 REM ROTOR 9
5095 DATA 5,12,7,22,13,14,11,17,21,9,1,20,10,8,0,15,24,16,18,23,2,6,19,25,3,4
5100 REM REFLECTOR 1
5105 DATA 2,4,0,21,1,13,8,9,6,7,18,25,20,5,16,19,14,22,10,15,12,3,17,24,23,11
5110 REM REFLECTOR 2
5115 DATA 5,22,23,11,12,0,25,18,24,16,19,3,4,20,15,14,9,21,7,10,13,17,1,2,8,6
5120 REM REFLECTOR 3
5125 DATA 18,11,3,2,10,15,25,24,17,22,4,1,21,19,16,5,14,8,0,13,23,12,9,20,7,6
