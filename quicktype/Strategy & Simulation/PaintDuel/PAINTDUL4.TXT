0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO130
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-16384+ZZ,:RETURN
10 M$="PRESS ENTER TO CONTINUE":GOSUB7:INPUTA$:CLS:RETURN
40 REM
70 REM 1. INITIALIZE SECTION
80 REM
100 REM PAINT DUEL BY LLOYD JOHNSON
110 REM 7/81 CREATIVE COMPUTING P.186
120 REM ADAPTED TO MC-10 BY JIM GERRIE
130 CC=RND(-(PEEK(9)*256+PEEK(10)))
135 DIM B(15,15):CLS:PRINT@32*7+11,"PAINT DUEL":PRINT:PRINT:PRINT"WANT INSTRUCTIONS (Y/N)";:INPUT A$
140 IF A$="Y" THEN 170
150 IF A$="N" THEN 620
170 CLS
180 M$="DUE TO SOME LONG AND COMPLICATED STORY, YOU ARE ENGAGED IN A TANK DUEL WITH A COMPUTER.":GOSUB1
190 M$="THE TANKS ARE NOT ALLOWED TO DESTROY EACH OTHER, BUT ARE ALLOWED TO LAUNCH CANISTERS OF PAINT AT EACH OTHER.":GOSUB1
200 M$="THE DUEL ENDS WHEN ONE OR BOTH TANKS ARE COVERED WITH PAINT.":GOSUB1
210 M$="THE DUEL TAKES PLACE ON A LANDSCAPE WHICH IS 9 SQUARES LONG AND 9 SQUARE WIDE.":GOSUB1
220 GOSUB10
300 M$="INITIALLY THE COMPUTER KNOWS YOUR PRESENT POSITION WHICH IS 8,5 (ROW 8, COLUMN 5).":GOSUB1
470 M$="ALL YOU KNOW OF THE COMPUTER'S INITIAL POSITION IS THAT IT IS LOCATED SOMEWHERE ON THE FIRST ROW.":GOSUB1
480 M$="YOU HAVE THE OPTION OF SHOOTING OR MOVING AT THE COMPUTER.":GOSUB1
490 M$="YOUR POSITION CAN ONLY BE IDENTIFIED BY THE COMPUTER IF YOUR SHOOT.":GOSUB1
500 M$="LIKEWISE YOU CAN ONLY IDENTIFY THE COMPUTER'S POSITION IF IT SHOOTS.":GOSUB1
501 GOSUB10
510 M$="A SHOT FIRED INTO A SQUARE WILL COVER THE SQUARE COMPLETELY WITH PAINT.":GOSUB1
515 M$="NEITHER YOU OR THE COMPUTER WILL BE ALLOWED TO ENTER IT.":GOSUB1
520 M$="BOTH YOU AND THE COMPUTER COMPLETE YOUR TURN (MOVE OR SHOOT) SIMULTANEOUSLY"
525 M$=M$+" WITHOUT ANY KNOWLEDGE OF WHAT THE OTHER IS DOING.":GOSUB1
530 GOSUB10
540 M$="IF YOU CHOOSE THE MOVE OPTION YOU MAY EITHER HOLD YOUR PRESENT POSITION WITHOUT SHOOTING OR MOVE ONE SQUARE "
545 M$=M$+"IN ANY DIRECTION PROVIDED THE SQUARE YOU ARE MOVING INTO IS NOT COVERED WITH PAINT OR OFF THE LANDSCAPE.":GOSUB1
550 GOSUB10
560 CLS:M$="THE COMMANDS AVAILABLE ARE AS FOLLOWS:":GOSUB1
580 PRINT TAB(5);"m EXECUTE MOVE OPTION
590 PRINT TAB(5);"s EXECUTE SHOOT OPTION
600 PRINT TAB(5);"d DISPLAY LANDSCAPE
610 PRINT:PRINT:INPUT"PRESS ENTER WHEN READY";R$
620 CLS
630 FOR I=1 TO 9
640 FOR J=1 TO 9
650 B(I,J)=0
660 NEXT:NEXT
670 R4=8:C4=5:B(R4,C4)=2:R1=8:C1=5:R2=1:C2=INT(9*RND(0))+1:B(R2,C2)=1:D1=0:D2=0:F1=0:F2=0:T1=0:T2=0:A=0:M1=0:R9=1
680 REM
681 REM 2. MAIN LOGIC SECTION
682 REM
690 T1=T1+1
700 GOSUB 800
710 IF M1=1 THEN 740
720 GOSUB 1220
730 GOTO 750
740 GOSUB 1330
750 GOSUB 1470
760 GOSUB 2330
770 GOSUB 2560
780 IF (D1+D2)=0 THEN 690
790 GOTO 2790
800 REM 2.1 GATHER DATA AND MOVE SHOOT DECISION
810 GOSUB 860
820 GOSUB 980
830 GOSUB 1090
840 GOSUB 1150
850 RETURN
860 REM 2.1.1 CALCULATE N2
870 N2=0
880 N=T1-T2
890 FOR I=-N TO N
900 IF R1+I<1 OR R1+I>9 THEN 960
910 FOR J=-N TO N
920 IF C1+J<1 OR C1+J>9 THEN 950
930 IF B(R1+I,C1+J)=4 THEN 950
940 N2=N2+1
950 NEXT J
960 NEXT I
970 RETURN
980 REM 2.1.2 CALCULATE N1
990 N1=0
1000 FOR I=-1 TO 1
1010 IF R2+I<1 OR R2+I>9 THEN 1070
1020 FOR J=-1 TO 1
1030 IF C2+J<1 OR C2+J>9 THEN 1060
1040 IF B(R2+I,C2+J)=4 THEN 1060
1050 N1=N1+1
1060 NEXT J
1070 NEXT I
1080 RETURN
1081 REM
1082 REM 2.1.3 IS COMPUTER ANGRY OR CALM
1083 REM
1090 IF F1<F2 OR RND(0)>.25 THEN 1120
1100 A=0
1110 GOTO 1140
1120 IF F1>F2 OR RND(0)>.25 THEN 1140
1130 A=20
1140 RETURN
1150 REM 2.1.4 MAKE MOVE OR SHOOT DECISION
1151 REM
1160 P=50-M1*40-5*(9-N1)+2.5*(ABS(9-N2)+9-N2)+A
1170 IF RND(0)*100>P THEN 1200
1180 M1=1
1190 GOTO 1210
1200 M1=0
1210 RETURN
1211 REM
1220 REM 2.2 COMPUTER MOVES
1221 REM 
1230 I=INT(RND(0)*3)-1
1240 IF R2+I<1 OR R2+I>9 THEN 1230
1250 J=INT(RND(0)*3)-1
1260 IF C2+J<1 OR C2+J>9 THEN 1250
1270 IF B(R2+I,C2+J)=4 THEN 1230
1280 B(R2,C2)=B(R2,C2)-1
1290 R2=R2+I
1300 C2=C2+J
1310 B(R2,C2)=B(R2,C2)+1
1320 RETURN
1330 REM
1331 REM 2.3 COMPUTER SHOOTS
1332 REM
1340 F1=F1+1
1350 IF RND(0)>.35 OR N>1 THEN 1380
1360 I=0:J=0
1370 GOTO 1430
1380 I=INT(RND(0)*(2*N+1))-N
1390 IF R1+I<1 OR R1+I>9 THEN 1380
1400 J=INT(RND(0)*(2*N+1))-N
1410 IF C1+J<1 OR C1+J>9 THEN 1400
1420 IF B(R1+I,C1+J)=4 THEN 1380
1430 IF R1+I=R2 AND C1+J=C2 AND N2>1 THEN 1380
1440 R3=R1+I
1450 C3=C1+J
1460 RETURN
1470 REM
1471 REM 2.4 ACCEPT HUMAN'S COMMAND
1472 REM
1480 PRINT:M$="TURN"+STR$(T1)+" COMMAND (M,S,D)":GOSUB7:INPUTA$
1490 IF A$<>"M" THEN 1530
1510 GOSUB 1640
1520 GOTO 1590
1530 IF A$<>"S" THEN 1560
1540 GOSUB 1900
1550 GOTO 1590
1560 IF A$<>"D" THEN 1610
1580 GOSUB 2110
1590 IF R9=0 THEN 1630
1600 GOTO 1480
1610 PRINT"INVALID COMMAND"
1620 GOTO 1480
1630 RETURN
1640 REM
1641 REM 2.4.1 INPUT HUMAN'S MOVE
1642 REM
1650 R9=1
1660 PRINT:PRINT"ROW,COLUMN OF DESTINATION";
1670 INPUT I,J
1680 IF ABS(R4-I)<2 AND ABS(C4-J)<2 THEN 1720
1690 PRINT
1700 PRINT"YOU CAN ONLY MOVE ONE SQUARE"
1710 GOTO 1890
1720 IF I>0 AND I<10 AND J>0 AND J<10 THEN 1760
1730 PRINT
1740 M$="YOU ARE NOT ALLOWED TO LEAVE THE LANSCAPE":GOSUB1
1750 GOTO 1890
1760 IF I=INT(I) AND J=INT(J) THEN 1800
1770 PRINT
1780 PRINT"INVALID COORDINATES"
1790 GOTO 1890
1800 IF B(I,J)<4 THEN 1840
1810 PRINT
1820 M$="MOVEMENT INTO SQUARES COVERED WITH PAINT IS NOT ALLOWED":GOSUB1
1830 GOTO 1890
1840 B(R4,C4)=B(R4,C4)-2
1850 R4=I
1860 C4=J
1870 B(R4,C4)=B(R4,C4)+2
1880 R9=0
1890 RETURN
1900 REM 2.4.2 INPUT HUMAN'S SHOT
1910 R9=1
1920 PRINT
1930 INPUT"ROW,COLUMN OF TARGET";I,J
1940 IF I>0 AND I<10 AND J>0 AND J<10 THEN 1990
1950 M$="NO SHOTS ALLOWED OFF THE LANDSCAPE":GOSUB1
1980 GOTO 2100
1990 IF I=INT(I) AND J=INT(J) THEN 2030
2000 PRINT
2010 PRINT"INVALID COORDINATES"
2020 GOTO 2100
2030 R5=I
2040 C5=J
2050 T2=T1
2060 F2=F2+1
2070 R9=0
2080 R1=R4
2090 C1=C4
2100 RETURN
2110 REM 2.4.3 DISPLAY LANDSCAPE
2120 R9=1
2130 PRINT "  ";
2140 FOR I=1 TO 9
2150 PRINT RIGHT$(" "+STR$(I),2);
2160 NEXT I
2170 PRINT
2180 FOR I=1 TO 9
2190 PRINT RIGHT$(" "+STR$(I),2);
2200 FOR J=1 TO 9
2210 IF B(I,J)>1 THEN 2240
2220 B$="."
2230 GOTO 2280
2240 IF B(I,J)<>4 THEN 2270
2250 B$="X"
2260 GOTO 2280
2270 B$="H"
2280 PRINT" ";B$;
2290 NEXT J
2300 PRINT
2310 NEXT I
2320 RETURN
2330 REM 2.5 EVALUATE HUMAN'S MOVE OR SHOT
2340 IF T1=T2 THEN 2380
2350 PRINT
2360 M$="YOUR NEW POSITION IS"+STR$(R4)+","+MID$(STR$(C4),2):GOSUB1
2370 GOTO 2550
2380 PRINT
2390 M$="YOUR SHOT WILL COVER"+STR$(R5)+","+MID$(STR$(C5),2)+" WITH PAINT":GOSUB1
2400 M$="THE COMPUTER HAS IDENTIFIED YOUR POSITION":GOSUB1
2420 IF B(R5,C5)>0 AND B(R5,C5)<4 THEN 2450
2430 B(R5,C5)=4
2440 GOTO 2550
2450 REM SOMETHING WAS PAINTED
2460 IF B(R5,C5)=2 THEN 2510
2470 PRINT
2480 M$="YOU HAVE SPLATTERED THE COMPUTER'S TANK WITH PAINT":GOSUB1
2490 SOUND 100,5
2500 D1=1
2510 IF B(R5,C5)=1 THEN 2550
2520 PRINT
2530 M$="YOU HAVE SPLATTERED YOUR OWN TANK WITH PAINT":GOSUB1
2540 D2=1
2550 RETURN
2560 REM 2.6 EVALUATE COMPUTERS MOVE OR SHOT
2570 IF M1=1 THEN 2620
2580 IF D1=1 THEN 2780
2590 PRINT
2600 M$="NO INFORMATION REGARDING THE COMPUTER'S TANK IS AVAILABLE":GOSUB1
2610 GOTO 2780
2620 PRINT
2630 M$="THE COMPUTER HAS FIRED A SHOT FROM"+STR$(R2)+","+MID$(STR$(C2),2):GOSUB1
2640 M$="WHICH HAS COVERED"+STR$(R3)+","+MID$(STR$(C3),2)+" WITH PAINT":GOSUB1
2660 IF B(R3,C3)>0 AND B(R3,C3)<4 THEN 2680
2670 B(R3,C3)=4:GOTO 2780
2680 REM SOMETHING WAS PAINTED
2690 IF D2=1 THEN 2780
2700 D2=1
2710 PRINT
2720 M$="THE COMPUTER HAS SPLATTERED YOUR TANK WITH PAINT":GOSUB1
2740 SOUND1,5
2750 IF B(R3,C3)=2 THEN 2780
2760 PRINT:M$="THE COMPUTER HAS SPLATTERED HIS OWN TANK WITH PAINT":GOSUB1
2770 D1=1
2780 RETURN
2790 REM 3.3 END PROGRAM SECTION
2800 IF D1+D2<2 THEN 2830
2810 PRINT:M$="TIE GAME-- BOTH TANKS COVERED IN PAINT":GOSUB1
2820 GOTO 2880
2830 IF D1=0 THEN 2860
2840 PRINT:M$="HUMAN WINS-- COMPUTER'S TANK COVERED IN PAINT":GOSUB1
2850 GOTO 2880
2860 PRINT
2870 M$="COMPUTER WINS-- HUMAN'S TANK COVERED IN PAINT":GOSUB1
2880 PRINT:PRINT"WANT TO TRY AGAIN (Y/N)";:INPUT A$
2890 CLS
2900 IF A$="Y" THEN 630
2910 IF A$="N" THEN 2930
2920 PRINT:GOTO 2880
2930 END
