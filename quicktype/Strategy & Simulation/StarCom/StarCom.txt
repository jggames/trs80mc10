30 CLEAR200:SCREEN1,1,1:CONSOLE0,16,0,0
40 DIMS(8,8),L(8,8),C$(8),P(8),W(3)
50 KL$="KLIMZON":DEFFNR(RN)=INT(RND(1)*RN+1):A$="50"
60 CLS:LOCATE 0,7:INPUT"How many KLINGONS (30-300)";A$:KL=VAL(A$)
70 IFKL<30ORKL>300THEN60
80 GOSUB4020:IFCC=0THENSCREEN1,1:CLS:LOCATE7,7:PRINT"STAR COMMAND SIGMA
90 W=KL/.8:W0=W-.3:M=430:E0=50:MP=0:BA=INT(KL/12)+1
100 REM
110 RESTORE4250:FORT=1TO3:W(T)=W-2.5:NEXT
120 FORT=&H1TO&H8:Q(T)=W:READA$:C$(T)=MID$(A$,2)
125 B$=LEFT$(A$,1):I$=I$+CHR$(ASC(B$)+20)
130 FORR=1TO8:L(T,R)=FNR(8)+10000:NEXT:NEXT:K=KL
140 T=FNR(8):R=FNR(8):A=FNR(5)
150 L=L(T,R):IFINT((L-10000)/100)+A>9THEN140
160 IFK<ATHENA=K
170 L(T,R)=L+A*100:K=K-A:IFK>0THEN140
180 FORB=1TOBA:T=FNR(8):R=FNR(8):A=FNR(8):L(T,R)=L(T,R)+10:NEXT
190 XX=FNR(8):YY=FNR(8):X=FNR(8):Y=FNR(8)
200 GOSUB2990:L=L(XX,YY):SCREEN 1,1,1:CLS
210 CONSOLE10,6:GOSUB1280:GOTO1180
220 REM
230 GOSUB240:GOTO510
240 IFMPTHENCONSOLE0,9:CLS:CONSOLE10,16:MP=0
250 IFQ(4)<WTHEN460
260 LOCATE0,0:PRINT"   1 2 3 4 5 6 7 8";SPC(20)
270 FORT=&H1TO&H8:LOCATE0,T:PRINTT;":";SPC(15)
280 FORR=1TO&H8:IFS(T,R)=0THEN360
290 S=S(T,R):LOCATE R*2+1,T:IFS>6THENPRINT"K":GOTO360
300 ONSGOSUB320,330,340,350
310 GOTO360
320 PRINT"E":RETURN
330 PRINT"*":RETURN
340 PRINT"B":RETURN
350 S(T,R)=0:RETURN
360 NEXT:NEXT:RETURN
370 REM
380 PRINT
390 PRINT"  315ß  0ß   45ß"
400 PRINT"  270ß  E    90ß"
410 PRINT"  225ß 180ß 135ß"
420 RETURN
430 LOCATE0,15:PRINT:LOCATE0,15:RETURN
440 LOCATE0,10:PRINTSPC(31)+CHR$(13):LOCATE0,10:RETURN
450 LOCATE INT(L+.5)*2+1,INT(S+.5):RETURN
460 REM
470 COLOR 2:FORT=1TO8
480 LOCATE3,T:PRINTSPC(15)
490 NEXT:LOCATEY*2+1,X:PRINT"E":COLOR1:RETURN
500 PRINT:PLAY "O7A4":PRINT"Illegal Function call!":PRINTSPC(32)
510 REM
520 FORT=1TO3:IFW-W(T)<2.5THENW(T)=W-2.5
530 NEXT:IFW<W0THENW0=W-.3:GOSUB2650
540 E=E+E1:E1=E*E0/100:E=E-E1:IFMP=0THENGOSUB2100
550 IFQ(4)<WTHENGOSUB460
560 GOSUB2250:IFRND(1)<.02+(430-M)/699THENCLS:GOSUB2910
570 LOCATE0,9:PRINTSPC(32):LOCATE0,9
580 IFE<0ORM<0ORW<0THEN3120
590 IFKL<1THEN3250
600 IFINKEY$<>""THEN600
610 W=W-.02:PLAY "V15O6C24":PRINT"COMMAND?":LOCATE9,9:C=0
620 C$=INKEY$:IFC$=""ORC$="&"THEN620
630 C=VAL(C$):IFC>0ANDC<9THENPRINTC$(C);
640 IFC=0ORC>8THEN700
650 IFQ(C)>=WTHEN670
660 CLS:T2=C:PRINT:GOSUB2950:GOTO570
670 C$=INKEY$:IFC$=""THEN670
680 IFC$<>CHR$(13)THEN570
690 ONCGOTO3360,3620,1870,230,1970,720,1050,1470
700 CLS:FORT=1TO8:PRINT":"RIGHT$(STR$(T),1)" "C$(T),:NEXT
710 PLAY"S9M3000O6E8C4":GOTO570
720 REM
730 CLS:LOCATE23,9:PRINT"TEMP"RIGHT$(" "+STR$(INT((W-W(3))*10)),3)"ßC";
740 IFW-W(3)>30THEN1860
750 A$="":GOSUB440:INPUT"Course ";A$:O=VAL(A$):IFA$="r"THEN500
760 IF(O=0ANDA$<>"0")ORO>360THENGOSUB370:GOTO750
770 GOSUB440:INPUT"Sector ";T$:T=VAL(T$):IFT<1ORT>99THEN500
780 GOSUB1840:W=W-T*.02:E=E-T*3:W(3)=W(3)-T*.25:B1=0
790 IFT>1ANDO-INT(O/90)*90=45THENT=T+1
800 GOSUB2970:S(X,Y)=0:S=X:L=Y:GOSUB450
810 FORT1=1TOT:X=X-B
820 IFX>8.4THENX=1:L=180:GOTO890
830 IFX<.5THENX=8:L=0:GOTO890
840 Y=Y+A:IFY>8.4THENY=1:L=90:GOTO890
850 IFY<.5THENY=8:L=270:GOTO890
860 GOSUB450:S=INT(X+.5):L=INT(Y+.5):IFS(S,L)THEN920
870 PRINT" ":GOSUB450:PRINT"E":PLAY"V15O5A64":NEXT
880 X=INT(X+.5):Y=INT(Y+.5):S(X,Y)=1:GOTO230
890 Z1=T-T1:Z2=O:O=L:T=1:W=W-.7:X=INT(X+.5):Y=INT(Y+.5)
900 S(X,Y)=1:GOSUB1210:GOSUB240:T=Z1:O=Z2:IFT=0THEN880
910 GOTO800
920 X=INT(X+.5):Y=INT(Y+.5):IFS(S,L)>6THEN1000
930 PRINT"E":IFS(X,Y)<>3THENGOSUB440:GOTO960
940 S(X,Y)=0:GOSUB3010:GOTO880
950 X=X+B:Y=Y-A:GOTO880
960 GOSUB3970:PRINT"DANGER !";
970 IFQ(8)>=WTHENPRINT"Emergency STOP!":GOTO950
980 PRINT" BIG DAMAGE 700 units":GOSUB3870
990 E1=E1-700:GOSUB2910:GOTO950
1000 GOSUB3870
1010 IFS(X,Y)>T*150THEN E1=E1-S(X,Y):IFE1<0THENE=E+E1:E1=0
1020 PRINT" ":T=INT(T-(S(X,Y)/150)):IFS(X,Y)>E1/9THENGOSUB430:GOSUB2910
1030 KL=KL-1:V=V-1:GOSUB450:PRINT"E":S(X,Y)=0:IFT<1THEN880
1040 GOTO860
1050 REM
1060 CLS:LOCATE22,9:PRINT"TEMP"INT((W-W(2))*10)"ßC";:IFW-W(2)>30THEN1860
1070 A$="":GOSUB440:INPUT"Course ";A$:O=VAL(A$)
1080 IFA$="r"ORO<0THEN500
1090 IF(O=0ANDA$<>"0")ORO>360THENGOSUB370:GOTO1070
1100 A$="":GOSUB440:INPUT"Quod.  ";A$:T=VAL(A$):IFT<1ORT>8THEN500
1110 GOSUB440:INPUT"Speed  ";A$:SE=VAL(A$)
1120 IFSE<1ORSE>15THEN500
1130 W=W-(1/SE):E=E-T*SE*128:W(2)=W(2)-T*SE/1.57
1140 CLS:PRINT"":PRINT"USE ENERGY is"T*SE*128" at";
1150 PRINTINT((1/SE)*100)/100" time"
1160 GOSUB1840:LOCATEY*2+1,X:PRINT" "
1170 S=X:L=Y:GOSUB1210
1180 GOSUB240:IFQ(8)<WTHEN510
1190 IFQ(3)<WORV>0THEN1590
1200 GOTO1870
1210 L(XX,YY)=V*100+H1*10+H
1220 GOSUB2970:FORT1=1TOT:XX=XX-B:YY=YY+A:NEXT
1230 IFXX>8.4THENXX=XX-8
1240 IFYY>8.4THENYY=YY-8
1250 IFXX<.5THENXX=XX+8
1260 IFYY<.5THENYY=YY+8
1270 XX=INT(XX+.5):YY=INT(YY+.5)
1280 REM
1290 IFL(XX,YY)>9999THENL(XX,YY)=L(XX,YY)-10000
1300 L=L(XX,YY):FORT=1TO8:FORR=1TO8:S(T,R)=0:NEXT:NEXT
1310 S1=INT(L/100):S2=INT(L/10)-S1*10:S3=L-S1*100-S2*10:V=S1
1320 S(X,Y)=1:IFS1=0THEN1380
1330 FORA=1TOS1
1340 T=FNR(8):R=FNR(8):IFS(T,R)<>0THEN1340
1350 S(T,R)=RND(1)*RND(1)*6000+999
1360 IFRND(1)<.02THENS(T,R)=S(T,R)+RND(1)*5000
1370 NEXT
1380 IFS2=0THEN1420
1390 FORA=1TOS2
1400 T=FNR(8):R=FNR(8):IFS(T,R)THEN1400
1410 S(T,R)=3:NEXT
1420 IFS3<1THEN1460
1430 FORA=1TOS3
1440 T=FNR(8):R=FNR(8):IF S(T,R) THEN1440
1450 S(T,R)=2:NEXT
1460 V=S1:H1=S2:H=S3:RETURN
1470 REM
1480 CLS:A$="":LOCATE0,9:PRINTSPC(32)
1490 LOCATE0,9:PRINTSPC(32):LOCATE0,9:INPUT"COMPUTER JOB";A$
1500 IFA$="r"THEN500
1510 CJ=VAL(A$):IFCJ<1ORCJ>4THEN1530
1520 CLS:ONCJGOTO1590,1650,1750,1790
1530 PRINT":1 ENERGY OF KLINGONS"
1540 PRINT":2 ASTEROID BELT"
1550 PRINT":3 BREAKDOWM DISPLAY"
1560 PRINT":4 GET ENERGY FROM STAR"
1570 CJ$="":GOTO1490
1590 CLS:FORT=1TO8:FORR=1TO8:IFS(T,R)<6THEN1630
1600 PRINTR;-T" ¥¥¥";
1610 IFS(T,R)>5000ORRND(1)<.03THENPRINT" ???",:GOTO1630
1620 PRINTINT(S(T,R)),
1630 NEXT:NEXT:PRINT:PRINT" Offensive Cycle  is "INT((W-W0)*100)/100;
1640 GOTO230
1650 PLAY"S12M1000O5","S12M1000O5"
1670 FORT=1TO8:FORR=1TO8:IFS(T,R)<>2THEN1730
1680 PLAY"AAA","CCC"
1690 S(T,R)=0:GOSUB3060:S=T:L=R:GOSUB450
1700 S=S+B:L=L+A:IFS(S+.5,L+.5)THEN1720
1710 PRINT" ":GOSUB450:PRINT"*":GOSUB450:GOTO1700
1720 E=E-64:S=S-B:L=L-A:S(S+.5,L+.5)=2:GOSUB450:PRINT"*"
1730 NEXT:NEXT:PRINTCHR$(7):GOTO510
1750 FORT=1TO8:IFQ(T)>WTHEN1770
1760 PRINTC$(T);" at"INT((W-Q(T))*100)/100;" time"
1770 NEXT:GOTO510
1790 IFH=0THENPRINT" NO STAR!":GOTO510
1800 INPUT"How many times";T$:T=VAL(T$):IFT<0.1ORT>11THEN500
1810 E=E+T*64*H:W=W-T:PRINT
1820 PRINT" CHARGE ENERGY is"T*64*H" units"
1830 GOTO510
1840 IFMP=1THENT0=T:GOSUB240:T=T0
1850 RETURN
1860 PLAY"V15O6A2":CLS:PRINT" OVER HEAT!!":GOTO510
1870 REM
1880 W=W-.1:GOSUB430
1890 FORT1=XX-1TOXX+1:PRINT"   ";:FORR1=YY-1TOYY+1:T=T1:R=R1
1900 IFT1<1THENT=8
1910 IFT1>8THENT=1
1920 IFR1<1THENR=8
1930 IFR1>8THENR=1
1940 L=L(T,R):IFL>9999THENL(T,R)=L-10000:L=L-10000
1950 GOSUB2040
1960 NEXT:PRINT:NEXT:GOTO510
1970 REM
1980 LOCATE0,0
1990 FORT=1TO8:FORR=1TO8
2000 L=L(T,R):IFL>9999THENPRINT" ***";:GOTO2020
2010 GOSUB2040
2020 NEXT:NEXT:PRINT
2030 MP=1:GOTO570
2040 PRINT" ";:IFT=XXANDR=YYTHENL=V*100+H1*10+H:COLOR2
2050 A$=MID$(STR$(L),2,LEN(STR$(L)))
2060 IFL<100THENPRINT"0";
2070 IFL<10THENPRINT"0";
2080 PRINTA$;
2090 COLOR1:RETURN
2100 REM
2110 S$="      "
2120 A$=STR$(INT(E)):LOCATE18,1:PRINT":ENERGY ";:GOSUB2240
2130 A$=STR$(INT(E1)):LOCATE18,2:PRINT":SHIELD ";:GOSUB2240
2140 A$=STR$(M):LOCATE18,3:PRINT":MEN    ";:GOSUB2240
2150 A$=STR$(I):LOCATE18,4:PRINT":TORPEDO";:GOSUB2240
2160 A$=STR$(INT(W)):LOCATE18,5:PRINT":TIME   ";:GOSUB2240
2170 A$=STR$(KL):LOCATE18,6:PRINT":"KL$;:GOSUB2240
2180 A$=STR$(BA):LOCATE18,7:PRINT":BASE   ";:GOSUB2240
2190 LOCATE18,8:PRINT":CONDITION ";
2200 IFE<1000THENCOLOR4:PRINT"YEW";:GOTO2230
2210 IFVTHENCOLOR3:PRINT"RED";:GOTO2230
2220 COLOR2:PRINT"GRN";
2230 COLOR1:PRINT:RETURN
2240 PRINTLEFT$(S$,6-LEN(A$));A$;:RETURN
2250 REM
2260 GOSUB2540
2270 TM=FNR(8):RM=FNR(8):IFS(TM,RM)<6ORS(TM,RM)>1000THENRETURN
2280 LOCATERM*2+1,TM:PRINT" ":LOCATE0,15:PRINTRM;-TM" "KL$" ";
2290 SM=S(TM,RM):S(TM,RM)=0
2300 TM=TM+FNR(3)-2:RM=RM+FNR(3)-2
2310 IFTM<1ORTM>8ORRM<1ORRM>8THEN2350
2320 IFS(TM,RM)THEN2350
2330 PRINT"MOVE "RM;-TM" Point":LOCATERM*2+1,TM:PRINT"K"
2340 PLAY"S9M999O5A":S(TM,RM)=SM:RETURN
2350 PRINT"WARP!":PLAY"S7M5000O3C3"
2360 TM=FNR(8):RM=FNR(8):IFXX=TMANDYY=RMTHEN2360
2370 LM=L(TM,RM):IFLM<9999THENLM=LM+10000
2380 IFLM>10899THEN2360
2390 V=V-1:L(TM,RM)=LM+100:RETURN
2400 REM
2410 IFGG=1THEN2500
2420 ST=STICK(0)
2430 IFST=0THENRETURN
2440 GT=(ST=8ORST<3)-(ST>3ANDST<7)
2450 GR=(ST>5)-(ST>1ANDST<5)
2460 GX=X+GT:GY=Y+GR:IFGX>8ORGY>8THENRETURN
2470 IFS(GX,GY)THENRETURN
2480 IFE<256THENRETURN
2490 E=E-256:PLAY"S9M999O6B":GG=1:GOTO2510
2500 LOCATEGY*2+1,GX:PRINT" ":S(GX,GY)=0:GX=GX+GT:GY=GY+GR
2510 IFGX>8ORGX<1ORGY>8ORGY<1THENGG=0:RETURN
2520 IFS(GX,GY)THENGG=0:RETURN
2530 S(GX,GY)=4:LOCATEGY*2+1,GX:PRINT"¥":RETURN
2540 REM
2550 IFRND(1)<E/99999+.7THENRETURN
2560 TM=FNR(8):RM=FNR(8):IFTM=XXANDRM=YYTHEN2560
2570 IFS(TM,RM)<>0ORV>8THENRETURN
2580 IFL(TM,RM)>9999ORL(TM,RM)<100THENRETURN
2590 PLAY"S7M6999O3A3"
2600 LOCATERM*2+1,TM:PRINT"K"
2610 V=V+1:S(TM,RM)=RND(1)*4000+999
2620 L(TM,RM)=L(TM,RM)-100+10000
2630 GOSUB430:PRINT"Came out ";KL$" at "RM;-TM" Point "
2640 RETURN
2650 REM
2660 IFV=0THENRETURN
2670 GOSUB1840:GG=0:FORT=1TO8:FORR=1TO8
2680 IFS(T,R)<6THEN2820
2690 GOSUB2260
2700 S1=S(T,R):S2=S1/(RND(1)*6+2):S(T,R)=S1-S2
2710 IFS2<300THEN2840
2720 LOCATEY*2+1,X:PRINT"#":A$=STR$(INT(S2)):GOSUB3810
2730 LOCATE0,15:PRINT"HIT "R;-T" "KL$" at";:GOSUB2240:PRINT" units"
2740 LOCATEY*2+1,X:PRINT"E"
2750 E1=E1-S2:IFE1<0THEN2770
2760 GOTO2810
2770 E=E+E1:E1=0:LOCATE0,15
2780 M0=INT(RND(1)*20+1)
2790 PRINT" SHIELD OVER!!"M0" men DEAD!":GOSUB2910
2800 M=M-M0:IFM<0THENM=0
2810 GOSUB2100
2820 IFGG=1THENS(GX,GY)=0:LOCATEGY*2+1,GX:PRINT" "
2830 GG=0:NEXT:NEXT:GOTO240
2840 GOSUB3060:A=A+RND(1)/5- .1:B=B+RND(1)/5-.1:S=T:L=R:GOTO2860
2850 GOSUB2400:LOCATEINT(L+.5)*2+1,S+.5:PRINT" "
2860 S=S+B:L=L+A:IFS<.5ORL<.5ORS>8.4ORL>8.4THEN2820
2870 S1=S(S+.5,L+.5):GOSUB450:PRINT"„":PLAY"V15O5G+64"
2880 IFS1=1THENS2=700:GOTO2720
2890 IFS1=0THEN2850
2900 T0=T:R0=R:GOSUB450:GOSUB3540:R=R0:T=T0:GOTO2820
2910 REM
2920 R2=RND(1)*3:T2=FNR(9):IFT2>8THENT2=8
2930 IFQ(T2)<WTHENRETURN
2940 Q(T2)=W-R2
2950 PRINTC$(T2);" is BREAKDOWN"
2960 GOSUB3920:PRINT"     at "INT((W-Q(T2))*100)/100;" times":RETURN
2970 REM
2980 A=SIN(O*3.1416/180):B=COS(O*3.1416/180):RETURN
2990 E=10000:E1=10000:I=20
3000 RETURN
3010 REM
3020 CLS:PRINT" ARRIVE at BASE !"
3030 PLAY"S9M5000O5L16CEGO6C1"
3040 FORT=1TO3:W(T)=W(T)+20:NEXT
3050 W=W-0.3:BA=BA-1:H1=H1-1:GOTO2990
3060 REM
3070 S=Y-R:L=X-T:IFABS(S)>ABS(L)THENA=1:B=ABS(L/S):GOTO3090
3080 B=1:A=ABS(S/L)
3090 IFX<TTHENB=-B
3100 IFY<RTHENA=-A
3110 RETURN
3120 REM
3130 CLS:CONSOLE0,16:LOCATE0,9:PRINT"***** ENTERPRISE DESTROYED *****"
3140 LOCATE9,11:IFE<0THENPRINT"ENERGY EMPTY"
3150 IFM<0THENPRINT"ALL MEN DEAD"
3160 IFW<0THENPRINT"TIME OVER"
3170 T$="T120S9M9999L4O5":PLAYT$,T$,T$
3180 A$="DD8.D16D2CC8.C16C2O4BB8.B16BL12BBBL4O5CC8.C16C1"
3190 B$="FF8.F16F2EE8.E16E2DD8.D16DL12DDDL4O5EE8.E16E1"
3200 C$="AA8.A16A2GG8.G16G2FF8.F16FL12BABL4O6CC8.C16C1"
3210 PLAYA$,B$,C$
3220 PRINT
3230 PRINT"    --- G A M E  O V E R ---"
3240 GOTO3350
3250 REM
3260 CLS:CONSOLE0,16
3270 LOCATE0,9:PRINT"*** ALL "KL$"S DESTORYED!! ***"
3280 PRINT
3290 A$="T200S9M6000O5L8AFCAFCAFGECGECGEO6CO5GCO6CO5GCO6CO5G"
3300 B$="A+A4A-A2"
3310 C$="O6FC4O5AF1"
3320 PLAYA$+B$:PLAYA$+C$
3330 PRINT"GET YOUR PRICE JUST"INT(BA*10+W)*(M-230)" CREDIT"
3340 LOCATE10,13:PRINT"GOOD LUCK!"
3350 FORT=1TO1500:NEXT:END
3360 REM
3370 Q=0:CLS:IFI<1THENPRINT"NO "C$(1):GOTO500
3380 Q=Q+1:INPUT"Course ";A$
3390 A=VAL(A$):IFA$="r"ORA<0THEN500
3400 IFA>360ORA=0ANDA$<>"0"ANDA$<>"g"THENQ=Q-1:GOSUB370:GOTO3380
3410 LOCATE 15,CSRLIN-1:PRINT"OK!"
3420 O(Q)=VAL(A$):IFQ<9ANDA$<>"g"ANDQ<ITHEN3380
3430 W=W-.29:PRINT" GO!":IFA$="g"THENQ=Q-1
3440 FORQQ=1TOQ:S=X:L=Y:I=I-1
3450 O=O(QQ):GOSUB2970:S=S-B:L=L+A:GOSUB450
3460 GOTO3480
3470 GOSUB450:PRINT" ":S=S-B:L=L+A:GOSUB450
3480 IFS>8.4ORS<.5ORL>8.4ORL<.5THEN3520
3490 IFS(INT(S+.5),INT(L+.5))THEN3510
3500 PRINT"…":GOSUB2260:PLAY"V15O6C+64":GOTO3470
3510 GOSUB3540
3520 IFI<1THEN510
3530 NEXTQQ:GOTO510
3540 S=INT(S+.5):L=INT(L+.5):S1=S(S,L):IFS1ANDS1<6THENGOSUB3870
3550 PRINT"X":IFS1=2THENGOSUB430:H=H-1:S(S,L)=0:PRINT"STAR DESTROYED!"
3560 IFS2=4THENS(S,L)=0
3570 IFS1=3THENGOTO3600
3580 IFS1>6THENGOSUB450:PRINT"#":T=S:R=L:P1=1000:GOSUB3730
3590 RETURN
3600 GOSUB430:PRINT" BASE DESTROYED!"
3610 H1=H1-1:BA=BA-1:S(S,L)=0:RETURN
3620 REM
3630 IFV=0THEN500
3640 LOCATE22,9:PRINT"TEMP"INT((W-W(1))*10);"ßC";:IFW-W(1)>30THEN1860
3650 A$="":GOSUB440:CLS:INPUT"POWER  ";A$:P=VAL(A$):IFP<1ORE<PTHEN500
3660 CLS:W=W-.3:E=E-P:W(1)=W(1)-P/225:GOSUB430:P1=P/V
3670 FORT=1TO8:FORR=1TO8
3680 IFS(T,R)<6THEN3710
3690 LOCATER*2+1,T:PRINT"#"
3700 GOSUB3730
3710 NEXT:NEXT:GOTO510
3720 REM
3730 GOSUB3840:S1=S(T,R):S1=S1-P1:LOCATE0,15
3740 PRINTR;-T;" "KL$;
3750 IFS1<6THEN3790
3760 A$=STR$(INT(S1)):PRINT" DAMAGE";:GOSUB2240:PRINT" left"
3770 S(T,R)=S1:LOCATER*2+1,T:PRINT"K"
3780 RETURN
3790 GOSUB3870:PRINT" DESTROYED!":S(T,R)=0:LOCATER*2+1,T:PRINT"X"
3800 V=V-1:KL=KL-1:GOTO3780
3810 REM
3820 T$="S14M300O5T120L4":PLAYT$,T$,T$
3830 PLAY"C","F+","A+":RETURN
3840 REM
3850 T$="S12M600O5T120L4":PLAYT$,T$,T$:PLAY"C+","G","B"
3860 RETURN
3870 REM
3880 SOUND7,0:SOUND8,15
3890 FORSO=1TO255STEP30:SOUND0,SO:SOUND6,INT(SO/8)+1
3900 NEXT:SOUND7,&HF8:SOUND8,0
3910 RETURN
3920 REM
3930 SOUND7,&HF7:SOUND8,15
3940 FORTS=1TO255STEP20:SOUND6,INT(TS/8)+1
3950 NEXT:SOUND7,&HF8:SOUND8,0
3960 RETURN
3970 REM
3980 SOUND7,&HF8:SOUND1,1:SOUND8,15 :ST=0
3990 FORTS=1TO80STEP7:ST=ST+TS/100:SOUND0,TS
4000 NEXT:PRINTCHR$(7)
4010 RETURN
4020 REM DEMO
4030 SCREEN2,1,1:CLS:RESTORE5000
4040 FORT=0TO1:FORR=0TO7
4050 LINE(0,R*12+T*96)-(255,R*12+T*96+11),R+1,BF
4060 IFSTRIG(0)THENRETURN
4070 NEXTR,T:PLAY"S9M999O6L64"
4080 FORT=0TO50:READ A,B,C,D
4100 LINE(A*4,B*4)-(C*4,D*4),0
4110 PLAY"a":IFSTRIG(0)THENRETURN
4120 NEXT:T$="T125L4V13":PLAYT$,T$,T$
4140 A$="O3G8GGGGGGGG":B$="O4R8EEEO5CO4GGGG":C$="O4R8R2CCCCCC"
4150 PLAYA$,B$,C$:PLAYA$,B$,C$
4160 IFSTRIG(0)=1THENRETURN
4170 A$="T115V14O4GGGGV13O3GGG"
4180 B$="T115L4V15O5EEDCV14D.O4B8GG"
4190 C$="T115V14O4CCCCV13DDDD"
4200 PLAYA$,B$,C$
4210 B$="L4V14O5EEDCV14D.O4B8GV13O5G"
4220 PLAYA$,B$,C$
4230 A$="T90GGT80GGGGGG":B$="T90FFT80EDEEEE":C$="T90CCT80CCCCCC"
4240 PLAYA$,B$,C$:CC=1:RETURN
4250 DATANTORPEDO,ePHASER,-LONG-RANGE SENSOR,?SHORT-RANGE SENSOR
4260 DATA/SPACE MAP,5IMPULSE ENGINE,5WARP ENGINE,&COMPUTER
4270 RETURN
5000 DATA42,17,42,25,45,19,50,22,18,25,13,25,44,10,44,3 
5010 DATA14,6,14,3,34,10,34,3,18,17,18,25,37,34,39,32 
5020 DATA53,25,53,17,45,25,45,17,39,3,39,10,5,25,5,17 
5030 DATA37,25,37,17,31,39,24,46,5,17,10,17,21,17,23,19 
5040 DATA39,32,24,32,45,6,49,10,24,19,26,17,13,17,18,17 
5050 DATA37,21,42,21,26,17,26,25,29,17,31,19,56,25,53,25 
5060 DATA29,25,29,17,44,3,49,3,24,46,39,46,58,23,56,25 
5070 DATA50,25,50,17,10,25,5,25,49,6,44,6,27,3,27,10 
5080 DATA32,19,34,17,39,46,37,44,34,3,39,3,34,6,39,6 
5090 DATA37,17,42,17,24,3,30,3,53,17,56,17,49,10,58,10 
5100 DATA19,10,19,6,19,6,14,6,34,17,34,25,13,25,13,17 
5110 DATA14,3,19,3,21,25,21,17,5,10,19,10,56,17,58,19 
5120 DATA24,32,31,39,58,19,58,23,49,3,49,6,0,0,0,0 
5130 REM 1982 BY ASCII
