0 CLS:CLEAR500:DIMN(21):J=RND(-(PEEK(9)*256+PEEK(10))):J=16384
1 X0=1:FORX1=1TO63STEP3:N(X0)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",X1,3)):X0=X0+1
2 NEXT:GOTO20
3 FORX0=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,X0,1))-64),ASC(MID$(M$,X0+1,1))-48:NEXT:RETURN
4 X0=(PEEK(17024)AND1)*256+PEEK(17025)-1+J:FORY0=1TOLEN(M$):X1=ASC(MID$(M$,Y0)):POKEX0+Y0,X1-(X1AND64):NEXT:?@X0-J+Y0,;:RETURN
7 FORY0=0TO9:PRINT@32*Y0,"":NEXT:PRINT@0,;:RETURN
8 POKE16895,31:FORX0=Y1TO0STEP-1:X1=-(INKEY$=""):X0=X0*X1:NEXT:POKE16895,96:GOSUB10:RETURN
9 PRINT@32*15,LEFT$(SP$,31);
10 FORY0=10TO14:PRINT@32*Y0,"":NEXT:PRINT@32*10,;:RETURN
20 DIMX0,Y0,X1,Y1,M$,S(8,8),L(8,8),C$(8),P(8),W(3),Q(8)
40 SP$="                                ":KL$="KLINGON"
50 CLS:PRINT@32*7:INPUT"HOW MANY KLINGONS (30-300)";A$:KL=VAL(A$)
60 IFKL<30ORKL>300THEN50
70 IFCC=0THENCC=1:GOSUB4020
80 CLS:PRINT@7+32*7,"STAR COMMAND SIGMA"
90 W=KL/.8:W0=W-.3:M=430:E0=50:MP=0:BA=INT(KL/12)+1
100 REM INIT
110 RESTORE:I$="":FORT=1TO3:W(T)=W-2.5:NEXT
115 FORX0=0TO8:FORY0=0TO8:S(X0,Y0)=0:L(X0,Y0)=0:NEXT:NEXT:FORX0=0TO8:P(X0)=0:NEXT
120 FORT=1TO8:Q(T)=W:READA$:C$(T)=MID$(A$,2)
125 B$=LEFT$(A$,1):I$=I$+CHR$(ASC(B$)+20)
130 FORR=1TO8:L(T,R)=RND(8)+10000:NEXT:NEXT:K=KL
140 T=RND(8):R=RND(8):A=RND(5)
150 L=L(T,R):IFINT((L-10000)/100)+A>9THEN140
160 IFK<ATHENA=K
170 L(T,R)=L+A*100:K=K-A:IFK>0THEN140
180 FORB=1TOBA:T=RND(8):R=RND(8):A=RND(8):L(T,R)=L(T,R)+10:NEXT
190 XX=RND(8):YY=RND(8):X=RND(8):Y=RND(8)
200 GOSUB2990:L=L(XX,YY):CLS
210 GOSUB1280:GOTO1180
220 REM SR SCANNER
230 GOSUB240:GOTO510
240 IFMPTHENGOSUB7:PRINT@32*10,;:MP=0
250 IFQ(4)<WTHEN460
260 PRINT@0,"   1 2 3 4 5 6 7 8";LEFT$(SP$,20)
270 FORT=1TO8:PRINT@32*T,STR$(T)":";LEFT$(SP$,15)
280 FORR=1TO8:IFS(T,R)=0THEN360
290 S=S(T,R):PRINT@R*2+1+32*T,;:IFS>6THENPRINT"K";:GOTO360
300 ONSGOSUB320,330,340,350
310 GOTO360
320 PRINT"E";:RETURN
330 PRINT"*";:RETURN
340 PRINT"B";:RETURN
350 S(T,R)=0:RETURN
360 NEXT:NEXT:RETURN
370 REM DIR HELP
380 GOSUB9:PRINT@32*11,;
390 PRINT"  315'  0'   45'"
400 PRINT"  270'  e    90'"
410 PRINT"  225' 180' 135'":IFC=1THENPRINTCHR$(34)"G"CHR$(34)" TO LAUNCH, "CHR$(34)"R"CHR$(34)" TO CANCEL"
420 GOTO440
430 PRINT@32*15,LEFT$(SP$,31);:PRINT@32*15,;:RETURN
440 PRINT@32*10,"":PRINT@32*10,;:RETURN
450 PRINT@INT(L+.5)*2+1+32*INT(S+.5),;:RETURN
460 REM SR SCANNER OUT
470 FORT=1TO8
480 PRINT@3+32*T,LEFT$(SP$,15);
490 NEXT:PRINT@Y*2+1+32*X,"E";:RETURN
500 GOSUB9:PRINT:PRINT"ILLEGAL FUNCTION CALL!":M$="A4":GOSUB3:GOSUB9
510 REM COMMAND
520 FORT=1TO3:IFW-W(T)<2.5THENW(T)=W-2.5
530 NEXT:IFW<W0THENW0=W-.3:GOSUB2650
540 E=E+E1:E1=E*E0/100:E=E-E1:IFMP=0THENGOSUB2100
550 IFQ(4)<WTHENGOSUB460
560 GOSUB2250:IFRND(0)<.02+(430-M)/699THENGOSUB9:GOSUB2910
570 PRINT@32*9,"":PRINT@32*9,;
580 IFE<0ORM<0ORW<0THEN3120
590 IFKL<1THEN3250
600 IFINKEY$<>""THEN600
610 W=W-.02:SOUND113,1:PRINT"COMMAND?":PRINT@9+32*9,;:C=0
620 C$=INKEY$:IFC$=""ORC$="&"THEN620
630 C=VAL(C$):IFC>0ANDC<9THENPRINTC$(C);
640 IFC=0ORC>8THEN700
650 IFQ(C)>=WTHEN670
660 GOSUB9:T2=C:PRINT:GOSUB2950:GOTO570
670 C$=INKEY$:IFC$=""THEN670
680 IFC$<>CHR$(13)THEN570
690 ONCGOTO3360,3620,1870,230,1970,720,1050,1470
700 GOSUB9:FORT=1TO8:PRINT":"RIGHT$(STR$(T),1)" "LEFT$(C$(T),11),:NEXT
710 SOUND229,4:SOUND221,2:GOTO570
720 REM IMPULSE
730 GOSUB9:PRINT@20+32*9,". TEMP"STR$(INT((W-W(3))*10))"'";
740 IFW-W(3)>30THEN1860
750 A$="":GOSUB440:INPUT"COURSE ";A$:O=VAL(A$):IFA$="R"THEN500
760 IF(O=0ANDA$<>"0")ORO>360THENGOSUB370:GOTO750
770 GOSUB440:INPUT"SECTOR ";T$:T=VAL(T$):IFT<1ORT>99THEN500
780 GOSUB1840:W=W-T*.02:E=E-T*3:W(3)=W(3)-T*.25:B1=0
790 IFT>1ANDO-INT(O/90)*90=45THENT=T+1
800 GOSUB2970:S(X,Y)=0:S=X:L=Y:GOSUB450
810 FORT1=1TOT:X=X-B
820 IFX>8.4THENX=1:L=180:GOTO890
830 IFX<.5THENX=8:L=0:GOTO890
840 Y=Y+A:IFY>8.4THENY=1:L=90:GOTO890
850 IFY<.5THENY=8:L=270:GOTO890
860 GOSUB450:S=INT(X+.5):L=INT(Y+.5):IFS(S,L)THEN920
870 PRINT" ";:GOSUB450:PRINT"E";:SOUND171,2:NEXT
880 X=INT(X+.5):Y=INT(Y+.5):S(X,Y)=1:GOTO230
890 Z1=T-T1:Z2=O:O=L:T=1:W=W-.7:X=INT(X+.5):Y=INT(Y+.5)
900 S(X,Y)=1:GOSUB1210:GOSUB240:T=Z1:O=Z2:IFT=0THEN880
910 GOTO800
920 X=INT(X+.5):Y=INT(Y+.5):IFS(S,L)>6THEN1000
930 PRINT"E";:IFS(X,Y)<>3THENGOSUB440:GOTO960
940 S(X,Y)=0:GOSUB3010:GOTO880
950 X=X+B:Y=Y-A:GOTO880
960 GOSUB3970:PRINT"DANGER!";
970 IFQ(8)>=WTHENPRINT" EMERGENCY STOP!":GOTO950
980 PRINT"SEVERE DAMAGE 700 UNITS":GOSUB3870
990 E1=E1-700:GOSUB2910:GOTO950
1000 GOSUB3870
1010 IFS(X,Y)>T*150THENE1=E1-S(X,Y):IFE1<0THENE=E+E1:E1=0
1020 PRINT" ";:T=INT(T-(S(X,Y)/150)):IFS(X,Y)>E1/9THENGOSUB430:GOSUB2910
1030 KL=KL-1:V=V-1:GOSUB450:PRINT"E";:S(X,Y)=0:IFT<1THEN880
1040 GOTO860
1050 REM WARP
1060 GOSUB9:PRINT@22+32*9,"TEMP"STR$(INT((W-W(2))*10))"'";:IFW-W(2)>30THEN1860
1070 A$="":GOSUB440:INPUT"COURSE ";A$:O=VAL(A$)
1080 IFA$="R"ORO<0THEN500
1090 IF(O=0ANDA$<>"0")ORO>360THENGOSUB370:GOTO1070
1100 A$="":GOSUB440:INPUT"QUOD.  ";A$:T=VAL(A$):IFT<1ORT>8THEN500
1110 GOSUB440:INPUT"SPEED  ";A$:SE=VAL(A$)
1120 IFSE<1ORSE>15THEN500
1130 W=W-(1/SE):E=E-T*SE*128:W(2)=W(2)-T*SE/1.57
1140 GOSUB9:PRINT:PRINT"ENERGY USE IS"STR$(T*SE*128)" FOR TIME"STR$(INT((1/SE)*100)/100)
1160 GOSUB1840:PRINT@Y*2+1+32*X," ";
1170 S=X:L=Y:GOSUB1210
1180 GOSUB240:IFQ(8)<WTHEN510
1190 IFQ(3)<WORV>0THEN1590
1200 GOTO1870
1210 L(XX,YY)=V*100+H1*10+H
1220 GOSUB2970:FORT1=1TOT:XX=XX-B:YY=YY+A:NEXT
1230 IFXX>8.4THENXX=XX-8
1240 IFYY>8.4THENYY=YY-8
1250 IFXX<.5THENXX=XX+8
1260 IFYY<.5THENYY=YY+8
1270 XX=INT(XX+.5):YY=INT(YY+.5)
1280 REM CHECKS & UPDATES
1290 IFL(XX,YY)>9999THENL(XX,YY)=L(XX,YY)-10000
1300 L=L(XX,YY):FORT=1TO8:FORR=1TO8:S(T,R)=0:NEXT:NEXT
1310 S1=INT(L/100):S2=INT(L/10)-S1*10:S3=L-S1*100-S2*10:V=S1
1320 S(X,Y)=1:IFS1=0THEN1380
1330 FORA=1TOS1
1340 T=RND(8):R=RND(8):IFS(T,R)<>0THEN1340
1350 S(T,R)=RND(0)*RND(0)*6000+999
1360 IFRND(0)<.02THENS(T,R)=S(T,R)+RND(0)*5000
1370 NEXT
1380 IFS2=0THEN1420
1390 FORA=1TOS2
1400 T=RND(8):R=RND(8):IFS(T,R)THEN1400
1410 S(T,R)=3:NEXT
1420 IFS3<1THEN1460
1430 FORA=1TOS3
1440 T=RND(8):R=RND(8):IFS(T,R)THEN1440
1450 S(T,R)=2:NEXT
1460 V=S1:H1=S2:H=S3:RETURN
1470 REM COMPUTER
1480 GOSUB9:A$="":PRINT@32*9,LEFT$(SP$,32);
1490 PRINT@32*9,"":PRINT@32*9,;:INPUT"COMPUTER FUNCTION";A$
1500 IFA$="R"THEN500
1510 CJ=VAL(A$):IFCJ<1ORCJ>4THEN1530
1520 GOSUB9:ONCJGOTO1590,1650,1750,1790
1530 PRINT":1 ENERGY OF KLINGONS"
1540 PRINT":2 ASTEROID BELT"
1550 PRINT":3 DAMAGE REPORT"
1560 PRINT":4 GET ENERGY FROM STAR"
1570 CJ$="":GOTO1490
1590 GOSUB9:ZZ=0:FORT=1TO8:FORR=1TO8:IFS(T,R)<6THEN1630
1600 PRINTSTR$(R);-T"%%%";
1610 IFS(T,R)>5000ORRND(0)<.03THENPRINT" ???",:ZZ=ZZ+1:GOTO1630
1620 PRINTINT(S(T,R)),:ZZ=ZZ+1
1630 IFZZ=4THENY1=1500:GOSUB8:ZZ=0
1635 NEXT:NEXT:PRINT:PRINT"OFFENSIVE CYCLE IS"INT((W-W0)*100)/100
1640 GOTO230
1650 SOUND225,4
1670 FORT=1TO8:FORR=1TO8:IFS(T,R)<>2THEN1730
1680 SOUND171,1:SOUND171,1:SOUND171,1:SOUND185,1:SOUND185,1:SOUND185,1
1690 S(T,R)=0:GOSUB3060:S=T:L=R:GOSUB450
1700 S=S+B:L=L+A:IFS(S+.5,L+.5)THEN1720
1710 PRINT" ";:GOSUB450:PRINT"*";:GOSUB450:GOTO1700
1720 E=E-64:S=S-B:L=L-A:S(S+.5,L+.5)=2:GOSUB450:PRINT"*";
1730 NEXT:NEXT:SOUND1,1:GOTO510
1750 ZZ=0:FORT=1TO8:IFQ(T)>WTHEN1770
1760 PRINTC$(T)" REPAIR TIME";STR$(INT((W-Q(T))*100)/100):ZZ=ZZ+1:IFZZ=4THENY1=1500:GOSUB8:ZZ=0
1770 NEXT:GOTO510
1790 IFH=0THENPRINT"NO STAR!":GOTO510
1800 INPUT"HOW MANY TIMES";T$:T=VAL(T$):IFT<0.1ORT>11THEN500
1810 E=E+T*64*H:W=W-T:PRINT
1820 PRINT"CHARGE ENERGY IS"T*64*H"UNITS"
1830 GOTO510
1840 IFMP=1THENT0=T:GOSUB240:T=T0
1850 RETURN
1860 SOUND215,8:GOSUB9:PRINT"OVERHEATED!!":GOTO510
1870 REM LR SCANNER
1880 W=W-.1:GOSUB440:PRINT
1890 FORT1=XX-1TOXX+1:PRINT"   ";:FORR1=YY-1TOYY+1:T=T1:R=R1
1900 IFT1<1THENT=8
1910 IFT1>8THENT=1
1920 IFR1<1THENR=8
1930 IFR1>8THENR=1
1940 L=L(T,R):IFL>9999THENL(T,R)=L-10000:L=L-10000
1950 GOSUB2040
1960 NEXT:PRINT:NEXT:PRINT:GOTO510
1970 REM MAP
1980 PRINT@0,;
1990 FORT=1TO8:FORR=1TO8
2000 L=L(T,R):IFL>9999THENPRINT" ***";:GOTO2020
2010 GOSUB2040
2020 NEXT:NEXT:PRINT
2030 MP=1:GOTO570
2040 PRINT" ";:IFT=XXANDR=YYTHENL=V*100+H1*10+H
2050 M$=MID$(STR$(L),2,LEN(STR$(L)))
2060 IFL<100THENM$="0"+M$
2070 IFL<10THENM$="0"+M$
2071 IFT=XXANDR=YYTHENGOSUB4:RETURN
2080 PRINTM$;:RETURN
2100 REM STATS
2120 A$=STR$(INT(E)):PRINT@18+32*1,":ENERGY ";:GOSUB2240
2130 A$=STR$(INT(E1)):PRINT@18+32*2,":SHIELD ";:GOSUB2240
2140 A$=STR$(M):PRINT@18+32*3,":CREW   ";:GOSUB2240
2150 A$=STR$(I):PRINT@18+32*4,":TORPEDO";:GOSUB2240
2160 A$=STR$(INT(W)):PRINT@18+32*5,":TIME   ";:GOSUB2240
2170 A$=STR$(KL):PRINT@18+32*6,":"KL$;:GOSUB2240
2180 A$=STR$(BA):PRINT@18+32*7,":BASE   ";:GOSUB2240
2190 PRINT@18+32*8,":CONDITION ";
2200 IFVTHENPRINT"¿¿¿":RETURN
2210 IFE<1000THENPRINT"ŸŸŸ":RETURN
2220 PRINT"ßßß":RETURN
2240 PRINTRIGHT$("     "+A$,6);:RETURN
2250 REM COMBAT
2260 GOSUB2540
2270 TM=RND(8):RM=RND(8):IFS(TM,RM)<6ORS(TM,RM)>1000THENRETURN
2280 PRINT@RM*2+1+32*TM," ";:GOSUB430:PRINTMID$(STR$(RM),2);-TM;KL$" ";
2290 SM=S(TM,RM):S(TM,RM)=0
2300 TM=TM+RND(3)-2:RM=RM+RND(3)-2
2310 IFTM<1ORTM>8ORRM<1ORRM>8THEN2350
2320 IFS(TM,RM)THEN2350
2330 PRINT"MOVES"STR$(RM);-TM;:PRINT@RM*2+1+32*TM,"K";:SOUND171,2:Y1=350:GOSUB8:S(TM,RM)=SM:RETURN
2350 PRINT"WARPS!";:SOUND185,4:Y1=350:GOSUB8
2360 TM=RND(8):RM=RND(8):IFXX=TMANDYY=RMTHEN2360
2370 LM=L(TM,RM):IFLM<9999THENLM=LM+10000
2380 IFLM>10899THEN2360
2390 V=V-1:L(TM,RM)=LM+100:RETURN
2400 REM TORP COUNTER MEASURE
2410 IFGG=1THEN2500
2420 ST=.:IFPEEK(2)=.THENRETURN
2421 IF4ANDNOTPEEK(16952)THENST=1
2422 IF1ANDNOTPEEK(16950)THENST=2
2423 IF1ANDNOTPEEK(16949)THENST=3
2424 IF1ANDNOTPEEK(16948)THENST=4
2425 IF8ANDNOTPEEK(16945)THENST=5
2428 IF8ANDNOTPEEK(16947)THENST=6
2427 IF1ANDNOTPEEK(16946)THENST=7
2426 IF4ANDNOTPEEK(16946)THENST=8
2430 IFST=.THENRETURN
2440 GT=(ST=8ORST<3)-(ST>3ANDST<7)
2450 GR=(ST>5)-(ST>1ANDST<5)
2460 GX=X+GT:GY=Y+GR:IFGX>8ORGY>8THENRETURN
2470 IFS(GX,GY)THENRETURN
2480 IFE<256THENRETURN
2490 E=E-256:SOUND219,4:GG=1:GOTO2510
2500 PRINT@GY*2+1+32*GX," ";:S(GX,GY)=0:GX=GX+GT:GY=GY+GR
2510 IFGX>8ORGX<1ORGY>8ORGY<1THENGG=0:RETURN
2520 IFS(GX,GY)THENGG=0:RETURN
2530 S(GX,GY)=4:PRINT@GY*2+1+32*GX,"@";:SOUND220,1:RETURN
2540 REM COMBAT MOVES
2550 IFRND(0)<E/99999+.7THENRETURN
2560 TM=RND(8):RM=RND(8):IFTM=XXANDRM=YYTHEN2560
2570 IFS(TM,RM)<>0ORV>8THENRETURN
2580 IFL(TM,RM)>9999ORL(TM,RM)<100THENRETURN
2590 SOUND87,4
2600 PRINT@RM*2+1+32*TM,"K";
2610 V=V+1:S(TM,RM)=RND(0)*4000+999
2620 L(TM,RM)=L(TM,RM)-100+10000
2630 GOSUB430:PRINT"SCANNERS SPOT ";KL$;" AT";STR$(RM);STR$(-TM);:Y1=350:GOSUB8
2640 RETURN
2650 REM E HITS
2660 IFV=0THENRETURN
2670 GOSUB1840:GG=0:FORT=1TO8:FORR=1TO8
2680 IFS(T,R)<6THEN2820
2690 GOSUB2260
2700 S1=S(T,R):S2=S1/(RND(0)*6+2):S(T,R)=S1-S2
2710 IFS2<300THEN2840
2720 PRINT@Y*2+1+32*X,"#";:A$=STR$(INT(S2)):GOSUB3810
2730 GOSUB430:PRINT"HIT BY"STR$(R);-T;KL$;:GOSUB2240:PRINT" UNITS";
2740 PRINT@Y*2+1+32*X,"E";:Y1=350:GOSUB8
2750 E1=E1-S2:IFE1<0THEN2770
2760 GOTO2810
2770 E=E+E1:E1=0:GOSUB9
2780 M0=INT(RND(0)*20+1)
2790 PRINT:PRINT"SHIELD BREACHED!"M0"CREW LOST!":Y1=1500:GOSUB8:GOSUB2910
2800 M=M-M0:IFM<0THENM=0
2810 GOSUB2100
2820 IFGG=1THENS(GX,GY)=0:PRINT@GY*2+1+32*GX," ";
2830 GG=0:NEXT:NEXT:GOTO240
2840 GOSUB3060:A=A+RND(0)/5-.1:B=B+RND(0)/5-.1:S=T:L=R:GOTO2860
2850 GOSUB2410:PRINT@INT(L+.5)*2+1+32*INT(S+.5)," ";
2860 S=S+B:L=L+A:IFS<.5ORL<.5ORS>8.4ORL>8.4THEN2820
2870 S1=S(S+.5,L+.5):GOSUB450:PRINT".";:SOUND209,1
2880 IFS1=1THENS2=700:GOTO2720
2890 IFS1=0THEN2850
2900 T0=T:R0=R:GOSUB450:GOSUB3540:R=R0:T=T0:GOTO2820
2910 REM DAMAGE
2920 R2=RND(0)*3:T2=RND(9):IFT2>8THENT2=8
2930 IFQ(T2)<WTHENRETURN
2940 Q(T2)=W-R2
2950 GOSUB9:PRINT:PRINTC$(T2);" IS DAMAGED"
2960 PRINT"ESTIMATED REPAIR TIME:";STR$(INT((W-Q(T2))*100)/100):GOSUB3920:RETURN
2970 REM DOCK
2980 A=SIN(O*3.1416/180):B=COS(O*3.1416/180):RETURN
2990 E=10000:E1=10000:I=20
3000 RETURN
3010 REM BASE
3020 GOSUB9:PRINT:PRINT"ARRIVE AT BASE!"
3030 SOUND113,4:SOUND142,4:SOUND161,4:SOUND185,2
3040 FORT=1TO3:W(T)=W(T)+20:NEXT
3050 W=W-0.3:BA=BA-1:H1=H1-1:GOTO2990
3060 REM
3070 S=Y-R:L=X-T:IFABS(S)>ABS(L)THENA=1:B=ABS(L/S):GOTO3090
3080 B=1:A=ABS(S/L)
3090 IFX<TTHENB=-B
3100 IFY<RTHENA=-A
3110 RETURN
3120 REM LOSE
3130 GOSUB9:PRINT@32*9,"***** ENTERPRISE DESTROYED *****";
3140 PRINT@32*11,;:IFE<0THENPRINTTAB(9)"OUT OF ENERGY"
3150 IFM<0THENPRINTTAB(9)"ALL CREW DEAD"
3160 IFW<0THENPRINTTAB(10)"OUT OF TIME"
3170 FORX0=1TO8:SOUND1,1:NEXT
3180 A$="D1D2D4D1C1C2C4C1B1B2B4B1B3B3B3C1C2C4C1"
3190 B$="F1F2F4F1E1E2E4E1D1D2D4D1D3D3D3E1E2E4E1"
3200 C$="A1A2A4A1G1G2G4G1F1F2F4F1B3A3B3C1C2C4C4"
3210 M$=A$+B$+C$:GOSUB3
3220 PRINT
3230 PRINT"    --- G A M E  O V E R ---"
3240 GOTO3350
3250 REM WIN
3260 GOSUB9
3270 PRINT@32*9,"*** ALL "KL$"S DESTROYED!! ***"
3280 PRINT
3290 A$="A2F2C2A2F2C2A2F2G2E2C2G2E2C2G2E2C2G2C2C2G2C2C2G2"
3300 B$="A2A2A2A2"
3310 C$="F2C4A2F8"
3320 M$=A$+B$:GOSUB3:GOSUB3
3330 PRINT@9+32*11,;:M$="RATING:"+STR$(INT(BA*10+W)*(M-230)):PRINTTAB((32-LEN(M$))*.5)M$
3340 PRINT@11+32*13,"WELL DONE!"
3350 PRINT@488,"TRY AGAIN? (Y/N)";
3351 M$=INKEY$:IFM$=""THEN3351
3352 IFM$="Y"THEN50
3353 IFM$="N"THENEND
3354 GOTO3351
3360 REM TORP
3370 Q=0:GOSUB9:IFI<1THENPRINT"NO "C$(1):GOTO510
3380 Q=Q+1:INPUT"COURSE ";A$
3390 A=VAL(A$):IFA$="R"ORA<0THEN500
3400 IF(A>360ORA=0)ANDA$<>"0"ANDA$<>"G"THENQ=Q-1:GOSUB370:GOTO3380
3410 PRINTCHR$(8)CHR$(8)CHR$(8)"OK!";:IF(Q=4ORQ=8)ANDA$<>"G"THENGOSUB440
3420 O(Q)=VAL(A$):IFQ<9ANDA$<>"G"ANDQ<ITHEN3380
3430 W=W-.29:PRINTCHR$(8)CHR$(8)CHR$(8)"GO!";:IFA$="G"THENQ=Q-1
3440 FORQQ=1TOQ:S=X:L=Y:I=I-1
3450 O=O(QQ):GOSUB2970:S=S-B:L=L+A:GOSUB450
3460 GOTO3480
3470 GOSUB450:PRINT" ";:S=S-B:L=L+A:GOSUB450
3480 IFS>8.4ORS<.5ORL>8.4ORL<.5THEN3520
3490 IFS(INT(S+.5),INT(L+.5))THEN3510
3500 PRINT"+";:GOSUB2260:SOUND221,1:GOTO3470
3510 GOSUB3540
3520 IFI<1THEN510
3530 NEXTQQ:GOTO510
3540 S=INT(S+.5):L=INT(L+.5):S1=S(S,L):IFS1ANDS1<6THENGOSUB3870
3550 PRINT"X";:IFS1=2THENGOSUB430:H=H-1:S(S,L)=0:PRINT"STAR DESTROYED!";:Y1=350:GOSUB8
3560 IFS2=4THENS(S,L)=0
3570 IFS1=3THEN3600
3580 IFS1>6THENGOSUB450:PRINT"#";:T=S:R=L:P1=1000:GOSUB3730
3590 RETURN
3600 GOSUB430:PRINT"BASE DESTROYED!";:Y1=350:GOSUB8
3610 H1=H1-1:BA=BA-1:S(S,L)=0:RETURN
3620 REM PHASER
3630 IFV=0THEN500
3640 PRINT@22+32*9,"TEMP"STR$(INT((W-W(1))*10))"'";:IFW-W(1)>30THEN1860
3650 A$="":GOSUB440:GOSUB9:INPUT"POWER  ";A$:P=VAL(A$):IFP<1ORE<PTHEN500
3660 GOSUB9:W=W-.3:E=E-P:W(1)=W(1)-P/225:GOSUB430:P1=P/V
3670 FORT=1TO8:FORR=1TO8
3680 IFS(T,R)<6THEN3710
3690 PRINT@R*2+1+32*T,"#";
3700 GOSUB3730
3710 NEXT:NEXT:GOTO510
3720 REM K HIT
3730 GOSUB3840:S1=S(T,R):S1=S1-P1:GOSUB430:PRINTMID$(STR$(R),2);-T;KL$;
3750 IFS1<6THEN3790
3760 A$=STR$(INT(S1)):PRINT" HIT!  ";:GOSUB2240:PRINT" LEFT";
3770 S(T,R)=S1:PRINT@R*2+1+32*T,"K";:Y1=350:GOSUB8
3780 RETURN
3790 GOSUB3870:PRINT" DESTROYED!        ";:S(T,R)=0:PRINT@R*2+1+32*T,"X";:Y1=350:GOSUB8
3800 V=V-1:KL=KL-1:GOTO3780
3810 REM E HIT
3820 FORX0=1TO5:SOUND95,1:NEXT
3830 SOUND113,2:SOUND155,2:SOUND176,2:RETURN
3840 REM K HIT
3850 FORX0=1TO5:SOUND77,1:NEXT:SOUND121,2:SOUND161,2:SOUND103,2
3860 RETURN
3870 REM EXPLOSION
3880 SOUND8,1
3890 FORX0=1TO8:SOUND1,1
3900 NEXT:SOUND7,1
3910 RETURN
3920 REM DAMAGE REPORT
3930 SOUND175,1
3940 FORX0=1TO8:SOUND200,1
3950 NEXT:SOUND225,1
3960 RETURN
3970 REM COLLISION
3980 SOUND7,1:SOUND1,1:SOUND8,15
3990 FORX0=1TO8STEP7:X1=X0*10:SOUNDX1,1
4000 NEXT:SOUND1,1
4010 RETURN
4020 REM DEMO
4030 CLS8
4031 PRINT"ŒŒŒŒŒŒŒŒŒŒŒŒ„ŒŒŠ„Œˆ„Œˆ";
4032 PRINT"ŸŸŸŸŸŸŸŸŸšŸŸŸšŸŸŸ•ŸšŸŸ•›ŸŸŸŸŸŸŸŸŸ›“““““““ŸŸŸ›ŸŸŸ—Ÿ›ŸŸ—Ÿ›““““—ŸŸ";
4033 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯®¬¬­®¬¬­®­®­®­®­®¬¬­®¯¯­®¬¬¯¯¯";
4034 PRINT"¿¿º¿¿¿º¿¿µº»·µº»·µº¼¼µº¶¿µº¿¿µ¿¿¿¿º¿¿¿º¿¿µº¿¿µº¿¿µº¿¿µº¿¶µº¿¿µ¿¿";
4035 PRINT"ÏÏËÃÃÇËÃÃÇËÏÏÇËÏÏÇËÏÏÇËÏÏÇËÃÃÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏÏ";
4036 PRINT"ßßßßßßßßßßßßÒÓÓÓÓÓÓÑßßßßßßßßßßßßßßßßßßßßßßßßßÖßßßßÛßßßßßßßßßßßßß";
4037 PRINT"ïïïïïïïïïïïïïïæïïïïïïïïïïïïïïïïïïïïïïïïïïïïïïîçïïïïïïïïïïïïïïïïï";
4038 PRINT"ÿÿÿÿÿÿÿÿÿÿÿÿþ÷ÿÿÿÿûýÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿóóóóóóóó";
4230 A$="G4G4G2G2G2G2G2G2":B$="F4F4E2D2E2E2E2E2":C$="C4C4C2C2C2C2C2C2"
4240 M$=A$+B$+C$:GOSUB3:RETURN
4250 DATANTORPEDO,ePHASER,-LONG-RANGE SENSOR,?SHORT-RANGE SENSOR
4260 DATA/SPACE MAP,5IMPULSE ENGINE,5WARP ENGINE,&COMPUTER
5130 REM 1982 BY ASCII
5140 REM MC-10 JIM GERRIE
