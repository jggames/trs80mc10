5 CLS:CLEAR125:PRINT@3,"SPEAKEASY SOFTWARE PRESENTS":GOSUB525
6 PRINT@70,"**BULLS AND BEARS**":GOSUB525
7 PRINT@131,"A GAME OF CORPORATE FINANCE":GOSUB525
10 PRINT@196,"SPEAKEASY SOFTWARE":PRINT@232,"BOX 1220"
11 PRINT@266,"KEMPTVILLE, ON":PRINT@298,"CANADA":STOP
15 CLEAR:PRINT@704,"ENTER NUMBER OF PLAYERS (1--5) ";:INPUTP:IFP<1ORP>5GOTO15:ELSEJ=P+1:K=J+1:L=K+1:M=L+1:N=M+1:O=N+1:DIMN$(P),C$(J),C(J,O),CE(J),PL(P),PC(P):CLS:FORX=1TOP:PC(X)=50
20 PRINT@64*X,"ENTER NAME OF PLAYER";X;:INPUTA$:IFLEN(A$)=0ORA$=N$(X-1)GOTO20:ELSEIFLEN(A$)>12THENA$=MID$(A$,1,12)
25 N$(X)=A$:NEXT:CLS:PRINT@15,"* * BULLS  AND  BEARS * *":PRINT@85,"PLAYERS  LIST":FORX=1TOP:PRINT"PLAYER  NUMBER ";X;"-- ";N$(X):NEXT:PRINT:PRINT"GAME HINT: AVOID LETTING OTHER PLAYERS SEE YOUR CASH OR HOLDINGS";
30 PRINT"THIS WILL AID YOU IN MAKING SURPRISE TAKEOVER BIDS....":Z=P*5+15:GOSUB600:FORX=1TOJ:READC$(X):C(X,J)=100:C(X,K)=20:C(X,M)=4:C(X,N)=6:NEXT:RANDOM:YR=1917+RND(55)
35 Z=12:CI=RND(10)+9:PI=RND(10)+9:FORX=1TOP:GOSUB700:PRINT@L*64+20,"PLAYER  FINANCE":PRINT"PLAYER ";X;"-- ";N$(X)
40 GOSUB710:PRINT"YOUR CASH BALANCE";:GOSUB750:ONFGOTO45,40:PRINT@N*64,CHR$(30);"YOUR CASH BALANCE IS  $";PC(X)*1000:GOSUB600
45 PRINT@N*64,CHR$(30);:GOSUB710:PRINT"YOUR SHAREHOLDINGS";:GOSUB750:ONFGOTO60,45:CLS:PRINT"SHAREHOLDINGS -- PLAYER ";X;"-- ";N$(X):PRINT"CORPORATION";TAB(25)"SHARES";TAB(40)"VALUE":W=0:FORY=1TOJ
50 IFC(Y,X)=0GOTO55:ELSEF=C(Y,X)*C(Y,M):PRINTC$(Y);TAB(24)C(Y,X)*1000;TAB(39)F*1000:W=W+F
55 NEXTY:PRINT:PRINT"TOTAL VALUE OF SHARES = $";W*1000:PRINT"YOUR NET WORTH IS  $";(W+PC(X)-PL(X))*1000
60 PRINT"YOU OWE THE BANK  $";PL(X)*1000:IFPL(X)=0GOTO65:ELSEY=PC(X):GOSUB765:PC(X)=Y-I:PL(X)=PL(X)-I
65 GOSUB710:PRINT"TO BORROW";:GOSUB750:ONFGOTO70,65:I=PI:Y=W+PC(X)-PL(X):GOSUB780:PC(X)=PC(X)+F:PL(X)=PL(X)+F
70 NEXTX:FORX=1TOP:GOSUB700:PRINT@L*64+20,"PLAYER TRADING":PRINT"PLAYER ";X;"-- ";N$(X):PRINT
75 GOSUB710;:PRINT"TO BUY OR SELL STOCK";:GOSUB750:ONFGOTO120,75
80 INPUT"FOR WHAT COMPANY(NUMBER)";Y:IFY<1ORY>JPRINTCHR$(27);:GOTO80
85 INPUT"BUY OR SELL";A$:F=ASC(A$):IFF=66ORF=83GOTO90:ELSEPRINTCHR$(27);CHR$(30);:GOTO85
90 CLS:PRINT@20,C$(Y):PRINT:PRINT"BID",C(Y,M),"PRODUCTION UNITS =";C(Y,L):PRINT"ASKED",C(Y,N),"SHARES AVAILABLE =";C(Y,K)*1000:PRINT:IFF=66ANDC(Y,K)=0PRINT"* * NO SHARES AVAILABLE * *":GOSUB600:PRINT:GOTO75
95 PRINT"HOW MANY SHARES TO ";:IFF=66PRINT"BUY";:ELSEPRINT"SELL";
100 INPUT"(000'S)";I:I=FIX(ABS(I)):IFF=66GOTO110:ELSEIFI>C(Y,X)PRINT"YOU DO NOT OWN";I*1000;"SHARES IN ";C$(Y):GOSUB600:GOTO120
105 C(Y,K)=C(Y,K)+I:C(Y,X)=C(Y,X)-I:PC(X)=PC(X)+I*C(Y,M):PRINT"STOCK SALE COMPLETE":PRINT"CREDITED TO YOUR ACCOUNT $";I*C(Y,M)*1000:PRINT:GOTO75
110 IFI>C(Y,K)PRINT"ONLY";C(Y,K)*1000;"SHARES AVAILABLE";CHR$(29);CHR$(27);CHR$(30);:GOTO95:ELSEIFI*C(Y,N)>PC(X)THENGOSUB650:GOTO120
115 PC(X)=PC(X)-I*C(Y,N):C(Y,K)=C(Y,K)-I:C(Y,X)=C(Y,X)+I:PRINT"PURCHASE PRICE IS $";I*C(Y,N)*1000:PRINT"STOCK PURCHASE COMPLETE":PRINT:GOTO75
120 NEXTX:FORX=1TOJ:F=1:FORY=1TOP:IFC(X,Y)>C(X,F)THENF=Y
125 NEXTY:IFC(X,F)=0GOTO177:ELSECLS:PRINT@0,C$(X);TAB(19)"** ANNUAL REPORT ** - DIRECTOR : ";N$(F):PRINT@64,"CASH  $";C(X,J)*1000:PRINT@96,"LOANS OUTSTANDING $";C(X,O)*1000
130 PRINT"PRODUCTION UNITS =";C(X,L):W=C(X,J)+C(X,L)*10-C(X,O):PRINT@160,"PER SHARE BOOK VALUE =";FIX(W/20):PRINT@192,"SHARES AVAIL =";C(X,K)*1000;TAB(32)"BID =";C(X,M);TAB(49)"ASK =";C(X,N):PRINT:Y=FIX((C(X,J)-100)/20):IFY<1GOTO145
135 GOSUB710:PRINT"TO DECLARE DIVIDENDS";:GOSUB750:ONFGOTO145,135
140 PRINT@320,CHR$(31);"DIVIDEND LIMIT PER SHARE=";Y;:INPUT"-- ENTER DIVIDEND PER SHARE";F:F=FIX(ABS(F)):IFF>YGOTO140:ELSEFORY=1TOP:PC(Y)=PC(Y)+C(X,Y)*F:NEXTY:C(X,J)=C(X,J)-20*F:GOSUB625
145 IFC(X,L)=0GOTO160
150 GOSUB710:PRINT"TO SELL PRODUCTION UNITS";:GOSUB750:ONFGOTO160,150
155 PRINTCHR$(27);CHR$(31);"RESALE VALUE IS $9000 -- LIMIT IS";C(X,L);:INPUT"--HOW MANY";F:F=FIX(ABS(F)):IFF>C(X,L)GOTO155:ELSEC(X,J)=C(X,J)+F*9:C(X,L)=C(X,L)-F:GOSUB625
160 GOSUB710:PRINT"TO BUY PRODUCTION UNITS";:GOSUB750:ONFGOTO170,160
165 PRINTCHR$(27);CHR$(31);"PRODUCTION UNITS COST $10000 EACH -- HOW MANY";:INPUTF:F=FIX(ABS(F)):IFF*10>C(X,J)THENGOSUB650:GOTO165:ELSEC(X,L)=C(X,L)+F:C(X,J)=C(X,J)-F*10:GOSUB625
170 IFC(X,O)<1GOTO175:ELSEPRINTC$(X);" OWES THE BANK $";C(X,O)*1000:Y=C(X,J):GOSUB765:C(X,J)=Y-I:C(X,O)=C(X,O)-I
175 PRINT"DOES ";C$(X);" WANT TO BORROW";:GOSUB750:ONFGOTO177,175:Y=C(X,J)+C(X,L)*10-C(X,O):I=CI:GOSUB780:C(X,J)=C(X,J)+F:C(X,O)=C(X,O)+F
177 NEXTX
180 CLS:PRINT"GAME TO CONTINUE (Y/N) ";:GOSUB750:ONFGOTO245,180:FORX=1TOP:PL(X)=PL(X)+FIX(PL(X)*PI/100):NEXTX:FORX=1TOJ:C(X,O)=C(X,O)+FIX(C(X,O)*CI/100):CE(X)=C(X,L)*(6-RND(8)):F=FIX((C(X,J)+C(X,L)*10-C(X,O))/20)
185 I=FIX(CE(X)*(4+RND(10))/20):IFI>FTHENC(X,M)=I-1:C(X,N)=I+1:ELSEC(X,M)=F-1:C(X,N)=F+1
190 IFYR=29THENCE(X)=-CE(X)
195 C(X,J)=C(X,J)+CE(X):IFC(X,J)<0THENC(X,J)=100:C(X,O)=0:C(X,L)=0:C(X,M)=3:C(X,N)=5
200 NEXTX:YR=YR+1:X=RND(50):IFX/10<>INT(X/10)GOTO35:ELSECLS
201 PRINT@20,"NEWS  FLASH !":ONX/10GOTO210,220,230
205 PRINT"THE TAXMAN COMETH !":PRINT"THE FEDERAL GOVERNMENT HAS LEVIED A CAPITAL GAINS TAX...":PRINT"10 % OF PLAYERS' CASH IS TAKEN !":FORX=1TOP:PC(X)=PC(X)-INT(PC(X)/10):NEXTX:GOTO240
210 PRINT"SPECIAL DEPRECIATION ALLOWANCE !":PRINT"THE GOVERNMENT,IN ITS FIGHT AGAINST THE RECESSION,":PRINT"HAS GRANTED A SPECIAL ALLOWANCE...COMPANIES WITH PRODUCTION":PRINT"UNITS RECEIVE $ 5000 PER PRODUCTION UNIT..."
215 FORX=1TOJ:C(X,J)=C(X,J)+C(X,L)*5:NEXTX:GOTO240
220 PRINT"STOCK FRAUD DISCOVERED !":PRINT"WALL STREET IN A PANIC ! THE MAFIA HAS BEEN ISSUING FALSE STOCK CERTIFICATES FOR YEARS...EVERY PLAYER LOSES $10000 CASH":FORX=1TOP:PC(X)=PC(X)-10:IFPC(X)<0THENPC(X)=0
225 NEXTX:GOTO240
230 PRINT"ARABS SELL OIL FOR $ 5 A BARREL !":PRINT"PRINCE KHALIM-FUZZY ,IN RETURN FOR NUCLEAR ARMS -- FOR DEFENSE  PURPOSES ,OF COURSE -- AGREES TO SELL HIS OIL FOR $ 5 A BARREL.":PRINT"INDUSTRY PROSPERS...ALL STOCK PRICES UP $ 3..."
235 FORX=1TOJ:C(X,M)=C(X,M)+3:C(X,N)=C(X,N)+3:NEXTX
240 Z=65:GOSUB600:GOTO35
245 CLS:PRINT"PLAYER";TAB(30)"NET WORTH":PRINT:FORX=1TOP:F=0:FORY=1TOJ:F=F+C(Y,X)*C(Y,M):NEXTY:F=F+PC(X)-PL(X):PRINTN$(X);TAB(29)"$";F*1000:NEXTX:PRINT"END OF GAME...":END
525 FORX=1TO32:PRINT"$";:FORY=1TO10:NEXTY,X:RETURN
550 DATA"OFF-SHORE OIL","ELECTRO LTD.","NORTHMINE HOLDINGS","DATAMACHINE","GASSO","BOING AIRCRAFT"
600 FORI=1TOZ*100:NEXTI:RETURN
625 PRINT"YOUR CASH BALANCE IS NOW $";C(X,J)*1000:RETURN
650 PRINT"INSUFFICIENT CASH";CHR$(29);:GOSUB600:RETURN
700 CLS:PRINT@17,"MARKET  REPORT --";YR:PRINT"  COMPANY";TAB(25)"EARNINGS   BID   ASK":FORY=1TOJ:PRINTY;C$(Y);TAB(25)CE(Y)*1000;TAB(35)C(Y,M);TAB(41)C(Y,N):NEXTY:RETURN
710 PRINT"DO YOU WANT ";:RETURN
750 F=0:INPUTA$:I=ASC(A$):IFI=78ORI=89GOTO755:ELSEPRINTCHR$(27);CHR$(30);"RE-TYPE...ANSWER 'Y' OR 'N'":F=2:GOSUB600:PRINTCHR$(27);CHR$(31);:RETURN
755 IFI=78THENF=1
760 RETURN
765 GOSUB710:PRINT"TO MAKE A LOAN PAYMENT";:GOSUB750:I=0:ONFGOTO775,765
770 PRINTCHR$(27);CHR$(31);:INPUT"ENTER AMOUNT OF LOAN PAYMENT(000'S)";I:I=FIX(ABS(I)):IFI>YTHENGOSUB650:GOTO770:ELSEPRINT"THANK YOU"
775 RETURN
780 PRINTCHR$(27);"INTEREST RATES ARE";I;"% YEARLY -- HOW MUCH(000'S)";:INPUTF:F=FIX(ABS(F)):IFF>0ANDF<10THENPRINT"MINIMUM IS $10000";CHR$(29);:GOTO780
785 IFF>Y/2THENPRINT"AMOUNT EXCESSIVE--REFUSED !":F=0:GOTO790:ELSEPRINTCHR$(30);F*1000;"IS APPROVED";
790 GOSUB600:RETURN
