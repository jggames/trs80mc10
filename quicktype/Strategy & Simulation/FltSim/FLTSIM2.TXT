10 GOSUB2600:REM FLIGHT SIMULATION
20 CLEAR1100:RP=0 
30 LD=INT(RND(0)*360)
40 DIM E$(1000):REM THIS HOLDS FLIGHT RECORD
50 DIM A$(7),C$(7):REM THESE ARRAYS HOLD HORIZON AND COMPASS OUTPUT
60 REM **************************
70 GOSUB2320:REM INITIALIZE
80 IFCR=0THENGOSUB820:REM HORIZON/COMPASS
90 GOSUB500:REM PRINTOUT
100 IFCR=1THEN2500
110 IFLA=1ANDUF=1THENPRINT@0,"WELL DONE. A PERFECT LANDING!":GOTO2500
120 IF(LA=1ANDUF=0)OR(AI=0ANDUF=0)THENPRINT@480,":     YOUR WHEELS ARE UP      ";:FORJ=1TO2500:NEXT:GOSUB1780:GOTO90
130 T=AI:S=0
140 X$=INKEY$
150 IFX$="R"THENRP=1:GOTO70
160 IFRP=1ANDE$(CL+1)=""THENRP=0:GOTO140
170 IFRP=1THENX$=E$(CL+1)
180 IFX$=""THEN140
190 IFCL<999THENE$(CL+1)=X$
200 IFTA=1THENEL=INT(EL+RND(0)*2-RND(0)*3)
210 IFAI<3THEN290
220 IFX$="W"THENEL=EL+5:EF=5:IFEL>60THENST=1
230 IFX$="Z"THENEL=EL-5:EF=-5:IFEL<-70THENST=-1
240 IFST<>0THENGOSUB1640
250 IFAL<1THEN290:REM PREVENTS DRAMATIC TURNS ON THE GROUND
260 IFX$="A"THENWA=WA-.5:AN=AN-6:IFWA<-3THENWA=-3
270 IFX$="S"THENWA=WA+.5:AN=AN+6:IFWA>3THENWA=3
280 AN=INT(AN+RND(0)*2-RND(0)*2)
290 IFX$=" "THENAI=AI+8.5
300 IFX$="."THENAI=AI-7
310 AI=AI-EL/5
320 IFUF=1THENAI=AI-1.5:FU=FU-.5
330 IFAI<0THENAI=0
340 IFAI>400THENAI=400
350 IFX$="1"ANDUF=0THENUF=1:GOTO370
360 IFX$="1"ANDUF=1THENUF=0
370 FU=FU-(ABS(T-AI)/10)-3.75
380 IFFU<1THENGOSUB1780
390 IFTA=1THEN420
400 IFEL>10ANDAI>45ANDAI<60ANDUF=1THENTA=1
410 IFTA=0THENAL=0:GOTO450
420 IFLA=0ANDAI<30THENEL=EL-5:AL=9*AL/10
430 AL=AL+INT(((EL+.1)*AI)+EF*AI/1000)/80
440 IFAL<300ANDTA=1THENAL=AL+AI/30+EL
450 IFAL<0THENGOSUB1780:REM CRASH
460 REM CHANGE NEXT TWO LINES TO MAKE IT EASIER (OR EVEN HARDER) TO LAND
470 IFAL>15ANDAI>20ORTA=0THEN80
480 IFABS(AN-LD)<13ORABS(AN+360-LD)<13THENLA=1
485 GOTO80
490 REM **************************
500 REM PRINTOUT
510 PRINT@0,; 
520 PRINT"   HORIZON";TAB(19);"HEADING"
530 EV=INT(EL/10)
540 IFEV>2THENEV=2
550 IFEV<-2THENEV=-2
560 IFEV<>0ANDTA=1ANDCR=0THENGOSUB1920
570 PRINT":----------------:---------:"
580 FORJ=1TO7
590 PRINT": ";A$(J ); " :";C$(J) ;" :"
600 A$(J) =""
610 NEXTJ
620 PRINT":----------------:---------:"
630 DI=DI+ABS((COS(EL))*AI)/360
640 CL=CL+1
650 PRINT":RANGE"INT(DI*10)/10": TIME"INT(CL)/10;":"LD
670 PRINT":AIRSPEED :"INT(AI)
690 PRINT":ALTIMETER:"INT(AL);
700 IFAN<0THENPRINTTAB(18);360+AN"DEG."
710 IFAN>=0THENPRINTTAB(18);AN"DEG."
720 MR=INT(AL/30):IFMR>20THENMR=20
740 PRINT":FUEL     :"INT(FU)
770 PRINT":ELEVATION:"EL": ";:GOSUB2210:PRINTU$
780 IFUF=1THENPRINT@480,":";TAB(4);"> UNDERCARRIAGE DOWN < :";
790 IFUF=0THENPRINT@480,":";TAB(5);"> UNDERCARRIAGE UP <  :";
800 RETURN
810 REM **************************
820 REM ASSIGN HORIZON/COMPASS
830 IFABS(INT(WA+.5))=3THENGOSUB980
840 IFABS(INT(WA+.5))=2THENGOSUB1070
850 IFABS(INT(WA+.5))=1THENGOSUB1160
860 IFINT(WA+.5)=0THENGOSUB1250
870 REM NEXT TWO LINES USED TO GRADUALLY STRAIGHTEN UP WINGS
880 IFWA>0THENWA=WA-.2
890 IFWA<0THENWA=WA+.2
900 IFWA>.2THEN1350
910 FORZ=1TO7
920 M$(8-Z)=A$(Z)
930 NEXTZ
940 FORZ=1TO7
950 A$(Z)=M$(Z)
960 NEXTZ
970 GOTO1350
980 REM WA=3 OR -3
990  A$(1)="            * "
1000 A$(2)="          **  "
1010 A$(3)="        **    "
1020 A$(4)="      **      "
1030 A$(5)="    **        "
1040 A$(6)="  **          "
1050 A$(7)="**            " 
1060 RETURN 
1070 REM WA=2 OR -2
1080 A$(1)="              "
1090 A$(2)="           ** "
1100 A$(3)="        ***   " 
1110 A$(4)="     ***      "
1120 A$(5)="  ***         " 
1130 A$(6)="***           "
1140 A$(7)="              "
1150 RETURN
1160 REM WA=1 OR -1
1170 A$(1)="              "
1180 A$(2)="              "
1190 A$(3)="         *****"
1200 A$(4)="   ******     "
1210 A$(5)="****          "
1220 A$(6)="              "
1230 A$(7)="              "
1240 RETURN
1250 REM WA=0
1260 A$(1)="              "
1270 A$(2)="              "
1280 A$(3)="              "
1290 A$(4)="**************"
1300 A$(5)="              "
1310 A$(6)="              "
1320 A$(7)="              "
1330 RETURN
1340 REM **************************
1350 REM ASSIGN COMPASS STRINGS
1360 F2=AN-F1
1370 IFF2<0THENFA=INT((F2+375)/30)
1380 IFF2>=0THENFA=INT((F2+15)/30) 
1390 IFFA=12THENFA=0
1400 C$(1)="   .N.  "
1410 IFFA=11THENC$(2)="  .@:.. ":GOTO1450
1420 IFFA=0THEN C$(2)="  ..@.. ":GOTO1450
1430 IFFA=1THEN C$(2)="  ..:@. ":GOTO1450
1440 C$(2)="  ..:.. " 
1450 IFFA=10THENC$(3)=" .@ : ..":GOTO1480
1460 IFFA=2THEN C$(3)=" .. : @.":GOTO1480
1470 C$(3)=" .. : .."
1480 IFFA=9THEN C$(4)=" W@-X--E":GOTO1510 
1490 IFFA=3THEN C$(4)=" W--X-@E":GOTO1510
1500 C$(4)=" W--X--E"
1510 IFFA=8THEN C$(5)=" .@ : ..":GOTO1540
1520 IFFA=4THEN C$(5)=" .. : @.":GOTO1540
1530 C$(5)=" .. : .."
1540 IFFA=7THEN C$(6)="  .@:.. ":GOTO1580
1550 IFFA=6THEN C$(6)="  ..@.. ":GOTO1580 
1560 IFFA=5THEN C$(6)="  ..:@. ":GOTO1580
1570 C$(6)="  ..:.. "
1580 C$(7)="   .S.  "
1590 IFAN>360THENAN=AN-360 
1600 F2=AN 
1610 IFW>0THENW=W-.4
1620 IFW<0THENW=W+.4
1630 RETURN
1640 REM STALL/FALL
1650 IFST=-1THEN1710
1660 PRINT:FORJ=1TO10
1670 PRINTTAB(J-1);"YOU HAVE STALLED!"
1680 NEXTJ
1690 AI=AI/4
1700 RETURN 
1710 PRINT:FORJ=1TO10
1720 PRINTTAB(J-1);"UNCONTROLLED DIVE!!"
1730 PRINTTAB(20-J);"PULL UP!!"
1740 NEXTJ
1750 AL=4*AL/5
1760 RETURN 
1770 REM *************************
1780 REM CRASH
1790 CR=1
1800 AL=0
1810 M$="** *C R**  A ** S* H* !!*" :REM 25 CHARACTERS LONG
1820 PRINT:FORJ=1TO20
1830 PRINTTAB(J-1);"CRASH!"
1840 PRINTTAB(20-J);"CRASH!"
1850 NEXTJ
1860 FORJ=1TO7
1870 G=INT(RND(0)*11)+1
1880 A$(J)=MID$(M$,G,14)
1890 NEXTJ
1900 RETURN
1910 REM ************************** 
1920 REM ADJUST HORIZON
1930 G$="              ":REM 14 SPACES
1940 ONEV+3GOSUB1960,2020,2070,2080,2140
1950 RETURN
1960 REM EV=-2
1970 FORJ=1TO4
1980 A$(J)=A$(J+3)
1990 NEXTJ
2000 A$(5)=G$:A$(6)=G$:A$(7)=G$
2010 RETURN
2020 REM EV=-1
2030 FORJ=1TO6
2040 A$(J)=A$(J+1)
2050 NEXTJ
2060 A$(7)=G$
2070 RETURN:REM EV=0
2080 REM EV=1
2090 FORJ=6TO1STEP-1
2100 A$(J+1)=A$(J)
2110 NEXTJ
2120 A$(1)=G$
2130 RETURN
2140 REM EV=2
2150 FORJ=4TO1STEP-1
2160 A$(J+3)=A$(J)
2170 NEXTJ
2180 A$(1)=G$:A$(2)=G$:A$(3)=G$
2190 RETURN
2200 REM ************************** 
2210 REM INPUT INTO COMMAND NAME
2220 U$="------------"
2230 IFX$=" "THENU$="THROTTLE ON "
2240 IFX$="."THENU$="THROTTLE OFF"
2250 IFX$="W"ANDAL>0THENU$="   CLIMB    "
2260 IFX$="W"ANDAL=0THENU$="  NOSE UP   " 
2270 IFX$="Z"THENU$=" NOSE DOWN  "
2280 IFX$="A"THENU$=" BANK LEFT  "
2290 IFX$="S"THENU$=" BANK RIGHT "
2300 RETURN
2310 REM **************************
2320 REM INITIALIZATION
2330 CLS 
2340 J=RND(-(PEEK(9)*256+PEEK(10)))
2350 REM LINE STRING REMOVED
2360 UF=1:REM UNDERCARRIAGE - 1 - DOWN, - UP
2370 EF=0:REM CLIMB RATE
2380 AN=0:TA=0:LA=0
2390 AI=0
2400 DI=0:REM DISTANCE COVERED 'RANGE'
2410 AL=0
2420 EL=0:REM ANGLE OF ELEVATION
2430 WA=0:REM 'WING ANGLE; USED IN HORIZON PRINTOUT
2440 FU=750:CR=0:F2=0:F1=0:REM FOR DIRECTION CHANGE/COMPASS ROUTINE
2450 CL=0:REM TIME
2460 X$=""
2470 RETURN
2500 PRINT@480,"  GO FOR ANOTHER FLIGHT (Y/N)?";
2510 X$=INKEY$:IFX$=""THEN2510
2520 IFX$="Y"THENRUN20
2530 IFX$="N"THENEND
2540 GOTO2510
2600 CLS:?"       FLIGHT SIMULATION"
2610 PRINT"        BY TIM HARTNELL"
2620 PRINT"     MC-10 VER. JIM GERRIE"
2630 PRINT
2640 PRINT"ALL TAKE-OFFS ARE INTO THE NORTH";
2650 PRINT"(SPEED 45-60; ELEVATION >10)."
2670 PRINT"LANDING DIRECTION SHOWN BELOW"
2680 PRINT"COMPASS. YOU MUST BE WITHIN 12"
2700 PRINT"DEGREES OF THIS FOR SUCCESSFUL"
2710 PRINT"LANDING."
2720 PRINT"W=NOSE UP  Z=NOSE DOWN  1=GEAR"
2730 PRINT"A=BANK LEFT  S=BANK RIGHT"
2740 PRINT"SPACE=THROTTLE ON  .=OFF"
2750 PRINT"R=REPLAY FLIGHT SO FAR":PRINT
2760 PRINT"     HIT <ENTER> TO BEGIN.";
2770 X$=INKEY$:IFX$<>CHR$(13)THEN2770
2780 RETURN
