0 CLS:CLEAR500:SC=16:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

REM 'USING' ARRANGE IN COLUMNS
REM CC IS VALUE; Z1,Z2=WHOLE NUM LENGTH,DECIMCAL LENGTH
3 Z3=INT(CC*10^Z2+.5):M$=RIGHT$("     "+STR$(Z3),Z1+Z2+1):IFZ2<1THENFORZ3=.TO.:GOTO5
4 FORZ3=Z1+2TOZ1+Z2+1:IFASC(MID$(M$,Z3))>47THENZ4=Z3-Z1-1:M$=MID$(M$,Z4,Z1+1)+LEFT$(".00000",Z4)+MID$(M$,Z3):Z3=255
5 NEXT:IFASC(M$)>47THENM$=LEFT$("**********",Z1+Z2+2+(Z2=.))
6 RETURN
9 INPUT"HIT enter TO CONTINUE";M$:FORCC=1TO32:PRINTCHR$(8);:NEXT:PRINT:RETURN

10 CLS:DIMCC,ZZ,M$,I$,A(100),A0(100),A1(7),A2(7),A3(6),A4(6)
11 PRINT"*** A SPACECRAFT SIMULATOR  ***"
12 PRINT"***      BY GARY SIVAK      ***"
13 PRINT"***      BYTE MAGAZINE      ***"
14 PRINT"***  NOVEMBER 1979. P. 104  ***"
15 PRINT
20 PRINT"DESIGN AND ORBIT A SPACE SHIP.":PRINT"ENTER NO. STAGES UP TO 6."
30 INPUTA5
40 M$="VERIFICATION:"+STR$(A5)+" STAGES.":GOSUB1:PRINT
50 A6=A5+1
60 M$="ENTER ITERATION TIME IN SEC. AND ORBIT HEIGHT IN MI.":GOSUB1
70 M$=".1 SEC. IS OK AND .01 BETTER, BUT WITH MORE CPU TIME.":GOSUB1
80 INPUT"SEC,MI";A7,A8
90 M$="VERIFICATION: ITERATION TIME"+STR$(A7)+", ORBIT HEIGHT"+STR$(A8):GOSUB1:PRINT
100 PRINT"ENTER PAYLOAD WEIGHT IN POUNDS."
110 INPUTA2(A6)
120 A1(A6)=0
130 M$="VERIFICATION: PAYLOAD WEIGHT,"+STR$(A2(A6)):GOSUB1:PRINT
140 FORA9=1TOA5
150 B=A6-A9
160 B0=3+1
170 M$="ENTER STAGE"+STR$(B)+" FUEL AND HULL WEIGHTS IN LBS.":GOSUB1
180 INPUTA1(B),A2(B)
190 M$="STAGE"+STR$(B)+" FUEL"+STR$(A1(B))+" LBS., HULL"+STR$(A2(B))+" LBS.":GOSUB1
200 A2(3)=A2(B)+A2(B0)+A1(B0)
210 B1=A2(B)+A1(B)
220 M$="ENTER STAGE"+STR$(B)+" THRUST, AT LEAST"+STR$(B1)+" LBS.":GOSUB1
230 INPUTA3(B)
240 M$="STAGE"+STR$(B)+" THRUST,"+STR$(A3(B))+" LBS.":GOSUB1
250 M$="ENTER SPECIFIC IMPULSE OF STAGE"+STR$(B)+" FUEL/OXIDIZER.":GOSUB1
260 M$="THIS IS THE THRUST-TO-BURN RATE RATIO: FOR GASOLINE=250, PEROXIDE=300, LIQUID HYDROGEN=500.":GOSUB1
280 INPUTA4(B)
290 M$="VERIFICATION: STAGE"+STR$(B)+" SPECIFIC IMPULSE:"+STR$(A4(B)):GOSUB1:PRINT
300 NEXTA9
310 B2=10
320 B3=B2*A7
330 B4=360
340 B5=B3/100
350 B6=5280*.3048
360 B7=6.67E-11*5.983E24
370 X0=1:GOSUB2800:B8=AT/45
380 B9=90
390 C=1
400 C0=SQR(B7/9.80665)
410 C1=C0
420 C2=SQR(B7/(C0+B6*A8))/.3048
430 C3=0
440 C4=0
450 C5=0
460 C6=0
470 C7=0
480 C8=0
490 C9=0
500 D=0
510 D0=0
520 D1=0
530 D2=0
540 D3=0
550 M$="THE SHIP CAN SWIVEL"+STR$(B2)+" DEG/SEC.":GOSUB1
560 M$="EARTH'S GRAVITY IS 32.174 FT/SEC/SEC.":GOSUB1
570 M$="FORWARD VELOCITY NEEDED FOR ORBIT:"+STR$(C2)+" FT/SEC.":GOSUB1
580 D=D+1
590 D4=A2(D)/2.2046
600 D5=A3(D)/A4(D)/2.2046
610 D6=A1(D)/2.2046
620 D7=D6
630 D8=A3(D)/2.2046*9.80665
640 M$="IGNITION OF STAGE"+STR$(D)+", ENTER THE STAGE NUMBER TO LAUNCH.":GOSUB1
645 INPUTX1
650 GOTO1090
660 M$="ENTER (1) THROTTLE SETTING IN %, FROM 0 TO 100, (2) THRUST ANGLE IN DEG. FROM "+STR$(-B4)+" TO"+STR$(B4)+", "
680 M$=M$+"AND (3) BURN TIME IN SECONDS.":GOSUB1
690 INPUT"1,2,3";D9,E,E0
700 D9=ABS(D9/100)
710 E1=D9*D8
720 E2=D9*D5*A7
730 E3=E2/100
740 E4=E0-(A7/100)
750 E5=C5*C1
760 E6=0
770 IFE0=0THEN1080
780 IFC1<C0THEN1080
790 E6=E6+A7
800 E7=D7-E2
810 E8=E1/(D4+(D7+E7)/2)
820 IFE7>=E3THEN850
830 E7=0
840 E8=0
850 IFABS(E-B9)<B5THEN930
860 IFE<B9THEN890
870 B9=B9+B3
880 GOTO900
890 B9=B9-B3
900 E9=B9*B8
910 C4=COS(E9)
920 C=SIN(E9)
930 F=E8*C4
940 F0=E8*C
950 F1=C5+F*A7
960 C6=(C5+F1)/2
970 C7=C7+C6*A7
980 F2=F0+C6^2/C1-B7/C1^2
990 F3=C8+F2*A7
1000 F4=C1+(C8+F3)/2*A7
1010 IFD9THEN1030
1020 F1=E5/F4
1030 D7=E7
1040 C5=F1
1050 C8=F3
1060 C1=F4
1070 IFE6<E4THEN770
1080 C3=C3+E6
1090 D2=D2+1
1100 A(D2)=(C1-C0)/.3048
1110 IFC9>=A(D2)THEN1130
1120 C9=A(D2)
1130 IFA(D2)>=0THEN1150
1140 A(D2)=0
1150 IFA(D2)<400000THEN1170
1160 D3=D3+1
1170 F5=A(D2)/5280
1180 F6=C8/.3048
1190 F7=F6*15/22
1200 F8=C5/.3048
1210 F9=F8*15/22
1220 A0(D2)=C7/B6
1230 G=100*D7/D6
1240 G0=D7/D5
1250 G1=B7/C1^2-C6^2/C1
1260 G2=D8/(D4+D7)/.3048
1270 G3=G2*15/22
1280 G4=G2-(G1/.3048)
1290 G5=G4*15/22
1300 G6=G1/.3048/G2
1310 G7=100*G6
1320 G8=90
1330 IFG6>=1THEN1350
1340 X0=G6/SQR(1-G6^2):GOSUB2800:G8=AT/B8
1350 G9=SQR(B7/C1)/.3048
1360 H=100*F8/C2
1370 H0=100*A(D2)/(A8*5280)
1380 H1=100*F8/G9
1390 H2=(C2-F8)/G2
1400 H3=(G9-F8)/G2
1410 IFF6=0THEN1440
1420 H4=(A8*5280-A(D2))/F6
1430 IFH4<=9999.99THEN1460
1440 H4=9999.99
1450 REM TIMES OVER 9999.99 SET TO 9999.99 TO NOT EXCEED DISPLAY
1460 IFD3<>1THEN1480
1470 M$="400K FT. ACHIEVED, YOU ARE IN VACUUM.":GOSUB1
1480 PRINT"    TIME"TAB(8)"    FUEL"TAB(16)"   @FULL"TAB(24)"   ANGLE";
1490 CC=C3:Z1=5:Z2=1:GOSUB3:PRINTM$;
1491 CC=G:Z1=5:Z2=1:GOSUB3:PRINTM$;
1492 CC=G0:Z1=5:Z2=1:GOSUB3:PRINTM$;
1493 CC=B9:Z1=5:Z2=1:GOSUB3:PRINTM$;
1494 PRINT"     SEC"TAB(8)"       %"TAB(16)"     SEC"TAB(24)"     DEG";
1500 PRINT
1510 PRINT"     ALT"TAB(8)"  ASCENT"TAB(16)"  FWD.V."TAB(24)"   RANGE";
1520 CC=A(D2):Z1=5:Z2=1:GOSUB3:PRINTM$;
1521 CC=F6:Z1=5:Z2=1:GOSUB3:PRINTM$;
1522 CC=F8:Z1=5:Z2=1:GOSUB3:PRINTM$;
1523 CC=A0(D2):Z1=5:Z2=1:GOSUB3:PRINTM$;
1524 PRINT"     FT."TAB(8)"  MI/HR."TAB(16)"  MI/HR."TAB(24)"     MI.";
1530 CC=F5:Z1=5:Z2=1:GOSUB3:PRINTM$;
1531 CC=F7:Z1=5:Z2=1:GOSUB3:PRINTM$;
1532 CC=F9:Z1=5:Z2=1:GOSUB3:PRINTM$
1535 PRINT"     MI."TAB(8)"  MI/HR."TAB(16)"  MI/HR."
1540 PRINT
1550 PRINT" MAX ACC"TAB(8)"  MAX V."TAB(16)"   ANGLE"TAB(24)"   THROT";
1560 REM ANGLE (C.A.), CRITICAL ANGLE FOR CONST ASCENT AT FULL THROT.
1570 REM THROT (C.A.), CRITICAL THROT. OF CONST. ASCENT AT 90 DEG.
1580 CC=G2:Z1=5:Z2=1:GOSUB3:PRINTM$;
1581 CC=G4:Z1=5:Z2=1:GOSUB3:PRINTM$;
1582 PRINT" FULL TH   V.POS";
1590 CC=G3:Z1=5:Z2=1:GOSUB3:PRINTM$;
1591 CC=G5:Z1=5:Z2=1:GOSUB3:PRINTM$;
1592 CC=G8:Z1=5:Z2=1:GOSUB3:PRINTM$;
1593 CC=G7:Z1=5:Z2=1:GOSUB3:PRINTM$;
1600 GOSUB9
1610 CC=H:Z1=5:Z2=2:GOSUB3:M$=M$+"% ORBITAL VELOCITY":GOSUB1
1611 CC=H0:Z1=5:Z2=2:GOSUB3:M$=M$+"% ORBITAL HEIGHT.":GOSUB1
1620 CC=H1:Z1=5:Z2=2:GOSUB3:M$=M$+"% VELOCITY NEEDED FOR ORBIT AT CURRENT ALTITUDE.":GOSUB1
1630 GOSUB9
1640 PRINT"TIME TO ACHIEVE (IN SECONDS):"
1650 PRINT"ORB.ALT."TAB(8)" ORB.VEL"TAB(16)" CUR.ALT"
1660 PRINT"   @CUR."TAB(8)"   @FULL"TAB(16)"   @FULL"
1670 CC=H4:Z1=5:Z2=1:GOSUB3:PRINTM$;
1671 CC=H2:Z1=5:Z2=1:GOSUB3:PRINTM$;
1672 CC=H3:Z1=5:Z2=1:GOSUB3:PRINTM$
1680 GOSUB9
1690 IFH<100THEN1760
1700 IFH0<100THEN1760
1710 D0=D0+1
1720 IFD0>1THEN1760
1730 M$="IN DESIRED ORBIT. TO CONTINUE ENTER 1, TO PLOT ENTER 2.":GOSUB1
1740 INPUTH5
1750 IFH5=2THEN1920
1760 IFC3=0THEN660
1770 IFD7<=E3THEN1800
1780 IFA(D2)<=0THEN1800
1790 GOTO660
1800 IFA(D2)=0THEN1890
1810 IFD<A5THEN580
1820 D1=D1+1
1830 IFD1<>1THEN1850
1840 PRINT"LAST STAGE SHUTDOWN."
1850 IFD0THEN1880
1860 IFA(D2)<=0THEN1880
1870 GOTO660
1880 IFA(D2)>0THEN1920
1890 H6=INT(SQR(F6^2+F8^2)+.5)
1900 H7=INT(SQR(F7^2+F9^2)+.5)
1910 M$="YOU CRASHED AT"+STR$(H6)+" FT/SEC,"+STR$(H7)+" MI/HR.":GOSUB1:GOTO1960
1920 M$="AFTER"+STR$(D2)+" PLOT POINTS:":GOSUB1:GOSUB9:IFSC=0THENSC=16
1930 CLS0:FORCC=0TO14:PRINT@32*CC,CHR$(175);:NEXT:FORH8=1TOD2
1940 XX=ABS(A0(H8)/(SC*3)):YY=ABS((A(H8)/5280)/SC):IFXX>=0ANDXX<=31ANDYY>=0ANDYY<=63THENSET(YY,XX,5)
1950 NEXTH8:SET(0,0,3):PRINT@480,;:GOSUB9:SC=VAL(M$):IFSC>0THEN1930
1955 END
1960 H9=25
1970 REM-LOWER 25% CUTOFF OF ALTITUDE FOR A BLOWUP PLOT.
1980 I=C9*H9/100*1.0001
1990 I0=D2+1
2000 I0=I0-1
2010 IFA(I0)>ITHEN2000
2020 I1=100*A0(I0)/A0(D2)
2030 M$="LOWER"+STR$(H9)+"% OR"+STR$(I)+" MI. OF MAX ALT. ATTAINED.":GOSUB1
2040 M$="FIRST"+STR$(I1)+"% OR"+STR$(A0(I0))+" MI. OF TOTAL RANGE.":GOSUB1
2050 M$="WITH"+STR$(I0)+" STEPS:":GOSUB1:GOSUB9:IFSC=0THENSC=16
2060 CLS0:FORCC=0TO14:PRINT@32*CC,CHR$(175);:NEXT:FORI2=1TOI0
2070 XX=ABS(A0(I2)/(SC*3)):YY=ABS((A(I2)/5280)/SC):IFXX>=0ANDXX<=31ANDYY>=0ANDYY<=63THENSET(YY,XX,4)
2080 NEXTI2
2090 SET(0,0,3):PRINT@480,;:GOSUB9
2100 SC=VAL(M$):IFSC>0THEN2060
2110 END
2600 X2=X1*X1:CC=1:ZZ=X1
2610 FORI=18TO2STEP-2
2620 CC=1-X2*CC/(I*(I-1))
2630 ZZ=X1-X2*ZZ/(I*(I+1))
2640 NEXTI:RETURN
2650 REM END OF SIN COS ROUTINE
2660 P2=355/226:X1=P2/2:GOSUB2600:P2=P2+CC*CC-ZZ*ZZ:RETURN
2700 REM SUBROUTINE FOR ATN
2710 REM REQ: SIN COS (INITIALIZED)
2720 REM TO CALCULATE: GOSUB 2800
2730 REM INPUT VARIABLE: X0
2740 REM OUTPUT VARIABLE: AT
2750 REM DBLE PREC: X0,AT,X1,X3,X4
2770 REM
2800 GOSUB2660:IFX0<-1THENAT=-P2:GOTO2820
2810 IFX0>1THENAT=P2:GOTO2820
2811 GOTO2840
2820 X3=1/X0:GOSUB2900
2830 AT=AT-X4:RETURN
2840 X3=X0:GOSUB2900
2850 AT=X4:RETURN
2860 REM
2900 X4=0:N=3
2910 X1=X3*(1-X3*X3/3)
2920 X4=X4+X1
2930 N=N-1:IFN=0THENRETURN
2940 GOSUB2600:X1=ZZ/CC
2950 X3=(X3-X1)/(1+X1*X3)
2960 GOTO2910
2970 REM END OF ATN ROUTINE
3000 REM REM-OUT 1960-2080, AS NO PLOTTING/PRINTING ROUTINE
3010 REM PROVIDED IN BYTE ARTICLE. ADDED 1935, 1937 -- DHH (2/2024)
