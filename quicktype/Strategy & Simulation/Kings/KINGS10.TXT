0 CLEAR1000:DIMCC,ZZ,M$,I$,R$(5):MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO9500
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 M$=MID$(STR$(D),2)+" ":RETURN
4 FORX=1TO15:NEXT:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 CLS:M$=" YOUR AMBITION IS TO DEMOLISH YOUR OPPONENT BY DESTROYING HIS CASTLE'S HIT POINTS OR HIS WARRIORS, "
11 M$=M$+"OR BY KILLING THE KING HIMSELF.":GOSUB1:M$=" YOU ACCOMPLISH THIS BY ADVANCING YOUR TROOPS INTO BATTLE, "
13 M$=M$+"FIRING CANNON, OR BY UNLEASHING YOUR CATAPULT, IF YOU ARE FORTUNATE ENOUGH TO AQUIRE ONE.":GOSUB1:GOSUB460
16 CLS:M$=" YOU HAVE A NUMBER OF SPIES AT YOUR DISPOSAL, AT A COST OF $10,000 EACH, WHO WILL SEEK OUT THE "
17 M$=M$+"OPPOSING KING AND KILL HIM-- if THEY ELUDE THE KING'S PROTECTORS.":GOSUB1
18 M$=" EACH OF YOUR WARRIORS IS PAID $2 EACH PAYDAY FOR FIGHTING.":GOSUB1
19 M$=" IF YOU HAVE MORE WARRIORS THAN YOU CAN PAY THEY CAN BE RELEASED AND SENT HOME.":GOSUB1:GOSUB460
20 CLS:M$=" IF YOUR CANNON ARSENAL SHOULD  DIMINISH, MORE ARE AVAILABLE FOR PURCHASE.":GOSUB1
21 M$=" YOUR FATHERS ARE BOTH EMPERORS AND FROM TIME TO TIME WILL SEND GIFTS TO HELP YOU.":GOSUB1
22 M$=" THERE ARE NATURAL HAZARDS, SUCH AS FLOODS AND PLAGUES, WHICH CAN BE DEVASTATING, SO KEEP YOUR TROOP NUMBERS UP."
23 GOSUB1:GOSUB460
40 CLS:GOSUB9000:SOUND180,3:PRINT@8,"KINGS & CASTLES":PRINT:INPUT"WHO SHALL LEAD THE RED KINGDOM? (UP TO EIGHT LETTERS)";N$
41 SOUND220,3:INPUT"AND THE KING OF THE BLUE KINGDOMWILL BE? (TYPE IN computer IF   YOU WISH TO WAGE WAR AGAINST ME)";S$
45 IFS$="COMPUTER"THENGOSUB765:PRINT@483,"*I WELCOME THE CHALLANGE*";:GOSUB765:GOSUB765:GOSUB765
50 ER=0:WR=0:N$=LEFT$(N$,8):S$=LEFT$(S$,8)
55 GOSUB545:M5=0:M=RND(30000):M=M+150000:NM=M:SM=M
60 R=0:R1=0
65 X=RND(10000):NW=X+20000:X=RND(10000):SW=X+20000:N=RND(10000):N=N+20000:S=RND(10000):S=S+20000:CN=500:CS=500
70 NC=RND(10000):SC=RND(10000):I2=RND(5):I1=RND(5)
75 X=RND(100):IFX<25THENNS=1:GOTO80
76 NS=0
80 X=RND(100):IFX<25THENSS=1:GOTO85
81 SS=0
85 ON-(NW<1)GOTO465:ON-(N<1)GOTO550:ON-(SW<1)GOTO470:ON-(S<1)GOTO555:CLS:PRINT@2,;:IFT=0THENM$=N$:GOSUB7:GOTO87
86 PRINTN$;
87 PRINT@18,;:IFT=1THENM$=S$:GOSUB7:GOTO90
88 PRINTS$
90 Q=0
95 IFNM<1THENNM=0
100 IFSM<1THENSM=0
105 IFNW<1THENNW=0
106 IFSW<1THENSW=0
107 IFNC<1THENNC=0
110 IFSC<1THENSC=0
115 PRINT@32,"øøøøøøøøøøøøøø  ØØØØØØØØØØØØØØ ":T$="ø":PRINT@0,T$;:PRINT@11,WR;
116 PRINT@11,T$;:PRINT@13,T$;:T$="Ø":PRINT@16,T$;:PRINT@27,ER;:PRINT@27,T$;:PRINT@29,T$;
120 PRINT@96,"$"NM:PRINT@112,"$"SM
125 PRINT@128,"TROOPS:";:IFNW<=0THEN465
126 PRINTNW;:PRINT@144,"TROOPS:";:IFSW<=0THEN470
127 PRINTSW;:PRINT@160,"HIT PTS:";:IFN<=0THEN550
128 PRINTN;:PRINT@176,"HIT PTS:";:IFS<=0THEN555
129 PRINTS;
130 IFCN<=0THENCN=0
135 IFCS<=0THENCS=0
140 PRINT@192,"CANNON:"CN:PRINT@208,"CANNON:"CS:PRINT@224,"CIVIL'NS:";:IFNC<=0THENNC=0
141 PRINTLEFT$(STR$(NC),6);
145 PRINT@240,"CIVIL'NS:";:IFSC<=0THENSC=0
146 PRINTLEFT$(STR$(SC),6);
150 PRINT@256,"SPIES:";I2:PRINT@272,"SPIES:";I1:IFNS>=1THENPRINT@288,"CATAPULTS:";NS
155 IFSS>=1THENPRINT@304,"CATAPULTS:";SS
160 IFT=0THENT=1:PRINT@480,N$"'S TURN";:GOTO165
161 IFT=1THENT=0:PRINT@480,S$"'S TURN";:IFS$="COMPUTER"THENPRINT@499,"THINKING...";
162 GOTO285
165 PRINT@352,"F)IRE CAN.  A)DVANCE  C)ATAPULT H)IRE CIV.  B)UY CAN. I)NFILTRT R)ELEASE WARRIORS":SOUND180,1
170 A$=INKEY$:ON-(A$="F"ORA$="A"ORA$="C"ORA$="H"ORA$="B"ORA$="I"ORA$="R")GOTO171:GOTO170
171 CLS:GOSUB435:GOSUB475:GOSUB670:ON-(A$="F")GOTO175:ON-(A$="A")GOTO215:ON-(A$="C")GOTO225:ON-(A$="H")GOTO245
172 ON-(A$="B")GOTO265:ON-(A$="I")GOTO560:ON-(A$="R")GOTO190:STOP
175 CLS:IFCN<=0THENPRINT"NO CANNON LEFT, SIRE":CN=0:GOTO540
181 PRINT"FIRE CANNON":PRINT"AT HIS C)ASTLE OR HIS M)EN?"
185 A$=INKEY$:IFA$="C"THEN195
186 IFA$="M"THEN210
187 GOTO185
190 CLS:PRINT"RELEASING WARRIORS":PRINT:INPUT"HOW MANY TO LET GO";A:IFA>NWTHEN190
191 NW=NW-A:GOSUB775:PRINT"THEY'RE ON THEIR WAY HOME!":GOTO540
195 PRINT"CASTLE":GOSUB760:V=RND(3):IFV=1THENH=2:GOSUB9000:GOTO2000
196 H=1:GOSUB9000:D=RND(5000):S=S-D:GOSUB815:CLS:PRINT:PRINT"BOOM!!"
197 M$=S$+"'S CASTLE LOST"+STR$(D)+" HIT POINTS!":GOSUB1:D=RND(100):CS=CS-D:PRINT"AS WELL AS"D"CANNON":D=RND(99)
200 SC=SC-D:PRINT"AND";D"CIVILIANS"
201 D=RND(10):IFD<4THENIFSS>0THENPRINT"***AND HIS CATAPULT!***":SS=SS-1
205 GOTO540
210 PRINT"MEN":GOSUB760:V=RND(3):IFV=1THENPRINT"YOU HIT A FIELD!":GOTO540
211 D=RND(5000):SW=SW-D:M$=S$+" LOST"+STR$(D)+" MEN!":GOSUB1:IFD>3000THEN213
212 GOTO540
213 D=RND(3000):NW=NW-D:M$="BUT "+N$+"'S WARRIORS WERE TOO CLOSE TO "+S$+"'S TROOPS.":GOSUB1
214 PRINTN$" LOST"D"MEN.":GOTO540
215 CLS:PRINT"TROOPS ADVANCING!!":FORX=1TO1000:NEXTX:PRINT:H=11:GOSUB9000
220 D=RND(4500):SW=SW-D:PRINT:PRINT:PRINT:GOSUB815
221 M$=S$+" LOST"+STR$(D)+" MEN, AND":GOSUB1
223 D=RND(4500):NW=NW-D:M$=N$+" LOST"+STR$(D)+" OF HIS WARRIORS IN THIS BATTLE.":GOSUB1:GOTO540
225 CLS:IFNS<1THENPRINT:PRINT"YOU DON'T HAVE ONE!":GOTO540
226 PRINT"FIRE CATAPULT":PRINT"AT HIS C)ASTLE OR HIS M)EN?"
230 A$=INKEY$:IFA$="C"THENPRINT"CASTLE":GOSUB760:GOTO235
231 IFA$="M"THENPRINT"MEN":GOSUB760:GOTO240
232 GOTO230
235 X=RND(10):IFX=5THENPRINT"IT BACKFIRED!":D=RND(1000):N=N-D:M$=N$+" LOST"+STR$(D)+" HIT POINTS!":GOSUB1:GOTO540
236 X=RND(2):IFX=2THENH=6:GOSUB9000:GOTO2000
237 H=5:GOSUB9000:D=RND(11000):S=S-D:GOSUB815:CLS:M$=S$+"'S CASTLE LOST"+STR$(D)+" HIT POINTS!":GOSUB1
240 X=RND(3):IFX<3THENPRINT"NO CASUALTIES.":GOTO540
241 D=RND(11000):SW=SW-D:PRINT:M$=S$+" LOST"+STR$(D)+" OF HIS WARRIORS!":GOSUB1:GOTO540
245 CLS:PRINT"HIRING CIVILIANS":PRINT
250 IFNC<1THENPRINT"NO ONE LEFT TO RECRUIT!":GOTO540
255 INPUT"HOW MANY DO YOU NEED";A
260 IFA>NCTHENPRINT"THERE ARE ONLY"NC"!":GOTO540
261 NC=NC-A:NW=NW+A:GOSUB785:PRINT"THEY'RE HEADING FOR THE FRONT!":GOTO540
265 CLS:PRINT"BUYING CANNON":PRINT
270 GOSUB780:M$="A LOUD HORN IS BLOWN BELOW. ONE OF THE MERCHANTS ASKS HOW MANY WOULD YOU LIKE?":GOSUB1
275 X=RND(100):PRINT"CANNON COST $";X"EACH"
280 INPUTA:A1=A:A1=A1*X:IFA1>NMTHENPRINT"YOU DON'T HAVE ENOUGH GOLD SIRE!":GOTO540
281 M$="THE CANNONSMITH IS LOADING YOUR WAGONS!":GOSUB1:NM=NM-A1:CN=CN+A:GOTO540
285 PRINT@352,"F)IRE CAN.  A)DVANCE  C)ATAPULT H)IRE CIV.  B)UY CAN. I)NFILTRT R)ELEASE WARRIORS":SOUND220,1
290 IFS$="COMPUTER"THENGOSUB816:GOSUB505:GOTO300
291 A$=INKEY$:ON-(A$="F"ORA$="A"ORA$="C"ORA$="H"ORA$="B"ORA$="I"ORA$="R")GOTO292:GOTO291
292 CLS:GOSUB505:GOSUB670:ON-(A$="F")GOTO320:ON-(A$="A")GOTO365:ON-(A$="C")GOTO375:ON-(A$="H")GOTO395
293 ON-(A$="B")GOTO415:ON-(A$="I")GOTO580:ON-(A$="R")GOTO295:STOP
295 IFS$="COMPUTER"THEN290
296 CLS:PRINT"RELEASING WARRIORS":PRINT:INPUT"HOW MANY ARE LEAVING";A:IFA>SWTHEN295
297 SW=SW-A:GOSUB775:PRINT"THEY'RE GLAD TO GO HOME!":GOTO540
300 ON-(NS>=1ANDSW<11000ANDSC>=1)GOTO310:ON-(SW<8000ANDSC>=1)GOTO310:ON-(CS<1ANDSM>4999)GOTO315
301 ON-(NS>=1ANDSW<11000ANDSM>9999ANDI1>=1)GOTO580:ON-(SW<8000ANDSM>9999ANDI1>=1)GOTO580
302 ON-(NS>=1ANDS<11000ANDSM>9999ANDI1>=1)GOTO580
305 ON-(S<8000ANDSM>9999ANDI1>=1)GOTO580:ON-(NW<3000ANDSW>6000)GOTO365:ON-(SS>=1)GOTO375:ON-(CS>=1)GOTO320:GOTO365
310 IFSW<11000THEN312
311 GOTO315
312 D=11000:X=D-SW:IFSC>XTHENCLS:PRINT"HIRING"X"CIVILIANS":SW=11000:SC=SC-X:GOSUB785:GOTO540
313 IFSW<8000ANDSC>50THENCLS:PRINT"HIRING"SC"CIVILIANS":GOSUB785:SW=SW+SC:SC=0:GOTO540
315 IFCS<1ANDSM>5000THEND=RND(50):A1=D*100:CS=CS+100:SM=SM-A1:CLS:PRINT"BUYING 100 CANNON AT $"D"EACH.":GOSUB780:GOTO540
320 CLS:IFCS<=0THENPRINT"YOU HAVE NONE LEFT":GOTO540
326 PRINT"FIRE CANNON":PRINT"AT HIS M)EN OR HIS C)ASTLE?"
330 IFS$="COMPUTER"THEN335
331 A$=INKEY$:IFA$=""THEN330
332 IFA$="C"THEN340
333 IFA$="M"THEN355
335 IFN<NWTHEN340
336 GOTO355
340 PRINT"CASTLE":GOSUB760:V=RND(3):IFV=1THENH=4:GOSUB9000:GOTO2000
341 H=3:GOSUB9000:D=RND(5000):N=N-D:GOSUB815:CLS:PRINT:PRINT"BOOM!"
342 M$=N$+"'S CASTLE LOST"+STR$(D)+" HIT POINTS!":GOSUB1:D=RND(100):CN=CN-D:PRINT"AS WELL AS"D"CANNON":D=RND(99)
345 NC=NC-D:PRINT"AND";D"CIVILIANS"
346 D=RND(10):IFD<4THENIFNS>0THENPRINT"***AND HIS CATAPULT!***":NS=NS-1
347 GOTO540
355 PRINT"MEN":GOSUB760:V=RND(3):IFV=1THENPRINT"YOU HIT A FIELD!":GOTO540
356 D=RND(5000):NW=NW-D:PRINTN$" LOST"D"MEN!":IFD>3000THEN358
357 GOTO540
358 D=RND(3000):SW=SW-D:M$="BUT "+S$+"'S WARRIORS WERE TOO CLOSE TO "+N$+"'S TROOPS.":GOSUB1
360 PRINTS$" LOST"D"MEN.":GOTO540
365 CLS:PRINT"TROOPS ADVANCING":FORX=1TO1000:NEXTX:PRINT:H=11:GOSUB9000
370 D=RND(4500):NW=NW-D:PRINT:PRINT:PRINT:GOSUB815:PRINTN$" LOST"D"MEN, AND":D=RND(4500):SW=SW-D
371 M$=S$+" LOST"+STR$(D)+" OF HIS TROOPS IN THIS BATTLE.":GOSUB1:GOTO540
375 IFS$="COMPUTER"ANDSS<1THEN290
376 CLS:IFSS<1THENPRINT:PRINT"YOU DON'T HAVE ONE!":GOTO540
377 PRINT"FIRE CATAPULT":PRINT"AT HIS C)ASTLE OR HIS M)EN?"
380 IFS$="COMPUTER"ANDN<NWTHENPRINT"CASTLE":GOSUB760:GOTO385
381 IFS$="COMPUTER"THENPRINT"MEN":GOSUB760:GOTO390
382 A$=INKEY$:IFA$="C"THENPRINT"CASTLE":GOSUB760:GOTO385
383 IFA$="M"THENPRINT"MEN":GOSUB760:GOTO390
384 GOTO380
385 X=RND(10):IFX=5THENPRINT"IT BACKFIRED!":D=RND(1000):S=S-D:M$=S$+" LOST"+STR$(D)+" HIT POINTS!":GOSUB1:GOTO540
386 X=RND(2):IFX=2THENH=8:GOSUB9000:GOTO2000
387 H=7:GOSUB9000:D=RND(11000):N=N-D:GOSUB815:CLS:M$=N$+"'S CASTLE LOST"+STR$(D)+" HIT POINTS!":GOSUB1
390 X=RND(3):IFX<3THENPRINT"NO CASUALTIES.":GOTO540
391 D=RND(11000):NW=NW-D:M$=N$+" LOST"+STR$(D)+" OF HIS WARRIORS!":GOSUB1:GOTO540
395 CLS:PRINT"HIRING CIVILIANS...":PRINT
400 IFSC<=0THENPRINT"NO ONE LEFT, SIRE.":GOTO540
405 INPUT"HOW MANY DO YOU WANT";A
410 IFA>SCTHENPRINT"YOU ONLY HAVE"SC"CIVILIANS!":GOTO540
411 SC=SC-A:SW=SW+A:GOSUB785:PRINT"THEY'RE DONNING THEIR ARMOUR!":GOTO540
415 PRINT"BUYING CANNON":PRINT
420 GOSUB780:M$="A TRUMPET'S BLARE COMES UP FROM THE VILLAGE. THE CANNONSMITH WANTS TO KNOW HOW MANY?":GOSUB1
425 X=RND(100):PRINT"CANNON COST $"X"EACH."
430 INPUTA:A1=A:A1=A1*X:IFA1>SMTHENPRINT"NOT ENOUGH CASH ON HAND!":GOTO540
431 M$="THEY'VE BEEN ADDED TO YOUR ARSENAL!":GOSUB1:SM=SM-A1:CS=CS+A:GOTO540
435 M5=M5+2:IFM5=12THEN437
436 RETURN
437 CLS:PRINT"PAYDAY!":GOSUB795:M$="$2 PER WARRIOR IS BEING DEDUCTED.":GOSUB1:PRINT
438 M5=0:P=NW*2:P1=SW*2:NM=NM-P:SM=SM-P1:ON-(NM<=0ANDSM<=0)GOTO450:ON-(NM<=0)GOTO445:ON-(SM<=0)GOTO440:GOTO535
440 X=RND(2):IFX=1THEND=RND(SW):SW=SW-D:GOSUB3:M$=M$+"OF "+S$+"'S MEN HAVE DIED OF STARVATION":GOSUB1:GOTO535
441 D=RND(SW):SW=SW-D:NW=NW+D:GOSUB3:M$=M$+"OF "+S$+"'S MEN JOINED "+N$+"'S ARMY!":GOSUB1:GOTO535
445 X=RND(2):IFX=1THEND=RND(NW):NW=NW-D:GOSUB3:M$=M$+"OF "+N$+"'S MEN JUST PERISHED FROM HUNGER.":GOSUB1:GOTO535
446 D=RND(NW):NW=NW-D:SW=SW+D:GOSUB3:M$=M$+"OF "+N$+"'S MEN SWITCHED SIDES- "+N$+" CAN'T PAY THEM!":GOSUB1:GOTO535
450 X=RND(2):IFX=1THEND=RND(NW):NW=NW-D:GOSUB3:M$=M$+"OF "+N$+"'S MEN DIED OF STARVATION- HE CAN'T PAY THEM!":GOSUB1:GOTO455
451 M$=N$+" CAN'T PAY HIS MEN, BUT THEY REMAIN LOYAL!":GOSUB1:GOTO455
455 X=RND(2):IFX=1THEND=RND(SW):SW=SW-D:GOSUB3:M$=M$+"OF "+S$+"'S MEN DIED OF STARVATION- HE CAN'T PAY THEM!":GOSUB1:GOTO535
456 M$=S$+" CAN'T PAY HIS MEN, BUT THEY REMAIN LOYAL!":GOSUB1:GOTO535
460 GOTO535
465 CLS:PRINT"SORRY, "N$".":GOSUB770:PRINT
466 M$="YOUR WARRIORS HAVE ALL BEEN KILLED. YOUR CASTLE HAS BEEN TAKEN OVER BY "+S$+"'S TROOPS.":GOSUB1:Y=2:GOTO615
470 CLS:PRINT"SORRY, "S$".":GOSUB770:PRINT
471 M$="ALL OF YOUR WARRIORS ARE DEAD. YOUR CASTLE HAS BEEN OCCUPIED BY "+N$+"'S FORCES.":GOSUB1:Y=1:GOTO615
475 X=RND(100):IFX>15THENRETURN
476 R=R+1:IFR>7THENRETURN
477 CLS:PRINT@12,"**AHOY**":GOSUB790:PRINT:M$=N$+"'S FATHER JUST BROUGHT-":GOSUB1:P=RND(7)
480 IFP=1THENX=RND(100):CN=CN+X:PRINTX;C$:GOTO535
481 IFP=2THENX=RND(10000):NW=NW+X:PRINTX;W$:GOTO535
482 IFP=3THENNS=NS+1:PRINT" A";D$:GOTO535
483 IFP=4THENX=RND(20000):N=N+X:PRINT" A CASTLE REPAIR CREW-":PRINT" HE NOW HAS";STR$(N);H$:GOTO535
485 IFP=5THENX=RND(30000):NM=NM+X:PRINT" MONEY $";X:GOTO535
486 IFP=6THENX=RND(10000):NC=NC+X:PRINTX;E$:GOTO535
490 C5=RND(500):W5=RND(30000):NS=NS+1:H5=RND(30000):M8=RND(30000):E5=RND(10000):IFNM<1THENNM=0
491 CN=CN+C5:NW=NW+W5:N=N+H5:NM=NM+M8:NC=NC+E5:PRINTC5;C$:PRINTW5;W$:PRINT" A";D$
495 PRINT" A CASTLE REPAIR CREW WHICH":PRINT" RESTORES";STR$(H5);H$
496 PRINT" MONEY $";M8:PRINTE5;E$:X=RND(5):PRINT" AND";X;"SPIES!":I=I+X
500 GOTO535
505 X=RND(100):IFX>15THENRETURN
506 IFS$="COMPUTER"ANDQ>=1THEN300
507 R1=R1+1:IFR1>7THENRETURN
508 CLS:PRINT@12,"**AHOY**":GOSUB790:PRINT:M$=S$+"'S FATHER JUST BROUGHT-":GOSUB1:Q=Q+1:P=RND(7)
510 IFP=1THENX=RND(100):CS=CS+X:PRINTX;C$:GOTO535
511 IFP=2THENX=RND(10000):SW=SW+X:PRINTX;W$:GOTO535
512 IFP=3THENSS=SS+1:PRINT" ONE";D$:GOTO535
513 IFP=4THENX=RND(20000):S=S+X:PRINT" A CASTLE REPAIR CREW-":PRINT" IT NOW HAS";STR$(S);H$:GOTO535
515 IFP=5THENX=RND(30000):SM=SM+X:PRINT" MONEY $";X:GOTO535
516 IFP=6THENX=RND(10000):SC=SC+X:PRINTX;E$:GOTO535
520 C5=RND(500):W5=RND(30000):SS=SS+1:H5=RND(30000):M8=RND(30000):E5=RND(10000):IFSM<1THENSM=0
521 CS=CS+C5:SW=SW+W5:S=S+H5:SM=SM+M8:SC=SC+E5:PRINTC5;C$:PRINTW5;W$:PRINT" A";D$
525 PRINT" A CASTLE REPAIR CREW WHICH":PRINT" RESTORES";STR$(H5);H$
526 PRINT" MONEY $";M8:PRINTE5;E$:X=RND(5):PRINT" AND";X;"SPIES!":I1=I1+X
535 PRINT:PRINT:INPUT"PRESS <ENTER>...";A:RETURN
540 GOSUB535:GOTO85
545 C$="CANNON":W$="WARRIORS":D$=" CATAPULT":H$=" HIT POINTS":E$="CIVILIANS":RETURN
550 CLS:PRINT"SORRY, "N$".":GOSUB770:PRINT:M$="YOUR CASTLE HAS BEEN LEVELED BY KING "+S$+". YOU'VE LOST!":GOSUB1:Y=2:GOTO615
555 CLS:PRINT"SORRY, "S$".":GOSUB770:PRINT:M$="YOUR CASTLE HAS BEEN LEVELED BY KING "+N$+". YOU'VE LOST!":GOSUB1:Y=1:GOTO615
560 CLS:PRINT"INFILTRATING":PRINT:GOSUB765:GOSUB765:GOSUB765
565 IFNM<10000THENPRINT"YOU DON'T HAVE ENOUGH MONEY!":GOTO540
570 IFI2<=0THENPRINT"THEY'RE DEAD!":GOTO540
571 X4=RND(5):IFX4=1THEN570
572 I2=I2-1:H=9:GOSUB9000:PRINT:PRINT"HE HAS TO FIGHT"X4"GUARDS.":NM=NM-10000:FORX2=1TOX4:X3=RND(2)
575 IFX3<>1THENGOSUB805:SOUND1,30:PRINT"THE GUARDS KILLED HIM!":GOTO540
576 GOSUB805:GOSUB765:PRINT"ONE DOWN!":NEXTX2:GOSUB805:GOSUB755:PRINT:PRINT"HE MADE IT!!":GOSUB810:GOTO600
580 CLS:PRINT"INFILTRATING":PRINT:GOSUB765:GOSUB765
585 IFSM<10000THENPRINT"NOT ENOUGH CASH ON HAND!":GOTO540
590 IFI1<=0THENPRINT"THEY'RE DEAD!":GOTO540
591 X4=RND(5):IFX4=1THEN590
592 I1=I1-1:H=10:GOSUB9000:PRINT:PRINT"HE HAS TO FIGHT"X4"GUARDS.":SM=SM-10000:FORX2=1TOX4:X3=RND(2)
595 IFX3<>1THENGOSUB805:SOUND1,30:PRINT"THE GUARDS KILLED HIM!":GOTO540
596 GOSUB805:GOSUB810:PRINT"ONE DOWN!":NEXTX2:GOSUB805:GOSUB755:PRINT:PRINT"HE MADE IT!!":GOSUB810:GOTO605
600 CLS:PRINT@160,;:GOSUB611:PRINT"     ";N$"'S INFILTRATOR"
601 PRINTTAB(7)"KILLED KING "S$"!!":PRINT:GOSUB611:GOSUB770:Y=1:GOTO615
605 CLS:PRINT@160,;:GOSUB611:PRINT"     ";S$"'S INFILTRATOR"
606 PRINTTAB(7)"KILLED KING "N$"!!":PRINT:GOSUB611:GOSUB770:Y=2:GOTO615
610 RETURN
611 PRINT"********************************":RETURN
615 GOSUB815:GOSUB815
620 CLS
625 GOSUB740:M$=N$+"'S RANK IS NOW: "+R$(WR):GOSUB1
630 IFWR<5THENSOUNDWR*10+50,10:GOTO635
631 GOSUB765:GOTO660
635 M$=S$+"'S RANK IS NOW: "+R$(ER):GOSUB1
640 IFER<5THENSOUNDER*10+50,10:GOTO650
645 GOSUB765:GOTO665
650 PRINT:PRINT"WANT ANOTHER GAME?":INPUT"IT CAN BE ANY TWO PLAYERS...";A$:IFA$=""THEN650
651 IFA$="Y"THEN655
652 END
655 PRINT:M$="PRESS enter TO KEEP THE SAME PLAYERS AND RANKS. IF YOU'D LIKE TO BEGIN AGAIN "
656 M$=M$+"WITH NEW BATTLERS, SIMPLY TYPE THE WORD new...":GOSUB1:INPUTA$:IFA$="NEW"THEN0
657 GOTO55
660 PRINT:M$=N$+"'S RANK ENDS AT RANK"+STR$(WR)+".":GOSUB1:PRINT:M$=N$+", HOWEVER, HAS RISEN FAR "
661 M$=M$+"ABOVE KINGMANSHIP AND HAS BEEN GIVEN COMMAND OF THE ENTIRE COUNTRY!!":GOSUB1:GOSUB765:GOSUB765:END
665 PRINT:M$=S$+"'S RANK ENDS AT RANK"+STR$(ER)+".":GOSUB1:PRINT:M$=S$+", HOWEVER, HAS RISEN FAR "
666 M$=M$+"ABOVE KINGMANSHIP AND HAS BEEN GIVEN CONTROL OF THE ENTIRE COUNTRY!!":GOSUB1:GOSUB765:GOSUB765:END
670 IFNW<10000ORN<10000ORSW<10000ORS<10000THENRETURN
671 X=RND(1000):IFX>=500ANDX<=515THEN675
672 X=RND(1000):IFX>=500ANDX<=515THEN700
673 RETURN
675 CLS:FORL=1TO5:SOUND1,5:GOSUB4:SOUND10,5:GOSUB4:NEXT:PRINT"TORRENTIAL RAINS CAUSE FLOODING!"
680 GOSUB815:D=RND(10000):NW=NW-D:PRINTN$" LOST-":PRINTD"MEN":D=RND(10000):N=N-D
681 PRINTD"HIT POINTS":D=RND(250):CN=CN-D:PRINTD"CANNON"
685 D=RND(1000):NC=NC-D:PRINTD"CIVILIANS"
690 GOSUB810:PRINTS$" LOST-":D=RND(10000):SW=SW-D:PRINTD"MEN":D=RND(10000):S=S-D
691 PRINTD"HIT POINTS":D=RND(250):CS=CS-D:PRINTD"CANNON"
695 D=RND(1000):SC=SC-D:PRINTD"CIVILIANS":GOTO535:RETURN
700 GOSUB810:CLS:SOUND50,3:GOSUB4:SOUND50,3:GOSUB4:SOUND50,3:SOUND10,20
701 M$="DRUDGERY! THE FOOD HAS SPOILED! THERE IS A PLAGUE!":GOSUB1:PRINT:GOSUB810
705 PRINTN$" LOST-":D=RND(10000):NW=NW-D:PRINTD"MEN":D=RND(10000):N=N-D:PRINTD"HIT POINTS":D=RND(1000)
706 NC=NC-D:IFNC<1THENNC=0
710 PRINTD"CIVILIANS":GOSUB810
715 PRINTS$" LOST-":D=RND(10000):SW=SW-D:PRINTD"MEN":D=RND(10000):S=S-D:PRINTD"HIT POINTS":D=RND(1000)
716 SC=SC-D:IFSC<1THENSC=0
717 PRINTD"CIVILIANS":GOTO535
740 IFY=1THENWR=WR+1:IFWR>5THENWR=5
745 IFY=2THENER=ER+1:IFER>5THENWR=5
746 RETURN
755 FORL=55TO250STEP2:SOUNDL,1:NEXT:RETURN
760 FORL=30TO1STEP-2:SOUNDL,1:NEXT:SOUND1,10:RETURN
765 FORL=100TO150STEP5:SOUNDL,1:NEXT:RETURN
770 SOUND1,10:GOSUB4:SOUND1,5:SOUND70,20:GOSUB4:GOSUB4:GOSUB4:SOUND1,10:SOUND70,5:SOUND110,15:FORL=110TO1STEP-11
771 SOUNDL,3:NEXT:SOUND1,30:RETURN
775 SOUND100,5:SOUND130,2:GOSUB4:SOUND130,9:SOUND151,5
776 GOSUB4:GOSUB4:SOUND151,5:SOUND179,2:GOSUB4:SOUND179,30:RETURN
780 SOUND100,10:SOUND130,5:SOUND150,10:GOSUB4:GOSUB4:SOUND150,2:SOUND179,30:RETURN
785 SOUND1,5:SOUND70,3:GOSUB4:GOSUB4:SOUND70,3:SOUND110,5:SOUND1,2:SOUND70,30:RETURN
790 SOUND118,5:SOUND154,10:SOUND118,10:FORX=1TO150:NEXTX:SOUND134,4:SOUND154,4:SOUND144,4:SOUND134,4
791 SOUND154,4:SOUND144,5:SOUND134,5:FORX=1TO45:NEXTX:SOUND71,10:SOUND99,25:RETURN
795 SOUND125,4:SOUND149,9:SOUND125,2:SOUND136,5:SOUND149,25:RETURN
800 SOUND25,10:SOUND90,10:SOUND25,6:GOSUB805:SOUND25,3:SOUND90,15:SOUND25,5:RETURN
805 FORX=1TO300:NEXTX:RETURN
810 FORX=1TO750:NEXTX:RETURN
815 FORX=1TO2500:NEXTX:RETURN
816 FORX=1TO3000:NEXTX:RETURN
2000 FORX=1TO1500:NEXTX:GOTO85
9000 PRINT@320,;:PRINT"èèèøÖèèèèèèèèèèèèèèèèèèèèØÖèèè"
9002 PRINT"éééèÖééÄÉèèèèèèèèèèèèãÇÑéèÖééé":PRINT"èÄÖèÖèÄÖèèèèèèèèèèèèèèèÄÖèÖèÄÖ":PRINT"èäÄÄÄÄÄèèèèèèèèèèèèèèèèäÄÄÄÄÄ   ";
9004 PRINT"ˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇ";:POKE16384+511,255
9005 ONHGOSUB9060,9080,9090,9120,9200,9210,9220,9230,9270,9280,9290:PRINT@480,;:H=0:RETURN
9060 Y=22.5:PRINT@361,CHR$(159);:FORX=1TO25:NEXT:PRINT@361,"è";
9061 FORX=39/2TO125/2:MM=RND(7)*.02:Y=Y+MM:RESET(X,Y):RESET(X+1,Y):IFPOINT(X+2,Y)<>1THEN9063
9062 FORQQ=1TO5:NEXTQQ:SET(X,Y,1):SET(X+1,Y,1):NEXTX
9063 PRINT@406,CHR$(159);:FORYY=1TO25:NEXTYY
9064 PRINT@406,"è";:FORY=160TO128STEP-1
9065 PRINT@407,CHR$(Y);:NEXTY:PRINT@407,"â";:RETURN
9080 Y=22.5:PRINT@361,CHR$(159);:FORX=1TO25:NEXTX:PRINT@361,"è";
9081 FORX=39/2TO100/2:MM=5:Y=Y+MM*.0355:RESET(X,Y):RESET(X+1,Y)
9082 FORQQ=1TO8:NEXTQQ:SET(X,Y,1):SET(X+1,Y,1):IFPOINT(X,Y+1)<>1THENX=100
9084 NEXTX:GOTO9124
9090 Y=22.5:PRINT@372,CHR$(159);:FORX=1TO25:NEXTX:PRINT@372,"è";
9091 FORX=85/2TO33/2STEP-1:MM=RND(7)*.02:Y=Y+MM:RESET(X,Y):RESET(X-1,Y):IFPOINT(X-2,Y)<>1THEN9093
9092 FORQQ=1TO5:NEXTQQ:SET(X,Y,1):SET(X-1,Y,1):NEXTX
9093 PRINT@392,CHR$(159);:FORYY=1TO25:NEXTYY
9094 PRINT@392,"è";:FORY=160TO128STEP-1
9095 PRINT@391,CHR$(Y);:NEXTY:PRINT@391,"â";:RETURN
9120 Y=22.5:PRINT@372,CHR$(159);:FORX=1TO25:NEXT:PRINT@372,"è";
9121 FORX=42.5TO18STEP-1:MM=5:Y=Y+MM*.037:RESET(X,Y):RESET(X-1,Y)
9122 FORQQ=1TO8:NEXTQQ:SET(X,Y,1):SET(X-1,Y,1):IFPOINT(X,Y+1)<>1THENX=0
9123 NEXTX
9124 PRINT@268,"MISSED!!":RETURN
9200 A=353:FORX=1TO10:A=A-31:PRINT@A,"å";:FORU=1TO50:NEXT:PRINT@A," ";:NEXTX
9201 FORX=1TO3:A=A+1:PRINT@A,"å";:FORU=1TO100:NEXT:PRINT@A," ";:NEXTX
9202 FORX=1TO12:A=A+33:PRINT@A,"å";:FORU=1TO70:NEXT:PRINT@A," ";:NEXTX
9205 PRINT@A,"*";:PRINT@A-33,"***";:PRINT@A-66,"*****";:PRINT@A-99,"*******";:FORX=1TO100:NEXT
9206 PRINT@A," ";:PRINT@A-33," è ";:PRINT@A-66,"     ";:PRINT@A-99,"       ";:RETURN
9210 A=353:FORX=1TO10:A=A-31:PRINT@A,"å";:FORU=1TO50:NEXT:PRINT@A," ";:NEXTX
9211 FORX=1TO3:A=A+1:PRINT@A,"å";:FORU=1TO100:NEXT:PRINT@A," ";:NEXTX
9212 FORX=1TO8:A=A+34:PRINT@A,"å";:FORU=1TO70:NEXT:PRINT@A," ";:NEXTX
9215 PRINT@A," ";:PRINT@268,"MISSED!!":RETURN
9220 A=381:FORX=1TO10:A=A-33:PRINT@A,"å";:FORU=1TO50:NEXT:PRINT@A," ";:NEXTX
9221 FORX=1TO3:A=A-1:PRINT@A,"å";:FORU=1TO100:NEXT:PRINT@A," ";:NEXTX
9222 FORX=1TO12:A=A+31:PRINT@A,"å";:FORU=1TO70:NEXT:PRINT@A," ";:NEXTX
9225 GOTO9205
9230 A=381:FORX=1TO10:A=A-33:PRINT@A,"å";:FORU=1TO50:NEXT:PRINT@A," ";:NEXTX
9231 FORX=1TO3:A=A-1:PRINT@A,"å";:FORU=1TO100:NEXT:PRINT@A," ";:NEXTX
9232 FORX=1TO8:A=A+30:PRINT@A,"å";:FORU=1TO70:NEXT:PRINT@A," ";:NEXTX
9235 GOTO9215
9270 Y=27:FORX=15TO46:RESET(X,Y):FORYY=1TO25:NEXTYY:SET(X,Y,1):NEXTX:RETURN
9280 Y=27:FORX=46TO15STEP-1:RESET(X,Y):FORYY=1TO25:NEXTYY:SET(X,Y,1):NEXTX:RETURN
9290 PRINT@268,"CHARGE!!":Y=27:X=16:X1=44:FORXX=1TO13:X=X+1:X1=X1-1
9291 RESET(X,Y):RESET(X1,Y):FORYY=1TO25:NEXTYY:SET(X-3,Y,1):SET(X1+3,Y,1):NEXTXX:RESET(34,27)
9292 FORXX=143TO128STEP-1:PRINT@431,CHR$(XX);:FORYY=1TO25:NEXTYY:NEXTXX
9293 FORXX=191TO176STEP-1:PRINT@431,CHR$(XX);:FORYY=1TO25:NEXTYY:NEXTXX:PRINT@431,"π";:RETURN
9500 CLS:PRINT@43,"K I N G S":PRINT@109,"A N D":PRINT@169,"C A S T L E S"
9501 PRINT@257," A FANTASY ROLE-PLAYING ADVEN-"
9502 PRINT"TURE GAME WHICH PITS TWO KINGS  AND THEIR KINGDOMS AGAINST ONE  ANOTHER."
9503 PRINT@420,"*BY GARY SMITH MICHAELS*":PRINT"  EDITS BY T.CONNOR & J.GERRIE";
9510 R$(0)="(0) WATERBOY FOR A SHEPHERD":R$(1)="(1) CUSTER'S STRATEGIC ADVISOR":R$(2)="(2) JERRY LEWIS IN A WAR MOVIE"
9520 R$(3)="(3) BONAPARTE AT WATERLOO":R$(4)="(4) WASHINGTON AT VALLEY FORGE"
9530 R$(5)="(5) LOUIS XIV, THE BLOODLORD"
9540 GOSUB815:GOSUB815:CLS:INPUT"DO YOU WANT AN INSTRUCTIONS";A$:IFA$="Y"THEN10
9550 GOTO40
9600 REM FROM KINGS & CATAPULTS
9610 REM BY WILLIAM C. ADAMS
