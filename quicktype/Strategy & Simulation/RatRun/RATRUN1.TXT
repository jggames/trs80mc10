0 CLS
6 REM RATRUN COPYRIGHT (C) 1981 THE CODE WORKS
10 REM AS OF 10/8/81 RJ
90 PG$="CURSOR # 13  RATRUN":NM$="13":GOSUB 62000
100 H=12:V=7:H1=H+1:W=0:V1=0
110 REM RANDOMIZE I
130 DIM XL(4),N2(4),YL(4),XR(4),W(128),V1(128)
140 FOR I=0 TO 4:READ XL(I),N2(I),YL(I),XR(I):XR(I)=XR(I):NEXT I
150 DATA 8,20,-4,31,8,12,1,27,12,6,5,24,15,2,8,22,17,0,10,21
200 CX=INT(RND(0)*H)+1:CY=INT(RND(0)*V)+1
210 C=CX:R=CY:S=0
220 FOR I=0 TO (V+1)*H1:W(W+I)=0:V1(V1+I)=0:NEXT
230 PRINT@0,"SETTING UP MAZE...X";
300 DA=0:N=0:AV=V1+C+R*H1:AW=W+C+R*H1:W(AW)=1:S=S+1:IF S>=H*V THEN 900
320 PRINT CHR$(8);MID$("xX",(S AND 1)+1,1);
410 IF C>1 AND W(AW-1)=0 THEN DA=DA+2:N=N+1
420 IF C<H AND W(AW+1)=0 THEN DA=DA+1:N=N+1
430 IF R>1 AND W(AW-H1)=0 THEN DA=DA+8:N=N+1
440 IF (R<V AND W(AW+H1)=0) THEN DA=DA+4:N=N+1
450 N=INT(RND(0)*N)+1
460 ON DA+1 GOTO 500,850,800,600,750,610,620,630,700,640,650,660,670,680,690,695
500 S=S-1
510 C=C+1:IF C>H THEN C=1:R=R+1:IF R>V THEN R=1
520 ON W(W+C+R*H1)+1 GOTO 510,300
600 ON N GOTO 800,850
610 ON N GOTO 750,850
620 ON N GOTO 750,800
630 ON N GOTO 750,800,850
640 ON N GOTO 700,850
650 ON N GOTO 700,800
660 ON N GOTO 700,800,850
670 ON N GOTO 700,750
680 ON N GOTO 700,750,850
690 ON N GOTO 700,750,800
695 ON N GOTO 700,750,800,850
700 R=R-1:AV=AV-H1:V1(AV)=V1(AV) OR 1:GOTO 300
750 V1(AV)=V1(AV) OR 1:R=R+1:AV=AV+H1:GOTO 300
800 C=C-1:AV=AV-1:V1(AV)=V1(AV) OR 2:GOTO 300
850 V1(AV)=V1(AV) OR 2:C=C+1:AV=AV+1:GOTO 300
900 I=RND(0)>.5:J=RND(0):K=RND(0)>.5
920 IF I THEN X=INT(J*H)+1:Y=V+(V-1)*K
930 IF I=0 THEN Y=INT(J*V)+1:X=H+(H-1)*K
931 IF (CX-X)^2+(CY-Y)^2<(H^2+V^2)/9 THEN 900
950 SX=X:SY=Y
1000 CLS0:C=1
1010 FOR I=W TO W+(V+1)*H1:W(I)=0:NEXT
1020 D=INT(RND(0)*4)+1
1030 T4=TI:W(W+X+Y*H1)=1
1031 REM GOSUB2000
1032 REM GOTO1032
1045 GOSUB 3000:PRINT@480,INT(SQR((X-CX)^2+(Y-CY)^2)*10+.5)/10;:PRINT@480+16,D;
1049 IF CX=X AND CY=Y THEN 6000
1050 Q$=INKEY$:IF Q$=""THEN 1050
1060 IF Q$="?" THEN K=TI:GOTO 6100
1070 IF Q$="Z" THEN D=D+2
1080 IF Q$="A" THEN D=D-1
1090 IF Q$="S" THEN D=D+1
1100 IF D<1 THEN D=D+4
1110 IF D>4 THEN D=D-4
1120 IF Q$<>"W" THEN 1045
1200 AV=V1+X+Y*H1:AW=W+X+Y*H1
1201 ON D GOTO 1260,1310,1360
1210 IF Y>1 AND (V1(AV-H1) AND 1) THEN Y=Y-1:W(AW-H1)=1:GOTO 1045
1220 GOTO 1400
1260 IF X<H AND (V1(AV) AND 2) THEN X=X+1:W(AW+1)=1:GOTO 1045
1290 GOTO 1400
1310 IF Y<V AND (V1(AV) AND 1) THEN Y=Y+1:W(AW+H1)=1:GOTO 1045
1340 GOTO 1400
1360 IF X>1 AND (V1(AV-1) AND 2) THEN X=X-1:W(AW-1)=1:GOTO 1045
1400 DN=23:GOSUB 7000
1410 PRINT@480,"NO DOOR";:SOUND1,1:FOR I=1 TO 2500:NEXT
1420 PRINT@480,"€€€€€€€";:GOTO 1049
2000 CLS0:C=1:SET(0,0,C):A=1:B=0
2020 FOR I=1 TO H:SET(A,B,C):SET(A+1,B,C):SET(A+2,B,C):A=A+3:NEXT:B=B+3:A=0
2100 FOR J=1 TO V:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B+2
2101 FOR I=1 TO H
2125 K=V1(V1+I+J*H1)
2130 SET(A,B,C):SET(A+1,B,C):SET(A+2,B,C):IFK=1ORK=3THENRESET(A,B):RESET(A+1,B)
2135 A=A+3
2140 IF K<2 THEN B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B+2
2200 B=B-1:A=A-1
2210 IF W(W+I+J*H1)=1 THEN SET(A-1,B,0)
2250 IF I=SX AND J=SY THEN SET(A-1,B,6)
2260 IF I=CX AND J=CY THEN SET(A-1,B,2)
2270 IF I=X AND J=Y THEN SET(A-1,B,4)
2280 A=A+1:B=B+1
2290 NEXT:B=B+3:A=0:NEXT:RETURN
3000 S=0:CLS0:A=0:B=0
3005 AV=V1+X+Y*H1
3010 ON D GOTO 3200,3400,3600
3025 IF SY=Y-S AND SX=X THEN GOSUB 5100
3026 IF CY=Y-S AND CX=X THEN GOSUB 5000
3027 K=V1(AV-(S+1)*H1):IF K=0 OR K=2 THEN 3100
3030 IF V1(AV-S*H1) AND 2 THEN GOSUB 4000:GOTO 3040
3035 GOSUB 4200
3040 IF V1(AV-1-S*H1) AND 2 THEN GOSUB 4500:GOTO 3050
3045 GOSUB 4700
3050 S=S+1:IF S>4 THEN 3900
3060 GOTO 3025
3100 IF V1(AV-S*H1) AND 2 THEN GOSUB 4300:GOTO 3110
3105 GOSUB 4800
3110 IF V1(AV-1-S*H1) AND 2 THEN GOSUB 4400:GOTO 3120
3115 GOSUB 4900
3120 RETURN
3200 IF CY=Y AND CX=X+S THEN GOSUB 5000
3210 IF SY=Y AND SX=X+S THEN GOSUB 5100
3226 K=V1(AV+S):IF K=0 OR K=1 THEN 3300
3230 IF V1(AV+S)=2 THEN GOSUB 4200:GOTO 3240
3235 GOSUB 4000
3240 IF V1(AV+S-H1) AND 1 THEN GOSUB 4500:GOTO 3250
3245 GOSUB 4700
3250 S=S+1:IF S>4 THEN 3900
3260 GOTO 3200
3300 IF V1(AV+S)=0 THEN GOSUB 4800:GOTO 3310
3305 GOSUB 4300
3310 IF V1(AV+S-H1) AND 1 THEN GOSUB 4400:GOTO 3320
3315 GOSUB 4900
3320 RETURN
3400 IF CY=Y+S AND CX=X THEN GOSUB 5000
3410 IF SY=Y+S AND SX=X THEN GOSUB 5100
3426 K=V1(AV+S*H1):IF K=0 OR K=2 THEN 3500
3430 IF V1(AV-1+S*H1) AND 2 THEN GOSUB 4000:GOTO 3440
3435 GOSUB 4200
3440 IF V1(AV+S*H1)=1 THEN GOSUB 4700:GOTO 3450
3445 GOSUB 4500
3450 S=S+1:IF S>4 THEN 3900
3460 GOTO 3400
3500 IF V1(AV-1+S*H1) AND 2 THEN GOSUB 4300:GOTO 3510
3505 GOSUB 4800
3510 IF V1(AV+S*H1)=0 THEN GOSUB 4900:GOTO 3520
3515 GOSUB 4400
3520 RETURN
3600 IF CX=X-S AND CY=Y THEN GOSUB 5000
3610 IF SX=X-S AND SY=Y THEN GOSUB 5100
3625 K=V1(AV-(S+1)):IF K=0 OR K=1 THEN 3700
3630 IF V1(AV-S-H1) AND 1 THEN GOSUB 4000:GOTO 3640
3635 GOSUB 4200
3640 IF V1(AV-S) AND 1 THEN GOSUB 4500:GOTO 3650
3645 GOSUB 4700
3650 S=S+1:IF S>4 THEN 3900
3660 GOTO 3600
3700 IF V1(AV-S-H1) AND 1 THEN GOSUB 4300:GOTO 3710
3705 GOSUB 4800
3710 IF V1(AV-S) AND 1 THEN GOSUB 4400:GOTO 3720
3715 GOSUB 4900
3720 RETURN
3900 DN=11:GOSUB7000:A=19:SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=0:B=0:RETURN
4000 DN=YL(S):GOSUB7000
4015 IF S=0 THEN A=XR(S):SET(A,B,C):A=0:B=B+1
4020 IF S>0 AND S<4 THEN FOR I=1 TO 4-S:A=XR(S)+5-S:SET(A,B,C):A=0:B=B+1:NEXT
4045 IF S>0 THEN A=XR(S):FORZ=1TOS:SET(A,B,C):A=A+1:NEXT:A=0:B=B+1
4070 IF S=4 THEN A=XR(S):SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=0:B=B+1
4072 IF S<4 THEN FOR I=0 TO N2(S)+1:A=XR(S):SET(A,B,C):A=0:B=B+1:NEXT
4075 IF S>0 THEN A=XR(S):FORZ=1TOS:SET(A,B,C):A=A+1:NEXT:A=0:B=B+1
4095 IF S>0 AND S<4 THEN FOR I=1 TO 4-S:A=XR(S)+5-S:SET(A,B,C):A=0:B=B+1:NEXT
4096 IF S=0 THEN A=XR(S):SET(A,B,C)
4098 A=0:B=0
4099 RETURN
4200 DN=YL(S):GOSUB7000
4215 IF S=0 THEN A=XR(S):SET(A,B,C):A=0:B=B+1:GOTO 4230
4220 FOR I=1 TO 5-S:A=XR(S)+5-S-I:SET(A,B,C):A=0:B=B+1:NEXT
4230 DN=N2(S)+2:GOSUB 7100
4235 IF S=0 THEN A=XR(S):SET(A,B,C):A=0:B=B+1:GOTO 4249
4240 FOR I=0 TO 4-S:A=XR(S)+I:SET(A,B,C):A=0:B=B+1:NEXT
4249 A=0:B=0
4250 RETURN
4300 J=3-S:IF S=0 THEN J=0
4305 A=0:B=0
4306 IF S>0 THEN 4328
4307 A=20:B=B+1:FOR I=1 TO 18:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4310 DN=N2(0):GOSUB 7100
4320 A=20:FOR I=1 TO 19:SET(A,B,C):A=A+1:NEXT:GOTO 4398
4328 DN=YL(S):GOSUB 7100
4329 FOR I=1 TO 5-S:A=J+XR(S)+2:SET(A,B,C):A=0:B=B+1:NEXT
4330 A=20:FOR I=19 TO J+XR(S):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4370 IF S<4 THEN FOR I=1 TO N2(S):A=J+XR(S)+2:SET(A,B,C):A=0:B=B+1:NEXT
4380 A=20:FOR I=19 TO J+XR(S):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4385 FOR I=1 TO 5-S:A=J+XR(S)+2:SET(A,B,C):NEXT
4398 A=0:B=0
4399 RETURN
4400 A=0:B=0
4406 IF S>0 THEN 4430
4407 B=B+1:FOR I=1 TO 11+XL(0):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4410 DN=N2(0):GOSUB 7100
4420 FOR I=1 TO 12+XL(0):SET(A,B,C):A=A+1:NEXT:GOTO 4398
4430 DN=YL(S):GOSUB 7100
4431 FOR I=1 TO 5-S:A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4432 A=XL(S):SET(A,B,C):A=A+1:FOR I=XL(S) TO 17:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4470 IF S<4 THEN FOR I=1 TO N2(S):A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4480 A=XL(S):SET(A,B,C):A=A+1:FOR I=XL(S) TO 17:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=0:B=B+1
4485 FOR I=1 TO 5-S:A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4498 A=0:B=0
4499 RETURN
4500 DN=YL(S):GOSUB7000
4515 IF S=0 THEN A=XL(S):SET(A,B,C):A=0:B=B+1
4520 IF S>0 AND S<4 THEN FOR I=1 TO 4-S:A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4545 IF S>0 THEN A=XL(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT:A=0:B=B+1
4565 IF S=4 THEN A=XL(S):SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=0:B=B+1
4572 IF S<4 THEN FOR I=0 TO N2(S)+1:A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4575 IF S>0 THEN A=XL(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT
4595 IF S>0 AND S<4 THEN FOR I=1 TO 4-S:A=XL(S):SET(A,B,C):A=0:B=B+1:NEXT
4596 IF S=0 THEN A=XL(S):SET(A,B,C)
4597 A=0:B=0
4599 RETURN
4700 DN=YL(S):GOSUB7000
4715 IF S=0 THEN A=XL(S):SET(A,B,C):A=0:B=B+1:GOTO 4730
4720 FOR I=1 TO 5-S:A=XL(S)+I:SET(A,B,C):A=0:B=B+1:NEXT
4730 DN=N2(S)+2:GOSUB 7100
4735 IF S=0 THEN A=XL(S):SET(A,B,C):A=0:B=B+1:GOTO 4749
4740 FOR I=0 TO 4-S:A=XL(S)+5-S-I:SET(A,B,C):A=0:B=B+1:NEXT
4749 A=0:B=0
4750 RETURN
4800 GOSUB 4200
4815 DN=YL(S)+5-S:GOSUB7000
4820 A=20
4825 IF S<4 THEN FOR I=1 TO XR(S)-21:SET(A,B,C):A=A+1:NEXT
4830 SET(A,B,C):B=B+1
4835 IF S<4 THEN FOR I=1 TO N2(S):SET(A,B,C):B=B+1:NEXT
4840 SET(A,B,C):A=A-1
4850 IF S<4 THEN FOR I=1 TO XR(S)-20-1:SET(A,B,C):A=A-1:NEXT
4851 A=0:B=0
4855 RETURN
4900 IF S=0 THEN J=5:GOTO 4910
4902 J=0
4910 GOSUB 4700
4915 DN=YL(S)+5-S:GOSUB7000
4920 A=19
4925 IF S<4 THEN FOR I=1 TO 18-XL(S)-5+S+J:SET(A,B,C):A=A-1:NEXT
4930 SET(A,B,C):B=B+1
4935 IF S<4 THEN FOR I=1 TO N2(S):SET(A,B,C):B=B+1:NEXT
4940 SET(A,B,C):A=A+1
4950 IF S<4 THEN FOR I=1 TO 18-XL(S)-5+S+J:SET(A,B,C):A=A+1:NEXT
4951 A=0:B=0
4955 RETURN
5000 IF S=0 OR S=4 THEN RETURN
5010 DN=18:GOSUB7000
5020 IF S=3 THEN A=20:B=B-3:SET(A,B,2):A=0:B=B+1
5030 IF S=2 THEN A=19:B=B-1:SET(A,B,2):B=B+1:A=A-1:SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2):A=0:B=B+1
5040 IF S<>1 THEN RETURN
5041 A=18:B=B+1:SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2):SET(A+3,B,2):B=B-1:A=A+1:SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2)
5050 B=B-1:A=A+1:SET(A,B,2):SET(A+1,B,2):B=B-1:A=A+1:SET(A,B,2):RETURN
5100 IF S=0 OR S=4 THEN RETURN
5110 DN=20:GOSUB7000
5120 IF S=3 THEN A=20:B=B-3:SET(A,B,3):A=0:B=B+1
5130 IF S=2 THEN A=19:B=B-1:SET(A,B,3):B=B+1:A=A-1:SET(A,B,3):SET(A+1,B,2):SET(A+2,B,3):A=0:B=B+1
5140 IF S<>1 THEN RETURN
5141 A=18:B=B+1:SET(A,B,3):SET(A+1,B,3):SET(A+2,B,3):SET(A+3,B,3):B=B-1:A=A+1:SET(A,B,3):SET(A+1,B,3):SET(A+2,B,3)
5150 B=B-1:A=A+1:SET(A,B,3):SET(A+1,B,3):B=B-1:A=A+1:SET(A,B,3):RETURN
6000 SOUND1,1:PRINT@480,"DELICIOUS!";:FOR J=1 TO 2500:NEXT J
6010 REM DISAPPEAR CHEESE?
6100 GOSUB 2000
6110 PRINT@480+16,"ANOTHER?";
6115 SW=1:TM=0
6120 T$=INKEY$:IF T$<>"" THEN 6200
6130 REM IF TI>TM THEN PRINT MID$("? ..... ",SW,1} i"{1eft}"i:TM=TI+15:SW=3-SW
6140 GOTO 6120
6200 IF T$="Y" OR T$=CR$ THEN PRINT "YES";:GOTO 200
6210 IF T$="N" THEN PRINT "NO":END
6220 GOTO 6120
7000 A=0:B=0
7100 IFDN>0 THEN FORZ=1TODN:B=B+1:NEXT
7110 RETURN
62000 CLS:PRINT TAB((32-LEN(PG$))/2);PG$
62010 PRINT"COPYRIGHT 1979 BY C.T. NADOVICH"
62020 FOR I=1 TO 32:PRINT"-";:NEXT:PRINT
62030 PRINT"FIND YOUR WAY THROUGH A":PRINT"PERSPECTIVE MAZE":PRINT
62032 PRINT"YOUR CONTROLS ARE:"
62034 PRINT"W = MOVE FORWARD"
62035 PRINT"A = TURN LEFT"
62036 PRINT"S = TURN RIGHT"
62038 PRINT"Z = TURN AROUND"
62040 PRINT"? = HELP!"
62049 PRINT@480,"HIT enter TO BEGIN";
62050 T$=INKEY$:IF T$="" THEN 62050
62060 CLS
62070 GOTO 100
