0 CLS:DIMA,B,C,S,I,J,Z:X=32768:GOSUB9000
6 REM RATRUN COPYRIGHT (C) 1981 THE CODE WORKS
10 REM AS OF 10/8/81 RJ
90 PG$="CURSOR #13  RATRUN":NM$="13":GOSUB 62000
100 PRINT@448,"":H=12:V=7:H1=H+1:W=0:V1=0
110 I=RND(-(PEEK(9)*256+PEEK(10)))
130 DIMXL(4),N2(4),YL(4),XR(4),W(128),V1(128)
140 FORI=0TO4:READXL(I),N2(I),YL(I),XR(I):XR(I)=XR(I):NEXT I
200 CX=INT(RND(0)*H)+1:CY=INT(RND(0)*V)+1
210 C=CX:R=CY:S=0
220 FORI=0TO(V+1)*H1:W(W+I)=.:V1(V1+I)=.:NEXT
230 PRINT@448,"SETTING UP MAZE...X";
300 DA=0:N=0:AV=V1+C+R*H1:AW=W+C+R*H1:W(AW)=1:S=S+1:IF S>=H*V THEN 900
320 PRINTCHR$(8);MID$("xX",(SAND1)+1,1);
410 IFC>1ANDW(AW-1)=.THENDA=DA+2:N=N+1
420 IFC<HANDW(AW+1)=.THENDA=DA+1:N=N+1
430 IFR>1ANDW(AW-H1)=.THENDA=DA+8:N=N+1
440 IF(R<VANDW(AW+H1)=.)THENDA=DA+4:N=N+1
450 N=INT(RND(0)*N)+1
460 ONDA+1GOTO500,850,800,600,750,610,620,630,700,640,650,660,670,680,690,695
500 S=S-1
510 C=C+1:IFC>HTHENC=1:R=R+1:IFR>VTHENR=1
520 ONW(W+C+R*H1)+1GOTO510,300
600 ONNGOTO800,850
610 ONNGOTO750,850
620 ONNGOTO750,800
630 ONNGOTO750,800,850
640 ONNGOTO700,850
650 ONNGOTO700,800
660 ONNGOTO700,800,850
670 ONNGOTO700,750
680 ONNGOTO700,750,850
690 ONNGOTO700,750,800
695 ONNGOTO700,750,800,850
700 R=R-1:AV=AV-H1:V1(AV)=V1(AV)OR1:GOTO300
750 V1(AV)=V1(AV)OR1:R=R+1:AV=AV+H1:GOTO300
800 C=C-1:AV=AV-1:V1(AV)=V1(AV)OR2:GOTO300
850 V1(AV)=V1(AV)OR2:C=C+1:AV=AV+1:GOTO300
900 I=RND(0)>.5:J=RND(0):K=RND(0)>.5
920 IFITHENX=INT(J*H)+1:Y=V+(V-1)*K
930 IFI=.THENY=INT(J*V)+1:X=H+(H-1)*K
931 IF(CX-X)^2+(CY-Y)^2<(H^2+V^2)/9THEN900
950 SX=X:SY=Y:Z=USR(0)
1000 CLS0:C=0
1010 FORI=WTOW+(V+1)*H1:W(I)=.:NEXT
1020 D=INT(RND(0)*4)+1
1030 W(W+X+Y*H1)=1
1045 GOSUB3000:PRINT@455,INT(SQR((X-CX)^2+(Y-CY)^2)*10+.5)/10;
1049 IFCX=XANDCY=YTHEN6000
1050 Q$=INKEY$:IFQ$=""THEN1050
1060 IFQ$="H"THENT4=USR(1):GOTO6100
1070 IFQ$="S"THEND=D+2
1080 IFQ$="A"THEND=D-1
1090 IFQ$="D"THEND=D+1
1100 IFD<1THEND=D+4
1110 IFD>4THEND=D-4
1120 IFQ$<>"W"THEN1045
1200 AV=V1+X+Y*H1:AW=W+X+Y*H1
1201 ONDGOTO1260,1310,1360
1210 IFY>1AND(V1(AV-H1)AND1)THENY=Y-1:W(AW-H1)=1:GOTO1045
1220 GOTO1400
1260 IFX<HAND(V1(AV)AND2)THENX=X+1:W(AW+1)=1:GOTO1045
1290 GOTO1400
1310 IFY<VAND(V1(AV)AND1)THENY=Y+1:W(AW+H1)=1:GOTO1045
1340 GOTO1400
1360 IFX>1AND(V1(AV-1)AND2)THENX=X-1:W(AW-1)=1:GOTO1045
1400 PRINT@390,"NO DOOR";:POKE8,0:SOUND1,1:POKE8,8:FORI=1TO2500:NEXT
1420 PRINT@390,"€€€€€€€";:GOTO1049
2000 CLS0:C=1:SET(0,0,C):A=1:B=.
2020 FORI=1TOH:SET(A,B,C):SET(A+1,B,C):SET(A+2,B,C):A=A+3:NEXT:B=B+3:A=.
2100 FORJ=1TOV:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B+2
2101 FORI=1TOH
2125 K=V1(V1+I+J*H1)
2130 SET(A,B,C):SET(A+1,B,C):SET(A+2,B,C):IFK=1ORK=3THENRESET(A,B):RESET(A+1,B)
2135 A=A+3
2140 IFK<2THENB=B-1:A=A-1:SET(A,B,C):A=A+1:B=B-1:A=A-1:SET(A,B,C):A=A+1:B=B+2
2200 B=B-1:A=A-1
2210 IFW(W+I+J*H1)=1THENSET(A-1,B,0)
2250 IFI=SXANDJ=SYTHENSET(A-1,B,3)
2260 IFI=CXANDJ=CYTHENSET(A-1,B,2)
2270 IFI=XANDJ=YTHENSET(A-1,B,4)
2280 A=A+1:B=B+1
2290 NEXT:B=B+3:A=.:NEXT:RETURN
3000 S=.:CLS0:A=.:B=.
3005 AV=V1+X+Y*H1
3010 ONDGOTO3200,3400,3600
3025 IFSY=Y-SANDSX=XTHENGOSUB5100
3026 IFCY=Y-SANDCX=XTHENGOSUB5000
3027 K=V1(AV-(S+1)*H1):IF K=. OR K=2 THEN 3100
3030 IFV1(AV-S*H1)AND2THENGOSUB4000:GOTO 3040
3035 GOSUB 4200
3040 IFV1(AV-1-S*H1)AND2THENGOSUB4500:GOTO3050
3045 GOSUB 4700
3050 S=S+1:IFS>4THEN3900
3060 GOTO3025
3100 IFV1(AV-S*H1)AND2THENGOSUB4300:GOTO3110
3105 GOSUB 4800
3110 IFV1(AV-1-S*H1)AND2THENGOSUB4400:GOTO3120
3115 GOSUB 4900
3120 RETURN
3200 IFCY=YANDCX=X+STHENGOSUB5000
3210 IFSY=YANDSX=X+STHENGOSUB5100
3226 K=V1(AV+S):IFK=.ORK=1THEN3300
3230 IFV1(AV+S)=2THENGOSUB4200:GOTO3240
3235 GOSUB4000
3240 IFV1(AV+S-H1)AND1THENGOSUB 4500:GOTO 3250
3245 GOSUB4700
3250 S=S+1:IFS>4THEN3900
3260 GOTO 3200
3300 IFV1(AV+S)=.THENGOSUB4800:GOTO3310
3305 GOSUB4300
3310 IFV1(AV+S-H1)AND1THENGOSUB4400:GOTO 3320
3315 GOSUB4900
3320 RETURN
3400 IFCY=Y+SANDCX=XTHENGOSUB5000
3410 IFSY=Y+SANDSX=XTHENGOSUB5100
3426 K=V1(AV+S*H1):IFK=.ORK=2THEN3500
3430 IFV1(AV-1+S*H1)AND2THENGOSUB4000:GOTO3440
3435 GOSUB4200
3440 IFV1(AV+S*H1)=1THENGOSUB4700:GOTO3450
3445 GOSUB4500
3450 S=S+1:IFS>4THEN3900
3460 GOTO3400
3500 IFV1(AV-1+S*H1)AND2THENGOSUB4300:GOTO3510
3505 GOSUB4800
3510 IFV1(AV+S*H1)=.THENGOSUB4900:GOTO 3520
3515 GOSUB4400
3520 RETURN
3600 IFCX=X-SANDCY=YTHENGOSUB5000
3610 IFSX=X-SANDSY=YTHENGOSUB5100
3625 K=V1(AV-(S+1)):IFK=.ORK=1THEN3700
3630 IFV1(AV-S-H1)AND1THENGOSUB4000:GOTO3640
3635 GOSUB4200
3640 IFV1(AV-S)AND1THENGOSUB4500:GOTO3650
3645 GOSUB4700
3650 S=S+1:IFS>4THEN3900
3660 GOTO3600
3700 IFV1(AV-S-H1)AND1THENGOSUB4300:GOTO3710
3705 GOSUB4800
3710 IFV1(AV-S)AND1THENGOSUB4400:GOTO3720
3715 GOSUB4900
3720 RETURN
3900 DN=11:GOSUB7000:A=19:SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=.:B=.:RETURN

4000 DN=YL(S):GOSUB7000
4015 IFS=.THENA=XR(S):SET(A,B,C):A=.:B=B+1
4020 IFS>.ANDS<4THENFORI=1TO4-S:A=XR(S)+5-S:SET(A,B,C):A=.:B=B+1:NEXT
4045 IFS>.THENA=XR(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT:A=.:B=B+1
4070 IFS=4THENA=XR(S):SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=.:B=B+1
4072 IFS<4THENFORI=.TON2(S)+1:A=XR(S):SET(A,B,C):ON-(S>.)GOSUB4299:A=.:B=B+1:NEXT
4075 IFS>.THENA=XR(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT:A=.:B=B+1
4095 IFS>.ANDS<4THENFORI=1TO4-S:A=XR(S)+5-S:SET(A,B,C):A=.:B=B+1:NEXT
4096 IFS=.THENA=XR(S):SET(A,B,C)
4098 A=.:B=.:RETURN

4200 DN=YL(S):GOSUB7000
4215 IFS=.THENA=XR(S):SET(A,B,C):A=.:B=B+1:GOTO4230
4220 FORI=1TO5-S:A=XR(S)+5-S-I:SET(A,B,C):A=.:B=B+1:NEXT
4230 DN=N2(S)+2:GOSUB7100
4235 IFS=.THENA=XR(S):SET(A,B,C):A=.:B=B+1:GOTO4249
4240 FORI=.TO4-S:A=XR(S)+I:SET(A,B,C):A=.:B=B+1:NEXT
4249 A=.:B=.:RETURN
4299 SET(A+(5-S),B,C):RETURN

4300 J=3-S:IFS=.THENJ=.
4305 A=.:B=.
4306 IFS>.THEN4328
4307 A=20:B=B+1:FORI=1TO18:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4310 DN=N2(0):GOSUB7100
4320 A=20:FORI=1TO19:SET(A,B,C):A=A+1:NEXT:GOTO4398
4328 DN=YL(S):GOSUB7100
4329 FORI=1TO5-S:A=J+XR(S)+2:SET(A,B,C):A=.:B=B+1:NEXT
4330 A=20:FORI=19TOJ+XR(S):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4370 IFS<4THENFORI=1TON2(S):A=J+XR(S)+2:SET(A,B,C):A=.:B=B+1:NEXT
4380 A=20:FORI=19TOJ+XR(S):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4385 FORI=1TO5-S:A=J+XR(S)+2:SET(A,B,C):A=.:B=B+1:NEXT
4398 A=.:B=.:RETURN

4400 A=.:B=.
4406 IFS>.THEN4430
4407 B=B+1:FORI=1TO11+XL(0):SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4410 DN=N2(0):GOSUB7100
4420 FORI=1TO12+XL(0):SET(A,B,C):A=A+1:NEXT:GOTO4398
4430 DN=YL(S):GOSUB7100
4431 FORI=1TO5-S:A=XL(S):SET(A,B,C):A=.:B=B+1:NEXT
4432 A=XL(S):SET(A,B,C):A=A+1:FORI=XL(S)TO17:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4470 IFS<4THENFORI=1TON2(S):A=XL(S):SET(A,B,C):A=.:B=B+1:NEXT
4480 A=XL(S):SET(A,B,C):A=A+1:FORI=XL(S)TO17:SET(A,B,C):A=A+1:NEXT:SET(A,B,C):A=.:B=B+1
4485 FORI=1TO5-S:A=XL(S):SET(A,B,C):A=.:B=B+1:NEXT
4498 A=.:B=.:RETURN

4500 DN=YL(S):GOSUB7000
4515 IFS=.THENA=XL(S):SET(A,B,C):A=.:B=B+1
4520 IFS>.ANDS<4THENFORI=1TO4-S:A=XL(S):SET(A,B,C):A=.:B=B+1:NEXT
4545 IFS>.THENA=XL(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT:A=.:B=B+1
4565 IFS=4THENA=XL(S):SET(A,B,C):SET(A+1,B,C):B=B+1:SET(A,B,C):SET(A+1,B,C):A=.:B=B+1
4572 IFS<4THENFORI=.TON2(S)+1:A=XL(S):SET(A,B,C):ON-(S>.)GOSUB4299:A=.:B=B+1:NEXT
4575 IFS>.THENA=XL(S):FORZ=1TO6-S:SET(A,B,C):A=A+1:NEXT
4595 IFS>.ANDS<4THENFORI=1TO5-S:A=XL(S):SET(A,B,C):A=.:B=B+1:NEXT
4596 IFS=.THENA=XL(S):SET(A,B,C)
4597 A=.:B=.:RETURN

4700 DN=YL(S):GOSUB7000
4715 IFS=.THENA=XL(S):SET(A,B,C):A=.:B=B+1:GOTO4730
4720 FORI=1TO5-S:A=XL(S)+I:SET(A,B,C):A=.:B=B+1:NEXT
4730 DN=N2(S)+2:GOSUB7100
4735 IFS=.THENA=XL(S):SET(A,B,C):A=.:B=B+1:GOTO4749
4740 FORI=.TO4-S:A=XL(S)+5-S-I:SET(A,B,C):A=.:B=B+1:NEXT
4749 A=.:B=.:RETURN

4800 GOSUB4200
4815 DN=YL(S)+5-S:GOSUB7000
4820 A=20
4825 IFS<4THENFORI=1TOXR(S)-21:SET(A,B,C):A=A+1:NEXT
4830 SET(A,B,C):B=B+1
4835 IFS<4THENFORI=1TON2(S):SET(A,B,C):B=B+1:NEXT
4840 SET(A,B,C):A=A-1
4850 IFS<4THENFORI=1TOXR(S)-20-1:SET(A,B,C):A=A-1:NEXT
4851 A=.:B=.:RETURN

4900 IFS=.THENJ=5:GOTO4910
4902 J=.
4910 GOSUB4700
4915 DN=YL(S)+5-S:GOSUB7000
4920 A=19
4925 IFS<4THENFORI=1TO18-XL(S)-5+S+J:SET(A,B,C):A=A-1:NEXT
4930 SET(A,B,C):B=B+1
4935 IFS<4THENFORI=1TON2(S):SET(A,B,C):B=B+1:NEXT
4940 SET(A,B,C):A=A+1
4950 IFS<4THENFORI=1TO18-XL(S)-5+S+J:SET(A,B,C):A=A+1:NEXT
4951 A=.:B=.:RETURN
5000 IFS=.ORS=4THENRETURN
5010 DN=18:GOSUB7000
5020 IFS=3THENA=20:B=B-3:SET(A,B,2):A=.:B=B+1
5030 IFS=2THENA=19:B=B-1:SET(A,B,2):B=B+1:A=A-1:SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2):A=.:B=B+1
5040 IFS<>1THENRETURN
5041 A=18:B=B+4:SET(A-1,B,2):SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2):SET(A+3,B,2):B=B-1:SET(A,B,2):SET(A+1,B,2):SET(A+2,B,2)
5050 B=B-1:A=A+1:SET(A,B,2):RETURN
5100 IFS=.ORS=4THENRETURN
5110 DN=20:GOSUB7000
5120 IFS=3THENA=20:B=B-3:SET(A,B,3):A=.:B=B+1
5130 IFS=2THENA=19:B=B-1:SET(A,B,3):SET(A+1,B,3):B=B+1:SET(A,B,3):SET(A+1,B,3):A=.:B=B+1
5140 IFS<>1THENRETURN
5141 A=18:B=B+4:SET(A,B,3):SET(A+1,B,3):SET(A+2,B,3):B=B-1:SET(A,B,3):SET(A+1,B,3):SET(A+2,B,3)
5150 B=B-1:SET(A,B,3):SET(A+1,B,3):SET(A+2,B,3):RETURN
6000 T4=USR(1):POKE8,0:SOUND240,1:POKE8,8:FORI=1TO25:PRINT@484,"DELICIOUS!";:FORJ=1TO250:NEXTJ
6010 PRINT@484,"€€€€€€€€€€";:FORJ=1TO250:NEXTJ,I
6100 GOSUB2000:PRINT@22,INT(T4/60);:PRINT@22+32*2,"˜CHEESE";:PRINT@22+32*4,"¨START ";:PRINT@22+32*6,"¸STOP  ";
6110 PRINT@452,"ANOTHER? €";
6115 Z=USR(0):TM=USR(1)+30
6120 T$=INKEY$
6125 IFUSR(1)<TMTHEN6200
6130 IFUSR(1)<TM+30THENPRINTCHR$(8)" ";:GOTO6200
6140 PRINTCHR$(8)"€";:GOTO6115
6200 IFT$="Y"THENPRINTCHR$(8)"YES";:GOTO200
6210 IFT$="N"THENPRINTCHR$(8)"NO":END
6220 GOTO6120
7000 A=.:B=.
7100 IFDN>.THENFORZ=1TODN:B=B+1:NEXT
7110 RETURN

9000 Z=16896:GOSUB9001:Z=X:GOSUB9001:EXECX:RETURN
9001 READT$:FORY=1TOLEN(T$)STEP2:POKEZ,(ASC(MID$(T$,Y,1))-65)*16+ASC(MID$(T$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
9002 DATAHPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
9003 DATAINCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
9150 DATA8,20,-4,31,8,12,1,27,12,6,5,24,15,2,8,22,17,0,10,21

62000 CLS:PRINT TAB((32-LEN(PG$))/2);PG$
62010 PRINT"COPYRIGHT 1979 BY C.T. NADOVICH"
62020 FOR I=1 TO 32:PRINT"-";:NEXT:PRINT
62030 PRINT"FIND YOUR WAY THROUGH A":PRINT"PERSPECTIVE MAZE":PRINT
62032 PRINT"YOUR CONTROLS ARE:"
62034 PRINT"  W MOVE FORWARD"
62035 PRINT"  A TURN LEFT"
62036 PRINT"  D TURN RIGHT"
62038 PRINT"  S TURN AROUND"
62040 PRINT"  H HELP!"
62049 PRINT@448,"HIT enter TO BEGIN"
62050 T$=INKEY$:IFT$=""THEN62050
62060 GOTO100
