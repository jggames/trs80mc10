1 ' IBM conversion version 1.4 (C) 1982 Andrew P. Bartorillo
2 KEY OFF:KEY 1,"PAGE 1"+CHR$(13):KEY 2,"PAGE 2"+CHR$(13):KEY 3,"PAGE 3"+CHR$(13)
3 KEY 5,"PAGE 5"+CHR$(13):KEY 6,"PAGE 6"+CHR$(13):KEY 7,"MAP"+CHR$(13)
4 KEY 8,"SAVE GAME"+CHR$(13):KEY 4,"PAGE 4"+CHR$(13)
5 KEY 9,"DONE"+CHR$(13):KEY 10,"ALL"+CHR$(13):DEF SEG:POKE 106,0:SCREEN 0,0,0:WIDTH 80:LOCATE 1,1,0
6 CLS:RANDOMIZE(VAL(RIGHT$(TIME$,2))):DIM LIN$(24),LB$(11),MP%(45),SS(10),ML%(45,1):GOSUB 73:LOCATE 9,36:PRINT"Acorn Software Products, Inc.":LOCATE 10,47:PRINT"presents":PLOT$="UUMFDD<<<====POOORXXZZ\]":GOSUB 100
7 LOCATE 11,45:PRINT"LOST  COLONY":LOCATE 12,35:PRINT"A Resource Management Simulation":LOCATE 13,50:PRINT"by":LOCATE 14,37:PRINT"David Feitelberg - (C) 1981":GOSUB 230:GOTO 257
8 NU=INT(RND(1)*6)+11-HL:FOR X=1 TO NU:IN=INT(RND(1)*45)+1:MP%(IN)=MP%(IN)+1:NEXT:NU=INT(RND(1)*8)+12-HL:FOR X=1 TO NU:IN=INT(RND(1)*45)+1:MP%(IN)=MP%(IN)+8:NEXT
9 FOR X=29 TO 31:IN=INT(MP%(X)/8):FI=FI+IN:MI=INT(MP%(X)-IN*8)+MI:NEXT:MS=251+INT(RND(1)*125):FS=501+INT(RND(1)*150):AS=1001+INT(RND(1)*400):GA=3
10 FOR X=1 TO 45:READ ML%(X,0),ML%(X,1):ML%(X,0)=-ML%(X,0):NEXT:DATA 2,37,2,55,4,28,4,44,4,50,4,56,6,21,5,34,6,40,6,46,6,53,6,66,8,12,9,17,9,23,7,27,8,34,8,40,9,46,8,52,7,61,10,28,12,13,12,33,11,40
11 DATA 11,51,10,59,14,35,-14,40,-13,45,-14,50,12,57,15,54,12,63,14,61,14,67,16,33,17,42,16,47,17,59,20,42,20,47,19,57,19,61,23,45
12 YR=1:SO=1+(15-INT(RND(1)*30)+1)/100:LP(6)=1701+INT(RND(1)*600):PL=LP(6)/(9.5+RND(1)):FOR X=1 TO 5:SS(X)=SO:NEXT
13 CA=1201+INT(RND(1)*400):DT=301+INT(RND(1)*50):RT=199-INT(RND(1)*30):CP=551+INT(RND(1)*300):MP(0)=901+INT(RND(1)*400):LP(0)=LP(6):CLS:GOSUB 73:GOSUB 275:GOSUB 14:GOTO 17
14 LP(0)=INT(LP(0)+.5):GOSUB 228:FLAG=1:GOSUB 238:GOSUB 82:GOSUB 62:GOSUB 96:IF GO<1 THEN 14
15 IF NU<0 THEN GOSUB 235:GOTO 14 ELSE IF NU>LP(0) THEN GOSUB 236:GOTO 14 ELSE IF INT(NU)<>NU THEN GOSUB 233:GOTO 14
16 LP(0)=LP(0)-NU:LP(GO)=LP(GO)+NU:IF LP(0)>.01 THEN GOTO 14 ELSE RETURN
17 MP(0)=INT(MP(0)+.5):GOSUB 82:GOSUB 228:FLAG=2:GOSUB 239:GOSUB 62:GOSUB 96:IF GO<1 THEN 17
18 IF NU<0 THEN GOSUB 235:GOTO 17 ELSE IF NU>MP(0) THEN GOSUB 236:GOTO 17 ELSE IF INT(NU)<>NU THEN GOSUB 233:GOTO 17
19 MP(0)=MP(0)-NU:MP(GO)=MP(GO)+NU:IF MP(0)>.01 THEN 17 ELSE GOSUB 82
20 GOSUB 228:LOCATE 5,1:PRINT SPC(79):LOCATE 4,1:PRINT" We report the following production:":IF LP(1)=0 THEN AO=0:T(1)=0:GOTO 24 ELSE WM=MP(1)/LP(1):IF WM>SS(1)+1.5 THEN WM=SS(1)+1.5
21 X=1/(SQR((INT(RND(1)*50)+1)+2*GA)):IF X>.33 THEN X=.33 ELSE IF X<.08 THEN X=0
22 IF INT(RND(1)*20)+1>9 THEN X=-X
23 AO=(1-X)*(WM+.5+SS(1))*LP(1):T(1)=AO:IF AO<LP(1) THEN A9=1:M9=LP(1)-AO:GOSUB 160
24 IF LP(2)<=0 OR MI=0 THEN MO=0:T(2)=0:GOTO 27 ELSE GO=MI*115*(SS(2)+1):IF LP(2)<GO THEN GO=LP(2)
25 WM=MP(2)/GO:IF WM>SS(2)+1 THEN WM=SS(2)+1
26 MO=(WM+.5+SS(2))*GO:T(2)=MO:IF MO<LP(2) THEN A9=2:M9=LP(2)-MO:GOSUB 160
27 IF LP(3)<=0 OR FI=0 THEN FO=0:T(4)=0:GOTO 30 ELSE GO=FI*100*(SS(3)+1):IF LP(3)<GO THEN GO=LP(3)
28 WM=MP(3)/GO:IF WM>SS(3)+1 THEN WM=SS(3)+1
29 FO=(WM+.5+SS(3))*GO:T(3)=FO:IF FO<LP(3) THEN A9=3:M9=LP(3)-FO:GOSUB 160
30 FOR PG%=2 TO 4:GOSUB 73:GOSUB 230:NEXT:GOTO 42
31 MP(0)=INT(MP(0)+.5):T(4)=IU:C9=0:P1=0:GOSUB 228:FLAG=4:GOSUB 241
32 IF MP(0)<.01 THEN GOSUB 229:LOCATE 3,1:PRINT"You have nothing remaining to assign.":GOSUB 230:GOTO 35
33 GOSUB 62:IF A$="SAVE GAME" THEN LN%=2:GOTO 264
34 IF NU>MP(0) THEN GOSUB 236:GOTO 33 ELSE IF NU<0 THEN GOSUB 235:GOTO 33 ELSE GOSUB 96:IF GO<1 THEN GOSUB 234:GOTO 33 ELSE MP(0)=MP(0)-NU:MP(GO)=MP(GO)+NU:GOSUB 82:GOTO 31
35 IF GA*100-(LP(5)+MP(5))*(1+SS(5))<=DT THEN RT=RT+T9 ELSE DT=DT+T9
36 GOSUB 228:NU=AO+AS:IF LP(6)>NU THEN LOCATE 3,1:PRINT"We have"INT(LP(6)-NU)"starving.":GOSUB 230:AO=0:AS=0:M9=LP(6)-NU:A9=INT(RND(1)*5)+1:GOSUB 160 ELSE AO=AO-LP(6):IF AO<0 THEN AS=AS+AO:AO=0
37 GOSUB 82:GOSUB 228:FOR X%=6 TO 10:SS(X%)=0:NEXT
38 FLAG=5:GOSUB 243:GOSUB 62:IF A$="DONE" THEN 40 ELSE IF A$="SAVE GAME" THEN LN%=3:GOTO 264 ELSE IF A$="ALL" THEN SO=CP/LP(6):T(5)=CP+T(5):CP=0:FOR X=1 TO 5:SS(X+5)=SS(X+5)+SO*LP(X):NEXT:A$="DONE":GOTO 40
39 IF NU<0 THEN GOSUB 235:GOTO 38 ELSE IF NU>CP THEN GOSUB 236:GOTO 38 ELSE GOSUB 96:IF GO<1 THEN 38 ELSE SS(GO+5)=SS(GO+5)+NU:CP=CP-NU:T(5)=T(5)+NU
40 FOR X=1 TO 5:IF LP(X)=0 THEN SS(X)=0 ELSE SS(X)=SS(X+5)/LP(X)
41 NEXT:SO=0:FOR X=1 TO 5:SO=SO+SS(X)*LP(X):NEXT:SO=SO/LP(6):GOSUB 82:IF A$="DONE" THEN 153 ELSE 38
42 IF LP(4)<=0 OR PL=0 THEN IP=0:GOTO 31 ELSE IU=0:T9=0:GO=PL*2*(SS(4)+1):IF LP(4)<GO THEN GO=LP(4)
43 GOSUB 228:WM=MP(4)/GO:IF WM>SS(4)+2 THEN WM=SS(4)+2
44 IP=(WM+.5+SS(4))*GO
45 GOSUB 228:FLAG=3:GOSUB 240:GOTO 47
46 LOCATE 6,26:COLOR 0,7:PRINT" We have not made that many. ":COLOR 7,0:GOSUB 230:LOCATE 6,26:PRINT SPC(30)
47 PG%=5:GOSUB 73:GOSUB 62
48 IF A$="DONE" THEN 31 ELSE IF A$="SAVE GAME" THEN LN%=1:GOTO 264 ELSE IF NU=0 THEN GOSUB 234:GOTO 45
49 LB$(10)="ROBO":LB$(11)="TRAN":LB$(6)="PLAN":LB$(7)="CONS":GOSUB 98
50 PV=0:IF GO<1 THEN GOSUB 234:GOTO 47 ELSE ON GO GOTO 51,54,57,59
51 IF NU<0 AND ABS(NU)>MP(0) THEN 46 ELSE MU=.9000001:GOSUB 218:IF PV=1 THEN 47
52 MU=.5:GOSUB 215:IF PV=1 THEN 47 ELSE MU=.1:GOSUB 220:IF PV<>1 THEN MP(0)=MP(0)+NU:MS=M8:MO=M9:FS=F8:FO=F9:IU=I9
53 GOTO 47
54 IF NU<0 AND ABS(NU)>T9 THEN 46 ELSE MU=2.5:GOSUB 218:IF PV=1 THEN 47 ELSE MU=1.2:GOSUB 223:IF PV=1 THEN 47
55 MU=1:GOSUB 215:IF PV=1 THEN 47 ELSE MU=1.5:GOSUB 220:IF PV<>1 THEN T9=T9+NU:MS=M8:MO=M9:IU=I9:FS=F8:FO=F9
56 GOTO 47
57 IF NU<0 AND ABS(NU)>P1 THEN 46 ELSE MU=3.4:GOSUB 218:IF PV=1 THEN 47 ELSE MU=2.5:GOSUB 215:IF PV=1 THEN 47 ELSE MU=.9000001:GOSUB 220:IF PV<>1 THEN P1=P1+NU:PL=PL+NU:IU=I9:MS=M8:MO=M9:FS=F8:FO=F9
58 GOTO 47
59 IF NU<0 AND ABS(NU)>C9 THEN 46 ELSE MU=.5:GOSUB 218:IF PV=1 THEN 47 ELSE MU=.2:GOSUB 215:IF PV=1 THEN 47 ELSE MU=.25:GOSUB 220:MU=.5:IF PV=1 THEN 47
60 GOSUB 225:MU=.6:IF PV=1 THEN 47 ELSE GOSUB 223:IF PV<>1 THEN C9=C9+NU:AO=A9:AS=A8:FO=F9:IU=I9:FS=F8:MO=M9:MS=M8:CP=CP+NU
61 GOTO 47
62 A$="":LOCATE 5,1:PRINT SPC(79):LOCATE 5,1
63 LINE INPUT"",A$:FOR I=1 TO LEN(A$):IF ASC(MID$(A$,I,1))>90 THEN MID$(A$,I,1)=CHR$(ASC(MID$(A$,I,1))-32)
64 NEXT:NU=0:Y=0:X%=0:IF A$="MAP" THEN GOSUB 124:GOSUB 125:CLS:GOSUB 73:GOSUB 275:ON FLAG GOSUB 238,239,240,241,243,244,245,246,247,248,249,250:GOTO 62
65 IF A$="" THEN RETURN ELSE FOR X=LEN(A$) TO 1 STEP-1:P$=MID$(A$,X,1):IF P$>":" THEN 66 ELSE IF P$>"/" OR P$="." THEN NU=VAL(MID$(A$,X,LEN(A$))) ELSE IF P$>")" THEN X%=X:Y=NU:NU=0
66 NEXT:IF X%>0 THEN ON ASC(MID$(A$,X%,1))-41 GOSUB 68,69,70,71,70,72
67 IF LEFT$(A$,4)="PAGE" AND NU<7 AND NU>0 THEN PG%=INT(NU):GOSUB 73:GOTO 62 ELSE RETURN
68 NU=Y*NU:RETURN
69 NU=Y+NU:RETURN
70 RETURN
71 NU=NU-Y:RETURN
72 NU=NU/Y:RETURN
73 LOCATE 8,1:PRINT"имммммммммммкмммммммк";STRING$(58,205);"╩":FOR I=9 TO 14:LOCATE I,1:PRINT"╨":LOCATE I,13:PRINT"╨":LOCATE I,21:PRINT"╨":LOCATE I,80:PRINT"╨":NEXT:LOCATE I,1:PRINT"хмммммммммммймммммммй";STRING$(58,205);"╪"
74 LOCATE 1,25:COLOR 0,7:PRINT" WARREN'S WORLD - LOST COLONY ":COLOR 7,0:LOCATE 7,60:PRINT "Year";YR:GOSUB 93:ON PG% GOTO 75,76,77,78,79,80,81
75 LB$(0)="Population":LB$(1)="Unemployed":LB$(2)="Consumer Gd":LB$(3)="Cash":LB$(4)="Living Std":LB$(5)="Areas":GOSUB 95:LOCATE 7,10:PRINT "Region Summary       ":GOTO 82
76 GOSUB 92:GOSUB 95:LOCATE 7,10:PRINT"Farm ";P$;"          ":GOTO 82
77 GOSUB 92:GOSUB 95:LOCATE 7,10:PRINT"Mining ";P$;"        ":GOTO 82
78 GOSUB 92:GOSUB 95:LOCATE 7,10:PRINT"Energy ";P$;"        ":GOTO 82
79 GOSUB 92:LB$(4)="Max. Mfg.":LB$(5)="Mfg. Used":GOSUB 95:LOCATE 7,10:PRINT"Manufacturing ";P$;" ":GOTO 82
80 GOSUB 92:LB$(3)="Transports":LB$(4)="Exploration":LB$(5)="Settling":GOSUB 95:LOCATE 7,10:PRINT"Transportation ";P$:GOTO 82
81 LB$(0)="Farm Prod":LB$(1)="Mine Prod":LB$(2)="Energy Prod":LB$(3)="Mfg Prod":LB$(4)="Cons Goods":LB$(5)="Tax Base":GOSUB 95:LOCATE 7,10:PRINT"Year End ";P$;"     "
82 ON PG% GOTO 83,84,85,86,87,88,89
83 LP(6)=INT(LP(6)):P(0)=LP(6):UL=INT(UL):P(1)=UL:P(2)=CP:P(3)=CA:P(4)=SO:P(5)=GA:GOSUB 90:RETURN
84 LP(1)=INT(LP(1)):P(0)=LP(1):MP(1)=INT(MP(1)):P(1)=MP(1):P(2)=SS(1):P(3)=GA:P(4)=AO:P(5)=AS:GOSUB 90:RETURN
85 LP(2)=INT(LP(2)):P(0)=LP(2):MP(2)=INT(MP(2)):P(1)=MP(2):P(2)=SS(2):P(3)=MI:P(4)=MO:P(5)=MS:GOSUB 90:RETURN
86 LP(3)=INT(LP(3)):P(0)=LP(3):MP(3)=INT(MP(3)):P(1)=MP(3):P(2)=SS(3):P(3)=FI:P(4)=FO:P(5)=FS:GOSUB 90:RETURN
87 LP(4)=INT(LP(4)):P(0)=LP(4):MP(4)=INT(MP(4)):P(1)=MP(4):P(2)=SS(4):P(3)=PL:P(4)=IP:P(5)=IU:GOSUB 90:RETURN
88 LP(5)=INT(LP(5)):P(0)=LP(5):MP(5)=INT(MP(5)):P(1)=MP(5):P(2)=SS(5):P(3)=DT+RT:P(4)=P(3)+(P(0)+P(1))*(1+SS(5))-GA*100:P(5)=P(3)+(LP(5)+MP(5))*SS(5)-GA*100:GOSUB 90:RETURN
89 P(0)=T(1):P(1)=T(2):P(2)=T(3):P(3)=T(4):P(4)=T(5):P(5)=T(1)+T(2)+T(3)+T(4)+T(5):GOSUB 90:RETURN
90 FOR IN=0 TO 5:LOCATE IN+9,14:PRINT USING "#####.#";P(IN):X=INT(P(IN)*.01):IF X>58 THEN X=58 ELSE IF X<0 THEN X=0
91 LOCATE IN+9,22:PRINT STRING$(58,32):LOCATE IN+9,22:PRINT STRING$(X,254):NEXT:RETURN
92 LB$(0)="Labor":LB$(1)="Robots":LB$(2)="Living Std":LB$(3)="Prod. Sites":LB$(4)="Production":LB$(5)="Stock":RETURN
93 P$="Report":LOCATE 17,10:PRINT"Page 1 - Region Summary":LOCATE 18,10:PRINT"Page 2 - Farm ";P$:LOCATE 19,10:PRINT"Page 3 - Mining ";P$:LOCATE 17,40:PRINT"Page 4 - Energy ";P$:LOCATE 18,40:PRINT"Page 5 - Manufacturing ";P$
94 LOCATE 19,40:PRINT"Page 6 - Transportation ";P$:RETURN
95 FOR X%=0 TO 5:LOCATE X%+9,2:PRINT USING"\         \";LB$(X%):NEXT:RETURN
96 GO=0:FOR X=1 TO LEN(A$):PP$=MID$(A$,X,4):IF PP$="FARM" THEN GO=1 ELSE IF PP$="MINI" THEN GO=2 ELSE IF PP$="ENER" THEN GO=3 ELSE IF PP$="MANU" THEN GO=4 ELSE IF PP$="TRAN" THEN GO=5
97 NEXT:IF GO>0 THEN PG%=GO+1:GOSUB 73:RETURN ELSE GOSUB 234:RETURN
98 GO=0:FOR X=1 TO LEN(A$):PP$=MID$(A$,X,4):IF PP$=LB$(10) THEN GO=1 ELSE IF PP$=LB$(11) THEN GO=2 ELSE IF PP$=LB$(6) THEN GO=3 ELSE IF PP$=LB$(7) THEN GO=4
99 NEXT:RETURN
100 LIN$(1)="здддд©           здддд©"
101 LIN$(2)="Ё    юд©     зддд╢    юд©"
102 LIN$(3)="зддддд© Ё   зддаддддбы   юбддддбы"
103 LIN$(4)="зддд© зы здддададддеддд© зда©    Ё    ю©     здд©"
104 LIN$(5)="зды   цдадбы        зы   цды  цддддадд©  юдбддды  Ё"
105 LIN$(6)="Ё    зы   юдбддддддбы    Ё    Ё       юдбддадд©   Ё"
106 LIN$(7)="здддддбдадддбад©    Ё      цдддддадбддедддддд©  Ё     юдбды"
107 LIN$(8)="Ё    зы   зды  ю© здад©    Ё    здды  ю©     юдбы       Ё"
108 LIN$(9)="ю©  зы    Ё     цды  зы    Ё    Ё      Ё здддбдаддддбддды"
109 LIN$(10)="юбда© зддад©   Ё   заддддбаддддад©  здады   Ё      Ё"
110 LIN$(11)="зы  юбы    юдддаддд╢     ю©      юдбы       цддддбдаддд©"
111 LIN$(12)="Ё    Ё             Ё      Ё    здддадд©   зды    ю©    цдддд©"
112 LIN$(13)="юдддды             Ё  здддабддда©    задддеддддбддаддбды  зды"
113 LIN$(14)="юбды   зы    юбддды   зы    Ё     Ё    Ё"
114 LIN$(15)="здаддддбы зд©  цддддддбы    зы     цдддды"
115 LIN$(16)="Ё      Ё  Ё юдд╢      Ё   здадддддбы"
116 LIN$(17)="юдд©   цдбы    юд©  здадддад©    зы"
117 LIN$(18)="юддды ю©     задбы     здадбддад©"
118 LIN$(19)="заддддд╢  Ё      Ё   Ё   зы"
119 LIN$(20)="юд©    Ё  юдд©   Ё  заддды"
120 LIN$(21)="Ё    цдддбды   юдды"
121 LIN$(22)="юдбдды   Ё"
122 LIN$(23)="ю©   зды"
123 LIN$(24)="юддды":RETURN
124 CLS:FOR I=1 TO 24:LOCATE I,ASC(MID$(PLOT$,I,1))-50:PRINT LIN$(I);:NEXT:RETURN
125 LOCATE 21,1:PRINT"Commands:":PRINT"         S-Display settled areas":PRINT"         R-Display resource sites":PRINT"         E-Exit map";
126 GOSUB 231:IF A$="E" THEN RETURN ELSE IF A$="R" THEN 127 ELSE IF A$="S" THEN 130 ELSE BEEP:GOTO 126
127 GOSUB 124:LOCATE 25,30:COLOR 0,7:PRINT" Display resource sites. ";:COLOR 7,0:FOR X=1 TO 45:IF ML%(X,0)<0 THEN 129 ELSE IN=MP%(X):IF IN>7 THEN LOCATE ABS(ML%(X,0)),ABS(ML%(X,1)):PRINT "Ч";
128 IN=IN-FIX(IN/8)*8:IF IN>0 THEN LOCATE ABS(ML%(X,0)),ABS(ML%(X,1)):PRINT"Ч";
129 NEXT:GOSUB 230:GOTO 125
130 GOSUB 124:LOCATE 25,30:COLOR 0,7:PRINT" Display settled areas. ";:COLOR 7,0:FOR IN=1 TO 45:IF ML%(IN,0)<=0 THEN 131 ELSE LOCATE ABS(ML%(IN,0)),ABS(ML%(IN,1)):PRINT"Ч";
131 NEXT:GOSUB 230:GOTO 125
132 FOR IN=1 TO 45:IF ML%(IN,0)<0 THEN LOCATE ABS(ML%(IN,0)),ABS(ML%(IN,1)):PRINT"Ч";
133 NEXT
134 LOCATE 1,1:PRINT"Ы";:TEMP$=" "
135 ON ERROR GOTO 136:A$=INKEY$:IF A$="" THEN 135 ELSE IF A$=CHR$(13) THEN 151 ELSE B=ASC(MID$(A$,2,1)):IF B<>72 AND B<>80 AND B<>75 AND B<>77 THEN 135 ELSE 137
136 RESUME 135
137 ON ERROR GOTO 0:IF B=72 THEN 138 ELSE IF B=80 THEN 141 ELSE IF B=77 THEN 144 ELSE 148
138 X=CSRLIN:Y=POS(0)-1:LOCATE X,Y:PRINT TEMP$;:IF X=1 THEN X=25
139 TEMP$=CHR$(SCREEN(X-1,Y)):IF TEMP$="Ч" THEN BEEP
140 LOCATE X-1,Y:PRINT "Ы";:GOTO 135
141 X=CSRLIN:Y=POS(0)-1:LOCATE X,Y:PRINT TEMP$;:IF X=24 THEN X=0
142 TEMP$=CHR$(SCREEN(X+1,Y)):IF TEMP$="Ч" THEN BEEP
143 LOCATE X+1,Y:PRINT "Ы";:GOTO 135
144 X=CSRLIN:Y=POS(0)-1
145 LOCATE X,Y:PRINT TEMP$;:IF Y=79 THEN Y=0
146 TEMP$=CHR$(SCREEN(X,Y+1)):IF TEMP$="Ч" THEN BEEP
147 LOCATE X,Y+1:PRINT "Ы";:GOTO 135
148 X=CSRLIN:Y=POS(0)-1:LOCATE X,Y:PRINT TEMP$;:IF Y=1 THEN Y=80
149 TEMP$=CHR$(SCREEN(X,Y-1)):IF TEMP$="Ч" THEN BEEP
150 LOCATE X,Y-1:PRINT"Ы";:GOTO 135
151 ON ERROR GOTO 0:X=CSRLIN:Y=POS(0)-1:A9=0:FOR X%=1 TO 45:IF X=ABS(ML%(X%,0)) AND Y=ABS(ML%(X%,1)) THEN IN=MP%(X%):A9=X%:X%=45
152 NEXT:RETURN
153 M9=0:M8=0:IN=0:P=10000!:FOR X=1 TO 5:IF IN<SS(X) THEN IN=SS(X):M8=X
154 IF P>SS(X) AND LP(X)>0 THEN P=SS(X):A9=X
155 IF SS(X)<YR*(.011+4.400001E-02*(HL/5))+.1 THEN M9=M9+LP(X)
156 NEXT:IF IN-P>SO/2 THEN GO=FIX(LP(A9)*(1+IN-P)*10)/10:IF GO>M9 THEN M9=GO
157 IF M9<=0 THEN 163 ELSE X=INT(RND(1)*12)+1:M9=M9*X/10:C9=FIX(X*CP/(INT(RND(1)*6)+1)+M9):IF M9+UL>LP(6)/3 THEN 251 ELSE FLAG=6:GOSUB 244
158 GOSUB 62:IF LEFT$(A$,1)="Y" THEN 159 ELSE IF LEFT$(A$,1)="N" THEN GOSUB 160:GOTO 163 ELSE GOSUB 234:GOTO 158
159 CP=CP-C9:GOSUB 82:GOTO 163
160 IF M9<=LP(A9) THEN LP(A9)=FIX((LP(A9)-M9)*10)/10:UL=UL+FIX(M9*10)/10:M9=0:RETURN
161 PV=0:X=INT(M9*10)/50:FOR P=1 TO 5:IF LP(P)>=X THEN LP(P)=LP(P)-X:M9=M9-X:PV=1:UL=UL+X
162 NEXT:IF M9>1 AND PV=1 THEN 161 ELSE IF M9>1 THEN 251 ELSE RETURN
163 IF CP<=0 THEN 169 ELSE GOSUB 228:FLAG=7:GOSUB 245:GOSUB 62:IF A$="SAVE GAME" THEN LN%=4:GOTO 264 ELSE IF LEFT$(A$,1)="N" THEN 169
164 GOSUB 82:FLAG=8:GOSUB 246:GOSUB 62:IF A$="DONE" THEN 169 ELSE IF NU<1 THEN GOSUB 233:GOTO 164 ELSE IF NU>CP THEN GOSUB 236:GOTO 164 ELSE GOSUB 96
165 IF GO=0 THEN 164 ELSE CO=UL:A9=SS(1)
166 IF UL>0 THEN IF UL>=NU THEN UL=UL-NU:LP(GO)=LP(GO)+NU:CP=CP-NU:T(5)=T(5)+NU:GOTO 164 ELSE CP=CP-(NU-UL):NU=UL:GOTO 166
167 M9=NU+(SS(GO)-SO)*125:IF M9<1 THEN 164 ELSE CP=CP-NU:T(5)=T(5)+NU:FOR X%=1 TO 5:IF A9>SS(X%) AND LP(X%)>0 THEN A9=SS(X%)
168 NEXT:GOSUB 160:LP(GO)=LP(GO)+UL-CO:UL=CO:GOTO 164
169 GOSUB 228:FLAG=9:GOSUB 247
170 GOSUB 62:IF A$="DONE" THEN 178 ELSE IF A$="SAVE GAME" THEN LN%=5:GOTO 264 ELSE IF A$="ALL" THEN 171 ELSE IF NU<0 THEN GOSUB 235:GOTO 170 ELSE GOTO 174
171 NU=AO:GOSUB 175:IF MO>.04 THEN NU=MO:GOSUB 176
172 IF FO>.04 THEN NU=FO:GOSUB 177
173 GOTO 178
174 GOSUB 96:IF GO<1 OR GO>3 THEN GOSUB 234:GOTO 170 ELSE ON GO GOSUB 175,176,177,:GOTO 170
175 IF NU>AO+.04 THEN LOCATE 6,32:COLOR 0,7:PRINT" Only"AO"remain. ":COLOR 7,0:GOSUB 230:LOCATE 6,32:PRINT SPC(30):GOTO 170 ELSE CA=CA-NU:AO=AO-NU:AS=AS+NU:GOSUB 82:RETURN
176 IF NU>MO+.04 THEN LOCATE 6,32:COLOR 0,7:PRINT" Only"MO"remain. ":COLOR 7,0:GOSUB 230:LOCATE 6,32:PRINT SPC(30):GOTO 170 ELSE CA=CA-300-NU:MO=MO-NU:MS=MS+NU:GOSUB 82:RETURN
177 IF NU>FO+.04 THEN LOCATE 6,32:COLOR 0,7:PRINT" Only"FO"remain. ":COLOR 7,0:GOSUB 230:LOCATE 6,32:PRINT SPC(30):GOTO 170 ELSE CA=CA-200-NU:FO=FO-NU:FS=FS+NU:GOSUB 82:RETURN
178 GOSUB 228:FLAG=10:GOSUB 248:GOSUB 62:IF A$="SAVE GAME" THEN LN%=6:GOTO 264
179 IF NU>0 AND NU<=75 THEN NU=NU/100:GOSUB 229 ELSE LOCATE 6,13:COLOR 0,7:PRINT" Only tax rates between 0% and 75% are enforceable. ":COLOR 7,0:GOSUB 230:LOCATE 6,1:PRINT SPC(75):GOTO 178
180 XX=NU-.7+(INT(RND(1)*15)+INT(RND(1)*15)+2)/100:M9=0:FOR P=1 TO 5:M9=M9+T(P):NEXT:M9=INT(M9*NU*100)/100
181 IF CA<0 THEN LOCATE 4,1:PRINT"The required interest payment on the debt is $";MID$(STR$(INT(.2*ABS(CA)*100)/100),2);".":GOSUB 230:M9=M9-INT(.2*ABS(CA)*100)/100
182 GOSUB 228:LOCATE 3,1:PRINT"Revenue to the treasury is $";MID$(STR$(M9),2);".":CA=CA+M9:PG%=1:GOSUB 73:GOSUB 230
183 GOSUB 228:LOCATE 3,1:PRINT"We are polling the captains of industry to determine depreciation losses.":FOR X=1 TO 5:IN=((INT(RND(1)*17)+1)/100)+XX:IF IN>0 THEN MP(X)=MP(X)-MP(X)*IN
184 NEXT:IN=((INT(RND(1)*25)+1)/100)+XX:IF IN>0 THEN DT=DT-DT*IN
185 IN=((INT(RND(1)*35)+1)/100)+XX:IF IN>0 THEN AS=AS-AS*IN
186 IN=((INT(RND(1)*22)+1)/100)+XX:IF IN>0 THEN MS=MS-MS*IN
187 IN=((INT(RND(1)*18)+1)/100)+XX:IF IN>0 THEN PL=PL-PL*IN
188 IN=((INT(RND(1)*22)+1)/100)+XX:IF IN>0 THEN FS=FS-FS*IN
189 GOSUB 230:IN=((INT(RND(1)*27)+1)/100)+XX:IF IN>0 THEN CP=CP-CP*IN
190 M9=GA*100:GOSUB 228:IF M9>=RT+DT+(LP(5)+MP(5))*(1+SS(5)) THEN 196 ELSE FLAG=11:GOSUB 249:GOSUB 62:IF A$="SAVE GAME" THEN LN%=7:GOTO 264 ELSE IF LEFT$(A$,1)="N" THEN 197 ELSE IF LEFT$(A$,1)<>"Y" THEN 190
191 PV=0:GOSUB 124:LOCATE 20,1:PRINT"EXPLORATION";:GOSUB 132:IF A9=0 THEN LOCATE 22,1:COLOR 0,7:PRINT" Bad entry. ";:BEEP:COLOR 7,0:GOSUB 230:LOCATE 25,34:PRINT SPC(15);:GOTO 191
192 M9=M9+100:IF IN>7 AND INT(RND(1)*20)+1<18 THEN LOCATE 22,1:PRINT"Energy Resources";:PV=1
193 IN=IN-FIX(IN/8)*8:IF IN>0 AND INT(RND(1)*20)+1<19 THEN LOCATE 23,1:PRINT"Mining Resources":PV=1
194 IF MP%(A9)=0 OR PV=0 THEN LOCATE 22,1:PRINT"The explorers found nothing."
195 GOSUB 237:A$=INKEY$:GOSUB 231:IF A$="R" OR M9>=RT+DT+(1+SS(5))*(LP(5)+MP(5)) THEN 196 ELSE 191
196 CLS:GOSUB 73:GOSUB 275
197 IF GA*100>=RT+DT+(LP(5)+MP(5))*SS(5) THEN 202 ELSE GOSUB 228:FLAG=12:GOSUB 250:GOSUB 62:IF A$="SAVE GAME" THEN LN%=8:GOTO 264 ELSE IF LEFT$(A$,1)="N" THEN 202 ELSE IF LEFT$(A$,1)<>"Y" THEN 197
198 GOSUB 124:LOCATE 20,1:PRINT"SETTLING":GOSUB 132:IF A9=0 THEN LOCATE 22,1:COLOR 0,7:PRINT" Bad entry. ":BEEP:COLOR 7,0:GOSUB 230:GOTO 198
199 ML%(A9,0)=ABS(ML%(A9,0)):GA=GA+1:FI=FI+INT(IN/8):IN=IN-INT(IN/8)*8:MI=MI+INT(IN):IF DT<GA*100 AND RT>=100 THEN DT=DT+100:RT=RT-100
200 GOSUB 237:A$=INKEY$:GOSUB 231:IF A$<>"R" AND GA*100<RT+DT+SS(5)*(LP(5)+MP(5)) THEN 198
201 CLS:GOSUB 275
202 LOCATE 5,1:PRINT SPC(79):IF YR=INT(YR/3)*3 THEN LP(0)=FIX(LP(6)*5/3)/10:LP(6)=LP(6)+LP(0):GOSUB 228:GOSUB 73:GOSUB 14
203 GOSUB 228:PG%=7:GOSUB 73
204 LOCATE 3,1:PRINT"Do you want to save the game at this point (Y/N)? ";:GOSUB 231:IF A$="Y" THEN PRINT A$:LN%=9:GOTO 264 ELSE IF A$="N" THEN PRINT A$:GOTO 205 ELSE 204
205 GOSUB 228:LOCATE 3,1:PRINT"Hit any key to continue with the game.":GOSUB 231
206 IF INT(RND(1)*25)+1<INT(22-HL/3) THEN 214 ELSE GOSUB 228:PV=0:ON INT(RND(1)*4)+1 GOTO 207,208,209,210
207 LOCATE 3,1:COLOR 0,7:PRINT" An infestation of vermin has destroyed some food stockpiles and property. ":COLOR 7,0:AS=.83*AS:CP=.9000001*CP:GOTO 211
208 LOCATE 3,1:COLOR 0,7:PRINT" An unknown disease is epidemic! Although quarantined, inhabitants were lost. ":COLOR 7,0:PV=3:GOTO 211
209 LOCATE 3,1:COLOR 0,7:PRINT" Sudden storms and flash floods have washed out roads & isolated many people. ":COLOR 7,0:DT=DT-(INT(RND(1)*ABS(DT))+1)/(INT(RND(1)*2)+1):GOTO 211
210 LOCATE 3,1:COLOR 0,7:PRINT" There has been a huge explosion, of unknown origin, at our largest factory. ":COLOR 7,0:PL=PL-INT(RND(1)*(PL/GA)+1)
211 GOSUB 230:GOSUB 230:IF PV=0 THEN 214
212 X%=INT(RND(1)*GA)+1:FOR IN=1 TO 45:IF ML%(IN,0)>0 THEN X%=X%-1:IF X%=0 THEN ML%(IN,0)=-ML%(IN,0):GA=GA-1:A9=INT(RND(1)*5)+1:A8=UL:M9=INT(RND(1)*(LP(6)/GA))+1:GOSUB 160:UL=A8:A8=FIX(MP%(IN)/8):FI=FI-A8:MI=MI-INT(MP%(IN)-A8*8)
213 NEXT:IF GA<1 THEN 251
214 T(5)=0:IF YR<12+INT(RND(1)*9) THEN YR=YR+1:LP(6)=FIX(UL*10)/10:FOR X=1 TO 5:LP(6)=LP(6)+FIX(LP(X)*10)/10:NEXT:GOTO 20 ELSE 255
215 M9=MU*NU:M8=MS:IF M9<=MO THEN M9=MO-M9:RETURN
216 M8=M9-MO:IF M8<=MS THEN M8=MS-M8:M9=0:RETURN
217 LOCATE 6,24:COLOR 0,7:PRINT" Insufficient mineral reserves. ":COLOR 7,0:GOSUB 230:LOCATE 6,24:PRINT SPC(40):PV=1:RETURN
218 I9=IU+MU*NU:IF I9>IP THEN LOCATE 6,19:COLOR 0,7:PRINT" The manufacturers would be overburdened. ":COLOR 7,0:GOSUB 230:LOCATE 6,19:PRINT SPC(60):PV=1
219 RETURN
220 F9=MU*NU:F8=FS:IF F9<=FO THEN F9=FO-F9:RETURN
221 F8=F9-FO:IF F8<=FS THEN F8=FS-F8:F9=0:RETURN
222 LOCATE 6,29:COLOR 0,7:PRINT" We lack the energy. ":COLOR 7,0:GOSUB 230:LOCATE 6,29:PRINT SPC(30):PV=1:RETURN
223 CA=CA-MU*NU:IF CA<0 THEN CA=CA+MU*NU:LOCATE 6,27:COLOR 0,7:PRINT" We cannot finance that. ":COLOR 7,0:GOSUB 230:LOCATE 6,27:PRINT SPC(30):PV=1
224 RETURN
225 A9=MU*NU:A8=AS:IF A9<=AO THEN A9=AO-A9:RETURN
226 A8=A9-AO:IF A8<=AS THEN A8=AS-A8:A9=0:RETURN
227 PV=1:LOCATE 6,24:COLOR 0,7:PRINT" Sir, there is not enough food. ":COLOR 7,0:GOSUB 230:RETURN
228 LOCATE 3,1:PRINT SPC(79):LOCATE 4,1:PRINT SPC(79):RETURN
229 LOCATE 5,1:PRINT SPC(79):RETURN
230 FOR X=1 TO 3999:NEXT:RETURN
231 A$=INKEY$:IF A$="" THEN 231 ELSE IF ASC(A$)>90 THEN A$=CHR$(ASC(A$)-32)
232 RETURN
233 LOCATE 6,34:COLOR 0,7:PRINT LB$(8):BEEP:COLOR 7,0:GOSUB 230:LOCATE 6,34:PRINT SPC(20):RETURN
234 LOCATE 6,24:COLOR 0,7:PRINT LB$(9):BEEP:COLOR 7,0:GOSUB 230:LOCATE 6,24:PRINT SPC(31):RETURN
235 LOCATE 6,22:COLOR 0,7:PRINT" Negative numbers are not allowed. ":BEEP:COLOR 7,0:GOSUB 230:LOCATE 6,22:PRINT SPC(40):RETURN
236 LOCATE 6,28:COLOR 0,7:PRINT" There are not enough. ":BEEP:COLOR 7,0:GOSUB 230:LOCATE 6,28:PRINT SPC(40):RETURN
237 LOCATE 25,15:COLOR 0,7:PRINT" Type 'R' to return, or any other key to continue. ";:COLOR 7,0:RETURN
238 LOCATE 3,1:PRINT"How do you want labor to be distributed? We have"LP(0)"people seeking jobs.":RETURN
239 LOCATE 3,1:PRINT"Where shall we employ the robots? There are"MP(0)"general purpose machines.":RETURN
240 LOCATE 3,1:PRINT"Describe manufacturing.":RETURN
241 LOCATE 3,1:PRINT"You have";:IF MP(0)>=.01 THEN PRINT MP(0)"robot";:IF MP(0)>1 THEN PRINT "s";
242 PRINT " to assign.   ":RETURN
243 LOCATE 3,1:PRINT"Describe consumer goods distribution.":RETURN
244 LOCATE 3,1:PRINT"Oh no!"INT(M9+.5)"workers threaten to strike.":LOCATE 4,1:PRINT"They demand"C9"consumer goods. Shall we grant it?":RETURN
245 LOCATE 3,1:PRINT"Do you want to reallocate labor?":RETURN
246 LOCATE 3,1:PRINT"How many consumer goods will you offer, and for what sector?":RETURN
247 LOCATE 3,1:PRINT"Describe stockpiling.":RETURN
248 LOCATE 3,1:PRINT"What tax rate prevails for this year?":RETURN
249 LOCATE 3,1:PRINT"Do you want to explore new territory?":RETURN
250 LOCATE 3,1:PRINT"Do you want to settle new territory?":RETURN
251 GOSUB 228:GOSUB 229:LOCATE 3,1:PRINT MID$(STR$(INT(100*(M9+UL)/LP(6))),2);" percent of the population demand you leave office
252 PRINT"because of ";:IF SO<YR*(.011+4.400001E-02*(HL/5))+.1 THEN PRINT"mismanagement."; ELSE PRINT"inequitable payments to workers.";
253 PRINT" Perhaps you are good with a hoe?"
254 END
255 GOSUB 228:GOSUB 229:LOCATE 3,1:PRINT"Congratulations! A ship has arrived from earth. You will be taken":PRINT"home to a hero's welcome, including perhaps a ticker tape parade."
256 END
257 LB$(8)=" How Many? ":LB$(9)=" I don't understand that here. ":LOCATE 3,1:PRINT"Shall we: 1) Start a new game, or 2) Continue an old one?":GOSUB 62
258 IF NU=1 THEN LOCATE 3,1:PRINT"Please choose the level of difficulty; the range is 1 to 10." :GOTO 260
259 IF NU=2 THEN GOSUB 229:GOTO 261 ELSE BEEP:GOTO 257
260 PRINT"One is the easiest, ten the most difficult.":GOSUB 62:IF A$="" THEN HL=5:GOTO 8 ELSE IF NU<1 OR NU>10 OR INT(NU)<>NU THEN BEEP:NU=1:GOTO 258 ELSE HL=NU:GOTO 8
261 FLAG2=1:ON ERROR GOTO 267:LOCATE 22,29:COLOR 16,7:PRINT" Restoring game data ":COLOR 7,0:OPEN "COLONY.DAT" FOR INPUT AS 1:ON ERROR GOTO 0:INPUT #1,PG%,YR,UL,CP,CA,SO,GA,AO,AS,MO,MS,FO,FS,PL,FI,MI,IP,IU,DT,RT,LP(6)
262 FOR X=0 TO 5:INPUT #1,LP(X),MP(X),SS(X),T(X):NEXT
263 FOR X=1 TO 45:INPUT #1,MP%(X),ML%(X,0),ML%(X,1):NEXT:INPUT #1,MP(0),T9,LN%,HL:CLOSE:LOCATE 22,29:PRINT SPC(22):GOSUB 275:GOSUB 73:ON LN% GOTO 45,31,37,163,169,178,190,196,203
264 FLAG2=2:ON ERROR GOTO 267:LOCATE 22,29:COLOR 16,7:PRINT" Saving game data ":COLOR 7,0:OPEN "COLONY.DAT" FOR OUTPUT AS 1:ON ERROR GOTO 0:PRINT #1,PG%;YR;UL;CP;CA;SO;GA;AO;AS;MO;MS;FO;FS;PL;FI;MI;IP;IU;DT;RT;LP(6);
265 FOR X=0 TO 5:PRINT #1,LP(X);MP(X);SS(X);T(X);:NEXT:FOR X=1 TO 45:PRINT #1,MP%(X);ML%(X,0);ML%(X,1);:NEXT:PRINT#1,MP(0);T9;LN%;HL;:CLOSE:LOCATE 22,29:PRINT SPC(19)
266 ON LN% GOTO 45,31,37,163,169,178,190,196,205
267 LOCATE 22,1:PRINT SPC(79):LOCATE 21,1
268 IF ERR=70 THEN PRINT"Your diskette is write protected-use another diskette!":RESUME 274
269 IF ERR=71 THEN PRINT"Disk not ready error-perhaps disk door is open or no disk is inserted-please    check your setup before trying disk I/O again!":RESUME 274
270 IF ERR=61 THEN PRINT"Disk full error-all diskette storage space is in use-try a different diskette!":RESUME 274
271 IF ERR=67 THEN PRINT"Your diskette already contains the maximum # of files-use another diskette!":RESUME 274
272 IF ERR=53 THEN PRINT"COLONY data file not found-perhaps you have the wrong diskette inserted in the  disk drive or you have no previous game data-check for correct diskette!":RESUME 274
273 PRINT"An error occurred when trying to do disk I/O-please check setup!":RESUME 274
274 ON ERROR GOTO 0:GOSUB 230:GOSUB 230:GOSUB 230:LOCATE 21,1:PRINT STRING$(159," "):IF FLAG2=1 THEN 257 ELSE 266
275 LOCATE 23,1:PRINT STRING$(79,"_"):LOCATE 24,6:PRINT"F1=Page 1     F3=Page 3     F5=Page 5     F7=Display Map     F9=Done";:LOCATE 25,6:PRINT"F2=Page 2     F4=Page 4     F6=Page 6     F8=Save Game       F10=All";:RETURN
