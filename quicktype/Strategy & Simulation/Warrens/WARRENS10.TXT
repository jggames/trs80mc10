0 DIMC1,C2,C3,M$:M=RND(-(PEEK(9)*256+PEEK(10))):M=16384:GOTO50
3 IFTM=0THENRESET(Y,X):RETURN
4 SET(Y,X,TM):RETURN
6 PRINT@128,LEFT$(M$+"                    ",20);:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
8 C3=INT(C1*10)/10:M$=LEFT$(STR$(C3),7):RETURN
50 CLS:DIML$(16),LB$(11),S(10),M(45,3),T(6):GOSUB720:PRINT@32*6+21,"ACORN SOFT";:PRINT@32*7+22,"PRESENTS":GOSUB990
60 PRINT@8*32+20,;:M$="LOST  COLONY":GOSUB7:PRINT@9*32+21,"SIMULATION";:PRINT@32*10+25,"BY";
61 PRINT@32*11+20,"D.FEITELBERG";:GOSUB2290:GOTO2560
70 NU=INT(RND(0)*6)+11-HL:FORX=1TONU:IN=INT(RND(0)*45)+1:M(IN,2)=M(IN,2)+1:NEXT:NU=INT(RND(0)*8)+12-HL
71 FORX=1TONU:IN=INT(RND(0)*45)+1:M(IN,2)=M(IN,2)+8:NEXT
80 FORX=29TO31:IN=INT(M(X,2)/8):FI=FI+IN:MI=INT(M(X,2)-IN*8)+MI:NEXT:MS=251+INT(RND(0)*125)
81 FS=501+INT(RND(0)*150):AS=1001+INT(RND(0)*400):GA=3
90 FORX=1TO45:READM(X,0),M(X,1):M(X,0)=-M(X,0):NEXT
100 DATA1,26,1,49,6,18,4,32,3,40,6,49,10,11,7,24,7,28,6,36,7,45,7,57,13,2,13,9,13,13,10,17,12,24,10,31,10,36
101 DATA10,42,10,52,13,18,15,5,15,21,15,28,13,41,13,49,18,24,-20,30,-16,37,-18,41,17,46,21,44,16,53,21,50,19
102 DATA55,21,23,23,32,21,37,24,48,28,29,28,37,28,45,27,49,30,33
110 YR=1:SO=1+(15-INT(RND(0)*30)+1)/100:LP(6)=1701+INT(RND(0)*600):PL=LP(6)/(9.5+RND(0)):FORX=1TO5:S(X)=SO:NEXT
120 CA=1201+INT(RND(0)*400):DT=301+INT(RND(0)*50):RT=199-INT(RND(0)*30):CP=551+INT(RND(0)*300)
121 MP(0)=901+INT(RND(0)*400):LP(0)=LP(6):CLS:GOSUB720:GOSUB130:GOTO160
130 LP(0)=INT(LP(0)+.5):GOSUB2270:FL=1:GOSUB2370:GOSUB810:GOSUB610:GOSUB950:IFGO<1THEN130
140 IFNU<0THENGOSUB2340:GOTO130
141 IFNU>LP(0)THENGOSUB2350:GOTO130
142 IFINT(NU)<>NUTHENGOSUB2320:GOTO130
150 LP(0)=LP(0)-NU:LP(GO)=LP(GO)+NU:IFLP(0)>.01THEN130
151 RETURN
160 MP(0)=INT(MP(0)+.5):GOSUB810:GOSUB2270:FL=2:GOSUB2380:GOSUB610:GOSUB950:IFGO<1THEN160
170 IFNU<0THENGOSUB2340:GOTO160
171 IFNU>MP(0)THENGOSUB2350:GOTO160
172 IFINT(NU)<>NUTHENGOSUB2320:GOTO160
180 MP(0)=MP(0)-NU:MP(GO)=MP(GO)+NU:IFMP(0)>.01THEN160
181 GOSUB810
190 GOSUB2270:PRINT@96,"PRODUCTION REPORT:":IFLP(1)=0THENAO=0:T(1)=0:GOTO230
191 WM=MP(1)/LP(1):IFWM>S(1)+1.5THENWM=S(1)+1.5
200 X=1/(SQR((INT(RND(0)*50)+1)+2*GA)):IFX>.33THENX=.33:GOTO210
201 IFX<.08THENX=0
210 IFINT(RND(0)*20)+1>9THENX=-X
220 AO=(1-X)*(WM+.5+S(1))*LP(1):T(1)=AO:IFAO<LP(1)THENA9=1:M9=LP(1)-AO:GOSUB1590
230 IFLP(2)<=0ORMI=0THENMO=0:T(2)=0:GOTO260
231 GO=MI*115*(S(2)+1):IFLP(2)<GOTHENGO=LP(2)
240 WM=MP(2)/GO:IFWM>S(2)+1THENWM=S(2)+1
250 MO=(WM+.5+S(2))*GO:T(2)=MO:IFMO<LP(2)THENA9=2:M9=LP(2)-MO:GOSUB1590
260 IFLP(3)<=0ORFI=0THENFO=0:T(4)=0:GOTO290
261 GO=FI*100*(S(3)+1):IFLP(3)<GOTHENGO=LP(3)
270 WM=MP(3)/GO:IFWM>S(3)+1THENWM=S(3)+1
280 FO=(WM+.5+S(3))*GO:T(3)=FO:IFFO<LP(3)THENA9=3:M9=LP(3)-FO:GOSUB1590
290 FORPG=2TO4:GOSUB720:GOSUB2290:NEXT:GOTO410
300 MP(0)=INT(MP(0)+.5):T(4)=IU:C9=0:P1=0:GOSUB2270:FL=4:GOSUB2400
310 IFMP(0)<.01THENGOSUB2280:PRINT@32,"NOTHING REMAINS TO ASSIGN.":GOSUB2290:GOTO340
320 GOSUB610:IFA$="SAVE GAME"THENM(0,1)=2:GOTO2630
330 IFNU>MP(0)THENGOSUB2350:GOTO320
331 IFNU<0THENGOSUB2340:GOTO320
332 GOSUB950:IFGO<1THENGOSUB2330:GOTO320
333 MP(0)=MP(0)-NU:MP(GO)=MP(GO)+NU:GOSUB810:GOTO300
340 IFGA*100-(LP(5)+MP(5))*(1+S(5))<=DTTHENRT=RT+T9:GOTO350
341 DT=DT+T9
350 GOSUB2270:NU=AO+AS:IFLP(6)<=NUTHEN352
351 PRINT@32,"WE HAVE"INT(LP(6)-NU)"STARVING.":GOSUB2290:AO=0:AS=0:M9=LP(6)-NU:A9=INT(RND(0)*5)+1:GOSUB1590:GOTO360
352 AO=AO-LP(6):IFAO<0THENAS=AS+AO:AO=0
360 GOSUB810:GOSUB2270:FORX1=6TO10:S(X1)=0:NEXT
370 FL=5:GOSUB2420:GOSUB610:IFA$="DONE"THEN390
371 IFA$="SAVE GAME"THENM(0,1)=3:GOTO2630
372 IFA$="ALL"THENSO=CP/LP(6):T(5)=CP+T(5):CP=0:FORX=1TO5:S(X+5)=S(X+5)+SO*LP(X):NEXT:A$="DONE":GOTO390
380 IFNU<0THENGOSUB2340:GOTO370
381 IFNU>CPTHENGOSUB2350:GOTO370
382 GOSUB950:IFGO<1THEN370
383 S(GO+5)=S(GO+5)+NU:CP=CP-NU:T(5)=T(5)+NU
390 FORX=1TO5:IFLP(X)=0THENS(X)=0:GOTO400
391 S(X)=S(X+5)/LP(X)
400 NEXT:SO=0:FORX=1TO5:SO=SO+S(X)*LP(X):NEXT:SO=SO/LP(6):GOSUB810:IFA$="DONE"THEN1520
401 GOTO370
410 IFLP(4)<=0ORPL=0THENIP=0:GOTO300
411 IU=0:T9=0:GO=PL*2*(S(4)+1):IFLP(4)<GOTHENGO=LP(4)
420 GOSUB2270:WM=MP(4)/GO:IFWM>S(4)+2THENWM=S(4)+2
430 IP=(WM+.5+S(4))*GO
440 GOSUB2270:FL=3:GOSUB2390:GOTO460
450 PRINT@96,;M$="WE HAVE NOT MADE THAT MANY":GOSUB7:GOSUB2290:GOSUB2280
460 PG=5:GOSUB720:GOSUB610
470 IFA$="DONE"THEN300
471 IFA$="SAVE GAME"THENM(0,1)=1:GOTO2630
472 IFNU=0THENGOSUB2330:GOTO440
480 LB$(10)="ROBO":LB$(11)="TRAN":LB$(6)="PLAN":LB$(7)="CONS":GOSUB970
490 PV=0:IFGO<1THENGOSUB2330:GOTO460
491 ONGOGOTO500,530,560,580
500 IFNU<0ANDABS(NU)>MP(0)THEN450
501 MU=.9000001:GOSUB2170:IFPV=1THEN460
510 MU=.5:GOSUB2140:IFPV=1THEN460
511 MU=.1:GOSUB2190:IFPV<>1THENMP(0)=MP(0)+NU:MS=M8:MO=M9:FS=F8:FO=F9:IU=I9
520 GOTO460
530 IFNU<0ANDABS(NU)>T9THEN450
531 MU=2.5:GOSUB2170:IFPV=1THEN460
532 MU=1.2:GOSUB2220:IFPV=1THEN460
540 MU=1:GOSUB2140:IFPV=1THEN460
541 MU=1.5:GOSUB2190:IFPV<>1THENT9=T9+NU:MS=M8:MO=M9:IU=I9:FS=F8:FO=F9
550 GOTO460
560 IFNU<0ANDABS(NU)>P1THEN450
561 MU=3.4:GOSUB2170:IFPV=1THEN460
562 MU=2.5:GOSUB2140:IFPV=1THEN460
563 MU=.9000001:GOSUB2190:IFPV<>1THENP1=P1+NU:PL=PL+NU:IU=I9:MS=M8:MO=M9:FS=F8:FO=F9
570 GOTO460
580 IFNU<0ANDABS(NU)>C9THEN450
581 MU=.5:GOSUB2170:IFPV=1THEN460
582 MU=.2:GOSUB2140:IFPV=1THEN460
583 MU=.25:GOSUB2190:MU=.5:IFPV=1THEN460
590 GOSUB2240:MU=.6:IFPV=1THEN460
591 GOSUB2220:IFPV<>1THENC9=C9+NU:AO=A9:AS=A8:FO=F9:IU=I9:FS=F8:MO=M9:MS=M8:CP=CP+NU
600 GOTO460
610 A$="":GOSUB2280
620 INPUTA$:NU=0:Y=0:X1=0
630 IFA$="MAP"THENGOSUB1230:GOSUB1240:CLS:GOSUB720:ONFLGOSUB2370,2380,2390,2400,2420,2430,2440,2450,2460,2470,2480,2490:GOTO610
640 IFA$=""THENRETURN
641 FORX=LEN(A$)TO1STEP-1:P$=MID$(A$,X,1):IFP$>":"THEN650
642 IFP$>"/"ORP$="."THENNU=VAL(MID$(A$,X,LEN(A$))):GOTO650
643 IFP$>")"THENX1=X:Y=NU:NU=0
650 NEXT:IFX1>0THENONASC(MID$(A$,X1,1))-41GOSUB670,680,690,700,690,710
660 IFLEFT$(A$,4)="PAGE"ANDNU<7ANDNU>0THENPG=INT(NU):GOSUB720:GOTO610
661 RETURN
670 NU=Y*NU:RETURN
680 NU=Y+NU:RETURN
690 RETURN
700 NU=NU-Y:RETURN
710 NU=NU/Y:RETURN
720 PRINT@160,"";
721 PRINT"";
722 PRINT"";
730 PRINT@0,;:M$="  WARREN'S WORLD - LOST COLONY  ":GOSUB7:PRINT@148,"YEAR";YR:GOSUB920
731 ONPGGOTO740,750,760,770,780,790,800
740 LB$(0)="POPULATION":LB$(1)="UNEMPLOYED":LB$(2)="CONSUMER GD":LB$(3)="CASH":LB$(4)="LIVING STD":LB$(5)="AREAS":GOSUB940
741 M$="REGION SUMMARY":GOSUB6:GOTO810
750 GOSUB910:GOSUB940:M$="FARM REPORT":GOSUB6:GOTO810
760 GOSUB910:GOSUB940:M$="MINING REPORT":GOSUB6:GOTO810
770 GOSUB910:GOSUB940:M$="ENERGY REPORT":GOSUB6:GOTO810
780 GOSUB910:LB$(4)="MAX. MFG.":LB$(5)="MFG. USED":GOSUB940:M$="MANUFACT. REPORT":GOSUB6:GOTO810
790 GOSUB910:LB$(3)="TRANSPORTS":LB$(4)="EXPLORATION":LB$(5)="SETTLING"
791 GOSUB940:M$="TRANSPORT REPORT":GOSUB6:GOTO810
800 LB$(0)="FARM PROD":LB$(1)="MINE PROD":LB$(2)="ENERGY PROD":LB$(3)="MFG PROD":LB$(4)="CONS GOODS":LB$(5)="TAX BASE"
801 GOSUB940:M$="YEAR END REPORT":GOSUB6
810 ONPGGOTO820,830,840,850,860,870,880
820 LP(6)=INT(LP(6)):P(0)=LP(6):UL=INT(UL):P(1)=UL:P(2)=CP:P(3)=CA:P(4)=SO:P(5)=GA:GOSUB890:RETURN
830 LP(1)=INT(LP(1)):P(0)=LP(1):MP(1)=INT(MP(1)):P(1)=MP(1):P(2)=S(1):P(3)=GA:P(4)=AO:P(5)=AS:GOSUB890:RETURN
840 LP(2)=INT(LP(2)):P(0)=LP(2):MP(2)=INT(MP(2)):P(1)=MP(2):P(2)=S(2):P(3)=MI:P(4)=MO:P(5)=MS:GOSUB890:RETURN
850 LP(3)=INT(LP(3)):P(0)=LP(3):MP(3)=INT(MP(3)):P(1)=MP(3):P(2)=S(3):P(3)=FI:P(4)=FO:P(5)=FS:GOSUB890:RETURN
860 LP(4)=INT(LP(4)):P(0)=LP(4):MP(4)=INT(MP(4)):P(1)=MP(4):P(2)=S(4):P(3)=PL:P(4)=IP:P(5)=IU:GOSUB890:RETURN
870 LP(5)=INT(LP(5)):P(0)=LP(5):MP(5)=INT(MP(5)):P(1)=MP(5):P(2)=S(5):P(3)=DT+RT
871 P(4)=P(3)+(P(0)+P(1))*(1+S(5))-GA*100:P(5)=P(3)+(LP(5)+MP(5))*S(5)-GA*100:GOSUB890:RETURN
880 P(0)=T(1):P(1)=T(2):P(2)=T(3):P(3)=T(4):P(4)=T(5):P(5)=T(1)+T(2)+T(3)+T(4)+T(5):GOSUB890:RETURN
890 FORIN=0TO5:C1=P(IN):GOSUB8:PRINT@32*(IN+6)+12,M$;:X=INT(P(IN)*.01):IFX>58THENX=58:GOTO900
891 IFX<0THENX=0
900 PRINT@32*(IN+6)+20,LEFT$("............",X/5);:NEXT:RETURN
910 LB$(0)="LABOR":LB$(1)="ROBOTS":LB$(2)="LIVING STD":LB$(3)="PROD. SITES":LB$(4)="PRODUCTION":LB$(5)="STOCK":RETURN
920 PRINT@32*13,"page 1=REGIONS 2=FARM   3=MINING     4=ENERGY  5=MANUFA 6=TRANSP     map save game done all";:RETURN
940 FORX1=0TO5:PRINT@32*(X1+6),LB$(X1);:NEXT:RETURN
950 GO=0:FORX=1TOLEN(A$):M$=MID$(A$,X,4):IFM$="FARM"THENGO=1
951 IFM$="MINI"THENGO=2
952 IFM$="ENER"THENGO=3
953 IFM$="MANU"THENGO=4
954 IFM$="TRAN"THENGO=5
960 NEXT:IFGO>0THENPG=GO+1:GOSUB720:RETURN
961 GOSUB2330:RETURN
970 GO=0:FORX=1TOLEN(A$):M$=MID$(A$,X,4):IFM$=LB$(10)THENGO=1
971 IFM$=LB$(11)THENGO=2
972 IFM$=LB$(6)THENGO=3
973 IFM$=LB$(7)THENGO=4
980 NEXT:RETURN

990 L$(1)="߀":L$(2)="ϟ߀":L$(3)="ϟ"
1020 L$(4)="":L$(5)="ߏϏό":L$(6)="ߟϏπ"
1050 L$(7)="":L$(8)="쌏":L$(9)=""
1080 L$(10)="ϟ":L$(11)="ߟ"
1081 L$(12)="ߟ߀":L$(13)=""
1082 L$(14)="ӀÀ":L$(15)="Ϗ߀Ȁ"
1140 L$(16)="":RETURN

1230 CLS0:FORI=1TO16:PRINT@(I-1)*32,L$(I);:NEXT:RETURN
1240 PRINT@416,;:M$="S=SETTLED":GOSUB7:PRINT@448,;:M$="R=RESOURCES":GOSUB7:PRINT@480,;:M$="E=EXIT MAP":GOSUB7
1250 GOSUB2300:IFA$="E"THENRETURN
1251 IFA$="R"THEN1260
1252 IFA$="S"THEN1290
1253 SOUND1,1:GOTO1250
1260 GOSUB1230:PRINT@0,;:M$="RESOURCE":GOSUB7:PRINT@32,;:M$="SITES":GOSUB7:FORX=1TO45:IFM(X,0)<0THEN1280
1261 IN=M(X,2):IFIN>7THENRESET(ABS(M(X,1)),ABS(M(X,0)))
1270 IN=IN-INT(IN/8)*8:IFIN>0THENRESET(ABS(M(X,1)),ABS(M(X,0)))
1280 NEXT:GOSUB2290:GOTO1240
1290 GOSUB1230:PRINT@0,;:M$="SETTLED":GOSUB7:PRINT@32,;:M$="AREAS":GOSUB7:FORIN=1TO45:IFM(IN,0)<=0THEN1300
1291 RESET(ABS(M(IN,1)),ABS(M(IN,0)))
1300 NEXT:GOSUB2290:GOTO1240

1310 FORIN=1TO45:IFM(IN,0)<0THENRESET(ABS(M(IN,1)),ABS(M(IN,0)))
1320 NEXT

1330 X=16:Y=32:TM=POINT(Y,X):SOUND100,1
1340 SET(Y,X,0):RESET(Y,X):FORC1=1TO25:NEXT:B=PEEK(2)ANDPEEK(17023):GOSUB3:FORC1=1TO25:NEXT:IFB=.THEN1340
1341 IFB=13THEN1500
1342 IFB<>65ANDB<>83ANDB<>90ANDB<>87THEN1340
1360 IFB=87THEN1370
1361 IFB=90THEN1400
1362 IFB=83THEN1430
1363 GOTO1470
1370 IFX=0THENX=32
1380 X=X-1:TM=POINT(Y,X):IFTM=0THENSOUND1,1
1390 GOTO1340
1400 IFX=31THENX=0
1410 X=X+1:TM=POINT(Y,X):IFTM=0THENSOUND1,1
1420 GOTO1340
1430 IFY=63THENY=0
1450 Y=Y+1:TM=POINT(Y,X):IFTM=0THENSOUND1,1
1460 GOTO1340
1470 IFY=0THENY=64
1480 Y=Y-1:TM=POINT(Y,X):IFTM=0THENSOUND1,1
1490 GOTO1340
1500 A9=0:FORX1=1TO45:IFX=ABS(M(X1,0))ANDY=ABS(M(X1,1))THENIN=M(X1,2):A9=X1:X1=45:SOUND100,1
1510 NEXT:RETURN

1520 M9=0:M8=0:IN=0:P=10000:FORX=1TO5:IFIN<S(X)THENIN=S(X):M8=X
1530 IFP>S(X)ANDLP(X)>0THENP=S(X):A9=X
1540 IFS(X)<YR*(.011+4.400001E-02*(HL/5))+.1THENM9=M9+LP(X)
1550 NEXT:IFIN-P>SO/2THENGO=INT(LP(A9)*(1+IN-P)*10)/10:IFGO>M9THENM9=GO
1560 IFM9<=0THEN1620
1561 X=INT(RND(0)*12)+1:M9=M9*X/10:C9=INT(X*CP/(INT(RND(0)*6)+1)+M9):IFM9+UL>LP(6)/3THEN2500
1562 FL=6:GOSUB2430
1570 GOSUB610:IFLEFT$(A$,1)="Y"THEN1580
1571 IFLEFT$(A$,1)="N"THENGOSUB1590:GOTO1620
1572 GOSUB2330:GOTO1570
1580 CP=CP-C9:GOSUB810:GOTO1620
1590 IFM9<=LP(A9)THENLP(A9)=INT((LP(A9)-M9)*10)/10:UL=UL+INT(M9*10)/10:M9=0:RETURN
1600 PV=0:X=INT(M9*10)/50:FORP=1TO5:IFLP(P)>=XTHENLP(P)=LP(P)-X:M9=M9-X:PV=1:UL=UL+X
1610 NEXT:IFM9>1ANDPV=1THEN1600
1611 IFM9>1THEN2500
1612 RETURN
1620 IFCP<=0THEN1680
1621 GOSUB2270:FL=7:GOSUB2440:GOSUB610:IFA$="SAVE GAME"THENM(0,1)=4:GOTO2630
1622 IFLEFT$(A$,1)="N"THEN1680
1630 GOSUB810:FL=8:GOSUB2450:GOSUB610:IFA$="DONE"THEN1680
1631 IFNU<1THENGOSUB2320:GOTO1630
1632 IFNU>CPTHENGOSUB2350:GOTO1630
1633 GOSUB950
1640 IFGO=0THEN1630
1641 CO=UL:A9=S(1)
1650 IFUL>0THEN1655
1651 GOTO1660
1655 IFUL>=NUTHENUL=UL-NU:LP(GO)=LP(GO)+NU:CP=CP-NU:T(5)=T(5)+NU:GOTO1630
1656 CP=CP-(NU-UL):NU=UL:GOTO1650
1660 M9=NU+(S(GO)-SO)*125:IFM9<1THEN1630
1661 CP=CP-NU:T(5)=T(5)+NU:FORX1=1TO5:IFA9>S(X1)ANDLP(X1)>0THENA9=S(X1)
1670 NEXT:GOSUB1590:LP(GO)=LP(GO)+UL-CO:UL=CO:GOTO1630
1680 GOSUB2270:FL=9:GOSUB2460
1690 GOSUB610:IFA$="DONE"THEN1770
1691 IFA$="SAVE GAME"THENM(0,1)=5:GOTO2630
1692 IFA$="ALL"THEN1700
1693 IFNU<0THENGOSUB2340:GOTO1690
1694 GOTO1730
1700 NU=AO:GOSUB1740:IFMO>.04THENNU=MO:GOSUB1750
1710 IFFO>.04THENNU=FO:GOSUB1760
1720 GOTO1770
1730 GOSUB950:IFGO<1ORGO>3THENGOSUB2330:GOTO1690
1731 ONGOGOSUB1740,1750,1760,:GOTO1690
1740 IFNU>AO+.04THENPRINT@96,;:M$="ONLY"+STR$(INT(AO*100)/100)+" REMAIN":GOSUB7:GOSUB2290:PRINT@96,"":GOTO1690
1741 CA=CA-NU:AO=AO-NU:AS=AS+NU:GOSUB810:RETURN
1750 IFNU>MO+.04THENPRINT@96,;:M$="ONLY"+STR$(INT(MO*100)/100)+" REMAIN":GOSUB7:GOSUB2290:PRINT@96,"":GOTO1690
1751 CA=CA-300-NU:MO=MO-NU:MS=MS+NU:GOSUB810:RETURN
1760 IFNU>FO+.04THENPRINT@96,;:M$="ONLY"+STR$(INT(FO*100)/100)+" REMAIN":GOSUB7:GOSUB2290:PRINT@96,"":GOTO1690
1761 CA=CA-200-NU:FO=FO-NU:FS=FS+NU:GOSUB810:RETURN
1770 GOSUB2270:FL=10:GOSUB2470:GOSUB610:IFA$="SAVE GAME"THENM(0,1)=6:GOTO2630
1780 IFNU>0ANDNU<=75THENNU=NU/100:GOSUB2280:GOTO1790
1781 PRINT@96,;:M$="ONLY 0%-75% IS ENFORCEABLE":GOSUB7:GOSUB2290:PRINT@96,"":GOTO1770
1790 XX=NU-.7+(INT(RND(0)*15)+INT(RND(0)*15)+2)/100:M9=0:FORP=1TO5:M9=M9+T(P):NEXT:M9=INT(M9*NU*100)/100
1800 IFCA<0THEN?@32,"INTEREST PAYMENT $";MID$(STR$(INT(.2*ABS(CA)*100)/100),2):GOSUB2290:M9=M9-INT(.2*ABS(CA)*100)/100
1810 GOSUB2270:PRINT@32,"TO THE TREASURY $";MID$(STR$(M9),2):CA=CA+M9:PG=1:GOSUB720:GOSUB2290
1820 GOSUB2270:PRINT@32,"POLLING CAPTAINS OF INDUSTRY TO DETERMINE DEPRECIATION LOSSES."
1821 FORX=1TO5:IN=((INT(RND(0)*17)+1)/100)+XX:IFIN>0THENMP(X)=MP(X)-MP(X)*IN
1830 NEXT:IN=((INT(RND(0)*25)+1)/100)+XX:IFIN>0THENDT=DT-DT*IN
1840 IN=((INT(RND(0)*35)+1)/100)+XX:IFIN>0THENAS=AS-AS*IN
1850 IN=((INT(RND(0)*22)+1)/100)+XX:IFIN>0THENMS=MS-MS*IN
1860 IN=((INT(RND(0)*18)+1)/100)+XX:IFIN>0THENPL=PL-PL*IN
1870 IN=((INT(RND(0)*22)+1)/100)+XX:IFIN>0THENFS=FS-FS*IN
1880 GOSUB2290:IN=((INT(RND(0)*27)+1)/100)+XX:IFIN>0THENCP=CP-CP*IN
1890 M9=GA*100:GOSUB2270:IFM9>=RT+DT+(LP(5)+MP(5))*(1+S(5))THEN1950
1891 FL=11:GOSUB2480:GOSUB610:IFA$="SAVE GAME"THENM(0,1)=7:GOTO2630
1892 IFLEFT$(A$,1)="N"THEN1960
1893 IFLEFT$(A$,1)<>"Y"THEN1890
1900 PV=0:GOSUB1230:PRINT@0,;:M$="EXPLORE":GOSUB7:PRINT@32,;:M$="USE: AWSZ":GOSUB7:GOSUB1310
1901 IFA9=0THENPRINT@480,;:M$="BAD ENTRY":GOSUB7:SOUND1,1:GOSUB2290:PRINT@480,"";:GOTO1900
1910 M9=M9+100:IFIN>7ANDINT(RND(0)*20)+1<18THENPRINT@416,;:M$="ENERGY RES.":GOSUB7:PV=1
1920 IN=IN-INT(IN/8)*8:IFIN>0ANDINT(RND(0)*20)+1<19THENPRINT@448,;:M$="MINING RES.":GOSUB7:PV=1
1930 IFM(A9,2)=0ORPV=0THENPRINT@416,;:M$="EXPLORERS":GOSUB7:PRINT@448,;:M$="FOUND NOTHING":GOSUB7
1940 GOSUB2360:A$=INKEY$:GOSUB2300:IFA$="R"ORM9>=RT+DT+(1+S(5))*(LP(5)+MP(5))THEN1950
1941 GOTO1900
1950 CLS:GOSUB720
1960 IFGA*100>=RT+DT+(LP(5)+MP(5))*S(5)THEN2010
1961 GOSUB2270:FL=12:GOSUB2490:GOSUB610:IFA$="SAVE GAME"THENM(0,1)=8:GOTO2630
1962 IFLEFT$(A$,1)="N"THEN2010
1963 IFLEFT$(A$,1)<>"Y"THEN1960
1970 GOSUB1230:PRINT@0,;:M$="SETTLING":GOSUB7:PRINT@32,;:M$="USE: AWSZ":GOSUB7:GOSUB1310
1971 IFA9=0THENPRINT@480,;:M$="BAD ENTRY":GOSUB7:SOUND1,1:GOSUB2290:PRINT@480,"";:GOTO1970
1980 M(A9,0)=ABS(M(A9,0)):GA=GA+1:FI=FI+INT(IN/8):IN=IN-INT(IN/8)*8:MI=MI+INT(IN)
1981 IFDT<GA*100ANDRT>=100THENDT=DT+100:RT=RT-100
1990 GOSUB2360:A$=INKEY$:GOSUB2300:IFA$<>"R"ANDGA*100<RT+DT+S(5)*(LP(5)+MP(5))THEN1970
2000 CLS
2010 PRINT@96,"":IFYR=INT(YR/3)*3THENLP(0)=INT(LP(6)*5/3)/10:LP(6)=LP(6)+LP(0):GOSUB2270:GOSUB720:GOSUB130
2020 GOSUB2270:PG=7:GOSUB720
2030 PRINT@32,"SAVE THE GAME NOW (Y/N)?";:GOSUB2300:IFA$="Y"THENPRINTA$:M(0,1)=9:GOTO2630
2031 IFA$="N"THENPRINTA$:GOTO2040
2032 GOTO2030
2040 GOSUB2270:PRINT@96,;:M$="HIT ANY KEY TO CONTINUE":GOSUB7:GOSUB2300
2050 IFINT(RND(0)*25)+1<INT(22-HL/3)THEN2130
2051 GOSUB2270:PV=0:ONINT(RND(0)*4)+1GOTO2060,2070,2080,2090
2060 PRINT@32,"VERMIN DESTROYED FOOD & PROPERTY";:AS=.83*AS:CP=.9000001*CP:GOTO2100
2070 PRINT@32,"EPIDEMIC! INHABITANTS WERE LOST.";:PV=3:GOTO2100
2080 PRINT@32,"FLASH FLOODS! ROADS WASHED OUT!":DT=DT-(INT(RND(0)*ABS(DT))+1)/(INT(RND(0)*2)+1):GOTO2100
2090 PRINT@32,"EXPLOSION AT LARGEST FACTORY!":PL=PL-INT(RND(0)*(PL/GA)+1)
2100 GOSUB2290:GOSUB2290:IFPV=0THEN2130
2110 X1=INT(RND(0)*GA)+1:FORIN=1TO45:IFM(IN,0)<0THEN2120
2111 X1=X1-1:IFX1=0THEN2115
2112 GOTO2120
2115 M(IN,0)=-M(IN,0):GA=GA-1:A9=INT(RND(0)*5)+1:A8=UL:M9=INT(RND(0)*(LP(6)/GA))+1:GOSUB1590
2116 UL=A8:A8=INT(M(IN,2)/8):FI=FI-A8:MI=MI-INT(M(IN,2)-A8*8)
2120 NEXT:IFGA<1THEN2500
2130 T(5)=0:IFYR<12+INT(RND(0)*9)THENYR=YR+1:LP(6)=INT(UL*10)/10:FORX=1TO5:LP(6)=LP(6)+INT(LP(X)*10)/10:NEXT:GOTO190
2131 GOTO2540
2140 M9=MU*NU:M8=MS:IFM9<=MOTHENM9=MO-M9:RETURN
2150 M8=M9-MO:IFM8<=MSTHENM8=MS-M8:M9=0:RETURN
2160 PRINT@96,;:M$="INSUFFICIENT MINERAL RESERVES":GOSUB7:GOSUB2290:PRINT@96,"":PV=1:RETURN
2170 I9=IU+MU*NU:IFI9>IPTHENPRINT@96,;:M$="MANUFACTURERS CAN'T COMPLY":GOSUB7:GOSUB2290:PRINT@96,"":PV=1
2180 RETURN
2190 F9=MU*NU:F8=FS:IFF9<=FOTHENF9=FO-F9:RETURN
2200 F8=F9-FO:IFF8<=FSTHENF8=FS-F8:F9=0:RETURN
2210 PRINT@96,;:M$="WE LACK THE ENERGY":GOSUB7:GOSUB2290:PRINT@96,"":PV=1:RETURN
2220 CA=CA-MU*NU:IFCA<0THENCA=CA+MU*NU:PRINT@96,;:M$="WE CANNOT FINANCE THAT":GOSUB7:GOSUB2290:PRINT@96,"":PV=1
2230 RETURN
2240 A9=MU*NU:A8=AS:IFA9<=AOTHENA9=AO-A9:RETURN
2250 A8=A9-AO:IFA8<=ASTHENA8=AS-A8:A9=0:RETURN
2260 PV=1:PRINT@96,;:M$="THERE IS NOT ENOUGH FOOD":GOSUB7:GOSUB2290:RETURN
2270 PRINT@32,"":PRINT@64,"":RETURN
2280 PRINT@96,"":PRINT@96,;:RETURN
2290 FORX=1TO2500:NEXT:RETURN
2300 A$=INKEY$:IFA$=""THEN2300
2301 IFASC(A$)>90THENA$=CHR$(ASC(A$)-32)
2310 RETURN
2320 PRINT@96,;:M$=LB$(8)
2321 GOSUB7:SOUND1,1:GOSUB2290:PRINT@96,"":RETURN
2330 PRINT@96,;:M$=LB$(9):GOTO2321
2340 PRINT@96,;:M$="NEGATIVE #S NOT ALLOWED.":GOTO2321
2350 PRINT@96,;:M$="THERE ARE NOT ENOUGH.":GOTO2321
2360 PRINT@480,;:M$="R=RETURN, OR ANY OTHER KEY":GOSUB7:RETURN
2370 PRINT@32,"HOW DISTRIBUTE LABOR?":PRINTMID$(STR$(LP(0)),2)" JOB SEEKERS.":RETURN
2380 PRINT@32,"WHERE EMPLOY ROBOTS?":PRINTMID$(STR$(MP(0)),2)" ALL PURPOSE MACHINES.":RETURN
2390 PRINT@32,"DESCRIBE MANUFACTURING.":RETURN
2400 PRINT@32,"YOU HAVE ";:IFMP(0)>=.01THENPRINTMID$(STR$(MP(0)),2)" ROBOT";:IFMP(0)>1THENPRINT"S";
2410 PRINT" TO ASSIGN.":RETURN
2420 PRINT@32,"DESCRIBE CONSUMER GOODS":PRINT"DISTRIBUTION.":RETURN
2430 PRINT@32,MID$(STR$(INT(M9+.5)),2)" THREATEN TO STRIKE.":PRINT"THEY WANT"C9"IN GOODS. GRANT?":RETURN
2440 PRINT@32,"REALLOCATE LABOR?":RETURN
2450 PRINT@32,"HOW MANY CONSUMER GOODS WILL YOUOFFER AND FOR WHAT SECTOR?":RETURN
2460 PRINT@32,"DESCRIBE STOCKPILING.":RETURN
2470 PRINT@32,"TAX RATE FOR THIS YEAR?":RETURN
2480 PRINT@32,"EXPLORE NEW TERRITORY?":RETURN
2490 PRINT@32,"SETTLE NEW TERRITORY?":RETURN
2500 GOSUB2270:GOSUB2280:PRINT@32,MID$(STR$(INT(100*(M9+UL)/LP(6))),2);"% OF POP. DEMAND YOU RESIGN"
2510 PRINT"BECAUSE OF ";:IFSO<YR*(.011+4.400001E-02*(HL/5))+.1THENPRINT"MISMANAGEMENT.":GOTO2520
2511 PRINT"UNFAIR PAY."
2520 PRINT"PERHAPS YOU ARE GOOD WITH A HOE?";
2530 M$="":GOSUB6:PRINT@128,;:INPUT"TRY AGAIN";M$:IFLEFT$(M$,1)="Y"THENRUN
2531 END
2540 GOSUB2270:GOSUB2280:PRINT@32,"A SHIP HAS ARRIVED FROM EARTH!. YOU'RE GOING HOME A HERO!"
2550 GOTO2530
2560 LB$(8)="HOW MANY?":LB$(9)="I DON'T UNDERSTAND THAT HERE"
2561 PRINT@32,"1) NEW GAME, OR 2) OLD?":GOSUB610
2570 IFNU=1THENPRINT@32,"LEVEL OF DIFFICULTY (1 TO 10)?":GOTO2590
2580 IFNU=2THENGOSUB2280:GOTO2600
2581 SOUND1,1:GOTO2560
2590 PRINT"1=EASIEST, 10=MOST DIFFICULT.":GOSUB610:IFA$=""THENHL=5:GOTO70
2591 IFNU<1ORNU>10ORINT(NU)<>NUTHENSOUND1,1:NU=1:GOTO2570
2592 HL=NU:GOTO70
2600 PRINT@96,;:M$="RESTORING GAME DATA":GOSUB7:PRINT:CLOAD*M,"COLONYDA"
2601 PG=M(7,3):YR=M(8,3):UL=M(9,3):CP=M(10,3):CA=M(11,3):SO=M(12,3):GA=M(13,3):AO=M(14,3):AS=M(15,3):MO=M(16,3):MS=M(17,3)
2602 FO=M(18,3):FS=M(19,3):PL=M(20,3):FI=M(21,3):MI=M(22,3):IP=M(23,3):IU=M(24,3):DT=M(25,3):RT=M(26,3):LP(6)=M(6,3)
2610 FORX=0TO5:LP(X)=M(X,3):MP(X)=M(27+X,3):S(X)=M(33+X,3):T(X)=M(39+X,3):NEXT:HL=M(0,0):T9=M(45,3)
2620 GOSUB2270:GOSUB2280:GOSUB720:ONM(0,1)GOTO440,300,360,1620,1680,1770,1890,1950,2020
2630 PRINT@96,;:M$="SAVING GAME DATA":GOSUB7:PRINT
2631 M(7,3)=PG:M(8,3)=YR:M(9,3)=UL:M(10,3)=CP:M(11,3)=CA:M(12,3)=SO:M(13,3)=GA:M(14,3)=AO:M(15,3)=AS:M(16,3)=MO:M(17,3)=MS
2640 M(18,3)=FO:M(19,3)=FS:M(20,3)=PL:M(21,3)=FI:M(22,3)=MI:M(23,3)=IP:M(24,3)=IU:M(25,3)=DT:M(26,3)=RT:M(6,3)=LP(6)
2641 FORX=0TO5:M(X,3)=LP(X):M(27+X,3)=MP(X):M(33+X,3)=S(X):M(39+X,3)=T(X):NEXT:M(0,0)=HL:M(45,3)=T9
2650 CSAVE*M,"COLONYDA":GOSUB2270:GOSUB2280:ONM(0,1)GOTO440,300,360,1620,1680,1770,1890,1950,2040
