0 CLEAR500:DIMX,J,M,M$,I$:M=16384:X=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 J=1:X=32:FORX=XTOJSTEP-1:I$=MID$(M$,X,1):IFI$<"!"THEN?MID$(M$,J,X-J):J=X+1:X=J+32:IFI$=""THENX=.
2 X=X+(X>255)*(X-255):NEXT:M$="":RETURN
5 M$="PRESS ANY KEY.":GOSUB7
6 ON-(INKEY$="")GOTO6:CLS:RETURN
7 X=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORJ=1TOLEN(M$):POKEX+J,ASC(MID$(M$,J))AND63:NEXT:?@X-M+J,:RETURN
20 MZ=0
30 DIME$(4):E$(1)="YOU ARE UNHARMED AND RARING TO GO...!"
31 E$(2)="YOU ARE SLIGHTLY WOUNDED NOW. BE BRAVE!"
32 E$(3)="YOU HAVE RECEIVED MULTIPLE LACERATIONS."
33 E$(4)="IT'S YOUR LAST CHANCE NOW. GOOD LUCK..."
100 DIMV(9)
110 FORJ=1TO9:READV(J):NEXTJ
120 DATA 3,4,3,4,2,5,2,2,4
200 DIMV$(9)
210 FORK=1TO9:READV$(K):NEXT
220 DATA"GANGRENE GOAT","NORTH-SEA BOIL","VAMPIRE CAT"
230 DATA"POLITICAL WORM","MAD METER-MAID","SCHOOL SPINACH"
240 DATA"DANDRUFFED BEAR","RAMPANT BANKER","PICKLED ALBATROSS"
300 CLS:PRINTTAB(11)"swordsman"
310 M$="IN THIS GAME, YOU ARE TO BE PUT AGAINST A HORDE OF TERRIFYING MONSTERS. YOU ARE ARMED ONLY WITH A SWORD.":GOSUB1
340 PRINT"YOUR CONTROLS ARE:"
341 M$="A  MOVE TO THE LEFT ":GOSUB7:PRINT
350 M$="D  MOVE TO THE RIGHT":GOSUB7:PRINT
360 M$="S  LUNGE WITH SWORD ":GOSUB7:PRINT
370 M$="THERE ARE TWO SWORD POSITIONS. THE FIRST TIME YOU PRESS 'S', YOU PUT YOUR SWORD 'ON-GUARD'.":GOSUB1
400 M$="THE SECOND KEY-PRESS COMMITS YOU TO A SWORD THRUST.":GOSUB1:GOSUB5
420 M$="SOME MONSTERS ARE HARDER TO KILL THAN OTHERS. THE DREADED 'SCHOOL SPINACH' IS PARTICULARLY NASTY TO MEET.":GOSUB1
440 M$="YOU GET MORE POINTS IF YOU KILL THE MONSTER FURTHER TO THE RIGHT OF THE SCREEN.":GOSUB1
470 M$="EACH TIME YOU WIN, YOUR WOUNDS ARE SOOTHED JUST A LITTLE.":GOSUB1
480 M$="IF YOU ARE WOUNDED MORE THAN 4 TIMES THEN YOU ARE DEFINITELY   D E A D!":GOSUB1:GOSUB5
500 DIMD$(4,3),M$(9,4,3)
510 FORI=1TO4:FORJ=1TO3:READD$(I,J)
530 NEXT:NEXT
550 REM THESE DATA LINES HOLD VARIOUS STANCES OF THE SWORDSMAN
600 DATA " 0!","<€+"," ))"
602 DATA " 0!","<€+","/)"
604 DATA " 0 /","<€+"," X"
606 DATA "(0"," €-+-","/ )"
650 FORK=1TO9:FORJ=1TO4:FORI=1TO3
660 READM$(K,J,I):IFRIGHT$(M$(K,J,I),1)="!"THENM$=M$(K,J,I):X=LEN(M$):M$(K,J,I)=LEFT$(M$,X-2)+CHR$(92)+CHR$(92)
661 IFRIGHT$(M$(K,J,I),1)="L"THENM$=M$(K,J,I):X=LEN(M$):M$(K,J,I)=LEFT$(M$,X-1)+CHR$(92)
680 NEXT:NEXT:NEXT
699 REM LINES 700-876 CONTAIN STANCES OF THE MONSTERS WHO OPPOSE THE SWORDSMAN
700 DATA " @"," ïï'"," < >"
702 DATA " @"," ïï'"," <!!"
704 DATA "","@ïï'","/ !!"
706 DATA "@  /"," ïïL"," ("
720 DATA " ***","*@+@*","*:::*"
722 DATA " ***","*@+@*","*'''*"
723 DATA ""," ***","*@+@*"
724 DATA " ***","*@+@*","*:::*"
740 DATA "","VŒ/"," X"
742 DATA "","VŒ/"," <<"
744 DATA "VŒ-"," !!",""
746 DATA "VŒ)","//",""
760 DATA "","@O  O ","  OO O"
762 DATA "","@  OO"," OO  O"
764 DATA "","  OO O","@O  O"
766 DATA "","   O","@OO OO"
780 DATA "$$$$$","$O.O$","$ + $"
782 DATA "$$$$$","$O.O$","$ = $"
784 DATA "$$$$$","$O.O$","$ - $"
786 DATA "$$$$$","$O.O$","$ * $"
800 DATA "## #","#O##"," ##"
802 DATA "####"," O###","  #"
804 DATA "###"," ##"," O####"
806 DATA "###","## ##"," #O"
820 DATA "#ýý#."," ÿÿ  "," <> "
822 DATA "#ýý# "," ÿÿ ."," << "
824 DATA "#ýý# "," ÿÿ  "," >< ."
825 DATA "#ýý# "," ÿÿ  "," >> "
840 DATA " 0","-€-"," X"
842 DATA " 0","/€L","( )"
844 DATA " 0","(€)","< >"
846 DATA " 0",")€(","/ L"
860 DATA "  // ","-O€-<",""
862 DATA "-O€-<","  !!",""
864 DATA "  // ","-O€-<",""
866 DATA "-O€-<","  !!",""
940 REM START OF GAME
950 REM VK=NUMBER OF DEAD MONSTERS
960 REM WK=WOUNDED LEVEL
970 REM PK=POINTS SCORED
980 VK=0:WK=1:MZ=0:MR=0:D8=0:D7=0:M6=0:M7=0:M8=0:D9=0:M9=0:P5=0:HS=PK*-(PK>HS)+HS*-(HS>=PK):PK=0:CLS
1000 GOSUB2800
1100 GOSUB4000
1900 GOTO1000
2750 FORJ=1TO2000:NEXT:RETURN
2760 PRINT@352,;:M$="THE "+V$(MZ)+" CUT YOU UP!       ":GOSUB1
2765 FORI=1TO1500:NEXTI:RETURN
2799 REM CLEAR TOP SCREEN
2800 PRINT@0,"                                ";
2801 PRINT"ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßŸßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";
2802 FORX=128TO256STEP32:PRINT@X,"                                ";:PRINT@RND(96)+31,"Ï";:NEXT
2820 PRINT@RND(96)+31,"Ï";:PRINT@RND(96)+31,"Ï";:PRINT@0,;:RETURN
2950 MR=MZ
2960 MZ=RND(9):IFMZ=MRTHEN2960
2970 RETURN
4000 GOSUB2950:REM MONSTER BATTLE
4002 PRINT@288,"ÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿ";
4005 M$="IT'S THE "+V$(MZ)+"!":GOSUB1
4006 M$="(DEGREE OF DIFFICULTY="+STR$(V(MZ))+")":GOSUB1
4007 M$=E$(WK):GOSUB1
4010 M7=4:D7=2:M8=20+RND(5)
4011 D8=12:M6=4:D6=2:M9=M8:D9=D8
4015 PRINT@0,"POINTS";PK;TAB(16)"HIGH"HS
4020 K1=ASC(INKEY$+"."):IFK1=65THEND8=D8-1:D7=1:IFD8<4THEND8=4
4030 IFK1=68THEND8=D8+1:D7=2:IFD8>27THEND8=27
4035 IFK1=83THEND7=D7+1:IFD7>3THEND7=4:D8=D8+1:IFD8>27THEND8=27
4036 IFK1=83ANDD7=2THEND7=3
4037 PRINT@191+D9,"     ";:PRINT@223+D9,"     ";:PRINT@255+D9,"     ";
4038 K3=192+D9:FORX=1TO3:PRINT@K3,D$(D7,X):K3=K3+32:NEXT:O9=D9:O7=D7:D9=D8:D6=D7:IFD7=4THEN4060
4050 GOTO4085
4060 K3=192+M9:FORX=1TO3:PRINT@K3,M$(MZ,M6,X)" ";:K3=K3+32:NEXT
4065 K3=225+M+D8+2:Z1=PEEK(K3):IFZ1<>96ANDZ1<>107ANDZ1<>109THEN4090
4070 PRINT@225+D8,"-+-";:D7=2
4085 M7=M7+1:IFM7=5THENM7=1
4086 IFRND(.)>.35THENM8=M8-1
4088 K3=192+M8:FORX=1TO3:PRINT@K3,M$(MZ,M6,X)" ";:K3=K3+32:NEXT:IFM8<D8+2THEN4100
4089 M9=M8:M6=M7:GOTO4020
4090 PRINT@225+D8,"-+¿";:SOUND100,2:PRINTCHR$(8)"-";
4091 K3=191+O9+64:X=3:FORJ=1TO4:IFPEEK(M+K3+J)=96THENPRINT@K3+J,MID$(D$(O7,X),J,1);
4092 NEXT:PRINT@352,;:M$="WELL DONE! YOU GOT THE "+V$(MZ)+".":GOSUB1
4093 P5=INT(V(MZ)*D8):VK=VK+1:WK=WK-1:IFWK<1THENWK=1
4094 PRINT@0,"POINTS FOR KILL =";P5:PK=PK+P5:GOSUB2750:GOSUB2800:RETURN
4100 K3=191+O9:FORX=1TO3:FORJ=1TO3:IFPEEK(M+K3+J)=96THENPRINT@K3+J,MID$(D$(O7,X),J,1);
4101 NEXT:K3=K3+32:NEXT:WK=WK+1:IFWK<5THENPRINT@0,"TRY AGAIN...":GOSUB2760:GOSUB2800:GOTO4002
4105 POKE49151,64:PRINT@352,"":PRINT@352,;:FORI=1TO3:PRINT"OH DEAR...";
4107 FORJ=1TO999:NEXTJ:NEXTI:PRINT:SOUND1,1
4110 PRINT"AAAAAAAAAAARGH!"
4120 PRINT"ANOTHER HERO BITES THE DUST."
4125 M$="YOU KILLED"+STR$(VK)+" MONSTERS SCORING   ":GOSUB7
4126 PRINT@480,;:M$=LEFT$(MID$(STR$(PK),2)+" PTS. ANOTHER GO? (Y/N)           ",31):GOSUB7:POKEM+511,32
4130 A$=INKEY$:IFA$=""THEN4130
4133 IFA$="N"THENEND
4134 IFA$="Y"THEN980
4135 GOTO4130
5000 REM **********************
5010 REM .SWORDSMAN.....WALWYN.
5020 REM MC-10 VER.  JIM GERRIE
