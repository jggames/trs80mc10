0 CLS:CLEAR650:DIMCC,ZZ,Z$,I$:GOTO2005
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1)
2 IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
3 C1=((PEEK(17024)AND1)*256+PEEK(17025))+1:IFC1>479THENGOSUB5
4 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 PRINT"hit any key";
6 IFINKEY$=""THEN6
7 CLS:RETURN

10 ZZ=(1-(R-500)^2/40000)*SIN((3.1415926*ABS(90-ABS(CC))/180))*(3-INT(ABS(CC/90)))/3:RETURN
11 ZZ=(1-(R-200)^2/40000)*SIN((3.1415926*ABS(90-ABS(CC))/180))*(5-INT(ABS(CC/90)))/5:RETURN
12 ZZ=INT(ABS(CC/90)):RETURN
13 ZZ=LEN(Z$):PRINTTAB((32-ZZ)/2)Z$:RETURN

2005 C1=RND(-(PEEK(9)*256+PEEK(10)))
2010 DIMN$(16),O$(19),Z$(19):F1=0
2170 FORI=1TO8
2180 READL$(I)
2190 NEXTI
2200 DATA"BETA AURIGAE 5","GAMMA TRISKELLION 7"
2210 DATA"EPSILON CYGNI 2","ALPHA ARCTURUS 4"
2220 DATA"DELTA CANOPIS 12","OMICRON BETELGUESE 27"
2230 DATA"SIGMA RIGEL 3","KAPPA SIRIUS 8"
2240 FORI=1TO16
2250 READN$(I)
2260 NEXTI
2270 DATAENTERPRISE,VALIANT,HOOD,FREE-ENTERPRISE
2280 DATADEFIANT,GRAPEFRUIT,EXCALIBUR,EXETER
2290 DATALEXINGTON,YORKTOWN,REPUBLIC,CONSTITUTION
2300 DATAFARRAGUT,KONGO,CONSTELLATION,GEORGE
2310 FORI=1TO3
2320 READK$(I)
2330 NEXTI
2340 DATAKLINGON,ROMULAN,THOLIAN
2350 FORI=1TO5
2360 READH$(I)
2370 NEXTI
2380 DATAKRULIX,GRANAK,SLORK,QUARLO,TROBLAK
2390 FORI=1TO5
2400 READW$(I)
2410 NEXTI
2420 DATATRAKKA,KURDA,VARNOR,GRANOLT,SHLURG
2430 FORI=1TO19
2440 READO$(I)
2450 NEXTI
2460 DATA"ENEMY RANGE AND BEARING"
2470 DATA"FIRE FORWARD PHASERS","FIRE REAR PHASERS"
2480 DATA"FIRE FORWARD PHOTON TORPEDOES","FIRE REAR PHOTON TORPEDOES"
2490 DATA"LAUNCH ANTIMATTER PROBE","CLOSE ON ENEMY VESSEL"
2500 DATA"MOVE AWAY FROM ENEMY","CLOSE AT WARP SPEED"
2510 DATA"ESCAPE AT WARP SPEED"
2520 DATA"OPTIMUM SHIELD DEFENSE, MR. SULU"
2530 DATA"60 DEGREES RIGHT ABOUT, MR. SULU"
2540 DATA"FIRING PROBABILITIES, MR. SPOCK"
2550 DATA"A BRIEFING, MR. SPOCK","DAMAGE REPORT, MR. SPOCK"
2560 DATA"OPEN A CHANNEL TO STAR FLEET, LIEUTENANT"
2570 DATA"LET'S SEE WHAT THE ENEMY DOES NEXT"
2580 DATA"ACTIVATE AUTO-DESTRUCT SEQUENCE"
2590 DATA"OPEN A CHANNEL TO THE ENEMY VESSEL, LIEUTENANT"
2600 FORI=1TO19
2610 READZ$(I)
2620 NEXTI
2630 DATA"RANGE","PHASEF","PHASER","TORPF","TORPR"
2635 DATA"PROBE","CLOSE","AWAY","PURSUE","ESCAPE"
2640 DATA"SHIELDS","ROTATE","CHANCES","COMMANDS","DAMAGE"
2645 DATA"BLUFF","WAIT","SUICIDE","YIELD"
2660 CLS
2670 S$=N$(INT(RND(0)*16)+1)
2672 IFF1>0THEN2810
2680 Z$="SPACE, THE FINAL FRONTIER. THIS IS A VOYAGE OF THE STARSHIP "+S$+".":GOSUB1
2700 Z$="ITS FIVE YEAR MISSION, TO EXPLORE STRANGE NEW WORLDS, TO SEEK OUT NEW LIFE AND NEW CIVILIZATIONS, "
2720 Z$=Z$+"TO BOLDLY GO WHERE NO MAN HAS GONE BEFORE.":GOSUB1
2730 Z$=" ":GOSUB1
2750 Z$="S T A R   T R E K":GOSUB13
2760 Z$="-----------------":GOSUB13
2770 Z$=" ":GOSUB1
2780 Z$="YEOMAN:  WOULD YOU PLEASE ENTER YOUR NAME FOR THE LOG, SIR":GOSUB1
2800 INPUTC$
2810 Z$="SPOCK:  YOU ARE IN COMMAND OF THE "+S$+", CAPTAIN "+C$+".":GOSUB1
2820 Z$="DO YOU WISH A LIST OF THE POSSIBLE COMMANDS, SIR?":GOSUB1
2830 INPUTA$
2840 IFLEFT$(A$,1)<>"Y" THEN 2870
2850 GOSUB7140
2855 Z$="SPOCK:  SHALL I PROCEED WITH INFORMATION ABOUT OUR WEAPONS CAPABILITIES?":GOSUB1
2857 INPUTA$
2860 IFLEFT$(A$,1)<>"Y"THEN2870
2863 GOSUB7590 
2865 Z$="SPOCK:  ARE YOU READY TO PROCEED WITH THE MISSION, SIR?":GOSUB1
2866 INPUTA$
2867 IFLEFT$(A$,1)="Y"THEN2870
2868 Z$="SPOCK (IMPATIENTLY):  HOW ABOUT NOW... SIR?":GOSUB1
2869 GOTO2866
2870 Z$=" ":GOSUB1
2874 IFF1>0THEN3070
2880 E$=K$(INT(RND(0)*3)+1)
2910 D$=L$(INT(RND(0)*8)+1)
2930 Z$=C$+":  CAPTAIN'S LOG, STAR DATE "+STR$(999*RND(0))+".":GOSUB1
2940 Z$="WE ARE PRESENTLY ON COURSE FOR "+D$
2950 ONINT(RND(0)*5)+1GOTO2960,2990,3020,3040,3060
2960 Z$=Z$+" IN ORDER TO RESCUE COLONISTS UNDER HEAVY ATTACK BY "+E$+" BATTLE CRUISERS.":GOSUB1
2980 GOTO3070
2990 Z$=Z$+" WITH A CARGO OF VITAL SERUM TO COMBAT AN EPIDEMIC OF RIGELLIAN FEVER THERE.":GOSUB1
3010 GOTO3070
3020 Z$=Z$+" TO SECURE DILITHIUM MINING RIGHTS FOR THE FEDERATION.":GOSUB1
3030 GOTO3070
3040 Z$=Z$+" WITH THE NEW FEDERATION AMBASSADOR TO THAT PLANET.":GOSUB1
3050 GOTO3070
3060 Z$=Z$+" FOR ASTROPHYSICAL RESEARCH ON QUASARS IN THAT AREA.":GOSUB1
3070 F$=H$(INT(RND(0)*5)+1)
3071 F1=0
3072 G$=W$(INT(RND(0)*5)+1)
3074 Y=50*(RND(0)-.5)
3078 Z$="SULU:  I'M PICKING UP A VESSEL ON AN ATTACK VECTOR WITH THE "+S$+".":GOSUB1
3090 Z$="SPOCK:  SHIP'S COMPUTERS INDICATE THAT IT IS THE "+E$+" VESSEL "
3110 Z$=Z$+F$+" UNDER THE COMMAND OF CAPTAIN "+G$+".":GOSUB1
3120 Z$=C$+":  SOUND RED ALERT, LIEUTENANT UHURA.":GOSUB1
3130 Z$="UHURA:  AYE, SIR.":GOSUB1:FORZZ=1TO12:SOUND100,1:NEXT
3140 IFRND(0)>.5THEN3170
3150 X$="SULU"
3160 GOTO3180
3170 X$="CHEKOV"
3180 H2=0
3185 IFF1>0THEN3200
3190 H1=0:X=0:S=0:G=0:P=0
3200 FORI=1TO4
3205 IFF1>0THEN3220
3210 S1(I)=100
3220 S2(I)=100
3230 NEXTI
3235 REM R B B1
3240 R=1000-100*RND(0)
3250 B=360*(RND(0)-.5)
3260 B1=360*(RND(0)-.5)
3261 GOTO3270
3262 IFI<7THEN3280
3263 IFI>12THEN3280
3270 GOSUB7400

3280 Z$=X$+":  WHAT ARE YOUR ORDERS, SIR?":GOSUB1
3290 INPUT"command";M$
3293 I=VAL(M$):IFI>0THEN3370
3300 FORI=1TO19
3310 IFZ$(I)=M$THEN 3370
3320 NEXTI
3325 GOTO3390
3370 IFI<1THEN3390
3380 IFI<20THEN3420
3390 Z$=X$+":  I'M SORRY CAPTAIN "+C$+", BUT I DON'T UNDERSTAND YOUR ORDERS.":GOSUB1
3410 GOTO3280
3420 Z$=C$+":  "+O$(I)+".":GOSUB1
3426 REM CENTRAL DISPATCH
3430 IFI>12THEN3470
3440 IFI>6THEN3460
3450 ONIGOTO3270,3480,3510,3540,3570,3600
3460 ONI-6GOTO3630,3630,3660,3660,4530,3630
3470 ONI-12GOTO4640,4690,4710,3720,5500,5330,5420
3475 REM RUN CHECK
3480 IFH1<7THEN3790
3490 Z$="CHEKOV:  FORWARD PHASERS ARE DEAD, SIR.":GOSUB1
3500 GOTO5500
3510 IFH1<6THEN4190
3520 Z$="CHEKOV:  REAR PHASER IS DEAD, SIR.":GOSUB1
3530 GOTO5500
3540 IFH1<9THEN4210
3550 Z$="CHEKOV:  FORWARD PHOTON TORPEDOES ARE DEAD, SIR.":GOSUB1
3560 GOTO5500
3570 IFH1<8THEN4260
3580 Z$="CHEKOV:  REAR PHOTON TORPEDO IS DEAD, SIR.":GOSUB1
3590 GOTO5500
3600 IFH1<11THEN4280
3610 Z$="CHEKOV:  PROBE LAUNCHER IS DEAD, SIR.":GOSUB1
3620 GOTO5500
3630 IFH1<14THEN4380
3640 Z$="SULU:  IMPULSE ENGINES ARE DEAD, SIR.":GOSUB1
3650 GOTO5500
3660 IFH1<11THEN4380
3670 Z$="SULU:  WARP DRIVE IS DEAD, SIR.":GOSUB1
3680 GOTO5500
3690 IFH2<11THEN3720
3700 Z$="SPOCK:  THE "+E$+" HAS NO ENGINES, SIR.":GOSUB1
3710 GOTO5500
3720 IFG=0THEN5060
3730 Z$="SPOCK:  I DO NOT THINK THAT THE "+E$+"S WILL BE FOOLED BY THAT MANEUVER AGAIN, SIR.":GOSUB1
3745 G=1
3750 GOTO5060
3760 IFABS(B)<90THEN3790
3770 Z$="CHEKOV:  INCORRECT VECTOR, SIR.":GOSUB1
3780 GOTO3280
3785 REM HITS ON ENEMY
3790 Z$="CHEKOV:  PHASERS FIRING, SIR.":GOSUB1
3800 IFR>400THEN3810
3808 CC=B:GOSUB11:IFRND(0)<ZZTHEN3830
3810 Z$="CHEKOV:  MISSED HIM, SIR.":GOSUB1
3820 GOTO5500
3830 IFRND(0)<.2THEN4010
3840 V=.5
3850 K=1
3860 FORK1=2TO4
3870 IFS2(K)>=S2(K1)THEN3890
3880 K=K1
3890 NEXTK1
3900 IFS2(K)>50THEN3920
3910 K=INT(RND(0)*4+1)
3920 H2=H2+V
3930 Z$="SPOCK:  A HIT ON SHIELD #"+STR$(K)+".":GOSUB1
3940 IFS2(K)=0THEN4040
3950 S2(K)=S2(K)-30*V*(RND(0)+.1)
3970 IFS2(K)>0THEN4000
3980 Z$="WHICH IS NOW GONE.":GOSUB1
3990 S2(K)=0
4000 IFH2>11THEN4008
4003 GOSUB6610
4006 GOSUB6550
4008 GOSUB5500
4010 V=1
4020 Z$="CHEKOV:  DIRECT HIT, SIR!":GOSUB1
4030 GOTO3850
4040 Z$="CHEKOV:  GOT HIM, SIR!":GOSUB1
4050 IFRND(0)<.5THEN7050
4060 Z$="SPOCK:  THE "+E$+" VESSEL REMAINS INTACT, CAPTAIN.":GOSUB1
4070 Z$=C$+":  OPEN A HAILING FREQUENCY, LIEUTENANT.":GOSUB1
4080 Z$="UHURA:  HAILING FREQUENCY OPEN, SIR.":GOSUB1
4090 Z$=C$+":  THIS IS CAPTAIN "+C$+" OF THE STARSHIP "+S$+".":GOSUB1
4100 Z$="PREPARE TO COMMENCE BEAMING OVER SURVIVORS.":GOSUB1
4110 IFRND(0)<.5THEN4170
4120 Z$=G$+":  I AM AFRAID THAT WILL BE QUITE IMPOSSIBLE, "
4130 Z$=Z$+"CAPTAIN, SINCE WE HAVE JUST INITIATED OUR AUTO-DESTRUCT.":GOSUB1
4140 FORCC=10TO1STEP-1:PRINTCC;:SOUND200,1:FORZZ=1TO1500:NEXT:NEXT:FORCC=1TO10:CLSRND(8):SOUNDRND(99),1:NEXT
4150 CLS
4160 GOTO7050
4170 Z$=G$+":  VERY WELL, CAPTAIN. OUR SHIELDS HAVE BEEN DEACTIVATED.":GOSUB1
4180 GOTO7430
4190 IFABS(B)<90THEN3770
4200 GOTO3790
4205 REM TORP
4210 IFABS(B)>=90THEN3770
4215 IFR>700THEN3810
4220 Z$="CHEKOV:  PHOTON TORPEDOES FIRING, SIR.":GOSUB1
4238 CC=B:GOSUB10:IFRND(0)>ZZTHEN3810
4240 IFRND(0)<.25THEN3840
4250 GOTO4010
4255 REM TORPR
4260 IFABS(B)<90THEN3770
4270 GOTO4220
4275 REM PROBE
4280 IFX<10THEN4310
4290 Z$="CHEKOV:  WE HAVE NO MORE PROBES, SIR.":GOSUB1
4300 GOTO5510
4310 Z$="CHEKOV:  ANTIMATTER PROBE AWAY, SIR.":GOSUB1
4320 X=X+1
4330 IFRND(0)<.15THEN4360
4340 Z$="SPOCK:  PROBE LOST, CAPTAIN.":GOSUB1
4350 GOTO5500
4360 Z$="SPOCK:  PROBE IS HOMING IN ON THE "+F$+" , SIR.":GOSUB1
4370 GOTO7050
4375 REM 7-12
4380 ONI-6GOTO4390,4420,4460,4490,4530,4600
4390 GOSUB6550
4410 GOTO5500
4420 GOSUB6610
4430 R=ABS(R+Y)
4440 IFR>5000THEN6450
4450 GOTO5500
4460 GOSUB6650
4480 GOTO5500
4490 GOSUB6710
4500 R=ABS(R+2*Y)
4510 IFR>5000THEN6450
4520 GOTO5500
4525 REM SHIELD
4530 S=1
4540 FORJ=2TO4
4550 IFS1(J)<=S1(S)THEN4570
4560 S=J
4570 NEXTJ
4580 Z$="SULU:  SHIELD #"+STR$(S)+" IS IN POSITION.":GOSUB1
4590 GOTO3280
4595 REM ROTATE
4600 B=B+60
4610 IFB<=180THEN3270
4620 B=B-360
4630 GOTO3270
4635 REM CHANGES
4640 IFR>400THEN4643
4641 CC=B:GOSUB11:W2=ZZ
4642 GOTO4644
4643 W2=0
4644 IFABS(R-500)>200THEN4647
4645 CC=B:GOSUB10:W1=ZZ
4646 GOTO4648
4647 W1=0
4648 Z$="SPOCK:  AT RANGE"+STR$(R)+" I WOULD ESTIMATE THE PROBABILITY "
4650 Z$=Z$+"OF A PHASER HIT AT"+STR$(W2*100)+" AND THE PROBABILITY "
4660 Z$=Z$+"OF A PHOTON TORPEDO HIT AT"+STR$(W1*100)+".":GOSUB1
4680 GOTO3280
4690 GOSUB7140
4700 GOTO3280
4705 REM DAMAGE REPORT
4710 Z$="SPOCK:  DAMAGES ARE AS FOLLOWS:":GOSUB1
4720 Z$="OF SHIELDS REMAINING":GOSUB1
4730 Z$=" #  "+LEFT$(S$+"            ",12)+LEFT$(F$+"            ",12):GOSUB1
4740 FORJ=1TO4
4750 Z$=LEFT$(STR$(J)+"    ",4)+LEFT$(STR$(S1(J))+"             ",13)+LEFT$(STR$(S2(J))+"             ",13):GOSUB1
4760 NEXTJ
4762 Z$=" ":GOSUB1
4764 Z$="OTHER DAMAGES ":GOSUB1
4770 Z$=S$:GOSUB1
4780 IFH1>5.5THEN4810
4790 Z$="NONE":GOSUB1
4800 GOTO4930
4810 Z$="REAR PHASERS DEAD":GOSUB1
4820 IFH1<7THEN4930
4830 Z$="FORWARD PHASERS DEAD":GOSUB1
4840 IFH1<8THEN4930
4850 Z$="REAR PHOTON TORPEDOES DEAD":GOSUB1
4860 IFH1<9THEN4930
4870 Z$="FORWARD PHOTON TORPEDOES DEAD":GOSUB1
4880 IFH1<11THEN4930
4890 Z$="PROBE LAUNCHER DESTROYED":GOSUB1
4900 Z$="WARP DRIVE LOST":GOSUB1
4910 IFH1<14THEN4930
4920 Z$="IMPULSE POWER LOST":GOSUB1
4930 Z$=F$:GOSUB1
4940 IFH2>5.5THEN4970
4950 Z$="NONE":GOSUB1
4960 GOTO5040
4970 Z$="ALL PHASERS DEAD":GOSUB1
4980 IFH2<9THEN5040
4990 Z$="ALL PHOTON TORPEDOES DEAD":GOSUB1
5000 IFH2<11THEN5040
5010 Z$="WARP DRIVE DEAD":GOSUB1
5020 IFH2<14THEN5040
5030 Z$="IMPULSE ENGINES DEAD":GOSUB1
5040 Z$=" ":GOSUB1
5050 GOTO3280
5055 REM BLUFF
5060 Z$=C$+":  USE CODE 2.":GOSUB1
5070 Z$="UHURA:  CODE 2, SIR? THE "+E$+"S BROKE CODE 2 YESTERDAY, SIR.":GOSUB1
5080 Z$=C$+":  CODE 2, LIEUTENANT, IMMEDIATELY.":GOSUB1
5090 Z$="UHURA:  AYE, SIR. GO AHEAD, SIR.":GOSUB1
5100 Z$=C$+":  THIS IS CAPTAIN "+C$+" OF THE STARSHIP "+S$+".":GOSUB1
5110 Z$="WE ARE UNDER ATTACK BY THE "+E$+" SHIP "+F$:GOSUB1
5120 Z$="AND, IN ORDER TO PREVENT THE "+S$+" FROM FALLING INTO ENEMY HANDS, "
5140 Z$=Z$+"WE ARE ACTIVATING THE CORBOMITE DEVICE.":GOSUB1
5150 Z$="SINCE THIS WILL RESULT IN THE COMPLETE ANNIHILATION OF ALL MATTER WITHIN A RANGE OF "
5160 Z$=Z$+"5000 MEGAMETERS, ALL VESSELS SHOULD BE WARNED TO STAY CLEAR OF THIS AREA FOR THE "
5170 Z$=Z$+"NEXT"+STR$(INT(RND(0)*4)+2)+" SOLAR YEARS.":GOSUB1
5190 Z$="I WISH TO RECORD COMMENDATIONS FOR THE ENTIRE CREW AND ESPECIALLY COMMANDER SPOCK, "
5210 Z$=Z$+"LIEUTENANT COMMANDER SCOTT, DOCTOR MCCOY, LIEUTENANT UHURA, LIEUTENANT SULU, AND ENSIGN CHEKOV.":GOSUB1
5230 G=G+1
5234 IFG>1THEN5238
5236 GOTO5240
5238 IFRND(0)>.15THEN5290
5240 IFRND(0)<.2THEN5290
5250 Z$="SULU:  "+E$+" IS MOVING AWAY AT WARP 10, SIR.":GOSUB1
5260 Z$="SPOCK:  THE TACTIC APPEARS TO HAVE BEEN EFFECTIVE, SIR.":GOSUB1
5270 Z$="THE "+E$+" HAS BEEN REPULSED.":GOSUB1
5280 GOTO7430
5290 Z$="SULU:  NO IMMEDIATE CHANGE IN "+E$+" COURSE AND SPEED, SIR.":GOSUB1
5300 Z$="SPOCK:  IT WOULD SEEM THAT THEY HAVE, AS YOU HUMANS PUT IT, 'CALLED OUR BLUFF', CAPTAIN.":GOSUB1
5320 GOTO5500
5330 FORCC=10TO1STEP-1:PRINTCC;:SOUND150,1:FORZZ=1TO1500:NEXT:NEXT:FORCC=1TO10:CLSRND(8):SOUNDRND(99),1:NEXT:CLS
5340 Z$="THE "+S$+" HAS BEEN DESTROYED.":GOSUB1
5350 Q=200*RND(0)
5360 Z$="RADIUS OF EXPLOSION:"+STR$(Q)+" MGM.":GOSUB1
5370 IFQ>=RTHEN5400
5380 Z$=E$+" VESSEL REMAINS INTACT.":GOSUB1
5390 GOTO7430
5400 Z$=E$+" VESSEL DESTROYED.":GOSUB1
5410 GOTO7430
5420 IFE$<>"ROMULAN"THEN5450
5430 Z$="UHURA:  NO ANSWER FROM THE "+F$+", SIR.":GOSUB1
5440 GOTO5500
5450 Z$=C$+":  THIS IS CAPTAIN "+C$+" OF THE STARSHIP "+S$+".":GOSUB1
5460 Z$="WILL YOU ACCEPT OUR UNCONDITIONAL SURRENDER?":GOSUB1
5470 Z$=G$+":  ON BEHALF OF THE "+E$+" EMPIRE, I ACCEPT YOUR UNCONDITIONAL SURRENDER. PREPARE FOR IMMEDIATE BOARDING.":GOSUB1
5490 GOTO7430
5500 REM ENEMY MOVES
5510 IFH2<9THEN5820
5520 IFH2<11THEN5660
5530 IFH2>13.9THEN6340
5540 IFH1>10.9THEN6450
5550 IFH1>8.9THEN5610
5560 IFR<200*RND(0)THEN6500
5570 GOSUB6550
5580 R=ABS(R+Y)
5590 IFR>5000THEN6450
5600 GOTO3270
5610 IFRND(0)<.5THEN5570
5620 GOSUB6610
5630 R=ABS(R-Y)
5640 IFR>5000THEN6450
5650 GOTO3270
5660 IFH1<7THEN5790
5670 IFH1<9THEN5560
5680 IFH1>10.9THEN6450
5690 IFRND(0)<.5THEN5610
5700 IFRND(0)<.5THEN5750
5710 GOSUB6650
5720 R=ABS(R+2*Y)
5730 IFR>5000THEN6450
5740 GOTO3270
5750 GOSUB6710
5760 R=ABS(R-2*Y)
5770 IFR>5000THEN6450
5780 GOTO3270
5790 IFR>700THEN5750
5800 IFR>200THEN5710
5810 GOTO5560
5820 IFH2<6THEN5930
5830 IFH1<7THEN5910
5840 IFR<300THEN5750
5850 IFR>700THEN5710
5860 IFH1>7.9THEN5880
5870 CC=B1:GOSUB12:C1=ZZ:CC=B:GOSUB12:IFC1>ZZTHEN5750
5880 IFABS(B1-90)>=ABS(B-90)-20THEN6750
5890 IFRND(0)<.5THEN5750
5900 GOTO5710
5910 IFR>400THEN5913
5911 CC=B:GOSUB11:W2=ZZ
5912 GOTO5914
5913 W2=0
5914 IFABS(R-500)>200THEN5917
5915 CC=B:GOSUB10:W1=ZZ
5916 GOTO5918
5917 W1=0
5918 IFW2>W1THEN5750
5920 GOTO5840
5930 IFH1<7THEN6020
5940 IFR>150THEN5970
5950 IFRND(0)<.5THEN5620
5960 GOTO5750
5970 IFR>=400THEN6000
5980 IFABS(B1)<30THEN6790
5990 GOTO5620
6000 IFR>700THEN5710
6010 GOTO5870
6020 IFR>700THEN5710
6030 IFRND(0)<.2THEN5870
6050 CC=B1:GOSUB12:C1=ZZ:CC=B:GOSUB12:IFC1>ZZTHEN5750
6060 IFABS(B1-90)>=ABS(B-90)-20THEN6790
6070 GOTO5750
6080 IFH1<6THEN6330
6090 T=H1-V
6100 IFABS(T-6)<.1THEN6140
6110 IFABS(H1-6.25)>.3THEN6140
6120 Z$="CHEKOV:  REAR PHASER DEAD, SIR.":GOSUB1
6130 GOTO6330
6140 IFABS(T-7)<.1THEN6180
6150 IFABS(H1-7.25)>.3THEN6180
6160 Z$="CHEKOV:  FORWARD PHASERS DEAD, SIR.":GOSUB1
6170 GOTO6330
6180 IFABS(T-8)<.1THEN6220
6190 IFABS(H1-8.25)>.3THEN6220
6200 Z$="CHEKOV:  REAR PHOTON TORPEDOES DEAD, SIR.":GOSUB1
6210 GOTO6330
6220 IFABS(T-9)<.1THEN6260
6230 IFABS(H1-9.25)>.3THEN6260
6240 Z$="CHEKOV:  FORWARD PHOTON TORPEDOES DEAD, SIR.":GOSUB1
6250 GOTO6330
6260 IFABS(T-11)<.1THEN6300
6270 IFABS(H1-11.25)>.3THEN6300
6280 Z$="CHEKOV:  PROBE LAUNCHER AND WARP DRIVE GONE, SIR.":GOSUB1
6290 GOTO6330
6300 IFABS(T-14)<.1THEN6330
6310 IFABS(H1-14.25)>.3THEN6330
6320 Z$="CHEKOV:  IMPULSE ENGINES DEAD, SIR.":GOSUB1
6330 RETURN
6335 REM WIN
6340 IFP>0THEN3262
6350 P=1
6360 Z$="SPOCK:  THE "+E$+" SHIP IS COMPLETELY CRIPPLED, SIR.":GOSUB1
6370 Z$="DO YOU WANT THEM TO SURRENDER?":GOSUB1
6380 INPUTA$
6390 IFLEFT$(A$,1)="Y"THEN4070
6400 Z$="SPOCK:  DO YOU WANT TO DESTROY THE "+F$+", CAPTAIN?":GOSUB1
6410 INPUTA$
6420 IFLEFT$(A$,1)="Y"THEN3280
6430 GOTO6460
6440 REM LOSS OF CONTACT
6450 Z$="SULU:  CONTACT WITH THE "+E$+" VESSEL HAS BEEN BROKEN, SIR.":GOSUB1
6460 Z$=C$+":  RESUME COURSE FOR "+O$+", MR. SULU.":GOSUB1
6470 Z$="CHEKOV:  AYE, SIR.":GOSUB1
6480 GOTO7430
6490 REM ENEMY SUICIDE SECTION
6500 Z$="SPOCK:  SENSORS INDICATE THE THE "+F$+" IS OVERLOADING.":GOSUB1
6510 Z$="WHAT REMAINS OF ITS ANTIMATTER PODS, UNDOUBTEDLY A SUICIDAL MOVE, CAPTAIN.":GOSUB1
6520 Z$="PODS WILL DETONATE IN 10 SECONDS.":GOSUB1
6530 FORCC=10TO1STEP-1:PRINTCC;:SOUND100,1:FORZZ=1TO1500:NEXT:NEXT:FORCC=1TO10:CLSRND(8):SOUNDRND(99),1:NEXT:CLS
6540 GOTO7050
6545 REM CLOSE
6550 R=R-200*(RND(0)+.5)
6560 B=360*(RND(0)-.5)
6570 B1=360*(RND(0)-.5)
6580 IFR>0THEN6600
6590 R=100*RND(0)+1
6600 RETURN
6605 REM AWAY
6610 R=R+200*(RND(0)+.5)
6620 B=360*(RND(0)-.5)
6630 B1=360*(RND(0)-.5)
6640 RETURN
6645 REM PURSUE
6650 R=R-400*(RND(0)+.5)
6660 B=360*(RND(0)-.5)
6670 B1=360*(RND(0)-.5)
6680 IFR>0THEN6700
6690 R=100*RND(0)+1
6700 RETURN
6705 REM ESCAPE
6710 R=R+400*(RND(0)+.5)
6720 B=360*(RND(0)-.5)
6730 B1=360*(RND(0)-.5)
6740 RETURN
6745 REM HITS BY ENEMY
6750 IFR>500THEN3262
6755 Z$="SPOCK:  THE "+E$+" IS FIRING PHOTON TORPEDOES AT US, SIR.":GOSUB1
6768 CC=B1:GOSUB10:IFRND(0)>ZZTHEN7030
6770 IFRND(0)<.5THEN6970
6780 GOTO6820
6790 IFR>200THEN6755
6795 Z$="SPOCK:  THE "+E$+" IS FIRING PHASERS AT US, SIR.":GOSUB1
6808 CC=B1:GOSUB11:IFRND(0)>ZZTHEN6970
6810 IFRND(0)<.25THEN6970
6820 V=.5
6830 K=INT(RND(0)*4)+1
6840 IFS=0THEN6860
6850 K=S
6860 Z$="A HIT ON SHIELD #"+STR$(K)+".":GOSUB1
6870 IFS1(K)<=0THEN6950
6880 S1(K)=S1(K)-30*V*(RND(0)+.1)
6890 H1=H1+V
6900 GOSUB6080
6910 IFS1(K)>0THEN3262
6920 S1(K)=0
6930 Z$="SHIELD #"+STR$(K)+" IS GONE.":GOSUB1
6940 GOTO3262
6950 Z$="COMPUTER:  THE "+S$+" HAS BEEN DESTROYED.":GOSUB1
6960 GOTO7430
6970 V=1
6980 K=INT(RND(0)*4)+1
6990 IFS=0THEN7010
7000 K=S
7010 Z$="A DIRECT HIT ON SHIELD #"+STR$(K)+".":GOSUB1
7020 GOTO6870
7030 Z$="EVASIVE MANEUVERS WERE EFFECTIVE, NO DAMAGE.":GOSUB1
7040 GOTO3262
7045 REM CONT
7050 Z$=" ":GOSUB1
7060 Q=200*RND(0)
7070 IFQ<RTHEN7110
7080 Z$="COMPUTER:  RADIUS OF EXPLOSION:"+STR$(Q)+" MGM.":GOSUB1
7090 Z$=S$+" HAS BEEN DESTROYED.":GOSUB1
7100 GOTO7430
7110 Z$="SPOCK:  "+E$+" VESSEL DESTROYED.":GOSUB1
7120 Z$="RADIUS OF EXPLOSION:  "+STR$(Q)+" MGM.":GOSUB1
7130 GOTO7430
7135 REM SUB
7140 Z$=" ":GOSUB1
7150 Z$="SPOCK:  THE POSSIBLE COMMANDS ARE DESIGNATED BY THE FOLLOWING NUMBERS OR CODE WORDS:":GOSUB1
7180 Z$=" # CODE COMMAND":GOSUB1
7200 Z$=" 1 RANGE (AND BEARING)":GOSUB1
7210 Z$=" 2 PHASEF (FORWARD PHASERS)":GOSUB1
7220 Z$=" 3 PHASER (REAR PHASER)":GOSUB1
7230 Z$=" 4 TORPF (FORWARD TORPEDOES)":GOSUB1
7240 Z$=" 5 TORPRV (REAR TORPEDOES)":GOSUB1
7250 Z$=" 6 PROBE (ANTIMATTER-- MAX 10)":GOSUB1
7260 Z$=" 7 CLOSE (ON ENEMY VESSEL)":GOSUB1
7270 Z$=" 8 AWAY (FROM ENEMY VESSEL)":GOSUB1
7280 Z$=" 9 PURSUE (ENEMY AT WARP SPEED)":GOSUB1
7290 Z$="10 ESCAPE (AT WARP SPEED)":GOSUB1
7300 Z$="11 SHIELDS (OPTIMIZE)":GOSUB1
7310 Z$="12 ROTATE (WEAPONS 60 DEG)":GOSUB1
7320 Z$="13 CHANCES (FIRING PROBABILITY)":GOSUB1
7330 Z$="14 COMMANDS (THIS LIST)":GOSUB1
7340 Z$="15 DAMAGE (REPORT)":GOSUB1
7350 Z$="16 BLUFF (CORBOMITE MANEUVER)":GOSUB1
7360 Z$="17 WAIT (LET ENEMY MOVE)":GOSUB1
7370 Z$="18 SUICIDE (SELF-DESTRUCT)":GOSUB1
7380 Z$="19 YIELD (SURRENDER TO ENEMY)":GOSUB1:RETURN
7395 REM SUB
7400 Z$="SPOCK:  "+F$+" IS AT RANGE: "+STR$(R)+" MGM., BEARING: "+STR$(B)+" DEGREES.":GOSUB1:RETURN
7425 REM LOST CONTACT
7430 Z$=" ":GOSUB1
7440 Z$="COMPUTER:  DO YOU WISH TO ATTEMPT ANOTHER BATTLE?":GOSUB1
7460 INPUTA$
7470 IFLEFT$(A$,1)<>"Y"THEN7720
7480 F1=1
7482 Z$="COMPUTER:  DO YOU WANT ANOTHER ASSIGNMENT?":GOSUB1
7485 INPUTA$
7487 IFLEFT$(A$,1)<>"Y"THEN7490
7488 S$=N$(RND(0)*16+1)
7489 GOTO2880
7490 Z$="COMPUTER:  DO YOU WISH TO CHANGE SHIPS?":GOSUB1
7500 INPUTA$
7510 IFLEFT$(A$,1)<>"Y"THEN3070
7520 S$=N$(RND(0)*16+1)
7530 GOTO2660
7535 REM SUB
7590 Z$=" ":GOSUB1
7600 Z$="note:  WEAPON RANGES ARE:":GOSUB1
7610 Z$="PHASERS 0-400 MGM (OPT 200)":GOSUB1
7620 Z$="TORP.   300-700 MGM (OPT 500)":GOSUB1
7630 Z$="PROBES  ALL RANGES. 10 IN STOCK":GOSUB1
7640 Z$=" ":GOSUB1
7650 Z$="PHASERS ARE MORE DEADLY THAN TORPEDOES.":GOSUB1
7660 Z$="PROBES CAUSE TOTAL DESTRUCTION BUT ARE EFFECTIVE ONLY 15 PERCENT OF THE TIME.":GOSUB1
7680 Z$="TORPEDOES AND PHASERS ARE MORE DEADLY WHEN THE BEARING OF THE ENEMY IS "
7700 Z$=Z$+"CLOSE TO 0, 180, AND -180 DEGREES.":GOSUB1
7701 Z$=" ":GOSUB1
7702 Z$="IF YOU KEEP YOUR ASSIGNMENT WITHOUT CHANGING SHIPS, DAMAGE WILL BE CUMULATIVE.":GOSUB1
7710 RETURN
7720 END
