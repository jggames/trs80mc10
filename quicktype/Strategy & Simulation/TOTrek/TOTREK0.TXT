01000 REM NAME--TUFTS UNIVERSITY STAR TREK
01010 REM
01020 REM DESCRIPTION--PLAYS A BATTLE BETWEEN THE FEDERATION AND
01030 REM               AN ENEMY VESSEL
01040 REM
01050 REM SOURCE--WILLIAM PETERSON, WORCESTER POLYTECHNIC INSTITUTE
01060 REM
01070 REM MAINTENANCE--SEE PETER VAINE
01071 REM
01075 REM 2022/09/11: Ported to DEC BASIC-Plus
01078 REM
01080 REM
01090 REM *  *  *  *  *  *  MAIN PROGRAM  *  *  *  *  *  *
01100 REM
02000 REM
02005 RANDOMIZE
02010 DIM N$(16),O$(19)
02020 DIM Z$(19)
02030 DEF FNT(B)=(1-(R-500)^2/40000)*SIN(FNX(B))*(3-FND(B))/3
02090 DEF FNP(B)=(1-(R-200)^2/40000)*SIN(FNX(B))*(5-FND(B))/5
02150 DEF FND(B)=INT(ABS(B/90))
02152 REM FNC$ is used to process a user's Yes or No response. The CVT$$
02153 REM function removes all spaces and converts to upper case. LEFT
02154 REM returns just the first character, so we can test for "Y"
02155 DEF FNC$(A$)=LEFT(CVT$$(A$,2%+32%),1)
02160 DEF FNX(B)=3.1415926*ABS(90-ABS(B))/180
02162 REM
02165 LET F1=0
02170 FOR I=1 TO 8
02180 READ L$(I)
02190 NEXT I
02200 DATA "BETA AURIGAE 5", "GAMMA TRISKELLION 7"
02210 DATA "EPSILON CYGNI 2", "ALPHA ARCTURUS 4"
02220 DATA "DELTA CANOPIS 12", "OMICRON BETELGUESE 27"
02230 DATA "SIGMA RIGEL 3", "KAPPA SIRIUS 8"
02240 FOR I=1 TO 16
02250 READ N$(I)
02260 NEXT I
02270 DATA ENTERPRISE,VALIANT,HOOD,FREE-ENTERPRISE
02280 DATA DEFIANT,GRAPEFRUIT,EXCALIBUR,EXETER
02290 DATA LEXINGTON,YORKTOWN,REPUBLIC,CONSTITUTION
02300 DATA FARRAGUT,KONGO,CONSTELLATION,GEORGE
02310 FOR I=1 TO 3
02320 READ K$(I)
02330 NEXT I
02340 DATA KLINGON,ROMULAN,THOLIAN
02350 FOR I=1 TO 5
02360 READ H$(I)
02370 NEXT I
02380 DATA KRULIX,GRANAK,SLORK,QUARLO,TROBLAK
02390 FOR I=1 TO 5
02400 READ W$(I)
02410 NEXT I
02420 DATA TRAKKA,KURDA,VARNOR,GRANOLT,SHLURG
02430 FOR I=1 TO 19
02440 READ O$(I)
02450 NEXT I
02460 DATA "ENEMY RANGE AND BEARING"
02470 DATA "FIRE FORWARD PHASERS","FIRE REAR PHASERS"
02480 DATA "FIRE FORWARD PHOTON TORPEDOES","FIRE REAR PHOTON TORPEDOES"
02490 DATA "LAUNCH ANTIMATTER PROBE","CLOSE ON ENEMY VESSEL"
02500 DATA "MOVE AWAY FROM ENEMY","CLOSE AT WARP SPEED"
02510 DATA "ESCAPE AT WARP SPEED"
02520 DATA "OPTIMUM SHIELD DEFENSE, MR. SULU"
02530 DATA "60 DEGREES RIGHT ABOUT, MR. SULU"
02540 DATA "FIRING PROBABILITIES, MR. SPOCK"
02550 DATA "A BRIEFING, MR. SPOCK","DAMAGE REPORT, MR. SPOCK"
02560 DATA "OPEN A CHANNEL TO STAR FLEET, LIEUTENANT"
02570 DATA "LET'S SEE WHAT THE ENEMY DOES NEXT"
02580 DATA "ACTIVATE AUTO-DESTRUCT SEQUENCE"
02590 DATA "OPEN A CHANNEL TO THE ENEMY VESSEL, LIEUTENANT"
02600 FOR I=1 TO 19
02610 READ Z$(I)
02620 NEXT I
02630 DATA "RANGE   ","PHASEF  ","PHASER  ","TORPF   ","TORPR   "
02635 DATA "PROBE   ","CLOSE   ","AWAY    ","PURSUE  ","ESCAPE  "
02640 DATA "SHIELDS ","ROTATE  ","CHANCES ","COMMANDS","DAMAGE  "
02645 DATA "BLUFF   ","WAIT    ","SUICIDE ","YIELD   "
02660 PRINT
02670 LET S$=N$(RND*16+1)
02672 IF F1>0 THEN 2810
02680 PRINT "SPACE, THE FINAL FRONTIER."
02690 PRINT "THIS IS A VOYAGE OF THE STARSHIP ";S$;"."
02700 PRINT "ITS FIVE YEAR MISSION, TO EXPLORE STRANGE NEW WORLDS,"
02710 PRINT "TO SEEK OUT NEW LIFE AND NEW CIVILIZATIONS,"
02720 PRINT "TO BOLDLY GO WHERE NO MAN HAS GONE BEFORE."
02730 PRINT
02740 PRINT
02750 PRINT TAB(20);"S T A R   T R E K"
02760 PRINT TAB(20);"-----------------"
02770 PRINT
02780 PRINT "YEOMAN:  WOULD YOU PLEASE ENTER YOUR NAME FOR"
02790 PRINT "   THE LOG, SIR";
02800 INPUT C$
02810 PRINT "SPOCK:  YOU ARE IN COMMAND OF THE ";S$;", CAPTAIN ";C$;"."
02820 PRINT "   DO YOU WISH A LIST OF THE POSSIBLE COMMANDS, SIR";
02830 INPUT A$
02840 IF FNC$(A$)<>"Y" THEN 2870
02850 GOSUB 7140
02855 PRINT "SPOCK:  SHALL I PROCEED WITH INFORMATION ABOUT OUR"
02856 PRINT "   WEAPONS CAPABILITIES";
02857 INPUT A$
02860 IF FNC$(A$)<>"Y" THEN 2870
02863 GOSUB 7590 
02865 PRINT "SPOCK:  ARE YOU READY TO PROCEED WITH THE MISSION, SIR";
02866 INPUT A$
02867 IF FNC$(A$)="Y" THEN 2870
02868 PRINT "SPOCK (IMPATIENTLY): HOW ABOUT NOW... SIR";
02869 GO TO 02866
02870 PRINT
02874 IF F1>0 THEN 3070
02880 LET E$=K$(RND*3+1)
02910 LET D$=L$(RND*8+1)
02930 PRINT C$;": CAPTAIN'S LOG, STAR DATE ";999*RND "."
02940 PRINT "   WE ARE PRESENTLY ON COURSE FOR ";D$
02950 ON INT(RND*5)+1 GO TO 2960,2990,3020,3040,3060
02960 PRINT "   IN ORDER TO RESCUE COLONISTS UNDER HEAVY ATTACK"
02970 PRINT "   BY ";E$;" BATTLE CRUISERS."
02980 GO TO 3070
02990 PRINT "   WITH A CARGO OF VITAL SERUM TO COMBAT AN EPIDEMIC"
03000 PRINT "   OF RIGELLIAN FEVER THERE."
03010 GO TO 3070
03020 PRINT "   TO SECURE DILITHIUM MINING RIGHTS FOR THE FEDERATION."
03030 GO TO 3070
03040 PRINT "   WITH THE NEW FEDERATION AMBASSADOR TO THAT PLANET."
03050 GO TO 3070
03060 PRINT "   FOR ASTROPHYSICAL RESEARCH ON QUASARS IN THAT AREA."
03070 LET F$=H$(RND*5+1)
03071 LET F1=0
03072 LET G$=W$(RND*5+1)
03074 LET Y=50*(RND-.5)
03078 PRINT "SULU: I'M PICKING UP A VESSEL ON AN ATTACK VECTOR"
03080 PRINT "   WITH THE ";S$;"."
03090 PRINT "SPOCK:  SHIP'S COMPUTERS INDICATE THAT IT IS THE ";
03100 PRINT E$ " VESSEL "
03110 PRINT "  " F$ " UNDER THE COMMAND OF CAPTAIN " G$ "."
03120 PRINT C$;": SOUND RED ALERT, LIEUTENANT UHURA."
03130 PRINT "UHURA: AYE, SIR."
03140 IF RND>.5 THEN 3170
03150 LET X$="SULU"
03160 GO TO 3180
03170 LET X$="CHEKOV"
03180 LET H2=0
03185 IF F1>0 THEN 3200
03190 LET H1, X, S, G, P = 0
03200 FOR I=1 TO 4
03205 IF F1>0 THEN 3220
03210 LET S1(I)=100
03220 LET S2(I)=100
03230 NEXT I
03235 REM ---------R B B1---------
03240 LET R=1000-100*RND
03250 LET B=360*(RND-.5)
03260 LET B1=360*(RND-.5)
03261 GO TO 3270
03262 IF I<7 THEN 3280
03263 IF I>12 THEN 3280
03270 GOSUB 7400
03280 PRINT X$;":  WHAT ARE YOUR ORDERS, SIR";
03285 REM ----------ORDERS---------
03290 INPUT M$
03291 LET M$=CVT$$(M$,2%+32%) ! Strip spaces and convert to upper case
03292 ON ERROR GOTO 03295
03293 LET I=VAL(M$)
03294 GO TO 3370
03295 RESUME 3300
03300 FOR I=1 TO 19
03310 IF Z$(I)=M$ THEN 3370
03320 NEXT I
03325 GO TO 3390
03370 IF I<1 THEN 3390
03380 IF I<20 THEN 3420
03390 PRINT X$;":  I'M SORRY CAPTAIN ";C$;", BUT I DON'T UNDERSTAND";
03400 PRINT " YOUR ORDERS."
03410 GO TO 3280
03420 PRINT C$;":  ";O$(I)
03425 REM ------------------------------
03426 REM --------CENTRAL DISPATCH--------
03430 IF I>12 THEN 3470
03440 IF I>6 THEN 3460
03450 ON I GO TO 3270,3480,3510,3540,3570,3600
03460 ON (I-6) GO TO 3630,3630,3660,3660,4530,3630
03470 ON (I-12) GO TO 4640,4690,4710,3720,5500,5330,5420
03475 REM --------RUN CHECK--------
03480 IF H1<7 THEN 3790
03490 PRINT "CHEKOV:  FORWARD PHASERS ARE DEAD, SIR."
03500 GO TO 5500
03510 IF H1<6 THEN 4190
03520 PRINT "CHEKOV:  REAR PHASER IS DEAD, SIR."
03530 GO TO 5500
03540 IF H1<9 THEN 4210
03550 PRINT "CHEKOV:  FORWARD PHOTON TORPEDOES ARE DEAD, SIR."
03560 GO TO 5500
03570 IF H1<8 THEN 4260
03580 PRINT "CHEKOV: REAR PHOTON TORPEDO IS DEAD, SIR."
03590 GO TO 5500
03600 IF H1<11 THEN 4280
03610 PRINT "CHEKOV:  PROBE LAUNCHER IS DEAD, SIR."
03620 GO TO 5500
03630 IF H1<14 THEN 4380
03640 PRINT "SULU:  IMPULSE ENGINES ARE DEAD, SIR."
03650 GO TO 5500
03660 IF H1<11 THEN 4380
03670 PRINT "SULU:  WARP DRIVE IS DEAD, SIR."
03680 GO TO 5500
03690 IF H2<11 THEN 3720
03700 PRINT "SPOCK:  THE " E$ " HAS NO ENGINES, SIR."
03710 GO TO 5500
03720 IF G=0 THEN 5060
03730 PRINT "SPOCK:  I DO NOT THINK THAT THE ";E$ "S WILL BE FOOLED"
03740 PRINT "  BY THAT MANEUVER AGAIN, SIR."
03745 LET G=1
03750 GO TO 5060
03760 IF ABS(B)<90 THEN 3790
03770 PRINT "CHEKOV:  INCORRECT VECTOR, SIR."
03780 GO TO 3280
03785 REM --------HITS ON ENEMY--------
03790 PRINT "CHEKOV:  PHASERS FIRING, SIR."
03800 IF R>400 THEN 3810
03808 IF RND<FNP(B) THEN 3830
03810 PRINT "CHEKOV:  MISSED HIM, SIR."
03820 GO TO 5500
03830 IF RND<.2 THEN 4010
03840 LET V=.5
03850 LET K=1
03860 FOR K1=2 TO 4
03870 IF S2(K)>=S2(K1) THEN 3890
03880 LET K=K1
03890 NEXT K1
03900 IF S2(K)>50 THEN 3920
03910 LET K=INT(RND*4+1)
03920 LET H2=H2+V
03930 PRINT "SPOCK:  A HIT ON SHIELD #";K;"."
03940 IF S2(K)=0 THEN 4040
03950 LET S2(K)=S2(K)-30*V*(RND+.1)
03970 IF S2(K)>0 THEN 4000
03980 PRINT "   WHICH IS NOW GONE."
03990 LET S2(K)=0
04000 IF H2>11 THEN 4008
04003 GOSUB 6610
04006 GOSUB 6550
04008 GOSUB 5500
04010 LET V=1
04020 PRINT "CHEKOV:  DIRECT HIT, SIR!"
04030 GO TO 3850
04040 PRINT "CHEKOV:  GOT HIM, SIR!"
04050 IF RND<.5 THEN 7050
04060 PRINT "SPOCK:  THE " E$ " VESSEL REMAINS INTACT, CAPTAIN."
04070 PRINT C$;":  OPEN A HAILING FREQUENCY, LIEUTENANT."
04080 PRINT "UHURA:  HAILING FREQUENCY OPEN, SIR."
04090 PRINT C$;":  THIS IS CAPTAIN ";C$;"OF THE STARSHIP ";S$;"."
04100 PRINT "  PREPARE TO COMMENCE BEAMING OVER SURVIVORS."
04110 IF RND<.5 THEN 4170
04120 PRINT G$ ;":  I AM AFRAID THAT WILL BE QUITE IMPOSSIBLE,"
04130 PRINT "  CAPTAIN, SINCE WE HAVE JUST INITIATED OUR AUTO-DESTRUCT."
04140 PRINT "  10  9  8  7  6  5  4  3  2  1"
04150 PRINT
04160 GO TO 7050
04170 PRINT  G$;": VERY WELL, CAPTAIN. OUR SHIELDS HAVE BEEN DEACTIVAT";
04175 PRINT "ED."
04180 GO TO 7430
04190 IF ABS(B)<90 THEN 3770
04200 GO TO 3790
04205 REM ----------TORP----------
04210 IF ABS(B)>=90 THEN 3770
04215 IF R>700 THEN 3810
04220 PRINT "CHEKOV:  PHOTON TORPEDOES FIRING, SIR."
04238 IF RND>FNT(B) THEN 3810
04240 IF RND<.25 THEN 3840
04250 GO TO 4010
04255 REM ----------TORPR----------
04260 IF ABS(B)<90 THEN 3770
04270 GO TO 4220
04275 REM ----------PROBE----------
04280 IF X<10 THEN 4310
04290 PRINT "CHEKOV:  WE HAVE NO MORE PROBES, SIR."
04300 GO TO 5510
04310 PRINT "CHEKOV:  ANTIMATTER PROBE AWAY, SIR."
04320 LET X=X+1
04330 IF RND<.15 THEN 4360
04340 PRINT "SPOCK:  PROBE LOST, CAPTAIN."
04350 GO TO 5500
04360 PRINT "SPOCK:  PROBE IS HOMING IN ON THE ";F$ " , SIR."
04370 GO TO 7050
04375 REM ----------7-12----------
04380 ON (I-6) GO TO 4390,4420,4460,4490,4530,4600
04390 GOSUB 6550
04410 GO TO 5500
04420 GOSUB 6610
04430 LET R=ABS(R+Y)
04440 IF R>5000 THEN 6450
04450 GO TO 5500
04460 GOSUB 6650
04480 GO TO 5500
04490 GOSUB 6710
04500 LET R=ABS(R+2*Y)
04510 IF R>5000 THEN 6450
04520 GO TO 5500
04525 REM ----------SHIELD----------
04530 LET S=1
04540 FOR J=2 TO 4
04550 IF S1(J)<=S1(S) THEN 4570
04560 LET S=J
04570 NEXT J
04580 PRINT "SULU:  SHIELD #";S;" IS IN POSITION."
04590 GO TO 3280
04595 REM ----------ROTATE----------
04600 LET B=B+60
04610 IF B<=180 THEN 3270
04620 LET B=B-360
04630 GO TO 3270
04635 REM ----------CHANGES----------
04640 IF R>400 THEN 4643
04641 LET W2=FNP(B)
04642 GO TO 4644
04643 LET W2=0
04644 IF ABS(R-500)>200 THEN 4647
04645 LET W1=FNT(B)
04646 GO TO 4648
04647 LET W1=0
04648 PRINT "SPOCK:  AT RANGE ";R;" I WOULD ESTIMATE THE PROBABILITY"
04650 PRINT "   OF A PHASER HIT AT ";W2*100;"  AND THE PROBABILITY"
04660 PRINT "   OF A PHOTON TORPEDO HIT AT ";W1*100"  ."
04680 GO TO 3280
04690 GOSUB 7140
04700 GO TO 3280
04705 REM -------DAMAGE REPORT-------
04710 PRINT "SPOCK:  DAMAGES ARE AS FOLLOWS:"
04720 PRINT TAB(10);"  OF SHIELDS REMAINING"
04730 PRINT TAB(6);"SHIELD #";TAB(16);S$;TAB(30);F$
04740 FOR J=1 TO 4
04750 PRINT TAB(9);J;TAB(16);S1(J);TAB(30);S2(J)
04760 NEXT J
04762 PRINT
04764 PRINT TAB(10);" OTHER DAMAGES "
04770 PRINT S$;
04780 IF H1>5.5 THEN 4810
04790 PRINT TAB(20);"NONE"
04800 GO TO 4930
04810 PRINT TAB(20);"REAR PHASERS DEAD"
04820 IF H1<7 THEN 4930
04830 PRINT TAB(20);"FORWARD PHASERS DEAD"
04840 IF H1<8 THEN 4930
04850 PRINT TAB(20);"REAR PHOTON TORPEDOES DEAD"
04860 IF H1<9 THEN 4930
04870 PRINT TAB(20);"FORWARD PHOTON TORPEDOES DEAD"
04880 IF H1<11 THEN 4930
04890 PRINT TAB(20);"PROBE LAUNCHER DESTROYED"
04900 PRINT TAB(20);"WARP DRIVE LOST"
04910 IF H1<14 THEN 4930
04920 PRINT TAB(20);"IMPULSE POWER LOST"
04930 PRINT F$;
04940 IF H2>5.5 THEN 4970
04950 PRINT TAB(20);"NONE"
04960 GO TO 5040
04970 PRINT TAB(20);"ALL PHASERS DEAD"
04980 IF H2<9 THEN 5040
04990 PRINT TAB(20);"ALL PHOTON TORPEDOES DEAD"
05000 IF H2<11 THEN 5040
05010 PRINT TAB(20);"WARP DRIVE DEAD"
05020 IF H2<14 THEN 5040
05030 PRINT TAB(20);"IMPULSE ENGINES DEAD"
05040 PRINT
05050 GO TO 3280
05055 REM ----------BLUFF----------
05060 PRINT C$;":  USE CODE 2."
05070 PRINT "UHURA: CODE 2, SIR? THE " E$;"S BROKE CODE 2 YESTERDAY, S";
05075 PRINT "IR."
05080 PRINT C$;":  CODE 2, LIEUTENANT, IMMEDIATELY."
05090 PRINT "UHURA: AYE, SIR.  GO AHEAD, SIR."
05100 PRINT C$;":  THIS IS CAPTAIN ";C$;" OF THE STARSHIP ";S$;"."
05110 PRINT "  WE ARE UNDER ATTACK BY THE ";E$ ;" SHIP ";F$
05120 PRINT "  AND, IN ORDER TO PREVENT THE ";S$;" FROM FALLING"
05130 PRINT "  INTO ENEMY HANDS, WE ARE ACTIVATING THE CORBOMITE"
05140 PRINT "  DEVICE.  SINCE THIS WILL RESULT IN THE COMPLETE"
05150 PRINT "  ANNIHILATION OF ALL MATTER WITHIN A RANGE OF 5000"
05160 PRINT "  MEGAMETERS, ALL VESSELS SHOULD BE WARNED TO STAY"
05170 PRINT "  CLEAR OF THIS AREA FOR THE NEXT "INT(RND*4)+2
05180 PRINT "  SOLAR YEARS."
05190 PRINT "  I WISH TO RECORD COMMENDATIONS FOR THE ENTIRE CREW"
05200 PRINT "  AND ESPECIALLY COMMANDER SPOCK, LIEUTENANT"
05210 PRINT "  COMMANDER SCOTT, DOCTOR MCCOY, LIEUTENANT UHURA,"
05220 PRINT "  LIEUTENANT SULU, AND ENSIGN CHEKOV."
05230 LET G=G+1
05234 IF G>1 THEN 5238
05236 GO TO 5240
05238 IF RND>.15 THEN 5290
05240 IF RND<.2 THEN 5290
05250 PRINT "SULU: ";E$ ;" IS MOVING AWAY AT WARP 10, SIR."
05260 PRINT "SPOCK: THE TACTIC APPEARS TO HAVE BEEN EFFECTIVE, SIR."
05270 PRINT "  THE ";E$ ;" HAS BEEN REPULSED."
05280 GO TO 7430
05290 PRINT "SULU: NO IMMEDIATE CHANGE IN ";E$ " COURSE AND SPEED, SIR."
05300 PRINT "SPOCK: IT WOULD SEEM THAT THEY HAVE, AS YOU HUMANS PUT IT,"
05310 PRINT "  'CALLED OUR BLUFF', CAPTAIN."
05320 GOTO 5500
05330 PRINT "COMPUTER: 10  9  8  7  6  5  4  3  2  1"
05340 PRINT "  THE ";S$;" HAS BEEN DESTROYED."
05350 LET Q=200*RND
05360 PRINT "  RADIUS OF EXPLOSION:";Q;" MGM."
05370 IF Q>=R THEN 5400
05380 PRINT "  ";E$ ;" VESSEL REMAINS INTACT."
05390 GO TO 7430
05400 PRINT "  ";E$ ;" VESSEL DESTROYED."
05410 GO TO 7430
05420 IF E$ <>"ROMULAN" THEN 5450
05430 PRINT "UHURA:  NO ANSWER FROM THE ";F$ ;", SIR."
05440 GO TO 5500
05450 PRINT C$;":  THIS IS CAPTAIN ";C$;" OF THE STARSHIP ";S$;"."
05460 PRINT "  WILL YOU ACCEPT OUR UNCONDITIONAL SURRENDER?"
05470 PRINT G$ ;": ON BEHALF OF THE ";E$ ;" EMPIRE, I ACCEPT YOUR"
05480 PRINT " UNCONDITIONAL SURRENDER.  PREPARE FOR IMMEDIATE BOARDING."
05490 GO TO 7430
05500 REM ---ENEMY MOVE DECISION SECTION---
05510 IF H2<9 THEN 5820
05520 IF H2<11 THEN 5660
05530 IF H2>13.9 THEN 6340
05540 IF H1>10.9 THEN 6450
05550 IF H1>8.9 THEN 5610
05560 IF R<200*RND THEN 6500
05570 GOSUB 6550
05580 LET R=ABS(R+Y)
05590 IF R>5000 THEN 6450
05600 GO TO 3270
05610 IF RND<.5 THEN 5570
05620 GOSUB 6610
05630 LET R=ABS(R-Y)
05640 IF R>5000 THEN 6450
05650 GO TO 3270
05660 IF H1<7 THEN 5790
05670 IF H1<9 THEN 5560
05680 IF H1>10.9 THEN 6450
05690 IF RND<.5 THEN 5610
05700 IF RND<.5 THEN 5750
05710 GOSUB 6650
05720 LET R=ABS(R+2*Y)
05730 IF R>5000 THEN 6450
05740 GO TO 3270
05750 GOSUB 6710
05760 LET R=ABS(R-2*Y)
05770 IF R>5000 THEN 6450
05780 GO TO 3270
05790 IF R>700 THEN 5750
05800 IF R>200 THEN 5710
05810 GO TO 5560
05820 IF H2<6 THEN 5930
05830 IF H1<7 THEN 5910
05840 IF R<300 THEN 5750
05850 IF R>700 THEN 5710
05860 IF H1>7.9 THEN 5880
05870 IF FND(B1)>FND(B) THEN 5750
05880 IF ABS(B1-90)>=ABS(B-90)-20 THEN 6750
05890 IF RND<.5 THEN 5750
05900 GO TO 5710
05910 IF R>400 THEN 5913
05911 LET W2=FNP(B)
05912 GO TO 5914
05913 LET W2=0
05914 IF ABS(R-500)>200 THEN 5917
05915 LET W1=FNT(B)
05916 GO TO 5918
05917 LET W1=0
05918 IF W2>W1 THEN 5750
05920 GO TO 5840
05930 IF H1<7 THEN 6020
05940 IF R>150 THEN 5970
05950 IF RND<.5 THEN 5620
05960 GO TO 5750
05970 IF R>=400 THEN 6000
05980 IF ABS(B1)<30 THEN 6790
05990 GO TO 5620
06000 IF R>700 THEN 5710
06010 GO TO 5870
06020 IF R>700 THEN 5710
06030 IF RND<.2 THEN 5870
06050 IF FND(B1)>FND(B) THEN 5750
06060 IF ABS(B1-90)>=ABS(B-90)-20 THEN 6790
06070 GO TO 5750
06080 IF H1<6 THEN 6330
06090 LET T=H1-V
06100 IF ABS(T-6)<.1 THEN 6140
06110 IF ABS(H1-6.25)>.3 THEN 6140
06120 PRINT "CHEKOV: REAR PHASER DEAD, SIR."
06130 GO TO 6330
06140 IF ABS(T-7)<.1 THEN 6180
06150 IF ABS(H1-7.25)>.3 THEN 6180
06160 PRINT "CHEKOV: FORWARD PHASERS DEAD, SIR."
06170 GO TO 6330
06180 IF ABS(T-8)<.1 THEN 6220
06190 IF ABS(H1-8.25)>.3 THEN 6220
06200 PRINT "CHEKOV:  REAR PHOTON TORPEDOES DEAD, SIR."
06210 GO TO 6330
06220 IF ABS(T-9)<.1 THEN 6260
06230 IF ABS(H1-9.25)>.3 THEN 6260
06240 PRINT "CHEKOV:  FORWARD PHOTON TORPEDOES DEAD, SIR."
06250 GO TO 6330
06260 IF ABS(T-11)<.1 THEN 6300
06270 IF ABS(H1-11.25)>.3 THEN 6300
06280 PRINT "CHEKOV:  PROBE LAUNCHER AND WARP DRIVE GONE, SIR."
06290 GO TO 6330
06300 IF ABS(T-14)<.1 THEN 6330
06310 IF ABS(H1-14.25)>.3 THEN 6330
06320 PRINT "CHEKOV:  IMPULSE ENGINES DEAD, SIR."
06330 RETURN
06335 REM ----------WIN----------
06340 IF P>0 THEN 3262
06350 LET P=1
06360 PRINT "SPOCK: THE ";E$ ;" SHIP IS COMPLETELY CRIPPLED, SIR."
06370 PRINT "  DO YOU WANT THEM TO SURRENDER";
06380 INPUT A$
06390 IF FNC$(A$)="Y" THEN 4070
06400 PRINT "SPOCK: DO YOU WANT TO DESTROY THE ";F$ ;", CAPTAIN";
06410 INPUT A$
06420 IF FNC$(A$)="Y" THEN 3280
06430 GO TO 6460
06440 REM -----LOSS OF CONTACT SECTION-----
06450 PRINT "SULU:  CONTACT WITH THE ";E$ ;"VESSEL HAS BEEN BROKEN, S"
06455 PRINT "IR."
06460 PRINT C$;":  RESUME COURSE FOR ";O$;", MR. SULU."
06470 PRINT "CHEKOV:  AYE, SIR."
06480 GO TO 7430
06490 REM -----ENEMY SUICIDE SECTION-----
06500 PRINT "SPOCK:  SENSORS INDICATE THE THE ";F$ ;" IS OVERLOADING"
06510 PRINT "   WHAT REMAINS OF ITS ANTIMATTER PODS, UNDOUBTEDLY"
06520 PRINT "   A SUICIDAL MOVE, CAPTAIN.  PODS WILL DETONATE"
06530 PRINT "   IN 12 SECONDS - 10  9  8  7  6  5  4  3  2  1"
06540 GO TO 7050
06545 REM ----------CLOSE----------
06550 LET R=R-200*(RND+.5)
06560 LET B=360*(RND-.5)
06570 LET B1=360*(RND-.5)
06580 IF R>0 THEN 6600
06590 LET R=100*RND+1
06600 RETURN
06605 REM ----------AWAY----------
06610 LET R=R+200*(RND+.5)
06620 LET B=360*(RND-.5)
06630 LET B1=360*(RND-.5)
06640 RETURN
06645 REM ----------PURSUE----------
06650 LET R = R-400*(RND+.5)
06660 LET B=360*(RND-.5)
06670 LET B1=360*(RND-.5)
06680 IF R>0 THEN 6700
06690 LET R=100*RND+1
06700 RETURN
06705 REM ----------EXCAPE----------
06710 LET R=R+400*(RND+.5)
06720 LET B=360*(RND-.5)
06730 LET B1=360*(RND-.5)
06740 RETURN
06745 REM ----------HITS BY ENEMY----------
06750 IF R>500 THEN 3262
06755 PRINT "SPOCK:  THE ";E$ ;" IS FIRING PHOTON TORPEDOES AT US, SIR."
06768 IF RND>FNT(B1) THEN 7030
06770 IF RND<.5 THEN 6970
06780 GO TO 6820
06790 IF R>200 THEN 6755
06795 PRINT "SPOCK:  THE ";E$ ;" IS FIRING PHASERS AT US, SIR."
06808 IF RND>FNP(B1) THEN 6970
06810 IF RND<.25 THEN 6970
06820 LET V = .5
06830 LET K=INT(RND*4)+1
06840 IF S = 0 THEN 6860
06850 LET K = S
06860 PRINT "  A HIT ON SHIELD #";K;"."
06870 IF S1(K)<=0 THEN 6950
06880 LET S1(K)=S1(K)-30*V*(RND+.1)
06890 LET H1=H1+V
06900 GOSUB 6080
06910 IF S1(K)>0 THEN 3262
06920 LET S1(K)=0
06930 PRINT "  SHIELD #";K;" IS GONE."
06940 GO TO 3262
06950 PRINT "COMPUTER: THE ";S$;" HAS BEEN DESTROYED."
06960 GO TO 7430
06970 LET V = 1
06980 LET K=INT(RND*4)+1
06990 IF S = 0 THEN 7010
07000 LET K = S
07010 PRINT "  A DIRECT HIT ON SHIELD #";K;"."
07020 GO TO 6870
07030 PRINT "  EVASIVE MANEUVERS WERE EFFECTIVE, NO DAMAGE."
07040 GO TO 3262
07045 REM ----------CONT----------
07050 PRINT
07060 LET Q = 200 * RND
07070 IF Q<R THEN 7110
07080 PRINT "COMPUTER: RADIUS OF EXPLOSION: ";Q;" MGM."
07090 PRINT "  ";S$;" HAS BEEN DESTROYED."
07100 GO TO 7430
07110 PRINT "SPOCK: "E$ ;" VESSEL DESTROYED."
07120 PRINT "  RADIUS OF EXPLOSION: ";Q;" MGM."
07130 GO TO 7430
07135 REM ----------SUB----------
07140 PRINT
07150 PRINT "SPOCK:  THE POSSIBLE COMMANDS ARE DESIGNATED BY"
07160 PRINT "   THE FOLLOWING NUMBERS OR CODE WORDS:"
07170 PRINT
07180 PRINT " # CODE                  COMMAND"
07190 PRINT
07200 PRINT " 1 RANGE       PRESENT RANGE AND BEARING"
07210 PRINT " 2 PHASEF      FIRE FORWARD PHASERS"
07220 PRINT " 3 PHASER      FIRE REAR PHASERS"
07230 PRINT " 4 TORPF       FIRE FORWARD PHOTON TORPEDOES"
07240 PRINT " 5 TORPR       FIRE REAR PHOTON TORPEDOES"
07250 PRINT " 6 PROBE       LAUNCH ANTIMATTER PROBE (MAXIMUM 10)
07260 PRINT " 7 CLOSE       CLOSE ON ENEMY VESSEL"
07270 PRINT " 8 AWAY        MOVE AWAY FROM ENEMY VESSEL"
07280 PRINT " 9 PURSUE      CLOSE ON ENEMY VESSEL AT WARP SPEED"
07290 PRINT "10 ESCAPE      ESCAPE AT WARP SPEED"
07300 PRINT "11 SHIELDS     SET OPTIMUM SHIELD POSITION"
07310 PRINT "12 ROTATE      ROTATE WEAPONS 60 DEGREES CLOCKWISE"
07320 PRINT "13 CHANCES     FIRING PROBABILITY REPORT"
07330 PRINT "14 COMMANDS    TYPE THIS LIST AGAIN"
07340 PRINT "15 DAMAGE      DAMAGE REPORT"
07350 PRINT "16 BLUFF       ATTEMPT CORBOMITE BLUFFING MANEUVER"
07360 PRINT "17 WAIT        LET ENEMY MOVE NEXT"
07370 PRINT "18 SUICIDE     ACTIVATE SELF-DESTRUCTION (END GAME)
07380 PRINT "19 YIELD       SURRENDER TO ENEMY"
07390 RETURN
07395 REM ----------SUB----------
07400 PRINT "SPOCK:  ";F$ ;" IS AT RANGE:";R;" MGM., BEARING:";B;
07410 PRINT " DEGREES."
07420 RETURN
07425 REM ----------LOST CONTACT----------
07430 PRINT
07440 PRINT "COMPUTER: DO YOU WISH TO ATTEMPT ANOTHER BATTLE";
07460 INPUT A$
07470 IF FNC$(A$)<>"Y" THEN 7720
07480 LET F1=1
07482 PRINT "COMPUTER: DO YOU WANT ANOTHER ASSIGNMENT";
07485 INPUT A$
07487 IF FNC$(A$)<>"Y" THEN 7490
07488 LET S$=N$(RND*16+1)
07489 GO TO 2880
07490 PRINT "COMPUTER: DO YOU WISH TO CHANGE SHIPS";
07500 INPUT A$
07510 IF FNC$(A$)<>"Y" THEN 3070
07520 LET S$=N$(RND*16+1)
07530 GO TO 2660
07535 REM ----------SUB----------
07590 PRINT
07600 PRINT "NOTE:  WEAPON RANGES ARE:"
07610 PRINT "       PHASERS     0-400 MGM (OPTIMUM 200)"
07620 PRINT "       TORPEDOES   300-700 MGM (OPTIMUM 500)"
07630 PRINT "       PROBES      ALL RANGES. TEN IN STOCK"
07640 PRINT
07650 PRINT "PHASERS ARE MORE DEADLY THAN TORPEDOES."
07660 PRINT "PROBES CAUSE TOTAL DESTRUCTION BUT ARE EFFECTIVE"
07670 PRINT "ONLY 15 PERCENT OF THE TIME."
07680 PRINT "TORPEDOES AND PHASERS ARE MORE DEADLY WHEN THE"
07690 PRINT "BEARING OF THE ENEMY IS CLOSE TO 0, 180, AND -180"
07700 PRINT "DEGREES."
07701 PRINT
07702 PRINT "IF YOU KEEP YOUR ASSIGNMENT WITHOUT CHANGING"
07704 PRINT "SHIPS, DAMAGE WILL BE CUMULATIVE."
07710 RETURN
07720 END
