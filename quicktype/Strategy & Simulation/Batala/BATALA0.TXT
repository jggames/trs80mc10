160 CLEAR300:DIMP(9):Y$=CHR$(127)
165 K=176:QQ=501:XX=0:II=0:Z=0
200 CLS3:PRINT"€€€€€€€€BATALHA NO MAR";
210 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
215 PRINT"********************************";
220 PRINT"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
225 PRINT@160,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
226 PRINT@K,"<X>";
255 FORX=.TO9
260 P(X)=RND(280)+221
265 IFX=.THEN275
270 FORQ=.TOX-1:IFP(X)=>P(Q)+2ORP(X)=<P(Q)-2THENNEXT:GOTO275
271 GOTO260
275 PRINT@P(X),Y$;
280 NEXT
285 FORX=.TO9
290 IFP(X)=.THEN315
295 P(X)=P(X)-1
300 IFP(X)<192THEN325
305 PRINT@P(X)+1,"¯";
310 PRINT@P(X),Y$;
315 NEXT
320 IFA$<>" "THEN330
321 RETURN
325 PRINT@P(X)+1,"¯";:XX=XX-5:P(X)=507:GOTO305
330 QQ=QQ-1:PRINT@36,"HITS:"XX,"TIME:"QQ;:IFQQ=.THEN580
370 A$=INKEY$:IFA$=" "THENGOSUB410
375 IFA$="A"THENK=K-2:IFK<160THENK=160
380 IFA$="S"THENK=K+2:IFK>189THENK=189
395 PRINT@160,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";:PRINT@K,"<X>";
400 GOSUB285
405 GOTO330
410 II=K
415 FORI=.TO9
420 II=II+32
425 IFII>511THEN500
430 GOSUB285
460 PRINT@II,":";
465 FORT=.TO9
470 IFP(T)=IITHEN475
471 GOTO485
475 P(T)=.:XX=XX+10:Z=Z+1:PRINT@II,"¯";:SOUND10,1
480 IFZ=10THEN530
481 GOTO500
485 NEXT
490 PRINT@II,"¯";:NEXT
500 A$="":GOTO285
530 Z=.
535 CLS0:PRINT@224,"    ALL SUBMARINES DESTROYED!"
540 PRINT@6,"HITS:"XX"TIME:"QQ;
545 FORTM=1TO10:SOUNDTM*10,1:NEXT:FORZZ=0TO2500:NEXT:GOTO200
580 PRINT@71,"YOUR TIME IS UP";
585 PRINT@96,"      RESULTS >> ";
590 IFXX<100THENPRINT"AWFUL!":GOTO615
595 IFXX<200THENPRINT"OKAY.":GOTO615
600 IFXX<300THENPRINT"GOOD.":GOTO615
605 IFXX<400THENPRINT"SUPER!":GOTO615
610 PRINT"AMAZING!"
615 PRINT@488,"PLAY AGAIN (Y/N)?";:A$=""
620 A$=INKEY$:IFA$=""THEN620
625 IFA$="N"THENCLS:END
626 IFA$="Y"THEN165
627 GOTO615
640 REM ***********************
650 REM 165 INICIALIZACAO
651 REM 200 PREPARA A TELA DO
652 REM JOGO. (USE A,S SPACE)
653 REM 255 ROTINA PRINCIPAL
654 REM 370 VERIFA TECLADO
655 REM 460 VERIFICA SE ACERTOU
656 REM 530 PREPARA NOVA ETAPA
657 REM 580 AVALIA RESULTADOS,
658 REM RETOURNA OU FINALIZA
659 REM ***********************
700 REM    BATALHA NO MAR
710 REM   PARA CP-400 COLOR
720 REM      16K OU 64K
730 REM MC-10 EDIT JIM GERRIE
