0 CLEAR1500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 PRINTTAB((32-LEN(M$))/2)M$:RETURN
10 CLS:PRINT@192,;
20 M$="hockey":GOSUB9
30 M$="COPYRIGHT BY":GOSUB9
40 M$="CREATIVE COMPUTING":GOSUB9
50 M$="MORRISTOWN, NEW JERSEY 07960":GOSUB9
60 PRINT@448,;:GOSUB2250
70 CLS
80 PRINT:PRINT:PRINT
90 X=1
100 PRINT:PRINT:PRINT
110 PRINT"WOULD YOU LIKE INSTRUCTIONS";:INPUTC$
120 CLS
130 IFLEFT$(C$,1)="N"THEN170
140 IFLEFT$(C$,1)="Y"THEN160
150 PRINT"ANSWER YES OR NO!!":GOTO110
160 GOTO1940
170 DIMA$(7),B$(7),H(20),T(5),T2(5),T3(5)
180 PRINT"ENTER THE TWO TEAMS";:INPUTA$(7),B$(7)
190 PRINT
200 PRINT"ENTER THE NUMBER OF MINUTES IN":PRINT"A GAME";:INPUTT6
210 PRINT
220 IFT6<1THEN200:PRINT
230 CLS
240 M$="WOULD THE "+A$(7)+" COACH ENTER HIS TEAM:":GOSUB1
250 PRINT
260 FORI=1TO6:PRINT"PLAYER"STR$(I);:INPUTA$(I):NEXTI:PRINT
270 CLS
280 M$="WOULD THE "+B$(7)+" COACH DO THE SAME:":GOSUB1
290 PRINT
300 FORT=1TO6:PRINT"PLAYER"STR$(T);:INPUTB$(T):NEXTT:PRINT
310 CLS
320 PRINT"ENTER THE REFEREE FOR THIS GAME ";:INPUTR$
330 PRINT:M$=A$(7)+" STARTING LINEUP":GOSUB9
340 FORT=1TO6:PRINTA$(T):NEXTT:PRINT
350 GOSUB2250
360 PRINT:M$=B$(7)+" STARTING LINEUP":GOSUB9
370 FORT=1TO6:PRINTB$(T):NEXTT:PRINT
380 M$="WE'RE READY FOR TONIGHT'S OPENING FACE-OFF.":GOSUB1
390 M$=R$+" WILL DROP THE PUCK BETWEEN "+A$(2)+" AND "+B$(2)+".":GOSUB1
400 FORL=1TOT6:IFL=1THEN420
410 M$="AND WE'RE READY FOR THE FACE-OFF.":GOSUB1
420 C=RND(2):ONCGOTO430,440
430 M$=A$(7)+" HAVE CONTROL OF THE PUCK.":GOSUB1:GOTO450
440 M$=B$(7)+" HAVE CONTROL.":GOSUB1
450 PRINT"pass? ";:GOSUB2290:P=TW:FORN=1TO3:H(N)=0:NEXTN
460 CLS:PRINT"PASS";P
470 IFP<0THEN450
480 IFP>3THEN450
490 FORJ=1TO(P+2)
500 H(J)=RND(5)
510 NEXTJ:IFH(J-1)=H(J-2)THEN490
520 IFP+2<3THEN550
530 IFH(J-1)=H(J-3)THEN490
540 IFH(J-2)=H(J-3)THEN490
550 IFP=0THEN570
560 GOTO700
570 PRINT"shot? ";:GOSUB2290:S=TW:IFS<1THEN570
580 IFS>4THEN570
590 ONCGOTO600,690
600 M$=A$(H(J-1)):G=H(J-1):G1=0:G2=0
610 ONSGOTO620,640,650,670
620 M$=M$+" LET'S A BOOMER GO FROM THE RED LINE!!":GOSUB1
630 Z=10:GOTO1100
640 M$=M$+" FLIPS A WRISTSHOT DOWN THE ICE.":GOSUB1
641 Z=25:GOTO1100
650 M$=M$+" BACKHANDS ONE IN ON THE GOALTENDER.":GOSUB1
660 Z=25:GOTO1100
670 M$=M$+" SNAPS A LONG FLIP SHOT.":GOSUB1
680 Z=17:GOTO1100
690 M$=B$(H(J-1)):G1=0:G2=0:G=H(J-1):GOTO610
700 ONCGOTO710,850
710 ONPGOTO720,750,780
720 M$=A$(H(J-2))+" LEADS "+A$(H(J-1))+" WITH A PERFECT PASS.":GOSUB1
730 M$=A$(H(J-1))+" CUTTING IN!!!":GOSUB1
740 G=H(J-1):G1=H(J-2):G2=0:Z1=3:GOTO980
750 M$=A$(H(J-2))+" GIVES TO A STREAKING "+A$(H(J-1))+".":GOSUB1
760 M$=A$(H(J-3))+" COMES DOWN ON "+B$(5)+" AND "+B$(4)+".":GOSUB1
770 G=H(J-3):G1=H(J-1):G2=H(J-2):Z1=2:GOTO980
780 M$="OH MY!! A '4 ON 2' SITUATION":GOSUB1
790 M$=A$(H(J-3))+" LEADS "+A$(H(J-2))+".":GOSUB1
800 M$=A$(H(J-2))+" IS WHEELING THROUGH CENTRE.":GOSUB1
810 M$=A$(H(J-2))+" GIVES AND GOES WITH "+A$(H(J-1))+".":GOSUB1
820 PRINT"PRETTY PASSING!"
830 M$=A$(H(J-1))+" DROPS IT TO "+A$(H(J-4))+".":GOSUB1
840 G=H(J-4):G1=H(J-1):G2=H(J-2):Z1=1:GOTO980
850 ONPGOTO860,880,930
860 M$=B$(H(J-1))+" HITS "+B$(H(J-2))+" FLYING DOWN THE LEFT SIDE.":GOSUB1
870 G=H(J-2):G1=H(J-1):G2=0:Z1=3:GOTO980
880 M$="IT'S A '3 ON 2'!":GOSUB1
890 M$="ONLY "+A$(4)+" AND "+A$(5)+" ARE BACK.":GOSUB1
900 M$=B$(H(J-2))+" GIVES OFF TO "+B$(H(J-1))+".":GOSUB1
910 M$=B$(H(J-1))+" DROPS TO "+B$(H(J-3))+".":GOSUB1
920 G=H(J-3):G1=H(J-1):G2=H(J-2):Z1=2:GOTO980
930 M$="A '3 ON 2' WITH A 'TRAILER'!":GOSUB1
940 M$=B$(H(J-4))+" GIVES TO "+B$(H(J-2))+" WHO SHUFFLES IT OFF TO "
950 M$=M$+B$(H(J-1))+" WHO FIRES A WING TO WING PASS TO "
960 M$=M$+B$(H(J-3))+" AS HE CUTS IN ALONE!":GOSUB1
970 G=H(J-3):G1=H(J-1):G2=H(J-2):Z1=1:GOTO980
980 PRINT"shot? ";:GOSUB2290:S=TW:IFS>4THEN980:IFS<1THEN980
990 ONCGOTO1000,1090
1000 M$=A$(G):ONSGOTO1010,1030,1050,1070
1010 M$=M$+" LETS A BIG SLAP SHOT GO!":GOSUB1
1020 Z=4:Z=Z+Z1:GOTO1100
1030 M$=M$+" RIPS A WRIST SHOT OFF.":GOSUB1
1040 Z=2:Z=Z+Z1:GOTO1100
1050 M$=M$+" GETS A BACKHAND OFF.":GOSUB1
1060 Z=3:Z=Z+Z1:GOTO1100
1070 M$=M$+" FLICKS OFF A SNAP SHOT.":GOSUB1
1080 Z=2:Z=Z+Z1:GOTO1100
1090 M$=B$(G):ONSGOTO1010,1030,1050,1070
1100 PRINT"area? ";:GOSUB2290:A=TW:IFA<1THEN1100
1110 IFA>4THEN1100
1120 ONCGOTO1130,1140
1130 S2=S2+1:GOTO1150
1140 S3=S3+1
1150 A1=RND(4):IFA<>A1THEN1430
1160 H(20)=RND(100)
1170 IFINT(H(20)/Z)=H(20)/ZTHEN1380
1180 ONCGOTO1190,1200
1190 PRINT"GOAL "A$(7):H(9)=H(9)+1:GOTO1210
1200 PRINT"SCORE "B$(7):H(8)=H(8)+1
1210 M$="SCORE: ":IFH(8)>H(9)THEN1230
1220 M$=M$+A$(7)+":"+STR$(H(9))+"  "+B$(7)+":"+STR$(H(8)):GOSUB1:GOTO1240
1230 M$=M$+B$(7)+":"+STR$(H(8))+"  "+A$(7)+":"+STR$(H(9)):GOSUB1
1240 ONCGOTO1250,1310
1250 M$="GOAL SCORED BY: "+A$(G)+",":IFG1=0THEN1280
1260 IFG2=0THEN1290
1270 M$=M$+" ASSISTED BY: "+A$(G1)+" AND "+A$(G2)+".":GOSUB1:GOTO1300
1280 M$=M$+" UNASSISTED.":GOSUB1:GOTO1300
1290 M$=M$+" ASSISTED BY: "+A$(G1)+".":GOSUB1
1300 T(G)=T(G)+1:T1(G1)=T1(G1)+1:T1(G2)=T1(G2)+1:GOTO1770
1310 M$="GOAL SCORED BY: "+B$(G)+","
1320 IFG1=0THEN1350
1330 IFG2=0THEN1360
1340 M$=M$+" ASSISTED BY: "+B$(G1)+" AND "+B$(G2)+".":GOSUB1:GOTO1370
1350 M$=M$+" UNASSISTED.":GOSUB1:GOTO1370
1360 M$=M$+" ASSISTED BY: "+B$(G1)+".":GOSUB1:GOTO1370
1370 T2(G)=T2(G)+1:T3(G1)=T3(G1)+1:T3(G2)=T3(G2)+1:GOTO1770
1380 A2=RND(100):IFINT(A2/4)=A2/4THEN1400
1390 GOTO1430
1400 ONCGOTO1410,1420
1410 M$="SAVE "+B$(6)+" -- REBOUND":GOSUB1:GOTO1160
1420 M$="SAVE "+A$(6)+" -- FOLLOW UP":GOSUB1:GOTO1160
1430 S1=RND(6)
1440 ONCGOTO1450,1610
1450 ONS1GOTO1460,1490,1520,1530,1560,1580
1460 M$="STICK SAVE BY "+B$(6)+" AND CLEARED OUT BY "+B$(3)+".":GOSUB1
1480 GOTO420
1490 M$="WHAT A SPECTACULAR GLOVE SAVE BY "+B$(6)+"!":GOSUB1
1500 M$="AND "+B$(6)+" GOLFS IT INTO THE CROWD.":GOSUB1
1510 GOTO1770
1520 M$="SKATE SAVE ON A LOW STEAMER BY "+B$(6)+".":GOSUB1:GOTO420
1530 M$="PAD SAVE BY "+B$(6)+" OFF THE STICK OF "+A$(G)+" AND "+B$(6)+" COVERS UP.":GOSUB1
1550 GOTO1770
1560 M$="WHISTLES ONE OVER THE HEAD OF "+B$(6)+"!":GOSUB1
1570 GOTO420
1580 M$=B$(6)+" MAKES A FACE SAVE!! AND HE IS HURT!":GOSUB1
1590 M$="THE DEFENSEMAN "+B$(5)+" COVERS UP FOR HIM.":GOSUB1
1600 GOTO1770
1610 ONS1GOTO1620,1640,1670,1700,1720,1750
1620 M$="STICK SAVE BY "+A$(6)+" AND CLEARED OUT BY "+A$(4)+".":GOSUB1
1630 GOTO420
1640 M$="OH MY! "+B$(G)+" RATTLES ONE OFF THE POST TO THE RIGHT OF "+A$(6)+" AND "+A$(6)+" COVERS "
1660 M$=M$+"ON THE LOOSE PUCK!":GOSUB1:GOTO1770
1670 M$="SKATE SAVE BY "+A$(6)+".":GOSUB1
1680 M$=A$(6)+" WHACKS THE LOOSE PUCK INTO THE STANDS.":GOSUB1
1690 GOTO1770
1700 M$="STICK SAVE BY "+A$(6)+" AND HE CLEARS IT OUT HIMSELF.":GOSUB1
1710 GOTO420
1720 M$="KICKED OUT BY "+A$(6)+" AND IT REBOUNDS ALL THE WAY TO CENTRE ICE.":GOSUB1
1740 GOTO420
1750 M$="GLOVE SAVE "+A$(6)+" AND HE HANGS ON.":GOSUB1
1760 GOTO1770
1770 NEXTL:PRINT"THAT'S THE SIREN":SOUND50,10
1780 INPUT"HIT enter FOR FINAL STATS";M$
1790 CLS
1800 PRINT"SCORING SUMMARY"
1810 M$=A$(7)+" --"+STR$(H(9)):GOSUB9
1820 PRINT"NAME";TAB(16);"GOALS";TAB(24);"ASSISTS"
1830 PRINT"----";TAB(16);"-----";TAB(24);"-------"
1840 FORI=1TO5:PRINTLEFT$(A$(I),14)TAB(16);T(I);TAB(24);T1(I)
1850 NEXTI:PRINT
1860 GOSUB2250
1870 M$=B$(7)+" --"+STR$(H(8)):GOSUB9
1880 PRINT"NAME";TAB(16);"GOALS";TAB(24);"ASSISTS"
1890 PRINT"----";TAB(16);"-----";TAB(24);"-------"
1900 FORT=1TO5:PRINTLEFT$(B$(T),14);TAB(16);T2(T);TAB(24);T3(T)
1910 NEXTT:PRINT
1920 PRINT"SHOTS ON NET":PRINTA$(7)":";S2:PRINTB$(7)":";S3
1930 END
1940 PRINT"THIS IS A SIMULATED HOCKEY GAME."
1970 PRINT"pass"
1971 PRINT"TYPE NUMBER OF PASSES YOU WOULD"
1980 PRINT"LIKE TO MAKE, FROM 0 TO 3.":PRINT
1990 PRINT"shot"
1991 PRINT"TYPE THE NUMBER OF THE KIND OF"
2000 PRINT"SHOT YOU WANT TO MAKE:"
2010 PRINT"1 FOR A SLAPSHOT"
2020 PRINT"2 FOR A WRISTSHOT"
2030 PRINT"3 FOR A BACKHAND"
2040 PRINT"4 FOR A SNAP SHOT"
2050 PRINT
2060 GOSUB2250
2070 PRINT:PRINT"area"
2071 PRINT"ENTER NUMBER CORRESPONDING TO"
2080 PRINT"AREA YOU ARE AIMING AT:"
2090 PRINT"1 FOR UPPER LEFT HAND CORNER"
2100 PRINT"2 FOR UPPER RIGHT HAND CORNER"
2110 PRINT"3 FOR LOWER LEFT HAND CORNER"
2120 PRINT"4 FOR LOWER RIGHT HAND CORNER"
2130 PRINT
2140 GOSUB2250
2150 PRINT
2160 M$="AT THE START OF THE GAME, YOU WILL BE ASKED FOR THE NAMES OF YOUR PLAYERS.":GOSUB1
2170 M$="THEY ARE ENTERED IN THE ORDER:":GOSUB1
2180 M$="LEFT WING, CENTRE, RIGHT WING, LEFT DEFENSE, RIGHT DEFENSE, GOALKEEPER.":GOSUB1
2190 M$="ANY OTHER INPUT REQUIRED WILL HAVE EXPLANATORY INSTRUCTIONS.":GOSUB1
2210 PRINT
2220 GOSUB2250
2230 GOTO170
2240 END
2250 M$="hit any key to continue":GOSUB9:FORZZ=1TO25:CC=RND(0):M$=INKEY$:NEXT
2260 A$=INKEY$:IFA$=""THEN2260
2280 CLS:RETURN
2290 A$=INKEY$:IFA$=""THEN2290
2300 IFA$<"0"ORA$>"9"THEN2290
2310 PRINTA$:SOUND100,2
2320 TW=VAL(A$)
2330 RETURN
