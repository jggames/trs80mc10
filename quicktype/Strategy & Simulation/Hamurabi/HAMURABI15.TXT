0 CLS:CLEAR400,36762:DIMB,G,K,C,Z,M$,A$:GOSUB50:GOTO100
1 B=1:C=32
2 IFMID$(M$,C,1)<>""ANDMID$(M$,C,1)<>" "THENC=C-1:GOTO2
3 A$=MID$(M$,B,C-B):PRINTA$;:Z=((PEEK(17024)AND1)*256+PEEK(17025))+16383:GOSUB7:B=C+1:C=B+31:IFB<=LEN(M$)THEN2
4 RETURN
7 FORG=Z-LEN(A$)+1TOZ:POKEG,PEEK(G)-64:NEXT:K=INT((Z-16384)/32)+1:IFK<16THENPRINT@32*K,;:RETURN
9 PRINT@511,CHR$(127);:POKE16863,32:RETURN
10 FORB=0TO15:GOSUB9:NEXT:RETURN
50 V=32:X=36762:FORI=0TO103:POKEX+I,PEEK(64022+I):NEXT:DATA49,49,60,55,54
53 FORI=0TO4:READY:POKEX+I,Y:NEXT:POKE36855,V:POKE17033,143:POKE17034,154:POKE17032,126:RETURN
60 I$="":Z=((PEEK(17024)AND1)*256+PEEK(17025))+16384:POKEZ,96
61 M$=INKEY$:IFM$=""THEN61
63 IFM$=CHR$(13)THEN70
64 IFM$=CHR$(8)THENIFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):PRINTCHR$(8);:POKEZ,32:Z=Z-1:POKEZ,96
65 IFM$<"0"ORM$>"Z"THEN61
66 PRINTM$;:POKEZ,PEEK(Z)-64:POKEZ+1,96:Z=Z+1:I$=I$+M$:GOTO61
70 M$=" ":GOSUB1:RETURN
100 POKE49151,64:GOSUB10:M$=" ":GOSUB1:M$="TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA "
101 M$=M$+"SUCCESSFULLY FOR A 10-YR TERM OF OFFICE.":GOSUB1:M$=" ":GOSUB1:GOSUB1000
102 B=RND(-(PEEK(9)*256+PEEK(10)))
103 D1=0:P1=0
110 YR=0:P=95:S=2800:H=3000:E=H-S
120 Y=3:A=H/Y:I=5:Q=1
210 D=0
215 M$=" ":GOSUB1:M$="HAMURABI:":GOSUB1:M$="I BEG TO REPORT TO YOU, ":GOSUB1:YR=YR+1
217 M$="IN YEAR"+STR$(YR)+","+STR$(D)+" PEOPLE STARVED AND"+STR$(I)+" CAME TO THE CITY.":GOSUB1
218 P=P+I
227 IF Q>0 THEN 230
228 P=INT(P/2)
229 M$="A HORRIBLE PLAGUE STRUCK! HALF THE PEOPLE DIED.":GOSUB1
230 M$="THE POPULATION IS NOW"+STR$(P):GOSUB1 
232 M$="THE CITY NOW OWNS"+STR$(A)+" ACRES.":GOSUB1
235 M$="YOU HARVESTED"+STR$(Y)+" BUSHELS PER ACRE.":GOSUB1
250 M$="RATS ATE"+STR$(E)+" BUSHELS.":GOSUB1
260 M$="YOU NOW HAVE"+STR$(S)+" BUSHELS IN STORE.":GOSUB1
270 IF YR=11 THEN GOSUB1000:GOTO 860
310 C=INT(10*RND(0)):Y=C+17
312 M$="LAND IS TRADING AT"+STR$(Y)+" BUSHELS PER ACRE.":GOSUB1
315 AA=A:SS=S:CC=C:DD=D
320 M$="HOW MANY ACRES DO YOU WISH TO BUY?":GOSUB1
321 GOSUB60:Q=VAL(I$):IFI$>"A"THENGOSUB850:GOTO320
322 IF Y*Q<=S THEN 330
323 GOSUB 710
324 GOTO 320
330 IF Q=0 THEN 340
331 A=A+Q:S=S-Y*Q:C=0
334 GOTO 400
340 M$="HOW MANY ACRES DO YOU WISH TO SELL?":GOSUB1
341 GOSUB60:Q=VAL(I$):IFI$>"A"THENGOSUB850:GOTO320
342 IF Q<A THEN 350
343 GOSUB 720
344 GOTO 340 
350 A=A-Q:S=S+Y*Q:C=0
400 M$="HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE?":GOSUB1
411 GOSUB60:Q=VAL(I$):IFI$>"A"THENGOSUB850:GOTO320
418 REM *** TRYING TO USE MORE GRAIN THAN IN THE SILOS?
420 IF Q<=S THEN 430 
421 GOSUB 710
422 GOTO 400 
430 S=S-Q:C=1
440 M$="HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED?":GOSUB1
441 GOSUB60:D=VAL(I$):IFI$>"A"THENGOSUB850:GOTO320
442 IF D=0 THEN 511
444 REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
445 IF D<=A THEN 450
446 GOSUB 720
447 GOTO 440
449 REM *** ENOUGH GRAIN FOR SEED?
450 IF INT(D/2)<S THEN 455
452 GOSUB 710
453 GOTO 440 
454 REM *** ENOUGH PEOPLE TO TEND THE CROPS?
455 IF D<10*P THEN 510
460 M$="BUT YOU HAVE ONLY"+STR$(P)+" PEOPLE TO TEND THE FIELDS. NOW THEN,":GOSUB1
470 GOTO 440
510 S=S-INT(D/2)
511 GOSUB 800
512 REM *** A BOUNTYFULL HARVEST!!
515 Y=C:H=D*Y:E=0
521 GOSUB 800
522 IF INT(C/2)<>C/2 THEN 530
523 REM *** THE RATS ARE RUNNING WILD!!
525 E=INT(S/C)
530 S=S-E+H
531 GOSUB 800
532 REM *** LET'S HAVE SOME BABIES
533 I=INT(C*(20*A+S)/P/100+1)
539 REM *** HOW MANY PEOPLE HAD FULL TUMMIES?
540 C=INT(Q/20)
541 REM *** HORRORS, A 15% CHANCE OF PLAGUE
542 Q=INT(10*(2*RND(0)-.3))
550 IF P<C THEN 210
551 REM *** STARVE ENOUGH FOR IMPEACHMENT?
552 D=P-C:IF D>.45*P THEN 560
553 P1=((YR-1)*P1+D*100/P)/YR
555 P=C:D1=D1+D:GOTO 215
560 M$=" ":GOSUB1:M$="YOU STARVED"+STR$(D)+" PEOPLE IN ONE YEAR!!!":GOSUB1
565 M$="DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY BEEN IMPEACHED AND THROWN OUT OF OFFICE":GOSUB1
566 M$="BUT YOU HAVE ALSO BEEN DECLARED 'NATIONAL FINK' !!":GOSUB1:GOTO 990
710 M$="HAMURABI: THINK AGAIN. YOU HAVE ONLY "
711 M$=M$+STR$(S)+" BUSHELS OF GRAIN. NOW THEN,":GOSUB1
712 RETURN 
720 M$="HAMURABI: THINK AGAIN. YOU OWN ONLY"+STR$(A)+" ACRES. NOW THEN,":GOSUB1
730 RETURN
800 C=INT(RND(0)*5)+1
801 RETURN
850 IFI$="QUIT"ORI$="EXIT"THENM$="DO YOU WISH TO QUIT?":GOSUB1:GOSUB60:IFLEFT$(I$,1)="Y"THENEND
855 M$=" ":GOSUB1:M$="HAMURABI: FORGIVE ME, I DO NOT UNDERSTAND YOUR REQUEST.":GOSUB1
857 M$="PLEASE LET US START AGAIN FROM THE BEGINNING...":GOSUB1:A=AA:S=SS:C=CC:D=DD:RETURN
860 M$="IN YOUR 10-YEAR TERM OF OFFICE,"+STR$(INT(P1*10)/10)+" PERCENT OF THE POPULATION STARVED PER YEAR ON AVERAGE,":GOSUB1
862 M$="I.E., A TOTAL OF"+STR$(D1)+" PEOPLE DIED!!":GOSUB1:L=A/P
870 M$="YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
875 M$=M$+STR$(INT(L*10)/10)+" ACRES PER PERSON.":GOSUB1:M$=" ":GOSUB1
880 IF P1>33 THEN 565
885 IF L<7 THEN 565
890 IF P1>10 THEN 940
892 IF L<9 THEN 940
895 IF P1>3 THEN 960
896 IF L<10 THEN 960
900 M$="A FANTASTIC PERFORMANCE!!! CHARLEMANGE, DISRAELI, AND":GOSUB1
905 M$="JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!":GOSUB1:GOTO 990
940 M$="YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV.":GOSUB1
945 M$="THE PEOPLE (REMAINING) FIND YOU AN UNPLEASANT RULER, AND,":GOSUB1
950 M$="FRANKLY, HATE YOUR GUTS!":GOSUB1:GOTO 990
960 M$="YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT REALLY WASN'T TOO BAD AT ALL.":GOSUB1
966 M$=MID$(STR$(INT(P*.8*RND(0))),2)+" PEOPLE WOULD "
970 M$=M$+"DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR TRIVIAL PROBLEMS.":GOSUB1
990 M$=" ":GOSUB1:GOSUB1000
995 M$="DO YOU WISH TO TRY AGAIN?":GOSUB1
996 GOSUB60:IFLEFT$(I$,1)="Y"THEN100
999 END
1000 FOR N=1 TO 2500:NEXT N:RETURN
