0 CLS:CLEAR250:DIMC1,CC,ZZ,MC:MC=16384:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 PRINTTAB(INT((32-LEN(M$))/2));M$:RETURN
6 REM PRINT REVERSE MESSESAGE: INPUT VARS I$=MSG,C1=SCREEN POS: A,B,Z,M ALSO USED
8 PRINT@C1,M$;:C1=C1-1:FORZZ=1TOLEN(M$):CC=MC+C1+ZZ:POKECC,PEEK(CC)-64:NEXT:RETURN
9 C1=(PEEK(17024)AND1)*256+PEEK(17025):RETURN
10 M=8
20 DIMM(4*M*M),N$(1),S(7)
25 S(1)=ASC("*"):S(2)=ASC("."):S(3)=ASC("C"):S(4)=ASC("M")
26 S(5)=ASC("B"):S(6)=ASC("S"):S(7)=ASC("A")
30 DIMMM(1),B(1),P(1),A(1),A8(1),P8(1),B8(1),F8(1),SS(1),T(1,1)
40 FORI=1TO5:PRINT:NEXT:M$="** NUKEWAR **":GOSUB5:PRINT
50 M$="COPYRIGHT 1980 BY":GOSUB5:PRINT:M$="AVALON HILL":GOSUB5
60 M$="MICROCOMPUTER GAMES":GOSUB5:FORI=1TO4:PRINT:NEXT
65 PRINT:PRINT:PRINT
70 C1=RND(-(PEEK(9)*256+PEEK(10)))
80 INPUT"YOUR COUNTRY NAME";N$(0)
90 INPUT"ENEMY COUNTRY NAME";N$(1)
100 FORI=1TO4*M*M:M(I)=S(2):NEXT
110 FORP=1TOM*M+1STEPM*M:FORC=1TOM+2
120 L=INT(M*M*RND(0))+P:IFM(L)<>S(2)THEN120
130 IFC<=MTHENA=S(3):GOTO132
131 A=S(C-M+3)
132 M(L)=A:NEXT:NEXT
140 Y=1955+INT(10*RND(0)):Y0=Y
145 MI=.33*RND(0)
150 Y=Y+1:M$=STR$(Y)+" COLD WAR ":GOSUB5
160 GOSUB5000
190 T9=5:IFY>=1965THENT9=6:IFY>=1970THENT9=7
200 A$="":INPUT"COLD WAR ORDERS";A$:A$=LEFT$(A$,1):IFA$="W"THEN580
205 IFRND(0)<(Y-Y0)*.01THEN590
206 IFQ1=1THEN340
210 B=1:SS(0)=3*M:IFA$="S"THEN220
211 B=2:SS(0)=M
220 FORI=1TOB
230 A$="":INPUT"TYPE OF INSTALLATION";A$:IFA$=""THENA$=" "
231 A=ASC(A$)
250 T8=4
251 IFA=S(T8)THEN270
252 T8=T8+1:IFT8<=T9THEN251
260 PRINT"IMPROPER TYPE, TRY AGAIN.":GOTO230
270 INPUT"LOCATION";R,C:R=INT(ABS(R)):C=INT(ABS(C))
280 IFR<=0ORR>MORC<=0ORC>MTHEN320
290 L=C+(R-1)*M:IFM(L)<>S(2)THEN320
300 IFA<>S(6)THEN330
310 IFR=1ORR=MORC=1ORC=MTHEN330
320 PRINT"IMPROPER LOCATION, TRY AGAIN. ":GOTO270
330 M(L)=A:NEXT
340 B=1:SS(1)=3*M:IFRND(0)<.1THEN350
341 B=2:SS(1)=M
350 I=1
360 IFRND(0)<MITHENA=S(4):GOTO370
361 A=S(4+INT((T9-3)*SQR(RND(0))))
370 B8=1
380 IFA=S(6)THEN400
390 L=M*M+1+INT(M*M*RND(0)):GOTO420
400 C=1+INT(M*RND(0)):R=1+(M-1)*INT(2*RND(0))
410 IFRND(0)<.5THENL=M*(M+R-1)+C:GOTO420
411 L=M*(M+C-1)+R
420 IFM(L)=S(2)THEN440
430 B8=B8+1:IFB8<21THEN380
431 GOTO585
440 M(L)=A:I=I+1:IFI<=BTHEN360
441 C=2*M*M
450 FORI=0TO1:P=1+I*M*M:IFSS(1-I)<=0THEN471
451 FORB8=1TOSS(1-I)
460 L=P+INT(M*M*RND(0)):M(L+C)=M(L)
470 NEXT
471 NEXT
480 FORI=0TO1:P=I*M*M:FORR=0TOM-1:FORC=1TOM
490 L=C+P+M*R:T=1
491 IFM(L)=S(T)THEN500
492 T=T+1:IFT<8THEN491
500 IFT<4ORRND(0)<.998THEN550
510 IFI=0THENM$="NUCLEAR ACCIDENT AT"+STR$(R+1)","+STR$(C):GOSUB1
520 M(L)=S(1):R1=R-1+INT(3*RND(0)):C1=C-1+INT(C*RND(0))
530 IFR1<0ORR1>M-1ORC1>MTHEN550
540 L=C1+P+M*R1:M(L)=S(1)
550 NEXT:NEXT:NEXT
560 IFQ1=1THENM$="ORDER TO STRIKE FIRST HAS BEEN DISOBEYED.":GOSUB1
570 Q1=0:GOTO150
580 SS(0)=0:Q1=1:IFRND(0)<.25THEN205
581 GOTO600
585 Y=Y+1
590 Q2=1
600 M$="**NUCLEAR WAR**":GOSUB5:Q3=Q1:Q4=Q2:IFA$="S"THENQ1=1:Q=Q1
610 FORI=0TO1:FORL=I*M*M+1TO(I+1)*M*M
620 IFM(L)=S(4)THEN760
630 IFM(L)=S(5)THEN720
640 IFM(L)=S(6)THEN690
650 IFM(L)<>S(7)THEN770
660 IFRND(0)<.25+.25*QTHEN680
670 A(I)=A(I)+1:GOTO770
680 GOSUB40000:A8(I)=A8(I)+FR:M(L)=S(2):GOTO770
690 IFRND(0)<.5+1*QTHEN710
700 P(I)=P(I)+1:GOTO770
710 P8(I)=P8(I)+1:M(L)=S(2):GOTO770
720 IFRND(0)<.1+.2*QTHEN740
730 B(I)=B(I)+1:GOTO770
740 GOSUB40000:P=FR:C=INT((P+1)*RND(0)):P=P-C
750 B8(I)=B8(I)+P:F8(I)=F8(I)+C:M(L)=S(2):GOTO770
760 MM(I)=MM(I)+1
770 NEXT:Q=Q2:NEXT
780 IFB(0)+MM(0)+P(0)+A(0)=0THENQ1=0
790 IFB(1)+MM(1)+P(1)+A(1)=0THENQ2=0
800 GOSUB6000
810 IFQ1<1THEN940
811 I=0
820 INPUT"BASE TO ACTIVATE";R,C:R=INT(ABS(R)):C=INT(ABS(C))
830 IFR<1ORR>MORC<1ORC>MTHEN870
840 L=C+(R-1)*M:T=1
850 IFM(L)=S(T)THEN880
860 T=T+1:IFT<8THEN850
870 M$="NO BASE AT THAT LOCATION.":GOSUB1:GOTO820
880 ONTGOTO870,870,870,890,910,920,930
890 GOSUB7010:GOTO940
910 GOSUB8000:GOTO940
920 GOSUB9000:GOTO940
930 GOSUB10000
940 IFQ2=0THEN1040
941 T8=M*M+1+INT(M*M*RND(0)):T9=T8:I=1
950 T9=T9+1:IFT9>M*M*2THENT9=M*M+1
960 L=T9:T=1
970 IFM(T9)=S(T)THEN990
980 T=T+1:IFT<8THEN970
981 PRINT"E970"
990 ONTGOTO950,950,950,1000,1010,1020,1030
1000 GOSUB7010:GOTO1040
1010 GOSUB8000:GOTO1040
1020 GOSUB9000:GOTO1040
1030 GOSUB10000
1040 FORI=0TO1:IF(I=0ANDQ1=0)OR(I=1ANDQ2=0)THEN1115
1045 FORL=I*M*M+1TO(I+1)*M*M
1050 T=1
1051 IFM(L)=S(T)THEN1060
1052 T=1+T:IFT<8THEN1051
1053 PRINT"E1050"
1060 ONTGOTO1110,1110,1110,1070,1080,1090,1100
1070 IFRND(0)<.15THENGOSUB7000
1071 GOTO1110
1080 IFRND(0)<.10THENGOSUB8000
1081 GOTO1110
1090 IFRND(0)<.05THENGOSUB9000
1091 GOTO1110
1100 IFRND(0)<.25THENGOSUB10000
1110 NEXT
1115 NEXT
1120 FORI=0TO1:P7=P8(I):IFP7<=0THEN1171
1121 FORP9=1TOP7:IFRND(0)>.05*WTHEN1170
1130 IFI=1THEN1140
1135 GOSUB5000:M$="MISSILE SUBMARINE"+STR$(P(I)+P8(I))+" ON STATION.":GOSUB1
1140 GOSUB40000:J=FR:P8(I)=P8(I)-1
1150 IFI=0THENM$=STR$(J)+" SUBMARINE MISSILES READY TO FIRE. TARGETS:":GOSUB1
1160 IFI=1ANDA8(0)>0THENGOSUB7015
1161 GOSUB7030
1170 NEXT
1171 NEXT
1173 FORL=1TOM*M*2:P=L+M*M*2
1176 IFM(P)=S(1)THENM(L)=S(1)
1177 NEXT
1180 FORI=0TO1:IFRND(0)>.1*WORB8(I)=0THEN1340
1190 IFB8(I)<5THENJ=B8(I):GOTO1200
1191 J=INT((B8(I)-3)*RND(0))+4
1200 B8(I)=B8(I)-J:M$=STR$(J)+"BOMBERS ARRIVE OVER "+N$(1-I)+"."
1220 IFI=1ANDF8(0)+A8(0)>0THENM$="AIR DEFENSE COMMAND ALERT":GOSUB1
1230 IFF8(1-I)=0THEN1260
1240 IFRND(0)<.9THENJ=J-1
1241 IFJ=0THEN1330
1250 IFRND(0)<.8THENF8(1-I)=F8(1-I)-1
1251 GOTO1230
1260 IFA8(1-I)=0THEN1290
1270 A8(1-I)=A8(1-I)-1:IFRND(0)<.5THENJ=J-1
1280 IFJ=0THEN1330
1281 GOTO1260
1290 M$=STR$(J)+" BOMBERS SURVIVE AIR DEFENSE OVER"+N$(1-I)+".":GOSUB1
1300 FORJ2=1TOJ:GOSUB20000
1310 P=C+(R-1+(1-I)*M)*M:M(P)=S(1)
1315 P=P+2*M*M:M(P)=S(1)
1320 NEXT:GOTO1340
1330 M$="ALL BOMBERS DESTROYED BY AIR DEFENSE OVER"+N$(1-I)+".":GOSUB1
1340 NEXT
1370 Q=0:Q5=P8(0)*6+B8(0):Q6=P8(1)*6+B8(1):FORI=0TO1:Q2=0
1375 MM(I)=0:B(I)=0:P(I)=0:A(I)=0:FORL=1+I*M*MTO(I+1)*M*M
1380 T=1
1381 IFM(L)=S(T)THEN1390
1382 T=T+1:IFT<8THEN1381
1383 PRINT"E1380"
1390 ONTGOTO1450,1450,1450,1400,1410,1420,1430
1400 MM(I)=MM(I)+1:GOTO1440
1410 B(I)=B(I)+1:GOTO1440
1420 P(I)=P(I)+1:GOTO1440
1430 A(I)=A(I)+1
1440 Q=Q+1:Q2=Q2+1
1450 NEXT:NEXT:Q1=Q-Q2:Q=Q+Q5+Q6:Q5=Q1*3+Q5:Q6=Q2*3+Q6:IFQ=0THEN1530
1460 W=W+1:IFRND(0)<.5THEN800
1470 M$="THE PREMIER OF "+N$(1)+" IS ON THE HOT LINE. DO YOU WISH TO NEGOTIATE A PEACE SETTLEMENT?":GOSUB1
1480 A$="":INPUT"(Y/N)";A$
1490 IFLEFT$(A$,1)<>"Y"THEN800
1500 IF(INT(Q5)+1)/(INT(Q6)+1)>RND(0)THEN1520
1510 M$="NEGOTIATIONS ARE UNSUCCESSFUL.":GOSUB1:GOTO800
1520 M$="NEGOTIATIONS ARE SUCCESSFUL.":GOSUB1:GOTO1540
1530 M$="ALL NUCLEAR WEAPONS HAVE BEEN EXPENDED.":GOSUB1
1540 M$="NUCLEAR WAR IS OVER.":GOSUB1:P=M*M*2:Q=0:FORI=0TO1:P2=0
1550 FORL=1+M*M*ITO(I+1)*M*M:M(L+P)=M(L)
1560 IFM(L)=S(1)THEN1580
1561 Q=Q+1:P2=P2+1
1570 IFM(L)<>S(3)THEN1580
1571 Q=Q+10:P2=P2+10
1580 NEXT:NEXT:P1=Q-P2:GOSUB5000
1590 IFQ3*RND(0)>.5THEN1600
1591 Q3=0
1600 IFQ4*RND(0)>.5THEN1610
1601 Q4=0
1610 IFQ3>0THENM$="WORLD OPINION IS AGAINST "+N$(0)+".":GOSUB1
1611 M$="THE POPULATION OF"+N$(0)+" IS"+STR$(P1)+" MILLION.":GOSUB1
1620 IFQ4>0THENM$="WORLD OPINION IS AGAINST "+N$(1)+".":GOSUB1
1621 M$="THE POPULATION OF "+N$(1)+" IS"+STR$(P2)+" MILLION.":GOSUB1
1630 RI=(P1+1)*(Q4+1)/(P2+1)/(Q3+1):IFRI<1THENSI=1/RI:GOTO1640
1631 SI=RI
1640 IFSI=RITHENP=P1:GOTO1650
1641 P=P2
1650 IFP<.2*M*(M+10)THEN1690
1660 IFSI>3THEN1700
1670 IFSI>2THEN1710
1680 IFSI>1.5THEN1720
1690 M$="NEITHER COUNTRY GAINS ADVANTAGE.":GOTO1740
1700 M$="TOTAL STRATEGIC":GOTO1730
1710 M$="MAJOR":GOTO1730
1720 M$="MARGINAL"
1730 M$=M$+" VICTORY FOR ":IFSI=RITHENM$=M$+N$(0)+".":GOTO1740
1731 M$=M$+N$(1)+"."
1740 GOSUB1:GOTO63000
5000 PRINTN$(0);TAB(M*1.3+6);N$(1):GOSUB9:M$="1 2 3 4 5 6 7 8 1 2 3 4 5 6 7 8":GOSUB8:PRINT
5010 T=M*M*3:FORR=0TOM-1:GOSUB9:FORP=0TOTSTEPT
5020 FORC=1TOM:L=P+C+R*M:PRINTCHR$(M(L));" ";:NEXT
5030 NEXT:POKEMC+C1+16-33,49+R:NEXT:RETURN
6000 GOSUB5000
6010 M$="FIGHTERS AIRBORNE:"+STR$(F8(0)):GOSUB1:M$="BOMBERS AIRBORNE:"+STR$(B8(0)):GOSUB1
6030 IFY>1965THENM$="MISSILE SUBMARINES AT SEA:"+STR$(P8(0)):GOSUB1
6040 IFY>1970THENM$="ABM'S IN ACTIVE TRACKING MODE:"+STR$(A8(0)):GOSUB1
6050 RETURN
7000 GOSUB40000:J=FR:M(L)=S(2):IFI=0THEN7019
7001 GOTO7015
7010 GOSUB40000:J=FR:M(L)=S(2):IFI=0THEN7020
7015 IFA8(0)>0THENM$="ABM ALERT INCOMING ICBM'S":GOSUB1
7016 GOTO7030
7019 GOSUB5000
7020 M$=STR$(J)+" MISSILES READY AT MISSILE BASE"+STR$(MM(0))+". TARGETS:":GOSUB1
7030 FORJ2=1TOJ
7040 IFI=1THENGOSUB20020:GOTO7050
7041 GOSUB30000
7050 IFR<1ORR>MORC<1ORC>MTHEN7040
7060 IFA8(1-I)=0ORRND(0)<.1THEN7075
7070 A8(1-I)=A8(1-I)-1:IFRND(0)<.9THEN7110
7075 IFRND(0)>EXP((1960-Y)/10)THEN7100
7080 R=R-1+INT(3*RND(0)):C=C-1+INT(3*RND(0))
7090 IFR<1ORR>MORC<1ORC>MTHEN7110
7100 P=C+(R-1+(3-I)*M)*M:M(P)=S(1)
7110 NEXT:MM(I)=MM(I)-1:IFI=0THENPRINT
7120 RETURN
8000 GOSUB40000:J=FR:IFI>0THEN8040
8010 M$=STR$(J)+" AIRCRAFT READY AT AIR FORCE BASE"+STR$(B(0))+".":GOSUB1
8020 INPUT"NUMBER TO ARM AS FIGHTERS";P:P=INT(ABS(P))
8030 IFP>JTHEN8020
8031 GOTO8050
8040 P=INT(RND(0)*(J+1))
8050 B8(I)=B8(I)+J-P:F8(I)=F8(I)+P:B(I)=B(I)-1:M(L)=S(2)
8060 IFI=0THEN6010
8061 RETURN
9000 IFI=0THENM$="MISSILE SUBMARINE"+STR$(P(0))+" AT SEA.":GOSUB1
9010 M(L)=S(2):P8(I)=P8(I)+1:P(I)=P(I)-1:RETURN
10000 IFI=0THENM$="ABM BASE"+STR$(A(0))+" IS NOW ACTIVE.":GOSUB1
10010 GOSUB40000:M(L)=S(2):A8(I)=A8(I)+FR:A(I)=A(I)-1
10020 RETURN
20000 I3=0:L8=1+(1-I)*M*M:L9=M*M-1+L8:GOTO20030
20020 I3=1:L8=1+(3-I)*M*M:L9=M*M-1+L8
20030 L7=L8+INT((L9-L8+1)*RND(0)):L6=L7:IFT(I,I3)>0THEN20060
20040 L6=L6+1:IFL6>L9THENL6=L8
20050 T=3
20051 IFM(L6)=S(T)THEN20080
20052 T=T+1:IFT<8THEN20051
20055 IFL6<>L7THEN20040
20060 IFT(I,I3)>1THEN20080
20061 T(I,I3)=1
20065 L6=L6+1:IFL6>L9THENL6=L8
20070 IFM(L6)<>S(1)THEN20080
20071 IFL6<>L7THEN20065
20075 T(I,I3)=2
20080 R=INT((L6-L8)/M):C=L6-L8+1-R*M:R=R+1:RETURN
30000 PRINT":";:A$=""
30010 B$=INKEY$:IFB$=""THEN30010
30011 BA=ASC(B$):IFBA=13THEN30020
30012 IFBA=8THEN30014
30013 A$=A$+B$:PRINTB$;:GOTO30010
30014 IFLEN(A$)>=1THENA$=LEFT$(A$,LEN(A$)-1):PRINTB$;
30015 GOTO30010
30020 PN=1:LN=LEN(A$):IFLN<3THENPRINT"INPUT AGAIN";:GOTO30000
30030 IFMID$(A$,PN,1)=","THEN30050
30040 PN=PN+1:IFPN<LNTHEN30030
30041 PRINT"INPUT AGAIN";GOTO30000
30050 R=VAL(LEFT$(A$,PN-1)):C=VAL(MID$(A$,PN+1,LN)):RETURN
40000 FR=1+INT(M*.5*(1+RND(0))):RETURN
63000 END
