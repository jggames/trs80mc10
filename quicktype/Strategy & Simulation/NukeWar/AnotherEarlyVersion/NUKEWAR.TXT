1 REM 23-Dec-80
5 POKE 82,2:POKE 83,39:M=8:DIM B$(151):FOR J=1 TO 151:B$(J,J)=" ":NEXT J
10 GRAPHICS 0:POSITION 12,7:PRINT "** NUKEWAR **":POSITION 2,10:PRINT "Copyright 1980 by Microcomputer Games"
20 DIM S$(7),M$(4*M*M),N$(15),N1$(15),N2$(15),A$(1),L$(2),M(1),B(1),P(1),A(1),A8(1),P8(1),B8(1),F8(1),S(1),T(1,1)
30 S$="*.CMBSA":POSITION 2,20
80 PRINT "Your country name";:INPUT N1$
90 PRINT "Enemy country name";:INPUT N2$
100 FOR I=1 TO 4*M*M:M$(I,I)=".":NEXT I:FOR P=1 TO M*M+1 STEP M*M:FOR C=1 TO M+2
120 L=INT(M*M*RND(0))+P:IF M$(L,L)<>"." THEN 120
130 A$="C":IF C>M THEN A$=S$(C-M+3)
140 M$(L,L)=A$:NEXT C:NEXT P
145 Y=1955+INT(10*RND(0)):Y0=Y:M9=RND(0)/3:PRINT "}";:POKE 752,1:POSITION 22,0
146 PRINT "COLD WAR":PRINT N1$;:POSITION 22,1:PRINT N2$
147 FOR P=0 TO 20 STEP 20:FOR C=1 TO M:POSITION C+C+P,2:PRINT CHR$(64+C);:NEXT C:NEXT P
148 FOR R=4 TO M+3:POSITION 19,R:PRINT R-3;:NEXT R
150 Y=Y+1:GOSUB 5000:POSITION 2,0:PRINT Y:GOSUB 4000
190 T9=5:IF Y>1964 THEN T9=6:IF Y>1969 THEN T9=7
200 PRINT "Cold war orders";:INPUT A$:GOSUB 4000:IF A$="W" THEN 580
205 IF RND(0)<(Y-Y0)*0.01 THEN 590
206 IF Q1=1 THEN 340
210 B=1:S(0)=3*M:IF A$="S" THEN 220
215 B=2:S(0)=M
220 FOR I=1 TO B
230 PRINT "Base type";:INPUT A$:T8=4
240 IF A$=S$(T8,T8) THEN 270
250 T8=T8+1:IF T8<=T9 THEN 240
251 GOSUB 4000:PRINT "Try again. ";:GOTO 230
260 POP 
270 PRINT "Location";:GOSUB 3000:IF C=0 THEN 320
280 L=C+(R-1)*M:IF M$(L,L)<>"." THEN 320
290 IF A$<>"S" THEN 330
300 IF (R=1) OR (R=M) OR (C=1) OR (C=M) THEN 330
320 GOSUB 4000:PRINT "Try again. ";:GOTO 270
330 M$(L,L)=A$:GOSUB 3100:GOSUB 4000:NEXT I
340 B=1:S(1)=3*M:IF RND(0)<0.1 THEN 350
345 B=2:S(1)=M
350 I=0
351 I=I+1:A$="M":IF RND(0)<M9 THEN 370
360 A$=S$(4+INT((T9-3)*SQR(RND(0))))
370 B8=0
371 B8=B8+1
380 IF A$="S" THEN 400
390 L=M*M+1+INT(M*M*RND(0)):GOTO 420
400 C=1+INT(M*RND(0)):R=1+(M-1)*INT(2*RND(0))
410 L=M*(M+R-1)+C:IF RND(0)<0.5 THEN L=M*(M+C-1)+R
420 IF M$(L,L)="." THEN 440
430 IF B8<20 THEN 371
431 GOTO 585
440 M$(L,L)=A$:A$="":I=I+1:IF I<B THEN 351
441 C=2*M*M
450 FOR I=0 TO 1:P=1+I*M*M:FOR B8=1 TO S(1-I)
460 L=P+INT(M*M*RND(0)):M$(L+C,L+C)=M$(L,L)
470 NEXT B8:NEXT I
480 FOR I=0 TO 1:P=I*M*M:FOR R=0 TO M-1:FOR C=1 TO M
490 IF RND(0)<0.998 THEN 550
500 L=C+P+M*R:A$=M$(L,L):IF (A$="*") OR (A$=".") OR (A$="C") THEN 550
510 IF I=0 THEN PRINT "NUCLEAR ACCIDENT AT ";CHR$(C+64);CHR$(R+49);"!":GOSUB 3400
520 M$(L,L)="*":R1=R-1+INT(3*RND(0)):C1=C-1+INT(3*RND(0)):R=M+M:C=R
530 IF (R1<0) OR (R1>=M) OR (C1<=0) OR (C1>M) THEN 550
540 L=C1+P+R1*M:M$(L,L)="*"
550 NEXT C:NEXT R:NEXT I
560 IF Q1=1 THEN PRINT "Your generals will not strike first."
570 Q1=0:GOTO 150
580 S(0)=0:Q1=1:IF RND(0)<0.25 THEN 205
581 GOTO 600
585 Y=Y+1
590 Q2=1
600 POSITION 22,0:PRINT "NUCLEAR WAR":GOSUB 4000:Q3=Q1:Q4=Q2:IF (S(0)>M) OR (A$="S") THEN Q1=1
601 Q=Q1
610 W=0:FOR I=0 TO 1:M(I)=0:B(I)=0:P(I)=0:A(I)=0:B8(I)=0:F8(I)=0:P8(I)=0:A8(I)=0:T(I,0)=0:T(I,1)=0
615 FOR L=I*M*M+1 TO (I+1)*M*M
620 IF M$(L,L)="M" THEN 760
630 IF M$(L,L)="B" THEN 720
640 IF M$(L,L)="S" THEN 690
650 IF M$(L,L)<>"A" THEN 770
660 IF RND(0)<0.25+0.25*Q THEN 680
670 A(I)=A(I)+1:GOTO 770
680 A8(I)=A8(I)+1+INT(SQR(RND(0))*M):M$(L,L)=".":GOTO 770
690 IF RND(0)<0.5+0.1*Q THEN 710
700 P(I)=P(I)+1:GOTO 770
710 P8(I)=P8(I)+1:M$(L,L)=".":GOTO 770
720 IF RND(0)<0.1+0.2*Q THEN 740
730 B(I)=B(I)+1:GOTO 770
740 P=1+INT(SQR(RND(0))*M):C=INT((P+1)*RND(0)):P=P-C
750 B8(I)=B8(I)+P:F8(I)=F8(I)+C:M$(L,L)=".":GOTO 770
760 M(I)=M(I)+1
770 NEXT L:Q=Q2:NEXT I
780 IF B(0)+M(0)+P(0)+A(0)=0 THEN Q1=0
790 IF B(1)+M(1)+P(1)+A(1)=0 THEN Q2=0
800 GOSUB 5000:GOSUB 6000
810 IF Q1<1 THEN 940
820 I=0:PRINT "Base to activate";:GOSUB 3000:IF C=0 THEN 870
840 L=C+(R-1)*M:T=4
850 IF M$(L,L)=S$(T,T) THEN 880
860 T=T+1:IF T<8 THEN 850
870 GOSUB 4000:PRINT "Try again. ";:GOTO 820
880 ON T GOTO 870,870,870,890,910,920,930
890 GOSUB 7000:GOTO 940
910 GOSUB 8000:GOTO 940
920 GOSUB 9000:GOTO 940
930 GOSUB 10000
940 IF Q2=0 THEN 1040
941 T8=M*M+1+INT(M*M*RND(0)):T9=T8:I=1
950 T9=T9+1:IF T9>M*M*2 THEN T9=M*M+1
951 L=T9:T=1
960 IF M$(T9,T9)=S$(T,T) THEN 990
980 T=T+1:IF T<8 THEN 960
981 T=1
990 ON T GOTO 950,950,950,1000,1010,1020,1030
1000 GOSUB 7000:GOTO 1040
1010 GOSUB 8000:GOTO 1040
1020 GOSUB 9000:GOTO 1040
1030 GOSUB 10000
1040 FOR I=0 TO 1:IF ((I=0) AND (Q1=0)) OR ((I=1) AND (Q2=0)) THEN 1115
1045 FOR L=I*M*M+1 TO (I+1)*M*M:T=1
1050 IF M$(L,L)=S$(T,T) THEN 1060
1051 T=T+1:IF T<8 THEN 1050
1052 T=1
1060 ON T GOTO 1110,1110,1110,1070,1080,1090,1100
1070 IF RND(0)<0.15 THEN GOSUB 7000
1071 GOTO 1110
1080 IF RND(0)<0.1 THEN GOSUB 8000
1081 GOTO 1110
1090 IF RND(0)<0.05 THEN GOSUB 9000
1091 GOTO 1110
1100 IF RND(0)<0.25 THEN GOSUB 10000
1110 NEXT L
1115 NEXT I
1120 FOR I=0 TO 1:P7=P8(I):FOR P9=1 TO P7:IF (RND(0)>0.05*W) OR (P7=0) THEN 1170
1130 IF I=1 THEN 1140
1135 POSITION 2,20:PRINT "Submarine ";P(0)+P8(0);" on station."
1140 J=1+INT(SQR(RND(0))*M):P8(I)=P8(I)-1
1150 IF I=0 THEN PRINT J;" Trident missiles ready to fire.":GOSUB 7030
1160 IF I=1 THEN GOSUB 7015
1170 NEXT P9:NEXT I
1173 FOR L=1 TO M*M*2:P=L+M*M*2
1176 IF M$(P,P)="*" THEN M$(L,L)="*"
1180 NEXT L:FOR I=0 TO 1:IF (RND(0)>0.1*W) OR (B8(I)=0) THEN 1340
1190 J=INT((B8(I)-3)*RND(0))+4:IF B8(I)<5 THEN J=B8(I)
1200 B8(I)=B8(I)-J:I2=1-I:GOSUB 30000
1210 IF I=1 THEN POSITION 2,20:PRINT "Air defense alert!      "
1220 PRINT J;" bombers arrive over ";N$;"."
1230 IF F8(I2)=0 THEN 1260
1240 IF RND(0)<0.9 THEN J=J-1
1241 IF J=0 THEN 1330
1250 IF RND(0)<0.8 THEN F8(I2)=F8(I2)-1
1251 GOTO 1230
1260 IF A8(I2)=0 THEN 1290
1270 A8(I2)=A8(I2)-1:IF RND(0)<0.5 THEN J=J-1
1280 IF J=0 THEN 1330
1281 GOTO 1260
1290 PRINT J;" bombers survive air defense!";:GOSUB 3400
1300 FOR J2=1 TO J:GOSUB 20000:GOSUB 3300:P=C+(R-1+I2*M)*M:M$(P,P)="*":P=P+2*M*M:M$(P,P)="*":NEXT J2:GOTO 1340
1330 PRINT "All bombers destroyed!";:GOSUB 3400
1340 GOSUB 6000:NEXT I
1370 Q=0:Q5=P8(0)*2+B8(0)/3:Q6=P8(1)*2+B8(1)/3:FOR I=0 TO 1:Q2=0
1375 M(I)=0:B(I)=0:P(I)=0:A(I)=0:FOR L=1+I*M*M TO (I+1)*M*M:T=1
1380 IF M$(L,L)=S$(T,T) THEN 1390
1381 T=T+1:IF T<8 THEN 1380
1382 T=1
1390 ON T GOTO 1450,1450,1450,1400,1410,1420,1430
1400 M(I)=M(I)+1:GOTO 1440
1410 B(I)=B(I)+1:GOTO 1440
1420 P(I)=P(I)+1:GOTO 1440
1430 A(I)=A(I)+1
1440 Q=Q+1:Q2=Q2+1
1450 NEXT L:NEXT I:Q1=Q-Q2:Q=Q+Q5+Q6:Q5=Q1+Q5:Q6=Q6+Q2:IF Q=0 THEN 1530
1460 W=W+1:IF RND(0)<0.5 THEN 800
1470 PRINT "The Premier of ";N2$;" is"
1480 PRINT "on the Hot Line. Negotiate (Y/N)";:INPUT A$:GOSUB 4000:IF A$<>"Y" THEN 800
1500 IF (Q5+1)/(Q6+1)>RND(0) THEN 1520
1510 PRINT "Negotiations are unsuccessful.":GOTO 800
1520 PRINT "Negotiations are successful.":GOTO 1540
1530 PRINT "All offensive weapons have been used."
1540 PRINT "Nuclear war is over!":POSITION 22,0:PRINT "FINAL RESULTS";:P=M*M*2:Q=0:FOR I=0 TO 1:P2=0
1550 FOR L=1+M*M*I TO (I+1)*M*M:M$(L+P,L+P)=M$(L,L):IF M$(L,L)="*" THEN 1580
1560 Q=Q+1:P2=P2+1:IF M$(L,L)<>"C" THEN 1580
1570 Q=Q+10:P2=P2+10
1580 NEXT L:NEXT I:P1=Q-P2:GOSUB 5000
1590 IF Q3*RND(0)<0.5 THEN Q3=0
1600 IF Q4*RND(0)<0.5 THEN Q4=0
1610 GOSUB 4000:POSITION 2,18:IF Q3>0 THEN PRINT "World opinion against ";N1$;"."
1611 PRINT N1$;" has ";P1;" million people."
1620 IF Q4>0 THEN PRINT "World opinion against ";N2$;"."
1621 PRINT N2$;" has ";P2;" million people."
1630 R=(P1+1)*(Q4+1)/(P2+1)/(Q3+1):S=1/R:IF R>1 THEN S=R
1640 P=P2:IF S=R THEN P=P1
1650 IF P<0.2*M*(M+10) THEN 1690
1660 IF S>3 THEN 1700
1670 IF S>2 THEN 1710
1680 IF S>1.5 THEN 1720
1690 PRINT "Neither country gains advantage.":GOTO 1740
1700 PRINT "Total strategic";:GOTO 1730
1710 PRINT "Major";:GOTO 1730
1720 PRINT "Marginal";
1730 PRINT " victory for ";:N$=N2$:IF S=R THEN N$=N1$
1731 PRINT N$;"."
1740 PRINT "Play again (Y/N)";:INPUT A$:IF A$="Y" THEN 80
1750 STOP 
3000 INPUT L$
3010 C=0:R=0:IF LEN(L$)<2 THEN RETURN 
3020 C=ASC(L$)-64:R=ASC(L$(2))-48:IF (R<1) OR (R>M) OR (C<0) OR (C>M) THEN 3040
3030 RETURN 
3040 C=0:R=0:RETURN 
3100 POSITION C+C,3+R:PRINT A$;:RETURN 
3200 R=INT((L-1)/M):C=L-R*M:R=R+1:A$="+":GOSUB 3100:GOTO 6000
3300 POSITION C+C+20-20*I,3+R:GET #6,X:IF X<128 THEN X=X+128
3310 POSITION C+C+20-20*I,3+R:PUT #6,X:GOTO 4000
3400 FOR J3=1 TO 400:NEXT J3:RETURN 
4000 POSITION 2,20:PRINT B$;:POSITION 2,21:RETURN 
5000 T=M*M*3:FOR R=0 TO M-1:FOR P=0 TO T STEP T:FOR C=1 TO M:L=P+C+R*M
5010 POSITION C+C+20*(P>0),4+R:PRINT M$(L,L);:NEXT C:NEXT P:NEXT R
5090 RETURN 
6000 POSITION 2,13
6010 PRINT "Fighters in the air: ";F8(0);"  "
6020 PRINT "Bombers in the air: ";B8(0);"  "
6030 IF Y>1965 THEN PRINT "Submarines at sea: ";P8(0);"  "
6040 IF Y>1970 THEN PRINT "ABM's in active mode: ";A8(0);"  "
6050 GOTO 4000
7000 J=1+INT(SQR(RND(0))*M):M$(L,L)=".":IF I=0 THEN 7020
7015 POSITION 2,20:PRINT "Incoming ICBM's!           ":IF A8(0)>0 THEN POSITION 19,20:PRINT "ABM alert!"
7016 GOSUB 3400:GOTO 7030
7020 GOSUB 3200:PRINT J;" MX missiles ready at MX Base ";M(0)
7030 FOR J2=1 TO J:IF I=0 THEN 7040
7035 GOSUB 20020:GOTO 7060
7040 PRINT "Target for missile ";J2;:GOSUB 3000:IF C=0 THEN GOSUB 4000:GOTO 7040
7060 GOSUB 3300:IF (A8(1-I)=0) OR (RND(0)<0.1) THEN 7075
7070 A8(1-I)=A8(1-I)-1:IF RND(0)<0.9 THEN 7110
7075 IF RND(0)>EXP((1960-Y)/10) THEN 7100
7080 R=R-1+INT(3*RND(0)):C=C-1+INT(3*RND(0))
7090 IF (R<1) OR (R>M) OR (C<1) OR (C>M) THEN 7110
7100 P=C+(R-1+(3-I)*M)*M:M$(P,P)="*"
7110 NEXT J2:M(I)=M(I)-1:GOTO 6000
8000 J=1+INT(SQR(RND(0))*M):IF I>0 THEN 8040
8010 GOSUB 3200:PRINT J;" aircraft ready at AF Base ";B(0);".":PRINT "Number to arm as fighters";
8020 INPUT A$:P=0:IF LEN(A$)=0 THEN 8050
8030 IF ASC(A$)<48 OR ASC(A$)>57 THEN 8050
8035 P=VAL(A$):P=P-(P-J)*(P>J):GOTO 8050
8040 P=INT(RND(0)*(J+1))
8050 B8(I)=B8(I)+J-P:F8(I)=F8(I)+P:B(I)=B(I)-1:M$(L,L)=".":IF I=0 THEN GOSUB 6000
8060 RETURN 
9000 M$(L,L)=".":P8(I)=P8(I)+1:P(I)=P(I)-1
9020 IF I=0 THEN GOSUB 3200:PRINT "Submarine ";P(0)+1;" at sea.":GOSUB 3400
9030 RETURN 
10000 M$(L,L)=".":A(I)=A(I)-1:A8(I)=A8(I)+1+INT(SQR(RND(0))*M)
10020 IF I=0 THEN GOSUB 3200:PRINT "ABM base ";A(0)+1;" is now active.":GOSUB 3400
10030 RETURN 
20000 I3=0:L8=1+(1-I)*M*M:L9=M*M-1+L8:GOTO 20030
20020 I3=1:L8=1+(3-I)*M*M:L9=M*M-1+L8
20030 L7=L8+INT((L9-L8+1)*RND(0)):L6=L7:IF T(I,I3)>0 THEN 20060
20040 L6=L6+1:IF L6>L9 THEN L6=L8
20050 FOR T=3 TO 7:IF M$(L6,L6)=S$(T,T) THEN POP :GOTO 20080
20055 NEXT T:IF L6<>L7 THEN 20040
20060 IF T(I,I3)>1 THEN 20080
20061 T(I,I3)=1
20065 L6=L6+1:IF L6>L9 THEN L6=L8
20070 IF M$(L6,L6)<>"*" THEN 20080
20071 IF L6<>L7 THEN 20065
20075 T(I,I3)=2
20080 R=INT((L6-L8)/M):C=L6-L8+1-R*M:R=R+1:RETURN 
30000 IF I2=0 THEN N$=N1$:RETURN 
30010 N$=N2$:RETURN
