0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:ZZ=RND(-(PEEK(9)*256+PEEK(10))):GOTO5
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Z$:CLS:RETURN
5 L=0:W=0:R1=0:P1=0:Q1=0:M3=0:M4=0:P2=0:T1=0:T2=0:CLS
6 PRINT@256,"    t h e   c i v i l  w a r!!":GOSUB995:PRINT:PRINT
7 PRINT"  DO YOU WANT A DESCRIPTION OF":PRINT"  THE GAME (Y/N)";:INPUT Z$
9 FORU=1TO6 
10 PRINT
11 NEXT U 
13 IFZ$="N"THEN100
15 CLS:PRINT "THIS IS A CIVIL WAR SIMULATION."
20 M$="TO PLAY, TYPE A RESPONSE WHEN THE COMPUTER ASKS.":GOSUB1
30 M$="REMEMBER THAT ALL FACTORS ARE INTERRELATED AND THAT YOUR "
35 M$=M$+"RESPONSES COULD CHANGE HISTORY. FACTS AND FIGURES USED ARE " 
40 M$=M$+"BASED ON THE ACTUAL OCCURENCE.":GOSUB1
41 GOSUB3
45 CLS:M$="MOST BATTLES TEND TO RESULT "
46 M$=M$+"AS THEY DID IN THE CIVIL WAR, BUT IT ALL DEPENDS ON YOU!!":GOSUB1
50 PRINT
51 M$="THE OBJECT OF THE GAME IS TO WIN AS MANY BATTLES AS POSSIBLE.":GOSUB1
52 PRINT:GOSUB3:GOSUB55:GOTO99
55 M$="YOUR CHOICES FOR DEFENSIVE STRATEGY ARE:":GOSUB1
60 PRINT"(1) ARTILLERY ATTACK"
65 PRINT"(2) FORTIFY FOR FRONTAL ATTACK"
70 PRINT"(3) FORTIFY FLANKS"
75 PRINT"(4) FALLING BACK":GOSUB3
80 M$="YOUR CHOICES FOR OFFENSIVE STRATEGY ARE:":GOSUB1
85 PRINT"(1) ARTILLERY ATTACK"
90 PRINT"(2) FRONTAL ATTACK"
95 PRINT"(3) FLANKING MANUEVERS"
96 PRINT"(4) ENCIRCLEMENT"
97 M$="YOU MAY SURRENDER BY TYPING A '5' FOR YOUR STRATEGY.":GOSUB1
98 PRINT"TYPE 'H' FOR THIS MENU.":PRINT:RETURN
99 PRINT"YOU ARE THE confederacy.":PRINT"GOOD LUCK!":GOSUB3
100 READ M1,M2,C1,C2,M,A,U
101 I1=10+(L-W)*2
102 I2=10+(W-L)*2
103 D1=100*INT((M1*(100-I1)/2000)*(1+(R1-Q1)/(R1+1))+0.5)
104 D2=100*INT(M2*(100-I2)/2000+0.5)
105 F1=5*M1/6
106 A1=Z
107 FORU=1TO4
108 PRINT
109 NEXTU
110 CLS:M$="THIS IS THE BATTLE OF "
115 GOSUB800
120 CLS:PRINTTAB(9);"confederacy";TAB(21);"UNION"
130 PRINT"MEN"TAB(9);STR$(INT(M1*(1+(P1-T1)/(M3+1))));TAB(20);STR$(INT(M2*(1+(P2-T2)/(M4+1))))
140 PRINT"MONEY $"TAB(9);STR$(D1);TAB(19);"$";STR$(D2)
150 PRINT"INFLATION"TAB(9);STR$(I1+15)"%";TAB(20);STR$(I2);"%"
160 PRINT
170 PRINT"HOW MUCH DO YOU WISH TO SPEND   FOR FOOD";
180 INPUTF
185 IFF<0THEN750
190 PRINT"HOW MUCH DO YOU WISH TO SPEND   FOR SALARIES";
200 INPUTS
205 IFS<0THEN750
210 PRINT"HOW MUCH DO YOU WISH TO SPEND   FOR AMMUNITION";
220 INPUTB
221 IFB<0THEN750
222 PRINT
224 IFF+S+B<=D1THEN230
226 M$="THINK AGAIN! YOU HAVE ONLY $"+STR$(D1):GOSUB1
228 GOTO160 
230 O=((2*F^2+S^2)/F1^2+1)
235 IFO<10THEN260
240 PRINT"MORALE IS HIGH" 
250 GOTO300 
260 IFO<5THEN290
270 PRINT"MORALE IS FAIR" 
280 GOTO300 
290 PRINT"MORALE IS POOR" 
300 IFM<>3THEN330 
310 PRINT"YOU ARE ON THE OFFENSIVE" 
320 GOTO370
330 IFM<>1THEN360 
340 PRINT"YOU ARE ON THE DEFENSIVE" 
350 GOTO370 
360 M$="BOTH SIDES ARE ON THE OFFENSIVE":GOSUB1
370 PRINT
380 PRINT"YOUR STRATEGY";:INPUTM$:IFLEFT$(M$,1)="H"THENGOSUB55:GOTO380
390 Y=VAL(M$)
391 IFY=5THEN1487
392 IFABS(Y-3)<3THEN395
393 M$="YOU JERK! USE THE OTHER SET OF STRATEGIES!":GOSUB1
394 GOTO370
395 PRINT
400 PRINTTAB(9);"confederacy";TAB(21);"UNION"
410 C5=(2*C1/5)*(1+1/(2*(ABS(INT(RND(4)+1)-Y)+1))) 
412 C5=INT(C5*(1+1/O)*(1.28+F1/(B+1))+0.5) 
414 IF C5+100/O<M1*(1+(P1-T1)/(M3+1)) THEN 424 
416 C5=INT(13*M1/20*(1+(P1-T1)/(M3+1)))
418 E=7*C5/13
420 U=1
422 GOTO426 
424 E=100/O
426 PRINT"CASUALTIES"TAB(11);STR$(C5);TAB(20);STR$(INT(17*C2*C1/(C5*20)+0.5))
430 PRINT"DESERTIONS"TAB(11);STR$(INT(E));TAB(20);STR$(INT(5*O))
432 PRINT
433 IFC5-C1>=0THEN439
435 M$="YOUR CASUALTIES WERE"+STR$(INT(100*(C1-C5)/C1+0.5))+"% LESS THAN "
437 GOTO441
439 M$="YOUR CASUALTIES WERE"+STR$(INT(100*(C5-C1)/C1+0.5))+"% MORE THAN "
441 M$=M$+"THE ACTUAL CASUALITIES AT "
443 A1=1
445 GOSUB800
450 IFU=1THEN470
460 IFC5+E<17*C2*C1/(C5*20)+5*OTHEN490
470 M$="YOU LOSE "
471 L=L+1
480 GOTO555
490 M$="YOU WIN "
491 W=W+1
555 GOSUB800:PRINT:GOSUB3
556 IFW=8THEN1490 
580 T1=T1+C5+E 
590 T2=T2+17*C2*C1/(C5*20)+5*O 
600 P1=P1+C1
610 P2=P2+C2
620 Q1=Q1+(F+S+B)
630 R1=R1+M1*(100-I1)/20 
635 M3=M3+M1
637 M4=M4+M2
650 IFA=14THEN1500
660 GOTO100 
670 DATA 18000,18500,1967,2708,1,1,0 
672 DATA 40000,44894,10699,13047,3,2,0 
674 DATA 95000,115000,20614,15849,3,3,0
676 DATA 54000,63000,10000,14000,2,4,0 
678 DATA 40000,50000,10000,12000,3,5,0 
680 DATA 75000,120000,5377,12653,1,6,0 
682 DATA 38000,45000,11000,12000,1,7,0 
684 DATA 32000,90000,13000,17197,2,8,0 
686 DATA 50000,70000,12000,19000,1,9,0 
688 DATA 72500,85000,20000,23000,3,10,0
690 DATA 66000,60000,18000,16000,2,11,0
692 DATA 37000,60000,6700,5800,2,12,0
694 DATA 62000,110000,17723,18000,2,13,0 
696 DATA 65000,100000,8500,3700,1,14,0 
750 PRINT"GO TO JAIL."
752 PRINT"GO DIRECTLY TO JAIL." 
754 PRINT"DO NOT PASS GO."
756 PRINT"DO NOT COLLECT $200"
758 GOTO107 
800 IFA<>1THEN850 
810 M$=M$+"BULL RUN":GOSUB1 
820 IFA1=1THEN1480
830 PRINT"JULY 21, 1861":M$="GEN. BEAUREGARD COMMANDING THE SOUTH MET THE "
832 M$=M$+"UNION FORCES WITH GEN MCDOWELL IN A PREMATURE BATTLE AT BULL " 
834 M$=M$+"RUN.":GOSUB1:M$="GEN. JACKSON HELPED PUSH BACK THE UNION ATTACK.":GOSUB1
835 GOSUB3
840 GOTO1480 
850 IFA<>2THEN900 
860 M$=M$+"SHILOH":GOSUB1
870 IFA1=1THEN1480
880 PRINT"APRIL 6-7, 1862":M$="THE CONFEDERATE SURPRISE ATTACK AT SHILOH "
882 M$=M$+"FAILED DUE TO POOR ORGANIZATION.":GOSUB1
883 GOSUB3
890 GOTO1480
900 IFA<>3THEN950 
910 M$=M$+"SEVEN DAYS":GOSUB1
920 IFA1=1THEN1480
930 PRINT"JUNE 25-JULY 1, 1862":M$="GENERAL LEE (CSA) UPHELD THE OFFENSIVE "
932 M$=M$+"THROUGHOUT THE BATTLE AND FORCED GEN. MCCLELLAN AND THE UNION "
934 M$=M$+"FORCES AWAY FROM RICHMOND.":GOSUB1
935 GOSUB3
940 GOTO1480 
950 IFA<>4THEN1000
960 M$=M$+"THE SECOND BULL RUN":GOSUB1
970 IFA1=1THEN1480
980 PRINT"AUG 29-30, 1862":M$="THE COMBINED CONFEDERATE FORCES UNDER LEE AND "
982 M$=M$+"JACKSON DROVE THE UNION FORCES BACK INTO WASHINGTON.":GOSUB1
983 GOSUB3
990 GOTO1480 
995 FORZZ=1TO3:SOUND1,1:SOUND1,1:SOUND1,1:SOUND1,1:FORCC=1TO99:NEXT:SOUND1,2
996 FORCC=1TO99:NEXT:SOUND1,2:FORCC=1TO99:NEXT:NEXT:SOUND1,1:SOUND1,1
997 FORZZ=1TO3:FORCC=1TO99:NEXT:SOUND1,2:NEXT:RETURN
1000 IFA<>5THEN1050
1010 M$=M$+"ANTIETAM":GOSUB1
1020 IFA1=1THEN1480
1030 PRINT"SEPT 17, 1862":M$="THE SOUTH FAILED TO INCORPORATE MARYLAND INTO "
1032 M$=M$+"THE CONFEDERACY.":GOSUB1
1033 GOSUB3
1040 GOTO1480
1050 IFA<>6THEN1100
1060 M$=M$+"FREDERICKSBURG":GOSUB1
1070 IFA1=1THEN1480
1080 PRINT"DEC 13, 1862":M$="THE CONFEDERACY UNDER LEE SUCESSFULLY REPULSED "
1082 M$=M$+"AN ATTACK BY THE UNION UNDER GEN. BURNSIDE.":GOSUB1
1083 GOSUB3
1090 GOTO1480
1100 IFA<>7THEN1150 
1110 M$=M$+"MURFREESBORO":GOSUB1
1120 IFA1=1THEN1480
1130 PRINT"DEC 31, 1862":M$="THE SOUTH UNDER GEN. BRAGG WON A CLOSE BATTLE.":GOSUB1
1131 GOSUB3
1140 GOTO1480
1150 IFA<>8THEN1200
1160 M$=M$+"CHANCELLORSVILLE":GOSUB1
1170 IFA1=1THEN1480
1180 PRINT"MAY 1-6, 1863":M$="THE SOUTH HAD A COSTLY VICTORY AND LOST ONE "
1182 M$=M$+"OF THEIR OUTSTANDING GENERALS, 'STONEWALL' JACKSON.":GOSUB1:GOSUB3
1190 GOTO1480
1200 IFA<>9THEN1250
1210 M$=M$+"VICKSBURG":GOSUB1
1220 IFA1=1THEN1480
1230 PRINT"JULY 4, 1863":M$="VICKSBURG WAS A COSTLY DEFEAT FOR THE SOUTH " 
1232 M$=M$+"BECAUSE IT GAVE THE UNION ACCESS TO THE MISSISSIPPI.":GOSUB1:GOSUB3
1240 GOTO1480
1250 IFA<>10THEN1300 
1260 M$=M$+"GETTYSBURG":GOSUB1
1270 IFA1=1THEN1480
1280 PRINT"JUNE 30, 1863":M$="A SOUTHERN MISTAKE BY GEN. LEE AT GETTYSBURG" 
1282 M$=M$+"COST THEM ONE OF THE MOST CRUCIAL BATTLES OF THR WAR.":GOSUB1:GOSUB3
1290 GOTO1480
1300 IFA<>11THEN1350 
1310 M$=M$+"CHICKAMAUGA":GOSUB1
1320 IFA1=1THEN1480
1330 PRINT"NOV 25, 1863":M$="AFTER THE SOUTH HAD SIEGED GEN. ROSENCRANS'" 
1332 M$=M$+"ARMY FOR THREE MONTHS, GEN. GRANT BROKE THE SIEGE.":GOSUB1:GOSUB3
1340 GOTO1480
1350 IFA<>12THEN1400 
1360 M$=M$+"CHATTANOOGA":GOSUB1
1370 IFA1=1THEN1480
1380 PRINT"SEPT 15, 1863":M$="CONFUSION IN A FOREST NEAR CHICKAMAUGA LED" 
1382 M$=M$+"TO A COSTLY SOUTHERN VICTORY.":GOSUB1:GOSUB3
1390 GOTO1480
1400 IFA<>13THEN1450
1410 M$=M$+"SPOTSYLVANIA":GOSUB1
1420 IFA1=1THEN1480
1430 PRINT"MAY 5, 1864":M$="GRANT'S PLAN TO KEEP LEE ISOLATED BEGAN TO FAIL "
1432 M$=M$+"HERE, AND CONTINUED AT COLD HARBOR AND PETERSBURG.":GOSUB1:GOSUB3
1440 GOTO1480
1450 M$=M$+"ATLANTA":GOSUB1
1460 IFA1=1THEN1480
1470 PRINT"AUGUST, 1864":M$="SHERMAN AND THREE VETERAN ARMIES CONVERGED ON " 
1472 M$=M$+"ATLANTA AND DEALT THE DEATH BLOW TO THE CONFEDERACY.":GOSUB1:GOSUB3
1480 PRINT
1485 RETURN 
1487 PRINT"THE CONFEDERACY HAS SURRENDERED."
1488 GOTO1500
1490 PRINT"THE UNION HAS SURRENDERED."
1500 PRINT
1510 M$="YOU HAVE WON"+STR$(W)+" BATTLES AND LOST"+STR$(L)+" BATTLES.":GOSUB1
1515 IFY=5THEN1550 
1520 IFW<=LTHEN1550
1530 M$="THE CONFEDERACY HAS WON THE WAR!":GOSUB1:PRINT"и©ф":PRINT"©и©":PRINT"ф©и"
1531 INPUT"PLAY AGAIN (Y/N)";ZZ$
1532 IFZZ$="Y"THENRUN
1540 END
1550 M$="THE UNION HAS WON THE WAR.":GOSUB1:PRINT"╞╪╪":PRINT"ллл"
1551 INPUT"PLAY AGAIN (Y/N)";ZZ$
1552 IFZZ$="Y"THENRUN
1560 END
1570 REM PROGRAM BY L. CRAM, L.
1580 REM GOODIE, AND D. HIBBARD
1590 REM MC-10 EDITS JIM GERRIE
