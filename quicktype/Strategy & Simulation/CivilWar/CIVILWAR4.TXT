0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 PRINT:INPUT"PRESS <ENTER> TO CONTINUE";Z$:CLS:RETURN
10 PRINT:PRINT
20 PRINT"   ØøøøøØ            èØØØøøø"
21 PRINT"   øØøøØø            èØØØœœœ"
22 PRINT"   øøØØøø  CIVIL WAR èøøøøøø"
23 PRINT"   øØøøØøè            œœœœœœ"
24 PRINT"   ØøøøøØè            øøøøøø":PRINT
40 PRINTTAB(7)"CREATIVE COMPUTING":PRINTTAB(5)"MORRISTOWN, NEW JERSEY":PRINT:PRINT:PRINT
50 DIMS(4),C$(14),M1(14),M2(14),C1(14),C2(14),M(14)
60 REM UNION INFO ON LIKELY CONFEDERATE STRATEGY
70 S(1)=25:S(2)=25:S(3)=25:S(4)=25
82 REM READ HISTORICAL DATA.
84 FORD=1TO14
86 READC$(D),M1(D),M2(D),C1(D),C2(D),M(D)
88 NEXTD
89 D=RND(-(PEEK(9)*256+PEEK(10)))
90 PRINT
100 PRINT"DO YOU WANT INSTRUCTIONS";
110 INPUTX$
120 IFLEFT$(X$,1)="Y"THEN160
130 IFLEFT$(X$,1)="N"THENCLS:GOTO370
140 PRINT"YES OR NO -- ";
150 GOTO110
160 CLS
170 M$="THIS IS A CIVIL WAR SIMULATION.":GOSUB1
180 M$="TO PLAY TYPE A RESPONSE WHEN THE COMPUTER ASKS.":GOSUB1
190 M$="REMEMBER THAT ALL FACTORS ARE INTERRELATED AND THAT YOUR "
200 M$=M$+"RESPONSES COULD CHANGE HISTORY.":GOSUB1:M$="FACTS AND FIGURES USED ARE "
210 M$=M$+"BASED ON THE ACTUAL OCCURRENCE.":GOSUB1:M$="MOST BATTLES TEND TO RESULT "
220 M$=M$+"AS THEY DID IN THE CIVIL WAR, BUT IT ALL DEPENDS ON YOU!!":GOSUB1
230 GOSUB3
240 M$="THE OBJECT OF THE GAME IS TO WIN AS MANY BATTLES AS POSSIBLE.":GOSUB1
250 PRINT
260 M$="YOUR CHOICES FOR DEFENSIVE STRATEGY ARE:":GOSUB1
270 PRINT"(1) ARTILLERY ATTACK"
280 PRINT"(2) FORTIFY FOR FRONTAL ATTACK"
290 PRINT"(3) FORTIFY FLANKS"
300 PRINT"(4) FALL BACK":GOSUB3
310 M$="YOUR CHOICES FOR OFFENSIVE STRATEGY ARE:":GOSUB1
320 PRINT"(1) ARTILLERY ATTACK"
330 PRINT"(2) FRONTAL ATTACK"
340 PRINT"(3) FLANKING MANEUVERS"
350 PRINT"(4) ENCIRCLEMENT"
360 M$="YOU MAY SURRENDER BY TYPING A '5' FOR YOUR STRATEGY.":GOSUB1
370 PRINT:PRINT "ARE THERE TWO GENERALS PRESENT"
380 PRINT"(Y/N)";
390 INPUTB$
400 IFLEFT$(B$,1)="Y"THENCLS:GOTO430
410 IFLEFT$(B$,1)<>"N"THEN380
420 CLS:PRINT"YOU ARE THE CONFEDERACY.":PRINT"GOOD LUCK!"
425 PRINT
430 D=1
440 IFLEFT$(B$,1)<>"Y"THEN460
450 D=2
460 M$="SELECT A BATTLE BY TYPING A NUMBER FROM 1 TO 14 ON "
470 M$=M$+"REQUEST.":GOSUB1:M$="TYPE ANY OTHER NUMBER TO END THE SIMULATION.":GOSUB1
480 M$="BUT '0' BRINGS BACK EXACT PREVIOUS BATTLE SITUATION ALLOWING YOU TO REPLAY IT."
490 GOSUB1
500 GOSUB3
510 M$="NOTE: A NEGATIVE FOOD$ ENTRY CAUSES THE PROGRAM TO "
520 M$=M$+"USE THE ENTRIES FROM THE PREVIOUS BATTLE.":GOSUB1
530 PRINT
540 M$="AFTER REQUESTING A BATTLE, DO YOU WISH BATTLE DESCRIPTIONS?":GOSUB1
560 PRINT"(Y/N)";
570 INPUTX$
580 IFLEFT$(X$,1)="Y"THEN600
590 IFLEFT$(X$,1)<>"N"THEN560
600 L=0:W=0:R1=0:Q1=0:M3=0:M4=0:P1=0:P2=0:T1=0:T2=0
610 F(2)=0:H(2)=0:B(2)=0:R2=0:Q2=0:C6=0:F=0:W0=0:Y=0:Y2=0:U=0:U2=0
620 PRINT
630 PRINT"WHICH BATTLE DO YOU WISH TO     SIMULATE";
640 INPUTA
650 IFA<>0THEN660
655 IFR<>0THEN1140
660 IFA<=0THEN2860
665 IFA>=15THEN2860
670 C$=C$(A)
680 M1=M1(A)
690 M2=M2(A)
700 C1=C1(A)
710 C2=C2(A)
720 M=M(A)
960 U=0
970 REM INFLATION CALC
980 I1=10+(L-W)*2
990 I2=10+(W-L)*2
1000 REM - MONEY AVAILABLE
1010 D(1)=100*INT((M1*(100-I1)/2000)*(1+(R1-Q1)/(R1+1))+.5)
1020 D(2)=100*INT(M2*(100-I2)/2000+.5)
1030 IFLEFT$(B$,1)<>"Y"THEN1050
1040 D(2)=100*INT((M2*(100-I2)/2000)*(1+(R2-Q2)/(R2+1))+.5)
1050 REM - MEN   AVAILABLE
1060 M5=INT(M1*(1+(P1-T1)/(M3+1)))
1070 M6=INT(M2*(1+(P2-T2)/(M4+1)))
1080 F1=5*M1/6
1090 CLS
1100 M$="THIS IS THE BATTLE OF "+C$:GOSUB1
1110 IFLEFT$(X$,1)="N"THEN1150
1120 IFA>11THEN1130
1125 ONAGOTO3580,3620,3650,3690,3720,3750,3780,3800,3830,3860,3890
1130 ONA-11GOTO 3920,3950,3980
1140 M$=C$+" INSTANT REPLAY":GOSUB1
1150 PRINT
1160 PRINTTAB(10)" confederacy"TAB(22)" union"
1170 PRINT"MEN"TAB(10)M5;TAB(22)M6
1180 PRINT"MONEY"TAB(9)"$"D(1)TAB(21)"$"D(2)
1190 PRINT"INFLATION"TAB(10)STR$(I1+15)"%"TAB(22)STR$(I2)"%"
1195 PRINT
1200 REM - ONLY IN PRINTOUT IS CONFED INFLATION = I1+15%
1210 REM - IF TWO GENERALS, INPUT CONFED. FIRST
1220 FORI=1TOD
1230 IFLEFT$(B$,1)<>"Y"THEN1260
1240 IFI=2THEN1260
1250 PRINT"CONFEDERATE GENERAL-- HOW MUCH"
1260 PRINT"DO YOU WISH TO SPEND FOR"
1270 PRINT" - FOOD......";
1280 INPUTF
1290 IFF>=0THEN1360
1300 IFR1<>0THEN1330
1310 PRINT"NO PREVIOUS ENTRIES"
1320 GOTO1270
1330 M$="ASSUME YOU WANT TO KEEP SAME ALLOCATIONS":GOSUB1
1340 PRINT
1350 GOTO1510
1360 F(I)=F
1370 PRINT" - SALARIES..";
1380 INPUTH(I)
1390 N=1
1400 IFH(I)<0THEN1490
1410 PRINT" - AMMUNITION";
1420 INPUTB(I)
1430 N=2
1440 IFB(I)<0THEN1490
1450 PRINT
1460 IFF(I)+H(I)+B(I)<=D(I)THEN1510
1470 M$="THINK AGAIN! YOU HAVE ONLY $"+STR$(D(I)):GOSUB1
1480 GOTO1270
1490 PRINT"NEGATIVE VALUES NOT ALLOWED."
1500 ONNGOTO1370,1410
1510 IFLEFT$(B$,1)<>"Y"THEN1550
1520 IFI=2THEN1550
1530 PRINT"UNION GENERAL--  HOW MUCH"
1540 NEXTI
1550 FORZ=1TOD
1560 IFLEFT$(B$,1)<>"Y"THEN1620
1570 ONZGOTO1580,1600
1580 M$="CONFEDERATE "
1590 GOTO1620
1600 M$="      UNION "
1610 REM - FIND MORALE
1620 O=((2*F(Z)^2+H(Z)^2)/F1^2+1)
1630 IFO<10THEN1660
1640 M$=M$+"MORALE IS HIGH.":GOSUB1
1650 GOTO1700
1660 IFO<5THEN1690
1670 M$=M$+"MORALE IS FAIR.":GOSUB1
1680 GOTO1700
1690 M$=M$+"MORALE IS POOR.":GOSUB1
1700 IFLEFT$(B$,1)<>"Y"THEN1760
1710 O(Z)=O
1720 NEXTZ
1730 O2=O(2)
1740 O=O(1)
1750 M$="CONFEDERATE GENERAL-- "
1760 REM - ACTUAL OFF/DEF BATTLE SITUATION
1770 IFM<>3THEN1800
1780 M$=M$+"YOU ARE ON THE OFFENSIVE.":GOSUB1
1790 GOTO1840
1800 IFM<>1THEN1830
1810 M$=M$+"YOU ARE ON THE DEFENSIVE.":GOSUB1
1820 GOTO1840
1830 M$="BOTH SIDES ARE ON THE OFFENSIVE.":GOSUB1
1840 PRINT
1850 REM - CHOOSE STRATEGIES
1860 IFLEFT$(B$,1)<>"Y"THEN1910
1870 FORI=1TO2
1880 ONIGOTO1890,1920
1890 PRINT"CONFEDERATE STRATEGY";
1900 GOTO1920
1910 PRINT"YOUR STRATEGY";
1920 INPUTY
1930 IFABS(Y-3)<3THEN1960
1940 PRINT"STRATEGY";Y;"NOT ALLOWED."
1950 GOTO1910
1960 IFLEFT$(B$,1)="Y"THEN2000
1970 IFY=5THEN2830
1980 GOSUB3110
1990 GOTO2170
2000 IFI=2THEN2040
2010 Y1=Y
2020 PRINT"UNION STRATEGY ";
2030 NEXTI
2040 Y2=Y
2050 Y=Y1
2060 IFY2=5THEN2020
2070 REM : SIMULATED LOSSES-NORTH
2080 C6=(2*C2/5)*(1+1/(2*(ABS(Y2-Y)+1)))
2090 C6=C6*(1.28+(5*M2/6)/(B(2)+1))
2100 C6=INT(C6*(1+1/O2)+.5)
2110 REM - IF LOSS > MEN PRESENT, RESCALE LOSSES
2120 E2=100/O2
2130 IFINT(C6+E2)<M6THEN2190
2140 C6=INT(13*M6/20)
2150 E2=7*C6/13
2160 U2=1
2170 REM - CALCULATE SIMULATED LOSSES
2180 PRINT
2190 PRINTTAB(10)" confederacy"TAB(22)" union"
2200 C5=(2*C1/5)*(1+1/(2*(ABS(Y2-Y)+1)))
2210 C5=INT(C5*(1+1/O)*(1.28+F1/(B(1)+1))+.5)
2220 E=100/O
2230 IFC5+100/O<M1*(1+(P1-T1)/(M3+1))THEN2270
2240 C5=INT(13*M1/20*(1+(P1-T1)/(M3+1)))
2250 E=7*C5/13
2260 U=1
2270 IFD=1THEN2500
2280 PRINT"CASUALTIES"TAB(10);C5;TAB(22);C6
2290 PRINT"DESERTIONS"TAB(10);INT(E);TAB(22);INT(E2)
2300 PRINT
2310 IFLEFT$(B$,1)<>"Y"THEN2350
2320 M$="COMPARED TO THE ACTUAL CASUALTIES AT "+C$:GOSUB1
2330 M$="CONFEDERATE:"+STR$(INT(100*(C5/C1)+.5))+"% OF THE ORIGINAL":GOSUB1
2340 M$="UNION:      "+STR$(INT(100*(C6/C2)+.5))+"% OF THE ORIGINAL":GOSUB1
2350 PRINT
2360 REM - 1 WHO ONE
2370 IFU<>1THEN2380
2375 IFU2=1THEN2460
2380 IFU=1THEN2420
2390 IFU2=1THEN2440
2400 IFC5+E=C6+E2THEN2460
2410 IFC5+E<C6+E2THEN2440
2420 M$="THE UNION WINS "+C$:GOSUB1
2430 GOTO2600
2440 M$="THE CONFEDERACY WINS "+C$:GOSUB1
2450 GOTO2660
2460 M$="BATTLE OUTCOME UNRESOLVED":GOSUB1
2470 W0=W0+1
2480 IFA=0THEN2790
2490 GOTO2680
2500 C6=INT(17*C2*C1/(C5*20))
2510 E2=5*O
2520 GOTO2280
2530 M$="YOUR CASUALTIES WERE "+STR$(INT(100*(C5/C1)+.5))+"% OF THE ACTUAL CASUALTIES AT "+C$:GOSUB1
2550 PRINT
2560 REM - FIND WHO WON
2570 IFU=1THEN2590
2580 IFC5+E<17*C2*C1/(C5*20)+5*OTHEN2630
2590 M$="YOU LOSE "+C$:GOSUB1
2600 IFA=0THEN2790
2610 L=L+1
2620 GOTO2680
2630 M$="YOU WIN "+C$:GOSUB1
2640 REM - CUMULATIVE BATTLE FACTORS WHICH ALTER HISTORICAL
2650 REM  RESOURCES AVAILABLE.IF A REPLAY DON'T UPDATE.
2660 IFA=0THEN2790
2670 W=W+1
2680 T1=T1+C5+E
2690 T2=T2+C6+E2
2700 P1=P1+C1
2710 P2=P2+C2
2720 Q1=Q1+(F(1)+H(1)+B(1))
2730 Q2=Q2+(F(2)+H(2)+B(2))
2740 R1=R1+M1*(100-I1)/20
2750 R2=R2+M2*(100-I2)/20
2760 M3=M3+M1
2770 M4=M4+M2
2780 GOSUB3300
2790 U=0:U2=0
2800 PRINT "---------------"
2810 GOTO620
2820 REM------FINISH OFF
2830 M$="THE CONFEDERACY HAS SURRENDERED.":GOSUB1
2840 GOTO2860
2850 M$="THE UNION HAS SURRENDERED.":GOSUB1
2860 PRINT
2870 M$="THE CONFEDERACY "
2880 M$=M$+"HAS WON"+STR$(W)+" BATTLES AND LOST"+STR$(L)+".":GOSUB1
2890 IFY=5THEN2940
2900 IFY2=5THEN2920
2910 IFW<=LTHEN2940
2915 IFY=5THEN2940
2920 M$="THE CONFEDERACY HAS WON THE WAR.":GOSUB1
2930 GOTO2950
2940 M$="THE UNION HAS WON THE WAR.":GOSUB1
2950 GOSUB3
2960 IFR1=0THEN3100
2970 M$="FOR THE"+STR$(W+L+W0)+" BATTLES FOUGHT (EXCUDING RERUNS)":GOSUB1
2980 PRINT
2990 PRINT"LOSSES"TAB(10)" confederacy"TAB(22)" union"
3000 PRINT"HISTORICAL"TAB(10)INT(P1+.5)TAB(22)INT(P2+.5)
3010 PRINT"SIMULATED"TAB(10)INT(T1+.5)TAB(22)INT(T2+.5)
3020 PRINT
3030 PRINT"% OF ORIG."INT(100*(T1/P1)+.5)TAB(22)INT(100*(T2/P2)+.5)
3040 IFLEFT$(B$,1)="Y"THEN3100
3050 PRINT
3060 M$="UNION INTELLIGENCE SUGGESTS THAT THE SOUTH USED "
3070 M$=M$+"STRATEGIES  1, 2, 3, 4 IN THE FOLLOWING PERCENTAGES:":GOSUB1
3080 PRINTS(1);S2(2);S(3);S(4)
3090 REM---------------------------------
3100 END
3110 REM - UNION STRATEGY IS COMPUTER CHOSEN
3120 PRINT"UNION STRATEGY IS";
3130 IFA<>0THEN3180
3140 INPUTY2
3150 IFY2<=0THEN3160
3155 IFY2<5THEN3290
3160 M$="ENTER 1 , 2 ,3 , OR 4 (USUALLY PREVIOUS UNION STRATEGY)":GOSUB1
3170 GOTO3140
3180 S0=0
3190 R=100*RND(0)
3200 FORI=1TO4
3210 S0=S0+S(I)
3220 REM - IF ACTUAL STRATEGY INFO IS IN PROGRAM DATA STATEMENTS
3230 REM   THEN R-100 IS EXTRA WEIGHT GIVEN TO THAT STATEGY.
3240 IFR<S0THEN3270
3250 NEXTI
3260 REM - IF ACTUAL STRAT. IN,THEN HERE IS Y2= HIST. STRAT.
3270 Y2=I
3280 PRINT Y2
3290 RETURN
3300 REM LEARN  PRESENT STRATEGY, START FORGETTING OLD ONES
3310 REM - PRESENT STRATEGY OF SOUTH GAINS 3*S, OTHERS LOSE S
3320 REM   PROBABILITY POINTS, UNLESS A STRATEGY FALLS BELOW 5%.
3330 S=3
3340 S0=0
3350 FORI=1TO4
3360 IFS(I)<=5THEN3390
3370 S(I)=S(I)-S
3380 S0=S0+S
3390 NEXTI
3400 S(Y)=S(Y)+S0
3410 RETURN
3420 REM - HISTORICAL DATA...CAN ADD MORE (STRAT.,ETC) BY INSERTING
3430 REM   DATA STATEMENTS AFTER APPRO. INFO, AND ADJUSTING READ
3440 DATA"BULL RUN",18000,18500,1967,2708,1
3450 DATA"SHILOH",40000.,44894.,10699,13047,3
3460 DATA"SEVEN DAYS",95000.,115000.,20614,15849,3
3470 DATA"SECOND BULL RUN",54000.,63000.,10000,14000,2
3480 DATA"ANTIETAM",40000.,50000.,10000,12000,3
3490 DATA"FREDERICKSBURG",75000.,120000.,5377,12653,1
3500 DATA"MURFREESBORO",38000.,45000.,11000,12000,1
3510 DATA"CHANCELLORSVILLE",32000,90000.,13000,17197,2
3520 DATA"VICKSBURG",50000.,70000.,12000,19000,1
3530 DATA"GETTYSBURG",72500.,85000.,20000,23000,3
3540 DATA"CHICKAMAUGA",66000.,60000.,18000,16000,2
3550 DATA"CHATTANOOGA",37000.,60000.,36700.,5800,2
3560 DATA"SPOTSYLVANIA",62000.,110000.,17723,18000,2
3570 DATA"ATLANTA",65000.,100000.,8500,3700,1
3580 M$="JULY 21, 1861.  GEN. BEAUREGARD, COMMANDING THE SOUTH, MET "
3590 M$=M$+"UNION FORCES WITH GEN. MCDOWELL IN A PREMATURE BATTLE AT "
3600 M$="BULL RUN.":GOSUB1:M$="GEN. JACKSON HELPED PUSH BACK THE UNION ATTACK.":GOSUB1
3610 GOTO1150
3620 M$="APRIL 6-7, 1862. THE CONFEDERATE SURPRISE ATTACK AT "
3630 M$=M$+"SHILOH FAILED DUE TO POOR ORGANIZATION.":GOSUB1
3640 GOTO1150
3650 M$="JUNE 25-JULY 1, 1862. GENERAL LEE (CSA) UPHELD THE "
3660 M$=M$+"OFFENSIVE THROUGHOUT THE BATTLE AND FORCED GEN. MCCLELLAN"
3670 M$=M$+"AND THE UNION FORCES AWAY FROM RICHMOND.":GOSUB1
3680 GOTO1150
3690 M$="AUG 29-30, 1862. THE COMBINED CONFEDERATE FORCES UNDER "
3695 M$=M$+"LEE AND JACKSON DROVE THE UNION FORCES BACK INTO WASHINGTON.":GOSUB1
3710 GOTO1150
3720 M$="SEPT 17, 1862. THE SOUTH FAILED TO INCORPORATE MARYLAND "
3730 M$=M$+"INTO THE CONFEDERACY.":GOSUB1
3740 GOTO1150
3750 M$="DEC 13, 1862. THE CONFEDERACY UNDER LEE SUCCESSFULLY "
3760 M$=M$+"REPULSED AN ATTACK BY THE UNION UNDER GEN. BURNSIDE.":GOSUB1
3770 GOTO1150
3780 M$="DEC 31, 1862. THE SOUTH UNDER GEN. BRAGG WON A CLOSE BATTLE.":GOSUB1
3790 GOTO1150
3800 M$="MAY 1-6, 1863. THE SOUTH HAD A COSTLY VICTORY AND LOST "
3810 M$=M$+"ONE OF THEIR OUTSTANDING GENERALS, 'STONEWALL' JACKSON.":GOSUB1
3820 GOTO1150
3830 M$="JULY 4, 1863. VICKSBURG WAS A COSTLY DEFEAT FOR THE SOUTH "
3840 M$=M$+"BECAUSE IT GAVE THE UNION ACCESS TO THE MISSISSIPPI.":GOSUB1
3850 GOTO1150
3860 M$="JULY 1-3, 1863. A SOUTHERN MISTAKE BY GEN. LEE AT GETTYSBURG "
3870 M$=M$+"COST THEM ONE OF THE MOST CRUCIAL BATTLES OF THE WAR.":GOSUB1
3880 GOTO1150
3890 M$="SEPT. 15, 1863. CONFUSION IN A FOREST NEAR CHICKAMAUGA LED "
3900 M$=M$+"TO A COSTLY SOUTHERN VICTORY.":GOSUB1
3910 GOTO1150
3920 M$=M$+"NOV. 25, 1863. AFTER THE SOUTH HAD SIEGED GEN. ROSENCRANS' "
3930 M$=M$+"ARMY FOR THREE MONTHS, GEN. GRANT BROKE THE SIEGE.":GOSUB1
3940 GOTO1150
3950 M$="MAY 5, 1864. GRANT'S PLAN TO KEEP LEE ISOLATED BEGAN TO "
3960 M$=M$+"FAIL HERE, AND CONTINUED AT COLD HARBOR AND PETERSBURG.":GOSUB1
3970 GOTO1150
3980 M$="AUGUST, 1864. SHERMAN AND THREE VETERAN ARMIES CONVERGED "
3990 M$=M$+"ON ATLANTA AND DEALT THE DEATH BLOW TO THE CONFEDERACY.":GOSUB1
4000 GOTO1150
4010 END
4020 REM ORIGINAL GAME DESIGN: CRAM, GOODIE, HIBBARD LEXINGTON H.S.
4030 REM MODIFICATIONS: G. PAUL, R. HESS (TIES), 1973
