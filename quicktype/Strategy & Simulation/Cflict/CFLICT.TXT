10 REM *** CONFLICT ***
20 REM BY MIKE SHANTO & STEVE LOY
25 REM COPYRIGHT (C) 1981
40 CLS
45 PRINT
50 PRINT:PRINT:PRINTTAB(8)"C O N F L I C T"
52 PRINT:PRINTTAB(3)"(C) 1981 M.SHANTO & S.LOY"
53 PRINT:PRINT
55 PRINT:PRINTTAB(9)"DISTRIBUTED BY"
57 PRINT:PRINTTAB(3)"SOFT SECTOR MARKETING INC."
60 PRINT:PRINT"   MC-10 EDITS BY JIM GERRIE"
70 FORI=1TO1500:NEXTI
80 CLEAR 300
90 DIM CO$(21),CO(21,6),E(12),M$(17)
95 CL$="                                "
100 B=0:GOSUB2140
110 CLS:PRINT@64,"ENTER YOUR NAME (7 CHAR. MAX.)"
115 PRINT
120 INPUT"PLAYER ONE ";A$
130 IFLEN(A$)>7THENPRINTM$(1):GOTO160
140 IFA$=""THENPRINTM$(2):GOTO160
150 GOTO170
160 A$="":FORI=1TO1500:NEXTI:GOTO110
170 PRINT:INPUT"PLAYER TWO ";B$
180 IFLEN(B$)>7THENPRINTM$(1):GOTO210
190 IFB$=""THENPRINTM$(2):GOTO210
200 CLS:GOTO220
210 B$="":FORI=1TO1500:NEXTI:GOTO170
220 TE$(0)="BESTAC":TE$(1)="GRANDEL":TE$(2)="RANKOR":TE$(3)="SENTI"
230 DATA SIRIUS,VEGA,GORLO,LANDELL,RIGEL
240 DATA STERT,CASSON,AMPHOR,RADON,POLLUX
250 DATA TELVY,SENTOR,SATION,GOTELVY,ANTERES
260 DATA VERDEK,BOOLAC,CAPELLA,JITONE,SPICA
265 PRINT:PRINT:PRINT
270 PRINT"ONE MOMENT-DISTRIBUTING COLONIES"
280 FORK=1TO20:READCO$(K):NEXTK
290 GOSUB310:CO(K,4)=1:CO(K,5)=5
300 GOSUB310:CO(K,4)=2:CO(K,5)=5
305 B=B+1:IF B>7 THEN 320
306 GOTO290
310 K=RND(20):IF CO(K,4)=0THEN RETURN
311 GOTO310
320 FORK=1TO19:CO(K,1)=K+1:NEXTK
330 FORK=2TO19:CO(K,2)=K-1:NEXTK
340 CO(1,2)=3:CO(1,3)=19:CO(2,3)=4:CO(3,3)=1:CO(4,3)=2
350 CO(8,3)=10:CO(10,3)=8:CO(11,3)=13:CO(13,3)=11:CO(15,3)=17
360 CO(17,3)=15:CO(16,3)=18:CO(18,3)=16:CO(19,3)=1:CO(20,1)=19
370 J=2:CLS:GOSUB1640:GOSUB1650:GOSUB1450:GOTO430
380 CLS:GOSUB1640:GOSUB1650
390 E(J+4)=INT(E(J)/3+E(J+2)*3)
400 PRINT@416,"YOU GET";E(J+4);"REINFORCEMENTS";
410 PRINT@448,"DISTRIBUTE WHEN READY ";P$;
420 FORI=1TO1500:NEXTI:RETURN
430 CLS:GOSUB1640
440 GOSUB1650:GOSUB3000
441 PRINT@416,"D-DISTRIBUTE  A-ATTACK  M-MOVE";
442 PRINT@448,"S-STATUS  F-FINISH  R-RESIGN";
443 PRINT@480,"YOUR COMMAND ";P$;" ?";
444 D$=""
450 D$=INKEY$:IFD$=""THEN450
460 IFD$="D"THEN530
470 IFD$="A"THEN710
480 IFD$="M"THEN1080
490 IFD$="S"THEN1300
500 IFD$="F"THEN1450
510 IFD$="R"THEN2400
520 GOTO444
530 IFE(J+4)>0THEN580
540 PRINT@480,M$(16);
560 FORI=1TO1500:NEXTI
570 GOTO430
580 GOSUB3000:PRINT@416,"REINFORCE COLONY # ";
590 INPUT K:IFK<1 OR K>20THEN650
600 IFCO(K,4)<>J THEN650
610 PRINT@448,"HOW MANY ARMIES TO ";CO$(K);
620 INPUT TA:IFTA>E(J+4)THEN660
630 E(J+4)=E(J+4)-TA:CO(K,5)=CO(K,5)+TA
640 GOTO440
650 GOSUB1290:PRINT@416,M$(6);:PRINT@448,M$(13);:GOTO670
660 GOSUB1290:PRINT@416,M$(11);:PRINT@448,M$(13);
670 S$=INKEY$:IFS$=""THEN670
680 IFS$="Y"THEN580
690 IFS$="N"THEN440
700 GOTO670
710 E(J+4)=0:GOSUB3000
711 PRINT@416,"ATTACKING FROM COLONY # ";
720 INPUTC:IF C<1 OR C>20THEN820
721 IF CO(C,4)<>J THEN 820
730 IFCO(C,5)<2 THEN810
740 PRINT@448,"AGAINST COLONY # ";:INPUTD
750 FORK=1TO3
760 IFCO(C,K)=D THEN780
770 NEXTK:GOSUB1290:PRINT@416,M$(13):GOTO830
780 IFD<1 OR D>20ORCO(D,4)=0ORCO(D,4)=J THEN820
790 CLS:GOTO870
800 GOSUB1290:PRINT@416,M$(8):PRINTM$(13):GOTO830
810 GOSUB1290:PRINT@416,M$(17):PRINTM$(13):GOTO830
820 GOSUB1290:PRINT@416,M$(10):PRINTM$(13)
830 S$=INKEY$:IFS$=""THEN830
840 IFS$="Y"THEN710
850 IFS$="N"THEN440
860 GOTO830
870 E(10+J)=E(10+J)+1:AA=1
871 G=CO(C,5):H=CO(D,5)
872 GS=G:HS=H:GOSUB2950
873 GOSUB1680
880 IFG=GS-1THEN980
890 PRINT@256,"YOU HAVE BEAT ONE ";CO$(D);" ARMY"
900 CO(D,5)=CO(D,5)-1
910 IFCO(D,5)=0THENPRINT"YOU NOW CONTROL THE COLONY":GOTO930
920 GOTO1010
930 PRINT"HOW MANY ARMIES TO OCCUPY ?";
940 INPUTTA:IFTA=>CO(C,5)THEN PRINTM$(12):GOTO930
950 IFTA<1THENPRINT"YOU MUST OCCUPY WITH AT LEAST":PRINT"ONE ARMY":GOTO930
960 CO(C,5)=CO(C,5)-TA:CO(D,5)=CO(D,5)+TA:CO(D,4)=J
970 GOSUB1500:IFGW<>0THEN2400
971 GOTO430
980 PRINT@256,"YOU HAVE LOST 1 ARMY ";P$
990 CO(C,5)=CO(C,5)-1:FORI=1TO1000:NEXTI
1000 IFCO(C,5)=1THENPRINT"YOU ARE DOWN TO ONE ARMY":PRINT"YOU CAN'T CONTINUE BATTLE":FORI=1TO1000:NEXTI:GOTO430
1010 IFJ=1THENPRINTA$;", YOU HAVE";CO(C,5);" ARMIES LEFT":PRINTB$;" HAS";CO(D,5);" ARMIES LEFT"
1020 IFJ=2THENPRINTB$;", YOU HAVE";CO(C,5);" ARMIES LEFT":PRINTA$;" HAS";CO(D,5);" ARMIES LEFT"
1030 PRINT"CONTINUE BATTLE ";P$;" Y/N ?";
1040 Y$=INKEY$:IFY$=""THEN1040
1050 IFY$="Y"THEN870
1060 IFY$="N"THEN430
1070 GOTO1040
1080 IFAA>0THEN1150
1090 E(J+4)=0:GOSUB3000
1091 PRINT@416,"FROM COLONY # ?";
1100 INPUT MF:IFMF<1 ORMF>20THEN1160
1101 IFCO(MF,4)<>J THEN1160
1110 PRINT@448,"TO COLONY # ?";
1116 INPUT MT
1120 FORK=1TO3
1130 IFCO(MF,K)=MT THEN1170
1140 NEXTK:GOSUB1290:PRINT@416,M$(14):PRINTM$(13):GOTO1250
1150 GOSUB3000:PRINT@416,M$(3):FORI=1TO2000:NEXTI:GOTO440
1160 GOSUB1290:PRINT@416,M$(7):PRINTM$(13):GOTO1250
1170 IFMT<1ORMT>20THEN1190
1180 IFCO(MT,4)=0ORCO(MT,4)=J THEN1200
1190 GOSUB1290:PRINT@416,M$(9):PRINTM$(13):GOTO1250
1200 GOSUB3000
1205 PRINT@416,"HOW MANY ARMIES ";:INPUT MA
1210 IFCO(MF,5)>MA THEN1230
1220 GOSUB1290:PRINT@480,M$(12);:GOTO1205
1230 CO(MF,5)=CO(MF,5)-MA:CO(MT,5)=CO(MT,5)+MA:CO(MT,4)=J
1240 GOSUB1650:E(8+J)=E(8+J)+1:GOTO1450
1250 S$=INKEY$:IFS$=""THEN1250
1260 IFS$="Y"THEN1080
1270 IFS$="N"THEN440
1280 GOTO1250
1290 GOSUB3000:FORI=1TO5
1291 PRINT@416,"INVALID MOVE";:FORTI=1TO50:NEXTTI
1292 PRINT@416,CL$;:FORTI=1TO50:NEXTTI
1293 NEXTI
1294 RETURN
1300 W=0:Q=1:Z=5:GOSUB1340
1310 W=1:Q=5:Z=10:GOSUB1340
1320 W=2:Q=11:Z=14:GOSUB1340
1330 W=3:Q=15:Z=20:GOSUB1340
1335 GOTO430
1340 CLS:GOSUB1440
1350 FORK=Q TOZ:K$=RIGHT$(STR$(K),2)
1351 PRINTK$" ";CO$(K);
1355 PRINTTAB(11);
1360 IFCO(K,4)=1THENPRINTA$;:GOTO1380
1365 IFCO(K,4)=2THENPRINTB$;:GOTO1380
1370 PRINT"EMPTY";
1380 PRINTTAB(19);
1381 PRINTRIGHT$(STR$(CO(K,5)),2);" ";
1382 PRINTSTR$(CO(K,1));
1383 PRINTSTR$(CO(K,2));
1384 PRINTSTR$(CO(K,3))
1385 NEXTK
1390 PRINT:PRINT"'C' TO CONTINUE - 'Q' TO QUIT";
1400 S$=INKEY$:IFS$=""THEN1400
1410 IFS$="C"THENRETURN
1420 IFS$="Q"THEN430
1430 GOTO1400
1440 PRINT"TERRITORY ";TE$(W)
1441 PRINT:PRINT"COLONIES";TAB(11)"CONTROL";TAB(20);"#  LINKED TO";
1442 PRINT"--------------------------------";
1443 RETURN
1450 GOSUB3000
1460 AA=0:GOSUB1500:IFGW<>0THEN2400
1470 F=F+1:IF F=15THEN2190
1480 IFJ=2THEN1490
1481 J=2:P$=B$:GOSUB390:GOTO440
1490 J=1:P$=A$:GOSUB390:GOTO440
1500 P=J:J=1:GOSUB1520:J=2:GOSUB1520:J=P
1510 RETURN
1520 E(J+2)=0:M=1:N=4:JT=1:GOSUB1570
1530 M=5:N=10:JT=2:GOSUB1570
1540 M=11:N=14:JT=3:GOSUB1570
1550 M=15:N=20:JT=4:GOSUB1570:GOSUB1620
1560 RETURN
1570 T(0)=0:FORK=M TO N:IFCO(K,4)=J THEN1580
1571 GOTO1590
1580 T(0)=T(0)+1
1590 NEXTK:IF T(0)=N-M+1 THENE(J+2)=E(J+2)+1:T(JT)=J
1600 FORK=1TO20:IF CO(K,4)=J ORCO(K,4)=0 THEN NEXT K:GW=J
1610 RETURN
1620 E(J)=0:FORK=1TO20:IF CO(K,4)=J THENE(J)=E(J)+1
1630 NEXTK:RETURN
1640 FORK=1TO20
1641 PRINTRIGHT$(STR$(K),2);" ";CO$(K),
1642 NEXTK
1643 RETURN
1650 PRINT@320,"";
1651 PRINTA$;TAB(10);"COLON.";E(1);TAB(19);"TERRIT.";E(3)
1652 PRINTB$;TAB(10)"COLON.";E(2);TAB(19)"TERRIT.";E(4)
1653 PRINT"================================";
1654 RETURN
1680 BL$=CHR$(128)
1690 FORZ=0TO96STEP32:FORZZ=0TO6
1691 PRINT@69+Z+ZZ,BL$;
1692 PRINT@85+Z+ZZ,BL$;
1693 NEXT ZZ,Z
1740 IFJ=1THENPRINT@37,A$;:PRINT@53,B$;
1750 IFJ=2THENPRINT@37,B$;:PRINT@53,A$;
1800 DH$=CHR$(147):DL$=CHR$(156)
1801 ER$=BL$+BL$+BL$+BL$+BL$+BL$+BL$
1810 FORI=1TO15:PD=RND(6):CD=RND(6)
1815 GOSUB3100
1820 ONPD GOSUB 2010,2020,2030,2040,2050,2060
1830 ONCD GOSUB 2070,2080,2090,2100,2110,2120
1840 FORTI=1TO10:NEXT TI,I
1850 IFPD>CD THEN H=H-1:GOTO1860
1851 G=G-1
1860 PRINT@198,"";
1861 ONPD GOTO 1870,1880,1890,1900,1910,1920
1870 PRINT"ONE":GOTO1930
1880 PRINT"TWO":GOTO1930
1890 PRINT"THREE":GOTO1930
1900 PRINT"FOUR":GOTO1930
1910 PRINT"FIVE":GOTO1930
1920 PRINT"SIX"
1930 PRINT@214,"";
1931 ONCD GOTO 1940,1950,1960,1970,1980,1990
1940 PRINT"ONE":GOTO2000
1950 PRINT"TWO":GOTO2000
1960 PRINT"THREE":GOTO2000
1970 PRINT"FOUR":GOTO2000
1980 PRINT"FIVE":GOTO2000
1990 PRINT"SIX"
2000 GOTO2130
2010 PRINT@104,DH$;:RETURN
2020 PRINT@104,DL$;:PRINT@136,DH$;:RETURN
2030 PRINT@102,DL$;:PRINT@136,DL$;:PRINT@170,DL$;:RETURN
2040 PRINT@102,DL$;:PRINT@106,DL$;:PRINT@134,DH$;:PRINT@138,DH$;:RETURN
2050 GOSUB2040:GOTO2010
2060 PRINT@102,DL$;:PRINT@106,DL$;
2061 PRINT@134,DL$;:PRINT@138,DL$;
2062 PRINT@166,DL$;:PRINT@170,DL$;
2063 RETURN
2070 PRINT@120,DH$;:RETURN
2080 PRINT@120,DL$;:PRINT@152,DH$;:RETURN
2090 PRINT@118,DL$;:PRINT@152,DL$;:PRINT@186,DL$;:RETURN
2100 PRINT@118,DL$;:PRINT@122,DL$;:PRINT@150,DH$;:PRINT@154,DH$;:RETURN
2110 GOSUB2100:GOTO2070
2120 PRINT@118,DL$;:PRINT@122,DL$;
2121 PRINT@150,DL$;:PRINT@154,DL$;
2122 PRINT@182,DL$;:PRINT@186,DL$;
2123 RETURN
2130 FORI=1TO1000:NEXTI:RETURN
2139 PRINT@416,"";:STOP
2140 M$(1)="A SHORTER NAME PLEASE"
2141 M$(2)="DON'T BE SHY...ENTER YOUR NAME"
2142 M$(3)="NO MOVES ALLOWED AFTER AN ATTACK"
2143 M$(4)=" WINS THE GAME":M$(5)="THE GAME IS TIED"
2144 M$(6)="YOU DO NOT CONTROL THAT COLONY"
2145 M$(7)="YOU HAVE NO ARMIES THERE"
2146 M$(8)=M$(7)
2147 M$(9)="YOUR ENEMY CONTROLS THAT COLONY"
2148 M$(10)="YOU CAN'T ATTACK YOURSELF"
2149 M$(11)="YOU DON'T HAVE THAT MANY"
2150 M$(12)="YOU HAVE TO LEAVE ONE ARMY THERE"
2151 M$(13)="CONTINUE THIS COMMAND ? Y/N "
2152 M$(14)="THE TWO COLONIES DO NOT CONNECT"
2153 M$(15)="THE PLANET IS YOURS !!!"
2154 M$(16)="YOU HAVE NONE TO DISTRIBUTE"
2155 M$(17)="YOU CAN'T ATTACK WITH 1 ARMY"
2180 RETURN
2190 F=0:R=RND(10):IFR<8THEN1450
2200 K=RND(20):IFCO(K,4)=0THEN2200
2205 CLS
2210 IFR=8THEN2240
2220 IFR=9THEN2280
2230 GOTO2340
2240 PRINT:PRINT"DUE TO CHANGES IN ATMOSPHERIC"
2241 PRINT"CONDITIONS AN ION STORM HAS BEEN";
2250 PRINT"GENERATED, LEVELING ";CO$(K)
2255 PRINT"AND DESTROYING ALL LIFE ON THE"
2260 PRINT"COLONY, INCLUDING THE ARMIES"
2270 CO(K,4)=0:CO(K,5)=0:GOTO2380
2280 PRINT:PRINT"BECAUSE OF THE STRESS AND STRAIN";
2285 PRINT"OF BATTLE THE FORCES ON COLONY"
2290 PRINTCO$(K);" HAVE DEFECTED, LEAVING"
2310 IFCO(K,4)=1THENCO(K,4)=2:GOTO2320
2311 CO(K,4)=1
2320 IFCO(K,4)=1THENPRINTA$;:GOTO2330
2321 PRINTB$;
2330 PRINT" IN CONTROL OF"
2335 PRINT"THE COLONY ":GOTO2380
2340 PRINT:PRINT"MICRORGANISMS THAT HAD DESTROYED";
2345 PRINT"THE PREVIOUS INHABITANTS HAVE"
2350 PRINT"RE-INFESTED ";CO$(K);" KILLING"
2355 PRINT"ALL OCCUPYING ARMIES"
2360 CO(K,4)=0:CO(K,5)=0
2380 PRINT:PRINT"HIT ANY KEY TO CONTINUE"
2390 S$=INKEY$:IFS$=""THEN2390
2391 CLS:GOSUB1640:GOSUB1650
2392 GOTO1450
2400 FORK=1TO20
2410 IFCO(K,4)=1THENE(7)=E(7)+CO(K,5):GOTO2430
2420 IFCO(K,4)=2THENE(8)=E(8)+CO(K,5)
2430 NEXTK
2440 CLS:IFGW=0THEN2500
2450 IFGW=1THENPRINT:PRINTA$:PRINTM$(15):GOTO2470
2460 PRINT:PRINTB$:PRINTM$(15)
2470 PRINT"CONGRATULATIONS...YOU HAVE"
2471 PRINT"ACQUIRED ALL THE MINERAL WEALTH"
2480 PRINT"FOR YOUR CIVILIZATION"
2481 PRINT"LIVE LONG AND PROSPER."
2490 GOTO2670
2500 PRINT:PRINTTAB(13);A$;TAB(25);B$
2510 PRINT:PRINT"TERRITORIES";TAB(15);E(3);TAB(27);E(4)
2520 PRINT"COLONIES";TAB(15);E(1);TAB(27);E(2)
2530 PRINT"ARMIES";TAB(15);E(7);TAB(27);E(8)
2540 PRINT"MOVES";TAB(15);E(9);TAB(27);E(10)
2550 PRINT"ATTACKS";TAB(15);E(11);TAB(27);E(12)
2560 PRINT"********************************";
2570 PRINT"THE COMPUTER IS NOW CALCULATING"
2575 PRINT"YOUR SCORES."
2580 S(1)=(E(1)*5)+(E(3)*10)+E(7)-E(9)-E(11)
2590 S(2)=(E(2)*5)+(E(4)*10)+E(8)-E(10)-E(12)
2600 FORI=1TO3000:NEXTI
2605 GOSUB2960
2610 PRINT@320,A$;" HAS A RATING OF";S(1)
2620 PRINT@352,B$;" HAS A RATING OF";S(2)
2630 IFS(1)>S(2)THENPRINTA$;M$(4)
2640 IFS(2)>S(1)THENPRINTB$;M$(4)
2650 IFS(1)=S(2)THENPRINTM$(5)
2670 PRINT:PRINT"DO YOU WANT TO PLAY AGAIN Y/N";
2680 INPUT S$
2690 IFS$=""ORS$="Y"THENRUN
2700 IFS$<>"N"THEN2670
2720 CLS
2725 PRINT:PRINT"   HOPE YOU ENJOYED THE GAME"
2730 PRINT:PRINT"PLEASE ADDRESS ANY QUESTIONS OR"
2740 PRINT"COMMENTS TO :"
2750 PRINT:PRINTTAB(8);"C O N F L I C T"
2760 PRINT:PRINTTAB(9);"P.O. BOX 5340"
2770 PRINTTAB(9)"WILLOWICK, OH."
2780 PRINTTAB(18)"44094"
2790 PRINT:PRINT:END
2950 PRINT@256,CL$;
2960 PRINT@288,CL$;
2970 PRINT@320,CL$;
2980 PRINT@352,CL$;
2990 PRINT@384,CL$;
3000 PRINT@416,CL$;
3010 PRINT@448,CL$;
3020 PRINT@480,LEFT$(CL$,31);
3040 RETURN
3100 FORZ=69TO165STEP32
3110 PRINT@Z,ER$;
3115 PRINT@Z+16,ER$;
3120 NEXTZ
3130 RETURN
