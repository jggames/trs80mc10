0 CLS:X=32768:GOSUB3000:REM INSTALL COUNTER
1 REM ***SHEEPDOG TRIAL***
2 DIMS(6),HS(6):FORN=1TO6:HS(N)=32000:NEXT
5 CLS:W=16384:Q=203:H=247:YS=0:XS=0:YD=0:XD=0:WS=0:WD=0:D1=0:D=0:X=0:S=0:Z=0:P=0:NS=0:FORN=1TO6:S(N)=0:NEXT
9 REM ***RULES***
10 PRINT@9,"sheepdog trial";:PRINT@33,"BY GUY MORGAN OF MID-GLAMORGAN"
20 PRINT@3*32,"YOU MAY CHOOSE HOW MANY SHEEP TOROUND UP. NO MORE THAN 6. IF"
21 PRINT"YOU GET TOO CLOSE TO A SHEEP IT","WILL PANIC AND IF YOU ARE TOO","FAR AWAY SHEEP WILL WANDER AT","RANDOM."
25 PRINT"MOVE DOG WITH THE aswz KEYS.":PRINT"THE DOG IS ALLOWED 3 MOVES":PRINT"BETWEEN SHEEP MOVES."
26 PRINT"TO USE A TURN WITHOUT MOVING","THE DOG PRESS ANY OTHER KEY."
30 PRINT@485,"press any key to play";
40 IFINKEY$=""THEN40
50 CLS
90 GOTO300
100 REM ***CONV. TO CO-ORDS***
110 YS=INT(Z/32)+1
120 XS=(Z-(YS-1)*32)
130 RETURN
140 REM ***DOG CO-ORDS***
150 YD=INT(D/32)+1
160 XD=(D-(YD-1)*32)
170 RETURN
180 REM ***RANDOM MOVE***
190 Q=203:X=1-INT(RND(0)*3):XS=XS+X:IFX<0THENQ=199
200 YS=YS+1-INT(RND(0)*3)
210 GOTO865
220 REM ***CONV. TO POKE NO.***
230 WS=XS+32*(YS-1)
240 RETURN
300 REM ***NO. OF SHEEP***
310 PRINT "HOW MANY SHEEP DO YOU WANT","TO ROUND UP?"
315 INPUTNS
320 IFNS>0ANDNS<7THEN340
325 PRINT,,"SORRY, MORE THAN 0 AND LESS","THAN 7"
330 GOTO315
340 PRINT,,MID$(STR$(NS),2);" IS OKAY."
345 FORN=1TO1000
346 NEXTN
350 CLS
355 REM ***INITIALIZE***
410 REM ***PEN***
420 PRINT@9*32+13,"ƒ ƒ‚";
430 PRINT@10*32+13,"…   Š";
440 PRINT@11*32+13,"…   Š";
450 PRINT@12*32+13,"„ŒŒŒˆ";:PRINT@0,;
460 REM ***FENCE*** 
470 FORN=0TO31
480 POKEW+N,103:POKE15*32+W+N,103
490 NEXT
500 FORN=0TO15
510 POKE32*N+W,103:POKE32*N+W+31,103
520 NEXT
530 REM ***GENERATE SHEEP***
540 FORN=1TONS
550 WS=RND(480)
560 IFPEEK(W+WS)<>96THEN550
570 POKEW+WS,Q
580 S(N)=WS
590 NEXT
600 REM ***PLACE DOG***
610 WD=RND(480)
620 IFPEEK(WD+W)<>96THEN610 
630 POKEWD+W,H
640 D=WD
700 REM ***START***
707 S=0
708 REM INIT TIMER
709 X=USR(0)
710 FORN=1TO3
715 D1=D
720 M$=INKEY$:PRINT@14,MID$(STR$(INT(USR(1)/60)),2);:IFM$=""THEN720
730 IFM$="S"THEND=D+1:H=251
740 IFM$="A"THEND=D-1:H=247
750 IFM$="W"THEND=D-32
760 IFM$="Z"THEND=D+32
770 IFPEEK(W+D)<>96THEND=D1
780 POKEW+D1,96
790 POKEW+D,H
800 NEXT
810 REM ***MOVE SHEEP***
815 GOSUB140
820 FORN=1TONS
830 Z=INT(S(N))
835 GOSUB100
840 IFABS(XS-XD)>3ORABS(YS-YD)>3THEN180
843 IFABS(XS-XD)<2ANDABS(YS-YD)<2THEN180
845 IFXS>XDTHENXS=XS+1:Q=203
850 IFXS<XDTHENXS=XS-1:Q=199
855 IFYS>YDTHENYS=YS+1
860 IFYS<YDTHENYS=YS-1
865 GOSUB220
870 IFPEEK(W+WS)<>96THEN900
875 POKEW+S(N),96
880 POKEW+WS,Q
885 S(N)=WS
900 NEXT
910 REM ***ALL IN PEN?***
915 P=0
920 FORN=334TO336
925 IFPEEK(W+N)=199ORPEEK(W+N)=203THENP=P+1
930 IFPEEK(W+N+32)=199ORPEEK(W+N+32)=203THENP=P+1
935 IFP=NSTHEN1030
940 NEXT
945 GOTO710
1000 REM ***END OF GAME***
1010 GOSUB2010
1022 GOTO1040
1025 REM ***DOG IN GATE?***
1030 IFPEEK(W+303)=247ORPEEK(W+303)=251THEN1000
1035 GOTO710
1040 GOSUB1100
1050 PRINT@1*32+1,MID$(STR$(NS),2);" SHEEP IN ";:GOSUB1200
1060 IFS<=HS(NS)THENHS(NS)=S
1065 S=HS(NS):GOSUB1100:PRINT@2*32+1,"HIGH SCORE ";:GOSUB1200
1070 PRINT@3*32+1,"PLAY AGAIN (Y/N)?";
1080 M$=INKEY$:IFM$=""THEN1080
1081 IFM$="Y"THEN5
1082 IFM$="N"THENEND
1083 GOTO1080
1100 TM=INT((S/60)/60):TS=INT((S/60)-(TM*60)):RETURN
1200 PRINTMID$(STR$(TM),2)" MIN";:IFTM<>1THENPRINT"S";
1210 PRINT","TS"SECS";:RETURN
2010 S=USR(1):POKE8,0:SOUND10,5:POKE8,8:PRINT@0,"''''''''''''''''''''''''''''''";
2040 RETURN
3000 Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:RETURN
3001 READ U$:FORY=1TOLEN(U$)STEP2:POKEZ,(ASC(MID$(U$,Y,1))-65)*16+ASC(MID$(U$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
4000 REM MC-10 VER. FROM ZX81 
4010 REM JIM GERRIE JAN 2018
