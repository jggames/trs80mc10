5 DIMA(109):O=RND(-(PEEK(9)*256+PEEK(10))):O=.5:R=.66:CLS1:FORI=1TO100:A(I)=0:NEXTI
15 A(1)=578:A(10)=467:A(91)=235:A(100)=124
20 A(12)=1:A(19)=3:A(82)=6:A(89)=8
25 FORI=11TO81STEP10:A(I)=27:A(I+9)=27:NEXTI
30 FORI=2TO9:A(I)=45:A(I+90)=45:NEXTI
100 FORI=12TO89
101 PRINT:PRINT@860*O,"M A Z I N G . . ."
102 IFA(I)>99THEN190
140 Z=.
150 N=RND(8):GOSUB200
152 IFT=.THENZ=Z+1:PRINTZ:IFZ<33THEN150
154 IFT<>1THEN190
160 IFA(I)<1THENA(I)=N:PRINTI,A(I):GOTO102
170 IFA(I)<9THENA(I)=10*N+A(I):PRINTI,A(I):GOTO102
180 A(I)=A(I)+100*N:PRINTI,A(I):GOTO102
190 NEXTI:GOTO390
200 ONNGOTO210,220,230,240,250,260,270,280
210 X=I-11:P=8:GOSUB300:RETURN
220 X=I-10:P=7:GOSUB300:RETURN
230 X=I-9:P=6:GOSUB300:RETURN
240 X=I-1:P=5:GOSUB300:RETURN
250 X=I+1:P=4:GOSUB300:RETURN
260 X=I+9:P=3:GOSUB300:RETURN
270 X=I+10:P=2:GOSUB300:RETURN
280 X=I+11:P=1:GOSUB300:RETURN
300 IFA(X)>99THENT=.:RETURN
310 GOSUB800
330 IF(P=J)+(P=K)+(P=L)THENT=.:RETURN
340 IFA(X)<1THENA(X)=P:T=1:RETURN
350 IFA(X)<9THENA(X)=P*10+A(X):T=1:RETURN
360 A(X)=P*100+A(X):T=1:RETURN
390 CLS1:U=.
392 Q=RND(100):S=RND(100):IF ABS(S-Q)<41 GOTO392
394 GOSUB500:X=63:Y=23:GOSUB600
400 PRINT@288,"YOU";:PRINT@320,Q;:PRINT@125,"123";:PRINT@157,"4 5";:PRINT@189,"678";
401 PRINT@316,"BOMB";:PRINT@348,S;
402 PRINT@160,"TIME"STR$(15-U);
403 PRINT@18,"WHICH";:PRINT@50,"WAY?";:SOUND50,1
404 M$=INKEY$:ON-(M$="")GOTO404:W=VAL(M$)
405 X=Q:GOSUB800:IF(J=W)+(K=W)+(L=W)THEN408
406 GOTO403
408 X=63:Y=23
409 ONWGOTO 410,420,430,440,450,460,470,480
410 Q=Q-11:FORX=63TO27STEP-6
412 GOSUB600:GOSUB700:Y=Y-2:NEXTX:GOTO485
420 Q=Q-10:FORY=23TO3STEP-4
422 GOSUB600:GOSUB700:NEXTY:GOTO485
430 Q=Q-9:FORX=63TO115STEP9
432 GOSUB600:GOSUB700:Y=Y-3:NEXTX:GOTO485
440 Q=Q-1:FORX=63TO6STEP-10
442 GOSUB600:GOSUB700:NEXTX:GOTO485
450 Q=Q+1:FORX=63TO123STEP10
452 GOSUB600:GOSUB700:NEXTX:GOTO485
460 Q=Q+9:FORX=63TO27STEP-6
462 GOSUB600:GOSUB700:Y=Y+2:NEXTX:GOTO485
470 Q=Q+10:FORY=23TO43STEP4
472 GOSUB600:GOSUB700:NEXTY:GOTO485
480 Q=Q+11:FORX=63TO106STEP6
482 GOSUB600:GOSUB700:Y=Y+2:NEXTX
485 CLS1:GOSUB500:X=63:Y=23:GOSUB600
490 IFQ=STHEN900
492 U=U+1:IF U=15 GOTO955
495 GOTO400
500 J=INT(A(Q)/100):K=INT((A(Q)-J*100)/10):L=A(Q)-100*J-10*K
504 M=K:IFM<1THEN506
505 GOSUB509
506 M=J:IFM<1THEN508
507 GOSUB509
508 M=L:GOSUB509:RETURN
509 ONMGOTO510,520,530,540,550,560,570,580
510 Y=1:FORX=12TO54STEP3:RESET(X*O,Y*R):Y=Y+1:NEXTX
512 Y=7:FORX=3TO37STEP3:RESET(X*O,Y*R):Y=Y+1:NEXTX:RETURN
520 FORY=1TO15STEP3:RESET(55*O,Y*R):RESET(71*O,Y*R):NEXTY:RETURN
530 Y=15:FORX=72TO117STEP3:RESET(X*O,Y*R):Y=Y-1:NEXTX
532 Y=18:FORX=90TO126STEP3:RESET(X*O,Y*R):Y=Y-1:NEXTX:RETURN
540 FORX=1TO38STEP4:RESET(X*O,19*R):RESET(X*O,27*R):NEXTX:RETURN
550 FORX=88TO127STEP4:RESET(X*O,19*R):RESET(X*O,27*R):NEXTX:RETURN
560 Y=40:FORX=0TO36STEP3:RESET(X*O,Y*R):Y=Y-1:NEXTX
562 Y=46:FORX=9TO54STEP3:RESET(X*O,Y*R):Y=Y-1:NEXTX:RETURN
570 FORY=31TO46STEP3:RESET(55*O,Y*R):RESET(71*O,Y*R):NEXTY:RETURN
580 Y=31:FORX=72TO117STEP3:RESET(X*O,Y*R):Y=Y+1:NEXTX
582 Y=27:FORX=90TO126 STEP3:RESET(X*O,Y*R):Y=Y+1:NEXTX:RETURN
600 XX=X*O:YY=(Y-3)*R:RESET(XX,YY):YY=YY+1:RESET(XX,YY):XX=XX-1:RESET(XX,YY):XX=XX+2:RESET(XX,YY)
601 XX=XX-1:YY=YY+1:RESET(XX,YY):XX=XX-1:YY=YY+1:RESET(XX,YY):XX=XX+2:RESET(XX,YY):RETURN 
700 XX=X*O:YY=(Y-3)*R:SET(XX,YY,1):YY=YY+1:SET(XX,YY,1):XX=XX-1:SET(XX,YY,1):XX=XX+2:SET(XX,YY,1)
701 XX=XX-1:YY=YY+1:SET(XX,YY,1):XX=XX-1:YY=YY+1:SET(XX,YY,1):XX=XX+2:SET(XX,YY,1):RETURN 
800 J=INT(A(X)/100):K=INT((A(X)-J*100)/10)
810 L=A(X)-J*100-K*10:RETURN
900 FORS=1TO5
910 FORX=640TO678:PRINT@X*O,"YOU'RE  SAFE";:PRINT@640*O,"":NEXTX:NEXTS:GOTO390
955 POKE16384+511,97:FORS=1TO222:PRINT@RND(500),"  B L A M  ";:NEXTS:GOTO390
995 REM DAVID J. BOHLKE
996 REM 1978 SOFTWARE EXCHANGE
997 REM MC-10 EDITS JIM GERRIE
