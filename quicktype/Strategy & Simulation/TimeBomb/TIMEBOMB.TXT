5 DIMA(109):CLS:FOR I=1 TO 100:A(I)=0:NEXTI
15  A(1)=578:A(10)=467:A(91)=235:A(100)=124
20  A(12)=1:A(19)=3:A(82)=6:A(89)=8
25  FOR I=11 TO 81 STEP 10:A(I)=27:A(I+9)=27:NEXTI
30  FOR I=2 TO 9:A(I)=45:A(I+90)=45:NEXTI
100  FOR I=12 TO 89
101  PRINT:PRINT@860,"M A Z I N G . . ."
102  IF A(I)>99 GOTO190
140  Z=0
150  N=RND(8):GOSUB200
152  IF T=0 THEN Z=Z+1:PRINTZ:IF Z<33 GOTO150
154  IF T<>1 GOTO190
160  IF A(I)<1 THEN A(I)=N:PRINTI,A(I):GOTO102
170  IF A(I)<9 THEN A(I)=10*N+A(I):PRINTI,A(I):GOTO102
180  A(I)=A(I)+100*N:PRINTI,A(I):GOTO102
190  NEXTI:GOTO390
200  ON N GOTO 210,220,230,240,250,260,270,280
210  X=I-11:P=8:GOSUB300:RETURN
220  X=I-10:P=7:GOSUB300:RETURN
230  X=I-9:P=6:GOSUB300:RETURN
240  X=I-1:P=5:GOSUB300:RETURN
250  X=I+1:P=4:GOSUB300:RETURN
260  X=I+9:P=3:GOSUB300:RETURN
270  X=I+10:P=2:GOSUB300:RETURN
280  X=I+11:P=1:GOSUB300:RETURN
300  IF A(X)>99 THEN T=0:RETURN
310  GOSUB800
330  IF (P=J)+(P=K)+(P=L) THEN T=0:RETURN
340  IF A(X)<1 THEN A(X)=P:T=1:RETURN
350  IF A(X)<9 THEN A(X)=P*10+A(X):T=1:RETURN
360  A(X)=P*100 +A(X):T=1:RETURN
390  CLS:U=0
392  Q=RND(100):S=RND(100):IF ABS(S-Q)<41 GOTO392
394  GOSUB500:X=63:Y=23:GOSUB600
400  PRINT@640,"YOU'RE AT";:PRINT@704,Q;
401  PRINT@693,"BOMB'S AT";:PRINT@762,S;
402  PRINT@320,"TIME ";15-U;
403  PRINT@38,"";:INPUT"WHICH WAY ";W
405  X=Q:GOSUB800:IF (J=W)+(K=W)+(L=W) GOTO408
406  GOTO403
408  X=63:Y=23
409  ON W GOTO 410,420,430,440,450,460,470,480
410  Q=Q-11:FORX=63TO27STEP-6
412  GOSUB600:GOSUB700:Y=Y-2:NEXTX:GOTO485
420  Q=Q-10:FORY=23TO3STEP-4
422  GOSUB600:GOSUB700:NEXTY:GOTO485
430  Q=Q-9:FORX=63 TO 115 STEP9
432  GOSUB600:GOSUB700:Y=Y-3:NEXTX:GOTO485
440  Q=Q-1:FORX=63TO6STEP-10
442  GOSUB600:GOSUB700:NEXTX:GOTO485
450  Q=Q+1:FORX=63TO123STEP10
452  GOSUB600:GOSUB700:NEXTX:GOTO485
460  Q=Q+9:FORX=63TO27STEP-6
462  GOSUB600:GOSUB700:Y=Y+2:NEXTX:GOTO485
470  Q=Q+10:FORY=23TO43STEP4
472  GOSUB600:GOSUB700:NEXTY:GOTO485
480  Q=Q+11:FORX=63TO106STEP6
482  GOSUB600:GOSUB700:Y=Y+2:NEXTX
485  CLS:GOSUB500:X=63:Y=23:GOSUB600
490  IF Q=S GOTO900
492  U=U+1:IF U=15 GOTO955
495  GOTO400
500  J=INT(A(Q)/100):K=INT((A(Q)-J*100)/10):L=A(Q)-100*J-10*K
504  M=K:IF M<1GOTO506
505  GOSUB509
506  M=J:IF M<1 GOTO508
507  GOSUB509
508  M=L:GOSUB509:RETURN
509  ON M GOTO 510,520,530,540,550,560,570,580
510  Y=1:FOR X=12 TO 54 STEP3:SET(X,Y):Y=Y+1:NEXTX
512  Y=7:FOR X=3 TO 37 STEP3:SET(X,Y):Y=Y+1:NEXTX:RETURN
520  FOR Y=0 TO 15:SET(55,Y):SET(71,Y):NEXTY:RETURN
530  Y=15:FOR X=72 TO 117 STEP3:SET(X,Y):Y=Y-1:NEXTX
532  Y=18:FOR X=90 TO 126 STEP3:SET(X,Y):Y=Y-1:NEXTX:RETURN
540  FOR X=1 TO 38:SET(X,19):SET(X,27):NEXTX:RETURN
550  FOR X=88 TO 127:SET(X,19):SET(X,27):NEXTX:RETURN
560  Y=40:FOR X=0 TO 36 STEP3:SET(X,Y):Y=Y-1:NEXTX
562  Y=46:FOR X=9 TO 54 STEP3:SET(X,Y):Y=Y-1:NEXTX:RETURN
570  FOR Y=31 TO 46:SET(55,Y):SET(71,Y):NEXTY:RETURN
580  Y=31:FOR X=72 TO 117 STEP3:SET(X,Y):Y=Y+1:NEXTX
582  Y=27:FOR X=90 TO 126 STEP3:SET(X,Y):Y=Y+1:NEXTX:RETURN
600  FOR A=X-2 TO X+2:SET(A,Y):NEXTA:SET(X,Y+1)
610  SET(X-1,Y+2):SET(X-2,Y+3):SET(X+1,Y+2):SET(X+2,Y+3)
620  FOR A=X-1 TO X+1:SET(A,Y-2):SET(A,Y-3):NEXTA
630  SET(X-3,Y-1):SET(X,Y-1):SET(X+3,Y-1):RETURN
700  FOR A=X-2 TO X+2:RESET(A,Y):NEXTA:RESET(X,Y+1)
710  RESET(X-1,Y+2):RESET(X-2,Y+3):RESET(X+1,Y+2):RESET(X+2,Y+3)
720  FOR A=X-1 TO X+1:RESET(A,Y-2):RESET(A,Y-3):NEXTA
730  RESET(X-3,Y-1):RESET(X,Y-1):RESET(X+3,Y-1):RETURN
800  J=INT(A(X)/100):K=INT((A(X)-J*100)/10)
810  L=A(X)-J*100-K*10:RETURN
900 FORS=1TO5
910 FORX=640TO690:PRINT@X,"YOU'RE  SAFE";:PRINT@640,:NEXTX:NEXTS:GOTO390
955  FORS=1TO222:PRINT@RND(999),"    B L A M    ";:NEXTS:GOTO390

