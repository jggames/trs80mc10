1 REM GOSUB:RETURN
10 DIM S(12,15),T(12,12),T$(72),B(3,12)
20 DIM W,D9,K9,X9,D1,X1,P9,T9,S9,Y9,H
30 DIM Y1,R9,G9,Q,M(6,3),C(6,3)
35 DIM S1,T1,R
200 R9=2/7
210 D9=15
220 Q=.1
230 K9=3
240 W=30
250 X9=36
260 G9=1.25
265 R=0
270 REM D1 IS THE DAY OF THIS YEAR (1<=D1<=360)
280 REM Y1 IS THIS YEAR
290 D1=1
300 Y1=2070
302 REM SET UP ECONOMETRICS MODEL
304 RESTORE
306 FORCC=1TO3:FORZZ=1TO6:READM(ZZ,CC):NEXT:READC(ZZ,CC):NEXT
310 REM *** BLOCK #1
320 PRINT "INSTRUCTIONS (TYPE 'Y' OR 'N' PLEASE)";
330 INPUT A$
340 IF A$(1,1)="N" THEN 590
350 PRINT 
360 M$="     THE DATE IS JAN 1, 2070 AND INTERSTELLAR FLIGHT"
370 M$=M$+"HAS EXISTED FOR 70 YEARS. THERE ARE SEVERAL STAR "
380 M$=M$+"SYSTEMS THAT HAVE BEEN COLONIZED."GOSUB1:M$="SOME ARE ONLY"
390 M$=M$+"FRONTIER SYSTEMS, OTHERS ARE OLDER AND MORE DEVELOPED.":GOSUB1
400 INPUTM$
410 M$="     EACH OF YOU IS THE CAPTAIN OF TWO INTERSTELLAR "
420 M$=M$+"TRADING SHIPS.":GOSUB1:M$="YOU WILL TRAVEL FROM STAR SYSTEM TO "
430 M$=M$+"STAR SYSTEM, BUYING AND SELLING MERCHANDISE.":GOSUB1:M$="IF YOU "
440 PRINT "DRIVE A GOOD BARGAIN YOU CAN MAKE LARGE PROFITS."
450 INPUTM$ 
460 M$="     AS TIME GOES ON, EACH STAR SYSTEM WILL SLOWLY "
470 M$=M$+"GROW, AND ITS NEEDS WILL CHANGE.":GOSUB1:M$="A STAR SYSTEM THAT "
480 M$=M$+"HOW IS SELLING MUCH URANIUM AND RAW METALS CHEAPLY "
490 M$=M$+"MAY NOT HAVE ENOUGH FOR EXPORT IN A FEW YEARS.":GOSUB1
500 INPUTM$ 
510 M$="     YOUR SHIPS CAN TRAVEL ABOUT TWO LIGHTYEARS IN A "
520 M$=M$+"WEEK AND CAN CARRY UP TO"+STR$(W)+" TONS OF CARGO.":GOSUB1:M$="ONLY "
530 M$=M$+"CLASS I AND CLASS II STAR SYSTEMS HAVE BANKS ON THEM.":GOSUB1
540 M$="THEY PAY 5% INTEREST AND ANY MONEY YOU DEPOSIT ON ONE "
550 M$=M$+"PLANET IS AVAILABLE ON ANOTHER - PROVIDED THERE'S A LOCAL "
560 M$=M$+"BANK.":GOSUB1
570 INPUTM$ 
580 REM *** BLOCK #2
590 M$="HAVE ALL PLAYERS PLAYED BEFORE?":GOSUB1
600 INPUTA$
605 PRINT 
610 IF A$(1,1)="Y" THEN 630
620 GOTO 660
630 M$="DO YOU WANT TO SET UP YOUR OWN GAME?":GOSUB1
640 INPUTA$
645 PRINT 
650 IF A$(1,1)="Y" THEN 760
660 PRINT"HOW MANY PLAYERS?"
670 INPUTP9
675 PRINT 
680 ONP9-1GOTO710,710,710
690 PRINT"2,3, OR 4 CAN PLAY"
700 GOTO660
710 T9=2*P9
720 S9=3*P9+1
730 Y9=Y1+5
740 GOTO1350
750 REM *** BLOCK #3
760 PRINT"IS THIS A RESTART?"
770 INPUTA$
775 PRINT 
780 IF A$(1,1)="N" THEN 940
790 M$="LOAD THE TAPE INTO THE TAPE READER.  WHEN I TYPE A '?'"
800 M$="YOU CAN FLIP THE SWITCH TO 'START' WHENEVER YOU'RE READY"
810 PRINT 
820 INPUT T$
830 INPUT W,D9,K9,X9,D1,Y1
835 INPUT P9,T9,S9,Y9,T1,S1
840 FOR J=1 TO S9
845 FOR I=1 TO 9 STEP 4
850 INPUT S(I,J),S(I+1,J),S(I+2,J),S(I+3,J)
855 NEXT I
860 NEXT J
870 FOR J=1 TO T9
875 FOR I=1 TO 9 STEP 4
880 INPUT T(I,J),T(I+1,J),T(I+2,J),T(I+3,J)
885 NEXT I
890 NEXT J
900 FOR I=1 TO P9
910 INPUT B(1,I),B(2,I),B(3,I)
920 NEXT I
925 R=1
930 CHAIN "$TRADES"
940 PRINT"HOW MANY PLAYERS"
950 INPUTP9
955 PRINT 
960 IFP9>=2ANDP9<=12THEN990
970 M$="2,3,4, ... ,12 CAN PLAY":GOSUB1
980 GOTO940
990 PRINT"HOW MANY SHIPS PER PLAYER?"
1000 INPUTX
1005 PRINT 
1010 IF X<1 THEN 990
1020 T9=P9*X
1030 IFT9<=12THEN1070
1040 M$="I CAN'T KEEP TRACK OF MORE THAN 12 SHIPS;"+STR$(P9)+" PLAYERS TIMES"+STR$(X)+" SHIPS MAKES"+STR$(T9):GOSUB1
1060 GOTO990
1070 PRINT"HOW MANY STAR SYSTEMS?"
1080 INPUTS9
1085 PRINT 
1090 IF S9 >= 4 AND S9 <= 13 THEN 1120
1100 PRINT "FROM 4 TO 13 STARS"
1110 GOTO 1070
1120 PRINT "ENTER THE LENGTH OF GAME IN YEARS";
1130 INPUT X
1135 PRINT 
1140 IF X >= 1 AND INT(X)=X THEN 1170
1150 PRINT "CHOOSE A POSITIVE INTEGER"
1160 GOTO 1130
1170 Y9=Y1+X
1180 PRINT "WHAT'S THE MAX CARGOE TONNAGE(USUALLY 30)";
1190 INPUT W
1195 PRINT 
1200 IF W<25 THEN 1180
1210 PRINT "WHAT'S THE MINIMUM DISTANCE BETWEEN STARS(USUALLY 15)";
1220 INPUT D9
1225 PRINT 
1230 IF D9 <= 25 AND D9 >= 10 THEN 1260
1240 PRINT "MIN SPACING 10, MAX 25"
1250 GOTO1210
1260 PRINT"HOW MANY BIDS OR OFFERS"
1270 INPUT"(USUALLY 3)";K9
1275 PRINT 
1280 IFK9<1 THEN 1260
1290 M$="SET THE PROFIT MARGIN(1,2,3,4 OR 5)...THE HIGHER THE NUMBER, THE LOWER THE PROFIT % ... USUALLY SET TO 2"
1310 GOSUB1:PRINT "...YOUR NUMBER";
1320 INPUTX9
1330 IFABS(X9)<5THENCC=ABS(X9):GOTO1332
1331 CC=5
1332 X9=18*CC
1340 REM *** BLOCK #4.1
1350 S(11,1)=S(12,1)=0
1360 S(7,1)=15
1370 REM *** BLOCK #4.2
1380 H=1
1390 S1=2
1400 GOSUB1920
1410 S1=3
1420 GOSUB1920
1430 S1=4
1440 GOSUB2010
1450 FORS1=5 TO S9
1460 ONS1-3*INT((S1-1)/3)GOSUB1920,2010,2060
1470 NEXTS1
1480 REM *** BLOCK #4.3
1490 FORS1=1TOS9
1500 FORJ=1TO6
1510 S(J,S1)=0
1520 NEXT J
1530 IFS1>1THEN1560
1540 I=1
1550 GOTO1600
1560 I=4*INT(14*RND(0))+5
1570 FORJ=2TOS1-1
1580 IFI=S(8,J)THEN1560
1590 NEXT J
1600 S(8,S1)=I
1610 S(9,S1)=270
1620 S(10,S1)=Y1-1
1630 NEXTS1
1640 REM *** BLOCK #4.4
1650 T1=L=1
1655 PRINT 
1660 M$="CAPTAINS, NAME YOUR SHIPS (UP TO 6 LETTERS/BLANKS/NUMBERS)":GOSUB1
1670 FORI=1TOT9/P9
1680 PRINT 
1690 FORP1=1TOP9
1700 T(1,T1)=T(2,T1)=T(6,T1)=0
1710 T(3,T1)=15
1720 T(4,T1)=T(5,T1)=10
1730 T(7,T1)=25
1740 T(9,T1)=D1
1750 T(10,T1)=Y1
1760 T(11,T1)=5000
1770 M$="CAPTAIN"+STR$(P1)+" WHAT DO YOU CHRISTEN YOUR SHIP #"+STR$(I)+"?":GOSUB1
1780 INPUTT$(L,L+5)
1790 T1=T1+1
1800 L=L+6
1810 NEXTP1
1820 NEXTI
1830 REM *** BLOCK #4.5
1840 FORB1=1TOP9
1850 B(1,B1)=0
1860 B(2,B1)=D1
1870 B(3,B1)=Y1
1880 NEXTB1
1890 CHAIN "$TRADES"
1900 REM *** GOSUBS FOLLOW ***
1910 REM <FRONTIER> GOSUB
1920 X=(RND(0)-.5)*100
1930 Y=50*RND(0)
1940 IF(ABS(X)<25)AND(Y<25)THEN1920
1950 F=1
1960 GOSUB2190
1970 IFF=0THEN1920
1980 S(7,S1)=0
1990 RETURN
2000 REM *** <UNDERDEVELOPED> GOSUB
2010 E=100
2020 GOSUB2110
2030 S(7,S1)=5
2040 RETURN
2050 REM *** <DEVELOPED> GOSUB
2060 E=50
2070 GOSUB2110
2080 S(7,S1)=10
2090 RETURN
2100 REM *** <GENERATE CO-ORDS> GOSUB
2110 X=(RND(0)-.5)*E
2120 Y=RND(0)*E/2
2130 F=1
2140 GOSUB2190
2150 IFF=0THEN2110
2160 RETURN
2170 REM *** <TEST STAR CO-ORDS> GOSUB
2180 REM FIRST CONVERT CO-ORDS TO NEXT HALF-BOARD
2190 ONHGOTO2300,2260,2240,2200
2200 Z=X
2210 X=-Y
2220 Y=Z
2230 GOTO 2300
2240 Y=-Y
2250 GOTO 2300
2260 Z=X
2270 X=Y
2280 Y=Z
2290 REM SECOND, TEST PROXIMITY
2300 FORJ=1TOS1-1
2310 IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN2340
2320 F=0
2330 RETURN
2340 NEXTJ
2350 REM FINALLY, ENTER CO-ORDS AND INCREMENT HALF-BOARD CTR
2360 S(11,S1)=INT(X)
2370 S(12,S1)=INT(Y)
2380 H=1+ABS(H<=3)*H
2390 RETURN
2400 REM *** DATA FOR ECONOMETRIC MODEL FOLLOWS ***
2410 REM MODEL #1
2420 DATA -0.025,-0.05,-0.025,0,-0.025,-0.025,0,.1,.1,-0.025,.1,0,.1,.2,.1,.1,-0.025,0
2430 DATA 1,1.5,.5,.75,.75,.75,-0.25,-0.25,-0.25,0,-0.5,.5,0,-0.5,0,.5,1.5,0
2450 END