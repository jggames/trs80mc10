0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 GOSUB15:CC=(C1*.5+(ABS(NOT(C1)))*X/(2*ABS(S(I1,S1))))/K1
15 C1=ABS(X>=ABS(S(I1,S1))):RETURN
20 DIMS(27,15),T$,W,D9,K9,X9,D1,X1,P9,T9,S9,Y9,H,Y1,R9,G9,Q,M(6,3),C(6,3),S1,T1,R
30 DIMC$,S$:R9=2/7:D9=15:Q=.1:K9=3:W=30:X9=36:G9=1.25:R=0:D1=1:Y1=2070
37 FORCC=1TO3:FORZZ=1TO6:READM(ZZ,CC):NEXT:READC(ZZ,CC):NEXT
39 M$="INSTRUCTIONS?";
40 INPUTA$
41 IFMID$(A$,1,1)="N"THEN66
42 PRINT 
43 M$="     THE DATE IS JAN 1, 2070 AND INTERSTELLAR FLIGHT"
44 M$=M$+"HAS EXISTED FOR 70 YEARS. THERE ARE SEVERAL STAR "
45 M$=M$+"SYSTEMS THAT HAVE BEEN COLONIZED."GOSUB1:M$="SOME ARE ONLY"
46 M$=M$+"FRONTIER SYSTEMS, OTHERS ARE OLDER AND MORE DEVELOPED.":GOSUB1
47 INPUTM$
48 M$="     EACH OF YOU IS THE CAPTAIN OF TWO INTERSTELLAR "
49 M$=M$+"TRADING SHIPS.":GOSUB1:M$="YOU WILL TRAVEL FROM STAR SYSTEM TO "
50 M$=M$+"STAR SYSTEM, BUYING AND SELLING MERCHANDISE.":GOSUB1:M$="IF YOU "
51 PRINT "DRIVE A GOOD BARGAIN YOU CAN MAKE LARGE PROFITS."
52 INPUTM$ 
53 M$="     AS TIME GOES ON, EACH STAR SYSTEM WILL SLOWLY "
54 M$=M$+"GROW, AND ITS NEEDS WILL CHANGE.":GOSUB1:M$="A STAR SYSTEM THAT "
55 M$=M$+"HOW IS SELLING MUCH URANIUM AND RAW METALS CHEAPLY "
56 M$=M$+"MAY NOT HAVE ENOUGH FOR EXPORT IN A FEW YEARS.":GOSUB1
57 INPUTM$ 
58 M$="     YOUR SHIPS CAN TRAVEL ABOUT TWO LIGHTYEARS IN A "
59 M$=M$+"WEEK AND CAN CARRY UP TO"+STR$(W)+" TONS OF CARGO.":GOSUB1:M$="ONLY "
60 M$=M$+"CLASS I AND CLASS II STAR SYSTEMS HAVE BANKS ON THEM.":GOSUB1
61 M$="THEY PAY 5% INTEREST AND ANY MONEY YOU DEPOSIT ON ONE "
62 M$=M$+"PLANET IS AVAILABLE ON ANOTHER - PROVIDED THERE'S A LOCAL "
63 M$=M$+"BANK.":GOSUB1
64 INPUTM$ 
66 M$="HAVE ALL PLAYERS PLAYED BEFORE?":GOSUB1
67 INPUTA$
68 PRINT 
69 IFMID$(A$,1,1)="Y"THEN71
70 GOTO75
71 M$="DO YOU WANT TO SET UP YOUR OWN GAME?":GOSUB1
72 INPUTA$
73 PRINT 
74 IFMID$(A$,1,1)="Y"THEN86
75 PRINT"HOW MANY PLAYERS?"
76 INPUTP9
77 PRINT 
78 ONP9-1GOTO81,81,81
79 PRINT"2,3, OR 4 CAN PLAY"
80 GOTO75
81 T9=2*P9
82 S9=3*P9+1
83 Y9=Y1+5
84 GOTO159
85 REM *** BLOCK #3
86 PRINT"IS THIS A RESTART?"
87 INPUTA$
88 PRINT
89 IFMID$(A$,1,1)="N"THEN111
90 M$="LOAD THE TAPE INTO THE TAPE READER.  WHEN I TYPE A '?'"
91 M$="YOU CAN FLIP THE SWITCH TO 'START' WHENEVER YOU'RE READY"
92 PRINT 
93 INPUT T$
94 INPUT W,D9,K9,X9,D1,Y1
95 INPUT P9,T9,S9,Y9,T1,S1
96 FOR J=1 TO S9
97 FOR I=1 TO 9 STEP 4
98 INPUT S(I,J),S(I+1,J),S(I+2,J),S(I+3,J)
99 NEXTI
100 NEXTJ
101 FORJ=1 TO T9
102 FORI=1 TO 9 STEP 4
103 INPUT S(12+I,J),S(12+I+1,J),S(12+I+2,J),S(12+I+3,J)
104 NEXT I
105 NEXT J
106 FORI=1TOP9
107 INPUTS(24+1,I),S(24+2,I),S(24+3,I)
108 NEXTI
109 R=1
110 GOTO270
111 PRINT"HOW MANY PLAYERS"
112 INPUTP9
113 PRINT 
114 IFP9>=2ANDP9<=12THEN117
115 M$="2,3,4, ... ,12 CAN PLAY":GOSUB1
116 GOTO111
117 PRINT"HOW MANY SHIPS PER PLAYER?"
118 INPUTX
119 PRINT 
120 IF X<1 THEN 117
121 T9=P9*X
122 IFT9<=12THEN125
123 M$="I CAN'T KEEP TRACK OF MORE THAN 12 SHIPS;"+STR$(P9)+" PLAYERS TIMES"+STR$(X)+" SHIPS MAKES"+STR$(T9):GOSUB1
124 GOTO117
125 PRINT"HOW MANY STAR SYSTEMS?"
126 INPUTS9
127 PRINT 
128 IF S9 >= 4 AND S9 <= 13 THEN 131
129 PRINT "FROM 4 TO 13 STARS"
130 GOTO 125
131 PRINT "ENTER THE LENGTH OF GAME IN YEARS";
132 INPUT X
133 PRINT 
134 IF X >= 1 AND INT(X)=X THEN 137
135 PRINT "CHOOSE A POSITIVE INTEGER"
136 GOTO 132
137 Y9=Y1+X
138 PRINT "WHAT'S THE MAX CARGOE TONNAGE(USUALLY 30)";
139 INPUT W
140 PRINT 
141 IF W<25 THEN 138
142 PRINT "WHAT'S THE MINIMUM DISTANCE BETWEEN STARS(USUALLY 15)";
143 INPUT D9
144 PRINT 
145 IF D9 <= 25 AND D9 >= 10 THEN 148
146 PRINT "MIN SPACING 10, MAX 25"
147 GOTO142
148 PRINT"HOW MANY BIDS OR OFFERS"
149 INPUT"(USUALLY 3)";K9
150 PRINT 
151 IFK9<1 THEN 148
152 M$="SET THE PROFIT MARGIN(1,2,3,4 OR 5)...THE HIGHER THE NUMBER, THE LOWER THE PROFIT % ... USUALLY SET TO 2"
153 GOSUB1:PRINT "...YOUR NUMBER";
154 INPUTX9
155 IFABS(X9)<5THENCC=ABS(X9):GOTO157
156 CC=5
157 X9=18*CC
158 REM *** BLOCK #4.1
159 S(11,1)=S(12,1)=0
160 S(7,1)=15
161 REM *** BLOCK #4.2
162 H=1
163 S1=2
164 GOSUB217
165 S1=3
166 GOSUB217
167 S1=4
168 GOSUB226
169 FORS1=5 TO S9
170 ONS1-3*INT((S1-1)/3)GOSUB217,226,231
171 NEXTS1
172 REM *** BLOCK #4.3
173 FORS1=1TOS9
174 FORJ=1TO6
175 S(J,S1)=0
176 NEXT J
177 IFS1>1THEN180
178 I=1
179 GOTO184
180 I=4*INT(14*RND(0))+5
181 FORJ=2TOS1-1
182 IFI=S(8,J)THEN180
183 NEXT J
184 S(8,S1)=I
185 S(9,S1)=270
186 S(10,S1)=Y1-1
187 NEXTS1
188 REM *** BLOCK #4.4
189 T1=L=1
190 PRINT 
191 M$="CAPTAINS, NAME YOUR SHIPS (UP TO 6 LETTERS/BLANKS/NUMBERS)":GOSUB1
192 FORI=1TOT9/P9
193 PRINT 
194 FORP1=1TOP9
195 S(12+1,T1)=S(12+2,T1)=S(12+6,T1)=0
196 S(12+3,T1)=15
197 S(12+4,T1)=S(12+5,T1)=10
198 S(12+7,T1)=25
199 S(12+9,T1)=D1
200 S(12+10,T1)=Y1
201 S(12+11,T1)=5000
202 M$="CAPTAIN"+STR$(P1)+" WHAT DO YOU CHRISTEN YOUR SHIP #"+STR$(I)+"?":GOSUB1
203 INPUTM$:T$=T$+MID$(M$+"      ",1,6)
204 T1=T1+1
205 L=L+6
206 NEXTP1
207 NEXTI
208 REM
209 FORB1=1TOP9
210 S(24+1,B1)=0
211 S(24+2,B1)=D1
212 S(24+3,B1)=Y1
213 NEXTB1
214 GOTO270
215 REM
216 REM
217 X=(RND(0)-.5)*100
218 Y=50*RND(0)
219 IF(ABS(X)<25)AND(Y<25)THEN217
220 F=1
221 GOSUB244
222 IFF=0THEN217
223 S(7,S1)=0
224 RETURN
225 REM
226 E=100
227 GOSUB236
228 S(7,S1)=5
229 RETURN
230 REM
231 E=50
232 GOSUB236
233 S(7,S1)=10
234 RETURN
235 REM
236 X=(RND(0)-.5)*E
237 Y=RND(0)*E/2
238 F=1
239 GOSUB244
240 IFF=0THEN236
241 RETURN
242 REM *** <TEST STAR CO-ORDS> GOSUB
243 REM FIRST CONVERT CO-ORDS TO NEXT HALF-BOARD
244 ONHGOTO255,251,249,245
245 Z=X
246 X=-Y
247 Y=Z
248 GOTO 255
249 Y=-Y
250 GOTO 255
251 Z=X
252 X=Y
253 Y=Z
254 REM SECOND, TEST PROXIMITY
255 FORJ=1TOS1-1
256 IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN259
257 F=0
258 RETURN
259 NEXTJ
260 REM FINALLY, ENTER CO-ORDS AND INCREMENT HALF-BOARD CTR
261 S(11,S1)=INT(X)
262 S(12,S1)=INT(Y)
263 H=1+ABS(H<=3)*H
264 RETURN
265 REM
266 REM
267 DATA -0.025,-0.05,-0.025,0,-0.025,-0.025,0,.1,.1,-0.025,.1,0,.1,.2,.1,.1,-0.025,0
268 DATA 1,1.5,.5,.75,.75,.75,-0.25,-0.25,-0.25,0,-0.5,.5,0,-0.5,0,.5,1.5,0
269 END
270 C$="JANFEBMARAPRMIYJUNJULAUGSEPOCTNOVDEC"
271 S$="SOL YORKBOYDIVANREEFHOOKSTANTASKSINKSANDQUINGAOLKIRKKRISFATE"
273 DIMP(6),Q(6)
274 DIMA$,D$,N$,G(6)
275 Q(1)=5000:Q(2)=3500:Q(3)=4000:Q(4)=4500:Q(5)=3000:Q(6)=3000
276 N$="UR   MET  HE   MED  SOFT GEMS"
400 REM *** BLOCK #5
402 IFR=0THEN430
404 GOSUB3860
406 GOSUB5190
408 FORT2=1TOT9
410 IFT2=T1THEN420
412 L=(T2-1)*6+1
414 M$=MID$(T$,L,6)+" IS ENROUTE TO "+MID$(S$,S(12+8,T2),4):GOSUB1
420 NEXTT2
424 L=(T1-1)*6+1
426 M$=" AND "+MID$(T$,L,6)+" IS ABOUT TO LEAVE "+MID$(S$,S(12+8,T1),4):GOSUB1
428 GOTO2040
430 REM GOSUB5190
432 GOSUB3190
434 S1=1:T1=1:L1=1
440 PRINT
450 M$="ALL SHIPS START AT SOL":GOSUB1
460 M$="ADVICE;  VISIT THE CLASS III AND IV SYSTEMS -":GOSUB1
470 M$="SOL AND THE CLASS II STARS PRODUCE ALOT OF HE, MED AND SOFT, WHICH THE POORER STAR SYSTEMS (CLASS III AND"
490 M$=M$+" IV) NEED.":GOSUB1:M$="ALSO, THE POOR STARS PRODUCE THE RAW GOODS - "
500 M$=M$+"UR, MET, GEMS THAT YOU CAN BRING BACK TO SOL AND THE CLASS II SYSTEMS IN TRADE":GOSUB1
520 PRINT 
530 M$="STUDY THE MAP AND CURRENT PRICE CHARTS CAREFULLY -":GOSUB1
540 M$="CLASS I AND II STARS MAKE EXCELLENT TRADING PARTNERS WITH CLASS III OR IV STARS.":GOSUB1
560 FORI1=1TOT9/P9
570 FORP1=1TOP9
580 PRINT 
590 M$="PLAYER"+STR$(P1)+", WHICH STAR WILL "+MID$(T$,L1,6)+" TRAVEL TO?":GOSUB1
600 GOSUB2770
610 L1=L1+6
620 T1=T1+1
630 NEXTP1
640 NEXTI1
650 REM *** BLOCK #6
660 D=S(12+9,1)
670 Y=S(12+10,1)
680 T1=1
690 FORI=2TOT9
700 IFS(12+10,I)<YTHEN740
710 IFS(12+10,I)>YTHEN770
720 IFS(12+9,I)>DTHEN770
730 IFS(12+9,I)=DANDRND(0)>.5THEN770
740 D=S(12+9,I)
750 Y=S(12+10,I)
760 T1=I
770 NEXTI
780 IFY1=YTHEN900
790 D1=1
800 Y1=Y
810 T2=T1
820 GOSUB3190
822 IFY1<>2071THEN830
824 GOSUB4500
826 M$="THE LAST YEAR OF THIS GAME IS"+STR$(Y9)+" BUT IF YOU WANT TO QUIT BEFORE THEN, YOU CAN TYPE 'SAVE' AS "
829 M$=M$+"YOUR NEXT PORT OF CALL - THIS WILL SAVE TO TAPE SO YOU CAN CONTINUE THE GAME LATER"
831 T1=T2
840 IFY1<Y9THEN900
850 GOSUB4500
860 PRINT"NEW GAME";
870 INPUTA$
880 IFMID$(A$,1,1)="N"THEN5500
890 RUN
900 D1=D
910 M=INT((D1-1)/30)
920 L=3*M+1
930 PRINT 
940 PRINT 
950 PRINT"*****************"
960 M$="* "+MID$(C$,L,3)+STR$(D1-30*M)+","+MID$(STR$(Y1),2):GOSUB1
970 L=(T1-1)*6+1
980 S1=S(12+8,T1)
990 M=S(8,S1)
1000 M$="* "+MID$(T$,L,6)+" HAS LANDED ON "+MID$(S$,M,4):GOSUB1
1010 ONS(12+12,T1)+1GOTO1080,1060,1040,1020
1020 M$="3 WEEKS LATE - PIRATES ATTACKED MIDVOYAGE":GOSUB1
1030 GOTO1080
1040 M$="2 WEEKS LATE - 'WE GOT LOST.SORRY'":GOSUB1
1050 GOTO1080
1060 M$="1 WEEK LATE - 'OUR COMPUTER MADE A MISTAKE'":GOSUB1
1070 REM *** PRINT CARGO STATUS FOR CURRENT SHIP
1080 PRINT 
1090 PRINT"$ ON BOARD:"S(12+11,T1),"  NET WT"S(12+7,T1):PRINTN$
1100 PRINTMID$(STR$(S(12+1,T1))+"     ",2,5);
1101 PRINTMID$(STR$(S(12+2,T1))+"     ",2,5);
1102 PRINTMID$(STR$(S(12+3,T1))+"     ",2,5);
1103 PRINTMID$(STR$(S(12+4,T1))+"     ",2,5);
1104 PRINTMID$(STR$(S(12+5,T1))+"     ",2,5);
1105 PRINTMID$(STR$(S(12+6,T1))+"     ",2,5)
1120 REM *** BLOCK #7
1130 GOSUB3870
1140 PRINT 
1150 PRINT "WE ARE BUYING:"
1160 J1=1
1170 FORI1=1TO6
1180 IFS(I1,S1)>=0OR S(12+I1,T1)<.5THEN1480
1190 M$=N$(J1,J1+5):GOSUB5:PRINT" WE NEED ";-INT(S(I1,S1));" UNITS."
1200 PRINT"HOW MANY ARE YOU SELLING";
1210 GOSUB4430
1220 IFX=0THEN1480
1230 IFX<=S(12+I1,T1)THEN1270
1240 PRINT"YOU ONLY HAVE ";S(12+I1,T1);" UNITS"
1260 GOTO1200
1270 IFX<=2*-INT(S(I1,S1))THEN1300
1280 X=2*-INT(S(I1,S1))
1290 PRINT"WE'LL BID ON ";X;" UNITS."
1300 FORK1=1TOK9
1310 IFK9>2THENCC=K9:GOTO1312
1311 CC=2
1312 IFK1<>CCTHEN1340
1320 PRINT"OUR FINAL OFFER:";
1330 GOTO1350
1340 PRINT"WE OFFER ";
1342 Y2=(L1+1)*10/3
1350 PRINT100*INT(.009*P(I1)*X+.5);" WHAT DO YOU BID";
1360 INPUTY
1362 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1370
1364 PRINT"WATCH YOUR TYPING -- TRY AGAIN"
1366 GOTO1340
1370 IFY<=P(I1)*XTHEN1430
1380 GOSUB10:IFY>(1+CC)*P(I1)*XTHEN1410
1390 P(I1)=.8*P(I1)+.2*Y/X
1400 NEXTK1
1410 PRINT"WE'LL PASS THIS ONE"
1420 GOTO1480
1430 PRINT"WE'LL BUY!"
1440 S(12+I1,T1)=S(12+I1,T1)-X
1450 S(12+7,T1)=S(12+7,T1)-X*(I1<5)
1460 S(12+11,T1)=S(12+11,T1)+Y
1470 S(I1,S1)=S(I1,S1)+X
1480 J1=J1+6
1490 NEXTI1
1500 PRINT 
1510 REM *** BLOCK #8
1520 PRINT"WE ARE SELLING:"
1530 J1=1
1540 FORI1=1TO6
1550 IFG(I1)<=0ORS(I1,S1)<1THEN1960
1555 IFI1<=4ANDS(12+7,T1)>=WTHEN1960
1560 PRINTN$(J1,J1+5);" UP TO ";INT(S(I1,S1));" UNITS."
1570 PRINT"HOW MANY ARE YOU BUYING";
1580 GOSUB4430
1590 IFX=0THEN1960
1600 IFI1>4ORX+S(12+7,T1)<=WTHEN1660
1610 PRINT"YOU HAVE ";S(12+7,T1);" TONS ABOARD"
1620 PRINT"SO ";X;"TONS PUTS YOU OVER"
1630 PRINT"THE ";W;" TON LIMIT."
1650 GOTO1570
1660 IFX<=S(I1,S1)THEN1700
1670 PRINT"WE ONLY HAVE ";INT(S(I1,S1));" UNITS"
1690 GOTO1570
1700 FORK1=1TOK9
1710 IFK9>2THENCC=K1:GOTO1712
1711 CC=2
1712 IFK1>CCTHEN1740
1720 PRINT"OUR FINAL OFFER:";
1730 GOTO1750
1740 PRINT"WE WANT ABOUT ";
1750 PRINT100*INT(.011*P(I1)*X+.5);
1760 PRINT"YOUR OFFER";
1770 INPUTY
1772 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1780
1774 PRINT"WATCH YOUR TYPING -- TRY AGAIN"
1776 GOTO1740
1780 IFY>=P(I1)*XTHEN1840
1790 GOSUB10:IFY<(1-CC)*P(I1)*XTHEN1820
1800 P(I1)=.8*P(I1)+.2*Y/X
1810 NEXTK1
1820 PRINT"THAT'S TOO LOW"
1830 GOTO1960
1840 IFY<=S(12+11,T1)THEN1910
1850 PRINT"YOU BID $";Y:PRINT"BUT YOU HAVE ONLY $";S(12+11,T1)
1860 GOSUB4310
1870 IFS(7,S1)<10ORS(12+11,T1)+S(24+1,B1)<YTHEN1820
1890 GOSUB4020
1900 IFY>S(12+11,T1)THEN1820
1910 PRINT"SOLD!"
1920 S(12+I1,T1)=S(12+I1,T1)+X
1930 S(12+7,T1)=S(12+7,T1)+X*(I1<5)
1940 S(I1,S1)=S(I1,S1)-X
1950 S(12+11,T1)=S(12+11,T1)-Y
1960 J1=J1+6
1970 NEXTI1
1980 REM *** BLOCK #9
1990 GOSUB4310
2000 IFS(7,S1)<10ORS(12+11,T1)+S(24+1,B1)=0THEN2040
2010 PRINT 
2020 GOSUB4020
2030 PRINT 
2040 PRINT"WHAT IS YOUR NEXT PORT OF CALL";
2050 GOSUB2770
2060 REM *** BLOCK #10.1
2070 J=0
2080 FORI=1TO6
2090 IFS(I,S1)>=0THEN2120
2100 IFS(I,S1)<G(I)THEN660
2110 J=J+1
2120 NEXTI
2130 IFJ>1THEN660
2140 REM *** BLOCK #10.2
2150 S(7,S1)=S(7,S1)+G9
2160 G0=S(7,S1)
2162 IFG0<>5ANDG0<>10ANDG0<>15THEN2220
2170 GOSUB4580
2180 GOSUB4500
2190 PRINT"STAR SYSTEM ";S$(S(8,S1),S(8,S1)+3);" IS NOW A CLASS";
2200 PRINTD$;" SYSTEM"
2210 REM *** BLOCK #10.3
2220 IFS9=15THEN660
2230 J=0
2240 FORI=1TOS9
2250 J=J+S(7,I)
2260 NEXT I
2270 IF J/S9<10 THEN 660
2280 REM A NEW STAR IS BORN!
2290 S9=S9+1:S1=S9
2300 GOSUB4680
2310 GOSUB2450
2320 S(9,S1)=D1
2330 S(10,S1)=Y1
2340 FORJ=1TO6
2350 S(J,S1)=0
2360 NEXTJ
2370 GOSUB4500
2380 M$="A NEW STAR SYSTEM HAS BEEN DISCOVERED! IT IS A CLASS IV":GOSUB1
2390 M$="AND ITS NAME IS"+MID$(S$,S(8,S1),4):GOSUB1
2400 GOSUB5190
2410 GOTO660
2420 END
2430 REM *** GOSUBS FOLLOW ***
2440 REM <FRONTIER> GOSUB
2450 X=(RND(0)-.5)*100
2460 Y=50*RND(0)
2470 IF(ABS(X)<25)AND(Y<25)THEN2450
2480 F=1
2490 GOSUB2550
2500 IFF=0THEN2450
2510 S(7,S1)=0
2520 RETURN
2530 REM *** <TEST STAR CO-ORDS> GOSUB
2540 REM FIRST CONVERT CO-ORDS TO NEXT HALF-BOARD
2550 ONHGOTO2660,2620,2600,2560
2560 Z=X
2570 X=-Y
2580 Y=Z
2590 GOTO2660
2600 Y=-Y
2610 GOTO 2660
2620 Z=X
2630 X=Y
2640 Y=Z
2650 REM SECOND TEST PROXIMITY
2660 FORJ=1TOS1-1
2670 IF QR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN2700
2680 F=0
2690 RETURN
2700 NEXTJ
2710 REM FINALLY ENTER CO-ORDS AND INCREMENT HALF-BOARD COUNTER
2720 S(11,S1)=INT(X)
2730 S(12,S1)=INT(Y)
2740 H=1+ABS(H<=3)*H
2750 RETURN
2760 REM *** <NEXT ETA> GOSUB
2770 INPUTA$
2780 FORI=1TOS9
2790 J=S(8,I)
2800 IFA$(1,4)=S$(J,J+3)THEN2870
2810 NEXTI
2820 IFMID$(A$,1,4)<>"SAVE"THEN2832
2830 GOSUB4770
2832 IFMID$(A$,1,3)<>"MAP"THEN2840
2833 S2=S1
2834 GOSUB5190
2835 S1=S2
2836 GOTO2850
2840 IFMID$(A$,1,6)<>"REPORT"THEN2848
2842 GOSUB3180
2846 GOTO2850
2848 M$=MID$(A$,1,4)+" IS NOT A STAR NAME IN THIS GAME":GOSUB1
2850 PRINT"NEXT STAR";
2860 GOTO2770
2870 S(12+8,T1)=I
2880 IFI<>S1THEN2910
2890 PRINT "CHOOSE A DIFFERENT STAR SYSTEM TO VISIT"
2900 GOTO2850
2910 D2=SQR((S(11,S1)-S(11,I))^2+(S(12,S1)-S(12,I))^2)/R9
2920 D2=INT(D2)
2930 IFRND(0)>(Q/2)THEN3030
2940 I=1+INT(RND(0)*3)
2950 M$="":ONIGOTO3000,2980,2960
2960 M$="SHIP DOES NOT PASS INSPECTION"
2970 GOTO3010
2980 M$="CREWMEN DEMAND A VACATION"
2990 GOTO3010
3000 M$="LOCAL HOLIDAY SOON"
3010 M$=M$+" -"+STR$(I)+" WEEK DELAY.":GOSUB1
3020 D2=D2+7*I
3030 S(12+9,T1)=S(12+9,T1)+D2
3040 IFS(12+9,T1)<=360THEN3070
3050 S(12+9,T1)=S(12+9,T1)-360
3060 S(12+10,T1)=S(12+10,T1)+1
3070 M=INT((S(12+9,T1)-1)/30)
3080 L=3*M+1
3090 M$="THE ETA AT "+MID$(S$,J,4)+" IS "+MID$(C$,L,3)+STR$(S(12+9,T1)-30*M)+","+MID$(STR$(S(12+10,T1),2):GOSUB1
3100 REM UPDATE ETA PLUS RANOM DELAY FACTOR (0,1,2 OR 3 WEEKS)
3110 I=(INT(RND(0)*3)+1)*(RND(0)>(Q/2))
3120 S(12+9,T1)=S(12+9,T1)+7*I
3130 IFS(12+9,T1)<=360THEN3160
3140 S(12+9,T1)=S(12+9,T1)-360
3150 S(12+10,T1)=S(12+10,T1)+1
3160 S(12+12,T1)=I
3170 RETURN
3180 REM *** <REPORT> GOSUB
3190 GOSUB4500
3200 M$="JAN 1,"+STR$(Y1)+"     YEARLY REPORT #"+STR$(Y1-2069):GOSUB1
3210 PRINT 
3220 PRINT 
3230 IFY1>2070THEN3450
3240 PRINT "STAR SYSTEM CLASSES:"
3250 PRINT "     I  COSMOPOLITAN"
3260 PRINT "    II  DEVELOPED"
3270 PRINT "   III  UNDERDEVELOPED"
3280 PRINT "    IV  FRONTIER"
3300 PRINT 
3310 PRINT "MERCHANDISE:"
3320 PRINT "    UR  URANIUM"
3330 PRINT "   MET  METALS"
3340 PRINT "    HE  HEAVY EQUIPMENT"
3350 PRINT "   MED  MEDICINE"
3360 PRINT "  SOFT  COMPUTER SOFTWARE"
3370 PRINT "  GEMS  STAR GEMS"
3390 PRINT 
3400 M$="EACH TRADING SHIP CAN CARRY MAX"+STR$(W)+" TONS CARGO.":GOSUB1
3410 M$="STAR GEMS AND COMPUTER SOFTWARE, WHICH AREN'T SOLD BY THE TON, DON'T COUNT.":GOSUB1
3430 PRINT 
3450 PRINT "CURRENT PRICES"
3470 PRINT 
3480 PRINT "NAME  CLASS";N$
3490 PRINT 
3500 FORS1=1TOS9
3510 GOSUB3870
3520 FORI=1TO6
3530 P(I)=SGN(S(I,S1))*P(I)
3540 NEXTI
3550 GOSUB4580
3560 PRINTS$(S(8,S1),S(8,S1)+3)
3570 PRINTD$,P(1),P(2),P(3),P(4),P(5),P(6)
3580 IFS1/2<>INT(S1/2)THEN3600
3590 PRINT
3600 NEXTS1
3610 PRINT 
3620 PRINT"('+' MEANS SELLING AND '-' MEANS BUYING)"
3640 PRINT 
3650 PRINT "CAPTAINS"
3670 PRINT 
3680 PRINT "NUMBER  $ ON SHIPS   $ IN BANK     CARGOES      TOTALS"
3690 FORB1=1TOP9
3700 GOSUB4380
3710 NEXTB1
3720 FORP1=1TOP9
3730 PRINT
3740 M1=M2=0
3750 FORI1=0TOT9/P9-1
3760 M1=M1+S(12+11,P9*I1+P1)
3770 FORK=1TO6
3780 M2=M2+S(12+K,P9*I1+P1)*Q(K)
3790 NEXTK
3800 NEXTI1
3810 M3=M2+M1+S(24+1,P1)
3820 PRINTP1,M1,S(24+1,P1),M2,M3
3830 REM IMAGE  2X,2D,2X,4(2X,DDXDDDXDDD)
3840 NEXTP1
3850 RETURN
3860 REM *** <PRICES> GOSUB
3870 R1=1+ABS(S(7,S1)>=5)+ABS(S(7,S1)>=10)
3880 D2=12*(Y1-S(10,S1))+(D1-S(9,S1))/30
3890 FORI=1TO6
3900 G(I)=(1+S(7,S1)/15)*(M(I,R1)*S(7,S1)+C(I,R1))
3910 IFABS(G(I))>.01THEN3940
3920 P(I)=0
3930 GOTO3970
3940 IFSGN(G(I))*(ABS(G(I)*12)<ABS(S(I,S1)+D2*G(I)))THENCC=SGN(G(I))*(ABS(G(I)*12):GOTO3942
3941 CC=ABS(S(I,S1)+D2*G(I)))
3942 S(I,S1)=CC
3950 P(I)=Q(I)*(1-SGN(S(I,S1))*ABS(S(I,S1)/(G(I)*X9)))
3960 P(I)=100*INT(P(I)/100+.5)
3970 NEXTI
3980 S(9,S1)=D1
3990 S(10,S1)=Y1
4000 RETURN
4010 REM  *** <BANK CALL> GOSUB
4020 PRINT "DO YOU WISH TO VISIT THE LOCAL BANK";
4030 INPUT A$
4040 IF A$(1,1)="Y" THEN 4060
4050 RETURN
4060 GOSUB 4310
4070 GOSUB 4380
4080 PRINT TAB(5);"YOU HAVE $";S(24+1,B1);" IN THE BANK"
4082 PRINT TAB(5);"AND $";S(12+11,T1);" ON YOUR SHIP"
4090 IF S(24+1,B1)=0 THEN 4190
4100 PRINT TAB(5);"HOW MUCH DO YOU WISH TO WITHDRAW";
4110 INPUT Z
4120 IF Z <= S(24+1,B1) THEN 4150
4130 PRINT TAB(5);"TOO MUCH; ";
4140 GOTO 4100
4150 IF Z <= 0 THEN 4190
4160 S(24+1,B1)=S(24+1,B1)-Z
4170 S(12+11,T1)=S(12+11,T1)+Z
4180 RETURN
4190 PRINT TAB(5);"HOW MUCH DO YOU WISH TO DEPOSIT";
4200 INPUT Z
4210 IF Z >= 0 THEN 4240
4220 PRINT TAB(5);"YOU CAN'T DEPOSIT A NEGATIVE NUMBER"
4230 GOTO 4190
4240 IF Z <= S(12+11,T1) THEN 4270
4250 PRINT TAB(5);"YOU HAVE $";S(12+11,T1);" ON YOUR SHIP"
4260 GOTO 4190
4270 S(12+11,T1)=S(12+11,T1)-Z
4280 S(24+1,B1)=S(24+1,B1)+Z
4290 RETURN
4300 REM *** <B1> GOSUB
4310 B1=T1
4320 FOR I=1 TO S9/P9
4330 IF B1 <= P9 THEN 4360
4340 B1=B1-P9
4350 NEXT I
4360 RETURN
4370 REM   ***<BANK UPDATE> GOSUB
4380 S(24+1,B1)=S(24+1,B1)*(1+.05*(Y1-S(24+3,B1)+(D1-S(24+2,B1))/360))
4390 S(24+2,B1)=D1
4400 S(24+3,B1)=Y1
4410 RETURN
4420 REM *** <INPUT> GOSUB
4430 INPUTX
4440 IFINT(X)=XANDX>=0THEN4480
4450 PRINT"TYPE A ZERO IF YOU WANT TO PASS THIS ONE,"
4460 PRINT"BUT NO NEGATIVES OR DECIMALS"
4470 GOTO 4430
4480 RETURN
4490 REM *** <GA> GOSUB
4500 PRINT 
4530 PRINT"  *** GENERAL ANNOUNCEMENT ***"
4540 PRINT 
4560 RETURN
4570 REM *** <D$> GOSUB
4580 GOTO S(7,S1)/5+1 OF 4650,4630,4610,4590
4590 D$="    I"
4600 RETURN
4610 D$="   II"
4620 RETURN
4630 D$="  III"
4640 RETURN
4650 D$="   IV"
4660 RETURN
4670 REM *** <STAR NAME> GOSUB
4680 IF S1>1 THEN 4710
4690 I=1
4700 GOTO 4750
4710 I=4*INT(14*RND(0))+5
4720 FOR J=2 TO S1-1
4730 IF I=S(8,J) THEN 4710
4740 NEXT J
4750 S(8,S1)=I
4760 RETURN
4770 REM *** <SAVE GAME ON TAPE> GOSUB
4780 PRINT "WHEN I TYPE '?' THIS IS WHAT YOU SHOULD DO:"
4790 PRINT 
4800 PRINT "  1.  PUSH THE 'ON' BUTTON ON THE TAPE PUNCHER"
4810 PRINT "  2.  PRESS THE 'HERE IS' KEY (UPPER RIGHT) 3 TIMES"
4820 PRINT "  3.  PUSH THE 'OFF' BUTTON ON THE TAPE PUNCHER"
4830 PRINT "  4.  TYPE ANY NUMBER"
4840 PRINT "  5.  PRESS THE 'RETURN' KEY"
4850 PRINT 
4860 PRINT "WHEN I TYPE '!!!' THAT MEANS I'LL START PUNCHING"
4870 PRINT "THE TAPE IN ABOUT 10 SECONDS, SO:"
4880 PRINT 
4890 PRINT "   ***   DON'T FORGET TO TURN THE PUNCHER BACK ON   ***"
4900 PRINT LIN(3)
4910 INPUT X
4920 PRINT "!!!"
4930 FORI=1TO10000
4940 X=X+1
4950 NEXTI
4960 PRINT T$;""
4970 PRINT W;",";D9;",";K9;",";X9;",";D1;",";Y1;""
4980 PRINT P9;",";T9;",";S9;",";Y9;",";T1;",";S1;""
4990 FOR J=1 TO S9
5000 FOR I=1 TO 9 STEP 4
5010 PRINT S(I,J);",";S(I+1,J);",";S(I+2,J);",";S(I+3,J);""
5040 NEXT I
5060 NEXT J
5070 FOR J=1 TO T9
5080 FOR I=1 TO 9 STEP 4
5090 PRINT S(12+I,J);",";S(12+I+1,J);",";S(12+I+2,J);",";S(12+I+3,J);""
5120 NEXT I
5140 NEXT J
5150 FOR I=1 TO P9
5160 PRINT S(24+1,I);",";S(24+2,I);",";S(24+3,I);""
5170 NEXT I
5172 FOR I=1 TO 50
5174 PRINT " ";
5176 NEXT I
5180 STOP
5190 REM MAP
5200 CLS0
5250 FORL1=15TO-15STEP-1
5260 IFL1<>0THEN5290
5270 PRINT@32*8,"1--1--1--1--1--*SOL--1--1--1--1";
5280 GOTO5340
5290 REM
5300 IFABS(L1)/3=INT(ABS(L1)/3)THEN5330
5310 PRINT@32*(8-INT(L1/2))+INT(26/3),"!";
5320 GOTO5340
5330 PRINT@32*(8-INT(L1/2))+INT(26/3),"-";
5340 Y=L1*10/3
5342 Y0=(L1+1)*10/3
5350 FORS1=2TOS9
5360 IFS(12,S1)>=Y0ORS(12,S1)<YTHEN5400
5370 X1=INT(26+S(11,S1)/2)
5380 PRINT@32*(8-INT(L1/2))+INT(X1/3),MID$(S$,S(8,S1),4);
5400 NEXTS1
5450 NEXTL1
5490 RETURN
5500 END
