0 CLS:CLEAR400:DIMCC,ZZ,M$,I$:MC=16384:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
8 M$=CHR$(91)+"SPACE"+CHR$(93):GOSUB7
9 ON-(INKEY$<>" ")GOTO9:RETURN
10 GOSUB15:CC=(C1*.5+(ABS(NOT(C1)))*X/(2*ABS(S(I1,S1))))/K1
15 C1=ABS(X>=ABS(S(I1,S1))):RETURN
20 DIMS(27,15),T$,W,D9,K9,X9,D1,X1,P9,T9,S9,Y9,H,Y1,R9,G9,Q,M(6,3),C(6,3),S1,T1,R
30 DIMC$,S$,P(6),Q(6),A$,D$,N$,G(6):R9=2/7:D9=15:Q=.1:K9=3:W=30:X9=36:G9=1.25:R=0:D1=1:Y1=2070
37 FORCC=1TO3:FORZZ=1TO6:READM(ZZ,CC):NEXT:NEXT:FORCC=1TO3:FORZZ=1TO6:READC(ZZ,CC):NEXT:NEXT
40 INPUT"INSTRUCTIONS (Y/N)";A$
41 IFMID$(A$,1,1)="N"THEN66
43 CLS:M$="THE DATE IS JAN 1, 2070 AND INTERSTELLAR FLIGHT HAS EXISTED FOR 70 YEARS. THERE ARE SEVERAL STAR "
45 M$=M$+"SYSTEMS THAT HAVE BEEN COLONIZED.":GOSUB1:
46 M$="SOME ARE ONLY FRONTIER SYSTEMS, OTHERS ARE OLDER AND MORE DEVELOPED.":GOSUB1
48 M$="EACH OF YOU IS THE CAPTAIN OF TWO INTERSTELLAR TRADING SHIPS.":GOSUB1:M$="YOU WILL TRAVEL FROM STAR "
50 M$=M$+"SYSTEM TO STAR SYSTEM, BUYING AND SELLING MERCHANDISE.":GOSUB1
51 M$="IF YOU DRIVE A GOOD BARGAIN YOU CAN MAKE LARGE PROFITS.":GOSUB1
52 GOSUB8
53 CLS:M$="AS TIME GOES ON, EACH STAR SYSTEM WILL SLOWLY GROW, AND ITS NEEDS WILL CHANGE.":GOSUB1
54 M$="A SYSTEM THAT IS SELLING URANIUM AND RAW METALS CHEAPLY NOW "
56 M$=M$+"MAY NOT HAVE ENOUGH FOR EXPORT IN A FEW YEARS.":GOSUB1
58 M$="YOUR SHIPS CAN TRAVEL ABOUT 2 LIGHTYEARS IN A WEEK AND CARRY UP TO"+STR$(W)+" TONS OF CARGO.":GOSUB1
60 M$=M$+"ONLY CLASS I AND II SYSTEMS HAVE BANKS ON THEM.":GOSUB1
61 M$="THEY PAY 5% INTEREST AND DEPOSITS ARE AVAILABLE ON ANY PLANET WITH A BANK.":GOSUB1
64 GOSUB8
66 CLS:M$="HAVE ALL PLAYERS PLAYED BEFORE?":GOSUB1
67 INPUTA$
69 IFMID$(A$,1,1)="Y"THEN71
70 GOTO75
71 M$="DO YOU WANT TO SET UP YOUR OWN GAME?":GOSUB1
72 INPUTA$
74 IFMID$(A$,1,1)="Y"THEN86
75 PRINT:PRINT"HOW MANY PLAYERS?"
76 INPUTP9
78 ONP9-1GOTO81,81,81
79 PRINT"2,3, OR 4 CAN PLAY"
80 GOTO75
81 T9=2*P9
82 S9=3*P9+1
83 Y9=Y1+5
84 GOTO159
86 PRINT"IS THIS A RESTART?"
87 INPUTA$
89 IFMID$(A$,1,1)="N"THEN111
90 PRINT:PRINT"READY TAPE ";GOSUB8:CLOAD*S,"TRADEDAT"
94 INPUTW,D9,K9,X9,D1,Y1,P9,T9,S9,Y9,T1,S1
100 REMT$
109 R=1
110 GOTO270
111 PRINT"HOW MANY PLAYERS"
112 INPUTP9
114 IFP9>=2ANDP9<=12THEN117
115 PRINT"2-12 CAN PLAY"
116 GOTO111
117 PRINT:PRINT"HOW MANY SHIPS PER PLAYER?"
118 INPUTX
120 IFX<1THEN117
121 T9=P9*X
122 IFT9<=12THEN125
123 M$="I CAN'T KEEP TRACK OF MORE THAN 12 SHIPS;"+STR$(P9)+" PLAYERS TIMES"+STR$(X)+" SHIPS MAKES"+STR$(T9):GOSUB1
124 GOTO117
125 PRINT:PRINT"HOW MANY STAR SYSTEMS?"
126 INPUTS9
128 IFS9>=4ANDS9<=13THEN131
129 PRINT"FROM 4 TO 13 STARS"
130 GOTO125
131 M$="ENTER THE LENGTH OF GAME IN YEARS":GOSUB1
132 INPUTX
134 IFX>=1ANDINT(X)=XTHEN137
135 PRINT:PRINT"CHOOSE A POSITIVE INTEGER"
136 GOTO132
137 Y9=Y1+X
138 PRINT:M$="WHAT'S THE MAX CARGO TONNAGE (USUALLY 30)?":GOSUB1
139 INPUTW
141 IFW<25THEN138
142 PRINT:M$="WHAT'S THE MINIMUM DISTANCE BETWEEN STARS (USUALLY 15)?":GOSUB1
143 INPUTD9
145 IFD9<=25ANDD9>=10THEN148
146 PRINT:PRINT"MIN SPACING 10, MAX 25"
147 GOTO142
148 PRINT:PRINT"HOW MANY BIDS OR OFFERS?"
149 INPUT"(USUALLY 3)";K9
151 IFK9<1THEN148
152 PRINT:M$="SET THE PROFIT MARGIN (1-5)... THE HIGHER THE NUMBER, THE LOWER THE PROFIT %... USUALLY SET TO 2":GOSUB1
154 INPUT"...YOUR NUMBER";X9
155 IFABS(X9)<5THENCC=ABS(X9):GOTO157
156 CC=5
157 X9=18*CC
159 S(11,1)=S(12,1)=0
160 S(7,1)=15
162 H=1
163 S1=2
164 GOSUB217
165 S1=3
166 GOSUB217
167 S1=4
168 GOSUB226
169 FORS1=5TOS9
170 ONS1-3*INT((S1-1)/3)GOSUB217,226,231
171 NEXTS1
173 FORS1=1TOS9
174 FORJ=1TO6
175 S(J,S1)=0
176 NEXTJ
177 IFS1>1THEN180
178 I=1
179 GOTO184
180 I=4*INT(14*RND(0))+5
181 FORJ=2TOS1-1
182 IFI=S(8,J)THEN180
183 NEXTJ
184 S(8,S1)=I
185 S(9,S1)=270
186 S(10,S1)=Y1-1
187 NEXTS1
189 T1=1:L=1
190 PRINT:M$="CAPTAINS, NAME YOUR SHIPS (UP TO 6 CHARACTERS)":GOSUB1
192 FORI=1TOT9/P9
194 FORP1=1TOP9:S(13,T1)=S(14,T1)=S(18,T1)=0:S(15,T1)=15:S(16,T1)=S(17,T1)=10:S(19,T1)=25
199 S(21,T1)=D1
200 S(22,T1)=Y1
201 S(23,T1)=5000
202 M$="CAPTAIN"+STR$(P1)+" WHAT DO YOU CHRISTEN YOUR SHIP #"+STR$(I)+"?":GOSUB1
203 INPUTM$:T$=T$+MID$(M$+"      ",1,6):T1=T1+1:L=L+6:NEXTP1:NEXTI
204 T$=LEFT$(T$+"                                                                        ",72)
209 FORB1=1TOP9:S(24+1,B1)=0:S(24+2,B1)=D1:S(24+3,B1)=Y1:NEXTB1
214 GOTO270
217 X=(RND(0)-.5)*100
218 Y=50*RND(0)
219 IF(ABS(X)<25)AND(Y<25)THEN217
220 F=1:GOSUB244:IFF=0THEN217
223 S(7,S1)=0:RETURN
226 E=100
227 GOSUB236
228 S(7,S1)=5
229 RETURN
231 E=50
232 GOSUB236
233 S(7,S1)=10
234 RETURN
236 X=(RND(0)-.5)*E
237 Y=RND(0)*E/2
238 F=1
239 GOSUB244
240 IFF=0THEN236
241 RETURN
244 ONHGOTO255,251,249,245
245 Z=X:X=-Y:Y=Z:GOTO255
249 Y=-Y:GOTO255
251 Z=X:X=Y:Y=Z
255 FORJ=1TOS1-1:IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN259
257 F=0:RETURN
259 NEXTJ:S(11,S1)=INT(X):S(12,S1)=INT(Y):H=1+ABS(H<=3)*H:RETURN
267 DATA-0.025,-0.05,-0.025,,-0.025,-0.025,,.1,.1,-0.025,.1,0,.1,.2,.1,.1,-0.025,0
268 DATA1,1.5,.5,.75,.75,.75,-0.25,-0.25,-0.25,,-0.5,.5,,-0.5,0,.5,1.5,0
269 END
270 C$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"
271 S$="SOL YORKBOYDIVANREEFHOOKSTANTASKSINKSANDQUINGAOLKIRKKRISFATE"
275 Q(1)=5000:Q(2)=3500:Q(3)=4000:Q(4)=4500:Q(5)=3000:Q(6)=3000
276 N$="UR   MET  HE   MED  SOFT GEMS   "
400 IFR=0THEN430
404 GOSUB3870
406 GOSUB5190
408 FORT2=1TOT9
410 IFT2=T1THEN420
412 L=(T2-1)*6+1
414 M$=MID$(T$,L,6)+" IS ENROUTE TO "+MID$(S$,S(20,T2),4):GOSUB1
420 NEXTT2
424 L=(T1-1)*6+1
426 M$=" AND "+MID$(T$,L,6)+" IS ABOUT TO LEAVE "+MID$(S$,S(20,T1),4):GOSUB1
428 GOTO2040
430 GOSUB5190
432 GOSUB3190
434 S1=1:T1=1:L1=1
440 PRINT"ALL SHIPS START AT SOL."
470 M$="SOL AND THE CLASS II STARS PRODUCE ALOT OF HE, MED AND SOFT, WHICH THE POORER SYSTEMS (CLASS III AND"
490 M$=M$+" IV) NEED.":GOSUB1:M$="POOR STARS PRODUCE RAW GOODS - "
500 M$=M$+"UR, MET, GEMS THAT YOU CAN BRING BACK TO SOL AND CLASS II SYSTEMS FOR TRADE.":GOSUB1
530 M$="STUDY THE MAP AND CURRENT PRICE CHARTS CAREFULLY - "
540 M$=M$+"CLASS I AND II STARS MAKE EXCELLENT TRADING PARTNERS WITH CLASS III OR IV STARS.":GOSUB1:GOSUB8:CLS
560 FORI1=1TOT9/P9
570 FORP1=1TOP9
590 PRINT:PRINT"PLAYER"+STR$(P1)+", WHICH STAR WILL ";MID$(T$,L1,6);"TRAVEL TO";
600 GOSUB2770
610 L1=L1+6
620 T1=T1+1
630 NEXTP1
640 NEXTI1
660 D=S(21,1)
670 Y=S(22,1)
680 T1=1
690 FORI=2TOT9
700 IFS(22,I)<YTHEN740
710 IFS(22,I)>YTHEN770
720 IFS(21,I)>DTHEN770
730 IFS(21,I)=DANDRND(0)>.5THEN770
740 D=S(21,I)
750 Y=S(22,I)
760 T1=I
770 NEXTI
780 IFY1=YTHEN900
790 D1=1
800 Y1=Y
810 T2=T1
820 GOSUB3190
822 IFY1<>2071THEN830
824 GOSUB4500:M$="THE LAST YEAR OF THIS GAME IS"+STR$(Y9)+" BUT IF YOU WANT TO QUIT BEFORE THEN, "
829 M$=M$+"YOU CAN TYPE 'SAVE' AS YOUR NEXT PORT OF CALL SO YOU CAN CONTINUE THE GAME LATER":GOSUB1:GOSUB8
831 T1=T2
840 IFY1<Y9THEN900
850 GOSUB4500:INPUT"NEW GAME";A$
880 IFMID$(A$,1,1)="N"THEN5500
890 RUN
900 D1=D
910 M=INT((D1-1)/30)
920 L=3*M+1
930 PRINT 
940 PRINT 
950 PRINT"*****************"
960 M$="* "+MID$(C$,L,3)+STR$(D1-30*M)+","+STR$(Y1):GOSUB1
970 L=(T1-1)*6+1
980 S1=S(20,T1)
990 M=S(8,S1)
1000 M$="* "+MID$(T$,L,6)+" HAS LANDED ON "+MID$(S$,M,4):GOSUB1
1010 ONS(24,T1)+1GOTO1080,1060,1040,1020
1020 M$="3 WEEKS LATE - PIRATES ATTACKED MIDVOYAGE":GOSUB1
1030 GOTO1080
1040 M$="2 WEEKS LATE - 'WE GOT LOST. SORRY'":GOSUB1
1050 GOTO1080
1060 M$="1 WEEK LATE - 'OUR COMPUTER MADE A MISTAKE'":GOSUB1
1080 PRINT 
1090 PRINT"$ ON BOARD:"S(23,T1),"  NET WT"S(19,T1):
1100 M$=N$:GOSUB7:PRINTMID$(STR$(S(13,T1))+"     ",1,5);MID$(STR$(S(14,T1))+"     ",1,5);MID$(STR$(S(15,T1))+"     ",1,5);
1110 PRINTMID$(STR$(S(16,T1))+"     ",1,5);MID$(STR$(S(17,T1))+"     ",1,5);MID$(STR$(S(18,T1))+"     ",1,5)
1130 GOSUB3870
1140 PRINT:PRINT"WE ARE BUYING:"
1160 J1=1
1170 FORI1=1TO6
1180 IFS(I1,S1)>=0ORS(12+I1,T1)<.5THEN1480
1190 M$=MID$(N$,J1,5):GOSUB5:PRINT" WE NEED ";-INT(S(I1,S1));" UNITS."
1200 PRINT"HOW MANY ARE YOU SELLING";
1210 GOSUB4430
1220 IFX=0THEN1480
1230 IFX<=S(12+I1,T1)THEN1270
1240 PRINT"YOU ONLY HAVE ";S(12+I1,T1);" UNITS"
1260 GOTO1200
1270 IFX<=2*-INT(S(I1,S1))THEN1300
1280 X=2*-INT(S(I1,S1))
1290 PRINT"WE'LL BID ON ";X;" UNITS."
1300 FORK1=1TOK9
1310 IFK9>2THENCC=K9:GOTO1312
1311 CC=2
1312 IFK1<>CCTHEN1340
1320 PRINT"OUR FINAL OFFER:";
1330 GOTO1350
1340 PRINT"WE OFFER ";
1342 Y2=(L1+1)*10/3
1350 PRINT100*INT(.009*P(I1)*X+.5);" WHAT DO YOU BID";
1360 INPUTY
1362 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1370
1364 PRINT"WATCH YOUR TYPING -- TRY AGAIN"
1366 GOTO1340
1370 IFY<=P(I1)*XTHEN1430
1380 GOSUB10:IFY>(1+CC)*P(I1)*XTHEN1410
1390 P(I1)=.8*P(I1)+.2*Y/X
1400 NEXTK1
1410 PRINT"WE'LL PASS THIS ONE"
1420 GOTO1480
1430 PRINT"WE'LL BUY!"
1440 S(12+I1,T1)=S(12+I1,T1)-X
1450 S(19,T1)=S(19,T1)-X*(I1<5)
1460 S(23,T1)=S(23,T1)+Y
1470 S(I1,S1)=S(I1,S1)+X
1480 J1=J1+5
1490 NEXTI1
1500 PRINT 
1520 PRINT"WE ARE SELLING:"
1530 J1=1
1540 FORI1=1TO6
1550 IFG(I1)<=0ORS(I1,S1)<1THEN1960
1555 IFI1<=4ANDS(19,T1)>=WTHEN1960
1560 M$=MID$(N$,J1,5)+" UP TO "+STR$(INT(S(I1,S1)))+" UNITS.":GOSUB1
1570 PRINT"HOW MANY ARE YOU BUYING";
1580 GOSUB4430
1590 IFX=0THEN1960
1600 IFI1>4ORX+S(19,T1)<=WTHEN1660
1610 M$="YOU HAVE "+STR$(S(19,T1))+" TONS ABOARD.":GOSUB1
1620 M$="SO "+STR$(X)+" TONS PUTS YOU OVER THE"+STR$(W)+" TON LIMIT.":GOSUB1
1650 GOTO1570
1660 IFX<=S(I1,S1)THEN1700
1670 PRINT"WE ONLY HAVE ";INT(S(I1,S1));" UNITS."
1690 GOTO1570
1700 FORK1=1TOK9
1710 IFK9>2THENCC=K1:GOTO1712
1711 CC=2
1712 IFK1>CCTHEN1740
1720 PRINT"OUR FINAL OFFER:";
1730 GOTO1750
1740 PRINT"WE WANT ABOUT ";
1750 PRINT100*INT(.011*P(I1)*X+.5)
1760 INPUT"YOUR OFFER";Y
1772 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1780
1774 PRINT"WATCH YOUR TYPING -- TRY AGAIN"
1776 GOTO1740
1780 IFY>=P(I1)*XTHEN1840
1790 GOSUB10:IFY<(1-CC)*P(I1)*XTHEN1820
1800 P(I1)=.8*P(I1)+.2*Y/X
1810 NEXTK1
1820 PRINT"THAT'S TOO LOW"
1830 GOTO1960
1840 IFY<=S(23,T1)THEN1910
1850 M$="YOU BID $"+STR$(Y)+" BUT YOU HAVE ONLY $"+STR$(S(23,T1)):GOSUB1
1860 GOSUB4310
1870 IFS(7,S1)<10ORS(23,T1)+S(24+1,B1)<YTHEN1820
1890 GOSUB4020
1900 IFY>S(23,T1)THEN1820
1910 PRINT"SOLD!"
1920 S(12+I1,T1)=S(12+I1,T1)+X
1930 S(19,T1)=S(19,T1)+X*(I1<5)
1940 S(I1,S1)=S(I1,S1)-X
1950 S(23,T1)=S(23,T1)-Y
1960 J1=J1+5
1970 NEXTI1
1990 GOSUB4310
2000 IFS(7,S1)<10ORS(23,T1)+S(24+1,B1)=0THEN2040
2010 PRINT 
2020 GOSUB4020
2030 PRINT 
2040 M$="WHAT IS YOUR NEXT PORT OF CALL?":GOSUB1
2050 GOSUB2770
2070 J=0
2080 FORI=1TO6
2090 IFS(I,S1)>=0THEN2120
2100 IFS(I,S1)<G(I)THEN660
2110 J=J+1
2120 NEXTI
2130 IFJ>1THEN660
2150 S(7,S1)=S(7,S1)+G9
2160 G0=S(7,S1)
2162 IFG0<>5ANDG0<>10ANDG0<>15THEN2220
2170 GOSUB4580:GOSUB4500:M$="STAR SYSTEM "+MID$(S$,S(8,S1),4)+" IS NOW A CLASS "+D$+" SYSTEM":GOSUB1:GOSUB8
2220 IFS9=15THEN660
2230 J=0:FORI=1TOS9:J=J+S(7,I):NEXTI
2270 IFJ/S9<10THEN660
2290 S9=S9+1:S1=S9
2300 GOSUB4680:GOSUB2450
2320 S(9,S1)=D1:S(10,S1)=Y1:FORJ=1TO6:S(J,S1)=0:NEXTJ
2370 GOSUB4500:M$="A NEW STAR SYSTEM HAS BEEN DISCOVERED! IT IS A CLASS IV "
2390 M$=M$+"AND ITS NAME IS"+MID$(S$,S(8,S1),4):GOSUB1:GOSUB8
2400 GOSUB5190:GOTO660
2420 END
2450 X=(RND(0)-.5)*100
2460 Y=50*RND(0)
2470 IF(ABS(X)<25)AND(Y<25)THEN2450
2480 F=1
2490 GOSUB2550
2500 IFF=0THEN2450
2510 S(7,S1)=0
2520 RETURN
2550 ONHGOTO2660,2620,2600,2560
2560 Z=X:X=-Y:Y=Z
2590 GOTO2660
2600 Y=-Y
2610 GOTO2660
2620 Z=X:X=Y:Y=Z
2660 FORJ=1TOS1-1
2670 IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN2700
2680 F=0:RETURN
2700 NEXTJ
2720 S(11,S1)=INT(X):S(12,S1)=INT(Y):H=1+ABS(H<=3)*H:RETURN
2770 INPUTA$:A$=A$+" "
2780 FORI=1TOS9:J=S(8,I):IFMID$(A$,1,4)=MID$(S$,J,4)THEN2870
2810 NEXTI
2820 IFMID$(A$,1,4)<>"SAVE"THEN2832
2830 GOSUB4770
2832 IFMID$(A$,1,3)<>"MAP"THEN2840
2833 S2=S1
2834 GOSUB5190
2835 S1=S2
2836 GOTO2850
2840 IFMID$(A$,1,6)<>"REPORT"THEN2848
2842 GOSUB3190
2846 GOTO2850
2848 M$=MID$(A$,1,4)+" IS NOT A STAR NAME IN THIS GAME":GOSUB1
2850 PRINT"NEXT STAR";:GOTO2770
2870 S(20,T1)=I
2880 IFI<>S1THEN2910
2890 M$="CHOOSE A DIFFERENT STAR SYSTEM TO VISIT":GOSUB1
2900 GOTO2850
2910 D2=SQR((S(11,S1)-S(11,I))^2+(S(12,S1)-S(12,I))^2)/R9
2920 D2=INT(D2)
2930 IFRND(0)>(Q/2)THEN3030
2940 I=1+INT(RND(0)*3)
2950 M$="":ONIGOTO3000,2980,2960
2960 M$="SHIP DOES NOT PASS INSPECTION"
2970 GOTO3010
2980 M$="CREWMEN DEMAND A VACATION"
2990 GOTO3010
3000 M$="LOCAL HOLIDAY SOON"
3010 M$=M$+" -"+STR$(I)+" WEEK DELAY.":GOSUB1
3020 D2=D2+7*I
3030 S(21,T1)=S(21,T1)+D2
3040 IFS(21,T1)<=360THEN3070
3050 S(21,T1)=S(21,T1)-360
3060 S(22,T1)=S(22,T1)+1
3070 M=INT((S(21,T1)-1)/30)
3080 L=3*M+1
3090 M$="THE ETA AT "+MID$(S$,J,4)+" IS "+MID$(C$,L,3)+STR$(S(21,T1)-30*M)+","+STR$(S(22,T1)):GOSUB1
3110 I=(INT(RND(0)*3)+1)*ABS(RND(0)>(Q/2))
3120 S(21,T1)=S(21,T1)+7*I
3130 IFS(21,T1)<=360THEN3160
3140 S(21,T1)=S(21,T1)-360
3150 S(22,T1)=S(22,T1)+1
3160 S(24,T1)=I:RETURN
3180 PRINT"('+'=SELLING AND '-'=BUYING)":GOSUB8:CLS:RETURN
3190 GOSUB4500:M$="JAN 1,"+STR$(Y1)+"  YEARLY REPORT #"+STR$(Y1-2069):GOSUB1
3230 IFY1>2070THEN3450
3240 PRINT"STAR SYSTEM CLASSES:":PRINT"     I  COSMOPOLITAN":PRINT"    II  DEVELOPED"
3270 PRINT"   III  UNDERDEVELOPED":PRINT"    IV  FRONTIER":PRINT
3310 PRINT"MERCHANDISE:":PRINT"    UR  URANIUM":PRINT"   MET  METALS":PRINT"    HE  HEAVY EQUIPMENT"
3350 PRINT"   MED  MEDICINE":PRINT"  SOFT  COMPUTER SOFTWARE":PRINT"  GEMS  STAR GEMS":GOSUB8:CLS
3400 M$="EACH TRADING SHIP CAN CARRY MAX"+STR$(W)+" TONS CARGO.":GOSUB1
3410 M$="STAR GEMS AND COMPUTER SOFTWARE, WHICH AREN'T SOLD BY THE TON, DON'T COUNT.":GOSUB1
3450 PRINT"CURRENT PRICES"
3500 FORS1=1TOS9:IFS1=4ORS1=9THENGOSUB3180
3510 IFS1=1ORS1=4ORS1=9THENM$="NAME             CLASS          ":GOSUB7:M$=N$:GOSUB7
3511 GOSUB3870:FORI=1TO6:P(I)=SGN(S(I,S1))*P(I):NEXTI
3550 GOSUB4580
3560 PRINTMID$(S$,S(8,S1),4),D$
3570 PRINTMID$(STR$(P(1))+"     ",1,5);MID$(STR$(P(2))+"     ",1,5);MID$(STR$(P(3))+"     ",1,5);
3571 PRINTMID$(STR$(P(4))+"     ",1,5);MID$(STR$(P(5))+"     ",1,5);MID$(STR$(P(6))+"     ",1,5)
3580 IFS1/2<>INT(S1/2)THEN3600
3600 NEXTS1:GOSUB3180
3650 M$="CAPTAINS                        ":GOSUB7
3680 M$="SHIP$   BANK$   CARGOS  TOTALS  ":GOSUB7
3690 FORB1=1TOP9
3700 GOSUB4380
3710 NEXTB1
3720 FORP1=1TOP9
3740 M1=0:M2=0
3750 FORI1=0TOT9/P9-1
3760 M1=M1+S(23,P9*I1+P1)
3770 FORK=1TO6
3780 M2=M2+S(12+K,P9*I1+P1)*Q(K)
3790 NEXTK:NEXTI1
3810 M3=M2+M1+S(24+1,P1):PRINT"CAPTAIN";P1
3821 PRINTMID$(STR$(M1)+"        ",1,8);MID$(STR$(S(24+1,P1))+"        ",1,8);
3822 PRINTMID$(STR$(M2)+"        ",1,8);MID$(STR$(M3)+"        ",1,8);
3840 NEXTP1:GOSUB8:CLS:RETURN
3870 R1=1+ABS(S(7,S1)>=5)+ABS(S(7,S1)>=10)
3880 D2=12*(Y1-S(10,S1))+(D1-S(9,S1))/30
3890 FORI=1TO6
3900 G(I)=(1+S(7,S1)/15)*(M(I,R1)*S(7,S1)+C(I,R1))
3910 IFABS(G(I))>.01THEN3940
3920 P(I)=0
3930 GOTO3970
3940 IFABS(G(I)*12)<ABS(S(I,S1)+D2*G(I))THENCC=ABS(G(I)*12):GOTO3942
3941 CC=ABS(S(I,S1)+D2*G(I))
3942 S(I,S1)=SGN(G(I))*CC
3950 P(I)=Q(I)*(1-SGN(S(I,S1))*ABS(S(I,S1)/(G(I)*X9)))
3960 P(I)=100*INT(P(I)/100+.5)
3970 NEXTI
3980 S(9,S1)=D1
3990 S(10,S1)=Y1
4000 RETURN
4020 M$="DO YOU WISH TO VISIT THE LOCAL BANK?":GOSUB1
4030 INPUTA$:IFMID$(A$,1,1)="Y"THEN4060
4050 RETURN
4060 GOSUB4310
4070 GOSUB4380
4080 M$="YOU HAVE $"+STR$(S(25,B1))+" IN THE BANK AND $"+STR$(S(23,T1))+" ON YOUR SHIP.":GOSUB1
4090 IFS(24+1,B1)=0THEN4190
4100 M$="HOW MUCH DO YOU WISH TO WITHDRAW?":GOSUB1
4110 INPUTZ
4120 IFZ<=S(25,B1)THEN4150
4130 PRINT"TOO MUCH; ";
4140 GOTO4100
4150 IFZ<=0THEN4190
4160 S(24+1,B1)=S(24+1,B1)-Z
4170 S(23,T1)=S(23,T1)+Z
4180 RETURN
4190 M$="HOW MUCH DO YOU WISH TO DEPOSIT?":GOSUB1
4200 INPUTZ
4210 IFZ>=0THEN4240
4220 M$="YOU CAN'T DEPOSIT A NEGATIVE NUMBER":GOSUB1
4230 GOTO4190
4240 IFZ<=S(23,T1)THEN4270
4250 PRINT"YOU HAVE $";S(23,T1);" ON YOUR SHIP"
4260 GOTO4190
4270 S(23,T1)=S(23,T1)-Z
4280 S(25,B1)=S(25,B1)+Z
4290 RETURN
4310 B1=T1
4320 FORI=1TOS9/P9:IFB1<=P9THEN4360
4340 B1=B1-P9:NEXTI
4360 RETURN
4380 S(25,B1)=S(25,B1)*(1+.05*(Y1-S(27,B1)+(D1-S(26,B1))/360)):S(26,B1)=D1:S(27,B1)=Y1:RETURN
4430 INPUTX
4440 IFINT(X)=XANDX>=0THENRETURN
4450 M$="TYPE A ZERO IF YOU WANT TO PASS THIS ONE, BUT NO NEGATIVES OR DECIMALS.":GOSUB1
4470 GOTO4430
4500 M$="  *** GENERAL ANNOUNCEMENT ***  ":GOSUB7:RETURN
4580 ONS(7,S1)/5+1GOTO4650,4630,4610,4590
4590 D$="    I":RETURN
4610 D$="   II":RETURN
4630 D$="  III":RETURN
4650 D$="   IV":RETURN
4680 IFS1>1THEN4710
4690 I=1
4700 GOTO4750
4710 I=4*INT(14*RND(0))+5
4720 FORJ=2TOS1-1
4730 IFI=S(8,J)THEN4710
4740 NEXTJ
4750 S(8,S1)=I
4760 RETURN
4870 PRINT"READY TAPE ":GOSUB8:PRINT"SAVING"
4930 C1=1:FORZZ=13TO15:FORCC=13TO24:M$=MID$(T$,C1,1):A$=MID$(T$,C1+1,1)
4931 S(CC,ZZ)=VAL(STR$(ASC(M$))+MID$(STR$(ASC(A$)),2,2)):C1=C1+2:NEXT:NEXT

4970 PRINTW;",";D9;",";K9;",";X9;",";D1;",";Y1;""
4980 PRINTP9;",";T9;",";S9;",";Y9;",";T1;",";S1;""
5180 CSAVE*S,"TRADERDAT":END
5190 REM MAP
5200 CLS0:PRINT@32*8,;:M$="+--+--+--+--+--+--+--+--+--+--+":GOSUB7
5250 FORZZ=15TO-15STEP-2:IFABS(ZZ)/3=INT(ABS(ZZ)/3)THEN5330
5310 PRINT@32*INT(8-ZZ/2)+16,;:M$="!":GOSUB7:GOTO5340
5330 PRINT@32*INT(8-ZZ/2)+16,;:M$="+":GOSUB7
5340 NEXT:PRINT@32*8+15,"SOL";
5350 FORZZ=15TO-15STEP-1:Y=ZZ*10/3:Y0=(ZZ+1)*10/3:FORS1=2TOS9:IFS(12,S1)>=Y0ORS(12,S1)<YTHEN5400
5370 X1=INT(26+S(11,S1)/2)
5380 PRINT@32*INT(8-ZZ/2)+INT(X1/2),MID$(S$,S(8,S1),4);
5400 NEXT:NEXT
5410 M$=INKEY$:IFM$=""THEN5410
5490 CLS:RETURN
5500 END

9000 T$="":FORZZ=13TO15:FORCC=13TO24:M$=STR$(S(CC,ZZ)):T$=T$+CHR$(VAL(MID$(M$,2,2)))+CHR$(VAL(MID$(M$,3,2))):NEXT:NEXT
