0 CLS:CLEAR400:DIMCC,ZZ,M$,I$:G=16383:O=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))+G:FORO=CC-LEN(M$)+1TOCC:POKEO,PEEK(O)-64:NEXT:RETURN
8 M$=CHR$(91)+"HIT SPACE"+CHR$(93):GOSUB7
9 ON-(INKEY$<>" ")GOTO9:FORCC=1TO11:PRINTCHR$(8);:NEXT:RETURN
10 O=ABS(X>=ABS(S(I1,S1))):CC=(O*.5+(ABS(NOT(O)))*X/(2*ABS(S(I1,S1))))/K1:RETURN
11 PRINTTAB(20):FORO=1TO12:PRINTCHR$(ASC(MID$(M$,O))+ABS(8-S1)*16);:NEXT:RETURN
12 PRINT"WATCH YOUR TYPING-- TRY AGAIN":RETURN
16 FORCC=1TO6:IFN(CC)>-10000ANDN(CC)<10000THENPRINTLEFT$(STR$(INT(N(CC)))+"     ",5);:NEXT:PRINT:RETURN
17 PRINTLEFT$(STR$(N(CC)/1000)+"     ",5);:NEXT:PRINT:RETURN
20 DIMS(27,15),T$,W,D9,K9,X9,D1,X1,P9,T9,S9,Y9,H,Y1,R9,G9,Q,M(6,3),C(6,3),S1,T1,R,N(6)
30 DIMC$,S$,P(6),Q(6),A$,D$,N$,G(6):R9=2/7:D9=15:Q=.1:K9=3:W=30:X9=36:G9=1.25:R=0:D1=1:Y1=2070
37 FORZZ=1TO6:FORCC=1TO3:READM(ZZ,CC):NEXT:NEXT:FORZZ=1TO6:FORCC=1TO3:READC(ZZ,CC):NEXT:NEXT
40 PRINT@234,;:M$="STAR TRADERS":GOSUB7:PRINT@297,"BY DAVE KAUFMAN":PRINT@454,;:INPUT"INSTRUCTIONS (Y/N)";A$
41 IFMID$(A$,1,1)="N"THEN66
43 CLS:M$="THE DATE IS JAN 1, 2070 AND INTERSTELLAR FLIGHT HAS EXISTED FOR 70 YEARS. THERE ARE SEVERAL STAR "
45 M$=M$+"SYSTEMS THAT HAVE BEEN COLONIZED.":GOSUB1
46 M$="SOME ARE ONLY FRONTIER SYSTEMS, OTHERS ARE OLDER AND MORE DEVELOPED.":GOSUB1
48 M$="EACH OF YOU IS THE CAPTAIN OF TWO INTERSTELLAR TRADING SHIPS.":GOSUB1:M$="YOU WILL TRAVEL FROM STAR "
50 M$=M$+"SYSTEM TO STAR SYSTEM, BUYING AND SELLING MERCHANDISE.":GOSUB1
51 M$="IF YOU DRIVE A GOOD BARGAIN YOU CAN MAKE LARGE PROFITS.":GOSUB1
52 GOSUB8
53 CLS:M$="AS TIME GOES ON, EACH STAR SYSTEM WILL SLOWLY GROW, AND ITS NEEDS WILL CHANGE.":GOSUB1
54 M$="A SYSTEM THAT IS SELLING URANIUM AND RAW METALS CHEAPLY NOW "
56 M$=M$+"MAY NOT HAVE ENOUGH FOR EXPORT IN A FEW YEARS.":GOSUB1
58 M$="YOUR SHIPS CAN TRAVEL ABOUT 2 LIGHTYEARS IN A WEEK AND CARRY UP TO"+STR$(W)+" TONS OF CARGO.":GOSUB1
60 M$=M$+"ONLY CLASS I AND II SYSTEMS HAVE BANKS ON THEM.":GOSUB1
61 M$="THEY PAY 5% INTEREST AND DEPOSITS ARE AVAILABLE ON ANY PLANET WITH A BANK.":GOSUB1
64 GOSUB8
66 CLS:INPUT"HAVE ALL PLAYERS PLAYED BEFORE  ";A$:IFLEFT$(A$,1)="Y"THEN71
70 GOTO75
71 PRINT:INPUT"SET UP YOUR OWN GAME";A$:IFLEFT$(A$,1)="Y"THEN111
73 GOTO86
75 PRINT:INPUT"HOW MANY PLAYERS";P9
78 ONP9-1GOTO81,81,81
79 PRINT"2,3, OR 4 CAN PLAY"
80 GOTO75
81 T9=2*P9:S9=3*P9+1:Y9=Y1+5:GOTO160
86 PRINT:INPUT"IS THIS A RESTART";A$:IFLEFT$(A$,1)="N"THEN75
90 PRINT:PRINT"READY TAPE ":CLOAD*S,"STRADATA"
91 T$="":FORZZ=13TO15:FORCC=1TO24:T$=T$+CHR$(S(CC,ZZ)):NEXT:NEXT
92 W=S(1,0):D9=S(2,0):K9=S(3,0):X9=S(4,0):D1=S(5,0):Y1=S(6,0)
93 P9=S(7,0):T9=S(8,0):S9=S(9,0):Y9=S(10,0):T1=S(11,0):S1=S(12,0):R=1:GOTO270
111 PRINT:INPUT"HOW MANY PLAYERS";P9
114 IFP9>=2ANDP9<=12THEN117
115 PRINT"2 TO 12 CAN PLAY"
116 GOTO111
117 PRINT:INPUT"HOW MANY SHIPS PER PLAYER";X
120 IFX<1THEN117
121 T9=P9*X
122 IFT9<=12THEN125
123 M$="I CAN'T KEEP TRACK OF MORE THAN 12 SHIPS;"+STR$(P9)+" PLAYERS TIMES"+STR$(X)+" SHIPS MAKES"+STR$(T9):GOSUB1
124 GOTO117
125 PRINT:INPUT"HOW MANY STAR SYSTEMS";S9
128 IFS9>=4ANDS9<=13THEN131
129 PRINT"FROM 4 TO 13 STARS"
130 GOTO125
131 PRINT:INPUT"ENTER THE LENGTH OF GAME IN     YEARS";X:IFX>=1ANDINT(X)=XTHENY9=Y1+X:GOTO138
135 PRINT"CHOOSE A POSITIVE INTEGER":GOTO131
138 PRINT:INPUT"WHAT'S THE MAX CARGO TONNAGE    (USUALLY 30)";W:IFW<25THEN138
142 PRINT:INPUT"WHAT'S THE MINIMUM DISTANCE     BETWEEN STARS (USUALLY 15)";D9:IFD9<=25ANDD9>=10THEN148
146 PRINT"MIN SPACING 10, MAX 25":GOTO142
148 PRINT:INPUT"HOW MANY BIDS OR OFFERS?        (USUALLY 3)";K9:IFK9<1THEN148
152 PRINT:PRINT"SET THE PROFIT MARGIN (1-5)...  THE HIGHER THE NUMBER, THE LOWERTHE PROFIT % ";
154 INPUT"(USUALLY 2)";X9:IFX9<1ORX9>5THEN152
156 X9=18*CC
160 S(11,1)=0:S(12,1)=0:S(7,1)=15:H=1:S1=2:GOSUB217:S1=3:GOSUB217:S1=4:GOSUB226:FORS1=5TOS9
170 ONS1-3*INT((S1-1)/3)GOSUB217,226,231:NEXTS1
174 FORS1=1TOS9:FORJ=1TO6:S(J,S1)=0:NEXTJ:IFS1=1THENI=1:GOTO184
180 I=4*INT(14*RND(0))+5:FORJ=2TOS1-1:IFI=S(8,J)THEN180
183 NEXTJ
184 S(8,S1)=I:S(9,S1)=270:S(10,S1)=Y1-1:NEXTS1:T1=1:L=1
190 PRINT:PRINT"CAPTAINS, NAME YOUR SHIPS","(UP TO 6 CHARACTERS) "CHR$(95)
192 FORI=1TOT9/P9:FORP1=1TOP9:S(13,T1)=0:S(14,T1)=0:S(18,T1)=0:S(15,T1)=15:S(16,T1)=10:S(17,T1)=10
200 S(19,T1)=25:S(21,T1)=D1:S(22,T1)=Y1:S(23,T1)=5000
202 M$=LEFT$("CAPTAIN"+STR$(P1)+" ",10):GOSUB7:PRINT" WHAT DO YOU CHRISTEN YOUR SHIP #"STR$(I);
203 INPUTM$:T$=T$+MID$(M$+"      ",1,6):T1=T1+1:L=L+6:NEXTP1:NEXTI
204 T$=LEFT$(T$+"                                                                        ",72)
209 FORB1=1TOP9:S(25,B1)=0:S(26,B1)=D1:S(27,B1)=Y1:NEXTB1
214 GOTO270
217 X=(RND(0)-.5)*100:Y=50*RND(0)
219 IFABS(X)<25ANDY<25THEN217
220 F=1:GOSUB244:IFF=0THEN217
223 S(7,S1)=0:RETURN
226 E=100:GOSUB236:S(7,S1)=5:RETURN
231 E=50:GOSUB236:S(7,S1)=10:RETURN
236 X=(RND(0)-.5)*E:Y=RND(0)*E/2:F=1:GOSUB244:IFF=0THEN236
241 RETURN
244 ONHGOTO255,251,249,245
245 Z=X:X=-Y:Y=Z:GOTO255
249 Y=-Y:GOTO255
251 Z=X:X=Y:Y=Z
255 FORJ=1TOS1-1:IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN259
257 F=0:RETURN
259 NEXTJ:S(11,S1)=INT(X):S(12,S1)=INT(Y):H=1+ABS(H<=3)*H:RETURN
267 DATA-.1,-.2,-.1,0,-.1,-.1,0,.1,.1,-.1,.1,0,.1,.2,.1,.1,-.1,0
268 DATA1,1.5,.5,.75,.75,.75,-.75,-.75,-.75,-.5,-1.5,.5,-1,-1.5
269 DATA-.5,.5,1.5,-.5
270 C$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"
271 S$="SOL YORKBOYDIVANREEFHOOKSTANTASKSINKSANDQUINGAOLKIRKKRISFATE"
275 Q(1)=5000:Q(2)=3500:Q(3)=4000:Q(4)=4500:Q(5)=3000:Q(6)=3000
276 N$="UR   MET  HE   MED  SOFT GEMS   "
400 IFR=0THEN430
404 GOSUB3870:GOSUB5190:FORT2=1TOT9:IFT2=T1THEN420
412 L=(T2-1)*6+1
414 M$=MID$(T$,L,6)+" IS ENROUTE TO "+MID$(S$,S(8,S(20,T2)),4):GOSUB1
420 NEXTT2:L=(T1-1)*6+1:M$="AND "+MID$(T$,L,6)+" IS ABOUT TO LEAVE "+MID$(S$,S(8,S(20,T1)),4):GOSUB1
428 GOTO2040
430 GOSUB5190:GOSUB3190:S1=1:T1=1:L1=1
440 PRINT"ALL SHIPS START AT SOL."
470 M$="SOL AND THE CLASS II STARS PRODUCE A LOT OF HE, MED AND SOFT, WHICH THE POORER SYSTEMS (CLASS III AND"
490 M$=M$+" IV) NEED.":GOSUB1:M$="POOR STARS PRODUCE RAW GOODS-- "
500 M$=M$+"UR, MET, GEMS THAT YOU CAN BRING BACK TO SOL AND CLASS II SYSTEMS FOR TRADE.":GOSUB1
510 M$="YOU MAY TYPE 'MAP', 'REPORT' OR 'SAVE' AT ANY LAUNCH PROMPT FOR INFORMATION "
520 M$=M$+"OR TO SAVE AND EXIT THE GAME. TYPE 'INV' AT ANY INITIAL TRADE OFFER FOR A SHIP'S MANIFEST.":GOSUB1:GOSUB8:CLS
560 PRINT@416,TAB(15)"":PRINTTAB(15)"Š€":PRINTTAB(15)"Šˆ€ˆ";:FORCC=1TO16:PRINT:SOUND1,1:SOUND100,1:NEXT:CLS
580 FORI1=1TOT9/P9:FORP1=1TOP9
590 PRINT:PRINT"PLAYER"STR$(P1)", WHICH STAR WILL ":M$=MID$(T$,L1,6):GOSUB7:PRINT" TRAVEL TO";
600 TT=T1:P2=P1:I2=I1:GOSUB2770:T1=TT:P1=P2:I1=I2:L1=L1+6:T1=T1+1:NEXTP1:NEXTI1
660 D=S(21,1):Y=S(22,1):T1=1:FORI=2TOT9
700 IFS(22,I)<YTHEN740
710 IFS(22,I)>YTHEN770
720 IFS(21,I)>DTHEN770
730 IFS(21,I)=DANDRND(0)>.5THEN770
740 D=S(21,I):Y=S(22,I):T1=I
770 NEXTI
780 IFY1=YTHEN900
790 D1=1:Y1=Y:T2=T1
820 GOSUB3190
822 IFY1<>2071THEN830
824 GOSUB4500:M$="THE LAST YEAR OF THIS GAME IS"+STR$(Y9)+" BUT IF YOU WANT TO QUIT BEFORE THEN, "
829 M$=M$+"YOU CAN TYPE 'SAVE' AS YOUR NEXT PORT OF CALL SO YOU CAN CONTINUE THE GAME LATER":GOSUB1:GOSUB8:PRINT
830 T1=T2:IFY1<Y9THEN900
850 PRINT:PRINTTAB(8)"*** GAME OVER ***":GOSUB3650:INPUT"NEW GAME";A$
880 IFMID$(A$,1,1)="N"THENEND
890 RUN
895 PRINT"$ ON BOARD:"INT(S(23,T1))TAB(20)"NET WT"INT(S(19,T1)):M$=N$:GOSUB7:FORCC=1TO6:N(CC)=S(12+CC,T1):NEXT:GOSUB16:RETURN
900 S1=S(20,T1):D1=D:M=INT((D1-1)/30):L=3*M+1:PRINT:M$="€€‡ƒ€€":GOSUB11:PRINT"********************";:M$="€Š€‹€"
960 GOSUB11:PRINT"* "MID$(C$,L,3);STR$(D1-30*M);",";STR$(Y1);:M$="€Šˆ€ˆŠ":GOSUB11:L=(T1-1)*6+1:M=S(8,S1)
1000 M$="* "+MID$(T$,L,6)+" HAS LANDED ON *"+MID$(S$,M,4)+"*":GOSUB1
1010 ONS(24,T1)+1GOTO1080,1060,1040,1020
1020 M$="3 WEEKS LATE-- PIRATES ATTACKED MID VOYAGE":GOSUB1
1030 GOTO1080
1040 M$="2 WEEKS LATE-- 'WE GOT LOST. SORRY'":GOSUB1
1050 GOTO1080
1060 M$="1 WEEK LATE-- 'OUR COMPUTER MADE A MISTAKE'":GOSUB1
1080 GOSUB895:GOSUB3870
1140 PRINT"WE ARE BUYING:":J1=1:FORI1=1TO6
1180 IFS(I1,S1)>=0ORS(12+I1,T1)<.5THEN1480
1190 M$=MID$(N$,J1,5):GOSUB7:PRINT" WE NEED"STR$(-INT(S(I1,S1)))" UNITS."
1200 PRINT"HOW MANY ARE YOU SELLING";
1210 GOSUB4430
1220 IFX=0THEN1480
1230 IFX<=S(12+I1,T1)THEN1270
1240 PRINT"YOU ONLY HAVE"S(12+I1,T1)"UNITS"
1260 GOTO1200
1270 IFX<=2*-INT(S(I1,S1))THEN1300
1280 X=2*-INT(S(I1,S1)):PRINT"WE'LL BID ON"X"UNITS."
1300 FORK1=1TOK9
1310 IFK9>2THENCC=K9:GOTO1312
1311 CC=2
1312 IFK1<>CCTHEN1340
1320 PRINT"OUR FINAL OFFER:";
1330 GOTO1350
1340 PRINT"WE OFFER";
1342 Y2=(L1+1)*10/3
1350 PRINT100*INT(9E-03*P(I1)*X+.5):INPUT"WHAT DO YOU BID";Y
1362 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1370
1364 GOSUB12
1366 GOTO1340
1370 IFY<=P(I1)*XTHEN1430
1380 GOSUB10:IFY>(1+CC)*P(I1)*XTHEN1410
1390 P(I1)=.8*P(I1)+.2*Y/X
1400 NEXTK1
1410 PRINT"WE'LL PASS ON THIS ONE":GOTO1480
1430 ONRND(3)GOTO1431,1432,1433
1431 PRINT"WE'LL BUY!":GOTO1440
1432 PRINT"WE'LL TAKE IT!":GOTO1440
1433 PRINT"YOU GOTTA DEAL!"
1440 S(12+I1,T1)=S(12+I1,T1)-X:S(19,T1)=S(19,T1)-X*ABS(I1<5):S(23,T1)=S(23,T1)+Y:S(I1,S1)=S(I1,S1)+X
1480 J1=J1+5:NEXTI1
1520 PRINT:PRINT"WE ARE SELLING:"
1530 J1=1:FORI1=1TO6:IFG(I1)<=0ORS(I1,S1)<1THEN1960
1555 IFI1<=4ANDS(19,T1)>=WTHEN1960
1560 M$=MID$(N$,J1,5):GOSUB7:PRINT" UP TO"STR$(INT(S(I1,S1)))" UNITS."
1570 PRINT"HOW MANY ARE YOU BUYING";
1580 GOSUB4430
1590 IFX=0THEN1960
1600 IFI1>4ORX+S(19,T1)<=WTHEN1660
1610 M$="YOU HAVE"+STR$(S(19,T1))+" TONS ABOARD.":GOSUB1
1620 M$="SO"+STR$(X)+" TONS PUTS YOU OVER THE"+STR$(W)+" TON LIMIT.":GOSUB1
1650 GOTO1570
1660 IFX<=S(I1,S1)THEN1700
1670 PRINT"WE ONLY HAVE"INT(S(I1,S1))" UNITS."
1690 GOTO1570
1700 FORK1=1TOK9:IFK9>2THENCC=K9:GOTO1710
1701 CC=2
1710 IFK1<>CCTHEN1740
1720 PRINT"OUR FINAL OFFER:";:GOTO1750
1740 PRINT"WE WANT ABOUT";
1750 PRINT100*INT(.011*P(I1)*X+.5)
1760 INPUT"YOUR OFFER";Y
1772 IFY>P(I1)*X/10ANDY<P(I1)*X*10THEN1780
1774 GOSUB12
1776 GOTO1740
1780 IFY>=P(I1)*XTHEN1840
1790 GOSUB10:IFY<(1-CC)*P(I1)*XTHEN1820
1800 P(I1)=.8*P(I1)+.2*Y/X
1810 NEXTK1
1820 PRINT"THAT'S TOO LOW"
1830 GOTO1960
1840 IFY<=S(23,T1)THEN1910
1850 M$="YOU BID $"+STR$(Y)+" BUT YOU HAVE ONLY $"+STR$(S(23,T1)):GOSUB1
1860 GOSUB4310
1870 IFS(7,S1)<10ORS(23,T1)+S(25,B1)<YTHEN1820
1890 GOSUB4020
1900 IFY>S(23,T1)THEN1820
1910 PRINT"SOLD!"
1920 S(12+I1,T1)=S(12+I1,T1)+X:S(19,T1)=S(19,T1)+X*ABS(I1<5):S(I1,S1)=S(I1,S1)-X:S(23,T1)=S(23,T1)-Y
1960 J1=J1+5:NEXTI1
1990 GOSUB4310
2000 IFS(7,S1)<10ORS(23,T1)+S(25,B1)=0THEN2040
2020 GOSUB4020
2040 PRINT:M$="WHAT IS YOUR NEXT PORT OF CALL?":GOSUB1
2050 GOSUB2770
2070 J=0
2080 FORI=1TO6
2090 IFS(I,S1)>=0THEN2120
2100 IFS(I,S1)<G(I)THEN660
2110 J=J+1
2120 NEXTI
2130 IFJ>1THEN660
2150 S(7,S1)=S(7,S1)+G9
2160 G0=S(7,S1):IFG0<>5ANDG0<>10ANDG0<>15THEN2220
2170 GOSUB4580:GOSUB4500:M$="STAR SYSTEM "+MID$(S$,S(8,S1),4)+" IS NOW A CLASS"+D$+" SYSTEM":GOSUB1:GOSUB8
2220 IFS9=15THEN660
2230 J=0:FORI=1TOS9:J=J+S(7,I):NEXTI
2270 IFJ/S9<10THEN660
2290 S9=S9+1:S1=S9
2300 GOSUB4680:GOSUB2450
2320 S(9,S1)=D1:S(10,S1)=Y1:FORJ=1TO6:S(J,S1)=0:NEXTJ
2370 GOSUB4500:M$="A NEW STAR SYSTEM HAS BEEN DISCOVERED! "
2390 M$=M$+"IT IS A CLASS IV AND ITS NAME IS"+MID$(S$,S(8,S1),4):GOSUB1:GOSUB8
2400 GOSUB5190:GOTO660
2450 X=(RND(0)-.5)*100
2460 Y=50*RND(0)
2470 IFABS(X)<25ANDY<25THEN2450
2480 F=1
2490 GOSUB2550
2500 IFF=0THEN2450
2510 S(7,S1)=0
2520 RETURN
2550 ONHGOTO2660,2620,2600,2560
2560 Z=X:X=-Y:Y=Z
2590 GOTO2660
2600 Y=-Y
2610 GOTO2660
2620 Z=X:X=Y:Y=Z
2660 FORJ=1TOS1-1
2670 IFSQR((X-S(11,J))^2+(Y-S(12,J))^2)>=D9THEN2700
2680 F=0:RETURN
2700 NEXTJ
2720 S(11,S1)=INT(X):S(12,S1)=INT(Y):H=1+ABS(H<=3)*H:RETURN
2770 INPUTA$:A$=A$+" "
2780 FORI=1TOS9:J=S(8,I):IFMID$(A$,1,4)=MID$(S$,J,4)THEN2870
2810 NEXTI
2820 IFMID$(A$,1,4)="SAVE"THEN4770
2832 IFMID$(A$,1,3)="MAP"THENS2=S1:GOSUB5190:S1=S2:GOTO2850
2840 IFMID$(A$,1,6)="REPORT"THENRP=1:S2=S1:GOSUB3190:S1=S2:RP=0:GOTO2850
2848 M$=MID$(A$,1,4)+" IS NOT A STAR NAME IN THIS GAME":GOSUB1
2850 PRINT"NEXT STAR";:GOTO2770
2870 S(20,T1)=I
2880 IFI<>S1THEN2910
2890 M$="CHOOSE A DIFFERENT STAR SYSTEM TO VISIT":GOSUB1
2900 GOTO2850
2910 D2=SQR((S(11,S1)-S(11,I))^2+(S(12,S1)-S(12,I))^2)/R9
2920 D2=INT(D2)
2930 IFRND(0)>(Q/2)THEN3030
2940 I=1+INT(RND(0)*3):ONIGOTO3000,2980,2960
2960 M$="SHIP DOES NOT PASS INSPECTION"
2970 GOTO3010
2980 M$="CREWMEN DEMAND A VACATION"
2990 GOTO3010
3000 M$="LOCAL HOLIDAY SOON"
3010 M$=M$+"--"+STR$(I)+" WEEK DELAY.":GOSUB1:GOSUB8
3020 D2=D2+7*I
3030 S(21,T1)=S(21,T1)+D2
3040 IFS(21,T1)<=360THEN3070
3050 S(21,T1)=S(21,T1)-360
3060 S(22,T1)=S(22,T1)+1
3070 M=INT((S(21,T1)-1)/30)
3080 L=3*M+1
3090 M$="THE ETA AT "+MID$(S$,J,4)+" IS "+MID$(C$,L,3)+STR$(S(21,T1)-30*M)+","+STR$(S(22,T1)):GOSUB1
3110 I=(INT(RND(0)*3)+1)*ABS(RND(0)>(Q/2))
3120 S(21,T1)=S(21,T1)+7*I
3130 IFS(21,T1)<=360THEN3160
3140 S(21,T1)=S(21,T1)-360
3150 S(22,T1)=S(22,T1)+1
3160 S(24,T1)=I:RETURN
3180 PRINT"('+'=SELLING AND '-'=BUYING)":GOSUB8:CLS:RETURN
3190 GOSUB4500:M$="JAN 1,"+STR$(Y1)+"  YEARLY REPORT #"+STR$(Y1-2069):GOSUB1
3230 IFY1>2070THEN3450
3240 PRINT"STAR SYSTEM CLASSES:":PRINT"     I  COSMOPOLITAN":PRINT"    II  DEVELOPED"
3270 PRINT"   III  UNDERDEVELOPED":PRINT"    IV  FRONTIER":PRINT
3310 PRINT"MERCHANDISE:":PRINT"    UR  URANIUM":PRINT"   MET  METALS":PRINT"    HE  HEAVY EQUIPMENT"
3350 PRINT"   MED  MEDICINE":PRINT"  SOFT  COMPUTER SOFTWARE":PRINT"  GEMS  STAR GEMS":GOSUB8:CLS
3400 M$="EACH TRADING SHIP CAN CARRY MAX"+STR$(W)+" TONS CARGO.":GOSUB1
3410 M$="STAR GEMS AND COMPUTER SOFTWARE, WHICH AREN'T SOLD BY THE TON, DON'T COUNT.":GOSUB1
3450 PRINTTAB(5)"*** CURRENT PRICES ***"
3500 FORS1=1TOS9:IFS1=4ORS1=9THENGOSUB3180
3510 IFS1=1ORS1=4ORS1=9THENM$="NAME             CLASS          ":GOSUB7:M$=N$:GOSUB7
3520 IFRP=0THENGOSUB3870
3530 FORI=1TO6:P(I)=SGN(S(I,S1))*P(I):NEXTI
3550 GOSUB4580
3560 PRINTMID$(S$,S(8,S1),4),D$
3570 FORCC=1TO6:N(CC)=P(CC):NEXT:GOSUB16
3580 IFS1/2<>INT(S1/2)THEN3600
3600 NEXTS1:GOSUB3180
3650 M$="CAPTAINS                        ":GOSUB7
3680 M$="SHIP$   BANK$   CARGOS  TOTALS  ":GOSUB7
3690 FORB1=1TOP9:GOSUB4380:NEXTB1:FORP1=1TOP9:M1=0:M2=0:FORI1=0TOT9/P9-1:IFB1=7THENGOSUB8
3760 M1=M1+S(23,P9*I1+P1):FORK=1TO6:M2=M2+S(12+K,P9*I1+P1)*Q(K):NEXTK:NEXTI1
3810 M3=M2+M1+S(25,P1):PRINT"CAPTAIN";P1
3821 PRINTMID$(STR$(INT(M1))+"        ",1,8);MID$(STR$(INT(S(25,P1)))+"        ",1,8);
3822 PRINTMID$(STR$(INT(M2))+"        ",1,8);MID$(STR$(INT(M3))+"        ",1,8);
3840 NEXTP1:GOSUB8:PRINT:RETURN
3870 R1=1+ABS(S(7,S1)>=5)+ABS(S(7,S1)>=10)
3880 D2=12*(Y1-S(10,S1))+(D1-S(9,S1))/30
3890 FORI=1TO6
3900 G(I)=(1+S(7,S1)/15)*(M(I,R1)*S(7,S1)+C(I,R1))
3910 IFABS(G(I))>.01THEN3940
3920 P(I)=0
3930 GOTO3970
3940 IFABS(G(I)*12)<ABS(S(I,S1)+D2*G(I))THENCC=ABS(G(I)*12):GOTO3942
3941 CC=ABS(S(I,S1)+D2*G(I))
3942 S(I,S1)=SGN(G(I))*CC
3950 P(I)=Q(I)*(1-SGN(S(I,S1))*ABS(S(I,S1)/(G(I)*X9)))
3960 P(I)=100*INT(P(I)/100+.5)
3970 NEXTI:S(9,S1)=D1:S(10,S1)=Y1:RETURN
4020 PRINT:INPUT"DO YOU WISH TO VISIT THE LOCAL  BANK";A$:IFMID$(A$,1,1)="Y"THEN4060
4050 RETURN
4060 GOSUB4310:GOSUB4380
4080 M$="YOU HAVE $"+STR$(INT(S(25,B1)))+" IN THE BANK AND $"+STR$(INT(S(23,T1)))+" ON YOUR SHIP.":GOSUB1
4090 IFS(25,B1)=0THEN4190
4100 INPUT"HOW MUCH DO YOU WISH TO WITHDRAW";Z:IFZ<=S(25,B1)THEN4150
4130 PRINT"TOO MUCH":GOTO4100
4150 IFZ<=0THEN4190
4160 S(25,B1)=S(25,B1)-Z:S(23,T1)=S(23,T1)+Z:RETURN
4190 INPUT"HOW MUCH DO YOU WISH TO DEPOSIT ";Z:IFZ>=0THEN4240
4220 M$="YOU CAN'T DEPOSIT A NEGATIVE NUMBER":GOSUB1:GOTO4190
4240 IFZ<=S(23,T1)THEN4270
4250 PRINT"YOU HAVE $"INT(S(23,T1))"ON YOUR SHIP"
4260 GOTO4190
4270 S(23,T1)=S(23,T1)-Z
4280 S(25,B1)=S(25,B1)+Z
4290 RETURN
4310 B1=T1:FORI=1TOS9/P9:IFB1<=P9THEN4360
4340 B1=B1-P9:NEXTI
4360 RETURN
4380 S(25,B1)=S(25,B1)*(1+.05*(Y1-S(27,B1)+(D1-S(26,B1))/360)):S(26,B1)=D1:S(27,B1)=Y1:RETURN
4430 INPUTM$:X=VAL(M$):IFLEFT$(M$,3)="INV"THENGOSUB895:GOTO4430
4440 IFINT(X)=XANDX>=0THENRETURN
4450 M$="TYPE A ZERO IF YOU WANT TO PASS THIS ONE, BUT NO NEGATIVES OR DECIMALS.":GOSUB1:GOTO4430
4500 M$="  *** GENERAL ANNOUNCEMENT ***  ":GOSUB7:RETURN
4580 ONS(7,S1)/5+1GOTO4650,4630,4610,4590
4590 D$="    I":RETURN
4610 D$="   II":RETURN
4630 D$="  III":RETURN
4650 D$="   IV":RETURN
4680 IFS1>1THEN4710
4690 I=1
4700 GOTO4750
4710 I=4*INT(14*RND(0))+5:FORJ=2TOS1-1:IFI=S(8,J)THEN4710
4740 NEXTJ
4750 S(8,S1)=I:RETURN
4770 PRINT"READY TAPE ";:GOSUB8:PRINT:PRINT"SAVING..."
4930 O=1:FORZZ=13TO15:FORCC=1TO24:S(CC,ZZ)=ASC(MID$(T$,O,1)):O=O+1:NEXT:NEXT
4970 S(1,0)=W:S(2,0)=D9:S(3,0)=K9:S(4,0)=X9:S(5,0)=D1:S(6,0)=Y1
4980 S(7,0)=P9:S(8,0)=T9:S(9,0)=S9:S(10,0)=Y9:S(11,0)=T1:S(12,0)=S1
5180 CSAVE*S,"STRADATA":END
5190 CLS0:POKE49151,64:PRINT@32*8,;:M$="+--+--+--+--+--+--+--+--+--+--+":GOSUB7
5250 FORZZ=15TO-15STEP-2:IFABS(ZZ)/3=INT(ABS(ZZ)/3)THENPRINT@32*INT(8-ZZ/2)+16,;:M$="+":GOSUB7:GOTO5340
5310 PRINT@32*INT(8-ZZ/2)+16,;:M$="!":GOSUB7
5340 NEXT:PRINT@32*8+15,"SOL";
5350 FORZZ=15TO-15STEP-1:Y=ZZ*10/3:Y0=(ZZ+1)*10/3:FORS1=2TOS9:IFS(12,S1)>=Y0ORS(12,S1)<YTHEN5400
5370 X1=INT(26+S(11,S1)/2)
5380 PRINT@32*INT(8-ZZ/2)+INT(X1/2),MID$(S$,S(8,S1),4);
5400 NEXT:NEXT
5410 M$=INKEY$:IFM$=""THEN5410
5490 CLS0:POKE49151,0:CLS:RETURN
5500 REM JIM GERRIE 2022
