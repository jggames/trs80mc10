0 CLS:MC=16384:GOTO 100
6 CLS0:PRINT@480,"":RETURN
7 PRINT@416,"":PRINT@416,;:RETURN
8 PRINT@480,"hit any key";:I$=INKEY$:ON-(I$="")GOTO8:FORCC=1TO11:PRINTCHR$(8);:NEXT
9 FORCC=8TO14:PRINT@32*CC,"":NEXT:PRINT@32*8,;:RETURN

100 PRINT "THIS IS A LUNAR LANDING PROGRAM"
110 PRINT "DO YOU WISH INSTRUCTIONS";
120 INPUT V$
130 GOSUB6
140 XX=0:YY=0
160 FOR I=1 TO 5
170   XX=XX+10:SET(XX,YY,0):YY=YY+1:XX=0
180 NEXT I
190 FORXX=0TO20:SET(XX,YY,0):NEXT:YY=YY+1:XX=0
200 FOR I=1 TO 5
210   XX=XX+10:SET(XX,YY,0):YY=YY+1:XX=0
220 NEXT I:IF LEFT$(V$,1)="N" THEN 330
221 PRINT@32*6+5,"y";:PRINT@32*2+11,"x";:PRINT@32*6,"-";:PRINT@11,"+";:POKE32*6+MC,PEEK(32*6+MC)-64
222 POKE11+MC,PEEK(11+MC)-64:PRINT@32*2+14,"<-- LANDING ZONE";:SET(3,3,4)
225 PRINT@256,;              
230 PRINT "POSITIVE z-AXIS IS OUT OF THE":PRINT"SCREEN. SHIP IS THE RED DOT."
240 PRINT "TRY TO LAND AT CENTER OF CROSS."
250 PRINT "YOU CONTROL VERT(Z), HORIZ(Y),"
260 PRINT "AND TRANSVERSE(X) VELOCITIES."
270 PRINT "YOU ALSO CONTROL TIME OF BURN."
280 PRINT "SUPPLY ALL INIT DATA IN METRIC."
300 GOSUB8
320 REM
330 GOSUB9:PRINT "INITIAL ALTITUDE";
340 INPUT A3:GOSUB9
350 PRINT "INITIAL VERTICAL VELOCITY";
360 INPUT V6:GOSUB9
370 PRINT "INITIAL DISTANCE Y";
380 INPUT D4:GOSUB9
390 PRINT "INITIAL Y VELOCITY";
400 INPUT V4:GOSUB9
410 PRINT "INITIAL DISTANCE X";
420 INPUT D5:GOSUB9
430 PRINT "INITIAL X VELOCITY";
440 INPUT V5:GOSUB9
450 PRINT "MAXIMUM BURN RATE";
460 INPUT M:GOSUB9
470 PRINT "AMOUNT OF FUEL";
480 INPUT F3:GOSUB9
490 PRINT "GRAVITATIONAL CONSTANT";
500 INPUT G:GOSUB9
510 PRINT "NAME OF YOUR LANDER";
520 INPUT N1$:GOSUB9
530 REM
540 PRINT "CONTROL TO ";N1$;"...":PRINT"COMMENCE LANDING.":FORCC=1TO2500:NEXT
550 GOSUB 1900
560 V=V6
570 V1=V4
580 V2=V5
590 T=0
600 F=F3
610 A=A3
620 D1=D4
630 D2=D5
640 T3=T3+T
650 GOSUB 2080
660 GOSUB9:PRINT "TIME =";INT(T3*100)/100;" SECONDS."
670 PRINT LEFT$("ALT"+STR$(INT(A*100)/100)+" M.  VEL"+STR$(INT(V*100)/100)+" M/SEC.",31)
680 PRINT LEFT$("X DIST"+STR$(INT(D2*100)/100)+" M.  VEL"+STR$(INT(V2*100)/100)+" M/SEC.",31)
690 PRINT LEFT$("Y DIST"+STR$(INT(D1*100)/100)+" M.  VEL"+STR$(INT(V1*100)/100)+" M/SEC.",31)
700 PRINT "FUEL =";INT(F*100)/100;"UNITS"
705 REM
710 GOSUB7:PRINT"TIME INTERVAL";:INPUTM$:IFM$=""THENM$="1"
720 T=VAL(M$)
730 GOSUB7:PRINT"VERTICAL BURN";
740 INPUT B
750 GOSUB7:PRINT"TRANSVERSE(X) BURN";
760 INPUT B2
770 B2=-B2
780 GOSUB7:PRINT"HORIZONTAL(Y) BURN";
790 INPUT B1
800 B1=-B1
810 REM
820 GOTO 1340
830 F1=F
840 A1=A
850 IF F<=0 THEN 1190
860 A=A-V*T-((G-B)*T)/2
870 IF A<=0 THEN 910
880 V=V+(G-B)*T
890 GOSUB 2010
900 GOTO 640
910 G1=G-B
920 T1=((-2*V)+SQR(ABS(4*ABS(V)^2+8*A1*G)))/(2*G)
930 V=V+(G1*T1)
940 GOSUB 2010
950 T4=T3+T1
960 IF V<3 THEN 990
970 GOSUB9:PRINT"AT TIME=";INT(T4*100)/100;"THE ";N1$:PRINT"CRASHED WITH A DESCENT"
971 PRINT "VELOCITY OF";INT(V*100)/100;"M/SEC."
972 PRINT "AT POINT (";STR$(INT(D2));",";STR$(INT(D1*100)/100);")"
973 PRINT MID$(STR$(SQR(ABS(D1)^2+ABS(D2)^2)),2);" METRES":PRINT"FROM THE LANDING SITE."
975 GOTO 1800
980 GOTO 1000
990 GOSUB9:PRINT "AT TIME=";INT(T4*100)/100;"THE ";N1$:PRINT"LANDED WITH A DESCENT"
1000 PRINT "VELOCITY OF";INT(V*100)/100;"M/SEC."
1010 R=SQR(ABS(V1)^2+ABS(V2)^2)
1020 IF R>5 THEN 1050
1030 PRINT "A HORIZONTAL VELOCITY":PRINT"OF";INT(R*100)/100;"M/SEC. ";
1040 GOTO 1080
1050 P9=1
1060 PRINT "AND FLIPPED WITH A HORIZONTAL"
1065 PRINT "VELOCITY OF";INT(R*100)/100;"M/SEC."
1070 P9=1
1080 PRINT "AT POINT (";STR$(INT(D2));",";STR$(INT(D1));")"
1090 D=SQR(ABS(D1)^2+ABS(D2)^2)
1100 IF R<=5 THEN 1140
1110 PRINT MID$(STR$(INT(D*100)/100),2);"METRES OFF TARGET."
1120 GOTO 1790
1130 IF P9=1 THEN 1790
1140 IF D>100 THEN 1170
1150 PRINT "BEAUTIFUL ";N1$;". YOU WERE"
1155 PRINT "ONLY ";MID$(STR$(INT(D*100)/100),2);" METRES OFF."
1160 GOTO 1790
1170 PRINT "GOOD LANDING ";N1$;",":PRINT"BUT YOU MISSED BY";INT(D*100)/100;"M."
1180 GOTO 1790
1190 T=F1/(ABS(B)+ABS(B1)+ABS(B2))
1200 A=A-(V*((G-B)/2))
1210 V=V+(G-B)
1220 T1=((-2*V)+SQR(ABS(4*ABS(V)^2+8*A1*G)))/(2*G)
1230 V=V+G*T1
1240 GOSUB 2010
1250 D1=D1+V1*(T1-T)
1260 D2=D2+V2*(T1-T)
1270 IF V<3 THEN 950
1280 T4=T3+T1
1290 GOSUB8:PRINT "THE ";N1$;" CRASHED":PRINT"AT";INT(T4*100)/100;"SECONDS"
1295 PRINT "AT POINT (";STR$(INT(D2));",";STR$(INT(D1));")"
1300 R=SQR(V1^2+V2^2)
1310 PRINT "WITH A DOWNWARD VELOCITY":PRINT"OF";STR$(INT(V*100)/100);", AND A FORWARD"
1315 PRINT "VELOCITY OF";INT(R*100)/100
1320 PRINT "CRASH DUE TO LACK OF FUEL"
1330 GOTO 1790
1340 IF ABS(B)<=M THEN 1400
1350 IF B<0 THEN 1380
1360 B=M
1370 GOTO 1390
1380 B=-M
1390 Z=Z+1
1400 IF ABS(B1)<=M THEN 1460
1410 IF B1<0 THEN 1440
1420 B1=M
1430 GOTO 1450
1440 B1=-M
1450 Z1=Z1+1
1460 IF ABS(B2)<=M THEN 1520
1470 IF B2<0 THEN 1500
1480 B2=M
1490 GOTO 1510
1500 B2=-M
1510 Z2=Z2+1
1520 F=F-((ABS(B)+ABS(B1)+ABS(B2))*T)
1530 IF Z=1 THEN 1600
1540 IF Z=2 THEN 1780
1550 IF Z1=1 THEN 1660
1560 IF Z1=2 THEN 1780
1570 IF Z2=1 THEN 1720
1580 IF Z2=2 THEN 1780
1590 GOTO 840
1600 IF B=0 THEN 1550
1610 E=E-1
1620 IF E=0 THEN 1780
1630 GOSUB 1880
1640 GOSUB9:PRINT "VERTICAL ENGINE WILL":PRINT"BLOW IN";E;" BURN(S)":GOSUB8
1650 GOTO 1550
1660 IF B1=0 THEN 1570
1670 E1=E1-1
1680 IF E1=0 THEN 1780
1690 GOSUB 1880
1700 GOSUB9:PRINT "HORIZONTAL ENGINE WILL":PRINT"BLOW IN";E1;"BURN(S)":GOSUB8
1710 GOTO 1570
1720 IF B2=0 THEN 840
1730 E2=E2-1
1740 IF E2=0 THEN 1780
1750 GOSUB 1880
1760 GOSUB9:PRINT "TRANSVERSE ENGINE WILL":PRINT"BLOW IN";E2;"BURN(S)":GOSUB8
1770 GOTO 840
1780 GOSUB9:PRINT "BANG. AN ENGINE BLEW.":PRINT"YOU BLEW UP.":GOSUB8
1790 REM
1800 PRINT "WOULD YOU LIKE TO TRY AGAIN";
1810 INPUT V$
1820 IF V$="N" THEN 1870
1830 GOSUB6:GOSUB9:PRINT "SAME INITIAL VALUES";
1840 INPUT V$
1850 IF V$="Y" THEN 530
1860 GOTO 320
1870 END
1880 PRINT "******* WARNING *******";:SOUND1,6
1890 RETURN
1900 T3=0
1910 Z=0
1920 Z1=0
1930 Z2=0
1940 E=11
1950 E1=11
1960 E2=11
1970 P9=0
1980 RETURN
2010 V7=V1-B1*T
2020 D1=D1+(V1*T)+(T*(V1-V7))/2
2030 V8=V2-B2*T
2040 D2=D2+(V2*T)+(T*(V2-V8))/2
2050 V1=V7
2060 V2=V8
2070 RETURN
2080 GOSUB6:IF A>100 THEN 2410
2090 IF D2>50 OR D2<-50 THEN 2410
2100 IF D1>50 OR D1<-50 THEN 2410
2110 Q1=INT(D2/10+.5)
2120 Q2=INT(D1/10+.5)
2130 PRINT@480,"":XX=0:YY=0:FOR Q=5 TO -5 STEP -1
2140   IF Q=0 THEN 2180
2150   IF Q=Q1 THEN 2330
2160   XX=XX+10:SET(XX,YY,0):YY=YY+1:XX=0
2170   GOTO 2400
2180   IF Q1=0 THEN 2210
2190   FORXX=0TO20:SET(XX,YY,0):NEXT:YY=YY+1:XX=0
2200   GOTO 2400
2210   FOR Q3=-5 TO 5 STEP .5
2220     IF Q3=0 THEN 2260
2230     IF Q3=Q2 THEN 2290
2240     SET(XX,YY,0):XX=XX+1
2250     GOTO 2300
2260     IF Q2=0 THEN 2290
2270     SET(XX,YY,8):XX=XX+1
2280     GOTO 2300
2290     SET(XX,YY,4):XX=XX+1
2300   NEXT Q3
2310   YY=YY+1:XX=0
2320   GOTO 2400
2330   IF Q2<0 THEN 2370
2340   IF Q2>0 THEN 2390
2350   XX=XX+10:SET(XX,YY,4):YY=YY+1:XX=0
2360   GOTO 2400
2370   XX=(Q2+6)*2-2:SET(XX,YY,4):XX=10:SET(XX,YY,0):YY=YY+1:XX=0
2380   GOTO 2400
2390   XX=10:SET(XX,YY,0):XX=(Q2+6)*2-2:SET(XX,YY,4):YY=YY+1:XX=0
2400 NEXT Q
2410 RETURN
2420 END
3010 REM LEM.ECB
3020 REM PROGRAM: LEM
3030 REM FROM CREATIVE COMPUTING-NOV-DEC 1976
3040 REM AUTHOR UNKNOWN
