10 DIM W1(7),W2(7),W0$(7),WF(6),DF(7)
20 GOSUB 1010
30 RANDOMIZE TIMER
40 REM SET PRICES AND MAX SALES
50 COLOR 14,0:CLS:D=1000:PL=75 :PS=106:PC=98:MAX=100
60 F=14:B=0:S=0:L=0:C=0:S1=0:L1=0:C1=0
70 W$="SUNNY        PARTLY CLOUDYCLOUDY       RAINY        CLOUDY       PARTLY CLOUDY"
80 GOSUB 1200
90 COLOR 3,0:GOSUB 820:GOSUB 900
100 REM DO WEEKS
110 FOR WK=1 TO 10
120 TEMP=60+5*INT(10*RND(0))
130 COLOR 2,0:LOCATE 3,70:PRINT WK
140 WTHR=1+13*INT(6*RND(0)):LOCATE 4,45:PRINT MID$(W$,WTHR,13);
150 LOCATE 4,60:PRINT TEMP;"'  "
160 COLOR F,B:LOCATE 13,42:PRINT"PURCHASES:";
170 GOSUB 740:GOSUB 580:GOSUB 630:GOSUB 680
180 GOSUB 740:LOCATE 19,1:PRINT"DO YOU WANT TO MAKE ANY CHANGES (ENTER NUMBER)?"
190 LOCATE 20,10:PRINT"0 - NO CHANGES"
200 LOCATE 21,10:PRINT"1 - CHANGE SUGAR"
210 LOCATE 22,10:PRINT"2 - CHANGE LEMONADE"
220 LOCATE 23,10:PRINT"3 - CHANGE CUPS"
230 LOCATE 24,8:INPUT;I
240 LOCATE 19,1:PRINT SPACE$(75):PRINT SPACE$(75):PRINT SPACE$(75)
250 PRINT SPACE$(75):PRINT SPACE$(75):PRINT SPACE$(75);
260 IF I=0 THEN 320
270 ON I GOTO 290,300,310
280 SOUND 1,1:GOTO 180
290 D=D+PS*S1:GOSUB 940:GOSUB 580:GOTO 180
300 D=D+PL*L1:GOSUB 940:GOSUB 630:GOTO 180
310 D=D+PC*C1:GOSUB 940:GOSUB 680:GOTO 180
320 GOSUB 780:GOSUB 900:IF AMT<>0 THEN 340
330 COLOR 4,0:SOUND 1,1:LOCATE 24,1:PRINT"YOU HAVE NO SUPPLIES!!!";:GOTO 160
340 GOSUB 740:INPUT"HOW MUCH WILL YOU CHARGE FOR LEMONADE (1 - 99) ",PR
350 IF PR>0 AND PR<100 THEN 370
360 COLOR 4,0:SOUND 1,1:PRINT"ENTER A NUMBER BETWEEN 1 AND 99!!!";:COLOR F,B:GOTO 340
370 GOSUB 990:COLOR 0,7:LOCATE 14,10:PRINT"           ";
380 LOCATE 14,11:PRINT PR;"CENTS";
390 REM DO EACH DAY
400 FOR DAY=1 TO 7:GOSUB 1420:COLOR 0,0:GOSUB 740:COLOR 9,0:PRINT W0$(DAY)
410 COLOR 0,0:FOR J=13 TO 4 STEP -1:LOCATE J,33:PRINT B$;:NEXT J
420 COLOR 4,0:J=13:K=55
430 LOCATE J,33:PRINT B$;:J=J-1:K=K+5:IF K<W1(DAY) THEN 430
440 GOSUB 1510:GOSUB 1690
450 FOR I=1 TO SALES:COLOR 14,0
460 LOCATE 23,20:PRINT"SALES ==> ";I;
470 D=D+PR:C=C-1:L=L-10:S=S-4:GOSUB 900
480 IF AMT<>0 THEN 510
490 I=SALES:DAY=7:LOCATE 24,1:COLOR 4,0
500 SOUND 1,1: PRINT"YOU RAN OUT OF SUPPLIES!!!";
510 NEXT I:NEXT DAY
520 NEXT WK
530 LOCATE 24,40:COLOR 4,0:PRINT"DO YOU WANT TO PLAY AGAIN (Y/N)?";
540 A$=INKEY$:IF A$="" THEN 540
550 IF LEFT$(A$,1)="Y" OR LEFT$(A$,1)="Y" THEN 50
560 IF LEFT$(A$,1)<>"N" AND LEFT$(A$,1)<>"N" THEN 540
570 GOSUB 1140:END
580 GOSUB 740:PRINT"HOW MUCH SUGAR DO YOU WANT ($ ";
590 PRINT PS/100;:INPUT" / LB.) ",S1
600 IF D-S1*PS<0 THEN GOSUB 750:GOTO 580
610 LOCATE 14,45:PRINT "LBS. OF SUGAR = ";S1;"  "
620 D=D-S1*PS:GOSUB 940:RETURN
630 GOSUB 740:PRINT"HOW MUCH LEMONADE ($ ";
640 PRINT PL/100;:INPUT" / QUART) ",L1
650 IF D-L1*PL<0 THEN GOSUB 750:GOTO 630
660 LOCATE 15,45:PRINT"QUARTS OF LEMONADE = ";L1;"  "
670 D=D-L1*PL:GOSUB 940:RETURN
680 GOSUB 740:PRINT"HOW MANY PACKAGES OF 100 CUPS ($ ";
690 PRINT PC/100;:INPUT" / PACKAGE) ",C1
700 IF D-C1*PC<0 THEN GOSUB 750:GOTO 680
710 LOCATE 16,45:PRINT"PACKAGES OF 100 CUPS = ";C1;"  "
720 D=D-C1*PC:GOSUB 940:RETURN
730 REM CLEAR QUESTION LINE
740 LOCATE 23,1:PRINT SPACE$(75):LOCATE 23,1:RETURN
750 COLOR 4,0:SOUND 1,1:LOCATE 24,1:PRINT"YOU DON'T HAVE THAT MUCH!!!";
760 COLOR F,B:RETURN
770 REM ADD PURCHASES TO SUPPLIES
780 S=S+100*S1:L=L+100*L1:C=C+100*C1:S1=0:L1=0:C1=0
790 LOCATE 13,42:PRINT SPACE$(35):LOCATE 1,42:PRINT SPACE$(35)
800 LOCATE 1,42:PRINT SPACE$(35):LOCATE 1,42:PRINT SPACE$(35):RETURN
810 REM PRINT HEADINGS
820 LOCATE 1,42:PRINT"YOU HAVE";
830 LOCATE 3,42:PRINT"WEATHER PREDICTION FOR WEEK";
840 LOCATE 6,42:PRINT"SUPPLIES:"
850 LOCATE 7,45:PRINT"LBS. OF SUGAR "
860 LOCATE 8,45:PRINT"QUARTS OF LEMONADE "
870 LOCATE 9,45:PRINT"NUMBER OF CUPS "
880 RETURN
890 REM UPDATE VALUES
900 COLOR 4,0
910 LOCATE 7,70:PRINT S/100
920 LOCATE 8,70:PRINT L/100
930 LOCATE 9,70:PRINT C;"  "
940 COLOR 4,0:LOCATE 1,70:PRINT "$";:PRINT D/100
950 COLOR  2,0:X1=INT((S+S1*100)/4):X2=INT((L+L1*100)/10):X3=C+(C1*100) 
960 AMT=X1:IF X2<X1 THEN AMT=X2
970 IF X3<AMT THEN AMT=X3
980 LOCATE 11,42:PRINT"YOU CAN MAKE";AMT;" CUPS OF LEMONADE   ";
990 LOCATE 24,1:PRINT SPACE$(75);
1000 COLOR F,B:RETURN
1010 REM SWITCH OUTPUT TO THE COLOR MONITOR - REMOVED UNEEDED
1070 REM INIT. ARRAYS
1080 W0$(1)="SUNDAY   ":W0$(2)="MONDAY   ":W0$(3)="TUESDAY  "
1090 W0$(4)="WEDNESDAY":W0$(5)="THURSDAY ":W0$(6)="FRIDAY   "
1100 W0$(7)="SATURDAY ":WF(1)=1:WF(2)=.8:WF(3)=.6:WF(4)=.3:WF(5)=.6:WF(6)=.8
1110 DF(1)=.9:DF(2)=.5:DF(3)=.7:DF(4)=.6:DF(5)=.7:DF(6)=.8:DF(7)= 1
1120 RETURN
1130 REM SWITCH TO MONOCHROME - REMOVED UNEEDED
1180 RETURN
1190 REM DRAW LEMONADE STAND
1200 LOCATE 1,1:COLOR 14,1:B$=CHR$(127):C$=B$+B$:D$=C$+C$:E$=D$+D$
1210 FOR I=1 TO 4:PRINT SPACE$(30):NEXT I
1220 PRINT"    "B$E$D$E$B$"    "
1230 PRINT"   "C$E$D$E$C$"   "
1240 PRINT"  "B$C$E$D$E$C$B$"  "
1250 COLOR 6,1
1260 FOR I=1 TO 4:PRINT"   "B$"                      "B$"   ":NEXT I
1270 COLOR 14,1
1280 FOR I=1 TO 4:PRINT"  "B$C$E$D$E$C$B$"  ":NEXT I
1290 COLOR 1,2:PRINT SPACE$(30)
1300 COLOR 5,15:LOCATE 6,6:PRINT"ROG'S LEMONADE STAND";
1310 COLOR 0,7:LOCATE 13,10:PRINT" $ PRICE $ "
1320 LOCATE 14,10:PRINT"           "
1330 COLOR 7,0:T=105:LOCATE 2,32:PRINT "TEMP."
1340 LOCATE 3,32:PRINT CHR$(127);:PRINT B$;CHR$(127)
1350 FOR I=1 TO 10:LOCATE I+3,32:PRINT CHR$(127)" "CHR$(127);T
1360 T=T-5:NEXT I:COLOR 7,4
1370 LOCATE 14,32:PRINT B$;:PRINT" ";B$
1380 LOCATE 15,32:PRINT CHR$(127);" ";CHR$(127)
1390 LOCATE 16,32:PRINT B$;:PRINT CHR$(127);:PRINT B$
1400 RETURN
1410 REM FIGURE WEATHER FOR WEEK
1420 FOR K=1 TO 7:W1(K)=TEMP-10+5*INT(5*RND(0))
1430 IF W1(K)<65 THEN W1(K)=65 ELSE IF W1(K)>105 THEN W1(K)=105
1440 NEXT K:WTHR=1+INT(WTHR/13):FOR K=1 TO 7:W2(K)=WTHR:Z=1+INT(10*RND(0))
1450 ON Z GOTO 1460,1470,1470,1490,1490,1490,1490,1470,1470,1460
1460 W2(K)=WTHR+2:GOTO 1480
1470 W2(K)=WTHR+1
1480 IF W2(K)>6 THEN W2(K)=W2(K)-6
1490 NEXT K:RETURN
1500 REM ADD WEATHER
1510 F$=" ":WB=0:ON W2(DAY) GOTO 1520,1520,1530,1540,1530,1520
1520 WF=1:GOTO 1550
1530 WF=8:WB=1:F$=CHR$(127):GOTO 1550:REM WAS CHR$(177)
1540 WF=8:WB=0:F$="/"
1550 COLOR WF,WB:FOR J=1 TO 4:LOCATE J,1:FOR K=1 TO 30:PRINT F$;:NEXT K,J
1560 LOCATE 5,1:PRINT F$F$F$F$;:LOCATE 5,27:PRINT F$F$F$F$;
1570 LOCATE 6,1:PRINT F$F$F$;:LOCATE 6,28:PRINT F$F$F$;
1580 LOCATE 7,1:PRINT F$F$;:LOCATE 7,29:PRINT F$F$;
1590 FOR J=8 TO 11:LOCATE J,1:PRINT F$F$F$;:LOCATE J,5:FOR K=1 TO 22:PRINT F$;
1600 NEXT K:LOCATE J,28:PRINT F$F$F$;:NEXT J
1610 FOR J=12 TO 15:LOCATE J,1:PRINT F$F$;:LOCATE J,29:PRINT F$F$;:NEXT J
1620 IF W2(DAY)=1 THEN RETURN
1630 F$=CHR$(127)+CHR$(127):WF=15:IF W2(DAY)>2 AND W2(DAY)<6 THEN WF=7
1640 FOR K=9 TO 20:LOCATE 1,K:IF RND(0)>.7 THEN PRINT F$;
1650 NEXT K:FOR K=3 TO 27:LOCATE 2,K:IF RND(0)>.3 THEN PRINT F$;
1660 NEXT K:FOR K=7 TO 22:LOCATE 3,K:IF RND(0)>.6 THEN PRINT F$;
1670 NEXT K:RETURN
1680 REM FIND SALES FOR DAY
1690 TF=((W1(DAY)-55)*2)/100
1700 PF=(100-PR)/100
1710 SALES=1+INT(MAX*TF*PF*WF(W2(DAY))*DF(DAY))
1720 RETURN
