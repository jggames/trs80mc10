0 CLS:CLEAR100:DIMS(4,7,1),S$(1),L(32,4),A$(1),P(4,16),C(1,8),LY,NY,Q1,QT,NX,C1:V=32:GOTO10
1 PRINT@V*(LY*4+NY-(QT<15))+NX+Q1,"*";:RETURN
2 PRINT@V*CY+CX+O,M$;:RETURN
3 CX=SX-2:CY=SY-1:C1=V*CY+CX+O:PRINT@C1-V," …";:?@C1,"Š  ";:GOSUB7:?@C1-V,"  ";:?@C1,S$(WP);:RETURN
4 SOUND25,1:RETURN
5 SOUND38,1:RETURN
6 SOUND64,1:RETURN
7 FORC2=1TO50:NEXT:RETURN
10 DIMJ,K,DY,DX,VX,SD,WX,WY,NB,SX,JX,RR,TY,EX,WP,CX,CY,O,SY,QR,SF,TX,SG,K,AC,A,A$:QR=1:GOSUB3000
20 S$(0)="ƒ„":S$(1)="Œ‡":A$(0)="<-":A$(1)="->"
30 Q1=-3:O=-1:FORJ=1TO4:READP(J,.)
40 FORA=0TO1:FORB=.TO8:C(A,B)=.:NEXT:NEXT
50 FORK=1TO7:READL:P(J,K+7)=L:P(J,8-K)=L:NEXT:NEXT
60 DATA10
70 DATA2,2,2,2,2,2,2
80 DATA40
90 DATA1,2,3,5,8,13,21
100 DATA20
110 DATA2,3,4,5,6,7,8
120 DATA80
130 DATA1,4,9,16,25,36,49
140 INPUT"PLAYER 1";P1$
150 INPUT"PLAYER 2";P2$:P1$=LEFT$(P1$,3):P2$=LEFT$(P2$,3):PRINTP1$;" VS ";P2$
160 INPUT"IS THIS CORRECT";G$:IFG$<>"Y"ANDG$<>"YES"THEN140
170 GOSUB410:GOSUB510
180 FORRR=1TO4:PRINT@V*RR,"*";:PRINT@V*RR+27,"*";
200 QR=1-QR:TY=QR*20:TX=26-TY:CX=TX:CY=.
210 M$=RIGHT$(STR$(P(RR,.)),2):GOSUB2
220 CX=6+TY:CY=.:M$=A$(QR):GOSUB2
230 GOSUB660:IFC(1-QR,RR)>=P(RR,.)THEN250:REM END OF ROUND
240 GOTO200
250 FORJ=.TO1:FORK=5TO8:C(J,K)=.:NEXT:NEXT
260 FORJ=.TO1:FORK=1TO4:GL=P(K,.):AC=C(J,K):C(J,5)=C(J,5)+AC
270 C(J,6)=C(J,6)-(AC>=GL)*GL:C(J,7)=C(J,7)+(C(J,K)-C(1-J,K)):NEXT:NEXT
280 FORJ=.TO1:FORK=6TO7:C(J,K)=C(J,K)+C(J,5):NEXT:NEXT
290 FORJ=.TO1:FORK=5TO7:C(J,8)=C(J,8)+C(J,K):NEXT:NEXT
300 FORJ=.TO1:FORK=5TO8:Y$=STR$(C(J,K)):L=LEN(Y$):TX=7+J*26-L
310 TY=K:CX=TX+(TX<20):CY=TY:M$=Y$:GOSUB2:NEXT:NEXT
320 NEXTRR:REM END OF MAIN LOOP
330 REM GAME OVER
340 PRINT@V*10+7,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
350 PRINT@V*11+7,"ŸPLAY AGAIN? (Y/N)Ÿ";
360 PRINT@V*12+7,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
390 I$=INKEY$:IFI$=""THEN390
391 IFI$="Y"THENRUN
392 IFI$="N"THENEND
393 GOTO390
410 CLS:GOSUB2000
440 RETURN
510 FORJ=.TO3:SY=4+J*4:FORK=.TOJ+3:SX=12-J*2+K*4
520 CX=SX-1:CY=SY-2:M$=" ":GOSUB2
530 WP=INT(RND(0)*2)
540 S(J,K,.)=WP:S(J,K,1)=.:GOSUB3
550 NEXT:NEXT
560 CX=2:CY=.:M$=P1$:GOSUB2
570 CX=29:CY=.:M$=P2$:GOSUB2
571 RETURN
660 FORJ=.TOV:L(J,.)=.:NEXT:NB=1
670 A$=INKEY$:IFA$=""THEN670
680 IFA$="-"THENRETURN
690 IFA$="+"THENA$=STR$(RND(8))
700 A=VAL(A$):IF(A<1)OR(A>8)THEN670
710 L(.,.)=1:FORJ=1TO3:L(.,J)=.:NEXT:L(.,4)=10+A*2

720 EX=1:FORJ=.TOV:IFL(J,.)THENEX=.:GOSUB760
740 NEXT:IFEXTHENRETURN
750 GOTO720

760 DY=L(J,.):DX=L(J,1):LY=L(J,2):NY=L(J,3):NX=L(J,4):QT=LY*4+NY
770 IFLY+NYTHENIFLY<4THENPRINT@V*(LY*4+NY)+NX+Q1," ";
780 L(J,3)=(NY+1)AND3:ONNY+1GOTO790,810,860,880
790 IFLY>3THENL(J,.)=.:GOTO950:REM SCORING ROUTINE
800 GOSUB1:ONRND(3)GOTO4,5,6
810 VX=.:GOSUB940:IFS(WY,WX,1)=.OR(S(WY,WX,.)=SD)=.THEN840
820 VX=1-2*SD:L(J,1)=VX:L(J,3)=NY+1
830 L(J,4)=NX+VX:PRINT@V*(LY*4+NY-(QT<15))+NX+Q1+VX,"*";:RETURN
840 IFS(WY,WX,.)=SDTHENL(J,.)=.:S(WY,WX,1)=1:GOSUB1:RETURN
850 L(J,3)=NY+1:GOSUB1:ONRND(3)GOTO4,5,6
860 L(J,1)=.:L(J,4)=NX+DX:PRINT@V*(LY*4+NY-(QT<15))+NX+Q1+DX,"*";
870 SOUND185,1:RETURN
880 IFQT<15THENGOSUB1
890 L(J,2)=LY+1:GOSUB940:S(WY,WX,.)=1-S(WY,WX,.)
900 IFS(WY,WX,1)=.THEN930
910 L(NB,.)=1:L(NB,1)=.:L(NB,2)=LY:L(NB,3)=.:L(NB,4)=NX+2-SD*4:NB=NB+1
920 S(WY,WX,1)=.:PRINT@V*(LY*4+NY-1)+NX+Q1+2-SD*4," ";:SOUND225,1
930 SX=12-WY*2+WX*4:SY=4+WY*4:WP=S(WY,WX,.):GOSUB3:RETURN
940 WY=LY:JX=INT(NX/2)+LY-6:WX=INT(JX/2):SD=JXAND1:RETURN
950 SF=P(RR,NX/2-2)
960 SG=C(QR,RR)+SF
970 TX=4+27*QR+(SG>9)+(SG>99)+(SG>999)
980 TY=RR:A$=MID$(STR$(SG),2)
990 CX=TX:CY=TY:M$=A$:GOSUB2:C(QR,RR)=SG:RETURN
1100 REM CHAR COMMAND 
2000 PRINT"        …1…2…3…4…5…6…7…8…    "
2011 PRINT"        … … … … … … … … …    "
2012 PRINT"        …   …   …   …   …    "
2013 PRINT"        … … … … … … … … …    "
2014 PRINT"        …   …   …   …   …    "
2015 PRINT"      ŒŒ… … … … … … … … „Œ  "
2016 PRINT"      …   …   …   …   …   …  "
2017 PRINT"      … … … … … … … … … … …  "
2018 PRINT"      …   …   …   …   …   …  "
2019 PRINT"    ŒŒ… … … … … … … … … … „Œ"
2020 PRINT"    …   …   …   …   …   …   …"
2021 PRINT"    … … … … … … … … … … … … …"
2022 PRINT"    …   …   …   …   …   …   …"
2023 PRINT"  ŒŒ… … … … … … … … … … … … „Œ"
2024 PRINT"  …   …   …   …   …   …   …   …"
2025 PRINT"  … … … … … … … … … … … … … … …";
2040 RETURN
3000 CLS:PRINT"   SWITCHBOX BY TODD HEIMARCK   ";
3010 PRINT"PLAYERS ALTERNATE DROPPING"
3020 PRINT"BALLS INTO ONE OF 8 SLOTS."
3030 PRINT"THESE BALLS MIGHT MAKE IT"
3040 PRINT"TO ONE OF THE 14 EXIT PATHS"
3050 PRINT"DEPENDING ON THE SWITCHBOXES"
3060 PRINT"THEY MUST PASS THROUGH. THE"
3070 PRINT"GAME LASTS 4 ROUNDS. ROUND 1"
3080 PRINT"EACH EXIT = 2 PTS. YOUR GOAL"
3090 PRINT"IS 10 PTS. THE OTHER ROUNDS"
3100 PRINT"HAVE THE FOLLOWING SEQUENCES"
3101 PRINT"FROM THE CENTER OUTWARDS:
3110 PRINT"2. 1,2,3,5,8,13,21    goal 40"
3120 PRINT"3. 2,3,4,5,6,7,8      goal 20"
3130 PRINT"4. 1,4,9,16,25,36,49  goal 80"
3140 PRINT"    HIT ANY KEY TO CONTINUE";
3150 A$=INKEY$:IFA$=""THEN3150
3160 CLS:PRINT"BONUS PTS ARE AWARDED AT THE"
3170 PRINT"END OF EACH ROUND. 4 NUMBERS"
3180 PRINT"APPEAR BELOW THE SCORECARDS."
3190 PRINT"THE 1ST IS SIMPLY THE TOTAL"
3200 PRINT"SO FAR. THE 2ND IS THE TOTAL"
3210 PRINT"PLUS A BONUS. BONUS = GOAL,"
3220 PRINT"IF PLAYER'S PTS >= THE GOAL."
3230 PRINT"THE 3RD IS THE DIFFERENCE" 
3240 PRINT"BETWEEN PLAYERS, WHICH IS"
3250 PRINT"ADDED TO THE WINNER'S SCORE"
3260 PRINT"AND SUBTRACTED FROM THE LOSER."
3270 PRINT"THE FINAL NUMBER IS THE GRAND"
3280 PRINT"TOTAL. USE ROUNDS 1 AND 3"
3282 PRINT"TO SEED THE FIELD WITH BALLS."
3283 PRINT"'+'=RANDOM #. '-'=SKIP TURN."
3290 PRINT"    HIT ANY KEY TO CONTINUE";
3300 A$=INKEY$:IFA$=""THEN3300
3310 CLS:RETURN
3320 REM ATARI ST KEVIN MYKYTYN
3330 REM MC-10 PORT BY J GERRIE
