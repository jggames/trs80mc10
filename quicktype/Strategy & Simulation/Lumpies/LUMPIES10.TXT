0 CLS:DIMZ(23,25,2),XP(10,4),YP(10,4),DR(4),H,V,C,P,Z,X,Y,L,A,B,D,Z1,O,B$,A$,WP$(7),W1(7),W2(7),CO$(4):GOTO50
2 Z=INT(ABS(Z(X+XP(P,D),Y+YP(P,D),L))):RETURN
3 Z1=ABS(Z(X+XP(P,D),Y+YP(P,D),L)):RETURN
50 PRINT@199,"LUMPIES OF LOTIS IV";:PRINT@261,"BY JOHN AND JEFF KLEIN";:PRINT@324,"MC-10 EDITS BY JIM GERRIE"
90 LV=2
110 O=36:YS=20:YP=0:YN=0:D=3:L=1:GX=1:HO=0
120 FORA=1TO4:READDR(A):NEXT:FORA=1TO4:FORB=1TO10:READXP(B,A):NEXT:NEXT:FORA=1TO4:FORB=1TO10:READYP(B,A):NEXT:NEXT
130 FORA=0TO7:READWP$(A),W1(A),W2(A):NEXT:FORA=1TO4:READCO$(A):NEXT
131 FORC=1TOLV:FORA=2TO23:READA$:FORB=2TO21:Z(B,A,C)=VAL(MID$(A$,B-1,1)):IFZ(B,A,C)=1ANDRND(0)<.023THENZ(B,A,C)=5
140 NEXT:NEXT:NEXT:FORA=1TO16:B$=B$+CHR$(128):NEXT:GOSUB5080:GOSUB5100:CLS0:C=2:GOSUB5070:GOSUB5060:GOSUB5050:X=7:Y=2:GOTO410
170 XO=X:YO=Y
180 X=XO:Y=YO
190 A$=INKEY$:RESET(X+O,Y):SET(X+O,Y,C)
200 IFA$<>"R"ANDA$<>"T"ANDA$<>"B"ANDA$<>"U"ANDA$<>"D"ANDA$<>"Z"ANDA$<>"W"ANDA$<>"S"ANDA$<>"A"ANDA$<>"O"ANDA$<>"L"THEN190
205 IFA$="W"THEN5030
210 IFA$="R"THEN1110
220 IFA$="T"ANDINT(ABS(Z(X,Y,L)))=3THEN5020
230 IFA$="B"THENP=5:GOSUB3:IFZ1=2THENPRINT@416,"DOOR BROKEN";:Z(X+XP(5,D),Y+YP(5,D),L)=-2.1:SOUND9,2:SOUND50,2:GOTO170
240 IFA$="L"THEN5180
250 IFA$="O"THEN5150
260 IFA$="U"ANDABS(Z(X,Y,L-1))=4ANDL<>1THENL=L-1:FORV=100TO200STEP25:SOUNDV,2:NEXT:GOTO960
270 IFA$="D"THENIFABS(Z(X,Y,L))=4ANDL<>LVTHENL=L+1:FORV=200TO100STEP-25:SOUNDV,2:NEXT:GOTO960
280 IFA$="Z"THEND=D+2:IFD>4THEND=D-4
300 IFA$="S"THEND=D+1:IFD>4THEND=1
310 IFA$="A"THEND=D-1:IFD<1THEND=4
320 IFZ(X,Y,L)=0ORZ(X,Y,L)=-2THEN5035
330 GOSUB5023
340 FORA=-1TO1:FORB=-1TO1:IFA+Y=1ORA+Y=24ORB+X=1ORB+X=22THEN380
360 IFZ(X+B,Y+A,L)=0THEN380
370 SET(X+O,Y,C):IFZ(X+B,Y+A,L)>0THENZ(X+B,Y+A,L)=-Z(X+B,Y+A,L):SET(X+O+B,Y+A,C)
380 NEXT:NEXT
410 PRINT@0,"ûúúúúúúúúúúúúúúù";:PRINT@32,"ûíÄÄÄÄÄÄÄÄÄÄÄÄëù";:PRINT@64,"öîììììììììììììòï";
411 PRINT@96,"öÄÄÄÄÄÄÄÄÄÄÄÄÄÄï";:PRINT@128,"öÄÄÄÄÄÄÄÄÄÄÄÄÄÄï";:PRINT@160,"öÄÄÄÄÄÄÄÄÄÄÄÄÄÄï";
412 PRINT@192,"öÄÄÄÄÄÄÄÄÄÄÄÄÄÄï";:PRINT@224,"öÄÄÄÄÄÄÄÄÄÄÄÄÄÄï";:PRINT@256,"öëúúúúúúúúúúúúíï";
420 PRINT@288,"õòÄÄÄÄÄÄÄÄÄÄÄÄîó";:PRINT@320,"õììììììììììììììó";:P=1:GOSUB2:IFZ<>0ANDZ<>2THEN430
421 FORV=2TO8:PRINT@32*V,"üü";:NEXT:SET(1,3,C):SET(1,18,C):FORV=6TO15:SET(4,V,C):NEXT
422 IFZ=2THENFORV=6TO18:SET(1,V,8):NEXT:FORV=6TO18:SET(2,V,8):NEXT:GOTO460
430 P=4:GOSUB2:IFZ=0ORZ=2THENFORV=3TO7:PRINT@32*V,"üüü";:NEXT:FORH=1TO4:SET(H,5,C):SET(H,16,C):NEXT:GOTO460
440 FORH=1TO4:SET(H,9,C):SET(H,12,C):NEXT:FORH=0TO5:SET(H,5,C):SET(H,16,C):NEXT
450 P=8:GOSUB2:IFZ=0ORZ=2THENFORH=0TO7:FORV=9TO12:SET(H,V,C):NEXT:NEXT
460 P=2:GOSUB2:IFZ<>0ANDZ<>2THEN470
461 FORV=2TO8:PRINT@32*V+14,"üü";:NEXT:SET(30,3,C):SET(30,18,C):FORV=6TO15:SET(27,V,C):NEXT
462 IFZ=2THENFORV=6TO18:SET(30,V,8):NEXT:FORV=6TO18:SET(29,V,8):NEXT:GOTO500
470 P=6:GOSUB2:IFZ=0ORZ=2THENFORV=3TO7:PRINT@32*V+13,"üüü";:NEXT:FORH=27TO30:SET(H,5,C):SET(H,16,C):NEXT:GOTO500
480 FORH=27TO31:SET(H,9,C):SET(H,12,C):NEXT:FORH=26TO31:SET(H,5,C):SET(H,16,C):NEXT
490 P=10:GOSUB2:IFZ=0ORZ=2THENFORH=24TO31:FORV=9TO12:SET(H,V,C):NEXT:NEXT
500 P=5:GOSUB2:IFZ<>0ANDZ<>2THEN505
501 FORV=3TO7:PRINT@32*V+2,"üüüüüüüüüüüü";:NEXT
502 IFZ=2THENFORV=3TO7:PRINT@32*V+4,"ˇˇˇˇˇˇˇˇ";:NEXT:FORH=8TO22STEP2:SET(H,16,8):NEXT:GOTO580
503 GOTO580
505 SET(5,15,C):SET(6,14,C):SET(7,13,C):SET(8,12,C):SET(5,6,C):SET(6,7,C):SET(7,8,C):SET(8,9,C):SET(26,15,C):SET(25,14,C)
506 SET(24,13,C):SET(23,12,C):SET(23,9,C):SET(24,8,C):SET(25,7,C):SET(26,6,C):FORH=9TO22:SET(H,9,C):SET(H,12,C):NEXT
510 P=4:GOSUB2:IFZ<>0ANDZ<>2THEN520
511 FORV=4TO6:PRINT@32*V+2,"üü";:NEXT:SET(5,7,C):SET(5,14,C):FORV=10TO11:SET(8,V,C):NEXT
512 IFZ=2THENFORV=9TO14:SET(6,V,8):NEXT:FORV=12TO13:SET(6,V,8):NEXT:GOTO540
520 FORH=5TO8:SET(H,9,C):SET(H,12,C):NEXT:FORV=9TO12:SET(8,V,C):NEXT
540 P=6:GOSUB2:IFZ<>0ANDZ<>2THEN550
541 FORV=4TO6:PRINT@32*V+12,"üü";:NEXT:SET(26,7,C):SET(26,14,C):FORV=10TO11:SET(23,V,C):NEXT
542 IFZ=2THENFORV=9TO14:SET(25,V,8):NEXT:FORV=12TO13:SET(25,V,8):NEXT:GOTO570
550 FORH=23TO27:SET(H,9,C):SET(H,12,C):NEXT:FORV=9TO12:SET(23,V,C):NEXT
570 P=9:GOSUB2:IFZ=0ORZ=2THENPRINT@164,"üüüüüüüü";
571 IFZ=2THENSET(15,12,8):SET(15,10,8):SET(17,12,8):SET(17,10,8)
580 P=5:GOSUB3:IFZ1=2THENPRINT@384,"(B)BREAK DOOR";
590 IFINT(ABS(Z(X,Y,L)))<>3THEN600
591 NW=VAL(MID$(STR$(Z(X,Y,L)),4,1)):NN=VAL(MID$(STR$(Z(X,Y,L)),5,1)):W1=PEEK(16678):W2=PEEK(16679)
592 POKE16678,W1(NW):IFW2(NW)<>180THENPOKE16679,W2(NW)
593 PRINT@448,"(T)AKE";:PRINT@480,"#";RIGHT$(STR$(NN),1);" ";WP$(NW);
600 IFZ(X,Y,L)=-4ORABS(Z(X,Y,L-1))=4THENGOSUB910
610 IFZ(X,Y,L)<=-5THENONINT(ABS(Z(X,Y,L)))-4GOTO630,830,870,1000
620 GOTO170
630 IFINT(Z(X,Y,L))<>Z(X,Y,L)THEN5040
640 IFRND(0)>.85THENZ(X,Y,L)=-1:GOTO170
641 CL=INT(RND(0)*2)*2+1:WP=RND(7):WN=INT(RND(0)*(9/LV)+1+(9*L-9)/LV)
660 PRINT@165,"–Œ≥Õ¿";:PRINT@197,"∞æºΩ∞";:PRINT@229,"ºøøøΩ";:PRINT@261,"∞∑∞∑∞";:POKE16580,W1(WP):POKE16612,W2(WP)
680 PRINT@384,"A LUMPY WITH A";:PRINT@416,"#";RIGHT$(STR$(WN),1);" ";WP$(WP);:HT=0
700 PRINT@480,"(F)IGHT OR (R)UN";:A$=INKEY$:IFA$<>"F"ANDA$<>"R"THEN700
705 GOSUB5023:IFA$="R"THENA=5:GOTO790
710 IFRND(100)<=50+(WN-YN)*5THENPRINT@384,"YOU MISSED";:GOTO720
711 PRINT@384,"YOU HIT";:HT=HT+RND(2):IFHT>4THEN750
712 PRINT@448,CO$(HT);
720 IFRND(100)<=50+(YN-WN)*5THENPRINT@416,"HE MISSED";:GOTO740
725 PRINT@416,"HE HIT";:YS=YS-1:GOSUB5060:IFYS<=0THEN1110
740 IFRND(0)<.075ANDYN<>0THENYP=0:YN=0:SOUND230,1:GOSUB5050
741 GOTO700
750 PRINT@416,"HE HAS FLED";:LT=LT+1:A=3
790 IFWN=0THENZ(X,Y,L)=-1:GOTO800
791 Z(X,Y,L)=-A-(WP*.1)-(WN*.01)-(CL*.001)
800 IFA=5THENX=XO:Y=YO
811 GOTO340
830 PRINT@384,"YOU FOUND FOOD  ";:FD=RND(6):PRINT@416,"WORTH";FD;"STRENGTH";:YS=YS+FD:GOSUB5060
840 FORA=1TOFD:POKE16676+A,15:NEXT:Z(X,Y,L)=-1:GOTO170
870 PRINT@165,"êêüêê";:PRINT@197,"–ﬂﬂﬂ–";:PRINT@229,"‘’ﬂ⁄ÿ";:PRINT@261,"–◊—⁄–";:PRINT@384,"YOU FREED";
890 PRINT@416,"A PRISONER!";:Z(X,Y,L)=-1:PF=PF+1:GOTO170
910 IFABS(Z(X,Y,L))=4ANDL<>LVTHENPRINT@263,"ŒÕ";:PRINT@295,"ŒÕ";:PRINT@480,"(D)OWN LADDER";:RETURN
930 IFABS(Z(X,Y,L-1))=4ANDL>1THENFORV=1TO9:PRINT@32*V+7,"ŒÕ";:NEXT:PRINT@480,"(U)P LADDER";
940 RETURN
960 CLS0:C=2:GOSUB5070:FORA=2TO23:FORB=2TO21:IFZ(B,A,L)<0THENSET(B+O,A,C)
980 NEXT:NEXT:GOSUB5060:GOSUB5050:GOTO340
1000 PRINT@384,"COMM ROOM";:IFHO=0THENPRINT@448,"(C)ALL HOME";:PRINT@480,"(I)GNORE";
1010 PRINT@165,"ﬁ‹‹›";:PRINT@197,"€””◊";:PRINT@229,"€€€ﬂ";:PRINT@261,"ﬂ◊◊◊";
1030 A$=INKEY$:A=A-20:IFA<=0THENA=360:SOUND200,2
1050 IFA$="C"ANDHO=0THENHO=-1:GOTO1080
1060 IF(A$="I"ANDHO=0)OR(A$<>""ANDHO=-1)THEN1080
1070 GOTO1030
1080 GOSUB5023:PRINT@480,"STATUS (R)EPORT";:X=XO:Y=YO:GOTO340
1110 CLS:PRINT"PLAYER STATUS":PRINT:PRINT"PRISONERS FREED:";PF:PRINT"LUMPIES DEFEATED:";LT
1120 US=0:FORC=1TOLV:FORA=2TO23:FORB=2TO21:IFZ(B,A,C)>0THENUS=US+1
1130 NEXT:NEXT:NEXT:PRINT"UNITS NOT SEEN:";US
1140 PRINT"COMM ROOM ";:IFHOTHENPRINT"SEEN":GOTO1150
1141 PRINT"UNSEEN"
1150 IFYS<=0THENPRINT"YOU ARE DEFEATED.":END
1160 IFHOTHENPRINT:PRINT"RETURN HOME (Y/N)?":GOTO1170
1161 PRINT:PRINT"HIT ANY KEY TO CONTINUE"
1170 A$=INKEY$:IFA$="Y"ANDHOTHENCLS:PRINT"YOU RETURN HOME SAFELY.":END
1180 IFA$="N"ANDHOORA$<>""ANDHO=0THEN960
1181 GOTO1170
1210 DATA 24,26,25,27,-1,1,-2,-1,0,1,2,-1,0,1,0,0,1,1,1,1,1,2,2,2,1,-1,2,1,0,-1,-2,1,0,-1,0,0,-1,-1,-1,-1,-1,-2,-2,-2
1280 DATA 0,0,-1,-1,-1,-1,-1,-2,-2,-2,-1,1,-2,-1,0,1,2,-1,0,1,0,0,1,1,1,1,1,2,2,2,1,-1,2,1,0,-1,-2,1,0,-1
1330 DATA "NONE",128,180,"BRIEFCASE",128,32,"5/16 WRENCH",25,180,"GUN",31,180,"ARROW",30,180,"YO-YO",128,0
1340 DATA "REFRESHMENT",21,180,"BOMB",42,180,"HE'S WORRIED","HE'S NERVOUS","HE'S A BIT SORE","HE'S WEAKENING"
2010 DATA 12510106152111111256,10110101110100201000,10612125110106501251,10000100000100201016,11111111111101101000
2060 DATA 20000100002001521251,11610125011101001011,11110106051506011061,00000100020200010000,15211101110111011111
2110 DATA 61010001000001000102,00010721270721270105,11110001000001000100,10000721270721270111,12560002000002000101
2160 DATA 10652101115111011101,10000100002000010001,11111104015101111111,00001005061101000020,11111025000001016051
2210 DATA 20201110011111251011,70700011110000011016,11001111100004511011,11602000111100111256,11105110000110111000
3040 DATA 11501112110110111061,00200000110110051015,11121110152111120002,20101110010100000111,12101112000101160511
3090 DATA 10000001111101110200,10111600000105110111,10001101110102000101,11101101110111111101,00100502020010000002
3140 DATA 80112121011012707251,20010001251010000011,11111111011012707211,00010002000010000020,07212701015212515210
3190 DATA 00020000011010111020,16070150611010000051,11000110000012510111,15212112111110110611
5020 TM=YN:YN=NN:NN=TM:TM=YP:YP=NW:NW=TM:POKE16678,W1:POKE16679,W2:GOSUB5050:IFNN=0THENZ(X,Y,L)=-1:GOSUB5023:GOTO580
5022 Z(X,Y,L)=-3-(NW*.1)-(NN*.01):GOSUB5023:GOTO580
5023 FORV=12TO15:PRINT@32*V,B$;:NEXT:RETURN
5030 IFD=1ANDY>2THENY=Y-1:GOTO320
5031 IFD=2ANDX<21THENX=X+1:GOTO320
5032 IFD=3ANDY<23THENY=Y+1:GOTO320
5033 IFD=4ANDX>2THENX=X-1:GOTO320
5035 SOUND1,1:GOTO180
5040 WP=VAL(MID$(STR$(Z(X,Y,L)),4,1)):WN=VAL(MID$(STR$(Z(X,Y,L)),5,1)):CL=VAL(MID$(STR$(Z(X,Y,L)),6,1)):GOTO660
5050 PRINT@497,"              ";:PRINT@497,"#";RIGHT$(STR$(YN),1)" ";WP$(YP);:RETURN
5060 PRINT@465,"              ";:PRINT@465,YS;"STRENGTH";:RETURN
5070 PRINT@20,"LEVEL";L;:RETURN
5080 PRINT@480,"HIT A KEY";
5090 A$=INKEY$:A=RND(1000):IFA$=""THEN5090
5095 RETURN
5100 CLS:PRINT"INTERGALACTIC TRADE SHIPS HAVE":PRINT"BEEN HIJACKED NEAR THE PLANET":PRINT"LOTIS IV, WHICH IS INHABITATED"
5107 PRINT"BY LUMPIES - A PRIMITIVE, BUT":PRINT"CUNNING RACE WHO LIVE DEEP":PRINT"UNDERGROUND. ALTHOUGH THEIR"
5110 PRINT"TECHNOLOGY IS CRUDE, THE":PRINT"LUMPIES ARE FAMOUS FOR THEIR":PRINT"ABILITY TO PUT ALMOST ANYTHING"
5113 PRINT"TO USE AS A WEAPON. YOU HAVE":PRINT"BEEN DISPATCHED TO SPY ON THE":PRINT"LUMPIES AND DISCOVER WHETHER"
5116 PRINT"THEY ARE INDEED HIJACKING SHIPS.":GOSUB5080:CLS:PRINT"IF THE LUMPIES HAVE TAKEN ANY":
5117 PRINT"PRISONERS FROM THE CREWS":PRINT"OF THE MISSING SHIPS, YOU MUST":PRINT"FREE THESE CAPTIVES."
5121 PRINT"ON YOUR APPROACH TO LOTIS IV,":PRINT"A SEVERE ATMOSPHERIC STORM":PRINT"SENDS YOUR SPACECRAFT CRASHING"
5124 PRINT"TO THE PLANET'S SURFACE. YOU":PRINT"FIND YOURSELF WANDERING IN A":PRINT"WARREN OF UNDERGROUND CAVES."
5127 PRINT"YOUR ONLY HOPE FOR SURVIVAL IS":PRINT"TO FIND THE LUMPIES' COMMUNICA-":PRINT"TIONS CENTER SO YOU CAN SUMMON"
5130 PRINT"A RESCUE TEAM FROM HOME.":GOSUB5080:CLS:RETURN
5150 Z(1,0,0)=X:Z(2,0,0)=Y:Z(3,0,0)=L:Z(4,0,0)=D:Z(5,0,0)=XO:Z(6,0,0)=YO:Z(7,0,0)=HO:Z(8,0,0)=YP:Z(9,0,0)=YN
5160 Z(10,0,0)=NN:Z(11,0,0)=NW:Z(12,0,0)=WN:Z(13,0,0)=YS:Z(14,0,0)=PF:Z(15,0,0)=LT
5170 CLS:PRINT"SAVE GAME":GOSUB5080:IFA$<>"Q"THENPRINT@480,"SAVING...";:CSAVE*Z,"LUMPDATA"
5175 GOTO960
5180 CLS:PRINT"LOAD GAME":GOSUB5080:IFA$="Q"THEN960
5190 PRINT@480,"PRESS PLAY";:CLOAD*Z,"LUMPDATA":X=Z(1,0,0):Y=Z(2,0,0):L=Z(3,0,0):D=Z(4,0,0):XO=Z(5,0,0):YO=Z(6,0,0)
5200 HO=Z(7,0,0):YP=Z(8,0,0):YN=Z(9,0,0):NN=Z(10,0,0):NW=Z(11,0,0):WN=Z(12,0,0):YS=Z(13,0,0):PF=Z(14,0,0):LT=Z(15,0,0)
5210 GOTO960
