
1 REM OREGON TRAIL NOV '83
2 REM
3 REM  PROGRAM SIMULATES A TREK ACROSS THE COUNTRY FROM THE            EAST COAST TO THE WEST COAST (OREGON).
4 REM
5 REM   MICROSOFT BASIC 32K
6 REM
7 REM TRANSLATION FROM THE TRS-80 MODEL 3 TO THE TRS-80 COLOR COMPUTER BY JASON LAWHERN
8 REM  PUBLISHED WITH PERMISSION FROM CREATIVE COMPUTING               MAY 1978
9 REM
10 REM
11 REM  ***********************
12 REM  *                     *
13 REM  *  SAN MATEO COUNTY   *
14 REM  * OFFICE OF EDUCATION *
15 REM  *        AND          *
16 REM  *   COMPUTER-USING    *
17 REM  *      EDUCATORS      *
18 REM  *                     *
19 REM  *      SOFTSWAP       *
20 REM  *                     *
21 REM  *   333 MAIN STREET   *
23 REM  *       CA 94063      *
24 REM  *   (415) 363-5472    *
25 REM  *                     *
26 REM  *                     *
27 REM  ***********************
28 REM  *PROGRAM MAY BE COPIED*
29 REM  *FOR EDUCATIONAL USE &*
30 REM  *MAY NOT BE SOLD.COPY-*
31 REM  *RIGHT 1983 CUE,INC   *
32 REM  ***********************
100 ' OREGON TRAIL WAS WORKED ON BY THE FOLLOWING PEOPLE        TRANSLATION:        GRAPHICS       JEREMY GRATT        LOWERCASE + FORMAT BOB JACKSON
110 CLS:PRINT@0,"WAIT PLEASE"
120 CLEAR2000
130 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
140 FOR A=1TO15:READZ:NEXTA:DIMD$(20):FORA=1TO18:READD$(A):NEXT
150 DATA "APRIL 12","APRIL 26","MAY 10","MAY 24","JUNE 7"
160 DATA "JUNE 21","JULY 5","JULY 19","AUGUST 2","AUGUST 16"
170 DATA "AUGUST 31","SEPTEMBER 13","SEPTEMBER 27","OCTOBER 11"
180 DATA "OCTOBER 25","NOVEMBER 8","NOVEMBER 22","DECEMBER 20"
190 GOSUB5220 
200 AA=4:CLS:PRINT:PRINT:PRINTTAB(AA)"------------------------":PRINTTAB(AA)":     O R E G O N      :":PRINTTAB(AA)":                      :":PRINTTAB(AA)":      T R A I L       :":PRINTTAB(AA)"------------------------":PRINT:PRINT:DIMC$(5):PRINT
210 PRINT@288,"DO YOU NEED INSTRUCTIONS        (Y OR N)";CHR$(31);:INPUT N$:IF N$="N" THEN 560  ELSE IF N$<>"Y" THEN 210  
220 CLS:PRINT:PRINT:PRINT"THIS PROGRAM SIMULATES A TRIP   OVER THE ";
230 PRINT"OREGON TRAIL FROM      INDEPENDENCE, MISSOURI, ";
240 PRINT"TO      OREGON CITY, OREGON, IN 1847.":PRINT
250 PRINT"YOUR FAMILY WILL COVER THE 2040 MILE OREGON ";
260 PRINT"TRAIL IN 5-6 MONTHS --- IF YOU  MAKE IT ALIVE."
270 PRINT:PRINT"YOU HAVE SAVED $900 TO SPEND FORTHE TRIP, AND YOU'VE JUST PAID"
280 PRINT"$200 FOR A WAGON.";:GOSUB 310
290 PRINT@0,"YOU WILL NEED TO SPEND THE REST OF YOUR MONEY ON THE FOLLOWING"
300 PRINT"ITEMS:":GOSUB310  :GOTO330  
310 AA$="PRESS <ENTER> TO CONTINUE":PRINT@495-(LEN(AA$)/2),AA$;
320 Y$=INKEY$:IFY$<>CHR$(13)THEN320  ELSE CLS:PRINT@480,STRING$(31," ");:RETURN
330 PRINT@160,"OXEN - YOU CAN SPEND $200-$300.        THE MORE YOU SPEND THE          FASTER YOU WILL GO              BECAUSE YOU WILL HAVE           BETTER ANIMALS.":GOSUB 310
360 PRINT@160,"FOOD - THE MORE YOU HAVE THE           LESS CHANCE THERE IS OF         GETTING SICK."
370 GOSUB 310
380 PRINT@160,"AMMUNITION - $1 BUYS A BELT OF               50 BULLETS FOR                  ATTACKS BY ANIMALS,             BANDITS AND FOR                 HUNTING FOOD."
400 GOSUB 310
410 PRINT@160,"CLOTHING - THIS IS ESPECIALLY              IMPORTANT FOR THE               COLD WEATHER YOU WILL           ENCOUNTER WHEN                  CROSSING THE                    MOUNTAINS."   
430 GOSUB 310
440 PRINT@160,"MISCELLANEOUS SUPPLIES - THIS   INCLUDES MEDICINE AND OTHER     THINGS YOU WILL NEED FOR        SICKNESS AND EMERGENCY          REPAIRS." 
460 GOSUB 310
470 PRINT@128,"YOU CAN SPEND ALL OF YOUR MONEY BEFORE YOU START YOUR TRIP"
480 PRINT"OR YOU CAN SAVE SOME OF YOUR    CASH TO SPEND AT FORTS ALONG THE";
490 PRINT"WAY WHEN YOU RUN LOW ON FOOD ANDOTHER SUPPLIES."
500 GOSUB 310:PRINT@128,"HOWEVER, ITEMS COST MORE AT THE FORTS. YOU CAN ALSO GO HUNTING"
510 PRINT"ALONG THE WAY TO GET MORE FOOD. WHENEVER YOU HAVE TO USE YOUR"
520 PRINT"TRUSTY RIFLE, YOU WILL BE TOLD  TO TYPE IN A WORD THAT SOUNDS   LIKE A GUNSHOT.":GOSUB 310
530 PRINT@128,"THE FASTER YOU TYPE IN THE WORD AND PRESS THE <ENTER> , THE    ";
540 PRINT" BETTER CHANCE YOU WILL HAVE OF  HITTING YOUR TARGET."
550 GOSUB 320:PRINT@128,"AT EACH TURN, ALL ITEMS ARE     SHOWN IN DOLLAR AMOUNTS EXCEPT  BULLETS. WHEN YOU ARE ASKED TO  ENTER DOLLAR AMOUNTS, DON'T USE THE '$'.":GOSUB 310
560 CLS:PRINTTAB(8) "-----------------":FORD=1TO14:PRINTTAB(8)"G O O D  L U C K":FORDD=1TO25:NEXTDD,D:FORDD=1TO500:NEXTDD
570 CLS:PRINT"HOW GOOD A SHOT ARE YOU WITH    YOUR RIFLE?"
580 PRINT"      1 - ACE MARKSMAN"
590 PRINT"      2 - GOOD SHOT"
600 PRINT"      3 - FAIRLY FAIR"
610 PRINT"      4 - NEED PRACTICE"
620 PRINT"      5 - SHAKY KNEES"
630 PRINT:PRINT"ENTER ONE OF THE ABOVE -- THE   BETTER YOU CLAIM YOU ARE, THE   FASTER YOU'LL HAVE TO BE WITH   YOUR GUN TO BE SUCCESSFUL."
660 D8=0:D9=0:PRINT@416,"";CHR$(31);:INPUT"WELL, HOW GOOD A SHOT ARE YOU   (1 TO 5)";D8$:D8=VAL(D8$)
670 IF D8<1 OR D8>5 GOTO 660  
690 X1=-1:T=700
700 K8=0:S4=0:F1=0:F2=0:M=0:M9=0:D3=0
710 CLS
720 PRINT@0,"HOW MUCH DO YOU WANT TO SPEND ONYOUR ";
730 INPUT"OXEN TEAM ($200-$300)";IN$
740 IF VAL(IN$)>199 AND VAL(IN$)<301 THEN 760  
750 PRINT" !!!!! NOT POSSIBLE !!!!!!!":GOSUB 310  :GOTO720  
760 A=INT(VAL(IN$)):P=A:GOSUB880  
770 M$="FOOD":GOSUB820  :F=P
780 M$="AMMUNITIONS? ($1 = 50 BULLETS)":GOSUB820  :B=P
790 M$="CLOTHING":GOSUB 820  :C=P
800 M$="MISCELLANEOUS SUPPLIES":Q0=1:GOSUB820  :M1=P
810 GOTO 900  
820 P$="":PRINT@0,"HOW MUCH DO YOU WANT TO SPEND ON";M$;CHR$(31);:INPUT P$
830 P=VAL(P$):IF T-P>0 AND P>-1 THEN 850  
840 PRINT"IMPOSSIBLE!!!!!!":GOSUB 310  :GOTO 820  
850 GOSUB 880  
860 IF M1<0 THEN 3860 
870 RETURN
880 T=T-P:IFQ0=1THENRETURNELSECLS:PRINT"YOU HAVE";T;"DOLLARS LEFT.":GOSUB 310  
890 RETURN
900 B=B*50
910 CLS:PRINT"AFTER ALL YOUR PURCHASES, YOU   HAVE $";T;"DOLLARS LEFT."
920 PRINT"MONDAY, MARCH 29, 1847":GOSUB 310  
930 GOTO 1100 
940 IF M>=2040 THEN 4140 
950 IF F<0 THEN 3810 
960 IF B<0 THEN B=0
970 IF C<0 THEN  C=0
980 IF M1<0 THEN M1=0
990 IF T<0 THEN T=0
1000 D3=D3+1
1010 CLS:PRINT@0,ER$;:PRINT@0,"";
1020 PRINT"MONDAY, ";
1030 IF D3=18 THEN 1060 
1040 PRINT D$(D3);", 1847"
1050 GOTO 1100 
1060 PRINT"YOU HAVE BEEN ON THE TRAIL TOO  LONG."
1070 PRINT"---------YOUR FAMILY DIES IN THE";
1080 PRINT"FIRST BLIZZARD OF WINTER."
1090 GOTO 3920 
1100 IF F>=13 THEN 1120 
1110 PRINT@64,"YOU'D BETTER DO SOME HUNTING OR BUY FOOD, AND SOON!"
1120 F=INT(F):B=INT(B):C=INT(C):M1=INT(M1):T=INT(T):M=INT(M)
1130 M2=M
1140 IF S4=1 OR K8=1 THEN 1160 
1150 GOTO1190 
1160 T=T-20:IF T<0 THEN 3830 
1170 PRINT"THE DOCTOR BILL WAS $20."
1180 K8=0:S4=0
1190 PRINT@256," YOU HAVE ";T;"DOLLARS LEFT.";:GOSUB 310:CLS
1200 REM
1210 GOSUB 5530:X=0:PRINT@352,TAB(8);"DO YOU WANT TO                  1) HUNT                         2) CONTINUE"
1220 IF X1=1THENA2=1:PRINT@192-128+30,FT$;:PRINT@448,TAB(8);"3) STOP AT NEXT FORT";
1230 X$=INKEY$:IFX$=""THEN GOTO 1230 ELSE X=VAL(X$)
1240 IF X<1 THEN 1230
1250 IF X>3 THEN 1230 
1260 IF X<>0 OR B>39 THEN GOSUB 310:GOTO1290 
1270 PRINT"TOUGH --- YOU NEED MORE BULLETS TO GO HUNTING."
1280 GOTO 1210 
1290 PRINT@94+192,CL$;:PRINT@0,ER$;:X1=-X1
1300 ON X GOTO 1550 ,1690 ,1310 
1310 IFA2<>1THENX1=0:GOTO1210 ELSEPRINT@0,ER$;:CLS:PRINT@0,"";:PRINT"HOW MUCH DO YOU WISH TO SPEND ONTHE FOLLOWING: ";
1320 GOTO 1410 
1330 P$="":INPUT P$:IF P$="" THEN 1400 ELSE P=VAL(P$):IFP<0 THEN 1400 
1340 T=T-P
1350 IF T>-1 THEN 1400 
1360 PRINT"YOU DO NOT HAVE THAT MUCH MONEY."
1370 PRINT"KEEP YOUR SPENDING DOWN.":PRINT@0,ER$;:PRINT@0,"";
1380 T=T+P
1390 P=0
1400 RETURN
1410 PRINT"FOOD";
1420 GOSUB 1330 
1430 F=F+2/3*P
1440 PRINT"AMMUNITION";
1450 GOSUB 1330 
1460 B=INT(B+2/3*P*50)
1470 PRINT"CLOTHING";
1480 GOSUB 1330 
1490 C=C+2/3*P
1500 PRINT"MISC. SUPPLIES";
1510 GOSUB 1330 
1520 M1=M1+2/3*P
1530 M=M-45
1540 GOTO 1690 
1550 M=M-45
1560 A1=1:GOSUB 4740 :CLS
1570 IF B1<=2 THEN 1630 
1580 IF RND(100)<13*B1 THEN 1680 
1590 F=F+48-2*B1
1600 PRINT"NICE SHOT ---- RIGHT ON TARGET  ---- GOOD EATING!!"
1610 B=B-10-3*B1
1620 GOTO 1670 
1630 PRINT"RIGHT BETWEEN THE EYES -- YOU   GOT A BIG ONE"
1640 PRINT"-- FULL BELLIES TONIGHT!!"
1650 F=F+52+RND(6)
1660 B=B-10-RND(4)
1670 GOSUB 310:GOTO 1690 
1680 PRINT"YOU MISSED -- YOUR DINNER GOT   AWAY!!":GOTO1670 
1690 IF F>13 THEN 1710 
1700 GOTO 3810 
1710 CLS:PRINT@0,ER$;:PRINT@192,TAB(8);"DO YOU WANT TO EAT:"
1720 PRINTTAB(8)"1. POORLY"
1730 PRINTTAB(8)"2. MODERATELY "
1740 PRINTTAB(8)"3. WELL"
1750 PRINT@320,TAB(8)"";CHR$(30);:INPUT E$:IF E$="" THEN 1750 ELSE E=VAL(E$)
1760 IF E<1 THEN 1750 
1770 IF E>3 THEN 1750 
1780 E=INT(E)
1790 F=F-(8+5*E)
1800 IF F>0 THEN 1830 
1810 F=F+(8+5*E)
1820 PRINT"YOU CAN'T EAT THAT WELL!!":GOSUB 310:GOTO 1710 
1830 PRINT@0,ER$:PRINT@0,"";:M=M+200+(A-220)/5+INT(RND(10))
1840 L1=0:C1=0
1850 IF RND(10)>((M/100)^2+72)/((M/100-4)^2+12)-1 THEN 2340 
1860 CLS:PRINT@0,ER$;:PRINT@0,"RIDERS AHEAD. THEY ";
1870 S5=0
1880 IF RND(10)>8 THEN 1900 
1890 PRINT"DON'T "
1900 PRINT"LOOK HOSTLE.":PRINT"TACTICS:"
1910 PRINT"1. RUN  "
1920 PRINT"2. ATTACK  "
1930 PRINT"3. CONTINUE  "
1940 PRINT"4. CIRCLE WAGONS  "
1950 IF RND(10)<2 THEN S5=1-S5
1960 IN$=INKEY$:IF IN$="" THEN 1960
1970 PRINT@0,ER$;:PRINT@0,"";:IF VAL(IN$)<0 OR VAL(IN$)>4 THEN 1960 
1975 GOSUB 310
1980 T1=VAL(IN$)
1990 T1=INT(T1)
2000 IF S5=1 THEN 2170 
2010 IF T1>1 THEN 2030 
2020 M=M+20:M1=M1-15:B=B-150:GOTO2270 
2030 A=A-40:IF T1>2 THEN 2120 
2040 A1=2:GOSUB 4730 :CLS
2050 B=B-B1*40-80
2060 IF B1>1.5 THEN 2080 
2070 PRINT"NICE SHOOTING -- YOU DROVE THEM OFF!!":GOTO 2270 
2080 IF B1<=4 THEN 2110 
2090 PRINT"LOUSY SHOT -- YOU GOT KNIFED!!":K8=1
2100 PRINT"YOU'LL HAVE TO GO SEE OL' DOC   BLANCHARD.":GOTO 2270 
2110 PRINT"KINDA SLOW WITH YOUR COLT .44!!":GOTO 2270 
2120 IF T1>3 THEN 2150 
2130 IF RND(10)>8 THEN 2250 
2140 B=B-150:M1=M1-15:GOTO 2270 
2150 M=M-50:GOTO2340 
2160 B=B-B1*30-80:M=M-25:GOTO 2060 
2170 IF T1>1 THEN 2190 
2180 M=M+15:A=A-10:GOTO 2270 
2190 IF T1>2 THEN 2210 
2200 B=B-100:M=M-5:GOTO 2270 
2210 IF T1>3 THEN 2230 
2220 GOTO 2270 
2230 M=M-20
2240 GOTO 2270 
2250 CLS:PRINT"THEY DID NOT ATTACK!!"
2260 GOTO 2320 
2270 IF S5=0 THEN 2290 
2280 CLS:PRINT"THE RIDERS WERE FRIENDLY, BUT   CHECK FOR POSSIBLE LOSSES.":GOTO 2320 
2290 CLS:PRINT"THE RIDERS WERE HOSTILE -->     CHECK FOR LOSSES!!"
2320 GOSUB 310
2330 REM
2340 D1=0
2350 RESTORE
2360 R1=RND(100)
2370 D1=D1+1
2380 PRINT@0,ER$;:PRINT@0,"";:IF D1=16 THEN 3410 
2390 READ D
2400 IF R1>D THEN 2370 
2410 IF D1>10 THEN 2430 
2420 CLS:ON D1 GOTO 2440 ,2480 ,2510 ,2550 ,2580 ,2640 ,2710 ,2880 ,2940 
2430 CLS:ON D1-10 GOTO 2970 ,3050 ,3100 ,3300 ,3350 ,3410 ,3460 
2440 PRINT"WAGON BREAKS DOWN -- YOU LOSE   TIME AND SUPPLIES FIXING IT."
2450 M=M-15-5*RND(1)
2460 M1=M1-8
2470 GOTO 3430 
2480 PRINT"AN OX HURTS ITS LEG AND SLOWS   YOU DOWN THE REST OF THE WAY."
2490 M=M-25:A=A-20
2500 GOTO 3430 
2510 PRINT"BAD LUCK -- YOUR DAUGHTER BROKE HER ARM.  YOU HAD TO STOP AND   USE SUPPLIES TO MAKE A SLING."
2520 M=M-5-4*RND(1)
2530 M1=M1-2-3*RND(1)
2540 GOTO 3430 
2550 PRINT"AN OX WANDERS OFF AND YOU SPEND TIME LOOKING FOR IT."
2560 M=M-17
2570 GOTO 3430 
2580 PRINT"YOUR SON GETS LOST - YOU SPEND  HALF THE DAY LOOKING FOR HIM.
2590 M=M-10
2600 GOTO 3430 
2610 PRINT"UNSAFE WATER -- LOSE TIME LOOKING FOR A CLEAN SPRING."
2620 M=M-10*RND(1)-2
2630 GOTO 3430 
2640 IF M>950 THEN 3230 
2650 PRINT"HEAVY RAINS -- TIME AND SUPPLIESLOST."
2660 F=F-10
2670 B=B-500
2680 M1=M1-15
2690 M=M-10*RND(1)-5
2700 GOTO 3430 
2710 PRINT"BANDITS ARE ATTACKING!!":FORZZ=1TO1000:NEXT
2720 GOSUB 4740
2730 B=B-20*B1
2740 IF B>=0 THEN 2780 
2750 PRINT"YOU RAN OUT OF BULLETS AND THEY GOT A LOT OF YOUR CASH."
2760 T=INT((T/3*100)/100):PRINT"YOU NOW HAVE $";T
2770 GOTO 2790 
2780 IF B1<1.5 THEN 2850 
2790 PRINT"YOU GOT SHOT IN THE LEG -- AND  THEY TOOK SOME OF YOUR SUPPLIES."
2800 K8=K8-1
2810 PRINT"YOU'D BETTER ASK DOC TO LOOK    AT YOUR WOUND."
2820 M1=M1-5
2830 A=A-20
2840 GOTO 3430 
2850 PRINT"QUICKEST DRAW OUTSIDE OF DODGE  CITY!!!!"
2860 PRINT"----- YOU GOT 'EM-----"
2870 GOTO 3430 
2880 PRINT"THERE WAS A FIRE IN YOUR WAGON  FOOD AND SUPPLIES DAMAGED."
2890 F=F-40
2900 B=B-400
2910 M1=M1-RND(1)*8-3
2920 M=M-15
2930 PRINT@0,ER$;:PRINT@0,"";:GOTO 3430 
2940 PRINT"LOSE YOUR WAY IN HEAVY FOG --   TIME IS LOST."
2950 M=M-10-5*RND(1)
2960 GOTO 3430 
2970 PRINT@0,"YOU KILL A -- RATTLESNAKE!!"
2980 GOSUB 310
2990 PRINT@32,"AFTER IT BIT YOU!!"
3000 B=B-10
3010 M1=M1-5
3020 IF M1>0 THEN 3040 
3030 GOSUB 310:CLS:PRINT"YOU DIE OF SNAKE-BITE SINCE YOU HAVE NO MEDICINE.":GOTO 3920 
3040 GOTO 3430 
3050 PRINT"THE WAGON GETS SWAMPED FORDING ARIVER -- LOSE FOOD AND CLOTHES."
3060 F=F-30
3070 C=C-20
3080 M=M-20-20*RND(1)
3090 PRINT@128,"";:GOTO 3430 
3100 PRINT"WILD ANIMALS ARE ATTACKING!!"
3110 IF B>39 THEN 3150 
3120 PRINT@0,"YOU WERE TOO LOW ON BULLETS --  THE WOLVES OVERPOWERED YOU.":GOSUB 310
3130 K8=K8-1
3140 GOTO 3870 
3150 GOSUB 310:GOSUB 4740:IF B1>2 THEN 3180 
3160 PRINT"NICE SHOOTING -- THEY DIDN'T GETMUCH."
3170 GOTO 3190 
3180 PRINT"SLOW ON THE DRAW -- THEY GOT AT YOUR FOOD AND CLOTHES."
3190 B=B-20*B1
3200 C=C-4*B1
3210 F=F-8*B1
3220 GOTO 3430 
3230 PRINT"COLD WEATHER -- BRRRR!!!!"
3240 IF C>22+4*RND(1) THEN 3270 
3250 PRINT"YOU DON'T HAVE ENOUGH CLOTHING  TO KEEP YOU WARM."
3260 C1=1
3270 PRINT:PRINT"YOU HAVE ENOUGH CLOTHING TO KEEPYOU WARM."
3280 IF C1=0 THEN 3430 
3290 GOTO 4960 
3300 PRINT"HAIL STORM -- SUPPLIES DAMAGED."
3310 M=M-5-RND(1)*10
3320 B=B-200
3330 M1=M1-4-RND(1)*3
3340 GOTO 3430 
3350 IF E=1 THEN 4960 
3360 IF E=3 THEN 3390 
3370 IF RND(100)>25 THEN 4960 ;
3380 GOTO 3460 
3390 IF RND(10)<5 THEN 4960 
3400 GOTO 3460 
3410 CLS:PRINT"HELPFUL INDIANS SHOW YOU WHERE  TO FIND MORE FOOD."
3420 F=F+14
3430 REM
3440 GOSUB 310
3450 REM
3460 REM
3470 IF M<=950 THEN 940  
3480 IF RND(1)*10>9-((M/100-15)^2+12) THEN 3620 
3490 PRINT"RUGGED MOUNTAINS!!"
3500 IF RND(100)>11 THEN 3540 
3510 PRINT"YOU GET LOST -- LOSE TIME       LOOKING FOR TRAIL."
3520 M=M-60
3530 GOTO 3620 
3540 IF RND(100)>11 THEN 3600 
3550 PRINT"WAGONS DAMAGED!! LOSE TIME AND  SUPPLIES."
3560 M1=M1-5
3570 B=B-200
3580 M=M-20-RND(1)*30
3590 GOTO 3620 
3600 PRINT"THE GOING GETS SLOW."
3610 M=M-45-RND(1)/.2
3620 IF F1=1 THEN 3660 
3630 F1=1
3640 IF RND(10)<8 THEN 3730 
3650 CLS:PRINT"YOU MADE IT THROUGH THE SOUTH   PASS (NO SNOW).":GOTO5210 
3660 IF M<1700 THEN 3700 
3670 IF F2=1 THEN 3700 
3680 F2=1
3690 IF RND(10)<7 THEN 3730 
3700 IF M>950 THEN GOTO940  
3710 M9=1
3720 GOTO 5210 
3730 CLS:PRINT"BLIZZARD IN MOUNTAIN PASS --    LOSE TIME AND SUPPLIES."
3740 L1=1
3745 GOSUB 310
3750 F=F-25
3760 M1=M1-10
3770 B=B-300
3780 M=M-30-40*RND(1)
3790 IF C<18+2*RND(1) THEN 4960 
3800 GOTO 3700 
3810 CLS:PRINT"YOU HAVE RUN OUT OF FOOD AND    STARVED TO DEATH."
3820 GOTO 3920 
3830 T=0
3840 PRINT"YOU CAN'T AFFORD A DOCTOR."
3850 GOTO 3870 
3860 CLS:PRINT"YOU RAN OUT OF MEDICAL SUPPLIES."
3870 PRINT"YOU DIED OF ";
3880 IF K8=1 THEN 3910 
3890 PRINT"PNEUMONIA."
3900 GOTO 3920 
3910 PRINT"INJURIES."
3920 PRINT
3930 PRINT"DUE TO YOUR UNFORTUNATE         SITUATION, THERE ARE A FEW"
3940 PRINT"FORMALITIES WE MUST GO THROUGH."
3950 INPUT"WOULD YOU LIKE A MINISTER       (Y OR N) THEN PRESS 'ENTER'";Y1$
3960 IFLEFT$(Y1$,1)="Y" THEN PRINT:GOTO 3990 
3970 IFLEFT$(Y1$,1)<>"N" THEN 3950 
3980 GOTO 4010 
3990 PRINT@320,"";CHR$(31);:INPUT"WOULD YOU LIKE A FANCY FUNERAL  (Y OR N)";IN$
4000 IFLEFT$(IN$,1)<>"N" ANDLEFT$(IN$,1)<>"Y" THEN  3990 
4010 PRINT"WOULD YOU LIKE US TO NOTIFY YOURNEXT OF KIN (Y OR N)?"
4020 INPUT IN$
4030 IF LEFT$(IN$,1)="Y" THEN PRINT:GOTO 4070 
4040 IF LEFT$(IN$,1)<>"N" THEN 4020 
4050 PRINT:CLS:PRINT"BUT YOUR AUNT SADIE IS WORRIED  ABOUT YOU!!!"
4060 GOTO 4080 
4070 CLS:PRINT"THAT WILL BE $4.50 FOR THE      TELEGRAPH CHARGE."
4080 PRINT:PRINT:PRINT"WE THANK YOU FOR THIS           INFORMATION AND WE ARE SORRY YOU";
4090 PRINT"DIDN'T MAKE IT TO THIS GREAT    TERRITORY OF OREGON. BETTER LUCKNEXT TIME.
4110 PRINT TAB(15);"SINCERELY,"
4120 PRINT:PRINT"THE OREGON CITY CHAMBER OF      COMMERCE"
4130 GOTO 5130 
4140 F9=(2040-M2)/(M-M2)
4150 F=F+(1-F9)*(8+5*E)
4160 PRINT
4170 CLS:PRINT"YOU FINALLY ARRIVED AT OREGON   CITY AFTER"
4180 PRINT"AFTER 2040 LONG MILES"
4200 PRINT"-------- HOORAY!!"
4210 PRINT:PRINT:PRINT"A REAL PIONEER!!"
4220 PRINT
4230 F9=INT(F9*14)
4240 D3=D3*14+F9
4250 F9=F9+1
4260 IF F9<8 THEN 4280 
4270 F9=F9-7
4280 PRINT"PRESS <ENTER> TO CONTINUE."
4290 I1$=INKEY$:IFI1$<>CHR$(13)THEN4290 
4300 CLS
4310 REM
4320 DATA MONDAY,TUESDAY,WEDNEDAY,THURSDAY,FRIDAY,SATURDAY,SUNDAY
4330 GOTO 4450
4450 IF D3>124 THEN 4490 
4460 D3=D3-93
4470 PRINT"JULY";D3;"1847"
4480 GOTO 4670 
4490 IF D3>155 THEN 4530 
4500 D3=D3-124
4510 PRINT"AUGUST";D3;"1847"
4520 GOTO 4670 
4530 IF D3>185 THEN 4570 
4540 D3=D3-155
4550 PRINT"SEPTEMBER";D3;"1847"
4560 GOTO 4670 
4570 IF D3>216 THEN 4610 
4580 D3=D3-185
4590 PRINT"OCTOBER";D3;"1847"
4600 GOTO 4670 
4610 IF D3>246 THEN 4650 
4620 D3=D3-216
4630 PRINT"NOVEMBER";D3;"1847"
4640 GOTO 4670 
4650 D3=D3-246
4660 PRINT"DECEMBER";D3;"1847"
4670 REM
4680 PRINT:PRINT
4690 PRINT"PRESIDENT POLK SENDS YOU HIS    HEARTIEST CONGRATULATIONS AND   WISHES YOU A PROSPEROUS LIFE    AHEAD IN YOUR NEW HOME."
4720 GOTO 5130 
4730 REM
4740 CLS:PRINT@0,"PRESS <ENTER> TO START SHOOTING."
4750 INPUT Y$:PRINT@0,ER$;:PRINT@0,"";:IFA1=1THENGOSUB5540 
4760 S$(1)="BANG"
4770 S$(2)="BLAM"
4780 S$(3)="SHOT"
4790 S$(4)="WHAM"
4800 S=INT(RND(4)+.5)
4810 PRINT@0,"TO SHOOT, TYPE ";CHR$(34);S$(S);CHR$(34);
4820 PRINT" AND PRESS <ENTER>.";
4830 B1=0
4840 VV=0:XX=0:S$=""
4850 IN$=INKEY$:IF IN$=""THEN 4870 
4860 XX=ASC(IN$):IF XX=13 THEN 4890 
4870 B1=B1+1:IF IN$="" THEN 4850 
4880 VV=VV+1:S$=S$+IN$:IFA1=1THENGOSUB5550 :GOTO4850 ELSEGOTO4850 
4890 IF VV<3 OR S$<>S$(S) THEN PRINT"TRY AGAIN  ";:GOTO 4840 
4900 B1=B1*.03003003
4910 D9=D9-1
4920 B1=B1-(D9*.05)
4930 D9=D9+1
4940 PRINT:PRINT
4950 IFA1=1THENRETURNELSEPRINT@0,ER$;:PRINT@0,"";:RETURN
4960 IF 100*RND(1)<10+35*(E-1)THEN 5030 
4970 IF 100*RND(1)<100-(40/4^(E-1))THEN 5070 
4980 PRINT"SERIOUS ILLNESS --"
4990 PRINT:PRINT"YOU MUST STOP FOR MEDICAL       ATTENTION."
5000 M1=M1-10
5010 S4=1
5020 GOTO 5100 
5030 PRINT"MILD ILLNESS -- YOUR MEDICINE   CURED YOU."
5040 M1=M1-5
5050 M=M-5
5060 GOTO 5100 
5070 PRINT"BAD ILLNESS -- YOU USED YOUR    MEDICINE"
5080 M=M-5
5090 M1=M1-2
5100 IF M1<0 THEN 3860 
5110 IF L1=1 THEN 3700 
5120 GOTO 3430 
5130 PRINT"PRESS <ENTER> TO CONTINUE."
5140 IN$=INKEY$:IF IN$="" THEN 5140 
5150 IF ASC(IN$)<>13 THEN 5140 
5160 CLS:PRINT"ARE THERE ANY MORE PIONEERS     WAITING TO ";
5170 INPUT"MAKE THE JOURNEY     (TYPE Y OR N)";G$
5180 IF LEFT$(G$,1)="Y"  THEN RUN
5190 GOTO 5200 
5200 CLS:END
5210 GOSUB 310:GOTO940  
5220 RETURN
5490 PRINT@288,TAB(8);"MILES TO GO:";2040-M;:RETURN
5500 RETURN
5510 IFM>960THENGOTO5520 ELSEPRINT@390-64,TR$;:PRINT@527-64,BS$;:RETURN
5520 PRINT@510+5,M2$;:PRINT@509+19,M2$;:RETURN
5530 AA$="O R E G O N  T R A I L":PRINT@16-(LEN(AA$)/2),AA$;:PRINT@128,"";:AA=8:PRINTTAB(AA)"MONEY :";T:PRINTTAB(AA)"FOOD :";INT(F):PRINTTAB(AA)"CLOTHING : ";INT(C):PRINTTAB(AA)"AMMO :";INT(B):PRINT TAB(AA)"MISC. SUPPLIES :";INT(M1);:GOSUB 5490:RETURN
5540 CLS:RETURN
5550 RETURN