0 CLEAR700:DIMA(16),C1,CC,M1$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO1500
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 M1$=MID$(M$,C1,CC-C1):GOSUB6:C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 IFFL=0THENFORZZ=1TO175:PRINT
5 RETURN
6 FORC2=1TOLEN(M1$):PRINTMID$(M1$,C2,1);:IFMID$(M1$,C2,1)=" "ANDFL=0THENFORZZ=1TO175:NEXT
7 NEXT:PRINT:RETURN
8 C1=1:CC=32
9 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO9
10 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN9
11 RETURN
14 REM DEFLECTOR SHIELDS
15 M$="":INPUT"SPECIFY AMOUNT OF ENERGY TO     TRANSFER TO SHIELDS";H:IFA(6)+H>5*UTHEN15
16 IFG-H<0THENPRINT"INSUFFICENT ENERGY  <";G;">":GOTO15
17 G=G-H:A(6)=A(6)+H:Q=10:RETURN
19 REM SHIP SYSTEMS SETUP
20 S=INT(RND(A/U)):X=S:G=RND(A+U)+A*S:FORJ=1TO5:A(J)=1:NEXT:A(6)=RND(U*5)
25 K=0:FORJ=10TO15:A(J)=1:NEXT:A(16)=RND(U+A)+A:GOTO40
30 GOSUB900:X=X-1:IFA(6)<0ORG<0ORX<=0THENX=S-X:A=1:GOTO45
31 GOTO25
35 PRINT@96,"OUT OF";S;"HAVE BEEN DESTROYED":PRINT@256,"THE FEDERATION ";
36 IFX=STHENPRINT"HAS BEEN SAVED!":WN=WN+1:EN=EN+1:GOTO500
37 PRINT"WILL BE LOST...":PRINTTAB(13);"IDIOT!":DF=DF+1:EN=EN+1:GOTO510
40 R=L*10*RND(A/U):B=INT(180*RND(A/L/10)):F=0:I=0:GOSUB650:FORC=1TO5:CLS4:PRINT@264,"R E D  A L E R T";
41 GOSUB920:CLS0:GOSUB920:NEXTC:CLS
45 K7=K7+1:PRINT@64,"SENSOR REPORT:";X;B$;
46 IFX=1THENPRINT"SHIP":GOTO48
47 PRINT"SHIPS"
48 IFA=1THEN35
49 PRINT@96,"APPROACHING"
50 PRINT:PRINT"RANGE";INT(R);TAB(16);"AZIMUTH";STR$(INT(B*2));"'":IFZ>0THENA(6)=Z-H:Z=0:H=ABS(H)
55 IFA(6)<1426THENPRINTTAB(9);"*SHIELDS LOW*"
60 PRINT"BANKS";INT(G);TAB(16);"SHIELDS";INT(A(6))
61 VV$=B$+STR$(INT(A(16))):PRINTTAB((32-LEN(VV$))/2);VV$
62 PRINTTAB(12);"COMMAND"
70 C$=INKEY$:IFC$=""THEN70
71 IFC$=" "ORVAL(C$)=0THEN70
72 C=VAL(C$):CLS:M$=A$:IFC<4THENM$=M$+"FIRING "
75 ONCGOSUB130,135,150,170,175,185,15,190,195:GOTO205
78 REM
79 REM KLINGONS LOGIC ROUTINE
80 W=A(11)+A(12):Y=A(14)+A(15):M$=B$:IFS>1THENM$=M$+"#"+STR$(K7)+" "
85 IFG<=A(16)THEN90
86 IFA(16)<499OR(W=0ANDA(13)=0ANDY=0)THEN370
90 M=RND(A)*U:IFR>=M+L*10THEN110
95 IFA(13)=1ANDR>L*5ANDB<111THEN120
100 IFW>0ANDR<=M/4+LTHEN115
101 IFW>0THEN110
105 IFA(16)>UANDA(13)=1THEN125
106 IFY>0THENC=5:GOSUB175:GOTO205
107 C=6:GOTO75
110 IFA(14)>0THENC=4:QR=21:GOSUB170:QR=0:B=0:Q=150:GOTO205
111 GOTO105
115 IFW>0THENM$=M$+"FIRING PHASER":GOSUB8:Q=780:GOSUB140:Q=500:C=1:GOTO205
116 GOTO95
120 IFA(13)=0ORA(16)<UTHEN100
125 M$=M$+"FIRING PLASMA WEAPON":GOSUB8:C=3:B=55:Q=1425:GOSUB160:Q=U:GOTO205
130 M$=M$+"FORWARD PHASERS":GOSUB8:IFA(1)=0THEN180
131 Q=700:IFB>90THEN155
132 GOTO140
135 M$=M$+"REAR PHASERS":GOSUB8:IFA(2)=0THEN180
136 Q=350:IFB<90THEN155
140 IFR>LTHENH=Q*(L/R):RETURN
145 H=Q+Q*(1-R/L):RETURN
150 M$=M$+"PHOTON TORPEDO":GOSUB8:IFA(3)=0THEN180
151 GOSUB960:Q=U:IFB<=90THEN160
155 H=0:RETURN
160 IFR>5*LANDR<=6.8*LTHENH=Q:RETURN
161 IFR>6.8*LTHENH=Q*.8*5*L/R:RETURN
162 IFI=0THENCLS:FORC1=1TO10:CLS1+RND(7):PRINT@228,"DANGER REFLECTIVE ENERGY";:GOSUB930:CLS:GOSUB940:NEXTC1
165 F=1:H=RND(Q)+200:PRINT"REFLECTION DATA":GOTO225
170 IFJ=0ANDA(4)=0THENM$=M$+"IMPULSE DRIVE":GOSUB8:GOTO180
171 M$=M$+"CLOSING IN":GOSUB8:R=R/2:B=0:Q=150:RETURN
175 IFJ=0ANDA(4)=0ANDA(5)=0THENM$=M$+"ENGINES":GOSUB8:GOTO180
176 M$=M$+"STEERING AWAY":GOSUB8:R=R*(1+A(5+I)+.05*A(4+I)):Q=350:RETURN
180 PRINTTAB(6);"*SYSTEM INOPERATIVE*":GOTO50
185 M$=M$+"STANDING BY":GOSUB8:Q=10:RETURN
190 M$=M$+"REACTIVATING DILITHIUM CRYSTALS":GOSUB8:Q=-INT(1575*((100E3-G)/100E3)):Z=A(6):A(6)=Z/4:RETURN
195 M$="":PRINT"SPECIFY SYSTEM TO BE REPAIRED ";:H$=""
200 H$=INKEY$:IFH$=""THEN200
201 PRINTH$:A(VAL(H$))=1:Q=650:RETURN
203 REM
204 REM ATTACK ASSESSMENT
205 IFI=0THEN207
206 GOTO210
207 G=G-Q:IFG<0THEN385
208 GOTO215
210 A(16)=A(16)-Q:IFA(16)<0THEN385
211 IFC<3ANDA(6)-H<=0ANDA(6)<600THEN2000
215 IFC=5THENM$="CONTACT ":GOTO217
216 GOTO220
217 IFR>1E8THENM$=M$+"LOST":GOSUB8:X=X+1:GOTO30
218 M$=M$+"NOT LOST":GOSUB8
220 IFI=0THENJ=10:GOTO225
221 J=0
225 IFH>A*2THENH=A*2
230 D=RND(H):IFC>3THENH=0:GOTO400
231 IFJ=0THENPRINT"DAMAGE REPORT: ";A$:GOTO235
232 GOTO3000
233 PRINT"STANDBY--RECEIVING VISUAL":PRINT"FROM A ";B$;"VESSEL":GOSUB960:K5=1:CLS0:GOSUB1770
234 CLS0:PRINT@170,K1$;:GOSUB960:GOSUB640:GOSUB930:GOTO390
235 PRINT"HIT BY";INT(H);"UNITS OF ENERGY"
240 A(6+J)=A(6+J)-H:IFA(6+J)<0THEN385
241 IFD<300THENPRINT"MINOR DAMAGE--SHIELDS HOLDING":GOTO400
242 IFD<900ORA(3+J)=0OR(A(1+J)=0ANDA(2+J)=0)THENM$="":GOTO260
243 FORC1=1TO3:A(C1+J)=0:NEXT:M$="WEAPONS SYSTEM "
245 IFA(5+J)=1ORA(4+J)=1THENM$=M$+"AND ":GOTO260
246 GOTO365
260 IFD<800ORA(5+J)=0THEN280
261 A(5+J)=0:M$=M$+"WARP DRIVE":GOTO365
280 IFD<700THEN300
281 IFA(3+J)=1THEN285
282 GOTO290
285 A(3+J)=0:IFJ=0THENM$=M$+"TORPEDO TUBES":GOTO365
286 M$=M$+"PLASMA WEAPON":GOTO365
290 IFRND(3)=1THEN320
300 IFD<500ORA(4+J)=0THEN320
301 A(4+J)=0:M$=M$+"IMPULSE DRIVE":GOTO365
320 IFD>399ANDA(1+J)=1THENA(1+J)=0:M$="FORWARD":GOTO360
321 IFA(2+J)=1THENA(2+J)=0:M$="REAR":GOTO360
322 B3=B*2:IFB3<90THENM$="FORWARD":GOTO330
326 IFB3<180THENM$="STARBOARD":GOTO330
327 IFB3<270THENM$="AFT":GOTO330
328 M$="PORT"
330 M$=M$+" SHIELDS ":IFA(6+J)<400THENM$=M$+"DEPLETED":GOSUB8:GOTO340
331 IFA(6+J)<2000THENM$=M$+"DISRUPTED":GOSUB8:GOTO340
332 IFA(6+J)<3000THENM$=M$+"BUCKLING":GOSUB8:GOTO340
333 M$=M$+"LOSING PHASE":GOSUB8
340 D=0:GOTO400
360 M$=M$+" PHASERS"
365 M$=M$+" DAMAGED":GOSUB8:GOTO400
370 GOSUB900:PRINT@224,"DANGER ";B$;"SELF-DESTRUCTED";:GOSUB910:CLS0:PRINT@232,K$;:GOSUB920
371 GOSUB1450:Q=A(16):GOSUB140:K=1:IFA(6)-H<0THENX=X-1
380 C=1:J=0:GOTO225
385 GOSUB910
390 GOSUB1450:CLS:PRINT@229," ";:IFJ=0THENPRINTA$;:X=X+1:GOTO395
391 PRINTB$;
395 PRINT"DESTROYED!":SOUND10,2:SOUND1,1:SOUND5,2:GOTO30
397 REM
398 REM KLINGON RANGE & POSITION ROUTINE
400 IFF=1THEN403
401 GOTO405
403 F=0:GOSUB910:IFI=10THENH=1425:RETURN
404 H=U:RETURN
405 T1=.5*(A(4)+A(14)+.05*(A(5)+A(15))):IFK=1THENGOSUB900:GOTO30
407 R=ABS(R+T1*(RND(0)-.5)*L):B=INT(ABS(B-150+20*(RND(0)))):IFB>180THENB=ABS(360-B)
410 IFJ=0THENI=0:GOTO50
411 I=10:J=10:GOTO80
498 REM
499 REM CONGRATULATIONS
500 PRINT@448,"AN EXCELLENT DISPLAY OF":PRINT"TACTICAL ABILITY CAPTAIN":GOSUB905:GOSUB4000
501 PRINT@448,"TAKE ANOTHER TURN AROUND THE":PRINT"OLD GALAXY (Y/N)";
505 INPUTF$:IFF$="NO"ORF$="N"THENEND
506 A3=1:GOTO1510
508 REM
509 REM INSULTS
510 IS=RND(10):ON IS GOTO 515,520,525,530,535,540,545,550,555,560
515 PRINT@448,"WAS THAT EVER A DISPLAY OF ":PRINT"TACTICAL POWERS--YUK!":GOTO565
520 PRINT@448,"ANOTHER BRILLIANT ROUND    ":PRINT"CAPTAIN":GOTO565
525 PRINT@448,"OH, NOW THAT WAS           ":PRINT"GOOOOOOOOOOOOOOOOD!":GOTO565
530 PRINT@448,"AH, LEADING WITH YOUR CHIN ":PRINT"AGAIN I SEE":GOTO565
535 PRINT@448,"STARFLEET COMMAND FROWNS ON":PRINT"ON THOSE WHO BREAK THEIR SHIPS":GOTO565
540 PRINT@448,"IN THE IMMORTAL WORDS OF MR":PRINT"SPOCK 'BOY ARE YOU A TURKEY!'":GOTO565
545 PRINT@448,"HMMMM, CAPTAIN CRASH":PRINT"I PRESUME!!!!!!":GOTO565
550 PRINT@448,"A SPECIAL COMMUNIQUE FROM  ":PRINT"THE FEDERATION--NERD!":GOTO565
555 PRINT@448,"ABSOLUTELY BRILLIANT--WITH ":PRINT"YOU WHO NEEDS KLINGONS!":GOTO565
560 PRINT@448,"WONDERFUL, ANOTHER DAZZLING":PRINT"DISPLAY OF TACTICAL BRILLIANCE"
565 GOSUB905:GOSUB4000:PRINT@448,"WANT TO TRY AND SAVE FACE";:GOTO505
598 REM
599 REM KLINGON GRAPHICS
600 CLS0:PRINT@75,K1$;:IFK4=1THENRETURN
601 GOSUB960
620 POKE16559,195:FORT=1TO5:NEXT:POKE16559,128:POKE16591,194:GOSUB970:POKE16591,128:POKE16623,200:GOSUB970:POKE16623,193
621 GOSUB970:POKE16623,128:POKE16655,202:GOSUB970:POKE16655,128
622 POKE16687,202:GOSUB970:POKE16687,128:POKE16719,203:GOSUB970:POKE16719,128
630 POKE16751,207:GOSUB970:POKE16751,128:POKE16783,207:POKE16815,204:GOSUB970:POKE16783,128:POKE16815,128:POKE16815,207
631 POKE16847,207:GOSUB970:POKE16815,128:POKE16847,128:GOSUB930:RETURN
640 POKE16879,207:GOSUB940:POKE16879,128:POKE16847,195:GOSUB940:POKE16847,128:POKE16815,203:GOSUB940:POKE16815,128
641 POKE16783,202:GOSUB940:POKE16783,128:POKE16751,200:GOSUB940:POKE16751,128:POKE16719,194:GOSUB940:POKE16719,128
645 POKE16719,196:GOSUB940:POKE16719,128:POKE16655,194:GOSUB940:POKE16655,128:POKE16623,193:GOSUB940:POKE16623,128:RETURN
650 CLS0:B=1:C=128:FORT=1TO10:PRINT@RND(510),"д";:NEXT:POKE16623,193:GOSUB920
655 POKE16623,202:GOSUB940:POKE16623,144:GOSUB940:POKE16655,160:GOSUB940:POKE16655,C:POKE16719,196:GOSUB940:POKE16719,C
656 POKE16719,194:GOSUB940:POKE16719,C:POKE16751,200:GOSUB940:POKE16751,C
657 POKE16783,202:GOSUB940:POKE16783,C
660 POKE16815,203:GOSUB940:POKE16815,C:POKE16847,195:GOSUB940:POKE16847,C:POKE16879,207:GOSUB940:POKE16879,C
661 IFB=1THENB=0:GOSUB920:GOSUB655:GOTO800
662 B3=1:GOTO1450
800 FORT=1TO250:NEXT:CLS:RETURN
900 FORT=1TO4000:NEXT:CLS:RETURN
905 FORT=1TO4000:IFINKEY$=""THENNEXT:RETURN
906 T=4000:NEXT:RETURN
910 FORT=1TO2000:NEXT:RETURN
920 FORT=1TO300:NEXT:RETURN
930 FORT=1TO200:NEXT:RETURN
940 FORT=1TO50:NEXT:RETURN
950 FORT=1TO2:NEXT:RETURN
960 FORT=1TO1500:NEXT:RETURN
970 FORT=1TO25:NEXT:RETURN
1448 REM
1449 REM BOOM MAKER GRAPHICS
1450 CLS0:PRINT@240,"л";:PRINT@239,"ллл";:PRINT@238,"ллллл";:PRINT@237,"ллллллл";:PRINT@236,"ллллллллл";
1451 PRINT@231,"ллллллллллллллллллл";:PRINT@237,"ооооооо";:SOUND200,3
1452 PRINT@237,"ЪЪЪЪЪЪЪ";:PRINT@207,"©©©";:PRINT@239,"©©©";:PRINT@271,"©©©";:SOUND10,1:SOUND1,1:SOUND7,1
1453 CLS0:PRINT@235,"ллллллллллллллллллл";
1455 PRINT@224,"лллллллллллллллллллллллллллллллл";:GOSUB950:CLS0:IFB3=1THENB3=0:RETURN
1460 FORT=1TO15:PRINT@200+RND(100),"а";:NEXTT:FORT=1TO200:PRINT@200+RND(100),CHR$(128);:NEXT:CLS:RETURN
1498 REM
1499 REM INTRODUCTION & SETUP
1500 CLS0:SOUND1,1:PRINT@237,"SPACE";:GOSUB910:PRINT@231,"THE FINAL FRONTIER";:GOSUB910
1510 PRINT@16,"д";:GOSUB950:CLS0:PRINT@16,"а";:GOSUB950:CLS0:PRINT@48,"д";:GOSUB950:CLS0:PRINT@48,"а";:GOSUB950
1511 CLS0:PRINT@80,"д";:GOSUB950:CLS0:PRINT@80,"а";:GOSUB950:CLS0:PRINT@80,"ц";:GOSUB950:CLS0:PRINT@112,"л";:GOSUB950:CLS0
1515 PRINT@112,"ц";:GOSUB940:CLS0:PRINT@111,"цц";:GOSUB950:CLS0:PRINT@143,"лл";:GOSUB950:CLS0:PRINT@143,"ццц";:GOSUB950
1516 CLS0:PRINT@143,"ллл";:GOSUB950:CLS0:PRINT@174,"ллллл";:GOSUB950:CLS0:PRINT@174,"ццццц";
1520 GOSUB950:CLS0:PRINT@173,"цццццццццццц";:PRINT@205,"лллллллллллл";:GOSUB950:CLS0:PRINT@172,"оооооооооооооо";
1521 GOSUB950:CLS0:PRINT@204,"оооооооооооооо";:GOSUB950:CLS0:PRINT@234,"оооооооооооооооооооо";
1522 PRINT@266,"оооооооооооооооооооо";:PRINT@298,"оооооооооооооооооооо";:GOSUB940
1550 CLS0:PRINT@138,"STAR TREK";:PRINT@199,"ENCOUNTER IN THE";:PRINT@261,"NEAR THOLIAN SECTOR";
1551 PRINT@322,"ENTER A STARDATE (XXXX)";:INPUTA:A=ABS(A):IFA<1000ORA>9999THENA=9999
1560 SD=SD+A:Z=0:K7=0
1570 IFA3=1THEN20
1595 A3=1:FORT=1TO40:C1=RND(510):IFPEEK(16384+C1)=128THENPRINT@C1,"д";
1596 NEXT:GOSUB910
1600 PRINT@150/2-9,"A TREATY OF PEACE BETWEEN  ";:PRINT@214/2-9,"THE KLINGON EMPIRE AND THE ";
1601 PRINT@278/2-9,"FEDERATION OF PLANETS HAS  ";:PRINT@342/2-9,"LASTED SINCE ORGANIAN STAR ";
1602 PRINT@406/2-9,"DATE 3199.5  HOWEVER, WITH ";:PRINT@470/2-9,"THE PASSING OF AYELBORNE,  ";
1610 PRINT@534/2-9,"THE KLINGONS HAVE OPENLY   ";:PRINT@598/2-9,"PROVOKED HOSTILITIES WITH  ";
1611 PRINT@662/2-9,"FEDERATION SHIPPING, AND   ппп";:PRINT@726/2-9,"HAVE ILLEGALLY CONFISCATED ";
1612 PRINT@790/2-9,"A VAST AMOUNT OF CARGO. THE";:PRINT@854/2-9,"SPECTRE OF WAR AGAIN LOOMS ";
1620 PRINT@918/2-9,"OVER THE ENTIRE GALAXY!    ";:FORT=1TO4000
1623 FY$=INKEY$:IFFY$<>""THENT=4000
1624 NEXTT
1640 E$="сссъъсссрп÷ъъъъъъъъчппппппппппппппппптэъъсрппъпппппппппппппппппппппппппщъъъъъъъэ"
1650 A$="ENTERPRISE ":B$="KLINGON ":U=1000:L=U*U
1660 K$="╫©╪╪╪╪╪©©©©©©©╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╢©©©©╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╢©©©©©©╡"
1670 K1$="╠╥╪╪©╬╪╪╩╡╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟©╟╟╟╣╟╟╟╟©╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╟╢©╬"
1750 CLS0:FORT=1TO50:PRINT@RND(510),"д";:NEXTT:PRINT@2,"SHIP'S LOG: STARDATE";A+.1;
1751 PRINT@34, "THE ENTERPRISE IS IN DOCK AT";:PRINT@66 ,"STARBASE 12. MR SPOCK AND   ";
1753 PRINT@98, "LT. SULU ARE SUPERVISING    ";:PRINT@130,"REPAIRS WHILE LT. UHURA     ";
1755 PRINT@162,"RUNS COM CHECKS. MR SCOTT   ";:PRINT@194,"AND MR CHEKOV ARE CONDUCTING";
1757 PRINT@226,"FIRING TESTS.               ";
1760 PRINT@288+11,E$;
1765 REM ENTERPRISE GRAPHICS
1770 PRINT@288+11,E$;:FORC=1TO3:PRINT@298,"о";:SOUND240,1:FORT=297TO288STEP-1:PRINT@T,"ц╟";:FORZZ=1TO20:NEXT:NEXTT
1771 PRINT@288,"╟";:GOSUB920:NEXTC
1772 IFK5=1THENK5=0:RETURN
1780 GOSUB905:CLS3:FORX=1TO3:PRINT@110,"BEEP";:SOUND200,5:GOSUB940:CLSRND(7)+1:GOSUB950:NEXT:GOSUB920
1785 PRINT@226,"CAPTAIN, WE'RE RECEIVING A ";:PRINT@258,"MESSAGE FROM STARFLEET HQ  ";
1786 PRINT@290,"I'LL PIPE IT TO YOUR VIEWER";:GOSUB910
1800 FL=0:CLS:M$="COMMUNIQUE ":GOSUB1:IFINKEY$<>""THEN1820
1801 M$="FROM STARFLEET COMMAND ":GOSUB1:IFINKEY$<>""THEN1820
1802 M$="STARDATE"+STR$(A):GOSUB1:IFINKEY$<>""THEN1820
1803 M$="ALERT STATUS ":GOSUB1:IFINKEY$<>""THEN1820
1804 M$="KLINGON BATTLE CRUISERS HAVE BEEN CONDUCTING SORTIES ACROSS THE NEUTRAL ZONE AGAINST FEDERATION "
1805 M$=M$+"OUTPOST COLONIES ALONG THE THOLIAN AND GOTHOSIAN SECTORS. ":GOSUB1:IFINKEY$<>""THEN1820
1806 M$="OUTPOSTS 7 AND 8 HAVE BEEN DESTROYED AS WELL AS FEDERATION STARSHIPS VALIANT AND CONSTELLATION. ":GOSUB1
1807 M$="YOUR MISSION IS TO IMMEDIATELY LEAVE STARBASE 12 AND MAKE FOR THE OUTPOST ASTEROIDS AND PATROL "
1808 M$=M$+"THE NEAR THOLIAN SECTOR. ":GOSUB1:IFINKEY$<>""THEN1820
1809 M$="YOU ARE TO ENGAGE AND DESTROY THE KLINGON RAIDING PARTIES OPERATING IN THAT SECTOR. "
1810 M$=M$+"FAILURE WILL MEAN THE LOSS OF THE FEDERATION. GOOD LUCK! ":GOSUB1:GOSUB900
1820 CLS:PRINTTAB(9);"USS ENTERPRISE":PRINTTAB(9);"--------------"
1821 PRINT"CAPTAIN'S LOG";TAB(16);"STARDATE";STR$(A+.3):PRINT:FL=1
1822 M$="THE ENTERPRISE IS NOW ENROUTE TO THOLIAN SPACE TO RELIEVE THE EMBATTLED FEDERATION COLONIES. ":GOSUB8
1824 M$="A GLIMPSE OF THE LAST GARBLED MESSAGE FROM OUTPOST 7 IS ALL THAT IS NECESSARY TO REMIND US ALL OF THE "
1825 M$=M$+"VICIOUSNESS OF THE ENEMY.":GOSUB8
1830 PRINT"LOG 2  SCAN OF KLINGON SHIP":PRINT"LOG 3  LAST TRANSMISSION":PRINT"LOG 4  ENTERPRISE COMMANDS"
1831 PRINT"    5  CLOSE LOG";
1832 F$=" ":F$=INKEY$:F=VAL(F$):IFF<2ORF>5THEN1832
1833 IFF=3THEN1900
1834 IFF=5THEN20
1835 IFF=2THEN1840
1836 CLS:PRINT"ENTERPRISE CONTROL COMMANDS":PRINT"1. FORWARD PHASERS":PRINT"2. REAR PHASERS":PRINT"3. PHOTON TORPEDOES"
1837 PRINT"4. CLOSE ON ENEMY VESSEL":PRINT"5. PULL AWAY FROM ENEMY":PRINT"6. STAND BY":PRINT"7. ENERGY TO SHIELDS"
1838 PRINT"8. RECHARGE DILITHIUM CRYSTALS":PRINT"9. REPAIR":GOSUB905:GOTO1820
1840 CLS:PRINT"KLINGON BATTLE CRUISER"
1841 PRINT"CIRCA ORGANIAN STARDATE 3199.5"
1842 PRINT"           TOP VIEW":PRINT
1844 PRINT"             ┼── "CHR$(95)" BRIDGE AND"
1845 PRINT"            / ┼    2 PHASORS"
1846 PRINT"      PLASMA  ┼ "
1847 PRINT"      WEAPON  ─┘"
1848 PRINT"              ─┘"
1849 PRINT"         ▌  ▄───└  █"
1850 PRINT"         ┼─────────┘"
1851 PRINT"         ┼┐┐┐───│┐┐┘  STARBOARD"
1852 PRINT"         ┼    ^    ┘"CHR$(95)" NACELLE"
1853 PRINT"        IMPLUSE ENGINE"
1854 PRINT"      AND SINGLE PHASOR"
1865 GOSUB905:GOTO1820
1900 CLS:PRINT"LAST TRANSMISSION FROM OUTPOST 7--------------------------------":GOSUB910
1901 PRINT"OUTPOST 7 CALLING STARFLEET     COMMAND ON PRIORITY CHANNEL 1":GOSUB910
1905 PRINT"STARFLEET COME IN":GOSUB910:PRINT"...STARFLEET DO YOU HEAR US?":GOSUB910
1906 PRINT"OUTPOST 7 CALLING STARFLEET     COMMAND--ACKNOWLEDGE!":GOSUB910
1908 PRINT"STARFLEET WE ARE UNDER HEAVY    ATTACK....ENEMY UNKNOWN!":GOSUB910:SOUND1,3
1910 PRINT"OUR TRANSMISSIONS ARE BEING     JAMMED (CRACKLE)...":GOSUB910:PRINT"...EXPLOSIONS (CRACKLE)"
1911 PRINT"...WE...WAIT!"
1912 GOSUB910:PRINT"WE'RE RECEIVING A (CRACKLE)":GOSUB910:PRINT"(CRACKLE)":SOUND5,1:SOUND1,1:SOUND2,1
1915 GOSUB910:GOSUB910:PRINT"WE CAN SEE SOMETHING ON THE":PRINT"MAIN SCREEN--STAND BY":GOSUB910:PRINT"(CRACKLE)"
1916 GOSUB910:PRINT"MY GOD, THEY'RE KLINGONS!"
1918 GOSUB910:PRINT"WE...CAN'T SUSTAIN ANOTHER HIT!"
1920 GOSUB910:PRINT"SWITCHING TO MAIN VIEWER ":GOSUB910:PRINT"...STARFLEET DO YOU COPY?":GOSUB910
1921 PRINT"(CRACKLE)";:GOSUB900
1930 CLS:PRINT@192,"";TAB(11);"EARTHLINGS":PRINTTAB(3);"PREPARE TO MEET YOUR DOOM":PRINTTAB(2);
1931 PRINT"AT THE HANDS OF THE KLINGON"
1932 PRINTTAB(13);"EMPIRE":GOSUB910:GOSUB910:CLS0:GOSUB600:GOSUB620:IFK3=1THENK3=0:RETURN
1933 GOSUB1450:GOSUB910
1935 PRINT@230,"(CRACKLE)";:GOSUB910:PRINT" (CRACKLE)...";:GOSUB900
1936 PRINT:PRINT"END OF TRANSMISSION":GOSUB910:GOTO1820
2000 K3=1:A(6)=-1:J=0:PRINT"WE ARE RECEIVING A TRANSMISSION":PRINT"FROM THE ";B$;"COMMANDER"
2010 GOSUB900:GOSUB1930:CLS0:PRINT@266,E$;:GOSUB960:GOSUB640:GOSUB930:GOTO390
3000 PRINT"SENSOR REPORT: ";B$:IFA(16)-H>0THEN235
3010 IFR<6E5THEN234
3020 IFK7<SAND(A(11)=0ANDA(12)=0ANDA(13)=0)ORA(16)<349THEN233
3030 GOTO235
4000 CLS:PRINTTAB(9);"USS ENTERPRISE":PRINTTAB(9);"--------------"
4001 PRINT:PRINT"STARDATE:";SD/EN
4003 PRINT:PRINTTAB(7)"PERFORMANCE REVIEW":PRINT
4010 PRINT"VICTORIES:      ";WN
4020 PRINT"DEFEATS:        ";DF
4030 PRINT"ENGAGEMENTS:    ";EN
4050 PRINT"VICTORY AVERAGE:";INT(WN*100/EN);"%"
4055 PRINT"BATTLE SCORE:   ";INT((SD/EN)*INT(WN*100/EN))*EN
4060 RETURN
