0 CLEAR700:DIMA(16),C1,CC,M1$,MC:MC=(1024-16384):C1=RND(-TIMER):GOTO1500
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>"" AND MID$(M$,CC,1)<>" " THENCC=CC-1:GOTO2
3 M1$=MID$(M$,C1,CC-C1):GOSUB6:C1=CC+1:CC=C1+31:IFC1<LEN(M$) THEN2
4 IFFL=0 THENFORZZ=1 TO175:PRINT
5 RETURN
6 FORC2=1 TOLEN(M1$):PRINTMID$(M1$,C2,1);:IFMID$(M1$,C2,1)=" " AND FL=0 THENFORZZ=1 TO175:NEXT
7 NEXT:PRINT:RETURN
8 C1=1:CC=32
9 IFMID$(M$,CC,1)<>"" AND MID$(M$,CC,1)<>" " THENCC=CC-1:GOTO9
10 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$) THEN9
11 RETURN
14 REM DEFLECTOR SHIELDS
15 M$="":INPUT"SPECIFY AMOUNT OF ENERGY TO     TRANSFER TO SHIELDS";H:IFA(6)+H>5*U THEN15
16 IFG-H<0 THENPRINT"INSUFFICENT ENERGY  <"G">":GOTO15
17 G=G-H:A(6)=A(6)+H:Q=10:RETURN
19 REM SHIP SYSTEMS SETUP
20 S=INT(RND(A/U)):X=S:G=RND(A+U)+A*S:FORJ=1 TO5:A(J)=1:NEXT:A(6)=RND(U*5)
25 K=0:FORJ=10 TO15:A(J)=1:NEXT:A(16)=RND(U+A)+A:GOTO40
30 GOSUB900:X=X-1:IFA(6)<0 OR G<0 OR X<=0 THENX=S-X:A=1:GOTO45
31 GOTO25
35 PRINT@96,"OUT OF"S"HAVE BEEN DESTROYED":PRINT@256,"THE FEDERATION ";
36 IFX=S THENPRINT"HAS BEEN SAVED!":WN=WN+1:EN=EN+1:GOTO500
37 PRINT"WILL BE LOST...":PRINTTAB(13);"IDIOT!":DF=DF+1:EN=EN+1:GOTO510
40 R=L*10*RND(A/U):B=INT(180*RND(A/L/10)):F=0:I=0:GOSUB650:FORC=1 TO5:CLS4:PRINT@264,"R E D  A L E R T";
41 GOSUB920:CLS0:GOSUB920:NEXTC:CLS
45 K7=K7+1:PRINT@64,"SENSOR REPORT:";X;B$;
46 IFX=1 THENPRINT"SHIP":GOTO48
47 PRINT"SHIPS"
48 IFA=1 THEN35
49 PRINT@96,"APPROACHING"
50 PRINT:PRINT"RANGE";INT(R);TAB(16);"AZIMUTH";STR$(INT(B*2));"'":IFZ>0 THENA(6)=Z-H:Z=0:H=ABS(H)
55 IFA(6)<1426 THENPRINTTAB(9);"*SHIELDS LOW*"
60 PRINT"BANKS";INT(G);TAB(16);"SHIELDS";INT(A(6))
61 VV$=B$+STR$(INT(A(16))):PRINTTAB((32-LEN(VV$))/2);VV$
62 PRINTTAB(12);"COMMAND"
70 C$=INKEY$:IFC$="" THEN70
71 IFC$=" " OR VAL(C$)=0 THEN70
72 C=VAL(C$):CLS:M$=A$:IFC<4 THENM$=M$+"FIRING "
75 ONC GOSUB130,135,150,170,175,185,15,190,195:GOTO205
78 REM
79 REM KLINGONS LOGIC ROUTINE
80 W=A(11)+A(12):Y=A(14)+A(15):M$=B$:IFS>1 THENM$=M$+"#"+STR$(K7)+" "
85 IFG<=A(16) THEN90
86 IFA(16)<499 OR (W=0 AND A(13)=0 AND Y=0) THEN370
90 M=RND(A)*U:IFR>=M+L*10 THEN110
95 IFA(13)=1 AND R>L*5 AND B<111 THEN120
100 IFW>0 AND R<=M/4+L THEN115
101 IFW>0 THEN110
105 IFA(16)>U AND A(13)=1 THEN125
106 IFY>0 THENC=5:GOSUB175:GOTO205
107 C=6:GOTO75
110 IFA(14)>0 THENC=4:QR=21:GOSUB170:QR=0:B=0:Q=150:GOTO205
111 GOTO105
115 IFW>0 THENM$=M$+"FIRING PHASER":GOSUB8:Q=780:GOSUB140:Q=500:C=1:GOTO205
116 GOTO95
120 IFA(13)=0 OR A(16)<U THEN100
125 M$=M$+"FIRING PLASMA WEAPON":GOSUB8:C=3:B=55:Q=1425:GOSUB160:Q=U:GOTO205
130 M$=M$+"FORWARD PHASERS":GOSUB8:IFA(1)=0 THEN180
131 Q=700:IFB>90 THEN155
132 GOTO140
135 M$=M$+"REAR PHASERS":GOSUB8:IFA(2)=0 THEN180
136 Q=350:IFB<90 THEN155
140 IFR>L THENH=Q*(L/R):RETURN
145 H=Q+Q*(1-R/L):RETURN
150 M$=M$+"PHOTON TORPEDO":GOSUB8:IFA(3)=0 THEN180
151 GOSUB960:Q=U:IFB<=90 THEN160
155 H=0:RETURN
160 IFR>5*L AND R<=6.8*L THENH=Q:RETURN
161 IFR>6.8*L THENH=Q*.8*5*L/R:RETURN
162 IFI=0 THENCLS:FORC1=1 TO10:CLS1+RND(7):PRINT@228,"DANGER REFLECTIVE ENERGY";:GOSUB930:CLS:GOSUB940:NEXTC1
165 F=1:H=RND(Q)+200:PRINT"REFLECTION DATA":GOTO225
170 IFJ=0 AND A(4)=0 THENM$=M$+"IMPULSE DRIVE":GOSUB8:GOTO180
171 M$=M$+"CLOSING IN":GOSUB8:R=R/2:B=0:Q=150:RETURN
175 IFJ=0 AND A(4)=0 AND A(5)=0 THENM$=M$+"ENGINES":GOSUB8:GOTO180
176 M$=M$+"STEERING AWAY":GOSUB8:R=R*(1+A(5+I)+.05*A(4+I)):Q=350:RETURN
180 PRINTTAB(6);"*SYSTEM INOPERATIVE*":GOTO50
185 M$=M$+"STANDING BY":GOSUB8:Q=10:RETURN
190 M$=M$+"REACTIVATING DILITHIUM CRYSTALS":GOSUB8:Q=-INT(1575*((100E3-G)/100E3)):Z=A(6):A(6)=Z/4:RETURN
195 M$="":PRINT"SPECIFY SYSTEM TO BE REPAIRED ";:H$=""
200 H$=INKEY$:IFH$="" THEN200
201 PRINTH$:A(VAL(H$))=1:Q=650:RETURN
203 REM
204 REM ATTACK ASSESSMENT
205 IFI=0 THEN207
206 GOTO210
207 G=G-Q:IFG<0 THEN385
208 GOTO215
210 A(16)=A(16)-Q:IFA(16)<0 THEN385
211 IFC<3 AND A(6)-H<=0 AND A(6)<600 THEN2000
215 IFC=5 THENM$="CONTACT ":GOTO217
216 GOTO220
217 IFR>1E8 THENM$=M$+"LOST":GOSUB8:X=X+1:GOTO30
218 M$=M$+"NOT LOST":GOSUB8
220 IFI=0 THENJ=10:GOTO225
221 J=0
225 IFH>A*2 THENH=A*2
230 D=RND(H):IFC>3 THENH=0:GOTO400
231 IFJ=0 THENPRINT"DAMAGE REPORT: ";A$:GOTO235
232 GOTO3000
233 PRINT"STANDBY--RECEIVING VISUAL":PRINT"FROM A ";B$;"VESSEL":GOSUB960:K5=1:CLS0:GOSUB1770
234 CLS0:PRINT@170,K1$;:GOSUB960:GOSUB640:GOSUB930:GOTO390
235 PRINT"HIT BY";INT(H);"UNITS OF ENERGY"
240 A(6+J)=A(6+J)-H:IFA(6+J)<0 THEN385
241 IFD<300 THENPRINT"MINOR DAMAGE--SHIELDS HOLDING":GOTO400
242 IFD<900 OR A(3+J)=0 OR (A(1+J)=0 AND A(2+J)=0) THENM$="":GOTO260
243 FORC1=1 TO3:A(C1+J)=0:NEXT:M$="WEAPONS SYSTEM "
245 IFA(5+J)=1 OR A(4+J)=1 THENM$=M$+"AND ":GOTO260
246 GOTO365
260 IFD<800 OR A(5+J)=0 THEN280
261 A(5+J)=0:M$=M$+"WARP DRIVE":GOTO365
280 IFD<700 THEN300
281 IFA(3+J)=1 THEN285
282 GOTO290
285 A(3+J)=0:IFJ=0 THENM$=M$+" TORPEDO TUBES":GOTO365
286 M$=M$+"PLASMA WEAPON":GOTO365
290 IFRND(3)=1 THEN320
300 IFD<500 OR A(4+J)=0 THEN320
301 A(4+J)=0:M$=M$+"IMPULSE DRIVE":GOTO365
320 IFD>399 AND A(1+J)=1 THENA(1+J)=0:M$="FORWARD":GOTO360
321 IFA(2+J)=1 THENA(2+J)=0:M$="REAR":GOTO360
322 B3=B*2:IFB3<90 THENM$="FORWARD":GOTO330
326 IFB3<180 THENM$="STARBOARD":GOTO330
327 IFB3<270 THENM$="AFT":GOTO330
328 M$="PORT"
330 M$=M$+" SHIELDS ":IFA(6+J)<400 THENM$=M$+"DEPLETED":GOSUB8:GOTO340
331 IFA(6+J)<2000 THENM$=M$+"DISRUPTED":GOSUB8:GOTO340
332 IFA(6+J)<3000 THENM$=M$+"BUCKLING":GOSUB8:GOTO340
333 M$=M$+"LOSING PHASE":GOSUB8
340 D=0:GOTO400
360 M$=M$+" PHASERS"
365 M$=M$+" DAMAGED":GOSUB8:GOTO400
370 GOSUB900:PRINT@224,"DANGER ";B$;"SELF-DESTRUCTED";:GOSUB910:CLS0:PRINT@232,K$;:GOSUB920
371 GOSUB1450:Q=A(16):GOSUB140:K=1:IFA(6)-H<0 THENX=X-1
380 C=1:J=0:GOTO225
385 GOSUB910
390 GOSUB1450:CLS:PRINT@229," ";:IFJ=0 THENPRINTA$;:X=X+1:GOTO395
391 PRINTB$;
395 PRINT"DESTROYED!":SOUND10,2:SOUND1,1:SOUND5,2:GOTO30
397 REM
398 REM KLINGON RANGE & POSITION ROUTINE
400 IFF=1 THEN403
401 GOTO405
403 F=0:GOSUB910:IFI=10 THENH=1425:RETURN
404 H=U:RETURN
405 T1=.5*(A(4)+A(14)+.05*(A(5)+A(15))):IFK=1 THEN GOSUB900:GOTO30
407 R=ABS(R+T1*(RND(0)-.5)*L):B=INT(ABS(B-150+20*(RND(0)))):IFB>180 THENB=ABS(360-B)
410 IFJ=0 THENI=0:GOTO50
411 I=10:J=10:GOTO80
498 REM
499 REM CONGRATULATIONS
500 PRINT@448,"AN EXCELLENT DISPLAY OF":PRINT"TACTICAL ABILITY CAPTAIN":GOSUB905:GOSUB4000
501 PRINT@448,"TAKE ANOTHER TURN AROUND THE":PRINT"OLD GALAXY (Y/N)";
505 INPUTF$:IFF$="NO" OR F$="N" THENEND
506 A3=1:GOTO1510
508 REM
509 REM INSULTS
510 IS=RND(10):ON IS GOTO 515,520,525,530,535,540,545,550,555,560
515 PRINT@448,"WAS THAT EVER A DISPLAY OF ":PRINT"TACTICAL POWERS--YUK!":GOTO565
520 PRINT@448,"ANOTHER BRILLIANT ROUND    ":PRINT"CAPTAIN":GOTO565
525 PRINT@448,"OH, NOW THAT WAS           ":PRINT"GOOOOOOOOOOOOOOOOD!":GOTO565
530 PRINT@448,"AH, LEADING WITH YOUR CHIN ":PRINT"AGAIN I SEE":GOTO565
535 PRINT@448,"STARFLEET COMMAND FROWNS ON":PRINT"ON THOSE WHO BREAK THEIR SHIPS":GOTO565
540 PRINT@448,"IN THE IMMORTAL WORDS OF MR":PRINT"SPOCK 'BOY ARE YOU A TURKEY!'":GOTO565
545 PRINT@448,"HMMMM, CAPTAIN CRASH":PRINT"I PRESUME!!!!!!":GOTO565
550 PRINT@448,"A SPECIAL COMMUNIQUE FROM  ":PRINT"THE FEDERATION--NERD!":GOTO565
555 PRINT@448,"ABSOLUTELY BRILLIANT--WITH ":PRINT"YOU WHO NEEDS KLINGONS!":GOTO565
560 PRINT@448,"WONDERFUL, ANOTHER DAZZLING":PRINT"DISPLAY OF TACTICAL BRILLIANCE"
565 GOSUB905:GOSUB4000:PRINT@448,"WANT TO TRY AND SAVE FACE";:GOTO505
598 REM
599 REM KLINGON GRAPHICS
600 CLS0:PRINT@75,K1$;:IFK4=1 THENRETURN
601 GOSUB960
620 POKEMC+16559,195:FORT=1 TO5:NEXT:POKEMC+16559,128:POKEMC+16591,194:GOSUB970:POKEMC+16591,128:POKEMC+16623,200:GOSUB970:POKEMC+16623,193
621 GOSUB970:POKEMC+16623,128:POKEMC+16655,202:GOSUB970:POKEMC+16655,128
622 POKEMC+16687,202:GOSUB970:POKEMC+16687,128:POKEMC+16719,203:GOSUB970:POKEMC+16719,128
630 POKEMC+16751,207:GOSUB970:POKEMC+16751,128:POKEMC+16783,207:POKEMC+16815,204:GOSUB970:POKEMC+16783,128:POKEMC+16815,128:POKEMC+16815,207
631 POKEMC+16847,207:GOSUB970:POKEMC+16815,128:POKEMC+16847,128:GOSUB930:RETURN
640 POKEMC+16879,207:GOSUB940:POKEMC+16879,128:POKEMC+16847,195:GOSUB940:POKEMC+16847,128:POKEMC+16815,203:GOSUB940:POKEMC+16815,128
641 POKEMC+16783,202:GOSUB940:POKEMC+16783,128:POKEMC+16751,200:GOSUB940:POKEMC+16751,128:POKEMC+16719,194:GOSUB940:POKEMC+16719,128
645 POKEMC+16719,196:GOSUB940:POKEMC+16719,128:POKEMC+16655,194:GOSUB940:POKEMC+16655,128:POKEMC+16623,193:GOSUB940:POKEMC+16623,128:RETURN
650 CLS0:B=1:C=128:FORT=1 TO10:PRINT@RND(510),CHR$(196);:NEXT:POKEMC+16623,193:GOSUB920
655 POKEMC+16623,202:GOSUB940:POKEMC+16623,144:GOSUB940:POKEMC+16655,160:GOSUB940:POKEMC+16655,C:POKEMC+16719,196:GOSUB940:POKEMC+16719,C
656 POKEMC+16719,194:GOSUB940:POKEMC+16719,C:POKEMC+16751,200:GOSUB940:POKEMC+16751,C
657 POKEMC+16783,202:GOSUB940:POKEMC+16783,C
660 POKEMC+16815,203:GOSUB940:POKEMC+16815,C:POKEMC+16847,195:GOSUB940:POKEMC+16847,C:POKEMC+16879,207:GOSUB940:POKEMC+16879,C
661 IFB=1 THENB=0:GOSUB920:GOSUB655:GOTO800
662 B3=1:GOTO1450
800 FORT=1 TO250:NEXT:CLS:RETURN
900 FORT=1 TO4000:NEXT:CLS:RETURN
905 FORT=1 TO4000:IFINKEY$="" THENNEXT:RETURN
906 T=4000:NEXT:RETURN
910 FORT=1 TO2000:NEXT:RETURN
920 FORT=1 TO300:NEXT:RETURN
930 FORT=1 TO200:NEXT:RETURN
940 FORT=1 TO50:NEXT:RETURN
950 FORT=1 TO2:NEXT:RETURN
960 FORT=1 TO1500:NEXT:RETURN
970 FORT=1 TO25:NEXT:RETURN
1448 REM
1449 REM BOOM MAKER GRAPHICS
1450 CLS0:PRINT@240,CHR$(204);:PRINT@239,CHR$(204)CHR$(204)CHR$(204);:PRINT@238,STRING$(5,204);:PRINT@237,STRING$(5,204)CHR$(204)CHR$(204);
1451 PRINT@236,STRING$(5,204)CHR$(204)CHR$(204)CHR$(204)CHR$(204);:PRINT@231,STRING$(5,204)STRING$(5,204)STRING$(5,204)CHR$(204)CHR$(204)CHR$(204)CHR$(204);
1452 PRINT@237,STRING$(5,207)CHR$(207)CHR$(207);:SOUND200,3:PRINT@237,CHR$(255)CHR$(255)CHR$(255)CHR$(255)CHR$(255)CHR$(255)CHR$(255);
1453 PRINT@207,CHR$(191)CHR$(191)CHR$(191);:PRINT@239,CHR$(191)CHR$(191)CHR$(191);::PRINT@271,CHR$(191)CHR$(191)CHR$(191);:SOUND10,1:SOUND1,1:SOUND7,1
1454 CLS0:PRINT@235,STRING$(5,204)STRING$(5,204)STRING$(5,204)CHR$(204)CHR$(204)CHR$(204)CHR$(204);
1455 PRINT@224,STRING$(5,204)STRING$(5,204)STRING$(5,204)STRING$(5,204)STRING$(5,204)STRING$(5,204)CHR$(204)CHR$(204);:GOSUB950:CLS0:IFB3=1 THENB3=0:RETURN
1460 FORT=1 TO15:PRINT@200+RND(100),CHR$(193);:NEXTT:FORT=1 TO200:PRINT@200+RND(100),CHR$(128);:NEXT:CLS:RETURN
1498 REM
1499 REM INTRODUCTION & SETUP
1500 CLS0:SOUND1,1:PRINT@237,"SPACE";:GOSUB910:PRINT@231,"THE FINAL FRONTIER";:GOSUB910
1510 PRINT@16,CHR$(196);:GOSUB950:CLS0:PRINT@16,CHR$(193);:GOSUB950:CLS0:PRINT@48,CHR$(196);:GOSUB950:CLS0:PRINT@48,CHR$(193);:GOSUB950
1511 CLS0:PRINT@80,CHR$(196);:GOSUB950:CLS0:PRINT@80,CHR$(193);:GOSUB950:CLS0:PRINT@80,CHR$(195);:GOSUB950:CLS0:PRINT@112,CHR$(204);:GOSUB950:CLS0
1515 PRINT@112,CHR$(195);:GOSUB940:CLS0:PRINT@111,CHR$(195)CHR$(195);:GOSUB950:CLS0:PRINT@143,CHR$(204)CHR$(204);:GOSUB950:CLS0:PRINT@143,STRING$(3,195);:GOSUB950
1516 CLS0:PRINT@143,CHR$(204)CHR$(204)CHR$(204);:GOSUB950:CLS0:PRINT@174,STRING$(5,204);:GOSUB950:CLS0:PRINT@174,STRING$(5,195);
1520 GOSUB950:CLS0:PRINT@173,STRING$(5,195)CHR$(195)CHR$(195)STRING$(5,195);:PRINT@205,STRING$(5,204)STRING$(5,204)CHR$(204)CHR$(204);
1521 GOSUB950:CLS0:PRINT@172,STRING$(5,207)CHR$(207)STRING$(5,207)CHR$(207)CHR$(207)CHR$(207);
1522 GOSUB950:CLS0:PRINT@204,STRING$(5,207)STRING$(5,207)CHR$(207)CHR$(207)CHR$(207)CHR$(207);
1523 GOSUB950:CLS0:PRINT@234,STRING$(5,207)STRING$(5,207)STRING$(5,207)STRING$(5,207);
1524 PRINT@266,STRING$(5,207)STRING$(5,207)STRING$(5,207)STRING$(5,207);
1525 PRINT@298,STRING$(5,207)STRING$(5,207)STRING$(5,207)STRING$(5,207);
1526 GOSUB940
1550 CLS0:PRINT@138,"STAR TREK";:PRINT@199,"ENCOUNTER IN THE";:PRINT@261,"NEAR THOLIAN SECTOR";
1551 PRINT@322,"ENTER A STARDATE (XXXX)";:INPUTA:A=ABS(A):IFA<1000 OR A>9999 THENA=9999
1560 SD=SD+A:Z=0:K7=0
1570 IFA3=1 THEN20
1595 A3=1:FORT=1 TO40:C1=RND(510):IFPEEK(MC+16384+C1)=128 THENPRINT@C1,CHR$(196);
1596 NEXT:GOSUB910
1600 PRINT@150/2-9,"A TREATY OF PEACE BETWEEN  ";:PRINT@214/2-9,"THE KLINGON EMPIRE AND THE ";
1601 PRINT@278/2-9,"FEDERATION OF PLANETS HAS  ";:PRINT@342/2-9,"LASTED SINCE ORGANIAN STAR ";
1602 PRINT@406/2-9,"DATE 3199.5  HOWEVER, WITH ";:PRINT@470/2-9,"THE PASSING OF AYELBORNE,  ";
1610 PRINT@534/2-9,"THE KLINGONS HAVE OPENLY   ";:PRINT@598/2-9,"PROVOKED HOSTILITIES WITH  ";
1611 PRINT@662/2-9,"FEDERATION SHIPPING, AND   "CHR$(208)CHR$(208)CHR$(208);:PRINT@726/2-9,"HAVE ILLEGALLY CONFISCATED ";
1612 PRINT@790/2-9,"A VAST AMOUNT OF CARGO. THE";:PRINT@854/2-9,"SPECTRE OF WAR AGAIN LOOMS ";
1620 PRINT@918/2-9,"OVER THE ENTIRE GALAXY!    ";:FORT=1 TO4000
1623 FY$=INKEY$:IFFY$<>"" THENT=4000
1624 NEXTT
1640 E$=CHR$(211)+CHR$(211)+CHR$(211)+CHR$(223)+CHR$(223)+CHR$(211)+CHR$(211)+CHR$(211)+CHR$(210)+CHR$(208)+CHR$(159)
1641 E$=E$+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(222)+CHR$(208)+CHR$(208)
1642 E$=E$+STRING$(5,208)+STRING$(5,208)+CHR$(208)
1643 E$=E$+CHR$(208)+CHR$(208)+CHR$(208)+CHR$(208)+CHR$(212)+CHR$(220)+CHR$(223)+CHR$(223)+CHR$(211)+CHR$(210)+CHR$(208)
1644 E$=E$+CHR$(208)+CHR$(223)+STRING$(5,208)+CHR$(208)+CHR$(208)+CHR$(208)+CHR$(208)
1645 E$=E$+STRING$(5,208)+STRING$(5,208)+CHR$(208)
1646 E$=E$+STRING$(5,208)+CHR$(221)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)
1647 E$=E$+CHR$(223)+CHR$(223)+CHR$(220)
1650 A$="ENTERPRISE ":B$="KLINGON ":U=1000:L=U*U
1660 K$=CHR$(189)+CHR$(191)+CHR$(188)+CHR$(188)+CHR$(188)+CHR$(188)+CHR$(188)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
1661 K$=K$+CHR$(191)+CHR$(191)+CHR$(191)+STRING$(5,176)+CHR$(176)+CHR$(176)+CHR$(176)
1662 K$=K$+STRING$(5,176)+STRING$(5,176)+CHR$(176)
1663 K$=K$+STRING$(5,176)+CHR$(176)+CHR$(176)+CHR$(180)+CHR$(191)+CHR$(191)+CHR$(191)
1664 K$=K$+CHR$(191)+STRING$(5,176)+STRING$(5,176)
1665 K$=K$+STRING$(5,176)+STRING$(5,176)+CHR$(176)
1666 K$=K$+STRING$(5,176)+CHR$(180)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
1667 K$=K$+CHR$(191)+CHR$(178)
1670 K1$=CHR$(177)+CHR$(183)+CHR$(188)+CHR$(188)+CHR$(191)+CHR$(190)+CHR$(188)+CHR$(188)+CHR$(187)+CHR$(178)+CHR$(176)
1671 K1$=K1$+STRING$(5,176)+STRING$(5,176)+CHR$(176)
1672 K1$=K1$+STRING$(5,176)+STRING$(5,176)+CHR$(191)
1673 K1$=K1$+CHR$(176)+CHR$(176)+CHR$(176)+CHR$(181)+CHR$(176)+CHR$(176)+CHR$(176)+CHR$(176)+CHR$(191)+CHR$(176)+CHR$(176)
1674 K1$=K1$+STRING$(5,176)+STRING$(5,176)+CHR$(176)
1675 K1$=K1$+STRING$(5,176)+STRING$(5,176)+CHR$(176)
1676 K1$=K1$+CHR$(176)+CHR$(180)+CHR$(191)+CHR$(190)
1750 CLS0:FORT=1 TO50:PRINT@RND(510),CHR$(196);:NEXTT:PRINT@2,"SHIP'S LOG: STARDATE";A+.1;
1751 PRINT@34, "THE ENTERPRISE IS IN DOCK AT";:PRINT@66 ,"STARBASE 12. MR SPOCK AND   ";
1753 PRINT@98, "LT. SULU ARE SUPERVISING    ";:PRINT@130,"REPAIRS WHILE LT. UHURA     ";
1755 PRINT@162,"RUNS COM CHECKS. MR SCOTT   ";:PRINT@194,"AND MR CHEKOV ARE CONDUCTING";
1757 PRINT@226,"FIRING TESTS.               ";
1760 PRINT@288+11,E$;
1765 REM ENTERPRISE GRAPHICS
1770 PRINT@288+11,E$;:FORC=1 TO3:PRINT@298,CHR$(207);:SOUND240,1:FORT=297 TO288 STEP-1:PRINT@T,CHR$(195)+CHR$(176);:FORZZ=1 TO20:NEXT:NEXTT
1771 PRINT@288,CHR$(176);:GOSUB920:NEXTC
1772 IFK5=1 THENK5=0:RETURN
1780 GOSUB905:CLS3:FORX=1 TO3:PRINT@110,"BEEP";:SOUND200,5:GOSUB940:CLSRND(7)+1:GOSUB950:NEXT:GOSUB920
1785 PRINT@226,"CAPTAIN, WE'RE RECEIVING A ";:PRINT@258,"MESSAGE FROM STARFLEET HQ  ";
1786 PRINT@290,"I'LL PIPE IT TO YOUR VIEWER";:GOSUB910
1800 FL=0:CLS:M$="COMMUNIQUE ":GOSUB1:IFINKEY$<>"" THEN1820
1801 M$="FROM STARFLEET COMMAND ":GOSUB1:IFINKEY$<>"" THEN1820
1802 M$="STARDATE"+STR$(A):GOSUB1:IFINKEY$<>"" THEN1820
1803 M$="ALERT STATUS ":GOSUB1:IFINKEY$<>"" THEN1820
1804 M$="KLINGON BATTLE CRUISERS HAVE BEEN CONDUCTING SORTIES ACROSS THE NEUTRAL ZONE AGAINST FEDERATION "
1805 M$=M$+"OUTPOST COLONIES ALONG THE THOLIAN AND GOTHOSIAN SECTORS. ":GOSUB1:IFINKEY$<>"" THEN1820
1806 M$="OUTPOSTS 7 AND 8 HAVE BEEN DESTROYED AS WELL AS FEDERATION STARSHIPS VALIANT AND CONSTELLATION. ":GOSUB1
1807 M$="YOUR MISSION IS TO IMMEDIATELY LEAVE STARBASE 12 AND MAKE FOR THE OUTPOST ASTEROIDS AND PATROL "
1808 M$=M$+"THE NEAR THOLIAN SECTOR. ":GOSUB1:IFINKEY$<>"" THEN1820
1809 M$="YOU ARE TO ENGAGE AND DESTROY THE KLINGON RAIDING PARTIES OPERATING IN THAT SECTOR. "
1810 M$=M$+"FAILURE WILL MEAN THE LOSS OF THE FEDERATION. GOOD LUCK! ":GOSUB1:GOSUB900
1820 CLS:PRINTTAB(9);"USS ENTERPRISE":PRINTTAB(9);"--------------"
1821 PRINT"CAPTAIN'S LOG";TAB(16);"STARDATE";STR$(A+.3):PRINT:FL=1
1822 M$="THE ENTERPRISE IS NOW ENROUTE TO THOLIAN SPACE TO RELIEVE THE EMBATTLED FEDERATION COLONIES. ":GOSUB8
1824 M$="A GLIMPSE OF THE LAST GARBLED MESSAGE FROM OUTPOST 7 IS ALL THAT IS NECESSARY TO REMIND US ALL OF THE "
1825 M$=M$+"VICIOUSNESS OF THE ENEMY.":GOSUB8
1830 PRINT"LOG 2  SCAN OF KLINGON SHIP":PRINT"LOG 3  LAST TRANSMISSION":PRINT"LOG 4  ENTERPRISE COMMANDS"
1831 PRINT"    5  CLOSE LOG";
1832 F$=" ":F$=INKEY$:F=VAL(F$):IFF<2 OR F>5 THEN1832
1833 IFF=3 THEN1900
1834 IFF=5 THEN20
1835 IFF=2 THEN1840
1836 CLS:PRINT"ENTERPRISE CONTROL COMMANDS":PRINT"1. FORWARD PHASERS":PRINT"2. REAR PHASERS":PRINT"3. PHOTON TORPEDOES"
1837 PRINT"4. CLOSE ON ENEMY VESSEL":PRINT"5. PULL AWAY FROM ENEMY":PRINT"6. STAND BY":PRINT"7. ENERGY TO SHIELDS"
1838 PRINT"8. RECHARGE DILITHIUM CRYSTALS":PRINT"9. REPAIR":GOSUB905:GOTO1820
1840 CLS:PRINT"KLINGON BATTLE CRUISER"
1841 PRINT"CIRCA ORGANIAN STARDATE 3199.5"
1842 PRINT"           TOP VIEW":PRINT
1844 PRINT"             "CHR$(138)CHR$(128)CHR$(128)" "CHR$(95)" BRIDGE AND"
1845 PRINT"            / "CHR$(138)"    2 PHASORS"
1846 PRINT"      PLASMA  "CHR$(138)" "
1847 PRINT"      WEAPON  "CHR$(128)CHR$(133)
1848 PRINT"              "CHR$(128)CHR$(133)
1849 PRINT"         "CHR$(142)"  "CHR$(140)CHR$(128)CHR$(128)CHR$(128)CHR$(132)"  "CHR$(141)
1850 PRINT"         "CHR$(138)CHR$(128)+STRING$(5,128)CHR$(128)CHR$(128)CHR$(128)CHR$(133)
1851 PRINT"         "CHR$(138)CHR$(131)CHR$(131)CHR$(131)CHR$(128)CHR$(128)CHR$(128)CHR$(129)CHR$(131)CHR$(131)CHR$(133)"  STARBOARD"
1852 PRINT"         "CHR$(138)"    ^    "CHR$(133)CHR$(95)" NACELLE"
1853 PRINT"        IMPLUSE ENGINE"
1854 PRINT"      AND SINGLE PHASOR"
1865 GOSUB905:GOTO1820
1900 CLS:PRINT"LAST TRANSMISSION FROM OUTPOST 7--------------------------------":GOSUB910
1901 PRINT"OUTPOST 7 CALLING STARFLEET     COMMAND ON PRIORITY CHANNEL 1":GOSUB910
1905 PRINT"STARFLEET COME IN":GOSUB910:PRINT"...STARFLEET DO YOU HEAR US?":GOSUB910
1906 PRINT"OUTPOST 7 CALLING STARFLEET     COMMAND--ACKNOWLEDGE!":GOSUB910
1908 PRINT"STARFLEET WE ARE UNDER HEAVY    ATTACK....ENEMY UNKNOWN!":GOSUB910:SOUND1,3
1910 PRINT"OUR TRANSMISSIONS ARE BEING     JAMMED (CRACKLE)...":GOSUB910:PRINT"...EXPLOSIONS (CRACKLE)"
1911 PRINT"...WE...WAIT!"
1912 GOSUB910:PRINT"WE'RE RECEIVING A (CRACKLE)":GOSUB910:PRINT"(CRACKLE)":SOUND5,1:SOUND1,1:SOUND2,1
1915 GOSUB910:GOSUB910:PRINT"WE CAN SEE SOMETHING ON THE":PRINT"MAIN SCREEN--STAND BY":GOSUB910:PRINT"(CRACKLE)"
1916 GOSUB910:PRINT"MY GOD, THEY'RE KLINGONS!"
1918 GOSUB910:PRINT"WE...CAN'T SUSTAIN ANOTHER HIT!"
1920 GOSUB910:PRINT"SWITCHING TO MAIN VIEWER ":GOSUB910:PRINT"...STARFLEET DO YOU COPY?":GOSUB910
1921 PRINT"(CRACKLE)";:GOSUB900
1930 CLS:PRINT@192,"";TAB(11);"EARTHLINGS":PRINTTAB(3);"PREPARE TO MEET YOUR DOOM":PRINTTAB(2);
1931 PRINT"AT THE HANDS OF THE KLINGON"
1932 PRINTTAB(13);"EMPIRE":GOSUB910:GOSUB910:CLS0:GOSUB600:GOSUB620:IFK3=1 THENK3=0:RETURN
1933 GOSUB1450:GOSUB910
1935 PRINT@230,"(CRACKLE)";:GOSUB910:PRINT" (CRACKLE)...";:GOSUB900
1936 PRINT:PRINT"END OF TRANSMISSION":GOSUB910:GOTO1820
2000 K3=1:A(6)=-1:J=0:PRINT"WE ARE RECEIVING A TRANSMISSION":PRINT"FROM THE ";B$;"COMMANDER"
2010 GOSUB900:GOSUB1930:CLS0:PRINT@266,E$;:GOSUB960:GOSUB640:GOSUB930:GOTO390
3000 PRINT"SENSOR REPORT: ";B$:IFA(16)-H>0 THEN235
3010 IFR<6E5 THEN234
3020 IFK7<S AND (A(11)=0 AND A(12)=0 AND A(13)=0) OR A(16)<349 THEN233
3030 GOTO235
4000 CLS:PRINTTAB(9);"USS ENTERPRISE":PRINTTAB(9);"--------------"
4001 PRINT:PRINT"STARDATE:";SD/EN
4003 PRINT:PRINTTAB(7)"PERFORMANCE REVIEW":PRINT
4010 PRINT"VICTORIES:      ";WN
4020 PRINT"DEFEATS:        ";DF
4030 PRINT"ENGAGEMENTS:    ";EN
4050 PRINT"VICTORY AVERAGE:";INT(WN*100/EN);"%"
4055 PRINT"BATTLE SCORE:   ";INT((SD/EN)*INT(WN*100/EN))*EN
4060 RETURN
