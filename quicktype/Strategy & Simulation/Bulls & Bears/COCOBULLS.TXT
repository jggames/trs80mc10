0 DIMC1,CC,M$,MC:GOTO5
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" " THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$) THEN2
4 RETURN
5 CLS:PRINT@3,"SPEAKEASY SOFTWARE PRESENTS":GOSUB525
6 PRINT@70,"**BULLS AND BEARS**":GOSUB525
7 PRINT@131,"A GAME OF CORPORATE FINANCE":GOSUB525
10 PRINT@196,"SPEAKEASY SOFTWARE INC.":PRINT@232,"P.O. BOX 1220"
11 PRINT@264,"KEMPTVILLE, ON":PRINT@296,"CANADA K0G 1J0"
15 CLEAR350:GOSUB810:MC=16384:PRINT@352,"NUMBER OF PLAYERS (1-5) ";:INPUTP
16 IFP<1 OR P>5 THEN15
17 J=P+1:K=J+1:L=K+1:M=L+1:N=M+1:O=N+1:DIMN$(P),C$(J),C(J,O),CE(J),PL(P),PC(P):CLS:FORX=1 TOP:PC(X)=50
20 PRINT@32*X,"ENTER NAME OF PLAYER";X;:INPUTA$:IFLEN(A$)=0 OR A$=N$(X-1) THEN20
21 IFLEN(A$)>6 THENA$=MID$(A$,1,6)
25 N$(X)=A$:NEXT:CLS:PRINT@8,"*BULLS AND BEARS*":PRINT@45,"PLAYERS":FORX=1 TOP:PRINT"PLAYER #";X;"-- ";N$(X):NEXT
26 PRINT:M$="GAME HINT: AVOID LETTING OTHERS SEE YOUR CASH OR HOLDINGS. "
30 M$=M$+"THIS WILL AID YOU IN MAKING SURPRISE TAKEOVER BIDS...":GOSUB1:X=RND(-TIMER)
31 Z=P*5+15:GOSUB600:FORX=1 TOJ:READC$(X):C(X,J)=100:C(X,K)=20:C(X,M)=4:C(X,N)=6:NEXT:YR=1917+RND(55)
35 Z=12:CI=RND(10)+9:PI=RND(10)+9:FORX=1 TOP:GOSUB700:PRINT@L*32+9,"PLAYER FINANCE":PRINT"PLAYER: ";N$(X)
40 PRINT@N*32,CL$;:GOSUB710:PRINT"YOUR CASH BALANCE   ";:GOSUB750:ONF GOTO45,40
41 PRINT@N*32,CL$;"CASH BALANCE: $";PC(X)*1000:GOSUB600
45 PRINT@N*32,CL$;:GOSUB710:PRINT"YOUR SHAREHOLDINGS  ";:GOSUB750:ONF GOTO60,45
46 CLS:PRINT"SHAREHOLDINGS: ";N$(X):PRINT"CORPORATION";TAB(16)"SHARES";TAB(23)"VALUE":W=0:FORY=1 TOJ
50 IFC(Y,X)=0 THEN55
51 F=C(Y,X)*C(Y,M):PRINTC$(Y);TAB(15)C(Y,X)*1000;TAB(22)F*1000:W=W+F
55 NEXTY:PRINT:PRINT"VALUE OF SHARES $";W*1000:PRINT"NET WORTH IS $";(W+PC(X)-PL(X))*1000
60 PRINT"YOU OWE THE BANK $";PL(X)*1000:IFPL(X)=0 GOTO65
61 Y=PC(X):GOSUB765:PC(X)=Y-I:PL(X)=PL(X)-I
65 GOSUB710:PRINT"TO BORROW FROM THE  BANK";:GOSUB750:ONF GOTO70,65:I=PI:Y=W+PC(X)-PL(X):GOSUB780:PC(X)=PC(X)+F:PL(X)=PL(X)+F
70 NEXTX:FORX=1 TOP:GOSUB700:PRINT@L*32+9,"PLAYER TRADING":PRINT"PLAYER: ";N$(X):PRINT;
75 GOSUB710:PRINT"TO BUY OR SELL STOCK";:GOSUB750:ONF GOTO120,75
80 INPUT"FOR WHAT COMPANY (NUMBER)";Y:IFY<1 OR Y>J THEN80
85 INPUT"BUY OR SELL";A$:F=ASC(A$):IFF=66 OR F=83 THEN90
86 GOSUB805:GOTO85
90 CLS:PRINT@8,C$(Y):PRINT:PRINT"BID",C(Y,M),"PRODUCTION UNITS";C(Y,L)
91 PRINT"ASKED",C(Y,N),"SHARES AVAILABLE";C(Y,K)*1000:PRINT
92 IFF=66ANDC(Y,K)=0 THENPRINT"*NO SHARES AVAILABLE*":GOSUB600:PRINT:GOTO75
95 PRINT"HOW MANY SHARES TO ";:IFF=66 THENPRINT"BUY?":GOTO100
96 PRINT"SELL?"
100 PRINTCL$;:INPUT"(000'S)";I:I=INT(ABS(I)):IFF=66 THEN110
101 IFI>C(Y,X) THENM$="YOU DO NOT OWN"+STR$(I*1000)+" SHARES IN "+C$(Y):GOSUB1:GOSUB600:GOTO120
105 C(Y,K)=C(Y,K)+I:C(Y,X)=C(Y,X)-I:PC(X)=PC(X)+I*C(Y,M):PRINT"STOCK SALE COMPLETE"
106 PRINT"CREDITED TO YOUR ACCOUNT":PRINT"$";I*C(Y,M)*1000:GOTO75
110 IFI>C(Y,K) THENPRINT"ONLY";C(Y,K)*1000;"SHARES AVAILABLE";:GOTO95
111 IFI*C(Y,N)>PC(X) THEN GOSUB650:GOTO120
115 PC(X)=PC(X)-I*C(Y,N):C(Y,K)=C(Y,K)-I:C(Y,X)=C(Y,X)+I
116 PRINT"PURCHASE PRICE IS $";I*C(Y,N)*1000:PRINT"STOCK PURCHASE COMPLETE":PRINT:GOTO75
120 NEXTX:FORX=1 TOJ:F=1:FORY=1 TOP:IFC(X,Y)>C(X,F) THENF=Y
125 NEXTY:IFC(X,F)=0 THEN177
126 CLS:PRINT@0,C$(X)" *ANNUAL REPORT*":PRINT"DIRECTOR: ";N$(F):PRINT@64,"CASH $";C(X,J)*1000
127 PRINT@96,"LOANS OUTSTANDING $";C(X,O)*1000
130 PRINT"PRODUCTION UNITS =";C(X,L):W=C(X,J)+C(X,L)*10-C(X,O)
131 PRINT@160,"PER SHARE BOOK VALUE =";INT(W/20)
132 PRINT@192,"SHARES AVAILABLE =";C(X,K)*1000:PRINT"BID =";C(X,M);TAB(16)"ASK =";C(X,N)
133 PRINT:Y=INT((C(X,J)-100)/20):IFY<1 THEN145
135 GOSUB710:PRINT"TO DECLARE DIVIDENDS";:GOSUB750:ONF GOTO145,135
140 PRINT@320,CL$;:M$="DIVIDEND LIMIT PER SHARE ="+STR$(Y):GOSUB1
141 PRINTCL$;:INPUT"ENTER DIVIDEND PER SHARE";F:F=INT(ABS(F))
142 IFF>Y THEN GOSUB805:GOTO141
143 FORY=1 TOP:PC(Y)=PC(Y)+C(X,Y)*F:NEXTY:C(X,J)=C(X,J)-20*F:GOSUB625
145 IFC(X,L)=0 THEN160
150 GOSUB710:PRINT"TO SELL PRODUCTION  UNITS";:GOSUB750:ONF GOTO160,150
155 PRINTCL$;:M$="RESALE VALUE IS $9000--LIMIT IS "+STR$(C(X,L)):GOSUB1
156 INPUT"HOW MANY";F:F=INT(ABS(F)):IFF>C(X,L) THEN GOSUB800:GOTO155
157 C(X,J)=C(X,J)+F*9:C(X,L)=C(X,L)-F:GOSUB625
160 GOSUB710:PRINT"TO BUY PRODUCTION   UNITS";:GOSUB750:ONF GOTO170,160
165 PRINT"PRODUCTION UNITS COST $10000."
166 PRINTCL$;:INPUT"HOW MANY";F
167 F=INT(ABS(F)):IFF*10>C(X,J) THEN GOSUB650:GOTO166
168 C(X,L)=C(X,L)+F:C(X,J)=C(X,J)-F*10:GOSUB625
170 IFC(X,O)<1 THEN175
171 M$=C$(X)+" OWES THE BANK $"+STR$(C(X,O)*1000):GOSUB1:Y=C(X,J):GOSUB765:C(X,J)=Y-I:C(X,O)=C(X,O)-I
175 PRINTCL$;"DOES ";C$(X);" WANT TO":PRINT"BORROW";:GOSUB750:ONF GOTO177,175:Y=C(X,J)+C(X,L)*10-C(X,O):I=CI:GOSUB780
176 C(X,J)=C(X,J)+F:C(X,O)=C(X,O)+F
177 NEXTX
180 CLS:PRINT:PRINT"GAME TO CONTINUE (Y/N)";:GOSUB750:ONF GOTO245,180
181 FORX=1 TOP:PL(X)=PL(X)+INT(PL(X)*PI/100):NEXTX:FORX=1 TOJ:C(X,O)=C(X,O)+INT(C(X,O)*CI/100)
182 CE(X)=C(X,L)*(6-RND(8)):F=INT((C(X,J)+C(X,L)*10-C(X,O))/20)
185 I=INT(CE(X)*(4+RND(10))/20):IFI>F THENC(X,M)=I-1:C(X,N)=I+1:GOTO190
186 C(X,M)=F-1:C(X,N)=F+1
190 IFYR=29 THENCE(X)=-CE(X)
195 C(X,J)=C(X,J)+CE(X):IFC(X,J)<0 THENC(X,J)=100:C(X,O)=0:C(X,L)=0:C(X,M)=3:C(X,N)=5
200 NEXTX:YR=YR+1:X=RND(50):IFX/10<>INT(X/10) THEN35
201 CLS:PRINT@10,"NEWS FLASH!":ONX/10 GOTO210,220,230
205 M$="THE TAXMAN COMETH! THE FEDERAL GOVERNMENT HAS LEVIED A CAPITAL GAINS TAX... 10% OF PLAYERS' CASH IS TAKEN!"
206 GOSUB1:FORX=1 TOP:PC(X)=PC(X)-INT(PC(X)/10):NEXTX:GOTO240
210 PRINT"SPECIAL DEPRECIATION ALLOWANCE!"
211 M$="THE GOVERNMENT, IN ITS FIGHT AGAINST THE RECESSION, HAS GRANTED A SPECIAL ALLOWANCE... COMPANIES WITH PRODUCTION"
212 M$=M$+" UNITS RECEIVE $5000 PER PRODUCTION UNIT...":GOSUB1
215 FORX=1 TOJ:C(X,J)=C(X,J)+C(X,L)*5:NEXTX:GOTO240
220 PRINT"STOCK FRAUD DISCOVERED!"
221 M$="WALL STREET IN A PANIC! THE MAFIA HAS BEEN ISSUING FALSE STOCK CERTIFICATES FOR YEARS... "
222 M$=M$+"EVERY PLAYER LOSES $10000 CASH":GOSUB1:FORX=1 TOP:PC(X)=PC(X)-10:IFPC(X)<0 THENPC(X)=0
225 NEXTX:GOTO240
230 M$="ARABS SELL OIL FOR $5 A BARREL! PRINCE KHALIM FUZZY, IN RETURN FOR NUCLEAR ARMS--"
231 M$=M$+"FOR DEFENSE PURPOSES, OF COURSE-- AGREES TO SELL HIS OIL FOR $5 A BARREL.":GOSUB1
232 M$="INDUSTRY PROSPERS...ALL STOCK PRICES UP $3...":GOSUB1
235 FORX=1 TOJ:C(X,M)=C(X,M)+3:C(X,N)=C(X,N)+3:NEXTX
240 Z=65:GOSUB600:GOTO35
245 CLS:PRINT"PLAYER";TAB(15)"NET WORTH":PRINT:FORX=1 TOP:F=0:FORY=1 TOJ:F=F+C(Y,X)*C(Y,M):NEXTY
246 F=F+PC(X)-PL(X):PRINTN$(X);TAB(14)"$";F*1000:NEXTX:PRINT"END OF GAME...":END
525 FORX=1 TO32:PRINT"$";:FORY=1 TO10:NEXTY,X:RETURN
550 DATA"OFF-SHORE OIL","ELECTRO LTD.","GIANT MINE","DATAMACHINE","GASSO","BOING AEROSP"
600 FORI=1 TOZ*250:NEXT:RETURN
625 PRINT"YOUR CASH BALANCE IS NOW":PRINT"$";C(X,J)*1000:RETURN
650 PRINT"INSUFFICIENT CASH";:GOSUB600:FORZZ=1 TO17:PRINTCHR$(8);:NEXT:GOSUB805:RETURN
700 CLS:PRINT@7,"MARKET REPORT";YR:PRINT"COMPANY      EARNINGS  BID  ASK"
701 FORY=1 TOJ:PRINTCHR$(8);Y;C$(Y);TAB(15)CE(Y)*1000;TAB(22)C(Y,M);TAB(27)C(Y,N):NEXTY:RETURN
710 PRINTCL$;"DO YOU WANT ";:RETURN
750 F=0:INPUTA$:A$=A$+"Y":I=ASC(A$):IFI=78 OR I=89 THEN755
751 PRINT"RE-TYPE...ANSWER (Y/N)";:F=2:GOSUB600:FORZZ=1 TO22:PRINTCHR$(8);:NEXT:GOSUB800:RETURN
755 IFI=78 THENF=1
760 RETURN
765 GOSUB710:PRINT"TO MAKE A LOAN      PAYMENT";:GOSUB750:I=0:ONF GOTO775,765
770 PRINTCL$;:INPUT"LOAN PAYMENT (000'S)";I:I=INT(ABS(I))
771 IFI>Y THEN GOSUB650:GOTO770
772 PRINT"THANK YOU"
775 RETURN
780 PRINTCL$;:M$="INTEREST RATES ARE"+STR$(I)+"% YEARLY.":GOSUB1
781 INPUT"HOW MUCH (000'S)";F:F=INT(ABS(F)):IFF>0ANDF<10 THENPRINT"MINIMUM IS $10000":GOSUB800:GOTO781
785 IFF>Y/2 THENPRINT"AMOUNT EXCESSIVE--REFUSED!":F=0:GOTO790
786 PRINT"$";MID$(STR$(F*1000),2);" IS APPROVED";
790 GOSUB600:RETURN
800 C2=(PEEK(136)AND1)*256+PEEK(137):PRINT@C2-64,"";:RETURN
805 C2=(PEEK(136)AND1)*256+PEEK(137):PRINT@C2-32,"";:RETURN
810 FORZZ=1 TO64:CL$=CL$+" "::NEXT:FORZZ=1 TO64:CL$=CL$+CHR$(8):NEXT:RETURN
