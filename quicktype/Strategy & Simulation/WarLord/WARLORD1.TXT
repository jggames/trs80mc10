0 DIMC1,C2,M$:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,:RETURN
8 PRINT@482,"PLEASE HIT enter TO CONTINUE";:FORC1=0TO1STEP0:C1=-(INKEY$=CHR$(13)):NEXT::CLS:RETURN
9 PRINT"                               ";:POKE(PEEK(17024)AND1)*256+PEEK(17025)+MC,96:FORC1=1TO31:PRINTCHR$(8);:NEXT:RETURN
10 GOSUB8000
20 GOSUB9000
25 GOSUB11000
30 GOSUB1000
31 FORX=1TO3000:NEXTX
32 PRINT@480,;:GOSUB9:PRINT"NEW CAMPAIGN";
34 FORX=1TO2500:NEXTX
50 C=RND(5)-1:IFC=0THEN50
60 FORN=1TOC
70 GOSUB7000
80 GOSUB2000
90 NEXTN
95 PRINT@480,;:GOSUB9:PRINT"END OF CAMPAIGN";
97 FORX=1TO2500:NEXTX
98 GOSUB4000
99 CLS
100 PRINT@32+10,"GAME STATUS"
120 FORX=1TOPN
125 PRINT@32*((X-1)*4+2),"WARLORD "N$(X)
135 A=0
140 FORZ=1TO49
150 IFO(Z)=XTHENA=A+S(Z)
160 NEXTZ
170 PRINT"TROOPS -"A"DIVISIONS"
180 PRINT"CROWNS -"PC(X)*1000
190 NEXTX
192 PRINT@480,"GAME TO CONTINUE (Y/N)?";
210 GOSUB6000:IFNLTHEN300
215 GOSUB10000
220 FORX=1TO4000:NEXTX:GOSUB1000
230 GOTO31
300 END 
513 S(D)=E-RND(INT(E/2))-1
1000 REM SET UP SCREEN BORDERS
1010 CLS:M$="   1   2   3   4   5   6   7    ":GOSUB7
1030 FORY=1TO7:X=Y*64-32+MC:POKEX,48+Y:POKEX+31,48+Y:POKEX+32,32:POKEX+63,32:NEXT
1100 REM LAY OUT SCREEN
1110 FORX=1TO7:FORY=1TO7:PRINT@-32+32*(X*2)+(Y*4-2),;
1145 IFT(X*7-7+Y)>1THENIFT(X*7-7+Y)<4THENPRINT@-32+32*(X*2)+(Y*4-1),;
1147 IFT(X*7-7+Y)=5THENPRINT@-32+32*(X*2)+(Y*4-1),;
1150 PRINTMID$(T$,T(X*7-7+Y),1);
1160 IFP(X*7-7+Y)THENPRINTMID$(STR$(P(X*7-7+Y)),2);
1170 IFO(X*7-7+Y)THENPRINTMID$(STR$(O(X*7-7+Y)),2);
1180 NEXTY:NEXTX
1185 PRINT@480,"S-SWAMP,F-FOREST,C-CASTLE,T-TOW";:POKE16895,ASC("N")
1190 FORX=1TO7:FORY=1TO7:IFS(X*7-7+Y)=.THEN1230
1210 PRINT@-32+32*(X*2+1)+(Y*4-1),MID$(STR$(S(X*7-7+Y)),2);
1230 NEXTY:NEXTX
1240 PRINT@480,;:RETURN
2000 REM GET BATTLE ORDERS
2010 FORZ=1TOPN
2020 PRINT@480,;:GOSUB9:Y=ST(Z):B=0:FORA=1TO49:IFO(A)=YTHENB=1
2030 NEXTA:IFB<>1THEN2999
2050 PRINTMID$(STR$(Y),2);" "N$(Y)" MOVE TROOPS (Y/N)?";
2070 GOSUB6000:IFNLTHEN2999
2080 CLS:PRINT"         BATTLE ORDERS":PRINT@32*1,"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
2090 PRINT@32*3,"WARLORD"Y;N$(Y);" CASTLE"STR$(INT((INT(CA(Y)-1)/7)+1));MID$(STR$(((CA(Y)-1)-(7*INT((CA(Y)-1)/7)))+1),2)
2100 PRINT@32*5,"FROM";:PRINTTAB(8):PRINT"TO";:PRINTTAB(16):PRINT"# OF DIVISIONS"
2110 PRINT@32*12,"ENTER from AND to CO-ORDINATES"
2120 PRINT"AS NUMBER IN THE FORM OF vh"
2130 PRINT"WHERE vh IS THE VERTICAL - "
2140 PRINT"HORIZONTAL INDEX (11-77)";
2150 REM GET FROM CO-ORDINATE
2155 FORX=7TO9:PRINT@32*X,;:GOSUB9:NEXTX
2170 PRINT@32*7,;:GOSUB9:INPUTC:A=C:C=(INT(C/10)*7)-7+(C-(10*INT(C/10))):IFATHEN2180
2175 GOSUB1000:GOTO2020
2180 IFINT(A/10)<1ORINT(A/10)>7THEN2190
2185 IF(A-(10*INT(A/10)))>0AND(A-(10*INT(A/10)))<8THEN2200
2190 PRINT@32*9,;:GOSUB9:M$="CO-ORDINATE OUTSIDE RANGE":GOSUB7:SOUND100,1:SOUND100,1:GOTO2170
2200 REM GET TO CO-ORDINATE
2220 PRINT@32*7+8,"":PRINT@32*7+8,;:INPUTD
2221 B=D:D=(INT(D/10)*7)-7+(D-(10*INT(D/10)))
2222 IFBTHEN2230
2225 GOSUB1000:GOTO2020
2230 IFINT(B/10)<1ORINT(B/10)>7THEN2240
2235 IF(B-(10*INT(B/10)))>0AND(B-(10*INT(B/10)))<8THEN2300
2240 PRINT@32*9,;:GOSUB9:M$="CO-ORDINATE OUTSIDE RANGE":GOSUB7:SOUND100,1:SOUND100,1
2260 GOTO2220
2300 REM GET NUMBER OF DIVISIONS
2310 PRINT@32*7+16,"":PRINT@32*7+16,;
2320 INPUTE
2330 IFE=0THEN2998
2340 REM DO IT!
2350 REM C=FROM CO-ORD
2360 REM D=TO CO-ORD
2370 REM E=NUMBER OF DIVISIONS
2400 IFO(C)=YTHEN2500
2410 PRINT@32*9,;:M$="NO TROOPS AT FROM POSITION":GOSUB7:SOUND100,1:SOUND100,1
2430 GOTO2170
2500 REM CHECK STRENGTH
2510 IFS(C)>=ETHEN2600
2520 PRINT@32*9,;:GOSUB9:M$="NOT ENOUGH TROOPS":GOSUB7:SOUND100,1:SOUND100,1
2540 GOTO2320
2600 REM MOVE TO WHERE
2610 IFT(D)<>2THEN2700
2620 PRINT@32*9,;:M$="INVALID MOVE-- LAKE":GOSUB7:SOUND100,1:SOUND100,1
2640 GOTO2220
2700 GOSUB3000:IFIM=1THEN2170:REM ILLEGAL MOVE?
2705 IFO(D)<>0ANDO(D)<>YTHEN2800
2710 REM OWNER OF TO CO-ORD
2715 REM OR EMPTY CO-ORD
2720 S(D)=S(D)+E:S(C)=S(C)-E
2725 IFS(C)=0THENO(C)=0
2728 O(D)=Y
2730 PRINT@32*9,;:GOSUB9:PRINT "MOVED!":SOUND100,1:SOUND100,1
2750 FORX=1TO2500:NEXTX
2760 GOTO2998
2800 GOSUB5000:REM BATTLE ENSUES
2998 GOSUB1000
2999 NEXTZ:RETURN 
3000 REM CHECK IF REQUESTED
3010 REM MOVE IS VALID
3020 REM C & D ARE 'FROM' AND
3030 REM 'TO' VECTOR CO-ORDS.
3040 REM A & B ARE INPUTED BOARD
3050 REM 'MATRIX CO-ORDINATES'
3060 IM=0:X=ABS(INT(B/10)-INT(A/10)):IFX>1THEN3100
3070 X=ABS((B-(10*INT(B/10)))-(A-(10*INT(A/10))))
3080 IFX>1THEN3100
3090 RETURN 
3100 LG=1:PRINT@480,;:GOSUB9:PRINT "ILLEGAL MOVE":SOUND100,1:SOUND100,1:FORC1=1TO1500:NEXT
3120 RETURN
4000 REM CALCULATE TAXES AND RAISE
4010 REM TROOPS
4020 REM CASH  FROM TREASURY ONLY
4030 REM MAY PLACE TROOPS ANY WHERE
4035 REM ALREADY OCCUPIED BY WARLORD
4040 CLS
4050 FORZ=1TOPN:CLS:IFO(CA(Z))<>ZTHEN4700
4060 PRINT@32*3+8,"WARLORD"Z;N$(Z)
4070 PRINT@32*5,"TREASURY BALANCE IS";PC(Z)
4080 X=0
4090 FORY=1TO49
4100 IFO(Y)=ZTHENX=X+P(Y)
4110 NEXTY
4120 PRINT@32*7,"TAXES RAISED THIS YEAR"X*1000
4122 C=X
4125 IFPC(Z)<2THEN4500
4130 PRINT@9*32,"DO YOU WISH TO BUY TROOPS FROM":PRINT"TREASURY MONEY (Y/N)?";
4140 GOSUB6000:IFNLTHEN4500
4150 PRINT@11*32,;:GOSUB9:PRINT"HOW MANY DIVISIONS";
4160 INPUTA
4170 IFA<=INT(PC(Z)/2)THEN4200
4180 PRINT@12*32,"DIVISIONS COST 2,000 CROWNS EACH":M$="INSUFFICIENT CASH":GOSUB7:FORC1=1TO2500:NEXT
4190 PRINT@12*32,;:GOSUB9:PRINT@13*32,:GOSUB9:PRINT@32*11,;:GOSUB9:GOTO4150
4200 GOSUB1000:PRINT@480,;:PRINT"TROOPS TO WHAT LOCATION";
4210 INPUTB
4240 IFINT(B/10)<1ORINT(B/10)>7THEN4300
4250 IF(B-(10*INT(B/10)))<1OR(B-(10*INT(B/10)))>7THEN4300
4260 B=INT(B/10)*7-7+(B-(10*INT(B/10)))
4265 IFB<>25THEN4270
4266 PRINT@480,;:GOSUB9:PRINT"NOT VALID TO CAPITAL":SOUND100,1:SOUND100,1
4268 GOTO4295
4270 IFO(B)<>ZTHEN4290
4275 CLS:PRINT@32*3,N$(Z)
4280 O(B)=Z:S(B)=S(B)+A:PC(Z)=PC(Z)-A*2:GOTO4500
4290 PRINT@480,;:GOSUB9:M$="INVALID MOVE!":GOSUB7:SOUND100,1:SOUND100,1
4295 FORB=1TO2500:NEXTB
4296 GOTO4200
4300 PRINT@480,;:GOSUB9:M$="INVALID BOARD POSITION":GOSUB7:SOUND100,1:SOUND100,1
4310 FORB=1TO2500:NEXTB:GOTO4200
4500 REM PLACE TROOPS FROM THIS YEARS CASH 
4510 REM MAY EXCEED THIS YEARS TAXES AS
4520 REM THEY WILL NOW BE ADDED TO TREASURY
4530 PC(Z)=PC(Z)+C
4540 PRINT@32*9,;:GOSUB9
4560 PRINT"$ TO RAISE CASTLE TROOPS";PC(Z)
4570 PRINT"WISH TO RAISE TROOPS (Y/N)?";
4590 GOSUB6000:IFNLTHEN4700
4600 PRINT@32*11,;:GOSUB9:PRINT"HOW MANY DIVISIONS ($2000)";
4610 INPUTX
4620 IFX<=INT(PC(Z)/2)THEN4640
4630 M="INSUFFICIENT CASH":GOSUB7:SOUND100,1:SOUND100,1:GOTO4600
4650 S(CA(Z))=S(CA(Z))+X
4660 PC(Z)=PC(Z)-X*2
4700 NEXTZ
4710 RETURN 
5000 REM BATTLE REPORT (CASUALITY LISTS)
5010 REM FIRST CALCULATE FACTORS
5020 AS=E+T(C):DS=S(D)+T(D)
5030 IFAS>DSTHEN5100
5040 REM DEFENDER HAS WON
5050 S(C)=S(C)-E:IFS(C)=0THENO(C)=0
5055 IF S(D)=1 THEN 5200
5060 S(D)=S(D)-RND(INT(S(D)/2))-1
5070 GOTO5200
5100 REM ATTACKER WINS
5110 S(C)=S(C)-E
5115 IFS(C)=0THENO(C)=0
5117 IFE=1THEN5200
5120 S(D)=E-RND(INT(E/2))-1
5130 O(D)=Y
5200 RETURN 
6000 REM ANSWER SUBROUTINE
6010 NL=0:FORC1=0TO1:B$=INKEY$:C1=-(B$<>""):NEXT
6020 IFB$="Y"THEN6070
6030 IFB$="N"THENNL=1:GOTO6070
6040 SOUND1,1
6050 GOTO6010
6070 RETURN 
7000 REM SET RANDOM ORDER PLAYER LIST
7010 FORX=1TOPN
7020 ST(X)=RND(PN+1)-1
7025 IFST(X)=.THEN7020
7030 IFX=1THEN7080
7040 FORY=1TOX-1
7050 IFST(Y)=ST(X)THENST(X)=0
7060 NEXTY
7070 IFST(X)=0THEN7020
7080 NEXTX:RETURN
8000 REM TITLE PAGE
8001 CLS
8020 FORX=1TO32:PRINT"**";:NEXTX:PRINT
8040 PRINTTAB(9)"W A R L O R D S":PRINT
8050 FORX=1TO32:PRINT"**";:NEXTX
8060 PRINT:PRINT
8070 PRINTTAB(5)"BY: SPEAKEASY SOFTWARE"
8090 PRINTTAB(9)"P.O. BOX 1220 "
8100 PRINTTAB(9)"KEMPTVILLE ONT."
8110 PRINTTAB(9)"CANADA K0G 1J0"
8120 PRINT@480,;
8130 INPUT"HOW MANY PLAYERS (2-4)";PN:CLS
8140 IFPN<2ORPN>4THEN8120
8145 DIMB$(20),N$(4),PC(PN),CA(PN)
8150 CLS
8160 PRINT"WARLORDS - PLAYERS LIST"
8170 FORX=1TOPN
8180 PRINT"ENTER NAME FOR PLAYER"STR$(X);
8190 INPUTB$
8195 IFLEN(B$)=0THEN8180
8210 N$(X)=LEFT$(B$,6)
8225 PC(X)=0
8230 NEXTX
8240 DIMST(PN)
8999 RETURN 
9000 REM VARIABLE ALLOCATION & INIT
9001 REM P=POPULATION
9002 REM O=OWNERSHIP
9003 REM T=TYPE OF CO-ORDINATE
9004 REM S=STRENGTH OF TROOPS
9005 REM T$=PRINT DESIGNATOR FOR T(TYPE)
9006 REM N$=STRING VAR. FOR PLAYERS NAMES
9007 REM B$ POS. IN N$ OF PLAYER NAME
9009 REM PC(PN) PLAYER CASH IN THOUSANDS
9010 DIMP(49),O(49),T(49),S(49),T$(7)
9020 T$=" LSTFCX"
9040 FORX=1TO49
9050 P(X)=0:O(X)=0:T(X)=0:S(X)=0:NEXTX
9060 REM INITIALIZE SCREEN MATRIX VALUES
9070 T(25)=7:P(25)=9
9080 IFPN=3THEN9110
9090 IFPN=2THEN9120
9100 T((RND(3)-1+4)*7+RND(3)-1+1)=6
9110 T((RND(3)-1+0)*7+RND(3)-1+5)=6
9120 T((RND(3)-1+4)*7+RND(3)-1+5)=6
9130 T((RND(3)-1+0)*7+RND(3)-1+1)=6
9140 REM SET LAKES(2),SWAMPS(3),TOWNS(4) AND THEIR POPULATIONS
9150 FORA=2TO4
9160 FORX=1TO4
9170 Y=RND(49)-1+1
9180 IFT(Y)THEN9170
9190 T(Y)=A
9200 IFA<>4THEN9210
9201 P(Y)=4+RND(3)-1
9210 NEXTX
9220 NEXTA
9230 FORX=1TO10
9240 Y=RND(49)
9250 IFT(Y)THEN9240
9260 T(Y)=5
9270 NEXTX
9280 FORX=1TO49
9290 IFT(X)THEN9330
9300 T(X)=1
9310 P(X)=1+RND(3)-1
9320 GOTO9350
9330 IFT(X)<>6THEN9350
9340 P(X)=5+RND(5)-1
9345 S(X)=9
9350 NEXT X
9360 FORY=1TOPN
9370 FORX=1TO49
9380 IFT(X)<>6ORO(X)<>0THEN9390
9382 CA(Y)=X
9385 O(X)=Y:GOTO9400
9390 NEXTX
9400 NEXTY
9410 RETURN 
10000 REM FATE!!
10010 X=RND(30)-1-29
10020 IFINT(X/5)=((INT(X/5)+X)-(5*INT((X/5+X)/5)))THEN10030
10025 GOTO10999
10030 CLS:SOUND100,1:SOUND100,1
10040 PRINT@32*2+14,"FATE!":PRINT
10050 IFX<>5THEN10100
10060 PRINT"PLAGUE THROUGHOUT THE LAND!":PRINT
10070 PRINT"EVERY TREASURY LOSES 5,000":PRINT"CROWNS"
10072 FORX=1TO3000:NEXTX
10080 FORA=1TOPN:PC(A)=PC(A)-5:NEXTA
10100 IFX<>10THEN10200
10110 PRINT"BUMPER CROPS IN THE LAND OF":PRINT"NERD!":PRINT
10120 PRINT"TREASURIES SWELL BY 10,000":PRINT"CROWNS"
10122 FORX=1TO3000:NEXTX
10130 FORA=1TOPN:PC(A)=PC(A)+10:NEXTA
10200 IFX<>15THEN10300
10210 PRINT"MAGICIAN AEDIPIOS CASTS CURSE":PRINT"ON WARLORDS!":PRINT
10220 PRINT"TURNS THEIR GOLD TO BLOOD!"
10230 PRINT"TREASURIES WIPED OUT":PRINT
10232 FORX=1TO3000:NEXTX
10240 FORA=1TOPN:PC(A)=0:NEXTA
10300 IFX<>20THEN10400
10310 PRINT"BLACK DEATH STRIKES CASTLES!":PRINT
10320 PRINT"HALF OF CASTLE TROOPS DIE"
10322 FORX=1TO3000:NEXTX
10330 FORA=1TOPN:S(CA(A))=INT(S(CA(A))/2):NEXTA
10400 IFX<>25THEN10999
10410 PRINT"THE PHANTOMS OF TOXIS SOLANIS":PRINT"INVADE!":PRINT
10420 PRINT"THE CAPITAL, RASCALLION DURING":PRINT"NIGHT"
10430 PRINT"ALL TROOPS MYSTERIOUSLY":PRINT"DISAPPEAR!":PRINT
10432 FORX=1TO3000:NEXTX
10440 O(25)=0:S(25)=0
10999 RETURN 
11000 CLS
11010 PRINT "DO YOU WANT INSTRUCTIONS (Y/N)?";
11020 GOSUB6000:IFNLTHEN11999
11030 REM  INSTRUCTIONS
11040 CLS 
11050 PRINT "   INSTRUCTIONS FOR WARLORDS"
11060 PRINT "   ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ":PRINT
11080 PRINT "THE GAME BOARD IS 7 BY 7 SPACES.";
11100 PRINT "EACH SPACE MAY TAKE THIS FORM:"
11110 PRINT
11120 PRINT "     C92"
11130 PRINT "      4"
11140 PRINT 
11150 PRINT "THE C STANDS FOR CASTLE."
11160 PRINT "THE MIDDLE NUMBER IS POPULATION.";
11170 PRINT "THE RIGHT NUMBER IS THE WARLORD.";
11180 PRINT "THE NUMBER 4 UNDERNEATH IS THE"
11190 PRINT "NUMBER OF DIVISIONS."
11200 DIM Q$(10)
11210 GOSUB8
11230 PRINT "SO, A SQUARE WITH 6 DIVISIONS"
11240 PRINT "BELONGING TO WARLORD 3 AND IN"
11250 PRINT "A SWAMP WOULD LOOK LIKE THIS:"
11260 PRINT
11270 PRINT "     S3"
11280 PRINT "     6"
11290 PRINT 
11300 PRINT "THERE IS NO POPULATION NUMBER"
11310 PRINT "BECAUSE NO ONE LIVES IN THE"
11320 PRINT "SWAMPS.":GOSUB8
11330 PRINT "IF YOU SEE A NUMBER ALL ALONE:"
11340 PRINT:PRINT "   3":PRINT
11350 PRINT "IT MEANS IT IS OPEN COUNTRY."
11370 PRINT "IT ALSO DOES NOT HAVE ANY"
11380 PRINT "DIVISIONS. THE 3 IS THE":PRINT"POPULATION."
11390 GOSUB8
11400 PRINT "X IS THE CAPITAL. IF YOU SEE"
11410 PRINT "T4 THIS IS A TOWN WITH A"
11440 PRINT "POPULATION OF 4. IF WARLORD 1"
11450 PRINT "OWNED IT WITH 13 DIVISIONS,"
11470 PRINT "IT WOULD SHOW:":PRINT:PRINT"     T41"
11480 PRINT "     13"
11490 GOSUB8
11500 PRINT "KEEP IN MIND THAT THE MORE"
11510 PRINT "POPULATION COUNTS YOU OWN"
11520 PRINT "THE MORE TAX MONEY YOU GET!"
11530 PRINT 
11540 PRINT "ON THE OTHER HAND, BEING IN A"
11550 PRINT "SWAMP OR A FOREST GIVES YOU A"
11560 PRINT "MILITARY ADVANTAGE."
11590 PRINT "YOU WILL GAIN A SENSE OF THE "
11600 PRINT "BATTLE STRENGTHS FROM PLAYING"
11610 PRINT "THE GAME."
11620 GOSUB8
11630 PRINT "HINT! PROTECT YOUR CASTLE!"
11640 PRINT "IF YOU LOSE YOUR CASTLE YOU"
11650 PRINT "WILL NOT BE ABLE TO RAISE TAXES.";
11660 PRINT "SHOULD YOU SUFFER SUCH A LOSS,"
11670 PRINT "TRY TO GET IT BACK QUICKLY."
11675 PRINT "ALWAYS KEEP AT LEAST ONE"
11676 PRINT "DIVISION IN YOUR CASTLE!":PRINT
11700 PRINT "ALSO, IF YOU LOSE ALL OF YOUR"
11800 PRINT "DIVISIONS, YOU ARE ELIMINATED!":PRINT
11811 PRINT "A PIECE OF PAPER TO MAKE NOTES"
11812 PRINT "AS YOU PLAY IS RECOMMENDED."
11820 GOSUB8
11830 PRINT "     MOVEMENT OF DIVISIONS"
11832 PRINT "     ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
11836 PRINT "YOU WILL BE TOLD WHEN IT IS"
11838 PRINT "YOUR TURN TO MOVE DIVISIONS."
11840 PRINT "ALL PLAYERS WILL HAVE THE SAME"
11842 PRINT "NUMBER OF TURNS PER CAMPAIGN"
11844 PRINT "BUT NOT IN A SET ORDER."
11848 PRINT "ENTER THE CO-ORDINATES OF"
11850 PRINT "THE SPACE YOU WANT TO MOVE"
11852 PRINT "FROM FIRST... EXAMPLE: 23"
11854 PRINT "FOR 2 ROWS DOWN AND 3 COLUMNS"
11856 PRINT "ACROSS. DO THE SAME FOR THE"
11860 PRINT "SPACE YOU WANT TO MOVE TO."
11861 PRINT "THEN ENTER THE NUMBER OF"
11868 PRINT "DIVISIONS YOU WANT TO MOVE."
11870 GOSUB8
11872 PRINT "IF YOU FORGET WHERE YOU ARE"
11874 PRINT "YOU CAN ENTER A ZERO IN THE"
11876 PRINT "from OR to SPACE AND IT WILL"
11878 PRINT "SHOW YOU THE BOARD."
11882 PRINT 
11884 PRINT "KEEP IN MIND THAT YOU CAN ONLY"
11886 PRINT "MOVE TO A TOUCHING SQUARE."
11888 PRINT
11900 PRINT "********************************";
11902 PRINT "ALL IS FAIR IN WARLORDS. YOU CAN";
11904 PRINT "MAKE ANY DIRTY DEALS YOU WANT"
11906 PRINT "TO GANG UP ON SOMEONE AND THEN"
11908 PRINT "DOUBLE-CROSS THE OTHERS. OF"
11910 PRINT "COURSE THEY CAN DO THIS TOO!"
11912 PRINT "********************************";
11990 GOSUB8
11999 RETURN
