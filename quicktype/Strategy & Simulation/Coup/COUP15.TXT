0 CLS:PRINT@171,"COUP D'ETAT":CLEAR400:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENPRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 CC=(32-LEN(M$))/2:PRINT,,TAB(CC)M$:PRINT:RETURN
4 GOSUB1:GOTO396
5 PRINT@46,A$:PRINT@78,M$:PRINT@110,I$:PRINT@142,S$:PRINT:RETURN
6 CLS:GOSUB5:GOTO13
7 CLS:GOSUB5:ON-(CC>0)GOTO11:GOTO10
8 FORCC=1TO64:PRINTCHR$(8);:NEXT:RETURN
9 PRINTCHR$(127);:FORA=1TO1500:M$=INKEY$:ON-(M$<>"")GOSUB14:NEXT:PRINTCHR$(8);:RETURN
10 PRINT@114,"Õß";:GOTO12
11 PRINT@114,"µ¿";
12 PRINT@146,"…":PRINT
13 FORA=1TO500:NEXT:RETURN
14 A=1500:RETURN
15 PRINT"SOLD!":GOTO13
16 FA=0:GOTO396
17 CC=RND(3+LV)*PS:MY=MY+CC:M$="YOU'VE BEEN GIVEN A CONTRIBUTION OF"+STR$(CC)+" DOLLARS":GOTO4
18 NS=5+5*LV:FORZ=1TONS
19 HO=RND(30):IFSH(HO)=1THEN19
20 SH(HO)=1:NEXT:RETURN
21 M$="THE"+LT$+" HAS BEEN RECAPTURED":GOTO4
40 DIMC$(2),D$(2),H$(4),IN$(4),TW$(4),TR(13),SH(30),A$(5),TN(13),T(13)
41 FORA=1TO2:READC$(A):READD$(A):NEXT:FORA=1TO4:READH$(A):NEXT:FORA=1TO4:READIN$(A):READTW$(A):NEXT:FORA=1TO6
43 CC=RND(6):IFT(CC)THEN43
44 T(CC)=A:NEXT:FORA=7TO13:T(A)=A:NEXT
48 Y$="YOUR MAN ":E$="YOUR MEN HAVE ":FORA=1TO13:TR(A)=1:NEXT:FORE=1TO5:READA$(E):NEXTE:PRINT@298,"LEVEL (1-3)?":GOSUB471
49 INPUTM$:A=VAL(M$):IFA<1ORA>3THENPRINT:GOSUB8:GOTO49
50 LV=4-A:GOSUB18:W=1:PS=50:EL=LV*100:NL=10+5*LV:MY=1000:UF=40+(LV*3):ES=2100-LV*100:PL=1100-LV*100:MV=0:TK=1:GA=225+25*LV
51 CLS:M$="status":GOSUB3:PRINT"U.S. FRIENDSHIP:   "UF"%":PRINT"E.L.O. FORCES:     "EL:PRINT"POPULAR SUPPORT:   "PS"%"
52 PRINT"NO. OF LIEUTENANTS:"NL:PRINT"FIGHTING FUND:    $"MY:PRINT"NO. OF SAFE HOUSES:"NS
65 M$="TYPE OF WEAPONS:   "+TW$(W):GOTO4
66 IFNL<1OREL<1ORPS<1ORUF<1THEN747
67 IFBT=0ANDEL>LV*300THENEL=LV*300
68 IFES<0THENES=0
69 IFUF>100THENUF=100
70 IFPL<0THENPL=0
71 IFPS>100THENPS=100
72 IFNL>15+5*LVTHENNL=15+5*LV
73 CLS:M$="move":GOSUB3:PRINT"(0) MAP","(1) STATUS":PRINT"(2) BARRIO","(3) BANK":PRINT"(4) PORT","(5) SQUARE"
74 PRINT"(6) TV STATION","(7) POLICE":PRINT"(8) HOTEL","(9) PRISON":PRINT"(10) CHURCH","(11) BARRACKS"
75 PRINT"(12) PALACE","(13) TOWERS":GOSUB471
84 INPUTM$:MO=VAL(M$):IFMO=NXANDMO>1THENPRINTY$"HAS JUST BEEN THERE":GOSUB9:GOSUB8:GOTO84
85 IFMO=0THEN472
86 IFMO=1THEN51
87 NX=MO:MV=MV+1:IFMO>1ANDMO<14THENONMO-1GOTO99,117,134,157,175,216,253,280,324,338,378,352
89 PRINT:GOSUB8:GOTO84
99 GOSUB636:ONRND(6)GOTO102,104,106,111,112
100 IFTV=0THENGOSUB424:PS=PS-RND(8)+2:GOTO396
101 ONRND(LV)GOTO111,102,106
102 S=RND(PS)+LV:EL=EL+S:M$=MID$(STR$(S),2)+" NEW RECRUITS ASK TO JOIN E.L.O.":GOTO4
104 S=RND(50-LV*10):GOSUB800:EL=EL-S:M$=E$+"BEEN AMBUSHED BY THE ARMY":GOTO4
106 S=RND(9):IFFO>0THENPS=PS+S+LV:FO=FO-1:M$="YOUR PEOPLE GIVE FOOD TO THE POOR":GOTO4
108 PS=PS-S+LV:M$="THE PEOPLE ARE UNHAPPY THAT YOU HAVEN'T BROUGHT ANY FOOD":GOTO4
111 S=RND(14-LV):UF=UF-S:MY=MY+RND(100*LV):M$="ONE OF YOUR MEN ROBS A U.S. REPORTER":GOTO4
112 IFTR(11)=0THEN99
113 M$="YOUR TRUCK IS SEARCHED ON THE BRIDGE. ":Y=RND(LV+1):IFY>1THENM$=M$+"THEY COULDN'T FIND AN ELEPHANT":GOTO4
115 IFFO>0THENFO=FO-1:M$=M$+"THEY CONFISCATE YOUR FOOD":GOTO4
116 EL=EL-2:M$=M$+"THEY ARREST THE DRIVERS":GOTO4
117 LT$=" BANK":GOSUB680:A=RND(3):ONAGOTO797,123
118 K=RND(6+LV):IFBA>0ANDK<3ANDES>BATHENEL=EL-BA:BA=0:K=9:GOTO21
120 IFK>5THENMY=MY+RND(3+LV)*PS:M$="YOU'VE BEEN GIVEN SOME CASH":GOTO4
121 M$=Y$+"STILL CAN'T GET A LOAN":GOTO4
123 S=INT(ES/6):EN$=" SOLDIERS":GOSUB466:IFA$="N"THEN396
125 LO=BA:N=4:RS=0:MS=RND(1100+LV*100):M=RND(6-LV):T=6:GOSUB398:IFFA=1THEN16
127 IFAC=2ORAC=3THENES=ES-S:GOTO396
128 IFAC=1THENES=ES-S:BA=Z:MY=MY+MS:GOTO396
130 IFAC=4THEN396
131 IFAC=5ANDMY>M*STHENCC=M*S:MY=MY+MS-CC:M$="THEY TAKE"+STR$(CC)+" DOLLARS, YOU GET"+STR$(MS):GOTO4
132 GOTO396
134 GOSUB556
135 A=RND(6):ONAGOTO434,145,143,152,154
136 K=RND(7):IFK<4ANDES>10ANDTR(7)=1THENGOSUB800:NL=NL-1:M$=Y$+"DIDN'T GET PAST THE TOWER":GOTO4
140 IFK>5THEN17
141 IFK<3ANDRD=0THENRD=1:M$=Y$+"FOUND A CRATE OF RADIOS":GOTO4
142 M$="WATER LOOKS DIRTY":GOTO4
143 IFTV=0THENGOSUB424:GOTO396
144 GOTO135
145 IFBO=1THEN135
146 M$=Y$+"SEES A MOTOR BOAT AT THE QUAY. WOULD YOU LIKE TO BORROW IT?":GOSUB1:GOSUB468:IFA$="N"THEN396
149 CLS:S=RND(2+LV):IFS<=2THENNL=NL-1:M$="UNFORTUNATELY "+Y$+"FORGOT HE CAN'T SWIM":GOTO4
150 BO=1:M$="NICE BOAT YOU'VE GOT THERE":GOTO4
152 IFTR(7)=1THEN797
153 M$="NOT MUCH DOING":GOTO4
154 M$=Y$+"IS SHOT AT":GOSUB1:GOSUB800:A=RND(LV+1):IFA=1THENNL=NL-1:M$="A GOOD SHOT, UNFORTUNATELY":GOTO4
156 M$="THEY MISSED":GOTO4
157 GOSUB700
158 A=RND(5):ONAGOTO434,159,160,168,172
159 HO=RND(PS/4):IFSH(HO)=0ANDPS>55THENSH(HO)=1:NS=NS+1:M$="YOU HAVE GAINED A SAFE HOUSE":GOTO4
160 LO$="THE SQUARE"
161 M$=LO$+" HAS BEEN SURROUNDED BY TROOPS. "+Y$+"MUST GO TO A SAFE HOUSE. CHOOSE FROM 1-30":GOSUB1:GOSUB801:GOSUB471
163 INPUTM$:HO=VAL(M$):IFHO<1ORHO>30THENPRINT"ARE YOU ILLITERATE COMRADE?":GOSUB9:GOSUB8:GOTO163
164 IFSH(HO)>0THENM$="PHEW THAT WAS CLOSE":GOTO4
165 NL=NL-1:IFRND(2)=1THENM$="THE HOUSE HAS BEEN BETRAYED":GOTO4
166 M$="IT WASN'T VERY SAFE":GOTO4
168 IFHE=1ANDHD=1THENHD=0:M$="A BOX OF HELICOPTER PARTS HAS FALLEN OFF THE BACK OF A TRUCK. NUDGE! NUDGE!":GOTO4
169 GOTO158
172 M$=Y$+"MEETS A SHADY LOOKING TRADER... ":GOSUB1:IFFO>0ORRD=1ORVR=1ORGB=1ORJP=1THEN455
174 M$=M$+"WHO SELLS HIM SOME DIRTY POSTCARDS":GOTO4
175 LT$=" TV STATION":GOSUB519:A=RND(5):ONAGOTO182,191,194,197
176 K=RND(6+LV):IFTV>0ANDK<3ANDPL>TVTHENK=9:EL=EL-TV:TV=0:GOTO21
179 IFK>5THEN17
180 IFK<3ANDVR=0THENVR=1:M$=Y$+"FINDS SOME VIDEO RECORDERS":GOTO4
181 M$=Y$+"GETS A PART IN 'RATOK STREET'":GOTO4
182 S=INT(PL/4):EN$=" POLICE":GOSUB466:IFA$="N"THEN396
184 LO=TV:N=4:RS=0:P=RND(5)+1:M=RND(5):T=2:GOSUB398:IFFA=1THEN16
186 IFAC=2ORAC=3THENPL=PL-S:GOTO396
187 IFAC=1THENPL=PL-S:TV=Z:PS=100:GOTO396
189 IFAC=5ANDMY>M*STHENMY=MY-M*S:PS=PS+P*5:M$="THE POLICE TAKE"+STR$(M)+" EACH AND YOU BROADCAST":GOTO4
190 GOTO396
191 M$=Y$+"HAS BEEN SHOT BY GUARDS... ":GOSUB800:Y=RND(LV+1):IFY=1ANDTR(2)=1THENNL=NL-1:M$=M$+"STONE DEAD!":GOTO4
193 M$=M$+"ONLY WINGED HIM":GOTO4
194 M$=Y$+"IS SHOT AT BY SOLDIERS... ":GOSUB800:S=RND(LV+1):IFS=1THENNL=NL-1:M$=M$+"THAT'S ONE LESS FOR DINNER":GOTO4
196 M$=M$+"THEY COULDN'T HIT THE BROAD SIDE OF A BARN":GOTO4
197 M$=Y$+"MEETS A GOVERNMENT CONTACT ":IFHS>0THEN199
198 M$=M$+"WHO SAYS SOME RUDE THINGS ABOUT YOU":GOTO4
199 M$=M$+"DO YOU WISH TO EXCHANGE HOSTAGES?":GOSUB1:GOSUB468:IFA$="N"THEN396
202 M=RND(200)+200:IFLL>0THENN$=" LT. LAPIDUS":S=LV:GOSUB210:IFY=1THENLL=0:MY=MY+M:HS=HS-1:NL=NL+SS:GOTO396
205 IFCL>0THENN$=" CAPT. LE SHAN":S=1+LV:M=M*2:GOSUB210:IFY=1THENCL=0:MY=MY+M:HS=HS-1:NL=NL+SS:GOTO396
207 IFGJ>0THENN$=" GEN. JUNG":S=2+LV:M=M*3:GOSUB210:IFY=1THENGJ=0:MY=MY+M:HS=HS-1:NL=NL+SS:GOTO396
209 GOTO396
210 CLS:SS=RND(S)+1:M$="THE GOVERNMENT OFFERS TO RELEASE"+STR$(SS)+" LIEUTENANTS AND PAY"+STR$(M)+" DOLLARS "
212 M$=M$+"FOR"+N$+". DO YOU ACCEPT?":GOSUB1:GOSUB468:Y=0:IFA$="Y"THENY=1:PRINT"THEN WE HAVE AN AGREEMENT"
215 RETURN
216 LT$=" POLICE STATION":GOSUB661:ONRND(6)GOTO224,239,241,251,252
217 K=RND(6+LV):IFPH>0ANDK<3ANDES>PHTHENK=9:EL=EL-PH:PH=0:GOTO21
220 IFK>5THEN17
221 IFK=4ORK=5THEN377
222 IFK=2ORK=3THENIFGB=0THENGB=1:M$="YOU'VE AQUIRED A KILO OF COCAINE":GOTO4
223 M$=Y$+"IS GIVEN A SIGNED PHOTO OF THE FOOTBALL TEAM":GOTO4
224 S=PL:EN$=" POLICE":GOSUB466:IFA$="N"THEN396
226 LO=PH:N=4:RS=1:T=4:M=RND(500):GOSUB398:IFFA=1THEN16
228 IFAC=2THENPL=PL-S:GOTO396
229 IFAC=1THENPH=Z:PL=0:W=4:M$="YOU NOW HAVE AUTOMATIC WEAPONS":GOTO4
230 IFAC=3THENPL=PL-S:GOTO396
231 IFAC=4ANDCL=0THENONRND(3)GOTO236:CL=2:HS=HS+1:M$="YOU HAVE CAPTURED CAPT. LE SHAN":GOTO4
232 IFAC=4THEN323
233 IFAC=5ANDMY>M*STHENONRND(2)GOTO234,235
234 M$="THEIR RESPONSE IS NOT PRINTABLE":GOTO4
235 MY=MY-M:NL=NL+2:M$="I'LL LEAVE A CELL DOOR OPEN FOR"+STR$(M)+" DOLLARS":GOTO4
236 C=RND(Z):EL=EL-C:M$="YOUR ATTEMPT FAILED AND YOU LOST"+STR$(C)+" MEN":GOTO4
239 FO=FO+1:M$="YOU'VE LIBERATED SOME FOOD SUPPLIES FROM THE KITCHENS":GOTO4
241 J=RND(5+LV):IFJ=5ANDW>1THENW=1:M$="YOUR ARMS STORE IS FOUND":GOTO4
242 IFTR(11)=1ANDJ>5THENTR(11)=0:TN(11)=MV:M$="THE BRIDGE TOWER HAS BEEN ABANDONED":GOTO4
243 IFJ=1ANDDT>0THENDT=0:M$="YOUR DYNAMITE HAS BEEN DISCOVERED":GOTO4
244 IFJ=2ANDHD=1THENHD=0:M$="YOU'RE GIVEN SOME CHOPPER SPARES":GOTO4
245 M$=Y$+"IS RECOGNIZED AND RUNS FOR A SAFE HOUSE":GOSUB1:GOSUB801
246 PRINT"WHICH ONE? (1-30)":GOSUB471:GOTO163
251 IFTR(4)=1THEN797
252 GOTO377
253 GOSUB505:B=RND(10*UF)+100:ONRND(5)GOTO258,274,276,277:MY=MY+B
256 M$=Y$+"IS CONTACTED BY"+C$(RND(2))+" (THE WELL KNOWN C.I.A. AGENT) WHO GIVES HIM"+STR$(B)+" DOLLARS IN A"+H$(RND(4)):GOTO4
258 M=RND(300)+50:M$=Y$+"IS CONTACTED BY"+D$(RND(2))+" WHO OFFERS TO SELL INFORMATION FOR"+STR$(M)+" DOLLARS. WILL YOU?"
260 GOSUB1:IFM>MYTHENM$="HE WON'T TAKE I.O.U'S":GOTO4
261 GOSUB468:IFA$="N"THEN396
262 CLS:MY=MY-M:N=RND(4):M$=IN$(N):GOSUB1:IFN=1ORN=2THEN264
263 M$="NOT A LOT OF PEOPLE KNOW THAT":GOTO4
264 M$="WOULD YOU LIKE TO TRY A KIDNAP?":GOSUB1:GOSUB468:IFA$="N"THEN396
267 IFN=1ANDGJ>0THEN273
268 IFN=2ANDCL>0THEN273
269 IFRND(1+LV)=1THENNL=NL-1:M$="GOOD TRY. BAD RESULT":GOTO4
270 M$="YOU'VE GOT HIM":IFN=1THENGJ=1:HS=HS+1:GOTO4
272 IFN=2THENCL=1:HS=HS+1:GOTO4
273 M$="IT SEEMS SOMEONE BEAT YOU TO IT":GOTO4
274 IFTR(10)=1THEN797
275 M$="NICE HOTEL THIS":GOTO4
276 GOSUB800:NL=NL-1:M$=Y$+"IS SHOT BY POLICE ":GOTO4
277 IFBA=0ANDES<2000THENES=ES+RND(2000-ES):M$="DR. RATOK JUST HIRED SOME MERCENARIES":GOTO4
278 GOTO275
280 LT$=" PRISON":GOSUB597:ONRND(4)GOTO287,154,306:K=RND(6+LV)
282 IFPR>0ANDK<3ANDES>PRTHENK=9:EL=EL-PR:PR=0:GOTO21
284 IFK>5THENPS=PS+RND(15):M$="YOU'VE SMUGGLED IN SOME FOOD":GOTO4
285 IFK<6THENUF=UF+RND(12):M$=Y$+"HAS GOT EVIDENCE OF TORTURE":GOTO4
286 M$="I LIKE THESE NEO-GOTHIC BUILDINGS":GOTO4
287 M$="DO YOU WISH TO ATTEMPT A RESCUE:":GOSUB1:PRINT"(1) BY SEA","(2) BY AIR":PRINT"(3) NO WAY":GOSUB471
289 INPUTM$:A=VAL(M$):S=RND(4):IFA<1ORA>2THEN66
291 IFA=2AND(HE=0ORHD=1)THENM$="ARM FLAPPING IS TIRING WORK":GOTO4
292 IFA=1ANDBO=0THENNL=NL-1:M$="THE SHARKS ENJOYED THE MEAL":GOTO4
293 IFA=1ANDRP=0ANDS>2THENCLS:NL=NL-1:M$=Y$+"FELL SCALING THE CLIFFS":GOTO4
294 IFA=1ANDS>2THEN302
295 CLS:SS=RND(15)+5*LV:IFA=1ANDS=1THENEL=EL+SS:M$=Y$+"HAS RESCUED"+STR$(SS)+" COMRADES":GOTO4
297 IFA=1ANDS>1THENNL=NL-1:M$=Y$+"WAS CAUGHT ON THE BEACH MAKING SAND CASTLES":GOTO4
298 SS=RND(LV+1)+1:IFA=2THENNL=NL+SS:M$=MID$(STR$(SS),2)+" LIEUTENANTS HAVE BEEN RESCUED":GOSUB1
299 S=RND(6+LV):IFA=2ANDS>5THENHD=1:M$="YOUR HELICOPTER WAS DAMAGED":GOSUB1
300 GOTO396
302 CLS:SS=RND(15*LV)+5:EL=EL+SS:M$=Y$+"SCALED THE CLIFFS AND RESCUED"+STR$(SS)+" COMRADES":GOSUB1
303 M=RND(1+LV):IFM=1THENM$="BUT THE FOOL LEFT THE ROPE BEHIND":GOSUB1:RP=RP+(RP>0)
304 GOTO396
306 S=GA:EN$=" GUARDS":GOSUB466:IFA$="N"THEN396
308 LO=PR:N=4:RS=1:M=1:T=3:R=RND(400)+800:GOSUB398:IFFA=1THEN16
312 IFAC=2ORAC=3THENGA=GA-S:GOTO396
313 IFAC=1THENGA=GA-S:PR=Z:EL=EL+R:GOTO396
314 IFAC=4THENY=RND(3):GOTO320
315 IFAC=5ANDMY>M*STHENM=M*S:ONRND(1+LV)GOTO317,318:GOTO318
316 GOTO396
317 NL=NL-1:M$="WE DON'T TAKE BRIBES FROM PLEBS":GOTO4
318 MY=MY-M:NL=NL+J:M$="O.K. I'LL DELIVER THE CAKE (WITH FILE) FOR"+STR$(M)+" DOLLARS":GOTO4
320 IFY=1ANDLL=0THENLL=1:HS=HS+1:Y=5:M$="LT. LAPIDUS IS NOW YOUR GUEST":GOTO4
322 IFY=2THENNL=NL-1:M$=Y$+"BUNGLED IT":GOTO4
323 M$="THERE WAS NO ONE WORTH KIDNAPPING":GOTO4
324 GOSUB575:A=RND(5):ONAGOTO325,326,337,336:GOTO197
325 LO$="THE CHURCH":GOTO161
326 M$=MID$(D$(RND(2)),2)+" TELLS YOU THAT AN AMBASSADOR IS COMING. HOW ABOUT KIDNAPPING HIM?":GOSUB1:GOSUB468:IFA$="N"THEN396
330 CLS:S=RND(5):M$="YOU'VE GRABBED THE"+A$(S)+" AMBASSADOR. ":ONSGOTO333,333,332,333
331 UF=UF-19+LV:M$=M$+"THIS WAS NOT A SMART MOVE":GOTO4
332 PS=PS-19+LV:M$=M$+"WHAT ARE YOU TRYING FOR!? EXCOMMUNICATION?":GOTO4
333 M$=M$+"HOW MANY DOLLARS WILL YOU ASK FOR?":GOSUB1:GOSUB471:INPUTM
334 J=RND(500)+500:IFM>S*JTHENM$="IT SEEMS YOU CAN KEEP HIM":GOTO4
335 MY=MY+M:M$="IT'S A DEAL":GOTO4
336 MY=MY+RND(100):PS=PS-RND(14):M$="ALRIGHT, WHO STOLE THE POOR BOX?":GOTO4
337 M$=Y$+"GETS AN ASSESSMENT OF YOUR ASSETS":GOSUB1:GOTO784
338 LT$=" BARRACKS":GOSUB722:A=RND(5):ONAGOTO345,356,361,377:K=RND(6+LV)
340 IFBT>0ANDK<3ANDPL>BTTHENK=9:EL=EL-BT:BT=0:GOTO21
342 IFK>5THENMY=MY+RND(100):M$="YOU'VE LIBERATED SOME MONEY":GOTO4
343 IFK<3ANDJP=0THENJP=1:M$=Y$+"HAS FOUND AN ARMY JEEP":GOTO4
344 M$=Y$+"IS ASKED IF HE'D LIKE TO JOIN UP":GOTO4
345 G=RND(13):IFTR(G)=1THEN348
346 IFMV-TN(G)<20THEN345
347 TR(G)=1:M$="NO."+STR$(T(G))+" GUARD TOWER HAS BEEN REBUILT":GOTO4
348 GOSUB613:M$="NO."+STR$(T(G))+" GUARD TOWER IS CURRENTLY UNMANNED. WANT TO BLOW IT UP?":GOSUB1:GOSUB468:IFA$="N"THEN396
349 IFDT<1THENM$="WHAT WITH? A BIKE PUMP?!":GOTO4
350 S=RND(1+LV):Z=RND(20)+8:IFS=1THENEL=EL-RND(10):DT=DT-1:M$="THE BUTTERFINGERED IDIOTS BLEW THEMSELVES UP!":GOTO4
351 TR(G)=0:TN(G)=MV:DT=DT-1:PL=PL-Z:M$="THAT'S WHAT I CALL A BANG!":GOTO4
352 GOSUB613:ONRND(3)GOTO345,153,154
356 M$=Y$+"HAS BEEN CAPTURED AND IS BEING INTERROGATED. HE HAS GIVEN THE ADDRESS OF A HOUSE...":S=RND(30):IFSH(S)=1THEN359
358 EL=EL-1:M$=M$+" IT BELONGS TO THE PRESIDENT'S MISTRESS":GOTO4
359 J=RND(10-LV):EL=EL-J:SH(S)=0:NS=NS-1:M$=M$+STR$(J)+" OF YOUR MEN ARE CAUGHT THERE":GOTO4
361 IFES<200THENES=200
362 S=ES:EN$=" SOLDIERS":GOSUB466:IFA$="N"THEN396
366 LO=BR:N=4:RS=1:M=RND(1000):T=5:GOSUB398:IFFA=1THEN16
369 IFAC=1THENES=ES-S:BT=Z:BZ=1:M$="YOU NOW HAVE A BAZOOKA":GOTO4
370 IFAC=2ORAC=3THENES=ES-S:GOTO396
372 IFAC=4THENONRND(3)GOTO374,375,376
373 M$="YOU CAN'T MATCH THE DOC'S PAY":GOTO4
374 IFGJ=0THENGJ=1:HS=HS+1:M$="GEN. JUNG IS NOW YOUR GUEST":GOTO4
375 NL=NL-1:M$=Y$+"BUNGLED IT":GOTO4
376 M$=E$+"CAPTURED A CRATE OF BEER":GOTO4
377 M$=Y$+"SEES A MAP":GOSUB1:MP=1:GOSUB9:GOTO472
378 LT$=" PALACE":GOSUB539:ONRND(2)GOTO379,383
379 M$=Y$+"SEES A CUBAN ADVISOR. ":ONRND(4)GOTO381,382:UF=UF+RND(14):M$=M$+Y$+"CAPTURED HIM":GOTO4
381 NL=NL-1:M$=M$+Y$+"IS CAPTURED":GOTO4
382 M$=M$+"HOW EMBARASSING, HE'S A CIGAR SALESMAN":GOTO4
383 S=400:EN$=" SOLDIERS":GOSUB466:IFA$="N"THEN396
387 LO=0:N=B:RS=1:T=1:GOSUB398:IFFA=1THEN16
389 IFAC=1ANDBZ=0THENEL=EL+C1-Z:M$="... BUT "+E$+"BEEN WIPED OUT BY BAZOOKA ATTACK FROM THE BARRACKS":GOTO4
390 IFAC=1THENGOSUB9:GOTO744
393 IFAC=2ORAC=3THENES=ES-S:GOTO396
394 IFAC=4THENM$="YOU MUST BE JOKING":GOTO4
395 M$="YOU COULDN'T PAY OUR BAR BILL":GOTO4
396 IFA$="N"THENA$="":GOTO66
397 GOSUB9:GOTO66
398 IFLO>0THENM$="IDIOT, YOU'VE ALREADY LIBERATED THE"+LT$:GOSUB1:FA=1:RETURN
399 CLS:L=0
400 M$="action":GOSUB3:PRINT"(1) CAPTURE","(2) RAID":PRINT"(3) ASSASSINATE","(4) KIDNAP":PRINT"(5) OFFER BRIBES":GOSUB471
407 INPUTM$:AC=VAL(M$):FA=0:IFAC<1ORAC>5THENPRINT"COMO?":GOSUB9:GOSUB8:GOTO407
408 IFAC=1ORAC=2THEN410
409 Z=20:GOTO418
410 INPUT"HOW MANY MEN WILL YOU SEND";Z:Y=Z*W:U=S*RND(2):IFZ>ELTHENPRINT"YOU'RE NOT THAT POPULAR":GOSUB9:GOSUB8:GOTO408
412 IFY<UTHEN415
413 K=RND(1+LV):IFTR(T)=1ANDK=1THEN415
414 GOTO416
415 C1=RND(INT(Z/W)):EL=EL-C1:NL=NL-1:FA=1:M$="YOUR ATTEMPT FAILED":GOTO1
416 IFAC=1THENM$="YOU HAVE TAKEN THE"+LT$:GOTO1
418 IFAC=2ORAC=3THENGOSUB803:S=Y:Z=U:EL=EL-Z:M$=E$+"KILLED"+STR$(S)+EN$+" AND LOST"+STR$(Z):GOTO1
420 IFAC=4ANDRS=0THENM$="NO RANSOM IS PAID FOR"+EN$:GOTO1
421 IFAC=4THENRETURN
422 IFAC=5ANDMY>M*STHENRETURN
423 M$="YOU HAVEN'T GOT ENOUGH TO BRIBE THESE GUYS":GOTO1
424 M$="DR. RATOK IS BROADCASTING ONE OF HIS 6 HOUR SPEECHES ABOUT YOU. THE PEOPLE BLAME YOU FOR HAVING TO PUT UP WITH IT"
425 GOTO1
426 DATA" JOHN KIPINSKI"," LOPEZ"," RAOUL SMITH"," GOMEZ"," STUFFED OWL","N OLD SOCK"," COPY OF THE DIAZ TIMES"," TRUSS"
429 DATA"GEN. JUNG IS ABOUT TO VISIT THE HOTEL"," KNIVES","CAPT. LE SHAN IS ABOUT TO VISIT THE HOTEL"," HAND GUNS"
431 DATA"LT. LAPIDUS WEARS TIGHTS"," RIFLES","SGT. SADOUL HATES MICE"," AUTOMATIC WEAPONS"
433 DATA" CUBAN"," RUSSIAN"," PAPAL"," SYRIAN"," AMERICAN"
434 M$=Y$+"MEETS AN ARMS DEALER WITH... A CATALOGUE":GOSUB1:GOSUB9
435 CLS:M$="KHAGOSSHI GLOBAL ARMS SALES... (MINIMUM ORDER OF 300 FOR GUNS)":GOSUB1
436 HA=RND(4)+5:RI=RND(9)+10:DY=RND(99)+99:RO=RND(50)+50:PRINT
437 PRINT"(1)"TW$(2);HA"EACH":PRINT"(2) RIFLES"RI"EACH":PRINT"(3)"LV"DYNAMITE"DY:PRINT"(4) ROPE"RO
439 IFHE>0ORMY<3500THEN442
440 M$="(5) *SPECIAL OFFER* EX-U.S. ARMY HELICOPTER (LOW MILEAGE)":GOSUB1
442 PRINT"(6) NOTHING":M$="WHICH ITEM WOULD YOU LIKE TO BUY?":GOSUB1:GOSUB471:INPUTM$:AB=VAL(M$)
444 IFAB=1ANDMY>=HA*300THENGOSUB15:W=2:MY=MY-HA*300:GOTO66
446 IFAB=2ANDMY>=RI*300THENGOSUB15:W=3:MY=MY-RI*300:GOTO66
448 IFAB=3ANDMY>=DYTHENGOSUB15:MY=MY-DY:DT=DT+LV:GOTO66
450 IFAB=4ANDMY>=ROTHENGOSUB15:MY=MY-RO:RP=RP+1:GOTO66
452 IFAB=5ANDMY>=3500THENGOSUB15:MY=MY-3500:HE=1:GOTO66
453 IFAB>0ANDAB<6THENPRINT"YOU CAN'T AFFORD IT":GOTO396
454 GOTO66
455 GOSUB1:GOSUB9:CLS:M$="THE BLACK MARKETEER OFFERS":GOSUB1
456 IFFO>0THENM=RND(400+100*LV):SP$="SOME FOOD":GOSUB462:IFA$="Y"THENFO=FO-1
457 IFRD=1THENM=RND(900+100*LV):SP$="YOUR RADIOS":GOSUB462:IFA$="Y"THENRD=2
458 IFVR=1THENM=RND(1400+100*LV):SP$="YOUR VIDEOS":GOSUB462:IFA$="Y"THENVR=2
459 IFJP=1THENM=RND(600+100*LV):SP$="YOUR JEEP":GOSUB462:IFA$="Y"THENJP=2
460 IFGB=1THENM=RND(2000+500*LV):SP$="YOUR COCAINE":GOSUB462:IFA$="Y"THENGB=2
461 GOTO396
462 M$=MID$(STR$(M),2)+" DOLLARS FOR "+SP$+". WHAT ABOUT IT?":GOSUB1:GOSUB468:IFA$="Y"THENMY=MY+M:PRINT"DEAL!"
465 RETURN
466 M$=Y$+"IS TOLD THAT THERE ARE"+STR$(S)+EN$+" IN THE AREA. WILL YOU TAKE ACTION?":GOSUB1
468 M$="* PRESS y OR n *":GOSUB3
469 A$=INKEY$:IFA$=""THEN469
470 ON-(A$<>"Y"ANDA$<>"N")GOTO469:RETURN
471 M$="* SELECT # THEN HIT enter *":GOTO3
472 CLS:PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯city¯of¯diaz¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯n¯¯¯¯¯¯¯ŒŒŒŸ ¯®¯¯¯¯¯¯¯¯Ÿ…";
473 PRINT"¯¯¯¯¯¯¯¯ˆ€€ŠŠ…‚‡CHURCH€€ …¯¯¯¯¯¯¯¯ PALACEŸ Š…€…POLICE…¯¯¯¯®ŒŒŒŒŒŒŒŒŒŒˆŠ… BANK€€Ÿ  Ÿ…";
474 PRINT"¯¯¯¯Ÿ BARRACKS Š€€€€€€€€€€€€€€€€¯¯¯ Š€…€€Š€…Ÿ  Š…ŒŒHOTELŠSQ…¯¯ŒŒŒŒŒŒŒŒŒŒŒŒˆŠ…ƒƒŠŒŒ…";
475 PRINT"¯¯¯Ÿ   PRISON Ÿ Š…€…TV…¯¯¯¯¯ Š€€€€…  ŠŠ„ŒŒPORT…¯¯¯¯¯¯‹ƒƒ‡ Š ‹ƒƒ€Ÿ  ¯¯¯Ÿ…";
476 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯ Š ¯¯¯¯« ­¯¯¯¯¯¯¯…¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯« ­¯¯¯WALL¯¯Ÿ   BARRIOŸ  ¯¯¯¯¯¯¯« ¯¯…";
477 PRINT"TOWER€ROAD";:IFMP=0THEN490
480 PRINT@126,MID$(STR$(T(4)),2);:PRINT@144,MID$(STR$(T(1)),2);:PRINT@188,MID$(STR$(T(6)),2);
481 PRINT@228,MID$(STR$(T(5)),2);:PRINT@292,MID$(STR$(T(3)),2);:PRINT@304,MID$(STR$(T(2)),2);
482 PRINT@80,"8";:PRINT@191,"9";:PRINT@240,"10";:PRINT@376,"11";:PRINT@383,"7";:PRINT@451,"12";:PRINT@462,"13";:MP=0
490 POKE16482,30:GOTO396
505 A$="…":M$="…":I$="…":S$="€€…":GOTO6
519 A$="Š":M$="ˆ":I$="…":S$="€€…":CC=TV:GOTO7
539 A$="ŽŽŽ":M$="Š‚‚":I$="…":S$="€€…":CC=0:GOTO7
556 A$="Ž":M$="Ž‡Ž":I$="…€…":S$="…£§":GOTO6
575 A$="":M$="€…€…":I$="…":S$="€€…":GOTO6
597 A$="":M$="……":I$="€€€…":S$="€€…":CC=PR:GOTO7
613 A$="Œ":M$="Š„€":I$="€…":S$=I$:GOTO6
636 A$="":M$="":I$="Ž":S$="€ˆ€":GOTO6
661 A$="…":M$="€€…":I$="Œ":S$="‚ƒ‚‡":CC=PH:GOTO7
680 A$="":M$="ŽŒŒ":I$="…":S$="€€…":CC=BA:GOTO7
700 A$="ˆ‰":M$="‹Š":I$="Ž„„":S$="ŠØÀÐØ":GOTO6
722 A$="":M$="ŒŒŒ":I$="„„„…":S$="„„…":CC=BT:GOTO7
744 CLS:M$="CONGRATULATIONS!":GOSUB3
745 M$="PRESIDENT FOR LIFE DR. EMMANUEL RATOK HAS RESIGNED AFTER"+STR$(MV)+" MOVES. FANCY THE JOB?":GOSUB1
746 PRINT:GOSUB749:GOSUB9:GOTO763
747 CLS:M$="FAILURE!":GOSUB3
748 M$="AFTER"+STR$(MV)+" MOVES YOU HAVE BEEN ARRESTED AFTER HAVING":GOSUB1:GOSUB749:GOTO757
749 PRINT"CAPTURED (AT LEVEL"STR$(4-LV)"):":IFBT=0ANDPR=0ANDTV=0ANDPH=0ANDBA=0THENPRINT"NOTHING":RETURN
750 IFBT>0THENPRINT"THE BARRACKS"
751 IFPR>0THENPRINT"THE PRISON"
752 IFTV>0THENPRINT"THE TV STATION"
753 IFPH>0THENPRINT"THE POLICE STATION"
754 IFBA>0THENPRINT"THE BANK"
756 RETURN
757 PRINT:PRINT"ˆŒ               ˆ":PRINT"‰               ‰":GOSUB9:GOSUB8
758 PRINT"ˆŒ               Ž„":PRINT"‰               Œˆ…":GOSUB800:END
763 GOSUB700:M$="THEY ERECT A NEW STATUE IN THE CENTRAL SQUARE IN YOUR HONOUR":GOSUB1
764 M$="FUNNY... IT LOOKS A LOT LIKE THE OLD ONE...":GOSUB1:PRINT:END
784 IFDT>0THENPRINTMID$(STR$(DT),2)" STICK(S) OF DYNAMITE"
785 IFRP>0THENPRINTMID$(STR$(RP),2)" ROPE(S)"
786 IFBO=1THENPRINT"A BOAT"
787 IFHE=1THENPRINT"A HELICOPTER ";:IFHD=1THENPRINT"(SLIGHTLY DAMAGED)";
788 IFHE=1THENPRINT
789 IFHS>0THENPRINTMID$(STR$(HS),2)" HOSTAGE(S)"
790 IFGB=1THENPRINT"A KILO OF COCAINE"
791 IFJP=1THENPRINT"A JEEP"
792 IFVR=1THENPRINT"VIDEO RECORDERS"
793 IFRD=1THENPRINT"RADIO'S"
794 IFFO>0THENPRINTMID$(STR$(FO),2)" FOOD BOX(ES)"
795 IFDT>0ORRP>0ORBO>0ORHE>0ORHS>0ORGB>0ORJP>0ORVR>0ORRD>0THEN396
796 M$="YOU'RE NOT DOING VERY WELL, ARE YOU?":GOTO4
797 GOSUB800:M$=Y$+"IS SHOT BY THE OPPOSITION. ":Y=RND(LV+1):IFY=1THENNL=NL-1:M$=M$+"ANOTHER MARTYR FOR THE CAUSE":GOTO4
799 M$=M$+"OUCH! PAINFUL BUT NOT FATAL":GOTO4
800 FORA=1TO4:SOUND1,1:FORB=1TO50:NEXT:NEXT:RETURN
801 FORA=1TO10:SOUND200,1:NEXT:RETURN
803 Y=INT(RND(Z)*W):IFY>STHENY=S
804 U=INT(RND(Z+10)/W):IFAC=2THENIFU>ZTHENU=Z
805 RETURN
