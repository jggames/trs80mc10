0 DIMX2(40),Y2(40),C2(40),X,Y,A,B,C,D,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,Z,SH,HS,SC,R,I$,HS$:GOTO900
900 Q=3.1415:H=.8:K=31:M=32:N=25:O=180:T=.02:W=50
1000 CLS:PRINT"            TREBUCHET":HS=33:HS$="CFG":R=0:SH=0:
1010 PRINT " ------------------------------" 
2000 PRINT " A TREBUCHET IS A TYPE OF "
2001 PRINT " MEDIEVAL WEAPON THAT WAS USED "
2002 PRINT " TO THROW PROJECTILES INTO AN  "
2003 PRINT " ENEMY FORTIFICATION.  IN THIS "
2004 PRINT " GAME YOUR JOB IS TO THROW A   "
2005 PRINT " PROJECTILE FROM THE GREEN BASE"
2006 PRINT " TO HIT THE RED TARGET.  YOU   "
2007 PRINT " CONTROL THE FLIGHT BY         "
2008 PRINT " SELECTING THE INITIAL SPEED   "
2009 PRINT " AND ANGLE.  YOU HAVE 5 ROUNDS "
2010 PRINT " TO ACHIEVE YOUR SCORE/AVERAGE."
2011 PRINT " ------------------------------":PRINT
2013 PRINT@489,"PRESS ANY KEY";
2015 I$=INKEY$:G=RND(1000):IFI$=""THEN2015
2016 CLS0:RR=8:GOSUB4040:PRINT@(M*10)+26,"ROUND";:PRINT@(M*11)+26,RIGHT$("  "+STR$(R+1)+"  ",5);
2017 PRINT@(M*13)+26,"HIGH:";
2018 PRINT@(M*14)+26,RIGHT$("  "+STR$(INT(HS)),4);"%";
2019 PRINT@(M*15)+26," ";HS$;" ";
2020 G=26+RND(18):L=G-1
2030 A=RND(N):IFINT(A/2)-(A/2)=0THEN2030
2040 B=RND(N):IFINT(B/2)-(B/2)=0THEN2040
2050 C=ABS(K-A)-1:D=ABS(K-B)-1
2060 FORI=0TO24:FORJ=0TOC:SET(I,J,6):NEXT:NEXT
2090 FORI=25TO49:FORJ=0TOD:SET(I,J,6):NEXT:NEXT
2120 IFA>BTHENS=-1:GOTO2130
2121 S=1
2130 FORI=ATOBSTEPS:RESET(N,ABS(K-I)):NEXT
2170 SET(10, ABS(K-A),1)
2190 SET(G+1,ABS(K-B),4)
2200 SET(G+2,ABS(K-B),4)
2210 SET(G+3,ABS(K-B),4)
2230 FOR I=B+1 TO B+3
2240 RESET(G+1,ABS(K-I))
2250 RESET(G+2,ABS(K-I))
2260 RESET(G+3,ABS(K-I))
2270 RESET(G,ABS(K-(I+2)))
2280 RESET(G+4,ABS(K-(I+1)))
2290 RESET(G+2,ABS(K-(I+3)))
2300 NEXT
3000 X=10
3020 Y=A+1
3040 PRINT@(M*0)+26,"SPEED ";:PRINT@(M*1)+26,"(1-10)";:PRINT@(M*2)+26,"";:INPUTP
3050 IFP>=1ANDP<=10THEN3080
3060 SOUND1,1
3070 GOTO3040
3080 PRINT@(M*4)+26,"ANGLE ";:PRINT@(M*5)+26,"(0-90)";:PRINT@(M*6)+26,"";:INPUTF
3090 IFF>=0ANDF<=90THEN3130
3100 SOUND1,1
3120 GOTO3080
3130 SH=SH+1:RR=8:GOSUB4040
3150 U=P/5*COS(F/O*Q)
3170 V=P/5*SIN(F/O*Q)
3175 Z=6
3190 ON1-(Y<M)GOTO3210:ON1-(Z<1)GOTO3200:RESET(X,ABS(K-Y)):GOTO3210
3200 SET(X,ABS(K-Y),Z)
3210 X=X+U:Y=Y+V
3240 ON1-((X<NANDY<A)ORX>W)GOTO3260,4000
3260 ON1-(X>NANDY<B)GOTO3275,3500
3275 ON1-(Y<M)GOTO3280:Z=POINT(X,ABS(K-Y))
3280 ON1-(Y<M)GOTO3310:RESET(X,ABS(K-Y))
3310 V=V-T:GOTO3190
3500 IFABS(X-(G+2))>2THEN4000
3520 PRINT@(M*8)+26,"HIT!";CHR$(128);CHR$(128);:FORI=1TO40:X2(I)=RND(5)+L:Y2(I)=RND(5)+B:C2(I)=8:NEXT
3620 FORJ=1TO5:FORI=1TO40:ON1-(Y2(I)<B+3)GOTO3641:SET(X2(I),ABS(K-Y2(I)),6):RESET(X2(I),ABS(K-Y2(I))):GOTO3650
3641 SET(X2(I),ABS(K-Y2(I)),6)
3650 Y2(I)=Y2(I)+1
3660 SET(X2(I),ABS(K-Y2(I)),C2(I))
3670 ON1-(RND(0)>H)GOTO3680:C2(I)=4
3680 ON1-(RND(0)>H)GOTO3690:C2(I)=8
3690 ON1-(RND(0)>H)GOTO3700:C2(I)=5
3700 ON1-(RND(0)>.3ANDY2(I)<B+9)GOTO3710,3750
3710 ON1-(Y2(I)<B+3)GOTO3711:SET(X2(I),ABS(K-Y2(I)),6):RESET(X2(I),ABS(K-Y2(I))):GOTO3720
3711 SET(X2(I),ABS(K-Y2(I)),6) 
3720 X2(I)=RND(5)+L:Y2(I)=B+1:C2(I)=8
3750 NEXT:NEXT
3780 FORI=1TO40
3790 IFY2(I)<B+3THENSET(X2(I),ABS(K-Y2(I)),6):RESET(X2(I),ABS(K-Y2(I))):GOTO3800
3791 SET(X2(I),ABS(K-Y2(I)),6)
3800 NEXT:R=R+1:IFR<5THEN2013
3805 SC=5/SH*100:IFSC<HSTHEN3830
3810 HS=SC
3815 RR=8:GOSUB4040:PRINT@(M*8)+26,"INIT- ";:RR=9:GOSUB4040:PRINT@(M*9)+26,"IALS  ";
3820 RR=11:GOSUB4040:PRINT@(M*10)+26,"      ";:PRINT@(M*10)+26,"";:INPUTHS$:HS$=LEFT$(HS$,3) 
3830 RR=8:GOSUB4040:PRINT@(M*10)+26,"SCORE:";:RR=9:GOSUB4040:PRINT@(M*11)+26,LEFT$(STR$(SC)+"   ",5);"%";
3840 PRINT@(M*13)+26,"HIGH:";
3850 PRINT@(M*14)+26,RIGHT$("  "+STR$(INT(HS)),4);"%";
3855 PRINT@(M*15)+26," ";HS$;" ";
3860 PRINT@489," PLAY AGAIN? ";
3880 I$=INKEY$:IFI$=""THEN3880
3885 IFI$="N"THENEND
3890 IFI$="Y"THENSH=0:R=0:GOTO2016
3895 GOTO3880
4000 PRINT@(M*8)+26,"MISS!";
4010 GOTO3000
4040 PRINT@(M*RR)+26,CHR$(128);CHR$(128);CHR$(128);CHR$(128);CHR$(128);CHR$(128);:RETURN
