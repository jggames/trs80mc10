1 PRINT CHR$(147): POKE 53280,0: POKE 53281,0:POKE 646,7
2 PRINT "    {REVERSE ON}ORBIT{DOWN*2}{REVERSE OFF}"
4 PRINT "    CREATIVE COMPUTING"
5 PRINT "    MORRISTOWN, NEW JERSEY"
6 PRINT:PRINT
10 PRINT "SOMEWHERE ABOVE YOUR PLANET THERE IS A "
15 PRINT "ROMULAN SHIP. THE SHIP IS IN A CONSTANT"
20 PRINT "POLAR ORBIT. ITS DISTANCE FROM THE "
25 PRINT "CENTER OF YOUR PLANET IS FROM 10,000 TO"
30 PRINT "30,000 MILES AND AT ITS PRESENT "
31 PRINT "VELOCITY CAN CIRCLE YOUR PLANET"
35 PRINT "ONCE EVERY 12 TO 36 HOURS."
38 PRINT
40 PRINT "UNFORTUNATELY, THEY ARE USING A "
45 PRINT "CLOAKING DEVICE SO YOU ARE UNABLE TO"
50 PRINT "SEE THEM, BUT WITH A SPECIAL INSTRUMENT"
52 PRINT "YOU CAN TELL HOW NEAR THEIR SHIP YOUR"
53 PRINT "PHOTON BOMB EXPLODED."
54 PRINT: PRINT: PRINTTAB(8)"{REVERSE ON}HIT ANY KEY TO CONTINUE{REVERSE OFF}"
55 GET A$:IF A$="" GOTO 55
58 PRINT CHR$(147)
60 PRINT "YOU HAVE SEVEN HOURS UNTIL THEY HAVE"
64 PRINT "BUILT UP SUFFICIENT POWER IN ORDER TO"
65 PRINT "ESCAPE YOUR PLANET'S GRAVITY."
70 PRINT
75 PRINT "YOUR PLANET HAS ENOUGH POWER TO FIRE "
80 PRINT "ONE BOMB AN HOUR."
82 PRINT
85 PRINT "AT THE BEGINNING OF EACH HOUR YOU WILL "
90 PRINT "BE ASKED TO GIVE AN ANGLE (BETWEEN 0"
95 PRINT "AND 360) AND A DISTANCE IN UNITS OF 100"
96 PRINT "MILES (BETWEEN 100 AND 300), AFTER"
100 PRINT "WHICH YOUR BOMB'S DISTANCE FROM THE"
102 PRINT "ENEMY SHIP WILL BE GIVEN."
105 PRINT
110 PRINT "AN EXPLOSION WITHIN 5,000 MILES OF THE"
111 PRINT "ROMULAN SHIP WILL DESTROY IT."
112 PRINT:PRINT"HERE IS THE ORBIT AND ITS ANGLES"
115 PRINT: PRINT: PRINTTAB(8)"{REVERSE ON}HIT ANY KEY TO CONTINUE{REVERSE OFF}"
116 GET A$:IF A$="" GOTO 116
117 PRINT CHR$(147)
168 PRINT "                  90"
170 PRINT "            0000000000000000"
171 PRINT "          00000000000000000000"
173 PRINT "        00000             00000"
174 PRINT "       00000    XXXXXXX    00000"
175 PRINT "      00000    XXXXXXXXX    00000"
176 PRINT "     0000     XXXXXXXXXXX     0000"
177 PRINT "    0000     XXXXXXXXXXXXX     0000"
178 PRINT "1  0000     XXXXXXXXXXXXXXX     0000"
179 PRINT "8<00000     XXXXXXXXXXXXXXX     00000>0"
180 PRINT "0  0000     XXXXXXXXXXXXXXX     0000"
181 PRINT "    0000     XXXXXXXXXXXXX     0000"
182 PRINT "     0000     XXXXXXXXXXX     0000"
183 PRINT "      00000    XXXXXXXXX    00000"
184 PRINT "       00000    XXXXXXX    00000"
185 PRINT "         0000             0000"
186 PRINT "          0000000000000000000"
187 PRINT "           00000000000000000"
188 PRINT "                  270"
190 PRINT " X - YOUR PLANET"
191 PRINT " O - THE ORBIT OF THE ROMULAN SHIP":PRINT
192 PRINTTAB(8)"{REVERSE ON}HIT ANY KEY TO CONTINUE{REVERSE OFF}"
193 GET A$:IF A$="" GOTO 193
197 PRINT CHR$(147)
200 PRINT "ON THE ABOVE DIAGRAM, THE ROMULAN SHIP"
207 PRINT "IS CIRCLING COUNTERCLOCKWISE AROUND"
208 PRINT "YOUR PLANET. DON'T FORGET THAT WITHOUT"
212 PRINT "SUFFICIENT POWER THE ROMULAN SHIP'S"
215 PRINT "ALTITUDE AND ORBITAL RATE WILL REMAIN"
218 PRINT "CONSTANT."
220 PRINT
230 PRINT "GOOD LUCK."
240 PRINT "THE FEDERATION IS COUNTING ON YOU."
270 A=INT(360*RND(1))
280 D=INT(200*RND(1)+200)
290 R=INT(20*RND(1)+10)
300 H=0
310 IF H=7 THEN 490
320 H=H+1
325 PRINT
326 PRINT
330 PRINT "{CYAN}THIS IS HOUR";H;
332 PRINT "AT WHAT ANGLE DO YOU WISH TO SEND"
335 PRINT "YOUR PHOTON BOMB{YELLOW}";
340 INPUT A1
350 PRINT "{CYAN}HOW FAR OUT DO YOU WISH"
355 PRINT "TO DETONATE IT{YELLOW}";
360 INPUT D1
365 PRINT
366 PRINT
370 A=A+R
380 IF A<360 THEN 400
390 A=A-360
400 T=ABS(A-A1)
410 IF T<180 THEN 430
420 T=360-T
430 C=SQR(D*D+D1*D1-2*D*D1*COS(T*3.14159/180))
440 PRINT "YOUR PHOTON BOMB EXPLODED"
442 PRINT C;"* 10^2 "
445 PRINT "MILES FROM THE ROMULAN SHIP."
450 IF C<=50 THEN 470
460 GOTO 310
470 PRINT:PRINT "{RED}YOU HAVE SUCCESFULLY"
475 PRINT "COMPLETED YOUR MISSION.{YELLOW}":PRINT
480 GOTO 500
490 PRINT: PRINT "{RED}YOU HAVE ALLOWED THE ROMULANS"
495 PRINT "TO ESCAPE. YOU LOST!{YELLOW}":PRINT
500 PRINT "ANOTHER ROMULAN SHIP"
505 PRINT "HAS GONE INTO ORBIT."
510 PRINT "DO YOU WISH TO TRY TO DESTROY IT";
520 INPUT C$
530 IF C$="YES" THEN 270
540 PRINT "GOOD BYE."
999 END
