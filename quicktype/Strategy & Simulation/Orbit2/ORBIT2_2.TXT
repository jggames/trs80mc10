0 CLEAR500:DIMC1,C2,M$,A$:M=16384:X=16:Y=4:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:A$=MID$(M$,C1,1):IFA$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFA$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 FORC1=1TOLEN(M$):C2=VAL(MID$(M$,C1,1)):IFC2THENSET(X,Y,C2)
4 X=X+1:NEXT:Y=Y+1:X=16:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
9 PRINT@484,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7:FORC1=0TO1:C1=-(INKEY$<>""):NEXT:RETURN

10 CLS:PRINTTAB(14)"orbit":PRINTTAB(7)"CREATIVE COMPUTING":PRINTTAB(5)"MORRISTOWN, NEW JERSEY"
11 PRINTTAB(7)"JEFF LEDERER 1978":PRINT

12 M$="SOMEWHERE ABOVE YOUR PLANET THERE IS A ROMULAN SHIP. THE SHIP IS IN A CONSTANT POLAR ORBIT.":GOSUB1
15 M$="ITS DISTANCE FROM THE CENTER OF YOUR PLANET IS FROM 10,000 TO 30,000 MILES AND AT ITS PRESENT "
31 M$=M$+"VELOCITY CAN CIRCLE YOUR PLANET ONCE EVERY 12 TO 36 HOURS.":GOSUB1
38 GOSUB9:CLS
40 M$="UNFORTUNATELY, THEY ARE USING A CLOAKING DEVICE SO YOU ARE UNABLE TO SEE THEM, BUT WITH A SPECIAL INSTRUMENT "
52 M$=M$+"YOU CAN TELL HOW NEAR THEIR SHIP YOUR PHOTON BOMB EXPLODED.":GOSUB1
60 M$="YOU HAVE SEVEN HOURS UNTIL THEY HAVE BUILT UP SUFFICIENT POWER IN ORDER TO "
65 M$=M$+"ESCAPE YOUR PLANET'S GRAVITY.":GOSUB1
75 M$="YOUR PLANET HAS ENOUGH POWER TO FIRE ONE BOMB AN HOUR.":GOSUB1
82 GOSUB9:CLS
85 M$="AT THE BEGINNING OF EACH HOUR YOU WILL BE ASKED TO GIVE AN ANGLE (BETWEEN 0 "
95 M$=M$+"AND 360) AND A DISTANCE IN UNITS OF 100 MILES (BETWEEN 100 AND 300).":GOSUB1
100 M$="AFTER DOING THAT YOUR BOMB'S DISTANCE FROM THE ENEMY SHIP WILL BE GIVEN.":GOSUB1
110 M$="AN EXPLOSION WITHIN 5,000 MILES OF THE ROMULAN SHIP WILL DESTROY IT.":GOSUB1
111 GOSUB9:CLS0
112 PRINT@0,"HERE IS THE ORBIT AND ITS ANGLES";
170 M$="          7777777777777777":GOSUB3
171 M$="        77777777777777777777":GOSUB3
173 M$="      77777             77777":GOSUB3
174 M$="     77777    6666666    77777":GOSUB3
175 M$="    77777    666666666    77777":GOSUB3
176 M$="   7777     66666666666     7777":GOSUB3
177 M$="  7777     6666666666666     7777":GOSUB3
178 M$=" 7777     666666666666666     7777":GOSUB3
179 M$="77777     666666666666666     77777":GOSUB3
180 M$=" 7777     666666666666666     7777":GOSUB3
181 M$="  7777     6666666666666     7777":GOSUB3
182 M$="   7777     66666666666     7777":GOSUB3
183 M$="    77777    666666666    77777":GOSUB3
184 M$="     77777    6666666    77777":GOSUB3
185 M$="       7777             7777":GOSUB3
186 M$="        7777777777777777777":GOSUB3
187 M$="         77777777777777777":GOSUB3
188 PRINT@1*32+16,;:M$="90":GOSUB7:PRINT@6*32+26,;:M$="0":GOSUB7
189 PRINT@6*32+5,;:M$="180":GOSUB7:PRINT@11*32+15,;:M$="270":GOSUB7
190 PRINT@384,"ﬂ–YOUR PLANET";:PRINT@416,"Ô‡ROMULAN SHIP'S ORBIT";:GOSUB9:CLS

200 M$="ON THE ABOVE DIAGRAM, THE ROMULAN SHIP IS CIRCLING COUNTERCLOCKWISE AROUND YOUR PLANET.":GOSUB1
210 M$="DON'T FORGET THAT WITHOUT SUFFICIENT POWER THE ROMULAN SHIP'S ALTITUDE AND ORBITAL RATE WILL "
218 M$=M$+"REMAIN CONSTANT.":GOSUB1
220 PRINT
230 PRINT"GOOD LUCK."
240 M$="THE FEDERATION IS COUNTING ON YOU.":GOSUB1:GOSUB9:CLS
270 A=INT(360*RND(0))
280 D=INT(200*RND(0)+200)
290 R=INT(20*RND(0)+10)
300 H=0
310 IF H=7 THEN 490
320 H=H+1
325 PRINT
326 PRINT
330 M$="THIS IS HOUR"+STR$(H):GOSUB7:PRINT
332 PRINT "AT WHAT ANGLE DO YOU WISH TO"
335 PRINT "SEND YOUR PHOTON BOMB";
340 INPUT A1
350 PRINT "HOW FAR OUT DO YOU WISH"
355 PRINT "TO DETONATE IT";
360 INPUT D1
365 PRINT
370 A=A+R
380 IF A<360 THEN 400
390 A=A-360
400 T=ABS(A-A1)
410 IF T<180 THEN 430
420 T=360-T
430 C=SQR(D*D+D1*D1-2*D*D1*COS(T*3.14159/180))
440 PRINT "YOUR PHOTON BOMB EXPLODED"
442 PRINT MID$(STR$(C),2);" * 10^2 "
445 PRINT "MILES FROM THE ROMULAN SHIP."
450 IF C<=50 THEN 470
460 GOTO 310
470 PRINT:PRINT "YOU HAVE SUCCESFULLY"
475 PRINT "COMPLETED YOUR MISSION.":PRINT
480 GOTO 500
490 PRINT:M$="YOU HAVE ALLOWED THE ROMULANS TO ESCAPE. YOU LOST!":GOSUB1:PRINT
500 M$="ANOTHER ROMULAN SHIP HAS GONE INTO ORBIT.":GOSUB1
510 PRINT "WANT TO TRY TO DESTROY IT";
520 INPUT C$
530 IF LEFT$(C$,1)="Y" THEN 270
540 PRINT "GOOD BYE."
999 END
