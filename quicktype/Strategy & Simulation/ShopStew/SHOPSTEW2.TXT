100 CLS 
110 REM SHOP STEWARD 
120 PRINT "PROGRAM BY S.N. GOODWIN.":PRINT
130 PRINT "YOU ARE SHOP STEWARD AT A SMALL FIRM PRODUCING CONSUMER DURABLES." 
140 PRINT "YOU AIM TO MAXIMISE SUPPORT, NUMBER OF WORKERS, AND PAY LEVEL."
150 PRINT "WARNING! THE ECONOMY IS ON THE VERGE OF COLLAPSE...":PRINT
160 INPUT A$: REM WAIT 
170 REM INITIALISE 
180 P=100:E=RND(10)+10:B=E:S=60
190 C=100:W=100:F=100:M=100:L=10
200 FOR G=0 TO 1000:NEXT G
210 T=T+1:PRINT"CURRENT SITUATION:";T:PRINT
220 PRINT "PRODUCTION ";P;" PROFIT/LOSS";L
230 PRINT "BANK BALANCE";B:PRINT
240 PRINT "PRICES ";C;" WAGES";W 
250 PRINT "SUPPORT FOR UNION ";S;"%":PRINT "NUMBER OF WORKERS";F:PRINT
260 PRINT "ENTER INDUSTRIAL ACTION--" 
270 PRINT "0=NONE, 1=PRESSURE ON MANAGEMENT,"
280 PRINT "2=WORK-TO-RULE, 3=WlLDCAT STRIKES," 
290 PRlNT "4=ALL OUT, COMRADES, 5=END."
295 Q=A
300 D=A:INPUT A: IF A=5 THEN 1300
305 IF A<0 OR A>5 THEN 260
310 S=S+A+(W-C)*200/W
320 IF L<-20 OR B<-60 OR W>C THEN S=S-A*2
330 IF S>100 THEN S=99 
340 IF S<20 THEN 1000
350 IF A=4 THEN 360
355 G=0:GOTO 380
360 G=40-S/2:IF G<0 THEN G=0
370 PRINT "THERE ARE";G;" BLACKLEGS."
380 R=0:B=B+L/2:P=4*F/5-(3^A)*F/100+E+G*2
390 L=P-F+60-50*W/C+RND(3)-3*D
400 IF B<-150 THEN 1100 
410 IF B<-50 THEN PRINT "THE FIRM IS IN FINANCIAL TROUBLE."
420 IF B<-100 THEN PRINT "THEY ARE CLOSE TO BANKRUPTCY!"
430 O=O+A+(B>-90)*(W-C)+((ABS(M/20)+S/20)*C)/200-5-(A>2)*A:IF O<1 OR A<Q THEN 440 PRINT "MANAGEMENT OFFER A";O;"% RISE."
450 INPUT "TYPE 'A' TO ACCEPT";A$
460 IF LEFT$(A$,1)<>"A" THEN 600
470 W=W+O 
480 O=0
490 IF B>RND(50)+50 AND L>1 THEN 700
500 IF B<-(RND(30)+5) THEN 800 
510 IF L<-(RND(40)+10) THEN 900
520 REM RETURN TO LOOP 
530 C=C+(-(L>1)*(L/6))+RND(3)+(-(F>100)*((F-100)/6))+(-(W>C)*(W-C)/3) 
540 IF W<20 THEN 1200
550 IF S>100 THEN S=99
560 REM
570 REM
580 GOTO 200 
590 REM * MANAGEMENT RESPONSE *
600 IF A>2 AND M>20+RND(80) THEN 620
610 O=O+(50-M)/25:GOTO 520 
620 PRINT"MANAGEMENT LOCKOUT: NO WORK POSSIBLE!":PRINT
630 B=B-L-F/4:S=S-(M/10)*-(M<0)
640 GOTO 520
690 REM * EXPAND WORKFORCE *
700 R=(-B+W-100)/2-1:IF R>-1 THEN 520
705 S=S-R*3/2
710 PRINT "THE MANAGEMEN HIRE";-R;" NEW WORKERS."
720 M=M+M/8:F=F-R:B=B+R*3/2:GOTO 520
790 REM * REDUNDANCIES *
800 G=RND(4)+(L/2*-(L<0))+1
805 IF G<2 THEN 520 
810 PRINT:PRINT "REDUNDENCIES--" PRINT
815 R=G
820 PRINT "TYPE 'A' TO ACCEPT VOLUNTARY REDUNDANCY PLANS";:INPUT A$ 
830 IF LEFT$(A$,1)<>"A" THEN 860
840 PRINT:S=S-G:R=(100-S)/10+G/2:M=M+10-ABS(R-G)
850 PRINT"THERE ARE";R;"VOLUNTEERS.":F=F-R:GOTO 520
860 PRINT:PRINT MID$(STR$(R),2);"WORKERS MUST GO."
865 INPUT "TYPE 'A' TO ACCEPT";A$:IF LEFT$(A$,1)="A" THEN 880
870 M=M/8:O=-O-RND(R+1):S=S+R-2:GOTO 520 
880 F=F-R:M=M+M/10:S=S-R*3/2:GOTO 520
890 REM * NEW TECHNOLOGY *
900 PRINT:R=ABS(B/10)+1 
910 PRINT "NEW TECHNOLOGY PLANS REQUIRE";R;"REDUNDANCIES."
920 INPUT "TYPE 'A' TO ACCEPT:";A$:IF LEFT$(A$,1)="A" THEN 940 
930 GOTO 870
940 E=E+R:GOTO 880
990 REM * LOW SUPPORT KO *
1000 PRINT:PRINT "THE UNION REMOVE YOU FROM OFFICEDUE TO COMPLAINTS FROM THE WORKERS."
1010 G=-100:GOTO 1300
1090 REM * BANKRUPCY KO *
1100 PRINT:PRINT"THE FIRM HAVE BEE FORCED INTOLIQUIDATION." 
1110 G=-80:GOTO 1300
1190 REM * WORKFORCE SHRUNK KO *
1200 PRINT:PRINT"THE SIZE OF THE WORKFORCE MAKESTHE FIRM NO LONGER VIABLE."
1210 G=-60: GOTO 1300
1290 REM * THAT'S ALL FOLKS *
1300 PRINTTAB(20)"GAME OVER."
1310 G=S+F+(C-W)*400/W+B/3+M/6+G-150+T*4
1320 PRINT:PRINT "YOU SCORED";G
1330 FOR G=0 TO 2000:NEXT G
1350 END
