0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:MC=16384:GOTO50
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORCC=1TOLEN(M$):PRINTMID$(M$,CC,1);:IFMID$(M$,CC,1)="."THENPRINTMID$(M$,CC+1,1);:CC=LEN(M$)
4 NEXT:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
10 PRINT@.,"  èèèèèååååååååèèÄ";MID$("ÉåÜááÜéèåÇâåéáá ",(RND(2)-1)*8+1,8)
11 PRINT"  èèèèèîòúîòúîòååÄ":PRINT"  èèèèèîòúîòúîò’⁄Ä":RETURN
50 CLS
110 GOSUB10:M$="         SHOP STEWARD 2         ":GOSUB7
120 PRINT "        BY S.N. GOODWIN.":PRINT
130 M$="YOU ARE SHOP STEWARD AT A SMALL FIRM PRODUCING CONSUMER DURABLES.":GOSUB1
140 M$="YOU AIM TO MAXIMISE SUPPORT, NUMBER OF WORKERS, AND PAY LEVEL.":GOSUB1
150 M$="WARNING! THE ECONOMY IS ON THE VERGE OF COLLAPSE...":GOSUB1:PRINT
160 PRINT"PLEASE PRESS 'ENTER' TO PLAY";
165 M$=INKEY$:CC=RND(16):ON-(CC=1)GOSUB10:IF M$<>CHR$(13) THEN165
170 REM INITIALISE
180 CLS:P=100:E=INT(RND(0)*10)+10:B=E:S=60
190 C=100:W=100:F=100:M=100:L=10
200 FOR G=0 TO 1200:NEXT G
205 CLS
210 T=T+1:PRINT"CURRENT SITUATION";T:PRINT
220 M$="PRODUCTION"+STR$(INT(P))+" PROFIT/LOSS "+STR$(INT(L))+" BANK BALANCE "+STR$(INT(B)):GOSUB1:PRINT
240 PRINT "PRICES";:M$=STR$(C):GOSUB3:PRINT" WAGES";:M$=STR$(W):GOSUB3:PRINT
250 PRINT "SUPPORT FOR UNION";:M$=STR$(S):GOSUB3:PRINT"%"
255 PRINT "NUMBER OF WORKERS";:M$=STR$(INT(F+.5)):GOSUB3:PRINT:PRINT
260 M$="    ENTER INDUSTRIAL ACTION-    ":GOSUB7
270 PRINT "0=NONE, 1=PRESSURE ON MANAGEMENT";
280 PRINT "2=WORK-TO-RULE, 3=WILDCAT STRIKE";
290 PRINT "4=ALL OUT COMRADES!, 5=END";
295 Q=A
300 D=A:INPUT A:IF A=5 THEN 1300
305 IF A<0 OR A>5 THEN 260
310 S=S+A+(W-C)*200/W
320 IF L<-20 OR B<-60 OR W>C THEN S=S-A*2
330 IF S>100 THEN S=99
340 IF S<20 THEN 1000
350 IF A=4 THEN 360
355 G=0:GOTO 380
360 G=40-S/2:IF G<0 THEN G=0
370 M$="THERE ARE"+STR$(INT(G))+" BLACKLEGS.":GOSUB1
380 R=0:B=B+L/2:P=4*F/5-(3^A)*F/100+E+G*2
390 L=P-F+60-50*W/C+INT(RND(0)*3)-3*D
400 IF B<-150 THEN 1100
410 IF B<-50 THEN M$="THE FIRM IS IN FINANCIAL TROUBLE.":GOSUB1
420 IF B<-100 THEN M$="THEY ARE CLOSE TO BANKRUPTCY!":GOSUB1
430 O=O+A+(B>-90)*(W-C)+((ABS(M/20)+S/20)*C)/200-5-(A>2)*A:IF O<1 OR A<Q THEN 480
440 PRINT"MANAGEMENT OFFER A";:M$=STR$(O):GOSUB3:PRINT"% RISE."
450 INPUT "ENTER 'A' TO ACCEPT";A$
460 IF LEFT$(A$,1)<>"A" THEN 600
470 W=W+O 
480 O=0
490 IF B>INT(RND(0)*50)+50 AND L>1 THEN 700
500 IF B<-(INT(RND(0)*30)+5) THEN 800
510 IF L<-(INT(RND(0)*40)+10) THEN 900
520 REM RETURN TO LOOP 
530 C=C+(-(L>1)*(L/6))+INT(RND(0)*3)+(-(F>100)*((F-100)/6))+(-(W>C)*(W-C)/3)
540 IF W<20 THEN 1200
550 IF S>100 THEN S=99
560 REM
570 REM
580 GOTO 200
590 REM * MANAGEMENT RESPONSE *
600 IF A>2 AND M>20+INT(RND(0)*80) THEN 620
610 O=O+(50-M)/25:GOTO 520
620 M$="MANAGEMENT LOCKOUT: NO WORK POSSIBLE!":GOSUB1:PRINT
630 B=B-L-F/4:S=S-(M/10)*-(M<0)
640 GOTO 520
690 REM * EXPAND WORKFORCE *
700 R=(-B+W-100)/2-1:IF R>-1 THEN 520
705 S=S-R*3/2
710 M$="THE MANAGEMENT HIRE"+STR$(INT(-R))+" NEW WORKERS.":GOSUB1
720 M=M+M/8:F=F-R:B=B+R*3/2:GOTO 520
790 REM * REDUNDANCIES *
800 G=INT(RND(0)*4)+(L/2*-(L<0))+1
805 IF G<2 THEN 520 
810 PRINT:PRINT "REDUNDENCIES--" PRINT
815 R=G
820 M$="ENTER 'A' TO ACCEPT VOLUNTARY REDUNDANCY PLANS":GOSUB1:INPUT A$
830 IF LEFT$(A$,1)<>"A" THEN 860
840 PRINT:S=S-G:R=(100-S)/10+G/2:M=M+10-ABS(R-G)
850 M$="THERE ARE"+STR$(R)+" VOLUNTEERS.":GOSUB1:F=F-R:GOTO 520
860 PRINT:PRINT MID$(STR$(INT(R)),2);" WORKERS MUST GO."
865 INPUT "ENTER 'A' TO ACCEPT";A$:IF LEFT$(A$,1)="A" THEN 880
870 M=M-M/8:O=-O-INT(RND(0)*(R+1)):S=S+R-2:GOTO 520
880 F=F-R:M=M+M/10:S=S-R*3/2:GOTO 520
890 REM * NEW TECHNOLOGY *
900 PRINT:R=ABS(B/10)+1
910 M$="NEW TECHNOLOGY PLANS REQUIRE"+STR$(INT(R))+" REDUNDANCIES.":GOSUB1
920 INPUT "ENTER 'A' TO ACCEPT:";A$:IF LEFT$(A$,1)="A" THEN 940
930 GOTO 870
940 E=E+R:GOTO 880
990 REM * LOW SUPPORT KO *
1000 PRINT:M$="THE UNION REMOVES YOU FROM OFFICE DUE TO COMPLAINTS FROM THE WORKERS.":GOSUB1
1010 G=-100:GOTO 1300
1090 REM * BANKRUPTCY KO *
1100 PRINT:M$="THE FIRM HAS BEEN FORCED INTO LIQUIDATION.":GOSUB1
1110 G=-80:GOTO 1300
1190 REM * WORKFORCE SHRUNK KO *
1200 PRINT:M$="THE SIZE OF THE WORKFORCE MAKES THE FIRM NO LONGER VIABLE.":GOSUB1
1210 G=-60:GOTO 1300
1290 REM * THAT'S ALL FOLKS *
1300 PRINTTAB(11)"GAME OVER."
1305 IF G>-50 THEN G=0
1310 G=S+F+(C-W)*400/W+B/3+M/6+G-150+T*4
1320 PRINT:PRINT "YOU SCORED";:M$=STR$(G):GOSUB3:PRINT" IN"T*2"MONTHS"
1330 PRINT:PRINT"PLAY AGAIN (Y/N)?";
1340 M$=INKEY$:IF M$="" THEN 1340
1350 IF M$="Y" THEN RUN
1360 IF M$="N" THEN END
1370 GOTO 1340 
