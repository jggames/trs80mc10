5 CLEAR:DIMWA(31):DIMD(31):X$="X=":Y$="Y=":DIMWT(24):DIMS(24):FORA=1TO3:X(A)=22:Y(A)=22:B(A)=8640:NEXT
10 CLS:PRINT"ATLANTIC BALLOON CROSSING...
THIS IS A SIMULATION OF THE
FLIGHT OF THE DOUBLE EAGLE II
ACROSS THE ATLANTIC, SOME
3105 MILES TO A SAFE LANDING
NEAR PARIS FRANCE IN A TIME
OF 137 HOURS (ABOUT 6 DAYS !)"
20 PRINT:PRINT"THIS WAS AN OPEN BASKET, LOW 
ALTITUDE FLIGHT STARTING AT
PRESQUE ISLE MAINE..AUG.11,1978.
ALTITUDE MAX WAS 24,950 FT.
& ABOUT 1/3 WAS ABOVE 15,000 FT.
USING OXYGEN MASKS TO BREATHE"
80 Z=100:FORT=0TO5:S(T)=Z:Z=Z-20:NEXT:Z=0:FORT=10TO21:S(T)=Z:Z=Z+10::NEXT:FORT=22TO24:Z=Z-10:S(T)=Z:NEXT:GOSUB8000:GOTO111
105 X(U)=X:Y(U)=Y:AL(U)=AL:B(U)=B:IFH1=1THENH(U)=0
106 PRINT@832,"12 HR TURN IS OVER...YOU'VE GONE";TD(U);"MILES":FORI=1TO3000:NEXT:GOTO115
111 INPUT"DO YOU WANT INSTRUCTIONS";A$:IFLEFT$(A$,1)="Y"GOTO10000
112 CLS:INPUT"HOW MANY PLAYING (1-3)";UP:IFUP>3GOTO112ELSEFORA=1TOUP:AL(A)=100:PRINT"VOLUME (140000-160000) CUBIC FEET) FOR BALLON #";A;:INPUTZ:V(A)=Z:IFZ>160000PRINT"TOO MUCH: VOLUME=150000":V(A)=150000
113 IFZ<140000PRINT"TOO LITTLE: VOLUME=140000":V(A)=140000
114 A$(1)="EAGLE II":A$(2)="ZANUSSI":A$(3)="SILVER FOX":PRINT"YOUR NAME---";A$(A):NEXTA:FORI=1TO2000:NEXT
115 U=U+1:IFU>UPTHENU=1:D5=D5+1:DA=INT(D5/2):H1=H1+1:IFH1=2THENH1=0:HR=0ELSEGOSUB9000:FORI=1TOUP:B2=INT(Y(I)/3)*64+INT(X(I)/2):PRINT@B2,I;:PRINT@704,"THE NUMBER ";I;"MARKS THE CURRENT POSITION FOR BALLOON #  ";I:PRINT:PRINT:PRINT:FORI1=1TO1000:NEXTI1:NEXTI
116 IF(AL(1)=<0)AND(AL(2)=<0)AND(AL(3)=<0)GOTO50000
117 HR=H(U):IFAL(U)=<0CLS:PRINTA$(U);" IS DOWN! NEXT!":FORI=1TO2000:NEXTI:GOTO115
120 CLS:PRINT:PRINT:PRINTA$(U),"WEATHER REPORT COMING........."
300 'WIND
310 W=RND(5)+2
320 FORT=0TO5:WT(T)=RND(5)+3:NEXT:FORT=6TO11:WT(T)=RND(5):NEXT:FORT=12TO17:WT(T)=RND(5)+3:NEXT:FORT=18TO23:WT(T)=RND(7)+4
330 D1=RND(9)-6:FORA=0TO4:D(A)=D1:NEXT:D1=RND(5):FORA=5TO9:D(A)=D1:NEXT:D1=RND(5)+8:FORA=10TO14:D(A)=D1:NEXT:D1=RND(5)+13:FORA=15TO19:D(A)=D1:NEXT:D1=RND(9)+19:FORA=20TO30:D(A)=D1:NEXT
340 W1=RND(15)-8:FORA=0TO4:WA(A)=W1:NEXT:W1=RND(5)+5:FORA=5TO9:WA(A)=W1:NEXT:W1=RND(6)+8:FORA=10TO14:WA(A)=W1:NEXT:W1=RND(7)+9:FORA=15TO19:WA(A)=W1:NEXT:W1=RND(5)+13:FORA=20TO30:WA(A)=W1:NEXT
500 X=X(U):Y=Y(U):V=V(U):B=B(U):AL=AL(U)
510 'TURN
511 CL=1-RND(0)*.3:H=H+.5:IFH>12THENH=0:H(U)=12:GOTO105
516 V(U)=V(U)-RP(U)
519 'TEMP
520 CT=280+RND(9)+CL*S(HR)-(3*AL/1000)
524 'PRESSURE
525 P=30*(1-.000015*AL)
529 'VOL,LIFT,&RISE@TEMP&PRES
530 CV=V(U)/12*CT/P:IFCV>160000THENCV=160000:IFRND(0)>.9PRINT@832,"* A LEAK *";:RP(U)=RP(U)+RND(300):FORI=1TO2000:NEXT
535 L=.07*CV*(1-.015*AL/1000)
536 AR=(L-B-IC)/(B+IC)*250
539 'WIND:DIR,SPD(ALT,TIME,POSI)
540 CW=W+20-Y+WT(HR)+WA(INT(AL/1000)):IFF=9THENF=0:RETURN
541 D=D(INT(AL/1000))+53-X+18-Y:IFF=9THENF=0:RETURN
545 IFINT(H)=0GOSUB9000:GOSUB9995
560 J=COS(D*.0174533)*CW:X=X+J/70:MF=CW/20:TD(U)=TD(U)+CW/2
569 'ICE
570 IF(Y<15)AND(CT<273)THENIC=IC+273-CT
571 IF(IC>0)AND(CT>273)THENIC=IC-CT+273:IFIC<0THENIC=0
656 K=SIN(D*.014533)*CW:Y=Y-K/120
1960 IFAL<=0GOTO20000
7000 'SMALL BALLOON
7001 CLS:IFRND(0)>.7GOSUB30000
7002 IFCL<.9PRINT@128,"-  -  --  - -    -    --   - -   --  -  -   - -  -   --   --    -    - -    ---   - -   - -   --    -    -   - -     --  -    -    --      ---     ---   - -     --    --  -
7003 IFCL<.8PRINT@128,"== == -  -  =  -  --     - -    -   --     - -   -- - -    -- - =  =  -   = = === -- - === -  -  = -    =   =    =  == = = ===  == = == - == - == = = = == - --== - =   --  ---- ----   -
7004 PRINT@880,A$(U)
7006 S1=15363+(10-INT(AL/2500))*64:IFS1<15360THENS1=15363ELSEIFS1>16028GOTO20000
7008 FORZ=1TO10:XC=XC+MF:M=M+3:S=S1+INT(XC*1.3):AL=AL+AR*3:IFAL<=0THENZ=10
7010 PRINT@0,"B=BALLAST G=GAS R=RADIO ALT=";INT(AL);"WIND=";INT(CW);"MPH  RISE=";INT(AR);
7020 PRINT@896,"TEMP: VOLUME: PRESSURE: DIRECTION: WEIGHT: DAY-HOUR-MINUTE";
7025 PRINT@960,INT(CT);:PRINTTAB(5)INT(CV);:PRINTTAB(13)INT(P)"INCH";:PRINTTAB(23)INT(D);"DEGREES  ";INT(B+IC);:PRINTTAB(45)DA;"-";HR;":";M;
7050 PRINT@384,"--------------------------------------------------------------";
7090 S0=S-15362:PRINT@S0,"        ";
7100 POKES+1,160:POKES+2,188:POKES+3,191:POKES+4,191:POKES+5,191:POKES+6,189:POKES+7,180:S=S+64:PRINT@S-15363,"          ";:POKES,136:FORI=S+1TOS+7:POKEI,191:NEXT:POKES+8,157:POKES+9,148:S=S+64:PRINT@S-15362,"         ";
7150 POKES+1,130:POKES+2,164:POKES+6,160:POKES+7,166:POKES+8,134:S=S+64:PRINT@S-15360,"        ";:POKES+3,137:POKES+4,172:POKES+5,140:POKES+6,153:POKES+7,129:S=S+64:PRINT@S-15360,"        ";:POKES+3,168:POKES+4,188:POKES+5,156
7940 A$=INKEY$:IFA$="B"THENB=B-100ELSEIFA$="G"THENV(U)=V(U)-1000ELSEIFA$="R"THENIFRND(0)>.8GOSUB9000:GOSUB9995:CLSELSEPRINT@750,"NO RADIO CONTACT";
7947 IFB<2400PRINT@800,"BALLAST GONE !!";:B=2400
7950 NEXTZ
7960 IFAL<0THENAL=0:GOTO20000
7990 G9=G9+1:IFG9>1THENG9=0:XC=0:M=0:HR=HR+1
7991 IFAL>28500THENV(U)=V(U)-10000:PRINT@800,"TOO HIGH-VENT OPEN";:IF(AL>30000)AND(CV=160000)PRINT@800,"/R/I/P/ IN BALLOON";:RP(U)=1000
7999 GOTO511
8000 'BALLOON
8050 B=15391
8100 FORJ=1TO16:FORI=BTOB+29:POKEI,191:NEXTI:B=B+64:NEXTJ:B=15392
8200 FORI=B+10TOB+16:POKEI,188:NEXT:POKEB+9,179:POKEB+17,179:POKEB+18,155:POKEB+8,167:POKEB+7,143:POKEB+19,143:POKEB+6,159:POKEB+20,175:B=B+64
8250 POKEB+3,143:POKEB+4,179:POKEB+5,188:POKEB+6,190:POKEB+20,189:POKEB+21,188:POKEB+22,179:POKEB+23,143:B=B+64
8300 POKEB+1,159:POKEB+2,185:POKEB+24,182:POKEB+25,175:B=B+64
8350 POKEB,167:POKEB+1,190:POKEB+25,189:POKEB+26,155:POKEB+27,143:B=B+64
8400 POKEB,170:POKEB+26,149:POKEB+27,128:B=B+64
8450 POKEB,189:POKEB+1,155:POKEB+25,167:POKEB+26,150:POKEB+27,168:B=B+64
8500 POKEB+2,182:POKEB+3,175:POKEB+23,159:POKEB+24,185:POKEB+25,159:POKEB+26,160:B=B+64
8550 POKEB+3,189:POKEB+4,144:FORI=B+5TOB+21:POKEI,128:NEXT:POKEB+22,160:POKEB+23,190:POKEB+24,135:POKEB+25,184:B=B+64
8600 POKEB+5,180:FORI=B+6TOB+20:POKEI,128:NEXT:POKEB+21,184:POKEB+22,159:POKEB+23,161:POKEB+24,190:B=B+64
8650 POKEB+6,189:POKEB+7,144:FORI=B+8TOB+18:POKEI,128:NEXT:POKEB+19,160:POKEB+20,190:POKEB+21,135:POKEB+22,184:B=B+64
8700 POKEB+8,180:FORI=B+9TOB+17:POKEI,128:NEXT:POKEB+18,184:POKEB+19,159:POKEB+20,161:POKEB+21,190:B=B+64
8750 POKEB+9,189:POKEB+10,144:FORI=B+11TOB+15:POKEI,128:NEXT:POKEB+16,160:POKEB+17,190:POKEB+18,135:POKEB+19,184:B=B+64
8800 POKEB+11,180:POKEB+12,128:POKEB+13,128:POKEB+14,128:POKEB+15,184:POKEB+16,159:POKEB+17,161:POKEB+18,190:B=B+64
8850 POKEB+12,157:POKEB+13,128:POKEB+14,174:POKEB+15,135:POKEB+16,184:B=B+64
8900 POKEB+10,135:POKEB+11,167:POKEB+12,158:POKEB+13,139:POKEB+14,189:POKEB+15,155:POKEB+16,139:B=B+64
8950 FORI=B+10TOB+13:POKEI,48:NEXT:POKEB+17,184:POKEB+14,128:POKEB+15,168:POKEB+16,136:RETURN
9000 'MAP
9050 A=15360:CLS:FORI=ATOA+9:POKEI,191:NEXT:POKEA+10,145:POKEA+12,176:POKEA+14,129:POKEA+15,188:POKEA+16,191:POKEA+17,151:POKEA+18,129:POKEA+24,140:POKEA+25,178:FORI=A+26TOA+37:POKEI,191:NEXT:POKEA+38,176:POKEA+39,179:POKEA+58,160:POKEA+59,184
9055 POKEA+60,188:FORI=A+61TOA+63:POKEI,191:NEXT:A=A+64
9100 FORI=ATOA+4:POKEI,191:NEXT:FORI=A+24TOA+32:POKEI,191:NEXT:FORI=A+58TOA+63:POKEI,191:NEXT
9105 POKEA+5,159:POKEA+6,163:POKEA+7,176:POKEA+8,144:POKEA+10,160:POKEA+11,176:POKEA+12,176:POKEA+13,184:POKEA+14,180:POKEA+15,190:POKEA+16,143:POKEA+17,171:POKEA+18,157:POKEA+19,132:POKEA+23,168:POKEA+33,159
9110 POKEA+34,139:POKEA+35,129:POKEA+36,130:POKEA+37,129:POKEA+56,160:POKEA+57,190:A=A+64
9200 POKEA,191:FORI=A+25TOA+28:POKEI,191:NEXT:FORI=A+56TOA+60:POKEI,191:NEXT:POKEA+63,191:POKEA+1,143:POKEA+2,131:POKEA+6,131:POKEA+13,130:POKEA+14,175:POKEA+15,155:POKEA+16,135:POKEA+24,175:POKEA+29,159
9205 POKEA+30,131:POKEA+31,129:POKEA+38,138:POKEA+41,189:POKEA+42,191:POKEA+43,132:POKEA+55,176:POKEA+61,143:POKEA+62,179:POKEA+39,191:POKEA+40,191:A=A+64
9300 FORI=A+10TOA+12:POKEI,191:NEXT:POKEA+27,191:FORI=A+55TOA+58:POKEI,191:NEXT:POKEA+62,191:POKEA+63,191:POKEA,149:POKEA+9,190:POKEA+13,156:POKEA+15,160:POKEA+16,144:POKEA+24,138:POKEA+25,175:POKEA+26,159
9305 POKEA+28,135:POKEA+46,136:POKEA+53,184:POKEA+54,190:POKEA+59,151:A=A+64
9350 FORI=A+9TOA+12:POKEI,191:NEXTI:POKEA+58,191:POKEA,189:POKEA+1,188:POKEA+2,176:POKEA+13,188:POKEA+14,188:POKEA+15,190:POKEA+16,189:POKEA+47,168:POKEA+48,148:POKEA+49,144:POKEA+53,138:POKEA+54,143:POKEA+55,135:POKEA+56,139:POKEA+57,187:POKEA+59,135
9355 POKEA+62,144:POKEA+63,140:A=A+64
9400 FORI=ATOA+4:POKEI,191:NEXT:FORI=A+8TOA+15:POKEI,191:NEXT:POKEA+55,191:POKEA+62,191:POKEA+63,191:POKEA+5,181:POKEA+7,190:POKEA+16,189:POKEA+17,188:POKEA+18,188:POKEA+45,184:POKEA+46,180:POKEA+48,143:POKEA+49,181:POKEA+54,178:POKEA+59,176
9405 POKEA+60,176:POKEA+61,186:A=A+64
9450 FORI=ATOA+16:POKEI,191:NEXT:FORI=A+48TOA+63:POKEI,191:NEXT:POKEA+5,188:POKEA+6,190:POKEA+13,143:POKEA+14,143:POKEA+17,143:POKEA+18,175:POKEA+19,129:POKEA+44,136:POKEA+45,143:POKEA+46,131:POKEA+47,136:POKEA+50,141:POKEA+52,184
9455 POKEA+53,190:POKEA+51,128:A=A+64
9500 FORI=A+5TOA+12:POKEI,191:NEXT:FORI=A+51TOA+63:POKEI,191:NEXT:POKEA,188:POKEA+1,176:POKEA+2,155:POKEA+3,143:POKEA+4,175:POKEA+13,179:POKEA+17,140:POKEA+18,141:POKEA+19,140:POKEA+20,144:POKEA+47,136:POKEA+48,172:POKEA+49,188
9505 POKEA+50,190:A=A+64
9550 FORI=ATOA+9:POKEI,191:NEXT:FORI=A+50TOA+63:POKEI,191:NEXT:POKEA+3,152:POKEA+4,143:POKEA+5,179:POKEA+6,179:POKEA+10,135:POKEA+11,131:POKEA+12,131:POKEA+14,131:POKEA+13,140:POKEA+15,129:FORI=A+45TOA+48:POKEI,176:NEXT:POKEA+49,186
9555 POKEA+52,159:POKEA+53,175:POKEA+55,143:POKEA+56,175:POKEA+57,182:POKEA+58,147:POKEA+59,143:A=A+64
9600 FORI=ATOA+5:POKEI,191:NEXT:FORI=A+45TOA+49:POKEI,191:NEXT:POKEA+63,191:POKEA+6,159:POKEA+7,131:POKEA+8,131:POKEA+33,160:POKEA+44,136:POKEA+50,159:POKEA+51,131:POKEA+52,129:POKEA+55,188:POKEA+57,130:POKEA+58,139:POKEA+59,173
9605 POKEA+60,172:POKEA+61,130:POKEA+62,175:A=A+64
9650 FORI=ATOA+5:POKEI,191:NEXT:POKEA+5,143:POKEA+35,129:POKEA+46,130:POKEA+47,179:POKEA+48,131:POKEA+49,177:FORI=A+51TOA+56:POKEI,188:NEXT:POKEA+59,130:POKEA+63,131
9965 PRINT@704,A$(U);" BALLOON #";U;"CURRENT POSITION ";X$;X;Y$;Y
9975 PRINT"ALTITUDE=     GROUND     5000     10000     15000      20000+"
9981 I1=CW:I2=D:I=AL:PRINT"AVG WIND= ";:FORAL=0TO20000STEP5000:F=9:GOSUB540:PRINT"      ";INT(CW);:NEXT
9985 PRINT@896,"DIRECTION=";:FORAL=0TO20000STEP5000:F=9:GOSUB541:PRINT"      ";INT(D);:NEXT:AL=I:CW=I1:D=12
9990 PRINT@964,"HEADING SHOULD BE ";INT((64-X)/1.4-22+Y);"   TOTAL MILES=";TD(U);:RETURN
9995 POKE15860,80:POKE15819,80:B1=INT(Y/3)*64+INT(X/2):PRINT@B1,"?";:FORI=1TO4000:NEXT:RETURN
10000 CLS:PRINT@0,"BETWEEN 1873 AND 1978, SOME 14 BALLOONS HAVE MANAGED TO GET
AIRBORNE IN AN ATTEMPT TO CROSS THE ATLANTIC. MOST HAD TO DITCH
AT SEA, 1 BURST IN MIDAIR, 2 VANISHED, OTHERS WERE FORCED DOWN
BY UNFAVORABLE CONDITIONS--";
10010 PRINT"WINDS SHIFTING NORTH, SOUTH OR EVEN
REVERSING ALTOGETHER; OR LOSS OF GAS BY TEARS OR VALVING. IN ALLSOME 5 AERONAUTS DIED. THE HAZARDS ARE MANY.
FROM THESE ATTEMPTS, A SUCCESSFUL STRATEGY DEVELOPED.";
10020 PRINT" BY GOING
ACROSS WITH A HIGH PRESSURE WEATHER SYSTEM, YOU CAN FOLLOW ITS  WINDS IN A CLOCKWISE ROTATION FIRST NORTHEAST THEN SOUTHEAST TO LAND IN EUROPE.  THE WINDS VARY WITH ALTITUDE AND POSITION SO   THAT YOU MUST STAY IN THE";
10030 PRINT" GROOVE OF CORRECT ALTITUDE. TO GO TOO HIGH OR TOO LOW FOR VERY LONG WILL CARRY YOU OFF COURSE--NORTH
IF YOU ARE TOO HIGH AND SOUTHWARD IF YOU ARE TOO LOW. THE MORE  OFF COURSE YOU GET, THE MORE DIFFICULT IT IS TO RECOVER."
10035 A$=INKEY$:IFA$=""GOTO10035
10036 CLS:PRINT"YOUR TRIP STARTS IN THE EVENING AT 6PM WHEN THE COOLING PERIOD
BEGINS. BY DOING THIS THE BALLOON DOES NOT RISE TOO FAR NOR DO  YOU HAVE TO DROP MUCH BALLAST TILL THE COOLING PERIOD THE NEXT  DAY. COVERING 500 MILES";
10037 PRINT"/DAY IS A GOOD RATE AND WILL GET YOU OVERIN GOOD TIME. IT'S POSSIBLE TO GO ACROSS FASTER-SET A RECORD!":INPUTA$
10040 CLS:PRINT"ASCEND OR DESCEND TO MAINTAIN A FAVORABLE COURSE BY RELEASING
BALLAST TO ASCEND OR VALVING GAS TO DESCEND. YOU MAY RELEASE
BALLAST BY PRESSING THE  B  KEY. THIS DROPS 100 LBS AND IS"
10050 PRINT"RECORDED ON THE INSTRUMENTS YOU HAVE ON BOARD. TO RELEASE GAS
YOU PRESS  G  AND 1000 CUBIC FEET IS RELEASED. YOU START WITH A
140000-150000 CUBIC FT VOLUME IN A 160000 CUBIC FT BALLOON AS 
IS DETERMINED BY YOUR STRATEGY--";
10052 PRINT"MORE VOLUME MEANS FASTER RISE &
TO HIGHER LEVEL. BEWARE OF MAX VOLUME AS YOU MAY CAUSE LEAKS!"
10060 PRINT"IF YOU CLIMB ABOVE 28,000 FT YOUR BALLOON AUTOMATICALLY VALVES
OFF 10000 CUBIC FEET TO PREVENT UNDUE PRESSURE AND BURSTING OF  THE BALLOON. YOUR BASE WEIGHT IS 2400 LBS AND YOU CAN NOT GO
BELOW THIS WITHOUT CUTTING OFF YOUR";
10070 PRINT" GONDOLA AND RIDING ON THE 
WEIGHT RING * VERY PRECARIOUS * !! YOU MAY ALSO RADIO FOR YOUR
EXACT POSITION AND THE WEATHER-WIND AND WIND DIRECTION FOR THE
VARIOUS ALTITUDES BY PRESSING  R  BUT, YOU WILL NOT ALWAYS GE"
10080 PRINT"A REPORT AS THE RADIO MAY NOT WORK WHEN NEEDED";:INPUTA$:CLS
10090 PRINT"THE SUCCESSFUL STRATEGY WAS TO RISE ABOVE 5000 FT QUICKLY TO GETGOOD WIND, BUT NOT TOO HIGH OR THE DIRECTION WOULD BE TOO FAR
NORTH. TRY TO MAINTAIN A FAIRLY LEVEL COURSE. IF YOU GET TOO
FAR NORTH YOU WILL GET ICE FORMING WHEN";
10091 PRINT" THE TEMPERATURE IS LOW,
BELOW 273 ON THE KELVIN SCALE...THIS IS THE TEMP GIVEN ON YOUR
GAUGE. YOU WILL ALSO FIND MORE WIND WITH INCREASED HEIGHT":INPUTA$:CLS
10092 PRINT"THE LIFT OF THE BALLOON IS DUE TO THE DISPLACEMENT OF AIR. THIS
AIR IS THINNER AT HIGHER ALTITUDE SO THE LIFT DECREASES, BUT THEPRESSURE BEING LESS CAUSES THE GAS WITHIN THE BALLOON TO EXPAND
TO A GREATER VOLUME. THE BALLOON EXPANDS WITH INCREASE";
10093 PRINT" IN THE 
TEMPERATURE AND SHRINKS WITH A DECREASE. TEMPERATURE DECREASES
WITH ALTITUDE AT 7 DEGREES/1000. THE COMPUTER DOES THE COMPLEX
CALCULATIONS FOR YOU. YOU WILL GAIN UNDERSTANDING AS YOU PLAY
SO THAT THE PROPER COMBINATION OF EXPERIENCE,";
10094 PRINT" WEATHER, AND
A BIT OF LUCK WILL LEAD TO A SUCCESSFUL CROSSING. FOR THE GAME
A WINNER IS THE ONE LANDING NEAREST PARIS AT X=104, Y=22 SAFELY.STARTING COORDINATES ARE X=22, Y=22 AND THE STANDARD TRS-80
SYSTEM IS Y=0 AT THE TOP";
10095 PRINT" INCREASING TO 47 AT THE BOTTOM OF THE
DISPLAY WITH X INCREASING AS YOU MOVE EAST (RIGHT). TO BE SAFE
YOUR IMPACT MUST BE LESS THAN 100 FEET/MIN LOSS OF ALTITUDE
OR YOU MAY BE HURT ON IMPACT !";:INPUTA$:CLS
10096 PRINT"YOUR INSTRUMENTS INCLUDE YOUR CURRENT VOLUME INCLUDING EFFECT OFPRESSURE AND TEMPERATURE, THE BAROMETRIC PRESSURE AT PRESENT
ALTITUDE IN INCHES OF MURCURY, THE WIND SPEED IN MPH, THE 
DIRECTION OF THE WIND IN DEGREES WITH POSITIVE ANGLE IF"
10097 PRINT"MOVING NORTHEAST AND MINUS IF SOUTHEAST, YOUR CURRENT WEIGHT
IN LBS INCLUDING BALLAST AND BASE WEIGHT (FABRIC, PEOPLE AND
CATAMARAN STYLE GONDOLA), ELAPSED TIME IN DAYS-HOURS-MINUTES.
THERE WERE THREE PEOPLE ON BOARD THE DOUBLE";
10098 PRINT" EAGLE II .....
                      GOOD LUCK !"
10099 INPUT=A$:GOTO112
20000 'TOUCHDOWN
20001 AR=-AR:CLS:IFAR<30PRINT"PERFECT LANDING AT ";X,Y:GOTO20090
20010 IFAR<60PRINT"LANDING A BIT ROUGH":GOTO20090
20020 IFAR<100PRINT"OOOOOFFFF!!! YOU'RE DOWN BUT A BIT SHAKEN!!":GOTO20090
20030 IFAR>100PRINT"* * * C R A S H * * * YOU K L U T Z  #$%&%"
20040 IFRND(0)>.8PRINT"YOU BROKE YOUR LEG !!":GOTO20090
20050 IFRND(0)>.7PRINT"YOU SPRAINED YOUR ANKLE !!":GOTO20090
20060 PRINT"YOU HAVE THE WIND KNOCKED OUT OF YOU AND ARE BEING DRAGGED BY
THE BALLOON AS YOU HANG OVER THE SIDE !"
20090 PRINT"AND THIS IS WHERE YOU ENDED UP              ":FORI=1TO2000:NEXT:GOSUB9000
21000 'WHERE, SAVE FINAL STATS
22000 IFPOINT(X,Y)=-1PRINT@768,"YOU'RE ON LAND!":GOTO22300
22100 PRINT@768," S P L A S H, YOU'RE IN THE DRINK!!":FORI=1TO2000:NEXT
22250 IFRND(0)>.8PRINTA$(U);" LOST AT SEA":GOTO22300
22260 IFPOINT(X,Y)=0PRINT"A PASSING BOAT PICKED YOU UP"
22300 FORI=1TO1500:NEXT:GOSUB9995:FORI=1TO2000:NEXT:TT(U)=DA*24+HR+M/60:X(U)=X:Y(U)=Y:AL(U)=0:G9=0:XC=0:M=0:H=0:GOTO115
30000 PRINT@512,"@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@
@@@@";
31000 RETURN
50000 'END
50100 CLS:PRINT"FINAL POSITIONS
";:FORI=1TOUP:PRINT"BALLOON #";I;"TIME=";TT(I):PRINT"TOTAL DISTANCE=";TD(I):PRINT"END POSITION":PRINTX$;X(I);Y$;Y(I):D=SQR(ABS(X(I)-104)[2+ABS(Y(I)-22)[2)*30:PRINT"DISTANCE TO PARIS=";D:NEXTI:GOSUB8000:INPUTA$:END
