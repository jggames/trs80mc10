0 CLEAR500:DIMC1,C2,M$:M=16384:GOTO35
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:RETURN
8 PRINT@480,;:M$="HIT ANY KEY":GOSUB7:M$=INKEY$:ON-(M$="")GOTO8:CLS:RETURN
9 FORC1=1TO12:SOUNDC2,1:NEXT:RETURN
20 D=D+1:IFC1=32THENRETURN
30 G=G+10000:PRINT@34,;:M$=RIGHT$("$$$$$$$$"+MID$(STR$(G),2),8):PRINTM$;:RETURN
35 CLS:PRINT@8,"*** GOLDMINE ***":PRINT:PRINT"COPYRIGHT 1976 BY ROYCE A. JONES"
40 C1=RND(-(PEEK(9)*256+PEEK(10))):DIMX(10,61)
41 M$="I'VE BEEN A MINER FOR A HEART OF GOLD AND I'M GETTING OLD-- NEIL YOUNG":GOSUB1
42 PRINT
43 INPUT"INSTRUCTIONS";K$:IFLEFT$(K$,1)="N"THEN400 
50 M$="THROUGHOUT HUMANKIND'S HISTORY GOLD HAS BEEN THE MAGIC METAL.":GOSUB1
51 M$="GOLD HAS BEEN PRIZED BY NEARLY EVERY CIVILIZATION. NOW IS " 
52 M$=M$+"YOUR CHANCE TO HAVE YOUR OWN GOLD MINE.":GOSUB1:GOSUB8
53 M$="YOU HAVE, A YEAR TO MINE BEFORE THE BANK RECALLS YOUR LOAN AND REPOSSES THE MINE.":GOSUB1
54 M$="THE MOST YOUR MINE IS WORTH IS ONE MILLION DOLLARS! BUT YOU MUST MINE ALL OF IT.":GOSUB1
56 M$="YOUR MINE CONTAINS 100 VEINS OF GOLD. EACH VEIN IS WORTH $10000.":GOSUB1
57 M$="TO START WITH, YOUR MINE HAS JUST ONE VERTICAL SHAFT.":GOSUB1
58 M$="YOU MUST ENLARGE ON THIS BY MINING.":GOSUB1:GOSUB8
60 M$="IT TAKES ONE DAY TO MINE ONE POSITION WHETHER IT CONTAINS ORE OR NOT.":GOSUB1
60 M$="IF THE POSITIONS HAVE ALREADY BEEN MINED YOU CAN MOVE 4 POSITIONS IN ONE DAY.":GOSUB1
61 M$="YOU CAN MOVE IN FOUR DIRECTIONS? w-UP, z-DOWN, a-LEFT, AND s-RIGHT.":GOSUB1
62 M$="YOU CAN MOVE AS MANY POSITIONS AS YOU WANT IN ONE DIRECTION "
63 M$=M$+"EXCEPT THAT YOU CANNOT GO PAST THE BORDER OF THE MINE.":GOSUB1:GOSUB8
64 M$="IF YOU TRY TO, YOU WILL BE STOPPED AT THE BORDER AND TIME WILL BE WASTED.":GOSUB1
65 M$="YOU ARE THE MINER AND ARE REPRESENTED BY THE SYMBOL ^.":GOSUB1
66 M$="YOU BEGIN AT THE TOP OF THE SHAFT. ALL GOLD VEINS ARE SHOWN AS *.":GOSUB1:GOSUB8

400 CLS6:C=2:PRINT@1,"ØÐÐbankÐÐÔ";:PRINT@33,"Ú$$$$$$$$Õ";
410 FORT=2TO15:PRINT@T*32,;:M$="                               "+CHR$(47+C):GOSUB7
420 IFT=10THENC=0
430 PRINT@T*32+16," ";:C=C+1:NEXT
440 D=0:G=0:FORT=1TO100
510 C1=RND(31)-1:C2=RND(14)+1:IFPEEK(M+C2*32+C1)=32THENPOKEM+C2*32+C1,42:GOTO530
520 GOTO510
530 NEXT:X=16:Y=2:PRINT@X+32*Y,CHR$(94);:PRINT@20,"DAY"INT(D);

700 REM MAIN LOOP
710 M$=INKEY$:IFM$=""THEN710
720 IFM$="W"THENC1=PEEK(X+32*Y+M-32):D=D+.25:IFC1=96ORC1=32ORC1=42THENPRINT@X+32*Y," ";:Y=Y-1:IFC1<96THENGOSUB20
730 IFM$="Z"THENC1=PEEK(X+32*Y+M+32):D=D+.25:IFC1=96ORC1=32ORC1=42THENPRINT@X+32*Y," ";:Y=Y+1:IFC1<96THENGOSUB20
740 IFM$="A"THENC1=PEEK(X+32*Y+M-1):D=D+.25:IFC1=96ORC1=32ORC1=42THENPRINT@X+32*Y," ";:X=X-1:IFC1<96THENGOSUB20
750 IFM$="S"THENC1=PEEK(X+32*Y+M+1):D=D+.25:IFC1=96ORC1=32ORC1=42THENPRINT@X+32*Y," ";:X=X+1:IFC1<96THENGOSUB20
760 PRINT@X+32*Y,"^";:IFC1=96THEN780
770 FORC2=1TO50:NEXT:PRINTCHR$(8)CHR$(95);:FORC2=1TO150:NEXT:SOUND225,1:IFC1=42THENSOUND150,1
780 PRINTCHR$(8)"^";:PRINT@20,"DAY"INT(D);
790 IFG<1000000ANDD<365THEN710
1000 IFG<1000000THENPRINT@0,"YOUR YEAR IS UP!":C2=25:GOSUB9:GOTO1100
1010 PRINT@0,"YOU CLEARED OUT YOUR CLAIM!":C2=200:GOSUB9
1100 C=INT(G/D):PRINT@32,"YOUR SCORE WAS";C:IFC>HSTHENHS=C
1110 PRINT@32+21,"HIGH"STR$(HS)
1120 PRINT@485," WANT TO PLAY AGAIN? ";
1210 M$=INKEY$:IFM$="Y"THEN400
1220 IFM$="N"THEN1600
1230 GOTO1210
1600 CLS:PRINT"WELL THEN OLD TIMER."
1610 M$="IT'S BEEN NICE PLAYING WITH YOU.":GOSUB1
1620 END 
