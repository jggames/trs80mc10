10 X=RND(-(PEEK(9)*256+PEEK(10)))
90 U=INT(400*RND(0))+100:U$="U-"+RIGHT$(STR$(U),3)
100 DIM B(50),E(50),G(50),H(50),K(50),L(50)
110 DIM R(50),S(50),T(50),W(50)
150 Z9=INT(RND(0)*7)+3:L=0:M9=1:W=0:PRINT
160 FORX=1TO12:K(Z)=0:NEXT
170 CLS
210 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
220 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
225 PRINT"€€€€€€€€€€€€ÔÚÐÐÐÐ€€€€€€€€€€€€€€";
230 PRINT"€€€€€€€€€€€€€ÚÑÐÐ€€€€€€€€€€€€€€€";
240 PRINT"€€€€€€€€€€€€ÑÛ×ÐÐÓÐ€€€€€€€€€€€€€";
250 PRINT"€€€€€€€€ÑÓÓ€ÕßßßÛÛÐ€€€€€€€€€€€€€";
260 PRINT"ÓÓÓÓÓÓÓÓÓ×ÛÓ×ßßßßßÓÓÓÓÓÓÓÓÓÓÓÓÓÓ";
270 PRINT"ÔÝ×ÝßßßßßßßßßßßßßßßßßßßßßßßßßßÜÐ";
280 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
285 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
286 PRINT:PRINT"   BY RALPH HOPKINS 4/25/2003":PRINT"    MC-10 EDITS JIM GERRIE":PRINT
289 PRINT@13,U$;:FORX=16397TO16401:POKEX,PEEK(X)-64:NEXT             
300 DATA0,1,0,1,1,1,2,25,26,5
310 READ A,C,K,M,N,Q1,S,S1,T,Z
320 O=INT(RND(0)*20.5)+1
330 D1=INT(RND(0)*15)+15
400 B=INT((RND(0)*70)+15)*10
410 DATA8,2,46,47,48,50,49,12,3,49
420 DATA3,4,13,14,15,1,16,6,17,18
430 DATA19,1,20,10,21,21,23,3,7,24
440 DATA12,25,1,9,31,6,26,3,32,33
450 DATA34,11,28,5,27,29,35,48,2,30
490 FORX=1TO50:READ T(X):H(X)=1:G(X)=0:NEXT 
500 PRINT@448,"INSTRUCTIONS FOR U-BOAT?"
520 IFRND(0)>.6THENZ=Z+INT(RND(0)*15)
540 INPUT"INPUTS ARE #S (1=YES 0=NO)";Y
550 FOR X=1 TO Z
560 Z1=INT(RND(0)*50)+1:PRINT
570 Z2=INT(RND(0)*50)+1:T(Z1)=T(Z2)
580 NEXT
590 IFY=0THEN700
600 PRINT"MESSAGE FROM HIGH COMMAND:" 
620 PRINT"ATTACK AND SINK ENEMY"
630 PRINT"SHIPPING OPERATING ";
635 GOSUB7700:IFLEFT$(A$,3)="OFF"THENPRINT:GOTO637
636 PRINT"IN THE"
637 PRINTA$"."
640 PRINT"BEWARE THE DEADLY 'HOT SHOT'"
650 PRINT"WHO HAS SUNK";Z9;"OF OUR BOATS."
660 PRINT"YOU WILL HAVE";D1;"DAYS.":PRINT
700 FORX=1 TO 50:L(X)=10:NEXT 
710 PRINT:PRINT"ENTER -1 TO SEE COMMAND LIST"
720 PRINT
730 Q=O+Z
800 FOR X=1 TO 50
810 IFT(X)=6THENIFO>7THENIFO<11THENT(X)=44:GOTO990
820 IFT(X)=8THENIFO<4THENT(X)=41:GOTO990
830 IFT(X)=10THENIFO<3THENT(X)=43:GOTO990
835 IFT(X)=10THENIFO>12THENT(X)=43:GOTO990
840 IFT(X)=17THENIFO>7THENIFO<11THENT(X)=44:GOTO990
850 IFT(X)=20THENIFO>8THENIFO<13THENT(X)=42:GOTO990
860 IFT(X)=26THENIFO>3THENIFO<12THENT(X)=42:GOTO990
870 IFT(X)=29THENIFO=8THENT(X)=34:GOTO990
875 IFT(X)=29THENIFO=11THENT(X)=34:GOTO990
880 IFT(X)=31THENIFO>7THENIFO<11THENT(X)=44:GOTO990
885 IFT(X)=31THENIFRND(0)>.5THENT(X)=37:GOTO990
890 IFT(X)=32THENIFO=8THENT(X)=34:GOTO990
895 IFT(X)=32THENIFO=11THENT(X)=34:GOTO990
897 IFT(X)=32THENIFO>12THENT(X)=39:GOTO990
900 IFT(X)=33THENIFO=8THENT(X)=34:GOTO990
905 IFT(X)=33THENIFO=11THENT(X)=34:GOTO990
910 IFT(X)=33THENIFO>12THENT(X)=40:GOTO990
915 IFT(X)=33THENIFRND(0)>.5THENT(X)=38:GOTO990
920 IFT(X)=35THENIFRND(0)>.5THENT(X)=37:GOTO990
925 IFT(X)=35THENIFO=8THENT(X)=34:GOTO990
930 IFT(X)=35THENIFO=11THENT(X)=34:GOTO990
970 IFT(X)=49THENIFO>3THENIFO<8THENT(X)=45:GOTO990
990 NEXT 
1000 PRINT"GOOD HUNTING "U$
1020 Z=INT(RND(0)*12)+1:Y=0
1030 Z1=INT(RND(0)*28)+13:Y1=0
1040 Z2=INT(RND(0)*10)+41:Y2=0
1050 FOR X=1 TO 50
1060 IFT(X)<11THENL(X)=6
1100 IFT(X)=ZTHENY=Y+1:PRINTCHR$(8);Y;:GOSUB7000:PRINT"(S) IN AREA"
1110 IFT(X)=Z1THENY1=Y1+1:PRINTCHR$(8);Y1;:GOSUB7000:PRINT"(S) REPORTED"
1120 IFT(X)=Z2THENY2=Y2+1:PRINTCHR$(8);Y2;:GOSUB7000:PRINT"(S) SPOTTED"
1130 IFT(X)=1THENW(X)=1050:G(X)=1:S(X)=250:GOTO1900
1140 IFT(X)=2THENW(X)=1800:H(X)=2:G(X)=2:S(X)=300:GOTO1900
1150 IFT(X)=3THENW(X)=1350:H(X)=2:G(X)=2:S(X)=325:GOTO1900
1160 IFT(X)=4THENW(X)=1200:G(X)=2:S(X)=300:GOTO1900
1170 IFT(X)=5THENW(X)=900:G(X)=1:S(X)=450:GOTO1900
1180 IFT(X)=6THENW(X)=900:G(X)=1:S(X)=400:GOTO1900
1190 IFT(X)=7THENH(X)=4:IFO>4THENIFO<8THENH(X)=INT(RND(0)*3.5)+1
1195 IFT(X)=7THENW(X)=2100:G(X)=3:S(X)=300:GOTO1900
1200 IFT(X)=8THENW(X)=2500:H(X)=3:G(X)=2:S(X)=400:GOTO1900
1210 IFT(X)=9THENW(X)=1400:G(X)=1:S(X)=275:GOTO1900
1220 IFT(X)=10THENW(X)=70:G(X)=1:S(X)=600:GOTO1900
1230 IFT(X)=11THENW(X)=90:G(X)=1:S(X)=500:GOTO1900
1240 IFT(X)=12THENIFO=4THENG(X)=1
1245 IFT(X)=12THENW(X)=3100:H(X)=2:L(X)=7:S(X)=300:GOTO1900
1250 IFT(X)=13THENW(X)=3500:L(X)=8:S(X)=275:GOTO1900
1260 IFT(X)=14THENW(X)=6500:H(X)=2:L(X)=8:S(X)=250:GOTO1900
1270 IFT(X)=15THENW(X)=9150:H(X)=2:L(X)=9:S(X)=250:GOTO1900
1280 IFT(X)=16THENIFO>3THENG(X)=1:IFO=8THENG(X)=0
1285 IFT(X)=16THENW(X)=11500:H(X)=3:L(X)=8:S(X)=275:GOTO1900
1290 IFT(X)=17THENW(X)=9500:H(X)=2:L(X)=9:S(X)=275:GOTO1900
1300 IFT(X)=18THENG(X)=1:IFO>5THENIFO<13THENG(X)=0
1305 IFT(X)=18THENW(X)=11500:H(X)=3:L(X)=9:S(X)=100:GOTO1900
1310 IFT(X)=19THENW(X)=8500:H(X)=2:L(X)=9:S(X)=150:GOTO1900
1320 IFT(X)=20THENW(X)=6000:H(X)=2:L(X)=8:S(X)=200:GOTO1900
1330 IFT(X)=21THENW(X)=7500:H(X)=2:L(X)=8:S(X)=200:GOTO1900
1340 IFT(X)=22THENIFO=INT(RND(0)*16)+1THENG(X)=1
1345 IFT(X)=22THENW(X)=9650:L(X)=7:S(X)=200:GOTO1900
1350 IFT(X)=23THENW(X)=8850:H(X)=3:L(X)=7:S(X)=100:GOTO1900
1355 IFT(X)=23THENG(X)=1:IFO>2THENG(X)=0:IFO=12THENG(X)=1
1360 IFT(X)=24THENIFO>4THENG(X)=2:IFO>13THENG(X)=1
1365 IFT(X)=24THENW(X)=4350:H(X)=3:L(X)=7:S(X)=300:GOTO1900
1370 IFT(X)=25THENIFO=16THENG(X)=1
1375 IFT(X)=25THENW(X)=1350:L(X)=7:S(X)=300:GOTO1900
1380 IFT(X)=26THENW(X)=7100:H(X)=3:L(X)=8:S(X)=250:GOTO1900
1390 IFT(X)=27THENW(X)=2250:H(X)=2:L(X)=8:S(X)=250:GOTO1900
1400 IFT(X)=28THENW(X)=2000:L(X)=7:S(X)=150:GOTO1900
1410 IFT(X)=29THENW(X)=1800:H(X)=2:S(X)=150:GOTO1900
1420 IFT(X)=30THENW(X)=3100:H(X)=2:L(X)=9:S(X)=275:GOTO1900
1430 IFT(X)=31THENW(X)=650:S(X)=200:GOTO1900
1440 IFT(X)=32THENW(X)=120:S(X)=100:GOTO1900
1450 IFT(X)=33THENW(X)=100:S(X)=50:GOTO1900
1460 IFT(X)=34THENW(X)=650:S(X)=90:GOTO1900
1470 IFT(X)=35THENW(X)=950:S(X)=70:GOTO1900
1480 IFT(X)=36THENW(X)=6000:H(X)=2:L(X)=8:S(X)=200:GOTO1900
1490 IFT(X)=37THENW(X)=850:S(X)=150:GOTO1900
1500 IFT(X)=38THENW(X)=500:S(X)=50:GOTO1900
1510 IFT(X)=39THENW(X)=90:S(X)=50:GOTO1900
1520 IFT(X)=40THENW(X)=100:S(X)=50:GOTO1900
1530 IFT(X)=41THENW(X)=3000:H(X)=3:G(X)=2:S(X)=400:L(X)=6:GOTO1900
1540 IFT(X)=42THENW(X)=7500:H(X)=3:S(X)=400:L(X)=8:GOTO1900
1550 IFT(X)=43THENW(X)=150:G(X)=1:S(X)=400:L(X)=6:GOTO1900
1560 IFT(X)=44THENW(X)=800:G(X)=1:L(X)=12:S(X)=200:GOTO1900
1570 IFT(X)=45THENW(X)=13500:H(X)=4:G(X)=2:L(X)=2:S(X)=300:GOTO1900
1580 IFT(X)=46THENW(X)=33500:H(X)=8:G(X)=6:L(X)=1:S(X)=325:GOTO1900
1590 IFT(X)=47THENW(X)=25700:H(X)=5:G(X)=3:L(X)=2:S(X)=400:GOTO1900
1600 IFT(X)=48THENW(X)=18500:H(X)=4:G(X)=4:L(X)=3:S(X)=375:GOTO1900
1610 IFT(X)=49THENW(X)=12800:H(X)=3:G(X)=3:L(X)=3:S(X)=350
1620 IFT(X)=50THENW(X)=10500:H(X)=3:G(X)=3:L(X)=3:S(X)=375
1900 NEXT 
1950 IFY+Y1+Y2>0THEN1990
1960 PRINT"NO INTELLEGENCE REPORT"
1990 Y=0
3000 IFN>0THEN3200
3020 Q=INT(RND(0)*45)+1:Q1=INT(RND(0)*5)+1
3025 IFRND(0)>.4THENQ1=INT(RND(0)*2.6)+1
3030 FORX=QTOQ+(Q1-1)
3040 IFH(X)<=0THEN3090
3050 R(X)=INT((RND(0)*25)+5)*250
3055 IFS=3THENR(X)=INT((RND(0)*10)+3)*1000
3060 E(X)=INT(R(X)/2):Z=1
3065 IFRND(0)>.3THENZ=-1
3070 S(X)=S(X)*(Z):E=INT(R(X)*.9)
3080 B(X)=INT(RND(0)*3600)/10
3090 NEXT 
3100 C=INT(RND(0)*32)+1
3110 D=INT(RND(0)*20)+1
3120 D1=D1-1
3130 A=0:B=INT((RND(0)*70)+15)*10
3200 N=0
3230 FORX=QTOQ+(Q1-1)
3240 IFH(X)<=0THEN3290
3250 N=N+1:R(X)=R(X)+(M*S(X))
3260 IFS=3THENIFG(X)=0THENS(X)=ABS(S(X))
3270 IFR(X)<0THENS(X)=ABS(S(X))
3280 R(X)=INT(R(X)/10)*10
3285 IFS>1THENIFR(X)=0THENS=-4
3290 NEXT
3300 IFS<0THEN9000
3310 IFN<=0THEN4000
3320 IFA=0THEN4000
3330 IFS<3THEN3400
3340 IFRND(0)>.95THEN3400
3350 FOR X=Q TO Q+(Q1-1)
3360 IFH(X)<=0THEN3390
3370 IFG(X)>0THENGOSUB3800
3380 IFS<0THEN9000
3390 NEXT 
3400 IFO<8THEN3600
3405 IFO>10THEN3600
3410 IFS<1THEN3600
3420 FORX=QTOQ+(Q1-1)
3430 IFH(X)<=0THEN3485
3435 IFL(X)<11THEN3485
3440 H=INT(RND(0)*6)-INT((R(X)/1000)-3):Z=10
3450 IFS=2THENZ=INT((RND(0)*75)+1)
3460 IFS=3THENGOSUB7000:PRINT"FIRING TORPEDOES"
3465 IFS<3THENPRINT"SONAR REPORTS:":GOSUB9195:PRINT"INCOMING TORPEDOES!":GOSUB9195
3470 IFH>0THENIFABS(S1-Z)<11THENS=-3:GOTO9000
3480 PRINT"ENGLISH TORPEDOES MISSED"
3485 NEXT 
3490 IFS>1THEN3600
3500 FORX=QTOQ+(Q1-1)
3510 IFH(X)<=0THEN3590
3520 IFL(X)<11THEN3590
3530 IFA=0THENL(X)=12:GOTO3590
3540 IFL(X)=12THENGOSUB7000:PRINT"DIVING":L(X)=11
3590 NEXT 
3600 IFS=3THEN4000
3605 PRINT
3610 IFA=0THEN5000
3620 FOR X=Q TO Q+(Q1-1)
3625 IFH(X)<=0THEN3795
3630 IFL(X)<>6THEN3795
3640 IFR(X)>INT(RND(0)*4000)+2500THENIFRND(0)>.7THENA=0
3645 S(X)=ABS(S(X))*(-1):IFR(X)>1000THENR(X)=R(X)+S(X)
3650 IFR(X)>0THEN3660
3655 R(X)=INT((RND(0)*10)*250)+500:B(X)=INT(RND(0)*360)+1
3660 GOSUB7000
3670 IFR(X)=>4500THENPRINT" APROACHING FAST":GOTO3690
3680 PRINT" IS ATTACKING!":PRINT"BEARING:";B(X)
3690 Z1=INT(RND(0)*4)+3
3700 GOSUB7000
3710 PRINT" @"R(X)"M."
3715 IFR(X)>2500THEN3725
3720 IFRND(0)>.4THENPRINT"V A A R O O M":FORZZ=1TO3:SOUNDRND(100),5:NEXT:GOTO3730
3725 PRINTU$" @"S1"M."
3730 IFL(X)<11THENR(X)=R(X)+(S(X))
3740 S1=S1+25:IFS1>BTHENS1=B
3745 IFR(X)=>4500THEN3795
3750 IFS1=>400THEN3760
3755 IFR(X)>0THEN3700
3760 R(X)=INT((RND(0)*25)+1)*100:Z=INT((RND(0)*10)*25)+50
3765 IFS1<=125THENIFR(X)<=0THENS=-2:GOTO9000
3770 FOR Y=1 TO Z1
3772 PRINT"V A A R O O M":SOUND35,5
3774 NEXT 
3775 PRINT
3780 IFS1=BTHENPRINT"ON THE BOTTOM AT"B"M."
3785 IFZ+25=>S1THENIFZ-25<=S1THENS=-2:GOTO9000
3790 PRINT"* WHEW! * "U$" ESCAPED!"
3795 NEXT 
3799 GOTO4000
3800 FORZ=1TOG(X)
3810 IFRND(0)>.9THEN3940
3820 PRINT"B O O M":SOUND1,7
3830 GOSUB7000:PRINT" @"R(X)"M. FIRING"
3840 E(X)=INT(RND(0)*(R(X)-E(X))+E(X))
3850 IFE(X)>R(X)+10THENE(X)=R(X)-INT(RND(0)*50)
3870 IFR(X)-E(X)>ABS(10)THENIFABS(R(X))>500THEN3900
3880 S=-1
3890 GOTO3995
3900 PRINT"* S P L A S H *"
3910 PRINT"MISSED BY";R(X)-E(X);"M."
3920 IFL(X)>2THENS(X)=ABS(S(X))*(-1)
3940 NEXT 
3950 IFL(X)=6THENA=1
3995 RETURN
4000 IFS=3THENA=1
5000 IFS<0THEN9000
5020 IFD1<=0THEN9000
5030 IFT<=0THEN9000
5040 IFK=>50THEN9000
5050 M=1
5080 IFN=0THEN5100
5090 IFC<>2THEN5200
5100 PRINT
5110 IFS=3THENPRINT"LOOKOUT REPORT"
5120 IFS<3THENPRINT"SONAR REPORT":GOSUB9195
5130 PRINT
5140 IFN=0THENPRINT"...NOTHING.":GOTO5200
5145 IFS=3THENGOSUB5700:GOTO5200
5150 IFN=1THENIFR(Q)>5000THENPRINT"...DISTANT SOUNDS"
5155 IFN=1THENIFR(Q)<=5000THENPRINT"...SHIP SOUNDS ABOVE"
5160 IFN>3THENPRINT"* FLASH *":PRINT
5190 IFN>1THENPRINT"SOUNDS LIKE";N;"SHIPS!"
5200 PRINT
5210 INPUT"COMMAND CAPTAIN (0-9)";C
5220 PRINT
5300 IFC=0THEN6000
5310 IFC=1THEN6100
5320 IFC=2THEN6200
5330 IFC=3THEN6300
5340 IFC=4THEN6400
5350 IFC=5THEN6500
5360 IFC=6THEN6600
5370 IFC=7THEN6700
5380 IFC=8THEN6800
5390 IFC=9THEN6900
5500 PRINT"COMMAND SUMMARY":PRINT
5505 PRINT"0=SURFACE THE U-BOAT"
5510 PRINT"1=PERISCOPE OR SURFACE SCAN"
5520 PRINT"2=WAIT FOR THE ENEMY TO MOVE"
5530 PRINT"3=FIRE THE DECK GUN"
5540 PRINT"4=FIRE TORPEDOES"
5550 PRINT"5=WAIT ONE DAY--GET NEW SHIPS"
5560 PRINT"6=SHIP STATUS--DAYS LEFT"
5570 PRINT"7=INFO ON HITS TO SINK SHIPS"
5580 PRINT"8=DIVE
5590 PRINT"9=SHIPS LOG OF VESSELS SUNK"
5690 GOTO5200
5700 FOR X=Q TO Q+(Q1-1)
5710 IFH(X)<=0THEN5770
5720 IFC>1THENPRINTX;
5730 GOSUB7000
5740 PRINT" @"R(X)"M."
5770 NEXT
5780 IFC>2THENIFC<5THENGOSUB5900
5790 RETURN
5800 FORE=QTOQ+(Q1-1)
5810 IFH(E)>0THENX=E:GOTO5890
5830 NEXT 
5840 E=0
5850 PRINT"NO SHIPS IN SIGHT"
5890 RETURN
5900 PRINT:Y=0
5910 INPUT"SHIP # TO SHOOT AT";E
5920 PRINT
5930 IFE=0THENPRINT"SHOOTING ABORTED":GOTO5995
5940 IFE<QTHENPRINT"# TOO LOW-TRY AGAIN ON CMD";C:Y=1:GOTO5995
5950 IFE>Q+Q1THENPRINT"# TOO HIGH-TRY AGAIN ON CMD";C:Y=1:GOTO5995
5960 IFW(E)<=0THENGOSUB7000:PRINT" *SUNK* ":Y=1:GOTO5995
5995 RETURN
6000 IFS<3THENPRINTU$" SURFACING" 
6020 IFS=3THENGOSUB8800
6025 IFS=3THENIFRND(0)>.93THENGOSUB8900
6030 PRINT
6040 GOSUB5700
6050 S1=10:S=3
6060 A=0
6095 GOTO3000
6100 IFS<2THENPRINT"RISING TO PERISCOPE DEPTH"
6120 IFS=3THENPRINT"DIVING TO PERISCOPE DEPTH"
6130 S=2:PRINT:S1=25
6150 GOSUB5700
6195 GOTO3000
6200 M=2
6220 IFS<3THENA=0
6295 GOTO3000
6300 Y=0:E=1
6310 IFS<3THENPRINT"YOU'RE UNDER WATER":GOTO3000
6320 GOSUB5800
6325 IFN>1THENGOSUB5700
6330 IFY=1THEN5000
6335 IFE=0THEN5000
6340 PRINT"B A M":X=E:SOUND25,5
6350 PRINT"FIRING AT THE ";:GOSUB7000:PRINT
6360 E1=200:IFS(E)<0THENE1=-300
6370 GOSUB8000
6375 IFRND(0)>.9THENGOSUB8900
6380 IFN<=0THENPRINT"THAT'S ALL FOR TODAY"
6385 IFN<=0THEN6500
6395 GOTO3000
6400 GOSUB5800
6415 IFN>1THENGOSUB5700
6420 IFY=1THEN5000
6425 IFE=0THEN5000
6430 PRINT"TORPEDOES LEFT:";T:PRINT
6440 INPUT"INPUT # TO FIRE";Z
6445 PRINT:IFZ<0THENPRINT"FIRING CANCELLED":GOTO5000
6450 IFZ>TTHENZ=T
6455 IFZ>6THENZ=6
6460 T=T-Z:PRINTCHR$(8);Z;"TORPEDOES UNDER WAY"
6465 PRINT:A=1:X=0:H=0
6470 IFS<2THEN3000
6480 IFR(E)>5000THENX=INT(R(E)/1000)-5
6485 H=INT(RND(0)*(Z-X)+.5):IFR(E)<=1000THENH=Z
6490 X=E:GOSUB8400
6495 GOTO3000
6500 IFS=3THENPRINTU$" NOW SAFELY AT";S1;"M."
6520 IFS=3THENS1=INT((RND(0)*10)+10)*10:S=2
6530 N=0
6540 PRINT"WAITED ONE DAY.";D1;"DAYS LEFT."
6595 GOTO3000
6600 FORX=1TO6:PRINT:NEXT
6605 PRINT"STATUS--MISSION #";M9:PRINT
6610 GOSUB7700:PRINTA$
6620 PRINT"WITH";T;"TORPEDOES":PRINT"AND";D1;"DAYS LEFT."
6640 IFK>1THENPRINT"WE'VE SUNK";K;"ENEMY SHIPS AND":PRINTCHR$(8);W;"TONNES OF SHIPPING."
6670 IFM9>1THENPRINT"SINCE STARTING, YOU'VE SUNK ":PRINT"A TOTAL OF";L;"ALLIED SHIPS.":PRINT
6690 PRINT"DEPTH IS";S1;"M."
6695 GOTO5000
6700 GOSUB7500
6790 GOTO5200
6800 PRINT"DIVE";:SOUND100,13:PRINT"--DIVE";:SOUND100,13:PRINT"--DIVE":SOUND100,13
6810 Z=INT((RND(0)*15)*10)+S1
6820 FOR S1=S1 TO Z STEP 25
6830 IFS1=>BTHENS1=B:GOTO6880
6840 PRINT"BOAT AT";S1;"METRES."
6870 NEXT
6880 S=1
6890 IFS1=BTHENPRINT"ON THE BOTTOM AT";S1;"M."
6895 GOTO3000
6900 Z=0
6910 GOSUB6950:PRINT"YOU'VE SUNK";Z;"SHIPS"
6920 GOTO3000
6950 FORX=1TO50
6955 IFH(X)>0THEN6990
6960 Z=Z+1:GOSUB7000:PRINTW(X);"TONNES."
6970 IFZ=12THENGOSUB6999
6975 IFZ=24THENGOSUB6999
6980 IFZ=36THENGOSUB6999
6990 NEXT
6995 RETURN
6999 INPUT"# TO SEE MORE";Y:RETURN
7000 ONT(X)GOTO7010,7020,7030,7040,7050,7060,7070,7080,7090,7100
7001 ONT(X)-10GOTO7110,7120,7130,7140,7150,7160,7170,7180,7190,7200
7002 ONT(X)-20GOTO7210,7220,7230,7240,7250,7260,7270,7280,7290,7300
7003 ONT(X)-30GOTO7310,7320,7330,7340,7350,7360,7370,7380,7390,7400
7004 ONT(X)-40GOTO7410,7420,7430,7440,7450,7460,7470,7480,7490
7006 GOTO7490
7010 PRINT"CORVETTE";:RETURN
7020 PRINT"DESTROYER";:RETURN
7030 PRINT"DESTROYER";:RETURN
7040 PRINT"DESTROYER";:RETURN
7050 PRINT"CORVETTE";:RETURN
7060 PRINT"SUB CHASER";:RETURN
7070 PRINT"FAST-BOAT";:RETURN
7080 PRINT"MYSTERY SHIP";:RETURN
7090 PRINT"UNKNOWN SHIP";:RETURN
7100 PRINT"TORPEDO BOAT";:RETURN
7110 PRINT"PATROL BOAT";:RETURN
7120 PRINT"SUPPLY SHIP";:RETURN
7130 PRINT"SMALL CARGO";:RETURN
7140 PRINT"MERCHANTMAN";:RETURN
7150 PRINT"TANKER";:RETURN
7160 PRINT"TROOP SHIP";:RETURN
7170 PRINT"TANKER";:RETURN
7180 PRINT"BIG TANKER";:RETURN
7190 PRINT"TANKER";:RETURN
7200 PRINT"SUPPLY SHIP";:RETURN
7210 PRINT"CARGO SHIP";:RETURN
7220 PRINT"AMMO SHIP";:RETURN
7230 PRINT"ORE SHIP";:RETURN
7240 PRINT"SUB TENDER";:RETURN
7250 PRINT"SMALL SHIP";:RETURN
7260 PRINT"U.S. FREIGHTER";:RETURN
7270 PRINT"FREIGHTER";:RETURN
7280 PRINT"STEAMER";:RETURN
7290 PRINT"STEAMSHIP";:RETURN
7300 PRINT"FLEET OILER";:RETURN
7310 PRINT"MINESWEEPER";:RETURN
7320 PRINT"FISHING BOAT";:RETURN
7330 PRINT"BOAT";:RETURN
7340 PRINT"LANDING CRAFT";:RETURN
7350 PRINT"TUG";:RETURN
7360 PRINT"SHRIMPER";:RETURN
7370 PRINT"MINE-LAYER";:RETURN
7380 PRINT"BARGE";:RETURN
7390 PRINT"YACHT";:RETURN
7400 PRINT"FERRY BOAT";:RETURN
7410 PRINT"* HOT SHOT *";:RETURN
7420 PRINT"BIG FREIGHTER";:RETURN
7430 PRINT"PATROL BOAT";:RETURN
7440 PRINT"ENGLISH SUB";:RETURN
7450 PRINT"LIGHT CARRIER";:RETURN
7460 PRINT"BATTLESHIP";:RETURN
7470 PRINT"FLEET CARRIER";:RETURN
7480 PRINT"HEAVY CRUISER";:RETURN
7490 PRINT"LIGHT CRUISER";:RETURN
7500 PRINT"BATTLESHIPS","7-10"
7520 PRINT"CARRIERS","4-6"
7530 PRINT"CRUISERS","3-4"
7540 PRINT"DESTROYERS","1-2"
7550 PRINT"ESCORTS","1"
7560 PRINT"ANTI-SUB SHIPS","1-5"
7570 PRINT"CARGO","1-3"
7575 PRINT"STEAMERS","1-2"
7580 PRINT"FREIGHTERS","2-3"
7585 PRINT"TANKERS","2-3"
7590 PRINT"WORTHLESS","1"
7595 RETURN
7600 ON X GOSUB7610,7620,7630,7595,7595,7660,7670,7680,7685,7690,7595,7695:RETURN
7610 PRINT"BATTLESHIPS:";:RETURN
7620 PRINT"CARRIERS:";:RETURN
7630 PRINT"CRUISERS:";:RETURN
7660 PRINT"ANTI-SUB SHIPS:";:RETURN
7670 PRINT"CARGO:";:RETURN
7680 PRINT"STEAMERS:";:RETURN
7685 PRINT"FREIGHTERS:";:RETURN
7690 PRINT"WORTHLESS:";:RETURN
7695 PRINT"ENGLISH SUBS:";:RETURN
7700 ON O GOTO7710,7720,7730,7740,7750,7760,7770,7780,7790,7800,7810,7820,7830,7840,7850,7860,7870,7875,7880:GOTO7890
7710 A$="NORTH SEA":RETURN
7720 A$="BALTIC SEA":RETURN
7730 A$="OFF THE FRENCH COAST":RETURN
7740 A$="AEGEAN SEA":RETURN
7750 A$="MEDITERRANEAN":RETURN
7760 A$="BARENTS SEA":RETURN
7770 A$="SOUTH ATLANTIC":RETURN
7780 A$="DENMARK STRAIT":RETURN
7790 A$="JUTLAND STRAITS":RETURN
7800 A$="OFF THE NORWEGIAN COAST":RETURN
7810 A$="FALKLAND ISLAND AREA":RETURN
7820 A$="NORTH ATLANTIC":RETURN
7830 A$="OFF THE SOUTH AMERICAN COAST":RETURN
7840 A$="OFF THE AFRICAN COAST":RETURN
7850 A$="OFF THE AMERICAN COAST":RETURN
7860 A$="TYRRHENIAN SEA":RETURN
7870 A$="IONIAN SEA":RETURN
7875 A$="OFF MONTAUK POINT":RETURN
7880 A$="OFF LONG ISLAND":RETURN
7890 A$="BERMUDA TRIANGLE":RETURN
7900 PRINT"* ALLIED SHIPS SUNK *":PRINT
7910 FORX=1TO50
7920 GOSUB7000
7930 PRINT"TONNAGE:";W(X)
7940 NEXT 
7945 RETURN
8000 E2=INT(RND(0)*(R(E)-E2)+E2)+INT(RND(0)*25)+(E1)
8010 IFL(E)<7THENE2=E2-INT((RND(0)*10+1)*5)
8020 IFABS(R(E)-E2)<500THENE2=R(E)
8030 IFR(E)>15000THENIFR(E)=15000>R(E)-E2THENE2=15000
8040 IFABS(R(E)-E2)>30THENPRINT"*SPLASH*"
8050 IFABS(R(E)-E2)<=30THENPRINT"*BOOM*"
8060 X=E:GOSUB7000
8070 IFABS(R(E)-E2)>30THENPRINT" MISSED BY";R(E)-E2;"M."
8080 IFABS(R(E)-E2)<=30THENPRINT"  H I T !"
8090 IFR(E)>15000THENGOSUB7000:PRINT" *OUT OF GUN RANGE*"
8100 H=0:IFABS(R(E)-E2)<31THENH=RND(0)
8110 IFH>0THENIFL(E)=10THENH=H*5
8120 IFH>0THENH(E)=H(E)-H
8130 IFT(E)=22THENIFH(E)<-.3THEN8150
8140 GOTO8200
8150 PRINT"B A A V A R O O O M N":PRINT
8170 PRINT"MEIN GOT...WHAT AN EXPLOSION!"
8200 Z=INT(RND(0)*10):IFH(E)<1THENGOSUB7000
8210 IFH(E)>.6THENIFH(E)<1THENPRINT" LISTING..."
8220 IFH(E)>.25THENIFH(E)<.65THENPRINT" +SMOKING+"
8230 IFH(E)<.3THENPRINT" *BURNING*"
8240 IFH(E)<=0THEN8300
8245 IFH<=0THEN8300
8260 IFZ>8THENPRINT"*GOOD HIT*"
8270 IFZ=5THENPRINT"HIT NEAR THE WATER LINE!"
8280 IFH(E)<.1THENPRINT"IT'S A FLOATING WRECK!"
8300 IFH(E)<=0THENGOSUB7000:PRINT" *SINKING*":GOSUB8650
8390 PRINT:A=1
8395 RETURN
8400 Z=RND(0)
8410 IFH>0THENH(E)=H(E)-H
8420 IFH<=0THENGOSUB7000:PRINT" MISSED.":GOTO8600
8430 PRINT"B O O M!":SOUND1,5:PRINT
8450 IFRND(0)>.06THEN8480
8460 PRINT"WHAT A LUCKY HIT!":PRINT
8470 H(E)=INT(RND(0)*4)-3
8480 PRINTCHR$(8);H;"HIT";:IFH>1THENPRINT"S";
8500 PRINT:GOSUB7000
8510 IFH(E)>6THENPRINT" FELT THAT!"
8520 IFH(E)>3THENIFH(E)<7THENPRINT"...DENTED."
8530 IFH(E)>0THENIFH(E)<4THENPRINT" DAMAGED!"
8550 IFH(E)=0THENPRINT" * S U N K *"
8560 IFH(E)=-1THENPRINT" SUNK. "
8570 IFH(E)=-2THENPRINT" C R E A M E D!"
8580 IFH(E)=-3THENPRINT" BLOWN TO PIECES!"
8590 IFH(E)<-3THENPRINT" BLOWN TO HELL!"
8600 IFR(E)=>11000THENGOSUB7000
8610 IFR(E)=>11000THENPRINT" IS OUT RANGE!"
8630 IFH(E)>0THEN8690
8650 N=N-1:K=K+1:L=L+1:W=W+W(E):K(L(E))=K(L(E))+1
8690 RETURN
8800 PRINTU$" MOVING 250 METRES":PRINT"TOWARDS SHIPS."
8810 FOR X=Q TO Q+(Q1-1)
8820 R(X)=R(X)-250
8830 NEXT 
8890 RETURN
8900 IFQ+Q1<50THENQ1=Q1+1
8910 X=Q+Q1-1:IFX>51THEN8990
8930 IFH(X)<=0THEN8990
8940 PRINT"LOOKOUT REPORT"
8950 GOSUB7000
8960 R(X)=INT((RND(0)*9)+10):R(X)=R(X)*1000
8970 PRINT" @"R(X)"M."
8980 S(X)=ABS(S(X))*(-1):N=N+1
8990 RETURN
9000 IFS>-1THEN9200
9010 PRINT
9020 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿";
9050 PRINT"¿¿¿    K  A  B  L  A  M  !   ¿¿¿";
9080 PRINT"¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿";
9090 PRINT:PRINTU$" HAS BEEN SUNK BY "
9100 IFS=-1THENPRINT"GUNFIRE"
9110 IFS=-2THENPRINT"DEPTH CHARGES"
9120 IFS=-3THENPRINT"TORPEDOES"
9130 IFS=-4THENPRINT"RAMMING!"
9160 INPUT"TRY AGAIN (1=YES 0=NO)";Y
9180 IFY=1THENRUN
9190 GOTO9900
9195 FORSS=1TO2:SOUND200,2:FORZZ=1TO1800:NEXT:SOUND175,3:FORZZ=1TO1800:NEXT:NEXT:RETURN
9200 FORX=1TO5:PRINT:NEXT 
9210 PRINT"CONGRATULATIONS "U$
9230 PRINT"YOU END A SUCCESSFUL":PRINT"MISSION #";M9
9240 M9=M9+1:PRINT
9250 IFT<=0THENPRINT"ALL TORPEDOES SHOT"
9270 IFD1<=0THENPRINT"YOU ARE OUT OF SAILING DAYS"
9280 INPUT"SEE YOUR LOG (1=YES 0=NO)";Y
9285 Z=0:Z1=0:IFY=0THENPRINT:GOTO9300
9290 GOSUB6950:PRINT"YOU'VE SUNK";Z;"SHIPS"
9300 INPUT"SEE PREVIOUS TOTALS (1=YES 0=NO)";Y
9320 IFY=0THEN9500
9330 PRINT"SHIP TYPES SUNK"
9340 Z=0:Z1=0
9400 FORX=1TO12
9410 IFK(X)<=0THEN9480
9420 GOSUB7600:PRINTTAB(16);K(X)
9450 Z=Z+K(X):Z1=Z1+W(X)
9480 NEXT
9490 PRINT"TOTAL:";Z1;"TONNES"
9500 INPUT"TRY AGAIN (1=YES 0=NO)";Y
9510 IFM9>10THEN9600
9520 IFY=1THENRESTORE:GOTO300
9530 END
9600 PRINT"IT IS MAY 1941"
9610 PRINT"ENGLAND HAS SURRENDERED"
9620 PRINTU$" WILL ENTER A MUSEUM"
9630 PRINT"YOU RETIRE WITH RECORD OF"Z
9640 PRINT"ENEMY SHIPS."
9650 PRINT"YOUR TOTAL TONNAGE WAS:"W
9900 END 
