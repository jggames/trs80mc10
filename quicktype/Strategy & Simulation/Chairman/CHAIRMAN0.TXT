10 REM CHAIRMAN OF THE BOARD
20 GOSUB 1670:REM INITIALISE
30 WE=WE+1
40 GOSUB 930:REM REPORT
50 GOSUB 1300:REM STAFF
60 GOSUB 930:REM REPORT
70 GOSUB 1130:REM PRODUCTION
80 GOSUB 930:REM REPORT
90 GOSUB 730:REM SALES
100 GOSUB 140:REM PROBLEMS
110 CA=CA-WA*WO-RC
120 GOTO 30
130 REM ********
140 REM PROBLEMS
150 CLS
160 IF RND(0)<.45 THEN 260
170 A=RND(7)
180 PRINT:PRINT:PRINT
190 PRINT"THE UNIONS ARE DEMANDING A"
200 PRINT"PAY RISE OF"A"%"
210 WA=INT(100*(WA+WA*A/100))/100
220 FORZZ=1TO2500:NEXT
230 PRINT:PRINT"PAY PER EMPLOYEE IS NOW":PRINT"$"WA
240 GOSUB 1840
250 CLS
260 IF RND(0)<.81 THEN 410
270 PRINT:PRINT:PRINT
280 PRINT"A FIRE IN YOUR WAREHOUSE HAS"
290 PRINT"DESTROYED SOME STOCK. PLEASE"
300 PRINT"STAND BY FOR A REPORT ON"
310 PRINT"THE DAMAGE CAUSED..."
320 FORZZ=1TO2500:NEXT
330 A=RND(ST/2)
340 ST=ST-A
350 PRINT:PRINT"THERE WERE"A;A$
360 PRINT"DESTROYED. THEY WERE"
370 PRINT"WORTH $"A*SP"RETAIL"
380 FORZZ=1TO2500:NEXT
390 PRINT:PRINT"STOCK IN HAND IS"
400 PRINT"NOW"ST;A$:GOSUB 1840
410 IF RND(0)>.3 THEN 560
420 CLS
430 PRINT:PRINT
440 PRINT"YOUR MAIN SUPPLIER HAS ANNOUNCED";
450 PRINT"A DRAMATIC PRICE RISE..."
460 FORZZ=1TO2500:NEXT
470 A=RND(100*CO/7)/100
480 IF A<1 THEN 470
490 PRINT:PRINT"THE COST OF MAKING "A$
500 PRINT"HAS RISEN BY $"A"EACH"
510 FORZZ=1TO2500:NEXT
520 CO=CO+A
530 PRINT:PRINT"IT NOW COSTS $"CO
540 PRINT"TO MAKE EACH ONE..."
550 GOSUB 1840
560 IF RND(0)<.65 AND MA<SP THEN RETURN
570 CLS
580 PRINT:PRINT
590 PRINT"YOU HAVE A CHANCE TO RAISE"
600 PRINT"YOUR PRICE. YOUR "A$
610 PRINT"NOW SELL FOR $"SP
630 PRINT
640 PRINT"WHAT PERCENTAGE INCREASE WOULD"
650 INPUT"YOU LIKE TO IMPOSE";A:IF A<0 THEN 630
660 RE=RE+A
670 SP=INT(100*(SP+A*SP/100))/100
680 FORZZ=1TO2500:NEXT
690 PRINT:PRINT"THE "A$" NOW SELL FOR":PRINT"$"SP
700 GOSUB 1840
710 RETURN
720 REM *****
730 REM SALES
740 PRINT:PRINT"YOUR TOTAL STOCK OF "A$
750 PRINT"IS"ST
760 GOSUB 1840
770 PRINT:PRINT"PLEASE STAND BY FOR A"
780 PRINT"SALES REPORT...":FORZZ=1TO2500:NEXT
790 A=INT(RND(0)*ST/(RE/1000))+1
800 IF A>ST THEN 790
810 CLS
820 PRINT:PRINT
830 PRINT"THE TOTAL NUMBER OF"
840 PRINTA$" SOLD IS"A
850 ST=ST-A
860 ZA=A*SP
870 PRINT:PRINT"THE INCOME FROM THAT SALE WAS"
880 PRINT"$"ZA
890 CA=INT(A*SP*100)/100+CA
900 GOSUB 1840
910 RETURN
920 REM **********************
930 REM REPORT TO THE CHAIRMAN
940 CLS
950 IF CS+ST<1 THEN 1510:REM BANKRUPTCY
960 IF CA+ST>9999 THEN PRINT"YOU'VE MADE $10000 AND":PRINT"CAN NOW RETIRE...":GOTO 1590
970 PRINT:PRINT"SHOP FLOOR REPORT, SIR,"
980 PRINT TAB(6);"FOR WEEK"WE
990 PRINT:PRINT"CAPITAL IN HAND IS $"INT(CA*100)/100
1000 PRINT"RUNNING COSTS ARE":PRINTTAB(6)"$"RC"A WEEK"
1010 PRINT:PRINT"YOUR STORES HOLD":PRINTTAB(5)ST;A$
1020 PRINT TAB(6);"WORTH $"INT(ST*SP*100)/100
1030 PRINT:PRINT"THEY SELL FOR $"SP"EACH"
1040 PRINT"AND COST $"CO"EACH TO MAKE"
1045 GOSUB 1840
1050 PRINT:PRINT"YOUR WORKFORCE IS NOW"
1060 PRINT MID$(STR$(WO),2)" STRONG, AND YOU ARE"
1070 PRINT"PAYING THEM $"WA"EACH"
1080 PRINT"THE WAGES BILL THIS WEEK IS":PRINT"$"WA*WO
1090 PRINT:PRINT"EACH PERSON CAN MAKE"PR
1100 PRINT A$" A WEEK, A TOTAL"
1110 PRINT"OUTPUT OF"PR*WO
1120 RETURN
1130 PRINT:INPUT "HOW MANY DO YOU WISH TO PRODUCE ";MA:IF MA<0 THEN 1130
1140 IF MA=0 THEN RETURN
1150 PRINT
1160 IF MA*CO>CA THEN PRINT"YOU DO NOT HAVE ENOUGH MONEY":GOTO1130
1170 IF MA>PR*WO THEN PRINT"YOU DO NOT HAVE ENOUGH PEOPLE":PRINT"TO MAKE THAT MANY":GOTO1130
1180 PRINT"YES SIR...":PRINT"THE TARGET FOR WEEK"WE
1190 PRINT"IS"MA;A$
1200 MA=INT(MA-RND(0)*MA/5)
1210 FORZZ=1TO2500:NEXT
1220 PRINT:PRINT"THE NUMBER OF "A$
1230 PRINT"ACTUALLY PRODUCED IN WEEK"WE
1240 PRINT"WAS"STR$(MA)"..."
1250 ST=ST+MA
1260 CA=CA-CO*MA
1270 GOSUB 1840
1280 RETURN
1290 REM *****
1300 REM STAFF
1310 PRINT:PRINT"HOW MANY PEOPLE DO YOU WISH TO"
1320 INPUT"HIRE";A:IF A<0 THEN 1310
1330 WO=WO+A
1340 PRINT:PRINT"THE TOTAL WORKFORCE IS NOW"
1350 PRINT MID$(STR$(WO),2)" STRONG"
1360 GOSUB 1840
1370 IF A>0 THEN RETURN
1380 GOSUB 930
1390 PRINT:PRINT"HOW MANY PEOPLE DO YOU WISH TO"
1400 INPUT"FIRE";A:IF A<0 THEN 1390
1410 IF A=0 THEN RETURN
1420 IF A>WO THEN 1390
1430 A=RND(A)
1440 FORZZ=1TO2500:NEXT
1450 PRINT:PRINT"THE UNIONS WILL ALLOW YOU TO"
1460 PRINT"GET RID OF"A
1470 WO=WO-A
1480 GOSUB 1840
1490 RETURN
1500 REM ************
1510 REM BOTTOM LINE!
1520 PRINT:PRINT"YOU'RE BANKRUPT!!":SOUND 185,8
1530 FORZZ=1TO2500:NEXT
1540 PRINT:PRINT"OH THE SHAME OF IT!!":SOUND 103,8
1550 FORZZ=1TO2500:NEXT:SOUND 87,8
1560 PRINT:PRINT"STILL, YOU KEPT THE BUSINESS"
1570 PRINT"GOING FOR"WE"WEEKS"
1580 FORZZ=1TO2500:NEXT
1590 PRINT:PRINT"ENTER 'Y' FOR ANOTHER STINT"
1600 PRINT"AS CHAIRMAN OF THE BOARD..."
1610 PRINT"(OR 'N' IF YOU WANT TO QUIT)"
1620 A$=INKEY$
1630 IF A$<>"Y" AND A$<>"N" THEN 1620
1640 IF A$="Y" THEN RUN
1650 END
1660 REM **********
1670 REM INITIALISE
1690 FOR Z=1 TO RND(8)
1700 READ A$
1710 NEXT Z
1720 CA=500+RND(500)
1730 ST=100+RND(500)
1740 SP=10+RND(5)
1750 CO=7+RND(5)
1760 IF CO>SP THEN 1750
1770 WO=7+RND(10)
1780 WA=12+RND(SP)
1790 PR=5+RND(6)
1800 RC=100+RND(20)
1810 WE=0
1820 RE=1:REM RE IS SALES RESISTANCE FACTOR
1830 RETURN
1840 REM DELAY ROUTINE
1850 FORZZ=1TO25:ZZ$=INKEY$:NEXT:PRINT:PRINT"HIT space KEY";
1856 IF INKEY$<>" " THEN ZZ=RND(0):GOTO1856
1857 FORZZ=1TO45:PRINTCHR$(8);:NEXT:RETURN
1860 DATA "EPONYMS","BICYCLES","HARMONICAS"
1870 DATA "KAZOOS","LECTERNS","MOLESKINS"
1880 DATA "CARPET BAGS","PITH HELMETS","SKYHOOKS"
1890 DATA "BARBELLS"
1900 REM THIS GAME IS FROM THE
1910 REM VZ200 GIANT BOOK OF 
1920 REM GAMES
1930 REM MC-10 EDITS JIM GERRIE
