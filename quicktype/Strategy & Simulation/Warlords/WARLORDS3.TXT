0 CLEAR500:DIMCC,ZZ,M$,I$:MC=16384:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
20 DIM N$(4),A(4),S(4),MP(4),SY(4),FR(4),TR(4),IT(4),PT(4),NN(4),BG(4),NP(4),NR(4),NF(4)
21 GOSUB150
22 CLS:INPUT"NUMBER OF PLAYERS? (2-4)";NP:IFNP<2ORNP>4ORNP<>INT(NP)THEN22
23 TP=NP
24 FORI=1TONP:CLS
25 PRINT"ENTER PLAYER"STR$(I)"'S NAME";:INPUT N$(I):IFN$(I)=""THEN25
26 NEXT
27 Y=1:FORI=1 TO NP
28 A(I)=5000:TR(I)=2000
29 IT(I)=20:PT(I)=15
30 NN(I)=1:BG(I)=20000+RND(3)*1000+RND(1000):NP(I)=2000+RND(1100)
31 NEXT
32 CLS:PRINT"YEAR ";Y:PRINT:PRINT
33 WT=RND(5):ONWTGOTO34,35,36,34,35
34 PRINT"GOOD WEATHER -- GREAT HARVEST!":GOTO37
35 PRINT"AVERAGE YEAR -- FAIR HARVEST":GOTO37
36 PRINT"POOR WEATHER -- POOR HARVEST"
37 FORI=1 TO NP:ON WT GOTO38,39,40
38 BG(I)=BG(I)+((RND(2)+1)/2)*A(I):GOTO41
39 BG(I)=BG(I)+(((RND(2)+1)/2)*A(I))*.75:GOTO41
40 BG(I)=BG(I)+(((RND(2)+1)/2)*A(I))*.5
41 BG(I)=INT(BG(I)):NEXT
42 GOSUB138
43 FOR PN=1 TO NP:IF N$(PN)=CHR$(1) THEN127
44 CLS:PRINTN$(PN);"'S TURN.":PRINT:PRINT
45 M$="YOU HAVE"+STR$(BG(PN))+" BUSHELS OF GRAIN.":GOSUB1
46 NR(PN)=NP(PN)*(3+RND(2))
47 M$="THE PEOPLE REQUIRE"+STR$(NR(PN))+" BUSHELS OF GRAIN.":GOSUB1
48 PRINT"HOW MUCH DO YOU WANT TO FEED    THEM";:INPUTNF(PN)
49 IFNF(PN)>BG(PN)THENM$="YOU DON'T HAVE THAT MUCH GRAIN!":GOSUB1:FORTD=1TO3500:NEXT:GOTO44
50 CLS
51 PRINT"TREASURY: "RIGHT$("      "+STR$(INT(TR(PN))),6)" NOBLES:    "RIGHT$("   "+STR$(INT(NN(PN))),3)
52 PRINT"INCOME TAX: "RIGHT$("   "+STR$(INT(IT(PN))),3)"% PROP. TAX:"RIGHT$("   "+STR$(INT(PT(PN))),3)"%"
53 PRINT"SOLDIERS: "RIGHT$("      "+STR$(INT(S(PN))),6)" MARKETS:   "RIGHT$("   "+STR$(INT(MP(PN))),3)
54 PRINT"IRON MILLS:  "RIGHT$("   "+STR$(INT(FR(PN))),3)" SHIPYARDS: "RIGHT$("   "+STR$(INT(SY(PN))),3)
55 GOSUB500
57 K$=INKEY$:IFK$=""THEN57
58 K=VAL(K$):IFK<1ORK>7THEN57
59 ONKGOTO60,63,66,73,78,83,88
60 PRINT
61 INPUT"NEW INCOME TAX";IT(PN):IFIT(PN)<0ORIT(PN)>100ORIT(PN)<>INT(IT(PN))THENGOSUB670:GOTO61
62 GOTO50
63 PRINT
64 INPUT"NEW PROPERTY TAX";PT(PN):IFPT(PN)<0ORPT(PN)>100ORPT(PN)<>INT(PT(PN))THENGOSUB670:GOTO64
65 GOTO50
66 PRINT
67 IFS(PN)>=NN(PN)*20THEN680
68 INPUT"HOW MANY";NS:IFNS*20>TR(PN)THENGOSUB660:GOTO50
69 IFNS+S(PN)>NN(PN)*20THENGOSUB670:M$="YOU DON'T HAVE ENOUGH NOBLES TO LEAD THAT MANY.":GOSUB1:FORTD=1TO3500:NEXT:GOTO50
70 TR(PN)=TR(PN)-NS*20
71 S(PN)=S(PN)+NS
72 GOTO50
73 PRINT
74 IFTR(PN)<1000THENGOSUB660:GOTO50
75 TR(PN)=TR(PN)-1000
76 MP(PN)=MP(PN)+1
77 GOTO50
78 PRINT
79 IFTR(PN)<2000THENGOSUB660:GOTO50
80 TR(PN)=TR(PN)-2000
81 FR(PN)=FR(PN)+1
82 GOTO50
83 PRINT
84 IFTR(PN)<5000THENGOSUB660:GOTO50
85 TR(PN)=TR(PN)-5000
86 SY(PN)=SY(PN)+1
87 GOTO50
88 ON-(S(PN)<=0)GOTO110:CLS:M$="WOULD YOU LIKE TO FIGHT FOR LAND? (Y/N)":GOSUB1
89 K$=INKEY$:IFK$="N"THEN110
90 IFK$<>"Y"THEN89
91 CLS:PRINTTAB(10)" 0) BARBARIANS":IF Y<5 THEN 94
92 FORI=1TONP:ON-(I=PN OR N$(I)=CHR$(1))GOTO93:PRINTTAB(10)STR$(I)") ";N$(I)
93 NEXT
94 PRINT"WHO WOULD YOU LIKE TO FIGHT?"
95 K$=INKEY$:IFK$="" THEN95
96 K=VAL(K$):IFY<5ANDK<>0THEN95
97 IFK>NPORN$(K)=CHR$(1)ORK=PNTHEN95
98 AM=S(PN):DS=S(K)
99 IFK=0THENN$(K)="BARBARIANS":DS=RND(30)
100 IFDS=0THEN108
101 BC=1
102 PRINT@288,N$(PN);RIGHT$("    "+STR$(AM),4):PRINT@320,N$(K);:PRINTRIGHT$("    "+STR$(DS),4)
103 IFDS=0THEN600
104 IFAM=0THEN610
105 AM=AM-RND(2):DS=DS-RND(2):BC=BC+1:IF AM<0 THEN AM=0
106 IFDS<0THENDS=0
107 GOTO102
108 CLS:GOSUB620
109 IFTP=1THEN128
110 CLS:PRINT"YEAR END REPORT"
111 PRINT:IFNF(PN)>NR(PN)THEN116
112 IFABS(NF(PN)-NR(PN))=5000ORNF(PN)=0THEN630
113 IFN$(PN)=CHR$(1)THEN127
114 R=RND(200)+100:IFR>NP(PN)THENR=NP(PN)
115 NP(PN)=NP(PN)-R:M$=MID$(STR$(INT(R)),2)+" PEOPLE LEFT YOUR LANDS.":GOSUB1:GOTO120
116 R=RND(200)+100:NP(PN)=NP(PN)+R:M$=MID$(STR$(INT(R)),2)+" PEOPLE MIGRATED TO YOUR LANDS.":GOSUB1
117 IFIT(PN)+PT(PN)>100THEN640
118 IFN$(PN)=CHR$(1)THEN127
119 IFNF(PN)-NR(PN)>2000ANDRND(3)=RND(2)THENPRINT"YOU AQUIRED 1 NOBLE.":NN(PN)=NN(PN)+1
120 R=RND(50):M$=MID$(STR$(INT(R)),2)+" PEOPLE DIED OF DISEASE.":GOSUB1:NP(PN)=NP(PN)-R:IF NP(PN)<0THENNP(PN)=0
121 IFNP(PN)=0THEN650
122 IFN$(PN)=CHR$(1)THEN127
123 R=200+RND(300):TR(PN)=TR(PN)+R-(IT(PN)/100*R)
124 R=300+RND(300):TR(PN)=TR(PN)+R-(IT(PN)/100*R)
125 R=500+RND(300):TR(PN)=TR(PN)+R-(IT(PN)/100*R)
126 TR(PN)=TR(PN)+((IT(PN)+PT(PN))/100)*NP(PN)
127 BG(PN)=BG(PN)-NF(PN):GOSUB138:NEXT:Y=Y+1:GOTO32
128 FORI=1TONP:IFN$(I)<>CHR$(1)THEN130
129 NEXTI
130 FORTD=1TO3500:NEXT
131 CLS:PRINT"CONGRATULATIONS!"
132 PRINT:M$=N$(I)+" HAS NOW BEEN CROWNED EMPEROR OF ALL THE LANDS.":GOSUB1:PRINT:PRINT
133 PRINTTAB(8)"PLAY AGAIN? (Y/N)"
134 K$=INKEY$:IFK$=""THEN134
135 IFK$="Y"THENRUN
136 IFK$<>"N"THEN134
137 END
138 PRINT@484,;:M$="PRESS "+CHR$(91)+"ENTER"+CHR$(93)+" TO CONTINUE":GOSUB7
139 R=RND(0):IFINKEY$<>CHR$(13)THEN139
140 RETURN
150 CLS:PRINTTAB(12)"warlords":PRINT
200 M$="IN THIS STRATEGIC GAME, YOU ARE DESTINED TO RULE OVER THOUSANDS OF PEOPLE.":GOSUB1
210 M$="YOU WILL DETERMINE HOW MUCH TO FEED THEM, HOW BIG YOUR ARMY SHOULD BE AND HOW INDUSTRIALIZED YOUR LAND WILL BE."
211 GOSUB1
220 M$="THIS GAME IS INTENDED TO BE PLAYED BY FROM 2 TO 4 PLAYERS.":GOSUB1:GOSUB138:CLS
230 M$="THE OBJECT OF THIS GAME IS TO BE THE LAST REMAINING LEADER ALIVE.":GOSUB1
240 M$="WATCH OUT FOR ASSASSINATIONS. BE SURE TO FEED THE PEOPLE ENOUGH."
250 M$="YOU WILL ALSO CONTROL THE TAX RATES.":GOSUB1
260 M$="TAXES WILL BRING MONEY INTO THE TREASURY, WHICH CAN THEN BE USED TO BUY SOLDIERS, MARKETPLACES, "
261 M$=M$+"IRON MILLS, AND SHIPYARDS, WHICH WILL, IN TURN, PRODUCE MORE MONEY IN TAXES.":GOSUB1:GOSUB138:CLS
270 M$="THE AMOUNT OF GRAIN PRODUCED PER YEAR IS RELATED TO THE NUMBER OF ACRES OF LAND THAT YOU HAVE AND THE WEATHER, "
271 M$=M$+"IN GENERAL, FOR THAT YEAR.":GOSUB1
280 M$="NOTE ALSO THAT AS YOUR POPULATION INCREASES, YOU WILL NEED TO GAIN MORE LAND FOR FARMING.":GOSUB1
290 M$="THIS IS DONE BY DECLARING WAR ON EITHER THE BARBARIANS OR THE OTHER PLAYERS.":GOSUB1
300 M$="IF YOU'RE LUCKY, YOU CAN GAIN A SUBSTANTIAL AMOUNT OF LAND.":GOSUB1:GOSUB138:CLS
310 M$="ALSO NOTE THAT IF YOU HAVE NO SOLDIERS AND YOU ARE ATTACKED BY ANOTHER LAND, "
311 M$=M$+"YOU WILL BE DEFENSELESS AND YOUR LAND WILL BE LOST.":GOSUB1
320 M$="THERE WILL, HOWEVER, BE NO WARS ALLOWED BETWEEN OPPONENTS UNTIL THE FIFTH YEAR IN THE SIMULATION IS REACHED."
321 GOSUB1:GOSUB138:CLS
330 M$="IT SHOULD BE NOTED, HOWEVER, THAT THE SIZE OF YOUR ARMY IS LIMITED BY THE NUMBER OF NOBLES YOU HAVE IN YOUR LAND."
331 GOSUB1
340 M$="EACH NOBLE MAY LEAD UP TO 20 MEN IN THE ARMY.":GOSUB1
350 M$="FOR EXAMPLE, IF YOU HAVE 2 NOBLES, YOU MAY HAVE UP TO 40 MEN IN THE ARMY.":GOSUB1
360 M$="IT IS A WISE IDEA TO TRY TO ATTRACT NOBLES TO YOUR LAND BY FEEDING YOUR PEOPLE WELL.":GOSUB1
370 PRINT:PRINTTAB(11)"GOOD LUCK!":GOSUB138:RETURN
500 PRINTTAB(14)"menu"
501 PRINT"  1) CHANGE INCOME TAX"
502 PRINT"  2) CHANGE PROPERTY TAX"
503 PRINT"  3) BUY SOLDIERS ($20 EACH)"
504 PRINT"  4) BUY A MARKETPLACE ($1000)"
505 PRINT"  5) BUY AN IRON MILL  ($2000)"
506 PRINT"  6) BUY A SHIPYARD    ($5000)"
507 PRINT"  7) CONTINUE":RETURN
600 PRINT:PRINTN$(PN);" WINS -- ";:AG=BC*(10+RND(10)):M$=MID$(STR$(AG),2)+" ACRES GAINED.":GOSUB1:A(K)=A(K)-AG
601 A(PN)=A(PN)+AG:GOSUB138:S(K)=DS:S(PN)=AM:GOTO110
610 PRINT:M$=N$(K)+" FIGHTS OFF THE ATTACK":GOSUB1:AG=BC*(10+RND(2))
611 PRINT"-- LOSING ";AG;" ACRES.":A(K)=A(K)-AG:A(PN)=A(PN)+AG:GOSUB138:S(PN)=AM:S(K)=DS:GOTO110
620 M$=N$(K)+"'S LAND IS OVER-RUN!":GOSUB1:M$=N$(K)+" IS KNOCKED OUT OF THE GAME.":GOSUB1
621 A(PN)=A(PN)+A(K):N$(K)=CHR$(1):TP=TP-1:FORTD=1TO3000:NEXT:RETURN
630 M$=N$(PN)+" IS ASSASSINATED FOR STARVING HIS PEOPLE.":GOSUB1:N$(PN)=CHR$(1):TP=TP-1:FORTD=1TO3000:NEXT
631 IFTP=1THEN128
632 GOTO113
640 M$=N$(PN)+" IS HANGED FOR BEING A TYRANT WITH TOO HIGH TAXES.":GOSUB1:N$(PN)=CHR$(1):TP=TP-1:FORTD=1TO3000:NEXT
641 IFTP=1THEN128
642 GOTO118
650 M$="ALL YOUR PEOPLE ARE GONE. "+N$(PN)+" IS OUT OF THE GAME.":GOSUB1:N$(PN)=CHR$(1):TP=TP-1:FORTD=1TO3000:NEXT
651 IFTP=1THEN128
652 GOTO122
660 SOUND1,1:M$="YOU DON'T HAVE ENOUGH MONEY.":GOSUB1:FORTD=1TO3500:NEXT:RETURN
670 SOUND1,1:FORTD=1TO32:PRINTCHR$(8);:NEXT:RETURN
680 GOSUB670:M$="YOU MAY NOT BUY ANY MORE SOLDIERS UNTIL YOU GET MORE NOBLES.":GOSUB1:FORTD=1TO3500:NEXT:GOTO50
900 REM    COPYRIGHT (C)
910 REM  T&D SOFTWARE 1983
920 REM MC-10 EDITS J GERRIE
