0 CLEAR500:DIMCC,ZZ,M$,I$:MC=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-MC+ZZ,:RETURN

20 CLS(0)
30 XX=63
40 FORX=0TO31:POKEMC+X,36
70 SOUND190,1
80 NEXTX
100 FORX=0TO15:POKEX*32+31+MC,36
140 SOUND190,1
150 NEXTX
170 FORX=31TO0STEP-1:POKEX+MC+480,36
200 SOUND190,1
210 NEXTX
240 FORX=14TO0STEP-1:POKEX*32+MC,36
280 SOUND190,1
290 NEXTX
300 PRINT@174,"the";
310 PRINT@234,;:M$="STOCK MARKET":GOSUB7
320 PRINT@302,"game";
330 FORAQ=1TO1500:NEXTAQ
350 FORX=MCTO16895:POKEX,36:NEXT
420 PRINT@7*32+3,"DO YOU WANT INSTRUCTIONS";:INPUTA$:A$=LEFT$(A$,1):IFA$="Y"THEN430
421 GOTO660
430 CLS(3)
440 PRINT"OBJECTIVE OF THE GAME IS EASY-"
450 PRINT:PRINT"MAKE MONEY BY BUYING AND"
460 PRINT:PRINT"SELLING STOCKS."
470 PRINT@420,"PRESS ANY KEY TO CONTINUE";
480 I$=INKEY$:IFI$=""THEN480
481 GOTO490
490 CLS(3)
500 PRINT"THE STOCK PRICES ARE AFFECTED"
510 PRINT:PRINT"BY REAL LIFE SITUATIONS THAT"
520 PRINT:PRINT"ARE CREATED BY THE COMPUTER."
530 PRINT@420,"PRESS ANY KEY TO CONTINUE";
540 I$=INKEY$:IFI$=""THEN540
541 GOTO550
550 CLS(3)
560 PRINT"THE DOW JONES AVERAGE CHANGES"
570 PRINT:PRINT"AFTER EVERY TURN."
580 PRINT:PRINT"YOU MAY PLAY UNTIL YOU EARN AS"
590 PRINT:PRINT"MUCH AS YOU CAN."
600 PRINT@420,"PRESS ANY KEY TO CONTINUE";:I$=INKEY$:IFI$=""THEN600
601 GOTO610
610 CLS(3):PRINT"GOOD LUCK & GOOD EARNINGS"
620 PRINT:PRINT"BUT WATCH OUT FOR THE...":PRINT@234,"bear¯market";
630 PRINT@420,"PRESS ANY KEY TO CONTINUE";
640 I$=INKEY$:IFI$=""THEN640
641 GOTO650
650 CLS(7):PRINT"PLEASE ENTER y OR n TO ANSWER":PRINT:PRINT"THE COMPUTER. IT WILL SAVE TIME.":FORX=1TO2500:NEXTX
660 XX=149
670 AD=0
680 AD=0
690 DW=800
700 REM INITIALIZE VALUES AND STRINGS
710 X=14
720 DA=0
730 AC=10000
740 DIM S$(X):DIM BV(X):DIM SY$(X)
750 DIMSO(X):DIMCV(X)
760 FORT=1TOX
770 DATAAMTRACK,CAT TRACTORS,DOW CHEMICAL,DOUGLAS WEAPONS,EXXON OIL
780 DATAGENERAL FOODS,GENERAL MOTORS,IBM,LILY DRUGS,NY POWER
790 DATASEARS,UNITED AIRLINES,US STEEL,WARNER BROS.
800 READS$(T)
810 NEXTT
820 FORT=1TOX
830 DATA40,40,25,30,27,44,57,89,17,15,30,30,18,55
840 READBV(T)
850 NEXTT
860 FORT=1TOX
870 DATAAM,CT,DC,DW,EO,GF,GM,IB,LD,NP,SE,UA,US,WB
880 READSY$(T)
890 NEXTT
900 FORT=1TOX
910 DATA0,0,0,0,0,0,0,0,0,0,0,0,0,0
920 READSO(T)
930 NEXTT
940 FORT=1TOX 
950 READCV(T)
960 DATA40,40,25,30,27,44,57,89,17,15,30,30,18,55
970 NEXTT
980 CLS(6)
1020 INPUT"DO YOU WANT TO SEE YOUR         PORTFOLIO (Y/N)";P$
1030 IFP$="Y"THEN1390
1031 GOTO1040
1040 IFP$="Y"THEN1390
1080 CLS(3)
1120 REM BUY ROUTINE
1130 INPUT "DO YOU WANT TO BUY STOCKS";Y$
1140 IFY$="Y"THEN1150
1141 GOTO1490
1150 CLS(2)
1190 FORT=1TOX
1200 PRINTS$(T)TAB(20)SY$(T)TAB(24)"$"CV(T)
1210 NEXTT
1220 INPUT"WHICH STOCK (ENTER SYMBOL)";E$
1230 FORI=1TOX
1240 IFE$=SY$(I)THEN1280
1250 NEXTI
1260 CLS
1270 GOTO1130
1280 CLS(3)
1320 PRINT"HOW MANY SHARES OF                      "S$(I);:INPUTV
1330 AC=AC-V*(CV(I))-30
1340 IFAC<1THEN1850
1350 SO(I)=SO(I)+V
1360 V=0
1370 CLS
1380 GOTO1040
1390 CLS(0)
1430 PRINT"CASH"TAB(24)"$"AC
1440 FORT=1TOX
1450 IFSO(T)<1THEN1470
1460 PRINTS$(T)TAB(20)SO(T)TAB(26)"$"CV(T)
1470 NEXTT
1480 GOTO1130
1490 CLS(6)
1530 INPUT"DO YOU WANT TO SEE YOUR          PORTFOLIO (ENTER Y/N)";P$
1540 CLS(5)
1550 FORSD=100TO200STEP9
1560 SOUNDSD,1
1570 NEXTSD
1580 IFP$="Y"THEN2130
1590 REM SELL ROUTINE
1600 INPUT"DO YOU WANT TO SELL STOCKS";Y$
1610 IFY$<>"Y"THEN2250
1620 CLS
1660 FORT=1TOX
1670 PRINTS$(T)TAB(20)SY$(T)TAB(24)"$"CV(T)
1680 NEXTT
1690 SOUND120,10
1700 PRINT"WHICH STOCK TO SELL (SYMBOL)":INPUTE$
1710 FORI=1TOX
1720 IFE$=SY$(I)THEN1760
1730 NEXTI
1740 CLS(5)
1750 GOTO1600
1760 CLS(5)
1800 PRINT"HOW MANY SHARES OF                     "S$(I);:INPUTV
1810 AC=AC+V*(CV(I))-40
1820 SO(I)=SO(I)-V
1830 IFSO(I)<0THEN1990
1840 GOTO1490
1850 CLS(4)
1860 PRINT@0,"NOT ENOUGH  CASH TO BUY!"
1870 FORT=1TO750:NEXTT
1880 PRINT@64,"THIS IS ILLEGAL!"
1890 FORT=1TO750:NEXTT
1900 PRINT@128,"YOU ARE SENT TO PRISON!"
1910 FORT=1TO750:NEXTT
1920 PRINT@192,"YOU LOSE YOUR ASSETS, FRIENDS            AND THE GAME!"
1930 FORDN=1TO10
1940 FORSD=10TO100STEP5
1950 SOUNDSD,1
1960 NEXTSD
1970 NEXTDN
1980 END
1990 CLS(8)
2000 PRINT@0,"YOU SOLD STOCK THAT YOU          DON'T OWN!"
2010 FORT=1TO750:NEXTT
2020 PRINT@96,"IT'S ILLEGAL TO DO THAT!"
2030 FORT=1TO750:NEXTT
2040 PRINT@160,"YOU GO TO PRISON FOR            7 YEARS!"
2050 FORT=1TO750:NEXTT
2060 PRINT@288,"YOU LOSE YOUR ASSETS,           FRIENDS AND GAME!!"
2070 FORDN=1TO5
2080 FORSD=10TO100STEP3
2090 SOUNDSD,1
2100 NEXTSD
2110 NEXTDN
2120 END
2130 CLS(5)
2140 FORSD=100TO200STEP10
2150 SOUNDSD,1
2160 NEXTSD
2170 PRINT"CASH"TAB(24)"$"AC
2180 FORT=1TOX
2190 IFSO(T)<1THEN2210
2200 PRINTS$(T)TAB(20)SO(T)TAB(2)"$"CV(T)
2210 NEXTT
2220 PRINT"DO YOU WANT TO SELL STOCK?":INPUTY$
2230 IFY$="Y"THEN1620
2240 CLS(6)
2250 IFDD=1THEN2310
2260 DIMA$(XX):DIMCA(XX):DIMC(XX)
2270 FORT=1TOXX
2280 READA$(T),CA(T),C(T)
2290 NEXTT
2300 DD=1
2310 CLS(6)
2320 FORT=1TO3
2330 N=RND(XX):IFN=0THENN=N+1
2340 M$=A$(N):GOSUB1
2350 CV(CA(N))=CV(CA(N))+C(N)
2360 NEXTT
2370 REM AFFECT ENTIRE MARKET ROUTINE
2380 A=RND(3)
2390 IFA=0THENA=1
2400 IFA=1THEN2640
2410 IFA=2THEN2570
2420 PRINT@32*8+10,;:M$="MIXED MARKET":GOSUB7
2430 SOUND150,3
2440 M=RND(2)
2450 IFM=0THENM=1
2460 IFM=1THEN2520
2470 DW=DW+1
2480 FORT=1TOX
2490 CV(T)=CV(T)+1
2500 NEXTT
2510 GOTO2700
2520 FORT=2TOXSTEP2
2530 CV(T)=CV(T)-1
2540 NEXTT
2550 DW=DW-1
2560 GOTO2700
2570 PRINT@32*8+10,;:M$="BULL MARKET":GOSUB7
2580 SOUND200,5
2590 D=RND(3):DW=DW+5+D
2600 FORT=1TOX
2610 CV(T)=CV(T)+2
2620 NEXTT
2630 GOTO2700
2640 PRINT@32*8+10,;:M$="BEAR MARKET":GOSUB7
2650 SOUND50,5
2660 D=RND(4):DW=DW-4-D
2670 FORT=1TOX
2680 CV(T)=CV(T)-2
2690 NEXTT
2700 PRINT@360,;:M$="DOW JONES AT...":GOSUB7:PRINT@377,DW;:PRINT@449,"PRESS ANY KEY TO CONTINUE GAME";
2710 IFAC>999999THEN2720
2711 GOTO2790
2720 FORX=1TO4:CLS(X):PRINT@197,;:M$="YOU'RE A MILLIONAIRE":GOSUB7
2730 SOUND161,4:SOUND161,2:SOUND161,4:SOUND161,4:SOUND155,4:SOUND161,4:SOUND87,2:SOUND161,2
2770 NEXTX
2780 END
2790 FORT=1TOX
2800 IFCV(T)<1THENCV(T)=1
2810 NEXTT
2820 I$=INKEY$:IFI$=""THEN2820
2821 GOTO2830
2830 AD=0
2840 FORT=1TOX
2850 AD=AD+(SO(T)*CV(T))
2860 NEXTT
2870 AD=AD+AC
2880 DA=DA+1
2890 CLS(5)
2900 M$="YOUR ASSETS ARE  $"+STR$(AD):PRINT@128+(32-LEN(M$))/2,;:GOSUB7
2910 M$=STR$(DA)+" DAYS OF TRADING":PRINT@256+(32-LEN(M$))/2,;:GOSUB7
2915 PRINT@484,"PRESS ANY KEY TO CONTINUE";
2920 I$=INKEY$:IFI$=""THEN2920
2921 GOTO980
2930 DATATRAIN RIDERSHIP DOWN,1,-1
2940 DATADOCTORS DENOUNCE ASPIRIN,9,-1
2950 DATADEFENSE BUDGET INCREASES,4,+2
2960 DATASTEEL IMPORTS ARE UP,13,-3
2970 DATASTARWARS EARNINGS UP,14,2
2980 DATAARCADE GAMES SALES UP,14,2
2990 DATA"747 CRASHES. KILLS 200.",12,-2
3000 DATAAUTO SALES INCREASE,7,2
3010 DATAFARMERS SPENDING LESS,2,-2
3020 DATACHINA BUYS TRACTORS,2,1
3030 DATAMILITARY BUDGET ATTACKED,4,-1
3040 DATAMX MISSLE BUDGET DEFEATED,4,-2
3050 DATANUCLEAR PLANT SHUTS DOWN,10,-2
3060 DATAELECTRICITY USE UP,10,2
3070 DATARETAIL SALES DOWN,11,-2
3080 DATAHOME COMPUTER SALES UP,8,+2
3090 DATAAUTO WORKERS ON STRIKE,7,-1
3100 DATAAM BUILD "SUPERTRAIN",1,+1
3110 DATATRAIN RIDERSHIP UP,1,1
3120 DATAPCB IS BANNED,3,-2
3130 DATAGOVERNMENT OK'S PCB USE,3,+2
3140 DATANAPALM IS BANNED BY USA,3,-1
3150 DATASTEEL EXPORTS DOWN,13,-2
3160 DATASTEEL EXPORTS UP,13,2
3170 DATAAUTO IMPORTS UP,7,-2
3180 DATAAUTO SALES HIT SLUMP,7,-1
3190 DATAOIL GLUT EXISTS,5,-2
3200 DATAAUTO TRAVEL UP,5,+1
3210 DATACHRISTMAS SALES UP,11,+2
3220 DATAGEN FOODS EARNINGS DOWN,6,-2
3230 DATADOG FOOD SALES UP,6,1
3240 DATAUTILITY RATE INCREASE DENIED,10,-1
3250 DATAAIR TRAVEL INCREASES,12,2
3260 DATAIBM EARNINGS UP,8,2
3270 DATAJOHN DEERE SALES DOWN,2,1
3280 DATACHINA WANTS POWER PLANT,10,1
3290 DATARUSSIA NEEDS FARM MACHINES,2,1
3300 DATASPACE PROGRAM NEEDS COMPUTERS,8,1
3310 DATAWARNER DECREASE STOCK SHARES,14,2
3320 DATAMOVIE ATTENDANCE DOWN,14,-1
3330 DATAFROZEN FOODS SALES UP,6,1
3340 DATASAUDIS LOWER OIL PRICES,5,-2
3350 DATASAUDIS RAISE OIL PRICES,5,2
3360 DATAJAPAN LIMITS US AUTO IMPORTS,7,-1
3370 DATAGERMANY WANTS MISSLES,4,1
3380 DATANEW INDUSTRIES NEED EL.POWER,10,1
3390 DATAVIDEO PLAYERS SALES UP,14,+2
3400 DATAWARNER EARNINGS UP,14,2
3410 DATADOW INVENTS NONPOLLUTING SOAP,3,2
3420 DATADOW EARNINGS DOWN,3,-2
3430 DATALILY INVENTS NEW MEDICINE,9,+2
3440 DATADOCTORS DENOUNCE NEW MEDICINE,9,-1
3450 DATANEW DRUG CRIPPLES INFANT,9,-5
3460 DATAOVERCOUNTER DRUG SALES UP,9,2
3470 DATAGEN MILLS WANTS TO BUY GEN FOODS,6,3
3480 DATAGEN FOODS WON'T SELL TO G MILLS,6,-1
3490 DATAWHEAT SALES UP, 2,1
3500 DATAEL SALVADOR BUYS RIFLES,4,2
3510 DATAWAR BREAKS OUT IN MIDEAST,4,3
3520 DATACOMPUTER SALES UP,8,2
3530 DATAFOREIGN COMPUTER SALES UP,8,-2
3540 DATAOIL SHORTAGE CLAIMED,5,3
3550 DATAWB MUSICAL IS FLOP,14,-2
3560 DATAR&R RECORD SALES DOWN,14,-1
3570 DATAC&W RECORD SALES UP,14,1
3580 DATAR&R RECORD SALES UP,14,1
3590 DATAC&W RECORD SALES DOWN,14,-1
3600 DATAKEYSTONE PIPELINE TO BE BUILT,13,2
3610 DATANY HAS BLACKOUT,10,-1
3620 DATANY HAS BROWNOUT,10,-1
3630 DATANUCLEAR PLANT COSTS RISE,10,-1
3640 DATADOW SUED FOR FAULTY DRUG,3,-3
3650 DATANAPALM SALES TO S.E.A. UP,3,2
3660 DATADOW INVENTS NEW PLASTIC,3,2
3670 DATARETAIL HOLIDAY SALES OFF,11,-2
3680 DATAUA MUST PAY BACK TAXES,12,-1
3690 DATAUA INTRODUCES LOWER RATES,12,1
3700 DATANAVY NEEDS NEW SHIPS,13,1
3710 DATAIBM SALES INCREASE,8,2
3720 DATAGM OFFERS REBATE,7,1
3730 DATAGM OFFERS REBATE,7,0
3740 DATACONSUMER SPENDING DOWN,11,-1
3750 DATANEW DW JET IS FAULTY,4,-1
3760 DATANEW GF CEREAL SALES UP,6,2
3770 DATACHEMICAL EXPLOSION KILLS 123,3,-1
3780 DATALILY'S EARNINGS DOWN,9,-2
3790 DATALILY'S EARNINGS UP,9,2
3800 DATAWB'S NEW SF FILM IS SUCCESS,14,1
3810 DATASTEEL WORKERS STRIKE,13,-1
3820 DATAUA INCREASE FARES,12,1
3830 DATASEARS EARNINGS UP,11,1
3840 DATANY P&L BORROW $500MILLION,10,-1
3850 DATALILY EARNINGS UP,9,1
3860 DATAIBM BUYS OUT JAPAN COMPANY,8,1
3870 DATAGM ANNOUNCES REBATE,7,0
3880 DATAGEN FOODS EARNINGS LEVEL OFF,6,1
3890 DATASTANDARD OIL LOWERS PRICES,5,-1
3900 DATAMILITARY BUDGET CUT BACK,4,-2
3910 DATADOW INVENTS NEW GLASS,3,2
3920 DATAFARMERS RAISING PRICES,2,1
3930 DATATOURISM DECREASE,1,-2
3940 DATAAMTRACK MAY SELL OUT,1,-3
3950 DATACAT MAY FILE CHAPTER 11,2,-4
3960 DATADOW MAY BUY OUT DUPONT,3,0
3970 DATADOUGLAS WEAPONS ASKS FOR LOAN,4,-2
3980 DATAHOLIDAY TRAVEL UP,5,1
3990 DATAFAMILIES EAT AT HOME LESS,6,-1
4000 DATAAUTO SALES HIT SLUMP,7,-2
4010 DATAAUTO SALES SLIDE DOWN,7,-3
4020 DATAAUTO IMPORTS DECREASE LITTLE,7,1
4030 DATAIBM EARNINGS HIT NEW HIGH,8,3
4040 DATAIBM IMPROVES COMPUTERS,8,3
4050 DATACOMPUTER SALES DOWN,8,-2
4060 DATATYLENOL SCARE EXISTS,9,-2
4070 DATAELECTRIC RATES INCREASE,10,2
4080 DATACLOTHING SALES INCREASE,11,1
4090 DATARETAIL SALES INCREASE BY 30%,11,3
4100 DATAPRICE WAR DECREASES AIR FARE,12,0
4110 DATAINDUSTRIAL CONSTRUCTION UP,13,2
4120 DATAVIDEO DISK SALES DOWN,14,-2
4130 DATAIBM SALES OFF,8,-2
4140 DATAJAPANESE COMPUTER SALES UP,8,-2
4150 DATAHOME COMPUTER SALES DOWN,8,-1
4160 DATANEW WB TV SHOW FLOPS,14,-1
4170 DATALARGE BLOCK OF IBM SOLD,8,-10
4180 DATARAIL WORKERS STRIKE,1,-2
4190 DATADROUGHT HURTS CORN CROP,6,2
4200 DATABANK LOANS TO FARMERS DROP,2,-2
4210 DATADC MUST CLEAN DUMP SITE,3,-1
4220 DATAPAKISTAN ASKS US FOR ARMS,4,1
4230 DATAUS BLOCKS ARMS SALES TO SEA,4,-1
4240 DATAMIDEAST OIL PRICES DROP,5,-2
4250 DATAGM RECALLS '83 WAGONS,7,-2
4260 DATASMALL CAR SALES UP,7,2
4270 DATAFRANCE SELLS SUPER COMPUTER,8,-1
4280 DATALILY DISCOVERS NEW BIRTH CONTROL PILL,9,1
4290 DATALD BCP MAY CAUSE CANCER,9,-2
4300 DATALILY DISCOVERS NEW INDUSTRIAL SUPERGLUE,9,2
4310 DATAENVIRONMENTALISTS BLOCK NEW NYP DAM,10,-1
4320 DATAUTILITY RATES GET 10% HIKE OK'D,10,+2
4330 DATAJUMBO JET CRASH KILLS 232 IN LA,12,-1
4340 DATA5000 STEELWORKERS LAID OFF,13,1
4350 DATA2 MORE STEEL PLANTS CLOSE,13,-2
4360 DATASTEEL EXPORTS UP,8,2
4370 DATAJAPAN AGREES TO LIMIT STEEL EXPORTS TO US,8,2
4380 DATAWB SUED FOR COPYRIGHT INFRINGEMENT,14,-2
4390 DATASEARS AUTO SERVICE UP,11,1
4400 DATAIBM LOSES LAWSUIT FOR PIRATING  SOFTWARE,8,-3
4410 DATAAFRICAN NATIONS WANT NEW TRACTORS,2,3
4420 DATABASEBALL CHEWING GUM SALES UP,6,1
4430 DATANEW NATURAL CEREAL SALES UP,6,1
4440 DATACOLORED CANDY CEREAL SALES UP,6,1
4450 DATADENTIST DENOUNCE SUGAR CEREALS,6,-1
4460 DATACIVIL WAR ERUPTS IN CHILE,4,2
4470 DATABIG CAR SALES DOWN,7,-4
5000 REM ***BY  GARY  WICK ***
5010 REM MC-10 EDITS JIM GERRIE
