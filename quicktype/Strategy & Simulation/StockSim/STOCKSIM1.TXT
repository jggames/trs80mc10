
0 '****THE STOCKMARKET GAME****
1 '****BY  GARY  WICK ****
2 '****MADISON, WISC. ****
3 '****FOR PRIVATE USE****
10 'XX=SITUATIONS,X=#COMPANIES
20 CLS(0)
30 XX=63
40 FOR X=1 TO 32:
50 XX=XX+1
60 PRINT@XX,CHR$(36);:IF XX>=95 THEN 90: 
70 SOUND 190,1
80 NEXT X
90 XX=96
100 FOR X=1 TO 13
110 XX=XX+31+P
120 P=1
130 PRINT@XX,CHR$(36);:IF XX>=480 THEN 160
140 SOUND 190,1
150 NEXT X
160 XX=479
170 FOR X=1 TO 32
180 XX=XX+1
190 PRINT@XX,CHR$(36);:IFXX>=511 THEN 220
200 SOUND 190,1
210 NEXT X
220 XX=1
230 P=0
240 FOR X=1TO14
250 XX=XX+31+P
260 P=1
270 PRINT@XX,CHR$(36);:IF XX>=511 THEN 300
280 SOUND 190,1
290 NEXT X
300 PRINT@173,"the";
310 PRINT@234,"stockmarket";
320 PRINT@301,"game";
330 XX=-1
340 FORX=1 TO 500:NEXTX
350 FORX=1TO480
360 XX=XX+1
370 PRINT@XX,CHR$(36):IFXX>=511THEN400
380 SOUND 195,1
390 NEXTX
400 FORAQ=1TO200:NEXTAQ
410 CLS(0)
420 PRINT"do you want instructions";:INPUTA$:A$=LEFT$(A$,1):IF A$="Y" THEN GOTO430 ELSE660
430 CLS(3)
440 PRINT"OBJECTIVE OF THE GAME IS EASY-";
450 PRINT:PRINT"MAKE MONEY BY BUYING AND";
460 PRINT:PRINT"SELLING STOCKS.";
470 PRINT@420,"ENTER ANY KEY TO CONTINUE";
480 I$=INKEY$:IF I$="" THEN 480 ELSE 490
490 CLS(3)
500 PRINT"THE STOCK PRICES ARE AFFECTED";
510 PRINT:PRINT"BY REAL LIFE SITUATIONS THAT";
520 PRINT:PRINT"ARE CREATED BY THE COMPUTER.";
530 PRINT@420,"ENTER ANY KEY TO CONTINUE";
540 I$=INKEY$:IF I$=""THEN540 ELSE550
550 CLS(3)
560 PRINT"THE DOW JONES AVERAGE CHANGES";
570 PRINT:PRINT"AFTER EVERY TURN.";
580 PRINT:PRINT"YOU MAY PLAY UNTIL YOU EARN AS";
590 PRINT:PRINT"MUCH AS YOU CAN.";
600 PRINT@420,"ENTER ANY KEY TO CONTINUE";:I$=INKEY$:IFI$=""THEN 600 ELSE 610
610 CLS(3):PRINT"GOOD LUCK & GOOD EARNINGS";
620 PRINT:PRINT"BUT WATCH OUT FOR THE...";:PRINT@234,"bear market";
630 PRINT@420,"PRESS ANY KEY TO CONTINUE";
640 I$=INKEY$:IF I$=""THEN640 ELSE650
650 CLS(7):PRINT"PLEASE ENTER y OR n TO ANSWER":PRINT:PRINT"THE COMPUTER. IT WILL SAVE TIME.";:FORX=1 TO 1000: NEXT X
660 XX=149
670 AD=0
680 AD=0
690 DW=800
700 ' INITIALIZE VALUES AND STRINGS
710 X=14
720 DA=0
730 AC=10000
740 DIM S$(X):DIM BV(X):DIM SY$(X)
750 DIMSO(X):DIMCV(X)
760 FOR T=1 TO X
770 DATA AMTRACK,CAT TRACTORS,DOW CHEMICAL,DOUGLAS WEAPONS,EXXON OIL
780 DATA GENERAL FOODS,GENERAL MOTORS,IBM,LILY DRUGS,NY POWER
790 DATA SEARS,UNITED AIRLINES,US STEEL,WARNER BROS.
800 READ S$(T)
810 NEXT T
820 FOR T=1 TO X
830 DATA 40,40,25,30,27,44,57,89,17,15,30,30,18,55
840 READ BV(T)
850 NEXT T
860 FOR T=1 TO X
870 DATA AM,CT,DC,DW,EO,GF,GM,IB,LD,NP,SE,UA,US,WB
880 READ SY$(T)
890 NEXT T
900 FOR T=1 TO X
910 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0
920 READ SO(T)
930 NEXT T
940 FOR T=1 TO X 
950 READ CV(T)
960 DATA  40,40,25,30,27,44,57,89,17,15,30,30,18,55
970 NEXT T
980 CLS(6)
990 FOR SD=100TO200STEP10
1000 SOUND SD,1
1010 NEXT SD
1020 INPUT "DO YOU WANT TO SEE YOUR         PORTFOLIO (Y/N)";P$
1030 IF P$="Y" THEN  GOTO 1390 ELSE 1040
1040 FOR SD=100 TO 200 STEP 20
1050 SOUND SD,1
1060 NEXT SD
1070 IF P$="Y" THEN GOTO 1390
1080 CLS(3)
1090 FOR SD=100TO200STEP10
1100 SOUND SD,1
1110 NEXT SD
1120 'BUY ROUTINE"
1130 INPUT "DO YOU WANT TO BUY STOCKS";Y$
1140 IF Y$="Y" THEN GOTO 1150 ELSE GOTO 1490
1150 CLS(2)
1160 FOR SD=100TO200STEP5
1170 SOUND SD,1
1180 NEXT SD
1190 FOR T=1 TO X
1200 PRINT S$(T)TAB(20)SY$(T)TAB(24)"$"CV(T)
1210 NEXT T
1220 INPUT"WHICH STOCK (ENTER SYMBOL)";E$
1230 FOR I=1 TO X
1240 IF E$=SY$(I) THEN 1280
1250 NEXT I
1260 CLS
1270 GOTO 1130
1280 CLS(3)
1290 FOR SD=100TO200STEP8
1300 SOUND SD,1
1310 NEXT SD
1320 PRINT"HOW MANY SHARES OF                      "S$(I);:INPUT V
1330 AC=AC-V*(CV(I))-30
1340 IF AC<1 THEN GOTO 1850
1350 SO(I)=SO(I)+V
1360 V=0
1370 CLS
1380 GOTO 1040
1390 CLS(0)
1400 FOR SD=100TO200STEP7
1410 SOUND SD,1
1420 NEXT SD
1430 PRINT"CASH"TAB(24)"$"AC
1440 FOR T=1TOX
1450 IF SO(T)<1 THEN GOTO 1470
1460 PRINT S$(T)TAB(20)SO(T)TAB(26)"$"CV(T)
1470 NEXT T
1480 GOTO 1130
1490 CLS (6)
1500 FOR SD=100TO200STEP10
1510 SOUND SD,1
1520 NEXT SD
1530 INPUT"DO YOU WANT TO SEE YOUR          PORTFOLIO(ENTER Y/N)";P$
1540 CLS(5)
1550 FOR SD=100TO200STEP9
1560 SOUND SD,1
1570 NEXT SD
1580 IF P$="Y" THEN GOTO 2130
1590 'SELL ROUTINE
1600 INPUT"DO YOU WANT TO SELL STOCKS";Y$
1610 IF Y$<>"Y" THEN GOTO 2250
1620 CLS
1630 FOR SD=100TO200STEP10
1640 SOUND SD,1
1650 NEXT SD
1660 FOR T= 1 TO X
1670 PRINTS$(T)TAB(20)SY$(T)TAB(24)"$"CV(T)
1680 NEXT T
1690 SOUND 120,10
1700 PRINT"WHICH STOCK TO SELL (SYMBOL)":INPUT E$
1710 FOR I= 1 TO X
1720 IF E$=SY$(I) THEN 1760
1730 NEXT I
1740 CLS(5)
1750 GOTO 1600
1760 CLS(5)
1770 FOR SD=100TO200STEP10
1780 SOUND SD,1
1790 NEXT SD
1800 PRINT"HOW MANY SHARES OF                     "S$(I);:INPUTV
1810 AC=AC+V*(CV(I))-40
1820 SO(I)=SO(I)-V
1830 IF SO(I)<0 THEN GOTO 1990
1840 GOTO 1490
1850 CLS(4)
1860 PRINT @0, "NOT ENOUGH  CASH TO BUY!";
1870 FOR T=1TO150:NEXT T
1880 PRINT@64, "THIS IS ILLEGAL!";
1890 FOR T=1 TO 150:NEXT T
1900 PRINT @128, "YOU ARE SENT TO PRISON!";
1910 FOR T=1 TO 150:NEXT T
1920 PRINT@192, "YOU LOSE YOUR ASSETS, FRIENDS            AND THE GAME!";
1930 FOR DN=1 TO 10
1940 FOR SD=10TO100STEP5
1950 SOUND SD,1
1960 NEXT SD
1970 NEXT DN
1980 GOTO1980
1990 CLS(8)
2000 PRINT@0,"YOU SOLD STOCK THAT YOU          DON'T OWN!"
2010 FORT=1 TO 200:NEXT T
2020 PRINT@96,"IT'S ILLEGAL TO DO THAT!";
2030 FOR T=1 TO 200:NEXT T
2040 PRINT@160,"YOU GO TO PRISON FOR            7 YEARS!";
2050 FOR T=1 TO 200:NEXT T
2060 PRINT@288,"YOU LOSE YOUR ASSETS,           FRIENDS AND GAME!!";
2070 FOR DN=1 TO 5
2080 FOR SD= 10 TO 100 STEP 3
2090 SOUND SD,1
2100 NEXT SD
2110 NEXT DN
2120 GOTO2120
2130 CLS(5)
2140 FOR SD= 100 TO 200 STEP10
2150 SOUND SD,1
2160 NEXT SD
2170 PRINT"CASH"TAB(24)"$"AC
2180 FORT=1 TO X
2190 IF SO(T)<1 THEN GOTO 2210
2200 PRINT S$(T)TAB(20)SO(T)TAB(2)"$"CV(T)
2210 NEXT T
2220 PRINT"DO YOU WANT TO SELL STOCK?":INPUT Y$
2230 IF Y$="Y" THEN GOTO 1620
2240 CLS (6)
2250 IF DD=1 THEN GOTO2310
2260 DIMA$(XX):DIMCA(XX):DIMC(XX)
2270 FOR T=1 TO XX
2280 READ A$(T),CA(T),C(T)
2290 NEXT T
2300 DD=1
2310 CLS(6)
2320 FOR T= 1 TO 3
2330 N=RND(XX): IF N=0 THEN N= N+1
2340 PRINTA$(N)
2350 CV(CA(N))=CV(CA(N))+C(N)
2360 NEXT T
2370 'AFFECT ENTIRE MARKET ROUTINE
2380 A=RND(3)
2390 IF A=0 THEN A=1
2400 IF A=1 GOTO 2640
2410 IF A=2 THEN GOTO 2570
2420 PRINT"MIXED MARKET";
2430 SOUND 150,3
2440 M=RND(2)
2450 IF M=0 THEN M=1
2460 IF M=1 THEN GOTO 2520
2470 DW=DW+1
2480 FOR T=1 TO X
2490 CV(T)=CV(T)+1
2500 NEXT T
2510 GOTO 2700
2520 FOR T=2 TO X  STEP2
2530 CV(T)=CV(T)-1
2540 NEXT T
2550 DW=DW-1
2560 GOTO 2700
2570 PRINT"BULL MARKET";
2580 SOUND 200,5
2590 D=RND(3):DW=DW+5+D
2600 FOR T=1 TO X
2610 CV(T)=CV(T)+2
2620 NEXT T
2630 GOTO 2700
2640 PRINT"BEAR MARKET";
2650 SOUND 50,5
2660 D=RND(4):DW=DW-4-D
2670 FOR T= 1TO X
2680 CV(T)=CV(T)-2
2690 NEXT T
2700 PRINT@360,"dow jones AT...";:PRINT@377,DW;:PRINT@448,"ENTER ANY KEY TO CONTINUE GAME.";
2710 IF AC>999999 THEN GOTO 2720 ELSE GOTO 2790
2720 FORX=1TO4:CLS(X):PRINT@197,"YOU'RE A MILLIONAIRE";
2730 A$="T4;O4;L8;G;L4;G;L8;G;L8;G"
2740 B$="L8;F#;L8;G;L4;A;L2;G"
2750 C$="XA$;XB$;"
2760 PLAY C$
2770 NEXT X
2780 END
2790 FORT=1TOX
2800 IF CV(T)<1 THEN CV(T)=1
2810 NEXT T
2820 I$=INKEY$:IF I$="" THEN 2820 ELSE GOTO2830
2830 AD=0
2840 FOR T=1TOX
2850 AD=AD+(SO(T)*CV(T))
2860 NEXTT
2870 AD=AD+AC
2880 DA=DA+1
2890 CLS(5)
2900 PRINT@130,"YOUR ASSETS ARE  $"AD;
2910 PRINT@390,DA" DAYS OF TRADING.";
2920 I$=INKEY$:IF I$=""THEN GOTO2920 ELSE GOTO980
2930 DATA TRAIN RIDERSHIP DOWN,1,-1
2940 DATA DOCTORS DENOUNCE ASPIRIN,9,-1
2950 DATA DEFENSE BUDGET INCREASES,4,+2
2960 DATA STEEL IMPORTS ARE UP,13,-3
2970 DATA STARWARS EARNINGS UP,14,2
2980 DATA ARCADE GAMES SALES UP,14,2
2990 DATA 747 CRASHES. KILLS 200.,12,-2
3000 DATA AUTO SALES INCREASE,7,2
3010 DATA FARMERS SPENDING LESS,2,-2
3020 DATA CHINA BUYS TRACTORS,2,1
3030 DATA MILITARY BUDGET ATTACKED,4,-1
3040 DATA MX MISSLE BUDGET DEFEATED,4,-2
3050 DATA NUCLEAR PLANT SHUTS DOWN,10,-2
3060 DATA ELECTRICITY USE UP,10,2
3070 DATA RETAIL SALES DOWN,11,-2
3080 DATA HOME COMPUTER SALES UP,8,+2
3090 DATA AUTO WORKERS ON STRIKE,7,-1
3100 DATA AM BUILD "SUPERTRAIN",1,+1
3110 DATA TRAIN RIDERSHIP UP,1,1
3120 DATA PCB IS BANNED,3,-2
3130 DATA GOVERNMENT OK'S PCB USE,3,+2
3140 DATA NAPALM IS BANNED BY USA,3,-1
3150 DATA STEEL EXPORTS DOWN,13,-2
3160 DATA STEEL EXPORTS UP,13,2
3170 DATA AUTO IMPORTS UP,7,-2
3180 DATA AUTO SALES HIT SLUMP,7,-1
3190 DATA OIL GLUT EXISTS,5,-2
3200 DATA AUTO TRAVEL UP,5,+1
3210 DATA CHRISTMAS SALES UP,11,+2
3220 DATA GEN FOODS EARNINGS DOWN,6,-2
3230 DATA DOG FOOD SALES UP,6,1
3240 DATA UTILITY RATE INCREASE DENIED,10,-1
3250 DATA AIR TRAVEL INCREASES,12,2
3260 DATA IBM EARNINGS UP,8,2
3270 DATA JOHN DEERE SALES DOWN,2,1
3280 DATA CHINA WANTS POWER PLANT,10,1
3290 DATA RUSSIA NEEDS FARM MACHINES,2,1
3300 DATA SPACE PROGRAM NEEDS COMPUTERS,8,1
3310 DATA WARNER DECREASE STOCK SHARES,14,2
3320 DATA MOVIE ATTENDANCE DOWN,14,-1
3330 DATA FROZEN FOODS SALES UP,6,1
3340 DATA SAUDIS LOWER OIL PRICES,5,-2
3350 DATA SAUDIS RAISE OIL PRICES,5,2
3360 DATA JAPAN LIMITS US AUTO IMPORTS,7,-1
3370 DATA W.GERMANY WANTS MISSLES,4,1
3380 DATA NEW INDUSTRIES NEED EL.POWER,10,1
3390 DATA VIDEO PLAYERS SALES UP,14,+2
3400 DATA WARNER EARNINGS UP,14,2
3410 DATA DOW INVENTS NONPOLLUTANT SOAP,3,2
3420 DATA DOW EARNINGS DOWN,3,-2
3430 DATA LILY INVENTS NEW MEDICINE,9,+2
3440 DATA DOCTORS DENOUNCE NEW MEDICINE,9,-1
3450 DATA NEW DRUG CRIPPLES INFANT,9,-5
3460 DATA OVERCOUNTER DRUG SALES UP,9,2
3470 DATA GEN MILLS WANTS TO BUY GEN FOODS,6,3
3480 DATA GEN FOODS WON'T SELL TO G MILLS,6,-1
3490 DATA WHEAT SALES UP, 2,1
3500 DATA EL SALVADOR BUYS RIFLES,4,2
3510 DATA WAR BREAKS OUT IN MIDEAST,4,3
3520 DATA COMPUTER SALES UP,8,2
3530 DATA FOREIGN COMPUTER SALES UP,8,-2
3540 DATA OIL SHORTAGE CLAIMED,5,3
3550 DATA WB MUSICAL IS FLOP,14,-2
3560 DATA R&R RECORD SALES DOWN,14,-1
3570 DATA C&W RECORD SALES UP,14,1
3580 DATA R&R RECORD SALES UP,14,1
3590 DATA C&W RECORD SALES DOWN,14,-1
3600 DATA PIPELINE TO BE BUILT,13,2
3610 DATA NY HAS BLACKOUT,10,-1
3620 DATA NY HAS BROWNOUT,10,-1
3630 DATA NUCLEAR PLANT COSTS RISE,10,-1
3640 DATA DOW SUED FOR FAULTY DRUG,3,-3
3650 DATA NAPALM SALES TO S.E.A. UP,3,2
3660 DATA DOW INVENTS NEW PLASTIC,3,2
3670 DATA RETAIL HOLIDAY SALES OFF,11,-2
3680 DATA UA MUST PAY BACK TAXES,12,-1
3690 DATA UA INTRODUCES LOWER RATES,12,1
3700 DATA NAVY NEEDS NEW SHIPS,13,1
3710 DATA IBM SALES INCREASE,8,2
3720 DATA GM OFFERS REBATE,7,1
3730 DATA GM OFFERS REBATE,7,0
3740 DATA CONSUMER SPENDING DOWN,11,-1
3750 DATA NEW DW JET IS FAULTY,4,-1
3760 DATA NEW GF CEREAL SALES UP,6,2
3770 DATA CHEMICAL EXPLOSION KILLS 123,3,-1
3780 DATA LILY'S EARNINGS DOWN,9,-2
3790 DATA LILY'S EARNINGS UP,9,2
3800 DATA WB'S NEW SF FILM IS SUCCESS,14,1
3810 DATA STEEL WORKERS STRIKE,13,-1
3820 DATA UA INCREASE FARES,12,1
3830 DATA SEARS EARNINGS UP,11,1
3840 DATA NY P&L BORROW $500MILLION,10,-1
3850 DATA LILY EARNINGS UP,9,1
3860 DATA IBM BUYS OUT JAPAN COMPANY,8,1
3870 DATA GM ANNOUNCES REBATE,7,0
3880 DATA GEN FOODS EARNINGS LEVEL OFF,6,1
3890 DATA STANDARD OIL LOWERS PRICES,5,-1
3900 DATA MILITARY BUDGET CUT BACK,4,-2
3910 DATA DOW INVENTS NEW GLASS,3,2
3920 DATA FARMERS RAISING PRICES,2,1
3930 DATA TOURISM DECREASE,1,-2
3940 DATA AMTRACK MAY SELL OUT,1,-3
3950 DATA CAT MAY FILE CHAPTER 11,2,-4
3960 DATA DOW MAY BUY OUT DUPONT,3,0
3970 DATA DOUGLAS WEAPONS ASKS FOR LOAN,4,-2
3980 DATA HOLIDAY TRAVEL UP,5,1
3990 DATA FAMILIES EAT AT HOME LESS,6,-1
4000 DATA AUTO SALES HIT SLUMP,7,-2
4010 DATA AUTO SALES SLIDE DOWN,7,-3
4020 DATA AUTO IMPORTS DECREASE LITTLE,7,1
4030 DATA IBM EARNINGS HIT NEW HIGH,8,3
4040 DATA IBM IMPROVES COMPUTERS,8,3
4050 DATA COMPUTER SALES DOWN,8,-2
4060 DATA TYLENOL SCARE EXISTS,9,-2
4070 DATA ELECTRIC RATES INCREASE,10,2
4080 DATA CLOTHING SALES INCREASE,11,1
4090 DATA RETAIL SALES INCREASE BY 30%,11,3
4100 DATA PRICE WAR DECREASES AIR FARE,12,0
4110 DATA INDUSTRIAL CONSTRUCTION UP,13,2
4120 DATA VIDEO DISK SALES DOWN,14,-2
4130 DATA IBM SALES OFF,8,-2
4140 DATA JAPANESE COMPUTER SALES UP,8,-2
4150 DATA HOME COMPUTER SALES DOWN,8,-1
4160 DATA NEW WB TV SHOW FLOPS,14,-1
4170 DATA LARGE BLOCK OF IBM SOLD,8,-10
4180 DATA RAIL WORKERS STRIKE,1,-2
4190 DATA DROUGHT HURTS CORN CROP,6,2
4200 DATA BANK LOANS TO FARMERS DROP,2,-2
4210 DATA DC MUST CLEAN DUMP SITE,3,-1
4220 DATA PAKISTAN ASKS US FOR ARMS,4,1
4230 DATA US BLOCKS ARMS SALES TO SEA,4,-1
4240 DATA MIDEAST OIL PRICES DROP,5,-2
4250 DATA GM RECALLS '83 WAGONS,7,-2
4260 DATA SMALL CAR SALES UP,7,2
4270 DATA FRANCE SELLS SUPER COMPUTER,8,-1
4280 DATA LILY DISCOVERS NEW BIRTH        CONTROL PILL,9,1
4290 DATA LD BCP MAY CAUSE CANCER,9,-2
4300 DATA LILY DISCOVERS NEW INDUSTRIAL   SUPERGLUE,9,2
4310 DATA ENVIRONMENTALISTS BLOCK NEW     NYP DAM,10,-1
4320 DATA UTILITY RATES GET 10% HIKE OK'D,10,+2
4330 DATA JUMBO JET CRASH KILLS 232 IN LA,12,-1
4340 DATA 5000 STEELWORKERS LAID OFF,13,1
4350 DATA 2 MORE STEEL PLANTS CLOSE,13,-2
4360 DATA STEEL EXPORTS UP,8,2
4370 DATA JAPAN AGREES TO LIMIT STEEL     EXPORTS TO US,8,2
4380 DATA WB SUED FOR COPYRIGHT INFRINGE,14,-2
4390 DATA SEARS AUTO SERVICE UP,11,1
4400 DATA IBM LOSES LAWSUIT FOR PIRATING  SOFTWARE,8,-3
4410 DATA AFRICAN NATIONS WANT NEW TRACTORS,2,3
4420 DATA BASEBALL CHEWING GUM SALES UP,6,1
4430 DATA NEW NATURAL CEREAL SALES UP,6,1
4440 DATA COLORED CANDY CEREAL SALES UP,6,1
4450 DATA DENTIST DENOUNCE SUGAR CEREALS,6,-1
4460 DATA CIVIL WAR ERUPTS IN CHILE,4,2
4470 DATA BIG CAR SALES DOWN,7,-4
