20 CLS:DEFINTA-Z
30 PRINT@22,"E N G I N E E R":PRINT
40 PRINT"TO BECOME AN ENGINEER, YOU MUST CONSTRUCT A TRESTLE BRIDGE"
50 PRINT"CONNECTING THE TWO BLOCKS AT THE TOP OF THE SCREEN. THIS"
60 PRINT"IS DONE BY PLACING BEAMS IN ACCORDANCE WITH THE INSPECTOR'S"
70 PRINT"SPECIFICATIONS. JUST MOVE THE BLINKING GUIDE TO THE POSITION"
80 PRINT"YOU WISH TO PLACE THE BEAM, THEN ENTER THE DIRECTION YOU WANT"
90 PRINT"TO SET THE BEAM.YOUR STARTING POSITION MUST BE BELOW THE"
100 PRINT"INSPECTOR'S FEET."
110 PRINT
120 PRINT
130 PRINT"PRESS ENTER TO BEGIN.......";:INPUTZ$:CLS
140 PRINT@128,STRING$(4,191);:PRINT@188,STRING$(4,191);
150 REM SET CANYON
160 FORX=0TO127:SET(X,43):NEXT
170 Y=8:D=114:X=6
180 SET(X+2,Y):SET(X+3,Y):SET(X+D-2,Y):SET(X+D-3,Y)
190 SET(X,Y):SET(X+D,Y):SET(X+1,Y):SET(X+D-1,Y)
200 Y=Y+1:IFRND(9)>2THENX=X+1:D=D-2
210 IFY=43GOTO220ELSEGOTO180
220 X=45+RND(30):Y=42
230 GOSUB1030
240 M=RND(50)+40:N=0
250 PRINT@25,"E N G I N E E R";
260 IFPOINT(M,N+1)GOTO280
270 N=N+1:GOTO260
280 N=N-10:IFN<0THENN=0
290 SET(M,N):SET(M+1,N)
300 REM POSITION BEAM
310 PRINT@960,"MOVE GUIDE :  ";CHR$(93);" =LEFT   ";CHR$(94);" =RIGHT  ";CHR$(92);" =DOWN";
315 FORZZ=16437TO16444:POKEZZ,0:NEXT
320 C$=INKEY$:IFC$=""RESET(M,N):RESET(M+1,N):GOTO290
330 IFASC(C$)=9GOTO380
340 IFASC(C$)=8GOTO400
350 IFASC(C$)=10GOTO420
360 GOTO290
370 REM SET BEAM
380 IFPOINT(M+2,N)GOTO440
390 RESET(M,N):RESET(M+1,N):M=M+2:GOTO290
400 IFPOINT(M-1,N)GOTO440
410 RESET(M,N):RESET(M+1,N):M=M-2:GOTO290
420 IFPOINT(M,N+1)GOTO440
430 RESET(M,N):RESET(M+1,N):N=N+1:GOTO290
440 PRINT@960,CHR$(31);:A$=INKEY$
450 IFN<YPRINT@960,"STICK MUST START BELOW INSPECTOR";:RESET(M+1,N):RESET(M,N):FORI=1TO1000:NEXT:GOTO240
460 PRINT@960,"PRESS DIRECTION TO SET BEAM  (1-3) ";
470 PRINT@761,"1   2";:PRINT@825,CHR$(140);"    3";:PRINT@887,"DIR.";
480 C$=INKEY$:IFC$=""THEN480ELSED=VAL(C$)
490 IFD<1ORD>3GOTO440
500 PRINT@761,"    ";:PRINT@825,"       ";:PRINT@886,"      ";
510 DA=DA+1:M1=M:N1=N
520 FORI=1TO18
530 IFM>122ORN<4ORM<2GOTO620
540 SET(M,N):SET(M+1,N):SET(M+2,N)
545 IFD=1GOTO560
550 IFPOINT(M+3,N)THEN770
560 ONDGOTO570,580,590,600
570 I=I+RND(2):N=N-1:I1=I1+1:GOTO610
580 M=M+2:N=N-1:GOTO610
590 M=M+2:GOTO610
600 M=M+2:N=N+1
610 NEXTI
620 IFPOINT(M+2,N-1)=1ORD=1GOTO770
630 PRINT@960,CHR$(31);:M=M1:N=N1
640 REM ILLEGAL PLACEMENT
650 PRINT@960,"THE RIGHT END OF THE BEAM MUST BE SUPPORTED !";
660 FORI=1TO666:NEXT
670 FORI=1TO18:RESET(M,N):RESET(M+1,N):RESET(M+2,N)
680 ONDGOTO690,700,710,720
690 N=N-1:GOTO730
700 M=M+2:N=N-1:GOTO730
710 M=M+2:GOTO730
720 M=M+2:N=N+1
730 IFN<2GOTO750
740 NEXTI
750 PRINT@832,"DAY ";DA;:PRINT@960,CHR$(31);
760 GOTO240
770 PRINT@960,CHR$(31);
780 REM MOVE INSPECTOR
790 PRINT@960,"INSPECTION.  .  .  ";
800 IFW=0THENW=1ELSEW=0
810 FORI=1TORND(40)+10
820 GOSUB1050
830 IFW=1GOTO880
840 IFPOINT(X-2,Y+1)=0ANDPOINT(X,Y+1)=0ANDPOINT(X+2,Y+1)=0THENY=Y+1:GOTO910
850 IFPOINT(X-1,Y)=0THENX=X-1:GOTO910
860 IFPOINT(X,Y-1)=0THENY=Y-1:GOTO910
870 GOTO910
880 IFPOINT(X,Y+1)=0ANDPOINT(X+3,Y+1)=0THENY=Y+1:GOTO910
890 IFPOINT(X+3,Y)=0THENX=X+1:GOTO910
900 IFPOINT(X,Y-1)=0THENY=Y-1:GOTO910
910 GOSUB1030:IFY<6ORX<8ORX>122GOTO930
920 NEXTI
930 PRINT@960,CHR$(31);
940 PRINT@832,"DAY  ";DA;
950 REM CHECK FOR COMP. BRIDGE
960 FORI=15TO155STEP5:FORJ=4TO8
970 IFPOINT(I,J)GOTO1010
980 NEXTJ:GOTO240
990 NEXTI
1000 PRINT@960,CHR$(31);
1010 PRINT@960,"YOU HAVE FINISHED !!        PRESS ENTER FOR A NEW GAME";:INPUTZ$:RUN
1020 REM SET INSPECTOR
1030 SET(X,Y):SET(X+1,Y-1):SET(X+2,Y):SET(X+1,Y-3)
1040 SET(X,Y-2):SET(X+1,Y-2):SET(X+2,Y-2):RETURN
1050 RESET(X,Y):RESET(X+1,Y-1):RESET(X+2,Y):RESET(X+1,Y-3)
1060 RESET(X,Y-2):RESET(X+1,Y-2):RESET(X+2,Y-2):RETURN
1070 END

