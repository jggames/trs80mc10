0 CLEAR700:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
9 REM MASTER ROUTINE
10 CLS
11 W$="MEN              TANKS            FIELD GUNS       MISSILE LAUNCHERSBIOLOGICAL       CHEMICAL         NUCLEAR          "
20 GOSUB 1000:REM INITIALIZE
30 GOSUB 1500:REM INTRO
40 D=1
50 GOSUB 2000:REM MENU
60 GOSUB 4000:REM ENEMY ACTION
70 GOSUB 4500:REM SITUATION CHANGE
80 GOTO 5000:REM END CHECK
90 D=D+1
100 GOTO 50
997 REM
998 REM
999 REM INITIALIZE
1000 DIM P(6,7)
1010 DIM E(6,7)
1020 DIM F(5)
1030 DIM G(5)
1040 FOR X=1 TO 4
1050 READ I
1060 P(6,X)=I
1070 E(6,X)=I
1080 NEXT X
1090 RETURN
1099 REM
1100 DATA 100000,400,500,150
1497 REM
1498 REM
1499 REM INTRO
1500 M$="THIRD WORLD WAR":GOSUB7:PRINT
1510 PRINT
1520 M$="YOU ARE THE COMMANDER OF NATO FORCES IN EUROPE AT THE OUTBREAK OF WAR.":GOSUB1
1540 M$="YOU DIRECT LAND FORCES BETWEEN THE RESERVES AND THE 5 FRONT-LINE SECTORS.":GOSUB1
1560 M$="YOU CANNOT SEND FORCES BETWEEN SECTORS DIRECTLY.":GOSUB1
1580 PRINT:GOSUB9900
1590 M$="IF EITHER SIDE PENETRATES TO A DEPTH OF 100 KM INSIDE ENEMY TERRITORY WITH SUFFICIENT FORCE, "
1600 M$=M$+"THE BATTLE IS OVER.":GOSUB1
1620 PRINT
1630 M$="IT IS ALSO POSSIBLE TO START AN ALL-OUT NUCLEAR WAR, DESTROYING THE WHOLE WORLD "
1650 M$=M$+"IF YOU ARE NOT CAREFUL.":GOSUB1
1660 GOSUB 9900
1670 RETURN
1997 REM
1998 REM
1999 REM MENU
2000 CLS
2010 PRINT"DAY";D;"OF THE BATTLE"
2020 PRINT
2030 M$="OPTIONS:":GOSUB7:PRINT
2040 PRINT"1. DISPLAY SECTOR SITUATION."
2050 PRINT"2. GIVE MOVEMENT ORDERS."
2060 PRINT"3. GIVE ESCALATION ORDERS."
2070 PRINT"4. STRATEGIC MAP."
2075 PRINT"5. END OF DAY'S ORDERS."
2080 M$="WHICH ONE":GOSUB7
2090 INPUT A
2100 A=INT(ABS(A))
2110 IF A>5 THENA=5
2120 IF A<1 THENA=1
2130 IF A=5 THEN RETURN
2135 IF A=4 THEN GOSUB 8000:GOTO2230
2140 IF A=2 THEN M$="TO WHICH"
2150 IF A<>2 THEN M$="WHICH"
2160 M$=M$+" SECTOR?":GOSUB1:PRINT"(6 FOR RESERVES)";
2170 INPUT S
2180 S=ABS(INT(S))
2190 IF S>6 OR S<1 THEN RETURN
2200 IF A=1 THEN GOSUB 2500
2210 IF A=2 THEN GOSUB 3000
2220 IF A=3 THEN GOSUB 3500
2230 GOSUB 9900
2240 GOTO 2000
2498 REM
2499 REM SITUATION DISPLAY
2500 CLS
2510 IF S<6 THEN M$="SECTOR"+STR$(S):GOSUB7:PRINT
2520 IF S=6 THEN PRINT"reserves"
2530 PRINT
2540 REM 
2550 FOR X=1 TO 4
2560 I$=MID$(W$,(X-1)*17+1,17)
2570 PRINT I$;TAB(20);P(S,X)
2580 NEXT X 
2590 IF S=6 THEN RETURN
2600 PRINT"PROPORTION OF SPECIAL SHELLS:"
2610 FOR X=5 TO 7
2620 I$=MID$(W$,(X-1)*17+1,17)
2630 PRINT I$;TAB(20);STR$(P(S,X));"%"
2640 NEXT X
2650 PRINT
2660 M$="THE FRONT LINE HAS "
2670 IF F(S)>=0 THEN M$=M$+"ADVANCED"
2680 IF F(S)<0 THEN M$=M$+"RETREATED"
2690 M$=M$+STR$(ABS(F(S)))+" KM ALTOGETHER."
2700 GOSUB1
2710 RETURN
2999 REM MOVEMENT ORDERS
3000 IF S=6 THEN GOSUB 3300
3010 PRINT
3020 PRINT"1. MEN"
3030 PRINT"2. TANKS"
3040 PRINT"3. FIELD GUNS"
3050 PRINT"4. MISSILE LAUNCHERS"
3060 M$="WHICH ONE":GOSUB7
3070 INPUT E
3080 E=INT(ABS(E))
3090 IF E>4 OR E<1 THEN E=1
3100 PRINT"HOW MANY";
3110 INPUT Q
3120 IF S<6 THENT=6
3130 IF Q>P(T,E) THEN Q=P(T,E)
3140 P(S,E)=P(S,E)+Q
3150 P(T,E)=P(T,E)-Q
3160 RETURN
3299 REM
3300 M$="FROM WHICH SECTOR":GOSUB7
3310 INPUT T
3320 T=INT(ABS(T))
3330 IF T>5 OR T<1 THEN T=6
3340 RETURN
3498 REM
3499 REM ESCALATION
3500 PRINT
3510 PRINT"1. BIOLOGICAL"
3520 PRINT"2. CHEMICAL"
3530 PRINT"3. NUCLEAR"
3540 M$="WHICH TYPE":GOSUB7
3550 INPUT T
3560 IF T>3 OR T<1 OR T<>ABS(T) THEN RETURN
3570 M$="TO WHAT % OF SHELLS FIRED?":GOSUB1
3580 INPUT Q
3590 Q=ABS(INT(Q))
3600 IF Q>30 THENQ=30
3610 P(S,4+T)=Q
3620 RETURN
3997 REM
3998 REM
3999 REM ENEMY ACTION
4000 FC=0:MC=0:TD=0
4010 FOR X=1 TO 5
4020 TD=TD+ABS(G(X)-F(X))
4030 FC=FC+P(X,1)
4040 MC=MC+P(X,2)
4050 FOR Y=1 TO 4
4060 E(X,Y)=INT(E(X,Y)/2)
4070 E(6,Y)=E(6,Y)+E(X,Y)
4080 NEXT Y
4090 NEXT X
4100 FOR X=1 TO 5
4110 FOR Y=1 TO 2
4120 C=INT(ABS(G(X)-F(X)+1)/(TD+5)*E(6,Y))
4130 E(X,Y)=E(X,Y)+C
4140 NEXT Y
4150 C=INT((P(X,1)+1)/(FC+5)*E(6,3))
4160 E(X,3)=E(X,3)+C
4170 C=INT((P(X,2)+1)/(MC+5)*E(6,4))
4180 E(X,4)=E(X,4)+C
4190 FOR Y=5 TO 7
4200 E(X,Y)=P(X,Y)+INT((RND(0)-RND(0))*5)
4210 IF P(X,1)>E(X,1) AND E(X,1)>0 THEN E(X,Y)=20+INT(RND(0)*10)
4220 IF E(X,Y)>30 THEN E(X,Y)=30
4230 IF E(X,Y)<0 THEN E(X,Y)=0
4240 NEXT Y
4250 NEXT X
4260 FOR Y=1 TO 4
4270 E(6,Y)=0
4280 NEXT Y
4290 RETURN
4497 REM
4498 REM
4499 REM SITUATION CHANGE
4500 FOR X=1 TO 5
4510 CLS
4520 DP=E(X,5)+E(X,6)+E(X,7)
4530 DE=P(X,5)+P(X,6)+P(X,7)
4540 FOR Y=1 TO 4
4550 P(X,Y)=INT(P(X,Y)*(1-DP/300))
4560 E(X,Y)=INT(E(X,Y)*(1-DE/300))
4570 NEXT Y
4580 RESTORE:READCC:READCC:READCC:READCC
4590 FOR Y=1 TO 4
4600 FOR Z=1 TO 4
4610 READ I
4620 P(X,Y)=INT(P(X,Y)-I*E(X,Z))
4630 IF P(X,Y)<0 THEN P(X,Y)=0
4640 E(X,Y)=INT(E(X,Y)-I*P(X,Z))
4650 IF E(X,Y)<0 THEN E(X,Y)=0
4660 NEXT Z
4670 NEXT Y
4680 G(X)=F(X)
4690 F(X)=INT(F(X)+(P(X,1)+P(X,2)*5-E(X,1)-E(X,2)*5)/2E3*(1+(RND(0)-RND(0))/5))
4700 REM
4710 M$="SECTOR"+STR$(X):GOSUB7:PRINT
4720 PRINT
4730 PRINT"REPORTS SHOW THE ENEMY HAVE"
4740 FOR Y=1 TO 4
4750 IF P(X,Y)<0 THEN P(X,Y)=0
4760 IF E(X,Y)<0 THEN E(X,Y)=0
4770 I$=MID$(W$,(Y-1)*17+1,17)
4780 PRINTI$;TAB(20);E(X,Y)
4790 NEXT Y
4800 PRINT"IN THE AREA."
4810 M$="THE FRONT LINE HAS "
4820 IF F(X)-G(X)>=0 THEN M$=M$+"ADVANCED"
4830 IF F(X)-G(X)<0 THEN M$=M$+"RETREATED"
4840 M$=M$+" BY"+STR$(ABS(F(X)-G(X)))+" KM":GOSUB1
4850 PRINT
4860 PRINT"PROPORTION OF SPECIAL SHELLS:"
4870 FOR Y=5 TO 7
4880 I$=MID$(W$,(Y-1)*17+1,17)
4890 PRINTI$;TAB(20);STR$(E(X,Y));"%"
4900 NEXT Y
4910 GOSUB 9900
4920 NEXT X
4930 RETURN
4939 REM
4940 DATA .1,.3,.5,.3
4950 DATA 1E-3,.1,.1,.4
4960 DATA .01,.2,.2,.3
4970 DATA .1,.2,.2,.3
4997 REM
4998 REM
4999 REM END CHECK
5000 CLS
5010 FOR X=1 TO 5
5020 IF P(X,7)+E(X,7)>=50 AND RND(0)<.4 THEN 5500
5030 IF F(X)>100 THEN 6000
5040 IF F(X)<-100 THEN 6500
5050 NEXT X
5060 GOTO 90
5497 REM
5498 REM
5499 REM NUCLEAR HOLOCAUST
5500 M$="A NUCLEAR HOLOCAUST HAS BROKEN OUT. THE WORLD HAS BEEN DESTROYED"
5520 GOTO 7000
5997 REM
5998 REM
5999 REM VICTORY
6000 M$="YOU PENETRATED 100 KM INSIDE ENEMY TERRITORY IN SECTOR"+STR$(X)+". "
6020 M$=M$+"THE ENEMY SURRENDERED"
6030 GOTO 7000
6497 REM
6498 REM
6499 REM LOSS
6500 M$="THE ENEMY PENETRATED 100 KM INSIDE YOUR TERRITORY IN SECTOR"+STR$(X)+".":GOSUB1
6510 M$="YOU LOST"
6520 GOTO 7000
6997 REM
6998 REM
6999 REM REPLAY
7000 M$=M$+" ON DAY"+STR$(D)+".":GOSUB1
7010 PRINT
7020 PRINT"WANT ANOTHER GAME";
7030 INPUT M$
7040 IF LEFT$(M$,1)="Y" THEN RUN
7050 END
8000 CLS0
8020 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
8030 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
8040 PRINT"¯¯¯¯¯¯¯¯¯¯¯ŒŒ¯¯¯¯¯¯¯¯¯¯¯";
8050 PRINT"¯¯¯¯Ž‰Š¯¯¯¯¯„";
8060 PRINT"¯¯¯¯¯¯¯¯†‹……Š";
8070 PRINT"¯¯¯¯¯ƒ…„ŒŒŒ„Š";
8080 PRINT"¯¯¯¯¯¯‹ŒŒ…ŒŒƒƒƒƒ‡";
8090 PRINT"¯¯¯¯¯¯‰Œƒƒ‚ƒƒƒƒ†ŒŒƒƒ†";
8100 PRINT"¯¯¯¯¯¯Š‰ƒ†ŒŒƒ†ŒŽ‡†¯";
8110 PRINT"¯†Š…ƒ†¯";
8120 PRINT"¯ƒ†‹ƒ¯¯¯¯¯¯ƒƒƒƒƒƒ¯";
8130 PRINT"¯Š¯¯¯¯¯¯¯¯Š¯";
8140 FORS=1TO5:PRINT@64+32*S+15+INT(F(S)/12),"ß¿";:NEXT
8200 RETURN
9897 REM
9898 REM
9899 REM KEY PRESS ROUTINE
9900 PRINT@480,;
9910 M$="PRESS A KEY TO CONTINUE":GOSUB7
9920 M$=INKEY$:IFM$=""THEN9920
9930 CLS
9940 RETURN
