0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 CLS:PRINT@256:PRINTTAB(14)"camel":PRINT
20 PRINTTAB(7)"CREATIVE COMPUTING"
30 PRINTTAB(5)"MORRISTOWN, NEW JERSEY"
35 PRINT:PRINT:PRINT:PRINT:PRINT
110 PRINT"WOULD YOU LIKE INSTRUCTIONS";
120 INPUT D$
130 IF LEFT$(D$,1)="N" THEN 320
140 CLS:PRINTTAB(8)"WELCOME TO CAMEL":PRINT
150 M$="THE OBJECT IS TO TRAVEL 200 MILES ACROSS THE GREAT GOBI DESERT.":GOSUB1:PRINT
160 M$="A TRIBE OF KNOCKED-KNEED PYGMIES WILL BE CHASING YOU.":GOSUB1:PRINT
170 M$="YOU WILL BE ASKED FOR COMMANDS EVERY SO OFTEN.":GOSUB1:PRINT
180 GOSUB190:GOSUB200:GOSUB190:GOTO280
190 PRINT:INPUT"HIT enter TO CONTINUE";M$:FORCC=1TO32:PRINTCHR$(8);:NEXT:RETURN

200 CLS:PRINT"C O M M A N D S :   Ž"
201 PRINT"                Œ€€"
202 PRINT"1. DRINK FROM   ‹€ƒ€€…"
203 PRINT"   YOUR CANTEEN  …€€"
204 PRINT"2. AHEAD MODERATE…Š€…ŽŒ"
205 PRINT"   SPEED         …€€„Žˆ€€€Œ"
206 PRINT"3. AHEAD FULL    …‚€€€€€€€€€"
207 PRINT"   SPEED         ‹ˆ€€€€€€€€€€"
208 PRINT"4. STOP FOR THE ‚€€€€€€€€€‚"
209 PRINT"   NIGHT         ‹€€‡Š€€"
210 PRINT"5. STATUS CHECK  €…€"
211 PRINT"6. HOPE FOR HELP ‚…‚…"
212 PRINT"                 ˆ…ˆ…":RETURN

280 M$="YOU HAVE ONE QUART OF WATER WHICH WILL LAST YOU SIX DRINKS.":GOSUB1
290 M$="YOU MAY RENEW YOUR WATER SUPPLY COMPLETELY AT AN OASIS.":GOSUB1
300 M$="YOU GET A HALF A QUART IF FOUND BY HELP.":GOSUB1:GOSUB190
310 M$="IF HELP DOES NOT FIND YOU AFTER COMMAND SIX, YOU LOSE.":GOSUB1
320 M$="GOOD LUCK AND GOOD CAMELING!":GOSUB1
330 M$="YOU ARE IN THE MIDDLE OF THE DESERT AT AN OASIS.":GOSUB1:GOSUB190
335 GOSUB 2000
340 IF C>199 THEN 1210
350 Z=Z-1
355 IF Z=1 THEN PRINT"---W A R N I N G--- GET A DRINK"
360 IF Z<0 THEN 1630
370 P=P+1
380 X2=INT(10*RND(0)+2.5)
390 IF Q>0 THEN 940
400 IF P<4 THEN 470
410 C1=C1+X2
420 IF C1<C THEN 460
430 M$="THE PYGMIES HAVE CAPTURED YOU. CAMEL AND PEOPLE SOUP IS THEIR FAVORITE DISH!!!":GOSUB1
450 GOTO 1560
460 M$="THE PYGMIES ARE"+STR$(C-C1)+" MILES BEHIND YOU.":GOSUB1
470 M$="YOU HAVE TRAVELLED"+STR$(C)+" MILES ALLTOGETHER.":GOSUB1
480 PRINT"WHAT IS YOUR COMMAND";
490 INPUT Y
500 ON Y GOTO 830,610,680,760,790,550:GOSUB200:GOTO480
550 T=INT(10*RND(0))
560 IF T<>1 THEN 1200
570 M$="HELP HAS FOUND YOU IN A STATE OF UNCONSCIOUSNESS.":GOSUB1
580 S=3
590 Z=4
600 GOTO 340
610 F=F+1
620 IF F=8 THEN 1190
630 GOSUB 880
640 X1=INT(10*RND(0))
650 C=C+X1
660 M$="YOUR CAMEL LIKES THIS PACE.":GOSUB1
670 GOTO 340
680 F=F+3
690 IF F>7 THEN 1190
700 GOSUB 880
710 X1=2*INT(10*RND(0))
720 C=C+X1
730 M$="YOUR CAMEL IS BURNING ACROSS THE DESERT SANDS.":GOSUB1
740 PRINT
750 GOTO 340
760 M$="YOUR CAMEL THANKS YOU!":GOSUB1
770 F=0
780 GOTO 350
790 M$="YOUR CAMEL HAS"+STR$(7-F)+" GOOD DAYS LEFT.":GOSUB1
800 M$="YOU HAVE"+STR$(S)+" DRINKS LEFT IN YOUR CANTEEN.":GOSUB1
810 M$="YOU CAN GO"+STR$(Z)+" COMMANDS WITHOUT DRINKING.":GOSUB1
830 S=S-1
840 IF S<0 THEN 1200
850 M$="BETTER WATCH FOR AN OASIS!":GOSUB1
860 Z=4
870 GOTO 480
880 A=INT(100*RND(0))
890 IF A>5 THEN 1120
900 M$="WILD BERBERS HIDDEN IN THE SAND HAVE CAPTURED YOU.":GOSUB1
910 M$="LUCKILY THE LOCAL SHEIK HAS AGREED TO THEIR RANSOM DEMANDS... BUT... WATCH FOR THE PYGMIES!":GOSUB1
930 M$="YOU HAVE A NEW CHOICE OF SUB-COMMANDS:":GOSUB1
940 M$="7. ATTEMPT AN ESCAPE":GOSUB1
950 M$="8. WAIT FOR PAYMENT":GOSUB1
960 PRINT"YOUR SUB-COMMAND";
970 INPUT X
980 IF X=8 THEN 1060
990 X1=INT(10*RND(0))
1000 IF X1<5 THEN 1040
1010 M$="CONGRATULATIONS, YOU SUCCESSFULLY ESCAPED!!!":GOSUB1
1020 Q=0
1030 GOTO 340
1040 M$="YOU WERE MORTALLY WOUNDED BY A PIG STABBER WHILE ESCAPING.":GOSUB1
1050 GOTO 1410
1060 X1=INT(100*RND(0))
1070 REM
1080 IF X1>24 THEN 1100
1090 M$="YOUR RANSOM HAS BEEN PAID AN YOU ARE FREE TO GO.":GOSUB1
1095 Q=0
1096 GOTO 340
1100 M$="THE LOCAL SULTAN IS COLLECTING... JUST WAIT...":GOSUB1
1110 GOTO 340
1120 A=INT(10*RND(0))
1130 IF A>2 THEN 1240
1140 M$="YOU HAVE ARRIVED AT AN OASIS-- YOUR CAMEL IS FILLING YOUR CANTEEN AND EATING FIGS.":GOSUB1
1160 Z=4
1170 S=6
1180 RETURN
1190 M$="YOU DIRTY RASCALLION! YOU RAN YOUR POOR CAMEL TO DEATH!!!":GOSUB1
1200 GOTO 1410
1210 M$="YOU WIN, A PARTY IS BEING GIVEN IN YOUR HONOR... THE PYGMIES ARE PLANNING TO ATTEND...":GOSUB1
1230 GOTO 1560
1240 X1=INT(100*RND(0))
1250 IF X1>5 THEN 1350
1260 M$="YOU HAVE BEEN CAUGHT IN A SANDSTORM... GOOD LUCK!":GOSUB1
1270 X5=INT(10*RND(0))
1280 X6=INT(10*RND(0))
1290 IF X6<5 THEN 1320
1300 C=C+X5
1310 GOTO 1330
1320 C=C-X5
1330 M$="YOUR NEW POSITION IS"+STR$(C)+" MILES SO FAR!":GOSUB1
1340 RETURN
1350 X1=INT(100*RND(0))
1360 IF X1>5 THEN RETURN
1370 C1=C1+1
1380 M$="YOUR CAMEL HURT HIS HUMP.":GOSUB1
1390 M$="LUCKILY THE PYGMIES WERE FOOTWEARY!!!":GOSUB1
1400 RETURN
1410 U=INT(10*RND(0))
1420 M$="YOU DIED IN THE DESERT.":GOSUB1
1430 IF U>1 THEN 1460
1440 M$="THE NATIONAL CAMEL'S UNION IS NOT ATTENDING YOUR FUNERAL!!!":GOSUB1
1450 GOTO 1560
1460 IF U>3 THEN 1490
1470 M$="YOUR BODY WAS EATEN BY VULTURES AND IMPORTED CANNIBALS!!!":GOSUB1
1480 GOTO 1560
1490 IF U>5 THEN 1520
1500 M$="THE LOCAL SHEIK NOW USES YOUR SKULL FOR A CHANGE PURSE!!!":GOSUB1
1510 GOTO 1560
1520 IF U>7 THEN 1550
1530 M$="PEOPLE WITH LITTLE INTELLIGENCE SHOULD STAY OUT OF THE DESERT!!!":GOSUB1
1540 GOTO 1560
1550 M$="TURKEYS SHOULD FLY, NOT RIDE CAMELS!!!":GOSUB1
1560 PRINT
1570 PRINT
1580 PRINT"WANT A NEW CAMEL AND A NEW GAME ";
1590 INPUT D$
1600 IF LEFT$(D$,1)="Y" THEN 320
1620 GOTO 1650
1630 M$="YOU RAN OUT OF WATER... SORRY CHUM!!!":GOSUB1
1640 GOTO 1410
1650 PRINT"-----------------"
1655 PRINT"     CHICKEN"
1657 PRINT"-----------------"
1660 END
2000 Z=4
2010 S=6
2020 C=0
2030 C1=0
2040 Q=0
2050 F=0
2060 P=0
2070 RETURN
