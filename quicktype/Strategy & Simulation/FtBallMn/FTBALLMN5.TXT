0 DIMC1,C2,M$:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO9500

4 C1=LEN(M$):PRINTTAB((32-C1)/2)M$:RETURN
5 IFLEFT$(M$,1)="-"THENPRINTRIGHT$("       $"+MID$(M$,1),7);:RETURN
6 PRINTRIGHT$("       $"+MID$(M$,2),7);:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-16384+C2,:RETURN
8 C1=LEN(M$):PRINTTAB((32-C1)/2);:GOSUB7:RETURN

9 M$=LEFT$(M$+"            ",12):I$="":FORK=1TO12:I$=I$+MID$(STR$(ASC(MID$(M$,K))),2,2):NEXT
10 N(1)=VAL(MID$(I$,1,8)):N(2)=VAL(MID$(I$,9,8)):N(3)=VAL(MID$(I$,17,8)):RETURN

11 M$="":I$=MID$(STR$(C2),2):FORK=1TO8STEP2:M$=M$+CHR$(VAL(MID$(I$,K,2))):NEXT
12 FORC1=4TO1STEP-1:IFMID$(M$,C1,1)<>" "THENM$=LEFT$(M$,C1):C1=1:NEXT:RETURN
13 NEXT:M$="":RETURN

14 FORI=1TO32:C2=D(5,I):GOSUB11:T$(I)=M$:C2=D(6,I):GOSUB11:T$(I)=T$(I)+M$:C2=D(7,I):GOSUB11:T$(I)=T$(I)+M$:NEXT
15 FORI=1TO24:C2=D(8,I):GOSUB11:P$(I)=M$:C2=D(9,I):GOSUB11:P$(I)=P$(I)+M$:C2=D(10,I):GOSUB11:P$(I)=P$(I)+M$:NEXT:RETURN
16 C2=D(0,16):GOSUB11:OT$=M$:C2=D(0,17):GOSUB11:OT$=OT$+M$:C2=D(0,18):GOSUB11:OT$=OT$+M$:RETURN

40 CLS:PRINT"PLEASE WAIT...":RETURN
50 GOSUB40:FORI=1TO32:M$=T$(I):GOSUB9:D(5,I)=N(1):D(6,I)=N(2):D(7,I)=N(3):NEXT
51 FORI=1TO24:M$=P$(I):GOSUB9:D(8,I)=N(1):D(9,I)=N(2):D(10,I)=N(3):NEXT
52 M$=OT$:GOSUB9:D(0,16)=N(1):D(0,17)=N(2):D(0,18)=N(3)
53 D(0,1)=T1:D(0,2)=M:D(0,3)=L:D(0,4)=ML:D(0,5)=MA:D(0,6)=FA:D(0,7)=S:D(0,8)=S5:D(0,9)=D1:D(0,10)=D2
54 D(0,11)=C:D(0,12)=LT:D(0,13)=P1:D(0,14)=L1:D(0,15)=A(2):RETURN

100 GOSUB2000
120 GOSUB4000
220 GOSUB8000
240 GOSUB8100
260 GOSUB8700
499 GOTO100
800 GOSUB50:CLS:PRINT"PREPARE RECORDER TO RECORD ON   BLANK TAPE. PRESS ANY KEY WHEN  READY."
810 IFINKEY$=""THEN810
820 PRINT"SAVING...":CSAVE*D,"FMDATA":PRINT"DONE.":RETURN

899 CLS:PRINT"PLACE SAVED GAME CASSETTE IN    RECORDER, PRESS ANY KEY WHEN    READY, THEN PRESS PLAY."
910 IFINKEY$=""THEN910
920 PRINT"LOADING...":CLOAD*D,"FMDATA"
921 GOSUB40
922 T1=D(0,1):M=D(0,2):L=D(0,3):ML=D(0,4):MA=D(0,5):FA=D(0,6):S=D(0,7):S5=D(0,8):D1=D(0,9):D2=D(0,10)
923 C=D(0,11):LT=D(0,12):P1=D(0,13):L1=D(0,14):A(2)=D(0,15)
924 GOSUB16:GOSUB14:RETURN

1000 PRINT"YOU'VE  ";:M$=STR$(M):GOSUB5:PRINT"  YOU OWE ";:M$=STR$(L):GOSUB5:RETURN
1010 PRINT"YOU'VE TOO LITTLE MONEY":RETURN
1020 PRINT:PRINTA$(7)D(4,24+T1-LT)TAB(13)"MATCHES PLAYED"ML:RETURN
1030 PRINT:M$="PRESS ANY KEY":GOSUB8
1033 Y$=INKEY$:IFY$=""THEN1033
1035 RETURN
1040 IFXZ<1THENXZ=1
1041 IFXZ>20THENXZ=20
1043 RETURN
1050 CLS:PRINT"(p=PICKED TO PLAY, i=INJURED)":RETURN
1080 M$="OR TYPE 99 TO CONTINUE GAME":GOSUB7:PRINT:RETURN
1090 PRINT@32*5,T$(X(1))TAB(12)S(1),T$(X(2))TAB(28)S(2):RETURN
1100 PRINTTAB(10)A$(6)" ";D1:PRINT:RETURN
1120 P=0
1122 P1=0
1125 FORR=1TO24
1130 IFD(1,R)=2THENP=P+1
1135 IFD(1,R)>0THENP1=P1+1
1140 NEXTR
1145 RETURN
1150 FORJ=1TORND(200)+2500:NEXT:RETURN
1160 XZ=A(I):GOSUB1040:A(I)=XZ:RETURN
2000 CLS:M$="TO -            TYPE -":GOSUB7:PRINT,,,"SELL OR LIST",,"PLAYERS","A",,,"OBTAIN A "
2021 PRINT"LOAN","L",,,"PRINT STATUS","S",,,"SAVE GAME","KEEP",,,
2030 PRINT"RESTORE GAME","R",,,
2040 GOSUB1080
2180 INPUTX$
2200 IFX$="A"THENGOSUB2400
2204 IFX$="L"THENGOSUB2800
2206 IFX$="S"THENGOSUB3000
2220 IFX$="KEEP"THENGOSUB800
2230 IFX$="R"THENGOSUB899
2240 IFX$="99"THENRETURN
2250 GOTO2000
2400 OF=2
2410 GOSUB1050:GOSUB2540
2440 FORN=1TO24:IF(D(1,N)=OF)OR(D(1,N)=3AND OF=1)THEN2445
2444 GOTO2460
2445 PL=N
2450 GOSUB2550
2460 NEXTN
2470 PRINT"TYPE NO. TO SELL, S FOR OTHER   PLAYERS, ";:M$="99 TO CONTINUE":GOSUB7
2472 GOSUB1120:INPUTI$
2473 I=VAL(I$):IFI=99THEN2539
2474 IFI$="S"THENOF=1-1*(OF=1):GOTO2410
2475 IFI<1ORI>24THEN2472
2476 IFD(1,I)=0THEN2472
2480 CLS:IFD(1,I)<>3THEN2484
2481 PRINTTAB(11)"NO OFFERS"
2482 GOSUB1030:GOTO2539
2484 O=INT((RND(5)+7)*D(2,I)/10)
2485 I$=T$(RND(32)):IFI$=OT$THEN2485
2487 PRINTI$" HAVE OFFERED";:M$=STR$(O):GOSUB5:PRINT
2488 M$="FOR "+P$(I):GOSUB4:INPUT"       DO YOU ACCEPT";I$:IFLEFT$(I$,1)="Y"THEN2500
2494 GOTO2535
2500 D(1,I)=0
2505 M=M+O:FF=FF+O
2509 CLS
2510 M$=P$(I)+" HAS BEEN SOLD":GOSUB4:GOSUB1030:GOTO2539
2535 IFRND(3)=1THEN2539
2536 D(1,I)=3
2539 RETURN
2540 PRINT:PRINT"  NAME   NO. SKILL ENERGY VALUE":RETURN
2550 PRINTLEFT$(R$(INT((PL-1)/8+1))+"     ",2);
2552 PRINTLEFT$(P$(PL)+"      ",8);
2553 PRINTLEFT$(STR$(PL)+"      ",4);
2554 PRINTLEFT$(STR$(D(4,PL))+"       ",5);
2555 PRINTLEFT$(STR$(D(3,PL))+"    ",4);
2556 M$=STR$(D(2,PL)):GOSUB5
2557 PRINTRIGHT$("  "+Q$(D(1,PL)+(ABS(D(1,PL)=0))),2);:RETURN
2800 CLS:GOSUB1000
2804 INPUT"LOAN NEEDED ($1000'S)";I
2810 IFI<0THEN2800
2815 IFI/1000<>INT(I/1000)THEN2804
2820 IFI+L<=250000*D2THEN2830
2822 PRINT"SORRY, CREDIT LIMIT IS ";:M$=STR$(250000*D2):GOSUB5:PRINT:GOSUB1030:GOTO2800
2830 M=M+I:L=L+I:RE=RE+I/20:GOSUB1000:GOSUB1030:RETURN
3000 CLS:M$=T$(T1):GOSUB8:PRINT:IFS5=0THEN3010
3008 S1=INT(S/S5)*2:IFS1>100THENS1=100
3010 PRINT,,"MANAGERIAL RATING (MAX 100)=";S1
3012 PRINT,,"LEVEL "L1," SEASONS"S5
3016 GOSUB1000:PRINTA$(2)A(2):GOSUB1020:GOSUB1030:RETURN
4000 MA=MA+1:LC=2:IFMA=3THENLC=1:MA=0
4010 IFFA=1THENLC=2
4025 GOSUB4100
4029 RETURN
4100 CLS:IFLC<>1THEN4120
4110 C=C+1
4111 V1=INT((7-C)*4+RND(8)):IFV1=T1ORPB=V1THEN4111
4116 PB=V1
4120 IFLC<>2THEN4140
4121 ML=ML+1:V1=INT((ML+1)/2+LT)
4130 IFV1=T1 THENV1=LT+8
4140 FORI=1TO5
4150 IFLC=1THENU(I)=INT(RND(16)+1+L1+((T1-1)/8)-((V1-1)/8+1))
4152 IFLC=2THENU(I)=INT(RND(14)+L1+(D(1,24+V1-LT)/ML))
4155 XZ=U(I)
4157 GOSUB1040
4158 U(I)=XZ
4160 NEXTI
4190 IFLC=1THENGOSUB4500:H=RND(2)
4202 IFLC<>2THEN4205
4203 H=INT((1.5/H)+1):H1=H
4205 V=INT((1.5/H)+1)
4210 ONLCGOSUB4600,4700
4220 X(H)=T1:X(V)=V1:S(1)=0:S(2)=0
4230 IFLC=1THENM$="F.A. CUP MATCH":GOSUB8:PRINT
4231 GOSUB4500:IFLC=1THENM$=C$+" ROUND":GOSUB4
4232 IFLC=2THENM$="LEAGUE MATCH - "+A$(6)+STR$(D1):GOSUB8:PRINT
4235 GOSUB4240:GOTO4245
4240 M$=T$(X(1))+" V "+T$(X(2)):GOSUB4:PRINT:PRINTA$(5+LC)X1;TAB(20)X2:RETURN
4245 GOSUB1030
4250 GOSUB6000
4280 GOSUB5000
4300 PRINT@0,"":PRINT@0,;:M$="**FINAL SCORE**":GOSUB8
4303 IFS(H)>S(V)THENSOUND250,15:SOUND250,20
4305 IFS(H)<S(V)THENSOUND100,15:SOUND40,20
4307 IFS(H)=S(V)THENSOUND120,10:SOUND120,10
4310 GOSUB1090
4322 PRINT:PRINTTAB(5)"GATE RECEIPTS ";:M$=STR$(X9):GOSUB5:PRINT
4323 ZZ=ZZ+X9:M=M+X9:GOSUB1030:IFLC=2THEN4340
4330 IFS(H)<>S(V)THEN4340
4332 CLS:PRINTTAB(12)"REPLAY-":GOSUB1030
4335 CLS:H=INT((1.5/H)+1):V=INT((1.5/V)+1):GOTO4210
4340 ONLCGOSUB5200,7000
4350 IFS(H)>S(V)THENA(2)=A(2)+INT((20-A(2))/2)
4360 IFS(H)<S(V)THENA(2)=INT(A(2)/2)
4370 I=2
4374 GOSUB1160
4380 IFLC<>2THEN4499
4385 GOSUB7500
4390 GOSUB7710
4499 RETURN
4500 IFC<7THENC$=C$(C)
4510 IFC=6THENC$="SEMI-FINAL"
4520 IFC=7THENC$="FINAL"
4530 RETURN
4600 IFH<>1THEN4610
4602 X1=D1:X2=INT((V1-1)/8)+1:X9=G
4610 IFH<>2THEN4620
4612 X1=INT((V1-1)/8)+1:X2=D1:X9=(5-X1)*10000
4620 IFC=6THENX9=50000
4622 IFC=7THENX9=100000
4630 RETURN
4700 IFH<>1THEN4710
4702 X1=D(4,24+T1-LT):X2=D(4,24+V1-LT):X9=G
4710 IFH<>2THEN4720
4712 X1=D(4,24+V1-LT):X2=D(4,24+T1-LT):X9=(9-D(4,24+V1-LT))*D2*1000
4720 RETURN
5000 CLS:M$="**MATCH IN PROGRESS**":GOSUB8:C1=0:GOSUB9540
5002 FORI=1TO5:N=RND(100)+(A(I)-U(I))*5
5010 IFN<75THEN5050
5015 S(H)=S(H)+1
5020 GOSUB1150
5040 GOSUB1090:C1=1:GOSUB9540
5045 SOUND100+(S(H)*25),16
5050 N=RND(100)+(U(I)-A(I))*5
5060 IFN<75THEN5100
5070 S(V)=S(V)+1
5080 GOSUB1150
5090 GOSUB1090:C1=1:GOSUB9540
5100 NEXT:RETURN
5200 CLS
5203 IFS(H)>=S(V)THEN5210
5204 FA=1
5205 M$="++YOU'RE OUT OF THE CUP++":GOSUB4:GOTO5230
5210 IFC<7THENPRINT:PRINT:M$="YOU'RE THROUGH":GOSUB4:M$="TO THE NEXT ROUND!":GOSUB8:PRINT
5212 IFC=7THENM$="**YOU'VE WON THE F.A.CUP**":GOSUB4:FA=1
5213 IFC=7THENFA=1
5230 I=2
5237 GOSUB1160
5240 GOSUB1030
5299 RETURN
6000 CLS:IFML=1THEN6112
6020 FORI=1TO24
6030 IFD(1,I)=1ORD(1,I)=3THEND(3,I)=D(3,I)+10
6040 IFD(1,I)=2THEND(3,I)=D(3,I)-1
6050 IFD(1,I)=3THEND(1,I)=1
6060 XZ=D(3,I)
6062 GOSUB1040
6064 D(3,I)=XZ
6070 IFD(1,I)=0ORRND(19)<19THEN6100
6080 D(1,I)=3
6100 NEXT
6112 CLS:GOSUB6500:GOSUB1120:PRINTTAB(5)"TYPE C TO CHANGE TEAM"
6130 GOSUB1080
6135 INPUTI$
6136 IFI$="99"THENRETURN
6140 OF=2
6142 IFP>11THENOF=2
6143 GOSUB1050
6145 GOSUB2540
6150 FORI=1TO24:IF(D(1,I)=OF)OR(D(1,I)=3ANDOF=1)THEN6155
6152 GOTO6180
6155 PL=I
6160 GOSUB2550
6180 NEXT
6190 GOSUB1120
6200 IFP>11THEN6300
6210 IFOF=1THENPRINT"TYPE PLAYER NO. TO ADD TO TEAM"
6211 PRINT"TYPE S FOR OTHER PLAYERS,":M$="OR 99 TO CONTINUE":GOSUB7
6215 INPUTI$:I=VAL(I$):IFI$="S"THENOF=1-1*(OF=1):GOTO6142
6217 IFI=99THEN6112
6220 IFI<1ORI>24THEN6215
6225 IFD(1,I)<>1THEN6215
6230 D(1,I)=2
6240 GOSUB1120:GOTO6142
6300 PRINT"TYPE NO. TO REMOVE";
6310 INPUTI$:I=VAL(I$)
6320 IFI<1ORI>24THEN6310
6325 IFD(1,I)<>2THEN6310
6330 D(1,I)=1:GOTO6142

6500 FORI=1TO5:IFI=2THENI=3
6502 A(I)=A(I)*(I=2)
6503 NEXT
6505 FORI=1TO24
6510 IFD(1,I)<>2THEN6550
6520 Q=INT((I-1)/8)+3
6530 A(Q)=A(Q)+D(4,I)
6540 A(1)=A(1)+D(3,I)
6550 NEXTI
6560 A(1)=INT(A(1)/11)
6605 PRINTTAB(10);T$(T1);TAB(20);T$(V1)
6610 FORI=1TO5
6615 GOSUB1160
6620 PRINTA$(I);TAB(13);A(I);TAB(23);U(I)
6630 NEXTI
6640 RETURN
7000 D(2,24+T1-LT)=D(2,24+T1-LT)+S(H)
7005 D(3,24+T1-LT)=D(3,24+T1-LT)+S(V)
7010 D(2,24+V1-LT)=D(2,24+V1-LT)+S(V)
7015 D(3,24+V1-LT)=D(3,24+V1-LT)+S(H)
7020 IFS(H)<>S(V)THEN7030
7022 D(1,24+T1-LT)=D(1,24+T1-LT)+1
7024 D(1,24+V1-LT)=D(1,24+V1-LT)+1
7026 GOTO7499
7030 IFS(H)<S(H)THENG=G-INT(G/10)
7040 IFS(H)>S(V)THEN7070
7050 IFG<1000THENG=1000
7063 D(1,24+V1-LT)=D(1,24+V1-LT)+3
7065 GOTO7499
7070 G=G+INT(((10000*D2)-G)/10)
7090 D(1,24+T1-LT)=D(1,24+T1-LT)+3
7499 RETURN
7500 CLS:M$="OTHER MATCHES-":GOSUB8:PRINT:GOSUB1100:KK=(ML-1)*6
7560 FORI=1TO6STEP2
7570 R=OG(KK+I):R1=OG(KK+1+I)
7610 R2=INT((D(1,24+R)/ML)+RND(4))
7620 R3=INT((D(1,24+R1)/ML)+RND(4))
7630 PRINTT$(R+LT);TAB(12);R2,T$(R1+LT);TAB(28);R3
7640 D(2,24+R)=D(2,24+R)+R2
7645 D(2,24+R1)=D(2,24+R1)+R3
7650 D(3,24+R)=D(3,24+R)+R3
7655 D(3,24+R1)=D(3,24+R1)+R2
7660 IFR2>R3THEND(1,24+R)=D(1,24+R)+3
7670 IFR2<R3THEND(1,24+R1)=D(1,24+R1)+3
7682 D(1,24+R)=D(1,24+R)+ABS(R2=R3)
7684 D(1,24+R1)=D(1,24+R1)+ABS(R2=R3)
7690 NEXTI
7700 GOSUB1030
7705 RETURN
7710 CLS:PRINT"   TEAM            F   A   PTS":PRINT:FORQ=1TO8:D(4,24+Q)=0:NEXTQ
7740 FORI=1TO8:Q1=9:FORQ=1TO8:IFD(4,24+Q)<>0THEN7800
7780 IFD(1,24+Q)>D(1,24+Q1)THENQ1=Q
7790 IFD(1,24+Q)=D(1,24+Q1)THENIFD(2,24+Q)-D(3,24+Q)>D(2,24+Q1)-D(3,24+Q1)THENQ1=Q
7800 NEXT
7810 PRINTT$(Q1+LT)TAB(18)D(2,24+Q1)TAB(22)D(3,24+Q1)TAB(27)D(1,24+Q1)
7820 D(4,24+Q1)=I:NEXT:GOSUB1020:GOSUB1030:RETURN
8000 CLS:W=0:FORI=1TO24:IFD(1,I)>0THENW=W+D(4,I)*100*D2
8027 NEXT
8030 IFL<RETHEN RE=L
8032 M$="**WEEKLY BILLS**":GOSUB8:PRINT:PRINT
8033 PRINT"   WAGE BILL";TAB(20);:M$=STR$(W):GOSUB5:PRINT
8034 PRINT"   GROUND RENT";TAB(20);:M$=STR$(X):GOSUB5:PRINT
8035 PRINT"   LOAN REPAYMENT"TAB(20);:M$=STR$(INT(RE+L/100)):GOSUB5:PRINT
8036 M=M-W-X-INT(RE+L/100)
8040 PRINT"   WEEKLY BALANCE"TAB(20);:M$=STR$(ZZ-X-W-INT(RE+L/100)-HH+FF):GOSUB5:PRINT
8041 L=L-RE:ZZ=0:HH=0:FF=0
8045 XP=M-L
8050 IFM>=0THEN8060
8051 M=M+1000
8052 L=L+1000
8053 RE=RE+50
8054 IFM<0THEN8051
8056 PRINT"LOAN INCREASED TO PAY BILLS"
8060 IFL<=250000*D2 THENGOSUB1030:RETURN
8062 PRINT"YOU OWE OVER ";:M$=STR$(250000*D2):PRINT"YOU'RE SACKED BY THE CLUB"
8068 END
8100 IFML<14ORFA=0THENRETURN
8120 CLS:M$="**END OF SEASON**":GOSUB8:PRINT
8135 GOSUB1030
8140 GOSUB7710
8145 CLS:R1=9-D(4,24+T1-LT)
8150 S=S+R1*2+C*2+D1
8220 M=M+R1*5000*D2
8230 S5=S5+1
8235 FORI=1TO8
8240 GOSUB8500
8243 GOSUB8600
8246 NEXTI
8250 GOSUB8800
8252 GOSUB9300
8260 PRINT:M$="**NEW SEASON**":GOSUB8:PRINT:GOSUB1100:GOSUB1030:RETURN
8500 IFD(4,24+I)>2THEN8579
8515 IFD1=1THEN8570
8520 M$="**"+T$(LT+I)+" ARE PROMOTED**":GOSUB8:PRINT
8530 X$=T$(LT-D(4,24+I))
8540 T$(LT-D(4,24+I))=T$(LT+I)
8550 T$(LT+I)=X$
8552 IFLT+I=T1THENT1=(LT-D(4,24+I))
8560 GOTO8579
8570 IFD(4,24+I)=1THENM$="**"+T$(LT+I)+" ARE LEAGUE CHAMPIONS**":GOSUB8:PRINT
8579 RETURN
8600 IFD1=4THEN8699
8610 IFD(4,24+I)<7THEN8699
8620 M$="++"+T$(LT+I)+" ARE RELEGATED++":GOSUB4
8630 X$=T$(D1*8+9-D(4,24+I))
8640 T$(D1*8+9-D(4,24+I))=T$(LT+I)
8645 T$(LT+I)=X$
8650 IFLT+I=T1THENT1=D1*8+9-D(4,24+I)
8699 RETURN
8700 IFML=0THEN8780
8702 R1=RND(24)
8704 IFD(1,R1)>0THEN8700
8705 CLS:M$="**BUY PLAYER**":GOSUB8:PRINT:PRINT:PRINT:IFP1>15THENPRINT"NO PLAYERS FOR SALE":GOTO8778
8715 GOSUB1000
8720 GOSUB2540
8725 PL=R1
8730 GOSUB2550
8740 PRINT:PRINTTAB(8)"TYPE YOUR BID $":GOSUB1080
8745 INPUTN
8750 IFN=99THEN8780
8755 IFM>=NTHEN8760
8756 GOSUB1010:GOSUB1030:GOTO8705
8760 CLS:R=(RND(10)*N)/D(2,R1):IFR>=5THEN8770
8766 GOSUB8785:GOSUB1030:GOTO8705
8770 PRINTP$(R1)" HAS JOINED YOUR TEAM":D(2,R1)=D(4,R1)*5000*D2:M=M-N:HH=N:D(1,R1)=1
8778 GOSUB1030
8780 RETURN
8785 D(2,R1)=INT(D(2,R1)+D(2,R1)/5):PRINT"YOUR BID IS REFUSED":RETURN
8800 D1=INT(((T1-1)/8)+1):FA=0:LT=(D1-1)*8:LC=0:H=1:D2=5-D1:MA=0:ML=0:Y=0:Z=0
8810 FORI=1TO8:D(1,24+I)=0:D(4,24+I)=0:D(2,24+I)=0:D(3,24+I)=0:NEXTI:V=0
8820 FORI=1TO24:D(4,I)=RND(5):D(2,I)=INT(5000*D2*D(4,I)):D(3,I)=RND(20):NEXTI
8834 D(1,24+9)=-1:A(2)=10:G=5000*D2:C=0:X=500*D2:H1=2
8890 RETURN
8900 D1=0
8902 D1=D1+1
8905 IFD1>4THEN8900
8910 CLS:M$="CHOOSE TEAM TO MANAGE -":GOSUB7:PRINT
8915 PRINT"NUMBER","NAME"
8916 LT=(D1-1)*8
8920 FORI=LT+1TOLT+8
8925 PRINTI,T$(I)
8930 NEXTI
8935 PRINT"TYPE TEAM NUMBER (OR 99 FOR":INPUT"MORE CHOICE)";T1
8940 IFT1=99THEN8902
8945 IFT1<LT+1ORT1>LT+8THEN8935
8947 D1=4:Y$=T$(32):T$(32)=T$(T1):T$(T1)=Y$:T1=32
8948 OT$=T$(32)
8955 CLS:FORI=1TO7:PRINTI,;:M$=MID$("BEGINNERNOVICE  AVERAGE GOOD    EXPERT  SAGE    GENIUS  ",I*8-7,8):GOSUB7:PRINT:NEXT
8960 PRINT"TYPE YOUR LEVEL (1-7)";
8962 INPUTL1
8965 IFL1<1ORL1>7THEN8960
8970 FORI=1TO12
8975 R=RND(24):IFD(1,R)>0THEN8975
8985 D(1,R)=2
8986 IFI=12THEND(1,R)=1
8987 NEXT:GOSUB8800:RETURN
9000 FORI=1TO24:READP$(I):NEXT
9010 FORI=1TO7:READA$(I):NEXT
9020 FORI=1TO32:READT$(I):NEXT
9030 FORI=1TO3:READR$(I),Q$(I):NEXT
9035 FORI=1TO84:READOG(I):NEXT
9036 FORI=1TO6:READC$(I):NEXT
9040 DATASHILTON,SPINK,SMITH,BUTCHER,THOMPSON,SANSOM,MCQUEEN,MARTIN,WILKINS,SHELTON,BRADY,MABBUTT,LEE,BROOKING
9041 DATAROBSON,HODDLE,EDWARDS,BARNES,SHAW,DALGLISH,WOODCOCK,MARINER,RUSH,NICHOLAS
9050 DATAENERGY,MORALE,DEFENCE,MIDFIELD,ATTACK,DIVISION,LEAGUE POS.
9060 DATAARSENAL,MAN. UTD,EVERTON,LIVERPOOL,MAN. CITY,NORWICH,SWANSEA,WATFORD
9070 DATABLACKBURN,CAMBRIDGE,CHELSEA,DERBY CO.,GRIMSBY,MIDDLESBRO,OLDHAM,SHEFF.WED.
9080 DATABOURNEMOUTH,BRISTOL R.,DONCASTER,LINCOLN,NEWPORT,SHEFF.UTD,PRESTON,SOUTHEND
9090 DATABLACKPOOL,COLCHESTER,DARLINGTON,HARTLEPOOL,HULL,PORT VALE,SCUNTHORPE,TORQUAY
9100 DATAD," ",M,P,A,I
9110 DATA2,3,4,5,6,7,3,2,5,4,7,6
9111 DATA1,3,4,6,5,7,3,1,6,4,7,5
9112 DATA1,4,2,7,5,6,4,1,7,2,6,5
9113 DATA1,5,2,6,3,7,5,1,6,2,7,3
9114 DATA1,2,3,6,4,7,2,1,6,3,4,7
9115 DATA1,7,2,4,5,3,7,1,4,2,3,5
9116 DATA1,6,2,5,3,4,6,1,5,2,4,3
9120 DATAFIRST,SECOND,THIRD,FOURTH,FIFTH,SIXTH
9300 FORI=1TO7:IFT$(LT+I)=OT$THENT$(LT+I)=T$(LT+8):T$(LT+8)=OT$
9302 NEXT:T1=D1*8:RETURN
9500 CLS:CLEAR2500:DIMS(2),R$(3),A(5),U(5),D(10,33),P$(24),T$(32),X(2),A$(7),Q$(3),OG(84),N(3)
9504 RE=0:L=0:M=100000:S5=0:S1=0:XP=0:S=0:ZZ=0:PB=0:GOSUB9000:GOSUB8900:GOTO100

9540 FORK=1TO4:GOSUB9580:ONKGOSUB9601,9605,9609,9613:NEXT
9560 IFC1=0THENPRINT@80," ";:PRINT@83,"*";:SOUND10,5:GOSUB9580:GOSUB9617:GOTO9570
9565 PRINT@80," ";:PRINT@86,"*";:SOUND100,2:GOSUB9580:GOSUB9621
9570 FORC1=1TO2500:NEXT:RETURN
9580 PRINT@32,;:SOUNDI*20,2:RETURN
9601 PRINT"                          --    ";
9602 PRINT"    0/               0   !  !"CHR$(92)"  ";
9603 PRINT"   /€               /€"CHR$(92)"  !  !X"CHR$(92)" ";
9604 PRINT"   / >*              <"CHR$(92)"  !  !XX"CHR$(92);:RETURN
9605 PRINT"                          --    ";
9606 PRINT"    0/               0   !  !"CHR$(92)"  ";
9607 PRINT"   /€               /€"CHR$(92)"  !  !X"CHR$(92)" ";
9608 PRINT"   / "CHR$(92)"  *            <"CHR$(92);:RETURN
9609 PRINT"                          --    ";
9610 PRINT"    0/              "CHR$(92)"0   !  !"CHR$(92)"  ";
9611 PRINT"   /€        *       €"CHR$(92)"  !  !X"CHR$(92)" ";
9612 PRINT"   / "CHR$(92)"              / "CHR$(92);:RETURN
9613 PRINT"                          --    ";
9614 PRINT"    0/          *   "CHR$(92)"0   !  !"CHR$(92)"  ";
9615 PRINT"   /€                €"CHR$(92)"  !  !X"CHR$(92)" ";
9616 PRINT"   / "CHR$(92)"              / "CHR$(92);:RETURN
9617 PRINT"                          --    ";
9618 PRINT"    0             *"CHR$(92)CHR$(92)"0   !  !"CHR$(92)"  ";
9619 PRINT"   /€"CHR$(92)"               €   !  !X"CHR$(92)" ";
9620 PRINT"   / "CHR$(92)"              / "CHR$(92)"  !  !XX"CHR$(92);:RETURN
9621 PRINT"                          --    ";
9622 PRINT"    0/ goal!             ! *!"CHR$(92)"  ";
9623 PRINT"   /€           -0Œ/     !  !X"CHR$(92)" ";
9624 PRINT"   / "CHR$(92)"           /ƒ"CHR$(92)"     !  !XX"CHR$(92);:RETURN
