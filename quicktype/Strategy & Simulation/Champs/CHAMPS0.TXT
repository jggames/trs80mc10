10 'champions! copyright peaksoft 1983.
20 GOSUB5860
30 CLS
40 DIMDE$(12):DIMCE$(12):DIMMI$(18):DIMST$(18):DIMDD$(12):DIMCL$(12):DIMMD$(18):DIMSR$(18):DIMAT$(20):DIMBT$(12):DIMCT$(12):DIMDT$(12):DIMET$(12):DIMSP$(20):DIMXT$(12)
50 CA=175000:CZ=0:DI=4
60 FORN=1 TO20:SP$(N)=" ":AT$(N)=" ":NEXT:SS$=" "
70 FORN=1 TO3:SS$(N)=" ":NEXT
80 PRINT:PRINT
90 A$=""
100 SOUND100,1
110 INPUT"WHICH TEAM WOULD YOU LIKE TO BE";A$
120 GOSUB180
130 CLS:PRINT@97,"SETTLE BACK AND MAKE YOURSELF   UNCOMFORTABLE.  YOU'VE ASKED    FOR THE JOB AT ";A$:PRINT@193,"AND NOW YOU'RE STUCK WITH IT.   THE ONLY ESCAPE IS TO SUCCEED   OR TO gIVE UP!"
140 FORN=1 TO6000:NEXT
150 GOTO5400
160 FORN=200 TO100 STEP-10:SOUNDN,1:NEXT:FORN=100 TO200 STEP10:SOUNDN,1:NEXT
170 GOTO200
180 IFLEN(A$)>12 THEN CLS:PRINT@97,"SORRY! NO MORE THAN 12 LETTERS  PLEASE";:FORN=1TO2000:NEXT:CLS:GOTO110
190 RETURN
200 TT$(1)="DOCHERTY-9":TT$(2)="JOBSON-7":TT$(3)="LAMBERT-8":TT$(4)="FROGNALL-9":TT$(5)="SMITH-8":TT$(6)="JONES-7":TT$(7)="ROBINSON-9":TT$(8)="BAKER-8":TT$(9)="LOCKHART-7":TT$(10)="FROBISHER-9"
210 DATAGREGSON-6,GULLIVER-5,GIBSON-4,GREEN-3,GODDARD-2,GANNING-1
220 SOUND1,1
230 DATADENNISON-6,DEWEY-5,DOUGLAS-4,DONALD-3,DIX-2,DANBURY-1,DUNNING-6,DOBSON-5,DUNN-4,DUTTON-3,DRAPER-2,DOUGHTY-1
240 SOUND10,1
250 DATACHARLES-6,CARTWRIGHT-5,CARRUTHERS-4,CARR-3,CHURCHILL-2,CLARK-1,CUTHBERT-6,CHAPMAN-5,COOK-4,COLLINS-3,CRAWFORD-2,CAMPBELL-1
260 SOUND20,1
270 DATAMATTHEWS-6,MITCHELL-5,MCCREARY-4,MOORE-3,MUTIMER-2,MYALL-1,MACDONALD-6,MIX-5,MORGAN-4,MANN-3,MUDD-2,MEERS-1,MINTOFF-6,MOSS-5,MUIR-4,MERRICK-3,MARTIN-2,MACKENZIE-1
280 SOUND30,1
290 DATASMITH-6,STEWART-5,SERPELL-4,SWIFT-3,STEELE-2,SIMPKINS-1,SEWELL-6,STAINES-5,SALT-4,SUMMERS-3,STONE-2,STEIN-1,SCOBIE-6,SLOANE-5,SPRAY-4,SPINK-3,SIMONS-2,SILLOTH-1
300 SOUND40,1
310 FORN=1TO6:READGK$(N):NEXT
320 SOUND50,1
330 FORN=1TO12:READDE$(N):NEXT
340 SOUND60,1
350 FORN=1TO12:READCE$(N):NEXT
360 SOUND70,1
370 FORN=1TO18:READMI$(N):NEXT
380 SOUND80,1
390 FORN=1TO18:READST$(N):NEXT
400 SOUND90,1
410 FORN=1TO6
420 Q=RND(6)
430 IFGK$(Q)=" "THEN420
440 GP$(N)=GK$(Q):GK$(Q)=" "
450 NEXTN
460 SOUND100,1
470 FORN=1TO12
480 Q=RND(12)
490 IFDE$(Q)=" "THEN480
500 DD$(N)=DE$(Q):DE$(Q)=" "
510 NEXTN
520 SOUND110,1
530 FORN=1TO12
540 Q=RND(12)
550 IFCE$(Q)=" "THEN540
560 CL$(N)=CE$(Q):CE$(Q)=" "
570 NEXTN
580 SOUND120,1
590 FORN=1TO18
600 Q=RND(18)
610 IFMI$(Q)=" "THEN600
620 MD$(N)=MI$(Q):MI$(Q)=" "
630 NEXTN
640 SOUND130,1
650 FORN=1 TO18
660 Q=RND(18)
670 IFST$(Q)=" "THEN660
680 SR$(N)=ST$(Q):ST$(Q)=" "
690 NEXTN
700 SOUND140,1
710 FORN=1TO6
720 GK$(N)=GP$(N):NEXT
730 SOUND150,1
740 FORN=1TO12:DE$(N)=DD$(N):CE$(N)=CL$(N):NEXT
750 SOUND160,1
760 FORN=1TO18:MI$(N)=MD$(N):ST$(N)=SR$(N):NEXT
770 SOUND170,1
780 CLS
790 AT$(1)=GK$(1):AT$(2)=DE$(1):AT$(3)=CE$(1):AT$(4)=CE$(2):AT$(5)=DE$(2):AT$(6)=MI$(1):AT$(7)=MI$(2):AT$(8)=MI$(3):AT$(9)=ST$(1):AT$(10)=ST$(2):AT$(11)=ST$(3)
800 BT$(1)=GK$(2):BT$(2)=DE$(3):BT$(3)=CE$(3):BT$(4)=CE$(4):BT$(5)=DE$(4):BT$(6)=MI$(4):BT$(7)=MI$(5):BT$(8)=MI$(6):BT$(9)=ST$(4):BT$(10)=ST$(5):BT$(11)=ST$(6)
810 CT$(1)=GK$(3):CT$(2)=DE$(5):CT$(3)=CE$(5):CT$(4)=CE$(6):CT$(5)=DE$(6):CT$(6)=MI$(7):CT$(7)=MI$(8):CT$(8)=MI$(9):CT$(9)=ST$(7):CT$(10)=ST$(8):CT$(11)=ST$(9)
820 DT$(1)=GK$(4):DT$(2)=DE$(7):DT$(3)=CE$(7):DT$(4)=CE$(8):DT$(5)=DE$(8):DT$(6)=MI$(10):DT$(7)=MI$(11):DT$(8)=MI$(12):DT$(9)=ST$(10):DT$(10)=ST$(11):DT$(11)=ST$(12)
830 ET$(1)=GK$(5):ET$(2)=DE$(9):ET$(3)=CE$(9):ET$(4)=CE$(10):ET$(5)=DE$(10):ET$(6)=MI$(13):ET$(7)=MI$(14):ET$(8)=MI$(15):ET$(9)=ST$(13):ET$(10)=ST$(14):ET$(11)=ST$(15)
840 SP$(1)=GK$(6):SP$(2)=DE$(11):SP$(3)=DE$(12):SP$(4)=CE$(11):SP$(5)=CE$(12):SP$(6)=MI$(16):SP$(7)=MI$(17):SP$(8)=MI$(18):SP$(9)=ST$(16):SP$(10)=ST$(17):SP$(11)=ST$(18)
850 FORN=1TO5
860 Q=RND(11)
870 IFSP$(Q)=" "THEN860
880 ON N GOTO890,900,910,920,930
890 AT$(12)=SP$(Q):SP$(Q)=" ":NEXT
900 BT$(12)=SP$(Q):SP$(Q)=" ":NEXT
910 CT$(12)=SP$(Q):SP$(Q)=" ":NEXT
920 DT$(12)=SP$(Q):SP$(Q)=" ":NEXT
930 ET$(12)=SP$(Q):SP$(Q)=" ":NEXT
940 CLS6:PRINT@102,"LEAGUE DIVISION";DI;:FORJP=1 TO3000:NEXT
950 'PEAKSOFT REGISTRATION NO.
960 CLS:FORJP=0 TO31:PRINT@JP,CHR$(143+16);:NEXT
970 WW=0:R2=0:PRINT@10,A$;
980 PRINT@2,DI;
990 PRINT@27,WK+1;:PRINT@32," "
1000 IN$=INKEY$:IFIN$="P"THENIN$=""
1010 FORN=1 TO12:PRINTN;AT$(N):NEXT
1020 FORN=1 TO11:RA$=RIGHT$(AT$(N),1)
1030 R1=VAL(RA$)
1040 R2=R2+R1
1050 NEXT N
1060 RA=INT(R2/11)
1070 PRINT@177,"RATING=";RA;
1080 PRINT@241,"BANK=";CA;
1090 IFWK=4 ORWK=8 ORWK=14 ORWK=18 THENPRINT@33,CHR$(128);
1100 PRINT@441,SE
1110 PRINT@480," cHECK/sELL/pLAY/uSE SUB?";
1120 IFCA<0THENFORN=1 TO2:SOUND1,8:SOUND10,8:NEXT:PRINT@273,"in the red!";
1130 IN$=INKEY$:V7=RND(10):IFIN$=""THEN1130
1140 IFCA<0 ANDIN$="P" THEN1130
1150 SOUND190,2:SOUND200,2
1160 IFIN$="C"THEN1220
1170 IFIN$="S"THEN3750
1180 IFIN$="P"THEN1650
1190 IFIN$="U"THEN3840
1200 IFIN$="G"THENRUN
1210 GOTO1130
1220 CLS:PRINT:PRINT"1..";B$:PRINT"2..";C$:PRINT"3..";D$:PRINT"4..";E$:PRINT"5..TRANSFER MARKET":PRINT"6..RESERVE SQUAD"
1230 PRINT:INPUT" CHECK WHICH (1-6)";YY
1240 IFYY>6 OR YY<1 THEN1230
1250 SOUND190,2:SOUND200,2
1260 IFYY=1 THEN X$=B$:FORN=1 TO12:XT$(N)=BT$(N):NEXT
1270 IFYY=2 THEN X$=C$:FORN=1 TO12:XT$(N)=CT$(N):NEXT
1280 IFYY=3 THENX$=D$:FORN=1 TO12:XT$(N)=DT$(N):NEXT
1290 IFYY=4 THEN X$=E$:FORN=1 TO12:XT$(N)=ET$(N):NEXT
1300 IFYY=5 THEN CLS:GOTO4100
1310 IFYY=6 THENCLS:GOTO4300
1320 R2=0:CLS:PRINT"  ";X$:PRINT
1330 FOR N=1 TO 12:PRINTN;XT$(N):NEXT
1340 FORN=1 TO11:RA$=RIGHT$(XT$(N),1)
1350 R1=VAL(RA$):R2=R2+R1:NEXTN
1360 RA=INT(R2/11)
1370 PRINT@177,"RATING=";RA;
1380 PRINT@481,"WANT TO BUY?";
1390 IN$=INKEY$:IFIN$=""THEN 1390
1400 IFIN$="Y"THEN4430
1410 IFIN$="Y"THENPRINT@481,"";:INPUT"WHICH PLAYER (1-12)";N
1420 IFIN$<>"Y"THENCLS:GOTO950
1430 IF N<1 OR N>12 THEN 1320
1440 CO$=RIGHT$(XT$(N),1)
1450 CO=10000*VAL(CO$)
1460 IFCA-CO<0 THEN CLS:PRINT@97,"NOT ENOUGH CASH IN THE BANK!";:FORQ=1TO2000:NEXT:GOTO950
1470 CA=CA-CO
1480 EX$=XT$(N)
1490 XT$(N)=" "
1500 FORQ=1 TO20
1510 IFSP$(Q)=" "THEN NEXTQ
1520 XT$(N)=SP$(Q)
1530 SP$(Q)=" "
1540 FORQ=12 TO15
1550 IFAT$(Q)=" "THENAT$(Q)=EX$:GOTO1600
1560 NEXTQ
1570 FORQ=1 TO11
1580 IFAT$(Q)=" "THENAT$(Q)=EX$:GOTO1600
1590 NEXTQ
1600 ONYY GOTO1610,1620,1630,1640
1610 FORN=1 TO12:BT$(N)=XT$(N):NEXT:GOTO950
1620 FORN=1 TO12:CT$(N)=XT$(N):NEXT:GOTO950
1630 FORN=1 TO12:DT$(N)=XT$(N):NEXT:GOTO950
1640 FORN=1TO 12:ET$(N)=XT$(N):NEXT:GOTO950
1650 GOTO3890
1660 CLS:WK=WK+1
1670 ONWK GOTO 1680,2300,2450,2600,2750,2890,3030,3170,3310,3450,1680,2300,2450,2600,2750,2890,3030,3170,3310,3450
1680 AH=1:BH=0:CH=1:DH=0:GOSUB1830
1690 CLS
1700 PRINT:PRINT:PRINT"     WEEK";WK
1710 PRINT:PRINT" ";A$;AS;
1720 PRINT" ";B$;BS
1730 IFAS>BS THENAW=AW+1:AP=AP+3:BL=BL+1
1740 IFBS>AS THENBW=BW+1:BP=BP+3:AL=AL+1
1750 IFAS=BS THENAD=AD+1:AP=AP+1:BD=BD+1:BP=BP+1
1760 PRINT" ";C$;CS;
1770 PRINT" ";D$;DS
1780 IFCS>DS THENCW=CW+1:CP=CP+3:DL=DL+1
1790 IFDS>CS THENDW=DW+1:DP=DP+3:CL=CL+1
1800 IFCS=DS THENCD=CD+1:CP=CP+1:DD=DD+1:DP=DP+1
1810 AG=AG+1:BG=BG+1:CG=CG+1:DG=DG+1
1820 GOTO2020
1830 A9=0:B9=0:C9=0:D9=0:E9=0
1840 A6=0:B6=0:C6=0:D6=0:E6=0
1850 FORN=1 TO11:A8$=RIGHT$(AT$(N),1):B8$=RIGHT$(BT$(N),1):C8$=RIGHT$(CT$(N),1):D8$=RIGHT$(DT$(N),1):E8$=RIGHT$(ET$(N),1)
1860 A7=VAL(A8$):B7=VAL(B8$):C7=VAL(C8$):D7=VAL(D8$):E7=VAL(E8$)
1870 A6=A6+A7:B6=B6+B7:C6=C6+C7:D6=D6+D7:E6=E6+E7
1880 NEXTN
1890 A7=INT(A6/11):B7=INT(B6/11):C7=INT(C6/11):D7=INT(D6/11):E7=INT(E6/11)
1900 AS=RND((A7+AH+RND(2)))-2:BS=RND((B7+BH+RND(2)))-2:CS=RND((C7+CH+RND(2)))-2:DS=RND((D7+DH+RND(2)))-2:ES=RND((E7+EH+RND(2)))-2
1910 IFAS<0 THENAS=0
1920 IFBS<0 THENBS=0
1930 IFCS<0 THENCS=0
1940 IFDS<0 THENDS=0
1950 IFES<0 THENES=0
1960 IFAS>4 THENAS=4
1970 IFBS>4 THENBS=4
1980 IFCS>4 THENCS=4
1990 IFDS>4 THENDS=4
2000 IFES>4 THENES=4
2010 RETURN
2020 PRINT:PRINT" READY FOR THE TABLE?";
2030 IN$=INKEY$:IFIN$=""THEN2030
2040 SOUND190,2:SOUND200,2
2050 CLS:PRINT"  LEAGUE DIVISION";DI;"   ";WK:PRINT:PRINT
2060 PRINT"              P  W  D  L  PT"
2070 X1=AP:X2=BP:X3=CP:X4=DP:X5=EP
2080 FORI=1TO 5
2090 ONI GOTO 2100,2110,2120,2130,2140
2100 W(I)=AP:NEXT
2110 W(I)=BP:NEXT
2120 W(I)=CP:NEXT
2130 W(I)=DP:NEXT
2140 W(I)=EP:NEXT
2150 GOSUB2240
2160 FORI=1 TO5
2170 IFW(I)=0 THEN AP=X1:BP=X2:CP=X3:DP=X4:EP=X5:IFWK=20 THEN4000ELSEGOTO3590
2180 IFW(I)=AP THENPRINTA$;TAB(13)AG;AW;AD;AL;AP:AP=0
2190 IFW(I)=BP THENPRINTB$;TAB(13)BG;BW;BD;BL;BP:BP=0
2200 IFW(I)=CP THENPRINTC$;TAB(13)CG;CW;CD;CL;CP:CP=0
2210 IFW(I)=DP THENPRINTD$;TAB(13)DG;DW;DD;DL;DP:DP=0
2220 IFW(I)=EP THENPRINTE$;TAB(13)EG;EW;ED;EL;EP:EP=0
2230 NEXT:AP=X1:BP=X2:CP=X3:DP=X4:EP=X5:IFWK=20 THEN 4000ELSEGOTO3590
2240 M=5
2250 F=0:FORI=1 TO4
2260 IFW(I)>=W(I+1)THEN2280
2270 T=W(I):W(I)=W(I+1):W(I+1)=T:F=1
2280 NEXTI:IFF=1 THENM=M-1:GOTO2250
2290 RETURN
2300 BH=1:CH=1:AH=0:EH=0:GOSUB1830
2310 CLS
2320 PRINT:PRINT:PRINT"    WEEK";WK
2330 PRINT:PRINT" ";B$;BS;
2340 PRINT" ";A$;AS
2350 IFBS>AS THENBW=BW+1:BP=BP+3:AL=AL+1
2360 IFAS>BS THENAW=AW+1:AP=AP+3:BL=BL+1
2370 IFAS=BS THENAD=AD+1:AP=AP+1:BD=BD+1:BP=BP+1
2380 PRINT" ";C$;CS;
2390 PRINT" ";E$;ES
2400 IFCS>ES THENCW=CW+1:CP=CP+3:EL=EL+1
2410 IFES>CS THEN EW=EW+1:EP=EP+3:CL=CL+1
2420 IFCS=ES THEN CD=CD+1:CP=CP+1:ED=ED+1:EP=EP+1
2430 AG=AG+1:BG=BG+1:CG=CG+1:EG=EG+1
2440 GOTO2020
2450 AH=1:EH=1:CH=0:DH=0:GOSUB1830
2460 CLS
2470 PRINT:PRINT:PRINT"    WEEK";WK
2480 PRINT:PRINT" ";A$;:PRINTAS;
2490 PRINT" ";C$;:PRINTCS
2500 IFAS>CS THENAW=AW+1:AP=AP+3:CL=CL+1
2510 IFCS>AS THEN CW=CW+1:CP=CP+3:AL=AL+1
2520 IFAS=CS THENAD=AD+1:AP=AP+1:CD=CD+1:CP=CP+1
2530 PRINT" ";E$;ES;
2540 PRINT" ";D$;DS
2550 IFES>DS THENEW=EW+1:EP=EP+3:DL=DL+1
2560 IFDS>ES THENDW=DW+1:DP=DP+3:EL=EL+1
2570 IFES=DS THENED=ED+1:DD=DD+1:EP=EP+1:DP=DP+1
2580 AG=AG+1:CG=CG+1:EG=EG+1:DG=DG+1
2590 GOTO2020
2600 EH=1:DH=1:AH=0:BH=0:GOSUB1830
2610 CLS
2620 PRINT:PRINT:PRINT"    WEEK";WK
2630 PRINT:PRINT" ";E$;ES;
2640 PRINT" ";A$;AS
2650 IFES>AS THENEW=EW+1:EP=EP+3:AL=AL+1
2660 IFAS>ES THENAW=AW+1:AP=AP+3:EL=EL+1
2670 IFAS=ES THENAD=AD+1:AP=AP+1:ED=ED+1:EP=EP+1
2680 PRINT" ";D$;DS;
2690 PRINT" ";B$;BS
2700 IFDS>BS THENDW=DW+1:DP=DP+3:BL=BL+1
2710 IFBS>DS THENBW=BW+1:BP=BP+3:DL=DL+1
2720 IFBS=DS THENBD=BD+1:DD=DD+1:BP=BP+1:DP=DP+1
2730 EG=EG+1:AG=AG+1:DG=DG+1:BG=BG+1
2740 GOTO2020
2750 BH=1:DH=1:CH=0:EH=0:GOSUB1830
2760 CLS:PRINT:PRINT:PRINT"    WEEK";WK
2770 PRINT:PRINT" ";B$;BS;
2780 PRINT" ";C$;CS
2790 IFBS>CS THENBW=BW+1:BP=BP+3:CL=CL+1
2800 IFCS>BS THENCW=CW+1:CP=CP+3:BL=BL+1
2810 IFCS=BS THENCD=CD+1:BD=BD+1:CP=CP+1:BP=BP+1
2820 PRINT" ";D$;DS;
2830 PRINT" ";E$;ES
2840 IFDS>ES THENDW=DW+1:DP=DP+3:EL=EL+1
2850 IFES>DS THENEW=EW+1:EP=EP+3:DL=DL+1
2860 IFES=DS THENED=ED+1:DD=DD+1:EP=EP+1:DP=DP+1
2870 BG=BG+1:CG=CG+1:DG=DG+1:EG=EG+1
2880 GOTO2020
2890 AH=1:CH=1:DH=0:BH=0:GOSUB1830
2900 CLS:PRINT:PRINT:PRINT"    WEEK";WK
2910 PRINT:PRINT" ";A$;AS;
2920 PRINT" ";D$;DS
2930 IFAS>DS THENAW=AW+1:AP=AP+3:DL=DL+1
2940 IFDS>AS THENDW=DW+1:DP=DP+3:AL=AL+1
2950 IFDS=AS THENDD=DD+1:AD=AD+1:DP=DP+1:AP=AP+1
2960 PRINT" ";C$;CS;
2970 PRINT" ";B$;BS
2980 IFCS>BS THENCW=CW+1:CP=CP+3:BL=BL+1
2990 IFBS>CS THENBW=BW+1:BP=BP+3:CL=CL+1
3000 IFBS=CS THENBD=BD+1:CD=CD+1:BP=BP+1:CP=CP+1
3010 AG=AG+1:DG=DG+1:CG=CG+1:BG=BG+1
3020 GOTO2020
3030 CH=1:BH=1:AH=0:EH=0:GOSUB1830
3040 CLS:PRINT:PRINT:PRINT"    WEEK";WK
3050 PRINT:PRINT" ";C$;CS;
3060 PRINT"  ";A$;AS
3070 IFCS>AS THENCW=CW+1:CP=CP+3:AL=AL+1
3080 IFAS>CS THENAW=AW+1:AP=AP+3:CL=CL+1
3090 IFAS=CS THENAD=AD+1:CD=CD+1:AP=AP+1:CP=CP+1
3100 PRINT" ";B$;BS;
3110 PRINT" ";E$;ES
3120 IFBS>ES THENBW=BW+1:BP=BP+3:EL=EL+1
3130 IFES>BS THENEW=EW+1:EP=EP+3:BL=BL+1
3140 IFES=BS THENED=ED+1:BD=BD+1:EP=EP+1:BP=BP+1
3150 CG=CG+1:AG=AG+1:BG=BG+1:EG=EG+1
3160 GOTO2020
3170 DH=1:EH=1:AH=0:CH=0:GOSUB1830
3180 CLS:PRINT:PRINT:PRINT"    WEEK";WK
3190 PRINT:PRINT" ";D$;DS;
3200 PRINT" ";A$;AS
3210 IFDS>AS THENDW=DW+1:DP=DP+3:AL=AL+1
3220 IFAS>DS THENAW=AW+1:AP=AP+3:DL=DL+1
3230 IFAS=DS THENAD=AD+1:DD=DD+1:AP=AP+1:DP=DP+1
3240 PRINT" ";E$;ES;
3250 PRINT" ";C$;CS
3260 IFES>CS THENEW=EW+1:EP=EP+3:CL=CL+1
3270 IFCS>ES THENCW=CW+1:CP=CP+3:EL=EL+1
3280 IFES=CS THENED=ED+1:CD=CD+1:EP=EP+1:CP=CP+1
3290 DG=DG+1:AG=AG+1:EG=EG+1:CG=CG+1
3300 GOTO2020
3310 EH=1:DH=1:BH=0:CH=0:GOSUB1830
3320 CLS:PRINT:PRINT:PRINT"    WEEK";WK
3330 PRINT:PRINT" ";E$;ES;
3340 PRINT" ";B$;BS
3350 IFES>BS THENEW=EW+1:EP=EP+3:BL=BL+1
3360 IFBS>ES THENBW=BW+1:BP=BP+3:EL=EL+1
3370 IFBS=ES THENBD=BD+1:ED=ED+1:BP=BP+1:EP=EP+1
3380 PRINT" ";D$;DS;
3390 PRINT" ";C$;CS
3400 IFDS>CS THENDW=DW+1:DP=DP+3:CL=CL+1
3410 IFCS>DS THENCW=CW+1:CP=CP+3:DL=DL+1
3420 IFCS=DS THENCD=CD+1:DD=DD+1:CP=CP+1:DP=DP+1
3430 EG=EG+1:BG=BG+1:DG=DG+1:CG=CG+1
3440 GOTO2020
3450 AH=1:BH=1:EH=0:DH=0:GOSUB1830
3460 CLS:PRINT:PRINT:PRINT"    WEEK";WK
3470 PRINT:PRINT" ";A$;AS;
3480 PRINT" ";E$;ES
3490 IFAS>ES THENAW=AW+1:AP=AP+3:EL=EL+1
3500 IFES>AS THENEW=EW+1:EP=EP+3:AL=AL+1
3510 IFAS=ES THENAD=AD+1:ED=ED+1:AP=AP+1:EP=EP+1
3520 PRINT" ";B$;BS;
3530 PRINT" ";D$;DS
3540 IFBS>DS THENBW=BW+1:BP=BP+3:DL=DL+1
3550 IFDS>BS THENDW=DW+1:DP=DP+3:BL=BL+1
3560 IFBS=DS THENBD=BD+1:DD=DD+1:BP=BP+1:DP=DP+1
3570 AG=AG+1:EG=EG+1:BG=BG+1:DG=DG+1
3580 GOTO2020
3590 PRINT:PRINT" READY TO PROCEED?";
3600 IN$=INKEY$:IF IN$="" THEN 3600
3610 IFWK=5 ORWK=9 ORWK=15 ORWK=19 THENCLS:GOTO3680
3620 Q=INT(5000*(AP/AG)*(5-DI))
3630 IFQ<2500 THENQ=2500
3640 SOUND180,2:SOUND180,2
3650 CLS6:PRINT@100,"GATE RECEIPTS=$";Q;
3660 CA=CA+Q
3670 FORN=1 TO2000:NEXTN
3680 R2=0:R1=0
3690 GOSUB3940
3700 CLS4:SOUND180,2:SOUND180,2:PRINT@100,"WEEKLY WAGE BILL=$";R2;
3710 CA=CA-R2
3720 FORN=1 TO2000:NEXT
3730 R2=0
3740 CLS:GOTO4460
3750 FORN=1 TO30:PRINT@479+N,CHR$(143);:NEXT
3760 PRINT@480,"";:INPUT"WHICH PLAYER";N
3770 IFN<1 ORN>15 THEN3760
3780 AT$(20)=AT$(N)
3790 Q$=RIGHT$(AT$(20),1):Q=VAL(Q$):CA=CA+Q*5000
3800 FORQ=1 TO 20
3810 IFSP$(Q)=" "THENSP$(Q)=AT$(20):AT$(N)=" ":CLS:GOTO950
3820 NEXTQ
3830 AT$(N)=" ":GOTO950
3840 FORN=1 TO30:PRINT@479+N," ";:NEXT
3850 PRINT@480,"";:INPUT"IN WHICH POSITION";N
3860 IFN<1 ORN>12 THEN3850
3870 AT$(20)=AT$(N):AT$(N)=AT$(12):AT$(12)=AT$(20):AT$(20)=" "
3880 CLS:GOTO950
3890 IFWK=4 ORWK=8 ORWK=14 ORWK=18 THEN1660
3900 FOR N=1 TO11
3910 IFAT$(N)=" " THEN CLS:SOUND1,10:PRINT@97,"NO NUMBER";N;"!":FORQ=1 TO2000:NEXTQ:GOTO950
3920 NEXTN
3930 GOTO1660
3940 FORN=1TO11:RA$=RIGHT$(AT$(N),1)
3950 R1=VAL(RA$)
3960 R2=R2+R1
3970 NEXTN
3980 R2=R2*200*(5-DI)
3990 RETURN
4000 FORN=1 TO 5000:NEXT
4010 Q=0
4020 IFAP<BP ANDAP<CP ANDAP<DP ANDAP<EP THENDI=DI+1:CA=CA+50000:GOTO5830
4030 IFAP=>BP ANDAP=>CP ANDAP=>DP ANDAP=>EP THEN4040ELSEPRINT@449,"better luck next season";:CA=CA+50000:FORN=1 TO4000:NEXT:GOTO53904040 FORN=1 TO8:CLSN:SOUNDN*25,N:NEXT
4050 N=RND(8):CLSN:PRINT@N*50,"!CHAMPIONS!";:SOUND50,10:Q=Q+1:IFQ<10 THEN4050
4060 CLS8:PRINT@234,"!CHAMPIONS!";
4070 FORN=1 TO2000:NEXT
4080 IFDI>1 THENDI=DI-1:CA=CA+100000:GOTO5390
4090 IFDI=1 THEN CLS2:PRINT@100,"YOU MADE IT!";:FORN=1 TO10000:NEXT:GOTO5510
4100 FORN=12 TO 16
4110 IFAT$(N)=" "THEN 4140
4120 NEXTN
4130 CLS:PRINT@97,"NO ROOM IN RESERVES!";:SOUND50,5:FORN=1 TO2000:NEXT:GOTO950
4140 PRINT:PRINT"  TRANSFER MARKET"
4150 PRINT:FORN=1 TO20:PRINTN;"..";SP$(N),:NEXT
4160 PRINT@481,"WANT TO BUY?";
4170 IN$=INKEY$:IFIN$=""THEN4170
4180 IFIN$<>"Y"THENCLS:GOTO950
4190 IFIN$="Y"THENPRINT@481,"";:INPUT"WHICH PLAYER (1-20)";N
4200 IFN<1 OR N>20 THENCLS:GOTO4140
4210 IFSP$(N)=" "THENCLS:GOTO4140
4220 CO$=RIGHT$(SP$(N),1)
4230 CO=10000*VAL(CO$)
4240 IFCA-CO<0 THENCLS:PRINT@97,"NOT ENOUGH CASH IN THE BANK!";:SOUND50,5:FORQ=1 TO2000:NEXT:GOTO950
4250 CA=CA-CO
4260 FORQ=12 TO15
4270 IFAT$(Q)=" "THENAT$(Q)=SP$(N):SP$(N)=" ":CLS:GOTO950
4280 NEXT
4290 CLS:PRINT@97,"NO VACANCIES!";:CA=CA+CO:SOUND50,5:CLS:GOTO950
4300 CLS:PRINT:PRINT" RESERVES"
4310 PRINT:FOR N=12 TO 15
4320 PRINTN;"..";AT$(N)
4330 NEXTN
4340 PRINT@481,"uSE, sELL OR nEITHER?";
4350 IN$=INKEY$:IFIN$=""THEN4340
4360 IFIN$="S"THEN3750
4370 IFIN$="N"THENCLS:GOTO950
4380 IFIN$<>"U"THEN4340
4390 FORN=480 TO510:PRINT@N," ";:NEXT
4400 PRINT@480," ";:INPUT"WHICH RESERVE";N
4410 IFN<12 ORN>15 THEN4300
4420 AT$(20)=AT$(N):AT$(N)=AT$(12):AT$(12)=AT$(20):AT$(20)=" ":CLS:GOTO950
4430 FORQ=1 TO15:IFAT$(Q)=" "THEN1410
4440 NEXTQ
4450 CLS:PRINT@97,"NO VACANCIES!";:SOUND50,5:FORQ=1 TO 2000:NEXT:CLS:GOTO950
4460 CLS5:FORN=1 TO5:SOUND40,1:SOUND50,1:NEXT:PRINT@42,"weekly news";
4470 FORN=1 TO3
4480 IFSS$(N)<>" "THEN5210
4490 NEXTN
4500 Q=RND(24):SOUND150,1:ONQ GOSUB4540,4550,4550,4550,4560,4560,4580,4590,4600,4610,4610,4620,4630,4640,4650,4670,5030,5080,5040,5060,5070,5050,4660,4770
4510 FORN=1 TO3000:NEXT:Q=RND(23):SOUND150,1:ONQ GOSUB4680,4690,4700,4720,4650,4730,4740,4750,4760,4770,4780,4790,4800,4810,4840,5090,5100,5150,5110,5140,5120,5130,4550
4520 FORN=1 TO3000:NEXT:Q=RND(23):SOUND150,1:ONQ GOSUB4850,4860,4930,4870,4880,4920,4820,4830,4890,4900,4880,4550,4910,4940,5000,5010,5020,4950,4960,4990,4970,4980,4550
4530 FORN=1 TO3000:NEXT:GOTO950
4540 PRINT@192," PROGRAMME ADS MAKE $7500        ":CA=CA+7500:RETURN
4550 SS=RND(11):IFAT$(SS)=" "THENRETURNELSEPRINT@192," NO.";SS;"SUSPENDED 1 WEEK         ":GOTO5360
4560 SS=RND(11):IFAT$(SS)=" " THENRETURNELSEPRINT@192,SS;"BREAKS LEG - MISSES SEASON      ":GOTO5170
4570 RETURN
4580 PRINT@192," FANS RIOT - $25000 DAMAGE       ":CA=CA-25000:RETURN
4590 PRINT@192," ILLEGAL APPROACH - FINE $10000  ":CA=CA-10000:RETURN
4600 SS=RND(11):IFAT$(SS)=" "THENRETURNELSEPRINT@192," NO.";SS;"CATCHES FLU-AWAY 1 WEEK    ":GOTO5360
4610 PRINT@192," SUPPORTERS' CLUB GIVES $10000   ":CA=CA+10000:RETURN
4620 PRINT@192," SPONSORS DONATE $20000          ":CA=CA+20000:RETURN
4630 SS=RND(11):IFAT$(SS)=" "THENRETURNELSEPRINT@192," NO.";SS;"IN JAIL UNTIL MONDAY      ":GOTO5360
4640 PRINT@192," RATES BILL - $7500             ":CA=CA-7500:RETURN
4650 PRINT@192," EXECUTIVE BOX LEASED-$7500     ":CA=CA+7500:RETURN
4660 PRINT@192," BILL FOR CARTILAGE OPERATION-   $300":CA=CA-300:RETURN
4670 PRINT@192," UNREGISTERED PLAYER-FINED 1PT  ":AP=AP-1:RETURN
4680 PRINT@192," $5000 LOAN REPAID TO DIRECTOR  ":CA=CA-5000:RETURN
4690 PRINT@192," GROUND ADVERTISERS PAY $10000  ":CA=CA+10000:RETURN
4700 SS=RND(11):IFAT$(SS)=" "THENRETURNELSEPRINT@192," NO.";SS;"WALKS OUT                     ":GOTO5170
4710 RETURN
4720 N=INT(CA/10):PRINT@192," BANK INTEREST-$";N;"             ":CA=CA+N:RETURN
4730 PRINT@192," CAR PARK LESSEES PAY $5000     ":CA=CA+5000:RETURN
4740 PRINT@192," NEW PLAYING KIT-$750            ":CA=CA-750:RETURN
4750 PRINT@192," ELECTRICITY BILL-$500          ":CA=CA-500:RETURN
4760 PRINT@192," GAS BILL-$400                  ":CA=CA-400:RETURN
4770 CZ=CZ+1:IFCZ>10 THENRETURNELSEPRINT@192," STAR TRIALIST SIGNS-IF ROOM":PRINT@224," IN SQUAD":GOTO5290
4780 PRINT@192," POLICE BILL-$2000              ":CA=CA-2000:RETURN
4790 PRINT@192," MANAGER'S NEW CAR-$7000        ":CA=CA-7000:RETURN
4800 PRINT@192," NEW DIRECTOR LENDS $5000       ":CA=CA+5000:RETURN
4810 PRINT@192," PROGRAMME SALES-$5000          ":CA=CA+5000:RETURN
4820 PRINT@192," DIRECTORS GIVE YOU VOTE OF":PRINT@224," CONFIDENCE":RETURN
4830 PRINT@192," REFRESHMENT BARS MAKE $500      ":CA=CA+500:RETURN
4840 PRINT@192," YOUR SHARE OF TV POOL-$25000    ":CA=CA+25000:RETURN
4850 PRINT@192," LOTTERY RAISES $10000          ":CA=CA+10000:RETURN
4860 PRINT@192," MIDWEEK MILK CUP MATCH":PRINT@224," MAKES $";WK*250:CA=CA+(WK*250):RETURN
4870 SS=RND(11):IFAT$(SS)=" " THENRETURNELSEPRINT@192," NO.";SS;"DRUNK IN NIGHT CLUB-":PRINT@224," FINED WEEK'S WAGES":CA=CA+300:RETURN
4880 PRINT@192," JIMMY HILL SAYS YOU'RE GREAT-":PRINT@224," MORALE PLUMMETS":RETURN
4890 IFCA<0 THENRETURNELSEPRINT@192," INLAND REVENUE WANTS $";INT(CA/4);"                   ":CA=CA-INT(CA/4):RETURN
4900 PRINT@192," TELEPHONE BILL-$500             ":RETURN
4910 SS=RND(11):IFAT$(SS)=" "THENRETURNELSEPRINT@192," ";SS;"BREAKS ARM-OUT FOR SEASON      ":GOTO5170
4920 PRINT@192," YOUTH TEAM PLAYER SOLD-$30000   ":CA=CA+30000:RETURN
4930 PRINT@192," POP CONCERT AT GROUND-$15000   ":CA=CA+15000:RETURN
4940 PRINT@192," PROGRAMME PRINTING COSTS $2000  ":CA=CA-2000:RETURN
4950 PRINT@192," OFFICES DECORATED-$800          ":CA=CA-800:RETURN
4960 PRINT@192," HOTEL BILL FROM LAST AWAY       MATCH-$450":CA=CA-450:RETURN
4970 PRINT@192," LATE FOR LAST MATCH -           FINED $1000":CA=CA-1000:RETURN
4980 PRINT@192," TAKE TEAM FOR BREAK IN SPAIN -  $2000":CA=CA-2000:RETURN
4990 PRINT@192," NEW MOWER - $850                ":CA=CA-850:RETURN
5000 PRINT@192," LAUNDRY BILL - $350             ":CA=CA-350:RETURN
5010 PRINT@192," BRIAN CLOUGH SAYS YOU'RE        RUBBISH - MORALE SOARS":RETURN
5020 IFCA<0 THENRETURNELSEPRINT@192," NATIONAL INSURANCE WANTS        $";INT(CA/10):CA=CA-INT(CA/10):RETURN
5030 PRINT@192," SOUVENIR SHOP MAKES $400        ":CA=CA+400:RETURN
5040 PRINT@192," WRITE OFF FIDDLING BY GATE      MEN - $300":CA=CA-300:RETURN
5050 PRINT@192," NEW CRUSH BARRIERS NEEDED -     $1500":CA=CA-1500:RETURN
5060 IFCA<0 THEN RETURNELSEPRINT@192," GENERAL REPAIRS - $";INT(CA/10);"                    ":CA=CA-INT(CA/10):RETURN
5070 PRINT@192," BOB WILSON TIPS YOU FOR         ENGLAND JOB":RETURN
5080 PRINT@192," MIDWEEK COUNTY CUP MATCH        MAKES $";WK*25:CA=CA+WK*25:RETURN
5090 PRINT@192," U.S. CLUB MAKES H.P. PAYMENT     FOR PLAYER-$2500":CA=CA+2500:RETURN
5100 PRINT@192," INSURANCE PAY-OUT FOR INJURED   PLAYER-$5000":CA=CA+5000:RETURN
5110 PRINT@192," BOILER REPAIRS - $240           ":CA=CA-240:RETURN
5120 PRINT@192," DISC JOCKEY NEEDS NEW           RECORDS - $50":CA=CA-50:RETURN
5130 PRINT@192," STATIONERY BILL - $200         ":CA=CA-200:RETURN
5140 PRINT@192," DRINKS FOR BOARD ROOM - $150   ":CA=CA-150:RETURN
5150 PRINT@192," INSURANCE PREMIUM-$5000         ":CA=CA-5000:RETURN
5160 RETURN
5170 FORPP=1 TO20
5180 IFSP$(PP)=" "THENSP$(PP)=AT$(SS):AT$(SS)=" ":GOTO4570
5190 NEXTPP
5200 AT$(SS)=" ":GOTO4570
5210 FORNN=12 TO15
5220 IFAT$(NN)=" "THENAT$(NN)=SS$(N):SS$(N)=" ":GOTO4470
5230 NEXTNN
5240 IFSS$(N)<>" "THEN5260
5250 GOTO4470
5260 FORNN=1 TO20
5270 IFSP$(NN)=" "THENSP$(NN)=SS$(N):SS$(N)=" ":GOTO4470
5280 NEXTNN
5290 FOR N=1 TO15
5300 IFAT$(N)=" " THENAT$(N)=TT$(CZ):GOTO5160
5310 NEXTN
5320 FORN=1 TO20
5330 IFSP$(N)=" " THENSP$(N)=TT$(CZ):GOTO5160
5340 NEXTN
5350 GOTO5160
5360 FORPP=1 TO3
5370 IFSS$(PP)=" "THENSS$(PP)=AT$(SS):AT$(SS)=" ":RETURN
5380 NEXTPP
5390 AP=0:BP=0:CP=0:DP=0:EP=0:WK=0:AG=0:BG=0:CG=0:DG=0:EG=0:AW=0:BW=0:CW=0:DW=0:EW=0:AD=0:BD=0:CD=0:DD=0:ED=0:AL=0:BL=0:CL=0:DL=0:EL=0
5400 PO=PO+1
5410 IFDI=4THENB$="WIMBLEDON":C$="ALDERSHOT":D$="BRADFORD":E$="BURY":F$="MANSFIELD"
5420 IFDI=3 THENB$="CARDIFF":C$="WREXHAM":D$="OXFORD":E$="MILLWALL":F$="PRESTON"
5430 IFDI=2 THENB$="DERBY":C$="QPR":D$="BOLTON":E$="CARLISLE":F$="GRIMSBY"
5440 IFDI=1 THENB$="LIVERPOOL":C$="ARSENAL":D$="MAN UTD":E$="FOREST":F$="EVERTON"
5450 IFA$=B$ THENB$=F$
5460 IFA$=C$ THENC$=F$
5470 IFA$=D$ THEND$=F$
5480 IFA$=E$ THENE$=F$
5490 SE=SE+1
5500 IFPO=1 THEN160 ELSE940
5510 DATACELTIC,HAMBURG,ANDERLECHT,REAL MADRID,JUVENTUS,FEYENOORD,SP LISBON
5520 FORN=1 TO7
5530 READ EU$(N):NEXTN
5540 FORQ=1 TO7
5550 N=RND(7)
5560 IFEU$(N)=" "THEN5550
5570 E2$(Q)=EU$(N)
5580 EU$(N)=" "
5590 NEXTQ
5600 CLS:FORN=1 TO10:SOUND190,1:SOUND200,1:NEXTN:PRINT@97,"EUROPEAN CUP SEMI-FINALS"
5610 FORN=200 TO100 STEP-10:SOUNDN,1:NEXT:FORN=100 TO200 STEP10:SOUNDN,1:NEXT
5620 FORN=1 TO2000:NEXT
5630 N=RND(3):M=RND(3):L=RND(3):K=RND(3)
5640 IFN=M THENN=N+1
5650 IFL=K THENL=L+1
5660 PRINT:PRINT:PRINT:PRINT" ";A$;N;" ";E2$(1);M
5670 PRINT
5680 PRINT" ";E2$(2);L;" ";E2$(3);K
5690 FORQ=1 TO5000:NEXT
5700 CLS3
5710 SOUND190,2:SOUND200,2:PRINT@100,"THE FINAL";
5720 FORQ=1 TO3000:NEXT
5730 A=RND(3):B=RND(3)
5740 IFA=B THENA=A+1
5750 CLS
5760 PRINT:PRINT:PRINT
5770 IFN>M THENPRINT" ";A$;ELSEPRINTE2$(1);
5780 PRINTA;
5790 IFL>K THENPRINT" ";E2$(2);ELSEPRINTE2$(3);
5800 PRINTB
5810 FORQ=1 TO3000:NEXT:IFN>M ANDA>B THENPRINT@450,"WATCH OUT, CLOUGHIE!"ELSEGOTO6080
5820 END
5830 CLS0
5840 IFDI=5 THENDI=4:PRINT@100,"RE-ELECTED..THIS TIME!";:FORQQ=1 TO3000:NEXTQQ:GOTO5390
5850 PRINT@100,"RELEGATION!";:FORQQ=1 TO3000:NEXTQQ:GOTO5390
5860 PMODE4,1:PCLS
5870 LINE(4,180)-(12,8),PSET,BF
5880 LINE(12,8)-(250,16),PSET,BF
5890 LINE(250,16)-(242,180),PSET,BF
5900 SCREEN1,1
5910 DRAW"BM35,26;L13;D150;R13"
5920 SOUND1,1
5930 DRAW"BM+10,+0;U150;D75;R13;U75;D150"
5940 SOUND25,2
5950 DRAW"BM+10,+0;U150;R13;D75;L13;R13;D75"
5960 SOUND50,3
5970 DRAW"BM+10,+0;U150;R13;D150;U150;R13;D150":SOUND75,4
5980 DRAW"BM+10,+0;U150;R13;D75;L13":SOUND100,5
5990 DRAW"BM+23,-75;D150":SOUND125,6
6000 DRAW"BM+10,+0;U150;R13;D150;L13":SOUND150,7
6010 DRAW"BM+23,+0;U150;R13;D150":SOUND175,8
6020 DRAW"BM+13,+0;R13;U75;L13;U75;R13":SOUND200,9
6030 DRAW"BM+10,-0;D135;BM+0,+5;D10
6040 FORN=1 TO3500:NEXT
6070 RETURN
6080 DI=1:CA=CA+100000
6090 CLS:PRINT@97,"OH WELL, THERE'S ALWAYS         NEXT SEASON!":FORN=1 TO3000:NEXT:GOTO5390
9000 FORN=1TO400:NEXT:CSAVE"CHAMPS!"
9010 IFINKEY$=""THEN9010
9020 MOTORON
9030 IFINKEY$=""THEN9030
9040 MOTOROFF
9050 IFINKEY$=""THEN9050
9060 RUN9000
