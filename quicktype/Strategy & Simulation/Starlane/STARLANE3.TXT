0 CLEAR500:DIMCC,ZZ,Z$,I$,LL:GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):LL=LL+1:ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
3 I$=I$+Z$:IFRIGHT$(Z$,1)<>"."THENRETURN
4 Z$=I$:GOSUB1:IFLL>10THENLL=0:PRINT:INPUT"HIT enter TO CONTINUE";Z$:CLS
5 Z$="":I$="":RETURN
40 DIMM(10,13),S(5,4),N$(5),D1(5),S1(5),Q(5),M$(12),C$(25)
50 DIMC1$(25),C2$(25)
60 DATA1,"'ALTAIR STARWAYS'"
70 DATA2,"'BETELGEUSE,LTD.'"
80 DATA3,"'CAPELLA FREIGHT CO.'"
90 DATA4,"'DENEBOLA SHIPPERS'"
100 DATA5,"'ERIDANI EXPEDITERS'"
120 CLS
130 FOR I=1 TO 5
140 FOR J=1 TO 4
150 S(I,J)=0: D1(I)=0: S1(I)=100: Q(I)=0: B(I)=6000
160 NEXT J
170 NEXT I
180 L$=".+*ABCDE"
190 M$="ABCDEFGHIJKL"
195 PRINT"********** STAR LANES **********"
200 CC=RND(-(PEEK(9)*256+PEEK(10)))
230 FOR I=1 TO 9
240 FOR J=1 TO 12
250 IF INT(20*RND(0)+1)<>10 THEN M(I,J)=1:GOTO260
251 M(I,J)=3
260 NEXT J
270 NEXT I
290 INPUT"HOW MANY PLAYERS (2-4)";P1
300 PRINT
310 INPUT"DO YOU NEED INSTRUCTIONS";Q$
320 IFLEFT$(Q$,1)="Y"THENGOSUB3410
330 CLS
340 FOR I=1 TO P1
350 PRINT "PLAYER",I,
370 INPUT "WHAT IS YOUR NAME";P$
380 IF I=1 THEN P1$=P$
390 IF I=2 THEN P2$=P$
400 IF I=3 THEN P3$=P$
410 IF I=4 THEN P4$=P$
420 NEXT I
430 CLS
440 Z$="NOW I WILL DECIDE WHO GOES FIRST...":GOSUB1
450 PRINT
460 I=INT(P1*RND(0)+1)
470 GOSUB 490
480 GOTO550
490 REM PICK FIRST PLAYER
500 ON I GOTO 510,520,530,540: REM - IDENTIFY PLAYER
510 Z$=P1$:P5$=P1$:RETURN 
520 Z$=P2$:P5$=P2$:RETURN 
530 Z$=P3$:P5$=P3$:RETURN 
540 Z$=P4$:P5$=P4$:RETURN 
550 Z$=Z$+" IS THE FIRST PLAYER TO MOVE.":GOSUB1
560 FORW=1TO2500:NEXT
570 K=0
580 P=I:GOTO610
590 K=K+1:IF K=48 THEN 4450
600 P=P+1:IF P=P1+1 THEN P=1
610 FOR I=1 TO 5: REM SELECT 5 LEGAL MOVES
620 R(I)=INT(9*RND(0)+1)
630 C(I)=INT(12*RND(0)+1)
640 FOR I1=I-1 TO 0 STEP -1
650 IF R(I)=R(I1) AND C(I)=C(I1) THEN  620
660 NEXT I1
670 IF M(R(I),C(I))>1 THEN 620
680 FOR I1=1 TO 5
690 IF Q(I1)=0 THEN  870
700 NEXT I1
710 IF M(R(I),C(I)+1)>3 THEN870
720 IF M(R(I),C(I)-1)>3 THEN870
730 IF M(R(I)+1,C(I))>3 THEN870
740 IF M(R(I)-1,C(I))>3 THEN870
750 A1=M(R(I),C(I)+1)
760 A2=M(R(I),C(I)-1)
770 A3=M(R(I)+1,C(I))
780 A4=M(R(I)-1,C(I))
790 IF A1=2 AND A2<4 AND A3<4 AND A4<4 THEN620
800 IF A2=2 AND A1<4 AND A3<4 AND A4<4 THEN620
810 IF A3=2 AND A1<4 AND A2<4 AND A4<4 THEN620
820 IF A4=2 AND A1<4 AND A2<4 AND A3<4 THEN620
830 IF A1=3 AND A2<4 AND A3<4 AND A4<4 THEN620
840 IF A2=3 AND A1<4 AND A3<4 AND A4<4 THEN620
850 IF A3=3 AND A1<4 AND A2<4 AND A4<4 THEN620
860 IF A4=3 AND A1<4 AND A2<4 AND A3<4 THEN620
870 NEXT I
880 GOSUB 2060
890 REM P 
900 I=P
910 GOSUB 490
920 Z$=Z$+", HERE ARE YOUR LEGAL MOVES":GOSUB1
930 REM P
940 FOR I=1 TO 5
950 PRINT MID$(STR$(R(I)),2)MID$(M$,C(I),1)" ";
960 NEXT I
970 PRINT
980 INPUT"WHAT IS YOUR MOVE";R$
990 IF LEN(R$)=0 THEN R$="S"
1000 IF LEFT$(R$,1)="M" THEN R$="":GOTO1010
1001 GOTO1030
1010 GOSUB 2060
1020 GOTO 900
1030 IF LEFT$(R$,1)="S" THEN R$="":GOTO1040
1031 GOTO1060
1040 GOSUB 3230
1050 GOTO 900
1060 IF LEN(R$)<>2 THEN 1110
1070 IF ASC(MID$(R$,2,1))-64<1 THEN 1110
1080 IF ASC(MID$(R$,2,1))-64>12 THEN 1110
1081 GOTO1120
1090 IF VAL(R$)<1 THEN 1110
1100 IF VAL(R$)>9 THEN 1110
1110 PRINT "try again":GOTO 980
1120 R=VAL(LEFT$(R$,1))
1130 C=ASC(RIGHT$(R$,1))-64
1140 FOR I=1 TO 5
1150 IF R=R(I) AND C=C(I) THEN  1190
1160 NEXT I
1170 PRINT"NOT IN THE LIST..."
1180 GOTO 980
1190 A1=M(R-1,C)
1200 A2=M(R+1,C)
1210 A3=M(R,C+1)
1220 A4=M(R,C-1)
1230 IF A1<=1 AND A2<=1 AND A3<=1 AND A4<=1 THEN M(R,C)=2:GOTO1240
1231 GOTO1250
1240 GOTO 1760
1250 IF A1>3 AND A2>3 AND A2<>A1 THEN GOSUB 2220:REM - LINE 2090 IS
1260 IF A1>3 AND A3>3 AND A3<>A1 THEN GOSUB 2220:REM   THE MERGER SUB.
1270 IF A1>3 AND A4>3 AND A4<>A1 THEN GOSUB 2220
1280 IF A2>3 AND A3>3 AND A3<>A2 THEN GOSUB 2220
1290 IF A2>3 AND A4>3 AND A4<>A2 THEN GOSUB 2220
1300 IF A3>3 AND A4>3 AND A4<>A3 THEN GOSUB 2220
1310 IF A1<4 AND A2<4 AND A3<4 AND A4<4 THEN 1410
1320 IF M(R,C)>3 THEN 1760
1330 IF A1>3 THEN I=A1-3
1340 IF A2>3 THEN I=A2-3
1350 IF A3>3 THEN I=A3-3
1360 IF A4>3 THEN I=A4-3
1370 Q(I)=Q(I)+1
1380 S1(I)=S1(I)+100
1390 M(R,C)=I+3
1400 GOTO 1570
1410 FOR I=1 TO 5
1420 IF Q(I)=0 THEN 1460
1430 NEXT I
1440 IF M(R,C)<3 THEN M(R,C)=2
1450 GOTO 1760
1460 CLS
1470 GOSUB 3370
1480 Z$="A NEW SHIPPING COMPANY HAS BEEN FORMED!":GOSUB1
1490 Z$="IT'S NAME IS "
1500 RESTORE
1510 READ N,C$
1520 IF I<>N THEN 1510
1530 Z$=Z$+C$:GOSUB1
1540 S(I,P)=S(I,P)+5
1550 Q(I)=1
1560 PRINT
1570 IF A1=3 THEN S1(I)=S1(I)+500
1580 IF A2=3 THEN S1(I)=S1(I)+500
1590 IF A3=3 THEN S1(I)=S1(I)+500
1600 IF A4=3 THEN S1(I)=S1(I)+500
1610 IF A1=2 THEN S1(I)=S1(I)+100:GOTO1620
1611 GOTO1640
1620 Q(I)=Q(I)+1
1630 M(R-1,C)=I+3
1640 IF A2=2 THEN S1(I)=S1(I)+100:GOTO1650
1641 GOTO1670
1650 Q(I)=Q(I)+1
1660 M(R+1,C)=I+3
1670 IF A3=2 THEN S1(I)=S1(I)+100:GOTO1680
1671 GOTO1700
1680 Q(I)=Q(I)+1
1690 M(R,C+1)=I+3
1700 IF A4=2 THEN S1(I)=S1(I)+100:GOTO1710
1701 GOTO1730
1710 Q(I)=Q(I)+1
1720 M(R,C-1)=I+3
1730 IF S1(I)>=3000 THEN T1=I:GOTO1740
1731 GOTO1750
1740 GOSUB 3100
1750 M(R,C)=I+3
1760 FOR I=1 TO 5
1770 B(P)=B(P)+INT(.05*S(I,P)*S1(I))
1780 NEXT I
1790 FOR I=1 TO 5
1800 IF Q(I)=0 THEN 2040
1810 PRINT"YOUR CURRENT CASH= $";B(P)
1820 PRINT:Z$="BUY HOW MANY SHARES OF "
1830 RESTORE
1840 READ N,C$
1850 IF I<>N THEN 1840
1860 Z$=Z$+C$
1870 Z$=Z$+" AT $"+STR$(S1(I)):GOSUB1
1880 Z$="YOU NOW OWN"+STR$(S(I,P)):GOSUB1
1890 INPUT R3$:IF LEN(R3$)=0 THEN R3$="0"
1900 IF R3$(1,1)="M" THEN R3$="":GOTO1910
1901 GOTO1930
1910 GOSUB 2060
1920 GOTO 1810
1930 IF R3$(1,1)="S" THEN R3$="":GOTO1940
1931 GOTO1960
1940 GOSUB 3230
1950 GOTO 1810
1960 R3=VAL(R3$)
1970 R3$=""
1980 IF R3*S1(I)<=B(P) THEN 2010
1990 Z$="YOU ONLY HAVE $"+STR$(B(P))+" - try again":GOSUB1
2000 GOTO 1810
2010 IF R3=0 THEN 2040
2020 S(I,P)=S(I,P)+R3
2030 B(P)=B(P)-(R3*S1(I))
2040 NEXT I
2050 GOTO 590
2060 CLS:REM SUBROUTINE - PRINT MAP
2070 PRINT TAB(8)"MAP OF THE GALAXY"
2090 PRINT "    A B C D E F G H I J K L"
2100 FOR R2=1 TO 9
2110 PRINT R2;
2120 FORC2=1TO12
2130 PRINT " ";
2140 Z2=M(R2,C2)
2150 IF Z2=0THEN Z2=Z2+1
2160 PRINTMID$(L$,Z2,1);
2180 NEXT
2190 PRINT
2200 NEXT
2210 RETURN 
2220 F1=A1-3: IF F1<0 THEN F1=0:REM  SUBROUTINE - CALCULATES THE
2230 F2=A2-3: IF F2<0 THEN F2=0:REM     SURVIVOR IN THE EVENT
2240 F3=A3-3: IF F3<0 THEN F3=0:REM        OF A MERGER
2250 F4=A4-3: IF F4<0 THEN F4=0
2260 T=Q(F1)
2270 T1=F1
2280 IF Q(F2)>Q(F1) THEN T=Q(F2):GOTO2290
2281 GOTO2300
2290 T1=F2
2300 IF Q(F3)>T THEN T=Q(F3):GOTO2310
2301 GOTO2320
2310 T1=F3
2320 IF Q(F4)>T THEN T=Q(F4):GOTO2320
2321 GOTO2340
2330 T1=F4
2340 IF F1=T1 OR A1<4 THEN 2370
2350 X=F1
2360 GOSUB 2470
2370 IF F2=T1 OR A2<4 THEN 2400
2380 X=F2
2390 GOSUB 2470
2400 IF F3=T1 OR A3<4 THEN 2430
2410 X=F3
2420 GOSUB 2470
2430 IF F4=T1 OR A4<4 THEN 2460
2440 X=F4
2450 GOSUB 2470
2460 RETURN 
2470 CLS
2480 GOSUB 3370:REM SUBROUTINE - PERFORMS CALCULATIONS
2490 RESTORE:REM TO ACCOMPLISH A MERGER
2500 READ N,C$
2510 IF X<>N THEN 2500
2520 C1$=C$
2530 Z$=C1$+" HAS JUST BEEN MERGED INTO "
2550 RESTORE
2560 READ N,C$
2570 IF T1<>N THEN 2560
2580 C2$=C$
2590 Z$=Z$+C2$:GOSUB1
2600 PRINT
2610 Z$="NOTE THE FOLLOWING TRANSACTIONS.":GOSUB1:PRINT
2630 LL=13:Z$="OLD STOCK="+C1$:GOSUB1:Z$="NEW STOCK="+C2$+".":GOSUB3
2660 PRINT"PLAYER  OLD  NEW  HOLD BONUS"
2680 FOR I=1 TO P1
2690 GOSUB 490:Z$=LEFT$(Z$+"      ",6)
2700 PRINTZ$;TAB(7)LEFT$(STR$(S(X,I)),4);TAB(12);LEFT$(STR$(INT((.5*S(X,I))+.5 )),4);
2710 PRINTTAB(17);LEFT$(STR$(S(T1,I)+INT((.5*S(X,I))+.5)),4)TAB(22);
2720 X1=0
2730 FORI1=1TOP1
2740 X1=X1+S(X,I1)
2750 NEXT
2760 PRINTLEFT$(STR$(INT(10*((S(X,I)/X1)*S1(X)))),7)
2770 NEXT I
2780 FOR I=1 TO P1
2790 S(T1,I)=S(T1,I)+INT((.5*S(X,I))+.5)
2800 B(I)=B(I)+INT(10*((S(X,I)/X1)*S1(X)))
2810 NEXT I
2820 FOR I=1 TO 9
2830 FOR J=1 TO 12
2840 IF M(I,J)=X+3 THEN M(I,J)=T1+3
2850 NEXT J
2860 NEXT I
2870 A1=M(R-1,C)
2880 A2=M(R+1,C)
2890 A3=M(R,C+1)
2900 A4=M(R,C-1)
2910 F1=A3-3
2920 IF F1<0 THEN F1=0
2930 F2=A2-3
2940 IF F2<0 THEN F2=0
2950 Q(T1)=Q(T1)+Q(X)
2960 S1(T1)=S1(T1)+S1(X)
2970 IF S1(T1)=>3000 THEN GOSUB 3100
2980 F3=A3-3
2990 IF F3<0 THEN F3=0
3000 F4=A4-3
3010 IF F4<0 THEN F4=0
3020 S1(X)=100
3030 Q(X)=0
3040 FOR I=1 TO P1
3050 S(X,I)=0
3060 NEXT I
3070 PRINT
3080 M(R,C)=T1+3
3090 RETURN 
3100 GOSUB 3370:REM SUBROUTINE - CALCULATES STOCK SPLITS
3110 Z$="THE STOCK OF "
3120 RESTORE
3130 READ N,C$
3140 IF T1<>N THEN 3130
3150 Z$=Z$+C$
3160 Z$=Z$+" HAS SPLIT 2 FOR 1!":GOSUB1
3170 S1(T1)=INT(S1(T1)/2)
3180 PRINT
3190 FOR I1=1 TO P1
3200 S(T1,I1)=2*S(T1,I1)
3210 NEXT I1
3220 RETURN 
3230 CLS
3250 PRINT"STOCK            PRICE   HOLD"
3270 FOR I3=1 TO 5
3280 IF S1(I3)=100 THEN 3340
3290 RESTORE
3300 READ N,C$
3310 IF I3<>N THEN 3300
3320 PRINT MID$(C$,2,15);
3330 PRINT TAB(16)LEFT$(STR$(S1(I3)),8);TAB(24);LEFT$(STR$(S(I3,P)),8)
3340 NEXT I3
3350 RESTORE
3360 RETURN 
3370 SOUND1,1
3380 PRINT"      special announcement!":PRINT
3400 RETURN 
3410 CLS
3420 Z$="   STAR LANES IS A GAME OF INTERSTELLAR TRADING.":GOSUB3
3430 Z$="THE OBJECT OF THE GAME IS TO AMASS THE GREATEST AMOUNT OF MONEY.":GOSUB3
3440 Z$="THIS IS ACCOMPLISHED BY ESTABLISHING VAST,":GOSUB3
3450 Z$=" INTERSTELLAR SHIPPING LANES, AND PURCHASING STOCK IN":GOSUB3
3460 Z$=" THE COMPANIES THAT CONTROL THOSE TRADE ROUTES.":GOSUB3
3470 Z$="DURING THE COURSE OF THE GAME, STOCK APPRECIATES IN VALUE AS":GOSUB3
3480 Z$=" THE SHIPPING COMPANIES BECOME LARGER.":GOSUB3
3490 Z$="ALSO, SMALLER COMPANIES CAN BE MERGED INTO LARGER ONES, AND STOCK":GOSUB3
3500 Z$=" IN THE SMALLER FIRM IS CONVERTED INTO STOCK IN THE":GOSUB3
3510 Z$=" LARGER ONE AS DESCRIBED BELOW.":GOSUB3
3520 Z$="EACH TURN, THE COMPUTER WILL PRESENT THE PLAYER WITH":GOSUB3
3530 Z$=" FIVE PROSPECTIVE SPACES TO OCCUPY (ROWS 1-9, COLUMNS A-L).":GOSUB3
3550 Z$="THE PLAYER WILL DECIDE WHICH SPACE HE WISHES":GOSUB3
3560 Z$=" TO OCCUPY AND TYPE ITS ROW AND COLUMN, I.E., 7E, 8A, ETC.":GOSUB3
3580 Z$="THERE ARE FOUR POSSIBLE MOVES A PLAYER CAN MAKE.":GOSUB3

3670 Z$="   1. HE CAN ESTABLISH AN UNATTACHED OUTPOST- IF HE":GOSUB3
3680 Z$=" SELECTS A SPACE THAT IS NOT ADJACENT TO A STAR, ANOTHER":GOSUB3
3690 Z$=" UNATTACHED OUTPOST, OR AN EXISTING SHIPPING LANE.":GOSUB3
3700 Z$="THIS SPACE WILL BE DESIGNATED WITH A '+'.  HE WILL THEN PROCEED":GOSUB3
3710 Z$=" WITH STOCK TRANSACTIONS, AS LISTED BELOW.":GOSUB3
3720 Z$="   2. HE CAN ADD TO AN EXISTING LANE, IF HE SELECTS A":GOSUB3
3730 Z$=" SPACE THAT IS ADJACENT TO ONE.":GOSUB3
3740 Z$="ONLY ONE EXISTING SHIPPING LANE, THE SPACE HE SELECTS, WILL BE ADDED TO":GOSUB3
3750 Z$=" THAT SHIPPING LANE.":GOSUB3
3760 Z$="IT WILL BE DISIGNATED WITH THE FIRST LETTER OF THE COMPANY THAT OWNS THAT LANE.":GOSUB3
3770 Z$="IF THERE ARE ANY STARS OR UNATTACHED OUTPOSTS ADJACENT TO THE":GOSUB3
3780 Z$=" SELECTED SPACE THEY WILL BE INCORPORATED INTO THE LANE.":GOSUB3
3790 Z$="EACH NEW SQUARE ADJACENT TO A STAR ADDS":GOSUB3
3800 Z$=" $500 PER SHARE AND EACH NEW OUTPOST ADDS $100":GOSUB3
3810 Z$=" TO THE VALUE OF EACH SHARE OF THAT COMPANY.":GOSUB3

3850 Z$="   3. HE MAY ESTABLISH A NEW SHIPPING LANE, IF THERE":GOSUB3
3860 Z$=" ARE FIVE OR LESS EXISTING SHIPPING LANES.":GOSUB3
3870 Z$="THE PLAYER MAY, GIVEN THE PROPER SPACE, ESTABLISH A NEW SHIPPING LANE":GOSUB3
3880 Z$=" BY OCCUPYING A SPACE ADJACENT TO A STAR OR UNATTACHED OUTPOST.":GOSUB3
3900 Z$="HOWEVER, THIS MANY NOT BE DONE ADJACENT TO AN EXISTING SHIPPING LANE.":GOSUB3
3910 Z$="IF HE ESTABLISHES A NEW SHIPPING LANE, HE IS AUTOMATICALLY":GOSUB3
3920 Z$=" ISSUED 5 SHARES IN THE NEW COMPANY.":GOSUB3
3930 Z$="HE MAY THEN PROCEED TO BUY STOCK IN ANY ACTIVE COMPANY,":GOSUB3
3940 Z$=" INCLUDING THE ONE JUST FORMED, AS DESCRIBED BELOW.":GOSUB3
3950 Z$="THE MARKET VALUE OF THE NEW STOCK IS ESTABLISHED BY":GOSUB3
3960 Z$=" THE NUMBER OF STARS AND OCCUPIED SPACES AS DESCRIBED ABOVE.":GOSUB3

4010 Z$="   4.  HE MAY MERGE TWO EXISTING COMPANIES, IF A PLAYER":GOSUB3
4020 Z$="SELECTS A SPACE ADJACENT TO TWO EXISTING SHIPPING LANES.":GOSUB3
4040 Z$="THE LARGER COMPANY TAKES OVER THE SMALLER (IF BOTH COMPANIES ARE THE SAME SIZE":GOSUB3
4050 Z$=" THEN THE SURVIVOR IS DETERMINED BY":GOSUB3
4060 Z$=" ALPHABETICAL ORDER OF THE COMPANY NAMES).":GOSUB3
4080 Z$="THE STOCK OF THE SURVIVING COMPANY IS INCREASED IN VALUE ACCORDING TO THE NUMBER OF":GOSUB3
4090 Z$=" SPACES AND STARS ADDED TO ITS LANE.":GOSUB3
4100 Z$="EACH PLAYERS STOCK IN THE DEFUNCT COMPANY IS EXCHANGED FOR SHARES IN THE":GOSUB3
4110 Z$=" SURVIVOR ON A RATIO OF 2 FOR 1.":GOSUB3
4120 Z$="ALSO, EACH PLAYER IS PAID A CASH BONUS PROPORTIONAL TO THE PERCENTAGE":GOSUB3
4130 Z$=" OF OUTSTANDING STOCK HELD IN THE DEFUNCT COMPANY.":GOSUB3
4140 Z$="NOTE: AFTER A COMPANY BECOMES DEFUNCT IT CAN REAPPEAR ELSEWHERE ON THE":GOSUB3
4160 Z$=" BOARD WHEN, AND IF, A NEW COMPANY IS ESTABLISHED.":GOSUB3

4200 Z$="   NEXT THE COMPUTER CDDS STOCK DIVIDENDS TO THE PLAYER'S":GOSUB3
4210 Z$=" CASH ON HAND (5% OF THE MARKET VALUE OF THE STOCK IN HIS":GOSUB3
4220 Z$=" POSSESSION ), AND OFFERS HIM THE OPPORTUNITY TO PURCHASE":GOSUB3
4230 Z$=" STOCK IN ANY OF THE ACTIVE COMPANIES ON THE BOARD.":GOSUB3
4240 Z$="STOCK MAY NOT BE SOLD, BUT THE MARKET VALUE OF EACH":GOSUB3
4250 Z$=" PLAYER'S STOCK IS TAKEN INTO ACCOUNT AT THE END OF THE":GOSUB3
4260 Z$=" GAME TO DETERMINE THE WINNER.":GOSUB3
4270 Z$="IF THE MARKET VALUE OF A GIVEN STOCK EXCEEDS $3000 AT ANY TIME DURING THE GAME, THAT":GOSUB3
4280 Z$="STOCK SPLITS 2 FOR 1.":GOSUB3
4290 Z$="THE PRICE IS CUT IN HALF, AND THE NUMBER OF SHARES OWNED BY THAT PLAYER IS DOUBLED.":GOSUB3

4310 Z$="NOTE:  THE PLAYER MAY LOOK AT HIS PORTFOLIO AT ANY TIME":GOSUB3
4320 Z$=" DURING THE COURSE OF HIS TURN BY RESPONDING WITH 'STOCK'":GOSUB3
4330 Z$=" TO AN INPUT STATEMENT.":GOSUB3
4340 Z$="LIKEWISE, HE CAN REVIEW THE MAP OF THE GALAXY BY TYPING 'MAP' TO AN INPUT STATEMENT.":GOSUB3
4390 Z$="** GAME ENDS AFTER 48 MOVES **":GOSUB3
4400 PRINT:PRINT 
4410 Z$="PLAYER WITH THE GREATEST NET WORTH AT THAT POINT IS THE WINNER.":GOSUB3
4420 PRINT
4430 FOR W=1 TO 2500:NEXT W
4440 RETURN
4450 CLS
4460 GOSUB 3370
4470 FOR W=1 TO 25:SOUND25,1:NEXT W:SOUND100,1
4490 PRINT"THE GAME IS OVER/FINAL STANDINGS"
4500 PRINT 
4510 SOUND1,1
4520 PRINT "PLAYER  VALUE   CASH    NET"
4540 PRINT 
4550 FOR I=1 TO P1
4560 FOR J=1 TO 5
4570 D1(I)=D1(I)+(S1(J)*S(J,I))
4580 NEXT J
4590 NEXT I
4600 FOR I=1 TO P1
4610 GOSUB 490
4620 PRINT LEFT$(Z$,6)TAB(7)LEFT$(STR$(D1(I)),7)TAB(15)LEFT$(STR$(B(I)),7)TAB(23);
4630 PRINT LEFT$(STR$(D1(I)+B(I)),7)
4640 NEXT I
4650 PRINT
4660 END
5000 REM ***** STAR LANES *****
5010 REM MODIFIED BY S J SINGER
5020 REM FOR ALTAIR BASIC 4.0
5030 REM MC-10 EDITS J GERRIE
