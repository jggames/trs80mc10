10 WL=RND(-(PEEK(9)*256+PEEK(10)))
100 DIMA(3):REM SET UP DIRECTION TABLE 
110 A(0)=2:A(1)=-64:A(2)=-2:A(3)=64:REM VALUES FOR 32 COLUMN SCREEN
111 REM FOR 80-COLUMN SCREEN CHANGE: A(1)=-160:A(3)=160
112 REM FOR THE VIC 22 COLUMN SCREEN CHANGE: A(1)=-44:A(3)=44
120 HL=32:SC=16384:A=SC+65:REM CHARACTER, SCREEN, & START
121 REM FOR UNEXPANDED VIC USE SC=7680:A=SC+45
130 CLS0:WL=PEEK(SC):REM CLEAR SCREEN AND GENERATE MAZE BACKGROUND FIELD
140 FORI=0TO15:POKESC+32*I+31,160:NEXT
150 REM FORI=0TO32:POKESC+I+480,160:NEXT
200 REM GENERATE MAZE 
210 POKEA,4
220 J=INT(RND(0)*4):X=J 
230 B=A+A(J):IFPEEK(B)=WLTHENPOKEB,J:POKEA+A(J)/2,HL:A=B:GOTO220
240 J=(J+1)*-(J<3):IFJ<>XTHEN230
250 J=PEEK(A):POKEA,HL:IFJ<4THENA=A-A(J):GOTO220 
260 POKEA,42
300 PRINT@480,"hit€a€key";:REM MAZE IS DONE. WAIT FOR A KEY TO BE PRESSED.
310 C$=INKEY$:IFC$=""THEN310
320 PRINT@480,"€€€€€€€€€";
1000 REM MAZE MOUSE 
1010 POKEA,42:J=2 
1020 B=A+A(J)/2:IFPEEK(B)=HLTHENPOKEB,42:POKEA,HL:A=B:J=(J+2)+4*(J>1)
1030 J=(J-1)-4*(J=0):GOTO1020
2010 REM ******************
2020 REM *                *
2030 REM * MAZE GENERATOR *
2040 REM * ============== *
2050 REM *     1981       *
2060 REM *                *
2070 REM *   BY C. BOND   *
2080 REM *                *
2090 REM ******************
3000 REM MC-10 JIM GERRIE
