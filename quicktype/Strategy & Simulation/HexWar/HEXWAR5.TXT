0 CLS:CLEAR425:DIMC1,C2,M$:M=16384:GOTO40
1 C1=(PEEK(17024)AND1)*256+PEEK(17025)+M-1:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
2 QV=0:A$=INKEY$:IFA$=""THEN2
3 IFA$="X"THENQV=3
4 IFA$="A"THENQV=7
5 IFA$="S"THENQV=1
6 IFA$="Z"THENQV=5
8 J=QV-128*(A$=" "):RETURN
10 C3=(INT(C2*.5)+2)*32+INT((C1*.75)+.5)+2:PRINT@C3,;:RETURN
20 PRINT@498,"(PRESS A KEY)";
21 A$=INKEY$:C1=RND(0):IFA$=""THEN21
22 PRINT@498,"             ";:RETURN
30 M$=""
31 Z2=INT(Z3/16):Z1=Z3-Z2*16:M$=MID$("0123456789ABCDEF",Z1+1,1)+M$:IFZ2=0THENRETURN
32 Z3=Z2:GOTO31
35 PRINT@13,"HEX WAR";:RETURN
40 GOSUB3310:GOSUB3310:GOSUB35:PRINT@39,;:M$="TODD HEIMARCK 1986":GOSUB1:PRINT@64,;
41 PRINT"   CAN YOU TAKE CONTROL OF THE"
42 PRINT"MINING TURF PIANETSIDE, OR WILL"
43 PRINT"YOUR OPPONENT'S ROBOT CREWS"
44 PRINT"PREVAIL? ARMY STRENGTHS LISTED"
45 PRINT"IN HEXADECIMAL. EACH HAS 3 MOVE"
46 PRINT"POINTS. MOVES THROUGH CONTROLLED";
47 PRINT"HEXES ARE EASIEST. USE space TO"
48 PRINT"TO SELECT ARMY THEN POSITION ON"
49 PRINT"NEIGHBOURING HEX AND HIT space."
50 PRINT"REPEAT. TO STOP, HIT space. USE:";
52 PRINT"aszx TO MOVE WHITE CURSOR, AND"
53 PRINT"AND enter TO END TURN. NEW UNITS";
54 PRINT"ENTER TOP LEFT AND BOTTOM RIGHT IF NOT BLOCKED.";
60 DIMY(31,6,1),L(64,1,3),P(9,9,2),Q(20,1),X(1),C(2),T(12,1)
70 PN=1:ME=31
80 MM=3
90 KA=1/48:KB=1/48:KC=1/32
260 RESTORE:FORJ=1TO12:FORK=0TO1:READT(J,K):NEXT:P(T(J,0),T(J,1),2)=1:NEXT:GOSUB20:CLS
270 DATA8,4,,4,8,,,8,4,,4,8,5,5,3,3,6,3,2,5,5,2,3,6
300 GOSUB600:GOSUB3200:GOSUB600:GOSUB35
340 GOSUB1500
420 GOSUB1900:GOSUB600:GOSUB1710
430 C1=INT(280/8):C2=INT(160/8):GOSUB10:IFPN=1THENPRINT@C3,"";:GOTO432
431 PRINT@C3,"";
432 GOSUB800:C1=35:C2=21:GOSUB10:PRINT@C3," ";
440 GOSUB2100
450 GOSUB2600
460 GOSUB2100
470 GOSUB2200
480 GOSUB2900
490 GOSUB3400
500 PN=1-PN:FT=0:PP=0
510 GOTO420
600 PRINT@0," ARMY";
601 PRINT@32,"------"
602 PRINT"STR               "
603 PRINT" DAM    "
604 PRINT" 1T    "
605 PRINT" 2T   "
606 PRINT"   "
607 PRINT"  "
608 PRINT"  "
609 PRINT"  "
610 PRINT"  "
611 PRINT""
612 PRINT"                  "
670 FORJ=1TO12:GOSUB710:NEXT
680 J=1:IFGN=1THENGOSUB718:J=2:GOSUB715
690 IFGN=2THENGOSUB718:J=3:GOSUB715
700 PRINT@0,;:RETURN
701 K=T(J,0):L=T(J,1):X=(K-L)*2+19:Y=(12-(K+L))*2+3:C1=X:C2=Y:GOSUB10:RETURN
710 K=T(J,0):L=T(J,1):X=(K-L)*2+19:Y=(12-(K+L))*2+3:C1=X:C2=Y:GOSUB10:PRINT@C3,"O";:RETURN
715 GOSUB701:PRINT@C3,"*";:RETURN
718 GOSUB701:PRINT@C3,"#";:RETURN
800 IFX(PN)<2THENRETURN
805 HT=4:HB=4:GOSUB1000
810 MV=0:CT=0:CB=0:PK=0:K=0
820 FORJ=1TOX(PN)-1:IFY(J,0,PN)>0THENIFY(J,6,PN)<1THENK=1:J=X(PN)-1
830 NEXTJ:IFK=0THENA$=CHR$(13)
840 IFA$=CHR$(13)THENPP=2:GOSUB1000:PP=0:A$="":RETURN
850 GOSUB2:IFJ=0THEN840
851 IF(JAND128)THEN1100
852 IF(JAND1)=.THEN840
860 J=(J-1)/2:IF(JAND1)THENB1=HB+J-2:T1=HT:GOTO870
861 T1=HT+1-J:B1=HB
870 IFT1<.ORT1>8THEN840
880 IFB1<.ORB1>8THEN840
890 S1=T1+B1:IFS1<4ORS1>12THEN840
895 HB=B1:HT=T1:GOSUB1000
896 FORZ=0TO5:PRINT@32*Z,"      ";:NEXT
900 QN=P(HT,HB,0):IFQN=0THENPRINT@0,;:GOTO840
901 Q1=P(HT,HB,1)-1
910 PRINT@.,INT(QN);:PRINT@32,"------";
920 FORJ=0TO3:PRINT@(J+2)*32,Y(QN,J,Q1);:NEXT
930 PRINT@.,;:GOTO840
1000 SX=(HT-HB)*2+19:SY=(12-(HT+HB))*2+3:C1=OX:C2=OY:GOSUB10:ONPP+1GOTO1010,1005,1040
1005 IFPN=1THENGOSUB1040:PRINT@C3,"";:OX=SX:OY=SY:RETURN
1006 IFPN=0THENGOSUB1040:PRINT@C3,"";:OX=SX:OY=SY:RETURN
1010 IFFTTHENGOSUB1040:PRINT@C3,"";:OX=SX:OY=SY:RETURN
1020 C1=SX:C2=SY:GOSUB10:OC=PEEK(M+C3):PRINT@C3,"";:OX=SX:OY=SY:FT=1
1030 RETURN
1040 POKEM+C3,OC:C1=SX:C2=SY:GOSUB10:OC=PEEK(M+C3):RETURN
1100 IFPK=1THEN1200
1120 IFP(HT,HB,1)<>PN+1ORP(HT,HB,0)=0THEN810
1130 AN=P(HT,HB,0):IFY(AN,6,PN)THEN810
1140 PK=1:CT=HT:CB=HB:CS=Y(AN,0,PN)
1150 SOUND220,4:PP=1:GOSUB1000:GOTO840
1200 J=((HT=CT)AND(HB=CB))
1210 IFJTHENIFMV=0THEN810
1220 IFJTHENIFMV>0THEN1420
1230 AS=Y(P(HT,HB,0),0,PN):IF((AS>ME)AND(CS>ME))OR((P(HT,HB,1)-1=1-PN)AND(AS>0))THEN840
1240 DT=ABS(CT-HT):DB=ABS(CB-HB):TL=DB+DT:IFNOT((TL=1)OR((CT+CB=HT+HB)AND(DT=1)))THEN840
1250 MG=P(HT,HB,0):IFMG=0THEN1300
1260 FORJ=0TO3:Y(MG,J,PN)=Y(MG,J,PN)+Y(AN,J,PN):Y(AN,J,PN)=0:NEXTJ
1270 Y(MG,6,PN)=1:P(CT,CB,0)=0
1280 CS=Y(MG,0,PN):AN=MG:MV=MM+1
1290 GOTO1380
1300 N8=P(HT,HB,1)-1:MV=MV+1:IFN8<>PNTHENMV=MV+1
1310 P(CT,CB,0)=.
1320 P(HT,HB,0)=AN:P(HT,HB,1)=PN+1:Y(AN,4,PN)=HT:Y(AN,5,PN)=HB:IFMV>=MMTHENY(AN,6,PN)=1
1330 K=0:FORJ=-1TO1STEP2:J1=HT+J:J2=HB+J:J3=HB-J:IFJ1<0ORJ1>8THEN1340
1331 IFP(J1,HB,0)>.THENIFP(J1,HB,1)=2-PNTHENK=1:J=1:GOTO1360
1340 IFJ2<0ORJ2>8THEN1350
1341 IFP(HT,J2,0)>.THENIFP(HT,J2,1)=2-PNTHENK=1:J=1:GOTO1360
1350 IFJ3<0ORJ3>8ORJ1<0ORJ1>8THEN1360
1351 IFP(J1,J3,0)>.THENIFP(J1,J3,1)=2-PNTHENK=1:J=1
1360 NEXTJ
1370 IFK=1THENY(AN,6,PN)=1:MV=MM+1
1380 A=PN:J=CT:K=CB:C=0:D=0:GOSUB1830
1390 J=HT:K=HB:C=CS:D=Y(AN,6,PN):GOSUB1440
1400 CT=HT:CB=HB
1410 IFMV<MMTHEN840
1420 Y(AN,6,PN)=1:J=HT:K=HB:C=CS:D=1:GOSUB1440
1430 PP=0:GOSUB1000:SOUND100,1:GOTO810
1440 GOSUB1830:Z3=PEEK(M+C3):IFZ3<>207THENOC=Z3:PRINT@C3,MID$("",PN+1,1);
1450 RETURN
1500 RESTORE:FORC1=1TO24:READC2:NEXT:FORJ=0TO1:X(J)=5:FORK=1TO4:READA,B,C
1510 Y(K,0,J)=A:Y(K,4,J)=B:Y(K,5,J)=C:P(B,C,0)=K:P(B,C,1)=J+1
1520 NEXTK,J
REM BLUE
1540 DATA64,2,8,64,3,7,64,5,6,64,6,6
REM MAGENTA
1550 DATA64,2,2,64,3,2,64,5,1,64,6,0
REM SET RANDOM REINFORCEMENTS
1610 FORJ=0TO1:FORK=0TO20
1620 A=INT(RND(0)*K*3):FORL=1TO5:A=A+INT(RND(0)*21-8):NEXTL:IFA<16THENA=0:GOTO1630
1621 A=(A+K*8)AND254
1630 Q(K,J)=A:NEXTK,J
1640 RETURN
REM ARMIES->MAP
1710 FORJ=0TO8:FORK=0TO8
1720 A=P(J,K,1):IFATHENA=A-1:GOSUB1800
1730 NEXTK,J
1740 FORA=0TO1:E=13+A*12:F=A*21:DX=2-4*A:D=.
1750 FORJ=0TO8:C=Q(J,A):GOSUB1840
1760 E=E+DX*2:IFJ>3THENF=F+DX:E=E-DX
1770 NEXTJ,A
1780 RETURN
1800 B=P(J,K,0)
1810 C=Y(B,0,A)
1820 D=Y(B,6,A)
REM T&B TO X/Y
1830 E=(J-K+10)*2-1:F=(13-J-K)*2+1
1840 IFATHENC1=E:C2=F:GOSUB10:PRINT@C3-1,"  ";:GOTO1850
1841 C1=E:C2=F:GOSUB10:PRINT@C3-1,"  ";
1850 IFC=0THENRETURN
1870 C1=E:C2=F:GOSUB10:PRINT@C3,;:Z3=C:GOSUB30:PRINTM$;:IFD=.THENRETURN
1880 IFA=0THENC1=E:C2=F:GOSUB10:PRINT@C3-1,"";:RETURN
1881 IFA=1THENC1=E:C2=F:GOSUB10:PRINT@C3-1,"";
1890 RETURN
1900 SW=0:E=X(PN)-1:IFE<1THENRETURN
1910 FORJ=1TOE-1:IFY(J,0,PN)>=1THEN1970
1930 FORK=JTOE:FORL=0TO6:Y(K,L,PN)=Y(K+1,L,PN):Y(K+1,L,PN)=0:NEXTL
1940 T=Y(K,4,PN):B=Y(K,5,PN):P(T,B,0)=K
1950 NEXTK
1960 X(PN)=X(PN)-1:J=E:SW=1
1970 NEXTJ:IFSWTHEN1900
2000 FORJ=1TOE:Y(J,0,PN)=Y(J,0,PN)+Y(J,2,PN)
2010 Y(J,2,PN)=Y(J,3,PN):Y(J,3,PN)=0
2020 Y(J,6,PN)=0
2030 NEXTJ:K=NX(1-PN):FORJ=1TOK:Y(J,6,1-PN)=0:NEXT
2040 GOSUB2400
2050 IFBP>0THENFORJ=0TO1:FORK=1TOBP:A=L(K,J,0):Y(A,6,J)=Y(A,6,J)+1:NEXTK,J
2060 RETURN
2100 GOSUB2400
2110 A=X(0):IFX(1)>ATHENA=X(1)
2120 FORJ=0TO1:FORK=1TOA:Y(K,6,J)=0:NEXTK,J
2130 GOSUB2050
2140 RETURN
2200 FORJ=0TO1:A=1-J:B=X(J)-1
2210 FORK=1TOB
2220 IFY(K,0,J)>=1THEN2280
2230 Q(2,A)=Q(2,A)+Y(K,2,J)+Y(K,3,J):IFQ(2,A)>255THENC=Q(2,A)-255:Q(3,A)=Q(3,A)+C:Q(2,A)=255
2240 Q(6,A)=Q(6,A)+Y(K,1,J):IFQ(6,A)>255THENC-Q(6,A)-255:Q(7,A)=Q(7,A)+C:Q(6,A)=255
2250 IFP(Y(K,4,J),Y(K,5,J),0)=KTHENIFP(Y(K,4,J),Y(K,5,J),1)=J+1THENP(Y(K,4,J),Y(K,5,J),0)=0
2260 FORL=0TO6:Y(K,L,J)=0:NEXTL
REM EVACUATE INJURED
2280 IFY(K,6,J)>=1THEN2320
2290 Q(4,J)=Q(4,J)+Y(K,1,J):Y(K,1,J)=0
2300 IFQ(4,J)>255THENC=Q(4,J)-255:Q(5,J)=Q(5,J)+C:Q(4,J)=255
2320 NEXTK,J:RETURN
2400 BP=.
2410 FORJ=0TO8:J1=(J-4)*(4-J>.):J2=8-(J>4)*(4-J):FORK=J1TOJ2
2420 A=P(J,K,0)
2430 R=P(J,K,1)
2440 IF(A=0)OR(R=0)THEN2490
2450 IFY(A,0,R-1)<1THEN2490
2460 T=J+1:B=K:GOSUB2500
2470 B=B-1:GOSUB2500
2480 T=T-1:GOSUB2500
2490 NEXTK,J:RETURN
2500 IF(T<0)OR(B<0)OR(T>8)OR(B>8)THENRETURN
2510 PA=P(T,B,0):IFPA=0THENRETURN
2520 IFP(T,B,1)=RTHENRETURN
2530 IFY(PA,0,2-R)<1THENRETURN
2540 BP=BP+1:L(BP,R-1,0)=A:L(BP,2-R,0)=PA:RETURN
2600 IFBP=0THENRETURN
2610 FORJ=1TOBP
2620 FORK=0TO1:A=1-K
2630 AN=L(J,K,0)
2640 AS=Y(AN,0,K):HT=Y(AN,6,K):CT=INT(AS/HT)+1
2650 L(J,A,1)=INT(CT*KA+1)
2660 L(J,A,2)=INT(CT*KB+1)
2670 L(J,A,3)=INT(CT*KC+1)
2680 NEXTK,J
2700 FORJ=1TOBP:J0=L(J,0,0):J1=L(J,1,0)
2710 GOSUB3100
2720 Y(J0,0,0)=Y(J0,0,0)-A*L(J,0,1)
2730 Y(J1,0,1)=Y(J1,0,1)-B*L(J,1,1)
2740 GOSUB3100
2750 C=A*L(J,0,2):Y(J0,0,0)=Y(J0,0,0)-C:Y(J0,1,0)=Y(J0,1,0)+C
2760 C=B*L(J,1,2):Y(J1,0,1)=Y(J1,0,1)-C:Y(J1,1,1)=Y(J1,1,1)+C
2770 GOSUB3100
2780 C=A*L(J,0,3):Y(J0,0,0)=Y(J0,0,0)-C:Y(J0,3,0)=Y(J0,3,0)+C
2790 C=B*L(J,1,3):Y(J1,0,1)=Y(J1,0,1)-C:Y(J1,3,1)=Y(J1,3,1)+C
2800 NEXTJ
2810 RETURN
2900 A=1-PN:B=0
2910 FORJ=0TO8:FORK=0TO8
2920 IFP(J,K,1)=PN+1THENB=B+1
2930 NEXTK,J
2950 Q(1,PN)=Q(1,PN)+B
2955 IFQ(1,PN)>255THENB=Q(1,PN)-255:Q(2,PN)=Q(2,PN)+B:Q(1,PN)=255
2960 T=4:B=PN*8
2970 IFP(T,B,0)THENRETURN
2980 IFP(T,B,1)=PN+1THENQ(0,A)=0:Q(1,A)=0:GOTO3060
2990 J=X(A):IFJ>31THENRETURN
3000 J1=Q(0,A):IFJ1<1THEN3060
3010 X(A)=X(A)+1
3020 P(T,B,0)=J:P(T,B,1)=A+1
3030 Y(J,0,A)=J1
3040 FORK=1TO3:Y(J,K,A)=0:NEXTK
3050 Y(J,4,A)=T:Y(J,5,A)=B
3060 FORK=0TO19:Q(K,A)=Q(K+1,A):NEXTK
3070 Q(20,A)=0
3080 RETURN
3100 A=0:FORC1=1TO6:IFRND(0)<.5THENA=A+1
3110 NEXTC1:B=6-A:RETURN
REM WINDOW
3200 PRINT@0,;
3210 PRINTTAB(12)"SCENARIO":GOSUB3310
3220 PRINTTAB(4)"1> CAPTURE CAPITAL/FAR"
3230 PRINTTAB(4)"2> CAPTURE CAPITAL/NEAR"
3240 PRINTTAB(4)"3> OCCUPY 8 OF 12 CITIES"
3250 PRINTTAB(4)"4> CONTROL 6 OF 12 CITIES"
3260 PRINTTAB(4)"5> OCCUPY 40 OF 61 HEXES":GOSUB3310
3280 A$=INKEY$:IFA$=""THEN3280
3281 GN=VAL(A$):IFGN<1ORGN>5THEN3280
3290 PRINT@(1+GN)*32+3,;:M$=STR$(GN)+">":GOSUB1:FORTD=1TO1000:NEXT:PRINT@0,""
3300 RETURN
3310 FORC1=1TO32:PRINT"*";:NEXT:RETURN
3400 A=0:ONGNGOSUB3430,3450,3480,3490,3580
3410 IFA=0THENRETURN
3411 EN$=C$:EA=A:GOSUB600:GOSUB1710:A=EA
3420 PRINT@480,"PLAYER"A"WINS";:PRINT@448,EN$:GOSUB20:RUN
3429 C$=C$+" CAPTURED THE CAPITAL":RETURN
3430 IFP(T(2,0),T(2,1),1)=1THENA=2:C$="RED":GOTO3429
3440 GOTO3460
3450 IFP(T(3,0),T(3,1),1)=1THENA=2:C$="RED":GOTO3429
3460 IFP(T(1,0),T(1,1),1)=2THENA=1:C$="MAGENTA":GOTO3429
3470 RETURN
3478 C$=C$+" OCCUPIES"+STR$(C1)+" HEXES":RETURN
3479 C$=C$+" HAS CAPTURED"+STR$(C1)+" CITIES":RETURN
3480 L=8:GOTO3500
3490 L=6
3500 C(1)=0:C(2)=0
3510 FORJ=1TO12:T=T(J,0):B=T(J,1)
3520 R=P(T,B,1):C(R)=C(R)+1
3530 IFGN=4THENAN=P(T,B,0):IFR>.THENIFAN=.ORY(AN,6,R-1)>.THENC(R)=C(R)-1
3540 NEXTJ
3550 IFC(1)=>LTHENA=2:C$="RED":C1=C(1):GOTO3479
3560 IFC(2)=>LTHENA=1:C$="MAGENTA":C1=C(2):GOTO3479
3570 RETURN
3580 C(1)=0:C(2)=0
3590 FORJ=0TO8:FORK=0TO8
3600 R=P(J,K,1):C(R)=C(R)+1
3610 NEXTK,J
3620 IFC(1)=>40THENA=2:C$="RED":C1=C(1):GOTO3478
3630 IFC(2)=>40THENA=1:C$="MAGENTA":C1=C(2):GOTO3478
3640 RETURN
3650 REM MC-10 EDITS JIM GERRIE
REM RED BAR 
REM PURPLE BAR 
REM RED TOP 
REM PURPLE TOP 
REM RED CAP 
REM PURPLE CAP  OR  
REM CURSOR  OR  OR 
