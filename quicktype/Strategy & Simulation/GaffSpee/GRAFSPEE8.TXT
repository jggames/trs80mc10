0 CLS:CLEAR350:DIMC1,CC,ZZ,M$,I$,MC:MC=16384:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
1 C1=1:CC=12:ZZ=((PEEK(17024)AND1)*256+PEEK(17025)):FORCC=CCTOC1STEP-1
2 I$=MID$(M$,CC,1):IFI$=" "ORI$=""THENPRINT@ZZ,MID$(M$,C1,CC-C1);:ZZ=ZZ+32:C1=CC+1:CC=C1+12:IFI$=""THENCC=0
3 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 FORZZ=1TO25:NEXT:RETURN
20 PRINT@20,;:M$="€SPEED€€€€€ €€€€€€€€€€€ €FUEL€€€€€€ €€€€€€€€€€€ €HEADING€€€ €€€€€€€€€€€ €SHELLS€€€€ €€€€€€€€€€€ "
30 M$=M$+"€TORPEDOES€ €€€€€€€€€€€ €POSITION€€ €N/S€€€€€€€ €E/W€€€€€€€ €€€€€€€€€€€ €TGT RANGE€ €€€€€€€€€€":GOSUB1
40 PRINT@32*7+23,;:M$=MID$(STR$(SH),2):GOSUB7:PRINT@32*9+23,;:M$=MID$(STR$(T0),2):GOSUB7:RETURN
60 FORC1=0TO4:PRINT@32*C1+20,"€€€€€€€€€€€€";:NEXT:RETURN
100 GOSUB8100
460 GOSUB9000
500 DIMX(10)
510 DIMY(10)
520 DIMZ(10)
530 DIMW(10)
790 GS=8E3
795 SC=0
800 FORN=1TO10
801 CLS 
802 GOSUB8110
805 PRINT@32*1+12,"CAPTAIN";:PRINT@32*2+12,"ƒƒƒƒƒƒƒ";:PRINT@32*3+4,"YOU HAVE CONTACTED YOUR";
806 PRINT@32*4+5,"SUPPLY SHIP THE ALTMARK.";:PRINT@32*6+4,"YOUR SUPPLIES HAVE BEEN";
807 PRINT@32*7+5,"REPLENISHED AND SOME";:PRINT@32*8+5,"REPAIRS TO YOUR SHIP";:PRINT@32*9+5,"HAVE BEEN CARRIED OUT.";
808 PRINT@32*10+4,"YOUR SCORE SO FAR IS";INT(SC);:PRINT@32*12+11," / V  V";:PRINT@32*13+11,"ˆ€ŒŒ€ŒŒ€";
809 GS=GS+INT(RND(0)*4000):IFGS>8E3THENGS=8E3
810 FORS=1TO10
820 Z(S)=INT(RND(0)*4+N)
825 IFZ(S)>5THENZ(S)=5
830 W(S)=(4000*ABS(Z(S)=1))+(6000*ABS(Z(S)=2ORZ(S)=4))+(8E3*ABS(Z(S)=3))+(1E4*ABS(Z(S)=5))+INT(RND(0)*3000)
840 Y(S)=(INT(RND(0)*3430))+250
850 X(S)=INT(RND(0)*((2E3*ABS(Y(S)<=2500))+(4E3*ABS(Y(S)>3500))+(3E3*ABS(Y(S)>2500ANDY(S)<=3500))))
851 X(S)=X(S)+500+(750*ABS(Y(S)>1E3))+(250*ABS(Y(S)>1250))-(500*ABS(Y(S)>2500))-(250*ABS(Y(S)>3500))
853 NEXTS
854 GOSUB8400
855 CLS0:FU=FU+INT(RND(0)*3000)+2000:T0=T0+INT(RND(0)*20)+5:SH=SH+INT(RND(0)*200)+20
875 IFFU>=8E3THENFU=8E3
876 IFSH>200THENSH=200
877 IFT0>20THENT0=20
878 GOSUB20:HE=6
880 PRINT@32*5+20,;:M$=B$(HE):GOSUB7
890 JJ=1
970 FORI=1TO10+RND(0)*5
980 IFJJ=1THEN1000
990 GOTO1230
1000 REM MAP
1001 PRINT@0,"¡££££££££££€€N€€€€€€";:PRINT@32,"¥«¯¯¯¯¯¯¯¯ª€";:M$="123":GOSUB7:PRINT"€€a€€";
1002 PRINT@2*32,"¥¯¯¯¯¯¯¯¯¯ªW";:M$="8":GOSUB7:PRINT"*";:M$="4":GOSUB7:PRINT"E€f€€";
1003 PRINT@3*32,"¥¯¯¯¯¯¯¯¯¯¯€";:M$="765":GOSUB7:PRINT"€€r€€";:PRINT@4*32,"€€¤¬¯¯¯¯¯¯¯«€S€€€i€€";
1004 PRINT@5*32,"s€a€¯¯¯¯¯¯¯¯¯¯¯«€c€€";:PRINT@6*32,"o€m€¤¯¯¯¯¯¯¯¯¯¯ª€a€€";
1005 CL=1
1010 PRINT@7*32,"u€e€€§¯¯¯¯¯¯¯¯¯¯¢€€€";:PRINT@8*32,"t€r€¡¯¯¯¯¯¯¯¯¯¯¯ª€€€";:PRINT@9*32,"h€i€§¯¯¯¯¯¯¯¯¯¯¯¨€€€";
1011 PRINT@10*32,"€€c¡¯¯¯¯¯¯¯¯¯¯¯¯¢€€€";:PRINT@11*32,"€€a§¯¯¯¯¯¯¯¯¯¯¯¯«€€€";
1020 PRINT@11*32,"€€¡¯¯¯¯¯¯¯¯¯¯¯¯¯¯€¡¢";:PRINT@12*32,"€€¥¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯ª";:PRINT@13*32,"€¡¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯ª";
1021 PRINT@14*32,"¡¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯ª";:PRINT@15*32,"¤¬¨¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¬¨";
1180 SP=18
1185 JJ=0
1190 LL=0
1200 REM BOTTOM INSTRUCTIONS
1220 PRINT@32*1+23,;:M$=MID$(STR$(SP),2)+"KTS":GOSUB7:PRINT"€";
1230 FORI=1TO10+RND(0)*5
1240 PX=INT(EW/120)
1250 PY=43-INT(NS/120)
1260 CX=PX
1270 CY=43-PY
1280 KK=POINT(CX,CY)
1290 IFKK<>3THEN7400
1300 IFRND(0)>.9THENGOSUB6400
1310 SET(PX,43-PY,3):FORZZ=1TO100:NEXT
1320 RESET(PX,43-PY):FORZZ=1TO100:NEXT
1330 I$=INKEY$:IFI$=""THEN1300
1340 VV=HE
1341 HE=ASC(I$)-48
1346 IFHE=29THEN7500
1350 IF(HE<1ORHE>8)ANDHE<>35THEN1300
1360 IFHE=35THEN1440
1370 SA=120
1380 PRINT@5*32+20,;:M$=B$(HE):GOSUB7
1390 GOSUB8010
1395 IFFU<=0THEN7500
1400 GOSUB8005:PRINT@32*3+20,"€€€€€€€€€€";:PRINT@32*3+20,;:M$=STR$(INT(FU))+" TONS":GOSUB7
1410 SET(PX,43-PY,3)
1420 NEXTI
1440 TR=1E7
1445 SET(PX,43-PY,3)
1450 FORS=1TO10
1460 IFX(S)>EW+240ORX(S)<EW-240ORY(S)>NS+240ORY(S)<NS-240THEN1510
1470 TX=X(S)
1480 TY=Y(S)
1490 GOSUB8050
1500 IFTR<24E4THEN4800
1510 SET(PX,43-PY,3)
1515 RESET(PX,43-PY)
1520 NEXTS
1522 IFHE=35THEN1300
1525 HE=VV
1530 NEXTN
1540 GOTO8600
4800 TT=W(S)
4810 TX=X(S)
4820 TY=Y(S)
4830 K=Z(S)
4831 SS=S
4835 PRINT@0,"€€€€€€€€€€€€€€€€€€€€";
4840 FORS=1TO9
4845 PRINT@32*S,"€ßßßßßßßßßßßßßßßßßß€";
4850 NEXTS
4855 FORS=10TO14
4860 PRINT@32*S,"€¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯€";
4865 NEXTS
4870 PRINT@32*15,"€€€€€€€€€€€€€€€€€€€€";
4880 PRINT@32*1+16,"N";:PRINT@32*2+15,;:M$="123":GOSUB7:PRINT@32*3+14,"W";:M$="8":GOSUB7
4881 PRINT"*";:M$="4":GOSUB7:PRINT"E";:PRINT@32*4+15,;:M$="765":GOSUB7:PRINT@32*5+16,"S";
4989 CL=0
5000 PRINT@10,;:M$="COMMANDS:":GOSUB7
5001 IFSM<1THENSM=0
5002 IFSP>SMTHENSP=SM
5003 PRINT@32*13+23,;:M$=MID$(STR$(SP),2)+"KTS":GOSUB7:PRINT"€";
5004 GOSUB8000
5008 IFGS<1ORFU<1THEN7500
5010 GOSUB8010
5060 IFTR>=24E4ORTT<1THENGOSUB20:GOTO1000
5070 PRINT@32*15+22,;:M$=MID$(STR$(INT(TR)),2)+" ":GOSUB7
5080 D=ABS(TR<4E4)+ABS(TR<3E4)+ABS(TR<2E4)+ABS(TR<1E4)
5088 GOSUB8500
5090 IFK>3ANDRND(0)>(.6*ABS(K>3))+(.1*ABS(K>4))ANDTR<26000THENGOSUB7000
5100 IFTH<>HEORD=0THENPRINT@32*7+5,"ßßßßßßßßßßß";:PRINT@32*8+5,"ßßßßßßßßßßß";:PRINT@32*9+5,"ßßßßßßßßßßß";
5110 IFTH=HEANDD<>0THENGOSUB8700
5120 GOSUB6500
5130 IFA>0THENONAGOTO5300,5500,5700
5140 PRINT@20,;:M$="0=TORPEDOES 1=FIRE GUNS 2=TURN€€€€ 3=INC SPEED 4=DEC SPEED":GOSUB1
5150 FORF=1TO100
5170 I$=INKEY$:IFI$<>""THENF=100:NEXT:SOUND1,1:GOTO5190
5180 NEXTF:I$="@"
5190 A=ASC(I$)-48
5192 IFA=29ORGS<1THEN7500
5200 REM GOSUB40
5204 B=(2*ABS(A=0))+(1*ABS(A=1))
5205 IFA=0THENA=1
5210 IFA<1ORA>4THENA=0
5220 C=0:IFA=4THENC=1:A=3
5250 IFCL=1THEN1395
5260 GOTO5000
5300 IFD=0ORTH<>HETHENA=0
5302 IFD=0ORHE<>THTHEN5140
5305 GOTO6000
5500 PRINT@20,;:M$="DIRECTION?€ 1=N/W  2=N€ 3=N/E  4=E€ 5=S/E  6=S€ 7=S/W  8=W€":GOSUB1
5510 FORF=1TO100
5520 I$=INKEY$:IFI$=""THEN5540
5530 IFI$<>""THENF=100:NEXT:SOUND50,1:GOTO5550
5540 NEXTF:I$="@"
5550 E=ASC(I$)-48
5560 REM GOSUB40
5570 IFE>0ANDE<9THEN5590
5575 IFCL=1THEN1395
5580 GOTO5000
5590 HE=E
5600 PRINT@5*32+20,;:M$=B$(HE):GOSUB7
5610 A=0
5620 GOTO5575
5700 PRINT@20,;
5710 IFC=0THENM$="SPEED€€€€€€ INCREASE€€€ (0-9)?€€€€€ €€€€€€€€€€€ €€€€€€€€€€€":GOSUB1
5715 IFC=1THENM$="SPEED€€€€€€ DECREASE€€€ (0-9)?€€€€€ €€€€€€€€€€€ €€€€€€€€€€€":GOSUB1
5720 FORF=1TO100
5730 I$=INKEY$:IFI$=""THEN5750
5740 IFI$<>""THENF=100:NEXT:SOUND100,1:GOTO5760
5750 NEXTF:I$="@"
5752 IFCL=1THEN1395
5753 GOTO5000
5760 E=ASC(I$)-48
5765 IFE<0ORE>9THEN5720
5780 IFC=0THENSP=SP+E
5790 IFC=1THENSP=SP-E
5800 IFSP>SMTHENSP=SM
5810 IFSP<0THENSP=0
5830 A=0
5840 C=0
5850 GOTO5752
5880 PRINT@32*13+23,;:M$=MID$(STR$(SP),2)+"KTS":GOSUB7:PRINT"€";
6000 IFB=1ANDSH<1ORB=2ANDT0<1THEN6160
6001 PRINT@20,;
6010 IFB=2THENM$="TORPEDOES€€ FIRING€€€€€ (1-2)?€€€€€ €€€€€€€€€€€ €€€€€€€€€€€":GOSUB1
6020 IFB=1THENM$="GUNS€€€€€€€ FIRING€€€€€ (1-6)?€€€€€ €€€€€€€€€€€ €€€€€€€€€€€":GOSUB1
6030 FORF=1TO100
6050 I$=INKEY$:IFI$<>""THENF=100:NEXT:SOUND75,1:GOTO6070
6060 NEXTF:I$="@"
6070 V=ASC(I$)-48
6080 IF((V<1ORV>2)ANDB=2)OR(B=1AND(V<1ORV>6))THEN5000
6085 W=0
6090 FORF=1TOV
6095 IFB=1ANDTR>26000THEN6140
6100 IF(RND(0)*TR)<1000THEN6210
6110 IFB=1THEN6140
6120 GOSUB8900
6130 GOTO6150
6140 PRINT@32*11+7+F,;:M$="+":GOSUB7:SOUND125,1:PRINT@32*11+7+F,"¯";
6150 NEXTF
6152 IFTH=HEANDD<>0THENGOSUB8700
6155 IFB=1THENSH=SH-V
6157 IFB=2THENT0=T0-V
6158 PRINT@32*7+23,;:M$=MID$(STR$(SH),2):GOSUB7:PRINT@32*9+23,;:M$=MID$(STR$(T0),2):GOSUB7
6160 A=0
6170 B=0
6180 V=0
6190 REM PRINT AT 20,0;Z$;Z$
6200 GOTO5000
6210 IFB=1THEN6250
6220 GOSUB8900
6230 R=RND(0)*10000
6240 GOTO6270
6250 PK=PEEK(MC+32*9+7+F)
6251 PRINT@32*9+7+F,;:M$="+":GOSUB7:SOUND50,2:PRINT@32*9+7+F,"¯";:PRINT@32*9+7+F,"Ï";:PRINT@32*9+7+F,CHR$(PK);
6260 R=RND(0)*1000
6270 TT=TT-R
6273 SC=SC+R
6275 IFTT<1THEN6290
6280 GOTO6150
6290 PRINT@32*7+5,"ßßÖÝßÏÏßÜßß";:PRINT@32*8+5,"ßßßÒÞÏÏØ×ßß";:PRINT@32*9+5,"ßßßßÒ€€×ßßß";
6291 FORF=1TO10:SOUND100,1:SOUND3,1:SOUND2,1
6292 NEXTF
6293 PRINT@32*7+5,"ßßßßßßßßßßß";:PRINT@32*8+5,"ßßßßßßßßßßß";:PRINT@32*9+5,"ßßßßßßßßßßß";
6294 Z(SS)=0
6295 Y(SS)=0
6296 X(SS)=0
6297 W(SS)=0
6300 GOTO6155
6400 BB=INT(RND(0)*10)+1
6465 IFY(BB)<2ORX(BB)<2THENRETURN 
6470 SY=43-INT(Y(BB)/120)
6480 SX=INT(X(BB)/120)
6482 FORS=1TO5:IFPOINT(SX,43-SY)<>3THENX(BB)=1916:Y(BB)=732:SOUND255,1:GOTO6470
6485 RESET(SX,43-SY)
6486 SET(SX,43-SY,3)
6487 NEXTS
6496 RETURN
6500 PRINT@32*15,"€€€€€€€€€€€€€€€€€€€";
6505 PRINT@32*15+3,;:M$="TARGET TO "+B2$(TH):GOSUB7
6510 RETURN 
7000 G=0
7010 FORF=1TO(3*ABS(K=4))+(4*ABS(K=5))
7020 PK=PEEK(MC+32*9+7):IFTH=HETHENPRINT@32*9+7,;:M$="+":GOSUB7:PRINT@32*9+11,;:M$="+":GOSUB7:SOUND175,1
7040 IFTH=HETHENPRINT@32*9+7,CHR$(PK);:PRINT@32*9+11,CHR$(PK);
7050 IFRND(0)*TR>800THEN7110
7060 G=G+1
7065 PRINT@0,;
7070 FORI=1TO4
7080 POKE49151,64:GOSUB9:SOUND200,1:SOUND190,1:REM L=USR SI
7090 NEXTI
7100 GOTO7130
7110 PRINT@32*14+6,"ÏÏ";:PRINT@32*14+13,"ÏÏ";
7111 PRINT@32*13+5,"ÏÏÏÏ";:PRINT@32*13+12,"ÏÏÏÏ";
7112 PRINT@32*12+5,"ÏÏÏÏ";:PRINT@32*12+12,"ÏÏÏÏ";
7113 PRINT@32*11+4,"ÏÏÏÏÏÏ";:PRINT@32*11+11,"ÏÏÏÏÏÏ";:SOUND1,5
7120 PRINT@32*14+6,"¯¯";:PRINT@32*14+13,"¯¯";
7121 PRINT@32*13+5,"¯¯¯¯";:PRINT@32*13+12,"¯¯¯¯";
7122 PRINT@32*12+5,"¯¯¯¯";:PRINT@32*12+12,"¯¯¯¯";
7123 PRINT@32*11+4,"¯¯¯¯¯¯";:PRINT@32*11+11,"¯¯¯¯¯¯";
7130 NEXTF
7135 IFTH=HETHENGOSUB8700
7140 IFG=0THENRETURN 
7150 FORF=1TOG
7160 IFK=4THENGS=GS-(RND(0)*500)
7165 IFK=5THENGS=GS-(RND(0)*1200)
7170 NEXTF
7180 PRINT@10,"€€";:M$="REPORT:":GOSUB7:GOSUB60:PRINT@20,;:M$="WE'VE BEEN HIT":IFG>1THENM$=M$+STR$(G)+" TIMES. "
7181 IFG=1THENM$=M$+". "
7182 M$=M$+"DAMAGE="+STR$(100-(INT((GS/8E3)*100)))+"%":GOSUB1
7190 SM=INT(.26*(GS/120))
7192 FORF=1TO1500
7193 NEXTF:GOSUB60
7200 PRINT@10,"€€€€€€€€€";:RETURN
7400 CLS
7405 GOSUB8110
7410 PRINT@32*6+4,"YOU HAVE RUN AGROUND";
7490 GOTO7506
7500 CLS
7505 GOSUB8110
7506 PRINT@32*3+11,;:M$="GRAF SPEE":GOSUB7
7510 PRINT@32*9+2,"HARD LUCK CAPTAIN YOUR SHIP";:PRINT@32*10+2,"HAS SUNK WITH THE LOSS OF";
7515 PRINT@32*11+2,"ALL YOUR CREW";
7520 PRINT@32*12+2,"SCORE=";INT(SC);
7530 PRINT@32*15+4,;:M$="PLAY ANOTHER GAME (Y/N)?":GOSUB7
7540 I$=INKEY$:IFI$=""THEN7540
7550 IFI$="N"THENEND 
7555 IFI$="Y"THENRUN
7560 GOTO7540
8000 SA=((SP/15)*ABS(TR<3E4))+((SP/5)*ABS(TR>=3E4ANDTR<6E4))+(SP*ABS(TR>=6E4))
8005 FU=FU-(SA*ABS(SP>0))-(.5*SA*ABS(SP>15))-(.5*SA*ABS(SP>20))
8006 IFFU<1THENFU=0
8007 IFFU=0THENSP=0
8009 RETURN 
8010 NS=NS+(SA*ABS(HE=6))-(SA*ABS(HE=2))-(SA*.7*ABS(HE=1ORHE=3))+(SA*.7*ABS(HE=7ORHE=5))
8020 PRINT@32*11+24,"€€€€€";:PRINT@32*11+24,;:M$=STR$(INT(NS)):GOSUB7
8030 EW=EW+(SA*ABS(HE=4))-(SA*ABS(HE=8))-(SA*.7*ABS(HE=1ORHE=7))+(SA*.7*ABS(HE=3ORHE=5))
8040 PRINT@32*12+24,"€€€€€";:PRINT@32*12+24,;:M$=STR$(INT(EW)):GOSUB7
8045 IFCL=1THENRETURN
8050 TR=(SQR(((ABS(TX-EW))^2)+((ABS(TY-NS))^2)))*2000
8060 RETURN
8100 CLS 
8102 Z$="                                "
8103 A$="ß                              ß"
8104 N=0
8105 GOTO 8120
8110 PRINT@0,"ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";
8116 PRINTA$;A$;A$;A$;A$;A$;A$;A$;A$;A$;A$;A$;A$;A$;
8118 PRINT"ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";:POKEMC+511,ASC("ß")
8119 RETURN 
8120 GOSUB8110
8130 PRINT@32*1+11,"GRAF SPEE";:PRINT@32*2+11,"ƒƒƒƒƒƒƒƒƒ";
8131 PRINT@32*3+2,;:M$="COPYRIGHT S.R.MANSFIELD 1982":GOSUB7
8150 PRINT@32*5+2,"THE YEAR IS 1940, AND YOU";:PRINT@32*6+3,"ARE IN COMMAND OF THE";
8170 PRINT@32*7+3,"GERMAN POCKET-BATTLESHIP,";
8175 PRINT@32*8+3,"THE ADMIRAL GRAF SPEE.";
8180 PRINT@32*9+2,"YOUR SHIP IS ARMED WITH SIX";
8190 PRINT@32*10+3,"11 INCH GUNS AND TWO";
8200 PRINT@32*11+3,"TORPEDO TUBES.";
8210 PRINT@32*12+2,"YOUR OBJECTIVE: SINK AS MANY";
8220 PRINT@32*13+3,"ENEMY SHIPS AS POSSIBLE.";
8260 GOSUB8400
8280 GOSUB8110
8285 PRINT@32*1+11,;:M$="GRAF SPEE":GOSUB7
8290 PRINT@32*2+2,"THE DISPLAY WILL SHOW A MAP";:PRINT@32*3+3,"OF THE SOUTH ATLANTIC.";
8300 PRINT@32*4+2,"A TEMPORARY FLASHING PIXEL";:PRINT@32*5+3,"WILL SHOW THE APPROXIMATE";
8301 PRINT@32*6+3,"POSITION OF AN ALLIED SHIP.";
8310 PRINT@32*7+2,"THE MAIN FLASHING PIXEL";:PRINT@32*8+3,"SHOWS YOUR POSITION.";
8330 PRINT@32*9+2,"TO DESTROY THE TARGET YOU";:PRINT@32*10+3,"MUST GET WITHIN VISUAL";
8331 PRINT@32*11+3,"RANGE, AND THEN FIRE.";
8340 PRINT@32*12+2,"PLOT COURSE USING (1-8).";:PRINT@32*13+2,"AT SEARCH POINT PRESS 'S'.";
8350 PRINT@32*14+2,"PRESS 'M' TO SCUTTLE SHIP.";
8370 GOSUB8400
8390 RETURN 
8400 PRINT@32*15+6,;:M$="HIT ENTER TO CONTINUE":GOSUB7
8410 IFINKEY$<>CHR$(13)THEN8410
8415 PRINT@32*15+6,"ßßßßßßßßßßßßßßßßßßßßß";
8420 RETURN
8500 IX=((TY-NS)+ABS(TY=NS))/((TX-EW)+ABS(TX=EW))
8510 TH=ABS(IX>.5ANDIX<2)+(2*ABS(IX>2ORIX<-2))+(3*ABS(IX>-2ANDIX<-.5))+(4*ABS(TY>NS))
8520 IF(IX>-.5ANDIX<.5)THENTH=1+(3*ABS(TX>EW))+(7*ABS(TX<EW))
8530 RETURN
8600 CLS 
8605 GOSUB8110
8610 PRINT@32*4+11,;:M$="GRAF SPEE":GOSUB7
8611 PRINT@32*9+4,"CONGRATULATIONS CAPTAIN";
8612 PRINT@32*11+2," YOU HAVE BEEN PROMOTED TO";:PRINT@32*12+2,"ADMIRAL. YOUR SCORE=";INT(SC);
8620 GOTO7530
8700 IFK3<>K+DTHENPRINT@32*7+5,"ßßßßßßßßßß";:PRINT@32*8+5,"ßßßßßßßßßß";:PRINT@32*9+5,"ßßßßßßßßßß";
8710 IFD=1THENPRINT@32*8+5,"ÏßÏ";:GOSUB9:GOSUB9:PRINT@32*8+6,"ßßßÏ";
8720 IFD=2THENPRINT@32*7+5,"ÏßÏÏ";:GOSUB9:GOSUB9:PRINT@32*7+8,"ßÏ";:GOSUB9:GOSUB9:PRINT@32*7+5,"ßßßßßÏßßßßß";
8730 IFK=1ANDD=3THENPRINT@32*7+5,"ÏÏ";:GOSUB9:PRINT@32*8+7,"Ï";:GOSUB9:PRINT@32*9+7,"ØÝßÞßÞ";
8740 IFK=1ANDD=4THENPRINT@32*7+6,"Ï";:PRINT@32*8+7,"ÝÙßÞßßÞ";:PRINT@32*9+6,"Ø€ÜÜ€ÜÜ€Ü";
8750 IFK=2ANDD=3THENPRINT@32*7+6,"ÏßÏ";:PRINT@32*8+9,"Ï";:PRINT@32*9+10,"ÝÝ";
8760 IFK=2ANDD=4THENPRINT@32*7+5,"ÏßÏ";:PRINT@32*8+9,"ÏÝÝ";:PRINT@32*9+6,"ÞÔÜØ€ÔÜØÝ";
8770 IFK=3ANDD=3THENPRINT@32*7+5,"ÏÏ";:PRINT@32*8+8,"Ï";:PRINT@32*9+5,"ßßßßßØÝßßßßß";
8780 IFK=3ANDD=4THENPRINT@32*7+5,"ÏÏ";:PRINT@32*8+8,"Ý";:PRINT@32*9+7,"Ø€ÜÜÜØÝ";
8790 IFK=4ANDD=3THENPRINT@32*8+5,"ßÏßÏ";:PRINT@32*9+5,"ßßßßßÏÝÝßßßß";
8800 IFK=4ANDD=4THENPRINT@32*7+5,"ÏßÏ";:PRINT@32*8+8,"ÏÝÝ";:PRINT@32*9+6,"Þ€Ø€Ô€Ý";
8810 IFK=5ANDD=3THENPRINT@32*7+5,"ÏßÏÏ";:PRINT@32*8+8,"ÏÝÝ";:PRINT@32*9+5,"ßßßßÔ€ßßßß";
8820 IFK=5ANDD=4THENPRINT@32*7+5,"ÏßÏÞÞ";:PRINT@32*8+7,"ÜÚØÕÜ";:PRINT@32*9+5,"ØÔ€€€€€Ô€Ü";
8825 K3=K+D
8830 RETURN 
8900 FORG=1TO5
8910 PRINT@32*(15-G)+3+G,"©";:SOUND225,1:PRINT@32*(15-G)+3+G,"¯";
8920 NEXTG
8930 RETURN 
9000 PX=10
9006 PY=22
9050 NS=1000
9060 EW=1700
9100 HE=6
9110 TH=10
9130 A=0
9140 DIMB$(8),B2$(8)
9150 B$(1)="NORTH/WEST":B2$(1)="N/W"
9160 B$(2)="  NORTH   ":B2$(2)="NORTH"
9170 B$(3)="NORTH/EAST":B2$(3)="N/E"
9180 B$(4)="   EAST   ":B2$(4)="EAST"
9190 B$(5)="SOUTH/EAST":B2$(5)="S/E"
9200 B$(6)="  SOUTH   ":B2$(6)="SOUTH"
9210 B$(7)="SOUTH/WEST":B2$(7)="S/W"
9220 B$(8)="   WEST   ":B2$(8)="WEST"
9230 C=0
9240 D=-1
9250 B=0
9280 SM=26
9290 K3=0
9291 SH=200
9292 T0=20
9293 FU=8E3
9300 RETURN 
