0 DIMC1,CC,M$,MC,ZZ:MC=16384:GOTO100
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORC1=1TOLEN(M$):POKECC+C1,ASC(MID$(M$,C1))AND63:NEXT:?@CC-MC+C1,:RETURN
8 PRINT@352+ZZ,;:M$=" #"+MID$(STR$(Z9+1),2)+"    ":GOSUB7:PRINT@384+ZZ,;:M$=NA$(Z9+1):GOSUB7
9 PRINT@416+ZZ,;:M$=MID$(STR$(T(Z9))+"        ",1,7):GOSUB7:RETURN
100 GOSUB770
110 REM
120 GOSUB1160
130 CLS0
140 GOSUB1230
150 REM -MAIN LOOP-
160 FORQ=1TO10:FORZ9=0TOA-1:GOSUB8
170 FORR=1TO10:PRINT@2*(Z9+1)*32,;:M$=MID$(STR$(Z9+1),2):GOSUB7
180 B1=0:GOSUB360
190 IFJ1<>10THENB1=1:GOSUB390
200 IFQ=10THENONSGOTO210,270,270,210,310
210 PRINT@2*(Z9+1)*32,MID$(STR$(Z9+1),2);:NEXTZ9:NEXTQ
215 FORZ9=0TOA-1:ZZ=Z9*8:GOSUB8:NEXTZ9:PRINT@480,"PLAY AGAIN (Y/N)?";
220 M$=INKEY$:IFM$=""THEN220 
230 IFM$<>"Y"ANDM$<>"N"THEN220
240 IFM$="Y"THEN120
250 CLS:END
260 REM -10TH FRAME : EXTRA BALLS-
270 PRINT@480,"TAKE TWO MORE BALLS, "NA$(Z9+1);
280 FORI=1TO3500:NEXT:GOSUB2000
290 S(Z9)=S-1:B1=1:GOSUB360:IFJ<>10THEN340
300 GOTO330
310 PRINT@480,"TAKE ONE MORE BALL, "NA$(Z9+1);
320 FORI=1TO3500:NEXT:GOSUB2000
330 S(Z9)=1:B1=2:GOSUB360:GOTO210
340 S(Z9)=1:B1=2:GOSUB390:GOTO210
350 REM -FIRST BALL-
360 FORI=1TO10:SET(B(I)+20,A(I)+10,1):NEXT
370 PS=1:J1=0:GOTO400
380 REM -SECOND BALL-
390 PS=0
400 GOSUB450:T=T(Z9):S=S(Z9):T=T+J
410 ONS(Z9)GOSUB660,690,710,730,750
420 T(Z9)=T:S(Z9)=S
430 GOSUB9:RETURN
440 REM -ROLL BALL-
450 H=1:C=19:E=11:D=-1
460 FORV=CTOESTEPD:SET(H+20,V+10,1)
470 IFINKEY$<>""THENT5=V:V=E:NEXT:GOTO510
480 FORR=1TOF:NEXT
490 RESET(H+20,V+10)
500 NEXTV:D=-D:T5=C:C=E:E=T5:GOTO460
510 V=T5:FORH=1TO35:RESET(H+20,V+10):SET(H+20+1,V+10,1):FORK=1TO10:NEXT:NEXT
520 J=0
530 IFPOINT(H+20+1,V+10)=0THEN570
540 J=J+1:SOUND200,1:FORD=-1TO1STEP2:X1=V:X2=H
550 X1=X1+D:X2=X2+1:IFPOINT(X2+20+1,X1+10)=1THENRESET(X2+20+1,X1+10):J=J+1:SOUND200,1:GOTO550
560 NEXT
570 RESET(H+20,V+10):SET(H+20+1,V+10,1):H=H+1:IFH<41THEN530
580 J1=J1+J
590 PRINT@(2*(Z9+1))*32+(3*Q)+B1-1,;:G=J+48:IFJ>10THENSTOP
600 IFJ1<>10THEN630
610 IFPSTHENG=88:SOUND50,5:SOUND100,5:GOTO630
620 G=47
630 PRINTCHR$(G);
640 RESET(H+20,V+10):RETURN 
650 REM -SCORING ROUTINES-
660 IFJ1<>10THENRETURN
670 IFPSTHENS=2:RETURN 
680 S=5:RETURN
690 T=T+J:IFJ=10THENS=3:RETURN
700 S=4:RETURN
710 T=T+J*2:IFJ<>10THENS=4
720 RETURN
730 T=T+J:IFJ1=10THENS=5:RETURN
740 S=1:RETURN
750 T=T+J:IFJ=10THENS=2:RETURN
760 S=1:RETURN
770 DIMA(10),B(10):FORI=1TO10:READA(I),B(I):X=X+A(I)+B(I):NEXT
775 IFX<>540THENPRINT"ERROR IN DATA STATEMENTS FOR PIN POSITIONS.":STOP
780 RETURN
790 REM -PIN DATA-
800 DATA 12,40,13,39,14,38,14,40 
810 DATA 15,37,15,39,16,38,16,40 
820 DATA 17,39,18,40
1150 REM TITLE SCREEN
1160 C1=RND(-(PEEK(9)*256+PEEK(10))):CLSRND(8):PRINT@9,"BOWLING CHAMP";:FORI=1TO4:NA$(I)="":NEXT
1170 PRINT@64,;:INPUT"HOW MANY BOWLERS (1-4)";A:IFA<1ORA>4THEN1170
1180 PRINT@192,;:FORI=1TOA:PRINT"BOWLER #";STR$(I);"'S NAME";
1190 INPUTA$:NA$(I)=LEFT$(A$+"       ",7):NEXT:FORI=0TOA-1:T(I)=0:NEXT
1200 PRINT@448,"DIFFICULTY (1-EASY 5-HARD)";:INPUTF:IFF<1ORF>5THEN1200
1210 F=50-5*F:RETURN
1220 REM DRAW GAME SCREEN
1230 CLS0
1240 PRINT"  1 2 3 4 5 6 7 8 9 10";
1250 PRINT"";
1260 PRINT"1                     ";
1270 PRINT"";
1280 PRINT"2                     ";
1290 PRINT"";
1300 PRINT"3                     ";
1301 PRINT"";
1302 PRINT"4                     ";
1303 PRINT"";
1304 GOSUB2000
1310 FORI=0TOA-1:S(I)=1:NEXT:ZZ=0:RETURN:REM INITIALIZE SCORE STATE
2000 PRINT@480,"";:POKEMC+511,ASC(""):RETURN
3000 REM BY JOSEPH GANCI
3020 REM COMPUTE! FEB 1985
3030 REM MC-10 VER. JIM GERRIE
