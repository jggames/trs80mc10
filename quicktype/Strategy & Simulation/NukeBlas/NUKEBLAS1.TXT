10 REM
20 REM
30 REM NUCLEAR BLAST CALCULATION
40 REM (C) JOHN R. FANCHI, JUNE 1985
50 REM MICROSOFT BASIC FOR CP/M-80 OPERATING SYSTEM
60 REM PART OF THIS CODE IS EXCERPTED FROM J.R. FANCHI' S
70 REM ADVENTURE SIMULATION GAME "NUCLEAR SURVIVAL"
80 CLS
90 REM DIM ANSER$(2)
100 CLS
110 PRINT "* NUCLEAR BLAST CALCULATION *"
120 PRINT
130 PRINT "(C) JOHN R. FANCHI, JUNE 1985"
140 PRINT
150 PRINT "THE CALCULATIONS PERFORMED HERE ARE BASED ON INFORMATION PUBLISHED"
160 PRINT "BY THE AMERICAN INSTITUTE OF PHYSICS. "
170 PRINT
180 INPUT "HIT CARRIAGE RETURN WHEN YOU ARE READY TO PROCEED"; ICAR$
190 CLS
200 PRINT
210 INPUT "SPECIFY YOUR SLANT RANGE (DISTANCE) FROM THE BLAST IN MILES"; D
220 PRINT
230 REM ............................. DETERMINE WEAPON YIELD (Y)
240 PRINT "SPECIFY THE YIELD OF THE BLAST IN MEGATONS. TYPICAL VALUES RANGE"
250 INPUT "FROM 0.8 TO 20 MEGATONS"; Y
260 REM ................................. DETERMINE BLAST HEIGHT
270 PRINT
280 INPUT "SPECIFY THE HEIGHT OF THE BLAST IN FEET"; H
290 PRINT
300 PRINT "SPECIFY THE THERMAL ENERGY FRACTION. THIS IS THE FRACTION OF HEAT"
310 PRINT "ENERGY IN THE NUCLEAR FIREBALL. IT SHOULD BE BETWEEN 0 AND 1."
320 INPUT "A TYPICAL VALUE IS 0.35"; TE
330 PRINT
340 REM ............................. DETERMINE TRANSMISSION FACTOR
350 PRINT "SPECIFY THE ATMOSPHERIC TRANSMISSION FACTOR. IT SHOULD BE BETWEEN"
360 INPUT "0 (VERY CLOUDY DAY) AND 1 (PERFECTLY CLEAR DAY)";TA
370 PRINT
380 REM ............................... CALC BLAST EFFECTS
390 REM THERMAL FLUX Q
400 Q=3000*TE*TA*Y/(D*D)
410 REM BLAST OVERPRESSURE P
420 Z=(Y^(.333))/D
430 P=22.4*(Z^3)+15.8*(Z^(1.5))
440 REM EMP RANGE
450 EM=SQR(2*H*3963/5280)
460 REM RADIATION DOSAGE REM
470 RE=(250*(1000*Y))/(16*3.1416*(D^2))
480 PRINT
490 PRINT "THE NUCLEAR BLAST HAD THE FOLLOWING EFFECTS:"
500 PRINT
510 PRINT "THERMAL FLUX (CAL/SQ CM)";Q
520 PRINT "OVERPRESSURE (LB/SQ IN)";P
530 PRINT "EMP RANGE (MILES)";EM
540 PRINT "RADIATION DOSAGE (RE)";RE
550 PRINT
560 INPUT "HIT CARRIAGE RETURN WHEN YOU ARE READY TO PROCEED";M$
570 CLS
580 PRINT
590 REM ............................... OVERPRESSURE EFFECTS
600 IF P<20 THEN 640
610 PRINT "OVERPRESSURE HAS CAUSED WINDS IN EXCESS OF 500 MI LES PER HOUR."
620 PRINT "EVEN MULTI-STORY REINFORCED CONCRETE BUILDINGS ARE LEVELED. "
630 GOTO 820
640 REM
650 IF P < 10 THEN 700
660 PRINT "OVERPRESSURE HAS CAUSED WINDS IN EXCESS OF 300 MILES PER HOUR."
670 PRINT "MOST FACTORIES AND COMMERCIAL BUILDINGS ARE LEVELED, AS ARE SMALL"
680 PRINT "WOOD AND BRICK RESIDENCES."
690 GOTO 820
700 REM
710 IF P<5 THEN 750
720 PRINT "OVERPRESSURE HAS CAUSED WINDS IN EXCESS OF 160 MILES PER HOUR."
730 PRINT "UNREINFORCED BRICK AND WOOD HOUSES ARE LEVELED. "
740 GOTO 820
750 REM
760 IF P<2 THEN 790
770 PRINT "OVERPRESSURE HAS CAUSED WINDS IN EXCESS OF 70 MILES PER HOUR."
780 GOTO 820
790 REM
800 PRINT "OVERPRESSURE HAS NOT HAD A SIGNIFICANT EFFECT ON WIND CONDITIONS"
810 PRINT "OR THE STRUCTURES OF BUILDINGS."
820 REM
830 PRINT
840 REM ............................... THERMAL EFFECT
850 IF Q<10 THEN 880
860 PRINT "THERMAL FLUX HAS BURNED YOU TO A CRISP."
870 GOTO 980
880 REM
890 IF Q<5 THEN 920
900 PRINT "YOU HAVE SUFFERED THIRD DEGREE BURNS BECAUSE OF THERMAL FLUX."
910 GOTO 980
920 REM
930 IF Q<1 THEN 960
940 PRINT "YOU HAVE SUFFERED SECOND DEGREE BURNS BECAUSE OF THERMAL FLUX."
950 GOTO 980
960 REM
970 PRINT "THERMAL FLUX HAS ADDED TO YOUR TAN. "
980 PRINT
990 REM ................................ RADIATION EFFECTS PRINT
1000 PRINT
1010 IF RE<5000 THEN 1050
1020 PRINT "YOU ARE EXPERIENCING CONVULSIONS, TREMORS AND ATAXIA BECAUSE"
1030 PRINT "OF RADIATION EXPOSURE. NO TREATMENT WILL HELP YOU."
1040 GOTO 1330
1050 REM
1060 IF RE<1000 THEN 1120
1070 PRINT "YOU HAVE DIARRHEA, FEVER AND A DISTURBANCE OF YOUR BODY"
1080 PRINT "CHEMISTRY BECAUSE OF RADIATION EXPOSURE. TREATMENT MAY"
1090 PRINT "ALLEVIATE PAIN, BUT IT WILL NOT SAVE YOUR LIFE. YOU HAVE"
1100 PRINT "LESS THAN 2 WEEKS TO LIVE. "
1110 GOTO 1330
1120 REM
1130 IF RE<600 THEN 1190
1140 PRINT "YOU HAVE VERY SEVERE LEUKOPENIA AND INTERNAL BLEEDING."
1150 PRINT "YOU HAVE ALSO DEVELOPED ULCERS AND INFECTION IS LIKELY BECAUSE"
1160 PRINT "OF RADIATION EXPOSURE. YOU NEED HOSPITALIZATION BUT NONE IS"
1170 PRINT "AVAILABLE, THEREFORE YOU HAVE LESS THAN A MONTH TO LIVE."
1180 GOTO 1330
1190 REM
1200 IF RE<200 THEN 1250
1210 PRINT "YOUR SYMPTOMS RANGE FROM LEUKOPENIA TO HAIR LOSS BECAUSE OF"
1220 PRINT "RADIATION EXPOSURE. HOSPITALIZATION IS REQUIRED AND IS"
1230 PRINT "AVAILABLE TO YOU BECAUSE YOU HAVE A REASONABLE CHANCE TO LIVE."
1240 GOTO 1330
1250 REM
1260 IF RE<100 THEN 1310
1270 PRINT "RADIATION EXPOSURE HAS MADE YOU SLCK--VOMITING. HEADACHE. SOME"
1280 PRINT "LOSS OF WHITE BLOOD CEL IS AND DIZZINESS. THIS IS GOOD. YOU"
1290 PRINT "WILL I COMPLETELY RECOVER."
1300 GOTO 1330
1310 REM
1320 PRINT "YOU HAVE SURVIVED RADIATION EXPOSURE WITH NO EFFECTS."
1330 PRINT
1340 REM .............................. EMP EFFECT
1350 IF D>EM THEN 1410
1360 PRINT "YOUR DISTANCE FROM THE BLAST IS WITHIN THE RANGE OF THE"
1370 PRINT "ELECTROMAGNETIC PULSE (EM). THE EMP IS CAPABLE OF BURNING"
1380 PRINT "OUT ELECTRICAL INSTRUMENTS."
1390 PRINT
1400 GOTO 1440
1410 REM
1420 PRINT "YOU ARE OUTSIDE THE RANGE OF THE ELECTROMAGNETIC PULSE (EMP)."
1430 PRINT
1440 PRINT
1450 INPUT "DO YOU WISH TO TRY AGAIN (Y/N)";I$
1460 PRINT
1470 IF I$="Y" THEN 100
1490 END
