5 REM CATTLE BARON, A BOARD
6 REM GAME WRITTEN BY JOHN DAY
7 REM MAR, 1985. COPYRIGHT (C)
8 REM   1985. JOHN S DAY.
9 REM ************************
10 CLS0:CLEAR500:X=RND(-(PEEK(9)*256+PEEK(10)))
20 FORX=0TO32:PRINT@X,"$":NEXT
30 FORX=63TO447STEP32:PRINT@X,"$$":NEXT
40 FORX=448TO479:PRINT@X,"$";:NEXT
45 PRINT@106,"åååååååååååå";
50 PRINT@138,"CATTLE BARON";
60 PRINT@170,"ÉÉÉÉÉÉÉÉÉÉÉÉ";
80 PRINT@257,"A GAME FOR TWO TO SIX PLAYERS.";
90 GOSUB9998
100 PRINT@96,"$":PRINT@127,"$$":PRINT@159,"$$":PRINT@191,"$$";:PRINT@256,"$":PRINT@287,"$$";
110 PRINT@197,;:INPUT"HOW MANY PLAYERS";N
120 IFN<2ORN>6THENPRINT@97,"OOPS! TRY THAT AGAIN!";:GOTO110
130 CLS2:PRINT"AND THE NAME OF -"
140 FORP=1TON
150 PRINT"PLAYER NO."P;:INPUTM$:P$(P)=LEFT$(M$,8)
160 NEXT
165 CLS:PRINT:PRINT"   RIGHT, PASTORALISTS, YOUR       PROPERTIES ARE AS FOLLOWS       *************************"
170 PRINT:FORP=1TON
180 ONPGOTO190,200,210,220,230,240
190 F$(P)="NULLA BORE":GOTO250
200 F$(P)="MOULE HILL":GOTO250
210 F$(P)="FLAT PLAINS":GOTO250
220 F$(P)="UNDULA DOWNS":GOTO250
230 F$(P)="DRY CREEK":GOTO250
240 F$(P)="BUFFALO BOG"
250 PRINTTAB(4)P$(P),F$(P)
260 NEXT
270 FORP=1TON:CA(P)=20000:HE(P)=3000:PP(P)=1:NEXT
280 PRINT@384,"  I'LL KEEP TABS ON EVERYTHING,   SO LET'S PLAY! <PRESS A KEY>"
290 IFINKEY$=""THEN290
295 GOTO500
300 SOUND240,1
310 PRINT@33,"PICK A NUMBER -";
320 R=RND(10)
330 PRINT@65,R;
340 IFINKEY$=""THEN320
350 SOUND200,1
360 PP(P)=PP(P)+R
365 IFPP(P)>32THENPP(P)=PP(P)-32:GOSUB1900:GOTO500
370 IFPP(P)>21THEN390
380 ONPP(P)GOSUB2000,2200,2300,2400,2500,2600,2700,2800,2900,3000,3100,3200,3300,3400,3500,3600,3700,3800,3900,4000,4100
385 GOTO500
390 ONPP(P)-21GOSUB4200,4300,4400,4500,4600,4700,4800,4900,5000,5100,5200
500 P=P+1:IFP>NTHENP=1
510 IFP$(P)=""THEN500
520 CLS2:PRINTP$(P)",  OF   "F$(P)
530 IFCA(P)<1THENGOSUB1300:GOTO300
550 IFHE(P)>9999ANDCA(P)>1ANDMB(P)<1THEN700
560 IFHE(P)<0THENHE(P)=0
570 GOTO300
700 FORS=1TO10:SOUND240,1:NEXT
710 CLS2:PRINT@32,"           END OF GAME"
720 PRINT@96,"HERE ARE THE FINAL FIGURES:-"
725 PRINT@448,P$(P)" OF "F$(P)" WON!!"
730 PRINT@128,"":FORP=1TON
740 PRINTP$(P)" OF "F$(P)
750 PRINT"CASH - $"STR$(CA(P))". HERD"STR$(HE(P))"."
770 NEXT
780 IFINKEY$=""THEN780
790 END
800 CLS4:PRINT@32," "P$(P)", YOU 'RE BANKRUPT!!"
810 PRINT@96," YOU ARE NOW OUT OF THE GAME!!"
820 P$(P)="":F$(P)=""
840 GOSUB11000:GOTO500
900 CLS:PRINTTAB(10)"STOCK SALE":PRINT:PRINT:PRINTP$(P)" OF "F$(P):PRINT"CASH $"CA(P)"HERD"HE(P)
901 PRINTMID$(STR$(SB(P)),2)" STUD BULLS";:IFSB(P)=1THENPRINTCHR$(8);
902 PRINT
910 PRINT"ARE YOU:-",,"BUYING  <B>",,"SELLING <S>",,"PASSING <P>"
1000 IFLD(P)=1THENCLS4:PRINT@43,"STOCK SALE";:PRINT@224,"        YOU'RE PASSING!!" :SOUND1,20:RETURN
1010 S$=INKEY$:IFS$=""THEN1010
1020 IFS$<>"S"ANDS$<>"B"ANDS$<>"P"THENPRINT"OOPS! TRY THAT AGAIN!":GOTO1010
1025 IFS$="P"THENRETURN
1030 IFS$="S"THENSL=1
1035 INPUT"HOW MANY HEAD OF CATTLE";HH
1040 IFSL=1ANDHH>HE(P)THENSOUND1,2:PRINT" YOU DON'T HAVE THAT MANY!":GOTO1035
1045 CLS2:PRINT@8,"WHAT AM I BID?";
1050 PRINT@385,"ûúù";
1060 PRINT@416,"õìêìóüúêêú";:PRINT@431,"úêêúêüüûúù";
1070 PRINT@448,"êêêêêüüöïüüüüüüúúêúúüììêìì";
1080 PRINT@480,"êêêêêüêêêêúêêúüêêêêêüúúêúúüúêêú";
1090 FORX=1TO15
1100 Y=32*RND(9)+32:SP=30+RND(20):BP=20+RND(20)
1110 IFSL=1THENPRINT@Y+RND(20),"$"SP;:GOTO1130
1120 PRINT@Y+RND(20),"$"BP;
1130 SOUNDRND(15)*10,1
1140 NEXT
1150 PRINT@192,"",,"":PRINT@236,"SOLD!!","  TO THE MAN WITH THE HAT, FOR"
1160 IFSL=0THENPRINTTAB(8)"$"BP"PER HEAD!",,"":GOTO1180
1170 PRINTTAB(8)"$"SP"PER HEAD!",,""
1180 IFSL=0THEN1220
1190 CA(P)=CA(P)+(SP*HH)-50:HE(P)=HE(P)-HH
1200 GOTO1250
1220 CA(P)=CA(P)-(BP*HH)-50:HE(P)=HE(P)+HH
1250 PRINT@384," "P$(P)"'S CASH NOW $"CA(P)," AND A HERD OF"HE(P)
1255 SL=0
1260 GOSUB9998:RETURN
1300 IFCA(P)<-50000THEN800
1305 PRINT@96," YOUR ACCOUNT IS OVERDRAWN, AND  YOU WILL NEED A MORTGAGE OF AT  LEAST $"ABS(CA(P))"TO CONTINUE."
1310 PRINT" INTEREST IS A MERE 10% SIMPLE,  AND PAYMENTS ARE WITHDRAWN AT   THE ANNUAL AUDIT IN TEN EASY    INSTALMENTS."
1320 INPUT" HOW MANY DOLLARS (in thousands) WILL YOU NEED";NN
1340 IFNN>50THENPRINT" "F$(P)" ISN'T WORTH THAT":PRINT" MUCH. TRY A LOWER AMOUNT!!":GOTO1320
1350 MO(P)=MB(P)+NN*1100:MB(P)=MO(P):MP(P)=10:CA(P)=CA(P)+(NN*1000)
1355 IFMO(P)>55000THENPRINT" YOU TRIED TO BORROW TOO MUCH!":FORX=1TO10:SOUND240,1:NEXT:GOTO800
1360 PRINT" YOUR CASH IS NOW $"CA(P)
1370 GOSUB11000:CLS2:RETURN
1900 IFLD(P)=0THEN2000
1910 LD(P)=0
1920 CLS5:PRINT@33,"  YOU ARE NOW OUT OF DROUGHT ";
1930 PRINT@129," YOU MAY NOW TRY AND MAKE UP ";:PRINT@161,"       LOST GROUND           ";
1940 GOSUB11000
2000 POKE49151,64:CLS3:PRINT" YEARLING SALE CHEQUE - "P$(P)
2010 PRINT@128," ***RURAL BANK OF QEENSMANIA***"
2020 PRINT:PRINT" PAY......."P$(P):PRINT@213,"..OR BEARER THE SUM OF...........$";
2025 S=SB(P)/10
2030 IFS>0THENPRINTHE(P)*(S+.2)*25:GOTO2040
2035 PRINTHE(P)*5
2040 PRINT:PRINT"           .....THOMAS MORPH."
2050 PRINT"           FOR ELDER,MORPH&CO."
2055 PRINTASC(P$(P))+1638400">>#   359:57-49151:64"
2060 IFSB(P)=0THEN2075
2070 CA(P)=CA(P)+(HE(P)*(S+.2)*25):CA(P)=INT(CA(P)):GOTO2080
2075 CA(P)=INT(CA(P)+(HE(P)*5))
2080 GOSUB11000:POKE49151,0:CLS
2100 IFMB(P)>1THENCA(P)=CA(P)-(MO(P)/10):MB(P)=MB(P)-(MO(P)/10):CA(P)=INT(CA(P)):MB(P)=INT(MB(P)):MP(P)=MP(P)-1
2110 PRINT@32,"ANNUAL AUDIT FOR "F$(P),"            STATION"
2120 PRINT:PRINT"CASH HOLDINGS -","$"CA(P)
2130 PRINT"HERD SIZE -"," "HE(P)
2140 PRINT"STUD BULLS -"," "SB(P)
2145 IFMP(P)<1THENMO(P)=0
2150 PRINT:PRINT"MORTGAGE -","$"MO(P)
2160 PRINT"BALANCE -","$"MB(P)
2170 IFMB(P)-CA(P)-(HE(P)*40)>1THEN800
2180 GOSUB11000:RETURN
2200 CLS3:PRINT@102,"WINDMILL MAINTENANCE";:PRINT@160,"";
2210 PRINT" DUE TO YOUR NEGLECT OF THEM IN  THE PAST, SOME OF YOUR MILLS    ARE IN NEED OF MAINTENANCE."
2220 PRINT:PRINT" THIS HAS NOW COST YOU $1000!"
2230 CA(P)=CA(P)-1000
2240 GOSUB11000:RETURN
2299 REM STOCK SALE
2300 GOSUB900:RETURN
2400 CLS2:PRINT@39,"DOG FENCE REPAIRS";
2410 PRINT@96," AS PART OF YOUR PASTORAL LEASE  YOU ARE OBLIGED TO MAINTAIN     YOUR PART OF THE LONGEST FENCE"
2420 PRINT" IN THE WORLD, (APART FROM THE   GREAT WALL OF CHINA), AND THAT  HAS JUST COST YOU $1500."
2430 CA(P)=CA(P)-1500
2440 GOSUB11000:RETURN
2500 CLS2:PRINT@43,"STUD BULL";
2505 IFSS(1)=1THENPRINT@237,SOLD";:GOTO2590
2510 PRINT@96," DUE TO A STROKE OF GOOD LUCK,   YOU HAVE THE UNIQUE OPTION TO   BUY THE MAGNIFICENT STUD BULL"
2520 PRINT@229,CHR$(34);"MUSCA DOMESTICA III";CHR$(34);"!";
2530 PRINT@288," FOR THE MISERABLE SUM OF $2000"
2540 PRINT" ARE YOU INTERESTED? <Y> OR <N>"
2550 S$=INKEY$:IFS$=""THEN2550
2560 IFS$<>"Y"THEN2590
2570 CA(P)=CA(P)-2000:SB(P)=SB(P)+1
2575 SS(1)=1
2580 PRINT:PRINT" OK. HE'S YOURS.  BUT WATCH OUT  FOR HIS TERRIBLE TEMPER!!!"
2590 GOSUB9999:RETURN
2600 CLS7:PRINT@37,"CATTLE TICK TREATMENT";
2610 PRINT@96," THAT SCRUB BULL YOU MISSED OUT  LAST YEAR HAS INFESTED YOUR     WHOLE HERD WITH TICK, WHICH IS"
2620 PRINT" GOING TO COST YOU FIFTY CENTS A HEAD TO ERADICATE. BETTER GET   HIM NEXT TIME YOU'RE OUT THERE, "P$(P)"!"
2630 CA(P)=CA(P)-INT(HE(P)/2)
2640 GOSUB11000:RETURN
2700 CLS7:PRINT@44,"SHOW DAY";
2710 PRINT@96," YOU HAVE DECIDED TO GIVE IT     ALL A REST AND FLY OVER TO MT.  MISERY FOR THE ANNUAL SHOW,"
2720 PRINT" AND IF THE BEER IS NICE AND     COLD, WE MAY NOT SEE YOU FOR    A WEEK OR MORE!"
2730 PRINT@320,"        HAVE A GOOD TIME!"
2750 GOTO2640
2800 GOSUB900:RETURN
2900 CLS8:PRINT@44,"BUSHFIRE!";
2905 IFFF(P)=1THENPRINT@96," BUT YOU PUT IT OUT BEFORE YOU   LOST ANYTHING!":GOSUB11000:RETURN
2910 PRINT@96," LIGHTNING WITHOUT RAIN IS THE   THING MOST FEARED IN THE        OUTBACK. IT STARTED THE WORST"
2920 PRINT" FIRE IN YOUR HISTORY.IT KILLED  15% OF YOUR STOCK,AND DESTROYED MORE THAN TWENTY MILES OF THE"
2930 PRINT" DOG FENCE. THIS WILL HAVE TO    BE REPLACED IMMEDIATELY, OR     DINGOS WILL GET WHAT IS LEFT    OF YOUR STOCK."
2940 PRINT@416,"    THAT LOT COST YOU $3000."
2950 HE(P)=INT(HE(P)*.85):CA(P)=CA(P)-3000
2960 GOSUB11000:RETURN
2999 REM:-POT LUCK
3000 GOSUB6000:RETURN
3100 CLS3:PRINT@42,"FLOOD DAMAGE";
3110 PRINT@96," HEAVY RAINS IN QUEENSLAND HAVE  FLOODED YOUR SEASONAL CREEKS    AND WASHED AWAY A GREAT DEAL "
3120 PRINT" OF VITAL ROADWAYS. IT HAS COST  YOU $3,000 TO REPLACE IT ALL."
3130 PRINT@416,"  YOU LOST NO STOCK, HOWEVER."
3140 CA(P)=CA(P)-3000
3150 GOSUB11000:RETURN
3199 REM STOCK SALE
3200 GOSUB900:RETURN
3300 CLS2:PRINT@43,"STUD BULL";
3305 IFSS(2)=1THENPRINT@237,"SOLD";:GOTO3390
3310 PRINT@96," AND NOW THE OPPORTUNITY OF A    LIFETIME LAYS ITSELF BEFORE     YOU - THE OPPORTUNITY TO BE"
3320 PRINT"      THE PROUD OWNER OF"
3330 PRINT@256,"      WILTSHIRE TAURUS II"
3340 PRINT@320," FOR THE BARGAIN PRICE OF $2000"
3350 PRINT" ARE YOU INTERESTED? <Y> OR <N>"
3360 S$=INKEY$:IFS$=""THEN3360
3370 IFS$<>"Y"THEN3390
3380 CA(P)=CA(P)-2000:SB(P)=SB(P)+1:SS(2)=1
3385 PRINT"OK. HE'S YOURS, BUT WATCH OUT   FOR THOSE SELF SHARPENING HORNS!";
3390 GOSUB9999:RETURN
3399 REM:-POT LUCK
3400 GOSUB6000:RETURN
3500 CLS6:PRINT@39,"ERADICATE RABBITS";
3510 PRINT@96," THEY'VE DONE IT AGAIN. OVER IN  THE TEN-MILE PADDOCK, THEY ARE  A REAL INFESTATION. GET RID OF"
3520 PRINT" THE LITTLE ROTTERS RIGHT AWAY!":PRINT@320," AND THAT ONLY COST YOU $500."
3530 CA(P)=CA(P)-500
3540 GOSUB11000:RETURN
3599 REM STOCK SALE
3600 GOSUB900:RETURN
3700 CLS4:PRINT@41,"LOCAL DROUGHT";
3710 PRINT@96," THIS IS THE THIRD YEAR WITHOUT  RAIN, AND YOU MUST NOW MAKE A   DEVASTATING DECISION. YOU NOW"
3720 PRINT" HAVE TO TRUCK half YOUR STOCK   SOUTH, AT A COST OF $5 A HEAD   TO AN OVERSUPPLIED MARKET. YOU"
3730 PRINT" ONLY REALIZED $30 A HEAD FOR    THEM, AND CANNOT RESTOCK UNTIL  OUT OF THE DROUGHT."
3750 CA(P)=CA(P)+(INT(HE(P)/2)*25):HE(P)=INT(HE(P)/2):LD(P)=1
3760 GOSUB11000:RETURN
3799 REM: -POT LUCK
3800 GOSUB6000:RETURN
3899 REM STOCK SALE
3900 GOSUB900:RETURN
4000 CLS8:PRINT@41,"DRENCH CATTLE";
4010 PRINT@96," EVERY YEAR THE SAME - DRENCH    THE FLAMING COWS. AND IT COSTS  50 CENTS A HEAD, AS WELL AS"
4020 PRINT" HAVING TO ROUND THEM UP, FEED   THEM IN THE YARDS, CHASE THEM   BACK OUT AGAIN. AND THE DUST!!"
4030 PRINT" SOMETIMES I WISH I WAS A CITY   BLOKE OR A FLAMING SAILOR OR    SOMETHING..."
4040 CA(P)=CA(P)-INT(HE(P)/2)
4050 GOSUB11000:RETURN
4100 CLS2:PRINT@43,"STUD BULL";
4105 IFSS(3)=1THENPRINT@237,"SOLD";:GOTO4190
4110 PRINT@96," 'E MIGHT LOOK LIKE A BIT OF A   SCRUBBER, BUT YOU ORTA SEE 'IM  WORK! GOT 110% CALVING OUT OF"
4120 PRINT" 'IM THE OTHER SEASON, AND SOLD  THE LOT FOR A PRETTY PENNY. I   CAN TELL YER."
4130 PRINT" BUT I'M PREPARED TO LET 'IM GO  FER $2000 TER YOU. INTERESTED?  <Y> OR <N>"
4140 S$=INKEY$:IFS$=""THEN4140
4150 IFS$<>"Y"THEN4190
4160 CA(P)=CA(P)-2000:SB(P)=SB(P)+1:SS(3)=1
4170 PRINT:PRINT" ORRITE! HE'S YOURS, BUT WATCH   'IM - 'E LIKES DONKEYS, TOO!"
4190 GOSUB9998:RETURN
4199 REM:-POT LUCK
4200 GOSUB6000:RETURN
4299 REM STOCK SALE
4300 GOSUB900:RETURN
4400 CLS3:PRINT@43,"LOCAL RAIN";
4410 ?@96," IT'S ALWAYS WELCOME, AND MORE   SO IN THE OUTBACK. THE GRASS    GROWS HIGH AND THE CATTLE GET   FAT, AND BREED."
4420 PRINT" YOUR RAIN HAS INCREASED YOUR    STOCK BY 500 HEAD, INCLUDING    THOSE WHO SURVIVED THE DROUGHT."
4430 PRINT@352," IF YOU WERE IN DROUGHT, YOU ARE NOT ANY MORE, "P$(P)"!"
4440 HE(P)=HE(P)+500:LD(P)=0
4450 GOSUB11000:RETURN
4500 CLS6:PRINT@44,"BULL DIES";
4510 PRINT@96," DUE TO OVERWORK, AND A LESS     THAN ADEQUATE DIET, YOUR BEST   STUD BULL HAS DIED."
4520 PRINT:PRINT" VET'S FEES WERE $50, BUT YOU    GOT THAT FOR THE CARCASE FROM   THE KNACKER, SO YOU'RE SQUARE"
4525 IFIS(P)=1THENPRINT" THERE, AND YOUR INSURANCE IS    PAID OUT ALSO.":IS(P)=0:CA(P)=CA(P)+2000:GOTO4540
4530 PRINT" THERE, BUT IF YOU DIDN'T OWN    A STUD BULL, THE OWNER OF THE   BULL YOU BORROWED WANTS $2000"
4540 IFSB(P)<1THENCA(P)=CA(P)-2000:GOTO4560
4550 SB(P)=SB(P)-1
4560 GOSUB11000:RETURN
4599 REM STOCK SALE
4600 GOSUB900:RETURN
4699 REM:-POT LUCK
4700 GOSUB6000:RETURN
4800 CLS8:PRINT@39,"APPLY FERTILIZER";
4810 PRINT@96," YOUR PASTURES, WHAT THERE IS    OF THEM IN THIS COUNTRY, WON'T  SURVIVE WITHOUT SOME HELP, SO"
4820 PRINT" SUPER SPREADING IS THE ORDER OF THE DAY, TO THE TUNE OF $500."
4830 PRINT@288,"      SO COUGH UP, MISER!!"
4840 CA(P)=CA(P)-500
4850 GOSUB11000:RETURN
4900 CLS2:PRINT@43,"STUD BULL";
4905 IFSS(4)=1THENPRINT@237,"SOLD";:GOTO4990
4910 PRINT@96," A THOROUGHLY GOOD ANIMAL, SIR,  WELL WORTHY OF YOUR CONSIDER-   ATION, IF I MIGHT SAY SO. I DO"
4920 PRINT" RECCOMMEND THAT YOU TAKE A GOOD LOOK AT THE RATHER LOW PRICES   BEING ASKED FOR IT AS WELL. I'M";
4930 ?" SURE WE CAN COME TO SOME SORT   OF ARRANGMENT WITH THE VENDOR.  SHALL WE OFFER $2000 ON YOUR    BEHALF? <Y> OR <N>"
4940 S$=INKEY$:IFS$=""THEN4940
4950 IFS$<>"Y"THEN4990
4960 CA(P)=CA(P)-2000:SB(P)=SB(P)+1:SS(4)=1
4970 PRINT" THANK YOU, SIR, WE'LL PUT THE   FEE ON YOUR ACCOUNT."
4990 GOSUB9998:RETURN
5000 GOSUB9900:PRINT@43,"ROUND-UP!";
5010 PRINT@96," THE BUSIEST TIME OF YEAR, AND   THE MOST EXCITING. FINDING THE"
5020 PRINT" CATTLE IN THE SCRUB - BRANDING  AND MARKING. DUST, NOISE AND    EXCITEMENT EVERYWHERE!"
5030 PRINT@288," AND YOU HAVE TO PAY FOR IT; AT  THE RATE OF A DOLLAR A HEAD!"
5040 CA(P)=CA(P)-(HE*3)
5050 GOSUB11000:RETURN
5099 REM STOCK SALE
5100 GOSUB900:RETURN
5199 REM:-POT LUCK
5200 GOSUB6000:RETURN
6000 CLS
6010 FORX=1TO10
6020 PRINT@RND(12)*32+RND(20),"POT LUCK":SOUNDRND(20)*10,1:NEXT
6030 CLS7:PRINT@32,"   $$$$$$$$ POT LUCK $$$$$$$$"
6040 R=RND(20)
6050 IFR>10THEN6070
6060 ONRGOSUB7100,7200,7300,7400,7500,7600,7700,7800,7900,8000
6065 GOTO7000
6070 ONR-10GOSUB8100,8200,8300,8400,8500,8600,8700,8800,8900,8900,9000
7000 RETURN
7100 PRINT@96,"       SPRING DRIES UP"
7110 PRINT:PRINT" ONE OF YOUR SPRINGS HAS DRIED   UP, AND YOU HAVE LOST 20 HEAD   IN THE MUD."
7120 HE(P)=HE(P)-20
7130 GOSUB11000:RETURN
7200 PRINT@96,"           STRAY BULL"
7210 PRINT:PRINT" MURPHY'S STUD BULL HAS BEEN IN  YOUR 50-MILE PADDOCK FOR MORE   THAN A YEAR, AND YOU HAVE 50"
7220 PRINT" MORE CATTLE THAN YOU THOUGHT!!"
7230 HE(P)=HE(P)+50
7240 GOSUB11000:RETURN
7300 PRINT@96,"     FIRE FIGHTING EQUIPMENT"
7310 PRINT:PRINT" YOU HAVE PURCHASED SOME FIRE    FIGHTING EQUIPMENT WHICH WILL   ENABLE YOU TO STOP THE GREAT"
7320 PRINT" DEVASTATION OF A BUSHFIRE."
7330 PRINT" THIS REMAINS WITH YOU FOR THE   WHOLE OF THE GAME."
7340 FF(P)=1:CA(P)=CA(P)-500
7350 PRINT@384," IT COST YOU $500, THOUGH!"
7360 GOSUB11000:RETURN
7400 PRINT@96,"           INCOME TAX"
7410 PRINT" THE TAXATION OFFICE HAS CAUGHT  UP WITH YOU AT LAST.  YOU OWE   10% OF YOUR CASH HOLDINGS, AND"
7420 PRINT" $1 PER HEAD OF CATTLE, WHICH    HAS BEEN PAINLESSLY(?) REMOVED  FROM YOUR COFFERS AS YOU READ   THIS AWFUL NOTE!"
7425 CA(P)=INT(CA(P)*.9)-HE(P)
7430 GOSUB11000:RETURN
7500 PRINT@96,"          PLANE CRASH"
7510 PRINT:PRINT" YOU HAVE PRANGED YOUR CESSNA,   AND THE REPAIR BILLS FOR YOU    AND THE PLANE HAVE SET YOU A"
7520 PRINT" COOL $2,000 BACK. BE MORE":PRINT" CAREFUL IN FUTURE!"
7530 CA(P)=CA(P)-2000
7540 GOSUB11000:RETURN
7600 PRINT@96," YOU HAVE WON THE COVETED SOIL   CONSERVATION TROPHY, AND WILL   BENEFIT BY YOUR ACTIONS AS"
7610 PRINT" MUCH AS THE $1,000 PRIZE."
7620 CA(P)=CA(P)+1000
7630 GOSUB11000:RETURN
7700 PRINT@96," FROM SALE OF HIGH QUALITY STUD  COWS TO THAT NEW COCKY OVER     ON THE FAR SIDE OF THE DESERT"
7710 PRINT" YOU RECEIVE $600."
7720 CA(P)=CA(P)+600
7730 GOSUB11000:RETURN
7800 PRINT@96," FOR AGISTING STOCK FROM THE     TERRITORY DURING THEIR DROUGHT  YOU RECEIVE $600."
7810 CA(P)=CA(P)+600
7820 GOSUB11000:RETURN
7900 PRINT@96," GOOD SPRING RAINS HAVE MADE THE YEAR FLY!":PRINT" MOVE ON TO "CHR$(34)"YEARLING SALE"CHR$(34)"!"
7910 PP(P)=1
7920 GOSUB11000:GOSUB1900:RETURN
8000 PRINT@96," YOUR STOCKYARDS ARE A DISGRACE  AND MUST BE REPLACED BEFORE     NEXT MUSTER - AND IF YOU HAVE
8010 PRINT" MORE THAN 3000 CATTLE, THEY     WILL COST YOU $2000. IF NOT,    THEN YOU ONLY NEED TO SPEND     $1000."
8030 IFHE(P)>3000THENCA(P)=CA(P)-2000:GOTO8050
8040 CA(P)=CA(P)-1000
8050 GOSUB11000:RETURN
8100 PRINT@96," DUE TO GOOD MANAGEMENT, YOUR    AVERAGE CARCASE WEIGHT HAS      INCREASED, AND YOU HAVE JUST"
8110 PRINT" RECEIVED AN UNEXPECTED BONUS    CHEQUE FOR $1,000!"
8120 CA(P)=CA(P)+1000
8130 GOSUB11000:RETURN
8200 PRINT@96," THIS IS AN INSURANCE POLICY     FOR ONE STUD BULL. IF YOUR BULL TURNS UP HIS TOES, YOU WILL"
8210 PRINT" RECEIVE $2000 COMPENSATION FROM THE BEAUT INSURANCE COMPANY."
8220 IS(P)=1
8230 GOSUB11000:RETURN
8300 PRINT@96," THERE IS A SPECIAL STOCK SALE   ON TODAY JUST DOWN THE ROAD.    HOP ON DOWN AND SEE WHAT YOU    CAN PICK UP."
8310 GOSUB11000
8320 PP(P)=31:GOSUB900:RETURN
8400 PRINT@96,"           INCOME TAX"
8410 PRINT:PRINT" THE TAXATION OFFICE HAS CAUGHT  UP WITH YOU AT LAST.  YOU OWE   10% OF YOUR CASH HOLDINGS, AND"
8420 PRINT" $1 PER HEAD OF CATTLE, WHICH    HAS BEEN PAINLESSLY(?) REMOVED  FROM YOUR COFFERS AS YOU READ   THIS AWFUL NOTE!"
8430 GOSUB11000:RETURN
8500 PRINT@96," BRUCELLOSIS HAS INFECTED YOUR   HERD, AND YOU HAVE LOST MOST    OF LAST YEAR'S CALVES. YOUR"
8510 PRINT" HERD HAS BEEN QUITE LITERALLY   DECIMATED, UNLESS YOU HAVE A    BRUCELLOSIS ACCREDITATION. IN"
8520 PRINT" THAT CASE, YOU ARE NOT":PRINT" AFFECTED."
8530 IFBR(P)=1THEN8550
8540 HE(P)=INT(HE(P)*.9)
8550 GOSUB11000:RETURN
8600 PRINT@96," BRUCELLOSIS ACCREDITATION HAS   FINALLY BEEN GIVEN TO YOUR      HERD, WHICH MAKES ALL THE WORK"
8610 PRINT" YOU HAVE DONE IN THE PAST TEN   YEARS WORTH WHILE."
8620 PRINT" YOU KEEP THIS PROTECTION FOR    THE WHOLE OF THE GAME."
8630 BR(P)=1
8640 GOSUB11000:RETURN
8700 PRINT@96," YOU PASTORAL LEASE HAS JUST     HAD TO BE RENEWED, AND THE      STAMP DUTY ON THE DOCUMENT HAS"
8710 PRINT" COST YOU $500. "," BUT YOUR TENURE IS SECURE FOR   ANOTHER 40 YEARS!"
8720 CA(P)=CA(P)-500
8730 GOSUB11000:RETURN
8800 PRINT@96," DUE TO DESTRUCTION OF SOME OF   YOUR FENCING BY THAT MINING     COMPANY, YOU RECEIVE $800 IN    COMPENSATION."
8810 CA(P)=CA(P)+800
8820 GOSUB11000:RETURN
8900 PRINT@96,"         GENERAL DROUGHT"
8910 PRINT" DESPITE MANY WARNINGS, YOU HAVE DESECRATED A SACRED SITE WITH A DUNNY, WHICH HAS INDUCED THE"
8920 PRINT" HARANGA TRIBE TO PLUNGE THE     WHOLE AREA INTO DROUGHT. EVERY  PLAYER IS NOW IN DROUGHT."
8925 PRINT@352," AREN'T YOU THE POPULAR ONE?!"
8930 FORX=1TON
8940 HE(X)=INT(HE(X)/2):CA(X)=CA(X)+(HE(X)*25):LD(X)=1
8950 NEXT
8960 GOSUB11000:RETURN
9000 PRINT@96,"          GENERAL RAIN"
9010 PRINT" DUE TO YOUR INFLUENCE WITH THE  HARANGA TRIBE, AND THE DECAY    OF CYCLONE "CHR$(34)"HUEY"CHR$(34)", THERE HAS"
9020 PRINT" BEEN GOOD GENERAL RAIN IN ALL   AREAS. NO PLAYER IS NOW IN      DROUGHT.  GOOD WORK, "P$(P)"!"
9030 FORX=1TON
9040 LD(X)=0:NEXT
9050 GOSUB11000:RETURN
9900 CLS0:FORZZ=1TO50:C1=RND(416):PRINT@C1,"Òˆ˜ÛÛÚ";:PRINT@C1+32,"Ù˝˝˝˝Ù";:PRINT@C1+64,"ÄÙÙÙÙÄ";:NEXT:RETURN
9998 FORD=1TO2500:NEXT
9999 FORD=1TO2500:NEXT:SOUND240,1:FORZZ=1TO25:M$=INKEY$:NEXT:RETURN
11000 FORZZ=1TO25:M$=INKEY$:NEXT:PRINT@448,"    PRESS A KEY TO CONTINUE "
11010 IFINKEY$=""THEN11010
11020 RETURN
12000 REM:-THAT'S ALL, FOLKS!
