0 CLS:CLEAR1000:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 CC=SQR((K1(I)-S1)^2+(K2(I)-S2)^2):RETURN:REM FND(N) FUNCTION
10 INPUT"DO YOU NEED INSTRUCTIONS";A$
20 IF LEFT$(A$,1)="Y" THEN GOSUB 2000
90 DIM D(5),K1(7),K2(7),K3(7),S(7,7),Q(7,7),D$(5)
100 Q$=".EKB*"
110 D$(0)="WARP ENGINES"
120 D$(1)="SHORT RANGE SENSORS"
130 D$(2)="LONG RANGE SENSORS"
140 D$(3)="PHASERS"
150 D$(4)="PHOTON TORPEDOES":D$(5)="GALACTIC RECORDS"
160 I=RND(-(PEEK(9)*256+PEEK(10)))
170 PRINT"WORKING-"
190 GOSUB 690:GOSUB 530:Q1=X:Q2=Y:X=8:Y=0:X1=.2075:Y1=6.28:X2=3.28
200 Y2=1.8:A=.96:C=100:W=10:K9=0:B9=0:S9=400:T9=3451:GOTO 220
210 K=K+(N<X2)+(N<Y2)+(N<.28)+(N<.08)+(N<.03)+(N<.01):K9=K9-K:GOTO 240
220 T0=3421:T=T0:E0=4000:E=E0:P0=10:P=P0:FOR I=0 TO 7
230 FOR J=0 TO 7:K=0:N=RND(Y):IF N<X1 THEN N=N*64:K=(N<Y1)-Y:GOTO 210
240 B=(RND(Y)>A):B9=B9-B:Q(I,J)=K*C+B*W-INT(RND(Y)*X+Y):NEXT J,I
250 IF K9>(T9-T0) THEN T9=T0+K9
260 IF B9>0 THEN 280
270 GOSUB 530:Q(X,Y)=Q(X,Y)-10:B9=1
280 PRINT"startrek":PRINT"ADAPTED BY L.E. COCHRAN 2/29/76":K0=K9
290 M$="OBJECTIVE: DESTROY"+STR$(K9)+" KLINGON BATTLE CRUISERS IN"+STR$(T9-T0)
300 M$=M$+" YEARS.":GOSUB1:M$="THE NUMBER OF STARBASES IS"+STR$(B9):GOSUB1
310 A=0:IF Q1<0 OR Q1>7 OR Q2<0 OR Q2>7 THEN N=0:S=0:K=0:GOTO 330
320 N=ABS(Q(Q1,Q2)):Q(Q1,Q2)=N:S=N-INT(N/10)*10:K=INT(N/100)
330 B=INT(N/10-K*10):GOSUB 530:S1=X:S2=Y
340 FOR I=0 TO 7:FOR J=0 TO 7:S(I,J)=1:NEXT J,I:S(S1,S2)=2
350 FOR I=0 TO 7:K3(I)=0:X=8:IF I<K THEN GOSUB 540:S(X,Y)=3:K3(I)=S9
360 K1(I)=X:K2(I)=Y:NEXT:I=S
370 IF B>0 THEN GOSUB 540:S(X,Y)=4
380 IF I>0 THEN GOSUB 540:S(X,Y)=5:I=I-1:GOTO 380
390 GOSUB 630:IF A=0 THEN GOSUB 560
400 IF E<=0 THEN 1450
410 I=1:IF D(I)>0 THEN 700
420 FOR I=0 TO 7:FOR J=0 TO 7:PRINT MID$(Q$,S(I,J),1);" ";:NEXT J
430 PRINT"  ";:ON I GOTO 460,470,480,490,500,510,520
440 PRINT"YEARS=";T9-T
450 NEXT:GOTO 730
460 PRINT"STARD=";T:GOTO 450
470 PRINT"COND: ";C$:GOTO 450
480 PRINT"QUAD.= ";MID$(STR$(Q1+1),2);"-";MID$(STR$(Q2+1),2):GOTO 450
490 PRINT"SECT.= ";MID$(STR$(S1+1),2);"-";MID$(STR$(S2+1),2):GOTO 450
500 PRINT"ENER.=";INT(E):GOTO 450
510 PRINT"TORP.=";P:GOTO 450
520 PRINT"KLING=";K9:GOTO 450
530 X=INT(RND(0)*8):Y=INT(RND(0)*8):RETURN
540 GOSUB 530:IF S(X,Y)>1 THEN 540
550 RETURN
560 IF K<1 THEN RETURN
570 IF C$="DOCKED" THEN PRINT"STARBASE PROTECTS ENTERPRISE":RETURN
580 FOR I=0 TO 7:IF K3(I)<=0 THEN NEXT:RETURN
590 H=K3(I)*.4*RND(0):K3(I)=K3(I)-H:GOSUB9:H=H/(CC^.4):E=E-H
600 E$="ENTERPRISE FROM":N=E:GOSUB 610:NEXT:RETURN
610 M$=MID$(STR$(INT(H)),2)+" UNIT HIT ON "+E$+" SECTOR "+MID$(STR$(K1(I)+1),2)+"-"+MID$(STR$(K2(I)+1),2)
620 IFN>0THENM$=M$+" ("+MID$(STR$(INT(N)),2)+" LEFT)"
625 GOSUB1:RETURN
630 FOR I=S1-1 TO S1+1:FOR J=S2-1 TO S2+1
640 IF I<0 OR I>7 OR J<0 OR J>7 THEN 660
650 IF S(I,J)=4 THEN C$="DOCKED":E=E0:P=P0:GOSUB 690:RETURN
660 NEXT J,I:IF K>0 THEN C$="RED":RETURN
670 IF E<E0*.1 THEN C$="YELLOW":RETURN
680 C$="GREEN":RETURN
690 FOR N=0 TO 5:D(N)=0:NEXT:RETURN
700 M$=D$(I)+" DAMAGED. "
710 M$=M$+MID$(STR$(D(I)),2)+" YEARS ESTIMATED FOR REPAIR.":GOSUB1
720 IF A=1 THEN RETURN
730 INPUT"COMMAND";M$:A=VAL(M$):IFLEFT$(M$,1)="H"THENGOSUB2050:GOTO730
740 M$="":IF A<1 OR A>6 THEN 760
750 ON A GOTO 790,390,1330,1220,770,1380
760 FOR I=0 TO 5:PRINT I+1;"= ";D$(I):NEXT:GOTO 730
770 IF D(4)>0 THEN PRINT"SPACE CRUD BLOCKING TUBES.":I=4:GOTO 710
780 N=15:IF P<1 THEN PRINT"NO TORPEDOES LEFT.":GOTO 730
790 IF A=5 THEN PRINT"TORPEDO ";
800 INPUT"COURSE (1-8.9)";C:IF C<1 THEN 730
810 IF C>=9 THEN 790
820 IF A=5 THEN P=P-1:PRINT"TRACK:";:GOTO 980
830 INPUT"WARP (0-12)";W:IF W<=0 OR W>12 THEN 790
840 IF W<=.2 OR D(0)<=0 THEN 860
850 I=0:M$=D$(I)+" DAMAGED, MAX IS .2 ":GOSUB1:GOSUB 710:GOTO 830
860 GOSUB 560:IF E<=0 THEN 1450
870 IF RND(0)>.25 THEN 950
880 X=INT(RND(0)*6):IF RND(0)>.5 THEN 910
890 D(X)=D(X)+INT(6-RND(0)*5):M$="**SPACE STORM, "
900 M$=M$+D$(X)+" DAMAGED**":GOSUB1:I=X:GOSUB 710:D(X)=D(X)+1:GOTO 950
910 FOR I=X TO 5:IF D(I)>0 THEN 940
920 NEXT
930 FOR I=0 TO X:IF D(I)>0 THEN NEXT:GOTO 950
940 D(I)=.5:M$="**SPOCK USED A NEW REPAIR TECHNIQUE**":GOSUB1
950 FOR I=0 TO 5:IF D(I)=0 THEN 970
960 D(I)=D(I)-1:IF D(I)<=0 THEN D(I)=0:M$=D$(I)+" ARE FIXED!":GOSUB1
970 NEXT:N=INT(W*8):E=E-N-N+.5:T=T+1:S(S1,S2)=1
980 Y1=S1+.5:X1=S2+.5:IF T>T9 THEN 1450
990 Y=(C-1)*.785398:X=COS(Y):Y=-SIN(Y)
1000 FOR I=1 TO N:Y1=Y1+Y:X1=X1+X:Y2=INT(Y1):X2=INT(X1)
1010 IF X2<0 OR X2>7 OR Y2<0 OR Y2>7 THEN 1190
1020 IF A=5 THEN PRINTTAB(7);MID$(STR$(Y2+1),2);"-";MID$(STR$(X2+1),2)
1030 IF S(Y2,X2)=1 THEN NEXT:GOTO 1140
1040 IF A=1 THEN M$="BLOCKED BY "
1050 ON S(Y2,X2)-3 GOTO 1120,1100
1060 M$=M$+"KLINGON":IF A=1 THEN 1130
1070 FOR I=0 TO 7:IF Y2<>K1(I) THEN 1090
1080 IF X2=K2(I) THEN K3(I)=0
1090 NEXT:K=K-1:K9=K9-1:GOTO 1150
1100 M$=M$+"STAR":IF A=5 THEN S=S-1:GOTO 1150
1110 GOTO 1130
1120 M$=M$+"STARBASE":IF A=5 THEN B=2:GOTO 1150
1130 M$=M$+" AT SECTOR "+MID$(STR$(Y2+1),2)+"-"+MID$(STR$(X2+1),2):GOSUB1:Y2=INT(Y1-Y):X2=INT(X1-X)
1140 S1=Y2:S2=X2:S(S1,S2)=2:A=2:GOTO 390
1150 M$=M$+" DESTROYED!":IF B=2 THEN B=0:M$=M$+" GOOD WORK!"
1160 GOSUB1:S(Y2,X2)=1:Q(Q1,Q2)=K*100+B*10+S:IF K9<1 THEN 1480
1170 GOSUB 560:IF E<=0 THEN 1450
1180 GOSUB 630:GOTO 730
1190 IF A=5 THEN PRINT"MISSED!":GOTO 1170
1200 Q1=INT(Q1+W*Y+(S1+.5)/8):Q2=INT(Q2+W*X+(S2+.5)/8)
1210 Q1=Q1-(Q1<0)+(Q1>7):Q2=Q2-(Q2<0)+(Q2>7):GOTO 310
1220 I=3:IF D(I)>0 THEN 700
1230 PRINT"PHASERS READY:":INPUT"ENERGY UNITS TO FIRE";X:IF X<=0 THEN 730
1240 IF X>E THEN PRINT"ONLY GOT";INT(E):GOTO 1230
1250 E=E-X:Y=K:FOR I=0 TO 7:IF K3(I)<=0 THEN 1310
1260 GOSUB9:H=X/(Y*(CC^.4)):K3(I)=K3(I)-H
1270 E$="KLINGON AT":N=K3(I):GOSUB 610
1280 IF K3(I)>0 THEN 1310
1290 PRINT"**KLINGON DESTROYED**"
1300 K=K-1:K9=K9-1:S(K1(I),K2(I))=1:Q(Q1,Q2)=Q(Q1,Q2)-100
1310 NEXT:IF K9<1 THEN 1480
1320 GOTO 1170
1330 I=2:IF D(I)>0 THEN 700
1340 PRINT"LONG RANGE SENSOR SCAN FOR":PRINT"QUADRANT ";MID$(STR$(Q1+1),2);"-"+MID$(STR$(Q2+1),2)
1350 FOR I=Q1-1 TO Q1+1:FOR J=Q2-1 TO Q2+1:PRINT"   ";
1360 IF I<0 OR I>7 OR J<0 OR J>7 THEN PRINT"***";:GOTO 1430
1370 Q(I,J)=ABS(Q(I,J)):GOTO 1420
1380 I=5:IF D(I)>0 THEN 700
1390 M$="CUMULATIVE GALACTIC MAP FOR STARDATE:"+STR$(T):GOSUB1
1400 FOR I=0 TO 7:FOR J=0 TO 7:PRINT" ";
1410 IF Q(I,J)<0 THEN PRINT"***";:GOTO 1430
1420 E$=STR$(Q(I,J)):E$="00"+MID$(E$,2):PRINT RIGHT$(E$,3);
1430 NEXT J:IFA=3THENPRINT
1435 NEXT I:GOTO 730
1440 PRINT:PRINT"IT IS STARDATE";T:RETURN
1450 GOSUB 1440:M$="THANKS TO YOUR BUNGLING, THE FEDERATION WILL BE "
1460 M$=M$+"CONQUERED BY THE REMAINING"+STR$(K9)+" KLINGON CRUISERS!":GOSUB1
1470 M$="YOU ARE DEMOTED TO CABIN BOY!":GOSUB1:GOTO 1510
1480 GOSUB 1440:M$="THE FEDERATION HAS BEEN SAVED!":GOSUB1
1490 M$="YOU ARE PROMOTED TO ADMIRAL.":GOSUB1:M$=MID$(STR$(K0),2)+" KLINGONS IN"
1500 M$=M$+STR$(T-T0)+" YEARS.  RATING="+STR$(INT(K0/(T-T0)*1000)):GOSUB1
1510 INPUT"TRY AGAIN";E$:IF LEFT$(E$,1)="Y" THEN 190
1520 END

2000 CLS:M$="IT IS STARDATE 3421 AND THE FEDERATION IS BEING INVADED BY A BAND OF KLINGON 'PIRATES' "
2010 M$=M$+"WHOSE OBJECTIVE IS TO TEST OUR DEFENSES.":GOSUB1
2020 M$="IF EVEN ONE SURVIVES THE TRIAL PERIOD, KLINGON HEADQUARTERS WILL LAUNCH AN ALL-OUT ATTACK.":GOSUB1
2030 M$="AS CAPTAIN OF THE FEDERATION STARSHIP 'ENTERPRISE', YOUR MISSION IS TO FIND AND DESTROY THE INVADERS "
2040 M$=M$+"BEFORE THE TIME RUNS OUT.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2050 M$="THE KNOWN GALAXY IS DIVIDED INTO 64 QUADRANTS ARRANGED LIKE A SQUARE CHECKERBOARD, "
2060 M$=M$+"8 ON A SIDE.  EACH QUADRANT IS LIKEWISE DIVIDED INTO 64 SECTORS ARRANGED AS AN 8 BY 8 SQUARE.":GOSUB1
2070 M$="EACH SECTOR CAN CONTAIN A KLINGON (K), STAR (*), STARBASE (B), THE ENTERPRISE HERSELF (E), "
2080 M$=M$+"OR EMPTY SPACE (.).":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2090 M$="EACH SECTOR IS ALSO NUMBERED; A STARBASE IN SECTOR 3-5 IS 3 ROWS DOWN FROM THE TOP OF THE SHORT RANGE "
2100 M$=M$+"SCAN PRINT-OUT, AND 5 SECTORS TO THE RIGHT.":GOSUB1
2110 M$="DOCKING AT A STARBASE IS DONE BY OCCUPYING AN ADJACENT SECTOR, AND REPROVISIONS YOUR STARSHIP WITH "
2120 M$=M$+"ENERGY AND PHOTON TORPEDOES, AS WELL AS REPAIRING ALL DAMAGES.":GOSUB1
2120 M$="YOUR STARSHIP WILL ACT ON THE FOLLOWING COMMANDS:":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2130 PRINT"COMMAND 1 - WARP ENGINES"
2140 M$="WARP ENGINE CONTROL IS USED TO MOVE THE ENTERPRISE.":GOSUB1
2150 M$="YOU WILL BE ASKED TO SET THE DISTANCE (MEASURED IN WARPS), AND THE COURSE FOR THE MOVE.":GOSUB1
2160 M$="EACH MOVE THAT YOU MAKE WITH THE ENTERPRISE FROM ONE SECTOR TO ANOTHER, OR FROM ONE QUADRANT "
2170 M$=M$+"TO ANOTHER, COSTS YOU ONE STARDATE (ONE YEAR).":GOSUB1
2180 M$="THEREFORE, A 30-YEAR GAME MEANS YOU HAVE 30 MOVES TO WIN IN.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2190 M$="COURSE - A NUMBER FROM 1 TO 8.999. TO MOVE TO THE LEFT, USE A COURSE OF 5.":GOSUB1
2200 M$="A COURSE OF 3.5 IS HALFWAY BETWEEN 3 AND 4. A COURSE OF 8.75 IS THREE QUARTERS OF THE WAY FROM 8 TO 1.":GOSUB1
2230 PRINT"4   3   2"
2240 PRINT"  "CHR$(92)" ! /"
2250 PRINT"5 - * - 1"
2260 PRINT"  / ! "CHR$(92)
2270 PRINT"6   7   8":GOSUB2600:IFM$="A"THENRETURN

2280 M$="WARP - ONE WARP MOVES YOU THE WIDTH OF A QUADRANT.":GOSUB1
2290 M$="A WARP OF .5 WILL MOVE YOU HALFWAY THROUGH A QUADRANT; MOVING DIAGONALLY ACROSS A QUADRANT "
2300 M$=M$+"TO THE NEXT WILL REQUIRE 1.414 WARPS.":GOSUB1
2310 M$="WARP 3 WILL MOVE YOU 3 QUADRANTS PROVIDING NOTHING IN YOUR PRESENT QUADRANT BLOCKS YOUR EXIT.":GOSUB1
2315 GOSUB2600:IFM$="A"THENRETURN

2320 M$="ONCE YOU LEAVE THE QUADRANT THAT YOU WERE IN, YOU WILL ENTER HYPERSPACE.":GOSUB1
2330 M$="COMING OUT OF HYPERSPACE WILL PLACE YOU RANDOMLY IN THE NEW QUADRANT.":GOSUB1
2340 M$="KLINGONS IN A GIVEN QUADRANT WILL FIRE AT YOU WHENEVER YOU LEAVE, ENTER OR MOVE WITHIN THE QUADRANT.":GOSUB1
2350 M$="ENTERING A COURSE OR WARP OF ZERO CAN BE USED TO RETURN TO THE COMMAND MODE.":GOSUB1
2355 GOSUB2600:IFM$="A"THENRETURN

2360 PRINT"COMMAND 2 - SHORT RANGE SENSORS"
2370 M$="THIS COMMAND WILL PRINT OUT THE QUADRANT YOU PRESENTLY OCCUPY SHOWING THE CONTENT OF EACH OF THE 64 SECTORS, "
2380 M$=M$+"AS WELL AS OTHER PERTINENT INFORMATION.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2390 PRINT"COMMAND 3 - LONG RANGE SENSORS"
2400 M$="THIS COMMAND SUMMARIZES THE QUADRANT YOU ARE IN, AND THE ADJOINING ONES.":GOSUB1
2410 M$="EACH QUADRANT IS REPRESENTED AS A 3-DIGIT NUMBER.":GOSUB1
2420 M$="THE FIRST DIGIT IS THE NUMBER OF KLINGONS IN THAT QUADRANT, 
2430 M$=M$+"THE MIDDLE DIGIT, THE NUMBER OF STARBASES AND THE LAST DIGIT, THE NUMBER OF STARS.":GOSUB1
2440 M$="AN ENTRY OF 305 MEANS 3 KLINGONS, NO STARBASES, AND 5 STARS.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2450 PRINT"COMMAND 4 - FIRE PHASERS"
2460 M$="THE PORTION OF THE ENTERPRISE'S ENERGY THAT YOU SPECIFY WILL BE DIVIDED EVENLY "
2470 M$=M$+"AMONG THE KLINGONS IN THE QUADRANT AND FIRED AT THEM.":GOSUB1
2480 M$="SURVIVING KLINGONS WILL RETALIATE. PHASER FIRE BYPASSES STARS AND STARBASES, BUT "
2490 M$=M$+"IS ATTENUATED BY THE DISTANCE IT TRAVELS.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN

2500 M$="THE ARRIVING ENERGY DEPLETES THE SHIELD POWER OF ITS TARGET.":GOSUB1
2510 M$="ENERGY IS AUTOMATICALLY DIVERTED TO THE SHIELDS AS NEEDED, BUT IF YOU RUN OUT OF ENERGY YOU'LL GET FRIED.":GOSUB1
2515 GOSUB2600:IFM$="A"THENRETURN

2520 PRINT"COMMAND 5 - PHOTON TORPEDO"
2530 M$="THIS COMMAND WILL LAUNCH A TORPEDO ON A COURSE YOU SPECIFY WHICH WILL DESTROY ANY OBJECT IN ITS PATH.":GOSUB1
2530 M$="RANGE IS LIMITED TO THE LOCAL QUADRANT. EXPECT RETURN FIRE FROM SURVIVING KLINGONS.":GOSUB1
2535 GOSUB2600:IFM$="A"THENRETURN

2540 PRINT"COMMAND 6 - GALACTIC RECORDS"
2550 M$="THE SHIP'S COMPUTER RESPONDS TO THIS COMMAND BY PRINTING OUT A GALACTIC MAP "
2560 M$=M$+"SHOWING THE RESULTS OF ALL PREVIOUS SENSOR SCANS.":GOSUB1:GOSUB2600:IFM$="A"THENRETURN
2570 RETURN

2600 PRINT"space TO CONTINUE OR a TO ABORT"
2610 M$=INKEY$:IFM$<>" "ANDM$<>"A"THEN2610
2620 CLS:RETURN
