0 CLS
10 PRINT "THIS IS A COMPUTER SIMULATION OF";
11 PRINT "AN APOLLO LUNAR LANDING CAPSULE."
12 PRINT "THE ON-BOARD COMPUTER HAS FAILED(IT WASN'T MADE BY TANDY)"
13 PRINT "SO YOU HAVE TO LAND MANUALLY."
14 PRINT "SET BURN RATE OF RETRO ROCKETS"
15 PRINT "TO VALUE BETWEEN 0 (FREE FALL)":PRINT"AND 200 (MAXIMUM BURN) LBS/SEC."
16 PRINT "SET NEW BURN RATE EVERY 10 SEC."
17 PRINT "CAPSULE WEIGHT 32,500 LBS;":PRINT"FUEL WEIGHT 16,500 LBS"
18 PRINT "GOOD LUCK ! ! !"
19 L=0
20 PRINT:PRINT " MI + FT     MPH   LB FUEL BURN":A=120:V=1:M=33000:N=16500:G=1E-3:Z=1.8
21 PRINTLEFT$(STR$(INT(A))+"     ",6)LEFT$(STR$(INT(5280*(A-INT(A))))+"     ",6);
22 PRINTLEFT$(STR$(3600*V)+"     ",6)LEFT$(STR$(M-N)+"     ",7);
23 INPUT K:T=10
24 IF K<0 OR K>200 THEN FORCC=1TO7:PRINTCHR$(8);:NEXT:GOTO 23
31 ON -(M-N<.001) GOTO 41:ON -(T<.001) GOTO 21:S=T:IF M>=N+S*K THEN 35
32 S=(M-N)/K
35 GOSUB 91:ON -(I<=0) GOTO 71:ON -(V<=0) GOTO 38:IF J<0 THEN 81
38 GOSUB 61:GOTO 31
41 PRINT "FUEL OUT AT"L"SEC":S=(-V+SQR(V*V+2*A*G))/G:V=V+G*S:L=L+S
51 W=3600*V:PRINT"ON MOON AT"INT(L)"SEC":PRINT"IMPACT VELOCITY"W"MPH"
52 ON -(W>1.2) GOTO 53:PRINT "PERFECT LANDING! (LUCKY) ":GOTO 95
53 ON -(W>10) GOTO 56:PRINT "GOOD LANDING (COULD BE BETTER)":GOTO 95
56 ON -(W>60) GOTO 58:PRINT "CRAFT DAMAGE... YOU'RE STRANDED"
57 PRINT "UNTIL RESCUE PARTY ARRIVES.":PRINT"HOPE YOU HAVE ENOUGH OXYGEN!":GOTO 95
58 PRINT "SORRY, BUT THERE WERE NO":PRINT"SURVIVORS... YOU BLEW IT!"
59 PRINT "IN FACT, YOU BLASTED A NEW":PRINT"LUNAR CRATER"INT(W*.2777)"FT DEEP"
60 GOTO 95
61 L=L+S:T=T-S:M=M-S*K:A=I:V=J:RETURN
71 ON -(S<5E-3) GOTO 51:D=V+SQR(V*V+2*A*(G-Z*K/M)):S=2*A/D
73 GOSUB 91:GOSUB 61:GOTO 71
81 W=(1-M*G/(Z*K))/2:S=M*V/(Z*K*(W+SQR(W*W+V/Z)))+.05:GOSUB 91
83 ON-(I<=0)GOTO71:GOSUB 61:ON-(J>0)GOTO31:ON-(V>0)GOTO81:GOTO 31
91 Q=S*K/M:J=V+G*S+Z*(-Q-Q*Q/2-Q^3/3-Q^4/4-Q^5/5)
94 I=A-G*S*S/2-V*S+Z*S*(Q/2+Q^2/6+Q^3/12+Q^4/20+Q^5/30):RETURN
95 INPUT"TRY AGAIN";M$:IF LEFT$(M$,1)="Y" THEN CLS:GOTO14
99 END
