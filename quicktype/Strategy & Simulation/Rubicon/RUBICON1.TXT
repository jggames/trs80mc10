2 V=RND(TIMER):MG$="L190O1V28CV29DV20EV10F#V21G#V16GV31":SG=3+RND(3):CLS
3 R$=CHR$(175):A$=CHR$(137):B$=CHR$(141):C$=CHR$(134):H$=A$+C$+B$:D$="%%%":E$="<>":G$="2ND INF":I$="CONTACT":K$=" elim":H1$=H$:H2$=H$:H3$=H$:GT$="ground troop str":IU$="INFANTRY"
4 L$="14TH FTR":N$="191ST ART":O$="3RD INF":P$="1ST INF":Q$="9TH LT TNK":S$="6TH HV TNK":UN$=" UNITS":UT$=" UNIT"
5 DIMM(42),B$(42):SB$="standby":AV$="none available":NP$="not enough points"
6 SY=4:T$="#":U$="&":V$="*":QB=1:FV=1:LJ=RND(5)+1:VT$=" victory":BL$="bad location"
7 D=417:DA=427:DB=438:G=455:GA=466:B=1:NX=RND(5)
8 GOTO97
9 DIMLX(U),SX(U),X$(U),CX(U)
10 FORE=1TOU:X$(E)="grndr"
11 LX(E)=288+RND(95)
12 IFE<U/2THENLX(E)=RND(120)+200
13 SX(E)=RND(15)+8:Q=SX(E):S=S+Q
14 IFSX(E)>20-V THENX$(E)="panzr":M=M+1
15 NEXT
16 FORO=1TO40:M(O)=RND(168)+200:NEXT
17 AF=68+RND(19)+(RND(4)-1)*32
18 H(1)=98:H(2)=90:H(3)=210
19 FORX=1TO5:V(X)=(158+6*X)+(RND(5)*32):NEXT
20 RETURN
21 FORO=1TO40:PRINT@M(O),E$;:NEXT
22 FORX=1TO5:IFV(X)=0THEN25
23 LX(X)=V(X)
24 PRINT@V(X),"=";
25 NEXT
26 FORX=1TO3:LX(X+5)=H(X)-2+NX:NEXT
27 LX(9)=AF-2+NX
28 PRINT@AF,D$;
29 PRINT@H(1),H1$;
30 PRINT@H(2),H2$;
31 PRINT@H(3),H3$;
32 FORX=384TO415:PRINT@X,R$;:NEXT:RETURN
33 FORI=1TOF1+4
34 IFS(I)=0THEN39
35 PRINT@L(I),U$;
36 IFI=<H THENPRINT@L(I),V$;
37 IFI>H+J THENPRINT@L(I),T$;
38 IFI>F1 THENPRINT@L(I),"@";
39 NEXT
40 PRINT@0,"F"FF"E"EE
41 PRINT@22,"TN"B
42 INPUT"MOVE UNIT";I
43 IFAR=1THEN225
44 IFAT=1THEN241
45 IFI>F1+4THEN374
46 IFI=0THENCLS:GOTO82
47 IFS(I)=0THENCLS:PRINTUT$;I;K$:GOTO82
48 PRINT@L(I)-1,S(I);
49 IFI<=F1 AND SP(I)=0THENFORX=1TO500:NEXT:GOTO406
50 IFM1(I)<>0THEN52
51 FORX=1TO500:NEXT:GOTO80
52 W$=INKEY$
53 IFW$=""THEN52
54 IFW$="F"THEN407
55 IFW$="E"THENCLS:GOTO81
56 IFW$=CHR$(8)THENL(I)=L(I)-1:D(I)=1:GOSUB63
57 IFW$=CHR$(9)THENL(I)=L(I)+1:D(I)=2:GOSUB63
58 IFL(I)<33THEN60
59 IFW$=CHR$(94)THENL(I)=L(I)-32:D(I)=4:GOSUB63
60 IFL(I)>480THEN62
61 IFW$=CHR$(10)THENL(I)=L(I)+32:D(I)=3:GOSUB63
62 GOTO50
63 PRINT@L(I),CHR$(43);
64 M1(I)=M1(I)-1
65 IFL(I)>415THENRETURN
66 IFL(I)>383ANDL(I)<416THENM1(I)=0:TT=4
67 IFI=<H THEN72
68 FORO=1TO40
69 IFL(I)=M(O)THENM1(I)=0:GOTO71
70 IFL(I)=M(O)+1THENM1(I)=0
71 NEXT
72 FORE=1TOU
73 IFL(I)=LX(E)THENGOSUB95:GOTO78
74 IFL(I)=LX(E)+1THENGOSUB95:GOTO78
75 IFL(I)=LX(E)-1THENGOSUB95:GOTO78
76 IFL(I)=LX(E)+30+RND(3)THENGOSUB95
77 IFL(I)=LX(E)-30-RND(3)THENGOSUB95
78 NEXT
79 GOSUB307:RETURN
80 RR=GT-FF:IFRR<2*FF/5THENBX=1:GOTO324
81 QE=0:TT=0:FM=0:CLS:PRINTUT$;I"--MOVEMENT ENDED":PRINT"str now"S(I)
82 PRINT:PRINTGT$;RR:INPUT"SEE MOVES LEFT(9)";A:IFA=9THEN84
83 PRINTRND(TIMER):CLS:GOTO92
84 X=1:Z=1
85 PRINT"UNIT  LOC  SUP "," MP   STR"
86 FORI=1TOF1+4
87 IFS(I)=0ORM1(I)=0ORSP(I)=0THEN91
88 Z=Z+1
89 IFZ>X*10THENINPUT"CONTINUE";A:X=X+1
90 PRINTI;TAB(5)L(I)" "SP(I),M1(I)"  "S(I)
91 NEXT
92 QD=0:INPUT"END TURN(QQ)";Y$:CLS
93 IFY$="QQ"THENB=B+1:NX=RND(5):BB=0:CLS0:PRINT@204,SB$;:GOTO351
94 GOSUB21:GOTO33
95 FORO=1TO40:IFLX(E)=M(O)THENTT=2:NEXT
96 GOSUB278:RETURN
97 CLS4:PRINT@198,"across the rubicon";
98 PRINT@354,"level (1-4)";
99 INPUTV:IFV<1ORV>4THEN97
100 IFV=1THENUX=15:U=33
101 IFV=2THENUX=12:U=39
102 IFV=3THENUX=8:U=36
103 IFV=4THENUX=5:U=41
104 FJ=NX+1:CLS
105 PRINTIU$+UN$"(15PTS)";:INPUTH:IFH<15THEN 105          
106 PR=1200-H*15
107 X=42
108 DIMC(X),S(X),M1(X),L(X),D(X),SP(X):X=0
109 GOSUB275:FORI=1 TO H
110 SP(I)=3:S(I)=12+RND(5):Q=S(I):R=R+Q
111 B$(I)=G$
112 IFI<=H/3THENB$(I)=P$
113 IFI>H/3*2THENB$(I)=O$
114 NEXTI
115 GOSUB273
116 PRINTQ$+UN$"(30PTS)";:INPUTJ
117 IFJ=0THEN122
118 PR=PR-30*J:GOSUB275
119 FORI=H+1TOH+J
120 SP(I)=3:S(I)=16:B$(I)=Q$
121 NEXT:GOSUB273
122 PRINTS$+UN$"(55PTS)";:INPUTK
123 IFK=0THEN129
124 PR=PR-55*K:GOSUB275
125 FORI=H+J+1TOH+J+K
126 SP(I)=3:S(I)=25:B$(I)=S$
127 NEXT
128 GOSUB273
129 FORI=H+J+K+1TOH+J+K+4
130 B$(I)="4TH SUP":S(I)=1:SP(I)=4:NEXT
131 PRINTL$+UN$"(75PTS)";:INPUTL
132 IFL=0THEN136
133 PR=PR-L*75:GOSUB275
134 FORI=H+J+K+5TOH+J+K+4+L
135 S(I)=8:LT=8*L:B$(I)=L$:NEXT:GOSUB273
136 PRINTN$+UN$"(140PTS)";:INPUTP:P1=P
137 IFP=0THEN141
138 PR=PR-(P*140):GOSUB275
139 FORI=H+J+K+4+L+1TOH+J+K+4+L+P
140 S(I)=RND(15)+15:B$(I)=N$:NEXT
141 IFPR>100THENPR=100
142 CLS:PRINT"YOU COMMAND:
143 PRINTH;IU$+UN$
144 PRINTJ;Q$+UN$
145 PRINTK;S$+UN$
146 PRINTL;L$+UN$
147 PRINTP;N$+UN$
148 PRINT" 4 SUPPLY"+UN$
149 PRINTPR"RESERVE PTS
150 PRINTGT$;R+J*15+K*25:RR=R+J*15+K*25:GT=RR:GOSUB9
151 INPUT"ENTER TO BEGIN";A:CLS0:PRINT@204,SB$;
152 F=H+J+K+4+L+P
153 F1=H+J+K
154 IFB=1THEN178
155 GOSUB385:CLS:GOSUB156:GOTO157
156 PRINT"unit loc  sup  str    status":RETURN
157 FORI=1TOF
158 C(I)=0:IFS(I)=0THENL(I)=0
159 IFL(I)=<0THENL(I)=1
160 PRINTI;TAB(4)L(I);TAB(10)SP(I);TAB(15)S(I);TAB(21)B$(I)
161 IFI=INT(F/4)THENGOSUB274
162 IFI=INT(F/2)THENGOSUB274
163 IFI=INT((F/4)*3)THENGOSUB274
164 NEXT
165 PRINT"LOSSES F"FF"E"EE:RR=GT-FF
166 PRINTGT$;RR
167 PRINT"TURN"B
168 INPUT"MAP(1)-INT RPT(2)REIN(3)-REPL(4)ART(5)-AIR(6)-PARA(7)-TTY(8)";C
169 IFC=1THEN188
170 IFC=2THEN196
171 IFC=3THENCLS0:GOTO203
172 IFC=4THEN256
173 IFC=5THEN219
174 IFC=6THEN238
175 IFC=7THEN412
176 IFC=8THENIFB>LJ THEN437ELSEPRINTAV$:GOTO168
177 GOTO157
178 IFB>1THEN188
179 FORI=1TOF1+4
180 C(I)=0:IFB$(I)=P$THENL(I)=D:D=D+1
181 IFB$(I)=G$THENL(I)=DA:DA=DA+1
182 IFB$(I)=O$THENL(I)=DB:DB=DB+1
183 IFB$(I)=Q$THENL(I)=G:G=G+1
184 IFB$(I)=S$THENL(I)=GA:GA=GA+1
185 IFI>F1 THENL(I)=421+Y:Y=Y+7
186 NEXT
187 Y=0:GOTO155
188 CLS:GOSUB21
189 FORI=1TOF
190 IFL(I)=0THEN194
191 M1(I)=4:C(I)=0
192 IFI=<H THENM1(I)=2
193 IFI>H+J THENM1(I)=3
194 NEXT
195 GOTO33
196 CLS:PRINT@9,"intelligence
197 PRINT
198 PRINT"ENEMY STRENGTH:"
199 PRINT S-EE "MEN" M"ARMOR"UN$
200 PRINTEU"ENEMY"UN$+K$:PRINT
201 PRINTVP"VICTORY PTS--" LT"tac"UN$:PRINT
202 GOTO165
203 PRINT"REINFORCEMENTS--LIMIT 15 STR
204 PRINTPR"PTS LEFT"
205 PRINT
206 INPUT"UNIT NO. (MUST BE ELIM INF)";I
207 IFI=0ORI>F THENCLS:GOTO165
208 IFI>H OR S(I)>0THENPRINTUT$;I"not elim inf":GOTO203
209 PRINT"1 S/P= 1 R/P
210 PRINT
211 INPUT"NEW STRENGTH";N
212 IFN>15THENCLS:GOTO203
213 PR=PR-N:GOSUB214:M1(I)=2:GOTO215
214 IFPR<0THENPRINTNP$::PR=PR+N:GOTO203
215 B$(I)="REINF
216 S(I)=N:L(I)=432
217 ZM=ZM+N:PRINT"UNIT"I"REINF--STR:"S(I):GT=GT+N:SP(I)=3
218 GOTO165
219 IFP1=0THENCLS6:PRINT"art--"AV$:GOTO168
220 AR=1:PRINT"art barrage":GOSUB221:GOTO188
221 INPUT"LOCATION";BC
222 IFBC=0THENAR=0:AT=0:GOTO165
223 IFBC<33ORBC>480THENPRINTBC;""BL$:GOTO221
224 RETURN
225 FORX=1TO8
226 ONRND(3)GOSUB368,369,370
227 PRINT@BD,CHR$(36);:PLAYMG$+MG$+MG$
228 FORE=1TO U
229 IFLX(E)=BD THENGOSUB371
230 NEXT:PRINT@BD," ";
231 NEXT
232 CLS3:PRINT"barrage over
233 PRINT"ENEMY LOSSES--"BH
234 EE=EE+BH:BG=0:BH=0:AR=0
235 IFRND(6)=1THENP1=P1-1
236 AB=AB+1
237 GOTO165
238 IFL=0ORLT=0THENCLS4:PRINTAV$:GOTO168
239 AT=1:PRINT"air strike
240 GOSUB221:GOTO188
241 FORX=1TO 8
242 ONRND(2)GOSUB368,370
243 PRINT@BD,":";:FORXX=1TO4:PLAYMG$+MG$:NEXT
244 FORE=1TO U
245 IFLX(E)=BD THENGOSUB371
246 NEXTE
247 PRINT@BD," ";
248 NEXTX
249 CLS8:PRINT"strike over
250 AP=AP+1:PRINT"ENEMY LOSSES--"BH
251 EE=EE+BH:BG=0:BH=0:AT=0
252 IFRND(6)>2THENLT=LT-1:IFRND(6)>RND(4)THENLT=LT-2
253 IFRND(6)<3THENLT=LT-RND(5)
254 IFLT<0THENLT=0
255 AT=0:PRINT"TAC UNITS LEFT"LT:GOTO237
256 CLS2:PRINT@10,"REPLACEMENTS
257 IFBB=1THEN269
258 BB=1
259 IFFF<RND(100)THEN269
260 FORX=1TO FV
261 I=RND(F1)
262 IFSP(I)=0THEN261
263 IFS(I)=0ORC(I)=1THEN267
264 IFS(I)>11THEN267
265 C(I)=1:RP=RND(3)+1:S(I)=S(I)+RP:TR=TR+RP:GT=GT+RP
266 PRINTUT$;I"RECEIVED"RP"REPL":PRINT"STRENGTH  NOW"S(I)
267 NEXT:FORI=1TOF1:C(I)=0:NEXT
268 GOTO165
269 PRINT@105,AV$;
270 FORX=1TO1500:NEXT:CLS:GOTO165
271 GOTO246
272 GOTO278
273 PRINT"POINTS LEFT"PR:RETURN
274 INPUT"CONTINUE";A:GOSUB156:RETURN
275 IFPR<0THENPRINTNP$ELSERETURN
276 PRINT"START OVER(RUN)
277 END
278 W=SX(E):Y=S(I):AN=Y:KR=W:C(I)=1:CX(E)=2
279 IFW=0THEN350
280 IFY=0THEN350
281 QD=1:B$(I)="CONTACT":GOSUB375
282 M1(I)=0:IFQE=1THENVV=0
283 PRINT@22,X$(E);:PRINT@28,W;
284 PRINT@64,"C @"LX(E);
285 PRINT@LX(E),"!";
286 IFI>F1 THENSY=SY+1:GOTO290
287 IFW=>Y THENT=INT(W/Y)
288 IFY=>W THENV=INT(Y/W)
289 V=V+VV:T=T+TT:IFX$(E)="panzr"THENT=T+1
290 FORX=1TO14:PLAYMG$:NEXT
291 IFI>F1 THENL(I)=432:GOTO310
292 ONRND(6)GOTO293,294,295,296,297,298
293 W=W-V:Y=Y-2*T:GOTO299
294 W=W-V-1:Y=Y-T-1:GOTO299
295 W=W-V-1:Y=Y-T-1:GOTO299
296 W=W-V-2:Y=Y-T-2:GOTO299
297 W=W-V:Y=Y-T-1:GOTO299
298 W=W-2*V:Y=Y-T
299 IFY=<0THENY=0:SOUND10,6:L(I)=0:B$(I)=K$:M1(I)=0
300 IFY>0THEN302
301 IFI>H THENHT=HT+1ELSELM=LM+1
302 IFW=<0 THENW=0:SOUND150,3:SOUND50,6:CX(E)=4:M1(I)=1:EU=EU+1:C(I)=2:QD=0:LX(E)=0:GOSUB451:GOSUB380
303 S(I)=Y:SX(E)=W
304 IFS(I)=0OR L(I)>383THENM1(I)=0
305 AK=AN-Y:DK=KR-W:VV=0:FF=FF+AK:EE=EE+DK:V=0:T=0
306 IFL(I)=LX(E)THENL(I)=L(I)+32
307 FORX=1TO5:IFV(X)=0THEN309
308 IFL(I)=V(X)THENFV=FV+1:VP=VP+1:SOUND200,10:V(X)=0
309 NEXT
310 QE=1:IFQD=1THENRETURN
311 IFOB=1THEN313
312 IFL(I)=96+NX THEN NN=8:OB=1:H1$="....":SOUND160,10:GOTO320
313 IFOZ=1THEN315
314 IFL(I)=88+NX THEN NN=6:OZ=1:H2$="....":SOUND160,10:GOTO320
315 IFOY=1THEN317
316 IFL(I)=208+NX THENNN=3:OY=1:H3$="....":SOUND160,10:GOTO320
317 IFOQ=1THEN319
318 IFL(I)=AF-2+NX THEN NN=4:OQ=1:D$="....":SOUND200,10:GOTO320
319 GOTO322
320 VP=VP+NN:IFVP>24THEN324
321 RETURN
322 IFVP>24THEN324
323 GOTO342
324 CLS:PRINT@9,"BATTLE OVER
325 IFBX=1THENB=B+3:PRINT@44,"fl/gts"
326 PRINTVP"VP'S:","LOSSES:"
327 PRINTFV-1"RPL OBJ",FF"MEN
328 PRINTOB+OZ+OY"CITIES",LM"INF UNITS
329 PRINTOQ"AIRFIELD",HT"TANK UNITS
330 PRINT"ENEMY LOST:"EE"MEN "EU;UN$
331 PRINT"WITH:
332 PRINTRR;GT$
333 PRINTPP"PARA DROPS",SY"SUP UNITS"
334 PRINTAB"ART BARRAGES",AP"AIR STRIKES"
335 PRINTTR"REPLACEMENTS",ZM"REINF'MNTS"
336 IFB-VP<-6THENPRINT"decisive"VT$:GOTO341
337 IFB-VP<-4THENPRINT"conditional"VT$:GOTO341
338 IF B-VP<0THENPRINT"marginal"VT$:GOTO341
339 IFB-VP<3THENPRINT"draw":GOTO341
340 PRINT"battle lost
341 GOTO341
342 IFC(I)=1THEN350
343 IFC(I)=2THEN350
344 IFB$(I)=K$THEN350
345 B$(I)=G$
346 IFI=<H/3THENB$(I)=P$
347 IFI>H/3*2THENB$(I)=O$
348 IFI>H THENB$(I)=Q$
349 IFI>H+J THENB$(I)=S$
350 RETURN
351 FORE=1TO U:IFSX(E)=0THENIFRND(UX)=1THENSX(E)=5+RND(5):Q=LX(E):S=S+7:X$(E)="volks":GOSUB363:LX(E)=Q:IFRND(3)=1THENCX(E)=0
352 IFCX(E)>0THEN360
353 Q=LX(E)
354 IFB=<SGTHENIFRND(7)<4THENQ=Q+RND(3)+30
355 IFB>SGTHENIFRND(7)<6THENQ=Q-RND(3)-30
356 IFOB+OZ+OY+OQ+FV>2+RND(3)THENQ=Q-RND(3)-30
357 IFQ<70THENGOSUB363
358 IFQ>383THENQ=Q-30-RND(3)
359 LX(E)=Q
360 IFCX(E)=2THENCX(E)=0
361 NEXT
362 GOTO152
363 CX(E)=1:ONRND(4)GOTO364,365,366,367
364 Q=97+NX:RETURN
365 Q=89+NX:RETURN
366 Q=209+NX:RETURN
367 Q=AF-1+NX:RETURN
368 BD=BC-2+RND(3):RETURN
369 BD=BC+30+RND(3):RETURN
370 BD=BC-30-RND(3):RETURN
371 BE=SX(E):BF=BE-RND(6):IF BF=<0THENBF=0:EU=EU+1:LX(E)=0:GOSUB451
372 SX(E)=BF:BG=BE-BF:BH=BH+BG
373 RETURN
374 CLS:PRINT"NO UNIT"I:GOTO82
375 IFD(I)=1THENL(I)=L(I)+1:IFL(I)<LX(E)+3THENVV=1
376 IFD(I)=2THENL(I)=L(I)-1:IFL(I)<LX(E)THENVV=1
377 IFD(I)=3THENL(I)=L(I)-32:VV=2
378 IFD(I)=4THENL(I)=L(I)+32
379 RETURN
380 IFD(I)=1THENL(I)=L(I)-1
381 IFD(I)=2THENL(I)=L(I)+1
382 IFD(I)=3THENL(I)=L(I)+32
383 IFD(I)=4THENL(I)=L(I)-32
384 RETURN
385 IFB=1THEN390
386 FORI=1TOF1
387 IFL(I)<2THEN389
388 SP(I)=SP(I)-1:IFSP(I)<0THENSP(I)=0
389 NEXT
390 FORX=H+J+K+1TOH+J+K+4
391 B$(X)="4TH SUP
392 S(X)=1:SP(X)=4
393 FORI=1TOF1
394 IFL(I)=0THEN397
395 IFSP(I)=4THEN397
396 IFABS(L(X)-L(I))<8THENSP(I)=3
397 NEXT
398 NEXT
399 FORI=1TOF1
400 GOSUB342
401 IFL(I)<2THEN404
402 IFSP(I)=0THENB$(I)="O/S":S(I)=S(I)-2:FF=FF+2
403 IFS(I)=<0THENS(I)=0:B$(I)=K$:IFI>H THENHT=HT+1ELSELM=LM+1
404 NEXT
405 RETURN
406 CLS:PRINT"UNIT"I"O/S-NO MOVES":FORX=1TO1300:NEXT:GOTO81
407 IFI>H THEN55
408 IFFM=1THEN55
409 PRINT@22,"F/M  ";:X=S(I)
410 FM=1:M1(I)=M1(I)*2:S(I)=S(I)-(RND(5)-1)*RND(2):XX=S(I):FF=FF+X-XX:IFS(I)<0THENS(I)=0:LM=LM+1
411 GOTO55
412 IFPR=0THENCLS:PRINTNP$:GOTO165
413 PRINTPR"RES PTS LEFT
414 INPUT"unit for drop";I
415 IFI>F1+4 THEN414
416 IFI=0THEN168
417 IFL(I)<416THENCLS:PRINTUT$""I"NORTH OF RIVER-NO DROP":GOTO165
418 IFI>F1 THEN420
419 IFSP(I)<3ORS(I)<9THENCLS:PRINTUT$;I;"CANNOT BE DROPPED":GOTO165
420 PRINT"DROP ZONES--
421 PRINT"R/O'S---"
422 FORX=1TO5:PRINTV(X):NEXT
423 PRINT"CITIES--"
424 FORX=1TO3:PRINTH(X):NEXT
425 PRINT"AIRFIELD--
426 PRINTAF
427 INPUT"LOC";BC:IFBC=0THEN165
428 IFBC<32ORBC>480THENPRINTBC"";BL$:GOTO427
429 ONRND(3)GOSUB368,369,370
430 PP=PP+1:PR=PR-10:IFI>H THENPR=PR-5:IFI>H+J THENPR=PR-5:IFI>F1 THENPR=PR-5
431 IFPR=<0THENPR=0:GOTO412
432 IFI>F1 THEN434
433 L(I)=BD:XX=RND(3)*(RND(4)-1):S(I)=S(I)-XX:FF=FF+XX
434 PRINT"UNIT"I"LANDED AT LOC"BD
435 PRINTXX"SP'S LOST":XX=0
436 PRINT"points left"PR:GOTO165
437 CLS:GOSUB21
438 FORE=U TO1STEP-1
439 IFLX(E)<33ORSX(E)=0THEN444
440 LO=INT(LX(E)/32)
441 FORX=0TO LO-1
442 PRINT@LX(E)-(X*32),CHR$(190)+CHR$(190);
443 NEXT
444 NEXT
445 FORI=1TO42
446 IFL(I)<10THEN448
447 PRINT@L(I),CHR$(206);
448 NEXT
449 IN$=INKEY$:IFIN$=""THEN449
450 CLS:GOTO165
451 IFX$(E)="panzr"THENM=M-1
452 RETURN
