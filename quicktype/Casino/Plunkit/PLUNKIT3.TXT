0 DIMC1,CC,M$,MC:MC=16384:C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
15 FORCC=1TO50:I$=INKEY$:NEXT
16 B=B-1:PRINT@0,"SCORE"SC;:PRINT@13,"HIGH"HI;:PRINT@24,B"LEFT";
20 PRINT@P-33,"Žƒ";:PRINT@P,"@";:I$=INKEY$:IFI$=""THEN20
21 PRINT@P," ";:PRINT@P-33,"   ";
22 IFI$="A"ANDP>2*32+6THENP=P-2
23 IFI$="S"ANDP<2*32+24+2THENP=P+2
24 IFI$=" "THENPRINT@P," ";:PRINT@P-33,"Žƒ";:GOTO30
25 GOTO20
30 FORT=P+32+MCTO32*14+MCSTEP32
31 POKET,0:SOUND200,1:FORC1=1TO100:NEXT
32 ONRND(2)GOTO33,35
33 IFPEEK(T+33)=96THEN35
34 POKET,32:T=T+1:CC=-1:GOTO37
35 IFPEEK(T+31)=96THEN33
36 POKET,32:T=T-1:CC=1
37 NEXT:T=T-32+CC:POKET,0:C1=PEEK(T+32)-48:IFC1=-6THENC1=15:FORCC=1TO10:SOUND100+CC*10,1:NEXT
38 FORCC=1TO10:POKET,0:SOUND1,1:SOUND100,1:POKET,32:SOUND50,1:NEXT:SC=SC+C1:PRINT@0,"SCORE"SC;
39 IFB>0THEN15
50 FORCC=1TO15:SOUND200,1:NEXT
52 IFSC>HITHENHI=SC:FORT=1TO10:PRINT@13,"HIGH"HI;:SOUND1,1:SOUND100,1:PRINT@13,"            ";:SOUND50,1:NEXT
54 PRINT@13,"HIGH"HI;
56 PRINT@488,"PLAY AGAIN (Y/N)?";
60 I$=INKEY$:IFI$=""THEN60
62 IFI$="Y"THEN70
64 IFI$="N"THENEND
66 GOTO60
70 PRINT@P," ";:PRINT@P-33,"   ";:PRINT@0,"":PRINT@480,"                              ";:GOTO1070
100 CLS
110 PRINT"    PLUNKIT BY ROGER TAYLOR":GOSUB210
120 PRINT"  MC-10 VERSION BY JIM GERRIE":GOSUB210
130 PRINT:GOSUB210
140 PRINT"       FOR THE CHILDREN":GOSUB210:REM FOR NAOMI GERRIE, LOVE DADDY
150 PRINT"HERE'S A FUN GAME FOR THE MC-10":GOSUB210
160 PRINT"WHERE IT FALLS, NOBODY KNOWS...":GOSUB210
170 PRINT:GOSUB210
180 PRINT"    TRY FOR THE HIGH NUMBERS":GOSUB210
190 PRINT" a<-MOVE PLUNK->s   space=DROP":GOSUB210
200 GOSUB210:CLS:GOTO1000
210 FORC1=1TO2000:NEXT:RETURN
1000 FORT=3TO12STEP2
1010 PRINT@(T)*32+4,;:M$=". . . . . . . . . . . . .":GOSUB7
1020 PRINT@(T+1)*32+4,;:M$=" . . . . . . . . . . . . ":GOSUB7
1030 NEXT
1050 PRINT@T*32+4,;:M$=". . . . . . . . . . . . .":GOSUB7
1060 PRINT@(T+1)*32+4,;:M$=" 7 0 * 4 1 0 9 2 * 3 0 6 ":GOSUB7
1070 SC=0:P=2*32+13+4:B=10:GOTO15
