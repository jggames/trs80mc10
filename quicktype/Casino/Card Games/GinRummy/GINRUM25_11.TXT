0 CLS0:POKE49151,64:GOSUB3600:Q=RND(-(PEEK(9)*256+PEEK(10))):GOTO30
1 G=1:FORC4=QTOQ+160STEP32:PRINT@C4,LEFT$(K$(R,G),3);:G=G+1:NEXT:PRINT@Q,LEFT$(C$(PH(X)),1);:POKEQ+V,PEEK(Q+V)-64:RETURN
2 R=VAL(RIGHT$(C$(X),1)):PRINT@Q+192,M$;:G=1:FORC4=QTOQ+160STEP32:PRINT@C4,LEFT$(K$(R,G),W);:G=G+1:NEXT
3 PRINT@Q,LEFT$(C$(X),1);:POKEQ+V,PEEK(Q+V)-64:RETURN
5 PRINTRIGHT$("  "+STR$(Q),3);:RETURN
6 PRINT@32*Q,"                               ";:PRINT@Q*32,TAB((32-LEN(M$))*.5)M$;:RETURN
7 PRINT@279,53-K:PRINT@308,"CARDS LEFT":RETURN
8 X=PH(10):R=VAL(RIGHT$(C$(X),1)):Q=27:W=5:M$="10":GOSUB2:RETURN
9 CLS:PRINT@192," 1  2  3  4  5  6  7  8  9 10":FORX=1TO10:R=VAL(RIGHT$(C$(PH(X)),1)):Q=(X-1)*3:GOSUB1:NEXT:RETURN

30 PRINT@198," * GIN RUMMY 2.5 * ";:PRINT@230,"MANHATTAN SOFT 1980";:FORX=1TO4000:NEXTX:CLS0:POKE49151,16
35 CLS:CLEAR800:PRINT@204,"STARTING"
40 DIMK$(3,6),Q,G,C2,C3,C4,R,V,W,C(55),SC(57),CH(12),PH(12),I(55),R(13),V(58),V$(13),C$(57),M(57),DC(15):V=16384
50 GOSUB3700:GOSUB2180:WD=2
60 CK=0:PK=0:YT=0:FC=0:RL=0:T=0:G1=0:IFW$="YOUR"THENWD=1:GOTO70
61 WD=2
70 CLS:CL=1:Z=1:U=1:GOSUB350
80 CLS:M=11:N=0
90 GOSUB9:IFWD=2THENPH(11)=UC:GOTO140
110 IFWD=1THENGOSUB8:CH(11)=UC:Q=14:M$="ARRANGING MY HAND":GOSUB6:GOTO520
120 IFPK=1THENGOSUB8:GOTO940
130 IFYT=0THENGOSUB8:GOTO260
140 X=PH(11):R=VAL(RIGHT$(C$(X),1)):Q=30:W=2:M$="11":GOSUB2:Q=8:M$="0=DISCARD   99=KNOCK":GOSUB6

150 YT=1:WD=0:Q=13:M$="MOVE WHICH CARD":GOSUB6:INPUTM:IFM>11ANDM<>99THEN150
160 IFM>11ANDM<>99THEN150
170 IFM=99THENPK=1:FC=1:SOUND66,2:SOUND66,2:GOTO220
180 IFM=0THEN220
190 S=PH(M):Q=14:M$="TO WHICH LOCATION":GOSUB6:INPUTN:IFN<1ORN>11THEN190
200 IFN>MTHENFORX=MTON-1:PH(X)=PH(X+1):NEXTX:PH(N)=S:GOTO80
210 FORX=MTON+1STEP-1:PH(X)=PH(X-1):NEXTX:PH(N)=S:GOTO80
220 Q=14:M$="DISCARD WHICH CARD":GOSUB6:INPUTD:IFD<1ORD>11THEN220
240 IFPH(D)=UCANDZ<>1THEN245
241 GOTO250
245 Q=14:M$="YOU CAN'T DISCARD THE UPCARD":GOSUB6:Q=15:M$="YOU JUST PICKED UP":GOSUB6:FORX=1TO4000:NEXTX:GOTO80
250 T=T+1:DC(T)=PH(D):UC=PH(D):WD=0:YT=0:FORX=DTO10:PH(X)=PH(X+1):NEXTX:GOTO80
260 GOSUB7
270 X=UC:Q=236:W=6:M$="UPCARD":GOSUB2:WD=0:IFYT=0THEN440
280 Q=14:M$="tAKE UPCARD OR dRAW FROM DECK":GOSUB6:Q=15:M$="":GOSUB6
290 A$=INKEY$:IFA$=""THEN290
300 IFA$<>"T"ANDA$<>"D"THEN290
310 IFA$="T"THENPH(11)=UC:PU(U)=UC:U=U+1:GOTO80 
320 PH(11)=C(K):K=K+1:IFK=52THEN3520
321 GOTO80
330 A$=INKEY$:IFA$=""THEN330
340 IFA$="Y"ORA$="N"THENRETURN
341 GOTO330 
350 PRINT@204,"SHUFFLING":FORI=1TO55:I(I)=0:NEXTI:I(14)=1:I(28)=1:I(42)=1
360 FORI=1TO52
370 J=RND(55):IFI(J)<>0THEN370
380 C(I)=J:I(J)=1:NEXTI
390 IFWD=2THENPRINT@204," I DEAL":GOTO400
391 PRINT@204,"YOU DEAL"
400 FORX=1TO500:NEXTX
410 FORX=1TO10:PH(X)=C((X*2)-1):NEXTX
420 FORX=1TO10:CH(X)=C(X*2):NEXTX:UC=C(21):K=22:RETURN
430 DATAA,2,3,4,5,6,7,8,9,T,J,Q,K
440 Q=14:M$="CONSIDERING UPCARD":GOSUB6:IFZ=1THENCH(11)=57:GOSUB1030
450 GOTO780
460 FORA=1TO10:IFUC=CH(A)+1ORUC=CH(A)-1THEN480
470 NEXTA:GOTO920
480 FORB=1TO10
490 IFM(UC)=M(CH(B))THEN510
500 NEXTB:GOTO920
510 Q=14:M$="I TAKE THE UPCARD":GOSUB6:CH(11)=UC
520 Z=Z+1:GOSUB1030
530 FORX=1TO11:CH(X)=R(X):NEXTX
540 IFTS=0THENQ=14:M$="* 11-CARD GIN! *":GOSUB6:G1=1:GOTO970
550 IFTS<21THENGOSUB1130 
560 L=RND(3):IFL<>2ORZ>3THEN590
570 FORD=1TO11:IFV(CH(D))=0ANDM(CH(D))>2THENQ=D:D=11:NEXTD:D=Q:GOTO740
580 NEXTD
590 REM ERROR TRAP
600 REM CRAZY VARIABLE ROUTINE
610 RJ=0
620 FORD=0TO1:NEXTD:FORY=0TO1:NEXTY:FORY=13TO4STEP-1
630 FORD=11TO1STEP-1
640 IFV(CH(D))=0ANDM(CH(D))=YTHEN2350
650 NEXTD
660 NEXTY
670 FORY=13TO4STEP-1
680 FORD=11TO1STEP-1
690 IFV(CH(D))=1ANDM(CH(D))=YTHEN2350
700 NEXTD
710 NEXTY
720 RJ=RJ+1:SOUND1,1:IFRJ<2THEN620
740 UC=CH(D):TS=TS-SC(UC)
750 FORX=DTO11:CH(X)=CH(X+1):NEXTX
760 PRINT@32*9,"I DISCARD";
770 YT=1:GOTO270
780 FORA=1TO9
790 IFV(CH(A))=4THEN840
800 FORB=A+1TO10
810 IFV(CH(B))=4THEN830
820 IFM(UC)=M(CH(A))ANDM(UC)=M(CH(B))THEN510
830 NEXTB
840 NEXTA
850 FORA=1TO9
860 IFV(CH(A))=3ORV(CH(A+1))=3THEN890
870 IFCH(A)=CH(A+1)-1THENIFUC=CH(A)-1ORUC=CH(A)+2THEN510
880 IFCH(A)=CH(A+1)-2ANDUC=CH(A)+1THEN510
890 NEXTA
900 IFTS<21ANDSC(UC)<4THEN510
910 IFZ<4THEN460
920 Q=14:M$="I DRAW ":GOSUB6:CH(11)=C(K):K=K+1:IFK=52THEN3520
930 GOSUB7:GOTO520
940 Q=14:M$="KNOCK POINTS":GOSUB6:INPUTPP
960 IFPP>10THEN965
961 GOTO970
965 Q=14:M$="YOU CAN'T KNOCK WITH"+STR$(PP)+"-- RETRY":GOSUB6
966 FORX=1TO3000:NEXTX:PH(11)=UC:PK=0:YT=1:FC=0:GOTO80
970 Q=14:M$="PLEASE WAIT WHILE":GOSUB6:Q=15:M$="I CHECK HANDS...":GOSUB6:FC=1
971 FORX=0TO1:NEXTX:FORD=0TO1:NEXTD:FORY=0TO1:NEXTY:FORI=0TO1:NEXTI:FORJ=0TO1:NEXTJ
972 FORA=0TO1:NEXTA:FORB=0TO1:NEXTB:FORC=0TO1:NEXTC
980 AB=TS:FORX=1TO10:TH(X)=V(CH(X)):V(CH(X))=0:NEXTX
990 FORX=1TO10:R(X)=PH(X):NEXTX
1000 R(11)=57:GOSUB1040
1010 IFZ=1THENCH(11)=57:GOSUB1030:FORX=1TO10:CH(X)=R(X):NEXTX:AB=TS
1020 GOTO1210 
1030 FORX=1TO11:R(X)=CH(X):NEXTX
1040 FORI=1TO10
1050 FORJ=I+1TO11
1060 IFR(I)<R(J)THEN1080
1070 A=R(I):R(I)=R(J):R(J)=A
1080 NEXTJ
1090 NEXTI
1100 IFZ=1THENFORX=1TO10:CH(X)=R(X):NEXTX:RETURN
1110 GOTO2420 
1120 RETURN
1130 FORY=10TO1STEP-1
1140 FORD=1TO11
1150 IFSC(CH(D))=YANDV(CH(D))<3ANDTS-SC(CH(D))<11THENTS=TS-SC(CH(D)):GOTO1180
1160 NEXTD,Y
1170 RETURN
1180 IFTS=0THENPRINT@32*9,"    GIN!   ";:GOTO1200
1190 PRINT@32*9," I KNOCK   ";:PRINT@32*10," WITH"TS"  ";:GOSUB6:CK=1:FC=1:SOUND56,2:SOUND56,2
1200 FORX=DTO10:CH(X)=CH(X+1):NEXTX:GOTO970
1210 FORX=1TO10:V(CH(X))=TH(X):NEXTX

1220 GOSUB9:IFG1=.THENGOSUB8
1230 FORX=1TO10:IFV(PH(X))>1THENPOKEV+(X-1)*3+160,42
1231 NEXTX
1240 IFV(PH(11))>1ANDG1=1THENX=PH(11):R=VAL(RIGHT$(C$(X),1)):Q=30:W=2:M$="11":GOSUB2:X=11:POKEV+(X-1)*3+160,42
1260 PRINT@32*13,"UNMATCHED: YOURS"PS
1270 IFPK=1ANDPS>10THEN1275
1271 GOTO1280
1275 Z=1:Q=14:M$="YOU KNOCKED WITH MORE THAN 10":GOSUB6
1276 Q=15:M$="POINTS! PRESS enter":GOSUB6:INPUTA$:PK=0:YT=1:PH(11)=UC:FC=0:GOTO80
1280 FORP=1TO10:X=CH(P):R=VAL(RIGHT$(C$(X),1)):Q=(P-1)*3+224:W=3:M$="":GOSUB2
1281 NEXT:FORX=1TO10:IFV(CH(X))>1THENPOKEV+(X-1)*3+224+160,42
1282 NEXT
1290 TS=AB:PRINT@32*13+22,"MINE"TS
1300 IFG1=1THENP=11:X=CH(P):R=VAL(RIGHT$(C$(X),1)):Q=(P-1)*3+224:W=3:M$="":GOSUB2:POKEV+(P-1)*3+224+160,42
1310 IFPS=0ORTS=0THENGOSUB1430:GOTO1350
1320 IFCK=1THEN1700
1330 Q=14:M$="CHECKING LAYOFFS...":GOSUB6
1340 ONCLGOTO1480,1550,1590 
1350 IFPS>TSTHENNS=PS-TS:GOTO1360
1351 NS=TS-PS
1360 IFPK=1THENQ=14:M$="CHECK COMPLETE":GOSUB6
1370 IFPK=1ANDPS=>ABTHENFORX=1TO2500:NEXTX:Q=14:M$="I'VE UNDERCUT YOU -25 PTS":GOSUB6:NS=NS+25:W$="MY":GOTO1400
1380 IFCK=1ANDAB=>PSTHENFORX=1TO2500:NEXTX:Q=14:M$="YOU UNDERCUT ME -25 PTS":GOSUB6:NS=NS+25:W$="YOUR":GOTO1400
1390 IFPS>TSTHENW$="MY":GOTO1400
1391 W$="YOUR"
1400 Q=14:M$=W$+" NET SCORE:"+STR$(NS):GOSUB6:Q=15:M$="PRESS enter FOR SCORES":GOSUB6:INPUTA$:GOTO1920
1430 REM GIN
1440 IFPS=0THENG$="YOU":NS=TS+25:GOTO1460
1450 G$="ME":NS=PS+25
1460 Q=14:M$=G$+": +25 POINT GIN BONUS":GOSUB6:GOTO1390
1480 FORY=1TO10
1490 IFV(CH(Y))>1THEN1540
1500 FORA=1TO10
1510 IFV(PH(A))<4THEN1530
1520 IFCH(Y)+1=PH(A)ORCH(Y)-1=PH(A)THENV(CH(Y))=6:GOTO1670
1530 NEXTA
1540 NEXTY
1550 FORY=1TO10
1560 IFV(CH(Y))>1THEN1580
1570 IFCH(Y)=CH(Y+1)-1ANDV(CH(Y+1))=6ORCH(Y)=CH(Y-1)+1ANDV(CH(Y-1))=6THENV(CH(Y))=6:CL=2:GOTO1670 
1580 NEXTY
1590 FORX=1TO10
1600 IFV(PH(X))<>3THEN1650
1610 FORY=1TO10
1620 IFV(CH(Y))>1THEN1640
1630 IFM(CH(Y))=M(PH(X))THENV(CH(Y))=6:CL=3:GOTO1670
1640 NEXTY
1650 NEXTX
1660 CL=1:GOTO1350 
1670 REM PROGRAM LAYOFF
1680 AB=AB-SC(CH(Y)):Q=14:M$="I LAY OFF"+STR$(Y)+"-- NEW TOTAL:"+STR$(AB):GOSUB6
1690 GOTO1910
1700 REM PLAYER LAYOFF
1710 Q=14:M$="CLAIM A LAYOFF (Y/N)?":GOSUB6:GOSUB330 
1720 IFA$="N"THEN1350 
1730 Q=14:M$="NUMBER OF CARD":GOSUB6:INPUTQQ:IFQQ<1ORQQ>11THEN1730
1750 Q=14:M$="LAYOFF ON mELD OR rUN?":GOSUB6
1760 A$=INKEY$:IFA$=""THEN1760
1770 IFA$<>"M"ANDA$<>"R"THEN1760
1780 IFA$="R"THEN1840
1790 FORX=1TO10
1800 IFV(CH(X))<>3THEN1820
1810 IFM(PH(QQ))=M(CH(X))THENV(PH(QQ))=3:GOTO1900
1820 NEXTX
1830 Q=14:M$=STR$(QQ)" DOESN'T LAY OFF":GOSUB6:GOTO1910
1840 FORX=1TO10:IFPH(QQ)=PH(X)+1ORPH(QQ)=PH(X)-1THENIFV(PH(X))=7THENV(PH(QQ))=7:GOTO1900
1850 NEXTX
1860 FORX=1TO10
1870 IFV(CH(X))<4THEN1890
1880 IFPH(QQ)=CH(X)+1ORPH(QQ)=CH(X)-1THENV(PH(QQ))=7:GOTO1900
1890 NEXTX:GOTO1830
1900 PS=PS-SC(PH(QQ)):Q=14:M$="LAY OFF"STR$(QQ)+"-- NEW TOTAL"+STR$(PS):GOSUB6
1910 Q=15:M$="PRESS enter TO CONTINUE":GOSUB6:INPUTA$:GOTO1220
1920 REM ** SCORE SHEET **
1930 CLS1
1940 FORX=1TOU:PU(X)=0:NEXTX
1950 PRINTTAB(21)"SCORE SHEET";
1960 PRINT@53,"YOURS";:PRINT@59,"MINE";
1970 FORQ=2TO31:RESET(53,Q):NEXTQ
1980 PRINT@391,"BOXES.......";
1990 PRINT@423,"GAME........";
2000 PRINT@455,"SCHNEIDER...";
2010 PRINT@487,"TOTAL.......";
2020 PRINT@96,W$" SCORE FOR THIS";:PRINT@128,"HAND WAS"NS;:P1=86:P2=92
2030 IFW$="MY"THENF=F+1:CS(F)=CS(F-1)+NS:BF=BF+25:GOTO2050 
2040 E=E+1:HS(E)=HS(E-1)+NS:BE=BE+25
2050 IFE>0THENFORX=1TOE:PRINT@P1,;:Q=HS(X):GOSUB5:P1=P1+32:NEXTX
2060 IFF>0THENFORX=1TOF:PRINT@P2,;:Q=CS(X):GOSUB5:P2=P2+32:NEXTX
2070 IFHS(E)=>100THENGOSUB2100:GOTO2170
2080 IFCS(F)=>100THENGOSUB2120:GOTO2170
2090 GOTO2160
2100 PRINT@406,;:Q=BE:GOSUB5:PRINT@438,;:Q=100:GOSUB5:IFCS(F)=0THENSN=(HS(E)+BE+100):PRINT@470,;:Q=SN:GOSUB5
2110 HT=HS(E)+BE+100+SN:PRINT@502,;:Q=HT:GOSUB5:PRINT@412,;:Q=BF:GOSUB5:PRINT@508,;:Q=CS(F)+BF:GOSUB5
2115 NH=HT-CS(F)-BF:GOSUB2140:RETURN
2120 PRINT@412,;:Q=BF:GOSUB5:PRINT@444,;:Q=100:GOSUB5:IFHS(E)=0THENSN=(CS(F)+BF+100):PRINT@476,;:Q=SN:GOSUB5
2130 CT=CS(F)+BF+100+SN:PRINT@508,;:Q=CT:GOSUB5:PRINT@406,;:Q=BE:GOSUB5:PRINT@502,;:Q=HS(E)+BE:GOSUB5
2135 NC=CT-HS(E)-BE:GOSUB2140:RETURN
2140 PRINT@192,W$" SCORE FOR THIS";:PRINT@224,"GAME WAS";:IFNH>0THENPRINTSTR$(NH);:RETURN
2141 PRINTSTR$(NC);:RETURN
2160 PRINT@288,"PRESS enter FOR";:PRINT@320,"ANOTHER HAND";:INPUTA$:IFA$<>"C"THEN60
2161 PRINT@0,;INPUT"YOUR SCORE";HS(E):PRINT@0;:INPUT"COMPUTER SCORE";CS(F):GOTO60
2170 PRINT@288,"PRESS enter FOR";:PRINT@320,"ANOTHER GAME";:INPUTA$:GOTO30
2180 FORX=1TO55:C(X)=X:NEXTX
2190 FORX=1TO13:M(C(X))=X:M(C(X+14))=X:M(C(X+28))=X:M(C(X+42))=X:NEXTX
2200 FORX=1TO10:SC(C(X))=X:NEXTX
2210 FORX=15TO24:SC(C(X))=X-14:NEXTX
2220 FORX=29TO38:SC(C(X))=X-28:NEXTX
2230 FORX=43TO52:SC(C(X))=X-42:NEXTX
2240 FORX=11TO13:SC(C(X))=10:SC(C(X+14))=10:SC(C(X+28))=10:SC(C(X+42))=10:NEXTX
2250 RESTORE:FORI=1TO13:READV$(I):NEXTI
2260 S1$="∞2"
2270 S2$="ø1"
2280 S3$="ø0"
2290 S4$="∞3"
2300 FORX=1TO13:C$(X)=V$(X)+S1$:NEXTX
2310 FORX=15TO27:C$(X)=V$(X-14)+S2$:NEXTX
2320 FORX=29TO41:C$(X)=V$(X-28)+S3$:NEXTX
2330 FORX=43TO55:C$(X)=V$(X-42)+S4$:NEXTX
2340 RETURN
2350 IFRJ>1THEN740
2360 FORX=1TOU
2370 IFM(CH(D))=M(PU(X))THENQ=X:X=U:NEXTX:X=Q:GOTO2390
2380 NEXTX:GOTO740
2390 FORX=1TOT:IFM(DC(X))=M(CH(D))THENQ=X:X=T:NEXTX:X=Q:GOTO740
2400 NEXTX
2410 NEXTD:NEXTY:RJ=RJ+1:SOUND1,1:GOTO620
2420 FORX=1TO11:V(R(X))=0:NEXTX
2430 RV=0:MV=0:LM=0:LR=0
2440 FORA=1TO9
2450 IFR(A)=R(A+1)-1ANDR(A)=R(A+2)-2THENV(R(A))=4:V(R(A+1))=5:V(R(A+2))=6:RV=RV+1
2460 NEXTA
2470 IFFC=0THENQ=15:M$="I'M LOOKING OVER MY HAND":GOSUB6
2480 FORA=1TO8
2490 IFR(A)=R(A+3)-3THENV(R(A))=5:V(R(A+1))=4:V(R(A+2))=5:V(R(A+3))=6:RV=RV-1:LR=1
2500 IFR(A)=R(A+4)-4THENV(R(A+1))=5:V(R(A+2))=4:V(R(A+3))=5:V(R(A+4))=6
2510 IFR(A)=R(A+5)-5THENV(R(A+3))=4:V(R(A+4))=5:V(R(A+5))=6
2520 NEXTA
2530 FORA=1TO9
2540 IFV(R(A))=4THEN2620
2550 FORB=A+1TO10
2560 IFV(R(B))=4THEN2610
2570 FORC=B+1TO11
2580 IFV(R(C))=4THEN2600
2590 IFM(R(A))=M(R(B))ANDM(R(A))=M(R(C))THEN2595
2591 GOTO2600
2595 IFV(R(A))=3THENV(R(C))=3:LM=LM+1:GOTO2600
2596 V(R(A))=3:V(R(B))=3:V(R(C))=3:MV=MV+1
2600 NEXTC
2610 NEXTB
2620 NEXTA
2630 IFMV=1ANDRV=2ANDLM>0THEN3260
2640 IFMV=1ANDRV=2THEN2950
2650 IFMV=2ANDRV=1ANDLM>0THEN3530
2660 IFMV=2ANDRV=2THEN3150
2670 FORX=1TO11:IFV(R(X))>3THENV(R(X))=0
2680 NEXTX
2690 IFMV=2ANDRV=1ANDLM=0THEN3240
2700 IFFC=1THENQ=14:M$="HERE ARE THE HANDS":GOSUB6
2710 IFRV=3THEN3490
2720 IFMV=1ANDRV=1THEN3240
2730 IFMV=2ANDRV=1THEN3050
2740 IFMV=0ANDRV=2THEN3240
2750 FORX=1TO9:IFV(R(X))=3ORV(R(X+1))=3ORV(R(X+2))=3THEN2770 
2760 IFR(X)=R(X+1)-1ANDR(X)=R(X+2)-2THENV(R(X))=4:V(R(X+1))=5:V(R(X+2))=5
2770 NEXTX
2780 IFFC=0THENQ=15:M$="JUST A SECOND...":GOSUB6
2790 IFFC=1THEN2910
2800 FORA=1TO10
2810 IFV(R(A))>1ORV(R(A+1))>1THEN2830
2820 IFR(A)=R(A+1)-1THENV(R(A))=1:V(R(A+1))=1
2830 NEXTA
2840 FORA=1TO10
2850 IFV(R(A))>1THEN2900
2860 FORB=A+1TO11
2870 IFV(R(B))>1THEN2890
2880 IFM(R(A))=M(R(B))THENV(R(A))=1:V(R(B))=1
2890 NEXTB
2900 NEXTA
2910 TS=0
2920 FORX=1TO11:IFV(R(X))>1THEN2940
2930 TS=TS+SC(R(X))
2940 NEXTX:PS=TS:GOTO1120
2950 REMMV1/RV2
2960 IFLM>0THEN3380
2970 FORX=2TO11:IFV(R(X))=3ANDV(R(X)-1)>3THEN2990
2980 NEXTX:GOTO3030
2990 FORY=X+1TO11:IFV(R(Y))=3ANDV(R(Y)-1)>3THEN3010
3000 NEXTY:GOTO3030
3010 FORX=1TO11:V(R(X))=0:NEXTX
3020 GOTO2750
3030 FORX=1TO11:IFV(R(X))>3THENV(R(X))=0
3040 NEXTX:GOTO2750
3050 REM ** SPECIAL MV2/RV1 CHECKS **
3060 FORX=1TO8
3070 IFV(R(X))=3ANDV(R(X)+3)=3THEN3100
3080 NEXTX
3090 GOTO2750
3100 IFR(X)=R(X+1)-1ANDR(X)=R(X+2)-2THEN3110
3101 GOTO3080
3110 FORY=1TO11
3120 IFM(R(Y))=M(R(X))THENV(R(Y))=0
3130 NEXTY
3140 GOTO2750
3150 REM MV2/RV2
3160 IFLM>0THEN3420
3170 FORX=2TO11
3180 IFV(R(X))=3ANDV(R(X)+1)=3THEN3230
3190 NEXTX
3200 FORX=3TO11
3210 IFV(R(X))=3ANDR(X)=R(X-1)+1THEN3110
3220 NEXTX
3230 IFLR=0THEN2670
3231 GOTO3010

3240 REM HIDDEN MELD
3250 IFLM=3THEN3380
3260 FORA=1TO9
3270 IFR(A)=R(A+1)-1ANDR(A)=R(A+2)-2THENIFV(R(A+1))=3ORV(R(A+2))=3THEN3290
3280 NEXTA:GOTO2750
3290 FORB=1TO11
3300 IFM(R(B))=M(R(A))ANDR(B)<>R(A)THEN3320
3310 NEXTB:GOTO2750
3320 FORC=1TO11
3330 IFM(R(C))=M(R(B))ANDR(C)<>R(B)ANDR(C)<>R(A)THENV(R(C))=3:V(R(B))=3:V(R(A))=3:MV=2:IFRV<>3THEN3350
3340 NEXTC:GOTO2750
3350 IFLM>0THEN3360
3351 GOTO2720
3360 FORX=(C+1)TO11:IFM(R(X))=M(R(C))THENV(R(X))=3:GOTO2670
3370 NEXTX:GOTO2670
3380 REM LM IN MV1/RV2 & MV1/RV1
3390 FORX=2TO11
3400 IFV(R(X))=3ANDR(X)=R(X-1)+1AND(R(X)=R(X-2)+2ORR(X)=R(X+1)-1)THENV(R(X))=0:GOTO2750
3410 NEXTX:GOTO2750
3420 REM 2 LM IN MV2/RV2
3430 FORX=3TO11
3440 IFV(R(X))=0ANDR(X)=R(X-1)+1THENV(R(X-1))=0:GOTO2750
3450 NEXTX
3460 FORX=3TO11
3470 IFV(R(X))=3ANDR(X)=R(X-1)+1ANDR(X)=R(X-2)+2THENV(R(X))=0:V(R(X-1))=0:GOTO2750
3480 NEXTX:GOTO2750
3490 REM RV3
3500 IFMV=2ANDLM>0THEN3260
3510 GOTO3010
3520 PRINT@78,"ONLY 2 CARDS LEFT";:PRINT@110,"-- HAND IS A DRAW";:FORX=1TOU:PU(X)=0:NEXTX:FORX=1TO4000:NEXTX:GOTO60
3530 FORX=1TO9:IFV(R(X))=3ANDV(R(X+1))>3ANDV(R(X+2))>3THENV(R(X))=0:GOTO2670
3540 NEXTX:GOTO2670

3700 K$(0,1)=" ŒÕŒÕœ"
3701 K$(0,2)="Œ∑ª∑ªÕ"
3702 K$(0,3)=" øøøø≈"
3703 K$(0,4)="ÀΩøøæ«"
3704 K$(0,5)="œÀΩæ«œ"
3705 K$(0,6)="œœÀ«œœ"

3710 K$(1,1)=" œŒÕœœ"
3711 K$(1,2)="œŒ∑ªÕœ"
3712 K$(1,3)="Œ∑øøªÕ"
3713 K$(1,4)="ÀΩøøæ«"
3714 K$(1,5)="œÀΩæ«œ"
3715 K$(1,6)="œœÀ«œœ"

3720 K$(2,1)=" ŒÃÃÕœ"
3721 K$(2,2)="œ ¿¿≈œ"
3722 K$(2,3)="ŒÃ ≈ÃÕ"
3723 K$(2,4)=" ¿¿¿Ä≈"
3724 K$(2,5)="À√ ≈√«"
3725 K$(2,6)="œÀ√√«œ"

3730 K$(3,1)=" œŒÕœœ"
3731 K$(3,2)="œŒ¿¿Õœ"
3732 K$(3,3)="Œ¿¿¿¿Õ"
3733 K$(3,4)="À¿¿¿¿«"
3734 K$(3,5)="œÀ¬¡«œ"
3735 K$(3,6)="œÀ√√«œ":RETURN

3600 FORY=4TO8STEP2:FORX=4TO24STEP8:Q=32*Y+X:PRINT@Q,;:PRINT"ª∫óíØ™◊“";:PRINT@Q+32,"¥ÄîÄ§Ä‘Ä";:NEXTX,Y:RETURN
4000 REM COPYRIGHT 1980
4010 REM MANHATTAN SOFTWARE INC
4020 REM PO BOX 5200 GRAND CENT
4030 REM STA. NYC NY 10017
4050 REM MC-10 EDITS JIM GERRIE
4060 REM GREG DIONNE 2023
4070 REM ENTER C AT 
4080 REM 'ANOTHER HAND' PROMPT
4080 REM TO CORRECT SCORES
