0 CLS:CLEAR500:DIMC,Z,C1,C2,C3,M$,M:M=16384:GOTO50
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 C3=C1-(C2*INT(C1/C2)):RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-M+C2,:RETURN

8 REM FUNCTION VALDEPL (B)
9 NB=.:FORC=1TO4:IFF(C,1)=.THENNB=NB+1
10 NEXT:NJ=.:FORC=1TO8:IFJ(1,C,1)=.ANDC<>BTHENNJ=NJ+1
11 NEXT:PO=.:FORC=NJTO0STEP-1:PO=PO+C+NB:NEXT:PO=PO+1:RETURN

50 PRINT" freecell FOR MICRO COLOR BASIC"
60 PRINT"     BY CHRISTIAN HOFFMANN"
70 PRINT"   MC-10 EDITS BY JIM GERRIE":PRINT
99 REM THE FOLLOWING ARE ALL SHARED VARIABLES
100 DIMC$(13,4),OK$(12):REM CARD IMAGES
110 DIMJ(18,8,2):REM GAME POSITIONS
120 DIMF(4,2):REM 4 SUPPLEMENTARY
130 DIMT(4):REM 4 REPLACEMENTS
140 DIMA,B:REM START,DESTINATION VARS
150 PRINT"DO YOU NEED INSTRUCTIONS? (Y/N)":INPUTM$
160 IFM$="Y"ORM$="YES"THENGOSUB2000

200 CLS:M$="10  11  12  13  ":GOSUB7:PRINT@32*2,;:M$="1   2   3   4   5   6   7   8   ":GOSUB7:POKE16431,32
210 GOSUB7000:REM INIT
220 GOSUB6000:REM DEAL
230 GOSUB1000:REM DISPLAY CARDS
250 GOSUB5000:REM DEMANDE
260 GOSUB3000:REM CALCUL
270 GOSUB1000:REM REDRAW SCREEN
280 GOSUB4000:REM CALCULTAS
290 IFNOT(T(1)+T(2)+T(3)+T(4)=52)THEN250
300 PRINT@16,"* * * WIN * * *";:SOUND50,2:SOUND100,2:SOUND150,2
310 PRINT@32,"TRY AGAIN y/n? ";
320 M$=INKEY$:IFM$=""THEN320
330 IFM$="Y"THENRUN
340 IFM$="N"THENEND
350 GOTO320

999 REM SUB AFFICHE
1000 PRINT@16,"":PRINT@480,"                               ";
1020 FORC=1TO12:FORZ=1TO8:PRINT@32*(C+2)+(Z-1)*4,C$(J(C,Z,1),J(C,Z,2)):NEXT:NEXT
1030 FORC=13TO18:FORZ=1TO8:PRINT@480+(Z-1)*4,C$(J(C,Z,1),J(C,Z,2));:NEXT:NEXT:PRINT@32,"":POKE16431,32
1200 FORC=1TO4:PRINT@32+(C-1)*4,C$(F(C,1),F(C,2));:PRINT@32+(C-1)*4+16,C$(T(C),C):NEXT:RETURN

1999 REM SUB HELP
2000 CLS:PRINT"freecell help":PRINT
2020 M$="THE OBJECT OF THIS GAME IS TO PLACE THE CARDS BY SUIT IN THEIR HOLDER AT THE TOP RIGHT.":GOSUB1
2030 M$="TO DO THIS, YOU MUST FORM COLUMNS WHERE THE CONSECUTIVE CARDS FOLLOW EACH OTHER.":GOSUB1
2040 M$="THE COLUMNS MUST BE ARRANGED IN DESCENDING ORDER WITH ALTERNATE RED/ORANGE AND BLUE/CYAN SUITS.":GOSUB1:GOSUB2200
2050 CLS:PRINT"freecell help":PRINT:M$="THE BOXES AT THE TOP LEFT ARE A RESERVE OF SPACES.":GOSUB1
2060 M$="TO MOVE A CARD FROM PILE 1 TO PILE 3, ENTER 1.3":GOSUB1
2061 M$="ENTER R TO RESTART GAME OR Q TO QUIT.":GOSUB1
2070 M$="AVAILABLE CARDS GO UP AUTOMATICALLY INTO THEIR HOLDER.":GOSUB1
2080 GOSUB2200:CLS:GOTO2998
2200 PRINT@448,;:M$="PRESS A KEY":GOSUB7
2210 M$=INKEY$:IFM$=""THEN2210
2998 RETURN

2999 REM SUB CALCUL
3000 OK=.:IFB<9THEN3500
3200 IFF(B-9,1)THEN3990
3300 IFA<9THEN3400
3310 IFF(A-9,1)>.THENOK=1:F(B-9,1)=F(A-9,1):F(B-9,2)=F(A-9,2):F(A-9,1)=.:F(A-9,2)=.:GOTO3990
3400 I=.
3410 I=I+1:IFJ(I,A,1)THEN3410
3420 OK=1:F(B-9,1)=J(I-1,A,1):F(B-9,2)=J(I-1,A,2):J(I-1,A,1)=.:J(I-1,A,2)=.:GOTO3990
3500 IFA<9THEN3600
3510 K0=.:IFF(A-9,1)>.THEN3530
3520 GOTO3990
3530 N1=F(A-9,1):S1=F(A-9,2):I=19
3540 I=I-1:IFJ(I,B,1)>.ORI=1THEN3550
3541 GOTO3540
3550 C1=S1:C2=2:GOSUB3:C=C3:C1=J(I,B,2):GOSUB3:Z=C3:IFJ(I,B,1)=N1+1ANDABS(C-Z)=1THENK0=1
3560 IFJ(I,B,1)=.ORK0=1THEN3570
3561 GOTO3990
3570 OK=1:IFJ(I,B,1)=.THENI=.
3580 J(I+1,B,1)=F(A-9,1):J(I+1,B,2)=F(A-9,2):F(A-9,1)=.:F(A-9,2)=.:GOTO3990

3600 IFJ(1,B,1)=.THEN3610
3601 GOTO3800
3610 I=19:K0=1:I0=.:I2=.
3620 I=I-1:IFI0>.THEN3630
3621 GOTO3640
3630 C1=J(I+1,A,2)-J(I,A,2):C2=2:GOSUB3:IFJ(I,A,1)=J(I+1,A,1)+1ANDABS(C3)=1THENK0=1:GOTO3640
3631 K0=.
3640 IFI0=.ANDJ(I,A,1)>.THENI0=I
3650 IFI=1ORJ(I,A,1)=N2-1ORK0=.THEN3670
3660 GOTO3620
3670 IFK0=.THENI1=I+1:GOTO3680
3671 I1=I
3680 N1=J(I1,A,1):S1=J(I1,A,2):GOSUB9:IFI0-I1+1<=POTHENPO=I0-I1+1
3690 FORI=1TOPO:OK=1:J(I2+I,B,1)=J(I0-PO+I,A,1):J(I2+I,B,2)=J(I0-PO+I,A,2)
3700 J(I0-PO+I,A,1)=.:J(I0-PO+I,A,2)=.:NEXT:GOTO3990

3800 I=19
3810 I=I-1:IFJ(I,B,1)<=.THEN3810
3820 I2=I:N2=J(I2,B,1):S2=J(I2,B,2):I=19:I0=.:K0=1
3830 I=I-1:IFI0>.THEN3840
3831 GOTO3850
3840 C1=J(I+1,A,2)-J(I,A,2):C2=2:GOSUB3:IFJ(I,A,1)=J(I+1,A,1)+1ANDABS(C3)=1THENK0=1:GOTO3850
3841 K0=.
3850 IFI0=.THENIFJ(I,A,1)>.THENI0=I
3860 IFI=1ORJ(I,A,1)=N2-1ORK0=.THEN3870
3861 GOTO3830
3870 C1=S2:C2=2:GOSUB3:C=C3:C1=J(I,A,2):GOSUB3:Z=C3:IFJ(I,A,1)=N2-1ANDABS(C-Z)=1THENK0=K0+1
3880 IFK0=2THEN3890
3881 GOTO3990
3890 I1=I:N1=J(I1,A,1):S1=J(I1,A,2):IFI0-I1+1>1THEN3900
3891 O0=1:GOTO3920
3900 GOSUB9:IFPO>=I0-I1+1THENO0=1
3920 IFO0<>1THEN3990
3930 FORI=1TOI0-I1+1:OK=1:J(I2+I,B,1)=J(I1+I-1,A,1):J(I2+I,B,2)=J(I1+I-1,A,2)
3940 J(I1+I-1,A,1)=.:J(I1+I-1,A,2)=.:NEXT
3990 IFOK=.THENPRINT@16,"":PRINT@16,"ERROR":SOUND50,10
3998 RETURN

3999 REM SUB CALCULTAS
4000 OK=.:FORI=1TO8:IFJ(1,I,1)<=.THEN4190
4030 J=19
4040 J=J-1:IFJ(J,I,1)<=.THEN4040
4080 FORK=1TO4:NU=T(K):IFJ(J,I,1)=NU+1THENIFJ(J,I,2)=KTHEN4110
4101 GOTO4170
4110 OK=1:T(K)=NU+1:J(J,I,1)=.:J(J,I,2)=.:GOSUB1000
4170 NEXT
4190 NEXT
4200 FORI=1TO4:FORK=1TO4:NU=T(K):IFF(I,1)=NU+1THENIFF(I,2)=KTHEN4440
4431 GOTO4490
4440 OK=1:T(K)=NU+1:F(I,1)=.:F(I,2)=.:GOSUB1000
4490 NEXT:NEXT
4990 IFOKTHEN4000
4998 RETURN

4999 REM INPUT
5000 OK=.:REM CHECK FOR 2 NUMBERS
5060 PRINT@16,"":PRINT@16,;:INPUTDE$:IFDE$="R"THENRUN
5061 IFDE$="Q"THENEND
5070 X=.:Z=LEN(DE$):FORC=1TOZ:IFMID$(DE$,C,1)="."THENX=C
5071 NEXT
5080 IFX=.THEN5500
5090 A$=LEFT$(DE$,X-1):B$=RIGHT$(DE$,LEN(DE$)-X)
5110 FORC=1TO12:IFA$=OK$(C)THENOK=OK+1
5130 IFB$=OK$(C)THENOK=OK+1
5140 NEXT:IFA$=B$THENOK=1
5500 IFOK<2ANDX>.THENPRINT@16,"":PRINT@16,"ERROR":SOUND1,10
5510 IFOK<>2THEN5000
5520 A=VAL(A$):B=VAL(B$):RETURN

5999 REM DEAL
6000 PRINT@16,"SHUFFLING...":FORI=5TO8
6010 J(7,I,1)=1:J(7,I,2)=1
6020 NEXTI
6030 I=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE TIMER
6040 FORC=1TO13:FORZ=1TO4
6060 X=RND(8):Y=RND(7):IFJ(Y,X,1)THEN6060
6090 J(Y,X,1)=C:J(Y,X,2)=Z
6110 NEXT:NEXT
6120 FORI=5TO8:J(7,I,1)=.:J(7,I,2)=.:NEXT:RETURN

6999 REM SUB INIT
7000 PRINT@16,"INITIALIZING...":FORC=1TO13:REM NUM DE LA CARTE
7010 FORZ=1TO4:REM SYMBOLE DE LA CARTE
7020 IFC>=1ORC<=9THENA$=MID$(STR$(C),2,1)
7030 IFC=10THENA$="T"
7040 IFC=11THENA$="J"
7050 IFC=12THENA$="Q"
7060 IFC=13THENA$="K"
7065 IFC=1THENA$="A"
7070 IFZ=1THENB$="÷ò"
7080 IFZ=2THENB$="×Ò"
7090 IFZ=3THENB$="¹¸"
7100 IFZ=4THENB$="­¨"
7110 C$(C,Z)=A$+B$
7120 NEXT:NEXT
7130 FORC=1TO18
7140 FORZ=1TO8
7150 J(C,Z,1)=.:J(C,Z,2)=.
7170 NEXT:NEXT
7200 FORC=1TO12
7210 IFC>8THENZ=C+1:GOTO7230
7220 Z=C
7230 OK$(C)=MID$(STR$(Z),2)
7240 NEXT:RETURN
