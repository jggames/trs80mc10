
'********************************************************
'********* Programme crÇÇ par Christian Hoffmann ********
'********* E-Mail: jemaf@free.fr                 ********
'********* tÇlÇchargÇ sur le site jemaf.free.fr  ********
'********************************************************

'FREECELL FOR QBASIC
'Comme sous Windows, mais ‡ la place de la souris, tapez 7.5 par exemple.

'Pour jouer, taper 7.5 par exemple pour dÈplacer les cartes de la colonne 7 vers 5

'Pour afficher l'aide, taper F1 et valider

'Taper Ctrl+Pause pour quitter

DECLARE SUB Fond ()
DECLARE SUB aide ()
DECLARE SUB CalculTas ()
DECLARE SUB Calcul ()
DECLARE SUB Demande ()
DECLARE SUB Affiche ()
DECLARE SUB Done ()
DECLARE SUB Init ()
DECLARE FUNCTION ValDepl! (Dem2!)
DECLARE FUNCTION Coul! (sign!)
SCREEN 7, 1, 0: COLOR 0, 15: CLS

KEY(1) ON: ON KEY(1) GOSUB help

'Carreau=1
'Picque=2
'Coeur=3
'Träfle=4

DIM SHARED carte$(13, 4)       'Nom affichÇ des cartes
DIM SHARED Jeu(18, 8, 2)       'Contenu des positions du jeu
DIM SHARED Libre(4, 2)         'Contenu des 4 cases supplÇmentaires
DIM SHARED tas(4)              'Contenu des 4 tas Ö remplir
DIM SHARED Dem1, Dem2          'Colonne de dÇpart, colonne de desination


Fond                           'Affichage du dÇcor
Init                           'Initialisation du nom des cartes
Done                           'Distribution des cartes
Affiche                        'Affichage des cartes

DO
        Demande                'VÇrification de la saisie du joueur
        Calcul                 'Calcul du dÇplacement
        Affiche                'Rafraichissement de l'affichage
        CalculTas              'Envoi automatique vers le sabot

LOOP UNTIL tas(1) + tas(2) + tas(3) + tas(4) = 52

COLOR 12: LOCATE 23, 1: PRINT " * * * GAGNE * * * GAGNE * * * GAGNE * *";
SLEEP
COLOR 8
END

help:
aide                           'Affichage de l'Çcran d'aide
RETURN

SUB Affiche

FOR i = 1 TO 18
        FOR j = 1 TO 8
                a$ = ""
                COLOR Coul(Jeu(i, j, 2)):
                IF i > 11 THEN
                        LOCATE 22, j * 5 - 3
                        IF Jeu(i, j, 1) > 0 THEN
                                PRINT "+" + LTRIM$(carte$(Jeu(i, j, 1), Jeu(i, j, 2)))
                        END IF
                ELSE
                        LOCATE 11 + i, j * 5 - 3
                        a$ = carte$(Jeu(i, j, 1), Jeu(i, j, 2))
                        IF a$ = "" THEN a$ = "   "
                        PRINT a$;
                END IF
        NEXT j
NEXT i

FOR i = 1 TO 4
        COLOR Coul(Libre(i, 2)):
        LOCATE 6, i * 4 - 1
        a$ = carte$(Libre(i, 1), Libre(i, 2))
        IF a$ = "" THEN a$ = "   "
        PRINT a$;
NEXT i

FOR i = 1 TO 4
        COLOR Coul(i):
        LOCATE 6, i * 4 + 20
        a$ = carte$(tas(i), i)
        IF a$ = "" THEN a$ = "   "
        PRINT a$;
NEXT i

FOR i = 1 TO 20000: NEXT i

END SUB

SUB aide

SCREEN 7, 1, 1: COLOR 15, 0
CLS

LOCATE 2, 12: COLOR 13: PRINT "Aide au Freecell"
LOCATE 4, 5: COLOR 15: PRINT "Le but de ce jeu est de placer"
LOCATE 5, 5: PRINT "les cartes par couleur dans leur"
LOCATE 6, 5: PRINT "sabot en-haut Ö droite."
LOCATE 8, 5: PRINT "Pour cela, vous devez constituer"
LOCATE 9, 5: PRINT "des colonnes oó les cartes"
LOCATE 10, 5: PRINT "consÇcutives se suivent de maniäre"
LOCATE 11, 5: PRINT "dÇcroissante et en alternant"
LOCATE 12, 5: PRINT "le rouge et le noir."
LOCATE 14, 5: PRINT "Les cases en haut Ö gauche"
LOCATE 15, 5: PRINT "sont une rÇserve de places"
LOCATE 16, 5: PRINT "disponibles. "; ""
LOCATE 18, 5: COLOR 9: PRINT "Pour dÇplacer une carte"
LOCATE 19, 5: PRINT "du tas 1 vers le tas 3, tapez"
LOCATE 19, 35: COLOR 13: PRINT "1.3"
LOCATE 21, 5: COLOR 15: PRINT "Les cartes disponibles montent"
LOCATE 22, 5: PRINT "automatiquement dans leur sabot."

SLEEP

SCREEN 7, , 0: COLOR 0, 15

END SUB

SUB Calcul

OK = 0                          'Validation ou erreur de la demande

IF Dem2 > 8 THEN
        
        IF Libre(Dem2 - 9, 1) = 0 THEN
              
                IF Dem1 > 8 THEN
                       
                        '*** depuis une case vide vers une case vide ********

                        IF Libre(Dem1 - 9, 1) > 0 THEN
                                OK = 1
                                Libre(Dem2 - 9, 1) = Libre(Dem1 - 9, 1)
                                Libre(Dem2 - 9, 2) = Libre(Dem1 - 9, 2)
                                Libre(Dem1 - 9, 1) = 0
                                Libre(Dem1 - 9, 2) = 0
                        END IF
                ELSE
                        
                        '*** depuis une colonne vers une case vide ********
                       
                        i = 0
                       
                        DO
                                i = i + 1
                        LOOP UNTIL Jeu(i, Dem1, 1) = 0
                       
                        OK = 1
                        Libre(Dem2 - 9, 1) = Jeu(i - 1, Dem1, 1)
                        Libre(Dem2 - 9, 2) = Jeu(i - 1, Dem1, 2)
                        Jeu(i - 1, Dem1, 1) = 0
                        Jeu(i - 1, Dem1, 2) = 0

                END IF
        END IF

ELSE

        IF Dem1 > 8 THEN

                '*** depuis une case vide vers une colonne ********

                OK0 = 0
       
                IF Libre(Dem1 - 9, 1) > 0 THEN
                       
                        num1 = Libre(Dem1 - 9, 1)
                        sign1 = Libre(Dem1 - 9, 2)

                        i = 19
                       
                        DO
                                i = i - 1
                        LOOP UNTIL Jeu(i, Dem2, 1) > 0 OR i = 1
                       
                        IF Jeu(i, Dem2, 1) = num1 + 1 AND ABS(sign1 MOD 2 - Jeu(i, Dem2, 2) MOD 2) = 1 THEN OK0 = 1
               
                        IF Jeu(i, Dem2, 1) = 0 OR OK0 = 1 THEN
                                OK = 1
                                IF Jeu(i, Dem2, 1) = 0 THEN i = 0
                                Jeu(i + 1, Dem2, 1) = Libre(Dem1 - 9, 1)
                                Jeu(i + 1, Dem2, 2) = Libre(Dem1 - 9, 2)
                                Libre(Dem1 - 9, 1) = 0
                                Libre(Dem1 - 9, 2) = 0
                        END IF
                END IF

        ELSE
       
                IF Jeu(1, Dem2, 1) = 0 THEN
               
                '*** depuis une colonne vers une colonne vide ********

                        i = 19
                        OK0 = 1
                        i10 = 0
                       
                        DO
                                i = i - 1
                               
                                IF i10 > 0 THEN
                               
                                        IF Jeu(i, Dem1, 1) = Jeu(i + 1, Dem1, 1) + 1 AND ABS((Jeu(i + 1, Dem1, 2) - Jeu(i, Dem1, 2)) MOD 2) = 1 THEN OK0 = 1 ELSE OK0 = 0
                                END IF
                       
                                IF i10 = 0 AND Jeu(i, Dem1, 1) > 0 THEN i10 = i
                       
                        LOOP UNTIL i = 1 OR Jeu(i, Dem1, 1) = num2 - 1 OR OK0 = 0

                        IF OK0 = 0 THEN i1 = i + 1 ELSE i1 = i
                       
                        num1 = Jeu(i1, Dem1, 1)
                        sign1 = Jeu(i1, Dem1, 2)

                        poss = ValDepl(Dem2)
               
                        IF i10 - i1 + 1 <= poss THEN poss = i10 - i1 + 1

                        FOR i = 1 TO poss
                                OK = 1
                                Jeu(i2 + i, Dem2, 1) = Jeu(i10 - poss + i, Dem1, 1)
                                Jeu(i2 + i, Dem2, 2) = Jeu(i10 - poss + i, Dem1, 2)
                                Jeu(i10 - poss + i, Dem1, 1) = 0
                                Jeu(i10 - poss + i, Dem1, 2) = 0
                        NEXT i
       
                ELSE
       
                        '*** depuis une colonne vers une colonne non vide ********

                        i = 19
                       
                        DO
                                i = i - 1
                        LOOP UNTIL Jeu(i, Dem2, 1) > 0
      
                        i2 = i
                        num2 = Jeu(i2, Dem2, 1)
                        sign2 = Jeu(i2, Dem2, 2)

                        i = 19
                        i10 = 0
                        OK0 = 1
                       
                        DO
                                i = i - 1
                               
                                IF i10 > 0 THEN
                                        IF Jeu(i, Dem1, 1) = Jeu(i + 1, Dem1, 1) + 1 AND ABS((Jeu(i + 1, Dem1, 2) - Jeu(i, Dem1, 2)) MOD 2) = 1 THEN OK0 = 1 ELSE OK0 = 0
                                END IF

                                IF i10 = 0 AND Jeu(i, Dem1, 1) > 0 THEN i10 = i
                       
                        LOOP UNTIL i = 1 OR Jeu(i, Dem1, 1) = num2 - 1 OR OK0 = 0
               
                        IF Jeu(i, Dem1, 1) = num2 - 1 AND ABS(sign2 MOD 2 - Jeu(i, Dem1, 2) MOD 2) = 1 THEN OK0 = OK0 + 1
       
                        IF OK0 = 2 THEN
                                i1 = i
                                num1 = Jeu(i1, Dem1, 1)
                                sign1 = Jeu(i1, Dem1, 2)

                                IF i10 - i1 + 1 > 1 THEN
                                        poss = ValDepl(Dem2)
                                        IF poss >= i10 - i1 + 1 THEN OKDepl = 1
                                ELSE
                                        OKDepl = 1
                                END IF

                                IF OKDepl = 1 THEN
                                        FOR i = 1 TO i10 - i1 + 1
                                                OK = 1
                                                Jeu(i2 + i, Dem2, 1) = Jeu(i1 + i - 1, Dem1, 1)
                                                Jeu(i2 + i, Dem2, 2) = Jeu(i1 + i - 1, Dem1, 2)
                                                Jeu(i1 + i - 1, Dem1, 1) = 0
                                                Jeu(i1 + i - 1, Dem1, 2) = 0
                                        NEXT i
                                END IF
                        END IF
                END IF
        END IF
END IF

IF OK = 0 THEN LOCATE 23, 3: PRINT "Erreur " + STRING$(31, " ")

END SUB

SUB CalculTas

DO
        OK = 0
       
        FOR i = 1 TO 8
                IF Jeu(1, i, 1) > 0 THEN
                       
                        j = 19
                       
                        DO
                                j = j - 1
                        LOOP UNTIL Jeu(j, i, 1) > 0

                        FOR k = 1 TO 4
                                num = tas(k)
                               
                                IF Jeu(j, i, 1) = num + 1 AND Jeu(j, i, 2) = k THEN
                                        OK = 1
                                        tas(k) = num + 1
                                        Jeu(j, i, 1) = 0
                                        Jeu(j, i, 2) = 0
                                        Affiche
                                END IF
                        NEXT k
                END IF
        NEXT i
       
        FOR i = 1 TO 4
                FOR k = 1 TO 4
                        num = tas(k)
                        IF Libre(i, 1) = num + 1 AND Libre(i, 2) = k THEN
                                OK = 1
                                tas(k) = num + 1
                                Libre(i, 1) = 0
                                Libre(i, 2) = 0
                                Affiche
                        END IF
                NEXT k
        NEXT i
LOOP UNTIL OK = 0

END SUB

FUNCTION Coul (sign)

IF sign = 2 OR sign = 4 THEN c = 8 ELSE c = 12
Coul = c

END FUNCTION

SUB Demande

DIM OK$(12)                     'Textes autorisÇs

FOR i = 1 TO 12
        IF i > 8 THEN j = i + 1 ELSE j = i
        OK$(i) = LTRIM$(STR$(j))
NEXT i

DO
       
        OK = 0                   'Validation des 2 demandes

        COLOR 1: LOCATE 23, 1: PRINT STRING$(40, " ")
        LOCATE 23, 1: INPUT "? ", Dem$
        x = INSTR(Dem$, ".")
        IF x > 0 THEN
                Dem1$ = LEFT$(Dem$, x - 1)
                Dem2$ = RIGHT$(Dem$, LEN(Dem$) - x)
                FOR i = 1 TO 12
                        IF Dem1$ = OK$(i) THEN
                                        OK = OK + 1
                        END IF
                        IF Dem2$ = OK$(i) THEN OK = OK + 1
                NEXT i
        END IF
        IF OK < 2 AND x > 0 THEN LOCATE 23, 3: PRINT "Erreur " + STRING$(31, " ")
        SLEEP 1
LOOP UNTIL OK = 2

Dem1 = VAL(Dem1$)
Dem2 = VAL(Dem2$)

END SUB

SUB Done

FOR i = 5 TO 8
        Jeu(7, i, 1) = 1: Jeu(7, i, 2) = 1
NEXT i

RANDOMIZE TIMER

FOR num = 1 TO 13
        FOR sign = 1 TO 4

                DO
                        x = CINT(RND * 7) + 1
                        y = CINT(RND * 6) + 1
                LOOP UNTIL Jeu(y, x, 1) = 0
                Jeu(y, x, 1) = num
                Jeu(y, x, 2) = sign

        NEXT sign
NEXT num


FOR i = 5 TO 8
        Jeu(7, i, 1) = 0: Jeu(7, i, 2) = 0
NEXT i

END SUB

SUB Fond

COLOR 13: LOCATE 2, 12: PRINT "FREECELL For QBasic"


FOR i = 0 TO 1
COLOR 1 + i
LOCATE 4, 2 + i * 21: PRINT "…ÕÕÕÀÕÕÕÀÕÕÕÀÕÕÕª"
LOCATE 5, 2 + i * 21: PRINT "∫   ∫   ∫   ∫   ∫"
LOCATE 6, 2 + i * 21: PRINT "∫   ∫   ∫   ∫   ∫"
LOCATE 7, 2 + i * 21: PRINT "∫   ∫   ∫   ∫   ∫"
LOCATE 8, 2 + i * 21: PRINT "»ÕÕÕ ÕÕÕ ÕÕÕ ÕÕÕº"
NEXT i

COLOR 1: LOCATE 9, 4: PRINT "10  11  12  13"

COLOR 13: LOCATE 11, 4: PRINT "1    2    3    4    5    6    7    8"


END SUB

SUB Init

FOR num = 1 TO 13                       'NumÇro de la carte
        FOR sign = 1 TO 4               'Symbole de la carte

                SELECT CASE num
                        CASE 1 TO 9: a$ = STR$(num)
                        CASE 10: a$ = LTRIM$(STR$(num))
                        CASE 11: a$ = " V"
                        CASE 12: a$ = " D"
                        CASE 13: a$ = " R"
                END SELECT

                SELECT CASE sign
                        CASE 1: b$ = CHR$(4)
                        CASE 2: b$ = CHR$(6)
                        CASE 3: b$ = CHR$(3)
                        CASE 4: b$ = CHR$(5)
                END SELECT

                carte$(num, sign) = a$ + b$
        NEXT sign
NEXT num

LOCATE 24, 30: COLOR 7: PRINT "F1 = Aide";

'**********Initialisation du jeu***************************

FOR i = 1 TO 18
FOR j = 1 TO 8
Jeu(i, j, 1) = 0
Jeu(i, j, 2) = 0
NEXT j
NEXT i

END SUB

FUNCTION ValDepl (Dem2)


nblibre = 0                     'Nombre de cases libres

FOR i = 1 TO 4
        IF Libre(i, 1) = 0 THEN nblibre = nblibre + 1
NEXT i


nbjeu = 0                       'Nombre de colonnes libres

FOR i = 1 TO 8
        IF Jeu(1, i, 1) = 0 AND i <> Dem2 THEN nbjeu = nbjeu + 1
NEXT i


poss = 0                        'Nombre de cartes dÇplaáables

FOR i = nbjeu TO 0 STEP -1
        poss = poss + i + nblibre
NEXT i

poss = poss + 1


ValDepl = poss

END FUNCTION
