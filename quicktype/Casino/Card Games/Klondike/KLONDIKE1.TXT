10 CLS
15 DIM DK$(52),PRE(52),SU$(4),CA$(13),CLR(4,2),ROW$(7,19,2),ND$(52)
20 DIM ACES$(4)
30 PRINT "Klondike Solitaire":PRINT:PRINT "Do you need Instructions? ";
35 A$=INKEY$:IF A$="" THEN 35
36 IF A$<>"Y" AND A$<>"N" THEN 35
37 IF A$="Y" THEN GOSUB 5000
40 PRINT "Initializing ... "
45 GOSUB 1000
50 PRINT "Shuffling ... "
60 GOSUB 1200
65 PRINT "Dealing ... "
70 GOSUB 1500
80 GOSUB 1700
90 TOP=3
91 PRINT@15,50:PRINT "C=more cards";
92 PRINT@16,50:PRINT "D=move from deck";
93 PRINT@17,50:PRINT "Row # to move";
100 PRINT@19,1:PRINT "Next card in deck:";
110 IF ND<1 THEN PRINT "None";:GOTO 140
120 C$=ND$(TOP):GOSUB 1300
140 PRINT@21,1:PRINT USING "Card ## of ## left in deck";TOP;ND;
150 PRINT@23,1:PRINT "                               ";
152 IF NC>51 THEN 900
155 PRINT@22,1:PRINT "What do you want to do?   ";:PRINT@22,23
160 A$=INKEY$:IF A$="" THEN 160
170 IF A$="C" THEN PRINT A$;:GOSUB 300:GOTO 100
180 IF A$="D" THEN PRINT A$;:GOSUB 350:GOTO 100
190 IF A$>="1" AND A$<="7" THEN PRINT A$;:GOSUB 600:GOTO 100
200 IF A$="Q" THEN 900
210 GOTO 160
300 REM Get more cards from deck
310 IF TOP=ND THEN TOP=3:GOTO320
311 TOP=TOP+3
320 IF TOP>ND THEN TOP=ND
330 RETURN
350 REM Move from deck to a row
360 PRINT@23,1:PRINT "From deck to row? ";
380 A$=INKEY$:IF (A$>="1" AND A$<="7") OR A$="A" THEN 390
381 GOTO380
390 IF A$="A" THEN PRINT "Aces";:GOTO395
391 PRINT A$;
395 IF A$="A" THEN 540
400 TW=VAL(A$)
410 FOR PL=19 TO 1 STEP -1
420 IF ROW$(TW,PL,2)<>"   " THEN 460
430 NEXT PL
432 IF ROW$(TW,1,2)="   " AND LEFT$(ND$(TOP),2)=" K" THEN PL=0:GOTO 500
440 GOSUB 990
450 RETURN
460 BELOW$=ROW$(TW,PL,2)
470 AB$=ND$(TOP)
480 GOSUB 2000       :REM check if match
490 IF OK$="NO" THEN 440
500 ROW$(TW,PL+1,1)=ND$(TOP)
510 ROW$(TW,PL+1,2)=ND$(TOP)
520 ROW=TW:GOSUB 3100
525 GOSUB 2200        :REM repack deck
530 RETURN
540 REM Play Deck to Aces
550 AB$=ND$(TOP)
560 GOSUB 2300        :REM play to aces
570 IF OK$="NO" THEN 440
580 GOTO 525
600 REM Move from one row to another
610 FW=VAL(A$)
620 FOR FE=1 TO 19
630 IF ROW$(FW,FE,2)<>DN$ THEN 660
640 NEXT FE
650 GOSUB 990:RETURN
660 PRINT@23,1:PRINT "From row";FW;"to row? ";
680 A$=INKEY$:GOSUB 10000:IF (A$>="1" AND A$<="7") OR A$="A" THEN 690
681 GOTO680
690 IF A$="A" THEN PRINT "Aces";:GOTO695
691 PRINT A$;
695 IF A$="A" THEN 830
700 TW=VAL(A$)
710 FOR T0=19 TO 1 STEP -1
720 IF ROW$(TW,T0,2)<>"   " THEN 760
730 NEXT T0
732 IF ROW$(TW,1,2)="   " AND LEFT$(ROW$(FW,FE,2),2)=" K" THEN T0=0:GOTO 800
740 GOSUB 990
750 RETURN
760 REM
770 AB$=ROW$(FW,FE,2)
775 BELOW$=ROW$(TW,T0,2)
780 GOSUB 2000:REM check if match
790 IF OK$="NO" THEN 740
800 GOSUB 3200:REM move the stack
810 ROW=FW:GOSUB 3100:REM redraw a row
815 ROW=TW:GOSUB 3100:REM redraw a row
820 RETURN
830 REM play from a row to aces
832 FOR FE=19 TO 1 STEP -1
834 IF ROW$(FW,FE,2)<>"   " THEN 840
836 NEXT FE
838 GOSUB 990:RETURN
840 AB$=ROW$(FW,FE,2)
850 GOSUB 2300        :REM play to aces
851 IF OK$="NO" THEN 740
853 REM ROW$(FW,FE,1)="   "
854 ROW$(FW,FE,2)="   "
855 IF FE>1 THEN ROW$(FW,FE-1,2)=ROW$(FW,FE-1,1)
856 ROW=FW:GOSUB 3100   :REM redraw a row
860 RETURN
900 REM Exit game
910 GOSUB 1800
920 GOSUB 1900
925 FOR I=1 TO 20:A$=INKEY$:NEXT I
930 GOSUB 3400:PRINT "Play again? (Y or N) ";
940 A$=INKEY$:IF A$="" THEN 940
950 GOSUB 10000
960 IF A$="N" THEN 970 
961 IF A$="Y" THEN CLS:PRINT "Re-";:GOTO 40
965 GOTO 940
970 CLS:PRINT "Bye":END
990 REM Bad Play
992 PRINT@23,50:PRINT "Can't do that!";
994 PRINT@24,50:PRINT "Press any key to continue";
996 IF INKEY$="" THEN 996
997 PRINT@23,50:PRINT "                            ";
998 PRINT@24,50:PRINT "                            ";
999 RETURN
1000 REM
1005 DN$=CHR$(178):DN$=DN$+DN$+DN$
1009 IF COL$="N" THEN 1020
1010 FOR I=1 TO 2:CLR(I,1)=4:CLR(I,2)=7:NEXT
1015 FOR I=3 TO 4:CLR(I,1)=0:CLR(I,2)=7:NEXT
1019 GOTO 1030
1020 FOR I=1 TO 2:CLR(I,1)=0:CLR(I,2)=7:NEXT
1025 FOR I=3 TO 4:CLR(I,1)=7:CLR(I,2)=0:NEXT
1030 FOR I=1 TO 4:SU$(I)=CHR$(I+2):NEXT I
1035 FOR I=1 TO 4:ACES$(I)="   ":NEXT I
1040 FOR I=2 TO 9:CA$(I)=STR$(I):NEXT I
1050 CA$(1)=" A":CA$(10)="10":CA$(11)=" J":CA$(12)=" Q":CA$(13)=" K"
1060 FOR I=1 TO 4
1070 FOR J=1 TO 13
1080 K=(I-1)*13+J
1090 DK$(K)=CA$(J)+SU$(I)
1100 NEXT J
1110 NEXT I
1120 FOR I=1 TO 7
1130 FOR J=1 TO 19
1140 ROW$(I,J,1)="   ":ROW$(I,J,2)="   "
1150 NEXT J,I
1160 NC=0
1170 RETURN
1200 REM Shuffle
1210 FOR I=1 TO 52:ND$(I)="":NEXT I
1220 FOR I=1 TO 52
1230 R=RND(52)
1240 IF ND$(R)="" THEN ND$(R)=DK$(I):GOTO1250
1241 GOTO1230
1250 NEXT I
1260 FOR I=1 TO 52
1270 DK$(I)=ND$(I)
1280 NEXT I
1290 RETURN
1300 REM Display a card (C$)
1310 IF C$=DN$ THEN 1350
1320 IF C$="   " THEN 1350
1330 S=ASC(RIGHT$(C$,1))-2
1340 COLOR CLR(S,1),CLR(S,2)
1350 PRINT C$;:COLOR 7,0:PRINT "    ";
1360 RETURN
1500 REM Deal the game
1510 C=1
1520 FOR PL=1 TO 7
1530 FOR ROW=1 TO 7
1535 IF ROW<PL THEN ROW$(ROW,PL,1)="   ":ROW$(ROW,PL,2)="   ":GOTO 1570
1540 ROW$(ROW,PL,1)=DK$(C)
1550 C=C+1
1560 IF ROW=PL THEN ROW$(ROW,PL,2)=ROW$(ROW,PL,1):GOTO1570
1561 ROW$(ROW,PL,2)=DN$
1570 NEXT ROW,PL
1580 CLS
1590 L=10:PRINT@1,5:PRINT "Aces:"
1640 PRINT@4,1
1645 PRINT " 1      2      3      4      5      6      7"
1650 FOR PL=1 TO 7
1660 FOR ROW=1 TO 7
1661 C$=ROW$(ROW,PL,2)
1665 C$=ROW$(ROW,PL,2)
1670 GOSUB 1300
1680 NEXT ROW:PRINT:NEXT PL
1690 RETURN
1700 REM Prepare to play
1710 FOR I=1 TO 24
1715 J=I+28
1720 ND$(I)=DK$(J)
1730 NEXT I
1740 FOR I=25 TO 52
1750 ND$(I)=""
1760 NEXT I
1770 ND=24
1780 RETURN
1800 REM Show Deck
1810 LL=3
1815 PRINT@3,50:PRINT "Cards left in deck:";
1817 IF ND=0 THEN 1890
1820 FOR I=1 TO ND STEP 3
1830 LL=LL+1:PRINT@LL,50
1840 FOR J=0 TO 2
1845 IF I+J>ND THEN 1870
1850 C$=ND$(I+J)
1860 GOSUB 1300
1870 NEXT J:PRINT "     ";:NEXT I
1880 RETURN
1890 PRINT@4,50:PRINT "None.                     ";
1895 FOR I=2 TO 3:PRINT@LL+I,50:PRINT "                          ";:NEXT
1899 RETURN
1900 REM show cards
1910 LL=4:CN=0
1920 FOR PL=1 TO 19
1925 LL=LL+1:PRINT@LL,1
1930 FOR ROW=1 TO 7
1935 C$=ROW$(ROW,PL,2)
1940 IF C$=DN$ THEN C$=ROW$(ROW,PL,1):CN=CN+1
1950 GOSUB 1300
1960 NEXT ROW
1965 IF CN=0 THEN PL=20
1970 PRINT@LL,1:CN=0
1980 NEXT PL
1990 RETURN
2000 REM Check if AB$ can go on below$
2010 IN$=AB$:GOSUB 3000:A=N:AS=SUIT
2020 IN$=BELOW$:GOSUB 3000:BN=N:BSUIT=SUIT
2030 OK$="YES"
2040 IF A+1<>BN THEN OK$="NO"
2050 IF AS<=2 AND BSUIT<=2 THEN OK$="NO"
2060 IF AS>=3 AND BSUIT>=3 THEN OK$="NO"
2070 RETURN
2200 REM Repack Deck
2205 IF ND=1 THEN TOP=0:ND=0:RETURN
2210 FOR I=TOP+1 TO ND
2220 ND$(I-1)=ND$(I)
2230 NEXT I
2240 ND=ND-1
2250 TOP=TOP-1
2260 IF TOP<1 THEN TOP=3
2280 IF TOP>ND THEN TOP=ND
2290 RETURN
2300 REM Play AB$ to aces
2310 IN$=AB$:GOSUB 3000:A=N:AS=SUIT
2315 IN$=ACES$(AS):IF IN$="   " THEN BN=0:GOTO 2330
2320 GOSUB 3000:BN=N
2330 IF A=BN+1 THEN OK$="YES":GOTO2340
2331 OK$="NO":RETURN
2340 ACES$(AS)=AB$
2350 PRINT@1,AS*10:C$=AB$
2360 GOSUB 1300
2365 NC=NC+1:PRINT@1,50:PRINT "Score";NC
2370 RETURN
3000 REM change in$ to num and suit
3005 IF A$="   " THEN N=0:SUIT=0:RETURN
3010 A$=LEFT$(IN$,2)
3020 IF A$=" A" THEN A$=" 1"
3030 IF A$=" J" THEN A$="11"
3040 IF A$=" Q" THEN A$="12"
3050 IF A$=" K" THEN A$="13"
3060 N=VAL(A$)
3070 SUIT=ASC(RIGHT$(IN$,1))-2
3080 RETURN
3100 REM Redraw a row
3110 R=((ROW-1)*7)+1
3130 FOR PL=1 TO 19
3140 IF ROW$(ROW,PL,1)="   " THEN PL=20:GOTO 3180
3150 PRINT@4+PL,R
3160 C$=ROW$(ROW,PL,2)
3170 GOSUB 1300
3180 NEXT PL
3190 RETURN
3200 REM Copy part of a row
3205 FP=FE:TP=T0
3207 IF TP=1 AND LEFT$(ROW$(FW,FE,2),2)=" K" THEN 3220
3210 TP=TP+1
3220 ROW$(TW,TP,1)=ROW$(FW,FP,1)
3225 ROW$(TW,TP,2)=ROW$(FW,FP,2)
3230 REM ROW$(FW,FP,1)="   "
3235 ROW$(FW,FP,2)="   "
3240 FP=FP+1
3250 IF ROW$(FW,FP,2)<>"   " THEN 3210
3260 IF FE>1 THEN 3300
3270 ROW$(FW,FP,1)="   "
3280 ROW$(FW,1,2)="   "
3290 RETURN
3300 ROW$(FW,FE-1,2)=ROW$(FW,FE-1,1)
3310 RETURN
3400 REM Evaluate game performance
3405 PRINT@19,1:PRINT "You placed";NC;"cards on the Aces piles"
3410 SC=INT(NC/10)+1
3415 IF NC=52 THEN SC=7
3420 IF NC=0 THEN GOSUB 3520:RETURN
3430 ON SC GOSUB 3450,3460, 3470, 3480, 3490, 3500, 3510
3440 RETURN:REM score 0-9  10-19 20-29 30-39 40-49 50+
3450 PRINT "Too bad, better try again!":RETURN
3460 PRINT "Not too bad.":RETURN
3470 PRINT "Pretty good, keep trying.":RETURN
3480 PRINT "Good job.  Keep up the good work!":RETURN
3490 PRINT "Very good.  Now you have the idea!":RETURN
3500 PRINT "Almost got it. Bad break!":RETURN
3510 PRINT "YOU DID IT!!  Congrats!":RETURN
3520 PRINT "Must have had a bad shuffle!":RETURN
5000 REM Instructions
5010 CLS:PRINT "Klondike Solitaire"
5020 PRINT 
5030 PRINT "This program plays a solitaire  game.  A deck of 52 cards is" 
5040 PRINT "used and 28 cards are dealt     into 7 rows (piles).  The first" 
5050 PRINT "row at the left has one card,   the second two, and so on, up" 
5060 PRINT "to seven in the last row.  The  last card of each row is face" 
5070 PRINT "up and all the rest are face    down.  On each row you may"
5080 PRINT "build in descending sequence:   red on black, such as the ten"
5090 PRINT "of hearts on the jack of clubs  or spades.":PRINT 
5091 IF INKEY$="" THEN 5091
5100 PRINT "You can move the face up cards  in a row as a unit.  The top"
5110 PRINT "card being moved must fit in    sequence and color with the"
5120 PRINT "card being moved under in the   other row.  When you uncover a"
5130 PRINT "face-down card on a row, it willbe turned up.":PRINT 
5140 PRINT "You are always entitled to have 7 rows, and if one is"
5150 PRINT "entirely open you may put a kingin the space.  Whenever you"
5160 PRINT "free an ace, move it too the acefoundations.  On the aces"
5161 IF INKEY$="" THEN 5161
5170 PRINT "you may build up in suit and    sequence and then to win the"
5180 PRINT "game, you have to build each    suit up to a king.  A card must"
5190 PRINT "be the top card of a row to be  played from, to the rows onto"
5200 PRINT "the ace foundations. Once playedon the ace foundations, a"
5210 PRINT "card can NOT be removed to help elsewhere."
5220 PRINT "Press any key to continue";
5230 IF INKEY$="" THEN 5230
5231 CLS
5240 PRINT "The remaining 24 cards in the deck are used as the stock."
5250 PRINT "Every third card may be turned up and the deck may be gone"
5260 PRINT "through any number of times.  The top card is available for"
5270 PRINT "play to any row or the ace foundations.  When the top card"
5280 PRINT "is played, the next card will then be available.":PRINT 
5290 PRINT "You win if you can build all 4 ace foundations up to kings."
5300 PRINT "You lose when you can not make any further plays from the"
5310 PRINT "deck, or rows.":PRINT 
5320 PRINT "Plays are controlled by keyboard commands as follows:":PRINT 
5330 PRINT "D = Play top card of the Deck to aces (A) or row (#1-7)."
5340 PRINT "    Answer 'A' or row number 1-7 to next question to"
5350 PRINT "    indicate where the card is to be moved."
5360 PRINT "C = Get next Card in the deck and wait for next play command."
5370 PRINT "Q = Quit game and turn over all face-down cards."
5380 PRINT "#1-7 = Play cards from row# to Aces (A) or another row (#1-7)"
5390 PRINT 
5400 PRINT "An error message will be displayed if an invalid response is"
5410 PRINT "given to any question or any invalid plays attempted."
5420 PRINT:PRINT "     ** Press any key to continue **";
5430 IF INKEY$="" THEN 5430
5431 CLS:RETURN
