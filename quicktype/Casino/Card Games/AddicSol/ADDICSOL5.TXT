0 CLS:PRINT@262,"ADDICTION SOLITAIRE":GOTO30
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
8 PRINTTAB((32-LEN(M$))/2)M$:RETURN
9 FORA=0TO3:PRINT@12*32+32*A,"                               ";:NEXT:PRINT@12*32,;:RETURN
10 REM INITIALIZE
30 FORC1=1TO3000:NEXT:CLS:GOSUB2000:PRINT@486,"HIT ANY KEY TO START";
40 A$=INKEY$:IFA$=""THEN40
45 CLS:CLEAR100:DIMC1,C2,M,M$,P(4,13),D(52),T(52):M=16384:F5=1:C=52:W=1:C1=RND(-(PEEK(9)*256+PEEK(10)))
50 GOSUB770:GOSUB9:M$="SHUFFLING...":GOSUB8:GOSUB820
60 GOSUB9:M$="DEALING...":GOSUB8
70 FORI=1TO4:FORJ=1TO13:GOSUB870:NEXTJ:NEXTI:GOSUB950
80 GOSUB480

100 IFW=5THENW=1:F1=0:GOSUB980:IFF1=0THEN520
110 I=INT((E(W)-1)/13)+1:J=E(W)-13*INT((E(W)-1)/13)
120 IFJ=1THEN150
130 LL=P(I,J-1):IFLL/13=INT(LL/13)THENW=W+1:GOTO100
140 IFLL=1ORLL=14ORLL=27ORLL=40THENW=W+1:GOTO100
150 N$=CHR$(8):S$="  ":S1$=" ":GOSUB940

160 X$=INKEY$:IFX$=""THENFORC1=1TO190:NEXT:GOSUB940:GOTO160
190 IFX$="P"THEN210
200 IFX$="M"THENN$=" ":GOSUB940:W=W+1:GOTO100
201 GOTO160

210 IFJ=1THEN340
220 LL=P(I,J-1)
230 IFLL/13=INT(LL/13)THEN160
240 IFLL=1ORLL=14ORLL=27ORLL=40THEN160
250 TE=P(I,J):TT=T(P(I,J)):L=T(P(I,J-1)+1)
260 T(P(I,J))=T(P(I,J-1)+1)
270 T(P(I,J-1)+1)=TT
280 P(I,J)=P(INT((L-1)/13+1),L-13*INT((L-1)/13))
290 P(INT((L-1)/13)+1,L-13*INT((L-1)/13))=TE
300 GOSUB870
310 I=INT((L-1)/13)+1:J=L-13*INT((L-1)/13):GOSUB870
320 GOSUB950:W=1:GOTO100
330 REM OFFER CHOICE OF TWOS
340 GOSUB9
350 M$="NOW YOU HAVE A CHOICE OF":GOSUB8
360 M$="WHICH '2' YOU WANT TO PLACE.":GOSUB8
370 M$="TWO OF 'S','H','D',OR 'C'?":GOSUB8
380 T$=INKEY$:IFT$=""THEN380
390 IFT$="S"THENN2=2:GOTO450
400 IFT$="H"THENN2=15:GOTO450
410 IFT$="D"THENN2=28:GOTO450
420 IFT$="C"THENN2=41:GOTO450
430 GOTO380
440 REM NOW EXCHANGE LOCATION 
450 TE=P(I,J):TT=T(P(I,J)):L=T(N2) 
460 T(P(I,J))=T(N2)
470 T(N2)=TT:GOSUB480:GOTO280
480 GOSUB9:M$="'M'=MOVE TO THE NEXT EMPTY SLOT":GOSUB8
490 PRINT@13*32,;:M$="'P'=PLACE CARD AT THE CURSOR":GOSUB8:PRINT
500 RETURN
510 REM PLAYER CANNOT MOVE SO RESHUFFLE
520 FORI=1TO4:N(I)=.
530 IFP(I,1)<>2ANDP(I,1)<>15ANDP(I,1)<>28ANDP(I,1)<>41THEN580
540 N(I)=1
550 FORJ=2TO12:IFP(I,J)-1<>P(I,J-1)THENJ=14:GOTO570
560 N(I)=N(I)+1
570 NEXTJ
580 NEXTI
590 IFN(1)=12ANDN(2)=12ANDN(3)=12ANDN(4)=12THEN1040
600 F5=F5+1
610 REM ERASE THE WRONG ENTRIES
620 GOSUB9
630 N$=" ":S$="  ":GOSUB9:M$="RESHUFFLING...":GOSUB8
640 FORI=1TO52:D(I)=I:NEXT
650 FORI=1TO4:FORJ=N(I)+1TO13:GOSUB940:NEXTJ,I
660 C3=52:FORI=1TO4
670 IFN(I)=.THEN690
680 FORJ=1TON(I):D(P(I,J))=.:NEXTJ
690 NEXTI
700 FORI=1TO4:FORJ=1+N(I)TO13
710 R1=INT(RND(0)*C3+1)
720 IFD(R1)=0THEND(R1)=D(C3):C3=C3-1:GOTO710
730 P(I,J)=D(R1):D(R1)=D(C3):C3=C3-1:NEXTJ,I
740 FORII=1TO52:T(II)=0:NEXT
750 GOSUB850:GOTO60
760 REM SET UP BOXES
770 FORI=34TO61:PRINT@I,"Ï";:PRINT@I+288,"Ï";:NEXT
780 FORJ=34TO349STEP32:PRINT@J,"Ï";:PRINT@J+27,"Ï";:NEXT
800 RETURN
810 REM SET UP DECK
820 FORI=1TO52:D(I)=I:NEXT
830 FORI=1TO4:FORJ=1TO13:R1=INT(RND(0)*C+1):P(I,J)=D(R1):D(R1)=D(C):C=C-1
840 NEXT:NEXT
850 FORII=1TO4:FORJJ=1TO13:T(P(II,JJ))=(II-1)*13+JJ:NEXT:NEXT:RETURN
860 REM SHOW CARD
870 S$=MID$("×Ò»º·²ßÚ",INT((P(I,J)-1)/13)*2+1,2):S1$=MID$("Ô´´Ô",INT((P(I,J)-1)/13)+1,1)
890 N=P(I,J)-13*INT((P(I,J)-1)/13):IFN=1THENS$="  ":S1$=" "
910 N$=MID$(" 23456789TJQK",N,1)
930 REM LOCATE CARD POSITION
940 PRINT@I*64+J*2+1,S$;:PRINT@I*64+J*2+33,S1$;:C1=ASC(N$):IFC1<33THENPRINT"Ï";:FORC1=1TO200:NEXT:PRINTCHR$(8);:RETURN
941 M$=N$:GOSUB7:RETURN
950 Z=1:FORI=1TO52STEP13:E(Z)=T(I):Z=Z+1:NEXT
960 FORJ=1TO4:FORI=1TO3:IFE(I)>E(I+1)THENAA=E(I):E(I)=E(I+1):E(I+1)=AA
970 NEXTI,J:RETURN
980 FORK=1TO4
990 X=INT((E(K)-1)/13+1):Y=E(K)-13*INT((E(K)-1)/13)
1000 IFY=1THENF1=1
1010 W2=P(X,Y-1):IFW2<>1ANDW2<>14ANDW2<>27ANDW2<>40ANDW2/13<>INT(W2/13)THENF1=1
1020 NEXT:RETURN
1030 REM YOU WON
1040 GOSUB9:IFF5<5THENM$="CONGRATULATIONS! YOU WON!":GOSUB8
1050 PRINT@13*32,;:M$="IT TOOK"+STR$(F5)+" SHUFFLES TO COMPLETE":GOSUB8
1060 PRINT@14*32,;:M$="TYPE 'Y' TO PLAY AGAIN":GOSUB8
1070 X$=INKEY$:IFX$="Y"THEN45
1071 IFX$="N"THENEND
1072 GOTO1070

2000 PRINT"THE COMPUTER DEALS 4 ROWS OF 13"
2001 PRINT"CARDS, THEN REMOVES ACES. YOU"
2002 PRINT"MUST REARRANGE CARDS INTO 4 ROWS";
2003 PRINT"OF SAME SUIT IN ASCENDING ORDER"
2004 PRINT"WITH NO SPACES. YOU MUST MOVE A"
2005 PRINT"CARD INTO ONE OF THE EMPTY"
2006 PRINT"SPACES. YOUR POSITION IS SHOWN"
2007 PRINT"BY A CURSOR. PRESS 'M' TO MOVE"
2008 PRINT"AND 'P' TO PLACE A CARD. WHICH"
2009 PRINT"CARD DEPENDS ON THE CARD TO THE"
2010 PRINT"LEFT. WHEN POSSIBLE, THE NEXT"
2011 PRINT"CARD IN SUIT IS PLACED. AT START";
2012 PRINT"OF A ROW YOU MAY SELECT SUIT."
2013 PRINT"WHEN NO MOVES ARE POSSIBLE, THE"
2014 PRINT"UNORDERED CARDS ARE RESHUFFLED.";:RETURN

2020 REM BEN ELIZER
2030 REM KEVIN MYKYTYN
2040 REM COMPUTE JAN 1986
2050 MC-10 EDITS JIM GERRIE
