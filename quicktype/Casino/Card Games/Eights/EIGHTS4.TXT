0 CLS:PRINT@45,"eights":PRINT@71+32,"MIKE COSTELLO 1982":GOTO15

5 T1=ASC(F$):IFT1<65THENT1=T1+64
6 FORJJ=16384TO16540STEP4:IFPEEK(JJ)=T1THENIFPEEK(JJ+1)=WSTHENPOKEJJ,96:POKEJJ+1,96:JJ=16540
7 NEXT:RETURN

10 C$=MID$(STR$(M(Z)),2):S$=LEFT$(C$,2):D$=RIGHT$(C$,2):SS=VAL(S$):S=MM(SS):D=VAL(D$)-1:RETURN
11 P(D,S)=M(Z):RETURN
12 T(D,S)=M(Z):RETURN
13 PRINT@0,;:FORS=0TO3:FORD=0TO12:IFP(D,S)THENY=P(D,S):GOSUB2550:PRINTF$;CHR$(WS)"  ";
14 NEXT:NEXT:PRINT:RETURN

15 CLEAR 300: REM DEFSTR A-L: DEFINT M-Z
18 REM
20 DIM M(51),MM(83),P(12,3),T(12,3),Q(3),QS(3),QT(3),QD(3),D$(12):V5=RND(-(PEEK(9)*256+PEEK(10)))
21 V5=1:S7=111
30 C5$="CLUBS":C6$="DIAMONDS":C7$="HEARTS":C8$="SPADES":C9$=""
40 D$(0)="A":D$(1)="2":D$(2)="3":D$(3)="4":D$(4)="5":D$(5)="6":D$(6)="7":D$(7)="8":D$(8)="9":D$(9)="T"
41 D$(10)="J":D$(11)="Q":D$(12)="K":MM(67)=0:MM(68)=1:MM(72)=2:MM(83)=3

54 REM
60 PRINT@194,"A MOMENT WHILE I SHUFFLE...":GOSUB16100
80 REM

100 X=0:Y2=6701:GOSUB 150:Y2=6801:GOSUB 150:Y2=7201:GOSUB 150:Y2=8301:GOSUB 150
101 REM GOSUB145
110 FOR X=51 TO 0 STEP-1:Y5=M(X):Y6=RND(X):M(X)=M(Y6):M(Y6)=Y5:NEXT:Y5=M(0):Y6=RND(51):M(0)=M(Y6):M(Y6)=Y5
111 REM GOSUB145:STOP
130 Y5=0:X5=0:GOTO 200:REM CONTROL PASSES TO RULES-DISPLAY ROUTINE
140 REM
145 FORX=0TO51:PRINTM(X);:NEXT:PRINT:RETURN

150 FOR Y=Y2 TO Y2+12:M(X)=Y:X=X+1:NEXT:RETURN

200 REM RULES
210 CLS:FORZ=0TO6:GOSUB10:GOSUB11:NEXT:PC=7
230 FORZ=7TO13:GOSUB10:GOSUB12:NEXT:Z=Z:U=7


250 GOSUB13:PRINT@224,"ENTER 0,0 TO DRAW A CARD":PRINT@192,"":PRINT@192,;:GOSUB1000:WC=S
251 IFWD=0ANDZ>51THENPRINT@224,"NO CARDS LEFT":WD=Y5:WC=X5:GOTO270
252 IFWD=0THENPC=PC+1:GOSUB2100:GOSUB10:GOSUB11:Z=Z+1:GOSUB7700:GOTO250
253 IFP(WD-1,WC)=0THENPRINT@224,"YOU DON'T HAVE THAT CARD":GOTO285
254 IFWD=Y5ORWC=X5OR(Y5=0ANDX5=0)ORWD=8THEN256
255 PRINT@224,"MATCH SUIT, DENOMINATION":PRINT"OR PLAY AN 8":GOTO285
256 NS=-1:IFWD=8THENPRINT@224,"":PRINT@224,"NEW SUIT (C,D,H,S)";:INPUTS$:S=-1:GOSUB1006:NS=S:IFNS>3ORS<0THEN256
257 GOSUB290
260 PC=PC-1:GOSUB2100:X=WD-1:Y=P(WD-1,WC):GOSUB2550:GOSUB5:PRINT@96,"":PRINT@S7,F$;CHR$(WS);:P(WD-1,WC)=0
261 IFPC=0THENPRINT@11*32+12,"YOU WIN!":PRINT@0,"":GOSUB16200:GOTO300
265 IFNS<>-1THENWC=NS
270 V5=V5+1:X5=0:GOSUB6000
271 IFU=0THENPRINT@11*32+13,"I WIN!":GOSUB16200:GOTO300
280 GOTO250
285 SOUND1,10::GOSUB16100:GOSUB16100:PRINT@224,"":PRINT:GOTO250
290 PRINT@224,"":PRINT:PRINT@480,"                            ";:RETURN

300 PRINT@480,"WANT ANOTHER GAME (Y/N)?";
310 M$=INKEY$:IFM$="Y"THENRUN
320 IFM$="N"THENEND
330 GOTO310

1000 INPUT"ENTER CARD TO PLAY D,S";D$,S$:WD=VAL(D$):S=VAL(S$)
1001 IFD$="A"THENWD=1
1002 IFD$="T"THENWD=10
1003 IFD$="J"THENWD=11
1004 IFD$="Q"THENWD=12
1005 IFD$="K"THENWD=13
1006 S$=LEFT$(S$,1):IFS$="C"THENS=0
1007 IFS$="D"THENS=1
1008 IFS$="H"THENS=2
1009 IFS$="S"THENS=3
1010 RETURN

2100 PRINT@128,"YOUR CARDS"PC,"    MY CARDS"U:RETURN

2300 PRINT@224,"A";
2301 IFY5=1THENPRINT"N ACE";
2302 IFY5=2THENPRINT" TWO";
2303 IFY5=3THENPRINT" THREE";
2304 IFY5=4THENPRINT" FOUR";
2305 IFY5=5THENPRINT" FIVE";
2306 IFY5=6THENPRINT" SIX";
2307 IFY5=7THENPRINT" SEVEN";
2308 IFY5=8THENPRINT"N EIGHT";
2309 IFY5=9THENPRINT" NINE";
2310 IFY5=10THENPRINT" TEN";
2311 IFY5=11THENPRINT" JACK";
2312 IFY5=12THENPRINT" QUEEN";
2313 IFY5=13THENPRINT" KING";
2315 IFX5=0THEN2319
2316 PRINT:RETURN
2319 X5=X3:PRINT" OF ";
2320 IFX5=0THENPRINTC5$
2340 IFX5=1THENPRINTC6$
2350 IFX5=2THENPRINTC7$
2360 IFX5=3THENPRINTC8$
2370 RETURN

2550 C$=MID$(STR$(Y),2):S$=LEFT$(C$,2):D$=RIGHT$(C$,2):WS=VAL(S$):S=MM(WS):D=VAL(D$)-1:WD=D+1:F$=D$(D):RETURN
2600 GOSUB2550:TC=S:RETURN

6000 PD=WD-1:IF WD=8 THEN 6400
6001 FOR X=0 TO 3:QD(X)=0:NEXT:X=0:FOR TC=0 TO 3:Y=T(PD,TC):IF Y>0 THEN QD(TC)=1:X=X+1:NEXT:GOTO6010
6002 NEXT
6003 REM NOTE ANY MATCHING DENOMINATIONS IN OTHER SUITS (UNLESS FOLLOWING 8 THEN ONLY WANT TO LOOK AT THE FORCED SUIT)
6010 IF X=0 THEN 6400:REM WE JUMP TO 6400 IF THE MACHINE CANNOT MATCH THE DENOMINATION OF THE CARD LED
6015 REM
6020 GOSUB 7010:REM ON RETURN FROM THIS SUBROUTINE WE HAVE STORED THE LENDTH OF EACH SUIT HELD BY THE MACHINE IN THE ARRAY QS
6025 REM
6030 FOR X=0 TO 3:IF QD(X)=0 AND X<>WC THEN QS(X)=0
6031 NEXT:REM THIS TELLS THE MACHINE TO IGNORE SUITS WITHOUT MATCHING DENOMINATIONS BY NULLING THE VALUE IN QS
6035 REM
6040 GOSUB 8600:REM ON RETURN, X3 HAS A VALUE EQUAL TO THE COLUMN IN THE T ARRAY FROM WHICH THE MACHINE WILL PLAY ITS CARD
6045 REM
6050 IF X3=WC THEN 6100:REM TESTING TO SEE IF THE MACHINE'S PREFERRED SUIT IS THE SAME AS THE PLAYER'S LED SUIT
6055 REM
6060 TC=X3:TR=PD:GOTO 8000:REM MACHINE'S CHOSEN SUIT IS IN X3 AND THE DENOMINATION IS THE SAME AS THAT OF THE PLAYER'S CARD
6065 REM
6100 GOSUB 7030:TC=X3:TR=X:GOTO 8000:REM ON RETURN FROM 7030 WE HAVE FOUND THE HIGHEST LARD (OTHER THAN AN 8) IN THE CORRECT SUIT
6395 REM X3=THE SUIT AND X=THE DENOMINATION
6400 X3=WC:REM WE DON'T HAVE THE SAME DENOMINATION IN ANOTHER SUIT, OR WE ARE FOLLOWING AN 8 WHICH FORCES A PARTICULAR SUIT
6405 REM LOOK FOR A CARD IN THAT SUIT
6410 GOSUB 7030:REM ON RETURN, X= THE HIGHEST CARD WHICH IS NOT AN 8
6415 REM
6420 IF X=-1 THEN 6440:REM WE HAVE NO CARDS IN THE LED OR FORCED SUIT EXCEPT PERHAPS AN 8
6425 REM
6430 TR=X:TC=WC:GOTO 8000:REM PLAY THE CHOSEN CARD WHICH IS IN THE LED SUIT
6435 REM
6440 FOR X8=0 TO 3:IF T(7,X8)>0 THEN 6460
6441 NEXT:REM IF THE VALUE IS GREATER THAN ZERO WE HAVE AN 8 AND JUMP TO 6460
6445 REM
6450 GOTO 7600:REM WE CAN'T PLAY A CARD SO WILL HAVE TO DRAW ONE
6455 REM
6460 U6=0:IF U=1 THEN TC=X8:TR=7:GOTO 8000
6461 GOSUB 7300:XF=X3:GOSUB 6490:IF U6=0 THEN GOSUB 7400:TC=X8:TR=7:GOTO8000
6462 U5=X8:GOTO 7600
6465 REM
6490 IF U<4 THEN RETURN:REM VARIABLE U ORIGINALLY PU-- NEVER SET SO FOLLOWING NEVER RUNS. TYPO?
6491 IF Z>45 THEN RETURN
6492 U8=INT(INT(52-Z)/4):U7=0:U6=1:RETURN
6499 REM ROUTINE TO BUILD UP VALUES IN QS
7000 GOSUB 7010:GOSUB 8600:GOSUB 7030:GOSUB 7050:TR=X:TC=X3:RETURN
7005 REM
7010 FOR X=0 TO 3:QS(X)=0:NEXT:FOR TC=0 TO 3:FOR TR=0 TO 12:IF TR=7 THEN 7020
7011 IF T(TR,TC)>0 THEN GOSUB 7025
7020 NEXT:NEXT:RETURN
7025 QS(TC)=QS(TC)+1:RETURN
7029 REM ROUTINE TO FIND HIGHEST CARD
7030 FOR X=12 TO 0 STEP-1:IF X=7 THEN 7040
7031 IF T(X,X3)>0 THEN RETURN
7040 NEXT:RETURN
7049 REM ROUTINE TO DISPLAY PLAYED CARD (CALLED BY LINE 8000)
7050 Q(X3)=Q(X3)+1:XL=T(X,X3):T(X,X3)=0:Y=XL:GOSUB 2550:PRINT@96,"":PRINT@S7,F$;CHR$(WS);:Y5=WD:GOSUB 2300:RETURN
7299 REM ROUTINE TO WEIGHT SUIT VALUES
7300 FOR ZZ=0 TO 3:QS(Z1)=0:NEXT:FOR ZZ=0 TO 3:FOR ZY=0 TO 12:GOSUB 7350:NEXT:NEXT
7301 VS=(Q(0)*5)+(QS(0)*20):VH=(Q(1)*5)+(QS(1)*20):VD=(Q(2)*5)+(QS(2)*20):VC=(Q(3)*5)+(QS(3)*20)
7305 IF QS(0)=0 THEN VS=0
7306 IF QS(1)=0 THEN VH=0
7307 IF QS(2)=0 THEN VD=0
7308 IF QS(3)=0 THEN VC=0
7309 REM DON'T FORCE A SUIT MACHINE IS VOID IN, WHATEVER THE WEIGHTING FACTORS SAY
7310 QS(0)=VS:QS(1)=VH:QS(2)=VD:QS(3)=VC
7320 GOSUB 8600:REM COME BACK WITH THE CORRECT SUIT TO FORCE = X3
7330 RETURN
7350 IF ZY=7 OR T(ZY,ZZ)=0 THEN RETURN
7351 QS(ZZ)=QS(ZZ)+1:RETURN
7399 REM ROUTINE TO PLAY AN 8
7400 IF U=1 THEN PRINT@192,"MY LAST CARD'S AN 8";:RETURN
7401 IF X3=0 THEN J$=C5$:GOTO7410
7402 IF X3=1 THEN J$=C6$:GOTO7410
7403 IF X3=2 THEN J$=C7$:GOTO7410
7404 J$=C8$
7410 X5=X3:PRINT@192,"I PLAY AN 8--":PRINT"NEW SUIT IS "J$:C9$=J$
7411 GOSUB 16100:GOSUB 16100:PRINT@192,"":PRINT@224,"":PRINT@416,"":PRINT@480,"NEW SUIT: "C9$;:RETURN
7599 REM ROUTINE TO DRAW CARDS UNTIL PLAYABLE CARD FOUND OR OTHER CONDITIONS MET
7600 IF Z=52 THEN 8500
7601 PRINT@192,;:GOSUB 7700:GOSUB16100:Y=M(Z):Z=Z+1:U7=U7+1:GOSUB 2550
7602 PRINT@192,"":IF Z=47 THEN GOSUB 26000:GOTO7610
7603 IF Z=52 THEN GOSUB 10500:REM AT THIS POINT WD=DENOM. AND WS=SUIT
7610 GOSUB 2600:REM TC NOW HAS VALUE FROM 0 TO 3
7620 T(WD-1,TC)=Y:U=U+1:GOSUB 2100:REM DRAWN CARD HAS BEEN STORED IN MACHINE'S HAND
7630 IF WD=8 OR WD-1=PD OR TC=WC THEN 7650:REM 7650 IF IT CAN BE USED AS DISCARD
7640 IF U6=0 THEN 7600
7641 IF U7<=U8 THEN 7600
7642 X3=XF:GOSUB 7400:TC=U5:TR=7:GOTO 8000
7650 IF WD=8 THEN XT=X:TT=TC:GOSUB 7010:X=XT:TC=TT:GOSUB 7300:XF=X3:GOSUB 7400:TR=7:GOTO 8000
7651 TR=WD-1:GOTO 8000:REM NOTE THE SUIT THE MACHINE IS FORCING IF PLAYING AN 8, ELSE JUST PLAY IT

7700 SOUND1,1:RETURN

7990 REM ****************************
7995 REM ROUTINES IN LISTING 3 JUMP HERE
8000 PRINT@192,"HERE'S MY DISCARD...":GOSUB 16100:S4=0:R6=0
8010 U6=0:X=TR:X3=TC:GOSUB 7050:U=U-1:GOSUB 2100:R=0:IF TR=7 AND U=0 THENPRINT@192,"I WIN WITH AN 8!":GOSUB 16200:RETURN
8011 IF TR=7 THEN TC=XF
8012 RETURN
8011 REM ****************************
8499 REM ROUTINE CALLED WHEN MACHINE CAN'T PLAY ONE AND PACK IS EMPTY
8500 R6=R6+1:IF R6=2 THEN 19900
8501 PRINT@192,"I CAN'T GO: YOUR TURN":GOSUB 16100:GOSUB 16100:PRINT@192,"":R=0:TR=PD:TC=WC:RETURN
8599 REM ROUTINE TO FIND SUIT WITH MOST CARDS IN AND STORE ITS ARRAY COLUMN IN X3
8600 IF QS(0)>=QS(1) THEN X1=0:GOTO8610
8601 X1=1
8610 IF QS(X1)>=QS(2) THEN X2=X1:GOTO8620
8611 X2=2
8620 IF QS(X2)>=QS(3) THEN X3=X2:GOTO8630
8621 X3=3
8630 RETURN

10500 PRINT@224,"LAST CARD.":SOUND200,8:GOSUB16100:PRINT@224,"":RETURN
16100 FORZZ=1TO2500:NEXT:RETURN
16200 FORZZ=50TO200STEP10:SOUNDZZ,1:NEXT:RETURN
19900 SOUND1,8:PRINT@192,"IT'S A DRAW!":GOTO300
26000 PRINT@224,"ONLY 5 CARDS LEFT":SOUND200,8:GOSUB16100:PRINT@224,"":RETURN
