0 CLEAR700:DIMN$(12),S$(3),B(3,4),T(3,4),C(51),S(4,4),X(4,4),Y(4,4),V(12),K(3),FS(9),W(9),V$(9),C1$(4),C2$(4):GOSUB3000:GOTO3
1 PRINT@L,MID$(CA$,V+1,1);C1$(S);:PRINT@L+32,C2$(S);:RETURN
3 CLS1:PRINT@11,"solo poker":POKE16399,32:PRINT@73,"BY JAMES GARON":PRINT@132,"MC-10 EDITS JIM GERRIE":B$=CHR$(0)
4 FORI=0TO51:C(I)=I:NEXT:FORI=0TO51:PRINT@261+I-3*INT((I+1)/3),"  s h u f f l i n g  ";:R=RND(51):REM JG
5 Q=PEEK(9)AND128:POKE49151,Q:POKE49151,128-Q:T=C(I):C(I)=C(R):C(R)=T:NEXT:ON-(SC$(10)>"")GOTO12:FORI=0TO9:READFS$(I):NEXT
6 GOSUB102
7 V$(0)="1PR":V$(1)="2PR":V$(2)="3KI":V$(3)="STR":V$(4)="FLS":V$(5)="FUL":V$(6)="4KI":V$(7)="SFL":V$(8)="RFL":V$(9)="---"
12 CLS1:PRINT@11,"solo poker":POKE16399,32:L=187:S=4:V=13:GOSUB1:L=218:GOSUB1
13 FORR=0TO4:FORC=0TO4:GOSUB29:IFR=CTHENCA=13:SU=.:GOSUB1:NEXT:NEXT:GOTO15
14 PRINT@L+2,"Œ";:PRINT@L+34,"ƒ";:NEXT:NEXT
15 FORR=0TO4:FORC=0TO4:S(R,C)=-1:X(R,C)=RND(0):NEXT:NEXT:FORP=0TO4:S(P,P)=100+C(P):NEXT:R=0:C=4:NP=0:DX=5:H=R:I$=INKEY$
16 FORP=5TO23:L=32*7+25:PRINT@L,"  ÃÂ";:PRINT@L+32,"    ";:GOSUB28
17 GOSUB29:B1=PEEK(L+2+Z):B2=PEEK(L+34+Z):RESET(C*8+4,R*6+3):RESET(C*8+5,R*6+3):RESET(C*8+4,R*6+4):RESET(C*8+5,R*6+4)
18 B=ASC(INKEY$+B$):GOSUB30:IFB=.THEN17
19 GOSUB500:IFB=13THEN34
20 GOTO17
28 GOSUB62:GOSUB1:RETURN
29 L=R*96+C*4+32:X=C*8+4:Y=R*6+3:RETURN
30 IFB1=140THENSET(C*8+4,R*6+3,.):SET(C*8+5,R*6+3,.):SET(C*8+4,R*6+4,.):SET(C*8+5,R*6+4,.)
31 IFB=.THENFORI=1TO75:NEXT
32 POKEL+2+Z,B1:POKEL+34+Z,B2:RETURN
33 B1=PEEK(L+2+Z):B2=PEEK(L+34+Z):RESET(C*8+4,R*6+3):RESET(C*8+5,R*6+3):RESET(C*8+4,R*6+4):RESET(C*8+5,R*6+4):GOTO30
34 IFS(R,C)+1THENSOUND25,1:GOTO18
35 GOSUB29:GOSUB1:SOUND200,1:SOUND142,1
36 S(R,C)=C(P):GOSUB63:PRINT@249,"  ÏÏ";:PRINT@281,"    ";
37 IFDXTHENGOSUB58:IFN>NPTHENGOSUB46:NP=N:GOTO37
38 NEXTP
39 FORI=0TO4:FORJ=0TO4:IFS(I,J)=-1THENRR=I:CC=J
40 NEXT:NEXT
41 PRINT@249,"  ÏÏ";:PRINT@281,"    ";:GOSUB33:GOSUB1
42 R=RR:C=CC:GOSUB29:GOSUB28:GOSUB63:SOUND25,1:IFDXTHENGOSUB100
43 GOTO64
44 FORI=0TO12:V(I)=.:NEXT:FORI=0TO3:K(I)=0:NEXT:FL=0:ST=0:PR=0:K3=0:K4=0:FH=0:P2=0:SF=0:RF=0:V=9:RETURN
46 FORII=1TON-NP:IFDX=.THENII=N:GOTO57
47 FORI=1TO4:SOUND142,1:NEXT:GOSUB800:FORI=4TO0STEP-1:ON-(S(I,I)>99)GOSUB510:NEXT
48 GOSUB29
49 GOSUB33:IFDX=1THEN55
50 B=ASC(INKEY$+B$):IFB=.THEN49
51 GOSUB520
53 IFB-13THENC=R:GOTO48
54 IFS(R,C)<52THENSOUND25,1:GOTO49
55 GOSUB29:PP=P:P=R:SOUND53,1:GOSUB28:P=PP
56 DX=DX-1:S(R,C)=S(R,C)-100:Y(R,C)=INT(C(R)/13):X(R,C)=C(R)-13*Y(R,C)
57 NEXTII:RETURN
58 N=.:FORR1=0TO4:FORC1=0TO3:FORC2=C1+1TO4:IFX(R1,C1)=X(R1,C2)THENN=N+1
59 NEXT:NEXT:NEXT
60 FORC1=0TO4:FORR1=0TO3:FORR2=R1+1TO4:IFX(R1,C1)=X(R2,C1)THENN=N+1
61 NEXT:NEXT:NEXT:RETURN
62 S=INT(C(P)/13):V=C(P)-S*13:RETURN
63 X(R,C)=V:Y(R,C)=S:RETURN
64 FORQ=.TO14:PRINT@Q*32+21,"":NEXT:SC=.
65 FORR=0TO4:GOSUB44:FORC=0TO4:V(X(R,C))=V(X(R,C))+1:K(Y(R,C))=K(Y(R,C))+1:NEXT
66 GOSUB73:PRINT@32*(R*3+1)+21,V$;:NEXT
67 FORC=0TO4:GOSUB44:FORR=0TO4:V(X(R,C))=V(X(R,C))+1:K(Y(R,C))=K(Y(R,C))+1:NEXT
68 GOSUB73:PRINT@481+C*4,V$;:NEXT:GOSUB600:CLS:PRINT@11,"solo poker":POKE16399,32
69 FORH=0TO9:GOSUB530
70 GOSUB700:NEXT:PRINT"--------------------------------YOUR SCORE IS:"TAB(28);:PRINTRIGHT$(STR$(SC),3)
71 I$=INKEY$:PRINT:PRINTTAB(5)"PRESS enter TO CONTINUE";
72 I$=INKEY$:ON-(I$=CHR$(13))GOTO82:GOTO72
73 GOSUB730
78 GOSUB540:RETURN
82 CLS:M=.:FORI=1TO10:IFSC>SC(I)THENM=I
83 NEXT:IFM=.THEN87
84 PRINT@9,"CONGRATULATIONS";:PRINT@32,"YOUR SCORE IS ONE OF THE TOP TENPLEASE ENTER YOUR NAME":PRINT
85 PRINT@105,;:INPUTSC$:IFSC$=""THEN85
86 FORI=2TOM:SC(I-1)=SC(I):SC$(I-1)=SC$(I):NEXT:SC(M)=SC:SC$(M)=LEFT$(SC$,7)
87 PRINT@0,"TOP TEN SCORES:":PRINT:FORI=10TO1STEP-1:GOSUB650
88 NEXT:PRINT:PRINT"PRESS enter TO PLAY AGAIN":I$=INKEY$
89 I$=INKEY$:IFI$=CHR$(13)THENCLS:GOSUB102:GOTO3
90 GOTO89
100 FORK=0TO4:IFS(K,K)>51THENS(K,K)=S(K,K)-100:C=K:R=K:P=K:GOSUB29:GOSUB28:GOSUB63
101 NEXTK:RETURN
102 CLS:PRINT"  VALUE OF HANDS":PRINT
103 PRINTFS$(0),10,FS$(1),30,FS$(2),50,FS$(3),60,FS$(4),"100",FS$(5),"120",FS$(6),"160",FS$(7),"300",FS$(8),"500"
104 PRINT:PRINT"  PRESS enter ...":I$=INKEY$
105 IFINKEY$=""THEN105
106 CLS:RETURN
500 IFB=65THENIFC>.THENC=C-1:RETURN
501 IFB=83THENIFC<4THENC=C+1:RETURN
502 IFB=87THENIFR>.THENR=R-1:RETURN
503 IFB=90THENIFR<4THENR=R+1:RETURN
508 RETURN
510 C=I:R=I:RETURN
520 IFB=87ORB=65THENIFR>.THENR=R-1:RETURN
522 IFB=94ORB=8THENR=.:RETURN
524 IFB=83ORB=90THENIFR<4THENR=R+1:RETURN
526 IFB=9ORB=10THENR=4
528 RETURN
530 IFH<5THENPRINT"ROW";:RETURN
531 IFH=5THENPRINT:PRINT"COL";:RETURN
532 PRINT"COL";:RETURN
540 N=0:FORI=0TO12:IFV(I)=4THENK4=160:V=6:GOTO550
541 IFV(I)=3THENK3=50:V=2:GOTO550
542 IFV(I)=2THENPR=10:V=0:N=N+1
550 NEXT:IFN=2THENPR=0:P2=30:V=1:GOTO560
551 IFK3THENIFN=1THENK3=0:PR=0:FH=120:V=5
560 FS(H)=RF+SF+K4+ST+FH+K3+FL+P2+PR:SC=SC+FS(H):W(H)=V:H=H+1:V$=V$(V):IFV=9THENSOUND25,2:RETURN
561 SOUND53,1:RETURN
600 I$=INKEY$:PRINT@501,"hit enter";:POKEZ+504,32
610 I$=INKEY$:IFI$<>CHR$(13)THEN610
620 RETURN
650 IFSC$(I)>""THENPRINTSC$(I),:PRINTRIGHT$("    "+STR$(SC(I)),4):RETURN
660 PRINT"....","....":RETURN
700 PRINTSTR$(H+1+5*(H>4))TAB(11)FS$(W(H))TAB(28)
701 PRINTRIGHT$(STR$(FS(H)),3):RETURN
730 FORI=0TO3:IFK(I)=5THENFL=100:V=4
731 NEXT
740 IFV(0)*V(1)*V(2)*V(3)*V(12)THENST=60:V=3:GOTO770
741 FORI=0TO8:ST=60:FORJ=0TO4:IFV(I+J)=0THENST=0:GOTO760
750 NEXT:IFST THENI=8:V=3
760 NEXTI
770 IFFL*ST THENFL=0:ST=0:SF=300:V=7:IFV(12)*V(11)THENSF=0:RF=500:V=8
780 RETURN
800 I$=INKEY$:IFI$<>""THEN800
810 RETURN
1000 DATAPAIR,TWO PAIR,THREE OF A KIND,STRAIGHT,FLUSH,FULL HOUSE,FOUR OF A KIND,STRAIGHT FLUSH,ROYAL FLUSH,NOTHING
3000 C1$(0)="¥¯ "
3010 C2$(0)=" ¬®¨"
3020 C1$(1)="±»°"
3030 C2$(1)=" ½¿¸"
3040 C1$(2)="··²"
3050 C2$(2)=" ´¾°"
3060 C1$(3)="¡« "
3070 C2$(3)=" ¬®¨"
3080 C1$(4)="ÏÏÏ"
3090 C2$(4)=" ÏÏÏ"
3100 CA$="23456789TJQKA ":Z=RND(-(PEEK(9)*256+PEEK(10))):Z=16384:RETURN
