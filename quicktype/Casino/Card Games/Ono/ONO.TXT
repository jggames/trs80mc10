0 GOTO10
1 '***** ONO V3.1 **************   **** CHARLES BARTLETT *******
3 SAVE"255:3":END'1
10 CLEAR2000:PMODE0:PCLEAR1:HBUFF1,4880:HBUFF2,3040
20 ON BRK GOTO 1560
30 ON ERR GOTO 920
40 POKE65497,0:DIM W$(26),BP(40,1),D(108,1),C(14,3),PD(40,1),CD(40,1):GOSUB810:HSCREEN2
50 HCLS15:HCOLOR8:GOSUB1540:FOR K3=1 TO 5:FOR K4=1TO 10
60 MS$="BM"+STR$(K3+90)+","+STR$(K4+90)+"S32C5"+W$(15)+"C6"+W$(14)+"C7"+W$(15):HDRAW MS$:NEXT K4,K3
70 HGET(66,0)-(238,55),1:HGET(90,56)-(224,99),2:GOSUB1530
80 HCLS15:HPUT(90,56)-(224,99),2,PSET:HPUT(66,0)-(238,55),1,PSET:HCOLOR8:HPRINT(5,17),"(c) 1/2/87  Charles Bartlett":HPRINT(1,21),"YOUR SCORE":HPRINT(12,21),XP:HPRINT(23,21),"Coco's SCORE":HPRINT(35,21),XC:HPRINT(30,12),"Ver 3.1"
90 B=1:FOR CY=5 TO 140 STEP 45:FOR CX=10 TO 280 STEP 30:BP(B,0)=CX:BP(B,1)=CY:B=B+1:NEXTCX,CY
100 SN=0:EN=0:VN=1:GOSUB480:SN=1:EN=12:VN=2:GOSUB480:SN=13:EN=14:VN=1:GOSUB480
110 GOSUB490:HCLS15:'SHUFFLE
120 FOR D=1 TO 108:IF D(D,0)=11 THEN D(D,0)=10:NEXT ELSE NEXT
130 Z=1:FOR Q=1 TO 13 STEP 2:PD(Z,0)=D(Q,0):PD(Z,1)=D(Q,1):CD(Z,0)=D(Q+1,0):CD(Z,1)=D(Q+1,1):Z=Z+1:NEXTQ:TN=7:PC=1:CT=7
140 TC=1:FOR SP=1 TO TN:NC=PD(TC,0):CC=PD(TC,1):GOSUB620:TC=TC+1:NEXT
150 SP=39:NC=15:GOSUB620:SP=40:NC=D(15,0):CC=D(15,1):DN=NC:DC=CC:GOSUB620:Q=16
160 IF DN=13 THEN IC=DC:GOSUB420
170 IF DN=14 OR DN=12 OR DN=10 THEN SP=40:GOSUB290:NC=D(Q,0):CC=D(Q,1):DN=NC:DC=CC:GOSUB620:Q=Q+1:GOTO160
180 IF TN=0 OR CT=0 OR TN>38 OR CT>38 THEN 1360 ELSE GOSUB540
190 GOSUB1680
200 GOSUB350:IF BUTTON(0)=0 THEN 200 ELSE SOUND100,1:SOUND100,1:IF LC=14 THEN DC=PD(PC,1):LC=0
210 IF PD(PC,0)>12 THEN 250
220 IF PC=39 THEN GOSUB1330:GOTO940
230 IF PD(PC,1)=DC THEN GOSUB430:IF DN=10 THEN 180 ELSE IF DN=12 THEN GOSUB470:GOTO180 ELSE 940
240 IF PD(PC,0)=DN THEN GOSUB430:IF DN=10 THEN 180 ELSE IF DN=12 THEN GOSUB470:GOTO180 ELSE 940
250 IF PD(PC,0)=13 THEN GOSUB430:MS$="What color ?":GOSUB520:GOSUB390:IF TN=0 THEN 270 ELSE 940
260 IF PD(PC,0)=14 THEN GOSUB370:IF OK THEN GOSUB430:GOSUB470:GOSUB470:LC=14:GOTO180 ELSE 280
270 IF TN=0 OR CT=0 OR TN>38 OR CT>38 THEN 1360
280 MS$="ILLEGAL MOVE":GOSUB520:SOUND10,3:FOR K=1 TO 500:NEXT K:GOTO180
290 HCOLOR15:HLINE(BP(SP,0),BP(SP,1))-(BP(SP,0)+25,BP(SP,1)+40),PSET,BF:RETURN
300 J0=JOYSTK(0):J1=JOYSTK(1):IF J0<5 THEN IF PC=39 THEN PC=TN ELSE PC=PC-1:IF PC<1 THEN PC=1
310 IF J0>60 THEN PC=PC+1:IF PC>TN THEN PC=39
320 IF J1<5 THEN IF PC=39 THEN PC=TN ELSE PC=PC-10:IF PC<1 THEN PC=PC+10
330 IF J1>60 THEN PC=PC+10:IF PC>TN THEN PC=39
340 RETURN
350 HCOLOR8:GOSUB360:HCOLOR15:GOSUB360:GOSUB300:RETURN
360 FOR W=1 TO 2:HLINE(BP(PC,0)-W,BP(PC,1)-W)-(BP(PC,0)+25+W,BP(PC,1)+40+W),PSET,B:NEXT:RETURN
370 OK=-1:FOR T=1 TO TN:IF PD(T,1)=DC AND T<>PC AND PD(T,0)<>13 AND PD(T,0)<>14 THEN OK=0:RETURN
380 NEXT:RETURN
390 I$=INKEY$:IFI$=""THEN390 ELSE IF I$="G" THEN I$="GREEN":IC=0  ELSE IF I$="Y" THEN I$="YELLOW":IC=1 ELSE IF I$="B" THEN I$="BLACK":IC=2 ELSE IF I$="R" THEN I$="RED":IC=3 ELSE SOUND10,2:GOTO390
400 SOUND200,1
410 MS$="COLOR IS "+I$:GOSUB520
420 DC=IC:HPAINT(BP(40,0)+4,BP(40,1)+24),IC,8:HPAINT(BP(40,0)+12,BP(40,1)+18),IC,8:RETURN
430 SP=40:GOSUB290:SP=PC:GOSUB290
440 DC=PD(PC,1):DN=PD(PC,0):NC=DN:CC=DC:SP=40:GOSUB620:IF PC<>TN THEN PD(PC,0)=PD(TN,0):PD(PC,1)=PD(TN,1):SP=PC:NC=PD(PC,0):CC=PD(PC,1):GOSUB620
450 SP=TN:GOSUB290:TN=TN-1:IF PC=TN+1 THEN PC=PC-1
460 RETURN
470 CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1:GOSUB1680:CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1:GOSUB1680:RETURN
480 FOR Z=SN TO EN:FOR SU=0 TO 3:C(Z,SU)=VN:NEXT SU,Z:RETURN
490 MS$="SHUFFLING - Please wait - 25 seconds":GOSUB520:FOR D=1 TO 108
500 PALETTE5,RND(63):PALETTE6,RND(63):PALETTE7,RND(63):CN=RND(15)-1:CC=RND(4)-1:IF C(CN,CC)=0 THEN 500 ELSE D(D,0)=CN:D(D,1)=CC:C(CN,CC)=C(CN,CC)-1 
510 NEXT D:RETURN
520 RGB:PALETTE2,0:HCOLOR13:HLINE(0,183)-(320,192),PSET,BF:HCOLOR2:SOUND200,1:HPRINT(1,23),MS$:FOR U=1TO 300:NEXT:SOUND150,1:RETURN
530 MS$="INVALID INPUT":GOSUB520:SOUND10,1:FOR H=1 TO 300:NEXT:GOSUB540:RETURN
540 MS$="Play which card ?":GOSUB520:HPRINT(22,23),"I HOLD":HPRINT(29,23),CT:IF CT>1 THEN HPRINT(33,23),"CARDS" ELSE HPRINT(33,23),"CARD"
550 RETURN
560 MS$="What color ? ":GOSUB520:INPUT DC:RETURN
570 FOR QQ=1 TO TN:IF PD(QQ,1)=DC THEN OK=0:RETURN 
580 NEXT:RETURN
590 CC=15:GOSUB650:GOSUB600:DN=NC:DC=CC
600 CX=BP(PC,0):CY=BP(PC,1):NC=PD(PC,0):CC=PD(PC,1):RETURN
610 CX=BP(TN,0):CY=BP(TN,1):NC=PD(TN,0):CC=PD(TN,1):GOTO630
620 CX=BP(SP,0):CY=BP(SP,1)
630 IF NC>12 THEN CC=8
640 IF NC>9 AND NC<>15 THEN OS=4 ELSE OS=0
650 HCOLOR CC:HLINE(CX,CY)-(CX+25,CY+40),PSET,B:HPAINT(CX+1,CY+1),4,CC:HCIRCLE(CX+22,CY+22+OS),20,CC,1,.50,.75:HCIRCLE(CX+3,CY+(18-OS)),20,CC,1,0,.25:HLINE(CX+3,CY+14)-(CX+3,CY+36),PSET:HLINE(CX+22,CY+2)-(CX+22,CY+20),PSET:HPAINT(CX+1,CY+1),CC,CC
660 IF NC=14 THEN HLINE(CX+12,CY)-(CX+12,CY+40),PSET:HLINE(CX,CY+20)-(CX+25,CY+20),PSET:HPAINT(CX+14,CY+22),0,8:HPAINT(CX+10,CY+22),1,8:HPAINT(CX+10,CY+18),3,8:HPAINT(CX+14,CY+18),2,8
670 IF CC=1 THEN CD=8 ELSE CD=4
680 IF NC>9 THEN 720
690 HN$="BM"+STR$(CX+2)+","+STR$(CY+2)+"C"+STR$(CD)+"S4"+N$(NC):HDRAW HN$
700 LN$="BM"+STR$(CX+19)+","+STR$(CY+32)+"C"+STR$(CD)+N$(NC):HDRAW LN$
710 FOR K2=0 TO 1:FOR K=0 TO 1:MN$="BM"+STR$(CX+8+K)+","+STR$(CY+14+K2)+"C"+STR$(CC)+"S8"+N$(NC):HDRAW MN$:NEXT K,K2:RETURN
720 CD=4:HW$="BM"+STR$(CX+2)+","+STR$(CY+7)+"C"+STR$(CD)+"S4"
730 LW$="BM"+STR$(CX+2)+","+STR$(CY+39)+"C"+STR$(CD)+"S4"
740 IF NC=14 THEN M$="DRAW":GOSUB880:HW$=HW$+M1$:HDRAW HW$:M$="FOUR":GOSUB880:LW$=LW$+M1$:HDRAW LW$
750 IF NC=13 THEN M$="WILD":GOSUB880:HW$=HW$+M1$:HDRAW HW$:M$="CARD":GOSUB880:LW$=LW$+M1$:HDRAW LW$:WN=23:GOSUB800
760 IF NC=12 THEN M$="DRAW":GOSUB880:HW$=HW$+M1$:HDRAW HW$:M$="TWO":GOSUB880:LW$=LW$+M1$:HDRAW LW$:WN=4:GOSUB800
770 IF NC=10 THEN M$="SKIP":GOSUB880:HW$=HW$+M1$:HDRAW HW$:M$="TURN":GOSUB880:LW$=LW$+M1$:HDRAW LW$:WN=19:GOSUB800
780 IF NC=15 THEN M$="ONO":GOSUB880:FOR K2=0 TO 1:FOR K1=0 TO 1:MW$="BM"+STR$(CX+1+K1)+","+STR$(CY+25+K2)+"S6"+M1$:HDRAW MW$:NEXT K1,K2
790 RETURN
800 FOR K1=0 TO 1:FOR K2=0 TO 1:MW$="BM"+STR$(CX+7+K1)+","+STR$(CY+26+K2)+"C8S8"+W$(WN):HDRAW MW$:NEXTK2,K1:RETURN
810 N$(0)="BRGD4FR2EU4HNL2BR2":N$(1)="BR2NGD6NLRBU6BR2":N$(2)="BDER2FDG4R4BU6BR1":N$(3)="BDER2FDGNLFDGL2HBR5BU5":N$(4)="BD3NR4E3ND6BR2":N$(5)="NR4D3R3FDGL2HBR5BU5":N$(6)="BDNED4FR2EUHNL3BU3NL2NFBR2":N$(7)="BD6UE4UNL4BR"
820 N$(8)="BRGDFNR2GDFR2EUHEUHNL2BR2":N$(9)="BD5FR2EU4HL2GDFR3BU3BR1"
830 W$(0)="BR6":W$(1)="U5ER2FD2NL4D3BR2":W$(2)="U6R3FDGNL3FDGNL3BR3":W$(3)="BRNR2HU4ER2FBD4GBR3":W$(4)="U6R3FD4GNL3BR3":W$(5)="NR4U3NR3U3R4BD6BR2":W$(6)="U3NR3U3R4BD6BR2":W$(7)="BRNR2HU4ER2FBD3NL2DGBR3":W$(8)="U3NU3R4NU3D3BR2"
840 W$(9)="BRRU6NLRBD6LBR4"
850 W$(10)="BU2DFR2ENU5BDBR2":W$(11)="U3NU3RNE3F3BR2":W$(12)="NU6R4BR2":W$(13)="U6F2NDE2D6BR2":W$(14)="U5NUF4NU5DBR2":W$(15)="BRHU4ER2FD4GNL2BR3":W$(16)="U6R3FDGNL3BD3BR3":W$(17)="BRHU4ER2FD4NH2NFGNL2BR3":W$(18)="U6R3FDGLNL2F2DBR2"
860 W$(19)="BUFR2EUHL2HUER2FBD5BR2":W$(20)="BR2U6NL2R2BD6BR2":W$(21)="BUNU5FR2ENU5BDBR2":W$(22)="BU2NU4F2E2NU4BD2BR2":W$(23)="BUNU5FENU2FENU5BDBR2":W$(24)="UE4UBL4DF4DBR2":W$(25)="BU5NUF2ND3E2NUBD5BR2":W$(26)="BU6R4DG4DR4BR2"
870 RETURN
880 M1$="":FOR U=1 TO LEN(M$):M1=ASC(MID$(M$,U,1))-64:M1$=M1$+W$(M1):NEXT
890 IF NC=15 THEN M1$="C10"+M1$:RETURN
900 IF CC=1 THEN M1$="C8"+M1$ ELSE M1$="C4"+M1$
910 RETURN
920 POKE65496,0:END
930 PRINT"ERROR ";ERNO;" IN LINE ";ERLIN:GOTO920
940 IF CT=0 OR TN=0 OR CT>38 OR TN>38 THEN 1360
950 BI=-1:BN=-1:BC=-1:MS$="MY TURN.    I HOLD"+STR$(CT)+" CARD":IF CT>1 THEN MS$=MS$+"S"
960 GOSUB520
970 IF TN<=3 THEN GOTO1240
980 FOR K=1 TO CT
990 IF DN=14 AND CT>1 THEN BI=1:BN=CD(1,0):BC=CD(1,1):FOR K=2 TO CT:IF CD(K,0)>BN THEN BN=CD(K,0):BC=CD(K,1):BI=K:NEXT ELSE NEXT
1000 IF DN=14 AND CT>0 THEN BI=1:GOTO 1170
1010 IF CD(K,1)=DC AND CD(K,0)=12 THEN BI=K:GOTO1170 ELSE IF DN=12 AND CD(K,0)=12 THEN BI=K:GOTO1170
1020 IF CD(K,1)=DC AND CD(K,0)=10 THEN BI=K:GOTO1170 ELSE IF DN=10 AND CD(K,0)=10 THEN BI=K:GOTO1170
1030 NEXT K
1040 BN=CD(1,0):BC=CD(1,1):OK=0
1050 IF CT=1 THEN 1090
1060 FOR K=2 TO CT:IF CD(K,0)>BN AND CD(K,1)=DC THEN BN=CD(K,0):BC=CD(K,1):BI=K:OK=-1
1070 IF CD(K,0)>BN AND CD(K,0)=DN THEN BN=CD(K,0):BC=CD(K,1):BI=K:OK=-1
1080 NEXTK:IF OK THEN 1170
1090 FOR K=1 TO CT:IF CD(K,0)=DN OR CD(K,1)=DC THEN BI=K:GOTO1170
1100 NEXTK
1110 FOR K=1 TO CT:IF CD(K,0)=13 THEN GOSUB1570:GOTO1170
1120 NEXTK:OK=-1:FOR K=1 TO CT:IF CD(K,1)=DC AND CD(K,0)<>13 AND CD(K,0)<>14 THEN OK=0
1130 NEXTK:IF OK THEN FOR K=1 TO CT:IF CD(K,0)=14 THEN BI=K:GOTO1170 ELSE NEXT
1140 MS$="I'm taking a card from the pack":GOSUB520:CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1
1150 IF Q=109 THEN Q=1
1160 GOTO180
1170 SP=40:GOSUB290:GOSUB1300
1180 IF CD(BI,0)=14 THEN FOR EK=1 TO 4:GOSUB1330:NEXTEK:GOSUB1310:GOTO940
1190 IF CD(BI,0)=12 THEN FOR EK=1 TO 2:GOSUB1330:NEXTEK:GOSUB1310:GOTO940
1200 IF CD(BI,0)=10 THEN GOSUB1310:GOTO 940
1210 IF CD(BI,0)=13 THEN GOSUB420:GOSUB1310:GOTO180
1220 GOSUB1310
1230 GOTO180
1240 FOR K=1 TO CT:IF CD(K,0)=13 THEN1270
1250 IF CD(K,0)=14 THEN IP=K:GOTO1280
1260 NEXT:GOTO980
1270 GOSUB1570:GOTO 1170
1280 OK=-1:FOR K=1 TO CT:IF (CD(K,1)=DC AND (CD(K,0)<>13 OR CD(K,0)<>14)) THEN 980
1290 NEXT:BI=IP:GOTO1170
1300 DC=CD(BI,1):DN=CD(BI,0):NC=DN:CC=DC:SP=40:GOSUB620:RETURN
1310 IF BI<>CT THEN CD(BI,0)=CD(CT,0):CD(BI,1)=CD(CT,1)
1320 CT=CT-1:RETURN
1330 TN=TN+1:PD(TN,0)=D(Q,0):PD(TN,1)=D(Q,1):GOSUB610:Q=Q+1
1340 GOSUB1680
1350 RETURN
1360 IF CT=0 OR TN>38 THEN GOSUB1410
1370 IF XC>=500 THEN MS$="I WIN! :-) PLAY AGAIN? (Y/N)":GOSUB520:GOTO1520
1380 IF TN=0 OR CT>38 THEN GOSUB1470
1390 IF XP>=500 THEN MS$="YOU WIN! PLAY AGAIN? (Y/N)":GOSUB520:GOTO1520
1400 GOTO80
1410 RS=0:FOR Y1=1 TO TN
1420 IF PD(Y1,0)=14 OR PD(Y1,0)=13 THEN RS=RS+50:GOTO1450
1430 IF PD(Y1,0)=12 OR PD(Y1,0)=10 THEN RS=RS+20:GOTO1450
1440 RS=RS+PD(Y1,0)
1450 NEXT:XC=XC+RS:TS$=STR$(XC):MS$="I WIN! :-)":GOSUB1460:RETURN
1460 MS$=MS$+"Score"+STR$(RS)+"  Total score "+TS$:GOSUB520:SOUND100,1:SOUND200,1:SOUND100,1:FOR U=1 TO 1200:NEXT:RETURN
1470 RS=0:FOR Y1=1 TO CT
1480 IF CD(Y1,0)=14 OR CD(Y1,0)=13 THEN RS=RS+50:GOTO1510
1490 IF CD(Y1,0)=12 OR CD(Y1,0)=10 THEN RS=RS+20:GOTO1510
1500 RS=RS+CD(Y1,0)
1510 NEXT:XP=XP+RS:TS$=STR$(XP):MS$="YOU WIN! ":GOSUB1460:RETURN
1520 I$=INKEY$:IF I$=""THEN 1520 ELSE IF I$="Y" THEN RUN 20 ELSE IF I$="N" THEN 920 ELSE 1520
1530 PALETTE RGB:PALETTE15,56:PALETTE13,29:PALETTE10,28:FOR R=0 TO 3:PALETTE 5+R,R:NEXT:RETURN
1540 RETURN'ZC=5:FOR R=180 TO 170 STEP-5:FOR Z=R TO R-5 STEP-1:HCIRCLE(160,190),Z,ZC,1,.65,.85:HCIRCLE(160,190),Z+15,ZC,1,.65,.85:NEXTZ:ZC=ZC+1:NEXT
1550 FOR K1=1 TO 3:FOR K2=1 TO 5:MS$="BM"+STR$(70+K1)+","+STR$(30+K2)+"S16C0"+W$(18)+"C1"+W$(1)+"C2"+W$(9)+"C3"+W$(14)+"C0"+W$(2)+"C1"+W$(15)+"C2"+W$(23):HDRAW MS$:NEXT K2,K1:RETURN
1560 MS$="WANT TO KEEP PLAYING? (Y/N)":GOSUB520:GOTO1520
1570 H1=0:H2=0:H3=0:H4=0:FOR JK=1 TO CT:IF JK=K THEN 1590
1580 ON CD(JK,1)+1  GOSUB1640,1650,1660,1670
1590 NEXT:IF H1>=H2 AND H1>=H3 AND H1>=H4 THEN BI=K:IC=0:CD(K,1)=0:RETURN
1600 IF H2>=H1 AND H2>=H3 AND H2>=H4 THEN BI=K:IC=1:CD(K,1)=1:RETURN
1610 IF H3>=H1 AND H3>=H2 AND H3>=H4 THEN BI=K:IC=2:CD(K,1)=2:RETURN
1620 IF H4>=H1 AND H4>=H2 AND H4>=H3 THEN BI=K:IC=3:CD(K,1)=3:RETURN
1630 BI=K:IC=CD(K,1):RETURN
1640 H1=H1+1:RETURN
1650 H2=H2+1:RETURN
1660 H3=H3+1:RETURN
1670 H4=H4+1:RETURN
1680 IF Q=109 THEN Q=1:RETURN ELSE RETURN
1700 ** RGB Mods by Joe Sannucci ** 4/3/90 **
