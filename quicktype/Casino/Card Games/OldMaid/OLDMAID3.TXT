100 CLS:GOSUB5000:DIMC(52),PL(30),CM(26),CP(2):X=RND(-(PEEK(9)*256+PEEK(10)))
115 REM CHARACTERS IN CA$ AND SU$
120 CA$="A23456789TJQK":SU$="%&'(":REM PUT THE CARDS IN THE STRINGS
130 PRINT@32*15,"SHUFFLING...";:GOSUB1050:CD=1:CLS
140 FORX=1TO49:IFX/2=INT(X/2)THENPL(X/2)=C(X):GOTO160
150 CM(INT(X/2)+1)=C(X)
160 NEXTX:LC=24:CC=25:GOSUB2000
170 PRINT@32*9,"DO YOU WANT TO GO FIRST (y/n)?";:CS=3:R=9:C1=30:GOSUB1100:IFB$=""THEN170
180 IFB$="N"THEN400:REM COMPUTER GOES FIRST
190 PRINT@32*9,"":PRINT@32*11,"":CP(1)=0:CP(2)=0:REM 40 BLANKS FOR 2 ROWS 
200 PRINT@32*9,"FIRST CARD IS-":CS=1:R=9:C1=15:GOSUB1100:IFB$=""THEN370 
210 FORX=1TO13:IFMID$(CA$,X,1)=MID$(B$,1,1)THENCP(1)=X+(ASC(MID$(B$,2,1))-36)/10:GOTO230
220 NEXTX
230 PRINT@32*11,"    NEXT CARD-":CS=1:R=11:C1=15:GOSUB1100:IFB$=""THEN230
240 FORX=1TO13:IFMID$(CA$,X,1)=MID$(B$,1,1)THENCP(2)=X+(ASC(MID$(B$,2,1))-36)/10:GOTO260
250 NEXTX
260 IFINT(CP(1))<>INT(CP(2))ORABS(INT(CP(1)*10)-INT(CP(2)*10))<>2THEN340
270 FORX=1TOLC:IFCP(1)<>PL(X)THENNEXTX:CA=CP(1):GOTO320
280 PL(X)=0:GOSUB3020:FORX=1TOLC:IFCP(2)<>PL(X)THENNEXTX:CA=CP(2):GOTO320
290 PL(X)=0:GOSUB550
300 IFLC>0THENGOSUB2000:GOTO400:REM COMPUTER'S TURN
310 GOTO600:REM END OF GAME
320 GOSUB3000:GOSUB345:PRINT@32*15,"NOT FOUND ";:Z=1:X=5:Y=14:GOSUB3010:GOSUB1000
330 GOSUB4000:GOSUB345:GOTO190
340 GOSUB3000:GOSUB345:PRINT@32*15,"NOT A MATCH";:GOTO330
345 PRINT@32*15,"                               ";:RETURN
370 IFLC=30THEN200:REM MUST HAVE A PAIR
380 PRINT@32*9,"I HAVE";CC;"CARDS":PRINT@32*11,"PICK ONE- ":CS=4:R=11:C1=15:GOSUB1100:IFB$=""THEN380
385 B=VAL(B$):IFB>CCORB<1THEN380
390 LC=LC+1:PL(LC)=CM(B):CM(B)=CM(CC):CM(CC)=0:CC=CC-1:IFCC=0THEN600
395 GOSUB2000
400 FORQ=1TOCC-1:FORV=Q+1TOCC:IFINT(CM(Q))<>INT(CM(V))THENNEXTV:GOTO470:REM CARDS DON'T MATCH
410 IFABS(INT(CM(Q)*10)-INT(CM(V)*10))<>2THENNEXTV:GOTO470:REM SUITS DON'T MATCH
420 PRINT@32*9,"I HAVE A MATCH ";:X=6:Z=1:Y=14:CA=CM(Q):GOSUB1000:PRINT" AND ";
430 X=6:Y=15:CA=CM(V):GOSUB1000:PRINT:GOSUB3020:GOSUB3100:CM(Q)=0:CM(V)=0:IFCC=2THENCC=0:GOTO600
440 FORX=1TOCC-1:IFCM(X)=0THENFORY=X+1TOCC:CM(Y-1)=CM(Y):NEXTY:CC=CC-1:GOTO440
450 NEXTX:IFCM(X)=0THENCC=CC-1
460 GOSUB2000:GOTO190
470 NEXTQ
500 GOSUB3010:PRINT@32*9,"I MUST PICK FROM YOU.":C1=INT(RND(0)*LC)+1
510 PRINT@32*11,"I PICKED A ";:Z=1:X=4:Y=15:CA=PL(C1):GOSUB1000:GOSUB3100
520 CC=CC+1:CM(CC)=CA:PL(C1)=PL(LC):LC=LC-1:IFLC=0THEN600
530 GOTO460
550 IFLC=2ANDPL(1)=0ANDPL(2)=0THENLC=0:RETURN
560 FORX=1TOLC-1:IFPL(X)=0THENFORY=X+1TOLC:PL(Y-1)=PL(Y):NEXTY:LC=LC-1:GOTO560
570 NEXTX:IFPL(X)=0THENLC=LC-1
580 RETURN
600 GOSUB2030:PRINT@32*13,"THE WINNER IS-- ";
610 IFCC=0THENPRINT"THE COMPUTER";:GOTO630
620 PRINT"THE HUMAN";
630 GOSUB4000:RUN
1000 CV=INT(CA):S=INT(CA*10)-INT(CV*10):PRINTMID$(CA$,CV,1)MID$(" ¹¸ÝØ·²×Ò",INT(S)*2,2);:RETURN
1050 Q=0:FORV=0.1TO0.4STEP0.1:FORX=1TO13:C(X+Q)=X+V:NEXTX:Q=Q+13:NEXTV
1060 C(26)=C(50):REM GET RID OF ONE QUEEN & TWO KINGS
1070 FORX=1TO5:FORQ=49TO1STEP-1:V=INT(RND(0)*Q)+1:REM PICK A CARD
1080 C(0)=C(V):C(V)=C(Q):C(Q)=C(0):REM MOVE THE CARDS
1090 NEXTQ:NEXTX:RETURN:REM DO IT 5 TIMES
1100 B=1:B$=""
1110 K=ASC(INKEY$+"@"):IFK=13THEN1220
1120 IFK>127THENSOUND1,1:GOTO1110
1130 IFK=8THENB=1:B$="":PRINT@C1+B+32*R,"":CS=1:SOUND100,1:GOTO1110
1150 IFCS=1ANDB<3THENIF(K>49ANDK<58)ORK=65ORK=74ORK=75ORK=81ORK=84THEN1200
1160 IFCS=2ANDB<3THENIFK=67ORK=68ORK=72ORK=83THEN1230
1170 IFCS=3ANDB<3THENIFK=89ORK=78THENCS=0:GOTO1200
1180 IFCS=4ANDB<3THENIF(K>47ANDK<58)THEN1200
1190 GOTO1110
1200 PRINT@C1+B+32*R,;:PRINTCHR$(K);
1205 B$=LEFT$(B$,B-1)+CHR$(K)+MID$(B$,B+1):B=B+1:IFCS=1THENCS=2:B$=B$+" "
1210 IFCS<>0THEN1110
1220 RETURN
1230 IFK=67THENK=38:PRINTMID$(" ¹¸ÝØ·²×Ò",2*2,2);:REM CLUBS
1240 IFK=68THENK=39:PRINTMID$(" ¹¸ÝØ·²×Ò",3*2,2);:REM DIAMONDS
1250 IFK=72THENK=37:PRINTMID$(" ¹¸ÝØ·²×Ò",1*2,2);:REM HEARTS
1260 IFK=83THENK=40:PRINTMID$(" ¹¸ÝØ·²×Ò",4*2,2);:REM SPADES
1270 GOTO1205
2000 CLS:Y=1:Z=1:FORX=1TOLC:CA=PL(X):PRINT@(X-Z)*4+32*Y,;
2010 GOSUB1000:IFX/7=INT(X/7)THENY=Y+2:Z=Z+7
2020 NEXTX
2030 PRINT@448,"HUMAN";LC,"COMPUTER";CC; 
2040 RETURN
3000 SOUND196,8:FORTI=1TO50:NEXTTI:RETURN
3010 FORTI=1TO10:SOUNDTI,1:NEXTTI:RETURN
3020 FORTI=10TO200STEP10:SOUNDTI,1:NEXTTI:RETURN
3100 FORTI=1TO2000:NEXTTI:RETURN
4000 PRINT@502,"hit a key";
4005 IFINKEY$=""THEN4005
4010 RETURN
5000 PRINT@32*6+4,"old maid FOR ONE PLAYER"
5010 PRINT@32*7+9,"AND COMPUTER"
5020 PRINT@32*9+5,"BY L.M. SCHREIBER FOR"
5040 PRINT@32*10+6,"TAB BOOKS (C) 1984":RETURN
