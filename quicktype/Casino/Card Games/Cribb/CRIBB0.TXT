5 DIMA(219):CLS:Q=0:Z=RND(2)
10 PRINT@401,"CRIBBAGE - YOU VS COMPUTER
11 PRINT@586,"COPYRIGHT 1978 - TRS-80 SOFTWARE EXCHANGE
12 RANDOM
20 PRINT@782,"DO YOU NEED INSTRUCTIONS (Y OR N)?"
21 A$=INKEY$:IFA$=""GOTO21
22 IFA$="Y"CLS:GOSUB9000
25 IFA$<>"Y"PRINTTAB(22);"O.K. - SHUFFLING
100 GOTO1006
101 PRINT@X-64,"A";:RETURN
102 PRINT@X-64,"2";:RETURN
103 PRINT@X-64,"3";:RETURN
104 PRINT@X-64,"4";:RETURN
105 PRINT@X-64,"5";:RETURN
106 PRINT@X-64,"6";:RETURN
107 PRINT@X-64,"7";:RETURN
108 PRINT@X-64,"8";:RETURN
109 PRINT@X-64,"9";:RETURN
110 PRINT@X-64,"10";:RETURN
111 PRINT@X-64,"J";:RETURN
112 PRINT@X-64,"Q";:RETURN
113 PRINT@X-64,"K";:RETURN
1006 E=1:FORI=0TO104:A(I)=0:NEXTI
1009 Q=0:Z=Z+1:IFZ>2Z=1
1010 FORI=1TO13
1011 A=RND(52):FORB=1TOI:IFA=A(B)GOTO1011
1012 NEXTB:A(I)=A:NEXTI:P=2:C=6
1210 D=C:I=1:B=2
1211 IFA(I)>A(B)GOTO1220
1212 A(0)=A(I):A(I)=A(B):A(B)=A(0):B=B+1:IFB<C+1GOTO1211
1213 GOTO1230
1220 B=B+1:IFB>CGOTO1230
1221 GOTO1211
1230 I=I+1:B=I+1:IFI<CGOTO1211
1231 C=C+D:IFI<(P*D)GOTO1230
1300 CLS:GOSUB1301:GOTO1401
1301 PRINT@338,"THIS IS YOUR HAND  ";:IFZ=1PRINT"(IT IS YOUR KITTY)
1302 IFZ=2PRINT"(IT'S MY KITTY)
1303 PRINT@466,"#1 #2 #3 #4 #5 #6
1310 I=D:B=0:X=591
1330 X=X+3:I=I+1:B=B+1:IFB>DRETURN
1345 IFA(I)=0GOTO1330
1348 ONINT((A(I)-1)/13)GOTO1365,1370,1375
1360 PRINT@X,"CL";:GOTO1380
1365 PRINT@X,"DM";:GOTO1380
1370 PRINT@X,"HT";:GOTO1380
1375 PRINT@X,"SP";
1380 A=A(I)
1382 IFA>13A=A-13:GOTO1382
1385 ONAGOSUB102,103,104,105,106,107,108,109,110,111,112,113,101
1386 GOTO1330
1401 A(0)=0:PRINT@X+64,"FIX'IN MY HAND (30 SECS.)":GOTO2000
1500 CLS:PRINT@21,"PLAY REPORT
1501 PRINT"YOUR HAND","      CUT CARD"," ","DECK COUNT
1502 PRINT"    #1 #2 #3 #4":X=257:B=0:I=24:D=4:GOSUB1330
1503 B=0:D=1:I=12:X=215:GOSUB1330
1504 PRINT@180,V;:PRINT@325,"YOUR SCORE";A(14),"  COMPUTER SCORE";A(15)
1505 PRINT@599,"PLAYED CARDS
1506 B=0:I=60:D=C:X=724:GOSUB1330:RETURN
2000 RESTORE:P=0:T=0:M=0
2001 FORL=1TO15:READA,B,C,D,J,K
2003 A=A(A):B=A(B):C=A(C):D=A(D)
2005 GOSUB2100:GOSUB2090:NEXTL:GOTO2600
2090 G=P+T:T=0:P=0:IFG<MRETURN
2091 I=150:A(I+4)=A(J):A(I+5)=A(K)
2092 I=150:A(I)=A:A(I+1)=B:A(I+2)=C:A(I+3)=D:M=G:RETURN
2100 E=A:F=B:G=C:H=D:T=0:P=12/23
2115 I=E:GOSUB2150:E=I:I=F:GOSUB2150:F=I:I=G:GOSUB2150:G=I:I=H:GOSUB2150
2120 H=I:IFQ=1I=N:GOSUB2150:N=I
2125 GOTO2300
2150 IFI>13I=I-13:GOTO2150
2151 IFI=10P=P+12/46
2152 IFI<13I=I+1:RETURN
2153 I=1:RETURN
2270 FORI=201TO213:ONA(I)+1GOTO2272,2272,2273,2274,2275
2272 NEXTI:RETURN
2273 T=T+2:GOTO2272
2274 T=T+6:GOTO2272
2275 T=T+12:GOTO2272
2300 FORI=200TO214:A(I)=0:NEXTI
2301 A(E+200)=1:A(F+200)=A(F+200)+1:A(G+200)=A(G+200)+1
2302 A(H+200)=A(H+200)+1:IF(Q=1)+(Q=3)THENA(N+200)=A(N+200)+1
2303 A(214)=A(201):IFQ<2GOSUB2270
2304 IFQ>0GOTO2350
2305 FORI=201TO212:IFA(I)>0GOTO2310
2306 P=P+(4/46*A(I+1)*A(I+2)*3):REM
2307 IFI<212NEXTI
2308 GOTO2400
2310 IFA(I+1)>0GOTO2320
2311 P=P+(A(I)*4/46*A(I+2)*3):REM AWARDS POTENTIAL
2312 GOTO2307
2320 IFA(I+2)>0GOTO2330
2321 P=P+(A(I)*A(I+1)*4/46*3):REM AWARDS POTENTIAL
2322 GOTO2307
2330 T=T+(3*(A(I)+A(I+1)+A(I+2)-2)):IFA(I+3)>0GOTO2340
2331 IFA(I)+A(I+1)+A(I+2)=4GOTO2335
2332 P=P+(12/46*3):GOTO2307
2335 P=P+(10/46)
2336 GOTO2342
2340 T=T+1:P=P+9/46
2342 IFI>209GOTO2400
2343 P=P+12/46:GOTO2400
2350 FORI=201TO212:IFA(I)=0GOTO2360
2351 IFA(I+1)=0GOTO2360
2352 IFA(I+2)=0GOTO2360
2353 IFA(I+3)>0GOTO2357
2354 IFA(I)+A(I+1)+A(I+2)=3T=T+3:GOTO2400
2355 IFA(I)+A(I+1)+A(I+2)=4T=T+6:GOTO2400
2356 IFA(I)+A(I+1)+A(I+2)=5T=T+12:GOTO2400
2357 IFA(I)+A(I+1)+A(I+2)+A(I+3)=4T=T+4:GOTO2390
2358 T=T+8:GOTO2400
2360 NEXTI
2390 IFA(I+4)>0T=T+1
2400 IFQ=2RETURN
2410 I=E:GOSUB2420:E=I:I=F:GOSUB2420:F=I:I=G:GOSUB2420:G=I:I=H:GOSUB2420
2414 H=I:IFQ<>1GOTO2430
2416 I=N:GOSUB2420:N=I:GOTO2430
2420 IFI<11RETURN
2421 I=10:RETURN
2430 I=E+F:GOSUB2440:I=E+G:GOSUB2440:I=E+H:GOSUB2440:I=F+G:GOSUB2440:I=F+H
2431 GOSUB2440:I=G+H:GOSUB2440:I=E+F+G:GOSUB2440:I=E+F+H:GOSUB2440:I=F+G+H
2432 GOSUB2440:I=E+G+H:GOSUB2440:I=E+F+G+H:GOSUB2440
2433 I=E:GOSUB2440:I=F:GOSUB2440:I=G:GOSUB2440:I=H:GOSUB2440:GOTO2500
2440 IFI=15T=T+2
2441 IFI>15GOTO2446
2442 IFI<5GOTO2446
2443 IFI>5GOTO2445
2444 P=P+(16/46*2):GOTO2446
2445 P=P+(4/46*2):GOTO2446
2446 IFQ<>1RETURN
2447 IFU=1GOTO2449
2448 U=1:I=I+N:GOTO2440
2449 U=0:RETURN
2450 T=T+2:P=P-(2/26):RETURN
2500 ONINT((A-1)/13)GOTO2530,2540,2549
2520 E=0:F=14:GOTO2550
2530 E=12:F=27:GOTO2550
2540 E=26:F=40:GOTO2550
2549 E=39:F=53
2550 IF(B>E)*(B<F)*(C>E)*(C<F)*(D>E)*(D<F)GOTO2556
2551 RETURN
2556 T=T+4:P=P+9/46:IFQ<>1RETURN
2557 IF(N>E)*(N<F)T=T+1:RETURN
2558 RETURN
2600 PRINT:INPUT"WHICH CARDS DO YOU WISH TO PUT INTO THE KITTY (#,#)";A,B
2605 A(156)=A(A+6):A(157)=A(B+6)
2606 A(A+6)=0:A(B+6)=0
2607 FORA=1TO4:A(A)=A(A+149):NEXTA:REM COMPUTER AHND TO WORKING AREA
2608 B=5:FORA=7TO12:IFA(A)=0GOTO2610
2609 A(B)=A(A):B=B+1
2610 NEXTA
2612 B=9:FORA=154TO157:A(B)=A(A):B=B+1:NEXTA:REM MOVES KITTY
2615 Q=1
2616 CLS:GOSUB2805:PRINT@600," "
2620 A=A(1):B=A(2):C=A(3):D=A(4):N=A(13):GOSUB2100
2621 R=T
2625 A=A(5):B=A(6):C=A(7):D=A(8):N=A(13):GOSUB2100
2626 S=T
2631 A=A(9):B=A(10):C=A(11):D=A(12):N=A(13):GOSUB2100
2632 W=T
2633 FORI=1TO12:X=A(I)
2634 IFX>13X=X-13:GOTO2634
2635 IFX=10IFINT(A(I)/13)=INT((A(13)-1)/13)GOTO2637
2636 NEXTI:GOTO2640
2637 IFI<5R=R+1:GOTO2640
2638 IFI<9S=S+1:GOTO2640
2639 W=W+1
2640 FORA=1TO8:I=A(A):A(A+20)=I:NEXTA
2642 FORA=1TO8:I=A(A):GOSUB2150:A(A+30)=I:NEXTA:REM GENERIC VAL
2643 FORA=31TO38:I=A(A):GOSUB2420:A(A+10)=I:NEXTA:REM NUMERIC VAL
2645 C=0:M=0:FORI=50TO70:A(I)=0:NEXTI
2646 V=0:L=0:Y=0:K=0:J=0
2647 A(49)=0:A(50)=0
2648 GOSUB2980
2650 ONZGOTO2700,2660
2660 PRINT@778,;:INPUT"WHICH CARD DO YOU PLAY(0=GO)";A:IF(A>4)+(A<0)GOTO2660
2662 IFA=0GOTO2680
2663 A=A+44:IFA(A)+V>31GOTO2679
2664 IFA(A)=0GOTO2678
2665 T=0:V=V+A(A):IF(V=15)+(V=31)THENT=T+2
2667 GOSUB2685
2668 A=A-10:IFA(A)=A(C+50)T=T+2:IFA(A)=A(C+49)T=T+4:IFA(A)=A(C+48)T=T+6
2669 A=A+10:IFT>0GOSUB2915
2670 C=C+1:A(C+50)=A(A-10):A(C+60)=A(A-20):A(A)=0:A(A-10)=0:A(A-20)=0
2671 K=K+1:IF(K>3)*(L>3)GOTO2674
2672 IFV=31GOTO2900
2673 J=0:GOTO2700
2674 IFV<31PRINT"PEG GO":GOSUB2770:GOTO3000
2675 PRINT"PEG 2":GOSUB2770:GOSUB2915:GOTO3000
2678 PRINT"THAT CARD HAS BEEN PLAYED ALREADY - TRY AGAIN":GOTO2660
2679 PRINT"PLAY DECK WOULD EXCEED 31, TRY AGAIN":GOTO2660
2680 FORI=45TO48:IF(A(I)<>0)*(A(I)+V<32)PRINT"YOU HAVE A VALID PLAY":GOTO2660
2681 NEXTI:IFJ=0J=1:GOTO2700
2682 IFV=31T=2:GOSUB2915:J=0:GOTO2900
2683 T=1:GOSUB2915:J=0:GOTO2900
2685 IFC=1RETURN
2686 O=T:T=0:Q=2:E=A(A-10):F=A(C+50):G=A(C+49):H=0:GOSUB2300
2687 IF(T>0)*(C>2)O=O+T:T=0:H=A(C+48):GOSUB2300:GOTO2689
2688 T=O+T:RETURN
2689 IF(T>3)*(C>3)O=O+1:T=0:Q=3:N=A(C+47):GOSUB2300:IFT>4O=O+1
2690 IFT>0PRINT"SCORES";T;"ON STRAIGHT"
2691 T=O:RETURN
2700 FORI=71TO74:A(I)=0:NEXTI
2705 FORI=41TO44:IF(A(I)=0)+(A(I-10)+V>31)A(I+30)=-10:NEXTI:GOTO2730
2706 IFA(I)+V=15THENA(I+30)=2
2707 IFA(I)+V=31THENA(I+30)=2.5
2708 IFA(I)+V=5THENA(I+30)=-1
2709 IFA(I)+V=10THENA(I+30)=-.3
2710 IFA(I)+V<5THENA(I+30)=.2
2711 IFA(I)+V>15THENA(I+30)=.2
2712 IFA(I)+V=21THENA(I+30)=-1
2715 A=I-10:O=I+30:IFA(A)=A(C+50)A(O)=A(O)+2:IFA(A)=A(C+49)A(O)=A(O)+4
2716 IFV+2*A(I)>31THENA(O)=A(O)+.2
2717 IFC>1B=I:E=A(A):F=A(C+50):G=A(C+49):H=A(C+48):N=A(C+47):T=0
2718 IFC>1Q=2:GOSUB2300:I=B:IFT>0THENA(O)=A(O)+T
2719 IF((A(A)=A(C+50)+1)+(A(A)=A(C+50)-1))*((2*A(A))+V<32)A(O)=A(O)-.5
2720 FORB=31TO34:IFB=I-10NEXTB
2721 IF(A(B)=A(I-10))*(V+2*A(I)<32)THENA(I+30)=A(I+30)+.3
2728 NEXTI
2730 A=-10:FORI=71TO74:IFA(I)>ATHENN1=I:A=A(I)
2731 NEXTI:IFA=-10GOTO2760
2732 A=N1-30:N1=N1-40:V=V+A(A)
2735 IFV=15PRINT"FIFTEEN FOR TWO":T=2:GOSUB2910
2736 IFV=31PRINT"THIRTY-ONE FOR TWO":T=2:GOSUB2910
2740 T=0:GOSUB2685:IFT>0GOSUB2910
2741 T=0
2742 IFA(N1)=A(C+50)T=T+2:IFA(N1)=A(C+49)T=T+4:IFA(N1)=A(C+48)T=T+6
2745 IFT=2PRINT"PAIR FOR TWO":GOSUB2910:GOSUB2770
2746 IFT=6PRINT"TRIPS FOR SIX":GOSUB2910:GOSUB2770
2747 IFT=12PRINT"QUADS FOR TWELVE":GOSUB2910:GOSUB2770
2750 C=C+1:A(C+50)=A(N1):A(C+60)=A(N1-10):A(N1)=0:A(A)=0:A(A-20)=0
2751 J=0:L=L+1:IF(L>3)*(K>3)GOTO3002
2752 CLS:IFV=31GOSUB2901
2753 GOSUB1500:PRINT@768," ":J=0:GOTO2660
2760 IFJ=1THENJ=0:GOTO2762
2761 J=1:GOSUB1500:PRINT@832,"COMPUTER GO":GOSUB2770:GOTO2660
2762 IFV=31GOTO2901
2763 T=1:GOSUB2910:GOTO2901
2770 FORO=1TO500:NEXTO:RETURN
2805 PRINT@320,"PLAYER HAND   #1 #2 #3 #4          CUT CARD"
2806 X=459:B=0:D=4:I=4:GOSUB1330:X=482:B=0:D=1:I=12:GOSUB1330:RETURN
2900 FORI=51TO50+C:A(I)=0:A(I+10)=0:NEXTI:C=0:V=0:CLS:GOTO2905
2901 FORI=51TO50+C:A(I)=0:A(I+10)=0:NEXTI:C=0:V=0:CLS
2902 PRINT@530,"NEW PLAY DECK":FORI=1TO999:NEXTI:GOSUB1500:PRINT@900," ":GOTO2660
2905 PRINT@530,"NEW PLAY DECK":FORI=1TO1000:NEXTI:GOTO2700
2910 A(15)=A(15)+T:IFA(15)>120GOTO2950
2912 PRINT"COMPUTER SCORES";T:FORI=1TO1000:NEXTI:RETURN
2915 A(14)=A(14)+T:IFA(14)>120GOTO2960
2916 PRINT"PLAYER SCORES  ";T:FORI=1TO1000:NEXTI:RETURN
2950 PRINT@532,"I WIN!!!!!!!!!!!!":PRINT@596,"I FEEL SO WOUNDERFULLLL!!
2955 PRINT:INPUT"SHALL WE GO AT IT AGAIN (1=YES)";A:IFA=1GOTO1006
2956 END
2960 CLS:PRINT@532,"PLAYED THIS GAME BEFORE? - YOU WIN!!":GOTO2955
2980 X=A(13):REM CHECK FOR CUT JACK
2981 IFX>13X=X-13:GOTO2981
2982 IFX<>10RETURN
2983 IFZ=1PRINT"PLAYER CUTS JACK":T=2:GOSUB2915:RETURN
2984 PRINT"COMPUTER CUTS JACK":T=2:GOSUB2910:RETURN
3000 IFV=31A(14)=A(14)+2:GOTO3005
3001 A(14)=A(14)+1:GOTO3005
3002 IFV=31A(15)=A(15)+2:GOTO3005
3003 A(15)=A(15)+1
3005 GOSUB1505:FORI=1TO3000:NEXTI:CLS:ONZGOTO3020,3030
3010 FORI=1TO13:A(I)=RND(52):GOSUB2805:GOTO2600
3020 PRINT@0,"COMPUTER SCORE"," "," ","PLAYER SCORES":PRINT@69,A(15),
3021 PRINT@86,"CURRENT SCORES",:PRINT@117,A(14):A(15)=A(15)+R
3022 PRINT@133,R:PRINT@152,"MELD SCORE
3023 PRINT@181,S:PRINT@216,"KITTY SCORE",:PRINT@245,W
3024 A(14)=A(14)+S+W:PRINT@261,A(15),:PRINT@280,"NEW SCORES",
3025 PRINT@309,A(14)
3026 GOTO3036
3030 PRINT@0,"PLAYER SCORE"," "," ","COMPUTER SCORE":PRINT@69,A(14),
3031 PRINT@86,"CURRENT SCORES",:PRINT@117,A(15):A(14)=A(14)+S
3032 PRINT@133,S:PRINT@152,"MELD SCORE
3033 PRINT@181,R:PRINT@216,"KITTY SCORE",:PRINT@245,W
3034 A(15)=A(15)+R+W:PRINT@261,A(14),:PRINT@280,"NEW SCORES",
3035 PRINT@309,A(15)
3036 PRINT:PRINT:PRINT"     YOUR HAND"," COMPUTER HAND","  KITTY HAND","CUT CARD
3037 X=577:D=4:I=4:B=0:GOSUB1330:X=591:D=4:I=0:B=0:GOSUB1330
3038 X=607:D=4:I=8:B=0:GOSUB1330:X=624:D=1:I=12:B=0:GOSUB1330:PRINT@847,"";
3039 GOSUB10000:IF((A(14)>120)*(Z=2))+((A(14)>120)*(A(15)<121))GOTO2960
3040 IF((A(15)>120)*(Z=1))+((A(15)>120)*(A(14)<121))GOTO2950
3041 GOTO1009
9000 PRINT"CRIBBAGE DIRECTIONS
9001 PRINT"   HOPEFULLY YOU HAVE PLAYED CRIBBAGE SOME TIME BEFORE.
9002 PRINT"IF NOT, THESE DIRECTION MAY PROVE INADEQUATE. IF SO, HOYLE'S
9003 PRINT"BOOK OF CARD GAMES SHOULD BE OF GREATER ASSISTANCE.
9004 PRINT"    IN A NUTSHELL, CRIBBAGE JUST HAPPENS TO BE ONE OF THE BEST.
9005 PRINT"BEST TWO PLAYER CARD GAMES GOING (ALTHOUGH IT CAN BE PLAYED
9006 PRINT"BY MORE). TO START WITH, EACH PLAYER IS DEALT SIX CARDS
9007 PRINT"TO LOOK OVER. THE PLAYERS MUST THEN SELECT WHICH FOUR THEY
9008 PRINT"INTEND TO KEEP. THE OTHER TWO ARE PLACED IN THE KITTY.
9009 PRINT"THE CARDS PLACED IN THE KITTY WILL NOT BE PART OF THE 
9010 PRINT"PLAY PHASE OF THE GAME, AND THE PLAYERS ALTERNATE 
9011 PRINT"FOR POSSESSION OF THE 'KITTY' FOR THE MELD PHASE OF EACH
9012 PRINT"HAND.":PRINT"":PRINT"":GOSUB10000
9020 PRINT"   THE BIGGEST CONSIDERATION IS TO SELECT THOSE CARDS WHICH
9021 PRINT"PRESERVE THE MAXIMUM MELD COMBINATIONS. THE FOLLOWING IS
9022 PRINT"A LIST OF THE VARIOUS MELD COMBINATIONS - REMEMBER, ANY
9023 PRINT"DIFFERING COMBINATION COMPOUNDS THE SCORING (EX, 2 5'S & 2 10'S
9024 PRINT"PRODUCE 4 DIFFERENT 15 COMBINATIONS & TWO PAIRS FOR 12 PTS).
9025 PRINT"FIFTEEN'S",2,"FACE CARDS = 10 & ACE = 1
9026 PRINT"PAIRS",2;"3 OF KIND = 3 PAIRS
9027 PRINT"STRAIGHTS",3;"MINIMUM OF 3 CARDS ANY SUIT
9028 PRINT"FLUSH",4;"ALL FOUR CARDS IN HAND SAME SUIT
9029 PRINT"JACK",1;"IF IN SAME SUIT AS CUT CARD
9030 PRINT"REMEMBER, CARDS MAY BE USED AND REUSED IN ANY COMBINATION
9031 PRINT"AS LONG AS THE EXACT SAME CARDS ARE NOT BEING USED OVER.
9032 PRINT"EX. 6,7,8,9 OF HEARTS = STRAIGHTS 6-7-8-9 FIFTEENS 7-8,6-9
9033 PRINT"FLUSH 6-7-8-9  TOTAL PTS 12":GOSUB10000
9040 PRINT"   AFTER THE PLAYERS HAVE MADE THEIR SELECTIONS AND PRODUCED
9041 PRINT"THE 'KITTY', THE DECK IS CUT FOR THE COMMON CARD (CUT CARD).
9042 PRINT"THIS CARD IS INCLUDED AS PART OF EACH HAND DURING THE MELD
9043 PRINT"PHASE OF THE GAME. IF THE CUT CARD IS A JACK, THE DEALER
9044 PRINT"(WHOEVER HAS THE KITTY) IS AWARDED 2 PTS IMMEDIATELY.
9045 PRINT"   AFTER THE CUT CARD IS PRODUCED, THE PLAY PHASE BEGINS WITH
9046 PRINT"THE PLAYER WHO DOES NOT HAVE THE KITTY PLAYING THE FIRST CARD.
9047 PRINT"   ALL COMBINATIONS THAT APPLY DURING THE MELD PHASE CAN
9048 PRINT"APPLY DURING THE PLAY PHASE, THE PRIME DIFFERENCE BEING
9049 PRINT"A GREATER ATTENTION TO THE ORDER IN WHICH THEY WERE PLAYED.
9050 PRINT"FOR INSTANCE, TWO POINTS ARE AWARDED TO THE PLAYER WHO PLACES 
9051 PRINT"THE SECOND CARD OF A PAIR, HOWEVER, THEY MUST BE CONSECUTIVE
9052 PRINT"(LAST TWO CAARDS PLAYED). THE SAME IS TRUE FOR
9053 PRINT"STRAIGHTS,  ETC.":GOSUB10000
9060 PRINT"   THIS PLAY OCCURS WHILE A COUNT IS BEING MADE OF THEIR COUNT
9061 PRINT"VALUE. THIS ASPECT OF CRIBBAGE IS SIMILAR TO BLACK JACK. THE
9062 PRINT"COUNT VALUES OF EACH CARD IS IDENTICAL TO BLACK JACK EXCEPT
9063 PRINT"THAT THE ACE IS ONLY ONE. AS THE PLAYERS ALTERNATE IN PLAYING
9064 PRINT"THEIR CARDS, THE COMBINED 'BLACK JACK' VALUE IS THE DECK
9065 PRINT"COUNT. THE MAXIMUM VALUE THE DECK MAY REACH IS 31. WHEN 
9066 PRINT"THE PLAYER WHOSE TURN IT IS TO PLAY A CARD DOES NOT HAVE A
9067 PRINT"CARD LEFT THAT WOULD KEEP THE DECK VALUE UNDER 32, HE GIVES A
9068 PRINT"'GO'. THE OTHER PLAYER MUST THEN ATTEMPT TO ADD TO THE DECK
9069 PRINT"VALUE. IF HE CANNOT PLAY A CARD AND KEEP THE DECK VALUE UNDER
9070 PRINT"32, THEN THE LAST PLAYER TO PLAY A CARD IS IMMEDIATELY AWARDED
9071 PRINT"1PT. (2 IF THE DECK VALUE IS EXACTLY 31). THE DECK COUNT THEN
9072 PRINT"CYCLES BACK TO ZERO AND THE PLAYER WHO GAVE THE 'GO' PLACES
9073 PRINT"THE FIRST CARD OF THE NEW DECK.":GOSUB10000
9080 PRINT"   DURING THE PLAY, A PLAYER WHO PLAYS THE CARD THAT BRINGS THE
9081 PRINT"DECK COUNT TO EXACTLY 15 IS IMMEDIATELY AWARDED 2 POINTS.
9082 PRINT"   THE OBJECT OF THE GAME IS TO BE THE FIRST PLAYER TO GET A
9083 PRINT"SCORE OVER 120 PTS. REMEMBER, IT IS THE FIRST PLAYER TO REACH
9084 PRINT"A SCORE OF OVER 120, NOT THE ONE WHO PLAYS AFTER THE MELD HAS
9085 PRINT"BEEN ADDED, THAT WINS. THERE IS A SPECIFIC SEQUENCE TO
9086 PRINT"CREDIT POINTS. FIRST, THE CUT JACK IS AWARDED, THEN THE PLAY
9088 PRINT"POINTS ARE ADDED IMMEDIATELY AS THEY OCCUR, AND THEN THE MELD
9089 PRINT"POINT ARE ADDED, FIRST TO THE PLAYER WHO DOES NOT CONTROL THE
9090 PRINT"KITTY, THEN THE MELDS OF THE DEALER HAND AND KITTY HAND.
9091 PRINT"   IF THE NON KITTY PLAYER GETS OVER 120 FIRST, THE GAME ENDS
9092 PRINT"IMMEDIATELY. EVEN IF THE OTHER PLAYER WOULD HAVE HAD A HIGHER
9093 PRINT"SCORE IF THE GAME CONTINUED.":GOSUB10000
9100 CLS:PRINT@530,"GOOD LUCK!!!!!!!!":FORI=1TO1000:NEXTI:RETURN
10000 PRINT@916,"";:INPUT"PRESS ENTER";A$:CLS:RETURN
30000 DATA1,2,3,4,5,6,1,2,3,5,4,6,1,2,3,6,4,5,1,2,4,5,3,6,1,2,4,6,3,5
30010 DATA1,3,4,5,2,6,1,3,4,6,2,5,1,4,5,6,2,3,1,2,5,6,3,4,1,3,5,6,2,4
30020 DATA2,3,4,5,1,6,2,3,4,6,1,5,2,3,5,6,1,4,2,4,5,6,1,3,3,4,5,6,1,2

