10 DIMA(219):CLS:Q=0:Z=RND(2)
20 PRINT@530,"CRIBBAGE - YOU VS COMPUTER
30 RANDOM
40 INPUT"DO YOU NEED INSTRUCTIONS (Y OR N)";A$:IF A$="Y"GOSUB2820
50 GOTO200
60 PRINT@X-64,"A";:RETURN
70 PRINT@X-64,"2";:RETURN
80 PRINT@X-64,"3";:RETURN
90 PRINT@X-64,"4";:RETURN
100 PRINT@X-64,"5";:RETURN
110 PRINT@X-64,"6";:RETURN
120 PRINT@X-64,"7";:RETURN
130 PRINT@X-64,"8";:RETURN
140 PRINT@X-64,"9";:RETURN
150 PRINT@X-64,"10";:RETURN
160 PRINT@X-64,"J";:RETURN
170 PRINT@X-64,"Q";:RETURN
180 PRINT@X-64,"K";:RETURN
190 REM GO
200 E=1:FORI=0TO104:A(I)=0:NEXTI
210 Q=0:Z=Z+1:IFZ>2Z=1
220 FORI=1TO13
230 A=RND(52):FORB=1TOI:IFA=A(B)GOTO230
240 NEXTB:A(I)=A:NEXTI:P=2:C=6
250 D=C:I=1:B=2
260 IFA(I)>A(B)GOTO290
270 A(0)=A(I):A(I)=A(B):A(B)=A(0):B=B+1:IFB<C+1GOTO260
280 GOTO310
290 B=B+1:IFB>CGOTO310
300 GOTO260
310 I=I+1:B=I+1:IFI<CGOTO260
320 C=C+D:IFI<(P*D)GOTO310
330 CLS:GOSUB340:GOTO490
340 PRINT@338,"THIS IS YOUR HAND  ";:IFZ=1PRINT"(IT IS YOUR CRIB)
350 IFZ=2PRINT"IT'S MY CRIB
360 PRINT@525,"#1 #2 #3 #4 #5 #6
370 I=D:B=0:X=650
380 X=X+3:I=I+1:B=B+1:IFB>DRETURN
390 IFA(I)=0GOTO380
400 ONINT((A(I)-1)/13)GOTO420,430,440
410 PRINT@X,"CL";:GOTO450
420 PRINT@X,"DM";:GOTO450
430 PRINT@X,"HT";:GOTO450
440 PRINT@X,"SP";
450 A=A(I)
460 IFA>13A=A-13:GOTO460
470 ONAGOSUB70,80,90,100,110,120,130,140,150,160,170,180,60
480 GOTO380
490 A(0)=0:PRINT@X+64,"FIXIN' MY HAND":GOTO570
500 CLS:PRINT@20,"PLAY REPORT
510 PRINT"YOUR HAND","      CUT CARD"," ","DECK COUNT
520 PRINT"   #1 #2 #3 #4":X=257:B=0:I=24:D=4:GOSUB380
530 B=0:D=1:I=12:X=215:GOSUB380
540 PRINT@180,V;:PRINT@325,"YOUR SCORE";A(14),"  COMPUTER SCORE";A(15)
550 PRINT@599,"PLAYED CARDS
560 B=0:I=60:D=C:X=724:GOSUB380:RETURN
570 RESTORE:P=0:T=0:M=0
580 FORL=1TO15:READA,B,C,D,J,K
590 A=A(A):B=A(B):C=A(C):D=A(D)
600 GOSUB640:GOSUB610:NEXTL:GOTO1420
610 G=P+T:T=0:P=0:IFG<MRETURN
620 I=150:A(I+4)=A(J):A(I+5)=A(K)
630 I=150:A(I)=A:A(I+1)=B:A(I+2)=C:A(I+3)=D:M=G:RETURN
640 E=A:F=B:G=C:H=D:T=0:P=12/23
650 I=E:GOSUB680:E=I:I=F:GOSUB680:F=I:I=G:GOSUB680:G=I:I=H:GOSUB680
660 H=I:IFQ=1I=N:GOSUB680:N=I
670 GOTO770
680 IFI>13I=I-13:GOTO680
690 IFI=10P=P+12/46
700 IFI<13I=I+1:RETURN
710 I=1:RETURN
720 FORI=201TO213:ONA(I)+1GOTO730,730,740,750,760
730 NEXTI:RETURN
740 T=T+2:GOTO730
750 T=T+6:GOTO730
760 T=T+12:GOTO730
770 FORI=200TO214:A(I)=0:NEXTI
780 A(E+200)=1:A(F+200)=A(F+200)+1:A(G+200)=A(G+200)+1
790 A(H+200)=A(H+200)+1:IF(Q=1)+(Q=3)THENA(N+200)=A(N+200)+1
800 IFQ<2GOSUB720
810 IFQ>0GOTO1000
820 FORI=201TO212:IFA(I)>0GOTO860
830 P=P+(4/46*A(I+1)*A(I+2)*3):REM
840 IFI<212NEXTI
850 GOTO1110
860 IFA(I+1)>0GOTO890
870 P=P+(A(I)*4/46*A(I+2)*3):REM AWARDS POTENTIAL
880 GOTO840
890 IFA(I+2)>0GOTO920
900 P=P+(A(I)*A(I+1)*4/46*3):REM AWARDS POTENTIAL
910 GOTO840
920 T=T+(3*(A(I)+A(I+1)+A(I+2)-2)):IFA(I+3)>0GOTO970
930 IFA(I)+A(I+1)+A(I+2)=4GOTO950
940 P=P+(12/46*3):GOTO840
950 P=P+(10/46)
960 GOTO980
970 T=T+1:P=P+9/46
980 IFI>209GOTO1110
990 P=P+12/46:GOTO1110
1000 FORI=201TO212:IFA(I)=0GOTO1090
1010 IFA(I+1)=0GOTO1090
1020 IFA(I+2)=0GOTO1090
1030 IFA(I+3)>0GOTO1070
1040 IFA(I)+A(I+1)+A(I+2)=3T=T+3:GOTO1110
1050 IFA(I)+A(I+1)+A(I+2)=4T=T+6:GOTO1110
1060 IFA(I)+A(I+1)+A(I+2)=5T=T+12:GOTO1110
1070 IFA(I)+A(I+1)+A(I+2)+A(I+3)=4T=T+4:GOTO1100
1080 T=T+8:GOTO1110
1090 NEXTI
1100 IFA(I+4)>0T=T+1
1110 IFQ=2RETURN
1120 I=E:GOSUB1150:E=I:I=F:GOSUB1150:F=I:I=G:GOSUB1150:G=I:I=H:GOSUB1150
1130 H=I:IFQ<>1GOTO1170
1140 I=N:GOSUB1150:N=I:GOTO1170
1150 IFI<11RETURN
1160 I=10:RETURN
1170 I=E+F:GOSUB1210:I=E+G:GOSUB1210:I=E+H:GOSUB1210:I=F+G:GOSUB1210:I=F+H
1180 GOSUB1210:I=G+H:GOSUB1210:I=E+F+G:GOSUB1210:I=E+F+H:GOSUB1210:I=F+G+H
1190 GOSUB1210:I=E+G+H:GOSUB1210:I=E+F+G+H:GOSUB1210
1200 I=E:GOSUB1210:I=F:GOSUB1210:I=G:GOSUB1210:I=H:GOSUB1210:GOTO1320
1210 IFI=15T=T+2
1220 IFI>15GOTO1270
1230 IFI<5GOTO1270
1240 IFI>5GOTO1260
1250 P=P+(16/46*2):GOTO1270
1260 P=P+(4/46*2):GOTO1270
1270 IFQ<>1RETURN
1280 IFU=1GOTO1300
1290 U=1:I=I+N:GOTO1210
1300 U=0:RETURN
1310 T=T+2:P=P-(2/26):RETURN
1320 ONINT((A-1)/13)GOTO1340,1350,1360
1330 E=0:F=14:GOTO1370
1340 E=13:F=27:GOTO1370
1350 E=26:F=40:GOTO1370
1360 E=39:F=53
1370 IF(B>E)*(B<F)*(C>E)*(C<F)*(D>E)*(D<F)GOTO1390
1380 RETURN
1390 T=T+4:P=P+9/46:IFQ<>1RETURN
1400 IF(N>E)*(N<F)T=T+1:RETURN
1410 RETURN
1420 PRINT:INPUT"WHICH CARDS DO YOU WISH TO PUT INTO THE CRIB (#,#)";A,B
1430 A(156)=A(A+6):A(157)=A(B+6)
1440 A(A+6)=0:A(B+6)=0
1450 FORA=1TO4:A(A)=A(A+149):NEXTA:REM COMPUTER HAND TO WORKING AREA
1460 B=5:FORA=7TO12:IFA(A)=0GOTO1480
1470 A(B)=A(A):B=B+1
1480 NEXTA
1490 B=9:FORA=154TO157:A(B)=A(A):B=B+1:NEXTA:REM MOVES CRIB
1500 Q=1
1510 CLS:GOSUB2380:PRINT@600," "
1520 A=A(1):B=A(2):C=A(3):D=A(4):N=A(13):GOSUB640
1530 R=T
1540 A=A(5):B=A(6):C=A(7):D=A(8):N=A(13):GOSUB640
1550 S=T
1560 A=A(9):B=A(10):C=A(11):D=A(12):N=A(13):GOSUB640
1570 W=T
1580 FORI=1TO12:X=A(I)
1590 IFX>13X=X-13:GOTO1590
1600 IFX=10IFINT(A(I)/13)=INT((A(13)-1)/13)GOTO1620
1610 NEXTI:GOTO1650
1620 IFI<5R=R+1:GOTO1650
1630 IFI<9S=S+1:GOTO1650
1640 W=W+1
1650 FORA=1TO8:I=A(A):A(A+20)=I:NEXTA
1660 FORA=1TO8:I=A(A):GOSUB680:A(A+30)=I:NEXTA:REM GENERIC VAL
1670 FORA=31TO38:I=A(A):GOSUB1150:A(A+10)=I:NEXTA:REM NUMERIC VAL
1680 C=0:M=0:FORI=50TO70:A(I)=0:NEXTI
1690 V=0:L=0:Y=0:K=0:J=0
1700 A(49)=0:A(50)=0
1710 GOSUB2520
1720 ONZGOTO2010,1730
1730 PRINT@778,;:INPUT"WHICH CARD DO YOU PLAY (0=GO)";A:IF(A>4)+(A<0)GOTO1730
1740 IF(A>4)+(A<0)GOTO1730
1750 IFA=0GOTO1900
1760 A=A+44:IFA(A)+V>31GOTO1890
1770 IFA(A)=0GOTO1880
1780 T=0:V=V+A(A):IF(V=15)+(V=31)THENT=T+2
1790 GOSUB1940
1800 A=A-10:IFA(A)=A(C+50)T=T+2:IFA(A)=A(C+49)T=T+4:IFA(A)=A(C+48)T=T+6
1810 A=A+10:IFT>0GOSUB2460
1820 C=C+1:A(C+50)=A(A-10):A(C+60)=A(A-20):A(A)=0:A(A-10)=0:A(A-20)=0
1830 K=K+1:IF(K>3)*(L>3)GOTO1860
1840 IFV=31GOTO2400
1850 J=0:GOTO2010
1860 IFV<31PRINT"PEG GO":GOSUB2370:GOTO2570
1870 PRINT"PEG 2":GOSUB2370:GOSUB2460:GOTO2570
1880 PRINT"THAT CARD HAS BEEN PLAYED ALREADY - TRY AGAIN":GOTO1730
1890 PRINT"PLAY DECK WOULD EXCEED 31, TRY AGAIN":GOTO1730
1900 FORI=45TO48:IF(A(I)<>0)*(A(I)+V<32)PRINT"YOU HAVE A VALID PLAY":GOTO1730
1910 NEXTI:IFJ=0J=1:GOTO2010
1920 IFV=31T=2:GOSUB2460:J=0:GOTO2400
1930 T=1:GOSUB2460:J=0:GOTO2400
1940 IFC=1RETURN
1950 O=T:T=0:Q=2:E=A(A-10):F=A(C+50):G=A(C+49):H=0:GOSUB770
1960 IF(T>0)*(C>2)O=O+T:T=0:H=A(C+48):GOSUB770:GOTO1980
1970 T=O+T:RETURN
1980 IF(T>3)*(C>3)O=O+1:T=0:Q=3:N=A(C+47):GOSUB770:IFT>4O=O+1
1990 IFT>0PRINT"SCORES";T;"ON STRAIGHT"
2000 T=O:RETURN
2010 FORI=71TO74:A(I)=0:NEXTI
2020 FORI=41TO44:IF(A(I)=0)+(A(I-10)+V>31)A(I+30)=-10:NEXTI:GOTO2180
2030 IFA(I)+V=15THENA(I+30)=2
2040 IFA(I)+V=31THENA(I+30)=2.5
2050 IFA(I)+V=5THENA(I+30)=-1
2060 IFA(I)+V=10THENA(I+30)=-.3
2070 IFA(I)+V<5THENA(I+30)=.2
2080 IFA(I)+V>15THENA(I+30)=.2
2090 IFA(I)+V=21THENA(I+30)=-1
2100 A=I-10:O=I+30:IFA(A)=A(C+50)A(O)=A(O)+2:IFA(A)=A(C+49)A(O)=A(O)+4
2110 IFV+2*A(I)>31THENA(O)=A(O)+.2
2120 IFC>1B=I:E=A(A):F=A(C+50):G=A(C+49):H=A(C+48):N=A(C+47):T=0
2130 IFC>1Q=2:GOSUB770:I=B:IFT>0THENA(O)=A(O)+T
2140 IF((A(A)=A(C+50)+1)+(A(A)=A(C+50)-1))*((2*A(A))+V<32)A(O)=A(O)-.5
2150 FORB=31TO34:IFB=I-10NEXTB
2160 IF(A(B)=A(I-10))*(V+2*A(I)<32)THENA(I+30)=A(I+30)+.3
2170 NEXTI
2180 A=-10:FORI=71TO74:IFA(I)>ATHENN1=I:A=A(I)
2190 NEXTI:IFA=-10GOTO2330
2200 A=N1-30:N1=N1-40:V=V+A(A)
2210 IFV=15PRINT"FIFTEEN FOR TWO":T=2:GOSUB2440
2220 IFV=31PRINT"THIRTY-ONE FOR TWO":T=2:GOSUB2440
2230 T=0:GOSUB1940:IFT>0GOSUB2440
2240 T=0
2250 IFA(N1)=A(C+50)T=T+2:IFA(N1)=A(C+49)T=T+4:IFA(N1)=A(C+48)T=T+6
2260 IFT=2PRINT"PAIR FOR TWO":GOSUB2440:GOSUB2370
2270 IFT=6PRINT"TRIPS FOR SIX":GOSUB2440:GOSUB2370
2280 IFT=12PRINT"QUADS FOR TWELVE":GOSUB2440:GOSUB2370
2290 C=C+1:A(C+50)=A(N1):A(C+60)=A(N1-10):A(N1)=0:A(A)=0:A(A-20)=0
2300 J=0:L=L+1:IF(L>3)*(K>3)GOTO2590
2310 CLS:IFV=31GOSUB2410
2320 GOSUB500:PRINT@768," ":J=0:GOTO1730
2330 IFJ=1THENJ=0:GOTO2350
2340 J=1:GOSUB500:PRINT@832,"COMPUTER GO":GOSUB2370:GOTO1730
2350 IFV=31GOTO2410
2360 T=1:GOSUB2440:GOTO2410
2370 FORO=1TO1000:NEXTO:RETURN
2380 PRINT@320,"PLAYER HAND   #1 #2 #3 #4          CUT CARD"
2390 X=459:B=0:D=4:I=4:GOSUB380:X=482:B=0:D=1:I=12:GOSUB380:RETURN
2400 FORI=51TO50+C:A(I)=0:A(I+10)=0:NEXTI:C=0:V=0:CLS:GOTO2430
2410 FORI=51TO50+C:A(I)=0:A(I+10)=0:NEXTI:C=0:V=0:CLS
2420 PRINT@530,"NEW PLAY DECK":FORI=1TO999:NEXTI:GOSUB500:PRINT@900," ":GOTO1730
2430 PRINT@530,"NEW PLAY DECK":FORI=1TO1000:NEXTI:GOTO2010
2440 A(15)=A(15)+T:IFA(15)>120GOTO2480
2450 PRINT"COMPUTER SCORES";T:FORI=1TO1000:NEXTI:RETURN
2460 A(14)=A(14)+T:IFA(14)>120GOTO2510
2470 PRINT"PLAYER SCORES  ";T:FORI=1TO1000:NEXTI:RETURN
2480 PRINT@532,"I WIN!!!!!!!!!!!!":PRINT@596,"I FEEL SO WONDERFULLLL!!
2490 PRINT:INPUT"SHALL WE GO AT IT AGAIN (1=YES)";A:IFA=1GOTO190
2500 END
2510 CLS:PRINT@532,"PLAYED THIS GAME BEFORE? - YOU WIN!!":GOTO2490
2520 X=A(13):REM CHECK FOR CUT JACK
2530 IFX>13X=X-13:GOTO2530
2540 IFX<>10RETURN
2550 IFZ=1PRINT"PLAYER CUTS JACK":T=2:GOSUB2460:RETURN
2560 PRINT"COMPUTER CUTS JACK":T=2:GOSUB2440:RETURN
2570 IFV=31A(14)=A(14)+2:GOTO2610
2580 A(14)=A(14)+1:GOTO2610
2590 IFV=31A(15)=A(15)+2:GOTO2610
2600 A(15)=A(15)+1
2610 GOSUB550:FORI=1TO3000:NEXTI:CLS:ONZGOTO2630,2700
2620 FORI=1TO13:A(I)=RND(52):GOSUB2380:GOTO1420
2630 PRINT@0,"COMPUTER SCORE"," "," ","PLAYER SCORES":PRINT@69,A(15),
2640 PRINT@86,"CURRENT SCORES",:PRINT@117,A(14):A(15)=A(15)+R
2650 PRINT@133,R:PRINT@152,"MELD SCORE
2660 PRINT@181,S:PRINT@216,"CRIB SCORE",:PRINT@245,W
2670 A(14)=A(14)+S+W:PRINT@261,A(15),:PRINT@280,"NEW SCORES",
2680 PRINT@309,A(14)
2690 GOTO2760
2700 PRINT@0,"PLAYER SCORE"," "," ","COMPUTER SCORE":PRINT@69,A(14),
2710 PRINT@86,"CURRENT SCORES",:PRINT@117,A(15):A(14)=A(14)+S
2720 PRINT@133,S:PRINT@152,"MELD SCORE
2730 PRINT@181,R:PRINT@216,"CRIB SCORE",:PRINT@245,W
2740 A(15)=A(15)+R+W:PRINT@261,A(14),:PRINT@280,"NEW SCORES",
2750 PRINT@309,A(15)
2760 PRINT:PRINT:PRINT"     YOUR HAND"," COMPUTER HAND","  CRIB HAND","CUT CARD
2770 X=577:D=4:I=4:B=0:GOSUB380:X=591:D=4:I=0:B=0:GOSUB380
2780 X=607:D=4:I=8:B=0:GOSUB380:X=624:D=1:I=12:B=0:GOSUB380:PRINT@847,"";
2790 GOSUB3510:IF((A(14)>120)*(Z=2))+((A(14)>120)*(A(15)<121))GOTO2510
2800 IF((A(15)>120)*(Z=1))+((A(15)>120)*(A(14)<121))GOTO2480
2810 GOTO210
2820 CLS:PRINT"CRIBBAGE DIRECTIONS
2830 PRINT"   HOPEFULLY YOU HAVE PLAYED CRIBBAGE SOME TIME BEFORE.
2840 PRINT"IF NOT, THESE DIRECTION MAY PROVE INADEQUATE. IF SO, HOYLE'S
2850 PRINT"BOOK OF CARD GAMES SHOULD BE OF GREATER ASSISTANCE.
2860 PRINT"    IN A NUTSHELL, CRIBBAGE JUST HAPPENS TO BE ONE OF THE
2870 PRINT"BEST TWO PLAYER CARD GAMES GOING (ALTHOUGH IT CAN BE PLAYED
2880 PRINT"BY MORE). TO START WITH, EACH PLAYER IS DEALT SIX CARDS
2890 PRINT"TO LOOK OVER. THE PLAYERS MUST THEN SELECT WHICH FOUR THEY
2900 PRINT"INTEND TO KEEP. THE OTHER TWO ARE PLACED IN THE CRIB.
2910 PRINT"THE CARDS PLACED IN THE CRIB WILL NOT BE PART OF THE 
2920 PRINT"PLAY PHASE OF THE GAME, AND THE PLAYERS ALTERNATE 
2930 PRINT"FOR POSSESSION OF THE 'CRIB' FOR THE MELD PHASE OF EACH
2940 PRINT"HAND.":PRINT"":PRINT"":GOSUB3510
2950 PRINT"   THE BIGGEST CONSIDERATION IS TO SELECT THOSE CARDS WHICH
2960 PRINT"PRESERVE THE MAXIMUM MELD COMBINATIONS. THE FOLLOWING IS
2970 PRINT"A LIST OF THE VARIOUS MELD COMBINATIONS - REMEMBER, ANY
2980 PRINT"DIFFERING COMBINATION COMPOUNDS THE SCORING (EX, 2 5'S & 2 10'S
2990 PRINT"PRODUCE 4 DIFFERENT 15 COMBINATIONS & TWO PAIRS FOR 12 PTS).
3000 PRINT"FIFTEEN'S",2,"FACE CARDS = 10 & ACE = 1
3010 PRINT"PAIRS",2;"3 OF KIND = 3 PAIRS
3020 PRINT"STRAIGHTS",3;"MINIMUM OF 3 CARDS ANY SUIT
3030 PRINT"FLUSH",4;"ALL FOUR CARDS IN HAND SAME SUIT
3040 PRINT"JACK",1;"IF IN SAME SUIT AS CUT CARD
3050 PRINT"REMEMBER, CARDS MAY BE USED AND REUSED IN ANY COMBINATION
3060 PRINT"AS LONG AS THE EXACT SAME CARDS ARE NOT BEING USED OVER.
3070 PRINT"EX. 6,7,8,9 OF HEARTS = STRAIGHTS 6-7-8-9 FIFTEENS 7-8,6-9
3080 PRINT"FLUSH 6-7-8-9  TOTAL PTS 12":GOSUB3510
3090 PRINT"   AFTER THE PLAYERS HAVE MADE THEIR SELECTIONS AND PRODUCED
3100 PRINT"THE 'CRIB', THE DECK IS CUT FOR THE COMMON CARD (CUT CARD).
3110 PRINT"THIS CARD IS INCLUDED AS PART OF EACH HAND DURING THE MELD
3120 PRINT"PHASE OF THE GAME. IF THE CUT CARD IS A JACK, THE DEALER
3130 PRINT"(WHOEVER HAS THE CRIB) IS AWARDED 2 PTS IMMEDIATELY.
3140 PRINT"   AFTER THE CUT CARD IS PRODUCED, THE PLAY PHASE BEGINS WITH
3150 PRINT"THE PLAYER WHO DOES NOT HAVE THE CRIB PLAYING THE FIRST CARD.
3160 PRINT"   ALL COMBINATIONS THAT APPLY DURING THE MELD PHASE CAN
3170 PRINT"APPLY DURING THE PLAY PHASE, THE PRIME DIFFERENCE BEING
3180 PRINT"A GREATER ATTENTION TO THE ORDER IN WHICH THEY WERE PLAYED.
3190 PRINT"FOR INSTANCE, TWO POINTS ARE AWARDED TO THE PLAYER WHO PLACES 
3200 PRINT"THE SECOND CARD OF A PAIR, HOWEVER, THEY MUST BE CONSECUTIVE
3210 PRINT"(LAST TWO CARDS PLAYED). THE SAME IS TRUE FOR
3220 PRINT"STRAIGHTS,  ETC.":GOSUB3510
3230 PRINT"   THIS PLAY OCCURS WHILE A COUNT IS BEING MADE OF THEIR COUNT
3240 PRINT"VALUE. THIS ASPECT OF CRIBBAGE IS SIMILAR TO BLACK JACK. THE
3250 PRINT"COUNT VALUES OF EACH CARD IS IDENTICAL TO BLACK JACK EXCEPT
3260 PRINT"THAT THE ACE IS ONLY ONE. AS THE PLAYERS ALTERNATE IN PLAYING
3270 PRINT"THEIR CARDS, THE COMBINED 'BLACK JACK' VALUE IS THE DECK
3280 PRINT"COUNT. THE MAXIMUM VALUE THE DECK MAY REACH IS 31. WHEN 
3290 PRINT"THE PLAYER WHOSE TURN IT IS TO PLAY A CARD DOES NOT HAVE A
3300 PRINT"CARD LEFT THAT WOULD KEEP THE DECK VALUE UNDER 32, HE GIVES A
3310 PRINT"'GO'. THE OTHER PLAYER MUST THEN ATTEMPT TO ADD TO THE DECK
3320 PRINT"VALUE. IF HE CANNOT PLAY A CARD AND KEEP THE DECK VALUE UNDER
3330 PRINT"32, THEN THE LAST PLAYER TO PLAY A CARD IS IMMEDIATELY AWARDED
3340 PRINT"1PT. (2 IF THE DECK VALUE IS EXACTLY 31). THE DECK COUNT THEN
3350 PRINT"CYCLES BACK TO ZERO AND THE PLAYER WHO GAVE THE 'GO' PLACES
3360 PRINT"THE FIRST CARD OF THE NEW DECK.":GOSUB3510
3370 PRINT"   DURING THE PLAY, A PLAYER WHO PLAYS THE CARD THAT BRINGS THE
3380 PRINT"DECK COUNT TO EXACTLY 15 IS IMMEDIATELY AWARDED 2 POINTS.
3390 PRINT"   THE OBJECT OF THE GAME IS TO BE THE FIRST PLAYER TO GET A
3400 PRINT"SCORE OVER 120 PTS. REMEMBER, IT IS THE FIRST PLAYER TO REACH
3410 PRINT"A SCORE OF OVER 120, NOT THE ONE WHO PLAYS AFTER THE MELD HAS
3420 PRINT"BEEN ADDED, THAT WINS. THERE IS A SPECIFIC SEQUENCE TO
3430 PRINT"CREDIT POINTS. FIRST, THE CUT JACK IS AWARDED, THEN THE PLAY
3440 PRINT"POINTS ARE ADDED IMMEDIATELY AS THEY OCCUR, AND THEN THE MELD
3450 PRINT"POINT ARE ADDED, FIRST TO THE PLAYER WHO DOES NOT CONTROL THE
3460 PRINT"CRIB, THEN THE MELDS OF THE DEALER HAND AND CRIB HAND.
3470 PRINT"   IF THE NON-CRIB PLAYER GETS OVER 120 FIRST, THE GAME ENDS
3480 PRINT"IMMEDIATELY. EVEN IF THE OTHER PLAYER WOULD HAVE HAD A HIGHER
3490 PRINT"SCORE IF THE GAME CONTINUED.":GOSUB3510
3500 CLS:PRINT@530,"GOOD LUCK!!!!!!!!":FORI=1TO1000:NEXTI:RETURN
3510 PRINT@916,"";:INPUT"PRESS ENTER";A$:CLS:RETURN
3520 DATA1,2,3,4,5,6,1,2,3,5,4,6,1,2,3,6,4,5,1,2,4,5,3,6,1,2,4,6,3,5
3530 DATA1,3,4,5,2,6,1,3,4,6,2,5,1,4,5,6,2,3,1,2,5,6,3,4,1,3,5,6,2,4
3540 DATA2,3,4,5,1,6,2,3,4,6,1,5,2,3,5,6,1,4,2,4,5,6,1,3,3,4,5,6,1,2

