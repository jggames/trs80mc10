P0
20 '- by Listrun
30 '- 1988.Jan.&Feb.
100 CLEAR:DIM CD(5)
110 DEFCHR$(252)="FE007C827C"
120 PR$="98765432AQKJ"+CHR$(252)
130 CLS:PRINT"Card Game Ninety-Nine by run";:BEEP
140 FOR I=0 TO 5
150 CD(I)=INT(RAN#*13)+1
160 NEXT I
170 LOCATE 0,1:PRINT "Score:";T;
180 LOCATE 10,1:PRINT ":     ";
190 IF T>99 THEN #2
200 FOR I=0 TO 2
210 LOCATE 12+I*2,1
220 PRINT MID$(PR$,CD(I),1);
230 NEXT I
240 K$=INKEY$
250 IF K$<"1" OR K$>"3" THEN 240
260 KY=VAL(K$)-1:BEEP 1
270 SB=CD(KY):GOSUB #1
280 CD(KY)=INT(RAN#*13)+1
290 T=T+SB
300 LOCATE 8,1:PRINT "      ";
310 LOCATE 6,1:PRINT T;
320 IF T>99 THEN #3
330 LOCATE 12,1:PRINT"   FX";
340 FOR I=3 TO 4
350 FOR J=I TO 5
360 IF CD(I)>CD(J) ; X=CD(I):CD(I)=CD(J):CD(J)=X
370 NEXT J
380 NEXT I
390 KY=-1
400 FOR I=3 TO 5
410 SB=CD(I):GOSUB #1
420 IF SB+T<=99 ; I=I+9
430 NEXT I
440 I=I-1:IF I>9 ; I=I-9
450 T=T+SB
460 LOCATE 12,1:PRINT MID$(PR$,CD(I),1);
470 CD(I)=INT(RAN#*13)+1
480 FOR I=0 TO 500:NEXT I
490 GOTO 170
P1
100 IF SB<=9 ; SB=10-SB:RETURN
110 IF SB=12 ; SB=0:RETURN
120 IF SB<=11 ;SB=0:T=98:RETURN
130 SB=10:IF T>89;SB=-SB:RETURN
140 IF T<=9 ; RETURN
150 IF KY<0 ; RETURN
160 LOCATE 20,1:PRINT "+ or -";
170 K$=INKEY$
180 IF K$="+" THEN 210
190 IF K$<>"-" THEN 170
200 SB=-SB
210 LOCATE 20,1:PRINT "      ";
220 BEEP 1:RETURN
P2
100 BEEP 1:BEEP 1:BEEP 1
110 LOCATE 0,2
120 PRINT "Vous avez gagn√© ! - Game over"
130 GOTO #0
P3
100 BEEP :BEEP :BEEP
110 LOCATE 0,2
120 PRINT "Vous avez perdu ! - Game over"
130 GOTO #0