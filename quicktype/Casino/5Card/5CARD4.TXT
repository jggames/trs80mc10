0 CLEAR500:GOTO10000
9 C2=INT(((C1/80)-INT(C1/80))*80):C3=INT(C1/80):PRINT@INT(C2/2.5)+32*INT(C3/1.5),;:RETURN

20 C$(((X*5)-5)+NC(1))=C$(DR):S$(((X*5)-5)+NC(1))=S$(DR):CD$(((X*5)-5)+NC(1))=CD$(DR)
30 C(((X*5)-5)+NC(1))=C(DR):S(((X*5)-5)+NC(1))=S(DR):DR=DR+1:RETURN
40 C$(((X*5)-5)+NC(2))=C$(DR):S$(((X*5)-5)+NC(2))=S$(DR):CD$(((X*5)-5)+NC(2))=CD$(DR)
41 C(((X*5)-5)+NC(2))=C(DR):S(((X*5)-5)+NC(2))=S(DR):DR=DR+1:RETURN
50 C$(((X*5)-5)+NC(3))=C$(DR):S$(((X*5)-5)+NC(3))=S$(DR):CD$(((X*5)-5)+NC(3))=CD$(DR)
51 C(((X*5)-5)+NC(3))=C(DR):S(((X*5)-5)+NC(3))=S(DR):DR=DR+1:RETURN

90 FORX=1TO5
100 IFP(X)<>0THEN1550
130 FORY=1TO5
140 V(X,Y)=C(Y+((X-1)*5)):V$(X,Y)=C$(Y+((X-1)*5)):ST(X,Y)=S(Y+((X-1)*5)):ST$(X,Y)=S$(Y+((X-1)*5))
170 NEXT
200 REM ONE PAIR
210 FORN=1TO4
220 FORI=2TO5
240 IFN<>IANDV(X,N)=V(X,I)THENH$(X)="P"+RIGHT$(STR$(N),1)+RIGHT$(STR$(I),1):N=4:I=5
250 NEXTI,N
260 IFLEFT$(H$(X),1)<>"P"THEN610
270 IFV(X,VAL(MID$(H$(X),2,1 )))>=11THENH$(X)=H$(X)+"J"
280 IFMID$(H$(X),2,1)="3"THEN390
290 IFMID$(H$(X),2,1)="4"ANDRIGHT$(H$(X),1)<>"J"THEN610
300 IFMID$(H$(X),2,1)="4"THEN1360
310 FORN=1TO4
320 FORI=N+1TO5
330 IFN=VAL(MID$(H$(X),2,1))ORN=VAL(MID$(H$(X),3,1))THENI=5:GOTO350
340 IFV(X,N)=V(X,I)THENH$(X)="2"+LEFT$(H$(X),3)+RIGHT$(STR$(N),1)+RIGHT$(STR$(I),1):I=5:N=4
350 NEXTI,N
360 IFV(X,VAL(MID$(H$(X),3,1)))=V(X,VAL(MID$(H$(X),5,1)))THEN365
361 GOTO370
365 H$(X)="FOUR "+V$(X,VAL(MID$(H$(X),3,1)))="S":GOTO1360
370 IFLEFT$(H$(X),1)="2"ANDMID$(H$(X),3,1)="1"THEN530
380 IFLEFT$(H$(X),1)="2"THEN1360
390 FORN=1TO5
400 IFN=VAL(MID$(H$(X),2,1))ORN=VAL(MID$(H$(X),3,1))THEN420
410 IFV(X,N)=V(X,VAL(MID$(H$(X),2,1)))THENH$(X)="T"+MID$(H$(X),2,2)+RIGHT$(STR$(N),1):N=5
420 NEXTN
430 IFLEFT$(H$(X),1)="T"ORRIGHT$(H$(X),1)="J"THEN1360
450 GOTO610

530 FORN=1TO5
540 FORI=3TO6
550 IFN=VAL(MID$(H$(X),I,1))THENI=6:F=1
560 NEXT:IFF=1THENF=0:GOTO590
570 IFV(X,N)=V(X,VAL(MID$(H$(X),3,1)))THENH$(X)="FULL HOUSE "+V$(X,VAL(MID$(H$(X),3,1)))+"S":N=5
580 IFV(X,N)=V(X,VAL(RIGHT$(H$(X),1)))THENH$(X)="FULL HOUSE "+V$(X,VAL(RIGHT$(H$(X),1)))+"S":N=5
590 NEXT
600 GOTO1360

610 FORN=1TO5
620 FORI=1TO5
630 IFDU$(X)="ON"ANDN=S4THENI=5:GOTO650
635 IFDU$(X)="ON2"ANDN=S1THENI=5:GOTO650
640 IFV(X,N)=V(X,I)+3THENS1=I:S4=N:N=5:I=5:F=1
650 NEXTI,N:IFF=1THENF=0:GOTO760
660 IFIN(X)=0THEN720
670 IN(X)=0
680 FORN=1TO5
690 IFV(X,N)=1THENV(X,N)=14
700 NEXT
705 IFDU$(X)="ON"THENDU$(X)="ON2":GOTO610
710 DU$(X)="":GOTO910
720 FORN=1TO5
730 IFV(X,N)=14THENV(X,N)=1
740 NEXT
750 IN(X)=1:GOTO610
760 FORN=1TO5
770 IFV(X,N)=V(X,S1)+2THENS3=N:N=5:F=1
780 NEXT:IFF=1THENF=0:GOTO810
790 IFDU$(X)=""THENDU$(X)="ON":GOTO610
800 GOTO680
810 FORN=1TO5
820 IFV(X,N)=V(X,S1)+1THENS2=N:N=5:F=1
830 NEXT:IFF=1THENF=0:GOTO850
840 GOTO680
850 H$(X)="4S"+RIGHT$(STR$(S1),1)+RIGHT$(STR$(S2),1)+RIGHT$(STR$(S3),1)+RIGHT$(STR$(S4),1)
860 FORN=1TO5
870 IFV(X,N)=V(X,S1)+4THENH$(X)="STRAIGHT TO "+V$(X,N):CH(X)=V(X,N)
880 IFV(X,N)=V(X,S1)-1THENH$(X)="STRAIGHT TO "+V$(X,S4):CH(X)=V(X,S4)
890 NEXT
900 GOTO1140

910 REM INSIDE STRAIGHT
920 IFX=2ORX=3ORX=5ORH$(X)<>""THEN1140
930 FORN=1TO5
940 FORI=1TO5
950 IFDU$(X)="ON"ANDN=S5(X)THENI=5:GOTO970
955 IFDU$(X)="ON2"ANDN=S1THENI=5:GOTO970
960 IFV(X,N)=V(X,I)+4THENS1=I:S5(X)=N:N=5:I=5:F=1
970 NEXTI,N:IFF=1THENF=0:GOTO1070
980 IFIN(X)=0THEN990
985 IN(X)=0:GOTO1030
990 FORN=1TO5
1000 IFV(X,N)=14THENV(X,N)=1
1010 NEXT
1020 IN(X)=1:GOTO930
1030 FORN=1TO5
1040 IFV(X,N)=1THENV(X,N)=14
1050 NEXT
1055 IFDU$(X)="ON"THENDU$(X)="ON2":GOTO930
1060 IN(X)=0:DU$(X)="":GOTO1140
1070 FORN=1TO5
1080 FORI=1TO5
1090 IFV(X,N)=V(X,S1)+3ANDV(X,I)=V(X,S1)+2THEN
1091 GOTO1100
1095 H$(X)=H$(X)+"IS"+RIGHT$(STR$(S1),1)+RIGHT$(STR$(S1),1)+RIGHT$(STR$(I),1)
1096 H$(X)H$(X)+RIGHT$(STR$(N),1)+RIGHT$(STR$(S5(X)),1)+RIGHT$(STR$(V(X,S1)+1),1)
1100 IFV(X,N)=V(X,S1)+3ANDV(X,I)=V(X,S1)+1THEN1105
1101 GOTO1110
1105 H$(X)=H$(X)+"IS"+RIGHT$(STR$(S1),1)+RIGHT$(STR$(I),1)+RIGHT$(STR$(N),1)+RIGHT$(STR$(S5(X)),1)+RIGHT$(STR$(V(X,S1)+2),1)
1110 IFV(X,N)=V(X,S1)+2ANDV(X,I)=V(X,S1)+1THEN1115
1111 GOTO1120
1115 H$(X)=H$(X)+"IS"+RIGHT$(STR$(S1),1)+RIGHT$(STR$(I),1)+RIGHT$(STR$(N),1)+RIGHT$(STR$(S5(X)),1)+RIGHT$(STR$(V(X,S1)+3),1)
1120 NEXTI,N
1130 IFLEFT$(H$(X),1)<>"I"ANDDU$(X)=""THENDU$(X)="ON":GOTO930

1140 FORN=2TO5
1150 IFST(X,N)<>ST(X,1)THENN=5:F=1
1160 NEXT:IFF=1THENF=0:GOTO1200
1170 IFLEFT$(H$(X),4)<>"STRA"THENH$(X)="FLUSH "+ST$(X,1)+S$(X,1)+ST$(X,1)
1180 IFLEFT$(H$(X),4)="STRA"THENH$(X)="STRAIGHT FLUSH"
1190 GOTO1360
1200 FORN=1TO5
1210 FORN2=1TO5
1220 FORN3=1TO5
1230 IFN2=N3ORN=N3ORN=N2THEN1250
1240 IFST(X,N)=ST(X,N2)ANDST(X,N)=ST(X,N3)THEN1245
1241 GOTO1250
1245 H$(X)=H$(X)+"3F"+ST$(X,N):F(X)=N:F2=N2:F3=N3:N=5:N2=5:N3=5:F=1
1250 NEXTN3,N2,N:IFF=0THEN1360
1251 F=0:GOTO1270
1270 FORN4=1TO5
1280 IFN4=F2ORN4=F3ORN4=F(X)THEN1300
1290 IFST(X,N4)=ST(X,F(X))THENH$(X)="4F"+ST$(X,F(X)):N4=5:F=1
1300 NEXT:IFF=1THENF=0:GOTO1360
1310 IFX<>1ANDLEFT$(H$(X),2)="3F"THENH$(X)="":GOTO1360
1320 IFLEFT$(H$(X),2)="3F"THEN1360
1330 FORI=1TOLEN(H$(X))
1340 IFMID$(H$(X),I,2)="3F"THENH$(X)=LEFT$(H$(X),I-1)
1350 NEXT

1360 REM ASSIGN H(X) VALUES
1380 IFH$(X)=""THENH(X)=0
1381 M$=LEFT$(H$(X),2):FORC1=1TO4:IFM$=MID$(" IS3F4S4F",C1*2,2)THENH(X)=C1
1382 NEXT
1430 IFLEFT$(H$(X),1)="P"THENH(X)=5:IFLEN(H$(X))>3THENH(X)=6
1450 IFLEFT$(H$(X),2)="2P"THENH(X)=7
1460 IFLEFT$(H$(X),1)="T"THENH(X)=8
1470 IFLEFT$(H$(X),10)="STRAIGHT T"THENH(X)=9
1480 IFLEFT$(H$(X),2)="FL"THENH(X)=10
1490 IFLEFT$(H$(X),2)="FU"THENH(X)=11
1500 IFLEFT$(H$(X),2)="FO"THENH(X)=12
1510 IFLEFT$(H$(X),10)="STRAIGHT F"THENH(X)=13
1520 IFREDO=1THENRETURN
1540 ONXGOSUB1570,1630,1710,1900
1550 NEXT
1560 RETURN

1570 REM MARVIN
1580 IFH(1)<8THENBF(1)=1:RF(1)=0
1590 IFH(1)=8ORH(1)=9THENBF(1)=2:RF(1)=2
1600 IFH(1)>9THENBF(1)=3:RF(1)=5
1610 DF(1)=21
1620 RETURN

1630 REM GERRY
1640 IFH(2)<3THENDF(2)=0
1650 IFH(2)=3ORH(2)=4THENRF(2)=0:DF(2)=6
1660 IFH(2)>4ANDH(2)<8THENBF(2)=1:RF(2)=0:DF(2)=4:IFH(2)>5THENDF(2)=14
1670 IFH(2)=8ORH(2)=9THENBF(2)=2:RF(2)=1:DF(2)=21
1680 IFH(2)=10THENBF(2)=3:RF(2)=3:DF(2)=21
1690 IFH(2)>10THENBF(2)=3:RF(2)=5:DF(2)=21
1700 RETURN

1710 REM WALTER
1720 IFH(3)< 3THENDF(3)=0
1730 IFH(3)=3ORH(3)=4THENRF(3)=3:DF(3)=6
1740 IFH(3)<8ANDH(3)>4THENBF(3)=1:RF(3)=0:DF(3)=4:IFH(3)>5THENDF(3)=10
1750 IFH(3)=8THENBF(3)=2:RF(3)=2:DF(3)=21
1760 IFH(3)>=9THENBF(3)=3:RF(3)=3:DF(3)=21
1770 IFH(3)=6THENGOSUB1800:RETURN
1771 IFH(3)=8THENGOSUB1850
1780 RETURN
1800 FORI=1TO5
1810 IFI=VAL(MID$(H$(3),2,1))ORI=VAL(RIGHT$(H$(3),1))THEN1830
1820 IFV(X,VAL(MID$(H$(X),2,1)))<>14ANDV(3,I)=14THEN1825
1821 GOTO1830
1825 H$(3)=H$(3)+"K"+RIGHT$(STR$(I),1):BF(3)=2:RF(3)=4:DF(3)=21
1830 NEXT
1840 RETURN
1850 FORI=1TO5
1860 IFI=VAL(MID$(H$(3),2,1))ORI=VAL(MID$(H$(3),2,2))ORI=VAL(RIGHT$(H$(3),1))THEN1880
1870 IFV(X,VAL(MID$(H$(X),2,1)))<>14ANDV(3,I)=14THEN1875
1871 GOTO1880
1875 H$(3)=H$(3)+"K"+RIGHT$(STR$(I),1):BF(3)=1:RF(3)=4:DF(3)=21
1880 NEXT
1890 RETURN

1900 REM BETSY
1910 IFH(4)<3THENDF(4)=0
1920 IFR(4)>50ANDH(4)<3THENRF(4)=0:DF(4)=6
1930 IFH(4)>2ANDH(4)<6THENRF(4)=0:DF(4)=6
1940 IFH(4)=6THENBF(4)=1:RF(4)=0:DF(4)=9
1950 IFH(4)=7ORH(4)=8THENBF(4)=2:RF(4)=2:DF(4)=21
1960 IFH(4)>8THENBF(4)=3:RF(4)=3:DF(4)=21
1970 RETURN

2000 REM BET DRAW CARD AND FIGURE HANDS
2010 DR=26:X=D+1:IFX=6THENX=1
2030 IFP(X)<>0THEN4190
2035 FORI=0TO3:NC(I)=0:NEXT
2040 ONXGOSUB7000,7040,7080,7120,7160
2050 IFX=5THEN3470
2060 ONH(X)GOTO2320,2520,2910,3000,3070,3140,3220,3310
2070 IFH(X)>8THEN3460

2080 D(X)=3:C1=L:GOSUB9:PRINTB$(10)+"3";
2090 FORI=1TO5
2100 FORII=1TO5
2110 IFV(X,I)<V(X,II)THENII=5:F=1
2120 NEXT:IFF=1THENF=0:GOTO2140
2130 K1=I:V(X,K1)=V(X,I):I=5
2140 NEXT
2150 FORI=1TO5
2155 IFI=K1THEN2220
2160 FORII=1TO5
2180 IFII=K1THEN2200
2190 IFV(X,I)<V(X,II)THENII=5:F=1
2200 NEXT:IFF=1THENF=0:GOTO2220
2210 K2=I:V(X,K2)=V(X,I):I=5
2220 NEXT
2250 FORI=1TO3
2255 IFI>1THENNC(I)=NC(I-1)+1:GOTO2270
2260 NC(I)=NC(I)+1
2270 IFNC(I)=K1ORNC(I)=K2THEN2260
2300 NEXT
2310 GOSUB20:GOSUB40:GOSUB50:GOTO2820

2320 D(X)=1:C1=L:GOSUB9:PRINTB$(10)+"1";
2330 NC(1)=1
2340 FORI=3TO7
2350 IFNC(1)=VAL(MID$(H$(X),I,1))THENNC(1)=NC(1)+1:I=7:F=1
2360 NEXT:IFF=1THENF=0:GOTO2340
2370 GOSUB20
2380 IFC(DR-1)=VAL(MID$(H$(X),7))THENCH(X)=V(X,S5(X)):H(X)=9:H$(X)="STRAIGHT TO "+V$(X,S5(X)):GOTO3750
2390 IFIN(X)=0THEN2440
2400 FORN=1TO5
2410 IFV(X,N)=1THENV(X,N)=14
2420 NEXT
2430 IN(X)=0
2440 FORY=1TO5
2460 IFY<>NC(1)ANDC(DR-1)=V(X,Y)THENH$(X)="PAIR OF "+C$(DR-1)+"S"
2470 NEXTY
2480 IFLEFT$(H$(X),1)<>"P"THEN3750
2500 CH(X)=C(DR-1):H(X)=6:IFCH(X)<11THENH(X)=5
2510 GOTO3750

2520 D(X)=2:C1=L:GOSUB9:PRINTB$(10)+"2";
2530 FORI=1TO5
2540 IFST$(X,I)<>ST$(X,F(X))THENNC(2)=I:I=5
2550 NEXT
2560 FORI=NC(1)+1TO5
2570 IFST$(X,I)<>ST$(K,F(X))THEN NC(2)=I:I=5
2580 NEXT
2590 GOSUB20:GOSUB40
2600 IFS$(DR-2)<>ST$(X,F(X))ORS$(DR-1)<>ST$(X,F(X))THEN2820
2620 H$(X)="FLUSH "+S$(DR-1)+S$(DR-1)+S$(DR-1)
2630 REM CHECK FOR STRAIGHT FLUSH
2635 IN(X)=0
2640 FORZ1=1TO5
2641 FORZ5=1TO5
2642 IFV(X,Z5)=V(X,Z1)+4THENS1=Z1:S5=Z5:Z1=5:Z5=5:F=1
2643 NEXTZ5,Z1:IFF=1THENF=0:GOTO2649
2644 IFIN(X)=1THENGOSUB2740:GOTO3750
2645 FORI=1TO5
2646 IFV(X,I)=14THENV(X,I)=1
2647 NEXT
2648 GOTO2640
2649 FORZ2=1TO5
2650 IFV(X,Z2)=V(X,S1)+1THENS2=Z2:Z2=5:F=1
2651 NEXT:IFF=1THENF=0:GOTO2653
2652 GOSUB2740:GOTO3750
2653 FORZ3=1TO5
2654 IFV(X,Z3)=V(X,S2)+1THENS3=Z3:Z3=5:F=1
2655 NEXT:IFF=1THENF=0:GOTO2657
2656 GOSUB2740:GOTO3750
2657 FORZ4=1TO5
2658 IFV(X,Z4)=V(X,S3)+1THENH$(X)="STRAIGHT FLUSH":Z4=5:F=1
2659 NEXT:IFF=0THENGOSUB2740:GOTO2730
2660 F=0:H(X)=13
2730 GOTO3750
2740 IN(X)=0
2741 FORI=1TO5
2742 IFV(X,I)=1THENV(X,I)=14
2743 NEXT
2745 FORI=1TO5
2750 FORN=1TO5
2760 IFV(X,I)<V(X,N)THENN=5:F=1
2770 NEXT:IFF=1THENF=0:GOTO2790
2780 CH(X)=V(X,I):I=5
2790 NEXT:IFCH(X)=0THENCH(X)=V(X,5)
2800 H(X)=10:RETURN
2820 REDO=1:GOSUB100:REDO=0
2840 IFH(X)=6THENCH(X)=V(X,VAL(MID$(H$(X),2,1))):H$(X)="PAIR OF "+V$(X5,VAL(MID$(H$(X),2,1)))+"S"
2850 IFH(X)=7ANDVAL(MID$(H$(X),3,1))>VAL(MID$(H$(X),5,1))THENCH(X)=V(X,VAL(MID$(H$(X),3,1))):H$(X)="TWO PAIRS "
2860 IFH(X)=7ANDVAL(MID$(H$(X),3,1))<VAL(MID$(H$(X),5,1))THENCH(X)=V(X,VAL(MID$(H$(X),5,1))):H$(X)="TWO PAIRS"
2870 IFH(X)=8THENCH(X)=V(X,VAL(RIGHT$(H$(X),1))):H$(X)="THREE "+V$(X,VAL(RIGHT$(H$(X),1)))+"S"
2880 IFH(X)=9THENCH(X)=V(X,F3)
2890 IFH(X)=11ORH(X)=12THENGOSUB32000
2900 GOTO3750

2910 D(X)=1:C1=L:GOSUB9:PRINTB$(10)+"1";
2920 NC(1)=1
2930 FORI=3TO7
2940 IF NC(1)=VAL(MID$(H$(X),I,1))THENNC(1)=NC(1)+1:I=7:F=1
2950 NEXT:IFF=1THENF=0:GOTO2930
2960 GOSUB20
2970 IFC(DR-1)=V(X,VAL(MID$(H$(X),3,1)))-1THEN2975
2971 GOTO2980
2975 CH(X)=V(X,VAL(MID$(H$(X),6,1))):H$(X)="STRAIGHT TO "+V$(X,VAL(MID$(H$(X),6, 1))):H(X)=9:GOTO3750
2980 IFC(DR-1)=V(X,VAL(MID$(H$(X),6,1)))+1 THENH$(X)="STRAIGHT TO "+C$(DR-1):CH(X)=C(DR-1):H(X)=9:GOTO3750
2985 IFC(DR-1)=14ANDVAL(MID$(H$(X),3,1))=2 THENC(DR-1)=1:IN(X)=1:GOTO2970
2990 V(X,NC(1))=C(DR-1):GOTO2390

3000 D(X)=1:C1=L:GOSUB9:PRINTB$(10)+"1";
3010 FORI=1TO5
3020 IFST$(X,I)<>ST$(X,F(X))THENNC(1)=I:I=5
3030 NEXT
3040 GOSUB20
3050 IFS$(DR-1)=ST$(X,F(X))THEN2620
3060 GOTO2820
3070 D(X)=3:C1=L:GOSUB9:PRINTB$(10)+"3";
3080 FORI=1TO3
3090 NC(I)=NC(I-1)+1
3100 IFNC(I)=VAL(MID$(H$(X),2,1))ORNC(I)=VAL(MID$(H$(X),3,1))THENNC(I)=NC(I)+1:GOTO3100
3110 NEXT
3120 GOSUB20:GOSUB40:GOSUB50:GOTO2820

3140 IFLEN(H$(X))=4THEN3070
3150 D(X)=2:C1=L:GOSUB9:PRINTB$(10)+"2";
3160 FORI=1TO2
3170 NC(I)=NC(I-1)+1
3180 IFNC(I)=VAL(MID$(H$(X),2,1))ORNC(I)=VAL(MID$(H$(X),3,1))ORNC(I)=VAL(RIGHT$(H$(X),1))THENNC(I)=NC(I)+1:GOTO3180
3190 NEXT
3200 GOSUB20:GOSUB40:GOTO2820

3220 C1=L:GOSUB9:PRINTB$(10)+"1";:D(X)=1:NC(1)=1
3240 FORI=3TO7
3250 IFNC(1)=VAL(MID$(H$(X),I,1))THENNC(1)=NC(1)+1:I=7:F=1
3260 NEXT:IFF=1THENF=0:GOTO3240
3270 GOSUB20
3280 IFC(DR-1)=V(X,VAL(MID$(H$(X),3,1)))THENH$(X)="FULL HOUSE, "+C$(DR-1)+"S":CH(X)=C(DR-1):H(X)=11:GOTO3750
3290 IFC(DR-1)=V(X,VAL(MID$(C$(X),5,1)))THENH$(X)="FULL HOUSE, "+C$(DR-1)+"S":CH(X)=C(DR-1):H(X)=11:GOTO3750
3300 GOTO2850

3310 IFLEN(H$(X))>4THENC1=L:GOSUB9:PRINTB$(10)+"1";:D(X)=1
3320 IFLEN(H$(X))=4THENC1=L:GOSUB9:PRINTB$(10)+"2";:D(X)=2

3340 FORI=1TO2
3350 NC(I)=NC(I-1)+1
3360 IFNC(I)=VAL(MID$(H$(X),2,1))ORNC(I)=VAL(MID$(H$(X),3,1))ORNC(I)=VAL(MID$(H$(X),4,1))THENNC(I)=NC(I)+1:GOTO3360
3370 NEXT
3380 GOSUB20:IFLEN(H$(X))>4THEN3430
3381 GOSUB40
3400 IFC(DR-1)=C(DR-2)THENH$(X)="FULL HOUSE, "+V$(X,VAL(RIGHT$(H$(X),1)))="S":H(X)=11:GOSUB32000
3410 IFC(DR-1)=V(X,VAL(RIGHT$(H$(X),1)))ORC(DR-2)=V(X,VAL(RIGHT$(H$(X),1)))THEN3415
3411 GOTO3420
3415 H$(X)="FOUR "+V$(X,VAL(RIGHTH(H$(X),1)))
:GOSUB32000
3420 IFLEFT$(H$(X),1)="T"THENCH(X)=V(X,VAL(MID$(H$(X),2,1))):H$(X)="THREE "+V$(X,VAL(MID$(H$(X),2,1)))+"S"
3425 GOTO3750
3430 IFC(DR-1)=14THENH$(X)="FULL HOUSE, "+V$(X,VAL(MID$(H$(X),2,1)))+"S":GOSUB32000
3440 IFC(DR-1)=V(X,VAL(MID$(H$(X),2,1)))THENH$(X)="FOUR "+V$(X,VAL(MID$(H$(X),2,1))):GOSUB32000
3450 GOTO3420
3460 C1=L:GOSUB9:PRINT"I'LL PLAY THESE.";:GOSUB63000:GOSUB27180:GOTO4190

3750 L=412+((X-1)*20):IFX=4THENL=1372
3760 FORI=1TOD(X):C1=L:GOSUB9:PRINTERAS$;:L=L-83:NEXT
3770 C1=L:GOSUB9:PRINTART$;:L=L+83:GOSUB63020
3780 FORI=1TOD(X):C1=L:GOSUB9:PRINTART$;:L=L+83:GOSUB63020:NEXT

3470 C1=L:GOSUB9:PRINT"HOW MANY CARDS";:INPUTD(5)
3480 GOSUB7160
3490 IFD(5)<>INT(D(5))ORD(5)>3ORD(5)<0THEN3470
3500 IFD(5)=0THENC1=L:GOSUB9:PRINT"OKAY.":GOTO4190
3510 I=0
3520 FORN=21TO25
3525 GOSUB7160
3530 C1=L:GOSUB9:PRINT"REPLACE "+C$(N)+S$(N)+" (Y/N)";
3540 INPUTAN$
3550 IFAN$="N"THEN3590
3560 IFAN$<>"Y"THEN3525
3561 CD$(N)=CD$(DR):C$(N)=C$(DR):S$(N)=S$(DR):C(N)=C(DR):S(N)=S(DR)
3570 DR=DR+1
3580 I=I+1:IFI=D(5)THENN=25
3590 NEXT
3600 IFI<>D(5)THEN3605
3601 GOTO3610
3605 C1=L:GOSUB9:PRINT"YOU ONLY TOOK"STR$(I)". LET'S TRY AGAIN.":GOSUB63000
3606 C1=L:GOSUB9:PRINTB$(12)+B$(12);:GOTO3510
3610 L=1060
3620 FORN=21TO25
3630 C1=L:GOSUB9:PRINTCD$(N);
3640 GOSUB63020
3650 L=L+83
3660 NEXT
3670 GOTO4190

4190 X=X+1
4200 IFX=6THENX=1
4210 IFX=1ANDD<>5THEN2030
4220 IFX<>1ANDX<>D+1THEN2030
4230 RETURN

5000 FORX=1TO4
5010 IFP(X)<>0THEN5030
5020 ONXGOSUB5050,5110,5190,5270
5030 NEXT
5040 RETURN

5050 REM MARVIN
5060 IFH(1)<6THENDF(1)=0:BF(1)=0
5070 IFH(1)<8ANDH(1)>5THENBF(1)=1:RF(1)=0:DF(1)=14
5080 IFH(1)>7ANDH(1)<10THENBF(1)=2:RF(1)=2:DF(1)=21
5090 IFH(1)>9THENBF(1)=3:RF(1)=5:DF(1)=21
5100 RETURN

5110 REM GERRY
5120 IFH(2)=3ORH(2)=4THENBL(X)=RND(10):IFBL(X)<3THENBF(2)=2:RF(2)=3:DF(2)=14:RETURN
5130 IFH(2)<6THENDF(2)=0:BF(2)=0
5140 IFH(2)=6ORH(2)=7THENBF(2)=1:RF(2)=0:DF(2)=8
5150 IFH(2)=8ORH(2)=9THENBF(2)=2:RF(2)=1:DF(2)=12:IFH(2)=9THENDF(2)=21
5160 IFH(2)=10THENBF(2)=3:RF(2)=3:DF(2)=21
5170 IFH(2)>10THENBF(2)=3:RF(2)=5:DF(2)=21
5180 RETURN

5190 REM WALTER
5200 IFH(3)<3THENDF(3)=0:BF(3)=0
5210 IFH(3)=3ORH(3)=4THENBL(X)=RND(10):IFBL(X)=7THENBF(3)=2:RF(3)=3:DF(3)=10
5220 IFBL(X)<>7ANDH(3)<6THENDF(3)=0:BF(3)=0
5230 IFH(3)<8ANDH(3)>5 THENBF(3)=1:RF(3)=0:DF(3)=7
5240 IFH(3)=8THENBF(3)=2:RF(3)=2:DF(3)=21
5250 IFH(3)>=9THENBF(3)=3:RF(3)=3:DF(3)=21
5260 RETURN

5270 REM BETSY
5280 IFH(4)<9THENBL(X)=RND(4):IFBL(X)=4THENBF(4)=3:RF(4)=3:DF(4)=21:GOTO5340
5290 IFH(4)<6THENDF(4)=0:BF(4)=0
5300 IFH(4)=6THENBF(4)=2:RF(4)=0:DF(4)=8
5310 IFH(4)=7THENBF(4)=2:RF(4)=1:DF(4)=10
5320 IFH(4)=8THENBF(4)=2:RF(4)=2:DF(4)=12
5330 IFH(4)>8THENBF(4)=3:RF(4)=3:DF(4)=21
5340 IFR(4)>50ANDDF(4)>0THEN5345
5341 GOTO5350
5345 BF(4)=BF(4)+1:RF(4)=RF(4)+1:IFR(4)>100THENBF(4)=BF(4)+1:RF(4)=RF(4)+1:DF(4)=DF(4)+1
5350 RETURN

6000 REM SHUFFLE
6010 CLS:N=-1
6020 PRINT"shuffling"
6060 FORX=1TO40
6070 C(X)=RND(52)
6080 IFCH(C(X))=1THEN6070
6090 CH(C(X))=1
6130 S(X)=INT((C(X)-1)/13)+1
6140 S$(X)=MID$("SHDC",5-S(X),1)
6180 IFC(X)>13THENC(X)=C(X)-13:GOTO6180
6190 IFC(X)<10THENC$(X)=STR$(C(X))
6195 IFC(X)=10THENC$(X)="T"
6200 IFC(X)=11THENC$(X)="J"
6210 IFC(X)=12THENC$(X)="Q"
6220 IFC(X)=13THENC$(X)="K"
6230 IFC(X)=1THENC$(X)="A":C(X)=14
6240 CD$(X)=RIGHT$(C$(X),1)+S$(X)
6245 IFX/6=INT(X/6)THENGOSUB6270
6250 NEXT
6255 FORX=1TO52:CH(X)=0:NEXT
6260 RETURN

6270 REM ANTE
6275 IFN=-1THENPRINT:PRINT"PLEASE ANTE:":N=0:RETURN
6280 N=N+1:IFN=5THEN6320
6290 IFP(N)<>0THENPRINTP$(N)+" IS BUSTED.":RETURN
6300 R(N)=R(N)-1:PRINTP$(N)+" IS IN.":PT=PT+1:RETURN
6320 PRINT"ARE YOU IN?"
6325 C$=INKEY$
6330 IFC$="Y"THENR(5)=R(5)-1:PT=PT+1:RETURN
6340 IFC$="N"THENEND
6341 GOTO6325

7000 L=720:GOTO7200
7040 L=745:GOTO7200
7080 L=770:GOTO7200
7120 L=1680:GOTO7200
7160 L=1705
7200 C1=L:GOSUB9:PRINTB$(12):RETURN

8000 REM FIRST ROUND OF BETTINB
8010 NB=0:RD=1:OP=D+1:IFOP=6THENOP=1
8050 IFP(OP)=0THEN8110
8060 OP=OP+1:IFOP=6THENOP=1
8070 RD=RD+1:IFRD=6THENDROPF=1:D=D+1:GOTO8090
8080 GOTO8050
8090 IFD=6THEND=1
8095 IFP(D)=1THEND=D+1:GOTO8090
8100 GOSUB29170:CLS:RETURN

8110 ONOPGOSUB7000,7040,7080,7120,7160
8120 IFOP<>5THEN8250
8130 C1=L:GOSUB9:PRINT"BET";
8140 INPUTB(OP)
8150 IFH(5)<6ANDB(OP)>0THENGOSUB7160:C1=L:GOSUB9:PRINT"YOU CAN'T";:OP=1:GOTO8070
8160 IFB(OP)>5ORB(OP)>R(5)ORB(OP)<0ORB(OP)<>INT(B(OP))THENGOSUB7160:C1=L:GOSUB9:PRINT"ILLEGAL BET";:GOSUB63000:GOTO8130
8170 IFB(OP)=0THENOP=1:GOTO8070
8240 R(5)=R(5)-B(OP):PT=PT+B(OP):LB(OP)=B(OP):GOTO8330
8250 IFH(OP)>=6ANDR(OP)>=BF(OP)THEN8255
8251 GOTO8260
8255 C1=L:GOSUB9:PRINTB$(1)+RIGHT$(STR$(BF(OP)),1);:R(OP)=R(OP)-BF(OP)
8256 PT=PT+BF(OP):LB(OP)=BF(OP):GOTO8330
8260 IFH(OP)>=6THENC1=L:GOSUB9:PRINTB$(1)+"1";:R(OP)=R(OP)-1:PT=PT+1:LB(OP)=1:GOTO8330
8270 C1=L:GOSUB9:PRINTB$(8);:GOSUB63000:GOTO8060
8330 BE=LB(OP):ST=OP:RD=1:GOSUB63000

8340 REM BET CONTINUE AFTER OPENER
8350 OP=OP+1:IFOP=6THENOP=1
8360 IFP(OP)<>0THENRD=RD+1:GOTO8530
8370 GOSUB31000
8380 ONOPGOSUB7000,7040,7080,7120,7160
8390 IFOP<>5THEN8480
8400 C1=L:GOSUB9:PRINT"BET";
8410 INPUTB(OP)
8420 IFB(OP)>R(5)ORB(OP)>BE-LB(OP)+5ORB(OP)<0OR(B(OP)>BE-LB(OP)ANDNB=3)ORB(OP)<>INT(B(OP))THEN8425
8421 GOTO8430
8425 GOSUB7160:C1=L:GOSUB9:PRINT"ILLEGAL BET";:GOSUB63000:GOTO8400
8430 IFB(OP)<>0ANDB(OP)<BE-LB(OP)THEN8425
8440 IFB(OP)=0THENP(5)=10:GOSUB40000
8450 R(5)=R(5)-B(OP):PT=PT+B(OP):RD=RD+1
8460 IFB(OP)>BE-LB(OP)THENRF(OP)=B(OP)-BE-LB(OP):BE=BE+RF(OP):NB=NB+1:RD=1
8470 LB(OP)=BE:GOTO8520
8480 IFDF(OP)<=BETHENC1=L:GOSUB9:PRINTB$(9);:RD=RD+1:P(OP)=10:GOSUB40000:GOTO8520
8490 IFNB<3ANDRF(OP)>0THEN8495
8491 GOTO8500
8495 C1=L:GOSUB9:PRINTB$(3)+RIGHT$(STR$(RF(OP)),1);:R(OP)=R(OP)-RF(OP)-BE+LB(OP)
8496 PT=PT+RF(OP)+BE-LB(OP):RD=1
8500 IFNB<3ANDRF(OP)>0THENBE=BE+RF(OP):LB(OP)=BE:NB=NB+1:GOTO8520
8510 RD=RD+1:PT=PT+BE-LB(OP):R(OP)=R(OP)-BE+LB(OP):LB(OP)=BE:C1=L:GOSUB9:PRINTB$(2);
8520 GOSUB63000
8530 IFRD<>5THEN8350

8540 BE=0:LB(OP)=0:GOSUB31000
8550 SO=0
8560 FORN=1TO5
8570 IFP(N)<>0THENSO=SO+1
8580 NEXT
8590 IFSO<>4THENSO=0:RETURN
8600 FORN=1TO5
8610 IFP(N)=0THENW=N
8620 NEXT
8630 GOSUB28250:WINF=1:RETURN

9000 REM SECOND ROUND OF BETTING
9020 FOROP=1TO5:LB(OP)=0:NEXT
9050 OP=STARTER:RD=1:NB=0
9060 GOTO9080
9070 OP=OP+1:IFOP=6THENOP=1
9080 IFP(OP)<>0THENRD=RD+1:GOTO9260
9090 GOSUB31000
9100 ONOPGOSUB7000,7040,7080,7120,7160
9110 IFOP<>5THEN9200

9120 C1=L:GOSUB9:PRINT"BET";
9130 INPUTB(OP)
9140 IFB(OP)>R(5)ORB(OP)>BE-LB(OP)+5ORB(OP)<0OR(B(OP)>BE-LB(OP)ANDNB=3)ORB(OP)<>INT(B(OP))THEN9145
9141 GOTO9150
9145 GOSUB7160:C1=L:GOSUB9:PRINT"ILLEGAL BET";:GOSUB63000:GOTO9120
9150 IFB(OP)<>0ANDB(OP)<>0ANDB(OP)<BE-LB(OP)THENGOSUB7160:C1=L:GOSUB9:PRINT"ILLEGAL BET";:GOSUB63000:GOTO9120
9160 IFB(OP)=0ANDBE>0THENP(5)=10:GOSUB40000
9170 R(5)=R(5)-B(OP):PT=PT-B(OP):RD=RD+1:IFBE=0THENBE=B(OP):RD=1
9180 IFB(OP)>BE-LB(OP)THENRF(OP)=B(OP)-BE-LB(OP):BE=BE+RF(OP):IFBE<>0THENNB=NB+1:RD=1
9190 LB(OP)=BE:GOTO9250

9200 IFBF(OP)>0ANDNB=0ANDBE=0THEN9205
9201 GOTO9210
9205 C1=L:GOSUB9:PRINTB$(4)+RIGHT$(STR$(BF(OP)),1);:LB(OP)=BF(OP):BE=BF(OP)
9206 RD=1:PT=PT+BE:R(OP)=R(OP)-BE:GOTO9250
9210 IFDF(OP)<BETHENC1=L:GOSUB9:PRINTB$(9);:RD=RD+1:P(OP)=10:GOSUB40000:GOTO9250
9220 IFNB<3ANDRF(OP)>0THENC1=L:GOSUB9:PRINTB$(3)+RIGHT$(STR$(RF(OP)),1);:R(OP)=R(OP)-RF(OP)-BE+LB(OP):RD=1
9230 IFNB<3ANDRF(OP)>0THENPT=PT+RF(OP)+BE-LB(OP):BE=BE+RF(OP):LB(OP)=BE:NB=NB+1:GOTO9250
9240 LB(OP)=BE-LB(OP):RD=RD+1:PT=PT+LB(OP):R(OP)=R(OP)-LB(OP):C1=L:GOSUB9:PRINTB$(2);

9250 GOSUB63000
9260 IFRD<>5THEN9070
9270 BE=0:LB(OP)=0:GOSUB31000
9280 REM CHECK FOR DEFAULT WINNER
9300 FORN=1TO5
9305 IFP(N)<>0THENSO=SO+1
9310 NEXT
9320 IFSO<>4THENRETURN
9330 GOTO8600

10000 GOSUB60000:REM INTRO AND SEED
10010 GOSUB50000:REM HOUSEKEEPING
10020 GOSUB6000:REM SHUFFLE AND ANTE
10030 GOSUB18000:REM DEAL
10040 GOSUB90:REM FIGURE HANDS
10050 GOSUB8000:REM FIRST ROUND OF BETTING
10055 IFDROPF=1THENDROPF=0:GOTO10020
10056 IFWINF=1THENWINF=0:GOTO 10140
10060 GOSUB19000:REM CLEAR SCREEN
10070 GOSUB2000:REM GET DRAW CARD:AND FIGURE HANDS
10080 GOSUB5000:REM SET NEW PATTERN:OF BETTING
10090 GOSUB19000
10100 GOSUB9000:REM SECOND ROUND OF BETTING
10105 IFWINF=1THENWINF=0:GOTO10140
10110 GOSUB19000
10120 GOSUB27000:REM DECLARATION
10130 GOSUB28000:REM FIGURE WINNER
10140 GOSUB29000:REM CLEAR THE DECKS AND DO*AGAIN
10150 GOTO10020

18000 REM DEAL
18010 CLS
18050 IFP(1)=0THENC1=0:GOSUB9:PRINTP$(1);
18070 IFP(2)=0THENC1=25:GOSUB9:PRINTP$(2);
18090 IFP(3)=0THENC1=50:GOSUB9:PRINTP$(3);
18110 IFP(4)=0THENC1=960:GOSUB9:PRINTP$(4);
18130 C1=985:GOSUB9:PRINTP$(5);
18140 FORX=1TO5
18150 L=(80*X)+((X-1)*3)
18160 FORY=1TO5
18170 IFY>1THENL=L+20:IFY=4THENL=L+900
18180 IFP(Y)<>0THEN18200
18190 IFY<>5THENC1=L:GOSUB9:PRINTART$;
18195 IFY=5THENC1=L:GOSUB9:PRINTCD$(20+X);
18200 NEXTY
18210 NEXTX
18240 GOSUB31000:RETURN

19000 REM CLEAR SCREEN A BIT FOR TIDINESS
19010 GOSUB63000:GOSUB63000:GOSUB63000
19020 GOSUB7000
19030 GOSUB7040
19040 GOSUB7080
19050 GOSUB7120
19060 GOSUB7160
19070 NB=0:BE=0:FORI=1TO5:L(I)=0:NEXT:GOSUB31000:GOSUB63000:RETURN

27000 REM DECLARATION
27010 GOSUB47000
27020 FORX=1TO5
27030 IFP(X)<>0THEN27080
27040 ONXGOSUB7000,7040,7080,7120,7160
27050 IFX=5THENGOSUB27100:C1=L:GOSUB9:PRINT"YOU GOT "+H$(5);:GOTO27080
27060 IFH(X)>5THENC1=L:GOSUB9:PRINTH$(X);
27070 IFH(X)<=5THENC1=L:GOSUB9:PRINTB$(11);
27080 NEXT
27090 RETURN

27100 REM TRANLATE PREVIOUSLY UNTRANSLATED HAND:IN H$(X)+CHECK(X)
27110 REDO=1:GOSUB130:GOSUB1360:REDO=0
27120 IFH(X)<6THENH$(5)="DOODLY SQUAT":RETURN
27130 IFH(X)=6THENCH(X)=V(X,VAL(MID$(H$(X),2,1))):H$(X)="PAIR OF "+V$(X,VAL(MID$(H$(X),2,1)))+"S"
27140 IFH(X)=7ANDVAL(MID$(H$(X),3,1))>VAL(MID$(H$(X),5,1))THENCH(X)=V(X,VAL(MID=(H$(X),3,1))):H$(X)="TWO PAIR "
27150 IFH(X)=7ANDVAL(MID$(H$(X),3,1))<VAL(MID$(H$(X),5,1))THENCH(X)=V(X,VAL(MID$(H$(X),5,1))):H$(X)="TWO PAIRS"
27160 IFH(X)=8THENCH(X)=V(X,VAL(RIGHT$(H$(X),1))):H$(X)="THREE "+V$(X,VAL(RIGHT$(H$(X),1)))+"S"
27180 IFH(X)=10THENGOSUB2740
27190 IFH(X)=11ORH(X)=12THENGOSUB32000
27200 RETURN

28000 REM FIGURE WHO WON
28010 FORX=1TO5
28020 IFP(X)<>0THEN28080
28030 FORI=1TO5
28040 IFH(X)<H(I)THENI=5:F=1
28050 NEXT:IFF=1THENF=0:GOTO28080
28060 W=X:X=5

28080 Y=2:TIE(1)=W
28090 FORN=1TO5
28100 IFP(N)<>0ORN=WTHEN28120
28110 IFH(N)=H(W)THENTIE(Y)=N:Y=Y+1
28120 NEXT
28130 IFY=2THEN28250

28140 FORX=1TOY-1
28150 FORI=1TOY-1
28160 IFCH(TIE(X))<CH(TIE(I))THENI=Y-1:F=1
28170 NEXT:IFF=1THENF=0:GOTO28185
28180 W=TIE(X):X=5
28185 NEXT
28190 MULTI=2:MULTI(1)=W
28200 FORN=1TOY-1
28210 IFTIE(N)=WTHEN28230
28220 IFCH(TIE(N))=CH(W)THENMULTI(MULTI)=TIE(N):MULTI=MULTI+1
28230 NEXT
28240 IFMULTI>2THEN28320

28250 REM WINNER'S CIRCLE
28260 ONWGOSUB46100,46200,46300,46400,46500
28261 C1=L:GOSUB9:PRINT"->"+P$(W)+"WIN";:IFW<>5THENPRINT"S";
28270 GOSUB63000:R(W)=R(W)+PT:PT=0
28300 GOSUB7160:C1=L:GOSUB9:PRINT"PRESS space "+"->";
28310 C$=INKEY$:IFC$<>" "THEN28310
28315 RETURN

28320 IFTPC=0ANDH(MULTI(W))=7THEN28370
28325 FORN=1TOMULTI-1
28330 ONMULTI(N)GOSUB46100,46200,46300,46400,46500
28331 C1=L:GOSUB9:PRINT"->"+P$(MULTI(N))+" WIN";:IFMULTI(N)<>5THENPRINT"S"
28340 R(MULTI(N))=R(MULTI(N))+INT(PT/(MULTI-1))
28350 NEXT
28360 GOSUB63000:PT=0:GOSUB7160:C1=L:GOSUB9
28361 PRINT"MULTIPLE WINNERS SPLIT":PRINT"PRESS space BAR"+"->";
28365 C$=INKEY$:IFC$<>" "THEN28365
28366 RETURN
28370 FORZ=1TO2
28380 FORI=1TO4
28390 FORII=2TO5
28400 IFI=IIORV(MULTI(Z),I)=CH(MULTI(2))THEN28420
28410 IFV(MULTI(Z),I)=V(MULTI(Z),II)THENCH(MULTI(Z))=V(MULTI(Z),I)
28420 NEXTII,I,Z

28430 TIE(1)=MULTI(1):TIE(2)=MULTI(2):Y=3:TPC=1:GOTO28140

29000 CLS
29010 FORX=1TO5
29020 PRINT
29030 IFP(X)=10THENP(X)=0
29040 IFP(X)<>0THENPRINTP$(X)+" IS OUT!":GOTO29090
29050 IFR(X)<1ANDX=5THENF=1:GOTO29090
29060 IFR(X)<5ANDX<>5THENPRINTP$(X)+" HAS ONLY $"MID$(STR$(R(X)),2+(R(X)<0):PRINT"GOODBYE.":GOTO29090
29070 IFX<5THENPRINTP$(X)+" $"MID$(STR$(R(X)),2+(R(X)<0)):GOTO29090
29071 PRINT"YOU $"R(5)
29090 NEXT:IFF=1THENF=0:GOTO29320
29110 PRINT:PRINT:INPUT"ANOTHER HAND (Y/N)";C$
29130 IFC$="N"THENPRINT"SEE YOU LATER!":END
29140 IFC$<>"Y"THEN29110

29150 REM CLEAR THE DECKS
29160 GOSUB29170:GOTO29290
29170 FORI=1TO5:B(I)=0:LB(I)=0:IN(I)=0:NEXT
29190 BE=0
29200 CH$=""
29210 FORI=ITO5:CH(I)=0:D(I)=0:BF(I)=0:BL(I)=0:DU$(I)="":NEXT
29220 FORI=1TO5:F(I)=0:H$(I)="":H(I)=0:NEXT
29230 NB=0:TPC=0
29240 FORI=0TO3:NC(I)=0:NEXT
29250 REDO=0:SO=0
29260 FORI=1TO5:TIE(I)=0:NEXT
29270 W=0:STARTER=0
29280 RETURN

29290 D=D+1:IFD=6THEND=1
29300 IFP(D)<>0THEN29290
29310 RETURN

31000 REM SET ONGOING INFORMATION
31010 IFSTARTER<>0THENC1=1090:GOSUB9:PRINTP$(STARTER)=B$(7);
31030 PRINT@495,"DEALER:"+P$(D);
31040 C1=1250:GOSUB9:PRINTB$(12);
31050 C1=1250:GOSUB9:PRINTB$(4);BE-LB(OP);
31080 C1=1330:GOSUB9:PRINT"RAISE#";NB;
31110 C1=1410:GOSUB9:PRINTB$(5);PT;
31120 C1=1490:GOSUB9:PRINTB$(12);
31130 PRINT@480,B$(6)R(5);
31140 RETURN

32000 REM FULL HOUSE AND FOUR OF A KIND CH(X) ROUTINE
32010 CH$=RIGHT$(H$(X),2)
32020 IFCH$="AS"THENCH(X)=14:GOTO32140
32030 IFCH$="KS"THENCH(X)=13:GOTO32140
32040 IFCH$="QS"THENCH(X)=12:GOTO32140
32050 IFCH$="JS"THENCH(X)=11:GOTO32140
32060 IFCH$="TS"THENCH(X)=10:GOTO32140
32070 CH(X)=VAL(MID$(H$(X),LEN(H$(X))-1,1))
32140 RETURN

40000 REM CHANGE DISPLAY WHEN A PLAYER FOLDS
40010 L=412:IFOP=2THENL=432:GOTO40020
40011 IFOP=3THENL=452
40012 IFOP=4THENL=1372
40013 IFOP=5THENL=1392
40020 FORI=1TO5
40030 C1=L:GOSUB9:PRINTERAS$;
40040 L=L-83
40050 NEXT
40060 RETURN

46100 L=0:RETURN
46200 L=25:RETURN
46300 L=50:RETURN
46400 L=960:RETURN
46500 L=985:RETURN

47000 REM SHOW CARDS
47010 FORI=1TO5
47020 L=(80*I)+((I-1)*3)
47030 FORY=1TO5
47040 IFY>1THENL=L+20:IFY=4THENL=L+900
47050 IFP(Y)<>0THEN47070
47060 C1=L:GOSUB9:PRINTCD$(((Y-1)*5)+I);
47070 NEXTY
47080 NEXTI
47090 RETURN

50000 REM INIT
50010 DIMR(5),P(5),P$(5)
50020 FORX=1TO5:R(X)=100:NEXT
50040 D=1
50060 P$(1)="MARVIN"
50070 P$(2)="GERRY"
50080 P$(3)="WALTER"
50090 P$(4)="BETSY"
50100 P$(5)="YOU"
50110 DIMC(40),C$(40),S(40),S$(40),CD$(40),CH(52),V(5,5),V$(5,5),ST(5,5),ST$(5,5)
50160 ART$="??"
50162 ERAS$=""
50170 DIMB$(12)
50190 B$(1)="OPEN $"
50200 B$(2)="SEE"
50210 B$(3)="RAISE $"
50220 B$(4)="BET $"
50230 B$(5)="POT $"
50240 B$(6)="YOU $"
50250 B$(7)=": OPENER"
50260 B$(8)="I CAN'T"
50270 B$(9)="OUT!"
50280 B$(10)="TAKE "
50290 B$(11)="ZILCH!"
50300 B$(12)="      "
50320 RETURN

60000 CLS:PRINTTAB(11)"DRAW POKER"
60010 PRINT:PRINT
60020 PRINTTAB(2)"COPYRIGHT 1984 BY JIM MENICK":PRINT
60060 PRINT"EACH PLAYER BEGINS WITH:"
60070 PRINT" $1 ANTE"
60080 PRINT" JACKS OR BETTER TO OPEN"
60090 PRINT" THREE RAISE LIMIT"
60100 PRINT"$5 BETTING LIMIT":PRINT:PRINT"HIT space"
60120 REM RANDOMIZE
60130 X$=INKEY$
60140 IFX$=" "THENRETURN
60141 GOTO60130

63000 FORPA=1TO1500:NEXT
63010 RETURN
63020 FORPA=1TO2500:NEXT
63030 RETURN
