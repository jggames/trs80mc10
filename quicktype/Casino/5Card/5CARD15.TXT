0 CLEAR350:M=16384:GOSUB75:GOSUB50000:GOTO10020
1 C2=VAL(MID$(H$(X),C1,1)):RETURN
2 C3=VAL(MID$(H$(X),C1,1)):RETURN

3 IFLEN(M$)<10THENPRINT@L,M$;:RETURN
4 FORC1=10TO1STEP-1:IFMID$(M$,C1,1)=" "THENPRINT@L,LEFT$(M$,C1);:PRINT@L+32,MID$(M$,C1+1);:C1=1
5 NEXT:RETURN

6 PRINT@L,"":PRINT@L,"BET";:INPUTC1:B(J)=C1:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-M+C2,:RETURN
8 FORC2=1TO500:NEXT:RETURN
9 FORC1=1TO5:GOSUB8:NEXT:RETURN
10 M$="I'LL SEE THAT":GOTO3

REM CHANGE DISPLAY WHEN A PLAYER FOLDS
11 L=160+(J-1)*10-(J>3)*226:FORI=1TO5:PRINT@L,"  ";:L=L-32:NEXT:RETURN

12 L=(C1-1)*10-(C1>3)*226:RETURN

REM SHOW CARDS
13 FORI=1TO5:L=32*I:FORY=1TO5:L=L-(Y>1)*10-(Y=4)*226:IFP(Y)=0THENPRINT@L,K$(((Y-1)*5)+I);
14 NEXTY,I:RETURN

15 M$=STR$(C1):PRINT" $"MID$(M$,2+(C1<0))" ";:RETURN

16 H$(X)=H$(X)+"IS"+RIGHT$(STR$(S),1)+RIGHT$(STR$(I),1)+RIGHT$(STR$(N),1)+RIGHT$(STR$(S5(X)),1)+RIGHT$(STR$(V(X,S)+C1),1)
17 RETURN

18 GOSUB6400:PRINT@L+32,"ILLEGAL BET";:RETURN

19 PRINT@L,"I TAKE"D(X);:RETURN

20 C1=1:GOTO60
40 C1=2:GOTO60
50 C1=3:GOTO60
60 C$(((X*5)-5)+N(C1))=C$(DR):S$(((X*5)-5)+N(C1))=S$(DR):K$(((X*5)-5)+N(C1))=K$(DR)
70 C(((X*5)-5)+N(C1))=C(DR):S(((X*5)-5)+N(C1))=S(DR):DR=DR+1:RETURN

75 CLS:PRINT@43,;:M$="DRAW POKER":GOSUB7:PRINT@103,"BY JIM MENICK 1984":PRINT:PRINT@225,"HOUSE RULES:"
76 PRINT" $1 ANTE",," JACKS OR BETTER TO OPEN"," 3 RAISE LIMIT",," $5 BETTING LIMIT"
78 PRINT@500,"HIT space";
80 M$=INKEY$:C1=RND(0):IFM$=" "ORM$="Y"ORM$="N"THENRETURN
81 GOTO80

90 FORX=1TO5
100 IFP(X)<>0THEN1550
130 FORY=1TO5:V(X,Y)=C(Y+((X-1)*5)):V$(X,Y)=C$(Y+((X-1)*5)):Z(X,Y)=S(Y+((X-1)*5)):Z$(X,Y)=S$(Y+((X-1)*5)):NEXT

REM ONE PAIR
200 FORN=1TO4:FORI=2TO5:IFN<>IANDV(X,N)=V(X,I)THENH$(X)="P"+RIGHT$(STR$(N),1)+RIGHT$(STR$(I),1):N=4:I=5
250 NEXTI,N:IFLEFT$(H$(X),1)<>"P"THEN610
270 C1=2:GOSUB2:IFV(X,C3)>=11THENH$(X)=H$(X)+"J"
280 M$=MID$(H$(X),2,1):IFM$="3"THEN390
290 IFM$="4"ANDRIGHT$(H$(X),1)<>"J"THEN610
300 IFM$="4"THEN1360
310 C1=3:GOSUB1:C1=2:GOSUB2:FORN=1TO4:FORI=N+1TO5:IFN=C3ORN=C2THENI=5:GOTO350
340 IFV(X,N)=V(X,I)THENH$(X)="2"+LEFT$(H$(X),3)+RIGHT$(STR$(N),1)+RIGHT$(STR$(I),1):I=5:N=4
350 NEXTI,N:C1=3:GOSUB1:C1=5:GOSUB2:IFV(X,C2)=V(X,C3)THENH$(X)="FOUR "+V$(X,C2)+"S":GOTO1360
370 IFLEFT$(H$(X),1)="2"ANDMID$(H$(X),3,1)="1"THEN530
380 IFLEFT$(H$(X),1)="2"THEN1360
390 C1=3:GOSUB1:C1=2:GOSUB2:FORN=1TO5:IFN=C3ORN=C2THEN420
410 IFV(X,N)=V(X,C3)THENH$(X)="T"+MID$(H$(X),2,2)+RIGHT$(STR$(N),1):N=5
420 NEXTN:IFLEFT$(H$(X),1)="T"ORRIGHT$(H$(X),1)="J"THEN1360
450 GOTO610

530 C1=3:GOSUB1:FORN=1TO5:FORI=3TO6:IFN=VAL(MID$(H$(X),I,1))THENI=6:F=1
560 NEXT:IFF=1THENF=0:GOTO590
570 IFV(X,N)=V(X,C2)THENH$(X)="FULL HOUSE "+V$(X,C2)+"S":N=5
580 IFV(X,N)=V(X,VAL(RIGHT$(H$(X),1)))THENH$(X)="FULL HOUSE "+V$(X,VAL(RIGHT$(H$(X),1)))+"S":N=5
590 NEXT:GOTO1360

610 FORN=1TO5:FORI=1TO5:IFD$(X)="ON"ANDN=S4THENI=5:GOTO650
635 IFD$(X)="ON2"ANDN=STHENI=5:GOTO650
640 IFV(X,N)=V(X,I)+3THENS=I:S4=N:N=5:I=5:F=1
650 NEXTI,N:IFF=1THENF=0:GOTO760
660 IFI(X)=0THEN720
670 I(X)=0
680 FORN=1TO5:IFV(X,N)=1THENV(X,N)=14
700 NEXT:IFD$(X)="ON"THEND$(X)="ON2":GOTO610
710 D$(X)="":GOTO910

720 FORN=1TO5:IFV(X,N)=14THENV(X,N)=1
740 NEXT:I(X)=1:GOTO610

760 FORN=1TO5:IFV(X,N)=V(X,S)+2THENS3=N:N=5:F=1
780 NEXT:IFF=1THENF=0:GOTO810
790 IFD$(X)=""THEND$(X)="ON":GOTO610
800 GOTO680

810 FORN=1TO5:IFV(X,N)=V(X,S)+1THENS2=N:N=5:F=1
830 NEXT:IFF=1THENF=0:GOTO850
840 GOTO680

850 H$(X)="4S"+RIGHT$(STR$(S),1)+RIGHT$(STR$(S2),1)+RIGHT$(STR$(S3),1)+RIGHT$(STR$(S4),1)
860 FORN=1TO5:IFV(X,N)=V(X,S)+4THENH$(X)="STRAIGHT TO "+V$(X,N):E(X)=V(X,N)
880 IFV(X,N)=V(X,S)-1THENH$(X)="STRAIGHT TO "+V$(X,S4):E(X)=V(X,S4)
890 NEXT:GOTO1140

REM INSIDE STRAIGHT
910 IFX=2ORX=3ORX=5ORH$(X)<>""THEN1140
930 FORN=1TO5:FORI=1TO5:IFD$(X)="ON"ANDN=S5(X)THENI=5:GOTO970
955 IFD$(X)="ON2"ANDN=STHENI=5:GOTO970
960 IFV(X,N)=V(X,I)+4THENS=I:S5(X)=N:N=5:I=5:F=1
970 NEXTI,N:IFF=1THENF=0:GOTO1070
980 IFI(X)=0THEN990
985 I(X)=0:GOTO1030

990 FORN=1TO5:IFV(X,N)=14THENV(X,N)=1
1010 NEXT:I(X)=1:GOTO930

1030 FORN=1TO5:IFV(X,N)=1THENV(X,N)=14
1050 NEXT:IFD$(X)="ON"THEND$(X)="ON2":GOTO930
1060 I(X)=0:D$(X)="":GOTO1140

1070 FORN=1TO5:FORI=1TO5:IFV(X,N)=V(X,S)+3ANDV(X,I)=V(X,S)+2THENC1=1:GOSUB16
1100 IFV(X,N)=V(X,S)+3ANDV(X,I)=V(X,S)+1THENC1=2:GOSUB16
1110 IFV(X,N)=V(X,S)+2ANDV(X,I)=V(X,S)+1THENC1=3:GOSUB16
1120 NEXTI,N:IFLEFT$(H$(X),1)<>"I"ANDD$(X)=""THEND$(X)="ON":GOTO930

1140 FORN=2TO5:IFZ(X,N)<>Z(X,1)THENN=5:F=1
1160 NEXT:IFF=1THENF=0:GOTO1200
1170 IFLEFT$(H$(X),4)<>"STRA"THENH$(X)="FLUSH "+Z$(X,1)+Z$(X,1)+Z$(X,1)
1180 IFLEFT$(H$(X),4)="STRA"THENH$(X)="STRAIGHT FLUSH"
1190 GOTO1360

1200 FORN=1TO5:FORC2=1TO5:FORC3=1TO5:IFC2=C3ORN=C3ORN=C2THEN1250
1240 IFZ(X,N)=Z(X,C2)ANDZ(X,N)=Z(X,C3)THENH$(X)=H$(X)+"3F"+Z$(X,N):F(X)=N:F2=C2:F3=C3:N=5:C2=5:C3=5:F=1
1250 NEXTC3,C2,N:IFF=0THEN1360
1251 F=0

1270 FORC1=1TO5:IFC1=F2ORC1=F3ORC1=F(X)THEN1300
1290 IFZ(X,C1)=Z(X,F(X))THENH$(X)="4F"+Z$(X,F(X)):C1=5:F=1
1300 NEXT:IFF=1THENF=0:GOTO1360
1310 IFX<>1ANDLEFT$(H$(X),2)="3F"THENH$(X)="":GOTO1360
1320 IFLEFT$(H$(X),2)="3F"THEN1360
1330 FORI=1TOLEN(H$(X)):IFMID$(H$(X),I,2)="3F"THENH$(X)=LEFT$(H$(X),I-1)
1350 NEXT

REM ASSIGN H(X) VALUES
1360 IFH$(X)=""THENH(X)=0
1381 M$=LEFT$(H$(X),2):FORC1=1TO4:IFM$=MID$(" IS3F4S4F",C1*2,2)THENH(X)=C1:C1=4
1382 NEXTC1:IFLEFT$(H$(X),1)="P"THENH(X)=5:IFLEN(H$(X))>3THENH(X)=6
1450 IFM$="2P"THENH(X)=7
1460 IFLEFT$(H$(X),1)="T"THENH(X)=8
1470 IFLEFT$(H$(X),10)="STRAIGHT T"THENH(X)=9
1480 FORC1=1TO3:IFM$=MID$(" FLFUFO",C1*2,2)THENH(X)=9+C1:C1=3
1490 NEXTC1:IFLEFT$(H$(X),10)="STRAIGHT F"THENH(X)=13
1520 IFE=1THENRETURN
1540 ONXGOSUB1570,1630,1710,1900
1550 NEXT:RETURN

REM MARVIN
1570 IFH(1)<8THENQ(1)=1:J(1)=0
1590 IFH(1)=8ORH(1)=9THENQ(1)=2:J(1)=2
1600 IFH(1)>9THENQ(1)=3:J(1)=5
1610 G(1)=21:RETURN

REM GERRY
1630 IFH(2)<3THENG(2)=0
1650 IFH(2)=3ORH(2)=4THENJ(2)=0:G(2)=6
1660 IFH(2)>4ANDH(2)<8THENQ(2)=1:J(2)=0:G(2)=4:IFH(2)>5THENG(2)=14
1670 IFH(2)=8ORH(2)=9THENQ(2)=2:J(2)=1:G(2)=21
1680 IFH(2)=10THENQ(2)=3:J(2)=3:G(2)=21
1690 IFH(2)>10THENQ(2)=3:J(2)=5:G(2)=21
1700 RETURN

REM WALTER
1710 IFH(3)<3THENG(3)=0
1730 IFH(3)=3ORH(3)=4THENJ(3)=3:G(3)=6
1740 IFH(3)<8ANDH(3)>4THENQ(3)=1:J(3)=0:G(3)=4:IFH(3)>5THENG(3)=10
1750 IFH(3)=8THENQ(3)=2:J(3)=2:G(3)=21
1760 IFH(3)>=9THENQ(3)=3:J(3)=3:G(3)=21
1770 IFH(3)=6THENGOSUB1800:RETURN
1771 IFH(3)=8THENGOSUB1850
1780 RETURN

1800 FORI=1TO5:IFI=VAL(MID$(H$(3),2,1))ORI=VAL(RIGHT$(H$(3),1))THEN1830
1820 C1=2:GOSUB2:IFV(X,C3)<>14ANDV(3,I)=14THEN1825
1821 GOTO1830
1825 H$(3)=H$(3)+"K"+RIGHT$(STR$(I),1):Q(3)=2:J(3)=4:G(3)=21
1830 NEXT:RETURN

1850 FORI=1TO5:IFI=VAL(MID$(H$(3),2,1))ORI=VAL(MID$(H$(3),2,2))ORI=VAL(RIGHT$(H$(3),1))THEN1880
1870 C1=2:GOSUB2:IFV(X,C3)<>14ANDV(3,I)=14THENH$(3)=H$(3)+"K"+RIGHT$(STR$(I),1):Q(3)=1:J(3)=4:G(3)=21
1880 NEXT:RETURN

REM BETSY
1900 IFH(4)<3THENG(4)=0
1920 IFR(4)>50ANDH(4)<3THENJ(4)=0:G(4)=6
1930 IFH(4)>2ANDH(4)<6THENJ(4)=0:G(4)=6
1940 IFH(4)=6THENQ(4)=1:J(4)=0:G(4)=9
1950 IFH(4)=7ORH(4)=8THENQ(4)=2:J(4)=2:G(4)=21
1960 IFH(4)>8THENQ(4)=3:J(4)=3:G(4)=21
1970 RETURN

REM BET DRAW CARD AND FIGURE HANDS
2000 DR=26:X=D+1:IFX=6THENX=1
2030 IFP(X)<>0THEN4190
2035 FORI=0TO3:N(I)=0:NEXT:C1=X:GOSUB6500:IFX=5THEN3470
2060 ONH(X)GOTO2320,2520,2910,3000,3070,3140,3220,3310
2070 IFH(X)>8THENPRINT@L,"OKAY";:GOSUB9:GOSUB27180:GOTO4190

2080 D(X)=3:GOSUB19:FORI=1TO5:FORC1=1TO5:IFV(X,I)<V(X,C1)THENC1=5:F=1
2120 NEXT:IFF=1THENF=0:GOTO2140
2130 K1=I:V(X,K1)=V(X,I):I=5
2140 NEXT:FORI=1TO5:IFI=K1THEN2220
2160 FORC1=1TO5:IFC1=K1THEN2200
2190 IFV(X,I)<V(X,C1)THENC1=5:F=1
2200 NEXT:IFF=1THENF=0:GOTO2220
2210 K2=I:V(X,K2)=V(X,I):I=5
2220 NEXT:FORI=1TO3:IFI>1THENN(I)=N(I-1)+1:GOTO2270
2260 N(I)=N(I)+1
2270 IFN(I)=K1ORN(I)=K2THEN2260
2300 NEXT:GOSUB20:GOSUB40:GOSUB50:GOTO2820

2320 D(X)=1:GOSUB19:N(1)=1
2340 FORI=3TO7:IFN(1)=VAL(MID$(H$(X),I,1))THENN(1)=N(1)+1:I=7:F=1
2360 NEXT:IFF=1THENF=0:GOTO2340
2370 GOSUB20:IFC(DR-1)=VAL(MID$(H$(X),7))THENE(X)=V(X,S5(X)):H(X)=9:H$(X)="STRAIGHT TO "+V$(X,S5(X)):GOTO3750
2390 IFI(X)=0THEN2440
2400 FORN=1TO5:IFV(X,N)=1THENV(X,N)=14
2420 NEXT:I(X)=0
2440 FORY=1TO5:IFY<>N(1)ANDC(DR-1)=V(X,Y)THENH$(X)="PAIR OF "+C$(DR-1)+"S"
2470 NEXTY:IFLEFT$(H$(X),1)<>"P"THEN3750
2500 E(X)=C(DR-1):H(X)=6:IFE(X)<11THENH(X)=5
2510 GOTO3750

2520 D(X)=2:GOSUB19:FORI=1TO5:IFZ$(X,I)<>Z$(X,F(X))THENN(2)=I:I=5
2550 NEXT:FORI=N(1)+1TO5:IFZ$(X,I)<>Z$(X,F(X))THENN(2)=I:I=5
2580 NEXT:GOSUB20:GOSUB40:IFS$(DR-2)<>Z$(X,F(X))ORS$(DR-1)<>Z$(X,F(X))THEN2820
2620 H$(X)="FLUSH "+S$(DR-1)+S$(DR-1)+S$(DR-1)

REM CHECK FOR STRAIGHT FLUSH: NOTE S5 WAS INCORRECTLY ASSIGNED INSTEAD OF S5(X)
2630 I(X)=0
2640 FORC1=1TO5:FORC2=1TO5:IFV(X,C2)=V(X,C1)+4THENS=C1:S5(X)=C2:C1=5:C2=5:F=1
2643 NEXTC2,C1:IFF=1THENF=0:GOTO2649
2644 IFI(X)=1THENGOSUB2740:GOTO3750
2645 FORI=1TO5:IFV(X,I)=14THENV(X,I)=1
2647 NEXT:GOTO2640

2649 FORC1=1TO5:IFV(X,C1)=V(X,S)+1THENS2=C1:C1=5:F=1
2651 NEXT:IFF=1THENF=0:GOTO2653
2652 GOSUB2740:GOTO3750
2653 FORC1=1TO5:IFV(X,C1)=V(X,S2)+1THENS3=C1:C1=5:F=1
2655 NEXT:IFF=1THENF=0:GOTO2657
2656 GOSUB2740:GOTO3750

2657 FORC1=1TO5:IFV(X,C1)=V(X,S3)+1THENH$(X)="STRAIGHT FLUSH":C1=5:F=1
2659 NEXT:IFF=0THENGOSUB2740:GOTO3750
2660 F=0:H(X)=13
2730 GOTO3750

2740 I(X)=0:FORI=1TO5:IFV(X,I)=1THENV(X,I)=14
2743 NEXT:FORI=1TO5:FORN=1TO5:IFV(X,I)<V(X,N)THENN=5:F=1
2770 NEXT:IFF=1THENF=0:GOTO2790
2780 E(X)=V(X,I):I=5
2790 NEXT:IFE(X)=0THENE(X)=V(X,5)
2800 H(X)=10:RETURN
2820 E=1:GOSUB100:E=0
2840 GOSUB2901
2850 GOSUB2903:IFH(X)=9THENE(X)=V(X,F3)
2890 IFH(X)=11ORH(X)=12THENGOSUB32000
2900 GOTO3750

2901 C1=2:GOSUB2:IFH(X)=6THENE(X)=V(X,C3):H$(X)="PAIR OF "+V$(X,C3)+"S"
2902 RETURN
2903 C1=3:GOSUB1:C1=5:GOSUB2:IFH(X)=7ANDC2>C3THENE(X)=V(X,C2):H$(X)="TWO PAIRS "
2904 IFH(X)=7ANDC2<C3THENE(X)=V(X,C3):H$(X)="TWO PAIRS"
2905 IFH(X)=8THENE(X)=V(X,VAL(RIGHT$(H$(X),1))):H$(X)="THREE "+V$(X,VAL(RIGHT$(H$(X),1)))+"S"
2906 RETURN

2910 D(X)=1:GOSUB19:N(1)=1
2930 FORI=3TO7:IFN(1)=VAL(MID$(H$(X),I,1))THENN(1)=N(1)+1:I=7:F=1
2950 NEXT:IFF=1THENF=0:GOTO2930
2960 GOSUB20
2970 C1=3:GOSUB1:IFC(DR-1)=V(X,C2)-1THEN2975
2971 GOTO2980
2975 C1=6:GOSUB1:E(X)=V(X,C2):H$(X)="STRAIGHT TO "+V$(X,C2):H(X)=9:GOTO3750

2980 C1=6:GOSUB1:IFC(DR-1)=V(X,C2)+1THENH$(X)="STRAIGHT TO "+C$(DR-1):E(X)=C(DR-1):H(X)=9:GOTO3750
2985 C1=3:GOSUB1:IFC(DR-1)=14ANDC2=2THENC(DR-1)=1:I(X)=1:GOTO2970
2990 V(X,N(1))=C(DR-1):GOTO2390

3000 D(X)=1:GOSUB19:FORI=1TO5:IFZ$(X,I)<>Z$(X,F(X))THENN(1)=I:I=5
3030 NEXT:GOSUB20:IFS$(DR-1)=Z$(X,F(X))THEN2620
3060 GOTO2820

3070 D(X)=3:GOSUB19:FORI=1TO3:N(I)=N(I-1)+1
3100 C1=3:GOSUB1:C1=2:GOSUB2:IFN(I)=C3ORN(I)=C2THENN(I)=N(I)+1:GOTO3100
3110 NEXT
3120 GOSUB20:GOSUB40:GOSUB50:GOTO2820

3140 IFLEN(H$(X))=4THEN3070
3150 D(X)=2:GOSUB19:C1=3:GOSUB1:C1=2:GOSUB2:C1=VAL(RIGHT$(H$(X),1)):FORI=1TO2:N(I)=N(I-1)+1
3180 IFN(I)=C3ORN(I)=C2ORN(I)=C1THENN(I)=N(I)+1:GOTO3180
3190 NEXT:GOSUB20:GOSUB40:GOTO2820

3220 D(X)=1:GOSUB19:N(1)=1
3240 FORI=3TO7:IFN(1)=VAL(MID$(H$(X),I,1))THENN(1)=N(1)+1:I=7:F=1
3260 NEXT:IFF=1THENF=0:GOTO3240
3270 GOSUB20:C1=3:GOSUB1:IFC(DR-1)=V(X,C2)THEN3305
3290 IFC(DR-1)=V(X,VAL(MID$(C$(X),5,1)))THEN3305
3300 GOTO2850

3305 H$(X)="FULL HOUSE, "+C$(DR-1)+"S":E(X)=C(DR-1):H(X)=11:GOTO3750

3310 IFLEN(H$(X))>4THEND(X)=1:GOSUB19
3320 IFLEN(H$(X))=4THEND(X)=2:GOSUB19

3340 FORI=1TO2:N(I)=N(I-1)+1
3360 C1=3:GOSUB1:C1=2:GOSUB2:IFN(I)=C3ORN(I)=C2ORN(I)=VAL(MID$(H$(X),4,1))THENN(I)=N(I)+1:GOTO3360
3370 NEXT:GOSUB20:IFLEN(H$(X))>4THEN3430
3381 GOSUB40:IFC(DR-1)=C(DR-2)THENH$(X)="FULL HOUSE, "+V$(X,VAL(RIGHT$(H$(X),1)))+"S":H(X)=11:GOSUB32000
3410 C1=VAL(RIGHT$(H$(X),1)):IFC(DR-1)=V(X,C1)ORC(DR-2)=V(X,C1)THENH$(X)="FOUR "+V$(X,C1):GOSUB32000
3420 C1=2:GOSUB2:IFLEFT$(H$(X),1)="T"THENE(X)=V(X,C3):H$(X)="THREE "+V$(X,C3)+"S"
3425 GOTO3750

3430 C1=2:GOSUB2:IFC(DR-1)=14THENH$(X)="FULL HOUSE, "+V$(X,C3)+"S":GOSUB32000
3440 IFC(DR-1)=V(X,C3)THENH$(X)="FOUR "+V$(X,C3):GOSUB32000
3450 GOTO3420

3470 PRINT@L,"HOW MANY CARDS";:INPUTD(5):GOSUB6400:IFD(5)<>INT(D(5))ORD(5)>3ORD(5)<0THEN3470
3500 IFD(5)=0THENPRINT@L,"OKAY":GOTO4190

3510 I=0:FORN=21TO25
3525 GOSUB6400:PRINT@L,"REPLACE "C$(N)S$(N)" (y/n)?";:GOSUB80:PRINT@L,"":IFM$="N"THEN3590
3560 IFM$<>"Y"THEN3525
3561 K$(N)=K$(DR):C$(N)=C$(DR):S$(N)=S$(DR):C(N)=C(DR):S(N)=S(DR):DR=DR+1:I=I+1:IFI=D(5)THENN=25
3590 NEXT:IFI<>D(5)THENM$="YOU ONLY TOOK"+STR$(I):GOSUB3:GOSUB78:GOTO3510
3610 L=298:FORN=21TO25:PRINT@L,K$(N)C$(N)S$(N);:GOSUB8:L=L+32:NEXT:GOTO4190

3750 L=160+(X-1)*10-(X=4)*226
3760 FORI=1TOD(X):PRINT@L,"  ";:L=L-32:NEXT:PRINT@L,"??";:L=L+32:GOSUB8:FORI=1TOD(X):PRINT@L,"??";:L=L+32:GOSUB8:NEXT

4190 X=X+1:IFX=6THENX=1
4210 IFX=1ANDD<>5THEN2030
4220 IFX<>1ANDX<>D+1THEN2030
4230 RETURN

5000 FORX=1TO4:IFP(X)<>0THEN5030
5020 ONXGOSUB5050,5110,5190,5270
5030 NEXT:RETURN

REM MARVIN
5050 IFH(1)<6THENG(1)=0:Q(1)=0
5070 IFH(1)<8ANDH(1)>5THENQ(1)=1:J(1)=0:G(1)=14
5080 IFH(1)>7ANDH(1)<10THENQ(1)=2:J(1)=2:G(1)=21
5090 IFH(1)>9THENQ(1)=3:J(1)=5:G(1)=21
5100 RETURN

REM GERRY
5110 IFH(2)=3ORH(2)=4THENL(X)=RND(10):IFL(X)<3THENQ(2)=2:J(2)=3:G(2)=14:RETURN
5130 IFH(2)<6THENG(2)=0:Q(2)=0
5140 IFH(2)=6ORH(2)=7THENQ(2)=1:J(2)=0:G(2)=8
5150 IFH(2)=8ORH(2)=9THENQ(2)=2:J(2)=1:G(2)=12:IFH(2)=9THENG(2)=21
5160 IFH(2)=10THENQ(2)=3:J(2)=3:G(2)=21
5170 IFH(2)>10THENQ(2)=3:J(2)=5:G(2)=21
5180 RETURN

REM WALTER
5190 IFH(3)<3THENG(3)=0:Q(3)=0
5210 IFH(3)=3ORH(3)=4THENL(X)=RND(10):IFL(X)=7THENQ(3)=2:J(3)=3:G(3)=10
5220 IFL(X)<>7ANDH(3)<6THENG(3)=0:Q(3)=0
5230 IFH(3)<8ANDH(3)>5THENQ(3)=1:J(3)=0:G(3)=7
5240 IFH(3)=8THENQ(3)=2:J(3)=2:G(3)=21
5250 IFH(3)>=9THENQ(3)=3:J(3)=3:G(3)=21
5260 RETURN

REM BETSY
5270 IFH(4)<9THENL(X)=RND(4):IFL(X)=4THENQ(4)=3:J(4)=3:G(4)=21:GOTO5340
5290 IFH(4)<6THENG(4)=0:Q(4)=0
5300 IFH(4)=6THENQ(4)=2:J(4)=0:G(4)=8
5310 IFH(4)=7THENQ(4)=2:J(4)=1:G(4)=10
5320 IFH(4)=8THENQ(4)=2:J(4)=2:G(4)=12
5330 IFH(4)>8THENQ(4)=3:J(4)=3:G(4)=21
5340 IFR(4)>50ANDG(4)>0THEN5345
5341 RETURN
5345 Q(4)=Q(4)+1:J(4)=J(4)+1:IFR(4)>100THENQ(4)=Q(4)+1:J(4)=J(4)+1:G(4)=G(4)+1
5350 RETURN

REM SHUFFLE
6000 CLS:N=-1:PRINT"shuffling":FORX=1TO40
6070 C(X)=RND(52):IFE(C(X))=1THEN6070
6090 E(C(X))=1:S(X)=INT((C(X)-1)/13)+1:S$(X)=MID$("SHDC",5-S(X),1)
6180 IFC(X)>13THENC(X)=C(X)-13:GOTO6180
6190 C$(X)=MID$("123456789TJQK",C(X),1)
6230 IFC(X)=1THENC$(X)="A":C(X)=14
6240 K$(X)=C$(X)+S$(X):IFX/6=INT(X/6)THENGOSUB6270
6250 NEXT:FORX=1TO52:E(X)=0:NEXT:RETURN

REM ANTE
6270 IFN=-1THENPRINT:PRINT"PLEASE ANTE:":N=0:RETURN
6280 N=N+1:IFN=5THEN6320
6290 IFP(N)<>0THENPRINTP$(N)" IS BUSTED":RETURN
6300 R(N)=R(N)-1:PRINTP$(N)" IS IN":P=P+1:RETURN
6320 PRINT"ARE YOU IN?":GOSUB80:IFM$="Y"ORM$=" "THENR(5)=R(5)-1:P=P+1:RETURN
6340 END

6400 C1=5:PRINT@458,""
6500 L=192+(C1-1)*10-(C1>3)*226:PRINT@L,B$;:PRINT@L+32,B$;:RETURN

REM FIRST ROUND OF BETTING
8000 A=0:R=1:J=D+1:IFJ=6THENJ=1
8050 IFP(J)=0THEN8110
8060 J=J+1:IFJ=6THENJ=1
8070 R=R+1:IFR=6THEND2=1:D=D+1:GOTO8090
8080 GOTO8050
8090 IFD=6THEND=1
8095 IFP(D)=1THEND=D+1:GOTO8090
8100 GOSUB29170:CLS:RETURN

8110 C1=J:GOSUB6500:IFJ<>5THEN8250
8130 GOSUB6:IFH(5)<6ANDC1>0THENGOSUB6400:PRINT@L,"YOU CAN'T":J=1:GOTO8070
8160 IFC1>5ORC1>R(5)ORC1<0ORC1<>INT(C1)THENGOSUB18:GOTO8130
8170 IFC1=0THENJ=1:GOTO8070
8240 R(5)=R(5)-B(J):P=P+B(J):K(J)=B(J):GOTO8330
8250 IFH(J)>=6ANDR(J)>=Q(J)THENPRINT@L,"OPEN";:C1=Q(J):GOSUB15:R(J)=R(J)-Q(J):P=P+Q(J):K(J)=Q(J):GOTO8330
8260 IFH(J)>=6THENPRINT@L,"OPEN $1";:R(J)=R(J)-1:P=P+1:K(J)=1:GOTO8330
8270 M$="I CAN'T OPEN":GOSUB3:GOSUB9:GOTO8060
8330 B=K(J):ST=J:R=1:GOSUB9

REM BET CONTINUE AFTER OPENER
8350 J=J+1:IFJ=6THENJ=1
8360 IFP(J)<>0THENR=R+1:GOTO8530
8370 GOSUB31000:C1=J:GOSUB6500:IFJ<>5THEN8480
8400 GOSUB6:IFC1>R(5)ORC1>B-K(J)+5ORC1<0OR(C1>B-K(J)ANDA=3)ORC1<>INT(C1)THENGOSUB18:GOTO8400
8430 IFC1<>0ANDC1<B-K(J)THEN8425
8440 IFC1=0THENP(5)=10:GOSUB11
8450 R(5)=R(5)-B(J):P=P+B(J):R=R+1
8460 IFB(J)>B-K(J)THENJ(J)=B(J)-B-K(J):B=B+J(J):A=A+1:R=1
8470 K(J)=B:GOTO8520
8480 IFG(J)<=BTHENPRINT@L,"I'M OUT";:R=R+1:P(J)=10:GOSUB11:GOTO8520
8490 IFA<3ANDJ(J)>0THENPRINT@L,"RAISE";:C1=J(J):GOSUB15:R(J)=R(J)-J(J)-B+K(J):P=P+J(J)+B-K(J):R=1
8500 IFA<3ANDJ(J)>0THENB=B+J(J):K(J)=B:A=A+1:GOTO8520
8510 R=R+1:P=P+B-K(J):R(J)=R(J)-B+K(J):K(J)=B:GOSUB10
8520 GOSUB9
8530 IFR<>5THEN8350

8540 B=0:K(J)=0:GOSUB31000:SO=0:FORN=1TO5:IFP(N)<>0THENSO=SO+1
8580 NEXT:IFSO<>4THENSO=0:RETURN
8600 FORN=1TO5:IFP(N)=0THENW=N
8620 NEXT:GOSUB28250:W1=1:RETURN

REM SECOND ROUND OF BETTING
9000 FORJ=1TO5:K(J)=0:NEXT:J=ST:R=1:A=0:GOTO9080
9070 J=J+1:IFJ=6THENJ=1
9080 IFP(J)<>0THENR=R+1:GOTO9260
9090 GOSUB31000:C1=J:GOSUB6500:IFJ<>5THEN9200

9120 GOSUB6:IFC1>R(5)ORC1>B-K(J)+5ORC1<0OR(C1>B-K(J)ANDA=3)ORC1<>INT(C1)THEN9145
9141 GOTO9150
9145 GOSUB18:GOTO9120
9150 IFB(J)<>0ANDB(J)<>0ANDB(J)<B-K(J)THENGOSUB18:GOTO9120
9160 IFB(J)=0ANDB>0THENP(5)=10:GOSUB11
9170 R(5)=R(5)-B(J):P=P+B(J):R=R+1:IFB=0THENB=B(J):R=1
9180 IFB(J)>B-K(J)THENJ(J)=B(J)-B-K(J):B=B+J(J):IFB<>0THENA=A+1:R=1
9190 K(J)=B:GOTO9250

9200 IFQ(J)>0ANDA=0ANDB=0THEN9205
9201 GOTO9210
9205 PRINT@L,"BET";:C1=Q(J):GOSUB15:K(J)=Q(J):B=Q(J)
9206 R=1:P=P+B:R(J)=R(J)-B:GOTO9250
9210 IFG(J)<BTHENPRINT@L,"I'M OUT";:R=R+1:P(J)=10:GOSUB11:GOTO9250
9220 IFA<3ANDJ(J)>0THENPRINT@L,"RAISE";:C1=J(J):GOSUB15:R(J)=R(J)-J(J)-B+K(J):R=1
9230 IFA<3ANDJ(J)>0THENP=P+J(J)+B-K(J):B=B+J(J):K(J)=B:A=A+1:GOTO9250
9240 K(J)=B-K(J):R=R+1:P=P+K(J):R(J)=R(J)-K(J):GOSUB10

9250 GOSUB9
9260 IFR<>5THEN9070
9270 B=0:K(J)=0:GOSUB31000

REM CHECK FOR DEFAULT WINNER
9300 FORN=1TO5:IFP(N)<>0THENSO=SO+1
9310 NEXT:IFSO<>4THENRETURN
9330 GOTO8600

10020 GOSUB6000:GOSUB18000:GOSUB90:GOSUB8000:IFD2=1THEND2=0:GOTO10020
10056 IFW1=1THENW1=0:GOTO10140
10060 GOSUB19000:GOSUB2000:GOSUB5000:GOSUB19000:GOSUB9000:IFW1=1THENW1=0:GOTO10140
10110 GOSUB19000:GOSUB27000:GOSUB28000
10140 GOSUB29000:GOTO10020

REM DEAL
18000 CLS:FORC1=1TO4:IFP(C1)=0THENGOSUB12:PRINT@L,P$(C1);
18130 NEXT:PRINT@266,P$(5);:FORX=1TO5:L=32*X:FORY=1TO5:L=L-(Y>1)*10-(Y=4)*226:IFP(Y)<>0THEN18200
18190 IFY<>5THENPRINT@L,"??";
18195 IFY=5THENPRINT@L,K$(20+X);
18200 NEXTY,X:GOTO31000

REM CLEAR SCREEN A BIT FOR TIDINESS
19000 FORC1=1TO5:GOSUB6500:NEXT:A=0:B=0:FORI=1TO5:L(I)=0:NEXT:GOTO31000

REM DECLARATION
27000 GOSUB13:FORX=1TO5:IFP(X)<>0THEN27080
27040 C1=X:GOSUB6500:IFX=5THENGOSUB27100:M$=H$(5):GOSUB3:GOTO27080
27060 IFH(X)>5THENM$=H$(X):GOSUB3
27070 IFH(X)<=5THENPRINT@L,"ZILCH!";
27080 NEXT:RETURN

REM TRANLATE PREVIOUSLY UNTRANSLATED HAND:IN H$(X)+E(X)
27100 E=1:GOSUB130:GOSUB1360:E=0:IFH(X)<6THENH$(5)="DOODLY SQUAT":RETURN
27130 GOSUB2901:GOSUB2903
27180 IFH(X)=10THENGOSUB2740
27190 IFH(X)=11ORH(X)=12THENGOSUB32000
27200 RETURN

REM WHO WON?
28000 FORX=1TO5:IFP(X)<>0THEN28070
28030 FORI=1TO5:IFH(X)<H(I)THENI=5:F=1
28050 NEXT:IFF=1THENF=0:GOTO28070
28060 W=X:X=5
28070 NEXT

28080 Y=2:T(1)=W:FORN=1TO5:IFP(N)<>0ORN=WTHEN28120
28110 IFH(N)=H(W)THENT(Y)=N:Y=Y+1
28120 NEXT:IFY=2THEN28250

28140 FORX=1TOY-1:FORI=1TOY-1:IFE(T(X))<E(T(I))THENI=Y-1:F=1
28170 NEXT:IFF=1THENF=0:GOTO28185
28180 W=T(X):X=5
28185 NEXT:M2=2:M2(1)=W:FORN=1TOY-1:IFT(N)=WTHEN28230
28220 IFE(T(N))=E(W)THENM2(M2)=T(N):M2=M2+1
28230 NEXT:IFM2>2THEN28320

REM WINNER'S CIRCLE
28250 C1=W:GOSUB12:C2=W:GOSUB28300:GOSUB9:R(W)=R(W)+P:P=0:GOTO78

28300 L=L+35:PRINT@L,"win";:IFC2<>5THENPRINT"s";
28310 RETURN

28320 IFT1=0ANDH(M2(W))=7THEN28370
28325 FORN=1TOM2-1:C1=M2(N):GOSUB12:C2=M2(N):GOSUB28300:R(M2(N))=R(M2(N))+INT(P/(M2-1)):NEXT:GOSUB9:P=0:GOSUB6400
28361 PRINT@L,"MULTIPLE WINNERS":GOTO78

28370 FORC2=1TO2:FORI=1TO4:FORC1=2TO5:IFI=C1ORV(M2(C2),I)=E(M2(2))THEN28420
28410 IFV(M2(C2),I)=V(M2(C2),C1)THENE(M2(C2))=V(M2(C2),I)
28420 NEXTC1,I,C2

28430 T(1)=M2(1):T(2)=M2(2):Y=3:T1=1:GOTO28140

29000 CLS:FORX=1TO5:PRINT:PRINT:IFP(X)=10THENP(X)=0
29040 IFP(X)<>0THENPRINTP$(X)+" IS OUT!":GOTO29090
29050 IFR(X)<1ANDX=5THENPRINT"YOU'RE BROKE":END
29060 IFR(X)<5ANDX<>5THENPRINTP$(X)" HAS ONLY";:C1=R(X):GOSUB15:PRINT". GOODBYE":GOTO29090
29070 IFX<5THENPRINTP$(X);:C1=R(X):GOSUB15:GOTO29090
29071 PRINT"YOU";:C1=R(5):GOSUB15
29090 NEXT:GOSUB78

REM CLEAR THE DECKS
29170 B=0:FORI=1TO5:B(I)=0:K(I)=0:I(I)=0:E(I)=0:D(I)=0:Q(I)=0:L(I)=0:D$(I)="":F(I)=0:H$(I)="":H(I)=0:NEXT
29220 A=0:T1=0:FORI=0TO3:N(I)=0:NEXT:E=0:SO=0:FORI=1TO5:T(I)=0:NEXT
29270 W=0:ST=0:RETURN

REM SET ONGOING INFORMATION
31000 PRINT@458,"":PRINT@490,B$B$;:IFST<>0THENPRINT@276,"OPENER:":PRINT@308,P$(ST);
31030 C1=D:GOSUB12:PRINT@L,;:M$=P$(D):GOSUB7:PRINT@340,"BET";:C1=B-K(J):GOSUB15:PRINT@372,"RAISE #"A
31110 PRINT@404,"POT";:C1=P:GOSUB15:PRINT@269,;:C1=R(5):GOSUB15:RETURN

REM FULL HOUSE AND FOUR OF A KIND E(X) ROUTINE
32000 M$=RIGHT$(H$(X),2):FORC1=1TO5:IFM$=MID$(" TSJSQSKSAS",C1*2,2)THENE(X)=9+C1:C1=5:NEXT:RETURN
32001 NEXT:E(X)=VAL(MID$(H$(X),LEN(H$(X))-1,1)):RETURN

REM INIT
50000 DIMI,N,X,S,H$(5),V(5,5),V$(5,5),R(5),C(40),C$(40),S(40),S$(40),K$(40),E(52),Z(5,5),Z$(5,5),N(5),H(5)
50010 DIMD(5),P(5),P$(5),Q(5),G(5),J(5),I(5),T(5),F(5),D$(5),K(5),B(5),L(5),S5(5):FORX=1TO5:R(X)=100:NEXT
50020 D=1:P$(1)="MARVIN":P$(2)="GERRY":P$(3)="WALTER":P$(4)="BETSY":P$(5)="YOU":B$="          ":RETURN
