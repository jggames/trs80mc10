0 CLS:CLEAR500:DIMC1,C2,C3,Z,M$,I$,B(10,10),L(75),A$(5),X,Y,F,U:Z=16384:GOTO200
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+Z:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-Z+C2,:RETURN

200 REM *MAIN PROGRAM*
210 C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
220 REM  TITLE PAGE
230 PRINT@9,;:M$="LARRY BETHURUM":GOSUB7
231 PRINT@32,"PHILLIPS EXETER ACADEMY. 1/23/66"
233 PRINT"YOU ARE NOW GOING TO PLAY A":M$="COMPUTERIZED VERSION OF BINGO-- ":GOSUB7:PRINT
234 M$="IF YOU SELECT PROMPT MODE THEN HIT THE ROW NUMBER (1-5/TOP TO BOTTOM) OF YOUR CARD AS IT IS CALLED.":GOSUB1
235 M$="I'LL BE WATCHING MY OWN CARDS FROM UP HERE IN DARTMOUTH.":GOSUB1
238 PR=3:PRINT@448,"PROMPT FOR CARD MARKING";:INPUTM$:IFLEFT$(M$,1)="N"THENPR=1:GOTO240
239 PRINT@448,;:INPUT"LEVEL OF DIFFICULTY (1-3)";LV$:LV=300-VAL(LV$)*50:IFLV<150ORLV>250THEN239
240 CLS:F=1:V=0:W=0
270 RESTORE:FOR C1=1 TO 5:READ A$(C1):NEXT
280 DATA B ,I ,N ,G ,O 
290 REM  THIS SEQUENCE GENERATES THE CARD NUMBERS
300 FOR K1=1 TO 75
310 L(K1)=0
320 NEXT K1
330 REM 
340 IF F>1 THEN 370
350 LL=0:PRINT@LL,"   YOUR CARD    ";:PRINT@LL+32,"Š€b€€i€€n€€g€€o…";:LL=LL+64
360 GOTO 380
370 LL=16:PRINT@LL,"    MY CARD     ";:PRINT@LL+32,"Š€b€€i€€n€€g€€o…";:LL=LL+64
380 M=16
390 G=F+4
400 FOR Y=F TO G
410 FOR X=F TO G
420 R=INT(M*RND(0))
430 IF R<M-15 THEN 420
440 IF L(R)<>0 THEN 420
450 B(X,Y)=R
460 L(R)=1
470 NEXT X
480 M=M+15
490 NEXT Y

500 REM  THIS SEQUENCE PRINTS THE CARD
520 RR=0
530 FOR X=F TO G
570 FOR Y=F TO G
580 IF B(X,Y)=B(F+2,F+2) THEN 610
590 PRINT@LL+RR,"Š"MID$(STR$(B(X,Y))+" ",2,2)"…";:RR=RR+3
600 GOTO 620
610 PRINT@LL+RR,"Š";:M$="  ":GOSUB7:PRINT"Š";:RR=RR+3
620 NEXT Y:LL=LL+32:RR=0
630 NEXT X
670 PRINT@LL,"‹ƒƒƒƒƒƒƒƒƒƒƒƒƒƒ‡";
720 IF F=6 THEN 750
730 F=6
740 GOTO 300

750 REM  CONTINUE
810 ONPRGOTO840
820 PRINT@256,"ARE YOU READY";
830 INPUT R$
840 IF LEFT$(R$,1)<>"N" THEN 880
860 PRINT"*********:HURRY UP:*********"
870 GOTO 810

880 REM
890 FOR K1=1 TO 75
900 L(K1)=0
910 NEXT K1
930 B(3,3)=0
940 B(8,8)=0

950 REM  THIS SEQUENCE GENERATES THE BINGO NUMBERS
960 U=INT(75*RND(0))+1
970 IF L(U)<>0 THEN 960
980 L(U)=1
990 PRINT
1000 IF RND(0)>.5 THEN 1030
1010 PRINT@256,"THE NUMBER COMES UP:":PRINT:PRINT@32*9,;
1020 GOTO 1040
1030 PRINT@256,"THE NEXT ONE IS:":PRINT:PRINT@32*9,;
1040 GU=0:FOR C1=1 TO PR:PRINT A$(INT((U-1)/15)+1);U;"  ";:ONPRGOTO1049:FOR C2=1 TO LV
1042 I$=INKEY$:IFI$<>""THENIFGU=.THENGU=VAL(I$):SOUND200,1
1043 NEXT
1049 NEXT:IFGUTHENPRINT GU;

1050 REM  THIS IS THE "NUMBER ON CARD?" SEQUENCE
1060 FOR Y=1 TO 10
1070 FOR X=1 TO 10
1080 IF B(X,Y)=U THEN 1200
1090 NEXT X
1100 NEXT Y
1110 F=1
1120 GOSUB 1250
1130 F=6
1140 GOSUB 1250
1150 IF V=1 AND W=1 THEN 1900
1160 IF V=1 THEN 2000
1170 IF W=1 THEN 2080
1180 GOTO 960

1200 C1=32*(X-1)+(Y-1)*3+65:IFX>5THENIFY>5THEN1215
1201 IFPR=3THENIFGU<>XTHEN1210
1205 PRINT@C1,;:M$=MID$(STR$(B(X,Y))+" ",2,2):GOSUB7:B(X,Y)=0
1210 PRINT@320,;:GOTO 1090

1215 C1=C1+1:C1=C1-160:IFRND(LV)<5THEN1210
1216 GOTO1205

1220 REM  THIS IS THE BINGO DETERMINING SEQUENCE
1230 REM  

1240 REM  THIS IS THE VERTICAL CHECK FOR BINGO***
1250 G=F+4
1260 FOR Y=F TO G
1270 FOR X=F TO G
1280 IF B(X,Y)<>0 THEN X=G:GOTO1390
1290 IF X<F+4 THEN 1390
1300 IF F=6 THEN 1350
1310 PRINT"YOU'VE GOT A   B I N G O * * *"
1330 W=1:WW=WW+1
1340 GOTO1380
1350 PRINT"I'VE GOT A   B I N G O * * * *"
1370 V=1:VV=VV+1
1380 X=G:NEXT X:Y=G:NEXT Y:RETURN
1390 NEXT X
1400 NEXT Y

1410 REM  THIS IS THE HORIZONTAL CHECK FOR BINGO***
1420 G=F+4
1430 FOR X=F TO G
1440 FOR Y=F TO G
1450 IF B(X,Y)<>0 THEN Y=G:GOTO1560
1460 IF Y<F+4 THEN 1560
1470 IF F=6 THEN 1520
1480 PRINT"YOU'VE GOT A   B I N G O * * *"
1500 W=1:WW=WW+1
1510 GOTO1550
1520 PRINT"I'VE GOT A   B I N G O * * * *"
1540 V=1:VV=VV+1
1550 Y=G:NEXT Y:X=G:NEXT X:RETURN
1560 NEXT Y
1570 NEXT X

1580 REM  THIS IS THE SLANT CHECK (M=-1) FOR BINGO*** 
1590 X=F
1600 Y=F
1610 IF B(X,Y)<>0 THEN 1740:REM FIXED ERROR-- CHANGED TO 1740 FROM 1720
1620 X=X+1
1630 Y=Y+1
1640 IF Y<F+5 THEN 1610
1650 IF Y=11 THEN 1700
1660 PRINT"YOU'VE GOT A   B I N G O * * *"
1670 W=1:WW=WW+1
1690 RETURN
1700 PRINT"I'VE GOT A   B I N G O * * * *"
1710 V=1:VV=VV+1
1720 RETURN

1730 REM  THIS IS THE SLANT CHECK (M=1) FOR BINGO***
1740 X=F+4
1750 Y=F
REM 1750 LET Y=X (THIS SEEMS TO BE AN ERROR)
1760 IF B(X,Y)<>0 THEN 1880
1770 X=X-1
1780 Y=Y+1
1790 IF Y<F+5 THEN 1760
1800 IF Y=11 THEN 1850
1810 PRINT"YOU'VE GOT A   B I N G O * * *"
1830 W=1:WW=WW+1
1840 RETURN
1850 PRINT"I'VE GOT A   B I N G O * * * *"
1870 V=1:VV=VV+1
1880 RETURN

1890 REM  THIS THE TIE PRINTOUT SEQUENCE
1900 PRINT"********* IT'S A TIE *********":GOTO1940
1920 REM  THIS IS THE "PLAY AGAIN?" SEQUENCE
1930 REM GOTO240:REM THIS OUT
1940 PRINT@448,"DO YOU WANT TO PLAY AGAIN";
1950 INPUT E$
1970 IF E$="NO" OR E$="N" THEN 3000
1980 GOTO 240
1990 REM  THIS IS THE "I WIN" SEQUENCE
2000 FOR S=1 TO 4
2020 PRINT"I WIN!  ";:FOR C2=1 TO 1500:NEXT
2030 NEXT S
2060 GOTO 1930
2070 REM  THIS IS THE "YOU WIN" SEQUENCE
2080 PRINT TAB(10)"YOU WIN....."
2100 GOTO 1930
2110 REM  THIS IS THE "END" OF THE LIST OF PROGRAM ENTITLED "BINGO"
2120 END
3000 CLS:PRINT"YOU"WW,"ME"VV
3010 IFLVTHENPRINT"AT LEVEL "LV$
3030 END

3100 REM NAME--BINGO
3110 REM
3120 REM DESCRIPTION--
3130 REM GAME OF BINGO
3140 REM SOURCE--
3150 REM LARRY BETHURUM
3160 REM PHILLIPS EXETER
3170 REM ACADEMY. 1/23/66
3180 REM MC-10 EDITS BY
3190 REM JIM GERRIE 2022
