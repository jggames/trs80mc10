0 DIM N$(3),V$(4),A$(5,12),B(5,12),G$(5):GOTO100
2 PRINT@X+32*Y," ";:PRINT@X+32*(Y-1)," ";:PRINT@X+32*(Y-2)," ";:RETURN
3 PRINT@X+32*Y,V$(0);:PRINT@X+32*(Y-1),V$(1);:PRINT@X+32*(Y-2),V$(2);:RETURN
4 FORY=2TO13:PRINT@8+32*Y,"     ";:NEXT:RETURN
10 REM
20 REM made by RURUN / 1999.2.1
30 REM E-mail:yoshiro@athena.dricas.com
40 REM
100 CLS4
110 REM
130 G$(0)="+"
140 G$(1)="$"
150 G$(2)="@"
160 G$(3)="O"
170 G$(4)="*"
390 GOSUB4
400 REM DRAW BOX
410 REM AROUND PLAYING FIELD
420 PRINT@8+32*5,"=====";
430 PRINT@8+32*7,"COLMS";
440 PRINT@8+32*9,"=====";
450 PRINT@8+32*11,"PRESS";
460 PRINT@8+32*12,"SPACE";
470 IFINKEY$=" "THEN490
480 GOTO470
490 REM ----- 
500 REM BACKGROUND
510 CLS4:GOSUB4
520 TI=0:TT=0:CH=0:C=0
530 PRINT@15+32*9,"TOTAL";
540 PRINT@15+32*12,"CHAIN";
550 FORK=0TO4:FORL=0TO11
560 A$(K,L)=" ":B(K,L)=0
570 NEXT:NEXT
580 FORL=0TO2
590 N$(L)=G$(RND(5)-1)
600 NEXT
610 REM *LOOP
620 SP=3:REM 135-20*(TT\5)
630 REM IFSP<100THENSP=100
640 IFC>2THENCH=CH+C-2
650 PRINT@16+32*10,RIGHT$("00"+RIGHT$(STR$(TT),LEN(STR$(TT))-1),3);
660 PRINT@16+32*13,RIGHT$("00"+RIGHT$(STR$(CH),LEN(STR$(CH))-1),3);
670 X=10:Y=4:C=0
680 FORL=0TO2:V$(L)=N$(L):NEXT
690 FORL=0TO2
700 N$(L)=G$(RND(5)-1)
710 NEXT
720 PRINT@16+32*4,N$(2);
730 PRINT@16+32*5,N$(1);
740 PRINT@16+32*6,N$(0);
750 PRINT@X+32*Y,V$(0);
760 PRINT@X+32*(Y-1),V$(1);
770 PRINT@X+32*(Y-2),V$(2);
780 IFA$(2,2)<>" "THEN2180:REM *OVER
790 REM *FALL
800 TI=TI+1
810 IFTI<=SPTHEN940
820 IFY=13ORA$(X-8,Y-1)<>" "THEN1410:REM *JUDGE
840 PRINT@X+32*Y," ";
850 PRINT@X+32*(Y-1)," ";
860 PRINT@X+32*(Y-2)," ";
870 Y=Y+1:TI=0
880 PRINT@X+32*Y,V$(0);
890 PRINT@X+32*(Y-1),V$(1);
900 PRINT@X+32*(Y-2),V$(2);
910 GOTO800
930 REM -----
940 I$=INKEY$
1000 IFI$=" "THENV$(3)=V$(0):V$(0)=V$(1):V$(1)=V$(2):V$(2)=V$(3):GOSUB3
1080 IFI$="A"THENIFX>8THENIFA$(X-9,Y-2)=" "THENGOSUB2:X=X-1:GOSUB3
1200 REM ----- 
1210 IFI$="S"THENIFX<12THENIFA$(X-7,Y-2)=" "THENGOSUB2:X=X+1:GOSUB3
1330 REM -----
1340 IFI$="Z"THENIFY<13THENIFA$(X-8,Y-1)=" "THEN840
1350 GOTO800
1400 REM *JUDGE
1410 TI=0:A$(X-8,Y-2)=V$(0):A$(X-8,Y-3)=V$(1):A$(X-8,Y-4)=V$(2)
1450 REM *RENSA
1460 R=0:C=C+1
1470 FORJ=0TO4:FORL=0TO11:FORK=0TO2:IFA$(K,L)=G$(J)ANDA$(K+1,L)=G$(J)ANDA$(K+2,L)=G$(J)THENB(K,L)=1:B(K+1,L)=1:B(K+2,L)=1
1550 NEXT:NEXT
1570 FORK=0TO4:FORL=2TO9:IFA$(K,L)=G$(J)ANDA$(K,L+1)=G$(J)ANDA$(K,L+2)=G$(J)THENB(K,L)=1:B(K,L+1)=1:B(K,L+2)=1
1640 NEXT:NEXT
1660 FORK=0TO2:FORL=2TO9:IFA$(K,L)=G$(J)ANDA$(K+1,L+1)=G$(J)ANDA$(K+2,L+2)=G$(J)THENB(K,L)=1:B(K+1,L+1)=1:B(K+2,L+2)=1
1730 NEXT:NEXT
1750 FORK=0TO2:FORL=4TO11:IFA$(K,L)=G$(J)ANDA$(K+1,L-1)=G$(J)ANDA$(K+2,L-2)=G$(J)THENB(K,L)=1:B(K+1,L-1)=1:B(K+2,L-2)=1
1820 NEXT:NEXT:NEXT
1840 REM -----
1842 FORK=0TO4:FORL=2TO11:IFB(K,L)=1THENA$(K,L)="(":B(K,L)=0
1846 NEXT:NEXT
1850 FORK=0TO4:FORL=2TO11:PRINT@(8+K)+32*(2+L),A$(K,L);:NEXT:NEXT
1880 FORK=0TO4:FORL=2TO11:IFA$(K,L)="("THENA$(K,L)=")":R=1
1900 NEXT:NEXT
1910 FORK=0TO4:FORL=2TO11:PRINT@(8+K)+32*(2+L),A$(K,L);:NEXT:NEXT
1940 FORK=0TO4:FORL=2TO11:IFA$(K,L)=")"THENA$(K,L)="-"
1960 NEXT:NEXT
1980 IFR=0THEN610
1990 FORK=0TO4:FORL=2TO11:PRINT@(8+K)+32*(2+L),A$(K,L);
2010 NEXT:NEXT
2020 REM -----
2030 FORK=0TO4:FORL=2TO11:IFA$(K,L)<>"-"THEN2120
2060 TT=TT+1:FORM=0TOL-1:A$(K,L-M)=A$(K,L-M-1):IFL-M-1=0THENA$(K,L-M-1)=" "
2100 NEXT
2120 NEXT:NEXT
2130 FORK=0TO4:FORL=2TO11:PRINT@(8+K)+32*(2+L),A$(K,L);:NEXT:NEXT
2160 SOUND200,1:GOTO1450
2170 REM*RENSA
2180 REM *OVER
2190 SOUND1,10
2200 PRINT@8+32*2,"GAME ";
2210 PRINT@8+32*3," OVER";
2220 PRINT@4+32*15,"END Q / RETRY C";
2230 I$=INKEY$:IFI$=""THEN2230
2240 IFI$="C"THEN490
2245 IFI$="Q"THENEND
2250 GOTO2230
