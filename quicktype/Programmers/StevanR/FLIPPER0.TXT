1 REM COMME UNE BOULE DE FLIPPER - VERSION 1.0 - SANYO PHC-25 - CREE EN 2025 PAR STEVANR
5 CLEAR 100,&HF000:SCREEN 1,1,1:P=0:K=0:I=0:D=0:O=0:C=0:E=0:T=0:NC=0:S=0:J=0:H=0:X=0:Y=0:U=0:V=0
10 FOR I=0 TO 16:READ A$:POKE &HF000+I,VAL("&H"+A$):NEXT
29 REM PRESENTATION
30 COLOR 2,,2:CLS:GOSUB 750
35 LOCATE 7,0:PRINT "MEILLEUR SCORE:";H;
40 COLOR 1,,2:LOCATE 3,2:PRINT "COMME UNE BOULE DE FLIPPER";:COLOR 2,,2
41 LOCATE 4,8:PRINT "APPUYEZ SUR [ESPACE] OU";
42 LOCATE 2,9:PRINT "SUR LE BOUTON DU JOYSTICK 1";
43 LOCATE 10,10:PRINT "POUR JOUER";
44 LOCATE 5,12:PRINT "APPUYEZ SUR [I] POUR";
45 LOCATE 3,13:PRINT "AFFICHER LES INSTRUCTIONS";
46 LOCATE 4,15:PRINT "CREE EN 2025 PAR STEVANR";:X=15:Y=5:I=1:J=1
60 IF STRIG(0)=1 THEN C=0:GOTO 90
61 IF STRIG(1)=1 THEN C=1:GOTO 90
62 A$=INKEY$:IF A$="i" OR A$="I" THEN 7000
63 IF X=0 OR X=31 THEN I=-I
64 IF Y=3 OR Y=7 THEN J=-J
65 LOCATE X,Y:PRINT CHR$(&H20);:X=X+I:Y=Y+J:LOCATE X,Y:PRINT CHR$(&H85);
66 GOTO 60
69 REM INITIALISATION NOUVELLE PARTIE
90 S=0:N=1:RESTORE 6000
99 REM INITIALISATION NIVEAU
100 B=3:GOSUB 1000:P=P0+24576
110 O=P:D=1:K=133:E=20
199 REM DEBUT BOUCLE PRINCIPALE
200 E=E-1:IF E<>0 THEN 210
205 E=20:T=T-1:LOCATE 16,0:PRINT T;" ";:IF T=0 THEN 500
210 POKE O,&H20:POKE P,K:O=P:P=P+D:I=PEEK(P):IF I=W THEN 320
220 IF STICK(C)=1 THEN P=P-32
230 IF STICK(C)=5 THEN P=P+32
240 I=PEEK(P):IF I=&H24 THEN 400:REM DOLLAR
250 IF I=&H81 THEN 410:REM COEUR
260 IF I=&H1C THEN 430:REM PIQUE
270 IF I=32 THEN 200
280 P=O:P=P+D:I=PEEK(P):IF I=32 THEN 200
290 IF I=&H24 THEN 400:REM DOLLAR
300 IF I=&H81 THEN 410:REM COEUR
310 IF I=&H1C THEN 430:REM PIQUE
320 PLAY "o1l32a":P=O:D=-D:GOTO 200
330 REM FIN BOUCLE PRINCIPALE
399 REM ATTRAPE ARGENT
400 PLAY "o2l20del10c":T=T+5:LOCATE 16,0:PRINT T;" ";:GOTO 200
409 REM ATTRAPE COEUR
410 PLAY "o3l20gal10b":NC=NC-1:IF K=133 THEN S=S+N*50:LOCATE 26,0:PRINT S;
415 IF NC=0 THEN POKE O,&H20:POKE P,K:GOTO 600
420 GOTO 200
429 REM PERCUTE PIQUE
430 POKE O,&H20:POKE P,&H2A
440 COLOR W0,,W1:GOSUB 550:FOR I=1 TO 3:LOCATE 7,15:PRINT "OBSTACLE PERCUTE!";
450 FOR J=1 TO 200:NEXT:LOCATE 7,15:PRINT SPC(17);:FOR J=1 TO 200:NEXT:NEXT:POKE P,&H1C:GOTO 520
499 REM TEMPS ECOULE
500 COLOR W0,,W1:GOSUB 550:FOR I=1 TO 5:LOCATE 9,15:PRINT "TEMPS ECOULE!";
510 FOR J=1 TO 200:NEXT:LOCATE 9,15:PRINT SPC(13);:FOR J=1 TO 200:NEXT:NEXT:POKE O,&H20:T=T0:E=20
519 REM MOINS UNE VIE
520 FOR I=7 TO 24:LOCATE I,15:PRINT CHR$(W);:NEXT:COLOR A0,,A1
530 B=B-1:GOSUB 2000:IF B<=0 THEN B=0:K=132
540 P=24576+P0:O=P:D=1:GOTO 200
550 FOR J=60 TO 30 STEP -2:PLAY "o3l12n=J;":NEXT:RETURN:REM PERDU...
599 REM FIN NIVEAU
600 FOR I=0 TO 300:NEXT:LOCATE 5,4:PRINT SPC(22);:LOCATE 6,4:PRINT "NIVEAU";N;" TERMINE!";:IF B=0 THEN 680
605 GOSUB 750
610 FOR I=0 TO 300:NEXT:LOCATE 5,5:PRINT SPC(22);:LOCATE 5,6:PRINT SPC(22);:LOCATE 6,6:PRINT "BONUS POUR LE TEMPS:";
620 FOR I=0 TO 100:NEXT:U=N*20:V=T*U:LOCATE 5,7:PRINT SPC(22);:LOCATE 5,7:PRINT T;" x";U;" =";V;:S=S+V
630 FOR I=0 TO 300:NEXT:LOCATE 5,8:PRINT SPC(22);:LOCATE 5,9:PRINT SPC(22);:LOCATE 6,9:PRINT "BONUS POUR LES VIES:";
640 FOR I=0 TO 100:NEXT:LOCATE 5,10:PRINT SPC(22);:U=N*200:V=B*U:LOCATE 5,10:PRINT B;" x";U;" =";V;:S=S+V
650 FOR I=0 TO 300:NEXT:LOCATE 5,11:PRINT SPC(22);:LOCATE 5,12:PRINT SPC(22);:LOCATE 6,12:PRINT "NOUVEAU SCORE:";S;" ";
660 LOCATE 26,0:PRINT S;:FOR I=0 TO 1000:NEXT:N=N+1
665 IF N=9 THEN 2500
670 GOTO 100
680 FOR I=0 TO 300:NEXT:LOCATE 5,5:PRINT SPC(22);:LOCATE 5,6:PRINT SPC(22);:LOCATE 6,6:PRINT "PAS DE VIE,";
690 FOR I=0 TO 100:NEXT:LOCATE 5,7:PRINT SPC(22);:LOCATE 6,7:PRINT "PAS DE BONUS";
700 GOTO 660
750 PLAY "o4l08cdecagfdr2ag":RETURN:REM J'VIS COMME...
751 PLAY "o4l08fr4defr4fr4defr4e":RETURN:REM MOI...
752 PLAY "r4cder4er4cder4dr4":RETURN
753 PLAY "o3bo4cdr4dr4o3bo4cdr4edc":RETURN
999 REM AFFICHAGE NIVEAU
1000 READ W,A0,A1,W0,W1,T0,P0
1010 COLOR A0,,A1:GOSUB 1300:T=T0:GOSUB 2000:COLOR W0,,W1
1020 FOR I=1 TO 30:LOCATE I,1:PRINT CHR$(W);:LOCATE I,15:PRINT CHR$(W);:NEXT:REM MUR
1030 FOR I=2 TO 14:LOCATE 0,I:PRINT CHR$(W);:LOCATE 31,I:PRINT CHR$(W);:NEXT:REM MUR
1040 READ R:IF R<>0 THEN FOR I=1 TO R:READ X,Y:LOCATE X,Y:PRINT CHR$(W);:NEXT:REM MUR
1050 READ R:IF R<>0 THEN FOR I=1 TO R:READ F,X,Y,U,V:FOR J=1 TO F:LOCATE X,Y:PRINT CHR$(W);:X=X+U:Y=Y+V:NEXT:NEXT:REM MUR
1060 COLOR A0,,A1
1070 READ R:IF R<>0 THEN FOR I=1 TO R:READ P:P=P+24576:POKE P,&H1C:NEXT:REM PIQUE
1080 READ R:IF R<>0 THEN FOR I=1 TO R:READ F,P,U:P=P+24576:FOR J=1 TO F:POKE P,&H1C:P=P+U:NEXT:NEXT:REM PIQUE
1090 READ R:IF R<>0 THEN FOR I=1 TO R:READ P:P=P+24576:POKE P,&H24:NEXT:REM DOLLAR
1100 READ NC:FOR I=1 TO NC:READ P:P=P+24576:POKE P,&H81:NEXT:REM COEUR
1110 RETURN
1200 REM FIN AFFICHAGE NIVEAU
1299 REM PRESENTATION NIVEAU
1300 CLS:POKE &HF004,&H85:EXEC &HF000:FOR I=0 TO 300:NEXT:POKE &HF004,&H20:EXEC &HF000
1310 LOCATE 12,3:PRINT "NIVEAU";N;
1320 IF N<>1 THEN 1340
1325 LOCATE 3,6:PRINT "Moi j'suis comme une bombe";:LOCATE 3,7:PRINT "qu'on a larguee";
1330 LOCATE 3,9:PRINT "Et puis qui tombe";:LOCATE 3,10:PRINT "au beau milieu";
1335 LOCATE 3,12:PRINT "D'un slow d'enfer";:LOCATE 3,13:PRINT "sans partenaire";:GOSUB 751
1340 IF N<>2 THEN 1360
1345 LOCATE 3,6:PRINT "Moi qui suis pas faite";:LOCATE 3,7:PRINT "pour les dollars";:LOCATE 3,8:PRINT "et la defaite";
1350 LOCATE 3,10:PRINT "J'ai un chagrin";:LOCATE 3,11:PRINT "de baby doll,";:LOCATE 3,12:PRINT "sans idylle, sans idole";
1355 GOSUB 751
1360 IF N<>3 THEN 1380
1365 LOCATE 3,6:PRINT "Et j'vis";:LOCATE 3,7:PRINT "comme une boule de flipper";:LOCATE 3,8:PRINT "qui roule";
1370 LOCATE 3,10:PRINT "Avec les oreillers du coeur";:LOCATE 3,11:PRINT "en boule";:GOSUB 750
1380 IF N<>4 THEN 1400
1385 LOCATE 3,6:PRINT "Et j'vis";:LOCATE 3,7:PRINT "comme une boule de flipper";:LOCATE 3,8:PRINT "qui roule";
1390 LOCATE 3,10:PRINT "Voilier";:LOCATE 3,11:PRINT "si t'as pas de skipper,";:LOCATE 3,12:PRINT "tu coules";:GOSUB 750
1400 IF N<>5 THEN 1420
1405 LOCATE 3,6:PRINT "J'lis des romans fleuve,";:LOCATE 3,7:PRINT "il faut qu'j'oublie";
1410 LOCATE 3,9:PRINT "Pour etre toute neuve,";:LOCATE 3,10:PRINT "les souvenirs";
1415 LOCATE 3,12:PRINT "C'est du poison";:LOCATE 3,13:PRINT "au gout citron";:GOSUB 751
1420 IF N<>6 THEN 1440
1425 LOCATE 3,6:PRINT "J'reve a des endroits";:LOCATE 3,7:PRINT "que j'me dessine";
1430 LOCATE 3,9:PRINT "Du bout des doigts";:LOCATE 3,10:PRINT "sur les miroirs";:LOCATE 3,11:PRINT "de mon studio";
1435 LOCATE 3,13:PRINT "Pas d'histoire,";:LOCATE 3,14:PRINT "pas de photo";:GOSUB 751
1440 IF N<>7 THEN 1460
1445 LOCATE 3,6:PRINT "Et j'vis";:LOCATE 3,7:PRINT "comme une boule de flipper";:LOCATE 3,8:PRINT "qui roule";
1450 LOCATE 3,10:PRINT "Tous mes beaux chateaux";:LOCATE 3,11:PRINT "d'Equateur";:LOCATE 3,12:PRINT "s'ecroulent";:GOSUB 750
1460 IF N<>8 THEN 1480
1465 LOCATE 3,6:PRINT "Et j'vis";:LOCATE 3,7:PRINT "comme une boule de flipper";:LOCATE 3,8:PRINT "qui roule";
1470 LOCATE 3,10:PRINT "Capitaine";:LOCATE 3,11:PRINT "d'un bateau chanteur,";:LOCATE 3,12:PRINT "tu coules";:GOSUB 750
1480 FOR I=0 TO 2500:NEXT:POKE &HF004,&H20:EXEC &HF000:RETURN
1490 REM FIN PRESENTATION NIVEAU
1999 REM AFFICHAGE INFORMATIONS
2000 LOCATE 0,0:PRINT "VIES:    ";:IF B>0 THEN P=24576+6:POKE P,&H85
2010 IF B>1 THEN P=24576+7:POKE P,&H85
2020 IF B>2 THEN P=24576+8:POKE P,&H85
2030 LOCATE 10,0:PRINT "TEMPS:";T;:LOCATE 20,0:PRINT "SCORE:";S;
2040 RETURN
2499 REM VICTOIRE FINALE
2500 COLOR 2,,2:CLS:RESTORE 6200:GOSUB 751:FOR I=0 TO 10:READ U:POKE &HF004,U:EXEC &HF000:COLOR 1,,2
2510 LOCATE 7,7:PRINT "VOUS AVEZ TERMINE";:LOCATE 2,8:PRINT "COMME UNE BOULE DE FLIPPER!";
2530 IF I=3 THEN GOSUB 752
2540 IF I=6 THEN GOSUB 753
2550 IF I=10 THEN FOR J=0 TO 750:NEXT:GOSUB 750
2560 FOR J=0 TO 600:NEXT:NEXT:FOR J=0 TO 1500:NEXT
2999 REM EVALUATION FINALE
3000 COLOR 2,,2:CLS:LOCATE 3,1:PRINT "VOS POINTS:";S;:FOR J=1 TO 500:NEXT
3010 LOCATE 3,2:IF S>H THEN H=S:PRINT "C'EST LE NOUVEAU RECORD!";:GOTO 3030
3020 PRINT "LE RECORD EST DE:";H;
3030 FOR J=1 TO 500:NEXT:R=7:IF S>15000 THEN R=6
3040 IF S>25000 THEN R=5
3050 IF S>35000 THEN R=4
3060 IF S>45000 THEN R=3
3070 IF S>55000 THEN R=2
3080 IF S>65000 THEN R=1
3090 LOCATE 3,4:PRINT "VOTRE EVALUATION FINALE:";:FOR J=1 TO 500:NEXT
3100 FOR I=1 TO 7:ON I GOSUB 3500,3510,3520,3530,3540,3550,3560:NEXT
3109 REM PASSAGE ASCENDANT
3110 FOR I=1 TO 7
3120 P=9-I:IF I<>1 THEN LOCATE 6,5+P:COLOR 2,,2:PRINT "  ":ON P GOSUB 3500,3510,3520,3530,3540,3550,3560
3130 P=8-I:LOCATE 6,5+P:PRINT "=>":COLOR 1,,2:ON P GOSUB 3500,3510,3520,3530,3540,3550,3560:FOR J=1 TO 200:NEXT
3140 NEXT:FOR J=1 TO 400:NEXT
3159 REM PASSAGE DESCENDANT
3160 D=1
3170 IF R=D THEN 3210
3180 COLOR 2,,2:LOCATE 6,5+D:PRINT "  ":ON D GOSUB 3500,3510,3520,3530,3540,3550,3560
3190 D=D+1:LOCATE 6,5+D:PRINT "=>":COLOR 1,,2:ON D GOSUB 3500,3510,3520,3530,3540,3550,3560
3200 FOR J=1 TO 200:NEXT:GOTO 3170
3209 REM CLIGNOTEMENT FINAL
3210 GOSUB 750:FOR I=1 TO 6
3220 C=2:IF I=2 OR I=4 OR I=6 THEN C=1
3230 COLOR C,,2:ON R GOSUB 3500,3510,3520,3530,3540,3550,3560:FOR J=1 TO 250:NEXT:NEXT
3239 REM FIN
3240 COLOR 2,,2:LOCATE 4,14:PRINT "APPUYEZ SUR [ESPACE] OU";
3250 LOCATE 2,15:PRINT "SUR LE BOUTON DU JOYSTICK 1";
3260 IF STRIG(0)<>1 AND STRIG(1)<>1 THEN 3260
3270 GOTO 30
3500 LOCATE 9,6:PRINT "Boule de chrome ";:RETURN
3510 LOCATE 9,7:PRINT "Boule d'or      ";:RETURN
3520 LOCATE 9,8:PRINT "Boule d'argent  ";:RETURN
3530 LOCATE 9,9:PRINT "Boule de bronze ";:RETURN
3540 LOCATE 9,10:PRINT "Boule de cristal";:RETURN
3550 LOCATE 9,11:PRINT "Boule de gomme  ";:RETURN
3560 LOCATE 9,12:PRINT "Boule de neige  ";:RETURN
3570 REM FIN EVALUATION FINALE
4999 REM ROUTINE EFFACEMENT ECRAN
5000 DATA 21,00,60,3E,2A,01,00,02,77,23,0D,C2,08,F0,10,F8,C9
5999 REM NIVEAU 1
6000 DATA 35,2,2,1,2,60,271,0,0,4,168,183,360,375,0,4,79,143,399,463,8,98,125,227,252,291,316,418,445
6009 REM NIVEAU 2
6010 DATA 61,2,1,1,1,55,271,0,2,6,22,5,0,1,6,9,6,0,1,4,94,169,374,449,0,4,120,215,328,423,8,65,150,207,225,318,335,393,478
6019 REM NIVEAU 3
6020 DATA 37,1,2,2,2,50,271,0,4,9,5,4,0,1,9,26,4,0,1,12,10,5,1,0,12,10,11,1,0
6025 DATA 4,65,149,394,478,0,4,94,138,405,449,8,79,164,187,217,326,356,379,463
6029 REM NIVEAU 4
6030 DATA 72,1,1,2,1,45,92,14,25,2,4,11,4,12,8,13,8,14,12,11,12,12,16,13,16,14
6031 DATA 20,11,20,12,24,13,24,14,27,11,3,27,4,4,1,0,27,1,7,1,0,27,4,10,1
6032 DATA 0,5,190,207,257,303,449,0,6,84,131,212,355,371,473,10,72,104,168,200,264,296,360,373,380,392
6039 REM NIVEAU 5
6040 DATA 64,2,2,1,2,40,175,16,10,2,21,2,4,4,27,4,10,5,21,5,10,6,21,6,10,10,21,10,10,11,21,11,4
6041 DATA 12,27,12,10,14,21,14,4,3,4,7,0,1,3,15,7,0,1,3,16,7,0,1,3,27,7,0,1,6,65,94,207,336,449,478,0
6042 DATA 6,73,84,227,316,459,470,10,75,86,133,241,252,291,302,410,457,468
6049 REM NIVEAU 6
6050 DATA 38,2,1,1,1,35,271,6,10,2,17,2,24,2,10,14,17,14,24,14,0,2,94,478
6051 DATA 2,26,197,1,26,325,1,6,73,80,87,457,464,471,10,65,75,82,89,257,286,449,459,466,473
6059 REM NIVEAU 7
6060 DATA 88,1,2,2,2,30,93,8,1,3,2,3,4,5,5,5,1,8,2,8,4,11,5,11,5,10,6,2,0,1,10,11,5,0,1,10,16,2,0,1,10,21,5,0,1,10,26,2,0,1
6061 DATA 6,65,71,81,449,460,470,0,6,266,271,276,281,286,355,10,66,75,85,139,149,400,410,454,464,474
6069 REM NIVEAU 8
6070 DATA 63,1,1,2,1,30,111,8,6,4,15,4,16,4,25,4,11,7,20,7,12,14,19,14,0,6,65,94,234,245,449,478
6071 DATA 2,12,321,1,12,339,1,8,66,93,233,246,333,338,450,477,10,135,152,225,236,243,254,385,414,459,468
6199 REM CARACTERES DE FIN
6200 DATA 129,36,129,36,129,36,129,36,129,36,129
6999 REM INSTRUCTIONS
7000 CLS:COLOR 1,,2:LOCATE 6,1:PRINT "INSTRUCTIONS DU JEU:";:COLOR 2,,2
7010 LOCATE 2,3:PRINT "Parcourez 8 niveaux de":PRINT " difficulte croissante avec":PRINT " votre boule de flipper."
7020 LOCATE 2,7:PRINT "Vous controlez ses":PRINT " deplacements verticaux":PRINT " avec les touches flechees"
7030 PRINT " [Haut] et [Bas] ou bien le":PRINT " joystick 1."
7040 LOCATE 8,13:PRINT "APPUYEZ SUR [I]";:LOCATE 6,14:PRINT "POUR VOIR LA SUITE.";
7060 A$=INKEY$:IF A$<>"i" AND A$<>"I" THEN 7060
7070 CLS:COLOR 1,,2:LOCATE 2,1:PRINT "INSTRUCTIONS DU JEU (SUITE):";:COLOR 2,,2
7080 LOCATE 2,3:PRINT "Attrapez tous les coeurs pour":PRINT " completer le niveau."
7090 LOCATE 2,6:PRINT "Ramasser les dollars est":PRINT " facultatif mais ils vous":PRINT " donneront du temps"
7100 PRINT " supplementaire (le temps":PRINT " c'est de l'argent!)."
7110 LOCATE 8,13:PRINT "APPUYEZ SUR [I]";:LOCATE 6,14:PRINT "POUR VOIR LA SUITE.";
7130 A$=INKEY$:IF A$<>"i" AND A$<>"I" THEN 7130
7140 CLS:COLOR 1,,2:LOCATE 2,1:PRINT "INSTRUCTIONS DU JEU (SUITE):";:COLOR 2,,2
7150 LOCATE 2,3:PRINT "Evitez les obstacles X et que":PRINT " le temps s'ecoule entierement,"
7160 PRINT " pour ne pas perdre une vie.":LOCATE 2,7:PRINT "Vous avez droit a 3 vies par":PRINT " niveau. Si vous n'avez plus"
7170 PRINT " de vie, vous pourrez continuer":PRINT " a jouer mais vous ne marquerez":PRINT " plus de points."
7180 LOCATE 8,13:PRINT "APPUYEZ SUR [I]";:LOCATE 6,14:PRINT "POUR VOIR LA SUITE.";
7190 A$=INKEY$:IF A$<>"i" AND A$<>"I" THEN 7190
7200 CLS:COLOR 1,,2:LOCATE 3,1:PRINT "INSTRUCTIONS DU JEU (FIN):";:COLOR 2,,2
7210 LOCATE 2,3:PRINT "Ce jeu trouve une partie de":PRINT " son inspiration dans la":PRINT " chanson 'Boule de flipper',"
7220 PRINT " dont les paroles furent":PRINT " ecrites par Jean-Michel":PRINT " Beriat, sur une composition"
7230 PRINT " de Christophe. Elle est connue":PRINT " pour son interpretation par":PRINT " Corynne Charby en 1986."
7240 LOCATE 8,13:PRINT "APPUYEZ SUR [I]";:LOCATE 5,14:PRINT "POUR REVENIR AU MENU.";
7250 A$=INKEY$:IF A$<>"i" AND A$<>"I" THEN 7250
7260 GOTO 30
