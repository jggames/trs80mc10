0 WIDTH 32:REM NOT NEEDED ON COCO 1&2
1 POKE 65497,0:REM REPLACE FOR 65495,0 ON COCO 1&2
5 GOSUB 6000:REM INTRO & INSTRUCTIONS, NO NEED TO TYPE LINES 6000 TO 7000
7 HS=5000:REM WORST SCORE
10 DIM M(27,14)
15 MR=45:ML=45:REM MR IS MINES FLAGGED, ML MINES REALLY LEFT
17 FOR X=1 TO 27:FOR Y=1 TO 14:M(X,Y)=0:NEXT Y,X:REM CLEAR MINEFIELD
18 BL=0
20 FOR A=1 TO 45
30 X=RND(25)+1:Y=RND(12)+1
40 IF M(X,Y)=0 THEN M(X,Y)=9 ELSE 30
50 NEXT A
60 CLS 0
70 PRINTCHR$(161);STRING$(25,CHR$(163));CHR$(162)
80 FOR A=1 TO 12
90 PRINTCHR$(165);STRING$(25,CHR$(160));CHR$(170)
100 NEXT A
110 PRINT CHR$(164);STRING$(25,CHR$(172));CHR$(168)
120 TIMER=0
200 H=INT(JOYSTK(0)/2.56)+1
210 V=INT(JOYSTK(1)/5.3)+1
220 S=PEEK(1024+V*32+H)
230 POKE 1024+V*32+H,223
235 FOR LO=1 TO 10
240 BT=PEEK(65280)
250 IF BT=254 OR BT=126 GOTO 1000
260 IF BT=251 OR BT=123 THEN GOSUB 2000
262 A$=INKEY$:IF A$="" THEN 270
264 IF A$=" " GOTO 1000
265 IF ASC(A$)=13 GOSUB 2000
270 NEXT LO
300 POKE 1024+V*32+H,S
310 GOTO 200
1000 REM IS THERE A MINE?
1004 PRINT@219,"TIME";:PRINT@251,INT(TIMER/60);
1005 X=H+1:Y=V+1:C=0
1007 IF S=191 AND M(X,Y)<>9 THEN MR=MR+1:PRINT@28,"LEFT";:PRINT@60,MR;
1008 IF MR=0 AND ML=0 THEN TT=INT(TIMER/60):GOTO 10000
1010 IF M(X,Y)=9 THEN GOSUB 5000:GOTO 200
1020 FOR A=X-1 TO X+1
1030 FOR B=Y-1 TO Y+1
1032 IF B<2 THEN NEXT B
1034 IF B>13 THEN GOTO 1050
1036 IF A<2 THEN NEXT A
1038 IF A>26 THEN GOTO 1055
1040 IF M(A,B)=9 THEN C=C+1
1050 NEXT B,A
1055 IF C=0 THEN C=31:REM TO GET A SPACE IN THE NEXT POKE
1056 IF Y=1 OR Y=14 OR X=1 OR X=27 THEN 1065
1060 POKE 1024+(Y-1)*32+X-1,C+112
1065 IF C=31 AND Z=0 THEN GOTO 1100
1068 IF Z=1 THEN RETURN
1070 GOTO 200
1100 REM THE SQUARE WAS CLEAR
1110 REM SO, SHOW ALL AROUND
1115 Z=1
1120 FOR CX=H TO H+2: FOR CY=V TO V+2
1130 C=0:X=CX:Y=CY:GOSUB 1020
1140 NEXT CY,CX
1145 Z=0
1150 GOTO 200
2000 REM PUT A FLAG
2002 IF S=191 THEN RETURN
2005 PLAY"T32EFE"
2010 POKE 1024+V*32+H,191:MR=MR-1
2015 S=191
2020 IF M(H+1,V+1)=9 THEN ML=ML-1
2030 IF MR=0 AND ML=0 THEN TT=INT(TIMER/60):GOTO 10000
2035 IF ML=0 THEN PLAY"T12CDEFCDEFCDEF"
2040 IF MR=0 THEN PLAY"T8FCFCFC"
2045 PRINT@28,"LEFT";:PRINT@60,MR;
2050 RETURN
5000 PLAY"O1V31T8BDV<T>ACV<T>GA"
5010 PRINT@448,"YOU BLASTED A MINE";
5020 PRINT@480,"KEEP GOING ANYWAY (Y/N)?";
5030 A$=INKEY$:IF A$="" THEN 5030
5035 IF A$="N" THEN 5100
5036 IF A$<>"Y" THEN 5030
5038 BL=BL+1
5040 PRINT@448,STRING$(28,CHR$(128));:PRINT@480,STRING$(28,CHR$(128));
5050 PRINT@443,"MINE";:PRINT@474,"BLASTS";:PRINT@507,BL;
5055 GOSUB 2000
5060 RETURN
5100 FOR X=2 TO 26:FOR Y=2 TO 13
5110 IF M(X,Y)=9 THEN PRINT@32*(Y-1)+X-1,"M";
5120 NEXT Y,X
5130 PRINT@448,"YOU WERE ABLE TO FIND";
5140 PRINT@480,"A TOTAL OF"45-ML"MINES";STRING$(8,CHR$(128));
5150 EXEC 44539
5160 END
6000 REM INTRO
6010 CLS 0:SCREEN 0,2
6020 PRINT@10,"'MINE CAMP'";
6025 PRINT@32,"DO YOU WANT INSTRUCTIONS? (Y/N)";
6026 A$=INKEY$:IF A$="Y" THEN 6028 ELSE IF A$="" THEN 6026
6027 RETURN
6028 PRINT@32,STRING$(32,CHR$(128));
6030 PRINT@64,"LAND MINES ARE ONE OF THE";
6040 PRINT@96,"GREATEST DANGERS CIVILIANS FACE IN AREAS WERE THERE IS, OR WAS  AN ARMED CONFLICT.";
6050 PRINT@192,"BEFORE BEING DEPLOYED TO HELP UNAND RELIEF CONVOYS, YOU MUST";
6060 PRINT@256,"COMPLETE YOUR BASIC MINE-HUNTINGTECHNIQUES AT THE 'MINE CAMP'";
6070 PRINT@320,"USE THE RIGHT JOYSTIK TO MOVE";:PRINT@352,"THE MARKER, THE RED BUTTON OR";
6080 PRINT@384,"THE space KEY TO DIG FOR MINES,";
6085 PRINT @482,"* PRESS A KEY TO CONTINUE *";
6086 A$=INKEY$:IF A$="" THEN 6086
6087 CLS 0
6088 PRINT@0,"AND THE BLACK BUTTON OR THE";:PRINT@32,"enter KEY TO PUT A FLAG AND MARKA MINE.";
6090 PRINT@96,"EVERY TIME YOU DIG, A NUMBER";:PRINT@128,"WILL INDICATE YOU HOW MANY MINESARE THERE IN THE SURROUNDING";
6100 PRINT@192,"TERRAIN";
6110 PRINT@258,"'MINE CAMP' V. 1.0, NOV 2005";
6120 PRINT@293,"DO YOU WANT TO SEE THE";
6130 PRINT@323,"INSTRUCTIONS AGAIN? (Y/N)";
6140 A$=INKEY$:IF A$="Y" THEN CLS0:GOTO 6028 ELSE IF A$="" THEN 6140
6150 RETURN
10000 CLS 6
10010 PRINT@9,"CONGRATULATIONS";
10020 PRINT@39,"YOU FOUND THEM ALL";
10030 PRINT@64,"IT TOOK YOU ";TT;"SECONDS"
10035 PRINT@96,"";
10040 IF BL=0 THEN PRINT"AND YOU DIDN'T BLAST ANY MINES"; ELSE PRINT"AND YOU BLASTED";BL;"MINES";
10050 SC=TT+(BL*25):PRINT@128,"YOUR TOTAL SCORE IS"SC;
10052 IF SC<HS THEN 10080
10060 PRINT@192,"THE BEST SCORE SO FAR IS"HS;
10065 PRINT"BY "HS$;
10070 GOTO 10100
10080 PRINT@197,"YOU HAVE THE BEST SCORE... SO FAR..."
10085 INPUT"WHAT'S YOUR NAME";HS$
10090 HS=SC
10100 PRINT@384,"DO YOU WANT TO PLAY AGAIN (Y/N)?";
10110 A$=INKEY$:IF A$="N" THEN PRINT:PRINT"BYE":POKE 65496,0:END
10120 IF A$="Y" THEN 15
10130 GOTO 10110
80 PRINT@197,"YOU HAVE THE BEST SCORE... SO FAR..."
10085 INPUT"WHAT'S YOUR NAME";HS$
10090 HS=SC
10100 PRINT@384,"DO YOU WANT TO PLAY AGAIN (Y/N)?";
10110 A$=INKEY$:IF A$="N" THEN PRINT:PRINT"BYE":POKE 65496,0:END
10120 IF A$="Y" THEN 15
10130 GOTO 10110
