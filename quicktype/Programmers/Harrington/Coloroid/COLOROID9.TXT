0 GOSUB3000:CLEAR1000:DIMX(256),Y(256),L(256),E,J,I,W,X,Y,S,M,U,K,L,B,C,T,Q,H,R,H$,WI,LV,OF,KH,PA(16),R:GOTO800
2 PRINT@32*Y+X,CHR$(143+16*(C-1));:U=U-E:RETURN
3 W=W-E:X(W)=X:Y(W)=Y:GOSUB2:L(C)=L(C)-E:L(R)=L(R)+E:RETURN
4 W=0:U=0:X(0)=0:Y(0)=0:L(H)=L(H)-E:L(R)=L(R)+E:C=H:X=0:Y=0:GOSUB2
5 I=X(W):J=Y(W):W=W+E:X=I:Y=J+S
6 IFY<L-ETHENIFPOINT(X*2,Y*2)=RTHENGOSUB3
7 IFJ-S>ETHENIFPOINT(X*2,(J-S)*2)=RTHENX=I:Y=J-S:GOSUB3
8 IFI+S<K-ETHENIFPOINT((I+S)*2,J*2)=RTHENX=I+S:Y=J:GOSUB3
9 IFI-S>ETHENIFPOINT((I-S)*2,J*2)=RTHENX=I-S:Y=J:GOSUB3
10 IFW>ETHEN5
11 RETURN
800 R=RND(-(PEEK(9)*256+PEEK(10)))
820 GOSUB3040:INPUT"INPUT BOARD SIZE (x BY x)";S$:B=INT(VAL(S$)):MB=B*B:IFB<4ORB>16THEN820
830 CLS0:WI=0:OF=0:FORT=1TO13:READPA(T):NEXT
1000 LV=B-3:E=-1:SS=7:S=1:M=0:U=0:K=B*S+E:L=B*S+E:C=0:X=0:Y=0:T=0:Q=0:KH=0:GOSUB2800:GOSUB2840
1001 H=0:W=0:ML=0:FORX=0TO256:X(X)=0:Y(X)=0:L(X)=0:NEXT
1500 FORX=0TOKSTEPS:FORY=0TOL
1501 C=1+RND(6):L(C)=L(C)+1:GOSUB2:NEXT:NEXT
2500 ML=0:FORT=1TO6:PRINT@32*T+18,RIGHT$(STR$(T),1)+"="+CHR$(143+16*T)+"("+RIGHT$(" "+STR$(L(T+1)),3)+")"
2580 IFL(T+1)>MLTHENML=L(T+1)
2590 NEXT
2600 GOSUB2790:SOUND100,1
2610 IFML>(MB+E)THENOF=OF+1:GOTO2720
2640 H$=INKEY$:IFH$=""THEN2640
2655 KH=KH+1:IFKH=1ANDH$="R"THEN1000
2650 IFH$="Q"THENOF=OF+1:GOTO2730
2660 H=INT(VAL(H$)):IFH<1ORH>6THEN2640
2680 H=H+1:R=POINT(0,0):IFH=RTHEN2640
2710 M=M-E:GOSUB4:GOTO2500
2720 IFM<=PA(LV)THEN2750
2730 PRINT@32*15+18,"PLAY AGAIN?";
2740 H$=INKEY$:IFH$=""THEN2740
2745 IFH$="Y"THEN1000
2746 IFH$="N"THENEND
2747 GOTO2740
2750 WI=WI+1:PRINT@32*11+18,"YOU BEAT":PRINT@32*12+18,"LEVEL";STR$(LV);"!":SOUND100,2:SOUND120,4:GOSUB2830
2760 PRINT@32*13+18,"UPGRADE TO":IFB+1<10THENPRINT@32*14+18,RIGHT$(STR$(B+1),1);"X";RIGHT$(STR$(B+1),1);" (Y/N)?":GOTO2770
2765 PRINT@32*14+18,RIGHT$(STR$(B+1),2);"X";RIGHT$(STR$(B+1),2);"?"
2770 H$=INKEY$:IFH$=""THEN2770
2775 IFH$="Y"THEN2780
2776 IFH$="N"THEN2730
2777 GOTO2770
2780 B=B+1:IFB>16THENB=16
2785 MB=B*B:GOTO1000
2790 PRINT@32*7+18,"MOVES:";M:RETURN
2800 PRINT@32*0+18," * COLOROID *"
2810 PRINT@32*9+18,"PAR:";PA(LV)
2820 PRINT@32*8+18,"SIZE: ";:IFB<10THENPRINTRIGHT$(STR$(B),1);"X";RIGHT$(STR$(B),1):GOTO2830
2825 PRINTRIGHT$(STR$(B),2);"X";RIGHT$(STR$(B),2)
2830 PRINT@32*10+18,"WINS: ";RIGHT$(" "+STR$(WI),3);"/";RIGHT$(" "+STR$(OF),3):RETURN
2840 FORT=11TO14:PRINT@32*T+18,"":NEXT:PRINT@32*15+18,"             ";:POKE16895,96:RETURN
2900 DATA7,9,11,13,15,17,19,20,22,24,26,28,33
3000 CLS
3001 PRINT"coloroid STARTS WITH A GRID OF "
3022 PRINT"MULTI-COLORED BLOCKS. THE GAME"
3023 PRINT"IS PLAYED FROM THE TOP LEFT."
3025 PRINT"CHOOSE A # TO CHANGE THE COLOR"
3026 PRINT"OF THE TOP LEFT BLOCK. IF YOU'VE";
3027 PRINT"CHOSEN WISELY, THAT IS IF YOU'VE";
3028 PRINT"CHOSEN A COLOR THAT WAS ADJACENT";
3029 PRINT"TO THE TOP LEFT BLOCK, IT"
3031 PRINT"WILL JOIN WITH THE NEWLY COLORED";
3032 PRINT"BLOCK. AS YOU KEEP CHANGING THE"
3033 PRINT"GROUP OF BLOCKS, IT GROWS AND "
3034 PRINT"CONTINUES TO MERGE WITH ANY "
3035 PRINT"TOUCHING BLOCKS OF THE SAME"
3036 PRINT"COLOR. THE GOAL IS TO TURN THE"
3037 PRINT"WHOLE SCREEN TO A SINGLE COLOR."
3038 RETURN
3040 PRINT"    press any key to begin";
3045 H$=INKEY$:IFH$=""THEN3045
3050 CLS:PRINT@32,"(MIN=4, MAX=16)";:PRINT@0,"";:RETURN
3500 REM
4000 REM CREDITS
4010 REM BY CHRIS HARRINGTON chris.harrington@gmail.com
4020 REM BASED ON COLOROID FOR ANDROID
4030 REM FOR MY WIFE ALISSA
4040 REM CREATED 7/4/2010;REVISED 7/9/2010
4050 REM 13 SECONDS FOR 10X10 BLOCKFILL
