0 CLS:CLEAR1000:DIMMX(256),MY(256),L(256),EE,OY,OX,W,X,Y,S,MV,UU,XL,YL,B,C,T,Q,H,H$,WI,LV,OF,KH,PA(16):GOTO820
1 RETURN
2 PRINT@32*Y+X,CHR$(143+(16*(C-1)));:UU=UU-EE:RETURN
3 W=W-EE:MX(W)=X:MY(W)=Y:GOSUB2:L(C)=L(C)-EE:L(CR)=L(CR)+EE:RETURN
4 W=0:UU=0:MX(0)=0:MY(0)=0:L(H)=L(H)-EE:L(CR)=L(CR)+EE:C=H:X=0:Y=0:GOSUB2
5 OX=MX(W):OY=MY(W):W=W+EE:X=OX:Y=OY+S
6 ON1-(Y<YL-EE)GOTO7:ON1-(POINT(X*2,Y*2)=CR)GOTO7:GOSUB3
7 ON1-(OY-S>EE)GOTO8:ON1-(POINT(X*2,(OY-S)*2)=CR)GOTO8:X=OX:Y=OY-S:GOSUB3
8 ON1-(OX+S<XL-EE)GOTO9:ON1-(POINT((OX+S)*2,OY*2)=CR)GOTO9:X=OX+S:Y=OY:GOSUB3
9 ON1-(OX-S>EE)GOTO10:ON1-(POINT((OX-S)*2,OY*2)=CR)GOTO10:X=OX-S:Y=OY:GOSUB3
10 ON1-(W>EE)GOTO1,5
820 CLS:INPUT"INPUT BOARD SIZE (x BY x)";S$:B=INT(VAL(S$)):MB=B*B:IFB<4ORB>16THEN820
830 CLS0:WI=0:OF=0:FORT=1TO13:READPA(T):NEXT
1000 LV=B-3:EE=-1:SS=7:S=1:MV=0:UU=0:XL=(B*S+EE):YL=B*S+EE:C=0:X=0:Y=0:T=0:Q=0:KH=0:GOSUB2800:GOSUB2840
1001 H=0:W=0:ML=0:FORX=0TO256:MX(X)=0:MY(X)=0:L(X)=0:NEXT
1500 FORX=0TOXLSTEPS:FORY=0TOYL
1501 C=1+RND(6):L(C)=L(C)+1:GOSUB2:NEXT:NEXT
2500 ML=0:FORT=1TO6:PRINT@32*T+18,RIGHT$(STR$(T),1)+"="+CHR$(143+(16*T))+"("+RIGHT$(" "+STR$(L(T+1)),3)+")"
2580 IFL(T+1)>MLTHENML=L(T+1)
2590 NEXT
2600 GOSUB2790
2610 IFML>(MB+EE)THENOF=OF+1:GOTO2720
2640 H$=INKEY$:IFH$=""THEN2640
2655 KH=KH+1:IFKH=1ANDH$="R"THEN1000
2650 IFH$="Q"THENOF=OF+1:GOTO2730
2660 H=INT(VAL(H$)):IFH<1ORH>6THEN2640
2680 H=H+1:CR=POINT(0,0):IFH=CRTHEN2640
2710 MV=MV-EE:GOSUB4:GOTO2500
2720 IFMV<=PA(LV)THEN2750
2730 PRINT@32*15+18,"PLAY AGAIN?";
2740 H$=INKEY$:IFH$=""THEN2740
2745 IFH$="Y"THEN1000
2746 IFH$="N"THENEND
2747 GOTO2740
2750 WI=WI+1:PRINT@32*11+18,"YOU BEAT":PRINT@32*12+18,"LEVEL";STR$(LV);"!":SOUND100,2:SOUND120,4:GOSUB2830
2760 PRINT@32*13+18,"UPGRADE TO":IFB+1<10THENPRINT@32*14+18,RIGHT$(STR$(B+1),1);"X";RIGHT$(STR$(B+1),1);"?":GOTO2770
2765 PRINT@32*14+18,RIGHT$(STR$(B+1),2);"X";RIGHT$(STR$(B+1),2);"?"
2770 H$=INKEY$:IFH$=""THEN2770
2775 IFH$="Y"THEN2780
2776 IFH$="N"THEN2730
2777 GOTO2770
2780 B=B+1:IFB>16THENB=16
2785 MB=B*B:GOTO1000
2790 PRINT@32*7+18,"MOVES:";MV:RETURN
2800 PRINT@32*0+18," * COLOROID *"
2810 PRINT@32*9+18,"PAR:";PA(LV)
2820 PRINT@32*8+18,"SIZE: ";:IFB<10THENPRINTRIGHT$(STR$(B),1);"X";RIGHT$(STR$(B),1):GOTO2830
2825 PRINTRIGHT$(STR$(B),2);"X";RIGHT$(STR$(B),2)
2830 PRINT@32*10+18,"WINS: ";RIGHT$(" "+STR$(WI),3);"/";RIGHT$(" "+STR$(OF),3):RETURN
2840 FORT=11TO14:PRINT@32*T+18,"":NEXT:PRINT@32*15+18,"             ";:POKE16895,96:RETURN
2900 DATA 7,9,11,13,15,17,19,20,22,24,26,28,33
3500 REM
4000 REM CREDITS
4010 REM BY CHRIS HARRINGTON chris.harrington@gmail.com
4020 REM BASED ON COLOROID FOR ANDROID
4030 REM FOR MY WIFE ALISSA
4040 REM CREATED7/4/2010;REVISED7/9/2010
4050 REM 13 SECONDS FOR 10X10 BLOCKFILL
