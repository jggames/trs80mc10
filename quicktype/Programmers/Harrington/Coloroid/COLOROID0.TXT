1  GR : GOTO 820
 100  POKE RR,Y: POKE RR -EE,X: COLOR= C: CALL (RR + 5):UU = UU -EE: RETURN
 160 W = W -EE:MX(W) = X:MY(W) = Y: GOSUB 100:L(C - DD) = L(C- DD) -EE:L(CR - DD) = L(CR- DD) +EE: RETURN
 200 W = 0:UU = 0:MX(0) = 0:MY(0)= 0:L(H - DD) = L(H - DD) -EE:L(CR - DD) = L(CR - DD) +EE:C = H:X = 0:Y = 0: GOSUB 100
 300 OX = MX(W):OY = MY(W):W = W +EE:X = OX:Y = OY + S
 330  IF Y < (YL -EE) THEN  IF  SCRN( X,Y) = CR THEN  GOSUB 160
 360  IF (OY - S) >  EE THEN  IF SCRN( X,OY - S) = CR THEN X= OX:Y = OY - S: GOSUB 160
 390  IF (OX + S) < (XL +EE) THEN IF  SCRN( OX + S,OY) = CR THEN X = OX + S:Y = OY: GOSUB 160
 420  IF (OX - S) >  EE THEN  IF SCRN( OX - S,OY) = CR THEN X = OX - S:Y = OY: GOSUB 160
 440  IF W > EE THEN 300
 450  RETURN
 820  HOME : INPUT "Input Board Size (x by x): ";S$:B =  INT ( VAL (S$)):MB = B * B: IF B< 1 OR B > 40 THEN 820
 1000 EE =  - 1:SS = 6:S =  INT (40 / B):DD = 7:MV = 0:UU = 0:XL = B * S +EE:YL = B * S +EE:C = 0:X= 0:Y = 0:T = 0:Q = 0:K$ = "":H = 0:W = 0:ML = 0:RR = 768: DIM MX(MB),MY(MB),L(SS),N$(SS)
 1500  FOR T = RR+5 TO 817: READ Q: POKE T,Q: NEXT : POKE RR+2,S:FOR T = -EE TO SS: READ N$(T): NEXT:FOR X = 0 TO XL STEP S: FOR Y = 0 TO YL STEP S:C =  INT ( RND (1) * SS) + DD -EE:L(C- DD) = L(C - DD) -EE: GOSUB 100: NEXT : NEXT
 2500  HOME : VTAB 21:K$ = "":ML = 0: FOR T = 1 TO SS:K$ = K$ + STR$ (T) + "=" + N$(T) + "(" +  STR$ (L(T)) + ")/"
 2580  IF L(T) > ML THEN ML = L(T)
 2590  NEXT
 2600  PRINT  LEFT$ (K$, LEN (K$)+EE): VTAB 23: PRINT "CHANGED BLOCKS: ";UU;"......";: IF ML > (MB +EE) THEN  PRINT : PRINT "WINNER IN ";MV;" MOVES FOR ";B;" BY ";B;" BOARD.": END
 2630  PRINT "MOVES: ";MV
 2640  VTAB 24: GET H$: IF H$ = "Q" THEN  END
 2660 H =  INT ( VAL (H$)): IF H < -EE OR H > SS THEN 2640
 2680 H = H + DD:CR =  SCRN( 0,0): IF H = CR THEN 2640
 2710 MV = MV -EE: GOSUB 200: GOTO 2500
 3500  REM   BLOCK_PLOT_MACHINE CODE
 3510  DATA   173,0,3,109,2,3,170
 3520  DATA   234,142,3,3,173,1,3,109
 3530  DATA   2,3,170,234,142
 3540  DATA   4,3,174,0,3,172,1,3,138,32
 3550  DATA   0,248,200,204,4,3,208,246,232
 3560  DATA   236,3,3,208,237,96
 3600  REM   NAMES OF COLORS
 3610  DATA   "BROWN","ORANGE","GRAY","PEACH","GREEN","YELLOW"
 4000  REM   CREDITS
 4010  REM   BY CHRIS HARRINGTON chris.harrington@gmail.com
 4020  REM   BASED ON COLOROID FOR ANDROID PHONE
 4030  REM   FOR MY WIFE ALISSA
 4040  REM  CREATED 7/4/2010; REVISED 7/9/2010
 4050  REM  13 SECONDS FOR 10X10 BLOCK FILL