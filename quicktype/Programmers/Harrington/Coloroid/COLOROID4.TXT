1 CLEAR1000:GOTO820
100 PRINT@32*Y+X,CHR$(143+(16*(C-1)));:UU=UU-EE:RETURN
160 W=W-EE:MX(W)=X:MY(W)=Y:GOSUB100:L(C)=L(C)-EE:L(CR)=L(CR)+EE:RETURN
200 W=0:UU=0:MX(0)=0:MY(0)=0:L(H)=L(H)-EE:L(CR)=L(CR)+EE:C=H:X=0:Y=0:GOSUB100
300 OX=MX(W):OY=MY(W):W=W+EE:X=OX:Y=OY+S
330 IFY<(YL-EE)THENIFPOINT(X*2,Y*2)=CRTHENGOSUB160
360 IF(OY-S)>EETHENIFPOINT(X*2,(OY-S)*2)=CRTHENX=OX:Y=OY-S:GOSUB160
390 IF(OX+S)<(XL-EE)THENIFPOINT((OX+S)*2,OY*2)=CRTHENX=OX+S:Y=OY:GOSUB160
420 IF(OX-S)>EETHENIFPOINT((OX-S)*2,OY*2)=CRTHENX=OX-S:Y=OY:GOSUB160
440 IFW>EETHEN300
450 RETURN
820 CLS:INPUT"INPUT BOARD SIZE (x BY x):";S$:B=INT(VAL(S$)):MB=B*B:IFB<1ORB>16THEN820
830 CLS0
1000 EE=-1:SS=7:S=1:MV=0:UU=0:XL=(B*S+EE):YL=B*S+EE:C=0:X=0:Y=0:T=0:Q=0:K$=""
1001 H=0:W=0:ML=0:DIMMX(MB),MY(MB),L(SS),N$(SS)
1500 FORX=0TOXLSTEPS:FORY=0TOYLSTEPS
1501 C=1+RND(6):L(C)=L(C)+1:GOSUB100:NEXT:NEXT
2500 ML=0:FORT=1TO6:PRINT@32*T+18,RIGHT$(STR$(T),1)+"="+CHR$(143+(16*T))+"("+RIGHT$(" "+STR$(L(T+1)),3)+")"
2580 IFL(T+1)>MLTHENML=L(T+1)
2590 NEXT
2600 PRINT@416+18,"CHANGED:";UU
2602 IFML>(MB+EE)THENPRINT@242,"COMPLETED IN":PRINT@256+18,MV;"MOVES":PRINT@288+18,"FOR A":PRINT@338,B;"BY";B;"GRID.":END
2630 PRINT@448+18,"MOVE:";MV
2640 H$=INKEY$:IFH$="Q"THENEND
2641 IFH$=""THEN2640
2660 H=INT(VAL(H$)):IFH<1ORH>6THEN2640
2680 H=H+1:CR=POINT(0,0):IFH=CRTHEN2640
2710 MV=MV-EE:GOSUB200:GOTO2500
3500 REM
4000 REM CREDITS
4010 REM BY CHRIS HARRINGTON chris.harrington@gmail.com
4020 REM BASED ON COLOROID FOR ANDROID PHONE
4030 REM FOR MY WIFE ALISSA
4040 REM CREATED7/4/2010;REVISED7/9/2010
4050 REM 13 SECONDS FOR 10X10 BLOCKFILL
