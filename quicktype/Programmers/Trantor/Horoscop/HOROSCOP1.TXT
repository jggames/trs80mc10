0 CLEAR1000:CLS:DIMC1,CC,M$:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 REM HOROSCOPE GENERATOR
20 REM BY JEFF TRANTER <TRANTER@POBOX.COM>
30 REM 13 MAY 2012
35 DIM N$(20), C$(20), T$(20), S$(12), A$(3)
40 GOSUB 10000 : REM CLEAR SCREEN
50 PRINT "        YOUR HOROSCOPE"
60 PRINT "        --------------"
80 PRINT "THE PROGRAM GENERATES A PERSONAL";
90 PRINT "HOROSCOPE. I NEED SOME BASIC "
100 PRINT "DETAILS ABOUT YOU. I WILL THEN"
110 PRINT "MAKE A PREDICTION BASED"
120 PRINT "ON YOUR ASTROLOGICAL DATA."
140 REM GET INPUT FROM USER
150 INPUT "FIRST NAME";N$
160 INPUT "YEAR OF BIRTH (####)";Y
170 IF Y < 1900 OR Y > 2012 THEN 160
180 INPUT "MONTH OF BIRTH (1=JAN)";M
190 IF M < 1 OR M >12 THEN 180
200 INPUT "DAY OF THE MONTH";D
210 IF D < 1 OR D > 31 THEN 200
220 INPUT "COUNTRY OF BIRTH";C$
230 INPUT "IN WHAT CITY";T$
240 PRINT "CALCULATING HOROSCOPE..."
250 FOR I = 1 TO 3000 : NEXT I
260 PRINT
270 GOSUB 11000
280 GOSUB 10000
290 M$=N$+", YOUR ASTROLOGICAL SIGN IS:":GOSUB1
300 M$=S$+".":GOSUB1
320 PRINT "HERE IS MY ANALYSIS:"
340 L1 = RND(35)
350 L2 = RND(35)
360 IF L2 = L1 THEN 350
370 L3 = RND(35)
380 IF L3 = L1 THEN 370
390 IF L3 = L2 THEN 370
400 ON L1 GOSUB 12010,12020,12030,12040,12050,12060,12070,12080,12090,12100:IFL1<11THEN410
401 ON L1-10 GOSUB 12110,12120,12130,12140,12150,12160,12170,12180,12190,12200:IFL1<21THEN410
403 ON L1-20 GOSUB 12210,12220,12230,12240,12250,12260,12270,12280,12290,12300,12310,12320,12330,12340,12350
410 PRINT
420 ON L2 GOSUB 12010,12020,12030,12040,12050,12060,12070,12080,12090,12100:IFL2<11THEN430
421 ON L2-10 GOSUB 12110,12120,12130,12140,12150,12160,12170,12180,12190,12200:IFL2<21THEN430
423 ON L2-20 GOSUB 12210,12220,12230,12240,12250,12260,12270,12280,12290,12300,12310,12320,12330,12340,12350
430 PRINT
440 ON L3 GOSUB 12010,12020,12030,12040,12050,12060,12070,12080,12090,12100:IFL3<11THEN450
441 ON L3-10 GOSUB 12110,12120,12130,12140,12150,12160,12170,12180,12190,12200:IFL3<21THEN450
443 ON L3-20 GOSUB 12210,12220,12230,12240,12250,12260,12270,12280,12290,12300,12310,12320,12330,12340,12350
450 PRINT
460 INPUT "ANOTHER HOROSCOPE (Y/N)";A$
470 IF A$ ="Y" THEN 40
480 IF A$ = "N" THEN END
490 GOTO 460
10000 REM CLEAR SCREEN
10010 CLS
10040 RETURN
11000 REM CALCULATE SIGN BASED ON BIRTH DATE
11010 IF M = 1 AND D <= 19 THEN S$ = "CAPRICORN"
11020 IF M = 1 AND D >= 20 THEN S$ = "AQUARIUS"
11030 IF M = 2 AND D <= 18 THEN S$ = "AQUARIUS"
11040 IF M = 2 AND D >= 19 THEN S$ = "PISCES"
11050 IF M = 3 AND D <= 20 THEN S$ = "PISCES"
11060 IF M = 3 AND D >= 21 THEN S$ = "ARIES"
11070 IF M = 4 AND D <= 19 THEN S$ = "ARIES"
11080 IF M = 4 AND D >= 20 THEN S$ = "TAURUS"
11090 IF M = 5 AND D <= 20 THEN S$ = "TAURUS"
11100 IF M = 5 AND D >= 22 THEN S$ = "GEMINI"
11110 IF M = 6 AND D <= 20 THEN S$ = "GEMINI"
11120 IF M = 6 AND D >= 21 THEN S$ = "CANCER"
11130 IF M = 7 AND D <= 22 THEN S$ = "CANCER"
11140 IF M = 7 AND D >= 21 THEN S$ = "LEO"
11150 IF M = 8 AND D <= 22 THEN S$ = "LEO"
11160 IF M = 8 AND D >= 23 THEN S$ = "VIRGO"
11170 IF M = 9 AND D <= 22 THEN S$ = "VIRGO"
11180 IF M = 9 AND D >= 23 THEN S$ = "LIBRA"
11190 IF M = 10 AND D <= 22 THEN S$ = "LIBRA"
11200 IF M = 10 AND D >= 23 THEN S$ = "SCORPIO"
11210 IF M = 11 AND D <= 21 THEN S$ = "SCORPIO"
11220 IF M = 11 AND D >= 22 THEN S$ = "SAGITTARIUS"
11230 IF M = 12 AND D <= 21 THEN S$ = "SAGITTARIUS"
11240 IF M = 12 AND D >= 22 THEN S$ = "CAPRICORN"
11250 RETURN
12000 REM PRINT ANALYSIS
12010 M$="AT TIMES YOU ARE EXTROVERTED, AFFABLE, SOCIABLE, WHILE AT OTHER TIMES YOU ARE INTROVERTED, WARY, RESERVED."
12015 GOSUB1:RETURN
12020 M$="AT TIMES YOU HAVE SERIOUS DOUBTS AS TO WHETHER YOU HAVE MADE THE RIGHT"
12025 M$=M$+" DECISION OR DONE THE RIGHT THING.":GOSUB1:RETURN
12030 M$="DISCIPLINED AND SELF-CONTROLLED OUTSIDE YOU TEND TO BE WORRISOME AND INSECURE"
12035 M$=M$+" INSIDE.":GOSUB1:RETURN
12040 M$="PEOPLE OFTEN TELL YOU THAT YOU REMIND THEM OF SOMEONE ELSE.":GOSUB1:RETURN
12050 M$="SECURITY IS ONE OF YOUR MAJOR GOALS IN LIFE.":GOSUB1:RETURN
12060 M$="SOME OF YOUR ASPIRATIONS TEND TO BE PRETTY UNREALISTIC.":GOSUB1:RETURN
12070 M$="THE BIRTHDAY OF A FRIEND OR RELATIVE IS APPROACHING.":GOSUB1:RETURN
12080 M$="YOU RECENTLY HAD A PROBLEM WITH YOUR AUTOMOBILE OR THAT OF A FAMILY MEMBER.":GOSUB1:RETURN
12090 M$="WHILE YOU HAVE SOME PERSONALITY WEAKNESSES, YOU ARE GENERALLY ABLE TO"
12095 M$=M$+" COMPENSATE FOR THEM.":GOSUB1:RETURN
12100 M$="YOU ARE SOMETIMES INSECURE, ESPECIALLY WITH PEOPLE YOU DON'T KNOW VERY WELL.":GOSUB1:RETURN
12110 M$="YOU ARE SOMETIMES UNCOMFORTABLE IN FRONT OF LARGE GROUPS OF PEOPLE.":GOSUB1:RETURN
12120 M$="YOU DON'T ALWAYS LISTEN TO YOUR SIGNIFICANT OTHER AS MUCH AS YOU SHOULD.":GOSUB1:RETURN
12130 M$="YOU DON'T ALWAYS STOP TO ENJOY THE SIMPLE THINGS IN LIFE.":GOSUB1:RETURN
12140 M$="YOU DON'T ALWAYS TAKE AS MUCH CARE OF YOURSELF AS YOU SHOULD.":GOSUB1:RETURN
12150 M$="YOU DON'T LIKE TIDYING UP.":GOSUB1:RETURN
12160 M$="YOU DON'T LOVE YOURSELF ENOUGH.":GOSUB1:RETURN
12170 M$="YOU HAD AN ACCIDENT WHEN YOU WERE A CHILD INVOLVING WATER.":GOSUB1:RETURN
12180 M$="YOU HAVE A BOX OF OLD UNSORTED PHOTOGRAPHS IN YOUR HOUSE.":GOSUB1:RETURN
12190 M$="YOU HAVE A GREAT DEAL OF UNUSED CAPACITY WHICH YOU HAVE NOT TURNED"
12195 M$=M$+" TO YOUR ADVANTAGE.":GOSUB1:RETURN
12200 M$="YOU HAVE A GREAT NEED FOR OTHER PEOPLE TO LIKE AND ADMIRE YOU.":GOSUB1:RETURN
12210 M$="YOU HAVE A TENDENCY TO BE CRITICAL OF YOURSELF.":GOSUB1:RETURN
12220 M$="YOU HAVE FOUND IT UNWISE TO BE TOO FRANK IN REVEALING YOURSELF TO OTHERS.":GOSUB1:RETURN
12230 M$="YOU ONCE SAID SOMETHING MEAN TO A PERSON THAT YOU LATER REGRETTED SAYING.":GOSUB1:RETURN
12240 M$="YOU PREFER A CERTAIN AMOUNT OF CHANGE AND VARIETY AND BECOME DISSATISFIED"
12245 M$=M$+" WHEN HEMMED IN BY RESTRICTIONS AND LIMITATIONS.":GOSUB1:RETURN
12250 M$="YOU SEE YOURSELF AS AN INDEPENDENT THINKER AND DO NOT ACCEPT OTHERS'"
12255 M$=M$+" STATEMENTS WITHOUT SATISFACTORY PROOF.":GOSUB1:RETURN
12260 M$="YOU RECENTLY HAD AN UNEXPECTED EXPENSE YOU NEEDED TO PAY.":GOSUB1:RETURN
12270 M$="YOU SOMETIMES ASK OTHERS FOR ADVICE WHEN YOU KNOW IN YOUR HEART WHAT THE"
12275 M$=M$+" CORRECT COURSE OF ACTION IS.":GOSUB1:RETURN
12280 M$="YOU SOMETIMES TEND TO GOSSIP TOO MUCH.":GOSUB1:RETURN
12290 M$="YOU WISH THAT YOUR FINANCES WERE MORE SECURE.":GOSUB1:RETURN
12300 M$="YOU WORK HARDER THAN MOST PEOPLE BUT DON'T OFTEN GET THE RECOGNITION YOU"
12305 M$=M$+" FEEL YOU DESERVE.":GOSUB1:RETURN
12310 M$="YOU WORRY ABOUT YOUR HEALTH AS YOU GET OLDER.":GOSUB1:RETURN
12320 M$="YOU'RE HAVING PROBLEMS WITH A FRIEND OR RELATIVE.":GOSUB1:RETURN
12330 M$="YOUR SEXUAL ADJUSTMENT HAS PRESENTED PROBLEMS FOR YOU.":GOSUB1:RETURN
12340 M$="YOUR WORK IS SOMETIMES BORING WHILE AT OTHER TIMES YOU FIND IT VERY CHALLENGING.":GOSUB1:RETURN
12350 M$="YOU ARE CONCERNED ABOUT THE HEALTH OF AN AGING RELATIVE.":GOSUB1:RETURN
13000 END
