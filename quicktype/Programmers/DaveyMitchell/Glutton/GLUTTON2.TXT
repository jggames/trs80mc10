0 CLS0:DIMC,Z,M$:M=16384:GOTO10
7 C=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZ=1TOLEN(M$):POKEC+Z,ASC(MID$(M$,Z))AND63:NEXT:PRINT@C-M+Z,:RETURN

10 TT$="GLUTTON":TBY$="DAVY MITCHELL":TD$="BASED ON GREED BY M.DAY"
50 TV$="VERSION 1.0 AUG 2025":TC$=CHR$(175):GOSUB830
60 GOSUB1450
70 CLS:TL=0:T$="WELCOME TO GLUTTON":GOSUB1330
80 TL=2:T$="W / S = UP AND DOWN":GOSUB1320
90 TL=3:T$="A / D = LEFT AND RIGHT":GOSUB1320
100 TL=5:T$="ENTER = FINISH ROUND ":GOSUB1320
110 TL=7:T$="MOVING ONTO A NUMBERED SQUARE":GOSUB1320
120 TL=8:T$="MOVES YOU N STEPS IN THAT":GOSUB1320
130 TL=9:T$="DIRECTION. YOU MUST STAY ON THE":GOSUB1320
140 TL=10:T$="BOARD AND CANNOT STEP ON A":GOSUB1320
150 TL=11:T$="SQUARE TWICE. KEEP PLAYING":GOSUB1320
160 TL=12:T$="UNTIL YOU CANNOT MAKE A MOVE.":GOSUB1320

170 GOSUB 1450

REM SET THE INITIAL PLAYING FIELD
190 FORC=MTOM+512:POKEC,48+RND(9):NEXT

REM INIT GAME
260 S=0:X=16:Y=8
280 POKEM+X-1+32*(Y-1),106
290 A$=INKEY$:IFA$=""THEN290
300 IFA$="A"ANDX>1THENGOSUB400
310 IFA$="D"ANDX<32THENGOSUB470
320 IFA$="W"ANDY>1THENGOSUB550
330 IFA$="S"ANDY<16THENGOSUB630
340 IFA$=CHR$(13)THENGOSUB710:GOTO70
350 S=S+D:GOTO280

REM *** MOVE LEFT ***
400 D=PEEK(M+X-2+32*(Y-1))-48:IFD=>XTHENSOUND100,1:RETURN
410 V=0:FORJ=X-1TOX-DSTEP-1:IFPEEK(M+J-1+32*(Y-1))>99THENV=1
420 NEXT:IFV=1THENSOUND100,1:RETURN
430 FORJ=XTOX-DSTEP-1:POKEM+J-1+32*(Y-1),128:NEXT
440 X=X-D:RETURN

REM *** MOVE RIGHT ***
470 D=PEEK(M+X+32*(Y-1))-48:IFD+X>32THENSOUND100,1:RETURN
490 V=0:FORJ=X+1TOX+D:IFPEEK(M+J-1+32*(Y-1))>99THENV=1
500 NEXT:IFV=1THENSOUND100,1:RETURN
510 FORJ=XTOX+D:POKEM+((Y-1)*32)+(J-1),128:NEXT
520 X=X+D:RETURN

REM *** MOVE UP ***
550 D=PEEK(M+X-1+32*(Y-2))-48:IFY-D<1THENSOUND100,1:RETURN
570 V=0:FORJ=Y-1TOY-DSTEP-1:IFPEEK(M+X-1+32*(J-1))>99THENV=1
580 NEXT:IFV=1THENSOUND100,1:RETURN
590 FORJ=YTOY-DSTEP-1:POKEM+(J-1)*32+X-1,128:NEXT
600 Y=Y-D:RETURN

REM *** MOVE DOWN ***
630 D=PEEK(M+X-1+32*(Y))-48:IFY+D>16THENSOUND100,1:RETURN
650 V=0:FORJ=Y+1TOY+D:IFPEEK(M+X-1+32*(J-1))>99THENV=1
660 NEXT:IFV=1THENSOUND100,1:RETURN
670 FORJ=YTOY+D:POKEM+((J-1)*32)+(X-1),128:NEXT:Y=Y+D:RETURN

REM SCORE PAGE
710 CLS:IFS>HSTHENHS=S
730 TT$="GLUTTON":TBY$="SCORE:"+STR$(S):TD$="":TV$="HIGH SCORE:"+STR$(HS):TC$=CHR$(175):GOSUB830:GOSUB1450
760 RETURN

REM TITLE SCREEN
830 TM=LEN(TT$):IFLEN(TBY$)>TMTHENTM=LEN(TBY$)
840 IFLEN(TV$)>TMTHENTM=LEN(TV$)
850 IFLEN(TD$)>TMTHENTM=LEN(TD$)
860 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1260
870 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1140
880 PRINT@(BY+1)*32+(32-LEN(TT$))/2,TT$;
890 PRINT@(BY+3)*32+(32-LEN(TBY$))/2,TBY$;
900 PRINT@(BY+5)*32+(32-LEN(TD$))/2,TD$;
910 PRINT@(BY+7)*32+(32-LEN(TV$))/2,TV$;
920 RETURN

REM VERTICAL LINE
970 LZ=LY+LL-1:FORBR=LYTOLZ:PRINT@LX+BR*32,LC$;:NEXT:RETURN

REM HORIZONTAL LINE
1040 LO$="":FORBR=1TOLL:LO$=LO$+LC$:NEXT:PRINT@LX+(LY*32),LO$;:RETURN

REM BOX OUTLINE
1140 BR$="":FORBR=1TOBW:BR$=BR$+BC$:NEXT:PRINT@BX+(BY+1)*32,BR$;:PRINT@BX+(BY+BH)*32,BR$;
1190 BH=BH-1:BY=BY+1:BW=BW-1:FORBQ=1TOBH:PRINT@BX+((BY+BQ)*32),BC$;:PRINT@BX+BW+(BY+BQ)*32,BC$;:NEXT:RETURN

1220 REM FILLED BOX
1260 BR$="":FORBR=1TOBW:BR$=BR$+BC$:NEXT:FOR BQ=1TOBH:PRINT@BX+(BY-1+BQ)*32,BR$;:NEXT:RETURN

REM CENTER TEXT
1320 PRINT@(32-LEN(T$))/2+32*TL,T$;:RETURN
1330 M$=T$:PRINT@(32-LEN(T$))/2+32*TL,;:GOSUB7:RETURN

REM WAIT FOR ANY KEY
1400 WA$=INKEY$:IF WA$=""THEN1400
1410 RETURN

REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1450 PRINT@483,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7:GOSUB1400:RETURN

1460 REM MC-10 EDITS JIM GERRIE
