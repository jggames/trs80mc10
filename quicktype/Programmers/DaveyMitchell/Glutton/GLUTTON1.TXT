0 CLS0:DIMC,Z,M$:M=16384:GOTO10
7 C=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZ=1TOLEN(M$):POKEC+Z,ASC(MID$(M$,Z))AND63:NEXT:?@C-M+Z,:RETURN

10 DIMG(32,16)
40 TT$="GLUTTON":TBY$="DAVY MITCHELL":TD$="BASED ON GREED BY M.DAY"
50 TV$="VERSION 1.0 AUG 2025":TC$=CHR$(175):GOSUB830
60 GOSUB1450
70 CLS:TL=0:T$="WELCOME TO GLUTTON":GOSUB1330
80 TL=2:T$="W / S = UP AND DOWN":GOSUB1320
90 TL=3:T$="A / D = LEFT AND RIGHT":GOSUB1320
100 TL=5:T$="ENTER = FINISH ROUND ":GOSUB1320
110 TL=7:T$="MOVING ONTO A NUMBERED SQUARE":GOSUB1320
120 TL=8:T$="MOVES YOU N STEPS IN THAT":GOSUB1320
130 TL=9:T$="DIRECTION. YOU MUST STAY ON THE":GOSUB1320
140 TL=10:T$="BOARD AND CANNOT STEP ON A":GOSUB1320
150 TL=11:T$="SQUARE TWICE. KEEP PLAYING":GOSUB1320
160 TL=12:T$="UNTIL YOU CANNOT MAKE A MOVE.":GOSUB1320

170 GOSUB 1450

REM SET THE INITIAL PLAYING FIELD
190 FORY=1TO16
200 FORX=1TO32
210 G(X,Y)=RND(9)
220 POKEM+(Y-1)*32+X-1,48+G(X,Y)
230 NEXT
240 NEXT

REM INIT GAME
260 S=0
270 X=15:Y=8:G(X,Y)=0
280 POKEM+(Y-1)*32+X-1,106
290 A$=INKEY$:IFA$=""THEN290
300 IFA$="A"ANDX>1THENGOSUB390
310 IFA$="D"ANDX<32THENGOSUB470
320 IFA$="W"ANDY>1THENGOSUB550
330 IFA$="S"ANDY<16THENGOSUB620
340 IFA$=CHR$(13)THENGOSUB710:GOTO70
350 S=S+D
360 GOTO280

REM *** MOVE LEFT ***
390 D=G(X-1,Y)
400 IFD=0ORD=>XTHENSOUND100,1:RETURN
410 V=0:FORJ=X-1TOX-DSTEP-1:IFG(J,Y)=0THENV=1
420 NEXT:IFV=1THENSOUND100,1:RETURN
430 FORJ=XTOX-DSTEP-1:G(J,Y)=0:POKEM+(Y-1)*32+J-1,128:NEXT
440 X=X-D
450 RETURN

REM *** MOVE RIGHT ***
470 D=G(X+1,Y)
480 IFD=0ORD+X>32THENSOUND100,1:RETURN
490 V=0:FORJ=X+1TOX+D:IFG(J,Y)=0THENV=1
500 NEXT:IFV=1THENSOUND100,1:RETURN
510 FORJ=XTOX+D:G(J,Y)=0:POKEM+((Y-1)*32)+(J-1),128:NEXT
520 X=X+D
530 RETURN

REM *** MOVE UP ***
550 D=G(X,Y-1)
560 IFD=0ORY-D<1THENSOUND100,1:RETURN
570 V=0:FOR J=Y-1 TO Y-D STEP-1:IFG(X,J)=0THENV=1
580 NEXT:IFV=1THENSOUND100,1:RETURN
590 FORJ=YTOY-DSTEP-1:G(X,J)=0:POKEM+(J-1)*32+X-1,128:NEXT
600 Y=Y-D
610 RETURN
620 REM *** MOVE DOWN ***
630 D=G(X,Y+1)
640 IFD=0ORY+D>16THENSOUND100,1:RETURN
650 V=0:FOR J=Y+1 TO Y+D:IFG(X,J)=0THENV=1
660 NEXT:IFV=1THENSOUND100,1:RETURN
670 FORJ=YTOY+D:G(X,J)=0:POKEM+((J-1)*32)+(X-1),128:NEXT
680 Y=Y+D
690 RETURN

REM SCORE PAGE
710 CLS
720 IFS>HSTHENHS=S
730 TT$="GLUTTON":TBY$="SCORE:"+STR$(S):TD$=""
740 TV$="HIGH SCORE:"+STR$(HS):TC$=CHR$(175):GOSUB830
750 GOSUB1450
760 RETURN

REM TITLE SCREEN
830 TM=LEN(TT$):IFLEN(TBY$)>TMTHENTM=LEN(TBY$)
840 IFLEN(TV$)>TMTHENTM=LEN(TV$)
850 IFLEN(TD$)>TMTHENTM=LEN(TD$)
860 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB1260
870 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB1140
880 ?@(BY+1)*32+(32-LEN(TT$))/2,TT$;
890 ?@(BY+3)*32+(32-LEN(TBY$))/2,TBY$;
900 ?@(BY+5)*32+(32-LEN(TD$))/2,TD$;
910 ?@(BY+7)*32+(32-LEN(TV$))/2,TV$;
920 RETURN

REM VERTICAL LINE
970 LZ=LY+LL-1:FORBR=LYTOLZ:PRINT@LX+BR*32,LC$;:NEXT:RETURN

REM HORIZONTAL LINE
1040 LO$="":FORBR=1TOLL:LO$=LO$+LC$:NEXT:PRINT@LX+(LY*32),LO$;:RETURN

REM BOX OUTLINE
1140 BR$="":FORBR=1TOBW:BR$=BR$+BC$:NEXT
1170 PRINT@BX+(BY+1)*32,BR$;:PRINT@BX+(BY+BH)*32,BR$;
1190 BH=BH-1:BY=BY+1:BW=BW-1
1200 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:PRINT@BX+BW+(BY+BQ)*32,BC$;:NEXT
1210 RETURN

1220 REM FILLED BOX
1260 BR$="":FORBR=1TOBW:BR$=BR$+BC$:NEXT:FOR BQ=1TOBH:PRINT@BX+(BY-1+BQ)*32,BR$;:NEXT:RETURN

REM CENTER TEXT
1320 PRINT@(32-LEN(T$))/2+32*TL,T$;:RETURN
1330 M$=T$:PRINT@(32-LEN(T$))/2+32*TL,;:GOSUB7:RETURN

REM WAIT FOR ANY KEY
1400 WA$=INKEY$:IF WA$=""THEN1400
1410 RETURN

REM INVERTED ANY KEY PROMPT AT BOTTOM OF SCREEN.
1450 PRINT@483,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7:GOSUB1400:RETURN

REM MC-10 EDITS JIM GERRIE
