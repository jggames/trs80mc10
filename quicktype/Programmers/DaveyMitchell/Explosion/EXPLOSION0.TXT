10 DIM X,Y,SI,EX,EY,PX,PY,PV,RB(5,5), SB(5,5), ST(5,5) 
20 CLS0:TT$="EXPLOSION":TBY$="HAL RENKO & SAM EDWARDS":TD$="MAY 2023":TV$="MC-10 PORT DAVY MITCHELL":TC$=CHR$(175)
25 GOSUB 6000:GOSUB 9930
30 GOTO 2000 
40 FOR X=1 TO SI:FOR Y=1 TO SI:SB(X,Y)=RB(X, Y): NEXT Y,X: RETURN 
50 FOR X=1 TO SI:FOR Y=1 TO SI:RB(X,Y)=SB(X, Y): NEXT Y,X: RETURN 
100 REM**INITIALIZE**
105 PRINT "WELCOME TO EXPLOSION!" : PRINT
110 PRINT "ENTER SIZE OF BOARD (3 OR 4)" : PRINT
115 A$ = INKEY$:IF A$="3" OR A$="4"THEN SI=VAL(A$):GOTO120
117 GOTO 115
120 IF SI<3 OR SI>4 THEN 110 
130 FOR X=1 TO SI 
140 FOR Y=1 TO SI 
150 ST(X,Y)=4+(X=1)+(X=SI)+(Y=1)+(Y=SI) 
160 NEXT Y,X 
170 PRINT "WHO STARTS? YOU (1) OR ME (2)"
175 A$ = INKEY$:IF A$="1" OR A$="2"THEN CM=VAL(A$)-1:GOTO 190
178 GOTO 175
190 CLS 
200 PRINT @69, "X1 X2 X3" 
210 IF SI=4 THEN PRINT@78,"X4"
220 FOR Y=1 TO SI 
230 PRINT@(Y+2)*32,"Y" + RIGHT$(STR$(Y),1);
240 FOR X=1 TO SI 
250 REM PRINT RB(X, Y); : PX = X: PY = Y: PV = RB(X, Y): GOSUB 3000
251 PX = X: PY = Y: PV = RB(X, Y): GOSUB 3000
260 NEXT 
270 REM PRINT 
280 NEXT Y 
290 RETURN 
300 REM **INPUT AND EXECUTE MOVE** 
310 PRINT @224,; 
320 INPUT"X";MX 
330 MX=INT (MX) 
340 IF MX<1 OR MX>SI THEN 310 
350 INPUT"Y";MY 
360 MY=INT (MY) 
370 IF MY<1 OR MY>SI THEN 310
380 IF RB(MX, MY)<0 THEN 310 
390 GOSUB 40 
400 X=MX:Y=MY:DI=-1
410 GOSUB 500: REM OUTPUT EXPLOSION 
420 IF ET THEN 1200:REM END 
430 GOSUB 50 
440 RETURN 
500 REM **OUTPUT EXPLOSION** 
510 SB(X,Y)=SB(X,Y)+PL 
520 NE = 0 
530 REM IF DI THEN PRINT @66+3*X+32*Y, SB(X, Y); 
531 IF DI THEN PX = X: PY = Y: PV = SB(X, Y): GOSUB 3000 
540 XP=0 
550 FOR X = 1 TO SI 
560 FOR Y = 1 TO SI 
570 IF ABS(SB(X,Y)) < ST(X,Y) THEN 660 
580 XP = - 1 
590 NE = NE + 1 
600 SB(X,Y)=SB(X,Y)-ST(X,Y)*PL 
610 REM IF DI THEN PRINT @66+32*Y+3*X, SB(X, Y);
611 IF DI THEN PX = X: PY = Y: PV = SB(X, Y): GOSUB 3000
620 EX=X:EY=Y+1: GOSUB 700
630 EX=X+1:EY=Y: GOSUB 700 
640 EX=X:EY=Y-1: GOSUB 700 
650 EX=X-1:EY=Y: GOSUB 700 
660 NEXT Y, X 
670 ET=(NE>SI*SI) 
680 IF XP AND NOT ET THEN 540
690 RETURN 
700 REM **ADD TO NEIGHBOURS** 
710 SB(EX, EY)=PL*(ABS(SB(EX, EY))+1) 
720 REM IF DI AND ST(EX, EY)>0 THEN PRINT @66+32*EY+3*EX, SB(EX, EY);
721 IF DI AND ST(EX, EY)>0 THEN PX = EX: PY = EY: PV = SB(EX, EY): GOSUB 3000
730 RETURN 
800 REM **COMPUTER MOVES** 
805 PRINT @224,"":PRINT @256,"":PRINT @290, "CPU THINKING"
810 BE=1000 
820 FOR TX = 1 TO SI 
825 PRINT @290+12+TX,".";
830 FOR TY = 1 TO SI 
840 IF RB(TX, TY)>0 THEN 910 
850 GOSUB 40 
860 X=TX: Y=TY:DI=0 
870 GOSUB 500: REM OUTPUT EXPLOSION 
880 IF ET THEN MX=TX: MY=TY:GOTO 920 
890 GOSUB 1000: REM EVALUATE SITUATION
900 IF EN<BE OR EN=BE AND RND(10)<4 THEN BE=EN: MX=TX:MY=TY
910 NEXT TY, TX 
920 REM ACTUAL MOVE 
930 GOSUB 40 
935 PRINT @290, "CPU MOVE "MX","MY
940 X=MX:Y=MY:DI=-1
950 GOSUB 500: REM OUTPUT EXPLOSION 
960 IF ET THEN 1200: REM GAME OVER
970 GOSUB 50 
980 RETURN 
1000 REM** EVALUATE SITUATION ** 
1010 EN=0 
1020 FOR X = 1 TO SI 
1030 FOR Y = 1 TO SI 
1040 EN=EN+SB(X, Y) 
1050 IF -SB(X,Y)<ST(X, Y)-1 THEN 1110 
1060 EN=EN-2 
1070 IF SB(X + 1, Y) = ST(X + 1, Y) - 1 THEN EN= EN + 10
1080 IF SB(X, Y + 1) = ST(X, Y + 1) - 1 THEN EN= EN + 10 
1090 IF SB(X - 1, Y) = ST(X - 1, Y) - 1 THEN EN= EN + 10 
1100 IF SB(X, Y - 1) = ST(X, Y - 1) - 1 THEN EN= EN + 10 
1110 NEXT Y,X 
1120 RETURN
1200 REM ** END ** 
1210 CLS :PRINT @160, "AN ETERNAL EXPLOSION RESULTED,  SO "; 
1220 IF PL = 1 THEN PRINT "YOU":GOTO1230
1225 PRINT"I"; 
1230 PRINT" HAVE WON IN "NT" TURNS" 
1235 FOR X=0 TO 4:FOR Y=0 TO 4:RB(X,Y)=0:SB(X,Y)=0: NEXT Y,X:NT=0
1240 PRINT:PRINT"PRESS A KEY TO PLAY AGAIN"
1245 GOSUB9810
1259 RUN
2000 REM ** MAIN PROGRAM ** 
2010 CLS:GOSUB 100: REM INITIALIZE 
2020 IF CM THEN 2040 
2030 PL = 1 : GOSUB 300: REM INPUT AND EXECUTE MOVE 
2040 PL = -1 : GOSUB 800: REM COMPUTER MOVES 
2050 NT = NT + 1 
2060 GOTO 2030
3000 REM ** DRAW ON BOARD **
3020 Z=16384 + 67 + (PX*3) + (PY * 32)
3021 OC = 65: IF PV=0 THEN OC=110:GOTO 3025
3023 IF PV<0 THEN OC = 48 + ABS(PV): GOTO 3030
3025 OC=112+PV
3030 POKE Z, OC
3040 RETURN

6000 REM TITLE SCREEN
6005 TM=LEN(TT$):IFLEN(TBY$)>TMTHENTM=LEN(TBY$)
6010 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
6020 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
6030 ?@BX+(BY+1)*32 + ((BW-LEN(TT$))/2),TT$;
6040 ?@BX+(BY+3)*32 + ((BW-LEN(TBY$))/2),TBY$;
6050 ?@BX+(BY+5)*32 + ((BW-LEN(TD$))/2),TD$;
6060 ?@BX+(BY+7)*32 + ((BW-LEN(TV$))/2),TV$;
6100 RETURN

9410 BR$=""
9420 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN

9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN

9610 PRINT@(32-LEN(T$))/2+32*TL,T$;:RETURN

9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN

9930 TL=15:BC$=CHR$(128):T$="press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue"
9940 GOSUB9610:GOSUB9810:RETURN
