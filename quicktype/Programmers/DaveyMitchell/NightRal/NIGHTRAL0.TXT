1 DIML,X,Y,P,G,H,I,S,HS
5 CLS0:GOSUB9000:FORD=1 TO 1000:NEXT:SOUND200,4:GOSUB9800
10 GOSUB1000
15 X=16:L=10:P=16:S=0
20 FORY=16TO31STEP3:SET(L+X,Y,8):G=L+X:H=Y:SET(L+42-X,Y,8):I=L+42-X:X=X-0.5:NEXT
25 S=S+1
30 IFPOINT(G,H-1)=8ORPOINT(I,H-1)=8THENGOSUB8000:GOTO5
100 X=16:FORY=16TO31STEP3:RESET(L+X,Y):RESET(L+42-X,Y):X=X-0.5:NEXT
190 K=PEEK(2)ANDPEEK(17023)
200 IF K=80 THEN PRINT@480+P,CHR$(128);:P=P+1:PRINT@480+P,CHR$(191);:GOTO260
210 IF K=81 THEN PRINT@480+P,CHR$(128);:P=P-1:PRINT@480+P,CHR$(191);
260 X=16:L=L+4-(RND(7)):IFL<2THENL=2:GOTO20
265 IFL>19THENL=19:GOTO20
399 GOTO20
999 REM GAME SCREEN
1000 CLS0:PRINT@496,CHR$(191);:P=16
1010 FORX=0TO63:SET(X,15,6):NEXT
1020 FORD=0TO10:SET(12+RND(25)*2,RND(6)*2,2):NEXT
1250 P$=CHR$(207)
1260 PRINT@3, P$+P$+P$+P$;
1270 PRINT@32+1, P$+P$+P$+P$+P$+P$+P$+P$;
1280 PRINT@64+1, P$+P$+P$+P$+P$+P$+P$+P$;
1290 PRINT@96+3, P$+P$+P$+P$;
1400 RETURN
5000 END
8000 REM CRASH
8010 FORR=0TO10
8040 SOUND45+RND(50),2:POKE49151,64:FORD=1TO50:NEXTD
8050 TL=RND(10):T$="C*R*A*S*H @ SCORE " + STR$(S):GOSUB9600:A$=INKEY$
8100 NEXT
8200 IF S>HS THEN HS=S
8500 RETURN
9000 REM TITLE SCREEN
9010 TL=2:T$="NIGHT RALLY":GOSUB9600
9020 TL=4:T$="BY DAVY MITCHELL":GOSUB9600
9025 TL=8:T$="HIGH SCORE : " + STR$(HS) :GOSUB9600
9030 TL=11:T$="INSPIRED BY 'NIGHT DRIVER'":GOSUB9600
9040 TL=12:T$=" ON D32 BY CLIVE GIFFORD":GOSUB9600
9050 P$=CHR$(207)
9060 PRINT@3, P$+P$+P$+P$;
9070 PRINT@32+1, P$+P$+P$+P$+P$+P$+P$+P$;
9080 PRINT@64+1, P$+P$+P$+P$+P$+P$+P$+P$;
9090 PRINT@96+3, P$+P$+P$+P$;
9500 RETURN
9600 PRINT@(32-LEN(T$))/2+32*TL,T$;
9700 RETURN
9800 A$=INKEY$:IF A$="" THEN 9800
9810 RETURN
