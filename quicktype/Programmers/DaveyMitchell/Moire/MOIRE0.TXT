10 DIM L(64,32),QX,QY,X,Y,C,D
20 GOSUB2000:CLS0: POKE 65495,0:PRINT"PLEASE WAIT";
30 QY=-1
40 FOR Y=-15 TO 15:QY=QY+1:QX=-1
50 FOR X=-31 TO 32:QX=QX+1
60 L(QX,QY)=SQR(X*X+Y*Y)
70 NEXT:?".";:NEXT
80 CLS 0:C=C+1 : QY=-1:D=RND(8)
90 FOR Y=-15 TO 15:QY=QY+1:QX=-1
100 FOR X=-31 TO 32:QX=QX+1
120 IF L(QX,QY)*C AND 2 THEN SET(X+31,Y+15,D)
130 NEXT:NEXT:GOTO 80

2000 CLS0:TT$="MOIRE PATTERNS":TBY$="JOSIP KALEBIC":TD$="BASIC PROGRAMMING FB GROUP"
2010 TV$="PORTED FROM THE C64":TC$=CHR$(159):GOSUB 9010
2020 GOSUB9930:GOTO9810
2500 RETURN
9010 TM=LEN(TT$):IF LEN(TBY$)>TM THEN TM=LEN(TBY$)
9015 IF LEN(TV$)>TM THEN TM=LEN(TV$)
9017 IF LEN(TD$)>TM THEN TM=LEN(TD$)
9020 BH=7:BW=TM+2:BY=3:BX=(30-BW)/2+1:BC$=CHR$(143):GOSUB9510
9030 BH=9:BW=TM+4:BY=1:BX=(32-BW)/2:BC$=TC$:GOSUB9410
9040 ?@(BY+1)*32 + ((32-LEN(TT$))/2),TT$;
9050 ?@(BY+3)*32 + ((32-LEN(TBY$))/2),TBY$;
9060 ?@(BY+5)*32 + ((32-LEN(TD$))/2),TD$;
9070 ?@(BY+7)*32 + ((32-LEN(TV$))/2),TV$;
9100 RETURN
9410 BR$=""
9420 BR$=STRING$(BW,BC$)
9430 ?@BX+((BY+1)*32),BR$;
9435 ?@BX+((BY+BH)*32),BR$;
9437 BH=BH-1:BY=BY+1:BW=BW-1
9440 FOR BQ=1TOBH:?@BX+((BY+BQ)*32),BC$;:?@BX+BW+((BY+BQ)*32),BC$;:NEXT
9499 RETURN
9510 BR$=""
9520 FOR BR=1TOBW:BR$=BR$+BC$:NEXT
9530 FOR BQ=1TOBH:?@BX+((BY-1+BQ)*32),BR$;:NEXT
9599 RETURN
9810 WA$=INKEY$:IF WA$="" THEN 9810
9820 RETURN
9930 BC$=CHR$(128):?@483,"press"+BC$+"any"+BC$+"key"+BC$+"to"+BC$+"continue";:GOSUB9810:RETURN