0 T=0:P=16:A$=CHR$(92)+"/":B$="########################       #########################":C$="########":D$="/"+CHR$(92):GOTO6
1 K=PEEK(2)ANDPEEK(17023):X=(K=65ANDP>0):Y=-(K=83):P=P+X+Y:E=PEEK(P+16384):F=PEEK(P+16385):?@P,A$;:IFE=96ANDF=96THENRETURN
2 FORT=0TO8:R=(P-T)*-(P-T>0):?@R,"*";:?@P+T,"*";:?@P+(T*32),"*";:NEXT:?@480,"SCORE"S,"AGAIN";:INPUTI$:S=0:ON-(I$="Y")GOTO0:END
3 S=S+100:FORT=1TO14:PRINT@480,"":SOUNDT*10,2:NEXT:RETURN
4 CLS:GOSUB3:I=8:FORS=STOS+200:R=2-RND(3):FORT=1TORND(3):I=I+R*-(I+R>0ANDI+R<16):?@480,MID$(B$,I,32);:GOSUB1:NEXT:NEXT:GOSUB3
5 FORS=STOS+500:SOUND9,1:FORT=1TO100:?@480,C$;TAB(24);C$;:?@408+RND(47),"#";:GOSUB1:NEXT:S=S+T:?@480,MID$(B$,8+RND(8),32);:NEXT
6 CLS0:FORT=1TO20:SET(RND(63),RND(31),5):NEXT:SOUND100,9:GOSUB3:FORT=1TO10:P(T)=RND(256)+250:NEXT:R(1)=1:R(2)=-1:R(3)=-2:R(4)=2
7 FORS=STOS+100:FORT=1TO5:R=RND(4):PRINT@P(T),"  ";:P(T)=P(T)+(R(R)*-(P(T)+R(R)>0)):PRINT@P(T),D$;:P(T)=P(T)-32:IFP(T)<0THEN9
8 NEXT:PRINT@511," ";:GOSUB1:NEXT:GOTO4
9 P(T)=RND(256)+250:GOTO8
10 REM THE MONTEIRO CHALLENGE
11 REM A&S TO MOVE LEFT & RIGHT
12 REM 3 LEVELS. 100 PTS PER LV
13 REM THEN BACK TO THE START
14 REM 5 GATES ON LEVEL 5.
15 REM GO AS FAR AS POSSIBLE.
16 REM CONCEPT: ERICO MONTEIRO
17 REM PROGRAMMING: JIM GERRIE
