0 CLEAR500:CLS:PRINT"THE GRAIL OF THE GODS":PRINT"PLEASE WAIT...":GOTO10
1 P3=P1-(P2*INT(P1/P2)):RETURN
5 C1=1:CC=10:FORVP=0TO14:PRINT@32*VP+22,"          ";:NEXT:VP=0
6 IFMID$(A$,CC,1)<>""ANDMID$(A$,CC,1)<>" "THENCC=CC-1:GOTO6
7 PRINT@32*VP+22,MID$(A$,C1,CC-C1);:C1=CC+1:CC=C1+9:VP=VP+1:IFC1<LEN(A$)THEN6
8 RETURN
9 W=W+1:RETURN
10 DIML(10,4):RESTORE:FORT=1TO10:FORU=1TO4:READA:L(T,U)=A:NEXTU:NEXTT
20 DIMM$(12,2):FORT=1TO12:READA$,B$:M$(T,1)=A$:M$(T,2)=B$:NEXT
30 DIMW$(20),M(22,18),N(22,18):FORT=1TO20:READA$:W$(T)=A$:NEXT
40 DEPTH=0:RANK=1:XP=0:GOLD=0:WEP=20:ARM=20:HP=40
50 DEPTH=DEPTH+1:FAIL=0
60 FORX=1TO20:FORY=1TO16:N(X,Y)=0:IFX=1ORX=20ORY=1ORY=16THENM(X,Y)=1:GOTO80
70 M(X,Y)=0
80 NEXT:NEXT
90 FAIL=FAIL+1:X=RND(19)+2:Y=RND(15)+2:P=0:Q=0
110 FORT=X-1TOX+1:FORU=Y-1TOY+1:IFM(T,U)=1THENQ=INT(Q+(2^P))
120 P=P+1:NEXT:NEXT
140 IFQ=2ORQ=3ORQ=6ORQ=7ORQ=8ORQ=9ORQ=32ORQ=36ORQ=72ORQ=73ORQ=128ORQ=192ORQ=288ORQ=292ORQ=384ORQ=448THENM(X,Y)=1:FAIL=0:GOTO90
150 IFFAIL<=500THEN90
160 Y=RND(14)+2:IFM(2,Y)<>0THEN160
170 M(2,Y)=2:PX=2:PY=Y
180 Y=RND(14)+2:IFM(19,Y)<>0THEN180
190 IFDEPTH<10THENM(19,Y)=3:GOTO210
200 M(19,Y)=10
210 WW=4:TI=5+(DEPTH/2):ITEM=5:GOSUB1000
220 WW=2:P1=(DEPTH+3):P2=3:GOSUB1:TI=P3:ITEM=8:GOSUB1000
230 WW=7:P1=DEPTH:P2=3:GOSUB1:TI=P3+2:ITEM=4:GOSUB1000
240 TI=1:ITEM=6:GOSUB1000:ITEM=7:GOSUB1000:ITEM=9:GOSUB1000:GOSUB2000
250 FORXX=PX-1TOPX+1:FORYY=PY-1TOPY+1:ON1-(M(XX,YY)=5)GOTO280:GOSUB300
280 NEXT:NEXT:GOTO400
300 P1=(XX*YY):P2=3:GOSUB1:MT=P3+DEPTH
310 H=MT*8
320 D=RND(WEP)+RANK:WEP=WEP-1:H=H-D:A$="YOU HIT THE "+M$(MT,1)+"FOR"+STR$(D)+" DAMAGE":GOSUB5
322 PRINT@(XX-1)+(YY-1)*32,"X";:FORZZ=1TO2500:NEXT
328 D=RND(MT*3)+DEPTH:DR=(ARM*2)/D:IFDR>=DTHENDR=D-1:D=1
330 HP=HP-D:ARM=ARM-1:A$="THE "+M$(MT,1)+M$(MT,2)+"YOU FOR"+STR$(D)+" DAMAGE":GOSUB5
334 PRINT@(PX-1)+(PY-1)*32,"X";:FORZZ=1TO2500:NEXT
340 IFWEP<3THENWEP=3
342 IFARM<1THENARM=0
344 IFH<1ORHP<1THEN350
346 GOTO320
350 IFHP<1THEN4000
352 GOSUB2000
354 A$="YOU KILLED THE "+M$(MT,1):GOSUB5:FORZZ=1TO2500:NEXT
358 XP=XP+INT((MT/3)+1):M(XX,YY)=0
399 RETURN
400 DX=0:DY=0:I$=INKEY$:IFI$=""THEN400
410 IFI$="A"THENDX=-1:GOTO450
420 IFI$="S"THENDX=1:GOTO450
430 IFI$="W"THENDY=-1:GOTO450
440 IFI$="Z"THENDY=1:GOTO450
445 GOTO400
450 V=M(PX+DX,PY+DY):A$=""
460 IFV=4THENGOLD=GOLD+(DEPTH+2)*5*DEPTH:V=0:XP=XP+1:A$="YOU FOUND "+STR$((DEPTH+2)*5*DEPTH)+" GOLD"
470 IFV=6THENARM=ARM+10:V=0:A$="YOU FOUND "+W$(11+RND(4))+W$(16+RND(4))
480 IFV=7THENWEP=WEP+10:V=0:A$="YOU FOUND A "+W$(RND(4)+1)+W$(RND(4)+6)
490 IFV=8THENHP=HP-RND(DEPTH*4):V=0:A$="YOU WERE HURT BY A TRAP":IFHP<1THENHP=1
500 IFV=9THENHP=15+RANK*25:V=0:A$="YOU FOUND A HEALING POTION"
520 IFV=10THEN4000
530 IFV=0THENM(PX,PY)=0:PX=PX+DX:PY=PY+DY:M(PX,PY)=2:GOSUB2000:IFA$<>""THENGOSUB5
550 IFXP>=(RANK*3)THENGOSUB950
560 IFWEP>RANK*10+10THENWEP=RANK*10+10
570 IFARM>RANK*10+10THENARM=RANK*10+10
580 IFV=3THEN50
590 GOTO250
950 XP=XP-(RANK*3):RANK=RANK+1:HP=15+RANK*25:WEP=WEP+10:ARM=ARM+10:GOSUB2000:A$="YOU HAVE GAINED A HIGHER RANK!"
960 GOSUB5:FORZZ=1TO2500:NEXT
970 RETURN
999 REM 23 LINES (1 LINE) TO PLACE THINGS
1000 FORG=1TOTI:FAIL=0
1010 REM REPEAT + REPEAT
1020 FAIL=FAIL+1:X=RND(18)+2:Y=RND(14)+2:ONM(X,Y)GOTO1020,1020,1020,1020,1020,1020,1020,1020,1020,1020
1030 W=0:FORT=X-1TOX+1:FORU=Y-1TOY+1:ONM(T,U)GOSUB9
1040 NEXT:NEXT:IFW=WWORWW=0ORFAIL>2500THEN1100
1050 GOTO1020
1100 IFFAIL<2500THENM(X,Y)=ITEM
1110 NEXTG
1120 RETURN
2000 CLS:FORX=1TO20:FORY=1TO16:IFSQR((X-PX)*(X-PX)+(Y-PY)*(Y-PY))<4ORN(X,Y)=1THENGOSUB2500
2010 NEXT:NEXT
2020 A$="DEPTH:"+STR$(DEPTH)+" RANK:"+STR$(RANK)+" XP:"+STR$(XP)+"/"+STR$(RANK*3)+" SWORD:"+STR$(WEP)
2030 A$=A$+" ARMOR:"+STR$(ARM)+" STR:"+STR$(HP)+" GOLD:"+STR$(GOLD):GOSUB5
2040 RETURN
2500 V=M(X,Y):N(X,Y)=1:IFV>0THEN2600
2510 RETURN
2600 IFV=5THENP1=(X*Y):P2=3:GOSUB1:V=P3+DEPTH:PRINT@(X-1)+(Y-1)*32,LEFT$(M$(V,1),1);:RETURN
2610 PRINT@(X-1)+(Y-1)*32,CHR$(L(V,1));:RETURN
3000 REM 1    2    3    4    5       6     7      8    9      10
3010 REM WALL YOU! EXIT GOLD MONSTER ARMOR WEAPON TRAP POTION GOAL
3020 DATA 35,102,102,102,64,255,255,255,187,0,119,0,36,255,255,0,167,255,0,0,135,0,0,255,134,255,102,0
3030 DATA 215,136,0,0,161,255,102,255,165,102,0,102
3040 DATA "RAT ","BITES","SNAKE ","BITES","GOBLIN ","POKES","ZOMBIE ","TOUCHES","ORC ","STABS"
3050 DATA "SKELETON ","GRABS ","TROLL ","KNOCKS","WILL-O-WISP ","SHOCKS","MANTICORE ","LUNGES AT"
3060 DATA "ELEMENTAL ","BURNS","BEHOLDER ","BITES","DRAGON ","BREATHES FIRE AT"
3070 DATA "RUSTY ","BRONZE ","STEEL ","SILVER ","MAGIC ","DAGGER","SHORT SWORD","BROADSWORD"
3080 DATA "LONGSWORD","CLAYMORE","CLOTH","LEATHER","CHAIN","SCALE","PLATE"," BOOTS"," GLOVES"
3090 DATA " GREAVES"," HELM"," ARMOR"
3999 REM GAME_OVER
4000 CLS
4010 IFHP<1THENPRINT@256,"YOU DIED!";:A$="":GOTO4050
4020 PRINT@256,"YOU WIN!";:A$="YOU FOUND THE GRAIL OF THE GODS"
4050 PRINT"YOU REACHED A DEPTH OF "+STR$(DEPTH)
4060 PRINT"YOU WERE RANK "+STR$(RANK)
4070 PRINT"AND YOU FOUND "+STR$(GOLD)+" GOLD"
4080 PRINTA$
4090 I$=INKEY$:IFI$=""THEN4090
5000 END
6000 FORY=1TO16:FORX=1TO20:PRINTRIGHT$(STR$(M(X,Y)),1);:NEXT:PRINT:NEXT
6010 IFINKEY$<>"C"THEN6010
