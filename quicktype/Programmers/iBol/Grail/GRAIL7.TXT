0 CLEAR500:DIML(10),M$(12,2),W$(20),M(22,18),A(20),B(20),C(16),D(16),P(8,1),W(10),X,Y,PX,PY,V,U,T,Q,P:GOTO7000
1 P3=P1-(P2*INT(P1/P2)):RETURN
3 PRINT@32*15+21,"HIT A KEY";:IFINKEY$=""THEN3
4 PRINT@32*15+21,"         ";:RETURN
5 C1=1:CC=11:FORVP=0TO8:PRINT@32*VP+21,"          ";:NEXT:VP=0
6 IFMID$(A$,CC,1)<>""ANDMID$(A$,CC,1)<>" "THENCC=CC-1:GOTO6
7 PRINT@32*VP+21,MID$(A$,C1,CC-C1);:C1=CC+1:CC=C1+10:VP=VP+1:IFC1<LEN(A$)THEN6
8 RETURN
9 W=W+1:RETURN
10 PRINT@(X-1)+(Y-1)*32,CHR$(L(V));:RETURN
11 P1=(X*Y):P2=3:GOSUB1:V=P3+DE:PRINT@(X-1)+(Y-1)*32,LEFT$(M$(V,1),1);:RETURN
12 FORX=A(PX)TOB(PX):FORY=C(PY)TOD(PY):V=M(X,Y):ONVGOSUB10,10,10,10,11,10,10,10,10,10:NEXT:NEXT:RETURN
20 RESTORE:FORT=1TO10:READA:L(T)=A:NEXT:FORT=1TO12:READA$,B$:M$(T,1)=A$:M$(T,2)=B$:NEXT
30 FORT=1TO20:READA$:W$(T)=A$:NEXT:FORT=1TO20:READA(T):NEXT:FORT=1TO20:READB(T):NEXT
35 FORT=1TO16:READC(T):NEXT:FORT=1TO16:READD(T):NEXT:FORT=0TO8:READP(T,1):NEXT:W(1)=1
40 DE=0:RA=1:XP=0:GO=0:WE=20:AR=20:HP=40
50 DE=DE+1:FA=0:PRINT"PREPARE FOR LEVEL";DE
60 FORX=1TO20:FORY=1TO16:IFX=1ORX=20ORY=1ORY=16THENM(X,Y)=1:GOTO80
70 M(X,Y)=0
80 NEXT:NEXT
90 FORFA=1TO448:PRINT@64,FA;:X=RND(18)+1:Y=RND(14)+1:P=0:Q=0:FORT=X-1TOX+1:FORU=Y-1TOY+1:Q=Q+P(P,M(T,U)):P=P+1:NEXT:NEXT
91 ON1-(Q=2ORQ=3ORQ=6ORQ=7ORQ=8ORQ=9ORQ=32ORQ=36ORQ=72ORQ=73ORQ=128ORQ=192ORQ=288ORQ=292ORQ=384ORQ=448)GOTO92:M(X,Y)=1:FA=0
92 NEXT
160 Y=RND(14)+1:IFM(2,Y)<>0THEN160
170 M(2,Y)=2:PX=2:PY=Y
180 Y=RND(14)+1:IFM(19,Y)<>0THEN180
190 IFDE<10THENM(19,Y)=3:GOTO210
200 M(19,Y)=10
210 WW=4:TI=5+(DE/2):IT=5:GOSUB1000
220 WW=2:P1=(DE+3):P2=3:GOSUB1:TI=P3:IT=8:GOSUB1000
230 WW=7:P1=DE:P2=3:GOSUB1:TI=P3+2:IT=4:GOSUB1000
240 TI=1:IT=6:GOSUB1000:IT=7:GOSUB1000:IT=9:GOSUB1000
245 CLS:GOSUB12:GOSUB2020
250 FORXX=PX-1TOPX+1:FORYY=PY-1TOPY+1:ON1-(M(XX,YY)=5)GOTO280:GOSUB300
280 NEXT:NEXT:GOTO400
300 P1=(XX*YY):P2=3:GOSUB1:MT=P3+DE
310 H=MT*8
320 D=RND(WE)+RA:WE=WE-1:H=H-D:A$="YOU HIT THE "+M$(MT,1)+"FOR"+STR$(D)+" DAMAGE":GOSUB5
322 PRINT@(XX-1)+(YY-1)*32,"X";:GOSUB3
328 D=RND(MT*3)+DE:DR=(AR*2)/D:IFDR>=DTHENDR=D-1:D=1
330 HP=HP-D:AR=AR-1:A$="THE "+M$(MT,1)+M$(MT,2)+" YOU FOR"+STR$(D)+" DAMAGE":GOSUB5
334 PRINT@(PX-1)+(PY-1)*32,"X";:GOSUB3
340 IFWE<3THENWE=3
342 IFAR<1THENAR=0
344 IFH<1ORHP<1THEN350
346 GOTO320
350 IFHP<1THEN4000
354 A$="YOU KILLED THE "+M$(MT,1):GOSUB5:GOSUB3
358 XP=XP+INT((MT/3)+1):M(XX,YY)=0:GOSUB12:GOSUB2020
399 RETURN
400 DX=0:DY=0:I$=INKEY$:IFI$=""THEN400
410 IFI$="A"THENDX=-1:GOTO450
420 IFI$="S"THENDX=1:GOTO450
430 IFI$="W"THENDY=-1:GOTO450
440 IFI$="Z"THENDY=1:GOTO450
445 GOTO400
450 V=M(PX+DX,PY+DY):A$=""
460 IFV=4THENGO=GO+(DE+2)*5*DE:V=0:XP=XP+1:A$="YOU FOUND "+STR$((DE+2)*5*DE)+" GOLD"
470 IFV=6THENAR=AR+10:V=0:A$="YOU FOUND "+W$(11+RND(4))+W$(16+RND(4))
480 IFV=7THENWE=WE+10:V=0:A$="YOU FOUND A "+W$(RND(4)+1)+W$(RND(4)+6)
490 IFV=8THENHP=HP-RND(DE*4):V=0:A$="YOU WERE HURT BY A TRAP":IFHP<1THENHP=1
500 IFV=9THENHP=15+RA*25:V=0:A$="YOU FOUND A HEALING POTION"
510 IFV=3THENCLS:GOTO50
520 IFV=10THEN4000
530 IFV=0THENPRINT@(PX-1)+(PY-1)*32," ";:M(PX,PY)=0:PX=PX+DX:PY=PY+DY:M(PX,PY)=2:GOSUB12:IFA$<>""THENGOSUB5:GOSUB3:GOSUB2020
550 IFXP>=(RA*3)THENGOSUB950
560 IFWE>RA*10+10THENWE=RA*10+10
570 IFAR>RA*10+10THENAR=RA*10+10
590 GOTO250
950 XP=XP-(RA*3):RA=RA+1:HP=15+RA*25:WE=WE+10:AR=AR+10:A$="YOU HAVE GAINED A HIGHER RANK!"
960 GOSUB5:GOSUB3:GOSUB2020
970 RETURN
999 REM 23 LINES (1 LINE) TO PLACE THINGS
1000 FORG=1TOTI:FORFA=1TO448
1020 X=RND(18)+1:Y=RND(14)+1:ONM(X,Y)GOTO1020,1020,1020,1020,1020,1020,1020,1020,1020,1020
1030 W=0:FORT=X-1TOX+1:FORU=Y-1TOY+1:W=W+W(M(T,U)):NEXT:NEXT:ON1-(W=WWORFA=447)GOTO1050:FA=448:M(X,Y)=IT
1050 NEXT:NEXT:RETURN
2020 A$="DEPTH:"+STR$(DE)+" RANK:"+STR$(RA)+" XP:"+RIGHT$(STR$(XP),2)+"/"+RIGHT$(STR$(RA*3),2)+" SWORD:"+STR$(WE)
2030 A$=A$+" ARMOR:"+STR$(AR)+" HEALTH:"+RIGHT$(STR$(HP),3)+" GOLD:"+STR$(GO):GOSUB5:RETURN
3010 REM WALL(35)YOU!(64)EXIT(126)GOLD(36)MONSTER(167)ARMOR(42)
3020 DATA 233,64,126,36,167,42
3025 REM WEAPON(95)TRAP(63)POTION(38)GOAL(120)
3030 DATA 95,63,38,120
3040 DATA "RAT ","BITES","SNAKE ","BITES","GOBLIN ","POKES","ZOMBIE ","TOUCHES","ORC ","STABS"
3050 DATA "COCKATRICE ","CLAWS AT ","TROLL ","KNOCKS","WILL-O- WISP ","SHOCKS","MANTICORE ","LUNGES AT"
3060 DATA "ELEMENTAL ","BURNS","BEHOLDER ","BITES","DRAGON ","BREATHES FIRE AT"
3070 DATA "RUSTY ","BRONZE ","STEEL ","SILVER ","MAGIC ","DAGGER","SHORT SWORD","BROADSWORD"
3080 DATA "LONGSWORD","CLAYMORE","CLOTH","LEATHER","CHAIN","SCALE","PLATE"," BOOTS"," GLOVES"
3090 DATA " GREAVES"," HELM"," ARMOR"
3999 REM GAME OVER
4000 CLS
4010 IFHP<1THENPRINT@32*4+12,"YOU DIED!";:A$="":GOTO4050
4020 PRINT@32*4+13,"YOU WIN!":A$="YOU FOUND THE GRAIL OF THE GODS"
4050 PRINT@256,"YOU REACHED A DEPTH OF "+STR$(DE)
4060 PRINT"YOU WERE RANK "+STR$(RA)
4070 PRINT"AND YOU FOUND "+STR$(GO)+" GOLD"
4080 PRINTA$
4090 I$=INKEY$:IFI$=""THEN4090
5000 END
7000 CLS:PRINT"GRAIL OF THE GODS by ibol":DIMP1,P2,P3,C1,CC,VP,A$,IT:GOTO20
4100 DATA 1,1,1,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17
4110 DATA 4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,20,20,20
4120 DATA 1,1,1,1,2,3,4,5,6,7,8,9,10,11,12,13
4130 DATA 4,5,6,7,8,9,10,11,12,13,14,15,16,16,16,16
4140 DATA 1,2,4,8,16,32,64,128,256
