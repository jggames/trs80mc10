0 CLEAR500:DIMT,U,P,Q,F,P(8,1),L(10),M(22,18),N(20,16),Q(512),A(20),B(20),C(16),D(16),M$(14,2),W$(20),W(10),X,Y,PX,PY:GOTO7000
1 P3=P1-(P2*INT(P1/P2)):RETURN
2 PRINT@N(X,Y),CHR$(L(V));:RETURN
3 PRINT@32*15+21,"HIT A KEY";:IFINKEY$=""THEN3
4 PRINT@32*15+21,"         ";:RETURN
5 C1=1:CC=11:FORVP=0TO8:PRINT@32*VP+21,"          ";:NEXT:VP=0
6 IFMID$(A$,CC,1)<>""ANDMID$(A$,CC,1)<>" "THENCC=CC-1:GOTO6
7 PRINT@32*VP+21,MID$(A$,C1,CC-C1);:C1=CC+1:CC=C1+10:VP=VP+1:IFC1<LEN(A$)THEN6
8 RETURN
9 W=W+1:RETURN
10 P1=(X*Y):P2=3:GOSUB1:V=P3+DE:PRINT@N(X,Y),LEFT$(M$(V,1),1);:RETURN
11 FORX=A(PX)TOB(PX):FORY=C(PY)TOD(PY):V=M(X,Y):ONVGOSUB2,2,2,2,10,2,2,2,2,2:NEXT:NEXT:RETURN
13 M(X,Y)=1:NEXT:NEXT:RETURN
14 FORX=2TO19:FORY=2TO15:P=0:Q=0:FORT=G(X)-1TOG(X)+1:FORU=H(Y)-1TOH(Y)+1:Q=Q+P(P,M(T,U)):P=P+1:NEXT:NEXT
15 ONQ(Q)GOTO13:NEXT:NEXT:RETURN
20 FORT=1TO10:READA:L(T)=A:NEXT:FORT=1TO14:READA$,B$:M$(T,1)=A$:M$(T,2)=B$:NEXT
30 FORT=1TO20:READA$:W$(T)=A$:NEXT:FORT=1TO20:READA(T):NEXT:FORT=1TO20:READB(T):NEXT
35 FORT=1TO16:READC(T):NEXT:FORT=1TO16:READD(T):NEXT:FORT=0TO8:READP(T,1):NEXT:W(1)=1:FORT=1TO16:READQ:Q(Q)=1:NEXT
36 FORX=1TO20:FORY=1TO16:N(X,Y)=32*(Y-1)+(X-1):NEXT:NEXT:X=RND(-(PEEK(9)*256+PEEK(10)))
40 DE=0:RA=1:XP=0:GO=0:WE=10:AR=10:HP=40:GOSUB5000:CLS
50 DE=DE+1:F=0:PRINT"PREPARE FOR LEVEL";DE:FORX=1TO20:FORY=1TO16:IFX=1ORX=20ORY=1ORY=16THENM(X,Y)=1:NEXT:NEXT:GOTO60
51 M(X,Y)=0
52 NEXT:NEXT
60 FORI=2TO19
65 A=1+RND(18):ON1-(G(A)=0)GOTO65:G(A)=I:NEXT
70 FORI=2TO15
75 A=1+RND(14):ON1-(H(A)=0)GOTO75:H(A)=I:NEXT
100 GOSUB14
160 Y=RND(14)+1:IFM(2,Y)<>0THEN160
170 M(2,Y)=2:PX=2:PY=Y:GOTO245
180 Y=RND(14)+1:IFM(19,Y)<>0THEN180
190 IFDE<(10+DF)THENM(19,Y)=3:GOTO210
200 M(19,Y)=10
210 WW=4:TI=5+DE:IT=5:GOSUB1000
220 WW=2:P1=(DE+3):P2=3:GOSUB1:TI=P3:IT=8:GOSUB1000
230 WW=7:P1=DE:P2=3:GOSUB1:TI=P3+RND(2):IT=4:GOSUB1000
240 TI=1:IT=6:GOSUB1000:IT=7:GOSUB1000:TI=3-RND(2):IT=9:GOSUB1000
245 CLS:GOSUB11:GOSUB2020
250 FORXX=PX-1TOPX+1:FORYY=PY-1TOPY+1:ON1-(M(XX,YY)=5)GOTO280:GOSUB300
280 NEXT:NEXT:GOTO400
300 P1=(XX*YY):P2=3:GOSUB1:MT=P3+DE
310 H=MT*8
320 D=INT(RND(WE)+RA):IFD>HTHEN D=H
321 WE=INT(WE-(D/7)):IFWE<5+RATHENWE=5+RA
322 H=H-D:A$="YOU HIT THE "+M$(MT,1)+"FOR"+STR$(D)+" DAMAGE":GOSUB5
323 PRINT@N(XX,YY),"X";:GOSUB3
328 IFH<=0THEN334
329 D=RND((MT*3)+DE)
330 IFAR>0THENDR=INT(D/2):D=D-DR:AR=AR-DR
331 HP=HP-D:A$="THE "+M$(MT,1)+M$(MT,2)+" YOU FOR"+STR$(D)+" DAMAGE":GOSUB5
334 PRINT@N(PX,PY),"X";:GOSUB3
342 IFAR<1THENAR=0
344 IFH<1ORHP<1THEN350
346 GOTO320
350 IFHP<1THEN4000
354 A$="YOU KILLED THE "+M$(MT,1):GOSUB5:GOSUB3:PRINT@N(XX,YY),CHR$(175);:SOUND1,1:PRINT@N(XX,YY)," ";
358 KI=KI+1:XP=XP+INT((MT/3)+1):M(XX,YY)=0:GOSUB11:GOSUB2020
399 RETURN
400 DX=0:DY=0:A$=INKEY$:IFA$=""THEN400
410 IFA$="A"THENDX=-1:GOTO450
420 IFA$="S"THENDX=1:GOTO450
430 IFA$="W"THENDY=-1:GOTO450
440 IFA$="Z"THENDY=1:GOTO450
445 GOTO400
450 V=M(PX+DX,PY+DY):A$=""
460 IFV=4THENGO=GO+(DE*5*DE):V=0:XP=XP+1:A$="YOU FOUND "+STR$((DE+2)*5*DE)+" GOLD"
470 IFV=6THENAR=AR+10+DE:V=0:A$="YOU FOUND "+W$(11+RND(4))+W$(16+RND(4))
480 IFV=7THENWE=WE+10+(DE*3):V=0:A$="YOU FOUND A "+W$(RND(4)+1)+W$(RND(4)+6)
490 IFV=8THENHP=HP-(RND(DE*4)+1):V=0:A$="YOU WERE HURT BY A TRAP":IFHP<1THENHP=1
500 IFV=9THENHP=15+RA*25:V=0:A$="YOU FOUND A HEALING POTION"
510 IFV=3THENCLS:GOTO50
520 IFV=10THEN4000
530 IFV=0THENM(PX,PY)=0:PRINT@N(PX,PY)," ";:PX=PX+DX:PY=PY+DY:M(PX,PY)=2:GOSUB11:IFA$<>""THENGOSUB5:GOSUB3:GOSUB2020
550 IFXP>=(RA*3)THENGOSUB950
590 GOTO250
950 XP=XP-(RA*3):RA=RA+1:WE=WE+RA:AR=AR+RA:A$="YOU HAVE GAINED A HIGHER RANK!"
960 GOSUB5:GOSUB3:GOSUB2020
970 RETURN
999 REM 23 LINES (1 LINE) TO PLACE THINGS
1000 FORG=1TOTI:FORF=1TO448
1020 X=RND(18)+1:Y=RND(14)+1:ONM(X,Y)GOTO1020,1020,1020,1020,1020,1020,1020,1020,1020,1020
1030 W=0:FORT=X-1TOX+1:FORU=Y-1TOY+1:W=W+W(M(T,U)):NEXT:NEXT:ON1-(W=WWORF=447)GOTO1050:F=448:M(X,Y)=IT
1050 NEXT:NEXT:RETURN
2020 A$="DEPTH:"+STR$(DE)+" RANK:"+STR$(RA)+" XP:"+RIGHT$(STR$(XP),2)+"/"+RIGHT$(STR$(RA*3),2)+" SWORD:"+STR$(WE)
2030 A$=A$+" ARMOR:"+STR$(AR)+" HEALTH:"+RIGHT$(STR$(HP),3)+" GOLD:"+STR$(GO):GOSUB5:RETURN
3010 REM 1WALL(35)2YOU!(64)3EXIT(126)4GOLD(36)5MONSTER(167)6ARMOR(42)7WEAPON(95)8TRAP(63)9POTION(38)10GRAIL(120)
3020 DATA 233,64,126,36,167,42,95,63,38,120
3040 DATA "RAT ","BITES","SNAKE ","BITES","GOBLIN ","POKES","ZOMBIE ","TOUCHES","ORC ","STABS"
3050 DATA "COCKATRICE ","CLAWS AT ","TROLL ","KNOCKS","WILL-O- WISP ","SHOCKS","MANTICORE ","LUNGES AT"
3060 DATA "ELEMENTAL ","BURNS","BEHOLDER ","BITES","DRAGON ","BREATHES FIRE AT","UMBER HULK","CRUSHES","ARCHDEMON","CURSES"
3070 DATA "RUSTY ","BRONZE ","STEEL ","SILVER ","MAGIC ","DAGGER","SHORT SWORD","BROADSWORD"
3080 DATA "LONGSWORD","CLAYMORE"
3090 DATA "CLOTH","LEATHER","CHAIN","SCALE","PLATE"," BOOTS"," GLOVES"," GREAVES"," HELM"," ARMOR"
3999 REM GAME OVER
4000 CLS
4010 IFHP<1THENPRINT@32*4+12,"YOU DIED!";:A$="":GOTO4030
4020 PRINT@32*4+12,"YOU WIN!":A$="YOU FOUND THE GRAIL OF THE GODS"
4030 PRINTA$:PRINT
4050 PRINT"YOU REACHED A DEPTH OF"+STR$(DE)
4060 PRINT"YOU WERE RANK"+STR$(RA)
4065 PRINT"YOU DEFEATED";KI;"MONSTERS"
4070 PRINT"AND YOU FOUND"+STR$(GO)+" GOLD"
4090 A$=INKEY$:IFA$=""THEN4090
4099 END
4100 DATA 1,1,1,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17
4110 DATA 4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,20,20,20
4120 DATA 1,1,1,1,2,3,4,5,6,7,8,9,10,11,12,13
4130 DATA 4,5,6,7,8,9,10,11,12,13,14,15,16,16,16,16
4140 DATA 1,2,4,8,16,32,64,128,256
4150 DATA 2,3,6,7,8,9,32,36,72,73,128,192,288,292,384,448
5000 CLS
5010 PRINT"SEEK THE GRAIL OF THE GODS"
5020 PRINT"THRU 10 RANDOMLY GENERATED"
5030 PRINT"LEVELS. BATTLE 14 MONSTERS,"
5040 PRINT"FROM THE LOWLY RAT TO THE"
5050 PRINT"MIGHTY DRAKE. DISCOVER 25"
5060 PRINT"DIFFERENT WEAPONS AND ARMORS."
5070 PRINT"JUST USE THE AWSZ KEYS TO MOVE."
5080 PRINT"GET TOO CLOSE TO A MONSTER, AND"
5090 PRINT"IT'S A FIGHT TO THE DEATH! YOUR"
5110 PRINT"SWORD AND ARMOR DEGRADE WITH"
5120 PRINT"USE, SO BE SURE TO FIND"
5130 PRINT"REPLACEMENTS. SHINY GOLD ADDS"
5140 PRINT"TO YOUR WEALTH AND EXPERIENCE."
5150 PRINT"AVOID DEADLY TRAPS, AND DRINK"
5160 PRINT"THE HEALING POTIONS ON EACH"
5170 PRINT"LEVEL.";:GOSUB3:RETURN
7000 CLS:PRINT@32*7+8,"GRAIL OF THE GODS":PRINT@32*9+13,"by ibol":DIMG(20),H(16),V,P1,P2,P3,C1,CC,VP,A$,IT,A
7010 DF=0:GOTO20
7020 REM CHANGE DF=1 OR 2
7030 REM FOR EXTRA CHALLENGE
