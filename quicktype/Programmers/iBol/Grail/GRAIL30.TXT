0 CLEAR500:GOTO9300
1 P3=P1-(P2*INT(P1/P2)):RETURN
3 PRINT@501,"HIT A KEY";:A$=INKEY$:ON-(A$="")GOTO3:PRINT@501,"         ";:RETURN
4 A$=A$+" ---------- HEALTH:"+RIGHT$(STR$(HP*-(HP>0)),3)+" ----------":RETURN
5 G=1:F=11:FORI=0TO10:PRINT@32*I+21,"":NEXT:I=0
6 IFMID$(A$,F,1)<>""ANDMID$(A$,F,1)<>" "THENF=F-1:GOTO6
7 PRINT@32*I+21,MID$(A$,G,F-G);:G=F+1:F=G+10:I=I+1:IFG<=LEN(A$)THEN6
8 RETURN
11 M=1:FORX=A(L)TOB(L):FORY=C(R)TOD(R):IFM(X,Y)=5THENM=0:POKEN(X,Y),ASC(M$((X*Y)-(3*INT((X*Y)/3))+P,1)):NEXT:NEXT:RETURN
12 POKEN(X,Y),L(M(X,Y)):NEXT:NEXT:RETURN
13 FORG=1TOM:FORF=1TO400
14 X=RND(18)+1:Y=RND(14)+1:IFM(X,Y)THEN14
15 W=0:FORT=X-1TOX+1:FORU=Y-1TOY+1:W=W+W(M(T,U)):NEXT:NEXT:IFW=NORF=399THENF=400:M(X,Y)=C
16 NEXT:NEXT:RETURN
20 FORT=0TO10:READL(T):NEXT:FORT=1TO14:READM$(T,1),M$(T,2):NEXT
30 FORT=1TO20:READW$(T):NEXT:FORT=1TO20:READA(T):NEXT:FORT=1TO20:READB(T):NEXT
35 FORT=1TO16:READC(T):NEXT:FORT=1TO16:READD(T):NEXT:W(1)=1
36 FORX=1TO20:FORY=1TO16:N(X,Y)=32*(Y-1)+X+16383:NEXT:NEXT:X=RND(-(PEEK(9)*256+PEEK(10)))
40 P=0:RA=1:XP=0:GO=0:WE=10:AR=10:HP=40:GOSUB5000:CLS
50 CLS:P=P+1:PRINT"PREPARE FOR LEVEL";P:FORY=2TO15:FORX=2TO19:READM(X,Y):NEXT:NEXT:FORX=1TO20:M(X,1)=1:M(X,16)=1:NEXT
60 FORY=1TO16:M(1,Y)=1:M(20,Y)=1:NEXT
160 Y=RND(14)+1:IFM(2,Y)<>0THEN160
170 M(2,Y)=2:L=2:R=Y
180 Y=RND(14)+1:IFM(19,Y)<>0THEN180
190 IFP<(10+DF)THENM(19,Y)=3:GOTO210
200 M(19,Y)=10
210 N=4:M=5+P:C=5:GOSUB13:N=2:P1=(P+3):P2=3:GOSUB1:M=P3:C=8:GOSUB13:N=7:P1=P:P2=3:GOSUB1:M=P3+RND(2):C=4:GOSUB13
240 M=1:C=6:GOSUB13:C=7:GOSUB13:M=3-RND(2):C=9:GOSUB13
245 CLS:GOSUB11:GOSUB2020:GOTO360
300 P1=(Z*K):P2=3:GOSUB1:MT=P3+P:H=MT*8
320 D=INT(RND(WE)+RA):IFD>HTHEND=H
321 WE=INT(WE-(D/7)):IFWE<5+RATHENWE=5+RA
322 H=H-D:A$="YOU HIT THE "+M$(MT,1)+"FOR"+STR$(D)+" DAMAGE":GOSUB5
323 POKEN(Z,K),88:GOSUB3:IFH<1THEN350
330 D=RND((MT*3)+P):IFAR>0THENDR=INT(D/2):D=D-DR:AR=AR-DR
331 HP=HP-D
332 A$="THE "+M$(MT,1)+M$(MT,2)+" YOU FOR"+STR$(D)+" DAMAGE":GOSUB4:A$=A$+" (R)UN":GOSUB5
333 IFAR<1THENAR=0
334 POKEN(L,R),88:GOSUB3:ON-(HP<1)GOTO4000:IFA$="R"THENM=0:PRINT@501,"COWARD!  ";:RETURN
346 GOTO320
350 A$="YOU KILLED THE "+M$(MT,1):GOSUB5:GOSUB3:POKEN(Z,K),191:SOUND1,1:POKEN(Z,K),96
358 KI=KI+1:XP=XP+INT((MT/3)+1):M(Z,K)=0:GOSUB11:GOSUB2020:RETURN
360 IFMTHEN400
370 FORZ=L-1TOL+1:FORK=R-1TOR+1:IFM(Z,K)=5THENGOSUB300:IFA$="R"THENK=99:NEXT:Z=99:NEXT:GOSUB2020:ONRND(4)GOTO446,447,448,449
380 NEXT:NEXT
400 A$=INKEY$:IFA$=""THEN400
410 IFA$="A"THEN446
415 IFA$="E"THENDX=1:DY=-1:GOTO450
420 IFA$="D"THEN447
425 IFA$="Q"THENDX=-1:DY=-1:GOTO450
430 IFA$="W"THEN448
435 IFA$="C"THENDX=1:DY=1:GOTO450
440 IFA$="X"THEN449
441 IFA$="Z"THENDX=-1:DY=1:GOTO450
445 GOTO400
446 DX=-1:DY=0:GOTO450
447 DX=1:DY=0:GOTO450
448 DX=0:DY=-1:GOTO450
449 DX=0:DY=1
450 A$="":V=M(L+DX,R+DY):ONVGOTO550,550,50,460,550,470,480,490,500,4000:GOTO530
460 GO=GO+((P+2)*5*P):V=0:XP=XP+1:A$="YOU FOUND "+STR$((P+2)*5*P)+" GOLD":GOTO530
470 AR=AR+10+P:V=0:A$="YOU FOUND "+W$(11+RND(4))+W$(16+RND(4)):GOTO530
480 WE=WE+10+(P*3):V=0:A$="YOU FOUND A "+W$(RND(4)+1)+W$(RND(4)+6):GOTO530
490 HP=HP-(RND(P*4)+1):V=0:A$="YOU WERE HURT BY A TRAP":GOSUB4:SOUND1,1:IFHP<1THENHP=1
495 GOTO530
500 HP=15+RA*25:V=0:A$="YOU FOUND A HEALING POTION"
530 IFV=0THENM(L,R)=0:POKEN(L,R),96:L=L+DX:R=R+DY:M(L,R)=2:GOSUB11:IFA$<>""THENSOUND115,1:GOSUB5:GOSUB3:GOSUB2020
550 IFXP<RA*3THEN360
940 XP=XP-(RA*3):RA=RA+1:HP=HP+RA:IFHP>=15+RA*25THENHP=15+RA*25
950 WE=WE+(RA*2):AR=AR+(RA*2):A$="YOU HAVE GAINED A HIGHER RANK!":GOSUB5:GOSUB3:GOSUB2020:GOTO360
2020 A$="DEPTH:"+STR$(P)+" RANK:"+STR$(RA)+" XP:"+RIGHT$(STR$(XP),2)+"/"+RIGHT$(STR$(RA*3),2)+" SWORD:"+STR$(WE)
2030 A$=A$+" ARMOR:"+STR$(AR)+" HEALTH:"+RIGHT$(STR$(HP),3)+" GOLD:"+STR$(GO):GOSUB5:RETURN
3020 DATA96,233,64,30,100,167,106,95,127,102,24
3040 DATA"RAT ","BITES","SNAKE ","BITES","GOBLIN ","POKES","ZOMBIE ","TOUCHES","ORC ","STABS"
3050 DATA"COCKATRICE ","CLAWS AT ","TROLL ","KNOCKS","WILL-O- WISP ","SHOCKS","MANTICORE ","LUNGES AT"
3060 DATA"ELEMENTAL ","BURNS","BEHOLDER ","BITES","DRAGON ","BREATHES FIRE AT","UMBER HULK","CRUSHES","ARCHDEMON","CURSES"
3070 DATA"RUSTY ","BRONZE ","STEEL ","SILVER ","MAGIC ","DAGGER","SHORT SWORD","BROADSWORD"
3080 DATA"LONGSWORD","CLAYMORE"
3090 DATA"CLOTH","LEATHER","CHAIN","SCALE","PLATE"," BOOTS"," GLOVES"," GREAVES"," HELM"," ARMOR"
4000 CLS:IFHP<1THENPRINT@32*4+12,"YOU DIED!";:A$="":GOTO4030
4020 PRINT@32*4+12,"YOU WIN!":A$="YOU FOUND THE GRAIL OF THE GODS"
4030 PRINTA$:PRINT
4050 PRINT"YOU REACHED A DEPTH OF"+STR$(P)
4060 PRINT"YOU WERE RANK"+STR$(RA)
4065 PRINT"YOU DEFEATED";KI;"MONSTERS"
4070 PRINT"AND YOU FOUND"+STR$(GO)+" GOLD"
4090 PRINT:END
4100 DATA1,1,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18
4110 DATA3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,20,20
4120 DATA1,1,1,2,3,4,5,6,7,8,9,10,11,12,13,14
4130 DATA3,4,5,6,7,8,9,10,11,12,13,14,15,16,16,16
5000 PRINT@448,;:INPUT"LEVEL (1 EASY - 3 HARD)";DF:IFDF<1ORDF>3THEN5000
5010 DF=DF-1:CLS:PRINT"SEEK THE GRAIL OF THE GODS"
5020 PRINT"THROUGH 10 RANDOMLY POPULATED"
5030 PRINT"LEVELS. BATTLE 14 MONSTERS,"
5040 PRINT"FROM THE LOWLY RAT TO THE"
5050 PRINT"MIGHTY DRAKE. DISCOVER 25"
5060 PRINT"DIFFERENT WEAPONS AND ARMORS."
5070 PRINT"USE THE aqwedcxz KEYS TO MOVE."
5080 PRINT"GET TOO CLOSE TO A MONSTER, AND"
5090 PRINT"IT'S A FIGHT TO THE DEATH! YOUR"
5110 PRINT"SWORD AND ARMOR DEGRADE WITH"
5120 PRINT"USE, SO BE SURE TO FIND"
5130 PRINT"REPLACEMENTS. SHINY GOLD ADDS"
5140 PRINT"TO YOUR WEALTH AND EXPERIENCE."
5150 PRINT"AVOID DEADLY TRAPS, AND DRINK"
5160 PRINT"THE HEALING POTIONS ON EACH"
5170 PRINT"LEVEL.";:GOSUB3:RETURN
8102 DATA,1,,,,1,,,1,,,,1,,1,,1,
8103 DATA,1,1,1,,1,1,,1,1,1,,1,,1,,1,
8104 DATA,,,,,,1,,1,,,,,,,,,
8105 DATA1,1,1,1,,,1,,1,1,,1,,1,,1,,
8106 DATA,,,,,1,1,,1,,,1,1,1,1,1,1,1
8107 DATA1,1,1,1,,,1,,1,,,,1,,1,,,
8108 DATA,,,,,1,1,,1,,1,1,1,,1,,1,1
8109 DATA1,,1,1,,,,,,,,,1,,,,,
8110 DATA,,1,,,,1,1,1,1,,,,,1,1,,1
8111 DATA1,1,1,1,1,,1,,,1,1,1,,,,1,,
8112 DATA,,,1,,,,,,1,,,,1,1,1,1,1
8113 DATA,1,,,,1,,1,,1,,1,,,,,,
8114 DATA,1,1,,1,1,,1,1,1,,1,,1,1,,1,
8115 DATA,1,,,,1,,,1,,,1,,,1,,1,
8202 DATA,1,,,1,,1,,,1,,1,,,,1,,
8203 DATA,,,1,1,,1,,1,1,,1,1,1,,,,
8204 DATA1,1,,,1,,,,1,,,,1,,,,1,1
8205 DATA,,,,,,1,1,1,,1,1,1,,1,,,
8206 DATA,,1,,1,,,1,,,,1,,,1,,1,
8207 DATA1,1,1,1,1,1,,1,1,1,,,,1,1,1,1,1
8208 DATA,,,,,,,1,,1,1,,,,1,,,
8209 DATA1,1,,,1,,,,,1,,,1,1,1,,1,
8210 DATA,,,1,1,1,,1,,,,1,1,,1,,1,1
8211 DATA,1,1,1,,,,1,,1,,,1,,,,1,
8212 DATA1,1,,1,,1,,1,,1,1,,1,,1,,,
8213 DATA,,,,,1,,1,,,1,,,,1,,,1
8214 DATA1,1,,1,1,1,,1,,1,1,1,,1,1,1,,
8215 DATA,,,,1,,,1,,1,,,,,1,,,
8302 DATA,,,1,,,1,,,1,,1,,1,,1,,
8303 DATA1,,1,1,1,,,,1,1,,1,,1,,1,1,
8304 DATA,,,,1,1,1,,,1,,,,1,,,,
8305 DATA1,1,,1,1,,1,,,1,1,1,,1,,1,1,1
8306 DATA,,,,1,,1,,1,1,,,,,,,,
8307 DATA1,1,1,,,,1,,,1,,1,,1,1,1,1,1
8308 DATA,,,,1,1,1,,,1,,1,,,,,1,
8309 DATA1,1,1,,,,1,,1,1,,1,1,,1,,1,
8310 DATA,,,,1,,,,,,,,1,1,1,1,1,
8311 DATA1,1,,1,1,,,1,,,1,,,,1,,,
8312 DATA,,,1,,,1,1,,1,1,1,1,,,,1,1
8313 DATA,,1,1,1,,1,,,,,1,,,1,,,
8314 DATA1,,,,1,,1,1,,1,1,1,1,,1,,1,1
8315 DATA,,1,,1,,1,,,,,1,,,1,,,
8402 DATA,,1,,1,,,1,,,,1,,1,,1,,
8403 DATA,,,,1,,1,1,1,,1,1,,,,1,,1
8404 DATA,1,1,,1,,1,,1,,,1,,1,1,1,,
8405 DATA1,1,,,,,1,,1,,1,1,,,1,,,
8406 DATA,,,1,,,,,,,,1,,1,1,,1,
8407 DATA1,1,,1,,1,,1,,1,1,1,,,1,,1,
8408 DATA,,,1,,1,1,1,,,,,,1,1,,1,1
8409 DATA1,1,1,1,1,1,,,,1,1,,1,1,,,1,
8410 DATA,,,,,,,1,,1,,,,,,1,1,
8411 DATA1,1,1,,1,,,1,1,1,1,1,,1,1,1,,
8412 DATA,,,,1,1,,,,1,,,,,,,,
8413 DATA1,1,1,1,1,,,1,1,1,1,1,,,1,1,,1
8414 DATA,,,,,,,,,1,,,,1,1,,,
8415 DATA,,1,,,1,,1,,1,,1,,,1,,1,
8502 DATA,,1,,1,,1,,,1,,1,,1,,1,,
8503 DATA,,1,,1,,,,1,1,,1,,1,,1,1,
8504 DATA1,,1,,1,1,,1,1,,,,,,,1,,
8505 DATA,,1,,1,,,,1,1,,1,1,1,1,1,,
8506 DATA,,,,,,,,,1,,,,1,,1,,1
8507 DATA1,1,,1,1,,,,,,,1,,,,1,,
8508 DATA,,,1,,,1,,1,1,,1,,1,,,,
8509 DATA1,1,1,1,1,,1,1,1,,,1,,1,1,,1,1
8510 DATA,,,,,,,,1,,,1,,,1,,,
8511 DATA1,1,1,1,1,,1,1,1,1,1,1,,1,1,1,,1
8512 DATA,1,,1,,,,,1,,,,,1,,,,
8513 DATA,,,,,,1,1,1,,1,,,1,,1,,
8514 DATA,1,1,,1,,,,1,1,1,1,,1,,1,,1
8515 DATA,1,,,1,,1,,1,,,,,1,,1,,
8602 DATA,,1,,1,,1,,1,,1,,1,,,,,
8603 DATA,,,,1,,1,,1,,1,,1,1,,1,1,1
8604 DATA,1,,,1,,1,,1,,1,,,1,,1,,
8605 DATA1,1,1,,1,,1,,1,,1,1,,1,,1,,1
8606 DATA,,,,,,,,1,,1,,,1,,,,
8607 DATA1,1,1,1,,1,,,,,,,1,1,,1,1,1
8608 DATA,,,,,1,,1,,1,,1,1,,,,,
8609 DATA,1,1,1,,1,,1,,1,1,1,,,1,1,,
8610 DATA1,1,,1,,1,,1,,,,,,,,1,,1
8611 DATA,1,,,,1,1,1,,,1,,,1,1,1,,
8612 DATA,,,1,,1,,,,1,1,1,,,,1,1,1
8613 DATA,1,,1,,1,,1,,,,1,,1,,,,
8614 DATA,1,1,1,,1,,1,,1,,1,,1,1,,1,1
8615 DATA,,1,,,1,,1,,1,,1,,1,,,,
8702 DATA,,,,1,,1,,,,1,,1,,1,,1,
8703 DATA1,1,,,1,,1,,,1,1,,1,,1,,,
8704 DATA,1,1,,,,1,,1,1,,,,,,,1,1
8705 DATA,,,,1,1,1,,,1,1,,1,1,1,1,1,
8706 DATA1,,1,1,1,,1,1,,1,,,,1,,,,
8707 DATA,,,1,,,,1,,1,,,1,1,,1,1,1
8708 DATA,1,,,,1,,,,1,1,,,,,,,
8709 DATA,1,,1,,1,,1,,,,,1,,,1,,1
8710 DATA1,1,1,1,,1,,1,,1,1,,1,,1,1,,
8711 DATA,,,,,1,1,1,,,1,,1,,,1,1,1
8712 DATA1,1,,1,,,,1,1,,1,,1,1,,,,
8713 DATA,,,1,1,1,,1,,,1,,,1,,,1,
8714 DATA1,,,,,1,,1,1,,1,,1,1,,1,1,1
8715 DATA,,1,,,1,,1,,,1,,,1,,,,
8802 DATA,,1,,1,,,1,,,1,,1,,,1,,
8803 DATA,1,1,,1,,,1,1,,,,1,1,,1,,1
8804 DATA,,,,1,1,,,1,1,,,1,,,,,
8805 DATA1,1,1,,1,,,,,1,,1,1,1,,1,1,1
8806 DATA,,,,,,,1,,,,1,,1,,,,
8807 DATA1,1,1,1,1,1,,1,1,1,1,1,,,,1,1,1
8808 DATA,,,,,1,,,,,,1,,1,,1,,
8809 DATA1,,1,1,,,,,,1,,1,,1,,,,
8810 DATA,,1,,,,1,,1,1,,1,,1,,1,1,1
8811 DATA1,1,1,1,,1,1,1,1,,,,,1,,,,
8812 DATA,,,,,,,1,,,,1,1,1,1,,1,1
8813 DATA,1,1,1,1,,1,1,1,1,,,,1,,,1,
8814 DATA,1,,,,,,1,,,,1,,1,1,,,
8815 DATA,1,,1,,1,,1,,1,,1,,1,,,1,
8902 DATA,,,1,,,1,,1,,1,,,1,,1,,
8903 DATA1,,1,1,1,,1,,1,,1,1,,1,,,,1
8904 DATA,,,1,,,1,,1,,1,,,,,1,,
8905 DATA,1,,,,,,,1,,1,,,1,,1,1,1
8906 DATA1,1,1,1,1,1,,,,,1,1,,1,1,1,,
8907 DATA,,,,,1,1,1,1,,,1,,1,,1,,1
8908 DATA1,1,1,1,,,,,,,1,1,,,,,,
8909 DATA,,,,,1,1,,1,,,1,,1,1,,1,1
8910 DATA1,1,1,1,1,1,,,1,,,1,,,1,,,
8911 DATA,,1,,,,,1,1,1,,,,1,1,1,1,1
8912 DATA1,,,,,1,1,1,,,,1,,,,,,
8913 DATA,,,1,,,,1,1,,,1,,,1,,1,1
8914 DATA,1,,1,,1,,1,,,1,1,,1,1,,,
8915 DATA,1,,1,,1,,1,,,1,,,1,,,1,
9002 DATA,1,,1,,,1,,,1,,1,,1,,,1,
9003 DATA,,,1,,1,1,1,,,,1,,,,,,
9004 DATA1,,,,,1,,1,,1,1,1,1,,,1,1,1
9005 DATA,,1,,,,,1,,,,,1,1,,,1,
9006 DATA,1,1,1,,,1,1,1,1,1,,,1,,,,
9007 DATA1,1,,1,,1,1,,,1,,,1,1,,1,1,1
9008 DATA,,,,,,1,1,,1,,,,,,1,,
9009 DATA1,1,1,,1,,,1,,1,1,,1,1,1,1,,1
9010 DATA,,,,1,,,1,,,1,,1,,,,,
9011 DATA1,1,1,1,1,1,,,,,,,,,1,1,1,1
9012 DATA,,1,,1,,,,1,,1,1,1,,,,,
9013 DATA1,,,,,,1,,1,,,1,,,1,,1,
9014 DATA,,,1,1,,1,,1,1,,1,1,,1,,1,1
9015 DATA,1,,,1,,1,,,1,,1,,,1,,,
9102 DATA,1,,1,,1,,1,,1,,1,,1,,,1,
9103 DATA,1,,,,1,,1,,1,,1,,,,,,
9104 DATA,1,1,1,,,,1,,1,,1,1,,1,,1,
9105 DATA,1,,1,1,,1,1,,1,,,1,,1,1,1,1
9106 DATA,,,,,,,,,1,,1,1,,,,,
9107 DATA1,1,1,1,,1,1,1,1,1,,,,,1,,1,
9108 DATA,,,,,,,1,,1,,1,1,1,1,1,1,1
9109 DATA1,1,,1,1,1,,,,,,,,1,,,,
9110 DATA,,,1,,,,1,1,,1,,,,,1,1,
9111 DATA1,1,1,1,1,,1,1,,,1,,1,,,,1,1
9112 DATA,,,,1,,1,,,,1,1,1,,1,,,
9113 DATA,,1,,,,1,,1,,,,1,,1,1,1,
9114 DATA1,,1,1,1,,1,,1,,1,,1,1,1,,,
9115 DATA,,,1,,,1,,1,,1,,,1,,,1,
9202 DATA,,1,,1,,1,,1,,,1,,1,,,1,
9203 DATA,1,1,,1,,1,,1,1,,1,,1,,,,
9204 DATA,,,,1,,1,,1,,,,,1,,1,1,1
9205 DATA1,1,1,,1,,1,,1,1,1,,1,1,,,,
9206 DATA,,,,1,,1,,1,,,,,,,1,1,1
9207 DATA1,1,1,,,,,,,,1,1,1,,1,1,,
9208 DATA,,,,,1,,,1,,,,1,1,1,,,
9209 DATA,1,1,1,1,1,,1,1,1,1,,,1,,,1,1
9210 DATA,,,,,,,,,1,,,,1,,1,1,
9211 DATA1,1,1,1,1,,1,1,,1,1,1,,,,,,
9212 DATA,,,,1,1,1,,,1,,,,1,,1,,
9213 DATA1,,1,,,,,,,1,,1,,1,1,1,1,1
9214 DATA,,1,,1,1,,1,,1,,1,,,,,,
9215 DATA,,1,,1,,,1,,1,,1,,1,,1,,
9300 CLS:PRINT@168,"GRAIL OF THE GODS":PRINT@237,"by ibol":PRINT@32*9," MC-10: JIM G. & WAYNE MICHAEL"
9310 DIMM(22,18),N(20,16),T,X,Y,U,F,G,W,P,M,C,V,L,R,W(10),L(10),A(20),B(20),C(16),D(16),Z,K,I,M$(14,2),W$(20)
9320 DIMP1,P2,P3,A$:GOTO20
