0 E=RND(-(PEEK(9)*256+PEEK(10))):E=143+16*RND(7)
1 FORI=1TO8:READP(I),P$(I),S(I):NEXT:GOTO5
2 DATA322,A,89,261,S,108,232,D,125,267,F,133,276,J,147,247,K,159,282,L,170,349,;,176
3 E$=INKEY$:IFE$<>P$(J) THEN3
4 PRINT@P(J), " ";:RETURN
5 CLS:PRINT@109,"TYPING":PRINT@205,"UNIT 1":PRINT@265,"HOME POSITION":PRINT@352,"YOU WILL SEE A DIAGRAM OF THE"
6 PRINT"HANDS. PLACE YOUR FINGERS ON    THE KEYS AS SHOWN.":PRINT:PRINT"PRESS ANY KEY TO START.";
7 E$=INKEY$:IF E$="" THEN 7
8 CLS:PRINT"PLACE YOUR FINGERS IN POSITION.":E$=CHR$(E)+CHR$(E):P$=E$+" "+E$+" "+E$+"        "+E$+" "+E$+" "+E$
9 PRINT@295,E$:PRINT@311,E$:PRINT@324,P$:PRINT@356,P$:P$=E$+" "+P$+" "+E$:PRINT@385,P$:PRINT@417,P$:PRINT@449,P$:PRINT@461,E$;:PRINT@465,E$;
10 P$=E$+E$+E$+E$+E$+CHR$(E):PRINT@481,P$+" "+E$+"  "+E$+ " "+P$;
11 FORI=1TO8:SOUNDS(1),2:PRINT@P(1),P$(1);:NEXT
12 PRINT@429,"SPACE";:PRINT@64,"PRESS ENTER TO CONTINUE."
13 E$=INKEY$:IF E$="" THEN 13
14 IF ASC(E$)<>13 THEN 13
15 FORI=1TO8:PRINT@P(I), " ";:NEXT
16 PRINT @64,"TYPE THE LETTER AS IT APPEARS."
17 FORI=1TO3:FORJ=1TO8:SOUNDS(J),2:PRINT@P(J),P$(J);:GOSUB3:NEXTJ,I
18 FORI=1TO30
19 J=RND(8):IFJ=L THEN19
20 SOUNDS(J),2:PRINT@P(J),P$(J);:GOSUB3:L=J:NEXT
21 PRINT@0,"PRESS 1 TO TRY AGAIN          ":PRINT@38,"2 TO CONTINUE":PRINT"                              "
22 E$=INKEY$:IFE$="2"THEN25
23 IFE$<>"1"THEN22
24 PRINT@0,"                    ":PRINT@38,"             ":GOTO 16
25 CLS:PRINT"NOW TRY TYPING WORDS.":PRINT"TYPE THE PHRASE SHOWN":PRINT"THEN PRESS ENTER.":RESTORE:FORI=1TO24:READE$:NEXT
26 DATA A SAD LAD;,A FAD;,ASK A LAD;,A SAD FAD,A LAD ASKS DAD;,ASK DAD;,ALFALFA;,ALAS A SAD DAD;,DAD ASKS A LAD;
27 FORI=1TO9:READA$(I):NEXT
28 FORI=1TO5
29 L=RND(9):IFA$(L)=""THEN29
30 B$="":PRINT@298,A$(L):SOUND227,1
31 FORJ=1TO20
32 E$=INKEY$:IFE$=""THEN32
33 IFASC(E$)=13THEN35
34 PRINT@329+J,E$:B$=B$+E$:NEXTJ
35 IFB$=A$(L)THEN40
36 SOUND125,2:SOUND89,2:PRINT@416,"WRONG":PRINT@448,"PRESS ENTER"
37 E$=INKEY$:IFE$=""THEN37
38 IFASC(E$)<>13THEN37
39 PRINT@416,"     ":PRINT@448, "           ":I=I-1:GOTO41
40 SOUND89,1:SOUND125,1:SOUND147,1:SOUND176,2:A$(L)=""
41 PRINT@298,"                ":PRINT@330,"                    ":NEXTI
42 CLS:PRINT"PRESS 1 MORE PRACTICE ON LETTERS      2 MORE PRACTICE ON PHRASES      3 END PROGRAM"
43 E$=INKEY$:IFE$="1"THEN8
44 IFE$="2"THEN25
45 IFE$<>"3"THEN43
46 CLS:END
