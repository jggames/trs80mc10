
20 GOTO420
30 L1=X:L2=X+1
40 FOR J=Y TO Y+4 STEP 2
50 FOR I=L1 TO L2
60 SET(I,J,C):SET(I,J+1,C)
70 NEXT I
80 L1=L1-2:L2=L2+2
90 NEXT J
100 RETURN
110 FOR J=Y TO Y+5
120 FOR I=X-4 TO X+5
130 SET(I,J,C)
140 NEXT I,J
150 RETURN
160 L2=X+1
170 FOR J=Y TO Y+4 STEP 2
180 FOR I=X TO L2
190 SET(I,J,C):SET(I,J+1,C)
200 NEXT I
210 L2=L2+2
220 NEXT J
230 RETURN
240 FOR I=X TO X+1
250 FOR J=Y TO Y+5
260 SET(I,J,C)
270 NEXT J,I
280 FOR I=X-4 TO X+5
290 SET(I,Y+2,C):SET(I,Y+3,C)
300 NEXT I
310 RETURN
320 FOR J=Y TO Y+4 STEP 2
330 SET(X,J,C):SET(X,J+1,C)
340 SET(X+1,J,C):SET(X+1,J+1,C)
350 X=X+2
360 NEXT J:X=X-2
370 SET(X,Y,C):SET(X,Y+1,C)
380 SET(X+1,Y,C):SET(X+1,Y+1,C)
390 SET(X-4,Y+4,C):SET(X-3,Y+4,C)
400 SET(X-4,Y+5,C):SET(X-3,Y+5,C)
410 RETURN
420 FORI=0TO4:READA(I),B(I):NEXT
430 DATA30,2,8,18,24,18,38,18,54,18
440 FORP=1TO10
450 CLS:PRINT"WHICH SHAPE MATCHES?"
460 N=RND(5):X=A(0):Y=B(0):C=RND(3)+1:ONN GOSUB30,110,160,240,320
470 D=RND(4)
480 FORM=1TO4:X=A(M):Y=B(M):C=RND(3)+1
490 IFM<>D THEN510
500 K(M)=N:ONN GOSUB30,110,160,240,320:GOTO560
510 E=RND(5):IFE=N THEN510
520 IFM=1THEN550
530 FORL=1TOM-1:IFE=K(L)THEN510
540 NEXTL
550 K(M)=E:ONE GOSUB30,110,160,240,320
560 NEXTM
570 PRINT@388,"1       2       3      4"
580 SOUND200,1
590 E$=INKEY$:IFE$=""THEN590
600 IFVAL(E$)=D THEN 620
610 SOUND125,1:SOUND89,1:GOTO590
620 PRINT@448,"CORRECT!"
630 SOUND89,1:SOUND125,1:SOUND147,1:SOUND176,2
640 NEXTP
650 PRINT:PRINT"TRY AGAIN? (Y/N)";
660 E$=INKEY$:IFE$="Y"THEN440
670 IFE$<>"N"THEN660
680 CLS:END
Matching Shapes.txt
Displaying Matching Shapes.txt.