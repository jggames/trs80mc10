1 REM COMPUTER BATTLE GAMES
2 REM BY DANIEL ISAAMAN AND JENNY TYLER
3 REM @1982 USBORNE PUBLISHING LTD.
10 CLS 0
20 DIM Y(3),F(3)
30 N=1
40 PS=INT(RND(0)*3+1)
50 P=INT(RND(0)*26+5)
60 GOSUB 400
70 FOR I=PS TO 63 STEP PS
80 GOSUB 300
90 F$=INKEY$
100 IF F$="" OR N>3 THEN 130
110 F(N)=1
120 N=N+1
130 FOR J=1 TO 3
140 RESET(15*J,31-Y(J))
150 IF F(J)=0 OR Y(J)>31 THEN 190
160 Y(J)=Y(J)+1
165 IF Y(J)=31 THEN 210
170 IF POINT(15*J,31-Y(J)) THEN 230
180 SET(15*J,31-Y(J),8)
190 NEXT
200 NEXT
210 PRINT @0,"MISSED"
220 END
230 PRINT @0,"HIT!!!"
240 END
300 RESET (I-PS,P):RESET(I-PS+1,P)
310 SET(I,P,6):SET(I+1,P,6)
320 RETURN
400 FOR J=1 TO 3
410 SET (J*15,31,8)
420 SET (J*15+1,31,8)
430 NEXT J
440 RETURN