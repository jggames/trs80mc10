10 REM BASED ON THE DUKE OF DRAGONFEAR BY TIM HARTNELL
15 REM MODIFIED BY JERRY YOUNG 
20 GOSUB 1280
30 CLS
40 GOSUB 1200
50 Q=RND(7)
60 IF Q=0 AND E<>55 THEN GOSUB 1200
70 CLS:GOSUB 1620
100 PRINT "YOU HAVE"40-H"MOVES LEFT"
110 PRINT "WHAT DO YOU WANT TO DO";
150 INPUT Z$:U=0:IF Z$<>"N" AND Z$<>"S" AND Z$<>"E" AND Z$<>"W" AND Z$<>"F" AND Z$<>"Q" AND Z$<>"M" THEN 150
160 IFZ$="N"ANDA(E-20)=88ORZ$="S"ANDA(E+20)=88ORZ$="E"ANDA(E+1)=88ORZ$="W"ANDA(E-1)=88THENPRINT"YOUR WAY IS BLOCKED!":GOTO150
170 IF Z$="Q" THEN Q=9:GOTO 1170
175 SOUND 1,1
180 A(E)=32:IF Z$="N" THEN E=E-20 
190 IF Z$="S" THEN E=E+20
200 IF Z$="E" THEN E=E+1
210 IF Z$="W" THEN E=E-1
220 IF Z$="F" THEN GOSUB 930
225 IF Z$="M" THEN GOSUB 1620
230 IF A(E)=63 THEN GOSUB 310:REM MAGIC
240 IF A(E)=68 THEN GOSUB 400:REM DRAGON
250 IF A(E)=81 THEN GOSUB 550:REM QUICKSAND
260 IF A(E)=36 THEN GOSUB 630:REM GOLD
270 H=H+1:IF H=25 THEN Q=9:GOTO 1160
280 GOSUB 1610
290 GOTO 50
300 REM **************************************
310 REM MAGIC
320 PRINT:PRINT A$", YOU HAVE STUMBLED." 
330 PRINT "INTO A MAGIC CAVE, AND NOW YOU"
340 PRINT "WILL BE WHISKED TO ANOTHER CAVE"
350 GOSUB 1610
360 A(E)=88
370 E=RND(176)+22:IF A(E)=88 THEN 370
380 RETURN
390 REM **************************************
400 REM DRAGON
410 PRINT "YOU HAVE FOUND A DRAGON'S"
420 PRINT "LAIR. START SAYING YOUR PRAYERS."
430 FOR D=1 TO 1500:NEXT:GOSUB 1610
440 H=RND(10):IF H<2 THEN PRINT "IT HAS FLOWN AWAY.":RETURN
450 PRINT "IT AWAKENS AND IT HAS SEEN YOU!"
460 FOR D=1 TO 1500:NEXT:GOSUB 1610
470 IF H>6 THEN PRINT "BUT IT HAS RECENTLY EATEN":PRINT "AND SO GOES BACK TO SLEEP":RETURN
480 PRINT "AND IT ATTACKS! "
490 FOR D=1 TO 2000:NEXT:GOSUB 1610
500 IF H>8 THEN PRINT "BUT YOU FIGHT BACK AND WIN!":FOR D=1 TO 2000:NEXT:GOSUB 1610:RETURN
510 PRINT:PRINT"GAME OVER. GOODBYE, "A$:FOR D=1 TO 1500:NEXT
520 FOR D=1 TO 1500:NEXT:GOSUB 1610
530 Q=9:GOTO 1160
540 REM ***********************************************
550 REM QUICKSAND
555 PRINT"OH NO QUICKSAND!"
560 FOR J=1 TO 12
570 FOR K=1 TO J:PRINT " ";:NEXT
580 PRINT "YOU ARE SINKING!"
590 FOR O=1 TO 90:NEXT
600 NEXT
610 Q=9:H=0:GOTO 1160
620 REM ***********************************************
630 REM TREASURE
640 K=RND(100)+100
700 GOSUB 1610
710 PRINT:PRINT "YOU'VE FOUND"K"GOLD PIECES"
730 G=G+K
740 RETURN
900 GOSUB 1610
910 RETURN 
920 REM ***********************************************
930 REM ATTACK DRAGON
940 PRINT
950 AR=AR-1:IF AR=0 THEN PRINT "YOU HAVE USED UP ALL":PRINT "YOUR ARROWS.":GOSUB 1610:RETURN
960 PRINT "YOU HAVE"AR"ARROWS REMAINING."
970 SS=0
980 PRINT "WHICH DIRECTION DO YOU WANT"
990 INPUT "TO SHOOT IN (N,S,E OR W)";S$
1000 IF S$="N" AND A(E-20)=68 THEN SS=1:YT=E-20
1010 IF S$="S" AND A(E+20)=68 THEN SS=1:YT=E+20
1020 IF S$="E" AND A(E+1)=68 THEN SS=1:YT=E+1
1030 IF S$="W" AND A(E-1)=68 THEN SS=1:YT=E-1
1040 PRINT
1050 IF SS=0 THEN PRINT "THERE WAS NO DRAGON THERE.":PRINT "YOU WASTED AN ARROW.":GOTO 1150
1060 PRINT "WELL DONE, "A$
1070 PRINT "YOU HAVE HIT"
1080 PRINT "A FEROCIOUS DRAGON"
1090 GOSUB 1610
1100 IF RND(10)>4 THEN 1140
1110 PRINT "YOU KILLED IT!":A(YT)=32:K=RND(100)+100
1120 PRINT
1130 PRINT "YOU ARE REWARDED WITH"K"GOLD:PRINT"PIECES":G=G+K:GOTO 1150
1140 PRINT "BUT YOU ONLY WOUNDED IT."
1150 GOSUB 1610:RETURN
1160 IF H<1 THEN PRINT "GAME OVER!":GOSUB 1610:GOTO 1180
1170 CLS:GOSUB 1620:PRINT"GAME OVER":PRINT "YOU HAD"25-H"MOVES LEFT"
1180 IF G>0 THEN PRINT "YOU AMASSED"G"GOLD PIECES."
1200 A(E)=72
1250 GOSUB 1610
1260 IF Q=9 THEN END
1270 RETURN
1280 CLS:PRINT:PRINT
1290 PRINT "WELCOME TO THE WORLD"
1295 PRINT "OF DRAGONFEAR"
1300 PRINT:PRINT "YOUR MISSION IS TO EXPLORE"
1310 PRINT "THE LANDS OF DRAGONFEAR,"
1320 PRINT "LOOKING FOR TREASURE AND"
1325 PRINT "ATTEMPTING TO SLAY THE"
1330 PRINT "FEARSOME DRAGONS WHO LIVE"
1340 PRINT "IN THE WORLD."
1345 PRINT:PRINT "WHAT IS YOUR NAME";
1350 INPUT A$
1360 CLS:PRINT:PRINT "WELCOME, "A$
1370 PRINT:PRINT "YOU START THIS EXPLORATION"
1380 PRINT "WITH 40 MOVES."
1390 PRINT "YOU MUST COMPLETE YOUR TASK"
1400 PRINT "BEFORE YOUR MOVES RUN OUT."
1410 GOSUB 1700
1420 N=1:PRINT:PRINT "PRESS ANY KEY TO BEGIN"
1430 N=N+1:IF INKEY$="" THEN 1430
1440 CLS
1450 PRINT A$" IS TRAVELING TO THE":PRINT "WORLD OF DRAGONFEAR"
1460 DIM A(200):H=0:Q=0:L=0:G=0:AR=6
1470 FOR B=1 TO 200:A(B)=32
1480 IF B<22 OR B>180 OR 20*INT(B/20)=B OR 20*INT(B/20)=B-1 THEN A(B)=88
1490 NEXT
1500 FOR B=1 TO 5:RESTORE:FOR D=1 TO 5
1510 Z=RND(176)+22:IF A(Z)=88 THEN 1510
1520 READ C:A(Z)=C
1530 NEXT:NEXT
1540 DATA 88,63,68,81,36
1570 E=110
1580 RETURN
1590 REM ENGAGE CAPS LOCK
1600 REM BEFORE PLAYING
1610 FOR O=1 TO 1500:NEXT:RETURN
1615 REM *************************************
1620 REM VIEW MAP
1630 A(E)=72
1640 FOR J=1 TO 200
1650 PRINT CHR$(A(J));
1660 IF 20*INT(J/20)=J THEN PRINT
1670 NEXT:RETURN
1700 REM ************************************
1701 REM INSTRUCTIONS
1710 CLS:PRINT"HOW TO NAVIGATE THE WORLD."
1720 PRINT "N-MOVE NORTH  S-MOVE SOUTH" 
1740 PRINT "E-MOVE EAST   W-MOVE WEST"
1750 PRINT "F-FIRE ARROW  Q-QUIT":PRINT
1760 PRINT "MAP KEY":PRINT "X-WALL     $-TREASURE CHEST" 
1770 PRINT "D-DRAGON   Q-QUICKSAND":PRINT "H-PLAYER   ?-MAGIC CAVE"
1780 FOR D=1 TO 2000:NEXT:RETURN
