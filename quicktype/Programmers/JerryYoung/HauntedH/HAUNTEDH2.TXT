0 CLS0:POKE49151,16:DIMG,I,K,M,M$:M=16383:GOTO10
7 G=(PEEK(17024)AND1)*256+PEEK(17025)+M:FORI=1TOLEN(M$):K=ASC(MID$(M$,I)):POKEG+I,K-(KAND64):NEXT:RETURN
10 GOSUB 1000
19 PRINT@128
20 PRINT "          HAUNTED HOUSE"
21 PRINT "         BY DAN ISAAMAN"
30 PRINT
31 PRINT "    MODIFIED BY JERRY YOUNG":PRINT
39 FOR I=1 TO 3500:NEXT I:GOSUB 2000
40 T=0:RESTORE
50 FOR R=1 TO 10
60 GOSUB 1000
70 READM$:M$="YOU ARE IN THE "+M$:GOSUB7:M$=INKEY$
80 N=RND(8)
90 FOR G=1 TO N
100 X=RND(19):Y=RND(15):IFPEEK(M+1+X+32*Y)=15ORPEEK(M+2+X+32*Y)=15THEN100
110 PRINT@(Y*32+X),"oo";
120 NEXT G
130 PRINT@0,;
140 FOR I=1 TO 350:GOSUB 3000
150 IF I$<>"" THEN I=9999
160 NEXT I
170 K=VAL("0"+I$)
180 IF K<N THEN PRINT "THERE ARE";N-K;"GHOST LEFT."
190 IF K>N THEN PRINT "YOU CREATED";K-N;"GHOSTS."
200 T=T+ABS(N-K)
210 FOR I=1 TO 1000:NEXT I
220 NEXT R
229 GOSUB 1000
230 PRINT "THERE ARE";T;"GHOSTS"
240 PRINT "LEFT IN THE HOUSE."
250 IF T=0 THEN PRINT "WELL DONE!":GOTO 4000
260 PRINT "YOU'LL JUST HAVE TO TRY AGAIN!"
270 FOR I=1 TO 2500:NEXT I
280 GOTO 40
500 DATA "LIVING ROOM","DINING ROOM","KITCHEN","BATHROOM","MASTER BEDROOM"
510 DATA "GUEST BEDROOM","DEN","OFFICE","ATTIC","GARAGE"
1000 CLS0:RETURN
2000 CLS0:POKE49151,64:CLS
2010 M$="HOW TO PLAY":GOSUB7:PRINT@32,;
2020 PRINT "YOU HAVE BEEN ASKED TO RID"
2025 PRINT "A FRIEND'S HOME OF THE GHOSTS"
2030 PRINT "THAT HAVE APPEARED. YOU HAVE AN"
2040 PRINT "INCANTATION TO RECITE THAT MUST"
2055 PRINT "BE RECITED FOR EACH GHOST. YOUR"
2060 PRINT "PSYCHIC POWERS LETS YOU SEE THE"
2070 PRINT "GHOSTS EYES WHEN YOU ENTER A"
2080 PRINT "ROOM. YOU HAVE SECONDS TO PRESS"
2085 PRINT "THE CORRECT NUMBER FOR THE"
2090 PRINT "NUMBER OF GHOST IN THAT ROOM."
2100 PRINT "DO NOT TO SPEAK THE INCANTATION"
2105 PRINT "MORE THAN NEEDED OR YOU WILL"
2110 PRINT "SUMMON MORE GHOST! GOOD LUCK.":PRINT@484,;:M$="HIT ANY KEY TO CONTINUE":GOSUB7
2999 GOSUB3000:ON-(I$="")GOTO2999:RETURN
3000 I$=INKEY$:RETURN
4000 POKE49151,16:END
4001 REM HAUNTED HOUSE
4002 REM DAN ISAAMAN
4003 REM COMPUTER GAMES TO
4004 REM PLAY AND WRITE
4005 REM COPYRIGHT 1983
4006 REM MODIFIED BY
4007 REM JERRY YOUNG 2023
