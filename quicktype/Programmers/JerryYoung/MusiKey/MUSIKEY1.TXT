0 CLEAR300:I=RND(-(PEEK(9)*256+PEEK(10))):GOTO16
1 PRINT@496,"PRESS <ENTER>";
2 G$=INKEY$:IFG$=""THEN2
3 IFASC(G$)<>13THEN2
4 CLS:RETURN
5 S=38:H$="F":RETURN
6 S=64:H$="G":RETURN
7 S=87:H$="A":RETURN
8 S=103:H$="B":RETURN
9 S=113:H$="C":RETURN
10 S=129:H$="D":RETURN
11 S=142:H$="E":RETURN
12 S=149:H$="F":RETURN
13 S=161:H$="G":RETURN
14 S=171:H$="A":RETURN
15 S=181:H$="B":RETURN
16 CLS:PRINT " ** THE MUSICAL KEYBOARD **"
17 A$=CHR$(159)+CHR$(154)+CHR$(144):B$=CHR$(159)+CHR$(159)+CHR$(149):L$=CHR$(159)+A$+A$+A$+B$+A$+A$+B$+A$+A$+A$+CHR$(159)
18 PRINT@128,"EACH KEY ON THE KEYBOARD":PRINT"HAS A LETTER NAME.":PRINT@224,"THE LETTERS WE USE ARE":PRINT" A B C D E F G"
19 PRINT "THEN THE LETTERS START OVER":PRINT " ABCDEFGABCDEFG ":GOSUB1
20 FORI=0TO192STEP32:PRINT@I,L$:NEXT:C$=CHR$(159)+CHR$(159)+CHR$(149):M$=""
21 FORI=1TO10:M$=M$+C$:NEXT:M$=CHR$(159)+M$+CHR$(159)
22 PRINT@224,M$:PRINT@256,M$:PRINT@289,"F  G  A  B  C  D  E  F  G  A  B":PRINT"THE KEYBOARD HAS GROUPS OF":PRINT"3 BLACK KEYS"
23 PRINT@416,"WITH GROUPS OF 2 BLACK KEYS.":GOSUB 1:PRINT" ONE OF THE EASIEST KEYS":PRINT" TO FIND IS C"
24 PRINT@97,"FIND A GROUP OF TWO BLACK KEYS."
25 FORI=170TO266STEP32:PRINT@I,A$+A$+CHR$(159)+CHR$(159):NEXT
26 FORI=298TO330STEP32:PRINT@I,B$+B$+CHR$(159)+CHR$(159):NEXT
27 PRINT@385,"JUST TO THE LEFT OF THE":PRINT@417,"TWO BLACK KEYS IS 'C'.":PRINT@330,"c";:GOSUB1
28 PRINT@33,"'D' IS BETWEEN THE":PRINT " TWO BLACK KEYS.":PRINT " 'E' IS ON THE RIGHT."
29 FORI=202TO298STEP32:PRINT@I,A$+A$+CHR$(159)+CHR$(159):NEXT
30 FORI=330TO362STEP32:PRINT@I,B$+B$+CHR$(159)+CHR$(159):NEXT
31 PRINT@394,"C  D  E":GOSUB1:PRINT@33,"THE KEYS BY THE GROUP OF":PRINT" 3 BLACK KEYS ARE:"
32 FORI=202TO298STEP32:PRINT@I,A$+A$+A$+CHR$(159)+CHR$(159):NEXT
33 FORI=330TO362STEP32:PRINT@I,B$+B$+B$+CHR$(159)+CHR$(159):NEXT:PRINT@394,"F  G  A  B":GOSUB1
34 PRINT@228,"NOW FOR A QUIZ . . .":GOSUB1
35 CLS:FORI=0TO192STEP32:PRINT@I,L$:NEXT
36 PRINT@224,M$:PRINT@256,M$:PRINT@394, "NAME THE KEY"
37 SC=10:FOR I=1 TO 10
38 N=RND(11):IF N=N1 THEN 38
39 P=286+3*N:N1=N:PRINT@P,CHR$(126)
40 G$=INKEY$:IF G$="" THEN 40
41 ON N GOSUB 5,6,7,8,9,10,11,12,13,14,15
42 IFG$=H$THEN44
43 SOUND125,1:SOUND89,1:SC=SC-1:GOTO40
44 PRINT@P,G$:SOUNDS,15:PRINT@P," "
45 NEXT:IFSC>=5THENPRINT@394,"   GOOD !!   "
46 PRINT@448,"TRY AGAIN? (Y/N)"
47 E$=INKEY$:IF E$="Y" THEN 35
48 IF E$<>"N" THEN 47
49 CLS:END
50 REM TYPED IN BY
60 REM JERRY YOUNG
