10 CLS0:DIMK(128),X(3),Y(3):GOSUB2000
15 ZZ=100:PTS=0
20 CLS0:FORX=0TO63:SET(X,0,2):SET(X,29,2):NEXT:FORY=0TO29:SET(0,Y,2):SET(63,Y,2):NEXT:X=32:Y=16:DX=1:DY=0
30 GOSUB110:GOSUB100:GOSUB300
40 C=POINT(X,Y):IFC=2THEN85
41 SET(X,Y,2)
50 IFC=4THENPTS=PTS+1:GOSUB100:GOSUB110:SOUND200,2:SET(X,Y,4):IF(PTS/100)-INT(PTS/100)=0THEN200
60 FORZ=1TOZZ:NEXT:A$=INKEY$:IFA$=""GOTO80
61 ND=K(ASC(A$))-1:IFND<0GOTO80
70 DX=X(ND):DY=Y(ND)
80 X=X+DX:Y=Y+DY:GOTO40
85 FORZ=1TO5:SOUND1,1:FORZZ=1TO100:NEXT:RESET(X,Y):FORZZ=1TO100:NEXT:SET(X,Y,2):NEXT:SET(X,Y,8)
89 IFPTS>HS THENHS=PTS:SOUND100,2:SOUND200,2:GOSUB300
90 PRINT@8,"PLAY AGAIN Y/N?";
91 I$=INKEY$:IFI$=""THEN91
92 IFI$="Y"THEN15
93 IFI$="N"THENEND
94 GOTO91
100 PRINT@480,"SCORE:";PTS;:RETURN
110 H=RND(30):V=RND(13):PRINT@32*V+H,CHR$(191);:RETURN
200 PTS=PTS+100:ZZ=ZZ-5:IFZZ<50THENZZ=50
210 FORZ=1TO5:SOUND200,1:SOUND201,1:SOUND100,2:SOUND198,3:SOUND110,2:NEXTZ:GOTO20
300 PRINT@496,"HIGH:";HS;:RETURN
2000 K(87)=1:K(65)=2:K(83)=3:K(90)=4:K(84)=1:K(70)=2:K(72)=3:K(71)=4:Y(0)=-1:Y(3)=1:X(1)=-1:X(2)=1
2006 PRINT@4*32,;:PRINTTAB(8)"A 'TRON-ISH' GAME"
2007 PRINTTAB(11)"EDUARDO OCHS":PRINT
2010 PRINTTAB(3)"MC-10 EDITS BY JIM GERRIE"
2014 PRINT@485,"HIT ANY KEY TO CONTINUE";
3020 A$=INKEY$:X=RND(0):IFA$=""THEN3020
3030 CLS0:RETURN
