10 CLS0:DIMK(128),X(3),Y(3):GOSUB2000
15 ZZ=100:PTS=0
20 CLS0:FORX=0TO63:SET(X,0,2):SET(X,29,2):NEXT:FORY=0TO29:SET(0,Y,2):SET(63,Y,2):NEXT:X=32:Y=16:DX=1:DY=0
30 GOSUB110:GOSUB100:GOSUB300
40 C=POINT(X,Y):IFC=2THEN85
41 SET(X,Y,2)
50 IFC=4THENPTS=PTS+1:GOSUB100:GOSUB110:SOUND200,2:SET(X,Y,4):IF(PTS/100)-INT(PTS/100)=0THEN200
60 FORZ=1TOZZ:NEXT:A$=INKEY$:IFA$=""GOTO80
61 ND=K(ASC(A$))-1:IFND<0GOTO80
70 DX=X(ND):DY=Y(ND)
80 X=X+DX:Y=Y+DY:GOTO40
85 FORZ=1TO5:SOUND1,1:FORZZ=1TO100:NEXT:RESET(X,Y):FORZZ=1TO100:NEXT:SET(X,Y,2):NEXT:SET(X,Y,8)
89 IFPTS>HSTHENHS=PTS:SOUND100,2:SOUND200,2:GOSUB300
90 PRINT@8,"PLAY AGAIN Y/N?";
91 I$=INKEY$:IFI$=""THEN91
92 IFI$="Y"THEN15
93 IFI$="N"THENEND
94 GOTO91
100 PRINT@480,"SCORE:";PTS;:RETURN
110 H=RND(30):V=RND(13):PRINT@32*V+H,"Â¿";:RETURN
200 PTS=PTS+100:ZZ=ZZ-5:IFZZ<50THENZZ=50
210 FORZ=1TO5:SOUND200,1:SOUND201,1:SOUND100,2:SOUND198,3:SOUND110,2:NEXTZ:GOTO20
300 PRINT@496,"HIGH:";HS;:RETURN
2000 K(87)=1:K(65)=2:K(83)=3:K(90)=4:K(84)=1:K(70)=2:K(72)=3:K(71)=4:Y(0)=-1:Y(3)=1:X(1)=-1:X(2)=1
2001 POKE49151,64:CLS:PRINT"********************************";
2002 PRINT"*                              *";
2003 PRINT"*                              *";
2004 PRINT"*        A TRON-ISH GAME.      *";
2005 PRINT"*                              *";
2006 PRINT"*             AUTHOR:          *";
2007 PRINT"*          EDUARDO OCHS        *";
2008 PRINT"*                              *";
2009 PRINT"*                              *";
2010 PRINT"*           MC-10 EDITS        *";
2011 PRINT"*          BY JIM GERRIE       *";
2012 PRINT"*                              *";
2013 PRINT"*                              *";
2014 PRINT"*    HIT ANY KEY TO CONTINUE   *";
2015 PRINT"*                              *";
2016 PRINT"*******************************";:POKE16895,106
2020 A$=INKEY$:X=RND(0):IFA$=""THEN2020
3000 CLS:PRINT"********************************";
3002 PRINT"*YOU'RE YELLOW AND YOU LEAVE A *";
3003 PRINT"*YELLOW TRAIL WHEN YOU MOVE.   *";
3004 PRINT"*YOU DIE WHEN YOU HIT SOMETHING*";  
3005 PRINT"*YELLOW. USE THE aszw KEYS TO  *";
3006 PRINT"*CHANGE DIRECTION. SOMEWHERE IN*";
3007 PRINT"*THE ARENA IS A 2X2 RED SQUARE.*";
3008 PRINT"*GOING OVER A RED PIXEL EARNS  *";
3009 PRINT"*YOU A POINT AND MAKES ANOTHER *";
3010 PRINT"*RED SQUARE APPEAR. EACH 100   *";
3011 PRINT"*PTS YOU WILL EARN A 100 POINT *";
3012 PRINT"*BONUS, A NEW ARENA AND THE    *";
3013 PRINT"*GAME WILL SPEED UP.           *";
3014 PRINT"*           GOOD LUCK          *";
3015 PRINT"*      HIT ANY KEY TO BEGIN    *";
3016 PRINT"*******************************";:POKE16895,106
3020 A$=INKEY$:X=RND(0):IFA$=""THEN3020
3030 CLS0:POKE49151,16:RETURN
