5 CLS
10 PRINT "TOFU SHOP GAME BY TAKEOKA"
20 PRINT "ORIGINAL PROLOG CODE BY TAKEOKA"
30 PRINT "CONVERTED TO TRS-80 COCO BY"
40 PRINT "CARLOS CAMACHO"
50 PRINT "PRESS SPACE TO CONTINUE.";
60 A$ = INKEY$: IF A$ <> " " THEN 60
70 DIM T(3)
80 X = RND(-TIMER) ' INITIALIZE RANDOM NUMBER GENERATOR
85 GOSUB 400 ' ADD SOUND EFFECT
90 GOSUB 500
95 CLS
100 PRINT "VIEW RULES? (Y/N)";
110 INPUT A$
120 IF A$="Y" THEN GOSUB 600
130 GOSUB 1000
140 PRINT "PLAY AGAIN? (Y/N)";
150 INPUT A$
160 IF A$="Y" THEN GOTO 90 ELSE END
170 END

400 REM SOUND EFFECT
410 PLAY "T120O3L16CDEFGAC" ' RAPID SOUND EFFECT
420 RETURN

500 REM INITIAL SETUP
510 A = 5000: B = 5000
515 D = 0 ' INITIALIZE DAY COUNTER
520 RETURN

600 REM DISPLAY RULES
605 CLS
610 PRINT "THIS IS THE TOFU SHOP GAME."
620 PRINT "THE ORIGINAL PRICE OF TOFU IS"
630 PRINT "40 YEN, AND THE SALE PRICE IS"
640 PRINT "50 YEN. THE NUMBER SOLD PER"
650 PRINT "DAY DEPENDS ON THE WEATHER."
660 PRINT "ON SUNNY DAYS, 500 ARE SOLD;"
670 PRINT "CLOUDY, 300; RAINY, 100. TOFU"
680 PRINT "DOES NOT LAST, SO UNSOLD"
690 PRINT "AMOUNTS ARE DISCARDED. DECIDE"
700 PRINT "HOW MANY TO MAKE BASED ON"
710 PRINT "TOMORROW'S WEATHER FORECAST."
720 PRINT "WIN BY EXCEEDING 30,000 YEN"
730 PRINT "STARTING FROM 5,000 YEN."
740 PRINT "PRESS SPACE TO CONTINUE.";
750 A$ = INKEY$: IF A$ <> " " THEN 750
760 CLS
770 RETURN

1000 REM GAME LOOP
1005 CLS
1010 IF A >= 30000 OR B >= 30000 THEN GOSUB 1200: RETURN
1020 PRINT "YOUR FUNDS: "; A; " YEN"
1030 PRINT "COCO'S FUNDS: "; B; " YEN"
1040 D = D + 1 ' INCREMENT DAY COUNTER
1045 PRINT "DAYS ELAPSED: "; D
1050 GOSUB 1500
1060 L = INT(A / 40) ' LIMIT BASED ON AVAILABLE FUNDS
1070 GOSUB 2000
1080 GOSUB 2500
1090 GOSUB 3000
1100 GOTO 1010

1200 REM DISPLAY RESULTS
1205 CLS
1210 IF A > B THEN PRINT "YOU WIN!" ELSE PRINT "THE COMPUTER WINS."
1220 PRINT "FINAL STATS..."
1230 PRINT "YOUR FINAL FUNDS: "; A; " YEN"
1240 PRINT "COCO'S FINAL FUNDS: "; B; " YEN"
1250 PRINT "TOTAL DAYS ELAPSED: "; D
1260 RETURN

1500 REM WEATHER FORECAST
1510 H = RND(101) - 1                ' SUNNY PERCENTAGE BETWEEN 0 AND 100
1520 R = RND(101 - H) - 1            ' RAINY PERCENTAGE BASED ON REMAINING
1530 C = 100 - H - R                 ' CLOUDY IS THE REMAINING PERCENTAGE
1540 PRINT "TOMORROW'S WEATHER FORECAST:"
1545 PRINT "SUNNY  "; H; "%"
1546 PRINT "CLOUDY "; C; "%"
1547 PRINT "RAIN   "; R; "%"
1560 RETURN

2000 REM INPUT TOFU PRODUCTION
2010 PRINT "HOW MANY TOFU TO MAKE?"
2015 PRINT "( 1 -";L;")";
2020 INPUT M
2030 IF M > 0 AND M <= L THEN RETURN
2040 GOTO 2010

2500 REM COMPUTER TOFU PRODUCTION
2510 C1 = INT(B / 40)
2520 RETURN

3000 REM WEATHER OUTCOME
3005 CLS
3010 X = RND(101) - 1
3020 IF X <= R THEN W$ = "RAIN": S = 100
3030 IF X > R AND X <= R + C THEN W$ = "CLOUDY": S = 300
3040 IF X > R + C THEN W$ = "SUNNY": S = 500
3050 PRINT "TODAY'S WEATHER: "; W$
3060 IF S < M THEN SOLD = S ELSE SOLD = M
3070 PA = SOLD * 50 - M * 40 ' CORRECT PROFIT CALCULATION FOR PLAYER
3080 IF S < C1 THEN SOLD = S ELSE SOLD = C1
3090 PB = SOLD * 50 - C1 * 40 ' CORRECT PROFIT CALCULATION FOR COMPUTER
3100 A = A + PA ' ADD PROFITS
3110 B = B + PB
3120 IF A < 0 THEN A = 0
3130 IF B < 0 THEN B = 0
3140 RETURN
