1 CLS:CLEAR2000:DIM CA$,CN$,TM$,N1$,SU(80),A,B,C,D,K,BX,IX,TR,O,RW,AR(9)
2 DIM GR$(18,9),RG$(18,9),UG$(9,9),OG$(9,9),TG$(9,9),S$,A$,X,Y,Z,S,L,F,SL,C1,CT,FL,I$
5 DIM I,J,LA,LS,SC,CC,R1,CL,PP,TG,FI,BT(81,2),SJ,R
6 TR=-1
20 CLS1
30 GOSUB6210
60 C=1:FORA=1TO35:RESET(A,1):RESET(A,20)
63 C=C+1:IFC=13THENRESET(A,0):RESET(A,21):C=1
67 NEXT
70 C=1:FORB=1TO20:RESET(1,B):RESET(36,B)
73 C=C+1:IFC=8THENRESET(0,B):RESET(37,B):C=1
77 NEXT
90 GOSUB6000:X=5:Y=5:VV=1:C=0:FL=1:A=0:B=0
100 PRINT@480-96," * SUDOKU SOLVER *"
110 PRINT"aswz=MOVE o=SOLVE ONCE g=GIVENS"
120 PRINT"space=CLEAR NUMBER c=CLEAR GRID"
130 PRINT"m=MAKE r=RESTORE GRID u=UNIQUE?";
140 PRINT@20+96, "  MC-10   ";
150 PRINT@20+128,"  TOOLS   ";
160 PRINT@20+160,"FOR SUDOKU";
170 PRINT@20+192,"JIM GERRIE";:PRINT@20+256,"           ";:
180 IFFL=1THENI$=CHR$(128)
190 IFFL=-1THENI$=GR$(X,Y)
200 GOSUB330
210 I$=""
220 C=C+1
230 IFC>5THENC=0:FL=FL*-1
240 I$=INKEY$
250 IFVAL(I$)>0THENGOSUB500:IFFL=-1THENGR$(X,Y)=I$:UG$(X,Y)=I$:GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
260 IFI$=" "THENGR$(X,Y)=" ":UG$(X,Y)=" ":GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
270 IFI$="A"ANDX>1THENGOSUB400:X=X-1:GOTO180
280 IFI$="S"ANDX<9THENGOSUB400:X=X+1:GOTO180
285 IFI$="W"ANDY>1THENGOSUB400:Y=Y-1:GOTO180
290 IFI$="Z"ANDY<9THENGOSUB400:Y=Y+1:GOTO180
295 IFI$="O"THENGOSUB400:GOSUB6100:GOSUB700:FORY=1TO9:FORX=1TO9:OG$(X,Y)=GR$(X,Y):NEXT:NEXT:GOTO90
297 IFI$="G"THENGOSUB400:GOSUB6100:GOSUB8000:GOTO90
300 IFI$="C"THENGOSUB400:GOSUB6100:GOSUB8010:GOTO90
305 IFI$="M"THENGOSUB400:GOSUB6100:GOSUB4000:GOTO90
306 IFI$="U"THENGOSUB400:GOSUB6110:AF=0:GOSUB7000:GOTO90
308 IFI$="R"THENGOSUB400:GOSUB6100:GOSUB8020:GOTO90
310 R=RND(1000):GOTO180
330 PRINT@(32*Y)+((X*2)-1),I$;:RETURN
400 PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:RETURN
500 FL=-1:A=1
530 IFGR$(A,Y)=I$ORGR$(X,A)=I$THENFL=1:RETURN
540 A=A+1:IFA<10THEN530
560 A=INT((X-1)/3):A=(A*3)+1:B=INT((Y-1)/3):B=(B*3)+1
600 FORC=ATOA+2:FORD=BTOB+2:IFGR$(C,D)=I$THENFL=1:D=B+2:NEXT:C=A+2:NEXT:GOTO640
630 NEXT:NEXT
640 RETURN
700 GOSUB6000:PRINT@480-96," * SUDOKU SOLVER *     "
710 LF=0:O=49:A=1:B=1:FORJ=49TO57:FORY=1TO9:FORX=1TO9:IFJ=49THENRG$(X,Y)=""
790 IFGR$(X,Y)<>" "THEN910
800 I$=CHR$(J):GOSUB500:IFFL=-1THENRG$(X,Y)=RG$(X,Y)+I$
820 IFJ=57ANDLEN(RG$(X,Y))=1THENGR$(X,Y)=RG$(X,Y):GOSUB400:LF=1:RG$(X,Y)=""
910 NEXT:NEXT:NEXT:IFLF=1THEN710
1010 I$=CHR$(O):Y=1
1030 CT=0:IFY>=10THEN2050
1040 FORX=1TO9:IFRG$(X,Y)=""THEN2000
1050 S=1:A$=RG$(X,Y):S$=I$:GOSUB3000:IFF>0THENCT=CT+1:V=X
2000 NEXT
2020 IFCT=1THENX=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2030 Y=Y+1
2040 IFY<10THEN1030
2050 X=1
2060 TC=0:IFX>=10THEN2230
2070 FORY=1TO9:IFRG$(X,Y)=""THEN2210
2100 S=1:A$=RG$(X,Y):S$=I$:IFF>0THENTC=TC+1:V=Y
2210 NEXT:IFTC=1THENY=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2220 X=X+1:IFX<10THEN2060
2230 CT=0:IFB>=10THEN2360
2240 FORC=ATOA+2:FORD=BTOB+2:IFRG$(C,D)=""THEN2310
2270 S=1:A$=RG$(C,D):S$=I$:IFF>0THENCT=CT+1:V=C:W=D
2310 NEXT:NEXT:IFCT=1THENX=V:Y=W:GR$(X,Y)=I$:GOSUB400:GOTO710
2330 A=A+3:IFA>7THENA=1:B=B+3
2350 IFB<10THEN2230
2360 O=O+1:IFO<58THEN1010
2380 IFVV=1THENVV=2:J=0:M=9:GOSUB2710
2390 MF=0:CO=0
2410 FORY=1TO9:FORX=1TO9:I$=GR$(X,Y):IFI$=" "THENCO=1:GOTO2510
2450 GR$(X,Y)=" ":GOSUB500:GR$(X,Y)=I$:IFFL=1OR(GR$(X,Y)=" "ANDRG$(X,Y)="")THENMF=1
2510 NEXT:NEXT
2520 IFCO=0ANDMF=0THENFORY=1TO9:FORX=1TO9:GOSUB400:NEXT:NEXT:SOUND200,5:RETURN
2530 IFMF=1THENJ=9:M=0:GOSUB2710
2620 SJ=0
2610 X=RND(9):Y=RND(9):SJ=SJ+1
2620 IFSJ>81THENGOSUB2720:IFSJ=0THENSF=1:RETURN
2625 IFLEN(RG$(X,Y))<2THEN2610
2630 C1=INT(RND(LEN(RG$(X,Y))))
2640 GR$(X,Y)=MID$(RG$(X,Y),C1,1):GOTO710
2710 FORY=1TO9:FORX=1TO9:GR$(X+M,Y)=GR$(X+J,Y):RG$(X+M,Y)=RG$(X+J,Y):NEXT:NEXT:RETURN
2720 SJ=0:FORY=1TO9:FORX=1TO9:IFLEN(RG$(X,Y))>1THENSJ=1:GOTO2750
2730 NEXT:NEXT
2740 PRINT@480-32,"* ILLOGICAL GRID *":FORY=1TO9:FORX=1TO9:GR(X,Y)=" ":GOSUB400:NEXT:NEXT
2750 RETURN
3000 L=LEN(S$):SL=LEN(A$):F=0
3010 FORZ=STOSL:IFMID$(A$,Z,L)=S$THENF=Z:Z=SL:NEXT:GOTO3030
3020 NEXT
3030 RETURN
3100 CN$="":RW=INT(I/9):CL=I-(RW*9)
3140 FORB=1TO9:AR(B)=0:FORA=(RW*9)TO((RW*9)+8):IFSU(A)=BTHENAR(B)=TR
3190 NEXT:FORA=CLTO(72+CL)STEP9:IFSU(A)=BTHENAR(B)=TR
3230 NEXT:NEXT
3260 BX=(INT(I/27)*27)+(INT(CL/3)*3)
3270 FORA=0TO2:FORB=0TO2:IX=BX+(A*9)+B:FORK=1TO9:IFSU(IX)=KTHENAR(K)=TR
3320 NEXT:NEXT:NEXT:FORA=1TO9:IFAR(A)THEN3380
3370 CN$=CN$+CHR$(A+48)
3380 NEXT:RETURN
4000 GOSUB6000:PRINT@480-96," * SUDOKU MAKER * ":C=0:FORY=1TO9:FORX=1TO9:SU(C)=VAL(GR$(X,Y)):C=C+1:NEXT:NEXT:SC=0
4010 FORJ=1TO81:LS=10:FORI=0TO80:IFSU(I)THEN4095
4050 GOSUB3100:TM$=CN$:CC=LEN(TM$):IFCC=0THENI=80:NEXT:J=81:NEXT:GOTO4000
4090 IFCC<LSTHENLS=CC:CA$=TM$:LA=I
4095 NEXT
4100 IFLEN(CA$)=1THENSU(LA)=VAL(CA$):GOTO4200
4110 R1=RND(LS):N1$=MID$(CA$,R1,1):SU(LA)=VAL(N1$)
4200 GOSUB4500:NEXT
4220 FORI=0TO80:IFSU(I)>0THENSC=SC+1
4240 NEXT:IFSC<>81THEN4000
4330 PRINT@480,"SEEKING GIVENS...";
4350 GOSUB5000
4400 RETURN
4500 C=0:FORY=1TO9:FORX=1TO9:PRINT@(32*Y)+((X*2)-1),CHR$(SU(C)+48);:C=C+1:NEXT:NEXT
4510 RETURN
5000 C=0:FORY=1TO9:FORX=1TO9:TG$(X,Y)=CHR$(SU(C)+48):UG$(X,Y)=TG$(X,Y):OG$(X,Y)=TG$(X,Y):C=C+1:NEXT:NEXT
5005 FI=0:AF=1:SF=0:FORCL=1TO30
5008 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):GOSUB400:NEXT:NEXT
5009 PZ=PZ+1:IFPZ>=100THENCL=30:GOTO5040
5010 OF=FI:A=RND(5)-1:B=RND(5)-1:GOSUB5050:IFFI-OF=0THEN5009
5020 GOSUB7000
5025 IFSF=1ORFF<>0THENGOSUB5100:FI=FI-2:GOTO5008
5035 FORY=1TO9:FORX=1TO9:UG$(X,Y)=TG$(X,Y):NEXT:NEXT
5040 NEXT:FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):GOSUB400:NEXT:NEXT:RETURN
5050 ONRND(2)GOSUB5051,5070:RETURN
5051 X=5-A:Y=5-B:IFPEEK(16384+(32*Y)+((X*2)-1))=96THENRETURN
5055 FI=FI+1:X=5-A:Y=5-B:BT(FI,1)=X:BT(FI,2)=Y:BT(FI,0)=ASC(GR$(X,Y)):GR$(X,Y)=" ":TG$(X,Y)=" ":GOSUB400
5060 FI=FI+1:X=5+A:Y=5+B:BT(FI,1)=X:BT(FI,2)=Y:BT(FI,0)=ASC(GR$(X,Y)):GR$(X,Y)=" ":TG$(X,Y)=" ":GOSUB400:RETURN
5070 X=5+A:Y=5-B:IFPEEK(16384+(32*Y)+((X*2)-1))=96THENRETURN
5080 FI=FI+1:X=5+A:Y=5-B:BT(FI,1)=X:BT(FI,2)=Y:BT(FI,0)=ASC(GR$(X,Y)):GR$(X,Y)=" ":TG$(X,Y)=" ":GOSUB400
5085 FI=FI+1:X=5-A:Y=5+B:BT(FI,1)=X:BT(FI,2)=Y:BT(FI,0)=ASC(GR$(X,Y)):GR$(X,Y)=" ":TG$(X,Y)=" ":GOSUB400:RETURN
5100 SF=0:IFFI-3<1THEN5130
5105 FI=FI-3:IFBT(FI,0)<>96THENUG$(BT(FI,1),BT(FI,2))=CHR$(BT(FI,0)):POKE16384+(32*BT(FI,2))+((BT(FI,1)*2)-1),BT(FI,0)
5110 FI=FI+1:IFBT(FI,0)<>96THENUG$(BT(FI,1),BT(FI,2))=CHR$(BT(FI,0)):POKE16384+(32*BT(FI,2))+((BT(FI,1)*2)-1),BT(FI,0)
5115 IFBT(FI+1,0)=96ORBT(FI+2,0)=96THEN5130
5120 POKE16384+(32*BT(FI+1,2))+((BT(FI+1,1)*2)-1),BT(FI+1,0):POKE16384+(32*BT(FI+2,2))+((BT(FI+2,1)*2)-1),BT(FI+2,0)
5130 RETURN
6000 PRINT@480-96,"                                ";
6010 PRINT"                                ";
6020 PRINT"                                ";
6030 PRINT"                               ";:POKE16895,96:RETURN
6100 PRINT@20+256,"WORKING... ";:RETURN
6110 PRINT@20+256,"TESTING... ";:RETURN
6200 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:NEXT:NEXT:RETURN
6210 FORA=1TO9:FORB=1TO9:GR$(A,B)=" ":UG$(A,B)=" ":OG$(A,B)=" ":NEXT:NEXT:RETURN
7000 FS=0:GOSUB6000
7010 TG=1:PRINT@480-96," * TEST UNIQUENESS *    ":GOSUB6200:IFOG$(1,1)=""OROG$(1,1)=" "THENTG=2
7020 GOSUB710
7030 FF=0:FORY=1TO9:FORX=1TO9:IFGR$(X,Y)<>OG$(X,Y)THENFF=1:FS=FS+1:X=9:NEXT:Y=9:NEXT:GOTO7050
7040 NEXT:NEXT
7050 IFTG=2THENFORY=1TO9:FORX=1TO9:OG$(X,Y)=GR$(X,Y):NEXT:NEXT:GOTO7000
7055 IFFF=0THENPRINT@480-32,"ORIGINAL SOLUTION FOUND";:GOTO7100
7060 PRINT@480-32,"NOT UNIQUE"
7100 IFAF=1THEN7120
7105 PRINT@20+256,"           ";:PRINT@480,"PRESS ANY KEY TO CONTINUE";
7110 I$=INKEY$:IFI$=""THEN7110
7120 RETURN
8000 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):GOSUB400:NEXT:NEXT:RETURN
8010 FORY=1TO9:FORX=1TO9:GR$(X,Y)=" ":UG$(X,Y)=" ":OG$(X,Y)=" ":GOSUB400:NEXT:NEXT:RETURN
8020 FORY=1TO9:FORX=1TO9:GR$(X,Y)=OG$(X,Y):GOSUB400:NEXT:NEXT:RETURN
