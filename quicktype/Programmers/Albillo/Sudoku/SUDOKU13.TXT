1 CLS:PRINT"INITIALIZING ...":CLEAR2000:DIM CA$,CN$,TM$,N1$,SU(80),A,B,C,D,K,BX,IX,TR,O,RW,AR(9)
2 DIM GR$(18,9),RG$(18,9),UG$(9,9),OG$(9,9),TG$(9,9),S$,A$,X,Y,Z,S,L,F,SL,C1,CT,FL,I$
5 DIM I,J,LA,LS,SC,CC,R1,CL,PP,TG,FI,BT(81,2),SJ,R
6 DIM A(9,9),L(9),B$,TM(9,9),BP$(3):X=RND(-(PEEK(9)*256+PEEK(10))):GOSUB80:CLS:GOTO87
20 FORI=1TO9:L(I)=I:NEXT
32 FORK=1TO20:I=RND(9):J=RND(9):U=L(I):L(I)=L(J):L(J)=U:NEXT
33 RR=RND(3):B$=BP$(RR):PRINT@27,RR;:REM REMOVE PRIOR TO NOT DISPLAY WHICH CLASS IS BEING USED
34 FORK=0TO80:I=(K-2*INT(K/2))+1
35 A(INT(K/9)+1,(K-9*INT(K/9))+1)=L(VAL(MID$(STR$(ASC(MID$(B$,INT(K/2)+1,1))-23),I+1,1))):NEXT
36 FORK=1TO20
37 U=RND(9):R=RND(0):V=((U+(R<.5))-3*INT((U+(R<.5))/3))+1+3*INT((U-1)/3):FORI=1TO9:N=A(I,U):A(I,U)=A(I,V):A(I,V)=N:NEXT
38 U=RND(9):R=RND(0):V=((U+(R<.5))-3*INT((U+(R<.5))/3))+1+3*INT((U-1)/3):FORJ=1TO9:N=A(U,J):A(U,J)=A(V,J):A(V,J)=N:NEXT
39 NEXT:IF RND(0)<.5 THEN FORA=1TO9:FORB=1TO9:TM(B,A)=A(A,B):NEXT:NEXT:FORA=1TO9:FORB=1TO9:A(A,B)=TM(A,B):NEXT:NEXT
40 RETURN
60 FORK=1TOT
61 I=RND(9):J=RND(9):IFA(I,J)THENA(I,J)=0:NEXT:RETURN
62 GOTO61
64 IFT-2*INT(T/2)THENA(5,5)=0
65 FORK=1TOINT(T/4)
66 I=RND(9):J=RND(9):IF I=5 OR J=5 OR A(I,J)=0 THEN 66
67 A(I,J)=0:A(I,10-J)=0:A(10-I,J)=0:A(10-I,10-J)=0:NEXT K:RETURN
80 BP$(1)="Xi^Bs$pFOFtX)4/Xj(O^Bso6scD)P;4FAJyV>WiP!"
81 BP$(2)=CHR$(91)+",>Db@Z3tx9Q#Z*K;kBQTx-"+CHR$(91)+"u"+CHR$(96)+"&&Uk"+CHR$(96)+"tu^K"+CHR$(91)+"i"+CHR$(96)+"wM."
82 BP$(3)="7*Mck"+CHR$(92)+"i"+CHR$(96)+"JQ^;H1Q/r"+CHR$(96)+"u;^iP#=OH*P"+CHR$(91)+".G;H0(2D$yS"
83 RETURN
87 TR=-1:CLS1:GOSUB6210:C=1:FORA=1TO35:RESET(A,1):RESET(A,20):C=C+1:ON1-(C=13)GOTO88:RESET(A,0):RESET(A,21):C=1
88 NEXT:C=1:FORB=1TO20:RESET(1,B):RESET(36,B):C=C+1:ON1-(C=8)GOTO89:RESET(0,B):RESET(37,B):C=1
89 NEXT
90 GOSUB6000:X=5:Y=5:VV=1:C=0:FL=1:A=0:B=0
100 PRINT@480-96,"g=GENERATE A SUDOKU p=PRINT GRID";
110 PRINT"aswz=MOVE o=SOLVE ONCE b=BLANKS"
120 PRINT"space=CLEAR NUMBER c=CLEAR GRID"
130 PRINT"m=MAKE r=RESTORE GRID u=UNIQUE?";
140 PRINT@1*32+20,"  MC-10   ";
150 PRINT@2*32+20,"  TOOLS   ";
160 PRINT@3*32+20,"FOR SUDOKU";
161 PRINT@4*32+20,"V. ALBILLO";
162 PRINT@5*32+20,"MC-10 VER.";
170 PRINT@6*32+20,"JIM GERRIE";:PRINT@8*32+20,"           ";:PRINT@9*32+20,"           ";
180 IFFL=1THENI$=CHR$(128)
190 IFFL=-1THENI$=GR$(X,Y)
200 GOSUB330
210 I$=""
220 C=C+1
230 IFC>5THENC=0:FL=FL*-1
240 I$=INKEY$
250 IFVAL(I$)>0THENGOSUB500:IFFL=-1THENGR$(X,Y)=I$:UG$(X,Y)=I$:GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
260 IFI$=" "THENGR$(X,Y)=" ":UG$(X,Y)=" ":GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
270 IFI$="A"ANDX>1THENGOSUB400:X=X-1:GOTO180
280 IFI$="S"ANDX<9THENGOSUB400:X=X+1:GOTO180
285 IFI$="W"ANDY>1THENGOSUB400:Y=Y-1:GOTO180
290 IFI$="Z"ANDY<9THENGOSUB400:Y=Y+1:GOTO180
295 IFI$="O"THENGOSUB400:GOSUB6100:GOSUB700:FORY=1TO9:FORX=1TO9:OG$(X,Y)=GR$(X,Y):NEXT:NEXT:GOTO90
297 IFI$="B"THENGOSUB400:GOSUB8100:GOTO90
300 IFI$="C"THENGOSUB400:GOSUB6100:GOSUB8010:GOTO90
305 IFI$="M"THENGOSUB400:GOSUB6100:GOSUB4000:GOTO90
306 IFI$="U"THENGOSUB400:GOSUB6100:AF=0:GOSUB7000:GOTO90
307 IFI$="G"THENGOSUB400:GOSUB6100:GOSUB4000:GOSUB7130:GOSUB6000:GOSUB8030:GOSUB8040:GN=0:GOSUB320:GOTO90
308 IFI$="R"THENGOSUB400:GOSUB6100:GOSUB8020:GOTO90
309 IFI$="P"THENGOSUB400:GOSUB6100:GOSUB8300:GOTO90
310 GOTO180
320 GN=GN+1::PRINT@9*32+20,"GEN:";GN;:GOSUB6100:GOSUB8020:GOSUB8180:ONG GOSUB64,60:GOSUB8230:GOSUB6100:AF=1:GOSUB7000
325 ONFF GOTO320:RETURN
330 PRINT@(32*Y)+((X*2)-1),I$;:RETURN
400 PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:RETURN
500 FL=-1:A=1
530 IFGR$(A,Y)=I$ORGR$(X,A)=I$THENFL=1:RETURN
540 A=A+1:IFA<10THEN530
560 A=INT((X-1)/3):A=(A*3)+1:B=INT((Y-1)/3):B=(B*3)+1
600 FORC=ATOA+2:FORD=BTOB+2:IFGR$(C,D)=I$THENFL=1:D=B+2:NEXT:C=A+2:NEXT:GOTO640
630 NEXT:NEXT
640 RETURN
700 GOSUB6000:PRINT@480-96," * SUDOKU SOLVER *     "
710 LF=0:O=49:A=1:B=1:FORJ=49TO57:FORY=1TO9:FORX=1TO9:IFJ=49THENRG$(X,Y)=""
790 IFGR$(X,Y)<>" "THEN910
800 I$=CHR$(J):GOSUB500:IFFL=-1THENRG$(X,Y)=RG$(X,Y)+I$
820 IFJ=57ANDLEN(RG$(X,Y))=1THENGR$(X,Y)=RG$(X,Y):GOSUB400:LF=1:RG$(X,Y)=""
910 NEXT:NEXT:NEXT:IFLF=1THEN710
1010 I$=CHR$(O):Y=1
1030 CT=0:IFY>=10THEN2050
1040 FORX=1TO9:IFRG$(X,Y)=""THEN2000
1050 S=1:A$=RG$(X,Y):S$=I$:GOSUB3000:IFF>0THENCT=CT+1:V=X
2000 NEXT
2020 IFCT=1THENX=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2030 Y=Y+1
2040 IFY<10THEN1030
2050 X=1
2060 TC=0:IFX>=10THEN2230
2070 FORY=1TO9:IFRG$(X,Y)=""THEN2210
2100 S=1:A$=RG$(X,Y):S$=I$:IFF>0THENTC=TC+1:V=Y
2210 NEXT:IFTC=1THENY=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2220 X=X+1:IFX<10THEN2060
2230 CT=0:IFB>=10THEN2360
2240 FORC=ATOA+2:FORD=BTOB+2:IFRG$(C,D)=""THEN2310
2270 S=1:A$=RG$(C,D):S$=I$:IFF>0THENCT=CT+1:V=C:W=D
2310 NEXT:NEXT:IFCT=1THENX=V:Y=W:GR$(X,Y)=I$:GOSUB400:GOTO710
2330 A=A+3:IFA>7THENA=1:B=B+3
2350 IFB<10THEN2230
2360 O=O+1:IFO<58THEN1010
2380 IFVV=1THENVV=2:J=0:M=9:GOSUB2710
2390 MF=0:CO=0
2410 FORY=1TO9:FORX=1TO9:I$=GR$(X,Y):IFI$=" "THENCO=1:GOTO2510
2450 GR$(X,Y)=" ":GOSUB500:GR$(X,Y)=I$:IFFL=1OR(GR$(X,Y)=" "ANDRG$(X,Y)="")THENMF=1
2510 NEXT:NEXT
2520 IFCO=0ANDMF=0THENFORY=1TO9:FORX=1TO9:GOSUB400:NEXT:NEXT:SOUND200,5:RETURN
2530 IFMF=1THENJ=9:M=0:GOSUB2710
2620 SJ=0
2610 X=RND(9):Y=RND(9):SJ=SJ+1
2620 IFSJ>81THENGOSUB2720:IFSJ=0THENSF=1:RETURN
2625 IFLEN(RG$(X,Y))<2THEN2610
2630 C1=INT(RND(LEN(RG$(X,Y))))
2640 GR$(X,Y)=MID$(RG$(X,Y),C1,1):GOTO710
2710 FORY=1TO9:FORX=1TO9:GR$(X+M,Y)=GR$(X+J,Y):RG$(X+M,Y)=RG$(X+J,Y):NEXT:NEXT:RETURN
2720 SJ=0:FORY=1TO9:FORX=1TO9:IFLEN(RG$(X,Y))>1THENSJ=1:GOTO2750
2730 NEXT:NEXT
2740 PRINT@32*14,"* ILLOGICAL GRID *":FORY=1TO9:FORX=1TO9:GR$(X,Y)=" ":GOSUB400:NEXT:NEXT:GOTO7105
3000 L=LEN(S$):SL=LEN(A$):F=0
3010 FORZ=STOSL:IFMID$(A$,Z,L)=S$THENF=Z:Z=SL:NEXT:GOTO3030
3020 NEXT
3030 RETURN
3100 CN$="":RW=INT(I/9):CL=I-(RW*9)
3140 FORB=1TO9:AR(B)=0:FORA=(RW*9)TO((RW*9)+8):IFSU(A)=BTHENAR(B)=TR
3190 NEXT:FORA=CLTO(72+CL)STEP9:IFSU(A)=BTHENAR(B)=TR
3230 NEXT:NEXT
3260 BX=(INT(I/27)*27)+(INT(CL/3)*3)
3270 FORA=0TO2:FORB=0TO2:IX=BX+(A*9)+B:FORK=1TO9:IFSU(IX)=KTHENAR(K)=TR
3320 NEXT:NEXT:NEXT:FORA=1TO9:IFAR(A)THEN3380
3370 CN$=CN$+CHR$(A+48)
3380 NEXT:RETURN
4000 GOSUB6000:PRINT@480-96," * SUDOKU MAKER * ":GOSUB20
4005 FORX=1TO9:FORY=1TO9:GR$(X,Y)=RIGHT$(STR$(A(X,Y)),1):OG$(X,Y)=GR$(X,Y):UG$(X,Y)=GR$(X,Y):GOSUB400:NEXT:NEXT
4010 RETURN
6000 FORY=12TO14:PRINT@Y*32,"                                ";:NEXT
6030 PRINT"                               ";:POKE16895,96:PRINT@12*32,"";:RETURN
6100 PRINT@20+256,"WORKING... ";:RETURN
6200 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:NEXT:NEXT:RETURN
6210 FORA=1TO9:FORB=1TO9:GR$(A,B)=" ":UG$(A,B)=" ":OG$(A,B)=" ":NEXT:NEXT:RETURN
7000 FS=0:GOSUB6000
7010 TG=1:PRINT@480-96," * TEST UNIQUENESS *    ":GOSUB6200:IFOG$(1,1)=""OROG$(1,1)=" "THENTG=2
7020 GOSUB710
7030 FF=0:FORY=1TO9:FORX=1TO9:IFGR$(X,Y)<>OG$(X,Y)THENFF=1:FS=FS+1:X=9:NEXT:Y=9:NEXT:GOTO7050
7040 NEXT:NEXT
7050 IFTG=2THENFORY=1TO9:FORX=1TO9:OG$(X,Y)=GR$(X,Y):NEXT:NEXT:GOTO7000
7055 IFFF=0THENPRINT@480-32,"ORIGINAL SOLUTION FOUND";:GOTO7105
7060 PRINT@480-32,"NOT UNIQUE"
7100 IFAF=1THEN7120
7105 PRINT@20+256,"           ";:PRINT@15*32,"PRESS ANY KEY TO CONTINUE";
7110 I$=INKEY$:IFI$=""THEN7110
7120 RETURN
7130 GOSUB6000:INPUT"HOW MANY BLANKS? (1-51)";T:ON1-(T>01 AND T<52)GOTO7130:RETURN
8000 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):GOSUB400:NEXT:NEXT:RETURN
8010 FORY=1TO9:FORX=1TO9:GR$(X,Y)=" ":UG$(X,Y)=" ":OG$(X,Y)=" ":GOSUB400:NEXT:NEXT:RETURN
8020 FORY=1TO9:FORX=1TO9:GR$(X,Y)=OG$(X,Y):GOSUB400:NEXT:NEXT:RETURN
8030 PRINT@13*32,"gENERATE RANDOM BLANKS";:PRINT@14*32,"GENERATE sYMMETRIC BLANKS"; :RETURN
8040 I$=INKEY$:ON1-(I$<>"")GOTO8040:IFI$="G"THENG=2:RETURN
8050 IFI$="S"THENG=1:RETURN
8060 GOTO8040
8100 GOSUB6000:PRINT@12*32,"rESTORE BLANKS";
8110 GOSUB8030 
8120 I$=INKEY$:IFI$=""THEN8120
8130 IFI$="R"THEN8000
8150 IFI$="G"THENG=2:GOTO8200
8160 IFI$="S"THENG=1:GOTO8200
8170 RETURN
8180 GOSUB6100:FORX=1TO9:FORY=1TO9:A(X,Y)=VAL(GR$(X,Y)):ON1-(A(X,Y)=0)GOTO8190:A(X,Y)=0
8190 NEXT:NEXT:PRINT@20+256,"           ";:RETURN
8200 GOSUB8180:GOSUB7130
8220 ON G GOSUB64,60
8230 FORX=1TO9:FORY=1TO9:I$=RIGHT$(STR$(A(X,Y)),1):ON1-(I$="0")GOTO8240:GR$(X,Y)=" ":UG$(X,Y)=" ":GOSUB400:NEXT:NEXT:RETURN
8240 GR$(X,Y)=I$:UG$(X,Y)=I$:GOSUB400:NEXT:NEXT:RETURN
8300 A$(1)="+--+--+--+--+--+--+--+--+--+"
8319 C$=CHR$(124):A$(2)="+        +        +        +"
8320 LPRINTA$(1):FORI=1TO9:FORJ=1TO9
8321 IF(J-1)-3*INT((J-1)/3)THENLPRINT" ";:GOTO8323
8322 LPRINTC$;
8323 LPRINT" ";GR$(J,I);
8325 NEXTJ:LPRINTC$:LPRINTA$(ABS(SGN(I-3*INT(I/3)))+1):NEXTI:LPRINT:RETURN
8330 REM *** MAKER SECTION FROM SUDOGEN (C) BY VALENTIN ALBILLO, 2006
