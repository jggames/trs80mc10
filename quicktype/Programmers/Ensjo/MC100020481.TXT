0 DIMC1,CC,M$:GOTO10
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 GOSUB 9000:REM INICIALIZA
20 GOSUB 4000:REM DETERMINA MOVIMENTOS POSSIVEIS
30 GOSUB 5000:REM IMPRIME TABULEIRO
40 IF FJ THEN GOTO 8000:REM FIM DE JOGO
50 GOSUB 7000:REM OBTEM MOVIMENTO
60 GOSUB 3000:REM MOVE
70 GOSUB 6000:REM INSERE NUMERO
80 GOTO 20
3000 REM MOVE
3010 FOR I=0 TO 3
3020 IF MH THEN IM=I:IN=I:GOTO 3040
3030 JM=I:JN=I
3040 N=0:K=0:FOR J=0 TO 3
3050 IF ME THEN JM=J:JN=K:GOTO 3090
3060 IF MD THEN JM=3-J:JN=3-K:GOTO 3090
3070 IF MC THEN IM=J:IN=K:GOTO 3090
3080 IM=3-J:IN=3-K
3090 M=T(IM,JM):IF M=0 THEN 3140
3100 IF N=0 THEN T(IM,JM)=0:T(IN,JN)=M:N=M:GOTO 3140
3110 IF M<>N THEN J=J-1:GOTO 3130
3120 T(IM,JM)=0:M=M+1:T(IN,JN)=M:IF M=10 THEN OA=V
3130 N=0:K=K+1
3140 NEXT J,I
3150 RETURN 
4000 REM DETERMINA MOVIMENTOS POSSIVEIS
4010 PC=F:PB=F:PE=F:PD=F:PV=F:PH=F:PT=F
4020 FOR I=0 TO 3:FOR J=0 TO 2
4030 IF PH THEN 4080
4040 M=T(I,J):N=T(I,J+1)
4050 IF NOT(PE) THEN IF N THEN IF M=0 THEN PE=V:PH=PD:PT=PH AND PV:GOTO 4080
4060 IF NOT(PD) THEN IF M THEN IF N=0 THEN PD=V:PH=PE:PT=PH AND PV:GOTO 4080
4070 IF M THEN IF M=N THEN PE=V:PD=V:PH=V:PT=PV
4080 IF PV THEN 4130
4090 M=T(J,I):N=T(J+1,I)
4100 IF NOT(PC) THEN IF N THEN IF M=0 THEN PC=V:PV=PB:PT=PH AND PV:GOTO 4130
4110 IF NOT(PB) THEN IF M THEN IF N=0 THEN PB=V:PV=PC:PT=PH AND PV:GOTO 4130
4120 IF M THEN IF M=N THEN PC=V:PB=V:PV=V:PT=PT
4130 IF PT THEN J=2:I=3
4140 NEXT J,I
4150 FJ=NOT(PC OR PB OR PE OR PD)
4160 RETURN 
5000 REM IMPRIME TABULEIRO
5010 FOR I=0 TO 3:FOR J=0 TO 3
5020 PRINT@32*(5+I*2)+(7+J*5),;:M$=N$(T(I,J)):GOSUB7
5030 NEXT J,I
5040 RETURN 
6000 REM INSERE NUMERO
6010 I=INT(RND(0)*4):J=INT(RND(0)*4)
6020 IF T(I,J)=0 THEN T(I,J)=1+ABS(RND(0)> .9):RETURN 
6030 J=J+1:IF J=4 THEN J=0:I=I+1:IF I=4 THEN I=0
6040 GOTO 6020
7000 REM OBTEM MOVIMENTO
7010 MC=F:MB=F:ME=F:MD=F:MV=F:MH=F
7020 PRINT@32*8+16,;
7030 I$=INKEY$
7040 IF PC THEN IF I$=MC$ THEN PRINT@32*3+16,;:MC=V:MV=V:RETURN 
7050 IF PB THEN IF I$=MB$ THEN PRINT@32*13+16,;:MB=V:MV=V:RETURN 
7060 IF PE THEN IF I$=ME$ THEN PRINT@32*8+5,;:ME=V:MH=V:RETURN 
7070 IF PD THEN IF I$=MD$ THEN PRINT@32*8+27,;:MD=V:MH=V:RETURN 
7080 GOTO 7030
8000 REM FIM DE JOGO
8010 IF OA THEN PRINT@32*1+5,"OBJETIVO ATINGIDO! :-D":GOTO 8030
8020 PRINT@32*1+3,"TABULEIRO OBSTRUIDO... :-("
8030 PRINT@32*15+6,"JOGAR DE NOVO (S/N)?";
8040 I$=INKEY$
8050 IF I$="S" THEN RUN 
8060 IF I$="N" THEN CLS:END 
8070 GOTO 8040
9000 REM INICIALIZA
9010 V=-1:F=0:REM BOOLEANOS
9020 REM 
9100 DIM T(3,3):GOSUB 6000:GOSUB 6000:REM TABULEIRO 4X4
9110 DIM N$(10):FOR I=0 TO 10:READ N$(I):NEXT :REM NUMEROS
9120 DATA "    ","  2 ","  4 ","  8 "," 16 "," 32 "," 64 "," 128"," 512","1024","2048"
9130 MC$="W":MB$="S":ME$="A":MD$="D":REM TECLAS
9140 OA=F:REM OBJETIVO ATINGIDO?
9150 FJ=F:REM FIM DE JOGO?
9500 CLS:M$="              "+"2048"+"              ":GOSUB7
9510 PRINT@32*3+16,MC$;
9520 PRINT@32*13+16,MB$;
9530 PRINT@32*8+5,ME$;
9540 PRINT@32*8+27,MD$;
9550 RETURN
9992 REM "2048"
9993 REM JOGO ORIGINAL POR
9994 REM GABRIELE CIRULLI
9995 REM 2014-03-??
9996 REM HTTP://EN.WIKIPEDIA.ORG/WIKI/2048_(VIDEO_GAME)
9997 REM VERSAO EM BASIC PARA MC-1000 POR
9998 REM EMERSON JOSE SILVEIRA DA COSTA (ENSJO)
9999 REM 2015-06-17  MC-10 EDITS JIM GERRIE
