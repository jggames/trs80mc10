0 GOSUB10005:GOSUB10000:GOTO1030
1 IFY>B ORY<0 THENRETURN
2 IFX>N ORX<0 THENRETURN
3 IFB(X,Y)<>L THENRETURN
4 M=M+1:Q(M)=X:R(M)=Y:Q=X:R=Y:GOSUB6:B(X,Y)=0:YD=YD+1:RETURN
5 IFMD<15 THEN7
6 PRINT@Q+32*R,"x";:RETURN
7 PRINT@Q+32*R,CHR$(C(B(Q,R)));:RETURN
9 X=E+1:Y=F:GOSUB1:X=E-1:Y=F:GOSUB1:X=E:Y=F-1:GOSUB1:X=E:Y=F+1:GOSUB1:IFM=0 THENRETURN
10 E=Q(M):F=R(M):M=M-1:GOTO9
11 FORX=0 TON:Y=B:D=0
12 IFB(X,Y)=0 THEND=D+1:GOTO14
13 IFD>0 THEN16
14 Y=Y-1:IFY<0 THEN19
15 GOTO12
16 FORZ=Y TO0 STEP-1:B(X,Z+D)=B(X,Z):NEXT:Y=Y+D
17 D=D-1:B(X,D)=0:IFD=0 THEN14
18 GOTO17
19 NEXT:X=0
20 IFB(X,B)THEN24
21 IFX>N THENRETURN
22 FORS=X TON-1:FORT=0 TOB:B(S,T)=B(S+1,T):NEXT:NEXT:FORT=0 TOB:B(N,T)=0:NEXT:N=N-1:IFN<0 THENRETURN
23 GOTO20
24 X=X+1:IFX<N THEN20
25 RETURN
26 FORR=0 TOB:FORQ=0 TOA:PRINT@Q+32*R,CHR$(C(B(Q,R)));:NEXT:NEXT:RETURN
1000 REM SWELL FOOP FOR VIDEOPAC+ (PHILLIPS O2EM)
1010 REM BY GERTK(AT)XS4ALL.NL
1020 REM BOARD DIMENSIONS AND NR. OF COLORS
1030 CLS
1040 DIMB(15,10),X,Y,Q,R,N,F,E,A,B,PY,PX,S,T,C,D,C(8),M,L:MC=1024-16384:FORA=0 TO8:C(A)=143+(A*16):NEXT:A=RND(-TIMER)
1050 REM PSEUDO STACK FOR SEARCH
1060 A=15:B=10:NC=4:DIMQ(A*B),R(A*B)
1080 PRINTTAB(2)"ORIGINALLY FOR VIDEOPAC C7420"
1140 T=32*3+8:GOSUB4000:GOSUB4030:PRINT:PRINT:PRINT
1190 PRINTTAB(4)"(C) 2012 GERTK@XS4ALL.NL":PRINT:PRINT
1200 PRINT" COCO/DRAGON MODS BY JIM GERRIE"
1210 FORT=1 TO4000:NEXT
1230 CLS
1260 PRINT"SWELL FOOP IS A PUZZLE GAME."
1280 PRINT
1290 PRINT"THE GOAL IS TO REMOVE AS MANY"
1310 PRINT"BLOCKS AS POSSIBLE IN AS FEW"
1330 PRINT"MOVES AS POSSIBLE."
1340 PRINT"BLOCKS ADJACENT TO EACH OTHER"
1350 PRINT"GET REMOVED AS A GROUP."
1360 PRINT"USE ARROW KEYS TO MOVE THE"
1365 PRINT"CURSOR AND <SPACE> TO SELECT."
1370 PRINT"THE REMAINING BLOCKS THEN"
1390 PRINT"COLLAPSE TO FILL IN THE GAPS"
1400 PRINT"AND NEW GROUPS ARE FORMED."
1420 PRINT"YOU CANNOT REMOVE SINGLE"
1430 PRINT"BLOCKS."
1440 PRINT
1460 PRINT"PRESS <ENTER> TO START...";
1480 GOSUB3570:IFI$<>CHR$(13)THEN1480
1490 REM RESET SCORE
1500 CLS:SC=0:O=RND(3)
1590 REM FILL ARRAY 
1600 FORX=0 TOA:FORY=0 TOB:B(X,Y)=RND(NC)+O:NEXT:NEXT
1650 PY=B:PX=0:N=A
1690 REM MAIN LOOP
1700 GOSUB3630:T=384:GOSUB4000:T=304:GOSUB4030:GOSUB3640:GOSUB26:GOSUB3020:GOSUB3030
1710 GOSUB3210:SOUND60,1
1730 Q=PX:R=PY
1740 MD=MD+1:GOSUB5:IFMD>45 THENMD=0
1770 I$=INKEY$:IFI$="" THEN1740
1780 GOSUB6:IFI$=" " THEN1940
1800 GOSUB7:IFCHR$(10)THENPY=PY+1:IFPY>B THENPY=0
1860 IFI$=CHR$(94)THENPY=PY-1:IFPY<0 THENPY=B
1880 IFI$=CHR$(9)THENPX=PX+1:IFPX>A THENPX=0
1900 IFI$=CHR$(8)THENPX=PX-1:IFPX<0 THENPX=A
1920 MD=15:Q=PX:R=PY:GOTO1730
1940 L=B(PX,PY)
1960 IFL=0 THENSOUND50,1:GOTO1730
1970 REM SET SEARCH COORDINATES
1980 E=PX:F=PY:YD=0:M=0:GOSUB9:SOUND1,1:PRINT@1*32+18,"YIELD:"YD;" ";:GOSUB11:IFYD<2 THENSOUND80,1:GOTO1730
2200 SC=SC+(YD-2)^2:GOTO1700
3020 PRINT@0*32+18,"SCORE:"INT(SC);:RETURN
3030 PRINT@5*32+18,"HIGH:"INT(HS);:RETURN
3210 X=0
3220 Y=B
3230 C=0
3240 IFB(X,Y)=0 THEN3300
3250 GOSUB3340:IFC>0 THENRETURN
3270 Y=Y-1:IFY>=0 THEN3230
3290 GOTO3310
3300 IFX=0ANDY=B THEN3420
3310 X=X+1:IFX>N THEN3430
3330 GOTO3220
3340 L=B(X,Y):IF(X+1)>N THEN3370
3360 IFB(X+1,Y)=L THEN3400
3370 IFY-1<0 THEN3390
3380 IFB(X,Y-1)=L THEN3400
3390 RETURN
3400 C=C+1:RETURN
3420 SC=SC+1000:PRINT@0*32+18,"bonus: 1000 ";:FORT=1 TO20:SOUND200,1:NEXT:GOSUB3020
3430 GOSUB3020
3490 PRINT@9*32+18,"GAME OVER";
3500 IFSC>HS THENHS=SC
3510 GOSUB3030
3520 PRINT@10*32+18,"PLAY AGAIN?";
3530 GOSUB3570
3540 IFI$="N"THENGOSUB10010:GOSUB10003:END
3550 IFI$="Y"THEN1500
3560 GOTO3530
3570 I$=INKEY$:IFI$="" THEN3570
3610 RETURN
3620 END
3630 FORT=16736 TO16767:POKEMC+T,147:NEXT:RETURN
3640 FORT=16864 TO16895:POKEMC+T,159:NEXT:RETURN
4000 PRINT@T,CHR$(158);CHR$(156);CHR$(156);CHR$(158);CHR$(159);CHR$(158);CHR$(158);CHR$(156);CHR$(156);CHR$(158);STRING$(2,159);CHR$(158);STRING$(3,159);
4010 PRINT@T+32,CHR$(154);CHR$(156);CHR$(156);CHR$(154);CHR$(159);CHR$(154);CHR$(154);CHR$(156);CHR$(159);CHR$(154);STRING$(2,159);CHR$(154);STRING$(3,159);
4020 PRINT@T+64,CHR$(158);CHR$(156);CHR$(152);CHR$(154);CHR$(152);CHR$(152);CHR$(154);CHR$(156);CHR$(156);CHR$(154);CHR$(156);CHR$(156);CHR$(154);CHR$(156);CHR$(156);CHR$(159);:RETURN
4030 PRINT@T+96,STRING$(2,159);CHR$(156);CHR$(156);CHR$(157);CHR$(156);CHR$(156);CHR$(157);CHR$(156);CHR$(156);CHR$(157);CHR$(156);CHR$(156);CHR$(157);STRING$(2,159);
4040 PRINT@T+128,STRING$(2,159);CHR$(148);CHR$(157);CHR$(159);CHR$(149);CHR$(159);CHR$(149);CHR$(149);CHR$(159);CHR$(149);CHR$(148);CHR$(156);CHR$(149);STRING$(2,159);
4050 PRINT@T+160,STRING$(2,159);CHR$(149);STRING$(2,159);CHR$(148);CHR$(156);CHR$(149);CHR$(148);CHR$(156);CHR$(149);CHR$(149);STRING$(4,159);:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN

