1000 REM SWELL FOOP FOR VIDEOPAC+ (PHILLIPS O2EM)
1010 REM BY GERTK(AT)XS4ALL.NL
1020 REM CLEAR SCREEN, DISABLE TOP LINE
1030 REM INIT 0
1040 CLS
1050 REM STORE
1060 REM TX 2,3,0
1070 PRINT "ORIGINALLY FOR VIDEOPAC C7420"
1110 REM TX 3,3,0
1120 PRINT
1130 PRINT
1140 PRINT "SWELL  FOOP"
1160 PRINT
1170 PRINT
1180 REM TX 4,0,0
1190 REM PRINT "(C)2012 GERTK@XS4ALL.NL";
1200 REM DISPLAY:SCREEN
1210 FORT=1TO4000:NEXTT
1220 REM INIT 0
1230 CLS:REM PRINTCHR$(150);
1240 REM STORE
1250 REM TX 7,0,0
1260 PRINT "SWELL FOOP IS A PUZZLE GAME."
1280 PRINT
1290 PRINT "THE GOAL IS TO REMOVE AS MANY"
1310 PRINT "BLOCKS AS POSSIBLE IN AS FEW"
1330 PRINT "MOVES AS POSSIBLE."
1340 PRINT "OBJECTS ADJACENT TO EACH OTHER"
1350 PRINT "GET REMOVED AS A GROUP."
1360 PRINT "
1370 PRINT "THE REMAINING OBJECTS THEN"
1390 PRINT "COLLAPSE TO FILL IN THE GAPS"
1400 PRINT "AND NEW GROUPS ARE FORMED."
1420 PRINT "YOU CANNOT REMOVE SINGLE"
1430 PRINT "OBJECTS."
1440 PRINT
1450 REM TX 2,0,0
1460 PRINT "PRESS <SPACE> TO START..."
1470 REM DISPLAY:SCREEN
1480 GOSUB3570
1490 CLS:REM RESET SCORE
1500 SC=0
1510 REM BOARD DIMENSIONS AND NR. OF COLORS
1520 MX=15:MY=10:NC=4
1530 REM DEFINE GRAPHICS CHARACTERS
1540 REM SETET 37,"00FEFEFEFEFEFEFEFEFE"
1550 REM SETET 38,"00412214080814224100"
1560 DIM BD(MX,MY)
1570 REM PSEUDO STACK FOR SEARCH
1580 DIM SX(MX*MY),SY(MX*MY)
1590 REM FILL ARRAY 
1600 FORX=0TOMX
1610 FORY=0TOMY
1620 BD(X,Y)=1+INT(RND(0)*NC)
1630 NEXTY
1640 NEXTX
1650 PY=MY:PX=0
1660 EX=MX
1670 REM INIT 4
1680 CLS:REM PRINTCHR$(150);
1690 REM MAIN LOOP
1700 GOSUB3010
1710 GOSUB3210
1720 SOUND6,1
1730 REM STORE
1740 MD=1:DX=PX:DY=PY:GOSUB2840
1750 REM SCREEN:DISPLAY
1760 REM WAIT FOR JOYSTICK MOVEMENT
1770 I$=INKEY$
1780 REM ACTION BUTTON PRESSED
1790 IFI$=" "THEN1940
1800 REM STORE
1810 MD=0:DX=PX:DY=PY:GOSUB2840
1820 REM SCREEN:DISPLAY
1830 REM CHECK DIRECTIONAL MOVEMENT
1840 IFI$="Z"THENPY=PY+1:IFPY>MYTHENPY=MY
1860 IFI$="W"THENPY=PY-1:IFPY<0THENPY=0
1880 IFI$="S"THENPX=PX+1:IFPX>MXTHENPX=MX
1900 IFI$="A"THENPX=PX-1:IFPX<0THENPX=0
1920 GOTO1730
1930 REM ACTION !
1940 FC=BD(PX,PY)
1950 REM CHECK FOR EMPTY SPACE
1960 IFFC=0THENSOUND5,1:GOTO1730
1970 REM SET SEARCH COORDINATES
1980 FX=PX:FY=PY
1990 REM RESET YIELD
2000 YD=0
2010 REM STORE
2020 GOSUB2230
2030 SOUND1,1
2040 REM SHOW YIELD IN DOUBLE HEIGHT
2050 REM TX 7,1,0
2060 REM CURSORY 10:CURSORX 33
2070 PRINT@1*32+18,"YIELD:"YD;" ";
2140 REM FORCE SCREEN UPDATE AND REDRAW
2150 REM DISPLAY:SCREEN
2160 REM CHECK FALL OR SWOOP
2170 GOSUB2350
2180 REM CALCULATE SCORE
2190 IFYD<2THENSOUND8,1:GOTO1730
2200 SC=SC+(YD-2)^2
2210 GOTO1700
2220 REM SEARCH START
2230 SP=0
2240 REM SEARCHLOOP
2250 X=FX+1:Y=FY:GOSUB2730
2260 X=FX-1:Y=FY:GOSUB2730
2270 X=FX:Y=FY-1:GOSUB2730
2280 X=FX:Y=FY+1:GOSUB2730
2290 REM IF NO ADJACENT PIECES FOUND
2300 IFSP=0THENRETURN
2310 REM GET NEXT COORDINATES FROM STACK
2320 FX=SX(SP):FY=SY(SP):SP=SP-1
2330 GOTO2250
2340 REM SWOOP OR FALL
2350 FORX=0TOEX
2360 Y=MY
2370 CC=0
2380 IFBD(X,Y)=0THEN2430
2390 IFCC>0THEN2450
2400 Y=Y-1
2410 IFY<0THEN2530
2420 GOTO2380
2430 CC=CC+1
2440 GOTO2400
2450 FORZ=YTO0STEP-1
2460 BD(X,Z+CC)=BD(X,Z)
2470 NEXTZ
2480 Y=Y+CC
2490 CC=CC-1
2500 BD(X,CC)=0
2510 IFCC=0THEN2400
2520 GOTO2490
2530 NEXTX
2540 REM CHECK FOR EMPTY COLUMNS
2550 X=0
2560 IFBD(X,MY)THEN2690
2570 IFX>EXTHEN2710
2580 FORS=XTOEX-1
2590 FORT=0TOMY
2600 BD(S,T)=BD(S+1,T)
2610 NEXTT
2620 NEXTS
2630 FORT=0TOMY
2640 BD(EX,T)=0
2650 NEXTT
2660 EX=EX-1
2670 IFEX<0THEN2710
2680 GOTO2560
2690 X=X+1
2700 IFX<EXTHEN2560
2710 RETURN
2720 REM CHECK LIMITS AND PIECE FOR COLOR
2730 IFY>MYORY<0THENRETURN
2740 IFX>EXORX<0THENRETURN
2750 IFBD(X,Y)<>FCTHENRETURN
2760 REM FOUND SAME COLOR, PUSH ON STACK
2770 SP=SP+1:SX(SP)=X:SY(SP)=Y
2780 DX=X:DY=Y:MD=1:GOSUB2840
2790 BD(X,Y)=0
2800 REM INCREASE YIELD COUNTER
2810 YD=YD+1
2820 RETURN
2830 REM DISPLAY WHITE CROSS IF EMPTY
2840 IFMD=0THEN2920
2850 REM ET 7,3,MD
2860 PRINT@DX+32*DY,"x";
2900 RETURN
2910 REM DISPLAY SINGLE PIECE
2920 DC=BD(DX,DY):PRINT@DX+32*DY,CHR$(143+(DC*16));:RETURN
3000 REM DRAW BOARD
3010 REM STORE
3020 REM DISPLAY SCORE
3030 REM CURSORY 0:CURSORX 33
3040 REM TX 6,1,0
3050 PRINT@0*32+18,"SCORE:"INT(SC);
3100 REM CURSORY 3:CURSORX 33
3120 REM DRAW ALL PIECES IN MODE 0
3130 MD=0
3140 FORDY=0TOMY
3150 FORDX=0TOMX
3160 GOSUB2920
3170 NEXTDX
3180 NEXTDY
3190 REM DISPLAY:SCREEN
3200 RETURN
3210 X=0
3220 Y=MY
3230 C=0
3240 IFBD(X,Y)=0THEN3300
3250 GOSUB3340
3260 IFC>0THENRETURN
3270 Y=Y-1
3280 IFY>=0THEN3230
3290 GOTO3310
3300 IFX=0ANDY=MYTHEN3420
3310 X=X+1
3320 IFX>EXTHEN3430
3330 GOTO3220
3340 FC=BD(X,Y)
3350 IF(X+1)>EXTHEN3370
3360 IFBD(X+1,Y)=FCTHEN3400
3370 IFY-1<0THEN3390
3380 IFBD(X,Y-1)=FCTHEN3400
3390 RETURN
3400 C=C+1
3410 RETURN
3420 SC=SC+1000
3430 REM STORE
3440 REM TX 6,1,0
3450 PRINT@0*32+20,"SCORE:"INT(SC);
3490 REM TX 7,3,1
3500 REM CURSORY 4:CURSORX 7
3510 PRINT@3*32+18"GAME OVER";
3540 REM DISPLAY:SCREEN
3550 GOSUB3570
3560 RUN1490
3570 I$=INKEY$:IFI$=""THEN3570
3610 RETURN
3620 END
