1 '******** COPYRIGHT 1984 BY DARIN ANDERSEN ********
2 U$=CHR$(128)
3 CLS:PRINT@64,STRING$(32,CHR$(239))
4 PRINT@96,STRING$(32,U$):PRINT@106,"dark"U$"castle";
5 PRINT@128,STRING$(32,CHR$(128)):PRINT@143 ,"by";
6 PRINT@160,STRING$(32,U$):PRINT@169 ,"darin"U$"andersen";
7 PRINT@192,STRING$(32,CHR$(239))
8 PRINT@456,"COPYRIGHT 1984"
9 PMODE 3,1:PCLS
10 CIRCLE(100,99),100,3,1,0,.07:CIRCLE(100,99),100,3,1,.49,1:'****MOON OUTLINE****
11 DRAW"C3;BM0,107;R120F5D2R4E3R5D5F7U3E4R7F4R3RD4R7F5D2E4R2F9":'****CLIFF OUTLINE***:
12 DRAW"C3BM53,107U15L3U5R4D2U2R4D2U2R4D2U2R4D5L2D3R15U7L3U4E9F10D3L4D7R15U3L2U5R4D2U2R4D2U2R4D2U2R4D5L2D15":'****CASTLE OUTLINE****13 DRAW"C3BM59,69L28G4L6G3L9E5L6E3R15E4R7E2U2R19G3R12D2E5R33G6L15G2L15G2L5":'****CLOUD****
14 DRAW"C3;BM109,8;G25;L7;E30;R7;G5":'****A OUTLINE ON MOON****
15 PAINT(100,50),3,3:'****PAINT MOON****
16 DRAW"C2BM0,107R122F5D2R4E3R5D5F7U3E4R7F4R3RD4R7F5D2E4R2F11R61":'****CLIFF SHADOW****
17 DRAW"C2BM118,108G6R2E3G4L8E3L18G9L4H4E5L19G3R2G2L12E3R2E6":'****CASTLE SHADOW****
18 DRAW"C2BM195,139G5L13F3L12E6L7G4L22G7D2L45D5L3G9L45D3G3F12R22D8":'****SEA OUTLINE****
19 'DRAW "C2BM220,142G20L6G15L4G15L12":DRAW"C2BM220,142D20L6D15L4D15L12":'****WAVES
20 PAINT(106,110),2,2
21 PAINT(280,180),2,2:'PAINTS SEA
22 PMODE4,1:SCREEN0,1
23 DRAW"C1BM15,10D25E3U25G3":CIRCLE(22,17),16,1,1,0,.24:CIRCLE(22,17),16,1,1,.6,1:CIRCLE(17,18),16,1,1,0,.24:CIRCLE(17,18),16,1,1,.8,1:'D
24 PAINT(17,12),1,1:PAINT(34,18),1,1
25 DRAW"C1BM110,10D25E3U25G3":DRAW"BM109,8G25L7E30R7G5":LINE(107,18)-(100,22),PSET,BF:'A 
26 PAINT(111,22),1,1:PAINT(93,22),1,1:PAINT(96,20),1,1
27 DRAW"C1;BM160,10;D25;E3;U25;G3":PAINT(161,22),1,1:CIRCLE(167,17),16,1,1,.6,1:CIRCLE(162,18),16,1,1,.8,1:LINE(172,18)-(183,18),PSET:CIRCLE(170,33),16,1,1,.6,1:CIRCLE(165,34),16,1,1,.8,1:LINE(178,34)-(189,34),PSET:'R
28 PAINT(178,8),1,1:PAINT(178,22),1,1
29 DRAW"C1BM220,10D25E3U25G3":PAINT(221,22),1,1:DRAW"C1BM240,4G9;L5;E9;R5":CIRCLE(230,33),16,1,1,.6,1:CIRCLE(225,34),16,1,1,.8,1:LINE(238,34)-(249,34),PSET:'****K****
30 PAINT(235,7),1,1:PAINT(237,22),1,1
31 'DRAW"C1BM220,142D20L6D15L4D15L12":DRAW"BM220,142G4D12L9D12L12D22"
32 LINE(215,141)-(230,141),PSET
33 LINE(195,142)-(250,142),PSET
34 LINE(220,143)-(246,143),PSET
35 LINE(195,200)-(249,200),PSET
36 LINE(130,160)-(249,160),PSET
37 LINE(100,161)-(239,161),PSET
38 LINE(120,162)-(234,162),PSET
39 LINE(100,189)-(249,189),PSET
40 LINE(70,170)-(90,170),PSET
41 LINE(50,171)-(120,171),PSET
42 LINE(189,179)-(245,179),PSET
43 SCREEN 1,1
44 PLAY"V31O1T6L8CP8DP8E-P8FP8GP8E-P8L2GL8G-P8DP8L2G-L8FP8D-P8L2FL8CP8DP8E-P8FP8GP8E-P8L4GL8A-P8GP8FP8E-P8DP8L2CL64CE-GO2CE-GO3CT3"
45 CLS3
46 PRINT@73,"HIGH SPEED Y/N";
47 A$=INKEY$:IF A$="" THEN 47
48 IF A$="Y" THEN POKE 65495,0 ELSE POKE 65494,0
49 PRINT@136,"INSTRUCTIONS Y/N";
50 A$=INKEY$:IF A$="" THEN 50
51 IF A$<>"Y" THEN 75
52 CLS 2
53 PRINT@34,"          CONTROLS          ";
54 PRINT@66,"                            ";
55 PRINT@98,"    sWORD       bOW         ";
56 PRINT@130,"    hEALINGS    tELIPORT    ";
57 PRINT@162,"    eAT         iNFORMATION ";
58 PRINT@194,"                            ";
59 PRINT@226,"    arrows MOVE CHARACTER   ";
60 PRINT@258,"                            ";
61 GOSUB 72
62 CLS:PRINTTAB(10)"dark"U$"castle":PRINT:PRINT"  YOU LIVE IN THE KINGDOM OF    'TARTH' WHERE A EVIL WIZARD HAS STOLEN THE GEM 'NASHAREL', WHICHIS THE KINGDOMS ONLY DEFENSE    FROM EVIL.  THE WIZARD HAS HID -DEN THE GEM IN A ROOM FAR  "
63 PRINT"REMOVED FROM ANY POSSIBLE HUMAN REACH.  THE KINGDOMS FATE IS IN YOUR HANDS, BRAVE ONE.  BEWARE, THE WIZARD HAS PLACED MANY      TREACHEROUS MONSTERS ALONG YOUR PATH.":PRINT:GOSUB72
64 CLS:PRINTTAB(10)"dark"U$"castle":PRINT"  THERE ARE MANY DIFFERENT WAYS TO ACHIEVE YOUR QUEST.  YOU WILLBE ABLE TO PURCHASE MANY HELPFULITEMS AT SEVERAL TRADING POSTS  ALONG THE WAY.  (THEY LOOK LIKE THIS ";CHR$(255)+CHR$(207)".)"
65 PRINT"  YOU ARE GIVEN 200 GOLD PIECES TO BEGIN YOUR JOURNEY.  YOU MAY PURCHASE WHAT YOU WANT, JUST RE-MEMBER THE NECESSITIES.  EACH   MONSTER PROTECTS TREASURES OF   IT'S OWN, WHICH YOU MAY STEAL   AND KEEP FOR YOURSELF.  (THEY   LOOK LIKE ";
66 PRINT"THIS "CHR$(145)CHR$(146)" ).":GOSUB 72
67 CLS:PRINT TAB(10)"dark"U$"castle":PRINT:PRINT"   THERE ARE SOME MAGICAL ITEMS WHICH YOU CAN PURCHASE, BUT THEYARE QUITE EXPENSIVE.  TELEPORTS CAN TELEPORT YOU TO THE NEAREST TRADING POST.  HEALINGS WILL    BRING YOUR STRENGTH TO 100% "
68 PRINT:PRINT"   ONCE YOU ARE IN A ROOM YOU   CANNOT RETURN OR ESCAPE BACK TO THE KINGDOM.  THE WIZARD IS     TRYING TO PROTECT THE GEM BY    PREVENTING YOUR ESCAPE!":GOSUB 72
69 CLS:PRINTTAB(10)"dark"U$"castle":PRINT:PRINT"   TO USE YOUR BOW YOU MUST BE  MOVING IN IN THE DIRECTION YOU  WISH TO SHOOT, THEN PRESS b.    TO USE YOUR SWORD YOU MUST BE ONTHE VICTIM."
70 PRINT:PRINT"  DON'T EXPECT THE MONSTERS TO  RUN AWAY,  FOR THEY ARE HIGHLY  SUICIDAL.":PRINT:PRINTTAB(11)"good"U$"luck":GOSUB 72
71 GOTO 75
72 PRINT@484,"PRESS ANY KEY TO CONTINUE";
73 A$=INKEY$:IF A$="" THEN 73
74 RETURN
75 T4=67:ST=100:P1=300:ET=10:GP=200
76 B1$=STRING$(32,CHR$(244))
77 ' MAIN LOOP
78 GOSUB 133
79 GOSUB 185
80 IF A<>23.5 THEN GOSUB 144
81 IFOL=0 OR LT=O THEN CLS 0:PRINT@265,"its"U$"dark";:GOTO 103
82 P2=476
83 CLS:PRINT@0,B1$:PRINT@D2,CHR$(128);:PRINT"  ";:PRINT@352,B1$
84 PRINT@460,NM,M$
85 IF A=23.5 THEN PRINT @P1+32,"  ";:FOR I=1 TO 17:SOUND 255,1:NEXT I:PRINT@P1+32,CHR$(239)CHR$(239);:SOUND 1,3:A=0
86 OL=OL-.015:IF OL<0 THEN OL=0
87 IF M=4 AND RND(200)<4 THEN M=135:PLAY "L8O2CO1AL4AO3":A=TS:GOSUB 185
88 ET=ET-.0255
89 IF ET>5 THEN E1=223
90 IF ET<5 THEN E1=159
91 IF ET<2 THEN E1=191
92 IF ET<0 THEN D=3:ET=.15:GOTO 281
93 IF OL=0 OR LT=0 THEN 81
94 IF (T1+1)/32=INT((T1+1)/32) THEN T1=T1-4
95 IF TS<>0 AND M=4 THEN PRINT@T1,CHR$(145)CHR$(146); ELSE PRINT@T1, CHR$(129)CHR$(130);
96 PRINT@P2," ";
97 PRINT@P1,CHR$(175);
98 P2=P1
99 PRINT @M1,CHR$(M);
100 REM
101 IF (T4+1)/32=INT((T4+1)/32) THEN T4=T4-3
102 PRINT@T4,TD$;
103 PRINT @396,XP;
104 PRINT@448,"HUNGER"CHR$(E1);
105 LET CH$=A$
106 '//\\ INPUT CHOICE //\\"
107 A$=INKEY$
108 IF A$="" THEN A$=CH$
109 IF A$="B" AND BW>0 THEN 219
110 P2=P1
111 IF A$="S" AND SW>0 THEN 248
112 IF A$=CHR$(8) AND P1/32=INT(P1/32) THEN A$=""
113 IF A$=CHR$(8) THEN LET P1=P1-1
114 IF A$=CHR$(9) THEN LET P1=P1+1
115 IF A$=CHR$(10) THEN LET P1=P1+32
116 IF A$=CHR$(94) THEN LET P1=P1-32
117 IF P1=D2+1 OR P1=D2+2 THEN 120ELSE 118
118 IF P1<32 THEN P1=P1+32
119 IF P1>352 THEN P1=P1-32
120 IF P1>1 AND P1<32 THEN A=23.5:P1=P1+320:GOTO 78
121 IF A$=CHR$(9) AND P1/32=INT(P1/32) THEN P1=P1-1
122 IF P1<>T4 AND P1<>T4+1 THEN 124
123 IF TD$=CHR$(255)+CHR$(207) THEN 80
124 IF A$="E" AND RT>=1 THEN RT=RT-1:ET=10:PLAY" O4L16EDCO3":A$=""
125 IF M<>4 THEN GOSUB 201
126 IF A$="T" AND TL>=1 THEN 210
127 IF A$="I"OR A$="H" THENGOSUB 253:GOTO 83
128 IF T1<>P1 AND P1<>T1+1 THEN 130
129 IF TS>0 AND M<>135 THEN PRINT@T1,TS:PLAY "L16CL16EL18FL4GL16EL4G":GP=GP+TS:TS=0:PRINT@T1,"      "
130 IF M1=P1 THEN 278
131 GOTO 86
132 '\\// RANDOM ROOM DES. \\//
133 LET X2=0:LET RM=RM+1
134 IF RM=45 THEN 300
135 M=4
136 TD$=""
137  R=(250-64)*RND(0)+64:T1=INT(R)
138  D2=RND(26)
139 IF RM/15<>INT(RM/15) AND RM<>1 THEN RETURN
140 TD$=CHR$(255)+CHR$(207) 
141 R=(200-64)*RND(0)+64:T4=INT(R)
142 RETURN
143 '\\// MARKET PLACE \\//"
144 PLAY"L8CDEGDEC
145 CLS
146 I=GP
147 PRINT USING"  YOU HAVE ####### GOLD PIECES";GP
148 PRINTU$U$U$U$"you"U$"may"U$"buy"U$"these"U$"things"U$U$U$U$;
149 PRINT"     1. SWORD          20GP."
150 PRINT"     2. BOW            60GP."
151 PRINT"     3. HEALING        40GP."
152 PRINT"     4. ARROWS          1GP."
153 PRINT"     5. RATIONS         5GP."
154 PRINT"     6. OIL             2GP."
155 PRINT"     7. LANTERN        10GP."
156 PRINT"     8. SHIELD         10GP."
157 PRINT"     9. TELIPORT      300GP."
158 PRINT"    10. PLATE MAIL     60GP."
159 PRINT"    11. CHAIN MAIL     50GP."
160 PRINT"    12. LEATHER ARMOR  25GP."
161 PRINT@448,"WHICH ITEM";:INPUT A$:A=VAL(A$)
162 IF A$="I"  OR A$="H" THEN GOSUB 253:GOTO 145
163 IF A=0 THEN A$=CHR$(10):RETURN
164 IF A<0 OR A>12 THEN CLS 3:PRINT@132,"WE HAVE NO ITEM #"A;:PRINT@389,"<enter> LEAVES MARKET";:FOR I=1 TO 2000:NEXT I
165 IF A=1 AND GP>=20 THEN SW=SW+1: GP=GP-20
166 IF A=2 AND GP>=60 THEN BW=BW+1:GP=GP-60
167 IF A=8 AND GP>=10 THEN SH=1:GP=GP-10
168 IF A=7 AND GP>=10 THEN LT=1:GP=GP-10
169 IF A=10 AND GP>=60 THEN AC=5:GP=GP-60
170 IF A=11 AND GP>=50 THEN AC=4:GP=GP-50
171 IF A=12 AND GP>=25 THEN AC=2:GP=GP-25
172 IF A=3 OR A=4 OR A=5 OR A=6 OR A=9 THEN 175
173 IF I=GP THEN 182
174 GOTO 145
175 PRINT@448,"     HOW MANY ";:INPUT B
176 IF A=3 AND GP>=40*B THEN HE=HE+B:GP=GP-40*B
177 IF A=4 AND GP>=B THEN Y=Y+B:GP=GP-B
178 IF A=5AND GP>=5*B THEN RT=RT+B:GP=GP-5*B
179 IF A=6AND GP>=2*B THEN OL=OL+B:GP=GP-2*B
180 IF A=9 AND GP>=300*B THEN TL=TL+B:GP=GP-300*B
181 IF A<1 ORA>12 THEN RETURN
182 IF I=GP THEN CLS 2:PRINT@131,"SORRY, I DONT EXTEND CREDIT":PLAY "L4GCCL8DCL4DEG"
183 GOTO 145
184 '>>>> RANDOM MONSTERS <<<<
185 REM
186 R=(6-1)*RND(0)+1:LET I=INT(R)
187 IF I=1 THEN LET M$ ="OGRE":LET X=12:D=12:TS=100
188 IF I=2 THEN LET M$="GOBLIN":LET X=6:LET D=8:LET TS=50
189 IF I=3 THEN M$="ORC":X=8:D=8:TS=75
190 IF I=4 THEN M$="CRAKEN":X=12:D=15:TS=120
191 IF I=5 THEN LET M$="VAMPIRE":X=12:D=30:TS=150
192 IF I=6 THEN M$="TROLL":X=15:D=15:TS=200
193 LET X1=X
194 TS=RND(TS)
195 NM=RND(6)
196 TS=TS*NM
197 IF M=135 THEN TS=A:PRINT@460,NM,M$
198 M1=T1
199 M=135
200 RETURN
201 IF OL=0 OR LT=O THEN 203
202 PRINT@M1," ";
203 IF M1>32+P1 THEN LET M1=M1-32
204 IF M1<P1-32 THEN LET M1=M1+32
205 IF ABS(P1-M1)<=32 AND INT(P1/32)<>INT(M1/32) AND M1>P1 THEN M1=M1-32
206 IF ABS(P1-M1)<=32 AND INT(P1/32)<>INT(M1/32)AND M1<P1 THEN M1=M1+32
207 IF P1-M1<32 AND P1-M1>0 THEN LET M1=M1+1
208 IF P1-M1>-32 AND P1-M1<0 THEN LET M1=M1-1
209 RETURN
210 LET TL=TL-1
211 FOR I=150 TO 222
212 PRINT@P1,CHR$(I);
213 SOUND I,1
214 PRINT@P1," ";
215 NEXT I
216 LET A$="4"
217 GOTO 80
218 '<><><><><> BOW <><><><><>
219 IF Y<1 THEN A$=CH$:GOTO 86
220 R=RND(200):IF R=27 THEN A$="BOW":BW=BW-1:GOTO 289
221 LET A$=CH$ 
222 IF CH$=CHR$(10) THEN Y$="!":Y1=32
223 IF CH$=CHR$(94) THEN Y$="!":Y1=-32
224 IF CH$=CHR$(8) THEN Y$="-":Y1=-1
225 IF CH$=CHR$(9) THEN Y$="-":Y1=1
226 IF Y1=0 THEN 86
227 SOUND 200,3:Y=Y-1:Y2=P1
228 IF LT=1AND OL>0 THEN 231
229 IF CH$="I" OR CH$="," THEN Y$=CHR$(126):GOTO 231
230 Y$=CHR$(127)
231 FOR I=1 TO 13:Y2=Y2+Y1
232 IF Y2<32 OR Y2>352 THEN 86
233 PRINT@Y2,Y$;
234 IF Y2=M1 THEN 241
235 IF OL<=0 OR LT=0 THEN PRINT@Y2,U$;
236 IF OL>0 AND LT=1 THEN PRINT @Y2," ";
237 NEXT I
238 Y1=0
239 GOTO 86
240 '((((((COMBAT)))))))
241 LET X=X-RND(10)
242 Y$=""
243 '>> BOW DAMAGE <<
244 IF X<=0 THEN  NM=NM-1:X=X1:SOUND 1,1:XP=XP+X1:M1=T1
245 IF NM<=0 THEN PRINT@403,"BONUS+"X1*2;:PLAY"O3CCL8CCD#DDCCO2BO3C":M=4:PRINT@403,"          ";:XP=XP+X1
246 IF NM<=0 THEN M1=476
247 GOTO 84
248 '>>>>SWORD ATTACK
249 IF SW>0 THEN R=RND(200):IF R=123 THEN A$="SWORD":SW=SW-1:GOTO 289
250 IF RND(20)<15 AND M1=P1 THEN SOUND 130,1:CH$="":GOTO 241
251 A$=""
252 SOUND 255,1:GOTO 84
253 CLS5
254 IF A$="H" AND HE>=1 THEN FOR I=ST TO 100:SOUND 225,1 :ST=I
255 PRINT@96
256 PRINT USING"STRENGTH       ###";ST
257 IF A$="H" AND HE>=1 THEN NEXT I
258 IF A$="H" AND HE>=1 THEN HE=HE-1
259 PRINT USING"EXPERIENCE   #####";XP
260 PRINT USING"ARROWS         ###";Y
261 PRINT USING"TELIPORTS       ##";TL
262 PRINT USING "HEALINGS        ##";HE
263 PRINT USING"GOLD          ####";GP
264 PRINT "RATIONS ";
265 FOR I=1 TO RT
266 IF I<24 AND RT<>0 THEN PRINT CHR$(140);:NEXT I:PRINT""ELSE PRINT"":GOTO 267
267 PRINT "OIL     ";
268 FOR I=1 TO INT(OL)
269 IF I<24 AND OL<>0THEN PRINT CHR$(140);:NEXT I:PRINT "" ELSE PRINT"":GOTO 270
270 IF LT=1 THEN PRINT"LANTERN"
271 IF SW>0 THEN PRINT SW" SWORD"
272 IF BW>0 THEN PRINT BW" BOW"
273 IF A$="H" AND HE>=1 THEN FOR I=X TO 1
274 A$="":CH$=""
275 A$=INKEY$:IF A$="" THEN 275
276 RETURN
277 '<><><>< MONSTER ATTACK
278  R=(20-0)*RND(0)
279 R=R-(AC-SH)
280 IFR <12 THEN 86
281 CLS 4
282 ST=ST-INT(R)
283 PLAY "O1L255V30CEDV10O3"
284 IF ST<0 THEN CLS0:GOTO 286
285 GOTO 81
286 PRINT@138,"rest"U$"in"U$"peace";
287 PRINT@448,;
288 PLAY"T1O3CCL8CCD#DDCCO2BO3C":GOTO 337
289 CLS
290 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT"         YOUR "A$" BROKE"
291 SCREEN 0,1
292 FOR I=1 TO 500:NEXT I
293 A$=INKEY$:IF A$="" THEN 293
294 GOTO 83
295 'GEM ROOM
296 CLS:PRINT:PRINT"   CONGRADULATIONS!!!  YOU HAVE MADE IT TO THE GEM ROOM.  THE   ONLY THING YOU CAN TAKE WITH YOUIS YOUR GOLD. THERE IS A SPIDER";
297 PRINT" IN EVERY ROOM, AND THEY WILL DO THEIR BEST TO PROTECT THE GEM."
298 PRINT"   USE THE MAP TO LOCATE YOUR   POSITION IN THE GEM ROOM (m).   ONCE YOU GRAP THE GEM YOU WILL  BE TELIPORTED OUT OF THE CASTLE."
299 A$=INKEY$:IF A$="" THEN 299
300 V=8:H=40:B1=20:A1=30:B2=20:A2=30:A=0:B=-1:M1=5
301 CLS0
302 A$=INKEY$
303 RESET(A2,B2)
304 SET(A1,B1,3)
305 A2=A1:B2=B1
306 IF H<>30 OR V<>15 THEN 349
307 IF A$=CHR$(94) THEN B=-1:A=0
308 IF A$=CHR$(10) THEN B=1:A=0
309 IF A$=CHR$(8) THEN A=-1:B=0
310 IF A$=CHR$(9) THEN A=1:B=0
311 IF A$="M" THEN 321
312 A1=A1+A:B1=B1+B
313 IF A1<1 THEN H=H+1:A1=60:CLS0
314 IF A1>60 THEN H=H-1:A1=2:CLS0
315 IF B1<1 THEN V=V+1:B1=29:CLS0
316 IF B1>30 THEN V=V-1:B1=2:CLS0
317 IF H=30 AND V=15 THEN RESET(30,15):SOUND 255,1:SET(30,15,2)
318 IF POINT(A1,B1)=2 THEN 326
319 IF V<2 OR V>30 OR H>60 OR H<2 THEN PRINT@264,"outer"U$"darkness";:PRINT@300,"beware";
320 GOTO 302
321 CLS0
322 IF V<2 OR V>30 OR H>60 OR H<2 THEN 302
323 SET(H,V,2):SET(30,15,3)
324 A$=INKEY$:IF A$="" THEN 324
325 GOTO 301
326 CLS0:FOR I=1 TO 30:SET(30,15,3):FOR X=I TO 30:NEXT X:SET(30,15,2):FOR X=1 TO I:NEXT X:NEXT I
327 CLS 3
328 PRINT@39,"CONGRADULATIONS ";
329 PRINT@98,"YOU HAVE FOUND THE GEM AND  ";
330 PRINT@130,"SAVED THE KINGDOM OF TARTH. ";
331 PRINT@162,"   YOU HAVE BEEN AWARDED    ";
332 PRINT@194,"$10,000 GOLD PEICES FOR ITS ";:GP=GP+10000:XP=XP+1000
333 PRINT@226,"SAFE RETURN.                ";
334 PRINT@448,;
335 PLAY"V31O3T8L8CP8DP8E-P8FP8GP8E-P8L2GL8G-P8DP8L2G-L8FP8D-P8L2FL8CP8DP8E-P8FP8GP8E-P8L4GL8A-P8GP8FP8E-P8DP8L2CL64CE-GO2E-GO3CT3"
336 A$=INKEY$:IF A$="" THEN 336
337 FOR I=1 TO 25:PRINT:FOR X=1 TO 25:NEXT X:NEXT I
338 PRINT USING"    TOTAL POINTS        #####";XP
339 PRINT USING"    TREASURE            #####";GP
340 PRINT:PRINT" PLAY AGAIN (Y/N)?"
341 A$=INKEY$
342 PRINT@468,CHR$(150)
343 FOR I=1 TO 50:NEXT I
344 IF A$="Y" THEN RUN
345 IF A$="N" THEN END
346 PRINT@468,CHR$(153)
347 FOR I=1 TO 100:NEXT I
348 GOTO 341
349 RESET(M1,M2):IF M1<A1 THEN M1=M1+1
350 IF M1>A1 THEN M1=M1-1
351 IF M2<B1 THEN M2=M2+1
352 IF M2>B1 THEN M2=M2-1
353 SET(M1,M2,4)
354 IF M1=A1 AND M2=B1 THEN CLS4:PLAY"O1L255V30CEDV10O3":ST=ST-7
355 IF ST<0 THEN 286
356 GOTO 307
