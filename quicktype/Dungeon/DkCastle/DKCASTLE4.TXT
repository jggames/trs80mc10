0 DIMC1,C2,M$,N(21),M:Z=16384:Q=32:GOTO2000
1 C2=1:C1=Q:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+Q:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
4 FORC1=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,C1,1))-64),ASC(MID$(M$,C1+1,1))-48:NEXT:RETURN
5 FORC1=1TO250:NEXT:RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+Z:FORC2=1TOLEN(M$):C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:?@C1-Z+C2,:RETURN
8 FORC1=1TOC2:PRINTM$;:NEXT:RETURN
9 IFOL<=.ORLT=.THENPRINT@Y2,"Ä";:RETURN
10 PRINT@Y2," ";:RETURN
11 IFOL<=.ORLT=.THENPRINT@M1,"ÄÄ";:PRINT@M1+Q,"ÄÄ";:RETURN
12 PRINT@M1,"  ";:PRINT@M1+Q,"  ";:RETURN

20 IFTS<>.THENIFM=4THENPRINT@T1,"ëí";:PRINT@T1+Q,"ïö";:RETURN
21 PRINT@T1,"ÅÇ";:PRINT@T1+Q,"Öä";:RETURN
25 PRINT@P1,"éÑ";:PRINT@P1+Q,"éÜ";:PRINT@403,"BONUS+"X1*2;
26 M$="C1C1C4C4D2":GOSUB4:SOUND135,2:M$="D2D2C2C2B2B4":GOSUB4:M=4:PRINT@403,"          ";:XP=XP+X1:RETURN

30 A=23.5:P1=P1+288:GOTO78

48 GOSUB7000:CLS3
49 PRINT@134,"INSTRUCTIONS (Y/N)?";
50 A$=INKEY$:IFA$=""THEN50
51 IFA$<>"Y"THEN75
52 CLS2
53 PRINT@34,"          CONTROLS          ";
54 PRINT@66,"                            ";
55 PRINT@98,"    sWORD       bOW         ";
56 PRINT@130,"    hEALINGS    tELEPORT    ";
57 PRINT@162,"    eAT         rEPORT/INV  ";
58 PRINT@194,"                            ";
59 PRINT@226,"    ijkl TO MOVE CHARACTER  ";
60 PRINT@258,"                            ";
61 GOSUB72:GOSUB4000
72 PRINT@484,"PRESS ANY KEY TO CONTINUE";
73 A$=INKEY$:IFA$=""THEN73
74 RETURN
75 T4=67:ST=100:P1=300:ET=10:GP=200
76 B1$="ÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙÙ"
77 REM MAIN LOOP
78 GOSUB133
79 GOSUB185
80 IFA<>23.5THENGOSUB144
81 IFOL=0ORLT=0THENCLS0:PRINT@265,"itÄisÄdark";:GOTO103
82 P2=476
83 CLS:PRINTB1$:PRINT@D2,"Ä  ";:PRINT@352,B1$
84 PRINT@460,NM,MM$;
85 IFA=23.5THENPRINT@P1+64,"  ";:FORI=1TO17:SOUND255-I,1:NEXTI:PRINT@P1+64,CHR$(239)CHR$(239);:SOUND1,3:A=0
86 OL=OL-.015:IFOL<0THENOL=0
87 IFM=4ANDRND(200)<4THENM=135:M$="C1A1H1":GOSUB4:A=TS:GOSUB185
88 ET=ET-.0255
89 IFET>5THENE1=223
90 IFET<5THENE1=159
91 IFET<2THENE1=191
92 IFET<0THEND=3:ET=.15:GOTO281
93 IFOL=0ORLT=0THEN81
94 IF(T1+1)/Q=INT((T1+1)/Q)THENT1=T1-4
95 GOSUB20
96 PRINT@P2,"  ";:PRINT@P2+Q,"  ";:PRINT@P1,"éÑ";:PRINT@P1+Q,"éÜ";:P2=P1:PRINT@M1,M1$;:PRINT@M1+Q,M2$;
101 IF(T4+1)/Q=INT((T4+1)/Q)THENT4=T4-3
102 PRINT@T4,TD$;:PRINT@T4+Q,TD$;
103 PRINT@396,XP;:PRINT@448,"HUNGER"CHR$(E1);:C$=A$
106 REM ** INPUT CHOICE
107 A$=INKEY$:IFA$=""THENA$=C$
109 IFA$="B"THENIFBW>.THEN219
110 P2=P1:IFA$="S"THENIFSW>.THEN248
113 IFA$="J"THENP1=P1-1:IF(P1+1)/Q=INT((P1+1)/Q)THENP1=P1+1
114 IFA$="L"THENP1=P1+1:IF(P1+1)/Q=INT((P1+1)/Q)THENP1=P1-1
115 IFA$="K"THENP1=P1+Q:IFP1>319THENP1=P1-Q
116 IFA$="I"THENP1=P1-Q:ON-(P1=D2+1ORP1=D2+2)GOTO30:IFP1<QTHENP1=P1+Q
122 IFP1<>T4THENIFP1<>T4+1THEN124
123 IFTD$="ˇœ"THEN80
124 IFA$="E"THENIFRT>=1THENRT=RT-1:ET=10:M$="E4D4C4":GOSUB4:A$=""
125 IFM<>4THENGOSUB201
126 IFA$="T"THENIFTL>=1THEN210
127 IFA$="R"ORA$="H"THENGOSUB253:GOTO83
128 IFT1<>P1THENIFP1<>T1+1THEN130
129 IFTS>.THENIFM<>135THENPRINT@T1,;:M$=MID$(STR$(TS),2):GOSUB7:M$="C4E4F4G2E4G2":GOSUB4:GP=GP+TS:TS=0:PRINT@T1,"      ";
130 IFM1=P1THEN278
131 GOTO86
132 REM ** RANDOM ROOM DES.
133 X2=0:RM=RM+1
134 IFRM=20THEN298
135 M=4:M1$="":M2$="":TD$=""
137 T1=(1+RND(7))*Q+RND(28)
138 D2=RND(26)
139 IFRM/5<>INT(RM/5)THENIFRM<>1THENRETURN
140 TD$="ˇœ"
141 T4=(1+RND(7))*Q+RND(28):IFT4=T1ORT4=T1+QORT4=T1+1ORT4=T1+33THEN141
142 RETURN
143 REM ** MARKET PLACE
144 M$="C4D4E4G4D4E4C4":GOSUB4
145 CLS
146 I=GP
147 PRINT"  YOU HAVE"STR$(INT(GP))" GOLD PIECES"
148 PRINT"ÄÄÄÄyouÄmayÄbuyÄtheseÄthingsÄÄÄÄ";
149 PRINT"     1. SWORD          20GP."
150 PRINT"     2. BOW            60GP."
151 PRINT"     3. HEALING        40GP."
152 PRINT"     4. ARROWS          1GP."
153 PRINT"     5. RATIONS         5GP."
154 PRINT"     6. OIL             2GP."
155 PRINT"     7. LANTERN        10GP."
156 PRINT"     8. SHIELD         10GP."
157 PRINT"     9. TELEPORT      300GP."
158 PRINT"    10. PLATE MAIL    150GP."
159 PRINT"    11. CHAIN MAIL     50GP."
160 PRINT"    12. LEATHER ARMOUR 25GP."
161 PRINT@448,"WHICH ITEM";:INPUTA$:A=VAL(A$)
162 IFA$="R"ORA$="H"THENGOSUB253:GOTO145
163 IFA=0THENA$="K":RETURN
164 IFA<0ORA>12THENCLS3:PRINT@132,"WE HAVE NO ITEM #"A;:PRINT@389,"<enter> LEAVES MARKET";:FORI=1TO3000:NEXT
165 IFA=1ANDGP>=20THENSW=SW+1:GP=GP-20
166 IFA=2ANDGP>=60THENBW=BW+1:GP=GP-60
167 IFA=8ANDGP>=10THENSH=1:GP=GP-10
168 IFA=7ANDGP>=10THENLT=1:GP=GP-10
169 IFA=10ANDGP>=150THENAC=5:GP=GP-150
170 IFA=11ANDGP>=50THENAC=4:GP=GP-50
171 IFA=12ANDGP>=25THENAC=2:GP=GP-25
172 IFA=3ORA=4ORA=5ORA=6ORA=9THEN175
173 IFI=GPTHEN182
174 GOTO145
175 PRINT@448,"    HOW MANY";:INPUTB
176 IFA=3ANDGP>=40*BTHENHE=HE+B:GP=GP-40*B
177 IFA=4ANDGP>=BTHENY=Y+B:GP=GP-B
178 IFA=5ANDGP>=5*BTHENRT=RT+B:GP=GP-5*B
179 IFA=6ANDGP>=2*BTHENOL=OL+B:GP=GP-2*B
180 IFA=9ANDGP>=300*BTHENTL=TL+B:GP=GP-300*B
181 IFA<1ORA>12THENRETURN
182 IFI=GPTHENCLS2:PRINT@128,"I'M SORRY THAT I CANNOT FULFIL  YOUR REQUEST.":M$="G2C2C2D4C4D2E2G2":GOSUB4
183 GOTO145
184 REM ** RANDOM MONSTERS
185 I=RND(6):M1$=MID$(" àÑéçàâàÑàçàÜ",I*2,2):M2$=MID$(" âÜÄÄâçÅÇÅÖÇÅ",I*2,2)
187 IFI=1THENMM$="OGRE     ":X=12:D=12:TS=50
188 IFI=2THENMM$="BLOB     ":X=6:D=8:TS=25
189 IFI=3THENMM$="ORC      ":X=8:D=8:TS=45
190 IFI=4THENMM$="KRAKEN   ":X=12:D=15:TS=60
191 IFI=5THENMM$="VAMPIRE  ":X=12:D=30:TS=75
192 IFI=6THENMM$="BEHOLDER ":X=15:D=15:TS=100
193 X1=X
194 TS=RND(TS)
195 NM=RND(6+INT(RM/5))
196 TS=TS*NM
197 IFM=135THENTS=A:PRINT@460,NM,MM$;
198 M1=T1
199 M=135
200 RETURN
201 IFOL=.ORLT=.THEN203
202 PRINT@M1,"  ";:PRINT@M1+Q,"  ";
203 IFM1>Q+P1THENM1=M1-Q
204 IFM1<P1-QTHENM1=M1+Q
205 IFABS(P1-M1)<=QTHENIFINT(P1/Q)<>INT(M1/Q)THENIFM1>P1THENM1=M1-Q
206 IFABS(P1-M1)<=QTHENIFINT(P1/Q)<>INT(M1/Q)THENIFM1<P1THENM1=M1+Q
207 IFP1-M1<QTHENIFP1-M1>.THENM1=M1+1
208 IFP1-M1>-QTHENIFP1-M1<.THENM1=M1-1
209 RETURN
210 TL=TL-1
211 FORI=150TO222
212 PRINT@P1,CHR$(128+RND(14))CHR$(128+RND(14));:PRINT@P1+Q,CHR$(128+RND(14))CHR$(128+RND(14));
213 SOUNDI,1
214 PRINT@P1,"  ";:PRINT@P1+Q,"  ";
215 NEXTI
216 A$="4"
217 GOTO80
218 REM ** BOW
219 IFY<1THENA$=C$:GOTO86
220 R=RND(200+(XP/200)):IFR<3THENA$="BOW":BW=BW-1:GOTO289
221 A$=C$
222 IFC$="K"THENY$="!":Y1=Q:Y2=P1+Q
223 IFC$="I"THENY$="!":Y1=-Q:Y2=P1
224 IFC$="J"THENY$="-":Y1=-1:Y2=P1
225 IFC$="L"THENY$="-":Y1=1:Y2=P1+1
226 IFY1=.THEN86
227 SOUND200,3:Y=Y-1:IFLT=1THENIFOL>.THEN231
229 IFC$="I"ORC$="K"THENY$=CHR$(126):GOTO231
230 Y$=CHR$(127)
231 FORI=1TO13:Y2=Y2+Y1:IFY2<QORY2>352THENI=13:NEXTI:GOTO86
233 PRINT@Y2,Y$;:IFY2=M1ORY2=M1+1THENI=13:NEXTI:SOUND130,1:GOSUB11:GOTO241
234 GOSUB9:NEXTI
238 Y1=.
239 GOTO86
240 REM ** COMBAT
241 X=X-RND(10):Y$=""
243 REM ** BOW DAMAGE
244 IFX<=.THENNM=NM-1:X=X1:SOUND1,1:XP=XP+X1:M1=T1
245 IFNM<=.THENGOSUB25 
246 IFNM<=.THENM1=476
247 GOTO84
248 REM ** SWORD ATTACK
249 IFSW>.THENR=RND(200+(XP/200)):IFR<3THENA$="SWORD":SW=SW-1:GOTO289
250 IFRND(20)<15THENIFM1=P1THENSOUND130,1:C$="":GOTO241
251 A$="":SOUND250,1:GOTO84
253 CLS5:PRINT@40,"VITAL STATISTICS";
254 IFA$="H"ANDHE>=1THENFORI=ABS(ST)+1TO100:SOUNDI,1:ST=I
255 PRINT@96,;
256 PRINT "STRENGTH       ";RIGHT$("   "+STR$(INT(ST)),3):IFA$="H"ANDHE>=1THENNEXTI
258 IFA$="H"ANDHE>=1THENHE=HE-1
259 PRINT"EXPERIENCE   ";RIGHT$("     "+STR$(INT(XP)),5)
260 PRINT"ARROWS       ";RIGHT$("     "+STR$(INT(Y)),5)
261 PRINT"TELEPORTS    ";RIGHT$("     "+STR$(INT(TL)),5)
262 PRINT"HEALINGS     ";RIGHT$("     "+STR$(INT(HE)),5)
263 PRINT"GOLD         ";RIGHT$("     "+STR$(INT(GP)),5)
264 PRINT"RATIONS ";
265 FORI=1TORT
266 IFI<24THENIFRTTHENPRINTCHR$(140);:NEXTI
267 PRINT:PRINT"OIL     ";
268 FORI=1TOINT(OL)
269 IFI<24THENIFOLTHENPRINTCHR$(140);:NEXTI
270 PRINT:IFLT=1THENPRINT"LANTERN"
271 IFSW>.THENPRINTSW" SWORD"
272 IFBW>.THENPRINTBW" BOW"
273 IFA$="H"ANDHE>=1THENFORI=XTO1
274 A$="":C$="":GOSUB8006
275 A$=INKEY$:IFA$<>"C"THEN275
276 FORC1=0TO1STEP0:C1=-(INKEY$=""):NEXT:RETURN
277 REM ** MONSTER ATTACK
278 R=20*RND(0)
279 R=R-(AC-SH)
280 IFR<12THEN86
281 CLS4:ST=ST-INT(R):SOUND240,1:IFST<.THENCLS0:GOTO286
285 GOTO81
286 PRINT@138,"restÄinÄpeace";
287 PRINT@448,;
288 M$="C4C4D4":GOSUB4:SOUND129,4:M$="D4C4C4B4C4":GOSUB4:GOTO337
289 CLS
290 PRINT:PRINT:PRINT:PRINT:PRINT:M$="YOUR "+A$+" BROKE":PRINTTAB((Q-LEN(M$))/2)M$
291 FORI=1TO12:POKE49151,64:FORC1=1TO75:NEXTC1:SOUND50,1
292 NEXTI:GOSUB8006
293 A$=INKEY$:IFA$<>"C"THEN293
294 GOTO83
295 REM ** GEM ROOM
298 GOSUB8000
299 A$=INKEY$:IFA$<>"C"THEN299
300 V=RND(8)+8:H=RND(2)+20:B1=20:A1=30:B2=20:A2=30:A=0:B=-1:M1=5+RND(50):M2=5+RND(25)
301 CLS0
302 A$=INKEY$
303 RESET(A2,B2)
304 SET(A1,B1,6)
305 A2=A1:B2=B1
306 IFH<>30ORV<>15THEN349
307 IFA$="I"THENB=-1:A=0
308 IFA$="K"THENB=1:A=0
309 IFA$="J"THENA=-1:B=0
310 IFA$="L"THENA=1:B=0
311 IFA$="M"THEN321
312 A1=A1+A:B1=B1+B
313 IFA1<1THENH=H+1:A1=60:M1=5+RND(50):M2=5+RND(25):CLS0
314 IFA1>60THENH=H-1:A1=2:M1=5+RND(50):M2=5+RND(25):CLS0
315 IFB1<1THENV=V+1:B1=29:M1=5+RND(50):M2=5+RND(25):CLS0
316 IFB1>30THENV=V-1:B1=2:M1=5+RND(50):M2=5+RND(25):CLS0
317 IFH=30ANDV=15THENRESET(30,15):SOUND250,1:SET(30,15,2)
319 IFV<2ORV>30ORH>60ORH<2THENPRINT@264,"outerÄdarkness";:PRINT@300,"beware";
320 GOTO302
321 CLS0
322 IFV<2ORV>30ORH>60ORH<2THEN302
323 SET(H,V,2):SET(30,15,6):IFPOINT(H,V)=6THEN326
324 A$=INKEY$:IFA$=""THEN324
325 GOTO301
326 CLS0:FORI=1TO30:SET(H,V,6):FORX=ITO30:NEXTX:SET(H,V,2):FORX=1TOI:NEXTX:NEXTI
327 CLS3
328 PRINT@33,"CONGRATULATIONS MY GOOD CHUM!!";
329 PRINT@98,"YOU HAVE FOUND THE GEM AND  ";
330 PRINT@130,"SAVED THE KINGDOM OF TARTH. ";
331 PRINT@162,"   YOU HAVE BEEN AWARDED    ";
332 PRINT@194,"10,000 GOLD PIECES FOR ITS  ";:GP=GP+10000:XP=XP+1000
333 PRINT@226,"SAFE RETURN.                ";
334 PRINT@448,;
335 GOSUB7000
336 A$=INKEY$:IFA$=""THEN336
337 FORI=1TO25:PRINT:FORX=1TO25:NEXTX:NEXTI
338 PRINT"    TOTAL POINTS        ";RIGHT$("     "+STR$(XP),5)
339 PRINT"    TREASURE            ";RIGHT$("     "+STR$(GP),5)
340 PRINT:PRINT" PLAY AGAIN (Y/N)?"
341 A$=INKEY$
342 PRINT@468,CHR$(150)
343 FORI=1TO50:NEXTI
344 IFA$="Y"THENRUN
345 IFA$="N"THENEND
346 PRINT@468,CHR$(153)
347 FORI=1TO100:NEXTI
348 GOTO341
349 RESET(M1,M2):IFM1<A1THENIFRND(3)>1THENM1=M1+1
350 IFM1>A1THENIFRND(3)>1THENM1=M1-1
351 IFM2<B1THENIFRND(3)>1THENM2=M2+1
352 IFM2>B1THENIFRND(3)>1THENM2=M2-1
353 SET(M1,M2,4):IFM1=A1ANDM2=B1THENCLS4:SOUND225,1:ST=ST-RND(7):CLS0
355 IFST<.THEN286
356 GOTO307

2000 CLS0:POKE49151,64:CLS:POKE17026,153
2003 CLS:PRINT@64,;:C2=Q:M$=CHR$(239):GOSUB8
2004 PRINT@96,;:C2=Q:M$="Ä":GOSUB8:PRINT@106,"darkÄcastle";
2005 PRINT@128,;:C2=Q:M$="Ä":GOSUB8:PRINT@143 ,"by";
2006 PRINT@160,;:C2=Q:M$="Ä":GOSUB8:PRINT@169 ,"darinÄandersen";
2007 PRINT@192,;:C2=Q:M$=CHR$(239):GOSUB8
2008 PRINT@457,"COPYRIGHT 1984"
2009 FORC1=1TO4000:NEXT:POKE49151,0
2100 CLS3
2101 PRINT"ÄÄÄÄÄÄÄÒÛÛÛÛÛÛÄÄÄÄÄÉÉÄ¿ÇÅÉÉÄÇ†É¿";
2102 PRINT"ÄÄÄÄÄÛˇˇˇˇˇˇˇˇˇ˚ÚÄÄÖÄäÜÑÇäÄääÜÄ¿";
2103 PRINT"ÄÄÄÄ˜ˇˇˇˇˇˇˇˇˇˇˇˇÚÄÖÄäãÉäééÄéÇÄÄ";
2104 PRINT"ÄÄÄ˜ˇˇˇˇˇˇˇˇˇˇˇˇˇˇÚáÉàäÄääÑÇäÑÉÄ";
2105 PRINT"ÄÄıˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇˇÄcÄaÄsÄtÄlÄeÄ";
2106 PRINT"ÄÄˇˇˇˇˇˇ¸ˇˇˇˇˇˇˇˇˇˇ˙ÄÄÄÄÄÄÄÄÄÄÄÄ";
2107 PRINT"ÄÒˇˇˇˇˇ¯Ùˇˇˇˇˇˇˇˇˇ˚ÄÄÄÄÄÄÄÄÄÄÄÄ";
2108 PRINT"ÄıˇÙÙı˚ÚÄÒ˜˙¯¯ˇˇˇˇˇˇÄÄÄÄÄÄÄÄÄÄÄÄ";
2109 PRINT"Äıˇ˙¸¸¯Ù¸¸ıˇˇˇˇˇˇÄÄÄÄÄÄÄÄÄÄÄÄ";
2110 PRINT"Äıˇ˙ÄÄÄÄÄÄÄıˇˇˇˇˇˇÄÄÄÄÄÄÄÄÄÄÄÄ";
2111 PRINT"ÛÛˇ˚ÛÛÛÛÛÛÛÛÛÙ¸¸ˇˇ˙ÄÄÄÄÄÄÄÄÄÄÄÄ";
2112 PRINT"Ä˜ˇ˛Òˇ˛¯ÄÄ††˝¿††††††††††††";
2113 PRINT"Ä˜˛¯Òˇ¸Ä††††††£ßØØØØØØØØØØØØØ";
2114 PRINT"ÄÄ††£ßØ´£ØØØØØØØØØØØØØØØØ";
2115 PRINT"ÄÄÄÄ††£ßØØØØØØØØØØØØØØØØØØØØØØØ";
2116 PRINT"ÄÄÄÄ†§≠ØØØØ";:M$="C A S T L E":PRINT@4*Q+20,;:GOSUB7
2121 C2=1:FORC1=1TO63STEP3:N(C2)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):C2=C2+1
2122 NEXT:GOTO48

4000 CLS:PRINTTAB(10)"darkÄcastle":PRINT:PRINT"  YOU LIVE IN THE KINGDOM OF    'TARTH' WHERE A EVIL WIZARD HAS STOLEN ";
4001 PRINT"THE GEM 'NASHAREL', WHICHIS THE KINGDOM'S ONLY DEFENSE   FROM EVIL.  THE WIZARD HAS":?"HIDDEN THE GEM IN A ROOM FAR"
4002 PRINT"REMOVED FROM ANY POSSIBLE HUMAN REACH. THE KINGDOM'S FATE IS IN YOUR HANDS, BRAVE ONE.  BEWARE, THE WIZARD HAS ";
4003 PRINT"PLACED MANY      TREACHEROUS MONSTERS ALONG YOUR PATH.":PRINT:GOSUB72
4004 CLS:PRINTTAB(10)"darkÄcastle":PRINT"  THERE ARE MANY DIFFERENT WAYS TO ACHIEVE YOUR QUEST.  YOU WILLBE ABLE TO ";
4005 PRINT"PURCHASE MANY HELPFULITEMS AT SEVERAL TRADING POSTS  ALONG THE WAY.  (THEY LOOK LIKE THIS ˇœ.)"
4006 PRINT"  YOU ARE GIVEN 200 GOLD PIECES TO BEGIN YOUR JOURNEY.  YOU MAY PURCHASE WHAT YOU WANT, JUST RE-MEMBER THE ";
4007 PRINT"NECESSITIES.  EACH   MONSTER PROTECTS TREASURES OF   IT'S OWN, WHICH YOU MAY STEAL   AND KEEP FOR YOURSELF.  ";
4008 PRINT"(THEY   LOOK LIKE ";
4009 PRINT"THIS "CHR$(145)CHR$(146)" ).":GOSUB 72
4010 CLS:PRINT TAB(10)"darkÄcastle":PRINT:PRINT"   THERE ARE SOME MAGICAL ITEMS WHICH YOU CAN PURCHASE, BUT THEYARE ";
4011 PRINT"QUITE EXPENSIVE.  TELEPORTS CAN TELEPORT YOU TO THE NEAREST TRADING POST.  HEALINGS WILL    BRING YOUR STRENGTH ";
4012 PRINT"TO 100%"
4013 PRINT:PRINT"   ONCE YOU ARE IN A ROOM YOU   CANNOT RETURN OR ESCAPE BACK TO THE KINGDOM.  THE WIZARD IS     TRYING ";
4014 PRINT"TO PROTECT THE GEM BY    PREVENTING YOUR ESCAPE!":GOSUB 72
4015 CLS:PRINTTAB(10)"darkÄcastle":PRINT:PRINT"   TO USE YOUR BOW YOU MUST BE  MOVING IN IN THE DIRECTION YOU  WISH TO ";
4016 PRINT"SHOOT, THEN PRESS <b>.  TO USE YOUR SWORD YOU MUST BE ONTHE VICTIM."
4017 PRINT:PRINT"  DON'T EXPECT THE MONSTERS TO  RUN AWAY,  FOR THEY ARE HIGHLY  SUICIDAL.":PRINT
4018 PRINTTAB(11);:M$="GOOD LUCK!":GOSUB7
4019 GOSUB72:GOTO75

7000 M$="C4D4":GOSUB4:SOUND135,4
7001 M$="F4G4":GOSUB4:SOUND135,4
7002 M$="G8":GOSUB4:SOUND155,4
7003 M$="D4":GOSUB4:SOUND155,8
7004 M$="F4D4F8C4D4":GOSUB4:SOUND135,4
7005 M$="F4G4":GOSUB4:SOUND135,4
7006 M$="G4":GOSUB4:SOUND166,4
7007 M$="G4F4":GOSUB4:SOUND135,4
7008 M$="D4C4C4":GOSUB4:SOUND135,4
7009 M$="G4C4":GOSUB4:SOUND135,4
7010 M$="G4C4":GOSUB4:RETURN

8000 CLS:PRINTTAB(8)"CONGRATULATIONS!!!":PRINT
8001 M$="YOU HAVE MADE IT TO THE GEM ROOM. THE ONLY THING YOU CAN TAKE WITH YOU IS YOUR GOLD.":GOSUB1
8003 M$="THERE IS A SPIDER IN EVERY PART OF THE ROOM, AND THEY WILL DO THEIR BEST TO PROTECT THE GEM.":GOSUB1
8004 M$="USE THE MAP <m> TO LOCATE YOUR POSITION RELATIVE TO THE GEM (THE ROOM IS LARGE).":GOSUB1
8005 M$="ONCE YOU REACH THE GEM YOU WILL BE TELEPORTED OUT OF THE CASTLE.":GOSUB1
8006 PRINT@485,"PRESS <c> TO CONTINUE";:RETURN
8010 REM MC-10 EDITS JIM GERRIE
