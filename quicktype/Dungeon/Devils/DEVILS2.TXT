1 CLS:PRINT"   ** THE DEVIL'S DUNGEON **":PRINT:GOSUB5000
7 PRINT:INPUT"INSTRUCTIONS (Y/N)";M$:IFLEFT$(M$,1)="Y"THENGOSUB3000:REM INSTRUCTIONS
8 GOSUB2000:REM RANDOMIZE
10 DIM R(16),L(65),F(16),X(19),B(16)
20 CLS:PRINT "PLEASE WAIT...":L=1:G=0:E=0:X=16
30 D=1:YS=101:YD=101
40 FOR I=0 TO 65:L(I)=0:NEXT
50 FOR I=1 TO X:N=INT(3*RND(0)+1)
60 IF I=1 THEN N=3
70 FOR J=1 TO N
80 R=INT(64*RND(0)+1)
90 IF L(R)<>0 THEN 80
100 L(R)=I
110 NEXT J
120 R(I)=INT(524287*RND(0)):B(I)=0
130 NEXT I:B(L)=1
135 CLS:PRINT
140 R(1)=24576:FOR I=1 TO 19:X(I)=0:NEXT
145 REM  HAZARDS
150 IF RND(0)<.01 THEN PRINT "TREMOR":FOR I=1 TO 20:L(I)=INT(X)*RND(0)+1:NEXT
160 IF RND(0)<.01 THEN PRINT "TREMOR":FOR I=1 TO 20:L(I)=0:NEXT
170 IF X(1)*X(12)=1 AND RND(0)<.4 THEN PRINT "CURSED!":YD=INT(.5*YD)
180 IF X(9)*X(11)=1 AND RND(0)<.4 THEN PRINT "GASSED!":YS=INT(.5*YS)
190 YD=YD-D
200 YS=YS-D
210 IF YS<=0 OR YD<=0 THEN PRINT "YOU'RE DEAD":END
215 REM  OUTPUT STATUS
220 PRINT"--------------------------------";
230 PRINT "GOLD";G;TAB(12)"EXP";E;TAB(23)"DEPTH"STR$(D)
240 PRINT "YOUR SPEED";YD;TAB(19)"STRENGTH"STR$(YS):GOSUB 250:GOTO 310
245 REM  ADJACENT ROOMS
250 FOR I=1 TO X:F(I)=0:NEXT
260 FOR I=1 TO 64
270 IF L<>L(I) THEN 300
280 IF L(I+1)<>0 AND L(I+1)<>L THEN F(L(I+1))=1
290 IF L(I-1)<>0 AND L(I-1)<>L THEN F(L(I-1))=1
300 NEXT:RETURN
305 REM  CONVERT
310 N=R(L)
320 FOR I=1 TO 19:Q=INT(N/2):X(I)=2*(N/2-Q):N=Q:NEXT I
325 REM  MONSTERS
330 IF X(2)=0 THEN MS=0:GOTO 380
340 IF F=1 THEN 370
350 MS=D*(X(3)+2*X(4)+4*X(5)+L)
360 MD=D*(X(6)+2*X(7)+4*X(8)+L)
370 PRINT"MONSTER'S SPEED"STR$(MD);TAB(19)"STRENGTH"STR$(MS)
380 IF X(1)*X(12)=1 THEN PRINT "DEMONS"
390 IF X(9)*X(11)=1 THEN PRINT "POISONOUS GAS"
395 REM  TREASURE
400 IF X(10)<>1 THEN T=0:GOTO 430
410 T=X(11)+2*X(12)+4*X(13)+1
420 PRINT "MAXIMUM GOLD";T*L*D+1
425 REM  SLIDES AND DROPOFFS
430 S=X(15)+2*X(16)+4*X(17)+8*X(18)+1
440 IF S>X THEN S=1
450 IF S=0 THEN S=1
460 IF X(14)=0 OR S=L THEN 480
470 PRINT "SLIDE TO";S
480 IF X(19)*X(13)=1 THEN PRINT "DROPOFF"
485 REM  INPUT MOVE
490 PRINT "MOVE FROM";L;"TO";
500 FOR I=1 TO X
510 IF F(I)=1 AND I<>L THEN PRINT STR$(I);
520 NEXT I
530 INPUT M:IF M=88 THEN 1000
540 IF M<0 AND X(19)*X(13)=1 THEN D=D+1:F=0:GOTO 40
550 IF M<0 THEN PRINT "NO DROPOFF":GOTO 150
560 IF M>X AND L=1 THEN PRINT "YOU FOUND";G;"GOLD":END
570 IF M<=X THEN 600
575 REM  MAGIC WAND
580 IF RND(0)<.4 THEN PRINT "BACKFIRE":YS=INT(.5*YS):YD=INT(.5*YD):GOTO 150
590 PRINT "WAND WORKS":R(L)=266240:GOTO 220
599 REM  TRADE
600 IF MS>0 THEN 700
610 IF M<>0 OR L<>1 THEN 920
620 PRINT "EXPERIENCE";E:PRINT "SPEED";YD:PRINT "STRENGTH";YS
630 INPUT "ADD SPEED";N
640 IF E-N<0 THEN PRINT "NEED MORE EXPERIENCE":GOTO 620
650 E=E-N:YD=YD+N:PRINT "EXPERIENCE LEFT";E
660 INPUT "ADD STRENGTH";N
670 IF E-N<0 THEN PRINT "NEED MORE EXPERIENCE":GOTO 660
680 E=E-N:YS=YS+N
690 GOTO 220
695 REM  FIGHT
700 F=1
710 IF M>0 THEN 900
720 YH=INT(RND(0)*YS):MH=INT(RND(0)*MS)
730 IF YH>MS THEN YH=MS
740 IF MH>YS THEN MH=YS
750 IF RND(0)*YD>RND(0)*MD THEN 780
760 PRINT "MONSTER ATTACKS":YS=YS-MH:MS=MS-INT(.5*YH)
770 GOTO 800
780 PRINT "YOU ATTACK":MS=MS-YH:YS=YS-INT(.5*MH)
800 E=E+2*YH
810 IF MS<=0 THEN PRINT "MONSTER DEAD":R(L)=R(L)-2:GOTO 150
820 PRINT "monster still alive":GOTO 150
895 REM  RUN
900 IF RND(0)*YD>RND(0)*MD THEN PRINT "ESCAPED":GOTO 970
910 PRINT "MONSTER HIT YOU":YS=YS-INT(.2*MS):GOTO 970
920 IF T=0 THEN 970
930 G1=INT(RND(0)*T*L*D)+1
940 IF X(1)*X(12)=1 AND RND(0)<.4 THEN PRINT "DEMON GOT GOLD":G1=0
950 PRINT "YOU FOUND";G1;"GOLD":G=G+G1:R(L)=R(L)-512
960 E=E+G1
965 REM  MOVE
970 IF F(M)=1 OR M=S THEN L=M:F=0:E=E+D:B(L)=1:GOTO 150
980 IF M THEN PRINT "NOT ADJASCENT"
990 GOTO 150
995 REM  PRINT ROOMS
1000 L1=L:FOR K=1 TO X
1010 IF B(K)<>1 THEN 1070
1020 PRINT K;"--";
1030 L=K:GOSUB 250
1040 FOR J=1 TO X
1050 IF F(J)=1 AND J<>K THEN PRINT J;
1060 NEXT J:PRINT
1070 NEXT K
1080 L=L1:GOTO 220
2000 RA=RND(-(PEEK(9)*256+PEEK(10))):RETURN

3000 CLS:PRINT "IN ROOM #1 ENTER:"
3010 PRINT " 0 TO TRADE EXPERIENCE"
3020 PRINT " 99 TO END ADVENTURE"
3030 PRINT "IN ANY ROOM BUT #1 ENTER:"
3040 PRINT " # TO MOVE TO ADJASCENT ROOM"
3050 PRINT " 0 TO FIGHT MONSTER"
3060 PRINT " -# TO USE A DROPOFF"
3070 PRINT " 99 TO USE YOUR MAGIC WAND"
3080 PRINT:PRINT "HIT ANY KEY TO BEGIN"
3090 I$=INKEY$:IFI$=""THEN3090
4000 RETURN

5000 PRINT"FOR MANY YEARS YOU HAVE HEARD"
5001 PRINT"RUMORS OF LARGE QUANTITIES OF"
5002 PRINT"GOLD HIDDEN IN A MAZE OF CAVES"
5003 PRINT"OF CONNECTING PASSAGEWAYS THAT"
5004 PRINT"LEAD DEEP BENEATH THE EARTH OF"
5005 PRINT"AN OCCASSIONALY ACTIVE VOLCANO."
5006 PRINT"THE STORIES TELL OF MONSTERS"
5007 PRINT"AND DEMONS WHO ROAM THE CAVES,"
5008 PRINT"POISONOUS GAS, TREMORS FROM THE"
5009 PRINT"VOLCANO, A MAN WHO RETUNED FROM"
5010 PRINT"THESE PERILS ALIVE AND NAMED"
5011 PRINT"THE CAVES THE DEVIL'S DUNGEON.":RETURN
5020 REM MC-10 EDITS JIM GERRIE
