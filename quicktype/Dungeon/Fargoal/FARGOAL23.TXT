1 CLS0:CLEAR200,35523:GOSUB3000:GOSUB338
2 GD=0:P1=0:L1=0:R1=50:S=20-LV:IFS<1THENS=1
3 CLS:PRINT@96,TAB(6)"EXPERIENCE PTS"EX:PRINTTAB(6)" EXPERIENCE LV"EL
4 PRINTTAB(6)"  MAXIMUM HITS"TH:PRINTTAB(6)"  BATTLE SKILL"BS:IFDE<LVTHENDE=LV
5 PRINTTAB(6)" DUNGEON LEVEL"LV:PRINTTAB(6)"MONSTERS SLAIN"MK:IFT2=.THENPRINT:GOTO8
6 I=2000-INT(((T+USR(1))-T2)/60):IFI<.THEN324
7 PRINTTAB(6)"TIMER:"I:IFLV=.THEN81
8 FORI=SCTOSC+479:POKEI,223:NEXT
9 FORR=1TO10:W=INT(4*RND(0)+2):X=31-W:X=INT(X*RND(0)):H=INT(4*RND(0)+2):Y=12-H:Y=INT(Y*RND(0)+1):M=SC+X+Y*32
12 R(R)=M+INT(H/2)*32+W/2+33:FORI=1TOH:FORJ=1TOW:POKEM+I*32+J,32:NEXT:NEXT:NEXT
13 PRINTTAB(6)"WAIT...";
14 Z=.:C=32:FORI=1TO10:M=R(I):IFI=5THEN103
15 V=Z
16 Z=RND(4):IFZ=V(V)THEN16
17 F=1:Y=INT(5*RND(0)+5):J=1
18 M=M+D(Z):P=PEEK(M):IFP=223THENF=2
19 IFM<SC+32ORM>SC+448THEN24
20 GOSUB107:IFX<1ORX>30THEN24
21 IFF=2THENIFP=32THENNEXT:GOTO25
22 POKEM,C:J=J+1:IFJ=YTHEN15
23 GOTO18
24 M=M-D(Z):GOTO15
25 F=.:H=.:GOSUB125:IFSR=1THENSR=.:PRINT@422,"YOUR QUEST BEGINS!!":GOTO27
26 PRINT@422,"YOUR QUEST CONTINUES!"
27 GOSUB110:POKEN,38:IFLV=QLTHENIFSF=.THENPOKEN,33
28 R=2:D=2:C=28:GOSUB147:R=5:D=6:C=36:GOSUB147:R=LV:D=3:C=63:GOSUB147
29 NN=INT(3*RND(0)):FORI=.TONN:GOSUB110:N(I)=N:Q(I)=32:GOSUB246:POKEN,B(I):K(I)=1
30 NEXT
31 NM=RND(3):FORI=.TONM:GOSUB110:M(I)=N:P(I)=32:GOSUB236:POKEN,A(I):NEXT
32 IFLV>1ORSF=1THENGOSUB110:POKEN,47
33 GOSUB110:GOSUB140:POKEN,K:O=N-E:POKEO,A:M=O+E:GOSUB112
34 FORI1=1TOTM:IFTZ(I1)=LVTHEN102
35 NEXT

37 PRINT@.,"HP:"HT" EX:"EX" GP:"GP
38 B=ABS(PEEK(2)ANDPEEK(17023)-B0):H=H+1:IFH>STHEN282
39 OND(B)GOTO55,55,55,55,55,55,55,55,98,41,42,43,44,45,46,47,48,49,800:GOTO55
41 ON-(HP>.)GOTO53:GOTO38
42 ON-(TP>.)GOTO52:GOTO38
43 ON-(SH<1)GOTO38:SH=SH-1:S1=1:S$="SHIELD":GOTO94
44 ON-(LG<1)GOTO38:LG=LG-1:L1=1:GOSUB148:S$="LIGHT":GOTO94
45 ON-(RG<1)GOTO38:RG=RG-1:R1=R1/2:S$="REGENERATE":GOTO94
46 SK=1:POKE8,.:SOUND250,1:GOSUB139:GOTO38
47 ON-(IV<1)GOTO38:IV=IV-1:V1=1:S$="INVISIBILITY":GOTO94
48 ON-(L1<1)GOTO38:GOTO91
49 ON-(GP<1)GOTO38:GOTO95

52 TP=TP-1:POKEO,K:GOSUB110:O=N-E:POKEO,A:K=32:GOSUB112:GOSUB115:GOTO38
53 HP=HP-1:X=INT(20*RND(0)+3*LV):HT=HT+X:IFHT>THTHENHT=TH
54 PRINT@.,"HEALING POTION TAKEN!":GOSUB128:GOTO37

55 IFHT<.THEN83
56 SP=SP+1:IFHT<THTHENIFSP>HT/TH*R1THENHT=HT+1:PRINT@.,"HP:"HT;:SP=.
57 B=D(D(B)):IFB=.THEN37
58 IFEX>EPTHENGOSUB88
59 IFR2=1THENR2=.:R1=R1*2
60 POKEO,K:I=O:V=K:O=O+B:K=PEEK(O):IFK=32THEN63
62 IFK=223THENO=I:K=V
63 POKEO,A:M=O+E:GOSUB112:IFL1=1THENGOSUB148
64 IFK=32THENI=PEEK(9)ANDB0:POKE49151,I:POKE49151,B0-I:GOTO37
65 IFSK=1THENSK=.:GOTO38
66 IFK=36THEN153
67 IFK=63THEN165
68 IFK=28THENPRINT@.,"STAIRS GOING DOWN":V=200:W=1:GOSUB82:GOTO37
69 IFK=47THENPRINT@.,"STAIRS GOING UP":V=191:W=-1:GOSUB82:GOTO37
70 IFK=38THEN86
71 IFK>24THENIFK<32ORK=35THENF=1:GOTO256
72 IFK>34THENIFK<44THENF=.:GOTO260
73 IFK=46THEN160
74 IFK=48THEN85
75 IFK=15THEN173
76 IFK=.THENPRINT@.,"CLIMBABLE PIT ABOVE!":GOTO205
77 IFK<>33THEN37

79 PRINT@.,"THE SWORD OF FARGOAL!!":GOSUB120:SF=1:K=32:POKEM,K:IFT2=.THENEX=EX*2:T2=T+USR(1)
80 GOTO205
81 GOSUB82:PRINT@.,"YOUR QUEST IS COMPLETE":POKE8,.:FORJ=1TO3:FORI=1TO15:SOUND160+J*20,1:NEXT:NEXT:GOSUB139:GOTO325

82 FORI=1TO500:NEXT:ON-((PEEK(2)ANDPEEK(17023))>.)GOTO111:FORI=1TO500:NEXT:RETURN

83 IFHP>.THEN53
84 GOSUB82:PRINT@.,"YOU DIED!!":GOTO332
85 PRINT@.,"HOLE IN THE CEILING!!":GOTO205
86 R1=R1/2:R2=1:IFGP>.THENPRINT@.,"SACRIFICE OF GOLD!":EX=EX+GP:GP=.:GOSUB123:GOTO205
87 PRINT@.,"TEMPLE!":GOSUB82:GOTO38
88 EP=EP*2:EL=EL+1:TH=TH+INT(15*RND(0)+5):BS=BS+INT(10*RND(0)+1)
89 PRINT@.,"LEVEL RAISED TO"EL:GOSUB82:RETURN
91 IFL1=1THENL1=2:PRINT@.,"LIGHT OFF":GOSUB82:GOTO38
92 L1=1:PRINT@.,"LIGHT ON":GOSUB82:GOTO38
94 PRINT@.,S$" SPELL CAST!":GOSUB128:GOTO37
95 IFGD>9ORK<>32THEN37
96 GD=GD+1:POKEO+E,46:GZ(GD)=GP:LZ(GD)=O:PRINT@.,"HIDING"GP"GOLD P'S":GP=.:GOSUB82:GOTO38
98 IFK=28THENW=1:EX=EX+INT(10*RND(0)+1)*LV:LV=LV+W:V=200:GOSUB124:K=47:GOTO2
99 IFK=47THENW=-1:LV=LV+W:V=191:GOSUB124:K=28:GOTO2
100 IFK=.THENP1=-1:P2=K:K=15:P$="UP":GOTO174
101 GOTO38
102 TZ(I1)=.:PRINT@.,"TREASURE MAP!!":GOSUB329:POKEO+E,K:I1=TM:GOTO35
103 CLS:PRINT@96,TAB(6)"hEALING POTIONS"HP:PRINTTAB(6)"MAGIC SACKS    "BG:PRINTTAB(6)"SPELLS:"
104 PRINTTAB(6)"  iNVISIBILITY "IV:PRINTTAB(6)"  rEGENERATION "RG:PRINTTAB(6)"  tELEPORT     "TP
105 PRINTTAB(6)"  SHIELD(enter)"SH:PRINTTAB(6)"  DRIFT (space)"FA:PRINTTAB(6)"  lIGHT        "LG
106 PRINTTAB(6)"ENCHANTED WEAPON +"PL:PRINT@422,"WAIT...":GOTO15
107 X=M-SC:Y=INT(X/32):X=INT(X-Y*32):RETURN
108 Z=O-MC:Y=INT(Z/32):X=INT(Z-Y*32):Z=U-SC:YL=INT(Z/32)
109 XL=INT(Z-YL*32):RETURN
110 N=RND(414)+SC+32:P=PEEK(N):IFP<>32THEN110
111 RETURN
112 FORI=1TO8:N=O+D(I):POKEN,PEEK(N+E):NEXT:RETURN

113 POKE8,.:FORI=.TO15:SOUND220-I,1:NEXT:SOUND100,2:GOTO139
115 POKE8,.:FORI=1TO5:SOUND200+I*10,1:NEXT:GOTO139
116 POKE8,.:SOUND161,2:SOUND142,2:SOUND129,4:GOTO139
117 POKE8,.:SOUND161,2:SOUND171,2:SOUND193,4
118 POKE8,.:SOUND181,2:SOUND171,2:SOUND181,4
119 POKE8,.:SOUND171,2:SOUND161,2:SOUND142,4:GOTO139
120 POKE8,.:SOUND142,4:SOUND171,4:SOUND161,2:SOUND171,2:SOUND193,4:SOUND171,2:SOUND161,2
121 SOUND171,4:SOUND161,2:SOUND142,2:SOUND129,4:GOTO139
122 POKE8,.:FORI=1TO3:SOUNDRND(50)+200,1:NEXT:GOTO139
123 POKE8,.:FORI=200TO230STEP10:SOUNDI,1:NEXT:GOTO139
124 POKE8,.:FORI=1TO5:SOUND51-(I*10)*W,1:FORJ=1TOI*10:NEXT:NEXT:GOTO139
125 POKE8,.:SOUND142,4:SOUND171,4:GOTO139
126 POKE8,.:SOUND200,4:SOUND215,4:GOTO139
128 POKE8,.:FORI=1TO5:SOUND225+I*5,I:NEXT:GOTO139
131 ONWGOTO134,135,137,136,137,134,136
132 ONWGOTO136,135,135,133,133,136,133
133 POKE8,.:SOUND223,4:GOTO139
134 POKE8,.:SOUND1,2:SOUND9,2:GOTO139
135 POKE8,.:FORI=1TO3:SOUND150,1:NEXT:GOTO139
136 POKE8,.:SOUND1,4:GOTO139
137 POKE8,.:FORI=1TO3STEP-1:SOUND1+I*3,1:NEXT
138 FORI=3TO1STEP-1:SOUND12-I*3,1:NEXT
139 POKE8,8:T=T+20:RETURN

140 J=MC+479:Y=.:FORI=MCTOMC+240:POKEI,B0:POKEJ-Y,B0:Y=Y+1:NEXT:RETURN

142 B=ABS(PEEK(2)ANDPEEK(17023)-B0):RETURN

145 FR=.:FORI=1TO99:IFPEEK(2)ANDPEEK(17023)=32THENFR=1:I=99
146 NEXT:RETURN

147 X=INT(R*RND(0)+D):FORI=1TOX:GOSUB110:POKEN,C:NEXT:RETURN
148 FORI=1TO8:N=O+D(I):IFPEEK(N)=223THENNEXT:RETURN
149 N=N+D(I):POKEN,PEEK(N+E):NEXT:RETURN
153 X=INT(20*RND(0)+10*LV):PRINT@.,"TREASURE: "X"GP'S":GOSUB122:IFGP+X>T1THEN155
154 GP=GP+X:K=32:POKEM,K:GOSUB82:GOTO38
155 IFGD>9THEN37
156 GD=GD+1:K=46:POKEM,K:GZ(GD)=GP+X-T1:LZ(GD)=O
157 GOSUB82:IFGP=T1THEN159
158 GP=T1:PRINT@.,"CAN'T CARRY MORE GOLD":GOTO205
159 PRINT@.,"HIDING THE GOLD":GOTO205
160 PRINT@.,"HIDDEN TREASURE!!":GOSUB122:FORJ=1TOGD:IFO=LZ(J)THENV=J:J=GD
161 NEXT
162 Z=GZ(V):IFGP+Z>T1THEN164
163 GP=GP+Z:K=32:POKEM,K:GOTO205
164 GZ(V)=GP+Z-T1:GP=T1:GOSUB82:PRINT@.,"GOLD TOO HEAVY":GOTO205
165 R=INT(9*RND(0)+1):ONRGOTO172,177,166,178,180,180,180,180,180
166 PRINT@.,"EXPLOSION!!":FORJ=1TO20:IFJ/2=INT(J/2)THENPOKEO,207:GOTO168
167 POKEO,A:POKE49151,64:FORK=1TO25:NEXT
168 POKE49151,.:NEXT
169 IFS1=.THENHT=HT-INT(15*RND(0)+LV):CL=1:GOTO171
170 S1=.:PRINT@.,"SHIELDED FROM BLAST!":GOSUB82
171 K=32:POKEM,K:POKEO,A:GOTO206
172 P1=.:PRINT@.,"PIT!!...YOU FELL!":H1=INT(10*RND(0)+LV):K=15:POKEM,K:POKEO,K:CL=1:GOSUB113:GOTO194
173 P$="DOWN":P1=1:P2=K:K=.:PT=INT(4*RND(0)+2)
174 X=PT*P1:LV=LV+X:PRINT@.,"CLIMBING THE PIT...":Y=INT(2*RND(0)):GOSUB82:POKEO,P2
175 IFY=1THENPRINT@.,"YOU FELL!":H1=INT(10*RND(0)+3*PT+LV):GOSUB113:GOTO193
176 PRINT@.,P$;ABS(X)"LEVELS":EX=EX+INT(10*RND(0)+PT*5):GOSUB82:GOTO2
177 PRINT@.,"CEILING TRAP!":H1=INT(10*RND(0)+LV):K=48:POKEM,K:POKEO,K:GOSUB116:CL=1:GOTO194
178 PRINT@.,"TELEPORT...":K=32:POKEM,K:POKEO,K:GOSUB110:O=N-E:POKEO,A
179 GOSUB112:GOSUB115:GOSUB82:CL=1:GOTO206
180 EX=EX+INT(50*RND(0)+LV):K=32:POKEM,K:X=RND(14):GOSUB126
181 ONXGOTO183,184,183,182,184,185,190,186,187,188,192,185,190,191
182 PRINT@.,"REGENERATION SPELL!!":RG=RG+1:GOTO205
183 PRINT@.,"HEALING POTION!!":HP=HP+1:GOTO205
184 PRINT@.,"MAGIC SACK!!":BG=BG+1:T1=T1+100:GOTO205
185 PRINT@.,"SHIELD SPELL!!":SH=SH+1:GOTO205
186 PRINT@.,"LIGHT SPELL!!":LG=LG+1:GOTO205
187 PRINT@.,"ENCHANTED WEAPON!!":PL=PL+1:BS=BS+INT(10*RND(0)+5):GOTO205
188 X=INT(8*RND(0)+LV+3):PRINT@.,"MAP TO"X"TH LEVEL!!"
189 TM=TM+1:TZ(TM)=X:GOTO205
190 PRINT@.,"TELEPORT SPELL!!":TP=TP+1:GOTO205
191 PRINT@.,"DRIFT SPELL!!":FA=FA+1:GOTO205
192 PRINT@.,"INVISIBILITY SPELL!!":IV=IV+1:GOTO205
193 IFP1<1THENLV=LV-X:K=.
194 GOSUB145:IFFR=1THEN196
195 GOTO202
196 F1=.:IFR=2THEN200
197 IFFA=.THEN202
198 FA=FA-1:PRINT@.,"LIKE A FEATHER...":GOSUB82:CL=.:GOTO203
200 IFTP=.THEN202
201 PRINT@.,"TELEPORT TO SAFETY!":GOSUB82:CL=.:GOTO52
202 HT=HT-H1
203 IFP1=1THEN2
204 GOTO206
205 FORJ=1TO1000:NEXT
206 IFCL=.ORINT(4*RND(0))>.THEN208
207 PRINT@.,"LOST YOUR MAP!":GOSUB140:POKEO,A:GOSUB112:GOSUB82
208 CL=.:GOTO37
209 POKE8,.:FORI1=1TO5:SOUND1,1:NEXT:FORI1=1TO5:SOUND100,1:NEXT:GOSUB139
211 M=O+E:IFF=.THEN214
212 SZ=S(J):IFSZ=.THEN234
213 HZ=I(J):D=.:X$=X$(J):GOTO216
214 SZ=T(J):IFSZ=.THEN233
215 HZ=J(J):D=1:X$=Y$(J)
216 PRINT@.,"YOU ARE ATTACKED BY":GOSUB82:PRINT@.,X$:GOSUB145:IFFR=1THEN230
217 X1=SZ/BS:H1=HZ
218 IFS1=1THEN220
219 HT=HT-INT(X1*4*LV*RND(0)+1)
220 IFD=1AND(GP>.ORSF=1)THENIFX1<.5ORX1>1ORC=42THEN225
221 HZ=HZ-INT(1/X1*4*LV*RND(0)+1+PL):IFHZ<.THEN228
222 IFHT<-5THENPRINT@.,"SLAIN BY ":GOSUB82:PRINT@.,X$:GOTO332
223 PRINT@.,"HP:"HT,:GOSUB291:GOSUB145:IFFR=1THEN230
224 GOTO218
225 IFSF=1THENSF=.:QL=LV:PRINT@.,"THE SWORD IS STOLEN!!":GOSUB82:U=.:RETURN
226 D=-1:P=A:PRINT@.,"YOUR GOLD IS STOLEN!!":GOSUB82
227 G(J)=GP:GP=.:GOTO304
228 S1=.:EX=EX+(SZ+H1)*LV:BS=BS+INT(5*RND(0)+1):MK=MK+1:K=32
229 U=.:PRINT@.,"YOU VANQUISHED":GOSUB82:PRINT@.,X$:GOSUB82:RETURN
230 IFTP=.THEN217
231 K=C:POKEM,K:IFFTHENM(J)=M:Q=1:RETURN
232 N(J)=M:Q=1:RETURN
233 PRINT@.,"THE MAGE TAKES YOUR":GOSUB82:PRINT@.,"MAGIC SPELLS!!":IV=.:RG=.:TP=.:SH=.:FA=.:LG=.:GOTO235
234 PRINT@.,"THE DEMON DRAINS YOUR":GOSUB82:PRINT@.,"EXPERIENCE LEVEL!!":EX=INT(EX/2):IFEL>1THENEL=EL-1
235 U=.:GOSUB82:RETURN
236 S$="":HZ=.:SZ=.:FORJ=1TO2+INT(LV*.25)
237 SZ=SZ+INT(4*RND(0)+LV):HZ=HZ+INT(6*RND(0)+LV*1.5):NEXT:X=SZ/BS:Y=INT(X*5)
238 IFY>6THENS$="A POWER ":GOTO240
239 IFY<1THENS$="A WEAK "
240 X=INT(4*RND(0)+LV/2):IFX<10THEN243
241 R=INT(6*RND(0)):IFR>.THENX=10-R:GOTO243
242 S(I)=.:A(I)=25:RETURN
243 A(I)=X(X):IFS$=""THENS$=A$(X)+" "
244 X$(I)=S$+B$(X):SZ=SZ+INT(X*RND(0)+X):HZ=HZ+INT(X*RND(0)+X)
245 S(I)=SZ:I(I)=HZ:RETURN
246 S$="":HZ=.:SZ=.:FORJ=1TO3+INT(LV*.25)
247 SZ=SZ+INT(3*RND(0)+LV*1.5):HZ=HZ+INT(4*RND(0)+LV):NEXT:X=SZ/BS:Y=INT(X*5)
248 IFY>6THENS$="AN EXPERT ":GOTO250
249 IFY<1THENS$="AN INFERIOR "
250 X=INT(4*RND(0)+LV/2):IFX<10THEN253
251 R=INT(6*RND(0)):IFR>.THENX=10-R:GOTO253
252 T(I)=.:B(I)=37:RETURN
253 B(I)=Y(X):IFS$=""THENS$=C$(X)+" "
254 Y$(I)=S$+D$(X):SZ=SZ+INT(X*RND(0)+X):HZ=HZ+INT(X*RND(0)+X)
255 T(I)=SZ:J(I)=HZ:RETURN
256 V=-1:FORJ=.TONM:IFM=M(J)THENV=J:J=NM
257 NEXT:IFV<.ORV>4THENV=4
258 SZ=S(V):IFSZ=.THENGOSUB234:K=P(V):POKEM,K:M(V)=.:GOTO37
259 HZ=I(V):D=.:X$=X$(V):GOTO264
260 V=-1:FORJ=.TONN:IFM=N(J)THENV=J:J=NN
261 NEXT:IFV<.ORV>3THENV=3
262 SZ=T(V):IFSZ=.THENGOSUB233:K=Q(V):POKEM,K:N(V)=.:GOTO37
263 HZ=J(V):D=K(V):X$=Y$(V)
264 PRINT@.,X$:GOSUB125:GOSUB142:IFD(B)>.THENIFD(B)<9THEN39
265 X=SZ/BS:H1=HZ
266 HZ=HZ-INT(1/X*4*LV*RND(0)+1+PL):IFHZ<.THEN272
267 IFS1=1THEN270
268 HT=HT-INT(X*4*LV*RND(0)+1)
269 IFHT<-5THENPRINT@.,"THOU ART SLAIN!":GOTO332
270 IFF=.THEN278
271 I(V)=HZ:GOTO279
272 V1=.:S1=.:POKEO,A:EX=EX+(SZ+H1)*LV:BS=BS+INT(5*RND(0)+1):MK=MK+1
273 PRINT@.,"YOU HAVE SLAIN ":GOSUB126
274 PRINT@.,X$:IFFTHENK=P(V):POKEM,K:M(V)=.:GOTO205
275 K=Q(V):POKEM,K:N(V)=.:IFD>.THEN205
276 GOSUB82:PRINT@.,"FOUND YOUR"G(V)"GOLD!!":GP=GP+G(V)
277 GOTO205
278 J(V)=HZ
279 PRINT@.,"HP:"HT,:C=K:GOSUB888:GOSUB291:GOSUB82:GOSUB142:IFD(B)>.THENIFD(B)<9THENS1=.:GOTO39
280 GOTO266

282 T=T+USR(1):I=USR(0):H=.:D=1:F=1:Q=.:FORJ=.TONM:U=M(J):IFU=.THENNEXT:GOTO286
283 P=P(J):C=A(J):GOSUB293:IFQTHENJ=NM:NEXT:GOTO52
285 P(J)=P:M(J)=U:NEXT
286 F=.:FORJ=.TONN:U=N(J):IFU=.THENNEXT:GOTO37
287 D=K(J):P=Q(J):C=B(J):IFC=40THENC=32:IFL1=1THENC=37
288 GOSUB293:IFQTHENJ=NN:NEXT:GOTO52
290 K(J)=D:Q(J)=P:N(J)=U:NEXT:GOTO37

291 W=RND(7):IFFTHENPRINTS$(W):GOSUB131:RETURN
292 PRINTT$(W):GOSUB132:RETURN

293 F2=.:GOSUB108:Z1=ABS(X-XL):Z2=ABS(Y-YL):IFZ1>9ORZ2>9THENRETURN
294 IFK=38THEN296
295 IFV1=.ORL1=1THEN297
296 X=RND(30):Y=RND(14)
297 DX=.:DY=.:F3=.:Z=.:W=P:L=U-E:IFC=35THEN319
299 IFX<XLTHENDX=-1:GOTO301
300 IFX>XLTHENDX=1
301 IFY<YLTHENDY=-32:GOTO303
302 IFY>YLTHENDY=32
303 Z=DX+DY:POKEU,P:K2=PEEK(L)
304 IFK2<>B0THENPOKEL,P
305 P=PEEK(U+Z*D):IFP<223THENIFP<24ORP=32ORP=28ORP=36ORP>43THEN311
307 IFXL=XORYL=YTHEN310
308 IFF3=.THENZ=Z-DX:F3=1:GOTO305
309 IFF3=1THENZ=Z+DX-DY:F3=2:GOTO305
310 P=W:Z=.:IFF2<5THENF2=F2+1:GOTO296
311 U=U+Z*D:L=L+Z*D:K1=PEEK(L)
312 IFK1=ATHENP=K:GOTO209
313 IFP=36THENIFF=.THENP=32:GOTO317
314 IFP=63THENP=32:GOTO317
315 IFD=-1THENIFK1=B0THENU=.:RETURN
316 IFP=15THENIFFTHENU=.:RETURN
317 POKEU,C:IFK1=B0THENRETURN
318 POKEL,C:RETURN

319 IFZ1>3ORZ2>3ORZ1=1ANDZ2=1THEN299
320 FORI1=1TO4:R=RND(8):M3=O+D(R):K2=PEEK(M3):IFK2=32THENI1=4:NEXT:GOTO322
321 NEXT:GOTO299
322 POKEU,P:U=M3+E:K2=PEEK(L):IFK2<>B0THENPOKEL,P
323 P=32:L=M3:GOTO317

324 PRINTTAB(6)"OUT OF TIME!":FORI=1TO4000:NEXT
325 CLS:PRINT@96,TAB(7)"YOUR SCORES:":PRINTTAB(7)"  EXPERIENCE"EX:PRINTTAB(7)"       LEVEL"EL:PRINTTAB(7)"  DEEPEST LV"DE
327 PRINTTAB(7)"    MONSTERS"MK:PRINTTAB(7)"  HEALTH:"HT"HP":PRINTTAB(7)"   SKILL:"BS:GOSUB999
328 PRINT:PRINTTAB(7)"PLAY AGAIN? ";:T=T+USR(1):POKE8,.:GOTO334
329 J=MC+479:Y=.:FORI=MCTOMC+240:POKEI,PEEK(I+E):POKEJ-Y,PEEK(J-Y+E):Y=Y+1:NEXT:RETURN
332 GOSUB82:PRINT@.,"EXPERIENCE LEVEL:"EL:GOSUB82:PRINT@.,"DUNGEON LEVEL:"LV:GOSUB118
333 GOSUB329:PRINT@.,"":PRINT@.,"AGAIN? ";
334 A$=INKEY$:IFA$=""THEN334
335 IFA$="Y"THENPRINT"YES";:FORI=1TO200:NEXT:RUN
336 IFA$="N"THENPRINT:GOSUB999:END
337 GOTO334
338 E=19108:MC=16416
340 CLS:PRINT@32,"   THIS PORT IS A TRIBUTE TO":PRINT" THE VIC-20 / COMMODORE 64 GAME":PRINT"    AND HAS BEEN AUTHORIZED"
341 PRINT"    BY THE ORIGINAL AUTHOR.":PRINT@32*7,TAB(5)"THE SWORD OF FARGOAL!!":PRINT@32*8,TAB(5)"ßßßßßßßßßßßßßßßßßßßßßß"
342 PRINT@328,"BY: JEFF MCCORD":PRINTTAB(4)"(C) COPYRIGHT 1982-2020":GOSUB82:GOSUB120:PRINT@488,"PRESS h FOR HELP";
343 DIMM(4),N(3),P(4),Q(3),I(4),J(3),K(3),T(3),S(4),V(4),D(128),I,J,T,D,C,P,Q,R,U,F,A(4),B(3),K,M,W,Z,F2,Z1,Z2,V1,L1
344 DIMDX,DY,F3,L,XL,YL,K1,K2,B,H,S:V(1)=2:V(2)=1:V(3)=4:V(4)=3:LV=1:EL=1:FORI=1TO8:READD(I):NEXT:A=24:B0=128
345 FORI=1TO19:READX:D(X)=I:NEXT:X=.:D(83)=3
346 FORI=1TO3:HT=HT+INT(6*RND(0)+1):BS=BS+INT(6*RND(0)+1):NEXT
347 SR=1:TH=HT:EP=200:K=32:HP=1:TP=1:T1=100:QL=INT(5*RND(0)+15)
348 FORI=0TO9:READA$(I),B$(I),X(I),C$(I),D$(I),Y(I):NEXT:FORI=1TO7:READS$(I),T$(I):NEXT
349 IFINKEY$="H"THENGOSUB4000
350 RETURN
800 PRINT@.,"QUIT! ARE YOU SURE?"
801 A$=INKEY$:IFA$="Y"THEN332
802 IFA$="N"THEN38
803 GOTO801
888 IFC=40THENC=32
889 RETURN
999 PRINTTAB(7)"QUEST TOOK"INT(T/3600)"MINUTES":RETURN

3000 X=RND(-(PEEK(9)*256+PEEK(10))):X=35524+512:Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:SC=X-512:X=USR(0):RETURN
3001 READ A$:FORY=1TOLEN(A$)STEP2:POKEZ,(ASC(MID$(A$,Y,1))-65)*16+ASC(MID$(A$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
3350 DATA1,-1,32,-32,-31,31,-33,33
3351 DATA68,65,88,87,69,90,81,67,71,72,84,13,76,82,80,73,79,66,75
3354 DATAA,DIRE WOLF,31,A,ROGUE,42
3355 DATAAN,OGRE,25,A,BARBARIAN,37,A,HOBGOBLIN,25,AN,ELVIN RANGER,37,A,WEREBEAR,25
3356 DATAA,DWARVEN GUARD,43,A,GARGOYLE,25,A,MERCENARY,37,A,TROLL,25,A,SWORDSMAN,37
3357 DATAA,WYVERN,30,A,MONK,37,A,DIMENSION SPIDER,35,A,DARK WARRIOR,37,A,SHADOW DRAGON,30
3358 DATAAN,ASSASSIN,40,A,FYRE DRAKE,30,A,WAR LORD,37,CRUNCH,CLANG,CLAW,OUCH!,GNARL,SLASH
3359 DATAUGH!,CLINK,GROWL!,CHOP,SHRED,THUD,THUMP,SHRIEK!
4000 CLS
4010 PRINT"     MC-10 SWORD OF FARGOAL":PRINT:PRINT"     WHAT GAMMAQUEST II MAY":PRINT"     HAVE BEEN LIKE ON THE"
4030 PRINT"     COMMODORE PET":PRINT:PRINT"     MC-10 EDITS JIM GERRIE":PRINT:PRINT"     wasd, qwe, zxc TO MOVE"
4060 PRINT"     k=QUIT GAME, p=PASS BY,":PRINT"     g=GO STAIRS/PITS, b=BURY":PRINT"     GOLD, o=ON/OFF LIGHT"
4070 PRINT"     SPELL, SWORD STOLEN=":PRINT"     RETURN TO CURRENT LEVEL":PRINT@491,"HIT ANY KEY";
4090 IFINKEY$=""THEN4090
5000 RETURN
