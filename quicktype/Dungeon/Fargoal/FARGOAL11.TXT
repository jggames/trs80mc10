1 CLS0:CLEAR200,35523:GOSUB3000:GOSUB338
2 GD=0:P1=0:L1=0:R1=50:S=20-L:IFS<1THENS=1
3 CLS:PRINT@96,TAB(6)"EXPERIENCE PTS"E:PRINTTAB(6)" EXPERIENCE LV"EL
4 PRINTTAB(6)"  MAXIMUM HITS"TH:PRINTTAB(6)"  BATTLE SKILL"BS:IFDE<LTHENDE=L
5 PRINTTAB(6)" DUNGEON LEVEL"L:PRINTTAB(6)"MONSTERS SLAIN"MK:IFT2=0THENPRINT:GOTO8
6 T3=2000-INT((USR(1)-T2)/60):IFT3<0THEN324
7 PRINTTAB(6)"TIMER:"T3:IFL=0THEN81
8 FORI=SCTOSC+479:POKEI,223:NEXT
9 FORR=1TO10
10 W=INT(4*RND(0)+2):X=31-W:X=INT(X*RND(0)):H=INT(4*RND(0)+2):Y=12-H
11 Y=INT(Y*RND(0)+1):M=SC+X+Y*32
12 R(R)=M+INT(H/2)*32+W/2+33:FORI=1TOH:FORJ=1TOW:POKEM+I*32+J,32:NEXTJ,I,R
13 PRINTTAB(6)"WAIT...";
14 Z=.:C=32:FORI=1TO10:M=R(I):IFI=5THEN103
15 V=Z
16 Z=RND(4):IFZ=V(V)THEN16
17 F=1:Y=INT(5*RND(0)+5):J=1
18 M=M+D(Z):P=PEEK(M):IFP=223THENF=2
19 IFM<SC+32ORM>SC+448THEN24
20 GOSUB107:IFX<1ORX>30THEN24
21 IFF=2THENIFP=32THEN25
22 POKEM,C:J=J+1:IFJ=YTHEN15
23 GOTO18
24 M=M-D(Z):GOTO15
25 NEXT:GOSUB125:IFSR=1THENSR=0:PRINT@422,"YOUR QUEST BEGINS!!":GOTO27
26 PRINT@422,"YOUR QUEST CONTINUES!"
27 GOSUB110:POKEN,38:IFL=QLTHENIFSF=.THENPOKEN,33
28 R=2:D=2:C=28:GOSUB147:R=5:D=6:C=36:GOSUB147:R=L:D=3:C=63:GOSUB147
29 NN=INT(3*RND(0)):FORI=.TONN:GOSUB110:NZ(I)=N:QZ(I)=32:GOSUB246:POKEN,B(I):DZ(I)=1
30 NEXT
31 NM=INT(3*RND(0)+1):FORI=.TONM:GOSUB110:MZ(I)=N:PZ(I)=32:GOSUB236:POKEN,A(I):NEXT
32 IFL>1ORSF=1THENGOSUB110:POKEN,47
33 GOSUB110:GOSUB140:POKEN,K:O=N-DZ:POKEO,A:GOSUB112
34 FORI1=1TOTM:IFTZ(I1)=LTHEN102
35 NEXT
36 PRINT@.,""
37 PRINT@.,"HP:"HT"  EX:"E"  GP:"T
38 B=PEEK(2)ANDPEEK(17023):A$=CHR$(B):A1=A1+1:IFA1>STHENA1=.:GOTO282
39 IFB=.ORB=65ORB=68ORB=83ORB=87THEN55
40 IFA$="C"THEN98
41 IFA$="H"THENIFHP>.THEN53
42 IFA$="T"THENIFTP>.THEN52
43 IFA$="E"THENIFSH>.THENSH=SH-1:S1=1:S$="SHIELD":GOTO94
44 IFA$="L"THENIFLG>.THENLG=LG-1:L1=1:GOSUB148:S$="LIGHT":GOTO94
45 IFA$="R"THENIFRG>.THENRG=RG-1:R1=R1/2:S$="REGENERATE":GOTO94
46 IFA$="P"THENSK=1
47 IFA$="I"THENIFIV>.THENIV=IV-1:V1=1:S$="INVISIBILITY":GOTO94
48 IFA$="O"THENIFL1>.THEN91
49 IFA$="B"THENIFT>.THEN95
50 IFA$="Q"THEN800
51 GOTO38
52 TP=TP-1:POKEO,K:GOSUB110:O=N-DZ:POKEO,A:K=32:GOSUB112:GOSUB115:GOTO38
53 HP=HP-1:X=INT(20*RND(0)+3*L):HT=HT+X:IFHT>THTHENHT=TH
54 GOSUB93:PRINT@.,"HEALING POTION TAKEN!":GOSUB128:GOTO36
55 IFHT<.THEN83
56 SP=SP+1:IFHT<THTHENIFSP>HT/TH*R1THENHT=HT+1:PRINT@.,"       ";:SP=0
57 GOSUB143:IFB=.THEN37
58 IFE>EPTHENGOSUB88
59 IFR2=1THENR2=.:R1=R1*2
60 POKEO,K:Q=O:V=K:O=O+B
61 K=PEEK(O):IFK=32THEN63
62 IFK=223THENO=Q:K=V
63 POKEO,A:M=O+DZ:GOSUB112:IFL1=1THENGOSUB148
64 IFK=32THENI=PEEK(9)AND128:POKE49151,I:POKE49151,128-I:GOTO37
65 IFSK=1THENSK=.:GOTO38
66 GOSUB93:IFK=36THEN153
67 IFK=63THEN165
68 IFK=28THENPRINT@.,"STAIRS GOING DOWN":V=200:W=1:GOSUB127:GOTO36
69 IFK=47THENPRINT@.,"STAIRS GOING UP":V=191:W=-1:GOSUB127:GOTO36
70 IFK=38THEN86
71 IFK>24ANDK<32ORK=35THENM2=1:GOTO256
72 IFK>34THENIFK<44THENM2=.:GOTO260
73 IFK=46THEN160
74 IFK=48THEN85
75 IFK=15THEN173
76 IFK=.THENPRINT@.,"CLIMBABLE PIT ABOVE!":GOTO205
77 IFK=33THEN79
78 GOTO37
79 PRINT@.,"THE SWORD OF FARGOAL!!":GOSUB120:SF=1:K=32:POKEM,K:IFT2=0THENE=E*2:T2=USR(1)
80 GOTO205
81 GOSUB82:PRINT@.,"YOUR QUEST IS COMPLETE":GOTO325
82 FORI=1TO1000:NEXT:GOTO93
83 IFHP>.THEN53
84 GOSUB82:PRINT@.,"YOU DIED!!":GOTO332
85 PRINT@.,"HOLE IN THE CEILING!!":GOTO205
86 R1=R1/2:R2=1:IFT>.THENPRINT@.,"SACRIFICE OF GOLD!":E=E+T:T=.:GOSUB123:GOTO205
87 PRINT@.,"TEMPLE!":GOSUB82:GOTO38
88 EP=EP*2:EL=EL+1:TH=TH+INT(15*RND(0)+5):BS=BS+INT(10*RND(0)+1)
89 GOSUB93:PRINT@.,"LEVEL RAISED TO"EL:FORJ=1TO2200:NEXT:GOSUB93
90 RETURN
91 GOSUB93:IFL1=1THENL1=2:PRINT@.,"LIGHT OFF":GOSUB82:GOTO38
92 L1=1:PRINT@.,"LIGHT ON":GOSUB82:GOTO38
93 PRINT@.,"":RETURN
94 GOSUB93:PRINT@.,S$" SPELL CAST!":GOSUB128:GOTO36
95 IFGD>9ORK<>32THEN37
96 GD=GD+1:POKEO+DZ,46:GZ(GD)=T:LZ(GD)=O:GOSUB93
97 PRINT@.,"HIDING"T"GOLD P'S":T=.:GOSUB82:GOTO38
98 IFK=28THENW=1:E=E+INT(10*RND(0)+1)*L:L=L+W:V=200:GOSUB124:K=47:GOTO2
99 IFK=47THENW=-1:L=L+W:V=191:GOSUB124:K=28:GOTO2
100 IFK=.THENP1=-1:P2=K:K=15:P$="UP":GOTO174
101 GOTO38
102 TZ(I1)=.:GOSUB93:PRINT@.,"TREASURE MAP!!":GOSUB329:POKEO+DZ,K:I1=TM:GOTO35
103 CLS:PRINT@96,TAB(6)"hEALING POTIONS"HP:PRINTTAB(6)"MAGIC SACKS    "BG:PRINTTAB(6)"SPELLS:"
104 PRINTTAB(6)"  iNVISIBILITY "IV:PRINTTAB(6)"  rEGENERATION "RG:PRINTTAB(6)"  tELEPORT     "TP
105 PRINTTAB(6)"  SHIeLD       "SH:PRINTTAB(6)"  DRIFT (space)"FA:PRINTTAB(6)"  lIGHT        "LG
106 PRINTTAB(6)"ENCHANTED WEAPON +"PL:PRINT@422,"WAIT...":GOTO15
107 X=M-SC:Y=INT(X/32):X=INT(X-Y*32):RETURN
108 Z=O-MC:Y=INT(Z/32):X=INT(Z-Y*32):Z=MZ-SC:YL=INT(Z/32)
109 XL=INT(Z-YL*32):RETURN
110 N=RND(384)+SC+32:P=PEEK(N):IFP<>32THEN110
111 RETURN
112 FORI=1TO8:N=O+D(I):POKEN,PEEK(N+DZ):NEXT:RETURN

113 POKE8,.:FORI=0TO15:SOUND220-I,1:NEXT:SOUND100,2:GOTO139
115 POKE8,.:FORI=1TO5:SOUND200+I*10,1:NEXT:GOTO139
116 POKE8,.:SOUND161,2:SOUND142,2:SOUND129,4:GOTO139
117 POKE8,.:SOUND161,2:SOUND171,2:SOUND193,4
118 POKE8,.:SOUND181,2:SOUND171,2:SOUND181,4
119 POKE8,.:SOUND171,2:SOUND161,2:SOUND142,4:GOTO139
120 POKE8,.:SOUND142,4:SOUND171,4:SOUND161,2:SOUND171,2:SOUND193,4:SOUND171,2:SOUND161,2
121 SOUND171,4:SOUND161,2:SOUND142,2:SOUND129,4:GOTO139
122 POKE8,.:FORI=1TO3:SOUNDRND(50)+200,1:NEXT:GOTO139
123 POKE8,.:FORI=200TO230STEP10:SOUNDI,1:NEXT:GOTO139
124 POKE8,.:FORI=1TO5:SOUND51-(I*10)*W,1:FORJ=1TOI*10:NEXT:NEXT:GOTO139
125 POKE8,.:SOUND142,4:SOUND171,4:GOTO139
126 POKE8,.:SOUND200,4:SOUND215,4:GOTO139
127 FORI=1TO1000:NEXT:RETURN
128 POKE8,.:FORI=1TO5:SOUND225+I*5,I:NEXT:GOTO139
131 ONWGOTO134,135,137,136,137,134,136
132 ONWGOTO136,135,135,133,133,136,133
133 POKE8,.:SOUND223,4:GOTO139
134 POKE8,.:SOUND1,2:SOUND9,2:GOTO139
135 POKE8,.:FORI=1TO3:SOUND150,1:NEXT:GOTO139
136 POKE8,.:SOUND1,4:GOTO139
137 POKE8,.:FORI=1TO3STEP-1:SOUND1+I*3,1:NEXT
138 FORI=3TO1STEP-1:SOUND12-I*3,1:NEXT
139 POKE8,8:RETURN

140 J=MC+479:Y=.:FORI=MCTOMC+240:POKEI,128:POKEJ-Y,128:Y=Y+1:NEXT:RETURN

142 A$=CHR$(PEEK(2)ANDPEEK(17023))
143 B=(A$="A")-(A$="D")-(A$="S")*32+(A$="W")*32:RETURN

145 FR=.:FORI=1TO105:IFPEEK(2)ANDPEEK(17023)=32THENFR=1:I=300
146 NEXT:RETURN
147 X=INT(R*RND(0)+D):FORI=1TOX:GOSUB110:POKEN,C:NEXT:RETURN
148 FORI=1TO8:N=O+D(I):X=PEEK(N):IFX=223THEN150
149 N=N+D(I):POKEN,PEEK(N+DZ)
150 NEXT:RETURN
151 N=O+F(I):POKEN,PEEK(N+DZ)
152 NEXT:GOTO64
153 X=INT(20*RND(0)+10*L):PRINT@.,"TREASURE: "X"GP'S":GOSUB122:IFT+X>T1THEN155
154 T=T+X:K=32:POKEM,K:GOSUB82:GOTO38
155 IFGD>9THEN36
156 GD=GD+1:K=46:POKEM,K:GZ(GD)=T+X-T1:LZ(GD)=O
157 FORI=1TO1000:NEXT:IFT=T1THEN159
158 T=T1:PRINT@.,"CAN'T CARRY MORE GOLD":GOTO205
159 PRINT@.,"HIDING THE GOLD":GOTO205
160 PRINT@.,"HIDDEN TREASURE!!":GOSUB122:FORJ=1TOGD:IFO=LZ(J)THENV=J:J=GD
161 NEXT
162 Z=GZ(V):IFT+Z>T1THEN164
163 T=T+Z:K=32:POKEM,K:GOTO205
164 GZ(V)=T+Z-T1:T=T1:GOSUB82:PRINT@.,"GOLD TOO HEAVY":GOTO205
165 R=INT(9*RND(0)+1):ONRGOTO172,177,166,178,180,180,180,180,180
166 PRINT@.,"EXPLOSION!!":FORJ=1TO20:IFJ/2=INT(J/2)THENPOKEO,207:GOTO168
167 POKEO,A:POKE49151,64:FORK=1TO25:NEXT
168 POKE49151,.:NEXT
169 IFS1=.THENHT=HT-INT(15*RND(0)+L):CL=1:GOTO171
170 S1=.:PRINT@.,"SHIELDED FROM BLAST!":GOSUB82
171 K=32:POKEM,K:POKEO,A:GOTO206
172 P1=.:PRINT@.,"PIT!!...YOU FELL!":H1=INT(10*RND(0)+L):K=15:POKEM,K:POKEO,K:CL=1:GOSUB113:GOTO194
173 P$="DOWN":P1=1:P2=K:K=0:PT=INT(4*RND(0)+2)
174 X=PT*P1:L=L+X:GOSUB93:PRINT@.,"CLIMBING THE PIT...":Y=INT(2*RND(0)):GOSUB82:POKEO,P2
175 IFY=1THENPRINT@.,"YOU FELL!":H1=INT(10*RND(0)+3*PT+L):GOSUB113:GOTO193
176 PRINT@.,P$;ABS(X)"LEVELS":E=E+INT(10*RND(0)+PT*5):GOSUB82:GOTO2
177 PRINT@.,"CEILING TRAP!":H1=INT(10*RND(0)+L):K=48:POKEM,K:POKEO,K:GOSUB116:CL=1:GOTO194
178 PRINT@.,"TELEPORT...":K=32:POKEM,K:POKEO,K:GOSUB110:O=N-DZ:POKEO,A
179 GOSUB112:GOSUB115:GOSUB82:CL=1:GOTO206
180 E=E+INT(50*RND(0)+L):K=32:POKEM,K:X=INT(14*RND(0)+1):GOSUB126
181 ONXGOTO183,184,183,182,184,185,190,186,187,188,192,185,190,191
182 PRINT@.,"REGENERATION SPELL!!":RG=RG+1:GOTO205
183 PRINT@.,"HEALING POTION!!":HP=HP+1:GOTO205
184 PRINT@.,"MAGIC SACK!!":BG=BG+1:T1=T1+100:GOTO205
185 PRINT@.,"SHIELD SPELL!!":SH=SH+1:GOTO205
186 PRINT@.,"LIGHT SPELL!!":LG=LG+1:GOTO205
187 PRINT@.,"ENCHANTED WEAPON!!":PL=PL+1:BS=BS+INT(10*RND(0)+5):GOTO205
188 X=INT(8*RND(0)+L+3):PRINT@.,"MAP TO"X"TH LEVEL!!"
189 TM=TM+1:TZ(TM)=X:GOTO205
190 PRINT@.,"TELEPORT SPELL!!":TP=TP+1:GOTO205
191 PRINT@.,"DRIFT SPELL!!":FA=FA+1:GOTO205
192 PRINT@.,"INVISIBILITY SPELL!!":IV=IV+1:GOTO205
193 IFP1<1THENL=L-X:K=.
194 GOSUB145:IFFR=1THEN196
195 GOTO202
196 F1=.:IFR=2THEN200
197 IFFA=.THEN202
198 FA=FA-1:PRINT@.,"LIKE A FEATHER...":GOSUB82:CL=.:GOTO203
200 IFTP=.THEN202
201 PRINT@.,"TELEPORT TO SAFETY!":GOSUB82:CL=0:GOTO52
202 HT=HT-H1
203 IFP1=1THEN2
204 GOTO206
205 FORJ=1TO1000:NEXT
206 IFCL=.ORINT(4*RND(0))>.THEN208
207 GOSUB93:PRINT@.,"LOST YOUR MAP!":GOSUB140:POKEO,A:GOSUB112:GOSUB82
208 CL=.:GOTO36
209 POKE8,.:FORI1=1TO5:SOUND1,1:NEXT:FORI1=1TO5:SOUND100,1:NEXT:GOSUB139
211 M=O+DZ:GOSUB93:IFM2=.THEN214
212 SZ=SZ(J):IFSZ=.THEN234
213 HZ=AZ(J):D=.:X$=X$(J):GOTO216
214 SZ=RZ(J):IFSZ=.THEN233
215 HZ=BZ(J):D=1:X$=Y$(J)
216 PRINT@.,"YOU ARE ATTACKED BY":GOSUB82:PRINT@.,X$:GOSUB145:IFFR=1THEN230
217 X1=SZ/BS:H1=HZ
218 IFS1=1THEN220
219 HT=HT-INT(X1*4*L*RND(0)+1)
220 IFD=1ANDT>.ORSF=1THENIFX1<.5ORX1>1ORC=42THEN225
221 HZ=HZ-INT(1/X1*4*L*RND(0)+1+PL):IFHZ<.THEN228
222 IFHT<-5THENGOSUB93:PRINT@.,"SLAIN BY ":GOSUB82:PRINT@.,X$:GOTO332
223 GOSUB93:PRINT@.,"HP:"HT,:GOSUB291:GOSUB145:IFFR=1THEN230
224 GOTO218
225 IFSF=1THENSF=.:PRINT@.,"THE SWORD IS STOLEN!!":GOSUB82:MZ=.:RETURN
226 D=-1:P=A:GOSUB93:PRINT@.,"YOUR GOLD IS STOLEN!!":GOSUB82
227 G(J)=T:T=.:GOTO304
228 S1=.:E=E+(SZ+H1)*L:BS=BS+INT(5*RND(0)+1):MK=MK+1:K=32
229 MZ=.:GOSUB93:PRINT@.,"YOU VANQUISHED":GOSUB82:PRINT@.,X$:GOSUB82:RETURN
230 IFTP=.THEN217
231 K=C:POKEM,K:GOSUB93:IFM2=.THENNZ(J)=M:FL=1:RETURN
232 MZ(J)=M:FL=1:RETURN
233 PRINT@.,"THE MAGE TAKES YOUR":GOSUB82:PRINT@.,"MAGIC SPELLS!!":IV=0:RG=0:TP=0:SH=0:FA=0:LG=0:GOTO235
234 PRINT@.,"THE DEMON DRAINS YOUR":GOSUB82:PRINT@.,"EXPERIENCE LEVEL!!":E=INT(E/2):IFEL>1THENEL=EL-1
235 MZ=.:GOSUB82:RETURN
236 S$="":HZ=.:SZ=.:FORJ=1TO2+INT(L*.25)
237 SZ=SZ+INT(4*RND(0)+L):HZ=HZ+INT(6*RND(0)+L*1.5):NEXT:X=SZ/BS:Y=INT(X*5)
238 IFY>6THENS$="POWER ":GOTO240
239 IFY<1THENS$="WEAK "
240 X=INT(4*RND(0)+L/2):IFX<10THEN243
241 R=INT(6*RND(0)):IFR>.THENX=10-R:GOTO243
242 SZ(I)=.:A(I)=25:RETURN
243 A(I)=X(X):IFS$=""THENS$=A$(X)+" "
244 X$(I)=S$+B$(X):SZ=SZ+INT(X*RND(0)+X):HZ=HZ+INT(X*RND(0)+X)
245 SZ(I)=SZ:AZ(I)=HZ:RETURN
246 S$="":HZ=.:SZ=.:FORJ=1TO3+INT(L*.25)
247 SZ=SZ+INT(3*RND(0)+L*1.5):HZ=HZ+INT(4*RND(0)+L):NEXT:X=SZ/BS:Y=INT(X*5)
248 IFY>6THENS$="EXPERT ":GOTO250
249 IFY<1THENS$="INFERIOR "
250 X=INT(4*RND(0)+L/2):IFX<10THEN253
251 R=INT(6*RND(0)):IFR>.THENX=10-R:GOTO253
252 RZ(I)=.:B(I)=37:RETURN
253 B(I)=Y(X):IFS$=""THENS$=C$(X)+" "
254 Y$(I)=S$+D$(X):SZ=SZ+INT(X*RND(0)+X):HZ=HZ+INT(X*RND(0)+X)
255 RZ(I)=SZ:BZ(I)=HZ:RETURN
256 FORJ=.TONM:IFM=MZ(J)THENV=J:J=NM
257 NEXT
258 SZ=SZ(V):IFSZ=.THENGOSUB234:K=PZ(V):POKEM,K:MZ(V)=.:GOTO37
259 HZ=AZ(V):D=.:X$=X$(V):GOTO264
260 FORJ=.TONN:IFM=NZ(J)THENV=J:J=NN
261 NEXT:IFV>3THENV=3
262 SZ=RZ(V):IFSZ=.THENGOSUB233:K=QZ(V):POKEM,K:NZ(V)=.:GOTO37
263 HZ=BZ(V):D=DZ(V):X$=Y$(V)
264 PRINT@.,X$:GOSUB125:GOSUB93:GOSUB142:IFBTHEN281
265 GOSUB93:X=SZ/BS:H1=HZ
266 HZ=HZ-INT(1/X*4*L*RND(0)+1+PL):IFHZ<.THEN272
267 IFS1=1THEN270
268 HT=HT-INT(X*4*L*RND(0)+1)
269 IFHT<-5THENPRINT@.,"THOU ART SLAIN!":GOTO332
270 IFM2=.THEN278
271 AZ(V)=HZ:GOTO279
272 V1=.:S1=.:POKEO,A:E=E+(SZ+H1)*L:BS=BS+INT(5*RND(0)+1):MK=MK+1
273 PRINT@.,"YOU HAVE SLAIN ":GOSUB126:GOSUB93
274 PRINT@.,X$:IFM2=1THENK=PZ(V):POKEM,K:MZ(V)=.:GOTO205
275 K=QZ(V):POKEM,K:NZ(V)=.:IFD>.THEN205
276 FORI=1TO1000:NEXT:GOSUB93:PRINT@0,"FOUND YOUR"G(V)"GOLD!!":T=T+G(V)
277 GOTO205
278 BZ(V)=HZ
279 GOSUB93:PRINT@.,"HP:"HT,:C=K:GOSUB291:GOSUB82:GOSUB142:IFB=.THEN266
280 S1=.
281 GOSUB93:GOTO39

282 D=1:M2=1:FORJ=.TONM:MZ=MZ(J):IFMZ=.THENNEXT:GOTO286
283 P=PZ(J):C=A(J):FL=.:GOSUB293:IFFL=1THEN52
285 PZ(J)=P:MZ(J)=MZ:NEXT
286 M2=.:FORJ=.TONN:MZ=NZ(J):IFMZ=.THENNEXT:GOTO37
287 D=DZ(J):P=QZ(J):C=B(J):IFC=40THENC=32:IFL1=1THENC=37
288 GOSUB293:IFFL=1THEN52
290 DZ(J)=D:QZ(J)=P:NZ(J)=MZ:NEXT:GOTO37
291 W=INT(7*RND(0)+1):IFM2=1THENPRINTS$(W):GOSUB131:RETURN
292 PRINTT$(W):GOSUB132:RETURN

293 F2=.:GOSUB108:Z1=ABS(X-XL):Z2=ABS(Y-YL):IFZ1>9ORZ2>9THENRETURN
294 IFK=38THEN296
295 IFV1=.ORL1=1THEN297
296 X=INT(32*RND(0)):Y=INT(16*RND(0))
297 DX=.:DY=.:F3=.:Z=.:W=P:M1=MZ-DZ:IFC=35THEN319
299 IFX<XLTHENDX=-1:GOTO301
300 IFX>XLTHENDX=1
301 IFY<YLTHENDY=-32:GOTO303
302 IFY>YLTHENDY=32
303 Z=DX+DY:POKEMZ,P:K2=PEEK(M1)
304 IFK2<>128THENPOKEM1,P
305 P=PEEK(MZ+Z*D):IFP<223THENIFP<24ORP=32ORP=28ORP=36ORP>43THEN311
307 IFXL=XORYL=YTHEN310
308 IFF3=.THENZ=Z-DX:F3=1:GOTO305
309 IFF3=1THENZ=Z+DX-DY:F3=2:GOTO305
310 P=W:Z=.:IFF2<5THENF2=F2+1:GOTO296
311 MZ=MZ+Z*D:M1=M1+Z*D:K1=PEEK(M1)
312 IFK1=ATHENP=K:GOTO209
313 IFP=36THENIFM2=.THENP=32:GOTO317
314 IFP=63THENP=32:GOTO317
315 IFD=-1THENIFK1=128THENMZ=.:RETURN
316 IFP=15THENIFM2=1THENMZ=.:RETURN
317 POKEMZ,C:IFK1=128THENRETURN
318 POKEM1,C:RETURN
319 IFZ1>3ORZ2>3ORZ1=1ANDZ2=1THEN299
320 FORI1=1TO4:R=INT(8*RND(0)+1):M3=O+D(R):K2=PEEK(M3):IFK2=32THENI1=4:NEXT:GOTO322
321 NEXT:GOTO299
322 POKEMZ,P:MZ=M3+DZ:K2=PEEK(M1):IFK2<>128THENPOKEM1,P
323 P=32:M1=M3:GOTO317
324 PRINTTAB(6)"OUT OF TIME!"
325 FORI=1TO2500:NEXT:CLS:PRINT@96,TAB(7)"YOUR SCORES:":PRINTTAB(7)"  EXPERIENCE"E
326 PRINTTAB(7)"       LEVEL"EL:PRINTTAB(7)"  DEEPEST LV"DE:PRINTTAB(7)"    MONSTERS"MK
327 PRINTTAB(7)"  HEALTH:"HT"HP":PRINTTAB(7)"   SKILL:"BS:IFT3<0THENT3=0
328 T3=INT((USR(1)-T4)/3600):PRINTTAB(7)"QUEST TOOK"T3"MIN":PRINT:PRINTTAB(7)"PLAY AGAIN? ";:GOTO334
329 J=MC+479:Y=.:FORI=MCTOMC+240:POKEI,PEEK(I+DZ):POKEJ-Y,PEEK(J-Y+DZ):Y=Y+1:NEXT:RETURN
332 GOSUB82:PRINT@.,"EXPERIENCE LEVEL:"EL:GOSUB82:PRINT@.,"DUNGEON LEVEL:"L:GOSUB118
333 GOSUB329:GOSUB93:PRINT@.,"AGAIN? ";
334 A$=INKEY$:IFA$=""THEN334
335 IFA$="Y"THENPRINT"YES";:FORI=1TO200:NEXT:RUN
336 IFA$="N"THENPRINT@.,;:POKE8,0:END
337 GOTO334
338 DZ=19108:MC=16416
341 CLS:PRINT@96,TAB(5)"EPYX PRESENTS:":PRINT@32*8,TAB(5)"THE SWORD OF FARGOAL!":PRINT@32*9,TAB(5)"ßßßßßßßßßßßßßßßßßßßßß"
342 PRINT@32*11,TAB(8)"BY: JEFF MCCORD":PRINT@32*12,TAB(9)"(C) MCMLXXXII":GOSUB120
343 DIMMZ(4),NZ(3),PZ(4),QZ(3),AZ(4),BZ(3),DZ(3),RZ(3),SZ(4),JZ(2,2)
345 V(1)=2:V(2)=1:V(3)=4:V(4)=3:L=1:EL=1:FORI=1TO8:READD(I):NEXT:A=24:T4=USR(1)
346 FORI=1TO3:HT=HT+INT(6*RND(0)+1):BS=BS+INT(6*RND(0)+1):NEXT
347 FORI=1TO4:READE(I):NEXT:SR=1:TH=HT:EP=200:K=32:HP=1:TP=1:T1=100:QL=INT(5*RND(0)+15)
348 FORI=0TO9:READA$(I),B$(I),X(I),C$(I),D$(I),Y(I):NEXT:FORI=1TO7:READS$(I),T$(I):NEXT
349 RETURN
800 PRINT@.,"QUIT! ARE YOU SURE?"
801 A$=INKEY$:IFA$="Y"THEN332
802 IFA$="N"THEN38
803 GOTO801
3000 X=RND(-(PEEK(9)*256+PEEK(10))):X=35524+512:Z=16896:GOSUB3001:Z=X:GOSUB3001:EXECX:SC=X-512:X=USR(0):RETURN
3001 READ A$:FORY=1TOLEN(A$)STEP2:POKEZ,(ASC(MID$(A$,Y,1))-65)*16+ASC(MID$(A$,Y+1,1))-65:Z=Z+1:NEXT:RETURN
3002 DATA HPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATA INCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
3353 DATA1,-1,32,-32,-31,31,-33,33,43,38,15,48
3354 DATAA,DIRE WOLF,31,A,ROGUE,42
3355 DATAAN,OGRE,25,A,BARBARIAN,37,A,HOBGOBLIN,25,AN,ELVIN RANGER,37,A,WEREBEAR,25
3356 DATAA,DWARVEN GUARD,43,A,GARGOYLE,25,A,MERCENARY,37,A,TROLL,25,A,SWORDSMAN,37
3357 DATAA,WYVERN,30,A,MONK,37,A,DIMENSION SPIDER,35,A,DARK WARRIOR,37,A,SHADOW DRAGON,30
3358 DATAAN,ASSASSIN,40,A,FYRE DRAKE,30,A,WAR LORD,37,CRUNCH,CLANG,CLAW,OUCH!,GNARL,SLASH
3359 DATAUGH!,CLINK,GROWL!,CHOP,SHRED,THUD,THUMP,SHRIEK!
4000 REM MC-10 SWORD OF FARGOAL
4010 REM WHAT GAMMAQUEST II MAY
4020 REM HAVE BEEN LIKE ON THE
4030 REM COMMODORE PET
4040 REM JIM GERRIE 2020
4050 REM USE WASD TO MOVE
4060 REM SPACE=PANIC BUTTON
