0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 GOSUB 6000:REM THE CITADEL OF PERSHU
30 GOSUB 4510:REM INITIALISE
40 PRINT"---------------------------------------":PRINT
50 GOSUB 260:REM PLAYER STATUS REPORT
60 GOSUB 440:PRINT:REM ROOM DESCRIPTION
70 IF RO=31 OR RO>43 THEN 190
80 IF A(RO,8)<>0 THEN GOSUB 3180:GOTO 110:REM MONSTER DESCRIPTION
90 IF RO=6 AND LI=0 THEN GOSUB 3340:REM TO GET TORCH
100 IF A(RO,7)<>0 AND LI=1 THEN GOSUB 3340:REM CONTENTS DESCRIPTION
110 GOSUB 4210:REM ACCEPT PLAYER COMMMAND
120 IF ST+CH+DE+IN+WI+CO<15 THEN 170
130 TA=TA+1
140 IF RO=12 AND C(2)<>0 THEN M$="YOU HAVE USED YOUR SILVER KEY TO ENTER THE EAST WINE CELLAR.":GOSUB1
145 IF RO=12 THEN C(2)=0
150 IF RO=20 AND C(3)<>0 THEN M$="YOU HAVE USED YOUR GOLD KEY TO ENTER THE MARBLED HALL.":GOSUB1
155 IF RO=20 THEN C(3)=0
160 GOTO 40
170 M$="YOUR COMBINED ATTRIBUTES ARE NO LONGER ENOUGH TO SUSTAIN YOU... YOU ARE DEAD!":GOSUB1
190 PRINT "YOUR FINAL SCORE, ";N$", IS";
200 PRINT 3*CA+30*MK+3*(ST+CH+DE+IN+WI+CO) + TA
210 W=RO+TA/1000:IF W>99 THEN W=W-RND(8):GOTO 210
220 IF RO=31 THEN W=100
230 PRINT "YOU COMPLETED";INT(W);"% OF THE QUEST"
240 END
250 REM ***
260 REM PLAYER STATUS REPORT
270 PRINT "WIZARD ";N$
280 IF M>0 THEN PRINT "MAGIC SPELLS: YOU HAVE ";M;" LEFT"
290 IF MK>0 THEN PRINT "MONSTERS KILLED: ";MK
300 PRINT "YOU HAVE ";
310 FOR J=1 TO 9:PRINT T$(C(J));" ";:NEXT J
320 IF CA>0 THEN PRINT "$";CA
330 IF ST<0 THEN ST=0
340 IF CH<0 THEN CH=0
350 IF DE<0 THEN DE=0
360 IF IN<0 THEN IN=0
370 IF WI<0 THEN WI=0
380 IF CO<0 THEN CO=0
390 PRINT TAB(1)"STR:";ST;
393 PRINT TAB(10);"CHA:";CH
400 PRINT TAB(1)"DEX:";DE;
402 PRINT TAB(10)"INT:";IN
410 PRINT TAB(1)"WIS:";WI;
412 PRINT TAB(10)"CON:";CO
420 RETURN
430 REM ***
440 REM ROOM DESCRIPTION
450 IF LI=0 AND RO<>6 THEN PRINT "IT IS VERY DARK":GOSUB 5430:RETURN
460 IF RO>19 THEN 490
470 ON RO GOSUB 550,600,650,710,760,810,880,940,1000,1070,1140,1190,1260,1330,1370,1440,1530,1610,1660
480 GOSUB 5430:RETURN
490 IF RO>35 THEN 520
500 Z=RO-19:ON Z GOSUB 1710,1770,1820,1860,1900,1940,1970,2010,2050,2120,2180,2210,2260,2320,2360,2420
510 GOSUB 5430:RETURN
520 Z=RO-35:ON Z GOSUB 2460,2520,2590,2650,2720,2760,2810,2850,2920,2990,3060,3110
530 GOSUB 5430:RETURN
540 REM ***
550 REM ROOM 1
560 PRINT "AN UNDERGROUND RIVER FLOWS SWIFTLY BY"
570 IF RND(10)>4 THEN PRINT "THERE IS AN EXIT TO THE WEST"
580 IF RND(10)>5 THEN PRINT "A TUNNEL LEADS TO THE SOUTH"
590 RETURN
595 REM ***
600 REM ROOM 2
610 M$="YOU ARE IN THE CITADEL'S FOOD STORAGE AREA.":GOSUB1
620 M$="AREA. OLD CHEESES AND BLACK LOAVES OF BREAD CAN BE SEEN, AS WELL AS MANY SACKS OF SUPPLIES.":GOSUB1
645 RETURN
650 REM ***
655 REM ROOM 3
660 PRINT "YOU ARE IN THE CITADEL'S KITCHEN. A HUGE"
670 PRINT "JOINT OF MEAT TURNS SLOWLY OVER A RAGING"
680 PRINT "FIRE. DOORS LEAD INTO CUPBOARDS, AS WELL"
690 PRINT "AS TO THE WEST AND TO THE SOUTH"
695 RETURN
700 REM ***
710 REM ROOM 4
720 PRINT "THIS IS THE CENTRAL LIBRARY. LEATHER-"
730 PRINT "BOUND VOLUMES LINE THE WALLS, RIGHT UP"
740 PRINT "TO THE ORNATELY-CARVED CEILING..."
745 RETURN
750 REM ***
760 REM ROOM 5
770 PRINT "THIS ROOM IS AN AWFUL MESS. IT USED TO"
780 PRINT "BE AN ARTIST'S STUDIO. PAINT AND EASELS"
790 PRINT "LIE AROUND THE FLOOR"
795 RETURN
800 REM ***
810 REM ROOM 6
820 PRINT "THIS IS THE ENTRANCE TO THE CITADEL OF"
830 PRINT "PERSHU. TURN NOW, IF YOU WISH. MANY"
840 PRINT "STRONGER THAN YOU HAVE TAKEN FRIGHT AT"
850 PRINT "AT ITS MENACING TOWERS AND DARK PORTALS.";
860 PRINT "IF YOU WISH TO PROCEED, MOVE EAST":PRINT "TOWARDS THE BLACK, GAPING DOORWAY..."
865 RETURN
870 REM ***
880 REM ROOM 7
890 PRINT "A STONE ALTAR STANDS IN THE MIDDLE OF"
900 PRINT "THE ROOM WITH TWO DEAD CANDLES ON IT. AN";
910 PRINT "OLD BOOK LIES ON ONE PART OF THE ALTER"
920 PRINT "TOP AND A FADED, RED PARCHMENT CLOTH":PRINT "COVERS THE FRONT OF IT"
925 RETURN
930 REM ***
940 REM ROOM 8
950 PRINT "YOU STAND HIGH ON THE BLACK TOWER, THE"
960 PRINT "CITADEL STRETCHES TO THE NORTH, SOUTH"
970 PRINT "AND EAST OF YOU. THERE IS ONLY ONE WAY":PRINT "OUT"
980 RETURN
990 REM ***
1000 REM ROOM 9
1010 PRINT "YOU ARE IN THE NORTHERN SECTION OF THE"
1020 PRINT "CITADEL'S LARGE WINE CELLAR. HEAVY"
1030 PRINT "BARRELS LIE ALL AROUND YOU IN THIS END"
1040 PRINT "OF THE CELLAR. THERE IS A DOOR TO THE"
1050 PRINT "NORTH AND ONE TO THE SOUTH"
1055 RETURN
1060 REM ***
1070 REM ROOM 10
1080 PRINT "YOU ARE IN THE WEST WING OF THE WINE"
1090 PRINT "CELLAR. THERE IS A DOOR TO THE WEST AND"
1100 PRINT "ONE TO THE EAST. THE CENTRAL, CIRCULAR"
1110 PRINT "PART OF THE CELLAR LIES BEYOUND THE"
1120 PRINT "EAST DOOR."
1125 RETURN
1130 REM ***
1140 REM ROOM 11
1150 PRINT "YOU ARE IN THE CENTRAL, CIRCULAR AREA OF"
1160 PRINT "THE WINE CELLAR. THERE IS A DOOR AT EACH"
1170 PRINT "COMPASS POINT."
1175 RETURN
1180 REM ***
1190 REM ROOM 12
1200 PRINT "YOU ARE IN THE EAST SECTION OF THE"
1210 PRINT "WINE CELLAR. THERE IS A DOOR TO THE"
1220 PRINT "WEST AND ONE - WHICH YOU CAN NOT USE,"
1230 PRINT "AS IT ONLY ALLOWS ENTRANCE TO WHERE"
1240 PRINT "YOU NOW STAND - TO THE EAST"
1245 RETURN
1250 REM ***
1260 REM ROOM 13
1270 PRINT "THERE ARE MANY, MANY WINE BOTTLES HERE"
1280 PRINT "LYING ON THEIR SIDES, IN THIS SOUTHERN"
1290 PRINT "SECTION OF THE WINE CELLAR. THERE IS A "
1300 PRINT "DARK, UNFRIENDLY-LOOKING HOLE TO THE"
1310 PRINT "WEST AND DOORS TO THE NORTH AND TO THE":PRINT"SOUTH"
1315 RETURN
1320 REM ***
1330 REM ROOM 14
1340 PRINT "THIS IS THE CITADEL'S ARMORY. ROW UPON"
1350 PRINT "ROW OF SHINY SUITS OF ARMOR ARE STORED":PRINT"HERE....."
1355 RETURN
1360 REM ***
1370 REM ROOM 15
1380 PRINT "YOU ARE IN THE RULER'S BEDCHAMBER"
1390 PRINT "A LARGE FIRE BURNS IN THE SOUTH OF"
1400 PRINT "THE ROOM, WITH A SMALL DOOR BESIDE"
1410 PRINT "IT. OTHER EXITS ARE TO THE NORTH"
1420 PRINT "AND TO THE WEST"
1425 RETURN
1430 REM ***
1440 REM ROOM 16
1450 PRINT "THIS CURIOUS ROOM HAS A FLOOR COVERED"
1460 PRINT "IN SAND, HEAPED ALL OVER THE PLACE"
1470 PRINT "BY PEEPING OVER THE 'DUNES' YOU CAN"
1480 PRINT "SEE A GOLDEN PASSAGEWAY LEADS TO THE"
1490 PRINT "WEST, AND THERE IS A DOOR TO THE SOUTH"
1500 PRINT "YOU ARE NOT SURE WHETHER OR NOT YOU"
1510 PRINT"HAVE SEEN ALL THE EXITS"
1515 RETURN
1520 REM ***
1530 REM ROOM 17
1540 PRINT "YOU ARE IN THE PICTURE GALLERY."
1550 PRINT "PORTRAITS OF LONG-DEAD PRINCES LINE ALL"
1560 PRINT "OF THE WALLS. THE ROOM IS DOMINATED BY A";
1570 PRINT "HUGE LANDSCAPE, HANGING ABOVE THE EXIT"
1580 PRINT "TO THE EAST WHICH LEADS, VIA THE GOLD"
1590 PRINT "PASSAGEWAY BACK TO THAT CURIOUS ROOM OF":PRINT "SAND"
1595 RETURN
1600 REM ***
1610 REM ROOM 18
1620 PRINT "YOU ARE ON A REMOTE TOWER BALCONY"
1630 IF RND(10)>5 THEN PRINT "A BAT FIES PAST YOU, SHRIEKING"
1640 PRINT "THERE ARE STAIRS HERE"
1645 RETURN
1650 REM ***
1660 REM ROOM 19
1670 PRINT "YOU WALK BENEATH A STONE ARCHWAY"
1680 PRINT "YOU CAN ONLY WALK NORTH OR SOUTH"
1690 PRINT "UNLESS YOU DECIDE TO TAKE THE STAIRS"
1695 RETURN
1700 REM ***
1710 REM ROOM 20
1720 PRINT "THIS VAST HALL HAS A MARBLE FLOOR, AND"
1730 PRINT "THE SLIGHTEST SOUND ECHOES VIOLENTLY"
1740 PRINT "THERE ARE PURPLE DRAPES CONCEALING"
1750 PRINT "THE EXITS FROM THIS HALL"
1755 RETURN
1760 REM ***
1770 REM ROOM 21
1780 PRINT "YOU ARE IN THE GLOVE STOREROOM"
1790 PRINT "THE WEST DOOR RADIATES HEAT"
1800 PRINT "ANOTHER DOOR LEADS TO THE SOUTH"
1805 RETURN
1810 REM ***
1820 REM ROOM 22
1830 PRINT "YOU ARE IN THE SILVER CROSSES STOREROOM"
1840 PRINT "THERE ARE ONLY TWO EXITS"
1845 RETURN
1850 REM ***
1860 REM ROOM 23
1870 PRINT "YOU ARE IN THE AMULET STOREROOM"
1880 PRINT "DOORS LEAD NORTH, AND SOUTH"
1885 RETURN
1890 REM ***
1900 REM ROOM 24
1910 PRINT "YOU ARE IN THE KAZOO STOREROOM"
1920 PRINT "THERE ARE TWO EXITS"
1925 RETURN
1930 REM ***
1940 REM ROOM 25
1950 PRINT "YOU ARE IN THE SATCHEL STOREROOM"
1960 RETURN
1970 REM ***
1975 REM ROOM 26
1980 PRINT "YOU ARE IN THE STOREROOM FOR WOODEN"
1990 PRINT "BOXES...THERE ARE TWO EXITS"
1995 RETURN
2000 REM ***
2010 REM ROOM 27
2020 PRINT "THIS IS WHERE PRINTED VASES ARE"
2030 PRINT "STORED...AS YOU CAN EASILY SEE"
2040 RETURN
2045 REM ***
2050 REM ROOM 28
2060 PRINT "THE HEAVY AIR OF THIS AREA SEEM TO MAKE"
2070 PRINT "YOUR TORCH VERY DIM. YOU CAN HARDLY SEE"
2080 PRINT "AIR IS RUSHING UP FORM SOMEWHERE"
2090 PRINT"YOU CAN JUST MAKE OUT THAT THIS AREA"
2100 PRINT "MUST BE A MINE OF SOME SORT"
2110 RETURN
2115 REM ***
2120 REM ROOM 29
2130 PRINT "YOU APPEAR TO BE IN AN ENDLESS LABYRINTH";
2140 PRINT "LINED WITH PAINTINGS........."
2150 PRINT "WHICHEVER WAY YOU TURN, THERE SEEMS TO"
2160 PRINT "BE MORE TUNNELS, ALL LINED WITH ":PRINT "PAINTINGS"
2170 RETURN
2175 REM ***
2180 REM ROOM 30
2190 PRINT "THIS IS THE SOUTHERN TOWER OF THE CITADEL"
2200 RETURN
2205 REM ***
2210 REM ROOM 31
2220 PRINT "WELL DONE, YOU HAVE MANAGED TO FIND THE"
2230 PRINT "THE EXIT. TAKE A DEEP BREATH OF GOOD"
2240 PRINT "CLEAN AIR...."
2245 RETURN
2250 REM ***
2260 REM ROOM 32
2270 PRINT "HTIS ROOM IS FILLED WITH SWIRLING SMOKE"
2280 PRINT "SO YOU CANNOT SEE...AIR RUSHING PAST A"
2290 PRINT "STATUE OF THE GODDESS DIANA. THIS"
2300 PRINT "MUST BE THE CITADEL'S MEDITATION CHAMBER"
2310 RETURN
2315 REM ***
2320 REM ROOM 33
2330 PRINT "A SMALL FORKED BRIDGE CROSSES A STREAM"
2340 PRINT"HERE. YOU CAN MOVE NORTH, SOUTH OR WEST"
2345 RETURN
2350 REM ***
2360 REM ROOM 34
2370 PRINT "YOU ARE IN A ROUGH STONE CAVERN, WITH"
2380 PRINT "STAIRS LEADING UP FROM IT"
2390 PRINT"THERE IS ALSO A SINGLE DOOR WHICH"
2400 PRINT "LEADS AWAY FROM THE CAVERN"
2405 RETURN
2410 REM ***
2420 REM ROOM 35
2430 PRINT "THIS IS THE FORMER CITADEL UNDERGROUND"
2440 PRINT "STABLE. IT SMELLS TERRIBLE"
2445 RETURN
2450 REM ***
2460 REM ROOM 36
2470 PRINT "YOU FIND YOURSELF IN AN UNDERGROUND"
2480 PRINT "COURTYARD. STRANGE, TWISTED TREES ARE"
2490 PRINT "AROUND YOU, AND A WIND OF INCREDIBLE"
2500 PRINT "COLDNESS BLOWS FROM THE EAST"
2510 RETURN
2515 REM ***
2520 REM ROOM 37
2530 PRINT "THIS IS THE ORACLE ROOM, ALTHOUGH THE"
2540 PRINT "MYSTIC VOICE HAS NOT SPOKEN FOR MANY"
2550 PRINT "YEARS"
2560 IF RND(10)>3 THEN PRINT "BUT NOW IT TELLS YOU THERE IS":PRINT "A HIDDEN STAIRWELL IN THE ROOM"
2570 IF RND(10)>6 THEN PRINT "THE VOICE FAINTLY MURMURS OF THE DOOR TO THE SOUTH"
2580 RETURN
2585 REM ***
2590 REM ROOM 39
2600 PRINT"HORRORS. A COLD SHUDDER PASSES THROUGH"
2610 PRINT "AS YOU REALISE THIS IS THE PRIEST'S"
2620 PRINT "SACRIFICE ROOM. DRIED UP BLOOD IS ON THE";
2630 PRINT "FLOOR, AND A SKULL GRINS AT YOU, FROM"
2640 PRINT "HIGH ON THE WALL"
2645 RETURN
2646 REM ***
2650 REM ROOM 39
2660 PRINT "OLD STRAW MATTRESSES, AND RINGS CHAINED"
2670 PRINT "TO THE WALL TELL YOU THIS WAS THE"
2680 PRINT "CITADEL'S DUNGEON.":IF RND(19)>4 THEN PRINT "A SMALL DOOR LEADS TO THE NORTH":PRINT "AND ANOTHER TO THE EAST"
2690 PRINT "THE DUNGEON SEEMS TO STRETCH FOREVER,"
2700 PRINT "WITH MANY SMALL PARTITIONED AREAS..."
2705 RETURN
2710 REM ***
2720 REM ROOM 40
2730 PRINT "YOU ARE IN A SMALL ALCOVE, WITH A SOLIOD"
2740 PRINT "GREY GRANITE THRONE IN THE MIDDLE OF IT"
2750 RETURN
2755 REM ***
2760 REM ROOM 41
2770 PRINT "THIS IS THE ORC'S GUARDROOM, WAY BELOW"
2780 PRINT "THE GROUND. A STAIRWELL ENDS HERE, AND"
2790 PRINT "A DOOR LEADS TO THE EAST"
2795 RETURN
2800 REM ***
2810 REM ROOM 42
2820 PRINT "THERE IS A HEALING POOL HERE, WITH A"
2830 PRINT "DANGEROUS, SWIRLING AREA OF WATER"
2835 RETURN
2840 REM ***
2850 REM ROOM 43
2860 PRINT"THE UNDERPRIESTS OF ORRIC USED THIS"
2870 PRINT "TINY HALL FOR THEIR FORBIDDEN WORSHIP"
2880 PRINT "EONS AGO. IT IS AN UNPLEASANT SIGHT"
2890 PRINT "SO YOU ARE THRILLED TO SEE A SET OF"
2900 PRINT "STONE STAIRS"
2905 RETURN
2910 REM ***
2920 REM ROOM 44, DEATH BY DROWNING
2930 PRINT "WATER COVERS YOUR HEAD"
2940 GOSUB 5430
2950 PRINT "YOU ARE DROWNING"
2960 GOSUB 5430
2970 PRINT "GLUG...GASP.............."
2980 RETURN
2985 REM ***
2990 REM ROOM 45, DEATH BY BURNING
3000 PRINT "THE FLAMES STRIKE AT YOU..."
3010 GOSUB 5430
3020 PRINT "AS SCREAM AND SLOWLY BURN TO DEATH"
3030 GOSUB 5430
3040 IF RND(10)>7 THEN 3000
3050 RETURN
3055 REM ***
3060 REM ROOM 46, DEATH BY FREEZING
3070 PRINT "YOU ARE HIT BY A FREEZING SPELL"
3080 PRINT "AS YOU TURN BLUE YOU ARE SLOWLY TURNED"
3090 PRINT "INTO A FREEZING BLOCK OF PERPETUAL"
3100 PRINT "LIVING STONE. THIS IS CUTAINS FOR YOU"
3107 REM ***
3110 REM ROOM 47, BOTTOMLESS PIT
3120 PRINT "YOU TUMBLE DOWN INTO A BOTTOMLESS PIT"
3130 GOSUB 5430
3140 PRINT"YOU REALIZE THIS IS THE END AS THE WALLS":PRINT"RUSH BY AT LIGHTENING SPEED. DOWN, DOWN, DOWN YOU GO NEVER TO BE SEEN AGAIN"
3150 RETURN
3160 REM ***
3180 REM MONSTER DESCRIPTION
3190 G$=M$(A(RO,8))
3200 S1=RND(6)*3
3210 H1=RND(6)*3
3220 D1=RND(6)*3
3230 I1=RND(6)*3
3240 W1=RND(6)*3
3250 C1=RND(6)*3
3260 IF RND(10)>5 THEN M$="YOU COME FACE TO FACE WITH A "+G$:GOSUB1:GOTO3270
3261 M$="THE ROOM CONTAINS A "+G$:GOSUB1
3270 PRINT "WITH ATTRIBUTES AS FOLLOWS:"
3280 PRINT TAB(1)"STR:"S1;
3282 PRINT TAB(10)"CHA:"H1
3290 PRINT TAB(1)"DEX:"D1;
3292 PRINT TAB(10)"INT:"I1
3300 PRINT TAB(1)"WIS:"W1;
3302 PRINT TAB(10);"CON:"C1
3310 GOSUB 5430
3320 RETURN
3330 REM ***
3340 REM CONTENTS DESCRIPTION
3350 LET G$=T$(A(RO,7))
3360 VA=RND(100)+56
3370 PRINT "YOU CAN SEE...."
3380 GOSUB 5430
3390 PRINT G$
3400 IF A(RO,7)>9 THEN PRINT "WORTH $";VA
3410 GOSUB 5430
3420 RETURN
3430 REM ***
3440 REM PICK UP TREASURE
3450 IF LI=0 AND A(RO,7)<>1 THEN PRINT "IT IS TOO DARK TO SEE ANYTHING":GOSUB 5430:RETURN
3460 IF A(RO,7)=1 THEN LI=1:REM PLAYER IS CARRYING LIGHT
3470 IF A(RO,7)>9 THEN CA=CA+VA:GOTO 3490
3480 C(A(RO,7))=A(RO,7)
3490 A(RO,7)=0
3500 RETURN
3510 REM ***
3520 REM GET RID OF OBJECTS
3530 FLAG=0
3540 FOR Z=1 TO 9:IF C(Z)<>0 THEN FLAG=1
3550 NEXT Z
3560 IF FLAG=0 THEN PRINT "YOU HAVE NOTHING TO GET RID OF":GOSUB 5430:RETURN
3570 IF A(RO,7)<>0 THEN PRINT"THERE IS ALREADY SOMETHING HERE":GOSUB 5430:RETURN
3580 PRINT "YOU ARE CARRYING:"
3590 FOR Z=1 TO 9
3600 IF C(Z)<>0 THEN PRINT Z;" ";T$(Z)
3610 NEXT Z
3620 PRINT:INPUT "ENTER NUMBER OF OBJECT TO DROP";K
3630 IF C(K)=0 THEN PRINT "YOU ARE NOT CARRYING ";T$(K):GOSUB 5430:GOTO 3620
3640 IF K=1 THEN LI=0:REM TURN LIGHT OFF
3650 A(RO,7)=K
3660 C(K)=0
3670 RETURN
3680 REM ***
3690 REM THE BIG FIGHT
3700 IF A(RO,8)=0 THEN RETURN
3710 PRINT :PRINT "YOUR OPPONENT IS A ":PRINT G$
3720 MT=0:HT=0:REM MONSTER TALLY, HUMAN TALLY
3730 PRINT "WITH THE FOLLOWING ATTRIBUTES:"
3740 PRINT "1 - STRENGTH :";S1;
3741 IF S1<10 THEN PRINT " ";
3742 PRINT TAB(2);"2 - CHARISMA    :";H1
3750 PRINT "3 - DEXTERITY:";D1;
3751 IF D1<10 THEN PRINT " ";
3752 PRINT TAB(2);"4 - INTELLIGANCE:";I1
3760 PRINT "5 - WISDOM   :";W1;
3761 IF W1<10 THEN PRINT " ";
3762 PRINT TAB(2);"6 - CONSTITUTION:";C1
3770 PRINT:PRINT "YOUR ATTRIBUTES ARE:"
3780 PRINT "1 - STRENGTH :";ST;
3781 IF ST<10 THEN PRINT " ";
3782 PRINT TAB(2);"2 - CHARISMA    :";CH
3790 PRINT "3 - DEXTERITY:";DE;
3791 IF DE<10 THEN PRINT " ";
3792 PRINT TAB(2);"4 - INTELLIGANCE:";IN
3800 PRINT "5 - WISDOM   :";WI;
3801 IF WI<10 THEN PRINT " ";
3802 PRINT TAB(2);"6 - CONSTITUTION:";CO
3810 PRINT :IF C(4)<>0 THEN PRINT "YOU HAVE A SWORD":HT=HT+1
3820 IF C(5)<>0 THEN PRINT "YOUR WAR HAMMER WIL BE OF AID":HT=HT+3
3830 IF C(6)<>0 THEN PRINT"CHAIN MAIL ARMOR GIVES YOU AN EDGE":HT=HT+1
3840 IF C(7)<>0 THEN PRINT "YOUR SHIELD WILL HELP IN THIS FIGHT":PRINT "AGAINST THE ";G$:HT=HT+3
3850 IF C(8)<>0 THEN PRINT"THE CLOAK OF PROTECTION SUUROUNDS YOU":HT=HT+3
3860 IF C(9)<>0 THEN PRINT "THE WAND OF FIREBALLS ENHANCES":PRINT "YOU STRENGTH":HT=HT+3
3870 IF M>0 THEN PRINT "ENTER 1 TO FIGHT WITH MAGIC":INPUT "OR 2 TO RELY ON SKILL";Q:IF Q<1 OR Q>2 THEN 3870
3880 IF M>0 AND Q=1 THEN M=M-1:PRINT "YOUR MAGIC DESTROYS IT":GOSUB 5430:MK=MK+1:GOTO 4190
3890 PRINT :INPUT "WHICH TWO ATTRIBUTES TO FIGHT WITH      (SEPARATE BY A COMMA)";Z,Q
3900 IF Z<1 OR Q<1 OR Z>6 OR Q>6 OR Z=Q THEN PRINT "NO CHEATING":GOTO 3890
3910 IF Z=1 OR Q=1 THEN MT=MT+S1:HT=HT+ST
3920 IF Z=2 OR Q=2 THEN MT=MT+H1:HT=HT+CH
3930 IF Z=3 OR Q=3 THEN MT=MT+D1:HT=HT+DE
3940 IF Z=4 OR Q=4 THEN MT=MT+I1:HT=HT+IN
3950 IF Z=5 OR Q=5 THEN MT=MT+W1:HT=HT+WE
3960 IF Z=6 OR Q=6 THEN MT=MT+C1:HT=HT+CO
3970 PRINT :PRINT "THE FIGHT STARTS IN FAVOR OF ";
3980 IF HT>MT THEN PRINT "YOU":GOTO3990
3981 PRINT "THE ";G$
3990 K=RND(8)
4000 PRINT :PRINT "THE ";G$;" -";MT
4010 PRINT N$;" -";HT:PRINT
4020 IF K=0 THEN PRINT "YOU GET IN A GLANCING BLOW":MT=MT-1
4021 IF RND(10)>6 THEN PRINT "Aaaaargh!!!!":GOSUB 5430:PRINT "RIP! TEAR! THUD!":MT=MT-1:CO=CO-1
4022 IF RND(10)>5 THEN PRINT "YOU WANT TO RUN BUT YOU STAY & FIGHT"
4023 IF RND(10)>6 THEN PRINT "##$$!* !*# !#**# !! *!#$%!#"
4024 IF RND(10)>7 THEN PRINT "WILL THIS BE A BATTLE TO THE DEATH?"
4025 IF RND(10)>7 THEN PRINT"BLOOD DRIPS ON YOUR FACE FROM HIS":PRINT "GASHING WOUND":PRINT
4030 IF K=1 THEN PRINT "THE ";G$;" STRIKES OUT!":HT=HT-RND(3):ST=ST-1
4040 IF K=2 THEN PRINT "YOU DRAW THE ";G$;"'S BLOOD":MT=MT-1
4050 IF K=3 THEN PRINT "YOU ARE WOUNDED!!":HT=HT-RND(3):DE=DE-1
4060 IF K=4 THEN PRINT "THE ";G$;" IS TIRING":MT=MT-1
4070 IF K=5 THEN PRINT "YOU ARE BLEEDING...":HT=HT-2:WI=WI-1
4080 IF K=6 THEN PRINT "YOU WOUND THE ";G$:MT=MT-1
4090 IF K=7 THEN K=RND(30)+INT(CA/2):PRINT "IT KNOCKS $";K;" FROM YOUR HAND":CA=CA-K
4100 IF RND(100)>25 AND HT>0 AND MT>0 THEN FOR T=1 TO 1600:NEXT T:GOTO 3990
4110 IF HT>MT THEN PRINT "YOU HAVE KILLED THE ":PRINT G$:MK=MK+1:GOTO 4190
4120 PRINT:PRINT "THE ";G$;" GOT THE BETTER OF YOU"
4130 IF Z=1 OR Q=1 THEN ST=4*INT(ST/5)
4140 IF Z=2 OR Q=2 THEN CH=3*INT(CH/4)
4150 IF Z=3 OR Q=3 THEN DE=6*INT(DE/7)
4160 IF Z=4 OR Q=4 THEN IN=2*INT(IN/3)
4170 IF Z=5 OR Q=5 THEN WI=5*INT(WI/6)
4180 IF Z=6 OR Q=6 THEN CO=3*INT(CO/6)
4190 A(RO,8)=0:GOSUB 5430:RETURN
4200 REM ***
4210 REM ACCEPT PLAYER COMMAND
4220 IF INKEY$<>"" THEN 4220
4230 PRINT:PRINT "WHAT DO YOU WANT TO DO?":PRINT
4240 Z$=INKEY$
4250 IF Z$="" THEN 4240 
4260 IF Z$="Q" THEN PRINT "COWARD..QUITTER...TURNCOAT...":GOTO 190
4270 IF Z$="F" AND A(RO,8)=0 THEN PRINT "THERE IS NOTHING TO FIGHT":GOTO 4220
4280 IF Z$="F" THEN GOSUB 3690
4290 IF Z$="P" AND A(RO,7)=0 THEN PRINT "THERE IS NOTHING TO PICK UP":GOTO 4220
4300 IF Z$="G" THEN GOSUB 3520
4310 IF Z$="R" AND RND(10)<4 AND A(RO,8)<>0 THEN PRINT" NO, YOU MUST STAND AND FIGHT":Z$="F"
4320 IF Z$="R" THEN INPUT "WHICH DIRECTION WILL YOU RUN";Z$:GOTO 4350
4330 IF Z$="P" THEN GOSUB 3440
4340 IF Z$="F" THEN GOSUB 3690
4345 IF A(RO,8)<>O THEN 4220
4350 IF Z$="N" AND A(RO,1)=0 THEN PRINT "NO EXIT THAT WAY":GOTO 4220
4360 IF Z$="S" AND A(RO,2)=0 THEN PRINT "THERE IS NO EXIT SOUTH":GOTO 4220
4370 IF Z$="E" AND A(RO,3)=0 THEN PRINT "YOU CANNOT GO IN THAT DIRECTION":GOTO 4220
4380 IF Z$="W" AND A(RO,4)=0 THEN PRINT"YOU CANNOT MOVE THROUGH SOLID STONE":GOTO 4220
4390 IF Z$="U" AND A(RO,5)=0  THEN PRINT"THERE IS NO WAY UP FROM HERE":GOTO 4220
4400 IF Z$="D" AND A(RO,6)=0 THEN  PRINT "YOU CANNOT DESCEND FROM HERE":GOTO 4220
4410 IF RO=15 AND C(2)=0 AND Z$="W" THEN PRINT "YOU NEED THE SILVER KEY TO UNLOCK THE":PRINT"DOOR":GOTO 4220
4420 IF RO=22 AND C(3)=0 AND Z$="W" THEN PRINT"YOU NEED THE GOLD KEY TO UNLOCK THE DOOR";:GOTO 4220
4430 IF Z$="N" THEN RO=A(RO,1)
4440 IF Z$="S" THEN RO=A(RO,2)
4450 IF Z$="E" THEN RO=A(RO,3)
4460 IF Z$="W" THEN RO=A(RO,4)
4470 IF Z$="U" THEN RO=A(RO,5)
4480 IF Z$="D" THEN RO=A(RO,6)
4490 RETURN
4500 REM ***
4510 REM INITIALISE
4520 CLS:PRINT:PRINT"ONE MOMENT PLEASE..."
4530 DIM A(43,8),C(9),T$(18),M$(19)
4540 TA=0:LI=0:RO=6:CA=100:MK=0:M=3
4550 ST=3*RND(6)
4560 CH=3*RND(6)
4570 DE=3*RND(6)
4580 IN=3*RND(6)
4590 WI=3*RND(6)
4600 CO=3*RND(6)
4610 REM ***
4620 REM ROOM DIRECTIONS
4630 FOR B=1 TO 43
4640 FOR C=1 TO 8
4650 READ A(B,C)
4660 NEXT C:NEXT B
4670 REM ***
4680 REM ALLOT MONSTERS
4690 FOR J=1 TO 15
4700 T=RND(43):IF T=6 OR T=31 OR T=4 OR T=21 THEN 4700
4710 IF A(T,8)<>0 THEN 4700
4720 A(T,8)=J
4730 NEXT J
4740 REM ***
4750 REM ALLOT TREASURE
4760 FOR J=4 TO 18
4770 T=RND(43):IF T=6 OR T=31 THEN 4770
4780 IF A(T,7)<>0 THEN 4770
4790 A(T,7)=J
4800 NEXT J
4810 REM ***
4820 INPUT "WHAT IS YOUR NAME, WIZARD";N$
4830 CLS
4840 REM ***
4850 REM NAME TREASURES/MONSTERS
4860 FOR J=1 TO 18
4870 READ T$(J)
4880 NEXT J
4890 FOR J=1 TO 19
4900 READ M$(J)
4910 NEXT J
4920 RETURN
4930 REM *****
4940 DATA 1,4,1,8,0,0,0,0
4950 DATA 0,5,3,0,0,0,0,0
4960 DATA 3,7,3,2,0,0,0,0
4970 DATA 1,0,5,0,0,0,2,0
4980 DATA 2,0,0,4,0,0,0,0
4990 DATA 0,0,7,0,0,0,1,0
5000 DATA 3,14,15,6,0,0,0,0
5010 DATA 1,8,8,8,0,0,0,0
5020 DATA 10,11,0,0,0,0,0,0
5030 DATA 0,0,11,9,0,0,0,0
5040 DATA 9,13,12,10,0,0,0,0
5050 DATA 0,0,0,11,0,0,0,0
5060 DATA 11,16,0,44,0,0,0,0
5070 DATA 7,0,0,0,0,0,0,0
5080 DATA 7,45,0,12,0,0,0,0
5090 DATA 0,19,0,17,0,37,0,0
5100 DATA 0,0,16,0,0,0,0,0
5110 DATA 0,30,0,0,0,34,0,0
5120 DATA 16,28,0,0,0,43,0,0
5130 DATA 0,31,22,0,0,0,0,0
5140 DATA 0,23,0,45,0,0,3,0
5150 DATA 0,24,0,20,0,0,0,0
5160 DATA 21,25,0,0,0,0,0,0
5170 DATA 22,0,25,0,0,0,0,0
5180 DATA 23,27,30,24,0,0,0,0
5190 DATA 0,29,27,0,0,0,0,0
5200 DATA 25,0,0,26,0,0,0,0
5210 DATA 19,28,28,28,0,47,0,0
5220 DATA 26,29,29,29,0,0,0,0
5230 DATA 18,0,0,25,0,0,0,0
5240 DATA 20,0,0,0,0,0,0,0
5250 DATA 0,0,34,0,0,47,0,0
5260 DATA 34,36,0,35,0,0,0,0
5270 DATA 34,33,34,32,18,0,0,0
5280 DATA 33,38,36,0,0,0,0,0
5290 DATA 33,39,46,35,0,0,0,0
5300 DATA 0,40,0,0,16,0,0,0
5310 DATA 35,0,0,0,0,41,0,0
5320 DATA 36,39,40,39,0,0,0,0
5330 DATA 37,0,0,39,0,0,0,0
5340 DATA 0,0,42,0,38,0,0,0
5350 DATA 42,43,42,41,0,47,0,0
5360 DATA 0,0,42,0,19,0,0,0
5370 REM ***
5380 REM TREASURES/MONSTERS
5390 DATA FLAMING TORCH,SILVER KEY,GOLD KEY,SWORD,WAR HAMMER,CHAIN MAIL ARMOR,SHIELD,CLOAK OF PROTECTION,WAND OF FIREBALLS
5400 DATA EMERALDS,SILVER RINGS,ELVEN AMYTHESTS,DIAMOND DRAGON EYES,CRYSTAL BALL,PIECES OF EIGHT,ELEMENTAL GEMS,SHAPE-SHIFTING STONES,GOLD DUBLOONS
5410 DATA SWASHBUCKLER,WEREBEAR,CAEDLIAE,MATICORE,VAMPIRE,PREDEBEAST,GARGOYLE,MEDUSAE,MAGI,FIRE LIZARD,PHASE SPIDER
5420 DATA TROLL,HELL HOUND,FROST GIANT,NECROMANCER,HYDRA OF 10 HEADS,PATRIACH,MASTER THIEF,LIVING STATUE
5430 FOR I=1 TO 499:NEXT I:RETURN

6000 CLS:M$="WELCOME TO THE ADVENTURE GAME CITADEL OF PERSHU!":GOSUB1
6001 M$="USE THESE LETTERS TO MOVE AROUND: n=NORTH s=SOUTH e=EAST w=WEST f=FIGHT r=RUN p=PICK UP g=GET RID OF u=UP d=DOWN q=QUIT.":GOSUB1
6010 M$="WRITE THESE DOWN SO YOU DON'T FORGET THEM. DRAW A MAP AS YOU PLAY SO YOU DON'T GET LOST. GOOD LUCK! YOU WILL NEED IT!":GOSUB1
6011 PRINT"PRESS ANY KEY TO PLAY"
6020 A$=INKEY$:IF A$<>""THEN6020
6030 RETURN
