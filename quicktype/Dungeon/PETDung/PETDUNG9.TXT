0 CLS:CLEAR500,26384:TS=26388:M$="350731190030":J=RND(-(PEEK(9)*256+PEEK(10)))
90 DIMAX,N,N1,Y,O,V,V1,S,I,E,QQ,L,W,A,O1,E1:GOSUB6200
100 RS=16:CS=32:SZ=RS*CS:BL=(16-RS)*32:RS=RS-1:CS=CS-1
110 HP=50:MG=0:EX=0:PX=0:Z=0:W$="‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰"
180 CLS0:PRINT@0,"SETTING UP..."
190 AX=16384
200 FORI=TS+32TOTS+SZ-33:POKEI,143:NEXTI
210 HG=0:FG=0:K1=0:E=0:S=0:W=32:ET=32:D=0:TI=0
230 GOSUB380:S=0:REM FORT=32TO511:POKEAX+T,PEEK(TS+T):NEXT

240 CLS0:TS=TS-BL:PRINT@32,W$;:FORJ=1TO15:POKEAX+32*J,137:POKEAX+32*J+31,137:NEXT:PRINT@480,LEFT$(W$,31);
250 L=INT(RND(0)*SZ+TS):IFPEEK(L)<>32THEN250
260 GOSUB1410:L=L+AX-TS:W=PEEK(L):GOSUB600:POKEL,24:W=32

270 GOSUB1250:IFD>.THEN1350
280 HP=HP-.15-2*SX:IFHP<.THENGOSUB1190:IFD>.THEN1350
290 Q=VAL(MID$("646566323334000102",A*2-1,2))-33:J=PEEK(L+Q):IFJ=143ANDSX<>1THEN270
310 IFJ=137THEN270
320 POKEL,W:L=L+Q:W=PEEK(L):POKEL,25:GOSUB600:POKEL,24:SX=.
330 IFW=42THENGOSUB1200:IFD>.THEN1350
340 IFW<>32THENIFW<>28THENIFW>=.THENIFW<=35THENGOSUB1000:IFD>.THEN1350
350 IFE>.THENS=S+1
360 IFS>1THENGOSUB830
370 GOTO270

380 PRINT@32,99-TI:TI=TI+1
400 W=INT(RND(0)*7+2):L=INT(RND(0)*7+2):I=INT(RND(0)*(RS-L)):J=INT(RND(0)*(CS-W)):P=TS+32*I+J
420 IFP+32*L+W>=TS+SZTHEN530
430 FORN=0TOL:FORN1=0TOW+1:IFPEEK(P+(N*32)+N1)<>143THENN1=W+1:NEXT:N=L:NEXT:GOTO530
440 NEXT:NEXT
450 FORN=1TOL:FORN1=1TOW:POKEP+N*32+N1,32:NEXTN1,N
460 FORN=P+34+L*32TOTS+999STEP32
470 IFPEEK(N)=32THENFORN1=P+34TONSTEP32:POKEN1,32:NEXT:POKEN1-64,28:N=TS+999
480 NEXT
490 FORN=P+65+WTOP+97+W:IF(N-TS)/32=INT((N-TS)/32)THEN520
500 IFPEEK(N)=32THENFORN1=P+65TON-1:POKEN1,32:NEXT:POKEN1-1,28:N=P+97+W
510 NEXT
520 S=RND(L):S1=RND(W):POKEP+S1+S*32,VAL(MID$(M$,INT(RND(0)*6)*2+1,2))
530 IFTI<100THEN380
540 FORN=1TO11
550 U=INT(RND(0)*SZ)+TS:IFPEEK(U)<>32THEN550
560 POKEU,42:HG=HG+1:NEXT
570 FORI=1TORS:POKETS+32*I,137:POKETS+32*I+CS,137:NEXTI
580 FORJ=0TOCS:POKETS+J+32,137:POKETS+J+32*RS,137:NEXTJ
590 RETURN

595 K1=1+K1+RND(INT(MG/3+1)):GN=GN+1:IFGN>FGTHENGOSUB1410:PRINT"GOLD IS NEAR! ";:GOSUB1430:FG=HG+1
596 GOTO820

600 GN=.:O=L-AX-2:FORN=-32TO32STEP32:FORN1=1TO3:IFN=.THENIFN1=2THEN820
650 Y=O+N+N1:V=PEEK(Y+TS):POKEY+AX,V:IFV=42THEN595
660 IFV=28ORV=32ORV>95THEN820
670 GOTO710
710 V1=V:S=.:POKEY+TS,32
720 IFV=35THENE$="SPIDER":I=3
730 IFV=7THENE$="GRUE":I=7
740 IFV=31THENE$="DRAGON":I=1
750 IFV=19THENE$="SNAKE":I=2
760 IFV=0THENE$="NUIBUS":I=9
770 IFV=30THENE$="WYVERN":I=5
780 I=INT(RND(0)*HP+(PX/I)+HP/4):IFE>.THENPOKETS+E,QQ
800 QQ=V:E=Y
810 GOSUB1410:PRINT"A "E$" WITH"I"HP IS NEAR. ";:GOSUB1430:CC=I
820 NEXTN1:NEXTN:FG=GN:RETURN

830 O1=.:A=.:E1=E+AX:IFABS(E1+32-L)<ABS(E1-L)THENA=32
840 IFABS(E1-32-L)<ABS(E1-L)THENA=-32
850 J=PEEK(E+A+TS):IFJ<>28THENIFJ<138THENO1=O1+A
860 IFABS(E1-1-L)<ABS(E1-L)THENA=-1
870 IFABS(E1+1-L)<ABS(E1-L)THENA=1
880 J=PEEK(E+O1+A+TS):IFJ<>28THENIFJ<138THENO1=O1+A
890 A=O1:IFE1+A=LTHEN960
900 IFE1+A<LTHEN940
910 IFA=33THENA=31
920 IFA=-31THENA=-33
930 GOTO960
940 IFA=31THENA=33
950 IFA=-33THENA=-31
960 J=PEEK(E+A+TS):IFJ=28ORJ>136THENPOKEE1,V1:RETURN
970 POKEE1,ET:E=E+A:ET=PEEK(E+AX):POKEE+AX,V1:IFE+AX=LTHENET=32:GOSUB1000
980 RETURN

1000 GOSUB1410:PRINT"AN ATTACK!!! ";:SOUND1,8:GOSUB1430:J=HP+EX
1010 H1=RND(1)*CC/2+CC/4:CH=RND(0)*J/2+J/4:CC=INT(CC-CH):HP=INT(HP-H1)
1020 IFHP<1THEN1190
1030 IF(CC/(HP+1)<2)THEN1090
1040 GOSUB1410:PRINT"THE "E$" WILL LEAVE... ";:GOSUB1430:GOSUB1430
1050 GOSUB1410:PRINT"IF YOU GIVE IT HALF YOUR GOLD. ";:GOSUB1430:GOSUB1430
1060 GOSUB1410:PRINT"WILL YOU CONSENT (Y OR N)";
1070 GOSUB1500:GOSUB1410
1080 IFL$="Y"THENMG=INT(MG-MG/2):W=32:E=0:S=0:POKEL,24:RETURN
1090 IFCC>1THEN1160
1100 GOSUB1410:W=32:S=0:E=0:POKEL,24:PRINT"THE "E$" IS DEAD! ";:GOSUB1430
1110 EX=EX+I:Z=Z+1
1120 IFEX<PX*2THEN1180
1130 PX=EX:HP=HP*3
1140 GOSUB1410:PRINT"YOUR HP HAVE BEEN RAISED. ";:GOSUB1430
1150 GOTO1180
1160 GOSUB1410:PRINT"THE "E$" HAS"CC"HP. ";:GOSUB1430
1170 GOSUB1340
1180 RETURN
1190 GOSUB1410:PRINT"YOU'RE DEAD! ";:SOUND40,4:SOUND20,4:GOSUB1430:D=1:RETURN

1200 MG=MG+K1:GOSUB1410:PRINT"YOU FOUND"K1"GP! ";:SOUND150,1:SOUND200,1:GOSUB1430
1210 POKEL-AX+TS,32:W=32:HG=HG-1:IFHG>.THENRETURN
1230 GOSUB1410:PRINT"YOU FOUND ALL THE GOLD! ";:SOUND140,4:SOUND160,4:GOSUB1430:D=2:RETURN

1250 GOSUB1340:IFD>.THENRETURN
1260 L$=INKEY$:IFL$=""THEN1260
1270 FORJ=1TO9:IFMID$("ZXCASDQWE",J,1)=L$THENL$=CHR$(48+J):J=8
1272 NEXT:IFL$="5"THENL$="2"
1280 A=ASC(L$):A=AAND127:IFA=82THEN:A=53:IFW<>143THENGOSUB1410:PRINT"RESTING... ";:GOSUB1430:HP=HP+1+SQR(EX/HP)
1300 IFA>48ANDA<58THENA=A-48:GOSUB1410:RETURN
1310 IFA=32THENFORJ=1TO7:SOUND150+10*J,1:NEXT:SX=1
1320 IFA=21THENGOSUB1410:PRINT"QUIT: ARE YOU SURE? (Y/N)";:GOSUB1500:GOSUB1410:IFL$="Y"THEND=1
1330 GOTO1250

1340 PRINT@0,"HP"INT(HP+.5):PRINT@11,"EX"EX:PRINT@21,"GP"MG:RETURN

1350 PRINT@0,"BEASTS"Z:PRINT@11,"EX"EX:PRINT@21,"GP"MG
1360 FORN=BL+32TOSZ-1+BL:A=PEEK(TS+N):POKEAX+N,A:NEXT:IFD=1THENPOKEL,191:HP=50:MG=0:EX=0:PX=0:Z=0:GOTO1385
1380 GOSUB1410:PRINT"CONTINUE TO NEXT LEVEL";:GOTO1390
1385 GOSUB1410:PRINT"WANT TO PLAY AGAIN";
1390 GOSUB1500:IFL$="Y"THEN180
1400 IFL$<>"N"THEN1380
1401 CLS:END

1410 PRINT@32,W$;:PRINT@32,;:RETURN
1430 FORJ=1TO1500:NEXT:RETURN

1500 TI=.:PRINT"  ";
1510 L$=INKEY$:IFL$<>""THEN1550
1520 TI=TI+1:IFTI=1THENPRINTCHR$(8)CHR$(8)"? ";:GOTO1510
1521 IFTI=25THENPRINTCHR$(8)CHR$(8)"  ";:GOTO1510
1530 IFTI=50THENTI=.
1531 GOTO1510
1550 PRINTCHR$(8)CHR$(8)"? "L$;:RETURN

6200 CLS:PRINT@7,"CURSOR #15 DUNGEON"
6210 PRINTTAB(1)"COPYRIGHT 1979 BY BRIAN SAWYER"," ";:FORJ=1TO30:PRINT"Œ";:NEXT:PRINT:PRINT
6230 PRINT" SEARCH FOR GOLD IN THE ANCIENT":PRINT" RUINS!":PRINT:PRINT" USE: wasd TO MOVE (AND qezxc)."
6240 PRINT" space TO PASS THROUGH WALLS.":PRINT" r TO REST AND ctrl-q TO QUIT.":PRINT
6280 PRINTTAB(6)"PRESS enter TO BEGIN"
6290 IFINKEY$<>CHR$(13)THEN6290
6300 CLS:RETURN
