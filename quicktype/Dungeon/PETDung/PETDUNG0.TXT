0 CLR:POKE59468,12
1 REM DUNGEON COPYRIGHT (C) 1979 BRIAN SAWYER
2 REM 1310 DOVER HILL ROAD
3 REM SANTA BARBARA, CA. 93103
4 :
5 REM CURSOR #15, NOV/DEC 1979
6 REM BOX 550, GOLETA, CA. 93017
7 REM LINES 61000-65000 (C) 1979 CURSOR MAGAZINE
8 :
10 REM AS OF 12/29/79
90 PG$="DUNGEON":NM$="15":GOSUB62000
100 RS=23:CS=40:SZ=RS*CS:BL=(25-RS)*40:RS=RS-1:CS=CS-1
130 REM TRICK: DG$() STRINGS GO AT END OF MEMORY!
140 DIMDG$(24):E$="                                        "
150 FORI=0TO24:DG$(I)=E$+"":NEXTI
160 ER$=CHR$(19)+CHR$(17)+"                                       "+CHR$(19)+CHR$(17)
170 E2$=CHR$(19)+"                                       "+CHR$(19)
180 PRINTCHR$(147);"SETTING UP..."
190 TS=PEEK(QM)+256*PEEK(QM+1)-SZ:AX=32768
200 FORI=TS+40TOTS+SZ-41:POKEI,32:NEXTI
210 HP=50:MG=0:EX=0:PX=0:HG=0:Z=0:FG=0:K1=0:E=0:S=0:W=160:ET=160
220 TI$="000000":TM=TI+3600
230 GOSUB380
240 TS=TS-BL
250 L=INT(RND(1)*SZ+TS):IFPEEK(L)<>160THEN250
260 TM=0:GOSUB1410:L=L+AX-TS:W=PEEK(L):GOSUB600:POKEL,209:W=160
270 POKEQK,0:POKEQP,255:GOSUB1240
280 HP=HP-.15-2*SX:IFHP<0THEN1190
290 Q=VAL(MID$("808182404142000102",A*2-1,2))-41
300 IF(PEEK(L+Q)=32)AND(SX<>1)THEN270
310 IFPEEK(L+Q)=127THEN270
320 POKEL,W:L=L+Q:W=PEEK(L):POKEL,209:GOSUB600:POKEL,209
330 IFW=135THENGOSUB1200
340 IFW>=214ANDW<=219THENGOSUB1000
350 IFE>0THENS=S+1
360 IFS>1THENGOSUB830
370 GOTO270
380 PRINTINT((TM-TI)/60);CHR$(157);"  ";CHR$(145)
400 W=INT(RND(1)*9+2):L=INT(RND(1)*9+2)
410 R0=INT(RND(1)*(RS-L-1))+1:C0=INT(RND(1)*(CS-W-1))+1:P=TS+40*R0+C0
420 IFP+40*L+W>=TS+SZTHEN530
430 FORN=0TOL+1:FORN1=0TOW+1:IFPEEK(P+(N*40)+N1)<>32THEN530
440 NEXTN1,N
450 FORN=1TOL:FORN1=1TOW:POKEP+(N*40)+N1,160:NEXTN1,N
460 FORN=P+42+(L*40)TOTS+999STEP40
470 IFPEEK(N)=160THENFORN1=P+42TONSTEP40:POKEN1,160:NEXT:POKEN1-80,102:GOTO490
480 NEXTN
490 FORN=P+81+WTOP+121+W:IF(N-TS)/40=INT((N-TS)/40)THEN520
500 IFPEEK(N)=160THENFORN1=P+81TON-1:POKEN1,160:NEXT:POKEN1-1,102:GOTO520
510 NEXT
520 S=INT(RND(1)*L)+1:S1=INT(RND(1)*W+1):POKEP+S1+S*40,INT(RND(1)*6+214)
530 IFTI<TMTHEN380
540 FORN=1TO11
550 U=INT(RND(1)*SZ)+TS:IFPEEK(U)<>160THEN550
560 POKEU,135:HG=HG+1:NEXT
570 FORR0=0TORS:POKETS+40*R0,127:POKETS+40*R0+CS,127:NEXTR0
580 FORC0=0TOCS:POKETS+C0,127:POKETS+C0+40*RS,127:NEXTC0
590 RETURN
600 K=-40:J=3:M=40:R=3:GN=0
610 IFSM=1THENK=-80:J=5:M=80:R=4:SM=0
620 O=L-32767-R
630 IFO+32811>33768THENM=0
640 FORN=-40TO40STEP40:FORN1=1TO3:IFN=0ANDN1=2THEN820
650 Y=O+N+N1:V=PEEK(Y+TS):POKEY+AX,V
660 IFV<135ORV=160THEN820
670 V=V-128:IFV<>7THEN710
680 K1=1+K1+INT((MG+1)*(RND(1)))
690 GN=GN+1:IFGN>FGTHENGOSUB1410:PRINT"GOLD IS NEAR!":GOSUB1430:FG=HG+1
700 GOTO820
710 V1=V+128:S=0:POKEY+TS,160
720 IFV=86THENE$="SPIDER":I=3
730 IFV=87THENE$="GRUE":I=7
740 IFV=88THENE$="DRAGON":I=1
750 IFV=89THENE$="SNAKE":I=2
760 IFV=90THENE$="NUIBUS":I=9
770 IFV=91THENE$="WYVERN":I=5
780 I=INT(RND(1)*HP+(PX/I)+HP/4)
790 IFE>0THENPOKETS+E,QQ
800 QQ=V+128:E=Y
810 GOSUB1410:PRINT"A "E$;" WITH";I;"POINTS IS NEAR.":GOSUB1430:CC=I
820 NEXTN1:NEXTN:FG=GN:RETURN
830 O1=0:A=0:E1=E+AX:IFABS(E1+40-L)<ABS(E1-L)THENA=40
840 IFABS(E1-40-L)<ABS(E1-L)THENA=-40
850 IF(PEEK(E+A+TS)=209)OR(PEEK(E+A+TS)>128)THENO1=O1+A
860 IFABS(E1-1-L)<ABS(E1-L)THENA=-1
870 IFABS(E1+1-L)<ABS(E1-L)THENA=1
880 IF(PEEK(E+O1+A+TS)=209)OR(PEEK(E+O1+A+TS)>128)THENO1=O1+A
890 A=O1:IFE1+A=LTHEN960
900 IFE1+A<LTHEN940
910 IFA=41THENA=39
920 IFA=-39THENA=-41
930 GOTO960
940 IFA=31THENA=41
950 IFA=-41THENA=-39
960 IFPEEK(E+A+TS)<128THENPOKEE1,V1:RETURN
970 POKEE1,ET:E=E+A:ET=PEEK(E+AX):POKEE+AX,V1:IFE+AX=LTHENET=160:GOSUB1000
980 RETURN
990 QQ=V+128:E=Y
1000 GOSUB1410:PRINTCHR$(18);"AN ATTACK!!!":GOSUB1430:PW=HP+EX
1010 H1=RND(1)*CC/2+CC/4:CH=RND(1)*PW/2+PW/4:CC=INT(CC-CH):HP=INT(HP-H1)
1020 IFHP<1THEN1190
1030 IF(CC/(HP+1)<2)THEN1090
1040 GOSUB1410:PRINT"THE "E$" WILL LEAVE..":GOSUB1430
1050 GOSUB1410:PRINT"IF YOU GIVE IT HALF YOUR GOLD.":GOSUB1430
1060 GOSUB1410:PRINT"WILL YOU CONSENT TO THIS (Y OR N) ";
1070 GOSUB1500
1080 IFL$="Y"THENMG=MG-MG/2:W=160:E=0:S=0:POKEL,209:RETURN
1090 IFCC>1THEN1160
1100 GOSUB1410:W=160:S=0:E=0:POKEL,209:PRINT"THE "E$" IS DEAD!":GOSUB1430
1110 EX=EX+I:Z=Z+1
1120 IFEX<PX*2THEN1180
1130 PX=EX:HP=HP*3
1140 GOSUB1410:PRINT"YOUR HIT PTS. HAVE BEEN RAISED.":GOSUB1430
1150 GOTO1180
1160 GOSUB1410:PRINT"THE "E$" HAS"CC"HIT POINTS":GOSUB1430
1170 GOSUB1340
1180 RETURN
1190 GOSUB1410:PRINT"YOU'RE DEAD!":GOSUB1430:GOTO1350
1200 MG=MG+K1:GOSUB1410:PRINT"YOU FOUND"K1"GOLD PIECES!":GOSUB1430
1210 POKEL-AX+TS,160:W=160
1220 HG=HG-1:IFHG>0THENRETURN
1230 GOTO1350
1240 IFIU=0THENGOSUB1430
1250 GOSUB1340
1260 IFIUTHENIFTI>TMTHENGOSUB1410:PRINT"YOU MAY MOVE."
1270 GETL$:IFL$=""THEN1260
1280 A=ASC(L$):SX=ABS(A>127):A=AAND127
1290 IFA=ASC("5")THENHP=HP+1+SQR(EX/HP)
1300 IFA>48ANDA<58THENA=A-48:TM=0:GOSUB1410:RETURN
1310 IFL$="S"THENSM=1:HP=HP-2
1320 IFL$="Q"THEN1350
1330 GOTO1250
1340 PRINTE2$;"HIT PTS.";INT(HP+.5);CHR$(157);"  EXP.";EX;CHR$(157);"  GOLD";MG;"   ":RETURN
1350 GOSUB1410:PRINTE2$;"GOLD:";MG;" EXP:";EX;" KILLED";Z;"BEASTS"
1360 FORN=BLTOSZ-1+BL:A=PEEK(TS+N):POKEAX+N,A:NEXT
1375 GETL$:IFL$<>""THEN1375
1380 GOSUB1410:PRINT"WANT TO PLAY AGAIN";
1390 GOSUB1500:IFL$<>"N"THEN180
1400 TM=0:GOSUB1410:PRINTCHR$(145);:END
1410 IFIUTHENIFTI<TMTHEN1410
1420 PRINTER$;:IU=0:RETURN
1430 TM=TI+3*60:IU=1:RETURN
1440 IFPEEK(32768)=0THENSTOP
1450 RETURN
1500 ZT=0:ZC=1
1510 GETL$:IFL$<>""THEN1550
1520 IFTI>ZTTHENPRINTMID$("? ",ZC,1);CHR$(157);:ZT=TI+30:ZC=3-ZC
1530 GOTO1510
1550 PRINT"? ";L$:RETURN
60300 PRINTCHR$(147):CLR:GOSUB60400:GOTO100
60400 QK=525:QM=134:QP=515:CR$=CHR$(13)
60410 IFPEEK(50000)=0THENRETURN
60420 QK=158:QM=52:QP=151
60430 RETURN
60500 FORI=1TO10:PRINTCHR$(192);CHR$(192);CHR$(192);CHR$(192);:NEXTI:RETURN
62000 PRINTCHR$(147);CHR$(17);CHR$(17);TAB(9);"CURSOR #";NM$;"  ";PG$
62010 PRINTCHR$(17);" COPYRIGHT (C) 1979  BY BRIAN SAWYER";CHR$(17)
62020 GOSUB60500
62030 PRINTCHR$(17);"SEARCH FOR GOLD IN THE ANCIENT RUINS"
62080 PRINTCHR$(17);CHR$(17);CHR$(17);"PRESS ";CHR$(18);"RETURN";CHR$(146);" TO BEGIN"
62090 GETT$:IFT$=""THEN62090
62100 GOTO60300