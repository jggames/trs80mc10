0 CLS:CLEAR500,26384:TS=26388:MO$="997195836494":ZZ=0
90 PG$="DUNGEON":NM$="15":GOSUB6200
100 RS=16:CS=32:SZ=RS*CS:BL=(16-RS)*32:RS=RS-1:CS=CS-1
130 REM MEMORY TRICK NOT NEEDED
180 PRINT@0,"SETTING UP..."
190 AX=16384
200 FORI=TS+32TOTS+SZ-33:POKEI,96:NEXTI
210 HP=50:MG=0:EX=0:PX=0:HG=0:Z=0:FG=0:K1=0:E=0:S=0:W=160:ET=160
230 GOSUB380
231 FORT=0TO511:POKE16384+T,PEEK(TS+T):NEXT:CLS6

240 TS=TS-BL
250 L=INT(RND(0)*SZ+TS):IFPEEK(L)<>160THEN250
260 TM=0:GOSUB1410:L=L+AX-TS:W=PEEK(L):GOSUB600:POKEL,24:W=160

270 POKEQK,0:POKEQP,255:GOSUB1240
280 HP=HP-.15-2*SX:IFHP<0THEN1190
290 Q=VAL(MID$("646566323334000102",A*2-1,2))-33
300 IF(PEEK(L+Q)=96)AND(SX<>1)THEN270
310 IFPEEK(L+Q)=137THEN270
320 POKEL,W:L=L+Q:W=PEEK(L):POKEL,24:GOSUB600:POKEL,24
330 IFW=135THENGOSUB1200
340 IFW>=214ANDW<=219THENGOSUB1000
350 IFE>0THENS=S+1
360 IFS>1THENGOSUB830
370 GOTO270

380 PRINT@16,TI:TI=TI+1
400 W=INT(RND(0)*7+2):L=INT(RND(0)*7+2)
410 R0=INT(RND(0)*(RS-L-1))+1:C0=INT(RND(0)*(CS-W-1))+1:P=TS+32*R0+C0
420 IFP+32*L+W>=TS+SZTHEN530
430 FORN=0TOL+1:FORN1=0TOW+1:IFPEEK(P+(N*32)+N1)<>96THEN530
440 NEXTN1,N
450 FORN=1TOL:FORN1=1TOW:POKEP+(N*32)+N1,160:NEXTN1,N
460 FORN=P+34+(L*32)TOTS+999STEP32
470 IFPEEK(N)=160THENFORN1=P+34TONSTEP32:POKEN1,160:NEXT:POKEN1-64,28:GOTO490
480 NEXTN
490 FORN=P+65+WTOP+97+W:IF(N-TS)/32=INT((N-TS)/32)THEN520
500 IFPEEK(N)=160THENFORN1=P+65TON-1:POKEN1,160:NEXT:POKEN1-1,28:GOTO520
510 NEXT
520 S=INT(RND(0)*L)+1:S1=INT(RND(0)*W+1):POKEP+S1+S*32,VAL(MID$(MO$,INT(RND(0)*6)*2+1,2))
530 IFTI<100THEN380
540 FORN=1TO11
550 U=INT(RND(0)*SZ)+TS:IFPEEK(U)<>160THEN550
560 POKEU,146:HG=HG+1:NEXT
570 FORR0=1TORS:POKETS+32*R0,137:POKETS+32*R0+CS,137:NEXTR0
580 FORC0=0TOCS:POKETS+C0+32,137:POKETS+C0+32*RS,137:NEXTC0
590 RETURN

600 K=-32:J=3:M=32:R=3:GN=0
610 IFSM=1THENK=-64:J=5:M=64:R=4:SM=0
620 O=L-AX-R
630 IFO>511THENM=0
640 FORN=-32TO32STEP32:FORN1=1TO3:IFN=.THENIFN1=2THEN820
650 Y=O+N+N1:V=PEEK(Y+TS):POKEY+AX,V:IFV=146THEN680
660 IFV=28ORV=96ORV>136THEN820
670 GOTO710
680 K1=1+K1+INT((MG+1)*(RND(0)))
690 GN=GN+1:IFGN>FGTHENGOSUB1410:PRINT"GOLD IS NEAR!";:GOSUB1430:FG=HG+1
700 GOTO820
710 V1=V-64:S=0:POKEY+TS,160
720 IFV=99THENE$="SPIDER":I=3
730 IFV=71THENE$="GRUE":I=7
740 IFV=95THENE$="DRAGON":I=1
750 IFV=83THENE$="SNAKE":I=2
760 IFV=64THENE$="NUIBUS":I=9
770 IFV=94THENE$="WYVERN":I=5
780 I=INT(RND(0)*HP+(PX/I)+HP/4)
790 IFE>.THENPOKETS+E,QQ
800 QQ=V-64:E=Y
810 GOSUB1410:PRINT"A "E$" WITH"I"HP IS NEAR";:GOSUB1430:CC=I
820 NEXTN1:NEXTN:FG=GN:RETURN

830 O1=0:A=0:E1=E+AX:IFABS(E1+32-L)<ABS(E1-L)THENA=32
840 IFABS(E1-32-L)<ABS(E1-L)THENA=-32
850 ZZ=PEEK(E+A+TS):IFZZ<>102THENIFZZ<>96THENIFZZ<>137THENO1=O1+A
860 IFABS(E1-1-L)<ABS(E1-L)THENA=-1
870 IFABS(E1+1-L)<ABS(E1-L)THENA=1
880 ZZ=PEEK(E+O1+A+TS):IFZZ<>102THENIFZZ<>96THENIFZZ<>137THENO1=O1+A
890 A=O1:IFE1+A=LTHEN960
900 IFE1+A<LTHEN940
910 IFA=33THENA=31
920 IFA=-31THENA=-33
930 GOTO960
940 IFA=31THENA=33
950 IFA=-33THENA=-31
960 ZZ=PEEK(E+A+TS):IFZZ>35ANDZZ<95THENPOKEE1,V1:RETURN
970 POKEE1,ET:E=E+A:ET=PEEK(E+AX):POKEE+AX,V1:IFE+AX=LTHENET=160:GOSUB1000
980 RETURN

1000 GOSUB1410:PRINT"AN ATTACK!!!";:GOSUB1430:PW=HP+EX
1010 H1=RND(1)*CC/2+CC/4:CH=RND(0)*PW/2+PW/4:CC=INT(CC-CH):HP=INT(HP-H1)
1020 IFHP<1THEN1190
1030 IF(CC/(HP+1)<2)THEN1090
1040 GOSUB1410:PRINT"THE "E$" WILL LEAVE..";:GOSUB1430
1050 GOSUB1410:PRINT"IF YOU GIVE IT HALF YOUR GOLD.";:GOSUB1430
1060 GOSUB1410:PRINT"WILL YOU CONSENT (Y OR N) ";
1070 GOSUB1500
1080 IFL$="Y"THENMG=MG-MG/2:W=160:E=0:S=0:POKEL,24:RETURN
1090 IFCC>1THEN1160
1100 GOSUB1410:W=160:S=0:E=0:POKEL,24:PRINT"THE "E$" IS DEAD!":GOSUB1430
1110 EX=EX+I:Z=Z+1
1120 IFEX<PX*2THEN1180
1130 PX=EX:HP=HP*3
1140 GOSUB1410:PRINT"YOUR HP HAVE BEEN RAISED.";:GOSUB1430
1150 GOTO1180
1160 GOSUB1410:PRINT"THE "E$" HAS"CC"HP";:GOSUB1430
1170 GOSUB1340
1180 RETURN
1190 GOSUB1410:PRINT"YOU'RE DEAD!";:GOSUB1430:GOTO1350
1200 MG=MG+K1:GOSUB1410:PRINT"YOU FOUND"K1"GP!";:GOSUB1430
1210 POKEL-AX+TS,160:W=160
1220 HG=HG-1:IFHG>0THENRETURN
1230 GOTO1350
1235 GOSUB1410:PRINT"TOO TIRED.";:GOTO1260

1240 IFIU=0THENGOSUB1430
1250 GOSUB1340
1260 IU=IU+1
1270 L$=INKEY$:IFL$=""THEN1260
1271 IFL$="W"THENL$="8":IFIU<20THEN1235
1272 IFL$="A"THENL$="4":IFIU<20THEN1235
1273 IFL$="D"THENL$="6":IFIU<20THEN1235
1274 IFL$="S"THENL$="2":IFIU<20THEN1235
1280 A=ASC(L$):SX=ABS(A>127):A=AAND127
1290 IFA=ASC("R")THENHP=HP+1+SQR(EX/HP)
1300 IFA>48ANDA<58THENA=A-48:TM=0:GOSUB1410:RETURN
1310 IFL$=" "THENSM=1:HP=HP-2
1320 IFL$="Q"THEN1350
1330 GOTO1250

1340 PRINT@0,"HP"INT(HP+.5):PRINT@16,"EX"EX:PRINT@24,"GP"MG:RETURN

1350 PRINT@0,"BEASTS"Z:PRINT@16,"EX"EX:PRINT@24,"GP"MG
1360 FORN=BLTOSZ-1+BL:A=PEEK(TS+N):POKEAX+N,A:NEXT
1375 L$=INKEY$:IFL$<>""THEN1375
1380 GOSUB1410:PRINT"WANT TO PLAY AGAIN";
1390 GOSUB1500:IFL$<>"N"THEN180
1400 TM=0:GOSUB1410:CLS:END

1410 PRINT@32,"‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰‰";:PRINT@32,;
1420 IU=0:RETURN
1430 FORZZ=1TO2500:NEXT:RETURN
1440 IFPEEK(32768)=0THENSTOP
1450 RETURN

1500 ZT=0:ZC=1
1510 L$=INKEY$:IFL$<>""THEN1550
1520 IFTI>ZTTHENPRINTMID$("? ",ZC,1);:FORZZ=1TO35:NEXT:PRINTCHR$(8);:ZT=TI+30:ZC=3-ZC
1530 GOTO1510
1550 PRINT"? ";L$:RETURN

6200 CLS:PRINT" CURSOR #15"
6210 PRINT" COPYRIGHT (C) 1979"
6220 PRINT" BY BRIAN SAWYER"
6230 PRINT" SEARCH FOR GOLD IN"
6235 PRINT" THE ANCIENT RUINS"
6280 PRINT"PRESS enter TO BEGIN"
6290 T$=INKEY$:IFT$=""THEN6290
6300 CLS:RETURN
