0 DIMC1,CC,M$,I$,MC:MC=16384:GOTO1050
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
24 W=10:G=30:R=25
27 DW=2:DG=4
28 X=2
89 M$="NUMBERS OF":GOSUB7:PRINT
90 PRINT"KNIGHTS="W;:IFWW=1THENPRINTTAB(16)"A WIZARD":GOTO92
91 PRINT
92 PRINT"RATIONS="R;:IFHH=1THENPRINTTAB(16)"A HEALER":GOTO94
93 PRINT
94 PRINT"GOLD="G;:IFGG=1THENPRINTTAB(16)"A GUIDE":GOTO96
95 PRINT
96 PRINT"KEYS="K;:IFMM=1THENPRINTTAB(16)"A DONKEY":GOTO100
97 PRINT
100 PRINT"WHICH WAY N,S,E,W?"
102 INPUTZ$:IFZ$=""THEN102
103 GOTO700
104 PRINT"YOU ARE ON A PLAIN"
106 GOTO890
120 IFW>0THENR=R-1
121 IFW>15THENR=R-1
122 IFW>30THENR=R-1
123 IFW>45THENR=R-1
124 IFW>60THENR=R-1
125 IFW>75THENR=R-1
126 IFW>90THENR=R-1
127 IFR<0THENR=0
128 IFR<1THENW=W-2:M$="NO RATIONS. 2 KNIGHTS STARVE":GOSUB1
130 IFW<1THENPRINT"NO KNIGHTS LEFT"
131 IFW<1THENM$="YOU HAVE FAILED IN YOUR QUEST":GOSUB7:PRINT
132 IFW<1THENSOUND4*9,6:SOUND4*9,6:SOUND4*9,3:SOUND7*9,6:SOUND6*9,3:SOUND6*9,3
133 IFW<1THENSOUND4*9,3:SOUND4*9,3:SOUND3*9,2:SOUND4*9,8:GOTO1000
134 YY=YY+25
135 PRINT:GOTO89
140 M$="HERE IS A REFUGE. GOING IN Y/N?":GOSUB1
141 INPUTK$:IFK$=""THEN141
142 IFK$="N"THEN280
143 IFK$="Y"THENT=INT(RND(0)*4)+4:GOTO145
144 GOTO141
145 M$="RECRUIT"+STR$(T+2)+" KNIGHTS":GOSUB1:PRINT"GATHER"T+1"RATIONS"
146 PRINT"GIVEN"T"BAGS OF GOLD"
147 W=W+T+2:IFW>99THENW=99
148 R=R+T+1:IFR>99THENR=99
149 G=G+T:IFG>W*6+MM*50THENG=W*6+MM*50
152 IFK=3THENPRINT"MORE RECRUITS":W=W*2:IFW>99THENW=99
153 GOTO120
160 M$="FIND A BAZAAR. GOING IN Y/N?":GOSUB1
161 INPUTX$:IFX$=""THEN161
162 IFX$="N"THEN280
163 IFX$<>"Y"THEN161
164 T=INT(RND(0)*5)+1:U=INT(RND(0)*3)+1:J=INT(RND(0)*5)+6
165 IFT=1THEN173
166 IFT=2THEN192
167 IFT=3THEN216
168 IFT=4THEN238
169 IFT=5THEN258
173 M$="FOR SALE rations "+MID$(STR$(U),2)" BAGS OF GOLD EACH":GOSUB1
174 PRINT"BUYING Y/N?"
175 INPUTA$
176 IFA$="N"THEN120
177 IFA$<>"Y"THEN175
182 PRINTU"BAGS OF GOLD EACH":PRINT"HOW MANY RATIONS?";:INPUTPP
184 G=G-U*PP:R=R+PP:GOTO187
186 G=G-J*U
187 IFG<0THENM$="NOT ABLE TO PAY, OWNER KILLS YOU":GOSUB1
188 IFG<0THENM$="YOU HAVE FAILED IN YOUR QUEST":GOSUB7:PRINT
189 IFG<0THENSOUND4*9,6:SOUND4*9,6:SOUND4*9,3:SOUND7*9,6:SOUND6*9,3:SOUND6*9,3:SOUND4*9,3:SOUND4*9,3:SOUND3*9,2
190 IFG<0THENSOUND4,8:GOTO1000
191 GOTO920
192 M$="FOR SALE"+STR$(U)+" knights "+MID$(STR$(J),2)+" BAGS OF GOLD EACH. BUYING Y/N?":GOSUB1
194 INPUTB$
195 IFB$="N"THEN120
196 IFB$<>"Y"THEN194
202 PRINT"THAT IS"J*U"BAGS"
204 PRINT"AGREE TO PRICE Y/N?"
206 INPUTC$
207 IFC$="Y"THEN186
208 IFC$<>"N"THEN206
210 L=INT(RND(3))
212 IFL=1THENPRINT"NO DEAL. ";:M$="BAZAAR CLOSED":GOSUB7:PRINT:GOTO120
213 J=J-1:IFJ<1THENL=1:GOTO212
214 PRINT"OK...WHAT ABOUT"J*U"BAGS?":GOTO204
216 IFMM=1THENL=1:GOTO212
218 M$="FOR SALE donkey"+STR$(J)+" BAGS GOLD. CARRIES 50 GOLD BAGS":GOSUB1
219 PRINT"BUYING Y/N?"
224 INPUTD$
225 IFD$="N"THEN120
226 IFD$<>"Y"THEN224
228 U=1:GOTO204
238 IFGG=1THENL=1:GOTO212
240 M$="FOR SALE guide"+STR$(J)+" BAGS OF GOLD. NEVER BE LOST":GOSUB1
241 PRINT"BUYING Y/N?"
242 INPUTE$
243 IFE$="N"THEN120
244 IFE$<>"Y"THEN242
245 U=1:GOTO204
258 IFHH=1THENL=1:GOTO212
260 M$="FOR SALE healer"+STR$(J)+" BAGS OF GOLD. NEVER BE ILL":GOSUB1
261 PRINT"BUYING Y/N?"
262 INPUTF$
263 IFF$="N"THEN120
264 IFF$<>"Y"THEN262
265 U=1:GOTO204
280 J=INT(RND(0)*12)+1
282 IFJ=1THEN302
284 IFJ=3THEN308
286 IFJ=5THEN318
288 IFJ=7THEN313
290 IFJ=9THEN327
291 M$="ALL CLEAR. NO DANGER":GOSUB1
301 GOTO120
302 PRINT"*UNMAPPED TERRITORY*"
303 IFGG=1THENM$="YOU HAVE A GUIDE! GAIN 2 KNIGHTS":GOSUB1
304 IFGG=1THENW=W+2:IFW>99THENW=99
305 IFGG=1THEN120
306 W=W-2:M$="NO GUIDE. LOSE 2 KNIGHTS":GOSUB1:GOTO120
308 PRINT"*PLAGUE AREA*"
309 IFHH=1THENM$="YOU HAVE A HEALER. GAIN 2 KNIGHTS":GOSUB1
310 IFHH=1THENW=W+2:IFW>99THENW=99
311 IFHH=1THEN120
312 W=W-2:M$="NO HEALER, 2 KNIGHTS DIE":GOSUB1:GOTO120
313 PRINT"*CURSED AREA*"
314 IFWW=1THENM$="YOU HAVE A WIZARD. CURSE FAILS":GOSUB1
315 IFWW=1THENPRINT"GAIN 2 KNIGHTS":W=W+2:IFW>99THENW=99
316 IFWW=1THEN120
317 M$="NO WIZARD. LOSE 1/4 OF GOLD":GOSUB1:G=3*G/4:G=INT(G):GOTO120
318 PRINT"*DRAGON ATTACKS*"
319 IFSS=1THENM$="YOU HAVE THE SWORD. SLAY DRAGON":GOSUB1
320 IFSS=1THENM$="CAPTURE"+STR$(DW)+" KNIGHTS AND"+STR$(DG)+"BAGS GOLD":GOSUB1
321 IFSS=1THENG=G+DG:W=W+DW:DW=4:DG=2:IFW>99THENW=99
322 IFSS=1ANDG>W*6+MM*50THENG=W*6+MM*50
323 IFSS=1THENPRINT"LOSE SWORD":SS=0:GOTO120
324 M$="NO SWORD. LOSE 1/4 GOLD AND KNIGHTS":GOSUB1:DG=DG+G/4:DW=DW+W/4
325 DG=INT(DG):DW=INT(DW):G=3*G/4:G=INT(G):W=3*W/4:W=INT(W)
326 GOTO120
327 J=INT(RND(0)*W)+INT(RND(0)*W)+1
328 PRINTMID$(STR$(J),2)+" BANDITS ATTACK":PRINT"YOU HAVE"W"KNIGHTS"
329 PRINT"FIGHT OR RETREAT F/R?"
330 INPUTG$
331 IFG$="R"THENW=W-2:PRINT"LOSE 2 KNIGHTS IN RETREAT":GOTO120
332 IFG$="F"THEN336
333 GOTO330
336 P=INT(RND(0)*6)+1:Q=INT(RND(0)*7)+3
338 W=W-P:J=J-Q
340 IFW<1THEN130
342 IFJ<1THENPRINT"LOSE"P"KNIGHTS IN THE BATTLE":GOTO360
343 PRINT"LOSE"P"KNIGHTS IN BATTLE":PRINTMID$(STR$(Q),2)+" BANDITS DEAD"
344 PRINT"SKIRMISH OVER!"
345 IFW-J>3ANDW<20THENM$="WELL DONE. CAPTURE"+STR$(J)+" MEN":GOSUB1
346 IFW-J>3ANDW<20THENPRINT"THEY JOIN YOUR QUEST":W=W+J:GOTO360
347 GOTO363
360 IFK=3ANDQQ=1THENPRINT"WELL DONE ";:M$="QUEST COMPLETED":GOSUB7:PRINT:SOUND17*9,2
361 IFK=3ANDQQ=1THENSOUND20*9,3:SOUND17*9,2:SOUND20*9,3:SOUND17*9,3:SOUND20*9,8:GOTO1000
363 J=INT(RND(0)*8)+1
364 IFJ=1ORJ=3THEN372
366 IFJ=2ORJ=7THEN376
368 IFJ=4ORJ=5THEN380
370 IFJ=6ORJ=8THEN384
371 GOTO390
372 IFSS=1THEN390
374 SS=1:M$="reward MAGIC SWORD KILLS DRAGONS":GOSUB1:GOTO390
376 IFWW=1THEN390
378 WW=1:M$="reward WIZARD PROTECTS FROM CURSE":GOSUB1:GOTO390
380 IFK=3THEN390
382 K=K+1:PRINT"YOU HAVE NOW FOUND"K"KEYS":GOTO390
384 PRINT"GAIN 2 RECRUITS":W=W+2:IFW>99THENW=99
390 J=INT(RND(0)*10)+1
392 IFJ=5THENPRINT"NO GOLD FOUND":GOTO120
394 G=G+4:PRINT"FIND 4 BAGS OF GOLD":GOTO120
400 ONRND(2)GOTO401,405
401 M$="FIND A CAVE. GOING IN Y/N?":GOSUB1:GOTO406
405 M$="FIND SOME RUINS. GOING IN Y/N?":GOSUB1:GOSUB406
406 INPUTV$
407 IFV$="N"THEN280
408 IFV$<>"Y"THEN406
410 J=INT(RND(0)*3)+1
420 IFJ=2THEN327
422 GOTO326
432 PRINT"YOU ARE AT THE CASTLE":PRINT"USE KEYS IN PROPER ORDER"
433 IFZZ=1THEN442
434 J=INT(RND(6)):ZZ=1
436 IFJ=1THENO$="BSG"
437 IFJ=2THENO$="BGS"
438 IFJ=3THENO$="GSB"
439 IFJ=4THENO$="GBS"
440 IFJ=5THENO$="SBG"
441 IFJ=6THENO$="SGB"
442 E=1
443 M$="THE KEYS ARE GOLD, SILVER AND BRASS":GOSUB1
444 PRINT"WHICH KEY g, s OR b";:INPUTH$
446 IFH$=MID$(O$,E,(1))THENE=E+1:GOTO450
448 M$="wrong YOU ARE BACK ON THE PLAIN":GOSUB1:SOUND4,4
449 PRINT"THE CASTLE IS DUE SOUTH":X=2:GOTO120
450 IFE>3THENQQ=1:GOTO327
460 PRINT"CORRECT":SOUND28,1:GOTO444
700 IFX=1ANDZ$<>"N"THEN711
701 IFX=3ANDZ$<>"W"THEN711
702 IFX=4ANDZ$<>"W"THEN711
703 IFX=7ANDZ$<>"S"THEN711
704 IFX=9ANDZ$<>"E"THEN711
705 IFX=8ANDZ$="S"THEN711
706 IFX=6ANDZ$="W"THEN711
707 IFX=5ANDZ$="N"THEN711
708 IFX=2ANDZ$="S"ANDK<>3THEN710
709 GOTO104
710 PRINT"YOU NEED 3 KEYS TO ENTER CASTLE":GOTO100
711 PRINT"CAN'T GO THAT WAY":GOTO104
890 IFX=1ANDZ$="N"THENX=2:GOTO280
900 IFX=2ANDZ$="N"THENX=5:GOTO280
901 IFX=2ANDZ$="S"THENX=1:GOTO432
902 IFX=1ANDZ$="E"THENX=3:GOTO160
903 IFX=2ANDZ$="W"THENX=8:GOTO280
904 IFX=3THENX=2:GOTO280
905 IFX=4THENX=5:GOTO280
906 IFX=5ANDZ$="E"THENX=4:GOTO400
907 IFX=5ANDZ$="W"THENX=6:GOTO280
908 IFX=5ANDZ$="S"THENX=2:GOTO280
909 IFX=6ANDZ$="N"THENX=7:GOTO140
910 IFX=6ANDZ$="S"THENX=8:GOTO280
911 IFX=6ANDZ$="E"THENX=5:GOTO280
912 IFX=7THENX=6:GOTO280
913 IFX=8ANDZ$="N"THENX=6:GOTO280
914 IFX=8ANDZ$="E"THENX=2:GOTO280
915 IFX=8ANDZ$="W"THENX=9:GOTO405
916 IFX=9THENX=8
920 IFT=2THENW=W+U
921 IFT=3THENMM=1
922 IFT=4THENGG=1
923 IFT=5THENHH=1
924 GOTO120
1000 PRINT"YOUR SCORE IS";
1010 SC=YY+50*(GG+HH+WW+SS+MM)+1000*K+1500*E+50*(G+R+W)
1020 PRINTSC
1030 PRINT"HIT enter TO START"
1035 INPUTZZ$
1040 RUN
1050 CLS:PRINT:PRINT
1055 PRINT"     ßßßßß ß  ß  ßßßß"
1060 PRINT"       ß   ß  ß  ß"
1065 PRINT"       ß   ßßßß  ßßß"
1070 PRINT"       ß   ß  ß  ß"
1075 PRINT"       ß   ß  ß  ßßßß"
1080 PRINT:PRINT
1085 PRINT"   ßßßßß ß   ß ßßß ßßßß ßßßßß"
1090 PRINT"   ßß ßß ß   ß ß   ß      ß"
2000 PRINT"   ß   ß ß   ß ßßß ßßßß   ß"
2005 PRINT"   ßß ßß ß   ß ß      ß   ß"
2006 PRINT"   ßßßßß ßßßßß ßßß ßßßß   ß"
2007 PRINT"       ß"
2010 SOUND17*9,2:SOUND20*9,3:SOUND17*9,2:SOUND20*9,3:SOUND17*9,3:SOUND20*9,8:FORI=1TO2000:NEXT
2015 CLS
2020 GOTO24
