0 CLS:CLEAR500:DIMZZ,CC,M$:GOTO10
1 ZZ=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+31:IFZZ<=LEN(M$)THEN2
4 RETURN
10 M$="WAIT TILL THE COAST IS CLEAR...":GOSUB1:PRINT
100 DIM R$(3,3,3),RZ(6),T$(3),MZ(5),M$(5),F$(7),G$(7)
112 DIM C$(7),B$(3),BZ(3),Z$(20),S$(3),W$(3)
120 DATA "W0ROUGH","01CARVED","02STONE","$3SQUARE"
130 DATA "œ1DECORATED","03CHILD'S"," 1MISTY","L0LAMP-LIT"
140 DATA "œ1DECORATED","00MOSSY","&3PINK","L1GREY"
150 DATA "X3POSTER","02DARK","81GLOOMY","(2ROUND"
160 DATA "E1ARMOURED","P0ICY","53PEACOCK","$0OCTAGONAL"
170 DATA "X2CRYSTAL","&3TUMBLE-DOWN","L0SLIMY","H1MAGNIFICENT"
180 DATA "T3TILTED",",0TREACLY","Y1HUMPED","$2WHISPERING"
190 DATA "J0ECHOING","X1MEDICAL"," 2LACE","H3GLASS"
200 DATA "T2VAST","%2VAST","D0HERMIT'S","D1BOULDER-STREWN"
210 DATA "H2VAST","H0SLOPING","L1NARROW","L2SILKEN"
220 DATA "T0LITTERED","01VAULTED",":2SMOKY","(3FROG"
230 DATA "Y1MUSTY","02RICH","03GLITTERING"," 0BONE-STREWN"
240 DATA "P3TATTERED","40JEWELLED","00JEWELLED"," 2TORTURE"
250 DATA "E0SILVER","X1SPLENDID","02DOCTOR'S","$3WAITING"
260 DATA "œ1FLICKERING","02MUSIC","03BEAR",",0EERIE"
270 DATA "X2BIRD"," 3TINY","P0FOUL-SMELLING","(1MOULDY"
280 FOR L=0 TO 3:FOR J=0 TO 3:FOR K=0 TO 3:READ R$(J,K,L)
282 NEXT K:NEXT J:NEXT L
290 DATA "CAVE","HALL","CHAMBER","ROOM"
300 FOR J=0 TO 3:READ T$(J):NEXT J
310 DATA -16,16,4,-4,1,-1
320 DATA "UP","DOWN","NORTH","SOUTH","EAST","WEST"
330 FOR X=0 TO 5:READ MZ(X):NEXT X:FOR X=0 TO 5:READ M$(X):NEXT X
340 DATA "/01MEAN","DWARF","#41NASTY LITTLE","GNOME"
350 DATA "942BLOOD SUCKING","VAMPIRE","244FEARSOME","OGRE"
352 DATA "G00HUGE AND SMELLY","WUMPUS"
360 DATA "M13GIBBERING","ORC","Ÿ25DEADLY","MANTICORE"
362 DATA "Q40FIERY","DRAGON"
370 FOR X=0 TO 7:READ F$(X):READ G$(X):NEXT X
380 DATA "#1SILVER COINS","+1DOUBLOONS","32DOLLARS",">2SOVEREIGNS"
390 DATA "N3PIECES OF EIGHT","G4BYZANTINE COINS","R6GOLD PIECES"
392 DATA "^9TREASURE COINS"
400 FOR X=0 TO 7:READ C$(X):NEXT X
401 DATA " DAYLIGHT FILTERS DOWN FROM ABOVE..."
402 DATA ",THERE ARE TRAVEL POSTERS ALL OVER THE WALLS."
403 DATA "0RUSTED SUITS OF ARMOUR LINE THE WALLS."
404 DATA "'WATER DRIPS DOWN FROM ABOVE..."
405 DATA "5THE STAIRS ARE BROKEN AND DANGEROUS."
406 DATA "9THE GOING GETS STICKY..."
407 DATA "EBROKEN STAIRS HANG OUT OF REACH ABOVE."
408 DATA "<SOMETHING BELCHES... BELCHES... BELCHES..."
409 DATA "1RUBBLE CHOKES A DOWNWARD TUNNEL.","1IT'S COLD..."
410 DATA "6...WATCH YOUR STEP!"
411 DATA "JSIGN - 'FOLDING STAIRS / ONE WAY ONLY'"
412 DATA "ZAN EMPTY BEAR CAGE IS BOLTED TO THE WALL"
413 DATA "OSOME BONES LOOK RECENT...","Q** NO SMOKING **"
414 DATA "RA LARGE TREASURE CHEST STANDS OPEN."
415 DATA "œEXOTIC BIRDS FLIT OVERHEAD."
416 DATA "SINCOME TAX FORMS ARE STREWN ABOUT."
417 DATA "SGHOSTLY SCREAMS ARE HEARD...","DA CLOGGED TUNNEL LIES BELOW."
418 DATA "KI HEAR CROAKING... RIBBIT... RIBBIT..."
419 FOR J=0 TO 20:READ Z$(J):NEXT J
420 DATA "%DOLL","=BOTTLE OF OINTMENT","FRING","›SWORD"
422 FOR X=0 TO 3:READ B$(X):NEXT X
430 DATA ",A POSTER SAYS 'TRAVEL",";A VOICE SAYS 'SLEEP"
440 DATA "BA SCRAWLED NOTE SAYS 'BATTLE","VA DIPLOMA READS 'HEALING"
450 X=RND(10):FOR X=0 TO 3:READ S$(X):W$(X)="X"
460 FOR J=1 TO X+2:Z$=CHR$(RND(0)*15+65):W$(X)=W$(X)+Z$:NEXT J,X
470 V=20
480 IF V<15 THEN V=15:PRINT "VITALITY RESTORED TO 15."
490 J=0:K=0:L=-1:D$=">":R$="YOU ARE ABOVE GROUND OVER A CAVE.":GOTO 520
500 D$=LEFT$(R$,1):T=VAL(MID$(R$,2,1)):H$=T$(T)
510 RR$=MID$(R$,3):R$="YOU ARE IN A":LR=ASC(LEFT$(RR$,1)):IFLR>128THENLR=LR-64
512 IF LR=97 OR LR=101 OR LR=105 OR LR=111 OR LR=117 THEN RG$="N"
514 R$=R$+RG$+" "+RR$+" "+H$+"."
520 M$=R$:GOSUB1:RG$="":A=J*4+K+L*16
530 R=-1:D=ASC(D$):IFD>128THEND=D-64
531 D1=1:FOR X=0 TO 5:IF (D AND D1)>0 THEN GOSUB 1320
540 D1=D1*2:NEXT X:A1=A+32
542 IF (R=0) AND (RZ(0)+M2=0) THEN PRINT "== DEAD END =="
545 FOR X=0 TO 20:IFZ$(X)=""THEN548
546 ZZ=ASC(Z$(X)):IFZZ>128THENZZ=ZZ-64
547 IF ZZ=A1 THEN M$=MID$(Z$(X),2):GOSUB1
548 NEXT X
550 FOR X=0 TO 3:IFS$(X)=""THEN560
551 ZZ=ASC(S$(X)):IFZZ>128THENZZ=ZZ-64
552 IFZZ=A1THENTM=X:X=3:NEXTX:X=TM:GOTO570
560 NEXT X:GOTO 580
570 IF (X<>3) OR (BZ(1)=1) THEN 572
571 GOTO 580
572 M$=MID$(S$(X),2)+" SPELL WORD IS "+W$(X)+"'.":GOSUB1
580 B1=0:FOR X=0 TO 3:IFB$(X)=""THEN590
581 ZZ=ASC(B$(X)):IFZZ>128THENZZ=ZZ-64
582 IFZZ=A1THENTM=X:X=3:NEXTX:X=TM:GOTO600
590 NEXT X:GOTO 620
600 B$=MID$(B$(X),2):B1=1:B2=X
610 M$="THERE IS A "+B$+" HERE.":GOSUB1
620 C1=0:FOR X=0 TO 7:IFC$(X)=""THENC$(X)=" ":GOTO630
625 ZZ=ASC(C$(X)):IFZZ>128THENZZ=ZZ-64
626 IFZZ=A1THENTM=X:X=7:NEXTX:X=TM:GOTO640
630 NEXT X:GOTO 660
640 C1=VAL(MID$(C$(X),2,1))*100:C$=MID$(C$(X),3)
642 M$="THERE ARE"+STR$(C1)+" "+C$+" HERE.":GOSUB1
650 C2=X
660 G$="":FOR X=0 TO 7:IFF$(X)=""THENF$(X)=" ":GOTO670
664 ZZ=ASC(F$(X)):IFZZ>128THENZZ=ZZ-64
665 IFZZ=A1THENTM=X:X=7:NEXTX:X=TM:GOTO680
670 NEXT X:GOTO 710
680 G$=G$(X):M$="A "+MID$(F$(X),4)+" "+G$+" BARS YOUR WAY!":GOSUB1
690 G1=VAL(MID$(F$(X),2,1)):G2=VAL(MID$(F$(X),3,1))*100
692 IF G2=0 THEN G2=1E9
700 G3=INT(X+1)*INT(X/3+1):G5=X
710 REM INPUT MOVE
720 INPUT "MOVE";Y$:Z$=LEFT$(Y$,1)
730 J1=0:K1=0:L1=0
740 IF Z$="D" THEN L1=1
750 IF Z$="U" THEN L1=-1
760 IF Z$="N" THEN J1=1
770 IF Z$="S" THEN J1=-1
780 IF Z$="E" THEN K1=1
790 IF Z$="W" THEN K1=-1
795 IF Z$="L" AND L=-1 THEN 490
796 IF Z$="L" THEN 870
797 IF Z$="Q" THEN 1440
800 M1=K1+4*J1+16*L1:IF M1=0 THEN 880
810 FOR X=0 TO R:IF M1=RZ(X) THEN 830
820 NEXT X:PRINT "YOU CAN'T GO THAT WAY.":GOTO 720
830 IF (G$<>"") AND (M1+M2<>0) THEN 834
832 GOTO 840
834 M$="THE "+G$+" WON'T LET YOU PASS!":GOSUB1:GOTO 720
840 M9=M9+1:M2=M1:J=J+J1:K=K+K1:L=L+L1
850 IF (L=-1) AND (J+K=0) THEN 480
860 IF L=-1 THEN 1440
870 R$=R$(J,K,L):GOTO 500
880 IF (Z$="F") OR (Z$="B") OR Z$="F" OR Z$="B" THEN 1120
890 IF Z$="T" OR Z$="T" THEN 1030
900 IF Z$="X" OR Z$="X" THEN 930
910 ?:M$="NORTH SOUTH EAST WEST UP DOWN FIGHT BARGAIN TAKE":GOSUB1:?
920 GOTO 720
930 FOR X=0 TO 3:IF Y$=W$(X) THEN 960
940 NEXT X
950 PRINT "?? NOTHING HAPPENS ??":GOTO 720
960 IF (L=3) AND (X=2) THEN 950
970 IF X=0 THEN J=3:K=3:M2=3:GOTO 850
980 IF (X>0) AND (X<3) AND (G$="") THEN 950
990 S$(X)="":W$(X)=""
1000 IF X=1 THEN M$="THE "+G$+" FALLS ASLEEP.":GOSUB1:G$="":GOTO 720
1010 IF X=2 THEN M$="THE "+G$+" VANISHES!":GOSUB1:G$="":F$(G5)="a":GOTO 720
1020 V=20:PRINT "VITALITY RESTORED TO 20":GOTO 720
1030 IF B1+C1=0 THEN PRINT "?? NOTHING HERE ??":GOTO 720
1040 IF G$<>"" THEN M$="THE "+G$+" WON'T LET YOU HAVE IT!":GOSUB1:GOTO 720
1050 IF B1=0 THEN 1110
1060 BZ(B2)=1:B1=0:B$(B2)="a"+MID$(B$(B2),2)
1070 PRINT"YOU NOW HAVE: ":Z=C$(L*2)=C$(L*2+1)
1072 FOR X=0 TO 3:IF BZ(X)=0 THEN 1090
1080 M$="... "+MID$(B$(X),2):GOSUB1
1090 NEXT X:IF Z THEN M$="*** ALL TREASURE IN LEVEL"+STR$(L+1)+".":GOSUB1
1100 M$="..."+STR$(V1)+" COINS.":GOSUB1:GOTO 720
1110 E=E+1:V1=V1+C1:C1=0:C$(C2)="a":T9=T9+1:GOTO 1070
1120 IF G$="" THEN PRINT "?? NOBODY HERE ??":GOTO 720
1130 IF Z$="B" OR Z$="B" THEN 1210
1140 M$="THE "+G$+" ":G4=5:IF G3<=5 THEN G4=G3:GOTO 1170
1150 M$=M$+"IS WOUNDED!"
1160 GOTO 1180
1170 M$=M$+"IS DEAD!":G$="":F$(G5)="a":E=E+1
1180 GOSUB1:V=V-INT(G4*10/(10+E+8*BZ(2)))
1182 IF V<=0 THEN M$=" -- BUT YOU'VE BEEN KILLED TOO!":GOSUB1:GOTO 1480
1190 M$="YOUR VITALITY IS REDUCED TO"+STR$(V)+".":GOSUB1
1192 IF V<5 THEN M$="I THINK YOU NEED MAGICAL ASSISTANCE...":GOSUB1
1200 G3=G3-G4:GOTO 720
1210 IF G2>V1 THEN 1240
1220 M$="THE GREEDY SWINE OF A "+G$+" WANTS"+STR$(G2)+" COINS.":GOSUB1
1222 INPUT"OK";Z$:IF LEFT$(Z$,1)="N" THEN 1240
1230 V1=V1-G2:GOTO 1290
1240 IF G1>3 THEN 1310
1250 IF BZ(G1)=0 THEN 1310
1260 M$="THE "+G$+" WILL TAKE THE "+MID$(B$(G1),2)+".":GOSUB1
1270 INPUT"OK";Z$:IF LEFT$(Z$,1)="N" THEN 1310
1280 BZ(G1)=0
1290 M$="IT'S A DEAL! THE "+G$+" IS GONE.":GOSUB1
1300 E=E+1:G$="":F$(G5)="a":GOTO 1070
1310 M$="NO DEAL! LOOKS LIKE A SMACKDOWN IS IN ORDER...":GOSUB1:GOTO 720
1320 R=R+1:M=MZ(X):RZ(R)=M:MM$=M$(X)
1330 X$="YOU MAY GO":IF L<0 THEN 1430
1340 A1=A+M:IF A1<0 THEN 1430
1350 L1=INT(A1/16):A1=A1-L1*16:J1=INT(A1/4):K1=A1-J1*4
1360 T1=VAL(MID$(R$(J1,K1,L1),2,1))
1370 IF T=1 THEN X$="THE "+H$+" CONTINUES":GOTO 1430
1380 IF (T<>3) AND (T1<>3) THEN 1410
1390 X$="A DOORWAY OPENS":IF ABS(M)>15 THEN X$="A STAIRWAY LEADS"
1400 GOTO 1430
1410 IF (T=0) OR (T=1) THEN X$="A TUNNEL GOES":GOTO 1430
1420 X$="A PASSAGE LEADS"
1430 M$=X$+" "+MM$+".":GOSUB1:RETURN
1440 M$="YOU HAVE LEFT THE CAVE AREA WITH"+STR$(V1)+" COINS.":GOSUB1
1460 FOR X=0 TO 7
1462 IF LEFT$(C$(X),1)<>"a" THEN M$="YOU MISSED THE "+MID$(C$(X),3)+".":GOSUB1
1470 NEXT X
1480 PRINT:PRINT "YOUR SCORE IS ";
1490 IF M9=0 THEN PRINT V*2:END
1495 PRINT INT(V1*24/M9+V*2)
1497 END
1501 REM *** DISCOVERED CIRCA 1980 ON A COMMODORE PET
1503 REM ***   AT HEREFORD TECHNICAL COLLEGE
1504 REM *** RESURRECTED & BUGFIXED BY JON BRADBURY / MICROŒBRAIN
1505 REM ***   FROM A YELLOWING 31 YEAR OLD DOT MATRIX PRINTOUT
1506 REM *** HTTP://FORUM.6502.ORG
1507 REM *** FOR UK101 8K BASIC, 71 CHARACTER LINES BUT SHOULD WORK ON ALL.
