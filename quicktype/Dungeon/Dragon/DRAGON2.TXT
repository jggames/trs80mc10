0 CLEAR500:DIMCC,ZZ,M$,Z$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$=" "ORZ$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=0
2 NEXT:RETURN
3 FORZZ=1TOCC:NEXTZZ:RETURN
4 CC=2500:GOSUB3:RETURN
10 GOSUB2900:GOSUB1600:I$=""
100 REM MAIN NEW LOCATION LOOP
110 GOSUB2500:CC=2000:GOSUB3
115 IFLV=0THENGOSUB1500:GOTO900
120 IFMF=1THEN200
130 GOSUB1000
140 GOSUB3400:IFI$="L"THENM$="THAT SOUNDS INTERESTING":GOSUB1:GOSUB4:GOTO130
142 IFI$="P"THENM$="HO HUM...":GOSUB1:GOSUB4:M$="LA DI DA...":GOSUB1:GOSUB4:MF=1:PM=PM+INT(RND(0)*4):PS=PS+4:GOTO200
144 IFI$="T"THENMM=1:M$="THIS CAN BE A LITTLE UNPREDICTABLE":GOSUB4:GOSUB2800:GOTO100
150 F=0:FORL=1TOLEN(MO$):IFASC(MID$(MO$,L,1)+" ")=ASC(I$+" ")+32THENF=1
160 NEXTL:IFF=0THENM$="YOU CAN'T MOVE IN THAT DIRECTION":GOSUB1:GOSUB4:GOTO130
170 IFI$="U"THENLV=LV-1:M$="YOU'LL GET OUT OF HERE YET":GOSUB1:GOTO195
180 IFI$="D"THENLV=LV+1:M$="OFF GOES THE HERO, IN SEARCH OF YET MORE ADVENTURE":GOSUB1:GOTO195
190 M$="THE ROAD GOES EVER ON, ANON.":GOSUB1
195 CC=500:GOSUB3:GOTO100
200 REM MONSTER ENCOUNTERED HERE
210 M$="YOU JUST BUMPED INTO SOMETHING":GOSUB1:CC=1000:GOSUB3
220 IFRND(0)<0.02THENGOSUB3500:GOTO240
230 GOSUB1700
240 CC=1000:GOSUB3
250 GOSUB1100:GOSUB3400:IFI$<>"S"ANDI$<>"D"ANDI$<>"R"ANDI$<>"M"THEN250
260 IFI$="S"THENWP=1:GOSUB2000
270 IFI$="D"THENWP=2:GOSUB2000
280 IFI$="R"ANDRND(0)<0.35THENM$="YOU GOT AWAY FROM THE "+DI$:GOSUB1:GOTO450
290 IFI$="R"THENM$="YOU COULDN'T GET AWAY FROM IT":GOSUB1
300 IFI$<>"M"THEN320
310 GOSUB2200:ONOPGOSUB2800,2700,2100
320 IFMF<>0THEN490
330 IFDF=2THENDF=1:GOSUB1200
340 GOTO100
450 REM PLAYER LEFT DRAGON WOUNDED
460 IFDF=2THENDF=0:DS=MS:DM=MM
470 GOTO100
490 REM IFDF=2THENDF=0:DS=MS:DM=MM
500 REM MONSTER ATTACKS PLAYER
510 GOSUB3300
520 IFPS<=0THENGOSUB1400:GOTO900
590 CC=1500:GOSUB3:IFMF>0THEN250
600 IFDF=2THENDF=0:DS=MS:DM=MM
610 GOTO100
900 REM END OF GAME ROUTINE
999 END
1000 REM MAIN MENU
1010 CLS
1020 M$="CHOOSE WHAT YOU WANT TO DO":GOSUB1
1030 M$="==========================":GOSUB1
1040 M$="MOVEMENT:":GOSUB1
1045 GOSUB2600
1050 M$="lOOK":GOSUB1
1060 M$="pAUSE":GOSUB1
1070 M$="tELEPORT":GOSUB1
1080 M$="PRESS INITIAL LETTER TO SELECT":GOSUB1
1099 RETURN
1100 GOSUB2500
1110 CLS
1120 M$="CHOOSE WHAT YOU WANT TO DO":GOSUB1
1130 M$="==========================":GOSUB1
1140 M$="sWORD FIGHT":GOSUB1
1150 M$="dAGGER FIGHT":GOSUB1
1160 M$="rUN AWAY":GOSUB1
1170 M$="mAGIC SPELL":GOSUB1
1175 GOSUB1800
1177 GOSUB2300
1180 GOTO1080
1200 REM PLAYER HAS KILLED THE DRAGON
1210 CLS
1220 M$="WELL PLAYED BRAVE KNIGHT":GOSUB1
1230 M$="YOU HAVE SLAIN THE FOUL BEAST":GOSUB1
1240 M$="THE DRAGON, TERROR OF YOUR PEOPLE, HAS FALLEN IN BATTLE":GOSUB1
1245 GOSUB4
1250 M$="(...AND YOUR SCORE HATH RISEN....)":GOSUB1
1260 SC=SC+200
1270 DF=1
1299 RETURN
1400 REM THE PLAYER MANAGED TO GET KILLED
1410 CLS
1420 M$="NOW COMES THAT MOMENT IN THE LIFE OF EVERY TRULY GALLANT KNIGHT":GOSUB1
1430 M$="WHEN THE COURT MEETS FOR A GREAT FEAST AND ALL MAKE MERRY AND EAT AND DRINK THEIR FILL.":GOSUB1
1440 GOSUB4:M$="....EXCEPT YOU, THAT IS....":GOSUB1
1450 M$="I'M AFRAID THAT YOU JUST DIED.":GOSUB1
1460 M$="YOU MANAGED TO AMASS THE TRULY NOBLE SCORE OF"+STR$(INT(SC)):GOSUB1
1470 M$="WHY NOT TRY DIFFERENT TACTICS NEXT TIME":GOSUB1
1480 GOSUB4:GOSUB4
1499 RETURN
1500 REM THE PLAYER HAS ESCAPED
1510 CLS
1520 M$="YOU'VE DONE IT, AGAINST ALL ODDS":GOSUB1
1530 M$="YOU'VE ACTUALLY ESCAPED FROM THE DREADFUL PLACE":GOSUB1
1540 IFDF=0THENM$="YOU DIDN'T 'QUITE' MANAGE TO STAY AROUND LONG ENOUGH TO KILL THE DRAGON.":GOSUB1:GOTO1560
1545 M$="YOU EVEN MANAGED TO KILL THE FOUL, CRUEL AND AWFULLY NASTY DRAGON ON THE WAY":GOSUB1
1557 M$="THAT WON'T BE ALTOGETHER BAD FOR YOUR BANK BALANCE":GOSUB1:GOSUB4
1558 M$="OR YOUR SCORE":SC=SC+400
1560 M$="YOU HAVE SCORED"+STR$(INT(SC))+" POINTS":GOSUB1
1570 M$="PLUS A SMALL PSYCHOLOGICAL BONUS":GOSUB1:GOSUB4:M$="YOU'RE ALIVE":GOSUB1
1599 RETURN
1600 REM SET RAND VARIABLE BY MAZE NUMBER REQUESTED
1610 CLS
1620 M$="   D R A G O N  D U N G E O N":GOSUB1:PRINT
1625 M$="YOU FIND YOURSELF ON THE 3RD LEVEL OF A DUNGEON. YOU WILL MEET 21 DIFFERENT TYPES OF MONSTER-- "
1626 M$=M$+"MOST HORRIBLE OF ALL, A DRAGON CAPABLE OF INFLICTING FATAL WOUNDS. CAN YOU MAKE YOUR WAY OUT?":GOSUB1
1630 PRINT:INPUT"MAZE NUMBER";N
1650 M$="WRITE THIS NUMBER DOWN":GOSUB1
1660 M$="SO THAT YOU CAN HAVE THE SAME MAZE AGAIN IF YOU LOSE":GOSUB1
1680 CC=RND(-N)
1699 RETURN
1700 REM CHOOSE A RANDOM MONSTER
1705 L=3*LV:IFL>20THENL=20
1710 N=INT(RND(0)*L+1):M=INT(RND(0)*20+1)
1720 DI$=A$(M)+" "+M$(N)
1730 MS=INT(RND(0)*MS(N)+1)
1740 MM=INT(RND(0)*MM(N)+1)
1750 MC=MC(N)
1760 NS=MS+MM*4*MC/100
1799 RETURN
1800 REM PRINT MONSTER STATISTICS
1810 M$="THE "+DI$
1820 M$=M$+" IS "
1830 IFMS<4THENM$=M$+"NEARLY DEAD":GOTO1899
1840 IFMS<10THENM$=M$+"BADLY WOUNDED":GOTO1899
1850 IFMS>PSTHENM$=M$+"STILL DANGEROUS"
1860 IFMS<=PSTHENM$=M$+"STILL HERE"
1899 GOSUB1:RETURN
2000 REM REDUCE MS BY PLAYER FIGHTING
2010 M$="YOU ATTACK THE MONSTER....":GOSUB1:CC=1000:GOSUB3
2020 IFRND(0)*WP<0.4THENM$=".... BUT MISS":GOSUB1:GOTO2099
2030 M$=".... AND HIT,":GOSUB1
2040 N=INT(RND(0)*PS/WP+1)
2050 IFN<=MS/4THENM$="CAUSING A SMALL AMOUNT OF DAMAGE":GOSUB1:GOTO2090
2060 IFN>MS/2THENM$="CAUSING SERIOUS DAMAGE TO THE MONSTER":GOSUB1
2070 IFN>=MSTHENM$="THE MONSTER DIES FROM IT'S WOUNDS":GOSUB1
2090 MS=MS-N:IFMS<0THENMS=0
2095 IFMS<=0THENSC=SC+NS:MF=0:IFDF=2THENGOSUB1200
2099 RETURN
2100 REM REDUCE MS BY PLAYER MAGIC
2110 M$="WAVING YOUR HANDS AND CHANTING, YOU BEGIN TO SUMMON THE FIRE DEMON":GOSUB1
2120 IFRND(0)*PM<MMTHEN2125
2121 GOTO2130
2125 M$="BUT YOUR SPELL IS DISRUPTED BY YOUR FOE AND THE DEMON RETURNS TO THE HELLS WHERE IT BELONGS":GOSUB1:GOTO2199
2130 M$="AND GRADUALLY THE DEMON FORMS AROUND YOUR OPPONENT'S BODY":GOSUB1
2135 CC=1500:GOSUB3
2140 N=INT(RND(0)*PM+1)
2145 MS=MS-N:PM=PM-N
2150 IFMS<0THENMS=0 
2160 IFMS=0THENM$="THE "+DI$+" IS SLAIN BY THE FIRE DEMON":GOSUB1:SC=SC+NS:MF=0:IFDF=2THENGOSUB1200
2199 GOSUB4:RETURN
2200 REM SET TELEPORT, HEAL, ATTACK MENU
2210 CLS
2220 M$="SELECT TYPE OF SPELL":GOSUB1
2230 M$="====================":GOSUB1
2240 M$="tELEPORT":GOSUB1
2250 M$="hEAL":GOSUB1
2260 M$="aTTACK":GOSUB1
2265 GOSUB1800
2270 M$="PRESS INITIAL LETTER TO SELECT":GOSUB1
2280 OP=0:GOSUB3400
2290 IFI$="T"THENOP=1
2293 IFI$="H"THENOP=2
2296 IFI$="A"THENOP=3
2298 IFOP=0THEN2280
2299 RETURN
2300 REM DISPLAY PLAYER CHARACTERISTICS
2310 M$="YOU ARE FEELING "
2320 ONINT(PS/5)+1GOTO2330,2340,2350,2360:GOTO2360
2330 M$=M$+"VERY WEAK":GOTO2370
2340 M$=M$+"WEAK":GOTO2370
2350 M$=M$+"FAIRLY STRONG":GOTO2370
2360 M$=M$+"VERY STRONG"
2370 IFPM=0THENM$=M$+". YOU HAVE NO MAGIC LEFT":GOTO2399
2380 IFPM<5THENM$=M$+". YOU'RE RUNNING OUT OF MAGIC"
2399 GOSUB1:RETURN
2400 REM READ NAMES AND ADJECTIVES
2410 RESTORE
2420 FORL=1TO20
2430 READA$(L):NEXTL
2450 FORL=1TO20
2460 READM$(L):NEXTL
2470 FORL=1TO20
2480 READMS(L),MM(L),MC(L)
2490 NEXTL
2499 RETURN
2500 REM GENERATE MO$ MOVEMENT STRING
2510 MO$="      "
2515 FORL=65TOASC(I$+" "):A=RND(0):NEXTL
2520 IFRND(0)<0.3THENMO$="n"+MID$(MO$,2)
2530 IFRND(0)<0.3THENMO$=MID$(MO$,1,1)+"e"+MID$(MO$,3)
2540 IFRND(0)<0.3THENMO$=MID$(MO$,1,2)+"s"+MID$(MO$,4)
2550 IFRND(0)<0.3THENMO$=MID$(MO$,1,3)+"w"+MID$(MO$,5)
2560 IFRND(0)<0.07THENMO$=MID$(MO$,1,4)+"u"+MID$(MO$,6)
2570 IFRND(0)<0.07THENMO$=MID$(MO$,1,5)+"d"
2580 IFMO$="      "THEN2520
2590 IFRND(0)<0.4THENMF=1
2599 RETURN
2600 REM DESCRIBE LOCATION EXITS
2610 M$="YOU CAN MOVE: "+MO$:GOSUB1
2699 RETURN
2700 REM PLAYER CHOSE THE HEAL SPELL
2710 M$="YOU BEGIN TO HEAL YOURSELF":GOSUB1
2720 L=INT(RND(0)*3+1)
2730 IFL=1THENM$="BUT YOUR SPELL LACKS SOMETHING":GOSUB1
2740 IFL=3THENM$="WOW! WHAT A POWERFUL HEALING SPELL":GOSUB1
2750 N=INT(RND(0)*PM+1)
2760 PS=PS+N*L:PM=PM-N
2770 IFPM=0THENM$="YOU HAVE USED UP ALL YOUR MAGIC":GOSUB1
2799 RETURN
2800 REM TELEPORT OUT OF BATTLE ROUTINE
2810 IFRND(0)*PM<MM*1.5THENM$="YOUR TELEPORT SPELL FAILS":GOSUB1:GOTO2899
2820 M$="YOUR MATERIAL FORM SHIMMERS AND THEN VANISHES":GOSUB1
2830 GOSUB4:M$="YOU RE-APPEAR SOMEWHERE ELSE":GOSUB1
2840 CC=RND(-(PEEK(9)*256+PEEK(10)))
2850 M$="THE SURROUNDINGS LOOK STRANGE":GOSUB1
2860 IFRND(0)<0.4THENM$="YOU'VE MOVED FURTHER DOWN INTO THE DUNGEON":LV=LV+INT(RND(0)*4+1)
2870 MF=0
2880 IFDF=2THENDF=0
2899 RETURN
2900 REM SETUP ALL VARIABLES/ARRAYS
2910 REM CLEAR
2920 DIMA$(20),M$(20)
2930 DIMMS(20),MM(20),MC(20)
2940 GOSUB2400
2945 DM=30:DC=75:DS=25
2950 DF=0
2960 PS=20:PM=20
2970 SC=0:LV=3
2980 MF=0
2999 RETURN
3000 REM TRANSFER DRAGON STATISTICS
3010 MM=DM:MC=DC:MS=DS
3040 NS=0
3050 DI$="AWE INSPIRING, HORRENDOUS DRAGON"
3099 RETURN
3100 REM THE MONSTER ATTACKS (FIGHTING)
3105 M$="THE MONSTER CLAWS AND BITES AT YOU":GOSUB1:CC=1500:GOSUB3
3110 IFRND(0)<0.4THENM$="BUT MISSES YOU":GOSUB1:GOTO3199
3120 M$="AND HITS YOU":GOSUB1
3130 N=INT(RND(0)*MS+1)
3140 PS=PS-N:IFPS<0THENPS=0
3150 IFPS<5THENM$="YOU'RE GETTING PRETTY BADLY HURT... BETTER WATCH YOUR STEP":GOSUB1
3199 RETURN
3200 REM THE MONSTER ATTACKS (MAGICALLY)
3205 M$="THE MONSTER BEGINS TO SUMMON A DEMON":GOSUB1:GOSUB4
3210 IFRND(0)*MM<PMTHENM$="THE DEMON OF COLD IS BANISHED":GOSUB1:GOTO3299
3220 N=INT(RND(0)*MM+1)
3230 PS=PS-N:MM=MM-N
3240 IFPS<=0THENPS=0:M$="YOU FROZE....":GOSUB1
3250 IFPS<5THENM$="YOU'RE NEARLY DEAD":GOSUB1
3260 IFPS>=5THENM$="BRRR. THAT WAS CHILLY":GOSUB1
3299 CC=1000:GOSUB3:RETURN
3300 REM CHOOSE MONSTER ATTACK TYPE
3310 IFRND(0)*PS>10*RND(0)*MSTHENM$="THE MONSTER TURNS TAIL AND FLEES FROM YOU, BRAVE FELLOW":GOSUB1:MF=0:SC=SC+NS:GOTO3399
3320 IFRND(0)*100<MCTHENGOSUB3200:GOTO3399
3330 GOSUB3100
3399 RETURN
3400 REM READ A CHARACTER FROM KBD:
3420 I$=INKEY$
3430 IFI$<"A"ORL$>"Z"THEN3420
3499 RETURN
3500 DF=2:GOSUB3000:M$="YOU'RE NOT GOING TO BELIEVE THIS, IT'S THE DRAGON!":GOSUB1:GOSUB4:GOSUB4000
3510 CLS:PRINT"HAVE FUN":RETURN
4000 CLS0
4010 PRINT"";
4011 PRINT"";
4012 PRINT"";
4013 PRINT"";
4014 PRINT"";
4015 PRINT"";
4016 PRINT"";
4017 PRINT"";
4018 PRINT"";
4019 PRINT"";
4020 PRINT"";
4021 PRINT"";
4022 PRINT"";
4023 PRINT"";
4024 PRINT"";
4025 PRINT"";:GOSUB4:RETURN
9000 DATA VICIOUS,NASTY,ENORMOUS,VIOLENT
9001 DATA SLY,UNDEAD,BEAUTIFUL,EVIL
9002 DATA ROTTING,SLIMY,ARMOURED
9003 DATA TERRIBLE,FOUL,BLOODY
9004 DATA FEARSOME,HYPNOTIC,DANGEROUS
9005 DATA SADISTIC,CRUEL,PITIFUL
9010 DATA ORC,KOBOLD,NEWT,WOLF
9015 DATA EAGLE,DWARF,TROLL,BALROG
9020 DATA ELF,WIGHT,KING,BEAR
9025 DATA BLACK RIDER,SNAKE,DEVIL,WILL O' THE WISP
9027 DATA GREEN SLIME,GIANT,VENUS MAN TRAP,BLOWFLY
9030 DATA 10,0,0,5,20,75,1,50,100,20,0,0
9040 DATA 15,5,20,15,0,0,30,0,0,15,20,50
9050 DATA 10,40,25,40,100,2,30,10,20,35,0,0
9060 DATA 20,40,10,10,0,0,4,20,40,20,70,10
9070 DATA 4,0,0,35,0,0,20,10,30,10,10,75 
