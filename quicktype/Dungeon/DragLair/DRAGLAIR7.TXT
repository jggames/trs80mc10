0 CLS0:CLEAR260,36762:DIMC2,C4,MC,CC,ZZ,VP,M$,I$,K$,W$:MC=16384:M$="O"+"K":W$=CHR$(127):GOSUB15:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THENGOSUB3:ZZ=CC+1:CC=ZZ+32:IFI$=""ORZZ>LEN(M$)THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 K$=MID$(M$,ZZ,CC-ZZ):VP=VARPTR(K$):C4=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:IFC4-MC+33=512THENGOSUB6:C4=C4-32
4 PRINT@C4-MC+33,;:FORC2=1TOLEN(K$):POKEC4+C2,ASC(MID$(K$,C2))AND63:NEXT:RETURN
5 FORCC=1TO16:GOSUB6:NEXT:PRINT@.,;:RETURN
6 PRINT@511,W$;:POKEMC+479,32:RETURN
7 I$="":ZZ=(PEEK(17024)AND1)*256+PEEK(17025)+MC:POKEZZ,96:CC=ZZ-MC
8 M$=INKEY$:IFM$=""THEN8
9 IFM$=CHR$(13)THENON-(I$<>"")GOTO13:I$=" ":GOTO13
10 IFM$=CHR$(8)THENIFI$<>""THENI$=LEFT$(I$,LEN(I$)-1):POKEZZ,32:ZZ=ZZ-1:POKEZZ,96
11 IFM$<" "ORM$>"Z"ORLEN(I$)>30THEN8
12 C2=ASC(M$):POKEZZ,C2+64*(C2>63):ZZ=ZZ+1:POKEZZ,96:I$=I$+M$:GOTO8
13 POKEZZ,32:IFCC+32=512THENGOSUB6:CC=CC-32
14 PRINT@CC+32,;:RETURN
15 FORCC=0TO103:POKE36762+CC,PEEK(64022+CC):NEXT:DATA49,49,60,55,54
16 FORCC=0TO4:READC2:POKE36762+CC,C2:NEXT:POKE36855,32:POKE17033,143:POKE17034,154:POKE17032,126:RETURN
100 POKE49151,64
110 PO=15:DIMO(3),O$(3):TR=0
120 FORI=0TO3:READO$(I):NEXT
130 DATASWORD,STAFF,PALANTIR,TALISMAN
140 O(INT(RND(0)*2))=1
150 FORZZ=1TO250:NEXT:GOSUB5:M$="YOU HAVE ENTERED A"
160 D=INT(RND(0)*4):IFD=0THENM$=M$+"N EVIL SMELLING"
170 IFD=1THENM$=M$+" HOMELY"
180 IFD=2THENM$=M$+" DANK AND WET"
190 IFD=3THENM$=M$+" DARK, MYSTIC"
200 D=INT(RND(0)*4+1):IFD=1THENM$=M$+" BARN"
210 IFD=2THENM$=M$+" DUNGEON"
220 IFD=3THENM$=M$+" CASTLE"
230 IFD=4THENM$=M$+" TOWER"
240 C=INT(RND(0)*6):M$=M$+", WHICH CONTAINS A ":C1=C
250 IFC=0THENM$=M$+"MYSTERIOUS OBJECT!":Z=INT(RND(0)*4)
260 IFC=1THENM$=M$+"BOOK OF SPELLS."
270 IFC=2THENM$=M$+"HORDE OF TREASURE."
280 IFC=3THENM$=M$+"SMALL ANIMAL RUMAGING IN THE CORNER."
290 IFC=4THENM$=M$+"MONSTER GUARDING IT."
300 IFC=5THENM$=M$+"DRAGON BLOCKING YOUR WAY."
301 GOSUB1
310 IFC<3ANDRND(0)>.6THENM$="THERE IS ALSO A MONSTER.":GOSUB1:C1=3
320 IFPO<1THENGOSUB5:M$="THOU ART SLAIN":GOSUB1:GOTO1100
330 IFPO<15THENPO=PO+O(3)
340 FORI=0TO3:IFRND(0)>.9THENPO=PO-O(I)
350 NEXT
360 M$=" ":GOSUB1:M$="WHAT NOW?":GOSUB1:GOSUB7:A$=I$:IFI$="HELP"THENGOSUB2000:GOTO360
370 V$="":N$="":P=1:FORI=1TOLEN(A$)
380 IFMID$(A$,I,1)=" "THENP=2:GOTO410
390 IFP=1THENV$=V$+MID$(A$,I,1)
400 IFP=2THENN$=N$+MID$(A$,I,1)
410 NEXTI
420 GOSUB5:M$=V$:IFN$<>""THENM$=M$+" "+N$
421 M$=M$+".":GOSUB1
430 IFV$="SLAY"THEN650
440 IFV$="GO"THEN710
450 IFV$="GET"THEN740
460 IFV$="EXAMINE"THEN770
470 IFV$="DROP"THEN810
480 IFV$="GAZE-INTO"THEN840
490 IFV$="SPELL"THEN920
500 IFV$="RETRIEVE"THEN1010
510 IFV$="READ"THEN1060
520 IFV$="INVENTORY"THEN1100
530 M$="I DON'T UNDERSTAND.":GOSUB1:GOTO320
540 IFC>3ANDRND(0)>.7THENC1=C1+1
550 FORI=1TO850:NEXT
560 IFC1=1ANDRND(0)>.9THENM$="THE ROOF FALLS IN!":GOSUB1:PO=PO-2:GOTO320
570 IFC1=2ANDRND(0)>.9THENM$="YOU FELL DOWN A PIT IN THE FLOOR.":GOSUB1:PO=PO-3:GOTO320
580 IFC1=3THENM$="THE ANIMAL KEEPS IT'S DISTANCE.":GOSUB1
590 IFC1=4ANDRND(0)>.6THENM$="THE MONSTER ADVANCES.":GOSUB1
600 IFC1>5THENC1=5
610 IFC1<5ORRND(0)<.7THEN320
620 M$="THE MONSTER ATTACKS YOU.":GOSUB1:PO=PO-INT(RND(0)*4)
630 IFRND(0)>.7THENM$="HE KILLS YOU!":GOSUB1:PO=0:GOTO1100
640 GOTO320
650 IFC1<3THENM$="THERE IS NO MONSTER.":GOSUB1:GOTO540
660 IFO(0)=0THENM$="YOU HAVE NO WEAPONS!":GOSUB1:GOTO540
670 PO=PO-INT(RND(0)*3)
680 IFC=5ANDRND(0)>.6THENM$="THE DRAGON LIVES.":GOSUB1:GOTO540
690 M$="THE BEAST IS DEAD.":GOSUB1:C1=0:GOTO540
710 IFC1>2ANDRND(0)<C1/7THENM$="THE MONSTER WON'T LET YOU.":GOSUB1:GOTO540
720 IFN$<>"LEFT"ANDN$<>"RIGHT"ANDN$<>"FORWARDS"THEN480
730 M$="OK":GOSUB1:GOTO150
740 IFN$<>O$(Z)THENM$="THERE IS NO "+N$+".":GOSUB1:GOTO540
750 O(Z)=O(Z)+1:M$="OK":GOSUB1
760 GOTO540
770 IFC1>3THENM$="THIS ANGERS THE BEAST.":GOSUB1:C1=C1+1:GOTO550
780 IFC>0THENM$="THERE IS NOTHING TO EXAMINE.":GOSUB1:GOTO540
790 M$="THE OBJECT TURNS OUT TO BE A "+O$(Z)+".":GOSUB1
800 GOTO540
810 FORI=0TO3:IFN$=O$(I)ANDO(I)=0THENM$="YOU DON'T HAVE A "+N$+".":GOSUB1:GOTO540
820 IFN$=O$(I)THENO(I)=O(I)-1:Z=I:M$="OK":GOSUB1
830 NEXT:GOTO540
840 IFN$<>"PALANTIR"THENM$="YOU CAN'T GAZE AT THAT.":GOSUB1:GOTO540
850 IFO(2)=0THENM$="YOU DON'T HAVE A PALANTIR.":GOSUB1:GOTO540
860 IFRND(0)>.3THEN900
870 IFRND(0)>.5THENM$="THE GLOBE REMAINS BLANK.":GOSUB1:GOTO540
880 IFRND(0)>.5THENM$="YOU DISCOVER A MONSTER LURKING IN THE SHADOWS.":GOSUB1:C1=3:GOTO550
890 M$="YOU SEE A GREAT TREASURE IN YOUR JOURNEYS.":GOSUB1:GOTO320
900 M$="YOU FEEL WEAKER.":GOSUB1
910 PO=PO-1:GOTO540
920 IFO(1)=0THENM$="YOU HAVEN'T THE MEANS TO CAST SPELLS.":GOSUB1:GOTO320
930 IFN$<>"MONSTER"THENM$="YOU CAN'T SPELL THAT.":GOSUB1:GOTO540
940 IFC1<3THENM$="THERE IS NO MONSTER.":GOSUB1:GOTO320
950 IFRND(0)<.8THEN980
960 M$="YOUR SPELL FAILED. IT HAS ANGERED THE BEAST.":GOSUB1:C1=C1+1:GOTO550
970 GOTO540
980 M$="THE SPELL WORKED.":GOSUB1:C1=0
990 IFRND(0)>.6THENM$="BUT YOUR STAFF SNAPPED WITH THE STRAIN.":GOSUB1:O(1)=O(1)-1
1000 GOTO320
1010 IFC<>2THENM$="THERE IS NOTHING TO RETRIEVE.":GOSUB1:GOTO540
1020 IFN$<>"TREASURE"THENM$="THAT CANNOT BE RETRIEVED.":GOSUB1:GOTO540
1030 IFC1>2ANDRND(0)>.3THENM$="THE MONSTER WON'T LET YOU.":GOSUB1:C1=C1+1:GOTO550
1040 TR=TR+INT(RND(0)*101)+100
1050 M$="OK":GOSUB1:GOTO320
1060 IFC<>1THENM$="THERE IS NO BOOK.":GOSUB1:GOTO540
1070 IFN$<>"BOOK"THEN530
1080 M$="YOU FEEL FAINT.":GOSUB1:PO=PO-2
1090 GOTO320
1100 M$="YOU HAVE"+STR$(PO)+" STRENGTH.":GOSUB1
1110 M$="YOU HAVE"+STR$(TR)+" SILVER COINS.":GOSUB1
1120 M$="YOU HAVE ":FORI=0TO3:M$=M$+MID$(STR$(O(I)),2)+" "+O$(I)
1130 IFO(I)<>1THENM$=M$+"S"
1140 GOSUB1:M$="":NEXT
1150 IFPO>0THEN540
1160 M$="TRY AGAIN (Y/N)?":GOSUB1
1170 GOSUB7:IFLEFT$(I$,1)="Y"THENRUN
1180 IFLEFT$(I$,1)="N"THENEND
1190 GOSUB1160
2000 M$="COMMANDS: SLAY, GO, GET, EXAMINE, DROP, GAZE-INTO, SPELL, RETRIEVE, READ, INVENTORY":GOSUB1
2010 RETURN
