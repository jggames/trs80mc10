0 CLS:PRINT@234,"THE 6 LILIES":PRINT@362,"BY INFOGRAMES":CLEAR100:GOSUB1600
3 GOSUB41000:GOSUB42000:PRINT@15*32+10,"initializing";:GOSUB500:SOUND135,1
4 GOSUB41035:CLS
5 DIMTS(6),RE(2):GOSUB4000:C9=7:X=10:Y=5:DI=1:CA=5:GOSUB20000:SOUND1,1
6 T$="":A=0:SA=0
7 GOSUB1000
8 GOTO120
10 IFXK=1THENIFCW=0THENGOSUB10000
11 IFXK=2THENIFCW=0THENGOSUB10000
12 A=SA:CW=CW-1:IFCW=-1THENCW=50
13 IFMATHEN3000
14 R$=INKEY$:IFR$=""THEN10
15 PRINT@416,"":PRINT:PRINT@416,
20 IFR$=CHR$(13)ANDT$=""THEN10
30 IFR$=CHR$(13)THEN200
40 IFR$=CHR$(8)ANDLEN(T$)<1THEN10
50 IFR$=CHR$(8)THENT$=MID$(T$,1,LEN(T$)-1):GOTO120
70 T$=T$+R$
71 IFLEN(T$)>5THENT$=""
120 PRINT@448,T$CHR$(175)
130 GOTO10
200 IFT$=""THEN10
205 PRINT@448,CHR$(175)
210 FORX1=LEN(T$)TO1STEP-1
220 A$=MID$(T$,X1,1)
225 IFATHEN245
240 IFASC(A$)>48ANDASC(A$)<55THENA=ASC(A$)-48
245 IFA$=" "ORASC(A$)>48ANDASC(A$)<58THENT$=MID$(T$,1,X1-1)+MID$(T$,X1+1,LEN(T$))
260 NEXT
270 RESTORE:X1=1
271 READA$:IFA$=T$THEN2000
272 X1=X1+1:IFX1<11THEN271
300 PRINT@416,"I DON'T UNDERSTAND!";
380 GOTO6

500 PA=1
502 O=INT(RND(12)):NO=INT(RND(4)-1):NM=INT(RND(3)-1)
503 IFNO=0THEN510
504 FORX1=0TONO-1
505 O=INT(RND(12)):IFO=11THEN505
506 IFR(PA,X1)=0THENR(PA,X1)=O
507 NEXTX1
510 IFNM=0THEN517
511 FORX1=0TONM-1
512 M=RND(40):IFM=37ORM=38THEN512
513 IFR(PA,6+X1)=0THENR(PA,6+X1)=M
515 NEXTX1
517 PA=PA+1
518 IFPA=44THENR(38,6)=0:R(38,7)=40:R(43,7)=37:R(35,7)=38:R(42,7)=37:R(44,0)=11:RETURN
520 GOTO502

600 DATA"N","S","E","W","AT","GE","DR","CO","RE","IN"

1000 PRINT@480,"STR"FM;TAB(11)"INT"IM;TAB(22)"CON"CM;:RETURN

1100 C8=C9+12:PRINT@C8,"ºººººººº";:C8=C8+32
1102 PRINT@C8,"ÄÄÄÄÄÄÄÄ";:C8=C8+32
1103 PRINT@C8,"ÄÄÄÄÄÄÄÄ";:C8=C8+32
1104 PRINT@C8,"ÄÄµººΩÄÄ";:C8=C8+32
1105 PRINT@C8,"ÄÄµÄÄµÄÄ";:C8=C8+32
1106 PRINT@C8,"ÄÄµÄÄµÄÄ";:C8=C8+32
1107 PRINT@C8,"ºººÄÄ¥ºº";:C8=C8+32
1108 PRINT@C8,"ÄÄÄÄÄÄÄÄ";:C8=C8+32
1109 PRINT@C8,"ÄÄÄÄÄÄÄÄ";:RETURN

1200 C8=C9+(9*32)+7:PRINT@C8,"¥ºººººººÄÄ¥ºººººº∏";:RETURN

1300 C8=C9+22:PRINT@C8,"ÄÄÄ";:C8=C8+32
1302 PRINT@C8,"πÄÄ";:C8=C8+32
1303 PRINT@C8,"ÄπÄ";:C8=C8+32
1304 PRINT@C8,"ªÄ∫";:C8=C8+32
1305 PRINT@C8,"∫∫∫";:C8=C8+32
1306 PRINT@C8,"∫∫∫";:C8=C8+32
1307 PRINT@C8,"∫∫∫";:C8=C8+32
1308 PRINT@C8,"∏∫∫";:C8=C8+32
1309 PRINT@C8,"Äπ∫";:RETURN

1400 C8=C9+7:PRINT@C8,"ÄÄÄ∑";:C8=C8+32
1402 PRINT@C8,"ÄÄ∂µ";:C8=C8+32
1403 PRINT@C8,"Ä∂Äµ";:C8=C8+32
1404 PRINT@C8,"µÄ∑µ";:C8=C8+32
1405 PRINT@C8,"µµµµ";:C8=C8+32
1406 PRINT@C8,"µµµµ";:C8=C8+32
1407 PRINT@C8,"µµµ∂";:C8=C8+32
1408 PRINT@C8,"µµ¥Ä";:C8=C8+32
1409 PRINT@C8,"µ∂ÄÄ";:RETURN

1500 C8=C9+7:PRINT@C8,"ÄÄÄ∑ººººººººººªÄÄÄ";:C8=C8+32
1502 PRINT@C8,"ÄÄ∂µÄÄÄÄÄÄÄÄÄÄ∫πÄÄ";:C8=C8+32
1503 PRINT@C8,"Ä∂ÄµÄÄÄÄÄÄÄÄÄÄ∫ÄπÄ";:C8=C8+32
1504 PRINT@C8,"µÄÄµÄÄÄÄÄÄÄÄÄÄ∫ÄÄ∫";:C8=C8+32
1505 PRINT@C8,"µÄÄµÄÄÄÄÄÄÄÄÄÄ∫ÄÄ∫";:C8=C8+32
1506 PRINT@C8,"µÄÄµÄÄÄÄÄÄÄÄÄÄ∫ÄÄ∫";:C8=C8+32
1507 PRINT@C8,"µÄÄ∂ººººººººººπÄÄ∫";:C8=C8+32
1508 PRINT@C8,"µÄ∂ÄÄÄÄÄÄÄÄÄÄÄÄπÄ∫";:C8=C8+32
1509 PRINT@C8,"µ∂ÄÄÄÄÄÄÄÄÄÄÄÄÄÄπ∫";:C8=C8+32
1510 PRINT@C8,"¥ºººººººººººººººº∏";:RETURN

1520 C8=C9+7:PRINT@C8,"–Ä¡»¿¿ƒ∆ÃÃÃÀ√∆ÃÃ¬Ä";:C8=C8+32
1521 PRINT@C8,"–¡…ÄÄÄ∆ƒ¬Ä∆Äƒ¬Ä√∆¬";:C8=C8+32
1522 PRINT@C8,"–∆¿…Ä∆ÄÄ≈Ã¬ÄÄ«Ã»Ä≈";:C8=C8+32
1523 PRINT@C8,"¡»Ä¡ÕÄÄ¡»Ä∆√∆≈ÄÄÄ≈";:C8=C8+32
1524 PRINT@C8,"≈ÄÄ∆Ä…¡»¡Ã¬ ∆ŒÃÃÃÕ";:C8=C8+32
1525 PRINT@C8," ÄÄ ÄÄ ÃÃ√∆ƒ∆ÄÄÄÄ∆";:C8=C8+32
1526 PRINT@C8," ¡√ √√ Ä∆≈Ä∆ƒ…√Ä∆Ä";:C8=C8+32
1527 PRINT@C8,"…ÃÄƒ¬Äƒ√»ƒ∆ÄÄÄÄŒ√¿";:C8=C8+32
1528 PRINT@C8,"ƒ¬ÄÄ≈√√ÃÃÃƒ¬ÄÄ∆ÄÄ…";:C8=C8+32
1529 PRINT@C8,"–ƒ√∆»ƒ√√√√√«√∆Ã…√»";:RETURN

1530 C8=C9+(4*32)+11:PRINT@C8,"ÄÄÄÄ√√¿ÄÄÄ";:C8=C8+32
1531 PRINT@C8,"Ä¡ÃÃÃÃÃÃ¬Ä";:C8=C8+32
1532 PRINT@C8,"¡ÃÃÃÃÃÃÃÃ¬";:C8=C8+32
1533 PRINT@C8,"ÃÃÃÃÃÃÃÃÃÃ";:RETURN

1600 DIMM$(6),N$(11,6),N(11,6),R(44,7),O$(12),MS$(12),MO$(41),FO(12),IO(12),CO(12),CC(12),SI(12),BE(40),CY(40),XK(40)
1610 M$(0)="aaaeaaeaeaaa"
1611 M$(1)="aacpddpdpbaa"
1612 M$(2)="aeamaamaiaea"
1613 M$(3)="amamacpbaama"
1614 M$(4)="cpdpbamaeama"
1615 M$(5)="amamacpdpdpb"
1616 M$(6)="aiaiaaiaiaia"
1617 D$="0000000100100011010001010110011110001001101010111100110111101111"

1620 N$(3,0)="NUAGO":N$(6,0)="FINE":N$(8,0)="PERDIRE":N$(2,1)="FINITEM":N$(3,1)="SATANUS"
1621 N$(4,1)="TENEBAM":N$(5,1)="STYX":N$(6,1)="SERPENTI":N$(7,1)="DAEDAL":N$(8,1)="MORTINA"
1622 N$(9,1)="SALOMEN":N$(1,2)="RUNAE":N$(3,2)="DANGERO":N$(6,2)="DRAGONA":N$(8,2)="MARAE"
1623 N$(10,2)="EQUALINA":N$(1,3)="DRAGONA":N$(3,3)="SULFURI":N$(5,3)="AUTELO":N$(6,3)="CAVERNI"
1624 N$(7,3)="MASQUARE":N$(10,3)="ANOMINA":N$(0,4)="TRESORA":N$(1,4)="CHOICE":N$(2,4)="PALADI"
1625 N$(3,4)="BUTANO":N$(4,4)="RATAE":N$(6,4)="GARDAE":N$(8,4)="BIBERUS":N$(10,4)="GIGANTUS"
1626 N$(1,5)="DRAGONA":N$(3,5)="DRAGO":N$(5,5)="MYSTERO":N$(6,5)="ALCHEMY":N$(7,5)="PEURAE"
1627 N$(8,5)="PIXIS":N$(9,5)="PESTNIA":N$(10,5)="ENTRANCE":N$(11,5)="VESTIA":N$(1,6)="PUELLA"
1628 N$(3,6)="LILY":N$(6,6)="MAGICA":N$(8,6)="PANEM":N$(10,6)="DEMONIA"

1630 M1$="◊“âÜ∆¬–Ä·‚”†††¶©Éã√Õˆ˘ÉÇ·‚∑ªôñÙ˛—ŸÌ‚ûõ¶©ÊÈ≠´ÈÊÀŒ÷Ÿ—“Üã—”∆…°´¬¬ÉÅˇ˙©¶ÜÇﬁ›ùêÌÎ˝˚Ã√áã"
1631 M2$="÷“éç∆¬˘ÛÓÌ’◊˛˛†ÄãäŒ ˘ˆççÔÔΩæÄÄı˝’ﬂ„Óùó≠ÆÊÈÆÆÌÓ«Õ¶©ñôÜâ››¡…•≠≈ÄÉçÛˆ¶©éäﬁ›ôñÌÌ˝˝Ã√éç"

1640 O$="±ªëõ¡Œ¡Œı˝°´ÅãÙõîñó”ƒŒÙõ"
1650 MS$(1)="RED IS STRENGTH":MS$(2)="YELLOW IS POISON":MS$(3)="SCROLL OF INSANITY"
1653 MS$(4)="SCROLL OF WISDOM":MS$(5)="MAGIC CHEST":MS$(6)="BLUE IS MADNESS!"
1656 MS$(7)="GREEN IS LIFE":MS$(8)="BON APETIT!":MS$(9)="IT'S FOR YOUR THIRST!"
1658 MS$(10)="SWORD OF ERIN":MS$(11)="THE LILY!":MS$(12)="WATCH OUT!"
1660 O$(1)="VIAL":O$(2)="VIAL":O$(3)="SCROLL"
1663 O$(4)="SCROLL":O$(5)="CHEST":O$(6)="VIAL"
1666 O$(7)="VIAL":O$(8)="CHICKEN":O$(9)="CHALICE"
1669 O$(10)="SWORD":O$(11)="LILY":O$(12)="CHICKEN"

1701 MO$(1)="grenoui":BE(1)=120:CY(1)=70:XK(1)=1
1702 MO$(2)="skelet":BE(2)=70:CY(2)=45:XK(2)=0
1703 MO$(3)="vermina":BE(3)=40:CY(3)=40:XK(3)=0
1704 MO$(4)="tarentul":BE(4)=90:CY(4)=70:XK(4)=1
1705 MO$(5)="naja":BE(5)=30:CY(5)=40:XK(5)=1
1706 MO$(6)="nono":BE(6)=30:CY(6)=45:XK(6)=0
1707 MO$(7)="vampyr":BE(7)=70:CY(7)=40:XK(7)=0
1708 MO$(8)="poulpi":BE(8)=140:CY(8)=100:XK(8)=1
1709 MO$(9)="nabotin":BE(9)=40:CY(9)=20:XK(9)=0
1710 MO$(10)="tasalmi":BE(10)=50:CY(10)=40:XK(10)=0
1711 MO$(11)="rampater":BE(11)=50:CY(11)=40:XK(11)=0
1712 MO$(12)="gluant":BE(12)=70:CY(12)=50:XK(12)=0
1713 MO$(13)="globo":BE(13)=90:CY(13)=70:XK(13)=1
1714 MO$(14)="chauveri":BE(14)=70:CY(14)=45:XK(14)=0
1715 MO$(15)="garde":BE(15)=50:CY(15)=40:XK(15)=0
1716 MO$(16)="fantom":BE(16)=40:CY(16)=20:XK(16)=0
1717 MO$(17)="milpat":BE(17)=40:CY(17)=20:XK(17)=0
1718 MO$(18)="momivore":BE(18)=70:CY(18)=50:XK(18)=1
1719 MO$(19)="cameleo":BE(19)=40:CY(19)=20:XK(19)=0
1720 MO$(20)="lonkou":BE(20)=60:CY(20)=40:XK(20)=0
1721 MO$(21)="rablato":BE(21)=60:CY(21)=40:XK(21)=0
1722 MO$(22)="sankou":BE(22)=40:CY(22)=20:XK(22)=0
1723 MO$(23)="machoi":BE(23)=90:CY(23)=50:XK(23)=1
1724 MO$(24)="hiponem":BE(24)=50:CY(24)=40:XK(24)=0
1725 MO$(25)="skarabis":BE(25)=50:CY(25)=40:XK(25)=0
1726 MO$(26)="trompes":BE(26)=60:CY(26)=30:XK(26)=0
1727 MO$(27)="tetardu":BE(27)=60:CY(27)=45:XK(27)=0
1728 MO$(28)="kokinel":BE(28)=40:CY(28)=20:XK(28)=0
1729 MO$(29)="hipopo":BE(29)=70:CY(29)=70:XK(29)=0
1730 MO$(30)="nain":BE(30)=50:CY(30)=40:XK(30)=0
1731 MO$(31)="ouistito":BE(31)=90:CY(31)=90:XK(31)=1
1732 MO$(32)="lemura":BE(32)=40:CY(32)=20:XK(32)=0
1733 MO$(33)="canivore":BE(33)=50:CY(33)=40:XK(33)=0
1734 MO$(34)="tetbas":BE(34)=70:CY(34)=30:XK(34)=0
1735 MO$(35)="robo":BE(35)=70:CY(35)=70:XK(35)=0
1736 MO$(36)="viperis":BE(36)=40:CY(36)=30:XK(36)=0
1737 MO$(37)="dragon":BE(37)=200:CY(37)=200:XK(37)=1
1738 MO$(38)="dragon":BE(38)=200:CY(38)=200:XK(38)=1
1739 MO$(39)="arienis":BE(39)=50:CY(39)=40:XK(39)=0
1740 MO$(40)="migalus":BE(40)=90:CY(40)=70:XK(40)=1

1810 N(3,0)=30:N(6,0)=21:N(8,0)=26:N(2,1)=31:N(3,1)=29
1811 N(4,1)=28:N(5,1)=27:N(6,1)=20:N(7,1)=23:N(8,1)=23
1812 N(9,1)=24:N(1,2)=41:N(3,2)=32:N(6,2)=19:N(8,2)=25
1813 N(10,2)=6:N(1,3)=42:N(3,3)=33:N(5,3)=17:N(6,3)=16
1814 N(7,3)=18:N(10,3)=5:N(0,4)=40:N(1,4)=39:N(2,4)=38
1815 N(3,4)=34:N(4,4)=37:N(6,4)=15:N(8,4)=9:N(10,4)=4
1816 N(1,5)=43:N(3,5)=35:N(5,5)=13:N(6,5)=12:N(7,5)=11
1817 N(8,5)=8:N(9,5)=7:N(10,5)=1:N(11,5)=2:N(1,6)=44
1818 N(3,6)=36:N(6,6)=14:N(8,6)=10:N(10,6)=3

1901 FO(1)=10:IO(1)=0:CO(1)=0:CC(1)=1:SI(1)=0
1902 FO(2)=0:IO(2)=0:CO(2)=50:CC(2)=1:SI(2)=1
1903 FO(3)=0:IO(3)=10:CO(3)=0:CC(3)=2:SI(3)=1
1904 FO(4)=0:IO(4)=10:CO(4)=0:CC(4)=2:SI(4)=0
1905 FO(5)=5:IO(5)=0:CO(5)=0:CC(5)=0:SI(5)=0
1906 FO(6)=0:IO(6)=5:CO(6)=0:CC(6)=1:SI(6)=1
1907 FO(7)=10:IO(7)=0:CO(7)=0:CC(7)=1:SI(7)=0
1908 FO(8)=0:IO(8)=0:CO(8)=10:CC(8)=1:SI(8)=0
1909 FO(9)=0:IO(9)=0:CO(9)=10:CC(9)=1:SI(9)=0
1910 FO(10)=10:IO(10)=0:CO(10)=10:CC(10)=0:SI(10)=0
1911 FO(11)=10:IO(11)=10:CO(11)=10:CC(11)=0:SI(11)=0
1912 FO(12)=0:IO(12)=0:CO(12)=0:CC(12)=0:SI(12)=0:RETURN

2000 CA=CA-1:IFCATHEN2005
2001 CA=5:CM=CM-IN-1:IFCM<=0THENMA=2:GOTO3000
2005 ONX1GOSUB6000,7000,8000,9000,10000,11000,12000,13000,14000,15000
2010 GOTO6
3000 IFMA=1THEN3100
3010 IFMA=2THEN3200
3020 IFMA=3THEN3300
3021 IFMA=5THEN3400
3100 PRINT@416,"THIS COMBAT WAS FATAL...";:GOTO3500
3200 PRINT@416,"YOU DIED! WHAT NEGLIGENCE!";:GOTO3500
3300 PRINT@416,"IT WAS POISONED...";:GOTO3500
3400 CLS:PRINT@32*3+7,"GAEL THE VALORUS,";:PRINT@32*5+6,"YOU HAVE SUCCEEDED.";
3401 PRINT@32*7+6,"ALICEA LIVES AGAIN!";:PRINT@32*9+8,"AND THANKS YOU.";
3405 FORX1=1TO50:SOUND230,1:NEXT
3420 HG$=INKEY$:IFHG$=""THEN3420
3500 END

4000 PRINT@39,"GAEL THE VALOROUS":PRINT@32*5,"CON":PRINT@32*6,"STR":PRINT@32*7,"INT"
4001 P=32*5+6:L=150:V=250:GOSUB5004:C1=A:L=20:V=40:GOSUB5004:F1=A:L=6:V=25:GOSUB5004:I1=A:GOSUB5015:GOSUB5007
4002 P=32*5+12:L=150:V=250:GOSUB5004:C2=A:L=20:V=40:GOSUB5004:F2=A:L=6:V=25:GOSUB5004:I2=A:GOSUB5020:GOSUB5007
4003 P=32*5+18:L=150:V=250:GOSUB5004:C3=A:L=20:V=40:GOSUB5004:F3=A:L=6:V=25:GOSUB5004:I3=A:GOSUB5030:GOSUB5007
4004 P=32*5+24:L=150:V=250:GOSUB5004:C4=A:L=20:V=40:GOSUB5004:F4=A:L=6:V=25:GOSUB5004:I4=A:GOSUB5035

5000 PRINT@448,"YOUR CHOICE? 1,2,3,4"
5001 PRINT@32*4+8,"1":PRINT@32*4+14,"2":PRINT@32*4+20,"3":PRINT@32*4+26,"4":A$="":SOUND240,4:GOTO5040
5004 A=INT(RND(V))
5005 IFA<LTHEN5004
5006 RETURN
5007 A=RND(300):PRINT@448,TAB(9)"PRESS ANY KEY":SOUND255,1
5008 A=RND(200):PRINT@448,TAB(9)"press any key":SOUND255,1
5009 A$=INKEY$:IFA$=""THEN5007
5010 RETURN
5015 PRINT@P,C1:PRINT@P+32,F1:PRINT@P+64,I1:RETURN
5020 PRINT@P,C2:PRINT@P+32,F2:PRINT@P+64,I2:RETURN
5030 PRINT@P,C3:PRINT@P+32,F3:PRINT@P+64,I3:RETURN
5035 PRINT@P,C4:PRINT@P+32,F4:PRINT@P+64,I4:RETURN
5040 A$=INKEY$:IFA$=""THEN5040
5042 IFASC(A$)>48ANDASC(A$)<53THEN5046
5044 GOTO5040
5046 CLS:ONASC(A$)-48GOTO5048,5050,5052,5054
5048 CM=C1:FM=F1:IM=I1:RETURN
5050 CM=C2:FM=F2:IM=I2:RETURN
5052 CM=C3:FM=F3:IM=I3:RETURN
5054 CM=C4:FM=F4:IM=I4:RETURN

REM NORTH
6000 IFN9=0THEN6006
6002 Y=Y-1:IFN(X,Y)=26THEN6010
6003 IFN(X,Y)=41THEN6020
6004 GOSUB36000:GOSUB20000:RETURN
6006 PRINT@416,"CAN'T GO NORTH";:RETURN
6010 IFRY(1)=11ORRY(2)=11ORRY(3)=11THENMA=5
6011 GOSUB20000:RETURN
6020 IFR(42,7)THEN7080
6050 GOSUB36000:GOSUB20000:RETURN

REM SOUTH
7000 IFS9=0THEN7006
7002 Y=Y+1:IFN(X,Y)=44THEN7011
7004 GOSUB36000:GOSUB20000:RETURN
7006 PRINT@416,"CAN'T GO SOUTH";:RETURN
7011 IFR(43,7)THEN7080
7012 GOSUB36000:GOSUB20000:RETURN
7080 Y=Y-1:PRINT@416,"YOU CANNOT PASS ME ALIVE!";:SOUND1,2:SOUND2,2:RETURN

REM EAST
8000 IFE9=0THEN8006
8002 X=X+1:GOSUB36000:GOSUB20000:RETURN
8006 PRINT@416,"CAN'T GO EAST";:RETURN

REM WEST
9000 IFW9=0THEN9006
9002 X=X-1:GOSUB36000:GOSUB20000:RETURN
9006 PRINT@416,"CAN'T GO WEST";:RETURN

10000 PRINT@416,"":PRINT@448,"":PRINT@480,"                               ";:IFXK=0THEN10004
10003 SA=A:A=XK:IFCM<0THENRETURN
10004 IFA=0THEN16065
10005 IFA>2THEN16060
10010 IFRE(A)=0THEN16060
10011 IFCM<250THEN10013
10012 X1=INT(CM/200)+1:SOUNDX1,1:SOUNDX1+1,1:GOTO10020
10013 SOUNDCM,1
10020 REM
10021 IFA=2THEN10025
10022 IFDI=3THEN10030
10023 DI=3:GOSUB20000:GOTO10029
10025 IFDI=5THEN10030
10026 DI=5:GOSUB20000
10029 REM DI=ATTACK DIRECTION
10030 BM=FM+IM:BE=BE(RE(A)):CY=INT(CY(RE(A))*1.5)
10035 DL=BM-BE:IFDL<0THEN10080
10042 GOSUB50000:IFRC<0THEN10103
10043 IFRCTHEN10045
10044 PRINT@416,"MISSED!";:GOTO10104
10045 GOTO10101
10080 DL=ABS(DL):GOSUB50000:RC=-RC:IFRC<0THEN10103
10090 IFRCTHEN10101
10095 PRINT@416,"GOOD HIT!";:GOTO10104
10101 CY=CY-RC:PRINT@416,"YOU WOUNDED IT";:GOTO10110
10103 CM=CM+RC:PRINT@416,"YOU ARE HIT!";:IFCM<=0THEN10200
10104 FORP=1TO50:NEXT:GOSUB1000:RETURN
10110 IFINT(CY/1.5)>0THEN10120
10112 CY(RE(A))=0:GOTO10130
10120 CY(RE(A))=INT(CY/1.5):GOSUB1000:RETURN
10130 RA=(FM/IM)+1:BE=(BE/15):FM=INT(FM+BE-(BE*.6/RA)):IM=INT(IM+(BE*.6/RA)):CM=CM+INT(BE*.4)
10135 PRINT@416,"THE MONSTER IS DEAD";:XK=0:SOUND210,1:SOUND211,1:SOUND212,1:SOUND213,1:DI=1
10140 R(R,5+A)=0:GOSUB20000:GOSUB1000:RETURN
10200 MA=1:PRINT@416,"YOU ARE DEAD!";:GOSUB1000:RETURN

REM GET
11000 IFA=0THEN16000
11010 IFIN=3THEN16010
11015 IFTS(A)=0THEN16060
11020 GOSUB16020:IN=IN+1:OB=TS(A):TS(A)=0:X1=1
11021 IFRY(X1)=0THEN11024
11022 X1=X1+1:IFX1<4THEN11021
11023 GOTO16010
11024 RY(X1)=OB
11025 IFCCTHEN11060
11030 IFSI=1THEN11050
11035 PRINT@416,"WISE MOVE";:SOUND245,1
11040 CM=CM+CO:FM=FM+INT(FO/2):IM=IM+INT(IO/2):GOTO11060
11050 CM=CM-CO:FM=FM-INT(FO/2):IM=IM-INT(IO/2)
11055 PRINT@416,"OOOPS!";:SOUND1,1
11060 R(R,A-1)=0
11080 GOTO12080

REM DROP
12000 IFIN=0THEN17000
12010 IFA=0THEN16000
12011 IFRY(A)=0THEN16040
12015 IFA>3THEN16050
12020 X1=1
12030 IFR(R,X1-1)=0THEN12042
12040 X1=X1+1:IFX1<7THEN12030
12041 GOTO16030
12042 TS(A)=RY(A):GOSUB16020:IFCCTHEN12060
12043 IFSI=0THEN12045
12044 CM=CM+CO:FM=FM+INT(FO/2):IM=IM+INT(IO/1.5):GOTO12060
12045 CM=CM-INT(CO/2):FM=FM-INT(FO/2):IM=IM-IO
12060 R(R,A-1)=RY(A)
12070 RY(A)=0:IN=IN-1
12080 GOSUB18000:GOSUB20000:RETURN

REM CONSUME
13000 IFA=0THEN16005
13010 IFTS(A)=0THEN16060
13020 GOSUB16020:IFCC<>1THEN16006
13030 IFSI=1THEN13080
13040 FM=FM+INT(FO/1.5):IM=IM+INT(IO/1.5):CM=CM+INT(CO*1.5):PRINT@416,"WISE MOVE";:SOUND245,1:GOTO13090
13080 FM=FM-INT(FO/1.2):IM=IM-INT(IO/1.5):CM=CM-CO:PRINT@416,"THAT WAS A MISTAKE!";:SOUND1,1
13081 IFCM<=0THENMA=3
13090 R(R,A-1)=0:GOSUB20000:RETURN

REM READ
14000 IFA=0THEN16000
14010 IFTS(A)=0THEN16060
14020 GOSUB16020:IFCC<>2THEN16080
14030 GOTO13040

REM EXAMINE
15000 IFA=0THEN16000
15010 IFTS(A)=0THEN16060
15020 IM=IM-2:PRINT@416,MS$(TS(A));:RETURN

16000 PRINT@416,"WHICH OBJECT?":RETURN
16005 PRINT@416,"PUT WHAT?":RETURN
16006 PRINT@416,"IT IS NOT CONSUMABLE!":RETURN
16010 PRINT@416,"YOU ARE OVERLOADED!":RETURN
16020 NB=TS(A):FO=FO(NB):IO=IO(NB):CO=CO(NB):CC=CC(NB):SI=SI(NB):RETURN
16030 PRINT@416,"THE ROOM IS EMPTY!":RETURN
16040 PRINT@416,"YOU DON'T HAVE THAT OBJECT!":RETURN
16050 PRINT@416,"YOU HAVE 3 OBJECTS!":RETURN
16060 PRINT@416,"IT'S NOT HERE!":RETURN
16065 PRINT@416,"WHICH MONSTER?":RETURN
16080 PRINT@416,"THERE IS NOTHING TO READ":RETURN
17000 PRINT@416,"YOU DON'T HAVE ANYTHING":RETURN

REM SEARCH ITEM ATTRIBS
REM NB=34188:FORA=1TO12:NB=NB+A*5-5:LPRINTPEEK(NB),PEEK(NB+1),PEEK(NB+2),PEEK(NB+3),PEEK(NB+4):NEXT

18000 PRINT@11*32,"CARRYING:":PRINT@12*32,"            ";:PRINT@12*32+10,"            ";:PRINT@12*32+22,""
18001 IFRY(1)=0THEN18100
18020 PRINT@12*32,"1 "O$(RY(1));
18100 IFRY(2)=0THEN18200
18130 PRINT@12*32+11,"2 "O$(RY(2));
18200 IFRY(3)=0THEN18300
18220 PRINT@12*32+22,"3 "O$(RY(3));
18300 RETURN

20000 CW=0:XK=0:RM=ASC(MID$(M$(Y),X+1,1))-97:R=N(X,Y)
20001 X1=1:FORS=0TO5:TS(X1)=R(R,S):X1=X1+1:NEXT:X1=1:FORS=6TO7:RE(X1)=R(R,S):X1=X1+1:NEXT
20003 N9=VAL(MID$(D$,RM*4+1,1)):S9=VAL(MID$(D$,RM*4+2,1)):E9=VAL(MID$(D$,RM*4+3,1)):W9=VAL(MID$(D$,RM*4+4,1))
20005 IFR=38THENGOSUB1520:GOTO20016
20010 GOSUB1500
20011 IFN9=1THENGOSUB1100
20012 IFS9=1THENGOSUB1200
20013 IFE9=1THENGOSUB1300
20014 IFW9=1THENGOSUB1400
20015 IFR=26THENGOSUB1530

20016 PRINT@246,MID$("◊“”⁄’”",DI,2);:PRINT@278,MID$("÷“—Ÿ÷“",DI,2);

20017 PRINT@416,"":PRINT@448,"":PRINT@480,"                               ";
20020 PRINT@0,"ROOM:";:PRINT@32,N$(X,Y)"    ";:PRINT@2*32,"OBJECTS:";:PRINT@9*32,"MONSTERS:";
20040 PM=208:W=1:FORS=1TO6:PRINT@(W+2)*32,"             ";:OB=TS(S):GOSUB30000:W=W+1:NEXT
20050 W1=1:PRINT@32*10,"":FORS=1TO2:MO=RE(S):GOSUB35000:W1=W1+1:NEXT
21000 IFRE(1)=0THEN22000
21010 PM=18:R9=RE(1):GOSUB25000:IFXK(R9)=1THENXK=1
22000 IFRE(2)=0THEN24030
22010 PM=26:R9=RE(2):GOSUB25000:IFXK(R9)=1THENXK=2
24030 RETURN

25000 PRINT@224+PM,MID$(M1$,R9*2+1,2);:PRINT@224+PM+32,MID$(M2$,R9*2+1,2);:RETURN

30000 IFOB=0THENRETURN
30001 PM=PM+2:PRINT@PM,MID$(O$,OB*2-1,2);
30002 PRINT@(W+2)*32,MID$(STR$(W),2)" "LEFT$(O$(OB)+"      ",11);:RETURN

35000 IFMO=0THENRETURN
35005 ONW1GOTO35020,35030
35020 PRINT@10*32,"1 "MO$(MO);:RETURN
35030 PRINT@10*32+16,"2 "MO$(MO);:RETURN
36000 DI=1:S=PEEK(9)AND128:POKE49151,S:POKE49151,128-S:RETURN

41000 CLS3:PRINT@1*32+9,"SCREEN DETAIL";:PRINT@3*32+6,"TOP LEFT: ROOM NAME ";
41010 PRINT@5*32+6,"OBJECTS IN THE ROOM ";:PRINT@7*32+6,"MONSTERS IN THE ROOM";
41020 PRINT@9*32+6,"INVENTORY. BOTTOM:  ";:PRINT@11*32+2,"DETAILS OF YOUR STRENGTH,   ";
41030 PRINT@13*32+2,"INTELLIGENCE & CONSTITUTION.";
41035 PRINT@490,"PRESS A KEY.";
41040 A$=INKEY$:A=RND(0):IFA$=""THEN41040
41050 RETURN

42000 CLS4:PRINT@1*32+12,"COMMANDS";:PRINT@3*32+5,"AT# = ATTACK MONSTER #";
42010 PRINT@5*32+5,"GE# = GET OBJECT #    ";:PRINT@7*32+5,"DR# = DROP OBJECT #   ";
42020 PRINT@9*32+5,"CO# = CONSUME OBJECT #";:PRINT@11*32+5,"RE# = READ SCROLL #   ";
42030 PRINT@13*32+5,"IN# = INVESTIGATE #   ";:RETURN

50000 RC=DL-(RND(DL+2)-1)-(RND(INT(.5*DL/1.5)+2)-1)+1:RETURN
