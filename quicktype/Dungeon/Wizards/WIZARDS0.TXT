0 POKE&H71,&HFF:POKE&H93,0:POKE&H1A6,&H7E:POKE&H1A7,&HAE:POKE&H1A8,&H75:REM COPY GUARD BY MIKE ROBERTS LAKE ORION MI.
1 CLEAR70:NN$="NOTHING NOW, JUST LOOKING.":GOTO10
2 PRINT@Y,CHR$(128);:GOTO8
3 FORZ=1TO999:NEXT:GOTO8
4 GOTO1025
5 CLS:GOTO500
6 PRINT@256,"YOU HAVE ONE!",,,,:GOTO8
7 PRINT@X,CHR$(143);
8 RETURN
9 GOTO900
10 CLS:PRINT@134,"THE WIZARD'S TOWER":PRINT@262,"A FANTASY ADVENTURE",,,,,,,"  @ 1982 BY STEVEN C. MITCHELL":P$="WILDERNESS.":GOSUB312 L=0:CLS:P=383:GA=104:GB=100:GC=102:GD=135:GE=131:GF=133:GG=163:GH=165:SP=143:GOTO30
16 RX=0:RT=RT-1:IF RT<0THEN PRINT@448,"YOU DIED OF STARVATION!":END
17 IF RT<5THEN PRINT@448,"FOOD LOW!":GOTO23
20 X$=INKEY$:X=VAL(X$):GOTO8
22 PRINT"NOT ENOUGH GOLD!"
23 FORZ=1TO400:NEXT:GOTO8
24 PRINT@448,"",,,"              ";:GOTO8
30 CLS:PRINT@70,"CHOOSE YOUR CHARACTER!":X$="         ":PRINTX$"(1) WIZARD",X$"(2) WARRIOR",X$"(3) ELF":ST=RND(6)+4:IQ=RND(6)+4
32 GOSUB20:IFX=1THEN R$=" (WIZARD)":IQ=IQ+4:GOTO40
33 IFX=2THEN R$=" (WARRIOR)":ST=ST+4:GOTO40
34 IFX=3THEN R$=" (ELF)":ST=ST+2:IQ=IQ+2:GOTO40
35 GOTO32
40 DX=RND(6)+4:HP=RND(6)+ST:INPUT"      NAME";N$
41 GP=RND(25)+60:RT=5:IF HP<8THEN HP=8
42 GOSUB90:DA=214:DB=337:DC=238:GI=11:GJ=23:GK=27:GL=36:GM=98:GN=225:GZ=293:GY=359:GQ=269:GR=342:GS=253:GT=182:GU=179:GV=217:GOTO5
50 PRINT@448,"(1)BUY (2)SELL (3)INVENTORY","(4)LEAVE";
51 IF TA$<>""AND TB$<>""AND TC$<>""THEN95
52 GOSUB20:IFX=1THEN57
53 IFX=2THEN230
54 IFX=3THEN100
55 IFX=4THEN PRINT@448,"DIRECTION?",,,;:GOTO580
56 GOTO52
57 CLS:PRINT"YOU HAVE "GP" GOLD PIECES","DO YOU WISH TO BUY:","(1)ARMOR",,"(2)WEAPONS",,"(3)MAGIC",,"(4)RATIONS"
59 GOSUB20:IFX=1THEN115
60 IFX=2THEN150
61 IFX=3THEN170
62 IFX=4THEN65
63 GOTO59
65 CLS:PRINT"RATIONS COST 1 GP EACH.":INPUT"HOW MANY DO YOU WISH";C:IFC>GP THEN GOSUB22:GOTO65
67 RT=RT+C:GOTO128
70 IF GP-C<0THEN PRINT@256,"NOT ENOUGH GOLD",,:GOTO8
80 IF EH>0THEN8
81 IFL=2THEN T=170:GOTO8
82 Z=RND(3):IFZ=1THEN T=330
83 IFZ=2THEN T=342
84 IFZ=3THEN T=175
85 GOTO8
90 TK=RND(10)
91 TL=RND(7):IF TK=TL THEN91
92 TM=RND(4):IF TM=TK OR TM=TL THEN92ELSE8
95 CLS:PRINT@165,"YOUR QUEST IS COMPLETE!",,,,,"   LONG LIVE THE NEW MONARCH!",,,,:SOUND50,5:SOUND50,5:SOUND100,15:END
100 CLS:PRINTN$R$
101 PRINT"ST:"ST,"IQ:"IQ,"DX:"DX,"HT PTS:"HP-H,"GOLD:"GP,"RATIONS:"RT,,,"*ARMOR*","*WEAPONS*",AR$,WA$,SH$,WB$,HE$,WC$,,,"*SPELLS*","*TREASURES*",SA$,TA$,SB$,TB$,SC$,TC$,SD$,,SE$;
110 X$=INKEY$:IFX$<>""THEN5ELSE110
115 CLS:PRINT"   *ARMOR*","   COST","(1)LEATHER ARMOR  10 GP","(2)CHAIN MAIL","  30 GP","(3)PLATE MAIL","  65 GP","(4)SHIELD","  15 GP","(5)HELMET","  10 GP","(6)"NN$
117 GOSUB20:IF AR>0 AND X<4 ANDX>0THEN PRINT@256,"YOU MAY HAVE ONLY ONE SUIT OF   ARMOR. YOU HAVE "AR$:GOTO117
118 IFX=1THEN AR=1:C=10:AR$="LEATHER ARMOR"
119 IFX=2THEN AR=3:C=30:AR$="CHAIN MAIL"
120 IFX=3THEN AR=5:C=65:AR$="PLATE MAIL"
121 IFX=4AND SH=2THEN GOSUB6:GOTO117
122 IFX=4THEN SH=2:C=15:SH$="SHIELD"
123 IFX=5AND HE=1THEN GOSUB6:GOTO117
124 IFX=5THEN HE=1:C=10:HE$="HELMET"
125 IFX=6THEN5
126 IFX<1OR X>7THEN117
127 GOSUB70:IF GP-C<0THEN130
128 GP=GP-C:GOTO5
130 IFX=4THEN SH=0:SH$="":GOTO117
131 IFX=5THEN HE=0:HE$="":GOTO117
132 AR=0:AR$="":GOTO117
150 CLS:PRINT"  *WEAPON*","  COST","(1)DAGGER","  4 GP","(2)HAND AXE","  7 GP","(3)SHORT SWORD"," 10 GP","(4)BATTLE AXE"," 15 GP","(5)BROADSWORD"," 20 GP","(6)"NN$
152 GOSUB20:IF WA>0 AND WB>0 AND WC>0 THEN PRINT@256,"YOU HAVE 3 WEAPONS, AND THAT","IS ALL YOU MAY HAVE.":GOSUB3:GOSUB3:GOTO5
153 IFX=1THEN X$="DAGGER":C=4
154 IFX=2THEN X$="HAND AXE":C=7
155 IFX=3THEN X$="SHORT SWORD":C=10
156 IFX=4THEN X$="BATTLE AXE":C=15
157 IFX=5THEN X$="BROADSWORD":C=20
158 IFX=6THEN5
159 IFX<1OR X>6THEN152
160 GOSUB70:IF GP-C<0THEN152
162 GP=GP-C:IF WA=0THEN WA=X:WA$=X$:GOTO5
163 IF WB=0THEN WB=X:WB$=X$:GOTO5
164 IF WC=0THEN WC=X:WC$=X$:GOTO5:ELSE152
170 CLS:PRINT"  *SPELL*","  COST","(1)MAGIC MISSILE  20 GP","(2)ESCAPE","  35 GP","(3)MAGIC SHIELD","  45 GP","(4)FIREBALL","  75 GP","(5)LIGHTNING"," 175 GP","(6)"NN$
171 GOSUB20:IFX=1THEN X$="MAGIC MISSILE":C=20
173 IFX=2THEN X$="ESCAPE":C=35
174 IFX=3THEN X$="MAGIC SHIELD":C=45
175 IFX=4THEN X$="FIREBALL":C=75
176 IFX=5THEN X$="LIGHTNING":C=175
177 IFX=6THEN5
178 IFX<1OR X>6THEN171
180 GOSUB70:IF GP-C<0THEN171
181 IFX=SA OR X=SB OR X=SC OR X=SD OR X=SE THEN PRINT@256,"YOU HAVE THAT ONE!",,:GOTO171
183 GP=GP-C:IF SA=0THEN SA=X:SA$=X$:GOTO5
184 IF SB=0THEN SB=X:SB$=X$:GOTO5
185 IF SC=0THEN SC=X:SC$=X$:GOTO5
186 IF SD=0THEN SD=X:SD$=X$:GOTO5
187 SE=X:SE$=X$:GOTO5
200 IFX<0OR X>383THENX=P
203 FORY=0TO352STEP32:IFX=Y AND P=Y-1THENX=P
204 IFP=Y AND X=Y-1THENX=P
205 NEXT:IFL=0THEN226
206 SJ=PEEK(X+1024):IF SJ=128THEN X=P
207 GOTO8
226 IFX=3OR X=7OR X=36OR X=37OR X=38OR X=68OR X=70THEN X=P
227 GOTO8
230 CLS:Z$="FOR ":Y$=" GP":Q=AR*8:U=SH*5:V=HE*3:J=WA*2:Y=WB*2:Z=WC*2:PRINT"YOU MAY SELL YOUR:","1."AR$,Z$;Q;Y$,"2."SH$,Z$;U;Y$,"3."HE$,Z$;V;Y$,"4."WA$,Z$;J;Y$,"5."WB$,Z$;Y;Y$,"6."WC$,Z$;Z;Y$,"7.NOTHING NOW, JUST PRICING."
235 GOSUB20:IFX=1THEN AR=0:AR$="":GP=GP+Q:GOTO5
237 IFX=2THEN SH=0:SH$="":GP=GP+U:GOTO5
238 IFX=3THEN HE=0:HE$="":GP=GP+V:GOTO5
239 IFX=4THEN WA=0:WA$="":GP=GP+J:GOTO5
240 IFX=5THEN WB=0:WB$="":GP=GP+Y:GOTO5
241 IFX=6THEN WC=0:WC$="":GP=GP+Z:GOTO5
242 IFX=7THEN5ELSE235
300 GOSUB24:IF XA=1THEN310
301 IF XA=2THEN350
302 IF XA=3THEN450ELSE846
310 X=RND(4):IFX=2THEN319
312 IFX=3THEN330
313 IFX=4THEN340ELSE846
319 X$="MAGIC MISSILE.":RP=8
320 PRINT@416,"HE THROWS A "X$,,,:GOSUB3
321 A=EA+RP+RND(6):D=AR+(ST/2)+HE+SH+RND(6)+MS:IF A>D THEN H=H+A-D
322 GOTO847
330 X$="FIREBALL.":RP=12:GOTO320
340 X=RND(4):IFX=1THEN X$="ST":ST=ST-1
342 IFX=2THEN X$="IQ":IQ=IQ-1
343 IFX=3THEN X$="DX":DX=DX-1
344 IFX=4THEN X$="HIT":HP=HP-1
345 PRINT@416,"HE MAGICALLY STEALS 1 "X$" POINT.":GOSUB24:GOSUB3:GOTO8
350 X=RND(3):IFX=1THEN GOSUB340:GOTO846
351 IFX=2THEN X$="WHIP!":RP=12
352 IFX=3THEN X$="FLAMING SWORD!":RP=15
353 PRINT@416,"HE USES HIS "X$,,,:GOSUB3:GOTO321
400 X=(XA*2)+1:Y=RND(30):IFX>Y THEN405ELSE8
405 PRINT@416,"YOUR WEAPON BROKE!","IT IS NOW USELESS!":IF BW=1THEN WA=0:WA$=""
407 IF BW=2THEN WB=0:WB$=""
408 IF BW=3THEN WC=0:WC$=""
409 R=0:GOTO23
410 X=RND(25)+20:PRINT@416,"YOU FIND A TREASURE CHEST!","IT CONTAINS"X"GP.":GP=GP+X
411 IF TZ=TK THEN PRINT@480,"AND THE *SCEPTER OF SPEED* !";:DX=DX+5:T$="SCEPTER (SPEED)":GOTO420
412 IF TZ=TL THEN PRINT@480,"AND THE *RING OF STRENGTH* !";:ST=ST+5:T$="RING (STRENGTH)":GOTO420
413 IF TZ=TM THEN PRINT@480,"AND THE *CROWN OF WISDOM* !";:IQ=IQ+5:T$="CROWN (WISDOM)":GOTO420
419 GOTO3
420 IF TA$=""THEN TA$=T$:GOTO3
421 IF TB$=""THEN TB$=T$:GOTO3
422 TC$=T$:GOTO3
450 Y=RND(3):GOSUB340:IFY=1GOSUB340:GOTO846
451 IFY=2THEN X$="HE BREATHES FIRE!":RP=20+RND(6)
452 IFY=3THEN X$="HE ATTACKS WITH HIS TAIL!":RP=15+RND(5)
453 PRINT@416,X$,,:GOSUB3:GOTO321
500 FORX=384TO415:PRINT@X,CHR$(131):NEXT:IF L=0THEN X$=CHR$(132):Y$=CHR$(136):PRINT@3,X$X$CHR$(126)Y$Y$;:X$=CHR$(128):PRINT@36,CHR$(244)X$CHR$(248);:PRINT@68,X$CHR$(243)X$;:GOSUB1000
501 IFL=1THEN GOSUB1500
502 IFL=2THEN GOSUB2000
511 RX=RX+1:IF RX=15GOSUB16
512 PRINT@P,"*";:IFL=0THEN GOSUB1010
513 IFL=1THEN GOSUB1550
514 IFL=2THEN GOSUB2050
520 IF SP=126OR SP=124THEN P$="SWAMP":GOSUB910
521 IF SP=111OR SP=92THEN P$="MOUNTAINS":GOSUB925
540 IF SP=71THEN SP=143
560 IFH>0THEN H=H-.5
570 IFL=2AND P=169THEN CLS:L=1:P=30:GOTO500
571 IFL=0AND P=350THEN P$="CITY."
572 IFL=0AND P=69 THEN P$="DUNGEON.":CLS:L=1:P=335:SP=143:SJ=0:GOTO500
573 IFL=1AND P=367 THEN P$="WILDERNESS":CLS:L=0:P=101:SP=143:GOTO500
574 IFL=1AND P=31THEN CLS:L=2:P=170:GOTO500
575 GOSUB24:PRINT@P,"*";:PRINT@416,"YOU ARE IN THE "P$,,,;:SOUND55,1
576 IFL=0ANDP=350THEN50
580 X=P:X$=INKEY$
581 IF X$=CHR$(94)THENX=X-32:GOTO595
582 IF X$=CHR$(10)THENX=X+32:GOTO595
583 IF X$=CHR$(8)THENX=X-1:GOTO595
584 IF X$=CHR$(9)THENX=X+1:GOTO595
585 IF X$="I"THEN100:ELSE580
595 PRINT@448,"":GOSUB200:POKE1024+P,SP:P=X:SP=PEEK(1024+P):IFL=0THEN GOSUB605:P$="WILDERNESS."
598 IFL>0THEN SP=143:GOSUB650
600 GOTO511
605 IFP=GA THEN GOSUB9:GA=101
606 IFP=GB THEN GOSUB9:GB=101
607 IFP=GC THEN GOSUB9:GC=101
608 IFP=GD THEN GOSUB9:GD=101
609 IFP=GE THEN GOSUB9:GE=101
610 IFP=GF THEN GOSUB9:GF=101
611 IFP=GG THEN GOSUB9:GG=101
612 IFP=GH THEN GOSUB9:GH=101
613 GOTO8
650 IFL=2THEN700
659 QQ=2:GOSUB1550:QQ=0
674 IFP=200AND TD=0THEN TD=1:TZ=4:GOSUB410
675 IFP=14AND TE=0THEN TE=1:TZ=5:GOSUB410
676 IFP=113AND TF=0THEN TF=1:TZ=6:GOSUB410
677 IFP=121AND TG=0THEN TG=1:TZ=7:GOSUB410
678 IFP=63AND TH=0THEN TH=1:TZ=8:GOSUB410
679 IFP=383AND TI=0THEN TI=1:TZ=9:GOSUB410
681 IFP=160AND TJ=0THEN TJ=1:TZ=10:GOSUB410
682 GOTO8
700 IFP=DA THEN GOSUB906:IF EH<=0THEN DA=170
701 IFP=DB THEN GOSUB904:IF EH<=0THEN DB=170
702 IFP=DC THEN GOSUB906:IF EH<=0THEN DC=170
705 IFP=215AND TA=0THEN TA=1:TZ=1:GOSUB410
706 IFP=369AND TB=0THEN TB=1:TZ=2:GOSUB410
707 IFP=271AND TC=0THEN TC=1:TZ=3:GOSUB410
710 GOTO8
799 PRINT@416,XX$:MD=0:MS=0:R=0:SOUND99,8
800 SOUND50,3:PRINT@448,"DO YOU:(1)RUN (2)FIGHT (3)MAGIC"
801 GOSUB20:IFX=1THEN810
803 IFX=2THEN819
804 IFX=3THEN850ELSE801
810 X=DX+MD+RND(6)-AR:Y=ES+RND(6)
811 GOSUB24:IFX>Y THEN PRINT@448,"YOU GOT AWAY":GOTO23
815 PRINT@448,"YOU DID NOT GET AWAY.",;:GOSUB23
819 IFR=2THEN826
820 GOSUB24:R=2:PRINT@416,"CHOOSE YOUR WEAPON:","(1)"WA$,"(2)"WB$,"(3)"WC$;
821 GOSUB20:BW=X:IFX=1THEN Y=WA:GOTO826
823 IFX=2THEN Y=WB:GOTO826
824 IFX=3THEN Y=WC:GOTO826
825 GOTO821
826 IF ES>DX THEN835
827 GOSUB840:IF EH<=0THEN950
828 GOSUB845:GOTO800
835 GOSUB845
836 GOSUB840:IF EH<=0THEN950ELSE800
840 A=Y+ST+RND(6):D=ED+RND(6):IF A>D THEN EH=EH-1
841 IF A>=D THEN EH=EH-1:PRINT@416,"YOU HIT HIM!",,,,,;:GOSUB23
843 IF A<D THEN PRINT@416,"YOU MISSED HIM!",,,,,;
844 GOSUB3:GOTO400
845 IF XA>0THEN300
846 A=EA+RND(6):D=AR+ST+HE+SH+RND(6)+MS:IF A>D THEN H=H+1
847 IFA>=D THEN H=H+1:PRINT@416,"HE HIT YOU!!",,"YOU HAVE"HP-H"HIT POINTS LEFT.",,;:GOSUB23
848 IF A<D THEN PRINT@416,"HE MISSED YOU",,"YOU STILL HAVE"HP-H"HT PTS.",,;
849 IF HP-H<=0THEN975ELSE3
850 PRINT@416,"WHICH SPELL?","1."SA$,"2."SB$,"3."SC$,"4."SD$,"5."SE$;:Z$="              "
851 GOSUB20:IFX=1THEN S=SA
853 IFX=2THEN S=SB
854 IFX=3THEN S=SC
855 IFX=4THEN S=SD
856 IFX=5THEN S=SE
858 IFX>0AND X<6THEN859ELSE851
859 GOSUB24:IFS=1THEN865
860 IFS=2THEN870
861 IFS=3THEN875
862 IFS=4THEN880
863 IFS=5THEN885
864 PRINT@416,"YOU THROW NO SPELL!":GOSUB23:PRINT"YOUR ENEMY ATTACKS!":GOSUB23:GOTO828
865 Z=3:S$="MAGIC MISSILE"
866 Y=Z+IQ-ED-RND(8)
867 IFY>0THEN EH=EH-Y:PRINT@416,"YOUR "S$" HITS!",,,,Z$;:GOSUB23:IF EH<=0THEN950
868 IF Y<=0THEN PRINT@416,"YOUR "S$" MISSES!",,,,Z$;:GOSUB23
869 GOTO828
870 MD=RND(IQ):GOTO810
875 MS=RND(IQ):GOTO819
880 Z=6:S$="FIRE BALL"
881 IF XA=3 THEN PRINT@416,"FIRE HAS NO EFFECT ON DRAGONS!":GOSUB23:GOTO828
882 GOTO866
885 Z=8:S$="LIGHTNING BOLT":GOTO866
900 XX$="YOU ARE ATTACKED BY A GOBLIN.":ES=RND(9):EA=RND(9)+6:ED=RND(9)+4:EH=RND(4)+4:XA=0:EGP=RND(3)-1:GOTO799
902 XX$="AN EVIL WIZARD ATTACKS.":ES=RND(7)+8:EA=RND(8)+7:ED=RND(8)+8:EH=RND(9)+6:XA=1:EGP=RND(15):GOTO799
904 XX$="A BALROG ATTACKS.":ES=RND(9)+6:EA=RND(9)+11:ED=RND(9)+9:EH=RND(9)+9:XA=2:EGP=RND(25)+10:GOTO799
906 XX$="A FIRE BREATHING DRAGON ATTACKS.":ES=RND(9)+9:EA=RND(9)+15:ED=RND(6)+16:EH=RND(9)+18:XA=3:EGP=RND(50):GOTO799
910 Y=RND(9):IFY>5THEN920
913 GOTO8
920 XX$="A GIANT SWAMP SNAKE ATTACKS!":XA=0:ES=15:EA=10:ED=5:EH=RND(3)+5:EG=0:GOTO799
925 Y=RND(5):IFY<3THEN8
927 XX$="A HILL TROLL ATTACKS!":XA=0:ES=10:EA=14:ED=9:EG=RND(3):EH=RND(5)+5:GOTO799
950 SOUND60,4:SOUND90,5:PRINT@416,"YOUR ENEMY IS DEAD!":GOSUB23:PRINT"HE HAS"EG"GOLD PIECES.";:GP=GP+EG:EX=EX+ED+EA:IF EX<100THEN23
960 HP=HP+1:DX=DX+1:IF R$=" (WIZARD)" THEN IQ=IQ+2:ST=ST+1
961 IF R$=" (WARRIOR)" THEN IQ=IQ+1:ST=ST+2
962 IF R$=" (ELF)"THEN ST=ST+1.5:IQ=IQ+1.5
965 EX=EX-100:GOTO23
975 FORX=50TO2STEP-2:SOUNDX,1:NEXT:PRINT"YOU ARE DEAD!":END
1000 PRINT@350,CHR$(159)
1001 X$="<<<<<":Y$=">>>>>>>>":PRINT@218,X$;:PRINT@239,Y$Y$;:PRINT@272,X$X$X$;:PRINT@300,Y$Y$;:PRINT@331,X$X$X$;:PRINT@365,Y$Y$
1009 X$="/\/\":PRINT@20,X$"    "X$;:PRINT@51,X$"  /\  /\/";:PRINT@82,X$"  /\/\  /\";:PRINT@113,X$"  /\"X$;:PRINT@145,"/\   "X$X$;:PRINT@178,"/\  "X$X$;:PRINT@214,X$;
1010 X=GA:GOSUB4:GA=X:X=GB:GOSUB4:GB=X:X=GC:GOSUB4:GC=X:X=GD:GOSUB4:GD=X:X=GE:GOSUB4:GE=X:X=GF:GOSUB4:GF=X:X=GG:GOSUB4:GG=X:X=GH:GOSUB4:GH=X:GOTO8
1025 PRINT@X," ";:Y=RND(5):IFY=1THENZ=X+1
1027 IFY=2THENZ=X-1
1028 IFY=3THENZ=X+32
1029 IFY=4THENZ=X-32
1030 Y=PEEK(1024+Z):IFY=143OR Y=106OR Y=96THEN Z=Z ELSE Z=X
1031 IFZ<0OR Z>383THEN Z=X
1038 X=Z:IFX=P THEN GOSUB900:X=352
1040 PRINT@X,"G";:GOTO8
1500 X$=CHR$(128):Z$=X$+X$:Y$=X$+Z$+Z$:PRINT@2,X$Z$Y$;:Y=64:GOSUB2:Y=96:GOSUB2:FORY=192TO288STEP32:GOSUB2:NEXT:Y=193:GOSUB2:FORY=130TO194STEP32:GOSUB2:NEXT:PRINT@66,Z$Z$X$;:PRINT@100,X$Z$;
1501 FORY=134TO198STEP32:GOSUB2:NEXT:PRINT@228,X$Y$;:FORY=40TO136STEP32:GOSUB2:NEXT:FORY=41TO201STEP32:GOSUB2:NEXT:PRINT@258,Z$X$;:Y=290:GOSUB2:PRINT@322,Y$;:PRINT@294,Z$Z$;
1502 PRINT@360,Z$Y$;:PRINT@368,Y$X$;:FORY=43TO299STEP32:GOSUB2:NEXT:PRINT@300,X$Z$;:PRINT@140,Z$Y$;:FORY=174TO238STEP32:GOSUB2:NEXT:Y=237:GOSUB2:FORY=13TO77STEP32:GOSUB2:NEXT:Y=78:GOSUB2
1503 PRINT@16,Z$Y$:FORY=48TO112STEP32:GOSUB2:NEXT:PRINT@82,Z$X$;:Y=114:GOSUB2:FORY=26TO186STEP32:GOSUB2:NEXT:PRINT@86,Z$Z$;:Y=57:GOSUB2:Y=118:GOSUB2:PRINT@149,Z$;
1504 PRINT@180,Z$;:PRINT@208,X$Y$;:Y=240:GOSUB2:Y=272:GOSUB2:PRINT@304,X$Y$;:PRINT@275,X$Z$;:PRINT@184,Z$;:FORY=215TO343STEP32:GOSUB2:NEXT:FORY=250TO346STEP32:GOSUB2:NEXT:Y=249:GOSUB2
1505 PRINT@344,Z$;:FORY=60TO220STEP32:GOSUB2:NEXT:FORY=284TO380STEP32:GOSUB2:NEXT:FORY=61TO125STEP32:GOSUB2:NEXT:PRINT@94,Z$;:FORY=190TO318STEP32:GOSUB2:NEXT:FORY=191TO319STEP32:GOSUB2:NEXT:GOTO8
1550 T=GI:GOSUB1600:GI=T
1551 T=GJ:GOSUB1602:GJ=T
1552 T=GK:GOSUB1602:GK=T
1553 T=GL:GOSUB1604:GL=T
1554 T=GM:GOSUB1600:GM=T
1555 T=GN:GOSUB1602:GN=T
1556 T=GZ:GOSUB1604:GZ=T
1557 T=GY:GOSUB1600:GY=T
1558 T=GQ:GOSUB1602:GQ=T
1559 T=GR:GOSUB1600:GR=T
1560 T=GS:GOSUB1604:GS=T
1561 T=GT:GOSUB1602:GT=T
1562 T=GU:GOSUB1600:GU=T
1563 T=GV:GOSUB1600:GV=T:IFQQ=2THEN8
1564 PRINT@367,"#";:PRINT@31,"%";:X$=CHR$(239)
1565 IF TD=0THEN PRINT@200,X$;
1566 IF TE=0THEN PRINT@14,X$;
1567 IF TF=0THEN PRINT@113,X$;
1568 IF TG=0THEN PRINT@121,X$;
1569 IF TH=0THEN PRINT@63,X$;
1570 IF TI=0THEN PRINT@383,X$;
1571 IF TJ=0THEN PRINT@160,X$;
1572 GOTO8
1579 IF QQ=2THEN8
1580 Y=RND(4):PRINT@T," ";:IFY=1THENZ=T+1
1582 IFY=2THENZ=T-1
1583 IFY=3THENZ=T+32
1584 IFY=4THENZ=T-32
1585 IFZ<0OR Z>383THEN Z=T
1586 Y=PEEK(1024+Z):IFY=128THEN Z=T
1588 T=Z:GOTO8
1600 GOSUB1579:IFT=P THEN GOSUB9:GOSUB80
1601 PRINT@T,"G";:GOTO8
1602 GOSUB1579:IFT=P THEN GOSUB902:GOSUB80
1603 PRINT@T,"W";:GOTO8
1604 GOSUB1579:IFT=P THEN GOSUB904:GOSUB80
1605 PRINT@T,"B";:GOTO8
1606 GOSUB1580:IFT=P THEN GOSUB906:GOSUB80
1607 PRINT@T,"D";:GOTO8
2000 X$="     ":CLS(0):PRINT@448,"",,,,:PRINT@169,X$X$X$;:PRINT@214,"  ";:FORX=209TO369STEP32:GOSUB7:NEXT:FORX=170TO362STEP32:GOSUB7:NEXT:X=363:GOSUB7:FORX=236TO364STEP32:GOSUB7:NEXT:PRINT@236,"    ";:PRINT@270,"  ";:GOTO8
2050 T=DA:GOSUB1606:DA=T
2051 T=DB:GOSUB1604:DB=T
2052 T=DC:GOSUB1606:DC=T
2053 PRINT@169,"#";:X$=CHR$(239)
2054 IF TA=0THEN PRINT@215,X$;
2055 IF TB=0THEN PRINT@369,X$;
2056 IF TC=0THEN PRINT@271,X$;
2060 GOTO8
