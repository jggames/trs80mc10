1 DEFINTB-F,W
10 CLS:PRINT@530,"DUNGEONS AND DRAGONS":CLEAR300:DIMW(6,9),W$(6),A(9),MN$(10),I$(2):RANDOM
20 FORI=0TO10:READMN$(I):NEXT:FORI=0TO6:READW$(I):NEXT:I$(0)="FIST":FORI=2TO9:READA(I):NEXT
50 FORJ=0TO6:FORI=0TO9:READW(J,I):NEXT:NEXTJ
75 CLS:INPUT"WHAT IS YOUR NAME";N$:INPUT"WHAT GOD DO YOU BELIEVE IN";G$
90 GOSUB890:DX=RO:GOSUB890:ST=RO+15:ES=ST:AC=RND(3)+1:M1=10+RND(25)
100 MO=M1:PRINT"YOUR DEX=";DX,"STRENGTH=";ST,"ARMOR CLASS=";AC,"MONEY=";MO;"GOLD  PIECES.":FORA=1TO2
110 INPUT"WHAT WEAPON DO YOU WANT";A$
120 FORI=0TO6:IFLEFT$(A$,3)=LEFT$(W$(I),3)THEN130
125 NEXT:PRINT"WHAT?   START OVER":GOTO100
130 IFMO-W(I,1)<0PRINT"YOU DON'T HAVE ENOUGH MONEY":GOTO110
140 I$(A)=W$(I):MO=MO-W(I,1):PRINT"NOW HAVE";MO;"GOLD PIECES":NEXTA
150 TC=MO*2:CA=MO:PRINT"YOU MAY ALSO HAVE";TC;"TORCHES AND";CA;"ELVEN CAKES":GOTO30000
200 A$="":PRINT"IN COMMAND MODE:":INPUTA$
205 IFA$=""ORA$="TRAVEL"RETURN
206 IFA$="N"ORA$="S"ORA$="E"ORA$="W"ORA$="U"ORA$="D"RETURN
207 IFA$="SCORE"PRINT"YOUR PRESENT SCORE IS";SC:GOTO200
210 IFA$="EAT"PRINT"EAT WHAT?":GOTO200
220 IFA$="SAY"PRINT"SAY WHAT?":GOTO200
230 IFA$="LIGHT LAMP"IFLF=2PRINT"OK.  IT WILL ACT AS AN ADDITIONAL TORCH.":TI=TI-20:LF=0:RETURNELSEIFLF=0GOSUB380:GOTO200
240 IFA$="EAT CAKE"IFCA>0THENES=ES+RND(6):CA=CA-1:GOTO260:ELSEPRINT"BUT YOU HAVE NO ELVEN CAKES":GOTO200
243 IFLEFT$(A$,8)="EAT MUSH"PRINT"IT DOESN'T TASTE THAT BAD":GOTO200
245 IFLEFT$(A$,6)="EAT FU"IFRND(2)=1GOTO13535ELSEPRINT"IT HAD A RATHER BITTER TASTE.":GOTO200
250 IFA$="EAT MOLD"ORA$="FILL LAMP"ORA$="SAY BLUB"ORA$="RUB LAMP"ORA$="TAKE"RETURN:'
255 GOSUB750:GOTO200
260 IFES>STTHENES=ST
262 IFCK=1ANDRND(2)=1GOSUB870:GOSUB900:PRINT"THE ELVEN CAKES WERE INFECTED WITH MUMMY ROT!":GOTO50000
270 PRINT"YOUR STRENGTH IS NOW UP TO";EST:GOTO200
380 PRINT"I'M GAME -- TELL ME HOW":RETURN
750 PRINT". . . NOTHING UNUSUAL HAPPENS":RETURN
800 PRINT"IN TRAVEL MODE:
802 IFRND(5)=1PRINT"YOU HEAR A RUSTLING NOISE IN A NEARBY ROOM!
804 PRINT"DIRECTION? ";
805 A$=INKEY$:IFA$=""THEN805ELSEPRINTA$
806 G=0:TI=TI+1:IFTI>18PRINT"YOUR TORCH BURNT OUT -- I ASSUME YOU WILL LIGHT ANOTHER":TI=0:TC=TC-1:PRINT"YOU NOW HAVE";TC;"TORCHES LEFT.
807 IFTC=0PRINT"YOU NOW HAVE NO TORCHES":GOSUB870:GOSUB900:PRINT"IT IS NOW PITCH BLACK!":GOSUB900:PRINT"YOU HAVE FALLEN INTO A PIT AND BROKE EVERY BONE IN YOUR BODY!":GOTO50000
810 IFA$="W"G=1
820 IFA$="E"G=2
830 IFA$="N"G=3
840 IFA$="S"G=4
850 IFA$="U"G=5
860 IFA$="D"G=6
862 IFA$="b"G=7:'
863 IFG=0THEN804ELSERETURN
870 PRINT"* * WARNING * *":RETURN
880 PRINT"* * ALERT * *":RETURN
890 RO=RND(6)+RND(6)+RND(6):RETURN
900 FORI=1TO800:NEXT:RETURN
910 PRINT"THERE IS NOWHERE TO GO IN THAT DIRECTION":RETURN
920 ML=0:PRINT"THERE IS A ";MN$(MN);" IN THIS ROOM.
930 PRINT"IT IS ARMED WITH A ";W$(MB);" AND IS ARMOR CLASS";MR:RETURN
940 J=0:INPUT"DO YOU WANT TO TRY TO RUN";A$:RETURN
950 SC=SC+3:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:J=0
960 IFRND(3)=1AND(A$="Y"ORA$="YES")PRINT"YOU GOT AWAY":J=1:SC=SC-3
962 RETURN
970 MN=RND(4):MB=RND(4):MR=RND(5)+4:MA=0:MS=RND(18):RETURN
1000 PRINT"- YOU ARE IN THE ROOM OF MISTS -  A THICK STEEL BLUE MIST SWIRLSOVER THE FLOOR",,,,"THERE IS A LIGHT ABOVE YOU AND A PASSAGE GOING EAST.
1100 IFRND(0)>.2ANDFA=1THEN1500
1110 GOSUB970:GOSUB950:IFJ=1GOTO1800ELSEGOSUB40000:FA=1:SC=SC+2
1500 GOSUB200
1800 GOSUB800:IFG=2GOTO16000
1810 IFG=5GOTO30000
1820 GOSUB910:GOTO1800
2000 PRINT"- YOU ARE IN THE SLAB ROOM -  A LARGE SLAB HAS FALLEN FROM THE  CEILING ONTO THE FLOOR.":PRINT"PASSAGES GO NORTH, EAST AND WEST.
2050 PRINT"A DEEP HOLLOW VOICE SAYS  -B L U B-!
2060 IF FB=1THEN2100ELSEPRINT"THERE ARE TWO ELVEN CAKES ON THE SLAB!"
2100 IFRND(0)>.1GOTO2500
2110 GOSUB970:GOSUB950:IFJ=1GOTO2800ELSEGOSUB40000
2500 GOSUB200
2510 IFA$="TAKE"CA=CA+2:FB=1:PRINT"GOT 'EM"
2800 GOSUB800:ONGGOTO16000,17000,22000
2810 GOSUB910:GOTO2800
3000 PRINT"- YOU ARE IN THE PARLOR OF THE DEAD -   ASSORTED BONES LIE SCAT-TERED OVER THE FLOOR.":PRINT"PASSAGES OPEN TO THE EAST, WEST, AND SOUTH.
3100 IFFC=1THEN3500
3110 MN=9:MB=RND(3)+3:MR=3:MA=3:MS=RND(15)+10:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1GOTO3800ELSEFC=1:GOSUB40000:SC=SC+10
3500 GOSUB200
3800 GOSUB800:ONGGOTO17000,21000,3810,18000
3810 GOSUB910:GOTO3800
4000 PRINT"- YOU ARE IN THE ANTE-ROOM -  THE WALLS ARE PANELLED WITH BLACK WALNUT; PLUSH FURNITURE LIES ON A THICK RED CARPET.":PRINT"THERE IS A DOOR TO THE EAST.
4100 IFFD=1THEN4500
4110 MN=0:MB=2:MR=2:MA=3:MS=RND(10)+15:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1THEN4800ELSEFD=1:GOSUB40000:SC=SC+13
4150 PRINT"THE VAMPIRE IS WEARING A JEWEL ENCRUSTED RING!"
4500 GOSUB200
4510 IFA$="TAKE"PRINT"IT IS A RING OF PROTECTION!  YOUR  AC. IS NOW 2!":AC=2
4800 GOSUB800:IFG=2GOTO5000
4810 GOSUB910:GOTO4800
5000 PRINT"-YOU ARE IN THE pURPLE VAULT-  THE WALLS WERE HEWN OUT OF A PUR-PLE STONE.  THE CEILNG ARCHES FAR OVERHEAD.":PRINT"PASSAGES GO NORTH, EAST AND WEST.
5100 IFFE=1THEN5500
5105 IF FE<>1PRINT"THERE IS AN ELVEN CAKE IN THE CORNER!
5110 MN=RND(3)+4:MB=RND(5)+1:MR=RND(5)+1:MA=1:MS=RND(10)+8:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1THEN5800ELSEFE=1:GOSUB40000:SC=SC+9
5500 GOSUB200
5510 IFA$="TAKE"CA=CA+1:PRINT"GOT IT"
5800 GOSUB800:ONGGOTO4000,20000,18000
5810 GOSUB910:GOTO5800
6000 PRINT"- YOU ARE IN THE CHAMBER OF FIRES -    THE AIR IS STIFLING HOT, AND THE WALLS ARE WARM TO THE TOUCH. A RED GLOW EMINATES FROM   THE SMOKE ALONG THE SE CORNER.
6010 PRINT"PASSAGES OPEN NORTH AND WEST.
6100 IF FF=1THEN6500
6110 MN=8:MB=3:MR=3:MA=0:MS=RND(5)+11:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1THEN6800ELSEFF=1:GOSUB40000:SC=SC+11
6200 PRINT"THERE IS A BOX IN THE CORNER WITH A FLASK IT!
6500 GOSUB200
6510 IFA$="TAKE"PRINT"IT IS A POTION OF SUPERHUMAN SPEED!":DX=DX+3
6800 GOSUB800:ONGGOTO20000,6810,21000
6810 GOSUB910:GOTO6800
7000 PRINT"-YOU ARE IN THE ORIENTAL ROOM-    ORIENTAL DRAWINGS DECORATE THEWALLS":PRINT"DOORS GO NORTH, EAST AND WEST.
7200 IF FG=1THEN7500ELSEPRINT"THERE IS A ANCIENT BRASS LAMP IN THE CENTER OF THE ROOM!"
7500 GOSUB200
7510 IFA$="TAKE"THENFG=1:LF=1:PRINT"IT'S YOURS"
7800 GOSUB800:ONGGOTO22000,24000,23000
7810 GOSUB910:GOTO7800
8000 PRINT"-YOU ARE IN THE GROTTO OF THE LOST-":PRINT"A STEEL DOOR CLOSED BEHIND YOU -- THERE IS NO ESCAPE!
8100 IFFH=1THEN8500
8110 MN=7:MB=5:MR=5:MA=3:MS=RND(10)+15:GOSUB890:MD=RO:GOSUB880:GOSUB920:FH=1:GOSUB900:GOSUB40000:SC=SC+14
8200 PRINT"THE ";MN$(MN);" IS WEARING A AMULET.
8500 GOSUB200
8510 IFA$="TAKE"PRINT"IT SEEMS TO HAVE INCREASED YOUR STRENGTH!":EST=EST+8:ST=ST+8:GOSUB200
8515 IFA$="SAY BLUB":SC=SC+2:GOTO11000:'
8520 GOTO8000
9000 PRINT"-YOU ARE IN THE DUST BIN-   THIS IS A CIRCULAR ROOM, AND A PILE OF RUBBISH LIES AGAINST THE EAST WALL":PRINT"THERE SEEMS TO BE AN OPENING ABOVE THE RUBBISH, AND A PASSAGE TOTHE WEST.
9100 IFRND(0)>.2THEN9500
9110 GOSUB970:GOSUB950:IFJ=1GOTO9800ELSEGOSUB40000
9500 GOSUB200
9800 GOSUB800:IFG=1GOTO23000
9810 IFG=5GOTO25000
9820 GOSUB910:GOTO9800
10000 PRINT"-YOU ARE IN THE PIT ROOM-     ALONGSIDE YOU THERE IS A TEN-FOOT DEEP PIT":PRINT"DOORS GO TO THE EAST AND WEST.
10100 IFFJ=1THEN10500
10110 MN=4:MB=RND(3)+1:MR=RND(3)+3:MA=2:MS=RND(10)+5:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1THEN10800ELSEFJ=1:GOSUB40000:SC=SC+8
10500 GOSUB200
10800 GOSUB800:ONGGOTO24000,19000
10810 IFG=6GOTO31000
10820 GOSUB910:GOTO10800
11000 PRINT"-YOU ARE IN THE CRYPT OF THE MINDLESS-   THERE IS A SARCOPHOGUS IN THE CENTER OF THE ROOM":PRINT"DOORS OPEN TO THE EAST AND WEST.
11200 IFCK=0PRINT"THERE IS TWO ELVEN CAKES ON THE SARCOPHAGUS!
11500 GOSUB200
11510 IFA$="TAKE"CK=1:CA=CA+2:PRINT"GOT 'EM"
11800 GOSUB800:ONGGOTO19000,12000
11810 GOSUB910:GOTO11800
12000 PRINT"-YOU ARE IN THE TORTURE ROOM-  ASSORTED GRUESOME EQUIPMENT FILLSTHE ROOM":PRINT"DOORS OPEN TO THE EAST AND WEST.
12100 IFFL=1THEN12500
12110 MN=10:MB=6:MR=4:MA=1:MS=RND(15)+20:GOSUB890:MD=RO:GOSUB880:GOSUB920:GOSUB940:GOSUB960:IFJ=1THEN12800ELSEFL=1:GOSUB40000:SC=SC+18
12500 GOSUB200
12800 GOSUB800:ONGGOTO11000,27000
13000 PRINT"-YOU ARE IN MYCORHYZZIA-  MOLDS, FUNGUS AND LARGE MUSHROOMS FILLTHE ROOM":PRINT"PASSAGES GO TO THE EAST AND WEST AND A STAIRCASE GOES UP.
13500 GOSUB200
13510 IFA$<>"EAT MOLD"THEN13800
13520 INPUT"WHAT COLOR DO YOU PREFER";A$
13530 IFA$="YELLOW"PRINT"ALL OF YOUR WOUNDS HAVE BEEN CURED!":ES=ST:GOTO13500
13535 PRINT"YOU HAVE POISONED YOURSELF!":GOTO50000
13800 GOSUB800:ONGGOTO27000,28000,13810,13810,26000
13810 GOSUB910:GOTO13800
14000 PRINT"-YOU ARE IN THE DRAGON'S LAIR-":PRINT"A LARGE OPENING LEADS TO THE WEST."
14100 IFFO=1THEN14800
14200 PRINT"AN ENORMOUS GREEN DRAGON WAS SLEEPING ON A BEAUTIFUL PERSIAN RUG":GOSUB900:PRINT"HOWEVER YOU JUST WOKE HIM UP, AND HE IS  V E R Y  ANGRY!"
14210 INPUT"COMMAND";A$
14220 IFA$="RUN"ORA$="TRAVEL"ORA$="T"PRINT"THE DRAGON SLAMMED THE DOOR CLOSED WITH ITS HUGE TAIL.":GOTO14210
14230 IFA$="ATTACK"ORA$="KILL"ORA$="FIGHT"GOTO14500
14235 IFA$="CLUE"ORA$="GIVE CLUE"PRINT"TRY USING SOMETHING YOU HAVE FOUND":GOTO14210
14240 IFLEFT$(A$,4)="FEED"PRINT"THE DRAGON ENJOYED THE MEAL --YOU AND THE ELVEN CAKES!":GOTO50000
14245 IFLEFT$(A$,4)="GIVE"PRINT"DRAGONS DON'T LIKE CHARITY -- IF THEY WANT SOMETHING, THEY TAKE IT.":GOTO14210
14250 IFLEFT$(A$,4)="THRO"PRINT"YOU INFURIATED THE DRAGON AND HE FLAMED YOU TO A CRISP!":GOTO50000
14260 IFA$<>"b"ANDA$<>"BLUB"THEN14265ELSEPRINT"GOOD TRY, ";:'
14262 PRINT"BUT THE SECRET WORD WILL NOT WORK YET":GOTO14210:'
14265 IFA$="RUB LAMP"ANDLF=1GOTO14600:'
14268 IFA$="PRAY"PRINT"NOT EVEN ";G$:PRINT"COULD DESTROY A DRAGON THIS LARGE WITH A LIGHTING BOLT":GOTO14210
14270 PRINT"WHAT?  TRY SOMETHING ELSE":GOTO14210
14500 INPUT"WITH YOUR PUNY LITTLE WEAPON";A$:IFA$="Y"ORA$="YES"PRINT"THE DRAGON FLAMED YOU WELL DONE!":GOTO50000
14501 GOTO14210
14600 PRINT"POOF !":PRINT:PRINT"A GENII POPPED OUT OF YOUR";:'
14610 PRINT" LAMP!":GOSUB900:PRINT:PRINT"THE GENII BANISHE";:'
14620 PRINT"D THE DRAGON WITH A SNAP OF THE FINGERS!":'
14700 PRINT"HE THANKED YOU FOR FREEING HIM AND DISAPPEARED!":FO=1:SC=SC+25
14800 GOSUB800:IFG=1GOTO28000
14810 IFG=7GOTO29000:'
14820 GOSUB910:PRINT"IF YOU'RE TRYING TO GET IN A SECRET ";:'
14830 PRINT"ROOM, TRY RUBBING YOUR LAMP":'
14840 PRINT"IN A CORRIDOR ELSEWHERE.":GOTO14800:'
15000 PRINT"-YOU ARE IN A MAZE OF LITTLE TWISTY PASSAGES, ALL DIFFERENT-
15500 GOSUB200
15800 GOSUB800:IFRND(0)>.12GOTO15000
15810 ONRND(2)GOTO25000,26000
16000 PRINT"- YOU ARE IN THE COBBLED HALL -  LARGE MOSS-COVERED COBBLESTONESCOVER THE FLOOR":PRINT"PASSAGES OPEN TO THE EAST AND WEST
16100 IFRND(0)>.1GOTO16500
16110 GOSUB970:GOSUB950:IFJ=1GOTO16800ELSEGOSUB40000
16500 GOSUB200
16800 GOSUB800:IFG=1GOTO1000
16810 IFG=2GOTO2000
16820 GOSUB910:GOTO16800
17000 PRINT"- YOU ARE IN THE NARROW TUNNEL -":PRINT"THERE ARE OPENINGS TO THE EAST AND WEST.
17100 IFRND(0)>.2GOTO17500
17110 GOSUB970:GOSUB950:IFJ=1GOTO17800ELSEGOSUB40000
17500 GOSUB200
17800 GOSUB800:ONGGOTO2000,3000
17810 GOSUB910:GOTO17800
18000 PRINT"- YOU ARE IN A TIGHT PASSAGE - MOLD APPEARS TO BE GROWING ABOVE.PASSAGES GO NORTH AND SOUTH.
18100 IFRND(0)>.2THEN18500
18110 GOSUB970:GOSUB950:IFJ=1GOTO18800ELSEGOSUB40000
18500 GOSUB200
18510 IFA$="EAT MOLD"PRINT"YOU HAVE POISONED YOURSELF!":GOTO50000
18800 GOSUB800:IFG=3GOTO3000
18810 IFG=4GOTO5000
18820 GOSUB910:GOTO18800
19000 PRINT"-YOU ARE AT THREE WAY INTERSECTION-":PRINT"PASSAGES GO EAST, WEST, AND SOUTH.
19100 IFRND(0)>.1THEN19500
19110 GOSUB970:GOSUB950:IFJ=1GOTO19800ELSEGOSUB40000
19500 GOSUB200
19800 GOSUB800:ONGGOTO10000,11000,19810,21000
19810 GOSUB910:GOTO19800
20000 PRINT"- YOU HAVE ENTERED THE TUNNEL OF THE LOST -  IT IS SAID THAT ANYONE WHO ENTERS THIS TUNNEL WILL NEVER RETURN BY THE SAME ROUTE  PASSAGES OPEN TO THE EAST AND WEST
20100 IFRND(0)>.25THEN20500
20110 GOSUB970:GOSUB950:IFJ=1GOTO20800ELSEGOSUB40000
20500 GOSUB200
20800 GOSUB800:ONGGOTO20840,6000
20820 GOSUB910:GOTO20800
20840 PRINT"A SPIKE-STUDDED DOOR WAS SLAMMED IN YOU FACE --  YOU CANNOT GET OUT THIS WAY":GOTO20800
21000 PRINT"-YOU ARE AT THREE CORNERS-":PRINT"DOORS OPEN TO THE NORTH, SOUTH AND WEST
21100 IFRND(0)>.1THEN21500
21110 GOSUB970:GOSUB950:IFJ=1GOTO21800ELSEGOSUB40000
21500 GOSUB200
21800 GOSUB800:ONGGOTO3000,21810,19000,6000
21810 GOSUB910:GOTO21800
22000 PRINT"- YOU ARE IN HIGH HALLS -    THE CEILING IS FAR ABOVE YOUR HEAD.THERE ARE OPENINGS SOUTH OF YOU, AND ANOTHER TURNS EAST
22100 IFRND(0)>.05GOTO22500
22110 GOSUB970:GOSUB950:IFJ=1GOTO22800ELSEGOSUB40000
22500 GOSUB200
22800 GOSUB800:IFG=2GOTO7000ELSEIFG=4THEN2000
22820 GOSUB910:GOTO22800
23000 PRINT"-YOU ARE IN T-SHAPED PASSAGE-":PRINT"DOORS OPEN TO THE EAST, WEST AND SOUTH.
23100 IFRND(0)>.15THEN23500
23110 GOSUB970:GOSUB950:IFJ=1GOTO23800ELSEGOSUB40000
23500 GOSUB200
23800 GOSUB800:ONGGOTO8000,9000,23810,7000
23810 GOSUB910:GOTO23800
24000 PRINT"-YOU ARE IN THE PADDED PASSAGE-":PRINT"THERE ARE OPENINGS TO THE EAST AND WEST.
24100 IFRND(0)>.05THEN24500
24110 GOSUB970:GOSUB950:IFJ=1GOTO24800ELSEGOSUB40000
24500 GOSUB200
24800 GOSUB800:ONGGOTO7000,10000
24810 GOSUB910:GOTO24800
25000 PRINT"-YOU ARE IN THE DUSTY CORRIDOR-":PRINT"THE PASSAGE GOES EAST AND WEST"
25100 IFRND(0)>.5THEN25500
25110 GOSUB970:GOSUB950:IFJ=1GOTO25800ELSEGOSUB40000
25500  GOSUB200
25510 IFA$="RUB LAMP"ANDLF=1PRINT"A TINY VOICE SAYS  'TRY SHIFT B'.":' 
25800 GOSUB800:ONGGOTO9000,15000
25810 GOSUB910:GOTO25800
26000 PRINT"-YOU ARE IN THE HALL OF 100 STAIRS-",,"DOORS OPEN AT THE TOP AND BOTTOM OF THE STAIRCASE.
26100 IFRND(0)>.3THEN26500
26110 GOSUB970:GOSUB950:IFJ=1GOTO26800ELSEGOSUB40000
26500 GOSUB200
26800 GOSUB800:IFG=5GOTO15000
26810 IFG=6GOTO13000
26820 GOSUB910:GOTO26800
27000 PRINT"-YOU ARE IN THE HALL OF NO RETURN-",,"DOORS GO EAST AND WEST.
27100 IFRND(0)>.6THEN27500
27110 GOSUB970:GOSUB950:IFJ=1GOTO27800ELSEGOSUB40000
27500 GOSUB200
27800 GOSUB800:IFG=2GOTO13000
27900 IFG=1PRINT"A SPIKE STUDDED DOOR SLAMMED SHUT IN YOUR FACE!":GOTO27800
27910 GOSUB910:GOTO27800
28000 PRINT"-YOU ARE ON THE CATWALK-  A FIERCE WIND CHILLS YOU AS YOU STAND OVER A BOTTOMLESS CHASM":PRINT"DOORS OPEN TO THE EAST AND WEST.
28100 IFRND(0)>.4THEN28500
28110 GOSUB970:GOSUB950:IFJ=1GOTO28800ELSEGOSUB40000
28500 GOSUB200
28800 GOSUB800:ONGGOTO13000,14000
28810 GOTO50000
29000 PRINT"-YOU ARE IN THE NICHE-":PRINT"A PASSAGE GOES UPWARD.
29100 IF FZ=1THEN29500
29200 PRINT"THERE IS AN EGG SIZED DIAMOND IN HERE!":'
29500 GOSUB200
29510 IFA$="TAKE"FZ=1:SC=SC+20:PRINT"YOU GOT IT!":'
29800 GOSUB800:IFG=5GOTO14000
29810 GOSUB910:GOTO29800
30000 PRINT"- YOU ARE IN THE GRAY FOREST - IMPENETRABLE FOREST SURROUNDS YOUTHERE IS A PIT THAT OPENS BELOW YOU
30200 IFFZ=1PRINT"YOU HAVE ESCAPED THE DUNGEON WITH THE DIAMOND!":PRINT:PRINT"CONSIDER YOURSELF A SEASONED EXPLORER AND A DAMM LUCKY PERSON!":SC=SC+20:GOTO50500
30500 GOSUB200
30800 GOSUB800
30810 IFG=6GOTO1000
30820 GOSUB910:GOTO30800
31000 PRINT"-YOU ARE IN THE PIT-
31200 PRINT"THERE IS A SMALL AMOUNT OF OIL BUBLING OUT OF A CRACK
31500 GOSUB200
31510 IFLF=1ANDA$="FILL LAMP"PRINT"OK.  YOU MAY LIGHT IT WHEN YOU WANT":LF=2:' 
31800 GOSUB800:IFG=5GOTO10000
31810 GOSUB910:GOTO31800
40000 CLS:PRINT"IN COMBAT MODE:":Z=0:YD=0
40020 PRINT:SL=0:MW=0
40030 A=RND(DX):B=RND(MD):IFA=BTHEN40030
40040 IFA>B+MATHEN41000
40045 IFML=4GOTO40180
40070 IFRND(0)>.9ANDMS>5GOTO40190
40080 PRINT"THE ";MN$(MN);" SWINGS ITS ";W$(MB);" !  ";
40090 N=RND(20)+W(MB,AC)+YD-ML+SL
40100 IFN>=20PRINT"EXTREME DAMAGE!":HI=RND(W(MB,0))*2+2:GOTO40130
40105 IFN>=A(AC)+2PRINT"MAJOR DAMAGE !":HI=RND(W(MB,0))+2:GOTO40130
40110 IFN>=A(AC)PRINT"HIT !":HI=RND(W(MB,0)):GOTO40130
40112 IFN=<0PRINT"THE ";MN$(MN);"'S ";W$(MB);" BROKE !":MB=0:Z=Z+1:ELSEPRINT"LUCKY . . . IT MISSED":GOTO40020
40114 IFZ<>3GOTO40020ELSEPRINT"THE ";MN$(MN);" SURRENDERS    DO YOU ACCEPT";:INPUTA$:IFA$="YES"ORA$="Y"PRINT"THE ";MN$(MN);" RUNS AWAY !":RETURN
40116 PRINT"YOU ANGERED ";G$;"!!":GOTO50000:'
40130 ES=ES-HI:PRINT"DAMAGE=";HI;" ";N$;"'s STRENGTH=";ES
40140 IFEST<0GOTO50000
40150 IFHI>8YD=4:PRINT"YOU WERE KNOCKED DOWN !":GOTO40020
40170 GOTO40020
40180 PRINT"THE ";MN$(MN);" STANDS UP":ML=0:GOTO40020
40190 PRINT"THE ";MN$(MN);" MAKES A WILD SLASH WITH ITS ";W$(MB);" !",:MS=MS-1:SL=4:GOTO40090
41000 FORI=1TODX*8
41030 A$=INKEY$:IFA$<>""THEN41050
41040 NEXT:GOTO40020
41050 IFA$="A"GOTO41110
41060 IFA$="B"SL=4:ES=ES-1:GOTO41110
41070 IFA$="S"GOTO41100
41075 IFA$="P"GOTO41090
41080 GOTO41040
41090 IFRND(0)>.9PRINTG$;" DESTROYED THE ";MN$(MN);" WITH A LIGHTING BOLT!":RETURN
41095 GOTO40020
41100 YD=0:PRINTN$;" STANDS UP":GOTO40020
41110 INPUT"WEAPON";A$
41120 FORI=0TO2
41130 IFLEFT$(A$,3)=LEFT$(I$(I),3)THENA$=I$(I):GOTO41150
41135 NEXT
41140 PRINT"YOU DONT HAVE A ";A$:GOTO40020
41150 FORW=0TO6
41160 IFA$=W$(W)THEN41180
41170 NEXT
41180 N=RND(20)+W(W,MR)-YD+ML+SL:PRINT"RESULT -- ";
41190 IFN<=1PRINT"YOUR ";A$;" BROKE!":I$(I)="":GOTO40020
41200 IFN>=20PRINT"EXTREME DAMAGE!":MW=RND(W(W,0))*2+2:GOTO41230
41205 IFN>A(MR)+2PRINT"MAJOR DAMAGE !":MW=RND(W(W,0))+2:GOTO41230
41210 IFN>=A(MR)PRINT"HIT !":MW=RND(W(W,0)):GOTO41230
41220 PRINTN$;" MISSED":GOTO40020
41230 MS=MS-MW:PRINT"DAMAGE=";MW;" ";MN$(MN);"'S STRENGTH=";MS
41235 IFMW>=8PRINT"THE ";MN$(MN);" FELL DOWN!":ML=4
41240 IFMS<0PRINT"YOU KILLED IT!":RETURN
41250 GOTO40020
50000 PRINT"YOU HAVE BEEN KILLED!!":SC=SC-10
50500 PRINT:PRINT"YOUR SCORE WAS";SC
60000 DATA VAMPIRE,GOBLIN,GNOME,DWARF,BESERKER,HOBGOBLIN,GNOLL,TROLL,GARGOYLE,DEMON,STONE GIANT
60010 DATAFIST,DAGGER,MACE,SWORD,BATTLE AXE,MORNING STAR,TWO-HANDED SWORD
60020 DATA17,16,15,14,13,12,11,10
60030 DATA2,0,-5,-4,-3,-2,-1,0,0,0
60040 DATA4,4,-3,-3,-1,0,0,0,1,2
60050 DATA6,11,0,1,0,0,0,0,0,0
60060 DATA7,10,-2,-1,0,0,0,0,0,0
60070 DATA8,12,-1,0,1,1,0,0,0,0
60080 DATA8,15,0,0,1,2,1,1,2,2
60090 DATA10,20,1,2,3,3,2,2,2,2

