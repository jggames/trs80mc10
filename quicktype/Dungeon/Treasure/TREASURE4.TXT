102 CLS:PRINT@103,"TREASURE DUNGEON 2"
105 CLEAR500
120 PA=10:PD=10:PT=0
125 W1$=CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)
130 O1$=CHR$(255)+CHR$(255)+"/"+CHR$(255)+CHR$(255)
135 T1$=CHR$(255)+CHR$(255)+" "+CHR$(255)+CHR$(255)
138 C1$=CHR$(255)+"   "+CHR$(255):C2$="     "
140 P1$=CHR$(143)+CHR$(143)+CHR$(142)+CHR$(143)+CHR$(143)
141 P2$=CHR$(143)+CHR$(143)+CHR$(130)+CHR$(135)+CHR$(143)
142 P3$=CHR$(143)+CHR$(143)+CHR$(135)+CHR$(135)+CHR$(143)
150 M1$=CHR$(143)+CHR$(138)+CHR$(128)+CHR$(143)+CHR$(143)
151 M2$=CHR$(143)+CHR$(140)+CHR$(132)+CHR$(141)+CHR$(143)
152 M3$=CHR$(143)+CHR$(136)+CHR$(130)+CHR$(141)+CHR$(143)
160 WH$=CHR$(255)+CHR$(255)+"-"+CHR$(255)+CHR$(255)
170 WV$=CHR$(255)+CHR$(255)+"I"+CHR$(255)+CHR$(255)
180 DIM L(4):K$="":W$="":FORLL=1TO32:CL$=CL$+CHR$(32):NEXT
190 GOSUB9000
195 IR=1:PR=1
200 CLS
210 PRINT"PREPARE FOR AN ADVENTURE."
220 PRINT"WITH LUCK YOU WILL SURVIVE."
230 PRINT
240 FORI=1TO4:PRINTAD$(I):NEXT
243 PRINT:PRINT"THERE ARE";NR;"SECTIONS AND";NM
245 PRINT"MONSTERS IN THE DUNGEON."
250 PRINT:PRINT"COMMANDS:":PRINT"n-NORTH","s-SOUTH","e-EAST","w-WEST":PRINT"d-DRAW","q-QUERY"
270 GOSUB5000
500 CLS:GOSUB6000
550 IFRM(IR)<>0THENGOSUB7000
610 PRINT@288,"COMMAND? (n,e,s,w,d,q)"
620 D$=INKEY$:IFD$=""THEN620
630 IFD$="N"THENI=1:GOTO700
640 IFD$="E"THENI=2:GOTO700
650 IFD$="S"THENI=3:GOTO700
660 IFD$="W"THENI=4:GOTO700
670 IFD$="D"THENGOSUB6000
680 IFD$="Q"THENGOSUB1000
690 GOTO610
700 K$=RIGHT$(RP$(IR,I),1)
715 IF K$="O"THENGOSUB3500
720 IF K$="W"OR K$="S"THENGOSUB2000
730 IF K$="D"OR K$="F"THENGOSUB3000
740 IF K$="P"OR K$="C"THENGOSUB4000
750 IF K$="T" THEN PRINT"YOU FALL THRU A TRAP!";:GOSUB5500:GOSUB4000
800 GOTO500
1000 CLS
1015 IH=INT(TM/60):IM=TM-60*IH
1020 PRINT"YOU'VE BEEN GONE FOR":PRINTIH;"HOURS AND";IM;"MINUTES."
1030 S$="STRONG"
1032 IF PA<=6 THEN S$="WEARY":IF PA<=3 THEN S$="WEAK"
1035 C$="NOT"
1037 IF PD<=8 THEN C$="SLIGHTLY":IF PD<=6 THEN C$="MODERATELY":IF PD<=3 THEN C$="SEVERELY"
1040 PRINT:PRINT"YOU FEEL ";S$;", AND","ARE ";C$;" WOUNDED."
1050 PRINT:PRINT"YOU HAVE DEFEATED":PRINTMK;"MONSTER(S)."
1060 PRINT:PRINT"YOU HAVE TREASURE WORTH":PRINTPT;"GOLD PIECES."
1065 IFMK=NMTHENPRINT:PRINT"YOU DEFEATED ALL THE MONSTERS.":PRINT"THE GAME IS NOW COMPLETED.":END
1070 IFPA+PD>16THEN1120
1080 PRINT:PRINT"YOU NEED TO RECOVER."
1085 PRINT"HOW MANY HOURS WILL YOU REST?"
1090 I$=INKEY$:IFI$=""THEN1090
1091 H=VAL(I$):IFH<1THEN1120
1092 PRINT"RESTING ";:FORIH=1TOH:FORX=1TO1000:NEXTX:PRINT" Z";:NEXT:PRINT
1093 PRINT"YOU REST FOR";H;"HOURS.":TM=TM+60*H
1094 PA=PA+RND(H):IFPA>10THENPA=10
1096 PD=PD+RND(H):IFPD>10THENPD=10
1120 IFIR<>1THEN1150
1125 PRINT:PRINT"YOU ARE BACK AT THE ENTRANCE.":PRINT"DO YOU WISH TO QUIT? (y,n)"
1130 K$=INKEY$:IFK$=""THEN1130
1135 IFK$="Y"THENPRINT"COME BACK AGAIN.":END
1150 GOSUB5000
1170 GOSUB6000
1190 RETURN
2000 PRINT@288,"YOU ARE AT THE ";D$;" WALL.":GOSUB5500
2020 PRINT@320,"DO YOU WISH TO SEARCH IT? (y,n)"
2030 I$=INKEY$:IFI$=""THEN2030
2040 IFI$<>"Y"THENRETURN
2045 TM=TM+5
2050 IF K$<>"S"ORRND(3)>=2THENPRINT"YOU FIND NOTHING.":GOTO2000
2060 PRINT"YOU FIND A SECRET DOOR."
2070 GOSUB3000
2080 RETURN
3000 IX=VAL(RP$(IR,I))
3020 PRINT@288,"YOU ARE AT THE ";D$;" DOOR.":GOSUB5500
3030 IFK$="O"THEN3500
3040 PRINT@320,"lISTEN, oPEN, OR rETURN?"
3050 I$=INKEY$:IFI$=""THEN3050
3060 IFI$="L"THEN3100
3070 IFI$="O"THEN3200
3075 IFI$="R"THENRETURN
3080 GOTO3040
3100 TM=TM+2:IFRM(IX)<>0THEN3120
3110 IFRND(10)>1THENPRINT"YOU HEAR NOTHING.":GOTO3190
3115 GOTO3130
3120 IFRND(3)>=2THENPRINT"YOU HEAR NOTHING.":GOTO3190
3130 ONRND(4)GOTO3132,3134,3136,3138
3132 PRINT"THERE IS A STRANGE SOUND.":GOTO3190
3134 PRINT"A MUFFLED SCREAM IS HEARD...":GOTO3190
3136 PRINT"A CLANKING NOISE":GOTO3190
3138 PRINT"SOMETHING IS BEING DRAGGED.":GOTO3190
3190 GOTO3020
3200 TM=TM+3
3210 IFRND(2)>=2ORK$="F"THENPRINT"THE DOOR DOES NOT OPEN.":GOTO3020
3215 PRINT"THE DOOR OPENS.":GOSUB5500
3220 IFRIGHT$(RP$(IR,I),1)<>"D" THEN3500
3225 RP$(IR,I)=STR$(IX)+"O"
3230 J=I+2:IFJ>4THENJ=J-4
3240 IFVAL(RP$(IX,J))=IRANDRIGHT$(RP$(IX,J),1)="D"THENRP$(IX,J)=STR$(IR)+"O"
3500 IX=VAL(RP$(IR,I))
3510 IFRM(IX)<>0ANDRND(2)=2THENPRINT@288,"THERE IS A MONSTER THERE!"
3520 PRINT@320,"THE DOOR IS OPEN, ENTER? (y,n)"
3530 I$=INKEY$:IFI$=""THEN3530
3540 IFI$="Y"THENGOSUB4000
3550 RETURN
4000 TM=TM+5
4010 IX=VAL(RP$(IR,I))
4015 IFIX<1ORIX>NRTHENPRINT@320,"YOU CAN'T GO THAT WAY.":GOSUB5500:RETURN
4030 IFRND(6)<=3ORK$<>"O"THEN4080
4040 RP$(IR,I)=STR$(IX)+"D"
4050 J=I+2:IFJ>4THENJ=J-4
4060 IFVAL(RP$(IX,J))=IRTHENRP$(IX,J)=STR$(IR)+"D"
4080 PR=IR:IR=IX
4090 RETURN
5000 X$=INKEY$
5020 PRINT@480,"<PRESS ANY KEY TO CONTINUE>";
5030 IFINKEY$=""THEN5030
5040 RETURN
5500 FORX=1TO500:NEXT:RETURN
6000 CLS:PRINT@0,RD$(IR);
6025 L(1)=77:L(2)=82:L(3)=205:L(4)=76
6030 FORI=1TO4
6035 IFI=1ORI=3THENST=1:W$=WH$:GOTO6040
6037 ST=32:W$=WV$
6040 K$=RIGHT$(RP$(IR,I),1)
6050 IFK$="W"ORK$="S"THENW$=W1$
6060 IFK$="O"THENW$=O1$
6070 IFK$="P"ORK$="T"THENW$=T1$
6080 IFK$="C"THENW$=C1$:IFI=1ORI=3THENW$=C2$
6087 FORLL=1TO5:PRINT@L(I),MID$(W$,LL,1);:L(I)=L(I)+ST:NEXT
6090 NEXT
6095 IFMD(RM(IR))<=0THENPRINT@109,P1$;:PRINT@141,P2$;:PRINT@173,P3$;:GOTO6100
6098 PRINT@109,M1$;:PRINT@141,M2$;:PRINT@173,M3$;
6100 RETURN
7000 IM=RM(IR):IFMD(IM)<=0THENRM(IR)=0:RETURN
7020 PRINT@224,MD$(IM)
7025 PRINT:IFMA(IM)>PDTHENPRINT"IT LOOKS PRETTY DANGEROUS.":GOTO7040
7030 PRINT"YOU CAN PROBABLY HANDLE IT."
7040 PRINT@448,"rUN OR fIGHT?"
7050 I$=INKEY$:IFI$=""THEN7050
7060 IFI$="R"THEN7100
7070 IFI$="F"THEN7200
7080 GOTO7050
7100 TM=TM+2:CLS:PRINT@224,"";
7110 IFRND(MA(IM))>PDTHENPRINT"THE MONSTER WOUNDS YOU.":SOUND1,1:PD=PD-1:GOTO7120
7115 PRINT"YOU ESCAPE UNHARMED. "
7120 FORJ=1TO4
7122 IF VAL(RP$(IR,J))=PRTHENI=J
7124 NEXT
7126 IFI>0ANDI<=4THEN7130
7128 I=RND(4):IFVAL(RP$(IR,I))=0THEN7128
7130 IFRND(6)>=3ORPT=0THEN7180
7140 LT=INT(PT*RND(5)/10):PT=PT-LT
7150 PRINT:PRINT"YOU DROP TREASURE WORTH";LT;"GP."
7160 TV(MT(IM))=TV(MT(IM))+LT
7180 FORX=1TO2000:NEXT
7185 GOSUB4000:GOSUB6000
7190 RETURN
7200 TM=TM+1
7204 PRINT@288,CL$;CL$;CL$;CL$;CL$
7205 PRINT@288,"THE MONSTER STRIKES AT YOU..."
7210 MP=MA(IM)-PD:IFMP<2THENMP=2
7215 FORX=1TO300:NEXT
7220 PX=RND(MP)-1:IFPX>0THENPRINT"THE MONSTER HITS YOU":SOUND1,1:GOTO7230
7225 PRINT"THE MONSTER MISSES"
7230 IFPX>0THENPA=PA-PX:PD=PD-PX
7240 IFPD<=0THENPRINT"YOU ARE KILLED!":END
7250 FORX=1TO400:NEXT:PRINT"YOU STRIKE AT THE MONSTER..."
7255 FORX=1TO300:NEXT
7260 PM=PA-MD(IM):IFPM<2THENPM=2
7270 MX=RND(PM)-1:IFMX>0THENPRINT"YOU WOUND THE MONSTER":SOUND200,1:GOTO7280
7275 PRINT"A MISS..."
7280 IFMX>0THENMA(IM)=MA(IM)-MX:MD(IM)=MD(IM)-MX
7290 IFMD(IM)<=0THEN7400
7295 IFPD<=3THENPRINT"YOU ARE SEVERELY WOUNDED. RUN!"
7300 GOTO7040
7400 GOSUB5500
7403 RM(IR)=0:MK=MK+1:GOSUB6000
7405 PRINT@256,"THE MONSTER IS DEFEATED!":PRINT
7410 IFMT(IM)=0THENPRINT"YOU FIND NO TREASURE":GOTO7470
7420 PRINT"THERE IS A TREASURE!"
7430 IT=MT(IM)
7440 PRINTTD$(IT)
7450 PT=PT+TV(IT)
7455 PRINT"IT IS WORTH";TV(IT);"GOLD PIECES."
7460 MT(IM)=0
7470 GOSUB5000
7480 GOSUB6000
7490 RETURN
9000 RESTORE
9040 READAD$(1),AD$(2),AD$(3),AD$(4),NR,NM,NT
9050 DIMRD$(NR),RP$(NR,4),RM(NR)
9060 FORI=1TONR
9070 READ IR,RD$(IR),RP$(IR,1),RP$(IR,2),RP$(IR,3),RP$(IR,4),RM(IR)
9080 NEXT
9110 DIMMD$(NM),MA(NM),MD(NM),MT(NM)
9120 FORI=1TONM
9130 READIM,MD$(IM),MA(IM),MD(IM),MT(IM)
9140 NEXT
9210 DIMTD$(NT),TV(NT)
9220 FORI=1TONT
9230 READIT,TD$(IT),TV(IT)
9240 NEXT
9290 RETURN
9310 DATA "ADVENTURE # 1"
9312 DATA THERE ARE MANY DANGERS HERE.
9314 DATA ORCS HAVE BEEN REPORTED.
9315 DATA BEWARE THE FIERCE MINOTAUR...
9318 DATA 75,13,13
9401 DATA  1,THIS IS THE DUNGEON ENTRANCE    WITH AN OPENING IN THE CEILING.,2D,6P,30P,4S,0
9402 DATA  2,A SMALL DRY STONE ROOM WITH SOMEBONES IN THE CORNERS.,W,W,W,3D,0
9403 DATA  3,A LONG RECTANGULAR ROOM.,22D,2D,4C,14D,1
9404 DATA  4,A LONG RECTANGULAR ROOM WITH    MOLD ON THE SOUTH WALL.,3C,1S,5D,W,1
9405 DATA  5,A SMALL SQUARE STONE BLOCK ROOM.,4D,30P,28S,W,0
9406 DATA  6,A LONG WINDING CORRIDOR GOING   UP.,7P,W,8S,1P,0
9407 DATA  7,A NARROW WINDING CORRIDOR GOING WEST.,W,27S,6P,22T,0
9408 DATA  8,A GREAT ROUND ROOM WITH A HIGH  DOMED CEILING.,6S,26D,13D,28D,2
9409 DATA  9,THE NORTHEAST CORNER OF A LRRGE L-SHAPED ROOM.,14S,W,10C,11C,0
9410 DATA 10,THE SOUTHERN END OF A LARGE     L-SHAPED ROOM.,9C,W,12D,W,0
9411 DATA 11,THE WESTERN END OF AN L-SHAPED  ROOM.,W,9C,W,65P,10
9412 DATA 12,A LONG CURVING PASSAGE,10D,13P,W,W,0
9413 DATA 13,A LONG CURVING PASSAGE.,8D,W,W,12P,0
9414 DATA 14,A DIRTY ROOM FULL OF ORC SIGNS.,W,3D,9S,W,0
9415 DATA 15,THERE ARE SIGNS OF PASSAGE HERE.,W,22P,W,16D,0
9416 DATA 16,THIS OPENS UP INTO A LARGE ROOM.,W,15D,W,17C,0
9417 DATA 17,THE ROOM SHOWS SIGNS OF USE.,18C,16C,21P,63D,0
9418 DATA 18,THERE ARE SOME SKELETONS IN THE NW CORNER.,W,19C,17C,W,0
9419 DATA 19,THERE ARE BONES SCATTERED ABOUT.,W,20D,W,18C,0
9420 DATA 20,THE ROOM SMELLS OF ROTTING      FLESH.,W,W,W,19D,4
9421 DATA 21,THE ROOM IS DUSTY AND FULL OF   SPIDER WEBS.,17P,W,11S,W,5
9422 DATA 22,A WELL USED CORRIDOR.,23P,W,3D,15P,0
9423 DATA 23,A LONG NORTH-SOUTH CORRIDOR.,29P,24D,22P,49D,0
9424 DATA 24,A LONG WINDING CORRIDOR.,W,W,25P,23D,0
9425 DATA 25,A LONG WINDING CORRIDOR.,24P,W,26P,27S,0
9426 DATA 26,A LONG WINDING CORRIDOR.,25P,W,W,8D,0
9427 DATA 27,A FLIGHT OF STAIRS GO UPWARD TO THE WEST.,W,25D,W,7D,6
9428 DATA 28,A NARROW CURVING PASSAGE.,5D,8D,W,W,0
9429 DATA 29,A LONG CORRIDOR.,31P,W,23P,W,0
9430 DATA 30,THE CORRIDOR CURVES FROM NORTH  TO WEST.,1P,W,W,5P,0
9431 DATA 31,A LONG N-S CORRIDOR.,32P,38D,29P,45D,0
9432 DATA 32,THE CORRIDOR ENDS.,33D,W,31P,W,0
9433 DATA 33,A SMALL SQUARE ROOM.,F,34D,32D,41D,0
9434 DATA 34,THERE ARE STRANGE SOUNDS IN THE EAST.,W,35P,W,33D,0
9435 DATA 35,DEBRIS IS SCATTERED ABOUT.,W,W,37D,34P,7
9436 DATA 36,THE WALL SEALS BEHIND YOU.,W,W,W,32S,0
9437 DATA 37,THERE ARE SOUNDS FROM BEHIND    THE DOOR.,35D,W,39C,W,0
9438 DATA 38,A ROOM OF STONE BLOCKS.,36D,39D,W,31D,0
9439 DATA 39,AN ORDINARY ROOM.,37C,W,W,38D,0
9440 DATA 40,ROUGH STONE WALLS DRIP MOISTURE.,W,41C,42D,W,0
9441 DATA 41,THERE IS DAMPNESS IN THE AIR.,W,33D,W,40C,0
9442 DATA 42,NO SIGNS OF RECENT ACTIVITY.,40D,W,44C,W,0
9443 DATA 43,THIS ROOM HAS BEEN A MONSTER'S  LAIR.,W,W,45S,W,8
9444 DATA 44,THERE ARE MYSTERIOUS MARKINGS   ON THE WEST WALL.,42C,45D,W,52S,0
9445 DATA 45,A SENSE OF FOREBODING FILLS THE ROOM.,43S,31D,W,44D,0
9446 DATA 46,THERE ARE ANIMAL DROPPINGS HERE.,W,W,48C,W,0
9447 DATA 47,THERE ARE SKELETONS SCATTERED   ABOUT.,W,W,49D,W,9
9448 DATA 48,THERE HAS BEEN PASSAGE THRU     HERE.,46C,49P,W,58D,0
9449 DATA 49,A WIDE PASSAGE.,47D,23D,W,48P,0
9450 DATA 50,THE LAIR OF THE MINOTAUR.,40S,51P,53P,W,3
9451 DATA 51,A MAZE.,50P,51P,54P,50P,0
9452 DATA 52,A MAZE.,51P,44S,55P,51P,0
9453 DATA 53,A MAZE.,50P,54P,53P,50P,0
9454 DATA 54,A MAZE.,51P,54P,57P,53P,0
9455 DATA 55,A MAZE.,52P,W,58P,54P,0
9456 DATA 56,A MAZE.,53P,57P,59P,53P,0
9457 DATA 57,A MAZE.,54P,58P,59P,56P,0
9458 DATA 58,A LABYRINTH.,55P,48D,57P,57P,0
9459 DATA 59,A MAZE.,56P,57P,61C,56P,0
9460 DATA 60,CLEAR,61C,60C,60C,60C,0
9461 DATA 61,CLEAR,59C,63C,60C,62C,0
9462 DATA 62,KEEP OUT! CONSTRUCTION SITE.,61C,61C,60C,64D,0
9463 DATA 63,"STRAIGHT AHEAD LEADS THE WAY,   OTHER WAYS WILL GO ASTRAY.",61C,17D,60C,61C,0
9464 DATA 64,THERE ARE TOOLS AND BROKEN ROCK SCATTERED ABOUT.,61P,62D,60P,P,11
9465 DATA 65,A SPRING FLOWS OUT OF THE ROCK  AND DOWNHILL TO THE WEST.,W,11P,W,66P,0
9466 DATA 66,THE STREAM DISAPPEARS UNDER THE WESTERN WALL.,W,65P,67P,68S,0
9467 DATA 67,A PASSAGE CARVED OUT OF THE     ROCK.,66P,70D,72D,69D,0
9468 DATA 68,THE STREAM FLOWS THRU A NARROW  CRACK IN THE FLOOR,W,66D,W,W,12
9469 DATA 69,BONES AND SKELETONS ARE         EVERYWHERE.,W,F,71S,W,0
9470 DATA 70,SOME EMPTY BOXES LIE ABOUT.,W,W,W,67D,0
9471 DATA 71,THE WALLS ARE ROUGH HEWEN STONE.,W,72C,W,W,0
9472 DATA 72,A LARGE ROOM.,67D,73C,74C,71C,0
9473 DATA 73,DUST LIES THICK HERE.,W,W,W,72C,0
9474 DATA 74," ",72C,W,W,75P,0
9475 DATA 75,BONES AND STONES ARE SCATTERED  ABOUT.,W,74P,W,W,13
9501 DATA  1,FOUR ORCS ARE GNAWING ON A      GNOME.,7,6,1
9502 DATA  2,THREE SKELETONS RISE OUT OF     COFFINS.,6,5,2
9503 DATA  3,A SAVAGE MINOTAUR RUSHES AT YOU!,11,11,3
9504 DATA  4,FIERCE CARRION EATING GHOULS    RUSH AT YOU.,7,5,4
9505 DATA  5,A HUGE SPIDER DROPS FROM THE    CEILING.,10,5,5
9506 DATA  6,A LARGE POISONOUS CENTIPEDE     COMES OUT OF A CRACK.,6,2,6
9507 DATA  7,A BAND OF TROLLS IS MUNCHING ON BONES.,8,7,7
9508 DATA  8,A LARGE GREEN CENTIPEDE         IS FEEDING OFF A CORPSE.,9,4,8
9509 DATA  9,A LARGE BUGBEAR RUSHES OUT TO   CRUSH YOU!,9,8,9
9510 DATA 10,A GIANT RAT RUSHES OUT OF A HOLEIT LOOKS HUNGRY.,5,3,10
9511 DATA 11,SOMES DWARVES WITH PICKS AND    SHOVELS ARE TUNNELING.,5,5,11
9512 DATA 12,A LARGE SNAKE COMES OUT OF THE  WATER.,5,3,0
9513 DATA 13,A DELIGHTED OGRE THINKS HIS     DINNER HAS ARRIVED!,8,7,13
9601 DATA  1,A SMALL WOODEN CHEST CONTAINS   SOME GOLD.,150
9602 DATA  2,ONE SKELETON WEARS A SWORD WITH A JEWELED HILT.,90
9603 DATA  3,A HEAP OF SILVER AND GEMS AWAITSYOU.,230
9604 DATA  4,THERE ARE SOME COINS IN THE     RUBBISH.,40
9605 DATA  5,THERE IS A LARGE SAPHIRE IN THE SPIDER'S BELLY.,200
9606 DATA  6,THERE IS A GOLD COIN IN A CRACK IN THE FLOOR.,1
9607 DATA  7,THERE IS A GOOD SET OF CHAIN    MAIL IN THE CORNER.,125
9608 DATA  8,YOU FIND A SMALL PURPLE GEM.,60
9609 DATA  9,A BOX OF TRINKETS AND COPPER    PIECES.,20
9610 DATA 10,IN THE RAT'S HOLE IS A SHINY    BRACELET.,50
9611 DATA 11,THEY RUN AWAY AND LEAVE THEIR   TOOLS BEHIND.,5
9612 DATA 12,NONE,0
9613 DATA 13,THERE IS A BOX OF COINS AND     TRINKETS.,120
