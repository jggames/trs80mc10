0 CLS3:CLEAR384:DIMM,N,M$,A:GOSUB3000:GOTO2500
7 PRINTM$;:N=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORM=N-LEN(M$)+1TON:POKEM,PEEK(M)-64:NEXT:RETURN
9 M=VARPTR(M$(N))+65536:POKE(PEEK(M+2)*256)+PEEK(M+3)+O-1,P:RETURN

10 ?@83,"LEVEL: "L"  ";:?@115,"ENERGY:"LEFT$(STR$(E)+"   ",5);:?@147,"TIME:       ";:?@214,D$(D);:IFK9=.THEN15
11 D1$="("+VD$:IFX1>EXTHEND1$=D1$+"W)":GOTO14
12 IFX1<EXTHEND1$=D1$+"E)":GOTO14
13 D1$=D1$+") "
14 PRINT@274,"EXIT: "D1$;:PRINT@306,"LEVEL:"E1" ";
15 Y3=Y+Y1:X3=X+X1:X4=X(D-1):X5=X(D):X6=X(D+1):Y4=Y(D-1):Y5=Y(D):Y6=Y(D+1):P1=ASC(MID$(M$(Y3+Y4),X3+X4))-32
16 P2=ASC(MID$(M$(Y3+Y6),X3+X6))-32:P3=ASC(MID$(M$(Y3+Y5),X3+X5))-32:P4=ASC(MID$(M$(Y3+Y5*2+Y4),X3+X5*2+X4))-32
17 P5=ASC(MID$(M$(Y3+Y5*2+Y6),X3+X5*2+X6))-32:P6=ASC(MID$(M$(Y3+Y5*3),X3+X5*3))-32:P7=ASC(MID$(M$(Y3+Y5*2),X3+X5*2))-32
18 P8=ASC(MID$(M$(Y3+Y5*4),X3+X5*4))-32:M=2:IFP1=3THENM=1:IFP3=3THENM=3:GOTO20
19 IFP3=3THENM=4
20 Q=3:IFP2=3THENQ=4:IFP3=3THENQ=2:GOTO30
21 IFP3=3THENQ=1
30 IFP3=3THENN=5:P=5:GOTO60
31 IFP7>3THENR$="Ο":IFP7=4THENR$="^"
40 N=2:IFP4=3THENN=1:IFP6=3ORP7=3THENN=3:GOTO50
41 IFP6=3THENN=4
50 P=3:IFP5=3THENP=4:IFP6=3ORP7=3THENP=2:GOTO60
51 IFP6=3THENP=1
60 IFM>2ORQ<3THENO=4:GOTO90
80 IFN>3ORP<3THENO=3:GOTO90
81 O=2:IFP8>3THENO=1:C$(8,1)="Δ":IFP8=4THENC$(8,1)="^"
90 A=.:PRINT@33,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=1:PRINT@65,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);
91 A=2:PRINT@97,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=3:PRINT@129,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);
92 A=4:PRINT@161,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=5:PRINT@193,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);
93 A=6:PRINT@225,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=7:PRINT@257,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);
94 A=8:PRINT@289,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=9:PRINT@321,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);
95 A=10:PRINT@353,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=11:PRINT@385,A$(A,M)N$(A,N)R$P$(A,P)E$(A,Q);
96 A=12:?@417,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:A=13:?@449,A$(A,M)N$(A,N)C$(A,O)P$(A,P)E$(A,Q);:IFE<1THEN1000
100 R$=Z$:IFR9=1THENR9=0:P7=ASC(MID$(M$(Y+Y1),X+X1))-32:P3=0:X5=0:Y5=0:GOTO300

200 T=T-1:IFT<1THEN1000
206 PRINT@152,T;:A$=INKEY$:IFA$=""THEN200
230 IFA$="W"THEN300
240 IFA$="A"THEND=D-1:GOTO280
250 IFA$="D"THEND=D+1:GOTO280
260 IFA$="U"THEN400
270 IFA$="S"THEN500
272 IFA$="H"THEN800
275 GOTO200

280 T=T-25:IFD=0THEND=4
287 IFD=5THEND=1
290 GOTO10

300 IFP7=4THEN1600
305 IFP3=3THENSOUND1,1:GOTO10
307 IFP7=5THEN600
310 X1=X1+X5*2:Y1=Y1+Y5*2:IFP7=1THEN700
345 E=E-1:T=T-35:GOTO10

400 IFL<3THEN420
405 PRINT@195,;:M$="YOU ARE ON ":GOSUB7:PRINT@226,;:M$="THE TOP LEVEL":GOSUB7
407 FORA=1TO1500:NEXT:GOTO10
420 FORB=33TO417STEP32:PRINT@B,"€€€€€€€€€€€€€€€";:PRINT@B+32,"οοοοοοοοοοοοοοο";:SOUNDB/2,1:NEXT
460 E=E-20:X=X+32:L=L+1:T=T-35:X5=0:Y5=0:GOTO2560

500 SOUND30,1
510 A1=0:D2=100:X2=X+X1:Y2=Y+Y1:FORA=1TO20
520 X3=BX(A):Y3=BY(A):IFX3=0ORX3<XORX3>X+35THEN530
525 D1=SQR((X3-X2)^2+(Y3-Y2)^2):IFD1<D2THEND2=D1:A1=A
530 NEXT:D2=D2/2:IFA1>0THEN540
535 PRINT@226,;:M$="NO MORE BOXES":GOSUB7:PRINT@258,;:M$="ON THIS LEVEL":GOSUB7:GOTO590
540 PRINT@227,;:M$="NEAREST BOX":GOSUB7:Y3=BY(A1):X3=BX(A1)
565 D$=B$(SGN(Y2-Y3)+2)+B$(SGN(X2-X3)*2+2)
585 PRINT@260,;:M$=MID$(STR$(D2),2,4):GOSUB7:PRINT@265,;:M$="-"+D$+"-":GOSUB7
587 E=E-5:T=T-40
590 FORA=1TO2500:NEXT:GOTO10

600 BP=BP+1
610 X9=X+X1+X5*2:Y9=Y+Y1+Y5*2:FORA=1TO20
620 IFBX(A)=X9ANDBY(A)=Y9THEN640
630 NEXT:GOTO10
640 A1=A:A=20:NEXT:A=A1:IFA<>KBTHEN670
650 K9=1:FORA=1TO6:PRINT@258,;:M$=" KEY TO EXIT ":GOSUB7:SOUND227,2
652 PRINT@258," KEY TO EXIT ";:SOUND196,1:NEXT:GOSUB1500
654 PRINT@252,"€σς";:PRINT@274,"EXIT: ";:PRINT@306,"LEVEL: ";:PRINT@284,"€ϋϊ";:PRINT@316,"€υπ";:PRINT@348,"€όπ";
656 X1=X1+X5*2:Y1=Y1+Y5*2:N=Y+Y1:O=X+X1:P=32:GOSUB9
657 IFA1=KBTHENB1=0
659 FORA=1TO200:NEXT:GOTO685
670 FORA=1TO3:PRINT@258,;:M$=" ENERGY PACK ":GOSUB7:SOUND10,1
672 PRINT@258," ENERGY PACK ";:SOUND227,1:NEXT:FORA=1TO150:NEXT
680 E=E+40:X1=X1+X5*2:Y1=Y1+Y5*2:N=Y+Y1:O=X+X1:P=32:GOSUB9
685 BX(A1)=0:BY(A1)=0:GOTO10

700 FORA=1TO3:PRINT@231,"pit";:SOUND93,2:PRINT@231,"PIT";
720 SOUND223,2:NEXT
725 FORB=417TO33STEP-32:PRINT@B,"οοοοοοοοοοοοοοο";:PRINT@B+32,"€€€€€€€€€€€€€€€";:SOUNDB/2,1:NEXT
730 L=L-1:X=X-32:R9=1:GOTO2560

800 NB=0
810 FORA=1TO20:IFBX(A)=0THEN830
820 IFINT((BX(A)-4)/32)+1=LTHENNB=NB+1
830 NEXT
840 PRINT@194,;:M$="BOXES ON THIS":GOSUB7:PRINT@228,;:M$="LEVEL:"+STR$(NB):GOSUB7
845 IFB1=0THEN880
850 PRINT@290,;:M$="KEY BOX IS ON":GOSUB7:PRINT@325,;:M$="LEVEL"+STR$(B1):GOSUB7
880 FORA=1TO2500:NEXT
890 T=T-40:E=E-150:GOTO10

1000 SOUND102,4:SOUND102,2:SOUND141,6:FORA=1TO50:NEXT:SOUND102,4:SOUND141,2:SOUND166,6:FORA=1TO50:NEXT
1011 SOUND102,4:SOUND102,2
1020 SOUND141,5:SOUND141,4:SOUND141,2:SOUND166,5:SOUND102,4:SOUND166,2:SOUND102,5:SOUND102,4:SOUND102,2:SOUND141,7
1030 CLS0
1031 IFE<=0THENE=0
1032 IFT<=0THENT=12000
1035 SC=SC+K9*1500+E*12+INT((12000-T)/4)+BP*50:CLS0:POKE49151,64
1040 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
1050 PRINT"€€€€€€€€€€€€€€€€€€€€€€player€€€€";
1060 PRINT"€€€€€€€ƒƒƒƒƒ€€€€€€€€€score€at€€€";
1070 PRINT"€€€€ƒ        ƒ€€€€€€€€point€€€€";
1080 PRINT"€€€‡    €    ‹€€€€€of€death€€€";
1090 PRINT"€€…      €      €€€€€";:M$=RIGHT$("000000"+MID$(STR$(SC),2),6):GOSUB7:PRINT"€€€€";
1100 PRINT"€€       ƒ       €€€€€€€€€€€€€€€";
1110 PRINT"€…   HERE LIES   €€€€is€there€€";
1120 PRINT"€‡  AN  UNKNOWN  ‹€€€€€anyone€€€";
1130 PRINT"€ ADVENTURER  WHO €€€€€who€is€€€";
1140 PRINT"… DIED FROM ARGON €€€prepared€€";
1150 PRINT"…  RAY  EXPOSURE  €€to€attempt€";
1160 PRINT"… WHILE TRYING TO €€€the€maze€€";
1170 PRINT"…   ESCAPE  THE   €€€€";:M$="AGAIN?":GOSUB7:PRINT"€€€";
1180 PRINT"…  MAZE OF ARGON  €€€€€€€€€€€€€";
1190 PRINT"…                 €€€€";:M$="(Y/N)":GOSUB7
1260 GOSUB1410:IFK>2ORK<1THEN1260

1360 IFK=1THEN1380
1370 SOUND1,1:CLS:GOSUB2520:END

1380 IFHS>=SCTHEN1392
1382 IFA$="N"THEN1370
1385 HS=SC:A=INT(SC/12000):SC=SC-A*12000:B=INT(SC/1000)
1386 SC=SC-B*1000:C=INT(SC/100):SC=SC-C*100:D=INT(SC/10)
1387 SC=SC-D*10:E=SC
1392 CLS3:SOUND100,1:GOSUB2520:GOTO2516

1410 A$=INKEY$:IFA$=""THEN1440
1420 IFA$="Y"THENK=1
1430 IFA$="N"THENK=2
1440 RETURN

1500 E1=RND(3):E2=4+(E1-1)*32:TB=RND(2)*2-3:EX=RND(12)*2-1:EY=12+12*TB
1520 N=EY+4:O=E2+EX:P=32:GOSUB9:N=EY+4+TB:O=E2+EX:P=36:GOSUB9:FORA=-1TO1STEP2:N=EY+4+TB:O=E2+EX+A:P=35:GOSUB9
1530 N=EY+4+TB*2:O=E2+EX+A:P=35:GOSUB9:NEXT:IFTB=1THENVD$="S":RETURN
1541 VD$="N":RETURN

1600 SOUND141,6:SOUND102,4:SOUND141,2:SOUND154,6:SOUND102,4:SOUND154,2:SOUND166,12
1610 PRINT@193,"€€€€€€€€€€€€€€€";:FORA=1TO100
1611 PRINT@225,"€ΏΌΌµΊµΊ΄ΏΈΌΏΌ€";:PRINT@257,"€ΏΌ€±¶Ή²€Ώ€€Ώ€€";
1612 PRINT@289,"€ΌΌΌ΄Έ΄Έ΄ΌΈ€Ό€€";
1613 PRINT@225,"€••”€";:PRINT@257,"€€‘–™’€€€€€";
1614 PRINT@289,"€”””€€€";
1615 PRINT@225,"€όόυϊυϊτψόό€";:PRINT@257,"€ό€ρφως€€€€€";
1616 PRINT@289,"€όόότψτψτόψ€ό€€";:NEXT
1680 CLS0:PRINT@4,;:M$="CONGRATULATIONS-- YOU GOT":GOSUB7
1685 M$="OUT OF THE MAZE":IFNT>0THENM$=M$+" AGAIN"
1690 M$=M$+"!":PRINT@32+(16-LEN(M$)/2),;:GOSUB7
1700 PRINT@96,;:NT=NT+1
1710 PRINT"€€€““““““““““€€€€€€€€€€€€€€€€€€€";
1712 PRINT"€€€€―ΏΟίο€€€€€€€€€€€€€€€€€€€€";
1713 PRINT"€€€€―ΏΟίο€€€€€€€€€€€€€€€€€€€€";
1714 PRINT"€€€€€€€€€€€€€€€€€€€€€€";
1715 PRINT"€€€€€“—›“€€€€€€€€€€€€€€€€€€€€€";
1716 PRINT"€€‘—›’€€€€€€€€€€€€€€€€€€";
1717 PRINT"€‘THE  MC-10’€€€€€€€€€€€€€€€€€";
1718 PRINT"€MAZE AWARD€€€€€€€€€€€€€€€€€";
1719 PRINT"•€€€€€€€€€€€€€€€€";
1720 PRINT"€MAZE"STR$(NT)"€€€€€€€€€€€€€€€€€";
1721 PRINT"€”€€€€€€€€€€€€€€€€€";
1722 PRINT"€€”€€€€€€€€€€€€€€€€€€";
1723 PRINT"€€€€€";:SC=SC+T*2+4000+E*20:LE=TAN((12000-T)/6366)*9:IFLE<2THENLE=2
1724 IFLE>50THENLE=50
1775 PRINT@114,"your€current";:PRINT@146,;:M$="SCORE: "+RIGHT$("000000"+MID$(STR$(SC),2),6):GOSUB7
1780 PRINT@209,"the€argon€rays";:PRINT@240,"you€were€exposed";:PRINT@275,;:M$="TO CUT"+STR$(INT(LE)):GOSUB7
1790 PRINT@305,"years€off€your";:PRINT@342,"life";:PRINT@371,"expectancy";
1800 PRINT@432,"ready€to€attempt";:PRINT@465,;:M$="THE NEXT MAZE?":GOSUB7:PRINT@501,;:M$="(Y/N)":GOSUB7
1810 PRINT@506," ";:PRINT@506,"€";:A$=INKEY$
1820 IFA$=""THEN1810
1825 IFA$="N"THEN1380
1830 IFA$<>"Y"THEN1810

1835 CLS3:SOUND100,1:GOSUB2520

1840 T=12000-NT*1000:IFT<8000THENT=8000
1850 X5=0:Y5=0:K9=0:E1=0:R9=0:VD$="":K=0:E2=0:TB=0:EX=0:EY=0
1870 GOSUB3000:PRINT@390,;:M$="PRESS <":GOSUB7:PRINT" P ";:M$="> TO PLAY":GOSUB7
1880 M$=INKEY$:IFM$<>"P"THEN1880
1882 PRINT@390,;:M$="GENERATING THE MAZE":GOSUB7:GOSUB2000
1890 GOTO2560

1900 E=175:T=12000:X(0)=-1:X(2)=1:X(4)=-1:Y(1)=-1:Y(3)=1:Y(5)=-1
1940 D$(1)="  N  ":D$(2)="  E  ":D$(3)="  S  ":D$(4)="  W  ":B$(0)="E":B$(1)="S":B$(2)="":B$(3)="N":B$(4)="W"
1960 C(1)=2:C(2)=6:C(3)=4
1990 RETURN

2000 FORX=36TO68STEP32
2210 Y=4:FORA=1TO6
2220 X2=RND(12)*2-1:Y2=RND(12)*2-1
2230 IFMID$(M$(Y2+Y),X2+X,1)<>" "ORMID$(M$(Y2+Y),X2+X-32,1)<>" "THEN2220
2240 X5=1+(X-4)/32:N=Y2+4:O=X2+X:P=33:GOSUB9:PX(X5,A)=X2+X:PY(X5,A)=Y+Y2
2250 NEXT:NEXT
2300 FORA=1TO20
2310 X=(RND(3)-1)*32+4:X2=RND(12)*2-1:Y2=RND(12)*2-1:IFMID$(M$(Y2+Y),X+X2,1)<>" "THEN2310
2325 N=Y+Y2:O=X+X2:P=37:GOSUB9:BX(A)=X+X2:BY(A)=Y+Y2
2330 NEXT
2340 L=3:X=4+(L-1)*32:Y=4:D=1
2341 X1=RND(7)*2+3:Y1=RND(7)*2+3:IFMID$(M$(Y+Y1),X+X1,1)<>" "THEN2341
2343 KB=RND(20):KX=BX(KB):KY=BX(DB):BP=0
2345 B1=INT((KX-4)/32)+1
2350 RETURN

2500 DIMX(5),Y(5),A$(13,4),N$(13,5),C$(13,4),P$(13,5),E$(13,4),M$(31),O,P,Q,P1,P2,P3,P4,P5,P6,P7,P8,X3,Y3,X4,Y4,X5,Y5
2511 DIMX6,Y6,B,X$,Y$,Z$,R$,BX(20),BY(20),PX(3,6),PY(3,6),B$(4),D$(4),C(3),A$:HS=0:M=RND(-(PEEK(9)*256+PEEK(10)))
2515 GOSUB6000:GOSUB1900

2516 SC=0:NT=0:E=175:GOTO1840

2520 FORX5=2TO3:FORA=1TO6:O=PX(X5,A):N=PY(X5,A):P=32:GOSUB9:NEXT:NEXT
2530 FORA=1TO20:O=BX(A):N=BY(A):IFO>0ANDN>0THENP=32:GOSUB9
2540 NEXT:N=EY+4:IFEX=0THENRETURN
2550 N=EY+4:O=E2+EX:P=35:GOSUB9:N=EY+4+TB:O=E2+EX:P=32:GOSUB9:FORA=-1TO1STEP2:N=EY+4+TB:O=E2+EX+A:P=32:GOSUB9
2551 N=EY+4+TB*2:O=E2+EX+A:P=32:GOSUB9:NEXT:RETURN

2560 CLSL*2+2:PRINT@18,;:M$="MAZE OF ARGON":GOSUB7
2570 PRINT@370,"(W) - FORWARD";:PRINT@402,"(A) - LEFT   ";:PRINT@434,"(D) - RIGHT  ";:PRINT@466,"(U) - UP     ";
2590 PRINT@498,"(S) - SONAR  ";:GOTO10

3000 PRINT@6," THE MAZE OF ARGON ";:M$="HIGH SCORE: "+RIGHT$("000000"+MID$(STR$(HS),2),6)
3005 PRINT@160+(32-LEN(M$))/2,;:GOSUB7:PRINT@204,;:M$="MAZE: "+MID$(STR$(NT+1),2,1):GOSUB7
3006 PRINT@257,"CAN YOU ESCAPE IN TIME? FIND ";
3007 PRINT@289,"THE KEY TO THE HIDDEN EXIT ON";
3008 PRINT@321,"ONE OF THE 3 LEVELS. H=HELP. ";
3010 PRINT@481,"BY STEPHEN CLARKE & JIM GERRIE";:RETURN

REM M
6000 X$="€€€":Y$="€€€€":Z$="€":A$(0,1)="ι€€":A$(1,1)="€ι€":A$(2,1)="€€ι":A$(3,1)=X$:A$(4,1)=X$:A$(5,1)=X$:A$(6,1)=X$
6010 A$(7,1)=X$:A$(8,1)=X$:A$(9,1)=X$:A$(10,1)=X$:A$(11,1)="€€ζ":A$(12,1)="€ζ€":A$(13,1)="ζ€€"

REM A2
6020 A$(0,2)=X$:A$(1,2)="γγ€":A$(2,2)="€ει":A$(3,2)="€ε€":A$(4,2)="€ε€":A$(5,2)="€ε€":A$(6,2)="€ε€":A$(7,2)="€ε€"
6028 A$(8,2)="€ε€":A$(9,2)="€ε€":A$(10,2)="€ε€":A$(11,2)="€εζ":A$(12,2)="μμ€":A$(13,2)=X$

REM A3
6040 A$(0,3)="ι€€":A$(1,3)="€ιγ":A$(2,3)="€εΰ":A$(3,3)="€ε€":A$(4,3)="€ε€":A$(5,3)="€ε€":A$(6,3)="€ε€":A$(7,3)="€ε€"
6048 A$(8,3)="€ε€":A$(9,3)="€ε€":A$(10,3)="€ε€":A$(11,3)="€εΰ":A$(12,3)="€ζμ":A$(13,3)="ζ€€"

REM A4
6060 A$(0,4)=X$:A$(1,4)="γγγ":A$(2,4)=X$:A$(3,4)=X$:A$(4,4)=X$:A$(5,4)=X$:A$(6,4)=X$:A$(7,4)=X$:A$(8,4)=X$:A$(9,4)=X$
6070 A$(10,4)=X$:A$(11,4)=X$:A$(12,4)="μμμ":A$(13,4)=X$

REM N
6100 N$(0,1)=Y$:N$(1,1)=Y$:N$(2,1)=Y$:N$(3,1)="ι€€€":N$(4,1)="€ι€€":N$(5,1)="€€ι€":N$(6,1)="€€€ι":N$(7,1)="€€€ζ"
6108 N$(8,1)="€€ζ€":N$(9,1)="€ζ€€":N$(10,1)="ζ€€€":N$(11,1)=Y$:N$(12,1)=Y$:N$(13,1)=Y$

REM B2
6120 N$(0,2)=Y$:N$(1,2)=Y$:N$(2,2)=Y$:N$(3,2)="ι€€€":N$(4,2)="εΰΰΰ":N$(5,2)="εμλ€":N$(6,2)="εΰκι":N$(7,2)="εΰκζ"
6128 N$(8,2)="εγξΰ":N$(9,2)="εΰ€€":N$(10,2)="ζ€€€":N$(11,2)=Y$:N$(12,2)=Y$:N$(13,2)=Y$

REM B3
6140 N$(0,3)=Y$:N$(1,3)=Y$:N$(2,3)=Y$:N$(3,3)="ι€€€":N$(4,3)="ΰιΰΰ":N$(5,3)="ΰ€ξμ":N$(6,3)="ΰ€κΰ":N$(7,3)="ΰ€κΰ"
6148 N$(8,3)="ΰΰλγ":N$(9,3)="ΰζ€€":N$(10,3)="ζ€€€":N$(11,3)=Y$:N$(12,3)=Y$:N$(13,3)=Y$

REM B4
6160 N$(0,4)=Y$:N$(1,4)=Y$:N$(2,4)=Y$:N$(3,4)="ι€€€":N$(4,4)="ε€€€":N$(5,4)="εμμμ":N$(6,4)="ε€€€":N$(7,4)="ε€€€"
6168 N$(8,4)="εγγγ":N$(9,4)="ε€€€":N$(10,4)="ζ€€€":N$(11,4)=Y$:N$(12,4)=Y$
6173 N$(13,4)=Y$:FORT=0TO13:N$(T,5)=N$(0,4):NEXT:N$(1,5)="γγγγ":N$(12,5)="μμμμ"

REM P
6200 P$(0,1)=Y$:P$(1,1)=Y$:P$(2,1)=Y$:P$(3,1)="€€€ζ":P$(4,1)="€€€κ":P$(5,1)="μμμκ":P$(6,1)="€€€κ":P$(7,1)="€€€κ"
6208 P$(8,1)="γγγκ":P$(9,1)="€€€κ":P$(10,1)="€€€ι":P$(11,1)=Y$:P$(12,1)=Y$:P$(13,1)=Y$

REM D2
6220 P$(0,2)=Y$:P$(1,2)=Y$:P$(2,2)=Y$:P$(3,2)="€€€ζ":P$(4,2)="ΰΰζΰ":P$(5,2)="μν€ΰ":P$(6,2)="ΰε€ΰ":P$(7,2)="ΰε€ΰ"
6228 P$(8,2)="γηΰΰ":P$(9,2)="€€ιΰ":P$(10,2)="€€€ι":P$(11,2)=Y$:P$(12,2)=Y$:P$(13,2)=Y$

REM D3
6240 P$(0,3)=Y$:P$(1,3)=Y$:P$(2,3)=Y$:P$(3,3)="€€€ζ":P$(4,3)="ΰΰΰκ":P$(5,3)="€ημκ":P$(6,3)="ζε€κ":P$(7,3)="ιε€κ"
6248 P$(8,3)="ΰνγκ":P$(9,3)="€€ΰκ":P$(10,3)="€€€ι":P$(11,3)=Y$:P$(12,3)=Y$:P$(13,3)=Y$

REM D4
6260 P$(0,4)=Y$:P$(1,4)=Y$:P$(2,4)=Y$:P$(3,4)="€€€ζ":P$(4,4)="€€ζ€":P$(5,4)="€ζ€€":P$(6,4)="ζ€€€":P$(7,4)="ι€€€"
6268 P$(8,4)="€ι€€":P$(9,4)="€€ι€":P$(10,4)="€€€ι":P$(11,4)=Y$:P$(12,4)=Y$:P$(13,4)=Y$

REM Q
6280 E$(0,1)=X$:E$(1,1)="γγγ":E$(2,1)=X$:E$(3,1)=X$:E$(4,1)=X$:E$(5,1)=X$:E$(6,1)=X$:E$(7,1)=X$:E$(8,1)=X$
6289 E$(9,1)=X$:E$(10,1)=X$:E$(11,1)=X$:E$(12,1)="μμμ":E$(13,1)=X$

REM E2
6300 E$(0,2)="€€ζ":E$(1,2)="γζΰ":E$(2,2)="ΰκ€":E$(3,2)="ΰκ€":E$(4,2)="€κ€":E$(5,2)="€κ€":E$(6,2)="€κ€":E$(7,2)="€κ€"
6308 E$(8,2)="€κ€":E$(9,2)="ΰκ€":E$(10,2)="ΰκ€":E$(11,2)="ΰκΰ":E$(12,2)="μιΰ":E$(13,2)="€€ι":

REM E3
6320 E$(0,3)=X$:E$(1,3)="€γγ":E$(2,3)="ζκ€":E$(3,3)="€κ€":E$(4,3)="€κ€":E$(5,3)="€κ€":E$(6,3)="€κ€":E$(7,3)="€κ€"
6328 E$(8,3)="€κ€":E$(9,3)="€κ€":E$(10,3)="€κ€":E$(11,3)="ικ€":E$(12,3)="€μμ":E$(13,3)=X$

REM E4
6340 E$(0,4)="€€ζ":E$(1,4)="€ζ€":E$(2,4)="ζ€€":E$(3,4)=X$:E$(4,4)=X$:E$(5,4)=X$:E$(6,4)=X$:E$(7,4)=X$:E$(8,4)=X$
6349 E$(9,4)=X$:E$(10,4)=X$:E$(11,4)="ι€€":E$(12,4)="€ι€":E$(13,4)="€€ι":FORT=0TO13:P$(T,5)=N$(T,5):NEXT

REM C3
6360 C$(0,3)=Z$:C$(1,3)=Z$:C$(2,3)=Z$:C$(3,3)=Z$:C$(4,3)=Z$:C$(5,3)="μ":C$(6,3)=Z$:C$(7,3)=Z$:C$(8,3)="γ":C$(9,3)=Z$
6370 C$(10,3)=Z$:C$(11,3)=Z$:C$(12,3)=Z$:C$(13,3)=Z$

REM C4
6380 C$(0,4)=Z$:C$(1,4)="γ":C$(2,4)=Z$:C$(3,4)=Z$:C$(4,4)=Z$:C$(5,4)=Z$:C$(6,4)=Z$:C$(7,4)=Z$:C$(8,4)=Z$:C$(9,4)=Z$
6390 C$(10,4)=Z$:C$(11,4)=Z$:C$(12,4)="μ":C$(13,4)=Z$:FORT=0TO13:C$(T,1)=C$(0,4):C$(T,2)=C$(0,4):NEXT:C$(6,2)="γ":C$(7,2)="μ"
6391 C$(6,1)="γ":C$(7,1)="μ":R$=Z$

7001 M$(01)="                                                                                                 "
7002 M$(02)="                                                                                                 "
7003 M$(03)="                                                                                                 "
7004 M$(04)="   ###########################     ###########################     ###########################   "
7005 M$(05)="   #           #   #   #   # #     #                         #     #       #       # #   #   #   "
7006 M$(06)="   # # ### ### # # # # # # # #     # ########### ### ### ### #     # # ### # # ### # # ### # #   "
7007 M$(07)="   #   #   # #   #   #   #   #     # #         #   # #     # #     # #   #   #             # #   "
7008 M$(08)="   # ### # # ### # ### # #####     # # ####### # # # # # # # #     # ### # # ##### # ##### # #   "
7009 M$(09)="   #     #       #     #   # #     # # #     # #   # # # #   #     #     # # #   # # #   #   #   "
7010 M$(10)="   ### # ### # # # # ##### # #     # # ##### # # # # # # ### #     ### # # # # # # # # # # # #   "
7011 M$(11)="   #   # # # # # #   #   # # #     # #       # # #   # #     #     # # # # # # # # # # # # # #   "
7012 M$(12)="   # # # # # # # # # # # # # #     # ######### # # # # ##### #     # # # # # # # # # # # # # #   "
7013 M$(13)="   # # #     #   # #   #     #     #           # #   #   # # #     # # # # # # # # # # # # # #   "
7014 M$(14)="   # # ### ##### # # ##### ###     ######### ### ### ### # # #     # # # # # # # # # # # # # #   "
7015 M$(15)="   #   #   #     #     #     #     #     #   #         #     #     #   #   #   #   #   #   # #   "
7016 M$(16)="   ### # # # ### # # # # # ###     # ### # ### # ### # ##### #     # ### ### ### ### ### ### #   "
7017 M$(17)="   #     #       # # #   #   #     # #   #     #   # #       #     # #   #   #   #   #   #   #   "
7018 M$(18)="   # ### ### ##### # ### # # #     # # # # # ####### # ##### #     # # # # # # # # # # # # # #   "
7019 M$(19)="   #   #   #     #         # #     # # #   # #       # #     #     # # # #   # #     # # # # #   "
7020 M$(20)="   ### ### # # # # # ##### # #     ########### ####### # #####     # # # # # # # # # # # # # #   "
7021 M$(21)="   #         # # # #       # #     #                   #   # #     #   # # #   # # #   #     #   "
7022 M$(22)="   # # ### # # # # ### ### # #     # ### ########### # ##### #     ##### # ##### # ###########   "
7023 M$(23)="   # #     #   # # #     #   #     # #   #   #   #   #       #     #     # #   # # #         #   "
7024 M$(24)="   # # # # ### # # # ### #####     # # # # # # # # # ##### ###     # # # # # # # # # ####### #   "
7025 M$(25)="   #   # # #         #       #     # # # # # #   # #   # #   #     #   #       # #   #     # #   "
7026 M$(26)="   ### # # # # # # # # ##### #     # ### # # # # # ### # ### #     ### ### # # # # ### # # # #   "
7027 M$(27)="   #     #   #   #   #       #     #   #   #   #   #   #     #     #   #   #     #     # #   #   "
7028 M$(28)="   ###########################     ###########################     ###########################   "
7029 M$(29)="                                                                                                 "
7030 M$(30)="                                                                                                 "
7031 M$(31)="                                                                                                 "

8000 RETURN
