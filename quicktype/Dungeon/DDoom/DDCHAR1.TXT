10 CLS:CLEAR500:GOSUB1060
20 CLS:PRINT@384,"   w UP        space NEXT PAGE"
21 PRINT"   z DOWN":PRINT"   s INCREMENT  a DECREMENT";
30 J=1:H=MP:H$="POINTS"
40 GOSUB810:GOSUB900
50 K=1:P=T+2
60 PRINT@1+32*INT(P/2),">";
70 GOSUB720
80 IFK=5THEN70
90 IFI$="S"ANDH>0THENF(J,K)=F(J,K)+1:H=H-1:GOSUB920
100 IFI$="A"ANDF(J,K)>1THENF(J,K)=F(J,K)-1:H=H+1:GOSUB920
110 C=1
120 IFF(1,4)>6ANDF(1,8)>7THENC=2
130 IFF(1,4)>8ANDF(1,7)>7THENC=3
140 IFF(1,1)>7ANDF(1,8)>5ANDF(1,1)+F(1,2)>10THENC=4
150 IFF(1,1)>8ANDF(1,2)+F(1,3)>12ANDF(1,8)<6THENC=5
160 M$=C$(C)
170 GOSUB860
180 IFI$<>" "THEN70
190 H=GC:H$="GOLD COINS:"
200 FORJ=2TO4
210 K=1:P=T+2
220 M$="CHOOSE WELL SIR!"
230 GOSUB810
240 GOSUB900
250 PRINT@1+32*INT(P/2),">";
260 GOSUB720
270 N=8*(J-2)+K
280 M$="YOUR CHOICE?"
290 GOSUB680
300 BR=0:OF=0
310 IFI$="B"ORI$=CHR$(13)THENOF=F(J,K):GOSUB610
320 IFI$="O"THENBR=RND(3):GOSUB570
330 GOSUB860
340 IFI$<>" "THEN260
350 NEXTJ
360 PRINT@1+32*INT(2/2),"NAME THY CHARACTER";
380 INPUTN$
390 IFLEN(N$)>10THEN360
400 PRINT@1+32*INT(3/2),"ONE MOMENT PLEASE";
410 PRINT@1+32*INT(3/2),;
420 O=D*3
430 S$=CHR$(O+AS)
440 FORI=1TO8
450 S$=S$+CHR$(F(1,I)+AS)
460 NEXTI
470 FORI=1TOO
480 S$=S$+CHR$(O(I)+AS)
490 NEXTI
500 S$=S$+CHR$(H+AS)
510 S$=S$+CHR$(AS)
520 S$=S$+N$+" - "+C$(C)
530 REM S=OPENOUT "HERO"
540 REM PRINT#S,S$
550 REM CLOSE#S
560 END
570 M$="":GOSUB860
580 PRINT@2+32*INT(2/2),"YOUR OFFER";
590 INPUTOF
600 GOSUB680
610 IFO(N)>0ANDN<23THENM$="YOU HAVE IT SIRE":RETURN
620 PR=F(J,K)-BR
630 IFH<PRTHENSOUND1,1:M$="YOU CANNOT AFFORD":RETURN
640 IFOF>=PRANDY=1THENO(N)=O(N)+P(N):H=H-PR:M$="TIS YOURS!"
650 IFOF<PRANDY=1THENM$="OFFER REJECTED"
660 IFH<0THENH=0
670 RETURN
680 Y=0
690 IFMID$(O$(N),C,1)="1"THENY=1
700 IFY=0THENM$="NOT FOR "+C$(C)
710 RETURN
720 I$=INKEY$
730 IFI$=""THEN720
740 REM REVERSE?
750 PRINT@1+32*INT(P/2)," ";
760 IFI$="W"ANDK>1THENK=K-1
770 IFI$="Z"ANDK<DTHENK=K+1
780 P=K*2+T-1
790 PRINT@1+32*INT(P/2),">";
800 RETURN
810 REM REVERSE ON?
830 PRINT@0,F$(J,9)
840 REM BG=2:T=1:L=2
850 REM GOSUB980
860 REM REVERSE OFF
870 PRINT@480,"                     ";:PRINT@480,M$;
880 PRINT@2+32*INT(3/2),H$;:PRINT@15+32*INT(3/2),H;" ";
890 RETURN
900 BG=2:T=2.5:L=11
910 GOSUB980:T=INT(T+.5)+2
920 REM REVERSE ON
930 FORI=1TO8
940 Y=T+(I-1)*2+1
950 PRINT@2+32*INT(Y/2),F$(J,I);:PRINT@16+32*INT(Y/2),F(J,I);" ";
960 NEXTI
970 RETURN
980 PRINT@0+32*INT(T/2),;
990 REM REVERSE ON
1000 FORYY=INT(T)TOINT(L):PRINT@32*YY,"":NEXT
1010 REM
1020 FORXX=1TO62:SET(XX,T*2,BG):SET(XX,L*2,BG):NEXT:FORYY=T*2TOL*2:SET(1,YY,BG):SET(62,YY,BG):NEXT
1050 RETURN
1060 GOSUB1600
1070 D=8
1080 DIMF(4,D+1),F$(4,D+1),C$(5),O(D*3),O$(D*3),P(D*3)
1140 DATA"00001","00011","10011","10011","10011","00011","11111","10011"
1150 DATA"00011","00011","10011","11111","00011","11011","11011","11111"
1160 DATA"11100","00100","11100","10100","11100","11100","11111","11111"
1170 FORI=1TOD*3:READO$(I):NEXTI:FORI=1TO8:F(1,I)=RND(5)+2:NEXTI
1230 F(1,5)=1
1240 DATA20,16,12,15,8,10,8,6
1250 DATA18,15,9,9,14,8,6,6
1260 DATA20,15,14,12,10,8,6,6
1270 FORJ=2TO4
1280 FORI=1TO8
1290 READF(J,I)
1300 NEXTI
1310 NEXTJ
1320 DATA5,4,3,3,2,2,1,1
1330 DATA5,4,3,1,2,1,3,1
1340 DATA4,3,2,2,3,1,1,1
1350 FORI=1TOD*3
1360 READP(I)
1370 NEXTI
1380 DATA"STRENGTH","VITALITY","AGILITY","INTELLIGENCE"
1390 DATA"EXPERIENCE","LUCK","AURA","MORALITY","character creation"
1400 DATA"2 HAND SWORD","BROARDSWORD","SHORT SWORD"
1410 DATA"AXE","MACE","FLAIL","DAGGER","GAUNTLET","armoury"
1420 DATA"HEAVY ARMOUR","CHAIN ARMOUR","LEATHER ARMOUR","HEAVY ROBE"
1430 DATA"GOLD HELMET","HEADPIECE","SHIELD","TORCH","accoutrements"
1440 DATA"NECRONOMICON","SCROLLS","RING","MYSTIC AMULET","SASH","CLOAK"
1450 DATA"HEALING SALVE","POTIONS","EMPORIUM"
1460 FORJ=1TO4
1470 FORI=1TO9
1480 READF$(J,I)
1490 NEXTI
1500 NEXTJ
1510 DATA"WANDERER","CLERIC","MAGICIAN","WARRIOR","BARBARIAN"
1520 FORI=1TO5
1530 READC$(I)
1540 NEXTI
1550 MP=3+RND(5)
1560 GC=120+RND(60)
1570 M$="":AS=65
1590 RETURN
1600 REM
1610 W=32:B=0
1620 REM
1630 RETURN
