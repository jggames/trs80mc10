0 GOSUB9300:GOTO9000

1 RM=R(XX,YY):IFRM=1THENSET(24+XX,YY,6)
2 IFABS(DX)<4ORRM<=C7THENRETURN
3 SET(24+XX,YY,6):MT=RM:M=MT-C2:MV=M/16:MS=M*6:DX=3:LX=XX:LY=YY:RESET(24+XX,YY):RETURN
4 PRINT@X+32*(Y),MID$("€ßvciextsggg",R(X,Y)+1,1);:RETURN

7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+MC:FORZZ=1TOLEN(M$):VV=ASC(MID$(M$,ZZ)):POKECC+ZZ,VV-(VVAND64):NEXT:?@CC-MC+ZZ,:RETURN

20 A=15:B=15:C=15:D=15:E=15:F=15:G=15:H=15:I=15:IFN(X+V(1,.,V),Y+V(1,1,V))THENC=13
22 IFN(X+V(5,.,V),Y+V(5,1,V))THENG=13
23 IFN(X+V(2,.,V),Y+V(2,1,V))THEND=13
24 IFN(X+V(4,.,V),Y+V(4,1,V))THENF=13
25 IFN(X+V(3,.,V),Y+V(3,1,V))THENE=13

26 IFN(X+V(6,.,V),Y+V(6,1,V))THENA=12:B=16:C=4
27 IFN(X+V(10,.,V),Y+V(10,1,V))THENG=6:H=16:I=12
28 IFN(X+V(7,.,V),Y+V(7,1,V))THENB=12:C=12:D=4
29 IFN(X+V(9,.,V),Y+V(9,1,V))THENF=6:G=12:H=12
30 IFN(X+V(8,.,V),Y+V(8,1,V))THEND=12:E=12:F=12

31 IFN(X+V(11,.,V),Y+V(11,1,V))THENA=2
32 IFN(X+V(15,.,V),Y+V(15,1,V))THENI=8
33 IFN(X+V(12,.,V),Y+V(12,1,V))THENA=11:B=11:C=3
34 IFN(X+V(14,.,V),Y+V(14,1,V))THENG=7:H=11:I=11
35 IFN(X+V(13,.,V),Y+V(13,1,V))THENC=11:D=11:E=11:F=11:G=11

36 IFN(X+V(16,.,V),Y+V(16,1,V))THENA=10:B=2
37 IFN(X+V(18,.,V),Y+V(18,1,V))THENH=8:I=10
38 IFN(X+V(17,.,V),Y+V(17,1,V))THENB=10:C=10:D=10:E=10:F=10:G=10:H=10

39 IFN(X+V(19,.,V),Y+V(19,1,V))THENA=1
42 IFN(X+V(20,.,V),Y+V(20,1,V))THENI=9

45 ?@P,MID$(A$,A,1)MID$(A$,B,1)MID$(A$,C,1)MID$(A$,D,1)MID$(A$,E,1)MID$(A$,F,1)MID$(A$,G,1)MID$(A$,H,1)MID$(A$,I,1);
46 ?@P+32,MID$(B$,A,1)MID$(B$,B,1)MID$(B$,C,1)MID$(B$,D,1)MID$(B$,E,1)MID$(B$,F,1)MID$(B$,G,1)MID$(B$,H,1)MID$(B$,I,1);
47 ?@P+64,MID$(C$,A,1)MID$(C$,B,1)MID$(C$,C,1)MID$(C$,D,1)MID$(C$,E,1)MID$(C$,F,1)MID$(C$,G,1)MID$(C$,H,1)MID$(C$,I,1);
48 ?@P+96,MID$(D$,A,1)MID$(D$,B,1)MID$(D$,C,1)MID$(D$,D,1)MID$(D$,E,1)MID$(D$,F,1)MID$(D$,G,1)MID$(D$,H,1)MID$(D$,I,1);:RETURN

50 IFN(X+V(17,.,V),Y+V(17,1,V))=.THENX=X+V(17,.,V):Y=Y+V(17,1,V):NX=NX+D(NF,1):NY=NY+D(NF,2):SOUND200,1:GOSUB20
60 RETURN

70 ONCCGOTO60,72,73,74,75,76,77,78,80,81,82:STOP
72 M$="€ÁÃÂ€€ÁÏÂ€€ÏÏÏ€€ÍÏÎ€":GOTO83
73 M$="€ñó€€€ÿòù€€úýüú€ô÷óú":GOTO83
74 M$="€‘™™€€€šš€€šŸšš€—œ’":GOTO83
75 M$="€÷ÿû€õÿÿÿúõÿÿþúõÿÿÿú":GOTO83
76 M$="€ÇÌË€ÅÈ€ÄÊÅ€€€ÊÅ€€ÈÊ":GOTO83
77 M$="ÄÂ€ÁÈ€Ä€È€€Á€Â€ÁÈ€ÄÂ":GOTO83
78 M$="€áãâ€€ê€å€€ê€å€€äìè€":GOTO83
80 M$="ÁÈÇË€ÅÅÇÇ€€ÈËË€€ÆÃÈÈ":GOTO83
81 M$="€¡£€€€««ª€€¥­¢€€«€«€":GOTO83
82 M$="€€±€€€±½¹€€¾¾¾º€¹€±¸"
83 FORCC=0TO3:PRINT@32*(CC+1)+3,MID$(M$,1+CC*5,5);:NEXT:RETURN

100 I$=INKEY$:IFI$="F"THENIFDX<255THENGOSUB870
101 IFI$="C"THENIFO(Z+7)>0ANDO(17)+O(18)>0THENGOSUB990
102 IFI$="G"THENGOSUB1410
103 IFI$="P"THENGOSUB1660
104 IFI$="R"THENGOSUB1690
105 IFI$="?"THENGOSUB940
110 IFI$="A"THENV=V-1:V=V-(V<1)*4+(V>4)*4:NF=V:GOSUB20
120 IFI$="S"THENV=V+1:V=V-(V<1)*4+(V>4)*4:NF=V:GOSUB20
150 IFI$="W"THENGOSUB50:FORYY=NY-1TONY+1:FORXX=NX-1TONX+1:GOSUB1:NEXT:NEXT
210 Q=R(NX,NY):IFQ=C1ORQ=C2ORQ=C3ORQ=C4ORQ>C7THENNX=OX:NY=OY:X=OX+3:Y=OY+3:GOSUB20:CC=Q:GOSUB70:IFQ>C7THENO(Z+1)=O(Z+1)-.03
220 IFQ=C6THENTX=NX:TY=NY:TF=1:CC=Q:GOSUB70
230 IFTF=1THENNX=TX:NY=TY:X=OX+3:Y=OY+3:GOSUB20
240 IFO(Z+1)<S1*.8THENIFRND(8)<O(Z+6)THENTF=0
250 IFI$<>""THENO(Z+1)=O(Z+1)*0.99
260 IFO(Z+1)<S1THENO(Z+1)=O(Z+1)+(O(Z+2)/1100)
270 GOSUB480:SET(24+NX,NY,0):OX=NX:OY=NY:RESET(24+NX,NY):IFDX<255THENGOSUB620
280 IFQ=5ORQ=8THENCC=Q:GOSUB70
310 IFO(Z+1)>.THENIFFI<1THENIFQ<>C5THEN100
320 IFQ=C5THENCC=Q:GOSUB70:M$=T$(12):GOSUB430:GOTO1760
330 IFO(Z+1)<1THENGOSUB810
340 RETURN

370 PRINT@332,M$;
390 I$=INKEY$:IFI$=""THEN390
410 PRINT@332,"?=HELP":RETURN

430 PRINT@332,M$;:FORZZ=1TO750:NEXT:GOTO410

480 PRINT@161,"STR:"INT(O(Z+1));:PRINT@193,"VIT:"INT(O(Z+2));:PRINT@225,"AUR:"INT(O(Z+7));
540 PRINT@257,"DIR: "MID$("NESW.",NF,1)" ";:PRINT@289,"EXP:"INT(O(Z+5));
550 PRINT@321,"ATT:"INT(O(Z+1)+O(1)+O(2)+O(3)+O(4)+O(5)+O(7)+O(8));:PRINT@353,"SPL:"INT(M(1)+M(2)+M(3)+M(4)+M(5)+M(6));
552 PRINT@385,"CAN:"INT(LT);:PRINT@417,"POT:"INT(O(23)+O(24));:RETURN

620 DX=LX-NX:SX=SGN(DX):DY=LY-NY:SY=SGN(DY):MX=LX-(MV*SX):MY=LY-(MV*SY):RM=R(MX,MY)
640 IF(RM>0ANDRM<>MT)OR(ABS(DX)<=1ANDABS(DY)<=1)THENMY=LY:MX=LX
650 R(LX,LY)=0:XX=LX:YY=LY:GOSUB1:R(MX,MY)=MT:XX=MX:YY=MY:GOSUB1
670 LX=MX:LY=MY:HH=0:IFABS(DX)<=1ANDABS(DY)<=1ANDQ<>C7THENHH=M*.5:CC=MT:GOSUB70:SOUNDHH,1
690 IFHH*12<O(Z+6)+O(Z+3)THENRETURN
700 M$=T$(5):GOSUB430:SOUND110,2
710 HH=HH/(3+O(9)+O(10)+O(11)+O(12)+O(13)+O(14)):O(Z+1)=O(Z+1)-HH:O(Z+2)=O(Z+2)-(HH/101)

730 K=1:WB=0:MB=RND(M):J=MT:SOUNDJ,1:SOUND110,2
750 IFMB=1ANDO(K)>0THENGOSUB780
760 IFK<11THENK=K+1:GOTO750
770 RETURN

780 O(K)=0:M$=T$(8)+W$(K):GOSUB430:MB=0:SOUND110,2:J=K:SOUNDJ,1:RETURN

810 NF=5:O(Z+1)=0:GOSUB480:PRINT@332,"THOU HAST EXPIRED!":GOSUB480:FORJ=150TO1STEP-4
840 SOUNDJ,1:NEXT:RETURN

870 M$=T$(RND(3)):SOUND110,2:HH=O(Z+1)+O(1)+O(2)+O(3)+O(4)+O(5)+O(7)+O(8)+RND(O(Z+6))
890 IFO(Z+3)+O(Z+6)<RND(M)+2THENM$=T$(4):HT=0
900 MS=MS-HH:GOSUB430:O(Z+1)=O(Z+1)-(HH/100):O(Z+5)=O(Z+5)+.05:IFMS<1THENGOSUB970
930 RETURN

940 GOSUB950:PRINT@364,"rEVEAL cONJURE gET":PRINT@396,"pOTION "
945 PRINT@332,"a w s MOVEMENT fIGHT";:PRINT@460,T$(10):GOSUB4195

950 FORCC=1TO5:PRINT@32*(9+CC)+12,"":NEXT:GOTO410

970 DX=255:MS=0:R(MX,MY)=0:O(Z+5)=O(Z+5)+.5:M$=T$(6):GOSUB430:FORJ=200TO150STEP-8:SOUNDJ,1:NEXT
980 GOSUB1:GOSUB20:RETURN

990 GOSUB480:PRINT@364,"YOU MAY USE MAGICS":IFO(17)>0THENPRINT@396,"FROM NECRONOMICON"
1020 IFO(18)>0THENPRINT@428,"FROM THE SCROLLS"
1030 PRINT@460,"CONSULT THE LORE"
1040 M$="USE SPELL NUMBER?":GOSUB370:SL=VAL(I$):IFSL=0OR(O(17)=0ANDSL<5)OR(O(18)=0ANDSL>3)ORSL>6THEN1040
1070 M(SL)=M(SL)-1:XX=NX:YY=NY:IFM(SL)<0THENM$=T$(9):SL=7
1090 GOSUB950:GOSUB1:ONSLGOSUB1140,1190,1220,1280,1300,1390,1130
1110 O(Z+5)=O(Z+5)+.2:GOSUB430
1130 RETURN

1140 FORJ=1TO12:SOUNDJ,1:SOUND110,2:NEXT:IFDX<255THENXX=MX:YY=MY:GOSUB970
1180 RETURN

1190 IFQ=0THENR(NX,NY)=C7:GOSUB20
1200 SOUND100,1:SOUND200,1:RETURN

1220 NX=RND(13):NY=RND(13):X=NX+3:Y=NY+3:GOSUB20:FORJ=8TO255STEP8:SOUNDJ,1:NEXT:GOSUB480:RETURN

1280 O(Z+2)=O(Z+2)+RND(M(SL)):O(Z+1)=O(Z+1)+RND(M(SL)):O(Z+7)=O(Z+7)-1:RETURN

1300 EX=NX+D(NF,1):EY=NY+D(NF,2):IF(EX<1OREX>15)OR(EY<1OREY>15)THENRETURN
1310 FORJ=1TO30:Q=RND(8)+1:CC=Q:GOSUB70:SOUNDJ,1:NEXT:R(EX,EY)=Q:IFQ<=C7THENDX=255:MS=0
1320 FORJ=1TO20STEP4:SOUNDJ,1:NEXT:RETURN

1390 O(Z+2)=S2:O(Z+1)=S1:O(Z+7)=O(Z+7)-1:RETURN

1410 GX=NX+D(NF,1):GY=NY+D(NF,2):IFGX<.THENGX=.
1430 IFGY<.THENGY=.
1440 IFGX>15THENGX=15
1450 IFGY>15THENGY=15
1460 GT=R(GX,GY):IFGT>C1ANDGT<C4THENR(GX,GY)=.
1470 IFGT=C2THENO(23)=O(23)+1:O(24)=O(24)+1
1480 IFGT=C3THENTR=TR+1
1490 IFGT=C4THENGOSUB1550
1500 XX=GX:YY=GY:GOSUB1:IFGT>C1ANDGT<C4THENSOUNDGT+1,1:SOUNDGT+5,1
1520 GOSUB20:RETURN

1550 PRINT@332,"THY QUEST IS OVER!";:FORCC=1TO18:J=T(CC):SOUNDJ,1:XX=NX:YY=NY
1600 FORNF=1TO4:GOSUB480:NEXT:NEXT:MS=0
1630 PRINT@396,"THY SCORE=";INT((TR*10)+(O(0)*O(Z+5))+O(Z+1)+O(Z+2)+O(Z+3)):FI=1:RETURN

1660 IFO(24)>0ANDO(Z+1)<S1THENO(Z+1)=S1:O(24)=O(24)-1
1670 IFO(23)>0ANDO(Z+2)<S2THENO(Z+2)=S2:O(23)=O(23)-1
1680 RETURN

1690 IFLT=.THENM$=T$(7):GOSUB430:RETURN
1700 FORYY=NY-3TONY+3:FORXX=NX-3TONX+3:IFXX>0ANDXX<16THENIFYY>0ANDYY<16THENGOSUB1
1710 NEXT:NEXT:LT=LT-1:RETURN

1760 IFO(Z+5)<S3+1THENM$=T$(11):GOSUB430:NX=OX:NY=OY:X=OX+3:Y=OY+3:GOSUB20:CC=Q:GOSUB70:GOTO100
1765 LL=1
1770 PRINT@486,;:M$="PREPARE DUNGEON TAPE":GOSUB7
1780 CLOAD*R,"DUNGEOND":LE=R(0,0):IX=R(0,1):IY=R(0,2):RETURN

2160 TR=65:S1=O(Z+1):S2=O(Z+2):S3=O(Z+5):FORCC=1TO2:FORJ=1TO3:M((CC-1)*3+J)=O(16+CC)*O(Z+7):NEXT:NEXT
2240 IFO(16)=1THENLT=20
2250 RETURN

2460 DATA"GR SWORD","SWORD","AXE","MACE","FLAIL","DAGGER","ARMOUR","ARMOUR","ARMOUR","HELMET","HEADPC."
2500 FORCC=1TO11:READW$(CC):NEXT:FORCC=1TO12:READT$(CC):NEXT
2610 DATA"A GOOD BLOW","WELL HIT SIR","THY AIM IS TRUE","MISSED!","HIT THEE!!"
2620 DATA"IT IS SLAIN","NO LIGHT","BROKEN THY ","SPELL EXAUSTED"
2630 DATA"PRESS ANY KEY","NEED EXPERIENCE","EXIT FROM THIS LEVEL",,-1,1,,,1,-1,0
2680 FORCC=1TO4:READD(CC,1),D(CC,2):NEXT:FORCC=1TO18:READT(CC):NEXT:RETURN
2760 DATA69,117,73,121,81,129,69,117,73,121,81,129,89,137,97,145,101,149

3000 CLS:PRINT"LOAD CHARACTER FROM TAPE (Y/N)?";:GOSUB4195
3001 CLS:IFM$="Y"THENINPUT"CHARACTER NAME";N$:CLOAD*O,N$:RETURN
3005 CLS:PRINT@384,"   w UP        space NEXT PAGE":PRINT"   z DOWN":PRINT"   s INCREMENT  a DECREMENT"
3010 M$="":J=1:HH=3+RND(5):H$="POINTS":MP=120+RND(60):FORI=1TO8:I(1,I)=RND(5)+2:NEXT:I(1,5)=1:TR=65
3015 FORCC=0TO24:O(CC)=0:NEXT:GOSUB3400:BG=2:GOSUB3440:K=1:P=T+2:PRINT@1+32*INT(P/2),">";
3035 GOSUB3355:IFK=5THEN3035
3045 IFI$="S"ANDHH>0THENI(J,K)=I(J,K)+1:HH=HH-1:GOSUB3450
3050 IFI$="A"ANDI(J,K)>1THENI(J,K)=I(J,K)-1:HH=HH+1:GOSUB3450
3055 PC=1:IFI(1,4)>6ANDI(1,8)>7THENPC=2
3065 IFI(1,4)>8ANDI(1,7)>7THENPC=3
3070 IFI(1,1)>7ANDI(1,8)>5ANDI(1,1)+I(1,2)>10THENPC=4
3075 IFI(1,1)>8ANDI(1,2)+I(1,3)>12ANDI(1,8)<6THENPC=5
3080 M$=C$(PC):GOSUB3420:IFI$<>" "THEN3035
3090 PRINT@448,"":PRINT@431,"bUY     oFFER":H$="GOLD COINS:":GC=MP:HH=GC
3095 FORJ=2TO4:BG=J+2:K=1:P=T+2:M$="CHOOSE WELL SIR!":GOSUB3400:GOSUB3440:PRINT@1+32*INT(P/2),">";
3130 GOSUB3355:N=8*(J-2)+K:M$="YOUR CHOICE?":GOSUB3335:BR=0:OF=0:IFI$="B"THENOF=I(J,K):GOSUB3300
3160 IFI$="O"THENBR=RND(3):GOSUB3280
3165 HH=GC:GOSUB3420:IFI$<>" "THEN3130
3175 NEXT
3180 PRINT@33,"NAME THY CHARACTER";:INPUTN$:IFLEN(N$)>8THEN3180
3190 O(0)=GC:O(33)=PC:FORJ=1TO8:O(Z+J)=I(1,J):NEXT
3200 PRINT@33,"save";:GOSUB4195:IFM$="Y"THENPRINT@33,T$(10):GOSUB4195:CSAVE*O,N$
3210 RETURN
3280 M$="":GOSUB3420:PRINT@448,"YOUR OFFER";:INPUTOF:PRINT@448,"":GOSUB3335
3300 IFO(N)>0ANDN<23THENM$="YOU HAVE IT SIR":RETURN
3305 PR=I(J,K)-BR:IFGC<PRTHENM$="YOU CANNOT AFFORD":RETURN
3315 IFOF>=PRANDY=1THENO(N)=O(N)+Q(N):GC=GC-PR:M$="TIS YOURS!"
3320 IFOF<PRANDY=1THENM$="OFFER REJECTED"
3325 IFGC<0THENGC=0
3330 RETURN
3335 Y=0:IFMID$(O$(N),PC,1)="1"THENY=1
3345 IFY=0THENM$="NOT FOR "+C$(PC)
3350 RETURN
3355 I$=INKEY$:IFI$=""THEN3355
3370 PRINT@1+32*INT(P/2)," ";
3375 IFI$="W"ANDK>1THENK=K-1
3380 IFI$="Z"ANDK<8THENK=K+1
3385 P=K*2+T-1:PRINT@1+32*INT(P/2),">";:RETURN
3400 PRINT@0,F$(J,9)
3420 PRINT@480,LEFT$(M$+"            ",31);:PRINT@34,H$;:PRINT@47,HH;
3435 RETURN
3440 T=2.5:L=11:GOSUB3480:T=INT(T+.5)+2
3450 FORI=1TO8:Y=T+(I-1)*2+1:PRINT@2+32*INT(Y/2),F$(J,I);:PRINT@16+32*INT(Y/2),I(J,I);:NEXT:RETURN
3480 PRINT@32*INT(T/2),;:FORYY=INT(T)TOINT(L):PRINT@32*YY,"":NEXT
3500 FORCC=1TO62:SET(CC,T*2,BG):SET(CC,L*2,BG):NEXT:FORCC=T*2TOL*2:SET(1,CC,BG):SET(62,CC,BG):NEXT:RETURN
3525 DATA"00001","00011","10011","10011","10011","00011","11111","10011"
3530 DATA"00011","00011","10011","11111","00011","11011","11011","11111"
3535 DATA"11100","00100","11100","10100","11100","11100","11111","11111"
3540 FORCC=1TO24:READO$(CC):NEXT:FORJ=2TO4:FORI=1TO8:READI(J,I):NEXT:NEXT:FORCC=1TO24:READQ(CC):NEXT
3550 DATA20,16,12,15,8,10,8,6,18,15,9,9,14,8,6,6,20,15,14,12,10,8,6,6
3590 DATA5,4,3,3,2,2,1,1,5,4,3,1,2,1,3,1,4,3,2,2,3,1,1,1
3620 DATA"STRENGTH","VITALITY","AGILITY","INTELLIGENCE","EXPERIENCE","LUCK","AURA","MORALITY","character creation"
3630 DATA"2 HAND SWORD","BROADSWORD","SHORT SWORD","AXE","MACE","FLAIL","DAGGER","GAUNTLET","armoury","HEAVY ARMOUR"
3640 DATA"CHAIN ARMOUR","LEATHER ARMOUR","HEAVY ROBE","GOLD HELMET","HEADPIECE","SHIELD","TORCH","accoutrements"
3650 DATA"NECRONOMICON","SCROLLS","RING","MYSTIC AMULET","SASH","CLOAK","HEALING SALVE","POTIONS","emporium"
3660 FORJ=1TO4:FORI=1TO9:READF$(J,I):NEXT:NEXT:FORCC=1TO5:READC$(CC):NEXT:RETURN
3685 DATA"WANDERER","CLERIC","MAGICIAN","WARRIOR","BARBARIAN"

4000 CLS:LE=1:IX=1:IY=1:GOSUB4370:PRINT@50,"MAP GENERATOR"
4020 X=1:Y=1:PRINT@82,"LEVEL:"LE:PRINT@114,"? FOR HELP"
4040 I$=INKEY$:IFI$="?"THENGOSUB4160
4055 IFI$="W"ANDY>1THENY=Y-1
4060 IFI$="Z"ANDY<15THENY=Y+1
4065 IFI$="A"ANDX>1THENX=X-1
4070 IFI$="S"ANDX<15THENX=X+1
4075 IFI$="L"THENGOSUB4140:GOSUB1780:GOSUB4380:PRINT@178,"":PRINT@210,"":GOTO4020
4080 IFI$>"/"ANDI$<":"THENGOSUB4120
4090 PRINT@X+32*(Y)," ";:GOSUB4:IFI$="N"THENLE=LE-(LR<5):GOSUB4370:GOTO4020
4110 IFI$<>"X"THEN4040
4115 PRINT@178,"SAVE TO TAPE?":GOSUB4195:IFM$<>"Y"THENRETURN
4116 GOSUB4150:R(0,0)=LE:R(0,1)=IX:R(0,2)=IY:CSAVE*R,"DUNGEOND":RETURN
4120 I=VAL(I$):IFI=9THENI=8+RND(3)
4130 IFI=5THENIX=X:IY=Y
4135 R(X,Y)=I:RETURN
4140 PRINT@178,"PREPARE TAPE":RETURN
4150 GOSUB4140:PRINT@210,T$(10):GOSUB4195:RETURN
4160 FORCC=1TO11:PRINT@178,H$(CC);:GOSUB4195:PRINT@178,"":NEXT:RETURN
4195 M$=INKEY$:IFM$=""THEN4195
4200 RETURN
4290 DATA"PRESS ANY KEY","MOVE W A S Z","1 WALL 2 VASE","3 CHEST 4 IDOL","5 ENTRY 6 EXIT","7 TRAP","8 SAFE PLACE"
4310 DATA"9 GUARD","0 ERASE","N NEXT LEVEL","L LOAD X EXIT"
4320 FORCC=1TO11:READH$(CC):NEXT:RETURN
4370 FORY=1TO15:FORX=1TO15:R(X,Y)=.:NEXT:NEXT
4380 FORY=1TO15:FORX=1TO15:GOSUB4:NEXT:NEXT:RETURN

5000 FORV=1TO4:FORX=1TO20:FORY=0TO1:READV(X,Y,V):NEXT:NEXT:NEXT:FORY=0TO23:FORX=0TO23:N(X,Y)=1:NEXT:NEXT
5030 A$="¯¯«¢€¡§¯¯ßßÓ€€€£":B$="¯¯¯¯€¯¯¯¯ßßßßÓ€¯":C$="¯¯®¨€¤­¯¯ßßÜ€€€¬":D$="®¨€€€€€¤­Ü€€€€€€":RETURN

5100 DATA-2,-4,-1,-4,,-4,1,-4,2,-4,-2,-3,-1,-3,,-3,1,-3,2,-3,-2,-2,-1,-2,,-2,1,-2,2,-2,-1,-1,,-1,1,-1,-1,,1,0
5110 DATA4,-2,4,-1,4,,4,1,4,2,3,-2,3,-1,3,,3,1,3,2,2,-2,2,-1,2,,2,1,2,2,1,-1,1,,1,1,,-1,,1
5120 DATA2,4,1,4,,4,-1,4,-2,4,2,3,1,3,,3,-1,3,-2,3,2,2,1,2,,2,-1,2,-2,2,1,1,,1,-1,1,1,,-1,0
5130 DATA-4,2,-4,1,-4,,-4,-1,-4,-2,-3,2,-3,1,-3,,-3,-1,-3,-2,-2,2,-2,1,-2,,-2,-1,-2,-2,-1,1,-1,,-1,-1,,1,,-1

9000 DIMX,Y,V(20,1,4),V,A,B,C,D,E,F,G,H,I,N(23,23),P,L,ZZ,CC,VV,M$,M,N,XX,YY:MC=16384
9010 DIMR(16,16),O(33),W$(11),T$(12),M(6),D(4,2),T(18),I(4,8),F$(4,9),C$(5),O$(24),Q(24),H$(11):W=32:Z=1

9100 GOSUB2500:GOSUB3540:GOSUB4320:GOSUB5000

9200 PRINT@104,"1. MAKE CHARACTER":PRINT@168,"2. MAKE DUNGEON":PRINT@232,"3. PLAY GAME"
9240 PRINT@300,"CHOICE?":GOSUB4195:CC=VAL(M$):IFCC>3THEN9200
9241 LL=0:ONCCGOSUB3000,4000,9800,9810:IFLL=1THENCC=4:GOTO9241
9250 GOSUB9300:GOTO9200

9300 CLS:PRINT@40,"DUNGEON OF DOOM":RETURN

9800 PRINT@416,"LOAD A DUNGEON FROM TAPE (Y/N)?";:GOSUB4195:PRINT@416,"":IFM$="Y"THENGOSUB1770
9810 IFLE>O(Z+5)ORFI>0ORO(Z+1)<1THENCC=0:RETURN
9830 R(0,0)=0:R(0,1)=0:R(0,2)=0

9900 CLS0:FORY=1TO15:FORX=1TO15:N(X+3,Y+3)=-(R(X,Y)=1):NEXT:NEXT
9915 FI=0:DX=255:TX=0:TY=0:TF=0:TR=0:MX=0:MY=0:DY=12:NX=1:NY=1:RE=0:LT=0
9920 X=IX+3:Y=IY+3:NX=IX:NY=IY:OX=IX:OY=IY:IFN(X,Y)THENRETURN
9940 M$=LEFT$(N$+"         ",9):V=2:L=1:NF=V:P=33:PRINT@P-32,;:GOSUB7
9950 GOSUB20::FORXX=24TO40:SET(XX,0,6):SET(XX,16,6):NEXT:FORYY=0TO16:SET(24,YY,6):SET(40,YY,6):NEXT
9970 GOSUB2160:FORCC=161TO417STEP32:PRINT@CC,"         ";:NEXT:GOSUB950
9980 PRINT@87,;:M$="DUNGEON":GOSUB7:PRINT@153,;:M$="OF":GOSUB7:PRINT@216,;:M$="DOOM":GOSUB7
9990 C1=1:C2=2:C3=3:C4=4:C5=6:C6=7:C7=8:PRINT@449,;:M$=LEFT$(C$(O(33))+"   ",9):GOSUB7:GOTO100
