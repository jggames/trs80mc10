10 GOSUB 1060
20 COLOUR 0+128:CLS
30 J=1:H=MP:H$="POINTS"
40 GOSUB 810:GOSUB 900
50 K=1:P=T+1
60 PRINT TAB(1,P);">";
70 GOSUB 720
80 IF K=5 THEN GOTO 70
90 IF I$="L" AND H>0 THEN F(J,K)=F(J,K)+1:H=H-1:GOSUB 920
100 IF I$="H" AND F(J,K)>1 THEN F(J,K)=F(J,K)-1:H=H+1:GOSUB 920
110 C=1
120 IF F(1,4)>6 AND F(1,8)>7 THEN C=2
130 IF F(1,4)>8 AND F(1,7)>7 THEN C=3
140 IF F(1,1)>7 AND F(1,8)>5 AND F(1,1)+F(1,2)>10 THEN C=4
150 IF F(1,1)>8 AND F(1,2)+F(1,3)>12 AND F(1,8)<6 THEN C=5
160 M$=C$(C)
170 GOSUB 860
180 IF I$<>" " THEN GOTO 70
190 H=GC:H$="GOLD COINS:"
200 FOR J=2 TO 4
210 K=1:P=T+1
220 M$="CHOOSE WELL SIRE!"
230 GOSUB 810
240 GOSUB 900
250 PRINT TAB(1,P);">";
260 GOSUB 720
270 N=8*(J-2)+K
280 M$="MAKE YOUR CHOICE"
290 GOSUB 680
300 BR=0:OF=0
310 IF I$="B" THEN OF=F(J,K):GOSUB 610
320 IF I$="O" THEN BR=RND(3):GOSUB 570
330 GOSUB 860
340 IF I$<>" " THEN GOTO 260
350 NEXT J
360 PRINT TAB(1,2);"NAME THY CHARACTER";
370 PRINT TAB(1,3);LEFT$(B$,W-2);:PRINT TAB(1,3);
380 INPUT N$
390 IF LEN(N$)>10 THEN GOTO 360
400 PRINT TAB(1,3);"ONE MOMENT PLEASE";
410 PRINT TAB(1,3);
420 O=D*3
430 S$=CHR$(O+AS)
440 FOR I=1 TO 8
450 S$=S$+CHR$(F(1,I)+AS)
460 NEXT I
470 FOR I=1 TO O
480 S$=S$+CHR$(O(I)+AS)
490 NEXT I
500 S$=S$+CHR$(H+AS)
510 S$=S$+CHR$(AS)
520 S$=S$+N$+" - "+C$(C)
530 S=OPENOUT "HERO"
540 PRINT#S,S$
550 CLOSE#S
560 END
570 M$="":GOSUB 860
580 PRINT TAB(2,2);"YOUR OFFER";
590 INPUT OF
600 GOSUB 680
610 IF O(N)>0 AND N<23 THEN M$="YOU HAVE IT SIRE":RETURN
620 PR=F(J,K)-BR
630 IF H<PR THEN M$="YOU CANNOT AFFORD":RETURN
640 IF OF>=PR AND Y=1 THEN O(N)=O(N)+P(N):H=H-PR:M$="TIS YOURS!"
650 IF OF<PR AND Y=1 THEN M$="OFFER REJECTED"
660 IF H<0 THEN H=0
670 RETURN
680 Y=0
690 IF MID$(O$(N),C,1)="1" THEN Y=1
700 IF Y=0 THEN M$="NOT FOR "+C$(C)
710 RETURN
720 I$=INKEY$(0)
730 IF I$="" THEN GOTO 720
740 COLOUR 3+128:COLOUR 1
750 PRINT TAB(1,P);" ";
760 IF I$="K" AND K>1 THEN K=K-1
770 IF I$="J" AND K<D THEN K=K+1
780 P=K*2+T-1
790 PRINT TAB(1,P);">";
800 RETURN
810 COLOUR 0+128:COLOUR 2
820 PRINT TAB(0,0);LEFT$(B$,W);
830 PRINT TAB(0,0);F$(J,9)
840 BG=2:FG=3:T=1:L=2
850 GOSUB 980
860 COLOUR 2+128:COLOUR 0
870 PRINT TAB(2,2);LEFT$(B$,17);TAB(2,2);M$;
880 PRINT TAB(2,3);H$;TAB(15,3);H;" ";
890 RETURN
900 BG=3:FG=2:T=5:L=15
910 GOSUB 980
920 COLOUR 3+128:COLOUR 0
930 FOR I=1 TO 8
940 Y=T+(I-1)*2+1
950 PRINT TAB(2,Y);F$(J,I);TAB(16,Y);F(J,I);" ";
960 NEXT I
970 RETURN
980 PRINT TAB(0,T);
990 COLOUR FG+128:PRINT LEFT$(B$,W);
1000 COLOUR BG+128:COLOUR FG
1010 FOR I=1 TO L
1020 PRINT CHR$(B);LEFT$(B$,W-2);CHR$(B);
1030 NEXT I
1040 COLOUR FG+128:PRINT LEFT$(B$,W);
1050 RETURN
1060 GOSUB 1600
1070 D=8
1080 DIM F(4,D+1)
1090 DIM F$(4,D+1)
1100 DIM C$(5)
1110 DIM O(D*3)
1120 DIM O$(D*3)
1130 DIM P(D*3)
1140 DATA "00001","00011","10011","10011","10011","00011","11111","10011"
1150 DATA "00011","00011","10011","11111","00011","11011","11011","11111"
1160 DATA "11100","00100","11100","10100","11100","11100","11111","11111"
1170 FOR I=1 TO D*3
1180 READ O$(I)
1190 NEXT I
1200 FOR I=1 TO 8
1210 F(1,I)=RND(5)+2
1220 NEXT I
1230 F(1,5)=1
1240 DATA 20,16,12,15,8,10,8,6
1250 DATA 18,15,9,9,14,8,6,6
1260 DATA 20,15,14,12,10,8,6,6
1270 FOR J=2 TO 4
1280 FOR I=1 TO 8
1290 READ F(J,I)
1300 NEXT I
1310 NEXT J
1320 DATA 5,4,3,3,2,2,1,1
1330 DATA 5,4,3,1,2,1,3,1
1340 DATA 4,3,2,2,3,1,1,1
1350 FOR I=1 TO D*3
1360 READ P(I)
1370 NEXT I
1380 DATA "STRENGTH","VITALITY","AGILITY","INTELLIGENCE"
1390 DATA "EXPERIENCE","LUCK","AURA","MORALITY","CHARACTER CREATION"
1400 DATA "2 HAND SWORD","BROARDSWORD","SHORT SWORD"
1410 DATA "AXE","MACE","FLAIL","DAGGER","GAUNTLET","ARMOURY"
1420 DATA "HEAVY ARMOUR","CHAIN ARMOUR","LEATHER ARMOUR","HEAVY ROBE"
1430 DATA "GOLD HELMET","HEADPIECE","SHIELD","TORCH","ACCOUTREMENTS"
1440 DATA "NECRONOMICON","SCROLLS","RING","MYSTIC AMULET","SASH","CLOAK"
1450 DATA "HEALING SALVE","POTIONS","EMPORIUM"
1460 FOR J=1 TO 4
1470 FOR I=1 TO 9
1480 READ F$(J,I)
1490 NEXT I
1500 NEXT J
1510 DATA "WANDERER","CLERIC","MAGICIAN","WARRIOR","BARBARIAN"
1520 FOR I=1 TO 5
1530 READ C$(I)
1540 NEXT I
1550 MP=3+RND(5)
1560 GC=120+RND(60)
1570 M$="":AS=65
1580 B$="":FOR I=1 TO W:B$=B$+" ":NEXT I
1590 RETURN
1600 MODE 5:VDU 23,0,8202;0;0;0;
1610 W=20:B=224
1620 VDU 23,224,255,255,255,255,255,255,255,255
1630 RETURN