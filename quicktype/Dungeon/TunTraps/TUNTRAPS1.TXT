1 CLS:DIM A(26):GOTO 7
3 D=0:FOR I=1TO 3:R=23*R:R=R-32767*INT(R/32767):D=D+R-6*INT(R/6)+1:NEXT :RETURN 
5 PRINT "ST"; S; " DX"; F; " WS"; W; " CN"; C; " GD"; G:RETURN
6 GOTO 30
7 PRINT "TUNNELS & TRAPS":INPUT "EXPLORE TUNNEL #";N:R=N
8 GOSUB 3:S=D:GOSUB 3:F=D:GOSUB 3:W=D:GOSUB 3:C=D:G=0:GOSUB 5
9 PRINT:INPUT "COMMAND";A$:GOSUB 3:GOSUB 10:GOTO 9
10 IF A$="A" THEN T=S-E:PRINT "SWORD";:GOTO 76
12 IF A$="C" THEN T=W-E/3:PRINT "SPELL";:GOTO 76
13 IF A$="D" THEN Z=Z-1:GOTO 6
14 IF A$="E" THEN X=X+1:GOTO 6
15 IF A$="F" THEN T=(F+L)*(E=0):PRINT "ARROW";:GOTO 76
18 IF A$="I" GOTO 5
20 IF A$="N" THEN Y=Y-1:GOTO 6
22 IF A$="S" THEN Y=Y+1:GOTO 6
23 IF A$="T" THEN T=W:GOTO 62
24 IF A$="U" THEN Z=Z+1:GOTO 6
26 IF A$="W" THEN X=X-1:GOTO 6
28 PRINT "NOR SOU EAST WEST UP DN":PRINT "ATTK CAST FIRE INV TRPS?":RETURN
30 IF M<0THEN GOSUB 62
31 IF M>0THEN A$="X":PRINT "PARRY";:T=W:GOSUB 80
32 L=INT((ABS(X)+ABS(Y)+3*ABS(Z))/3)+1:M=0:GOSUB 3:IF D>7 THEN M=L:REM
38 IF D<5 THEN PRINT "TRAP";:T=F:M=-L
40 IF M=0 THEN PRINT "NOTHING";
41 IF D<7 THEN M=-L
42 IF M=1 THEN PRINT "IMP";
44 IF M=2 THEN PRINT "KOBOLD";
46 IF M=3 THEN PRINT "GOBLIN";
48 IF M=4 THEN PRINT "HOBGOBLIN";
50 IF M=5 THEN PRINT "ORC";
52 IF M=6 THEN PRINT "HALFORC";
54 IF M=7 THEN PRINT "OGRE";
56 IF M>7 THEN PRINT "TROLL";
58 PRINT " HERE!"
60 IF D>4 THEN RETURN 
62 GOSUB 3:IF (D<T)AND(M<0) THEN PRINT "YOU JUST ESCAPE A TRAP!":M=0:RETURN
64 IF M>=0 THEN PRINT "NO TRAP DETECTED.":RETURN 
66 GOSUB 3:IF A$="T"THEN D=INT(D/2)
68 IF D>L THEN D=L
70 IF D>C THEN D=C-1
72 C=C-D:PRINT "A TRAP CAUSED ";D;" DAMAGE.":RETURN 
76 E=E+1:IF M<=0 THEN PRINT "S ARE USELESS HERE!":RETURN 
78 GOSUB 3:IF D<T THEN D=D+D
80 P=D:GOSUB 3:IF D<L*2 THEN D=D+D
82 IF P>=D AND A$<>"X" THEN M=M-(P-D)/3:PRINT " HIT FOR ";P-D;" DAMAGE."
83 IF P>=D AND A$="X" THEN PRINT " SUCCEEDED."
84 IF P<D THEN C=C+P-D:PRINT " MISSED.":PRINT "YOU TOOK ";D-P;" DAMAGE."
86 IF C<1 THEN PRINT "YOU DIED IN TUNNEL"STR$(N)"!":GOSUB 5:PRINT:END
88 IF M>0 THEN RETURN 
90 PRINT "YOU KILLED IT!":M=0:E=0
91 GOSUB 3:PRINT "ROLL OF ";D;": ";:IF C<L THEN C=L:IF C>18 THEN C=18
92 IF (D<=S)AND(D<=F)AND(D<=W)THEN G=G+D:PRINT "GOLD!":GOTO 5
93 T=23:A(6)=F:A(19)=S:A(23)=W
94 IF (S>=F)AND(S>=W)THEN T=19:U=23:V=6:IF (F>=W)THEN U=6: V=23
95 IF (F>S)AND(F>=W)THEN T=6:U=23:V=19:IF (S>=W)THEN U=19: V=23
96 IF (W>S)AND(W>F)THEN T=23:U=6:V=19:IF (S>=F)THEN U=19: V=6
97 IF D>A(T)THEN GOTO 195
98 IF D>A(U)THEN T=U:GOTO 195
99 T=V
195 IF (T=6)THEN F=F+1:PRINT "DEXTERITY!":GOTO 5
196 IF (T=19)THEN S=S+1:PRINT "STRENGTH!":GOTO 5
197 IF (T=23)THEN W=W+1:PRINT "WISDOM!":GOTO 5
198 GOTO 5
