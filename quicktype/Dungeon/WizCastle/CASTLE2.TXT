0 CLS:GOTO20
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB1:GOTO700
4 GOSUB1:GOSUB4370:RETURN
5 MCC=ASC
20 DIMCC,ZZ,M$,I$,C$(34),I$(34),R$(4),W$(8),E$(8),L(512),C(3,4),T(8),O(3),R(3):C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO100
50 N=N+8*((N=9)-(N=0)):RETURN
55 N=-N*(N<19)-18*(N>18):RETURN
60 N=64*(N-1)+8*(X-1)+Y:RETURN
65 E=E+100*(E>99):RETURN
70 Y$="ANSWER YES OR NO":NG=0
100 CLS:NG=NG+1:Q=RND(0):RESTORE:FORQ=1TO34:READC$(Q),I$(Q):NEXT
120 FORQ=1TO512:L(Q)=101:NEXT:FORQ=1TO8:READW$(Q),E$(Q):NEXT
130 FORQ=1TO4:READR$(Q):NEXT:IFNG>1THEN160
140 PRINTTAB(6)"THE WIZARD'S CASTLE":GOSUB4360:PRINT"HOLD ON WHILE I SET UP THE GAME."
160 X=1:Y=4:N=1:GOSUB60:L(N)=2:FORZ=1TO7:FORQ1=1TO2:Q=104:GOSUB4290:N=Z+1:GOSUB60:L(N)=103:NEXTQ1,Z
170 FORZ=1TO8:FORQ=113TO124:GOSUB4290:NEXT:FORQ1=1TO3:FORQ=105TO112:GOSUB4290:NEXT:Q=125:GOSUB4290:NEXTQ1,Z
200 FORQ=126TO133:Z=RND(8):GOSUB4290:NEXT:Q=101:FORA=1TO3:Z=RND(8):GOSUB4290:C(A,1)=X:C(A,2)=Y:C(A,3)=Z:C(A,4)=0:NEXT
230 RC=0:ST=2:DX=14:R$(3)="MAN":Q=112+RND(12):Z=RND(8):GOSUB4290:R(1)=X:R(2)=Y:R(3)=Z:Q=109:Z=RND(8):GOSUB4290
240 O(1)=X:O(2)=Y:O(3)=Z:BF=0:OT=8:AV=0:HT=0:T=1:VF=0:LF=0:TC=0:GP=60:RF=0:OF=0:BL=0:IQ=8:SX=0:FORQ=1TO8:T(Q)=0:NEXT
270 CLS:PRINT"ALL RIGHT, BOLD ONE."
280 M$="YOU MAY BE AN ELF, DWARF, MAN, OR HOBBIT.":GOSUB4:FORQ=1TO4
290 IFLEFT$(R$(Q),1)=O$THENRC=Q:ST=ST+2*Q:DX=DX-2*Q
300 NEXTQ:PRINT:OT=OT+4*(RC=1):IFRC>0THENR$(3)="HUMAN":GOTO320
310 PRINT"PLEASE TYPE E, D, M, OR H.":GOTO280
320 PRINT"WHICH SEX DO YOU PREFER";:GOSUB4370:IFO$="M"THENSX=1:GOTO350
330 IFO$="F"THEN350
340 M$="CUTE "+R$(RC)+", REAL CUTE. TRY M OR F.":GOSUB1:GOTO320
350 CLS:M$="OK, "+R$(RC)+", YOU HAVE THE FOLLOWING ATTRIBUTES:":GOSUB1
360 PRINT"STRENGTH:"ST:PRINT"INTELLIGENCE:"IQ:PRINT"DEXTERITY:"DX:PRINT
370 M$="AND"+STR$(OT)+" OTHER POINTS TO ALLOCATE AS YOU WISH.":GOSUB1:Z$="STRENGTH":PRINT
380 GOSUB4410:ST=ST+Q:IFOT=0THEN410
390 Z$="INTELLIGENCE":GOSUB4410:IQ=IQ+Q:IFOT=0THEN410
400 Z$="DEXTERITY":GOSUB4410:DX=DX+Q
410 CLS:M$="OK, "+R$(RC)+", YOU HAVE 60 GOLD PIECES (GP'S).":GOSUB1
420 Z$="ARMOR":GOSUB4500:AV=0:WV=0:FL=0:WC=0
430 M$="PLATE<30> CHAINMAIL<20> LEATHER<10> NOTHING<0>":GOSUB4
440 IFO$="N"THEN470
450 AV=-3*(O$="P")-2*(O$="C")-(O$="L"):IFAV>0THEN470
460 PRINT:M$="ARE YOU A "+R$(RC)+" OR "+C$(RND(12)+12)+"?":GOSUB1:GOTO420
470 AH=AV*7:GP=GP-AV*10:PRINT
480 CLS:M$="OK, BOLD "+R$(RC)+", YOU HAVE"+STR$(GP)+" GP'S LEFT.":GOSUB1
490 Z$="WEAPONS":GOSUB4500:M$="SWORD<30> MACE<20> DAGGER<10> NOTHING<0>":GOSUB1
500 GOSUB4370:IFO$="N"THEN530
510 WV=-3*(O$="S")-2*(O$="M")-(O$="D"):IFWV>0THEN530
520 PRINT:M$="IS YOUR IQ REALLY"+STR$(IQ)+"?":GOSUB1:GOTO490
530 GP=GP-WV*10:IFGP<20THEN580
540 CLS
550 M$="DO YOU WANT TO BUY A LAMP FOR 20 GP'S?":GOSUB4
560 IFO$="Y"THENLF=1:GP=GP-20:GOTO580
570 IFO$<>"N"THENPRINT:M$=Y$:GOSUB1:GOTO550
580 PRINT:IFGP<1THENQ=0:GOTO650
590 M$="OK, "+R$(RC)+", YOU HAVE"+STR$(GP)+" GOLD PIECES LEFT.":GOSUB1
600 M$="FLARES COST 1 GP EACH. HOW MANY DO YOU WANT":GOSUB1:INPUTO$:Q=VAL(O$):PRINT
610 IF(Q>0)OR(ASC(O$)=48)THEN640
620 M$="IF YOU DON'T WANT ANY, JUST TYPE 0 (ZERO).":GOSUB1
630 PRINT:GOTO600
640 IFQ>GPTHENM$="YOU CAN ONLY AFFORD"+STR$(GP)+".":GOSUB1:GOTO600
650 FL=FL+Q:GP=GP-Q:X=1:Y=4:Z=1
660 CLS:M$="OK, "+R$(RC)+", YOU ARE NOW ENTERING THE CASTLE!":GOSUB1:GOTO2370
700 T=T+1:IFRF+OF>0THEN780
710 IFC(1,4)>T(1)THENT=T+1
720 IFC(2,4)>T(3)THENGP=GP-RND(5)
730 IFGP<0THENGP=0
740 IFC(3,4)<=T(5)THEN780
750 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB60:E=L(N):GOSUB65:L(RND(Z))=E+100
760 X=A:Y=B:Z=C:N=Z:GOSUB60:IFL(N)<>1THEN780
770 FORQ=1TO3:C(Q,4)=-(C(Q,1)=X)*(C(Q,2)=Y)*(C(Q,3)=Z):NEXT
780 IFRND(5)>1THEN930
790 PRINT:PRINT"YOU ";:Q=RND(7)+BL:IFQ>7THENQ=4
800 ONQGOSUB890,830,880,820,900,910,920
810 GOTO930
820 PRINT"STEPPED ON A FROG!":RETURN
830 PRINT"HEAR ";:ONRND(4)GOTO840,850,860,870
840 PRINT"A SCREAM!":RETURN
850 PRINT"FOOTSTEPS!":RETURN
860 PRINT"A WUMPUS!":RETURN
870 PRINT"THUNDER!":RETURN
880 PRINT"SNEEZED!":RETURN
890 PRINT"SEE A BAT FLY BY!":RETURN
900 PRINT"SMELL "C$(12+RND(13))" FRYING!":RETURN
910 PRINT"FEEL LIKE YOU'RE BEING WATCHED!":RETURN
920 PRINT"HEAR FAINT RUSTLING NOISES!":RETURN
930 IFBL+T(4)<>2THEN950
940 PRINT:M$=C$(29)+" CURES YOUR BLINDNESS!":GOSUB1:BL=0
950 IFBF+T(6)<>2THEN970
960 PRINT:M$=C$(31)+" DISSOLVES THE BOOK!":GOSUB1:BF=0
970 PRINT:PRINT"ENTER YOUR COMMAND: ";:GOSUB4390
980 IFO$="N"THENPRINT"ORTH":GOTO1370
990 IFO$="S"THENPRINT"OUTH":GOTO1380
1000 IFO$="E"THENPRINT"AST":GOTO1380
1010 IFO$="W"THENPRINT"EST":GOTO1380
1020 IFO$="U"THENPRINT"P":GOTO1390
1030 IFO$<>"D"THEN1060
1040 I$=INKEY$:IFI$=""THEN1040
1041 O$=O$+I$
1050 IFO$="DR"THENPRINT"RINK":GOTO1780
1051 PRINT"OWN":GOTO1410
1060 IFO$="M"THENPRINT"AP":GOTO1430
1070 IFO$="F"THENPRINT"LARE":ONBL+1GOTO1530,1430
1080 IFO$="L"THENPRINT"AMP":ONBL+1GOTO1660,1430
1090 IFO$="O"THENPRINT"PEN":GOTO1930
1100 IFO$="G"THENPRINT"AZE":ON BL+1 GOTO2130,1430
1110 IFO$="T"THENPRINT"ELEPORT":PRINT:ONRF+1GOTO2260,2280
1120 IFO$="Q"THENPRINT"UIT":GOTO2300
1130 IFO$<>"H"THEN1360
1140 CLS
1150 PRINT"* WIZARD'S CASTLE INSTRUCTIONS *"
1160 PRINT"COMMANDS:":PRINT
1170 PRINT"H/ELP   N/ORTH    S/OUTH"
1171 PRINT"E/AST   W/EST     U/P"
1180 PRINT"D/OWN   DR/INK    M/AP"
1181 PRINT"F/LARE  L/AMP     O/PEN"
1190 PRINT"G/AZE   T/ELEPORT Q/UIT":GOSUB4530
1200 PRINT"CONTENTS OF ROOMS:":PRINT
1210 PRINT".=EMPTY ROOM  B=BOOK"
1220 PRINT"C=CHEST       D=STAIRS DOWN"
1230 PRINT"W=WARP/ORB    F=FLARES"
1240 PRINT"M=MONSTER     G=GOLD PIECES"
1250 PRINT"P=MAGIC POOL  O=CRYSTAL ORB"
1260 PRINT"S=SINKHOLE    T=TREASURE"
1270 PRINT"U=STAIRS UP   V=VENDOR"
1280 PRINT"E=ENTRANCE / EXIT":GOSUB4530
1290 PRINT"THE BENEFITS OF HAVING TREASURES ARE:":PRINT
1300 PRINT"RUBY RED - AVOID LETHARGY":PRINT"PALE PEARL - AVOID LEECH"
1310 PRINT"GREEN GEM - AVOID FORGETTING":PRINT"OPAL EYE - CURES BLINDNESS"
1320 PRINT"BLUE FLAME - DISSOLVES BOOKS":PRINT"NORN STONE - NO BENEFIT"
1330 PRINT"PALANTIR - NO BENEFIT":PRINT"SILMARIL - NO BENEFIT":PRINT
1340 GOSUB4530:GOTO700
1360 PRINT:M$="SILLY "+R$(RC)+", THAT WASN'T A VALID COMMAND!":GOTO3
1370 N=Z:GOSUB60:IFL(N)=2THEN3950
1380 X=X+(O$="N")-(O$="S"):Y=Y+(O$="W")-(O$="E"):N=X:GOSUB50:X=N:N=Y:GOSUB50:Y=N:GOTO2370
1390 N=Z:GOSUB60:IFL(N)=3THENZ=Z-1:GOTO2370
1400 Z$="UP":GOTO1420
1410 Z$="DOWN":N=Z:GOSUB60:IFL(N)=4THENZ=Z+1:GOTO2370
1420 PRINT:M$="THERE ARE NO STAIRS GOING "+Z$+" FROM HERE!":GOTO3
1430 IFBL<>1THEN1480
1440 PRINT:M$="YOU CAN'T SEE ANYTHING, YOU DUMB "+R$(RC)+"!":GOTO3
1480 CLS:A=X:B=Y:FORX=1TO8:FORY=1TO8:N=Z:GOSUB60:Q=L(N)
1490 IFQ>99THENQ=Q-100
1500 IFX=AANDY=BTHENPRINT"<"I$(Q)"> ";:GOTO1510
1501 PRINT" "I$(Q)"  ";
1510 NEXTY,X:X=A:Y=B:GOTO1620
1520 PRINT") LEVEL"Z:GOTO700
1530 IFFL<>0THEN1580
1540 M$="HEY, BRIGHT ONE, YOU'RE OUT OF FLARES!":GOTO3
1580 PRINT:FL=FL-1:A=X:B=Y:FOR Q1=A-1TOA+1:N=Q1:GOSUB50:X=N:FORQ2=B-1TOB+1
1590 N=Q2:GOSUB50:Y=N:N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q:PRINT" "I$(Q)"   ";:NEXTQ2:PRINT
1600 PRINT:NEXTQ1:X=A:Y=B
1620 GOSUB4510:GOTO700
1660 IFLF<>0THEN1680
1670 PRINT:M$="YOU DON'T HAVE A LAMP, "+R$(RC)+"!":GOTO3
1680 PRINT:M$="WHERE DO YOU WANT TO SHINE THE LAMP (N,S,E,W)?":GOSUB4
1690 A=X:B=Y:N=(X+(O$="N")-(O$="S")):GOSUB50:X=N:N=(Y+(O$="W")-(O$="E")):GOSUB50:Y=N
1700 IFA-X+B-Y<>0THEN1720
1710 PRINT:M$="THAT'S NOT A DIRECTION, "+R$(RC)+"!":GOTO3
1720 PRINT:M$="THE LAMP SHINES INTO ":GOSUB4515:PRINT
1730 N=Z:GOSUB60:E=L(N):GOSUB65:L(N)=E:M$="THERE YOU WILL FIND "+C$(L(N))+".":GOSUB1
1740 X=A:Y=B:GOTO700
1780 N=Z:GOSUB60:IFL(N)=5THEN1810
1790 PRINT:M$="IF YOU WANT A DRINK, FIND A POOL!":GOSUB1
1800 GOTO700
1810 Q=RND(8):PRINT:PRINT"YOU TAKE A DRINK AND ":IFQ<7THENPRINT"FEEL ";
1820 ONQGOTO1830,1840,1850,1860,1870,1880,1890,1910
1830 N=(ST+RND(3)):GOSUB55:ST=N:PRINT"STRONGER.":GOTO700
1840 ST=ST-RND(3):PRINT"WEAKER.":ON(1-(ST<1))GOTO700,3890
1850 N=(IQ+RND(3)):GOSUB55:IQ=N:PRINT"SMARTER.":GOTO700
1860 IQ=IQ-RND(3):PRINT"DUMBER.":ON(1-(IQ<1))GOTO700,3890
1870 N=(DX+RND(3)):GOSUB55:DX=N:PRINT"NIMBLER.":GOTO700
1880 DX=DX-RND(3):PRINT"CLUMSIER.":ON(1-(DX<1))GOTO700,3890
1890 Q=RND(4):IFQ=RCTHEN1890
1900 RC=Q:PRINT"BECOME A "R$(RC)".":GOTO700
1910 SX=1-SX:PRINT"TURN INTO A ";:IFSX=0THENPRINT"FE";
1920 PRINT"MALE "R$(RC)"!":GOTO700
1930 N=Z:GOSUB60:IFL(N)<>6THEN1950
1940 PRINT:PRINT"YOU OPEN THE CHEST AND":GOTO2070
1950 N=Z:GOSUB60:IFL(N)<>12THEN1970
1960 PRINT:PRINT"YOU OPEN THE BOOK AND":GOTO1980
1970 PRINT:M$="THE ONLY THING OPENED WAS YOUR BIG MOUTH!":GOTO3
1980 ONRND(6)GOTO1990,2000,2010,2020,2030,2040
1990 M$="FLASH! OH NO! YOU ARE NOW A BLIND "+R$(RC)+"!":GOSUB1:BL=1:GOTO2060
2000 M$+"IT'S ANOTHER VOLUME OF ZOT'S POETRY! - YECH!!":GOSUB1:GOTO2060
2010 M$="IT'S AN OLD COPY OF PLAY"+R$(RND(4))+"!":GOSUB1:GOTO2060
2020 PRINT"IT'S A MANUAL OF DEXTERITY!":DX=18:GOTO2060
2030 PRINT"IT'S A MANUAL OF STRENGTH!":ST=18:GOTO2060
2040 M$="THE BOOK STICKS TO YOUR HANDS - NOW YOU ARE UNABLE TO DRAW YOUR WEAPON!":GOSUB1:BF=1
2060 N=Z:GOSUB60:L(N)=1:GOTO700
2070 ONRND(4)GOTO2080,2100,2110,2100
2080 PRINT"KABOOM! IT EXPLODES!!":Q=RND(6):GOSUB3840
2090 ON(1-(ST<1))GOTO2060,3890
2100 Q=RND(1000):PRINT"FIND"Q"GOLD PIECES!":GP=GP+Q:GOTO2060
2110 M$="GAS! YOU STAGGER FROM THE ROOM!":GOSUB1:N=Z:GOSUB60:L(N)=1:T=T+20
2120 O$=MID$("NSEW",RND(4),1):GOTO1380
2130 N=Z:GOSUB60:IFL(N)=11THEN2150
2140 PRINT:M$="IT'S HARD TO GAZE WITHOUT AN ORB!":GOSUB1:GOTO3
2150 PRINT:M$="YOU SEE ":ONRND(6)GOTO2160,2180,2200,2210,2230,2250
2160 M$=M$+"YOURSELF IN A BLOODY HEAP!":GOSUB1
2170 ST=ST-RND(2):ON(1-(ST<1))GOTO700,3890
2180 M$=M$+"YOURSELF DRINKING FROM A POOL AND BECOMING "+C$(12+RND(13))+"!":GOSUB1
2190 GOTO700
2200 M$=M$+C$(12+RND(13))+" GAZING BACK AT YOU!":GOTO3
2210 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q
2220 M$=M$+C$(Q)+" AT ":GOSUB4515:X=A:Y=B:Z=C:GOTO700
2230 A=RND(8):B=RND(8):C=RND(8):IFRND(8)<4THEN A=O(1):B=O(2):C=O(3)
2240 M$=M$+" THE ORB OF ZOT AT ("+STR$(A)+","+STR$(B)+") LEVEL"+STR$(C)+"!":GOTO3
2250 M$=M$+"A SOAP OPERA RERUN!":GOTO3
2260 IFRF<>0THEN2280
2270 PRINT:M$="YOU CAN'T TELEPORT WITHOUT THE RUNESTAFF!":GOSUB1:GOTO3
2280 Z$="X-COORDINATE":GOSUB4450:X=Q:Z$="Y-COORDINATE":GOSUB4450:Y=Q
2290 Z$="Z-COORDINATE":GOSUB4450:Z=Q:O$="T":GOTO2370
2300 PRINT:PRINT"ARE YOU SURE":GOSUB4370:PRINT
2310 IFO$="Y"THEN2330
2320 PRINT"THEN DON'T SAY THAT YOU DO!":GOTO700
2330 PRINT:GOTO4000
2370 CLS:IFBL=0THENGOSUB4510
2380 PRINT"STRENGTH:"ST;TAB(17)"DEXTERITY:"DX:PRINT"INTELLIGENCE:"IQ;TAB(17)"GOLD:"GP
2390 PRINT"TREASURES:"TC;TAB(17)"FLARES:"FL
2400 PRINT"WEAPON = "W$(WV+1):PRINT"ARMOR = "W$(AV+5)
2410 IFLF=1THENPRINT"AND A LAMP";
2420 PRINT:WC=0:N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q:Z$="YOU NOW HAVE":PRINT
2430 M$="HERE YOU FIND "+C$(Q)+".":GOSUB1:IF(Q<7)OR(Q=11)OR(Q=12)THEN700
2440 IFQ=7THENGP=GP+RND(50):M$=Z$+STR$(GP)+".":GOSUB1:GOTO2060
2450 IFQ=8THENFL=FL+RND(5):M$=Z$+STR$(FL)+".":GOSUB1:GOTO2060
2460 IFQ>9THEN2490
2470 IF(O(1)=X)AND(O(2)=Y)AND(O(3)=Z)THENON(1-(O$="T"))GOTO1380,4140
2480 X=RND(8):Y=RND(8):Z=RND(8):GOTO2370
2490 IFQ=10THENN=Z+1:GOSUB50:Z=N:GOTO2370
2500 IFQ<=25ORQ>=34THEN2520
2510 PRINT:PRINT"IT'S NOW YOURS!":T(Q-25)=1:TC=TC+1:GOTO2060
2520 N=Z:GOSUB60:A=L(N)-12:WC=0:IF(A<13)OR(VF=1)THEN3170
2530 PRINT:M$="YOU MAY TRADE WITH, ATTACK, OR IGNORE THE VENDOR.":GOSUB4
2540 IFO$="I"THEN700
2550 IFO$<>"A"THEN2570
2560 VF=1:PRINT:M$="YOU'LL BE SORRY THAT YOU DID THAT!":GOSUB1:GOTO3170
2570 IFO$="T"THEN2590
2580 PRINT:M$="NICE SHOT, "+R$(RC)+"!":GOSUB1:GOTO2530
2590 FORQ=1TO8:A=RND(Q*1500):IFT(Q)=0THEN2630
2600 PRINT:M$="DO YOU WANT TO SELL "+C$(Q+25)+" FOR"+STR$(A)+"GP'S?":GOSUB4
2610 IFO$="Y"THENTC=TC-1:T(Q)=0:GP=GP+A:GOTO2630
2620 IFO$<>"N"THENPRINTY$:GOTO2600
2630 NEXTQ:IFGP>=1000THEN2650
2640 PRINT:M$="YOU'RE TOO POOR TO TRADE, "+R$(RC)+".":GOTO3
2650 IFGP<1250THEN2960
2660 IFAV=3THEN2800
2670 PRINT:M$="OK, "+R$(RC)+", YOU HAVE"+STR$(GP)+"GP'S AND ":IFLEFT$(W$(AV+5),2)="NO"THENM$=M$+W$(AV+5)+".":GOSUB1:GOTO2680
2672 M$=M$+W$(AV+5)+" ARMOR.":GOSUB1
2680 PRINT:Z$="ARMOR":GOSUB4500:PRINT"NOTHING<0> LEATHER<1250> "
2690 IFGP>1499THENPRINT"CHAINMAIL<1500> ";
2700 IFGP>1999THENPRINT"PLATE<2000>"
2710 PRINT:GOSUB4370:PRINT:IFO$="N"THEN2800
2720 IFO$="L"THENGP=GP-1250:AV=1:AH=7:GOTO2800
2730 IFO$<>"C"ORGP>=1500THEN2750
2740 M$="YOU HAVEN'T GOT THAT MUCH CASH ON HAND!":GOSUB1:GOTO2680
2750 IFO$="C"THENGP=GP-1500:AV=2:AH=14:GOTO2800
2760 IFO$<>"P"ORGP>=2000THEN2780
2770 M$="YOU CAN'T AFFORD PLATE ARMOR!":GOSUB1:GOTO2680
2780 IFO$="P"THEN GP=GP-2000:AV=3:AH=21:GOTO2800
2790 PRINT:M$="DON'T BE SILLY. CHOOSE A SELECTION.":GOSUB1:GOTO2710
2800 IFGP<1250THEN2960
2810 IFWV=3THEN2960
2820 PRINT:M$="YOU HAVE"+STR$(GP)+" GP'S LEFT WITH "+W$(WV+1)+" IN HAND.":GOSUB1
2830 PRINT:Z$="WEAPON":GOSUB4500:PRINT"NOTHING<0> DAGGER<1250> "
2840 IFGP>1499THENPRINT"MACE<1500> ";
2850 IFGP>1999THENPRINT"SWORD<2000>"
2860 PRINT:GOSUB4370:PRINT:IFO$="N"GOTO2960
2870 IFO$="D"THEN GP=GP-1250:WV=1:GOTO2960
2880 IFO$<>"M"ORGP>=1500THEN2900
2890 M$="SORRY SIR, I'M AFRAID I DON'T GIVE CREDIT!":GOSUB1:GOTO2830
2900 IFO$="M"THENGP=GP-1500:WV=2:GOTO2960
2910 IFO$<>"S"ORGP>=2000THEN2940
2920 M$="YOUR DUNGEON EXPRESS CARD - YOU LEFT HOME WITHOUT IT!":GOSUB1
2930 GOTO2830
2940 IFO$="S"THENGP=GP-2000:WV=3:GOTO2960
2950 PRINT"TRY CHOOSING A SELECTION!":GOTO2860
2960 IFGP<1000THEN700
2970 IFST>17THEN3010
2980 Z$="STRENGTH":GOSUB4470:IFO$<>"Y"THEN3000
2990 GP=GP-1000:N=(ST+RND(6)):GOSUB55:ST=N:Q=ST:GOSUB4490:GOTO2960
3000 IFO$<>"N"THENPRINTY$:GOTO2980
3010 IFGP<1000THEN700
3020 IFIQ>17THEN3060
3030 Z$="INTELLIGENCE":GOSUB4470:IFO$<>"Y"THEN3050
3040 GP=GP-1000:N=(IQ+RND(6)):GOSUB55:IQ=N:Q=IQ:GOSUB4490:GOTO3010
3050 IFO$<>"N"THENPRINTY$:GOTO3030
3060 IFGP<1000THEN700
3070 IFDX>17THEN3110
3080 Z$="DEXTERITY":GOSUB4470:IFO$<>"Y"THEN3100
3090 GP=GP-1000:N=(DX+RND(6)):GOSUB55:DX=N:Q=DX:GOSUB4490:GOTO3060
3100 IFO$<>"N"THENPRINTY$:GOTO3080
3110 IF(GP<1000)OR(LF=1)THEN700
3120 PRINT:M$="DO YOU WANT TO BUY A LAMP FOR 1000 GP'S?":GOSUB4
3130 IFO$<>"Y"THEN3150
3140 GP=GP-1000:LF=1:PRINT:PRINT"IT'S GUARANTEED TO OUTLIVE YOU!":GOTO700
3150 IFO$<>"N"THENPRINTY$:GOTO3120
3160 GOTO700
3170 Q1=1+INT(A/2):Q2=A+2:Q3=1
3180 IF(C(1,4)>T(1))OR(BL=1)OR(DX<RND(9)+RND(9))THEN3700
3190 PRINT:PRINT"YOU'RE FACING "C$(A+12)"!":PRINT
3200 PRINT"YOU MAY ATTACK OR RETREAT."
3210 IFQ3=1THENPRINT"YOU CAN ALSO ATTEMPT A BRIBE."
3220 IFIQ>14THENPRINT"YOU CAN ALSO CAST A SPELL."
3230 PRINT:PRINT"YOUR STRENGTH IS"ST:PRINT"YOUR DEXTERITY IS"DX".":GOSUB4370
3240 IFO$<>"A"THEN3440
3250 IFWV<>0THEN3270
3260 PRINT:M$="POUNDING ON "+C$(A+12)+" WON'T HURT IT!":GOSUB1:GOTO3700
3270 IFBF<>1THEN3290
3280 PRINT:M$="YOU CAN'T BEAT IT TO DEATH WITH A BOOK!":GOSUB1:GOTO3700
3290 IFDX>=RND(20)+(3*BL)THEN3310
3300 PRINT:PRINT"YOU MISSED, TOO BAD!":GOTO3700
3310 Z$=RIGHT$(C$(A+12),LEN(C$(A+12))-2):IFLEFT$(Z$,1)=" "THENZ$=MID$(Z$,2)
3320 PRINT:PRINT"YOU HIT THE EVIL "Z$"!":Q2=Q2-WV
3330 IF(A<>9ANDA<>12)THEN3360
3340 IF RND(8)<>1 GOTO3360
3350 PRINT:M$="OH NO! YOUR "+W$(WV+1)+" BROKE!":GOSUB1:WV=0
3360 IF Q2>0 GOTO3700
3370 PRINT:MC=MC-1:M$=C$(A+12)" LIES DEAD AT YOUR FEET!":GOSUB1
3380 IF H>T-60 GOTO3400
3390 PRINT:M$="YOU SPEND AN HOUR EATING "+C$(A+12)+E$(RND(8))+".":GOSUB1:H=T
3400 IFX<>R(1)ORY<>R(2)ORZ<>R(3)THENON(1-(A=13))GOTO3420,4310
3410 PRINT:M$="GREAT ZOT! YOU'VE FOUND THE RUNESTAFF!":GOSUB1:R(1)=0:RF=1
3420 Q=RND(1000):PRINT:M$="YOU NOW GET HIS HOARD OF"+STR$(Q)+" GP'S!":GOSUB1:GP=GP+Q
3430 GOTO2060
3440 IFO$="R"THEN3700
3450 IFO$<>"C"THEN3590
3460 IFIQ>=15ORQ3<=1THEN3480
3470 PRINT:M$="YOU CAN'T CAST A SPELL NOW!":GOSUB1:GOTO3190
3480 PRINT:M$="WHICH SPELL (WEB, FIREBALL, DEATHSPELL)?":GOSUB4:PRINT
3490 IFO$<>"W"THEN3510
3500 ST=ST-1:WC=RND(8)+1:ON (1-(ST<1)) GOTO3700,3890
3510 IFO$<>"F"THEN3540
3520 Q=RND(7)+RND(7):ST=ST-1:IQ=IQ-1:IF(IQ<1)OR(ST<1)THEN3890
3530 M$="IT DOES"+STR$(Q)+" POINTS WORTH OF DAMAGE.":GOSUB1:Q2=Q2-Q:GOTO3360
3540 IFO$="D"THEN3560
3550 PRINT:M$="TRY ONE OF THE OPTIONS GIVEN.":GOSUB1:GOTO3190
3560 PRINT"DEATH . . . ";
3570 IFIQ<RND(4)+15THENPRINT"YOURS!":IQ=0:GOTO3890
3580 PRINT"HIS!":Q2=0:GOTO3370
3590 IFO$="B"ANDQ3<=1THEN3610
3600 GOTO3550
3610 IFTC<>0THEN3640
3620 PRINT:PRINT"ALL I WANT IS YOUR LIFE!"
3630 GOTO3700
3640 Q=RND(8):IFT(Q)=0THEN3640
3650 PRINT:M$="I WANT "+C$(Q+25)+". WILL YOU GIVE IT TO ME?":GOSUB4
3660 IFO$="N"THEN3700
3670 IFO$<>"Y"THENPRINTY$:GOTO3650
3680 T(Q)=0:TC=TC-1:PRINT:PRINT"OK, JUST DON'T TELL ANYONE ELSE."
3690 N=Z:GOSUB60:VF=VF+(L(N)=25):GOTO700
3700 Q3=2:IFWC<=0THEN3720
3710 WC=WC-1:IF WC=0THENPRINT:PRINT"THE WEB JUST BROKE!"
3720 Z$=RIGHT$(C$(A+12),LEN(C$(A+12))-2):IF LEFT$(Z$,1)=" "THEN Z$=MID$(Z$,2)
3730 IFWC<=0THEN3750
3740 PRINT:M$="THE "+Z$+" IS STUCK AND CAN'T ATTACK NOW!":GOSUB1:GOTO3790
3750 PRINT:M$="THE "+Z$+" ATTACKS!":GOSUB1
3760 IFDX<RND(7)+RND(7)+RND(7)+3*BLTHEN3780
3770 PRINT:PRINT"WHAT LUCK, HE MISSED YOU!":GOTO3790
3780 PRINT:PRINT"OUCH! HE HIT YOU!":Q=Q1:GOSUB3840:IF ST<1 GOTO3890
3790 IFO$<>"R"THEN3190
3800 PRINT:PRINT"YOU HAVE ESCAPED!":PRINT
3810 PRINT"DO YOU WANT TO GO NORTH, SOUTH, EAST, OR WEST";:GOSUB4370
3820 IFO$="N"ORO$="S"ORO$="E"ORO$="W"THEN1380
3830 PRINT:M$="DON'T PRESS YOUR LUCK, "+R$(RC)+"!":GOSUB1:GOTO3810
3840 IFAV=0THEN3880
3850 Q=Q-AV:AH=AH-AV:IFQ<0THENAH=AH-Q:Q=0
3860 IF AH>=0 GOTO3880
3870 AH=0:AV=0:PRINT:M$="YOUR ARMOR HAS BEEN DESTROYED... GOOD LUCK!":GOSUB1
3880 ST=ST-Q:RETURN
3890 GOSUB4360
3900 M$="A NOBLE EFFORT, OH FORMERLY LIVING "+R$(RC)+"!":GOSUB1
3910 PRINT"YOU DIED DUE TO LACK OF ":IF ST<1 THEN PRINT"STRENGTH."
3920 IFIQ<1THENPRINT"INTELLIGENCE."
3930 IFDX<1THENPRINT"DEXTERITY."
3940 PRINT:Q3=1:PRINT"AT THE TIME YOU DIED, YOU HAD:":GOTO4030
3950 Q3=0:PRINT:PRINT"YOU LEFT THE CASTLE WITH";:IFOF=0THENPRINT"OUT";
3960 PRINT" THE ORB OF ZOT.":PRINT:IF OF=0 GOTO4000
3970 PRINT:PRINT"AN INCREDIBLY GLORIOUS VICTORY!!":PRINT
3980 PRINT"IN ADDITION, YOU GOT OUT WITH   THE FOLLOWING:"
3990 GOTO4020
4000 PRINT:PRINT"A STUPENDOUS DEFEAT!"
4010 PRINT"WHEN YOU LEFT, YOU HAD:"
4020 IFQ3=0THENPRINT"YOUR MISERABLE LIFE!"
4030 FORQ=1TO8:IF T(Q)=1 THEN PRINT C$(Q+25)
4040 NEXT Q:PRINTW$(WV+1)" AND "W$(AV+5);:IFLF=1THENPRINT" AND A LAMP";
4050 PRINT:PRINT"YOU ALSO HAD"FL"FLARES AND":PRINT GP"GOLD PIECES"
4060 IFRF=1THENPRINT"AND THE RUNESTAFF"
4070 PRINT:PRINT"AND IT TOOK YOU"T"TURNS!":PRINT
4080 M$="ARE YOU FOOLISH ENOUGH TO WANT TO PLAY AGAIN?":GOSUB4:PRINT
4090 IFO$<>"Y"THEN4120
4100 PRINT"SOME "R$(RC)"S NEVER LEARN!":PRINT
4110 M$="PLEASE BE PATIENT WHILE THE CASTLE IS RESTOCKED.":GOSUB1:GOTO100
4120 IFO$<>"N"THENPRINTY$:GOTO4080
4130 M$="MAYBE DUMB "+R$(RC)+" IS NOT SO DUMB AFTER ALL!":GOSUB1:GOTO4520
4140 PRINT:PRINT"GREAT UNMITIGATED ZOT!":PRINT
4150 PRINT"YOU JUST FOUND *THE ORB OF ZOT*!"
4160 PRINT"THE RUNESTAFF HAS DISAPPEARED!":RF=0:OF=1:O(1)=0:GOTO2060
4170 DATAAN EMPTY ROOM,.,THE ENTRANCE,E,STAIRS GOING UP,U,STAIRS GOING DOWN,D,A POOL,P,A CHEST,C,GOLD PIECES,G
4190 DATAFLARES,F,A WARP,W,A SINKHOLE,S,A CRYSTAL ORB,O,A BOOK,B,A KOBOLD,M,AN ORC,M,A WOLF,M,A GOBLIN,M,AN OGRE,M
4210 DATAA TROLL,M,A BEAR,M,A MINOTAUR,M,A GARGOYLE,M,A CHIMERA,M,A BALROG,M,A DRAGON,M,A VENDOR,V,THE RUBY RED,T
4230 DATATHE NORN STONE,T,THE PALE PEARL,T,THE OPAL EYE,T,THE GREEN GEM,T,THE BLUE FLAME,T,THE PALANTIR,T
4250 DATATHE SILMARIL,T,X,"?",NO WEAPON," SANDWICH",DAGGER," STEW",MACE," SOUP",SWORD," BURGER",NO ARMOR," ROAST"
4270 DATALEATHER," FILET",CHAINMAIL," TACO",PLATE," PIE",HOBBIT,ELF,MAN,DWARF
4290 X=RND(8):Y=RND(8):N=Z:GOSUB60:IFL(N)<>101THEN4290
4300 N=Z:GOSUB60:L(N)=Q:PRINT".";:RETURN
4310 PRINT:PRINT"YOU GET ALL HIS WARES:":PRINT"PLATE ARMOR":AV=3:AH=21
4320 PRINT"A SWORD":WV=3:PRINT"A STRENGTH POTION":N=(ST+RND(6)):GOSUB55:ST=N
4330 PRINT"AN INTELLIGENCE POTION":N=(IQ+RND(6)):GOSUB55:IQ=N:PRINT"A DEXTERITY POTION"
4340 N=(DX+RND(6)):GOSUB55:DX=N:IF LF=0 THEN PRINT"A LAMP":LF=1
4350 GOTO3420
4360 PRINT"********************************":RETURN
4370 PRINT:PRINT"YOUR CHOICE? ";
4390 O$=INKEY$:IFO$=""THEN4390
4391 PRINTO$:RETURN
4410 M$="HOW MANY POINTS DO YOU WISH TO ADD TO YOUR "+Z$:GOSUB4:PRINT
4420 Q=VAL(O$):IFQ=0ANDASC(O$)<>48THENQ=-1
4430 IF(Q<0)OR(Q>OT)OR(Q<>INT(Q))THEN4410
4440 OT=OT-Q:RETURN:INPUTO$:Q=INT(VAL(O$)):RETURN
4450 PRINT:PRINTZ$;:INPUTO$:Q=INT(VAL(O$)):IFQ>0ANDQ<9THENRETURN
4460 PRINT:M$="TRY A NUMBER FROM 1 TO 8.":GOSUB1:GOTO4450
4470 PRINT:M$="DO YOU WANT TO BUY A POTION OF "+Z$+" FOR 1000 GP'S":GOSUB1
4480 GOTO4370
4490 PRINT:PRINT"YOUR "Z$" IS NOW"Q".":RETURN
4500 PRINT:M$="THESE ARE THE TYPES OF "+Z$+" YOU CAN BUY:":GOSUB1:RETURN
4510 M$="YOU ARE AT "
4515 M$=M$+"("+MID$(STR$(X),2)+","+MID$(STR$(Y),2)+") LEVEL"+STR$(Z)+".":GOSUB1:RETURN
4520 END
4530 PRINT@504,CHR$(91)"ENTER"CHR$(93);
4540 IFINKEY$=""THEN4540
4550 CLS:RETURN
