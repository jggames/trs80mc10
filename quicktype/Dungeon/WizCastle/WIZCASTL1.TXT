0 CLS:CLEAR300:DIML$(8),CC,C1,M$,I$,R$(4),W$(8),E$(8),C(3,4),T(8),O(3),R(3),X,Y,Z,V,L:GOTO10
1 C1=1:CC=32:FORCC=CCTOC1STEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THENPRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 GOSUB1:GOTO135
7 GOSUB1:GOSUB833:PRINT:RETURN
10 L$(1)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
11 L$(2)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
12 L$(3)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
13 L$(4)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
14 L$(5)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
15 L$(6)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
16 L$(7)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee"
17 L$(8)="eeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeeee":GOTO35
26 M$="OK, BOLD "+R$(RC)+", YOU HAVE"+STR$(G)+" GOLD PIECES":GOSUB1:RETURN
27 N=N+8*((N=9)-(N=0)):RETURN
29 N=-N*(N<19)-18*(N>18):RETURN
31 V=VARPTR(L$(N))+65536:L=PEEK(V+2)*256+PEEK(V+3)+8*(X-1)+(Y-1):RETURN
33 E=E+100*(E>99):RETURN
34 RESTORE:FORQ=1TOC1:READC$,I$:NEXT:RETURN
35 Y$="ANSWER YES OR NO":NG=0
37 CLS:NG=NG+1:Q=RND(0):C1=34:GOSUB34
39 FORQ=1TO8:READW$(Q),E$(Q):NEXT
41 FORQ=1TO4:READR$(Q):NEXT:IFNG>1THEN45
43 PRINT@435,CHR$(95);:PRINT@6,"THE WIZARD'S CASTLE":GOSUB831:PRINT"HOLD ON WHILE I SET UP THE GAME.";
45 X=1:Y=4:N=1:GOSUB31:POKEL,2:FORZ=1TO7:FORQ1=1TO2:Q=104:GOSUB817:N=Z+1:GOSUB31:POKEL,103:NEXTQ1,Z
47 FORZ=1TO8:FORQ=113TO124:GOSUB817:NEXT:FORQ1=1TO3:FORQ=105TO112:GOSUB817:NEXT:Q=125:GOSUB817:NEXTQ1,Z
49 FORQ=126TO133:Z=RND(8):GOSUB817:NEXT:Q=101:FORA=1TO3:Z=RND(8):GOSUB817:C(A,1)=X:C(A,2)=Y:C(A,3)=Z:C(A,4)=0:NEXT
51 RC=0:ST=2:DX=14:R$(3)="MAN":Q=112+RND(12):Z=RND(8):GOSUB817:R(1)=X:R(2)=Y:R(3)=Z:Q=109:Z=RND(8):GOSUB817
53 O(1)=X:O(2)=Y:O(3)=Z:BF=0:OT=8:AV=0:HT=0:T=1:VF=0:LF=0:TC=0:G=60:RF=0:OF=0:BL=0:I=8:SX=0:FORQ=1TO8:T(Q)=0:NEXT
55 CLS:PRINT"ALL RIGHT, BOLD ONE, YOU MAY BE AN ELF, DWARF, MAN, OR HOBBIT.":GOSUB7:FORQ=1TO4
59 IFLEFT$(R$(Q),1)=O$THENRC=Q:ST=ST+2*Q:DX=DX-2*Q
61 NEXTQ:OT=OT+4*(RC=1):IFRC>0THENR$(3)="HUMAN":GOTO65
63 GOTO55
65 PRINT"WHICH SEX DO YOU PREFER?":GOSUB833:PRINT:IFO$="M"THENSX=1:GOTO71
67 IFO$<>"F"THEN65
71 CLS:M$="OK, "+R$(RC)+", YOU HAVE THE FOLLOWING ATTRIBUTES:":GOSUB1
73 PRINT"STRENGTH:"ST:PRINT"INTELLIGENCE:"I:PRINT"DEXTERITY:"DX
75 M$="AND"+STR$(OT)+" OTHER POINTS TO ALLOCATE AS YOU WISH.":GOSUB1:Z$="STRENGTH"
77 GOSUB839:ST=ST+Q:IFOT=0THEN83
79 Z$="INTELLIGENCE":GOSUB839:I=I+Q:IFOT=0THEN83
81 Z$="DEXTERITY":GOSUB839:DX=DX+Q
83 CLS:GOSUB26
85 Z$="ARMOR":GOSUB857:AV=0:WV=0:FL=0:WC=0
87 M$="PLATE<30> CHAINMAIL<20> LEATHER<10> NOTHING<0>":GOSUB7
89 IFO$="N"THEN95
91 AV=-3*(O$="P")-2*(O$="C")-(O$="L"):IFAV>0THEN95
93 C1=RND(12)+12:GOSUB34:M$="ARE YOU A "+R$(RC)+" OR "+C$+"?":GOSUB1:GOTO85
95 AH=AV*7:G=G-AV*10
97 CLS:GOSUB26
99 Z$="WEAPONS":GOSUB857:M$="SWORD<30> MACE<20> DAGGER<10> NOTHING<0>":GOSUB1
101 GOSUB833:PRINT:IFO$="N"THEN107
103 WV=-3*(O$="S")-2*(O$="M")-(O$="D"):IFWV>0THEN107
105 M$="IS YOUR IQ REALLY"+STR$(I)+"?":GOSUB1:GOTO99
107 G=G-WV*10:IFG<20THEN117
109 CLS
111 M$="DO YOU WANT TO BUY A LAMP FOR 20 GP'S?":GOSUB7
113 IFO$="Y"THENLF=1:G=G-20:GOTO117
115 IFO$<>"N"THENM$=Y$:GOSUB1:GOTO111
117 IFG<1THENQ=0:GOTO131
119 PRINT:M$="OK, "+R$(RC)+", YOU HAVE"+STR$(G)+" GOLD PIECES LEFT.":GOSUB1
121 M$="FLARES COST 1 GP EACH. HOW MANY DO YOU WANT?":GOSUB1:INPUTO$:Q=VAL(O$)
123 IF(Q>0)OR(ASC(O$)=48)THEN129
125 M$="TYPE 0 FOR NONE.":GOSUB1
127 GOTO121
129 IFQ>GTHENM$="YOU CAN ONLY AFFORD"+STR$(G):GOSUB1:GOTO121
131 FL=FL+Q:G=G-Q:X=1:Y=4:Z=1
133 CLS:M$="OK, "+R$(RC)+", YOU ARE NOW ENTERING THE CASTLE!":GOSUB1:GOTO443
135 T=T+1:IFRF+OF>0THEN151
137 IFC(1,4)>T(1)THENT=T+1
139 IFC(2,4)>T(3)THENG=G-RND(5)
141 IFG<0THENG=0
143 IFC(3,4)<=T(5)THEN151
145 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB31:E=PEEK(L):GOSUB33:X=1:Y=1:N=RND(Z):GOSUB31:POKEL,E+100
147 X=A:Y=B:Z=C:N=Z:GOSUB31:IFPEEK(L)<>1THEN151
149 FORQ=1TO3:C(Q,4)=-(C(Q,1)=X)*(C(Q,2)=Y)*(C(Q,3)=Z):NEXT
151 IFRND(5)>1THEN181
153 M$="YOU ":Q=RND(7)+BL:IFQ>7THENQ=4
155 ONQGOSUB173,161,171,159,175,177,179:GOSUB1:GOTO181
159 M$=M$+"STEPPED ON A FROG!":RETURN
161 M$=M$+"HEAR ":ONRND(4)GOTO163,165,167,169
163 M$=M$+"A SCREAM!":RETURN
165 M$=M$+"FOOTSTEPS!":RETURN
167 M$=M$+"A WUMPUS!":RETURN
169 M$=M$+"THUNDER!":RETURN
171 M$=M$+"SNEEZED!":RETURN
173 M$=M$+"SEE A BAT FLY BY!":RETURN
175 C1=12+RND(13):GOSUB34:M$=M$+"SMELL "+C$+" FRYING!":RETURN
177 M$=M$+"FEEL LIKE YOU'RE BEING WATCHED!":RETURN
179 M$=M$+"HEAR FAINT RUSTLING NOISES!":RETURN
181 IFBL+T(4)<>2THEN185
183 C1=29:GOSUB34:M$=C$+" CURES YOUR BLINDNESS!":GOSUB1:BL=0
185 IFBF+T(6)<>2THEN189
187 C1=31:GOSUB34:M$=C$+" DISSOLVES THE BOOK!":GOSUB1:BF=0
189 PRINT"ENTER YOUR COMMAND: ";:GOSUB835
191 IFO$="N"THENPRINT"ORTH":GOTO275
193 IFO$="S"THENPRINT"OUTH":GOTO277
195 IFO$="E"THENPRINT"AST":GOTO277
197 IFO$="W"THENPRINT"EST":GOTO277
199 IFO$="U"THENPRINT"P":GOTO279
201 IFO$<>"D"THEN211
203 I$=INKEY$:IFI$=""THEN203
205 O$=O$+I$
207 IFO$="DR"THENPRINT"RINK":GOTO333
209 PRINT"OWN":GOTO283
211 IFO$="M"THENPRINT"AP":GOTO287
213 IFO$="F"THENPRINT"LARE":ONBL+1GOTO303,287
215 IFO$="L"THENPRINT"AMP":ONBL+1GOTO315,287
217 IFO$="O"THENPRINT"PEN":GOTO363
219 IFO$="G"THENPRINT"AZE":ONBL+1GOTO401,287
221 IFO$="T"THENPRINT"ELEPORT":ONRF+1GOTO427,431
223 IFO$="Q"THENPRINT"UIT":GOTO435
225 IFO$<>"H"THEN273
227 CLS:PRINT"* WIZARD'S CASTLE INSTRUCTIONS *":PRINT"COMMANDS:":PRINT"H/ELP   N/ORTH    S/OUTH"
235 PRINT"E/AST   W/EST     U/P":PRINT"D/OWN   DR/INK    M/AP":PRINT"F/LARE  L/AMP     O/PEN"
241 PRINT"G/AZE   T/ELEPORT Q/UIT":GOSUB865:PRINT"CONTENTS OF ROOMS:":PRINT".=EMPTY ROOM  B=BOOK"
247 PRINT"C=CHEST       D=STAIRS DOWN":PRINT"W=WARP/ORB    F=FLARES":PRINT"M=MONSTER     G=GOLD PIECES"
253 PRINT"P=MAGIC POOL  O=CRYSTAL ORB":PRINT"S=SINKHOLE    T=TREASURE":PRINT"U=STAIRS UP   V=VENDOR"
259 PRINT"E=ENTRANCE / EXIT":GOSUB865:M$="THE BENEFITS OF HAVING TREASURES ARE:":GOSUB1
263 PRINT"RUBY RED - AVOID LETHARGY":PRINT"PALE PEARL - AVOID LEECH"
265 PRINT"GREEN GEM - AVOID FORGETTING":PRINT"OPAL EYE - CURES BLINDNESS"
267 PRINT"BLUE FLAME - DISSOLVES BOOKS":PRINT"NORN STONE - NO BENEFIT"
269 PRINT"PALANTIR - NO BENEFIT":PRINT"SILMARIL - NO BENEFIT"
271 GOSUB865:GOTO135
273 M$="SILLY "+R$(RC)+", THAT WASN'T A VALID COMMAND!":GOTO5
275 N=Z:GOSUB31:IFPEEK(L)=2THEN761
277 X=X+(O$="N")-(O$="S"):Y=Y+(O$="W")-(O$="E"):N=X:GOSUB27:X=N:N=Y:GOSUB27:Y=N:GOTO443
279 N=Z:GOSUB31:IFPEEK(L)=3THENZ=Z-1:GOTO443
281 Z$="UP":GOTO285
283 Z$="DOWN":N=Z:GOSUB31:IFPEEK(L)=4THENZ=Z+1:GOTO443
285 M$="THERE ARE NO STAIRS GOING "+Z$+" FROM HERE!":GOTO5
287 IFBL<>1THEN291
289 M$="YOU CAN'T SEE ANYTHING, YOU DUMB "+R$(RC)+"!":GOTO5
291 CLS:A=X:B=Y:FORX=1TO8:FORY=1TO8:N=Z:GOSUB31:Q=PEEK(L)
293 IFQ>99THENQ=Q-100
295 IFX=AANDY=BTHENC1=Q:GOSUB34:PRINT"<"I$"> ";:GOTO299
297 C1=Q:GOSUB34:PRINT" "I$"  ";
299 NEXTY,X:X=A:Y=B:GOTO313
301 PRINT") LEVEL"Z:GOTO135
303 IFFL<>0THEN307
305 M$="HEY, BRIGHT ONE, YOU'RE OUT OF FLARES!":GOTO5
307 FL=FL-1:A=X:B=Y:FORQ1=A-1TOA+1:N=Q1:GOSUB27:X=N:FORQ2=B-1TOB+1
309 N=Q2:GOSUB27:Y=N:N=Z:GOSUB31:E=PEEK(L):GOSUB33:Q=E:POKEL,Q:C1=Q:GOSUB34:PRINT" "I$"   ";:NEXTQ2
311 NEXTQ1:X=A:Y=B
313 GOSUB859:GOTO135
315 IFLF<>0THEN319
317 M$="YOU DON'T HAVE A LAMP, "+R$(RC)+"!":GOTO5
319 M$="WHERE DO YOU WANT TO SHINE THE LAMP (N,S,E,W)?":GOSUB7
321 A=X:B=Y:N=(X+(O$="N")-(O$="S")):GOSUB27:X=N:N=(Y+(O$="W")-(O$="E")):GOSUB27:Y=N
323 IFA-X+B-Y<>0THEN327
325 M$="THAT'S NOT A DIRECTION, "+R$(RC)+"!":GOTO5
327 M$="THE LAMP SHINES INTO ":GOSUB861
329 N=Z:GOSUB31:E=PEEK(L):GOSUB33:POKEL,E:C1=E:GOSUB34:M$="THERE YOU WILL FIND "+C$:GOSUB1
331 X=A:Y=B:GOTO135
333 N=Z:GOSUB31:IFPEEK(L)=5THEN339
335 M$="IF YOU WANT A DRINK, FIND A POOL!":GOSUB1
337 GOTO135
339 Q=RND(8):PRINT"YOU TAKE A DRINK AND ":IFQ<7THENPRINT"FEEL ";
341 ONQGOTO343,345,347,349,351,353,355,359
343 N=(ST+RND(3)):GOSUB29:ST=N:PRINT"STRONGER.":GOTO135
345 ST=ST-RND(3):PRINT"WEAKER.":ON(1-(ST<1))GOTO135,749
347 N=(I+RND(3)):GOSUB29:I=N:PRINT"SMARTER.":GOTO135
349 I=I-RND(3):PRINT"DUMBER.":ON(1-(I<1))GOTO135,749
351 N=(DX+RND(3)):GOSUB29:DX=N:PRINT"NIMBLER.":GOTO135
353 DX=DX-RND(3):PRINT"CLUMSIER.":ON(1-(DX<1))GOTO135,749
355 Q=RND(4):IFQ=RCTHEN355
357 RC=Q:PRINT"BECOME A "R$(RC)".":GOTO135
359 SX=1-SX:PRINT"TURN INTO A ";:IFSX=0THENPRINT"FE";
361 PRINT"MALE "R$(RC)"!":GOTO135
363 N=Z:GOSUB31:IFPEEK(L)<>6THEN367
365 PRINT"YOU OPEN THE CHEST AND":GOTO389
367 N=Z:GOSUB31:IFPEEK(L)<>12THEN371
369 PRINT"YOU OPEN THE BOOK AND":GOTO373
371 M$="THE ONLY THING OPENED WAS YOUR BIG MOUTH!":GOTO5
373 ONRND(6)GOTO375,377,379,381,383,385
375 M$="FLASH! OH NO! YOU ARE NOW A BLIND "+R$(RC)+"!":GOSUB1:BL=1:GOTO387
377 M$+"IT'S ANOTHER VOLUME OF ZOT'S POETRY! - YECH!!":GOSUB1:GOTO387
379 M$="IT'S AN OLD COPY OF PLAY"+R$(RND(4))+"!":GOSUB1:GOTO387
381 PRINT"IT'S A MANUAL OF DEXTERITY!":DX=18:GOTO387
383 PRINT"IT'S A MANUAL OF STRENGTH!":ST=18:GOTO387
385 M$="THE BOOK STICKS TO YOUR HANDS - NOW YOU ARE UNABLE TO DRAW YOUR WEAPON!":GOSUB1:BF=1
387 N=Z:GOSUB31:POKEL,1:GOTO135
389 ONRND(4)GOTO391,395,397,395
391 PRINT"KABOOM! IT EXPLODES!!":Q=RND(6):GOSUB739
393 ON(1-(ST<1))GOTO387,749
395 Q=RND(1000):PRINT"FIND"Q"GOLD PIECES!":G=G+Q:GOTO387
397 M$="GAS! YOU STAGGER FROM THE ROOM!":GOSUB1:N=Z:GOSUB31:POKEL,1:T=T+20
399 O$=MID$("NSEW",RND(4),1):GOTO277
401 N=Z:GOSUB31:IFPEEK(L)=11THEN405
403 M$="IT'S HARD TO GAZE WITHOUT AN ORB!":GOSUB1:GOTO5
405 M$="YOU SEE ":ONRND(6)GOTO407,411,415,417,421,425
407 M$=M$+"YOURSELF IN A BLOODY HEAP!":GOSUB1
409 ST=ST-RND(2):ON(1-(ST<1))GOTO135,749
411 C1=12+RND(13):GOSUB34:M$=M$+"YOURSELF DRINKING FROM A POOL AND BECOMING "+C$+"!":GOSUB1
413 GOTO135
415 C1=12+RND(13):GOSUB34:M$=M$+C$+" GAZING BACK AT YOU!":GOTO5
417 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB31:E=PEEK(L):GOSUB33:Q=E:POKEL,Q
419 C1=Q:GOSUB34:M$=M$+C$+" AT ":GOSUB861:X=A:Y=B:Z=C:GOTO135
421 A=RND(8):B=RND(8):C=RND(8):IFRND(8)<4THEN A=O(1):B=O(2):C=O(3)
423 M$=M$+" THE ORB OF ZOT AT ("+STR$(A)+","+STR$(B)+") LEVEL"+STR$(C)+"!":GOTO5
425 M$=M$+"A SOAP OPERA RERUN!":GOTO5
427 IFRF<>0THEN431
429 M$="YOU CAN'T TELEPORT WITHOUT THE RUNESTAFF!":GOSUB1:GOTO5
431 Z$="X-COORDINATE":GOSUB847:X=Q:Z$="Y-COORDINATE":GOSUB847:Y=Q
433 Z$="Z-COORDINATE":GOSUB847:Z=Q:O$="T":GOTO443
435 PRINT"ARE YOU SURE?":GOSUB833:PRINT
437 IFO$="Y"THEN441
439 PRINT:PRINT"THEN DON'T SAY THAT YOU DO!":GOTO135
441 GOTO771
443 CLS:IFBL=0THENGOSUB859
445 PRINT"STRENGTH:"ST;TAB(17)"DEXTERITY:"DX:PRINT"INTELLIGENCE:"I;TAB(17)"GOLD:"G
447 PRINT"TREASURES:"TC;TAB(17)"FLARES:"FL
449 PRINT"WEAPON = "W$(WV+1):PRINT"ARMOR = "W$(AV+5)
451 IFLF=1THENPRINT"AND A LAMP"
453 WC=0:N=Z:GOSUB31:E=PEEK(L):GOSUB33:Q=E:POKEL,Q:Z$="YOU NOW HAVE"
455 C1=Q:GOSUB34:M$="HERE YOU FIND "+C$:GOSUB1:IF(Q<7)OR(Q=11)OR(Q=12)THEN135
457 IFQ=7THENG=G+RND(50):M$=Z$+STR$(G):GOSUB1:GOTO387
459 IFQ=8THENFL=FL+RND(5):M$=Z$+STR$(FL):GOSUB1:GOTO387
461 IFQ>9THEN467
463 IF(O(1)=X)AND(O(2)=Y)AND(O(3)=Z)THENON(1-(O$="T"))GOTO277,799
465 X=RND(8):Y=RND(8):Z=RND(8):GOTO443
467 IFQ=10THENN=Z+1:GOSUB27:Z=N:GOTO443
469 IFQ<=25ORQ>=34THEN473
471 PRINT"IT'S NOW YOURS!":T(Q-25)=1:TC=TC+1:GOTO387
473 N=Z:GOSUB31:A=PEEK(L)-12:WC=0:IF(A<13)OR(VF=1)THEN605
475 M$="YOU MAY TRADE WITH, ATTACK, OR IGNORE THE VENDOR.":GOSUB7
477 IFO$="I"THEN135
479 IFO$<>"A"THEN483
481 VF=1:M$="YOU'LL BE SORRY THAT YOU DID THAT!":GOSUB1:GOTO605
483 IFO$="T"THEN487
485 M$="NICE SHOT, "+R$(RC)+"!":GOSUB1:GOTO475
487 FORQ=1TO8:A=RND(Q*1500):IFT(Q)=0THEN495
489 C1=Q+25:GOSUB34:M$="DO YOU WANT TO SELL "+C$+" FOR"+STR$(A)+"GP'S?":GOSUB7
491 IFO$="Y"THENTC=TC-1:T(Q)=0:G=G+A:GOTO495
493 IFO$<>"N"THENPRINTY$:GOTO489
495 NEXTQ:IFG>=1000THEN499
497 M$="YOU'RE TOO POOR TO TRADE, "+R$(RC)+".":GOTO5
499 IFG<1250THEN563
501 IFAV=3THEN531
503 GOSUB26:M$="AND ":IFLEFT$(W$(AV+5),2)="NO"THENM$=M$+W$(AV+5):GOSUB1:GOTO507
505 M$=M$+W$(AV+5)+" ARMOR.":GOSUB1
507 Z$="ARMOR":GOSUB857:M$="NOTHING<0> LEATHER<1250> "
509 IFG>1499THENM$=M$+"CHAINMAIL<1500> "
511 IFG>1999THENM$=M$+"PLATE<2000>"
513 GOSUB1:GOSUB833:PRINT:IFO$="N"THEN531
515 IFO$="L"THENG=G-1250:AV=1:AH=7:GOTO531
517 IFO$<>"C"ORG>=1500THEN521
519 M$="YOU HAVEN'T GOT THAT MUCH CASH ON HAND!":GOSUB1:GOTO507
521 IFO$="C"THENG=G-1500:AV=2:AH=14:GOTO531
523 IFO$<>"P"ORG>=2000THEN527
525 M$="YOU CAN'T AFFORD PLATE ARMOR!":GOSUB1:GOTO507
527 IFO$="P"THENG=G-2000:AV=3:AH=21:GOTO531
529 PRINT"CHOOSE A SELECTION.":GOTO513
531 IFG<1250THEN563
533 IFWV=3THEN563
535 GOSUB26:M$="WITH "+W$(WV+1)+" IN HAND.":GOSUB1
537 Z$="WEAPON":GOSUB857:M$="NOTHING<0> DAGGER<1250> "
539 IFG>1499THENM$=M$+"MACE<1500> "
541 IFG>1999THENM$=M$+"SWORD<2000>"
543 GOSUB1:GOSUB833:PRINT:IFO$="N"THEN563
545 IFO$="D"THENG=G-1250:WV=1:GOTO563
547 IFO$<>"M"ORG>=1500THEN551
549 M$="SORRY SIR, I'M AFRAID I DON'T GIVE CREDIT!":GOSUB1:GOTO537
551 IFO$="M"THENG=G-1500:WV=2:GOTO563
553 IFO$<>"S"ORG>=2000THEN559
555 M$="YOUR DUNGEON EXPRESS CARD - YOU LEFT HOME WITHOUT IT!":GOSUB1
557 GOTO537
559 IFO$="S"THENG=G-2000:WV=3:GOTO563
561 PRINT"TRY CHOOSING A SELECTION!":GOTO543
563 IFG<1000THEN135
565 IFST>17THEN573
567 Z$="STRENGTH":GOSUB851:IFO$<>"Y"THEN571
569 G=G-1000:N=(ST+RND(6)):GOSUB29:ST=N:Q=ST:GOSUB855:GOTO563
571 IFO$<>"N"THENPRINTY$:GOTO567
573 IFG<1000THEN135
575 IFI>17THEN583
577 Z$="INTELLIGENCE":GOSUB851:IFO$<>"Y"THEN581
579 G=G-1000:N=(I+RND(6)):GOSUB29:I=N:Q=I:GOSUB855:GOTO573
581 IFO$<>"N"THENPRINTY$:GOTO577
583 IFG<1000THEN135
585 IFDX>17THEN593
587 Z$="DEXTERITY":GOSUB851:IFO$<>"Y"THEN591
589 G=G-1000:N=(DX+RND(6)):GOSUB29:DX=N:Q=DX:GOSUB855:GOTO583
591 IFO$<>"N"THENPRINTY$:GOTO587
593 IF(G<1000)OR(LF=1)THEN135
595 M$="DO YOU WANT TO BUY A LAMP FOR 1000 GP'S?":GOSUB7
597 IFO$<>"Y"THEN601
599 G=G-1000:LF=1:M$="IT'S GUARANTEED TO OUTLIVE YOU!":GOSUB1:GOTO135
601 IFO$<>"N"THENPRINTY$:GOTO595
603 GOTO135
605 Q1=1+INT(A/2):Q2=A+2:Q3=1
607 IF(C(1,4)>T(1))OR(BL=1)OR(DX<RND(9)+RND(9))THEN711
609 C1=A+12:GOSUB34:PRINT"YOU'RE FACING "C$"!"
611 PRINT"YOU MAY ATTACK OR RETREAT."
613 IFQ3=1THENPRINT"YOU CAN ALSO ATTEMPT A BRIBE."
615 IFI>14THENPRINT"YOU CAN ALSO CAST A SPELL."
617 PRINT"YOUR STRENGTH IS"ST:PRINT"YOUR DEXTERITY IS"DX:GOSUB833:PRINT
619 IFO$<>"A"THEN659
621 IFWV<>0THEN625
623 C1=A+12:GOSUB34:M$="POUNDING ON "+C$+" WON'T HURT IT!":GOSUB1:GOTO711
625 IFBF<>1THEN629
627 M$="YOU CAN'T BEAT IT TO DEATH WITH A BOOK!":GOSUB1:GOTO711
629 IFDX>=RND(20)+(3*BL)THEN633
631 PRINT"YOU MISSED, TOO BAD!":GOTO711
633 C1=A+12:GOSUB34:Z$=RIGHT$(C$,LEN(C$)-2):IFLEFT$(Z$,1)=" "THENZ$=MID$(Z$,2)
635 PRINT"YOU HIT THE EVIL "Z$"!":Q2=Q2-WV
637 IF(A<>9ANDA<>12)THEN643
639 IFRND(8)<>1THEN643
641 M$="OH NO! YOUR "+W$(WV+1)+" BROKE!":GOSUB1:WV=0
643 IFQ2>0THEN711
645 C1=A+12:GOSUB34:MC=MC-1:M$=C$+" LIES DEAD AT YOUR FEET!":GOSUB1
647 IFH>T-60THEN651
649 C1=A+12:GOSUB34:M$="YOU SPEND AN HOUR EATING "+C$+E$(RND(8)):GOSUB1:H=T
651 IFX<>R(1)ORY<>R(2)ORZ<>R(3)THENON(1-(A=13))GOTO655,821
653 M$="GREAT ZOT! YOU'VE FOUND THE RUNESTAFF!":GOSUB1:R(1)=0:RF=1
655 Q=RND(1000):M$="YOU NOW GET HIS HOARD OF"+STR$(Q)+" GP'S!":GOSUB1:G=G+Q
657 GOTO387
659 IFO$="R"THEN711
661 IFO$<>"C"THEN689
663 IFI>=15ORQ3<=1THEN667
665 M$="YOU CAN'T CAST A SPELL NOW!":GOSUB1:GOTO609
667 M$="WHICH SPELL (WEB, FIREBALL, DEATHSPELL)?":GOSUB7
669 IFO$<>"W"THEN673
671 ST=ST-1:WC=RND(8)+1:ON (1-(ST<1)) GOTO711,749
673 IFO$<>"F"THEN679
675 Q=RND(7)+RND(7):ST=ST-1:I=I-1:IF(I<1)OR(ST<1)THEN749
677 M$="IT DOES"+STR$(Q)+" POINTS WORTH OF DAMAGE.":GOSUB1:Q2=Q2-Q:GOTO643
679 IFO$="D"THEN683
681 M$="TRY ONE OF THE OPTIONS GIVEN.":GOSUB1:GOTO609
683 PRINT"DEATH... ";
685 IFI<RND(4)+15THENPRINT"YOURS!":I=0:GOTO749
687 PRINT"HIS!":Q2=0:GOTO645
689 IFO$="B"ANDQ3<=1THEN693
691 GOTO681
693 IFTC<>0THEN699
695 PRINT"ALL I WANT IS YOUR LIFE!"
697 GOTO711
699 Q=RND(8):IFT(Q)=0THEN699
701 C1=Q+25:GOSUB34:M$="I WANT "+C$+". WILL YOU GIVE IT TO ME?":GOSUB7
703 IFO$="N"THEN711
705 IFO$<>"Y"THENPRINTY$:GOTO701
707 T(Q)=0:TC=TC-1:PRINT"OK, JUST DON'T TELL ANYONE ELSE."
709 N=Z:GOSUB31:VF=VF+(PEEK(L)=25):GOTO135
711 Q3=2:IFWC<=0THEN715
713 WC=WC-1:IF WC=0THENPRINT"THE WEB JUST BROKE!"
715 C1=A+12:GOSUB34:Z$=RIGHT$(C$,LEN(C$)-2):IFLEFT$(Z$,1)=" "THENZ$=MID$(Z$,2)
717 IFWC<=0THEN721
719 M$="THE "+Z$+" IS STUCK AND CAN'T ATTACK NOW!":GOSUB1:GOTO729
721 M$="THE "+Z$+" ATTACKS!":GOSUB1
723 IFDX<RND(7)+RND(7)+RND(7)+3*BLTHEN727
725 PRINT"WHAT LUCK, HE MISSED YOU!":GOTO729
727 PRINT"OUCH! HE HIT YOU!":Q=Q1:GOSUB739:IF ST<1 GOTO749
729 IFO$<>"R"THEN609
731 PRINT"YOU HAVE ESCAPED!"
733 M$="DO YOU WANT TO GO NORTH, SOUTH, EAST, OR WEST?":GOSUB1:GOSUB833:PRINT
735 IFO$="N"ORO$="S"ORO$="E"ORO$="W"THEN277
737 M$="DON'T PRESS YOUR LUCK, "+R$(RC)+"!":GOSUB1:GOTO733
739 IFAV=0THEN747
741 Q=Q-AV:AH=AH-AV:IFQ<0THENAH=AH-Q:Q=0
743 IFAH>=0THEN747
745 AH=0:AV=0:M$="YOUR ARMOR HAS BEEN DESTROYED... GOOD LUCK!":GOSUB1
747 ST=ST-Q:RETURN
749 GOSUB831:M$="A NOBLE EFFORT, OH FORMERLY LIVING "+R$(RC)+"!":GOSUB1
753 PRINT"YOU DIED DUE TO LACK OF ":IFST<1THENPRINT"STRENGTH."
755 IFI<1THENPRINT"INTELLIGENCE."
757 IFDX<1THENPRINT"DEXTERITY."
759 Q3=1:PRINT"AT THE TIME YOU DIED, YOU HAD:":GOTO777
761 Q3=0:M$="YOU LEFT THE CASTLE WITH":IFOF=0THENM$=M$+"OUT"
763 M$=M$+" THE ORB OF ZOT.":GOSUB1:IFOF=0THEN771
765 PRINT"AN INCREDIBLY GLORIOUS VICTORY!"
767 M$="IN ADDITION, YOU GOT OUT WITH THE FOLLOWING:":GOSUB1
769 GOTO775
771 PRINT"A STUPENDOUS DEFEAT!"
773 PRINT"WHEN YOU LEFT, YOU HAD:"
775 IFQ3=0THENPRINT"YOUR MISERABLE LIFE!"
777 FORQ=1TO8:IFT(Q)=1THENC1=Q+25:GOSUB34:PRINTC$
779 NEXTQ:M$=W$(WV+1)+" AND "+W$(AV+5):IFLF=1THENM$=M$+" AND A LAMP"
781 GOSUB1:M$="YOU ALSO HAD"+STR$(FL)+" FLARES AND"+STR$(G)+" GOLD PIECES"
783 IFRF=1THENM$=M$+" AND THE RUNESTAFF"
785 M$=M$+" AND IT TOOK YOU"T"TURNS!":GOSUB1
787 M$="ARE YOU FOOLISH ENOUGH TO WANT TO PLAY AGAIN?":GOSUB7
789 IFO$<>"Y"THEN795
791 PRINT"SOME "R$(RC)"S NEVER LEARN!":M$="RE-LOAD THE GAME AND TYPE 'RUN'":GOSUB1:NEW:END
795 M$="MAYBE DUMB "+R$(RC)+" IS NOT SO DUMB AFTER ALL!":GOSUB1:GOTO863
799 PRINT"YOU JUST FOUND THE ORB OF ZOT!"
803 PRINT"THE RUNESTAFF HAS DISAPPEARED!":RF=0:OF=1:O(1)=0:GOTO387
805 DATAAN EMPTY ROOM,.,THE ENTRANCE,E,STAIRS GOING UP,U,STAIRS GOING DOWN,D,A POOL,P,A CHEST,C,GOLD PIECES,G
807 DATAFLARES,F,A WARP,W,A SINKHOLE,S,A CRYSTAL ORB,O,A BOOK,B,A KOBOLD,M,AN ORC,M,A WOLF,M,A GOBLIN,M,AN OGRE,M
809 DATAA TROLL,M,A BEAR,M,A MINOTAUR,M,A GARGOYLE,M,A CHIMERA,M,A BALROG,M,A DRAGON,M,A VENDOR,V,THE RUBY RED,T
811 DATATHE NORN STONE,T,THE PALE PEARL,T,THE OPAL EYE,T,THE GREEN GEM,T,THE BLUE FLAME,T,THE PALANTIR,T
813 DATATHE SILMARIL,T,X,"?",NO WEAPON," SANDWICH",DAGGER," STEW",MACE," SOUP",SWORD," BURGER",NO ARMOR," ROAST"
815 DATALEATHER," FILET",CHAINMAIL," TACO",PLATE," PIE",HOBBIT,ELF,MAN,DWARF
817 X=RND(8):Y=RND(8):N=Z:GOSUB31:IFPEEK(L)<>101THEN817
819 N=Z:GOSUB31:POKEL,Q:PRINT".";:RETURN
821 PRINT"YOU GET ALL HIS WARES:":PRINT"PLATE ARMOR":AV=3:AH=21
823 PRINT"A SWORD":WV=3:PRINT"A STRENGTH POTION":N=(ST+RND(6)):GOSUB29:ST=N
825 PRINT"AN INTELLIGENCE POTION":N=(I+RND(6)):GOSUB29:I=N:PRINT"A DEXTERITY POTION"
827 N=(DX+RND(6)):GOSUB29:DX=N:IFLF=0THENPRINT"A LAMP":LF=1
829 GOTO655
831 FORC1=1TO32:PRINT"*";:NEXT:RETURN
833 PRINT"YOUR CHOICE? ";
835 O$=INKEY$:IFO$=""THENPRINT"!";:GOSUB838:PRINT"/";:GOSUB838:PRINT"-";:GOSUB838:PRINTCHR$(92);:GOSUB838:GOTO835
837 PRINTO$;:RETURN
838 FORC1=1TO30:NEXT:PRINTCHR$(8);:RETURN
839 M$="HOW MANY POINTS DO YOU WISH TO ADD TO YOUR "+Z$+"?":GOSUB7
841 Q=VAL(O$):IFQ=0ANDASC(O$)<>48THENQ=-1
843 IF(Q<0)OR(Q>OT)OR(Q<>INT(Q))THEN839
845 OT=OT-Q:RETURN
847 PRINTZ$;:INPUTO$:Q=INT(VAL(O$)):IFQ>0ANDQ<9THENRETURN
849 GOTO847
851 M$="DO YOU WANT TO BUY A POTION OF "+Z$+" FOR 1000 GP'S":GOSUB1:GOTO833:PRINT
855 M$="YOUR "+Z$+" IS NOW"+STR$(Q):GOSUB1:RETURN
857 M$="THESE ARE THE TYPES OF "+Z$+" YOU CAN BUY:":GOSUB1:RETURN
859 M$="YOU ARE AT "
861 M$=M$+"("+MID$(STR$(X),2)+","+MID$(STR$(Y),2)+") LEVEL"+STR$(Z):GOSUB1:RETURN
863 NEW:END
865 PRINT@504,CHR$(91)"ENTER"CHR$(93);
867 IFINKEY$=""THEN867
869 CLS:RETURN
