20 CLS
30 DIM C$(34),I$(34),R$(4),W$(8),E$(8)
40 DIM L(512),C(3,4),T(8),O(3),R(3)
45 GOTO70
50 N=N+8*((N=9)-(N=0)):RETURN
55 N=-N*(N<19)-18*(N>18):RETURN
60 N=64*(N-1)+8*(X-1)+Y:RETURN
65 E=E+100*(E>99):RETURN
70 Y$="** PLEASE ANSWER YES OR NO":NG=0
90 PRINT"HIT ANY KEY"
100 I$=INKEY$:Q=RND(1000):IFI$=""THEN100
110 CLS:NG=NG+1:Q=RND(0):RESTORE:FOR Q=1 TO 34:READC$(Q),I$(Q):NEXT
120 FOR Q=1 TO 512:L(Q)=101:NEXT:FOR Q=1 TO 8:READW$(Q),E$(Q):NEXT
130 FOR Q=1 TO 4:READ R$(Q):NEXT:IFNG>1THEN160
140 PRINT"* * * THE WIZARD'S CASTLE * * *":GOSUB4360
150 PRINT"HOLD ON WHILE I SET UP THE GAME."
160 X=1:Y=4:N=1:GOSUB60:L(N)=2:FORZ=1TO7:FORQ1=1TO2:Q=104:GOSUB4290:PRINT".";
170 N=Z+1:GOSUB60:L(N)=103:NEXTQ1,Z:FORZ=1TO8:FORQ=113TO124:GOSUB4290
180 PRINT".";
190 NEXT:FORQ1=1TO3:FORQ=105TO112:GOSUB4290:NEXT:Q=125:GOSUB4290
200 PRINT".";
210 NEXTQ1,Z:FORQ=126TO133:Z=RND(8):GOSUB4290:NEXT:Q=101
220 FORA=1TO3:Z=RND(8):GOSUB4290:C(A,1)=X:C(A,2)=Y:C(A,3)=Z:C(A,4)=0:NEXT
230 RC=0:ST=2:DX=14:R$(3)="MAN":Q=112+RND(12):Z=RND(8):GOSUB4290:R(1)=X:R(2)=Y
240 R(3)=Z:Q=109:Z=RND(8):GOSUB4290:O(1)=X:O(2)=Y:O(3)=Z
250 BF=0:OT=8:AV=0:HT=0:T=1:VF=0:LF=0
260 TC=0:GP=60:RF=0:OF=0:BL=0:IQ=8:SX=0:FORQ=1TO8:T(Q)=0:NEXT
270 SOUND200,1:CLS:PRINT"ALL RIGHT, BOLD ONE."
280 PRINT"YOU MAY BE AN ELF, DWARF, MAN,   OR HOBBIT.":GOSUB4370:FOR Q=1 TO 4
290 IFLEFT$(R$(Q),1)=O$THENRC=Q:ST=ST+2*Q:DX=DX-2*Q
300 NEXT Q:PRINT:OT=OT+4*(RC=1):IFRC>0THENR$(3)="HUMAN":GOTO320
310 PRINT"** THAT WAS INCORRECT. PLEASE    TYPE E, D, M, OR H.":GOTO280
320 PRINT"WHICH SEX DO YOU PREFER";:GOSUB4380:IFO$="M"THENSX=1:GOTO350
330 IFO$="F"THEN350
340 PRINT"** CUTE "R$(RC)",":PRINT" REAL CUTE. TRY M OR F.":GOTO320
350 CLS:PRINT"OK, "R$(RC)", YOU":PRINT" HAVE THE FOLLOWING ATTRIBUTES:"
360 PRINT"STRENGTH:"ST:PRINT"INTELLIGENCE:"IQ:PRINT"DEXTERITY:"DX:PRINT
370 PRINT"AND"OT"OTHER POINTS":PRINT" TO ALLOCATE AS YOU WISH.":Z$="STRENGTH":PRINT
380 GOSUB4410:ST=ST+Q:IFOT=0THEN410
390 Z$="INTELLIGENCE":GOSUB4410:IQ=IQ+Q:IFOT=0THEN410
400 Z$="DEXTERITY":GOSUB4410:DX=DX+Q
410 CLS:PRINT"OK, "R$(RC)", YOU":PRINT" HAVE 60 GOLD PIECES (GP'S)."
420 Z$="ARMOR":GOSUB4500:AV=0:WV=0:FL=0:WC=0
430 PRINT"PLATE<30>,  CHAINMAIL<20>":PRINT"LEATHER<10>, NOTHING<0>":GOSUB4370
440 IFO$="N"THEN470
450 AV=-3*(O$="P")-2*(O$="C")-(O$="L"):IFAV>0THEN470
460 PRINT:PRINT"** ARE YOU A "R$(RC)" OR "C$(RND(12)+12)"?":GOTO420
470 AH=AV*7:GP=GP-AV*10:PRINT
480 CLS:PRINT"OK, BOLD "R$(RC)", YOU":PRINT" HAVE"GP"GP'S LEFT."
490 Z$="WEAPONS":GOSUB4500:PRINT"SWORD<30> MACE<20> DAGGER<10>   NOTHING<0>"
500 GOSUB4370:IFO$="N"THEN530
510 WV=-3*(O$="S")-2*(O$="M")-(O$="D"):IFWV>0THEN530
520 PRINT:PRINT"** IS YOUR IQ REALLY"IQ"?":GOTO490
530 GP=GP-WV*10:IFGP<20THEN580
540 CLS
550 PRINT"DO YOU WANT TO BUY A LAMP FOR 20 GP'S";:GOSUB4380
560 IFO$="Y"THENLF=1:GP=GP-20:GOTO580
570 IFO$<>"N"THENPRINT:PRINT Y$:PRINT:GOTO550
580 PRINT:IFGP<1THENQ=0:GOTO650
590 PRINT"OK, "R$(RC)",":PRINT" YOU HAVE"GP"GOLD PIECES LEFT.":PRINT
600 INPUT"FLARES COST 1 GP EACH. HOW MANY  DO YOU WANT";O$:Q=VAL(O$):PRINT
610 IF(Q>0)OR(ASC(O$)=48)THEN640
620 PRINT"** IF YOU DON'T WANT ANY, JUST TYPE 0 (ZERO)."
630 PRINT:GOTO600
640 IFQ>GPTHENPRINT"** YOU CAN ONLY AFFORD"GP".":PRINT:GOTO600
650 FL=FL+Q:GP=GP-Q:X=1:Y=4:Z=1
660 CLS:PRINT"OK, "R$(RC)", YOU":PRINT" ARE NOW ENTERING THE CASTLE!":GOTO2370
700 T=T+1:IFRF+OF>0THEN780
710 IFC(1,4)>T(1)THENT=T+1
720 IFC(2,4)>T(3)THENGP=GP-RND(5)
730 IFGP<0THENGP=0
740 IFC(3,4)<=T(5)THEN780
750 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB60:E=L(N):GOSUB65:L(RND(Z))=E+100
760 X=A:Y=B:Z=C:N=Z:GOSUB60:IFL(N)<>1THEN780
770 FORQ=1TO3:C(Q,4)=-(C(Q,1)=X)*(C(Q,2)=Y)*(C(Q,3)=Z):NEXT
780 IFRND(5)>1THEN930
790 PRINT:PRINT"YOU ";:Q=RND(7)+BL:IFQ>7THENQ=4
800 ONQGOSUB890,830,880,820,900,910,920
810 GOTO930
820 PRINT"STEPPED ON A FROG!":RETURN
830 PRINT"HEAR ";:ONRND(4)GOTO840,850,860,870
840 PRINT"A SCREAM!":RETURN
850 PRINT"FOOTSTEPS!":RETURN
860 PRINT"A WUMPUS!":RETURN
870 PRINT"THUNDER!":RETURN
880 PRINT"SNEEZED!":RETURN
890 PRINT"SEE A BAT FLY BY!":RETURN
900 PRINT"SMELL "C$(12+RND(13))" FRYING!":RETURN
910 PRINT"FEEL LIKE YOU'RE BEING WATCHED!":RETURN
920 PRINT"HEAR FAINT RUSTLING NOISES!":RETURN
930 IFBL+T(4)<>2THEN950
940 PRINT:PRINT C$(29)" CURES YOUR BLINDNESS!":BL=0
950 IFBF+T(6)<>2THEN970
960 PRINT:PRINT C$(31)" DISSOLVES THE BOOK!":BF=0
970 PRINT:PRINT"ENTER YOUR COMMAND: ";:GOSUB4400
980 IFO$="N"THENPRINT"ORTH":GOTO1370
990 IFO$="S"THENPRINT"OUTH":GOTO1380
1000 IFO$="E"THENPRINT"AST":GOTO1380
1010 IFO$="W"THENPRINT"EST":GOTO1380
1020 IFO$="U"THENPRINT"P":GOTO1390
1030 IFO$<>"D"THEN1060
1040 I$=INKEY$:IFI$=""THEN1040
1041 O$=O$+I$
1050 IFO$="DR"THENPRINT"RINK":GOTO1780
1051 PRINT"OWN":GOTO1410
1060 IFO$="M"THENPRINT"AP":GOTO1430
1070 IFO$="F"THENPRINT"LARE":ON BL+1 GOTO1530,1430
1080 IFO$="L"THENPRINT"AMP":ON BL+1 GOTO1660,1430
1090 IFO$="O"THENPRINT"PEN":GOTO1930
1100 IFO$="G"THENPRINT"AZE":ON BL+1 GOTO2130,1430
1110 IFO$="T"THENPRINT"ELEPORT":PRINT:ON RF+1 GOTO2260,2280
1120 IFO$="Q"THENPRINT"UIT":GOTO2300
1130 IFO$<>"H"THEN1360
1140 CLS
1150 PRINT"* WIZARD'S CASTLE INSTRUCTIONS *"
1160 PRINT"COMMANDS:":PRINT
1170 PRINT"H/ELP     N/ORTH    S/OUTH      E/AST     W/EST     U/P"
1180 PRINT"D/OWN     DR/INK    M/AP        F/LARE    L/AMP     O/PEN"
1190 PRINT"G/AZE     T/ELEPORT Q/UIT":GOSUB4530
1200 PRINT"CONTENTS OF ROOMS:":PRINT
1210 PRINT". = EMPTY ROOM  B = BOOK"
1220 PRINT"C = CHEST       D = STAIRS DOWN"
1230 PRINT"W = WARP/ORB    F = FLARES"
1240 PRINT"M = MONSTER     G = GOLD PIECES"
1250 PRINT"P = MAGIC POOL  O = CRYSTAL ORB"
1260 PRINT"S = SINKHOLE    T = TREASURE"
1270 PRINT"U = STAIRS UP   V = VENDOR"
1280 PRINT"E = ENTRANCE / EXIT":GOSUB4530
1290 PRINT"THE BENEFITS OF HAVING TREASURES ARE:":PRINT
1300 PRINT"RUBY RED - AVOID LETHARGY":PRINT"PALE PEARL - AVOID LEECH"
1310 PRINT"GREEN GEM - AVOID FORGETTING":PRINT"OPAL EYE - CURES BLINDNESS"
1320 PRINT"BLUE FLAME - DISSOLVES BOOKS":PRINT"NORN STONE - NO BENEFIT"
1330 PRINT"PALANTIR - NO BENEFIT":PRINT"SILMARIL - NO BENEFIT":PRINT
1340 GOSUB4530
1350 GOTO700
1360 PRINT:PRINT"** SILLY "R$(RC)", THAT":PRINT" WASN'T A VALID COMMAND!":GOTO700
1370 N=Z:GOSUB60:IFL(N)=2THEN3950
1380 X=X+(O$="N")-(O$="S"):Y=Y+(O$="W")-(O$="E"):N=X:GOSUB50:X=N:N=Y:GOSUB50:Y=N:GOTO2370
1390 N=Z:GOSUB60:IFL(N)=3THENZ=Z-1:GOTO2370
1400 Z$="UP":GOTO1420
1410 Z$="DOWN":N=Z:GOSUB60:IFL(N)=4THENZ=Z+1:GOTO2370
1420 PRINT:PRINT"** THERE ARE NO STAIRS GOING":PRINT Z$" FROM HERE!":GOTO700
1430 IFBL<>1THEN1480
1440 PRINT:PRINT"** YOU CAN'T SEE ANYTHING, YOU  DUMB "R$(RC)"!":GOTO700
1480 CLS:A=X:B=Y:FOR X=1 TO 8:FOR Y=1 TO 8:N=Z:GOSUB60:Q=L(N)
1490 IFQ>99THENQ=Q-100
1500 IFX=AANDY=BTHENPRINT"<"I$(Q)"> ";:GOTO1510
1501 PRINT" "I$(Q)"  ";
1510 NEXTY,X:X=A:Y=B:GOTO1620
1520 PRINT") LEVEL"Z:GOTO700
1530 IFFL<>0THEN1580
1540 PRINT"** HEY, BRIGHT ONE, YOU'RE OUT  OF FLARES!":GOTO700
1580 PRINT:FL=FL-1:A=X:B=Y:FOR Q1=A-1 TO A+1:N=Q1:GOSUB50:X=N:FOR Q2=B-1 TO B+1
1590 N=Q2:GOSUB50:Y=N:N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q:PRINT" "I$(Q)"   ";:NEXT Q2:PRINT
1600 PRINT:NEXT Q1:X=A:Y=B
1620 GOSUB4510:GOTO700
1660 IFLF<>0THEN1680
1670 PRINT:PRINT"** YOU DON'T HAVE A LAMP, "R$(RC)"!":GOTO700
1680 PRINT:PRINT"WHERE DO YOU WANT TO SHINE THE   LAMP (N,S,E,W)";:GOSUB4380
1690 A=X:B=Y:N=(X+(O$="N")-(O$="S")):GOSUB50:X=N:N=(Y+(O$="W")-(O$="E")):GOSUB50:Y=N
1700 IFA-X+B-Y<>0THEN1720
1710 PRINT:PRINT"** THAT'S NOT A DIRECTION,":PRINT R$(RC)"!":GOTO700
1720 PRINT:PRINT"THE LAMP SHINES INTO ("X","Y")":PRINT" LEVEL"Z".":PRINT
1730 N=Z:GOSUB60:E=L(N):GOSUB65:L(N)=E:PRINT"THERE YOU WILL FIND "C$(L(N))"."
1740 X=A:Y=B:GOTO700
1780 N=Z:GOSUB60:IFL(N)=5THEN1810
1790 PRINT:PRINT"** IF YOU WANT A DRINK, FIND A  POOL!"
1800 GOTO700
1810 Q=RND(8):PRINT:PRINT"YOU TAKE A DRINK AND ":IFQ<7THENPRINT"FEEL ";
1820 ONQGOTO1830,1840,1850,1860,1870,1880,1890,1910
1830 N=(ST+RND(3)):GOSUB55:ST=N:PRINT"STRONGER.":GOTO700
1840 ST=ST-RND(3):PRINT"WEAKER.":ON(1-(ST<1))GOTO700,3890
1850 N=(IQ+RND(3)):GOSUB55:IQ=N:PRINT"SMARTER.":GOTO700
1860 IQ=IQ-RND(3):PRINT"DUMBER.":ON(1-(IQ<1))GOTO700,3890
1870 N=(DX+RND(3)):GOSUB55:DX=N:PRINT"NIMBLER.":GOTO700
1880 DX=DX-RND(3):PRINT"CLUMSIER.":ON(1-(DX<1))GOTO700,3890
1890 Q=RND(4):IFQ=RCTHEN1890
1900 RC=Q:PRINT"BECOME A "R$(RC)".":GOTO700
1910 SX=1-SX:PRINT"TURN INTO A ";:IFSX=0THENPRINT"FE";
1920 PRINT"MALE "R$(RC)"!":GOTO700
1930 N=Z:GOSUB60:IFL(N)<>6THEN1950
1940 PRINT:PRINT"YOU OPEN THE CHEST AND":GOTO2070
1950 N=Z:GOSUB60:IFL(N)<>12THEN1970
1960 PRINT:PRINT"YOU OPEN THE BOOK AND":GOTO1980
1970 PRINT:PRINT"** THE ONLY THING OPENED WAS    YOUR BIG MOUTH!":GOTO700
1980 ONRND(6)GOTO1990,2000,2010,2020,2030,2040
1990 PRINT"FLASH! OH NO! YOU ARE NOW A":PRINT" BLIND "R$(RC)"!":BL=1:GOTO2060
2000 PRINT"IT'S ANOTHER VOLUME OF ZOT'S    POETRY! - YECH!!":GOTO2060
2010 PRINT"IT'S AN OLD COPY OF PLAY"R$(RND(4))"!":GOTO2060
2020 PRINT"IT'S A MANUAL OF DEXTERITY!":DX=18:GOTO2060
2030 PRINT"IT'S A MANUAL OF STRENGTH!":ST=18:GOTO2060
2040 PRINT"THE BOOK STICKS TO YOUR HANDS -"
2050 PRINT"NOW YOU ARE UNABLE TO DRAW YOUR WEAPON!":BF=1
2060 N=Z:GOSUB60:L(N)=1:GOTO700
2070 ONRND(4)GOTO2080,2100,2110,2100
2080 PRINT"KABOOM! IT EXPLODES!!":Q=RND(6):GOSUB3840
2090 ON(1-(ST<1))GOTO2060,3890
2100 Q=RND(1000):PRINT"FIND"Q"GOLD PIECES!":GP=GP+Q:GOTO2060
2110 PRINT"GAS!! YOU STAGGER FROM THE ROOM!":N=Z:GOSUB60:L(N)=1:T=T+20
2120 O$=MID$("NSEW",RND(4),1):GOTO1380
2130 N=Z:GOSUB60:IFL(N)=11THEN2150
2140 PRINT:PRINT"** IT'S HARD TO GAZE WITHOUT AN ORB!":GOTO700
2150 PRINT:PRINT"YOU SEE ";:ON RND(6) GOTO2160,2180,2200,2210,2230,2250
2160 PRINT"YOURSELF IN A BLOODY HEAP!"
2170 ST=ST-RND(2):ON(1-(ST<1))GOTO700,3890
2180 PRINT"YOURSELF DRINKING FROM A POOL   AND BECOMING "C$(12+RND(13))"!"
2190 GOTO700
2200 PRINTC$(12+RND(13))" GAZING BACK AT YOU!":GOTO700
2210 A=X:B=Y:C=Z:X=RND(8):Y=RND(8):Z=RND(8):N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q
2220 PRINT C$(Q)" AT ("X","Y") LEVEL"Z".":X=A:Y=B:Z=C:GOTO700
2230 A=RND(8):B=RND(8):C=RND(8):IFRND(8)<4THEN A=O(1):B=O(2):C=O(3)
2240 PRINT"***THE ORB OF ZOT***":PRINT" AT ("A","B") LEVEL"C"!":GOTO700
2250 PRINT"A SOAP OPERA RERUN!":GOTO700
2260 IFRF<>0THEN2280
2270 PRINT:PRINT"** YOU CAN'T TELEPORT WITHOUT   THE RUNESTAFF!":GOTO700
2280 Z$="X-COORDINATE":GOSUB4450:X=Q:Z$="Y-COORDINATE":GOSUB4450:Y=Q
2290 Z$="Z-COORDINATE":GOSUB4450:Z=Q:O$="T":GOTO2370
2300 PRINT:PRINT"ARE YOU SURE":GOSUB4380:PRINT
2310 IFO$="Y"THEN2330
2320 PRINT"** THEN DON'T SAY THAT YOU DO!":GOTO700
2330 PRINT:GOTO4000
2370 CLS:IFBL=0THENGOSUB4510
2380 PRINT"STRENGTH:"ST;TAB(17)"DEXTERITY:"DX:PRINT"INTELLIGENCE:"IQ;TAB(17)"GOLD:"GP
2390 PRINT"TREASURES:"TC;TAB(17)"FLARES:"FL
2400 PRINT"WEAPON = "W$(WV+1):PRINT"ARMOR = "W$(AV+5)
2410 IFLF=1THENPRINT"  AND A LAMP";
2420 PRINT:WC=0:N=Z:GOSUB60:E=L(N):GOSUB65:Q=E:L(N)=Q:Z$="YOU NOW HAVE":PRINT
2430 PRINT"HERE YOU FIND "C$(Q)".":IF(Q<7)OR(Q=11)OR(Q=12)THEN700
2440 IFQ=7THENGP=GP+RND(50):PRINT Z$GP".":GOTO2060
2450 IFQ=8THENFL=FL+RND(5):PRINT Z$FL".":GOTO2060
2460 IFQ>9THEN2490
2470 IF(O(1)=X)AND(O(2)=Y)AND(O(3)=Z)THENON(1-(O$="T"))GOTO1380,4140
2480 X=RND(8):Y=RND(8):Z=RND(8):GOTO2370
2490 IFQ=10THENN=Z+1:GOSUB50:Z=N:GOTO2370
2500 IFQ<=25ORQ>=34THEN2520
2510 PRINT:PRINT"IT'S NOW YOURS!":T(Q-25)=1:TC=TC+1:GOTO2060
2520 N=Z:GOSUB60:A=L(N)-12:WC=0:IF(A<13)OR(VF=1)THEN3170
2530 PRINT:PRINT"YOU MAY TRADE WITH, ATTACK, OR  IGNORE THE VENDOR.":GOSUB4370
2540 IFO$="I"THEN700
2550 IFO$<>"A"THEN2570
2560 VF=1:PRINT:PRINT"YOU'LL BE SORRY THAT YOU DID    THAT!":GOTO3170
2570 IFO$="T"THEN2590
2580 PRINT:PRINT"** NICE SHOT, "R$(RC)"!":GOTO2530
2590 FORQ=1TO8:A=RND(Q*1500):IFT(Q)=0THEN2630
2600 PRINT:PRINT"DO YOU WANT TO SELL ":PRINTC$(Q+25)" FOR"A"GP'S";:GOSUB4380
2610 IFO$="Y"THENTC=TC-1:T(Q)=0:GP=GP+A:GOTO2630
2620 IFO$<>"N"THENPRINTY$:GOTO2600
2630 NEXTQ:IFGP>=1000THEN2650
2640 PRINT:PRINT"YOU'RE TOO POOR TO TRADE, "R$(RC)".":GOTO700
2650 IFGP<1250THEN2960
2660 IFAV=3THEN2800
2670 PRINT:PRINT"OK, "R$(RC)", YOU HAVE"GP"GP'S":PRINT"AND ";
2671 IFLEFT$(W$(AV+5),2)="NO" THEN PRINTW$(AV+5)".":GOTO2680
2672 PRINTW$(AV+5)" ARMOR."
2680 PRINT:Z$="ARMOR":GOSUB4500:PRINT"NOTHING<0> LEATHER<1250> "
2690 IFGP>1499THENPRINT"CHAINMAIL<1500> ";
2700 IFGP>1999THENPRINT"PLATE<2000>"
2710 PRINT:GOSUB4370:PRINT:IFO$="N"THEN2800
2720 IFO$="L"THENGP=GP-1250:AV=1:AH=7:GOTO2800
2730 IFO$<>"C"ORGP>=1500THEN2750
2740 PRINT"** YOU HAVEN'T GOT THAT MUCH    CASH ON HAND!":GOTO2680
2750 IFO$="C"THENGP=GP-1500:AV=2:AH=14:GOTO2800
2760 IFO$<>"P"ORGP>=2000THEN2780
2770 PRINT"** YOU CAN'T AFFORD PLATE ARMOR!":GOTO2680
2780 IFO$="P"THEN GP=GP-2000:AV=3:AH=21:GOTO2800
2790 PRINT:PRINT"** DON'T BE SILLY. CHOOSE A     SELECTION.":GOTO2710
2800 IFGP<1250THEN2960
2810 IFWV=3THEN2960
2820 PRINT:PRINT"YOU HAVE"GP"GP'S LEFT WITH":PRINT W$(WV+1)" IN HAND."
2830 PRINT:Z$="WEAPON":GOSUB4500:PRINT"NOTHING<0> DAGGER<1250> "
2840 IFGP>1499THENPRINT"MACE<1500> ";
2850 IFGP>1999THENPRINT"SWORD<2000>"
2860 PRINT:GOSUB4370:PRINT:IFO$="N"GOTO2960
2870 IFO$="D"THEN GP=GP-1250:WV=1:GOTO2960
2880 IFO$<>"M"ORGP>=1500THEN2900
2890 PRINT"** SORRY SIR, I'M AFRAID I      DON'T GIVE CREDIT!":GOTO2830
2900 IFO$="M"THENGP=GP-1500:WV=2:GOTO2960
2910 IFO$<>"S"ORGP>=2000THEN2940
2920 PRINT"** YOUR DUNGEON EXPRESS CARD -  YOU LEFT HOME WITHOUT IT!"
2930 GOTO2830
2940 IFO$="S"THENGP=GP-2000:WV=3:GOTO2960
2950 PRINT"** TRY CHOOSING A SELECTION!":GOTO2860
2960 IFGP<1000THEN700
2970 IFST>17THEN3010
2980 Z$="STRENGTH":GOSUB4470:IFO$<>"Y"THEN3000
2990 GP=GP-1000:N=(ST+RND(6)):GOSUB55:ST=N:Q=ST:GOSUB4490:GOTO2960
3000 IFO$<>"N"THENPRINTY$:GOTO2980
3010 IFGP<1000THEN700
3020 IFIQ>17THEN3060
3030 Z$="INTELLIGENCE":GOSUB4470:IFO$<>"Y"THEN3050
3040 GP=GP-1000:N=(IQ+RND(6)):GOSUB55:IQ=N:Q=IQ:GOSUB4490:GOTO3010
3050 IFO$<>"N"THENPRINTY$:GOTO3030
3060 IFGP<1000THEN700
3070 IFDX>17THEN3110
3080 Z$="DEXTERITY":GOSUB4470:IFO$<>"Y"THEN3100
3090 GP=GP-1000:N=(DX+RND(6)):GOSUB55:DX=N:Q=DX:GOSUB4490:GOTO3060
3100 IFO$<>"N"THENPRINTY$:GOTO3080
3110 IF(GP<1000)OR(LF=1)THEN700
3120 PRINT:PRINT"DO YOU WANT TO BUY A LAMP FOR   1000 GP'S";:GOSUB4380
3130 IFO$<>"Y"THEN3150
3140 GP=GP-1000:LF=1:PRINT:PRINT"IT'S GUARANTEED TO OUTLIVE YOU!":GOTO700
3150 IFO$<>"N"THENPRINTY$:GOTO3120
3160 GOTO700
3170 Q1=1+INT(A/2):Q2=A+2:Q3=1
3180 IF(C(1,4)>T(1))OR(BL=1)OR(DX<RND(9)+RND(9))THEN3700
3190 PRINT:PRINT"YOU'RE FACING "C$(A+12)"!":PRINT
3200 PRINT"YOU MAY ATTACK OR RETREAT."
3210 IFQ3=1THENPRINT"YOU CAN ALSO ATTEMPT A BRIBE."
3220 IFIQ>14THENPRINT"YOU CAN ALSO CAST A SPELL."
3230 PRINT:PRINT"YOUR STRENGTH IS"ST:PRINT"YOUR DEXTERITY IS"DX".":GOSUB4370
3240 IFO$<>"A"THEN3440
3250 IFWV<>0THEN3270
3260 PRINT:PRINT"** POUNDING ON "C$(A+12):PRINT" WON'T HURT IT!":GOTO3700
3270 IFBF<>1THEN3290
3280 PRINT:PRINT"** YOU CAN'T BEAT IT TO DEATH   WITH A BOOK!":GOTO3700
3290 IFDX>=RND(20)+(3*BL)THEN3310
3300 PRINT:PRINT"YOU MISSED, TOO BAD!":GOTO3700
3310 Z$=RIGHT$(C$(A+12),LEN(C$(A+12))-2):IFLEFT$(Z$,1)=" "THENZ$=MID$(Z$,2)
3320 PRINT:PRINT"YOU HIT THE EVIL "Z$"!":Q2=Q2-WV
3330 IF(A<>9ANDA<>12)THEN3360
3340 IF RND(8)<>1 GOTO3360
3350 PRINT:PRINT"OH NO! YOUR "W$(WV+1)" BROKE!":WV=0
3360 IF Q2>0 GOTO3700
3370 PRINT:MC=MC-1:PRINTC$(A+12)" LIES DEAD AT YOUR FEET!"
3380 IF H>T-60 GOTO3400
3390 PRINT:PRINT"YOU SPEND AN HOUR EATING "C$(A+12)E$(RND(8))".":H=T
3400 IFX<>R(1)ORY<>R(2)ORZ<>R(3)THENON(1-(A=13))GOTO3420,4310
3410 PRINT:PRINT"GREAT ZOT! YOU'VE FOUND THE     RUNESTAFF!":SOUND100,1:R(1)=0:RF=1
3420 Q=RND(1000):PRINT:PRINT"YOU NOW GET HIS HOARD":PRINT" OF"Q"GP'S!":GP=GP+Q
3430 GOTO2060
3440 IFO$="R"THEN3700
3450 IFO$<>"C"THEN3590
3460 IFIQ>=15ORQ3<=1THEN3480
3470 PRINT:PRINT"** YOU CAN'T CAST A SPELL NOW!":GOTO3190
3480 PRINT:PRINT"WHICH SPELL (WEB, FIREBALL,      DEATHSPELL)";:GOSUB4380:PRINT
3490 IFO$<>"W"THEN3510
3500 ST=ST-1:WC=RND(8)+1:ON (1-(ST<1)) GOTO3700,3890
3510 IFO$<>"F"THEN3540
3520 Q=RND(7)+RND(7):ST=ST-1:IQ=IQ-1:IF(IQ<1)OR(ST<1)THEN3890
3530 PRINT"IT DOES"Q"POINTS WORTH OF DAMAGE.":PRINT:Q2=Q2-Q:GOTO3360
3540 IFO$="D"THEN3560
3550 PRINT:PRINT"** TRY ONE OF THE OPTIONS GIVEN.":GOTO3190
3560 PRINT"DEATH . . . ";
3570 IFIQ<RND(4)+15THENPRINT"YOURS!":IQ=0:GOTO3890
3580 PRINT"HIS!":Q2=0:GOTO3370
3590 IFO$="B"ANDQ3<=1THEN3610
3600 PRINT:PRINT"** CHOOSE ONE OF THE OPTIONS    LISTED.":GOTO3190
3610 IFTC<>0THEN3640
3620 PRINT:PRINT"ALL I WANT IS YOUR LIFE!"
3630 GOTO3700
3640 Q=RND(8):IFT(Q)=0THEN3640
3650 PRINT:PRINT"I WANT "C$(Q+25)".":PRINT" WILL YOU GIVE IT TO ME";:GOSUB4380
3660 IFO$="N"THEN3700
3670 IFO$<>"Y"THENPRINTY$:GOTO3650
3680 T(Q)=0:TC=TC-1:PRINT:PRINT"OK, JUST DON'T TELL ANYONE ELSE."
3690 N=Z:GOSUB60:VF=VF+(L(N)=25):GOTO700
3700 Q3=2:IFWC<=0THEN3720
3710 WC=WC-1:IF WC=0THENPRINT:PRINT"THE WEB JUST BROKE!"
3720 Z$=RIGHT$(C$(A+12),LEN(C$(A+12))-2):IF LEFT$(Z$,1)=" "THEN Z$=MID$(Z$,2)
3730 IFWC<=0THEN3750
3740 PRINT:PRINT"THE "Z$" IS STUCK":PRINT" AND CAN'T ATTACK NOW!":GOTO3790
3750 PRINT:PRINT"THE "Z$" ATTACKS!"
3760 IF DX<RND(7)+RND(7)+RND(7)+3*BL GOTO3780
3770 PRINT:PRINT"WHAT LUCK, HE MISSED YOU!":GOTO3790
3780 PRINT:PRINT"OUCH! HE HIT YOU!":Q=Q1:GOSUB3840:IF ST<1 GOTO3890
3790 IFO$<>"R"THEN3190
3800 PRINT:PRINT"YOU HAVE ESCAPED!":PRINT
3810 PRINT"DO YOU WANT TO GO NORTH, SOUTH, EAST, OR WEST";:GOSUB4380
3820 IFO$="N"ORO$="S"ORO$="E"ORO$="W"THEN1380
3830 PRINT:PRINT"** DON'T PRESS YOUR LUCK, "R$(RC)"!":PRINT:GOTO3810
3840 IFAV=0THEN3880
3850 Q=Q-AV:AH=AH-AV:IFQ<0THENAH=AH-Q:Q=0
3860 IF AH>=0 GOTO3880
3870 AH=0:AV=0:PRINT:PRINT"YOUR ARMOR HAS BEEN DESTROYED . . . GOOD LUCK!"
3880 ST=ST-Q:RETURN
3890 SOUND200,30:GOSUB4360
3900 PRINT"A NOBLE EFFORT, OH FORMERLY     LIVING "R$(RC)"!":PRINT
3910 PRINT"YOU DIED DUE TO LACK OF ":IF ST<1 THEN PRINT"STRENGTH."
3920 IFIQ<1THENPRINT"INTELLIGENCE."
3930 IFDX<1THENPRINT"DEXTERITY."
3940 PRINT:Q3=1:PRINT"AT THE TIME YOU DIED, YOU HAD:":GOTO4030
3950 Q3=0:PRINT:PRINT"YOU LEFT THE CASTLE WITH";:IFOF=0THENPRINT"OUT";
3960 PRINT" THE ORB OF ZOT.":PRINT:IF OF=0 GOTO4000
3970 PRINT:PRINT"AN INCREDIBLY GLORIOUS VICTORY!!":PRINT
3980 PRINT"IN ADDITION, YOU GOT OUT WITH   THE FOLLOWING:"
3990 GOTO4020
4000 PRINT:PRINT"A STUPENDOUS DEFEAT!"
4010 PRINT"WHEN YOU LEFT, YOU HAD:"
4020 IFQ3=0THENPRINT"YOUR MISERABLE LIFE!"
4030 FORQ=1TO8:IF T(Q)=1 THEN PRINT C$(Q+25)
4040 NEXT Q:PRINTW$(WV+1)" AND "W$(AV+5);:IFLF=1THENPRINT" AND A LAMP";
4050 PRINT:PRINT"YOU ALSO HAD"FL"FLARES AND":PRINT GP"GOLD PIECES"
4060 IFRF=1THENPRINT"AND THE RUNESTAFF"
4070 PRINT:PRINT"AND IT TOOK YOU"T"TURNS!":PRINT
4080 PRINT"ARE YOU FOOLISH ENOUGH TO WANT   TO PLAY AGAIN";:GOSUB4380:PRINT
4090 IFO$<>"Y"THEN4120
4100 PRINT"SOME "R$(RC)"S NEVER LEARN!":PRINT
4110 PRINT"PLEASE BE PATIENT WHILE THE      CASTLE IS RESTOCKED.":PRINT:GOTO100
4120 IFO$<>"N"THENPRINTY$:GOTO4080
4130 PRINT"MAYBE DUMB "R$(RC):PRINT" IS NOT SO DUMB AFTER ALL!":PRINT:GOTO4520
4140 PRINT:PRINT"GREAT UNMITIGATED ZOT!":PRINT
4150 PRINT"YOU JUST FOUND *THE ORB OF ZOT*!"
4160 PRINT"THE RUNESTAFF HAS DISAPPEARED!":RF=0:OF=1:O(1)=0:GOTO2060
4170 DATA AN EMPTY ROOM,.,THE ENTRANCE,E,STAIRS GOING UP,U
4180 DATA STAIRS GOING DOWN,D,A POOL,P,A CHEST,C,GOLD PIECES,G
4190 DATA FLARES,F,A WARP,W,A SINKHOLE,S,A CRYSTAL ORB,O
4200 DATA A BOOK,B,A KOBOLD,M,AN ORC,M,A WOLF,M,A GOBLIN,M,AN OGRE,M
4210 DATA A TROLL,M,A BEAR,M,A MINOTAUR,M,A GARGOYLE,M,A CHIMERA,M
4220 DATA A BALROG,M,A DRAGON,M,A VENDOR,V,THE RUBY RED,T
4230 DATA THE NORN STONE,T,THE PALE PEARL,T,THE OPAL EYE,T
4240 DATA THE GREEN GEM,T,THE BLUE FLAME,T,THE PALANTIR,T,THE SILMARIL,T
4250 DATA X,"?",NO WEAPON," SANDWICH"
4260 DATA DAGGER," STEW",MACE," SOUP",SWORD," BURGER",NO ARMOR," ROAST"
4270 DATA LEATHER," FILET",CHAINMAIL," TACO",PLATE," PIE"
4280 DATA HOBBIT,ELF,MAN,DWARF
4290 X=RND(8):Y=RND(8):N=Z:GOSUB60:IFL(N)<>101THEN4290
4300 N=Z:GOSUB60:L(N)=Q:RETURN
4310 PRINT:PRINT"YOU GET ALL HIS WARES:":PRINT"PLATE ARMOR":AV=3:AH=21
4320 PRINT"A SWORD":WV=3:PRINT"A STRENGTH POTION":N=(ST+RND(6)):GOSUB55:ST=N
4330 PRINT"AN INTELLIGENCE POTION":N=(IQ+RND(6)):GOSUB55:IQ=N:PRINT"A DEXTERITY POTION"
4340 N=(DX+RND(6)):GOSUB55:DX=N:IF LF=0 THEN PRINT"A LAMP":LF=1
4350 GOTO3420
4360 PRINT"*******************************":PRINT:RETURN
4370 PRINT:PRINT"YOUR CHOICE";
4380 PRINT" ? ";
4390 O$=INKEY$:IF O$=""THEN4390
4391 PRINTO$:RETURN
4400 O$=INKEY$:IF O$=""THEN4400
4401 PRINT O$;:RETURN
4410 PRINT"HOW MANY POINTS DO YOU WISH TO   ADD TO YOUR "Z$;:GOSUB4380:PRINT
4420 Q=VAL(O$):IF Q=0 AND ASC(O$)<>48 THEN Q=-1
4430 IF(Q<0)OR(Q>OT)OR(Q<>INT(Q))THENPRINT"** ";:GOTO4410
4440 OT=OT-Q:RETURN:INPUT O$:Q=INT(VAL(O$)):RETURN
4450 PRINT:PRINT Z$;:INPUT O$:Q=INT(VAL(O$)):IF Q>0 AND Q<9 THEN RETURN
4460 PRINT:PRINT"** TRY A NUMBER FROM 1 TO 8.":GOTO4450
4470 PRINT:PRINT"DO YOU WANT TO BUY A POTION OF  "Z$" FOR 1000 GP'S";
4480 GOTO4380
4490 PRINT:PRINT"YOUR "Z$" IS NOW"Q".":RETURN
4500 PRINT:PRINT"THESE ARE THE TYPES OF ":PRINT Z$" YOU CAN BUY:":RETURN
4510 PRINT"YOU ARE AT ("X","Y") LEVEL"Z".":RETURN
4520 END
4530 PRINT@504,CHR$(91)"ENTER"CHR$(93);
4540 IFINKEY$=""THEN4540
4550 CLS:RETURN
