0 CLEAR300:CLS:GOTO20
10 C1=1:CC=32
11 IFMID$(W$,CC,1)<>""ANDMID$(W$,CC,1)<>" "THENCC=CC-1:GOTO11
12 PRINTMID$(W$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(W$)THEN11
13 RETURN
20 PRINT"GIANT MONSTER ATTACK***"
30 PRINT"BY LEE J. CHAPEL 5/1/1980"
40 PRINT"MC-10 VERSION BY JIM GERRIE"
50 PRINT"t=TREES ";CHR$(124);"=PATHS i=WALLS ";CHR$(126);"=INN"
55 PRINT"m=ENCHANTED CASTLES o=YOU"
60 DIM AAR(10,10),BAR(10,10),MAR(11),MAR$(11),NAR(11),T$(11),PAR(11),G$(3)
70 DIM CAR(15),DAR(15),GAM(5)
80 FOR I=1 TO 11:READ MAR$(I),T$(I),MAR(I),PAR(I):NEXT I
90 V=INT(RND(0)*3)
100 C=INT(RND(0)*1501)+500:S=INT(RND(0)*6):R=INT(RND(0)*4)
110 G$(1)="SLEEP SPELL":G$(2)="CHARM":G$(3)="INVISIBILITY SPELL"
120 PRINT:W$="DO YOU WISH TO USE THE STRENGTH AND MAGIC FROM A PREVIOUS GAME":GOSUB10:INPUT"(Y OR N)";X$:IF X$="Y" THEN 3210
140 D=C:V1=V:S1=S:R1=R:PRINT"PLEASE WAIT"
150 FOR I=1 TO 10:FOR J=1 TO 10
160 T=INT(RND(0)*10):IF T<>1 OR CS>14 THEN T=0
170 H=INT(RND(0)*2):W=INT(RND(0)*10):P=INT(RND(0)*51)
180 AAR(I,J)=10000*T+100*P+10*W+H
190 IF T=1 THEN CS=CS+1:CAR(CS)=I:DAR(CS)=J
200 NEXT J:NEXT I:T=0
210 X1=INT(RND(0)*8)+2:Y1=INT(RND(0)*8)+2
220  X=INT(RND(0)*10)+1:Y=INT(RND(0)*10)+1
230 IF X1<1 OR X1>10 OR Y1<1 OR Y1>10 THEN 2330
240 FOR I=1 TO 10:FOR J=1 TO 10:BAR(I,J)=0:NEXT J:NEXT I
250 CA=INT(AAR(X1,Y1)/10000):P=INT(((AAR(X1,Y1)-(10000*CA))/100))
260 W=INT((AAR(X1,Y1)-(10000*CA)-(100*P))/10)
270 H=AAR(X1,Y1)-10000*CA-100*P-10*W:I=0:J=0
280 IF CA=1 THEN I=INT(RND(0)*10)+1:J=INT(RND(0)*10)+1:BAR(I,J)=7
290 IF CA=1 AND I=X AND Y=J THEN BAR(I,J)=0:GOTO 280
300 IF H=1 THEN I=INT(RND(0)*10)+1:J=INT(RND(0)*9)+1
310 IF H=1 AND BAR(I,J)<>0 THEN 300
320 IF H=1 THEN BAR(I,J)=3
330 BAR(X,Y)=5:IF W=0 THEN 380
340 FOR I=1 TO W
350 J=INT(RND(0)*10)+1:K=INT(RND(0)*10)+1
360 IF BAR(J,K)<>0 THEN 350
370 BAR(J,K)=2:NEXT I
380 IF P=0 THEN 430
390 FOR I=1 TO P
400 J=INT(RND(0)*10)+1:K=INT(RND(0)*10)+1
410 IF BAR(J,K)<>0 THEN 400
420 BAR(J,K)=1:NEXT I
430 FOR I=1 TO 10: FOR J=1 TO 10
440 IF BAR(J,I)=0 THEN PRINT "t";
450 IF BAR(J,I)=1 THEN PRINT CHR$(124);
460 IF BAR(J,I)=2 THEN PRINT "i";
470 IF BAR(J,I)=3 THEN PRINT CHR$(126);
480 IF BAR(J,I)=5 THEN PRINT "o";
490 IF BAR(J,I)=7 THEN PRINT "m";
500 NEXT J:PRINT "  ";
510 IF I=2 THEN PRINT "STRENGTH";C
520 IF I=3 THEN PRINT "TREASURE";Q
530 IF I=4 THEN PRINT "MAGIC:"
540 IF I=5 THEN PRINT "SLEEP ";S
550 IF I=6 THEN PRINT "CHARMS";R
560 IF I=7 THEN PRINT "INVISIBILITY";V
570 IF I=9 THEN PRINT "DAYS IN FOREST";INT(D1)
580 IF I=1 OR I=8 OR I=10 THEN PRINT
590 NEXT I: PRINT:IF T=1 THEN RETURN
600 IF T=2 THEN 1270
610 I=INT(RND(0)*5):IF I=2 THEN GOSUB 2800
620 IF I=1 AND T<>9 THEN W$="THERE WAS NOTHING THERE":GOSUB10:GOTO 1270
630 IF I=1 AND T=9 THEN 1260
640 I=INT(RND(0)*16)+1:N=I:IF I=12 THEN 2040
650 IF I=13 THEN 2080
660 IF I=14 THEN 2120
670 IF I>14 THEN J=100:GOTO 720
680 J=INT(RND(0)*100/MAR(I)):N1=J:IF J=0 THEN J=1:N1=J
690 IF J=1 THEN M$=MAR$(I):W$="A "+M$+" IS GUARDING ":GOSUB10
700 IF J<>1 THEN M$=MAR$(I)+"S":W$="THERE ARE"+STR$(J)+" "+M$+" GUARDING ":GOSUB10
710 M=MAR(I)*J:I=INT(RND(0)*14)+1
720 IF I>11 AND J=100 THEN 610
730 IF I<12 AND J=100 THEN W$="NOTHING IS GUARDING ":GOSUB10
740 IF I>11 THEN 2230
750 IF I>11 THEN W$="NOTHING":GOSUB10:P=0:GOTO 770
760 W$=T$(I):GOSUB10:P=PAR(I)
770 IF M$=MAR$(11) AND M1=7 THEN 2030
780 IF J=100 THEN W$="YOU GET THE TREASURE FREE":GOSUB10:GOTO 1190
790 W$="(1)FIGHT, (2)RUN, (3)BRIBE, OR (4)CAST A SPELL":GOSUB10:INPUTK 
810 IF K<1 OR K>4 THEN 790
820 ON K GOTO 830,940,1080,1660
830 INPUT "USE HOW MANY COMBAT POINTS";K
840 IF K>C THEN W$="YOU ONLY HAVE"+STR$(C)+" COMBAT POINTS":GOSUB10:GOTO 830
850 I=INT(RND(0)*1001):L=2:C=C-K:K=K-.01*Q
860 FOR H=1000 TO 0 STEP -50
870 IF L*M<=K AND H>=I THEN 1170
880 L=L-.1:NEXT H
890 W$="THE "+M$+" KILLED YOU. ":GOSUB10
900 W$="YOU LOSE EVERYTHING!":GOSUB10
910 INPUT "WISH TO TRY AGAIN (Y OR N)";X$:PRINT
920 IF X$="Y" THEN RUN
930 PRINT:W$="SO LONG... BETTER LUCK NEXT TIME":GOSUB10:END
940 I=INT(RND(0)*12):IF I=11 THEN 890
950 FOR H=0 TO 10:IF H*10>M AND H<=I THEN 970
960 NEXT H: GOTO 1150
970 A=X:B=Y:K=0:T=0:C=C-INT((RND(0)*21)+.001*Q)-5
980 X=A+INT(RND(0)*3)-1:Y=B+INT(RND(0)*3)-1
990 IF X=A AND Y=B THEN 980
1000 D1=D1+.1:IF X>10 THEN X=1:X1=X1+1:K=1
1010 IF Y>10 THEN Y=1:Y1=Y1+1:K=1
1020 IF X<1  THEN X=10: X1=X1-1:K=1
1030 IF Y<1  THEN Y=10:Y1=Y1-1:K=1
1040 IF BAR(X,Y)>1 AND K=0 THEN 980
1050 BAR(A,B)=INT(RND(0)*3):BAR(X,Y)=5:IF I<>11 THEN Z=Z+1
1060 IF K=1 THEN 230
1070 GOTO 610
1080 INPUT "HOW MUCH DO YOU WISH TO PAY";K
1090 IF K>Q THEN W$="YOU ONLY HAVE"+STR$(Q)+" TREASURE POINTS.":GOSUB10:GOTO 1080
1100 I=INT(RND(0)*22):L=0:IF I=21 OR (I>15 AND K<2) THEN 890
1110 J=(P+(M*.1))*N1:IF K<2 THEN 1140
1120 FOR H=0 TO 20:IF K<=J*L AND I>=H THEN 1140
1130 L=L+.1:NEXT H: GOTO 1160
1140 W$="YOUR BRIBE WAS NOT ACCEPTED.":GOSUB10
1150 W$="YOU MUST FIGHT":GOSUB10:GOTO 830
1160 P=0:Q=Q-K:BR=BR+1:T=0:W$="YOUR BRIBE WAS ACCEPTED.":GOSUB10:GOTO 1240
1170 NAR(N)=NAR(N)+N1
1180 W$="YOU BEAT THE "+M$:GOSUB10
1190 IF N<12  THEN I=INT(RND(0)*7):IF I=3 THEN 2150
1200 IF J=100 THEN I=INT(RND(0)*5) :IF I=3 THEN 2210
1210 Q=Q+P
1220 IF P=25 THEN 1900
1230 IF T>5 AND T<>9 THEN Q=Q-P:GOTO 2250
1240 W$="YOU NOW HAVE"+STR$(Q)+" TREASURE POINTS."
1250 IF P=200 THEN 1950
1260 IF T=9 THEN GOSUB 2530
1270 INPUT "WHICH DIRECTION (1=MAP)";X$
1280 IF X$="1" THEN T=2:GOTO 430
1290 T=0:INPUT "WHAT DISTANCE";K:IF K<>INT(K) THEN 1270
1300 GOTO 2500
1310 A1=X1:B1=Y1:A=X:B=Y:C=C-INT(7.5*K*RND(0))
1320 IF  LEFT$(X$,1)="N" THEN Y=Y-K
1330 IF  LEFT$(X$,1)="S" THEN Y=Y+K
1340 IF RIGHT$(X$,1)="E" THEN X=X+K
1350 IF RIGHT$(X$,1)="W" THEN X=X-K
1360 IF X>10 THEN X=X-10:X1=X1+1:IF X>10 THEN 1360
1370 IF X<1  THEN X=X+10:X1=X1-1:IF X<1 THEN 1370
1380 IF Y>10 THEN Y=Y-10:Y1=Y1+1:IF Y>10 THEN 1380
1390 IF Y<1  THEN Y=Y+10:Y1=Y1-1:IF Y<1 THEN 1390
1400 IF BAR(X,Y)=1 THEN C=C-5
1410 IF BAR(X,Y)=0 THEN C=C-10
1420 IF C<=0 THEN W$="YOU DIED FROM LACK OF STRENGTH.":GOSUB10:GOTO 900
1430 IF X1<>A1 OR Y1<>B1 THEN 230
1440 IF BAR(X,Y)=7 THEN T=9
1450 IF BAR(X,Y)=2 THEN 1480
1460 IF BAR(X,Y)=3 THEN 1500
1470 BAR(A,B)=INT(RND(0)*3):BAR(X,Y)=5:GOTO 430
1480 W$="YOU TRIED TO GO THROUGH A WALL":GOSUB10
1490 C=C-INT(RND(0)*Q*.005)-25:X=A:Y=B:GOTO 1270
1500 Y=Y+1:C=D:BAR(A,B)=INT(RND(0)*3):BAR(X,Y)=5:T=1:V=V1
1510 R=R1:S=S1:GOSUB 430
1520 W$="YOU STOPPED AT AN INN AND REGAINED YOUR STRENGTH":GOSUB10
1530 I=INT(RND(0)*Q*.75):IF I<5 AND Q>5 THEN I=5
1540 IF I<5 AND Q<=5 THEN I=0
1550 W$="YOU PAID"+STR$(I)+" TREASURE POINTS TO STAY THERE":GOSUB10:Q=Q-I
1560 W$="YOU NOW HAVE"+STR$(Q)+" TREASURE POINTS":GOSUB10
1570 I=INT(RND(0)*3):IF I=2 THEN 1270
1580 IF I=1 THEN GOSUB 2670:GOTO 1270
1590 I=INT(RND(0)*4)+1
1600 W$="THE INNKEEPER TOLD YOU THAT THE FOREST EDGE IS LESS THAN":GOSUB10
1610 ON I GOTO 1620,1630,1640,1650
1620 W$=STR$(Y1*100)+" YARDS TO THE NORTH":GOSUB10:GOTO 1270
1630 W$=STR$((11-Y)*100)+" YARDS TO THE SOUTH":GOSUB10:GOTO 1270
1640 W$=STR$(X1*100)+" YARDS TO THE WEST":GOSUB10:GOTO 1270
1650 W$=STR$((11-X1)*100)+" YARDS TO THE EAST":GOSUB10:GOTO 1270
1660 IF T>5 THEN W$="YOU CAN'T USE MAGIC TO GET MAGIC":GOSUB10:GOTO 790
1670 IF S+V+R=0 THEN W$="YOU HAVE NO MAGIC":GOSUB10:GOTO 790
1680 W$="WHAT TYPE OF SPELL-(1)SLEEP, (2)CHARM, OR (3)INVISIBILITY":GOSUB10
1690 INPUT K:PRINT:IF K<1 OR K>3 THEN 1660
1700 ON K GOTO 1710,1790,1950
1710 IF S=0 THEN W$="YOU HAVE NO SLEEP SPELLS.":GOSUB10:GOTO 1150
1720 IF N=4 THEN W$="YOU CAN'T PUT "+STR$(MAR$(4))+"S TO SLEEP.":GOSUB10:S=S-1:GOTO 1150
1730 I=INT(RND(0)*10):S=S-1
1740 IF I<3 THEN W$="YOUR SPELL WAS UNSUCCESSFUL.":GOSUB10:GOTO 1150
1750 IF I<8 THEN W$="YOU GOT THE TREASURE SAFELY":GOSUB10:GOTO 1190
1760 W$="THE "+M$+" WOKE TOO SOON":GOSUB10
1770 P=INT(RND(0)*P):Q=Q+P
1780 W$="YOU GOT AWAY WITH"+STR$(P)+" TREASURE POINTS":GOSUB10:GOTO 1270
1790 IF R=0 THEN W$="YOU HAVE NO CHARMS.":GOSUB10:GOTO 1150
1800 I=INT(RND(0)*10):R=R-1
1810 IF M<60 AND I>6 THEN W$="YOUR CHARM DIDN'T WORK.":GOSUB10:GOTO 1150
1820 IF M>50 AND I<2 THEN W$="YOUR CHARM DIDN'T WORK.":GOSUB10:GOTO 1150
1830 IF I=3 THEN W$="THE CHARM WORE OFF TOO SOON.":GOSUB10:GOTO 1770
1840 I=3:GOTO 1750
1850 IF V=0 THEN W$="YOU HAVE NO INVISIBILITY SPELLS.":GOSUB10:GOTO 1150
1860 I=INT(RND(0)*10):V=V-1
1870 IF N>50 AND I>8 THEN W$="THE "+M$+" SMELLED YOU!":GOSUB10:GOTO 1770
1880 IF M<60 AND I=0 THEN W$="YOUR INVISIBILITY WORE OFF TOO SOON":GOSUB10:GOTO 1770
1890 GOTO 1840
1900 I=INT(RND(0)*2)+1:ON I GOTO 1910,1930
1910 C=2*C:W$="YOU WON AN ENCHANTED SWORD. YOUR COMBAT STRENGTH "
1920 W$=W$+"IS DOUBLED AND IS NOW"+STR$(C):GOSUB10:GOTO 1240
1930 W$="YOU WON AN ORDINARY SWORD. YOUR COMBAT STRENGTH IS NOT"
1940 W$=W$+" DOUBLED AND REMAINS AT"+STR$(C):GOSUB10:GOTO 1240
1950 J=INT(RND(0)*10):I=INT(RND(0)*10)
1960 IF J=7 AND M1<>7 THEN M1=7:GOTO 1990
1970 IF I=1 THEN 2010
1980 GOTO 1260
1990 W$="THERE WAS A MIRROR IN THE CHEST. IT WILL PROTECT YOU":GOSUB10
2000 W$="AGAINST ANY "+MAR$(11)+"S YOU MAY MEET":GOSUB10:M1=7:GOTO 1270
2010 W$="THE TREASURE CHEST WAS A TRAP. YOU WERE KILLED WHEN YOU OPENED IT":GOSUB10:GOTO 900
2030 W$="YOUR MIRROR KILLED THE "+M$:GOSUB10:NAR(11)=NAR(11)+1:M=0:GOTO 1190
2040 W$="A GIANT BAT GRABBED YOU AND CARRIED YOU TO A NEW SPOT":GOSUB10
2050 A=X:B=Y:T=0:D1=D1+.1
2060 X=INT(RND(0)*10)+1:Y=INT(RND(0)*10)+1:IF BAR(X,Y)>1 THEN 2060
2070 BAR(A,B)=INT(RND(0)*3):BAR(X,Y)=5:GOTO 610
2080 W$="YOU FELL INTO A PIT.":GOSUB10:I=INT((RND(0)*21)+.001*Q):C=C-I
2090 IF C<=0 THEN W$="YOU DIED TRYING TO GET OUT":GOSUB10:GOTO 900
2100 W$="YOU USED"+STR$(I)+" COMBAT POINTS TRYING TO GET OUT":GOSUB10:I=11
2110 FOR J=1 TO 750:NEXT J:GOTO 970
2120 J=0:FOR I=1 TO 11:J=J+NAR(I):NEXT I:IF J<11 THEN 610
2130 W$="A GIANT NEIL CARRIED YOU TO SAFETY.":GOSUB10
2140 FOR I=1 TO 1000:NEXT I: T=0:GOTO 2340
2150 I=INT(RND(0)*11)+1:M=MAR(I):M$=MAR$(I):N=I
2160 W$="A "+M$+" HEARD THE NOISE OF THE BATTLE AND CAME WANDERING BY":GOSUB10
2170 IF I=11 AND M1=7 THEN 2030
2180 INPUT"DO YOU WISH TO (1)FIGHT, (2)RUN,OR (3)CAST A SPELL";K
2190 IF K<1 OR K>3 THEN 2180
2200 ON K GOTO 830,940,1660
2210 I=INT(RND(0)*11)+1:M=MAR(I):M$=MAR$(I):N=I
2220 W$="A "+M$+" CAME WANDERING BY":GOSUB10:GOTO 2170
2230 IF I<>14 THEN 750
2240 I=INT(RND(0)*3)+1:T=I+5:W$="A "+G$(I):GOSUB10:P=INT(RND(0)*11):GOTO 770
2250 I=INT(RND(0)*10)
2260 IF I=5 THEN W$="YOU WERE UNABLE TO MASTER THE SPELL.":GOSUB10
2270 IF I=5 THEN W$="YOU GAIN NO "+G$(T-5)+"S":GOSUB10:GOTO 1270
2280 IF T=6 THEN S=S+1:S1=S1+1
2290 IF T=7 THEN R=R+1:R1=RA+1
2300 IF T=8 THEN V=V+1:V1=V1+1
2310 W$="YOU WON THE "+G$(T-5):GOSUB10:T=0:IF S1/5+R1/3+V1/2>6 THEN GOSUB 3280
2320 GOTO 1270
2330 FOR A=1 TO 750:NEXT A
2340 W$="YOU SURVIVED THE FOREST":GOSUB10:FOR I=1 TO 1000:NEXT I
2350 W$="DO YOU WISH TO SEE THE # OF MONSTERS YOU KILLED, RAN FROM, AND BRIBED":GOSUB10
2360 INPUT"(Y OR N)";X$:IF X$="N" THEN PRINT:GOTO 2420
2370 PRINT"MONSTER","# SLAIN"
2380 FOR I=1 TO 5:PRINT MAR$(I),NAR(I)
2400 NEXT I
2403 X$=INKEY$:IFX$=""THEN2403
2405 FOR I=6 TO 11:PRINT MAR$(I),NAR(I)
2407 NEXT I
2410 PRINT:PRINT "BRIBED-";BR,"RAN FROM-";Z
2420 PRINT "TREASURE TOTAL-";Q
2430 IF Q1<>0 THEN GOSUB 3250
2440 PRINT"CONGRATULATIONS";:IF Q1<>0 AND Q1>Q THEN PRINT " ANYWAY":PRINT
2450 PRINT:X$=""
2460 IF D1<30 THEN W$="DO YOU WISH TO RETURN TO THE FOREST":GOSUB10:INPUT"(Y OR N)";X$
2470 REM
2480 S=S1:V=V1:R=R1:C=D:IF X$<>"Y" THEN 3080
2490 GOTO 210
2500 D1=D1+K/10:IF D1<30 THEN 1310
2510 W$="YOUR TIME IS UP. 30 DAYS HAVE PASSED":GOSUB10
2520 FOR I=1 TO 1000:NEXT I:GOTO 2130
2530 FOR I=1 TO 2500:NEXT I:T=0
2540 W$="YOU MADE IT INTO THE ENCHANTED CASTLE":GOSUB10
2550 I=INT(RND(0)*21)*100:J=INT(RND(0)*9):AAR(X1,Y1)=AAR(X1,Y1)-10000
2560 GOSUB 2790:W$="YOU FOUND "+STR$(I)+" TREASURE POINTS THERE":GOSUB10:Q=Q+I
2570 IF J<>7 OR M1=7 THEN 2600
2580 W$="YOU ALSO FOUND A MIRROR WHICH WILL KILL ANY "+MAR$(11)+"S YOU MEET":GOSUB10:M1=7
2600 J=INT(RND(0)*20):IF J=2 THEN C=2*C
2610 IF J=2 THEN W$="YOU ALSO FOUND AN ENCHANTED SWORD WHICH DOUBLES ":GOSUB10
2620 IF J=2 THEN W$="YOUR STRENGTH":GOSUB10
2630 FOR I=1 TO CS-1:IF CAR(I)<>X1 THEN 2650
2640 FOR J=I TO CS-1:CAR(J)=CAR(J+1):DAR(J)=DAR(J+1):NEXT J
2650 NEXT I:CS=CS-1:IF CS=0 THEN W$="YOU FOUND THE LAST OF THE CASTLES":GOSUB10
2660 RETURN
2670 IF CS=0 THEN RETURN
2680 I=INT(RND(0)*CS)+1
2690 W$="THE INKEEPER TOLD YOU OF A LEGEND OF A CASTLE.":GOSUB10
2700 IF CAR(I)=X1 AND DAR(I)=Y1 THEN W$="VERY CLOSE BY":GOSUB10:RETURN
2710 J=X1-CAR(I):I=Y1-DAR(I)
2720 IF ABS(I)=ABS(J) THEN W$="DIRECTLY TO THE ":GOTO 2740
2730 W$="SOMEWHERE TO THE "
2740 IF I<0 THEN W$=W$+"SOUTH"
2750 IF I>0 THEN W$=W$+"NORTH"
2760 IF J>0 THEN W$=W$+"WEST"
2770 IF J<0 THEN W$=W$+"EAST"
2780 GOSUB10:RETURN
2790 REM
2800 I=INT(RND(0)*11)+1
2810 ON I GOTO 2820, 2860, 2840, 2890, 2920, 2950, 2970, 2990, 3010, 3020, 3050
2820 W$="YOU STEPPED INTO A TIME WARP AND LOST SEVEN DAYS":GOSUB10
2830 D1=D1+7:RETURN
2840 I=INT(RND(0)*10)+1:J=D1:D1=D1-I:IF D1<.1 THEN D1=.1:I=J-D1
2850 W$="YOU STEPPED INTO A TIME WARP AND GAINED"+STR$(I)+" DAYS":GOSUB10:RETURN
2860 IF C>=D THEN RETURN
2870 W$="YOU MET AN ELF WITH A MAGIC DRINK THAT GAVE YOUR COMBAT STRENGTH BACK":GOSUB10:C=D:RETURN
2890 IF V+R+S=V1+R1+S1 THEN RETURN
2900 W$="YOU RAN INTO A WIZARD WHO GAVE YOU A POTION THAT RESTORED ALL YOUR MAGIC":GOSUB10:V=V1:R=R1:S=S1:RETURN
2920 IF Q<2 THEN RETURN
2930 W$="YOU FELL INTO SOME QUICKSAND, YOU LOST HALF OF YOUR TREASURE":GOSUB10:Q=INT(Q/2):RETURN
2950 W$="YOU RAN INTO SOME THICK UNDERBRUSH AND USED UP HALF YOUR STRENGTH":GOSUB10:C=INT(C/2):RETURN
2970 I=INT(RND(0)*50)+1:W$="YOU FOUND"+STR$(I)+" COINS LYING ON THE GROUND AND PICKED THEM UP":GOSUB10:Q=Q+I:RETURN
2990 IF M1<>7 THEN RETURN
3000 W$="YOU TRIPPED OVER SOME ROOTS AND LOST YOUR MIRROR":GOSUB10:M1=0:RETURN
3010 W$="A HERMIT TOLD YOU THAT THERE ARE"+STR$(CS)+" CASTLES LEFT":GOSUB10:RETURN
3020 IF V+S+R=0 THEN RETURN
3030 W$="YOU WANDERED INTO AND AREA WHERE MAGIC DOESN'T WORK":GOSUB10
3040 W$="YOU LOSE ALL YOUR PRESENT MAGIC":GOSUB10:V=0:S=0:R=0:RETURN
3050 IF CS=0 THEN RETURN
3060 W$="YOU MET A HUNTER WHO TOLD YOU OF THE LEGEND OF A CASTLE ":GOSUB10:I=INT(RND(0)*CS)+1:GOSUB 2700:RETURN
3080 FOR I=1 TO 2000:NEXT:PRINT
3090 W$="DO YOU WISH TO GO TO A NEW FOREST WITH THE SAME STRENGTH AND MAGIC":GOSUB10
3100 INPUT"(Y OR N)";X$:IF X$="Y" THEN 3190
3110 W$="DO YOU WISH TO GO TO A NEW FOREST WITH A NEW STRENGTH AND MAGIC":GOSUB10
3120 INPUT"(Y OR N)";X$:IF X$="Y" THEN RUN
3130 W$="DO YOU PLAN ON USING THE SAME STRENGTH AND MAGIC AGAIN SOME OTHER TIME":GOSUB10
3140 INPUT"(Y OR N)";X$:IF X$="Y" THEN GOSUB 3350
3150 PRINT:W$="ONCE AGAIN, YOUR TREASURE TOTAL WAS"+STR$(Q):GOSUB10
3160 IF Q>Q1 THEN Q1=Q
3170 IF Q1<>0 THEN W$="THE LARGEST TREASURE TOTAL YOU GOT WITH THIS STRENGTH AND MAGIC WAS"+STR$(Q1):GOSUB10
3180 PRINT:PRINT"BYE NOW":END
3190 BR=0:Z=0:D1=0:FOR I=1 TO 11:NAR(I)=0:NEXT I: IF Q1<500 OR C>2000 THEN 3210
3195 GOTO140
3210 REM LOAD OLD GAME INFO
3215 INPUT"HIT enter AND PRESS play";X$
3217 CLOAD*GAM,"COMBAT"
3218 S=GAM(1):R=GAM(2):V=GAM(3):Q1=GAM(4):D=GAM(5)
3240 GOTO140
3250 IF Q1>Q THEN W$="YOU DIDN'T OBTAIN AS MUCH TREASURE THIS TIME":GOSUB10
3270 RETURN
3280 W$="YOUR MAGIC TOTAL IS RATHER LARGE. DO YOU WISH TO CONVERT IT TO":GOSUB10
3290 INPUT "COMBAT POINTS";X$:IF X$="N" THEN RETURN
3300 S1=S1-5:R1=R1-3:V1=V1-2:IF S1<=0 THEN S1=1
3310 IF R1<=0 THEN R1=1
3320 IF V1<=0 THEN V1=1
3330 S=S1:R=R1:V=V1:C=C+100:D=D+100:W$="YOUR COMBAT STRENGTH IS PERMANENTLY INCREASED BY 100":GOSUB10:RETURN
3350 PRINT"COMBAT STRENGTH-";D:PRINT"SLEEP SPELLS-";S1:PRINT"CHARMS-";R1
3360 PRINT"INVISIBILITY-";V1:PRINT
3362 INPUT"HIT play & record & THEN enter";X$
3364 GAM(1)=S:GAM(2)=R:GAM(3)=V:GAM(4)=Q:GAM(5)=D
3366 CSAVE*GAM,"COMBAT"
3368 RETURN
3370 DATA GOBLIN, 10 SILVER SPOONS (10 POINTS), 5, 10, MINOTAUR
3380 DATA A SWORD WHICH MIGHT BE ENCHANTED (25 POINTS), 10, 25
3390 DATA CYCLOPS, 50 SILVER COINS (50 POINTS), 20, 50, ZOMBIE
3400 DATA 100 GOLD PIECES (100 POINTS), 30, 100, GIANT
3410 DATA AN ENCHANTED BRACELET (50 POINTS), 40, 50, HARPY
3420 DATA A TREASURE CHEST (200 POINTS), 50, 200, GRIFFIN
3430 DATA A PEARL NECKLACE (50 POINTS), 60, 50, CHIMERA
3440 DATA A JEWELED SWORD (30 POINTS), 70, 30, DRAGON
3450 DATA A JAR OF RUBIES (75 POINTS), 80, 75, WYVERN
3460 DATA A BOX OF JEWELS (100 POINTS), 90, 100, BASILISK
3470 DATA A GOLD GOBLET (50 POINTS), 100, 50
3480 END
