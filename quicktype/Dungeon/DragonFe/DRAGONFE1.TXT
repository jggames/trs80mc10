0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 REM BASED ON THE DUKE OF DRAGONFEAR BY TIM HARTNELL
20 GOSUB 1280
30 CLS:PRINT
40 GOSUB 1200
50 Q=RND(7)
60 IF Q=0 AND E<>55 THEN GOSUB 1200
70 CLS:PRINT:PRINT:PRINT A$", YOU ARE AT"E
80 IF G>0 THEN PRINT "YOU ARE CARRYING"G"GOLD PIECES"
90 GOSUB 760
100 PRINT:PRINT "YOU HAVE"25-H"MOVES LEFT"
110 PRINT:PRINT "WHAT DO YOU WANT TO DO?"
120 PRINT "N-MOVE NORTH  S-MOVE SOUTH" 
130 PRINT "E-MOVE EAST   W-MOVE WEST"
140 PRINT "F-FIRE ARROW  Q-QUIT":PRINT 
150 INPUT Z$:U=0:IFZ$<>"N"ANDZ$<>"S"ANDZ$<>"E"ANDZ$<>"W"ANDZ$<>"F"ANDZ$<>"Q"ANDZ$<>"M"THEN150
160 IFZ$="N"ANDA(E-10)=88ORZ$="S"ANDA(E+10)=88ORZ$="E"ANDA(E+1)=88ORZ$="W"ANDA(E-1)=88THENPRINT"YOUR WAY IS BLOCKED!":GOTO150
170 IF Z$="Q" THEN Q=9:GOTO 1170
180 A(E)=46:IF Z$="N" THEN E=E-10 
190 IF Z$="S" THEN E=E+10
200 IF Z$="E" THEN E=E+1
210 IF Z$="W" THEN E=E-1
220 IF Z$="F" THEN GOSUB 930
225 IF Z$="M" THEN GOSUB 1620
230 IF A(E)=63 THEN GOSUB 310:REM MAGIC
240 IF A(E)=68 THEN GOSUB 400:REM DRAGON
250 IF A(E)=81 THEN GOSUB 550:REM QUICKSAND
260 IF A(E)=36 THEN GOSUB 630:REM GOLD
270 H=H+1:IF H=25 THEN Q=9:GOTO 1160
280 GOSUB 1610
290 GOTO 50
300 REM **************************************
310 REM MAGIC
320 PRINT:M$=A$+", YOU HAVE STUMBLED INTO A MAGIC CAVE, AND NOW YOU'LL BE WHISKED TO ANOTHER CAVE.":GOSUB1
350 GOSUB 1610
360 A(E)=46
370 E=RND(76)+12:IF A(E)=88 THEN 370
380 RETURN
390 REM **************************************
400 REM DRAGON
410 M$="YOU HAVE FOUND A DRAGON'S LAIR. START SAYING YOUR PRAYERS.":GOSUB1
430 GOSUB 1610
440 H=RND(10):IF H<2 THEN PRINT "IT HAS FLOWN AWAY.":RETURN
450 PRINT "IT AWAKENS AND IT HAS SEEN YOU!"
460 GOSUB 1610
470 IF H>6 THENM$="BUT IT HAS RECENTLY EATEN AND SO GOES BACK TO SLEEP":GOSUB1:RETURN
480 PRINT "AND IT ATTACKS! "
490 GOSUB 1610
500 IF H>8 THEN PRINT "BUT YOU FIGHT BACK AND WIN!":RETURN
510 PRINT:PRINT"GAME OVER. GOODBYE, "A$
520 GOSUB 1610
530 Q=9:GOTO 1160
540 REM ***********************************************
550 REM QUICKSAND
555 PRINT"OH NO QUICKSAND!"
560 FOR J=1 TO 12
570 FOR K=1 TO J:PRINT " ";:NEXT
580 PRINT "YOU ARE SINKING!"
590 FOR O=1 TO 90:NEXT
600 NEXT
610 Q=9:H=0:GOTO 1160
620 REM ***********************************************
630 REM TREASURE
640 K=RND(100)+100
650 FOR J=1 TO 12
660 FOR Z=1 TO J:PRINT " ";:NEXT 
670 PRINT "$treasure$"
680 FOR O=1 TO 200:NEXT
690 NEXT
700 GOSUB 1610
710 PRINT:PRINT "YOU'VE FOUND"K"GOLD PIECES"
730 G=G+K
740 RETURN
750 REM ***********************************************
760 REM AMULET DETAILS
770 Y=1
780 L=A(E+P(Y))
790 IF L<>46 THEN 820
800 IF Y<8 THEN Y = Y + 1: GOTO 780
810 IF L=46 THEN RETURN
820 PRINT "YOUR AMULET SIGNALS THAT"
830 PRINT "THERE IS ";
840 IF L=88 THEN PRINT "A SOLID WALL";
850 IF L=63 THEN PRINT "A MAGIC CAVE";
860 IF L=68 THEN PRINT "A DRAGON";
870 IF L=81 THEN PRINT "QUICKSAND";
880 IF L=36 THEN PRINT "GOLD";
890 PRINT " NEARBY."
900 GOSUB 1610
910 RETURN 
920 REM ***********************************************
930 REM ATTACK DRAGON
940 PRINT
950 AR=AR-1:IF AR=0 THEN PRINT "YOU HAVE USED UP ALL":PRINT "YOUR ARROWS.":GOSUB 1610:RETURN
960 PRINT "YOU HAVE"AR"ARROWS REMAINING."
970 SS=0
980 PRINT "WHICH DIRECTION DO YOU WANT"
990 INPUT "TO SHOOT IN (N,S,E OR W)";S$
1000 IF S$="N" AND A(E-10)=68 THEN SS=1:YT=E-10
1010 IF S$="S" AND A(E+10)=68 THEN SS=1:YT=E+10
1020 IF S$="E" AND A(E+1)=68 THEN SS=1:YT=E+1
1030 IF S$="W" AND A(E-1)=68 THEN SS=1:YT=E-1
1040 PRINT
1050 IF SS=0 THEN PRINT "THERE WAS NO DRAGON THERE.":PRINT "YOU WASTED AN ARROW.":GOTO 1150
1060 PRINT "WELL DONE, "A$
1070 PRINT "YOU HAVE HIT"
1080 PRINT "A FEROCIOUS DRAGON"
1090 GOSUB 1610
1100 IF RND(10)>4 THEN 1140
1110 PRINT "YOU KILLED IT!":A(YT)=46:K=RND(100)+100
1120 PRINT
1130 M$="YOU ARE REWARDED WITH"+STR$(K)+" GOLD PEICES":GOSUB1:G=G+K:GOTO 1150
1140 PRINT "BUT YOU ONLY WOUNDED IT."
1150 GOSUB 1610:RETURN
1160 IF H<1 THEN PRINT "GAME OVER!":GOSUB 1610:GOTO 1180
1170 PRINT "YOU HAVE"25-H"MOVES LEFT"
1180 IF G>0 THEN PRINT "YOU AMASSED"G"GOLD PIECES."
1200 A(E)=72
1205 PRINT"THE WORLD OF DRAGONFEAR"
1210 FOR J=1 TO 100
1220 PRINT CHR$(A(J));" ";
1230 IF 10*INT(J/10)=J THEN PRINT
1240 NEXT
1250 GOSUB 1610
1260 IF Q=9 THEN END
1270 RETURN
1280 CLS:PRINT:PRINT
1290 PRINT "WELCOME TO THE WORLD"
1295 PRINT "OF DRAGONFEAR"
1300 PRINT:PRINT "YOUR MISSION IS TO EXPLORE"
1310 PRINT "THE LANDS OF DRAGONFEAR,"
1320 PRINT "LOOKING FOR TREASURE AND"
1325 PRINT "ATTEMPTING TO SLAY THE"
1330 PRINT "FEARSOME DRAGONS WHO LIVE"
1340 PRINT "IN THE WORLD."
1345 PRINT:PRINT "WHAT IS YOUR NAME";
1350 INPUT A$
1360 CLS:PRINT:PRINT "WELCOME, "A$
1370 PRINT:PRINT "YOU START THIS EXPLORATION"
1380 PRINT "WITH 25 MOVES."
1390 PRINT "YOU MUST COMPLETE YOUR TASK"
1400 PRINT "BEFORE YOUR MOVES RUN OUT."
1420 N=1:PRINT:PRINT "PRESS ANY KEY TO BEGIN"
1430 N=N+1:IF INKEY$="" THEN 1430
1440 CLS
1450 PRINT "PLEASE STAND BY, "A$
1460 DIM A(100):H=0:Q=0:L=0:G=0:AR=6
1470 FOR B=1 TO 100:A(B)=46
1480 IF B<12 OR B>90 OR 10*INT(B/10)=B OR 10*INT(B/10)=B-1 THEN A(B)=88
1490 NEXT
1500 FOR B=1 TO 5:RESTORE:FOR D=1 TO 5
1510 Z=RND(76)+12:IF A(Z)=88 THEN 1510
1520 READ C:A(Z)=C
1530 NEXT:NEXT
1540 DATA 88,63,68,81,36
1550 FOR B=1 TO 8:READ Z:P(B)=Z:NEXT 
1560 DATA -11,-10,-9,-1,1,9,10,11
1570 E=55
1580 RETURN
1590 REM ENGAGE CAPS LOCK
1600 REM BEFORE PLAYING
1610 FORO=1TO3000:NEXT:RETURN
1615 REM *************************************
1620 REM VIEW MAP
1625 CLS
1626 PRINT "THE WORLD OF DRAGONFEAR"
1630 A(E)=72
1640 FOR J=1 TO 100
1650 PRINT CHR$(A(J));" ";
1660 IF 10*INT(J/10)=J THEN PRINT
1670 NEXT
1680 FORO=1TO2500:NEXT:RETURN
1690 REM MODIFIED BY JERRY YOUNG
1700 REM MC-10 EDITS JIM GERRIE
