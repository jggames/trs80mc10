0 CLS:CLEAR450:DIMCC,ZZ,M$,I$,M(32):M=16384:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 DD=21
4 ZZ=1:CC=10:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?@DD,MID$(M$,ZZ,CC-ZZ):DD=DD+32:ZZ=CC+1:CC=ZZ+10:IFI$=""THENCC=.
5 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
8 FORCC=0TO10:PRINT@CC*32+21,"":NEXT:RETURN
10 CLS0:GOSUB 1210
20 LE=1:HE=125
30 K$(1)=""
40 K$(2)=""
50 DIM MN$(20,1),TR$(9),AT$(12),DF(5,1),TR(10,1),RM$(5)
60 FOR I=1 TO 20:READ MN$(I,0),MN$(I,1):NEXT I
70 FOR I=1 TO 5:READ RM$(I):NEXT I:FOR I=1 TO 5:READ DF(I,0),DF(I,1):NEXT I
80 FOR I=1 TO 9:READ TR$(I):NEXT I:FOR I=1 TO 12:READ AT$(I):NEXT I
90 GOSUB 320:GOSUB 820
100 SET(X,Y,2):PRINT@407,"COORDS:";:PRINT@438,X;",";Y;
110 KB=PEEK(17023) AND PEEK(2)
120 IF KB=87 AND POINT(X,Y-1)=0 THEN RESET(X,Y):Y=Y-1:GOSUB 470
130 IF KB=65 AND POINT(X-1,Y)=0 THEN RESET(X,Y):X=X-1:GOSUB 470
140 IF KB=83 AND POINT(X+1,Y)=0 THEN RESET(X,Y):X=X+1:GOSUB 470
150 IF KB=90 AND POINT(X,Y+1)=0 THEN RESET(X,Y):Y=Y+1:GOSUB 470
160 IF X=TR(1,0) AND Y=TR(1,1) THEN SOUND50,1:KEY=-1:TR(1,0)=0:TR(1,1)=0:PRINT@279,K$(1);:PRINT@311,K$(2);
170 IFPOINT(X+2,Y)=3 AND KEY=-1 THEN LE=LE+1:GOSUB 320:GOSUB 820
180 FORI=2TO10:IFTR(I,0)=XANDTR(I,1)=YTHENTR(I,0)=0:TR(I,1)=0:GOSUB840
181 NEXTI
185 IFHE<=0THEN1160
190 GOTO100
200 DATA"DINGUS",10,"WRETCHED",25,"SLIME MOLD",15
210 DATA"WOOD ELF",10,"BLOB- BAGOO",10,"STALKER",25
220 DATA"CRAWLER",30,"RAP- SCALLION",20
230 DATA"FLESH EATER",20,"SHADOW- BEAST",25,"WRAITH",15
240 DATA"IMP",10,"BLOOD- FIEND",25,"SOUL REAPER",30
250 DATA"GIANT RAT",10,"WITCHER",25,"GHOST BAT",20
260 DATA"DUNG FUNGUS",10,"TWERP",5,"PLASMA-GOG",75
270 DATA"MAIN ENTRANCE","UPSTAIRS LOFT","SANCTUARY","GRAND HALL","MUSEUM"
280 DATA 5,6,8,8,10,10,10,15,15,20
290 DATA"ELIXIR OF HEALING","STAFF OF STRENGTH","HEALING MANA","POTION OF HEALTH","REMOTE VIEWING SPELL"
291 DATA"EMPTY CHEST","RATTLE- SNAKES","RABID MICE","ROTTING CORPSE"
300 DATA"KICK","PUNCH","ELBOW","SLASH","HEADBUTT","CHARGE","TROUNCE","JAB","BASH","PUMMEL","WALLOP","STRIKE"
310 REM **** LEVEL LOADER
320 IFLE=6THEN1270
325 X=3:Y=19:KEY=0
330 CLS:ONLEGOSUB5000,5000,5000,5000,5000,5000
340 PRINT@480,"                               ";
350 XK=RND(40):YK=RND(29)
360 IFPOINT(XK,YK)THEN350
370 TR(1,0)=XK:TR(1,1)=YK
380 FOR I=2 TO 10
390 XK=RND(40):YK=RND(29)
400 IF POINT(XK,YK)<>0 THEN GOTO 390
410 TR(I,0)=XK:TR(I,1)=YK
420 NEXT I
430 REM key loc PRINT@87,TR(1,0);",";TR(1,1);
440 E=(29-LEN(RM$(LE)))/2:PRINT@480+E,"*";RM$(LE);"*";
450 REM *** REFERENCE PRINT@119,TR(2,0);TR(2,1);:PRINT@151,TR(3,0);TR(3,1);:PRINT@183,TR(4,0);TR(4,1);:PRINT@215,TR(5,0);TR(5,1);:PRINT@247,TR(6,0);TR(6,1);
460 RETURN
470 ZZ=PEEK(9)AND128:POKE49151,ZZ:POKE49151,128-ZZ:MST=RND(100)
480 IF MST < DF(LE,0) THEN GOSUB 500
490 RETURN
500 EN=RND(DF(LE,1)):SOUND100,2
510 M$="encounter ":REM PCOPY 0 TO 1
520 M$=M$+MN$(EN,0)
530 GOSUB780
540 M$=M$+"! DO YOU CHOOSE TO "
550 M$=M$+"<1>RUN AWAY OR <2>FIGHT!":GOSUB3
560 I$=INKEY$:IF I$<>"1" AND I$<>"2" THEN560
570 IFI$="1"THENGOSUB740:IFRF<>1THENGOSUB8:RETURN:REM PCOPY 1 TO 0 IF RF<>1
580 IFI$="1"THENDD=32*8+21:M$="YOU CAN'T ESCAPE!":GOSUB4:GOTO590
581 DD=8*32+21:M$="FIGHT IT IS!":GOSUB4
590 I$=INKEY$:IFI$<>" "THEN590
600 GOSUB780
610 MH=VAL(MN$(EN,1))
620 A=0:A$="PRESS f TO FIGHT!"
630 IFMH<=0THENGOSUB1060:GOSUB 810:RETURN:PCOPY 1 TO 0 BEFORE 810 GOSUB
635 IFHE<=0THEN1160
640 PRINT@164,MN$(EN,0);":";MH;:PRINT@196,"YOU:";HE;
650 PRINT@233,A$;
660 I$=INKEY$:IF I$<>"F" AND A=0 THEN GOTO 660
670 A=-1:A$="":GOSUB 970
680 GOSUB 780
690 PRINT@260,MN$(EN,0);" ATTACKS!";
700 GOSUB 750
710 IF RF>1 THEN PRINT@292,"BUT MISSES!";:GOSUB 1030
720 IF MH>0 AND RF=1 THEN HP=INT(RND(HE*.25)):PRINT@292,"HITS FOR";HP;"DAMAGE!";:HE=HE-HP:GOSUB 1030
730 GOSUB 780:GOTO 630
740 REM *** CHOOSE TO RUN
750 RF=RND(3)
760 RETURN
770 REM *** BATTLE/RUN WINDOW
780 GOSUB8:RETURN
810 REM *** HEALTH DISPLAY
820 PRINT@343,"HEALTH:";:PRINT@375,HE;:RETURN
830 REM *** TREASURE FOUND
840 SOUND1,5:REM PCOPY 0 TO 1:PLAY"O3;L30;C;F;G;A"
850 GOSUB780
860 M$="YOU'VE FOUND A CHEST. "
870 M$=M$+"OPENING IT REVEALS: "
880 C=RND(9)
890 T=(32-LEN(TR$(C)))/2
900 M$=M$+TR$(C):GOSUB3
910 IFC<5THENH=10+RND(25):HE=HE+H
920 IFC>6THENH=5+RND(10):HE=HE-H
930 IFC=5ANDTR(1,0)<>0THENPRINT@297,"KEY AT:";TR(1,0);",";TR(1,1);
940 PRINT@32*10+21,;:M$="HIT SPACE":GOSUB7
950 I$=INKEY$:IFI$<>" "THEN950
960 GOSUB8:GOSUB820:RETURN
970 REM *** ATTACK SUB
980 PRINT@260,"YOU ";AT$(RND(12));" THE ";MN$(EN,0);
990 HIT=RND(2)
1000 IF HIT=1 THEN PRINT@292,"BUT MISS!";
1010 IF HIT=2 THEN HP=INT(RND(MH/2)):PRINT@292,MN$(EN,0);" HIT FOR";HP;:MH=MH-HP
1020 GOSUB 1030:RETURN
1030 REM *** FIGHT/MSG WAIT
1040 FOR I=1 TO 1000:NEXT I
1050 RETURN
1060 REM *** MONSTER DEFEATED
1070 PRINT@228,MN$(EN,0);" DEFEATED!!";:SOUND1,10
1080 PRINT@260,"YOU SEARCH THE ";MN$(EN,0);:PRINT@292,"AND FIND ";
1090 IF TR(1,0)=0 THEN POT=RND(2) ELSE POT=RND(3)
1100 IF POT=1 THEN PRINT "NOTHING.";
1110 IF POT=2 THEN PRINT "HEALING SALVE!";:HE=HE+20
1120 IF POT=3 THEN PRINT"A NOTE THAT SAYS:";:PRINT@324,"KEY AT";TR(1,0);",";TR(1,1); 
1130 PRINT@360,"PRESS SPACE";
1140 I$=INKEY$:IF I$<>" " THEN GOTO 1140
1150 RETURN
1160 REM *** player dead
1170 GOSUB 780:SOUND1,10:SOUND200,10
1180 PRINT@197,"** ALAS, YOU HAVE DIED **";
1190 PRINT@229,"RATS GNAW ON YOUR CORPSE.";
1200 GOTO 1200
1210 GOSUB2000
1250 I$=INKEY$:IF I$<>CHR$(13) THEN GOTO 1250
1260 CLS:RETURN
1270 '*** end screen
1280 LOADM"FINISH.BIN"
1290 GOTO 1290
2000 CLS0
2001 PRINT"";
2002 PRINT"";
2003 PRINT"а";
2004 PRINT"";
2005 PRINT"";
2006 PRINT"PRESS ";
2007 PRINT"ENTER";
2008 PRINT"IF U";
2009 PRINT"DARE!";
2010 PRINT"";
2011 PRINT"";
2012 PRINT"";
2013 PRINT"";
2014 PRINT"";
2015 PRINT"";:RETURN

4000 CLS:PRINT"     MONSTERS IN THE MANOR"
4001 M$="YOU MUST SURVIVE THE NIGHT AND ESCAPE THE MANSION FILLED WITH MONSTERS. "
4002 M$=M$+"USE <WASD> TO MOVE. YOU MUST SEARCH THE ROOMS AND CORRIDORS AND FIND THE KEY TO ESCAPE EACH LEVEL.":GOSUB1
4004 M$="YOU CAN CHOOSE <R>UN AWAY OR <F>IGHT. "
4005 M$=M$+"MONSTERS MIGHT BE CARRYING A NOTE WITH COORDINATES OF THE KEY OR A HEALING SALVE. "
4006 M$=M$+"THERE ARE 9 HIDDEN CHESTS ON EACH LEVEL THAT ALSO MIGHT CONTAIN THESE, OR SOME NOT-TOO NICE ITEMS.":GOSUB1
4010 RETURN

4999 GOTO4999

5000 PRINT"           ";
5001 PRINT"           ";
5002 PRINT"           ";
5004 PRINT"           ";
5005 PRINT"           ";
5006 PRINT"           ";
5007 PRINT"           ";
5008 PRINT"           ";
5009 PRINT"           ";
5010 PRINT"           ";
5011 PRINT"           ";
5012 PRINT"           ";
5013 PRINT"           ";
5014 PRINT"           ";
5015 PRINT"          ";:RETURN

9991 REM MONSTERS IN THE MANOR
9992 REM FOR THE TRS-80 MC-10
9993 REM BY JIM MCCLELLAN
9994 REM MAPS DRAWN WITH MCDRAW
9995 REM INTRO AND END SCREENS
9996 REM BY PAUL SHOEMAKER
9998 REM MC-10 EDITS JIM GERRIE
9999 GOTO9999
