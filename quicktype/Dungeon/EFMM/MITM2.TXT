0 CLS:CLEAR450:DIMCC,ZZ,M$,I$,M(32):M=16384:GOTO4000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 GOSUB8:DD=21
4 ZZ=1:CC=10:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?@DD,MID$(M$,ZZ,CC-ZZ);:DD=DD+32:ZZ=CC+1:CC=ZZ+10:IFI$=""THENCC=.
5 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
6 SOUND217,2:SOUND219,2:SOUND221,2:KEY=-1:TR(1,0)=0:TR(1,1)=0:PRINT@32*14+23,K$(1);:PRINT@32*15+23,K$(2);:RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
8 FORCC=0TO9:PRINT@CC*32+21,"":NEXT:RETURN
9 FORZZ=1TO25:NEXT:RETURN
10 CLS0:GOSUB1210
20 LE=1:HE=125
30 K$(1)=""
40 K$(2)=""
50 DIM MN$(20,1),TR$(9),AT$(12),DF(5,1),TR(10,1),RM$(5)
60 FORI=1TO20:READMN$(I,0),MN$(I,1):NEXTI
70 FORI=1TO5:READRM$(I):NEXTI:FORI=1TO5:READDF(I,0),DF(I,1):NEXTI
80 FORI=1TO9:READTR$(I):NEXTI:FORI=1TO12:READAT$(I):NEXTI
90 GOSUB320:GOSUB820
100 SET(X,Y,2):PRINT@407,"COORDS:";:PRINT@439,STR$(X)","MID$(STR$(Y),2)" ";
110 IFPEEK(2)AND4ANDNOTPEEK(16952)THENIFPOINT(X,Y-1)=0THENRESET(X,Y):Y=Y-1:GOSUB470:GOTO160
120 IFPEEK(2)AND1ANDNOTPEEK(16946)THENIFPOINT(X-1,Y)=0THENRESET(X,Y):X=X-1:GOSUB470:GOTO160
130 IFPEEK(2)AND4ANDNOTPEEK(16948)THENIFPOINT(X+1,Y)=0THENRESET(X,Y):X=X+1:GOSUB470:GOTO160
140 IFPEEK(2)AND8ANDNOTPEEK(16947)THENIFPOINT(X,Y+1)=0THENRESET(X,Y):Y=Y+1:GOSUB470
160 IFX=TR(1,0)ANDY=TR(1,1)THENGOSUB6
170 IFPOINT(X+2,Y)=3ANDKEY=-1THENLE=LE+1:GOSUB320:GOSUB820
180 FORI=2TO10:IFTR(I,0)=XANDTR(I,1)=YTHENTR(I,0)=0:TR(I,1)=0:GOSUB840
181 NEXTI
185 IFHE<=0THEN1160
190 GOTO100
200 DATA"DINGUS",10,"WRETCHED",25,"SLIME MOLD",15
210 DATA"WOOD ELF",10,"BLOB- BAGOO",10,"STALKER",25
220 DATA"CRAWLER",30,"RAP- SCALLION",20
230 DATA"FLESH EATER",20,"SHADOW- BEAST",25,"WRAITH",15
240 DATA"IMP",10,"BLOOD- FIEND",25,"SOUL REAPER",30
250 DATA"GIANT RAT",10,"WITCHER",25,"GHOST BAT",20
260 DATA"DUNG FUNGUS",10,"TWERP",5,"PLASMA-GOG",75
270 DATA"MAIN ENTRANCE","UPSTAIRS LOFT","SANCTUARY","GRAND HALL","MUSEUM"
280 DATA 5,6,8,8,10,10,10,15,15,20
290 DATA"ELIXIR OF HEALING","STAFF OF STRENGTH","HEALING MANA","POTION OF HEALTH","ENCOUNTVIEWING SPELL"
291 DATA"EMPTY CHEST","RATTLE- SNAKES","RABID MICE","ROTTING CORPSE"
300 DATA"KICK","PUNCH","ELBOW","SLASH","HEADBUTT","CHARGE","TROUNCE","JAB","BASH","PUMMEL","WALLOP","STRIKE"
310 REM **** LEVEL LOADER
320 IFLE=6THEN1280
325 X=3:Y=19:KEY=0
330 CLS:ONLEGOSUB5000,5100,5200,5300,5400
340 PRINT@480,"                               ";
350 XK=RND(40):YK=RND(29)
360 IFPOINT(XK,YK)THEN350
370 TR(1,0)=XK:TR(1,1)=YK
380 FORI=2TO10
390 XK=RND(40):YK=RND(29)
400 IFPOINT(XK,YK)THEN390
410 TR(I,0)=XK:TR(I,1)=YK
420 NEXTI
430 REM key loc PRINT@87,TR(1,0);",";TR(1,1);
440 E=(20-LEN(RM$(LE)))/2:PRINT@480+E,"*"RM$(LE)"*";
450 REM *** REFERENCE PRINT@119,TR(2,0);TR(2,1);:PRINT@151,TR(3,0);TR(3,1);:PRINT@183,TR(4,0);TR(4,1);:PRINT@215,TR(5,0);TR(5,1);:PRINT@247,TR(6,0);TR(6,1);
460 RETURN
470 ZZ=PEEK(9)AND128:POKE49151,ZZ:POKE49151,128-ZZ:MS=RND(100)
480 IFMS<DF(LE,0)THENGOSUB500
490 RETURN
500 EN=RND(DF(LE,1)):SOUND129,4:SOUND121,4:SOUND155,2
510 GOSUB780:PRINT@21,;:M$="*ENCOUNTER*":GOSUB7
520 DD=32*1+21:M$=MN$(EN,0)
540 M$=M$+"! DO YOU CHOOSE TO "
550 M$=M$+"<1>RUN AWAY OR <2>FIGHT!":GOSUB4
560 I$=INKEY$:IF I$<>"1" AND I$<>"2"THEN560
570 IFI$="1"THENGOSUB740:IFRF<>1THENGOSUB8:RETURN
580 IFI$="1"THENM$="YOU CAN'T ESCAPE! ":GOTO610
581 M$="FIGHT IT IS! "
610 MH=VAL(MN$(EN,1))
620 A=0
630 IFMH<=0THENGOSUB1060:GOSUB810:RETURN
635 IFHE<=0THEN1160
640 IFATHEN670
650 M$=M$+MN$(EN,0)+":"+STR$(MH)+" YOU:"+STR$(HE):GOSUB3:PRINT@32*9+21,;:M$="  HIT <F>  ":GOSUB7
660 I$=INKEY$:IFI$<>"F"THEN660
670 A=-1:GOSUB970
680 GOSUB780
690 M$=MN$(EN,0)+" ATTACKS! "
700 GOSUB750
710 IFRF>1THENM$=M$+"BUT MISSES!":GOSUB3:GOSUB1030
720 IFMH>0ANDRF=1THENHP=INT(RND(HE*.25)):M$=M$+"HITS FOR"+STR$(HP)+" DAMAGE!":GOSUB3:HE=HE-HP:GOSUB820:GOSUB1030
730 GOSUB780:GOTO630
740 REM *** CHOOSE TO RUN
750 RF=RND(3)
760 RETURN
770 REM *** BATTLE/RUN WINDOW
780 GOSUB8:RETURN
810 REM *** HEALTH DISPLAY
820 PRINT@343,"HEALTH:";:PRINT@375,HE;:RETURN
830 REM *** TREASURE FOUND
840 SOUND121,4:SOUND149,4:SOUND161,4:SOUND87,4
850 GOSUB780
860 M$="YOU'VE FOUND A CHEST. "
870 M$=M$+"OPENING IT REVEALS: "
880 C=RND(9)
890 T=(32-LEN(TR$(C)))/2
900 M$=M$+TR$(C)
910 IFC<5THENH=10+RND(25):HE=HE+H
920 IFC>6THENH=5+RND(10):HE=HE-H
930 IFC=5ANDTR(1,0)<>0THENM$=M$+"KEY AT:"+STR$(TR(1,0))+","+MID$(STR$(TR(1,1)),2)
931 GOSUB3
940 PRINT@32*9+21,;:M$="HIT <SPACE>":GOSUB7
950 I$=INKEY$:IFI$<>" "THEN950
960 GOSUB8:GOSUB820:RETURN
970 REM *** ATTACK SUB
980 M$="YOU "+AT$(RND(12))+" THE "+MN$(EN,0)
990 HIT=RND(2)
1000 IFHIT=1THENM$=M$+" BUT MISS!":GOSUB3
1010 IFHIT=2THENHP=INT(RND(MH/2)):M$=M$+" FOR A HIT OF"+STR$(HP):GOSUB3:MH=MH-HP
1020 GOSUB1030:RETURN
1030 REM *** FIGHT/MSG WAIT
1040 FORI=1TO1200:NEXTI
1050 RETURN
1060 REM *** MONSTER DEFEATED
1070 M$=MN$(EN,0)+" DEFEATED!":GOSUB3:SOUND113,4:GOSUB9:SOUND113,4:GOSUB9:SOUND113,4:GOSUB9:SOUND161,4
1080 GOSUB8:M$="YOU SEARCH THE "+MN$(EN,0)+" AND FIND "
1090 IFTR(1,0)=0THENPOT=RND(2):GOTO1100
1091 POT=RND(3)
1100 IFPOT=1THENM$=M$+"NOTHING."
1110 IFPOT=2THENM$=M$+"HEALING SALVE!":HE=HE+20
1120 IFPOT=3THENM$=M$+"A NOTE: KEY AT"+STR$(TR(1,0))+","+MID$(STR$(TR(1,1)),2) 
1121 GOSUB3
1130 PRINT@32*9+21,;:M$="HIT <SPACE>":GOSUB7
1140 I$=INKEY$:IFI$<>" "THEN1140
1150 GOSUB8:RETURN
1160 REM *** player dead
1170 GOSUB780
1171 SOUND103,8:SOUND103,8:SOUND103,4:SOUND103,8
1173 SOUND129,8:SOUND125,4:SOUND125,8
1174 SOUND103,4:SOUND103,8:SOUND95,4
1175 SOUND103,12
1180 M$="** ALAS, YOU HAVE DIED ** "
1190 M$=M$+"RATS GNAW ON YOUR CORPSE.":GOSUB3
1200 GOTO1295
1210 GOSUB2000
1250 I$=INKEY$:IFI$<>CHR$(13)THEN1250
1260 CLS:RETURN
1270 REM *** end screen
1280 CLS
1281 PRINT"߯߯";
1282 PRINT"߯߯";
1283 PRINT"߯";
1284 PRINT"߯";
1285 PRINT"";
1286 PRINT"";
1287 PRINT"ϓ";
1288 PRINT"";
1289 PRINT"̟";
1290 PRINT"";
1291 PRINT"";
1292 PRINT"";
1293 PRINT"";
1294 PRINT"";:PRINT@448,"  YOU MADE IT OUT ALIVE!!"  
1295 PRINT@480,"  TRY AGAIN? (Y/N)  ";
1296 M$=INKEY$:IFM$="Y"THENRUN
1297 IFM$<>"N"THEN1296
1298 END
2000 CLS0
2001 PRINT"";
2002 PRINT"";
2003 PRINT"а";
2004 PRINT"";
2005 PRINT"";
2006 PRINT"PRESS ";
2007 PRINT"ENTER";
2008 PRINT"IF U";
2009 PRINT"DARE!";
2010 PRINT"";
2011 PRINT"";
2012 PRINT"";
2013 PRINT"";
2014 PRINT"";
2015 PRINT"";:RETURN

4000 CLS:PRINTTAB(5);:M$="MONSTERS IN THE MANOR":GOSUB7:PRINT
4001 M$="YOU MUST SURVIVE THE NIGHT AND ESCAPE THE MANSION FILLED WITH MONSTERS. "
4002 M$=M$+"USE <WASD> TO MOVE. YOU MUST SEARCH THE ROOMS AND CORRIDORS AND FIND THE KEY TO ESCAPE EACH LEVEL.":GOSUB1
4004 M$="YOU CAN CHOOSE <1>RUN AWAY OR <2>FIGHT. "
4005 M$=M$+"MONSTERS MIGHT BE CARRYING A NOTE WITH COORDINATES OF THE KEY OR A HEALING SALVE. "
4006 M$=M$+"THERE ARE 9 HIDDEN CHESTS ON EACH LEVEL THAT ALSO MIGHT CONTAIN THESE, OR SOME NOT-TOO NICE ITEMS.":GOSUB1
4007 PRINT@485,;:M$="HIT <SPACE> TO CONTINUE":GOSUB7
4008 IFINKEY$<>" "THEN4008
4010 GOTO10

4999 GOTO4999

5000 PRINT""
5001 PRINT""
5002 PRINT""
5003 PRINT""
5004 PRINT""
5005 PRINT""
5006 PRINT""
5007 PRINT""
5008 PRINT""
5009 PRINT""
5010 PRINT""
5011 PRINT""
5012 PRINT""
5013 PRINT""
5014 PRINT"";:RETURN

5100 PRINT""
5101 PRINT""
5102 PRINT""
5103 PRINT""
5104 PRINT""
5105 PRINT""
5106 PRINT""
5107 PRINT""
5108 PRINT""
5109 PRINT""
5110 PRINT""
5111 PRINT""
5112 PRINT""
5113 PRINT""
5114 PRINT"";:RETURN

5200 PRINT""
5201 PRINT""
5202 PRINT""
5203 PRINT""
5204 PRINT""
5205 PRINT""
5206 PRINT""
5207 PRINT""
5208 PRINT""
5209 PRINT""
5210 PRINT""
5211 PRINT""
5212 PRINT""
5213 PRINT""
5214 PRINT"";:RETURN

5300 PRINT""
5301 PRINT""
5302 PRINT""
5303 PRINT""
5304 PRINT""
5305 PRINT""
5306 PRINT""
5307 PRINT""
5308 PRINT""
5309 PRINT""
5310 PRINT""
5311 PRINT""
5312 PRINT""
5313 PRINT""
5314 PRINT"";:RETURN

5400 PRINT""
5401 PRINT""
5402 PRINT""
5403 PRINT""
5404 PRINT""
5405 PRINT""
5406 PRINT""
5407 PRINT""
5408 PRINT""
5409 PRINT""
5410 PRINT""
5411 PRINT""
5412 PRINT""
5413 PRINT""
5414 PRINT"";:RETURN

9991 REM MONSTERS IN THE MANOR
9992 REM FOR THE TRS-80 MC-10
9993 REM BY JIM MCCLELLAN 2020
9994 REM MAPS DRAWN WITH MCDRAW
9995 REM INTRO AND END SCREENS
9996 REM BY PAUL SHOEMAKER
9998 REM MC-10 EDITS JIM GERRIE
9999 GOTO9999
