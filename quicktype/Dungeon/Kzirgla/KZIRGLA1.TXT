0 GOTO2000
1 PRINTTAB((32-LEN(M$))/2)M$:RETURN
2 FORZ=1TO2000:NEXT:RETURN
3 C=0:LL=LEN(I$):FORZ=C1TOLEN(M$):IFMID$(M$,Z,LL)=I$THENC=Z:Z=255
4 NEXT:RETURN

8 FORZ=181TO161STEP-10:SOUNDZ,2:NEXT:SOUND149,2:SOUND142,2:SOUND129,2:SOUND113,2:RETURN
REM INSTR SUBROUTINE
REM INPUT VARIABLE: M$=STRING TO BE SEARCHED,I$=SEARCH TARGET,C1=BEGINING POSITION OF SEARCH
REM OUTPUT VARIABLE:C=POSITION OF SEARCH TARGET OR ZERO IF NOT FOUND

50 CLEAR200:DIMC,Z,C1,M$,I$,LL,DT$(20),AT$(10),IT(9),F(3),D(3)
51 S=16384:IT(9)=1:IT(4)=1:IT(5)=1:TP$="WARRIOR"
60 CLS:PRINT@32,;:M$="WELCOME MORTAL":GOSUB1:M$="TO THE WORLD OF KZIRGLA!":GOSUB1:PRINT
61 PRINT"BE THEE (W)ARRIOR OR (M)AGICIAN?":SOUND200,1:GOSUB830:IFA=77THENTP=1:TP$="MAGICIAN"
70 HP=RND(29):ST=30-HP:CLS:PRINT:PRINT"PLAY WITH THESE QUANTITIES(Y/N)? ";
71 PRINT"HIT POWER ...";HP," STRENGTH ....";ST:GOSUB830:IFA$="N"THEN70
80 PRINT:PRINT"SKILL LEVEL 1-9 (1=EASY 9=HARD)?";:GOSUB830:SL=11-(A-47)
81 IFSL<1ORSL>9THENPRINT"TRY AGAIN.":SOUND113,2:SOUND103,2:SOUND113,2:CLS:GOTO80
90 ST=ST-INT((SL*.75)+.5):HP=HP-10
100 FORX=0TO20:READDT$(X):NEXT
110 DATAZUBU,ILDOR,GREMLIN,DWARF,CRAWLER,LIZARD-MAN,BERSERKER,SKELETON,ZOMBIE,KCAHS-OIDAR,2CLUB
111 DATA2DAGGER,2MACE,0AXE,0SWORD,1PENDANT,WALL,GOLD,SILVER,JEWEL,CARPET
120 E$="":FORX=1TO60:E$=E$+" ":NEXT:FORX=0TO10:AT$(X)="":NEXT:FORX=0TO3:READD(X):NEXT:E1$=LEFT$(E$,25)
130 DATA1,32,-1,-32
140 CLS(0):GOSUB760:X=RND(4)-1:IT(X)=IT(X)+1
150 GOSUB620:PRINT@0,"HIT POWER:";HP,"STRENGTH:";ST:TR=IT(6)+IT(7)*5+IT(8)*10
151 PRINT@32,"SUB LEVEL:";DL,"TREASURE:";TR:PRINT@64,E$:PRINT@66,"COMMAND: ";:GOSUB850
152 IFA=255THENAK=1:GOTO150
153 AK=0
160 IFA=9THENNL=PL+1:ED=0:PRINT"GO EAST":GOTO180
161 IFA=8THENNL=PL-1:ED=2:PRINT"GO WEST":GOTO180
162 IFA=10THENNL=PL+32:ED=1:PRINT"GO SOUTH":GOTO180
163 IFA=94THENNL=PL-32:ED=3:PRINT"GO NORTH":GOTO180
164 IFA=73THEN610
170 IFA=65ORA=32THENPRINT"ATTACK":GOTO320
171 IFA=88THEN730
172 IFA=66THENPRINT"BRIBE":GOTO950
173 IFA=70THENPRINT"FLY":GOTO1000
174 GOTO150
180 X=PEEK(S+NL):IFX<>252THEN190
181 PRINT"GOING DOWN TO LEVEL"DL+1:POKES+PL,128:FORX=1TO7:Z=PEEK(9)AND128:POKE49151,Z:POKE49151,128-Z:FORC=1TO350:NEXT:NEXTX
182 GOSUB760:GOTO150
190 IFX=128ORX=239THENSOUND221,1:POKES+PL,128:OL=PL:PL=NL:SOUND100,1:POKES+PL,174:GOTO200
191 GOTO300
200 IFX<>239THEN270
201 PRINT@96,"YOU HAVE FOUND ";:X=RND(3):ONXGOTO210,220,240
210 PRINT"A trap door";:POKEPL+S,128:FORX=1TO4:SOUND142,3:SOUND113,3:NEXTX
211 HP=HP-(20+RND(10)):IFHP<=0THEN730
212 T=PL:GOSUB760:POKES+PL,128:PL=T:POKES+PL,174:GOTO150
220 PRINT"POISONOUS GAS!":ST=ST-(5+RND(5))
221 FORX=1TO15:Z=PEEK(9)AND128:POKE49151,Z:POKE49151,128-Z:FORC=1TO25:NEXT
230 NEXTX:IFST<=0THEN730
231 GOTO150
240 PRINT"A TREASURE CHEST!":SOUND185,2:SOUND193,8:FORZ=1TO1500:NEXT
241 PRINT@96,E$;:PRINT@96,"IN THE CHEST YOU FIND:":FORX=1TORND(5)
250 X1=RND(11)-1:IFX1=6THEN250
251 PRINT@128,E1$;:PRINT@128,"A ";:IFX1<6THENPRINTMID$(DT$(X1+10),2);:IT(X1)=IT(X1)+1:GOTO260
252 PRINTDT$(X1+10);:IT(X1-1)=IT(X1-1)+1
260 SOUND206,2:GOSUB2:NEXTX:PRINT@128,E1$;:GOTO150
270 FORX=0TO3:EV=PEEK(S+OL+D(X)):IFEV<182OREV>238THEN290
271 NL=OL+D(X)+D(ED):IFPEEK(S+NL)=128THENPOKES+OL+D(X),128:GOSUB8:POKES+NL,EV
272 GOTO290
280 FORX1=1TO10:M$=AT$(X1):I$="@":C1=1:GOSUB3
281 IFVAL(AT$(X1))=OL+D(X)THENAT$(X1)=STR$(NL)+MID$(AT$(X1),C):X1=10
282 NEXTX1
290 NEXTX:GOTO150
300 PRINT"UGG, I RAN INTO A ";:IFX=159ORX=143ORX<128THENX=198
310 PRINTDT$(X-182);:PRINT"!!":SOUND73,6:SOUND25,6:PRINT"WANT TO ATTACK IT (Y/N)?"
311 GOSUB830:PRINT@96,E$:IFA=89THENPRINT@96,;:AL=NL:GOSUB350:GOTO370
312 GOTO150
320 ER=0:GOSUB330:IFER=1THEN150
321 GOTO360
330 PRINT"WHICH DIRECTION? ";:GOSUB830:IFA=9THENAL=PL+1:ED=0:PRINT"EAST":GOTO350
331 IFA=8THENAL=PL-1:ED=2:PRINT"WEST":GOTO350
332 IFA=10THENAL=PL+32:ED=1:PRINT"SOUTH":GOTO350
340 IFA=94THENAL=PL-32:ED=3:PRINT"NORTH":GOTO350
341 PRINT:PRINT"WHAT?":GOSUB8:PRINT@96,E$;:ER=1:RETURN
350 EV=PEEK(S+AL):RETURN
360 IFEV=128OREV=239THENPRINT@128,"NOTHING TO ATTACK!";:SOUND206,2:GOSUB2:PRINT@96,E$;:GOTO150
370 PRINT"WITH WHAT? ";:GOSUB830:IFA=72THENM=1:PRINT"HANDS":GOTO410
380 FORX=10TO15:IFA$=MID$(DT$(X),2,1)THEN385
381 GOTO400
385 IFIT(X-10)=0THENPRINT@128,"YOU DON'T HAVE ONE!":FORX=1TO22:SOUND1,1:NEXTX:?@96,E$;:PRINT@96,;:GOTO370
390 X1=VAL(LEFT$(DT$(X),1)):IFX1=TPORX1=2THENM=1+(.25*(X-9)):PRINTMID$(DT$(X),2);:GOTO410
391 PRINT@128,"YOU CANT'T USE THAT!";:FORX=1TO22:SOUND1,1:NEXT:PRINT@96,E$;:GOTO150
400 NEXTX:M=1:PRINT"HANDS";
410 W=X:FORX=0TO350:NEXT:IFW<15ORM=1THEN465
411 PRINT@128,"WHICH SPELL, 1 OR 2?":GOSUB830:IFA=49THEN450
420 FORX=ALTO900*SGN(D(ED))STEPD(ED):IFPEEK(S+X)<128ORPEEK(S+X)=143ORPEEK(S+X)>239THEN430
421 POKES+X,239:SOUND215,1:NEXT
430 IFX<>ALTHENX1=X-D(ED):FORX=ALTOX1STEPD(ED):POKES+X,128:NEXT
440 IFRND(100)<50+SLTHEN580
441 PRINT@128,"THE PENDANT BURNED OUT!"
442 FORX=1TO2:SOUND113,1:SOUND113,1:SOUND129,1:SOUND142,1:SOUND149,1:SOUND113,1:SOUND103,1:NEXT
443 IT(5)=IT(5)-1:GOTO580
450 FORX2=0TO3:EV=PEEK(S+PL+D(X2)):IFEV<129OREV=252OREV=143OREV=239THEN460
451 AL=PL+D(X2):GOSUB520:FORX1=0TO350:NEXT
460 NEXT:GOTO440
465 IFM=2.25THENM=3
470 PRINT@128,"":PRINT@128,"I ATTACK AND I ";:FORX1=1TO15:SOUND150,1:NEXT
471 ES=INT(((EV-181)*DL+DL^2)/2)+RND(10):EP=SL*(4-39/9)+39/9:IFEV=159OREV=143OREV<128THENEP=150:ES=50
480 FORX=0TO10:M$=AT$(X):I$="@":C1=1:GOSUB3:IFVAL(AT$(X))=ALTHENEP=VAL(MID$(AT$(X),C+1)):X=10
482 NEXTX
490 IFRND(100)<100*(ST/ES)THEN495
491 PRINT"MISS!":GOSUB940:GOTO580
495 PRINT"HIT!":GOSUB930:EP=EP-ST*M:IFM=1THEN510
500 IFRND(10+SL)=1THEN505
501 GOTO510
505 PRINT@128,"OOPS, MY "MID$(DT$(W),2)" JUST BROKE!"
506 FORX=1TO15:SOUND50,1:NEXT:IT(W-10)=IT(W-10)-1:FORX=0TO1000:NEXT
510 IFEP>0OREV=143OREV<129THEN590
511 GOSUB520:GOTO580
520 PRINT@96,E$;:PRINT@96,"ANOTHER ";:IFEV=159THENPRINT"WALL";:GOTO530
521 PRINTDT$(EV-182);
530 PRINT" BITES THE","DUST!!"
531 FORX=1TO15:POKES+AL,128+16*(RND(8)-1)+6
532 SOUNDRND(50),1:POKES+AL,128+16*(RND(8)-1)+9:SOUND50+RND(50),1:NEXT:POKES+AL,128:GOSUB1030
540 IFEV=159THENRETURN
541 HP=HP+RND((EV-181)*2)+EV-181+SL:PRINT@96,"ON THE BODY I FOUND:"
542 IFRND(EV-181)-1=0THENPRINT"NOTHING.":SOUND1,8:GOSUB2:RETURN
543 PRINT"A ";:TG=RND(EV-181):IFTG>6THENTG=7-RND(3)
550 PRINTMID$(DT$(TG+9),2):IT(TG-1)=IT(TG-1)+1:SOUND206,2:GOSUB2:FORX=1TO3:IFRND(EV-181)<X*2THENRETURN
551 AM=RND(EV-(181+X)):FORX1=0TO500:NEXT:PRINT@128,MID$(STR$(AM),2)" ";:M$=DT$(X+16)
560 PRINTM$;:IFAM>1ANDM$<>"GOLD"ANDM$<>"SILVER"THENPRINT"S";
570 PRINT:SOUND206,2:GOSUB2:IT(X+5)=IT(X+5)+AM:NEXT:RETURN
580 FORX=0TO350:NEXT:PRINT@96,E$;:GOTO150
590 IFEV=143OREV<128THEN580
591 FORX=0TO10:IFVAL(AT$(X))=ALTHEN600
592 NEXTX:FORX=0TO10:IFAT$(X)=""THEN600
593 NEXTX:X=0
600 AT$(X)=STR$(AL)+"@"+STR$(EP):GOTO580
610 PRINT@0,E$;E$;:PRINT@0,;:FORX=10TO15:A$=MID$(DT$(X),2):PRINTA$":";
611 PRINTLEFT$(E$,7-LEN(A$))IT(X-10),:NEXT:FORX=17TO20:PRINTDT$(X)":";
612 PRINTLEFT$(E$,7-LEN(DT$(X)))IT(X-11),:NEXT:GOSUB830:PRINT@0,E$;E$;E$;:GOTO150
620 NM=300+SL*30
630 IFRND(NM)>DL*3THEN635
631 FORX=0TO3:F(X)=0:NEXT:GOTO640
635 IFAK=1THEN680
636 RETURN
640 DR=RND(4)-1:IFF(0)+F(1)+F(2)+F(3)=4 THEN680
650 IFPEEK(S+PL+D(DR))<>128THENF(DR)=1:GOTO640
651 EL=PL+D(DR)
660 EV=RND(INT(DL/1.2+1.8)):IFEV>10THENEV=EV-3
670 POKES+EL,EV+181:AK=1:NM=INT(NM*.666):GOTO 630
680 AF=0:FORX=0TO3:EV=PEEK(S+PL+D(X)):IFEV<182THEN720
690 IFEV=252OREV=239THEN720
691 AF=AF+1
700 PRINT@96,"A "DT$(EV-182)" IS ATTACKING!":
701 FORX1=1TO15:SOUND99-X1,1:NEXT
702 PRINT@96,"AND IT ";:ES=INT(((EV-181)*DL+RND(DL*2))/3)+1
703 IFRND(100)<100*(ES/ST)+10THEN710
704 PRINT"MISSED!":GOSUB940:GOTO720
710 PRINT"HITS!":GOSUB930:HP=HP-ES:IFHP<1THEN730
711 FORX1=0TO350:NEXT
720 NEXT:PRINT@96,E$;:AK=0:RETURN
730 CLS:PRINT:PRINT"WE MOURN THE PASSING OF YET":PRINT"ANOTHER BRAVE ";:IFTP=0THENPRINT"WARRIOR.":GOTO740
731 PRINT"MAGICIAN."
740 SOUND50,8:SOUND50,8:SOUND50,4:SOUND50,8:SOUND83,8:SOUND73,8:SOUND73,8
741 SOUND50,8:SOUND50,8:SOUND38,4:SOUND50,8:GOSUB900
750 PRINT:PRINT"DO YOU DARE TRY AGAIN (Y/N)? ";:GOSUB830:IFA$="Y"THENRUN50
751 END
760 FORX3=0TO10:A$(X)="":NEXT:DL=DL+1:IFDL=13THEN860
761 HP=HP+10:ST=ST+INT((SL*.75)+.5)
770 CLS0:FORX=480TO511:POKES+X,143:NEXT:FORX=192TO448STEP32:POKES+X,143:POKEX+S+31,143:NEXT
771 FORX=0TO191:POKES+X,143:NEXT
780 FORX=1TO50+DL*5
790 X1=RND(325)+193:IFPEEK(S+X1)=128THENPOKES+X1,159:NEXT:GOTO800
791 GOTO790
800 FORX=1TO5+RND(5):X1=RND(325)+193:IFPEEK(S+X1)=128THENPOKES+X1,239
810 NEXT:X=RND(9)*32+191:POKES+X,252:POKES+X-1,128:POKES+X-2,128:PRINT@484,TP$;"    SKILL LEVEL"10-SL;
820 PL=RND(9)*32+160:IFPL=X+1THEN820
821 POKES+PL,174:POKES+PL+1,128:POKES+PL+2,128:RETURN
830 A$=INKEY$:IFA$<>""THEN830
831 A$=INKEY$:R=RND(99):IFA$=""THEN831
832 IFA$="A"THENA=8:RETURN
833 IFA$="D"THENA=9:RETURN
834 IFA$="W"THENA=94:RETURN
835 IFA$="S"THENA=10:RETURN
836 A=ASC(A$):SOUND206,2:RETURN

840 A$=INKEY$:IFPEEK(2)AND2ANDNOTPEEK(16946)THENA=73:Z=99:RETURN
841 IFPEEK(2)AND8ANDNOTPEEK(16952)THENA=65:Z=99:RETURN
842 IFPEEK(2)AND4ANDNOTPEEK(16952)THENA=94:Z=99:RETURN
843 IFPEEK(2)AND1ANDNOTPEEK(16946)THENA=8:Z=99:RETURN
844 IFPEEK(2)AND4ANDNOTPEEK(16948)THENA=10:Z=99:RETURN
845 IFPEEK(2)AND1ANDNOTPEEK(16949)THENA=9:Z=99:RETURN
846 IFA$<>""THENA=ASC(A$):Z=99:RETURN
847 RETURN
850 FORZ=1TOSL*2:GOSUB840:NEXT:IFZ>99THENRETURN
852 A=255:RETURN

860 CLS:M$="**  Y O U   D I D   I T  **":GOSUB1:GOSUB2:CLS7
861 PRINT"îìììììíïïïïïììííïíììíïïïïïïïïïïï";
862 PRINT"êÆÏÏÏÉåïïïïïïåïåïååïïïïïïïïïïïïï";
863 PRINT"êÏà“àÏåïïïïïïåïáãåáãïïïïïïïïïïïï";
864 PRINT"êÏ•àšÏåïïïïïïåïåïåäìíïïïïïïïïïïï";
865 PRINT"êÏàœàÏåïïïïìíïìíîììîìíîììîììîìíï";
866 PRINT"êÉÏÏÏÆáíïïêïëêïëêïïêïêïêïêïïêïêï";
867 PRINT"ëããããáçáíïïãæêïïêãçêãçïêïêãçêâçï";
868 PRINT"ïïïïïëáááíëìéëìéêììêïïïêïêììêïæï";
869 PRINT"ïïïïïïëääèíïïïïïïïïïìíïìíïïïïïïï";
870 PRINT"ïïïïïïïëäìàíïïïïïïïïêïêïëïïïïïïï";
871 PRINT"ïïïïïïïïëíåáíïïïïïïïêïïãæïïïïïïï";
872 PRINT"ïïïïïïïïïëääèíïïïïïïèíëìéïïïïïïï";
873 PRINT"ïïïïïïïïïïëäìàíïîïîïìíîïîîìíïìíï";
874 PRINT"ïïïïïïïïïïïëíåáíëíéêïêêïêêïêêïëï";
875 PRINT"ïïïïïïïïïïïïëääéïêïêïêêïêêâçïãæï";
876 PRINT"ïïïïïïïïïïïïïëéïïêïëìéëìéêïæëìé";:GOSUB2:GOSUB2:GOSUB2:CLS
890 PRINT:PRINT"YOU HAVE RECOVERED THE POWERFUL SCEPTER AND TAKEN THE KINGDOM",
891 PRINT"BACK FROM THE EVIL MAGICIAN!!":PRINT:PRINT" all HAIL THE KING!":GOSUB900:GOTO750
900 PRINT:PRINT:PRINT"PRESS ANY KEY TO CONTINUE":GOSUB830:CLS
910 PRINT@71,"FINAL INVENTORY":FORX=10TO15:A$=MID$(DT$(X),2):PRINTA$":";
911 PRINTLEFT$(E$,7-LEN(A$))IT(X-10),:NEXT:FORX=17TO20:PRINTDT$(X)":";
912 PRINTLEFT$(E$,7-LEN(DT$(X)))IT(X-11),:NEXT:PRINT:PRINT"HIT POWER:"HP,"STRENGTH:"ST:PRINT
920 RETURN
930 FORX1=1TO5:SOUND113,1:SOUND149,1:NEXT:RETURN
940 SOUND229,1:SOUND225,1:SOUND221,1:FORZ=1TO25:NEXT
941 SOUND200,1:SOUND193,1:SOUND185,1:FORZ=1TO25:NEXT
942 SOUND142,1:SOUND129,1:SOUND113,1:FORZ=1TO25:NEXT:RETURN
950 ER=0:GOSUB330:IFER=1THEN150
951 IFEV=128OREV=159OREV=143OREV>200THENPRINT"NOTHING TO BRIBE!":SOUND215,2:SOUND219,2:PRINT@96,E$;:GOTO150
952 EV=EV-181:IFRND(120)<EV*10THENPRINT"NO WAY, SUCKER!":GOSUB940:PRINT@96,E$;:GOTO150
960 FORX1=0TO350:NEXT:PRINT@96,E$;:PRINT@96,"";:INPUT"HOW MUCH DO YOU OFFER";OF
961 PRINT@96,E$;:PRINT@96,"THE ";DT$(EV-1);" SAYS:"
962 IFOF<RND(EV*10)+5THENPRINT"FORGET IT!":SOUND113,2:SOUND103,2:SOUND113,2:GOTO150
963 PRINT"I'LL TAKE IT!":SOUND206,2:GOSUB2
970 IFIT(8)*10+IT(7)*5+IT(6)<OFTHEN975
971 FORX1=2TO1STEP-1:X2=INT(OF/X1*5):IFIT(X1+6)<X2THENX2=IT(X1+6)
974 GOTO980
975 PRINT@96,E$;:PRINT@96,"BUT I DON'T HAVE THAT MUCH!"
976 SOUND113,2:SOUND103,2:SOUND113,2:GOTO960
980 IT(X1+6)=IT(X1+6)-X2:OF=OF-X2*X1*5:NEXT:IT(6)=IT(6)-OF
990 FORX1=0TO350:NEXT:PRINT@96,E$;:POKEAL+S,128:GOSUB1030:GOTO150
1000 OV=128:IFIT(9)=0THENPRINT@96,"HOW? I DON'T HAVE WINGS!":SOUND113,2:SOUND103,2:SOUND113,2:PRINT@96,E$;:GOTO150
1010 IFTP=1THEN1015
1011 IFRND(99)<50THENED=0:GOTO1020
1012 ED=RND(3):GOTO1020
1015 ER=0:PRINT@96,E$;:PRINT@96,"";:GOSUB330:IFER=1THENIT(9)=IT(9)-1:GOTO150
1016 GOTO1020 
1020 A$=INKEY$:IFA$<>""THEN1025
1021 POKEPL+S,175:NL=PL+D(ED):X=PEEK(NL+S):IFX=159ORX=143ORX>239ORX<128THENPOKEPL+S,174:GOTO1010
1022 Z=PEEK(9)AND128:POKE49151,Z:POKE49151,128-Z:POKEPL+S,OV:OV=X:PL=NL:GOTO1020
1024 GOTO1030
1025 POKEPL+S,174:IFTP=1THEN1010
1026 PRINT@96,E$;:IT(9)=IT(9)-1:GOTO150
1030 FORX3=0TO10:IFVAL(AT$(X3))=ALTHENAT$(X3)="":RETURN
1031 NEXT:RETURN

2000 CLS6:PRINT@64,"ßßßßßßÞßÞÞÜÜßÜÝÞÜÝßÜÜÞßßßÞßßßßßß";
2001 PRINT"ßßßßßßÚÞ×ßßÙßÚßÚßÚÚßßÚßßÞ×Ößßßßß";
2002 PRINT"ßßßßßßÚÖßßÙßßÚßÚÒ×ÚßÒÚßßÚÜØßßßßß";
2003 PRINT"ßßßßßßÚßÖÚÜÜßØÝÚßÖÛÜØÚÜÜÚßÚßßßßß";:PRINT@224,;
2004 PRINT"ßßßßßßßßßßßßß¿¿¿¿¿¿¿ßßßßßßßßßßßß";
2005 PRINT"ßßßßßßßßßßß¿¿ÿÿÿÿÿÿÿ¿ßßßßßßßßßßß";
2006 PRINT"ßßßßßßßßßß¿ÿÿŸŸŸŸŸŸÿÿ¿ßßßßßßßßßß";
2007 PRINT"ßßßßßßßßß¿ÿÿŸŸŸŸÿ¿ßßßßßßßßß";
2008 PRINT"ßßßßßßßßß¿ÿŸßßßßŸÿÿ¿ßßßßßßßß";
2009 PRINT"ßßßßßßßß¿ÿŸŸß¯¯¯¯ßßŸÿ¿ßßßßßßßß";
2010 PRINT"ßßßßßßßß¿ÿŸß¯ïïïï¯ßŸÿÿ¿ßßßßßßß";
2011 PRINT"ßßßßßßß¿ÿŸß¯ïïßßïï¯ßŸÿ¿ßßßßßßß";
2012 PRINT"ßßßßßßß¿ÿŸß¯ïßßßßï¯ßŸÿ¿";:PRINT@41,"theßscepterßof";
2050 GOSUB2:GOSUB2:GOTO50
2060 REM WRITTEN BY PAUL PENROSE
2070 REM (C) 1982 RAINBOW
2080 REM CONNECTION SOFTWARE
2090 REM MC-10 EDITS JIM GERRIE
