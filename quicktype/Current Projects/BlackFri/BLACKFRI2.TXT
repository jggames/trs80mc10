0 CLS:CLEAR500:DIMCC,ZZ,M$,I$,MC:MC=16384:GOTO50
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN

10 M$="HIT A KEY":GOSUB7
11 IFINKEY$=""THEN11
12 FORCC=1TO9:PRINTCHR$(8);:NEXT:RETURN

50 GOSUB8000:GOTO900

100 REM--CALCULATE NEW TREND--
110 LT=INT((1/(1-RND(0)^2)))
120 IF LT>7 THEN LT=7
130 EY=0
140 BIAS=INT((1/(1-RND(0)^2)))
150 IF BIAS>7 THEN BIAS=7
160 IF RND(0)>.5 THEN BIAS=0-BIAS
170 PRINT "LENGTH=";LT;"   BIAS=";BIAS
180 RETURN

200 REM--STOCK SPLIT--
210 PRINT
220 PRINT S$(X);" STOCK SPLIT"
230 SP(X)=75
240 NC(X)=0
250 FOR Q=1 TO NP:PF(Q,X)=PF(Q,X)*2:NEXT Q
260 RETURN

300 REM--BANKRUPT--
301 PRINT
302 PRINT S$(X);" WENT BANKRUPT"
303 PRINT "ALL HOLDINGS ARE LIQUIDATED AT $1 PER SHARE"
304 PRINT "A NEW STOCK WILL TAKE ITS PLACE ON THE 'BIG BOARD'"
305 PRINT
306 NS=NS+1
307 IF NS>=17 THEN PRINT "CROAK - GAME OVER":YEAR=10000:RETURN:REM FORCE END OF GAME
310 S$(X)=S$(NS)
340 A$(X)=A$(NS)
350 PRINT "THE NEW STOCK IS: ";S$(X)
351 PRINT "ITS ABBREVIATION IS: ";A$(X)
352 SP(X)=INT((16*RND(0))+92)
353 PRINT "ITS STARTING PRICE IS: $";SP(X)
354 NC(X)=0
355 FOR Q=1 TO NP
356 CA(Q)=CA(Q)+PF(Q,X):PF(Q,X)=0:NEXT Q:RETURN

600 REM--CALCULATE DOW JONES AVERAGE--
610 TP=0
620 FOR X=1 TO 10:TP=TP+SP(X):NEXT X
630 OD=DJ:DJ=TP:DF=OD-DJ
640 IF DF>=0 THEN Q$="DOWN":GOTO650
641 Q$="UP"
650 DF=ABS(DF):RETURN

700 REM--LIQUIDATE--
710 FOR Y=1 TO 10
720 CA(X)=CA(X)+SP(Y)*PF(X,Y)
730 NEXT Y
740 RETURN

750 REM--FIND THE WINNER--
760 HI=0
770 FOR X=1 TO NP
771 IF CA(X)>HI THEN WINNER=X:HI=CA(X)
772 NEXT X
780 RETURN

900 REM MAIN FOR / NEXT LOOP
910 FOR YEAR=1 TO NY
920 GOSUB 2000:REM CALC NEW MARKET
930 IF YEAR<NY THEN GOSUB 1000:REM REM BUY AND SELL
940 NEXT YEAR
950 GOSUB 3000:REM   END OF GAME
960 END

1000 REM--BUY AND SELL--
1010 FOR X=1 TO NP
1030 M$="PLAYER "+N$(X)+" YOU NOW HAVE $"+STR$(CA(X)):GOSUB1
1060 OF=0
1070 FOR Y=1 TO 10:IF PF(X,Y)>0 THEN OF=1
1071 NEXT Y
1080 IF OF=1 THEN GOSUB 5000:REM SELL RTN
1081 OF=0
1090 FOR Y=1 TO 10:IF CA(X)>=SP(Y) THEN OF=1
1091 NEXT Y
1100 IF OF=1 THEN GOSUB 6000:REM BUY RTN
1110 NEXT X:RETURN

2000	REM-----NEW YEAR ON THE MARKET-----------------------
2010 EY=EY+1
2020 IF EY>LT THEN GOSUB 100:REM CALC NEW TREND
2030 FOR X=1 TO 10
2040 CHANGE=(1/(1-RND(0)^2)):REM CHANGE FUNCTION
2050 IF CHANGE>15 THEN CHANGE=15
2060 IF RND(0)>.5 THEN CHANGE=0-CHANGE:REM 50/50 IT'S MINUS
2070 NC(X)=INT((CHANGE*FL(X))+BIAS)
2080 SP(X)=SP(X)+NC(X)
2090 NEXT X

2100 GOSUB 600:REM CALC DOW JONES

2110 FOR X=1 TO 10
2120 IF SP(X)>=150 THEN GOSUB 200:REM STOCK SPLITS
2130 IF SP(X)<=0 THEN GOSUB 300:REM STOCK BANKRUPTS
2140 NEXT X

2150 FOR X=1 TO 10
2160 YD(X)=INT(SP(X)/DF(X))
2170 FOR Y=1 TO NP
2180 DV(Y)=DV(Y)+YD(X)*PF(Y,X)
2190 NEXT Y:NEXT X

2200 PRINT
2210 PRINT
2220 PRINT "--------- YEAR ";YEAR;"---------"
2230 M$="DOW JONES INDUSTRIAL AVERAGE "+Q$+" "+STR$(DF)+" TO"+STR$(DJ):GOSUB1
2250 PRINT "STOCK NET CURR. DIVI- PLAYERS"
2260 PRINT "ABBR. CHG PRICE DEND  ";    
2270 FOR X=1 TO NP
2280 PRINT LEFT$(N$(X),2)" ";
2290 NEXT X
2300 PRINT
2310 FOR X=1 TO 10
2320 PRINT A$(X);TAB(6);NC(X);TAB(10);SP(X);TAB(16);YD(X);TAB(22);
2360 FOR Y=1 TO NP
2370 PRINT MID$(STR$(PF(Y,X)/10)+"  ",2,2)" ";
2380 NEXT Y
2390 PRINT
2400 NEXT X:GOSUB10

2500 PRINT "DIVIDENDS FOR THE YEAR:";
2510 FOR X=1 TO NP
2520 PRINT DV(X);
2530 NEXT X
2540 PRINT
2550 FOR X=1 TO NP
2560 PRINT TAB((X-1)*9+42);"______";
2570 NEXT X
2580 PRINT

2600 PRINT "PLAYERS NEW CASH TOTAL:";
2610 FOR X=1 TO NP
2620 CA(X)=CA(X)+DV(X)
2630 PRINT TAB((X-1)*9+42);CA(X);
2640 NEXT X
2650 PRINT " "

2660 FOR X=1 TO NP
2670 DV(X)=0
2680 NEXT X
2690 RETURN

3000	REM--E N D      O F      G A M E--
3010 FOR X=1 TO NP
3020 GOSUB 700:REM LIQUIDATE
3030 NEXT X
3040 GOSUB 750:REM GET WINNER
3050 PRINT
3060 PRINT
3070 PRINT "HERE IS THE FINAL ACCOUNTING AFTER ";YEAR-1;"YEARS"
3080 PRINT
3090 PRINT
3100 FOR X=1 TO NP
3110 IF X=WINNER THEN WINNER$="WINNER!":GOTO3120
3111 WINNER$=""
3120 PRINT N$(X);" $";CA(X);
3130 PRINT WINNER$
3140 NEXT X
3150 PRINT
3160 PRINT "-------------------------------"
3170 PRINT
3180 RETURN
3190 REM--SUBROUTINES--

5000 REM--S E L L--
5010 PRINT "SELL ANY STOCKS ";
5020 INPUT ANS$
5030 IF LEFT$(ANS$,1)="N" THEN RETURN
5040 IF LEFT$(ANS$,1)<>"Y" THEN 5000
5050 PRINT "STOCK ";
5060 INPUT ST$
5070 GOSUB 7000:REM RECOGNIZE STOCK NAME

5100 REM
5110 PRINT "HOW MANY SELLING ";
5120 INPUT SELL
5130 IF SELL<=0 THEN 5000
5140 IF SELL>PF(X,N) THEN PRINT "YOU ONLY HAVE ";PF(X,N);"SHARES" :GOTO 5100
5150 PF(X,N)=PF(X,N)-SELL
5160 RESALE=SELL*SP(N)
5170 PRINT "RESALE VALUE OF $";RESALE
5180 CA(X)=CA(X)+RESALE
5190 GOTO 5000

6000	REM--B U Y--
6010 PRINT "BUY ANY STOCKS ";
6020 INPUT ANS$
6030 IF LEFT$(ANS$,1)="N" THEN RETURN
6040 IF LEFT$(ANS$,1)<>"Y" THEN 6000
6050 PRINT "STOCK ";
6060 INPUT ST$
6070 GOSUB 7000:REM RECOGINIZE STOCK NAME

6100 REM
6110 PRINT "HOW MANY BUYING ";
6120 INPUT BUY
6130 IF BUY<=0 THEN 6000
6140 IF BUY*SP(N)>CA(X) THEN ?"YOU ONLY HAVE $";CA(X):?"THAT'S ENOUGH FOR ";:?INT(CA(X)/SP(N));"SHARES":GOTO6100
6150 CO=BUY*SP(N)
6160 PRINT "THAT COST $";CO
6170 CA(X)=CA(X)-CO
6180 PF(X,N)=PF(X,N)+BUY
6190 GOTO 6000

7000 REM--FIND STOCK NAME--
7010 FOR N=1 TO 10:IF ST$=A$(N) THEN RETURN
7020 NEXT N
7030 M$="THAT STOCK IS NOT TRADED ON THE 'BIG BOARD'":GOSUB1
7040 PRINT "THESE ARE THE CURRENT STOCKS:"
7060 GOSUB 7100:REM PRINT STOCK NAMES AND ABBR
7080 PRINT "STOCK ";
7090 INPUT ST$
7095 GOTO 7000

7100 REM--PRINT THE STOCK NAMES AND ABBR--
7110 PRINT
7120 FOR M=1 TO 10
7130 PRINT S$(M)TAB(26)A$(M)
7140 NEXT M
7150 RETURN

8000 DIM N$(4),S$(16),A$(16),FL(16),DF(16),CA(4),DV(4),PF(4,10),SP(10),NC(10)
8010 NS=10
8020 DIM YD(10)
8030 ESC$ = CHR$(27):LO$ = ESC$ + CHR$(108):LF$ = ESC$ + CHR$(109)
8040 CU$="W":CD$="Z":CR$="S":CL$="A":RU$="S":RD$="T"
8050 DJ=987:EY=0:LT=0
8060	S$(1)="HIGHWAY IMPROVEMENT BONDS"
8061	S$(2)="OLD WEST LAND CO."
8062	S$(3)="FINE AND DANDY CANDY CO."
8063	S$(4)="GENTLE TOUCH MASSAGE, INC"
8064	S$(5)="SQUARE WHEEL BICYCLE CO."
8065	S$(6)="BITE SHOPS, INC."
8066	S$(7)="GOOD KARMA SHOE CO."
8067	S$(8)="MAKE-A-BUCK HOLDING CORP."
8068	S$(9)="SLIPPERY OIL COMPANY"
8069	S$(10)="CONSOLIDATED INVESTMENTS"
8070	S$(11)="GUARD-ALL ALARM SYSTEMS"
8071	S$(12)="STUDEBAKER MOTORS"
8072	S$(13)="TREE FROG BREWERY"
8073	S$(14)="WINNEMUCCA MUNI BONDS"
8074	S$(15)="U.S. ROBOTS, INC."
8075	S$(16)="BONDO CEMENT CONTRACTORS"

8080 A$(1)="HIGH"
8081	A$(2)="LAND"
8082	A$(3)="CANDY"
8083	A$(4)="GENTL"
8084	A$(5)="BIKE"
8085	A$(6)="BITE"
8086	A$(7)="SHOE"
8087	A$(8)="BUCK"
8088	A$(9)="OIL"
8089	A$(10)="INVES"
8090	A$(11)="ALARM"
8091	A$(12)="STUDE"
8092	A$(13)="BREW"
8093	A$(14)="MUNI"
8094	A$(15)="ROBOT"
8095	A$(16)="CEMEN"

8100 FL(1)=0.25:FL(2)=2.5:FL(3)=1.75:FL(4)=1.0:FL(5)=1.25:FL(6)=2.25:FL(7)=2.5:FL(8)=1.75:FL(9)=0.75
8101 FL(10)=2.0:FL(11)=1.0:FL(12)=0.5:FL(13)=2.25:FL(14)=1.75:FL(15)=1.25:FL(16)=1.5:DF(1)=20
8102 DF(2)=60:DF(3)=50:DF(4)=35:DF(5)=40:DF(6)=55:DF(7)=60:DF(8)=50:DF(9)=30:DF(10)=53:DF(11)=29
8103 DF(12)=45:DF(13)=38:DF(14)=50:DF(15)=22:DF(16)=30

8200 PRINT "B L A C K   F R I D A Y"
8201 PRINT "-----------------------"
8202 PRINT:PRINT
8210 PRINT "WANT INSTRUCTIONS (Y OR N)";
8211 INPUT ANS$
8220 IF LEFT$(ANS$,1)="Y" THEN GOSUB 9000:GOTO 8240
8221 IF LEFT$(ANS$,1)<>"N" THEN PRINT "WHAT DID YOU SAY ?" GOTO 8210
8230 REM
8240 PRINT "NUMBER OF PLAYERS (1 TO 3) ";
8250 INPUT NP
8260 IF NP<1 OR NP>3 THEN PRINT "ONLY 1, 2, OR 3 CAN PLAY":GOTO 8240
8270 FOR X=1 TO NP:PRINT:PRINT "PLAYER #";X,,"ENTER YOUR NAME (8 CHARS)";
8280 INPUT N$(X)
8290 N$(X)=LEFT$(N$(X),8)
8300 NEXT X
8400 REM
8410 PRINT "NUMBER OF YEARS (3 TO 50) ";
8420 INPUT NY
8430 IF NY<3 OR NY>50 THEN PRINT "ANYWHERE FROM 3 TO 50" :GOTO 8400
8440 FOR X=1 TO 4:CA(X)=5000:REM STARTING DOLLARS
8450 DV(X)=0:REM ZERO DIVIDENDS
8460 FOR Y=1 TO 10
8470 PF(X,Y)=0:REM NO STOCKS TO START
8480 NEXT Y:NEXT X
8490 FOR X=1 TO 10:SP(X)=INT((16*RND(0))+92):NEXT X
8500 PRINT "THE STOCKS CURRENTLY TRADED"
8510 PRINT "     -----BIG BOARD-----"
8530 PRINT "STOCK NAME"TAB(26)"ABBR"
8540 GOSUB 7100:GOSUB10
8550 RETURN

9000 REM--I N S T R U C T I O N S--
9100 RETURN

9200 REM-------------------------
9210 REM  B L A C K F R I D A Y
9220 REM-------------------------
9230 REM      JUNE 14, 1977
