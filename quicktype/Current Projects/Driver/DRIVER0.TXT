10 REM PROGRAM TITLE: DRIVERS TEST
20 CLS
30 RANDOM
40 INPUT"HOW MANY DRIVERS";D
50 IF D<1 OR D>10 THEN 40
60 PRINT"FIRST NAMES"
70 FOR I=1 TO D:INPUT Q$(I)
80 NEXT:PRINT:YP=1
90 PRINT"THANK YOU..."
100 PRINT:M=0:WL=0:PA=0:TR=0:CA=0
105 IF YP>=2 THEN 430
110 PRINT"DO ANY OF YOU NEED THE"
120 PRINT"INSTRUCTIONS FOR DRIVERS TEST";
130 INPUT I$
140 IF I$="N" OR I$="NO" OR I$="" THEN 420
150 CLS
160 PRINT"YOU WILL TAKE THE DRIVERS SEAT"
170 PRINT"IN AN ATTEMPT TO COME OUT ALIVE."
180 PRINT"YOU WILL BE TRAVELLING DOWN A"
190 PRINT"ROAD THAT WILL BE"
200 PRINT"STRAIGHT OR WINDING. YOU WILL"
210 PRINT"bE TESTED FOR HOW WELL YOU CAN"
220 PRINT"STAY IN YOUR OWN LANE; MEANING,"
230 PRINT"DON'T HIT THE SHOULDER OR THE"
240 PRINT"CENTER MEDIAN (UNLESS PASSING)."
250 PRINT
260 INPUT"KEY ENTER";X
270 CLS
280 PRINT"WHEN A CAR COMES UP IN FRONT OF"
290 PRINT"YOU, YOU MUST PASS. TO DO THIS"
300 PRINT"PRESS AND HOLD THE <RIGHT ARROW>"
310 PRINT"KEY. AFTER PASSING QUICKLY MOVE"
320 PRINT"BACK TO YOUR LANE BY PRESSING"
330 PRINT"AND HOLDING THE <LEFT ARROW>"
340 PRINT"KEY. THESE KEYS ARE ALSO USED TO"
350 PRINT"KEEP YOUR CAR STEADY, IN YOUR"
360 PRINT"LANE. IF YOU ARE PASSING AND"
370 PRINT"ANOTHER CAR IS ALREADY IN THE"
380 PRINT"PASSING LANE...TAKE TO THE"
390 PRINT"DITCH !!!"
400 PRINT"A PERFECT SCORE IS 0 (ZERO)"
410 PRINT"MISTAKES OR ERRORS."
420 PRINT Q$(1);" WILL NOW TAKE THE WHEEL"
430 PRINT"PRESS ENTER WHEN READY";
440 INPUT X
450 CLS
500 REM TWO LANES / MEDIAN
510 R1=30:R2=44:C5=R1+3:M1=((R2-R1)/2)+R1
520 REM STRAIGHT OR CURVE / CAR
530 M3=M1:GOTO 700
540 C=RND(2):C1=RND(2):M=((R2-R1)/2)+R1
550 PRINT@C5,":i:";:IF ABS(C-C1)=0 THEN 580
560 REM STRAIGHT
570 TR=TR+1:GOTO 700
580 REM LEFT OR RIGHT CURVES
590 C2=RND(2)
600 IF ABS(C1-C2)=1 THEN 650
610 REM LEFT
620 IF ABS(R1-M1)>=19 THEN 670
630 R1=R1-1:R2=R2-1:M=M-1
640 GOTO 700
650 REM RIGHT
660 IF ABS(R2-M1)>=19 THEN 630
670 R1=R1+1:R2=R2+1:M=M+1
680 REM LANE AND A CAR
700 REM USER CAR MOVEMENT
710 Q=PEEK(15340)
720 IF R1>=960 OR R2>=960 THEN 740
730 R1=R1+64:R2=R2+64:M1=M1+64
740 PRINT@R1,"!";:PRINT@C5,":i:";
750 PRINT@M,"i";:PRINT@R2,"!";
755 IF PA=3 THEN CA=CA-64
756 IF CL=1 THEN CC=CC-64
757 IF ABS(CA-C5)<=3 THEN 1160
760 IF M=0 PRINT@M,"  ";
770 IF Q=32 THEN 810
780 IF Q=64 THEN 830
790 PRINT:IF TR>=255 THEN 970
792 IF ABS(CC+64-C5)<=3 THEN 1220
795 IF PA<>3 AND C5>M3 AND Q<>32 THEN 870
800 GOTO 900
810 REM CAR LEFT
812 IF C5<=0 THEN ZT=1:GOTO 1220
815 IF PA=3 THEN C5=C5-2:GOTO 790
820 C5=C5-1:GOTO 790
830 REM CAR RIGHT
832 IF C5>=62 THEN ZT=1:GOTO 1220
835 IF PA=3 THEN C5=C5+2:GOTO 850
840 C5=C5+1
850 IF PA<>3 AND C5>M3+2 THEN 870
860 GOTO 790
870 REM STAYING IN WRONG LANE
880 WL=WL+1:GOTO 1000
900 Q1=PEEK(C5+15360+64)
910 Q2=PEEK(C5+15360+66)
920 IF Q1<>32 AND Q<>32 THEN 940
925 IF Q2<>32 AND Q<>32 THEN 940
930 IF ABS(M3-C5)>=14 THEN 960
935 GOTO 1000
940 IF PA=3 THEN 1160
950 REM WRONG LANE / SHOULDER
960 WL=WL+1:GOTO 1000
970 PRINT@0,"DRIVING TEST COMPLETED..."
980 GOTO 1460
1000 REM CAR TO PASS
1001 IF PA=3 THEN 1040
1010 PA=RND(20)
1020 IF PA<>1 THEN 1330
1030 PA=PA+2
1040 IF R1<=860 THEN PA=0:GOTO 540
1050 IF CA>=100 THEN 1070
1060 IF CP=0 THEN CA=M-6
1070 IF CP=1 THEN 1110
1080 PRINT@CA,":<I>:";
1090 CP=1
1100 GOTO 540
1110 REM CLEAR IF LESS THAN USER CAR
1120 IF CA<=0 THEN 1140
1130 GOTO 540
1140 PA=0:CP=0:CL=0
1150 GOTO 1190
1160 REM HEAD-ON?
1170 IF ABS(CA-C5)<=4 THEN 1220
1180 IF ABS(CA+1-C5)<=3 THEN 1220
1190 IF PA+CP<>4 AND C5<R1-962 THEN 960
1200 IF PA+CP<>4 AND C5>R2-962 THEN 960
1210 GOTO 540
1220 X=1:WL=-99
1230 PRINT@C5,"*";:PRINT@C5+2,"*";
1240 PRINT@C5+64-1,"*";:PRINT@C5+64+4,"*";
1250 FOR U=1 TO 50:NEXT
1260 PRINT@C5,"      ";:PRINT@C5+64+1,"*";
1270 PRINT@C5+64-3,"        ";
1280 FOR U=1 TO 25:NEXT
1290 X=X+1
1300 IF X>=11 THEN 1400
1310 GOTO 1230
1330 REM CAR IN OPPOSITE LANE
1340 IF M<896 OR PA<>10 AND PA<>15 THEN 540
1350 IF CL=1 THEN 540
1360 CL=1:CC=M+2
1370 PRINT@M+2,":--:";
1380 GOTO 540
1400 REM FINISHED / DECEASED
1410 PRINT@0,;
1420 PRINT"YOU HAVE BECOME ANOTHER"
1430 PRINT"HIGHWAY STATISTIC."
1435 IF ZT=1 THEN ZT=0:GOTO 1460
1440 PRINT"MOVE TO THE PASSING LANE"
1450 PRINT"A LITTLE SOONER...NEXT TIME."
1460 UP(YP)=WL:YP=YP+1
1470 IF YP>D THEN 1520
1480 PRINT
1490 PRINT Q$(YP);" WILL NOW TAKE THE"
1500 PRINT"WHEEL, GOOD LUCK !!!"
1510 CP=0:CL=0:CC=0:GOTO 100
1520 FOR T=1 TO 2000:NEXT:CLS
1530 PRINT"END OF DRIVING TEST."
1540 PRINT"TOTAL NUMBERS OF ERRORS"
1550 PRINT"ARE AS FOLLOWS:"
1560 FOR I=1 TO D
1570 IF UP(I)=-99 THEN 1620
1580 PRINT Q$(I);" HAD";UP(I);
1590 PRINT"TOTAL ERRORS."
1600 NEXT
1610 GOTO 1640
1620 PRINT Q$(I);" IS DECEASED..."
1630 GOTO 1600
1640 PRINT
1650 PRINT"END OF DRIVERS TEST."
1660 END
