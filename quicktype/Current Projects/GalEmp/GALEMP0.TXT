1 CLEAR250:CLS:GOTO8:REM V3.0
2 FORT1=0TO500:NEXT
3 FORT1=0TO500:NEXT
4 FORT1=0TO500:NEXT
5 FORT1=0TO600:NEXT
6 FORT1=0TO220:NEXT
7 FORT1=0TO80:NEXT:RETURN
8 PRINT@209,"G A L A C T I C     E M P I R E";:PRINT@347,"VERSION 3.0";:PRINT@512,STRING$(64,170);:PRINT@672,"(C) 1979 D. G. CARLSTON";
9 DEFINTD-L,N-P,T-Z:DEFSTRA-C:DIMU(49),U1(49),U2(49),U3(49),C(19),RO(19),PS(19),PE(19),PA(19),PB(19),PC(19),PD(19):GOSUB3:CLS:RANDOM:GOTO204
10 PD(P1)=1:P=P1:GOSUB172:FORT8=0TO8:PRINT@585," PLANETFALL: "C(P);:GOSUB6:PRINT@586,CHR$(217);:GOSUB7:NEXT
11 PRINT@65,"ORBITING: "C(P);:GOSUB165:IFSD>1.5THEN31
12 BM="COMMAND":GOSUB166:IFA="Q"THEN201ELSEIFA="E"THENIFP=P1GOTO23ELSE13ELSEIFA="C"THENGOSUB39:GOTO12ELSEIFA="A"THEN92ELSEIFA="O"THEN118ELSE12
13 BS="SCOUT":BF="FIGHTER":BT="TRANSPORT":FORT9=65TO257STEP64:PRINT@T9,CHR$(234);:NEXT
14 PRINT@0,STRING$(41,176);:A=CHR$(170)+CHR$(26)+CHR$(8):PRINT@104,A;A;A;A;:B=CHR$(149)+CHR$(26)+CHR$(8):PRINT@64,B;B;B;B;:PRINT@320,STRING$(41,179);:PRINT@234,CHR$(151);STRING$(20,131);CHR$(171);:PRINT@298,B;B;B;B;B;B;
15 PRINT@319,A;A;A;A;A;A;:PRINT@618,STRING$(22,131);:PRINT@384,B;B;B;B;B;B;:PRINT@424,A;A;A;A;A;A;:PRINT@768,STRING$(41,131);:GOSUB16:GOTO21
16 SD=SD+.001:PRINT@44,"STARDATE: ";:PRINTUSING"###.#";SD;:PRINT@108,"MODE:";:PRINT@300,BF"S : "F;:PRINT@364,BT"S :";:PRINT@427,TE"EMPTY,"TF"FULL";:PRINT@492,BS"S : "V;:PRINT@556,"CREDITS : ";:PRINTUSING"#####";M;
17 IFSD>=1E3THEN178ELSED5=D5+1:IFD5/200=INT(D5/200)THEN18ELSERETURN
18 KK=0:FORT9=0TO19:RO(T9)=RO(T9)*1.01:NEXT:IFD5>2E4THEND5=0
19 FORT9=0TO49:IFU1(T9)=5ANDSD>=U(T9)THENPS(U2(T9))=2:U1(T9)=0:U(T9)=0
20 NEXT:RETURN
21 PRINT@682,"CONTROLS:";:PRINT@747,"=A= ATTACK";:PRINT@811,"=C= COMPUTER";:PRINT@875,"=E= EMBARK";:PRINT@939,"=O= ORDERS";
22 IFF9=0THENGOSUB188:GOTO30ELSEWS=SD:WQ=SD+DL:W1=0:W2=0:GOTO24
23 GOSUB172:PRINT@582,"NAVIGATOR HAS NO ORDERS";:GOSUB3:GOSUB165:GOTO12
24 Z=1:GOSUB172:PRINT@115,"UNDERWAY";:PRINT@585,"DESTINATION: "C(P1);:PRINT@643,"ESTIMATED ARRIVAL: STARDATE "INT(SD+DL);:GOSUB25:GOTO26
25 X(Z)=RND(40)+20:Y(Z)=7+RND(2):RETURN
26 FORT=1TODL*2:FORJ=2TO4STEP2:PRINT@20+J*64,"!";:NEXT:A="- - - - ":PRINT@204,A"+ "A;:SD=SD+.5:GOSUB16:FORZ=1TO3:IFX(Z)>40THENXN=X(Z)+2ELSEXN=X(Z)-2
27 IFY(Z)<9THENYN=Y(Z)-1ELSEYN=Y(Z)+1
28 RESET(X(Z),Y(Z)):IFXN>79ORXN<1ORYN>14ORYN<3THENGOSUB25ELSEX(Z)=XN:Y(Z)=YN
29 SET(X(Z),Y(Z)):NEXTZ,T:GOSUB186
30 IFF9=0THENF9=1:GOTO11ELSE10
31 PRINT@115,"SCANNING ";:FF=0:TN=0:VN=0:FORTT=0TO49:IFU(TT)<SD-5ORU(TT)>SDORU1(TT)>3THEN34
32 IFU3(TT)<>PTHEN34ELSEIFU1(TT)=1THENFF=FF+U2(TT)ELSEIFU1(TT)=2THENTN=TN+U2(TT)ELSEIFU1(TT)=3THENVN=VN+U2(TT)ELSE34
33 U2(TT)=0:U(TT)=0:U1(TT)=0
34 NEXTTT:FORTT=0TO49:IFU(TT)<SD-5ORU(TT)>SDORU1(TT)<>4ORU3(TT)<>PTHEN35ELSEGOSUB172:PRINT@586,BS" SHIP TO";:PRINT@652,C(U2(TT))" HAS RETURNED     ";:PD(U2(TT))=1:GOSUB5:V=V+1:U(TT)=0
35 NEXTTT:GOSUB165:IFFF+TN+VN=0THEN38ELSEGOSUB172:PRINT@451,"ADDITIONS TO FLEET: ";:IFFF=0THEN36ELSEPRINT@586,FF;BF"S";:F=F+FF:GOSUB16
36 IFTN=0THEN37ELSEPRINT@650,TN;BT"S";:TE=TE+TN:GOSUB16
37 IFVN=0THEN38ELSEPRINT@714,VN;BS"S";:V=V+VN:GOSUB16
38 GOSUB5:GOSUB165:GOTO12
39 GOSUB172:PRINT@452,"ENTER CATEGORY :";:PRINT@586,"=A=  STAR MAPS";:PRINT@650,"=B=  PLANETARY DIRECTORY";:PRINT@714,"=C=  STATUS REPORTS";
40 BM="COMPUTER":GOSUB166:IFA="A"THEN41ELSEIFA="B"THEN52ELSEIFA="C"THEN65ELSEIFA="R"THENGOSUB165:A="":RETURNELSE40
41 GOSUB165:PRINT@385,"STAR MAPS :";:PRINT@586,"=1=  LOCAL MAP";:PRINT@650,"=2=  RANGE FINDER";:PRINT@714,"=3=  GALAXY MAP";
42 GOSUB166:IFA="R"THENGOSUB165:GOTO39ELSEIFA="1"ORA="2"ORA="3"THENZ=VAL(A):GOSUB169:ONZGOTO43,46,72ELSE42
43 GOSUB165:XH=PA(P):YH=PB(P):FORXN=0TO19:X=PA(XN):Y=PB(XN)
44 IFABS(XH-X)>10ORABS(YH-Y)>10THENNEXTELSEX=(X-XH)*39/10+40:Y=(Y-YH)*9/10+27:SET(X,Y):NEXT
45 XP=40:YP=18:WW=1:GOTO73
46 GOSUB165:PRINT@385,"RANGE FINDER";:PRINT@451,"ENTER NAMES OF BOTH SYSTEMS";:WW=0
47 GOSUB166:WW=WW+1
48 IFA="R"THENGOSUB165:GOTO39ELSEGOSUB170:IFA="X"THEN46ELSEONWWGOTO49,50
49 PRINT@580,A"      ";:I1=XU:GOTO47
50 PRINT@600,A"      ";:I2=XU:GOSUB171:PRINT@642,"("PA(I1)PB(I1)PC(I1)")  ";:PRINT@662,"("PA(I2)PB(I2)PC(I2)")  ";:PRINT@707,"SYSTEMS ARE "D2" LIGHT YEARS APART ";
51 GOSUB2:WW=0:IFWP=9THENRETURNELSEGOTO47
52 GOSUB169:DX=394:GOSUB165:PRINT@842,"PLANETARY DIRECTORY";:PRINT@385,"EMPIRE:";:FORXN=0TO19:IFPS(XN)>1THENGOSUB53:GOTO55ELSE55
53 IF(DX/64-INT(DX/64))*64+LEN(C(XN))>39THENDX=INT(DX/64)*64+65
54 PRINT@DX,C(XN);:DX=DX+LEN(C(XN))+2:RETURN
55 NEXT:DX=INT(DX/64)*64+65:PRINT@DX,"INDEPENDENT: ";:DX=DX+14:FORXN=0TO19:IFPS(XN)<2THENGOSUB53:NEXTELSENEXT
56 GOSUB166:GOSUB165:FORXN=1TO80:SET(XN,36):NEXT:IFA="R"THENGOSUB169:GOTO39
57 PRINT@385,"DATA ON PLANETARY SYSTEM";
58 IFA="R"THENGOSUB165:GOSUB169:GOTO39ELSEGOSUB170:IFA="X"THEN52ELSEPRINT@449,A":  ("PA(XU)PB(XU)PC(XU)")  ";
59 IFPD(XU)=0PRINT@586,"NO DATA ON FILE";:GOSUB2:GOTO52
60 PRINT@579,"POPULATION: ";:B="###.#":IFPS(XU)<2THEN62ELSEQX=RO(XU)*10:IFQX>999THENQX=QX/1000:PRINTUSINGB;QX;:PRINT" BILLION";ELSEPRINTUSINGB;QX;:PRINT" MILLION";
61 GOTO63
62 IFRO(XU)>100PRINT"EST. OVER A BILLION";ELSEIFRO(XU)>RO(0)PRINT"GREATER THAN GALACTICA";ELSEIFRO(XU)>10PRINT"EST. OVER 100 MILLION";ELSEPRINT"EST. UNDER 100 MILLION";
63 PRINT@643,"TECHNICAL LEVEL: ";:IFPE(XU)=0THENPRINT"PRIMITIVE";ELSEIFPE(XU)=1THENPRINT"LIMITED ATOMIC";ELSEIFPE(XU)=2THENPRINT"SOPHISTICATED";ELSEPRINT"SUPERIOR TO OURS";
64 GOSUB166:GOSUB165:IFA="R"THENGOSUB169:GOTO39ELSE52
65 GOSUB165:PRINT@385,BS"S:    -- STATUS REPORT --";:L=449:FORTT=0TO49:IFU(TT)<SD-5THEN66ELSEIFU1(TT)<>4THEN66ELSEPRINT@L,STRING$(39,32);:PRINT@L,"TO "C(U2(TT))" DUE "C(U3(TT))" ON SD"U(TT);:L=L+64:GOSUB5:IFL>710THENL=L-320
66 NEXT:GOSUB166:GOSUB165:PRINT@385,"OTHERS:    -- STATUS REPORT --";:L=449:FORTT=0TO49:IFU(TT)<SD-5THEN68ELSEIFU1(TT)>3THEN68ELSEIFU2(TT)=0THEN68ELSEONU1(TT)GOSUB69,70,71
67 PRINT@L,CHR$(230);:PRINT@L,U2(TT)BB"S DUE "C(U3(TT))" ON SD"U(TT);:L=L+64:IFL>710THENL=L-320:GOSUB5ELSEGOSUB5
68 NEXTTT:GOSUB166:GOSUB165:GOTO39
69 BB=BF:RETURN
70 BB=BT:RETURN
71 BB=BS:RETURN
72 GOSUB165:FORXN=0TO19:X=PA(XN):Y=PB(XN):X=X*78/60+2:Y=Y*17/60+18:SET(X,Y):NEXT:XP=40:YP=18:WW=2
73 A=INKEY$:SET(XP,YP):IFA="R"THENA="":GOSUB165:GOSUB169:GOTO39ELSEIFPEEK(14400)=32THEN87ELSEIFPEEK(14400)=64THEN88ELSEIFPEEK(14400)=8THEN89ELSEIFPEEK(14400)=16THEN90ELSEFORT4=1TO7:NEXT:RESET(XP,YP):IFA=""THENGOSUB16:GOTO73
74 GOSUB170:IFA="X"THEN73ELSEONWWGOTO76,75
75 XL=PA(XU)*78/60+2:YL=PB(XU)*17/60+18:GOTO77
76 XL=(PA(XU)-XH)*39/10+40:YL=(PB(XU)-YH)*9/10+27
77 PRINT@832,A,"("PA(XU)PB(XU)PC(XU)")  ";:IFPS(XU)=2PRINT@898,"EMPIRE     ";ELSEIFPS(XU)=3PRINT@898,"GARRISONED ";ELSEPRINT@898,"INDEPENDENT";
78 IFWW=1ANDXL>80ORXL<1ORYL<18ORYL>35PRINT@832,A" SYSTEM NOT ON LOCAL MAP        ";:GOTO79ELSEFORXN=0TO100:RESET(XL,YL):SET(XL,YL):NEXT
79 IFPOINT(XP,YP)THEN80ELSESET(XP,YP):FORXZ=1TO10:NEXT:RESET(XP,YP):GOTO73
80 FORXZ=0TO19:ONWWGOTO82,81
81 XG=PA(XZ)*78/60+2:YG=PB(XZ)*17/60+18:IFXG=XPANDYG=YPTHEN83ELSE84
82 XG=(PA(XZ)-XH)*39/10+40:YG=(PB(XZ)-YH)*9/10+27:IFXG=XPANDYG=YPTHEN83ELSE84
83 PRINT@832,C(XZ),"(";PA(XZ);PB(XZ);PC(XZ);")   ";:IFPS(XZ)=2PRINT@898,"EMPIRE     ":GOSUB5ELSEIFPS(XZ)=3PRINT@898,"GARRISONED ":GOSUB5ELSEPRINT@898,"INDEPENDENT":GOSUB5
84 NEXTXZ:IFPOINT(XP+1,YP)THEN85ELSEXP=XP+1:GOTO73
85 IFPOINT(XP,YP+1)THEN86ELSEYP=YP+1:GOTO73
86 XP=XP-1:YP=YP-1:GOTO73
87 GOSUB91:IFXP>1XP=XP-1:GOTO79ELSE79
88 GOSUB91:IFXP<80XP=XP+1:GOTO79ELSE79
89 GOSUB91:IFYP>18YP=YP-1:GOTO79ELSE79
90 GOSUB91:IFYP<35YP=YP+1:GOTO79ELSE79
91 RESET(XP,YP):RETURN
92 F8=0:E8=0:IFPS(P)>1PRINT@586,"ATTACKING EMPIRE SYSTEM!!!";:GOTO185
93 IFTF<1THENPRINT@586,"NO FULL TRANSPORTS --";:PRINT@650,"IMPOSSIBLE TO ATTACK!!";:GOSUB4:GOSUB165:GOTO12
94 IFPE(P)<2THEN105ELSEGOSUB172:B=" AIR":GOSUB95:EF=(RND(100)+30):G=RND(4):IFPE(P)=3THENG=2:GOTO96ELSE96
95 PRINT@451,B;" BATTLE REPORT";:PRINT@515,"PROBABILITY OF SUCCESS:    %";:PRINT@586,"EMPIRE      ENEMY";:PRINT@650,"LOSSES      LOSSES";:RETURN
96 PRINT@538,INT(F*100/(F+EF));:PRINT@715,F8;:PRINT@727,E8;:IFF<=0ORTF<=0THEN116ELSEIFEF<=0THEN104ELSEONGGOTO97,98,99,100
97 G1=RND(3):G2=RND(3):G3=RND(20):GOTO101
98 G1=RND(3):G2=RND(2)-1:G3=RND(10):GOTO101
99 G1=RND(2)-1:G2=RND(3):G3=RND(50):GOTO101
100 G1=RND(2):G2=RND(4):G3=RND(40)
101 F=F-G1:F8=F8+G1:EF=EF-G2:E8=E8+G2:GOSUB7:A=INKEY$:IFG3=1GOTO173ELSEIFRND(30)=1GOSUB169:IFPE(P)<>3THENG=RND(4)
102 IFF<0THENF=0ELSEIFEF<0THENEF=0
103 IFA="R"THEN116ELSEGOSUB16:GOTO96
104 GOSUB169:GOSUB165:PRINT@586,"ENEMY AIR FORCES DEFEATED!!";:GOSUB2
105 GOSUB165:GOSUB172:B="LAND":GOSUB95:EF=RO(P)*2:G=RND(4):F8=0:E8=0:IFPS(P)=1THENG=2
106 PRINT@538,INT(TF*100/(TF+EF));:PRINT@715,F8;:PRINT@727,E8;:IFTF<=0THEN116ELSEIFEF<=0THEN114ELSEONGGOSUB107,108,109,110:GOTO111
107 G1=RND(3)-1:G2=RND(4):RETURN
108 G1=RND(3):G2=RND(2):RETURN
109 G1=RND(3):G2=RND(3):RETURN
110 G1=RND(2)-1:G2=RND(2):RETURN
111 PRINT@427,CHR$(212);:RO(P)=RO(P)-G2/10:TF=TF-G1:F8=F8+G1:EF=EF-G2:E8=E8+G2:IFRO(P)<0THENRO(P)=0:PRINT@650,"POPULATION ANNIHILATED";:GOTO114
112 IFTF<0THENTF=0ELSEIFEF<0THENEF=0
113 GOSUB16:GOSUB6:A=INKEY$:IFA="R"THEN116ELSEIFRND(30)=1ANDPS(P)<>1THENG=RND(4):GOTO106ELSE106
114 GOSUB163:U(XX)=SD+10:U1(XX)=5:U2(XX)=P:PS(P)=3:PD(P)=1:RO(P)=RO(P)-E8/100:GOSUB165:GOSUB172:PRINT@586,"PLANET SECURED";:GOSUB2:GOSUB165
115 KK=0:FORT9=0TO19:IFPS(T9)>1THENKK=KK+1:IFKK>19THEN178ELSENEXT:GOTO12ELSENEXT:GOTO12
116 GOSUB165:GOSUB172:PRINT@586,"ATTACK UNSUCCESSFUL";:GOSUB5:IFF<=0THENGOSUB16:GOTO181
117 GOSUB165:GOTO12
118 GOSUB165:PRINT@385,"ORDERS FOR WHICH OFFICER?";:PRINT@514,"LT. STARBUCK - "BS"S";:PRINT@578,"LT. BAYLISS - GROUND FORCES";:PRINT@642,"NAVIGATOR KIRMAN";:PRINT@706,"DR. HENDERSON - CRYONICS";
119 BM="ORDERS":GOSUB166:GOSUB165:GOSUB169:IFA="R"THEN12ELSEIFA="S"THEN120ELSEIFA="B"THEN124ELSEIFA="N"ORA="K"THEN142ELSEIFA="D"ORA="H"THEN146ELSE118
120 IFV<=0THENGOSUB169:PRINT@832,"SIR, I HAVE NO "BS"S!";:GOSUB3:GOSUB169:GOSUB165:GOTO118
121 PRINT@832,"WHICH SYSTEM SHOULD I "BS"?";:GOSUB122:IFPD(XU)=1GOSUB169:PRINT@832,"THAT DATA IS ALREADY";:PRINT@898,"  IN THE COMPUTER, SIR";:GOSUB3:GOSUB169:GOTO121ELSE123
122 GOSUB166:IFA="R"THENGOSUB165:GOSUB169:GOTO118ELSEGOSUB170:IFA="X"THEN122ELSEPRINT@896,A;:RETURN
123 I1=P:I2=XU:GOSUB171:K=SD+D2:GOSUB163:U1(XX)=4:U2(XX)=XU:GOSUB169:PRINT@832,"WHERE SHOULD I RETURN?";:GOSUB122:I1=XU:GOSUB171:U(XX)=K+D2:U3(XX)=XU:GOSUB4:GOSUB169:V=V-1:GOSUB165:GOTO118
124 IFPS(P)<2THENPRINT@832,"WE'RE OVER AN INDEPENDENT SYSTEM, SIR!";:GOSUB4:GOSUB169:GOTO118ELSEPRINT@385,"LT. BAYLISS' DUTIES :";:PRINT@517,"=1= SHIP CONSTRUCTION";:PRINT@581,"=2= ENLISTMENTS";:PRINT@645,"=3= TAXATION";
125 GOSUB166:GOSUB165:IFA="R"THEN118ELSEIFA="1"ORA="2"ORA="3"THENX=VAL(A):ONXGOTO126,148,152ELSE124
126 IFWS<>INT(SD)XF=22+RND(10):XT=10+RND(10):XS=2+RND(5):WS=SD
127 IFPS(P)<2THEN124ELSEIFPE(P)<2GOSUB169:PRINT@832,"SORRY SIR, THIS PLANET TOO BACKWARD";:PRINT@898,"TO BUILD SHIPS";:GOSUB2:GOSUB169:GOTO124ELSEGOSUB128:GOTO130
128 K2=RO(P)/10:IFK2<5ANDPE(P)<>3THENK2=5*ZZELSEIFK2<9ANDPE(P)=3THENK2=9*ZZELSEK2=K2*ZZ
129 GOSUB172:PRINT@451,"PRICES:";:PRINT@517,BF"S   --"XF;:PRINT@581,BT"S --"XT;:PRINT@645,BS"S     --"XS;:PRINT@707,"(MAX. OF"K2"SHIPS PER YEAR)";:RETURN
130 GOSUB131:IFWQ<SDTHENWQ=SD:GOTO132ELSE132
131 GOSUB169:PRINT@832,"LT. BAYLISS REPORTING, SIR!  ";:GOSUB5:RETURN
132 FORU=1TO3:N(U)=0:NEXT:PRINT@898,"PLACE ORDERS FOR HOW MANY YEARS? ";:GOSUB193:Y2=VAL(B):FORU=1TO3:ONUGOSUB69,70,71
133 GOSUB169:A=INKEY$:PRINT@832,"BUILD HOW MANY "BB"S EACH YEAR?";:PRINT@898,"";:VW=20:GOSUB193:VW=200:IFB="R"THENGOSUB169:WQ=Y3:U=3:NEXT:GOTO118
134 T6=0:N(U)=VAL(B):FORT5=1TOU:T6=T6+N(T5):NEXT:IFT6>K2THENGOSUB168:GOTO133ELSEIFT6=K2THENU=3
135 NEXT:FORY3=WQTOWQ+Y2-1:FORX9=1TO3:ONX9GOTO136,137,138
136 M=M-XF*N(X9):IFM<0THENM=M+XF*N(X9):GOTO139ELSE155
137 M=M-XT*N(X9):IFM<0THENM=M+XT*N(X9):GOTO139ELSE155
138 M=M-XS*N(X9):IFM<0THENM=M+XS*N(X9):GOTO139ELSE155
139 Y3=WQ+Y2-1:X9=3
140 GOSUB16:B="":NEXTX9,Y3:WH=0
141 WQ=Y3:GOSUB169:GOSUB165:GOTO124
142 GOSUB16:GOSUB169:PRINT@832,"KIRMAN REPORTING SIR!";:PRINT@898,"WHAT IS OUR DESTINATION?";:GOSUB166
143 WP=9:IFA="R"THENGOSUB165:GOSUB169:GOTO118ELSEGOSUB170:IFA="X"THEN142ELSEGOSUB169:PRINT@832,A"!!    YES SIR!!!";:IFXU=PTHEN145ELSEGOSUB172:PRINT@580,C(P);:I1=P:GOSUB50:WP=0:DLS=D2
144 GOSUB165:P1=XU:GOSUB169:GOTO118
145 FORT9=1TO200:NEXT:PRINT@832,"I MEAN NO SIR.  THAT'S WHERE WE";:PRINT@898,"ARE RIGHT NOW!!";:GOSUB3:GOSUB169:GOTO142
146 PRINT@832,"DR. HENDERSON REPORTING.";:PRINT@896,"HOW MANY YEARS OF SUSPENDED ANIMATION? ";:GOSUB193:IFB="R"THENGOSUB169:GOTO118ELSEY2=VAL(B):FORY3=1TOY2:GOSUB5:SD=SD+1:GOSUB16:NEXT
147 GOSUB169:GOSUB172:FORTL=1TO10:PRINT@585," WAKE UP, SIR!!!";:GOSUB6:PRINT@586,CHR$(207);:FORT8=1TO30:NEXTT8,TL:GOSUB165:GOTO11
148 IFW1=1THEN118ELSEW1=1:GOSUB172:PRINT@522,"ORBITING: ";C(P);:PRINT@586,"POPULATION: ";INT(RO(P)*10);" MILLION";:PRINT@650,"AVAILABLE "BT"S: ";TE;:K1=4-PS(P):IFC(P)="SPARTA"THENK1=10
149 GOSUB169:PRINT@832,"FILL HOW MANY "BT"S, SIR?";:PRINT@898,"";:GOSUB193:D1=VAL(B)
150 IFD1>TEGOSUB169:PRINT@832,"NOT ENOUGH ROOM ABOARD "BT"S, SIR!!";:GOSUB3:GOTO149ELSEIFD1>RO(P)*K1GOSUB169:PRINT@832,"OVER"K1"PERCENT OF POPULATION SIR!!";:GOSUB3:GOTO149
151 PRINT@427,CHR$(212);:RO(P)=RO(P)-D1/100:TE=TE-D1:TF=TF+D1:W1=1:GOSUB16:GOSUB165:GOSUB169:GOTO118
152 IFW2=1THEN118ELSEW2=1:IFPS(P)<2THEN118ELSEGOSUB169
153 IFRO(P)>400THENKH=4000ELSEKH=INT(RO(P)*20*ZZ)
154 GOSUB169:PRINT@832,"WE HAVE RAISED"KH"CREDITS IN TAXES, SIR!";:M=M+KH:GOSUB16:GOSUB4:GOSUB169:GOTO118
155 IFY3>SDTHEN159ELSEONX9GOTO156,157,158
156 F=F+N(X9):GOTO140
157 TE=TE+N(X9):GOTO140
158 V=V+N(X9):GOTO140
159 IFN(X9)=0THEN140ELSEIFWH=Y3THEN162ELSEWH=Y3
160 GOSUB169:PRINT@832,"SEND SHIPS COMPLETED IN SD"Y3"TO:";:PRINT@898,"(Enter =C= for reference to Computer)";:GOSUB166:IFA="C"THENGOSUB165:GOSUB39:GOSUB129:BM="ORDERS":GOTO160
161 GOSUB169:GOSUB170:IFA="X"THEN160ELSEPRINT@832,A;
162 GOSUB163:U1(XX)=X9:U2(XX)=N(X9):U3(XX)=XU:I1=P:I2=XU:GOSUB171:U(XX)=D2+Y3:GOTO140
163 FORTT=0TO49:IFU(TT)=0THENXX=TT:RETURNELSENEXT
164 FORTT=0TO49:IFU(TT)<SD-5ANDU1(TT)<>5THENXX=TT:RETURNELSENEXT
165 FORX8=385TO705STEP64:PRINT@X8,CHR$(231);:NEXT:FORY=18TO35:RESET(1,Y):RESET(80,Y):NEXT:RETURN
166 A=INKEY$:FORLP=1TO500:XV=FRE("0"):GOSUB7:PRINT@115,BM"  ";:SD=SD+.001:A=INKEY$:IFA=""THENGOSUB6:PRINT@115,"        ";:NEXTLP
167 GOSUB16:IFA=""THEN166ELSERETURN
168 PRINT@896,"SIR, THEY CAN ONLY BUILD"K2"SHIPS A YEAR! ":GOSUB3:RETURN
169 PRINT@832,STRING$(43,32);:PRINT@896,STRING$(43,32);:RETURN
170 FORXU=0TO19:IFLEFT$(C(XU),1)=ATHENA=C(XU):RETURNELSENEXT:A="X":RETURN
171 D2=INT(SQR((PA(I1)-PA(I2))[2+(PB(I1)-PB(I2))[2+(PC(I1)-PC(I2))[2)):RETURN
172 PRINT@385,"COMPUTER CENTRAL REPORTING:";:RETURN
173 PRINT@427,CHR$(212);:TF=TF-1:GOSUB16:GOSUB169:PRINT@832,"THEY GOT A "BT"!!!";:GOSUB6:GOSUB169:GOTO102
174 WQ=1:VW=100:SD=1:READC(0):FORP=1TO19
175 T9=RND(19):IFC(T9)=""THEN READC(T9):IFLEFT$(C(T9),1)="S"THENPS(T9)=1:NEXTELSENEXTELSE175
176 PD(0)=1:PS(0)=2:FORP=0TO19:READRO(P):NEXTP:FORP=0TO19:READPE(P):NEXT:FORP=0TO4:PA(P)=RND(15)+23:PB(P)=RND(15)+23:PC(P)=RND(5)+5:NEXT:FORP=5TO14:PA(P)=RND(30)+15:PB(P)=RND(30)+15:PC(P)=RND(15):NEXT
177 FORP=15TO19:PA(P)=RND(60):PB(P)=RND(60):PC(P)=RND(15):NEXTP:M=1000:F=100:TE=100:V=5:P=0:CLS:GOTO13
178 KK=0:FORT9=1TO19:IFPS(T9)>1THENKK=KK+1:SC=SC+RO(T9)*(PE(T9)+1):NEXTELSENEXT
179 GOSUB165:GOSUB172:PRINT@580,KK" SYSTEMS IN "SD" YEARS";:PRINT@714,"SCORE : "SC-SD;:GOSUB169:RETURN
180 A=INKEY$:IFA="Y"THENRUNELSEIFA="N"THENSTOPELSE180
181 GOSUB165:GOSUB169:GOSUB172:PRINT@586,"WE ARE UNDER ATTACK!";:GOSUB2:FORT9=1TO500:OUT255,8:PRINTCHR$(28);:NEXT:GOTO178
182 DATAGALACTICA,PROYC,ALHAMBRA,YANG-TZU,MOONSWEEP,SPARTA,JAVINY,BOK,EVENTIDE,LLYTHLL,OOTSI,UTOPIA,TWRYX,DRASSA 2,FARSIDE,KGOTLA,NOVENA,VIEJO,ZOE,HARKON
183 DATA40,25,13,68,102,37,2,7,181,50,88,34,67,92,137,14,19,28,3,19
184 DATA2,2,1,2,2,1,3,0,1,2,2,2,1,1,2,3,0,2,1,0
185 TF=TF/2:F=F/2:GOSUB16:GOSUB2:GOSUB165:GOTO12
186 PRINT@212,CHR$(174)CHR$(132);:GOSUB7:PRINT@211,CHR$(136)CHR$(191)CHR$(157);:GOSUB7;:PRINT@148," ";:PRINT@204,STRING$(22,32);
187 PRINT@210,CHR$(160)CHR$(190)CHR$(191)CHR$(191)CHR$(180);:PRINT@275,CHR$(139)CHR$(143)CHR$(143)CHR$(129);:GOSUB7
188 PRINT@207,CHR$(32)CHR$(176)CHR$(184)CHR$(188)CHR$(188);STRING$(7,191);CHR$(189)CHR$(188)CHR$(188)CHR$(176)CHR$(144);:PRINT@269,CHR$(160)CHR$(188);STRING$(17,191);CHR$(189)CHR$(180);:GOSUB7:RETURN
189 FORT=0TO49:X=832:PRINT@X,T:INPUT#1,U(T),U1(T),U2(T),U3(T):NEXTT:FORT=0TO19:INPUT#1,C(T),RO(T),PS(T),PE(T),PA(T),PB(T),PC(T),PD(T):PRINT@X,CHR$(30);C(T):NEXTT
190 INPUT#1,D5,F,KK,M,P1,P,V,SC,SD,VN,FF,TN,TE,TF,W1,W2,WP,WQ,WS,WW,XP,YP,Z,DL,VW,XF,XS,XT:RETURN
191 FORT=0TO49:PRINT#1,U(T);U1(T);U2(T);U3(T):NEXT:FORT=0TO19:PRINT#1,CHR$(34);C(T);CHR$(34);RO(T);PS(T);PE(T);PA(T);PB(T);PC(T);PD(T):NEXT:PRINT#1,D5;F;KK;M;P1;P;V;SC;SD;VN;FF;TN;TE;TF;W1;W2;WP;WQ;WS;WW;XP;YP;Z;DL;VW;XF;XS;XT
192 RETURN
193 A=INKEY$:IFA=""ORA=CHR$(13)ORA=CHR$(10)THEN193ELSEIFASC(A)<48ORASC(A)>57THEN193ELSEPRINTA;:B=A:FORT9=1TOVW:A=INKEY$:IFA=CHR$(13)THENRETURNELSEB=B+A:PRINTA;:NEXT:RETURN
194 GOSUB 198 :GOSUB169: RESUME 12
196 IFERR/2+1=54THENPRINT@896,"NO PREVIOUS RECORD ON DISK":FORI=1TO2000:NEXTI:RUN
197 GOSUB 198 : RESUME174
198 PRINT@832,"DISK ERROR ";ERR
199 PRINT"<C>ontinue  <S>tart new game  <E>xit to DOS"
200 ZZ$=INKEY$:IFZZ$="C"ORZZ$="c"THENRETURNELSEIFZZ$="E"ORZZ$="e"THENCMD"S"ELSEIFZZ$="S"ORZZ$="s"THENRUNELSE200
201 PRINT@832,"Saving Data   " : ON ERROR GOTO 194
202 OPEN"O",1,ZY$:GOSUB191:GOSUB178:GOSUB169:PRINT@832,"Data Saved      "
203 CLOSE1:F9=0:GOSUB199:GOTO211
204 PRINT"What is your first name "; : INPUT ZY$
205 ZY$ = LEFT$(ZY$,4)+"/EMP" : ZZ$=LEFT$(ZY$,1)
206 IF ZZ$ < "A" OR ZZ$ >"z" THEN CLS:GOTO204
207 PRINT:PRINT"Level of difficulty":PRINT"              1) Expert  2) Intermediate  3) Novice"
208 ZZ$=INKEY$:IFZZ$="1"ORZZ$="2"ORZZ$="3"THENZZ=VAL(ZZ$)ELSE208
209 PRINT:PRINT"Restore a previous game (Y/N) ? ";
210 ZZ$=INKEY$:IFZZ$="Y"ORZZ$="y"THENPRINTZZ$:GOTO211ELSEIFZZ$="N"ORZZ$="n"THENPRINTZZ$:GOTO174ELSE210
211 PRINT@832,"Loading Data        " :GOSUB5:GOSUB 169 : ON ERROR GOTO 196
212 OPEN"I",1,ZY$:GOSUB189:PRINT@832,"Data Loaded    "
213 CLOSE1:ONERRORGOTO0:GOSUB169:GOTO13
