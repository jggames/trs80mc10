100 CLEAR 500: CLS: DIM T$(50,2), N(5)
110 L=0
120 IF L<50 THEN 130
125 PRINT@64,"TOO MANY TABLE ENTRIES,"
127 PRINT@96,"FRENCH DRILL ABORTED": STOP
130 L=L+1
140 READ T$(L,1), T$(L,2)
150 IF (T$(L,1)="-1") OR (T$(L,2)="-1") THEN L=L-1: GOTO170
160 GOTO120
170 R=0: T=0
180 CLS: PRINT@7,"FRENCH NOUN DRILL"
190 PRINT@64,"SELECT ONE OF THE FOLLOWING:"
200 PRINT@96,"1. TRANSLATE FROM FRENCH-TO"
210 PRINT@131,"ENGLISH"
220 PRINT@160,"2. TRANSLATE FROM ENGLISH-TO-"
230 PRINT@195,"FRENCH"
240 PRINT@224,"WHICH ONE";: INPUT A$
250 IF A$="1" OR A$="2" THEN A=VAL(A$): GOTO290
260 PRINT@448,"INVALID SELECTION--TRY AGAIN"
270 FOR I=1 TO 300: NEXT I
280 PRINT@234,"": GOTO240
290 IF A$="1" THEN B=2: N$="FRENCH-TO-ENGLISH"
300 IF A$="2" THEN B=1: N$="ENGLISH-TO-FRENCH"
310 CLS: PRINT@7,N$
320 FOR I=0 TO 5
330 N(I)=RND(L)
340 IF N(I)=0 THEN 330
350 J=0 
360 IF (N(I)=N(J)) AND (I<>J) THEN 330
370 IF J<(I-1) THEN J=J+1: GOTO360
380 NEXT I
390 I=RND(5)
400 IF I<1 THEN 390
410 PRINT@64,T$(N(0),A)
420 N(I)=N(0): N(0)=I
430 PRINT@128,"CHOOSE ONE OF THE FOLLOWING:"
440 J=128
450 FOR I=1 TO 5
460 J=J+32: B$=STR$(I)
470 PRINT@J,MID$(B$,2,1)+". "+T$(N(I),B)
480 NEXT I
490 PRINT@320,"WHICH ONE";: INPUT B$
500 IF (B$>"0") AND (B$<"6") THEN 520
510 PRINT@329,"": GOTO490
520 T=T+1
530 IF VAL(B$) <> N(0) THEN 560
540 R=R+1
550 PRINT@384,R;"OUT OF";T;"CORRECT!": GOTO580
560 PRINT@384,"NO! THE CORRECT ANSWER IS";N(0)
580 PRINT@416,"ENTER R FOR RESTART, OR JUST"
590 PRINT@448,"<ENTER> FOR SAME";: INPUT B$
600 IF NOT (B$="R" OR B$="") THEN PRINT@465,": GOTO590
610 IF B$="R" THEN GOTO170
620 GOTO310
1000 DATA "LES FRUITS","THE FRUIT","LE VIN","THE WINE","LE PAIN","THE BREAD"
1010 DATA "LA CIEL","THE SKY","LA LUNE","THE MOON","LE SOLEIL","THE SUN"
1020 DATA "LA TETE","THE HEAD","LES MAINS","THE HANDS","LE NEZ","THE NOSE"
1030 DATA "L'OEIL","THE EYE","L'OREILLE","THE EAR","L'AVION","THE AIRPLANE"
1040 DATA "LA SOEUR","THE SISTER","LA MERE","THE MOTHER","LA MER","THE SEA"
1050 DATA "LE PETIT DEJEUNER","THE BREAKFAST","LE DINER","THE DINNER"
1060 DATA "LE PRIX","THE PRIZE","LE CAFE","THE COFFEE","LA PLUME","THE PEN"
1070 DATA "LE SAVON","THE SOAP","LE CHAPEAU","THE HAT","L'EAU","THE WATER"
1100 DATA -1,-1
