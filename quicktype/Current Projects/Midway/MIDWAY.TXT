2 X=MEM
3 IFX<2000THENCLS:SOUND50,2:PRINT@96,"INSUFFICIENT MEMORY TO RUN!!!":END
5 CLEAR1000
9 CLS0:DIMN$(7),P$(5),T$(6),S$(4)
10 DIM F(6,7),C(7,9),S(9,9),W(5),LL(3):SG$="                               "
19 GOSUB25000
20 CLS4:GOSUB26000:PRINT
25 PRINT TAB(5);"** MIDWAY CAMPAIGN **":PRINT:PRINT
26 PRINT TAB(7);"COPYRIGHT 1983 BY"
27 PRINT TAB(10);"AVALON HILL":PRINT TAB(6);"MICROCOMPUTER GAMES"
28 PRINT:PRINT
29 PRINT" COLOR COMPUTER ADAPTATION BY"
30 PRINTTAB(9);"CHUCK BRITE"
31 PRINT:PRINT
34 PRINT"  -- ENTER YOUR LAST NAME --":INPUT"       NAME: ";AA$
35 CLS:GOSUB26000:PRINT@34,"ENTER LEVEL OF DIFFICULTY:":PRINT@102,"1. BASIC.":PRINT@134,"2. ADVANCED."
36 PRINT@166,"3. EXPERT.":PRINT:PRINT"  ENTER 1, 2 OR 3: ";
37 PRINT@290,"BASIC-----JAPANESE VISIBLE.":PRINT"  ADVANCED--JAPANESE SPOTTED ON":PRINT@364,"RANDOM BASIS."
38 PRINT"  EXPERT----RANDOM SPOTTING +":PRINT@428,"INCREASED JAPANESE":PRINT@460,"SEARCH ABILITY.":PRINT@243,CHR$(191);
39 A$=INKEY$:IFA$=""THEN39
40 DF=VAL(A$):IFDF<>2ANDDF<>3THENDF=1
41 CLS0:GOSUB26000:PRINT@258," RUNNING DIFFICULTY LEVEL";DF;:J9=0:V0=0:V1=0
50 P1=.0175088:T=720:D=3
60 N$(0)="AKAGI":N$(1)="KAGA":N$(2)="SORYU":N$(3)="HIRYU"
61 N$(4)="ENTERPRISE":N$(5)="HORNET":N$(6)="YORKTOWN":N$(7)="MIDWAY"
62 M$="JC67":M$=M$+CHR$(207)
63 T$(0)="CV'S":T$(1)="TRANSPORTS":T$(2)="CRUISERS"
64 T$(3)="TF-16":T$(4)="TF-17":T$(5)="MIDWAY":T$(6)="CARRIER GROUP ESCORTS"
65 P$(0)="F4F'S":P$(1)="ZEKES":P$(2)="SBD'S"
66 P$(3)="VALS":P$(4)="TBD'S":P$(5)="KATES"
67 S$(0)="NONE":S$(1)="LGT":S$(2)="HVY":S$(3)="sunk":S$(4)="destroyed"
80 FORI=0TO5:FORJ=2TO7:READ F(I,J):NEXT:NEXT
90 FORI=0TO5:READ J,K,L:GOSUB 10000:NEXT
100 FORI=0TO7:FORJ=0TO3:READC(I,J):NEXT:FORJ=4TO8:C(I,J)=0
101 NEXT:NEXT
105 FORI=0TO5:READ W(I):NEXT
107 FORI=1TO3:LL(I)=-1:NEXT
110 DATA0,1,0,25,.1,.02,0,1,0,18,.2,.01,0,1,0,25,.1,.01
120 DATA0,3,0,25,.1,.06,0,4,0,25,.1,.04,2,5,0,0,.25,.04
130 DATA270,90,525,230,60,560,230,60,560,25,20,380,25,20,380
131 DATA0,0,0
140 DATA0,21,21,21,0,30,23,30,0,21,21,21,0,21,21,21
141 DATA3,27,38,14,3,27,35,15,4,25,37,13,5,14,14,10
150 DATA1.5,1.4,1.3,1.3,1.2,1
160 FORI=0TO9:S(I,9)=-1:NEXT:S6=.041:S7=.043
200 CLS1:GOSUB26000
201 GOSUB12100
202 FORI=11TO0STEP-1
205 FORZZ=1TO24:O$=O$+CHR$(175):NEXT
210 FORJ=0TO5:IF(INT(F(J,1)/100)<>I)OR(F(J,2)=0ANDJ<3)THEN250
215 K=INT(F(J,0)/100)+1
220 IFK<1ORK>12 THEN250
221 K=2*K-1:IFMID$(O$,K,1)=CHR$(175)THEN240
222 K=K+1
230 IFMID$(O$,K,1)=CHR$(175)THEN240
231 K=K-2
240 O$=LEFT$(O$,K-1)+MID$(M$,F(J,3),1)+RIGHT$(O$,24-K)
250 NEXT
260 IFI=11THENPRINTO$;"   ";MID$(A$,3,2);MID$(A$,7,2);:GOTO290
265 IFI=10THENPRINTO$;" 6/";:PRINT RIGHT$(STR$(D),1);:PRINT"/42";:GOTO 290
270 IFI=8THENPRINTO$;"  TF-16";:GOTO 290
275 IFI=7THENPRINTO$;" CRS";:PRINTRIGHT$(STR$(INT(F(3,4)/P1+.5)),3);:GOTO290
280 IFI=5THENPRINTO$;"  TF-17";:GOTO 290
285 IFI=4THENPRINTO$;" CRS";:PRINTRIGHT$(STR$(INT(F(4,4)/P1+.5)),3);:GOTO290
286 IFI=2THENPRINTO$;"   0";:GOTO290
287 IFI=1THENPRINTO$;"270*090";:GOTO290
288 IFI=0THENPRINTO$;"  180";:GOTO290
289 PRINTO$;
290 GOSUB27400:PRINT:NEXT:GOSUB 10100
295 MM$="Y"
300 CC=0:GOSUB26000:PRINT"FLEET COMMAND? (T,S,A,R,M,0,#)";CHR$(191);:A$=INKEY$
301 A$=INKEY$:IFA$=""THEN301
302 IFCC=1AND(A$<>"M"ORA$<>"S")THENCLS
303 X=VAL(A$)
304 IFX=6ORX=7ORX>9THENSOUND200,2:CLS:PRINT"INPUT OF 6 OR 7 FOR FLEET":PRINT"COMMANDS NOT ALLOWED!":PRINT:MM$="N":GOTO300
310 X=ASC(A$):IFX>=48ANDX<=57THEN 1210
312 A$=LEFT$(A$,1)
313 IFA$="T"THEN400
314 MM$="N"
315 IFA$="C"THEN340
317 IFA$="R"THEN14000
321 IFA$="S"THEN500
330 IFA$="M"THEN200
333 IFA$="A"THENCLS8:GOTO600
334 IFA$="Q"THEN7000
335 SOUND150,1
340 MM$="N":CLS0:GOSUB26030:PRINT@70,"LEGAL FLEET COMMANDS";:PRINT@128," C-REVIEW COMMANDS"
341 PRINT" T-CHANGE TF COURSE"
342 PRINT" S-GET STATUS REPORT"
343 PRINT" M-PRINT MAP"
344 PRINT" A-AIRCRAFT COMMANDS":PRINT" R-CALCULATE RANGE TO TARGET"
345 PRINT" Q-ABORT GAME, GET SCORE"
346 PRINT" 0-EXECUTE ONE TACTICAL TURN"
347 PRINT" 1-9 PROCEED THAT MANY HOURS"
348 PRINT @456,"FLEET COMMAND? ";CHR$(191);:CC=1:GOTO301
400 GOSUB26010
401 IFMM$="Y"THENGOSUB450
402 CLS6:PRINT"WHICH TASK FORCE? (6,7 OR F) ";CHR$(191);
403 II$=INKEY$:IFII$=""THEN403
404 IFII$="6"THENI=3:GOTO409
405 IFII$="7"THENI=4:GOTO409
406 IFII$<>"F"THENSOUND50,5:PRINT:PRINT"ENTER 6,7 OR F TO EXIT!";:X=3:GOSUB13100:GOTO400
407 IFMM$="Y"THENGOSUB450:GOTO300
408 CLS6:GOTO300
409 IFMM$="Y"THENPRINT@413,II$:GOTO411
410 PRINT@29,II$
411 GOSUB26200:PRINT"NEW COURSE FOR ";T$(I);:INPUTJ
412 IFJ<0ORJ>360THENSOUND50,5:PRINT"BAD COURSE!":X=2:GOSUB13100:GOTO401
413 F(I,4)=J*P1
415 J$=STR$(J):IFJ<100THENJ$="0"+MID$(J$,2):GOTO417
416 J$=MID$(J$,2)
417 IFJ<10THENJ$="0"+J$
418 IFJ=0 THENJ$="000"
420 GOSUB26020:PRINTT$(I);" TURNING TO COURSE ";J$;".";
430 IFI=3 ANDMM$="Y" THENPRINT@156,J$;
435 IFI=4 AND MM$="Y" THENPRINT@252,J$;
440 X=2:GOSUB13100:GOTO400
450 FORZZ=1TO4:PRINT@352+ZZ*32,SG$;:NEXTZZ:PRINT@384,"";:RETURN
500 GOSUB26000:CLS6:GOSUB 12100
501 PRINT "STATUS REPORT AS OF ";
502 PRINT MID$(A$,3,2);MID$(A$,7,2);
505 PRINT " 6/";:PRINTRIGHT$(STR$(D),1);:PRINT "/42"
510 PRINT "    CAP    ARMED    BELOW DECKS"
515 PRINT "--- --- ----------- -----------"
520 PRINT "CV  F4F F4F SBD TBD F4F SBD TBD"
530 FOR I=4TO7:PRINTLEFT$(N$(I),4);
531 PRINTRIGHT$(STR$(C(I,7)),4);:PRINTRIGHT$(STR$(C(I,4),4);
540 PRINTRIGHT$(STR$(C(I,5)+1000*(C(I,5)>=1000)),4);RIGHT$(STR$(C(I,6),4);:PRINTRIGHT$(STR$(C(I,1)),4);RIGHT$(STR$(C(I,2)),4);
550 PRINTRIGHT$(STR$(C(I,3)),4);
570 PRINT :NEXT
572 FORI=4TO7:PRINT N$(I);
573 X=I:GOSUB 12800:PRINT TAB(11);
574 IF S1=0THENPRINT"NO DAMAGE";:GOTO 577
575 IFS1<3THENPRINTS$(S1);" DAMAGE";:GOTO577
576 IFI<7THENPRINTS$(3);:GOTO578
577 PRINTS$(4);
578 IF F(C(I,0),2)=2 AND I<>7 AND S1<3 THEN PRINT TAB(23);"spotted";
579 PRINT " ":NEXT
580 GOSUB10100:IFL=0THENPRINT"NO CONTACTS.":GOTO300
600 CLS2:CC=0:GOSUB26000:PRINT"       AIRCRAFT OPERATIONS"
601 A$="":PRINT "CARRIER? (OR F TO EXIT) ";CHR$(191);:A$=INKEY$
602 A$="":A$=INKEY$:IFA$=""THEN602
604 IFA$="F"THENCLS:A$=INKEY$:GOTO300
607 IFA$="C"THEN650
608 IFA$="S"THEN500
610 I=ASC(A$):I=-4*(I=69)-5*(I=72)-6*(I=89)-7*(I=77):IFI<>0THEN616
611 IFCC=1THENCLS
612 CLS0:SOUND50,2:PRINT@66,"LEGAL RESPONSES TO CARRIER";
613 PRINT@160," E - ENTERPRISE":PRINT" H - HORNET":PRINT " Y - YORKTOWN":PRINT" M - MIDWAY ISLAND":PRINT" S - GET STATUS REPORT"
614 PRINT" F - RETURN TO FLEET COMMANDS":PRINT@419,"";:GOTO601
615 X=I:GOSUB12800:IFS1<2THEN619
616 PRINTN$(I);:IFS1=2THENPRINT" HEAVILY DAMAGED--":PRINT"  AIR OPERATIONS IMPOSSIBLE!":GOTO1140
617 IFI=7THENPRINT" AIRFIELD DESTROYED!":GOTO1140
618 PRINT" IS SUNK!":GOTO1140
619 IFI<7THENX$="USS "+N$(I):GOTO622
620 X$=N$(7)+" ISLAND"
621 ZZ=LEN(X$):CLS8:GOSUB26010:PRINTTAB((32-ZZ)/2);X$
622 A$="":INPUT"AIRCRAFT COMMAND (OR F)";A$:IFA$=""THEN600
623 IFA$="F"THENCLS:GOTO300
624 IFCC=1THENCLS8:PRINTN$(I):GOSUB26000:CC=0
625 IFA$="C"THEN650
626 IFA$="S"THEN500
627 IFA$="AA"THEN700
628 A$=A$+" "
630 A$=LEFT$(A$,2):IFA$="CA"THEN900
631 IFA$="CL"THEN800
640 IFA$="A "THEN700
642 IFA$="L "THEN1000
649 SOUND 50,2
650 CLS0:PRINT@37,"LEGAL AIRCRAFT COMMANDS";
652 PRINT@96," CA -- ALLOCATE CAP"
653 PRINT" A  -- ARM INDIVIDUAL AIRCRAFT"
654 PRINT" AA -- ARM ALL AIRCRAFT"
655 PRINT" CL -- CLEAR DECK/RUNWAY"
656 PRINT" L  -- LAUNCH STRIKE"
657 PRINT" C  -- REVIEW AIRCRAFT COMMANDS":PRINT" S  -- GET STATUS REPORT"
658 PRINT" F  -- RETURN TO FLEET COMMANDS"
659 PRINT "<ENTER> - SELECT CARRIER":PRINT@419,"";:CC=1:GOTO622
700 IFC(I,4)+C(I,5)+C(I,6)=0THEN725
720 PRINT "STRIKE ALREADY ARMING.":GOTO1140
725 IFC(I,1)+C(I,2)+C(I,3)=0THENPRINT"NO AIRCRAFT AVAILABLE!!":GOTO1140
726 IFA$="AA"THEN782
730 PRINT"NUMBER OF AIRCRAFT TO SPOT:"
731 INPUT " F4F'S";J
733 INPUT " SBD'S";K
734 INPUT " TBD'S";L
735 J=-J*(J>0):K=-K*(K>0):L=-L*(L>0)
740 IFJ>C(I,1)THENJ=C(I,1)
741 IFK>C(I,2)THENK=C(I,2)
742 IFL>C(I,3)THENL=C(I,3)
750 C(I,1)=C(I,1)-J:C(I,4)=J
760 C(I,2)=C(I,2)-K:C(I,5)=K+1000:C(I,3)=C(I,3)-L:C(I,6)=L
770 IFA$<>"AA"THENPRINTN$(I);" STRIKE ARMING."
775 GOSUB26040:GOTO1150
782 PRINT"ARMING ALL ";N$(I);" AIRCRAFT.":J=C(I,1):K=C(I,2):L=C(I,3):GOTO750
800 IFC(I,4)+C(I,5)+C(I,6)=0THENPRINT"NO AIRCRAFT ARMED!":GOTO1140
805 IFI<7THENPRINT "AIRCRAFT BEING STRUCK BELOW";:GOTO810
806 PRINT"AIRCRAFT BEING DISPERSED";
810 FORZZ=1TO250
811 IFZZ=60ORZZ=120ORZZ=180THENSOUND240,1:PRINT".";
815 NEXT 
820 GOSUB10200:GOSUB26200:PRINT:PRINTN$(I);:IFI<>7THENPRINT" DECKS NOW CLEAR.":GOTO822
821 PRINT" RUNWAYS NOW CLEAR."
822 GOTO1150
900 C(I,1)=C(I,1)+C(I,7):C(I,7)=0
905 IFC(I,1)+C(I,4)=0THENPRINT"NO AIRCRAFT AVAILABLE!":GOTO1140
910 INPUT"NUMBER OF 4F4'S FOR CAP";J:J=-J*(J>0)
915 IFJ>C(I,1)THEN930
920 C(I,7)=J:C(I,1)=C(I,1)-J:GOTO960
930 C(I,7)=C(I,1):C(I,1)=0:J=J-C(I,7)
940 C(I,7)=C(I,7)-J*(J<C(I,4))-C(I,4)*(J>=C(I,4))
950 C(I,4)=(J-C(I,4))*(C(I,4)>J)
960 GOSUB26000:IFI<6THEN965
961 PRINT"NOW";C(I,7);N$(I);" F4F'S ON CAP.":GOTO1150
965 PRINT:PRINT"TF-16 CAP NOW NUMBERS";C(4,7)+C(5,7);"F4F'S.":PRINT" ";C(4,7);TAB(5);"FROM ENTERPRISE."
966 PRINT" ";C(5,7);TAB(5);"FROM HORNET.":GOTO1150
1000 IFC(I,5)+C(I,6)>0ANDC(I,5)<1000THEN1020
1010 SOUND100,1:PRINTN$(I);" HAS NO STRIKE ARMED!":GOTO1150
1020 GOSUB10100:IFL>0THEN1030
1021 PRINT"NO TARGETS.":GOTO1140
1030 INPUT "CONTACT NUMBER (OR ENTER) ";J
1031 IFJ=0 THENPRINT:GOTO601
1032 IF J>L OR J<1 THENPRINT"BAD CONTACT NUMBER!":GOTO1140
1033 J=C1(J)
1040 X=J:Y=C(I,0):GOSUB12600:R=R1:IFR<=200THEN1050
1045 IFRC=0THENPRINTINT(R+.999999);"NM. ";:GOTO1047
1056 PRINT "---";
1047 PRINT "OUT OF RANGE.":GOTO1140
1050 L=R*.3:IFI=7OR(T+L+L>=240ANDT+L+L<=1140)THEN1070
1060 PRINT"NIGHT CV OPERATIONS IMPOSSIBLE!":GOTO1140
1070 IFT+L>=240ANDT+L<1140THEN1090
1080 PRINT"NIGHT ATTACKS NOT POSSIBLE!":GOTO1140
1090 K=0
1091 IFS(K,9)<0THEN1110
1092 K=K+1:IFK<10THEN1091
1100 PRINT "TOO MANY STRIKES ALOFT!":GOTO1140
1110 S(K,0)=C(I,4):S(K,2)=C(I,5):S(K,4)=C(I,6):C(I,4)=0:C(I,5)=0
1120 C(I,6)=0:S(K,6)=J:S(K,9)=I:S(K,7)=T+L:S(K,8)=T+L+L
1130 S(K,3)=1:S(K,5)=0:S(K,1)=-((S(K,2)/(S(K,2)+S(K,4)))>RND(0))
1138 GOSUB26050:PRINTN$(I);" STRIKE LAUNCHED!":GOTO1150
1140 SOUND50,2
1150 X=2:GOSUB13100:PRINT:GOTO601
1200 A$="0"
1210 CLS0:GOSUB26020:PRINTTAB(7);"-- PLOT RUNNING --":T0=T+INT(VAL(A$)*60):D0=D-1*(T0>1440):T0=T0+1440*(D0>D)
1220 FORI=4TO7:C(I,5)=C(I,5)+1000*(C(I,5)>=1000):NEXT
1300 FORK=1TO2:X=K:Y=5:GOSUB12600:IFR1<15 THENF(K,5)=0
1301 IFJ9=0 THEN1320
1310 F(K,5)=18-7*(K=2):F(K,4)=270*P1
1320 NEXT:X=0:GOSUB12600:X=850-F(0,0):Y=450-F(0,1):IFR1>250 THENGOSUB12500
1330 IFR1>100 THEN1350
1340 F(0,4)=F(0,4)+180*P1+360*P1*(180*P1>F(0,4))
1350 FORK=6TO4STEP-1:IFF(C(K,0),2)=0ORC(K,8)=100 THEN1360
1355 X=F(C(K,0),0)-F(0,0):Y=F(C(0,0),1)-F(0,1):GOSUB12500
1360 NEXT
1370 IFJ9>0THENF(0,4)=270*P1
1400 FORI=0TO3:X=I:GOSUB12800:IFC(I,7)=5 THEN1440
1401 IFS1>1THEN1440
1405 C(I,7)=C(I,7)+C(I,1):C(I,1)=0
1410 IFC(I,7)<5THEN1420
1411 C(I,1)=C(I,7)-5:C(I,7)=5:GOTO1440
1420 C(I,7)=C(I,7)+C(I,4):C(I,4)=0:IFC(I,7)<=5 THEN1440
1430 C(I,4)=C(I,7)-5:C(I,7)=5
1440 NEXT
1500 S9=0:A9=S9:A8=S9:IFT>1140 THEN1700
1501 I=0
1510 IFC(I,4)+C(I,5)+C(I,6)>0 THEN1520
1511 I=I+1:IFI<4THEN1510
1512 S9=0:GOTO1590
1520 I=4
1521 X=I:GOSUB12800:IFS1>1THEN1525
1522 X=C(I,0):GOSUB13000:IFE1=1THEN1530
1525 I=I+1:IFI<8 THEN1521
1226 I=0
1530 IFI>0THEN1550
1531 I=4
1532 X=I:GOSUB12800:IFS1>2 THEN1540
1535 X=C(I,0):GOSUB13000:IFE1=1THEN1542
1540 I=I+1:IFI<8THEN1532
1541 I=0
1542 IFI>0THEN1550
1543 X=5:GOSUB13000:I=-7*(E1=1)
1550 S9=C(I,0):IFS9<5THEN1590
1560 I=0
1561 IFS(I,6)<5ORS(I,9)=-1ORS(I,1)=-1THEN1580
1570 S9=0:GOTO1590
1580 I=I+1:IFI<10THEN1561
1590 IFF(3,2)>0ORF(4,2)>0 THENA9=1
1600 X=0:Y=5:GOSUB12600:IFR1>235 THEN1620
1601 L=60*R1/235
1610 IFT+L<240ORT+L+L>1140ORL>60 THEN1620
1615 A8=1:IFC(3,2)<12THENA9=1
1620 IFA9*1 THENA8=0
1700 IFS9<3 THEN1770
1720 FORJ=0TO9:IFS(J,9)=-1 THEN1730
1721 NEXT:GOTO1770
1730 S(J,6)=S9:S(J,9)=0:X=0:Y=S9:GOSUB12600:L=60*R1/235:S(J,7)=T+L
1740 S(J,8)=T+L+L
1750 S(J,0)=0:S(J,2)=0:S(J,4)=0
1751 FORI=0TO3:IFC(I,8)>60 THEN1767
1760 S(J,0)=S(J,0)+C(I,4):S(J,2)=S(J,2)+C(I,5):C(I,4)=0:C(I,5)=0
1765 S(J,4)=S(J,4)+C(I,6):C(I,6)=0
1767 NEXT:IFS(J,0)+S(J,2)+S(J,4)=0 THENS(J,9)=-1
1768 IFS(J,2)+S(J,4)=0 THEN1770
1769 S(J,3)=1:S(J,5)=0:S(J,1)=-(((S(J,2)/S(J,2)+S(J,4)))>RND(0))
1770 FORI=0TO3:GOSUB10200:X=I:GOSUB12800:IFS1>1THEN1820
1775 IFA9=0THEN1790
1776 C(I,4)=C(I,1):C(I,5)=C(I,2):C(I,6)=C(I,3)
1780 C(I,1)=0:C(I,2)=0:C(I,3)=0:GOTO1820
1790 IFA8=0THEN1820
1791 C(I,4)=INT(C(I,1)/2):C(I,5)=INT(C(I,2)/2)
1800 C(I,6)=INT(C(I,3)/2):C(I,1)=C(I,1)-C(I,4)
1810 C(I,2)=C(I,2)-C(I,5):C(I,3)=C(I,3)-C(I,6)
1820 NEXT
1900 F9=0:T1=30+INT(30*RND(0)):T=T+T1:IF T>=T0 AND D=D0 THENF9=1
1910 D=D-(T>1440):T=T+1440*(T>1440):IFT=T0ANDD>=D0 THENF9=1
2000 FORI=0TO4:F(I,0)=F(I,0)+T1*F(I,5)*SIN(F(I,4))/60
2010 F(I,1)=F(I,1)+T1*F(I,5)*COS(F(I,4))/60:NEXT
2100 IFT>1140ORT<240 THEN2220
2101 P=1-2*(T<300OR(T>720ANDT<780))
2110 FORI=0TO2:IFF(I,2)=2 THEN2160
2111 IFF(I,5)=0THENF(I,2)=2
2115 IFF(I,2)=1 ANDRND(0)>3*S7 THEN2160
2119 IFDF=1 THENF(I,2)=F(I,2)-(F(I,2)<2):GOTO2140
2125 IFRND(0)>P*S7 ANDF(I,2)=0 THEN2160
2126 F(I,2)=F(I,2)-(F(I,2)<2)
2130 IFRND(0)>3*S7 THEN2140
2131 F(I,2)=F(I,2)-(F(I,2)<2)
2140 GOSUB26800:IFI<2THEN2144
2142 IFRND(2)=2 OR DF=1 THEN2144
2143 PRINT"US SUBMARINE REPORTS PRESENCE":PRINT"   OF JAPANESE ";:GOTO2150
2144 PRINT"PBY SPOTS JAPANESE ";
2150 F9=1:IFF(I,2)=1THENPRINT"SHIPS."
2151 PRINTT$(I);"."
2160 NEXT:X=5:GOSUB13100:IFF(0,2)=2 THENF(0,3)=2
2170 P=1-(T>720 ANDT<780):IFDF=3 THENP=P+1
2171 FORI=3TO4:IFF(I,2)=2 THEN2210
2180 IFRND(0)<(P*S6) THENF(I,2)=1
2181 IFF(I,2)=0ORRND(0)>3*S6 THEN2210
2190 GOSUB26850:GOSUB14600:X=3:GOSUB13100
2200 F(I,2)=2:F9=1
2210 NEXT:GOTO2300
2220 FORI=0TO4:F(I,2)=0:NEXT:F(0,3)=1
2300 FU=1:FORI=0TO9:IF(S(I,9)=-1)OR(S(I,7)>T)OR(S(I,1)=-1) THEN4000
2310 IFS(I,6)<3THENJ=1:GOTO2312
2311 J=2
2312 FORK=0TO4STEP2
2315 IFS(I,K)=0THENS(I,K+1)=-1
2316 IFRND(0)>.2THEN2330
2320 IFS(I,1)=S(I,K+1)THENS(I,1)=-1
2321 S(I,K+1)=-1
2330 NEXT:IFJ=2THEN2360
2332 CLS:SOUND125,10
2333 FORK=0TO4STEP2
2335 IFS(I,K)=0ORS(I,K+1)>-1 THEN2350
2340 GOSUB14700:X=4:GOSUB13100
2350 NEXT
2360 IFS(I,3)+S(I,5)=-2ORS(I,2)+S(I,4)=0THEN4000
2362 IFJ=2 THENCLS:SOUND125,10
2365 F(C(S(I,9),0),2)=2:F(S(I,6),2)=2
2370 IFF(0,2)=2THENF(0,3)=2
2380 IFJ=1THENPRINTN$(S(I,9));:GOTO2390
2381 PRINT"JAPANESE";
2390 PRINT" STRIKE ATTACKS ";
2391 IF POS(0)+LEN(T$(S(I,6)))>31THENPRINT:PRINT"  ";
2392 PRINTT$(S(I,6));"!"
2395 X=1:GOSUB13100:F9=1
2400 IFS(I,6)=1ORS(I,6)=2THEN2420
2401 K=4*(J-1)
2410 IFS(I,6)=C(K,0) ANDC(K,8)<100 THEN2600
2411 K=K+1:IFK<=4*J-1 THEN2410
2420 PRINT"ON THE WAY IN,":GOSUB10300:FORK=2TO4STEP2
2430 IFS(I,K)=0ORS(I,K+1)=-1 THEN2520
2440 PRINTS(I,K);P$(J+K-1);" ATTACK ";
2441 SS=S(I,6):IFSS=0 THENSS=6
2442 XX=POS(0):YY=LEN(T$(SS))
2445 IFXX+YY>31THENPRINT:PRINT"   ";
2448 PRINTT$(SS);"!":X=2:GOSUB13100
2450 E=F(S(I,6),6)*(-.75*(K=4)-(K=2)):H=0:N=0
2460 FORL=1TOS(I,K):GOSUB26400:R=RND(0):IFR<E THENGOSUB15000:GOTO2475
2470 IF(K=2ORS(I,6)=5) AND (R<2*E ANDR>=E) THENGOSUB15100:GOTO2475
2471 GOSUB26500
2475 NEXT:IFPOS(0)>0 THENPRINT
2480 PRINTP$(J+K-1);" MAKE";H;"HITS";
2490 IFK=2 OR S(I,6)=5 THEN2495
2491 PRINT".":X=2:GOSUB13100:GOTO2500
2495 PRINT:PRINT"  AND";N;"NEAR MISSES.":X=2:GOSUB13100
2500 V=5*H+2*N:GOSUB27200:PRINTV;"VICTORY POINTS AWARDED.":X=3:GOSUB13100
2510 IFJ=1 THEN V0=V0+V:GOTO2520
2511 V1=V1+V
2520 NEXT:PRINT"ON THE WAY OUT,":GOSUB10300
2530 X=3:GOSUB13100:GOTO3950
2600 C=0:FORK=4*(J-1)TO(4*J)-1:IFC(K,0)<>S(I,6) THEN2620
2610 C=C+C(K,7):C(K,7)=0
2620 NEXT:IFC=0THEN2790
2621 K=2-2*(RND(0)>.5)
2630 IFS(I,K+1)=-1ORS(I,K)=0 THENK=2-2*(K=2)
2635 IFFU=0 ANDJ=1 THENK=4
2636 IFK=4THENFU=0
2640 IFS(I,K+1)=-1ORS(I,K)=0THEN2781
2650 PRINT "CAP ATTACKS ";P$(J+K-1);".":X=2:GOSUB13100
2660 IFS(I,1)=S(I,K+1)THENL1=S(I,0):GOTO2670
2661 L1=0
2670 IFL1>0THENPRINTP$(J-1);" DEFEND ";P$(J+K-1);".":X=2:GOSUB13100
2680 E=(C*W(J-1))/(L1*W(-(J=1))+S(I,K)*W(K-(J=1)))
2690 IFE>.85 THENE=.85
2691 H=0:FORL=1TOS(I,K):IFRND(0)<E THENH=H+1
2700 NEXT:GOSUB26100:PRINT"CAP SHOOTS DOWN";H;P$(J+K-1);".":X=2:GOSUB13100
2710 S(I,K)=S(I,K)-H:IFL1=0 THEN2781
2715 PRINTP$(J-1);" ATTACK CAP.":X=1:GOSUB13100
2720 E=(L1*W(-(J=1)))/(C*W(J-1)):IFE>.85 THENE=.85
2721 H=0
2730 FORL=1TOC:IFRND(0)<E THENH=H+1
2731 NEXT:GOSUB26100
2740 PRINTP$(J-1);" SHOOT DOWN";H;P$(2-J);".":X=2:GOSUB13100
2750 C=C-H:IFC=0 THEN2790
2751 E=.5*(C*W(J-1))/(L1*W(-(J=1))):IFE>.85 THENE=.85
2760 H=0:FORL=1TOL1:IFRND(0)<E THENH=H+1
2761 NEXT:GOSUB26100
2770 PRINT"CAP SHOOTS DOWN ";H;P$(J-1);".":X=2:GOSUB13100
2780 S(I,0)=S(I,0)-H
2781 IFC=0 THEN2790
2782 L=-1:FORK=1TOC
2783 L=-(L+1)*(L<7):IFS(I,6)<>C(L,0)ORC(L,8)>=60THEN2782
2784 C(L,7)=C(L,7)+1:NEXT
2785 IF(S(I,3)<0ORS(I,2)=0)AND(S(I,5)<0ORS(I,4)=0)THEN3950
2790 PRINT"ON THE WAY IN,":GOSUB10300
2800 FORK=4TO2STEP-2:IFS(I,K)=0ORS(I,K+1)=-1 THEN2940
2810 M=0:FORL=0TO7:C(L,9)=0
2820 IFC(L,8)<100 ANDC(L,0)=S(I,6) THENM=M+1
2821 NEXT:IFM=0THEN2940
2825 O=-1:FORN=1TOM
2827 O=-(O+1)*(O<7):IFC(O,0)<>S(I,6)ORC(O,8)=100 THEN2827
2830 C(O,9)=INT((S(I,K)+M-N)/M):NEXT
2840 FORL=0TO7:IFC(L,9)=0 THEN2930
2850 PRINTC(L,9);P$(J+K-1);" ATTACK ";N$(L);".":X=2:GOSUB13100
2860 N=0:H=0:E=.2-(K=4)*.06*(J=1)
2862 E=.2
2865 FORM=1TOC(L,9):R=RND(0)
2870 GOSUB26400:IFR>=E THEN2880
2872 GOSUB15000:GOTO2890
2880 IF R>=E+E OR(K=4 AND S(I,6)<>5) THENGOSUB26500:GOTO2890
2883 GOSUB15100
2890 NEXT:IFPOS(0)THENPRINT
2895 GOSUB26220:PRINTN$(L);" TAKES";H;"HITS";
2900 IFN>0 THENPRINT:PRINT"   AND";N;"NEAR MISSES.":GOTO2904
2901 PRINT"."
2904 X=1:GOSUB13100
2906 IFH+N>0ANDC(L,4)+C(L,5)+C(L,6)>0THEN2908
2907 GOTO2910
2908 GOSUB27100:PRINT"ARMED AIRCRAFT ARE EXPLODING ":PRINT"   ON THE ";:IFL=7THENPRINT"RUNWAY!":GOTO2910
2909 PRINT"DECK!"
2910 IFH>0 THENFORM=1TOH:GOSUB10400:GOSUB10400:NEXT
2920 IFN>0 THENFORM=1TON:GOSUB10400:NEXT
2930 NEXT
2940 NEXT:PRINT"ON THE WAY OUT,":GOSUB10300
2945 X=3:GOSUB13100
3950 FORK=0TO4STEP2:S(I,K+1)=-1:NEXT
4000 NEXT
4100 FORL=0TO7:IFC(L,8)=100ORC(L,8)<10 THEN4150
4110 IFRND(0)>.05*(1-(L<4))THEN4130
4115 IFL<4THENPRINT"PBY REPORTS ";
4120 GOSUB26750:PRINT"EXPLOSION ON ";N$(L);".":X=3:GOSUB13100:GOSUB10400
4130 IFC(L,8)=100ORRND(0)>.2*(1-(L>3)) THEN4150
4140 C(L,8)=C(L,8)-5*RND(0):C(L,8)=-C(L,8)*(C(L,8)>0)
4150 NEXT
5000 V2=0:FORJ=0TO9:IFS(J,9)=-1 THEN5220
5001 V2=1
5005 IFT<S(J,8) THEN5220
5006 IFS(J,9)<4 THEN5120
5010 F9=1:I=S(J,9):IFC(I,8)>60 THEN5050
5020 GOSUB26650:PRINT"STRIKE LANDING ON ";N$(I);".":X=2:GOSUB13100:GOSUB10200
5030 C(I,1)=C(I,1)+S(J,0):C(I,2)=C(I,2)+S(J,2)
5040 C(I,3)=C(I,3)+S(J,4):GOTO5210
5050 IFI>5OR(C(4,8)>60 ANDC(5,8)>60) THEN5060
5051 K=4-(I=4):GOTO5110
5060 K=4
5061 IFC(K,8)>60THEN5070
5065 X=C(I,0):Y=C(K,0):GOSUB12600:IFR1<100 THEN5100
5070 K=K+1:IFK<8THEN5061
5080 PRINT N$(I);" STRIKE TRYING TO DI-":PRINT"  VERT, DITCHES OUT OF FUEL.":GOSUB26610:X=3:GOSUB13100
5090 GOTO5210
5100 IFRND(0)>.8THEN5080
5110 PRINTN$(I);" STRIKE AIRCRAFT ";
5111 IFPOS(0)<27 THENPRINT"DIVERT":PRINT"   ";:GOTO5113
5112 PRINT:PRINT"   DIVERT ";
5113 PRINT"TO ";N$(K);" ";:GOSUB26700
5114 I=K:GOTO5020
5120 L=0:FORI=0TO3:GOSUB12800:IFS1<2THENL=L+1
5121 NEXT:IFL=0THEN5210
5130 FORI=0TO3:GOSUB10200:NEXT:FORK=0TO4STEP2:M=-1:FORI=1TOL
5140 M=-(M+1)*(M<3):X=M:GOSUB12800:IFS1>1THEN5140
5150 C(M,1+K/2)=C(M,1+K/2)+INT((L+S(J,K)-I)/L)
5170 NEXT:NEXT:FORI=0TO3
5180 IFC(I,1)+C(I,2)+C(I,3)<96THEN5200
5181 C(I,1)=C(I,1)+(C(I,1)>0)
5190 C(I,2)=C(I,2)+C(I,2)>0:C(I,3)=C(I,3)+C(I,3)>0:GOTO5180
5200 NEXT
5210 S(J,9)=-1
5220 NEXT
6000 I=0
6001 IFC(I,8)<=60THEN6010
6002 I=I+1:IFI<4THEN6001
6003 J9=1
6010 IFV2=1THEN6050
6011 IFJ9=1 ANDF(0,0)<0 THEN7000
6020 I=0
6021 IFC(I,8)<100THEN6030
6022 I=I+1:IFI<4THEN6021
6023 GOTO7000
6030 IFF(3,0)>1200ORF(4,0)>1200THEN7000
6031 I=4
6040 IFC(I,8)<100THEN6050
6041 I=I+1:IFI<8THEN6040
6042 GOTO7000
6050 IFF9=0THEN6053
6051 GOTO6055
6053 A$=INKEY$:IFA$="P"THENCLS:PRINT"PAUSING...":GOTO300
6054 GOSUB12100:GOSUB26030:PRINT"TIME: ";MID$(A$,3,2);MID$(A$,7,2):GOTO1300
6055 IFRND(0)<.8 THENRC=0:GOTO6057
6056 RC=1
6057 FORZZ=1TO2000:NEXT:J$=INKEY$:GOTO200
7000 CLS:SOUND100,10:PRINT"      THE GAME IS OVER!!!"
7010 PRINT"CARRIER";TAB(18);"DAMAGE STATUS":V2=0:V3=0:P1=0:PRINT"----------";TAB(18);"-------------"
7020 FORX=0TO7:GOSUB27400:GOSUB12800:IFX<7THENPRINTN$(X);TAB(22);S$(S1):GOTO7030
7021 SS$=S$(S1)
7030 S1=(S1=1)+3*(S1=2)+10*(S1>2):S1=S1*100:IFX<4THENV2=V2-S1:GOTO7040
7031 V3=V3-S1
7040 FORJ=1TO7:IFX<4THENP0=P0+C(X,J):GOTO7050
7041 P1=P1+C(X,J)
7050 NEXT:NEXT:P1=269-P1:P0=272-P0
7055 PRINTN$(7);" (";
7060 A$="":IFJ9>0THENA$="NOT ":GOTO7070
7061 V3=V3+1000
7070 PRINTA$;"INVADED)";TAB(22);SS$
7100 GOSUB27400:PRINT"AIRCRAFT LOST:     US:";:PRINTRIGHT$(STR$(P1),3);:PRINT" J:";:PRINTRIGHT$(STR$(P0),3)
7101 GOSUB27400:PRINT"OTHER VICTORY PTS: US:";:PRINTRIGHT$(STR$(V0),3);:PRINT" J:";:PRINTRIGHT$(STR$(V1),3)
7106 V0=V0+V2+5*P0:V1=V1+V3+5*P1:VV=V0-V1
7109 GOSUB27400:PRINT "TOTALS: US:";V0;" J:";V1
7110 IFVV<.01THENB$="JAPANESE":GOTO7120
7111 B$="US"
7120 VV=ABS(VV):PRINTB$;" ";:IFVV>2000THENA$="STATEGIC":GOTO7140
7130 IFVV<1000THENA$="MARGINAL":GOTO7140
7131 A$="TACTICAL"
7140 PRINTA$;" VICTORY."
7145 IFB$="JAPANESE" THENPRINT"TOUGH LUCK. ";:GOTO7220
7146 IFVV<1000THENPRINT"GOOD TRY. ";:GOTO7220
7148 IFVV<2000THENPRINT"EXCELLENT! ";:GOTO7220
7149 PRINT"MEDAL OF HONOR-";:GOSUB25500:GOTO7230
7220 PRINT"GO ";
7230 INPUT"AGAIN? (Y/N)";A$:IFA$="Y"THENCLS:RESTORE:GOTO35
7235 CLS0:PRINT@168,"SEE YOU NEXT TIME!";:X=4:GOSUB13100:PRINT@448,"";:END
10000 L=L+175*RND(0)-200*RND(0)*(I<3):J=(J+K*RND(0))*P1
10010 F(I,0)=850-L*SIN(J)*(I<>5)
10011 F(I,1)=450-L*COS(J)*(I<>5)
10012 IFI<3 THEN 10020
10013 IFF(I,0)>1199THENF(I,0)=1199
10014 IFF(I,1)>1199THENF(I,1)=1199
10020 J=J+180*P1+360*P1*(J>180*P1)
10030 IFI<3THENF(I,4)=J:RETURN
10031 F(I,4)=-205*P1*(I<>5)
10040 RETURN
10100 L=0:FORK=0TO2:IFF(K,2)=0THEN10140
10101 L=L+1
10110 GOSUB26250:PRINT"CONTACT ";
10111 PRINTRIGHT$(STR$(L),1);
10112 PRINT" AT ";
10113 LL(L)=K
10115 PRINTRIGHT$(STR$(INT(F(K,0)/100)+1),2);:PRINT",";
10116 ZZ=INT(F(K,1)/100)+1
10117 IFZZ>9THENPRINTRIGHT$(STR$(ZZ),2);:GOTO10120
10118 PRINTRIGHT$(STR$(ZZ),2);
10120 IFF(K,2)=>2THENPRINTTAB(19);T$(K);
10130 PRINT:C1(L)=K
10140 NEXT:RETURN
10200 C(I,5)=C(I,5)+1000*(C(I,5)>=1000)
10210 C(I,1)=C(I,1)+C(I,4):C(I,4)=0:C(I,2)=C(I,2)+C(I,5)
10220 C(I,5)=0:C(I,3)=C(I,3)+C(I,6):C(I,6)=0:RETURN
10300 FORK=0TO4STEP2:IFS(I,K)=0ORS(I,K+1)=-1THEN10340
10310 E=-F(S(I,6),7)*(.4*(K=0)+.7*(K=2)+(K=4)):H=0
10320 FORL=1TOS(I,K):IFRND(0)<ETHENH=H+1
10321 NEXT
10322 GOSUB26100
10325 IFS(I,6)=1THENPRINT"TRANP'S";: GOTO10330
10327 IFS(I,6)=2THENPRINT"CA'S";:GOTO10330
10328 PRINTT$(S(I,6));
10330 PRINT" AA SHOOTS DOWN";
10335 PRINTH;P$(J+K-1);".":S(I,K)=S(I,K)-H
10340 NEXT:RETURN
10400 IFC(L,8)>=100THENRETURN
10410 D9=RND(0)*8*(1-(C(L,4)+C(L,5)+C(L,6)>0))
10411 IFL=7THEND9=D9/2
10415 FORL1=1TO6
10419 IFC(L,L1)=0THEN10430
10420 FORL2=1TOC(L,L1):IFRND(0)<D9/100THENC(L,L1)=C(L,L1)-1
10421 NEXT
10430 NEXT:C(L,8)=C(L,8)+D9:IFC(L,8)<60THENRETURN
10432 L1=I:I=L:GOSUB10200:I=L1:C(L,1)=C(L,1)+C(L,7):C(L,7)=0
10433 IFC(L,8)<100THENRETURN
10435 C(L,8)=100:GOSUB14500
10437 IFL=7THENPRINT@256,"   MIDWAY AIRFIELD DESTROYED!":GOTO10442
10438 A$=N$(L)+" BLOWS UP AND SINKS!":XX=LEN(A$):XX=(32-XX)/2
10440 PRINT@256,"";:FORZZ=1TOXX:PRINT" ";:NEXT:PRINTN$(L);" BLOWS UP AND SINKS!"
10442 GOSUB27000:X=3:GOSUB13100
10480 FORL1=1TO6:C(L,L1)=0
10490 NEXT:CLS0:RETURN
12100 A$=STR$(100+INT(T/60))+STR$(100+T-60*INT(T/60))
12120 RETURN
12500 R2=(90-180*(X<0))*P1:IFY=0THEN12530
12501 R2=ATN(X/Y):IFY<0THENR2=R2+180*P1
12530 F(0,4)=R2:RETURN
12600 R1=SQR((F(X,0)-F(Y,0))^2+(F(X,1)-F(Y,1))^2):RETURN
12800 S1=C(X,8):S1=-(S1>0)-(S1>60)-(S1>=100)*(1-(X=7)):RETURN
13000 Y=0:GOSUB12600:E1=0:IFF(X,2)=0ORR1>235THENRETURN
13010 L=R1*60/235:IFT+L<240ORT+L+L>1140THENRETURN
13020 E1=1:RETURN
13100 FORY=1TOX*500:NEXT:RETURN
14000 IFRC=0THEN14010
14001 CLS:GOSUB26600:PRINT:PRINT:PRINT"SORRY ADMIRAL ";AA$;",":PRINT
14002 IFRC=2THENPRINT"RANGE COMPUTER STILL OUT.":GOTO14004
14003 PRINT"RANGE COMPUTER IS OUT OF ORDER.":RC=2
14004 PRINT:PRINT"WE'LL HAVE IT FIXED AS SOON AS":PRINT"   WE CAN.":PRINT:X=2:GOSUB13100:A$=INKEY$:GOTO300
14010 CLS:GOSUB26000:PRINT:PRINT"   RANGE COMPUTER READY:":PRINT
14020 PRINT"   1. CONTACT # 1":PRINT"   2. CONTACT # 2":PRINT"   3. CONTACT # 3":PRINT"   4. ENTERPRISE/HORNET"
14021 PRINT"   5. YORKTOWN":PRINT"   6. MIDWAY":PRINT:PRINT
14026 GOTO14030
14027 GOSUB27150:FORZZ=320TO448STEP32:PRINT@ZZ,SG$:NEXT
14030 PRINT@320,"RANGE---":PRINT@352,"FROM WHICH NUMBER (F TO END) ";CHR$(191);:A$=INKEY$
14031 JJ$=INKEY$:IFJJ$=""THEN14031
14032 IFJJ$="F"THENCLS:GOTO300
14033 PRINT@381,JJ$;
14034 IFASC(JJ$)>48 AND ASC(JJ$)<55 THENRX=VAL(JJ$):GOTO14040
14038 SOUND50,2:PRINT@384,"BAD #/RE-ENTER    ":X=3:GOSUB13100:GOTO14027
14040 PRINT@384,"TO WHICH NUMBER ";CHR$(191);:JJ$=INKEY$:IFJJ$=""THEN14040
14041 PRINT@400,JJ$;
14043 IFASC(JJ$)<49ORASC(JJ$)>54THENSOUND50,1:GOTO14038
14044 Y=VAL(JJ$)
14045 IFRX>3 THENX=RX-1:GOTO14048
14046 IFLL(RX)=-1THEN14058
14047 X=LL(RX):IFF(X,2)=0 THEN14058
14048 IFRY>3 THENY=RY-1:GOTO14059
14049 IFLL(RY)=-1 THEN14058
14050 Y=LL(RY):IFF(Y,2)>0 THEN14059
14058 SOUND50,2:PRINT@448,"NO ENEMY CONTACT!":X=3:GOSUB13100:GOTO14027
14059 IFX<Y THENXX=X:X=Y:Y=XX
14060 GOSUB12600:PRINT@448,"                 ":SOUND240,1:PRINT@448,"RANGE ";
14061 PRINTRIGHT$(STR$(RX),1);:PRINT"/";:PRINTRIGHT$(STR$(RY),1);:PRINT" IS";INT(R1+.999999);"NM."
14065 X=3:GOSUB13100:GOTO14027
14070 SOUND100,2:GOTO14030
14500 FORY=1TO3:SOUND100,1:FORX=0TO8:CLSX:FORXX=1TO20:NEXT:SOUND150,1:NEXT:NEXT:CLS4:RETURN
14590 REM
14600 ONRND(3)GOTO14620,14640,14660
14620 PRINT"JAPANESE FLOAT PLANE SIGHTED":PRINT"  OVER ";T$(I);"!":RETURN
14640 PRINT"DESTROYER REPORTS UNIDENTIFED":PRINT"  AIRCRAFT SIGHTED NEAR ";T$(I);"!":RETURN
14660 IFC(I,7)=0THENGOTO14620
14670 PRINT"CAP REPORTS JAPANESE FLOAT":PRINT"  PLANE SHOT DOWN NEAR ";T$(I);"!":RETURN
14690 REM
14700 GOSUB26900:PRINTN$(S(I,9));" ";P$(J+K-1);
14710 ONRND(3)GOTO14720,14740,14760
14720 PRINT" MISS TARGET":PRINT"  DUE TO NAVIGATIONAL ERROR!":RETURN
14740 PRINT" TURN THE WRONG":PRINT"  WAY AND MISS THE TARGET!":RETURN
14760 PRINT" BECOME LOST":PRINT"  AND MISS THE TARGET!":RETURN
15000 IFPOS(0)>28 THENPRINT
15005 GOSUB26300:PRINT"hit ";:X=1:GOSUB13100:H=H+1:RETURN
15100 IFPOS(0)>23 THENPRINT
15110 GOSUB26200:PRINT"near miss ";:X=1:GOSUB13100:N=N+1:RETURN
25000 REM
25010 Y=8
25020 CLS0:FORV=10TO16
25025 GOSUB25460
25030 SET(5,V,Y):SET(13,V,Y)
25040 SET(17,V,Y):SET(21,V,Y)
25050 SET(31,V,Y):SET(39,V,Y)
25070 IFV<>10 THEN25120
25080 SET(22,V,Y):SET(23,V,Y)
25090 SET(24,V,Y):SET(25,V,Y)
25100 SET(46,V,Y)
25110 SET(52,V,Y):SET(58,V,Y)
25120 IFV<>11 THEN25170
25130 SET(6,V,Y):SET(12,V,Y)
25140 SET(26,V,Y):SET(45,V,Y)
25150 SET(47,V,Y):SET(53,V,Y)
25160 SET(57,V,Y)
25170 IFV<>12 THEN25220
25180 SET(7,V,Y):SET(11,V,Y)
25190 SET(27,V,Y):SET(44,V,Y)
25200 SET(48,V,Y):SET(54,V,Y)
25210 SET(56,V,Y)
25220 IFV<>13 THEN25270
25230 SET(8,V,Y):SET(10,V,Y)
25240 SET(27,V,Y):SET(35,V,Y)
25250 SET(43,V,Y):SET(49,V,Y)
25260 SET(55,V,Y)
25270 IFV<>14 THEN25320
25280 SET(9,V,Y):SET(34,V,Y)
25290 SET(27,V,Y):SET(36,V,Y)
25300 SET(55,V,Y)
25310 FORH=43TO49:SET(H,V,Y):NEXTH
25320 IFV<>15 THEN25360
25330 SET(26,V,Y):SET(33,V,Y)
25340 SET(37,V,Y):SET(43,V,Y)
25350 SET(49,V,Y):SET(55,V,Y)
25360 IF V<>16 THEN 25410
25370 FORH=22TO25:SET(H,V,Y):NEXTH
25380 SET(32,V,Y):SET(38,V,Y)
25390 SET(43,V,Y):SET(49,V,Y)
25400 SET(55,V,Y)
25410 NEXT V
25420 PRINT @326,"TURNING POINT IN THE";
25430 PRINT @359,"WAR IN THE PACIFIC";
25440 GOTO25495
25460 SOUND200,1:RETURN
25495 FORX=1TO200:NEXT
25500 SOUND1,1
25515 X=2:GOSUB13100:RETURN
25520 RETURN
26000 SOUND1,1:RETURN
26010 SOUND2,1:RETURN
26020 SOUND3,1:RETURN
26030 SOUND4,1:RETURN
26040 SOUND5,1:RETURN
26050 SOUND6,1:RETURN
26100 SOUND7,1:RETURN
26200 SOUND8,1:RETURN
26220 SOUND9,1:RETURN
26250 SOUND10,1:RETURN
26300 SOUND11,1:RETURN
26400 SOUND12,1:RETURN
26500 SOUND13,1:X=.1:GOSUB13100:RETURN
26600 SOUND14,1:RETURN
26610 SOUND15,1:RETURN
26620 SOUND16,1:RETURN
26650 SOUND17,1:RETURN
26700 FORZZ=1TO6:GOSUB26200:PRINT".";:X=1:GOSUB13100:NEXT:PRINT:RETURN
26750 SOUND18,1:RETURN
26800 SOUND19,1:RETURN
26850 SOUND20,1:RETURN
26900 SOUND21,1:RETURN
27000 SOUND22,1
27005 FORX=225TO3STEP-3:SOUNDX,1:NEXT:SOUND3,10:RETURN
27100 SOUND23,1:RETURN
27150 SOUND24,1:RETURN
27200 SOUND25,1:RETURN
27300 FORX=1TO4:SOUND26,1:NEXT:SOUND200,10:RETURN
27400 SOUND26,1:RETURN
