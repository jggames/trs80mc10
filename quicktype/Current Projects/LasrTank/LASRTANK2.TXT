50 CLS:GOSUB6000:CLEAR4000
60 DIMC$(128)
61 FORI=0TO127:PRINT@492,"WAIT"INT((127-I)/2);:B$=CHR$(I+128):FORC2=1TO13:C$(I)=C$(I)+B$:NEXT
62 NEXT:PRINT@490,"hit any key";:FORI=0TO1STEP0:I=-(INKEY$<>""):NEXT
70 CLS0:GOSUB5090:IFH=5THENGOSUB5070
80 H=0:M=0:W$="12345":A$="":B$="":FORI=1TO91:A$=A$+"€":B$=B$+" ":NEXT:NG=1:W=0:T=0
90 J=46:S=5:Z=0:Z1=5:Z2=-1
110 F=80:E=80:D=0:G=32
120 C=16:B=96:Y=64
190 FORI=1TOLEN(W$)
200 R=RND(61)+15:IFMID$(A$,R,1)<>"€"THEN200
220 A$=LEFT$(A$,R-1)+MID$(W$,I,1)+MID$(A$,R+1,91):B$=LEFT$(B$,R-1)+MID$(W$,I,1)+MID$(B$,R+1,91)
230 NEXT
240 PRINT@277,"PRESS G";
250 PRINT@1,LEFT$(C$(B+3),13);
260 PRINT@161,LEFT$(C$(B+12),13);
270 FORI=1TO4
280 PRINT@32*I+1,LEFT$(C$(B+15),1);
290 PRINT@32*I+7,LEFT$(C$(B+15),1);
300 PRINT@32*I+13,LEFT$(C$(B+15),1);
310 NEXTI
320 PRINT@321,LEFT$(C$(C+3),30);
330 PRINT@481,LEFT$(C$(C+12),30);
340 FORI=11TO14
350 PRINT@32*I+1,LEFT$(C$(C+15),1);
360 PRINT@32*I+30,LEFT$(C$(C+15),1);
370 PRINT@32*I+7,LEFT$(C$(C+15),1);
380 PRINT@32*I+13,LEFT$(C$(C+15),1);
390 PRINT@32*I+19,LEFT$(C$(C+15),1);
400 PRINT@32*I+20,LEFT$(C$(C+15),10);
410 NEXTI
420 GOSUB2000
430 IFINKEY$<>"G"THEN430
440 PRINT@18,LEFT$(C$(F+3),13);
450 PRINT@50,LEFT$(C$(F+15),13);
460 PRINT@82,LEFT$(C$(F+15),13);
470 PRINT@114,LEFT$(C$(F+12),13);
480 PRINT@178,LEFT$(C$(E+3),13);
490 PRINT@210,LEFT$(C$(E+15),6);
500 PRINT@216,LEFT$(C$(G+15),1);
510 PRINT@217,LEFT$(C$(E+15),6);
520 PRINT@242,LEFT$(C$(E+15),13);
530 PRINT@274,LEFT$(C$(E+15),13);
540 PRINT@225,LEFT$(C$(Y+12),13);
550 PRINT@257,LEFT$(C$(Y+3),13);
560 J$=INKEY$
570 IFJ$="A"THENJ=J-S
580 IFJ$="D"THENJ=J+1
590 IFVAL(J$+" ")>0THENS=VAL(J$)
680 IFJ$="W"THENGOSUB1000
690 J=J+RND(3)-2
700 IFJ<1THENJ=1
710 IFJ>91THENJ=91
720 GOSUB2000
730 PRINT@146,MID$(A$,J,13);
740 IFH=LEN(W$)THENGOSUB5000:ON-(INKEY$<>"G")GOTO740:NG=0
750 Z=Z+1
760 IFZ=INT(Z/Z1)*Z1THENZ=0:GOSUB3000
770 IFNGTHEN560
780 GOTO70
1000 REM FIRE
1010 J8=J+6
1020 M=M+1
1030 FORI8=1TO5
1040 PRINT@216,"€";
1050 FORK8=1TO20:NEXTK8
1060 PRINT@216,LEFT$(C$(G+31),1);
1070 NEXTI8
1080 IFMID$(A$,J8,1)<>"€"THENA$=LEFT$(A$,J8-1)+"€"+MID$(A$,J8+1):B$=LEFT$(B$,J8-1)+" "+MID$(B$,J8+1):H=H+1:T=0:Z1=Z1+1
1090 GOSUB2000
1100 PRINT@216,LEFT$(C$(G+15),1);
1110 RETURN
2000 REM UPDATE PANEL
2010 PRINT@67,"TAR";
2020 IFTTHEN2050
2030 IFRND(2)=1THENW=VAL(LEFT$(B$,45)):IFWTHENT=1:GOTO2050
2040 W=VAL(MID$(B$,46)):IFWTHENT=2:GOTO2050
2045 IFH<5THEN2030
2050 IFT=1THENPRINT@99,"LFT";:GOTO2070
2060 IFT=2THENPRINT@99,"RGT";:GOTO2070
2065 PRINT@99,"   ";
2070 PRINT@73,"ROT";
2080 PRINT@105,S;
2090 PRINT@387,"VEC";
2100 PRINT@419,RIGHT$("    "+STR$(J-46),3);
2110 PRINT@393,"KIL";
2120 PRINT@425,RIGHT$(STR$(1000+H),3);
2130 PRINT@399,"MIS";
2140 PRINT@431,RIGHT$(STR$(1000+M),3);
2150 RETURN
3000 REM ENEMY RETURNING FIRE
3010 Z2=Z2+1
3020 R8=32*(11+INT(Z2/10))+20+Z2-INT(Z2/10)*10
3030 PRINT@R8,"€";
3040 IFZ2=39THENGOSUB4000:ON-(INKEY$<>"G")GOTO3040:NG=0
3050 RETURN
4000 REM ENEMY WINS
4010 FORI8=11TO14
4020 PRINT@32*I8+20,"€€€€€€€€€€";
4030 NEXTI8
4040 PRINT@409,"GAME";
4050 PRINT@441,"OVER";
4060 RETURN
5000 REM USER WINS
5010 FORI8=11TO14
5020 PRINT@32*I8+20,"€€€€€€€€€€";
5030 NEXTI8
5040 PRINT@409,"USER";
5050 PRINT@441,"WINS";
5060 RETURN
5070 SC=INT((H/M)*1000):IFSC>HSTHENHS=SC
5080 PRINT@225,"            ";:PRINT@225,"SCORE"SC;:RETURN
5090 PRINT@257,"            ";:PRINT@257,"HIGH "HS;:SC=0:RETURN
6000 CLS:PRINT"YOU ARE THE GUNNER OF A LASER"
6010 PRINT"TANK. USE a AND d TO ROTATE THE"
6020 PRINT"TURRET LEFT & RIGHT. 5 ENEMIES"
6030 PRINT"ARE ARRAYED RANDOMLY -45 TO 45"
6040 PRINT"DEGREES IN FRONT. VEC=DIRECTION"
6050 PRINT"AND ROT=SPEED OF TURRET. PRESS"
6060 PRINT"g TO START GAME. PRESS w TO FIRE";
6070 PRINT"1-9 CHANGES TURRET SPEED. TOP"
6080 PRINT"RIGHT IS THE SLIT AND BELOW IT"
6090 PRINT"THE ARMOR BEING WORN AWAY BY IN-";
6100 PRINT"COMING FIRE. THIS DECREASES AS"
6110 PRINT"AS ENEMIES ARE DESTROYED. YOUR"
6120 PRINT"COMMANDER GIVES ROUGH DIRECTIONS";
6130 PRINT"(LFT/RGT) IN THE UPPER LEFT."
6140 PRINT"KIL=KILLS AND MIS=MISSES.":RETURN
7000 REM PROGRAM: CHAPT9 LASRTANK
7010 REM AUTHORS: JOHN P GRILLO
7020 REM          JD ROBERTSON
7030 REM MC-10 EDITS JIM GERRIE
