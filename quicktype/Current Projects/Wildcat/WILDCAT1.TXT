10 REM LAST UPDATE - 7/17/82:AM:A.VANCHURA
20 REM ON F10 GOSUB 2670
40 XX=1:YY=1:REM 
50 REM KEY OFF:LOCATE 1,1,0
60 CLS:DEFSNG A-Y:DEFSTR Z:DIM Z(10),HIT(3,40),PAY(20,5,3),MAP(100,3),WELL(10)
70 REM RANDOMIZE(VAL(RIGHT$(TIME$,2)))
80 REM RANDOMIZE(RND*30000)
90 ZUM="$$########,.##"
100 CSH=1000000!
110 GOSUB 2350:GOSUB 2020
120 CHS=0
130 GOSUB 1770
140 GOSUB 2710
150 LOCATE 24,28:COLOR 14,0:PRINT"PLEASE PICK A DRILL SITE.";:COLOR 3,0
160 Z=INKEY$:IF (Z<"A" OR Z>"J") AND (Z<"A" OR Z>"J") THEN 160
170 IF Z>"J" THEN Z=CHR$(ASC(Z)-32):A=ASC(Z)-65 ELSE A=ASC(Z)-65
180 PRINT"  "Z;
190 Z=INKEY$:IF Z="" THEN 190
200 IF Z<"0" OR Z>"9" THEN LOCATE 24,28:PRINT SPC(50):GOTO 150
210 B=VAL(Z):PRINT Z;
220 C=A*10+B
230 IF MAP(C,0)=2 OR MAP(C,0)=1 THEN LOCATE 24,1:PRINT SPC(79);:GOTO 150
240 GOSUB 1590
250 IF YES THEN GOSUB 280
260 IF CHS<10 THEN 130
270 GOTO 2750
280 CLS:LOCATE 21,1:PRINT STRING$(80,178)
290 COLOR 12,0
300 LOCATE 9,40:PRINT      "บ"
310 LOCATE 10,35:PRINT""
320 LOCATE 11,35:PRINT"   บ   "
330 LOCATE 12,35:PRINT"  ฿บ฿  "
340 LOCATE 13,35:PRINT" ฿บ฿ "
350 LOCATE 14,35:PRINT" ฿ บ ฿ "
360 LOCATE 15,35:PRINT"  บ  "
370 LOCATE 16,35:PRINT" ฿บ฿ "
380 LOCATE 17,35:PRINT"      "
390 LOCATE 18,34:PRINT"฿ ฿บ฿ ฿"
400 LOCATE 19,34:PRINT"฿  บ  ฿"
410 LOCATE 20,34:PRINT"฿    บ    ฿"
420 LOCATE 21,40:PRINT     "บ"
430 COLOR 2,0
440 LOCATE 1,19:PRINT"POTENTIAL PAY ZONE   :";
450 COLOR 15,0:PRINT SZN"TO"EZN"FT.":COLOR 2,0
460 LOCATE 2,25:PRINT"COST TO DRILL PER FT : ";
470 COLOR 15,0:PRINT USING "$$#.##";30:COLOR 2,0
480 MAP(C,0)=1:CSF=SZN*30:DT=SZN+500
490 IF CSH-CSF<0 THEN OOM=1:YRN(CHS+1)=-(CSH):GOTO 2750
500 D=1:CHS=CHS+1
510 IF CSH-CSF<0 THEN OOM=1:YRN(CHS)=-(CSH):GOTO 2750
520 COLOR 2,0:LOCATE 3,25:PRINT"COST SO FAR ";
530 COLOR 15,0:PRINT USING ZUM;CSF:COLOR 2,0
540 LOCATE 4,25:PRINT"CURRENT DEPTH ";
550 COLOR 15,0:PRINT DT:COLOR 3,0
560 D=D+1
570 TRY=FIX(RND*40)+1
580 PAYOFF=HIT(TYPE,TRY)
590 IF PAYOFF>1 THEN 930
600 LOCATE 23,1:PRINT SPC(79);
610 LOCATE 24,1:PRINT SPC(79);
620 LOCATE 23,32:PRINT"NO SHOW AT";:PRINT USING " ##,### ";DT;:PRINT"FEET.";
630 IF D<3 THEN 660
640 LOCATE 24,28:PRINT"YOU MUST TRY A NEW WELL SIGHT";
650 FOR X=1 TO 4000:NEXT:GOTO 680
660 COLOR 13,0:LOCATE 24,27:PRINT"DO YOU WISH TO GO DEEPER? <Y/N>";:COLOR 3,0
670 GOSUB 1720:IF YES THEN 700
680 YRN(CHS)=-CSF
690 OPD=0:GSP=0:FRC=0:OPN=0:GOTO 1180
700 LOCATE 23,1:PRINT SPC(79);
710 LOCATE 24,1:PRINT SPC(79);
720 COLOR 15,0
730 LOCATE 24,27:PRINT"STRIKE ENTER KEY WHEN READY";
740 LOCATE 23,27:PRINT"ENTER NEW TEST DEPTH : ";
750 COLOR 3,0
760 Z1=""
770 Z=INKEY$:IF Z="" THEN 770
780 IF Z=CHR$(13) THEN 840
790 IF RIGHT$(Z,1)=CHR$(75) OR Z=CHR$(8) THEN 820
800 IF LEN(Z1)>6 THEN 770
810 PRINT Z;:Z1=Z1+Z:GOTO 770
820 IF LEN(Z1)<1 THEN 770
830 PRINT CHR$(29)CHR$(32)CHR$(29);:Z1=LEFT$(Z1,LEN(Z1)-1):GOTO 770
840 LOCATE 23,1,0:PRINT SPC(79);:COLOR 3,0:DPT=VAL(Z1)
850 IF DPT>=DT THEN 880
860 LOCATE 23,31:PRINT"YOU MUST GO DEEPER ";:FOR X=1 TO 4000:NEXT
870 LOCATE 23,1:PRINT SPC(79);:GOTO 740
880 IF DPT<=EZN THEN 920
890 LOCATE 24,1:PRINT SPC(79);
900 LOCATE 23,30:PRINT"YOU ARE PAST THE PAY ZONE";
910 FOR CC=1 TO 4000:NEXT:GOTO 700
920 CSF=CSF+30*(DPT-DT):DT=DPT:GOTO 510
930 LOCATE 23,1:PRINT SPC(79);
940 LOCATE 24,1:PRINT SPC(79);
950 LOCATE 23,27:PRINT"OIL AND GAS SHOW AT";:PRINT USING " ##### ";DT;:PRINT"FEET";
960 FRC=10*DT
970 LOCATE 24,27:PRINT"FRACTURE COST IS";:PRINT USING ZUM;FRC;
980 LOCATE 25,1:PRINT SPC(79);
990 LOCATE 25,27:PRINT"DO YOU WANT TO FRACTURE? <Y/N>";
1000 GOSUB 1720:IF NO THEN 680
1010 HIT=FIX(FIX(RND*10)*2)+1
1020 OPD=PAY(HIT,PAYOFF,TYPE)
1030 GSP=PAY(HIT+1,PAYOFF,TYPE)*1000
1040 LOCATE 23,1:PRINT SPC(79);
1050 LOCATE 24,1:PRINT SPC(79);
1060 LOCATE 25,1:PRINT SPC(79);
1070 LOCATE 23,20:PRINT"    !!  EUREKA,  WE  STRUCK  OIL  !!"
1080 GOTO 1090
1090 LOCATE 24,19:PRINT"WELL WILL PRODUCE";
1100 COLOR 15,0:PRINT USING " ### ";OPD;:COLOR 3,0
1110 PRINT"BARRELS OF OIL PER DAY";
1120 LOCATE 25,19:PRINT"";
1130 COLOR 15,0:PRINT USING " #,###,### ";GSP;:COLOR 3,0
1140 PRINT"CUBIC FEET OF NATURAL GAS PER DAY";
1150 OPN=(FIX(RND*75)+150)*12
1160 LOCATE 22,27:COLOR 14,0:PRINT"STRIKE ANY KEY TO CONTINUE":COLOR 3,0
1170 Z=INKEY$:IF Z="" THEN 1170
1180 COLOR 3,0:CLS
1190 LOCATE 1,20:PRINT STRING$(40,"ฑ")
1200 FOR A=2 TO 22:LOCATE A,20:PRINT"ฑ":LOCATE A,59:PRINT"ฑ":NEXT
1210 LOCATE 23,20:PRINT STRING$(40,"ฑ");
1220 LOCATE 2,26:COLOR 3,0:PRINT"***** INCOME STATEMENT *****"
1230 LOCATE 3,21:PRINT STRING$(38,"อ")
1240 LOCATE 4,22:COLOR 15,0:PRINT"WELL #"CHS
1250 LOCATE 5,38:COLOR 1:PRINT"COSTS"
1260 LOCATE 6,25:COLOR 3,0:PRINT"DRILLING"
1270 LOCATE 7,25:PRINT"FRACTURE"
1280 COLOR 1:LOCATE 8,25:PRINT"1 YR. OPER.             "
1290 TOTALCOST=OPN+FRC+CSF
1300 COLOR 3,0:LOCATE 6,40:PRINT USING ZUM;CSF
1310 LOCATE 7,40:PRINT USING ZUM;FRC
1320 COLOR 1:LOCATE 8,40:PRINT USING ZUM;OPN
1330 LOCATE 9,25:COLOR 15,0:PRINT"TOTAL COST"
1340 COLOR 15,0:LOCATE 9,40:PRINT USING ZUM;TOTALCOST
1350 COLOR 1:LOCATE 11,34:PRINT"GROSS INCOME"
1360 COLOR 3,0:LOCATE 12,25:PRINT"OIL"
1370 COLOR 1:LOCATE 13,25:PRINT"GAS                  "
1380 ODS=OPD*9000:GDS=GSP*2.1
1390 GRDS=ODS+GDS
1400 RVS=(ODS+GDS)*5:WELL(CHS)=RVS
1410 LOCATE 13,40:PRINT USING ZUM;GDS
1420 COLOR 3,0:LOCATE 12,40:PRINT USING ZUM;ODS
1430 COLOR 15,0:LOCATE 14,25:PRINT"TOTAL INCOME"
1440 LOCATE 14,40:PRINT USING ZUM;GRDS
1450 COLOR 1:LOCATE 15,25:PRINT"COST          -"
1460 NTP=GRDS-TOTALCOST
1470 LOCATE 15,40:PRINT USING ZUM;TOTALCOST
1480 COLOR 15,0:LOCATE 16,25:PRINT"NET PROFIT"
1490 LOCATE 16,40:PRINT USING ZUM;NTP:COLOR 3,0
1500 LOCATE 18,25:PRINT"ESTIMATED RESERVES IN GROUND"
1510 CSH=CSH+NTP
1520 YRN(CHS)=NTP
1530 COLOR 15,0:LOCATE 19,40:PRINT USING ZUM;RVS:COLOR 3,0
1540 ADD=0:FOR A=1 TO CHS:ADD=ADD+WELL(A):NEXT
1550 LOCATE 21,25:PRINT"TOTAL RESERVES SO FAR "
1560 COLOR 15,0:LOCATE 22,40:PRINT USING ZUM;ADD:COLOR 3,0
1570 COLOR 14,0:LOCATE 24,27:PRINT"STRIKE ANY KEY TO CONTINUE";
1580 COLOR 3,0:Z=INKEY$:IF Z="" THEN 1580 ELSE RETURN
1590 CLS:LOCATE 1,28:COLOR 13,0
1600 PRINT       "ษอออออออออออออออออออออออป"
1610 LOCATE 2,28:PRINT"บ    GEOLOGY REPORT     บ"
1620 LOCATE 3,28:PRINT"ศอออออออออออออออออออออออผ"
1630 LOCATE 4,20:PRINT"ษอออออออออออออออออออออออออออออออออออออออป"
1640 FOR X=5 TO 11:LOCATE X,20:PRINT"บ":LOCATE X,60:PRINT"บ"
1650 NEXT
1660 LOCATE 12,20:PRINT"ศอออออออออออออออออออออออออออออออออออออออผ":COLOR 3,0
1670 SZN=MAP(C,1):EZN=MAP(C,2):TYPE=MAP(C,3)
1680 LOCATE 6,30:PRINT"POTENTIAL PAY ZONE :"
1690 LOCATE 7,29:COLOR 15,0:PRINT SZN"TO"EZN"FT.":COLOR 3,0
1700 LOCATE 9,26:PRINT"TARGET ZONE STARTS AT"SZN+500"FT."
1710 LOCATE 14,27:COLOR 15,0:PRINT"DO YOU WISH TO DRILL? <Y/N>";:COLOR 3,0
1720 Z=INKEY$:IF Z="" THEN 1720
1730 IF Z="Y" OR Z="Y" THEN YES=1:NO=0:RETURN
1740 IF Z="N" OR Z="N" THEN NO=1:YES=0:RETURN
1750 GOTO 1720
1760 FOR X=1 TO 2000:NEXT X:RETURN
1770 COLOR 15,0:CLS
1780 LOCATE 1,26:PRINT"B O O M   C O U N T Y   U S A
1790 B1=179:B2=195:B3=197:B4=180:B5=192:B6=193:B7=217:B8=218:B9=194:B0=191
1800 DEF SEG=0
1810 IF (PEEK(1040) AND 48)=48 THEN DEF SEG=45056! ELSE DEF SEG=47104!
1820 A=178:LOCATE A\160+1,10:PRINT STRING$(60,196)
1830 POKE A,B8:POKE A+12,B9:POKE A+24,B9:POKE A+36,B9:POKE A+48,B9:POKE A+60,B9:     POKE A+72,B9:POKE A+84,B9:POKE A+96,B9:POKE A+108,B9:POKE A+120,B0
1840 FOR A=338 TO 3058 STEP 160
1850 POKE A,B1:POKE A+12,B1:POKE A+24,B1:POKE A+36,B1:POKE A+48,B1:POKE A+60,B1:     POKE A+72,B1:POKE A+84,B1:POKE A+96,B1:POKE A+108,B1:POKE A+120,B1
1860 A=A+160
1870 LOCATE A\160+1,10:PRINT STRING$(60,196)
1880 POKE A,B2:POKE A+12,B3:POKE A+24,B3:POKE A+36,B3:POKE A+48,B3:POKE A+60,B3:     POKE A+72,B3:POKE A+84,B3:POKE A+96,B3:POKE A+108,B3:POKE A+120,B4
1890 NEXT
1900 POKE A,B1:POKE A+12,B1:POKE A+24,B1:POKE A+36,B1:POKE A+48,B1:POKE A+60,B1:     POKE A+72,B1:POKE A+84,B1:POKE A+96,B1:POKE A+108,B1:POKE A+120,B1
1910 A=A+160
1920 LOCATE A\160+1,10:PRINT STRING$(60,196)
1930 POKE A,B5:POKE A+12,B6:POKE A+24,B6:POKE A+36,B6:POKE A+48,B6:POKE A+60,B6:     POKE A+72,B6:POKE A+84,B6:POKE A+96,B6:POKE A+108,B6:POKE A+120,B7
1940 COLOR 3,0:D=0
1950 FOR A=3 TO 21 STEP 2
1960     C=0:FOR B=13 TO 67 STEP 6
1970         IF MAP(D,0)=0 THEN LOCATE A,B:PRINT Z(A/2-1)  RIGHT$(STR$(C),1)
1980     D=D+1:C=C+1:NEXT
1990 NEXT
2000 LOCATE 23,28:PRINT"CASH ASSETS";:PRINT USING ZUM;CSH
2010 RETURN
2020 LOCATE 12,30:PRINT"ONE MOMENT PLEASE";
2030 FOR C=0 TO 100:IF RND<.6 THEN MAP(C,0)=2:GOTO 2070
2040 IF RND<.4 THEN MAP(C,1)=4000:MAP(C,2)=7000:MAP(C,3)=1:GOTO 2070
2050 IF RND<.6 THEN MAP(C,1)=7500:MAP(C,2)=10000:MAP(C,3)=2:GOTO 2070
2060 MAP(C,1)=10500:MAP(C,2)=15000:MAP(C,3)=3:GOTO 2070
2070 NEXT
2080 FOR A=1 TO 10:READ Z(A):NEXT
2090 FOR B=1 TO 3:FOR A=1 TO 40:READ HIT(B,A):NEXT A,B
2100 FOR C=1 TO 3
2110  FOR B=1 TO 5
2120   FOR A=1 TO 20
2130    READ PAY(A,B,C)
2140 NEXT A,B,C
2150 DATA A,B,C,D,E,F,G,H,I,J
2160 DATA 1,1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,3,3,3,3,3,3,3,3,3,3,4,4,4,4,4,4,4,4,5,5,5,5,5,5
2170 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,3,3,3,3,3,3,4,4,4,4,5,5,5,5
2180 DATA 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,2,5,5
2190 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2200 DATA 54,0,13,240,0,370,112,0,41,600,0,514,70,112,95,0,0,301,62,98
2210 DATA 37,0,12,128,0,131,19,50,0,167,6,114,42,0,36,20,0,185,39,11
2220 DATA 0,75,15,32,21,0,7,39,0,78,3,67,17,0,0,59,0,133,22,11
2230 DATA 0,22,8,11,2,0,6,13,0,61,3,36,8,2,0,60,4,0,0,24
2240 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2250 DATA 0,461,123,0,78,502,124,120,270,0,0,960,41,581,108,333,0,1333,121,380
2260 DATA 0,197,51,0,22,142,37,65,51,0,17,131,0,233,41,69,19,158,65,0
2270 DATA 0,141,13,88,30,0,6,106,15,53,28,0,21,50,0,137,25,83,49,0
2280 DATA 7,0,4,34,0,47,9,25,17,0,11,30,2,6,11,0,3,0,4,13
2290 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2300 DATA 276,620,29,1500,282,0,0,4400,241,1200,35,1400,860,0,80,997,240,0,105,           2200
2310 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2320 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2330 DATA 0,200,40,0,23,90,41,33,0,187,32,104,0,202,43,0,0,281,27,104
2340 RETURN
2350 CLS
2360 LOCATE 1,1:PRINT STRING$(80,219)
2370 FOR A=2 TO 22
2380  LOCATE A,1:PRINT"":LOCATE A,80:PRINT""
2390 NEXT
2400 LOCATE 23,1:PRINT STRING$(80,219);
2410 LOCATE 3,24:COLOR 15,0:PRINT"* * * W I L D C A T T E R * * *
2420 LOCATE 12,23:PRINT"WOULD YOU LIKE INSTRUCTIONS? <Y/N>":COLOR 3,0           
2430 GOSUB 1720:IF NO THEN CLS:RETURN
2440 LOCATE 4,10:PRINT"
2450 LOCATE 5,15:PRINT"IN THIS GAME,  YOU OWN AN  INDEPENDENT  OIL AND GAS
2460 LOCATE 6,15:PRINT"DRILLING COMPANY.  FIRST YOU MUST SELECT A DRILLING
2470 LOCATE 7,15:PRINT"SIGHT FROM THE MAP OF BOOM COUNTY.  AFTER YOU ENTER
2480 LOCATE 8,15:PRINT"YOUR SIGHT,  YOU  WILL  BE  SHOWN A  GEOLOGY REPORT
2490 LOCATE 9,15:PRINT"INDICATING THE  POTENTIAL PAY ZONE.  THE DEEPER YOU
2500 LOCATE 10,15:PRINT"DRILL A WELL,  THE LESS  CHANCE YOU HAVE OF FINDING
2510 LOCATE 11,15:PRINT"OIL OR GAS,  BUT THE CHANCE OF FINDING HUGE STRIKES
2520 LOCATE 12,15:PRINT"INCREASES. SHALLOW WELLS ARE MORE LIKELY TO PRODUCE.
2530 LOCATE 13,10:PRINT"
2540 LOCATE 14,15:PRINT"IF AFTER DRILLING TO THE  POTENTIAL PAY ZONE AND NO
2550 LOCATE 15,15:PRINT"OIL OR GAS IS INDICATED, YOU MAY CHOOSE TO DRILL TO
2560 LOCATE 16,15:PRINT"ONE  OTHER DEPTH AT THAT SIGHT.  IF YOU DO SHOW OIL
2570 LOCATE 17,15:PRINT"OR GAS, YOU MUST  FRACTURE A WELL BEFORE PRODUCTION.
2580 LOCATE 18,10:PRINT"
2590 LOCATE 18,15:PRINT"WE HAVE LOANED YOU  $1,000,000 TO BEGIN EXPLORATION.
2600 LOCATE 19,15:PRINT"THAT IS YOUR CREDIT LIMIT.  WHEN YOU HAVE COMPLETED
2610 LOCATE 20,15:PRINT"TEN WELLS OR RUN OUT OF OPERATING CAPITAL, YOU WILL
2620 LOCATE 21,15:PRINT"BE GIVEN A FINAL STATEMENT OF OPERATIONS. GOOD LUCK
2630 COLOR 15,0
2640 LOCATE 25,28:PRINT"STRIKE ANY KEY TO CONTINUE";
2650 COLOR 3,0
2660 Z=INKEY$:IF Z="" THEN 2660 ELSE CLS:RETURN
2670 KEY(10) OFF:XX=CSRLIN:YY=POS(0)
2680 LOCATE 25,1:PRINT SPC(79);:COLOR 15,0
2690 LOCATE 25,22:PRINT"DO YOU WISH TO LEAVE THIS GAME? <Y/N>";
2700 GOSUB 1720:IF YES THEN 2740
2710 LOCATE 25,1:PRINT SPC(79);:COLOR 0,7:LOCATE 25,24
2720 PRINT" STRIKE <F10> TO LEAVE THIS GAME ";:COLOR 3,0
2730 LOCATE XX,YY:KEY(10) ON:RETURN
2740 RUN"MENU
2750 CLS
2760 LOCATE 1,15:PRINT STRING$(50,"ฑ")
2770 FOR A=2 TO 22:LOCATE A,15:PRINT"ฑ":LOCATE A,64:PRINT"ฑ":NEXT
2780 LOCATE 23,15:PRINT STRING$(50,"ฑ");
2790 LOCATE 2,26:COLOR 3,0:PRINT"***** FINAL  STATEMENT *****"
2800 LOCATE 3,16:PRINT STRING$(48,"อ")
2810 LOCATE 4,17:PRINT"WELL #     1ST YEAR  EARNINGS         RESERVES"
2820 FOR A=1 TO 10:LOCATE ,19:PRINT USING "##";A;:PRINT SPC(10);
2830 TOTALRVS=TOTALRVS+WELL(A)
2840 PRINT USING ZUM;YRN(A);
2850 PRINT USING "    "+ZUM;WELL(A)
2860 NEXT
2870 COLOR 15,0
2880 IF OOM THEN LOCATE ,31:PRINT USING "$$#,###,###.##";-1000000!;:GOTO 2900
2890 LOCATE ,31:PRINT USING "$$#,###,###.##";CSH-1000000!;
2900 LOCATE ,POS(0)+1:PRINT USING "$$,###,###,###.##";TOTALRVS
2910 IF OOM THEN 2920 ELSE 2950
2920 LOCATE 18,23:PRINT"YOU RAN OUT OF MONEY AT "INT(CSH/30)"FEET.
2930 LOCATE 19,23:PRINT"YOU HAVE 30 DAYS TO REPAY YOUR LOAN
2940 LOCATE 20,23:PRINT"PERSONAL CHECKS ARE NOT ACCEPTED !!
2950 LOCATE 22,23:COLOR 15,0:PRINT"WOULD YOU LIKE TO PLAY AGAIN? <Y/N>";
2960 COLOR 3,0:GOSUB 1720:IF NO THEN 2740 ELSE RUN
