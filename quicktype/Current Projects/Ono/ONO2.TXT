0 CLEAR500:DIMC1,C2,C3,M$,I$,M:M=16384:GOTO40

5 I$="":C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):I$=I$+CHR$(PEEK(C1+C2))
7 C3=ASC(MID$(M$,C2)):POKEC1+C2,C3-(C3AND64):NEXT:RETURN
8 PRINT@C1-M+1,I$;:RETURN

30 CLSRND(8):PRINT@32,;:INPUTM$:PRINT@0,;:GOSUB5:FORZZ=1TO4000:NEXT:GOSUB8:PRINT:FORZZ=1TO4000:NEXT:GOTO30

40 DIMW$(26),BP(40,1),D(108,1),C(14,3),PD(40,1),CD(40,1)
50 CLSRND(8)
80 PRINT@32*5+6,"   ono (C) 1/2/87   ";:PRINT@32*6+6,"      VER 3.1       ";:PRINT@32*7+6,"  CHARLES BARTLETT  ";
81 PRINT@32*8+6,LEFT$("YOUR SCORE"+STR$(XP)+"        ",20);
82 PRINT@32*9+6,LEFT$("COCO SCORE"+STR$(XC)+"        ",20);
90 B=1:FORCY=0TO12STEP4:FORCX=0TO27STEP3:BP(B,0)=CX:BP(B,1)=CY:B=B+1:NEXTCX,CY
100 SN=0:EN=0:VN=1:GOSUB480:SN=1:EN=12:VN=2:GOSUB480:SN=13:EN=14:VN=1:GOSUB480
110 GOSUB490:CLS0:REM SHUFFLE
120 FORD=1TO108:IFD(D,0)=11THEND(D,0)=10
121 NEXT
130 Z=1:FORQ=1TO13STEP2:PD(Z,0)=D(Q,0):PD(Z,1)=D(Q,1):CD(Z,0)=D(Q+1,0):CD(Z,1)=D(Q+1,1):Z=Z+1:NEXTQ:TN=7:PC=1:CT=7
140 TC=1:FORSP=1TOTN:NC=PD(TC,0):CC=PD(TC,1):GOSUB620:TC=TC+1:NEXT
150 SP=39:NC=15:GOSUB620:SP=40:NC=D(15,0):CC=D(15,1):DN=NC:DC=CC:GOSUB620:Q=16
160 IFDN=13THENIC=DC:GOSUB420
170 IFDN=14ORDN=12ORDN=10THENSP=40:GOSUB290:NC=D(Q,0):CC=D(Q,1):DN=NC:DC=CC:GOSUB620:Q=Q+1:GOTO160
180 IFTN=.ORCT=.ORTN>38ORCT>38THEN1360
181 GOSUB540
190 GOSUB1680
200 GOSUB350:IFKI<>13THEN200
201 C1=M1:I$=M1$:GOSUB8:SOUND100,1:SOUND100,1:IFLC=14THENDC=PD(PC,1):LC=0
210 IFPD(PC,0)>12THEN250
220 IFPC=39THENGOSUB1330:GOTO940
230 IFPD(PC,1)=DCTHEN235
231 GOTO240
235 GOSUB430:IFDN=10THEN180
236 IFDN=12THENGOSUB470:GOTO180
237 GOTO940
240 IFPD(PC,0)=DNTHEN245
241 GOTO250
245 GOSUB430:IFDN=10THEN180
246 IFDN=12THENGOSUB470:GOTO180
247 GOTO940
250 IFPD(PC,0)=13THEN255
251 GOTO260
255 GOSUB430:MS$="WHAT COLOR?":GOSUB520:M2=C1:M2$=I$:GOSUB390:IFTN=.THEN270
256 GOTO940
260 IFPD(PC,0)=14THEN265
261 GOTO270
265 GOSUB370:IFOKTHENGOSUB430:GOSUB470:GOSUB470:LC=14:GOTO180
266 GOTO280
270 IFTN=.ORCT=.ORTN>38ORCT>38THEN1360
280 MS$="ILLEGAL MOVE":GOSUB520:SOUND10,3:FORK=1TO500:NEXTK:GOSUB8:GOTO180

290 CX=BP(SP,0):CY=BP(SP,1):C2=32*CY+CX:C1=1:GOSUB834:RETURN

300 KI=PEEK(2)ANDPEEK(17023):IFKI=65THEN305
301 GOTO310
305 IFPC=39THENPC=TN:GOTO310
306 PC=PC-1:IFPC<1THENPC=1
310 IFKI=83THENPC=PC+1:IFPC>TNTHENPC=39
320 IFKI=87THEN325
321 GOTO330
325 IFPC=39THENPC=TN:GOTO330
326 PC=PC-10:IFPC<1THENPC=PC+10
330 IFKI=90THENPC=PC+10:IFPC>TNTHENPC=39
340 RETURN
350 GOSUB300:GOSUB360:RETURN
360 X=BP(PC,0):Y=BP(PC,1):C1=32*Y+X+M:C2=PEEK(C1):C3=207:IFC2>144THENIFC2<160THENC3=255
361 POKEC1,C3:FORW=1TO50:NEXT:POKEC1,C2:FORW=1TO25:NEXT:RETURN
370 OK=-1:FORT=1TOTN:IFPD(T,1)=DCTHENIFT<>PCTHENIFPD(T,0)<>13THENIFPD(T,0)<>14THENOK=.:RETURN
380 NEXT:RETURN
390 I$=INKEY$:IFI$=""THEN390
391 IFI$="G"THENI$="GREEN":IC=.:GOTO400
392 IFI$="Y"THENI$="YELLOW":IC=1:GOTO400
393 IFI$="B"THENI$="BLUE":IC=2:GOTO400
394 IFI$="R"THENI$="RED":IC=3:GOTO400
395 SOUND10,2:GOTO390
400 SOUND200,1
410 MS$="COLOR IS "+I$:C1=M2:I$=M2$:GOSUB8:GOSUB520:FORW=1TO500:NEXTW:GOSUB8
420 DC=IC:C2=411:C1=22:ONDC+1GOSUB842,834,830,818:RETURN
430 SP=40:GOSUB290:SP=PC:GOSUB290
440 DC=PD(PC,1):DN=PD(PC,0):NC=DN:CC=DC:SP=40:GOSUB620
441 IFPC<>TNTHENPD(PC,0)=PD(TN,0):PD(PC,1)=PD(TN,1):SP=PC:NC=PD(PC,0):CC=PD(PC,1):GOSUB620
450 SP=TN:GOSUB290:TN=TN-1:IFPC=TN+1THENPC=PC-1
460 RETURN
470 CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1:GOSUB1680:CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1:GOSUB1680:RETURN
480 FORZ=SNTOEN:FORSU=0TO3:C(Z,SU)=VN:NEXTSU,Z:RETURN
490 MS$="SHUFFLING - PLEASE WAIT...":GOSUB520:FORD=1TO108
500 CN=RND(15)-1:CC=RND(4)-1:IFC(CN,CC)=.THEN500
501 D(D,0)=CN:D(D,1)=CC:C(CN,CC)=C(CN,CC)-1
510 NEXTD:RETURN
520 PRINT@480,;:M$=MS$:GOSUB5:SOUND150,1:RETURN
530 MS$="INVALID INPUT":GOSUB520:SOUND10,1:FORW=1TO500:NEXT:GOSUB8:GOSUB540:RETURN
540 MS$="WHICH CARD?":GOSUB520::M1$=I$:M1=C1:PRINT@30,;:M$="ME":GOSUB5:PRINT@62,;:M$=MID$(STR$(CT)+" ",2,2):GOSUB5:RETURN
570 FORC1=1TOTN:IFPD(C1,1)=DCTHENOK=.:C1=TN
580 NEXT:RETURN
590 CC=15:GOSUB650:GOSUB600:DN=NC:DC=CC
600 CX=BP(PC,0):CY=BP(PC,1):NC=PD(PC,0):CC=PD(PC,1):RETURN
610 CX=BP(TN,0):CY=BP(TN,1):NC=PD(TN,0):CC=PD(TN,1):GOTO630

620 CX=BP(SP,0):CY=BP(SP,1)
630 IFNC>12THENCC=8
640 IFNC>9ANDNC<>15THENOS=4:GOTO650
641 OS=.
650 REM
660 IFNC=14THENREM
670 IFCC=1THENCD=8:GOTO680
671 CD=4
680 IFNC>9THEN720
690 C2=32*CY+CX:GOSUB800:ONCC+1GOSUB822,814,810,818:RETURN
720 REM SPECIAL CARDS
740 IFNC=14THENC2=32*CY+CX:C1=1:GOSUB830:RETURN
750 IFNC=13THENC2=32*CY+CX:C1=19:GOSUB842:RETURN
760 IFNC=12THENC2=32*CY+CX:C1=25:ONCC+1GOSUB842,834,830,818:RETURN
770 IFNC=10THENC2=32*CY+CX:C1=22:ONCC+1GOSUB842,834,830,818:RETURN
780 IFNC=15THENC2=32*CY+CX:C1=1:GOSUB838:RETURN
790 RETURN
800 IFNC=.THENC1=28:RETURN
801 C1=(NC-1)*3+1:RETURN

810 PRINT@C2,MID$("®­ª®¬ª®¬ª®®ª®¬ª®¬ª®¬ª®¬ª®¬ª®¬ª¯¯",C1,3);
811 PRINT@C2+32,MID$("¯¥ª®¨ª¯¨ªª¨ªª¬ªª¬ª¯ªªª¨ªª¨ªªªª¯¯",C1,3);
812 PRINT@C2+64,MID$("®¤ªª¬ª®¨ª¯ªª®¨ªª¨ª¯ªªª¨ª¯ªªª¨ª¯¯",C1,3);
813 PRINT@C2+96,MID$("¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬",C1,3);:RETURN
814 PRINT@C2,MID$("žšžœšžœšžžšžœšžœšžœšžœšžœšžœšŸŸ",C1,3);
815 PRINT@C2+32,MID$("Ÿ•šž˜šŸ˜šš˜ššœššœšŸššš˜šš˜ššššŸŸ",C1,3);
816 PRINT@C2+64,MID$("ž”ššœšž˜šŸššž˜šš˜šŸššš˜šŸššš˜šŸŸ",C1,3);
817 PRINT@C2+96,MID$("œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ",C1,3);:RETURN
818 PRINT@C2,MID$("¾½º¾¼º¾¼º¾¾º¾¼º¾¼º¾¼º¾¼º¾¼º¾¼º¿¿",C1,3);
819 PRINT@C2+32,MID$("¿µº¾¸º¿¸ºº¸ºº¼ºº¼º¿ººº¸ºº¸ºººº¿¿",C1,3);
820 PRINT@C2+64,MID$("¾´ºº¼º¾¸º¿ºº¾¸ºº¸º¿ººº¸º¿ººº¸º¿¿",C1,3);
821 PRINT@C2+96,MID$("¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼",C1,3);:RETURN
822 PRINT@C2,MID$("ŽŠŽŒŠŽŒŠŽŽŠŽŒŠŽŒŠŽŒŠŽŒŠŽŒŠŽŒŠ",C1,3);
823 PRINT@C2+32,MID$("…ŠŽˆŠˆŠŠˆŠŠŒŠŠŒŠŠŠŠˆŠŠˆŠŠŠŠ",C1,3);
824 PRINT@C2+64,MID$("Ž„ŠŠŒŠŽˆŠŠŠŽˆŠŠˆŠŠŠŠˆŠŠŠŠˆŠ",C1,3);
825 PRINT@C2+96,MID$("ŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆŒŒ",C1,3);:RETURN

830 PRINT@C2,MID$("¾­ª®¬ª®¬ª®®ª®¬ª®¬ª®¬ª¯¬ª¯­ª¯­ª¯¯¯",C1,3);
831 PRINT@C2+32,MID$("ºªª®¨ª¯¨ªª¨ªª¬ªª¬ª¯ªª«­ª«¡ªªªª¯¯¯",C1,3);
832 PRINT@C2+64,MID$("š‰Šª¬ª®¨ª¯ªª®¨ªª¨ª¯ªª®©ª®®ª«©ª¯¯¯",C1,3);
833 PRINT@C2+96,MID$("œŒˆ¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¨¬¬¬",C1,3);:RETURN
834 PRINT@C2,MID$("€€€žœšžœšžžšžœšžœšžœšŸœšŸšŸšŸŸŸ",C1,3);
835 PRINT@C2+32,MID$("€€€ž˜šŸ˜šš˜ššœššœšŸšš›š›‘ššššŸŸŸ",C1,3);
836 PRINT@C2+64,MID$("€€€šœšž˜šŸššž˜šš˜šŸššž™šžžš›™šŸŸŸ",C1,3);
837 PRINT@C2+96,MID$("€€€œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœ˜œœœ",C1,3);:RETURN
838 PRINT@C2,MID$("îîêîíêïíê¾¾º¾¼º¾¼º¾¼º¿¼º¿½º¿½º¿¿¿",C1,3);
839 PRINT@C2+32,MID$("êêêêêêêêêº¸ºº¼ºº¼º¿ºº»½º»±ºººº¿¿¿",C1,3);
840 PRINT@C2+64,MID$("êèêêêêëéê¿ºº¾¸ºº¸º¿ºº¾¹º¾¾º»¹º¿¿¿",C1,3);
841 PRINT@C2+96,MID$("ììèììèììè¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¸¼¼¼",C1,3);:RETURN
842 PRINT@C2,MID$("ŽŠŽŒŠŽŒŠŽŽŠŽŒŠ¿¯ª¿¥ªŒŠŠŠ",C1,3);
843 PRINT@C2+32,MID$("…ŠŽˆŠˆŠŠˆŠŠŒŠ¿¯ª¼¤¨‹Š‹ŠŠŠŠ",C1,3);
844 PRINT@C2+64,MID$("Ž„ŠŠŒŠŽˆŠŠŠŽˆŠŸŠŸ…ŠŽ‰ŠŽŽŠ‹‰Š",C1,3);
845 PRINT@C2+96,MID$("ŒŒˆŒŒˆŒŒˆŒŒˆŒŒˆœŒˆœ„ˆŒŒˆŒŒˆŒŒˆŒŒŒ",C1,3);:RETURN

920 END

940 IFCT=.ORTN=.ORCT>38ORTN>38THEN1360
950 BI=-1:BN=-1:BC=-1:MS$="MY TURN. I HOLD"+STR$(CT):GOSUB520:FORW=1TO500:NEXTW:GOSUB8
970 IFTN<=3THEN1240
980 FORK=1TOCT
990 IFDN=14THENIFCT>1THEN995
991 GOTO1000
995 BI=1:BN=CD(1,0):BC=CD(1,1):FORK=2TOCT:IFCD(K,0)>BNTHENBN=CD(K,0):BC=CD(K,1):BI=K
996 NEXT
1000 IFDN=14THENIFCT>.THENBI=1:GOTO1170
1010 IFCD(K,1)=DCANDCD(K,0)=12THENBI=K:GOTO1170
1011 IFDN=12ANDCD(K,0)=12THENBI=K:GOTO1170
1020 IFCD(K,1)=DCANDCD(K,0)=10THENBI=K:GOTO1170
1021 IFDN=10ANDCD(K,0)=10THENBI=K:GOTO1170
1030 NEXTK
1040 BN=CD(1,0):BC=CD(1,1):OK=0
1050 IFCT=1THEN1090
1060 FORK=2TOCT:IFCD(K,0)>BNANDCD(K,1)=DCTHENBN=CD(K,0):BC=CD(K,1):BI=K:OK=-1
1070 IFCD(K,0)>BNANDCD(K,0)=DNTHENBN=CD(K,0):BC=CD(K,1):BI=K:OK=-1
1080 NEXTK:IFOKTHEN1170
1090 FORK=1TOCT:IFCD(K,0)=DNORCD(K,1)=DCTHENBI=K:GOTO1170
1100 NEXTK
1110 FORK=1TOCT:IFCD(K,0)=13THENGOSUB1570:GOTO1170
1120 NEXTK:OK=-1:FORK=1TOCT:IFCD(K,1)=DCANDCD(K,0)<>13ANDCD(K,0)<>14THENOK=.
1130 NEXTK:IFOKTHEN1135
1131 GOTO1140
1135 FORK=1TOCT:IFCD(K,0)=14THENBI=K:K=CT:NEXT:GOTO1170
1136 NEXT
1140 MS$="I'M TAKING A CARD":GOSUB520:CT=CT+1:CD(CT,0)=D(Q,0):CD(CT,1)=D(Q,1):Q=Q+1
1150 IFQ=109THENQ=1
1160 FORW=1TO500:NEXTW:GOSUB8:GOTO180
1170 SP=40:GOSUB290:GOSUB1300
1180 IFCD(BI,0)=14THENFOREK=1TO4:GOSUB1330:NEXTEK:GOSUB1310:GOTO940
1190 IFCD(BI,0)=12THENFOREK=1TO2:GOSUB1330:NEXTEK:GOSUB1310:GOTO940
1200 IFCD(BI,0)=10THENGOSUB1310:GOTO940
1210 IFCD(BI,0)=13THENGOSUB420:GOSUB1310:GOTO180
1220 GOSUB1310
1230 GOTO180
1240 FORK=1TOCT:IFCD(K,0)=13THEN1270
1250 IFCD(K,0)=14THENIP=K:GOTO1280
1260 NEXT:GOTO980
1270 GOSUB1570:GOTO 1170
1280 OK=-1:FORK=1TOCT:IF(CD(K,1)=DCAND(CD(K,0)<>13ORCD(K,0)<>14))THEN980
1290 NEXT:BI=IP:GOTO1170
1300 DC=CD(BI,1):DN=CD(BI,0):NC=DN:CC=DC:SP=40:GOSUB620:RETURN
1310 IFBI<>CTTHENCD(BI,0)=CD(CT,0):CD(BI,1)=CD(CT,1)
1320 CT=CT-1:RETURN
1330 TN=TN+1:PD(TN,0)=D(Q,0):PD(TN,1)=D(Q,1):GOSUB610:Q=Q+1:GOSUB1680:RETURN
1360 IFCT=.ORTN>38THENGOSUB1410
1370 IFXC>=500THENMS$="I WIN! PLAY AGAIN?":GOSUB520:GOTO1520
1380 IFTN=.ORCT>38THENGOSUB1470
1390 IFXP>=500THEN MS$="YOU WIN! PLAY AGAIN?":GOSUB520:GOTO1520
1400 GOTO80
1410 RS=.:FORY1=1TOTN
1420 IFPD(Y1,0)=14ORPD(Y1,0)=13THENRS=RS+50:GOTO1450
1430 IFPD(Y1,0)=12ORPD(Y1,0)=10THENRS=RS+20:GOTO1450
1440 RS=RS+PD(Y1,0)
1450 NEXT:XC=XC+RS:TS$=STR$(XC):MS$="I WIN! ":GOSUB1460:RETURN
1460 MS$=MS$+"SCORED"+STR$(RS)+" TOTAL"+TS$:GOSUB520:SOUND100,1:SOUND200,1:SOUND100,1:FORW=1TO4000:NEXTW:GOSUB8:RETURN
1470 RS=0:FORY1=1TOCT
1480 IFCD(Y1,0)=14ORCD(Y1,0)=13THENRS=RS+50:GOTO1510
1490 IFCD(Y1,0)=12ORCD(Y1,0)=10THENRS=RS+20:GOTO1510
1500 RS=RS+CD(Y1,0)
1510 NEXT:XP=XP+RS:TS$=STR$(XP):MS$="YOU WIN! ":GOSUB1460:RETURN
1520 I$=INKEY$:IFI$=""THEN1520
1521 IFI$="Y"THENRUN
1522 IFI$="N"THEN920
1523 GOTO1520
1560 MS$="KEEP PLAYING? (Y/N)":GOSUB520:GOTO1520
1570 H1=0:H2=0:H3=0:H4=0:FORJK=1TOCT:IFJK=KTHEN1590
1580 ONCD(JK,1)+1GOSUB1640,1650,1660,1670
1590 NEXT:IFH1>=H2ANDH1>=H3ANDH1>=H4THENBI=K:IC=.:CD(K,1)=.:RETURN
1600 IFH2>=H1ANDH2>=H3ANDH2>=H4THENBI=K:IC=1:CD(K,1)=1:RETURN
1610 IFH3>=H1ANDH3>=H2ANDH3>=H4THENBI=K:IC=2:CD(K,1)=2:RETURN
1620 IFH4>=H1ANDH4>=H2ANDH4>=H3THENBI=K:IC=3:CD(K,1)=3:RETURN
1630 BI=K:IC=CD(K,1):RETURN
1640 H1=H1+1:RETURN
1650 H2=H2+1:RETURN
1660 H3=H3+1:RETURN
1670 H4=H4+1:RETURN
1680 IFQ=109THENQ=1
1681 RETURN

2120 REM ** ONO V3.1 **
2130 REM ** CHARLES BARTLETT **
2140 REM MC-10 EDITS JIM GERRIE
