0 DIMZ1,Z2,CC,Z1(5),X:Z1=RND(-(PEEK(9)*256+PEEK(10))):GOTO60
6 PRINT@0,;:M$="         BRINGMEE DOWNS         ":GOSUB7:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORZ1=CC-LEN(M$)+1TOCC:POKEZ1,PEEK(Z1)-64:NEXT:RETURN
8 PRINTRIGHT$("        "+STR$(INT(CC)),Z1);:IFZ2THENPRINTMID$(STR$((CC-INT(CC))+1.00001)+"000",3,Z2+1);
9 RETURN
50 REM initialize
60 DIMH$(40):DIMH(40,15):DIMJ$(10):DIMJ(10,4):DIMR$(4):DIMR(4,4):DIMRN(20,4):DIMJS(5):DIMR1(4):DIMR2(4):DIMSC(5,4):DIMBE(4,3)
70 DATAAAA,BBBB,CCCC,DDDD,ONYX,LILY,OPAL,PARK,PASA,SLOT,SLOU,CORP,CLAR,AUBE,AURA,BRAN,BULL,INKY,LEAR,MEAN,XERO,YUGA,DUNK,TRUM
80 DATADOLL,TRAN,WEBB,RAJA,DECI,STIR,NIMM,MILL,YULE,NUTT,TELE,ORLY,CHAT,TIFF,BEAN,MIST
90 DATAYAMADA,SMYTHE,RODRIGUES,TAPANILA,MERCKLE,JACKSON,CHAN,SKIPSON,RITCHIE,KLEGG
100 DATA300,1400,500,400,1900,750,500,3000,900,900,5000,1500
110 DATA"$ 800 CLAIMING","$ 1600 CLAIMING","ALLOWANCE","HANDICAP STAKES"
120 FORI=1TO40:READH$(I):NEXT
130 FORI=1TO10:READJ$(I):J(I,4)=300:NEXT
140 FORI=1TO4:READR(I,1),R(I,2),R(I,3):NEXT
150 FORI=1TO4:READR$(I):NEXT
160 FORI=1TO40:H(I,9)=-200:NEXT
170 CLS:PRINT@0,;:M$=" WELCOME TO A YEAR AT THE RACES ":GOSUB7:GOSUB4520:GOSUB4510:GOSUB4520
175 PRINT@64,"YOU HAVE 4 HORSES AND $10,000":PRINT"FIRST YOU MUST NAME YOUR HORSES"
180 FORI=1TO4
190 PRINT"HORSE # "I" IS CALLED ":INPUTH$(I)
200 IFLEN(H$(I))=4THEN220
210 PRINT"USE 4 LETTER NAME ONLY":GOTO190
220 NEXT
230 M=10000
240 REM start the races
260 REM print main menu
270 CLS:K=1
280 PRINT@0,;:M$="      A YEAR AT THE RACES       ":GOSUB7:PRINT@65,"THERE ARE 10 RACING DAYS"
285 PRINT@97,"EACH DAY YOU MUST DO 1 & 2":PRINT@161,"1. SET UP THE DAY'S 4 RACES":PRINT@193,"2. START THE DAY'S RACES"
290 PRINT@225,"3. VIEW HORSE HISTORY":PRINT@257,"4. VIEW JOCKEY STANDINGS":PRINT@289,"5. VIEW CURRENT CASH BALANCE"
300 PRINT@321,"6. PRINTOUT OF HORSES"
310 PRINT@385,;
320 INPUT"ENTER THE # OF YOU CHOICE";P
330 IFP=1ANDSO=1THENCLS:PRINT@481,"RACES ARE ALREADY SET UP":GOTO280
340 IFP=2ANDSO=0THENCLS:PRINT@480,"MUST SET UP RACES BEFORE RUNNING":GOTO280
350 ONPGOTO390,1180,1070,2650,2730,2850
360 CLS:PRINT@481,"INVALID CHOICE, TRY AGAIN":GOTO280
370 IFBE(A,1)=0 THEN BP=0:GOTO2270
380 REM setup races
390 M=M-250
400 FORI=1TO20:RN(I,1)=0:RN(I,2)=0:NEXT
410 WE=RND(5):IFWE=1THENW$="RAIN":GOTO420
411 W$="FAIR"
420 DY=DY+1:HC=0
425 IFDY>10THEN2770
430 FORA=1TO4
440 L=RND(2):IFL=1THENL$="6 FURLONGS":GOTO450
441 L$="1 1/16 MILES"
450 R(A,4)=L
460 CLS:GOSUB6:PRINT@41,"DAY"DY" RACE"A
470 PRINT@69,R$(A)
480 PRINT@101,"LENGTH IS "L$
490 PRINT@133,"WEATHER FORECAST IS "W$
500 PRINT@165,"WINNER GETS $";:Z1=4:Z2=2:CC=R(A,2):GOSUB8
510 PRINT@197,"PLACE  GETS $";:Z1=4:Z2=2:CC=R(A,3):GOSUB8
520 PRINT@259,"COST OF ENTRY IS $";:Z1=4:Z2=2:CC=R(A,1):GOSUB8
530 PRINT@323,"'Y'= ENTER A HORSE":PRINT@355,"'N'= CONTINUE"
540 INPUT"===>";E$
550 IFE$="Y"THEN570
560 IFE$="N"THEN580
561 GOTO460
570 GOSUB620:GOTO590
580 JK=0:Y2=1:GOSUB910:GOTO590
590 NEXTA
600 GOTO270
605 M$=LEFT$("         DAY"+STR$(DY)+"  RACE"+STR$(A)+"              ",32):GOSUB7:RETURN
610 REM player's races
620 CLS
630 PRINT@0,;:GOSUB605
640 PRINT@67,"TYPE IN THE NUMBER OF":PRINT@99,"HORSE YOU WANT ENTERED":PRINT@133,"OR":PRINT@163,"'H'= HORSE HISTORY"
650 INPUT"===>";HS$
660 IFHS$="H"THENK=2:GOSUB1070:CLS:GOTO630
670 HS=VAL(HS$)
680 IFHS<1ORHS>4THEN690
681 GOTO700
690 CLS:PRINT@417,"INVALID ENTRY":PRINT@449,"ENTER 1, 2, 3, 4 OR H":GOTO630
700 IF(HS=RN(1,1))OR(HS=RN(6,1))OR(HS=RN(11,1))OR(HS=RN(16,1))THEN710
701 GOTO720
710 CLS:PRINT@352,"I AM SORRY, BUT YOU HAVE":PRINT@384,"ALREADY ENTERED THAT HORSE":PRINT@416,"IN A RACE.  PASS OR"
711 PRINT@448,"ENTER ANOTHER HORSE":GOTO630
720 HC=HC+1:RN(HC,1)=HS:CLS
730 PRINT@128,"ENTER THE NUMBER OF THE":PRINT@160,"JOCKEY YOU WANT TO RIDE":PRINT@192,H$(HS)
740 PRINT@227,"OR":PRINT@256,"TYPE 'J' TO SEE JOCKEYS"
750 INPUT"===>";JC$
760 IFJC$="J"THENK=2:GOSUB2650:CLS:GOTO730
770 JC=VAL(JC$)
780 IF(JC<1ORJC>10)THEN790
781 GOTO800
790 CLS:PRINT@448,"ENTER A NUMBER BETWEEN 1 & 10":GOTO730
800 CLS:PRINT@64,"YOU HAVE CHOSEN "J$(JC)
810 PRINT@192,"HIS FEE IS $"J(JC,4):PRINT@256,"ENTER AN 'N' TO CHOOSE ANOTHER":PRINT@288,"JOCKEY OR ENTER TO CONTINUE"
820 INPUT"===>";JC$
830 IFJC$="N"THEN840
831 GOTO850
840 CLS:GOTO730
850 RN(HC,2)=JC:JS(1)=JC:M=M-J(JC,4)-R(A,1)
860 IFM<0THEN870
861 GOTO880
870 FORXX=1TO1000:CLS:PRINT@96,"LOSE THIS ONE AND YOU'RE":PRINT@128,"IN HOCK BIG FELLA":NEXTXX
880 JK=1:Y2=2:GOSUB910
890 RETURN
900 REM fill out the race card
910 Y1=1
920 JK=JK+1:IFJK>5THEN1050
930 HC=HC+1
940 HN=RND(40):IFHN<5THEN940
950 FORY=1TO(HC-1):IFHN=RN(Y,1)THEN940
960 NEXTY
970 RN(HC,1)=HN
980 JP=RND(10)
990 FORZ=1TO(Y1)
1000 IFJP=JS(Z)THEN980
1010 NEXTZ
1020 JS(Y2)=JP:RN(HC,2)=JP
1030 Y1=Y1+1:Y2=Y2+1
1040 GOTO920
1050 SO=1:RETURN
1060 REM horse history
1070 CLS:PRINT@33,"ENTER THE NUMBER OF THE HORSE":PRINT@65,"YOU WANT INFORMATION ABOUT":PRINT@96,;:INPUT"===>";HN
1080 IFHN<1ORHN>40THEN1070
1090 CLS:PRINT@72,H$(HN)"  NUMBER "HN:PRINT@164,"# 1STS ";:Z1=3:Z2=0:CC=H(HN,1):GOSUB8
1100 PRINT@196,"# 2NDS ";:Z1=3:Z2=0:CC=H(HN,2):GOSUB8
1101 PRINT@228,"# RACES";:Z1=3:Z2=0:CC=H(HN,7):GOSUB8:PRINT@290,"SPEED AVERAGE ";:Z1=3:Z2=1:CC=H(HN,9):GOSUB8
1110 PRINT@322,"LAST RACE     ";:Z1=3:Z2=0:CC=H(HN,12):GOSUB8:PRINT@386,"ENTER ANOTHER NUMBER  OR"
1120 PRINT@418,"TYPE 'R' TO RETURN"
1130 INPUT"===>";Z$
1140 IFZ$="R"THEN1150
1141 HN=VAL(Z$):GOTO1080
1150 IFK=2THEN1170
1160 GOTO270
1170 K=1:RETURN
1180 REM run the races
1190 WF=RND(4):IFWF=1THEN1200
1191 GOTO1210
1200 IFWE>1THENWE=1:GOTO1210
1201 WE=2:GOTO1220
1210 IFWE>1THENWE=2
1220 FORX=1TO4:FORY=1TO3:BE(X,Y)=0:NEXTY:NEXTX
1230 FORX=1TO20:HR=RN(X,1)
1240 IFX<6THEND=R(1,4)+9:GOTO1270
1250 IFX<11THEND=R(2,4)+9:GOTO1270
1260 IFX<16THEND=R(3,4)+9:GOTO1270
1261 D=R(4,4)+9
1270 IFX<6THENC=3:GOTO1300
1280 IFX<11THENC=4:GOTO1300
1290 IFX<16THENC=5:GOTO1300
1291 C=6
1300 IFH(HR,2)>1THENS=2:GOTO1310
1301 S=H(HR,2)
1310 JN=J((RN(X,2)),2)
1320 C1=0:C2=0
1330 IF(H(HR,14)+2)>CTHENC1=1:C2=0
1340 IF(H(HR,14)+2)<CTHENC1=0:C2=1
1350 IFH(HR,1)>1THENF=2:GOTO1360
1351 F=H(HR,1)
1360 IFWE=1ANDH(HR,13)=1THENMU=1:GOTO1370
1361 MU=0
1370 T=0:IFDY/3<H(HR,7)THENT=1
1380 IFDY/2<H(HR,7)THENT=2
1390 RM=0:FORY=1TO4:B1=RND(84):B2=RND(2):IFB2=2THENRM=RM-B1:GOTO1400
1391 RM=RM+B1:NEXTY
1400 RM=RM/100
1410 RN(X,3)=RM+.53*H(HR,8)-.08*H(HR,C)-.44*H(HR,D)+.24*JN-.01*H(HR,9)+.1*C1+.12*C2+.3*F+.04*S-.25*MU+.12*T+10
1420 NEXTX
1430 FORA=1TO4:FORX=1TO2:BE(A,B)=0:NEXTX:NEXTA
1440 CT=-5:SO=0
1450 FORA=1TO4:CT=CT+5
1460 CLS:U=96:U1=114
1470 GOSUB6:PRINT@41,"DAY"DY" RACE"A:PRINT@96,"HORSE    NUMBER   JOCKEY"
1480 FORV=1TO5:U=U+32:PRINT@U,H$(RN((CT+V),1));:Z1=9:Z2=0:CC=RN((CT+V),1):GOSUB8:PP(V)=CC:PRINT
1490 IFR(A,4)=1THENL$="6 FURLONGS":GOTO1500
1491 L$="1 1/16 MILES"
1500 U1=U1+32:PRINT@U1,J$((RN((CT+V),2))):NEXT
1510 PRINT@289,L$:PRINT@303,R$(A):PRINT@321,"$";R(A,2);"TO WIN $";R(A,3);"TO PLACE"
1520 PRINT@352,"TRACK IS ":IFWE=1THEN1540
1530 PRINT@361,"FAST":GOTO1550
1540 PRINT@361,"MUDDY":GOTO1550
1550 PRINT@384,"'R'= RUN RACE       'B'= BET":PRINT@416,"'H'= HORSE HISTORY  'J'=JOCKEYS"
1560 INPUT"===>";B$
1570 IFB$="B"THEN1610
1580 IFB$="H"THEN1780
1590 IFB$="J"THENK=2:GOSUB2650:GOTO1460
1600 IFB$="R"THEN1790
1601 GOTO1460
1610 CLS
1620 PRINT@3,"ENTER NUMBER OF HORSE":PRINT@35,"YOU WANT TO BET ON":U=68:FORV=1TO5
1630 U=U+32:PRINT@U,H$(RN((CT+V),1))" # IS";:Z1=3:Z2=0:CC=RN((CT+V),1):GOSUB8:PRINT:NEXTV
1640 INPUT"===>";HB
1650 FORV=(CT+1)TO(CT+5):IFHB=RN(V,1)THEN1680
1660 NEXTV
1670 CLS:PRINT@450,"TRY AGAIN":GOTO1620
1680 CLS
1690 PRINT@3,"YOU CAN BET FROM $5 TO $500":INPUT"HOW MUCH";AB
1700 IFAB<5ORAB>500THEN1710
1701 GOTO1720
1710 CLS:PRINT@450,"ENTER AMOUNT AGAIN":GOTO1690
1720 CLS
1730 PRINT@1,"ENTER 1 FOR WIN OR 2 FOR PLACE"
1740 INPUT"===>";BT
1750 IFBT<1ORBT>2THEN1760
1751 GOTO1770
1760 CLS:PRINT@450,"'1' OR '2' ONLY":GOTO1730
1770 BE(A,1)=AB:BE(A,2)=BT:BE(A,3)=HB:GOTO1460
1780 K=2:GOSUB1070:GOTO1460
1790 HN=CT+1
1800 SC(1,1)=RN(HN,3):SC(1,2)=RN(HN,1):SC(1,4)=RN(HN,2)
1810 Y1=1
1820 FORX=(HN+1)TO(HN+4)
1830 Y=0:Y1=Y1+1
1840 FORZ=Y1TO2STEP-1
1850 IFY=1THEN1900
1860 IFSC((Z-1),1)>RN(X,3)THEN1880
1870 SC(Z,1)=RN(X,3):SC(Z,2)=RN(X,1):SC(Z,4)=RN(X,2):Y=1:GOTO1900
1880 SC(Z,1)=SC((Z-1),1):SC(Z,2)=SC((Z-1),2):SC(Z,4)=SC((Z-1),4)
1890 IFZ=2THENSC((Z-1),1)=RN(X,3):SC((Z-1),2)=RN(X,1):SC((Z-1),4)=RN(X,2)
1900 NEXTZ
1910 NEXTX
1920 IFA=1THENC=3:GOTO1950
1930 IFA=2THENC=4:GOTO1950
1940 IFA=3THENC=5:GOTO1950
1941 C=6
1950 D=R(A,4)+9
1960 SC(1,3)=102-RND(40):SC(5,3)=SC(1,3)-RND(40):SD=SC(1,3)-SC(5,3)
1970 SC(4,3)=SC(5,3)+.25*SD:SC(3,3)=SC(5,3)+.5*SD:SC(2,3)=SC(5,3)+.75*SD
1980 WN=SC(1,2):PL=SC(2,2):SH=SC(3,2):FO=SC(4,2):FI=SC(5,2)
1990 H(WN,1)=H(WN,1)+1:H(PL,2)=H(PL,2)+1
2000 H(WN,C)=H(WN,C)+1:IFH(WN,C)>1THENH(WN,C)=1
2010 H(PL,C)=H(PL,C)+1:IFH(PL,C)>1THENH(PL,C)=1
2020 H(WN,D)=H(WN,D)+1:IFH(WN,D)>1THENH(WN,D)=1
2030 H(PL,D)=H(PL,D)+1:IFH(PL,D)>1THENH(PL,D)=1
2040 H(WN,7)=H(WN,7)+1:H(PL,7)=H(PL,7)+1:H(SH,7)=H(SH,7)+1:H(FO,7)=H(FO,7)+1:H(FI,7)=H(FI,7)+1
2050 H(WN,8)=((H(WN,7)-1)*H(WN,8)+1)/H(WN,7)
2060 H(PL,8)=((H(PL,7)-1)*H(PL,8)+2)/H(PL,7)
2070 H(SH,8)=((H(SH,7)-1)*H(SH,8)+3)/H(SH,7)
2080 H(FO,8)=((H(FO,7)-1)*H(FO,8)+4)/H(FO,7)
2090 H(FI,8)=((H(FI,7)-1)*H(FI,8)+5)/H(FI,7)
2100 H(WN,9)=(((H(WN,7)-1)*H(WN,9))+SC(1,3))/H(WN,7)
2110 H(PL,9)=(((H(PL,7)-1)*H(PL,9))+SC(2,3))/H(PL,7)
2120 H(SH,9)=(((H(SH,7)-1)*H(SH,9))+SC(3,3))/H(SH,7)
2130 H(FO,9)=(((H(FO,7)-1)*H(FO,9))+SC(4,3))/H(FO,7)
2140 H(FI,9)=(((H(FI,7)-1)*H(FI,9))+SC(5,3))/H(FI,7)
2150 H(WN,12)=DY:H(PL,12)=DY:H(SH,12)=DY:H(FO,12)=DY:H(FI,12)=DY
2160 IFWE=1THENH(WN,13)=1
2170 H(WN,14)=A:H(PL,14)=A:H(SH,14)=A:H(FO,14)=A:H(FI,14)=A
2180 H(WN,15)=H(WN,15)+R(A,2)
2190 H(PL,15)=H(PL,15)+R(A,3)
2200 IFWN<5THENM=M+R(A,2)
2210 IFPL<5THENM=M+R(A,3)
2220 IFBE(A,2)=0THEN2230
2221 GOTO2240
2230 BP=0:GOTO2270
2240 IFBE(A,2)=1ANDWN=BE(A,3)THENM=M+BE(A,1):BP=1:GOTO2270
2250 IF(BE(A,2)=2)AND((WN=BE(A,3))OR(PL=BE(A,3)))THENM=M+BE(A,1)*.5:BP=2:GOTO2270
2260 M=M-BE(A,1):BP=3
2270 WN=SC(1,4):PL=SC(2,4):SH=SC(3,4):FO=SC(4,4):FI=SC(5,4)
2280 J(WN,3)=J(WN,3)+1
2290 J(WN,1)=J(WN,1)+1:J(PL,1)=J(PL,1)+1:J(SH,1)=J(SH,1)+1:J(FO,1)=J(FO,1)+1:J(FI,1)=J(FI,1)+1
2300 J(WN,2)=(((J(WN,1)-1)*J(WN,2))+1)/J(WN,1)
2310 J(PL,2)=(((J(PL,1)-1)*J(PL,2))+2)/J(PL,1)
2320 J(SH,2)=(((J(SH,1)-1)*J(SH,2))+3)/J(SH,1)
2330 J(FO,2)=(((J(FO,1)-1)*J(FO,2))+4)/J(FO,1)
2340 J(FI,2)=(((J(FI,1)-1)*J(FI,2))+5)/J(FI,1)
2350 GOSUB3000:CLS
2360 GOSUB605
2370 PRINT@73,"R E S U L T S"
2380 PRINT@160,"HORSE      #  SPEED  JOCKEY"
2390 U=161:U1=181:FORX=1TO5
2400 U=U+32:U1=U1+32:PRINT@U,H$(SC(X,2));:Z1=7:Z2=0:CC=SC(X,2):GOSUB8:Z1=5:Z2=1:CC=SC(X,3):GOSUB8:PRINT@U1,J$(SC(X,4)):NEXTX
2410 IFBP=0THEN2440
2420 IFBP=1THEN2450
2430 IFBP=2THEN2460
2431 GOTO2470
2440 PRINT@354,"NO BET":GOTO2480
2450 PRINT@354,"YOUR BET PAYS  $";:Z1=4:Z2=2:CC=BE(A,1)*2:GOSUB8:PRINT:GOTO2480
2460 PRINT@354,"YOUR BET PAYS  $";:Z1=4:Z2=2:CC=BE(A,1)*1.5:GOSUB8:PRINT:GOTO2480
2470 PRINT@354,"YOUR BET LOSES $";:Z1=4:Z2=2:CC=BE(A,1):GOSUB8:PRINT
2480 PRINT"PRESS ENTER TO CONTINUE"
2490 PRINT"'P'= PRINTOUT OF THIS RACE"
2500 INPUT"===>";XX$
2510 IFXX$="P"THENGOSUB2890
2520 NEXTA
2530 FORA=1TO10
2540 IFJ(A,2)<1.5THENJ(A,4)=500:GOTO2620
2550 IFJ(A,2)<2THENJ(A,4)=450:GOTO2620
2560 IFJ(A,2)<2.5THENJ(A,4)=400:GOTO2620
2570 IFJ(A,2)<3THENJ(A,4)=350:GOTO2620
2580 IFJ(A,2)<3.5THENJ(A,4)=300:GOTO2620
2590 IFJ(A,2)<4THENJ(A,4)=200:GOTO2620
2600 IFJ(A,2)<4.5THENJ(A,4)=150:GOTO2620
2610 J(A,4)=100
2620 NEXTA
2630 GOTO270
2640 REM jockey standings
2650 CLS
2660 PRINT@1,"JOCKEYS AND COST PER RACE":PRINT@65,"#  PRICE  WINS   NAME":U=68:FORX=1TO10:U=U+32
2670 Z1=2:Z2=0:CC=X:GOSUB8:PRINT:PRINT@U,J(X,4);:Z1=5:Z2=0:CC=J(X,3):GOSUB8:PRINT:PRINT@(U+14),J$(X):NEXT
2680 INPUT"===>";XX
2690 IFK=2THEN2710
2700 GOTO270
2710 K=1:RETURN
2720 REM display money
2730 CLS:PRINT@130,"YOU HAVE $"M
2740 INPUT"===>";XX
2750 GOTO270
2760 REM close the year
2770 TW=H(1,1)+H(2,1)+H(3,1)+H(4,1):RR=(DY-1)*4
2780 CLS:PRINT@2,"THE RACING YEAR AT":PRINT"  BRINGMEE DOWNS IS OVER":PRINT"  YOU HAVE $"M"--"
2811 IFM<10000THENPRINT"  STICK TO COMPUTERS":GOTO2820
2812 IFM<=15000THENPRINT"  NOT BAD":GOTO2820
2813 IFM<=20000THENPRINT"  DOING WELL":GOTO2820
2814 PRINT"  CONTACT ME AT DERBY TIME"
2820 PRINT"  YOUR HORSES WON"TW"RACES":PRINT"  OUT OF"RR"RACES --"
2821 IFTW<4THENPRINT"  SO BAD, TOO SAD":END
2822 IFTW<9THENPRINT"  ABOUT AS EXPECTED":END
2823 IFTW<13THENPRINT"  DOING WELL":END
2840 PRINT"  BORN TO THE GAME":END
2850 FORI=1TO40
2860 LPRINTH$(I)I" WINS= "H(I,1)" 2NDS= "H(I,2)" RACES= "H(I,7)" WINNINGS= "H(I,15)" SPEED= "H(I,9)
2870 NEXT
2880 GOTO270
2890 LPRINT" ":LPRINT" DAY "DY" RACE "A
2900 IFWE=1THEN2910
2901 GOTO2920
2910 LPRINT"TRACK IS MUDDY":GOTO2930
2920 LPRINT"TRACK IS FAST"
2930 LPRINTL$
2940 LPRINT"  R E S U L T S"
2950 LPRINT"HORSE   #  SPEED   JOCKEY"
2960 FORX=1TO5
2970 LPRINTH$(SC(X,2))"   "SC(X,2)"   "SC(X,3)"  "J$(SC(X,4)):NEXT
2980 LPRINT" "
2990 RETURN
3000 FORY=1TO5:X=Y:T=Y
3006 IFY>3THENT=T+2
3007 W=(T-1)*16
3010 G=151+W:K1=152+W:E=147+W:F1=148+W:A$(X,1)="êê"+CHR$(E)+CHR$(G)+CHR$(K1):A$(X,2)="êê"+CHR$(K1)+CHR$(F1)+"ê"
3020 A$(X,3)="Ä"+CHR$(F1)+"Ä"+CHR$(K1)+"Ä":A$(X,4)="ÄÄ"+CHR$(F1)+"Ä"+CHR$(K1):NEXTY
3050 CLS0:Y=0
3051 PRINT@Y,"ååååååååååååååååååååååååååååååå";
3052 Y=Y+96:IFY<500THEN3051
3054 FORX=0TO30:SET(10,X,1):SET(62,X,8):SET(63,X,8):NEXTX
3060 H1(1)=32:H1(2)=128:H1(3)=224:H1(4)=320:H1(5)=416
3065 GOSUB4000:FORX=1TO5:SOUND200,1:NEXTX
3066 FORX=1TO5:FORCC=1TO5:IFSC(CC,2)=PP(X)THENZ1(X)=SC(CC,3)/100
3067 NEXT:NEXT
3070 FORX=1TO5:H1(X)=H1(X)+Z1(X):G(X)=G(X)+1:IFG(X)=1THENW=3
3073 IFG(X)=2ORG(X)=4THENW=2
3074 IFG(X)=3THENW=4
3075 IFG(X)=5THENG(X)=1:W=3
3076 PRINT@H1(X)+32,A$(X,W);:PRINT@H1(X),A$(X,1);:IFH1(X)>58+((X-1)*96)THENZ1=X:X=5:NEXT:X=Z1:GOTO3080
3079 NEXT:GOTO3070
3080 FORY=1TO10:SOUND236,1:NEXTY
3090 G=0:K1=0:M$=H$(SC(1,2))
3100 K$=INKEY$:G=G+1:IFG=10THENPRINT@H1(X)-15,M$;
3110 IFG=20THENPRINT@H1(X)-15,;:GOSUB7:G=0
3120 K1=K1+1:IFK1=100THENRETURN
3130 IFK$=""THEN3100
3200 RETURN
4000 FORX=1TO5
4010 PRINT@H1(X),A$(X,1);:PRINT@H1(X)+32,A$(X,2);
4050 IFX=1THENPRINT@43,"bringmeeÄdowns";
4070 IFX=3THENPRINT@235,"theÄhorsesÄare";
4090 IFX=4THENPRINT@332,"atÄtheÄgate";
4091 SOUND(X*20)+100,1
4095 FORY=1TO100:NEXTY
4100 NEXTX
4200 FORY=1TO100:NEXTY
4210 FORX=1TO5:PRINT@40+((X-1)*96),"ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ";:NEXTX
4500 RETURN
4510 FORZ1=1TO25:NEXT:RETURN
4520 SOUND100,3:GOSUB4510:SOUND100,3:GOSUB4510:SOUND141,3:GOSUB4510:SOUND163,3:GOSUB4510:SOUND178,2
4521 GOSUB4510:SOUND178,2:GOSUB4510:SOUND178,2
4522 GOSUB4510:SOUND163,2:GOSUB4510:SOUND163,2:GOSUB4510:SOUND163,2:GOSUB4510:SOUND141,2:GOSUB4510:SOUND163,2
4523 GOSUB4510:SOUND141,2:GOSUB4510:SOUND100,4:RETURN
5020 REM a year at the races
5030 REM by glen tapanila
5040 REM MC-10 EDITS J. GERRIE
