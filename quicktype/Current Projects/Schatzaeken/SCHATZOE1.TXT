5 CLS:DIM D(3,2),T(20):Q=16384
8 Z$=" YOU ARE IN THE ENTRANCE HALL"
10 LR=0:C$="ÙÙÙÙÙÙÙÙÙÙÙÙ"+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(143)+"ÙÙÙÙÙÙÙÙÙÙÙÙÙÙ"
20 AR=5
25 FOR I=1 TO 20
30 T(I)=1
35 NEXT I
37 PS=0
40 S=0
60 TR=0
65 C1=RND(-(PEEK(9)*256+PEEK(10))) 
70 B$="@BE@AF@DG@CHAHIBIJCJKDEKEFLFGMGHNIOQJOPKPTLMRMNSLRTOQUPTUNQS@RS"
80 R1=0
82 R2=0
84 R3=0
95 GOTO 1300
150 CLS 
160 REM CAVES PLOT
165 FOR I=0 TO 14
170 IY=INT(RND(0)*12+2)
175 IX=INT(RND(0)*27+2)
180 PRINT@32*IY+IX,"€";
185 IF RND(0)>.5 THEN PRINT@32*(IY-1)+IX-1,MID$("€ˆ",RND(2),1);
190 PRINT@32*IY+IX+1,"ˆ";
195 PRINT@32*(IY+1)+IX,MID$("€ˆ",RND(2),1);
200 IF RND(0)>.6 THEN PRINT@32*(IY+1)+IX+1,"„";
205 PRINT@32*I,"Ù";:PRINT@32*I+31,"Ù";
210 NEXT I
212 POKEQ+480,217:POKEQ+511,217:PRINT@1,C$;:PRINT@32*15+1,LEFT$(C$,31);
214 PRINT@17,STR$(R1);:PRINT@32*15+17,STR$(R2);
215 N=9
220 M=1
222 IF T(PR)=1 THEN PRINT@32*IY+IX+1,"*";
224 T(PR)=0
225 REM MONSTER PLOT
230 DEM=INT(RND(0)*4):IFDEM=.THEN275
235 FOR I=1 TO DEM
240 D(I,1)=INT(RND(0)*13+1)
245 D(I,2)=INT(RND(0)*30+1)
250 PRINT@32*D(I,1)+D(I,2),"M";
255 NEXT I
265 REM HERO MOVEMENT
275 PRINT@32*N+M,"X";
280 IFPEEK(2)THENIF8ANDNOTPEEK(16952)THEN285
281 GOTO300
285 IF AR=0 THEN 300
290 AR=AR-1:PRINT@32*15,"ARROWS:";AR;
295 GOSUB 1000:PRINT@32*15,LEFT$(C$,12);
300 N1=N:M1=M
310 IFPEEK(2)THENIF4ANDNOTPEEK(16952)THENDY=-1:DX=.:N=N+DY:GOTO320
311 IFPEEK(2)THENIF1ANDNOTPEEK(16946)THENDX=-1:DY=.:M=M+DX:GOTO320
312 IFPEEK(2)THENIF4ANDNOTPEEK(16948)THENDY=1:DX=.:N=N+DY:GOTO320
313 IFPEEK(2)THENIF1ANDNOTPEEK(16949)THENDX=1:DY=.:M=M+DX
320 Q1=PEEK(Q+32*N+M)
325 IF Q1=77 THEN 2000
330 IF Q1=106 AND TR<50 THEN TR=TR+10:SOUND150,2:GOTO338
335 IFQ1=143ORN=.ORN=15THEN800
336 IFQ1<>96THENIFQ1<>88THENN=N-DY:M=M-DX
338 IF ABS(8-N)=8 THEN 2000
339 IF PR=21 THEN GOSUB 3000:GOTO345
340 IF DEM=0 AND RND(0)<.03 THEN DEM=1:D(1,1)=1:D(1,2)=1:SOUNDRND(200),1:SOUNDRND(200),1
342 IF RND(0)<.3+TR/200 THEN GOSUB 1200
345 PRINT@32*N1+M1," ";
350 GOTO 275
800 REM ROOM CHANGEOVER 
805 CLS
810 LR=PR
812 IF PR<>21 THEN 815
813 PR=INT(RND(0)*19+1)
814 GOTO 825
815 IF N=0 THEN PR=R1
820 IF N=15 THEN PR=R1
822 IF PR=0 THEN 1505
825 PRINT@32*3+2,"YOU ARE IN THE TUNNEL";
830 PRINT@32*5+2,"BETWEEN";LR;"AND";PR;
835 PRINT@32*8+2,"YOU HAVE";AR;"ARROWS";
840 PRINT@32*11+2,"YOUR SCORE IS";TR+S;
850 FORC1=1TO2500:NEXT
915 IF PR=21 THEN 2050
920 R$=MID$(B$,PR*3-2,1)
925 R1=ASC(R$)-64
930 R$=MID$(B$,PR*3-1,1)
935 R2=ASC(R$)-64
940 R$=MID$(B$,PR*3,1)
945 R3=ASC(R$)-64
960 IF R1=LR THEN R1=R3
965 IF R2=LR THEN R2=R3
970 GOTO 150
999 REM FIRE
1000 IF PR=21 THEN GOSUB 3000
1005 IF RND(0)<.5 THEN GOSUB 1200
1020 Y1=N:X1=M
1030 Y1=Y1+DY:X1=X1+DX
1040 C1=PEEK(Q+32*Y1+X1):IF C1=77 THEN 1070
1050 IF C1<>96 THEN RETURN
1055 PRINT@32*Y1+X1,"+";:SOUND225,1
1060 PRINT@32*Y1+X1," ";
1065 GOTO 1030
1070 DH=DEM:IFDH=.THEN1117
1075 FOR I=1 TO DH
1080 Y=D(I,1)
1085 X=D(I,2)
1090 IF X<>X1 OR Y<>Y1 THEN 1115
1095 PRINT@32*Y1+X1,"+";
1098 S=S+5
1100 D(I,1)=D(DEM,1)
1105 D(I,2)=D(DEM,2)
1110 DEM=DEM-1
1115 NEXT I
1117 IF PR=21 THEN FX=X1
1118 IF PR=21 THEN FY=Y1
1120 PRINT@32*Y1+X1," ";:SOUND100,2
1125 RETURN
1999 REM MONSTER MOVEMENT
1200 IFDEM=.THENRETURN
1201 FOR I=1 TO DEM
1205 X=D(I,2)
1210 Y=D(I,1)
1215 PRINT@32*Y+X," ";
1220 Y=Y+SGN(N-Y):X=X+SGN(M-X):IFPEEK(Q+32*Y+X)=128THENY=Y-SGN(N-Y):X=X-SGN(M-X)
1235 PRINT@32*Y+X,"M";:IF X=M AND Y=N THEN 2000
1240 D(I,1)=Y
1245 D(I,2)=X
1250 NEXT I
1255 RETURN
1265 REM START
1295 CLS 
1300 PRINT "WELCOME TO THE TREASURE CAVES"
1301 PRINT 
1305 PRINT "YOU ARE IN A NETWORK OF CAVES."
1310 PRINT "YOU ARE LOOKING FOR GOLD."
1315 PRINT "IF YOU ARE CAREFUL ENOUGH"
1320 PRINT "YOU CAN REACH THE TREASURE"
1322 PRINT "BUT WATCH OUT!--"
1325 PRINT "THERE ARE MONSTERS WHO WANT TO"
1330 PRINT "EAT YOU. USE: wasd TO MOVE"
1335 PRINT "AND space TO FIRE ARROWS."
1340 PRINT "YOU ARE IN THE ENTRANCE HALL.":PRINT:PRINT "CHOOSE CAVE 1,2,3 OF 4";
1345 INPUT PR
1350 IF PR<1 OR PR>4 THEN 1345
1360 LR=0
1500 GOTO 915
1505 PRINT "YOU ARE IN THE ENTRANCE HALL"
1510 PRINT "YOUR TREASURE IS SAFE HERE.":PRINT 
1515 PRINT "YOUR SCORE IS";S+TR;"POINTS"
1518 PRINT "YOU HAVE";AR;"ARROWS.":PRINT
1520 PRINT "ARE YOU GOING IN AGAIN? (Y/N)"
1525 INPUT F$
1530 IF LEFT$(F$,1)="N" THEN PRINT "YOU SCORED";S+TR;"POINTS"
1532 IF LEFT$(F$,1)="N" THEN PRINT "CHICKEN"
1535 IF LEFT$(F$,1)="N" THEN END
1540 S=S+TR
1545 TR=0
1550 PRINT "HOW MANY ARROWS DO YOU WANT?":PRINT
1560 PRINT "THEY COST 4 POINTS EACH."
1570 INPUT F
1580 IF F*4>S THEN 1650
1590 S=S-F*4
1600 AR=AR+F
1610 GOTO 1342
1650 CLS
1655 PRINT "  YOU CANNOT AFFORD THEM":FORC1=1TO2500:NEXT
1660 GOTO 1550
2000 REM DEATH OF A HERO
2005 CLS 
2010 PRINT@32*5+1,"UNFORTUNATELY, YOU'RE DEAD.";
2015 PRINT@32*8+1,"YOU SCORED:";S+TR;"POINTS";:FORC1=1TO2500:NEXT
2020 IF S+TR<50+PS THEN 3500
2025 S=S-15
2028 PS=S+TR
2029 PRINT "YOU PLAYED WELL:"
2030 PRINT "I WILL DEDUCT 15 POINTS":PRINT "FOR DAMAGE TO YOUR BODY":PRINT
2035 PRINT "YOU MAY CONTINUE";
2036 FOR C1=1 TO 2500
2037 NEXT C1
2038 CLS
2040 GOTO 1505
2045 REM TREASURE
2050 CLS0:PR=1
2055 FOR I=-7 TO 7
2058 AI=ABS(I)
2060 PRINT MID$(C$,1,AI);
2065 PRINT TAB(30-AI);MID$(C$,1,AI)"€€";
2070 NEXT I
2075 PRINT@32*7,MID$(C$,1,10);
2080 PRINT@32*15,MID$(C$,1,10)MID$(C$,1,10)MID$(C$,1,10);
2085 PRINT@32*7,"Ù";:PRINT@32*7+29,"Ù€€";
2086 DEM=1
2087 QW=INT(RND(0)*4)
2090 FOR I=1 TO QW+1
2095 PRINT@32*14+I+12+INT(RND(0)*2),"***";
2100 NEXT I
2105 N=8
2106 MY=-1
2107 MX=0
2108 K=18
2109 J=7
2110 M=3
2111 FX=J
2112 FY=K
2115 GOTO 265
2999 REM VAL
3000 IF PEEK(Q+32*(K+MY)+J+MX)=96 THEN 3015
3002 AD=MY
3005 IF AD=0 THEN MY=MX
3006 IF AD=0 THEN MX=0
3007 IF AD<>0 THEN MY=0
3008 IF AD<>0 THEN MX=0-AD
3015 J=J+MX
3020 K=K+MY
3025 PRINT@32*K+J,"Ù";
3030 IF RND(0)>.5 THEN PRINT@32*FY+FX,"Ù"
3032 IF RND(0)<.25 THEN GOSUB 1200
3035 RETURN 
3500 REM SECOND CHANCE
3502 FOR C1=1 TO 2500
3503 NEXT
3505 CLS 
3510 PRINT "WANT TO PLAY AGAIN? (Y/N)"
3515 INPUT Z$
3520 IF Z$="N" THEN 9990 
3525 IF Z$="Y" THEN 1295
3530 IF Z$="" THEN 3510
9990 END
9991 REM "SCHATZOEKEN"
9992 REM MC-10 EDITS JIM GERRIE
