0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO140
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
140 PRINT:PRINTTAB(10)"WILLIAM TELL":PRINT
170 DIM A$(20),B$(20)
190 PRINT "WILLIAM TELL IS A GAME OF LOGIC"
191 PRINT "IN WHICH THE COMPUTER GIVES YOU"
200 PRINT "EXAMPLES OF WHAT WILLIAM TELL"
201 PRINT "IS AND YOU HAVE TO GUESS THE"
210 PRINT "SECRET BEHIND THE WORDS THAT"
220 PRINT "CHARACTERIZE WILLIAM TELL. THE"
221 PRINT "COMPUTER WILL PROVIDE YOU WITH"
230 PRINT "EXAMPLES AS A HINT. YOU MUST"
231 PRINT "THEN GUESS WHAT WILLIAM TELL IS"
240 PRINT "AND IS NOT BY TYPING IN NEW":PRINT:INPUT"hit enter";M$:CLS
241 PRINT "WORDS YOU THINK MAY CHARACTERIZE";
250 PRINT "WILLIAM TELL. IF YOU HAVEN'T"
251 PRINT "FIGURED IT OUT AFTER 20 TRIES"
260 PRINT "YOU CAN ASK FOR THE ANSWER OR"
261 PRINT "TRY THE SAME SET AGAIN. HERE IS"
270 PRINT "YOUR FIRST EXAMPLE AND CHANCE"
280 PRINT "TO GUESS:"
290 PRINT:FORX=1TO20
300 READ A$(X),B$(X)
310 NEXT
330 REM THIS LIST OF EXAMPLES MAY BE EXPANDED
340 DATA BRILLANCE,GLORY,GLASSES,SPECTACLES,MELLOW,INTOXICATED
350 DATA DULL,STUPID,COMMODITY,OBJECT,SHEEN,BRITE,UNDRESSED
360 DATA NUDE,MISSION,OBJECTIVE,SPEECH,PROSE,PASSIVE,INERT
370 DATA SUGGESTION,ADVICE,SYMMETRICAL,REGULAR,JITTERY,NERVOUS
380 DATA IMPRESSIVE,MAGNIFICIENT,ERRONEOUS,FAULTY,FEEBLE,LAME
390 DATA CALLOUS,INSENSITIVE,SOOTHING,SERENE,SHALLOW
400 DATA SUPERFICIAL,NAMELESS,ANONYMOUS
410 FOR Y=1 TO 20
420 M$="WILLIAM TELL IS "+A$(Y)+" BUT NOT "+B$(Y):GOSUB1
430 PRINT"WILLIAM TELL IS...":INPUTC$
440 GOSUB730
450 REM HEY MY GOOD CHUM! ARE YOU LOOKING AT MY CODE? (EILRAHC)
460 REM GOD BLESS. LOVE TO YOU, MAD, NAE AND MUM. (CHRIST/CALVIN/LUTHER)
470 IF F=1 THEN 560
480 PRINT"BUT, WILLIAM TELL IS NOT...":INPUTC$
490 GOSUB730
500 IF F=1 THEN M$="WILLIAM TELL IS "+C$+"!!!"
510 GOSUB 790
530 PRINT
540 NEXT Y
550 GOTO 640
560 PRINT"BUT, WILLIAM TELL IS NOT":INPUTC$
570 GOSUB 730
580 IF F=1 AND G=1 THEN M$="WHOOPS, I THOUGHT YOU GOT IT. LET'S TRY SOME MORE...":GOSUB1:G=0:PRINT
590 IF F=0 AND G=1 THEN 630
600 IF F=0 THEN PRINT:M$="BY GOLLY, MAYBE YOU HAVE IT! LET'S TRY ONCE MORE...":GOSUB1:PRINT:G=1
610 IF F=1 THEN M$="WILLIAM TELL IS "+C$+"!!!":GOSUB1:PRINT
620 GOTO 540
630 PRINT"RIGHT!!!!!"
640 M$="WOULD YOU LIKE TO TRY AGAIN (A) OR SEE THE ANSWER (S)...":GOSUB1:INPUTA$
650 IF A$="S" THEN 670
651 GOTO 410
660 REM ENCODED ANSWER FOR WILLIAM TELL
670 PRINT "THE SECRET IS:"
680 A$="UGJJ?K/RCJJ/GQ/?LW/UMPB/AMLR?GLGLE/RUM/AMLQCASRGTC*/GBCLRGA?J/JCRRCPQ,"
690 M$="":FOR X=1 TO LEN(A$)
700 IF MID$(A$,X,1)="/"THEN M$=M$+" ":GOTO710
701 M$=M$+CHR$(ASC(MID$(A$,X,1))+2)
710 NEXT X:GOSUB1
720 END
730 F=0:REM ANSWER EVALUATION
750 FORX=1TOLEN(C$)
760 IF MID$(C$,X,1)=MID$(C$,X+1,1) THENTM=X:X=255:NEXTX:X=TM:F=1:GOTO770
761 NEXT X
770 RETURN
780 REM WRONG ANSWER CHOICES
790 ON RND(5) GOTO 800,810,820,830,840
800 M$="NOPE... GIVE IT ANOTHER TRY:":GOSUB1:RETURN
810 M$="SORRY, NOT EVEN CLOSE.":GOSUB1:RETURN
820 M$="THAT DOES NOT COMPUTE. (IT'S WRONG)":GOSUB1:RETURN
830 M$="YOU'RE STILL COLD... TRY AGAIN:":GOSUB1:RETURN
840 M$="THAT ONE ALMOST BLEW MY CIRCUITS... HERE'S ANOTHER HINT:":GOSUB1:RETURN
900 REM WILLIAM TELL
910 REM 1001 THINGS TO DO
920 REM WITH YOUR TRS-80
930 REM TAB BOOKS 1984
940 REM MC-10 EDITS JIM GERRIE
