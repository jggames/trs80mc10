0 CLS:GOTO3000
1 C=B(0,X,Y):ONM(5)GOSUB15:A=X:B=Y:B(1,X,Y)=C:GOSUB9:X=X+Q:Y=Y+R:C=1:B(1,X,Y)=C:A=X:B=Y:GOSUB9:RETURN
2 M=M+1:GOTO1
3 M=M+2:GOTO1
4 B(1,H(4,P),H(5,P))=B(0,H(4,P),H(5,P)):A=H(4,P):B=H(5,P):C=B(1,H(4,P),H(5,P)):GOSUB9
5 H(4,P)=H(4,P)+Q:H(5,P)=H(5,P)+R:A=H(4,P):B=H(5,P):B(1,A,B)=H(0,P):C=H(0,P):GOSUB9:Q=1:RETURN
6 M=M+1:GOTO4
7 M=M+2:GOTO4
8 Q=0:RETURN
9 PRINT@32*(B-1)+(A-1),CHR$(S(C));:RETURN
10 FORA=1TO4:PRINT@32*(A+C)+16,A$(A)M(A):NEXT:RETURN
11 FORA=5TO8:M(A)=VAL(MID$(B$,A,1)):PRINT@32*(A+C)+16,A$(A);:GOSUB750:NEXT:RETURN
12 FORT=0TO15:PRINT@T*32,B$(T+1);:NEXT:RETURN
13 FORB=-1TO1:FORA=-1TO1:C=B(1,X+A,Y+B):T=32*(8+(B*2))+23+(A*3):PRINT@T,C$(C);:PRINT@T+32,D$(C);:NEXT:NEXT:RETURN
15 T1=T1+1:IFT1>6THENT1=1
16 T(0,T1)=X:T(1,T1)=Y:T2=T2+1:IFT2>6THENT2=1
17 A=T(0,T2):B=T(1,T2):T=B(1,A,B):C=B(0,A,B):IFT=9THENB(1,A,B)=C:GOSUB9
18 C=9:RETURN
20 C=0:GOSUB10:DP=M(3):FORM=1TOM(4)
21 Q=0:R=0:I$=INKEY$:PRINT@16," *MONSTER MOVE* ";:IFI$=""THEN21
22 IFI$="A"THENQ=-1:ONU(B(1,X-1,Y),1)GOSUB1,2,3:GOTO35
23 IFI$="S"THENQ=1:ONU(B(1,X+1,Y),1)GOSUB1,2,3:GOTO35
24 IFI$="W"THENR=-1:ONU(B(1,X,Y-1),1)GOSUB1,2,3:GOTO35
25 IFI$="Z"THENR=1:ONU(B(1,X,Y+1),1)GOSUB1,2,3:GOTO35
26 IFI$="D"THENIFDP>0THENPRINT@16," *DESTRUCTION!* ";:GOSUB200:GOSUB240
27 IFI$="H"THENGOSUB248:PRINT@16,"     *HELP*     ";:GOSUB2050:GOSUB248:GOSUB280:C=0:GOSUB10:GOSUB13
28 IFI$=" "THENONM(5)GOSUB15:GOTO35
29 IFI$="F"THENIFM(7)THENPRINT@16," *BREATHE FIRE* ";:GOSUB200:GOSUB400:ONM(5)GOSUB15:GOTO35
30 IFI$="C"THENIFM(8)THENPRINT@16," *SONIC SCREAM* ";:GOSUB200:GOSUB500:M=M(4):ONM(5)GOSUB15:GOTO35
31 IFI$="X"THENIFM(6)THENPRINT@16,"   *MAKE WEB*   ";:GOSUB200:GOSUB600:M=M+1:ONM(5)GOSUB15:GOTO35
34 GOTO21
35 SOUND10,1:SOUND1,1:GOSUB13:PRINT@148,M(4)-M
36 NEXT:PRINT@148," 0 "
40 PRINT@16,"  *HUMAN MOVE*  ";:CD=8:IFRU<4THENCD=16
50 RU=0:FORP=1TO27:AT=0:ON-(ABS(X-H(4,P))>CD ORABS(Y-H(5,P))>CDORH(1,P)=0)GOTO67:ONH(0,P)-1GOTO60:ONRND(H(0,P)+2)GOTO60
51 FORM=1TOH(3,P):Q=0:R=0
52 IFH(4,P)>XTHENQ=-1:ONU(B(1,H(4,P)-1,H(5,P)),H(0,P))+1GOSUB8,4,6,7:ONQGOTO56:GOTO54
53 IFH(4,P)<XTHENQ=1:ONU(B(1,H(4,P)+1,H(5,P)),H(0,P))+1GOSUB8,4,6,7:ONQGOTO56
54 IFH(5,P)>YTHENR=-1:ONU(B(1,H(4,P),H(5,P)-1),H(0,P))+1GOSUB8,4,6,7:ONQGOTO56
55 IFH(5,P)<YTHENR=1:ONU(B(1,H(4,P),H(5,P)+1),H(0,P))+1GOSUB8,4,6,7
56 NEXT:RU=RU+1:GOTO67
60 FORM=1TOH(3,P):Q=0:R=0:IFRND(8)=1THENQ=2-RND(3):ONU(B(1,H(4,P)+Q,H(5,P)),H(0,P))+1GOSUB8,4,6,7:ONQGOTO66:Q=0
61 IFRND(8)=1THENR=2-RND(3):ONU(B(1,H(4,P),H(5,P)+R),H(0,P))+1GOSUB8,4,6,7:ONQGOTO66:R=0
62 IFH(4,P)>XTHENQ=1:ONU(B(1,H(4,P)+1,H(5,P)),H(0,P))+1GOSUB8,4,6,7:ONQGOTO66:GOTO64
63 IFH(4,P)<XTHENQ=-1:ONU(B(1,H(4,P)-1,H(5,P)),H(0,P))+1GOSUB8,4,6,7:ONQGOTO66
64 IFH(5,P)>YTHENR=1:ONU(B(1,H(4,P),H(5,P)+1),H(0,P))+1GOSUB8,4,6,7:ONQGOTO66
65 IFH(5,P)<YTHENR=-1:ONU(B(1,H(4,P),H(5,P)-1),H(0,P))+1GOSUB8,4,6,7
66 NEXT
67 NEXT:GOSUB13:GOSUB300:SOUND200,2
70 IFM(7)=0THEN90
71 PRINT@16,"  *FIRE PHASE*  ";
72 FORF=1TO20:IFF(0,F)THENT=F:ONRND(5)GOSUB450,451,452,452,453
73 NEXT
90 GOSUB13:IFM(1)>0ANDSC<GLANDU<27THEN20
91 GOSUB248:IFM(1)<1THENPRINT@16,"MONSTER IS DEAD!";
92 IFSC=GLANDM(1)<1THENPRINT@16,"IT'S A TIE!     ";:GOTO94
93 IFSC>=GLTHENPRINT@16,"MONSTER VICTORY!";
94 IFU=27THENPRINT@16,"HUMANITY LOSES! ";
95 GOSUB13:PRINT@32*2+16,A$:PRINT@32*3+16,"PLAYED AT LV"6-LV:PRINT@32*4+16,"SCORE"SC
96 PRINT@32*13+16,"UNITS DESTROYED":PRINT@32*14+22,U:PRINT@32*15+16,"PLAY AGAIN Y/N?";
97 I$=INKEY$:IFI$=""THEN97
98 ON-(I$="N")GOTO99:ON-(I$<>"Y")GOTO97:RUN
99 END
100 IF(AT/DF)<1THENQ=6-INT(1/(AT/DF)):IFQ<1THENQ=1
120 IF(AT/DF)>=1THENQ=4+INT(AT/DF):IFQ>9THENQ=9
130 R=RND(-(PEEK(9)*256+PEEK(10))):R=RND(6):CN=CN+1:PRINT@0,R;CN;:RETURN
200 PRINT@179,"1    2    3";:PRINT@307,"4";:PRINT@32*9+29,"5";:PRINT@403,"6    7    8";
209 I$=INKEY$:IFVAL(I$)<1ORVAL(I$)>8THEN200
210 IFI$="1"THENA=X-1:B=Y-1
211 IFI$="2"THENA=X:B=Y-1
212 IFI$="3"THENA=X+1:B=Y-1
213 IFI$="4"THENA=X-1:B=Y
214 IFI$="5"THENA=X+1:B=Y
215 IFI$="6"THENA=X-1:B=Y+1
216 IFI$="7"THENA=X:B=Y+1
217 IFI$="8"THENA=X+1:B=Y+1
218 PRINT@179,"           ";:PRINT@307," ";:PRINT@317," ";:GOSUB250:GOSUB280:RETURN
220 FORT=1TO27:IFH(4,T)=AANDH(5,T)=BTHENC=T
221 NEXT:RETURN
240 C=B(1,A,B):IFC<12ORC>14THEN470
241 VI=5:DF=3:IFC=12THENDF=2:VI=3
242 MN=1:MX=DP:C=MX
243 PRINT@432,A$(3)"="C;:GOSUB260:IFI$<>CHR$(13)THEN243
244 AT=C:GOSUB100:DP=DP-AT:I$=MID$(BD$(R),Q,1):IFI$="1"THENGOSUB270:GOSUB9:SC=SC+VI:GOSUB280:GOSUB13:SOUND1,1
246 PRINT@120,DP:GOSUB250:GOSUB280:RETURN
248 C=0:GOTO255
250 C=12
255 FORT=C TO15:PRINT@32*T+16,"               ";:POKE32*T+16415,96:NEXT:RETURN
260 I$=INKEY$:IFI$=""THEN260
261 IFVAL(I$)>0THENC=VAL(I$)
262 IFI$=" "THENC=C+1
263 IFC>MXTHENC=MN
264 RETURN
270 C=10:IFB(0,A,B)=14THENC=15
272 B(0,A,B)=C:B(1,A,B)=C:RETURN
280 PRINT@496,"SCORE"SC;:RETURN
300 PRINT@16," *ATTACK PHASE* ";
305 DF=0:FORP=1TO27:ON-(H(1,P)=0)GOTO320:ON-(ABS(X-H(4,P))>H(2,P)ORABS(Y-H(5,P))>H(2,P))GOTO320
310 DF=DF+H(1,P):IF(H(0,P)=4ORH(0,P)=2)AND(B(0,H(4,P),H(5,P))=12ORB(0,H(4,P),H(5,P))=13)THENDF=DF+H(1,P)
320 NEXT:IFDF=0THENRETURN
330 AT=INT((M(1)+M(2))/2+.5):GOSUB100:DM=VAL(MID$(K$(R),Q,1)):IFDM>0THENGOSUB350
340 DM=VAL(MID$(F$(R),Q,1)):IFDMTHENPRINT@464,"HUMANS DESTROYED";:GOTO380
345 GOTO395
350 IFDM=3THENSTOP
351 PRINT@432,"YOUR DAMAGE"DM;:FORT=1TODM:IFRND(2)=1ANDM(3)>0THENM(3)=M(3)-1:NEXT:RETURN
361 IFM(2)>1THENM(2)=M(2)-1:NEXT:RETURN
362 IFM(1)>0THENM(1)=M(1)-1:NEXT:RETURN
363 NEXT:RETURN
380 FORP=1TO27:ON-(H(1,P)=0)GOTO390:ON-(ABS(X-H(4,P))>H(2,P)ORABS(Y-H(5,P))>H(2,P))GOTO390
385 U=U+1:H(1,P)=0:A=H(4,P):B=H(5,P):C=B(0,A,B):B(1,A,B)=C:GOSUB9:GOSUB13:SOUND1,1:SC=SC+H(1,P):GOSUB280:DM=DM-1:IFDM<1THENP=27
390 NEXT
395 GOSUB777:GOSUB250:GOSUB280:RETURN
400 C=B(1,A,B):IFC<11ORC>14THEN470
401 IFRND(2)=1THENRETURN
402 FI=FI+1:IFFI>20THENFI=1
403 IFF(0,FI)THENT=FI:GOSUB450
404 F(0,FI)=A:F(1,FI)=B:C=8:GOSUB9:B(1,A,B)=C:GOSUB13:RETURN
450 B(1,F(0,T),F(1,T))=B(0,F(0,T),F(1,T)):GOSUB460:F(0,T)=0:RETURN
451 B(1,F(0,T),F(1,T))=10:B(0,F(0,T),F(1,T))=10:GOSUB460:F(0,T)=0:SC=SC+3:GOSUB280:RETURN
452 RETURN
453 PRINT@464,"FIRE SPREADING!";
454 A=F(0,F)-1:B=F(1,F):C=B(1,A,B):IFC>10ANDC<14THENONRND(2)GOSUB402
455 A=F(0,F)+1:B=F(1,F):C=B(1,A,B):IFC>10ANDC<14THENONRND(2)GOSUB402
456 A=F(0,F):B=F(1,F)-1:C=B(1,A,B):IFC>10ANDC<14THENONRND(2)GOSUB402
457 A=F(0,F):B=F(1,F)+1:C=B(1,A,B):IFC>10ANDC<14THENONRND(2)GOSUB402
458 RETURN
460 PRINT@32*(F(1,T)-1)+(F(0,T)-1),CHR$(S(B(1,F(0,T),F(1,T))));:RETURN
470 PRINT@464,"CAN'T";:GOTO595
500 C=B(1,A,B):IFC<2ORC>7ORM>1THEN470
505 T=0:DF=0:FORP=1TO27:IFH(4,P)=AANDH(5,P)=BANDH(1,P)>0THEN510
506 GOTO520
510 DF=H(1,P):IF(H(0,P)=4ORH(0,P)=2)AND(B(0,H(4,P),H(5,P))=12ORB(0,H(4,P),H(5,P))=13)THENDF=DF+H(1,P)
515 T=P:P=27
520 NEXT:P=T:IFDF=0THENSTOP
530 AT=INT((M(1)+M(2))/2+.5):GOSUB100:DM=VAL(MID$(K$(R),Q,1)):IFDMTHEN550
540 DM=VAL(MID$(F$(R),Q,1)):IFDMTHENPRINT@464,"HUMAN DESTROYED";:GOTO580
550 PRINT@464,"MISSED HUMAN";:GOTO595
580 U=U+1:SC=SC+H(1,P):GOSUB280:H(1,P)=0:A=H(4,P):B=H(5,P):C=B(0,A,B):B(1,A,B)=C:GOSUB9:GOSUB13:SOUND1,1
595 GOSUB777:GOSUB250:GOSUB280:RETURN
600 C=B(1,A,B):IFC<10ORC>16ORM>3THEN470
610 C=9:GOSUB9:B(1,A,B)=C:B(0,A,B)=C:GOSUB13:RETURN
750 IFM(A)=1THENPRINT" *":RETURN
751 RETURN
760 FORA=1TO4:M(A)=VAL(MID$(B$,A,1)):NEXT:FORA=1TO3:M(A)=M(A)+LV+4:NEXT:RETURN
777 FORZZ=1TO2000:NEXT:RETURN
800 A$(1)="DEFENCE":A$(2)="ATTACK":A$(3)="DESTRUCT":A$(4)="MOVE":A$(5)="RADIOACTIVE"
801 A$(6)="WEB":A$(7)="BREATHE FIRE":A$(8)="SONIC SCREAM"
804 RESTORE:B=0
805 B=B+1:IFB=9THEN804
810 READA$,B$:CLS:C=2:GOSUB900:PRINTA$:PRINT@22,C$(1);:PRINT@54,D$(1):GOSUB760:GOSUB10:GOSUB11
860 PRINT@368,"FLY ";:IFMID$(B$,18,1)="1"THENPRINT" *"
862 PRINT@400,"FIREPROOF";:IFMID$(B$,16,1)="1"THENPRINT" *"
875 PRINT@480,"PLAY THIS ONE Y/N?";
880 I$=INKEY$:IFI$=""THEN880
882 IFI$="N"THEN805
884 IFI$="Y"THEN890
886 GOTO880
890 CLS:PRINT"PLEASE WAIT...":FORA=1TO16:U(A,1)=VAL(MID$(B$,8+A,1)):NEXT:IFB<8THENFORT=B+1TO8:READI$,B$:NEXT
895 FORY=2TO7:FORX=1TO16:READU(X,Y):NEXT:NEXT
897 FORY=1TO6:FORX=1TO9:READA(X,Y):NEXT:NEXT:RETURN
900 ONBGOTO911,912,913,914,915,916,917,918
911 C$(1)="íêå":D$(1)="åîï":RETURN
912 C$(1)="æïé":D$(1)="îîî":RETURN
913 C$(1)="çïë":D$(1)="êïå":RETURN
914 C$(1)="åìê":D$(1)="îïí":RETURN
915 C$(1)="íåí":D$(1)="åçå":RETURN
916 C$(1)="çïå":D$(1)="áïì":RETURN
917 C$(1)="íãî":D$(1)="áïâ":RETURN
918 C$(1)="æîë":D$(1)="íçî":RETURN
1010 B$(1)="ß€ß¯¯ßßßßß€ß€ß€ß"
1011 B$(2)="ß€ß¯¯€ß€Ï€ß"
1012 B$(3)="€€€€€¬¬€€€€€€€€€"
1013 B$(4)="ß€ß€ß¯€Ï€Ï€ß"
1014 B$(5)="ß€ß€ß¯¯€ß€ß€ß"
1015 B$(6)="€€€€€€€¯¯¯¯¯€€€Ï"
1016 B$(7)="ß€ß€ÏÏ€€€¯¯€Ï€ß"
1017 B$(8)="ß€ß€ÏÏ€Ï€¬€¬€€€€"
1018 B$(9)="€€€€€€€€€¯¯€ß"
1019 B$(10)="ßßß€ÏÏ€Ï€¯¯¯¯€ß"
1020 B$(11)="ßÏÏ€ÏÏ€Ï€ß¯¯¯¯"
1021 B$(12)="€€€€€€€€€ß"
1022 B$(13)="ß€Ï€ÏÏ€ßßß"
1023 B$(14)="ß€€€ÏÏ€€€ß"
1024 B$(15)="ß€ßßÏßßÏ€€€€€€€€"
1025 B$(16)="€€€€€€€€€ßßß€ß€ß"
1100 C$(0)="€€€":C$(14)="¯¯¯":C$(15)="¯¯¯":C$(9)="ÆÆÆ":C$(8)=C$(0):C$(10)=C$(0):C$(13)="ÇÁ€":C$(2)="À€€":C$(12)="ÐÓÐ"
1111 D$(0)="€€€":D$(14)="¨£¤":D$(15)="¯¯¯":D$(9)="ÆÆÆ":D$(8)="¶¶²":D$(10)="ööò":D$(13)="ÏÏÏ":D$(2)="”š":D$(12)="ÕßÚ"
1114 C$(3)="±£°":C$(7)="ÝØÒ":C$(11)="…€":C$(6)="Ãƒ€":C$(4)=C$(0):C$(5)=C$(0):C$(16)=C$(0)
1115 D$(3)="­­¨": D$(7)="ÝÜØ":D$(11)="€úƒ":D$(6)="Ž":D$(4)="„Š":D$(5)="ŒŽ‚":D$(16)=C$(0)
1200 K$(1)="333211111"
1210 K$(2)="322111100"
1220 K$(3)="221111000"
1230 K$(4)="211100000"
1240 K$(5)="111000000"
1250 K$(6)="110000000"
1260 F$(1)="000000011"
1270 F$(2)="000000111"
1280 F$(3)="000001112"
1290 F$(4)="000111122"
1300 F$(5)="001111223"
1310 F$(6)="111222333"
1400 BD$(1)="000000001"
1410 BD$(2)="000000011"
1420 BD$(3)="000000111"
1430 BD$(4)="000001111"
1440 BD$(5)="000011111"
1450 BD$(6)="000111111"
1500 FORT=1TO16:READU$(T),S(T):NEXT:FORY=1TO16:FORX=1TO16:A=ASC(MID$(B$(Y),X,1)):GOSUB2000:B(0,X,Y)=B:B(1,X,Y)=B:NEXT:NEXT
1600 FORT=1TO27:READH(0,T),H(1,T),H(2,T),H(3,T),H(4,T),H(5,T):NEXT
1999 RETURN
2000 B=0:FORT=1TO16:IFA=S(T)THENB=T
2010 NEXT:RETURN
2050 PRINT@32*1+16," w":PRINT@32*2+16,"a s MOVE MONSTER";:PRINT@32*3+16," z":PRINT@32*5+16," d DESTROY STUFF";
2051 PRINT@32*6+16," f BREATHE FIRE":PRINT@32*7+16," x MAKE WEB":PRINT@32*8+16," c SONIC SCREAM"
2052 PRINT@32*10+16,"space NO MOVE OR";:PRINT@32*11+16,"      CHANGE #S":PRINT@32*12+16,"enter ACCEPT #"
2053 PRINT@32*14+16,"FOR SYMBOL LIST":PRINT@32*15+16,"HIT ANY KEY";:GOSUB2060
2054 GOSUB248:FORT=0TO15:PRINT@T*32+16,CHR$(S(T+1))" "U$(T+1);:NEXT
2060 I$=INKEY$:IFI$=""THEN2060
2070 RETURN
3000 DIMB$(16),B(1,17,17),U$(16),S(16),H(5,27),U(16,7),M(8),T,A,B,C,A$(8),A(9,6),C$(16),D$(16),K$(6),F$(6),T(1,6),F(1,20)
3010 DIMBD$(6)
3020 PRINT@32*3+1,"THE MONSTER THAT ATE SHEBOYGAN":PRINT@32*5+8,"WREAK HAVOC WITH          THE CREATURE OF YOUR CHOICE"
3030 PRINT@32*10+2,"GAME DESIGN: GREG COSTIKYAN"
3040 PRINT@32*11+3,"MC-10 VERSION: JIM GERRIE"
3050 PRINT@32*15+1,"DIFFICULTY (1 EASY - 5 HARD)?";
3110 I$=INKEY$:C=VAL(I$):R=RND(0):IFC<1ORC>5THEN3110
3120 CLS:LV=6-C:GL=105-(LV*10):GOSUB777:GOSUB800:GOSUB1010:GOSUB12:FORT=0TO15:PRINT@T*32,B$(T+1);:NEXT
3130 FORT=1TO27:IFH(4,T)THEN3133
3131 X=RND(16):Y=RND(16):IFB(1,X,Y)<>16THEN3131
3132 H(4,T)=X:H(5,T)=Y
3133 B(1,H(4,T),H(5,T))=H(0,T):PRINT@(H(5,T)-1)*32+H(4,T)-1,CHR$(S(B(1,H(4,T),H(5,T))));:NEXT
3135 ONRND(2)GOTO3140,3141
3140 X=1:ONRND(2)GOTO3145:X=16:GOTO3145
3141 Y=1:ONRND(2)GOTO3147:Y=16:GOTO3147
3145 Y=RND(16):IFB(1,X,Y)<>16THEN3145
3146 GOTO3150
3147 X=RND(16):IFB(1,X,Y)<>16THEN3147
3150 B(1,X,Y)=1:PRINT@(Y-1)*32+X-1,CHR$(S(B(1,X,Y)));
3152 GOSUB13:T1=0:T2=3:FORT=1TO6:T(0,T)=X:T(1,T)=Y:NEXT:GOTO20
9310 DATABRONTO,"576300010000000012200111"
9311 DATASQUIDRA,"456310000000000012200111"
9312 DATAARACHNIS,"344401000000000012211111"
9313 DATAMECHAZOID,"342300110000000112200111"
9314 DATACATERZILLA,"443410000000000012211111"
9315 DATAGOSHILLA,"464300100000000012200111"
9316 DATAMOTHRA,"542600000000000111111111"
9317 DATATHE BLOB,"574210000000000012200111"
9401 DATA0,,,,,,,,,2,2,2,3,1,,1
9402 DATA0,,,,,,,,,0,2,,,1,,1
9403 DATA0,,,,,,,,,2,2,,,1,1,1
9404 DATA0,,,,,,,,,2,2,,,1,,1
9405 DATA0,,,,,,,,,2,2,,,1,,1
9406 DATA0,,,,,,,1,1,1,1,1,1,1,1,1
9410 DATA2,2,1,1,,3,4,4,5
9411 DATA2,1,1,,3,4,4,5,5
9412 DATA1,1,,3,4,4,5,5,5
9413 DATA1,,3,4,4,5,5,5,5
9414 DATA0,3,4,4,5,5,5,5,5
9415 DATA3,4,4,5,5,5,5,3,5
9500 DATAMONSTER,109,CROWD,99,POLICE,112,NAT. GUARD,110,ARTILLERY,97,TANK,116,HELICOPTER,104,FIRE,191
9510 DATAWEB/TRAIL,120,RUBBLE,255,PARK,143,LOW BUILDING,223,HIGH BUILDING,207,BRIDGE,172,WATER,175,ROAD,128
9601 DATA2,1,1,1,3,2
9602 DATA2,1,1,1,14,4
9603 DATA2,1,1,1,8,8
9604 DATA2,1,1,1,1,8
9605 DATA2,1,1,1,6,11
9606 DATA2,1,1,1,3,10
9607 DATA2,1,1,1,13,13
9608 DATA2,1,1,1,10,16
9609 DATA3,1,1,3,,
9610 DATA3,1,1,3,,
9611 DATA3,1,1,3,,
9612 DATA3,1,1,3,,
9613 DATA3,1,1,3,,
9614 DATA3,1,1,3,,
9615 DATA4,3,1,3,,
9616 DATA4,3,1,3,,
9617 DATA4,3,1,3,,
9618 DATA4,3,1,3,,
9619 DATA4,3,1,3,,
9620 DATA4,3,1,3,,
9621 DATA5,5,6,2,,
9622 DATA5,5,6,2,,
9623 DATA6,6,2,5,,
9624 DATA6,6,2,5,,
9625 DATA6,6,2,5,,
9626 DATA7,1,2,7,,
9627 DATA7,1,2,7,,
