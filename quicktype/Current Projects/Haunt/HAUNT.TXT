0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO70
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 REM HAUNTED HOUSE ADVENTURE
20 REM ***********************
30 REM THIS VERSION FOR "MICROSOFT" BASIC
40 REM REQUIRES A MINIMUM OF 16K
50 REM SELECT "TEXT MODE" IF NECESSARY
60 REM ***********************
70 V=25: W=36: G=18
73 DIM R$(63), D$(63), O$(W), V$(V)
77 DIM C(W), L(G), F(W)
80 GOSUB1600
85 REM DESCRIPTION AND FEEDBACK
90 CLS:PRINT"HAUNTED HOUSE"
100 PRINT"-------------"
110 PRINT"YOUR LOCATION"
120 M$=D$(RM):GOSUB1
130 REM PRINT LOC
140 M$="EXITS: ":FORI=1TOLEN(R$(RM))
150 M$=M$+MID$(R$(RM),I,1)+", "
160 NEXTI:M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
170 PRINT
180 FOR I=1 TO G
190 IF L(I)=RM AND F(I)=0 THEN M$="YOU CAN SEE "+O$(I)+" HERE":GOSUB1
200 NEXT I
210 PRINT "================================";
220 M$=Z$:GOSUB1:Z$="WHAT"
225 REM INPUT AND INPUT ANALYSIS
230 INPUT"WHAT WILL YOU DO NOW";Q$
240 V$="": W$="": VB=0: OB=0
250 FORI=1TOLEN(Q$)
260 IFMID$(Q$,I,1)=" "ANDV$=""THENV$=LEFT$(Q$,I­1)
270 IFMID$(Q$,I+1,1)<>" "ANDV$<>""THENW$=MID$(Q$,I+1,LEN(Q$)­1):I=LEN(Q$)
280 NEXT I
290 IFW$=""THENV$=Q$
300 FORI=1TOV
310 IFV$=V$(I)THENVB=I
320 NEXTI
330 FORI=1TOW
340 IFW$=O$(I)THENOB=I
350 NEXTI
355 REM ERROR MESSAGES OVERRIDE CONDITIONS
360 IFW$>"" AND OB=0 THENZ$="THAT'S SILLY"
370 IFVB=0 THEN VB=V+1
380 IFW$="" THENZ$="I NEED TWO WORDS"
390 IFVB>V AND OB>0 THENZ$="YOU CAN'T '"+Q$+"'"
400 IFVB>V AND OB=0 THENZ$="YOU DON'T MAKE SENSE!"
410 IFVB<V AND OB>0 AND C(OB)=0 THENZ$="YOU DON'T HAVE '"+W$
420 IFF(26)=1 AND RM=13 AND INT(RND(1)*4)<>3ANDVB<>21THENZ$="BATS ATTACKING!":GOTO90
430 IFRM=44 AND INT(RND(1)*3)=1 AND F(24)<>1THEN F(27)=1
440 IFF(0)=1 THEN LL=LL-1
450 IFLL<1 THEN F(0)=0
455 REM BRANCH TO SUBROUTINES
460 ONVBGOSUB500,570,640,640,640,640,640,640,640,980,980,1030,1070,1140,1180,1220,1250,1300,1340,1380,1400,1430,1460,1490,1510,1590
470 IF LL=10 THENZ$="YOUR CANDLE IS WANING!"
480 IF LL=1 THENZ$="YOUR CANDLE IS OUT!"
490 GOTO90
495 REM VERB 1
500 M$="WORDS I KNOW: "
510 FORI=1TOV
520 M$=M$+V$(I)+", "
530 NEXTI:M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1
540 Z$="":PRINT
550 GOSUB1580
560 RETURN
565 REM VERB 2
570 M$="YOU ARE CARRYING: "
580 FORI=1TOG
590 IFC(I)=1THENM$=M$+O$(I)+", "
600 NEXTI:M$=LEFT(M$,LEN(M$)-2)+".":GOSUB1
610 Z$="":PRINT
620 GOSUB1580
630 RETURN
635 REM VERBS 3 TO 9 INCLUSIVE
640 D=0
650 IFOB=0THEND=VB-3
660 IFOB=19THEND=1
670 IFOB=20THEND=2
680 IFOB=21THEND=3
690 IFOB=22THEND=4
700 IFOB=23THEND=5
710 IFOB=24THEND=6
720 IFRM=20ANDD=5THEND=1
730 IFRM=20ANDD=6THEND=3
740 IFRM=22ANDD=6THEND=2
750 IFRM=22ANDD=5THEND=3
760 IFRM=36ANDD=6THEND=1
770 IFRM=36ANDD=5THEND=2
780 IFF(14)=1THENZ$="CRASH! YOU FELL OUT OF THE TREE!": F(14)=0: RETURN
790 IFF(27)=1AND RM=52 THENZ$="GHOSTS WILL NOT LET YOU MOVE": RETURN
800 IFRM=45 AND C(1)=1 AND F(34)=0 THENZ$="A MAGICAL BARRIER TO THE WEST":RETURN
810 IF(RM=26 AND F(0)=0) AND (D=1 OR D=4) THENZ$="YOU NEED A LIGHT": RETURN
820 IFRM=54 AND C(15)<>1 THENZ$="YOU'RE STUCK!": RETURN
830 IFC(15)=1ANDNOT(RM=53ORRM=54ORRM=55ORRM=47)THENZ$="YOU CAN'T CARRY A BOAT!":RETURN
840 IF(RM>26 AND RM<30) AND F(0)=0 THENZ$="TOO DARK TO MOVE": RETURN
850 F(35)=0:RL=LEN(R$(RM))
860 FORI=1TORL
870 U$=MID$(R$(RM), I, 1)
880 IF(U$="N"AND D=1ANDF(35)=0)THENRM=RM-8: F(35)=1
890 IF(U$="S"AND D=2ANDF(35)=0)THENRM=RM+8: F(35)=1
900 IF(U$="W"AND D=3ANDF(35)=0)THENRM=RM-1: F(35)=1
910 IF(U$="E"AND D=4ANDF(35)=0)THENRM=RM+1: F(35)=1
920 NEXT I
930Z$="OK"
940 IFF(35)=0 THENZ$="CAN'T GO THAT WAY!"
950 IFD<1THENZ$="GO WHERE?"
960 IFRM=41ANDF(23)=1THENR$="SW":Z$="THE DOOR SLAMS SHUT!":F(23)=0
970 RETURN
975 REM VERBS 10 AND 11
980 IFOB>G THENZ$="I CAN'T GET "+W$: RETURN
985 IFL(OB)<>RM THENZ$="IT ISN'T HERE"
990 IFF(OB)<>0 THENZ$="WHAT "+W$+"?"
1000 IFC(OB)=1 THENZ$="YOU ALREADY HAVE IT"
1010 IFOB>0 AND L(OB)=RM AND F(OB)=0 THEN C(OB)=1: L(OB)=65:Z$="YOU HAVE THE "+W$
1020 RETURN
1025 REM VERB 12
1030 IFRM=43 AND (OB=28 OR OB=29) THEN F(17)=0:Z$="DRAWER OPEN"
1040 IFRM=28 AND OB=25 THENZ$="IT'S LOCKED"
1050 IFRM=38 AND OB=32 THENZ$="THAT'S CREEPY!": F(2)=0
1060 RETURN
1065 REM VERB 13
1070 IFOB=30 THEN F(18)=0:Z$="SOMETHING HERE!"
1080 IFOB=31 THENZ$="THAT'S DISGUSTING!"
1090 IF(OB=28 OR OB=29) THENZ$="THERE'S A DRAWER"
1100 IFOB=33 OR OB=5 THEN GOSUB1140
1110 IFRM=43 AND OB=35 THENZ$="THERE'S SOMETHING BEYOND..."
1120 IFOB=32 THEN GOSUB1030
1130 RETURN
1135 REM VERB 14
1140 IFRM=42 AND OB=33 THENZ$="THEY ARE DEMONIC WORKS"
1150 IF(OB=3 OR OB=36) AND C(3)=1 AND F(34)=0 THENZ$="USE THIS WORD WITH CARE 'XZANFAR'"
1160 IFC(5)=1 AND OB=5 THENZ$="THE SCRIPT IS IN AN ALIEN TONGUE"
1170 RETURN
1175 REM VERB 15
1180 Z$="OK '"+W$+"'"
1190 IFC(3)=1ANDOB=34THENZ$="*MAGIC OCCURS*":IFRM<>45THENRM=INT(RND(1)*64)
1200 IFC(3)=1ANDOB=34ANDRM=45THEN F(34)=1
1210 RETURN
1215 REM VERB 16
1220 IFC(12)=1THENZ$="YOU MADE A HOLE"
1230 IFC(12)=1ANDRM=30THENZ$="DUG THE BARS OUT":D$(RM)="HOLE IN THE WALL":R$(RM)="NSE"
1240 RETURN
1245 REM VERB 17
1250 IFC(14)<>1 AND RM=7THENZ$="THIS IS NO TIME TO PLAY GAMES"
1260 IFOB=14 AND C(14)=1THENZ$="YOU SWUNG IT"
1270 IFOB=13 AND C(13)=1THENZ$="WHOOSH"
1280 IFOB=13ANDC(13)=1ANDRM=43THENR$(RM)="WN":D$(RM)="STUDY WITH A SECRET ROOM":Z$="YOU BROKE THE THIN WALL"
1290 RETURN
1295 REM VERB 18
1300 IFOB=14ANDC(14)=1THENZ$="IT ISN'T ATTACHED TO ANYTHING!"
1310 IFOB=14ANDC(14)<>1ANDRM=7ANDF(14)=0 THENZ$="YOU SEE THICK FORREST AND CLIFF SOUTH":F(14)=1:RETURN
1320 IFOB=14ANDC(14)<>1ANDRM=7ANDF(14)=1 THENZ$="GOING DOWN!": F(14)=0
1330 RETURN
1335 REM VERB 19
1340 IFOB=17ANDC(17)=1ANDC(8)=0THENZ$="IT WILL BURN YOUR HANDS"
1350 IFOB=17ANDC(17)=1ANDC(9)=0THENZ$="NOTHING TO LIGHT IT WITH"
1360 IFOB=17ANDC(17)=1ANDC(9)=1AND C(8)=1 THENZ$= "IT CASTS A FLICKERING LIGHT":F(0)=1
1370 RETURN
1375 REM VERB 20
1380 IF F(0)=1 THEN F(0)=0:Z$="EXTINGUISHED"
1390 RETURN
1395 REM VERB 21
1400 IF OB=16 AND C(16)=1 THENZ$="HISSSS"
1410 IF OB=16 AND C(16)=1 AND F(26)=1 THEN F(26)=0:Z$="PFFT! GOT THEM"
1420 RETURN
1425 REM VERB 22
1430 IFOB=10ANDC(10)=1ANDC(11)=1 THENZ$="SWITCHED ON": F(24)=1
1440 IFF(27)=1ANDF(24)=1THENZ$="WHIZZ-VACUUMED THE GHOSTS UP!":F(27)=0
1450 RETURN
1455 REM VERB 23
1460 IFRM=43AND(OB=27OROB=28)THEN GOSUB1030
1470 IFRM=28ANDOB=25ANDF(25)=0ANDC(18)=1THENF(25)=1:R$(RM)="SEW":D$(RM)="HUGE OPEN DOOR":Z$="THE KEY TURNS"
1480 RETURN
1485 REM VERB 24
1490 IFC(OB)=1THENC(OB)=0:L(OB)=RM:Z$="DONE"
1500 RETURN
1505 REM VERB 25
1510 S=0
1520 FORI=1TOG
1530 IFC(I)=1THENS=S+1
1540 NEXTI
1550 IFS=17ANDC(15)<>1ANDRM<>57THENMM$="YOU HAVE EVERYTHING. RETURN TO THE GATE FOR FINAL SCORE.":GOSUB1
1560 IFS=17ANDRM=57THENM$="DOUBLE SCORE FOR REACHING HERE!":GOSUB1:S=S*2
1570 PRINT"YOUR SCORE="; S: IF S>18 THENM$="WELL DONE! YOU HAVE FINISHED THE GAME.":GOSUB1:END
1580 INPUT"PRESS enter TO CONTINUE";Q$
1590 RETURN
1595 REM GAME INITIALISATION ROUTINE
1600 REM DIM R$(63), D$(63), O$(W), V$(V)
1610 REM DIM C(W), L(G), F(W)
1620 DATA 46,38,35,50,13,18,28,42,10,25,26,4,2,7,47,60,43,32
1630 FOR I=1 TO G
1640 READ L(I)
1650 NEXT I
1660 DATA HELP,CARRYING?,GO,N,S,W,E,U,D,GET,TAKE,OPEN,EXAMINE,READ,SAY
1665 DATA DIG,SWING,CLIMB,LIGHT,UNLIGHT,SPRAY,USE,UNLOCK,LEAVE,SCORE
1680 FORI=1TOV
1690 READV$(I)
1700 NEXTI
1705 REM movements
1710 DATA SE,WE,WE,SWE,WE,WE,SWE,WS
1720 DATA NS,SE,WE,NW,SE,W,NE,NSW
1730 DATA NS,NS,SE,WE,NWUD,SE,WSUD,NS
1740 DATA N,NS,NSE,WE,WE,NSW,NS,NS
1750 DATA S,NSE,NSW,S,NSUD,N,N,NS
1760 DATA NE,NW,NE,W,NSE,WE,W,NS
1770 DATA SE,NSW,E,WE,NW,S,SW,NW
1780 DATA NE,NWE,WE,WE,WE,NWE,NWE,W
1790 FOR I=0 TO 63
1800 READ R$(I)
1810 NEXT I
1815 REM descriptions
1820 DATA DARK CORNER,OVERGROWN GARDEN,BY LARGE WOODPILE,YARD BY RUBBISH
1830 DATA WEEDPATCH,FOREST,THICK FOREST,BLASTED TREE
1840 DATA CORNER OF HOUSE,ENTRANCE TO KITCHEN,KITCHEN AND GRIMEY COOKER,SCULLERY DOOR
1845 DATA ROOM WITH INCHES OF DUST,REAR TURRET ROOM,CLEARING BY HOUSE,PATH
1860 DATA SIDE OF HOUSE,BACK OF HALLWAY,DARK ALCOVE,SMALL DARK ROOM
1865 DATA BOTTOM OF SPIRAL STAIRCASE,WIDE PASSAGE,SLIPPERY STEPS,CLIFFTOP
1880 DATA NEAR CRUMBLING WALL,GLOOMY PASSAGE,POOL OF LIGHT,IMPRESSIVE VAULTED HALLWAY
1885 DATA HALL BY THICK WOODEN DOOR,TROPHY ROOM,CELLAR WITH BARRED WINDOW,CLIFF PATH
1900 DATA CUPBOARD WITH HANGING COAT,FRONT HALL,SITTING ROOM,SECRET ROOM
1905 DATA STEEP MARBLE STAIRS,DINING ROOM,DEEP CELLAR WITH COFFIN,CLIFF PATH
1920 DATA CLOSET,FRONT LOBBY,LIBRARY OF EVIL BOOKS,STUDY WITH DESK AND HOLE IN WALL
1925 DATA WEIRD COBWEBBY ROOM,VERY COLD CHAMBER,SPOOKY ROOM,CLIFF PATH BY MARSH
1940 DATA RUBBLE­STREWN VERANDAH,FRONT PORCH,FRONT TOWER,SLOPING CORRIDOR
1945 DATA UPPER GALLERY,MARSH BY WALL,MARSH,SOGGY PATH
1960 DATA BY TWISTED RAILING, PATH THROUGH IRON GATE,BY RAILINGS,BENEATH FRONT TOWER
1965 DATA DEBRIS FROM CRUMBLING FACADE,LARGE FALLEN BRICKWORK,ROTTING STONE ARCH,CRUMBLING CLIFFTOP
1980 FOR I=0 TO 63
1990 READ D$(I)
2000 NEXT I
2010 DATA PAINTING,RING,MAGIC SPELLS,GOBLET,SCROLL,COINS,STATUE,CANDLESTICK
2012 DATA MATCHES,VACUUM,BATTERIES,SHOVEL,AXE,ROPE,BOAT,AEROSOL,CANDLE,KEY
2014 DATA NORTH,SOUTH,WEST,EAST,UP,DOWN
2016 DATA DOOR,BATS,GHOSTS,DRAWER,DESK,COAT,RUBBISH
2018 DATA COFFIN,BOOKS,XZANFAR,WALL,SPELLS
2060 FOR I=1 TO W
2070 READ O$(I)
2080 NEXT I
2090 F(18)=1:F(17)=1:F(2)=1:F(26)=1:F(28)=1:F(23)=1:LL=60:RM=57:M$="OK"
2100 RETURN
