0 GOSUB2000:FORCC=1TO3000:NEXT:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 N3=N1-(N2*INT(N1/N2)):RETURN
5 PRINT@484,"PRESS space TO CONTINUE.";:GOSUB1480:CLS:RETURN

10 CLS0:POKE49151,64:CLEAR2000:DIMCC,ZZ,M$,I$
20 CLS:DIMP$(9,10),M$(5,6),CL$(13),CG$(13),F$(4),MP$(2),MW$(2)
40 S$="COPYRIGHT 1987 COMPUTE!":GOSUB1460:S$="ALL RIGHTS RESERVED":GOSUB 1460:PRINT
50 PRINT:S$="PRESENTING":GOSUB1460:PRINT:S$="+---------------+":GOSUB1460:S$="! MYSTERY MANIA !"
51 GOSUB1460:S$="+---------------+":GOSUB1460
60 PRINT:S$="THE GAME THAT GENERATES MURDER MYSTERY LOGIC PROBLEMS FOR YOU TO SOLVE":GOSUB1360:GOSUB5:CLS0:POKE49151,0:CLS
70 CLS:S$="THIS GAME CAN GENERATE 32001 DIFFERENT MYSTERIES, NUMBERED 0 TO 32000.":GOSUB1360
71 S$="YOU CAN PLAY THE SAME GAME REPEATEDLY BY CHOOSING THE SAME NUMBER.":GOSUB1360
72 S$="OR YOU CAN CHOOSE A DIFFERENT MYSTERY EACH TIME BY SELECTING A RANDOM number.":GOSUB1360
80 PRINT:PRINT"WHICH MYSTERY STORY (0-32000)":INPUTM:IFM<0ORM>32000THENPRINT"INVALID ANSWER!":GOTO80
90 CC=RND(-ABS(M))
100 N1=M:N2=99:GOSUB3:FORX=0TON3:Y=RND(0):NEXTX
110 CLS:S$="THERE ARE FIVE DIFFICULTY LEVELS.":GOSUB1360:PRINT
111 S$="LEVEL 1 IS EASY, WHILE LEVEL FIVE WOULD EVEN STUMP SHERLOCK HOLMES.":GOSUB1360
120 PRINT:PRINT"DIFFICULTY level (1-5)";
130 S$=INKEY$:IFS$<"1"ORS$>"5"THEN130
140 PRINTS$:L=VAL(S$):ONLGOTO141,142,143,144,145
141 AW$=" MEMBER":GOTO150
142 AW$="N OFFICER":GOTO150
143 AW$=" COMMANDER":GOTO150
144 AW$=" COMPANION":GOTO150
145 AW$=" KNIGHT"
150 PRINT:PRINT"DO YOU WANT HARD COPY OF THE":PRINT"STORY (y/n)?";:GOSUB1430:PR=YN:PRINTY$
160 CLS
170 REM READ IN DATA
180 FORX=0TO9:FORY=0TO10:READP$(X,Y):NEXTY:NEXTX
190 FORX=0TO2:READMP$(X):NEXTX:FORX=0TO2:READMW$(X):NEXTX
200 REM GENERATE SUSPECT NAMES
210 A=INT(RND(0)*11):B=INT(RND(0)*11):V$=P$(0,A)+" "+P$(1,B):P$(0,A)="":P$(1,B)=""
220 FORX=1TO5
230 A=INT(RND(0)*11):A$=P$(0,A):P$(0,A)="":IFA$=""THEN230
240 B=INT(RND(0)*11):B$=P$(1,B):P$(1,B)="":IFB$=""THEN240
250 M$(0,X)=A$+" "+B$
260 NEXTX
270 FORX=0TO4:F$(X)=M$(0,X+1):NEXTX
280 FORX=0TO9:A=INT(RND(0)*5):B=INT(RND(0)*5):M$=F$(A):F$(A)=F$(B):F$(B)=M$:NEXTX
290 S$="SIX MEN, "+V$+", "+F$(0)+", "+F$(1)+", "+F$(2)+", "+F$(3)+", AND "+F$(4)+" WERE IN "+MP$(INT(RND(0)*3))+" TOGETHER."
291 GOSUB1360:GOSUB5:S$="SUDDENLY, THE LIGHTS WENT OUT.":GOSUB1360
300 S$="WHEN THE LIGHTS CAME BACK ON, "+V$+" WAS FOUND "+MW$(INT(RND(0)*3))+".":GOSUB1360
310 PRINT:S$="THE DETECTIVES OF SCOTLAND YARD HAVE INVESTIGATED.":GOSUB1360
311 S$="THEY HAVE QUESTIONED THE SUSPECTS, THE WITNESSES, AND PEOPLE WHO KNOW THE SUSPECTS.":GOSUB1360
312 S$="THEY HAVE COLLECTED PHYSICAL EVIDENCE (HAIR SAMPLES, FIBER SAMPLES, ETC.) FROM THE CRIME SCENE.":GOSUB1360
320 REM GENERATE SUSPECT DATA
330 FORP=1TO5
340 A=INT(RND(0)*8+2):IFP$(A,0)=""THEN340
350 M$(P,0)=P$(A,0):P$(A,0)=""
360 FORY=1TO5
370 B=INT(RND(0)*9)+1:IFP$(A,B)=""THEN370
380 M$(P,Y)=P$(A,B):P$(A,B)=""
390 NEXTY
400 M$(P,6)=P$(A,10)
410 NEXTP
420 GOSUB5:S$="THEY HAVE COLLECTED 14 CLUES, BUT HAVE NOT BEEN ABLE TO SOLVE THE CRIME.":GOSUB1360
421 S$="THEREFORE, THEY HAVE CALLED IN THE CITY'S GREATEST CONSULTING DETECTIVE.":GOSUB1360
422 S$="THAT'S you! YOU WILL NOW BE GIVEN THE CLUES, AND MUST SOLVE THE MURDER.":GOSUB1360
430 REM GENERATE CLUES
440 K$=M$(0,6-L+(L=3)-(L=4)):C$=M$(4,0)+" "+M$(4,6-L+(L=3)-(L=4))
450 CL$(0)=M$(0,1)+" "+M$(2,0)+" "+M$(2,1)
460 CL$(1)=M$(0,2)+" "+M$(5,0)+" "+M$(5,2)
470 CL$(2)=M$(0,3)+" "+M$(1,0)+" "+M$(1,3)
480 CL$(3)=M$(0,4)+" "+M$(3,0)+" "+M$(3,4)
490 C1=(RND(0)<.5):IFC1<>0ORL=1THENCL$(4)=M$(0,5)+" "+M$(1,0)+" "+M$(1,5):GOTO500
491 CL$(4)=M$(0,5)+" "+M$(4,0)+" "+M$(4,5)
500 T$="THE SUSPECT WHO ":N$=" IS NOT THE ONE WHO "
510 A1$=M$(2,0):A2$=M$(2,4):B1$=M$(4,0):B2$=M$(4,4):GOSUB1500
520 CL$(5)=T$+A1$+" "+A2$+" "+B1$+" "+B2$
530 A1$=M$(1,0):A2$=M$(1,4):IFRND(0)<.5THENC1$=N$+M$(5,0)+" "+M$(5,2):GOTO560
540 B1$=M$(5,0):B2$=M$(5,4)
550 C1$=" "+B1$+" "+B2$
560 CL$(6)=T$+A1$+" "+A2$+C1$
570 A1$=M$(1,0):A2$=M$(1,4):B1$=M$(2,0):B2$=M$(2,4):GOSUB1500
580 NF=0:GOSUB1510:CL$(7)=Q$
590 A1$=M$(1,0):A2$=M$(1,2):B1$=M$(2,0):B2$=M$(2,2):GOSUB1500
600 GOSUB1510:CL$(8)=Q$
610 A1$=M$(2,0):A2$=M$(2,2):B1$=M$(4,0):B2$=M$(4,2):GOSUB1500
620 GOSUB1510:CL$(9)=Q$
630 A1$=M$(1,0):A2$=M$(1,5):B1$=M$(4,0):B2$=M$(4,5):GOSUB1500
640 GOSUB1510:CL$(10)=Q$
650 A1$=M$(1,0):A2$=M$(1,1):B1$=M$(4,0):B2$=M$(4,3):GOSUB1500
660 NF=1:GOSUB1510:CL$(11)=Q$
670 A1$=M$(2,0):A2$=M$(2,5):B1$=M$(3,0):B2$=M$(3,2):GOSUB1500
680 GOSUB1510:CL$(12)=Q$
690 CL$(13)="THE MURDERER "+C$:S$="PRESS space TO SEE CLUES.":GOSUB1470:CLS
700 REM OUTPUT CLUES
710 FORX=0TO13:CG$(X)=CL$(X):NEXTX
720 FORX=0TO25:A=INT(RND(0)*14):B=INT(RND(0)*14):M$=CG$(A):CG$(A)=CG$(B):CG$(B)=M$:NEXTX
730 CLS:S$="NO TWO SUSPECTS HAVE THE SAME "+M$(1,6)+", "+M$(2,6)+", "+M$(3,6)+", "+M$(4,6)+" OR "+M$(5,6)+"."
731 GOSUB1360:PRINT
740 FORX=0TO13:S$=CG$(X)+".":GOSUB1360:PRINT:NEXTX
770 S$="PRESS space TO MAKE AN ARREST.":GOSUB1470
780 REM GET PLAYER'S SOLUTION
790 H=0:PR=0:CLS:PRINT:PRINT"    YOU MAY NOW":PRINT:PRINT"0) REPRINT CLUES"
800 FORX=0TO4:PRINTMID$(STR$(X+1),2,1);") ARREST "F$(X):NEXTX
810 PRINT"6) QUIT WITHOUT SEEING SOLUTION":PRINT"7) SEE SOLUTION TO MYSTERY"
820 IFL>1THENPRINT "8) GET A HINT"
830 PRINT:PRINT"SELECT number OF YOUR CHOICE.";
840 Y$=INKEY$:IFY$<"0"ORY$>"8"THEN840
845 IFY$="0"THEN730
850 IFL=1ANDY$="8"THEN840
860 PRINTY$:V=VAL(Y$):PRINT
870 IFV<>6THEN880
871 S$="ENDING GAME.":GOSUB1360:I$="THIS WAS MYSTERY #"+STR$(M)+".":GOSUB1360
872 S$="MAKE A NOTE OF THIS SO YOU CAN COME BACK TO THIS GAME LATER IF YOU LIKE.":GOSUB1360:GOTO1230
880 IFV=7THEN940
890 IFV=8THENH=1:GOTO940
900 CLS:S$="YOU SELECTED "+K$:GOSUB1360:PRINT:IFF$(V-1)<>K$THEN910
901 S$="CORRECT! CONGRATULATIONS. YOU HAVE BEEN APPOINTED A"+AW$+" OF THE ORDER OF THE BRITISH EMPIRE."
902 GOSUB1360:GOTO1230
910 S$="SORRY, YOU'VE ARRESTED THE WRONG SUSPECT. DO YOU WANT TO SEE THE CORRECT SOLUTION (y/n)?":GOSUB1360
920 GOSUB1430:IFYN=0THEN1230
930 REM EXPLAIN SOLUTION AND GIVE HINTS
940 PR=0:CLS:S$="DO YOU WANT HARD COPY OF THE EXPLANATION (y/n)?":GOSUB1360:GOSUB1430:PR=YN
950 CLS
960 S$=CL$(4)+".":IFC1<>0ORL=1THENS$=S$+" "+CL$(10)+". THEREFORE, "+M$(0,5)+" "+M$(4,0)+" "+M$(4,5)+"."
970 GOSUB1360
980 IFL=1THENS$=CL$(13)+", SO "+K$+" IS THE MURDERER.":GOSUB1360:GOTO1230
990 S$=CL$(13)+", SO "+M$(0,5)+" IS NOT THE MURDERER.":GOSUB1360
1000 IFH=1THENPRINT:PRINT"WANT ANOTHER HINT (y/n)?";:GOSUB1430:PRINTY$:IFYN=0THEN790
1010 S$=CL$(5)+".":GOSUB1360:S$=CL$(7)+".":GOSUB1360:S$=CL$(6)+".":GOSUB1360
1011 S$="THEREFORE, ONE SUSPECT "+M$(2,0)+" "+M$(2,4)+", "+M$(4,0)+" "+M$(4,4)+", "+M$(1,0)+" "+M$(1,4)+", AND"+C1$+"."
1012 GOSUB1360
1020 S$="BUT "+CL$(0)+", "+CL$(1)+", "+CL$(2)+", AND "+M$(0,5)+" "+M$(4,0)+" "+M$(4,5)+".":GOSUB1360
1030 REM GOSUB5
1040 S$="THEREFORE, THAT ONE SUSPECT IS "+M$(0,4)+".":GOSUB1360
1050 IFL=2THENS$="SINCE "+M$(0,4)+" "+M$(4,0)+" "+M$(4,4)+", HE IS THE MURDERER.":GOSUB1360:GOTO1230
1060 S$="SINCE "+M$(0,4)+" "+M$(4,0)+" "+M$(4,4)+", HE IS NOT THE MURDERER.":GOSUB1360
1070 IFH=1THENPRINT"WANT ANOTHER HINT (y/n)?";:GOSUB1430:PRINTY$:IFYN=0THEN790
1080 S$=CL$(8)+". "+CL$(9)+".":GOSUB1360
1081 S$="THEREFORE, ONE SUSPECT "+M$(1,0)+" "+M$(1,2)+", "+M$(2,0)+" "+M$(2,2)+", AND "+M$(4,0)+" "+M$(4,2)+".":GOSUB1360
1090 S$=CL$(0)+", "+CL$(2)+", "+M$(0,5)+" "+M$(4,0)+" "+M$(4,5)+", AND "+M$(0,4)+" "+M$(4,0)+" "+M$(4,4)+".":GOSUB1360
1091 S$="THEREFORE, THAT ONE SUSPECT IS "+M$(0,2)+".":GOSUB1360
1100 IFL=3THENS$="SINCE "+M$(0,2)+" "+M$(4,0)+" "+M$(4,2)+", HE IS THE MURDERER.":GOSUB1360:GOTO1230
1110 S$="SINCE "+M$(0,2)+" "+M$(4,0)+" "+M$(4,2)+", HE IS NOT THE MURDERER.":GOSUB1360
1120 REM GOSUB5
1130 IFH=1THENPRINT"WANT ANOTHER HINT (y/n)?";:GOSUB1430:PRINTY$:IFYN=0THEN790
1140 IFC1THENS$=CL$(4)+".":GOTO1150
1141 S$=CL$(10)+". "+CL$(4)+". THEREFORE, "+M$(0,5)+" "+M$(1,0)+" "+M$(1,5)+"."
1150 GOSUB1360
1160 S$=CL$(2)+", "+M$(0,4)+" "+M$(1,0)+" "+M$(1,4)+", "+M$(0,2)+" "+M$(1,0)+" "+M$(1,2)+".":GOSUB1360
1161 S$="THEREFORE, "+M$(0,1)+" IS THE ONE WHO "+M$(1,0)+" "+M$(1,1)+".":GOSUB1360
1170 S$="THIS MEANS THAT "+M$(0,1)+" IS NOT THE SUSPECT WHO "+M$(4,0)+" "+M$(4,3)+".":GOSUB1360
1180 S$="SINCE "+M$(0,5)+" "+M$(4,0)+" "+M$(4,5)+", "+M$(0,4)+" "+M$(4,0)+" "+M$(4,4)+", AND "+M$(0,2)+" "+M$(4,0)
1181 S$=S$+" "+M$(4,2)+", "+M$(0,3)+" MUST BE THE ONE WHO "+M$(4,0)+" "+M$(4,3)+".":GOSUB1360
1190 IFL<>4THEN1210
1191 S$="SINCE THE MURDERER "+C$+", "+K$+" IS THE MURDERER.":GOSUB1360:GOTO1230
1200 S$="SINCE THE MURDERER "+C$+", "+M$(0,3)+" IS NOT THE MURDERER.":GOSUB1360
1210 IFH=1THENPRINT"WANT ANOTHER CLUE (y/n)?";:GOSUB1430:PRINTY$:IFYN=0THEN790
1220 S$="BY PROCESS OF ELIMINATION, "+K$+" IS THE MURDERER.":GOSUB1360
1230 PRINT:PRINT"DO YOU WANT TO PLAY ANOTHER":PRINT"GAME (y/n)?";:GOSUB1430:IFYN=1THENRUN10
1231 PRINT:END
1240 DATA BILL,DAVID,JOHN,TOM,FRED,LARRY,BRIAN,JIM,ROBERT,JACK,MARTY
1250 DATA FOX,MARTIN,SMITH,JONES,HARRISON,CRAIG,DAVIS,EDISON,BROWN,STEVENSON,ALBERTS
1260 DATA HAS,BLACK HAIR,NO HAIR,RED HAIR,BLOND HAIR,BROWN HAIR,WHITE HAIR,GRAY HAIR,"","",HAIR COLOUR
1270 DATA WAS WEARING,A RED SHIRT,AN ORANGE SHIRT,A YELLOW SHIRT,A GREEN SHIRT,A BLUE SHIRT
1271 DATA A PURPLE SHIRT,A WHITE SHIRT,A BLACK SHIRT,"",COLOUR SHIRT
1280 DATA OWNS, A RED CAR,AN ORANGE CAR,A YELLOW CAR,A GREEN CAR,A BLUE CAR,A PURPLE CAR
1281 DATA A WHITE CAR,A BLACK CAR,A SILVER CAR,COLOUR CAR
1290 DATA IS,"5 FEET TALL","5 FEET 3 INCHES TALL","5 FEET 6 INCHES TALL","5 FEET 9 INCHES TALL"
1291 DATA "6 FEET TALL","6 FEET 3 INCHES TALL","","","",HEIGHT
1300 DATA WEIGHS,"140 POUNDS","150 POUNDS","160 POUNDS","170 POUNDS","180 POUNDS","190 POUNDS"
1301 DATA "200 POUNDS","210 POUNDS","220 POUNDS",WEIGHT
1310 DATA WAS WEARING,RED SHOES,WHITE SHOES,BROWN SHOES,BLACK SHOES,TAN SHOES,BLUE SHOES
1311 DATA ORANGE SHOES,"","",COLOUR SHOES
1320 DATA HAS A WIFE NAMED,SUE,JOYCE,MARY,BETTY,PAM,CATHY,JILL,JUDY,SALLY,WIFE'S FIRST NAME
1330 DATA WAS CARRYING,A RED UMBRELLA,AN ORANGE UMBRELLA,A YELLOW UMBRELLA,A GREEN UMBRELLA
1331 DATA A PURPLE UMBRELLA,A WHITE UMBRELLA,A BLACK UMBRELLA,"","",COLOUR UMBRELLA
1340 DATA A RESTAURANT,AN ELEVATOR,A LIBRARY,STABBED,STRANGLED,SHOT
1350 REM PRINT S$ TO SCREEN AND PRINTER
1360 CC=((PEEK(17024)AND1)*256+PEEK(17025))-1:IFCC+LEN(S$)>447THENGOSUB5
1370 M$=S$:GOSUB1:IFPRTHENLPRINTS$
1410 RETURN
1430 Y$=INKEY$:IFY$<>"Y"ANDY$<>"N"THEN1430
1440 YN=0:IFY$="Y"THENYN=1
1450 RETURN
1460 PRINTTAB((32-LEN(S$))/2)S$:RETURN
1470 PRINT@480,TAB((32-LEN(S$))/2)S$;
1480 ON-(INKEY$<>" ")GOTO1480:RETURN
1500 IFRND(0)<.5THENM$=A1$:A1$=B1$:B1$=M$:M$=A2$:A2$=B2$:B2$=M$:RETURN
1501 RETURN
1510 N$=" ":IFNFTHENN$=" IS NOT THE ONE WHO "
1520 Q$=T$+A1$+" "+A2$+N$+B1$+" "+B2$:RETURN

2000 CLS0:POKE49151,28
2001 PRINT"€€€ÀÀïêëêÙàõàûàûÈëêÀÕßÅçÕåÕÅç€€€";
2002 PRINT"€€€À€èèÔ€ÌàÔ€ìÈèèÔ€ÀÔÔÔÔÔÔÔÔÔ€€€";
2003 PRINT"€€€€€€€€€€€€€€‡Ÿ¿‹‚€€€€€€€€€€";
2004 PRINT"€€€€€€€€€€€¿¿¿¿¿¿¿¿¿¯ƒ€€€€€€€€";
2005 PRINT"€€€€€€€€€€…¿¿¿¿¿¿¿¿¿¿¿¿¿¯€€€€€€€";
2006 PRINT"€€€€€€€€‡¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿ª€€€€€€";
2007 PRINT"€€€€€€€Œ¼¼¼¿¿¿¿¿¿¿¿¿¿¿¿¿¿ª€€€€€€";
2008 PRINT"€€€€€€€€€Ÿ¿¿¿¿¿¿¿¿¿¿¿¿¿¿¿‹€€€€€";
2009 PRINT"€€€€€€€€€¼¼¿¿¿¿¿¿¿¿¿¿¿¿¾ €° €€€€";
2010 PRINT"€€€€€€€€œ¿¿¿¿¿¿¿¿¿¿¿¿‹‚€€€€€€€";
2011 PRINT"€€€€€€€…¿€€¿¿¿¼¿¿¿¿¿¿¿¿¿¿‹€€€€€€";
2012 PRINT"€€€µ¿¿ Ÿ €€€€€€—¿¿¿¿¿¿¿¿¿¿«€€€€€";
2013 PRINT"€€€€´¼¼ €€€€€€€µ¿¿¿¿¿¿¿¿¿¿¿¯€€€€";
2014 PRINT"€€€€€€€€€€€€€¿¿¿¿¼¸°°°°°° €€€€";
2015 PRINT"€€€€€€€€€€€€¿¿¼¼°€€€€€€€€€€€€€€";
2016 PRINT"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";:RETURN


3000 REM BY DAVID LEITHAUSER 
3010 REM COPYRIGHT 1987
3020 REM COMPUTE! PUBLICATIONS
3030 REM ALL RIGHTS RESERVED
3040 REM MC-10 EDITS JIM GERRIE
