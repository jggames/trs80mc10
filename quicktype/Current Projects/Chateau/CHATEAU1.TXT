0 DIMN(21)
1 C2=1:FORC1=1TO63STEP3:N(C2)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",C1,3)):C2=C2+1
2 NEXT:GOTO60
4 FORC2=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,C2))-64),ASC(MID$(M$,C2+1))-48:NEXT:RETURN
11 FORC1=1TO250:NEXT:RETURN
60 GOSUB5140
70 REM
80 DIMM(7,7,3),T(7,7,3),D$(12),MM$(30),A(4),B(4),T$(7,7,3)
90 RESTORE
100 GOSUB200
110 GOSUB460
120 GOSUB5630
130 TIMER=0
140 GOSUB700
150 GOSUB950
160 IFRND(7)=4THENGOSUB3380
170 IFST<1THENGOSUB4510
180 IFZ=0ANDX=XXANDY=YYTHENGOSUB4160
190 GOTO140
200 REM
210 FORB=1TO3:FORA=1TO6
220 M(A,3,B)=1:M(3,A,B)=1
230 NEXTA,B
240 A=RND(7)
250 FORB=1TO3:M(A,3,B)=4:NEXTB
260 DATA2,2,1,1,2,3,4,1,5,2,6,2,3,1,4,2,5,2,6,4,4,5,4,6,5,4,6,4,9
270 FORF=1TO3:RESTORE
280 READA:IFA=9THEN320
290 B=RND(A)
300 FORC=1TOB:READD,E:NEXTC:M(D,E,F)=1+RND(2)
310 IFB=ATHEN280
311 READD,E:B=B+1:GOTO310
320 NEXTF
330 FORC=0TO3:FORA=1TO6:FORB=1TO6
340 IFM(B,A,C)THEN360
350 M(B,A,C)=5+RND(7)
360 NEXTB,A,C
370 FORD=1TO2
380 A=RND(7)
390 B=RND(7)
400 C=RND(3)
410 IFM(A,B,C)<5ORM(A,B,C)>8THEN380
420 IFD=1THENT(A,B,1)=5
430 IFD=2THENT(A,B,C)=7
440 NEXTD
450 RETURN
460 REM
470 K=0:X=3:Y=6:Z=1:L=0:TF=0
480 DATAHALL,DOOR,DOOR,STAIRCASE,DESK,CUPBOARD
490 DATACLOCK,PIANO,CHAIR,STATUE,FIREPLACE,BOOKCASE
500 FORA=1TO12:READD$(A):NEXTA
510 MO=0:ST=100:CL$=STR$(1000+RND(8999)):CC=1
520 FORA=1TO6:FORB=1TO6
530 M(A,B,0)=5+RND(7)
540 NEXTB,A
550 DATATROLL,GHOST,IMP,BAT,GIANT,ZOMBIE,GROCKLE,HOUND,DEVIL,KOBOLD,RAT,XORN
560 DATAGOBLIN,ORCUS,HYDRA,SERPENT,BASILISK,PHEONIX,MEDUSA,SALAMANDER
570 DATAGARGOYLE,BEHOLDER,JUBILEX,GOLEM,VAMPIRE,HELL HOUND,GAURDIAN,ORC,ELF
580 FORT=1TO29:READMM$(T):NEXT
590 XX=RND(6):YY=RND(6)
600 FORA=1TO6:FORB=1TO6:FORC=0TO3:IFT(A,B,C)THEN620
610 IFRND(7)=5THENT(A,B,C)=8
620 IFRND(5)=2ANDT(A,B,C)=0THENT(A,B,C)=RND(4)
630 NEXTC,B,A
640 FORA=1TO4
650 B=RND(7):C=RND(7):D=RND(3)
660 IFT(B,C,D)<>0ORM(B,C,D)<5ORM(B,C,D)>8THEN650
670 T(B,C,D)=6
680 NEXTA
690 RETURN
700 REM
710 GOSUB5520
720 GOSUB5690
730 PRINT@66,"";
740 M=M(X,Y-1,Z):A$="NORTH"
750 GOSUB890
760 PRINT@98,"";
770 M=M(X-1,Y,Z):A$="WEST"
780 GOSUB890
790 PRINT@130,"";
800 M=M(X+1,Y,Z):A$="EAST"
810 GOSUB890
820 PRINT@194,"";
830 M=M(X,Y,Z):A$="BY YOUR SIDE"
840 GOSUB890
850 PRINT@162,"";
860 M=M(X,Y+1,Z):A$="SOUTH"
870 GOSUB890
880 RETURN
890 REM
900 IF((M(X,Y,Z)=1ORM(X,Y,Z)=4)ANDM>4)ORM=0THENRETURN
910 IF(M(X,Y,Z)>4ORM(X,Y,Z)=3)AND(M<5ANDM<>3)THENRETURN
920 IFM(X,Y,Z)=2ANDM>4THENRETURN
930 PRINTA$;" IS A ";D$(M);
940 RETURN
950 REM
960 PRINT@234,"your choice";
970 PRINT@258,"(M) - MOVE";
980 PRINT@290,"(E) - EXPLORE";
990 PRINT@322,"(O) - OPEN";
1000 PRINT@354,"(S) - STATUS";
1010 PRINT@386,"(P) - PLAN";
1020 PRINT@482,"- ENTER M,E,O,S OR P ...";
1030 B$=INKEY$
1040 IFB$=""THEN1030
1050 SOUND150,1
1060 IFB$="M"THENA=1:GOTO1130
1070 IFB$="E"THENA=2:GOTO1130
1080 IFB$="O"THENA=3:GOTO1130
1090 IFB$="P"THENA=5:AA=1:GOTO1130
1100 IFB$="S"THENA=4:GOTO1130
1110 SOUND95,2:SOUND77,2
1120 GOTO1030
1130 ON A GOSUB1150,1470,1600,3900,5820
1140 RETURN
1150 REM
1160 PRINT@234,"           ";
1170 PRINT@264,"move";
1180 PRINT@307,"(N) - NORTH";
1190 PRINT@339,"(S) - SOUTH";
1200 PRINT@371,"(W) - WEST";
1210 PRINT@403,"(E) - EAST";
1220 PRINT@482,"- ENTER N,S,W OR E .....";
1230 C$=INKEY$
1240 IFC$=""THEN1230
1250 SOUND150,1
1260 IFC$="N"ORC$="S"ORC$="W"ORC$="E"THEN1270
1261 GOTO1150
1270 IFC$="N"THENC=-1:B=0
1280 IFC$="S"THENC=1:B=0
1290 IFC$="W"THENC=0:B=-1
1300 IFC$="E"THENC=0:B=1
1310 M=M(X+B,Y+C,Z)
1320 IFM=0THENRETURN
1330 IFC$="W"THENT$(X,Y,Z)="<"
1340 IFC$="E"THENT$(X,Y,Z)=">"
1350 IFC$="N"THENT$(X,Y,Z)="^"
1360 IFC$="S"THENT$(X,Y,Z)="V"
1370 M=M(X+B,Y+C,Z)
1380 IFM=0THENRETURN
1390 IFM(X,Y,Z)=3THEN1450
1400 IFM=1ANDM(X,Y,Z)=2THEN1450
1410 IFM(X,Y,Z)=2THENGOSUB2490:IFM>4THENRETURN
1420 IF((M(X,Y,Z)=1ORM(X,Y,Z)=4)ANDM>4)ORM=0THEN RETURN
1430 IF(M(X,Y,Z)>4ORM(X,Y,Z)=3)AND(M<5ANDM<>3)THENRETURN
1440 IFM(X,Y,Z)=2ANDM>4THENRETURN
1450 X=X+B:Y=Y+C
1460 RETURN
1470 REM
1480 IFM(X,Y,Z)=4THENGOSUB1690:GOTO1530
1490 IFT(X,Y,Z)=0THENGOSUB5520:GOSUB2700:T(X,Y,Z)=9:RETURN
1500 IFT(X,Y,Z)=9THEN1540
1510 ONT(X,Y,Z)GOSUB1830,1980,2160,2280,2380,450,450,4260
1520 IFT(X,Y,Z)=0THENT(X,Y,Z)=9
1530 RETURN
1540 GOSUB5520
1550 PRINT@102,"you have searched";
1560 PRINT@198,"here already .......";
1570 PRINT@294,"silly !!!!!!!!!";
1580 FORMIN=0TO1000:NEXTMIN
1590 RETURN
1600 REM
1610 ONM(X,Y,Z)GOSUB1530,2490,2490,1530,2610,2740,2810,2900
1620 IFM(X,Y,Z)>1ANDM(X,Y,Z)<9ANDM(X,Y,Z)<>4THEN1670
1630 GOSUB5520
1640 PRINT@100,"I'VE NEVER OPENED A ";:PRINT@132,D$(M(X,Y,Z));
1650 PRINT@196,"AND I DONT INTEND TO START";
1660 PRINT@228,"NOW!!!";
1670 GOSUB5630
1680 RETURN
1690 REM
1700 GOSUB5520
1710 PRINT@170,"stairs";
1720 PRINT@226,"DO YOU WANT TO CLIMB UP (U)";
1730 PRINT@258,"OR DOWN (D)?.........";
1740 PRINT@482,"ENTER 'U' OR 'D'.........";
1750 A$=INKEY$:IFA$=""THEN1750
1760 IFA$="U"THENA=1:GOTO1790
1770 IFA$="D"THENA=2:GOTO1790
1780 GOTO1750
1790 IF(A=1ANDZ=3)OR(A=2ANDZ=1)THENPRINT@322,"YOU CAN'T!!!";:GOTO1750
1800 IFA=1THENZ=Z+1:GOTO1810
1801 Z=Z-1
1810 T$(X,Y,Z)="S"
1820 RETURN
1830 REM
1840 GOSUB5520
1850 PRINT@104,"congratulations...";
1860 PRINT@164,"YOU'VE JUST FOUND A TRAP.";
1870 A=20+RND(30):PRINT@194,A" OF YOUR STRENGTH UNITS";
1880 PRINT@226,"ARE USED UP.......";
1890 ST=ST-A
1900 IFST<1THEN1960
1910 PRINT@258,"YOU ARE LUCKY....";
1920 PRINT@290,"YOU ARE STILL ALIVE....";
1930 IFST<30THENPRINT@322,"BUT ONLY JUST....";
1940 PRINT@356,"TREAD MORE CAREFULLY IN";
1950 PRINT@388,"FUTURE.....";
1960 GOSUB5630
1970 RETURN
1980 REM
1990 GOSUB5520
2000 PRINT@66,"YOU HAVE JUST FOUND A key";
2010 PRINT@98,"HOWEVER IT MAY BE....";
2020 PRINT@139,"BOOBYTRAPPED!!!";
2030 PRINT@194,"DO YOU WANT IT?";
2040 A$=INKEY$:IFA$=""THEN2040
2050 IFA$="N"THENPRINT@258,"no.. OK SUIT YOURSELF";:GOSUB5630:RETURN
2060 IFRND(9)<>3THENPRINT@258,"THE KEY'S YOURS";:K=1:T(X,Y,Z)=9:GOSUB5630:RETURN
2070 FORAB=1TO50
2080 PRINT@264,"****BOOM****";
2090 SOUND150,1
2100 PRINT@264,"****boom****";
2110 NEXTAB
2120 PRINT@326,"BOOBYTRAPPED!!!";
2130 GOSUB5630
2140 GOSUB1830
2150 RETURN
2160 REM
2170 GOSUB5520
2180 PRINT@66,"AH HA,ONE lamp IS YOURS";
2190 PRINT@98,"FOR THE TAKING....";
2200 PRINT@162,"DO YOU WANT IT?";
2210 A$=INKEY$:IFA$=""THEN2210
2220 IFA$="N"THENPRINT@180,"NO!!";:GOSUB5630:RETURN
2230 IF RND(9)<>7THENL=1:PRINT@226,"THE LAMP IS YOURS";:T(X,Y,Z)=9:GOSUB5630:RETURN
2240 PRINT@226,"OH DEAR THIS LAMP WASN'T FOR";
2250 PRINT@258,"SALE! YOU'VE GOT TROUBLE..";
2260 GOSUB5630
2270 GOSUB2280:RETURN
2280 REM
2290 GOSUB5520:PRINT@66,"well done......idiot!";
2300 PRINT@130,"YOU'VE JUST DISTURBED ALL ";
2310 PRINT@162,"THE 'ORRIBLE LITTLE BUGS!!!";
2320 PRINT@290,"BELIEVE ME .......";
2330 PRINT@322,"YOUR IN FOR FUN NOW!!";
2340 IF MO=1THENPRINT@386,"EVEN MORE THAN BEFORE..";
2350 M$="A1C1E1B1D1G1A1C1E1B1D1G1A1C1E1B1D1G1":GOSUB4
2360 MO=1
2370 RETURN
2380 REM
2390 GOSUB5520
2400 PRINT@66,"WELL WELL WELL LOOK WHAT ";
2410 PRINT@98,"YOU'VE FOUND.....";
2420 PRINT@132,"YES THE cellar entrance!!";
2430 PRINT@194,"DO YOU WANT TO GO IN?";
2440 A$=INKEY$:IFA$=""THEN2440
2450 IFA$="N"THENPRINT" NO!!";:FORAB=1TO1000:NEXTAB:RETURN
2460 IFL=0THENPRINT@258,"TOUGH, YOU'LL NEED A LAMP!";:GOSUB5630:RETURN
2470 Z=0:PRINT@234,"good luck";:GOSUB2320
2480 RETURN
2490 REM
2500 GOSUB5520
2510 IFM(X,Y,Z)=3THENPRINT@66,"OK THE DOOR IS OPEN";:FORNN=1TO500:NEXT:RETURN
2520 PRINT@66,"THE DOOR IS LOCKED";
2530 IFK<>1THENPRINT@130,"YOU WILL NEED A KEY";
2540 IFK<>1THENPRINT@162,"TO OPEN IT...";:GOSUB5630:RETURN
2550 PRINT@226,"DO WISH TO UNLOCK IT?";
2560 A$=INKEY$:IF A$=""THEN2560
2570 IFA$="N"THENPRINT"NO!";:PRINT@258,"SUIT YOURSELF!";:GOSUB5630:RETURN
2580 M(X,Y,Z)=3:PRINT@258,"THE DOOR IS OPEN";
2590 IFRND(9)=3THENPRINT@290,"THE KEY IS JAMMED....";:PRINT@322,"IN THE LOCK";:K=0
2600 GOSUB5630:RETURN
2610 REM
2620 IFRND(5)<>2THEN2700
2630 GOSUB5520
2640 PRINT@66,"THE DESK IS JAMMED...";
2650 PRINT@98,"DO YOU WISH TO FORCE IT? ";
2660 A$=INKEY$:IFA$=""THEN2660
2670 IFA$="N"THENGOSUB5630:RETURN
2680 ST=ST-RND(10):PRINT@162,"uggggh, OPENED IT!";
2690 FORAB=1TO500:NEXTAB
2700 IFT(X,Y,Z)=0THENPRINT@226,"WASTE OF TIME,......";:PRINT@258,"IT'S EMPTY...........";:FORMIN=0TO1000:NEXTMIN:RETURN
2710 ONT(X,Y,Z)GOSUB450,1980,2160,2280,2380,3100,3220
2720 FORMIN=0TO1000:NEXTMIN
2730 RETURN
2740 REM
2750 IFL=0THENGOSUB5520:PRINT@66,"IT'S TOO DARK TO SEE....";
2760 IFL=0THENPRINT@98,"YOU NEED A LAMP..";:GOTO2800
2770 GOSUB5520
2780 IFRND(9)=4THENPRINT@162,"YOUR LAMPS BLOWN OUT!";:L=0:GOTO2800
2790 GOSUB2700
2800 RETURN
2810 REM
2820 GOSUB5520
2830 PRINT@66,"CREEEEEEK, THE DOOR IS STIFF";:PRINT@98,"AND THIS WEAKENS YOU....";
2840 ST=ST-RND(5)
2850 FORCB=1TO10:SOUND87,1:FORAB=1TO80:NEXTAB:SOUND103,1:NEXTCB
2860 GOSUB5630
2870 IFRND(9)=4THENGOSUB3380
2880 GOSUB2700
2890 RETURN
2900 REM
2910 GOSUB5520
2920 PRINT@66,"CONGRATULATIONS,";:PRINT@98,"YOU HAVE FOUND A key";
2930 IFT(X,Y,Z)<>2THENPRINT@162,"BUT THEN AGAIN MOST PIANOS";:PRINT@194,"HAVE KEYS!!!";
2940 GOSUB5630
2950 GOSUB2700
2960 RETURN
2970 REM
2980 GOSUB5520
2990 PRINT@66,"ha ha ha ....";
3000 PRINT@130,"the house has claimed ";
3010 PRINT@194,"another body.....";
3020 PRINT@258,"the treasure lies hidden..";
3030 PRINT@322,"you have failed...";
3040 M$="G1G1G2G2":GOSUB4:SOUND95,1:M$="A1A1G1G1":GOSUB4:SOUND155,1:SOUND161,1
3050 PRINT@386,"your final status is......";
3060 DN=1
3070 GOSUB5630
3080 GOSUB3930
3090 GOTO4120
3100 REM
3110 GOSUB5520
3120 PRINT@100,"YOU HAVE FOUND A CLUE....";
3130 CC=CC+1:IFCC=5THENPRINT@162,"THE LAST ONE YOU'LL NEED!";
3140 CD=5-CC
3150 IFCC<>5THENPRINT@162,"YOU STILL NEED "CD" CLUE";
3160 IFCC=5THEN3180
3170 IFCD=1THENPRINT"!";:GOTO3180
3171 PRINT"S!";
3180 PRINT@226,"THE NUMBER IS...";MID$(CL$,CC,1);:PRINT@270,"REMEMBER IT!";
3190 T(X,Y,Z)=0
3200 FORMIN=1TO1000:NEXTMIN
3210 RETURN
3220 REM
3230 GOSUB5520
3240 PRINT@66,"YOU HAVE THE PLACE TO ";
3250 PRINT@98,"DEPOSIT YOUR CLUES";
3260 PRINT@162,"ARE YOU READY TO TRY ?";
3270 A$=INKEY$:IFA$=""THEN3260
3280 IFA$="N"THENPRINT@186,"NO!";:GOSUB5630:RETURN
3290 PRINT@258,"ENTER THE CODE, AND";
3300 PRINT@290,"AFTER CHECKING PRESS enter";
3310 PRINT@354,"";:LINEINPUT"####";CH$:CH$=" "+CH$
3320 IFCH$<>CL$THENPRINT@370,"wrong";:PRINT@383,CHR$(181);:PRINT@418,"YOU HAVE FAILED.. NOW DIE..";:GOSUB5630:GOTO2970
3330 PRINT@368,"brilliant";:PRINT@418,"YOU HAVE DONE WELL MY FRIEND";
3340 TF=1
3350 GOSUB5630:GOSUB4680
3360 GOSUB3900
3370 GOTO4120
3380 REM
3390 IFMO=0THENRETURN
3400 A=RND(29):B=A*RND(4)
3410 GOSUB5520
3420 PRINT@98,"YOU HAVE JUST BEEN CONFRONTED";
3430 PRINT@130,"BY A ";MM$(A);
3440 PRINT@194,"WHAT DO YOU WISH TO DO??";
3450 PRINT@258,"   (R) - RETREAT";
3460 PRINT@290,"   (F) - FIGHT ";
3470 PRINT@322,"   (G) - GAMBLE";
3480 PRINT@354,"   (A) - ADVICE";
3490 PRINT@482,"- ENTER R,G,F OR A .....";
3500 D$=INKEY$:IFD$=""THEN3500
3510 IFD$="R"THENC=1:GOTO3560
3520 IFD$="F"THENC=2:GOTO3560
3530 IFD$="G"THENC=3:GOTO3560
3540 IFD$="A"THENC=4:GOTO3560
3550 GOTO3500
3560 ON C GOTO3570,3650,3780,3850
3570 IFRND(3)<>2THENPRINT@390,"escaped";:GOTO3840
3580 GOSUB5520
3590 PRINT@66,"A ";MM$(A);" DOES NOT LIKE";
3600 PRINT@98,"CHICKENS";
3610 PRINT@130,"HE ATTACKS, GOOD LUCK.";
3620 GOTO3660
3630 GOSUB5520
3640 PRINT@162,"THE ";MM$(A);" NOW HAS ";B;:PRINT@194,"HIT POINTS";:GOTO3670
3650 GOSUB5520
3660 PRINT@162,"THE ";MM$(A)+" HAS ";B;" HIT";:PRINT@194," POINTS";
3670 PRINT@226,"WHAT'S YOUR PUNCH FACTOR";
3680 INPUTU:PRINT@255,CHR$(181);:IFU<1ORU>9THEN3670
3690 ST=ST-U
3700 U=U*RND(5):B=B-U
3710 PRINT@290,"YOUR PUNCH LEAVES THE ";
3720 PRINT@322,MM$(A)" ONLY"B"STRENGTH";:PRINT@354,"UNITS...";
3730 C=RND(10)
3740 B=B-C
3750 IFB<1THEN3755
3751 GOTO3760
3755 ST=ST-B:PRINT@386,"you win AND GAIN "-B" POINTS";
3756 PRINT@418,"& HAVE ";ST;" STRENGTH POINTS!";:MK=MK+1:GOTO3840
3760 ST=ST-(C*2):PRINT@386,"HIS HIT LEAVES YOU ";ST;:PRINT@418,"POINTS...";
3770 IFST<1THEN3840
3771 GOSUB5630:B=B+C:GOTO3630
3780 GOSUB5520
3790 A=RND(20):PRINT@66,A;" HIT POINTS ARE AT STAKE";
3800 PRINT@130,"CHOOSE A NUMBER BETWEEN....";
3810 PRINT@168,"...1 AND ";20-A;" ...";:INPUTB:PRINT@191,CHR$(181);
3820 IFB=(20-A)*RND(8)THENST=ST+A:PRINT@226,"YOU WIN........";:GOTO3840
3830 PRINT@226,"HA.. HA.. YOU LOSE";:ST=ST-A
3840 GOSUB5650:RETURN
3850 GOSUB5520:PRINT@43,"advice";
3860 PRINT@100,"IF YOU WANT TO retreat YOU";:PRINT@130,"HAVE A 2 TO 1 CHANCE";:PRINT@196,"IF YOU WANT TO gamble YOU";:PRINT@226,"CREATE YOUR OWN RANDOM ODDS.";:PRINT@292,"IF YOU CHOOSE TO fight";:PRINT@322,"THEN YOU HAVE TO INPUT punch";
3870 PRINT@354,"factor IN THE RANGE 1 TO 9.";
3880 PRINT@386,"THIS IS HOW HARD YOU WANT TO";:PRINT@418,"HIT YOUR FOE....";
3890 GOSUB5630:GOTO3410
3900 REM
3910 GOSUB5520
3920 PRINT@66,"YOUR CURRENT STATUS IS....";
3930 PRINT@130,"STRENGTH..........";ST;
3940 PRINT@162,"MONSTERS KILLED...";MK;
3950 PRINT@194,"TREASURE..........";TR;
3960 PRINT@226,"KEY...............";K;
3970 PRINT@258,"LAMP..............";L;"     ";
3980 PRINT@290,"CLUES.............";CC-1;
3990 PRINT@322,"LEVEL.............";Z;
4000 PRINT@354,"COORDINATES.......";X;Y;
REM 4010 T1=TIMER:T1=INT(T1/56)
REM 4020 T4=INT(T1/3600)
REM 4030 T3=INT(T1/60)-(T4*60)
REM 4040 T2=T1-(T3*60)
4050 PRINT@386,"TIME............";T4;":";T3;":";T2;
4060 SC=MK*100+TR+10*(K+L)+10*ST-VAL(LEFT$(TI$,4))
4070 IFCC=5THENSC=SC*2
4080 IFTF=1THENSC=SC*2
4090 PRINT@418,"your score is **"SC"**";
4100 IFDN=1THENRETURN
4110 GOSUB5650
4110 RETURN
4120 PRINT@482,"- DO YOU WANT ANOTHER GO? -";
4130 A$=INKEY$:IFA$=""THEN4130
4140 IFA$="Y"THENRUN
4150 CLS:END
4160 REM
4170 GOSUB5520
4180 PRINT@66,"WOOOOOOOOOOSH!!!!!";
4190 PRINT@98,"YOU'VE JUST FOUND A TRAVEL";
4200 PRINT@130,"AGENT!";
4210 PRINT@194,"AND YOU HAVE BEEN KIDNAPPED";:PRINT@226,"AND RELEASED SOMEWHERE,";:PRINT@258,"BUT WHERE?";
4220 X=RND(6)
4230 Y=RND(6)
4240 Z=RND(3)
4250 GOSUB5630:RETURN
4260 REM
4270 A=RND(900)
4280 GOSUB5520
4290 PRINT@66,"YOU HAVE FOUND ";A;" PIECES";
4300 PRINT@98,"OF gold";
4310 PRINT@162,"DO YOU WANT THEM? ";
4320 B$=INKEY$:IFB$=""THEN4320
4330 IFB$="N"THENPRINT@182,"NO!";:GOSUB5630:RETURN
4340 PRINT@182,"yes!!";
4350 TR=TR+A:ST=ST-INT(A/40)
4360 T(X,Y,Z)=9
4370 IFRND(9)<>2THENGOSUB5630:RETURN
4380 PRINT@226,"YOU HAVE ALSO FOUND:-";
4390 A=RND(9)
4400 IFA=1THENPRINT@290,"A SWORD          ";:ST=ST+RND(20)
4410 IFA=2THENPRINT@290,"BAD LUCK         ";:MO=1
4420 IFA=3THENPRINT@290,"A POTION         ";:ST=ST+RND(20)-5
4430 IFA=4THENPRINT@290,"AN AXE           ";:ST=ST+7
4440 IFA=5THENPRINT@290,"A LAMP           ";:L=1
4450 IFA=6THENPRINT@290,"A KEY            ";:K=1
4460 IFA=7THENPRINT@290,"A TIME WARP      ";:TIMER=0
4470 IFA=8THENPRINT@290,"A NUGGET         ";:TR=TR+RND(800)
4480 IFA=9THENPRINT@290,"A GOOD LUCK CHARM";:MO=0:ST=ST+10
4490 FORAB=0TO800:NEXTAB
4500 GOTO4370
4510 REM
4520 GOSUB5520
4530 IFTR<30THEN2970
4540 PRINT@66,"HMMM YOU SEEM TO BE OUT OF";
4550 PRINT@98,"ENERGY.";
4560 PRINT@130,"A GRAND SCORE OF ";ST;
4570 PRINT@162,"YOU NEED HELP! SINCE YOU ";
4580 PRINT@194,"HAVE ";TR;" ITEMS OF TREASURE";
4590 PRINT@226,"I FEEL I DO A DEAL WITH YOU";
4600 A=8+RND(20):A=INT(TR/A)
4610 PRINT@290,"I'LL SELL YOU ONE UNIT OF ";
4620 PRINT@322,"ENERGY FOR EVERY ";A;" PIECES";
4630 PRINT@354,"OF TREASURE YOU GIVE ME ok";
4640 PRINT@386,"( Y OR N )";
4650 B$=INKEY$:IFB$=""THEN4650
4660 IFB$="Y"THENST=ST+INT(TR/A):TR=TR-A*INT(TR/A):PRINT@418,"ok!";:GOSUB5630:RETURN
4661 GOTO4670
4670 IFRND(5)=3THENPRINT@418,"***fool***";:GOSUB5630:GOTO2970
4671 GOTO4560
4680 REM
4690 GOSUB5520
4700 PRINT@66,"  THE PREVIOUS OWNER OF THE";
4710 PRINT@98,"TREASURE MADE SURE THAT HIS";
4720 PRINT@130,"SECRET WOULD REMAIN HIDDEN";
4730 PRINT@162,"FOR A LONG TIME, HE ALSO";
4740 PRINT@194,"GAVE THE FINDER ONE FINAL";
4750 PRINT@226,"TEST, IF YOU WIN..";
4760 PRINT@258,"  THE TREASURE IS YOURS...";
4770 PRINT@290,"      IF YOU LOSE death";
4780 PRINT@354,"DO YOU WANT TO TAKE IT?";
4790 A$=INKEY$:IFA$=""THEN4790
4800 IFA$="N"THENPRINT@418,"ok you have done well";:GOSUB5630:RETURN
4810 PRINT@418,"good luck";
4820 GOSUB5630:GOSUB5520
4830 PRINT@66,"THE CHEST HAS A LOCK ON IT";
4840 PRINT@130,"TO OPEN IT YOU MUST CRACK";
4850 PRINT@162,"THE CODE";
4860 GOSUB5630
4870 FORX=1TO4:A(X)=RND(9):NEXTX
4880 FOR X=1TO4:FOR Y=1TO4:IF X=Y THEN4900
4890 IFA(X)=A(Y)THENV=1
4900 NEXTY,X:IFV=1THENV=0:GOTO4870
4910 PRINT@226,"3 POINTS WILL BE GIVEN IF ";
4920 PRINT@258,"YOU CHOOSE THE RIGHT NUMBER";
4930 PRINT@290,"IN THE RIGHT PLACE...";
4940 PRINT@322,"1 POINT FOR THE RIGHT NUMBER";
4950 PRINT@354,"WRONG PLACE";
4960 S=0
4970 FOR X=1TO1000:NEXTX
4980 GOSUB5520
4990 PRINT@66,"ok TYPE IN YOUR GUESS";
5000 FOR X=1TO4:PRINT@(32*X+66),"";:INPUT B(X):PRINT@(32*X+66+29),CHR$(181);:NEXTX
5010 FORX=1TO4:IFB(X)=A(X)THENS=S+3
5020 NEXTX
5030 FORX=1TO4:FORY=1TO4
5040 IF X=Y THEN5060
5050 IFB(Y)=A(X)THENS=S+1
5060 NEXTY,X
5070 PRINT@354,"";
5080 FORX=1TO4:PRINTB(X);:NEXTX
5090 PRINT@386,"**";S;"**";
5100 PRINT@418,"try again";:FOR X=0TO500:NEXTX:PRINT@418,"         ";
5110 IFS=12THENPRINT@436,"**ALL DONE**";:TR=TR+9999+RND(9999):GOSUB5630:RETURN
5120 IF RND(11)=4THEN PRINT@436,"times up";:GOSUB5630:GOTO2970
5130 S=0:GOTO5000
5140 REM
5150 GOSUB5520
5160 X=RND(-(PEEK(9)*256+PEEK(10)))
5170 PRINT@70,"CHATEAU OF GOLD";
5180 PRINT@101,"*****************";
5190 PRINT@162,"YOU ARE IN AN OLD CHATEAU";
5200 PRINT@194,"WHEN THE OLD COUNT WHO LIVED ";
5210 PRINT@226,"THERE DIED, HE HID ALL HIS";
5220 PRINT@258,"WORLDLY GOODS IN THE HOUSE.";
5230 PRINT@290,"HE ALSO HID A HOARD OF ....";
5240 PRINT@322," ..treasure (VALUED AT $2M!)";
5250 GOSUB 5650
5260 GOSUB5520
5270 PRINT@66,"  TO FIND THIS TREASURE YOU";
5280 PRINT@98,"MUST FIND VARIOUS CLUES AND,";
5290 PRINT@130,"MORE IMPORTANTLY, THE PLACE ";
5300 PRINT@162,"TO PUT THEM.....";
5310 PRINT@226," ..be warned, HE ALSO LEFT ";
5320 PRINT@258,"TRAPS FOR THE UNWARY....";
5330 PRINT@290,"SO GO CAREFULLY.....";
5340 PRINT@322,"ALSO, THINGS MAY NOT ALWAYS";
5350 PRINT@354,"BE AS THEY SEEM......";
5360 PRINT@386,"go carefully and good luck";
5370 GOSUB 5650
5380 GOSUB5520
5390 Z=RND(7):X=RND(7):Y=RND(7)
5400 GOSUB5690
5410 PRINT@100,"TO ASSIST WITH YOUR CHOICE";
5420 PRINT@130,"YOUR CURRENT POSITION IN THE";
5430 PRINT@162,"CHATEAU WILL BE DISPLAYED IN";
5440 PRINT@194,"THE TOP RIGHT HAND CORNER OF";
5450 PRINT@226,"THE MENU SCREEN.";
5460 PRINT@292,"THE 1ST. VALUE IS THE FLOOR";
5470 PRINT@322,"YOU ARE ON... (0 FOR CELLAR)";
5480 PRINT@356,"THE 2ND. THE X CO-ORDINATE.";
5490 PRINT@388,"THE 3RD. THE Y CO-ORDINATE.";
5500 PRINT@482,"- CHECKING OUT THE HOUSE.....";
5510 RETURN
5520 REM
5530 IFAA=1THENCLS3:AA=0:GOTO5540
5531 CLS
5540 PRINTCHR$(190);
5550 FOR XA=1TO30:PRINTCHR$(188);:NEXTXA
5560 PRINTCHR$(189);
5570 FORXA=1TO13:PRINT@(32*XA),CHR$(186);:PRINT@(32*XA+31),CHR$(181);:NEXTXA
5580 PRINTCHR$(187);
5590 FORXA=1TO30:PRINTCHR$(179);:NEXTXA
5600 PRINTCHR$(183);
5610 SOUND200,2
5620 RETURN
5630 REM
5640 FORAB=1TO2000:NEXTAB:RETURN
5650 PRINT@482,"- PRESS SPACE TO CONTINUE -";
5660 A$=INKEY$:IFA$<>" "THEN5660
5670 SOUND200,2
5680 RETURN
5690 REM
5700 PRINT@57,Z;:PRINT@59,X;:PRINT@61,Y;
5710 PRINT@57,CHR$(186);
5720 PRINT@59,"*";
5730 PRINT@61,"*";
5740 PRINT@89,CHR$(187);
5750 PRINT@90,CHR$(179);
5760 PRINT@91,CHR$(179);
5770 PRINT@92,CHR$(179);
5780 PRINT@93,CHR$(179);
5790 PRINT@94,CHR$(179);
5800 PRINT@63,CHR$(181);
5810 RETURN
5820 REM
5830 GOSUB5520
5840 FORXA=1TO11STEP2
5850 FORYA=1TO18STEP3
5860 PRINT@32*XA+YA+4,CHR$(161);
5870 PRINT@32*XA+YA+5,CHR$(163);
5880 PRINT@32*XA+YA+6,CHR$(162);
5890 PRINT@32*(XA+1)+YA+4,CHR$(165);
5900 PRINT@32*(XA+1)+YA+6,CHR$(170);
5910 NEXTYA,XA
5920 FORXA=1TO18:PRINT@(32*13)+4+XA,CHR$(163);
5930 NEXTXA
5940 PRINT@89,"floor";
5950 PRINT@122,Z;
5960 FORTY=1TO6
5970 FORTX=1TO6
5980 PRINT@32*(TY*2)+(TX*3)+3,T$(TX,TY,Z);
5990 NEXTTX,TY
6000 PRINT@482,"- PRESS SPACE TO CONTINUE -";
6010 PRINT@187,"n";:PRINT@219,"^";
6020 PRINT@249,"w_+-";"e";
6030 PRINT@315,"s";
6040 PRINT@283,"I";
6050 C$=INKEY$
6060 PRINT@32*(Y*2)+(X*3)+3,CHR$(207);
6070 PRINT@32*(Y*2)+(X*3)+3,"#";
6080 C$=INKEY$
6090 IFC$=""THEN6060
6100 RETURN
