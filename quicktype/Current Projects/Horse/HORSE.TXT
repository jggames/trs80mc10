0 ' HORSE.BA Author unknown
100 DIM A(100),L1(50),R1(50),L2(50),R2(50):U$="##.##":CE=0:REM ver 1.5 Advanced Thoroughbred Racing System
105 GOSUB 540:CLS:PRINT"Model 100 Thoroughbred Betting":PRINT:PRINT:PRINT
110 GOSUB 765:GOSUB 505:PRINT:PRINT"Enter Selection Number":PRINT:PRINT"1-Model 100 Thoroughbred Betting"
115 PRINT"2-Bet Return Analysis"
120 PRINT"3-Set Class Data Entry":PRINT"4-End Program":INPUT ZZ:IF (ZZ<1) OR (ZZ>4) THEN 105
125 ON ZZ GOTO 135,415,725,510
130 GOTO 105
135 CLS:PRINT"Model 100 Thoroughbred Betting":PRINT
140 INPUT"Enter Race # ";R:PRINT:IF R<1 THEN GOSUB 780:GOTO 135
145 PRINT"Race Length-Distance":GOSUB 555:TL=L:PRINT
150 INPUT"# Of Entries (26 max) ";U:IF (U<0) OR (U>26) THEN GOSUB 780:GOTO 150
155 CLS:FOR V=1 TO U
160 PRINT"Entry # ";V;"  Race # ";R:GOSUB 505
165 PRINT"Race Length-Last Race":GOSUB 555
170 GOSUB 645:PRINT:GOSUB 660:PRINT
175 PRINT"Speed Rating:":PRINT:PRINT"(Option: Add Speed Rating And Variant)":PRINT"(Ex: 65+15=80 Enter 80 For Speed Rating)";:PRINT
180 INPUT"Last Speed Rating ";A(28):IF A(28)<1 THEN GOSUB 780:GOTO 180
185 PRINT:INPUT"Speed Rating-1 Best Of Last 3 ";A(29):PRINT:IF A(29)<1 THEN GOSUB 780:GOTO 185
190 GOSUB 790:PRINT"Last 3 Positions At 3/4":PRINT
195 INPUT"1st ";X:IF (X<0) OR (X>26) THEN GOSUB 780:GOTO 195
200 INPUT"2nd ";Y:IF (Y<0) OR (Y>26) THEN GOSUB 780:GOTO 200
205 INPUT"3rd ";Z:IF (Z<0) OR (Z>26) THEN GOSUB 780:GOTO 205
210 GOSUB 615:PRINT:PH=PS/4
215 PRINT"Last 3 Positions In Stretch":PRINT
220 INPUT"1st ";X:IF (X<0) OR (X>26) THEN GOSUB 780:GOTO 220
225 INPUT"2nd ";Y:IF (Y<0) OR (Y>26) THEN GOSUB 780:GOTO 225
230 INPUT"3rd ";Z:IF (Z<0) OR (Z>26) THEN GOSUB 780:GOTO 230
235 GOSUB 605:GOSUB 615:PRINT
240 PRINT"Last 3 Finishes":PRINT
245 INPUT"1st ";X:IF (X<0) OR (X>26) THEN GOSUB 780:GOTO 245
250 INPUT"2nd ";Y:IF (Y<0) OR (Y>26) THEN GOSUB 780:GOTO 250
255 INPUT"3rd ";Z:IF (Z<0) OR (Z>26) THEN GOSUB 780:GOTO 255
260 A(28)=A(28)/100:A(28)=5*A(28)
265 A(29)=A(29)/100:A(29)=5*A(29)/1.5
270 X=6-X:IF (X<0) OR (X>5) THEN X=0
275 X=1.5*X
280 Y=6-Y:IF (Y<0) OR (Y>5) THEN Y=0
285 Y=1.5*Y
290 Z=6-Z:IF (Z<0) OR (Z>5) THEN Z=0
295 Y=X+Y+Z:Y=Y/3
300 IF CE=1 THEN GOSUB 680
305 GOSUB 715:INPUT E:IF (E<0) OR (E>1) THEN GOSUB 780:GOTO 305
310 CLS:IF E=0 THEN 160
315 Z=A(28)+A(29)+Y+TG+TP+PH+PS+O1+CL:Z=INT(Z*100+.5)/100
320 A(V)=Z:NEXT V:PRINT"Race # ";R:GOSUB 795:GOSUB 505
325 FOR V=1 TO U:PRINT"Entry ";V,"Rating  ";USING U$;A(V):IF V=7 OR V=14 OR V=21 THEN GOSUB 785
330 NEXT V:PRINT
335 INPUT"Press Enter To Sort High To Low ";Z$
340 FOR V=1 TO U:Z=A(V)
345 FOR Y=1 TO U:IF Z>A(Y) THEN 355
350 Z=A(Y):X=Y
355 NEXT Y
360 A(40+V)=Z:A(X)=0:A(72+V)=X
365 NEXT V
370 CLS:PRINT"Race # ";R:GOSUB 795:GOSUB 505:FOR V=1 TO U:X=72+V:Y=40+V
375 PRINT"Entry ";A(X),"Rating  ";USING U$;A(Y):IF V=7 OR V=14 OR V=21 THEN GOSUB 785
380 NEXT V:PRINT
385 INPUT"Enter 1 For Printout  0 To Continue ";P:IF (P<0) OR (P>1) THEN GOSUB 780:GOTO 385 
390 IF P=1 THEN GOSUB 515
395 PRINT:INPUT"View High To Low AGN 1-Yes 0-No ";V:IF (V<0) OR (V>1) THEN GOSUB 780:GOTO 395
400 IF V=1 THEN 370
405 GOSUB 545:IF V=1 THEN 135
410 GOTO 105
415 CLS:UB$="#####.##":PRINT"Bet Return Analysis":PRINT
420 PRINT:INPUT"# Of Races Bet ";U:IF U<1 THEN GOSUB 780:GOTO 415
425 G=0:Y=0:PRINT
430 FOR Z=1 TO U:PRINT"Race ";Z:PRINT
435 INPUT"Enter Amount Bet ";V:IF V<0 THEN GOSUB 780:GOTO 435
440 Y=Y+V
445 PRINT:INPUT"Enter Money Returned ";W:IF W<0 THEN GOSUB 780:GOTO 445
450 W=W-V:G=W+G
455 PRINT:PRINT"Race ";Z:PRINT:IF W<0 THEN W=0
460 PRINT"Amount + Gain - Loss = $";USING UB$;W:PRINT
465 X=INT((W/V)*100):PRINT"Percent Return = ";X:PRINT:INPUT"Press Enter To Continue ";Z$:PRINT:NEXT Z
470 CLS:PRINT"For ";U;" Races":PRINT
475 PRINT"Total Dollars Bet = $";USING UB$;Y:PRINT
480 PRINT"Total Dollars  + Gain - Loss = $";USING UB$;G
485 Q=INT((G/Y)*100):IF Q<0 THEN Q=0
490 PRINT:PRINT"Percent Return = ";Q
495 PRINT:GOSUB 545:IF V=1 THEN 415
500 GOTO 105
505 FOR AA=1 TO 1050:NEXT AA:RETURN
510 END
515 PRINT:INPUT"Printer Ready? 1-Print  0-ESC ";P:IF (P<0) OR (P>1) THEN GOSUB 780:GOTO 515
520 IF P=1 THEN 530
525 RETURN
530 LPRINT"Race # ";R,:GOSUB 800:LPRINT" ":FOR V=1 TO U:X=72+V:Y=40+V
535 LPRINT"Entry ";A(X),"Rating  ";USING U$;A(Y):NEXT V:LPRINT" ":P=0:RETURN
540 A(28)=0:A(29)=0:X=0:Y=0:Z=0:G=0:R=0:TL=0:TG=0:L=0:O1=0:O2=0:TP=0:PH=0:PS=0:CL=0:AJ=0:RETURN
545 PRINT:INPUT"Another Run 1-Yes 0-No ";V:IF (V<0) OR (V>1) THEN GOSUB 780:GOTO 545
550 RETURN
555 PRINT"1) 4 f       7) 7 f     13) 1 5/16 miles";
560 PRINT"2) 4 1/2 f   8) 7 1/2 f 14) 1 3/8  miles";
565 PRINT"3) 5 f       9) 1 mile  15) 1 7/16 miles";
570 PRINT"4) 5 1/2 f  10) 1 m 70y 16) 1 5/8  miles";
575 PRINT"5) 6 f      11) 1 1/16 miles"
580 PRINT"6) 6 1/2 f  12) 1 1/8  miles"
595 INPUT"Enter # (1-16)";L:IF (L<1) OR (L>16) THEN GOSUB 780:GOTO 595
600 RETURN
605 TG=L-TL:IF (TG>0) AND ((X>0) AND (X<5)) THEN TG=.25:RETURN
610 TG=0:RETURN
615 X=6-X:IF (X<0) OR (X>5) THEN X=0
620 X=(1.5*X)/2
625 Y=6-Y:IF (Y<0) OR (Y>5) THEN Y=0
630 Y=(1.5*Y)/2
635 Z=6-Z:IF (Z<0) OR (Z>5) THEN Z=0
640 Z=Z/2:PS=(X+Y+Z)/3:RETURN
645 PRINT:INPUT"Enter Today's Post Position ";TP:IF (TP<1) OR (TP>26) THEN GOSUB 780:GOTO 645
650 IF (TP>0) AND (TP<5) THEN TP=.3:RETURN
655 TP=0:RETURN
660 PRINT"Enter Morning Line Odds (1st #,2nd #)":PRINT"(Example: For Odds Of 9/2 Enter 9,2)"
665 INPUT O1,O2:IF (O1<1) OR (O2<1) THEN GOSUB 780:GOTO 660
670 IF (O1/O2>3) AND (O1/O2<13) THEN O1=.25:RETURN
675 O1=0:RETURN
680 GOSUB 790:CD=0:PRINT"Class Data":PRINT
685 PRINT"Enter 1 For Down In Class"
690 PRINT"      2 For Up In Class":PRINT"      0 For No Change"
695 INPUT CD:IF (CD<0) OR (CD>2) THEN GOSUB 780:GOTO 680
700 IF CD=1 THEN CL=1.5
705 IF CD=2 THEN CL=-1
707 IF CD=0 THEN CL=0
710 CLS:RETURN
715 PRINT:PRINT"Entry # ";V;" Data Correct?":PRINT:PRINT"Enter 1 To Continue":PRINT"      0 To Re-enter Data"
720 RETURN
725 CLS:GOSUB 765:GOSUB 505
730 PRINT"Set Class Data Entry":PRINT
735 PRINT"1-Activate Class Data Entry"
740 PRINT"2-Deactivate Class Data Entry"
745 PRINT"3-Return To Main Menu":INPUT CD:IF (CD<1) OR (CD>3) THEN GOSUB 780:GOTO 725
750 IF CD=1 THEN CE=1
755 IF CD=2 THEN CE=0
760 CLS:GOTO 110
765 IF CE=1 THEN PRINT"Activated-Class Data Entry-Activated":GOTO 775
770 IF CE=0 THEN PRINT"Deactivated-Class Data Entry-Deactivated"
775 RETURN
780 PRINT:PRINT"Invalid Entry-Re-Enter":GOSUB 505:PRINT:RETURN
785 INPUT"Press Enter To Continue ";Z$:RETURN
790 CLS:PRINT"Entry # ";V;"  Race # ";R:PRINT:RETURN
795 PRINT"Model 100 Thoroughbred Betting":RETURN
800 LPRINT"Model 100 Thoroughbred Betting":RETURN
