0 CLS0:DIMCC,ZZ,Z$,M,M$,PP(99),PG(99),DC(99),NI(50),IM(50):M=16384:GOTO120
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(Z$,CC,1):IFI$<"!"THEN?MID$(Z$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:Z$="":RETURN
5 FORCC=1TO25:Z$=INKEY$:IFZ$<>""THENCC=25
6 NEXT:RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORZZ=1TOLEN(Z$):POKECC+ZZ,ASC(MID$(Z$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
8 CC=(PEEK(17024)AND1)*256+PEEK(17025):POKECC+M,ASC(Z$)AND63:PRINT@CC+1,:RETURN
9 CC=(PEEK(17024)AND1)*256+PEEK(17025):PRINT@CC,M$;:PRINT@CC-1,:RETURN
10 PRINT@32*13+27,;:Z$=LEFT$(STR$(NP-NI-IM)+"   ",4):GOSUB7
11 PRINT@32*12+27,;:Z$=LEFT$(STR$(NI)+"   ",4):GOSUB7
12 PRINT@32*14+27,;:Z$=LEFT$(STR$(IM)+"   ",4):GOSUB7
13 RETURN

120 I=RND(-(PEEK(9)*256+PEEK(10)))
150 QP=515:QA=126
160 IG=96:VI=85:CO=67:GI=106
170 GOTO1360
180 CLS3:PRINT@1*32+1,;:Z$="HOW MANY ISLANDERS? ":GOSUB7:GOSUB1720:NP=VAL(Z1$)
190 IFNP=0THEN180
200 IFNP>99THEN180
210 PRINT@3*32+1,;:Z$="HOW MANY INFECTED AT START? ":GOSUB7:GOSUB1720:NI=VAL(Z1$)
220 IFNI=0ORNI>NPTHEN210
240 PRINT@5*32+1,;:Z$="CONTAGION TIME (DAYS)? ":GOSUB7:GOSUB1720:CT=VAL(Z1$)
250 IFCT=0THEN240
270 GOSUB1880
310 PRINT@0,:Z$="TIME"+STR$(CT):GOSUB7
360 FORCC=NPTO99:PP(CC)=.:PG(CC)=.:DC(CC)=.:NEXT
370 FORI=1TONP:PG(I)=VI:NEXT
380 FORI=1TONI:PG(I)=CO:NEXT
390 FORI=1TONP:DC(I)=0:NEXT
400 FORI=1TONI:DC(I)=CT:NEXT
410 FORI=1TONP
420 PP(I)=RND(511)+M:IFPEEK(PP(I))<>IGTHEN420
430 POKEPP(I),PG(I):NEXT
440 ND=0
450 IM=0:GOSUB10
460 PRINT@480,;:Z$="DAY"+STR$(ND):GOSUB7
470 ND=ND+1:NI=0:IM=0
480 FORI=1TONP
490 REM NEW CONTAGIOUS FROM LAST TIME
500 IF(PEEK(PP(I))=CO)AND(PG(I)=VI)THEN DC(I)=CT
510 PG(I)=PEEK(PP(I))
520 ONRND(9)GOSUB900,910,920,930,940,950,960,970,980
530 REM INFECTING?
540 IFPG(I)=COTHENGOSUB1060
550 REM INFECTED?
560 IFPG(I)=VITHENGOSUB1160
570 REM ONE DAY LESS
580 IFPG(I)=COTHEN DC(I)=DC(I) -1
590 REM END CONTAGION
600 IFDC(I)<0THENPG(I)=GI:POKEPP(I),GI
610 IFPEEK(PP(I))=COTHENNI=NI+1
620 IFPEEK(PP(I))=GITHENIM=IM+1
630 NEXT
640 IFND<=50THENNI(ND)=NI:IM(ND)=IM
650 GOSUB10
660 IFNI>0THEN460
670 GOSUB1250
680 GOSUB2000:PRINT@0,;:Z$="CONTAGIOUSNESS & INFECTION RATES":GOSUB7
690 FORND=1TO50:CC=NP+NI(ND)+IM(ND)
730 ZZ=NI(ND):IFNP-NI(ND)-IM(ND)=NPTHEN810
740 X=ND+3:FORY=11TO11-INT((ZZ*100/CC)/10)STEP-1:RESET(X,Y):NEXT
750 ZZ=IM(ND):IFNP-NI(ND)-IM(ND)=NPTHEN810
760 FORY=25TO25-INT((ZZ*100/CC)/10)STEP-1:RESET(X,Y):NEXT
810 NEXTND
850 PRINT@480,:Z$="WANT ANOTHER TRY (Y/N)? ":GOSUB7:GOSUB1720:Q$=Z1$
860 IFQ$=""THEN850
870 IFLEFT$(Q$,1)="Y"THEN180
880 END
890 REM MOVE S/R'S
900 MV=31:GOSUB1000:RETURN
910 MV=32:GOSUB1000:RETURN
920 MV=33:GOSUB1000:RETURN
930 MV=-1:GOSUB1000:RETURN
940 MV=0:GOSUB1000:RETURN
950 MV=1:GOSUB1000:RETURN
960 MV=-33:GOSUB1000:RETURN
970 MV=-32:GOSUB1000:RETURN
980 MV=-31:GOSUB1000:RETURN
990 REM MAKE MOVE
1000 IFPEEK(PP(I)+MV)<>IGTHENRETURN
1010 PG(I)=PEEK(PP(I))
1020 POKEPP(I),IG
1030 PP(I)=PP(I)+MV
1040 POKEPP(I),PG(I):RETURN
1050 REM INFECTING OTHERS
1060 IFPEEK(PP(I)-33)=VITHENPOKEPP(I)-33,CO
1070 IFPEEK(PP(I)-32)=VITHENPOKEPP(I)-32,CO
1080 IFPEEK(PP(I)-31)=VITHENPOKEPP(I)-31,CO
1090 IFPEEK(PP(I)-1)=VITHENPOKEPP(I)-1,CO
1100 IFPEEK(PP(I)+1)=VITHENPOKEPP(I)+1,CO
1110 IFPEEK(PP(I)+31)=VITHENPOKEPP(I)+31,CO
1120 IFPEEK(PP(I)+32)=VITHENPOKEPP(I)+32,CO
1130 IFPEEK(PP(I)+33)=VITHENPOKEPP(I)+33,CO
1140 RETURN
1150 REM INFECTION FROM OTHERS
1160 IFPEEK(PP(I)-33)=COTHENPOKEPP(I),CO
1170 IFPEEK(PP(I)-32)=COTHENPOKEPP(I),CO
1180 IFPEEK(PP(I)-31)=COTHENPOKEPP(I),CO
1190 IFPEEK(PP(I)-1)=COTHENPOKEPP(I),CO
1200 IFPEEK(PP(I)+1)=COTHENPOKEPP(I),CO
1210 IFPEEK(PP(I)+31)=COTHENPOKEPP(I),CO
1220 IFPEEK(PP(I)+32)=COTHENPOKEPP(I),CO
1230 IFPEEK(PP(I)+33)=COTHENPOKEPP(I),CO
1240 RETURN
1250 PRINT@32*15+21,"HIT ANY KE";:POKE16895,ASC("Y")
1260 Q$=INKEY$:IFQ$=""THEN1260
1270 RETURN
1280 REM *** VARIABLES ***
1290 REM NP=# OF PEOPLE,NI=# INFECTED,IM=# OF IMMUNE
1300 REM CT=DAYS FOR CONTAGIOUS,IG=ISLANDGRAPHIC,PP=POS OF PEOPLE
1310 REM ND=# OF DAYS,VI=NOTYETINFECTED
1320 REH CO=CONTAGIOUS,PG=PEOPLEGRAPHIC
1330 REM GI=GRAPHIC IMMUNE,MV=MOVE,DC=DAYS OF CONTAGION LEFT
1340 REM *****************
1350 REM TITLES
1360 CLS4
1380 PRINT@7*32+12,;
1390 Z$="EPIDEMIC":GOSUB7
1410 PRINT@9*32+8,;
1420 Z$="ANDY GAMBLE 1981":GOSUB7
1430 PRINT@448,;:Z$="DO YOU NEED INSTRUCTIONS? ":GOSUB7:GOSUB1720:Q$=Z1$
1440 IFQ$=""THEN1430
1450 IFLEFT$(Q$,1)<>"Y"THEN180
1460 REM INSTRUCTIONS
1470 CLS:Z$="AN EPIDEMIC HAS BROKEN OUT ON A SMALL ISLAND.":GOSUB1
1480 Z$="THE DISEASE IS NOT FATAL, AND ONCE CONTRACTED PROVIDES IMMUNITY FOR LIFE.":GOSUB1
1500 Z$="YOU ARE ALLOWED TO CHOOSE CERTAIN INITIAL CONDITIONS:":GOSUB1
1510 PRINT"- ISLAND POPULATION"
1520 PRINT"- NUMBER ORIGINALLY INFECTED"
1530 PRINT"- THE NUMBER OF DAYS THE"
1540 PRINT"  DISEASE IS CONTAGIOUS
1550 Z$="THE DURATION OF THE EPIDEMIC WILL VARY DEPENDING ON YOUR INPUTS.":GOSUB1
1590 GOSUB1250
1600 CLS:Z$="THE ISLANDERS WILL MOVE ABOUT RANDOMLY.":GOSUB1:Z$="IF THEY ARE CONTAGIOUS THEY WILL INFECT OTHERS.":GOSUB1
1610 Z$="THIS WILL CONTINUE UNTIL THE DISEASE HAS RUN ITS COURSE AND THERE ARE NO MORE CONTAGIOUS.":GOSUB1
1620 Z$="YOU WILL THEN BE GIVEN A DAY-BY-DAY BAR CHART OF THE HISTORY OF THE EPIDEMIC UP TO A MAX OF 50 DAYS.":GOSUB1
1650 GOSUB1250
1660 GOTO180
1720 Z$="":Z1$="":M$=CHR$(PEEK((PEEK(17024)AND1)*256+PEEK(17025)+M))
1730 PRINTM$;:GOSUB5:PRINTCHR$(8);:IFZ$<>""THEN1760
1740 PRINT" ";:GOSUB5:PRINTCHR$(8);:IFZ$=""THEN1730
1760 IFZ$<>CHR$(8)THEN1810
1770 IFZ$=""THEN1730
1780 ZZ=LEN(Z1$):IFZZ<1THEN1730
1790 Z1$=LEFT$(Z1$,ZZ-1):GOSUB9:GOTO1730
1810 IFZ$=CHR$(13)THENPRINTM$M$M$;:GOTO1850
1820 GOSUB8:Z1$=Z1$+Z$:GOTO1730
1850 FORI=1TO10:Z$=INKEY$:NEXTI
1870 RETURN
1875 GOSUB1880
1876 GOTO1876
1880 CLS3
1881 PRINT"day¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
1882 PRINT"¯¯¯¯¯¯¯   ¯¯¯          ¯     ¯¯¯";
1883 PRINT"¯¯¯¯       ¯                   ¯";
1884 PRINT"¯¯¯                            ¯";
1885 PRINT"¯¯                            ¯¯";
1886 PRINT"¯¯                            ¯¯";
1887 PRINT"¯                           ¯¯¯¯";
1888 PRINT"¯                            ¯¯¯";
1889 PRINT"¯                             ¯¯";
1890 PRINT"¯                              ¯";
1891 PRINT"¯¯¯                  ¯¯   ¯   ¯¯";
1892 PRINT"¯¯¯¯                ¯¯¯¯¯¯¯¯¯¯¯¯";
1893 PRINT"¯¯¯¯               ¯¯Contagious¯";
1894 PRINT"¯¯¯¯¯¯              ¯Uninfected¯";
1895 PRINT"¯¯¯¯¯¯¯     ¯¯¯¯    ¯*infected¯¯";
1896 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";:RETURN
2000 CLS1
2001 PRINT@32,;
2002 PRINT"0ˆ%POP";
2003 PRINT"8ˆCON-";
2004 PRINT"6ˆTAG-";
2005 PRINT"4ˆIOUS";
2006 PRINT"2ˆ";
2007 PRINT"0Š‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚"
2008 PRINT"DAYS  10   20   30   40   50"
2009 PRINT"0ˆ%POP";
2010 PRINT"8ˆINF-";
2011 PRINT"6ˆECT-";
2012 PRINT"4ˆED  ";
2013 PRINT"2ˆ";
2014 PRINT"0Š‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚‚"
2015 PRINT"DAYS  10   20   30   40   50";:RETURN
3000 REM EPIDEMIC : ANDY GAMBLE, AUG 80
3010 REM COLUMBIA COLLEGE, 1619 W10 AVE
3020 REM VANCOUVER BC V6J 2A2
