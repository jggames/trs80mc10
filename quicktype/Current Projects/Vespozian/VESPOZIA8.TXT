0 GOSUB7000:CLEAR450:DIMCC,ZZ,M$,Z$,N(21):MC=16384:CC=1:GOTO15
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:Z$=MID$(M$,CC,1):IFZ$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFZ$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 FORCC=1TO10:PRINTM$;:FORZZ=1TO250:NEXT:GOSUB4:FORZZ=1TO250:NEXT:NEXT:PRINTM$:RETURN
4 FORZZ=1TOLEN(M$):PRINTCHR$(8);:NEXT:RETURN
5 PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
8 FORCC=1TOLEN(M$)STEP2:SOUNDN(ASC(MID$(M$,CC,1))-64),VAL(MID$(M$,CC+1,1)):NEXT:RETURN
9 FORZZ=1TO2500:NEXT:RETURN
10 FORZZ=1TO25:NEXT:RETURN
11 SOUND215,8:GOSUB10:SOUND215,8:GOSUB10:SOUND217,8:GOSUB9:RETURN
12 INPUTM$:AA$=LEFT$(M$+"              ",14):RETURN
15 FORZZ=1TO63STEP3:N(CC)=VAL(MID$("087103113129142149161171181185193200203209215219221223229231233",ZZ,3)):CC=CC+1
16 NEXT
20 DIML$(15),B$(15),O$(19),R$(12),E$(15),D$(15),Q$(5),P(19),C(19)
25 FORI=1TO15:READL$(I):NEXT:FORI=1TO15:READB$(I):NEXT:FORI=1TO19:READO$(I):NEXT:FORI=1TO12:READR$(I):NEXT
30 FORI=1TO15:READE$(I):NEXT:FORI=1TO15:READD$(I):NEXT:FORI=1TO19:READP(I):NEXT:FORI=1TO19:READC(I):NEXT:C(0)=-2
40 PQ=0:GR=0:A$="NOTHING":K1=1:K2=1:FORI=1TO4:Q$(I)="":NEXT:IN=0:VS$="VESPOZIAN     "
50 JE=0:PN=2:PL=PN:CT=0:JM=-1:PV=6:CY=143:PF=600:CD=2:ST=1:PI=999:SF=1:SG$="STARGATE      "
60 V$="TAKDROLIFLOOEXAOPEUNLLOCCLOWEAREMINSRAIPREKILSLE"
70 N$="PORSTAFORAFTUP DOWOUTBUNSHUNOREASWESBULDOOCRAVES":P$="PSFAUDOBTNEWLRCV"
80 T$="SHUDEVPOLCRAASBSPACONCYLTONBEZGRAMAC005001002003004KEYROCDISPILDOOBUTAROSCRBUNSHE":GOSUB7075
90 Q$(1)="COMPUTER & VIDEO GAMES":Q$(2)="WELCOME YOU TO":Q$(3)="THE SEVENTH EMPIRE ADVENTURE"
100 IFPN=6ANDPL=10THENRT=6:GOTO102
101 IFPN<>6THENRT=0
102 P(10)=6:P(11)=6
103 IFP(8)=10OR(P(7)=10ANDC(7)>2)THENSF=1:GOTO105
104 SF=0
105 IFPV=4ANDP(3)=99THENP(3)=5
106 IFPN=3ANDC(0)=-4ANDC(5)<5THENGOSUB3530:GOTO6050
107 IFC(7)=3ANDP(7)=10THENC(7)=4:PF=PF+2000:Q$(3)="NEW FUEL LOADED"
108 CT=CT+1:IFST=1THENJE=JE+1
109 IFJE<25THEN113
110 IFST=1THENJE=JE+1:IFJE>24THENJE=0:PV=PV+JM:CLS:IFPV=CDTHEN112
111 M$="VESPOZIAN PASSING "+R$(PV):GOSUB1:GOSUB11:GOTO113
112 ST=2:PF=PF-100:CLS:M$="VESPOZIAN IN ORBIT AROUND "+R$(PV):GOSUB1:GOSUB11
113 IFRT=6THENP(10)=99:P(11)=7:GOTO115
114 IFPN=6ANDRT=0THENP(10)=6:P(11)=6
115 IFCT>200THENCT=1:CY=CY+1
116 IFCD=PVTHENST=2
117 IFST=2THENW$="ORBITING":GOTO119
118 W$="BEARING ON"
119 IFPN=10ANDC(5)<>5THEN6010
120 IFPN=14ANDA$="GO NORTH"THEN6055
121 IFPV=3ANDPN=5ANDP(13)=99THENP(13)=5
125 IFP(3)=14THENP(3)=88:PI=CY:Q$(3)="POLYPS VANISH INTO ROCKS"
126 IFCY>PI+19THENP(19)=14:PI=CY
129 IFP(2)=PNAND(PN=9ORPN=14)AND(P(19)<>14ORP(7)<>14)THENQ$(3)="SWITCH STARTS MOTOR. DRILL":Q$(4)="ENGAGES AND SPRAYS DUST!"
130 IFP(19)=14ANDP(2)=14ANDP(7)=14ANDC(7)=2THEN134
131 GOTO135
134 P(19)=88:O$(7)="FUEL CONTAINER":O$(8)=" ":C(7)=3:Q$(3)="SWITCH STARTS MOTOR. DRILL":Q$(4)="ENGAGES AND FILLS CONTAINER"
135 IFP(18)=55THENC(4)=3
140 IFC(5)>4ANDC(5)<7AND(PN=2ORPN=7ORPN=8ORPN=11)THENIG=IG+1:GOTO145
141 IG=0
145 IFIG>0THENP(11)=PN:IFIG=7THENGOSUB3530:GOTO6070
146 IFP(11)=PNTHENIFRND(6)=3THENQ$(1)="EVER GET THE FEELING...?"
150 IFP(5)<>88ANDP(5)<>PNANDP(5)<>55ANDP(5)<>12ANDP(5)<>66THENP(5)=88:Q$(1)="STRANGE FEELING...":GOTO160
155 IFP(6)<>PNANDP(6)<>88ANDP(6)<>55ANDP(6)<>12ANDP(6)<>66THENP(6)=88:Q$(1)="ODD-- I THOUGHT..."
160 IFC(4)=2ANDP(18)<>55ANDP(18)<>66ANDP(18)<>99THENP(18)=88:Q$(1)="FUNNY-- SOMETHING'S MISSING...":C(18)=3
170 IFP(11)=2ANDPN=2ANDK1=14THENGOSUB3530:GOTO6080
175 IFPN=7ORPN=8ORPN=11ORPN=2THENIFRND(3)=2THENP(11)=PN:GOTO180
177 IFPN<>6THENP(11)=88
180 IFP(18)=55ANDPN<>12ANDPN<>11ANDC(18)>2THENGOSUB3530:GOTO6100
185 IFC(0)=3ANDPN<>2ANDPN<>3THENGOSUB3530:GOTO6110
200 FORI=1TOLEN(E$(PN))
210 IFMID$(E$(PN),I,1)="P"THENS$=S$+"PORT, "
220 IFMID$(E$(PN),I,1)="S"THENS$=S$+"S'BOARD, "
230 IFMID$(E$(PN),I,1)="F"THENS$=S$+"FOR'D, "
240 IFMID$(E$(PN),I,1)="A"THENS$=S$+"AFT, "
250 IFMID$(E$(PN),I,1)="U"THENS$=S$+"UP, "
260 IFMID$(E$(PN),I,1)="D"THENS$=S$+"DOWN, "
270 IFMID$(E$(PN),I,1)="O"THENS$=S$+"OUT, "
280 NEXT:IFS$<>""THENS$=LEFT$(S$,LEN(S$)-2)+"."
330 CLS:M$="I AM "+L$(PN):GOSUB1:M$=B$(PN):GOSUB1:GOSUB5
331 IFLEN(S$)>0THENM$="EXITS: "+S$:GOSUB1
335 IFPN=9ORPN=14THENPRINT"WHICH WAY?"
340 II=1:IFPN=15ANDK2<>21THEN346
342 M$="":FORI=1TO19:IFP(I)=PNTHENM$=M$+O$(I)+", "
344 NEXT:IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2)+"."
345 M$="VISIBLE: "+M$:GOSUB1
346 IFPN=5THENPRINTVS$
360 GOSUB5:IFA$<>""THENM$="-----YOU SAID "+A$:GOSUB1
365 PRINT:FORI=1TO4:IFQ$(I)<>""THENM$=Q$(I):GOSUB1
370 NEXT
375 IFMID$(Q$(2),1,2)="OK"THENSOUND183,1
380 PRINT:PRINT"-----WHAT NOW";
390 PL=PN:FORI=1TO5:Q$(I)="":NEXT:A$="":G$="":H$="":I$="":J$="":S$="":INPUTA$
400 IFLEN(A$)<3THEN4000
401 H$=LEFT$(A$,3)
402 IFH$="INV"THEN460
403 IFH$="WAI"THEN500
404 IFH$="QUI"THEN6500
405 IFH$="HEL"THEN600
406 IFH$="SLE"THEN1800
410 J=0:FORI=1TOLEN(A$):IFMID$(A$,I,1)=" "THENJ=I
411 NEXT:IFJ=0THEN4110
412 G$=LEFT$(A$,J-1):I$=RIGHT$(A$,LEN(A$)-J):J$=LEFT$(I$,3)
414 IFG$="GO"ORH$="GET"THEN425
415 X$=V$:Y$=H$:GOSUB3500:IFJ=0THENQ$(2)="I DON'T KNOW HOW TO "+G$:GOTO100
416 K1=INT((J-1)/3+1)
417 X$=T$:Y$=J$:GOSUB3500
419 IFJ=0THENQ$(2)="WHAT IS A "+I$+"?":GOTO100
420 K2=INT((J-1)/3)+1
421 ONK1GOTO430,450,700,800,800,900,1000,1100,1200,1300,1400,1500,700,1600,1700,1800
425 X$=N$:Y$=J$:GOSUB3500:IFJ=0THEN4010
426 X$=E$(PN):Y$=MID$(P$,INT((J-1)/3+1),1):GOSUB3500:IFJ=0THEN4010
427 PN=ASC(MID$(D$(PN),J,1))-64:GOTO4020
430 IFK2=20THEN4030
431 IFP(11)=PNTHEN6080
432 IFK2>19THEN4070
433 IFP(K2)=55THEN4040
434 IFP(K2)<>PNTHEN4050
435 IFC(K2)=-2THEN4000
436 IFC(K2)=-1THEN4060
440 IF(K2=8OR(K2=7ANDC(7)=3))ANDP(9)<>55THENQ$(2)="TOO HOT TO HANDLE":GOTO100
441 IFIN>3THENQ$(2)="I'M OVERLOADED ALREADY!":GOTO100
442 IN=IN+1:P(K2)=55:GOTO4020
450 IFK2>19THEN4070
451 IFP(K2)<>55THEN4070
452 IF(K2=5ORK2=6)ANDC(5)=K2THEN1302
453 IFK2=2AND(PN<>9ANDPN<>14)THENGOSUB3530:GOTO6060
454 IN=IN-1:IFPN=15THENP(K2)=66:Q$(3)="SLIPPED IT UNDER PILLOW":GOTO4020
455 P(K2)=PN:GOTO4020
460 IFIN=0THENQ$(2)="NOTHING":GOTO100
462 J=1:M$="":FORI=1TO19:IFP(I)=55THENM$=M$+O$(I)+", "
470 NEXT:IFM$<>""THENM$=LEFT$(M$,LEN(M$)-2)+"."
480 M$="I AM CARRYING: "+M$:GOSUB1:GOSUB3520:GOTO100
500 CT=CT+15:IFST=1THENJE=JE+15
510 GOTO4020
600 Q$(2)="LOOK AROUND AND":Q$(3)="EXAMINE THINGS. TRY WORDS":Q$(4)="LIKE INSERT, LIFT, WAIT ETC.":GOTO100
700 IFK2<>21ORPN<>15THEN4000
701 Q$(2)="NOTHING!":FORI=1TO19:IFP(I)=66THENP(I)=15:Q$(2)="IT WAS STILL THERE!"
702 NEXT:GOTO100
800 IFK2<>24THEN812
801 IFPN=8THENQ$(2)="I SEE BUTTON MARKED":Q$(3)="-THINK CONTROL-":GOTO100
802 IFPN=11ANDLEFT$(E$(11),3)="PD "THENE$(11)="PDL"+MID$(E$(11),4):Q$(2)="WOW!":B$(11)=B$(11)+" AND A HIDDEN BULKHEAD":GOTO100
805 IFPN=10ANDE$(10)="S   "THENE$(10)="SC  ":P(4)=10:Q$(2)="AHA!":GOTO100
806 IFPN=7THENQ$(2)="COURSE DATA ON SCREEN":GOTO100
807 IFPN=3THENIFC(0)=-3THENQ$(3)="INNER DOOR OPEN.":Q$(4)="OUTER DOOR CLOSED.":GOTO4020
808 IFPN=3THENIFC(0)=-4THENQ$(3)="INNER DOOR CLOSED.":Q$(4)="OUTER DOOR OPEN.":GOTO4020
812 IFK2=20THEN4030
813 IFK2=2THENIFP(2)<>55ANDP(2)<>PNTHEN4030
814 IFK2=2THENQ$(2)="IT HAS HOLLOW CYLINDRICAL CUTTER":Q$(3)="AND SENSITIVE PRESSURE SWITCH":GOTO100
815 IF(PN=3ORPN=2)ANDK2=23THENQ$(2)="SIGN -AIRLOCK CONTROL-":GOTO100
820 IFK2<18ANDK2>12THENIFP(K2)<>55THEN4070
821 IFK2<18ANDK2>12THENONK2-12GOTO870,871,872,873,874
825 IFK2=21ANDPN=15THENQ$(3)="LOOKS STRANGELY LUMPY...":GOTO4020
826 IFK2=25ANDPN=7THEN4500
827 IFK2=10ANDPN=6ANDP(10)=6THEN4100
830 Q$(3)="NOTHING SPECIAL":GOTO4020
870 Q$(2)="LABEL -YUSES-":GOTO100
871 Q$(2)="LABEL -POLYPS-":GOTO100
872 Q$(2)="LABEL -FUNUS-":GOTO100
873 Q$(2)="LABEL -CONFIDENTIAL-":GOTO100
874 Q$(2)="LABEL -BLOODLINE-":GOTO100
900 IFK2<>22THEN4000
901 IFPN<>11THEN4050
902 IFP(11)=11ANDPN=11THEN6100
904 IFC(18)=2THENQ$(2)="LOCKED!":GOTO100
905 IFC(18)=4THENQ$(2)="ALREADY OPEN":GOTO100
906 IFP(8)=11THEN6000
910 C(18)=4:E$(11)=LEFT$(E$(11),3)+"R":GOTO4020
1000 IFK2<>22THEN4000
1001 IFPN<>11THEN4050
1002 IFC(18)>2THEN4090
1003 IFP(18)<>55THEN4060
1004 C(18)=3:GOTO4020
1100 IFK2<>22THEN4000
1101 IFPN<>11THEN4050
1103 IFP(11)=11ANDPN=11THEN6080
1104 IFC(18)<>3ORP(18)<>55THEN4060
1106 C(18)=3:GOTO4020
1200 IFK2<>22THEN4000
1201 IFPN<>11THEN4050
1203 IFP(11)=11ANDPN=11THEN6080
1204 IFC(18)<>4THEN4060
1206 C(18)=3:E$(11)=LEFT$(E$(11),3):GOTO4020
1300 IFK2<>5ANDK2<>6THEN4000
1301 IFP(K2)<>55THEN4070
1302 IFC(5)=K2THENQ$(2)="WHAT'S THIS I'M WEARING THEN?":GOTO100
1303 IFC(5)>3THENQ$(2)="WEARING "+LEFT$(O$(C(5)),LEN(O$(C(5)))-7):GOTO100
1304 C(5)=K2:O$(K2)=O$(K2)+" (WORN)":GOTO4020
1400 IF(K2<>5ANDK2<>6)ORC(5)=2THEN4080
1401 C(5)=2:O$(K2)=LEFT$(O$(K2),LEN(O$(K2))-7):GOTO4020
1500 IFK2=20THEN4030
1501 IFK2<13ORK2>17THEN4000
1502 IFP(K2)<>55THEN4070
1503 IFPN<>7THENQ$(2)="NO COMPUTER TO TAKE IT":GOTO100
1505 IFP(11)=7ANDK2>12ANDK2<18THENGOSUB3530:GOTO6090
1510 CLS:PRINTO$(K2):PRINT"SPINS IN ITS DRIVE...":SOUND87,4:SOUND232,4:GOSUB9:CLS:ONK2-12GOTO1550,1555,1560,1565,1570
1550 PRINT"-YUSES SYSTEM-":M$="STAR SYSTEM ADJACENT TO FUNUS. IT HAS 3 PLANETS:":GOSUB1:PRINT
1551 PRINTR$(11):PRINT" -BALL OF FIRE-":PRINTR$(10):PRINT" -MINING COLONY, NO FINDS-"
1552 PRINTR$(9):PRINT" -REMOTE DEAD PLANET":GOTO1580
1555 PRINT"-POLYPS-":M$=" -FREQUENT GALAXY IN MILLIONS AROUND ENERGY RICH PLANETS.":GOSUB1
1556 PRINT" -REGENERATE EVERY 20 YEARS.":M$=" -ACTIVE POLYPS CAN TURN DEAD PLANET INTO GREENHOUSE EFFECT "
1557 M$=M$+"PLANET, RICH IN STELLAR ENERGY, AFTER 20 YEARS...":GOSUB1:GOTO1580
1560 PRINT"-FUNUS SYSTEM-":M$="STAR WITH 7 PLANETS. IN ORDER FROM FUNUS:":GOSUB1:PRINT:PRINTR$(2)" -MOLTEN ROCK"
1561 PRINTR$(3)" -UNINHABITED":PRINTR$(4)" -GR'NHOUSE EFFECT";:PRINTR$(5)" -INHABITED"
1562 PRINTR$(6)" -ICY WASTE":PRINTR$(7)" -ICY WASTE":PRINTR$(8)" -LUMP OF ROCK":GOTO1580
1565 PRINT"-CAPTAIN'S LOG-":M$="BEARING TO DOCK WITH BLOODLINE STARGATE IN GYRATES ORBIT.":GOSUB1
1567 M$="MISSION ACCOMPLISHED-- D'TAAN'S EXPERIMENTS CONFIRM POLYP THEORY. PITY BLOODLINE WILL BENEFIT!":GOSUB1
1568 M$="GLAD TO GET SHOT OF THIS CREW! SUSPECT GRAKTA IS BLOODLINE SPY.":GOSUB1:M$="MACHEN, "
1569 M$=M$+"THE TIAITHEN LIZARD GIVES ME THE CREEPS. CAN'T GET RID OF HIM-- THE ONLY PILOT ABOARD!":GOSUB1:GOTO1580
1570 PRINT"-BLOODLINE-":M$="TYRANNICAL EMPIRE KEEPING CONTROL ON ENERGY SUPPLIES FROM GREENHOUSE PLANETS--":GOSUB1
1571 M$="ON WHICH INTERSTELLAR TRAVEL DEPENDS. STARGATE IN FUNUS SYSTEM CAN SUMMON CRUISERS TO QUELL REBELS.":GOSUB1
1580 FORCC=1TO8:SOUNDRND(250),2:GOSUB9:NEXT:Q$(2)="YOU REMOVE THE DISK...":GOTO100
1600 IFK2<>23THEN4020
1602 IFP(11)=2ANDPN=2THEN6000
1603 IFPN=8THENIFP(11)=8THEN6000
1604 IFPN=8THENQ$(2)="MACHEN, THE LIZARD PILOT":Q$(3)="RESTRAINS YOU":GOTO100
1606 CLS:IFPN<>13THEN1661
1607 IFPF<1THEN6020
1608 GOSUB3515:PRINT"HEADING,(H)":PRINT"CRYOGEN,(C)":PRINT"LASER,(L)":PRINT"END OF INPUT,(E)"
1612 M$=INKEY$:IFM$="H"THEN1618
1613 IFM$="C"THEN1633
1614 IFM$="L"THEN1639
1615 IFM$="E"THEN4020
1616 GOTO1612
1618 J=0:GOSUB3515:M$="CURRENTLY: "+W$:GOSUB1:PRINTR$(CD):PRINT:PRINT"NEW DESTINATION";:GOSUB12
1620 FORI=1TO12:IFAA$=R$(I)THENJ=I
1621 NEXT:IFJ=0THENPRINTAA$" ";:GOSUB1660:GOTO1606
1624 IFJ=CDTHEN1606
1625 IFJ>PVTHENJM=1:GOTO1627
1626 JM=-1
1627 CLS:ST=1:JE=0:CD=J:PF=PF-100:PRINT:PRINT"COURSE CORRECTION APPLIED":PRINT:PRINT"NEW BEARING:"
1628 PRINTR$(CD):PRINT:PRINT"FUEL:"PF"PICORATH":GOSUB3520
1629 IFPF<1THEN6020
1630 GOTO1606
1633 GOSUB3515:PRINT:PRINT"CRYOGENIC CONTROL":PRINT "ENERGY TO FREEZE CREW: 10 PR/CY"
1634 PRINT:PRINT"HOW MANY CLONE-YEARS FREEZE?":INPUTF$
1635 IFVAL(F$)*10>PFTHENPRINT:PRINT"FUEL DEFICIENCY.":PRINT"INTERLOCK OVERRIDE":PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO1606
1636 PF=PF-10*VAL(F$):CY=CY+VAL(F$):M$="STAND BY":GOSUB3:GOSUB3530:PN=15:Q$(2)="YAWN...!":GOTO100
1639 GOSUB3515:PRINT"LASER CONTROL":PRINT
1642 PRINT"TARGET";:GOSUB12:IFAA$=SG$THEN1664
1643 J=0:FORI=1TO12:IFR$(I)=AA$THENJ=I:I=12
1644 NEXT
1645 IFJ=0THENPRINTAA$" ";:GOSUB1660:GOTO1606
1646 PRINT"TARGETED ON ";:M$=AA$:GOSUB7:PRINT:GOSUB9:IFPF<1000THENM$="FUEL DEFICIENCY":GOSUB3
1647 IFAA$=SG$THEN1650
1649 IFJ<>PVTHENM$="OUT OF RANGE":GOSUB7:PRINT:GOTO1652
1650 IFAA$=SG$ANDPV<>3THENPRINTAA$" ";:M$="OUT OF RANGE":GOSUB7:PRINT:GOTO1652
1651 GOTO1655
1652 PRINT:PRINT"COMPUTER OVERRIDE":PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO1606
1655 GOSUB3540:IFAA$<>SG$THEN6030
1656 GOTO6040
1660 M$="NOT IN DATABASE":GOSUB7:PRINT:GOSUB3527:RETURN
1661 IFPN=2THEN1692
1664 IFPN=3THEN1692
1665 IFPN<>4THEN4050
1666 GOSUB3515:PRINT"THINK 'DESTINATION' OR 'END'":GOSUB12
1667 IFAA$=VS$THENIFP(1)=3THENPRINT"SHUTTLE AT VESPOZIAN":PRINT:PRINT"* MISSION ABORTED *":GOSUB3525:GOTO100
1668 IFAA$=VS$THENPRINT"IN FLIGHT VESPOZIAN":GOSUB3520:PRINT"DOCKING":GOSUB3520:P(1)=3:D$(4)="C   ":GOTO100
1672 IFLEFT$(AA$,3)="END"THEN4020
1675 J=0:FORI=1TO12:IFAA$=R$(I)THENJ=I
1678 NEXT:IFJ=0THENPRINTAA$" ";:GOSUB1660:GOTO100
1681 IFJ<>PVTHENPRINTAA$;:M$="OUT OF RANGE":GOSUB7:PRINT:GOSUB3520:GOTO100
1684 IFST=1THENM$="VESPOZIAN NOT IN ORBIT AROUND "+AA$:GOSUB1:PRINT:PRINT "* OPERATION ABORTED *":GOSUB3525:GOTO100
1687 IFJ<>9THEN1689
1688 GOTO1690
1689 PRINT"CONDITIONS ON "AA$:M$="NOT SUITABLE FOR SHUTTLE LANDING":GOSUB1:PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO100
1690 IFC(0)<>-4THENPRINT"SHUTTLE BAY DOORS CLOSED":GOSUB3520:PRINT"* OPERATION ABORTED *":GOSUB3525:GOTO100
1691 PRINT"HEADING-- "AA$:GOSUB3520:PRINT"LANDED ON "AA$:P(1)=9:D$(4)="I   ":GOSUB3520:GOTO100
1692 IFPN=2THENIFC(0)=-2THENC(0)=-3:B$(2)="BLUE BUTTON BY OPEN BULKHEAD":E$(2)="PFL ":GOTO4020
1693 IFPN=2THENIFC(0)=-3THENC(0)=-2:B$(2)="BLUE BUTTON BY CLOSED BULKHEAD":E$(2)="PF  ":GOTO4020
1694 IFPN=3ANDC(0)=-3THENC(0)=-4:E$(3)="OT  ":D$(3)="ED  ":Q$(3)="INNER DOOR CLOSES":Q$(4)="OUTER DOOR OPENS":GOTO4020
1695 IFPN=3ANDC(0)=-4THENC(0)=-3:E$(3)="TP  ":D$(3)="DB  ":Q$(3)="OUTER DOOR CLOSES":Q$(4)="INNER DOOR OPENS":GOTO4020
1698 GOTO4050
1700 Q$(2)="NO CHANCE-- TOO RISKY":GOTO100
1800 Q$(2)="I'M NOT TIRED":GOTO100
3500 J=0:CC=LEN(Y$):IFCC=0THENRETURN
3505 FORI=1TOLEN(X$)STEPCC:IFY$=MID$(X$,I,CC)THENJ=I:I=LEN(X$)+3
3510 NEXT:RETURN
3515 CLS:SOUND235,6:PRINT"A THOUGHT IS FRAMED IN YOUR MIND";:GOSUB5:RETURN
3520 FORI=1TO4500:NEXT:RETURN
3525 FORO=1TO6:SOUND183,1:SOUND150,1:NEXT:GOSUB9:RETURN
3526 SOUND183,4:RETURN
3527 FORO=1TO10:SOUND100,1:NEXT:GOSUB9:RETURN
3530 FORI=1TO15:CLSRND(8):NEXTI:CLS:RETURN
3540 FORI=1TO10:POKE49151,64:FORZZ=1TO25:NEXT:POKE49151,0:FORZZ=1TO25:NEXT:NEXTI:CLS:RETURN
3550 RETURN 
4000 Q$(2)="IMPOSSIBLE!":GOTO100
4010 Q$(2)="I CAN'T GO "+I$:GOTO100
4020 Q$(2)="OK":GOTO100
4030 Q$(2)="REFERENCE DISC BY ITS NUMBER":GOTO100
4040 Q$(2)="I'M ALREADY CARRYING IT":GOTO100
4050 Q$(2)="I DON'T SEE IT HERE":GOTO100
4060 Q$(2)="I CAN'T YET!":GOTO100
4070 Q$(2)="I'M NOT CARRYING IT":GOTO100
4080 Q$(2)="YOU MUST BE JOKING!":GOTO100
4090 Q$(2)="OK-- NOTHING HAPPENS":GOTO100
4100 Q$(2)="CAP'N WINKS AT ME AND":Q$(3)="GLANCES AT GRAKTA":GOTO100
4110 Q$(2)="HUH?":GOTO100
4500 CLS:PRINT:PRINT"CLONE YEAR:"CY;TAB(18)"ZONETIME:";STR$(CT):PRINT:GOSUB5
4501 PRINT:PRINTW$;TAB(14);R$(CD):IFST=1THENPRINT"ETA: "TAB(13)CT+ABS(CD-PV)*25-JE
4505 PRINT"NEAREST BODY:"TAB(14)R$(PV):IFPV=2THENPRINTTAB(14)"BLOODLINE STARGATE";
4510 PRINT"FUEL:"TAB(13)PF"PICORATH":PRINT"LASER:"TAB(14);:IFPF<1000THENPRINT"INOPERATIVE":GOTO4520
4511 PRINT"OPERATIVE"
4520 PRINT:GOSUB5
4530 PRINT@451,"THINK CONTROL REPORTING":M$="B4B4B4B4":GOSUB8:GOSUB3520:GOTO4020
4900 DATA"IN THE CREWS QUARTERS","IN THE RESEARCH LAB NEAR A","IN THE SHUTTLE BAY","IN A SHUTTLE"
4901 DATA"OUTSIDE THE HULL","IN THE CAPTAIN'S CABIN","IN THE NAVIGATION AND","ON THE BRIDGE"
4902 DATA"ON YUSES FAR UNDER","AT VESPOZIAN'S FUEL CORE"
4903 DATA"IN A METAL GANGWAY WITH A","IN A SUIT ROOM","IN A SECRET CONTROL ROOM","ON YUSES FAR","IN MY BUNK. IT HAS CLEAN"
4904 DATA"LINED WITH BUNKS","BLUE BUTTON BY A CLOSED BULKHEAD","BLUE BUTTON ON WALL","WITH-THINK CONTROL-BUTTON"
4905 DATA"OF THE VESPOZIAN","","COMPUTER ROOM","","A PURPLE SKY","I CAN FEEL HEAT"
4906 DATA"DOOR","","WITH DUPLICATE CONTROL","UNDER AN INDIGO SKY","SHEETS AND A LARGE PILLOW"
4907 DATA"AUTO SHUTTLE","STRANGE DEVICE","*POLYPS*","CRAWLWAY","ASBESTOS SUIT","SPACE SUIT","METAL CONTAINER"
4908 DATA"FUEL CYLINDER","INSULATED TONGS","CAP'N BEZEL","GRAKTA","MACHEN","DISK 005","DISK 001","DISK 002","DISK 003"
4909 DATA"DISK 004","KEY","GLOWING ROCK"
4910 DATA"FUNUS         ","LITTLE FUNUS  ","GYRATES       ","ROTH          ","BULGEN        ","GREATER BULGEN"
4911 DATA"OUTER FUNUS   ","FAR FUNUS     ","YUSES FAR     ","MID YUSES     ","YUSES NEAR    ","YUSES         "
4912 DATA"DSB ","PF  ","P   ","O   ","V   ","OC  ","AFUP","AU  ","TE  ","S   ","PD  ","P   ","O   ","NEW ","U   "
5007 DATA"HBO ","AGC ","B   ","C   ","C   ","GJ  ","BHKF","GA  ","DN  ","KF  ","JGML","K   ","K   ","NNI ","A   "
5008 DATA 3,2,99,99,12,12,6,10,2,6,7,8,66,2,8,6,7,13,99
5009 DATA -2,2,2,-2,2,2,2,2,2,-2,-2,-2,2,2,2,2,2,2,-2
6000 CLS:M$="GRAKTA, BLOODLINE SPY, BLASTS YOU WITH HIS LASER. YOU ARE NOW A PILE OF YUSES DUST!":GOSUB1:GOTO6200
6010 CLS:M$="AARGH...! YOU WENT INTO THE FUEL CORE WITHOUT PROTECTION-- SEARING HEAT...":GOSUB1:GOTO6200
6020 CLS:M$="WITH NO FUEL, THE VESPOZIAN WANDERS AIMLESSLY OUT OF CONTROL, LOST IN SPACE":GOSUB1:GOTO6200
6030 CLS:M$="TOO CLOSE! THE VESPOZIAN IS RIPPED APART BY THE EXPLOSION":GOSUB1:GOTO6200
6040 CLS:M$="WHAM! THE BLOODLINE STARGATE IS VAPOURISED. YOU HAVE SUCCEEDED IN YOUR MISSION. CONGRATULATIONS!"
6041 GOSUB1:GOTO6202
6050 CLS:M$="INNER DOOR CLOSES. AIR EVACUATED. OUTER DOOR OPENS-- NO SPACE SUIT!":GOSUB1
6051 M$="YOU PERISH AND DRIFT INTO SPACE...":GOSUB1:GOTO6200
6055 CLS:M$="YOU HAVE STUMBLED BLINDLY ONTO A RAZOR SHARP ROCK":GOSUB1:PRINT
6056 M$="YOUR SUIT HAS RIPPED AND YOU DIE FROM LACK OF OXYGEN.":GOSUB1:GOTO6200
6060 CLS:M$="PRESSURE SWITCH ENGAGES AND THE DRILL BORES THROUGH VESPOZIAN'S HULL.":GOSUB1
6061 IFC(5)=6THENM$="YOU DRIFT OFF INTO SPACE AND PERISH WHEN OXYGEN RUNS OUT.":GOSUB1:GOTO6200
6065 M$="YOU BURST APART WITH THE DEPRESSURISATION!":GOSUB1:GOTO6200
6070 CLS:M$="WALKING AROUND IN "+O$(K2):GOSUB1:PRINT"HIGHLY SUSPICIOUS!"
6080 CLS:M$="GRAKTA, BLOODLINE SPY, BLASTS YOU FOR TREACHERY AGAINST THE EMPIRE!":GOSUB1:GOTO6200
6090 CLS:M$="GRAKTA VAPOURISES YOU ON SUSPICION OF SPYING BY RETRIEVING COMPUTER DATA...":GOSUB1:GOTO6200
6100 CLS:M$="GRAKTA NOTICES SUIT ROOM DOOR OPEN. FINDS KEY ON YOU AND BLASTS YOU TOWARDS FUNUS. YOU PERISH..."
6101 GOSUB1:GOTO6200
6110 CLS:M$="GRAKTA NOTICES SHUTTLE BAY DOOR OPEN, AND SUSPECTS A PLOT AGAINST THE EMPIRE.":GOSUB1
6111 M$="YOU ARE EJECTED IN THE SHUTTLE AND FOREVER LOST IN SPACE.":GOSUB1
6200 FORO=15TO1STEP-1:SOUNDO,1:NEXT:GOTO6203
6202 GOSUB3525:GOSUB3526:GOSUB3527
6203 PRINT:PRINT:PRINT"YOUR ADVENTURE IS OVER.":M$="IF YOU WOULD LIKE ANOTHER GAME THEN PRESS 'Y'":GOSUB1
6205 M$=INKEY$:IFM$=""THEN6205
6210 IFM$="Y"THENCLS:RUN
6215 PRINT:M$="GOODBYE THEN":GOSUB3:END
6500 CLS:M$="THE BLOODLINE WILL BE YOUR MASTERS FOREVER!":GOSUB1:GOTO6203
7000 CLS0:PRINT"€€€€€€€€€€€€€€€€€€€€€Â€Óíÿÿþÿó€€€€ÁÀÄ€€€€€€€€€Â€€€€€€€Öáÿøÿþóÿû€€€€€€€€€€€€€€€€€€€€€€Õ€ïú÷úûõÿÿú";
7012 PRINT"€€€€€È€€€€€€€€€€€€€€€Úåÿûþù÷óÿÿÿ€€€€€€€€€€€€€Â€€€€€€€Úåþÿÿÿþþþýÿ€€€€€€€€€€€€€€€€€€€Â€Ùäÿÿû÷ÿÿÿÿþ";
7015 PRINT"€€ÁÀ€€€€€€€€€€€€€€€€ÀÔÒíÿûý÷ÿùÿø€€€€€€€€€Á€€€€€€€€€€€€ÔÓíÿó÷ûÿø€€€€€€€€€€€€ÄÍÏÃÃÏÃÃ€€€€€Üìüüü€€€";
7018 PRINT"€€€€€€”›üû»²ÃÍÏËËËÃÏÃÀ€€€€€€€€€€€€€€€€‘žóþ¾¸ÌÉÃÃÃÃÃÈ€€€€€€€€€ÈÀ€ÀÀ€€€€€€€€€€ÄÌÀ€€€€€€€€€€€€€€€€Á";
7021 PRINT"€È€€€€ÁÀ€€€€€€€€È€€€€€€€€À€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€Ä€€€€€€";:RETURN
7075 PRINT@448,;:M$="BY KEITH CAMPBELL. (C) C&VG 1983":GOSUB7
7080 PRINT@481,;:M$="PRESS 'H' FOR HELP 'P' TO PLAY":GOSUB7:PRINT@0,;:M$="THE VESPOZIAN AFFAIR":GOSUB7
7085 M$=INKEY$:IFM$=""THEN7085
7090 IFM$="P"THENRETURN
7091 IFM$="H"THEN8000
7095 GOTO7085:REM HEY MY GOOD CHUM!
8000 CLS:M$="THE 7TH EMPIRE IS DOMINATED BY THE BLOOD-LINE RACE-- "
8001 M$=M$+"CONTROLLERS OF THE 'GREENHOUSE EFFECT' PLANETS-- "
8002 M$=M$+"SOURCES OF ENERGY FOR INTERSTELLAR TRAVEL AND THE KEYS TO GALACTIC POWER.":GOSUB1
8003 M$="SCIENTIFIC THEORY SUGGESTS THAT THESE PLANETS ARE CREATED BY 'POLYPS'-- "
8004 M$=M$+"SMALL SPACE BORN ORGANISMS THAT CAN TURN A DEAD PLANET INTO A GHE PLANET IN 20 YEARS.":GOSUB1
8005 M$="THE EMPIRE HAS SENT THE VESPOZIAN, SKIPPERED BY CAPTAIN BEZEL OF THE PIRATE EMPIRE,":GOSUB1
8006 GOSUB8050:CLS:M$="TO INVESTIGATE THE FUNUS SYSTEM AND TEST THE THEORY.":GOSUB1
8008 M$="D'TAAN, A FEMALE HUMAN SCIENTIST, IS IN CHARGE OF THE INVESTIGATION.":GOSUB1
8009 M$="HOWEVER, SHE IS SECRETLY OPPOSED TO THE EMPIRE AND DESIRES TO BREAK ITS INFLUENCE BY "
8010 M$=M$+"DESTROYING THE STARGATE AT FUNUS, WHICH THE SHIP IS CURRENTLY APPROACHING AT THE ":GOSUB1
8011 M$="END OF ITS MISSION. FUEL IS RUNNING LOW AND THERE MIGHT BE A SPY ON BOARD...":GOSUB1:PRINT
8050 PRINTTAB(6);:M$="HIT 'C' TO CONTINUE":GOSUB7
8070 IFINKEY$<>"C"THEN8070
8080 RETURN
