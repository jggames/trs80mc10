
David Chance
1983
Level II BASIC for TRS-80 computers, from "33 Adult Computer Games in BASIC for the IBM PC, Apple II/IIe and TRS-80"
10 REM PROGRAM TITLE: THE COURSE
20 CLS:PRINT TAB(10);
30 PRINT">>> THE COURSE <<<"
40 PRINT
50 INPUT"REQUIRE INSTRUCTIONS";X$
60 IF X$="N" OR X$="NO" THEN 210
70 PRINT
80 PRINT"THE COURSE WILL REQUIRE THAT YOU"
90 PRINT"USE TOP NOTCH REFLEXES TO CONTROL"
100 PRINT"YOUR VEHICLE THROUGH A DISTANCE OF"
110 PRINT"10 LAPS. THE COURSE WILL BE BOTH"
120 PRINT"STRAIGHT AND WINDING. YOU WILL"
130 PRINT"CONTROL YOUR VEHICLE WILL THE"
140 PRINT"LEFT AND RIGHT ARROW KEYS."
150 PRINT"THE SLIGHTEST CONTACT WITH THE"
160 PRINT"EDGE OF THE TRACK WILL CAUSE"
170 PRINT"AN ACCIDENT, AND A HIT WILL BE"
180 PRINT"ADDED TO YOUR SCORE."
190 PRINT:PRINT:PRINT"PRESS A KEY..."
200 X$=INKEY$:IF X$="" THEN 200
210 CLS:T=1:AT=1
220 INPUT"FIRST NAME OF DRIVER";D$
225 IF D$="" THEN PRINT:GOTO 220
230 PRINT D$;" STRIKE A KEY TO BEGIN..."
240 X$=INKEY$:IF X$="" THEN 240
245 IF T>=2 THEN CLS:GOTO 260
250 CLS:DIM CX(18),CY(18)
260 PRINT@0,"DRIVER: ";D$;:GOSUB 1000
265 CC=0:LP=0:LL=0:HI=0:' THE COURSE
270 CX=15388:CY=CX+9:J=191:C=1
275 C1=CX+3:C2=C1+2:' VEHICLE
280 IF CY>=16383 OR C1>=16383 THEN 400
285 IF CC=1 THEN 420
290 POKE CX,J:POKE CY,J
295 CX(C)=CX:CY(C)=CY:' TO BLANK OLD COURSE
300 CX=CX+64:CY=CY+64
305 C=C+1
310 DD=RND(2)
315 KY=PEEK(14400):' KEYS
320 IF DD=1 THEN 340
330 CX=CX+DD-1:CY=CY+DD-1:GOTO 360
340 DD=-DD
350 CX=CX+DD:CY=CY+DD
360 ' CHECK FOR MOVEMENT (VEHICLE)
370 IF KY=32 THEN 500
380 IF KY=64 THEN 530
390 GOTO 280
400 CC=1:LP=LP+1
405 IF (LP/10)=.6 THEN LL=LL+1:LP=1
410 PRINT@64,"LAPS =";LL;
415 IF LL>=10 THEN 700 ELSE GOTO 270
420 POKE CX(C),32:POKE CY(C),32
430 POKE C1,32:POKE C2,32
440 IF C2>=16319 THEN 290
450 C1=C1+64:C2=C2+64
460 POKE C1,183:POKE C2,187
470 CL=PEEK(C1-1):CR=PEEK(C2+1)
480 IF ABS(CL+CR)<>64 THEN 600
485 GOTO 290
500 ' VEHICLE MOVEMENT (LEFT)
505 POKE C1,32:POKE C2,32
510 C1=C1-1:C2=C2-1
520 GOTO 280
530 ' VEHICLE MOVEMENT (RIGHT)
535 POKE C1,32:POKE C2,32
540 IF C2>=16319 THEN 280
550 C1=C1+1:C2=C2+1
560 GOTO 280
600 ' HITS TO VEHICLE
610 HI=HI+1
620 PRINT@128,"HITS =";HI;
630 GOTO 290
700 ' FINISH
710 PRINT@192,CHR$(30)
720 IF HI=0 THEN 800
730 PRINT"YOU DIDN'T MAKE THE";LL;"LAPS"
740 PRINT"WITHOUT ERROR. YOU TOOK A"
750 PRINT"TOTAL OF";HI;"HITS...."
760 PRINT"TRY AGAIN, ";D$;
770 INPUT X$
780 IF X$="N" OR X$="NO" THEN 1100
790 AT=AT+1:PRINT:GOTO 830
800 PRINT"NOT BAD! NO HITS AT ALL!"
810 PRINT"THINK YOU MIGHT WANT TO"
820 GOTO 760
830 T(T)=HI:' BEST SCORE
835 IF T<=1 THEN 920
840 I=1:Z=0
850 IF T(I)<=T(I+1) THEN 880
860 YS=T(I):T(I)=T(I+1):T(I+1)=YS
870 Z=1
880 I=I+1:IF I>=T THEN 900
890 GOTO 850
900 IF Z=1 THEN 840
910 BS=T(1)
920 T=T+1:IF T>=5 THEN BS=T(1):T=3
930 PRINT
940 PRINT:GOTO 230
1000 ' MESSAGE
1010 IF T<=2 THEN 1040
1020 PRINT@42,"BEST SCORE: ";BS;
1030 PRINT@106,"ATTEMPTS: ";AT;
1040 RETURN
1100 ' END
1110 CLS
1120 PRINT"END OF PROGRAM RUN..."
1130 END