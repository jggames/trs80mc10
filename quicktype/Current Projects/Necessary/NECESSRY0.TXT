10 REM PROGRAM TITLE: WHERE'S THE
20 REM NECESSARY ROOM ???
30 CLS
40 PRINT"WHERE'S THE NECESSARY ROOM ???"
50 PRINT
60 PRINT"SEE THE INSTRUCTIONS...";
65 FOR I=1TO500:NEXT:PRINT"YOU DRUNK";
70 INPUT X$
80 IF X$<>"Y" AND X$<>"YES" THEN DD=1:GOTO 275
90 PRINT"SO HERE YOU ARE AGAIN, YOU LUSH !!!"
100 PRINT"FILLED YOUR KIDNEYS TO THE BRINK;"
110 PRINT"YOUR HEAD IS SPINNING AND WHAT"
120 PRINT"DO YOU NEED ?? NATURALLY, THIS"
130 PRINT"BEING A COMPUTER PROGRAM, I YOUR"
140 PRINT"COMPUTER WILL TRY AND HELP YOU"
150 PRINT"LOCATE THAT LITTLE ROOM (IF THERE"
160 PRINT"IS ONE!!) STEP BY STEP."
170 PRINT"NATURALLY THIS BEING A COMPUTER"
180 PRINT"PROGRAM (DID I SAY THAT?) I HAVE"
190 PRINT"ALSO HAD A LITTLE TOO MUCH..."
200 PRINT
210 INPUT"HIT THE WHITE KEY";X$
220 CLS
230 PRINT"YOU DRUNK!!!"
240 PRINT"AS I SAY...I AM ALSO A LITTLE"
250 PRINT"TIPSY, IF MY CIRCUITS HOLD TO..."
260 PRINT"GETHER LONG ENOUGH, WE TOGETHER"
270 PRINT"WILL FIND THAT ROOM. WHAT'S YOUR"
275 IF DD=1 THEN PRINT"THE ";
280 INPUT"NAME CLYDE";N$
290 IF LEN(N$)>=3 THEN 310
300 GOTO 320
310 PRINT"YOUR KIDDING, SUCH A LONG NAME."
320 PRINT:IF DD=1 THEN 460
330 PRINT"SO...TO FIND THAT ROOM, I WILL"
340 PRINT"INSTRUCT YOU ON THE ROUTE. YOU"
350 PRINT"IN TURN WILL PRESS EITHER THE"
360 PRINT"LEFT ARROW KEY...OR THE RIGHT"
370 PRINT"ARROW KEY, THAT IS IF YOU"
380 PRINT"BELIEVE WHAT I TELL YOU. IF"
390 PRINT
400 INPUT"LUSH, HIT THAT KEY AGAIN";X$
410 CLS
420 PRINT"YOU DO NOT BELIEVE ME, PRESS"
430 PRINT"THAT LONG BAR, AT THE BOTTOM"
440 PRINT"OF THE KEYBOARD...OK ";N$;"?"
450 FOR I=1TO2000:NEXT
460 PRINT"EXCUSE ME WHILE I UNSCRAMBLE A"
470 PRINT"FEW FLIP-FLOPS....."
480 RANDOM
490 REM TABLES
500 TA=RND(50):IF TA<=15 THEN 500
510 REM START DISTANCE FROM ROOM
520 D=RND(150)
530 IF D<=50 THEN 520
540 REM CUSTOMERS IN BAR
550 P=RND(100)
560 IF P<=20 THEN 550
570 REM NUMBER OF WRONG DOORS
575 REM LEAVE TWO FOR FINAL
580 DR=RND(7)
590 IF DR<=4 THEN 580
600 REM READ WRONG DOOR TITLES
610 FOR I=1TODR:READ D$(I):U(I)=I:NEXT
615 FOR I=DR+1TO10:READ F$(I):NEXT
620 DATA WOMEN's ROOM,ATTIC,EXIT,BASEMENT
630 DATA OFFICE,WINE CELLAR,LUNCH
640 DATA LAUNDRY,DRESSING,NECESSARY
650 J=DR-4:PQ=1:PJ=1:REM TIME / MEANIES
660 FOR I=1TO5:READ M$(I):M(I)=I
665 IF LEFT$(M$(I),1)<>"M" THEN 660 ELSE NEXT
670 DATA MR. MUZZLE,MR. SMASH
680 DATA MS. LEAN TOO,MS. FOUL MOUTH
690 DATA MR. P. WADDLE
700 REM SOME STRING CONTENTS
710 K$="STILL":B$="MY CPU HURTS"
720 L$="LEFT":R$="RIGHT":Z$="ZZZZZZZZZZ"
730 FOR I=1TO1500:NEXT
740 PRINT
750 PRINT N$;" WE ARE NOW AT TABLE";TA
760 PRINT"MEANING, WE MUST TRAVEL";TA
770 PRINT"TABLES TO REACH THE BAR."
780 PRINT"TOTAL DISTANCE, I WOULD ESTIMATE"
790 PRINT"TO BE AROUND";D;"FEET."
800 PRINT"GLANCING AROUND(SOMETHING YOU'RE"
810 PRINT"NOT TO GOOD AT) I JUDGE.THERE ARE ABOUT";P
820 PRINT"CUSTOMERS WE MUST GET PAST..."
830 REM MESSAGE ---- B$
840 L=-64:I=0
850 L=L+64:I=I+1:FOR II=1TO500:NEXT
860 PRINT@L,STRING$(35,32)
870 PRINT@L+30,MID$(B$,I,1):IF L<=896 THEN 850
880 CLS
890 GOSUB 1070:GOTO 920
900 PRINT"OK, LUSH...I MEAN ";N$
910 T=0:RETURN
920 GOSUB 900:GOSUB 925:GOTO 960
925 REM MEANIE SELECT
930 M=RND(5):IF M(M)=0 THEN 930
940 M$=M$(M):M(M)=0
950 IF LEFT$(M$,2)="MS@" THEN C$=". SHE" ELSE C$=". HE"
955 RETURN
960 PRINT"GET UP AND MOVE ";T$;", AWAY"
970 PRINT"FROM ";M$;" ";C$;"'S A MEANIE,"
980 PRINT"WE DON'T WANT TO TANGLE WITH."
990 GOSUB 1000:GOTO 1130
1000 W$=INKEY$:T=T+1:IF T>=J*100 THEN 1025
1010 IF W$="" THEN 1000
1020 WW=ASC(W$):RETURN
1025 PRINT
1030 PRINT"PRESS THE ";R$;" ARROW, OR"
1040 PRINT"THE ";L$;" ARROW OR THE LONG"
1050 PRINT"BAR...YOU DRUNK, QUIT BOBBIN'"
1060 PRINT"YOUR HEAD !!":T=0:GOTO 990
1070 REM GET LEFT / RIGHT
1080 FOR I=1TO10
1090 Y=RND(2)
1100 IF Y=1 THEN T$=L$:GOTO 1120
1110 T$=R$
1120 NEXT:RETURN
1130 IF WW+85=93 THEN 1170
1140 IF WW+85=94 THEN 1170
1150 IF WW=32 THEN 1170
1160 PRINT:GOTO 1030
1170 WW=ASC(W$)
1180 IF ABS(T-J)>=100 GOSUB 1520
1190 IF T$="RIGHT" AND WW+85=94 THEN 1440
1200 IF T$="LEFT" AND WW+85=93 THEN 1440
1210 IF WW=32 THEN PRINT:GOTO 1700
1215 IF T$="RIGHT" THEN F$=L$ ELSE F$=R$
1216 PRINT
1220 PRINT"I SAID, GO ";T$;", YOU WENT ";F$
1230 PRINT N$;", YOU ARE A LITTLE MORE"
1240 PRINT"DRUNK THAN I FIRST THOUGHT !!"
1250 RE=RND(6):UU=RND(5):M$=M$(UU)
1260 ON RE GOTO 1270,1290,1320,1345,1365,1400
1270 PRINT M$;" JUST BROKE PART"
1275 PRINT"OF YOUR FACE !!"
1280 GOTO 1430
1290 PRINT"YOU JUST STEPPED ON ";M$;" TOES,"
1300 PRINT"CAN'T YOU LISTEN TO DIRECTIONS ??"
1310 GOTO 1430
1320 PRINT"HOW DID THAT BOTTLE THAT ";M$
1330 PRINT"JUST BROKE OVER YOUR HEAD FEEL??"
1340 GOTO 1430
1345 IF JA=1 THEN 1250
1350 PRINT"MOST LUSHES LISTEN, YOU ARE AN"
1360 PRINT"EXCEPTION...DINGY !!!":GOTO 1430
1365 IF JA=1 THEN 1250
1370 PRINT"THREE KEYS TO PRESS, AND YOU MY"
1380 PRINT"DRUNK FRIEND, PRESS THE WRONG ONE !!"
1390 GOTO 1430
1400 PRINT"OF ALL THE MEANIES TO RUN IN TO !!!"
1410 PRINT"NEXT TIME ";M$;" WILL PROBABLY"
1420 PRINT"FINISH BREAKING YOUR FACE..."
1430 PRINT:E$="STANDS":GOTO 1660
1440 REM MOVE CORRECT ?
1450 PL=RND(10)
1460 IF PL<=DR THEN 1610 ELSE PRINT
1470 PRINT"I'M SORRY, MY MISTAKE, I TOLD"
1480 PRINT"YOU THE WRONG DIRECTION ";N$;"."
1490 PRINT"DINGY !! YOU SHOULDN'T HAVE"
1500 PRINT"LISTENED THIS TIME !!!"
1510 PRINT:JA=1:GOTO 1250
1520 REM SNORE MESSAGE
1530 REM TAKING TOO LONG / WRONG KEY
1540 FOR I=1TO LEN(Z$)
1550 PRINT MID$(Z$,I,1);
1560 FOR ER=1TO50:NEXT ER
1570 NEXT I:PRINT
1580 PRINT"THOUGHT I WOULD CUT A FEW"
1590 PRINT"Z'S WHILE YOU WERE LOOKING"
1600 PRINT"FOR THE KEYS.....":PRINT
1605 FOR I=1TO1200:NEXT:RETURN
1610 IF D<=50 THEN 1830 ELSE PRINT
1615 PRINT"WE'rE GETTING CLOSER,  ";N$
1620 PRINT"HOLD THAT BLADDER !!!":PRINT
1630 D=(D-INT(D/TA)-10):E$="IS NOW"
1640 IF D<=0 THEN 2500
1650 TA=TA-1:P=P-RND(5)
1660 PRINT"OUR DISTANCE ";E$;" AT";D;"FEET."
1665 IF TA<=0 PRINT"AND ";:GOTO 1675
1670 PRINT"WITH ";TA;"TABLES, AND"
1675 IF P<=0 THEN PRINT"NO CUSTOMERS TO GET BY !!":GOTO 1690
1676 IF P<=10 THEN K$="ONLY"
1680 PRINT K$;" ";P;"CUSTOMERS TO GET BY."
1685 IF RND(TA)<=10 AND XX=0 THEN 2300
1690 XX=0:GOTO 2100
1700 REM SPACE BAR ?
1710 PL=RND(5)
1720 IF PL<=2 THEN 1790
1730 PRINT"WHY? WHY DID YOU PRESS THE SPACE"
1740 PRINT"BAR?? OF ALL THE TIMES NOT TO"
1750 PRINT"BELIEVE ME, THIS TIME I WAS ";R$;"."
1760 PRINT"YOU ARE AT THE SAME PLACE YOU"
1770 PRINT"WERE YOU...YOU DINGY @!/#&'!"
1780 PRINT:GOTO 1250
1790 PRINT"YOU WERE RIGHT, ";N$;" I TOLD"
1800 PRINT"A BLOODY LIE AND YOU"
1810 PRINT"DIDN'T FALL FOR IT !!!"
1820 PRINT:GOTO 1610
1830 REM WRONG DOOR MESSAGE
1840 IF PQ>DR THEN 1615
1850 DA=RND(DR)
1860 IF U(DA)=0 THEN 1850
1870 V$=D$(DA)
1880 U(DA)=0:PQ=PQ+1
1885 IF MID$(V$,2,1)="X" THEN 2040
1890 DA=RND(6):IF DA=6 THEN 1615
1900 ON DA GOTO 1910,1940,1970,2010,2040
1910 PRINT"YOU'VE JUST WALKED INTO THE ";V$
1920 PRINT"FEEL EMBARRASSED, YOU DINGY !!
1930 GOTO 2070
1940 PRINT"NO PLACE TO DRAIN A BLADDER HERE"
1945 IF LEFT$(V$,2)="OF" PRINT"IN THE ";V$;".":GOTO 2070
1950 PRINT"THIS IS THE ";V$;
1955 IF RIGHT$(V$,2)="CH" PRINT"ROOM !!"
1960 PRINT:GOTO 2070
1970 PRINT"FEEL ODD ? YOU'VE JUST STEPPED"
1980 PRINT"(ALMOST) STRAIGHT INTO THE ";V$
1990 PRINT"HAVING PROBLEMS READING SIGNS ??"
2000 GOTO 2070
2010 PRINT"W R O N G  D O O R  D I N G Y  ! !"
2020 PRINT"THIS IS THE ";V$;
2025 IF RIGHT$(V$,2)="CH" PRINT" ROOM"
2030 PRINT:GOTO 2070
2040 PRINT"GUESS WHAT YOU'VE JUST WALKED"
2045 IF LEFT$(V$,2)="EX" THEN 2065
2050 PRINT"INTO? YES, THE ";V$;
2055 IF RIGHT$(V$,2)="CH" PRINT"ROOM !!"
2060 PRINT:GOTO 1920
2065 PRINT"OUT THE ";V$;".":GOTO 2070
2070 PRINT:IF LEFT$(V$,2)="EX" THEN 2250
2080 GOTO 2410
2100 REM CONTINUE MOVEMENT TO HEAD
2110 PJ=PJ+1:GOSUB 900
2120 IF PJ<=5 GOSUB 930
2130 FOR I=1TO3500:NEXT:GOSUB 1070
2140 WE=RND(5):REM MOVEMENT MESSAGE SELECT
2150 ON WE GOTO 2160,2170,2180,2190,2200
2160 PRINT"WE WILL NOW MOVE ";T$;
2165 GOTO 2220
2170 PRINT"ALL DIRECTIONS POINT ";T$;
2175 GOTO 2220
2180 PRINT T$;" IS THE RIGHT DIRECTION ";
2185 GOTO 2220
2190 PRINT"LOOK ";T$;" THEN MOVE ";T$;
2195 GOTO 2220
2200 PRINT"DON'T STUMBLE NOW, DINGY !!!"
2210 PRINT"MOVE SLOWLY ";T$;
2220 IF PJ<=4 PRINT", AWAY":GOTO 970
2230 PRINT
2240 GOTO 990
2250 PRINT"GOING OUT THE ";V$;" WASN'T TOO"
2260 PRINT"SMOOTH...TURN AROUND, THEN WE'LL"
2270 PRINT"CONTINUE TO LOCATE THAT ROOM."
2280 FOR I=1TO3000:NEXT
2290 GOTO 2100
2300 REM REMOVE MORE CUSTOMERS
2305 IF P<=3 THEN XX=1:GOTO 1690
2310 AA=RND(5):DF=DF+1:XX=1
2315 IF DF>=4 THEN 2340 ELSE P=P-AA
2320 PRINT"EXCUSE ME, THAT's";P;"CUSTOMERS."
2325 IF DF>=2 THEN MM$="MORE"
2330 PRINT"A FEW ";MM$;" JUST PASSED OUT..."
2340 IF RND(P)<=10 THEN 2360
2350 GOTO 2410
2360 FOR I=1TO2000:NEXT:IF DF>=7 THEN 2420
2370 PRINT"HOLD IT !!! THE BOUNCER JUST"
2380 AA=RND(3):P=P-AA
2390 PRINT"CARRIED OUT";AA;"MORE, FOR"
2400 PRINT"FIGHTING DURING HAPPY HOUR..."
2410 FOR I=1TO2000:NEXT
2420 PRINT
2430 E$="STANDS":JA=0
2440 GOTO 1660
2500 REM FINAL -- ROOM REACHED
2510 PRINT"YOU'VE REACHED THE ROOM !!!"
2520 FOR I=1TO2500:NEXT
2530 I=RND(10)
2540 IF I<=DR THEN 2530
2550 PRINT"IF YOU CAN KEEP YOUR EYES OPEN"
2560 PRINT"LONG ENOUGH...RAISE YOUR HEAD"
2570 PRINT"AND READ THE SIGN ON THE DOOR."
2580 D$=F$(I)
2590 X=0
2600 FOR I=1TO900:NEXT
2610 PRINT:X=X+1
2620 IF X=7 PRINT@985,D$;" ROOM"
2630 IF X<=14 THEN 2600
2640 IF LEFT$(D$,1)="N" THEN 2690
2650 PRINT
2660 PRINT"** T H E  W R O N G  D O O R **"
2670 PRINT"ALL THIS FOR NOTHING, DINGY !!!"
2680 GOTO 2760
2690 PRINT"THIS IS IT !!!"
2700 PRINT"THE RIGHT NECESSARY ROOM !!!"
2710 PRINT"RELIEF IS ON THE WAY..."
2720 PRINT
2730 FOR I=1TO4000:NEXT
2740 PRINT"NOW THAT YOU ARE FEELING MUCH"
2750 PRINT"BETTER, SHALL WE HAVE....."
2760 PRINT"aNOTHER RUN OF NECESSARY ROOM";
2770 INPUT X$
2780 IF X$<>"Y" AND X$<>"YES" THEN 2810
2790 PRINT"GET TO DRINKING...HERE WE GO !!"
2800 FOR I=1TO1200:NEXT:CLEAR:DD=1:GOTO 275
2810 PRINT
2820 PRINT"END"
2830 END
