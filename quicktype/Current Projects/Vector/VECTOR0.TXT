100 REM ************************
110 REM FAST-VECTOR ALGORITHM BY
120 REM MICHALSKY, DDJ #74 12/82
130 REM ************************
140 CLS
150 REM ***** AQUIRE END-POINTS
170 PRINT@0,"START AT (X,Y)";:INPUT XA,YA
180 IF(XA<0) OR (YA<0) THEN 170
190 IF (XA>63) OR (YA>31) THEN 200
200 PRINT@0,"":PRINT@0,"ENDING AT (X,Y)";:INPUT XZ,YZ:CLS0
210 IF(XZ<0) OR (YZ<0) THEN 200
220 IF (XZ>63) OR (YZ>31) THEN 200
230 REM ***** INITIALIZE
240 X=XA:Y=YA:F=0
250 DX=XZ-XA:IX=SGN(DX):DX=ABS(DX)
260 DY=YZ-YA:IY=SGN(DY):DY=ABS(DY)
270 X2=DX/2:Y2=DY/2
280 IF DX<DY THEN 410
290 REM ***** CASE OF DX>=DY
300 REM STEP X BY ONES, Y AS NEEDED
310 SET(X,Y,1)
320 X=X+IX:F=F+DY
330 IF (F<0) OR (F<Y2) THEN 350
340 F=F-DX:Y=Y+IY
350 IF ABS(XZ-X)>1 THEN 310
360 SET(X,Y,1):GOTO 170
390 REM ***** CASE OF DX<DY
400 REM STEP Y BY ONES, X AS NEEDED
410 SET(X,Y,1)
420 Y=Y+IY:F=F+DX
430 IF (F<0) OR (F<X2) THEN 450
440 F=F-DY:X=X+IX
450 IF ABS(YZ-Y)>1 THEN 410
460 SET(X,Y,1):GOTO 170
