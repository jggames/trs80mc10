5 PR
7 PR"      WIPEOUT - SHEILA DOLGOWICH, IN CURSOR #11, '79"
8 PR"       Rewritten for TinyBASIC by Dave Hassler, '23"
9 PR
10 PR"   Remove all 10 numbers on the board, using the total of"
12 PR"two dice. You may remove any combination of the throw total,"
14 PR"four numbers, max. Enter '0' if no removal is possible."
16 PR"Rolling doubles earns 'extra rolls,' used if/when you can't"
18 PR"remove a number.  Good luck!"

23 P=276
24 O=280
25 T=224
27 Z=0
28 S=0
30 I=1
40 Q=USR(O,T+I,I)
45 I=I+1
50 IFI<=10 GOTO 40
55 PR
60 I=1
70 Q=USR(P,T+I)
72 IF I<10 PR" ";
75 PR I;"|";
85 I=I+1
90 IFI<=10 GOTO 70

95 PR
97 S=S+1
100 D=RND(6)+1
102 E=RND(6)+1
103 PR
105 PR"ROLL #";S;":",D,E

107 F=D+E

108 IF D=E GOSUB 800

110 PR
115 M=0
120 PR"HOW MANY #s TO REMOVE (0-4)";
125 INPUT N
130 IF N<1 GOTO 500
132 IF N>4 PR"TOO MANY."
134 IF N>4 GOTO 120
140 I=1
145 PR"#";I;":";
150 INPUT Q
155 IF Q<1 PR"BETW. 1-10 ONLY."
156 IF Q<1 GOTO 150
157 IF Q>10 PR "BETW. 1-10 ONLY."
158 IF Q>10 GOTO 150
160 J=USR(P,T+Q)
165 IF J=0 PR"ALREADY REMOVED."
170 IF J=0 GOTO 150
175 M=M+Q
180 J=USR(O,T+Q,99)
185 I=I+1
190 IF I<=N GOTO 145
200 IFM<>F GOTO 202

201 GOTO 215
202 I=1
204 J=USR(P,T+I)
206 IF J=99 THEN U=USR(O,T+I,I)
208 I=I+1
210 IF I<=10 GOTO 204
212 PR"PICKS MUST = ";F;". RE-ENTER."
213 GOTO 110

215 PR
216 PR
218 PR
220 I=1
230 Q=USR(P,T+I)
232 IF Q=99 THEN J=USR(O,T+I,0)
234 IF Q=99 THEN Q=0
236 IF Q=0 PR"  ";"|";
238 IF Q=0 GOTO 260
240 IF I<10 PR" ";
250 PR I;"|";
260 I=I+1
270 IFI<=10 GOTO 230


300 W=0
310 I=1
320 Q=USR(P,T+I)
330 W=W+Q
340 I=I+1
350 IF I<=10 GOTO 320
360 IF W>0 GOTO 95

400 PR
405 PR
410 PR"YOU REMOVED ALL 10 NUMBERS IN ";S;" ROLLS!"
420 GOTO 620

500 Z=Z-1
510 IF Z<0 GOTO 600
520 PR"ONE EXTRA ROLL USED. ";
530 IF Z=0 PR"NONE LEFT.";
540 GOSUB 810
550 GOTO 95

600 S=S-1
602 J=0
604 I=1
606 Q=USR(P,T+I)
608 IF Q=0 THEN J=J+1
609 I=I+1
610 IF I<=10 GOTO 606
612 PR
615 PR"IN ";S;" ROLLS, YOU REMOVED ";J;" NUMBERS OUT OF 10."
620 PR"PLAY AGAIN (1=YES)";
625 INPUT A
630 IF A=1 GOTO 27
640 GOTO 999

800 Z=Z+1
805 PR"DOUBLES - AN EXTRA ROLL! ";
810 IF Z>0 GOTO 850
820 RETURN
850 PR"TOTAL EXTRAS: ";Z;"."
860 RETURN

999 END