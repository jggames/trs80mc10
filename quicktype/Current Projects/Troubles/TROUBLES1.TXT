10 REM PROGRAM TITLE: TROUBLES
20 CLS:CLEAR 1200:GOSUB 30:GOTO 50
30 PRINT TAB(9)"T R O U B L E S"
40 PRINT:RETURN
50 PRINT"SO YOU HAVE SOME TROUBLES AND"
60 PRINT"YOU ARE TURNING TO ME, YOUR"
61 PRINT"COMPUTER. I WILL HELP YOU IN"
70 PRINT"EVERY WAY I CAN USING LOGICAL"
90 PRINT"DEDUCTIONS. LEASE BE SPECIFIC"
100 PRINT"WHEN ANSWERING ALL QUESTIONS"
110 PRINT"I WILL SUBMIT TO YOU. THE MORE"
120 PRINT"SPECIFIC YOU ARE, THE BETTER"
130 PRINT"YOUR ANSWER WILL BE."
140 PRINT"USE NO COMMAS, SEMICOLONS,"
145 PRINT"COLONS OR PERIODS."
150 PRINT:PRINT"YOUR FIRST & LAST NAME";
160 INPUT N$:GOSUB 490
170 INPUT"MALE (M) OR FEMALE (F)";S$
180 IF S$="M" THEN S$="MALE":GOTO 220
190 IF S$="F" THEN S$="FEMALE":GOTO 220
200 PRINT"TRY AGAIN; CHECK IF NECESSARY."
210 GOTO 170
220 CLS
230 GOSUB 30
240 REM BEGIN
250 PRINT"DATA COLLECTED THUS FAR:"
260 PRINT"NAME:    ";N$
270 PRINT"SEX:     ";S$
290 GOSUB 550
300 INPUT"CONFIRM (Y/N)";A$
310 IF A$<>"Y" THEN F$="":GOTO 140
320 DIM X$(46),A(12),A$(12)
325 P$="":W=1:PRINT
330 PRINT"WITH MORE THAN 1 WORD AND LESS"
340 PRINT"THAN 10, DESCRIBE YOUR TROUBLES"
345 IF Y=1 THEN Y=0:PRINT"NO PERIODS..."
350 INPUT P$
360 FOR I=1 TO LEN(P$)
370 IF MID$(P$,I,1)=" " THEN 390
375 IF MID$(P$,I,1)="." THEN Y=1:GOTO 325
380 NEXT:IF W<=1 THEN 455
381 GOTO 470
390 REM WORD COUNT
400 W=W+1
410 IF W>=11 THEN 430
420 GOTO 380
430 IF LEN(P$)<=200 THEN 470
440 PRINT"YOU'VE ENTERED";W;"WORDS ";N$
445 PRINT"REENTER A SHORTER VERSION."
450 GOTO 325
455 PRINT"BE A LITTLE MORE SPECIFIC, WHAT"
460 PRINT"ABOUT ";P$;"?"
465 GOTO 325
470 REM DEFINE PARTS OF PROBLEM
480 CLS:GOSUB 30:GOTO 580
490 REM FIRST NAME BASIS
500 FOR I=1 TO LEN(N$)
510 F$=F$+MID$(N$,I,1)
520 IF MID$(N$,I,1)=" " THEN RETURN
530 NEXT
540 RETURN
550 REM TIME LOOP
560 FOR TI=1TO1000:NEXT
570 RETURN
580 REM REPLACEMENT WORDS / WORD DATA
590 FOR I=1 TO 46:READ X$(I):NEXT:L=1:Q=1
600 REM WORD COUNT / WORD IN DATA LIST
610 I=1:K=LEN(P$)
620 IF MID$(P$,I,1)=" " THEN 650
630 I=I+1:IF I=K THEN 670
640 GOTO 620
650 A(Q)=L:L=I:Q=Q+1:REM WORD SEPARATION
660 GOTO 630
670 A(Q)=L
680 A(1)=0:I=1:Z=Q:Q=1
690 FOR L=A(I)+1 TO A(I+1)-1
700 A$(Q)=A$(Q)+MID$(P$,L,1)
710 NEXT:Q=Q+1
720 IF I=Z THEN 740
730 I=I+1:GOTO 690
740 REM LAST WORD IN SENTENCE
750 FOR L=A(I)+1 TO LEN(P$)
760 A$(Q)=A$(Q)+MID$(P$,L,1)
770 NEXT:Q=1
780 A$(Z)=A$(Z+1):A$(Z+1)=""
790 REM COMPARE EACH WORD IN SENTENCE
800 REM WITH WORDS IN DATA LIST FOR MATCH
810 I=1
820 IF X$(I)=A$(Q) THEN 1010
830 I=I+1:IF I>=30 AND Q>=3 THEN 890
840 IF I>=47 THEN 860
850 GOTO 820
860 Q=Q+1
870 IF Q>=Z+1 THEN 910
880 GOTO 810
890 PRINT@74,"SORTING WORDS"
900 GOTO 840
910 REM WORD NOT IN FILE
920 PRINT
930 PRINT"I'M VERY SORRY, BUT I AM UNABLE"
940 PRINT"TO DISCUSS THIS PROBLEM"
950 PRINT"WITH YOU. PERHAPS YOU WOULD"
960 PRINT"LIKE TO TRY ANOTHER";
970 GOSUB 980:GOTO 990
980 INPUT A$:PRINT:RETURN:REM STRING ENTRY
990 IF A$="Y" OR A$="YES" THEN 1180
1000 GOTO 4620
1010 REM WORD LOCATED / START OUTPUT
1020 W$=X$(I)
1030 M=I
1040 IF M>=1 AND M<=4 THEN 1190
1050 IF M>=5 AND M<=8 THEN 1190
1060 IF M>=9 AND M<=16 THEN 2700
1070 IF M>=20 AND M<=28 THEN 3300
1080 IF M>=29 AND M<=31 THEN 2000
1090 IF M>=32 AND M<=35 THEN 3600
1100 IF M>=36 AND M<=42 THEN 4030
1110 IF M=43 OR M=45 THEN 2100
1120 IF M=44 OR M=46 THEN 2100
1180 RESTORE:GOTO 325
1190 IF M=2 OR M=4 OR M=6 OR M=7 THEN 2100
1200 PRINT"JUST HOW MANY ";W$;
1205 IF M=5 OR M=8 THEN 5820
1210 GOSUB 980:A=VAL(A$):R$=X$(43)
1220 AA=A:IF AA>=3 PRINT"GOOD GRIEF !!"
1225 IF A=1 THEN 1270
1230 PRINT"ARE ANY ONE OF THESE YOUR ";R$;
1240 GOSUB 980
1250 IF A$="YES" OR A$="Y" THEN V=1
1260 IF V=1 THEN 1390
1270 PRINT"SO YOU'RE JUST PLAYING THE FIELD."
1280 PRINT"ARE YOU SEARCHING FOR A ";R$;
1290 GOSUB 980
1300 IF A$="YES" OR A$="Y" THEN 1750
1310 PRINT"YOU PLAN ON STAYING SINGLE";
1320 GOSUB 980
1330 IF A$="YES" OR A$="Y" THEN 1750
1340 PRINT"YOU'RE NOT GOING TO STAY SINGLE,"
1350 PRINT"BUT AT THE SAME TIME YOU'RE"
1360 PRINT"LOOKING FOR A ";R$;" YOU"
1370 PRINT"HAVE GOT TROUBLES !!"
1380 GOTO 4600
1390 PRINT"DO YOU FEEL YOU ARE CHEATING ON"
1400 PRINT"YOUR ";R$;" AND YOURSELF";
1410 GOSUB 980
1420 IF A$="YES" OR A$="Y" THEN 1560
1430 PRINT"YOU DON'T ";F$;
1440 GOSUB 980:IF A$="YES" THEN 1560
1450 PRINT"ONE OF THOSE HARD-NOSED HUMANS,"
1460 PRINT"HUH? I SUGGEST YOU NIP IT"
1470 PRINT"AT THE BUD, EXPLAIN THINGS TO"
1480 PRINT"YOUR ";R$;" AND FORGET THE OTHER"
1490 PRINT W$;" BEFORE YOU GET DEEPER THAN"
1500 PRINT"YOU ARE NOW...";F$
1505 PRINT
1510 PRINT"DO YOU AGREE";:GOSUB 980
1515 IF KL=1 THEN KL=0:GOTO 4700
1520 IF A$="NO" OR A$="N" THEN 1580
1530 PRINT"I THOUGHT YOU'D SEE THINGS"
1540 PRINT"MY WAY ";F$
1550 GOTO 4600
1560 PRINT"YOU SHOULD FORGET THE OTHER"
1570 GOTO 1490
1580 PRINT"WHY DON'T YOU AGREE ";F$;
1590 GOSUB 980:GOSUB 5740
1600 PRINT"BECAUSE ";A$;"?"
1610 PRINT"HOW LONG HAS THIS BEEN"
1620 PRINT"GOING ON ";F$
1630 PRINT"SELECT ONE:"
1640 PRINT"1> DAYS"
1650 PRINT"2> WEEKS"
1660 PRINT"3> MONTHS"
1670 PRINT"4> YEARS"
1680 GOSUB 980:A=VAL(A$)
1690 IF A=1 THEN DA$="DAYS":GOTO 1740
1700 IF A=2 THEN DA$="WEEKS":GOTO 1740
1710 IF A=3 THEN DA$="MONTHS":GOTO 1740
1720 IF A=4 THEN DA$="YEARS":GOTO 1740
1730 PRINT:GOTO 1610
1740 IF JK=1 THEN JK=0:RETURN
1750 IF KJ=1 THEN KJ=0:PRINT:GOTO 3640
1755 PRINT"DID THESE ";W$;" FIND OUT"
1760 PRINT"ABOUT ONE ANOTHER ";F$;
1770 GOSUB 980
1780 IF A$="YES" OR A$="Y" THEN 1800
1790 GOSUB 1850
1800 PRINT"THAT'S THE TROUBLE! JUST STICK"
1810 PRINT"TO ONE ";X$(4);" FOR A WHILE."
1820 PRINT"THINGS WILL WORK OUT MUCH"
1830 PRINT"BETTER, YOU'LL SEE ";F$;"."
1840 GOTO 4600
1850 PRINT"THEY DIDN'T? SO THE TROUBLE"
1860 PRINT"MUST BE WITH YOU. WHY IS IT"
1870 PRINT"NECESSARY FOR YOU TO SEE";AA
1880 PRINT W$;" AT ONCE? HAVE YOU TRIED"
1890 PRINT"JUST ONE";:GOSUB 980
1900 IF A$="YES" OR A$="Y" THEN 1930
1910 PRINT"YOU HAVEN'T? TRY IT!"
1920 GOTO 1820
1930 PRINT"YOU HAVE ? I ASSUME THAT YOU"
1940 PRINT"DIDN'T LIKE IT ";F$;
1950 GOSUB 980
1960 PRINT"I MIGHT SUGGEST YOU KEEP TO"
1970 Z=RND(6):IF Z=1 THEN 1970
1980 PRINT"YOURSELF FOR AT LEAST";Z;"WEEKS."
1990 GOTO 1820
2000 PRINT"ARE YOU AWARE WHAT ";X$(30)
2010 PRINT"WILL DO TO YOUR HEALTH";:GOSUB 980
2020 IF A$="N" OR A$="NO" THEN 2060
2030 PRINT"YOU ARE? THEN YOU SHOULD TAKE"
2040 PRINT"STEPS TO STOP...NOW ";F$;"!!"
2050 GOTO 4600
2060 PRINT"THROAT CANCER, LUNG CANCER, A"
2070 PRINT"SHORTER LIFE EXPECTANCY. JUST"
2080 PRINT"TO NAME A FEW. TAKE A FEW"
2090 GOTO 2040
2100 IF M<=4 OR M=44 OR M=43 THEN R$=X$(4):JJ=1:GOTO 2120
2110 R$=X$(6)
2120 PRINT"WHAT IS THE SINGLE WORD THAT"
2130 PRINT"WOULD BEST DESCRIBE YOUR"
2140 PRINT"TROUBLES WITH THIS ";R$
2150 GOSUB 980
2160 E$=A$
2220 PRINT"NOT ENOUGH ";E$;" ";F$;
2230 GOSUB 980
2235 IF A$<>"YES" AND A$<>"RIGHT" THEN 2280
2240 PRINT"SO YOU ARE JUST NOT SATISFIED"
2250 PRINT"WITH THE WAY THINGS ARE GOING"
2260 PRINT"WITH YOUR ";E$;
2270 GOSUB 980
2280 PRINT"OH I SEE ";F$;". HAVE YOU"
2290 PRINT"DISCUSSED THIS WITH ";
2300 IF JJ=1 THEN PRINT"HER";:GOTO 2320
2310 PRINT"HIM";
2320 GOSUB 980
2330 IF A$="N" OR A$="NO" THEN 2360
2340 PRINT"AND I ASSUME IT DIDN'T HELP."
2350 JK=1:GOSUB550:PRINT:GOSUB 1610:GOTO 2430
2360 PRINT"TRY IT; THINGS JUST MIGHT"
2370 PRINT"WORK OUT FOR THE BEST. IF"
2380 PRINT"NOT, CONSULT WITH ME AGAIN"
2390 PRINT"AND I WILL GIVE YOU A"
2400 PRINT"DIFFERENT ANSWER TO YOUR"
2410 PRINT"TROUBLES, ";F$;"."
2420 KL=1:GOTO 1505
2430 PRINT"DURING THE PAST ";DA$;" HOW"
2440 PRINT"MANY TIMES HAVE YOU DISCUSSED"
2450 PRINT"THIS WITH ";
2460 IF JJ=1 THEN PRINT"HER";:GOTO 2480
2470 PRINT"HIM";
2480 GOSUB 980
2490 A=VAL(A$)
2500 PRINT"DID ANY OF THOSE";A;"TIMES HELP"
2510 PRINT"AT ALL ";F$;
2520 GOSUB 980
2530 IF A$="Y" OR A$="YES" THEN 2580
2535 IF A<=4 THEN 2580
2540 PRINT"I WILL HAVE TO TELL YOU TO"
2550 PRINT"SEEK SOME OUTSIDE HELP ON"
2560 PRINT"THIS MATTER...";F$:KL=1
2570 X=RND(2):IF X=1 THEN 4600
2571 GOTO 1505
2580 PRINT"CONTINUE THEN TO DISCUSS THIS"
2590 PRINT"PROBLEM OF ";E$;" WITH ";
2600 IF JJ=1 THEN PRINT"HER":GOTO 2620
2610 PRINT"HIM"
2620 PRINT"IF YOU'VE PROGRESSED THIS"
2630 PRINT"FAR, THINGS WILL SURELY GET"
2640 PRINT"BETTER...";F$
2650 GOTO 4600
2700 REM MONEY
2710 PRINT"DO YOU HAVE A STEADY "
2720 PRINT X$(36);" ";F$;
2730 GOSUB 980
2740 IF A$="N" OR A$="NO" THEN 2880
2750 IF W$<>X$(13) THEN 2770
2760 GOTO 2820
2770 PRINT"BUT YOU ARE IN ";X$(13);:GOSUB 980
2780 IF A$="N" OR A$="NO" THEN 2990
2790 PRINT"HAVE YOU CONSIDERED A ";X$(14);
2800 GOSUB 980
2810 IF A$="Y" OR A$="YES" THEN 3020
2820 PRINT"FINANCE CHARGES ARE HIGH, BUT"
2830 PRINT"ONE PAYMENT WOULD PROBABLY BE"
2840 PRINT"EASIER THAN SEVERAL DIFFERENT"
2850 PRINT"ONES ";F$;". YOU SHOULD"
2860 PRINT"CONSIDER A ";X$(14);"."
2870 GOTO 4600
2880 PRINT"ARE YOU WORKING PART-TIME";
2890 GOSUB 980
2900 IF A$="N" OR A$="NO" THEN 3120
2910 PRINT"O.K. ";F$;" YOU HAVE GOT A ";X$(36)
2920 PRINT"BUT, YOU ARE ONLY WORKING"
2930 PRINT"PART-TIME.":PRINT"ARE YOU DEEPLY IN ";
2940 PRINT X$(13);:GOSUB 980
2950 IF A$="Y" OR A$="YES" THEN 3090
2960 PRINT"SO I WILL ASSUME THAT YOU JUST"
2970 PRINT"NEED SOME EXTRA CASH..."
2980 GOTO 2790
2990 PRINT"YOU HAVE A STEADY ";X$(36);" YOU"
3000 PRINT"ARE NOT IN ";X$(13);"..."
3010 GOTO 2960
3020 PRINT"DID THE ";X$(41);" TURN YOU DOWN";
3030 GOSUB 980
3040 IF A$="Y" OR A$="YES" THEN 3250
3050 PRINT"YOU NEED ";X$(11);", YOU APPLIED"
3060 PRINT"FOR A ";X$(14);", THEY DIDN'T"
3070 PRINT"TURN YOU DOWN. WHAT'S THE BEEF";
3080 GOSUB 980:JJ=1:GOTO 3250
3090 PRINT"SO THAT'S IT !!"
3100 PRINT"SEEK A HIGHER PAYING JOB ",F$;"."
3110 GOTO 4600
3120 PRINT"ARE YOU DISABLED ";F$;:GOSUB 980
3130 IF A$="N" OR A$="NO" THEN 3200
3140 PRINT"THAT EXPLAINS THE ";X$(11);
3150 PRINT" TROUBLE."
3160 PRINT"I MIGHT SUGGEST YOU SEARCH FOR A"
3170 PRINT"NICE FINANCE ";X$(41);" THAT WILL"
3180 PRINT"UNDERSTAND YOUR PROBLEM. TODAY'S"
3190 GOTO 2820
3200 PRINT"I WILL HAVE TO ASSUME FROM ALL"
3210 PRINT"OF YOU ANSWERS THAT YOU ARE JUST"
3220 PRINT"A LAZY PERSON ";F$;"--"
3230 PRINT"LOOKING FOR EASY CASH, GET A ",X$(36);"!!"
3240 GOTO 4600
3250 PRINT"WAIT A FEW MONTHS, IF POSSIBLE"
3260 PRINT"AND REAPPLY FOR THE ";X$(14)
3265 IF JJ=1 THEN JJ=0:GOTO 3290
3270 PRINT"THEY SHOULDN'T TURN YOU DOWN A"
3280 PRINT"SECOND TIME ";F$;"."
3290 GOTO 4600
3300 IF M>=20 AND M<=22 THEN 2790
3305 IF M=25 OR M=26 THEN JX=1:GOTO 4270
3310 PRINT"HAVE YOU DISCUSSED THIS WITH"
3320 PRINT"THE ";X$(25);" OR THE ";X$(26);
3330 GOSUB 980
3340 IF A$="N" OR A$="NO" THEN 3520
3350 PRINT"DID THIS SOLVE ANYTHING ";F$;
3360 GOSUB 980
3370 IF A$="N" OR A$="NO" THEN 3400
3380 PRINT"IS THIS A DUPLEX";:GOSUB 980
3390 IF A$="N" OR A$="NO" THEN 3540
3400 PRINT"HAVE YOU TALKED TO OTHER TENANTS";
3410 PRINT"ABOUT YOUR SITUATION";:GOSUB 980
3420 IF A$="N" OR A$="NO" THEN 3560
3430 PRINT"AND THIS, EVIDENTLY, DIDN'T"
3431 PRINT"HELP."
3440 PRINT"IF YOU HAVE IN FACT TALKED TO"
3450 PRINT"THE ";X$(26);" ABOUT THIS"
3460 PRINT"AND NOTHING WAS SOLVED, I WOULD"
3470 PRINT"SUGGEST MAKING UP A SCHEME TO"
3480 PRINT"BREAK THE ";X$(27);" OR LEASE."
3490 PRINT"SEEK A DIFFERENT LOCATION WITH"
3500 PRINT"OTHER ";X$(24);" ";F$;"."
3510 GOTO 4600
3520 PRINT"DO SO NOW, THEN ANSWER THE NEXT"
3530 PRINT"SET OF QUESTIONS, ";F$:GOTO 3350
3540 PRINT"YOU LIVE IN A SINGLE ";X$(23)
3550 GOTO 3440
3560 PRINT"DO SO NOW, THEY MIGHT BE GOING"
3570 PRINT"THROUGH THE SAME THING. IF THIS"
3580 PRINT"FAILS, ";F$;" I WOULD"
3590 GOTO 3470
3600 PRINT"WHY DO YOU FEEL ";W$;" ";F$,
3610 GOSUB 980
3620 PRINT"I FELT THAT WAY ONCE, UNTIL I"
3630 PRINT"RECEIVED A NEW RAM."
3640 PRINT"DO YOU HAVE FRIENDS ";F$;
3650 GOSUB 980
3660 IF A$="N" OR A$="NO" THEN 3830
3670 PRINT"DISCUSSING DIFFERENT THINGS WITH";
3680 PRINT"THEM SHOULD MAKE YOU FEEL SOME-"
3690 PRINT"WHAT BETTER. HOW OFTEN HAVE YOU"
3700 PRINT"FELT THIS WAY ";F$:JK=1
3710 GOSUB 1630
3720 IF A=4 THEN 3800
3730 PRINT"SO IT HASN'T BEEN GOING ON TOO"
3740 PRINT"LONG ";F$;". WHAT IS THE ONE"
3750 PRINT"THING THAT WOULD CHEER YOU MOST"
3760 PRINT"AT THIS MOMENT";:GOSUB 980
3770 PRINT"THEN DO THIS NOW ";F$;". IF"
3780 PRINT"IT FAILS YOU CAN RETURN TO ME."
3790 KL=1:GOTO 1505
3800 PRINT"THAT'S A LONG TIME TO BE IN THIS"
3810 PRINT"STATE OF MIND ";F$;"."
3820 PRINT"WHAT IS THE ONE":GOTO 3750
3830 PRINT"DO YOU LIVE WHERE YOU COULD MEET"
3840 PRINT"SOME NEW FRIENDS ";F$;:GOSUB 980
3850 IF A$="N" OR A$="NO" THEN 3920
3860 PRINT"WHY DON'T YOU GET OUT AND MEET"
3870 PRINT"SOME NOW? IS SOMETHING ELSE"
3880 PRINT"HOLDING YOU BACK";:GOSUB 980
3890 PRINT"OH, I SEE ";F$:GOTO 3920
3900 IF S$="MALE" THEN R$=X$(43):GOTO 1280
3910 R$=X$(45):JJ=1:GOTO 1280
3920 PRINT"HOW ABOUT MOVING TO A"
3930 PRINT"DIFFERENT LOCATION ";F$;:GOSUB 980
3940 IF A$="N" OR A$="NO" THEN 3980
3950 PRINT"I BELIEVE THINGS WILL WORK OUT"
3960 PRINT"FOR THE BEST WITH THIS MOVE."
3970 GOTO 4600
3980 PRINT"ARE YOU TRYING TO BE CONTRARY",F$;
3990 GOSUB 980
4000 PRINT"LET'S TRY A DIFFERENT APPROACH"
4010 PRINT"TO THIS SUBJECT";F$:GOSUB 550
4015 IF M=43 OR M=45 THEN PRINT:GOTO 3640
4020 KJ=1:GOTO 3900
4030 IF M=37 OR M=38 THEN 4270
4040 PRINT"WHAT'S THE TROUBLE WITH YOUR",W$;
4050 GOSUB 980:GOTO 4540
4060 IF W$=X$(36) THEN 4080
4070 IF M=41 OR M=42 THEN 4120
4080 PRINT"HAVE YOU ASKED FOR A RAISE"
4090 PRINT"IN PAY";:GOSUB 980
4100 IF A$="N" OR A$="NO" THEN 4520
4110 PRINT"I ASSUME YOU WERE TURNED DOWN."
4120 PRINT"DO YOU BELONG TO A UNION",F$;
4130 GOSUB 980
4140 IF A$="N" OR A$="NO" THEN 4340
4150 PRINT"DO OTHERS FEEL THE WAY YOU DO",
4160 GOSUB 980
4170 IF A$="N" OR A$="NO" THEN 4440
4180 PRINT"HAVE ALL OF YOU MET TO DISCUSS"
4190 PRINT"A POSSIBLE STRIKE";F$;
4200 GOSUB 980
4210 IF A$="N" OR A$="NO" THEN 4440
4220 PRINT"AGAIN, I ASSUME THINGS DIDN'T"
4230 PRINT"TURN OUT WELL ";F$;". I WOULD"
4240 PRINT"SUGGEST THAT YOU TRY AND LOCATE"
4250 PRINT"ANOTHER ";X$(36);" ";F$;"."
4260 GOTO 4600
4270 PRINT"WHAT EXACTLY IS THE PROBLEM"
4280 PRINT"WITH YOUR ";W$;" ";F$;:GOSUB 980
4290 PRINT"HAVE YOU BROUGHT THIS TO HIS OR"
4300 PRINT"HER ATTENTION, ";F$;:GOSUB 980
4310 IF A$="N" OR A$="NO" THEN 4455
4320 PRINT"YOU HAVE AND YOU FEEL YOU AREN'T"
4330 PRINT"BEING TREATED FAIRLY";:GOSUB 980
4335 IF JK=1 THEN 3400
4340 PRINT"HOW LONG HAVE YOU BEEN EMPLOYED"
4350 PRINT"WITH THIS ";X$(41);" ";F$;
4360 JK=1:GOSUB 1630
4370 IF A=4 THEN 4390
4380 GOTO 3100
4390 PRINT"YOU'VE BEEN WITH THIS ";X$(41)
4400 PRINT"FOR QUITE AWHILE. IF POSSIBLE"
4410 PRINT"YOU SHOULD STICK WITH IT FOR AS"
4420 PRINT"LONG AS YOU CAN ";F$
4430 GOTO 4600
4440 PRINT"YOU ARE ALONE ON THIS MATTER."
4450 GOTO 3100
4455 IF JX=1 THEN 3400
4460 PRINT"SIT DOWN WITH YOUR ";X$(37);"
4470 PRINT"DISCUSS THIS MATTER FULLY WITH"
4480 PRINT"HIM OR HER.IF THEY DO NOT UNDER-"
4490 PRINT"STAND YOUR PROBLEM ";F$;" I"
4500 PRINT"WOULD SEEK ANOTHER JOB."
4510 PRINT:GOTO 4120
4520 PRINT"TRY IT, ";F$;"!!":GOTO 4120
4530 PRINT"I ";:GOTO 4240
4540 FOR U=1 TO LEN(A$)
4550 IF MID$(A$,U,4)=X$(37) THEN 4580
4560 IF MID$(A$,U,6)=X$(38) THEN 4590
4570 NEXT:GOTO 4060
4580 W$=MID$(A$,U,4):GOTO 4595
4590 W$=MID$(A$,U,6)
4595 GOTO 4270
4600 GOSUB 550:PRINT
4610 GOTO 960
4620 PRINT
4630 PRINT"END OF PROGRAM..."
4640 END
4700 IF A$="N" OR A$="NO" THEN 3980
4710 PRINT"YOU DO AGREE, I FIGURED YOU"
4720 PRINT"WOULD ";F$;"."
4721 PRINT"DO YOU ALWAYS TEND TO AGREE"
4730 PRINT"WITH COMPUTERS";
4740 GOSUB 980
4750 IF A$="N" OR A$="NO" THEN 3980
4760 PRINT"SO THEN YOU AGREE WITH COMPUTERS";
4770 PRINT"ALMOST CONSTANTLY ";F$;"."
4780 PRINT"WOULD YOU AGREE IF I SAID THAT"
4790 PRINT"IF YOU PRESS ANOTHER"
4800 PRINT"KEY, I COULD KILL THIS WHOLE"
4810 PRINT"PROGRAM, ";F$;:GOSUB 980
4820 IF A$="N" OR A$="NO" THEN CLS:GOTO 4850
4830 PRINT"GOOD THING YOU SAID ";A$;"..."
4840 GOTO 4600
4850 FOR TT=1 TO 10000:NEXT
4860 PRINT"WERE YOU SCARED...";F$;"?"
4870 GOTO 4600
5200 REM WORDS / DATA BANK
5210 DATA GIRLS,GIRL,WOMEN,WOMAN,MEN,MAN
5220 DATA BOY,BOYS,BILLS,BILL,MONEY
5230 DATA FINANCIAL,DEBT,LOAN,TAX,TAXES
5240 DATA CAR,CARS,AUTO,HOUSE,HOUSES
5250 DATA HOME,APARTMENT,APARTMENTS
5260 DATA LANDLORD,OWNER,CONTRACT,RENT
5270 DATA CIGARETTES,SMOKING,CIGARETTE
5280 DATA LONELY,ALONE,DEPRESSED,AFFRAID
5290 DATA JOB,BOSS,FOREMAN,SALARY,INCOME
5300 DATA COMPANY,INDUSTRY,WIFE
5310 DATA GIRLFRIEND,HUSBAND,BOYFRIEND
5740 REM SHORTEN STRING
5750 U$=A$:A$=""
5770 FOR I=1 TO LEN(U$)
5780 IF MID$(U$,I,2)="I " THEN 5800
5790 A$=A$+MID$(U$,I,1):NEXT:RETURN
5800 A$=A$+"YOU":I=I+1
5810 GOTO 5780
5820 GOSUB 980:A=VAL(A$)
5830 REM REDO STRING CONTENTS
5840 R$=X$(45)
5850 GOTO 1220
