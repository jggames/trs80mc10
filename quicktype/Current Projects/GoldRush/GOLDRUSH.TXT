10 REM GOLDRUSH by Joseph Weber From COMPUTE! July 1982

100 REM Atari Goldrush

110 REM 

120 REM Custom Characters

130 DATA 56,56,16,56,84,16,40,68

140 DATA 170,85,170,85,170,85,170,85

150 DATA 0,28,60,110,126,62,28,0

160 DATA 129,66,36,24,24,36,66,129

170 DATA 128,64,32,16,8,4,2,1

180 DATA 1,2,4,8,16,32,64,128

190 DATA 16,16,124,16,16,16,56,124

200 DIM CHAR$(8),WHICH(3,2)

210 CHAR$="*+-=<>%":MINER=3

220 GRAPHICS 1+16:SETCOLOR 4,6,4:SETCOLOR 0,1,10:SETCOLOR 3,4,10

230 PRINT #6;"GOLDRUSH!":SETCOLOR 2,3,0

240 POSITION 9,0:PRINT #6;"please wait"

250 CHSET=(PEEK(106)-8)*256:CHORG=57344

260 IF PEEK(CHSET+9)<>0 THEN 340

270 FOR I=0 TO 511:POKE CHSET+I,PEEK(CHORG+I):NEXT I

280 FOR I=1 TO 7

290 CHPOS=CHSET+(ASC(CHAR$(I))-32)*8

300 FOR J=0 TO 7

310 READ A:POKE CHPOS+J,A

320 NEXT J:NEXT I

330 FOR I=32 TO 39:POKE CHSET+I,255-PEEK(CHORG+I):NEXT I

340 POKE 756,CHSET/256

350 POSITION 9,0:PRINT #6;"    ";CHR$(4);"       "

360 NUGGETS=0

370 FOR I=1 TO 22:FOR J=0 TO 19

380 IF RND(0)>0.4 THEN COLOR ASC(CHAR$(2))+128:PLOT J,I:GOTO 400

390 IF I>3 THEN COLOR ASC(CHAR$(3)):PLOT J,I:NUGGETS=NUGGETS+1

400 NEXT J:NEXT I

410 CHARGES=10:POSITION 9,23:PRINT #6;"CHARGES ";CHARGES

420 XPOS=11:YPOS=0:EMF=0:GOTO 590

430 REM Main Loop

440 ST=STICK(0):TR=STRIG(0)

450 IF PEEK(20)>15 THEN POKE 709,(INT(16*RND(0))*16+10):POKE 20,0

460 IF EMF THEN SOUND 1,T,10,8:T=T*(T<8)+2:TI=TI+1:IF TI>20 THEN SOUND 1,0,0,0:GOTO 1120

470 IF 1-TR THEN IF EMF=0 THEN 820

480 IF ST=15 THEN 440

490 V=-(ST=14)*(YPOS>0)+(ST=13)*(YPOS<22)

500 H=-(ST=11)*(XPOS>0)+(ST=7)*(XPOS<19)

510 COLOR 32:PLOT XPOS,YPOS

520 XPOS=XPOS+H:YPOS=YPOS+V

530 LOCATE XPOS,YPOS,WHAT

540 IF WHAT=32 THEN 590

550 IF WHAT=ASC(CHAR$(3)) THEN GOSUB 640:GOTO 590

560 IF WHAT=ASC(CHAR$(7)) THEN GOSUB 760:GOTO 590

570 IF WHAT=4 THEN GOSUB 670:REM Cash in

580 SOUND 0,100,12,8:FOR W=1 TO 20:NEXT W:SOUND 0,0,0,0:XPOS=XPOS-H:YPOS=YPOS-V:COLOR 138:PLOT XPOS,YPOS:GOTO 440

590 COLOR 138:PLOT XPOS,YPOS

600 IF EMF AND YPOS=0 THEN EMF=0:COLOR 32:PLOT XPOS,YPOS:SOUND 1,0,0,0:GOTO 410

610 FOR W=8 TO 0 STEP -1:SOUND 0,W*5,12,W:NEXT W

620 GOTO 440

630 GOTO 630

640 FOR W=15 TO 0 STEP -1:SOUND 0,20,10,W:NEXT W:GOLD=GOLD+1:NUGGETS=NUGGETS-1:IF NUGGETS<0 THEN NUGGETS=0

650 POSITION 0,23:PRINT #6;"gold ";GOLD;"  ";

660 RETURN 

670 REM Cash in !

680 SOUND 2,4,10,4

690 FOR W=10 TO 5 STEP -1:FOR I=15 TO 0 STEP -1:SOUND 0,W,10,I:NEXT I:NEXT W

700 SOUND 2,0,0,0

710 CASH=CASH+GOLD*CHARGES:GOLD=0

720 GOSUB 650

730 POSITION 14,0:PRINT #6;CASH

740 IF NUGGETS=0 THEN POP :GOTO 360

750 RETURN 

760 REM Get the gold from the dead miner

770 FOR I=3 TO 1 STEP -1

780 IF WHICH(I,0)=XPOS AND WHICH(I,1)=YPOS THEN 800

790 NEXT I:RETURN 

800 GOLD=GOLD+WHICH(I,2):GOSUB 650

810 RETURN 

820 REM Explosion

830 XP=XPOS+H:YP=YPOS+V:IF YP=0 THEN 440

840 RESTORE 850

850 DATA 0,0,-1,-1,1,1,-1,1,1,-1

860 FOR I=1 TO 5:READ A,B

870 IF XP+A>=0 AND XP+A<=19 AND YP+B>=1 AND YP+B<=22 THEN LOCATE XP+A,YP+B,ZZ:IF ZZ=45 THEN NUGGETS=NUGGETS-1

880 NEXT I:COLOR ASC(CHAR$(4))

890 IF XP>0 AND XP<20 THEN PLOT XP,YP

900 COLOR ASC(CHAR$(5)):IF YP>2 AND XP>0 THEN PLOT XP-1,YP-1

910 IF YP<22 AND XP<19 THEN PLOT XP+1,YP+1

920 COLOR ASC(CHAR$(6)):IF YP>2 AND XP<19 THEN PLOT XP+1,YP-1

930 IF YP<22 AND XP>0 THEN PLOT XP-1,YP+1

940 DL=PEEK(560)+256*PEEK(561):SV=PEEK(712)

950 FOR W=15 TO 0 STEP -0.5:SOUND 0,50,0,W:SW=1-SW:POKE 712,SW*(4*16+6):POKE DL,112*SW:NEXT W

960 POKE DL,112:POKE 712,SV

970 REM 

980 COLOR 32:PLOT XP,YP:IF YP>2 AND XP>0 THEN PLOT XP-1,YP-1

990 IF YP<22 AND XP<19 THEN PLOT XP+1,YP+1

1000 IF YP>2 AND XP<19 THEN PLOT XP+1,YP-1

1010 IF YP<22 AND XP>0 THEN PLOT XP-1,YP+1

1020 COLOR 138:PLOT XPOS,YPOS

1030 FOR I=1 TO 20

1040 RX=INT(20*RND(0)):RY=INT(22*RND(0)+1)

1050 LOCATE RX,RY-1,Z2

1060 LOCATE RX,RY,Z:IF Z=32 AND Z2=171 THEN COLOR 171:PLOT RX,RY

1070 IF Z=138 THEN 1130

1080 NEXT I

1090 CHARGES=CHARGES-1:POSITION 17,23:PRINT #6;CHARGES;" ";

1100 IF CHARGES>0 THEN 440

1110 TI=0:EMF=1:POSITION 9,23:PRINT #6;"get out";CHR$(1);"   ";:GOTO 440

1120 REM Dead miner

1130 FOR I=14 TO 0 STEP -0.5:SETCOLOR 3,4,I:SOUND 0,I,10,I:NEXT I:RESTORE 1190

1140 READ A,W:IF A>0 THEN SOUND 0,A,10,8:FOR I=1 TO W*2:NEXT I

1150 IF A>0 THEN SOUND 0,0,0,0:FOR W=1 TO 5:NEXT W:GOTO 1140

1160 COLOR ASC(CHAR$(7)):PLOT XPOS,YPOS:WHICH(MINER,2)=GOLD:GOLD=0:GOSUB 650

1170 WHICH(MINER,0)=XPOS:WHICH(MINER,1)=YPOS:MINER=MINER-1:IF MINER=0 THEN 1210:REM Game Over

1180 SETCOLOR 3,4,10:GOTO 410

1190 DATA 100,30,100,20,100,5,100,30,85,40,90,30,100,20,105,10,100,30

1200 DATA -1,0

1210 POSITION 0,0:PRINT #6;"game over"

1220 POKE 709,PEEK(53770)

1230 IF PEEK(53279)<>6 THEN 1220

1240 RUN 