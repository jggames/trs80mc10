8 CLS:DIMST(1600):REM ROW/COLUMN STACK
9 SP=0:REM STACK POINTER
10 DIMG(9,9):REM G=GRID
11 DIMCG(9,9):REM CG=COPY GRID
12 DIMSS(9):REM SS=SHUFFLE START, USED IN SHUFFLE:
13 DIMSF(9):REM SF=SHUFFLE FINISH, USED IN SHUFFLE:
14 T=RND(-(PEEK(9)*256+PEEK(10))):T=.:REM T=TEMP RESULT
15 CT=.:REM CT=COUNTER FOR # SOLUTIONS
20 CLS:PRINT"WAIT..."
30 GOSUB800
40 GOSUB100:REM DRAW GRID
45 GOSUB200:REM CHECK GRID
50 END
55 GOSUB1200:REM SOLVE GRID
60 GOSUB100:REM DRAW GRID
65 IFT=.THENPRINT"THE GRID IS NOT FULL":GOTO70
66 PRINT"THE GRID IS FULL"
70 GOTO70
75 END
100 REM DRAWGRID:
105 CLS:FORRA=0TO2:FORRB=0TO2:FORCA=0TO2:FORCB=0TO2
125 IFG(RA*3+RB+1,CA*3+CB+1)=.THENPRINT" ";:GOTO130
126 PRINTMID$(STR$(G(RA*3+RB+1,CA*3+CB+1)),2,1);
130 NEXT
135 IFCA<>2THENPRINT"€";
140 NEXT
145 PRINT
150 NEXT
155 IFRA<>2THENPRINT"€€€€€€€€€€€"
160 NEXT
165 RETURN
200 REM CHECK GRID:
201 REM T=1 IF GRID IS FULL, ELSE 0.
210 T=1:FORCR=1TO9:FORCC=1TO9:IFG(CR,CC)=.THENCC=9:NEXT:CR=9:NEXT:T=.:RETURN
250 NEXT:NEXT:RETURN
300 REM SHUFFLE:
310 FORN=1TO9:SS(N)=N:NEXT:FORN=1TO9
330 SC=RND(9):IFSS(SC)=.THEN330
350 SS(SC)=0:SF(N)=SC:NEXT
400 REM FIND FIRST EMPTY CELL:
401 REM RETURNS R,C AS THE FIRST EMPTY CELL.
410 FORR=1TO9:FORC=1TO9:IFG(R,C)=.THENRETURN
440 NEXT:NEXT:RETURN
500 REM ISNUMBERINCOLUMN:
502 REM IS VALUE N IN COLUMN C?
503 REM RETURNS T=0 IF NUMBER NOT IN COLUMN, ELSE 1.
510 T=.:FORIR=1TO9:IFG(IR,C)=NTHENIR=9:NEXT:T=1:RETURN
540 NEXT:RETURN
600 REM IS NUMBER IN ROW:
601 REM IS VALUE N IN ROW R?
602 REM RETURN T=0 IF NUMBER NOT IN ROW, ELSE 1.
610 T=.:FORIC=1TO9:IFG(R,IC)=NTHENIC=9:NEXT:T=1:RETURN
640 NEXT:RETURN
700 REM IS NUMBER IN SQUARE:
701 REM IS VALUE N IN 3X3 @ (R,C)?
702 REM RETURN T=0 IF NUMBER NOT IN SQUARE, ELSE 1.
710 RS=INT((R-1)/3)*3+1:CS=INT((C-1)/3)*3+1:T=.:FORRC=RSTORS+2:FORCC=CSTOCS+2:IFG(RC,CC)=NTHENCC=CS+2:NEXT:RC=RS+2:NEXT:GOTO750
740 NEXT:NEXT:RETURN
750 T=1:RETURN
800 REM FILL GRID:
801 GOSUB400:REM FIND FIRST EMPTY CELL
802 REM R,C IS OUR CELL
803 GOSUB300:REM SHUFFLE
804 FORI=1TO9:N=SF(I)
806 GOSUB500:REM IS NUMBER IN COLUMN
807 IFT=1THEN823
808 GOSUB600:REM IS NUMBER IN ROW
809 IFT=1THEN823
810 GOSUB700:REM ISN UMBER IN SQUARE
811 IFT=1THEN823
812 G(R,C)=N
813 GOSUB200:REM CHECK GRID
814 IFT=1THENRETURN
815 ST(SP)=R:ST(SP+1)=C:ST(SP+2)=I
816 FORSN=1TO9:ST(SP+2+SN)=SF(SN):NEXTSN
817 SP=SP+3+9:REM SAVE THE CURRENT I/ROW/COLUMN/SHUFFLES ON THE STACK
818 GOSUB800:REM FILL GRID
819 SP=SP-3-9:REM RESTORE THE CURRENT SHUFFLES/COLUMN/ROW/I FROM THE STACK
820 R=ST(SP):C=ST(SP+1):I=ST(SP+2)
821 FORSN=1TO9:SF(SN)=ST(SP+2+SN):NEXTSN
822 IFT=1THENRETURN
823 NEXTI
824 G(R,C)=0:REM HOW TO GET THE CORRECT R,C FOR THIS ITERATION?
825 T=.:RETURN
900 REM SOLVE GRID:
901 GOSUB400:REM FIND FIRST EMPTY CELL
902 REM R,C IS OUR CELL
903 FORN=1TO9:GOSUB500:REM IS NUMBER IN COLUMN
905 IFT=1THEN917
906 GOSUB600:REM IS NUMBER IN ROW
907 IFT=1THEN917
908 GOSUB700:REM IS NUMBER IN SQUARE
909 IFT=1THEN917
910 G(R,C)=N
911 GOSUB200:REM CHECK GRID
912 IFT=1THENCT=CT+1:N=9:GOTO917
913 ST(SP)=R:ST(SP+1)=C:ST(SP+2)=N:SP=SP+3:REM PUSH CONTEXT ONTO STACK
914 GOSUB900:REM SOLVE GRID
915 SP=SP-3:R=ST(SP):C=ST(SP+1):N=ST(SP+2):REM POP CONTEXT OFF OF STACK
916 IFT=1THENRETURN
917 NEXTN
918 G(R,C)=.:T=.:RETURN
1000 REM COPY GRID:
1001 FORCR=1TO9:FORCC=1TO9:CG(CR,CC)=G(CR,CC):NEXT:NEXT:RETURN
1100 REM RESTORE GRID:
1101 FORCR=1TO9:FORCC=1TO9:G(CR,CC)=CG(CR,CC):NEXT:NEXT:RETURN
1200 REM SPARSIFY GRID:
1205 AT=5:REM # REMAINING ATTEMPTS
1210 REM SELECT A RANDOM NON-EMPTY CELL
1215 R=RND(9):C=RND(9):IFG(R,C)=.THEN1215
1225 BK=G(R,C):REM REMEMBER ITS CELL VALUE IN CASE WE NEED TO PUT IT BACK
1230 G(R,C)=.
1235 GOSUB1000:REM COPYGRID
1240 CT=.
1245 REM COUNT THE # SOLUTIONS THAT THIS GRID HAS
1250 ST(SP)=R:ST(SP+1)=C:SP=SP+2
1255 GOSUB900:REM SOLVEGRID
1260 SP=SP-2:R=ST(SP):C=ST(SP+1)
1265 GOSUB1100:REM RESTORE GRID
1270 IFCT=1THEN1280
1275 G(R,C)=BK:AT=AT-1:REM CANCEL THE CHANGE IF MORE THAN 1 SOLUTION
1280 IFAT>.THENGOSUB100:GOTO1215:REM PICK A NEW RANDOM CELL
1285 GOSUB100:RETURN
