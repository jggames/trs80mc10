8 DIM ST(1600):REM ROW/COLUMN STACK
9 SP=0:REM STACK POINTER
10 DIM G(9,9):REM G=GRID
11 DIM CG(9,9):REM CG=COPY GRID
12 DIM SS(9):REM SS=SHUFFLE START, USED IN SHUFFLE:
13 DIM SF(9):REM SF=SHUFFLE FINISH, USED IN SHUFFLE:
14 T=0:REM T=TEMP RESULT
15 CT=0:REM CT=COUNTER FOR # SOLUTIONS
20 CLS
25 POKE 65497,0
30 GOSUB 800:REM FILLGRID
35 POKE 65496,0
40 GOSUB 100:REM DRAWGRID
45 GOSUB 200:REM CHECKGRID
50 STOP
55 GOSUB 1200:REM SOLVEGRID
60 GOSUB 100:REM DRAWGRID
65 IF T=0 THEN PRINT "THE GRID IS NOT FULL" ELSE PRINT "THE GRID IS FULL"
70 GOTO 70
75 END
100 REM DRAWGRID:
105 FOR RA=0 TO 2
110 FOR RB=0 TO 2
115 FOR CA=0 TO 2
120 FOR CB=0 TO 2
125 IF G(RA*3+RB+1,CA*3+CB+1)=0 THEN PRINT " "; ELSE PRINT USING "#";G(RA*3+RB+1,CA*3+CB+1);
130 NEXT CB
135 IF CA<>2 THEN PRINT "I";
140 NEXT CA
145 PRINT ""
150 NEXT RB
155 IF RA<>2 THEN PRINT "---+---+---"
160 NEXT RA
165 RETURN
200 REM CHECKGRID:
201 REM T=1 IF GRID IS FULL, ELSE 0.
210 T=1
220 FOR CR=1 TO 9
230 FOR CC=1 TO 9
240 IF G(CR,CC)=0 THEN GOTO 280
250 NEXT CC
260 NEXT CR
270 RETURN
280 T=0
290 RETURN
300 REM SHUFFLE:
310 FOR N=1 TO 9:SS(N)=N:NEXT N
320 FOR N=1 TO 9
330 SC=RND(9)
340 IF SS(SC)=0 THEN 330
350 SS(SC)=0:SF(N)=SC
360 NEXT N
400 REM FINDFIRSTEMPTYCELL:
401 REM RETURNS R,C AS THE FIRST EMPTY CELL.
410 FOR R=1 TO 9
420 FOR C=1 TO 9
430 IF G(R,C)=0 THEN RETURN
440 NEXT C
450 NEXT R
460 RETURN
500 REM ISNUMBERINCOLUMN:
502 REM IS VALUE N IN COLUMN C?
503 REM RETURNS T=0 IF NUMBER NOT IN COLUMN, ELSE 1.
510 T=0
520 FOR IR=1 TO 9
530 IF G(IR,C)=N THEN GOTO 560
540 NEXT IR
550 RETURN
560 T=1
570 RETURN
600 REM ISNUMBERINROW:
601 REM IS VALUE N IN ROW R?
602 REM RETURN T=0 IF NUMBER NOT IN ROW, ELSE 1.
610 T=0
620 FOR IC=1 TO 9
630 IF G(R,IC)=N THEN GOTO 660
640 NEXT IC
650 RETURN
660 T=1
670 RETURN
700 REM ISNUMBERINSQUARE:
701 REM IS VALUE N IN 3X3 @ (R,C)?
702 REM RETURN T=0 IF NUMBER NOT IN SQUARE, ELSE 1.
710 RS=INT((R-1)/3)*3+1
715 CS=INT((C-1)/3)*3+1
720 T=0
725 FOR RC=RS TO RS+2
730 FOR CC=CS TO CS+2
735 IF G(RC,CC)=N THEN GOTO 755
740 NEXT CC
745 NEXT RC
750 RETURN
755 T=1
760 RETURN
800 REM FILLGRID:
801 GOSUB 400:REM FINDFIRSTEMPTYCELL 
802 REM R,C IS OUR CELL
803 GOSUB 300:REM SHUFFLE
804 FOR I=1 TO 9
805 N=SF(I)
806 GOSUB 500:REM ISNUMBERINCOLUMN
807 IF T=1 THEN GOTO 823
808 GOSUB 600:REM ISNUMBERINROW
809 IF T=1 THEN GOTO 823
810 GOSUB 700:REM ISNUMBERINSQUARE
811 IF T=1 THEN GOTO 823
812 G(R,C)=N
813 GOSUB 200:REM CHECKGRID
814 IF T=1 THEN RETURN
815 ST(SP)=R:ST(SP+1)=C:ST(SP+2)=I
816 FOR SN=1 TO 9:ST(SP+2+SN)=SF(SN):NEXT SN
817 SP=SP+3+9:REM SAVE THE CURRENT I/ROW/COLUMN/SHUFFLES ON THE STACK
818 GOSUB 800:REM FILLGRID
819 SP=SP-3-9:REM RESTORE THE CURRENT SHUFFLES/COLUMN/ROW/I FROM THE STACK
820 R=ST(SP):C=ST(SP+1):I=ST(SP+2)
821 FOR SN=1 TO 9:SF(SN)=ST(SP+2+SN):NEXT SN
822 IF T=1 THEN RETURN
823 NEXT I
824 G(R,C)=0:REM HOW TO GET THE CORRECT R,C FOR THIS ITERATION?
825 T=0
826 RETURN
900 REM SOLVEGRID:
901 GOSUB 400:REM FINDFIRSTEMPTYCELL
902 REM R,C IS OUR CELL
903 FOR N=1 TO 9
904 GOSUB 500:REM ISNUMBERINCOLUMN
905 IF T=1 THEN GOTO 917
906 GOSUB 600:REM ISNUMBERINROW
907 IF T=1 THEN GOTO 917
908 GOSUB 700:REM ISNUMBERINSQUARE
909 IF T=1 THEN GOTO 917
910 G(R,C)=N
911 GOSUB 200:REM CHECKGRID
912 IF T=1 THEN CT=CT+1:GOTO 918
913 ST(SP)=R:ST(SP+1)=C:ST(SP+2)=N:SP=SP+3:REM PUSH CONTEXT ONTO STACK
914 GOSUB 900:REM SOLVEGRID
915 SP=SP-3:R=ST(SP):C=ST(SP+1):N=ST(SP+2):REM POP CONTEXT OFF OF STACK
916 IF T=1 THEN RETURN
917 NEXT N
918 G(R,C)=0
919 T=0
920 RETURN
1000 REM COPYGRID:
1001 FOR CR=1 TO 9
1002 FOR CC=1 TO 9
1003 CG(CR,CC)=G(CR,CC)
1004 NEXT CC
1005 NEXT CR
1006 RETURN
1100 REM RESTOREGRID:
1101 FOR CR=1 TO 9
1102 FOR CC=1 TO 9
1103 G(CR,CC)=CG(CR,CC)
1104 NEXT CC
1105 NEXT CR
1106 RETURN
1200 REM SPARSIFYGRID:
1205 AT=5:REM # REMAINING ATTEMPTS
1210 REM SELECT A RANDOM NON-EMPTY CELL
1215 R=RND(9):C=RND(9)
1220 IF G(R,C)=0 THEN GOTO 1215
1225 BK=G(R,C):REM REMEMBER ITS CELL VALUE IN CASE WE NEED TO PUT IT BACK
1230 G(R,C)=0
1235 GOSUB 1000:REM COPYGRID
1240 CT=0
1245 REM COUNT THE # SOLUTIONS THAT THIS GRID HAS
1250 ST(SP)=R:ST(SP+1)=C:SP=SP+2
1255 GOSUB 900:REM SOLVEGRID
1260 SP=SP-2:R=ST(SP):C=ST(SP+1)
1265 GOSUB 1100:REM RESTOREGRID
1270 IF CT=1 THEN GOTO 1280
1275 G(R,C)=BK:AT=AT-1:REM CANCEL THE CHANGE IF MORE THAN 1 SOLUTION
1280 IF AT>0 THEN GOSUB 100:GOTO 1215:REM PICK A NEW RANDOM CELL
1285 GOSUB 100
1290 RETURN
