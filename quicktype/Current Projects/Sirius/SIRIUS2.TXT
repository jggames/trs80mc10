0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:GOTO70
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 M$="I DON'T UNDERSTAND "+CHR$(34)+M$+CHR$(34)+", CHECK MY VOCABULARY.":GOSUB1:GOTO270
70 CLS0:POKE49151,64:PRINT@6*32+10," S I R I U S ";:PRINT@8*32+7," A D V E N T U R E ";:FORX=1TO2500:NEXT
80 POKE49151,0:VB=22:ND=26:L=21:OB=6:LN=337
90 CLS:PRINT@8,"SIRIUS ADVENTURE":PF$=" "
100 PRINT@132,"PRESS: iNSTRUCTIONS OR"
110 PRINT@171,"bEGIN."
120 DIMA$(VB),B$(ND),L$(L),B(OB+1):GOSUB1430:B$(0)="????"
130 A$=INKEY$:IFA$=""THEN130
140 IFA$="I"THEN1720
150 IFA$<>"B"THEN130
160 CLS
170 IFLO=OLTHEN270
180 OL=LO:CLS:PRINT@8,"SIRIUS ADVENTURE"
190 IFLO>4ANDB(1)<>-1THENPRINT:PRINT"IT'S TOO DARK TO SEE!":GOTO270
200 M$="I AM "+L$(LO):GOSUB1
210 PRINT"--------------------------------";
220 TR=0:M$="":PRINT"VISIBLE OBJECTS:"
230 FORI=1TOOB
240 IFB(I)=LOTHENM$=M$+B$(I)+", ":TR=-1
250 NEXTI:IFTR<>-1THENPRINT"NONE":GOTO270
260 M$=LEFT$(M$,LEN(M$)-2):GOSUB1
270 PRINT"WHAT SHOULD I DO";:C$="":GOSUB1900:PRINT
280 IFC$=""THENPRINT"HUH?":GOTO270
290 FORI=1TOLEN(C$):IFMID$(C$,I,1)=" "THEN310
300 NEXTI:GOTO320
310 LE$=LEFT$(C$,I-1):RI$=MID$(C$,I+1,LEN(C$)-LEN(LE$)-1):GOTO330
320 LE$=LEFT$(C$,I):RI$=""
330 L=LEN(LE$):IFRI$=""THENR=-1:GOTO340
331 R=LEN(RI$)
340 FORI=1TOVB:IFL>LEN(A$(I))THEN360
350 IFLE$<>LEFT$(A$(I),L)THEN360
351 CC=I:I=VB:NEXT:I=CC:GOTO380
360 NEXTI
370 IFC$<>""THENM$=C$:GOTO9
380 J=0:IFR=-1THEN420
390 FORJ=1TOND
400 IFRI$<>B$(J)THENNEXTJ:GOTO410
401 CC=J:J=ND:NEXT:J=CC:GOTO420
410 M$=RI$:GOTO9
420 ONIGOSUB450,450,450,450,830,880,880,880,950,950,950,950,980,1050,980,1230,980,1120,1160,1290,1360,2010
430 IFI>4ANDI<13THEN210
440 IFI=22THEN180
441 GOTO170
450 IFJ<OB+1THENM$="I CAN'T "+CHR$(34)+A$(L)+" "+RI$;CHR$(34)+"!":GOSUB1:GOTO270
460 J=J-OB:ONJGOTO550,640,710,750,550,640,710,750,470,490,510,530,470,490,510,530,790,810,790,810
470 IFLO=13THENLO=11:GOTO480
471 GOSUB1860
480 RETURN
490 IFLO=12THENLO=11:GOTO500
491 IFLO=14THENLO=15:GOTO500
492 GOSUB1860
500 RETURN
510 IFLO=11THENLO=12:GOTO520
511 IFLO=15THENLO=14:GOTO520
512 GOSUB1860
520 RETURN
530 IFLO=11THENLO=13:GOTO540
531 GOSUB1860
540 RETURN
550 IFLO=2THENLO=1:GOTO560
551 IFLO=5THENLO=4:GOTO560
552 IFLO=6THENLO=5
560 IFLO=7THENLO=9:GOTO570
561 IFLO=11THENLO=7
570 IFLO=16ANDB(4)=-1THENGOSUB1870
580 IFLO=16ANDB(4)<>-1THENLO=17
590 IFLO=18ANDB(5)=-1THENLO=19
600 IFLO=18ANDB(5)<>-1THENGOSUB1870
610 IFLO=15THENLO=16
620 IFLO=OLTHENGOSUB1860
630 RETURN
640 IFLO=1THENLO=2:GOTO650
641 IFLO=4THENLO=5:GOTO650
642 IFLO=5THENLO=6
650 IFLO=9THENLO=7:GOTO660
651 IFLO=7THENLO=11:GOTO660
652 IFLO=16THENLO=15
660 IFLO=17THENLO=16
670 IFLO=19ANDB(5)=-1THENLO=18
680 IFLO=19ANDB(5)<>-1THENGOSUB1870
690 IFLO=OLTHENGOSUB1860
700 RETURN
710 IFLO=3THENLO=2:GOTO720
711 IFLO=4THENLO=3:GOTO720
712 IFLO=10THENLO=7
720 IFLO=7THENLO=8:GOTO730
721 IFLO=19THENLO=20:GOTO730
722 IFLO=20THENLO=21
730 IFLO=OLTHENGOSUB1860
740 RETURN
750 IFLO=2THENLO=3:GOTO760
751 IFLO=3THENLO=4:GOTO760
752 IFLO=7THENLO=10
760 IFLO=8THENLO=7:GOTO780
761 IFLO=20THENLO=19:GOTO780
762 IFLO=21THENLO=20
770 IFLO=OLTHENGOSUB1860
780 RETURN
790 IFLO=7THENLO=6:GOTO800
791 IFLO=18THENLO=17:GOTO800
792 GOSUB1860
800 RETURN
810 IFLO=6THENLO=7:GOTO820
811 IFLO=17THENLO=18:GOTO820
812 GOSUB1860
820 RETURN
830 IFJ=0THENJ=3
840 IFJ<>2THENPRINT"I CAN'T EAT THAT, STUPID.":RETURN
850 IFJ=2ANDB(J)=0THENPRINT"I ALREADY ATE IT.":RETURN
860 IFJ=2THENM$="MUNCH, CHOMP, (BURP) - THE CREAM BUN WAS DELICIOUS!":GOSUB1:B(2)=0:RETURN
870 PRINT"ERROR":STOP
880 IFJ>OBTHENM$="I CAN'T"+CHR$(34)+C$+CHR$(34)+".":GOSUB1:RETURN
890 IFB(J)=-1THENPRINT"I ALREADY HAVE IT!":RETURN
900 IFB(J)<>LOTHENM$="I CAN'T SEE THE "+B$(J)+" HERE.":GOSUB1:RETURN
910 IT=1:FORI9=1TOOB:IFB(I9)=-1THENIT=IT+1
911 NEXTI9
920 IFIT>3THENM$="I AM CARRYING TOO MUCH, CHECK INVENTORY.":GOSUB1:RETURN
930 M$="OK. I ADD A "+B$(J)+" TO MY INVENTORY.":GOSUB1
940 B(J)=-1:RETURN
950 IFJ>OBTHENM$="I CAN'T "+CHR$(34)+C$+CHR$(34)+".":GOSUB1:RETURN
960 IFB(J)<>-1THENM$="I DON'T HAVE A "+RI$+".":GOSUB1:RETURN
970 B(J)=LO:PRINT"OK":RETURN
980 IFJ>OBTHENM$="I DON'T SEE ANYTHING SPECIAL.":GOSUB1:RETURN
981 IFJ=0THENI=22:RETURN
990 IFB(J)<>-1THENM$="I AM NOT CARRYING A "+B$(J)+".":GOSUB1:RETURN
1000 ONJGOTO1010,1020,1030,1030,1030,1040
1010 PRINT"IT BURNS BRIGHTLY.":RETURN
1020 PRINT"IT LOOKS TASTY!":RETURN
1030 M$="MAGIC SEEMS TO EMANATE FROM THE "+B$(J)+".":GOSUB1:RETURN
1040 PRINT"IT'S BEAUTIFUL!":RETURN
1050 IFJ>OBTHENPRINT"YOU ARE BEING SILLY.":RETURN
1060 IFB(J)<>-1THENM$="I DON'T HAVE THE "+B$(J)+".":GOSUB1:RETURN
1070 IFJ<>3THENM$="WAVING THE "+B$(J)+" IS NOT VERY REWARDING.":GOSUB1:RETURN
1080 M$="THE ROOM DIMS AND BLURS, AND...":GOSUB1
1090 FORI=1TO2500:NEXTI
1100 IFLO=13THENLO=14:GOTO1110
1101 IFLO=14THENLO=13:GOTO1110
1102 PRINT"NOTHING HAPPENS.":RETURN
1110 PRINT"I AM MAGICALLY TRANSPORTED!":FORI=1TO2500:NEXTI:RETURN
1120 PRINT"CONFIRM (Y/N)";:GOSUB1900
1130 IFC$="Y"THENCLS:END
1140 IFC$<>"N"THEN1120
1150 PRINT:PRINT"CONFIRM cancelled":RETURN
1160 IN=0:FORI9=4TO6
1170 IFB(I9)=1THENIN=IN+20
1180 NEXTI9
1190 IFIN=60THENM$="FANTASTIC! YOU HAVE SOLVED THE ADVENTURE!":GOSUB1
1200 M$="YOU HAVE"+STR$(IN)+" POINTS OUT OF A POSSIBLE 60.":GOSUB1
1210 IFIN=60THENEND
1220 RETURN
1230 PRINT"I AM CARRYING"
1240 IN=0:M$="":FORI9=1TOOB
1250 IFB(I9)=-1THENM$=M$+B$(I9)+", ":IN=-1
1260 NEXTI9
1270 IFIN<>-1THENPRINT"NOTHING AT ALL.":RETURN
1280 M$=LEFT$(M$,LEN(M$)-2)+".":GOSUB1:RETURN
1290 PRINT"READY TAPE... PRESS enter"
1300 IN$=INKEY$:IFIN$=""THEN1300
1310 B(OB+1)=LO:CSAVE*B,"SIRIUSDA"
1350 RETURN
1360 PRINT"READY TAPE... PRESS enter"
1370 IN$=INKEY$:IFIN$=""THEN1370
1380 CLOAD*B,"SIRIUSDA"
1400 LO=B(OB+1)
1420 RETURN
1430 LO=1
1440 FORI=1TOVB:READA$(I):NEXTI
1450 FORI=1TOND:READB$(I):NEXTI
1460 DATA GO,WALK,RUN,CRAWL,EAT,GET,TAKE,GRAB,DROP,THROW,PUT,LEAVE,LOOK,WAVE,EXAMINE,INVENTORY,INSPECT
1461 DATA QUIT,SCORE,SAVE,LOAD,VOCABULARY
1470 DATA LAMP,BUN,ROD,RING,STATUE,CROWN,N,S,W,E,NORTH,SOUTH,WEST,EAST
1471 DATA NW,NE,SW,SE,NORTHWEST,NORTHEAST,SOUTHWEST,SOUTHEAST,UP,DOWN,U,D
1480 DATA 1,6,9,8,12,21
1490 FORI=1TOOB:READB(I):NEXTI
1500 FORI=1TOL:READL$(I):NEXTI:RETURN
1510 DATA "AT A PLATEAU NEAR A CLIFF. A ROCKY PATH LEADS SOUTH. SOME OBVIOUS EXITS: SOUTH"
1520 DATA "ON A ROCKY PATH LEADING NORTH AND CURVING TO THE EAST. SOME OBVIOUS EXITS: NORTH EAST"
1530 DATA "AT THE MOUTH OF A DARK CAVE. A PATH TO THE WEST CURVES NORTH. THERE IS A SLIGHT BREEZE. EXITS: WEST EAST"
1540 DATA "JUST INSIDE A DARK CAVE. LIGHT COMES FROM THE WEST. THERE IS A RANK SMELL. A TUNNEL LEADS SOUTH. EXITS: WEST SOUTH"
1550 DATA "IN A LOW NORTH/SOUTH TUNNEL. SOME OBVIOUS EXITS: NORTH SOUTH"
1560 DATA "IN AN OVAL CAVERN. THERE IS A FORBIDDING STONE STAIRCASE HERE. SOME OBVIOUS EXITS: NORTH DOWN"
1570 DATA "IN A HIGH SQUARE CAVE WITH WALLS OF FROZEN ICE. THERE ARE MANY PASSAGES. OBVIOUS EXITS: NORTH SOUTH WEST EAST UP"
1580 DATA "IN A TRIANGULAR SIDE-CHAMBER. SOME OBVIOUS EXITS: EAST"
1590 DATA "IN A MUSTY-SMELING ALCOVE. SOME OBVIOUS EXITS: SOUTH"
1600 DATA "IN AN EERIE CHAMBER-- FAINT SQUEALING SOUNDS COME FROM THE WALLS. SOME OBVIOUS EXITS: WEST"
1610 DATA "IN AN ENORMOUS CAVE. THERE IS A DOUBLE PILLAR OF GREEN STONE DOWN THE CENTRE. SOME EXITS: NORTH SOUTHWEST SOUTHEAST"
1620 DATA "IN A MALODOUROUS TUNNEL. SOME OBVIOUS EXITS: NORTHEAST"
1630 DATA "IN A ROOM IN WHICH THE ONLY VISIBLE EXIT IS THE WAY I CAME IN. SOME OBVIOUS EXITS: NORTHWEST"
1640 DATA "IN A SECRET ROOM, REACHED ONLY BY MAGICAL MEANS. SOME OBVIOUS EXITS: NORTHEAST"
1650 DATA "IN A OCTAGONAL ROOM. SOME OBVIOUS EXITS: NORTH SOUTHWEST"
1660 DATA "IN AN ENORMOUS MISTY CAVERN. MIST OBSCURES THE CEILING. SOME OBVIOUS EXITS: NORTH SOUTH"
1670 DATA "IN A TINY BOX-SHAPED ROOM. A DOOR LEADS SOUTH AND STAIRS LEAD DOWN. SOME OBVIOUS EXITS: SOUTH DOWN"
1680 DATA "IN A STRANGE ROOM. THERE IS A FAINT WHIFF OF CHLORINE. SOME OBVIOUS EXITS: NORTH UP" 
1690 DATA "IN A STEAMY CHAMBER, WITH WARM WALLS. SOME OBVIOUS EXITS: WEST SOUTH"
1700 DATA "IN A LARGE ROOM, LITTERED WITH ALABASTER SLABS. SOME OBVIOUS EXITS: WEST EAST"
1710 DATA "IN THE THRONE ROOM OF THE EVIL URLORD! A LOW DOOR LEADS EAST. SOME OBVIOUS EXITS: EAST"
1720 CLS:M$="YOUR QUEST IS TO EXPLORE THE CAVE OF THE EVIL URLORD, "
1721 M$=M$+"AND BRING BACK TO THE EDGE OF THE CLIFF THE FOLLOWING VALUABLES:":GOSUB1:PRINT
1740 PRINT"1. THE WHITE GOLD RING"
1750 PRINT"2. THE SACRED SILVER STATUE"
1760 PRINT"3. THE JEWELLED CROWN OF THE":PRINT"   URLORD"
1770 PRINT
1780 PRINT"BE CAREFUL...":PRINT
1790 PRINT"PRESS c TO CONTINUE."
1800 FORI=1TO4000
1810 A$=INKEY$:IFA$=""THEN1840
1820 IFA$<>"C"THEN1810
1830 GOTO170
1840 NEXTI
1850 GOTO170
1860 M$="YOU CANNOT GO IN THAT DIRECTION.":GOSUB1:RETURN
1870 M$="AN INVISIBLE FORCE PREVENTS YOU FROM PASSING.":GOSUB1
1880 FORI=1TO2500:NEXTI
1890 RETURN
1900 INPUTC$:RETURN
2010 CLS:PRINTA$(22):PRINT@64,;
2020 FORI9=1TOVB:M$=A$(I9):GOSUB1:NEXTI9
2030 A$=INKEY$:IFA$=""THEN2030
2031 RETURN
3010 REM SIRIUS ADVENTURE
3020 REM (C) MAY 1983 MLADEN BAUK.
3040 REM MODIFIED FOR THE COLOUR COMPUTER BY--MICRO-80
