10 CLS:PRINT"STRONGHOLD OF THE DWARVEN LORDS"
20 GOSUB 640
30 GOSUB 480
50 M=M+1:IF M>150 THEN 960
60 CLS:PRINT:PRINT
70 PRINT"STEP NUMBER"M
80 PRINT
90 PRINT"NORTH: ";
100 IF A(D+1,E)=S THEN PRINT"OPEN"
110 IF A(D+1,E)=X THEN PRINT"WALL"
120 PRINT"SOUTH: ";
130 IF A(D-1,E)=S THEN PRINT"OPEN"
140 IF A(D-1,E)=X THEN PRINT"WALL"
150 PRINT"EAST: ";
160 IF A(D,E+1)=S THEN PRINT"OPEN"
170 IF A(D,E+1)=X THEN PRINT"WALL"
180 PRINT"WEST: ";
190 IF A(D,E-1)=S THEN PRINT"OPEN"
200 IF A(D,E-1)=X THEN PRINT"WALL"
210 PRINT
220 PRINT"DWARVEN SOURCE BEAM READS";
230 PRINT100*ABS(Z-D)+10*ABS(Y-E)
250 PRINT
260 PRINT"WHICH DIRECTION DO YOU WANT"
270 PRINT"TO MOVE?"
280 PRINT:PRINT"N-NORTH,S-SOUTH"
290 PRINT"E-EAST,W-WEST,H-HELP"
295 Z$=INKEY$
300 A$=INKEY$:IF A$="S" OR A$="N" OR A$="E" OR A$="W" OR A$="H" THEN 310
305 GOTO 300
310 IF A$="N" AND A(D+1,E)=X THEN 300
320 IF A$="S" AND A(D-1,E)=X THEN 300
330 IF A$="E" AND A(D,E+1)=X THEN 300
340 IF A$="W" AND A(D,E-1)=X THEN 300
350 IF A$="H" THEN GOSUB 480
360 IF A$="N" THEN D=D+1
370 IF A$="S" THEN D=D-1
380 IF A$="E" THEN E=E+1
390 IF A$="W" THEN E=E-1
400 IF Z=D AND Y=E THEN 430
410 GOTO 50
430 PRINT:PRINT"YOU FOUND THE DWARVEN RICHES"
440 PRINT"IN JUST"M"STEPS!":FOR J=1 TO 2500:NEXT J
450 GOSUB500
460 INPUT"ANOTHER GAME(Y/N)";N$:IFLEFT$(N$,1)="N"THENEND
465 IFLEFT$(N$,1)="Y"THEN RUN ELSE 460
480 REM HELP
490 CLS
500 PRINT
510 PRINT"NORTH"
520 FOR B=15 TO 1 STEP -1
530 FOR C=1 TO 15
540 IF A(B,C)=X THEN PRINT"€";
550 IF B=D AND C=E THEN PRINT"*";:GOTO570
560 IF A(B,C)=S THEN PRINT" ";
570 NEXT:IFB>1THENPRINT
580 NEXT
590 M=M+15
600 FOR J=1 TO 3000:NEXT J 
610 CLS:A(D,E)=S 
620 RETURN
630 REM ****
640 PRINT 
650 N=1:PRINT "PRESS ANY KEY" 
660 N=RND(0):IF INKEY$="" THEN 660 
680 CLS 
690 DIM A(15,15) 
700 B=INT(RND(0)*3)+1
710 Z=14:Y=14
720 IF B=2 THEN Y=2
730 IF B=3 THEN Z=2
740 X=1:S=2
750 FOR B=1 TO 15:FOR C=1 TO 15
760 A(B,C)=X:IF RND(0)>.8 THEN A(B,C)=S
770 IF C<2 OR C>14 OR B<2 OR B>14 THEN A(B,C)=X
780 NEXT C:NEXT B
790 D=2:E=2
800 FOR F=1 TO 68
810 READ B,C
820 A(B,C)=S
830 NEXT F
840 M=-15
850 RETURN
860 DATA 2,2,2,3,2,4,2,5,2,6,2,7
870 DATA 3,7,4,7,5,7,5,6,5,5,5,4,5,3,6,3
880 DATA 7,3,7,4,7,5,7,6,7,7,7,8,7,9,9,8
890 DATA 9,9,10,8,10,7,10,6,10,5,10,4,8,8
900 DATA 10,3,11,3,12,3,13,3,14,3,14,2,7,10
910 DATA 6,10,5,10,4,10,3,10,2,10,2,11,2,12
920 DATA 2,13,2,14,6,11,6,12,6,13,6,14,7,12
930 DATA 14,12,8,12,8,14,9,12,9,13,9,14,10,12
940 DATA 11,9,11,10,11,11,11,12,12,9,13,9,13,10
950 DATA 13,11,13,12,13,13,13,14,14,14
960 SOUND 200,6:SOUND 193,6:SOUND 181,8:FOR J=1 TO 2500:NEXT J
970 PRINT:PRINT:PRINT"AFTER"M"STEPS, YOU HAVEN YET"
980 PRINT"TO FIND THE DWARVEN":PRINTTAB(10)"TREASURE!!"
990 PRINT"SHAME ON YOU..."
1000 GOSUB 500:FOR J=1 TO 2500:NEXT:END
