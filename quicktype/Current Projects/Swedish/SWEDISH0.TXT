445 REM **
10 REM ** VERALLASPELARESKORT (FOR ALL PLAYER'S CARDS)
20 REM
30 REM ** 
40 REM ** GST
50 PRINT:PRINT"       *** S W E D I S H   W H I S T ***"
60 REM TRANSLATED (POORLY) FROM SWEDISH TO ENGLISH, AND MODIFIED
61 REM SCORING AND INPUT FROM TSC BASIC TO ALTAIR BASIC 1.1 
62 REM BY DAVE HASSLER, AND CORRECTED AND MODIFIED FURTHER BY
63 REM NILS ANDREAS, IN 2024, FROM A FIND ON AN ARCHIVED FLEX 2 DISK
70 REM IMAGE DATED 7/20/1980.  BRIEF INSTRUCTIONS ALSO ADDED '24.
75 REM (SOME QUIRKS AND ATTITUDE LEFT AS I FOUND THEM :^)
80 REM      GERMAN TRASNLATION BY NILS ANDREAS
85 PRINT:Z1=0:Z2=0:SD=0
0090 PRINT"WEISST DU WIE MAN '4 HAND WHIST' SPIELT? (J/N)";
0100 INPUT Q$
0110 IFQ$="J"THEN210
0120 IFQ$="N"THEN160
0130 PRINT
0140 PRINT"BITTE NUR 'J' ODER 'N', DANKE."
0150 GOTO100
0160 PRINT
0170 PRINT"SO EIN PECH; DANN WIRST DU ES LERNEN MUESSEN"
0180 PRINT
0190 PRINT"NA DANN VIEL GLUECK!"
200 GOSUB 4300: REM GOTO 2280
210 DIM A(4,13),B$(4),C$(13),E(6),K$(2),L$(4),V(2,4),W(4),CA$(13)
215 PRINT
220 FORI=1TO4
230 READ B$(I)
0240 DATA"HERZ","CARO","KREUZ","PIK"
250 NEXT I
260 FORI=1TO4
270 W(I)=0
280 NEXTI
290 FORI=1TO13
295 READ C$(I)
0300 DATA"ZWEI","DREI","VIER","FUENF","SECHS","SIEBEN"
0305 DATA"ACHT","NEUN","ZEHN","BUBE","DAME","KOENIG","ASS"
310 NEXTI
315 FORI=1TO13:READ CA$(I)
320 DATA"2","3","4","5","6","7","8","9","Z","B","D","K","A"
330 NEXT I
340 FORI=1TO2
350 READK$(I)
360 DATA"PASSEN","SPIELEN"
370 NEXT I
380 FORI=1TO4
390 READ L$(I)
400 DATA"DU","KARIN","LARS","KERSTIN"
410 NEXT I
420 PRINT"WARTE ENE MINUTE, ICH MISCHE......."
425 FORI=1TO4
430 FORJ=1TO13
440 A(I,J)=0
450 NEXT J
460 NEXT I
470 FORI=1TO4
480 E(I)=1
490 NEXT I
495 E(5)=0: E(6)=0
500 FORX=1TO13
510 FORY=1TO3
520 GOSUB 2290
530 A(I,J)=Y
540 NEXTY
550 NEXTX
552 FORI=1TO4
554 FORJ=1TO13
556 IFA(I,J)=0THENA(I,J)=4
558 NEXTJ
559 NEXTI
570 PRINT
580 PRINT"DIE KARTEN SIND VERTEILT, LASS UND ANFANGEN!"
590 F=0
600 GOSUB 2340
610 FORF=2TO4
620 G=0
630 GOSUB 2510
640 IFG>4 THEN E(F)=2
650 NEXT F
660 PRINT
670 PRINT"WAS WAEHLST DU?, 'PASSEN' OR 'SPIELEN'";
680 INPUT H$
690 IFH$=K$(1)THEN720
700 IFH$<>K$(2)THEN670
710 E(1)=2
720 REM
730 PRINT
770 PRINT"      ";L$(3);" WAEHLT ";K$(E(3))
810 PRINT L$(2);"-";K$(E(2));"     ";L$(4);"-";K$(E(4))
850 PRINT"       ";L$(1);" WAEHLST ";K$(E(1))
870 PRINT
890 M=0
900 I=1
910 IFE(I)=1THEN940
920 M=I
930 GOTO960
940 I=I+1:IF I<=4THEN910
950 IFM=0THEN1000
960 PRINTL$(I);" WAEHLT ";K$(2)
970 I=I-1
980 IFI=0THENI=4
990 GOTO1020
1000 PRINT"ALLE HABEN PASSEN GEWAEHLT. NOLLSPEL! EIN NULLSPIEL."
1010 I=1
1020 PRINT"DAS HEISST ";L$(I);" FAENGT AN."
1030 PRINT
1040 PRINT"BIST DU BEREIT? (J/N)";
1050 INPUT Q$
1060 IFQ$<>"J"THEN1040
1070 T=T+1
1080 PRINT
1090 ON I GOTO 1100,1270,1390,1540
1100 F=0
1110 GOSUB 2340
1120 GOSUB 3320
1130 IFW(1)=1THEN1170
1140 IFM=0THENV(1,1)=I
1150 IFM>0THENV(2,1)=I
1160 W(1)=1
1170 S=X
1180 O=1
1190 I=1
1200 GOSUB 3600
1210 Y=J
1220 FORI=2TO4
1230 GOSUB 4240
1240 GOSUB 3600
1250 NEXT I
1260 GOTO 1680
1270 O=2
1280 GOSUB 3800
1290 FORI=2TO4
1300 GOSUB 4240
1310 GOSUB 3600
1320 NEXT I
1330 GOSUB 4150
1340 GOSUB 4090
1350 GOSUB 2340
1360 GOSUB 3320
1370 GOSUB 3690
1380 GOTO 1680
1390 O=3
1400 GOSUB 3800
1410 FORI=3TO4
1420 GOSUB4240
1430 GOSUB3600
1440 NEXTI
1450 GOSUB4150
1460 GOSUB4090
1470 GOSUB2340
1480 GOSUB3320
1490 GOSUB3690
1500 I=2
1510 GOSUB4240
1520 GOSUB3600
1530 GOTO1680
1540 O=4
1550 GOSUB3800
1560 I=4
1570 GOSUB4240
1580 GOSUB3600
1590 GOSUB4150
1600 GOSUB4090
1610 GOSUB2340
1620 GOSUB3320
1630 GOSUB3690
1640 FORI=2TO3
1650 GOSUB4240
1660 GOSUB3600
1670 NEXTI
1680 REM PRINT
1690 PRINT
1700 PRINTL$(O);" NIMMT DEN STICH."
1710 REM PRINT
1720 REM PRINT
1730 REM PRINT
1740 ON O GOTO 1770,1750,1770,1750
1750 E(6)=E(6)+1
1760 GOTO1780
1770 E(5)=E(5)+1
1780 I=O
1790 IF E(6)+E(5)=13THEN1850
1800 PRINT
1810 PRINT"WIR HABEN";E(5);", DIE ANDEREN HABEN";E(6)
1815 PRINT
1820 PRINT"ZEIT FUER DEN NAECHSTEN ZUG. BIST DU BEREIT? - DRUECKE (B)."
1830 INPUT Q$
1835 REM PRINT CHR$(5)
1840 GOTO1070
1850 REM PRINT
1860 REM PRINT
1870 PRINT
1880 PRINT"LASS UND RECHNEN..."
1890 PRINT
1900 PRINTL$(1);" UND ";L$(3);" HABEN";E(5);"STICHE, UND ";
1910 PRINTL$(2);" UND ";L$(4);" HABEN";E(6);"STICHE."
1930 REM PRINT
1940 TS=E(6)-E(5)
1950 IFM>0THEN2050
1955 REM ****** SCORING FOR NULL GAME
1960 REM PRINT
1970 IF M=0 THEN PRINT"DIESE RUNDE WAR EIN NULLSPIEL."
1980 IFTS>0THEN2020
1990 I=2
2000 J=4
2010 GOTO2210
2020 I=1
2030 J=3
2040 GOTO2210
2045 REM ******* SCORING FOR REGULAR GAME
2050 IFTS>0THEN2160
2060 IFM=1THEN2080
2070 IFM<>3THEN2130
2080 PRINT
2090 PRINTL$(M);" WAEHLT ";K$(2);"."
2100 IFM=1THEN2020
2110 IFM=3THEN2020
2120 GOTO1990
2130 PRINT
2140 PRINT"SEHR GUT! DOPPELTE PUNKTZAHL!"
2150 GOTO2020
2160 IFM=2THEN2080
2170 IFM=4THEN2080
2180 PRINT
2190 PRINT"ABER NUR";E(5);"STICHE? DAFUER KASSIEREN SIE 2 X PUNKTE."
2200 GOTO1990
2205 REM ********** OUTCOME OF DEAL
2210 PRINT
2220 PRINT"DAS HEISST ";L$(I);" UND ";L$(J);
2223 PRINT" HABEN DIE RUNDE GEWONNEN.":PRINT:IF SD=1 THEN 5080
2225 GOSUB 4500: REM *** ADD IN CURRENT SCORE TO TOTAL SCORE
2230 IF Z1<13 AND Z2<13 THEN 2240: REM *** MORE TO PLAY
2235 GOTO 5000: REM END OF GAME
2240 PRINT"GIB 'B' EIN, WENN DU FUER DIE NAECHSTE RUNDE BEREIT BIST."
2250 INPUT Q$
2260 GOTO 420
2280 REM END
2290 REM ** DEAL THE CARDS
2300 I=INT(RND(1)*4)+1
2310 J=INT(RND(1)*13)+1
2320 IFA(I,J)>0THEN2300
2330 RETURN
2340 REM
2350 PRINT
2360 PRINT"DU HAST: "
2370 IFF=0THEN2400
2380 I=F
2390 GOTO2410
2400 I=1
2410 PRINT"  "
2420 D$=B$(I)
2430 FORJ=1TO13
2440 IFA(I,J)<>1THEN2470
2450 PRINTD$,C$(J)
2460 D$="   "
2470 NEXTJ
2480 IFF<>0THEN2500
2490 I=I+1:IF I<=4 THEN 2410
2500 RETURN
2510 REM ** 'CRUEL PLAYERS'
2520 Y=0
2530 FORI=1TO4
2540 X=0
2550 FORJ=11TO13
2560 IFA(I,J)<>FTHEN2590
2570 G=G+1
2580 X=X+1
2590 NEXT J
2600 IFX<YTHEN2630
2610 V(2,F)=I
2620 Y=X
2630 NEXT I
2640 Y=0
2650 FORI=1TO4
2660 X=0
2670 FORJ=4TO1STEP-1
2680 IFA(I,J)=FTHENX=X+1
2690 NEXT J
2700 IFX<YTHEN2730
2710 Y=X
2720 V(1,F)=I
2730 NEXT I
2740 IFV(1,F)=0THENS=INT(RND(1)*4)+1
2750 IFV(2,F)=0THENS=INT(RND(1)*4)+1
2760 RETURN
2770 REM ** 'MOST INVOLVED' CARDS OF THE GAME
2780 REM
2790 J=1
2800 IFA(S,J)<>ITHEN2860
2810 IFJ<YTHEN2860
2820 A(S,J)=0
2830 Y=J
2840 O=I
2850 GOTO3040
2860 J=J+1:IF J<=13 THEN 2800
2870 REM ** 'GRE'
2880 J=1
2890 IFA(S,J)<>ITHEN2920
2900 A(S,J)=0
2910 GOTO3040
2920 J=J+1:IF J<=13 THEN 2890
2930 REM
2940 FORP=1TO4
2950 J=1
2960 IFA(P,J)<>ITHEN3020
2970 IFE(I)=2THEN3000
2980 IFT>9THEN3000
2990 IFJ>10THEN3020
3000 A(P,J)=0
3010 GOTO3040
3020 J=J+1:IF J<=13 THEN 2960
3030 NEXT P
3035 P=4
3040 N=J
3050 RETURN
3060 REM ** MOST 'PARTICIPATING' CARD AT PASS
3070 REM
3080 J=13
3090 IFA(S,J)<>ITHEN3130
3100 IFJ>YTHEN3130
3110 A(S,J)=0
3120 GOTO3300
3130 J=J-1:IF J>=1 THEN 3090
3140 REM
3150 J=1
3160 IFA(S,J)<>ITHEN3210
3170 A(S,J)=0
3180 Y=J
3190 O=I
3200 GOTO3300
3210 J=J+1:IF J<=13 THEN 3160
3220 REM
3230 FORP=1TO4
3240 J=13
3250 IFA(P,J)<>ITHEN3280
3260 A(P,J)=0
3270 GOTO3300
3280 J=J-1:IF J>=1 THEN 3250
3290 NEXTP
3295 P=4
3300 N=J
3310 RETURN
3320 REM
3330 PRINT
3340 IFF=0THEN3370
3350 PRINT"BITTE NUR DIE ZAHL DER KARTE (2,3,4,5,6,7,8,9,Z,B,D,K,A)"
3360 GOTO3410
3370 PRINT"GIB DIE FARBE,ZAHL EIN, DANN <RETURN>"
3390 PRINT"WELCHE KARTE WAEHLST DU?";
3400 INPUT A$:Q$=LEFT$(A$,1):R$=RIGHT$(A$,1)
3405 GOTO3420
3410 INPUT R$:R$=RIGHT$(R$,1)
3415 PRINT
3420 I=1
3430 IFLEFT$(B$(I),1)<>LEFT$(Q$,1)THEN3500
3440 J=1
3450 IFCA$(J)<>R$THEN3490
3460 IFA(I,J)=1THEN3560
3470 PRINT"SIE HABEN DIESE KARTE NICHT."
3480 GOTO3530
3490 J=J+1:IF J<=13 THEN 3450
3500 I=I+1:IF I<=4 THEN3430
3510 PRINT"ICH KENNE DIESE KARTE NICHT."
3530 PRINT"VERSUCHEN SIE ES ERNEUT."
3540 GOSUB2340
3550 GOTO3330
3560 A(I,J)=0
3570 X=I
3580 N=J
3590 RETURN
3600 REM
3610 IFP<>0THEN3650
3620 PRINTL$(I),"SPIELT ";C$(N);" ";B$(S)
3630 REM PRINT
3640 GOTO3680
3650 PRINTL$(I),"LEGT ";C$(N);" ";B$(P);" AB"
3660 REM PRINT
3670 P=0
3680 RETURN
3690 REM
3700 IFX<>STHEN3760
3710 IFJ<YTHEN3740
3720 Y=J
3730 O=1
3740 P=0
3750 GOTO3770
3760 P=X
3770 I=1
3780 GOSUB3600
3790 RETURN
3800 REM
3810 Y=0
3820 IFM=0THENJ=1
3830 IFM>0THENJ=2
3840 IFO=3THEN3890
3850 IFO=4THEN3920
3860 IFW(4)=0THEN3950
3870 S=V(J,4)
3880 GOTO4030
3890 IFW(1)=0THEN3950
3900 S=V(J,1)
3910 GOTO4030
3920 IFW(2)=0THEN3950
3930 S=V(J,2)
3940 GOTO4030
3950 S=V(J,O)
3960 W(O)=1
3970 GOTO3990
3980 S=INT(RND(1)*4)+1
3990 I=1
4000 IFA(S,I)=OTHEN4070
4010 I=I+1:IF I<=13 THEN 4000
4020 GOTO3980
4030 I=1
4040 IFA(S,I)=OTHEN4070
4050 I=I+1:IF I<=13 THEN 4040
4060 GOTO3950
4070 X=S
4080 RETURN
4090 REM
4100 IFF<>0THEN4140
4110 PRINT"GIB (B) EIN, WENN DU SOWEIT BIST."
4120 INPUT Q$
4130 PRINT
4140 RETURN
4150 REM
4160 J=1
4170 IFA(S,J)<>1THEN4210
4180 F=S
4190 Q$=B$(S)
4200 GOTO4230
4210 J=J+1:IF J<=13 THEN 4170
4220 F=0
4230 RETURN
4240 REM
4250 IFM>0THENGOSUB2770
4260 IFM=0THENGOSUB3060
4270 RETURN

4300 PRINT"   NAJA, DAS HABE ICH IMMER GESAGT WENN ICH MEINE GEGNER"
4310 PRINT"FERTIG GEMACHT HABE. ABER MITTLERWEILE BIN ICH NETTER ;) ."
4320 PRINT "   ICH BIN 'LARS', UND IN DIESEM SPIEL DEIN PARTNER BEI"
4330 PRINT "4 HAND WHIST. ICH SITZE DIR GEGENUEBER. WIR"
4335 PRINT "SPIELEN GEGEN DIE QUASI ROBOTER KARIN UND"
4340 PRINT "KERSTIN - STADT CHAMPIONS AUS UPSALA."
4350 PRINT"   WHIST IST SKAT SEHR AEHNLICH. ES WIRD VERSUCHT STICHE ZU"
4360 PRINT "LANDEN ODER ZU VERMEIDEN. DIE ERSTE KARTE EINER RUNDE GIBT"
4361 PRINT "DEN TRUMPH VOR. NACHDEM DU ALLE KARTEN AUF DER HAND HAST "
4362 PRINT "WIRST DU GEFRAGT OB DU PASST ODER AKZEPTIERST:"
4363 PRINT "AKZEPTIERT AUCH NUR EIN EINZIGER SPIELER SEIN BLATT," 
4364 PRINT "WERDEN STICHE GESAMMELT. PASSEN ALLE SPIELER WIRD" 
4365 PRINT "EINE NULLRUNDE GESPIELT. DAS HEISST ES WIRD"
4366 PRINT "VERSUCHT SO WENIG WIE MOEGLICH STICHE ZU ERLANGEN. "
4367 INPUT "BITTE 'B' DRUECKEN";Q$
4370 PRINT "DAS TEAM MIT DER HOECHSTEN TRUMPH KARTE HOLT DEN STICH."
4380 PRINT "WENN DU KEINEN TRUMPH SPIELEN KANNST MUSST DU EINE ANDERE"
4390 PRINT"KARTE ABLEGEN. DER GEWINNER EINES STICHES GIBT DIE NAECHSTE"
4395 PRINT "KARTE. ES GIBT 13 MOEGLICHE STICKE(52/4)."
4400 PRINT:PRINT "WAEHLEN WIR 'SPIELEN' UND VERLIEREN, BEKOMMEN UNSERE"
4401 PRINT "GEGNER DIE DOPPELTEN PUNKTE!"
4402 PRINT "  UM EINE KARTE ZU LEGEN, GIB ZUERST DEN ERSTEN BUCHSTABEN"
4403 PRINT "DER FARBE EIN (K,C,H,P) (C FUER CARO!)," 
4404 PRINT "DANN DIE ZAHL (2,3,4,5,6,7,8,9,Z,B,D,K,A), ALSO Z.B. C9"
4410 PRINT:INPUT"   GEBE 'B' EIN, WENN DU STARTKLAR BIST. ";A$
4440 PRINT:PRINT"SPIELST DU 1) EIN VOLLES SPIEL AUF 13 PUNKTE,"
4445 INPUT"      ODER 2) EINE KLEINE RUNDE?";SD:SD=SD-1
4450 PRINT:INPUT"OK. GEBE 'B' EIN WENN DU SOWEIT BIST. ";A$
4460 RETURN
4500 REM *** ADD DEAL TOTAL TO OVERALL SCORE. IF NULL, CHECK 1ST
4510 IF M<>0 THEN 4570
4520 IF TS<0 THEN Z2=Z2+(7-E(6)):GOTO 4620
4530 Z1=Z1+(7-E(5))
4540 GOTO 4620
4569 REM *** SOMEONE CALLED 'PLAY' FOR THE DEAL, SO CHECK FOR SCORE
4570 PL=2:IF E(1)=2 OR E(3)=2 THEN PL=1: REM PL 1=US 2=THEM
4580 IF PL=1 AND E(5)>E(6) THEN Z1=Z1+(E(5)-6): GOTO 4620
4590 IF PL=1 AND E(6)>E(5)THENZ2=Z2+((E(6)-6)*2):GOTO 4620
4600 IF PL=2 AND E(6)>E(5) THEN Z2=Z2+(E(6)-6):GOTO 4620
4610 IF PL=2 AND E(5)>E(6)THENZ1=Z1+((E(5)-6)*2)
4620 PRINT"AKTUELLE PUNKTZAHL: DU/LARS";Z1;"- KARIN/KERSTIN";Z2
4630 PRINT:RETURN
4999 REM *** ONE TEAM GOT 13+ POINTS. END OF GAME
5000 PRINT"THAT IS THE END OF THE GAME, BECAUSE"
5010 IF Z2>Z1 THEN 5040
5020 PRINT"LARS UND DU HABT ZUSAMMEN 13 PUNKTE ERREICHT."
5030 GOTO 5050
5040 PRINT"KARIN UND KERSTIN HABEN 13 PUNKTE VOR EUCH ERREICHT."
5050 PRINT:PRINT"MOECHTEST DU NOCHMAL SPIELEN(J/N)";
5060 INPUT Q$
5070 IFQ$="J"THEN Z1=0:Z2=0:GOTO420
5080 PRINT:PRINT"DANKE FUERS SPIELEN. 'HA EN BRA DAG'."
5090 END
