0 CLS:CLEAR384:DIMCC,ZZ,M$,I$:GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 INPUT"HIT enter";M$:CLS:RETURN

40 K=0:Z$="I'VE TOO MUCH TOO CARRY. TRY -TAKE INVENTORY-":GOSUB1250:GOTO100
50 X=Y=Z:K=R=V:N=LL=F:TP$=K$
80 CLS:PRINT"WELCOME TO ADVENTURE LAND (#4.2)"
81 M$="UNLESS TOLD DIFFERENTLY YOU MUST FIND *TREASURES* AND RETURN THEM TO THEIR PROPER PLACE!":GOSUB1
85 M$="I'M YOUR PUPPET. GIVE ME ENGLISH COMMANDS THAT CONSIST OF A NOUN AND VERB. SOME EXAMPLES...":GOSUB1
86 M$="TO FIND OUT WHAT YOU'RE CARRYING YOU MIGHT SAY: TAKE INVENTORY":GOSUB1:GOSUB9
87 M$="TO GO INTO A HOLE YOU MIGHT SAY: GO HOLE":GOSUB1:M$="TO SAVE CURRENT GAME: SAVE GAME":GOSUB1
88 M$="YOU WILL AT TIMES NEED SPECIAL ITEMS TO DO THINGS, BUT I'M SURE YOU'LL FIGURE THESE THINGS OUT.":GOSUB1
90 PRINT:PRINT"HAPPY ADVENTURING...":GOSUB9:RETURN

100 R=AR:LX=LT:DF=0:SF=0:INPUT"USE 'SAVED' GAME";K$:IFLEFT$(K$,1)<>"Y"THEN130
110 INPUT"READY SAVED TAPE "M$:CLOAD*I,"ADVENTDA":SF=I(IL+1):LX=I(IL+2):DF=(IL+3):R=I(IL+4):GOSUB80
130 GOSUB50:GOSUB240:GOTO160

140 INPUT"TELL ME WHAT TO DO";TP$:PRINT:GOSUB170:IFFTHENPRINT"YOU USE WORD(S) I DON'T KNOW":GOTO140
150 GOSUB360:IFI(9)<>-1THEN160
151 LX=LX-1:IFLX<0THENPRINT"LIGHT HAS RUN OUT":I(9)=0:GOTO160
152 IFLX<25THENM$="LIGHT RUNS OUT IN"+STR$(LX)+" TURNS!":GOSUB1
160 NV(0)=0:GOSUB360:GOTO140

REM Parse input
170 K=0:NT$(0)="":NT$(1)=""
180 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1:GOTO190
181 NT$(K)=NT$(K)+K$
190 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN230
191 NT$(X)=LEFT$(NT$(X),LN):FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
200 IFX=1THENIFY<7THENK$=LEFT$(K$,LN)
210 IFNT$(X)=K$THENNV(X)=Y:GOTO220
211 NEXTY:GOTO230
220 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO220
230 NEXTX:F=NV(0)<1ORLEN(NT$(1))>.ANDNV(1)<1:RETURN

240 IFDFTHENIFI(9)<>-1ANDI(9)<>RTHENPRINT"I CAN'T SEE, IT'S TOO DARK.":RETURN
250 K=-1:IFLEFT$(RS$(R),1)="*"THENM$=MID$(RS$(R),2)+".":GOSUB1:GOTO260
251 M$="I'M IN A "+RS$(R)+".":GOSUB1
260 FORZ=0TOIL:IFKTHENIFI(Z)=RTHENPRINT"VISIBLE ITEMS HERE:":K=.
270 GOTO300

280 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/"THENRETURN
281 FORW=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1):W=1
282 NEXTW:RETURN

REM 300 IF I(Z)<>R THEN320 ELSE GOSUB280:IFPOS+LEN(TP$)+3>63THENPRINT
300 IFI(Z)<>RTHEN320
301 GOSUB280
310 M$=TP$+".":GOSUB1
320 NEXT
330 K=-1:M$="":FORZ=0TO5:IFKTHENIFR(R,Z)<>.THENM$=M$+"OBVIOUS EXITS: ":K=.
340 IFR(R,Z)<>.THENM$=M$+NV$(Z+1,1)+" "
350 NEXT:IFM$<>""THENGOSUB1
351 RETURN


REM *** Search for matching actions ***

REM Find a matching action
360 F2=-1:F=-1:F3=.:IFNV(0)=1THENIFNV(1)<7THEN610
361 FORX=0TOCL:V=INT(VAL(C$(X,.))/150):IFNV(.)=.THENIFV<>.THENCC=X:X=CL:NEXTX:X=CC:RETURN
370 IFNV(0)<>VTHENNEXTX:GOTO990
371 N=VAL(C$(X,0))-V*150
380 IFNV(0)<>0THEN390
381 F=.:IFRND(100)<=NTHEN400
382 NEXTX:GOTO990
390 IFN<>NV(1)THENIFN<>.THENNEXTX:GOTO990

REM Process the conditions                        K=condition code, LL="number"
400 F2=-1:F=.:F3=-1:FORY=1TO5:W=VAL(C$(X,Y)):LL=INT(W/20):K=W-LL*20:F1=-1
401 ONK+1GOTO550,430,450,470,490,500,510,520,530,540,410,420,440,460,480

REM 10. Is player carrying anything at all?
410 F1=-1:FORZ=0TOIL:IFI(Z)=-1THENCC=Z:Z=IL:NEXTZ:Z=CC:GOTO550
411 NEXTZ:F1=.:GOTO550

REM 11. Is player carrying nothing?
420 F1=0:FORZ=0TOIL:IFI(Z)=-1THENCC=Z:Z=IL:NEXTZ:Z=CC:GOTO550
421 NEXTZ:F1=-1:GOTO550

REM 1. Is player carrying object LL?
430 F1=I(LL)=-1:GOTO550

REM 12. Is obj LL not being carried by player and not in current room?
440 F1=I(LL)<>-1ANDI(LL)<>R:GOTO550

REM 2. Is player in room with object LL?
450 F1=I(LL)=R:GOTO550

REM 13. Is obj LL not in the storeroom (room zero)?
460 F1=I(LL)<>.:GOTO550

REM 3. Is player in room with obj LL or carrying it?
470 F1=I(LL)=RORI(LL)=-1:GOTO550

REM 14. Is obj LL in storeroom (room zero)?
480 F1=I(LL)=.:GOTO550

REM 4. Is player in room LL?
490 F1=R=LL:GOTO550

REM 5. Is player carrying obj LL, or obj LL isn't in current room?
500 F1=I(LL)<>R:GOTO550

REM 6. Is player not carrying obj LL?
510 F1=I(LL)<>-1:GOTO550

REM 7. Is player not in room LL?
520 F1=R<>LL:GOTO550

REM 8. Is numbered flag-bit set?
530 F1=SFANDINT(2^LL+.5):F1=F1<>.:GOTO550

REM 9. Is numbered flag-bit clear?
540 F1=SFANDINT(2^LL+.5):F1=F1=.:GOTO550

550 F2=F2ANDF1:IFF2THENNEXTY:GOTO560
551 CC=Y:Y=5:NEXTY:Y=CC:NEXTX:GOTO990

REM Process the commands
560 IP=.:FORY=1TO4:K=INT((Y-1)/2)+6:ONYGOTO570,580,570,580
570 AC=INT(VAL(C$(X,K))/150):GOTO590
580 AC=VAL(C$(X,K))-INT(VAL(C$(X,K))/150)*150
590 IFAC>101THEN600
591 IFAC=.THEN960
592 IFAC<52THENM$=MS$(AC):GOSUB1:GOTO960
593 ONAC-51GOTO660,700,740,760,770,780,790,760,810,830,840,850,860,870,890,920,930,940,950,710,750
600 M$=MS$(AC-50):GOSUB1:GOTO960

610 L=DF:IFLTHENL=DFANDI(9)<>RANDI(9)<>-1:IFLPRINT"DANGEROUS TO MOVE IN THE DARK!"
620 IFNV(1)<1THENPRINT"GIVE ME A DIRECTION TOO.":RETURN
630 K=R(R,NV(1)-1):IFK>=1THEN640
631 IFLTHENPRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=.:GOTO640
632 PRINT"I CAN'T GO IN THAT DIRECTION":RETURN
640 IFNOTLTHENCLS
650 R=K:GOSUB240:RETURN

REM 52. GETx
660 L=.:FORZ=1TOIL:IFI(Z)=-1THENL=L+1
670 NEXTZ
680 IFL>=MXTHENM$=Z$:GOSUB1:GOTO970
690 GOSUB1050:I(P)=-1:GOTO960

REM 53. DROPx
700 GOSUB1050:I(P)=R:GOTO960

REM 71. SAVE
710 PRINT"SAVING GAME":IFD=-1THENINPUT"READY TAPE & HIT ENTER";K$
720 I(IL+1)=SF:I(IL+2)=LX:I(IL+3)=DF:I(IL+4)=R:CSAVE*I,"ADVENTDA"
730 GOTO960	

REM 54. GOTOy
740 GOSUB1050:R=P:GOTO960

REM 72. EXx,x (Exchange the room locations of the Par #1 object and the Par #2 object)
750 GOSUB1050:L=P:GOSUB1050:Z=I(P):I(P)=I(L):I(L)=Z:GOTO960

REM 55/59. x->RM0
760 GOSUB1050:I(P)=.:GOTO960

REM 56. NIGHT
770 DF=-1:GOTO960

REM 57. DAY
780 DF=.:GOTO960

REM 58. SETz
790 GOSUB1050
800 SF=INT(.5+2^P)ORSF:GOTO960

REM 60. CLRz
810 GOSUB1050

820 SF=SFANDNOTINT(.5+2^P):GOTO960

REM 61. DEAD
830 PRINT"I'M DEAD...":R=RL:DF=.:GOTO860

REM 62. x->y (Move the Par #1 object to the Par #2 room)
840 GOSUB1050:L=P:GOSUB1050:I(L)=P:GOTO960

REM 63. FINI
850 PRINT"THE GAME IS NOW OVER.":INPUT"ANOTHER GAME?";K$:IFLEFT$(K$,1)="N"THENEND
851 FORX=0TOIL:I(X)=I2(X):NEXT:GOTO100

REM 64. DspRM
860 GOSUB240:GOTO960

REM 65. SCORE
870 L=.:FORZ=1TOIL:IFI(Z)=TRTHENIFLEFT$(IA$(Z),1)="*"THENL=L+1
880 NEXTZ:PRINT"I'VE STORED";L;"TREASURES.":M$="ON A SCALE OF 0 TO 100 THAT RATES A"+STR$(INT(L/TT*100)):GOSUB1
881 IFL=TTTHENPRINT"WELL DONE.":GOTO850
882 GOTO960

REM 66. INV
REM 890 PRINT"I'M CARRYING:":K$="NOTHING":PROCd3("890 FOR"):FORZ=0TOIL:IFI(Z)<>-1THEN910 ELSEGOSUB280:IFLEN(TP$)+POS>63PRINT
890 PRINT"I'M CARRYING:":M$="":K$="NOTHING":FORZ=0TOIL:IFI(Z)<>-1THEN910
891 GOSUB280
900 M$=M$+TP$+", ":K$=""
910 NEXT:IFK$<>""THENPRINTK$:GOTO960
911 M$=M$+LEFT$(M$,LEN(M$)-2):GOSUB1:GOTO960

REM 67. SET0 (Sets the flag-bit numbered 0)
920 P=.:GOTO800

REM 68. CLR0 (Clears the flag-bit numbered 0)
930 P=.:GOTO820

REM 69. FILL (Re-fill the artificial light source (obj 9) and pick it up)
940 LX=LT:I(9)=-1:GOTO960

REM 70. CLS
950 CLS:GOTO960

REM Next command
960 NEXTY

REM Stop processing non-automatic actions
970 IFNV(0)<>.THENCC=X:X=CL:NEXTX:X=CC:GOTO990

REM Next automatic action
980 NEXTX
990 IFNV(0)=.THENRETURN
1010 GOSUB1060
1020 IFFTHENPRINT"I DON'T UNDERSTAND YOUR COMMAND":RETURN
1030 IFNOTF2THENPRINT"I CAN'T DO THAT YET"

REM Return from action-matching routine
1040 RETURN

REM Get param from condition list
1050 IP=IP+1:W=VAL(C$(X,IP)):P=INT(W/20):M=W-P*20:IFM<>.THEN1050
1051 RETURN

REM Automatically GET or DROP
1060 IFNV(0)<>10THENIFNV(0)<>18ORF3THEN1230
1070 IFNV(1)=.THENPRINT"WHAT?":GOTO1180
1080 IFNV(0)<>10THEN1110
1090 L=.:FORZ=0TOIL:IFI(Z)=-1THENL=L+1
1100 NEXT:IFL>=MXTHENM$=Z$:GOSUB1:GOTO1180
1110 K=.:FORX=0TOIL:IFRIGHT$(IA$(X),1)<>"/"THEN1190
1111 LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FORY=LLTO2STEP-1:IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO1190
1112 CC=Y:Y=2:NEXTY:Y=CC
1120 TP$=LEFT$(MID$(TP$,Y+1),LN)
1130 IFTP$<>NV$(NV(1),1)THEN1190
1140 IFNV(0)=10THEN1160
1150 IFI(X)<>-1THENK=1:GOTO1190
1151 I(X)=R:K=3:GOTO1170
1160 IFI(X)<>RTHENK=2:GOTO1190
1161 I(X)=-1:K=3
1170 PRINT"OK.":CC=X:X=IL:NEXTX:X=CC
1180 F=0:RETURN
1190 NEXTX
1200 IFK=1THENPRINT"I'M NOT CARRYING IT":GOTO1210
1201 IFK=2THENPRINT"I DON'T SEE IT HERE"
1210 IFK=.THENIFNOTF3THENM$="IT'S BEYOND MY POWER TO DO THAT":GOSUB1:F=0
1220 IFK<>.THENF=.
1230 RETURN

1250 READIL,CL,NL,RL,MX,AR,TT,LN,LT,ML,TR
REM 1260 W=INT((IL+INT(CL/2)+INT(NL/10)+RL+ML)/12):REM PRINTW+1;"MINUTES TO LOAD."
1270 DIMNV(1),NV$(NL,1),IA$(IL),I(IL+4),RS$(RL),R(RL,5),MS$(ML),NT$(1),I2(IL)
1280 FORX=0TOCL:READM$,M$,M$,M$,M$,M$,M$,M$:NEXT
1290 FORX=0TONL STEP10:FORCC=0TO9:FORY=0TO1
1291 READNV$(X+CC,Y):NEXTY
REM 1295 P.NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y)
1297 NEXTCC,X
1298 FORX=0TORL:READR(X,0),R(X,1),R(X,2),R(X,3),R(X,4),R(X,5),RS$(X):NEXT:FORX=0TOML:READMS$(X):NEXT
1320 FORX=0TOIL:READIA$(X),I(X):I2(X)=I(X):NEXT:RETURN

2000 DATA60,151,59,33,5,11,13,3,125,71,3
2001 DATA75,161,386,160,200,,17612,,10,401,420,400,146,,1852,8850,8,421,,,,,2011,,8,521,520,260,,,2609,7800
2002 DATA100,108,760,820,420,100,8312,10264,100,484,,,,,5613,,5,141,140,20,246,0
2003 DATA6062,,5,406,426,400,442,146,7845,,8,482,152,,,,2311,,100,104,,,,,8550,,50,161,246,160,1100,,7259,7800
2004 DATA100,148,140,940,500,,10262,9900,30,841,426,406,400,,7845,,50,542,141,,,,10504,9150,100,248,642,720,640,700
2005 DATA8005,7950,100,248,542,1040,540,,8005,,100,28,49,20,40,,6360,8700,25,288,260,280,,,7860,9150,100,248,240,,,0
2006 DATA9660,,100,269,260,,,,16558,17357,100,28,48,20,40,,4110,9000,4366,22,,,,,6900,,4374,,,,,,9750,,4404
2007 DATA682,,,,,6900,,4407,82,,,,,6900,,1521,142,421,420,140,,8902,450,1542,462,141,460,,,7800,,1521,142,401
2008 DATA400,140,,8902,450,2742,461,460,502,780,500,8864,8005,2742,461,460,,,,7950,,1523,482,146,,,,2311,,1523,482
2009 DATA141,266,,,2400,,1523,482,141,261,260,520,8902,,1533,,,,,,9900,,4350,,,,,,9600,,5100,,,,,,9900
2010 DATA0,3450,581,344,460,,,8118,8614,2125,623,566,,,,2850,,2125,621,561,620,,,3021,9209,6794,,,,,,17100,0
2011 DATA2125,622,561,620,240,,10555,8720,184,404,702,380,,,8164,,1525,24,806,,,,2400,,1525,24,801,800,620,,8902,0
2012 DATA2725,621,620,800,,,8902,3450,2125,362,561,,,,3300,,6803,,,,,,17100,,185,384,,,,,3750,,1510,762,760
2013 DATA505,,,7800,,2710,761,760,582,20,,7986,8700,6343,921,920,,,,509,,1513,122,261,260,240,,8902,,900,384,420
2014 DATA726,,,8164,,900,424,380,,,,8164,,185,424,502,,,,3900,,185,424,505,440,,,8164,,7815,,,,,,10650,0
2015 DATA204,682,,,,,7650,,2723,521,502,520,480,280,4259,8008,1513,122,266,,,,2400,,5751,62,,,,,300,,207,40,102
2016 DATA0,,,8164,,2713,241,240,260,,,8902,4350,1528,442,440,200,,,8319,8294,1257,100,102,292,80,221,8303,1050,5870
2017 DATA104,322,286,,,900,,5570,104,322,286,,,900,,3611,221,60,220,,,4558,7950,5870,322,281,320,340,,8303,9600
2018 DATA2737,721,685,720,,,7950,,900,384,721,,,,5011,,2737,721,1120,900,680,720,8003,8309,1537,722,720,,,,4852,0
2019 DATA3300,,,,,,5100,,3900,,,,,,9813,,1510,762,502,,,,3900,,2710,761,585,820,760,,5303,8850,1050,68,765
2020 DATA60,,,16710,,4800,68,60,542,,,5910,,4950,,,,,,9750,,7050,401,,,,,16613,15750,7050,421,,,,,16613
2021 DATA15750,184,364,,,,,15300,,1554,682,,,,,7650,,5250,502,860,360,500,,6212,8250,2723,521,542,480,880,540
2022 DATA8003,8293,7350,68,60,,,,9001,16607,5870,342,,,,,9600,,166,702,380,,,,8164,,1050,68,760,100,80,502,8308
2023 DATA4710,6761,,,,,,16614,,5400,,,,,,197,,207,82,60,,,,8164,,1257,102,221,100,80,281,8303,1200,6038,502
2024 DATA0,,,,3947,,6039,542,,,,,5897,,6313,241,240,260,,,509,7800,6313,122,,,,,450,,6342,461,460,,,,509
2025 DATA0,7500,322,68,320,340,60,8303,810,4050,524,10,,,,4950,,4050,524,11,200,,,8164,,1200,226,,,,,5700,,6600
2026 DATA943,221,220,500,140,2762,8700,6600,221,527,220,500,,2762,9900,4217,183,,,,,7650,,1521,142,140,,,,7800,0
2027 DATA4217,203,169,960,160,,7403,8700,4217,203,228,,,,150,,4217,203,208,220,660,960,7558,8159,4217,203,188,200,660
2028 DATA980,7558,8159,4217,203,168,980,180,,7403,8700,7650,401,400,420,,,462,8902,7650,421,,,,,463,9150,4050,527,,0
2029 DATA0,,15300,,3450,586,,,,,150,,6600,,,,,,150,,5281,,,,,,16607,5100,6807,,,,,,15450,,2723,521
2030 DATA480,520,260,,8009,7800,6780,,,,,,15450,,6771,,,,,,15450,,7200,,,,,,15600,,207,224,560,,,,8164
2031 DATA0,7050,524,,,,,16605,16350,7050,224,,,,,16605,,7050,384,,,,,16605,,7050,464,,,,,16606,,7050,264
2032 DATA0,,,,16609,,7050,344,,,,,16609,,7050,304,,,,,16609,,7050,424,,,,,16605,,7050,164,,,,,16608,0
2033 DATA5570,281,322,340,320,,8005,,206,342,120,,,,8156,9600,2117,201,200,180,,,8902,1500,2119,181,180,200,,,8902
2034 DATA1350,1551,62,,,,,1711,,166,1042,480,,,,8164,,1549,,,,,,16611,,2100,561,365,,,,3600,,5250,,,,0
2035 DATA0,150,,3450,581,347,340,,,8118,8464,7050,24,,,,,16605,,3611,226,,,,,5700,,7050,404,,,,,16616,15450
2036 DATA6774,,,,,,9750,,166,84,100,,,,8164,,1542,462,482,146,,,2311,,7050,,,,,,150,,1200,,,,,,197
2037 DATA0,3600,,,,,,16800,,AUT,ANY,GO,NORTH

2040 DATA*ENT,SOUTH,*RUN,EAST,*WAL,WEST,*CLI,UP,JUM,DOWN,BEA,NET,CHO,FIS,*CUT,AWA,TAK,MIR,*GET,AXE,*PIC,*AX,*CAT,WAT,LIG
2041 DATABOT,*TUR,GAM,*LAM,HOL,*BUR,LAM,DRO,*ON,*REL,OFF,*SPI,DOO,*LEA,MUD,STO,*MED,AWA,BEE,THR,SCO,TOS,GAS,QUI,FLI,SWI
2042 DATAEGG,RUB,OIL,LOO,*SLI,*SHO,KEY,*SEE,HEL,DRA,BUN,SCO,INV,INV,LED,YEL,THR,WAK,CRO,UNL,BRI,REA,BEA,OPE,DRA,ATT,RUG,*HIT
2043 DATARUB,DRI,HON,*GAT,FRU,BUN,OX,FIN,RIN,*LOC,CHI,HEL,*BIT,SAY,BRA,WIN,SIG,DOO,BLA,SCR,WEB,SAV,*WRI,"",SWA,"",LAV,"",ARO
2044 DATA"",HAL,"",TRE,"",*STU,"",FIR

2050 DATA0,7,10,1,,24,"",23,1,1,25,,,DISMAL SWAMP,,,,,,1,TOP OF A TALL CYPRESS TREE,1,1,1,1,1,4
2051 DATALARGE HOLLOW DAMP STUMP IN THE SWAMP,,,,,3,5,ROOT CHAMBER UNDER THE STUMP
2052 DATA0,,,,4,,SEMI-DARK HOLE NEXT TO ROOT CHAMBER,,,,,5,7,LONG DOWN SLOPING HALL,8,9,,27,6,12,LARGE CAVERN
2053 DATA0,7,,,,,LARGE 8 SIDED ROOM,7,,,,20,,ROYAL ANTEROOM,11,10,26,1,,26,*I'M ON THE SHORE OF A LAKE
2054 DATA11,11,23,11,,,FOREST,13,15,15,,,13,MAZE OF PITS,,,,14,12,,MAZE OF PITS,17,12,13,16,16,17
2055 DATAMAZE OF PITS,12,,13,12,13,,MAZE OF PITS,,17,,,14,17,MAZE OF PITS,17,12,12,15,14,18,MAZE OF PITS
2056 DATA0,,,,17,,BOTTOM OF A CHASM. ABOVE ME IS A LEDGE WITH A BRICKED UP WINDOW AND LEDGE FACING A THRONE ROOM.
2057 DATA0,,,20,,,*I'M ON A NARROW LEDGE BY A CHASM. ACROSS THE CHASM IS THE THRONE ROOM,,,,,,9,ROYAL CHAMBER
2058 DATA0,,,,,,NARROW LEDGE BY A THRONE ROOM. ACROSS THE CHASM IS ANOTHER LEDGE.,,,,,,21,THRONE ROOM
2059 DATA10,1,10,11,,,SUNNY MEADOW
2060 DATA0,,,,,,*I THINK I'M IN REAL TROUBLE NOW. THERE'S A FELLOW HERE WITH A PITCHFORK AND POINTED TAIL... OH HELL!
2061 DATA11,,1,11,,,HIDDEN GROVE,,,,,,,QUICK-SAND BOG,,,7,,,,MEMORY RAM OF A TRS-80. I TOOK A WRONG TURN!
2062 DATA0,,,,,11,TOP OF AN OAK. TO THE EAST I SEE A MEADOW. BEYOND THAT A LAKE,,,,,,,"",,,,,,,""
2063 DATA0,,,,,,"",,,,,,,"",,24,11,24,28,24,LARGE MISTY ROOM WITH STRANGE UNREADABLE LETTERS OVER ALL THE EXITS.

2070 DATA"",NOTHING HAPPENS,CHOP IT DOWN!,BOY THAT REALLY HIT THE SPOT!,DRAGON SMELLS SOMETHING. AWAKENS. ATTACKS ME!
2071 DATALOCK SHATTERS,I CAN'T IT'S LOCKED,TIMBER. SOMETHING FELL FROM THE TREE TOP AND VANISHED!,TIMBER!,LAMP IS OFF
2072 DATAFLAMELESS LAMP IS LIT,I'M BIT BY A SPIDER,MY CHIGGER BITES ARE NOW INFECTED,MY BITES HAVE ROTTED MY WHOLE BODY
2073 DATABEAR EATS THE HONEY AND FALLS ASLEEP,BEES STING ME,I'VE NO CONTAINER,THE BEES ALL SUFFOCATED AND DISAPEARED
2074 DATASOMETHING I'M HOLDING VIBRATES AND...,NOTHING TO LIGHT IT WITH,GAS BLADDER BLEW UP,IN MY HANDS!
2075 DATAGAS NEEDS TO BE CONTAINED BEFORE IT WILL BURN,GAS DISIPATES. (I THINK YOU BLEW IT),THAT WON'T IGNITE
2076 DATAHOW? JUMP?,BEAR WON'T LET ME,DON'T WASTE *HONEY* GET MAD INSTEAD,BEES MADDEN BEAR. BEAR THEN ATTACKS ME!
2077 DATAIT SOAKS INTO THE GROUND,IN 1 WORD TELL ME AT WHAT.,OH NO...BEAR DODGES...CRASH.,HEAVY!
2078 DATASOMETHING'S TOO HEAVY. I FALL.,TO STOP GAME SAY QUIT,MIRROR HITS FLOOR AND SHATTERS INTO MILLION PIECES
2079 DATAMIRROR LANDS SOFTLY ON RUG. LIGHTS UP AND SAYS,YOU LOST *ALL* TREASURES.,NOT CARRYING AXE. TAKE INVENTORY!
2080 DATAIT DOESN'T BOTHER HIM AT ALL,THE MUD DRIED UP AND FELL OFF,THE BEAR IS SO STARTLED HE FALLS OFF THE LGDGE.
2081 DATA*DRAGON STING* AND FADES. I DON'T GET IT. I HOPE YOU DO.
2082 DATATHE BEES ATTACK THE DRAGON WHICH GETS SO ANNOYED IT GETS UP AND FLYS AWAY...
2083 DATAMAGIC OIL ATRACKS MAGIC LAMP. LAMP IS NOW FULL.,I'M BIT BY CHIGGERS
2084 DATATHERE'S SOMETHING THERE ALL RIGHT! MAYBE I SHOULD GO THERE?,MAYBE IF I THREW SOMETHING?...,TOO DRY. FISH DIE.
2085 DATAA GLOWING GENIE APPEARS. DROPS SOMETHING. THEN VANISHGS.
2086 DATAA GLOWING GENIE APEARS. SAYS -BOY YOU'RE SELFISH- TAKES SOMETHING AND THEN MAKES -ME- VANISH!
2087 DATANO. ITS TOO HOT,NOT HERE.,TRY THE SWAMP,USE ONE WORD
2088 DATATRY--> LOOK JUMP SWIM CLIMB THROW FIND TAKE INVENTORY SCORE DROP AND ANY OTHER VERB. SOME MAY NOT NEED A NOUN.
2089 DATAONLY 3 THINGS WILL WAKE THE DRAGON AND 1 ITEM IS DANGEROUS BY ITSELF!!
2090 DATAIF YOU EVER WANT A HINT ON SOMETHING TRY -'HELP'.,READ SIGN IN THE MEADOW!,YOU MAY NEED MAGIC WORDS HERE
2091 DATAA VOICE BOOOOMS OUT,PLEASE LEAVE IT ALONE,CAN ONLY THROW AX,MEDICINE IS GOOD FOR BITES.,I DON'T KNOW WHERE IT IS
2092 DATATO WIN GET 100 WHEN YOU SAY 'SCORE'. A TREASURE IS ANYTHING WITH AN * IN THE NAME,BLOW IT UP.
2093 DATAFISH ESCAPE BACK TO LAKE,"","","",""

2100 DATA"",,DARK HOLE,4,*POT OF RUBIES*/RUB/,4,SPIDER WEB WITH WRITING ON IT,2
2101 DATA-HOLLOW- STUMP AND REMAINS OF A FELLED TREE,,CYPRESS TREE,1,WATER,10,EVIL SMELLING MUD/MUD/,1
2102 DATA*GOLDEN FISH*/FIS/,10,LIT BRASS LAMP/LAM/,0
2103 DATAOLD FASHIONED BRASS LAMP/LAM/,3,RUSTY AXE (MAGIC WORD -BUNYON- ON IT)/AXE/,10,BOTTLE WITH WATER/BOT/,3
2104 DATAEMPTY BOTTLE/BOT/,,RING OF SKELETON KEYS/KEY/,2,SIGN -LEAVE TREASURES HERE. SAY SCORE.-,3,LOCKED DOOR,5
2105 DATAOPEN DOOR WITH A HALLWAY BEYOND,,SWAMP GAS,1,*GOLDEN NET*/NET/,18,CHIGGER BITES,,INFECTED CHIGGER BITES,0
2106 DATAFLOATING PATCHES OF OILY SLIME,1,*ROYAL HONEY*/HON/,8,LARGE AFRICAN BEES,8,VERY THIN BLACK BEAR,21
2107 DATABEES IN A BOTTLE/BOT/,,LARGE SLEEPING DRAGON,23,FLINT & STEEL/FLI/,13,*THICK PERSIAN RUG*/RUG/,17
2108 DATASIGN -MAGIC WORD IS AWAY. LOOK LA (REST OF SIGN IS MISSING),18,DISTENDED GAS BLADDER/BLA/,,BRICKED UP WINDOW,20
2109 DATASIGN HERE SAYS -IN MANY CASES MUD IS GOOD. IN OTHERS...-,23,STREAM OF LAVA,18
2110 DATABRICKED UP WINDOW WITH A HOLE IN IT. BEYOND IS A LEDGE.,,LOOSE FIRE BRICKS,,*GOLD CROWN*/CRO/,22
2111 DATA*MAGIC MIRROR*,21,SLEEPING BEAR,0
2112 DATAEMPTY WINE BLADDER/BLA/,9,BROKEN GLASS,,CHIGGERS/CHI/,1,DEAD BEAR,,*DRAGON EGGS* (VERY RARE)/EGG/,0
2113 DATALAVA STREAM WITH BRICK DAM,,*JEWELED FRUIT*/FRU/,25,*SMALL STATUE OF A BLUE OX*/OX/,26,*DIAMOND RING*/RIN/,0
2114 DATA*DIAMOND BRACLET*/BRA/,,STRANGE SCRATCHINGS ON ROCK SAYS -ALADIN WAS HERE-,14
2115 DATASIGN SAYS -LIMBO. FIND RIGHT EXIT AND LIVE AGAIN!-,33,SMOKING HOLE. PIECES OF DRAGON AND GORE.,0
2116 DATASIGN SAYS -NO SWIMMING ALLOWED HERE-,10,ARROW POINTING DOWN,17,DEAD FISH/FIS/,,*FIRESTONE*(COLD NOW)/FIR/,0
2117 DATASIGN SAYS -PAUL'S PLACE-,25,TREES,11,"",,"",0
