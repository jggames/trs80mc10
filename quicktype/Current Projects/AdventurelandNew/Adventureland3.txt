0 CLS:CLEAR384:DIMCC,ZZ,M$,I$:GOTO40
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
9 INPUT"HIT enter";M$:CLS:RETURN

40 K=0:Z$="I'VE TOO MUCH TOO CARRY. TRY -TAKE INVENTORY-":GOSUB1250:GOTO100
50 X=Y=Z:K=R=V:N=LL=F:TP$=K$
80 CLS:PRINT"WELCOME TO ADVENTURE LAND (#4.2)"
81 M$="UNLESS TOLD DIFFERENTLY YOU MUST FIND *TREASURES* AND RETURN THEM TO THEIR PROPER PLACE!":GOSUB1
85 M$="I'M YOUR PUPPET. GIVE ME ENGLISH COMMANDS THAT CONSIST OF A NOUN AND VERB. SOME EXAMPLES...":GOSUB1
86 M$="TO FIND OUT WHAT YOU'RE CARRYING YOU MIGHT SAY: TAKE INVENTORY":GOSUB1:GOSUB9
87 M$="TO GO INTO A HOLE YOU MIGHT SAY: GO HOLE":GOSUB1:M$="TO SAVE CURRENT GAME: SAVE GAME":GOSUB1
88 M$="YOU WILL AT TIMES NEED SPECIAL ITEMS TO DO THINGS, BUT I'M SURE YOU'LL FIGURE THESE THINGS OUT.":GOSUB1
90 PRINT:PRINT"HAPPY ADVENTURING...":GOSUB9:RETURN

100 R=AR:LX=LT:DF=0:SF=0:INPUT"USE 'SAVED' GAME";K$:IFLEFT$(K$,1)<>"Y"THEN130
110 INPUT"READY SAVED TAPE "M$:CLOAD*I,"ADVENTDA":SF=I(IL+1):LX=I(IL+2):DF=(IL+3):R=I(IL+4):GOSUB80
130 GOSUB50:GOSUB240:GOTO160

140 INPUT"TELL ME WHAT TO DO";TP$:PRINT:GOSUB170:IFFTHENPRINT"YOU USE WORD(S) I DON'T KNOW":GOTO140
150 GOSUB360:IFI(9)<>-1THEN160
151 LX=LX-1:IFLX<0THENPRINT"LIGHT HAS RUN OUT":I(9)=0:GOTO160
152 IFLX<25THENM$="LIGHT RUNS OUT IN"+STR$(LX)+" TURNS!":GOSUB1
160 NV(0)=0:GOSUB360:GOTO140

REM Parse input
170 K=0:NT$(0)="":NT$(1)=""
180 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THENK=1:GOTO190
181 NT$(K)=NT$(K)+K$
190 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN230
191 NT$(X)=LEFT$(NT$(X),LN):FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
200 IFX=1THENIFY<7THENK$=LEFT$(K$,LN)
210 IFNT$(X)=K$THENNV(X)=Y:GOTO220
211 NEXTY:GOTO230
220 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO220
230 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN

240 IFDFTHENIFI(9)<>-1ANDI(9)<>RTHENPRINT"I CAN'T SEE, IT'S TOO DARK.":RETURN
250 K=-1:IFLEFT$(RS$(R),1)="*"THENM$=MID$(RS$(R),2)+".":GOSUB1:GOTO260
251 M$="I'M IN A "+RS$(R)+".":GOSUB1
260 FORZ=0TOIL:IFKTHENIFI(Z)=RTHENPRINT"VISIBLE ITEMS HERE:":K=.
270 GOTO300

280 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/"THENRETURN
281 FORW=LEN(TP$)-1TO1STEP-1:IFMID$(TP$,W,1)="/"THENTP$=LEFT$(TP$,W-1):W=1
282 NEXTW:RETURN

REM 300 IF I(Z)<>R THEN320 ELSE GOSUB280:IFPOS+LEN(TP$)+3>63THENPRINT
300 IFI(Z)<>RTHEN320
301 GOSUB280
310 M$=TP$+".":GOSUB1
320 NEXT
330 K=-1:M$="":FORZ=0TO5:IFKTHENIFRM(R,Z)<>.THENM$=M$+"OBVIOUS EXITS: ":K=.
340 IFRM(R,Z)<>.THENM$=M$+NV$(Z+1,1)+" "
350 NEXT:IFM$<>""THENGOSUB1
351 RETURN


REM *** Search for matching actions ***

REM Find a matching action
360 F2=-1:F=-1:F3=.:IFNV(0)=1THENIFNV(1)<7THEN610
361 FORX=0TOCL:V=INT(VAL(C$(X,.))/150):IFNV(.)=.THENIFV<>.THENCC=X:X=CL:NEXTX:X=CC:RETURN
370 IFNV(0)<>VTHENNEXTX:GOTO990
371 N=VAL(C$(X,0))-V*150
380 IFNV(0)<>0THEN390
381 F=.:IFRND(100)<=NTHEN400
382 NEXTX:GOTO990
390 IFN<>NV(1)THENIFN<>.THENNEXTX:GOTO990

REM Process the conditions                        K=condition code, LL="number"
400 F2=-1:F=.:F3=-1:FORY=1TO5:W=VAL(C$(X,Y)):LL=INT(W/20):K=W-LL*20:F1=-1
401 ONK+1GOTO550,430,450,470,490,500,510,520,530,540,410,420,440,460,480

REM 10. Is player carrying anything at all?
410 F1=-1:FORZ=0TOIL:IFI(Z)=-1THENCC=Z:Z=IL:NEXTZ:Z=CC:GOTO550
411 NEXTZ:F1=.:GOTO550

REM 11. Is player carrying nothing?
420 F1=0:FORZ=0TOIL:IFI(Z)=-1THENCC=Z:Z=IL:NEXTZ:Z=CC:GOTO550
421 NEXTZ:F1=-1:GOTO550

REM 1. Is player carrying object LL?
430 F1=I(LL)=-1:GOTO550

REM 12. Is obj LL not being carried by player and not in current room?
440 F1=I(LL)<>-1ANDI(LL)<>R:GOTO550

REM 2. Is player in room with object LL?
450 F1=I(LL)=R:GOTO550

REM 13. Is obj LL not in the storeroom (room zero)?
460 F1=I(LL)<>.:GOTO550

REM 3. Is player in room with obj LL or carrying it?
470 F1=I(LL)=RORI(LL)=-1:GOTO550

REM 14. Is obj LL in storeroom (room zero)?
480 F1=I(LL)=.:GOTO550

REM 4. Is player in room LL?
490 F1=R=LL:GOTO550

REM 5. Is player carrying obj LL, or obj LL isn't in current room?
500 F1=I(LL)<>R:GOTO550

REM 6. Is player not carrying obj LL?
510 F1=I(LL)<>-1:GOTO550

REM 7. Is player not in room LL?
520 F1=R<>LL:GOTO550

REM 8. Is numbered flag-bit set?
530 F1=SFANDINT(2^LL+.5):F1=F1<>.:GOTO550

REM 9. Is numbered flag-bit clear?
540 F1=SFANDINT(2^LL+.5):F1=F1=.:GOTO550

550 F2=F2ANDF1:IFF2THENNEXTY:GOTO560
551 CC=Y:Y=5:NEXTY:Y=CC:NEXTX:GOTO990

REM Process the commands
560 IP=.:FORY=1TO4:K=INT((Y-1)/2)+6:ONYGOTO570,580,570,580
570 AC=INT(VAL(C$(X,K))/150):GOTO590
580 AC=VAL(C$(X,K))-INT(VAL(C$(X,K))/150)*150
590 IFAC>101THEN600
591 IFAC=.THEN960
592 IFAC<52THENM$=MS$(AC):GOSUB1:GOTO960
593 ONAC-51GOTO660,700,740,760,770,780,790,760,810,830,840,850,860,870,890,920,930,940,950,710,750
600 M$=MS$(AC-50):GOSUB1:GOTO960

610 L=DF:IFLTHENL=DFANDI(9)<>RANDI(9)<>-1:IFLPRINT"DANGEROUS TO MOVE IN THE DARK!"
620 IFNV(1)<1THENPRINT"GIVE ME A DIRECTION TOO.":RETURN
630 K=RM(R,NV(1)-1):IFK>=1THEN640
631 IFLTHENPRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=.:GOTO640
632 PRINT"I CAN'T GO IN THAT DIRECTION":RETURN
640 IFNOTLTHENCLS
650 R=K:GOSUB240:RETURN

REM 52. GETx
660 L=.:FORZ=1TOIL:IFI(Z)=-1THENL=L+1
670 NEXTZ
680 IFL>=MXTHENM$=Z$:GOSUB1:GOTO970
690 GOSUB1050:I(P)=-1:GOTO960

REM 53. DROPx
700 GOSUB1050:I(P)=R:GOTO960

REM 71. SAVE
710 PRINT"SAVING GAME":IFD=-1THENINPUT"READY TAPE & HIT ENTER";K$
720 I(IL+1)=SF:I(IL+2)=LX:I(IL+3)=DF:I(IL+4)=R:CSAVE*I,"ADVENTDA"
730 GOTO960	

REM 54. GOTOy
740 GOSUB1050:R=P:GOTO960

REM 72. EXx,x (Exchange the room locations of the Par #1 object and the Par #2 object)
750 GOSUB1050:L=P:GOSUB1050:Z=I(P):I(P)=I(L):I(L)=Z:GOTO960

REM 55/59. x->RM0
760 GOSUB1050:I(P)=.:GOTO960

REM 56. NIGHT
770 DF=-1:GOTO960

REM 57. DAY
780 DF=.:GOTO960

REM 58. SETz
790 GOSUB1050
800 SF=INT(.5+2^P)ORSF:GOTO960

REM 60. CLRz
810 GOSUB1050

820 SF=SFANDNOTINT(.5+2^P):GOTO960

REM 61. DEAD
830 PRINT"I'M DEAD...":R=RL:DF=.:GOTO860

REM 62. x->y (Move the Par #1 object to the Par #2 room)
840 GOSUB1050:L=P:GOSUB1050:I(L)=P:GOTO960

REM 63. FINI
850 PRINT"THE GAME IS NOW OVER.":INPUT"ANOTHER GAME?";K$:IFLEFT$(K$,1)="N"THENEND
851 FORX=0TOIL:I(X)=I2(X):NEXT:GOTO100

REM 64. DspRM
860 GOSUB240:GOTO960

REM 65. SCORE
870 L=.:FORZ=1TOIL:IFI(Z)=TRTHENIFLEFT$(IA$(Z),1)="*"THENL=L+1
880 NEXTZ:PRINT"I'VE STORED";L;"TREASURES.":M$="ON A SCALE OF 0 TO 100 THAT RATES A"+STR$(INT(L/TT*100)):GOSUB1
881 IFL=TTTHENPRINT"WELL DONE.":GOTO850
882 GOTO960

REM 66. INV
REM 890 PRINT"I'M CARRYING:":K$="NOTHING":PROCd3("890 FOR"):FORZ=0TOIL:IFI(Z)<>-1THEN910 ELSEGOSUB280:IFLEN(TP$)+POS>63PRINT
890 PRINT"I'M CARRYING:":M$="":K$="NOTHING":FORZ=0TOIL:IFI(Z)<>-1THEN910
891 GOSUB280
900 M$=M$+TP$+", "):K$=""
910 NEXT:IFK$<>""THENPRINTK$:GOTO960
911 M$=M$+(LEFT$(M$,LEN(M$)-2):GOSUB1:GOTO960

REM 67. SET0 (Sets the flag-bit numbered 0)
920 P=.:GOTO800

REM 68. CLR0 (Clears the flag-bit numbered 0)
930 P=.:GOTO820

REM 69. FILL (Re-fill the artificial light source (obj 9) and pick it up)
940 LX=LT:I(9)=-1:GOTO960

REM 70. CLS
950 CLS:GOTO960

REM Next command
960 NEXTY

REM Stop processing non-automatic actions
970 IFNV(0)<>.THENCC=X:X=CL:NEXTX:X=CC:GOTO990

REM Next automatic action
980 NEXTX
990 IFNV(0)=.THENRETURN
1010 GOSUB1060
1020 IFFTHENPRINT"I DON'T UNDERSTAND YOUR COMMAND":RETURN
1030 IFNOTF2THENPRINT"I CAN'T DO THAT YET"

REM Return from action-matching routine
1040 RETURN

REM Get param from condition list
1050 IP=IP+1:W=VAL(C$(X,IP)):P=INT(W/20):M=W-P*20:IFM<>.THEN1050
1051 RETURN

REM Automatically GET or DROP
1060 IFNV(0)<>10THENIFNV(0)<>18ORF3THEN1230
1070 IFNV(1)=.THENPRINT"WHAT?":GOTO1180
1080 IFNV(0)<>10THEN1110
1090 L=.:FORZ=0TOIL:IFI(Z)=-1THENL=L+1
1100 NEXT:IFL>=MXTHENM$=Z$:GOSUB1:GOTO1180
1110 K=.:FORX=0TOIL:IFRIGHT$(IA$(X),1)<>"/"THEN1190
1111 LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FORY=LLTO2STEP-1:IFMID$(TP$,Y,1)<>"/"THENNEXTY:GOTO1190
1112 CC=Y:Y=2:NEXTY:Y=CC
1120 TP$=LEFT$(MID$(TP$,Y+1),LN)
1130 IFTP$<>NV$(NV(1),1)THEN1190
1140 IFNV(0)=10THEN1160
1150 IFI(X)<>-1THENK=1:GOTO1190
1151 I(X)=R:K=3:GOTO1170
1160 IFI(X)<>RTHENK=2:GOTO1190
1161 I(X)=-1:K=3
1170 PRINT"OK.":CC=X:X=IL:NEXTX:X=CC
1180 F=0:RETURN
1190 NEXTX
1200 IFK=1THENPRINT"I'M NOT CARRYING IT":GOTO1210
1201 IFK=2THENPRINT"I DON'T SEE IT HERE"
1210 IFK=.THENIFNOTF3THENM$="IT'S BEYOND MY POWER TO DO THAT":GOSUB1:F=0
1220 IFK<>.THENF=.
1230 RETURN

1250 READIL,CL,NL,RL,MX,AR,TT,LN,LT,ML,TR
REM 1260 W=INT((IL+INT(CL/2)+INT(NL/10)+RL+ML)/12):REM PRINTW+1;"MINUTES TO LOAD."
1270 DIMNV(1),C$(CL,7),NV$(NL,1),IA$(IL),I(IL+4),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
1280 FORX=0TOCL:Y=X+1:READC$(X,0),C$(X,1),C$(X,2),C$(X,3),C$(X,4),C$(X,5),C$(X,6),C$(X,7):NEXT
1290 FORX=0TONL STEP10:FORCC=0TO9:FORY=0TO1
1291 READNV$(X+CC,Y):NEXTY
REM 1295 P.NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y)
1297 NEXTCC,X
1298 FORX=0TORL:READRM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT:FORX=0TOML:READMS$(X):NEXT
1320 FORX=0TOIL:READIA$(X),I(X):I2(X)=I(X):NEXT:RETURN

2000 DATA 60,151,59,33,5,11,13,3,125,71,3
2001 DATA 75,161,386,160,200,0,17612,0,10,401,420,400,146,0,1852,8850,8,421,0,0,0,0,2011,0,8,521,520,260,0,0,2609,7800
2002 DATA 100,108,760,820,420,100,8312,10264,100,484,0,0,0,0,5613,0,5,141,140,20,246,0
2003 DATA 6062,0,5,406,426,400,442,146,7845,0,8,482,152,0,0,0,2311,0,100,104,0,0,0,0,8550,0,50,161,246,160,1100,0,7259,7800
2004 DATA 100,148,140,940,500,0,10262,9900,30,841,426,406,400,0,7845,0,50,542,141,0,0,0,10504,9150,100,248,642,720,640,700
2005 DATA 8005,7950,100,248,542,1040,540,0,8005,0,100,28,49,20,40,0,6360,8700,25,288,260,280,0,0,7860,9150,100,248,240,0,0,0
2006 DATA 9660,0,100,269,260,0,0,0,16558,17357,100,28,48,20,40,0,4110,9000,4366,22,0,0,0,0,6900,0,4374,0,0,0,0,0,9750,0,4404
2007 DATA 682,0,0,0,0,6900,0,4407,82,0,0,0,0,6900,0,1521,142,421,420,140,0,8902,450,1542,462,141,460,0,0,7800,0,1521,142,401
2008 DATA 400,140,0,8902,450,2742,461,460,502,780,500,8864,8005,2742,461,460,0,0,0,7950,0,1523,482,146,0,0,0,2311,0,1523,482
2009 DATA 141,266,0,0,2400,0,1523,482,141,261,260,520,8902,0,1533,0,0,0,0,0,9900,0,4350,0,0,0,0,0,9600,0,5100,0,0,0,0,0,9900
2010 DATA 0,3450,581,344,460,0,0,8118,8614,2125,623,566,0,0,0,2850,0,2125,621,561,620,0,0,3021,9209,6794,0,0,0,0,0,17100,0
2011 DATA 2125,622,561,620,240,0,10555,8720,184,404,702,380,0,0,8164,0,1525,24,806,0,0,0,2400,0,1525,24,801,800,620,0,8902,0
2012 DATA 2725,621,620,800,0,0,8902,3450,2125,362,561,0,0,0,3300,0,6803,0,0,0,0,0,17100,0,185,384,0,0,0,0,3750,0,1510,762,760
2013 DATA 505,0,0,7800,0,2710,761,760,582,20,0,7986,8700,6343,921,920,0,0,0,509,0,1513,122,261,260,240,0,8902,0,900,384,420
2014 DATA 726,0,0,8164,0,900,424,380,0,0,0,8164,0,185,424,502,0,0,0,3900,0,185,424,505,440,0,0,8164,0,7815,0,0,0,0,0,10650,0
2015 DATA204,682,0,0,0,0,7650,0,2723,521,502,520,480,280,4259,8008,1513,122,266,0,0,0,2400,0,5751,62,0,0,0,0,300,0,207,40,102
2016 DATA 0,0,0,8164,0,2713,241,240,260,0,0,8902,4350,1528,442,440,200,0,0,8319,8294,1257,100,102,292,80,221,8303,1050,5870
2017 DATA 104,322,286,0,0,900,0,5570,104,322,286,0,0,900,0,3611,221,60,220,0,0,4558,7950,5870,322,281,320,340,0,8303,9600
2018 DATA 2737,721,685,720,0,0,7950,0,900,384,721,0,0,0,5011,0,2737,721,1120,900,680,720,8003,8309,1537,722,720,0,0,0,4852,0
2019 DATA 3300,0,0,0,0,0,5100,0,3900,0,0,0,0,0,9813,0,1510,762,502,0,0,0,3900,0,2710,761,585,820,760,0,5303,8850,1050,68,765
2020 DATA 60,0,0,16710,0,4800,68,60,542,0,0,5910,0,4950,0,0,0,0,0,9750,0,7050,401,0,0,0,0,16613,15750,7050,421,0,0,0,0,16613
2021 DATA 15750,184,364,0,0,0,0,15300,0,1554,682,0,0,0,0,7650,0,5250,502,860,360,500,0,6212,8250,2723,521,542,480,880,540
2022 DATA 8003,8293,7350,68,60,0,0,0,9001,16607,5870,342,0,0,0,0,9600,0,166,702,380,0,0,0,8164,0,1050,68,760,100,80,502,8308
2023 DATA 4710,6761,0,0,0,0,0,16614,0,5400,0,0,0,0,0,197,0,207,82,60,0,0,0,8164,0,1257,102,221,100,80,281,8303,1200,6038,502
2024 DATA 0,0,0,0,3947,0,6039,542,0,0,0,0,5897,0,6313,241,240,260,0,0,509,7800,6313,122,0,0,0,0,450,0,6342,461,460,0,0,0,509
2025 DATA 0,7500,322,68,320,340,60,8303,810,4050,524,10,0,0,0,4950,0,4050,524,11,200,0,0,8164,0,1200,226,0,0,0,0,5700,0,6600
2026 DATA 943,221,220,500,140,2762,8700,6600,221,527,220,500,0,2762,9900,4217,183,0,0,0,0,7650,0,1521,142,140,0,0,0,7800,0
2027 DATA 4217,203,169,960,160,0,7403,8700,4217,203,228,0,0,0,150,0,4217,203,208,220,660,960,7558,8159,4217,203,188,200,660
2028 DATA 980,7558,8159,4217,203,168,980,180,0,7403,8700,7650,401,400,420,0,0,462,8902,7650,421,0,0,0,0,463,9150,4050,527,0,0
2029 DATA 0,0,15300,0,3450,586,0,0,0,0,150,0,6600,0,0,0,0,0,150,0,5281,0,0,0,0,0,16607,5100,6807,0,0,0,0,0,15450,0,2723,521
2030 DATA 480,520,260,0,8009,7800,6780,0,0,0,0,0,15450,0,6771,0,0,0,0,0,15450,0,7200,0,0,0,0,0,15600,0,207,224,560,0,0,0,8164
2031 DATA 0,7050,524,0,0,0,0,16605,16350,7050,224,0,0,0,0,16605,0,7050,384,0,0,0,0,16605,0,7050,464,0,0,0,0,16606,0,7050,264
2032 DATA 0,0,0,0,16609,0,7050,344,0,0,0,0,16609,0,7050,304,0,0,0,0,16609,0,7050,424,0,0,0,0,16605,0,7050,164,0,0,0,0,16608,0
2033 DATA 5570,281,322,340,320,0,8005,0,206,342,120,0,0,0,8156,9600,2117,201,200,180,0,0,8902,1500,2119,181,180,200,0,0,8902
2034 DATA 1350,1551,62,0,0,0,0,1711,0,166,1042,480,0,0,0,8164,0,1549,0,0,0,0,0,16611,0,2100,561,365,0,0,0,3600,0,5250,0,0,0,0
2035 DATA 0,150,0,3450,581,347,340,0,0,8118,8464,7050,24,0,0,0,0,16605,0,3611,226,0,0,0,0,5700,0,7050,404,0,0,0,0,16616,15450
2036 DATA 6774,0,0,0,0,0,9750,0,166,84,100,0,0,0,8164,0,1542,462,482,146,0,0,2311,0,7050,0,0,0,0,0,150,0,1200,0,0,0,0,0,197
2037 DATA 0,3600,0,0,0,0,0,16800,0,AUT,ANY,GO,NORTH

2040 DATA *ENT,SOUTH,*RUN,EAST,*WAL,WEST,*CLI,UP,JUM,DOWN,BEA,NET,CHO,FIS,*CUT,AWA,TAK,MIR,*GET,AXE,*PIC,*AX,*CAT,WAT,LIG
2041 DATA BOT,*TUR,GAM,*LAM,HOL,*BUR,LAM,DRO,*ON,*REL,OFF,*SPI,DOO,*LEA,MUD,STO,*MED,AWA,BEE,THR,SCO,TOS,GAS,QUI,FLI,SWI
2042 DATA EGG,RUB,OIL,LOO,*SLI,*SHO,KEY,*SEE,HEL,DRA,BUN,SCO,INV,INV,LED,YEL,THR,WAK,CRO,UNL,BRI,REA,BEA,OPE,DRA,ATT,RUG,*HIT
2043 DATA RUB,DRI,HON,*GAT,FRU,BUN,OX,FIN,RIN,*LOC,CHI,HEL,*BIT,SAY,BRA,WIN,SIG,DOO,BLA,SCR,WEB,SAV,*WRI,"",SWA,"",LAV,"",ARO
2044 DATA "",HAL,"",TRE,"",*STU,"",FIR

2050 DATA 0,7,10,1,0,24,"",23,1,1,25,0,0,DISMAL SWAMP,0,0,0,0,0,1,TOP OF A TALL CYPRESS TREE,1,1,1,1,1,4
2051 DATA LARGE HOLLOW DAMP STUMP IN THE SWAMP,0,0,0,0,3,5,ROOT CHAMBER UNDER THE STUMP
2052 DATA 0,0,0,0,4,0,SEMI-DARK HOLE NEXT TO ROOT CHAMBER,0,0,0,0,5,7,LONG DOWN SLOPING HALL,8,9,0,27,6,12,LARGE CAVERN
2053 DATA 0,7,0,0,0,0,LARGE 8 SIDED ROOM,7,0,0,0,20,0,ROYAL ANTEROOM,11,10,26,1,0,26,*I'M ON THE SHORE OF A LAKE
2054 DATA 11,11,23,11,0,0,FOREST,13,15,15,0,0,13,MAZE OF PITS,0,0,0,14,12,0,MAZE OF PITS,17,12,13,16,16,17
2055 DATA MAZE OF PITS,12,0,13,12,13,0,MAZE OF PITS,0,17,0,0,14,17,MAZE OF PITS,17,12,12,15,14,18,MAZE OF PITS
2056 DATA 0,0,0,0,17,0,BOTTOM OF A CHASM. ABOVE ME IS A LEDGE WITH A BRICKED UP WINDOW AND LEDGE FACING A THRONE ROOM.
2057 DATA 0,0,0,20,0,0,*I'M ON A NARROW LEDGE BY A CHASM. ACROSS THE CHASM IS THE THRONE ROOM,0,0,0,0,0,9,ROYAL CHAMBER
2058 DATA 0,0,0,0,0,0,NARROW LEDGE BY A THRONE ROOM. ACROSS THE CHASM IS ANOTHER LEDGE.,0,0,0,0,0,21,THRONE ROOM
2059 DATA 10,1,10,11,0,0,SUNNY MEADOW
2060 DATA 0,0,0,0,0,0,*I THINK I'M IN REAL TROUBLE NOW. THERE'S A FELLOW HERE WITH A PITCHFORK AND POINTED TAIL... OH HELL!
2061 DATA 11,0,1,11,0,0,HIDDEN GROVE,0,0,0,0,0,0,QUICK-SAND BOG,0,0,7,0,0,0,MEMORY RAM OF A TRS-80. I TOOK A WRONG TURN!
2062 DATA 0,0,0,0,0,11,TOP OF AN OAK. TO THE EAST I SEE A MEADOW. BEYOND THAT A LAKE,0,0,0,0,0,0,"",0,0,0,0,0,0,""
2063 DATA 0,0,0,0,0,0,"",0,0,0,0,0,0,"",0,24,11,24,28,24,LARGE MISTY ROOM WITH STRANGE UNREADABLE LETTERS OVER ALL THE EXITS.

2070 DATA "",NOTHING HAPPENS,CHOP IT DOWN!,BOY THAT REALLY HIT THE SPOT!,DRAGON SMELLS SOMETHING. AWAKENS. ATTACKS ME!
2071 DATA LOCK SHATTERS,I CAN'T IT'S LOCKED,TIMBER. SOMETHING FELL FROM THE TREE TOP AND VANISHED!,TIMBER!,LAMP IS OFF
2072 DATA FLAMELESS LAMP IS LIT,I'M BIT BY A SPIDER,MY CHIGGER BITES ARE NOW INFECTED,MY BITES HAVE ROTTED MY WHOLE BODY
2073 DATA BEAR EATS THE HONEY AND FALLS ASLEEP,BEES STING ME,I'VE NO CONTAINER,THE BEES ALL SUFFOCATED AND DISAPEARED
2074 DATA SOMETHING I'M HOLDING VIBRATES AND...,NOTHING TO LIGHT IT WITH,GAS BLADDER BLEW UP,IN MY HANDS!
2075 DATA GAS NEEDS TO BE CONTAINED BEFORE IT WILL BURN,GAS DISIPATES. (I THINK YOU BLEW IT),THAT WON'T IGNITE
2076 DATA HOW? JUMP?,BEAR WON'T LET ME,DON'T WASTE *HONEY* GET MAD INSTEAD,BEES MADDEN BEAR. BEAR THEN ATTACKS ME!
2077 DATA IT SOAKS INTO THE GROUND,IN 1 WORD TELL ME AT WHAT.,OH NO...BEAR DODGES...CRASH.,HEAVY!
2078 DATA SOMETHING'S TOO HEAVY. I FALL.,TO STOP GAME SAY QUIT,MIRROR HITS FLOOR AND SHATTERS INTO MILLION PIECES
2079 DATA MIRROR LANDS SOFTLY ON RUG. LIGHTS UP AND SAYS,YOU LOST *ALL* TREASURES.,NOT CARRYING AXE. TAKE INVENTORY!
2080 DATA IT DOESN'T BOTHER HIM AT ALL,THE MUD DRIED UP AND FELL OFF,THE BEAR IS SO STARTLED HE FALLS OFF THE LGDGE.
2081 DATA *DRAGON STING* AND FADES. I DON'T GET IT. I HOPE YOU DO.
2082 DATA THE BEES ATTACK THE DRAGON WHICH GETS SO ANNOYED IT GETS UP AND FLYS AWAY...
2083 DATA MAGIC OIL ATRACKS MAGIC LAMP. LAMP IS NOW FULL.,I'M BIT BY CHIGGERS
2084 DATA THERE'S SOMETHING THERE ALL RIGHT! MAYBE I SHOULD GO THERE?,MAYBE IF I THREW SOMETHING?...,TOO DRY. FISH DIE.
2085 DATA A GLOWING GENIE APPEARS. DROPS SOMETHING. THEN VANISHGS.
2086 DATA A GLOWING GENIE APEARS. SAYS -BOY YOU'RE SELFISH- TAKES SOMETHING AND THEN MAKES -ME- VANISH!
2087 DATA NO. ITS TOO HOT,NOT HERE.,TRY THE SWAMP,USE ONE WORD
2088 DATA TRY--> LOOK JUMP SWIM CLIMB THROW FIND TAKE INVENTORY SCORE DROP AND ANY OTHER VERB. SOME MAY NOT NEED A NOUN.
2089 DATA ONLY 3 THINGS WILL WAKE THE DRAGON AND 1 ITEM IS DANGEROUS BY ITSELF!!
2090 DATA IF YOU EVER WANT A HINT ON SOMETHING TRY -'HELP'.,READ SIGN IN THE MEADOW!,YOU MAY NEED MAGIC WORDS HERE
2091 DATA A VOICE BOOOOMS OUT,PLEASE LEAVE IT ALONE,CAN ONLY THROW AX,MEDICINE IS GOOD FOR BITES.,I DON'T KNOW WHERE IT IS
2092 DATA TO WIN GET 100 WHEN YOU SAY 'SCORE'. A TREASURE IS ANYTHING WITH AN * IN THE NAME,BLOW IT UP.
2093 DATA FISH ESCAPE BACK TO LAKE,"","","",""

2100 DATA "",0,DARK HOLE,4,*POT OF RUBIES*/RUB/,4,SPIDER WEB WITH WRITING ON IT,2
2101 DATA -HOLLOW- STUMP AND REMAINS OF A FELLED TREE,0,CYPRESS TREE,1,WATER,10,EVIL SMELLING MUD/MUD/,1
2102 DATA *GOLDEN FISH*/FIS/,10,LIT BRASS LAMP/LAM/,0
2103 DATA OLD FASHIONED BRASS LAMP/LAM/,3,RUSTY AXE (MAGIC WORD -BUNYON- ON IT)/AXE/,10,BOTTLE WITH WATER/BOT/,3
2104 DATA EMPTY BOTTLE/BOT/,0,RING OF SKELETON KEYS/KEY/,2,SIGN -LEAVE TREASURES HERE. SAY SCORE.-,3,LOCKED DOOR,5
2105 DATA OPEN DOOR WITH A HALLWAY BEYOND,0,SWAMP GAS,1,*GOLDEN NET*/NET/,18,CHIGGER BITES,0,INFECTED CHIGGER BITES,0
2106 DATA FLOATING PATCHES OF OILY SLIME,1,*ROYAL HONEY*/HON/,8,LARGE AFRICAN BEES,8,VERY THIN BLACK BEAR,21
2107 DATA BEES IN A BOTTLE/BOT/,0,LARGE SLEEPING DRAGON,23,FLINT & STEEL/FLI/,13,*THICK PERSIAN RUG*/RUG/,17
2108 DATA SIGN -MAGIC WORD IS AWAY. LOOK LA (REST OF SIGN IS MISSING),18,DISTENDED GAS BLADDER/BLA/,0,BRICKED UP WINDOW,20
2109 DATA SIGN HERE SAYS -IN MANY CASES MUD IS GOOD. IN OTHERS...-,23,STREAM OF LAVA,18
2110 DATA BRICKED UP WINDOW WITH A HOLE IN IT. BEYOND IS A LEDGE.,0,LOOSE FIRE BRICKS,0,*GOLD CROWN*/CRO/,22
2111 DATA *MAGIC MIRROR*,21,SLEEPING BEAR,0
2112 DATA EMPTY WINE BLADDER/BLA/,9,BROKEN GLASS,0,CHIGGERS/CHI/,1,DEAD BEAR,0,*DRAGON EGGS* (VERY RARE)/EGG/,0
2113 DATA LAVA STREAM WITH BRICK DAM,0,*JEWELED FRUIT*/FRU/,25,*SMALL STATUE OF A BLUE OX*/OX/,26,*DIAMOND RING*/RIN/,0
2114 DATA *DIAMOND BRACLET*/BRA/,0,STRANGE SCRATCHINGS ON ROCK SAYS -ALADIN WAS HERE-,14
2115 DATA SIGN SAYS -LIMBO. FIND RIGHT EXIT AND LIVE AGAIN!-,33,SMOKING HOLE. PIECES OF DRAGON AND GORE.,0
2116 DATA SIGN SAYS -NO SWIMMING ALLOWED HERE-,10,ARROW POINTING DOWN,17,DEAD FISH/FIS/,0,*FIRESTONE*(COLD NOW)/FIR/,0
2117 DATA SIGN SAYS -PAUL'S PLACE-,25,TREES,11,"",0,"",0
