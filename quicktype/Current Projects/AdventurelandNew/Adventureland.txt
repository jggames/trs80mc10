10 REM COPYRIGHT SCOTT ADAMS. 1978

15 REM converted to BBC BASIC in 2020

REM L% = screen width in chars
20 MODE7:VDU23,0,10,8;0;0;0;23,0,11,12;0;0;0;:PRINT:D=-1:DIM S% 255:L%=40:*FX11,0

REM X=Y=Z:K=R=V:N=LL=F:TP$=K$:W=IP=P:

40 K=0:Z$="I'VE TOO MUCH TOO CARRY. TRY -TAKE INVENTORY-":GOSUB1240:GOTO100

50 CLS:PRINT'"*** WELCOME TO ADVENTURE LAND.(#4.6) ***":PRINT:PRINT" UNLESS TOLD DIFFERENTLY YOU MUST FIND"'"*TREASURES* AND RETURN THEM TO THEIR"'"PROPER PLACE!"
60 PRINT:PRINT"I'M YOUR PUPPET. GIVE ME ENGLISH"'"COMMANDS THAT ";
70 PRINT"CONSIST OF A NOUN AND"'"VERB. SOME EXAMPLES...":PRINT:PRINT"TO FIND OUT WHAT YOU'RE CARRYING YOU"'"MIGHT SAY: TAKE INVENTORY"'"TO GO INTO A HOLE YOU MIGHT SAY: GO HOLE";"TO SAVE CURRENT GAME: SAVE GAME"
80 PRINT:PRINT"YOU WILL AT TIMES NEED SPECIAL ITEMS TO DO THINGS, BUT I'M SURE YOU'LL BE A GOOD";"ADVENTURER AND FIGURE THESE THINGS OUT."
90 PRINT:INPUT"HAPPY ADVENTURING... HIT ENTER TO START"K$:PROCcls:RETURN

100 R=AR:LX=LT:DF=0:SF=0:INPUT"USE OLD 'SAVED' GAME? "K$:IFLEFT$(K$,1)<>"Y"THEN130
110 IFD<>-1THENCLOSE:OPEN"I",D,SV$ELSEINPUT"READY SAVED TAPE "K$:PRINT;INT(IL*5/60)+1;" MINUTES"
120 d=OPENIN("SAV"):INPUT#d,SF,LX,DF,R:FORX=0TOIL:INPUT#d,IA(X):NEXT:CLOSE#d:IFD<>-1CLOSE
130 GOSUB50:GOSUB240:GOTO160

140 PROCb:INPUT"TELL ME WHAT TO DO? "TP$:PRINT:GOSUB170:IF F PRINT"YOU USE WORD(S) I DON'T KNOW":GOTO140
150 GOSUB360:IFIA(9)<>-1 ELSE LX=LX-1:IFLX<0THENPRINT"LIGHT HAS RUN OUT":IA(9)=0ELSEIFLX<25PRINT"LIGHT RUNS OUT IN";LX;"TURNS!"
160 NV(0)=0:GOSUB360:GOTO140

REM Parse input 
170 K=0:NT$(0)="":NT$(1)=""
180 FORX=1TOLEN(TP$):K$=MID$(TP$,X,1):IFK$=" "THEN K=1 ELSE NT$(K)=LEFT$(NT$(K)+K$,ln)
190 NEXTX:FORX=0TO1:NV(X)=0:IFNT$(X)=""THEN230 ELSE FORY=0TONL:K$=NV$(Y,X):IFLEFT$(K$,1)="*"THENK$=MID$(K$,2)
200 IFX=1 IFY<7 THEN K$=LEFT$(K$,ln)
210 IF NT$(X)=K$ THEN NV(X)=Y:y=Y:Y=NL:NEXTY:Y=y ELSE NEXTY:GOTO230
220 IFLEFT$(NV$(NV(X),X),1)="*"THENNV(X)=NV(X)-1:GOTO220
230 NEXTX:F=NV(0)<1ORLEN(NT$(1))>0ANDNV(1)<1:RETURN

240 IF DF IF IA(9)<>-1 AND IA(9)<>R PRINT"I CAN'T SEE, ITS TOO DARK.":RETURN
250 K=-1:IFLEFT$(RS$(R),1)="*"THENPROCp(MID$(RS$(R),2)+".")ELSEPROCp("I'M IN A "+RS$(R)+".")
260 FORZ=0TOIL:IF K IF IA(Z)=R PRINT'"VISIBLE ITEMS HERE:":K=0
270 GOTO300

280 TP$=IA$(Z):IFRIGHT$(TP$,1)<>"/"THEN ELSE FOR W=LEN(TP$)-1TO1 STEP-1:IFMID$(TP$,W,1)="/"TP$=LEFT$(TP$,W-1):w=W:W=1:NEXTW:W=w:ELSE NEXT W
290 RETURN

REM 300 IF IA(Z)<>R THEN320 ELSE GOSUB280:IFPOS+LEN(TP$)+3>63THENPRINT
300 IF IA(Z)<>R THEN320 ELSE GOSUB280
310 PROCp(CHR$(32*-(POS>0))+TP$+"."):PROCj
320 NEXT:IFPOS PRINT' ELSEPRINT
330 K=-1:FORZ=0TO5:IF K IFRM(R,Z)<>0PRINT"OBVIOUS EXITS: ";:K=0
340 IFRM(R,Z)<>0PRINTNV$(Z+1,1);" ";
350 NEXT:IFPOS PRINT:PRINT:RETURN ELSE PRINT:RETURN


REM *** Search for matching actions ***

REM Find a matching action
360 F2=-1:F=-1:F3=0:IFNV(0)=1 AND NV(1)<7 THEN 610 ELSE FORX=0TOCL:V=INT(CA(X,0)/150):IF NV(0)=0 IFV<>0 x=X:X=CL:NEXTX:X=x:RETURN
370 IF NV(0)<>V THEN NEXTX:GOTO990 ELSE N=CA(X,0)-V*150
380 IF NV(0)<>0 ELSE F=0:IF RND(100)<=N THEN G.400 ELSE NEXTX:GOTO990
390 IF N<>NV(1) AND N<>0 THEN NEXTX:GOTO990

REM Process the conditions                        K=condition code, LL="number"
400 F2=-1:F=0:F3=-1:FORY=1TO5:W=CA(X,Y):LL=INT(W/20):K=W-LL*20:F1=-1:ON K+1 GOTO550,430,450,470,490,500,510,520,530,540,410,420,440,460,480

REM 10. Is player carrying anything at all?
410 F1=-1:FORZ=0TOIL:IFIA(Z)=-1 THEN z=Z:Z=IL:NEXTZ:Z=z:GOTO550 ELSENEXTZ:F1=0:GOTO550

REM 11. Is player carrying nothing?
420 F1=0:FORZ=0TOIL:IFIA(Z)=-1 THEN z=Z:Z=IL:NEXTZ:Z=z:GOTO550 ELSENEXTZ:F1=-1:GOTO550

REM 1. Is player carrying object LL?
430 F1=IA(LL)=-1:GOTO550

REM 12. Is obj LL not being carried by player and not in current room?
440 F1=IA(LL)<>-1 AND IA(LL)<>R:GOTO550

REM 2. Is player in room with object LL?
450 F1=IA(LL)=R:GOTO550

REM 13. Is obj LL not in the storeroom (room zero)?
460 F1=IA(LL)<>0:GOTO550

REM 3. Is player in room with obj LL or carrying it?
470 F1=IA(LL)=R OR IA(LL)=-1:GOTO550

REM 14. Is obj LL in storeroom (room zero)?
480 F1=IA(LL)=0:GOTO550

REM 4. Is player in room LL?
490 F1=R=LL:GOTO550

REM 5. Is player carrying obj LL, or obj LL isn't in current room?
500 F1=IA(LL)<>R:GOTO550

REM 6. Is player not carrying obj LL?
510 F1=IA(LL)<>-1:GOTO550

REM 7. Is player not in room LL?
520 F1=R<>LL:GOTO550

REM 8. Is numbered flag-bit set?
530 F1=SF AND INT(2^LL+.5):F1=F1<>0:GOTO550

REM 9. Is numbered flag-bit clear?
540 F1=SF AND INT(2^LL+.5):F1=F1=0:GOTO550

550 F2=F2 AND F1:IF F2 THEN NEXTY ELSE y=Y:Y=5:NEXTY:Y=y:NEXTX:GOTO990

REM Process the commands
560 IP=0:FORY=1TO4:K=INT((Y-1)/2)+6:ON Y GOTO570,580,570,580
570 AC=INT(CA(X,K)/150):GOTO590
580 AC=CA(X,K)-INT(CA(X,K)/150)*150
590 IF AC>101 THEN600 ELSE IFAC=0 THEN960 ELSE IFAC<52 THEN PROCp(MS$(AC)):GOTO960:ELSE ON AC-51 GOTO660,700,740,760,770,780,790,760,810,830,840,850,860,870,890,920,930,940,950,710,750
600 PROCp(MS$(AC-50)):GOTO960

610 L=DF:IFL THEN L=DF AND IA(9)<>R AND IA(9)<>-1:IF L PRINT"DANGEROUS TO MOVE IN THE DARK!"
620 IFNV(1)<1PRINT"GIVE ME A DIRECTION TOO.":GOTO1040
630 K=RM(R,NV(1)-1):IFK>=1 ELSE IFL THENPRINT"I FELL DOWN AND BROKE MY NECK.":K=RL:DF=0:ELSE PRINT"I CAN'T GO IN THAT DIRECTION":GOTO1040
640 IF NOT L PROCcls
650 R=K:GOSUB240:GOTO1040

REM 52. GETx
660 L=0:FORZ=1TOIL:IFIA(Z)=-1LETL=L+1
670 NEXTZ
680 IF L>=MX PRINT Z$:GOTO970
690 GOSUB1050:IA(P)=-1:GOTO960

REM 53. DROPx
700 GOSUB1050:IA(P)=R:GOTO960

REM 71. SAVE
710 PRINT"SAVING GAME":IFD=-1 INPUT"READY OUTPUT TAPE "K$:PRINT;INT(IL*5/60)+1;" MINUTES"ELSE OPEN"O",D,SV$
720 d=OPENOUT("SAV"):PRINT#d,SF,LX,DF,R:FORW=0TOIL:PRINT#d,IA(W):NEXT:CLOSE#d:IFD<>-1CLOSE
730 GOTO960

REM 54. GOTOy
740 GOSUB1050:R=P:GOTO960

REM 72. EXx,x (Exchange the room locations of the Par #1 object and the Par #2 object)
750 GOSUB1050:L=P:GOSUB1050:Z=IA(P):IA(P)=IA(L):IA(L)=Z:GOTO960

REM 55/59. x->RM0
760 GOSUB1050:IA(P)=0:GOTO960

REM 56. NIGHT
770 DF=-1:GOTO960

REM 57. DAY
780 DF=0:GOTO960

REM 58. SETz
790 GOSUB1050
800 SF=SF OR INT(.5+2^P):GOTO960

REM 60. CLRz
810 GOSUB1050

820 SF=SF AND NOT INT(.5+2^P):GOTO960

REM 61. DEAD
830 PRINT"I'M DEAD...":R=RL:DF=0:GOTO860

REM 62. x->y (Move the Par #1 object to the Par #2 room)
840 GOSUB1050:L=P:GOSUB1050:IA(L)=P:GOTO960

REM 63. FINI
850 INPUT"THE GAME IS NOW OVER"'"ANOTHER GAME? "K$:IFLEFT$(K$,1)="N"THENEND ELSE FORX=0TOIL:IA(X)=I2(X):NEXT:GOTO100

REM 64. DspRM
860 GOSUB240:GOTO960

REM 65. SCORE
870 L=0:FORZ=1TOIL:IFIA(Z)=TR IFLEFT$(IA$(Z),1)="*"LET L=L+1
880 NEXTZ:PRINT"I'VE STORED ";L;" TREASURES."'"ON A SCALE OF 0 TO 100 THAT RATES A ";INT(L/TT*100):IFL=TT THENPRINT"WELL DONE.":GOTO850 ELSE960

REM 66. INV
REM 890 PRINT"I'M CARRYING:":K$="NOTHING":PROCd3("890 FOR"):FORZ=0TOIL:IFIA(Z)<>-1THEN910 ELSEGOSUB280:IFLEN(TP$)+POS>63PRINT
890 PRINT"I'M CARRYING:":K$="NOTHING":FORZ=0TOIL:IFIA(Z)<>-1THEN910 ELSEGOSUB280
900 PROCp(CHR$(32*-(POS>0))+TP$+"."):PROCj:K$=""
910 NEXT:PRINTK$:IFPOS PRINT':GOTO960 ELSEPRINT:GOTO960

REM 67. SET0 (Sets the flag-bit numbered 0)
920 P=0:GOTO800

REM 68. CLR0 (Clears the flag-bit numbered 0)
930 P=0:GOTO820

REM 69. FILL (Re-fill the artificial light source (obj 9) and pick it up)
940 LX=LT:IA(9)=-1:GOTO960

REM 70. CLS
950 PROCcls:GOTO960

REM Next command
960 NEXTY

REM Stop processing non-automatic actions
970 IF NV(0)<>0 THEN x=X:X=CL:NEXTX:X=x:GOTO990

REM Next automatic action
980 NEXTX
990 :
1000 IFNV(0)=0THEN1040
1010 GOSUB1060
1020 IF F PRINT"I DON'T UNDERSTAND YOUR COMMAND":GOTO1040
1030 IF NOT F2 PRINT"I CAN'T DO THAT YET":GOTO1040

REM Return from action-matching routine
1040 RETURN

REM Get param from condition list
1050 IP=IP+1:W=CA(X,IP):P=INT(W/20):M=W-P*20:IF M<>0 THEN1050 ELSE RETURN

REM Automatically GET or DROP
1060 IF NV(0)<>10 AND NV(0)<>18 OR F3 THEN 1230
1070 IF NV(1)=0 PRINT"WHAT?":GOTO1180
1080 IF NV(0)<>10 THEN 1110
1090 L=0:FORZ=0TOIL:IFIA(Z)=-1THENL=L+1
1100 NEXT:IF L>=MX PRINTZ$:GOTO1180
1110 K=0:FOR X=0TOIL:IF RIGHT$(IA$(X),1)<>"/"THEN1190 ELSE LL=LEN(IA$(X))-1:TP$=MID$(IA$(X),1,LL):FORY=LL TO2 STEP-1:IFMID$(TP$,Y,1)<>"/"THEN NEXTY:GOTO1190 ELSE y=Y:Y=2:NEXTY:Y=y
1120 TP$=LEFT$(MID$(TP$,Y+1),ln)
1130 IFTP$<>NV$(NV(1),1)THEN1190
1140 IFNV(0)=10THEN1160
1150 IFIA(X)<>-1THENK=1:GOTO1190 ELSE IA(X)=R:K=3:GOTO1170
1160 IFIA(X)<>R THEN K=2:GOTO1190 ELSE IA(X)=-1:K=3
1170 PRINT"OK, ";:x=X:X=IL:NEXTX:X=x
1180 F=0:RETURN
1190 NEXTX
1200 IF K=1 THEN PRINT"I'M NOT CARRYING IT" ELSE IFK=2 PRINT"I DON'T SEE IT HERE"
1210 IF K=0 IF NOT F3 PRINT"ITS BEYOND MY POWER TO DO THAT":F=0
1220 IF K<>0 THEN F=0
1230 RETURN












NEW
AUTO

REM **** BE SURE AND USE A C-60 TAPE FOR YOUR DATA TAPE! ! ! ! 
REM ADVENTURE DATA TAPE BUILDER. BUILDER/BAS
REM VERSION 1.3 (C) SCOTT ADAMS '80.
REM ADVENTURE INTERNATIONAL BOX 3435 LONGWOOD FL 32750
REM PHONE (305)-862-6917
REM BUILDS DATA TAPE FOR ORIGINAL BASIC VERSION OF PIRATE'S ADVENTURE
REM PLEASE NOTE ON LINES THAT SUDDENLY GO TO THE
REM NEXT LINE LIKE
REM THIS USE <DOWN ARROW> KEY WHICH IS RIGHT ABOVE
REM THE LEFT HAND SHIFT KEY ON YOUR TRS-80!

ONERROR:ONERROROFF:P.:REPORT:P.'ERL:CLOSE#0:END

DATA 60,151,59,33,5,11,13,3,125,71,3,75,161,386,160,200,0,17612,0,10,401,420,400,146,0,1852,8850,8,421,0,0,0,0,2011,0,8,521,520,260,0,0,2609,7800,100,108,760,820,420,100,8312,10264,100,484,0,0,0,0,5613,0,5,141,140,20,246,0
DATA 6062,0,5,406,426,400,442,146,7845,0,8,482,152,0,0,0,2311,0,100,104,0,0,0,0,8550,0,50,161,246,160,1100,0,7259,7800,100,148,140,940,500,0,10262,9900,30,841,426,406,400,0,7845,0,50,542,141,0,0,0,10504,9150,100,248,642,720,640,700
DATA 8005,7950,100,248,542,1040,540,0,8005,0,100,28,49,20,40,0,6360,8700,25,288,260,280,0,0,7860,9150,100,248,240,0,0,0,9660,0,100,269,260,0,0,0,16558,17357,100,28,48,20,40,0,4110,9000,4366,22,0,0,0,0,6900,0,4374,0,0,0,0,0,9750,0,4404
DATA 682,0,0,0,0,6900,0,4407,82,0,0,0,0,6900,0,1521,142,421,420,140,0,8902,450,1542,462,141,460,0,0,7800,0,1521,142,401,400,140,0,8902,450,2742,461,460,502,780,500,8864,8005,2742,461,460,0,0,0,7950,0,1523,482,146,0,0,0,2311,0,1523,482
DATA 141,266,0,0,2400,0,1523,482,141,261,260,520,8902,0,1533,0,0,0,0,0,9900,0,4350,0,0,0,0,0,9600,0,5100,0,0,0,0,0,9900,0,3450,581,344,460,0,0,8118,8614,2125,623,566,0,0,0,2850,0,2125,621,561,620,0,0,3021,9209,6794,0,0,0,0,0,17100,0
DATA 2125,622,561,620,240,0,10555,8720,184,404,702,380,0,0,8164,0,1525,24,806,0,0,0,2400,0,1525,24,801,800,620,0,8902,0,2725,621,620,800,0,0,8902,3450,2125,362,561,0,0,0,3300,0,6803,0,0,0,0,0,17100,0,185,384,0,0,0,0,3750,0,1510,762,760
DATA 505,0,0,7800,0,2710,761,760,582,20,0,7986,8700,6343,921,920,0,0,0,509,0,1513,122,261,260,240,0,8902,0,900,384,420,726,0,0,8164,0,900,424,380,0,0,0,8164,0,185,424,502,0,0,0,3900,0,185,424,505,440,0,0,8164,0,7815,0,0,0,0,0,10650,0
DATA 204,682,0,0,0,0,7650,0,2723,521,502,520,480,280,4259,8008,1513,122,266,0,0,0,2400,0,5751,62,0,0,0,0,300,0,207,40,102,0,0,0,8164,0,2713,241,240,260,0,0,8902,4350,1528,442,440,200,0,0,8319,8294,1257,100,102,292,80,221,8303,1050,5870
DATA 104,322,286,0,0,900,0,5570,104,322,286,0,0,900,0,3611,221,60,220,0,0,4558,7950,5870,322,281,320,340,0,8303,9600,2737,721,685,720,0,0,7950,0,900,384,721,0,0,0,5011,0,2737,721,1120,900,680,720,8003,8309,1537,722,720,0,0,0,4852,0
DATA 3300,0,0,0,0,0,5100,0,3900,0,0,0,0,0,9813,0,1510,762,502,0,0,0,3900,0,2710,761,585,820,760,0,5303,8850,1050,68,765,60,0,0,16710,0,4800,68,60,542,0,0,5910,0,4950,0,0,0,0,0,9750,0,7050,401,0,0,0,0,16613,15750,7050,421,0,0,0,0,16613
DATA 15750,184,364,0,0,0,0,15300,0,1554,682,0,0,0,0,7650,0,5250,502,860,360,500,0,6212,8250,2723,521,542,480,880,540,8003,8293,7350,68,60,0,0,0,9001,16607,5870,342,0,0,0,0,9600,0,166,702,380,0,0,0,8164,0,1050,68,760,100,80,502,8308
DATA 4710,6761,0,0,0,0,0,16614,0,5400,0,0,0,0,0,197,0,207,82,60,0,0,0,8164,0,1257,102,221,100,80,281,8303,1200,6038,502,0,0,0,0,3947,0,6039,542,0,0,0,0,5897,0,6313,241,240,260,0,0,509,7800,6313,122,0,0,0,0,450,0,6342,461,460,0,0,0,509
DATA 0,7500,322,68,320,340,60,8303,810,4050,524,10,0,0,0,4950,0,4050,524,11,200,0,0,8164,0,1200,226,0,0,0,0,5700,0,6600,943,221,220,500,140,2762,8700,6600,221,527,220,500,0,2762,9900,4217,183,0,0,0,0,7650,0,1521,142,140,0,0,0,7800,0
DATA 4217,203,169,960,160,0,7403,8700,4217,203,228,0,0,0,150,0,4217,203,208,220,660,960,7558,8159,4217,203,188,200,660,980,7558,8159,4217,203,168,980,180,0,7403,8700,7650,401,400,420,0,0,462,8902,7650,421,0,0,0,0,463,9150,4050,527,0,0
DATA 0,0,15300,0,3450,586,0,0,0,0,150,0,6600,0,0,0,0,0,150,0,5281,0,0,0,0,0,16607,5100,6807,0,0,0,0,0,15450,0,2723,521,480,520,260,0,8009,7800,6780,0,0,0,0,0,15450,0,6771,0,0,0,0,0,15450,0,7200,0,0,0,0,0,15600,0,207,224,560,0,0,0,8164
DATA 0,7050,524,0,0,0,0,16605,16350,7050,224,0,0,0,0,16605,0,7050,384,0,0,0,0,16605,0,7050,464,0,0,0,0,16606,0,7050,264,0,0,0,0,16609,0,7050,344,0,0,0,0,16609,0,7050,304,0,0,0,0,16609,0,7050,424,0,0,0,0,16605,0,7050,164,0,0,0,0,16608,0
DATA 5570,281,322,340,320,0,8005,0,206,342,120,0,0,0,8156,9600,2117,201,200,180,0,0,8902,1500,2119,181,180,200,0,0,8902,1350,1551,62,0,0,0,0,1711,0,166,1042,480,0,0,0,8164,0,1549,0,0,0,0,0,16611,0,2100,561,365,0,0,0,3600,0,5250,0,0,0,0
DATA 0,150,0,3450,581,347,340,0,0,8118,8464,7050,24,0,0,0,0,16605,0,3611,226,0,0,0,0,5700,0,7050,404,0,0,0,0,16616,15450,6774,0,0,0,0,0,9750,0,166,84,100,0,0,0,8164,0,1542,462,482,146,0,0,2311,0,7050,0,0,0,0,0,150,0,1200,0,0,0,0,0,197
DATA 0,3600,0,0,0,0,0,16800,0

DATA AUT,ANY,GO,NORTH,*ENT,SOUTH,*RUN,EAST,*WAL,WEST,*CLI,UP,JUM,DOWN,BEA,NET,CHO,FIS,*CUT,AWA,TAK,MIR,*GET,AXE,*PIC,*AX,*CAT
DATA WAT,LIG,BOT,*TUR,GAM,*LAM,HOL,*BUR,LAM,DRO,*ON,*REL,OFF,*SPI,DOO,*LEA,MUD,STO,*MED,AWA,BEE,THR,SCO,TOS,GAS,QUI,FLI,SWI
DATA EGG,RUB,OIL,LOO,*SLI,*SHO,KEY,*SEE,HEL,DRA,BUN,SCO,INV,INV,LED,YEL,THR,WAK,CRO,UNL,BRI,REA,BEA,OPE,DRA,ATT,RUG,*HIT
DATA RUB,DRI,HON,*GAT,FRU,BUN,OX,FIN,RIN,*LOC,CHI,HEL,*BIT,SAY,BRA,WIN,SIG,DOO,BLA,SCR,WEB,SAV,*WRI,"",SWA,"",LAV,"",ARO
DATA "",HAL,"",TRE,"",*STU,"",FIR

DATA 0,7,10,1,0,24,""
DATA 23,1,1,25,0,0,DISMAL SWAMP
DATA 0,0,0,0,0,1,TOP OF A TALL CYPRESS TREE
DATA 1,1,1,1,1,4,LARGE HOLLOW DAMP STUMP IN THE SWAMP.
DATA 0,0,0,0,3,5,ROOT CHAMBER UNDER THE STUMP
DATA 0,0,0,0,4,0,SEMI-DARK HOLE NEXT TO ROOT CHAMBER
DATA 0,0,0,0,5,7,LONG DOWN SLOPING HALL
DATA 8,9,0,27,6,12,LARGE CAVERN
DATA 0,7,0,0,0,0,LARGE 8 SIDED ROOM
DATA 7,0,0,0,20,0,ROYAL ANTEROOM
DATA 11,10,26,1,0,26,*I'M ON THE SHORE OF A LAKE
DATA 11,11,23,11,0,0,FOREST
DATA 13,15,15,0,0,13,MAZE OF PITS
DATA 0,0,0,14,12,0,MAZE OF PITS
DATA 17,12,13,16,16,17,MAZE OF PITS
DATA 12,0,13,12,13,0,MAZE OF PITS
DATA 0,17,0,0,14,17,MAZE OF PITS
DATA 17,12,12,15,14,18,MAZE OF PITS
DATA 0,0,0,0,17,0,BOTTOM OF A CHASM. ABOVE ME IS A PAIR OF LEDGES. ONE HAS A BRICKED UP WINDOW ACROSS ITS FACE THE OTHER FACES A THRONE ROOM.
DATA 0,0,0,20,0,0,*I'M ON A NARROW LEDGE BY A CHASM. ACROSS THE CHASM IS THE THRONE ROOM
DATA 0,0,0,0,0,9,ROYAL CHAMBER
DATA 0,0,0,0,0,0,NARROW LEDGE BY A THRONE ROOM. ACROSS THE CHASM IS ANOTHER LEDGE.
DATA 0,0,0,0,0,21,THRONE ROOM
DATA 10,1,10,11,0,0,SUNNY MEADOW
DATA 0,0,0,0,0,0,*I THINK I'M IN REAL TROUBLE NOW. THERE'S A FELLOW HERE WITH A PITCHFORK AND POINTED TAIL. ...OH HELL!
DATA 11,0,1,11,0,0,HIDDEN GROVE
DATA 0,0,0,0,0,0,QUICK-SAND BOG
DATA 0,0,7,0,0,0,MEMORY RAM OF A TRS-80. I TOOK A WRONG TURN!
DATA 0,0,0,0,0,11,TOP OF AN OAK. TO THE EAST I SEE A MEADOW. BEYOND THAT A LAKE.
DATA 0,0,0,0,0,0,""
DATA 0,0,0,0,0,0,""
DATA 0,0,0,0,0,0,""
DATA 0,0,0,0,0,0,""
DATA 0,24,11,24,28,24,LARGE MISTY ROOM WITH STRANGE UNREADABLE LETTERS OVER ALL THE EXITS.

DATA ""
DATA NOTHING HAPPENS
DATA CHOP IT DOWN!
DATA BOY THAT REALLY HIT THE SPOT!
DATA DRAGON SMELLS SOMETHING. AWAKENS. ATTACKS ME!
DATA LOCK SHATTERS
DATA I CAN'T ITS LOCKED
DATA TIMBER. SOMETHING FELL FROM THE TREE TOP AND VANISHED!
DATA TIMBER!
DATA LAMP IS OFF
DATA FLAMELESS LAMP IS LIT
DATA I'M BIT BY A SPIDER
DATA MY CHIGGER BITES ARE NOW INFECTED
DATA MY BITES HAVE ROTTED MY WHOLE BODY
DATA BEAR EATS THE HONEY AND FALLS ASLEEP
DATA BEES STING ME
DATA I'VE NO CONTAINER
DATA THE BEES ALL SUFFOCATED AND DISAPEARED
DATA SOMETHING I'M HOLDING VIBRATES AND...
DATA NOTHING TO LIGHT IT WITH
DATA GAS BLADDER BLEW UP
DATA IN MY HANDS!
DATA GAS NEEDS TO BE CONTAINED BEFORE IT WILL BURN
DATA GAS DISIPATES. (I THINK YOU BLEW IT)
DATA THAT WON'T IGNITE
DATA HOW? JUMP?
DATA BEAR WON'T LET ME
DATA DON'T WASTE *HONEY* GET MAD INSTEAD
DATA BEES MADDEN BEAR. BEAR THEN ATTACKS ME!
DATA IT SOAKS INTO THE GROUND
DATA IN 1 WORD TELL ME AT WHAT.
DATA OH NO...BEAR DODGES...CRASH.
DATA HEAVY!
DATA SOMETHINGS TOO HEAVY. I FALL.
DATA TO STOP GAME SAY QUIT
DATA MIRROR HITS FLOOR AND SHATTERS INTO MILLION PIECES
DATA MIRROR LANDS SOFTLY ON RUG. LIGHTS UP AND SAYS
DATA YOU LOST *ALL* TREASURES.
DATA NOT CARRYING AXE. TAKE INVENTORY!
DATA IT DOESN'T BOTHER HIM AT ALL
DATA THE MUD DRIED UP AND FELL OFF
DATA THE BEAR IS SO STARTLED HE FALLS OFF THE LGDGE.
DATA *DRAGON STING* AND FADES. I DON'T GET IT. I HOPE YOU DO.
DATA THE BEES ATTACK THE DRAGON WHICH GETS SO ANNOYED IT GETS UP AND FLYS AWAY...
DATA MAGIC OIL ATRACKS MAGIC LAMP. LAMP IS NOW FULL.
DATA I'M BIT BY CHIGGERS
DATA THERE'S SOMETHING THERE ALL RIGHT! MAYBE I SHOULD GO THERE?
DATA MAYBE IF I THREW SOMETHING?...
DATA TOO DRY. FISH DIE.
DATA A GLOWING GENIE APPEARS. DROPS SOMETHING. THEN VANISHGS.
DATA A GLOWING GENIE APEARS. SAYS -BOY YOU'RE SELFISH- TAKES SOMETHING AND THEN MAKES -ME- VANISH!
DATA NO. ITS TOO HOT
DATA NOT HERE.
DATA TRY THE SWAMP
DATA USE ONE WORD
DATA TRY--> LOOK.JUMP.SWIM.CLIMB.THROW.FIND.TAKE.INVENTORY.SCORE.DROP AND ANY OTHER VERB YOU THINK OF. SOME MAY NOT NEED A NOUN.
DATA ONLY 3 THINGS WILL WAKE THE DRAGON AND 1 ITEM IS DANGEROUS BY ITSELF!!
DATA IF YOU EVER WANT A HINT ON SOMETHING TRY -'HELP'.
DATA READ SIGN IN THE MEADOW!
DATA YOU MAY NEED MAGIC WORDS HERE
DATA A VOICE BOOOOMS OUT
DATA PLEASE LEAVE IT ALONE
DATA CAN ONLY THROW AX
DATA MEDICINE IS GOOD FOR BITES.
DATA I DON'T KNOW WHERE IT IS
DATA TO WIN GET 100 WHEN YOU SAY 'SCORE'. A TREASURE IS ANYTHING WITH AN * IN THE NAME
DATA BLOW IT UP.
DATA FISH ESCAPE BACK TO LAKE
DATA ""
DATA ""
DATA ""
DATA ""

DATA "",0,DARK HOLE,4,*POT OF RUBIES*/RUB/,4,SPIDER WEB WITH WRITING ON IT,2,-HOLLOW- STUMP AND REMAINS OF A FELLED TREE,0,CYPRESS TREE,1,WATER,10,EVIL SMELLING MUD/MUD/,1,*GOLDEN FISH*/FIS/,10,LIT BRASS LAMP/LAM/,0
DATA OLD FASHIONED BRASS LAMP/LAM/,3,RUSTY AXE (MAGIC WORD -BUNYON- ON IT)/AXE/,10,BOTTLE WITH WATER/BOT/,3,EMPTY BOTTLE/BOT/,0,RING OF SKELETON KEYS/KEY/,2,SIGN -LEAVE TREASURES HERE. SAY SCORE.-,3,LOCKED DOOR,5
DATA OPEN DOOR WITH A HALLWAY BEYOND,0,SWAMP GAS,1,*GOLDEN NET*/NET/,18,CHIGGER BITES,0,INFECTED CHIGGER BITES,0,FLOATING PATCHES OF OILY SLIME,1,*ROYAL HONEY*/HON/,8,LARGE AFRICAN BEES,8,VERY THIN BLACK BEAR,21
DATA BEES IN A BOTTLE/BOT/,0,LARGE SLEEPING DRAGON,23,FLINT & STEEL/FLI/,13,*THICK PERSIAN RUG*/RUG/,17,SIGN -MAGIC WORD IS AWAY. LOOK LA (REST OF SIGN IS MISSING),18,DISTENDED GAS BLADDER/BLA/,0,BRICKED UP WINDOW,20
DATA SIGN HERE SAYS -IN MANY CASES MUD IS GOOD. IN OTHERS...-,23,STREAM OF LAVA,18,BRICKED UP WINDOW WITH A HOLE IN IT. BEYOND IS A LEDGE.,0,LOOSE FIRE BRICKS,0,*GOLD CROWN*/CRO/,22,*MAGIC MIRROR*,21,SLEEPING BEAR,0
DATA EMPTY WINE BLADDER/BLA/,9,BROKEN GLASS,0,CHIGGERS/CHI/,1,DEAD BEAR,0,*DRAGON EGGS* (VERY RARE)/EGG/,0,LAVA STREAM WITH BRICK DAM,0,*JEWELED FRUIT*/FRU/,25,*SMALL STATUE OF A BLUE OX*/OX/,26,*DIAMOND RING*/RIN/,0
DATA *DIAMOND BRACLET*/BRA/,0,STRANGE SCRATCHINGS ON ROCK SAYS -ALADIN WAS HERE-,14,SIGN SAYS -LIMBO. FIND RIGHT EXIT AND LIVE AGAIN!-,33,SMOKING HOLE. PIECES OF DRAGON AND GORE.,0
DATA SIGN SAYS -NO SWIMMING ALLOWED HERE-,10,ARROW POINTING DOWN,17,DEAD FISH/FIS/,0,*FIRESTONE*(COLD NOW)/FIR/,0,SIGN SAYS -PAUL'S PLACE-,25,TREES,11,"",0,"",0

REM
REM PROGRAM STARTS HERE...
REM
RESTORE
CLS:INPUT"PREPARE DATA TAPE (HIT ENTER)";TP$:D=-1
CLS:PRINT"ADVENTURE DATA TAPE BUILDER - WORKING"
D=OPENOUT("DAT")
READ IL,CL,NL,RL,MX,R,TT,ln,LT,ML,TR:PRINT#D,IL,CL,NL,RL,MX,R,TT,ln,LT,ML,TR
DIMCA(1,7),NV$(NL+10,1),IA$(IL),IA(IL),RS$(RL),RM(RL,5),MS$(ML),AA(5),A$(9),A(1,7)
FORZ=0TOCL STEP2:FORXX=0TO1:FORY=0TO7:READ CA(XX,Y):NEXTY,XX:X=0
Y=X+1:PRINT#D,CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7)
PRINT';CA(X,0);" ";CA(X,1);" ";CA(X,2);" ";CA(X,3);" ";CA(X,4);" ";CA(X,5);" ";CA(X,6);" ";CA(X,7);" ";CA(Y,0);" ";CA(Y,1);" ";CA(Y,2);" ";CA(Y,3);" ";CA(Y,4);" ";CA(Y,5);" ";CA(Y,6);" ";CA(Y,7):NEXTZ
OS."FX21":V.7,136:P."PRESS A KEY":IFGET
FORX=0TONL:FORY=0TO1:READ NV$(X,Y):P."NV$(";X;",";Y;")="NV$(X,Y):NEXTY,X
OS."FX21":V.7,136:P."PRESS A KEY":IFGET
FORX=0TORL:READ RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):P.;RM(X,0);" ";RM(X,1);" ";RM(X,2);" ";RM(X,3);" ";RM(X,4);" ";RM(X,5)'RS$(X)'':NEXT
OS."FX21":V.7,136:P."PRESS A KEY":IFGET
FORX=0TOML:READ MS$(X):P."MS$(";X;")="MS$(X):NEXT
OS."FX21":V.7,136:P."PRESS A KEY":IFGET
FORX=0TOIL:READ IA$(X),IA(X):P."IA$(";X;")=";IA$(X)'"IA(";X;")=";IA(X)':NEXT
P."Writing data to storage medium..."
FORX=0TONL STEP10:FORY=0TO1:PRINT#D,NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y):NEXTY,X
FORX=0TORL:PRINT#D,RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT
FORX=0TOML:PRINT#D,MS$(X):NEXT
FORX=0TOIL:PRINT#D,IA$(X),IA(X):NEXT
CLOSE#D
P."Finished writing data."'
INPUT"REWIND TAPE TO BE VERFIED";TP$
D=OPENIN("DAT")
RESTORE:FORX=1TO11:READA0:NEXT
INPUT#D,A0,A1,A2,A3,A4,A5,A6,A7,A8,A9,B0
IFB0<>TR OR A0<>IL OR A1<>CL OR A2<>NL OR A3<>RL OR A4<>MX OR A5<>R OR(A6<>TT) OR A7<>ln OR(A8<>LT) OR A9<>ML THEN6790
FORX=0TOCL STEP2:INPUT#D,A(0,0),A(0,1),A(0,2),A(0,3),A(0,4),A(0,5),A(0,6),A(0,7),A(1,0),A(1,1),A(1,2),A(1,3),A(1,4),A(1,5),A(1,6),A(1,7)
FORY=0TO1:FORZ=0TO7:READ A0:IFA(Y,Z)<>A0 THEN6790 ELSE NEXTZ,Y,X
FORX=0TONL STEP10:FORY=0TO1:INPUT#D,A$(0),A$(1),A$(2),A$(3),A$(4),A$(5),A$(6),A$(7),A$(8),A$(9):FORZ=0TO9:IFNV$(X+Z,Y)<>A$(Z) THEN6790 ELSE NEXTZ,Y,X
FORX=0TORL:INPUT#D,AA(0),AA(1),AA(2),AA(3),AA(4),AA(5),A$:FORY=0TO5:IFAA(Y)<>RM(X,Y)ORA$<>RS$(X)THEN6790 ELSE NEXTY,X
FORX=0TOML:INPUT#D,A$:IFA$<>MS$(X)THEN6790ELSENEXT
FORX=0TOIL:INPUT#D,A$,AA(0):IFAA(0)<>IA(X)ORA$<>IA$(X)THEN6790ELSENEXTX:CLOSE#D:PRINT"DATA TAPE VERIFIED":END
PRINT"BAD TAPE":CLOSE#D:END


1240 IFD<>-1THEN1330 ELSEINPUT"READY DATA TAPE. HIT ENTER "K$
1245 d=OPENIN("DAT")
1250 INPUT#d,IL,CL,NL,RL,MX,AR,TT,ln,LT,ML,TR
REM 1260 W=INT((IL+INT(CL/2)+INT(NL/10)+RL+ML)/12):REM PRINTW+1;"MINUTES TO LOAD."
1270 DIMNV(1),CA(CL,7),NV$(NL,1),IA$(IL),IA(IL),RS$(RL),RM(RL,5),MS$(ML),NT$(1),I2(IL)
1280 FORX=0TOCL STEP2:Y=X+1:INPUT#d,CA(X,0),CA(X,1),CA(X,2),CA(X,3),CA(X,4),CA(X,5),CA(X,6),CA(X,7),CA(Y,0),CA(Y,1),CA(Y,2),CA(Y,3),CA(Y,4),CA(Y,5),CA(Y,6),CA(Y,7):NEXT
1290 FORX=0TONL STEP10:FORY=0TO1:INPUT#d,NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y):
REM 1295 P.NV$(X,Y),NV$(X+1,Y),NV$(X+2,Y),NV$(X+3,Y),NV$(X+4,Y),NV$(X+5,Y),NV$(X+6,Y),NV$(X+7,Y),NV$(X+8,Y),NV$(X+9,Y)
1297 NEXTY,X
1300 FORX=0TORL:INPUT#d,RM(X,0),RM(X,1),RM(X,2),RM(X,3),RM(X,4),RM(X,5),RS$(X):NEXT
1310 FORX=0TOML:INPUT#d,MS$(X):NEXT
1320 FORX=0TOIL:INPUT#d,IA$(X),IA(X):I2(X)=IA(X):NEXT:CLOSE#d:RETURN
1330 REM

2000 DEFPROCcls:CLS:PRINT:ENDPROC

REM Word-wrap
2060 DEFPROCp($S%):LOCALA%,Z%,C%,N%,T%:N%=LEN$S%:A%=0:Z%=L%+1-POS:REPEATIFZ%>N%Z%=N%ELSEREPEATZ%=Z%-1:C%=S%?Z%:UNTILC%=32:IFZ%<A%Z%=A%+L%
2070 T%=S%?Z%:S%?Z%=13:PRINT$(S%+A%);:S%?Z%=T%:VDU32,-8*(POS=1):IFZ%-A%<=L%ANDPOS PRINTELSEIFC%=32A%=A%+1ELSEZ%=Z%-1
2080 A%=Z%+1:Z%=A%+L%+1:UNTILA%>=N%:IFPOS PRINT:ENDPROC ELSEENDPROC

REM Backtrack to previous non-space char 
2090 DEFPROCj:LOCALA%,C%:A%=&87:REPEATVDU8:C%=(USR(&FFF4)AND&FF00)DIV256:UNTILC%<>32:VDU9:ENDPROC

REM Is the previous line blank?
2100 DEFFNb:LOCALA%,C%,I%:VDU11,8:A%=&87:FORI%=1TOL%:VDU9:C%=(USR(&FFF4)AND&FF00)DIV256:IFC%=32NEXT:VDU10,13:=TRUE ELSEI%=L%:NEXT:VDU10,13:=FALSE
REM If at start of line, then print a blank line if prev line isn't blank.
2120 DEFPROCb:IFPOS ENDPROC ELSE IFNOT(FNb)PRINT:ENDPROC ELSEENDPROC