10 TI=RND(-(PEEK(9)*256+PEEK(10))):TI=0
20 GOTO320:CLS
30 A1=0
40 CLS:A=16416:Q=RND(7)+1
50 FORI=0TO62:SET(I,A1,Q):NEXT
60 FORI=0TO31:SET((A1*2)-(A1=31),I,Q):NEXT:IFA1=31THEN80
70 A1=31:GOTO50
80 L=16498:C=0:B=1:P=0:T=0:Y=48
90 Z=INT(RND(448))+A+32
100 IFZ=L-BORPEEK(Z)<>96THEN90
110 Y=Y+1:POKEZ,Y
130 A$=INKEY$:IFA$="/"ORA$="Z"THEN270
140 K=PEEK(L+B):IFK<>96THEN170
150 POKEL,96
160 L=L+B:POKEL,79:FORZZ=1TODL:NEXT:TI=TI+1:GOTO130
170 IFTI>1500-(DL*10)THEN490
180 IFK=YORK>111THEN230
190 C=SGN(B)*(33-ABS(B))
200 Z=87:IFK=111THENC=-C:Z=43
210 SOUNDZ,1:IFPEEK(L+B+C)<>96THEN150
220 POKEL,96:L=L+B:B=C:GOTO160
230 IFK=YTHEN250
240 B=-B:SOUND61,1:GOTO150
250 Z=225:FORJ=1TO9:SOUND390-Z,1:NEXT
260 T=T+1:GOTO90
270 C=SGN(B)*(33-ABS(B))
280 Z=78:IFA$="/"THENC=-C:Z=38
290 IFPEEK(L+C)<>96THEN150
300 POKEL,111+((A$="Z")*19):SOUNDZ,1
310 P=P+1:B=C:GOTO160
320 CLS:PRINT@172,"REBOUND"
330 GOSUB600
340 PRINT:PRINT"   THE ULTIMATE GAME OF SKILL":GOSUB600
350 CLS:PRINT" YOU HAVE ONE MINUTE TO HIT AS"," MANY TARGETS AS YOU CAN BY"
360 PRINT" PLACING PADDLES IN THE PATH OF";," THE BALL."
370 PRINT:PRINT"  PRESSING '/' GIVES YOU '/'"
380 PRINT"           'Z' GIVES YOU ' '":POKE16602,92
390 PRINT:PRINT" IF YOU USE TOO MANY PADDLES "," YOUR SCORE WILL BE REDUCED."
400 PRINT@457,"PRESS ANY KEY"
410 GOSUB610
420 CLS:PRINT" YOU WILL NOT BE ABLE TO HIT ",,," TARGETS WHEN VERY CLOSE AND"
430 PRINT:PRINT" UNWANTED PADDLES AND TARGETS ",,," MAY BE ERASED"
440 PRINT:PRINT" TO PLAY:-",,,,"  SELECT 'A' FOR AMATEUR",,,"         'P' FOR PROFESSIONAL"
450 PRINT:PRINT"         'N' TO END"
460 GOSUB610:IFA$<>"A"ANDA$<>"P"THEN460
470 IFA$="P"THENDL=50:TI=0:GOTO30
480 IFA$="A"THENDL=100:TI=0:GOTO30
490 FORZ=250TO4STEP-6:SOUNDZ,1:NEXT
510 PRINT@108,"GAME OVER";
520 GOSUB600
530 CLS:PRINT@39,"YOU HIT";T;"TARGETS"
540 PRINT@102,"YOU USED";P;"PADDLES"
550 S=INT(8000*T/(P+70))/10
560 PRINT@166,"YOUR SCORE IS";S;"%"
570 IFS>HSTHENHS=S:Z=200:FORJ=1TO9:Z=350-Z:SOUNDZ,1:?@224:FORZ1=1TO50:NEXT:?@234,"it's a record":FORZ1=1TO50:NEXT:NEXT
580 PRINT:PRINT"      HIGH SCORE IS";HS;"%"
590 GOSUB600:GOTO440
600 FORI=1TO2000:NEXT:RETURN
610 A$=INKEY$:IFA$=""THEN610
620 IFA$="N"THENEND
630 RETURN
700 REM VINCENT CROCKETT 1983
710 REM MC-10 EDITS JIM GERRIE
