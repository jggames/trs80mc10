10 CLS:PRINT:PRINTTAB(13)"lander"
20 PRINT:PRINT"  THIS IS A COMPUTER SIMULATION"
21 PRINT"OF A MOON LANDING. YOU HAVE TO"
23 PRINT"LAND IT MANUALLY. SET THE BURN"
25 PRINT"RATE OF THE RETRO TO ANY VALUE"
26 PRINT"BETWEEN 0-200. good luck"
27 PRINT:PRINT:PRINT
28 PRINT"  < PRESS ANY KEY TO START >
90 A$=INKEY$:IF A$="" THEN 90
100 CLS0:FOR A=0 TO 63:SET(A,30,2):NEXT:FOR A=15 TO 30
105 FORC1=0TOA-15:SET(C1,A,2):NEXT:FORC1=33+ATO63:SET(C1,45-A,2):NEXT:NEXT:CX=31:SX=CX:CY=8:SY=CY:A=120
120 L=0
130 PRINT"SEC  MI+FT     MPH  LB FUEL RATE"
140 A=120:V=1:M=32500:N=16500:G=.001:Z=1.8
150 PRINT@32,MID$(STR$(L),2);:PRINT@35,INT(A);:PRINT INT(5280*(A-INT(A))):PRINT@45,INT(3600*V):PRINT@51,INT(M-N)
155 GOSUB 1000:PRINT@58,;:INPUT K:PRINT@64,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";:T=10:PRINT@0,;
160 IF M-N<.001 THEN 240
170 IF T<.001 THEN 150
180 S=T:IF M>=N+S*K THEN 200
190 S=(M-N)/K
200 GOSUB 420:IF I<=0 THEN 340
210 IF V<=0 THEN 230
220 IF J<0 THEN 370
230 GOSUB 330:GOTO 160
240 PRINT"FUEL OUT AT";INT(L);"SECONDS":S=(-V+SQR(V*V+2*A*G))/G
250 V=V+G*S:L=L+S
260 W=3600*V:PRINT"ON MOON AT";INT(L);"SECONDS-IMPACT":PRINT"VELOCITY";W;"MPH"
270 IF W<1.2 THEN PRINT"PERFECT LANDING!":GOTO  440
280 IF W<10 THEN PRINT "GOOD LANDIG (COULD BE BETTER)":GOTO 440
282 IF W>60 THEN 300
284 PRINT"CRAFT DAMAGE... YOUR STRANDED"
288 GOTO 440
300 PRINT"SORRY NO SURVIORS"
310 PRINT"YOU BLAST A NEW CRATER";INT(W*.277);"FEET"
312 PRINT"DEEP!"
320 GOTO 440
330 L=L+S:T=T-S:M=M-S*K:A=I:V=J:RETURN
340 IF S<5E-03 THEN 260
350 D=V+SQR(V*V+2*A*(G-Z*K/M)):S=2*A/D
360 GOSUB420:GOSUB330:GOTO 340
370 W=(1-M*G/(Z*K))/2:S=M*V/(Z*K*(W+SQR(W*W+V/(2*Z)))):GOSUB 420
380 IF I<=0 THEN 340
390 GOSUB 330 :IF J>0 THEN 160
400 IF V>0 THEN 370
410 GOTO 160
420 Q=S*K/M:J=V+G*S+Z*(-Q-Q*Q/2-(Q*Q*Q)/3-(Q*Q*Q*Q)/4-(Q*Q*Q*Q*Q)/5)
430 I=A-G*S*S/2-V*S+Z*S*(Q/2+(Q*Q)/6+(Q*Q*Q)/12+(Q*Q*Q*Q)/20+(Q*Q*Q*Q*Q)/30):RETURN
440 PRINT"TRY AGAIN?"
450 A$=INKEY$
460 IF A$="N"THEN END
470 IF A$="Y"THEN 100
480 GOTO 450
1000 REM ship
1025 CY=INT((120-A)/5.5+8)
1027 RESET(SX,SY):SY=CY
1030 SET(SX,SY,5)
1040 RETURN
