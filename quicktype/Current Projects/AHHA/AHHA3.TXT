0 REM AHHA**ANOTHER HAUNTED HOUSE ADVENTURE
1 REM GEOFF WELLS
2 REM 21-12 EAST AVE N.
3 REM HAMILTON-ONTARIO
4 REM CANADA-L8L 5H2
5 REM (416) 529-1319
6 REM SPRING 1982
7 CLEAR1000
10 DIMMC$(3,6),IN$(4),FW$(40),SW$(39),FC$(40),SC$(39),OL(14,1),OD$(14),OK$(14)
20 FORNF=1TO4:IN$(NF)="*":NEXTNF
30 FORNF=1TO60:READDUMMY$:NEXTNF
40 FORNF=1TO40:READFW$(NF),FC$(NF):NEXTNF
50 FORNF=1TO39:READSW$(NF),SC$(NF):NEXTNF
60 FORX=1TO3:FORY=1TO6:READMC$(X,Y):NEXTY,X
70 FORNF=1TO14:READOL(NF,0),OL(NF,1),OD$(NF),OK$(NF):NEXTNF
90 X=1:Y=6
100 GOSUB4000:D$=MID$(MC$(X,Y),1,6):L1=VAL(MID$(MC$(X,Y),7,2))
101 L2=VAL(MID$(MC$(X,Y),9,2)):L3=VAL(MID$(MC$(X,Y),11,2)):L4=VAL(MID$(MC$(X,Y),13,2))
110 RESTORE:L$=""
120 FORNF=1TO4:READDUMMY$:IFL1=NFTHENL$=DUMMY$
121 NEXTNF
130 FORNF=1TO15:READDUMMY$:IFL2=NFTHENL$=L$+DUMMY$
131 NEXTNF
140 FORNF=1TO18:READDUMMY$:IFL3=NFTHENL$=L$+DUMMY$
141 NEXTNF
150 FORNF=1TO23:READDUMMY$:IFL4=NFTHENL$=L$+DUMMY$:NEXTNF
151 NEXTNF
160 OB$="":FORNF=1TO14:IFOL(NF,0)=X ANDOL(NF,1)=Y THENOB$=OB$+"-"+OD$(NF)
161 NEXTNF:IFOB$=""THENOB$=" NOTHING SPECIAL"
170 CLS:P$="YOU ARE "+L$:GOSUB500
180 P$="I SEE"+OB$:GOSUB500
190 PRINT"POSSIBLE DIRECTIONS  "
191 IFMID$(D$,1,1)="N"THENPRINT"NORTH ";
192 IFMID$(D$,2,1)="S"THENPRINT"SOUTH ";
193 IFMID$(D$,3,1)="E"THENPRINT"EAST ";
194 IFMID$(D$,4,1)="W"THENPRINT"WEST ";
195 IFMID$(D$,5,1)="U"THENPRINT"UP ";
196 IFMID$(D$,6,1)="D"THENPRINT"DOWN ";
197 PRINT:PRINTSTRING$(32,"*");
200 G=RND(10):IFX=1ANDY=4ORX=2ANDY=1THENG=G+4
201 IFG>9THENPRINT"THE MISERS GHOST IS HERE"
202 INPUT"WHAT NOW BOSS";C$
210 IFC$="N"THEN601
211 IFC$="S"THEN602
212 IFC$="E"THEN603
213 IFC$="W"THEN604
214 IFC$="U"THEN605
215 IFC$="D"THEN606
216 IFC$="LOOK"THEN160
217 IFC$="HELP"THEN650
218 IFC$="SAVE"THEN700
219 IFC$="LOAD"THEN800
220 IFLEFT$(C$,2)="GO"THENC$="*GO"
221 IFLEFT$(C$,3)="INV"THEN610
222 IFC$="SCORE"THEN3600
223 IFG>9THENPRINT"THE GHOST CLAIMS ANOTHER VICTIM":END
225 S=0:SP=0:FORNF=1TOLEN(C$):IFMID$(C$,NF,1)=" "THENS=NF:SP=SP+1
226 NEXTNF
230 IFS=0THENPRINT"          ---WHAT?---":GOTO200
240 IFSP>1THENPRINT"ONLY TWO WORDS PLEASE":GOTO200
250 LC$=LEFT$(C$,S-1):RC$=RIGHT$(C$,LEN(C$)-S):IFRC$="UP"THENRC$="*UP"
260 FC$(0)="*":FORNF=1TO40:IFLEFT$(LC$,3)=FW$(NF)THENFC$(0)=FC$(NF)
261 NEXTNF
270 IFFC$(0)="*"THENPRINT"I DON'T RECOGNIZE THE VERB":GOTO200
280 SC$(0)="*":FORNF=1TO39:IFLEFT$(RC$,3)=SW$(NF)THENSC$(0)=SC$(NF)
281 NEXTNF
300 IFSC$(0)="*"THENP$="I DON'T KNOW WHAT A "+RC$+" IS":GOSUB500:GOTO200
310 M=0:FORNF=1TOLEN(SC$(0)):IFMID$(SC$(0),NF,1)=FC$(0)THENM=99
311 NEXTNF
320 IFM=0THENP$="I DON'T KNOW HOW TO "+LC$+" A "+RC$:GOSUB500:GOTO200
330 ZZ=ASC(FC$(0))-64:IFZZ<=16THENGOSUB1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300,2400,2500:GOTO340
331 ONZZ-16GOSUB2600,2700,2800,2900,3000,3100,3200,3300,3400,3500
340 IFR=1THENR=0:GOTO100
341 GOTO200
500 IFLEN(P$)>32THENP1$=LEFT$(P$,32):P2$=RIGHT$(P$,LEN(P$)-32):GOTO510
502 PRINTP$:RETURN
510 IFLEFT$(P2$,1)=" "THENP2$=RIGHT$(P2$,LEN(P2$)-1):PRINTP1$;:P$=P2$:GOTO500
520 IFRIGHT$(P1$,1)=" "ANDLEN(P1$)=32THENPRINTP1$;:P$=P2$:GOTO500
521 IFRIGHT$(P1$,1)=" "THENPRINTP1$:P$=P2$:GOTO500
530 P2$=RIGHT$(P1$,1)+P2$:P1$=LEFT$(P1$,LEN(P1$)-1):GOTO520
601 ON-(MID$(D$,1,1)<>"N")GOTO607:Y=Y-1:GOTO100
602 ON-(MID$(D$,2,1)<>"S")GOTO607:Y=Y+1:GOTO100
603 ON-(MID$(D$,3,1)<>"E")GOTO607:X=X+1:GOTO100
604 ON-(MID$(D$,4,1)<>"W")GOTO607:X=X-1:GOTO100
605 ON-(MID$(D$,5,1)<>"U")GOTO607:Y=Y-3:GOTO100
606 IFMID$(D$,6,1)="D"THENY=Y+3:GOTO100
607 PRINT"YOU CAN'T GO THAT WAY":GOTO200
609 NEXTI:GOTO611
610 IN$(0)="":FORNF=1TO4:IFIN$(NF)<>"*"THENFORI=1TO14:ON-(IN$(NF)<>OK$(I))GOTO609:IN$(0)=IN$(0)+"-"+OD$(I):NEXTI
611 NEXTNF
612 IFIN$(0)=""THENIN$(0)="YOU ARE NOT CARRYING ANYTHING":GOTO615
613 IN$(0)="YOU ARE CARRYING"+IN$(0)
615 P$=IN$(0):GOSUB500
620 GOTO200
650 IFX=2ANDY=1THENP$="WHAT WAS WRITTEN IN THE BEGINNING MAY BE OF SOME HELP HERE":GOTO652
651 P$="I CAN BE OF NO ASSISTANCE HERE"
652 GOSUB500:GOTO200
700 CLS:INPUT"READY TAPE<PLAY & RECORD>ENTER";E$
710 OPEN"O",-1,"GAPADFIL":PRINT#-1,X,Y,SC,F:FORNF=1TO4:PRINT#-1,IN$(NF):NEXTNF
711 FORZ=1TO3:FORW=1TO6:PRINT#-1,MC$(Z,W):NEXTW,Z:FORNF=1TO14:PRINT#-1,OL(NF,0),OL(NF,1):NEXTNF:CLOSE:GOTO100
800 CLS:INPUT"READY TAPE<PLAY>ENTER";E$
810 OPEN"I",-1,"GAPADFIL":INPUT#-1,X,Y,SC,Y:FORNF=1TO4:INPUT#-1,IN$(NF):NEXTNF
811 FORZ=1TO3:FORW=1TO6:INPUT#-1,MC$(Z,W):NEXTW,Z:FORNF=1TO14:INPUT#-1,OL(NF,0),OL(NF,1):NEXTNF:CLOSE:GOTO100
1000 IN$(0)="":FORNF=1TO4:IFIN$(NF)=RC$THENIN$(0)="YOU ALREADY HAVE THE "+RC$
1001 NEXTNF
1002 IFIN$(0)<>""THEN1050
1005 IFRC$="BOOK"THENP$="IT CRUMBLES TO DUST AS YOU TOUCH IT":GOSUB500:OL(3,0)=-1:OL(3,1)=-1:B=-1:RETURN
1010 FORNF=1TO4:IFIN$(NF)="*"THENIN$(0)=STR$(NF):ZZ=NF:NF=4:NEXTNF:NF=ZZ
1011 NEXTNF
1020 IFIN$(0)=""THENIN$(0)="YOU CAN'T CARRY ANY MORE":GOTO1050
1030 FORNF=1TO14:IFRC$=OK$(NF)ANDOL(NF,0)=XANDOL(NF,1)=YTHENIN$(VAL(IN$(0)))=OK$(NF):OL(NF,0)=-1:OL(NF,1)=-1:IN$(0)=""
1031 NEXTNF
1035 IFRC$="CHEST"THENMC$(2,1)="******01111223"
1040 IFIN$(0)=""THENR=1:GOTO1050
1041 IN$(0)="I DON'T SEE A "+RC$
1050 PRINTIN$(0):RETURN
1060 OL(CK,0)=X:OL(CK,1)=Y:NF=4
1099 NEXTCK:GOTO1101
1100 IN$(0)="":FORNF=1TO4:IFIN$(NF)=RC$THENIN$(0)="*":IN$(NF)="*":FORCK=1TO14:ON-(OK$(CK)<>RC$)GOTO1099:GOTO1060
1101 NEXTNF
1110 IFIN$(0)=""THENPRINT"YOU ARE NOT CARRING THAT":RETURN
1111 R=1:RETURN
1200 IFX=2ANDY=2ANDRC$="WALL"THENCLS:PRINT@40,"THIS ADVENTURE":PRINT@106,"WRITTEN BY":PRINT@169,"GEOFF WELLS":PRINT@229,"21-12 EAST AVE NORTH":PRINT@295,"HAMILTON ONTARIO":PRINT@360,"CANADA L8L 5H2":PRINT@424,"(416) 529-1319":PRINT:RETURN
1201 IFX=3ANDY=2ANDRC$="BUTTON"THENPRINT"IT SAYS **WARNING** DO NOT TOUCHTHIS BUTTON!!!":RETURN
1210 PRINT"I DON'T HAVE MY GLASSES ON":RETURN
1300 PRINT"USE N S E W U D FOR DIRECTIONS":RETURN
1400 GOTO3700
1500 GOTO3700
1600 GOTO3700
1700 GOTO3700
1800 GOTO3700
1900 IFRC$="DRESSER"THEN1910
1901 IFRC$="BAG"THEN1920
1902 IFRC$="CHAIR"THEN1930
1903 IFRC$="BUTTON"THEN1940
1904 GOTO3700
1910 IFX=1ANDY=2THEN1911
1911 PRINT"I DON'T SEE A DRESSER":RETURN
1911 MID$(MC$(1,2),1,1)="N":MID$(MC$(1,2),13,2)="03":R=1:RETURN
1920 IFX=1ANDY=1THEN1921
1921 PRINT"I DON'T SEE ANY BAGS HERE":RETURN
1921 MID$(MC$(1,1),3,1)="E":MID$(MC$(1,1),11,4)="1822":R=1:RETURN
1930 IFX=3ANDY=6THEN1935
1931 PRINT"I'D BE GLAD TO IF IT WAS HERE":RETURN
1935 MID$(MC$(3,6),11,2)="17":OL(14,0)=3:OL(14,1)=6:R=1:RETURN
1940 IFX=3ANDY=2THENNEW
1941 PRINT"WHAT BUTTON":RETURN
2000 GOTO3700
2100 GOTO3700
2200 IFX=2ANDY=1ANDRC$="AHHA"THEN2201ELSEP$="OK '"+RC$+"' ---NOTHING HAPPENED":GOSUB500:RETURN:'SAY
2201 R=1:MC$(2,1)="***W**01111215":RETURN
2300 IFX=1ANDY=5THEN2301ELSEPRINT"I CAN'T SEE IT FROM HERE":RETURN
2301 IFB=0THENP$="IT SAYS--'KNOWLEDGE OF ESCAPE IS A WEIGHTY SECRET'":GOSUB500:RETURNELSEP$="ALL I SEE IS A PILE OF DUST":GOSUB500:RETURN
2400 GOTO3700
2500 IFX=2ANDY=5THENMID$(MC$(2,5),1,1)="N":R=1ELSEPRINT"YOURS OR MINE":RETURN
2501 IFF=0THENMID$(MC$(2,5),13,2)="10":RETURNELSEIFF=-1THENMID$(MC$(2,5),13,2)="11":RETURN
2600 GOTO3700
2700 GOTO3700
2800 GOTO3700
2900 GOTO3700
3000 GOTO3700
3100 GOTO3700
3200 IFX=1ANDY=2THEN3201ELSEPRINT"WHAT MIRROR?":RETURN
3201 M=0:FORNF=1TO4:IFIN$(NF)="TOWEL"THENM=99:NEXTNF ELSENEXTNF:IFM=0THENPRINT"WITH WHAT?":RETURN
3202 MID$(MC$(1,2),2,1)="S":MID$(MC$(1,2),11,2)="03":R=1:RETURN
3300 IFX=1ANDY=4THEN3301ELSEIFX=3ANDY=4THEN3304ELSEPRINT"ON WHAT":RETURN
3301 IN$(0)="":FORNF=1TO4:IFIN$(NF)="PRYBAR"THENIN$(0)="*":NEXTNF ELSENEXTNF
3302 IFIN$(0)=""THENPRINT"YOU DON'T HAVE ONE":RETURN
3303 P$="YOU HAVE DISCOVERED THE ROTTING CORPSE OF THE OLD MISER. THE SHOCK HAS SCARED YOU TO DEATH!!":GOSUB500:END
3304 IN$(0)="":FORNF=1TO4:IFIN$(NF)="FUSE"THENIN$(0)="*":IN$(NF)="*":NEXTNF ELSENEXTNF
3305 F=-1:MID$(MC$(2,5),13,2)="11":PRINT"OK":RETURN
3400 IN$(0)="":FORNF=1TO4:IFIN$(NF)="CHEESE"THENIN$(0)="*":NEXTNF ELSENEXTNF
3401 IFIN$(0)=""THENPRINT"YOU DON'T HAVE ANY CHEESE":RETURN
3402 PRINT"IT WAS POISONED":END
3500 GOTO3700:'DRINK
3600 IFX=1ANDY=6ANDSC=100THENPRINT"CONGRATULATIONS-YOU HAVE WON":ENDELSEPRINT"YOUR PRESENT SCORE IS";SC:GOTO200
3700 PRINT"I AM NOT PROGRAMED FOR THAT":RETURN
4000 IFX=1ANDY=3THEN4001ELSEIFX=2ANDY=4ANDF=0THEN4002ELSERETURN
4001 M=0:FORNF=1TO4:IFIN$(NF)="CHEST"THENM=99:NEXTNF ELSENEXTNF:IFM=0THENRETURNELSEY=6:SC=100:PRINT"THE FLOOR GIVES WAY-YOU ARE":FORNF=1TO14:PRINT"FALLING!!!!":NEXTNF:RETURN
4002 PRINT"YOU HAVE FALLEN AND BROKEN YOUR NECK":END
10000 DATA IN A ,ON THE ,AT THE ,IN THE 
10010 DATA LARGE CLOSET. THERE IS A COBWEB IN THE CORNER ,SMALL BEDROOM. THERE IS A ,SMALL ,BRIGHT & SUNNY CONSERVATORY ,FRONT PORCH OF A ,BOTTOM ,PANELED HALLWAY ,LARGE DAMP BASEMENT. ,UPPER LANDING ,LONG ,DUSTY ,SMALL STUFFY CLOSET ,MASTER BEDROOM. 
10011 DATA LARGE AIRY KITCHEN ,LARGE LIVING ROOM. THERE IS A PORTRAIT HANGING ON THE WALL AND 
10020 DATA WITH A ,VERY DUSTY MIRROR ON ONE WALL ,LARGE DARK OPENING WHERE THE MIRROR USED TO BE ,SECRET ROOM.,STUFFY ,SINISTER ,OF A ,FOOTPRINTS IN THE DUST LEAD TO ,THERE IS A WINE RACK ON THE FAR WALL AND ,OF THE RICKERTY ,NARROW CORRIDOR. ,DINGY 
10021 DATA A HUGE FOUR-POSTER BED ,BATHROOM. ,NARROW DINING ROOM. ,A CHAIR ,AN OVERTURNED CHAIR ,AND A PASSAGE TO THE EAST 
10030 DATA HUGE SPIDER SITTING IN THE CENTRE.,AND AN UGLY DRESSER OPPOSITE.,AND A DRESSER BESIDE AN OPEN CLOSET.,THE FLOOR CREAKS LOUDLY AS YOU WALK ACROSS IT.,FLAGSTONE FLOOR.,LIBRARY.,OLD MANSION.,WINDING STAIRCASE.,THE NORTH.
10031 DATA A DARK HOLE IN THE NORTH WALL.,A LIGHTED PASSAGE RUNNING NORTH.,A PILE OF BONES NEAR THE ENTRANCE.,OLD STAIRS.,YOU FEEL CLOSE TO SOMETHING IMPORTANT.,ATTIC FULL OF OLD BOXES.,SINGLE SHELF AT EYE LEVEL.,DOMINATES THE ROOM.
10032 DATA THERE ARE RED STAINS IN THE TUB.,TABLE IN THE CORNER AND A FUSE BOX ON THE WALL.,THERE ARE SIX CHAIRS AT THE TABLE. SKELETONS ARE SEATED AT FIVE OF THEM.,IN FRONT OF THE FIREPLACE.,HIDDEN IN THE REAR.
10033 DATA ATTIC FULL OF OLD BOXES. THE EXIT HAS SUDDENLY CLOSED. YOU ARE TRAPPED. WHAT DO YOU SAY ABOUT THAT.
20000 DATA GET,A,TAK,A,DRO,B,PUT,B,LOO,C,EXA,C,RUN,D,WAL,D,*GO,D,OPE,E,CLO,F,HIT,G,ATT,G,KIL,G,LOC,H,UNL,I,MOV,J,LIF,J,TIL,J,TWI,J,PUS,J,PUL,J,SHO,J,CLI,K,PLA,L,SAY,M,REA,N,RUB,O,TUR,P,TOU,Q,ASK,R,JUM,S,SWI,T,KIC,U,SMA,V,WIP,W,TRY,X,USE,X,EAT,Y,DRI,Z
30000 DATA CLO,C,COB,CQ,SPI,CGQ,MIR,CJQW,DRE,CJQ,FLO,CQ,PED,CJQV,BOO,ACEJNQ,MON,CQ,AHH,M,WAL,CNQ,STA,CK,BON,CJQSU,WIN,CQVZ,RAC,CJKQV,CHA,CQV,FLA,CJU,BAG,CEJQ,HEA,CJPQ,PRY,ABCJQUWX,CHE,ABCEFJQV,CHA,CJQV,POR,CJQV,KEY,ABCJQUX,SKE,CGJQUV,TAB,CJOQUVW
30001 DATA BUT,CJOQ,SHE,CQV,BED,CJQUV,CHE,ABCEFHIJQUVY,FUS,ABCJNQX,TOW,ABCQ,KNI,ABCJQ,NOR,D,SOU,D,EAS,D,WES,D,*UP,D,DOW,D
40000 DATA *S****01010101,**E***01020202,N*****01030404,******01040105,N*E***01030506,**E***02050607
40001 DATA ***W**01111215,*SEW**01101114,N*E**D02091013,*S****01080912,*SEW**01070809,N***U*03060708
40002 DATA *S****01120116,NS*W**04131317,N**W**01031418,*S****01140119,NS*W**01101520,N*****01151621
50000 DATA 1,1,A BLACK GARMENT BAG HANGING IN THE BACK,BAG,1,4,A LOOSE FLAGSTONE IN THE CORNER,FLAGSTONE,1,5,A PEDESTAL WITH A TATTERED BOOK LYING OPEN ON IT,BOOK
50001 DATA 2,1,A HEAVY SEA CHEST STUFFED FULL OF MONEY,CHEST,2,2,SOMETHING SCRALLED ON THE NORTH WALL,WALL,2,4,A RUSTY PRYBAR,PRYBAR,2,5,A MANGY MOOSE HEAD ON THE NORTH WALL,HEAD,2,6,A FABULOUS CRYSTAL CHANDELIER,CHANDELIER
50002 DATA 3,1,A SHINY NEW FUSE,FUSE,3,2,A BIG RED BUTTON BESIDE THE BED,BUTTON,3,3,A TATTERED SCRAP OF TOWEL,TOWEL,3,4,A BLOODSTAINED KNIFE,KNIFE,3,5,A LARGE WEDGE OF CHEDDAR CHEESE,CHEESE,-1,-1,A SMALL BRASS KEY,KEY
