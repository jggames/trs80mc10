0 REM01234567890123456789012345678901234567890123456
1 CLS:GOSUB10000
2 DATA60,54,55,150,246,129,126,38,25,220,247,195,1,5,131,1,1,221,252,206,67,113,236,1
3 DATA221,246,236,3,221,248,236,5,221,250,51,50,56,57,240,129,58,37,1,57,126
10 M=16384:GOTO220
50 DATA ".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... ...."
60 DATA ".... WN.. .NS. .NS. .N.. .NS. .NS. .NS. .N.. .NS. .NS. .NS. .NS. .NS. .N.E ...."
70 DATA ".... W..E .... .... W..E .... .... .... W..E .... .... .... .... .... W..E ...."
80 DATA ".... W..E .... WN.. ..S. .NS. .NS. .NS. ...E .... WN.. .NS. .NS. .NS. ...E ...."
90 DATA ".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E ...."
100 DATA ".... W... .NS. .... .NS. .NS. .NS. .NS. ...E .... W... .NS. .NS. .NS. ...E ...."
110 DATA ".... W..E .... W..E .... .... .... .... W..E .... W..E .... .... .... W..E ...."
120 DATA ".... W... .NS. ..S. .N.. .NS. .NS. .NS. ...E .... W..E .... WN.. .NS. ...E ...."
130 DATA ".... W..E .... .... W..E .... .... .... W..E .... W.S. .NS. ...E .... W..E ...."
140 DATA ".... W..E .... WN.. ..S. .N.E .... WN.. ..S. .N.E .... .... W..E .... W..E ...."
150 DATA ".... W..E .... W..E .... W..E .... W..E .... W.S. .N.E .... W..E .... W..E ...."
160 DATA ".... W..E .... W..E .... W..E .... W..E .... .... W..E .... W..E .... W..E ...."
170 DATA ".... W.S. .NS. ..S. .NS. ..S. .NS. ..S. .NS. .NS. ..S. .NS. ..S. .NS. ..SE ...."
180 DATA ".... .... .... .... .... .... .... .... .... .... .... .... .... .... .... ...."
190 DATA ""
200 REM FORMAT FOR EACH SPACE: WNSE FOR WALLS OR . IF NO WALL THAT DIRECTION
210 REM INITIALISATION
220 CLS0
230 DIMM(16,16),D$(3),W$(6),A(2,2),C,D,V,M$,WA$(1)
231 W$(0)="ßßßßßßßßßßßßßßßß":W$(1)="ïïïïïïïïïïïïïïïï":W$(5)=W$(0):W$(6)=W$(1)
235 D$(0)="NORTH":D$(1)="EAST ":D$(2)="SOUTH":D$(3)="WEST ":WA$(1)="-WALL"
240 WA$(0)="     "
250 X=0:Y=0:D=0:XS=0:YS=0:WA=0:A=0:B=0
260 PRINT"SETTING UP MAP";:REM READ MAP
270 REM INPUT L$
280 READL$
290 IFLEN(L$)=0THEN410
300 K=0
310 FORI=1TOLEN(L$):C$=MID$(L$,I,1)
330 IFC$="N"THENK=KOR8
340 IFC$="E"THENK=KOR4
350 IFC$="S"THENK=KOR2
360 IFC$="W"THENK=KOR1
370 IFC$=" "THENM(X,Y)=K:PRINT@16+(Y+1)*32+X,CHR$(143-(K=0));:K=0:X=X+1
380 IFX>=XSTHENXS=X
390 NEXT:M(X,Y)=K:X=0:Y=Y+1:GOTO270
410 PRINT@16+6*32+3,"B";:PRINT@17,"SCORE"SC;
420 REM CLS
430 YS=Y:XS=XS+1
440 X=1:Y=1
450 REM LOOP
470 GOSUB660: REM RENDER
480 GOSUB1940: REM INPUT
490 GOTO450
660 REM RENDER DUNGEON
670 REM --------------
680 REM CLEAR AREA
700 PRINT@0,"€€€€€€€€€€€€€€€€";:PRINT@32,"€€€€€€€€€€€€€€€€";:PRINT@64,"€€€€€€€€€€€€€€€€";:PRINT@96,"€€€€€€€€€€€€€€€€";
701 PRINT@128,"€€€€€€€€€€€€€€€€";:PRINT@160,"€€€€€€€€€€€€€€€€";:PRINT@192,"€€€€€€€€€€€€€€€€";:PRINT@224,"€€€€€€€€€€€€€€€€";
702 PRINT@256,"€€€€€€€€€€€€€€€€";:PRINT@288,"€€€€€€€€€€€€€€€€";:PRINT@320,"€€€€€€€€€€€€€€€€";:PRINT@352,"€€€€€€€€€€€€€€€€";
703 PRINT@384,"€€€€€€€€€€€€€€€€";:PRINT@416,"€€€€€€€€€€€€€€€€";:PRINT@448,"€€€€€€€€€€€€€€€€";:PRINT@480,"€€€€€€€€€€€€€€€€";
720 REM FIND CELLS / REORIENT SW
730 REM STORE IN MA - WE'RE AT (0,1)
740 A=X:B=Y
750 CO=D+X+Y AND 1
760 FOR TY=0 TO 2
770 GOSUB2440:REM LEFT/RIGHT A/B
780 A(1,TY)=0
790 IF A>=0 AND B>=0 AND A<XS AND B<YS THEN A(1,TY)=M(A,B)
800 A(0,TY)=RL
810 A(2,TY)=RR
820 GOSUB2550:REM ADVANCE
830 NEXT
840 REM DRAW BACK WALLS
850 L=A(1,2):GOSUB2330:IFF=0THEN930
870 V=5:IFCO=1THENV=6
871 C=V*16+143
880 M$=LEFT$(W$(CO),6):A=5:FORB=5TO9:PRINT@A+B*32,M$;:NEXT
930 V=6:IFCO=1THENV=5
931 C=V*16+143
940 FORDE=0TO2STEP2:L=A(DE,2):GOSUB2330:IFF=0THEN1020
970 FORA=DE*5.5TO4+DE*5.5:FORB=5TO9:POKEM+A+B*32,C:NEXT:NEXT
1020 NEXT
1030 REM 1,2 LEFT WALL
1040 L=A(1,2):GOSUB2330
1050 IFRL=0THEN1100
1060 A=4:C=V*16+143
1070 FORB=5TO9:POKEM+A+B*32,C:NEXT
1080 B=4:C=V*16+139:POKEM+A+B*32,C
1090 B=10:C=V*16+142:POKEM+A+B*32,C
1100 REM 1,2 RIGHT WALL
1110 IF RR=0 GOTO1160
1120 A=11:C=V*16+143
1130 FOR B=5 TO 9:POKEM+A+B*32,C:NEXT
1140 B=4:C=V*16+135:POKEM+A+B*32,C
1150 B=10:C=V*16+141:POKEM+A+B*32,C
1160 REM 1,1 BACK WALL
1170 L=A(1,1):GOSUB2330
1180 C=V*16+143
1190 IFF=0THEN1250
1200 M$=LEFT$(W$(V),8):A=4:FORB=4TO10:PRINT@A+B*32,M$;:NEXT
1250 REM (0-2),1 BACK WALLS
1260 V=5:IFCO=1THENV=6
1261 C=V*16+143
1270 FOR DE=0 TO 2 STEP 2
1280 L=A(DE,1):GOSUB2330
1290 IF F=0 GOTO1350
1300 FOR A=DE*6 TO 3+DE*6
1310 FOR B=4 TO 10
1320 POKEM+A+B*32,C
1330 NEXT
1340 NEXT
1350 NEXT
1360 REM 1,1 LEFT SIDE WALL
1370 V=5:IFCO=1THENV=6
1371 C=V*16+143
1380 L=A(1,1):GOSUB2330
1390 IFRL=0THEN1460
1400 IFV=6THEN1405
1401 PRINT@33,"Û€€";:PRINT@65,"ßÛ€";:PRINT@97,"ßßÛ";:PRINT@129,"ßßß";:PRINT@161,"ßßß";:PRINT@193,"ßßß";:PRINT@225,"ßßß";
1402 PRINT@257,"ßßß";:PRINT@289,"ßßß";:PRINT@321,"ßßß";:PRINT@353,"ßßÞ";:PRINT@385,"ßÞ€";:PRINT@417,"Þ€€";:GOTO1470
1405 PRINT@33,"ë€€";:PRINT@65,"ïë€";:PRINT@97,"ïïë";:PRINT@129,"ïïï";:PRINT@161,"ïïï";:PRINT@193,"ïïï";:PRINT@225,"ïïï";
1406 PRINT@257,"ïïï";:PRINT@289,"ïïï";:PRINT@321,"ïïï";:PRINT@353,"ïïî";:PRINT@385,"ïî€";:PRINT@417,"î€€";
1460 REM 1,1 RIGHT SIDE WALL
1470 IFRR=0THEN1550
1480 FORQX=1TO3:A=15-QX:B=QX:C=V*16+135:POKEM+A+B*32,C:B=14-QX:C=V*16+141:POKEM+A+B*32,C:C=V*16+143
1530 FORB=1+QXTO13-QX:POKEM+A+B*32,C:NEXT
1540 NEXT
1550 REM 0,1 BACK WALL
1560 L=A(1,0):GOSUB2330
1570 IFF=0THEN1630
1580 M$=LEFT$(W$(CO),14):A=1:FORB=1TO13:PRINT@A+B*32,M$;:NEXT
1630 REM (0,2)-0 BACK WALLS
1640 V=6:IFCO=1THENV=5
1641 C=V*16+143
1650 FORDE=0TO2STEP2:L=A(DE,0):GOSUB2330:IFF=0THEN1700
1680 A=DE*7.5:FORB=1TO13:POKEM+A+B*32,C:NEXT
1700 NEXT
1710 REM (1,0) LEFT SIDE WALL
1720 L=A(1,0):GOSUB2330
1730 IF RL=0 GOTO1780
1740 A=0:B=0:C=V*16+139:POKEM+A+B*32,C
1750 B=14:C=V*16+142:POKEM+A+B*32,C
1760 C=V*16+143
1770 FORB=1TO13:POKEM+A+B*32,C:NEXT
1780 REM (1,0) RIGHT SIDE WALL
1790 IFRR=0THEN1840
1800 A=15:B=0:C=V*16+135:POKEM+A+B*32,C
1810 B=14:C=V*16+141:POKEM+A+B*32,C
1820 C=V*16+143
1830 FORB=1TO13:POKEM+A+B*32,C:NEXT
1840 REM DONE
1850 RETURN
1860 PRINT@12*32+5,"‘““’";
1870 PRINT@13*32+5,"•CCš";
1880 PRINT@14*32+5,"•žžž’";
1890 PRINT@15*32+6,"œ””˜";:FORC1=1TO3500:NEXT
1900 PRINT@12*32+5,"ÁÃÃÂ";
1910 PRINT@13*32+5,"ÅMCÊ";
1920 PRINT@14*32+5,"ÅÎÎÎÂ";
1930 PRINT@15*32+6,"ÌÄÄÈ";:GOTO1980
1940 REM ASK FOR PROMPT & HANDLE INPUT
1950 PRINT@16+32*(Y+1)+X,"*";:PRINT@497,"FACING "D$(D);
1960 REM PRINT@17+4*32,"FORWARD":PRINT@17+5*32,"BACKWARD":PRINT@17+6*32,"LEFT"
1970 REM PRINT@17+7*32,"RIGHT":PRINT@17+8*32,"QUIT":PRINT@17+9*32,;
1980 C$=INKEY$:IFC$=""THEN1980
1990 PRINT@16+32*(Y+1)+X,".";
2000 IFC$="W"THENDM=1:GOTO2060
2010 IFC$="Z"THENDM=-1:GOTO2060
2020 IFC$="A"THEND=(D-1)AND3
2030 IFC$="S"THEND=(D+1)AND3
2040 IFC$="Q"THEN1860
2050 RETURN
2060 GOTO2170:REM CHECK WALLS
2070 IFD=0THENY=Y-DM
2080 IFD=1THENX=X+DM
2090 IFD=2THENY=Y+DM
2100 IFD=3THENX=X-DM
2110 WA=0
2120 IFY>=YSTHENY=0
2130 IFY<0THENY=YS-1
2140 IFX>=XSTHENX=0
2150 IFX<0THENX=XS-1
2160 RETURN
2170 REM CHECK WALLS
2180 K=M(X,Y)
2190 IFDM=-1 GOTO2270
2200 IFD=0AND(KAND8)THEN2250
2210 IFD=1AND(KAND4)THEN2250
2220 IFD=2AND(KAND2)THEN2250
2230 IFD=3AND(KAND1)THEN2250
2240 GOTO2070
2250 WA=1
2260 RETURN : REM WALL IN THE WAY
2270 REM BACKWARD
2280 IFD=2AND(KAND8)THEN2250
2290 IFD=3AND(KAND4)THEN2250
2300 IFD=0AND(KAND2)THEN2250
2310 IFD=1AND(KAND1)THEN2250
2320 GOTO2070
2330 REM RETURN FRONT/BACK/LEFT/RIGHT
2340 REM GIVEN L AND D
2350 SN=0:IFLAND8THENSN=1
2360 SE=0:IFLAND4THENSE=1
2370 SS=0:IFLAND2THENSS=1
2380 ZW=0:IFLAND1THENZW=1
2390 IFD=0THENF=SN:RR=SE:RB=SS:RL=ZW
2400 IFD=1THENF=SE:RR=SS:RB=ZW:RL=SN
2410 IFD=2THENF=SS:RR=ZW:RB=SN:RL=SE
2420 IFD=3THENF=ZW:RR=SN:RB=SE:RL=SS
2430 RETURN
2440 REM RETURN LEFT/RIGHT FROM A/B/D
2450 IFD=0THENLY=B:RY=B:LX=A-1:RX=A+1
2460 IFD=1THENLX=A:RX=A:LY=B-1:RY=B+1
2470 IFD=2THENLY=B:RY=B:LX=A+1:RX=A-1
2480 IFD=3THENLX=A:RX=A:LY=B+1:RY=B-1
2490 RL=0:RR=0
2500 IF LX<0 OR LX>=XS OR LY<0 OR LY>=YSTHEN2520
2510 RL=M(LX,LY)
2520 IF RX<0 OR RX>=XS OR RY<0 OR RY>=YSTHEN2540
2530 RR=M(RX,RY)
2540 RETURN
2550 REM STEP FORWARD
2560 IFD=0THENB=B-1:REM N
2570 IFD=1THENA=A+1:REM E
2580 IFD=2THENB=B+1:REM S
2590 IFD=3THENA=A-1:REM W
2600 RETURN
10000 FORA=0TO44:READB:POKE17227+A,B:NEXT:EXEC17227:RETURN
