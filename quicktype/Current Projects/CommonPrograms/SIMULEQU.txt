10 CLS
20 PRINT "SIMULTANEOUS EQUATIONS" 
30 DEFDBL A-Z : DEFSNG I,J,K
40 PRINT 
50 DIM A(20,20)
60 PRINT "NUMBER OF EQUATIONS"; 
70 INPUT R 
80 PRINT "COEFFICIENT MATRIX:" 
90 FOR J=1 TO R 
100 PRINT "EQUATION";J 
110 FOR I=1 TO R+1 
120 IF I=R+1 THEN 150  
130 PRINT "  COEFFICIENT";I;
140 GOTO 160  
150 PRINT " CONSTANT"; 
160 INPUT A(J,I)
170 NEXT I 
180 NEXT J 
190 FOR J=1 TO R 
200 FOR I=J TO R 
210 IF A(I,J)<>0 THEN 250  
220 NEXT I 
230 PRINT "NO UNIQUE SOLUTION" 
240 GOTO 460  
250 FOR K=1 TO R+1 
260 X=A(J,K)
270 A(J,K)=A(I,K)
280 A(I,K)=X
290 NEXT K 
300 Y=1/A(J,J)
310 FOR K=1 TO R+1 
320 A(J,K)=Y*A(J,K)
330 NEXT K 
340 FOR I=1 TO R 
350 IF I=J THEN 400  
360 Y=-A(I,J)
370 FOR K=1 TO R+1 
380 A(I,K)=A(I,K)+Y*A(J,K) 
390 NEXT K 
400 NEXT I 
410 NEXT J 
420 PRINT 
430 FOR I=1 TO R 
440 PRINT "X";I;"=";INT(A(I,R+1)*1000+.5)/1000
450 NEXT I 
460 END 
