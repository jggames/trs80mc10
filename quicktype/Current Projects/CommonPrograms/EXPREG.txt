10 CLS
20 DEFDBL A-Z : DEFSNG I
30 PRINT
40 PRINT "NUMBER OF KNOWN POINTS";
50 INPUT N
60 J=0
70 K=0
80 L=0
90 M=0
100 R2=0
110 FOR I=1 TO N
120 PRINT "X,Y OF POINT";I;
130 INPUT X,Y
140 Y=LOG(Y)
150 J=J+X
160 K=K+Y
170 L=L+X^2
180 M=M+Y^2
190 R2=R2+X*Y
200 NEXT I
210 B=(N*R2-K*J)/(N*L-J^2)
220 A=(K-B*J)/N
230 PRINT
240 PRINT "A =";EXP(A)
250 PRINT "B =";B
260 J=B*(R2-J*K/N)
270 M=M-K^2/N
280 K=M-J
290 PRINT
300 R2=J/M
310 PRINT "COEFFICIENT OF DETERMINATION (R[2) =";R2
320 PRINT "COEFFICIENT OF CORRELATION =";SQR(R2)
330 PRINT "STANDARD ERROR OF ESTIMATE =";SQR(K/(N-2))
340 PRINT
350 PRINT "INTERPOLATION: (ENTER X=0 TO END PROGRAM)"
360 PRINT "X =";
370 INPUT X
380 IF X=0 THEN 420  
390 PRINT "Y =";EXP(A)*EXP(B*X)
400 PRINT
410 GOTO 360  
420 END
