10 CLS
20 PRINT "NTH-ORDER REGRESSION" 
30 DEFDBL A-Z : DEFSNG I,J,K
40 PRINT 
50 DIM A(30),R(10,10),T(10)
60 PRINT "DEGREE OF EQUATION"; 
70 INPUT D 
80 PRINT "NUMBER OF KNOWN POINTS"; 
90 INPUT N 
100 A(1)=N 
110 FOR I=1 TO N 
120 PRINT "X,Y OF POINT";I; 
130 INPUT X,Y 
140 FOR J=2 TO 2*D+1 
150 A(J)=A(J)+X^(J-1) 
160 NEXT J 
170 FOR K=1 TO D+1 
180 R(K,D+2)=T(K)+Y*X^(K-1) 
190 T(K)=T(K)+Y*X^(K-1) 
200 NEXT K 
210 T(D+2)=T(D+2)+Y^2 
220 NEXT I 
230 FOR J=1 TO D+1 
240 FOR K=1 TO D+1 
250 R(J,K)=A(J+K-1) 
260 NEXT K 
270 NEXT J 
280 FOR J=1 TO D+1 
290 FOR K=J TO D+1 
300 IF R(K,J)<>0 THEN 340  
310 NEXT K 
320 PRINT "NO UNIQUE SOLUTION" 
330 GOTO 830  
340 FOR I=1 TO D+2 
350 S=R(J,I) 
360 R(J,I)=R(K,I) 
370 R(K,I)=S 
380 NEXT I 
390 Z=1/R(J,J) 
400 FOR I=1 TO D+2 
410 R(J,I)=Z*R(J,I) 
420 NEXT I 
430 FOR K=1 TO D+1
440 IF K=J THEN 490  
450 Z=-R(K,J) 
460 FOR I=1 TO D+2 
470 R(K,I)=R(K,I)+Z*R(J,I) 
480 NEXT I 
490 NEXT K 
500 NEXT J 
510 PRINT 
520 PRINT "              CONSTANT =";R(1,D+2) 
530 FOR J=1 TO D 
540 PRINT J;"DEGREE COEFFICIENT =";R(J+1,D+2) 
550 NEXT J 
560 PRINT 
570 P=0 
580 FOR J=2 TO D+1 
590 P=P+R(J,D+2)*(T(J)-A(J)*T(1)/N) 
600 NEXT J 
610 DEFDBL I,J
620 Q=T(D+2)-T(1)^2/N
630 Z=Q-P 
640 I=N-D-1 
650 PRINT 
660 J=P/Q 
670 PRINT "COEFFICIENT OF DETERMINATION (R[2) =";J 
680 PRINT "COEFFICIENT OF CORRELATION =";SQR(J) 
690 PRINT "STANDARD ERROR OF ESTIMATE =";SQR(Z/I)
700 PRINT 
710 DEFSNG J
720 PRINT "INTERPOLATION: (ENTER 0 TO END PROGRAM)" 
730 P=R(1,D+2)
740 PRINT "X ="; 
750 INPUT X 
760 IF X=0 THEN 830  
770 FOR J=1 TO D 
780 P=P+R(J+1,D+2)*X^J 
790 NEXT J 
800 PRINT "Y =";P 
810 PRINT 
820 GOTO 730  
830 END 
