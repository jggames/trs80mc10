10 CLS
20 PRINT "INTEGRATION: SIMPSON'S RULE" 
30 PRINT 
40 PRINT "SELECTION: 1=KNOWN FORMULA, 0=UNKNOWN FORMULA"; 
50 INPUT S 
60 REM - DEFINE YOUR FUNCTION IN LINE 330
70 PRINT "LOWER, UPPER LIMIT OF INTEGRATION"; 
80 INPUT A,B
90 PRINT "INCREMENT OF X"; 
100 INPUT X1 
110 IF (B-A)/X1<>INT((B-A)/X1) THEN 90   
120 IF S=1 THEN 160  
130 PRINT "FIRST, LAST VALUE OF F(X)"; 
140 INPUT Y1,Y2
150 GOTO 180  
160 X=A : GOSUB 330  : Y1=X
170 X=B : GOSUB 330  : Y2=X
180 C=0 
190 D=0 
200 FOR I=1 TO (B-A)/X1-.5
210 IF S=1 THEN 250  
220 PRINT "VALUE OF F(X) AT INTERVAL";I;"(X=";A+I*X1;")"; 
230 INPUT Y 
240 GOTO 260  
250 X=A+I*X1 : GOSUB 330  : Y=X
260 IF I/2=INT(I/2) THEN 290  
270 C=C+Y 
280 GOTO 300  
290 D=D+Y 
300 NEXT I 
310 PRINT "INTEGRAL IS";X1/3*(Y1+4*C+2*D+Y2)
320 GOTO 350  
330 REM - DEFINE FUNCTION HERE (X="FUNCTION")
340 RETURN
350 END
