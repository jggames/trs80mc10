10 CLS
20 PRINT "CHI-SQUARE TEST"
30 DEFDBL A-Z : DEFSNG I,J
40 PRINT 
50 DIM V1(25),V2(5),A(5)
60 PRINT "NUMBER OF ROWS"; 
70 INPUT R 
80 PRINT "NUMBER OF COLUMNS"; 
90 INPUT C 
100 PRINT "CONTINGENCY TABLE:" 
110 FOR I=1 TO R 
120 PRINT "ROW";I 
130 FOR J=1 TO C 
140 PRINT "  eLEMENT"; J; 
150 INPUT V1((I-1)*C+J) 
160 NEXT J 
170 NEXT I 
180 PRINT 
190 L=0 
200 M=1 
210 FOR I=1 TO R 
220 FOR J=1 TO C 
230 A(I)=A(I)+V1(M) 
240 M=M+1
250 NEXT J 
260 L=L+A(I) 
270 NEXT I 
280 N=R*C 
290 FOR I=1 TO C 
300 FOR J=I TO N STEP C 
310 V2(I)=V2(I)+V1(J) 
320 NEXT J 
330 NEXT I 
340 Z=0 
350 PRINT "OBSERVED VALUE    EXPECTED VALUE";
360 PRINT "        CHI[2 CONTRIBUTION"
370 FOR I=1 TO C 
380 PRINT "  cOLUMN";I 
390 FOR J=1 TO R 
400 P=A(J)*V2(I)/L
410 X=I+(J-1)*C 
420 IF R<>2 THEN 460  
430 IF C<>2 THEN 460  
440 Y=(ABS(V1(X)-P)-.5)^2/P
450 GOTO 470  
460 Y=(V1(X)-P)^2/P
470 Z=Z+Y 
480 PRINT "   ";V1(X),P;Y
490 NEXT J 
500 NEXT I 
510 PRINT 
520 PRINT "CHI-sQUARE =";Z 
530 PRINT "DEGREES OF FREEDOM =";(C-1)*(R-1)
540 END 
