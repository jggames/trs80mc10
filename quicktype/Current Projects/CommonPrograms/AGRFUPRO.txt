10 CLS
20 PRINT "AVERAGE GROWTH RATE, FUTURE PROJECTIONS" 
30 DEFDBL A-Z : DEFSNG I
40 PRINT 
50 DIM S(20) 
60 PRINT "NUMBER OF YEARS FIGURES ESTABLISHED"; 
70 INPUT N 
80 FOR I=1 TO N 
90 IF I>1 THEN 120  
100 PRINT "FIGURE:   YEAR";I; 
110 GOTO 130  
120 PRINT"          YEAR";I; 
130 INPUT S(I) 
140 NEXT I 
150 T=LOG(S(1))
160 V=0 
170 FOR I=2 TO N
180 L=LOG(S(I)) 
190 T=T+L 
200 V=V+(I-1)*L 
210 NEXT I 
220 A=6*(2*V/(N-1)-T)/(N)/(N+1) 
230 G=EXP(A)-1 
240 PRINT "AVERAGE GROWTH RATE =";INT(G*10000+.5)/100;"%"
250 PRINT 
260 S=EXP(T/N-A*(N-1)/2)
270 PRINT "(ENTER 0 TO END PROGRAM)" 
280 PRINT "PROJECTED SALES FOR YEAR"; 
290 INPUT Y1 
300 IF Y1=0 THEN 340  
310 S1=S*(1+G)^(Y1-1)
320 PRINT "                            =";INT(S1*100+.5)/100 
330 GOTO 280  
340 END 
