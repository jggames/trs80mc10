10 CLS
20 PRINT "ROoTS OF POLYNOMIALS: HALF-INTERVAL SEARCH"
30 PRINT 
40 REM - ENTER FUNCTION AT LINE 440
50 DIM D(3) 
60 PRINT "(TO END SEARCH ENTER 0,0)" 
70 PRINT "INTERVAL (LOWER, UPPER LIMIT)"; 
80 INPUT A,B 
90 IF A<>B THEN 130  
100 IF A=0 THEN 460  
110 PRINT "--INTERVAL LIMITS CANNOT BE EQUAL--" 
120 GOTO 70   
130 IF A<B THEN 160  
140 PRINT "---LOWER LIMIT MUST BE ENTERED FIRST--"
150 GOTO 70   
160 X9=A : GOSUB 440  : A1=SGN(X9)
170 X9=B : GOSUB 440  : B1=SGN(X9)
180 IF A1*B1=0 THEN 370  
190 IF A1*B1<0 THEN 290  
200 FOR I=1 TO 1000 
210 X=A+RND(1)*(B-A)
220 X9=X : GOSUB 440  : X1=SGN(X9)
230 IF X1=0 THEN 410  
240 IF A1*X1<0 THEN 280  
250 NEXT I 
260 PRINT "NO CHANGE OF SIGN FOUND" 
270 GOTO 70   
280 B=X 
290 D(2+A1)=A 
300 D(2-A1)=B 
310 Y=(D(1)+D(3))/2
320 X9=Y : GOSUB 440  : Y1=SGN(X9)
330 IF Y1=0 THEN 410  
340 D(2+Y1)=Y 
350 IF ABS(D(1)-D(3))/ABS(D(1)+ABS(D(3)))<5E-6 THEN 410  
360 GOTO 310  
370 IF A1=0 THEN 400  
380 Y=B1 
390 GOTO 410  
400 Y=A1 
410 PRINT "ROOT =";Y 
420 PRINT 
430 GOTO 70   
440 REM - DEFINE FUNCTION HERE (X9="FUNCTION")
450 RETURN
460 END
