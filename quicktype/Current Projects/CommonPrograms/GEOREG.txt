10 CLS
20 PRINT "GEOMETRIC REGRESSION" 
30 DEFDBL A-Z : DEFSNG I
40 PRINT 
50 PRINT "NUMBER OF KNOWN POINTS"; 
60 INPUT N 
70 J=0 
80 K=0 
90 L=0
100 M=0 
110 R2=0 
120 FOR I=1 TO N 
130 PRINT "X,Y OF POINT";I; 
140 INPUT X,Y 
150 Y=LOG(Y) 
160 X=LOG(X) 
170 J=J+X 
180 K=K+Y 
190 L=L+X^2
200 M=M+Y^2
210 R2=R2+X*Y 
220 NEXT I 
230 B=(N*R2-K*J)/(N*L-J^2) 
240 A=(K-B*J)/N 
250 PRINT 
260 PRINT "F(X) =";EXP(A);"* X[";B 
270 J=B*(R2-J*K/N) 
280 M=M-K^2/N
290 K=M-J 
300 PRINT 
310 R2=J/M 
320 PRINT "COEFFICIENT OF DETERMINATION (R[2)=";R2 
330 PRINT "COEFFICIENT OF CORRELATION =";SQR(R2) 
335 IF K<0 THEN K=0  ' THIS PREVENTS TRS-80 MATH ERRORS
340 PRINT "STANDARD ERROR OF ESTIMATE =";SQR(K/(N-2))
350 PRINT 
360 PRINT "INTERPOLATION: (ENTER X=0 TO END PROGRAM)" 
370 PRINT "X ="; 
380 INPUT X 
390 IF X=0 THEN 430  
400 PRINT "Y =";EXP(A)*X^B
410 PRINT 
420 GOTO 370  
430 END 
