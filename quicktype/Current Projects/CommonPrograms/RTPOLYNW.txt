10 CLS
20 PRINT "REAL ROOTS OF POLYNOMIALS: NEWTON"
30 DIM A(30),B(30)
40 PRINT
50 FOR I=1 TO 11 
60 A(I)=0 
70 B(I)=0 
80 NEXT I 
90 PRINT "DEGREE OF EQUATION"; 
100 INPUT N 
110 FOR I=1 TO N+1 
120 PRINT "COEFFICIENT A(";I-1;")"; 
130 INPUT A(I) 
140 NEXT I 
150 FOR I=1 TO 10 
160 B(I)=A(I+1)*I 
170 NEXT I 
180 PRINT 
190 PRINT "GUESS"; 
200 INPUT X 
210 Q=0 
220 S=1 
230 F1=0 
240 F0=0 
250 Q=Q+1
260 FOR I=1 TO N+1 
270 F0=F0+A(I)*S
280 F1=F1+B(I)*S 
290 S=S*X 
300 NEXT I 
310 IF F1=0 THEN 370  
320 S=X-F0/F1
330 IF X=S THEN 390  
340 X=S 
350 IF Q>100 THEN 500  
360 GOTO 220  
370 PRINT "DERIVATIVE = 0 AT X =";X 
380 GOTO 190  
390 PRINT 
400 PRINT " ROOT"," ERROR","DERIVATIVE" 
410 PRINT X,F0,F1
420 PRINT 
430 PRINT "ANOTHER VALUE (1=YES, 0=NO)"; 
440 INPUT A 
450 IF A=1 THEN 180  
460 PRINT "ANOTHER FUNCTION (1=YES, 0=NO)";
470 INPUT A 
480 IF A=1 THEN 40   
490 GOTO 560  
500 PRINT "100 ITERATIONNS COMPLETED:" 
510 PRINT " X=";X;"f(X) =";F0 
520 PRINT "  cONTINUE? (1=YES, 0=NO)";
530 INPUT A 
540 IF A=1 THEN 210  
550 GOTO 430  
560 END
