10 CLS
20 PRINT "ANALYSIS OF TWO VECTORS" 
30 DEFDBL A-Z : DEFSNG I
40 PRINT 
50 PRINT "VECTOR 1: X,Y,Z";
60 INPUT X(1),Y(1),Z(1)
70 PRINT "VECTOR 2: X,Y,Z"; 
80 INPUT X(2),Y(2),Z(2)
90 PRINT 
100 FOR I=1 TO 2 
110 M(I)=SQR(X(I)^2+Y(I)^2+Z(I)^2)
120 IF M(I)=0 THEN 260  
130 PRINT "VECTOR";I;":"
140 PRINT "MAGNITUE:";M(I) 
150 S=57.29578
160 J=X(I)/M(I) 
170 X9=J : GOSUB 410  
180 PRINT "ANGLE WITH X-AXIS:";X9*S
190 J=Y(I)/M(I)
200 X9=J : GOSUB 410  
210 PRINT "ANGLE WITH Y-AXIS:";X9*S
220 J=Z(I)/M(I) 
230 X9=J : GOSUB 410  
240 PRINT "ANGLE WITH Z-AXIS:";X9*S
250 PRINT 
260 NEXT I 
270 J=0 
280 IF M(1)=0 THEN 350  
290 IF M(2)=0 THEN 350  
300 J=(X(1)*X(2)+Y(1)*Y(2)+Z(1)*Z(2))/M(1)/M(2)
310 IF J<>0 THEN 340  
320 J=90 
330 GOTO 350  
340 J=ATN(SQR(1-J^2)/J)*S 
350 PRINT "ANGLE BETWEEN VECTORS:";J 
360 PRINT 
370 PRINT "MORE DATA? (1=YES, 0=NO)";
380 INPUT Z 
390 IF Z=1 THEN 40   
400 GOTO 440  
410 REM - ROUTINE TO CALCULATE ARCCOS(X)
420 X9=-ATN(X9/SQR(-X9*X9+1))+1.5708
430 RETURN
440 END 
