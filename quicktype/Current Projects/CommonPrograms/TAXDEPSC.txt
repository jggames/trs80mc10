10 CLS
20 PRINT "TAX DEPRECIATION SCHEDULE" 
30 DEFDBL A-Z : DEFSNG I
40 PRINT 
50 PRINT "PURCHASE PRICE"; 
60 INPUT V 
70 V=INT(V*100+.5)/100
80 PRINT "SALVAGE VALUE"; 
90 INPUT S 
100 S=INT(S*100+.5)/100
110 D=V-S 
120 PRINT "LIFE IN YEARS"; 
130 INPUT Y 
140 PRINT "ENTER 1 FOR SUM OF DIGITS,2 FOR DECLINING BALANCE"; 
150 INPUT X 
160 IF X=2 THEN 470  
170 IF X<>1 THEN 140  
180 R1=0 
190 N=66 
200 PRINT 
210 PRINT 
220 FOR I=1 TO Y 
230 IF N<55 THEN 350  
240 FOR I1=N  TO 66 
250 PRINT 
260 NEXT I1 
270 N=7 
280 PRINT "   sUM OF DIGITS TAX DEPRECIATION" 
290 PRINT "             PRICE $";V 
300 PRINT "      sALVAGE VALUE $";S 
310 PRINT "     NET DEPRECIATED $";V-S
320 PRINT "              LIFE";Y;"YEARS" 
330 PRINT    
340 PRINT "YEAR", "DEPRECIATION","BALANCE" 
350 R=2*D*(Y-I+1)/((Y+1)*Y) 
360 R=INT(R*100+.5)/100
370 R1=R1+R 
380 B=D-R1 
390 IF B>=0 THEN 430  
400 R1=R1+B 
410 R=R+B 
420 B=0 
430 PRINT I,R,B 
440 N=N+1 
450 NEXT I 
460 GOTO 730  
470 PRINT "METHOD IN %"; 
480 INPUT M 
490 M=M/100 
500 N=66 
510 R=D 
520 PRINT 
530 FOR I=1 TO Y 
540 IF N<55 THEN 670  
550 FOR I1=N TO 66 
560 PRINT 
570 NEXT I1 
580 N=8 
590 PRINT "    DECLINING BALANCE TAX DEPRECIATION" 
600 PRINT "          pRICE $";V 
610 PRINT "       sALVAGE VALUE $";S 
620 PRINT "     NET DEPRECIATED $";D 
630 PRINT "          lIFE";Y;"YEARS"
640 PRINT "          METHOD ";M*100;"%" 
650 PRINT 
660 PRINT "YEARS", "DEPRECIATION","BALANCE" 
670 R1=INT((R*M/Y)*100+.5)/100 
680 R=R-R1 
690 R=INT(R*100)/100
700 PRINT I,R1,R 
710 N=N+1 
720 NEXT I 
730 PRINT 
740 PRINT "MORE DATA? (1=YES, 0=NO)"; 
750 INPUT X 
760 IF X=1 THEN 40   
770 END 
