0 CLS:CLEAR500:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO1000
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

1000 PRINT "THE KINGDOM OF EUPHORIA"
1010 PRINT
1020 GOSUB 3510
1100 REM PRINT STATE OF THE KINGDOM
1110 PRINT
1120 PRINT "YEAR";INT(Y/7)
1130 PRINT "POPULATION:";INT(P)
1140 IF B>0 THEN PRINT MID$(STR$(INT(B)),2);" BIRTHS"
1150 IF D>0 THEN PRINT MID$(STR$(INT(D)),2);" DEATHS BY NATURAL CAUSES"
1160 IF S>0 THEN PRINT MID$(STR$(INT(S)),2);" DEATHS BY STARVATION"
1170 IF K>0 THEN PRINT MID$(STR$(INT(K)),2);" WAR CASUALTIES"
1180 IF V>0 THEN PRINT MID$(STR$(INT(V)),2);" VICTIMS OF DISEASE"
1190 IF M>0 THEN PRINT MID$(STR$(INT(M)),2);" VICTIMS OF LOOTING"
1210 D=D+S+K+V+M
1220 P=P+B-D
1230 IF B+D>0 THEN PRINT "TOTAL:";INT(P)
1250 PRINT "LAND (ACRES):";INT(A)
1260 IF T=0 THEN IF U=0 THEN 1350
1270 IF T>0 THEN PRINT MID$(STR$(INT(T)),2);" ACRES BOUGHT"
1280 IF T<0 THEN PRINT MID$(STR$(INT(-T)),2);" ACRES SOLD"
1290 IF U<>0 THEN PRINT "FRUITS OF WAR:";INT(U);"ACRES"
1310 A=A+T+U
1320 T=0
1330 U=0
1340 PRINT "TOTAL:";INT(A)
1350 PRINT "GRAIN (BUSHELS):";INT(G)
1360 IF C<0 THEN 1530
1370 IF C=0 THEN 1430
1380 R=X
1390 PRINT "CROP YIELD";INT(C);"AT"
1410 GOSUB 3220
1420 GOSUB3680
1430 IF F>0 THEN PRINT MID$(STR$(INT(F)),2);" BUSHELS USED FOR FOOD"
1440 IF Q>0 THEN PRINT MID$(STR$(INT(Q)),2);" BUSHELS PLANTED"
1450 IF L<>0 THEN PRINT "LAND DEALS:";INT(L);"BUSHELS"
1460 IF H>0 THEN PRINT "MERCENARY HIRE:";INT(H)
1470 IF E>0 THEN PRINT MID$(STR$(INT(E)),2);" BUSHELS LOST TO RATS"
1480 IF Z<>0 THEN PRINT "FRUITS OF WAR:";INT(Z);"BUSHELS"
1490 IF O>0 THEN PRINT "LOOTING LOSSES:";INT(O);"BUSHELS"
1510 G=G+C-F-Q+L-H-E+Z-O
1520 PRINT "TOTAL:";INT(G)
1530 GOSUB 3310
1600 REM NOTICE IF GAME ENDED
1610 IF J<100 THEN 1690
1620 M$="THE PEASANTS ARE TIRED OF WAR AND STARVATION":GOSUB1
1630 PRINT "YOU ARE DEPOSED."
1640 PRINT "DO YOU WISH TO PLAY AGAIN";
1660 INPUT M$
1670 IF M$="N" THEN END
1680 GOTO 1000
1690 IF P>1 THEN 1750
1710 PRINT "YOU AND THE REMAINING POPULATION"
1720 PRINT "RETIRE IN THE SWISS ALPS."
1730 GOTO 1640
1740 REM MAKE LAND DEALS
1750 R=23+RND(8)
1760 GOSUB 3250
1770 PRINT "BUY AT ";
1780 GOSUB 3220
1790 INPUT T
1810 IF T<0 THEN 1760
1820 IF T=0 THEN 1860
1830 IF R*T<=G THEN 2060
1840 GOSUB 3270
1850 GOTO 1760
1860 R=R-1
1880 GOSUB 3250
1890 PRINT "SELL AT ";
1910 GOSUB 3220
1920 INPUT T
1930 IF T<0 THEN 1880
1940 IF T=0 THEN 2060
1950 IF T<=A THEN 1990
1960 M$="BUT THERE IS INSUFFICIENT LAND":GOSUB1
1970 GOTO 1880
1990 IF T<A/10 THEN 2050
2010 R=R-1
2020 PRINT "FOR SELLING SO MUCH YOU CAN":PRINT"ONLY GET ";
2030 GOSUB 3220
2040 PRINT
2050 T=-T
2060 L=-R*T
2100 REM DISTRIBUTE GRAIN
2110 GOSUB 3250
2120 PRINT "PLANT";
2130 INPUT Q
2140 IF Q<0 THEN 2110
2150 IF Q<=A+T THEN 2180
2160 GOSUB 3270
2170 GOTO 2110
2180 IF Q<=P*10 THEN 2220
2190 M$="BUT THERE ARE INSUFFICIENT PEOPLE":GOSUB1
2210 GOTO 2110
2220 PRINT "HOW MANY BUSHELS DO YOU WISH TO"
2230 PRINT "USE AS FOOD";
2240 INPUT F
2250 IF F<0 THEN 2220
2260 IF G+L-Q-F>=0 THEN 2310
2270 GOSUB 3270
2280 GOTO 2220
2310 IF F<=40*P THEN S=P-F/40
2320 J=J+S
2330 X=5+RND(4)
2340 IF Y/7*8-Y/49*7=Y THEN X=X/2-1
2350 C=X*Q
2360 IF G+L-Q-F+C>=0 THEN 2390
2370 X=(32767-G-L+Q+F)/Q
2380 GOTO 2350
2390 IF RND(99)<25 THEN E=(G+L-F+C)/10
2400 REM WAR AND PLAGUE
2410 IF RND(99)>15 THEN 2880
2420 W=25
2430 PRINT "A NEARBY KINGDOM THREATENS WAR"
2440 PRINT "DO YOU WISH TO MAKE A"
2450 PRINT "PRE-EMPTIVE STRIKE";
2470 INPUT M$
2480 IF M$="Y" THEN 2520
2490 W=100
2510 J=J+5
2520 PRINT "HOW MANY MERCENARIES WILL YOU"
2530 PRINT "HIRE AT 80 BUSHELS EACH";
2540 INPUT N
2550 IF N<0 THEN 2520
2560 D=G+L-Q-F+C-E
2570 IF RND(99)<W THEN 2610
2580 PRINT "PEACE NEGOTIATIONS SUCCEED"
2590 GOTO 2810
2610 IF W=25 THEN W=150
2620 R=P-S
2630 IF N>R/10 THEN 2660
2640 R=3*W/5*N*N/R*100/R
2650 GOTO 2680
2660 R=3*W/5+N*100/R
2680 K=(P-S)/2
2690 U=-(A+T)/2
2710 Z=-D/2
2720 IF R>RND(99) THEN 2760
2730 PRINT "YOU HAVE LOST THE WAR"
2740 J=J+5000/W
2750 GOTO 2810
2760 PRINT "YOU HAVE WON THE WAR"
2770 K=K/2
2780 U=-U
2790 Z=D/4
2810 IF N*80<=D+Z THEN 2870
2820 GOSUB 3280
2830 PRINT "TO PAY THE MERCENARIES"
2840 M=3*(P-S-K)/4
2850 O=3*(R+Z)/4
2860 GOTO 2880
2870 H=N*80
2880 N=P-S-K-M
2890 IF RND(99)>4 THEN 2940
2910 PRINT "THE BLACK PLAGUE STRIKES"
2920 V=N/2
2930 GOTO 2970
2940 IF RND(99)>20 THEN 2980
2950 PRINT "A POX EPIDEMIC BREAKS OUT"
2960 V=N/20
2970 N=N-V
2980 B=(N*RND(5)+9)/100+1
2990 D=(N*RND(3)+4)/100
3010 IF Y/7*8-Y/49*7<>Y THEN 3030
3020 M$="SEVEN YEAR LOCUSTS REDUCE CROP YIELD":GOSUB1
3030 IF E>0 THEN PRINT "RATS INFEST YOUR SILOS"
3040 Y=Y+8-(Y-Y/7*7)/6
3050 GOSUB3680:GOTO 1110
3200 REM UTILITY PRINT ROUTINES
3220 PRINT MID$(STR$(R),2);" BUSHELS/ACRE"
3230 RETURN
3250 PRINT "HOW MANY ACRES DO YOU WISH TO"
3260 RETURN
3270 PRINT "BUT ";
3280 PRINT "THERE IS INSUFFICIENT GRAIN"
3290 RETURN
3300 REM INITIALIZE POPULATION AND GRAIN VECTORS
3310 B=0
3320 D=0
3330 K=0
3340 M=0
3350 S=0
3360 V=0
3410 C=0
3420 E=0
3430 F=0
3440 H=0
3450 L=0
3460 O=0
3470 Q=0
3480 Z=0
3490 RETURN
3500 REM INITIALIZATION
3510 Y=RND(6)+1
3520 J=0
3530 P=100
3540 A=1500
3550 G=5000
3560 T=0
3570 U=0
3580 GOSUB 3310
3590 C=-1
3600 REM PRINT INSTRUCTIONS (MAY BE OMITTED)
3610 PRINT "DO YOU NEED INSTRUCTIONS?":PRINT"(y OR n)";
3630 INPUT M$:N=VAL(M$)
3640 IF M$="N" THEN RETURN
3650 IF M$="Y" THEN 3710
3660 PRINT "PLEASE TYPE Y FOR YES, N FOR NO. OK";
3670 GOTO 3620
3680 INPUT"HIT enter TO CONTINUE";M$:FORCC=1TO32:PRINTCHR$(8);:NEXT:RETURN
3710 CLS:M$="YOU HAVE INHERITED THE THRONE TO THE MEDIEVAL KINGDOM OF EUPHORIA, SOMEWHERE IN EUROPE.":GOSUB1
3730 M$="AT THIS TIME IT IS ABOUT"+STR$(A)+" ACRES IN SIZE, WITH"+STR$(P)+" LOYAL PEASANTS TO SERVE YOU.":GOSUB1
3750 M$="IN YOUR ROYAL SILOS YOU HAVE"+STR$(G)+" BUSHELS OF NUTRITIOUS GRAIN WITH WHICH TO FEED THE PEOPLE "
3770 M$=M$+"AND TRANSACT INTERNATIONAL TRADE.":GOSUB1
3780 M$="CLOSE BY YOUR SIDE ARE JEALOUS NEIGHBORING KINGDOMS, BUT YOU CAN PROTECT YOURSELF BY "
3810 M$=M$+"HIRING EVIL MERCENARIES.":GOSUB1:GOSUB3680
3820 M$="EACH YEAR YOUR MINISTERS WILL PRESENT YOU WITH A SUMMARY OF YOUR CURRENT STATUS, "
3840 M$=M$+"AND THEN ASK YOU FOR DECISIONS ON WHAT TO DO FOR THE NEXT YEAR.":GOSUB1
3860 M$="PLEASE TYPE Y OR NF OR YES OR NO, OR A WHOLE NUMBER FOR NUMERIC ANSWERS.":GOSUB1
3870 M$="IF YOU CHANGE YOUR MIND BEFORE HITTING RETURN, HOLD THE 'ctrl' KEY "
3890 M$=M$+"DOWN WHILE TYPING THE LETTER 'a', THEN RETYPE YOUR ANSWER.":GOSUB1
3920 PRINT:PRINT"ARE YOU READY, YOUR HIGHNESS?"
3940 INPUT "(y OR n)";M$
3950 IF M$<>"Y" THEN 3920
3980 RETURN
3990 END
