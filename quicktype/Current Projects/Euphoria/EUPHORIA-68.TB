100 REM PROGRAM BY TOM PITTMAN.
110 REM LIGHTLY EDITED FOR TINY BASIC AS IMPLEMENTED ON
120 REM THE HM-68 HOMEMADE COMPUTER BY DAVE HASSLER
1000 PR "THE KINGDOM OF EUPHORIA"
1010 PR
1020 GOSUB 3510
1100 REM PR STATE OF THE KINGDOM
1110 PR
1120 PR "YEAR ";Y/7
1130 PR "POPULATION: ";P
1140 IF B>0 THEN PR B;" BIRTHS"
1150 IF D>0 THEN PR D;" DEATHS BY NATURAL CAUSES"
1160 IF S>0 THEN PR S;" DEATHS BY STARVATION"
1170 IF K>0 THEN PR K;" WAR CASUALTIES"
1180 IF V>0 THEN PR V;" VICTIMS OF DISEASE"
1190 IF M>0 THEN PR M;" VICTIMS OF LOOTING"
1210 D=D+S+K+V+M
1220 P=P+B-D
1230 IF B+D>0 THEN PR "TOTAL: ";P
1250 PR "LAND (ACRES): ";A
1260 IF T=0 THEN IF U=0 THEN GOTO 1350
1270 IF T>0 THEN PR T;" ACRES BOUGHT"
1280 IF T<0 THEN PR -T;" ACRES SOLD"
1290 IF U<>0 THEN PR "FRUITS OF WAR: ";U;" ACRES"
1310 A=A+T+U
1320 T=0
1330 U=0
1340 PR "TOTAL: ";A
1350 PR "GRAIN (BUSHELS): ";G
1360 IF C<0 THEN GOTO 1530
1370 IF C=0 THEN GOTO 1430
1380 R=X
1390 PR "CROP YIELD ";C;" AT ";
1410 GOSUB 3220
1420 PR
1430 IF F>0 THEN PR F;" BUSHELS USED FOR FOOD"
1440 IF Q>0 THEN PR Q;" BUSHELS PLANTED"
1450 IF L<>0 THEN PR "LAND DEALS: ";L;" BUSHELS"
1460 IF H>0 THEN PR "MERCENARY HIRE: ";H
1470 IF E>0 THEN PR E;" BUSHELS LOST TO RATS"
1480 IF Z<>0 THEN PR "FRUITS OF WAR: ";Z;" BUSHELS"
1490 IF O>0 THEN PR "LOOTING LOSSES: ";O;" BUSHELS"
1510 G=G+C-F-Q+L-H-E+Z-O
1520 PR "TOTAL: ";G
1530 GOSUB 3310
1600 REM NOTICE IF GAME ENDED
1610 IF J<100 THEN GOTO 1690
1620 PR "THE PEASANTS TIRE OF WAR AND STARVATION."
1630 PR "YOU ARE DEPOSED."
1640 PR "DO YOU WISH TO PLAY AGAIN (1=YES)";
1650 REM N=Y-1
1660 INPUT N
1670 IF Y<>1 THEN END
1680 GOTO 1000
1690 IF P>1 THEN GOTO 1750
1710 PR "YOU AND THE REMAINING POPULATION"
1720 PR "RETIRE IN THE SWISS ALPS."
1730 GOTO 1640
1740 REM MAKE LAND DEALS
1750 R=23+RND(8)
1760 GOSUB 3250
1770 PR "BUY AT ";
1780 GOSUB 3220
1790 INPUT T
1810 IF T<0 THEN GOTO 1760
1820 IF T=0 THEN GOTO 1860
1830 IF R*T<=G THEN GOTO 2060
1840 GOSUB 3270
1850 GOTO 1760
1860 R=R-1
1880 GOSUB 3250
1890 PR "SELL AT ";
1910 GOSUB 3220
1920 INPUT T
1930 IF T<0 THEN GOTO 1880
1940 IF T=0 THEN GOTO 2060
1950 IF T<=A THEN GOTO 1990
1960 PR "BUT THERE IS INSUFFICIENT LAND."
1970 GOTO 1880
1990 IF T<A/10 THEN GOTO 2050
2010 R=R-1
2020 PR "FOR SELLING SO MUCH YOU CAN ONLY GET ";
2030 GOSUB 3220
2040 PR
2050 T=-T
2060 L=-R*T
2100 REM DISTRIBUTE GRAIN
2110 GOSUB 3250
2120 PR "PLANT";
2130 INPUT Q
2140 IF Q<0 THEN GOTO 2110
2150 IF Q<=A+T THEN GOTO 2180
2160 GOSUB 3270
2170 GOTO 2110
2180 IF Q<=P*10 THEN GOTO 2220
2190 PR "BUT THERE ARE INSUFFICIENT PEOPLE."
2210 GOTO 2110
2220 PR "HOW MANY BUSHELS DO YOU WISH TO USE AS FOOD";
2240 INPUT F
2250 IF F<0 THEN GOTO 2220
2260 IF G+L-Q-F>=0 THEN GOTO 2310
2270 GOSUB 3270
2280 GOTO 2220
2310 IF F<=40*P THEN S=P-F/40
2320 J=J+S
2330 X=5+RND(4)
2340 IF Y/7*8-Y/49*7=Y THEN X=X/2-1
2350 C=X*Q
2360 IF G+L-Q-F+C>=0 THEN GOTO 2390
2370 X=(32767-G-L+Q+F)/Q
2380 GOTO 2350
2390 IF RND(99)<25 THEN E=(G+L-F+C)/10
2400 REM WAR AND PLAGUE
2410 IF RND(99)>15 THEN GOTO 2880
2420 W=25
2430 PR "A NEARBY KINGDOM THREATENS WAR."
2440 PR "DO YOU WISH TO MAKE A PRE-EMPTIVE STRIKE (1=YES)";
2460 REM N=Y-1
2470 INPUT N
2480 IF N=1 THEN GOTO 2520
2490 W=100
2510 J=J+5
2520 PR "HOW MANY MERCENARIES WILL YOU HIRE AT 80 BUSHELS EACH";
2540 INPUT N
2550 IF N<0 THEN GOTO 2520
2560 D=G+L-Q-F+C-E
2570 IF RND(99)<W THEN GOTO 2610
2580 PR "PEACE NEGOTIATIONS SUCCEED!"
2590 GOTO 2810
2610 IF W=25 THEN W=150
2620 R=P-S
2630 IF N>R/10 THEN GOTO 2660
2640 R=3*W/5*N*N/R*100/R
2650 GOTO 2680
2660 R=3*W/5+N*100/R
2680 K=(P-S)/2
2690 U=-(A+T)/2
2710 Z=-D/2
2720 IF R>RND(99) THEN GOTO 2760
2730 PR "YOU HAVE LOST THE WAR."
2740 J=J+5000/W
2750 GOTO 2810
2760 PR "YOU HAVE WON THE WAR!"
2770 K=K/2
2780 U=-U
2790 Z=D/4
2810 IF N*80<=D+Z THEN GOTO 2870
2820 GOSUB 3280
2830 PR "TO PAY THE MERCENARIES"
2840 M=3*(P-S-K)/4
2850 O=3*(R+Z)/4
2860 GOTO 2880
2870 H=N*80
2880 N=P-S-K-M
2890 IF RND(99)>4 THEN GOTO 2940
2910 PR "THE BLACK PLAGUE STRIKES!"
2920 V=N/2
2930 GOTO 2970
2940 IF RND(99)>20 THEN GOTO 2980
2950 PR "A POX EPIDEMIC BREAKS OUT!"
2960 V=N/20
2970 N=N-V
2980 B=(N*RND(5)+9)/100+1
2990 D=(N*RND(3)+4)/100
3010 IF Y/7*8-Y/49*7<>Y THEN GOTO 3030
3020 PR "SEVEN-YEAR LOCUSTS REDUCE CROP YIELD."
3030 IF E>0 THEN PR "RATS INFEST YOUR SILOS."
3040 Y=Y+8-(Y-Y/7*7)/6
3050 GOTO 1110
3200 REM UTILITY PR ROUTINES
3220 PR R;" BUSHELS/ACRE";
3230 RETURN
3250 PR "HOW MANY ACRES DO YOU WISH TO ";
3260 RETURN
3270 PR "BUT ";
3280 PR "THERE IS INSUFFICIENT GRAIN."
3290 RETURN
3300 REM INITIALIZE POPULATION AND GRAIN VECTORS
3310 B=0
3320 D=0
3330 K=0
3340 M=0
3350 S=0
3360 V=0
3410 C=0
3420 E=0
3430 F=0
3440 H=0
3450 L=0
3460 O=0
3470 Q=0
3480 Z=0
3490 RETURN
3500 REM INITIALIZATION
3510 Y=RND(6)+1
3520 J=0
3530 P=100
3540 A=1500
3550 G=5000
3560 T=0
3570 U=0
3580 GOSUB 3310
3590 C=-1
3600 REM PR INSTRUCTIONS (MAY BE OMITTED)
3610 PR "DO YOU NEED INSTRUCTIONS (1=YES)";
3620 REM N=P
3630 INPUT N
3640 IF N<>1 THEN RETURN
3650 REM IF N=Y THEN GOTO 3710
3660 REM PR "PLEASE TYPE Y FOR YES, N FOR NO. OK";
3670 REM GOTO 3620
3710 PR "YOU HAVE INHERITED THE THRONE TO THE MEDIEVAL"
3720 PR "KINGDOM OF EUPHORIA, SOMEWHERE IN EUROPE."
3730 PR "AT THIS TIME IT IS ABOUT ";A;" ACRES IN SIZE,"
3740 PR "WITH ";P;" LOYAL PEASANTS TO SERVE YOU."
3750 PR "IN YOUR ROYAL SILOS YOU HAVE ";G;" BUSHELS OF"
3760 PR "NUTRITIOUS GRAIN WITH WHICH TO FEED THE PEOPLE"
3770 PR "AND TRANSACT INTERNATIONAL TRADE."
3780 PR "CLOSE BY YOUR SIDE ARE JEALOUS NEIGHBORING"
3790 PR "KINGDOMS, BUT YOU CAN PROTECT YOURSELF BY"
3810 PR "HIRING EVIL MERCENARIES."
3820 PR "EACH YEAR YOUR MINISTERS WILL PRESENT YOU"
3830 PR "WITH A SUMMARY OF YOUR CURRENT STATUS,"
3840 PR "AND THEN ASK YOU FOR DECISIONS ON WHAT"
3850 PR "TO DO FOR THE NEXT YEAR. PLEASE TYPE Y OR N"
3860 PR "FOR YES OR NO, OR A WHOLE NUMBER FOR"
3870 PR "NUMERIC ANSWERS. IF YOU CHANGE YOUR MIND"
3880 PR "BEFORE HITTING RETURN, HOLD THE 'CTRL' KEY"
3890 PR "DOWN WHILE TYPING THE LETTER 'X', THEN"
3910 PR "RETYPE YOUR ANSWER ON THE NEXT LINE."
3920 PR "ARE YOU READY, YOUR HIGHNESS (1=YES)";
3930 REM N=P
3940 INPUT N
3950 IF N<>1 THEN GOTO 3920
3980 RETURN
3990 END
