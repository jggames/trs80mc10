1     REM *** L'ENFER DE DANTE *** 14/7/83 ***
10    CLEAR 10000:RC%=0:D%=0:MODE 0:PRINT CHR$(12);
20    CURSOR 10,10:PRINT "L'ENFER DE DANTE"
30    WAIT TIME 100:CLS$=CHR$(12)
40    PRINT CLS$
50    PRINT "Voulez vous les instructions ?"
60    GOSUB 5680
70    IF A$="O" THEN 1130:PRINT CLS$;"Il y a un bateau d'ames perdues qui descend le Styx."
80    PRINT "Vous devez attendre qu'ils aient ouvert la porte des enfers.":GOTO 1280
1130  PRINT CLS$;"En echange des richesses du monde,vous avez vendu":PRINT "votre ame au Diable."
1140  PRINT "Maintenant vous realisez que votre ame immortelle est plus":PRINT "importante que tout l'or du monde."
1150  PRINT "Votre tache est d'entrer dans le royaume des tenebres,":PRINT "de trouver votre contrat avec le Diable et de revenir"
1160  PRINT "avec votre contrat.":PRINT :PRINT "Bonne chance !!!"
1280  WAIT TIME 100
1290  M9%=45:TI%=44:T2%=45
1300  DIM W%(M9%),M%(6,M9%)
1310  FOR I%=1 TO M9%
1320  READ N%
1330  IF I%=N% THEN 1360
1340  PRINT "PROBLEME DE DATA ";I%,N%:STOP
1360  FOR J%=1 TO 6:READ M%(J%,I%):NEXT
1370  NEXT
1380  GOSUB 2600
1390  N%=5
1400  RC%=RC%+1
1410  IF RC%>=3 THEN GOSUB 5770
1420  M0%=0
1430  M6%=0
1440  T%=T2%:IF RS%=0 THEN T%=T1%
1450  P%=0
1460  JL%=0
1470  P1%=0
1480  DIM W%(M9%)
1510  PRINT 
1520  GOSUB 2670
1530  M0%=M0%+1
1540  GOSUB 2220
1550  GOSUB 1710
1560  GOSUB 1890
1570  IF T%>=0 THEN 1530
1580  IF N%<>5 THEN 1530
1590  GOSUB 1890
1600  PRINT CLS$;
1610  CURSOR 12,20:PRINT "Felicitations !!!"
1620  PRINT 
1630  PRINT "Vous avez retrouve votre ame"
1640  PRINT "en ";M0%;"mouvements"
1650  PRINT 
1660  PRINT "Voulez vous le refaire ?"
1670  GOSUB 5680
1680  IF A$="N" THEN 5860
1690  RS%=1
1700  GOTO 1390
1710  IF T%<>N% THEN RETURN
1720  IF T%<0 THEN RETURN
1730  IF M6%+5>M0% THEN RETURN
1740  PRINT "Voulez vous le prendre avec vous maintenant ?"
1750  GOSUB 5680
1760  IF A$="N" THEN 1810
1770  T%=-1
1780  PRINT CLS$;
1790  PRINT "Bon,sortons d'ici !"
1800  RETURN
1810  PRINT CLS$;
1820  PRINT "Nous le laisserons ici pour le moment."
1830  PRINT "Vous pourriez regarder autour un peu plus."
1840  PRINT 
1860  PRINT "Ne l'oubliez pas d'ici !!!"
1870  M6%=M0%
1880  RETURN
1890  IF N%=12 THEN RETURN
1900  IF P%=1 THEN RETURN
1910  IF T1%=T2% THEN RETURN
1920  IF -1<>T% THEN RETURN
1930  IF RC%>=3 THEN 1950
1940  IF N%=16 THEN P%=160
1950  IF P1%>0 THEN P1%=P1%+1
1960  IF RC%>=3 AND N%=41 THEN P1%=P1%+1:GOTO 1980
1970  IF N%=3 THEN P1%=P1%+1
1980  IF P1%<15 THEN RETURN
1990  PRINT CLS$;
2000  PRINT "Des tenebres sautent les diables de Mephistopheles."
2010  PRINT 
2020  PRINT "Ils vous reprennent le contrat."
2030  PRINT "Dans la confusion vous reussissez a vous sauver."
2040  PRINT 
2080  PRINT "Maintenant ils vous recherchent.Soyez tres prudent."
2100  P%=1:T%=T2%
2110  RETURN
2130  CLV%=GETC:IF CLV%=0 THEN 2130
2140  A$=CHR$(CLV%)
2150  COM$="RGHBDA"
2160  FOR I%=0 TO LEN(COM$)-1
2170  IF MID$(COM$,I%,1)=A$ THEN A1%=I%+1:RETURN
2180  NEXT:GOTO 2130
2220  N9%=N%
2230  N8%=0
2240  GOSUB 2570
2250  IF N%=1 THEN 2280
2260  N0%=N%
2270  A0%=A1%
2280  I%=M%(A1%,N%)
2290  IF -2=I% THEN I%=N9%
2300  IF D%<>0 THEN PRINT " DEBUG ";N%;" EN ";I%
2310  IF I%<500 THEN 2340
2320  I%=I%-500
2330  GOTO 2290
2340  ON I%/100 GOTO 2370,2400
2350  N%=I%
2360  GOTO 2420
2370  N%=I%-100
2380  IF -1=T% THEN N%=N%+1
2390  GOTO 2420
2400  N%=I%-200
2410  IF -1=T% THEN N%=N%+P%
2420  IF N%<>1 THEN 2470
2430  FOR J%=1 TO 6
2440  M%(J%,N%)=2
2450  NEXT
2460  M%(7-A0%,N%)=N0%
2470  IF N8%<>2 THEN GOSUB 2670
2480  W%(N%)=1
2490  N8%=N%
2500  IF -2<>M%(1,N%) THEN 2560
2510  I%=M%(6,N%)
2520  IF M%(4,N%)>RND(100) THEN I%=M%(5,N%)
2530  IF M%(2,N%)>RND(100) THEN I%=M%(3,N%)
2540  IF D%<>0 THEN PRINT "DEBUG BOUNCE TO ";I%
2550  GOTO 2290
2560  RETURN
2570  PRINT :PRINT "Quelle direction prenez vous ?";
2580  GOSUB 2130
2590  RETURN
2600  IF A$="N" THEN RETURN
2610  PRINT CLS$;
2620  PRINT "Tapez R,A,G,D,H,B pour reculer,avancer,gauche,droite,haut"
2630  PRINT "et bas."
2640  WAIT TIME 100
2650  RETURN
2670  I%=N%/5
2680  J%=N% MOD 5+1
2690  ON I%+1 GOSUB 2700,2710,2720,2730,2740,2750,2760,2770,2780,2790
2691  GOTO 2800
2700  ON J% GOSUB 2900,2900,2940,2990,3060
2701  return
2710  ON J% GOSUB 3120,3240,3300,3360,3410

2711 return
2720  ON J% GOSUB 3430,3490,3560,3600,3680
2721 return

2730  ON J% GOSUB 3730,3790,3870,3930,3990
2731 return
2740  ON J% GOSUB 4050,4120,4190,4210,4270
2741 return
2750  ON J% GOSUB 4330,4390,4500,4560,4590
2751 return
2760  ON J% GOSUB 4660,4720,4800,4880,4930
2761 return
2770  ON J% GOSUB 4980,5030,5080,5130,5230
2771 return
2780  ON J% GOSUB 5290,5340,5400,5480,5540
2781 return
2790  ON J% GOSUB 5600,5640,5650,5660,5670
2791 return
2800  IF T%<>N% THEN 2830
2810  PRINT "Vous avez aussi trouve la cache des contrats de SATAN."
2830  IF T%<>T2% THEN 2880
2840  IF T1%=T2% THEN 2880
2850  IF T1%<>N% THEN 2880
2860  PRINT "Mais ces derniers ont ete enleves."
2880  RETURN
2890  DATA 1,0,0,0,0,0,0
2900  PRINT CLS$;"Vous etes dans une impasse."
2910  RETURN
2920  DATA 2,-2,101,-2,0,0,0
2940  PRINT CLS$;"Vous ne pouvez pas prendre cette direction !"
2970  RETURN
2980  DATA 3,33,2,1,10,106,4
2990  PRINT CLS$;"Un tunnel s'etend devant et derriere vous."
3000  PRINT "Il y a un trou dans le mur sur votre droite."
3010  PRINT "De la lumiere rouge en sort."
3020  RETURN
3030  DATA 4,3,30,2,11,2,1
3060  PRINT CLS$;"Un fleuve de boue s'etend devant vous,elle vient"
3070  PRINT "d'une profonde fosse.":RETURN
3080  DATA 5,8,8,15,10,8,16
3120  PRINT CLS$;"Vous etes sur le bord de la riviere STYX."
3130  PRINT "Des blocs de soufre flottent sur un fleuve de lave."
3140  PRINT "Les portes de l'enfer sont devant vous."
3150  PRINT :PRINT "Sur les portes on peut lire :"
3160  PRINT TAB(10);"Vous qui entrez ici":PRINT TAB(10);"abandonnez tout espoir":RETURN
3170  DATA 6,16,3,2,10,2,2
3240  PRINT CLS$;"Vous entrez dans la piece de Satan.":PRINT "Vous avez de la chance,il est sorti.":RETURN
3290  DATA 7,-2,101,-2,0,0,0
3300  PRINT CLS$;"Satan est la.Il vaudrait mieux essayer ailleurs.":WAIT TIME 100:RETURN
3350  DATA 8,18,18,15,10,18,9
3360  PRINT CLS$;"Vous etes dans le purgatoire.L'enfer est droit devant vous."
3390  RETURN
3400  DATA 9,-2,33,5,1,0,-2
3410  RETURN
3420  DATA 10,-2,101,-2,0,0,0
3430  PRINT CLS$;"Vous n'etes pas alle tres loin dans la roche !!!":WAIT TIME 100:RETURN
3440  DATA 11,1,13,4,2,1,2
3490  PRINT CLS$;"Vous etes au dessus d'un trou."
3500  PRINT "Il est tres sombre et vous pouvez entendre le bruit de la"
3510  PRINT "lave en fusion.":RETURN
3550  DATA 12,36,2,1,2,1,2
3560  PRINT CLS$;"Vous etes dans une impasse !"
3570  RETURN
3580  DATA 13,2,37,2,1,11,14
3600  PRINT CLS$;"Vous etes entre dans une grande grotte."
3610  PRINT "Le sol est couvert d'ossements humains.":PRINT "Vous entendez de faibles bruits devant vous.":RETURN
3620  DATA 14,13,1,19,2,31,31
3680  PRINT CLS$;"Vous etes au fond d'un puit de chaleur.":RETURN
3690  DATA 15,-2,101,-2,0,0,0
3730  PRINT CLS$;"Vous n'etes pas un oiseau,vous ne pouvez pas voler.":WAIT TIME 100:RETURN
3740  DATA 16,5,33,2,10,1,106
3790  PRINT CLS$;"Vous etes aux portes de l'enfer.":PRINT "Sur votre gauche,une petite ouverture."
3800  PRINT "Vous voyez faiblement devant vous et sur votre droite.":PRINT "Il y a de la lumiere derriere vous.":RETURN
3810  DATA 17,-2,101,-2,0,0,0
3870  PRINT CLS$;"C'est un passage etroit et raide.":PRINT "Vous ne pouvez pas passer avec la boite de contrats.":RETURN
3880  DATA 18,-2,101,8,0,0,0
3930  PRINT CLS$;"Auriez vous peur d'aller en enfer ?":WAIT TIME 100:RETURN
3940  DATA 19,224,2,2,14,1,42
3990  PRINT CLS$;"Vous etes au sommet d'un puit de chaleur.":PRINT "La fumee venue du bas vous brule les yeux.":RETURN
4000  DATA 20,226,29,2,2,25,2
4050  PRINT CLS$;"Le sol du tunnel s'effondre brusquement devant vous.":PRINT "Le trou semble trop large pour pouvoir le sauter."
4060  PRINT "Il est tres profond.":RETURN
4070  DATA 21,1,226,2,2,38,25
4120  PRINT CLS$;"Vous etes dans la chaufferie de l'enfer.":PRINT "Il y a des tuyaux dans tous les sens.":RETURN
4130  DATA 22,-2,33,13,50,29,30
4190  RETURN
4200  DATA 23,2,1,2,31,44,2
4210  PRINT CLS$;"Vous etes sur une avancee rocheuse surplombant le troisieme":PRINT "niveau de tourments.":RETURN
4220  DATA 24,-2,101,19,0,0,0
4270  PRINT CLS$;"J'entends venir les anges du demon.":PRINT "Vous feriez mieux de partir.":WAIT TIME 100:RETURN
4280  DATA 25,21,20,2,2,1,27
4330  PRINT CLS$;"Vous etes dans le repere des anges du demon.":PRINT "Il serait prudent de ne pas etre la lorsqu'ils":PRINT "reviendront."
4340  RETURN
4350  DATA 26,-2,65,-2,50,11,14
4390  PRINT CLS$;"Vous etes dans la section de developpement et de recherche":PRINT "de l'enfer.":PRINT 
4400  PRINT "Ce niveau d'enfer n'est pas ouvert aux visiteurs.":PRINT "Vous etes prie de sortir.":WAIT TIME 100:RETURN
4490  DATA 27,25,40,2,2,21,20
4500  PRINT CLS$;:RETURN
4510  DATA 28,-2,60,221,50,14,19
4560  RETURN
4570  DATA 29,2,43,15,13,1,1
4590  PRINT CLS$;"Vous etes sur une etroite corniche au dessus d'une fosse"
4600  PRINT "ou des ames perdues alimentent les chaudieres de l'enfer."
4610  PRINT "Vous pouvez y descendre mais vous ne pourrez pas remonter.":RETURN
4620  DATA 30,34,34,2,1,4,2
4660  PRINT CLS$;"Vous etes dans une petite grotte.":PRINT "Des ames perdues envoient du charbon et du soufre dans":PRINT "les chaudieres."
4670  RETURN
4710  DATA 31,14,14,23,2,1,45
4720  PRINT CLS$;"Vous etes dans le quatrieme niveau de damnation.":PRINT "Autour de vous des tourmentes attaches par les bras et les"
4730  PRINT "jambes brulent a petit feu.":RETURN
4790  DATA 32,-2,101,516,0,0,0
4800  PRINT CLS$;"Vous etes dans le circuit de soufre.":PRINT "Le tube descend de plus en plus bas.":PRINT "Il n'y a pas d'autre issue."
4810  PRINT "Faites attention !!!.":WAIT TIME 100:RETURN
4870  DATA 33,2,1,2,1,116,3
4880  PRINT CLS$;"Le tunnel tourne et se retrecit.":RETURN
4920  DATA 34,1,35,2,1,30,30
4930  PRINT CLS$;"Vous etes dans un tunnel tournant et etroit.":RETURN
4970  DATA 35,2,1,2,37,34,36
4980  PRINT CLS$;"Vous etes dans un tunnel etroit et tournant.":RETURN
5020  DATA 36,35,2,1,37,34,12
5030  PRINT CLS$;"Vous etes dans un etroit tunnel tournant.":RETURN
5070  DATA 37,2,1,35,2,13,2
5080  PRINT CLS$;"Vous etes dans un tres etroit tunnel.":RETURN
5120  DATA 38,2,21,2,116,1,2
5130  PRINT CLS$;"Vous etes dans le 5eme cercle de l'empire de SA Majeste ":PRINT "Satan 1er."
5140  PRINT "Ici sont ceux que Lucifer garde avec une attention toute":PRINT "particuliere et qui y sont pour l'eternite."
5150  PRINT "Vous pouvez voir une petite ouverture dans le sol.":RETURN
5220  DATA 39,41,40,2,32,21,26
5230  PRINT CLS$;"Vous etes devant une fosse sentant mauvais.":PRINT "Vous pouvez y descendre.":PRINT "Faites attention !!!":RETURN
5280  DATA 40,40,40,2,2,27,41
5290  PRINT CLS$;"Vous etes dans la section des damnes deux fois !!!":RETURN
5330  DATA 41,40,36,2,10,40,39
5340  PRINT CLS$;"Vous etes dans la section des maudits.":PRINT "Elle est remplie de fumees empoisonnees.":RETURN
5390  DATA 42,28,28,28,28,28,28
5400  PRINT CLS$;"Vous etes dans la salle des ames perdues.":PRINT "Des fumees de soufre et de lave vous brulent les yeux et"
5410  PRINT "vous perdez le sens de l'orientation.":RETURN
5470  DATA 43,2,1,2,10,29,2
5480  PRINT CLS$;"Vous etes dans la caverne du feu.":PRINT "Votre peau est brulee.":PRINT "La douleur est insupportable.":RETURN
5530  DATA 44,2,23,2,10,2,2
5540  PRINT CLS$;"Vous etes dans une section d'archivage.":RETURN
5590  DATA 45,31,1,2,10,2,2
5600  PRINT CLS$;"Vous etes dans une autre section d'archivage.":RETURN
5640  WAIT TIME 100:RETURN
5650  WAIT TIME 200:RETURN
5660  WAIT TIME 300:RETURN
5670  WAIT TIME 400:RETURN
5680  CLV%=GETC:IF CLV%<>78 AND CLV%<>79 THEN 5680:A$=CHR$(CLV%):RETURN
5770  PRINT CLS$;"Vous pourriez le regretter:":PRINT "J'ai fait en sorte que ce soit un peu plus difficile":PRINT "cette fois.":PRINT 
5780  PRINT "Bonne chance !!!":WAIT TIME 100:RETURN
5860  PRINT CLS$:CURSOR 20,12:PRINT "Au revoir.":WAIT TIME 100:END
