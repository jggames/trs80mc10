10 REM IT CAME FROM THE WEST - EXP VER
10 REM---VARIABLES
10 DIM B$(11):REM BACKGROUND TILES
20 B$(0)="1111111111111111":B$(1)="1111122112211111":B$(2)="22212RJ122211111"
30 B$(3)="22222RR222J21111":B$(4)="12221JR212R21222":B$(5)="++++BBBBBBBB++++"
40 B$(6)="+BB+BBB+BBB+++++":B$(7)="++++BBB+BBB++BB+":B$(8)="+BB++BB++BB++BB+"
50 B$(9)="+++++BBB+BBB+BB+":B$(10)="+BB++BBB+BBB++++"

100 DIM PX(20)  :REM PLAYERS POSITION
110 DIM PY(20)
120 DIM PB(20)  :REM PLAYER BACKGROUND CELL
130 DIM PF(20)  :REM PLAYER FRAME PF=00=OFF
135 DIM P$(7)   :REM PRIZE 0 CHAO

140 LV=1  :REM LEVEL# (UP TO 10?)
141 HP=1:HS=15.9:HA=5:HK=5:HF=20.75 :REM PLAYER STATS AT STARTUP
142 CB=&HC0: REM CAR BACKGROUND CELL
143 PS=0  :REM PLAYER STATE 0WALK 1GUN 2CAR

152 P$(0)=CHR$(&HCF):P$(1)="s":P$(2)="a":P$(3)="k":P$(4)="f":P$(5)=CHR$(&HCF): P$(6)=CHR$(&HCF):P$(7)=CHR$(&HCF)
154 FOR T=1 TO 20:PF(T)=0:PB(T)=0:NEXT
155 PF$="":REM PRIZE BACKGROUND CHOICE
160 BP=0  :REM BACKGROUND TILE POSITION (8X4)
165 T=0   :REM GENERAL TIMER - GEN:32 TILE LOCATIONS / GAME:
170 TP=0  :REM GENERAL TIMER - GEN:TILE LINE (0-3) / GAME:TARGET PEEK
172 TT=0  :REM GENERAL TIMER - GAME:SIDE KICK MEMORIZE FORMULA POSITIONS
175 TX=0  :REM GENERAL TIMER - PLAYER MOVE INTENTION
180 TY=0  :REM GENERAL TIMER
181 T1=0  :REM TENERAL TIMER SHOOT LINE
182 T2=0  :REM GENERAL TIMER
185 BT=5  :REM TILE GRAPHICS (0-10)
187 N=20  :REM NUMBER OF UNIT CICLES FOR NEXT
188 GT=0  :REM GAME TURN TIMER FOR WAVES
189 BO=0  :REM ELEMENT FREE TO BE BORN ZOMBI

200 REM---GENERATE CITY BULDINGS
210 CLS3:POKE 359,128:FOR T=0 TO 31 :REM GFX ON
220 BT=RND(5)-1:FOR TP=0 TO 3:IF T=31 AND TP=3 THEN NEXT ELSE PRINT @BP+TP*32,MID$(B$(BT),TP*4+1,4);:NEXT
230 BP=BP+4:IF BP=32 OR BP=160 OR BP=288 THEN BP=BP+96
240 NEXT

300 REM---GENERATE ROAD
300 T=0:BT=5:BP=(RND(4)-1):PX(0)=1:PY(0)=BP*4:BP=BP*128 :REM CHOOSE START TILE SET PX/PY

310 IF BP=412 OR BP=284 OR BP=156 OR BP=28 THEN BT=5    :REM SET LAST ROAD !!!
320 FOR TP=0 TO 3                  :REM DRAW ROAD TILE
330 IF BP=412 AND TP=3 THEN ELSE PRINT @BP+TP*32,MID$(B$(BT),TP*4+1,4);
340 NEXT
350 IF BP=412 OR BP=284 OR BP=156 OR BP=28 THEN PY(0)=PY(0)+RND(2):PB(0)=176:POKE 359,126:GOTO 1000:REM END GEN !!!
400 IF T=0 THEN BP=BP+4:T=1        :REM SNAKE ROAD PATH
410 IF T=2 THEN BP=BP-128:IF RND(2)=1 AND BP>31 THEN BT=8 ELSE T=0:BT=9
420 IF T=3 THEN BP=BP+128:IF RND(2)=1 AND BP<384 THEN BT=8 ELSE T=0:BT=10
430 IF T=1 THEN IF RND(3)=1 AND BP>31 THEN T=2:BT=6 ELSE IF RND(3)=1 AND BP<384 THEN T=3:BT=7 ELSE T=0:BT=5
450 GOTO 310                       :REM ETERNAL ROAD LOOP

1000 REM---GENERATE TEST START POSITIONS PRE DRAW STUFF
1001 IF LV=1 THEN PF(0)=&HFF
1002 PRINT @PX(0)+PY(0)*32,CHR$(PF(0));  :REM DRAW PL
1004 FOR T=1 TO 10:PX(T)=RND(32)-1:PY(T)=RND(15)-1:PB(T)=&HAF:PF(T)=&H8F:IF PEEK(1024+PX(T)+PY(T)*32)=&HAF THEN NEXT ELSE T=T-1:NEXT
1005 FOR T=1 TO 10:IF T<LV THEN PF(T)=&H8F:NEXT ELSE PF(T)=&HBF:NEXT :REM PRE DRAW ZOMBIES/HUM
1006 FOR T=1 TO 10:PRINT @PX(T)+PY(T)*32,CHR$(PF(T));:NEXT
1008 PB(0)=&HC0:IF LV=1 THEN PRINT @PX(0)+2+PY(0)*32,CHR$(&HEB); :REM DRAW CAR

1010 REM---MAIN LOOP------------------------------------------------------>
1012 PRINT @480,STRING$(31," ");:?@505," WALK ";:POKE1535,&H1F                  :REM HUD
1014 IF HP<10 THEN ?@482,HP;:?@480,"P:0"; ELSE ?@481,HP;:?@480,"P:";            :REM P
1016 IF HS<10 THEN ?@487,INT(HS);:?@485,"S:0"; ELSE ?@486,INT(HS);:?@485,"S:";  :REM S
1017 IF HA<10 THEN ?@492,HA;:?@490,"A:0"; ELSE ?@491,HA;:?@490,"A:";            :REM A
1018 IF HK<10 THEN ?@497,HK;:?@495,"K:0"; ELSE ?@496,HK;:?@495,"K:";            :REM K
1019 IF HF<10 THEN ?@502,INT(HF);:?@500,"F:0"; ELSE ?@501,INT(HF);:?@500,"F:";  :REM F

1040 HS=HS-.1:IF HS<1 THEN HS=0:?@505," WEAK "; ELSE ?@505," WALK "; :REM FOOD DECAY
1041 IF PS>1 AND HF<1 THEN HF=0:PS=0:CB=PB(0):PB(0)=&HEB:PF(0)=&HFF:PRINT @PX(0)+PY(0)*32,CHR$(&HFF);  :REM FUEL CAR KICK
1042 IF PS>1 THEN ?@505,"DRIVE "; :REM DRIVE SIGN

1043 REM GAME TURN TIMER WAVES
1043 GT=GT+1
1044 IF GT=1 THEN FOR T1=1 TO 20:IF PF(T1)=0 THEN BO=T1:T1=20:NEXT ELSE NEXT:GT=0
1045 IF GT=1 THEN PX(BO)=0:PY(BO)=RND(16)-1:IF PEEK((PX(BO)+PY(BO)*32)+1024)<>&HAF THEN GT=0 ELSE GT=0:PF(BO)=&H8F:PRINT @(PX(BO)+PY(BO)*32),CHR$(PF(BO));:PB(BO)=&HAF  :REM NASCIMENTO ZUMBI!!!

1049 IF HP=0 THEN PRINT @480,"ENOUGH! THIS IS THE END.       ";:EXEC44539:END           :REM GAME-OVER
1050 FOR T=0 TO N               :REM LOOP ACTORS------

1051 IF PEEK((PX(T)+PY(T)*32)+1024)=&H9F AND PF(T)=&H8F THEN PF(T)=00:PRINT @PX(T)+PY(T)*32,CHR$(PB(T));:REM KILL ZOMBIE
1052 IF T=1 THEN IF PS=2 THEN PS=3:T=T-1 ELSE IF PS=3 THEN HF=HF-.25:PS=2:IF HF<1 THEN HF=0 :REM CAR DOUBLE DRIVE

1100 REM---CONTROL
1100 TX=0:TY=0
1110 REM PLAYER
1110 REM SAIDA DO CARRO
1110 IF T=0 THEN I$=INKEY$: IF PS>1 THEN :IF I$="E" THEN PS=0:CB=PB(0):PB(0)=&HEB:PF(0)=&HFF:PRINT @PX(0)+PY(0)*32,CHR$(&HFF);:NEXT

1111 REM PLAYER CONTROL
1111 IF T=0 AND PS<>1 THEN POKE1535,&H1F:IF I$="A" THEN TX=-1 ELSE IF I$="D" THEN TX=1 ELSE IF I$="W" THEN TY=-1 ELSE IF I$="S" THEN TY=1 ELSE IF I$="Q" THEN NEXT ELSE IF I$="E" AND HA>0 AND PS=0 THEN PS=1 ELSE GOTO 1100

1113 IF PX(0)=31 AND PB(0)=192 THEN IF LV=10 THEN PRINT @480,"YOU HAVE ESCAPED!              ";:EXEC44539:END ELSE LV=LV+1:GOTO 152 :REM ADVANCE NEXT STAGE
1114 IF T=0 AND PS=0 AND HS=0 AND RND(4)=1 THEN SOUND1,2:TX=0:TY=0:NEXT  :REM STARVE TROPEÃ‡O

1115 REM SHOOT CONTROL
1115 IF PS=1 THEN I$=INKEY$:?@505,"SHOOT ";:IF I$="A" THEN TX=-1 ELSE IF I$="D" THEN TX=1 ELSE IF I$="W" THEN TY=-1 ELSE IF I$="S" THEN TY=1 ELSE IF I$="E" THEN PS=0:?@505," WALK "; ELSE GOTO 1115:REM SHOOT !!!
1116 REM COLLISION BULLET
1116 IF PS=1 THEN FOR T1=1 TO 4:TT=PX(0)+T1*TX+(PY(0)+T1*TY)*32:TP=PEEK(TT+1024):SOUND1,1
1117 IF PS=1 THEN IF TP=&H8F AND RND(2)=1 THEN ?@TT,CHR$(&H9F);:SOUND90,3:T1=4:NEXT ELSE IF TP=&HB0 THEN T1=4:NEXT ELSE NEXT
1118 IF PS=1 THEN TX=0:TY=0:PS=0:?@505," WALK ";:HA=HA-1:NEXT

1120 REM ZOMBIE
1120 IF PF(T)=&H8F THEN I$=" ":?@505,"      ";:POKE1535,&H1E:IF RND(2)=1 THEN IF PX(0)<PX(T) THEN TX=-1 ELSE TX=1 ELSE IF RND(2)=1 THEN IF PY(0)<PY(T) THEN TY=-1 ELSE TY=1 ELSE TX=0:TY=0
1121 REM HUMANS
1121 IF PF(T)=&HBF THEN I$=" ":?@505,"      ";:POKE1535,&H1E:IF RND(2)=1 THEN TX=RND(3)-2 ELSE TY=RND(3)-2
1123 IF PEEK((PX(T)+PY(T)*32)+1024)=&HEF THEN PF(T)=00:PRINT @PX(T)+PY(T)*32,CHR$(PB(T));:REM CLEAR HUMAN COLLECTED
1124 IF PF(T)=&HBF AND PEEK((PX(T)+PY(T)*32)+1024)=&H9F THEN PF(T)=&H8F:PRINT @PX(T)+PY(T)*32,CHR$(PF(T));:REM TRANSFORM HUMAN TO ZOMBIE

1125 REM IF PF(T)=&H8F AND PEEK((PX(T)+PY(T)*32)+1024)=&H9F THEN PF(T)=00:PRINT @PX(T)+PY(T)*32,CHR$(PB(T));:REM KILL ZOMBIE

1127 IF PX(T)+TX<0 OR PX(T)+TX>31 THEN TX=0   :REM BORDERS CHECK
1128 IF PY(T)+TY<0 OR PY(T)+TY>14 THEN TY=0

1130 TP=PEEK((PX(T)+TX+(PY(T)+TY)*32)+1024) :REM CHECK STEP
1132 IF TP=&HBF THEN IF T=0 THEN HP=HP+1:PRINT @PX(0)+TX+(PY(0)+TY)*32,CHR$(&HEF);:TX=0:TY=0:NEXT :REM PL RESCUE HUMAN
1134 IF TP=&HFF THEN IF PF(T)=&H8F THEN HP=HP-1:?@PX(0)+PY(0)*32,CHR$(&HEF);:PLAY"L25;O2;C":PRINT @PX(0)+PY(0)*32,CHR$(PF(0));:PLAY"O1;C":TX=0:TY=0 :REM ZOMBI HIT PL
1136 IF TP=&HBF THEN IF PF(T)=&H8F THEN PRINT @PX(T)+TX+(PY(T)+TY)*32,CHR$(&H9F);:TX=0:TY=0

1138 REM CAR TOUCH
1138 IF T=0 AND TP=&HEB THEN PRINT @PX(T)+TX+(PY(T)+TY)*32,CHR$(CB);:PS=3:PF(0)=&HEB:TP=&HC0

1140 REM DOOR TOUCH
1140 IF T=0 AND TP=&HC8 AND HK>0 THEN FOR T1=-1 TO 1:FOR T2=-1 TO 1: IF PEEK((PX(T)+TX+T1+(PY(T)+TY+T2)*32)+1024)=&HD0 THEN PF$=P$(RND(8)-1):PRINT @PX(T)+TX+T1+(PY(T)+TY+T2)*32,PF$;:NEXT:NEXT ELSE NEXT:NEXT
1142 IF T=0 AND TP=&HC8 THEN IF HK>0 THEN HK=HK-1:PRINT @PX(T)+TX+(PY(T)+TY)*32,CHR$(&HCF);:TX=0:TY=0:NEXT ELSE IF RND(2)=1 THEN HK=1:GOTO 1140 ELSE NEXT:REM BUMP??

1145 REM PRIZE TOUCH
1145 IF T=0 THEN IF TP=19 THEN HS=HS+5:PRINT @PX(T)+TX+(PY(T)+TY)*32,P$(0);:TP=&HC0
1146 IF T=0 THEN IF TP=1 THEN HA=HA+5:PRINT @PX(T)+TX+(PY(T)+TY)*32,P$(0);:TP=&HC0
1147 IF T=0 THEN IF TP=11 THEN HK=HK+5:PRINT @PX(T)+TX+(PY(T)+TY)*32,P$(0);:TP=&HC0
1148 IF T=0 THEN IF TP=6 THEN HF=HF+5:PRINT @PX(T)+TX+(PY(T)+TY)*32,P$(0);:TP=&HC0

1150 REM CAR RUN OVER ZOMBIE
1150 IF T=0 AND PS>1 THEN IF TP=&H8F THEN PRINT @PX(T)+TX+(PY(T)+TY)*32,CHR$(&H9F);:HF=HF-5:NEXT

1155 REM DRIVE AT STREET ONLY
1155 IF T=0 AND PS>1 AND TP<>&HC0 THEN TX=0:TY=0:GOTO 1100

1200 REM WALK
1200 IF TP=&HC0 OR TP=&HA9 OR TP=&HAF OR TP=&HCF THEN ?@PX(T)+PY(T)*32,CHR$(PB(T));:PX(T)=PX(T)+TX:PY(T)=PY(T)+TY:PB(T)=PEEK((PX(T)+PY(T)*32)+1024):?@PX(T)+PY(T)*32,CHR$(PF(T));:NEXT ELSE IF T=0 THEN 1100 ELSE NEXT

1260 GOTO 1010 :REM OVER FOR NEXT
1500

