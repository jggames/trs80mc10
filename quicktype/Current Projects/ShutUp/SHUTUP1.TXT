0 DIMC1,C2,M$:M=16384:GOTO10
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-M+C2,:RETURN
10 REM SHUT UP AND JUST FOLLOW ME
20 DIM XX(4),YY(4)
30 C0$="X":C1$="@":C2$="*":C3$=CHR$(159):C4$="+":C5$=" "
40 GOSUB860
50 SC=0:FI=1:LE=1
60 REM INIT
70 X0=12:Y0=6:X1=19:Y1=9
80 XV=0:YV=0:D=4:F0=0:F1=0:FORC1=0TO1:C1=-(INKEY$=""):NEXT:M$=""
90 REM SETUP FIELD
100 CLS
110 FORA=1TO5:PRINT@INT(RND(0)*32)+32*(INT(RND(0)*14)+1),C2$;
120 NEXT
130 FORA=0TO31:PRINT@A+32*1,C2$;:PRINT@A+32*14,C2$;:NEXT
140 FORA=2TO13:PRINT@32*A,C2$;:PRINT@31+32*A,C2$;:NEXT
150 PRINT@X0+32*Y0,C0$;
160 PRINT@X1+32*Y1,C1$;
170 FORA=0TOD:X=INT(RND(0)*30)+1:Y=INT(RND(0)*12)+2
180 IFX=12ANDY=6THENX=X+7
190 IFX=19ANDY=9THENY=Y-3
200 PRINT@X+32*Y,C3$;:XX(A)=X:YY(A)=Y:NEXT
210 PRINT@0," SCORE:               LIFE LEFT";
220 PRINT@22+32*15,"FIELD:";
230 PRINT@28+32*15,STR$(FI);
240 PRINT@7,SC;:PRINT@19,STR$(LE);
250 PRINT@1+15*32,;:M$=">SHUT UP & FOLLOW!<":GOSUB7
270 REM MAIN LOOP
280 GOSUB310:IFF1=1THEN505
290 GOSUB390:IFF1=1THEN510
300 GOTO270
309 REM MOVE PLAYER
310 FORC1=1TOLV:NEXT:M$=INKEY$:IFM$=""THEN380
311 IFM$="W"THENYV=-1:XV=0
312 IFM$="A"THENXV=-1:YV=0
313 IFM$="S"THENXV=1:YV=0
314 IFM$="Z"THENYV=1:XV=0
340 PRINT@X0+32*Y0,C4$;
350 X0=X0+XV:Y0=Y0+YV
360 C=PEEK(X0+32*Y0+M):IFC=106ORC=107ORC=64ORC=159THENF1=1
370 PRINT@X0+32*Y0,C0$;
380 RETURN
390 REM MOVE PROXY
400 PRINT@X1+32*Y1,C5$;
410 X1=X1+XV:Y1=Y1+YV
420 C=PEEK(X1+32*Y1+M):IFC=88ORC=106ORC=107THENF1=1
430 PRINT@X1+32*Y1,C1$;
440 FORA=0TO4:IFX1=XX(A)ANDY1=YY(A)THENGOSUB470
450 NEXT:IFF0=1THEN590
460 RETURN
470 SOUND222,1:SC=SC+(50*L):GOSUB560:IFD=0THENF0=1
480 XX(A)=0:YY(A)=0
490 D=D-1:RETURN
500 REM HIT OBSTACLE
505 FORC2=1TO8:PRINT@X0+32*Y0,"x";:FORC1=1TO250:NEXT:PRINT@X0+32*Y0,"X";:FORC1=1TO250:NEXT:NEXT:GOTO530
510 FORC2=1TO8:POKEX1+32*Y1+M,0:FORC1=1TO250:NEXT:PRINT@X1+32*Y1,"@";:FORC1=1TO250:NEXT:NEXT
530 SOUND113,4:SOUND103,4
540 IFLE=0THEN660
550 LE=LE-1:FORA=1TO1250:NEXT:GOTO60
560 REM PRINT SCORE
570 PRINT@7,SC;
580 RETURN
590 REM FIELD CLEARED
600 PRINT@6+32*6,;:M$=">>> FIELD CLEAR! <<<":GOSUB7
610 PRINT@9+32*8,;:M$="= BONUS"+STR$(FI*500)+" =":GOSUB7
620 SC=SC+FI*500:GOSUB560
630 SOUND129,4:SOUND142,4:SOUND113,4:SOUND149,8:SOUND161,4:SOUND87,4:SOUND103,8
640 FORA=1TO1750:NEXT:FI=FI+1:IFFI=6THEN710
650 GOTO60
660 REM
670 FORA=1TO1250:NEXT
680 PRINT@6+32*6,;:M$="   >> GAME OVER <<   ":GOSUB7
690 PRINT@5+32*9,;:M$="  THANKS FOR PLAYING!  ":GOSUB7
700 FORA=1TO3000:NEXT:GOTO40
710 REM COMPLETED
720 FORA=1TO14:PRINT@32*A,"":NEXT
730 RESTORE:PRINT@32*2,;
740 SOUND225,4:SOUND229,4:SOUND233,4:SOUND231,8:SOUND219,4:SOUND233,4:SOUND229,4:SOUND221,4
750 FORA=1TO4:READB,A$:PRINTTAB(B);:FORAA=1TOLEN(A$)
760 PRINTCHR$(ASC(MID$(A$,AA,1))-64);:NEXT:PRINT:PRINT:PRINT:NEXT
770 PRINT@4+32*13,"Ai.AiHD120%   1  9  9  0"
780 FORA=1TO10000:NEXT:GOTO40
790 REM
800 DATA 7,"™•Ïˆ–…ÏƒŒ…”…„"
810 DATA 8,"ŒŒÏ“‰˜Ï†‰…Œ„“a"
820 DATA 8,"ƒŽ‡’”•Œ”‰Ž“a"
840 DATA 11,"’…“…Ž”Ï‚™"
860 REM TITLE
860 CLS
870 PRINT"     ŽŒŒŒŒŒ";
880 PRINT"     …‡………………………";
890 PRINT"     ‹ƒƒ………………ƒ";
900 PRINT"     †Œ‡……†Œ‡…†Œ‡…Š";
910 PRINT"     ‡";
920 PRINT"    Ž†„…‚ƒ………‰ƒƒ‡";
930 PRINT"    ………†…Š…………†Œ…";
940 PRINT"    ƒ………Š………………";
950 PRINT"    ‡‡‡‡ƒƒ‹ƒ‹ƒ‹ƒ‡";
960 PRINT"   ŒŒŽŒŽŒŒŒ";
970 PRINT"   ………………………‰……";
980 PRINT"   ‡………………………‡…ƒ";
985 PRINT"   …†Œ‡„Œ„Œ†Œ‡‹………„Œ";
990 PRINT"  PROGRAMMED BY  T  O  M  M  Y  ";
1000 PRINT"           1  9  9  0";
1020 A=0
1030 M$=INKEY$:IF M$="1" OR M$="2" OR M$="3" THEN 1080
1040 A=A+1:IFA=21 THEN A=1
1050 IF A>10 THEN 1070
1060 PRINT@483,"CHOOSE LEVEL OF PLAY (1-3)";:GOTO 1030
1070 PRINT@483,"choose level of play (1-3)";:GOTO 1030
1080 L=VAL(M$):LV=(4-L)*50+100:RETURN
2000 REM MC-10 VERSION
2010 REM JIM GERRIE 2024

