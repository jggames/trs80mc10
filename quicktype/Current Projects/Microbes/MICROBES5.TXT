10 GOSUB2460:CLS:GOTO1780
20 A=INT(RND(0)*B):RETURN
40 WR=M2:W$=R$
60 D(X,Y)=WR
70 XT=LX+DE*X:YT=LY+Y
90 PRINT@(YT-1)*32+XT-1,W$;:RETURN
130 IFCA=ZETHEN2380
140 SOUND100,1
150 B=CA
160 GOSUB20:FORYT=UNTOYM:FORXT=UNTOXM:IFD(XT,YT)<>M1THEN260
200 IFA>ZETHEN250
210 X=XT:Y=YT:XT=XM:YT=YM
250 A=A-UN
260 NEXT:NEXT:CA=CA-UN:GOSUB60:RETURN
310 X=XD+X:Y=YD+Y:A=D(X,Y):RETURN
350 IFNM=ZETHENRETURN
360 B=4
370 FORCM=UNTONM:GOSUB20:IFA=TRTHEN420
400 XD=A-UN:GOTO430
420 XD=ZE
430 IFA=ZETHEN460
440 YD=A-DE:GOTO470
460 YD=ZE
470 X=X(CM):Y=Y(CM):GOSUB310:IFA=ZETHEN630
510 IFA<>M1THEN660
520 WR=CM:W$=M$:X(CM)=X:Y(CM)=Y:GOSUB60:X=X-XD:Y=Y-YD:WR=M1:W$=B$:GOSUB60:GOTO660
630 XH=ZE:GOSUB1610:CM=NM+DE
660 NEXT:RETURN
680 B=ZE:X=XH:Y=YH
710 GOSUB310:IFA>ZETHEN780
730 IFA=M1THEN890
740 IFA=M3THENRETURN
750 IFMD=UNTHENRETURN
760 B=UN:GOTO710
780 IFB=UNTHEN820
790 XH=ZE:GOSUB1600:RETURN
820 SC=SC+UN:XT=UN:YT=YM+DE+LY
850 W$="SCORE:":SOUND200,2
860 GOSUB90:PRINTSC:GOSUB1600
890 IFB=ZETHEN930
900 GOSUB40:X=XH+XD:Y=YH+YD
930 XH=X:YH=Y:WR=ZE:W$=H$:GOSUB60:X=XH-DE*XD:Y=YH-DE*YD:W$=B$:WR=M1:IFMD=ZETHEN1080
1030 A=D(X,Y):IFA<>M2THEN1080
1050 GOSUB60:W$=R$:WR=M2
1080 GOSUB310:GOSUB60:RETURN
1110 IFXH=ZETHEN1390
1120 C=PEEK(2)ANDPEEK(17023):IFC=0THEN1240
1140 C$=CHR$(C):IFC$=B$THEN1220
1170 IFC$=U$THEN1300
1180 IFC$=D$THEN1280
1190 IFC$=T$THEN1330
1200 IFC$=L$THEN1350
1210 GOTO1240
1220 MD=ABS(MD-UN):SOUND100,1:SOUND110,1:SOUND120,1:FORC=1TO25:NEXT
1230 GOTO1110
1240 HR=HR-UN:IFHR<=ZETHEN1450
1260 GOSUB350:GOTO1110
1280 YD=UN:GOTO1310
1300 YD=M1
1310 XD=ZE:GOTO1370
1330 XD=UN:GOTO1360
1350 XD=M1
1360 YD=ZE
1370 GOSUB680:GOTO1240
1390 WR=ZE:W$=H$:GOSUB130:XH=X:YH=Y:GOTO1120
1450 HR=TM:IFNM=MMTHEN1540
1470 NM=NM+UN:WR=NM:W$=M$:GOSUB130:X(NM)=X:Y(NM)=Y:GOTO1110
1540 X=X(UN):Y=Y(UN):GOSUB40:CM=UN:GOSUB1690:GOTO1110
1600 CM=A
1610 GOSUB40:X=X-XD:Y=Y-YD:IFXH<>ZETHEN1680
1650 WR=M1:W$=B$:GOSUB60
1680 CA=CA+UN
1690 NM=NM-UN:IFCM>NMTHEN1760
1710 FORJ=CMTONM:X(J)=X(J+UN):Y(J)=Y(J+UN):D(X(J),Y(J))=J:NEXT
1760 SOUND1,1:RETURN
1780 DIMD(11,11)
1790 XM=10
1795 YM=10
1800 DIMX(5)
1805 DIMY(5)
1810 MM=5
1820 ZE=0
1830 UN=1
1840 CLS:SC=0:A=0:B=0:XD=0:YD=0:FORX=0TO5:X(X)=0:Y(X)=0:FORX=0TO11:FORY=0TO11:D(X,Y)=0:NEXT:NEXT:X=0:Y=0
1850 DE=2
1860 TR=3
1870 MD=0
1880 M1=-1
1890 M2=-2
1900 M3=-3
1910 R$="O"
1920 M$="X"
1930 H$="H"
1940 B$=" "
1960 LX=5
1970 LY=3
1980 U$="I"
1990 D$="K"
2000 L$="J"
2010 T$="L"
2020 DI=10
2030 DR=3
2040 TM=6
2050 HR=TM
2060 CA=XM*YM
2070 CLS
2080 B=DI
2090 FORY=ZETOYM+UN
2100 W$=" #"
2110 WR=M3
2120 X=ZE
2130 GOSUB60
2140 FORX=UNTOXM
2150 IFY<UNTHEN2280
2160 IFY>YMTHEN2280
2170 GOSUB20
2180 IFA>DRTHEN2230
2190 D(X,Y)=M2
2200 PRINTR$;
2210 CA=CA-UN
2220 GOTO2250
2230 D(X,Y)=M1
2240 PRINTB$;
2250 IFX=XMTHEN2310
2260 PRINTB$;
2270 GOTO2310
2280 W$="##"
2290 WR=M3
2300 GOSUB60
2310 NEXTX
2320 W$="#"
2330 WR=M3
2340 GOSUB60
2350 NEXTY
2360 GOSUB1390
2370 GOTO1110
2380 Y=YM+TR
2390 GOSUB70
2400 IFSC>HSTHENHS=SC
2405 PRINT@448+16,"HIGH:"HS;:PRINT@480,"PLAY AGAIN (Y/N)?";
2410 C$=INKEY$
2420 IFC$="Y"THEN1840
2430 IFC$="N"THEN2450
2440 GOTO2410
2450 END
2460 CLS:XT=5:YT=4
2500 W$="************************":GOSUB90:YT=YT+1
2510 W$="*                      *":GOSUB90:YT=YT+1
2520 W$="*     MICROGOUILLES    *":GOSUB90:YT=YT+1
2530 W$="*                      *":GOSUB90:YT=YT+1
2531 W$="* BY DOMINIQUE LAROCHE *":GOSUB90:YT=YT+1
2532 W$="*                      *":GOSUB90:YT=YT+1
2540 W$="************************":GOSUB90:YT=16:XT=3
2550 W$="PRESS b TO BEGIN h FOR HELP":GOSUB90
2560 C$=INKEY$:C=RND(0):IFC$=""THEN2560
2570 IFC$="B"THENRETURN
2580 IFC$="H"THEN2600
2590 GOTO2560
2600 CLS:PRINT"YOUR BODY HAS BEEN INVADED BY"
2610 PRINT"A NEW SPECIES OF MICROSCOPIC"
2620 PRINT"ORGANISM x. IT REPRODUCES VERY"
2625 PRINT"RAPIDLY, BUT ONLY LIVES FOR"
2630 PRINT"A BRIEF TIME. IT HAS A MAJOR"
2640 PRINT"WEAKNESS, CONTACT WITH DEAD"
2650 PRINT"CELLS o WILL KILL IT. YOUR"
2660 PRINT"IMMUNE SYSTEM HAS PRODUCED"
2670 PRINT"A NEW ANTIBODY h TO COUNTER THE"
2680 PRINT"PROLIFERATION OF THE ORGANISM,"
2690 PRINT"CALLED MICROGOUILLES. USE ikjl"
2700 PRINT"TO MANEUVER YOUR ANTIBODY AND"
2710 PRINT"CRUSH THE INVADERS. PUSH space"
2720 PRINT"TO SWITCH BETWEEN PUSH AND PULL"
2730 PRINT"MODE FOR MOVING DEAD CELLS."
2740 PRINT"    PRESS ANY KEY TO BEGIN";
2780 C$=INKEY$:IFC$=""THEN2780
2790 RETURN
3000 REM PUBLISHED IN
3010 REM SCIENCE & VIE MICRO
3020 REM NO. 3 FEVRIER 1984
3030 REM MC-10 EDITS BY
3040 REM JIM GERRIE
