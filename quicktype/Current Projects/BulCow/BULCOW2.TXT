5 GOSUB500
10 CLEAR100:DIM D(10,4),B(10),C(10),G(10):A=RND(-(PEEK(9)*256+PEEK(10)))
20 A=0:GOTO 200
30 PRINT:J=0
35 INPUT"YOUR GUESS";N:N=(N+.1)/100000
40 FOR I=0 TO 4:G(I)=INT(10*N):N=10*N-INT(10*N)
41 FOR K=0 TO I-1:IF I=0 GOTO 44
42 IFG(I)=G(K) GOTO 170
43 NEXT K
44 NEXT I
45 P=4:A=0:GOSUB 300
50 IF V=1 THEN B$="BULL":GOTO 52
51 B$="BULLS"
52 IF W-V=1 THEN C$="COW":GOTO60
53 C$="COWS"
60 IF V=5 THEN PRINT"5 BULLS, YOU WIN!":GOTO 110
61 PRINTV;B$;W-V;C$
65 IF J=0 THEN A=1:GOTO 200
68 GOSUB 400
70 PRINT"MY GUESS IS ";
75 FOR I=0 TO 4:PRINT CHR$(D(J,I)+48);:NEXT:PRINT
80 INPUT"MY SCORE #,#";B(J),C(J):C(J)=C(J)+B(J)
81 IF B(J)>-1 THEN IF B(J)<6 THEN IF C(J)<6 THEN IF C(J)-B(J)>-1 THEN 83
82 PRINT"RIDICULOUS!!":GOTO 70
83 IF B(J)=4 THEN IF C(J)=5 THEN 82
85 IF B(J)=5 THEN PRINT " - I WIN -":PRINT"MY NUMBER WAS ";:GOTO 100
90 GOTO 35
100 FOR I=0 TO 4:PRINT CHR$(D(0,I)+48);:NEXT:PRINT N$
110 PRINT
120 SOUND100,1:INPUT"TRY AGAIN (Y/N)";M$
130 IF LEFT$(M$,1)="N" THEN END
140 GOTO 10
150 PRINT:PRINT "YOU HAVE GIVEN ME IMPOSSIBLE":PRINT"SCORES-- GAME SPOILED.":GOTO 110
170 PRINT "REPEATED DIGITS NOT ALLOWED":GOTO 35
200 FOR P=0 TO 4
210 D(A,P)=INT(10*RND(0))
220 FOR I=0 TO P-1:IF P=0 GOTO 230
222 IF D(A,I)=D(A,P) THEN 210
230 NEXT I:NEXT P
250 IF A=0 THEN 30
260 J=1:GOTO 70
300 V=0:W=0
310 FOR I=0 TO P:IF D(A,I)=G(I) THEN V=V+1
320 FOR K=0 TO 4:IF D(A,K)=G(I) THEN W=W+1
322 NEXT K
330 NEXT I
350 RETURN
400 P=0
405 G(P)=D(J,P)
410 FOR I=0 TO P-1:IF P=0 GOTO 420
412 IF G(I)=G(P) GOTO 430
415 NEXT
420 FOR A=1 TO J:GOSUB 300
425 IF V<=B(A) THEN IF W<=C(A) THEN IF 4-P>=C(A)-W THEN IF 4-P>=B(A)-V GOTO 448
430 G(P)=G(P)+3:IF G(P)>9 THEN G(P)=G(P)-10
432 IF P=0 THEN IF G(P)=D(1,0) GOTO 150
435 IF G(P)<>D(J,P) GOTO 410
440 P=P-1:IF P<0 GOTO 150
445 GOTO 430
448 NEXT A
450 P=P+1:IF P<5 GOTO 405
455 J=J+1
460 FOR I=0 TO 4:D(J,I)=G(I):NEXT
465 RETURN
500 CLS:PRINT TAB(6)"bulls and cows game"
601 PRINT"BEAT THE COMPUTER AT GUESSING"
602 PRINT"A FIVE DIGIT RANDOM NUMBER"
603 PRINT"WITHOUT DUPLICATION."
604 PRINT
605 PRINT"THE PROGRAM WILL:"
606 PRINT
607 PRINT" - ASK FOR GUESSES AND ITS SCORE";
608 PRINT" - REJECT GUESSES"
609 PRINT"   THAT ARE MALFORMED"
610 PRINT" - SCORE YOUR GUESSES"
611 PRINT
612 PRINT"SCORES ARE COMPUTED AS:
613 PRINT"1 BULL FOR EACH CORRECT DIGIT"
614 PRINT"1 COW FOR EACH IN WRONG POSITION";
615 INPUT"      HIT enter TO BEGIN";M$:CLS:GOTO10
700 REM BRADFORD UNIVERSITY
710 REM CN NOV 1976
720 REM MC-10 EDITS JIM GERRIE
999 END
