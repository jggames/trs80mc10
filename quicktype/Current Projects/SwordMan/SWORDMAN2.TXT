0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
5 PRINT:PRINT"PRESS ANY KEY."
6 ON-(INKEY$="")GOTO6:CLS:RETURN
10 REM..........SWORDSMAN......WALWYN
25 MZ=0
30 DIME$(4):E$(1)="YOU ARE UNHARMED AND RARING TO GO...!"
31 E$(2)="YOU ARE SLIGHTLY WOUNDED NOW. BE BRAVE!"
32 E$(3)="YOU HAVE RECEIVED MULTIPLE LACERATIONS."
33 E$(4)="IT'S YOUR LAST CHANCE NOW. GOOD LUCK..."
100 DIMV(9)
110 FORJ=1TO9:READV(J):NEXTJ
120 DATA 3,4,3,4,2,5,2,2,4
150 REM VK=NUMBER OF DEAD MONSTERS
160 REM WK=WOUNDED LEVEL
170 REM PK=POINTS SCORED
180 VK=0:PK=1:WK=1
200 DIMV$(9)
210 FORK=1TO9:READV$(K):NEXT
220 DATA"GANGRENE GOAT","NORTH-SEA BOIL","VAMPIRE CAT"
230 DATA"POLITICAL WORM","MAD METER-MAID","SCHOOL SPINACH"
240 DATA"DANDRUFFED BEAR","RAMPANT BANKER","PICKLED ALBATROSS"
300 CLS:PRINTTAB(11)"SWORDSMAN":PRINT
310 M$="IN THIS GAME, YOU ARE TO BE PUT AGAINST HORDE OF TERRIFYING MONSTERS. YOU ARE ARMED ONLY WITH A SWORD.":GOSUB1
340 PRINT"YOUR CONTROLS ARE:"
341 PRINT"A  MOVE TO THE LEFT"
350 PRINT"D  MOVE TO THE RIGHT"
360 PRINT"S  LUNGE WITH SWORD":GOSUB5
370 M$="THERE ARE TWO SWORD POSITIONS. THE FIRST TIME YOU PRESS '5', YOU PUT YOUR SWORD 'ON-GUARD'.":GOSUB1
400 M$="THE SECOND KEY-PRESS COMMITS YOU TO A SWORD THRUST. SOME MONSTERS ARE HARDER TO KILL THAN OTHERS.":GOSUB1
420 M$="THE DREADED 'SCHOOL SPINACH' IS PARTICULARLY NASTY TO MEET.":GOSUB1:GOSUB5
440 M$="YOU GET MORE POINTS IF YOU KILL THE MONSTER FURTHER TO THE RIGHT OF THE SCREEN.":GOSUB1
470 M$="EACH TIME YOU WIN, YOUR WOUNDS ARE SOOTHED JUST A LITTLE.":GOSUB1
480 M$="IF YOU ARE WOUNDED MORE THAN 4 TIMES THEN YOU ARE DEFINITELY dead!":GOSUB1:GOSUB5
500 DIMD$(4,3),M$(9,4,3)
510 FORI=1TO4:FORJ=1TO3:READD$(I,J)
530 NEXT:NEXT
550 REM THESE DATA LINES HOLD VARIOUS STANCES OF THE SWORDSMAN
600 DATA " O!"," O "," ))"
602 DATA " O!"," O ","(("
604 DATA " O /"," O-"," X"
606 DATA " O"," O--","/ )"
650 FORK=1TO9:FORJ=1TO4:FORI=1TO3
660 READM$(K,J,I)
680 NEXT:NEXT:NEXT
699 REM LINES 700-876 CONTAIN STANCES OF THE MONSTERS WHO OPPOSE THE SWORDSMAN
700 DATA "O ","##-","(("
702 DATA "O ","#  -","II'"
704 DATA "O ","##-","I<'"
706 DATA "O   /"," ##"," //"
720 DATA "***","*O:O*","* # *"
722 DATA "***","*O:O*","* # *"
723 DATA "***","*O:O*",""
724 DATA "***","*O:O*","* # *"
740 DATA "","O- "," X"
742 DATA "","O- "," I"
744 DATA "","O--","<<"
746 DATA "","O- ","//"
760 DATA "","#    O"," OO O"
762 DATA "","#   OO"," OO  O"
764 DATA ""," O O","# O   "
766 DATA "","   OO","#O  OO"
780 DATA "$$$$$","$O.O$","<< >>"
782 DATA "$$$$$","$O.O$"," X X"
784 DATA "$$$$$","$O.O$","<< >>"
786 DATA "$$$$$","$O.O$"," X X"
800 DATA "## #","#O##"," ##"
802 DATA "###","####","O##"
804 DATA " ##","###","# #"
806 DATA "# #","## #","#O#"
820 DATA "*OO*.","   "," B  "
822 DATA "*OO*","   ."," E  "
824 DATA "*OO*","  "," A  ."
825 DATA "*OO*","  "," R  "
840 DATA " O","-:-"," X"
842 DATA " O"," : ","( )"
844 DATA " O","I:I","< >"
846 DATA " O","###","/ I"
860 DATA "  # ","O -<","''"
862 DATA "O--<","''",""
864 DATA "O!(<"," # ",""
866 DATA "","O--<","''"
999 REM START OF GAME
1000 CLS1
1100 GOSUB4000
1900 GOTO1000
2750 FORL=1TO2500:NEXTL:RETURN
2760 PRINT@352,;:M$="THE "+V$(MZ)+" CUT YOU UP!       ":GOSUB1
2765 FORI=1TO1500:NEXTI:RETURN
2799 REM CLEAR TOP SCREEN
2800 FORX=0TO320 STEP32:PRINT@X,"";
2815 NEXT
2817 PRINT@0,;
2820 RETURN
2950 MR=MZ
2960 MZ=RND(9):IFMZ=MRTHEN2960
2970 RETURN
4000 GOSUB2950:REM MONSTER BATTLE
4002 PRINT@288,"ÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿÿ";
4005 M$="IT'S THE "+V$(MZ)+"!":GOSUB1
4006 M$="(DEGREE OF DIFFICULTY="+STR$(V(MZ))+")":GOSUB1
4007 M$=E$(WK):GOSUB1
4010 M7=4:D7=2:M8=20+RND(5)
4011 D8=15:M6=4:D6=2:M9=M8:D9=D8
4015 PRINT@0,"TOTAL POINTS";PK
4020 ZZ=0
4021 K1=PEEK(2)ANDPEEK(17023):ZZ=ZZ+1:IFZZ<9ANDK1=.THEN4021
4025 IFK1=65THEND8=D8-1:D7=1:IFD8<4THEND8=4
4030 IFK1=68THEND8=D8+1:D7=2:IFD8>27THEND8=27
4035 IFK1=83THEND7=D7+1:IFD7>3THEND7=4:D8=D8+1:IFD8>27THEND8=27
4036 IFK1=83ANDD7=2THEND7=3
4037 PRINT@191+D9,"     ";:PRINT@223+D9,"     ";:PRINT@255+D9,"     ";
4038 K3=192+D9:FORX=1TO3:PRINT@K3,D$(D7,X):K3=K3+32:NEXT
4039 D9=D8:D6=D7
4040 IFD7=4THEN4060
4050 GOTO4085
4060 K3=192+M9:FORX=1TO3:PRINT@K3,M$(MZ,M6,X):K3=K3+32:NEXT:FORZZ=1TO20:NEXT
4065 K3=225+16384+D8:FORZ=1TO2
4068 Z1=PEEK(K3):IFZ1<>96ANDZ1<>107ANDZ1<>109THEN4090
4070 PRINT@226+D8,"+";
4072 NEXTZ
4082 D7=2
4085 M7=M7+1:IFM7=5THENM7=1
4086 IFRND(0)>.35THENM8=M8-1
4087 PRINT@191+M9,"      ";:PRINT@223+M9,"      ":PRINT@255+M9,"      ";
4088 K3=192+M8:FORX=1TO3:PRINT@K3,M$(MZ,M6,X):K3=K3+32:NEXT:IFM8<D8+2THEN4100
4089 M9=M8:M6=M7:GOTO4020
4090 PRINT@352,;:M$="WELL DONE! YOU GOT THE "+V$(MZ)+".":GOSUB1
4091 P5=INT(V(MZ)*D8):VK=VK+1:WK=WK-1:IFWK<1THENWK=1
4092 PRINT@0,"POINTS FOR KILL =";P5:PK=PK+P5:GOSUB2750
4093 GOSUB2800
4095 RETURN
4100 WK=WK+1
4102 IFWK<5THENPRINT@0,"TRY AGAIN...":GOSUB2760:GOSUB2800:GOTO4002
4103 CLS1
4105 PRINT:PRINT:FORI=1TO3:PRINT"OH DEAR...";
4107 FORJ=1TO50:NEXTJ:NEXTI
4110 PRINT:PRINT"AAAAAAAAAAARGH!"
4120 PRINT"ANOTHER HERO BITES THE DUST."
4125 PRINT:M$="YOU KILLED"+STR$(VK)+" MONSTERS SCORING"+STR$(PK)+" POINTS.":GOSUB1
4130 PRINT"ANOTHER GO? (Y/N)"
4132 A$=INKEY$:IFA$=""THEN4132
4133 IFA$="N"THENEND
4134 IFA$="Y"THENRUN
4135 GOTO4132
5000 REM ******************************
