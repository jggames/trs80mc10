0005 REM WORLDWAR (EARTH WAR GAME)
0006 REM 5-20-78 GH
0008 CLEAR 100
0010 W=3:S$=" SHORE"
0020 DIM A(10,10),C(10,10),N$(10),D$(5),L$(5)
0030 DATA N.AMERICA,AUSTRALIA,S.AMERICA,RUSSIA,JAPAN,AFRICA
0040 DATA KOREA,CHINA,GREENLAND,ENGLAND,TROOPS,PLANES,ARTILLARY
0050 DATA TANKS,MISSLES,NORTH,SOUTH,EAST,WEST
0065 PRINT "COMPUTER WORKING...":PRINT
0067 PRINT :PRINT:PRINT:PRINT
0070 FOR X=1TO10:READN$(X):NEXTX
0080 FOR X=1TO5:READD$(X):NEXTX
0090 FOR X=1TO4:READL$(X):NEXTX
0100 FOR X=1TO10:C(X,1)=INT((5000*RND(1))+5000)*10:NEXTX
0110 FOR X=1TO10:FORJ=2TO5:C(X,J)=INT(50*RND(1))+25:NEXTJ:NEXTX
0120 FOR J=1TO10:FORX=6TO10:LETC(J,X)=INT(4*RND(1))+1:NEXTX:NEXTJ
0130 FOR X=1TO5:FORJ=1TO10:LETA(X,J)=C(X,J):C(X,J)=0:NEXTJ:NEXTX
0140 PRINT
0150 PRINT TAB(22);"W O R L D  P O W E R S  W A R":PRINT
0160 INPUT "DO YOU WANT INSTRUCTIONS (Y/N)";Z$
0170 IF LEFT$(Z$,1)="Y" THEN GOSUB 6000
0200 Y6=Y6+1:PRINT:INPUT"MANEUVER";M
0210 IF M>5THEN200
0220 IF M<1THEN200
0230 ON M GOTO240,400,600,800,1000
0240 INPUT "COUNTRY";R
0242 IF R>10THEN240
0244 IF R<1THEN240
0250 PRINT :PRINT:PRINT:PRINT"STATUS REPORT: ";N$(R)
0260 PRINT :PRINT"DEFENSE","QUANTITY","LOCATION":FORX=1TO5
0270 PRINT D$(X);TAB(13);A(R,X);TAB(28);
0271 IF A(R,X)<>0THENPRINTL$(A(R,(X+5)))+S$:GOTO282
0280 PRINT 
0282 NEXT X:GOTO200
0400 PRINT "ENTER COUNTRIES: ('FROM,' THEN 'TO')"
0405 INPUT F,T
0410 IF F>10THEN400
0420 IF F<1THEN400
0430 IF T>10THEN400
0440 IF T<1THEN400
0450 INPUT "WHAT DEFENSE";D
0460 IF D>5THEN450
0470 IF D<1THEN450
0473 IF F<>T GOTO480
0475 Q=A(F,D)
0477 GOTO 520
0480 INPUT "QUANITY";Q$
0482 IF Q$="ALL"GOTO475
0485 Q=VAL(Q$)
0490 IF A(F,D)>=Q GOTO520
0495 PRINT "YOU ONLY HAVE ";A(F,D);D$(D);" IN ";N$(F):GOTO200
0520 LET A(T,D)=A(T,D)+Q:A(F,D)=A(F,D)-Q
0530 INPUT "WHAT SHORE";L
0540 IF L>4THEN530
0550 IF L<1THEN530
0560 LET A(T,(D+5))=L
0565 IF INT(RND(1)*100)>25THEN570
0567 GOSUB 2000
0570 GOTO 200
0600 PRINT :PRINT:PRINT:PRINTTAB(25);"--- GLOBAL REPORT ---"
0610 PRINT "COUNTRY","ALLIED TROOPS","ENEMY TROOPS","ALLIED POSSESION?"
0620 FOR X=1TO10:PRINTN$(X);TAB(18);A(X,1);TAB(34);C(X,1);TAB(55);
0630 K2=0:FORF2=1TO5:IFC(X,F2)<>0THENK2=K2+1
0635 NEXT F2
0636 IF K2>0THENPRINT"NO":GOTO638
0637 PRINT "YES"
0638 NEXT X
0640 GOTO 200
0800 PRINT "ENTER COUNTRIES: ('FROM,' THEN 'TO')"
0805 INPUT F,T
0810 IF A(F,2)>=10 THEN 820
0812 PRINT "YOU DON'T HAVE ENOUGH PLANES IN ";N$(F):GOTO200
0820 LET A(F,2)=A(F,2)-10
0830 PRINT :PRINT:PRINT:PRINT"SPY MISSION REPORT: ";N$(T):PRINT
0840 IF INT(RND(1)*100)>20THEN860
0850 IF INT(50*RND(1))<26THENPRINT"ALL PLANES LOST AT SEA":GOTO200
0860 PRINT "DEFENSE","QUANTITY","LOCATION":FOR X=1TO5
0870 IF INT(RND(1)*100)<40 THEN NEXT X:GOTO200
0880 PRINT D$(X);TAB(13);C(T,X);TAB(28);
0885 IF C(T,(X+5))<>0THENPRINTL$(C(T,(X+5))):GOTO890
0888 PRINT 
0890 NEXT X:GOTO200
1000 K=0:FORX=1TO10:FORJ=1TO5
1010 IF A(X,J)<>0THEN1040
1020 NEXT J:NEXTX:IFK>0THEN1000
1030 H=0:K=0:FORX=1TO10:FORJ=1TO5
1032 IF C(X,J)<>0THENK=K+1
1033 IF A(X,J)<>0THENH=H+1
1035 NEXT J:NEXTX:IFK=0THEN5000
1036 IF H=0THEN4000
1038 GOTO 200
1040 C=1
1050 IF C(X,C)<>0THEN1080
1060 C=C+1:IFC=6THEN1020
1070 GOTO 1050
1080 IF A(X,(J+5)) <> C(X,(C+5)) GOTO1060
1100 K=K+1:PRINT:PRINT:PRINT:PRINTCHR$(7);"BATTLE IN ";N$(X)
1110 PRINT "ON THE ";L$(A(X,(J+5)))+S$;"!"
1120 PRINT "ENEMY ";D$(C),"ALLIED ";D$(J)
1150 O=C(X,C):O1=A(X,J):E=O:E1=O1
1160 IF J=1THENIFC<>1THEN1360
1162 IF C=1THENIFJ<>1THEN1420
1165 PRINT E,E1
1170 GOSUB 2500
1175 IF E1=0THENE8=1:GOTO1270
1178 IF E=0THENE8=0:GOTO1270
1180 IF E1>E*1.7THENE8=0:GOTO1270
1190 IF E>E1*1.5THENE8=1:GOTO1270
1200 GOTO 1165
1270 PRINT E,E1:PRINT:PRINT"***** A VICTORY FOR THE ";
1280 IF E8=1 PRINT"ENEMY IN ";:GOTO1288
1285 PRINT "ALLIES IN ";
1288 PRINT N$(X);" ON THE ";L$(A(X,(J+5)))+S$
1300 PRINT "TOTAL CASUALTIES: "
1310 PRINT "ENEMY ";D$(C);": ";O-E
1320 PRINT "ALLIED ";D$(J);": ";O1-E1
1330 IF E8=1THENC(X,J)=C(X,J)+A(X,J):A(X,J)=0:GOTO1348
1340 A(X,C)=A(X,C)+C(X,C):C(X,C)=0:A(X,(C+5))=C(X,(C+5))
1348 C(X,(J+5))=A(X,(J+5)):GOTO1020
1360 PRINT E,E1
1370 GOSUB 2500
1375 IF E=0THENE8=0:GOTO1270
1377 IF E1=0THENE8=1:GOTO1270
1380 IF E1/1000>E  E8=0:GOTO1270
1385 IF E>E1*1000THENE8=1:GOTO1270
1390 GOTO 1360
1420 PRINT E,E1
1430 GOSUB 2500
1440 IF E=0THENE8=0:GOTO1270
1442 IF E1=0THENE8=1:GOTO1270
1445 IF E/1000>E1THENE8=1:GOTO1270
1448 IF E1>E*1000THENE8=1:GOTO1270
1450 GOTO 1420
2000 R=INT(RND(1)*100)+1:X2=INT(RND(1)*10)+2
2005 IFD=1THENX2=INT(100*RND(1))+2
2010 IF A(T,D)<=X2THENX2=A(T,D)
2015 A(T,D)=A(T,D)-X2
2020 IF R<33THEN2070
2030 IF R<66THEN2080
2040 PRINT "AMBUSH DURING TRANSPORT!"
2050 PRINT "YOU JUST LOST ";X2;D$(D)
2060 PRINT "TOTAL ARRIVED IN ";N$(T);"= ";A(T,D):RETURN
2070 PRINT "SNIPERS ON THE ";L$(A(T,(D+5)))+S$:GOTO2050
2080 PRINT "VIOLENT SEAS SINK A TRANSPORT SHIP":GOTO2050
2500 E=E-INT((E/3)*RND(1))-2
2510 IF E<=0 THEN E=0
2520 E1=E1-INT((E1/3)*RND(1))-2
2530 IF E1<=0 THEN E1=0
2540 A(X,J)=E1:C(X,C)=E
2550 RETURN 
4000 PRINT :PRINT:PRINT"SORRY, YOU BLEW IT. "
4010 PRINT "ALL YOUR DEFENSES ARE GONE. "PRINT:PRINT:PRINT:GOTO5010
5000 PRINT :PRINT:PRINT:PRINT"CONGRATULATIONS!"
5005 PRINT "YOU HAVE CONQUERED THE WORLD IN ";Y6;"MANEUVERS!"
5010 END
