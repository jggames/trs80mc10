1 CLEAR1000
100 DIMLA(10),SA(10),VW(10),MN(10),MO(10),BV(10),WI(10)
102 DIMVS(10),SF%(10),AR(10),SF$(4),MR(10),GR(10)
110 CLS:PRINT:PRINT" ALTES SPIEL FORTSETZEN ? (J/N)"
111 A$=INKEY$:A$=INKEY$:IFA$="J"THEN53000
112 IFA$<>"N"THEN111
120 CLS:PRINT:INPUT" WIEVIELE SPIELER (2-10) ";AZ:PRINT
122 IFAZ>10ORAZ<2THEN120
124 FORI=1TOAZ
126 PRINT" PLAYER NAME";I;:INPUTNA$(I):NA$(I)=LEFT$(NA$(I),16)
128 LA(I)=34D7/AZ
130 SA(I)=5:VW(I)=4
132 MN(I)=100:MO(I)=2D8
134 BV(I)=1D7:WI(I)=100
136 VS(I)=1D-5:SF%(I)=1
138 MR(I)=100:AR(I)=1D5
140 GR(I)=1
160 NEXT:SP=0
162 SF$(2)="DEMOKRATIE, MARKTW."
164 SF$(3)="DEMOKRATIE, PLANW."
166 SF$(1)="MONARCHIE, MARKTW."
168 SF$(4)="DIKTATUR"
200 SP=SP+1:IFSP>AZTHENSP=1
201 IFLA(SP)<0THENLA(SP)=0
202 IFMO(SP)<0THENMO(SP)=0
203 IFBV(SP)<0THENBV(SP)=0
204 VS(SP)=VS(SP)-RND(0)/10:IFVS(SP)<0THENVS(SP)=1D-5
205 CLS:PRINT:PRINT" SAVE ?  (J/N)"
206 A$=INKEY$:IFA$="J"THEN53500
207 IFA$<>"N"THEN206
208 IFWI(SP)>240THENWI(SP)=240
210 CLS:PRINT"STAAT VON ";NA$(SP)
212 MN(SP)=MN(SP)-RND(8)
214 MR(SP)=MR(SP)-SF%(SP):IFMR(SP)<4THENMR(SP)=4
220 PRINT"STAATSFORM :";SF$(SF%(SP))
222 PRINT
230 PRINTUSING"LAND:    ###########, QKM";LA(SP)
240 PRINTUSING"GELD:    ###########, WCU";MO(SP)
250 PRINTUSING"BEV.:    ###########, MENSCHEN";BV(SP)
260 B1=25-WI(SP)/10+7.5*VS(SP)
270 IFB1<0THENB1=1
280 BD=BV(SP)*INT(B1)
290 PRINTUSING"BEV. BEN. ##########, WCU";BD
300 PRINT
310 INPUT"AUSG. BEV. ";AB
320 INPUT"AUSG. WIRT. ";AW
330 IFAB+AW>MO(SP)THENPRINTCHR$(27)+CHR$(27);:GOTO310
340 CLS
350 T1=INT((BD-AB)/B1)
355 IFT1<0THENTD=0ELSETD=T1
356 T2=INT(RND(VS(SP)*100)/100*BV(SP)):TD=TD+T2
360 PRINTUSING"TODESFAELLE:  ###########,";TD
370 GB=RND(((BV(SP)/10)-BV(SP)*VS)/100)*10
380 PRINTUSING"GEBURTEN   :  ###########,";GB
390 BV(SP)=BV(SP)-TD+GB
400 IFT1>0THENWI(SP)=WI(SP)-RND(ABS(T1/100000))/2+AW/1D7
402 IFT1<0THENWI(SP)=WI(SP)+RND(ABS(T1/100000))/2+AW/1D7
406 IFT1>0THENMR(SP)=MR(SP)-RND(ABS(T1/100000))/2
407 IFT1<0THENMR(SP)=MR(SP)+RND(ABS(T1/100000))/2
410 PRINT
415 IFWI(SP)>200THENWI(SP)=200
416 IFWI(SP)<10THENWI(SP)=10
420 WE=(1+SF%(SP)/8)*(1+RND(0)/2)*WI(SP)*1D6:WE=INT(WE)
430 PRINTUSING"WI.-EINNAHMEN: ##########,";WE
440 MO(SP)=MO(SP)+WE-AB-AW
450 WS=AR(SP)*10:WW=SA(SP)*5+VW(SP)*4+AR(SP)*3
460 PRINTUSING"WEHRSOLD:      ##########,";WS
470 PRINTUSING"WARTUNG WAFFEN:##########,";WW
480 MO(SP)=MO(SP)-WW-WS
485 GOSUB62000
490 IFRND(0)>.1THEN512
492 V=RND(10000):K=RND(0)*10*V
494 CLS:PRINT"EINE AUSLAENDISCHE FIRMA BIETET"
496 PRINT"IHNEN DIE KUENSTLICHE VERGROE-"
498 PRINT"SSERUNG IHRES LANDES UM CA."
500 PRINTUSING"##### QKM AN. ES KOSTET GENAU";V
502 PRINTUSING"###### WCU. NEHMEN SIE AN ?";K:PRINT
504 GOSUB60000:IFTA=-1THEN1000
506 MO(SP)=MO(SP)-K:LA(SP)=LA(SP)+V:MR(SP)=MR(SP)+RND(0)*3
508 V=RND(AZ):IFV=AZTHEN508
510 MO(V)=MO(V)+K:GOTO1000
512 IFRND(0)>.1THEN560
514 CLS:PRINT"EINE IHRER FIRMEN HAT EIN NEUES"
516 PRINT"ARZNEIMITTEL ENTWICKELT, DA IN"
518 PRINT"IHREM LAND EINE SEUCHE TAUSENDE"
520 PRINT"VON MENSCHEN DAHINRAFFT. GEBEN"
524 PRINT"SIE DAS MEDIKAMENT FUER DEN     MARKT FREI ?":PRINT
526 GOSUB60000
528 IFTA=1THEN540
530 IFRND(0)>.4THEN1000
532 ML=INT(RND(0)/10*BV(SP)):BV(SP)=BV(SP)-ML
534 PRINT:PRINTUSING"DIE SEUCHE KOSTET ##########";ML
535 MR(SP)=MR(SP)-RND(10)
536 PRINT"MENSCHEN DAS LEBEN !":GOSUB62000:GOTO1000
540 IFRND(0)<.4THEN550
542 PRINT:PRINT"DAS MEDIKAMENT ERWEIST SICH ALS"
544 PRINT"ERFOLGREICH. ES RETTET IHRE BE-":MR(SP)=MR(SP)+RND(9)
545 PRINT"VOELKERUNG UND BELEBT DIE WIRT- SCHAFT."
546 WI(SP)=WI(SP)+3*RND(0):GOSUB62000:GOTO1000
550 PRINT:PRINT"DAS MEDIKAMENT WAR EIN FLOP."
552 MR(SP)=MR(SP)-RND(4):ML=INT(RND(0)/10*BV(SP))
554 PRINTUSING"########### MENSCHEN STERBEN.";ML:BV(SP)=BV(SP)-ML
556 GOSUB62000:GOTO1000
560 IFRND(0)>.05THEN600
562 CLS:M$="ONE OF YOUR ATOMIC REACTORS IS OUT OF CONTROL. "
563 M$=M$+"A FOREIGN GROUP OF EXPERTS OFFERS YOU HELP. THE COST IS ONLY 10 MILLION $!":GOSUB1
565 PRINT"WOLLEN SIE HILFE ?":PRINT:GOSUB60000
574 IFTA=1THEN586
576 IFRND(0)>.3THEN582
578 PRINT:PRINT"SIE HABEN GLUECK ! IHRE EIGENEN"
579 MR(SP)=MR(SP)+5+RND(10)
580 PRINT"SPEZIALISTEN LOESEN DAS PROBLEM.":GOSUB62000:GOTO1000
581 ML=INT(RND(0)/50*BV(SP))
582 PRINT:PRINTUSING"########## MENSCHEN VERLIEREN";ML
583 MR(SP)=MR(SP)-6:VS(SP)=VS(SP)+RND(0)/100
584 PRINT"BEI DEM UNFALL IHR LEBEN.":GOSUB62000:GOTO1000
586 V=RND(AZ):IFV=SPTHEN586
588 MO(V)=MO(V)+1D7:MO(SP)=MO(SP)-1D7
590 IFRND(0)<.1THEN594
592 PRINT"DAS TEAM KANN IHNEN HELFEN !":GOSUB62000:GOTO1000
594 PRINT:PRINT"ES KLAPPT NICHT !":ML=INT(RND(0)/50*BV(SP))
596 PRINTUSING"######### TOTE !";ML:BV(SP)=BV(SP)-ML
598 GOSUB62000:GOTO1000
600 IFRND(0)<.4ANDGR(SP)=-1THEN602
601 IFRND(0)>.1ORSF%(SP)<3THEN700
602 CLS:PRINT"IN IHREM STAAT FINDET EIN AUF-"
604 PRINT"STAND STATT. WOLLEN SIE IHN VON"
606 PRINT"IHRER ARMEE NIEDERSCHLAGEN      LASSEN ?":GOSUB60000
608 IFTA=1THEN630
610 IFRND(0)<.5THEN620
612 PRINT:PRINT"DIE REBELLIERENDE GRUPPE TOETET"
614 TD=RND(500):PRINTTD;"MITGLIEDER DER REGIERUNG"
616 PRINT"UND GIBT AUF.":BV(SP)=BV(SP)-TD:GOSUB62000:GOTO1000
620 PRINT:PRINT"DIE REBELLIERENDE GRUPPE GIBT   VON SELBST AUF !"
622 GOSUB62000:GOTO1000
630 PRINT:PRINT"DIE ARMEE SCHLAEGT DEN AUFSTAND"
632 PRINT"BLUTIG NIEDER. DABEI KOMMEN"
634 ML=INT(RND(0)/10*BV(SP))
636 PRINTUSING"########## MENSCHEN UMS LEBEN";ML
638 BV(SP)=BV(SP)-ML:GOSUB62000:GOTO1000
700 IFRND(0)>.1THEN720
702 CLS:PRINT"EIN SONDERANGEBOT: 3 ATOMRAKETEN";
704 PRINT"FUER NUR 40 MIO. WCU !"
706 PRINT"KAUFEN SIE ?":PRINT:GOSUB60000:IFTA=-1THEN1000
708 MO(SP)=MO(SP)-4D7:SA(SP)=SA(SP)+3
710 GOTO1000
720 IFRND(0)>.1THEN740
722 CLS:PRINT"EIN SONDERANGEBOT: 5000 SOLDATEN";
724 PRINT"FUER NUR 7.5 MIO. WCU !"
726 PRINT"KAUFEN SIE ?":PRINT:GOSUB60000:IFTA=-1THEN1000
728 MO(SP)=MO(SP)-75D5:AR(SP)=AR(SP)+5000:GOTO1000
740 IFRND(0)>.1THEN820
742 CLS:PRINT"EIN SPION IST ENTTARNT WORDEN."
744 PRINT"SOLL ER FUER SIE ARBEITEN ?":PRINT:GOSUB60000:PRINT
746 IFTA=-1THEN780
748 IFRND(0)<.5THEN760
750 PRINT"ER IST GEFLUECHTET ! EIN SCHWE-"
752 PRINT"RER SCHLAG FUER IHRE WIRTSCHAFT":GOSUB62000
754 WI(SP)=WI(SP)-RND(20):GOTO1000
760 PRINT"ER ARBEITET ERFOLGREICH !":GOSUB62000
762 WI(SP)=WI(SP)+RND(20):GOTO1000
780 IFRND(0)<.2THEN790
782 PRINT"OK. ER WIRD EXEKUTIERT.":GOSUB62000:GOTO1000
790 PRINT"DEM SPION GELANG DIE FLUCHT !":WI(SP)=WI(SP)-RND(15)
792 GOTO1000
820 IFGR(SP)=-1ANDRND(0)<.4THEN822
821 IFMR(SP)>15THEN900
822 CLS:PRINT"DIE BEVOELKERUNG IST SEHR UN-"
824 PRINT"ZUFRIEDEN. BEI EINEM AUFSTAND"
826 PRINT"WIRD FAST DIE GESAMTE REGIERUNG"
828 PRINT"UMGEBRACHT. SOLLEN DIE UNRUHE-"
830 PRINT"STIFTER HINGERICHTET WERDEN ?":PRINT:GOSUB60000:PRINT
832 BV(SP)=BV(SP)-INT(BV(SP)/125)
834 IFTA=1THEN1000
836 PRINT"ES GIBT NOCH VIELE TOTE..."
838 BV(SP)=BV(SP)-INT(BV(SP)/100):GOTO1000
900 IFMR(SP)>50ORRND(0)>.3THEN950
902 CLS:ML=INT(BV(SP)/(RND(15)+10))
904 PRINTUSING"############ AUSREISEANTRAEGE";ML
906 PRINT"WEGEN 'UNZUFRIEDENHEIT MIT DEM"
908 PRINT"SYSTEM' LIEGEN VOR. DUERFEN DIE"
910 PRINT"LEUTE GEHEN ?":PRINT:GOSUB60000
912 IFTA=1THENBV(SP)=BV(SP)-ML:MR(SP)=MR(SP)+RND(5)
914 IFTA=-1THENMR(SP)=MR(SP)-RND(5)
916 GOTO1000
950 IFMR(SP)<130ORRND(0)>.3THEN1000
952 CLS:ML=INT(BV(SP)/(RND(15)+5))
954 PRINTUSING"########## FREMDE MOECHTEN";ML
956 PRINT"INS LAND EINREISEN. LAESST DU   SIE ?":PRINT
958 GOSUB60000:IFTA=1THENBV(SP)=BV(SP)+ML:MR(SP)=MR(SP)+RND(9)
960 IFTA=-1THENMR(SP)=MR(SP)-RND(5)
1000 IFMR(SP)<5THENMR(SP)=5
1005 CLS:PRINT"STAATSAUSGABEN DES LFD. JAHRES"
1007 PRINT"IM STAAT VON ";NA$(SP)
1010 FORI=1TO32:PRINT"=";:NEXT
1020 PRINT:PRINT" 1) INFORMATIONEN"
1022 PRINT" 2) WIRTSCHAFT"
1024 PRINT" 3) BEVOELKERUNG"
1026 PRINT" 4) STAATSFORM"
1028 PRINT" 5) MILITAER"
1030 PRINT" 6) INNERE SICHERHEIT"
1048 PRINT" 7) WEITER"
1050 PRINT:PRINT" BITTE WAEHLEN !
1052 A$=INKEY$:A$=INKEY$:A=VAL(A$)
1054 IFA<1ORA>7THEN1052
1056 IFA=7THEN10000
1058 IFA<>1THEN1100
1060 CLS:PRINT"BERICHT":PRINT"======="
1062 PRINT:PRINTUSING"LAND:          ############,";LA(SP)
1064 PRINTUSING"GELD:          ############,";MO(SP)
1066 PRINTUSING"BEV.:          ############,";BV(SP)
1068 PRINTUSING"ARMEE:       ##############,";AR(SP)
1070 PRINTUSING"STRAT. ATOMWAFFEN:  #######,";SA(SP)
1072 PRINTUSING"VERTEIDIGUNGSWAFFEN:#######,";VW(SP)
1073 PRINTUSING"MILITAERBEREITSCHAFT:   ###%";MN(SP)
1074 PRINTUSING"WIRTSCHAFTSFAKTOR:      ###%";WI(SP)
1078 PRINTUSING"BEVOELKERUNGSMORAL:     ###%";MR(SP)
1080 PRINT:PRINT"STAATSFORM:";SF$(SF%(SP))
1082 GOSUB62000:GOTO1000
1100 IFA<>2THEN1200
1102 CLS:PRINT"WIRTSCHAFT":PRINT"==========":PRINT
1104 PRINT" 1) UMWELTSCHUTZ FOERDERN"
1106 PRINT" 2) PRODUKTIVITAETSERHOEHUNG"
1108 PRINT" 3) MONOPOLISIERUNG"
1110 PRINT" 4) RATIONALISIERUNG"
1112 PRINT" 5) NICHTS MACHEN"
1114 PRINT:PRINT" BITTE WAEHLEN":PRINT
1116 A$=INKEY$:A$=INKEY$:A=VAL(A$)
1118 IFA<1ORA>5THEN1116
1120 IFA=5THEN1000
1122 IFA<>1THEN1132
1123 QQ=RND(10)+5
1124 PRINTUSING" KOSTEN: ########## WCU";MO(SP)/QQ
1126 PRINT" OK ? ";:GOSUB60000:IFTA=-1THEN1000
1128 MO(SP)=MO(SP)-INT(MO(SP)/QQ):MR(SP)=MR(SP)+4
1130 WI(SP)=WI(SP)-5:GOTO1000
1132 IFA<>2THEN1136
1133 SOUND31,1:MR(SP)=MR(SP)-15:IFMR(SP)<5THENMR(SP)=5
1134 WI(SP)=WI(SP)+10:IFWI(SP)>200THENWI(SP)=200
1135 GOTO1000
1136 IFA<>3THEN1140
1138 MR(SP)=MR(SP)+2:WI(SP)=WI(SP)+5:GOTO1133
1140 MR(SP)=MR(SP)-10:WI(SP)=WI(SP)+2:GOTO1133
1200 IFA<>3THEN1300
1201 CLS:PRINT"BEVOELKERUNG":PRINT"============":PRINT
1202 PRINT" 1) SCHULE BAUEN"
1204 PRINT" 2) SPIELBANK BAUEN"
1206 PRINT" 3) EINKAUFSZENTRUM BAUEN"
1208 PRINT" 4) FUSSBALLSTADION BAUEN"
1210 PRINT" 5) STRASSEN BAUEN"
1212 PRINT" 6) NICHTS MACHEN"
1214 PRINT:PRINT" BITTE WAEHLEN":PRINT
1216 A$=INKEY$:A$=INKEY$:A=VAL(A$)
1218 IFA<1ORA>6THEN1216
1220 IFA=6THEN1000
1222 K=MO(SP)/(RND(10)+5):PRINTUSING" KOSTEN: ##########, WCU";K
1224 PRINT" OK ? ";:GOSUB60000
1226 IFTA=-1THEN1000
1228 MO(SP)=MO(SP)-INT(K)
1230 MR(SP)=MR(SP)+5:WI(SP)=WI(SP)+5:SOUND31,1:GOTO1000
1300 IFA<>4THEN1350
1302 CLS:PRINT"STAATSFORM":PRINT"==========":PRINT
1304 FORI=1TO4:PRINTI;") ";SF$(I):NEXT
1306 PRINT:PRINT" BISHER:";SF$(SF%(SP)):PRINT
1308 PRINT" BITTE WAEHLEN"
1310 A$=INKEY$:A$=INKEY$:A=VAL(A$):IFA<1ORA>4THEN1310
1312 IFA=SF%(SP)THEN1000
1314 MR(SP)=MR(SP)-10*A:SF%(SP)=A:IFMR(SP)<5THENMR(SP)=5
1315 SOUND31,1
1316 GOTO1000
1350 IFA<>6THEN1500
1352 CLS:PRINT"INNERE SICHERHEIT":PRINT"=================":PRINT
1354 PRINT" 1) REGIERUNGSFEINDLICHE GRUPPEN    AUFLOESEN"
1356 PRINT" 2) REGIERUNGSFEINDLICHE WIRT-"
1358 PRINT"    SCHAFTSBOSSE FEUERN"
1360 PRINT" 3) GRUNDRECHTE EINSCHRAENKEN"
1362 PRINT" 4) GRUNDRECHTE ZULASSEN"
1364 PRINT" 5) NICHTS MACHEN"
1366 PRINT:PRINT" BITTE WAEHLEN"
1368 A$=INKEY$:A$=INKEY$:A=VAL(A$):IFA<1ORA>5THEN1368
1369 IFA<>5THENSOUND31,1
1370 IFA=5ORA<3THEN1000
1372 IFA=3ANDGR(SP)=1THENGR(SP)=-1:SOUND31,1:MR(SP)=MR(SP)-15
1374 IFA=4ANDGR(SP)=-1THENGR(SP)=1:SOUND31,1:MR(SP)=MR(SP)+10
1376 GOTO1000
1500 CLS:PRINT"MILITAER":PRINT"========":PRINT
1502 PRINT" 1) ATOMRAKETE  (20 MIO. WCU)
1504 PRINT" 2) VERTEIDIGUNG  (15 MIO. WCU)"
1506 PRINT" 3) 1000 SOLDATEN  (5 MIO. WCU)"
1508 PRINT" 4) MANOEVER  (CA. 40 MIO. WCU)"
1510 PRINT" 5) KRIEG"
1512 PRINT" 6) NICHTS MACHEN"
1514 PRINT:PRINT" BITTE WAEHLEN"
1516 A$=INKEY$:A$=INKEY$:A=VAL(A$):IFA<1ORA>6THEN1516
1517 IFA=6THEN1000
1518 IFA=1THENMO(SP)=MO(SP)-2D7:SOUND31,1:SA(SP)=SA(SP)+1
1520 IFA=1THEN1516
1522 IFA=2THENMO(SP)=MO(SP)-15D6:SOUND31,1:VW(SP)=VW(SP)+1
1524 IFA=2THEN1516
1526 IFA=3THENMO(SP)=MO(SP)-5D6:SOUND31,1:AR(SP)=AR(SP)+1000
1528 IFA=3THEN1516
1530 IFA=5THEN2000
1532 CLS:MO(SP)=MO(SP)-4D7:SOUND31,1:PRINT"MANOEVER"
1534 PRINT"========":K=RND(0)*1D7:PRINT
1536 PRINTUSING"KOSTEN:       ############, WCU";4D7+K
1538 F=RND(0)*5D6
1540 PRINTUSING"FLURSCHAEDEN: ############, WCU";F
1542 MN(SP)=MN(SP)+RND(10)
1545 MO(SP)=MO(SP)-K-F:GOSUB62000:GOTO1500
2000 CLS:PRINT"KRIEG":PRINT"=====":PRINT
2002 FORI=1TOAZ:IFI<>SPTHENPRINTI;") ";NA$(I)
2004 NEXT:PRINTAZ+1;") NIEMANDEN":PRINT
2006 PRINT" WEN WOLLEN SIE ANGREIFEN ?"
2008 A$=INKEY$:A$=INKEY$:A=VAL(A$):IFA<1ORA>AZ+1THEN2008
2010 IFA=AZ+1THEN1000
2012 IFA=SPTHEN2008
2020 CLS:PRINTNA$(SP);", SIE HABEN"
2022 PRINTUSING"###### STRATEG. ATOMWAFFEN";SA(SP)
2024 PRINTUSING"###### VERTEIDIGUNGSWAFFEN";VW(SP)
2026 PRINTUSING"############,      SOLDATEN";AR(SP):PRINT
2028 PRINT"WIEVIEL SETZEN SIE EIN ?":PRINT
2030 INPUT"ATOMRAKETEN ";A1:IFA1<0ORA1>SA(SP)PRINTCHR$(27);:GOTO2030
2032 INPUT"VERTEIDIGUNG ";V1:IFV1<0ORV1>VW(SP)PRINTCHR$(27);:GOTO2032
2034 INPUT"SOLDATEN ";S1:IFS1<0ORS1>AR(SP)PRINTCHR$(27);:GOTO2034
2036 CLS:PRINTNA$(A);", SIE HABEN":PRINT
2038 PRINTUSING"###### STRATEG. ATOMWAFFEN";SA(A)
2040 PRINTUSING"###### VERTEIDIGUNGSWAFFEN";VW(A)
2042 PRINTUSING"############,     SOLDATEN";AR(A):PRINT
2044 PRINT"WIEVIEL SETZEN SIE EIN ?":PRINT
2046 INPUT"ATOMRAKETEN ";A2:IFA2<0ORA2>SA(A)PRINTCHR$(27);:GOTO2046
2048 INPUT"VERTEIDIGUNG ";V2:IFV2<0ORV2>VW(A)PRINTCHR$(27);:GOTO2048
2050 INPUT"SOLDATEN ";S2:IFS2<0ORS2>AR(A)PRINTCHR$(27);:GOTO2050
2052 CLS:PRINT"DER KRIEG BEGINNT":PRINT"================="
2054 TA=0:TV=0:LA=0:LV=0
2056 FORI=1TOA1
2058 Q1=RND(MN(SP)):Q2=RND(MN(A))
2060 IFQ1>Q2THENTA=TA+1:VS(A)=VS(A)+RND(0)/5
2062 NEXT
2064 FORI=1TOA2
2066 Q1=RND(MN(A)):Q2=RND(MN(SP))
2068 IFQ1>Q2THENTV=TV+1:VS(SP)=VS(SP)+RND(0)/5
2070 NEXT:GOSUB63000
2072 SA(SP)=SA(SP)-A1:SA(A)=SA(A)-A2
2073 WI(SP)=WI(SP)-TV:WI(A)=WI(A)-TA
2074 IFA1+A2<>0THENLA=LA(A)/(A1+A2)*TA
2076 IFA1+A2<>0THENLV=LA(SP)/(A1+A2)*TV
2078 SS=MN(SP)+MN(A)-10+RND(20)
2080 SA=INT(MN(SP)/SS*S1):SV=INT(MN(A)/SS*S2)
2086 PRINT"          ANGREIFER  VERTEIDIGER";
2088 PRINTUSING"LAND     +#########   +#########";LA;LV;
2090 PRINTUSING"SOLDATEN +#########   +#########";-SA;-SV;
2092 FORI=1TO32:PRINT"=";:NEXT
2094 PRINT"SIEGER IST ";
2096 IFLA>LVTHENPRINTNA$(SP):MR(SP)=MR(SP)+A1+A2:MR(A)=MR(A)-A1-A2
2098 IFLA<LVTHENPRINTNA$(A):MR(A)=MR(A)+A1+A2:MR(SP)=MR(SP)-A1-A2
2100 IFLA=LVTHENPRINT"KEINER"
2110 AR(SP)=AR(SP)-SA:AR(A)=AR(A)-SV
2120 LA(SP)=LA(SP)-LV+LA
2122 LA(A)=LA(A)-LA+LV
2124 MO(SP)=MO(SP)-A1*20
2126 MO(A)=MO(A)-A2*20
2128 VW(A)=VW(A)-V2:VW(SP)=VW(SP)-V1
2200 GOSUB62000
10000 CLS
10005 IFLA(SP)>3D8THEN40000
10010 IFMO(SP)>0ANDBV(SP)>1000ANDAR(SP)>0THEN200
10020 IFMO(SP)>0THEN10100
10022 PRINTNA$(SP);", DAS GELD":PRINT"IST ALLE !!!":PRINT
10024 PRINT"DIE UNO IST ABER BEREIT, DIR ZU"
10026 PRINT"HELFEN. DAS KOSTET DICH DIE"
10028 PRINT"HAELFTE DEINES LANDES !!"
10030 LA(SP)=LA(SP)/2:FORI=1TOAZ
10032 IFI<>SPTHENLA(I)=LA(I)+LA(SP)/(AZ-1)
10034 NEXT:MO(SP)=2D8:GOSUB62000:GOTO10000
10100 IFBV(SP)>1000THEN10200
10102 PRINTNA$(SP);",DEIN VOLK"
10104 PRINT"IST AUSGEROTTET !!"
10106 G1=0:G2=0:FORI=1TOAZ:IFI=SPTHEN10110
10108 IFAR(I)>G1THENG1=AR(I):G2=I
10110 NEXT
10112 PRINTNA$(G2);" MARSCHIERT"
10114 PRINT"IN DEIN LAND EIN. DU WIRST HIN-"
10116 PRINT"GERICHTET."
10117 LA(G2)=LA(G2)+LA(SP)
10118 FORI=SPTOAZ
10120 LA(I)=LA(I+1):SA(I)=SA(I+1):VW(I)=VW(I+1):MN(I)=MN(I+1)
10122 MO(I)=MO(I+1):BV(I)=BV(I+1):WI(I)=WI(I+1):VS(I)=VS(I+1)
10124 SF%(I)=SF%(I+1):NA$(I)=NA$(I+1):AR(I)=AR(I+1):MR(I)=MR(I+1)
10125 GR(I)=GR(I+1)
10126 NEXT
10127 GOSUB62000
10128 AZ=AZ-1:SP=SP-1:IFAZ=1THENSP=G2-1:GOTO40000
10130 GOTO200
10200 PRINTNA$(I);", DU HAST"
10202 PRINT"KEINE SOLDATEN MEHR, UM DEIN"
10204 PRINT"LAND ZU VERTEIDIGEN !":GOTO10106
40000 CLS:PRINT:PRINT"  DER SIEGER DIESES SPIELES"
40002 PRINTTAB(14-LEN(NA$(SP))/2);"IST ";NA$(SP):PRINT
40004 PRINT:PRINT"  IHM GEHOERT DER GROESSTE"
40006 PRINT"  TEIL DER ERDE !!"
40008 PRINT"  HERZLICHEN GLUECKWUNSCH !":PRINT:PRINT
40010 PRINT"  NOCHMAL SPIELEN ?  (J/N)"
40012 FORI=0TO31:POKE28672+I,107:POKE29152+I,107
40014 POKE28672+INT(I/2)*32,107:POKE28703+INT(I/2)*32,107:NEXT
40016 A$=INKEY$:A$=INKEY$:IFA$="J"THENRUNELSEIFA$<>"N"THEN40016
50000 END
53000 GOSUB55000:SP=SP-1:GOTO162
53500 GOSUB54000:GOTO210
54000 CLS:PRINT:PRINT"ready recorder hit enter"
54002 PRINT"recording"
54004 A$=INKEY$:IFA$<>CHR$(13)THEN54004
54010 CLS:PRINT#"WS#",AZ,SP
54020 FORI=1TOAZ
54030 PRINT#"WD@1",GR(I),LA(I),SA(I),VW(I),MN(I),MO(I),BV(I)
54040 PRINT#"WD@2",WI(I),VS(I),NA$(I),AR(I),MR(I),SF%(I)
54050 NEXT:RETURN
55000 CLS:INPUT#"WS#",AZ,SP
55020 FORI=1TOAZ
55030 INPUT#"WD@1",GR(I),LA(I),SA(I),VW(I),MN(I),MO(I),BV(I)
55040 INPUT#"WD@2",WI(I),VS(I),NA$(I),AR(I),MR(I),SF%(I)
55050 NEXT:RETURN
60000 PRINT"TIPPEN SIE ÿß¿¿Àþÿ ODER ÿ€ýû÷€ÿ !":TA=0
60010 A$=INKEY$:A$=INKEY$
60020 IFA$="J"THENTA=1
60030 IFA$="N"THENTA=-1
60040 IFTA=0THEN60010
60050 RETURN
62000 PRINT@480,"ÿþþ€þþÿÿƒíîíƒÿÿÙ¶¶¶Íÿÿþþ€þþÿÿ€¶¶¶¾ÿ";
62002 A$=INKEY$:IFINKEY$=""THEN62002
62004 RETURN
63000 FORI=1TOV1
63002 Q1=RND(MN(SP)*2):Q2=RND(MN(A)*2)
63004 IFQ1>Q2THENTV=TV-1
63006 NEXT:IFTV<0THENTV=0
63008 FORI=1TOV2
63010 Q1=RND(MN(A)*2):Q2=RND(MN(SP)*2)
63012 IFQ1>Q2THENTA=TA-1
63014 NEXT:IFTA<0THENTA=0
63016 RETURN
