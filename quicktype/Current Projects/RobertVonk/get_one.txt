10 rem get one
20 rem from "the pocket computer game book 2" by e schlossberg and j brockman
30 poke 53280,6:poke 53281,6:poke 646,1
40 print"“":print"this game is called 'get one'."
50 print:print"the object is to get a total of 1 after"
60 print"five turns."
70 print:print"enter a decimal number less than 1,"
80 print"containing five different digits (for"
90 print"example: .46235). then by adding,"
100 print"subtracting, multiplying, or dividing"
110 print"with one- or two-digit numbers, try to"
120 print"get a total of 1 after five turns. a"
130 print"'turn' is using a one- or two-digit"
140 print"number and an operation. you may use"
150 print"decimal numbers but only two-digit"
160 print"decimal numbers (for example: .44)."
170 for i=1 to 7:print:next i:print"                              hit a key"
180 get i$:if i$="" then 180
190 print"“":print"ok, choose a decimal number with five"
200 print"different digits";
210 input n$:if left$(n$,1)<>"." then print:print"number must be a decimal.":goto 210
220 if len(mid$(n$,2))=5 then 240
230 print:print"number must have five decimal places.":goto 210
240 f=0:for i=1 to 6:for j=1 to 6
250 if i<>j and mid$(n$,i,1)=mid$(n$,j,1) then f=1
260 next j:next i:if f=0 then 280
270 print:print"digits must all be different.":goto 210
280 n=val(n$):g=0:print"“":print"ok, your number is";n
290 input"enter an operation and a number";i$
300 f=0:n1=val(mid$(i$,2)):o$=left$(i$,1):if mid$(i$,2,1)="." then f=1
310 if f=0 and n1>99 then print:print"one or two digits please.":goto 290
320 if f=1 and len(str$(n1))<>4 then print:print"two digits please.":goto 290
330 if o$="*" or o$="+" or o$="-" or o$="/" then 350
340 print:print"operation must be (+-*/).":goto 290
350 print:print"ok,";n;o$;n1;"makes";
360 if o$="+" then n=n+n1
370 if o$="-" then n=n-n1
380 if o$="*" then n=n*n1
390 if o$="/" then n=n/n1
400 print n:g=g+1:if g=5 or n=1 then 420
410 print"you now have";5-g;"turns left.":print:goto 290
420 if n=1 then 440
430 print:print"hard luck, you didn't make it to 1.":goto 450
440 print:print"well done, you made it to 1!"
450 print:print"would you like to play again (y/n)";
460 input i$:if i$<>"y" and i$<>"n" then print:print"(y) or (n) please.":goto 460
470 if i$="y" then 190
