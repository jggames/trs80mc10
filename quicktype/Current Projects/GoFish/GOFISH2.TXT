0 DIMC1,CC,M$,MC:MC=16384:GOTO10
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
10 DIM PL(13),CO(13),DE(13),GU(13),PO(13),AS(13),SC(1),VI(1),PA(2),DR(2)
20 CLS:PRINT "GO FISH":PRINT "ŒŒŒŒŒŒŒ":PRINTTAB(6)"Š":PRINTTAB(6)"ŠŽŒŽ":PRINTTAB(6)"ŠŒ…†€†"
30 I=RND(-(PEEK(9)*256+PEEK(10))):CA$="A23456789TJQK"
40 RE=4*13:PA=0:PD=RND(13)-1:PD(1)=RND(13):PD(2)=RND(13):DR(1)=-1:DR(2)=1:DR=RND(2)
50 FORI=1TO13:DE(I)=4:PL(I)=0:CO(I)=0:GU(I)=0:PO(I)=0:AS(I)=0:NEXT:V=0:PO=0:I=0:NP=0:NC=0:CN=0
60 FORI=1TO9:GOSUB1250:DE(K)=DE(K)-1:CO(K)=CO(K)+1:GOSUB1250:DE(K)=DE(K)-1:PL(K)=PL(K)+1:NEXT I
160 PRINT:IFVI(0)>0ORVI(1)>0THEN190
170 INPUT "WHAT IS YOUR NAME";NM$:NA$=LEFT$(NM$,6)
180 REM PRINT HAND
190 PRINT "SCORE:  "NA$;SC(0);"  COMPUTER";SC(1)
200 PRINTCHR$(8)RE"CARDS REMAINING"
210 PRINT "YOUR HAND: ";
220 FOR I=1 TO 13
230 IF PL(I)=0 THEN 280
240 FOR J=1 TO PL(I)
250 M$=MID$(CA$,I,1):GOSUB7:PRINT" ";
260 NEXT J
270 REM NEXT CARD
280 NEXT I
290 PRINT
300 REM ASK CARD
310 GOSUB 1100:IFPA=1THEN20
320 S$=NA$
330 INPUT "WHICH CARD DO YOU ASK FOR";C$
340 IF C$="" THEN 310
350 M$=CA$:I$=C$:BB=1:GOSUB9000:CN=FF
360 IFCN=0THENPRINT"SORRY, THAT IS NOT A VALID      CHOICE.":GOTO310
370 IFPL(CN)=0THENPRINT"YOU DO NOT HAVE THAT CARD!":GOTO 310
380 GU(CN)=1
390 IF CO(CN)=0 THEN PRINT S$", GO FISH!":PRINT S$" DRAWS A ";:GOSUB 800:GOSUB 920:GOTO 470
400 V=CO(CN)
410 CO(CN)=0
420 PL(CN)=PL(CN)+V
430 PRINT S$" GETS"V"MORE CARDS."
440 GOSUB 920
450 GOTO 190
460 REM COMPUTER TURN 1
470 S$="COMPUTER"
480 FOR I=1 TO 13
490 AS(I)=0
500 NEXT
510 REM COMPUTER TURN 2
520 GOSUB 1100:IFPA=1THEN20
530 PO=0
540 FOR I=1 TO 13
550 IF (CO(I)>0) AND (GU(I)>0) THEN PO(I)=1:PO=PO+1
560 NEXT
570 IF PO=0 THEN 650
580 REM DRAW GUESS
590 K=RND(13)
600 IF PO(K)=0 THEN 590
610 GU(K)=0
620 AS(K)=1
630 IFCO(K)<>0THEN680
640 REM DRAW RAND
650 PD=PD+DR(DR):IFPD(1)=PDTHENPD=PD+1:IFRND(2)=1THENPD=PD(1)
651 IFPD(2)=PDTHENPD=PD+1:IFRND(2)=1THENPD=PD(2)
652 IFRND(50)=1THENDR=RND(2)
653 IFPD>13THENPD=1:GOTO655
654 IFPD<1THENPD=13
655 K=PD
660 IF CO(K)=0 OR AS(K) THEN 650
670 REM MAKE TURN
680 PRINT S$" WANTS YOUR ";:M$=MID$(CA$,K,1):GOSUB7:PRINT"'S."
690 AS(K)=1
700 IF PL(K)=0 THEN PRINT S$", GO FISH!":PRINT S$" DRAWS A ";:GOSUB 860:GOSUB 1010:GOTO 190
710 V=PL(K)
720 PL(K)=0
730 CO(K)=CO(K)+V
740 PRINT S$" GETS"V"MORE CARDS."
750 GOSUB 1010
760 GOTO 520
770 PRINT "VICTORIES:  "NA$;VI(0);"  IT";VI(1):SC(0)=0:SC(1)=0
775 PRINT:INPUT"PLAY AGAIN (Y/N)";M$:IFLEFT$(M$,1)="Y"THENPA=1:RETURN
780 END
790 REM DRAW CARD P
800 GOSUB 1250
810 M$=MID$(CA$,K,1):GOSUB7:PRINT"."
820 DE(K)=DE(K)-1
830 PL(K)=PL(K)+1
840 RETURN
850 REM DRAW CARD C
860 GOSUB 1250
870 PRINT "CARD."
880 DE(K)=DE(K)-1
890 CO(K)=CO(K)+1
900 RETURN
910 REM CHECK BOOK P
920 FOR I=1 TO 13
930 IF PL(I)<>4 THEN 980
940 PRINT S$" COMPLETES BOOK OF ";:M$=MID$(CA$,I,1):GOSUB7:PRINT"'S."
950 PL(I)=0
960 SC(0)=SC(0)+1
970 REM NEXT LOOP P
980 NEXT I
990 RETURN
1000 REM CHECK BOOK C
1010 FOR I=1 TO 13
1020 IF CO(I)<>4 THEN 1070
1030 PRINT S$" COMPLETES BOOK OF ";:M$=MID$(CA$,I,1):GOSUB7:PRINT"'S."
1040 CO(I)=0
1050 SC(1)=SC(1)+1
1060 REM NEXT LOOP C
1070 NEXT I
1080 RETURN
1090 REM CHECK END
1100 NP=0:NC=0
1110 FOR I=1 TO 13
1120 NP=NP+PL(I)
1130 NC=NC+CO(I)
1140 NEXT I
1150 IF RE=0 OR NP=0 OR NC=0 THEN 1180
1160 RETURN
1170 REM END GAME
1180 PRINT
1190 PRINT "*** GAME OVER! ***"
1200 PRINT
1210 IF SC(0)>SC(1) THEN PRINT NA$" HAS WON.":VI(0)=VI(0)+1:GOTO770
1220 IF SC(0)<SC(1) THEN PRINT "THE COMPUTER HAS WON.":VI(1)=VI(1)+1:GOTO770
1230 PRINT "IT'S A TIE!":VI(0)=VI(0)+1:VI(1)=VI(1)+1:GOTO770
1240 REM DEAL CARD
1250 RE=RE-1:SC=RND(RE+1):FORK=1TO13:SC=SC-DE(K):IFSC<=0THENRETURN
1300 NEXT:STOP
1997 REM INSTR SUBROUTINE
1998 REM INPUT VARIABLE: M$=STRING TO BE SEARCHED,I$=SEARCH TARGET,BB=BEGINING POSITION OF SEARCH
1999 REM OUTPUT VARIABLE:FF=POSITION OF SEARCH TARGET OR ZERO IF NOT FOUND
9000 FF=0:LL=LEN(I$):FORZZ=BBTOLEN(M$):ON1-(MID$(M$,ZZ,LL)=I$)GOTO9010:FF=ZZ:ZZ=255:NEXT:RETURN
9010 NEXT:RETURN
