   10 GOSUB 8000
   20 GOSUB 9000
   25 GOSUB 11000
   30 GOSUB 1000
   31 FOR X=1 TO 3000: NEXT X
   32 VTAB (23): CALL -868: PRINT "NEW CAMPAIGN"
   34 FOR X=1 TO 3000: NEXT X
   50 C= RND (5): IF C=0 THEN 50
   60 FOR N=1 TO C
   70 GOSUB 7000
   80 GOSUB 2000
   90 NEXT N
   95 VTAB (23): CALL -868: PRINT "END OF CAMPAIGN"
   97 FOR X=1 TO 2000: NEXT X
   98 GOSUB 4000
   99 CALL -936
  100 VTAB (3): TAB (10)
  110 PRINT "GAME STATUS"
  120 FOR X=1 TO PN
  125 VTAB (X*5)
  130 PRINT "WARLORD ";N$(N(X),N(X+1)-1)
  135 A=0
  140 FOR Z=1 TO 49
  150 IF O(Z)=X THEN A=A+S(Z)
  160 NEXT Z
  170 PRINT "TROOPS - ";A;" DIVISIONS"
  180 PRINT "CROWNS - ";PC(X);"000"
  190 NEXT X
  192 VTAB (24)
  200 PRINT "GAME TO CONTINUE ";
  210 NL=300: GOSUB 6000
  215 GOSUB 10000
  220 FOR X=1 TO 4000: NEXT X: GOSUB 1000
  230 GOTO 31
  300 END 
  513 S(D)=E- RND (E/2)
 1000 REM  SET UP SCREEN BORDERS
 1010 CALL -936: POKE 50,63: PRINT "   ";
 1020 FOR X=1 TO 7: PRINT X;"    ";: NEXT X
 1030 FOR X=1 TO 22
 1040 IF X MOD 3=0 THEN 1060
 1050 VTAB (X): PRINT " ";: GOTO 1070
 1060 PRINT X/3;
 1070 TAB 38: PRINT "  "
 1080 NEXT X
 1090 POKE 50,255
 1100 REM  LAY OUT SCREEN
 1110 FOR X=1 TO 7
 1120 FOR Y=1 TO 7
 1130 VTAB (X*3)
 1140 TAB (Y*5-2)
 1145 IF T(X*7-7+Y)>1 AND T(X*7-7+Y)<4 THEN TAB (Y*5-1)
 1147 IF T(X*7-7+Y)=5 THEN TAB (Y*5-1)
 1150 PRINT T$(T(X*7-7+Y),T(X*7-7+Y));
 1160 IF P(X*7-7+Y)#0 THEN PRINT P(X*7-7+Y);
 1170 IF O(X*7-7+Y)#0 THEN PRINT O(X*7-7+Y)
 1180 NEXT Y: NEXT X
 1183 TAB 1: VTAB (23)
 1185 PRINT "S-SWAMP,L-LAKE,F-FOREST,C-CASTLE,T-TOWN"
 1187 FOR X=1 TO 1000: NEXT X
 1190 FOR X=1 TO 7: FOR Y=1 TO 7
 1200 IF S(X*7-7+Y)=0 THEN 1230
 1210 VTAB (X*3+1): TAB (Y*5-1)
 1220 PRINT S(X*7-7+Y)
 1230 NEXT Y: NEXT X
 1240 VTAB (23)
 1250 RETURN 
 2000 REM  GET BATTLE ORDERS
 2010 FOR Z=1 TO PN
 2020 VTAB (23): CALL -868
 2021 Y=STR(Z)
 2022 B=0: FOR A=1 TO 49: IF O(A)=Y THEN B=1: NEXT A: IF B#1 THEN 2999
 2030 PRINT "WARLORD ";STR(Z);" ";
 2050 PRINT N$(N(Y),N(Y+1)-1);" MOVE TROOPS (Y/N)";
 2070 NL=2999: GOSUB 6000
 2072 CALL -936: POKE 50,63: FOR X=1 TO 8
 2074 VTAB (2): TAB (X*5-4): PRINT "     "
 2076 VTAB (5): TAB (X*5-4): PRINT "     "
 2078 VTAB (7): TAB (X*5-4): PRINT "     "
 2079 VTAB (23): TAB (X*5-4): PRINT "     "
 2080 NEXT X
 2082 POKE 50,255
 2089 VTAB (3)
 2090 TAB 13: PRINT "BATTLE ORDERS"
 2092 VTAB (7): PRINT "WARLORD ";Y;" ";N$(N(Y),N(Y+1)-1);
 2094 PRINT " CASTLE ";(CA(Y)-1)/7+1;(CA(Y)-1) MOD 7+1
 2100 VTAB (10): PRINT "FROM";: TAB (10): PRINT "TO";: TAB (20): PRINT "NUMBER OF DIVISIONS"
 2110 VTAB (17): PRINT "ENTER 'FROM' AND 'TO' CO-ORDINATES AS "
 2120 PRINT "ONE NUMBER IN THE FORM OF 'VH'"
 2130 PRINT "WHERE 'VH' IS THE VERTICAL - "
 2140 PRINT "HORIZONTAL INDEX IN THE RANGE 11-77"
 2150 REM  GET FROM CO-ORDINATE
 2155 FOR X=1 TO 3: VTAB (12+X): CALL -868: NEXT X
 2160 VTAB (6): CALL -868
 2170 VTAB (12): TAB (1): INPUT C
 2171 A=C:C=(C/10*7)-7+(C MOD 10)
 2172 IF A#0 THEN 2180
 2175 GOSUB 1000: GOTO 2020
 2180 IF A/10<1 OR A/10>7 THEN 2190
 2185 IF A MOD 10>0 AND A MOD 10<8 THEN 2200
 2190 VTAB (6): CALL -1052: CALL -1052: TAB (13)
 2195 PRINT "CO-ORDINATE OUTSIDE RANGE": GOTO 2170
 2200 REM  GET TO CO-ORDINATE
 2205 FOR X=1 TO 3: VTAB (12+X): CALL -868: NEXT X
 2210 VTAB (6): CALL -868
 2220 VTAB (12): TAB (10): INPUT D
 2221 B=D:D=(D/10*7)-7+(D MOD 10)
 2222 IF B#0 THEN 2230
 2225 GOSUB 1000: GOTO 2020
 2230 IF B/10<1 OR B/10>7 THEN 2240
 2235 IF B MOD 10>0 AND B MOD 10<8 THEN 2300
 2240 VTAB (6): CALL -1052: CALL -1052
 2245 CALL -868: TAB (13)
 2250 PRINT "CO-ORDINATE OUTSIDE RANGE"
 2260 GOTO 2220
 2300 REM  GET NUMBER OF DIVISIONS
 2305 FOR X=1 TO 3: VTAB (12+X): CALL -868: NEXT X
 2310 VTAB (6): CALL -868
 2320 VTAB (12): TAB (25): INPUT E
 2330 IF E=0 THEN 2998
 2340 REM  DO IT!
 2350 REM  C=FROM CO-ORD
 2360 REM  D=TO CO-ORD
 2370 REM  E=NUMBER OF DIVISIONS
 2400 IF O(C)=Y THEN 2500
 2410 VTAB (6): CALL -1052: CALL -1052
 2420 PRINT "NO TROOPS AT 'FROM' POSITION"
 2430 GOTO 2170
 2500 REM  CHECK STRENGTH
 2510 IF S(C)>=E THEN 2600
 2520 VTAB (6): CALL -1052: CALL -1052
 2530 PRINT "NOT ENOUGH TROOPS"
 2540 GOTO 2320
 2600 REM  MOVE TO WHERE
 2610 IF T(D)#2 THEN 2700
 2620 VTAB (6): CALL -1052: CALL -1052
 2630 PRINT "INVALID MOVE - LAKE "
 2640 GOTO 2220
 2700 GOSUB 3000: REM  LEGAL MOVE
 2705 IF O(D)#0 AND O(D)#Y THEN 2800
 2710 REM  OWNER OF TO CO-ORD
 2715 REM   OR EMPTY CO-ORD
 2720 S(D)=S(D)+E:S(C)=S(C)-E
 2725 IF S(C)=0 THEN O(C)=0
 2728 O(D)=Y
 2730 VTAB (6): CALL -1052: CALL -1052
 2735 CALL -868
 2740 PRINT "MOVED ! "
 2750 FOR X=1 TO 2000: NEXT X
 2760 GOTO 2998
 2800 GOSUB 5000: REM  BATTLE ENSUES
 2998 GOSUB 1000
 2999 NEXT Z: RETURN 
 3000 REM  CHECK IF REQUESTED
 3010 REM  MOVE IS VALID
 3020 REM  C & D ARE 'FROM' AND
 3030 REM  'TO' VECTOR CO-ORDS.
 3040 REM  A & B ARE INPUTED BOARD
 3050 REM  'MATRIX CO-ORDINATES'
 3060 X= ABS (B/10-A/10)
 3065 IF X>1 THEN 3100
 3070 X= ABS (B MOD 10-A MOD 10)
 3080 IF X>1 THEN 3100
 3090 RETURN 
 3100 POP : VTAB (6): CALL -1052: CALL -1052
 3110 CALL -868: TAB (10): PRINT "ILLEGAL MOVE"
 3120 GOTO 2170
 4000 REM  CALCULATE TAXES AND RAISE
 4010 REM  TROOPS
 4020 REM  CASH  FROM TREASURY ONLY
 4030 REM  MAY PLACE TROOPS ANY WHERE
 4035 REM  ALREADY OCCUPIED BY WARLORD
 4040 CALL -936
 4050 FOR Z=1 TO PN
 4051 CALL -936
 4052 IF O(CA(Z))#Z THEN 4700
 4060 VTAB (3): TAB 12: PRINT "WARLORD ";Z;" ";N$(N(Z),N(Z+1)-1)
 4070 VTAB (5): PRINT "TREASURY BALANCE IS ";PC(Z)
 4080 X=0
 4090 FOR Y=1 TO 49
 4100 IF O(Y)=Z THEN X=X+P(Y)
 4110 NEXT Y
 4120 VTAB (7): PRINT "TAXES RAISED THIS YEAR (000'S) ";X
 4122 C=X
 4125 IF PC(Z)<2 THEN 4500
 4130 VTAB (9): PRINT "DO YOU WISH TO BUY TROOPS FROM TREASURY MONEY ";
 4140 NL=4500: GOSUB 6000
 4150 VTAB (11): CALL -868: PRINT "HOW MANY DIVISIONS ";
 4160 INPUT A
 4170 IF A<=PC(Z)/2 THEN 4200
 4180 PRINT "DIVISIONS COST 2,000 CROWNS EACH - INSUFFICIENT CASH"
 4190 VTAB (10): CALL -868: GOTO 4150
 4200 GOSUB 1000: VTAB (23): CALL -958: PRINT "TROOPS TO WHAT BOARD POSITION ";
 4210 INPUT B
 4240 IF B/10<1 OR B/10>7 THEN 4300
 4250 IF B MOD 10<1 OR B MOD 10>7 THEN 4300
 4260 B=B/10*7-7+B MOD 10
 4265 IF B#25 THEN 4270
 4266 VTAB (23): CALL -1052: CALL -1052: CALL -868: PRINT "NOT VALID TO CAPITAL"
 4268 GOTO 4295
 4270 IF O(B)#Z THEN 4290
 4275 CALL -936: VTAB (3): PRINT "WARLORD ";Z;" ";N$(N(Z),N(Z+1)-1)
 4280 O(B)=Z:S(B)=S(B)+A:PC(Z)=PC(Z)-A*2: GOTO 4500
 4290 VTAB (23): CALL -1052: CALL -1052: CALL -868: PRINT "INVALID MOVE "
 4295 FOR B=1 TO 2000: NEXT B
 4296 GOTO 4200
 4300 VTAB (23): CALL -1052: CALL -1052: CALL -868: PRINT "INVALID BOARD POSITION "
 4310 FOR B=1 TO 2000: NEXT B: GOTO 4200
 4500 REM  PLACE TROOPS FROM THIS YEARS CASH 
 4510 REM  MAY EXCEED THIS YEARS TAXES AS
 4520 REM  THEY WILL NOW BE ADDED TO TREASURY
 4530 PC(Z)=PC(Z)+C
 4540 VTAB (14): CALL -868
 4560 PRINT "CASH TO RAISE CASTLE TROOPS IS ";PC(Z)
 4570 PRINT "DO YOU WISH TO RAISE TROOPS ";
 4580 NL=4700
 4590 GOSUB 6000
 4600 PRINT "HOW MANY DIVISIONS (2000 EACH) ";
 4610 INPUT X
 4620 IF X<=PC(Z)/2 THEN 4650
 4630 CALL -1052: CALL -1052
 4640 PRINT "INSUFFICIENT CASH ": GOTO 4600
 4650 S(CA(Z))=S(CA(Z))+X
 4660 PC(Z)=PC(Z)-X*2
 4700 NEXT Z
 4710 RETURN 
 5000 REM  BATTLE REPORT (CASUALITY LISTS)
 5010 REM  FIRST CALCULATE FACTORS
 5020 AS=E+T(C):DS=S(D)+T(D)
 5030 IF AS>DS THEN 5100
 5040 REM  DEFENDER HAS WON
 5050 S(C)=S(C)-E
 5055 IF S(C)=0 THEN O(C)=0
 5057 IF S(D)=1 THEN 5200
 5060 S(D)=S(D)- RND (S(D)/2)
 5070 GOTO 5200
 5100 REM  ATTACKER WINS
 5110 S(C)=S(C)-E
 5115 IF S(C)=0 THEN O(C)=0
 5117 IF E=1 THEN 5200
 5120 S(D)=E- RND (E/2)
 5130 O(D)=Y
 5200 RETURN 
 6000 REM  ANSWER SUBROUTINE
 6010 INPUT B$
 6015 IF LEN(B$)=0 THEN 6040
 6020 IF ASC( B$)=217 THEN 6070
 6030 IF ASC( B$)=206 THEN 6060
 6040 PRINT "ANSWER MUST BE 'Y' OR 'N'"
 6050 GOTO 6010
 6060 POP : GOTO NL
 6070 RETURN 
 7000 REM  SET RANDOM ORDER PLAYER LIST
 7010 FOR X=1 TO PN
 7020 STR(X)= RND (PN+1)
 7025 IF STR(X)=0 THEN 7020
 7030 IF X=1 THEN 7080
 7040 FOR Y=1 TO (X-1)
 7050 IF STR(Y)=STR(X) THEN STR(X)=0
 7060 NEXT Y
 7070 IF STR(X)=0 THEN 7020
 7080 NEXT X
 7130 RETURN 
 8000 REM  TITLE PAGE
 8001 CALL -936
 8010 VTAB (6)
 8020 FOR X=1 TO 40: PRINT "**";: NEXT X
 8025 VTAB (8)
 8030 TAB 12
 8040 PRINT "W A R L O R D S"
 8050 FOR X=1 TO 40: PRINT "**";: NEXT X
 8060 VTAB (16)
 8065 TAB 5
 8070 PRINT "BY : SPEAKEASY SOFTWARE LTD."
 8080 VTAB (18): TAB 10
 8090 PRINT "P.O. BOX 1220 "
 8100 TAB 10: PRINT "KEMPTVILLE ONT."
 8110 TAB 10: PRINT "CANADA K0G 1J0"
 8115 FOR X=1 TO 2000: NEXT X
 8120 VTAB (22)
 8130 INPUT "HOW MANY PLAYERS (2-4) ",PN
 8135 CALL -932
 8140 IF PN<2 OR PN>4 THEN 8120
 8145 DIM B$(20)
 8146 DIM N(5)
 8147 DIM N$(80)
 8148 DIM PC(PN)
 8149 DIM CA(PN)
 8150 CALL -936: VTAB (3): TAB 8
 8160 PRINT "WARLORDS - PLAYERS LIST"
 8170 VTAB (5): FOR X=1 TO PN
 8180 PRINT "ENTER NAME FOR PLAYER ";X;
 8190 INPUT B$
 8195 IF LEN(B$)=0 THEN 8180
 8200 Y= LEN(N$)
 8210 N$(Y+1)=B$
 8220 N(X)=Y+1
 8225 PC(X)=0
 8230 NEXT X
 8235 N(X)= LEN(N$)+1
 8240 DIM STR(PN)
 8999 RETURN 
 9000 REM  VARIABLE ALLOCATION & INIT
 9001 REM  P=POPULATION
 9002 REM  O=OWNERSHIP
 9003 REM  T=TYPE OF CO-ORDINATE
 9004 REM  S=STRENGTH OF TROOPS
 9005 REM  T$=PRINT DESIGNATOR FOR T(TYPE)
 9006 REM  N$=STRING VAR. FOR PLAYERS NAMES
 9007 REM  B$ POS. IN N$ OF PLAYER NAME
 9008 REM  N POS. IN N$ OF PLAYERS NAME
 9009 REM  PC(PN) PLAYER CASH IN THOUSANDS
 9010 DIM P(49)
 9011 DIM O(49)
 9012 DIM T(49)
 9013 DIM S(49)
 9014 DIM T$(7)
 9020 T$=" LSTFCX"
 9040 FOR X=1 TO 49
 9050 P(X)=0:O(X)=0:T(X)=0:S(X)=0: NEXT X
 9060 REM  INITIALIZE SCREEN MATRIX VALUES
 9070 T(25)=7:P(25)=9
 9080 IF PN=3 THEN 9110
 9090 IF PN=2 THEN 9120
 9100 T(( RND (3)+4)*7+ RND (3)+1)=6
 9110 T(( RND (3)+0)*7+ RND (3)+5)=6
 9120 T(( RND (3)+4)*7+ RND (3)+5)=6
 9130 T(( RND (3)+0)*7+ RND (3)+1)=6
 9140 REM  SET LAKES(2),SWAMPS(3),TOWNS(4) AND THEIR POPULATIONS
 9150 FOR A=2 TO 4
 9160 FOR X=1 TO 4
 9170 Y= RND (49)+1
 9180 IF T(Y)#0 THEN 9170
 9190 T(Y)=A
 9200 IF A#4 THEN 9210:P(Y)=4+ RND (3)
 9210 NEXT X
 9220 NEXT A
 9230 FOR X=1 TO 10
 9240 Y= RND (49)+1
 9250 IF T(Y)#0 THEN 9240
 9260 T(Y)=5
 9270 NEXT X
 9280 FOR X=1 TO 49
 9290 IF T(X)#0 THEN 9330
 9300 T(X)=1
 9310 P(X)=1+ RND (3)
 9320 GOTO 9350
 9330 IF T(X)#6 THEN 9350
 9340 P(X)=5+ RND (5)
 9345 S(X)=9
 9350 NEXT X
 9360 FOR Y=1 TO PN
 9370 FOR X=1 TO 49
 9380 IF T(X)#6 OR O(X)#0 THEN 9390
 9382 CA(Y)=X
 9385 O(X)=Y: GOTO 9400
 9390 NEXT X
 9400 NEXT Y
 9410 RETURN 
10000 REM  FATE !!
10010 X= RND (30)
10020 IF X/5=X/5+X MOD 5 THEN 10030
10025 GOTO 10999
10030 CALL -936: CALL -1052: CALL -1052
10040 VTAB (3): TAB 8: PRINT "FATE !"
10050 IF X#5 THEN 10100
10060 PRINT "PLAGUE THROUGHOUT THE LAND !"
10070 PRINT "EVERY TREASURY LOSES 5,000 CROWNS"
10072 FOR X=1 TO 3000: NEXT X
10080 FOR A=1 TO PN:PC(A)=PC(A)-5: NEXT A
10100 IF X#10 THEN 10200
10110 PRINT "BUMPER CROPS IN THE LAND OF NERD"
10120 PRINT "TREASURIES SWELL BY 10,000 CROWNS "
10122 FOR X=1 TO 3000: NEXT X
10130 FOR A=1 TO PN:PC(A)=PC(A)+10: NEXT A
10200 IF X#15 THEN 10300
10210 PRINT "MAGICIAN AEDIPIOS CASTS CURSE ON WARLORDS"
10220 PRINT "TURNS THEIR GOLD TO BLOOD"
10230 PRINT "TREASURIES WIPED OUT"
10232 FOR X=1 TO 3000: NEXT X
10240 FOR A=1 TO PN:PC(A)=0: NEXT A
10300 IF X#20 THEN 10400
10310 PRINT "BLACK DEATH STRIKES CASTLES"
10320 PRINT "HALF OF CASTLE TROOPS DIE"
10322 FOR X=1 TO 3000: NEXT X
10330 FOR A=1 TO PN:S(CA(A))=S(CA(A))/2: NEXT A
10400 IF X#25 THEN 10999
10410 PRINT "THE PHANTOMS OF TOXIS SOLANIS INVADE "
10420 PRINT "THE CAPITAL, RASCALLION DURING NIGHT"
10430 PRINT "ALL TROOPS MYSTERIOUSLY DISAPPEAR !"
10432 FOR X=1 TO 3000: NEXT X
10440 O(25)=0:S(25)=0
10999 RETURN 
11000 CALL -936: VTAB (3)
11010 PRINT "DO YOU WANT INSTRUCTIONS ?";
11020 NL=11999: GOSUB 6000
11030 REM  INSTRUCTIONS
11040 PRINT : PRINT : PRINT 
11050 PRINT "      INSTRUCTIONS FOR WARLORDS"
11060 PRINT "..............................."
11070 PRINT 
11080 PRINT "THE GAME BOARD..."
11090 PRINT "                 IS 7 BY 7 SPACES"
11100 PRINT "EACH SPACE MAY TAKE THIS FORM"
11110 PRINT : PRINT 
11120 PRINT "     C92"
11130 PRINT "      4"
11140 PRINT 
11150 PRINT "THE C STANDS FOR CASTLE ."
11160 PRINT "THE MIDDLE NUMBER..9..IS POPULATION"
11170 PRINT "THE NUMBER TO THE RIGHT IS THE WARLORD"
11180 PRINT "THE NUMBER 4 UNDERNEATH IS THE "
11190 PRINT "NUMBER OF DIVISIONS AT THIS SPACE."
11200 DIM Q$(10)
11210 PRINT : PRINT "PLEASE HIT RETURN": INPUT Q$
11220 PRINT : PRINT 
11230 PRINT "SO , A SQUARE WITH 6 DIVISIONS"
11240 PRINT "BELONGING TO WARLORD 3 AND IN A SWAMP"
11250 PRINT "WOULD LOOK LIKE THIS..."
11260 PRINT : PRINT 
11270 PRINT "     S3"
11280 PRINT "     6"
11290 PRINT 
11300 PRINT "THERE IS NO POPULATION NUMBER"
11310 PRINT "BECAUSE NO ONE LIVES IN THE SWAMPS"
11320 PRINT : PRINT 
11330 PRINT "IF YOU SEE A NUMBER ALL ALONE LIKE"
11340 PRINT : PRINT "   3"
11350 PRINT : PRINT "IT MEANS THAT IT IS OPEN COUNTRY"
11360 PRINT "AND IT IS NOT OWNED BY ANYONE."
11370 PRINT "IT ALSO DOES NOT HAVE ANY DIVISIONS."
11380 PRINT "THE 3 IS THE POPULATION COUNT."
11390 PRINT : PRINT "PLEASE HIT RETURN": INPUT Q$
11400 PRINT : PRINT "X IS THE CAPITAL."
11401 PRINT 
11410 PRINT "IF YOU SEE ...T4..."
11420 PRINT 
11430 PRINT "THIS IS A TOWN WITH A POPULATION"
11440 PRINT "COUNT OF 4 . IT IS NOT OWNED."
11450 PRINT : PRINT "IF WARLORD 1 OWNED IT"
11460 PRINT "WITH 13 DIVISIONS, IT WOULD SHOW.."
11470 PRINT : PRINT "     T41"
11480 PRINT "     13"
11490 PRINT : PRINT 
11500 PRINT "KEEP IN MIND THAT THE MORE"
11510 PRINT "POPULATION COUNTS YOU OWN"
11520 PRINT "THE MORE TAX MONEY YOU GET !!"
11530 PRINT : PRINT 
11540 PRINT "ON THE OTHER HAND ,BEING IN A "
11550 PRINT "SWAMP OR A FOREST GIVES YOU A"
11560 PRINT "MILITARY ADVANTAGE ."
11570 PRINT 
11580 PRINT "PLEASE HIT RETURN": INPUT Q$
11590 PRINT "YOU WILL GAIN A SENSE OF THE "
11600 PRINT "BATTLE STRENGTHS FROM PLAYING"
11610 PRINT "THE GAME."
11620 PRINT : PRINT 
11630 PRINT "HINT !! PROTECT YOUR CASTLE !"
11640 PRINT "IF YOU LOSE YOUR CASTLE YOU WILL"
11650 PRINT "NOT BE ABLE TO RAISE TAXES."
11660 PRINT "SHOULD YOU SUFFER SUCH A HORRIBLE"
11670 PRINT "LOSS..TRY TO GET IT BACK QUICKLY."
11675 PRINT "ALWAYS KEEP AT LEAST ONE DIVISION"
11676 PRINT "IN YOUR CASTLE !"
11680 PRINT : PRINT 
11700 PRINT "ALSO, IF YOU LOSE ALL OF YOUR"
11800 PRINT "DIVISIONS ,YOU ARE ELIMINATED !"
11810 PRINT : PRINT 
11811 PRINT "A PIECE OF PAPER TO MAKE NOTES"
11812 PRINT "AS YOU PLAY IS RECOMMENDED."
11820 PRINT : PRINT "PLEASE HIT RETURN": INPUT Q$
11830 PRINT "MOVEMENT OF DIVISIONS"
11832 PRINT "-------- -- ---------"
11834 PRINT 
11836 PRINT "YOU WILL BE TOLD WHEN IT IS YOUR"
11838 PRINT "TURN TO MOVE DIVISIONS."
11840 PRINT "ALL PLAYERS WILL HAVE THE SAME"
11842 PRINT "NUMBER OF TURNS PER CAMPAIGN BUT"
11844 PRINT "NOT IN A SET ORDER."
11846 PRINT 
11848 PRINT "YOU GIVE THE CO-ORDINATES OF THE"
11850 PRINT "SPACE YOU WANT TO MOVE DIVISIONS"
11852 PRINT "FROM FIRST...EXAMPLE :23"
11854 PRINT "FOR 2 ROWS DOWN AND 3 COLUMNS ACROSS"
11856 PRINT 
11858 PRINT "HIT RETURN BUTTON AND THEN ENTER"
11860 PRINT "CO-ORDINATES OF THE SPACE YOU"
11862 PRINT "WANT TO MOVE TO..EX: 24"
11864 PRINT 
11866 PRINT "HIT RETURN THEN ENTER NUMBER OF"
11868 PRINT "DIVISIONS YOU WANT TO MOVE."
11870 PRINT : PRINT "PLEASE HIT RETURN": INPUT Q$
11872 PRINT "IF YOU FORGET WHERE YOU ARE"
11874 PRINT "YOU CAN ENTER A ZERO IN THE"
11876 PRINT "FROM OR TO SPACE. IT WILL SHOW"
11878 PRINT "YOU THE BOARD AGAIN AND THEN"
11880 PRINT "ASK FOR YOUR MOVE."
11882 PRINT 
11884 PRINT "KEEP IN MIND THAT YOU CAN ONLY"
11886 PRINT "MOVE TO A TOUCHING SQUARE."
11888 PRINT : PRINT 
11900 PRINT "********************************"
11902 PRINT "ALL IS FAIR IN WARLORDS..YOU CAN"
11904 PRINT "MAKE ANY DIRTY DEALS YOU WANT"
11906 PRINT "TO GANG UP ON SOMEONE AND THEN"
11908 PRINT "DOUBLE-CROSS THE OTHERS."
11910 PRINT "OF COURSE..THEY CAN DO IT TO YOU TOO!"
11912 PRINT "********************************"
11914 PRINT : PRINT : PRINT : PRINT 
11990 PRINT "PLEASE HIT RETURN ": INPUT Q$
11999 RETURN 

