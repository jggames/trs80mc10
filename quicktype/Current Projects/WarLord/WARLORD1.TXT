0 DIMC1,C2,M$:MC=16384:GOTO10
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:?@C1-MC+C2,:RETURN
8 PRINT@480,"PLEASE HIT enter";:INPUTQ$:CLS:RETURN
9 PRINT"                               ";:POKE(PEEK(17024)AND1)*256+PEEK(17025)+MC,32:FORC1=1TO31:PRINTCHR$(8);:NEXT:RETURN
10 GOSUB8000
20 GOSUB9000
25 GOSUB11000
30 GOSUB1000
31 FORX=1TO3000:NEXTX
32 PRINT@480,;:GOSUB9:PRINT"NEW CAMPAIGN";
34 FORX=1TO2500:NEXTX
50 C=RND(5)-1:IFC=0THEN50
60 FORN=1TOC
70 GOSUB7000
80 GOSUB2000
90 NEXTN
95 PRINT@480,;:GOSUB9:PRINT"END OF CAMPAIGN";
97 FORX=1TO2500:NEXTX
98 GOSUB4000
99 CLS
100 VTAB (3): TAB (10)
110 PRINT "GAME STATUS"
120 FOR X=1 TO PN
125 VTAB (X*5)
130 PRINT "WARLORD ";N$(X)
135 A=0
140 FOR Z=1 TO 49
150 IF O(Z)=X THEN A=A+S(Z)
160 NEXT Z
170 PRINT "TROOPS - ";A;" DIVISIONS"
180 PRINT "CROWNS - ";PC(X);"000"
190 NEXT X
192 VTAB (24)
200 PRINT "GAME TO CONTINUE ";
210 GOSUB6000:IFNLTHEN300
215 GOSUB 10000
220 FOR X=1 TO 4000: NEXT X: GOSUB 1000
230 GOTO 31
300 END 
513 S(D)=E-RND(E/2)-1
1000 REM  SET UP SCREEN BORDERS
1010 CLS
1020 M$="   1   2   3   4   5   6   7    ":GOSUB7
1030 FORY=1TO7:X=Y*64-32+MC:POKEX,48+Y:POKEX+31,48+Y:POKEX+32,32:POKEX+63,32:NEXT
1100 REM  LAY OUT SCREEN
1110 FORX=1TO7:FORY=1TO7:PRINT@-32+32*(X*2)+(Y*4-2),;
1145 IFT(X*7-7+Y)>1THENIFT(X*7-7+Y)<4THENPRINT@-32+32*(X*2)+(Y*4-1),;
1147 IFT(X*7-7+Y)=5THENPRINT@-32+32*(X*2)+(Y*4-1),;
1150 PRINTMID$(T$,T(X*7-7+Y),1);
1160 IFP(X*7-7+Y)THENPRINTMID$(STR$(P(X*7-7+Y)),2);
1170 IFO(X*7-7+Y)THENPRINTMID$(STR$(O(X*7-7+Y)),2);
1180 NEXTY:NEXTX
1185 PRINT@480,"S-SWAMP,F-FOREST,C-CASTLE,T-TOW";:POKE16895,ASC("N")
1190 FORX=1TO7:FORY=1TO7:IFS(X*7-7+Y)=.THEN1230
1210 PRINT@-32+32*(X*2+1)+(Y*4-1),MID$(STR$(S(X*7-7+Y)),2);
1230 NEXTY:NEXTX
1240 PRINT@480,;
1250 RETURN
2000 REM GET BATTLE ORDERS
2010 FORZ=1TOPN
2020 PRINT@480,;:GOSUB9
2021 Y=ST(Z)
2022 B=0:FORA=1TO49:IFO(A)=YTHENB=1:NEXTA:IFB<>1THEN2999
2050 PRINTN$(Z)" MOVE TROOPS (Y/N)";
2070 GOSUB6000:IFNLTHEN2999
2072 CLS
2080 PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
2090 PRINT"         BATTLE ORDERS"
2091 PRINT"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ";
2092 PRINT"WARLORD"Y;N$(Y);" CASTLE "STR(INT((CA(Y)-1)/7+1));((CA(Y)-1)-(7*INT((CA(Y)-1)/7))+1
2100 PRINT:PRINT"FROM";:PRINTTAB(10):PRINT"TO";:PRINTTAB(16):PRINT"NUMBER OF DIVISIONS"
2110 PRINT@384,"ENTER from AND to CO-ORDINATES"
2120 PRINT"AS NUMBER IN THE FORM OF vh"
2130 PRINT"WHERE vh IS THE VERTICAL - "
2140 PRINT"HORIZONTAL INDEX (11-77)";
2150 REM  GET FROM CO-ORDINATE
2155 FORX=1TO3:PRINT@32*(5+X),;:GOSUB9:NEXTX
2160 PRINT@32*6,;
2170 INPUTC
2171 A=C:C=(C/10*7)-7+(C-(10*INT(C/10)))
2172 IFATHEN2180
2175 GOSUB1000:GOTO2020
2180 IFA/10<1ORA/10>7THEN2190
2185 IF(A-(10*INT(A/10)))>0AND(A-(10*INT(A/10)))<8THEN2200
2190 PRINT@32*5,;:SOUND100,1:SOUND100,1
2195 PRINT"CO-ORDINATE OUTSIDE RANGE":GOTO2160
2200 REM GET TO CO-ORDINATE
2205 PRINT@32*(5+X)+8,;
2220 INPUTD
2221 B=D:D=(D/10*7)-7+(D-(10*INT(D/10)))
2222 IFBTHEN2230
2225 GOSUB1000:GOTO2020
2230 IFB/10<1ORB/10>7THEN2240
2235 IF(B-(10*INT(B/10)))>0AND(B-(10*INT(B/10)))<8THEN2300
2240 SOUND100,1:SOUND100,1
2245 PRINT@32*5,;:GOSUB9
2250 PRINT"CO-ORDINATE OUTSIDE RANGE"
2260 GOTO2220
2300 REM GET NUMBER OF DIVISIONS
2310 PRINT@32*(5+X)+16,;
2320 INPUTE
2330 IFE=0THEN2998
2340 REM DO IT!
2350 REM C=FROM CO-ORD
2360 REM D=TO CO-ORD
2370 REM E=NUMBER OF DIVISIONS
2400 IFO(C)=YTHEN2500
2410 SOUND100,1:SOUND100,1
2420 PRINT@32*5,"NO TROOPS AT from POSITION"
2430 GOTO2170
 2500 REM  CHECK STRENGTH
 2510 IF S(C)>=E THEN 2600
 2520 VTAB (6): SOUND100,1: SOUND100,1
 2530 PRINT "NOT ENOUGH TROOPS"
 2540 GOTO 2320
 2600 REM  MOVE TO WHERE
 2610 IF T(D)<>2 THEN 2700
 2620 VTAB (6): SOUND100,1: SOUND100,1
 2630 PRINT "INVALID MOVE - LAKE "
 2640 GOTO 2220
 2700 GOSUB 3000: REM  LEGAL MOVE
 2705 IF O(D)<>0 AND O(D)<>Y THEN 2800
 2710 REM  OWNER OF TO CO-ORD
 2715 REM   OR EMPTY CO-ORD
 2720 S(D)=S(D)+E:S(C)=S(C)-E
 2725 IF S(C)=0 THEN O(C)=0
 2728 O(D)=Y
 2730 VTAB (6): SOUND100,1: SOUND100,1
 2735GOSUB9
 2740 PRINT "MOVED ! "
 2750 FOR X=1 TO 2000: NEXT X
 2760 GOTO 2998
 2800 GOSUB 5000: REM  BATTLE ENSUES
 2998 GOSUB 1000
 2999 NEXT Z: RETURN 
 3000 REM  CHECK IF REQUESTED
 3010 REM  MOVE IS VALID
 3020 REM  C & D ARE 'FROM' AND
 3030 REM  'TO' VECTOR CO-ORDS.
 3040 REM  A & B ARE INPUTED BOARD
 3050 REM  'MATRIX CO-ORDINATES'
 3060 X= ABS (B/10-A/10)
 3065 IF X>1 THEN 3100
 3070 X=ABS((B-(10*INT(B/10)))-(A-(10*INT(A/10))))
 3080 IF X>1 THEN 3100
 3090 RETURN 
 3100 POP : VTAB (6): SOUND100,1: SOUND100,1
 3110GOSUB9: TAB (10): PRINT "ILLEGAL MOVE"
 3120 GOTO 2170
 4000 REM  CALCULATE TAXES AND RAISE
 4010 REM  TROOPS
 4020 REM  CASH  FROM TREASURY ONLY
 4030 REM  MAY PLACE TROOPS ANY WHERE
 4035 REM  ALREADY OCCUPIED BY WARLORD
 4040 CLS
 4050 FOR Z=1 TO PN
 4051 CLS
 4052 IF O(CA(Z))<>Z THEN 4700
 4060 VTAB (3): TAB 12: PRINT N$(Z)
 4070 VTAB (5): PRINT "TREASURY BALANCE IS ";PC(Z)
 4080 X=0
 4090 FOR Y=1 TO 49
 4100 IF O(Y)=Z THEN X=X+P(Y)
 4110 NEXT Y
 4120 VTAB (7): PRINT "TAXES RAISED THIS YEAR (000'S) ";X
 4122 C=X
 4125 IF PC(Z)<2 THEN 4500
 4130 VTAB (9): PRINT "DO YOU WISH TO BUY TROOPS FROM TREASURY MONEY ";
 4140 GOSUB6000:IFNLTHEN4500
 4150 VTAB (11):GOSUB9: PRINT "HOW MANY DIVISIONS ";
 4160 INPUT A
 4170 IF A<=PC(Z)/2 THEN 4200
 4180 PRINT "DIVISIONS COST 2,000 CROWNS EACH - INSUFFICIENT CASH"
 4190 VTAB (10):GOSUB9: GOTO 4150
 4200 GOSUB 1000: PRINT@480,;: CALL -958: PRINT "TROOPS TO WHAT BOARD POSITION ";
 4210 INPUT B
 4240 IF B/10<1 OR B/10>7 THEN 4300
4250 IF(B-(10*INT(B/10)))<1OR(B-(10*INT(B/10)))>7THEN4300
4260 B=B/10*7-7+(B-(10*INT(B/10)))
 4265 IF B<>25 THEN 4270
 4266 PRINT@480,;: SOUND100,1: SOUND100,1:GOSUB9: PRINT "NOT VALID TO CAPITAL"
 4268 GOTO 4295
 4270 IF O(B)<>Z THEN 4290
 4275 CLS: VTAB (3): PRINT N$(Z)
 4280 O(B)=Z:S(B)=S(B)+A:PC(Z)=PC(Z)-A*2: GOTO 4500
 4290 PRINT@480,;: SOUND100,1: SOUND100,1:GOSUB9: PRINT "INVALID MOVE "
 4295 FOR B=1 TO 2000: NEXT B
 4296 GOTO 4200
 4300 PRINT@480,;: SOUND100,1: SOUND100,1:GOSUB9: PRINT "INVALID BOARD POSITION "
 4310 FOR B=1 TO 2000: NEXT B: GOTO 4200
 4500 REM  PLACE TROOPS FROM THIS YEARS CASH 
 4510 REM  MAY EXCEED THIS YEARS TAXES AS
 4520 REM  THEY WILL NOW BE ADDED TO TREASURY
 4530 PC(Z)=PC(Z)+C
 4540 VTAB (14):GOSUB9
 4560 PRINT "CASH TO RAISE CASTLE TROOPS IS ";PC(Z)
 4570 PRINT "DO YOU WISH TO RAISE TROOPS ";
 4590 GOSUB6000:IFNLTHEN4700
 4600 PRINT "HOW MANY DIVISIONS (2000 EACH) ";
 4610 INPUT X
 4620 IF X<=PC(Z)/2 THEN 4650
 4630 SOUND100,1: SOUND100,1
 4640 PRINT "INSUFFICIENT CASH ": GOTO 4600
 4650 S(CA(Z))=S(CA(Z))+X
 4660 PC(Z)=PC(Z)-X*2
 4700 NEXT Z
 4710 RETURN 
 5000 REM  BATTLE REPORT (CASUALITY LISTS)
 5010 REM  FIRST CALCULATE FACTORS
 5020 AS=E+T(C):DS=S(D)+T(D)
 5030 IF AS>DS THEN 5100
 5040 REM  DEFENDER HAS WON
 5050 S(C)=S(C)-E
 5055 IF S(C)=0 THEN O(C)=0
 5057 IF S(D)=1 THEN 5200
 5060 S(D)=S(D)-RND(S(D)/2)-1
 5070 GOTO 5200
 5100 REM ATTACKER WINS
 5110 S(C)=S(C)-E
 5115 IF S(C)=0 THEN O(C)=0
 5117 IF E=1 THEN 5200
 5120 S(D)=E-RND(E/2)-1
 5130 O(D)=Y
 5200 RETURN 
 6000 REM ANSWER SUBROUTINE
 6010 INPUT B$:NL=0
 6020 IFLEFT$(B$,1)="Y"THEN6070
 6030 IFLEFT$(B$,1)="N"THENNL=1:GOTO6070
 6040 PRINT "ANSWER MUST BE 'Y' OR 'N'"
 6050 GOTO 6010
 6070 RETURN 
 7000 REM SET RANDOM ORDER PLAYER LIST
 7010 FORX=1TOPN
 7020 ST(X)=RND(PN+1)-1
 7025 IFST(X)=.THEN7020
 7030 IFX=1THEN7080
 7040 FORY=1TOX-1
 7050 IFST(Y)=ST(X)THENST(X)=.
 7060 NEXTY
 7070 IFST(X)=.THEN7020
 7080 NEXTX:RETURN 
 8000 REM TITLE PAGE
 8001 CLS
 8020 FORX=1TO32:PRINT"**";:NEXTX:PRINT
 8040 PRINTTAB(9)"W A R L O R D S":PRINT
 8050 FORX=1TO32:PRINT"**";:NEXTX
 8060 PRINT:PRINT
 8070 PRINTTAB(5)"BY: SPEAKEASY SOFTWARE"
 8090 PRINTTAB(9)"P.O. BOX 1220 "
 8100 PRINTTAB(9)"KEMPTVILLE ONT."
 8110 PRINTTAB(9)"CANADA K0G 1J0"
 8115 FORX=1TO4000:NEXTX
 8120 PRINT@480,;
 8130 INPUT"HOW MANY PLAYERS (2-4)";PN
 8135 CLS
 8140 IF PN<2 OR PN>4 THEN 8120
 8145 DIM B$(20)
 8147 DIM N$(4)
 8148 DIM PC(PN)
 8149 DIM CA(PN)
 8150 CLS
 8160 PRINT"WARLORDS - PLAYERS LIST"
 8170 FORX=1TOPN
 8180 PRINT"ENTER NAME FOR PLAYER"STR$(X);
 8190 INPUTB$
 8195 IFLEN(B$)=0THEN8180
 8210 N$(X)=LEFT$(B$,6)
 8225 PC(X)=0
 8230 NEXTX
 8240 DIMST(PN)
 8999 RETURN 
9000 REM VARIABLE ALLOCATION & INIT
9001 REM P=POPULATION
9002 REM O=OWNERSHIP
9003 REM T=TYPE OF CO-ORDINATE
9004 REM S=STRENGTH OF TROOPS
9005 REM T$=PRINT DESIGNATOR FOR T(TYPE)
9006 REM N$=STRING VAR. FOR PLAYERS NAMES
9007 REM B$ POS. IN N$ OF PLAYER NAME
9009 REM PC(PN) PLAYER CASH IN THOUSANDS
9010 DIMP(49),O(49),T(49),S(49),T$(7)
9020 T$=" LSTFCX"
9040 FORX=1TO49
9050 P(X)=0:O(X)=0:T(X)=0:S(X)=0:NEXTX
9060 REM INITIALIZE SCREEN MATRIX VALUES
9070 T(25)=7:P(25)=9
9080 IFPN=3THEN9110
9090 IFPN=2THEN9120
9100 T((RND(3)-1+4)*7+RND(3)-1+1)=6
9110 T((RND(3)-1+0)*7+RND(3)-1+5)=6
9120 T((RND(3)-1+4)*7+RND(3)-1+5)=6
9130 T((RND(3)-1+0)*7+RND(3)-1+1)=6
9140 REM SET LAKES(2),SWAMPS(3),TOWNS(4) AND THEIR POPULATIONS
9150 FORA=2TO4
9160 FORX=1TO4
9170 Y=RND(49)-1+1
9180 IFT(Y)THEN9170
9190 T(Y)=A
9200 IFA<>4THEN9210
9201 P(Y)=4+RND(3)-1
9210 NEXTX
9220 NEXTA
9230 FORX=1TO10
9240 Y=RND(49)
9250 IFT(Y)THEN9240
9260 T(Y)=5
9270 NEXTX
9280 FORX=1TO49
9290 IFT(X)THEN9330
9300 T(X)=1
9310 P(X)=1+RND(3)-1
9320 GOTO9350
9330 IFT(X)<>6THEN9350
9340 P(X)=5+RND(5)-1
9345 S(X)=9
9350 NEXT X
9360 FORY=1TOPN
9370 FORX=1TO49
9380 IFT(X)<>6ORO(X)<>0THEN9390
9382 CA(Y)=X
9385 O(X)=Y:GOTO9400
9390 NEXTX
9400 NEXTY
9410 RETURN 
 10000 REM FATE !!
 10010 X=RND(30)-1-29
10020 IFX/5=((X/5+X)-(5*INT((X/5+X)/5)))THEN10030
 10025 GOTO 10999
 10030 CLS: SOUND100,1: SOUND100,1
 10040 VTAB (3): TAB 8: PRINT "FATE !"
 10050 IF X<>5 THEN 10100
 10060 PRINT "PLAGUE THROUGHOUT THE LAND !"
 10070 PRINT "EVERY TREASURY LOSES 5,000 CROWNS"
 10072 FOR X=1 TO 3000: NEXT X
 10080 FOR A=1 TO PN:PC(A)=PC(A)-5: NEXT A
 10100 IF X<>10 THEN 10200
 10110 PRINT "BUMPER CROPS IN THE LAND OF NERD"
 10120 PRINT "TREASURIES SWELL BY 10,000 CROWNS "
 10122 FOR X=1 TO 3000: NEXT X
 10130 FOR A=1 TO PN:PC(A)=PC(A)+10: NEXT A
 10200 IF X<>15 THEN 10300
 10210 PRINT "MAGICIAN AEDIPIOS CASTS CURSE ON WARLORDS"
 10220 PRINT "TURNS THEIR GOLD TO BLOOD"
 10230 PRINT "TREASURIES WIPED OUT"
 10232 FOR X=1 TO 3000: NEXT X
 10240 FOR A=1 TO PN:PC(A)=0: NEXT A
 10300 IF X<>20 THEN 10400
 10310 PRINT "BLACK DEATH STRIKES CASTLES"
 10320 PRINT "HALF OF CASTLE TROOPS DIE"
 10322 FOR X=1 TO 3000: NEXT X
 10330 FOR A=1 TO PN:S(CA(A))=S(CA(A))/2: NEXT A
 10400 IF X<>25 THEN 10999
 10410 PRINT "THE PHANTOMS OF TOXIS SOLANIS INVADE "
 10420 PRINT "THE CAPITAL, RASCALLION DURING NIGHT"
 10430 PRINT "ALL TROOPS MYSTERIOUSLY DISAPPEAR !"
 10432 FOR X=1 TO 3000: NEXT X
 10440 O(25)=0:S(25)=0
 10999 RETURN 
11000 CLS
11010 PRINT "DO YOU WANT INSTRUCTIONS";
11020 GOSUB6000:IFNLTHEN11999
11030 REM  INSTRUCTIONS
11040 CLS 
11050 PRINT "   INSTRUCTIONS FOR WARLORDS"
11060 PRINT "   ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ":PRINT
11080 PRINT "THE GAME BOARD IS 7 BY 7 SPACES.";
11100 PRINT "EACH SPACE MAY TAKE THIS FORM:"
11110 PRINT
11120 PRINT "     C92"
11130 PRINT "      4"
11140 PRINT 
11150 PRINT "THE C STANDS FOR CASTLE."
11160 PRINT "THE MIDDLE NUMBER IS POPULATION.";
11170 PRINT "THE RIGHT NUMBER IS THE WARLORD.";
11180 PRINT "THE NUMBER 4 UNDERNEATH IS THE"
11190 PRINT "NUMBER OF DIVISIONS."
11200 DIM Q$(10)
11210 GOSUB8
11230 PRINT "SO, A SQUARE WITH 6 DIVISIONS"
11240 PRINT "BELONGING TO WARLORD 3 AND IN"
11250 PRINT "A SWAMP WOULD LOOK LIKE THIS:"
11260 PRINT
11270 PRINT "     S3"
11280 PRINT "     6"
11290 PRINT 
11300 PRINT "THERE IS NO POPULATION NUMBER"
11310 PRINT "BECAUSE NO ONE LIVES IN THE"
11320 PRINT "SWAMPS.":GOSUB8
11330 PRINT "IF YOU SEE A NUMBER ALL ALONE:"
11340 PRINT:PRINT "   3":PRINT
11350 PRINT "IT MEANS IT IS OPEN COUNTRY."
11370 PRINT "IT ALSO DOES NOT HAVE ANY"
11380 PRINT "DIVISIONS. THE 3 IS THE":PRINT"POPULATION."
11390 GOSUB8
11400 PRINT "X IS THE CAPITAL. IF YOU SEE"
11410 PRINT "T4 THIS IS A TOWN WITH A"
11440 PRINT "POPULATION OF 4. IF WARLORD 1"
11450 PRINT "OWNED IT WITH 13 DIVISIONS,"
11470 PRINT "IT WOULD SHOW:":PRINT:PRINT"     T41"
11480 PRINT "     13"
11490 GOSUB8
11500 PRINT "KEEP IN MIND THAT THE MORE"
11510 PRINT "POPULATION COUNTS YOU OWN"
11520 PRINT "THE MORE TAX MONEY YOU GET!"
11530 PRINT 
11540 PRINT "ON THE OTHER HAND, BEING IN A"
11550 PRINT "SWAMP OR A FOREST GIVES YOU A"
11560 PRINT "MILITARY ADVANTAGE."
11590 PRINT "YOU WILL GAIN A SENSE OF THE "
11600 PRINT "BATTLE STRENGTHS FROM PLAYING"
11610 PRINT "THE GAME."
11620 GOSUB8
11630 PRINT "HINT! PROTECT YOUR CASTLE!"
11640 PRINT "IF YOU LOSE YOUR CASTLE YOU"
11650 PRINT "WILL NOT BE ABLE TO RAISE TAXES.";
11660 PRINT "SHOULD YOU SUFFER SUCH A LOSS,"
11670 PRINT "TRY TO GET IT BACK QUICKLY."
11675 PRINT "ALWAYS KEEP AT LEAST ONE"
11676 PRINT "DIVISION IN YOUR CASTLE!"
11700 PRINT "ALSO, IF YOU LOSE ALL OF YOUR"
11800 PRINT "DIVISIONS, YOU ARE ELIMINATED!"
11811 PRINT "A PIECE OF PAPER TO MAKE NOTES"
11812 PRINT "AS YOU PLAY IS RECOMMENDED."
11820 GOSUB8
11830 PRINT "     MOVEMENT OF DIVISIONS"
11832 PRINT "     ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
11836 PRINT "YOU WILL BE TOLD WHEN IT IS"
11838 PRINT "YOUR TURN TO MOVE DIVISIONS."
11840 PRINT "ALL PLAYERS WILL HAVE THE SAME"
11842 PRINT "NUMBER OF TURNS PER CAMPAIGN"
11844 PRINT "BUT NOT IN A SET ORDER."
11848 PRINT "ENTER THE CO-ORDINATES OF"
11850 PRINT "THE SPACE YOU WANT TO MOVE"
11852 PRINT "FROM FIRST... EXAMPLE: 23"
11854 PRINT "FOR 2 ROWS DOWN AND 3 COLUMNS"
11856 PRINT "ACROSS. DO THE SAME FOR THE"
11860 PRINT "SPACE YOU WANT TO MOVE TO."
11861 PRINT "THEN ENTER THE NUMBER OF"
11868 PRINT "DIVISIONS YOU WANT TO MOVE."
11870 GOSUB8
11872 PRINT "IF YOU FORGET WHERE YOU ARE"
11874 PRINT "YOU CAN ENTER A ZERO IN THE"
11876 PRINT "from OR to SPACE AND IT WILL"
11878 PRINT "SHOW YOU THE BOARD."
11882 PRINT 
11884 PRINT "KEEP IN MIND THAT YOU CAN ONLY"
11886 PRINT "MOVE TO A TOUCHING SQUARE."
11888 PRINT
11900 PRINT "********************************";
11902 PRINT "ALL IS FAIR IN WARLORDS. YOU CAN";
11904 PRINT "MAKE ANY DIRTY DEALS YOU WANT"
11906 PRINT "TO GANG UP ON SOMEONE AND THEN"
11908 PRINT "DOUBLE-CROSS THE OTHERS. OF"
11910 PRINT "COURSE THEY CAN DO THIS TOO!"
11912 PRINT "********************************";
11990 GOSUB8
11999 RETURN
