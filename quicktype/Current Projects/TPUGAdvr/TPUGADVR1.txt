0 CLS:CLEAR300:DIMCC,ZZ,M$,I$:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

9000 R9=6
9010 DIMR$(R9),R(R9,3)
9020 FORI=1TOR9
9030 READR$(I)
9040 FORJ=0TO3
9050 READR(I,J)
9060 NEXT:NEXT
9070 DATAON THE VILLAGE GREEN,3,0,4,2
9080 DATAIN A ONE-ROOM SCHOOL,0,0,1,6
9090 DATAIN A RUSTIC CHURCH,0,1,0,0
9100 DATAIN THE BANK,0,0,0,1
9110 DATAIN THE SAFE,4,0,0,0
9120 DATAIN A DUSTY STOREROOM,0,0,2,0

9500 N9=13
9510 DIMO$(N9),L(N9),A(N9),N$(N9)
9520 FORI=1TON9
9530 READO$(I),L(I),A(I),N$(I)
9540 NEXT
9550 DATAA PIECE OF CHALK,2,2,CHA
9560 DATAA HUGE OAK TREE,1,0,TRE
9570 DATAA SIGN,6,2,SIG
9580 DATAA BRASS KEY,0,2,KEY
9590 DATAA BAG OF COINS,5,3,COI
9600 DATAWOODEN PEWS,3,1,PEW
9610 DATAAN ALTAR,3,1,ALT
9620 DATAA LONG WHITE CANDLE,3,2,CAN
9630 DATAA SHOVEL,6,2,SHO
9640 DATAA BOX OF MATCHES,0,2,MAT
9650 DATAGOLD NUGGETS,0,3,GOL
9660 DATAA LARGE WOODEN DESK,2,1,DES
9670 DATAA BIG SAFE IN THE SOUTH WALL,4,0,SAF

9900 V9=23
9910 DIMV$(V9)
9920 FORI=1TOV9
9930 READV$(I):NEXT
9940 DATAN,NOR,S,SOU,E,EAS,W,WES
9950 DATAGO,INV,SCO,LOO,EXA
9960 DATATAK,GET,PIC,DRO,PUT
9970 DATAREA,OPE,DIG,LIG,QUI

9450 DIMD$(3)
9460 FORI=0TO3:READD$(I):NEXT

9470 DATANORTH,SOUTH,EAST,WEST
9990 R=1

9999 RETURN

100 GOSUB9000

200 CLS
210 IFR=6ANDCA=0THENPRINT"IT'S TOO DARK TO SEE ANYTHING.":GOTO300
220 M$="YOU'RE "+R$(R)+".":GOSUB1
230 PRINT
240 V$="YOU SEE "
250 FORI=1TON9:IFL(I)=RTHENM$=V$+O$(I):GOSUB1:V$="        "
260 NEXT

300 PRINT
310 M$="YOU CAN GO: "
320 FORI=0TO3
330 IFR(R,I)>0THENM$=M$+D$(I)+" "
340 NEXT:GOSUB1

400 PRINT:PRINT"WHAT NOW";
410 INPUTA$:PRINT
420 N$="":V$="":V=0:N=0:J=0
430 L=LEN(A$):FORI=1TOL
440 IFMID$(A$,I,1)=" "THENV$=LEFT$(A$,I-1):J=I:I=L
450 NEXT:IFJ=0THENV$=A$:GOTO550
460 FORI=LTOJSTEP-1
470 IFMID$(A$,I,1)=" "THENN$=MID$(A$,I+1):I=J
480 NEXT

500 T$=LEFT$(N$,3)
510 FORI=1TON9
520 IFT$=N$(I)THENN=I:I=N9
530 NEXT
550 T$=LEFT$(V$,3)
560 FORI=1TOV9
570 IFT$=V$(I)THENV=I:I=V9
580 NEXT

600 IFV=0THENM$="I DON'T KNOW WHAT "+V$+" MEANS.":GOSUB1:GOTO400
610 IFV<9THEN1200
620 ONV-8GOTO1000,1400,1600,1800,1800
630 ONV-13GOTO2200,2200,2200,2400,2400
640 ONV-18GOTO2600,2800,3000,3200,3400

1000 IFN$=""THENPRINT"GO WHERE?":GOTO400
1010 IFN=0THENV$=N$:N$="":GOTO550
1020 PRINT"USE COMPASS DIRECTIONS.":GOTO400

1200 V=(V-L)/2:I=R(R,V)
1210 IFITHENR=I:GOTO200
1220 PRINT"YOU CAN'T GO THAT WAY.":GOTO400

1400 PRINT"YOU ARE CARRYING:"
1410 Z=0:FORI=1TON9
1420 IFL(I)=-1THENM$="  "+O$(I):GOSUB1:Z=Z+1
1430 NEXT
1440 IFZ=0THENPRINT"  NOTHING"
1450 GOTO400

1600 GOSUB1610:GOTO400
1610 J=0:FORI=1TON9
1620 IFL(I)=6ANDA(I)=3THENJ=J+10
1630 NEXT
1640 M$="YOU HAVE SCORED"+STR$(J)+" POINTS OUT OF 20.":GOSUB1
1650 IFJ<20THENRETURN
1660 PRINT:PRINT"WELL DONE!":END

1800 IFN=0THEN200
1810 GOSUB930:IFNXTHEN400
1820 IFN=6ANDL(4)=0THENL(4)=R:PRINT"YOU FIND A KEY.":GOTO400
1830 IFN=12ANDL(10)=0THENL(10)=R:PRINT"SOMETHING'S THERE.":GOTO400
1840 IFN=8THENM$="IT IS "+MID$("UNLIT",CA*2+1)+".":GOUSUB1:GOTO400
1850 IFN=9THENPRINT"IT'S RUSTY.":GOTO400
1995 M$="IT'S JUST "+O$(N)+".":GOSUB1:GOTO400

930 NX=0:I=L(N)
940 IFI<>RANDI<>-1THENPRINT"I DON'T SEE IT HERE.":NX=1
950 RETURN

2200 IFN=0THEN900
2210 IFL(N)=-1THENPRINT"YOU'VE ALREADY GOT IT!":GOTO400
2220 GOSUB930:IFNXTHEN400
2230 IFA(N)=0THENPRINT"DON'T BE SILLY.":GOTO400
2240 IFA(N)=1THENPRINT"IT'S TOO HEAVY.":GOTO400
2250 IFZ>3THENPRINT"YOUR HANDS ARE FULL.":GOTO400
2260 Z=Z+1:L(N)=-1
2270 PRINT"OK":GOTO400

900 IFN$=""THENM$="YOU NEED TO SAY WHAT TO "+V$+".":GOSUB1:GOTO400
910 M$="I DON'T KNOW WHAT A "+N$+" IS.":GOSUB1:GOTO400

2400 IFN=0THEN900
2410 IFL(N)>-1THENM$="YOU HAVE NO "+N$+".":GOSUB1:GOTO400
2420 L(N)=R:Z=Z-1
2430 IFN=8THENCA=0
2440 PRINT"OK":GOTO400

2600 IFN=0THEN900
2610 GOSUB930:IFNXTHEN400
2620 IFN=3THENM$="IT SAYS: LEAVE TREASURE HERE.":GOSUB1:GOTO400
2700 PRINT"THERE'S NO WRITING.":GOTO400

2800 IFN=0THEN900
2810 GOSUB930:IFNXTHEN400
2820 IFN=12THENPRINT"TRY 'EXAMINE'":GOTO400
2830 IFN<>13THEN2900
2840 IFR(4,1)=5THENPRINT"IT'S ALREADY OPEN.":GOTO400
2850 IFL(4)>-1THENPRINT"YOU DON'T HAVE THE KEY.":GOTO400
2860 PRINT"OK":R(4,1)=5:GOTO400
2900 PRINT"THAT'S NOT NECESSARY.":GOTO400

3000 IFL(9)>-1THENPRINT"YOU HAVE NO SHOVEL.":GOTO400
3010 IFR<>1THENPRINT"YOU CAN'T DIG INSIDE BUILDINGS.":GOTO400
3020 PRINT"YOU FIND ";
3030 IFL(11)THENPRINT"NOTHING.":GOTO400
3040 L(11)=R:PRINT"GOLD!":GOTO400

3200 IFN=0THEN900
3210 GOSUB930:IFNXTHEN400
3220 IFL(10)>-1THENPRINT"YOU HAVE NO MATCHES.":GOTO400
3230 IFN=10THENPRINT"IT BURNS BRIEFLY.":GOTO400
3240 IFN=8THENPRINT"IT BURNS BRIGHTLY.":CA=1:GOTO400
3300 PRINT"IT DOESN'T BURN.":GOTO400

3400 GOSUB1610:END
