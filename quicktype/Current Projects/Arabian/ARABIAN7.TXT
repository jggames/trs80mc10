0 CLS:DIMZ,F,M$,M,M(11,11),X,Y,T,R,R(10),A,B,O,H,V:M=16384:X=32768:Z=RND(-(PEEK(9)*256+PEEK(10))):GOSUB3000:GOTO110
7 PRINTM$;:F=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORZ=F-LEN(M$)+1TOF:POKEZ,PEEK(Z)-64:NEXT:RETURN
110 REM     arabian rhapsody
120 REM        by g.yasuda
130 PT=0
140 FD=-1:CE=60:P=0:N=3
160 PRINT"INSTRUCTIONS (Y/N)?"
170 A$=INKEY$
180 IFA$="Y"ORA$=CHR$(13)THEN4000
190 IFA$<>"N"THEN170
200 FD=FD+1:AC=2
210 GOSUB7000:REM table initialize
220 E=40
230 CG=90*(0.9)^FD
290 REM screen initialize
330 CLS
340 M$="                         ":FORT=4TO15:PRINT@32*T+3,;:GOSUB7:NEXT
350 GOSUB2050
360 REM COLOR 3
380 GOSUB6000
410 X=10:Y=10:POKE32*(Y+4)+X*2+4+M,42
420 A=1:B=1:Q=PEEK(32*(B+4)+A*2+4+M):POKE32*(B+4)+A*2+4+M,24
430 POKE8,0:SOUND50,2:SOUND102,2:SOUND134,2:SOUND154,2:SOUND154,2:SOUND154,2:SOUND154,2
440 SOUND134,2:SOUND134,2:SOUND134,2:SOUND134,2:SOUND102,2:SOUND50,2:FORT=1TO100:NEXT:SOUND102,2:SOUND134,2
441 SOUND154,2:SOUND154,2:SOUND154,2:SOUND154,2:SOUND154,2:SOUND134,2:SOUND102,2
442 SOUND50,2:SOUND50,2:SOUND50,2:SOUND102,2
460 FORI=0TO1000:NEXT:POKE8,8:T=USR(0)
470 ET=USR(1):GT=USR(1)
600 REM main
610 IFUSR(1)-GT>CGTHEN1200
620 IFUSR(1)-ET>CETHENE=E-1:PRINT@114,E:ET=USR(1):IFE=0THEN1400
630 M$=INKEY$:IFM$=""THEN610
670 T=VAL(M$):IFM(X,Y-1)=TTHENV=-1:H=0:GOTO680
672 IFM(X,Y+1)=TTHENV=1:H=0:GOTO680
673 IFM(X-1,Y)=TTHENH=-1:V=0:GOTO680
674 IFM(X+1,Y)=TTHENH=1:V=0:GOTO680
675 GOTO610
680 POKE32*(Y+4)+X*2+4+M,M(X,Y)+112:X=X+H:Y=Y+V:O=PEEK(32*(Y+4)+X*2+4+M)
685 IFO>47ANDO<58THENP=P+M(X,Y):E=E+1:PRINT@50,P:AC=AC+1:IFA=XANDB=YTHEN1400
690 POKE32*(Y+4)+X*2+4+M,42:IFAC<100THEN610
700 REM level clear
710 POKE8,0:SOUND102,2:SOUND119,2:SOUND119,2:SOUND119,2:SOUND102,2
711 SOUND134,2:SOUND141,2:SOUND134,2:SOUND141,2:SOUND134,2
712 SOUND154,2:SOUND73,2:SOUND83,2:SOUND73,2:SOUND154,2
720 FORI=0TO1000:NEXT
730 GOSUB5000
740 ON(FDAND3)+1GOSUB2300,2100,2500,2700
750 GOTO200
990 REM energy decrement
1200 GT=USR(1):POKE32*(B+4)+A*2+4+M,M(A,B)+112:H=X-A:V=Y-B:ONRND(2)GOTO1211,1213
1211 IFHTHENA=A+SGN(H):GOTO1215
1213 IFVTHENB=B+SGN(V):GOTO1215
1214 IFHTHEN1211
1215 T=32*(B+4)+A*2+4+M:Q=PEEK(T):POKET,24:T=PEEK(9)AND128:POKE49151,T:POKE49151,128-T:IFA=XANDB=YTHEN1400
1220 IFQ>47ANDQ<58THENAC=AC+1:IFAC>99THEN710
1230 GOTO630
1390 REM died
1400 PRINT@32*3+18,E
1410 POKE8,0:SOUND73,4:SOUND73,4:SOUND73,2:SOUND73,8:SOUND102,4:SOUND93,8:SOUND93,4:SOUND73,8:SOUND73,4:SOUND73,4
1420 FORI=0TO1000:NEXT
1430 REM check lives
1490 REM and bonuses
1500 IFN=1THENGOSUB5000
1510 N=N-1:E=E+10
1520 PRINT@32*1+18,P;:IFN=0THEN1700
1530 M$="LIVES ="+STR$(N):PRINT@139,;:GOSUB7
1540 PRINT@32*15+10,;:M$="HIT "+CHR$(91)+"ENTER"+CHR$(93):GOSUB7
1570 A$=INKEY$
1580 IFA$<>CHR$(13)THEN1570
1590 PRINT@490,;:M$="           ":GOSUB7:PRINT@139,;:M$="         ":GOSUB7
1595 POKE32*(B+4)+A*2+4+M,M(A,B)+112:POKE32*(Y+4)+X*2+4+M,M(X,Y)+112:GOTO410
1700 E=-(P>1000)-(P>2000)-(P>3000)-(P>5000)+1
1710 ONEGOSUB1800,1820,1840,1860,1880:PRINT@32*4+INT((32-LEN(M$))/2),;:GOSUB7
1730 PRINT@32*15+8,;:M$="PLAY AGAIN (Y/N)?":GOSUB7
1750 IFP>HITHENHI=P
1760 A$=INKEY$
1770 IFA$="N"THEN4500
1780 IFA$="Y"THENN=3:P=0:FD=-1:GOTO200
1790 GOTO1760
1800 M$="MEDIOCRE HAUL":RETURN
1820 M$="ACCEPTABLE HAUL":RETURN
1840 M$="GOOD HAUL":RETURN
1860 M$="EXCELLENT HAUL!":RETURN
1880 M$="INCREDIBLE HAUL!":RETURN
2000 REM clear field
2010 M$="                     "
2020 FORI=5TO14:PRINT@32*I+5,M$;:NEXT:RETURN
2040 REM print field
2050 FORY=1TO10:FORX=1TO10:PRINT@32*(Y+4)+X*2+3,M(X,Y);:NEXT:NEXT
2060 FORY=1TO10:FORX=1TO10:POKE32*(Y+4)+X*2+4+M,PEEK(32*(Y+4)+X*2+4+M)-64:NEXT:NEXT:RETURN
2100 REM demo1
2110 POKE8,0:POKE49151,64
2140 GOSUB6000:M$="OPEN SESAME!":PRINT@32*4+INT((32-LEN(M$))/2),;:GOSUB7:FORI=0TO2500:NEXT
2150 SOUND70,1:SOUND120,1:SOUND110,1:SOUND10,1:SOUND80,1
2290 GOTO2640
2300 REM demo2
2310 POKE8,0
2320 SOUND70,1:SOUND10,1:SOUND30,1:SOUND50,1
2325 SOUND80,1:SOUND90,1:SOUND100,1:SOUND120,1:SOUND110,1
2390 GOTO2640
2500 REM demo3
2510 POKE8,0
2600 FORJ=15TO1STEP-1
2610 SOUND15*(16-J),J
2620 NEXTJ
2640 FORJ=1TO10:POKE49151,64:FORI=0TO25:NEXT:SOUND250,1:NEXT
2690 RETURN
2700 REM demo4
2710 POKE8,0
2720 SOUND10,1:SOUND30,1:SOUND50,1:SOUND70,8
2730 SOUND80,1:SOUND90,1:SOUND100,1
2760 YS=2:W=1:XS=3
2765 IFXS<WTHENYS=-YS
2770 FORJ=WTOXSSTEPYS
2775 SOUND10,J:SOUND20,J*1.2:SOUND60,1+J/6
2780 NEXTJ
2785 W=XS
2800 FORI=3TO2STEP-0.2
2810 SOUND80,I:SOUND90,I:SOUND100,I
2820 NEXT
2890 GOTO2640
2999 END
3000 T=16896:GOSUB3001:T=X:GOSUB3001:EXECX:RETURN
3001 READM$:FORY=1TOLEN(M$)STEP2:POKET,(ASC(MID$(M$,Y,1))-65)*16+ASC(MID$(M$,Y+1,1))-65:T=T+1:NEXT:RETURN
3002 DATAHPAAAIDLDLDLPOECBGKGACEKCGPCCBDLGOAD
3003 DATAINCFCABAABNMALMDDKEDJFAINNALNOPNAINPPNDLLNOLMHCGAEEPFPNNPNNMPNNNMKMGJAANHOPCMHDIPPECBGIGAIJHAIAODJ
4000 CLS
4010 PRINT"******* ARABIAN RHAPSODY *******";
4011 PRINT
4012 PRINT" ";:M$="*":GOSUB7:PRINT" YOU ARE ALI BABA COLLECTING"
4013 PRINT"   TREASURE FROM A SECRET CAVE."
4014 PRINT" x THIS IS THE VICIOUS THIEF "
4015 PRINT"   WHO IS TRYING TO STOP YOU.":PRINT
4016 PRINT"(FIG1) 3 2 4 9 0"
4017 PRINT"       7 1 3 6 1  IN FIG1, YOU"
4018 PRINT"       0 5 ";:M$="*":GOSUB7:PRINT" 8 7  HIT 8 TO MOVE" 
4019 PRINT"       9 2 6 0 3  RIGHT, 6 FOR"
4020 PRINT"       1 4 7 9 2  DOWN, 5 FOR"
4030 PRINT"                  LEFT AND 3
4040 PRINT"                  FOR UP."
4060 PRINT@486,;:M$="HIT "+CHR$(91)+"ENTER"+CHR$(93)+" TO BEGIN":GOSUB7
4080 A$=INKEY$
4090 IFA$=CHR$(13)THEN200
4100 FORT=0TO200:NEXT:PRINT@486,"HIT "CHR$(91)"ENTER"CHR$(93)" TO BEGIN";:FORT=0TO50:A$=INKEY$:IFA$<>""THEN4090
4110 NEXT:GOTO4060
4500 REM game finish
4510 GOSUB2000
4520 PRINT@32*6+6,"THANKS FOR PLAYING,";:GOSUB4800
4530 PRINT@32*7+6,"BUT ARE YOU SURE";:GOSUB4800
4540 PRINT@32*8+6,"YOU WANT TO QUIT?";:GOSUB4800:GOSUB4800
4550 PRINT@32*10+6,"OR WERE YOU JUST";
4560 PRINT@32*11+6,"TELLING A FIB?";:GOSUB4800
4570 PRINT@32*12+6,"PRESS "CHR$(91)"F"CHR$(93)" FOR FIB.";
4580 FORI=0TO1000
4590 A$=INKEY$
4600 IFA$="F"THEN4700
4610 NEXT
4620 GOSUB2000
4630 PRINT@32*9+8,"WE'RE VERY SAD";:GOSUB4800
4640 PRINT@32*11+8,"TO SEE YOU GO!!";:GOSUB4800:GOSUB4800
4650 CLS:END
4700 GOSUB2000
4710 PRINT@32*9+8,"THAT'S GREAT!";:GOSUB4800
4720 PRINT@32*11+8,"I THOUGHT SO...";:GOSUB4800:FD=-1:P=0:N=3
4730 GOTO200
4800 FORI=0TO1000:NEXT
4810 RETURN
5000 REM calc bonus
5010 IFE>99THENT=100:GOTO5100
5020 IFE>9THENT=10:GOTO5100
5030 IFE>0THENT=1:GOTO5100
5040 SOUND196,2
5050 FORI=0TO300:NEXT
5080 RETURN
5100 E=E-T:P=P+T*10
5110 PRINT@32+18,P:PRINT@32*3+18,E
5120 SOUND134,2
5130 FORI=0TO300:NEXT
5140 GOTO5000
6000 PRINT@8,"ARABIAN RHAPSODY";
6010 PRINT@32*1+11,"SCORE =";P;
6020 PRINT@32*2+6,"HIGH SCORE =";HI;
6030 PRINT@32*3+10,"ENERGY =";E;
6040 RETURN
7000 CLS:PRINT"CREATING STAGE"STR$(FD+1)"...":Z=10:FORY=0TO11:FORX=0TO11:M(X,Y)=Z:NEXT:NEXT
7010 FORY=1TO10:FORX=1TO10:FORT=0TO9:R(T)=.:NEXT
7015 R(M(X,Y))=1:R(M(X,Y-1))=1:R(M(X,Y+1))=1:R(M(X-1,Y))=1:R(M(X+1,Y))=1:R(M(X-1,Y+1))=1:IFX>1THENR(M(X-2,Y+1))=1
7021 IFM(X,Y)<10THEN7024
7022 R=RND(Z)-1:IFR(R)THEN7022
7023 M(X,Y)=R:R(R)=1
7024 IFM(X,Y-1)<10THEN7027
7025 R=RND(Z)-1:IFR(R)THEN7025
7026 M(X,Y-1)=R:R(R)=1
7027 IFM(X,Y+1)<10THEN7030
7028 R=RND(Z)-1:IFR(R)THEN7028
7029 M(X,Y+1)=R:R(R)=1
7030 IFM(X-1,Y)<10THEN7033
7031 R=RND(Z)-1:IFR(R)THEN7031
7032 M(X-1,Y)=R:R(R)=1
7033 IFM(X+1,Y)<10THEN7040
7034 R=RND(Z)-1:IFR(R)THEN7034
7035 M(X+1,Y)=R
7040 NEXT:NEXT:FORY=0TO11:M(0,Y)=10:M(11,Y)=Z:NEXT:FORX=0TO11:M(X,0)=Z:M(X,11)=Z:NEXT:RETURN
7050 REM MC-10 EDITS JIM GERRIE
