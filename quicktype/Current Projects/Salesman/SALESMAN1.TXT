0 CLS:CLEAR500:DIMC1,C2,M$,Z$:GOTO10
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:Z$=MID$(M$,C1,1):IFZ$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFZ$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
5 FORC2=1TO2500:NEXT:CLS:RETURN
10 GOSUB 9300
20 GOTO 200
100 FOR IS=FT TO LEN(I1$)
110 IF MID$(I1$,IS,LEN(I2$))=I2$ THEN IP=IS:IS=255:NEXT:RETURN
120 NEXT IS
130 IP=0:RETURN
200 PH=RND(QP+1)-1:PH$=QP$(PH)
210 I1$=PH$:I2$="/":FT=1:GOSUB 100:SP=IP
220 IF SP=0 THEN 300
230 IF LEFT$(Q$(Q),1)="@" THEN PH$=LEFT$(PH$,SP-1)+"ARE"+RIGHT$(PH$,LEN(PH$)-SP)
300 IF SP=0 THEN 400
310 IF LEFT$(Q$(Q),1)="&" THEN PH$=LEFT$(PH$,SP-1)+"IS"+RIGHT$(PH$,LEN(PH$)-SP)
400 I1$=PH$:I2$="*":FT=1:GOSUB 100:SP=IP
410 IF SP=0 THEN 440
420 PH$=LEFT$(PH$,SP-1)+" "+RIGHT$(Q$(Q),LEN(Q$(Q))-1)+RIGHT$(PH$,LEN(PH$)-SP)
430 GOTO 450
440 PH$=PH$+" "+RIGHT$(Q$(Q),LEN(Q$(Q))-1)
450 PRINT:PRINT
500 M$=PH$+"?":GOSUB1
600 PRINT
700 IN$=" ":PRINT">";
710 PRINT"Œ";:I$=INKEY$
720 PRINTCHR$(8);:IF I$="" THEN 710
725 IF I$=CHR$(8) THEN IFLEN(IN$)>1THENIN$=MID$(IN$,1,LEN(IN$)-1):I$="":GOTO720
730 IF I$=CHR$(13) THEN 800
750 IF I$>CHR$(31) THEN IN$=IN$+I$:PRINT I$;
760 GOTO 710
800 LD=1:OF=-1:FS=1:NP=0:RU=0:M=0:OM=0:S1=0:S2=0
900 I1$=IN$:I2$=",":FT=1:GOSUB 100:CM=IP
910 IF CM=0 THEN 1600
1000 I1$=IN$:I2$="AND":FT=1:GOSUB 100:S1=IP
1010 I2$=IN$:I2$="BUT":FT=1:GOSUB 100:S2=IP
1200 IF S1+S2=0 THEN 1500
1300 IF LEFT$(IN$,3)<>" NO" THEN 1400
1310 R(Q)=R(Q)-1:IC(Q)=IC(Q)-CR(Q):IP(Q)=IP(Q)-PR(Q):GOTO 1500
1400 R(Q)=R(Q)+1:IC(Q)=IC(Q)+CR(Q):IP(Q)=IP(Q)+PR(Q)
1500 IN$=RIGHT$(IN$,LEN(IN$)-CM)
1600 I1$=IN$:I2$="YES":FT=FS:GOSUB 100:SP=IP
1700 IF SP>0 THEN RU=RU+1:LD=1:M=1:FS=SP+1:GOTO 1600
1800 I1$=IN$:I2$="NO":FT=FS:GOSUB 100:SP=IP
1900 IF SP>0 THEN LD=-1:M=1:FS=SP+1:NP=NP+1:GOTO 1800
2000 I1$=IN$:I2$="N'T":FT=FS:GOSUB 100:SP=IP
2100 IF SP>0 THEN LD=-1:M=1:FS=SP+1:NP=NP+1:GOTO 2000
2200 IF NP=0 THEN 2300
2210 IF INT(NP/2)=NP/2 THEN RU=RU+1:LD=1:GOTO 2300
2250 RU=RU-1:LD=-1
2300 FOR N=0 TO LI
2400 I1$=IN$:I2$=LI$(N):FT=1:GOSUB 100:SP=IP
2410 IF SP=0 THEN 2500
2420 IF MID$(IN$,SP-1,1)=" " THEN LD=LD*1:M=1
2500 NEXT N
2600 FOR N=0 TO DL
2700 I1$=IN$:I2$=DL$(N):FT=1:GOSUB 100:SP=IP
2710 IF SP>0 THEN IF MID$(IN$,SP-1,1)=" " THEN LD=LD*-1:M=1
2800 NEXT N
2900 FOR N=0 TO OB
3000 I1$=IN$:I2$=OB$(N):FT=1:GOSUB 100:SP=IP
3010 IF SP>0 THEN IF MID$(IN$,SP-1,1)=" " THEN RU=RU+LD:OF=N:M=1:OM=OM+1
3100 NEXT N
3200 FOR N=0 TO AV
3300 I1$=IN$:I2$=AV$(N):FT=1:GOSUB 100:SP=IP
3310 IF SP=0 THEN 3600
3400 IF MID$(IN$,SP-1,1)<>" " THEN 3600
3500 RU=RU+LD:M=1
3600 NEXT N
3700 FOR N=0 TO NV
3800 I1$=IN$:I2$=NV$(N):FT=1:GOSUB 100:SP=IP
3810 IF SP=0 THEN 4100
3900 IF MID$(IN$,SP-1,1)<>" " THEN 4100
4000 LD=LD*-1:RU=RU+LD:M=1
4100 NEXT N
4200 FOR N=0 TO AJ
4300 I1$=IN$:I2$=AJ$(N):FT=1:GOSUB 100:SP=IP
4310 IF SP=0 THEN 4600
4400 IF MID$(IN$,SP-1,1)<>" " THEN 4600
4500 RU=RU+LD:M=1
4600 NEXT N
4700 FOR N=0 TO NJ
4800 I1$=IN$:I2$=NJ$(N):FT=1:GOSUB 100:SP=IP
4810 IF SP=0 THEN 5100
4900 IF MID$(IN$,SP-1,1)<>" "THEN 5100
5000 LD=LD*-1:RU=RU+LD:M=1
5100 NEXT N
5110 FOR N=0 TO HM
5120 I1$=IN$:I2$=HM$(N):FT=1:GOSUB 100:SP=IP
5130 IF SP=0 THEN 5180
5140 IF MID$(IN$,SP-1)<>" " THEN 5180
5150 XX=N:IF XX<2 THEN PRINT"CHEAP AND NASTY":GOTO 5180
5160 IF XX>=2 THEN PRINT"RATHER EXPENSIVE"
5180 NEXT N
5190 PRINT
5200 IF M<1 THEN M$="PARDON, PLEASE EXCUSE ME BUT I DON'T UNDERSTAND":GOSUB1:GOTO 200
5300 IF OM>1 THEN M$="HANG ON-- ONE THING AT A TIME...":GOSUB1:GOTO 200
5400 IF OF=-1 THEN 5440
5410 R(OF)=R(OF)+RU:IC(OF)=IC(OF)+(CR(OF)*RU)
5420 IP(OF)=IP(OF)+(PR(OF)*RU)
5430 GOTO 5490
5440 R(Q)=R(Q)+RU:IC(Q)=IC(Q)+(CR(Q)*RU):IP(Q)=IP(Q)+(PR(Q)*RU)
5490 GOTO5900
5500 CLS
5600 FOR N=0 TO R:PRINT R(N);:NEXT N:PRINT
5700 FOR N=0 TO R:PRINT IC(N);:NEXT N:PRINT
5800 FOR N=0 TO R:PRINT IP(N);:NEXT N:PRINT
5900 FOR N=0 TO OB
6000 TC=TC+IC(N)
6100 TP=TP+IP(N)
6200 NEXT N
6300 IF TP<Q*5 THEN TX=RND(0)*EX:PRINT:M$=EX$(TX):GOSUB1:GOSUB5
6400 IF TC>BB THEN PT=RND(0)*CS:PRINT:M$=CS$(PT):GOSUB1:GOSUB5
6500 TC=0:TP=0
6700 FOR X=9 TO 0 STEP-1:PO$=""
6800 FOR N=0 TO CO
6900 IF FE(N,Q)-R(Q)>X THEN PO$=PO$+RIGHT$(STR$(N),1):M=N
7000 NEXT N
7100 IF PO$="" THEN NEXT X:GOTO 7200
7110 IF LEN(PO$)<3 THEN NEXT X
7200 CLS
7310 GOTO7400
7350 PRINT PO$
7400 IF PO$="" THEN 9200
7500 Z$="THE POSSIBILITIES ARE: ":FOR N=1 TO LEN(PO$)
7600 Z$=Z$+CO$(VAL(MID$(PO$,N,1)))+", "
7700 NEXT N:M$=LEFT$(Z$,LEN(Z$)-2):GOSUB1
7800 PRINT
7900 TS=0:BS=10
8000 FOR CH=0 TO LEN(PO$)-1
8100 NC=VAL(MID$(PO$,CH+1,1))
8200 IF C(NC)>=TS THEN TS=C(NC):HI=NC
8300 IF C(NC)<=BS THEN BS=C(NC):LO=NC
8400 NEXT CH
8410 IF HI=LO THEN M$="YOUR ONLY OPTION IS THE "+CO$(HI):GOSUB1:GOTO 9200
8500 HI$=CO$(HI):LO$=CO$(LO)
8600 SE=RND(2)
8700 SL=RND(3)-1
8800 IF SE=2 THEN 9100
8900 M$=HI$(SL)+" "+HI$:GOSUB1
9000 GOTO 9200
9100 M$=LO$(SL)+" "+LO$:GOSUB1
9200 Q=Q+1:IF Q<20 THEN 200
9201 END
9300 QP=5:Q=19:R=Q:OB=R:AJ=8:AV=6:LI=3:DL=3:NJ=8:NV=2:HM=3:BB=100
9310 DIM OB$(OB),AJ$(AJ),NJ$(NJ),AV$(AV),NV$(NV),LI$(LI),DL$(DL),Q$(Q)
9320 DIM R(R),QP$(QP),CR(Q),PR(Q),IC(Q),IP(Q),HM$(HM)
9400 DATA BASIC,GRAPHIC,SOUND,KEYBOARD,FUNCTION,MEMORY,TAPE,MICRODRIVE,DISK
9410 DATA SOFTWARE,CARTRIDGE,JOYSTICK,ASSEMBL,CENTRONIC,RS232,EXPAND
9420 DATA NETWORK,16-BIT,MULTITASK,SERVICE
9500 DATA GOOD,EXCEL,SUPER,MAGNIF,FIRST,FAST,EFFIC,ESSENT,LOT
9600 DATA BAD,RUBBISH,POOR,SLOW,INEFFIC,FEW,WORS,LEAST,LESS
9700 DATA REAL,VERY,OFTEN,FREQ,NECESS,TRU,ABSOLU
9800 DATA NEVER,UNNECESS,INFREQ
9900 DATA WANT,LIKE,NEED,REQUIRE
10000 DATA HATE,DISLIKE,LOATHE,DETEST
10100 DATA &GOOD BASIC,5,2,@GRAPHICS,7,2,&SOUND,6,2,&A GOOD KEYBOARD,4,2
10110 DATA @FUNCTION KEYS,1,5,&A LARGE MEMORY,3,6,&A TAPE INTERFACE,2,2
10120 DATA @MICRODRIVES,2,4,@DISKS,5,8,&EXSTENSIVE SOFTWARE,0,9
10130 DATA &A CARTRIDGE PORT,1,6
10200 DATA &A JOYSTICK PORT,1,7,&AN ASSEMBLER,2,1 ,&A CENTRONICS PORT,2,5
10210 DATA &AN RS232 PORT,2,6,&EXPANDABILTY,2,9,&NETWORKING,3,4
10220 DATA &A 16-BIT CPU,1,7,&MULTITASKING,5,5,&GOOD SERVICE,1,9
10300 DATA WOULD YOU LIKE,WHAT ABOUT,HOW ABOUT,DO YOU WANT,DO YOU REQUIRE
10310 DATA /* IMPORTANT
10320 DATA CHEAP,INEXPENSIVE
10330 DATA DEAR,EXPENSIVE
10400 FOR N=0 TO OB:READ OB$(N):NEXT N
10500 FOR N=0 TO AJ:READ AJ$(N):NEXT N
10600 FOR N=0 TO NJ:READ NJ$(N):NEXT N
10700 FOR N=0 TO AV:READ AV$(N):NEXT N
10800 FOR N=0 TO NV:READ NV$(N):NEXT N
10900 FOR N=0 TO LI:READ LI$(N):NEXT N
11000 FOR N=0 TO DL:READ DL$(N):NEXT N
11100 FOR N=0 TO Q:READ Q$(N),CR(N),PR(N):NEXT N
11200 FOR N=0 TO QP:READ QP$(N):NEXT N
11210 FOR N=0 TO HM:READ HM$(N):NEXT N
11300 CLS:Q=0
11400 M$="IT IS MY PLEASURE TO WELCOME YOU TO THE MULTIMEGA MICROSTORE":GOSUB1
11410 PRINT
11500 M$="WE ARE UNDOUBTEDLY THE ULTIMATE SOURCE OF ALL COMPUTER PRODUCTS":GOSUB1
11505 PRINT
11510 M$="I SHALL HAVE THE PLEASURE OF HELPING YOU SELECT YOUR NEW MACHINE":GOSUB1
11600 PRINT:M$="SO THAT I CAN WORK OUT THE BEST COMPUTER FOR YOUR NEEDS PERHAPS "
11620 M$=M$+"YOU WOULD BE KIND ENOUGH TO ANSWER A FEW QUESTIONS":GOSUB1
11700 PRINT
11800 CO=9:FE=19:CT=9:DIM CO$(FE),FE(CO,FE),DF(CO,FE),C(CT)
11900 DATA JCN PC,7,8,8,9,8,8,8,0,9,9,7,7,0,7,6,8,8,9,9,9
12000 DATA KNACT SERIOUS,6,7,6,8,8,8,8,0,8,8,0,0,0,7,6,8,8,9,9,7
12100 DATA CLEARSIN MT,9,9,9,7,7,8,8,9,9,6,7,7,0,7,6,7,9,9,9,1
12200 DATA ACHRON ILLUSION,8,7,6,6,0,3,7,0,5,5,0,0,6,0,0,4,1,0,0,2
12300 DATA BANANA IIE,3,5,2,5,0,4,6,0,3,0,3,5,0,0,6,7,0,0,0,4
12400 DATA SI ELITE,8,8,8,7,7,8,8,0,7,2,7,4,0,0,6,0,0,0,0,0
12500 DATA COLECTOVISION CABBAGE,5,5,5,5,2,5,5,5,5,1,7,7,0,0,6,5,0,9,0,0
12600 DATA CANDY COLOURED COMPUTER,7,6,4,2,0,2,7,0,4,9,8,7,0,0,6,3,0,0,0,6
12700 DATA COMANDEAR 64,2,8,9,7,7,6,5,0,6,9,6,7,0,0,2,2,0,0,0,6
12800 DATA ATRIA 600GT,1,8,8,5,0,2,5,0,7,7,7,7,0,0,6,6,0,0,0,5
12900 DATA 10,9,7,3,8,4,6,5,2,1
13000 FOR N=0 TO CO
13100 READ CO$(N)
13200 FOR M=0 TO FE
13300 READ FE(N,M)
13400 NEXT M,N
13500 FOR N=0 TO CT
13600 READ C(N)
13700 NEXT N:PRINT"ARE YOU READY?";
13800 A$=INKEY$:IF A$="" THEN 13800
13810 CLS
13900 DATA I THINK YOU ARE GETTING OUT OF YOUR PRICE RANGE
13910 DATA THIS SPECIFICATION SEEMS TO BE EXCEEDING YOUR CREDIT LIMIT
13920 DATA I DON'T THINK THAT YOU CAN AFFORD SUCH LUXURIES
14000 DATA EXCUSE ME I CAN HEAR THE PHONE RINGING,I HAVE AN URGENT APPOINTMENT
14010 DATA WE CLOSE IN FIVE MINUTES
14100 CS=2:EX=2:DIM CS$(CS),EX$(EX)
14200 FOR N=0 TO CS:READ CS$(N):NEXT N
14300 FOR N=0 TO EX:READ EX$(N):NEXT N
14400 DATA IF YOU ARE IN THE BUDGET MARKET THEN WHAT ABOUT THE
14410 DATA AN INEXPENSIVE CHOICE IS THE,YOU GET GOOD VALUE FOR MONEY WITH THE
14500 DATA IF YOU WANT A FIRST-CLASS PRODUCT THEN YOU MUST TRY THE
14510 DATA FOR STATE OF THE ART TECHNOLOGY YOU CAN'T BEAT THE
14520 DATA IF YOU WANT A ROLLS-ROYCE THEN JUST LOOK AT THE
14600 HI=2:LO=2:DIM HI$(HI),LO$(LO)
14700 FOR N=0 TO LO:READ LO$(N):NEXT N
14800 FOR N=0 TO HI:READ HI$(N):NEXT N
14900 CLS:RETURN
