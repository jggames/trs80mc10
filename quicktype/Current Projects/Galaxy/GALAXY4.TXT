0 CLS:CLEAR300:FORZZ=32768TO33407:READCC:POKEZZ,CC:NEXT:EXEC32772:GOTO20010
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
3 RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
9 CC=USR(VARPTR(M$)):RETURN

60 DATA32,25,32,7,126,130,108,0,0,0,0,134,95,183,128,7,134,245,183,128
61 DATA8,134,130,198,53,32,36,134,95,183,128,7,134,245,183,128,8,134,110,198
62 DATA53,247,128,7,141,24,90,38,253,141,19,124,128,7,246,128,7,90,38,253
63 DATA246,128,7,141,5,193,95,38,233,57,182,128,9,136,128,183,191,255,183,128
64 DATA9,57,2,58,0,26,1,252,0,29,1,196,0,33,1,171,0,35,1,124
65 DATA0,39,1,83,0,44,1,46,0,49,1,29,0,52,0,254,0,59,0,226
66 DATA0,66,0,214,0,70,0,190,0,78,0,169,0,88,0,151,0,99,0,143
67 DATA0,105,0,127,0,117,0,113,0,132,0,107,0,140,0,95,0,157,0,85
68 DATA0,176,0,75,0,198,2,26,0,28,1,223,0,31,1,171,0,35,1,147
69 DATA0,37,1,103,0,42,1,64,0,47,1,29,0,52,1,13,0,55,0,240
70 DATA0,62,0,214,0,70,0,202,0,74,0,180,0,83,0,160,0,93,0,143
71 DATA0,105,0,135,0,111,0,120,0,124,0,107,0,140,0,101,0,148,0,90
72 DATA0,166,0,80,0,186,0,71,0,209,2,92,0,25,2,26,0,28,1,223
73 DATA0,31,1,196,0,33,1,147,0,37,1,103,0,42,1,64,0,47,1,46
74 DATA0,49,1,13,0,55,0,240,0,62,0,226,0,66,0,202,0,74,0,180
75 DATA0,83,0,160,0,93,0,151,0,99,0,135,0,111,0,120,0,124,0,113
76 DATA0,132,0,101,0,148,0,90,0,166,0,80,0,186,0,0,0,0,0,0
77 DATA0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,134,255,183
78 DATA129,92,134,1,183,129,91,206,0,28,255,129,83,206,128,82,255,129,85,57
79 DATA246,129,92,193,255,39,70,79,5,5,243,129,85,243,129,83,253,129,93,254
80 DATA129,93,236,0,253,129,95,8,8,236,0,253,129,97,253,129,99,254,129,95
81 DATA9,38,253,182,128,9,136,128,183,191,255,183,128,9,254,129,99,9,255,129
82 DATA99,38,230,254,129,97,255,129,99,122,129,91,38,219,189,129,101,57,189,235
83 DATA199,253,129,78,254,129,78,166,0,183,129,82,166,2,183,129,80,166,3,183
84 DATA129,81,189,129,101,182,129,82,38,4,189,129,124,57,254,129,80,166,0,129
85 DATA32,38,12,189,129,124,206,58,66,9,38,253,126,130,95,129,47,38,12,189
86 DATA129,124,204,0,0,253,129,83,126,130,95,129,42,38,12,189,129,124,204,0
87 DATA56,253,129,83,126,130,95,129,45,38,12,189,129,124,206,128,250,255,129,85
88 DATA126,130,95,129,43,38,12,189,129,124,206,128,166,255,129,85,126,130,95,129
90 DATA48,38,6,124,129,91,126,130,95,129,48,35,14,129,57,36,10,54,189,129
91 DATA124,50,128,49,183,129,92,122,129,82,254,129,80,8,255,129,80,126,129,229
92 DATA206,129,202,255,66,22,57,0,0,0,0,0,0,0,0,0,0,0,0,0

400 PRINT@6*32,"  ":PRINT@7*32," ":PRINT@8*32," "
401 PRINT@9*32," "
412 PRINT@6*32,"  ":PRINT@7*32," ":PRINT@8*32," "
413 PRINT@9*32," ":PRINT@10*32," "
414 PRINT@5*32,"":PRINT@6*32,"":PRINT@7*32,""
415 PRINT@8*32," ":PRINT@9*32,"":PRINT@10*32,""
416 PRINT@5*32," ":PRINT@6*32," ":PRINT@7*32," "
417 PRINT@8*32,"":PRINT@9*32,"":PRINT@10*32,""
418 PRINT@5*32,"":PRINT@6*32,"":PRINT@7*32,""
419 PRINT@8*32,"":PRINT@9*32,"":PRINT@10*32,""
420 PRINT@5*32,"            ":PRINT@6*32,"                  ":PRINT@7*32,"                    "
421 PRINT@8*32,"        ":PRINT@9*32,"       ":PRINT@10*32,"           ":RETURN

510 GOSUB1510
511 IN$=INKEY$
512 IN$=INKEY$:IFIN$=""THEN512
513 KY=ASC(IN$):RETURN
1110 EXEC32770:RETURN

1210 EXEC32768:RETURN
1310 FORZ=1TO32:CC=PEEK(9)AND128:POKE49151,CC:POKE49151,128-CC:NEXT:RETURN
1320 FORZ=1TO16:CC=PEEK(9)AND128:POKE49151,CC:POKE49151,128-CC:NEXT:RETURN
1330 M$="/2/-1000":GOSUB9:RETURN

1410 M$="*7*5*4*6*1*7*2*3*5*2":GOSUB9:RETURN
1510 M$="*7*5*4*6:GOSUB9:RETURN
1610 M$="*1-1*1-10":GOSUB9:RETURN
1660 M$="7-76-65-543-32-21":GOSUB9:GOSUB1310:RETURN
1710 EXEC32770:RETURN
1760 FORZ=1TO4:CC=PEEK(9)AND128:POKE49151,CC:POKE49151,128-CC:NEXT:RETURN
1810 GOSUB1310:RETURN
1820 M$="100000  10  40000000":GOSUB9:RETURN
1910 PRINT@.5*485,PRINT"HIT ANY KEY TO CONTINUE.";:GOSUB510:RETURN
1960 EXEC32768:RETURN

2010 GOSUB510:IFKY=91THENV=-10:RETURN
2011 IFKY=13THENV=0:RETURN
2012 V=-1:FORI=1TOWN:IFIN$=MID$(SN$,I,1)V=I
2013 NEXT:RETURN
2510 V=0:GOSUB1510
2520 GOSUB511:IFKY=91THENV=-10:RETURN
2521 IFKY=13THENRETURN
2522 IFKY<48ORKY>57THENV=-1:RETURN
2523 V=V*10+VAL(IN$):GOTO2520:RETURN
3010 W=1
3040 IFWC(W)=PTHEN3100
3041 W=W+1:IFW<=WNTHEN3040
3042 PRINT@QQ,P$NA$(P)" CONTROLS NO WORLDS.":GOSUB1910:RETURN
3100 PRINT@QQ,C3$;P$NA$(P)" FROM WORLD :";:GOSUB2010:IFV=-10THENGOSUB9010:ON-(FC(0)=-7)GOTO3:GOTO3010
3130 IFV=0THENRETURN
3131 IFV<0ORV>WNTHEN3171
3132 IFWC(V)=PTHEN3180
3133 PRINT@QQ,C3$"YOU DON'T CONTROL THAT WORLD.";:GOSUB1610:GOTO3100
3171 PRINT@QQ,C3$"THAT WORLD DOESN'T EXIST.":GOSUB1610:GOTO3100
3180 WR=V:PRINT" TO WORLD: ";:GOSUB2010
3200 IFV=-10THENGOSUB9010:ON-(FC(0)=-7)GOTO3:GOTO3010
3210 IFV<1ORV>WNTHEN3171
3220 WD=V:PRINT"NUMBER OF SHIPS:";
3230 GOSUB2510
3240 PRINT:IFV=-10THENGOSUB9010:ON-(FC(0)=-7)GOTO3:GOTO3010
3250 IFV<1THENGOSUB1610:GOTO3100
3260 IFV>WS(WR)PRINT@QQ,C3$;"NOT ENOUGH SHIPS THERE.";:GOSUB1610:GOTO3100
3270 FM=V
3300 GOSUB4010
3310 IFF<=50THEN3100
3999 RETURN
4010 F=1
4020 IFFC(F)=0THENWS(WR)=WS(WR)-FM:FC(F)=P:FD(F)=WD:FS(F)=FM:FT(F)=T+(SQR((WX(WR)-WX(WD))^2+(WY(WR)-WY(WD))^2)+.99)/3:GOTO4999
4030 F=F+1:IFF<=50THEN4020
4040 PRINT@QQ,C3$;"ION STORM LIMITS YEAR'S SHIPS.";:P(0)=PN
4050 PRINT:GOSUB1410
4999 RETURN
5010 W=FD(F):DE=WS(W):AK=FS(F):G1=SQR(RND(0)*.8+RND(0)*.2):G2=SQR(1-G1^2):AK$=NA$(FC(F)):DE$=NA$(WC(W))
5020 CLS:FORI=0TO8:PRINT@0*32+10,;:M$="SUBSPACE COMMUNICATION":GOSUB7
5021 PRINT@1*32+10,;:M$="WORLD "+MID$(SN$,W,1)+".":GOSUB7:GOSUB1410
5030 IFWC(W)<>FC(F)THEN5040
5031 PRINT@.5*88,"REINFORCEMENT TO";:PRINT@.5*320,P$;AK$" HAS SENT"AK"SHIPS TO REINFORCE HIS WORLD.":WS(W)=AK+DE:GOTO5300
5040 PRINT@.5*85,"ATTEMPTED INVASION OF";:PRINT@.5*259,"ATTACKER : "AK$;:PRINT@.5*299,"DEFENDER : "DE$;:GOSUB1710
5041 PRINT@.5*323,"INITIAL FORCE :"AK;:PRINT@.5*363,"INITIAL FORCE :"DE;:PRINT@.5*387,"GUNNERY FACTOR :";INT(G2*100);
5045 PRINT@.5*427,"GUNNERY FACTOR :";INT(G1*100);
5050 PRINT@.5*451,"SHIPS LEFT :"AK;:PRINT@.5*491,"SHIPS LEFT :"DE;
5070 IFDE=0THENPRINT@.5*720,C3$;"ATTACKER WINS WITHOUT A FIGHT.":WS(W)=AK:WC(W)=FC(F):GOTO5300
5080 PRINT@.5*960,CHR$(30);:PRINT@.5*1003,"DEFENDER FIRES.";:J=0
5090 J=J+1:PRINT@.5*672,CHR$(131);:GOSUB1210:PRINT@.5*672," ";
5100 IFRND(0)>G1*SQR(AK/(AK+DE))THEN5130
5110 GOSUB400:GOSUB1310
5122 IFAK=0THENPRINT@.5*717,C3$;"DEFENDERS DEFEAT AGGRESSORS.";:WS(W)=DE:GOTO5300
5130 IFJ<DETHEN5090
5200 PRINT@480,C3$"ATTACKER FIRES.";:J=0
5210 J=J+1:PRINT@.5*672,CHR$(131);:GOSUB1110:PRINT@.5*672," ";
5220 IFRND(0)>G2*SQR(DE/(AK+DE))THEN5250
5230 GOSUB400:GOSUB1310
5240 IFDE=0THENPRINT@.5*723,C3$;"ATTACKERS ARE VICTORIOUS!":WS(W)=AK:WC(W)=FC(F):GOTO5300
5250 IFJ<AKTHEN5210
5260 PRINT:GOTO5080
5300 FC(F)=0
5310 GOSUB1910
5990 FORI=1TO50:NEXT
5999 RETURN
6010 FORW=1TOWN
6020 IFAB=0ANDWC(W)=0THEN6040
6030 WS(W)=WS(W)+WI(W)
6040 NEXT
6999 RETURN
7010 FORF=1TO50
7020 IFFC(F)=0THEN7040
7030 IFFT(F)=TTHENGOSUB5010
7040 NEXT
7999 RETURN
8010 CLS:PRINT@6,"RESULTS FOR YEAR"T
8400 QQ=448
8500 IFWN>30THEN8750
8510 IN$="WRLD CONT PROD SHPS"+CHR$(170)+" "
8520 PRINTIN$;IN$;LEFT$(IN$,19)
8530 Z=0:FORW=1TOWN
8540 PRINT" ";MID$(SN$,W,1);"   ";:PRINTUSING"%  %";NA$(WC(W));:PRINT;" ";:GOSUB1960
8550 IFWC(W)>0THENPRINTUSING"###";WI(W);:PRINT"  ";:PRINTUSING"####";W2(W);ELSEPRINTSTRING$(9,32);
8560 Z=Z+1:IFZ=3THENPRINT:Z=0ELSEPRINTCHR$(170);" ";
8570 NEXT:IFZ=1THENPRINTTAB(40);CHR$(170);
8580 GOTO8820
8750 IN$="WD CONT PD SHPS"+CHR$(170)
8760 PRINTIN$;IN$;IN$;LEFT$(IN$,15)
8770 Z=0:FORW=1TOWN
8780 PRINT" ";MID$(SN$,W,1);" ";:PRINTUSING"%  %";NA$(WC(W));:PRINT" ";:GOSUB1960
8790 IFWC(W)>0PRINTUSING"##";WI(W);:PRINT" ";:PRINTUSING"####";W2(W);ELSEPRINTSTRING$(7,32);
8800 Z=Z+1:IFZ=4THENPRINT:Z=0ELSEPRINTCHR$(170);
8810 NEXT:IFZ=1THENPRINTTAB(31);CHR$(170);TAB(47);CHR$(170);
8815 IFZ%=2THENPRINTTAB(47);CHR$(170);
8820 IFT<TNPRINT@.5*778,"FLEET ORDERS FOR YEAR"T+1
8821 RETURN
9010 CLS:IN$="TURN OFF":IFSO=0THENIN$="TURN ON"
9011 PRINT@.5*202,"G = GO BACK TO REGULAR TURN":PRINT@.5*266,"M = DRAW STAR MAP":PRINT@.5*330,"T = TIME / DISTANCE CALCULATOR":PRINT@.5*394,"N = "IN$" BATTLE SOUND EFFECTS":PRINT@.5*458,"S = SAVE CURRENT GAME"
9020 PRINT@.5*522,"Q = QUIT (DROP OUT OF GAME)":PRINT@.5*586,"E = END THE GAME FOR EVERYONE":PRINT@.5*650,"C = CHANGE THE NUMBER OF YEARS IN THE GAME"
9035 PRINT@.5*847,C3$;"WHAT IS YOUR CHOICE?";:GOSUB510:IFKY=71THENT=T-1:GOSUB8010:T=T+1:RETURNELSEIFKY=77GOSUB30010:GOTO9400ELSEIFKY=84GOSUB35010:GOTO9010ELSEIFKY=78IFSOTHENSO=0:GOTO9010ELSESO=1:GOTO9010ELSEIFKY<>83GOTO9250
9245 PRINT@.5*911,"SAVE GAME";:GOSUB9810:IFIN$="Y"THENPRINT@.5*975,"THE GAME WILL BE SAVED AT THE END OF THIS YEAR.";:SF=-1:FORI=1TO2000:NEXT:GOTO9035ELSEFORI=1TO800:NEXT:GOTO9035
9250 IFKY=81THENGOTO9510ELSEIFKY=69THENGOSUB9710:RETURNELSEIFKY=67THENGOSUB9920:GOTO9010ELSEPRINT@.5*911,"I DON'T UNDERSTAND THAT.":GOSUB1610:GOTO9035
9400 PRINT@.5*53,"HIT ANY KEY";:PRINT@.5*117,"TO CONTINUE";:GOSUB510:GOTO9010
9510 PRINT@.5*911,"QUIT";:GOSUB9810:IFIN$="N"THENFORI=1TO1000:NEXT:GOTO9035ELSEFORI=1TO50:IFFC(I)=PTHENFC(I)=0
9520 NEXT:FORI=1TOWN:IFWC(I)=PTHENWC(I)=0
9530 NEXT:FORI=1TO1000:NEXT:T=T-1:GOSUB8010:T=T+1:RETURN
9710 PRINT@.5*911,"END GAME";:GOSUB9810:IFIN$="Y"THENFORI=1TO50:FC(I)=0:NEXT:FC(0)=-7:P(0)=PN:TN=T:FORI=1TO600:NEXT:RETURNELSEFORI=1TO600:NEXT:GOTO9035
9810 PRINT": ARE YOU SURE? ";
9820 GOSUB510:IFIN$="Y"THENPRINT"YES";:RETURNELSEIFIN$="N"THENPRINT"NO";:RETURNELSEGOSUB1610:GOTO9820
9920 CLS:PRINT"THIS IS NOW GAME YEAR"T".":PRINT"THERE ARE"TN"YEARS IN THE GAME.":GOSUB1510:PRINT"HOW MANY YEARS DO YOU WANT IN THE GAME NOW";:INPUTV$:V=VAL(V$):IFT>VORV>100PRINT"THAT ISN'T POSSIBLE.":GOSUB1610:GOTO9920ELSETN=V:RETURN
10010 PRINT@.5*896,"THE WAR IS OVER! HIT <ENTER> FOR FINAL STATISTICS.":IFFC(0)=-7GOSUB510:GOTO10015ELSEPRINT"HIT <C> TO CONTINUE FOR ONE MORE YEAR.":GOSUB510:IFKY=67TN=T:T=T-1:GOSUB8010:T=T+1:GOTO10999
10015 IFKY=13THENN=0:GOTO10030
10020 GOSUB1610:GOTO10010
10030 CLS:FORP=1TOPN:V=0
10050 FORW=1TOWN:IFWC(W)=PTHENV=V+1
10070 NEXT
10080 PRINTP$NA$(P)" HAS"V"PLANETS."
10090 IFV>NTHENN=V
10100 NEXT:PRINT
10200 IFPN=1THEN10400
10300 FORP=1TOPN:V=0
10320 FORW=1TOWN:IFWC(W)=PTHENV=V+1
10340 NEXT
10350 IFV=NTHENPRINT"CONGRATULATIONS TO "P$NA$(P)"!"
10360 NEXT
10370 GOSUB1660
10390 GOTO10500
10400 IFV=WNTHENPRINT"CONGRATULATIONS!":GOSUB1410:GOTO10500
10410 PRINT"BETTER LUCK NEXT TIME."
10500 PRINT:PRINT
10610 FORI=1TO36:PRINTMID$("THIS IS CENTRAL CONTROL SIGNING OFF.",I,1);:GOSUB1760:NEXTI
10630 FORI=1TO2000:NEXTI:GOSUB1760:CLS
10999 RETURN
20010 GOSUB21010
20060 GOSUB25010
20061 PRINT@480,"CONTINUE OLD GAME (Y/N)?";
20062 GOSUB510
20070 IFKY=78THENPRINT"NEW";:FORI=1TO1000:NEXT:GOTO20130
20080 IFKY=89THENPRINT"OLD";:FORI=1TO1000:NEXT:GOTO20100
20090 GOSUB1610:GOTO20062
20100 CLS:IFNOTDSKTHEN20110ELSEPRINT"WOULD YOU LIKE TO LOAD FROM TAPE OR DISK (T/D)? ";:GOSUB510:IFKY=84PRINT"TAPE":GOTO20110ELSEIFKY=68PRINT"DISK"ELSEGOSUB1610:GOTO20100
20102 PRINT"PREPARE DISK; PRESS <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=13THENONERRORGOTO20103:PRINT"READING OLD GAME FROM DISK.":OPEN"I",1,"GXOLD":GOTO20104ELSEIFKY=65THENCLS:PRINT"GAME LOAD ABORTED.":GOTO20061ELSEGOSUB1610:GOTO20102
20103 PRINT"?CANNOT FIND OLD GAME FILE. ERROR CODE"ERR/2+1".":CLOSE1:GOSUB1610:RESUME20061
20104 ONERRORGOTO20109:INPUT#1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:INPUT#1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:INPUT#1,NA$(I):NEXT:FORI=1TO50:INPUT#1,FS(I),FD(I),FC(I),FT(I):NEXT
20108 CLOSE1:GOTO20116
20109 PRINT"?ERROR ENCOUNTERED IN READING FILE. ERROR CODE"ERR/2+1".":GOSUB1610:IFDSKCLOSE1:RESUME20061ELSERESUME20061
20110 PRINT"PREPARE DATA TAPE; PRESS <ENTER> WHEN READY, <A> TO ABORT.":GOSUB510:IFKY=13THENONERRORGOTO20109ELSEIFKY=65CLS:PRINT"GAME LOAD ABORTED.":GOTO20061ELSEGOSUB1610:GOTO20110
20111 PRINT"READING OLD GAME FROM TAPE.":INPUT#-1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:INPUT#-1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:INPUT#-1,NA$(I):NEXT:FORI=1TO46STEP5:I1=I+1:I2=I+2:I3=I+3:I4=I+4
20112 INPUT#-1,FS(I),FS(I1),FS(I2),FS(I3),FS(I4),FD(I),FD(I1),FD(I2),FD(I3),FD(I4),FC(I),FC(I1),FC(I2),FC(I3),FC(I4),FT(I),FT(I1),FT(I2),FT(I3),FT(I4):NEXT
20116 ONERRORGOTO0:T=T-1:GOSUB8010:T=T+1:GOTO20265
20130 GOSUB28010
20140 GOSUB33010:GOSUB29010:GOSUB30010
20160 PRINT@58,"NEW":PRINT@58+32,"SETUP? ";:P=0:GOSUB510
20170 IFKY=89THENPRINT@58,"WAIT...";:GOTO20140
20180 IFKY=78THENPRINT@58,"WAIT...";:GOTO20200
20190 GOSUB1610:GOTO20160
20200 GOSUB31010:GOSUB34040:GOSUB8010:T=1
20265 FORP=0TOPN:P(P)=0:NEXT
20270 P=RND(PN):IFP(P)=1THEN20270
20275 P(P)=1:P(0)=P(0)+1
20280 GOSUB3010
20290 IFP(0)<PNTHEN20270
20295 CLS
20300 GOSUB6010
20310 GOSUB7010
20320 FORW=1TOWN:W2(W)=WS(W):NEXT
20325 IFSFTHEN20340
20330 GOSUB8010
20340 T=T+1
20345 IFNOTSFTHEN20440
20350 CLS:ONERRORGOTO0:IFNOTDSKTHEN20380ELSEPRINT"SAVE GAME TO DISK OR TAPE (D/T)? ";:GOSUB510:IFKY=68PRINT"DISK"ELSEIFKY=84PRINT"TAPE":GOTO20380ELSEGOSUB1610:GOTO20350
20351 PRINT"PREPARE DISK; HIT <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=65PRINT"GAME SAVE ABORTED.":GOTO20400ELSEIFKY<>13GOSUB1610:GOTO20351ELSEONERRORGOTO20352:PRINT"SAVING GAME TO DISK.":OPEN "O",1,"GXOLD":GOTO20355
20352 PRINT"?GAME FILE NOT ACCESSIBLE. ERROR CODE"ERR/2+1".":CLOSE1:RESUME 20351
20355 ONERRORGOTO20358:PRINT#1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:PRINT#1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:PRINT#1,NA$(I):NEXT:FORI=1TO50:PRINT#1,FS(I),FD(I),FC(I),FT(I):NEXT:CLOSE1:GOTO20399
20358 PRINT"?GAME FILE NOT WRITEABLE. ERROR CODE"ERR/2+1".":CLOSE1:RESUME20351
20380 PRINT"PREPARE TAPE; HIT <ENTER> WHEN READY, OR <A> TO ABORT.":GOSUB510:IFKY=65PRINT"GAME SAVE ABORTED.":GOTO20400ELSEIFKY<>13GOSUB1610:GOTO20380
20391 PRINT"SAVING GAME TO TAPE.":PRINT#-1,PN,WN,T,TN,FC(0),AB:FORI=1TOWN:PRINT#-1,WS(I),WC(I),WX(I),WY(I),WI(I),W2(I):NEXT:FORI=0TOPN:PRINT#-1,NA$(I):NEXT:FORI=1TO46STEP5:I1=I+1:I2=I+2:I3=I+3:I4=I+4
20392 PRINT#-1,FS(I),FS(I1),FS(I2),FS(I3),FS(I4),FD(I),FD(I1),FD(I2),FD(I3),FD(I4),FC(I),FC(I1),FC(I2),FC(I3),FC(I4),FT(I),FT(I1),FT(I2),FT(I3),FT(I4):NEXT
20399 PRINT"GAME SAVED."
20400 ONERRORGOTO0:SF=0:PRINT:PRINT"DO YOU WANT TO CONTINUE THE GAME? ";:GOSUB510
20410 IFKY=89THENPRINT"YES":FORI=1TO400:NEXT:T=T-1:GOSUB8010:T=T+1:GOTO20440
20420 IFKY=78THENCLS:GOTO20999
20430 GOSUB1610:GOTO20400
20440 IFT<=TNTHEN20265
20450 GOSUB10010
20500 IFT=TNTHEN20265
20999 END
21010 CLS:EP=0:DR=0:ML=0:SP=0:FR=0:Z=0:W=0:I=0:P=0
22010 P$="FLEET ADMIRAL ":SN$="ABCDEFGHIJKLMNOPQRSTUVWXYZ*#$%&+<=>?()@/"
23010 DIMWS(10),WX(10),WY(10),WI(10),WC(10),W2(10),FS(20),FD(20),FC(20),FT(20),P(20),NA$(20)
23011 M$="                               ":I$="":FORCC=1TO31:I$=I$+CHR$(8):NEXT:C3$=M$+I$:RETURN
25010 GOSUB1410:FORI=1TO200:NEXT:PRINT@.5*535,;:FORI=1TO13:PRINTMID$("G A L A X Y !",I,1);:GOSUB1760:NEXT
25020 RETURN
28010 CLS:PRINT@.5*25,"> NEW GAME <":PRINT:PRINT
28020 GOSUB1510:INPUT"HOW MANY PLAYERS (1-4)";PN$:PN=INT(VAL(PN$)):IFPN<1ORPN>4THENGOSUB1610:GOTO28020
28040 GOSUB1510:INPUT"HOW MANY WORLDS (5-10)";WN$:WN=INT(VAL(WN$))
28050 IFWN<=PNTHENPRINT"MUST BE MORE STARS THAN PLAYERS":GOSUB1610:GOTO28040
28055 IFWN>40THENPRINTWN"IS TOO MANY STARS.":GOSUB1610:GOTO28040
28060 IFWN<5THENPRINTWN"IS TOO FEW STARS.":GOSUB1610:GOTO28040
28070 GOSUB1510:INPUT"HOW MANY YEARS (1-100)";TN$:TN=INT(VAL(TN$))
28080 IFTN<1ORTN>100THENGOSUB1610:GOTO28070
28100 PRINT:PRINT"NEUTRAL WORLDS CAN BUILD SHIPS? ";:GOSUB510
28110 IFKY=89THENAB=1:PRINT"YES":GOTO28190
28120 IFKY=78THENAB=0:PRINT"NO":GOTO28190
28130 GOSUB1610:GOTO28100
28190 FORI=1TO400:NEXT:NA$(0)="MC-10"
28200 FORI=1TO130:NEXT:CLS
28210 FORP=1TOPN
28220 PRINT:PRINTP$;P;" WILL BEGIN THE GAME CONTROLLING WORLD "MID$(SN$,P,1)"."
28240 GOSUB1510:IN$="BOZO":INPUT"WHAT IS THE FLEET ADMIRAL'S NAME (1-4 CHARACTERS)";IN$
28250 IFLEN(IN$)>4THENGOSUB1610:GOTO28240
28260 NA$(P)=IN$
28300 NEXT
28800 PRINT:PRINT"PLEASE WAIT WHILE I CREATE THE UNIVERSE.";
28999 RETURN
29010 FORW=1TOWN
29020 WX(W)=RND(25):WY(W)=RND(15)
29030 FORI=0TOW-1
29040 IFWX(W)=WX(I)THENIFWY(W)=WY(I)THENI=W+1:FL=-1
29050 NEXT
29060 IFFLTHENFL=0:GOTO29020
29070 NEXT
29999 RETURN
30010 CLS:PRINT"***** S T A R  M A P *****";
30020 FORI=1TO15:PRINT:FORJ=1TO26:PRINT".";:NEXT:NEXT
30300 FORW=1TOWN
30310 DI=16384+WX(W)+WY(W)*32
30311 IFWC(W)=0THEN30320
30312 IFWC(W)=PTHENPOKEDI,42:GOTO30360
30313 POKEDI,ASC(MID$(SN$,W,1)):GOSUB1960:POKEDI,PEEK(DI)-64:GOTO30360
30320 POKEDI,ASC(MID$(SN$,W,1)):GOSUB1960
30360 NEXT
30999 RETURN
31010 FORP=1TOPN:WI(P)=10:NEXT
31020 FORW=1+PNTOWN
31030 IFRND(0)<.25THEN31060
31040 WI(W)=WI(W)+1
31045 IFWI(W)>99THENWI(W)=99
31050 IFRND(0)<.67THEN31040
31060 NEXT
31999 RETURN
33010 FORP=1TOPN:WC(P)=P:NEXT:RETURN
34040 FORP=1TOPN:WS(P)=12*WN-15*PN:FORI=1TOPN
34041 WS(P)=WS(P)+300/(SQR((WX(P)-WX(I))^2+(WY(P)-WY(I))^2)+6)
34042 NEXT:FORW=PN+1TOWN:WS(P)=WS(P)-100/(SQR((WX(P)-WX(W))^2+(WY(P)-WY(W))^2)+3):NEXT:IFWS(P)<50THENWS(P)=50
34043 NEXT
34120 IFPN=1THENW2(1)=WS(1):GOTO34200
34121 FORP=1TOPN:WS(0)=WS(0)+WS(P):NEXT:FORP=1TOPN:WS(P)=WS(P)*PN*100/WS(0):W2(P)=WS(P):NEXT:IFPN=WNTHENRETURN
34200 FORW=1+PNTOWN
34210 WS(W)=WS(W)+(WI(W)+2.2)*RND(0):IFRND(0)<.67THEN34210
34211 W2(W)=WS(W):NEXT:RETURN
35010 CLS:PRINT@.5*72,"TIME/DISTANCE CALCULATOR":PRINT@.5*202,"HIT <ENTER> TO EXIT."
35030 PRINT@.5*320,CHR$(30);"FROM WORLD:  ";:GOSUB2010:IFV=0THENRETURN
35051 IFV<1ORV>WNTHENGOSUB1610:GOTO35030
35052 WR=V:PRINT"TO WORLD:":GOSUB2010:IFV=0THENRETURN
35053 IFV<1ORV>WNTHENGOSUB1610:GOTO35030
35200 WD=V:DI=SQR((WX(WR)-WX(WD))^2+(WY(WR)-WY(WD))^2):TI=(DI+.99)/3:DI=INT(DI*100)/100
35201 PRINT@.5*458,C3$"DISTANCE:"DI"LIGHT YEARS":PRINT@.5*519,"TRAVEL TIME:"TI"YEARS"
35202 PRINT@.5*582,"CURRENT YEAR:"T:PRINT@.5*643,"YEAR OF ARRIVAL:";T+TI:GOTO35030
