0 GOTO1000
9 YY=INT(CC/64):XX=((CC/64)-YY)*64:SET(XX,YY,C1):RETURN
1000 CLEAR512:P=.5
1015 S$="STRINTWISCONDEXCHR"
1020 M$="GNOLL   KOBOLD  SKELETONHOBBIT  ZOMBIE  ORC     FIGHTER MUMMY   ELF     GHOUL   DWARF   TROLL   WRAITH"
1021 MO$=M$+"  OGRE    MINOTAURGIANT   SPECTER VAMPIRE DEMON   DRAGON  "
1035 T$="REFUSESILVERGOLD  GEMS  JEWELS"
1100 DIMP(3,3),ST(5),SF(11),I$(10),I(10),P$(36),B(4),FM(20),FL(20),FH(20),N$(10,3):XO=1.6915:YO=1.4278:ZO=1.2462:WO=4694
1105 I$(1)="SWORD":I$(2)="ARMOR":I$(3)="SHIELD":I$(4)="ELVEN CLOAK":I$(5)="ELVEN BOOTS":I$(6)="RING OF REGENERATION"
1135 I$(7)="RING OF PROTECTION":I$(8)="SCROLL OF RESCUE":I$(9)="POTION OF HEALING":I$(10)="POTION OF STRENGTH"
1155 B$="RED   YELLOWGREEN BLUE":CM$="WXADS82465HQPC."+CHR$(31)+"RUG"
1160 FORI=1TO36:READP$(I):NEXT:FORI=1TO20:FM(I)=0:NEXT:FORI=1TO3:FORK=1TO10:READIN$(K,I):NEXT:NEXT
1162 DATAMAGIC MISSILE,SLEEP,CURE LIGHT WOUNDS,LIGHT,TURN UNDEAD,PROT/EVIL,WEB,LEVITATE,CAUSE LIGHT WNDS,DETECT TRAPS,CHARM
1166 DATASTRENGTH,LIGHTNING BOLT,CURE SERIOUS WNDS,CONTINUAL LIGHT,INVISIBILITY,HOLD MONSTER,PHANTSML FORCES
1168 DATAPASS WALL,FIREBALL,CAUSE SERIOUS WND,FLESH TO STONE,FEAR,FINGER OF DEATH
1170 DATATELEPORT,ASTRAL WALK,POWER WORD KILL,ICE STORM,WALL OF FIRE,PLAGUE
1171 DATATIME STOP,RAISE DEAD,HOLY SYMBOL,WORD OF RECALL,RESTORATION,PRISMATIC WALL
1173 DATASALTY,BOLD,LOAD,OLD,GOODLY,WORTHY,LOFTY,FINE,ROCKY,AGED,ROAD,EYE,TOOTH,DRAGON,MUG,DEMON,WHARF,BRIDGE,MEADE,ALE
1175 DATATAVERN,ALEHOUSE,CELLAR,CLUB,INN,HOUSE,INN,LODGE,MEADHALL,RESTHOUSE
1200 CLS:PRINT"WELCOME TO TELENGARD":C=0
1201 REM
1213 PRINT"(S)TART A NEW CHARACTER OR":PRINT"(L)OAD OLD ONE?";
1215 GOSUB16000:IFC$="@"THEN1215
1216 IFC$<>"L"THENPRINT"START":GOSUB19000:GOTO1400
1220 PRINT"READ":STOP
1400 CLS:PRINT"enter TO USE STATS"
1500 FORI=0TO5:Q=0:FORQ1=1TO3:Q=Q+RND(6):NEXT:ST(I)=Q
1510 PRINTMID$(S$,I*3+1,3);ST(I):NEXT
1515 GOSUB16000:IFC$<>CHR$(13)THEN1400
1520 HP=ST(3):CH=HP:GD=0:TG=0:CZ=1:LV=1:FORI=1TO10:I(I)=0:SF(I)=0:NEXT
1525 SF(11)=0:GOSUB19800
1600 CX=25:CY=13:EX=0:SU=1:CS=1:CLS:PRINT"YOUR NAME, NOBLE SIRE";
1602 INPUTNM$
1610 PRINT"YOU ARE DESCENDING INTO THE"
1620 PRINT"DEPTHS OF THE TELENGARD DUNGEON...":PRINT:PRINT:PRINT:PRINT"BEWARE...";
1630 GOSUB19000:GOSUB19000:CLS:L=1:GOSUB20300:GOSUB15000:SY=1:GOTO3000
2000 FORI=1TO11:SF(I)=SF(I)-1:NEXT
2005 L=CZ:GOSUB17000:IFSY=1THENGOSUB15000
2010 SY=1:IFI(6)=0ORCH=HPTHEN3000
2015 CH=CH+I(6):IFCH>HPTHENCH=HP
2020 GOSUB20300
3000 IFRND(0)>.3THEN4000
3001 IFSF(6)>0ANDRND(0)>.2THEN4000
3005 PRINT@P*270,"#";:M=RND(20):IFSF(7)>0ANDM<4THEN3005
3006 IFRND(20)<=I(4)THEN20900
3007 ML=INT((RND(0)^1.5)*(CZ*2+1)+1):GOSUB20700
3008 IFSF(3)>0ANDUN=1ANDRND(0)>.8THEN3005
3010 GOSUB16500:GOSUB19700:PRINT"YOU HAVE ENCOUNTERED A LEVEL";ML;M$
3011 IFSF(9)>0ANDM<16ANDM<>11THENGOSUB17500:GOTO4000
3012 PRINT@P*673,"LEVEL";STR$(ML);" ";M$;:PRINT@P*768,"";
3015 MH=INT((RND(0)^.5)*ML*M+1):L=ML
3017 IFRND(0)>.95THENONRND(3)GOTO3030,3045,3072
3020 IFM<>9THEN3040
3025 IFRND(0)>.04*ST(5)THEN3040
3030 PRINT"THE ";M$;" LIKES YOUR BODY":PRINT"HE HEALS YOU TO FULL STRENGTH":CH=HP
3037 GOSUB20300:GOSUB19000:GOSUB20600:PRINT@P*270," ";:GOTO4000
3040 IFM<>4ORRND(0)<.04*ST(5)THEN3070
3045 PRINT"THE ";M$;" MAKES A QUICK MOVE":GOSUB19000
3050 FORI=1TO10:IFI(I)>0THEN3056
3052 NEXT:PRINT"YOU HAVE NOTHING HE WANTS TO STEAL!":GOTO3037
3056 PRINT"HE STEALS ";:IFI<8THENPRINT"YOUR ";I$(I):I(I)=0:GOTO3037
3060 PRINT"A ";I$(I):I(I)=I(I)-1:GOTO3037
3070 IFM<>20ORRND(0)*30>ST(3)THEN3090
3072 I=RND(7):IFI(I)>=MLTHEN3090
3074 C=ML-I(I):C=RND(C):I(I)=I(I)+C
3076 PRINT"THE ";M$;" LIKES YOU!":GOSUB17500
3078 PRINT"HE GIVES YOU A ";I$(I);" +";I(I):GOSUB20300:GOSUB17500
3080 PRINT@P*270," ";:GOSUB20600:GOTO4000
3090 IFRND(0)>.5+ST(4)*.02THEN3300
3100 PRINT"(F)IGHT, (C)AST, OR (E)VADE:";:GOSUB16000
3102 IFC$="@"THENPRINT"WAIT"
3105 FORI=1TO7:IFMID$("FCE0."+CHR$(13)+"@",I,1)=C$THEN3115
3110 NEXT:PRINT"THE ";M$;" IS NOT AMUSED":GOSUB17500:GOTO3100
3115 ONIGOTO3200,3400,3700,3200,3400,3700,3300
3200 I=INT(RND(0)*20)+LV+I(1)+ST(0)/2:PRINT"FIGHT"
3202 IFSF(1)>0THENI=I+4
3204 IFI<10THENPRINT"YOU MISSED...":GOTO3300
3205 I=INT(RND(0)*8+RND(0)*LV*2+I(1)+1):IFSF(1)>0THENI=I+5
3207 PRINT"YOU DO";I;"POINTS OF DAMAGE"
3210 MH=MH-I:IFMH>0THEN3300
3220 PRINT"IT DIED...";
3223 CC=272:C1=7:GOSUB9:E=ML*M*10:EX=EX+E:GOSUB19000
3225 GOSUB20600:GOSUB17000:PRINT"YOU GAIN";E;"EXPERIENCE POINTS":GOSUB20300:GOSUB18000
3230 GOSUB17500:GOTO3900
3300 DB=1:PA=0:DR=0:MB=0:GOTO3330
3305 I=INT(RND(0)*20)+ML-I(2)-I(3)+MB
3306 IFM>16ANDM<20ANDSF(4)>0THENI=I-6
3310 IFI<10THENPRINT"IT MISSED...";:GOSUB17500:GOTO3100
3315 I=INT((RND(0)*8+RND(0)*ML*2+1)*DB):PRINT"IT DOES";I;"POINTS DAMAGE";
3320 CH=CH-I:GOSUB20260:IFCH<1THEN9000
3321 GOSUB19000:IFRND(1)>DRORSF(4)>0THEN3326
3322 GOSUB17000:PRINT"IT DRAINS A LEVEL!":EX=INT(EX/2)
3324 GOSUB18000:GOSUB17500:GOTO3310
3326 IFRND(0)>PATHENGOSUB17500:GOTO3100
3327 GOSUB17000:PRINT"YOU'RE PARALYZED!":GOSUB17500:GOTO3300
3330 IFM=13THENDR=.1:GOTO3305
3335 IFM=17THENDR=.2:GOTO3305
3340 IFM=18THENDR=.3:PA=.3:GOTO3305
3345 IFM=10THENPA=.5:GOTO3305
3350 IFM<>19THEN3365
3352 GOSUB17500
3355 IFRND(0)>.6THENPRINT"IT USES IT'S SWORD!":MB=4:DB=3:GOTO3305
3360 PRINT"IT USES IT'S WHIP!":MB=2:DB=2:GOTO3305
3365 IFM<>20THEN3305
3370 IFRND(0)>.3THENMB=5:DB=2:GOTO3305
3375 PRINT"THE DRAGON BREATHS FIRE!":GOSUB17500
3378 I=RND(ML*20):IFRND(0)<.05*ST(3)THEN3382
3380 PRINT"YOU PARTIALLY DODGE IT":I=INT(I/2+.5)
3382 PRINT"YOU BURN FOR";I;"POINTS DAMAGE":GOTO3320
3400 D=1:GOTO21000
3700 Q=RND(18):PRINT"EVADE"
3705 IFQ<ST(4)+I(5)THEN3710
3707 PRINT"YOU'RE ROOTED TO THE SPOT":GOSUB17500:GOSUB16500:GOTO3300
3710 IFSF(8)>0THENC=RND(4):GOTO3795
3715 ONRND(4)GOTO3720,3730,3740,3750
3720 C=1:IF(LAND3)<3THEN3795
3730 C=2:IF(P(2,1)AND3)<3THEN3795
3740 C=3:IF(INT(L/4)AND3)<3THEN3795
3750 C=4:IF(INT(P(1,2)/4)AND3)<3THEN3795
3760 C=1:IF(LAND3)<3THEN3795
3770 C=2:IF(P(2,1)AND3)<3THEN3795
3780 C=3:IF(INT(L/4)AND3)<3THEN3795
3790 GOTO3707
3795 GOSUB20600:GOSUB17000:GOTO8120
3900 IFRND(0)>.5THEN4002
4000 IFRND(0)>.2THEN6000
4002 T=0:IFRND(0)>.85THENT=1
4005 PRINT@P*270,"$";:GOSUB16500:C=0:IFRND(0)>.7THEN4100
4007 PRINT"YOU SEE SOME ";:I=RND(5)-1
4010 PRINTMID$(T$,I*6+1,6):PRINT"enter TO PICK UP:";
4011 IFT=1THENIFSF(2)>0ANDRND(0)>.1THENPRINT:PRINT"YOU DETECT TRAPS!";
4012 GOSUB16000:IFC$=CHR$(13)THEN4020
4015 PRINT"LEAVE IT":GOSUB17500:GOTO6000
4020 PRINT"SNARF IT":IFT=0THEN4040
4025 Q=RND(L*3):GOSUB17000
4030 PRINT"IT'S TRAPPED!":PRINT"YOU SUFFER";Q;"POINTS DAMAGE"
4035 CH=CH-Q:GOSUB20260:IFCH<1THENGOSUB19000:GOTO9000
4040 J=INT(RND(0)*I*L*200+1):PRINT"IT'S WORTH";J;"GOLD"
4045 GD=GD+J:IFI=0ANDRND(0)>.8THENGOSUB20300:GOSUB17000:GOTO4200
4050 GOSUB20300:GOTO4900
4100 IFRND(0)>.5THEN4200
4105 PRINT"YOU HAVE FOUND A TREASURE CHEST!"
4110 PRINT"enter TO OPEN IT:";
4115 IFT=1THENIFSF(2)>0ANDRND(0)>.1THENPRINT:PRINT"YOU DETECT TRAPS!";
4120 GOSUB16000:IFC$<>CHR$(13)THENPRINT"IGNORE IT":GOTO6000
4125 PRINT"OPEN IT":IFT=0THEN4140
4130 GOSUB17500:PRINT"CHEST EXPLODES!":I=RND(10*CZ):CH=CH-I
4135 PRINT"YOU SUFFER";I;"POINTS DAMAGE":GOSUB20260:IFCH<1THEN9000
4140 GOSUB17500:I=INT(RND(0)*(1000*LV^2)+1)
4145 IFRND(0)>.9THENPRINT"INSIDE, THERE ARE ONLY COBWEBS...":GOTO4900
4150 PRINT"INSIDE IS";I;"GOLD PIECES!":GD=GD+I:GOSUB20300:IFRND(0)>.5THEN4900
4155 C=1:GOSUB17500
4200 I=RND(10):PRINT"YOU SEE A ";
4205 J=INT(RND(0)^.5*(L+1)+1):IFI>7THENPRINTI$(I):GOTO4215
4210 PRINTI$(I);" +";J
4215 PRINT"enter TO PICK IT UP:";:GOSUB16000:IFC$=CHR$(13)THEN4225
4220 PRINT"LEAVE IT":GOTO4240
4225 IFI<>1ORT<>1THEN4235
4230 PRINT:PRINT"IT'S A HOSTILE SWORD!":I=RND(I*5)
4232 PRINT"YOU SUFFER";I;"DAMAGE POINTS":CH=CH-I:GOSUB20300:IFCH<1THEN9000
4234 GOTO4900
4235 PRINT"IT'S YOURS!":I(I)=I(I)+1:IFI<8THENI(I)=J
4237 GOSUB20300
4240 IFC=1ANDRND(0)>.5THENGOSUB17500:GOTO4200
4900 PRINT@P*270," ";:GOSUB17500
4910 IFRND(1)>.5THENGOSUB30000:IFM<>0THEN3010
6000 X=CX:Y=CY:Z=CZ-1:GOSUB10010
6002 J=INT(H/256):I=INT(L/256)
6005 IFCZ=1THENJ=0
6010 IFI>9THENI=I-9:GOTO6010
6012 IFJ>9THENJ=J-9:GOTO6012
6013 IFCZ=50ANDI=4THENI=0
6014 IFI=0ANDJ<>4THEN8000
6015 IFJ=4THEN6500
6020 ONIGOTO6100,6300,6400,6500,6600,6700,6800,6900,7100
6100 IFCZ<>1THEN6200
6102 J=4:GOTO6500
6103 Q1=CX*CY:Q2=CX+CY:Q3=CX*3+CY*7:Q1=Q1-INT(Q1/10)*10+1:Q2=Q2-INT(Q2/10)*10+1:Q3=Q3-INT(Q3/10)*10+1:M$=IN$(Q1,1)+" "+IN$(Q2,2)+" "+IN$(Q3,3)+" "
6105 GOSUB17000:FORQ=0TO11:PRINT@P*Q*64,"                                ";:NEXT:FORQ=205TO214:CC=Q:C1=4:GOSUB9:CC=Q+192:GOSUB9:NEXT:CC=333:GOSUB9:CC=334:GOSUB9:CC=341:GOSUB9:CC=342:GOSUB9:CC=269:GOSUB9:CC=270:GOSUB9:CC=277:GOSUB9:CC=278:GOSUB9
6106 CC=141:C1=5:GOSUB9:CC=142:GOSUB9:CC=143:GOSUB9:CC=144:GOSUB9:CC=80:GOSUB9
6107 CC=81:GOSUB9:CC=82:GOSUB9:CC=83:GOSUB9:CC=147:GOSUB9:CC=148:GOSUB9:CC=149:GOSUB9:CC=150:GOSUB9:PRINT@P*335,"X";
6108 PRINT@P*450,IN$(Q1,1):PRINT@P*515,IN$(Q2,2):PRINT@P*580,IN$(Q3,3)
6110 PRINT@P*270,"INN";:GOSUB16500:PRINT"YOU HAVE FOUND THE ";M$
6111 PRINT"THEY CASH IN YOUR GOLD":EX=EX+GD:TG=TG+GD:GD=0:GOSUB20300
6115 GOSUB18000:GOSUB30200:GOSUB17000:CH=HP:PRINT"YOU HAVE";TG;"IN THE SAFE"
6116 FORI=0TO11:SF(I)=0:NEXT
6119 CS=SU:PRINT"YOU SPEND THE NIGHT":GOSUB19000
6120 PRINT"YOU FEEL BETTER":GOSUB20300:GOSUB17000
6125 PRINT"enter TO RETURN TO DUNGEON,"
6130 PRINT"<CLEAR> TO SAVE CHARACTER ON CASSETTE:";
6135 GOSUB16000:IFC$="@"THEN6135
6140 IFC$=CHR$(31)ANDNM$<>"DEMO"THENPRINT:GOTO8600
6142 IFC$<>CHR$(13)THENPRINT"???";:GOSUB17500:GOTO6125
6145 PRINT"REENTER";:GOSUB17500:CZ=1:GOTO2000
6200 PRINT@P*270,"ELV";:GOSUB16500:PRINT"YOU FEEL HEAVY FOR A MOMENT"
6210 GOSUB19000:CZ=CZ-1:GOTO2000
6300 IFCZ=50THEN6200
6302 PRINT@P*270,"PIT";:GOSUB16500:PRINT"YOU SEE A PIT"
6303 IFSF(5)>0THENPRINT"YOU ARE HOVERING ABOVE A PIT":GOTO6310
6305 IFRND(20)-1>ST(4)+I(5)THEN6330
6310 PRINT"DO YOU WANT TO DESCEND?";:GOSUB16000:IFC$="Y"THEN6320
6315 PRINT"NO":GOSUB17500:GOTO8000
6320 PRINT"YES":GOTO6335
6330 PRINT"YOU FALL IN!":L=3:GOSUB20000:GOSUB19000
6335 CZ=CZ+1:GOTO2000
6400 PRINT@P*270,"TPT";:GOSUB16500
6403 PRINT"ZZAP! YOU'VE BEEN TELEPORTED...":GOSUB20300
6405 IF((CX+CY)AND1)=0THENCZ=CZ-1:IF((CX+CY)AND2)=2THENCZ=CZ+2
6410 CX=CZ*8+CY*13:CY=CY+CZ*6+CX*17
6415 IFCX>200THENCX=CX-200:GOTO6415
6420 IFCY>200THENCY=CY-200:GOTO6420
6425 IFCZ=0THENCZ=1
6430 IFCZ>50THENCZ=50
6435 IFRND(0)>.8THEN6405
6440 GOTO2000
6500 PRINT@P*270,"";:IFJ=4THENPRINT"^";
6505 IFI=4THENPRINTCHR$(92);
6510 GOSUB17000:PRINT"YOU HAVE FOUND A CIRCULAR STAIRWAY"
6512 IFJ=4ANDCZ=1THENPRINT"YOU SEE LIGHT ABOVE"
6515 PRINT"DO YOU WANT TO ";
6520 IFJ=4THENPRINT"GO uP, ";
6525 IFI=4THENPRINT"GO dOWN, ";
6530 PRINT:PRINT"OR sTAY ON THE SAME LEVEL?";:GOSUB16000
6532 IFC$="@"THENC$="S"
6535 IF(I<>4AND(C$="D"ORC$="1"))OR(J<>4AND(C$="U"ORC$="7"))THEN6550
6540 IFC$="U"ORC$="7"THENPRINT"UP";:CZ=CZ-1:IFCZ>0THEN2000
6542 IFCZ<=0THEN6103
6545 IFC$="D"ORC$="1"THENPRINT"DOWN";:CZ=CZ+1:GOTO2000
6547 IFC$="S"ORC$="5"THENPRINT"STAY";:GOSUB17000:GOTO8000
6550 PRINT"???";:GOTO6510
6600 PRINT@P*270,"ALT";:GOSUB17000:PRINT"YOU HAVE FOUND A HOLY ALTAR"
6605 PRINT"enter TO WORSHIP";:GOSUB16000:IFC$=CHR$(13)THEN6630
6610 IFRND(0)>.7THEN8000
6615 PRINT:PRINT"DIRTY PAGAN TRASH!":PRINT@P*270,"#  ";
6620 M=INT(RND(0)*20+1):GOSUB20700:IFUN=0THEN6620
6625 GOSUB17500:GOTO3007
6630 PRINT:PRINT"enter TO DONATE";:GOSUB16000
6632 IFC$<>CHR$(13)THEN6610
6634 PRINT:PRINT"HOW MUCH GOLD?";:GOSUB20800:GOSUB17000
6636 IFC>GDTHENPRINT"YOU DON'T HAVE THAT MUCH!";:GOTO6615
6640 IFC<50*CZTHENPRINT@P*832,"";:GOTO6615
6645 GD=GD-C:GOSUB20300
6650 IFRND(0)<C/(GD+C)THEN6660
6655 PRINT"THANK YOU FOR YOUR DONATION":GOSUB17500:GOTO8000
6660 I=RND(7):IFSF(I)<0THENSF(I)=0
6665 SF(I)=SF(I)+INT(RND(0)*100*C/(GD+C)+1)
6680 PRINT"YOU'VE BEEN HEARD":GOSUB17500:GOTO8000
6700 PRINT@P*270,"FNT";:GOSUB17000:PRINT"YOU HAVE FOUND A FOUNTAIN"
6701 C=RND(5)
6702 PRINT"WITH RUNNING ";MID$("WHITEBLACKGREENCLEARRED",C*5-4,5);" WATER"
6705 PRINT"enter TO DRINK SOME:";:GOSUB16000:IFC$=CHR$(13)THEN6715
6710 GOSUB17000:GOTO8000
6715 GOSUB17000:IFRND(0)>.6THEN6750
6717 IFRND(0)>1-C*.15THEN6730
6720 PRINT"YOU FEEL BETTER":GOSUB19000:I=RND(3*CZ)
6725 PRINT"YOU HEAL";I;"POINTS":CH=CH+I:IFCH>HPTHENCH=HP
6727 GOSUB20260:GOSUB17500:GOTO8000
6730 IFRND(0)>.15*CTHEN6745
6735 PRINT"IT'S POISON!":I=RND(3*CZ):PRINT"YOU LOSE";I;"HIT POINTS"
6740 CH=CH-I:GOSUB20260:IFCH<1THEN9000
6742 GOSUB17500:GOTO8000
6745 GOSUB20100:GOSUB20300:GOSUB17500:GOTO8000
6750 IFRND(0)>.4THENPRINT"YOU FEEL REFRESHED!":GOSUB17500:GOTO8000
6752 IFRND(0)>.5THEN6780
6755 IFRND(1)>.5THEN6770
6760 PRINT"MAGICAL POWER SURGES THROUGH YOUR BODY":CS=CS+RND(4*CZ)
6765 PRINT"YOU NOW HAVE";CS;"SPELLS":GOSUB20250:GOSUB17500:GOTO8000
6770 PRINT"YOU HAVE BEEN DISPOSESSED!":FORI=1TO10:I(I)=0:NEXT:GOSUB19000
6775 GOSUB17500:CLS:GOSUB20300:GOSUB15000:GOSUB30025:GOTO8000
6780 PRINT"YOU FEEL REFRESHED!":GOSUB17500:PRINT"ACTUALLY YOU'RE DRUNK!"
6785 IFSF(11)<0THENSF(11)=0
6786 SF(11)=SF(11)+RND(16):GOSUB15000:GOSUB17000:GOTO8000
6800 PRINT@P*270,"GCB";:GOSUB17000:PRINT"YOU SEE A LARGE GRAY MISTY CUBE"
6805 PRINT"enter TO WALK IN:";:GOSUB16000:IFCHR$(13)<>C$THEN8000
6810 IFRND(0)>.2THEN6815
6812 CZ=RND(50):GOTO6820
6815 PRINT:PRINT"A NUMBER FROM 1 TO 50:";:GOSUB20800:IFC$="@"THEN6812
6817 IFC<1ORC>50THENPRINT"NO YOU FOOL!";:GOSUB17500:GOTO6810
6818 CZ=C
6820 FORQ=0TO11:PRINT@P*Q*64,"                                ";:NEXT:PRINT@P*131,"YOU FLOAT";:PRINT@P*198,"IN SPACE....";:GOTO2000
6900 PRINT@P*270,"THR";:GOSUB17000:PRINT"YOU SEE A JEWEL ENCRUSTED THRONE"
6905 PRINT"DO YOU WANT TO pRY SOME JEWELS,"
6910 PRINT"sIT DOWN, rEAD THE RUNES,":PRINT"OR iGNORE IT:";
6915 GOSUB16000:IFC$="I"ORC$="@"THENPRINT"IGNORE":GOSUB17500:GOTO8000
6920 IFC$<>"P"THEN6950
6925 PRINT"PRY":GOSUB17500:IFRND(0)>.7THEN7030
6930 IFRND(0)>.4THEN7025
6935 PRINT"THEY POP INTO YOUR GREEDY HANDS!":I=INT(RND(0)*1000*CZ+1)
6940 PRINT"THEY ARE WORTH";I;"GOLD":GD=GD+I:GOSUB20300:GOSUB17500:GOTO8000
6950 IFC$<>"S"THEN6985
6955 PRINT"SIT";:GOSUB17500:IFRND(0)>.7THEN7030
6960 IFRND(0)>.6THEN7025
6965 IFRND(0)>.4THEN6403
6970 PRINT"A LOUD GONG SOUNDS!";:GOSUB17500:IFRND(0)<.5THEN6975
6973 EX=INT(EX/2):IFLV=1THENEX=-1
6974 GOSUB18000:GOTO8000
6975 IFLV>CZTHENPRINT"NOTHING HAPPENS...";:GOTO7010
6980 EX=1000*2^LV:GOSUB18000:GOTO8000
6985 IFC$<>"R"THEN7015
6990 PRINT"READ";:GOSUB17500
6992 IFRND(0)>.7THEN7030
6995 IFRND(0)<ST(1)*.05THEN7005
7000 PRINT:PRINT"YOU DON'T UNDERSTAND THEM...";:GOTO7010
7005 PRINT:PRINT"A MYSTERIOUS MAGIC GRIPS YOU.":GOSUB20200
7010 GOSUB17500:GOTO8000
7015 PRINT"???";:GOSUB17500:GOTO6900
7025 PRINT"NOTHING HAPPENS...";:GOTO7010
7030 M=RND(20):GOSUB19700
7032 PRINT"THE ";M$;" KING RETURNS!":GOSUB17500:GOSUB20700
7035 PRINT@P*270,"#  ";:ML=RND(CZ*5)+4:GOTO3010
7100 PRINT@P*270,"SAF";:GOSUB17000
7105 PRINT"YOU SEE A SMALL BOX WITH FOUR COLORED"
7110 PRINT"LIGHTS. pUSH BUTTONS OR iGNORE:";:GOSUB16000
7115 IFC$<>"P"THENPRINT"IGNORE":GOSUB17500:GOTO8000
7120 PRINT"PUSH":GOSUB17500:CB=1
7125 PRINT"PUSH rED, gREEN, yELLOW, bLUE,":PRINT"OR sTOP:";
7130 GOSUB16000:IFC$="S"ORC$="@"THENPRINT"STOP":GOSUB17500:GOTO8000
7135 FORC=1TO4:IFC$=MID$(B$,C*6-5,1)THENM$=MID$(B$,C*6-5,6)::GOTO7145
7140 NEXT:PRINT"???":GOSUB17500:GOTO7125
7145 GOSUB19705:PRINTM$:IFB(CB)<>CTHEN7170
7150 CB=CB+1:IFCB<5THENGOSUB17500:GOTO7125
7155 PRINT"IT OPENS!  ";:GOSUB17500:PRINT"INSIDE YOU FIND JEWELS WORTH"
7160 GOSUB19800:I=INT(RND(0)*20000*CZ^2+1):PRINTI;"IN GOLD!":GD=GD+I:GOSUB20300
7165 GOSUB17000:GOTO8000
7170 I=RND(6*CZ):PRINT"AN ELECTRIC BOLT SHOOTS THROUGH YOU!"
7175 GOSUB17500:PRINT"YOU SUFFER"I"POINTS DAMAGE":CH=CH-I:GOSUB20260
7180 IFCH<1THEN9000
7185 GOSUB17500:GOTO8000
8000 M=0:GOSUB17000:GOSUB16500:PRINT"->";
8020 GOSUB16000:IFC$="@"THENC$="S"
8100 Q=1:FORC=QTO20:ON-(MID$(CM$,C,Q)=C$)GOTO8117:NEXT
8115 PRINT@P*704,"NO";:FORI=1TO200:NEXT:GOTO8000
8117 IFC>5THENC=C-5
8118 IFSF(11)>0ANDC<6THENPRINT"YOU'RE CONFUSED ->";:C=RND(5)
8120 ONCGOTO8150,8200,8250,8300,8350,8800,8700,8900,8970,8970,8600,8910,8925,8945
8150 IF(LAND3)>2ANDSF(8)<1ORCY=1THEN8115
8155 PRINT"NORTH";:CY=CY-1:GOSUB30100:GOTO2000
8200 IF(P(2,1)AND3)>2ANDSF(8)<1ORCY=200THEN8115
8205 PRINT"SOUTH";:CY=CY+1:GOSUB30100:GOTO2000
8250 IF(INT(L/4)AND3)>2ANDSF(8)<1ORCX=1THEN8115
8255 PRINT"WEST";:CX=CX-1:GOSUB30100:GOTO2000
8300 IF(INT(P(1,2)/4)AND3)>2ANDSF(8)<1ORCX=200THEN8115
8305 PRINT"EAST";:CX=CX+1:GOSUB30100:GOTO2000
8350 PRINT"STAY";:PRINT@P*270,"   ";:GOSUB30000:IFM<>0THEN3010
8355 SY=0:GOTO2000
8600 PRINT"STORE ";NM$;:IFNM$<>"DEMO"THEN8615
8601 PRINT:PRINT"CANNOT STORE DEMONSTRATION CHARACTER!":GOSUB17500:GOTO8670
8615 STOP
8670 GOSUB17500:IFCZ=0THEN6125
8675 GOTO8000
8700 PRINT"QUIT":PRINT"ARE YOU SURE?";:GOSUB16000
8702 IFC$<>"Y"THENPRINT"NO":GOSUB17500:GOTO8000
8710 PRINT"YES":PRINT"GOOD BYE CRUEL WORLD!":GOSUB19000:GOTO9000
8800 CLS:PRINT"COMMANDS ARE:":PRINT
8850 PRINT:PRINT"press any key";:GOSUB16000:GOSUB17500:CLS:GOSUB20300:GOSUB15000:GOSUB30025:GOTO8000
8900 PRINT"RE-PLOT":GOSUB19000:CLS:GOSUB20300:GOSUB15000:GOSUB30025:GOTO8000
8910 PRINT"USE SCROLL OF RESCUE"
8915 IFI(8)<1THEN8920
8917 I(8)=I(8)-1:CX=25:CY=13:CZ=1:PRINT"*ZAP!*":GOSUB19000:GOSUB30025
8918 GD=0:GOSUB20300:GOTO2000
8920 PRINT"YOU DON'T HAVE ONE!"
8922 GOSUB17500:GOTO8000
8925 PRINT"DRINK HEALING POTION":IFI(9)<1THEN8920
8935 I(9)=I(9)-1:GOSUB20300:CH=CH+RND(20):IFCH>HPTHENCH=HP
8940 PRINT"YOU FEEL BETTER!":GOSUB20300:GOTO8922
8945 PRINT"DRINK GIANT STRENGTH POTION":IFI(10)<1THEN8920
8950 IFSF(1)<0THENSF(1)=0
8955 SF(1)=SF(1)+19+RND(20):PRINT"STRENGTH FLOWS THROUGH YOUR BODY"
8960 I(10)=I(10)-1:GOSUB20300:GOTO8922
8970 D=0:GOTO21000
9000 GOSUB17000:PRINT"YOU DIED!":GOSUB17500
9001 IFSF(10)>0THEN27205
9002 CLS:PRINT@P*192,"ANOTHER ";:IFLV<4THENPRINT"NOT SO ";
9005 PRINT"MIGHTY ADVENTURER":PRINT"BITES THE DUST"
9010 PRINT@P*512,"TRY AGAIN?";
9012 GOSUB16000:IFC$="N"ORC$="9"THEN9020
9013 IFC$="@"THEN9012
9015 PRINT"YES":RUN
9020 END
10010 Q=X*XO+Y*YO+Z*ZO+(X+XO)*(Y+YO)*(Z+ZO)
10020 H=(Q-INT(Q))*WO:IFINT(H/256)>5THENH=HAND255
10025 IFINT(H/256)>0THENH=(INT((Q*10-INT(Q*10))*15+1)*256)OR(HAND255)
10030 IFX=1ORX=201THENH=HOR12
10035 IFY=1ORY=201THENH=HOR3
10040 RETURN
10050 IFW<2THENRETURN
10051 FORQ=0TO10:CC=L+Q:C1=4:GOSUB9:NEXT:IFW>2THENRETURN
10051 FORQ=3TO7:CC=L+Q:C1=3:GOSUB9:NEXT:RETURN
10075 IFW<2THENRETURN
10076 FORQ=0TO192STEP64:CC=L+Q:C1=4:GOSUB9:CC=L+Q+1:GOSUB9:NEXT:IFW>2THENRETURN
10077 CC=L+65:C1=2:GOSUB9:CC=L+64:GOSUB9:CC=L+129:GOSUB9:CC=L+128:GOSUB9:RETURN
15000 Z=CZ:FORQ2=0TO3:Y=CY+Q2-1:FORQ1=0TO3:X=CX+Q1-1:GOSUB10010
15005 P(Q2,Q1)=H:NEXT:NEXT
15006 FORQ=0TO11:PRINT@P*Q*64,"                                ";:NEXT:PRINT@P*640,"";
15007 Q=0:FORI=1TO11
15008 IFSF(I)<1THEN15011
15009 ON-(POS(0)<27)GOTO15010:Q=1:PRINT@P*640-64*Q,"";
15010 PRINTMID$("STRGDTRPLGHTPROTLEVTINVSFEARASTWTMSTRSEDDRNK",I*4-3,4);":";
15011 NEXT:IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN15015
15012 L=2:W=(P(0,0)AND3):GOSUB10050
15015 IF(P(1,0)AND3)>1OR((INT(P(1,1))/4)AND3)>1THEN15025
15020 L=2:W=(INT(P(0,0)/4)AND3):GOSUB10075
15025 IF(P(1,1)AND3)>1THEN15040
15030 L=12:W=(P(0,1)AND3):GOSUB10050:W=(INT(P(0,1)/4)AND3):GOSUB10075
15035 L=22:W=(INT(P(0,2)/4)AND3):GOSUB10075
15040 IF(P(1,1)AND3)>1OR(INT(P(0,2)/4)AND3)>1THEN15045
15042 L=22:W=P(0,2)AND3:GOSUB10050
15045 IF(INT(P(1,2)/4)AND3)>1OR(P(1,2)AND3)>1THEN15055
15050 L=32:W=INT(P(0,3)/4)AND3:GOSUB10075
15055 IF(INT(P(1,2)/4)AND3)>1THEN15080
15060 L=214:W=(P(1,2)AND3):GOSUB10050:L=224:W=INT(P(1,3)/4)AND3:GOSUB10075
15070 L=406:W=P(2,2)AND3:GOSUB10050
15080 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN15085
15082 L=598:W=P(3,2)AND3:GOSUB10050
15085 IF(INT(P(1,2)/4)AND3)>1OR(P(2,2)AND3)>1THEN15100
15090 L=416:W=(INT(P(2,3)/4)AND3):GOSUB10075
15100 IF(P(2,1)AND3)>1THEN15120
15105 L=406:W=INT(P(2,2)/4)AND3:GOSUB10075
15110 L=588:W=P(3,1)AND3:GOSUB10050:L=396:W=INT(P(2,1)/4)AND3:GOSUB10075
15120 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN15125
15122 L=578:W=P(3,0)AND3:GOSUB10050
15125 IF(INT(P(1,1)/4)AND3)>1OR(P(2,0)AND3)>1THEN15140
15130 L=386:W=INT(P(2,0)/4)AND3:GOSUB10075
15140 IF(INT(P(1,1)/4)AND3)>1THEN15155
15145 L=386:W=P(2,0)AND3:GOSUB10050
15150 L=194:W=INT(P(1,0)/4)AND3:GOSUB10075:W=P(1,0)AND3:GOSUB10050
15155 L=204:W=INT(P(1,1)/4)AND3:GOSUB10075:W=P(1,1)AND3:GOSUB10050
15160 L=214:W=INT(P(1,2)/4)AND3:GOSUB10075:L=396:W=P(2,1)AND3:GOSUB10050
15200 CC=337:C1=6:GOSUB9:L=P(1,1):IFSF(3)<=0THENRETURN
15300 IFINT(P(0,0)/256)=0THEN15320
15305 IF(P(1,0)AND3)<AAND(INT(P(1,1)/4)AND3)<2THEN15315
15310 IF(P(1,1)AND3)>1OR(INT(P(0,1)/4)AND3)>1THEN15320
15315 CC=135:C1=2:GOSUB9
15320 IFINT(P(0,1)/256)<>0AND(P(1,1)AND3)<2THENCC=145:C1=2:GOSUB9
15340 IFINT(P(0,2)/256)=0THEN15360
15345 IF(P(1,1)AND3)<2AND(INT(P(0,2)/4)AND3)<2THEN15355
15350 IF(P(1,2)AND3)>1OR(INT(P(1,2)/4)AND3)>1THEN15360
15355 CC=155:C1=2:GOSUB9
15360 IFINT(P(1,2)/256)<>0AND(INT(P(1,2)/4)AND3)<2THENCC=347:C1=2:GOSUB9
15380 IFINT(P(2,2)/256)=0THEN15400
15385 IF(P(2,2)AND3)<2AND(INT(P(1,2)/4)AND3)<2THEN15395
15390 IF(P(2,1)AND3)>1OR(INT(P(2,2)/4)AND3)>1THEN15400
15395 CC=539:C1=2:GOSUB9
15400 IFINT(P(2,1)/256)<>0AND(P(2,1)AND3)<2THENCC=529:C1=2:GOSUB9
15420 IFINT(P(2,0)/256)=0THEN15440
15425 IF(P(2,0)AND3)<2AND(INT(P(1,1)/4)AND3)<2THEN15435
15430 IF(P(2,1)AND3)>1OR(INT(P(2,1)/4)AND3)>1THEN15440
15435 CC=519:C1=2:GOSUB9
15440 IFINT(P(1,0)/256)<>0AND(INT(P(1,1)/4)AND3)<2THENCC=327:C1=2:GOSUB9
15499 RETURN
16000 FORQ=1TO300:C$=INKEY$:IFC$<>""THENRETURN
16005 NEXT:IFNM$="DEMO"THEN16000
16010 C$="@":RETURN
16500 PRINT@P*704,"";:RETURN
17000 FORQQ=0TO256STEP64:PRINT@P*704+QQ,"                               ";:NEXT:GOSUB16500:RETURN
17500 GOSUB19000:GOTO17000
18000 IFEX<1000*2^LVTHEN18040
18010 GOSUB17000:PRINT"YOU WENT UP A LEVEL!";:LV=LV+1:J=RND(ST(3))
18015 CH=CH+J:HP=HP+J:IFEX>1000*2^LVTHENEX=1000*2^LV-1
18017 CS=CS+LV:SU=SU+LV
18020 GOSUB20300:GOSUB17500:PRINT"YOU GAIN";J;"HIT POINTS":RETURN
18040 IFLV=1ANDEX>=0THENRETURN
18050 IFEX>1000*2^(LV-1)THENRETURN
18055 CS=CS-LV:SU=SU-LV:IFCS<0THENCS=0
18060 PRINT"YOU GO DOWN A LEVEL!";:LV=LV-1:J=RND(ST(3))
18065 CH=CH-J:HP=HP-J
18070 GOSUB20300:GOSUB17500:PRINT"YOU LOSE";J;"HIT POINTS"
18075 IFCH>0ANDLV>0THENRETURN
18080 GOSUB19000:GOTO9000
19000 FORQ=1TO800:NEXT:RETURN
19500 FORQ=1TO100:NEXTQ:RETURN
19700 M$=MID$(MO$,M*8-7,8)
19705 IFRIGHT$(M$,1)=" "THENM$=LEFT$(M$,LEN(M$)-1):GOTO19705
19710 RETURN
19800 FORQ=1TO4:B(Q)=RND(4):NEXT:RETURN
20000 D=INT(RND(0)*L*6+1):PRINT"YOU SUFFER"D"HIT POINTS":CH=CH-D
20005 IFCH>0THENGOSUB20260:RETURN
20010 PRINT"YOUR LIFE HAS BEEN TERMINATED":GOSUB20300:GOSUB17500:GOTO9000
20100 I=INT(RND(0)*500*CZ+1):PRINT"YOU JUST ";
20110 IFRND(0)>.5THENPRINT"LOST";:I=-I:GOTO20120
20115 PRINT"GAINED";
20120 PRINTABS(I);"EXPERIENCE POINTS":EX=EX+I:GOSUB18000:RETURN
20200 I=RND(6)-1:IFRND(0)>.5THEN20220
20210 IFST(I)=18THEN20200
20215 PRINT"YOUR ";MID$(S$,I*3+1,3);" GOES UP";:ST(I)=ST(I)+1:GOTO20240
20220 IFST(I)=3THEN20200
20225 PRINT"YOUR ";MID$(S$,I*3+1,3);" GOES DOWN";:ST(I)=ST(I)-1
20240 PRINT" BY 1":GOSUB20300:GOSUB19000:RETURN
20250 K=PEEK(16417):K1=PEEK(16416):EN=2:GOTO20320
20260 K=PEEK(16417):K1=PEEK(16416):EN=3:GOTO20320
20300 K=PEEK(16417):K1=PEEK(16416):EN=1
20321 IFEN=3THEN20396
20323 IFEN=2THEN20396
20330 REM
20335 QQ=0:FORI=1TO10:IFI<4THENIFI(I)>=0THEN20345
20340 IFI(I)<1THEN20380
20345 QQ=QQ+1:PRINT@P*QQ*64-16,"";:IFI>7THEN20365
20350 PRINTRIGHT$(I$(I),8);:IFI(I)>0THENPRINT" +";MID$(STR$(I(I)),2);"   ";:GOTO20380
20355 PRINT"     ";:GOTO20380
20365 PRINTSTR$(I(I));" ";RIGHT(I$(I),8);
20380 NEXT
20390 PRINT@P*QQ*64+48,"               ";
20396 POKE49151,64
20398 RETURN
20600 PRINT@P*673,"                  "
20605 GOSUB17000:PRINT@P*270," ";:RETURN
20700 UN=0:IFM=3ORM=5ORM=8ORM=10ORM=13ORM=17ORM=18THENUN=1
20705 RETURN
20800 D$=""
20805 GOSUB16000:IFC$<>CHR$(20)THEN20825
20810 IFD$=""THEN20805
20815 PRINT"20815";:IFLEN(D$)=1THEN20800
20820 D$=LEFT$(D$,LEN(D$)-1):GOTO20805
20825 IFC$=CHR$(13)THENC=VAL(D$):PRINT:RETURN
20826 IFC$="`"THENC=0:RETURN
20827 IFLEN(D$)>9THEN20805
20828 IFC$="-"ANDD$=""THEN20835
20830 IFC$<"0"ORC$>"9"THEN20805
20835 PRINTC$;:D$=D$+C$:GOTO20805
20900 GOSUB16500:PRINT"YOU HAVE NOT BEEN NOTICED..."
20905 PRINT"enter TO APPROACH:";:GOSUB16000
20910 IFC$=CHR$(13)THENGOSUB17000:GOTO3007
20915 PRINT@P*270," ";:GOSUB17000:GOTO4000
21000 PRINT"CAST":PRINT"SPELL LEVEL:";:GOSUB16000:C=VAL(C$):PRINTC$
21002 IFC>0ANDC<=INT(LV/3)+1ANDC<7THEN21010
21004 IFC=0THEN21910
21005 PRINT"SPELL LEVEL NOT HIGH ENOUGH!":GOSUB19000:GOTO21910
21010 IFC>CSTHENPRINT"NOT ENOUGH SPELL UNITS!":GOTO21900
21020 PRINT"SPELL( = TO LIST):";:GOSUB16000:IFC$=CHR$(13)ORC$="@"THEN21910
21025 IFC$<"1"ORC$>"6"THENC$="="
21030 IFC$="="THEN21040
21035 CS=CS-C:GOSUB20250:GOTO21050
21040 GOSUB17000:FORI=1TO3:PRINTI;LEFT$(P$(I+6*(C-1)),20);
21045 PRINTTAB(20);I+3;LEFT$(P$(I+3+6*(C-1)),19):NEXT
21046 PRINT"hit any key";:GOSUB16000:GOSUB17000
21047 IFC$<"1"ORC$>"6"THEN21020
21048 GOTO21035
21050 S=VAL(C$):ONCGOTO22000,23000,24000,25000,26000,27000
21700 MH=MH-I:PRINT"IT SUFFERS";I;"HIT POINTS":IFMH>0THEN21900
21705 GOTO3220
21750 PRINT"NOT IN MELEE!":GOSUB17500:GOTO3300
21800 PRINT"COMBAT SPELL WASTED!"
21900 GOSUB17500:IFD=1THEN3300
21905 GOTO8000
21910 GOSUB17000:IFD=1THEN3100
21915 GOTO8000
21920 GOSUB17500:GOSUB20600:GOTO4000
21930 PRINT"UNDEAD ARE ALREADY DEAD!":GOTO21900
22000 ONSGOTO22100,22200,22300,22400,22500,22600
22100 PRINTP$(1):GOSUB17500:IFD=0THEN21800
22105 I=RND(8)+4:GOTO21700
22200 PRINTP$(2):GOSUB17500:IFD=0THEN21800
22202 IFUN=1THENPRINT"UNDEAD DON'T SLEEP!":GOTO3300
22205 IFRND(20)>ST(1)THENPRINT"THE ";M$;" ISN'T SLEEPY!":GOTO3300
22210 PRINT"THE ";M$;" IS SLEEPING!":PRINT"enter TO KILL:";:GOSUB16000
22215 IFC$<>CHR$(13)THENGOSUB17000:GOTO3710
22220 IFRND(0)>.2THEN3220
22225 PRINT"IT WOKE UP!":GOSUB19000:GOTO3300
22300 PRINTP$(3):I=RND(8):PRINT"YOU FEEL BETTER":CH=CH+I
22305 IFCH>HPTHENCH=HP
22310 GOSUB20260:GOTO21900
22400 S=4:T=3:U=11
22402 PRINTP$(S):IFSF(T)<0THENSF(T)=0
22405 SF(T)=SF(T)+RND(U)+4:GOTO21900
22500 PRINTP$(5):GOSUB17500:IFD=0THEN21800
22505 IFUN=1THEN22515
22510 PRINT"THE ";M$;" IS INSULTED":PRINT"AT BEING CALLED UNDEAD":GOTO21900
22515 IFRND(0)<.05*ST(2)+.05*LV-.05*MLTHEN22525
22520 PRINT"THE ";M$;" LISTENS WITH DEAF EARS":GOTO21900
22525 ML=INT((ML/2)+.5):PRINT"IT RUNS IN FEAR!":GOTO3223
22600 S=6:T=4:U=11:GOTO22402
23000 ONSGOTO23100,23200,23300,23400,23500,23600
23100 PRINTP$(7):GOSUB17500:IFD=0THEN21800
23105 IFINT(RND(0)^2*20+ML)>ST(1)THENPRINT"THE ";M$;" DODGES ASIDE!":GOTO3300
23110 PRINT"THE ";M$;" IS WEBBED!":PRINT"enter TO KILL:";:GOSUB16000
23115 IFC$<>CHR$(13)THENGOSUB17000:GOTO3710
23120 IFRND(0)-ML/20>.2THEN3220
23125 PRINT"IT BROKE FREE!":GOSUB19000:GOTO3300
23200 S=8:T=5:U=21:GOTO22402
23300 PRINTP$(9):GOSUB17500:IFD=0THEN21800
23305 I=INT(RND(0)*12+3):GOTO21700
23400 S=10:T=2:U=21:GOTO22402
23500 PRINTP$(11):GOSUB17000:IFD=0THEN21800
23502 IFUN=1THENPRINT"THE UNDEAD IGNORE YOUR WILES!":GOTO3300
23505 IFINT((RND(0)^2)*20+1)>ST(5)THENPRINT"THE ";M$;" RESISTS YOU!":GOTO3300
23510 PRINT"THE ";M$;" IS CHARMED":PRINT"enter TO KILL:";:GOSUB16000
23515 GOTO23115
23600 S=12:T=1:U=21:GOTO22402
24000 ONSGOTO24100,24200,24300,24400,24500,24600
24100 PRINTP$(13):GOSUB17000:PRINT"ZZZZAAAAPP!":IFD=0THEN21800
24105 GOSUB19000:I=INT(RND(0)*6*LV+15):GOTO21700
24200 PRINTP$(14):I=INT(RND(0)*24+1):PRINT"YOU FEEL BETTER":CH=CH+I
24205 IFCH>HPTHENCH=HP
24210 GOSUB20260:GOTO21900
24300 S=15:T=3:U=31:GOTO22402
24400 S=16:T=6:U=21:GOTO22402
24500 PRINTP$(17):GOSUB17500:IFD=0THEN21800
24505 IFINT(RND(0)*20+ML)>ST(1)THENPRINT"THE ";M$;" IGNORES YOU!":GOTO3300
24510 PRINT"THE ";M$;" IS HELD":PRINT"enter TO KILL:";:GOSUB16000
24515 IFC$<>CHR$(13)THENGOSUB17000:GOTO3710
24520 IFRND(0)>.2+ML*.03THEN3220
24525 PRINT"IT BROKE FREE!":GOSUB19000:GOTO3300
24600 PRINTP$(18):GOSUB17500:IFD=0THEN21800
24605 IFRND(21)+ML<ST(1)THEN24615
24610 PRINT"THE ";M$;" DOESN'T BELIEVE!":GOTO3300
24615 PRINT"IT BELIEVES!....ARRGH...":GOTO3220
25000 ONSGOTO25100,25200,25300,25400,25500,25600
25100 PRINTP$(19):GOSUB17500:IFD=1THEN21750
25105 PRINT"DIRECTION ";:GOSUB16000
25110 FORI=1TO9:IFMID$("WXAD8246@",I,1)=C$THEN25117
25115 NEXT:PRINT"?":GOSUB17000:GOTO25105
25117 IFI>4THENI=I-4
25118 IFI=5THEN21900
25120 IF(I=1ANDCY>1)OR(I=2ANDCY<200)OR(I=3ANDCX>1)OR(I=4ANDCX<200)THEN25125
25122 PRINT"ONLY STONE THERE....":GOTO21900
25125 GOSUB30200:PRINT"*POOF*":ONIGOTO8155,8205,8255,8305
25200 PRINTP$(20):GOSUB17000:PRINT"WHOOOOSHH!":IFD=0THEN21800
25205 PRINT"THE ";M$;" IS BURNING!":I=INT(RND(0)*12*LV+15):GOSUB19000:GOTO21700
25300 PRINTP$(21):GOSUB17500:IFD=0THEN21800
25305 I=INT(RND(0)*32+10):GOTO21700
25400 PRINTP$(22):GOSUB17500:IFD=0THEN21800
25405 IFRND(0)>.6THENPRINT"THE ";M$;" ISN'T AFFECTED":GOTO3300
25410 PRINT"ONE STONE STATUE....":GOTO3220
25500 S=23:T=7:U=30:GOTO22402
25600 PRINTP$(24):GOSUB17500:PRINT"DIE!":IFD=0THEN21800
25603 IFUN=1THEN21930
25605 GOSUB19000:IFRND(0)>.3+ML*.4-LV*.03THEN3220
25610 PRINT"THE ";M$;" LAUGHS":GOSUB17500:GOTO3300
26000 ONSGOTO26100,26200,26300,26400,26500,26600
26100 PRINTP$(25):GOSUB17500:IFD=1THEN21750
26105 PRINT"+NORTH/-SOUTH:";:GOSUB20800:NY=C:PRINT"+EAST/-WEST:";:GOSUB20800
26110 NX=C:PRINT"+UP/-DOWN:";:GOSUB20800:NZ=C:I=SQR(NX^2+NY^2+(NZ*5)^2)-.1
26115 IFI>LV*5THENPRINT"TOO FAR...TRY AGAIN":GOSUB17500:GOTO26105
26120 NX=CX+NX:NY=CY-NY:NZ=CZ-NZ
26125 IFNX>0ANDNX<201ANDNY>0ANDNY<201ANDNZ<51THEN26135
26130 PRINT"ONLY STONE THERE....THE SPELL FAILS..":GOTO21900
26135 IFNZ<1THENPRINT"ONLY THIN AIR....THE SPELL FAILS..":GOTO21900
26140 CX=NX:CY=NY:CZ=NZ:PRINT"*POOF*":GOSUB30200:GOTO2000
26200 S=26:T=8:U=16:GOTO22402
26300 PRINTP$(27):GOSUB17500:IFD=0THEN21800
26305 PRINT"QWERTY!":GOSUB19000:IFRND(0)<.75THEN3220
26310 PRINT"THE ";M$;" DOESN'T HEAR...":GOTO21900
26400 PRINTP$(28):GOSUB17500:IFD=0THEN21800
26405 PRINT"BRRRR!":I=60:GOTO21700
26500 PRINTP$(29):GOSUB17500:PRINT@P*272,"*";:GOSUB17000:PRINT"WWHHOOOOOSSHHH!"
26505 PRINT"A WALL OF FIRE APPEARS ABOUT YOU":GOSUB19000:GOSUB17500
26507 IFD=0THEN21800
26510 PRINT@P*272," ";:GOSUB17000:IFRND(0)>.4THENPRINT"THE ";M$;" IS GONE":GOTO21920
26515 PRINT"THE ";M$;" WALKS THROUGH!":I=RND(12)+7:GOTO21700
26600 PRINTP$(30):GOSUB17500:IFD=0THEN21800
26605 IFUN=1THEN21930
26610 PRINT"BLACK DEATH FOR THE ";M$
26615 GOSUB17500:IFRND(0)>ST(2)*.05THENPRINT"IT IS IMMUNE!":GOTO21900
26620 IFRND(0)*2<ST(3)*.03+1THEN3220
26625 PRINT"THE SPELL BACKFIRES!":GOSUB17500:GOTO9000
27000 ONSGOTO27100,27200,27300,27400,27500,27600
27100 S=31:T=9:U=20:GOTO22402
27200 S=32:T=10:U=40:GOTO22402
27205 PRINTP$(32):GOSUB17500:ST(3)=ST(3)-1
27207 SF(10)=0:IFRND(0)>ST(3)*.06THEN27220
27210 PRINT"IT WORKS!":CH=HP:GOSUB20300:GOTO21900
27220 PRINT"IT DOESN'T WORK!":GOTO9000
27300 PRINTP$(33):GOSUB17500:IFD=0THEN21800
27305 FORU=1TO9:PRINT@P*724,MID$(".,+^O0* ",U,1);:GOSUB19500:NEXT
27307 PRINT@P*704,"";:GOSUB19000:IFRND(0)<.9THEN3220
27310 PRINT"THE ";M$;" DOESN'T SEE...":GOTO21900
27400 PRINTP$(34):GOSUB17500:IFD=1THEN21750
27405 FORI=0TO10:I(I)=0:NEXT:GD=0:CLS:GOSUB20300:GOSUB30200
27410 CX=25:CY=13:CZ=1:PRINT@P*262,"***ZAP!***":GOSUB19000:GOTO2000
27500 PRINTP$(35):GOSUB17500:PRINT"YOU FEEL BETTER":CH=HP:GOSUB20260
27505 GOTO21900
27600 PRINTP$(36):GOSUB17500:PRINT"A SHIFTING MULTI-COLORED WALL APPEARS!"
27605 IFD=0THEN21800
27610 FORJ=1TO16:PRINT@P*337,"*";:GOSUB19500:PRINT@P*337," ";:GOSUB19500:NEXT
27615 PRINT@P*335,"X";:PRINT@P*272," ";:GOSUB17000:PRINT"THE ";M$;" IS GONE":GOTO21920
30000 M=0:IFFM(1)=0THEN30020
30010 M=FM(1):ML=FL(1):MH=FH(1):GOSUB20700
30020 FORI=1TO19:FM(I)=FM(I+1):FL(I)=FL(I+1):FH(I)=FH(I+1):NEXT
30022 FM(20)=0:FL(20)=0:FH(20)=0:IFM<>0THENPRINTAT270,"#";
30025 X=-32:FORQ=1TO11:X=X+64:IFFM(Q)=0THENPRINT@P*X," ";:GOTO30040        
30030 PRINT@P*X,"@";
30040 NEXT:RETURN
30100 FORQ=19TO1STEP-1:FM(Q+1)=FM(Q):FL(Q+1)=FL(Q):FH(Q+1)=FH(Q):NEXT
30105 IFM=0THENFM(1)=0:FL(1)=0:FH(1)=0:GOTO30025
30110 FM(1)=M:FL(1)=ML:FH(1)=MH:GOTO30025
30200 FORQ=1TO20:FM(Q)=0:FL(Q)=0:FH(Q)=0:NEXT:GOTO30025
