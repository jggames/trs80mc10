0 CLEAR450:DIMZZ,C1,MC,M$:MC=16384:GOTO5
1 ZZ=1:C1=31:FORC1=C1TOZZSTEP-1:I$=MID$(M$,C1,1):IFI$=" "ORI$=""THEN?" "MID$(M$,ZZ,C1-ZZ):ZZ=C1+1:C1=ZZ+31:IFI$=""THENC1=0
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
3 PRINTM$;:ZZ=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=ZZ-LEN(M$)+1TOZZ:POKEC1,PEEK(C1)-64:NEXT:RETURN
4 M$="AVAILABLE ENERGY IS"+STR$(AE)+" UNITS.":GOSUB1:RETURN
5 FORD=1TO9:D(D)=1:READO$:DE$(D)=O$:NEXT
6 DATADAMAGE CONTROL,SHORT RANGE SENSORS,LONG RANGE SENSORS,PHASER BANKS,WARP ENGINES,PH. TORPEDO TUBES
7 DATASHIELD CONTROL,LIBRARY COMPUTER,SELF-DESTRUCT
8 P=10:GOSUB14000:PRINTTAB(9)"INITIALIZING...":GOSUB100:AE=3000:T=42:CO=57.29577951:CO$="DAMSRSLRSPHANAVTORSHECOMXXX"
10 EX=RND(69):NX=EX:EY=RND(89):NY=EY:GOSUB14050:IFVAL(LEFT$(GR$(QX,QY),1))>0THEN10
12 CLS3:PRINT@104,:M$=" "+CHR$(93)+" C - T R E K "+CHR$(91)+" ":GOSUB3:GOSUB50:GOSUB21300
13 PRINT@192," PLEASE ENTER YOUR NAME IN THE   SHIP'S LOG";:INPUTN$
14 GOSUB17500:GOSUB14000:PRINT" YOUR MISSION, CAPTAIN":PRINT" "N$":  DESTROY"K"
15 PRINT" KLINGONS BY STARDATE"STR$(2700+T)".":PRINT" ";:M$="PRESS ENTER TO BEGIN":GOSUB3:INPUTM:GOSUB3000
16 GOSUB16910:GOSUB22000:PRINT" WHAT ARE YOUR ORDERS,":PRINT" CAPTAIN "N$;:SOUND235,1:INPUTO$
20 FORD=1TO9:IFLEFT$(O$,3)<>MID$(CO$,3*D-2,3)THENNEXT:GOSUB15000:GOTO16
30 GOSUB14000:ONDGOSUB1000,3000,4000,5000,6000,7000,8000,10000,11000:IFKS=0THENGOSUB22100:GOTO16
31 GOTO16
50 PRINT@10*32,:M$=" ...---"+CHR$(34)+"---.       (==="+CHR$(92)+"=======)":GOSUB3
60 PRINT@11*32,:M$="'-----...--.`--<.......! !.      ":GOSUB3
70 PRINT@12*32,:M$="            "+CHR$(92)+"<...... ,----'     ":GOSUB3
80 PRINT@13*32,:M$="             ''...../           ":GOSUB3:RETURN
100 FORI=1TO7:FORJ=1TO9:GR$(I,J)=":::":G$(I,J)="***":NEXTJ,I:B=3:FORQ=1TO B
110 I=RND(7):J=RND(9):IFMID$(GR$(I,J),2,1)="b"THEN110
111 GR$(I,J)=LEFT$(GR$(I,J),1)+"b"+RIGHT$(GR$(I,J),1):NEXTQ
112 FORI=1TO7:FORJ=1TO9:GR$(I,J)=LEFT$(GR$(I,J),2)+RIGHT$(STR$(RND(7)),1):NEXTJ,I:K=24:FORQ=1TOK
150 I=RND(7):J=RND(9)
151 IFVAL(LEFT$(GR$(I,J),1))<3THENGR$(I,J)=RIGHT$(STR$(VAL(LEFT$(GR$(I,J),1))+1),1)+RIGHT$(GR$(I,J),2):NEXTQ:GOTO190
152 GOTO150
190 K1=K:RETURN
250 FORD=1TO15:SOUND100+RND(50),1:NEXT:RETURN
1000 IFD(1)<.6THENGOSUB14000:PRINTTAB(4)"*DAMAGE CONTROL IS OUT*":PRINT:RETURN
1001 IFD(1)<1THENGOSUB18000
1005 GOSUB1010:GOTO1015
1010 CLS:PRINT:PRINTTAB(8)"=DAMAGE REPORT="
1011 PRINT" DEVICE"TAB(23)"STATE OF":PRINT
1012 PRINTTAB(23)"REPAIR":FORI=1TO9:PRINTI"-"TAB(5)DE$(I)TAB(25)STR$(INT(D(I)*1000)/10)"%":NEXT:RETURN
1015 PRINT:INPUT" DEVICE TO REPAIR ( 1 - 9 )";O$:I=VAL(O$):IFI<=0THEN2310
1016 IFI>9THEN1005
1017 GOSUB14000
1020 GOSUB4
1021 PRINT" (20 UNITS REPAIRS 1% DAMAGE)":GOSUB1030:INPUT" UNITS TO EXPEND";O$:J=VAL(O$):IFJ<=0THEN2310
1022 IFJ>AETHENGOSUB16000:GOTO1020
1023 AE=AE-J:D(I)=D(I)+J/2000:IFD(I)>1THEND(I)=1
1025 GOSUB1030:RETURN
1030 PRINT:PRINT" STATE OF REPAIR OF":PRINT" "DE$(I):PRINT" IS NOW"STR$(D(I)*100)"%.":PRINT:RETURN
2000 CLS:PRINTTAB(8)"=STATUS REPORT="
2001 PRINTTAB(6)"=U.S.S.  ENTERPRISE=":GOSUB16600:GOSUB16800:PRINT:PRINT"   *** CONDITION "C$
2002 PRINT" ORIGINAL # OF KLINGONS"K1
2010 PRINT" KLINGONS IN GALAXY"TAB(23)K"
2011 PRINT" STARDATE"TAB(23)2700+TT"
2012 PRINT" STARDATES REMAINING"TAB(23)T-TT"
2013 PRINT" SHIELDS LEVEL"TAB(23)SL"
2014 PRINT" AVAILABLE ENERGY"TAB(23)AE"
2015 PRINT" PHOTON TORPEDOS"TAB(23)P"
2016 PRINT" STARBASES"TAB(23)B:PRINT:RETURN
2310 GOSUB14000:PRINT" *REPAIR ABORTED*":PRINT:RETURN
3000 GOSUB16800:IF(D(2)>.5)AND(D(2)<1)THEND=2:GOSUB18000:GOTO3010
3001 IFD(2)<=.5THENGOSUB14000:PRINT" *SHORT RANGE SENSORS ARE OUT*":PRINT:PRINT"   *** CONDITION "C$:PRINT:RETURN
3010 CLS:PRINTTAB(7)"=SHORT RANGE SCAN=":O$="€€€€€€€€€€€€€€€€€€€€":PRINTO$"  QUADRANT: "O$"  "QX","QY:PRINTO$"  SECTOR:"
3011 PRINTO$"  "SX","SY:PRINTO$"  STARDATE: "O$"  "2700+TT:PRINTO$"PH TORPS"STR$(P):PRINTO$"SHIELD"STR$(SL)
3012 PRINTO$"ENERGY"STR$(AE):PRINTO$"KLINGONS"STR$(K)
3015 SP=MC-1:IFBY>0THENPOKESP+(2*BX)+(32*BY),2
3016 POKESP+(2*SX)+(32*SY),5:IFSS>0THENFORQ=1TOSS:POKESP+(2*SX(Q))+(32*SY(Q)),42:NEXT
3017 IFKS<=0THEN3020
3018 FORQ=1TOKS:IFKS(Q)>0THENPOKESP+(2*KX(Q))+(32*KY(Q)),11
3019 NEXT
3020 PRINT"   *** CONDITION "C$:IFC$="RED ***"AND SL<200THENPRINT" SHIELDS LEVEL DANGEROUSLY LOW":GOTO3030
3021 PRINT
3030 RETURN
4000 IFD(3)<.6THENGOSUB14000:PRINT"  *LONG RANGE SENSORS ARE OUT*":PRINT:RETURN
4001 IFD(3)<1THENGOSUB18000
4010 CLS:PRINT@70,"=LONG RANGE  SCAN=":PRINTTAB(6)"FOR QUADRANT"QX","QY:PRINT
4011 FORJ=QY-1TOQY+1:PRINTTAB(9);:FORI=QX-1 TO QX+1:IFVAL(LEFT$(GR$(I,J),1))>0AND(I<>QX)AND(J<>QY)THENC=1
4020 IFI<1ORJ<1ORI>7ORJ>9THENPRINT"=== ";:NEXT:PRINT:PRINT:NEXT:PRINT:RETURN
4030 IFD(3)>=.85ANDD(8)>.9THENG$(I,J)=GR$(I,J)
4040 PRINTGR$(I,J)" ";:NEXT:PRINT:PRINT:NEXT:PRINT:RETURN
5000 GOSUB14000:IF(.45<D(4) ANDD(4)<1)THENGOSUB18000:GOTO5010
5001 IFD(4)<=.45THENPRINT" *** PHASER BANKS ARE OUT ***":PRINT:RETURN
5010 IFKS<1THENPRINT" SENSORS SHOW NO ENEMY VESSELS   IN THIS QUADRANT.":PRINT:RETURN
5011 IFC=2THEN5500
5012 IFD(8)<1THENPRINTTAB(4)"*** COMPUTER FAILURE ***":PRINTTAB(4)"*** HAMPERS ACCURACY ***"
5020 IFAE<1THEN16000
5021 PRINT:PRINT" PHASERS LOCKED ON TARGET.":GOSUB4
5024 INPUT" NUMBER OF UNITS TO FIRE";PH:IFPH<1THEN14500
5025 IFPH>AETHENGOSUB16000:GOTO5020
5030 AE=AE-INT(PH):CLS:PRINT:PRINTTAB(7)"=SENSOR REPORT=":FORI=1TO KS
5031 IFKS(I)>0THEN5035
5032 GOTO5050
5035 X2=KX(I):Y2=KY(I):GOSUB17700:H=D(4)*D(8)*(PH/VAL(LEFT$(GR$(QX,QY),1)))/R
5036 IFH<(.12*KS(I))THENPRINTTAB(4)"NO EFFECT ON KLINGON AT":PRINTTAB(7);:GOSUB14100:GOTO5060
5050 H=INT(H):IFKS(I)>0THEN5055
5051 GOTO5060
5055 KS(I)=KS(I)-H:PRINT"  "H"UNIT HIT ON KLINGON AT":PRINTTAB(7);:GOSUB14100
5056 PRINTTAB(4)"<"KS(I)"UNITS REMAINING >":IFKS(I)<1THENPRINT"   *** KLINGON DESTROYED ***":GOSUB14200
5060 NEXT:PRINT:GOSUB16950:GOSUB12000:RETURN
5500 PRINT"€€€€€€€€priority€message€€€€€€€€":GOSUB250:PRINT"starbase€to€starship€enterprise€"
5501 PRINT"PERMISSION TO USE WEAPONS"
5502 PRINT" WHILE DOCKED *DENIED* FOR"
5502 PRINT" REASONS OF SAFETY.":RETURN
6000 IFD(5)<1THENGOSUB18000
6005 GOSUB17000:PRINT" ENTERPRISE IS AT":GOSUB16600
6010 PRINT" COURSE ( 0 - 8 )";:INPUTO$:IFO$=""THEN16050
6011 H=VAL(O$):IFH>8ORH<0THENGOSUB16060:GOTO6010
6015 IFD(5)<.26THENPRINT" ONLY IMPULSE POWER AVAILABLE.":WM=.2:GOTO6020
6016 WM=INT(D(5)*8)
6020 PRINT" ENTER WARP SPEED ( 0 -"WM")";:INPUTO$:IFO$=""THEN16050
6021 W=VAL(O$):IFW>WMORW<0THENGOSUB16060:GOTO6020
6025 IF(2.5*W>SL+AE)THENGOSUB16000:GOTO6020
6030 DI=W*10:WT=2.5*W:IFWT>SL+AETHENGOSUB16000:GOTO6020
6031 IFWT>AETHEN6035
6032 AE=AE-INT(WT+1):GOTO6040
6035 WT=WT-AE:AE=0:SL=SL-INT(WT+1):GOSUB14000
6036 M$="DILITHIUM CRYSTALS ARE NOW EXHAUSTED. SHIELDS SUPPLY POWER TO COMPLETE MANEUVER.":GOSUB1
6037 GOSUB16900:FORD=1TO3500:NEXT
6040 GOSUB3000:TT=TT+1:H=H*45:FORV=1TO DI:HX=NX:HY=NY:NX=EX+V*SIN(H/CO):NY=EY-V*SIN((90-H)/CO)
6041 IFNX<0ORNX>70ORNY<0ORNY>90THEN19000
6070 OX=QX:OY=QY:XO=SX:YO=SY:GOSUB14050:IFOX<>QXOROY<>QYTHENC=0:GOSUB17500:GOSUB16800:GOSUB3000
6080 IFKS>0THEN6085
6081 GOTO6090
6085 FORZ=1TOKS
6086 IFSX=KX(Z)ANDSY=KY(Z)THENGOSUB16700:GOSUB14000:GOSUB16910:GOSUB16550:PRINT" DUE TO KLINGON INTERCEPTION.":?:GOTO6600
6087 NEXTZ
6090 IFBS>0ANDSX=BXANDSY=BYTHEN6095
6091 GOTO6100
6095 GOSUB16700:GOSUB16750:C=2:GOSUB14000:GOSUB16910:PRINT" DOCKED WITH STARBASE AT":GOSUB16600
6096 PRINT:PRINT" ALL DAMAGE REPAIRED; ALL"
6097 PRINT" LEVELS RETURNED TO MAXIMUM.":PRINT:FORZ=1TO9:D(Z)=1:NEXT:SL=500:AE=3000:P=10:GOTO6600
6100 IFSS>0THEN6105
6101 GOTO6200
6105 FORZ=1TOSS:IFSX=SX(Z)ANDSY=SY(Z)THEN6107
6106 NEXTZ:GOTO6200
6107 SOUND50,2:SOUND1,1:GOSUB16700:GOSUB14000:GOSUB16910:GOSUB16550:PRINT" DUE TO BAD NAVIGATION.":PRINT:GOTO6600
6200 CC=C:C=C*-(C<>2):IFC<>CCTHENGOSUB16800:PRINT@355,"*** CONDITION "C$
6400 IFD(2)>=.5 THENPS=SP+(2*XO)+(32*YO):IFPEEK(PS)=5THENPOKEPS,128
6450 IFD(2)>=.5 THENPOKESP+(2*SX)+(32*SY),5:PRINT@150,SX","SY:PRINT@214,STR$(2700+TT);:PRINT@314,STR$(AE);:PRINT@416,
6500 NEXTV:EX=NX:EY=NY:C=C*-(C<>2)
6600 IFKS>0THENGOSUB12000:RETURN
6601 RETURN
7000 GOSUB14000:IFD(6)<.7THENPRINT" *PHOTON TORPEDO TUBES ARE OUT*":PRINT:RETURN
7001 IFD(2)<.5THENPRINT"  *PHOTON TORPEDOS INOPERABLE*":PRINT" *WITHOUT SHORT  RANGE SENSORS*":PRINT:RETURN
7002 IFP<1THENPRINT" *ALL PHOTON TORPEDOS EXPENDED*":PRINT:RETURN
7005 IFD(6)<1THENGOSUB18000:GOSUB17000:GOTO7020
7006 IFC=2THEN5500
7007 GOSUB17000
7020 PRINT" ENTER PHOTON TORPEDO COURSE":INPUT" ( 0 - 8 )";O$:IFO$=""THEN14500
7021 PC=VAL(O$):IFPC<0ORPC>8THENGOSUB16060:GOTO7020
7022 GOSUB3000:P=P-1:PRINT@385,"torpedo€away!":GOSUB16960:PRINT" TRACKING TORPEDO:";:IFD(2)>.5 THENPRINT@252,P;
7060 PC=PC*45:FORM=1TO50:T1=INT(SX*4+M*.8741572*SIN(PC/CO))-1
7061 T2=INT(SY*2-M*.485643*SIN((90-PC)/CO))+1
7062 IFT1<0ORT2<2ORT1>39ORT2>21THENPRINT@384,"TORP LEAVES QUADRANT AT"STR$(INT(TX/4)+1)","INT(TY/2):GOTO6600
7063 PRINT@434,INT(T1/4)+1","INT(T2/2)
7065 IFM<3ANDPOINT(T1,T2)<>0THENTX=T1:TY=T2:NEXT:GOTO7070
7066 IFPOINT(T1,T2)<>0THEN7070
7067 SET(T1,T2,5):FORD=1TO8:NEXT:RESET(T1,T2):TX=T1:TY=T2:NEXT:RETURN
7070 PE=MC+INT(T1/2)+INT(T2/2)*32:PC=PEEK(PE):GOSUB19600
7071 IFPC=42THENPOKEPE,PC:PRINT@384,"STAR ABSORBS TORP AT"INT(T1/4)+1","INT(T2/2):GOTO6600
7080 IFPC=2THENPRINT@384,"STARBASE AT"INT(TX/4)+1","INT(TY/2)"DESTROYED":GOSUB15900:GOSUB19900:GOTO6600
7090 PRINT@384,"   *** KLINGON DESTROYED ***":FORI=1TO3:IF(SP+2*KX(I)+32*KY(I)=PE)THENGOSUB14200:GOTO6600
7091 NEXT
8000 IFD(7)<.6THENGOSUB14000:PRINTTAB(4)"*SHIELD CONTROL IS OUT*":PRINT:RETURN
8001 IFD(7)<1THENGOSUB18000
8005 GOSUB14000:PRINTTAB(8)"=SHIELDS CONTROL=":PRINT
8010 PRINT" PRESENT SHIELDS ENERGY":PRINT" LEVEL IS"SL"UNITS.":GOSUB4
8011 PRINT" ENTER NEW SHIELDS LEVEL";:INPUTO$:SR=VAL(O$):IFO$=""ORSR=SLTHENPRINT" SHIELDS LEVEL UNCHANGED.":PRINT:RETURN
8015 IFSR>500THENGOSUB14000:PRINTSR"UNITS EXCEEDS MAXIMUM":PRINT" SHIELDS CAPACITY.":PRINT:GOTO8010
8020 IF(SR-SL)>AETHENGOSUB16000:GOTO8010
8021 IFSR>SLTHENQ=SR-SL:AE=AE-Q:SL=SR:PRINT" SHIELDS INCREASED TO:"SL:PRINT:RETURN
8022 IFSR<SLTHENQ=SL-SR:AE=AE+Q:SL=SR:PRINT" SHIELDS DECREASED TO:"SL:PRINT:RETURN
8040 PRINT" SHIELDS LEVEL UNCHANGED.*":RETURN
9000 CLS:PRINTTAB(7)"=THE KNOWN GALAXY=":PRINT" ENTERPRISE AT QUADRANT"QX","QY:PRINT
9001 PRINT"    1   2   3   4   5   6   7":FORJ=1TO9:PRINTSTR$(J);:FORI=1TO7:PRINT" "G$(I,J);:NEXT:PRINT:NEXT:PRINT:RETURN
10000 IFD(8)<.8THENPRINT"   *LIBRARY COMPUTER IS OUT*":PRINT:RETURN
10001 IFD(8)<1THENGOSUB18000
10010 CLS:PRINT:PRINTTAB(7)"=LIBRARY COMPUTER=":PRINT:PRINT" w o r k i n g...":PRINT
10011 GOSUB250:PRINTTAB(4);CHR$(91)"1"CHR$(93)" CUMULATIVE GALACTIC"
10012 PRINTTAB(8)"RECORD"
10013 PRINTTAB(4);CHR$(91)"2"CHR$(93)" SHIP STATUS":PRINT:PRINT
10050 INPUT" ENTER FUNCTION NUMBER";F:IFF>2THEN10050
10051 IFF<1THENPRINT" COMPUTER OFF.":PRINT:RETURN
10052 ONFGOTO9000,2000
11000 GOSUB14000:PRINTTAB(5)"€self€destruct€":PRINT:GOSUB16900:PRINT" CAPTAIN "N$", DO"
11001 PRINT" YOU VERIFY";:INPUTX$:IFLEFT$(X$,1)<>"Y"THENPRINT:PRINT" *** WHEW! ***":PRINT:RETURN
11002 GOTO13000
12000 IFKS>0THEN12002
12001 RETURN
12002 PRINT"¿¿¿¿¿¿¿ SENSORS INDICATE ¿¿¿¿¿¿¿¿¿¿¿¿¿ KLINGONS ATTACKING ¿¿¿¿¿¿";:GOSUB16900:GOSUB13900
12003 PRINT"¿ ENTERPRISE ENGAGED IN COMBAT ¿":PRINT:FORM=1TOKS
12005 IFKS(M)<1THEN12040
12006 XX=KX(M):YY=KY(M)
12010 KX(M)=KX(M)+(KX(M)>SX+1)-(KX(M)<SX-1):KY(M)=KY(M)+(KY(M)>SY+1)-(KY(M)<SY-1):FORQ=1TOSS
12011 IFKX(M)=SX(Q)ANDKY(M)=SY(Q)THEN12050
12012 NEXTQ:IFKX(M)=BX ANDKY(M)=BYTHEN12050
12013 FORQ=1TOKS:IFKX(M)=KX(Q)ANDKY(M)=KY(Q)ANDM<>QTHEN12050
12014 NEXT
12015 IFKS(M)>0THEN12017
12016 GOTO12040
12017 IFC=2THEN12060
12018 X2=KX(M):Y2=KY(M):GOSUB17700:H=INT((KS(M)/R)*(2*RND(0)))
12020 I=M:SL=SL-H:PRINTH"UNIT HIT ON ENTERPRISE":PRINTTAB(5)"FROM";:GOSUB14100
12021 PRINT" <SHIELDS DOWN TO "SL"UNITS>":IFSL<1THENGOSUB13200:GOSUB13000:GOTO1240
12022 IF(H/SL)>=.14THENGOSUB13500
12040 GOSUB13200:NEXTM:RETURN
12050 KX(M)=XX:KY(M)=YY:GOTO12015
12060 PRINT"   *STARBASE SHIELDS PROTECT*":PRINTTAB(9)"*ENTERPRISE*":GOTO12040
13000 PRINT:PRINT" ENTERPRISE IS DESTROYED!":GOSUB15900:GOSUB15900:FORD=1TO9:D(D)=0:NEXT:GOTO23000
13200 IFKS(M)>0THENFORD=1TO15:SOUND180+M*5,1:NEXT:FORD=1TO600:NEXT:RETURN
13201 RETURN
13500 D=RND(9):PRINT">DAMAGE TO "DE$(D):D(D)=D(D)-INT(H/5)/100:IFD(D)<0THEND(D)=0:RETURN
13501 RETURN
13900 PRINT@482,">>PRESS ANY KEY TO CONTINUE";:IFINKEY$=""THEN13900
13901 CLS:PRINT:RETURN
14000 CLS:PRINT@96,:RETURN
14050 QX=INT(NX/10)+1:QY=INT(NY/10)+1:SX=INT(NX-INT(NX/10)*10)+1:SY=INT(NY-INT(NY/10)*10)+1:RETURN
14100 PRINT" SECTOR"KX(I)","KY(I):RETURN
14200 GR$(QX,QY)=RIGHT$(STR$(VAL(LEFT$(GR$(QX,QY),1))-1),1)+RIGHT$(GR$(QX,QY),2)
14201 K=K-1:KS(I)=0:KX(I)=0:KY(I)=0:IFD(2)=1THENG$(QX,QY)=GR$(QX,QY)
14210 IFLEFT$(GR$(QX,QY),1)="0"THENKS=0:RETURN
14211 RETURN
14500 PRINT:PRINT" FIRING ABORTED.":PRINT:RETURN
15000 CLS:PRINT:PRINTTAB(3)"=list€of€valid€commands=":PRINT
15001 PRINT"   srs"TAB(9)"SHORT RANGE SCAN"
15002 PRINT"   lrs"TAB(9)"LONG RANGE SCAN"
15003 PRINT"   nav"TAB(9)"NAVIGATION"
15004 PRINT"   pha"TAB(9)"FIRE PHASERS"
15005 PRINT"   tor"TAB(9)"FIRE PHOTON TORPEDOS"
15010 PRINT"   she"TAB(9)"SHIELD CONTROL"
15011 PRINT"   com"TAB(9)"LIBRARY COMPUTER"
15012 PRINT"   dam"TAB(9)"DAMAGE CONTROL"
15013 PRINT"   xxx"TAB(9)"SELF-DESTRUCT":PRINT:RETURN
15500 GOSUB15900:GOSUB14000:M$="YOU'VE RUN OUT OF STARDATES! THE KLINGONS ARE RUNNING THINGS NOW...":GOSUB1:GOTO23000
15600 GOSUB15900:GOSUB14000:M$="THE ENTERPRISE IS COMPLETELY OUT OF ENERGY! THE MISSION IS LOST!":GOSUB1:GOTO23000
15800 FORD=1TO1500:NEXT:CLS3:PRINT@96,"€message€from€starfleet€command€":GOSUB250
15801 PRINT" YOU HAVE DESTROYED THE ENTIRE":PRINT" KLINGON INVASION FLEET!"
15802 PRINT" CONGRATULATIONS!!":GOSUB21300:GOSUB13900:GOTO23000
15900 SOUND176,2:SOUND147,2:SOUND117,2:SOUND89,2:SOUND32,12:RETURN
16000 CLS:PRINT@64," ENGINEERING CANNOT COMPLY WITH  YOUR ORDER DUE TO INSUFFICIENT  AVAILABLE ENERGY.":PRINT:RETURN
16050 PRINT:PRINT" MANEUVER ABORTED.":PRINT:RETURN
16060 PRINT" HELM CANNOT COMPLY ---":PRINTTAB(6)"TRY AGAIN:":RETURN
16550 PRINT" WARP ENGINES SHUT DOWN AT"
16600 PRINT" QUADRANT"QX","QY"SECTOR"SX","SY:RETURN
16700 NX=HX:NY=HY:EX=NX:EY=NY:GOSUB14050:PRINT:RETURN
16750 CLS3:PRINT@167," CONGRATULATIONS! ";:PRINT@228," =DOCKING ACCOMPLISHED= ";:GOSUB21300:RETURN
16800 IFVAL(LEFT$(GR$(QX,QY),1))>0THENC$="RED ***":RETURN
16801 IFC=1THENC$="YELLOW ***":RETURN
16802 IFC=2THENC$="- DOCKED ***":RETURN
16803 C$="GREEN ***"
16810 RETURN
16900 FORRA=1TO3:POKE49151,64:FORC1=1TO500:NEXT:FORRB=100TO140STEP4:SOUNDRB,1:NEXT:FORD=1TO30:NEXTD,RA:RETURN
16910 IFD(2)=1ANDD(8)>.9THENG$(QX,QY)=GR$(QX,QY):RETURN
16911 RETURN
16950 FORM=1TO15:SOUND230,1:NEXT:RETURN
16960 SOUND147,1:SOUND176,1:SOUND185,1:SOUND197,1:RETURN
17000 CLS:PRINT@78,"8,0":PRINT@107,"7   *   1":PRINT@140,"*. : .*"
17001 PRINT@169,"6*----Ï----*2":PRINT@204,"*' : '*":PRINT@235,"5   *   3":PRINT@271,"4":PRINT:RETURN
17500 KS=0:SS=0:BS=0:BY=0:AT=0:FORM=1TO3:KX(M)=0:KY(M)=0:KS(M)=0:NEXTM
17501 KS=VAL(LEFT$(GR$(QX,QY),1)):SS=VAL(RIGHT$(GR$(QX,QY),1)):BS=(MID$(GR$(QX,QY),2,1)="b")*-1
17510 BX=-(BS=1)*RND(10):BY=-(BS=1)*RND(10):IFBX=SX ANDBY=SYTHEN17510
17520 FORQ=1TOKS
17530 I=RND(10):J=RND(10):IF(I=BX ANDJ=BY)OR(I=SX ANDJ=SY)THEN17530
17531 KX(Q)=I:KY(Q)=J:KS(Q)=100+RND(200):NEXTQ:FORQ=1TOSS
17540 I=RND(10):J=RND(10):IF(I=BX ANDJ=BY)OR(I=SX ANDJ=SY)THEN17540
17541 FORQQ=1TO KS:IFI=KX(Q)ANDJ=KY(Q)THEN17540
17542 NEXTQQ:SX(Q)=I:SY(Q)=J:NEXTQ:IFKS>0THENC$="RED ***":GOTO17700
17543 C$="GREEN ***":RETURN
17700 R=(X2-SX)*(X2-SX)+(Y2-SY)*(Y2-SY):RETURN
18000 GOSUB14000:I=(32-LEN(DE$(D)))/2:H$="########################"
18001 PRINTTAB(4)H$:PRINTTAB(I)DE$(D):PRINTTAB(4)H$
18002 PRINT@320,"*** THIS UNIT HAS SUSTAINED ***      *** BATTLE DAMAGE ***"
18003 FORJ=1TO5:PRINT@75,"*warning*";:SOUND220,2:PRINT@75,"+WARNING+";:SOUND120,2:NEXT
18010 FORJ=1TO1800:NEXT:GOSUB14000:RETURN
19000 NX=RND(69):NY=RND(89):GOSUB14050:PRINT@423,"*** RED ALERT ***":GOSUB16900:GOSUB14000
19001 M$="CROSSING GALACTIC PERIMETER HAS TRIGGERED WORM-HOLE EFFECT.":GOSUB1
19010 EX=NX:EY=NY:M$="ENTERPRISE HAS RE-ENTERED THE GALAXY AT AN UNKNOWN LOCATION.":GOSUB1
19011 FORD=1TO3500:NEXT:GOSUB17500:GOSUB3000:GOTO6600
19600 IFD(2)>=.5 THENFORW=1TO5:POKEPE,42:SOUND10,1:POKEPE,15:SOUND230,1:NEXT:POKEPE,128:RETURN
19601 RETURN
19900 BS=0:BX=0:BY=0:B=B-1:GR$(QX,QY)=LEFT$(GR$(QX,QY),1)+"0"+RIGHT$(GR$(QX,QY),1):GOSUB14000
19901 PRINT" YOU JUST DESTROYED ONE OF":PRINT" YOUR OWN STARBASES, YOU JERK!":PRINT
19902 IFD(2)>.74 ANDD(8)=1THENG$(QX,QY)=GR$(QX,QY)
19910 N$="POOPDECKER":RETURN
21300 SOUND32,4:SOUND89,4:SOUND125,4:SOUND108,4:SOUND65,4:SOUND159,4:SOUND147,12:FORD=1TO1500:NEXT:RETURN
22000 IFK=0THEN15800
22001 RETURN
22100 IF(TT=>T)AND(K>0)THEN15500
22101 IFAE<1ANDSL<1THEN15600
22102 RETURN
23000 PRINT:M$="COMPLETE STATUS AND DAMAGE REPORTS FOLLOW...":GOSUB1
23001 GOSUB13900:GOSUB2000:GOSUB13900:GOSUB1010:GOSUB13900
23002 GOSUB9000:GOSUB13900
23100 GOSUB14000:PRINT" THE FEDERATION HAS NEED OF A":PRINT" VOLUNTEER FOR A SIMILAR":PRINT" MISSION.
23101 PRINT:PRINT" PLAY AGAIN";:INPUTO$:IFLEFT$(O$,1)="Y"THENRUN
23102 END
