0 REM LAST UPDATE: 6/23/80
1 REM COPYRIGHT MECC, 1979
2 REM ODELL2 8/23/79
3 REM AUTHOR: MIKE FISH, MARGE KOSEL
4 REM
5 REM LAST UPDATE: 1/5/82
6 REM
21 TEXT : HOME : VTAB 4
22 PRINT TAB( 13);"O D E L L 2"
24 PRINT : PRINT "THIS PROGRAM IS ABOUT THE ANIMALS THAT": PRINT
25 PRINT "LIVE AROUND ODELL LAKE. YOU WILL ROLE": PRINT
26 PRINT "PLAY AN ANIMAL."
27 PRINT
28 PRINT "THERE ARE A FEW THINGS THAT DECIDE": PRINT
29 PRINT "WHETHER YOU MAKE IT THROUGH THE WOODS"
30 PRINT : PRINT " 1. HOW WELL YOU SURVIVE ATTACK": PRINT : PRINT " 2. YOUR HUNGER LEVEL": PRINT : PRINT " 3. YOUR INJURY LEVEL"
31 GOSUB 50000
40 J$ = "WHAT WOULD YOU LIKE TO DO"
50 DIM H(11):I = 0:E = 46:ZM = 24
100 HOME
150 PRINT : PRINT "YOU CAN BE A:": PRINT
160 PRINT " RABBIT": PRINT : PRINT " MOUSE": PRINT : PRINT " FOX": PRINT : PRINT " OR WOLF": PRINT
180 PRINT "WHICH ANIMAL WOULD YOU"
183 PRINT " LIKE TO BE";: CALL -868: INPUT F$
200 IF F$ < >"RABBIT" THEN 220
210 A = 1:E = E +15: GOTO 300
220 IF F$ < >"MOUSE" THEN 240
230 A = 2:E = E +10: GOTO 300
240 IF F$ < >"FOX" THEN 00260
250 A = 3: GOTO 300
260 IF F$ < >"WOLF" THEN 0280
270 A = 4:E = E -6: GOTO 300
280 PRINT : PRINT "PLEASE BE A RABBIT, MOUSE, FOX OR WOLF.": VTAB PEEK(37) -2: CALL -868: GOTO 183
300 HOME : PRINT "AS A ";F$;" YOUR OPTIONS ARE:"
301 PRINT : PRINT " 1. RUN 2. IGNORE IT": PRINT : PRINT " 3. EAT IT 4. CHASE IT"
302 POKE 34,6
303 VTAB 6: PRINT "---------------------------------------"
310 PRINT : PRINT
320 FOR Y = 1 TO 10
330 X = INT( RND(1) *10) +1
340 FOR N = 1 TO Y
350 IF H(N) = X THEN 00330
360 NEXT N
380 H(Y) = X
400 ON X GOSUB 00580,01380,02390,03000,03370,03970,04750,05070,05830,06500
410 GOSUB 07680
420 NEXT Y
430 GOSUB 50000
440 POKE 34,0: HOME : PRINT : PRINT : PRINT "CONGRATULATIONS...": PRINT : PRINT "YOU MADE IT THROUGH ODELL WOODS": PRINT
450 PRINT "WITH ";
460 IF I <0 THEN PRINT "NO ";: GOTO 530
470 IF I <10 THEN PRINT "ALMOST NO ";: GOTO 530
480 IF I <30 THEN PRINT "SOME ";: GOTO 530
490 PRINT "HEAVY ";
530 PRINT "INJURIES."
540 PRINT : PRINT "I HOPE YOU HAD FUN.": PRINT : PRINT : PRINT "COME BACK AGAIN"
570 GOTO 07950
580 ON A GOTO 00590,00590,00920,00920
590 REM
600 PRINT : PRINT "AS YOU ARE GOING DOWN THE PATH,"
610 PRINT : PRINT "YOU COME ACROSS A RED FOX."
620 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
660 ON V GOTO 670,790,870,890
670 R = INT( RND(1) *100) +1 -I
680 IF R < = 10 THEN 00770
690 IF R < = 20 THEN 00740
700 PRINT "YOU WERE VERY SMART TO ESACPE."
710 PRINT : PRINT "YOU ARE SAFE NOW, BUT YOUR ENERGY": PRINT
720 PRINT "WENT DOWN FROM RUNNING."
730 E = E -10: RETURN
740 PRINT "AS YOU WERE RUNNING AWAY, THE FOX "
750 PRINT : PRINT "INJURED YOU WITH HIS SHARP TEETH."
760 I = I +10: RETURN
770 PRINT "THE FOX CAUGHT UP AND GOT YOU."
780 B$ = "FOX": GOTO 8010
790 R = INT( RND(1) *100) +1
800 IF R <90 THEN 00850
810 PRINT "YOU WERE LUCKY, THE FOX"
820 PRINT : PRINT "DIDN'T NOTICE YOU THERE. YOU": PRINT
830 PRINT "SHOULD BE MORE CAREFUL."
840 RETURN
850 PRINT "THAT WASN'T A SMART IDEA..."
860 GOTO 00780
870 PRINT "YOU GOT KILLED TRYING."
880 GOTO 00780
890 PRINT "THE FOX FOUND YOU AND CAUGHT YOU."
910 GOTO 00780
920 PRINT : PRINT "YOU SEE A RABBIT IN YOUR TRAVELS."
940 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
980 ON V GOTO 990,1030,1070,1160
985 RETURN
990 PRINT "THERE IS NO REASON TO TRY TO RUN"
1000 PRINT : PRINT "FROM A RABBIT. REMEMBER...": PRINT
1005 PRINT "YOU ARE A ";F$;",AND YOU JUST": PRINT
1010 PRINT "GOT WEAKER FROM RUNNING FROM A RABBIT."
1020 E = E -15: RETURN
1030 PRINT "OF COURSE YOU ARE SAFE, BUT YOU SHOULD": PRINT : PRINT "HAVE EATEN FOR MORE ENERGY."
1060 E = E -5: RETURN
1070 PRINT "YOU GOT IT. THAT WAS A GOOD MOVE": PRINT
1075 PRINT "BECAUSE YOU'LL HAVE MORE ENERGY";
1080 IF I <1 THEN 1110
1090 PRINT : PRINT : PRINT "AND YOUR WOUNDS WOULD HEAL FASTER";
1110 PRINT "."
1120 E = E +15:I = I -5
1130 PRINT : PRINT "NOW THAT YOU HAVE FINISHED YOUR MEAL"
1140 PRINT : PRINT "YOU CAN MOVE ON."
1150 RETURN
1160 PRINT "WHY WOULD YOU CHASE A RABBIT": PRINT : PRINT "AND NOT EAT IT?": PRINT
1170 PRINT "THE RABBIT RAN INTO THE BUSHES": PRINT
1180 E = E -15
1200 PRINT "AND YOU LOST ENERGY CHASING IT."
1210 R = INT( RND(1) *100) +1
1220 IF R < = 20 THEN 01240
1230 RETURN
1240 PRINT : PRINT "AS YOU CHASED THE RABBIT,"
1270 PRINT : PRINT "YOU STUMBLED INTO A TRAP. EVEN THOUGH": PRINT
1280 PRINT "YOU ESCAPE, YOU HAD MAJOR INJURIES."
1290 GOSUB 1910
1300 I = I +15: RETURN
1380 PRINT
1390 ON A GOTO 1400,1400,1700,2080
1400 PRINT "WALKING DOWN THE TRAIL,"
1410 PRINT : PRINT "YOU COME TO A HUNGRY WOLF PACK."
1420 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
1460 IF V = 1 THEN 01510
1470 PRINT "THAT WASN'T A SMART MOVE ON YOUR PART."
1480 PRINT : PRINT "THE WOLF PACK ATE YOU."
1490 B$ = "WOLF PACK": GOTO 8010
1500 PRINT
1510 PRINT "THAT WAS A GOOD CHOICE TO"
1520 PRINT : PRINT "RUN FROM THE WOLF PACK,": PRINT
1530 R = INT( RND(1) *100) +1 +I
1540 IF R > = 95 THEN 01670
1550 IF R > = 80 THEN 01600
1560 PRINT "THEY HARDLY NOTICED YOU. "
1570 PRINT : PRINT "BUT YOU DID LOSE SOME ENERGY": PRINT
1580 PRINT "BY RUNNING SO FAST."
1590 E = E -15: RETURN
1600 PRINT "BUT AS YOU WERE RUNNING YOU LOST ENERGY"
1610 PRINT : PRINT "AND THE WOLF PACK INJURED YOU ";
1620 IF I >0 THEN 01650
1630 PRINT "."
1640 GOTO 01660
1650 PRINT "MORE."
1660 I = I +15:E = E -15: RETURN
1670 PRINT "BUT UNFORTUNATELY,THE WOLF PACK CAUGHT"
1680 PRINT : PRINT "YOU AND KILLED YOU."
1690 GOTO 1490
1700 PRINT "YOU STROLLED OFF THE PATH AND"
1703 PRINT : PRINT "SEE A WOLF PACK ";
1710 R = INT( RND(1) *2) +1
1720 IF R = 2 THEN 01730
1730 PRINT "OFF TO YOUR LEFT."
1740 GOTO 01760
1750 PRINT "OFF TO YOUR RIGHT."
1760 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
1810 ON V GOTO 01820,01970,01970,01970
1820 PRINT "THAT WAS A GOOD IDEA, YOU MUST HAVE"
1830 PRINT : PRINT "KNOWN THAT A WOLF WILL EAT A FOX.": PRINT
1840 PRINT "AS YOU RAN FROM THE WOLVES,": PRINT : PRINT "YOU LOST ENERGY AND": PRINT
1850 PRINT "THEY BEGAN TO CATCH UP TO YOU.": PRINT
1860 E = E -15
1870 R = INT( RND(1) *100) +1 +I
1880 IF R > = 95 THEN 01940
1890 IF R > = 85 THEN 01920
1900 PRINT "YOU WERE SAFE,": PRINT : PRINT "BECAUSE YOU RAN LONG AND HARD."
1910 GOSUB 50000: HOME : RETURN
1920 PRINT "YOU RAN FAST,": PRINT : PRINT "BUT THE WOLF PACK INJURED YOU."
1930 I = I +10: GOSUB 1910: RETURN
1940 PRINT "THE WOLF PACK MOVED IN,": PRINT : PRINT "AND YOU FELL INTO SOME ROCKS.": PRINT
1950 PRINT "THEY CAUGHT UP TO YOU AND ATE YOU."
1960 GOTO 01490
1970 PRINT "THAT WAS A BAD IDEA TO TRY TO": PRINT
1980 ON V GOTO 01990,01990,02020,02020
1990 PRINT "IGNORE THE WOLF PACK,": PRINT : PRINT "BECAUSE IT WAS OUT LOOKING": PRINT
2000 PRINT "FOR FOOD, AND YOU WERE IT"
2010 GOTO 01490
2020 PRINT "EAT A WOLF PACK?": PRINT : PRINT "THERE IS NO WAY YOU COULD": PRINT
2030 PRINT "HAVE EATEN A WOLF."
2040 GOTO 01490
2050 PRINT "CHASE A WOLF PACK? YOU LET THEM KNOW": PRINT
2060 PRINT "WHERE YOU WERE, AND THEY GOT YOU."
2070 GOTO 01490
2080 PRINT : PRINT "YOU ARE RESTING UNDER A TREE"
2085 PRINT : PRINT "WHEN YOU SEE A GOPHER."
2090 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
2130 ON V GOTO 02140,02180,02210,02350
2140 PRINT "THAT WASN'T A GOOD IDEA TO "
2150 PRINT : PRINT "ESCAPE FROM A GOPHER. ALL YOU DID WAS": PRINT
2160 PRINT "USE UP YOUR ENERGY."
2170 E = E -15: RETURN
2180 PRINT "OF COURSE YOU CAN IGNORE IT,"
2190 PRINT : PRINT "BUT THAT WAS ENERGY THAT": PRINT : PRINT "YOU COULD HAVE USED."
2200 RETURN
2210 PRINT "THAT WAS A GOOD CHOICE,": PRINT
2220 IF E <30 THEN 02260
2230 PRINT "IT'S ALWAYS GOOD TO BUILD"
2240 PRINT : PRINT "UP YOUR ENERGY,": PRINT
2250 GOTO 02270
2260 PRINT "YOU NEEDED THAT ENERGY,"
2270 R = INT( RND(1) *100) +1 +I
2280 IF R > = 95 THEN 02330
2290 IF R > = 80 THEN 01240
2300 PRINT "AND YOU GOT IT.": PRINT
2310 PRINT "ALTHOUGH HE DID PUT UP A GOOD STRUGGLE."
2320 E = E +10:I = I -10: RETURN
2330 PRINT "BUT HE BURROWED INTO THE GROUND."
2340 E = E -5: RETURN
2350 PRINT "AS YOU CHASED THE GOPHER INTO THE WOODS,"
2360 PRINT : PRINT "YOU LOST HIM. BOY, YOU MUST": PRINT
2370 PRINT "REALLY FEEL TIRED AFTER THAT RUN"
2380 E = E -10: RETURN
2390 ON A GOTO 02400,02580,02750,02750
2400 PRINT : PRINT "JUST OFF THE PATH IS A CLOVER FIELD."
2420 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
2460 ON V GOTO 02470,02490,02520,02550
2470 PRINT "YOU DON'T NEED TO RUN FROM A FIELD."
2480 RETURN
2490 PRINT "YOU SHOULDN'T HAVE IGNORED SUCH A": PRINT
2500 PRINT "BEAUTIFUL FIELD AS THAT."
2510 E = E -10: RETURN
2520 PRINT "GOOD IDEA...THAT SURE IS GOOD CLOVER."
2530 PRINT : PRINT "NOW THAT YOU ARE DONE EATING,": PRINT : PRINT "LET'S MOVE ON..."
2540 E = E +15:I = I -15: RETURN
2550 PRINT "DON'T BE FOOLISH. LET'S MOVE ON."
2560 E = E -15: RETURN
2570 E = E -15
2580 PRINT : PRINT "THERE IS A RABBIT IN FRONT OF YOU."
2590 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
2630 ON V GOTO 02640,02660,02690,02720
2640 PRINT "SURE, BUT A RABBIT WOULDN'T HURT YOU."
2650 E = E -15: RETURN
2660 PRINT "GOOD... IT CAN'T HURT YOU..."
2670 PRINT
2680 RETURN
2690 PRINT "WHY WOULD YOU WANT TO EAT A RABBIT?"
2700 PRINT : PRINT "IT RAN AWAY BEFORE YOU GOT CLOSE."
2710 E = E -10: RETURN
2720 PRINT "CHASING A RABBIT WASN'T": PRINT : PRINT "A VERY GOOD IDEA...": PRINT
2730 PRINT "IT HOPPED AWAY, AND YOU LOST ENERGY."
2740 E = E -15: RETURN
2750 PRINT : PRINT "AS YOU LOOK DOWN THE PATH, YOU SEE"
2760 PRINT : PRINT "A MOUSE IN FRONT OF YOU..."
2770 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
2810 ON V GOTO 02820,02850,02930,02880
2820 PRINT "THINK ABOUT IT...YOU ARE A ";F$;"."
2830 PRINT : PRINT "YOU CAN SURELY EAT A SMALL MOUSE."
2840 E = E -15: RETURN
2850 PRINT "YOU COULD HAVE GOTTEN": PRINT
2860 PRINT "A MOUSE IF YOU WOULD HAVE TRIED..."
2870 E = E -5: RETURN
2880 PRINT "THE MOUSE HID UNDER A ROCK "
2890 PRINT : PRINT "WHERE YOU COULDN'T GET HIM.": PRINT
2900 PRINT "HE SURE WAS SURPRISED": PRINT : PRINT "WHEN A ";F$;" TRIED": PRINT
2910 PRINT "TO CHASE HIM."
2920 E = E -15: RETURN
2930 R = INT( RND(1) *100) -1 +I
2940 IF R > = 95 THEN 02980
2950 PRINT "YOU GOT IT..."
2960 PRINT : PRINT "YOU MUST FEEL BETTER ABOUT EATING": PRINT : PRINT "SOMETHING."
2970 I = I -10:E = E +10: RETURN
2980 PRINT "OOPS HE GOT AWAY. HE WAS VERY LUCKY."
2990 E = E -10: RETURN
3000 PRINT : PRINT "THERE IS A FALLEN TREE WITH BARK ON IT "
3001 PRINT : PRINT "IN FRONT OF YOU."
3010 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
3020 ON A GOTO 03060,03060,03190,3190
3060 ON V GOTO 3070,3110,3150,3170
3070 PRINT "WHY RUN FROM A TREE? BEING"
3080 PRINT : PRINT "A ";F$;" YOU WOULD HAVE LIKED TO EAT": PRINT
3090 PRINT "BARK FROM THAT TREE..."
3100 E = E -15: RETURN
3110 PRINT "THE BARK OF A TREE IS GOOD FOOD."
3120 PRINT : PRINT "BEING A ";F$;" YOU COULD HAVE": PRINT : PRINT "BUILT UP YOUR ENERGY."
3140 E = E -5: RETURN
3150 PRINT "THAT TASTES GOOD. LETS MOVE ON.."
3160 I = I -15:E = E +20: RETURN
3170 PRINT "BUT YOU DID GET HUNGRY FROM TRYING."
3180 E = E -15: RETURN
3190 ON V GOTO 3230,3260,3290,3330
3230 PRINT "BEING A ";F$;" YOU DON'T NEED": PRINT : PRINT "TO RUN FROM A TREE.": PRINT
3240 PRINT "SAVE YOUR ENERGY"
3250 E = E -15: RETURN
3260 PRINT "GOOD, THE TREE WON'T HELP YOU"
3270 PRINT : PRINT "MUCH ANYWAY. LET'S MOVE ON..."
3280 RETURN
3290 PRINT "EATING BARK FROM TREES IS NOT GOOD FOR"
3300 PRINT : PRINT "YOU. YOU SHOULD BE MORE CAREFUL": PRINT : PRINT "ABOUT WHAT YOU EAT."
3320 I = I +5: RETURN
3330 PRINT "A ";F$;" DOESN'T USUALLY GO"
3340 PRINT : PRINT "AROUND CHASING TREES. WHY CHASE A TREE?": PRINT
3350 PRINT "LET'S GO ON..."
3360 E = E -15: RETURN
3370 PRINT : PRINT "THERE IS AN OWL SITTING ON A BRANCH"
3380 PRINT : PRINT "IN THE TREE BEHIND YOU."
3390 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
3415 ON A GOTO 3440,3440,3710,3710
3440 ON V GOTO 03470,03600,03680,03680
3470 R = INT( RND(1) *100) +1 +I
3480 E = E -15
3490 IF R > = 80 THEN 03540
3500 PRINT "THAT WAS SMART OF YOU"
3510 PRINT : PRINT "TO ESCAPE FROM THE OWL -- IT HAD ITS": PRINT
3520 PRINT "EYE ON YOU."
3530 RETURN
3540 PRINT "AS YOU WERE ESCAPING, THE OWL": PRINT
3550 IF R > = 90 THEN 03580
3560 PRINT "SWOOPED DOWN AND CLAWED YOU LIGHTLY."
3570 I = I +1: RETURN
3580 PRINT "SWOOPED DOWN AND GOT YOU."
3590 B$ = "OWL": GOTO 8010
3600 R = INT( RND(1) *100) +1 +I
3610 IF R > = 40 THEN 03650
3620 PRINT "YOU WERE LUCKY THAT THE OWL"
3630 PRINT : PRINT "DIDN'T SEE YOU."
3640 RETURN
3650 PRINT "YOU SHOULDN'T HAVE IGNORED THE OWL."
3660 PRINT : PRINT "IT FLEW DOWN AND GOT YOU."
3670 GOTO 03590
3680 PRINT "NO, NO, NO...A ";F$;" CAN'T FLY."
3690 PRINT : PRINT "YOU WERE JUST KILLED BY THE OWL": PRINT : PRINT "IN THE ATTEMPT."
3700 GOTO 03590
3710 ON V GOTO 3760,3790,3810,3940
3760 PRINT "YOU DON'T NEED TO DO THAT---"
3770 PRINT : PRINT "YOU JUST WASTED ENERGY.": PRINT
3780 E = E -15: RETURN
3790 PRINT "O.K. LET'S MOVE ON..."
3800 RETURN
3810 IF A = 3 THEN 03900
3820 R = INT( RND(1) *100) -1 +I
3830 IF R > = 50 THEN 03870
3840 PRINT "IT WAS ALREADY INJURED AND YOU"
3850 PRINT : PRINT "GOT IT FROM A LOW BRANCH."
3860 E = E +15:I = I -15: RETURN
3870 PRINT "NO. WHEN YOU TRIED FOR THE"
3880 PRINT : PRINT "OWL, IT FLEW FROM ITS BRANCH."
3890 E = E -15: RETURN
3900 PRINT "YOU WERE JUST INJURED"
3910 PRINT : PRINT "BY THE OWL AS IT FLEW": PRINT
3920 PRINT "PAST YOU ON IT'S ESCAPE."
3930 I = I +15: RETURN
3940 PRINT "AS YOU RAN UP TO THE OWL'S TREE,"
3950 PRINT : PRINT "IT FLEW AWAY."
3955 E = E -15
3960 GOTO 03890
3970 PRINT : ON A GOTO 3980,4310,4530,4530
3980 PRINT "AS YOU ARE HOPPING DOWN THE PATH, YOU"
3990 PRINT : PRINT "COME UP TO AN OLD FARMER'S HUNTING DOG."
4000 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
4040 ON V GOTO 04050,04160,04250,04280
4050 R = INT( RND(1) *100) +1 +I
4060 IF R > = 95 THEN 04110
4070 IF R > = 85 THEN 04130
4080 PRINT "YOU WERE SAFE THIS TIME."
4090 PRINT : PRINT "THE DOG WAS TOO OLD."
4100 E = E -10: RETURN
4110 PRINT "YOU GOT AWAY, BUT THE DOG BIT YOU."
4120 GOTO 02540
4130 PRINT "IT WAS RIGHT TO RUN BUT"
4140 PRINT : PRINT "THE DOG GOT YOU ANYWAY."
4150 B$ = "HUNTING DOG": GOTO 8010
4160 PRINT "REMEMBER...THE DOG IS TRAINED FOR"
4170 PRINT : PRINT "HUNTING, ";
4180 R = INT( RND(1) *2) -1
4190 IF R = 2 THEN 04230
4200 PRINT "BUT YOU WERE LUCKY BECAUSE"
4210 PRINT : PRINT "IT DIDN'T NOTICE YOU."
4220 RETURN
4230 PRINT "AND HE SNIFFED YOU OUT AND CAUGHT YOU."
4240 GOTO 04150
4250 PRINT "WHAT?? YOU'RE TRYING TO EAT A DOG?"
4260 PRINT : PRINT "YOU GOT KILLED TRYING."
4270 GOTO 04150
4280 PRINT "A RABBIT CAN'T CHASE A DOG---YOU"
4290 PRINT : PRINT "GOT KILLED TRYING."
4300 GOTO 04150
4310 PRINT "GOING DOWN THE PATH, YOU COME"
4320 PRINT : PRINT "ACROSS A SNAKE."
4330 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
4370 ON V GOTO 04380,04420,04450,04470
4380 R = INT( RND(1) *100) +1 +I
4390 IF R > = 90 THEN 04500
4400 PRINT "YOU ARE SAFE. HE DIDN'T NOTICE YOU."
4410 E = E -15: RETURN
4420 PRINT "MAYBE YOU IGNORED IT, BUT IT"
4430 PRINT : PRINT "DIDN'T IGNORE YOU..."
4440 B$ = "SNAKE": GOTO 8010
4450 PRINT "NO. YOU DIDN'T EAT IT---IT ATE YOU."
4460 GOTO 04440
4470 PRINT "THE SNAKE ATTACKED YOU"
4480 PRINT : PRINT "AS YOU GOT TOO CLOSE,"
4490 GOTO 04510
4500 PRINT "YOU WEREN'T LUCKY ENOUGH THIS TIME---"
4510 PRINT : PRINT "IT GOT YOU."
4520 GOTO 04440
4530 PRINT "AS YOU GO DOWN THE PATH, YOU"
4540 PRINT : PRINT "GO THROUGH A FARMER'S LAND.": PRINT
4550 PRINT "THE FARMER IS OUT HUNTING."
4560 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
4600 PRINT
4610 IF V >1 THEN 4710
4620 R = INT( RND(1) *100) +1 +I
4630 IF R > = 90 THEN 04710
4640 IF R >80 THEN 04680
4650 PRINT "WHEW---THAT WAS CLOSE, HE MISSED TWICE."
4670 E = E -15: RETURN
4680 PRINT "MISSED ONCE--JUST NICKED YOU": PRINT
4690 PRINT "WITH THE SECOND BULLET."
4700 E = E -10:I = I +15: RETURN
4710 PRINT "HE GOT YOU."
4720 B$ = "FARMER": GOTO 8010
4750 PRINT : PRINT "AS YOU GO DOWN THE TRAIL FARTHER, "
4765 PRINT : PRINT "YOU COME UP TO A DEER."
4768 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
4800 ON A GOTO 04810,04810,04810,04960
4810 ON V GOTO 04820,04860,04880,04880
4820 PRINT "WHY WOULD A ";F$;" ESCAPE FROM A "
4830 PRINT : PRINT "DEER? A DEER WOULDN'T EAT YOU.": PRINT
4840 PRINT "LET'S GO ON..."
4850 E = E -15: RETURN
4860 PRINT "GOOD---A DEER WOULDN'T HURT YOU."
4870 RETURN
4880 GOSUB 04930
4890 PRINT "WHAT?--A ";F$;" DOESN'T ";G$;" A DEER."
4900 PRINT : PRINT "YOU GOT IT EXCITED AND IT RAN AWAY."
4920 RETURN
4930 ON V GOTO 04940,04940,04940,04950
4940 G$ = "EAT":E = E -15: RETURN
4950 G$ = "CHASE":E = E -15: RETURN
4960 ON V GOTO 04820,04970,05000,04970
4970 PRINT "YOU SHOULD HAVE TRIED TO EAT THE DEER."
4980 PRINT : PRINT "YOU HAD A CHANCE OF GETTING IT."
4990 E = E -5: RETURN
5000 R = INT( RND(1) *100) +1 +I
5010 IF I <50 THEN 05040
5020 PRINT "IT WAS CLOSE, BUT THE DEER WAS TOO FAST."
5030 E = E -15: RETURN
5040 PRINT "WHAT A BIG MEAL. YOU SHOULDN'T"
5050 PRINT : PRINT "BE HUNGRY FOR A WHILE---"
5060 E = E +20:I = I -15: RETURN
5070 PRINT : ON A GOTO 5080,5240,5400,5570
5080 PRINT "AS YOU WALK DOWN THE PATH, YOU NOTICE"
5081 PRINT : PRINT "A MOUSE."
5090 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
5130 ON V GOTO 05140,05170,05190,05210
5140 PRINT "DON'T WORRY ABOUT A MOUSE--A RABBIT IS ": PRINT : PRINT "MUCH LARGER."
5160 E = E -10: RETURN
5170 PRINT "GOOD IDEA. A MOUSE COULDN'T HURT YOU."
5180 RETURN
5190 PRINT "YOU WOULD NOT EAT A MOUSE."
5200 RETURN
5210 PRINT "THE MOUSE SCURRIED INTO ITS HOLE BEFORE"
5220 PRINT : PRINT "YOU GOT CLOSE."
5230 RETURN
5240 PRINT "NOW YOU SEE A ROOT."
5245 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
5280 ON V GOTO 05380,05320,05350,05300
5290 PRINT
5300 PRINT "WHY CHASE A ROOT---BE SENSIBLE."
5310 GOTO 05160
5320 PRINT "THAT'S UP TO YOU. BUT BEING A MOUSE,": PRINT : PRINT "YOU SHOULD REALLY ENJOY EATING ROOTS."
5340 RETURN
5350 PRINT "GOOD, THIS IS ONE OF YOUR FAVORITES. ": PRINT : PRINT "HOPE YOU ARE ENJOYING IT."
5370 E = E +15:I = I -10: RETURN
5380 PRINT "WHY RUN FROM A ROOT? ...THINK..."
5390 E = E -15: RETURN
5400 PRINT "NOW YOU MEET A GOPHER."
5401 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
5440 ON V GOTO 05460,05480,05500,05550
5450 PRINT
5460 PRINT "YOU DON'T HAVE TO RUN FROM IT."
5470 GOTO 05490
5480 PRINT "YOU ARE IGNORING THE GOPHER AND IT": PRINT : PRINT "RAN AWAY."
5490 E = E -10: RETURN
5500 R = INT( RND(1) *100) +1 +I
5510 IF R >90 THEN 05800
5520 PRINT "YOU GOT IT EVEN THOUGH IT DID PUT": PRINT : PRINT "UP A GOOD FIGHT, AND SLIGHTLY": PRINT : PRINT "INJURED YOU."
5540 I = I +5:E = E +15: RETURN
5550 PRINT "THAT WAS NOT A GOOD CHOICE."
5560 GOTO 05490
5570 PRINT "YOU TURN YOUR HEAD AROUND AND NOTICE"
5580 PRINT : PRINT "A FOX IS BEHIND YOU."
5590 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
5630 ON V GOTO 05650,05680,05700,05760
5650 PRINT "YOU'RE MUCH LARGER THAN A FOX."
5660 PRINT : PRINT "IT WON'T HURT YOU."
5670 E = E -15: RETURN
5680 PRINT "OKAY."
5690 RETURN
5700 R = INT( RND(1) *100) +1 +I
5710 IF R > = 70 THEN 05800
5720 PRINT "YOU GOT IT."
5730 E = E +15
5740 I = I -10
5750 RETURN
5760 PRINT "YOU CHASED IT BACK INTO ITS DEN AND YOU": PRINT : PRINT "CAN'T CHASE IT ANY FARTHER."
5780 E = E -15
5790 RETURN
5800 PRINT "WELL, YOU DIDN'T GET IT, BUT KEEP YOUR": PRINT : PRINT "HOPES UP, YOU'LL HAVE ANOTHER CHANCE."
5820 RETURN
5830 PRINT : ON A GOTO 5840,5840,6070,6290
5840 PRINT "THERE IS A GRASSY FIELD AHEAD OF YOU."
5850 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
5890 ON V GOTO 5910,5950,5990,6040
5900 PRINT
5910 PRINT "THERE IS NO REASON FOR YOU TO "
5920 PRINT : PRINT "RUN FROM A FIELD OF GRASS."
5930 E = E -15
5940 RETURN
5950 PRINT "IGNORING THE FIELD IS FINE, BUT IT HAD": PRINT : PRINT "FOOD FOR YOU. YOU MISSED IT."
5970 E = E -5
5980 RETURN
5990 PRINT "GOOD..A GRASSY FIELD IS GOOD FOR": PRINT : PRINT "A ";F$;"."
6000 PRINT : PRINT "NOW THAT YOU'VE HAD A FULL MEAL,": PRINT : PRINT "IT IS TIME TO MOVE ON."
6010 E = E +15
6020 I = I -15
6030 RETURN
6040 PRINT "DON'T CHASE A FIELD....": PRINT : PRINT "IT MAKES MOTHER NATURE MAD."
6050 E = E -10
6060 RETURN
6070 PRINT "THERE IS A BUSH OF BERRIES NEAR."
6080 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
6120 ON V GOTO 06140,06180,06240,06210
6140 PRINT "PLEASE BE SENSIBLE..YOU DON'T NEED TO ": PRINT : PRINT "RUN FROM BERRIES."
6160 E = E -15
6170 RETURN
6180 PRINT "WHY IGNORE THE BERRIES..A FOX LIKES": PRINT : PRINT "THEM ALOT."
6190 E = E -5
6200 RETURN
6210 PRINT "A FOX WOULD NOT CHASE BERRIES."
6220 E = E -15
6230 RETURN
6240 PRINT "VERY GOOD...YOU LIKE BERRIES.": PRINT : PRINT "LET'S MOVE ON..."
6260 E = E +15
6270 I = I -15
6280 RETURN
6290 PRINT "NOW THERE IS A GRASSY FIELD NEAR YOU."
6300 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
6340 ON V GOTO 6360,6400,6420,6460
6350 PRINT
6360 PRINT "I DON'T LIKE THE COLOR GREEN EITHER..": PRINT : PRINT "BUT TO RUN FROM GREEN GRASS IS NOT": PRINT : PRINT "A GOOD MOVE."
6380 E = E -15
6390 RETURN
6400 PRINT "GOOD...LET'S MOVE ON."
6410 RETURN
6420 PRINT "EVEN THOUGH GRASS IS NOT ON YOUR DIET,"
6430 PRINT : PRINT "IT DID SETTLE YOUR STOMACH AND IT MADE": PRINT : PRINT "YOU FEEL BETTER."
6440 E = E +10
6450 RETURN
6460 PRINT "ARE YOU TRYING TO CHASE GRASS???": PRINT : PRINT "IT TAKES A LOT OF ENERGY."
6480 E = E -15
6490 RETURN
6500 PRINT : ON A GOTO 6510,6690,6990,7290
6510 PRINT "ON YOUR TRAVELS THROUGH THE WOODS,": PRINT : PRINT "YOU COME ACROSS A RACCOON."
6520 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
6560 ON V GOTO 6570,6610,6630,6630
6570 PRINT "THAT WAS SMART, BECAUSE RACCOONS ARE": PRINT : PRINT "MORE FEROCIOUS THAN MOST PEOPLE THINK."
6590 E = E -15
6600 RETURN
6610 R = INT( RND(1) *100) +1 +I
6620 IF R >20 THEN 06670
6630 PRINT "YOU WEREN'T LUCKY THIS TIME---": PRINT : PRINT "THE RACCOON ATTACKED AND YOU WERE": PRINT : PRINT "KILLED FIGHTING HIM."
6650 B$ = "RACCOON"
6660 GOTO 08010
6670 PRINT "DON'T TAKE A CHANCE LIKE THAT...": PRINT : PRINT "YOU COULD GET KILLED."
6680 RETURN
6690 PRINT "THERE IS A HAWK FLYING ABOVE, AND IT": PRINT : PRINT "HAS AN EYE ON YOU."
6700 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
6740 ON V GOTO 6750,6900,6960,6960
6750 R = INT( RND(1) *100) +1 +I
6760 IF R >70 THEN 06800
6770 PRINT "YOU WERE LUCKY TO HAVE ESCAPED INTO": PRINT : PRINT "THE WOODS."
6780 E = E -15
6790 RETURN
6800 IF R >95 THEN 06860
6810 PRINT "YOU WERE JUST CAUGHT BY ONE OF ITS CLAWS": PRINT "AND YOU WERE SERIOUSLY HURT."
6830 E = E -15
6840 I = I +15
6850 RETURN
6860 PRINT "YOU WEREN'T LUCKY THIS TIME,": PRINT : PRINT "THE HAWK SWOOPED DOWN AND CAUGHT YOU."
6880 B$ = "HAWK"
6890 GOTO 08010
6900 R = INT( RND(1) *100) +1 -I
6910 IF R > = 75 THEN 06930
6920 GOTO 06860
6930 PRINT "YOU WERE VERY LUCKY, YOU DIDN'T HAVE": PRINT : PRINT "MUCH OF A CHANCE AGAINST HIM, BUT": PRINT
6940 PRINT "IT SEEMS AS THROUGH YOU CAME THROUGH."
6950 RETURN
6960 PRINT "THERE IS NO WAY YOU COULD HAVE GOTTEN": PRINT : PRINT "TO IT. REMEMBER YOU ARE A MOUSE. THE": PRINT
6970 PRINT "HAWK SWOOPED DOWN AND GOT YOU."
6980 GOTO 06880
6990 PRINT "THERE IS A BIG BEAR IN FRONT OF YOU."
7000 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
7040 ON V GOTO 07050,07190,07250,07250
7050 R = INT( RND(1) *100) +1 +I
7060 IF R < = 90 THEN 07150
7070 IF R < = 95 THEN 07110
7080 PRINT "YOU WERE SLOW AND THE BEAR GOT YOU."
7090 B$ = "BEAR"
7100 GOTO 08010
7110 PRINT "YOU WERE INJURED BY THE BEAR'S CLAW."
7120 I = I +15
7130 E = E -15
7140 RETURN
7150 PRINT "YOU WERE FAST ENOUGH TO GET AWAY IN": PRINT : PRINT "TIME BEFORE HE CAUGHT YOU."
7170 E = E -15
7180 RETURN
7190 R = INT( RND(1) *100) +1 +I
7200 IF R > = 97 THEN 07220
7205 PRINT "YOU WERE KILLED BY IGNORING THE BEAR."
7210 GOTO 07090
7220 PRINT "YOU WERE VERY LUCKY NOT TO HAVE BEEN": PRINT : PRINT "CAUGHT BUT THE BEAR WAS SCARED OFF."
7240 RETURN
7250 PRINT "THAT WAS BRAVE OF YOU, BUT IT DIDN'T": PRINT : PRINT "WORK. THE BEAR GOT YOU WITH A SWING": PRINT
7260 PRINT "OF HIS POWERFUL ARM AND CLAWS. "
7280 GOTO 07090
7290 PRINT "THERE IS A LARGE HAWK IN FRONT OF YOU, "
7300 PRINT : PRINT J$;: GOSUB 40000:V = ZN: HOME
7340 ON V GOTO 07350,07390,07410,07410
7350 PRINT "YOU SHOULD HAVE IGNORED IT. YOU ARE A": PRINT : PRINT "BIG WOLF."
7370 E = E -20
7380 RETURN
7390 PRINT "GOOD, LET'S MOVE ON..."
7400 RETURN
7410 PRINT "AS YOU RAN TOWARDS THE BIRD, IT": PRINT : PRINT "ESCAPED INTO THE AIR."
7430 E = E -20
7440 RETURN
7680 IF E < = 0 THEN 07710
7690 IF I > = 30 THEN 07750
7700 RETURN
7710 GOSUB 50000: TEXT : HOME : PRINT : PRINT "YOU JUST RAN OUT OF ENERGY AND WITHOUT"
7720 PRINT : PRINT "ANY FOOD YOU STARVED TO DEATH.": PRINT
7730 PRINT "VISIT ODELL WOODS AGAIN SOON SOMETIME."
7740 GOTO 7950
7750 PRINT "YOUR INJURIES ARE TOO GREAT AND": PRINT
7760 R = INT( RND(1) *2) +1
7770 IF R = 1 THEN 07810
7780 PRINT "YOU FELL PREY TO A FLOCK OF SCAVENGER"
7790 PRINT : PRINT "BIRDS."
7800 GOTO 07820
7810 PRINT "YOU BLED TO DEATH IN THE WOODS."
7820 PRINT : PRINT "COME BACK TO ODELL WOODS AGAIN SOMETIME SOON."
7950 GOSUB 50000: GOTO 43000
7980 GOTO 40
8010 GOSUB 50000
8012 TEXT : HOME : VTAB 6: PRINT "ALTHOUGH THE ";B$;" GOT YOU": PRINT
8020 PRINT "COME BACK TO ODELL WOODS AND TRY AGAIN."
8030 GOSUB 50000
8040 GOTO 43000
40000 CH = PEEK(36):CV = PEEK(37)
40001 INPUT ZN$: IF LEN(ZN$) < > LEN( STR$( VAL(ZN$))) THEN 40003
40002 IF VAL(ZN$) < >0 OR ZN$ = "0" THEN 40006
40003 VTAB ZM: PRINT "PLEASE TYPE A NUMBER - TRY AGAIN";: GOSUB 40010: GOTO 40001
40006 VTAB ZM: CALL -868:ZN = VAL(ZN$): IF ZN >0 AND ZN <5 AND ZN = INT(ZN) THEN VTAB CV +1 +(CV <23): RETURN
40007 PRINT "TYPE A 1,2,3, OR 4";: VTAB CV +1: POKE 36,CH: CALL -868: GOTO 40001
40010 VTAB CV +1: POKE 36,CH: CALL -868: RETURN
43000 REM END OF PROGRAM
43002 TEXT : HOME : VTAB 5
43005 VTAB 5: PRINT "WOULD YOU LIKE TO RUN THIS PROGRAM": PRINT : PRINT "AGAIN - YES OR NO";: CALL -868
43006 INPUT A$
43007 IF MID$ (A$,1,1) = "Y" THEN CLEAR :ZM = 24: GOTO 40
43008 IF MID$ (A$,1,1) = "N" THEN 43030
43009 PRINT : PRINT "PLEASE ANSWER WITH YES OR NO": GOTO 43005
43030 IF PEEK(1023) < >123 THEN 43050
43032 HOME : VTAB 10: PRINT "GETTING THE MENU PROGRAM...": PRINT CHR$(13); CHR$(4);"RUN HELLO"
43050 HOME : END
50000 POKE -16368,0: VTAB 24: HTAB 6: PRINT "PRESS ";: INVERSE : PRINT "SPACE BAR";: NORMAL : PRINT " TO CONTINUE";
50001 IF PEEK( -16384) <128 THEN 50001
50002 RETURN
\