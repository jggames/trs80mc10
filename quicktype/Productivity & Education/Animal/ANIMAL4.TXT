0 CLS:CLEAR9000:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
10 PRINT TAB(13)"ANIMAL"
20 PRINT TAB(7)"CREATIVE COMPUTING"
21 PRINT TAB(5)"MORRISTOWN, NEW JERSEY"
30 PRINT:PRINT
40 PRINT "PLAY 'GUESS THE ANIMAL'"
45 PRINT
50 M$="THINK OF AN ANIMAL AND THE COMPUTER WILL TRY TO GUESS IT.":GOSUB1
60 PRINT
70 DIM A$(200)
80 READA$(0):N=VAL(A$(0))
90 FOR I=1 TO N-1
100 READ A$(I)
110 NEXT I
120 REM          MAIN CONTROL SECTION
130 PRINT"ARE YOU THINKING OF AN ANIMAL?":INPUTA$
140 IF LEFT$(A$,4)="LIST" THEN 600
145 IF LEFT$(A$,1)="N" THEN 690
150 IF LEFT$(A$,1)<>"Y" THEN M$="PLEASE RESPOND 'YES', 'NO', OR 'LIST'":GOSUB1:GOTO120
160 K=1
170 GOSUB 390
180 IF LEN(A$(K))=0 THEN 690
190 IF LEFT$(A$(K),2)="^Q" THEN 170
200 M$=LEFT$(A$(K),1):IFM$="A"ORM$="E"ORM$="I"ORM$="O"ORM$="U"THENM$="IS IT AN ":GOTO205
201 M$="IS IT A "
205 M$=M$+RIGHT$(A$(K),LEN(A$(K))-2)+"?":GOSUB1
210 INPUT A$
220 A$=LEFT$(A$,1)
230 IF LEFT$(A$,1)="Y" THEN PRINT "WHY NOT TRY ANOTHER ANIMAL...":GOTO 120
240 PRINT "THE ANIMAL YOU WERE THINKING OF":INPUT"WAS A";V$
250 M$="PLEASE TYPE IN A QUESTION THAT WOULD DISTINGUISH A "
260 M$=M$+V$+" FROM A "+RIGHT$(A$(K),LEN(A$(K))-2)+".":GOSUB1
270 INPUTX$:IFRIGHT$(X$,1)="?"THENX$=LEFT$(X$,LEN(X$)-1)
280 M$="FOR A "+V$+" THE ANSWER WOULD BE?":GOSUB1
290 INPUT A$
300 A$=LEFT$(A$,1):IF A$<>"Y" AND A$<>"N" THEN 280
310 IF A$="Y" THEN B$="N"
320 IF A$="N" THEN B$="Y"
330 Z1=VAL(A$(0))
340 A$(0)=MID$(STR$(Z1+2),2)
350 A$(Z1)=A$(K)
360 A$(Z1+1)="^A"+V$
370 A$(K)="^Q"+X$+"^"+A$+MID$(STR$(Z1+1),2)+"^"+B$+MID$(STR$(Z1),2)+"^"
380 GOTO 120
390 REM     SUBROUTINE TO PRINT QUESTIONS
400 X=0:Q$=A$(K)
410 FOR Z=3 TO LEN(Q$)
415 IF MID$(Q$,Z,1)<>"^" THEN 417
416 M$=MID$(Q$,3,Z-3)+"?":GOSUB1:Z=255
417 NEXT Z
420 INPUT C$
430 C$=LEFT$(C$,1)
440 IF C$<>"Y" AND C$<>"N" THEN 410
450 T$="^"+C$
455 FOR X=3 TO LEN(Q$)-1
460 IF MID$(Q$,X,2)=T$ THEN 480
470 NEXT X
475 STOP
480 FOR Y=X+1 TO LEN(Q$)
490 IF MID$(Q$,Y,1)="^" THEN 510
500 NEXT Y
505 STOP
510 K=VAL(MID$(Q$,X+2,Y-X-2))
520 RETURN
530 DATA"4","^QDOES IT SWIM^Y2^N3^","^AFISH","^ABIRD"
531 DATA"24"
532 DATA"^QDOES IT SWIM^Y2^N3^"
533 DATA"^QDOES IT HAVE FLIPPERS^Y5^N4^
534 DATA"^QDOES IT HAVE A TRUNK^Y7^N6^
535 DATA"^QIS IT THE LARGEST KNOWN MAMMAL^Y19^N18^
536 DATA"^ASEAL
537 DATA"^QDOES IT GO 'ARF ARF'^Y9^N8^
538 DATA"^AELEPHANT
539 DATA"^QDOES IT HAVE RETRACTIBLE CLAWS^Y11^N10^
540 DATA"^ADOG
541 DATA"^QDOES IT GIVE MILK AND GO 'MOO'^Y15^N14^
542 DATA"^QDOES IT LIVE IN THE JUNGLE^Y13^N12^
543 DATA"^ACAT
544 DATA"^QIS IT THE KING OF THE JUNGLE^Y23^N22^
545 DATA"^QDOES IT GIVE MILK AND EAT TIN CANS AND ALMOST EVERYTHING ELSE^Y17^N16^
546 DATA"^ACOW
547 DATA"^QDOES IT HAVE A WOOLY COAT AND SAY 'BAA BAA'^Y21^N20^
548 DATA"^AGOAT
549 DATA"^AFISH
550 DATA"^AWHALE
551 DATA"^ABIRD
552 DATA"^ASHEEP
553 DATA"^ATIGER
554 DATA"^ALION
600 PRINT:PRINT "ANIMALS I ALREADY KNOW ARE:"
605 X=0
610 FOR I=1 TO 200
620 IF LEFT$(A$(I),2)<>"^A" THEN 650
630 FOR Z=3 TO LEN(A$(I)):IFZ>16THENZ=255:GOTO645
641 PRINT MID$(A$(I),Z,1);
645 NEXT Z:PRINT,:X=X+1:IFX=30THENINPUT"HIT ENTER TO CONTINUE";M$:X=0
650 NEXT I
660 PRINT
680 GOTO 120
690 M$="OK, SEE YOU LATER. HOPE YOU HAD FUN PLAYING! TYPE LIST AND HIT ENTER FOR SOME FURTHER INSTRUCTIONS.":GOSUB1:END
700 REM ANIMAL
710 REM BY ARTHUR LUEHRMANN
720 REM AT DARTMOUTH COLLEGE
730 REM MODS: NATHAN TEICHHOLTZ
740 REM AT DEC, STEVE NORTH
750 REM AT CREATIVE COMPUTING
760 REM MC-10 MODS: JIM GERRIE
770 REM
780 REM TYPE 530 AND HIT ENTER
790 REM TO ALLOW SOME PRE-INPUT
800 REM DATA TO BE USED
