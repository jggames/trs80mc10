0 CLS:CLEAR750:DIMCC,ZZ,M$,I$:M=16384:GOTO100
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN

100 CLS
110 PRINT"THE DUNGEON OF HTAM"
120 PRINT "TRS-80 MC-10"
130 PRINT "COPYRIGHT (C) 1980":PRINT"BY HOWARD BERENBON"
140 PRINT
150 PRINT "AN ";:M$="EDUCATIONAL MATH DUNGEON":GOSUB7:PRINT
160 GOSUB 440
170 GOSUB 440
180 CLS:DIMA(9,9,2)
190 PRINT "YOU WILL BE TELEPORTED TO"
200 PRINT
210 PRINT "THE DUNGEON OF HTAM
220 PRINT
230 CC=RND(-(PEEK(9)*256+PEEK(10)))
240 MA=0 :CA=0:G=1000:M1=1:K=0
250 PRINT "ENTER YOUR CHARACTER'S NAME"
260 INPUT A$
270 GOSUB 440
280 PRINT:M$="YOU CARRY WITH YOU 1000 GOLD PIECES":GOSUB1
290 PRINT:GOSUB 440:M$=A$+", YOU ARE ON YOUR WAY":GOSUB1
300 GOSUB 440
310 GOSUB 480
320 CLS
330 PRINT "YOU HAVE ARRIVED AT"
335 PRINT
340 PRINT "THE DUNGEON OF HTAM ";:M$="*LEVEL 2*":GOSUB7:PRINT
360 PRINT
370 PRINT "YOU WILL ENCOUNTER MONSTERS AND"
380 PRINT "THIEVES, AND GOLD, BUT WATCH 
390 PRINT "YOUR STEP...":PRINT
400 PRINT "TRAP DOORS CAN BE COSTLY"
410 GOSUB 450
420 GOSUB 450
430 GOTO 1010
440 REM DELAY
450 FOR Z2=1 TO 2000
460 NEXT Z2
470 RETURN
480 REM SET UP 2 LEVEL DUNGEON
490 FOR X=1 TO 8
500 FOR Y=1 TO 8
510 FOR Z=1TO 2
520 A(X,Y,Z)=RND(7)
530 NEXT Z
540 NEXT Y
550 NEXT X
560 REM TRAP DOORS $8, MIN-1,MAX-3
570 H=RND(3)
580 FOR A=1 TO 2
590 FOR N=1 TO H
600 X=RND (8)
610 Y=RND(8)
620 A(X,Y,A)=8
630 NEXT N
640 NEXT A
650 REM STAIRWAYS $9, MIN-3,MAX-6
660 S=RND(4)+2
670 FOR A= 1 TO 2
680 FOR N=1 TO S
690 X=RND(8)
700 Y=RND(8)
710 A(X,Y,A)=9
720 NEXT N
730 NEXT A
740 RETURN
750 REM STAIRWAY
760 L1=L1-1
770 PRINT "YOU WALK UP THE STAIRWAY..."
780 GOSUB 440
790 M$="THE ENCHANTED KEY OPENS THE LOCK...":GOSUB1
800 GOSUB 440
810 IF L1=0 THEN 870
820 MA=0:K=0:K4=RND(4)+4
830 PRINT:CB=CA+K4
840 PRINT "YOU ARE AT LEVEL 1"
850 GOSUB 440:GOSUB 440
860 GOTO 1070
870 PRINT "YOU FOUND YOUR WAY"
880 PRINT "OUT OF THE DUNGEON OF HTAM"
885 PRINT
890 M$="YOU ACQUIRED"+STR$(G)+" GOLD PIECES":GOSUB1
910 GOSUB 930
920 GOTO 1910
930 GG=G+100:REM RATING
940 R=INT((GG*CA-7000+1)/M1)
950 PRINT
960 PRINT "GAME RATING IS ";R
970 PRINT:IFG<=0THEN4300
980 M$="YOU TOOK"+STR$(M1)+" TURNS TO FIND THE WAY OUT, AND ANSWERED"+STR$(CA)+" QUESTIONS CORRECTLY.":GOSUB1
1000 RETURN
1010 REM SET UP 1ST MOVE
1020 C=RND(8)
1030 D=RND(8)
1040 A(C,D,2)=1 
1050 L1=2
1060 K4=RND(4)+4
1070 REM PLAYER MOVE ROUTINE
1080 CLS
1090 A=A(C,D,L1)
1100 GOSUB 440
1110 ON A GOSUB 2220,2280,2340,2340,2390,2700,2750,2800,3080
1120 PRINT
1130 IF G<=0 THEN 1820
1140 M$=A$+", WHAT IS YOUR ACTION OR MOVE?":GOSUB1
1150 PRINT
1160 M$="nORTH, sOUTH, eAST, wEST, uP, mAP, gOLD, kEY":GOSUB1
1180 INPUT M1$
1190 M1=M1+1:IF K=0 AND M1>=140/L1 THEN 4210
1200 IF M1$="N" THEN 1290
1210 IF M1$="E" THEN 1340
1220 IF M1$="S" THEN 1390
1230 IF M1$="W" THEN 1440
1240 IF M1$="U" THEN 1490
1250 IF M1$="M" THEN 1610
1260 IF M1$="G" THEN 1670
1265 IF M1$="K" THEN 1282
1270 PRINT
1280 GOTO 1120
1282 IF K=1 THEN PRINT"YOU HAVE THE KEY"
1283 IF K=0 THEN PRINT"YOU HAVE NO KEY"
1284 GOTO 1120
1290 REM NORTH MOVEMENT
1300 IF A=7 THEN 1710
1305 IF (D-1)=0 THEN 1980
1310 D=D-1
1330 GOTO 1070
1340 REM EAST MOVEMENT
1350 IF A=6 THEN 1770
1360 IF (C+1)=9 THEN 2030
1370 C=C+1
1380 GOTO 1070
1390 REM SOUTH MOVEMENT
1400 IF A=7 THEN 1710
1410 IF (D+1)=9 THEN 2050
1420 D=D+1
1430 GOTO 1070
1440 REM WEST MOVEMENT
1450 IF A=6 THEN 1770
1460 IF (C-1)=0 THEN 2070
1470 C=C-1
1480 GOTO 1070
1490 CLS:REM STAIRWAY UP
1500 IF A<>9 THEN 1580
1510 IF K=1 THEN 750
1520 PRINT
1530 PRINT "YOU CANNOT GO UP THE STAIRWAY"
1540 PRINT "YOU DON'T HAVE THE KEY"
1550 GOSUB 440
1560 PRINT
1570 GOTO 1120
1580 PRINT "YOU ARE NOT AT A STAIRWAY"
1590 GOSUB 440
1600 GOTO 1120
1610 CLS:REM MAP
1620 IF MA=1 THEN 2090
1630 PRINT "YOU DON'T HAVE THE MAP"
1640 PRINT
1650 GOSUB 440
1660 GOTO 1120
1670 REM GOLD PIECES;
1680 CLS:M$="YOU HAVE"+STR$(G)+" GOLD PIECES WITH YOU":GOSUB1
1690 PRINT
1700 GOTO 1120
1705 REM ***CHECKED TO THIS LINE***
1710 REM EW CORRICDOR
1720 PRINT
1730 CLS:M$="YOU ARE IN AN EAST-WEST CORRIDOR":GOSUB1
1740 PRINT "YOU CAN ONLY GO EAST OR WEST"
1750 PRINT
1760 GOTO 1120
1770 REM NS CORRIDOR
1780 PRINT
1790 CLS:M$="YOU ARE IN A NORTH-SOUTH CORRIDOR":GOSUB1
1800 PRINT "YOU CAN ONLY GO NORTH OR SOUTH"
1810 GOTO 1750 
1820 REM GOLD ZERO
1830 GOSUB 440: GOSUB 440
1840 PRINT
1850 M$="YOU LOST ALL YOUR GOLD AND YOU WERE UNABLE TO MEET THE DEMANDS OF THE DUNGEON OF HTAM":GOSUB1
1880 PRINT:PRINT
1890 PRINT "BETTER LUCK NEXT TIME!"
1900 GOSUB 930
1910 PRINT
1920 PRINT "ANOTHER GAME"
1930 PRINT "ENTER '1'-YES '0'-NO"
1940 INPUT AA
1950 IF AA<>1 THEN 1970
1960 CLS:GOTO 210
1970 END
1980 CLS: PRINT "YOU ARE AT THE NORTH WALL"
1990 PRINT "YOU CANNOT PASS THROUGH"
2000 PRINT
2010 PRINT "TRY ANOTHER DIRECTION"
2020 GOTO 1120
2030 CLS:PRINT "YOU ARE AT THE EAST WALL"
2040 GOTO 1990
2050 CLS:PRINT "YOU ARE AT THE SOUTH WALL"
2060 GOTO 1990
2070 CLS:PRINT "YOU ARE AT THE WEST WALL"
2080 GOTO 1990
2090 REM DISPLAY MAP
2100 CLS
2110 PRINT "THE DUNGEON OF HTAM MAP LEVEL"STR$(L1)
2130 FOR Q=1 TO 8
2140 FOR N=1 TO 8
2150 S1=A(N,Q,L1)
2160 ON S1 GOSUB 3120,3120,3140,3140,3160,3180,3200,3220,3230:IFC=NTHENIFD=QTHENGOSUB7:GOTO2180
2170 PRINTM$;
2180 NEXT N
2200 NEXT Q
2210 GOTO 1120
2220 REM EMPTY ROOM
2230 PRINT
2240 PRINT "YOU ARE IN A COLD AND DARK"
2250 PRINT "EMPTY CHAMBER"
2260 PRINT
2270 RETURN
2280 REM EMPTY ROOM 2
2290 PRINT
2300 PRINT "YOU AND IN A DAMP AND MISTY"
2310 PRINT "EMPTY CHAMBER"
2320 PRINT
2330 RETURN
2340 CLS:GOSUB 4160
2350 M4=RND(4)
2360 ON M4 GOSUB 3250,3380,3550,3680
2370 PRINT
2380 RETURN
2390 CLS:M$="THERE IS A THIEF IN THIS CHAMBER...":GOSUB1
2400 A(C,D,L1)=2
2410 GOSUB 440
2420 G4=RND(350/L1)
2430 Y=RND(8)
2440 IF Y<=3 THEN 2610
2450 PRINT
2460 PRINT "HE SURPRISES YOU"
2470 GOSUB 440
2480 M$="AS HE QUICKLY PASSES BY YOU HE SNATCHES"+STR$(G4)+" GOLD PIECES":GOSUB1:PRINT
2500 G=G-G4
2510 REM LOOK FOR MAP
2520 IF MA=1 THEN RETURN
2530 MA=RND(4):IF MA<=2 THEN MA=1
2540 IF MA=1 THEN 2570
2550 RETURN
2560 GOSUB 440
2570 PRINT "YOU SEARCH THE CHAMBER AND..."
2580 GOSUB 440
2590 PRINT "YOU FIND A MAP"
2600 RETURN
2610 PRINT "YOU SURPRISED THE THIEF!"
2620 GOSUB 440
2630 PRINT "AS HE RUNS OUT HE DROPS"
2640 PRINT "* * *";G4; "GOLD PIECES"
2650 PRINT "YOU PICK UP THE GOLD PIECES":G=G+G4
2660 PRINT:IF MA=1 THEN RETURN
2670 MA=RND(4): IF MA<=2 THEN MA=1
2680 IF MA=1 THEN 2570
2690 RETURN
2700 CLS
2710 PRINT
2720 GOSUB 4350:PRINT "ENTER A NORTH-SOUTH CORRIDOR"
2730 PRINT:GOSUB 4330
2740 RETURN
2750 CLS
2760 PRINT
2770 GOSUB 4350:PRINT"ENTER AN EAST-WEST CORRIDOR"
2780 PRINT
2790 RETURN
2800 REM TRAP DOOR
2810 PRINT "YOU ACTIVATED A TRAP DOOR"
2820 GOSUB 440
2830 TD=RND(4)
2840 IF TD>=3 THEN 2890
2850 PRINT
2860 PRINT "YOU CAUGHT YOURSELF"
2870 PRINT "FROM FALLING"
2880 RETURN
2890 IF L1=2 THEN 3000
2900 L1=L1+1:PRINT:K=1
2910 PRINT "YOU FELL THRU TO LEVEL 2 AND..."
2920 G=100
2930 GOSUB 440
2940 PRINT
2950 PRINT "YOU LOST"
2960 PRINT "MOST OF YOUR GOLD PIECES":PRINT
2970 PRINT "YOU HAVE";G;"GOLD PIECES LEFT"
2980 PRINT "BUT YOU STILL HAVE YOUR KEY"
2990 RETURN
3000 PRINT "YOU FELL INTO A DEEP PIT"
3010 GOSUB 440
3020 PRINT "YOU DIDN'T GET HURT"
3040 PRINT
3050 GOSUB 440
3060 PRINT "BUT IN CLIMBING OUT"
3070 GOTO 4250
3080 PRINT "YOU ARE AT A STAIRWAY"
3090 PRINT "GOING UP"
3100 PRINT
3110 RETURN
3120 M$="O   "
3130 RETURN
3140 M$="M   "
3150 RETURN
3160 M$="?   "
3170 RETURN
3180 M$="NS  "
3190 RETURN
3200 M$="EW  "
3210 RETURN
3220 GOTO 3160
3230 M$="UP  "
3240 RETURN
3250 REM ADD
3260 PRINT "HALT! I AM dda"
3270 GOSUB 3810
3280 GOSUB 3860
3290 M$=STR$(X)+"  + "+STR$(Y)+"  =  ":GOSUB7
3300 Z=X+Y
3310 INPUT A1
3320 IF A1=Z THEN 3360
3330 LOSE GOLD
3340 GOSUB 4020
3350 RETURN
3360 GOSUB 3920
3370 RETURN
3380 REM SUB
3390 PRINT "STOP! I AM bus"
3400 GOSUB 3810
3410 GOSUB 3860
3420 M$=STR$(X)+"  - "+STR$(Y)+"  =  ":GOSUB7
3430 Z=X-Y
3440 INPUT A1
3450 IF A1=Z THEN 3480
3460 GOSUB 4020
3470 RETURN
3480 GOSUB 3920
3490 RETURN
3500 GOSUB 480
3510 H=1:O=9:W=8
3520 B=0:E=5:R=14
3530 C=0:PR=0
3540 GOTO 1010
3550 REM MULT
3560 PRINT "HALT! I AM lum"
3570 GOSUB 3810
3580 GOSUB 3860
3590 M$=STR$(X)+"  X "+STR$(Y)+"  =  ":GOSUB7
3600 Z=X*Y
3610 INPUT A1
3620 IF A1=Z THEN 3660
3630 REM LOSE GOLD
3640 GOSUB 4020
3650 RETURN
3660 GOSUB 3920
3670 RETURN
3680 REM DIV
3690 PRINT "STOP! I AM vid"
3700 GOSUB 3810
3710 GOSUB 3860
3720 M$=STR$(X*Y)+"  / "+STR$(Y)+"  =  ":GOSUB7
3730 Z=X
3740 INPUT A1
3750 IF A1=Z THEN 3790
3760 REM LOSE GOLD
3770 GOSUB 4020
3780 RETURN
3790 GOSUB 3920
3800 RETURN
3810 PRINT
3820 M$="YOU MAY NOT PASS THRU UNTIL YOU ANSWER THIS MATH QUESTION":GOSUB1
3840 PRINT
3850 RETURN
3860 REM RANDOM ROUTINE
3870 X=RND(30/L1)
3880 Y=RND(30/L1)
3885 PRINT "WHAT IS  "
3890 GOSUB 440
3910 RETURN
3920 PRINT "CORRECT"
3930 G4=RND(400/L1)+25
3940 G=G+G4
3950 GOSUB 440
3960 PRINT "YOU WIN";G4;" GOLD PIECES"
3970 A(C,D,L1)=1
3980 CA=CA+1:IF K=1 THEN RETURN
3990 IF CA=K4 THEN 4110
4000 IF L1=1 THEN 4110
4010 RETURN
4020 PRINT
4030 PRINT "INCORRECT"
4040 PRINT "THE CORRECT ANSWER IS"Z
4050 PRINT
4055 G4=RND(350/L1)
4060 G=G-G4
4080 GOSUB 440
4090 PRINT "YOU LOSE";G4;"GOLD PIECES"
4100 RETURN
4110 GOSUB 440
4120 K=1
4130 PRINT:M$="YOU HAVE FOUND THE ENCHANTED KEY":GOSUB1
4140 GOSUB 440
4150 RETURN
4160 M$="YOU DISTURBED A MONSTER IN THIS CHAMBER...":GOSUB1
4170 GOSUB 440
4180 PRINT "AND HE SPEAKS...":PRINT
4190 GOSUB 440
4200 RETURN
4210 GOSUB 4120
4220 GOTO 1200
4230 IF CA=CB THEN 4110
4240 RETURN
4250 G=100:GOSUB 440:PRINT
4260 M$="YOU DROPPED MOST OF YOUR GOLD PIECES. YOU HAVE"+STR$(G)+" GOLD PIECES LEFT.":GOSUB1
4290 RETURN
4300 M$="YOU ANSWERED"+STR$(CA)+" QUESTIONS CORRECTLY IN"+STR$(M1)+" TURNS.":GOSUB1
4320 RETURN
4330 M$="THE DOOR CLOSES AND LOCKS BEHIND YOU":GOSUB1:GOSUB 440
4340 RETURN
4350 PRINT "YOU OPEN A SECRET DOOR AND...":GOSUB 440
4360 RETURN
