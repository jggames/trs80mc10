0 L=0:B$=CHR$(128)+CHR$(128)+CHR$(128)
1 S$="YES":Z=70
2 O=0:S=0
3 U=0
4 CLS5
5 POKE49151,67:POKE17026,RND(255)
6 PRINT@41,"TYPING ATTACK";:PRINT@101,"(C) 1984 DARREN OTTERY";
7 FORI=1TO3:SOUND1,1:SOUND50,1:SOUND100,1:SOUND150,1:SOUND200,1:SOUND150,1:SOUND100,1:SOUND50,1:SOUND1,1
8 NEXTI
9 PRINT@163,"YOUR CITY IS UNDER ATTACK!";
10 PRINT@229,"PRESS <SPACE> TO BEGIN";:PRINT@291,"PRESS <I> FOR INSTRUCTIONS";
11 I$=INKEY$:IFI$="I"GOTO5000
12 IF I$<>" "THEN 11
13 CLS0:O=0
14 IF LEFT$(S$,1)="Y"THENO=1
15 FORI=0TO63STEP2
20 A=22+RND(9)
30 FORD=ATO31
40 SET(I,D,2):SET(I+1,D,2)
50 NEXT D,I
51 PRINT@238,"READY!";
52 FORI=1TO3:FORSO=1TO255STEP50:SOUNDSO,1:NEXTSO,I
53 PRINT@238,CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128);
55 IFO=0THEN70
56 PRINT@235,"SHIELDS UP!";
60 FORI=0TO63STEP2:SET(I,18,RND(6)+2):SOUND(I+1)*3,1:NEXTI
62 PRINT@235,B$;B$;B$;B$;
70 A$=CHR$(231)+CHR$(236)+CHR$(235)
75 REM
76 PRINT@481,"score"S;
80 FORI=0TORND(27)
85 PRINT@I+L,A$;
90 SOUND100,1
95 PRINT@I+L,B$;
97 NEXTI
98 PRINT@I+L,A$;
99 A=64+RND(26):PRINT@481,"score"S;
100 FORX=I+L+33TO511STEP32
101 Q$=INKEY$
102 IF Q$=CHR$(A) THEN 700
103 IF U>=1000THEN1000
105 IF PEEK(16384+X)<>128 THEN 600
110 PRINT@X,CHR$(A);
120 SOUNDX/2,1
130 FORDL=1TOZ:NEXTDL
140 PRINT@X,B$;
300 NEXTX
600 REM
610 FORI=1TO5:SOUND1,1:NEXT
615 PRINT@X,B$;
620 IF X>319 THEN 900
630 FORI=1TO500:NEXTI
635 FORI=0TO31:PRINT@I+L,CHR$(128);:NEXTI
640 GOTO 76
700 SS=511-X:SOUND1,1
701 POKE49151,67
702 PRINT@X-33,SS;
703 FOR DL=1TO100:NEXTDL
705 SOUND200,1:SOUND100,1
710 PRINT@X-33,CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128);
720 S=S+SS
725 U=U+80
730 GOTO635
900 FORI=200TO10 STEP-10:SOUNDI,1:NEXTI
901 FORI=1TO300
902 E=RND(128)
903 PRINT@382+E,"¿";
904 FOR DL=1TO5:NEXTDL
905 PRINT@382+E,"€";
907 NEXTI
910 PRINT@33,"GAME OVER";
912 PRINT@97,"SCORE"S;
915 IF HS<S THEN HS=S:SOUND220,1:SOUND220,1:SOUND220,1:SOUND220,1
917 PRINT@161,"HIGH"HS;
918 PRINT@225,"PRESS <SPACE> FOR NEW GAME";
920 IF INKEY$=" "THEN 0
930 GOTO 920
1000 PRINT@481,"score"S;:FORI=1TO5:CLS RND(7)+1:SOUND1,1:SOUND100,1:SOUND200,1:SOUND100,1:NEXTI
1005 U=0
1008 FORI=1TO200:NEXTI
1010 CLS0
1020 PRINT@233,"challenge";CHR$(128);"stage";
1021 SX=5
1022 PRINT@481,"score"S;
1025 SOUND100,5
1026 SOUND100,2
1027 SOUND125,2
1028 SOUND140,5
1030 FORI=1TO1000:NEXTI:CLS0
1035 FORT=1TO10
1037 CLSRND(7)+1
1040 A=64+RND(26)
1050 FORI=(3+RND(25))TO511STEP32
1060 PRINT@I,CHR$(A);
1070 SOUND10,1
1080  REM FOR DL=1TOZ:NEXTDL
1090 PRINT@I,CHR$(128);
1100 Q$=INKEY$
1110 IFQ$=CHR$(A)THEN1500
1200 NEXTI,T
1205 PRINT@235,"NEXT WAVE!";
1206 FORSO=1TO5:SOUND1,1:NEXTSO
1207 Z=Z-20:IF Z<1 THEN Z=1
1208 IF Z<2 THEN S$="NO":L=L+32:GOTO4000
1209 IF L>256 THEN L=256
1210 GOTO 1600
1500 SX=SX*2
1505 S=S+SX
1507 PRINT@I-1,SX;
1509 PRINT@481,"score"S;
1510 FORSO=1TO5:SOUNDRND(255),1:NEXTSO
1512 FORDL=1TO100:NEXTDL
1515 PRINT@I-2,CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128);
1517 NEXTT
1530 GOTO 1205
1600 FORI=1TO10:SOUND200,1:NEXT
1610 GOTO13
4000 FOR LL=1TO4
4010 CLS4
4020 PRINT@229,"SHIELD GENERATOR DOWN!";
4030 SOUND220,4
4032 CLS4
4035 FORDL=1TO100:NEXTDL
4040 NEXT LL
4050 GOTO 1209
5000 CLS
5010 FORI=0 TO63 STEP2:SET(I,0,3):SET(I,31,3):SET(0,I/2,3):SET(63,I/2,3):NEXTI
5020 PRINT@66,"ALIEN INVASION LOOMS!";
5025 PRINT@98,"THEY ARE USING LETTERS OF";
5030 PRINT@130,"MASS DESTRUCTION AGAINST US.";
5035 PRINT@162,"THE CITY HAS A SHIELD";
5040 PRINT@194,"HOWEVER IT WON'T LAST LONG!";
5045 PRINT@226,"BE QUICK ON THE KEYS";
5050 PRINT@258,"EACH WAVE WILL GET FASTER.";
5060 PRINT@290,"AFTER EACH WAVE THERE IS A";
5065 PRINT@322,"CHALLENGE TO RACK UP POINTS.";
5070 PRINT@354,"WE KNOW THEY WILL BEGIN TO";
5075 PRINT@386,"INVADE ONCE THE SHIELD'S DOWN";
5080 PRINT@418,"PRESS <SPACE> WHEN READY...";
5085 I$=INKEY$:IF I$<>" "THEN 5085
5090 GOTO13
