6 CLS0:CLEAR250:DIM T(90),F(90)
7 K=10
60 FOR X=0 TO 90
65 T(X)=142
70 NEXT X
100 CLS
110 GOSUB 990
120 PRINT T$
121 FOR C=1 TO LEN(T$)
124 X=ASC(MID$(T$,C,1))
125 IF X<65 OR X>90 THEN 170
130 PRINTCHR$(T(X));
140 NEXT C
160 GOTO 200
170 IF X<32 THEN STOP
175 PRINT CHR$(X);
180 GOTO 140
200 PRINT
205 PRINT "COMMAND";
210 INPUT F$
220 IF MID$(F$,2,1)="=" THEN T(ASC(F$))=ASC(MID$(F$,3,1)+"Ž"):REM Ž=CHR$(142) THE CODE FOR BLANK LETTERS
222 IF F$="F" THEN 400
230 IF F$="" THEN 300
235 IF F$="R" THEN 100
232 IF F$="Q" THEN END
240 GOTO 100
300 PRINT "fREQUENCY REPORT"
310 PRINT "enter FOR HELP"
320 PRINT "A=B SUBSTITUTES A FOR B"
330 PRINT "A=  RETURN A TO BLANK"
340 PRINT "rEDISPLAY   qUIT"
350 PRINT "MOST COMMON ENGLISH LETTERS:"
360 PRINT "etaonrishdlfcmugypwbvkxjqz"
370 GOTO 200
400 CLS
410 GOSUB 990
420 FOR X=65 TO 90:F(X)=0:NEXT X:FT=0
430 L=LEN(T$)
440 FOR C=1 TO L
460 N=ASC(MID$(T$,C,1))
480 IF N<65 OR N>90 THEN L=L-1:GOTO490
483 F(N)=F(N)+1
490 NEXT C
500 FOR X=65 TO 90
505 S=INT((F(X)*100/L)*K)/K
510 PRINT CHR$(X)"("MID$(STR$(S),2)")",
520 F(X)=S
530 NEXT X
540 GOTO 200
990 T$="UVQ JVQQXZX KQ JQYGZKZ UBKV YWJVBPZX WJJZGKX KVZ JQPMBKBQPX QC XFWDZLH"
995 IF T$="" THEN INPUTT$
999 RETURN
