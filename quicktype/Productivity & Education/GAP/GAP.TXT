1 REM ACCOUNTING PROGRAM
2 REM * BY LARRY ALLEN *
3 REM        V1.2
10 CLS:CLEAR4000
20 DIM DL$(100),AM(100),FILE$(1),TR(1)
22 TR=0
25 PRINT"   generic€accounting€program"
26 PRINT"         by€larry€allen":PRINT
30 INPUT"FILE NAME";FILE$
35 INPUT"CURRENT DATE(MM/DD/YY)";DATE$
40 CLS:PRINT"file:";FILE$
42 PRINT
45 PRINT"         date:";DATE$
50 PRINT:PRINT"     1. LOAD OLD FILE"
55 PRINT"     2. SAVE CURRENT FILE"
60 PRINT"     3. ENTER DATA"
65 PRINT"     4. CALCULATE ACCOUNT"
70 PRINT"     5. CORRECT ERRORS
75 A$=INKEY$:IF A$=""THEN 75
80 A=VAL(A$):IFA<1 OR A>5 THEN 75
85 ON A GOTO 100,200,300,400,500
90 GOTO 75
100 CLS:PRINT
120 PRINT:PRINT"PLACE FILE TAPE IN RECORDER AND REWIND. PRESS <ENTER> WHEN READY"
130 INPUTDD$
140 PRINT:PRINT"PRESS <PLAY> BUTTON ON RECORDER"
160 CLOAD*AM
180 CLOAD*TR
185 TR=TR(1)
190 GOTO 40
200 CLS:PRINT
210 PRINT"POSITION TAPE IN RECORDER.","PRESS <RECORD> BUTTON, THEN","PRESS <ENTER> TO SAVE DATA"
220 TR(1)=TR
230 INPUTDD$
255 FOR TT=1 TO 200:NEXT TT
260 CSAVE*AM
265 FOR TT=1 TO 200:NEXT TT
280 CSAVE*TR
290 GOTO 40
300 CLS:PRINT
310 PRINT"(C)REDIT, (D)EBIT OR (M)ENU"
315 A$=INKEY$:IF A$=""THEN 315
316 IF A$="M"THEN 40
317 IF A$="D"THEN P=-1:GOTO 320
318 IF A$="C"THEN P=1:GOTO 320
319 GOTO 315
320 TR=TR+1
322 PRINT"ITEM #";TR
325 INPUT"ITEM NAME";DL$(TR)
330 INPUT"AMOUNT=";XX
335 DL$(TR)=LEFT$(DL$(TR),20)
340 AM(TR)=XX*P
350 PRINT:GOTO 310
400 CLS:PRINT
410 PRINT"OUTPUT TO SCREEN OR PRINTER"
415 PRINTTAB(13)"(S/P)"
420 A$=INKEY$:IF A$=""THEN 420
425 IF A$="S"THEN 600
430 IF A$="P"THEN 450
440 GOTO 420
450 PRINT"output to printer - IF PRINTER  IS CONNECTED AND TURNED ON, THENPRESS <ENTER>":INPUTDD$
452 BA=0
454 LPRINT FILE$,,DATE$
455 FOR X=1TOTR
456 LPRINT" "
458 BA=BA+AM(X)
459 BA=(INT(BA*100+.5))/100
460 M=40
462 IF ABS(AM(X))<100 THEN M=41
464 IF ABS(AM(X))<10  THEN M=42
466 IF ABS(AM(X))<1   THEN M=43
470 LPRINTX;TAB(5)DL$(X);TAB(M)AM(X);TAB(55)BA
475 NEXT X
480 LPRINT" ":LPRINTTAB(30)"BALANCE-";TAB(55)BA
490 GOTO 40
500 CLS:PRINT
510 PRINT"ITEM NUMBER TO CORRECT?"
512 INPUTNC
514 PRINT"(C)REDIT, (D)EBIT OR (M)ENU"
516 A$=INKEY$:IF A$=""THEN 516
518 IF A$="M"THEN 40
520 IF A$="C"THEN P=1:GOTO 530
522 IF A$="D"THEN P=-1:GOTO 530
524 GOTO 516
530 INPUT"ITEM NAME";DL$(NC)
535 DL$(NC)=LEFT$(DL$(NC),20)
540 INPUT"AMOUNT=";XX
545 AM(NC)=XX*P
550 GOTO40
600 PRINT"screen€output-":PRINT
610 PRINT"DO YOU WANT A RUNNING TOTAL?"
612 PRINTTAB(13)"(Y/N)"
615 A$=INKEY$:IF A$=""THEN 615
620 IF A$="Y" THEN ST=5
625 IF A$="N" THEN ST=10
630 IF A$<>"Y" AND A$<>"N" THEN 615
635 SS=0:BA=0
638 CLS
640 PRINT FILE$;TAB(21)DATE$
645 PRINT
650 FOR X=1 TO TR
651 M=23
652 IF ABS(AM(X))<100 THEN M=24
653 IF ABS(AM(X))<10  THEN M=25
654 IF ABS(AM(X))<1   THEN M=26
655 PRINTX;TAB(4)DL$(X);TAB(M)AM(X)
656 BA=BA+AM(X)
657 BA=(INT(BA*100+.5))/100
658 IF ST=5 THEN PRINT"     BALANCE-";BA
660 SS=SS+1:IF SS=ST THEN 700
670 NEXT X
680 PRINT:PRINT"     BALANCE=";BA
685 PRINT
690 PRINT"PRESS <ENTER> FOR MENU":INPUTDD$
695 GOTO 40
700 PRINT:SS=0:INPUT"PRESS <ENTER> TO CONTINUE";DD$
705 CLS:PRINT
710 PRINTFILE$;TAB(21)DATE$
720 NEXT X
730 GOTO 680
