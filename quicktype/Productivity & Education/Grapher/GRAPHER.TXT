10 CLEAR200:DIMA$(26),D(20),XT,NX,T1,B,XT,X,Y,SL,SY,DY,X(11),Y(11),CO,A1,TI$,LO,IT$(20)
20 GOTO110
30 XC=0:YC=R:PC=3-2*R 
35 GOSUB70
40 IFPC<0THENPC=PC+4*XC+6:GOTO50
45 PC=PC+4*(XC-YC)+10:YC=YC-1   
50 XC=XC+1
55 IFXC<YCTHEN35
60 IFXC=YCTHENGOSUB70 
65 RETURN
70 XX=CX+XC:YY=CY+YC:SET(XX,YY,CO)
71 XX=CX-XC:YY=CY+YC:SET(XX,YY,CO)
72 XX=CX+XC:YY=CY-YC:SET(XX,YY,CO)
73 XX=CX-XC:YY=CY-YC:SET(XX,YY,CO)
74 XX=CX+YC:YY=CY+XC:SET(XX,YY,CO)
75 XX=CX-YC:YY=CY+XC:SET(XX,YY,CO) 
76 XX=CX+YC:YY=CY-XC:SET(XX,YY,CO)
77 XX=CX-YC:YY=CY-XC:SET(XX,YY,CO)
79 RETURN
110 PI=3.1415966:CX=32:CY=16:R=12:CO=2
130 GOTO10000
200 REM DELAY UNTIL ENTER IS HIT
210 PRINT@452,"PRESS ENTER TO CONTINUE";
215 CO=2:FL=0:GOSUB600
220 C$=INKEY$:IFC$=""THEN220
230 IFC$=CHR$(13)THEN260
240 IFC$="P"ANDFL>0THENGOSUB7000:GOTO260
250 GOTO220
260 RETURN
350 REM GET A NUMBER 1 TO MAX
355 PRINT:PRINTTAB(10);"CHOOSE 1 -";MX;
360 RE$=INKEY$:C=VAL(RE$)
365 GOSUB600
370 IFC<1ORC>MXTHEN360
375 RETURN
400 DX=X(1)-X(0):DY=Y(1)-Y(0):SX=SGN(DX):SY=SGN(DY)
440 IFDX=0THEN550
450 SL=DY/DX:B=Y(0)-SL*X(0)
460 T1=SQR(SL*SL+1)
470 NX=1/T1*SX
480 FORXT=X(0)TOX(1)STEPNX
490 X=INT(XT+.5)
500 Y=INT(SL*XT+B+.5)
510 SET(X,Y,CO)
520 NEXTXT
525 RETURN
550 X=X(0)
560 FORY=Y(0)TOY(1)STEPSY
570 SET(X,Y,CO)
580 NEXT
585 RETURN
599 REM BORDER
600 FORY=0TO31:SET(0,Y,CO):SET(63,31-Y,CO):NEXT
620 FORX=0TO63:SET(X,0,CO):SET(63-X,31,CO):NEXT
630 CO=CO+1:IFCO=9THENCO=2
699 RETURN
2000 REM BAR GRAPH
2005 IFN=0THENCLS:PRINT:PRINT:PRINTTAB(12);"no data!":SOUND150,2:GOSUB200:RETURN
2010 CO=2:FL=2
2100 CLS0:PRINT@(32-LEN(TI$))/2,TI$;
2105 PRINT@64,MAX;:PRINT@448,MIN;:PRINT@224+32,INT(MIN+(MAX-MIN)/2+0.5);
2110 FORI=1TON
2115 YTP=26-INT(D(I)/(MAX/26)):XST=4+(30-N):IF(XST/2<>INT(XST/2))THENXST=XST-1
2120 FORY=4+YTP TO 29
2130 FORX=0TO1
2150 SET(XST+I*2+X,Y,CO)
2160 NEXTX,Y
2170 CO=CO+1:IF(CO=8)THENCO=2
2180 NEXT I
2190 LO=XST/2+481
2200 FORI=1TON
2210 POKE16383+LO+I,I
2220 NEXT I
2250 REM SCREEN 0,1
2900 GOSUB220
2999 FL=0:RETURN
3000 REM PIE GRAPH
3003 IFN=0THENCLS:PRINT:PRINT:PRINTTAB(12);"no data!":SOUND150,2:GOSUB200:RETURN
3005 CO=6:FL=1:ARC=0.020:X1=R+CX:Y1=CY:X=X1+12:Y=CY
3010 CLS0:PRINT@(32-LEN(TI$))/2,TI$;
3030 X(0)=CX:Y(0)=CY:X(1)=X1:Y(1)=Y1:GOSUB400
3040 GOSUB30
3050 FORI=1TON
3055 A1=ARC+(D(I)/2)/SUM:ARC=ARC+D(I)/SUM:IF(D(I)/SUM<=0.02)THEN3200
3060 IF(ARC>1.00)THEN3135
3075 PHASE=-PI/2
3080 X1=INT(CX-R*SIN(2*PI*(ARC-0.01)+PHASE)+0.5):Y1=INT(CY+R*COS(2*PI*(ARC-0.01)+PHASE)+0.5)
3100 X(0)=CX:Y(0)=CY:X(1)=X1:Y(1)=Y1:GOSUB400
3120 REM
3135 X=INT(CX-(R+2.5)*SIN(2*PI*(A1-0.015)+PHASE)+0.5):Y=INT(CY+(R+2.5)*COS(2*PI*(A1-0.015)+PHASE)+0.5)
3140 GOSUB5000
3200 NEXTI:GOSUB30
3300 GOSUB220
3999 FL=0:RETURN
5000 POKEINT(Y/2)*32+INT(X/2)+16384,I
5999 RETURN
6000 REM FETCH DATA
6005 SUM=0
6010 CLS:PRINT:PRINTTAB(11);"DATA ENTRY"
6015 PRINTTAB(11);"==========":PRINT
6016 PRINT"GRAPH TITLE:":INPUT TI$
6020 INPUT"NUMBER OF ITEMS (1-20)";N
6030 IFN<1ORN>20ORN<>INT(N)THENSOUND150,3:PRINT"bad number!":GOTO6020
6040 INPUT"MAXIMUM DATA VALUE";MAX
6050 IF(MAX<=0)THENSOUND150,3:PRINT"bad number!":GOTO6040
6060 INPUT"MINIMUM DATA VALUE";MIN
6070 IF(MIN<0)OR(MIN>MAX)THENSOUND150,3:PRINT"bad number!":GOTO6060
6080 PRINT:PRINTTAB(11);"ENTER DATA":PRINT
6090 FORI=1TON
6100 PRINT"ITEM ";CHR$(64+I);:INPUT D(I)
6110 IFD(I)<MINORD(I)>MAXTHENSOUND150,3:PRINT"bad number!":GOTO6100
6112 PRINT"TITLE";:INPUT IT$(I)
6115 SUM=SUM+D(I)
6120 NEXTI
6999 RETURN
7000 LPRINT TAB((64-LEN(TI$))/2);TI$:LPRINT:FORY=2TO31:FORX=0TO63:IFPOINT(X,Y)=-1THENGOSUB8000:GOTO7030
7010 IFPOINT(X,Y)>0ANDFL=1THENLPRINT"*";:GOTO7030
7015 IFPOINT(X,Y)>0ANDFL=2THENLPRINT"* ";:X=X+1:GOTO7030
7020 LPRINT" ";
7030 NEXT:LPRINT:NEXT
7040 LPRINT:FORI=1TON:IFIT$(I)<>""THENLPRINTCHR$(64+I);" = ";IT$(I)
7050 NEXT:RETURN
8000 P=PEEK(16384+(INT(Y/2)*32)+INT(X/2)):POKE16384+(INT(Y/2)*32)+INT(X/2),128
8010 IFP<32THENP=P+64
8011 IFP>95THENP=P-64
8020 LPRINTCHR$(P);
8030 RETURN
10000 REM MAINLINE
10010 CLS:PRINT:PRINT:PRINT
10020 PRINTTAB(5);"PRESS ENTER FOR A DEMO."
10030 PRINTTAB(5);"TO SKIP THE DEMO, PRESS"
10040 PRINTTAB(5);"ANY OTHER KEY."
10060 GOSUB600:C$=INKEY$
10070 IFC$=""THEN10060
10075 IFC$<>CHR$(13)THEN10140
10080 N=20:MAX=100
10090 FORI=1TO20
10100 D(I)=RND(MAX)
10110 SUM=SUM+D(I)
10120 NEXTI
10130 GOSUB2000
10135 GOSUB3000
10140 CLS:PRINT:PRINT:PRINTTAB(12);"GRAPHER":PRINT
10150 PRINT:PRINTTAB(8);"1. NEW DATA"
10160 PRINT:PRINTTAB(8);"2. BAR GRAPH"
10170 PRINT:PRINTTAB(8);"3. PIE GRAPH"
10180 PRINT:PRINTTAB(8);"4. QUIT"
10190 MX=4:GOSUB350
10200 ON C GOSUB 6000,2000,3000
10210 IF C=4 THEN CLS:END
10220 GOTO10140
