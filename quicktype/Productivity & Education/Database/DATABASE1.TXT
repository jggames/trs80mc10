0 CLS:CLEAR500:GOTO1500
1 DATA"THIS IS A DATA ENTRY FOR TESTING THE DATABASE PROGRAM."
298 DATA"END"
299 DATA"TESTDATA"
300 CLS0:IFNA$<>""THENPRINT@39,"FILE   : "LEFT$(NA$+"        ",8);:PRINT@71,"ENTRIES:"LEFT$(STR$(NE)+"        ",9);
310 PRINT@109,"menu";
375 PRINT@141,;:C1=RND(4):FORI=C1TOC1+3:PRINTCHR$(140+16*I);:NEXT:CC=163
400 PRINT@CC,"D) DEFINE A NEW DATABASE ";:CC=CC+64
550 IFNA$=""THEN900
600 PRINT@CC,"I) INSERT/DELETE AN ENTRY";:CC=CC+64
800 PRINT@CC,"V) VIEW THE DATA         ";:CC=CC+64
850 PRINT@CC,"S) SAVE THE DATABASE     ";:CC=CC+64
900 PRINT@CC,"E) EXIT THE PROGRAM      ";:CC=CC+64
950 PRINT@C,"ENTER THE LETTER OF YOUR CHOICE";
1000 GOSUB9000:DU$=Z$:IFDU$=""THEN1325
1010 IFDU$="D"THEN2000
1050 IFDU$="I"THEN4000
1250 IFDU$="V"THEN7000
1300 IFDU$="S"THEN3000
1325 IFDU$="E"THENCLS:END
1350 GOTO300

1500 RESTORE:FORI=1TO64:FL$=FL$+CHR$(127+16*RND(8)):F2$=F2$+CHR$(128):NEXT
1510 READM$:IFM$<>"END"THENNE=NE+1:GOTO1510
1520 READNA$:GOTO300

1600 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
1610 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN

2000 CLS0
2020 PRINT@66,"DEFINITION OF A NEW DATABASE";
2030 IFNA$=""THEN2190
2040 SOUND110,3:SOUND80,3
2050 PRINT@131,"WARNING--DATA WILL BE LOST";
2100 PRINT@168,"CONTINUE? (Y/N)";:GOSUB9000:DU$=Z$
2110 IFDU$<>"Y"THEN300
2120 PRINT@224,"TYPE THE FOLLOWING:":PRINT"299 DATA"CHR$(34)"INSERT DATABASE NAME"CHR$(34)
2130 PRINT"THEN HIT ENTER AND TYPE":PRINT"1 AND HIT ENTER. THEN TYPE":PRINT"RUN AGAIN AND HIT ENTER":END

3000 CLS0:PRINT@67,"SAVE THE DATA TO TAPE FILE";:IFNA$=""THEN300
3005 SOUND120,3:SOUND90,3
3010 PRINT@129,"ABOUT TO SAVE CURRENT DATABASE";
3020 PRINT@168,"CONTINUE? (Y/N)";:GOSUB9000:DU$=Z$
3060 IFDU$<>"Y"THEN300
3100 PRINT@226,"ENTER FILENAME OF DATABASE";:ZP=265:GOSUB9200:IFZ2$=""THENZ2$=NA$
3125 PRINT@388,"READY CASSETTE FOR SAVING";:PRINT@420,"PRESS 'PLAY' AND 'RECORD'";
3165 GOSUB9710:PRINT@458," SAVING... ";
3170 CSAVEZ2$
3180 GOTO300

4000 CLS0:PRINT@32*2+4,"INSERT/DELETE AN ENTRY";
4090 IFNE<298THEN4300
4095 SOUND145,3:SOUND105,3
4100 PRINT@228,"THE DATABASE IS FULL!";
4150 GOSUB9710:GOTO300

4300 FORI=224TO448STEP64:PRINT@I,F2$;:NEXT
4315 PRINT@132,"ENTER NUMBER OF ENTRY ";
4350 PRINT@164,"(0 TO RETURN TO MENU) ";
4400 ZP=187:GOSUB9200:TA=VAL(Z2$)
4410 IFTA=0THEN300
4420 PRINT@224,"TO INSERT TYPE THE FOLLOWING:":PRINTZ2$" DATA"CHR$(34)"INSERT ENTRY HERE"CHR$(34)
4430 PRINT"THEN HIT ENTER"
4440 PRINT"TYPE "Z2$" AND HIT ENTER TO DELETE"
4450 PRINT"WHEN DONE TYPE RUN AND HIT ENTER";:END
7000 CLS0
7040 PRINT@32*2+9,"VIEW AN ENTRY";
7075 PRINT@164,"(0 TO RETURN TO MENU ) ";
7080 ZP=187:GOSUB9200:TA=VAL(Z2$)
7100 IFTA<=NEANDTA>0THEN7180
7120 IFTA=0THEN300
7125 PRINT@321,"YOU DO NOT HAVE AN ENTRY"TA;
7140 SOUND90,3:SOUND120,3
7150 FORT=0TO2500:NEXT
7175 GOTO7000
7180 GZ$="VIEW AN ENTRY":GOSUB9500
7500 GOTO7000

9000 PRINT@.,MID$(FL$,RND(32),32);:PRINT@480,MID$(FL$,RND(32),31);:POKE16895,143+RND(7)*16
9005 IFZFTHENPRINT@ZP,CHR$(RND(128)+127)CHR$(128);
9010 Z$=INKEY$:IFZ$=""THEN9000
9020 RETURN

9200 REM ***INPUT ROUTINE***
9210 Z2$="":ZF=1
9220 GOSUB9000:IFZ$=CHR$(13)THENPRINT@ZP,CHR$(128);:ZF=0:RETURN
9225 IFZ$=","ORZ$=":"THENZ$="-"
9230 PRINT@ZP,Z$;:ZP=ZP+1
9240 IFZ$=CHR$(8)ANDZ2$<>""THENZ2$=LEFT$(Z2$,LEN(Z2$)-1):ZP=ZP-2:GOTO9220
9250 IFZ$=<CHR$(32)THENSOUND1,1:GOTO9220
9260 Z2$=Z2$+Z$:GOTO9220

9500 REM ***FILE OUTPUT***
9505 CLS0:PRINT@.,MID$(FL$,RND(32),2)NA$;MID$(FL$,RND(32),29-LEN(NA$));
9506 PRINT@480,MID$(FL$,RND(32),29-LEN(NA$));:POKE16895,143+RND(7)*16
9520 PRINT@33,"ENTRY"STR$(TA);:PRINT@48,"->"GZ$;
9530 PRINT@64,"================================";
9540 LN=0
9550 RESTORE:FORL1=1TOTA:READM$:NEXT:PRINT@96,;:GOSUB1600
9710 PRINT@458,"HIT ANY KEY";
9720 ZF=0:GOSUB9000
9740 RETURN
