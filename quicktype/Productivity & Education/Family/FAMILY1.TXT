0 CLEAR450:DIMC1,C2,M$,M,CH$(2),EY$(2),HA$(2),HH$(2),EE$(2):M=16384:GOTO150
1 C2=1:C1=32:FORC1=C1TOC2STEP-1:I$=MID$(M$,C1,1):IFI$<"!"THEN?MID$(M$,C2,C1-C2):C2=C1+1:C1=C2+32:IFI$=""THENC1=.
2 C1=C1+(C1>255)*(C1-255):NEXT:M$="":RETURN
7 C1=(PEEK(17024)AND1)*256+PEEK(17025)-1+M:FORC2=1TOLEN(M$):POKEC1+C2,ASC(MID$(M$,C2))AND63:NEXT:RETURN
8 PRINT@32*3+16,"€€€€€€€€€€€€€€€€";:PRINT@32*3+16,;:RETURN

150 REM HP<=35,VP<=19
170 X=RND(-(PEEK(9)*256+PEEK(10)))
180 REM FNR(X)=RND(2)
190 NO$="°Έ€"
200 CH$(1)="":REM FEM
210 CH$(2)="ϊψ":REM HAIR
220 EY$(1)="„„€":HH$(1)="Y"
230 EY$(2)="ΤΤ€":HH$(2)="O"
240 HA$(1)="““€":EE$(1)="G"
250 HA$(2)="σσ€":EE$(2)="B"
260 REM W OVER G, R OVER S
270 GOSUB1430
280 PRINT@9*32+9," DO YOU NEED ";
281 PRINT@10*32+9,"INSTRUCTIONS?";
290 ZZ$=INKEY$:IFZZ$=""THEN290
300 IFLEFT$(ZZ$,1)="Y"THEN1520
310 IFLEFT$(ZZ$,1)<>"N"THEN290

320 CLS:PRINT"DO YOU WANT TO GUESS THE"
330 INPUT"GENOTYPES (Y/N)";GE$
350 GE$=LEFT$(GE$,1):IFGE$="N"THEN370
360 GOTO430
370 CLS:PRINT"ARE THE GENES FOR THE PARENTS":PRINT"TO BE CHOSEN RANDOMLY (Y/N)";
380 INPUTZZ$
390 IFZZ$=""THEN370
400 IFLEFT$(ZZ$,1)="N"THEN500
410 IFLEFT$(ZZ$,1)<>"Y"THEN320
420 REM RANDOM
430 FORI=1TO2:FE$(I)=EE$(1):IFRND(0)>.5THENFE$(I)=EE$(2)
440 FH$(I)=HH$(1):IFRND(0)>.5THENFH$(I)=HH$(2)
450 NEXT
460 FORI=1TO2:ME$(I)=EE$(1):IFRND(0)>.5THENME$(I)=EE$(2)
470 MH$(I)=HH$(1):IFRND(0)>.5THENMH$(I)=HH$(2)
480 NEXT
490 GOTO1090
500 SL=3*32:PA=SL
510 CLS:PRINT"CHOOSE GENES FOR FATHER'S HAIR:"
520 PRINT:PRINT"YO"
530 E$(1)="":E$(2)=""
540 GOSUB2020
550 POKEM+PA,94
560 FORI=1TO2
570 GOSUB1960
580 FH$(I)=HH$(2):IFPA=SLTHENFH$(I)=HH$(1)
590 H$(I)=FH$(I)
600 NEXT
610 VP=2:HP=1:SEX=2
620 GOSUB1920
630 GOSUB1360
640 GOSUB2040
660 PRINT@0,"CHOOSE GENES FOR FATHER'S EYES: ";
670 PRINT:PRINT"GB"
680 GOSUB2020
690 POKEM+PA,94
700 FORI=1TO2
710 GOSUB1960
720 FE$(I)=EE$(2):IFPA=SLTHENFE$(I)=EE$(1)
730 E$(I)=FE$(I)
740 NEXT
750 VP=2:HP=1:SEX=2
760 GOSUB1920
770 GOSUB1360
780 GOSUB2040
790 GOSUB1890
800 CLS:PRINT"CHOOSE GENES FOR MOTHER'S HAIR:"
810 PRINT:PRINT"YO"
820 GOSUB2020
830 E$(1)="":E$(2)=""
840 POKEM+PA,94
850 FORI=1TO2
860 GOSUB1960
870 MH$(I)=HH$(2):IFPA=SLTHENMH$(I)=HH$(1)
880 H$(I)=MH$(I)
890 NEXT
900 VP=2:HP=1:SEX=1
910 GOSUB1920
920 GOSUB1360
930 GOSUB2040
950 PRINT@0,"CHOOSE GENES FOR MOTHER'S EYES: ";
960 PRINT:PRINT"GB"
970 GOSUB2020
980 POKEM+PA,94
990 FORI=1TO2
1000 GOSUB1960
1010 ME$(I)=EE$(2):IFPA=SLTHENME$(I)=EE$(1)
1020 E$(I)=ME$(I)
1030 NEXT
1040 VP=2:HP=1:SEX=1
1050 GOSUB1920
1060 GOSUB1360
1070 GOSUB2040
1080 GOSUB1890
1090 CLS0:PRINT"New€parents€€€€€€€€€€father€€€€€More€kids€€€€€€€€€€€€MOTHER€€€€€End";
1100 VP=1:HP=4:SEX=2
1110 FORI=1TO2:H$(I)=FH$(I):E$(I)=FE$(I):NEXT
1120 GOSUB1920
1130 GOSUB1360
1140 VP=1:HP=5:SEX=1
1150 FORI=1TO2:H$(I)=MH$(I):E$(I)=ME$(I):NEXT
1160 GOSUB1920
1170 GOSUB1360
1180 REM OFFSPRING
1190 FORI=1TO3:FORJ=0TO7
1200 VP=1+I:HP=1+J
1210 H$(1)=FH$(RND(2)):H$(2)=MH$(RND(2))
1220 E$(1)=FE$(RND(2)):E$(2)=ME$(RND(2))
1230 HC=1:IFH$(1)=HH$(2)ANDH$(2)=HH$(2)THENHC=2
1240 EC=1:IFE$(1)=EE$(2)ANDE$(2)=EE$(2)THENEC=2
1250 SEX=RND(2)
1260 GOSUB1360
1270 NEXTJ,I
1280 IFGE$="Y"THENGOSUB2070
1300 GOSUB1900
1310 IFZZ$="N"THEN320
1320 IFZZ$="M"THEN1190
1330 IFZZ$<>"E"THEN1300
1340 CLS:END

1350 REM DRAW FACE
1360 CC=(VP-1)*128+(HP-1)*4
1370 PRINT@CC,HA$(HC);
1380 PRINT@CC+32,EY$(EC);:IFSEX=1THENPRINTCHR$(8)LEFT$(CH$(HC),1);
1390 PRINT@CC+64,NO$;:IFSEX=1THENPRINTCHR$(8)RIGHT$(CH$(HC),1);
1400 PRINT@CC+96,;:IFGE$="N"THENIF1ANDHPTHENPRINTH$(1)H$(2)E$(1)E$(2);:RETURN
1410 IFGE$="N"THENM$=H$(1)+H$(2)+E$(1)+E$(2):GOSUB7
1420 RETURN

1429 REM TITLE SCREEN
1430 CLS0:FORI=1TO8:HP=I:VP=1:HC=RND(2):H$(1)=HH$(HC):H$(2)=HH$(HC)
1431 EC=RND(2):E$(1)=EE$(EC):E$(2)=EE$(EC)
1432 SEX=RND(2):GOSUB1360:VP=4:GOSUB1360:NEXT
1440 FORI=2TO3:HP=1:VP=I:HC=RND(2):H$(1)=HH$(HC):H$(2)=HH$(HC)
1441 EC=RND(2):E$(1)=EE$(EC):E$(2)=EE$(EC)
1442 SEX=RND(2):GOSUB1360:HP=8:GOSUB1360:NEXT
1450 PRINT@5*32+10,"  FAMILY   ";
1490 PRINT@6*32+10,"ANDY GAMBLE";
1510 RETURN

1520 CLS:PRINTTAB(12)"family":PRINT
1530 M$="THIS PROGRAM SHOWS YOU A TYPICAL FAMILY OF MARTIANS: TWO PARENTS AND THEIR 24 CHILDREN.":GOSUB1
1540 M$="YOU CAN TELL THE DIFFERENCE BETWEEN MALE AND FEMALE MARTIANS VERY EASILY. "
1550 M$=M$+"MALES HAVE SHORT HAIR AND FEMALES HAVE LONG HAIR. "
1560 M$=M$+"OTHER THAN THAT THEY HAVE YELLOW OR ORANGE HAIR, AND GREEN OR LIGHT BLUE EYES.":GOSUB1:GOSUB1890
1570 CLS:M$="THESE TRAITS ARE CONTROLLED BY GENES Y AND O FOR THE HAIR AND G AND B FOR THE EYES.":GOSUB1
1580 M$="Y IS DOMINANT OVER O AND G IS DOMINANT OVER B.":GOSUB1
1590 PRINT:M$="HERE ARE TWO TYPICAL MARTIANS:":GOSUB1
1640 VP=3:HP=4:SEX=1:H$(1)="O":H$(2)="Y":E$(1)=EE$(1):E$(2)=EE$(2):EC=1:HC=1
1650 GOSUB1360
1660 VP=3:HP=5:SEX=2:H$(1)="O":H$(2)="O":E$(1)="B":E$(2)="B":EC=2:HC=2
1670 GOSUB1360
1680 GOSUB1890
1690 CLS:M$="YOU MAY CHOOSE THE GENES FOR THE HAIR AND EYES OF BOTH THE MOTHER AND THE "
1710 M$=M$+"FATHER WHEN THE PROGRAM REQUESTS IT, BY CHOOSING TWO GENES SUCH AS 'GG' "
1730 M$=M$+"OR 'RR' - WHATEVER YOU LIKE.":GOSUB1
1740 M$="YOU CAN ALSO LET THE PROGRAM CHOOSE THE PARENTS' GENES RANDOMLY.":GOSUB1:GOSUB1890:CLS
1760 M$="AFTER YOU ARE SHOWN THE OFFSPRING, YOU WILL HAVE A CHOICE AS TO MORE OFFSPRING "
1780 M$=M$+"BY THE SAME PARENTS, DIFFERENT PARENTS, OR ENDING THE PROGRAM.":GOSUB1
1800 M$="ALTERNATIVELY YOU CAN CHOOSE TO BE SHOWN THE FACES, AND TRY TO GUESS THE "
1820 M$="PARENTS' GENES (THEIR GENOTYPES).":GOSUB1:REM GOSUB1890:CLS
1830 M$="NOTE THAT THE COMPUTER HAS IN MIND ONE PARTICULAR SET OF GENES: OTHERS MAY BE "
1850 M$=M$+"POSSIBLE BUT WILL GIVE A WRONG ANSWER.":GOSUB1
1860 GOSUB1890
1870 GOTO320

1880 REM GET-CONT
1890 PRINT@484,;:M$="PRESS ANY KEY TO CONTINUE":GOSUB7
1900 ZZ$=INKEY$:IFZZ$=""THEN1900
1910 RETURN

1920 HC=1:IFH$(1)=HH$(2)ANDH$(2)=HH$(2)THENHC=2
1930 EC=1:IFE$(1)=EE$(2)ANDE$(2)=HH$(2)THENEC=2
1940 RETURN

1950 REM CHOOSE GENES
1960 GOSUB1900:IFZZ$=""THEN1960
1970 IFZZ$="A"ANDPA=SL+1THENPOKEM+PA,96:PA=SL:POKEM+PA,94
1980 IFZZ$="S"ANDPA=SLTHENPOKEM+PA,96:PA=SL+1:POKEM+PA,94
1990 IFZZ$<>CHR$(13)THEN1960
2000 SOUND150,1:RETURN

2010 REM MESSAGE
2020 PRINT@448,"a TO MOVE LEFT, s FOR RIGHT.";
2030 PRINT@480,"HIT enter WHEN GENE IS CHOSEN.";:RETURN
2040 PRINT@448,"                            ";
2050 PRINT@480,"                              ";:RETURN
2060 REM HAIR GENE GUESS
2070 PRINT@0 ,"HAIR GENES ";
2071 PRINT@32,"FOR 1 PAR- ";
2072 PRINT@64,"ENT (Y/O)? ";
2080 FG=0
2090 GOSUB1900
2100 IFZZ$=HH$(1)ORZZ$=HH$(2)THEN2130
2110 IFZZ$="E"THEN1340
2120 GOSUB1900
2130 HG$(1)=ZZ$
2140 GOSUB1900:IFZZ$=HH$(1)ORZZ$=HH$(2)THEN2160
2150 GOSUB1900
2160 HG$(2)=ZZ$
2170 GOSUB8
2180 IFFH$(1)=HG$(1)ANDFH$(2)=HG$(2)THEN2220
2190 IFFH$(1)=HG$(2)ANDFH$(2)=HG$(1)THEN2220
2200 GOTO2250
2210 REM FATHER GUESS CORRECT
2220 PRINT@32*0+28,FH$(1)FH$(2);
2230 FG=1
2240 GOTO2340
2250 IFMH$(1)=HG$(1)ANDMH$(2)=HG$(2)THEN2290
2260 IFMH$(1)=HG$(2)ANDMH$(2)=HG$(1)THEN2290
2270 GOTO2320
2280 REM MOTHER GUESS CORRECT
2290 PRINT@32*1+28,MH$(1)MH$(2);
2300 GOTO2340
2310 REM BOTH WRONG
2320 PRINT@32*3+16,"WRONG! TRY AGAIN";
2330 GOTO2090
2340 PRINT@0,"HAIR GENES ";
2341 PRINT@32,"FOR OTHER  ";
2342 PRINT@64,"(Y/O)?     ";
2350 GOSUB1900
2360 IFZZ$=HH$(1)ORZZ$=HH$(2)THEN2390
2370 IFZZ$="E"THEN1340
2380 GOSUB1900
2390 HG$(1)=ZZ$
2400 GOSUB1900:IFZZ$=HH$(1)ORZZ$=HH$(2)THEN2420
2410 GOSUB1900
2420 HG$(2)=ZZ$
2430 GOSUB8
2440 IFFG=1THEN2520
2450 IFFH$(1)=HG$(1)ANDFH$(2)=HG$(2)THEN2500
2460 IFFH$(1)=HG$(2)ANDFH$(2)=HG$(1)THEN2500
2470 IFFG=0THEN2590
2480 GOTO2520
2490 REM FATHER GUESS CORRECT
2500 PRINT@32*0+28,FH$(1)FH$(2);
2510 GOTO2620
2520 IFMH$(1)=HG$(1)ANDMH$(2)=HG$(2)THEN2560
2530 IFMH$(1)=HG$(2)ANDMH$(2)=HG$(1)THEN2560
2540 GOTO2590
2550 REM MOTHER GUESS CORRECT
2560 PRINT@32*1+28,MH$(1)MH$(2);
2570 GOTO2620
2580 REM BOTH WRONG
2590 PRINT@32*3+16,"WRONG! TRY AGAIN";
2600 GOTO2350
2610 REM EYE GENE GUESS
2620 PRINT@0,"EYE GENES  ";
2621 PRINT@32,"FOR 1 PAR- ";
2622 PRINT@64,"ENT (G/B)? ";
2630 FG=0
2640 GOSUB1900
2650 IFZZ$=EE$(1)ORZZ$=EE$(2)THEN2680
2660 IFZZ$="E"THEN1340
2670 GOSUB1900
2680 EG$(1)=ZZ$
2690 GOSUB1900:IFZZ$=EE$(1)ORZZ$=EE$(2)THEN2710
2700 GOSUB1900
2710 EG$(2)=ZZ$
2720 GOSUB8
2730 IFFE$(1)=EG$(1)ANDFE$(2)=EG$(2)THEN2770
2740 IFFE$(1)=EG$(2)ANDFE$(2)=EG$(1)THEN2770
2750 GOTO2800
2760 REM FATHER GUESS CORRECT
2770 PRINT@32*0+30,FE$(1)FE$(2);
2780 FG=1
2790 GOTO2890
2800 IFME$(1)=EG$(1)ANDME$(2)=EG$(2)THEN2840
2810 IFME$(1)=EG$(2)ANDME$(2)=EG$(1)THEN2840
2820 GOTO2870
2830 REM MOTHER GUESS CORRECT
2840 PRINT@32*1+30,ME$(1)ME$(2);
2850 GOTO2890
2860 REM BOTH WRONG
2870 PRINT@32*3+16,"WRONG! TRY AGAIN";
2880 GOTO2640
2890 PRINT@0 ,"EYE GENES  ";
2891 PRINT@32,"FOR OTHER  ";
2892 PRINT@64,"(G/B)?     ";
2900 GOSUB1900
2910 IFZZ$=EE$(1)ORZZ$=EE$(2)THEN2940
2920 IFZZ$="E"THEN1340
2930 GOSUB1900
2940 EG$(1)=ZZ$
2950 GOSUB1900:IFZZ$=EE$(1)ORZZ$=EE$(2)THEN2970
2960 GOSUB1900
2970 EG$(2)=ZZ$
2980 GOSUB8
2990 IFFG=1THEN3070
3000 IFFE$(1)=EG$(1)ANDFE$(2)=EG$(2)THEN3050
3010 IFFE$(1)=EG$(2)ANDFE$(2)=EG$(1)THEN3050
3020 IFFG=1THEN3140
3030 GOTO3070
3040 REM FATHER GUESS CORRECT
3050 PRINT@32*0+30,FE$(1)FE$(2);
3060 GOTO3170
3070 IFME$(1)=EG$(1)ANDME$(2)=EG$(2)THEN3110
3080 IFME$(1)=EG$(2)ANDME$(2)=EG$(1)THEN3110
3090 GOTO3140
3100 REM MOTHER GUESS CORRECT
3110 PRINT@32*1+30,ME$(1)ME$(2);
3120 GOTO3170
3130 REM BOTH WRONG
3140 PRINT@32*3+16,"WRONG! TRY AGAIN";
3150 GOTO2900
3160 REM ALL CORRECT
3170 PRINT@32*3+16,"CORRECT! AGAIN?";
3180 GOSUB1900
3190 IFZZ$="Y"THEN320
3200 IFZZ$<>"N"THEN3180
3210 GOTO1340

4000 CLS0:REM PROOF OF CONCEPT
5000 PRINT"€€€€€€€€€€€€σσ€€““€€€€€€€€€€€€€";
5001 PRINT"New€parents€„„€€ΤΤ€€father€€€€€";
5002 PRINT"More€kids€€€€Έ€€°Έ€€MOTHER€€€€€";
5003 PRINT"End€€€€€€€€€yoxoXOXO€€€€€€€€€€€€";
5004 PRINT"““€σσπ€““€€σσ€€““€σσπ€““€€σσ€€";
5005 PRINT"ΤΤ€„„ϊ€ΤΤ€€„„€€ΤΤ€„„ϊ€ΤΤ€€„„€€";
5006 PRINT"°Έ€Έψ€πΈ€€€Έ€€°Έ€Έψ€πΈ€€€Έ€€";
5007 PRINT"XOXOyoxoXOXOxoxoXOXOyoxoXOXOxoxo";
5008 PRINT"““€σσπ€““€€σσ€€““€σσπ€““€€σσ€€";
5009 PRINT"ΤΤ€„„ϊ€ΤΤ€€„„€€ΤΤ€„„ϊ€ΤΤ€€„„€€";
5010 PRINT"°Έ€Έψ€πΈ€€€Έ€€°Έ€Έψ€πΈ€€€Έ€€";
5011 PRINT"XOXOyoxoXOXOxoxoXOXOyoxoXOXOxoxo";
5012 PRINT"““€σσπ€““€€σσ€€““€σσπ€““€€σσ€€";
5013 PRINT"ΤΤ€„„ϊ€ΤΤ€€„„€€ΤΤ€„„ϊ€ΤΤ€€„„€€";
5014 PRINT"°Έ€Έψ€πΈ€€€Έ€€°Έ€Έψ€πΈ€€€Έ€€";
5015 PRINT"XOXOyoxoXOXOxoxoXOXOyoxoXOXOxox";:POKE16895,ASC("O")-64
6000 GOTO6000

6100 REM FAMILY
6110 REM ANDY GAMBLE JUNE 81
6120 REM COLUMBIA COLLEGE,
6125 REM 1619 W10 AVE
6130 REM VANCOUVER BC V6J 2A2
6140 REM MC-10 EDITS JIM GERRIE
