10 CLS
25 PRINT"GAME OF HANGMAN":PRINT
30 DIM P$(12,12),L$(20),D$(20),N$(26),U(50):FORC1=1TO32:BL$=BL$+CHR$(8):NEXT
40 C=1:N=50:C1=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
50 FOR I=1 TO 20:D$(I)="-":NEXT:M=0
60 FOR I=1 TO 26:N$(I)="":NEXT
70 FOR I=1 TO 12:FOR J=1 TO 12:P$(I,J)=" ":NEXT J:NEXT I
80 FOR I=1 TO 12:P$(I,1)="X":NEXT
90 FOR I=1 TO 7:P$(1,I)="X":NEXT:P$(2,7)="X"
95 IF C<N THEN 100
96 PRINT "YOU DID ALL THE WORDS!":END
100 Q=INT(N*RND(0)+1)
101 IF U(Q)=1 THEN 100
120 U(Q)=1:C=C+1:RESTORE:T1=0
150 FOR I=1 TO Q:READ A$:NEXT
160 L=LEN(A$):FOR I=1 TO L:L$(I)=MID$(A$,I,1):NEXT
170 PRINT"HERE ARE THE LETTERS YOU USED:"
180 FOR I=1 TO 26:PRINT N$(I);:IF N$(I+1)="" THEN C1=I:I=26:NEXT I:I=C1:GOTO 200
190 PRINT ",";:NEXT I
200 PRINT:FOR I=1 TO L:PRINT D$(I);:NEXT I:PRINT
210 INPUT "WHAT IS YOUR GUESS";G$:R=0:G$=LEFT$(G$,1):IF G$="" THEN 210
220 FOR I=1 TO 26:IF N$(I)="" THEN C1=I:I=26:NEXT I:I=C1:GOTO 250
230 IF G$=N$(I) THEN PRINT "YOU GUESSED THAT LETTER BEFORE":C1=I:I=26:NEXT I:I=C1:GOTO 170
240 NEXT I:PRINT "PROGRAM ERROR. RUN AGAIN.":STOP
250 N$(I)=G$:T1=T1+1
260 FOR I=1 TO L:IF L$(I)=G$ THEN 280
270 NEXT I:IF R=0 THEN 290
271 GOTO 300
280 D$(I)=G$:R=R+1:GOTO 270
290 M=M+1:GOTO 400
300 FOR I=1 TO L:IF D$(I)="-" THEN C1=I:I=26:NEXT I:I=C1:GOTO320
310 NEXT I:GOTO 390
320 PRINT:FOR I=1 TO L:PRINT D$(I);:NEXT I:PRINT
330 PRINT"WHAT IS YOUR GUESS FOR THE":INPUT"WORD";B$
340 IF B$=A$ THEN 360
350 PRINT"WRONG. TRY ANOTHER LETTER.":PRINT:GOTO 170
360 PRINT"RIGHT! IT TOOK YOU"T1-1"GUESSES!"
370 INPUT "WANT ANOTHER WORD";W$:IF LEFT$(W$,1)="Y" THEN CLS:GOTO 50
380 PRINT"IT'S BEEN FUN! BYE FOR NOW.":END
390 PRINT"YOU FOUND THE WORD!":GOTO 370
400 PRINT:PRINT"SORRY, THAT LETTER ISN'T IN":PRINT"THE WORD."
410 ON M GOTO 415,420,425,430,435,440,445,450,455,460
415 PRINT"FIRST, WE DRAW A HEAD":GOTO 470
420 PRINT"NOW WE DRAW A BODY":GOTO 470
425 PRINT"NEXT WE DRAW AN ARM":GOTO 470
430 PRINT"THIS TIME IT'S THE OTHER ARM":GOTO 470
435 PRINT"NOW, LET'S DRAW THE RIGHT LEG":GOTO 470
440 PRINT"THIS TIME WE DRAW THE LEFT LEG":GOTO 470
445 PRINT"NOW WE PUT UP A HAND":GOTO 470
450 PRINT"NEXT THE OTHER HAND":GOTO 470
455 PRINT"NOW WE DRAW ONE FOOT":GOTO 470
460 PRINT"HERE'S THE OTHER FOOT--":PRINT"YOU'RE HUNG!!"
470 INPUT"HIT enter TO CONTINUE";M$:PRINT BL$;
471 ON M GOTO 480,490,500,510,520,530,540,550,560,570
480 P$(3,6)="-":P$(3,7)="-":P$(3,8)="-":P$(4,5)="(":P$(4,6)="."
481 P$(4,8)=".":P$(4,9)=")":P$(5,6)="-":P$(5,7)="-":P$(5,8)="-":GOTO 580
490 FOR I=6 TO 9:P$(I,7)="X":NEXT:GOTO 580
500 FOR I=4 TO 7:P$(I,I-1)=CHR$(92):NEXT:GOTO 580
510 P$(4,11)="/":P$(5,10)="/":P$(6,9)="/":P$(7,8)="/":GOTO 580
520 P$(10,6)="/":P$(11,5)="/":GOTO 580
530 P$(10,8)=CHR$(92):P$(11,9)=CHR$(92):GOTO 580
540 P$(3,11)=CHR$(92):GOTO 580
550 P$(3,3)="/":GOTO 580
560 P$(12,10)=CHR$(92):P$(12,11)="-":GOTO 580
570 P$(12,3)="-":P$(12,4)="/"
580 FOR I=1 TO 12:FOR J=1 TO 12:PRINT P$(I,J);:NEXT J
590 PRINT:NEXT I:IF M=10 THEN 600
591 GOTO 170
600 PRINT"SORRY, YOU LOSE.":PRINT"THE WORD WAS "A$
610 GOTO 370
700 DATA "GUM","SIN","FOR","CRY","LUG","BYE","FLY"
710 DATA "UGLY","EACH","FROM","WORK","TALK","WITH","SELF"
720 DATA "PIZZA","THING","FEIGN","FIEND","ELBOW","FAULT","DIRTY"
730 DATA "BUDGET","SPIRIT","QUAINT","MAIDEN","ESCORT","PICKAX"
740 DATA "EXAMPLE","TENSION","QUININE","KIDNEY","REPLICA","SLEEPER"
750 DATA "TRIANGLE","KANGAROO","MAHOGANY","SERGEANT","SEQUENCE"
760 DATA "MOUSTACHE","DANGEROUS","SCIENTIST","DIFFERENT","QUIESCENT"
770 DATA "MAGISTRATE","ERRONEOUSLY","LOUDSPEAKER","PHYTOTOXIC"
780 DATA "MATRIMONIAL","PARASYMPATHOMIMETIC","THIGMOTROPISM"
900 REM ** GAME OF HANGMAN **
901 REM BY DAVE AHL, DIGITAL
905 REM BASED ON A PROGRAM
906 REM BY KEN AUPPERLE,
907 REM CLASS OF '75,
920 REM HALF HOLLOW HILLS H.S.
921 REM DIX HILLS, NY
930 REM MC-10 EDITS JIM GERRIE
999 END
