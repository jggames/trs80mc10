0 CLEAR1000:DIMG(20,30),K(255),A,B,C,D,E,F,G,H,X,Y,Z,G$(20,30),I$,N$,FN$
5 GOSUB1000:GOSUB2000:GOSUB10:GOTO20
6 G$(E+A,F+B)="":N$=N$+I$:G(E+A,F+B)=VAL(N$):PRINT@(32*F)+(E*9)+36,G(E+A,F+B);:RETURN
7 PRINT@480,"WAIT...";:GOSUB3000:GOSUB10:RETURN
8 H=LEN(N$):IFH>0THENN$=LEFT$(N$,H-1):G(E+A,F+B)=VAL(N$):PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
9 RETURN
10 CLS:C=3:X=A:PRINT@C,X;:PRINT@C+9,X+1;:PRINT@C+18,X+2:C=C+1
11 FORY=BTOB+13:C=C+32:PRINT@C-4,Y;
12 FORD=0TO2:IFG$(X+D,Y)<>""THENPRINT@C+(9*D),G$(X+D,Y);:GOTO14
13 PRINT@C+(9*D),G(X+D,Y);
14 NEXT:NEXT:RETURN
20 FORC=1TO65000:FORT=1TO25:PRINT@(32*F)+(E*9)+36,CHR$(128);
21 I$=INKEY$:IFI$<>""THENT=25:C=65000:GOTO27
22 NEXT
23 FORT=1TO15:IFG$(E+A,F+B)<>""THENPRINT@(32*F)+(E*9)+36,G$(E+A,F+B);:GOTO26
25 PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
26 I$=INKEY$:IFI$<>""THENT=15:C=65000
27 NEXT:NEXT:IFG$(E+A,F+B)<>""THENPRINT@(32*F)+(E*9)+36,G$(E+A,F+B);:GOTO29
28 PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
29 ONK(ASC(I$+" "))GOSUB31,34,37,40,6,6,6,6,6,6,6,6,6,6,6,6,7,8,600,700,800,900
30 GOTO20
31 IFF-1=-1THENIFB>1THENB=B-1:GOSUB10
32 IFF>0THENF=F-1
33 N$="":RETURN
34 IFE-1=-1THENIFA>1THENA=A-1:GOSUB10
35 IFE>0THENE=E-1
36 N$="":RETURN
37 IFE+1=3THENIFA<18THENA=A+1:GOSUB10
38 IFE<2THENE=E+1
39 N$="":RETURN
40 IFF+1=14THENIFB<17THENB=B+1:GOSUB10
41 IFF<13THENF=F+1
42 N$="":RETURN
600 CLS:INPUT"FILENAME TO SAVE";FN$
610 IFFN$<>""THENCSAVE*G,FN$
620 GOSUB10:RETURN
700 CLS:INPUT"FILENAME TO LOAD";FN$
720 IFFN$<>"Q"THENCLOAD*G,FN$
730 GOSUB10:RETURN
800 CLS:INPUT"JUMP TO CELL X,Y";X,Y:IF(X>20ORX<0)OR(Y>30ORY<0)THEN830
805 IFX=0ORY=0THENX=1:Y=1
810 A=X:E=0:IFA>18THENE=A-18:A=18
820 B=Y:F=0:IFB>17THENF=B-17:B=17
830 GOSUB10:RETURN
900 FORY=1TO30:FORX=1TO10:LPRINTLEFT$(G$(X,Y)+STR$(G(X,Y))+"         ",9);:NEXT:LPRINT:NEXT:LPRINT
910 FORY=1TO30:FORX=11TO20:LPRINTLEFT$(G$(X,Y)+STR$(G(X,Y))+"         ",9);:NEXT:LPRINT:NEXT
920 RETURN
1000 K(87)=1:K(65)=2:K(83)=3:K(90)=4:K(79)=19:K(76)=20:K(74)=21:K(80)=22
1005 K(48)=5:K(49)=6:K(50)=7:K(51)=8:K(52)=9:K(53)=10:K(54)=11:K(55)=12:K(56)=13:K(57)=14:K(45)=15:K(46)=16:K(13)=17:K(8)=18
1010 X=1:A=1:B=1:C=0:E=0:F=0
1020 FORY=1TO30:FORX=1TO20:G$(X,Y)=" ":NEXT:NEXT
1090 RETURN:REM PLACE LABELS HERE
2000 G$(1,1)="MONTH":G$(1,2)="YEAR":G$(1,4)="BUDGET"
2010 G$(1,7)="RENT":G$(1,8)="UTILITIES":G$(1,9)="CAR/GAS":G$(1,10)="FOOD":G$(1,11)="MISC.":G$(1,13)="TOTAL"
2020 G$(2,4)="BUDGETED":G$(2,5)="AMOUNT":G$(3,4)="% OF":G$(3,5)="INCOME":G$(4,4)="ACTUAL":G$(4,5)="AMOUNT"
2030 G$(5,5)="VARIANCE":G$(3,1)="BUDGET":G$(3,2)="INCOME"
2999 RETURN:REM PLACE FUNCTIONS HERE
3000 G(2,13)=0:FORY=7TO11:G(2,13)=G(2,13)+G(2,Y):NEXT
3001 G(3,13)=0:FORY=7TO11:G(3,13)=G(3,13)+G(3,Y):NEXT
3010 FORY=7TO11:G(3,Y)=(G(2,Y)/G(3,3))*100:NEXT
3020 FORY=7TO11:G(5,Y)=G(2,Y)-G(4,Y):NEXT
3999 RETURN
4000 REM https://www.computerhistory.org/revolution/personal-computers/17/305
4010 REM RECODE OF DAN BRICKLIN'S
4020 REM VISICALC PROTOTYPE
