10 ::::::::::::::::::::::::::::::::::
20 REM       TOWERS OF HANOI
30 REM    BY TIMOTHY J. O'MALLY
40 REM COPYRIGHT 1983 TAB BOOKS INC.
50 REM CONVERTED FROM C64 BASIC TO 
60 REM TRS-80 MC-10 BY JIM GERRIE
65 ::::::::::::::::::::::::::::::::::
70 GOSUB100
80 GOSUB500
100 REM *** PRINT INSTRUCTIONS ***
105 NM=0
110 CLS
120 PRINT"     ARTIFICIAL INTELLIGENCE"
130 PRINT"            PROGRAM 1"
140 PRINT:PRINTTAB(7)"THE TOWERS OF HANOI":PRINT
150 PRINT"THE PROBLEM IS TO MOVE ALL OF"
160 PRINT"THE DISKS FROM THE FIRST POST"
170 PRINT"TO THE THIRD POST, ONE DISK AT"
180 PRINT"A TIME, WITHOUT PLACING A"
190 PRINT"LARGER ONE ON TOP OF A SMALLER"
195 PRINT"ONE.":PRINT
210 INPUT"ENTER NUMBER OF DISKS (1-8)";N
220 IFN<1ORN>8THENFORS=1TO32:PRINTCHR$(8);:NEXT:GOTO210
400 CLS1:
420 PRINT"     ARTIFICIAL INTELLIGENCE"
430 PRINT"            PROGRAM 1"
440 PRINT:PRINTTAB(7)"THE TOWERS OF HANOI":PRINT
450 PRINT@352,TAB(8)"1"TAB(17)"2"TAB(25)"3"
499 REM *** DETERMINE SOLUTION ***
500 TT=1:TF=2:FORS=1TON:D=N-(S-1)+.5:GOSUB700:NEXT
510 T(1)=INT(2^N+.5)-1
520 NM=NM+1:S=0
530 D=-((NM AND INT(2^S+.5))=INT(2^S+.5))*(S+1):S=S+1:IFD=0THEN530
540 TF=0
550 TF=TF+1:IF 0=((INT(2^(D-1)+.5)ANDT(TF))=(INT(2^(D-1)+.5)))THEN550
560 DT=(((1ANDN)=1)*2+1)*(((1ANDD)=1)*2+1)
570 TT=TF-DT-DT*3*((TF-DT>3)OR(TF-DT<1))
580 T(TF)=T(TF)-INT(2^(D-1)+.5)
590 T(TT)=T(TT)+INT(2^(D-1)+.5)
600 GOSUB700:REM DISPLAY DISK MOTION
610 IF NM<INT(2^N+.5)-1THEN520
620 PRINT@416,;:INPUT"PRINT SOLUTION (Y/N)";M$:IFLEFT$(M$,1)="Y"THEN800
630 IFLEFT$(M$,1)="L"THEN800
640 END
650 :::::::::::::::::::::::::::::::::::
699 REM *** DISPLAY DISK MOTION***
700 Y=20:X=20
711 IFPOINT(TF*17,X-1)=.THENX=X-1:GOTO711
715 IFPOINT(TT*17,Y)=.THENY=Y-1:GOTO715
720 FORC1=1TOD:SET(TF*17,X,1):SET(TF*17-C1,X,1):SET(TF*17+C1,X,1):NEXT
730 FORC1=1TOD:RESET(TT*17,Y):RESET(TT*17-C1,Y):RESET(TT*17+C1,Y):NEXT:RETURN
800 CLS:PRINT"     ARTIFICIAL INTELLIGENCE"
810 PRINT"            PROGRAM 2"
820 PRINT:PRINTTAB(7)"THE TOWERS OF HANOI":PRINT
860 M=(1=(1 AND N))*2+1
870 DIM K(N):FOR I=1 TO N:K(I)=1:NEXT
880 A$="ABC":FOR L=1 TO INT(2^N+.5)-1:I=0
890 D=-((L AND INT(2^I+.5))=INT(2^I+.5))*(I+1):I=I+1:IF D=0 THEN 890
8100 T=M*(((1 AND D)=1)*2+1)
8110 F=K(D)-T:J=F-T*3*(F<1 OR F>3)
8120 PRINT "MOVE"L": "TAB(15)"FROM "MID$(A$,K(D),1)" TO ";
8130 PRINT MID$(A$,J,1):IFM$<>"L"THEN8160
8140 LPRINT "MOVE"L": "TAB(15)"FROM "MID$(A$,K(D),1)" TO ";
8150 LPRINT MID$(A$,J,1)
8160 K(D)=J:NEXT
8170 END
