10 REM MASTER MIND "CODEBREAKER"
20 REM CODED IN RT-11 BASIC
30 CLS:J=RND(-(PEEK(9)*256+PEEK(10))):REM RANDOMIZE
40 DIM R$(9,3),S(9,1)
45 DIM A$(6),B$(3),C$(3),D$(3)
50 REM INITIALIZATION
60 FOR J=0 TO 6
70 READ A$(J)
80 NEXT J:J=J-1
90 DATA "RED","BLUE ","GREEN","YELLOW","ORANGE","BROWN","SPACE"
100 L0=0
110 L1=0
120 L2=0
130 L3=0
140 PRINT "MASTER MIND CODEBREAKER"
145 PRINT "PLEASE BE PATIENT, SOMETIMES I":PRINT"TAKE A FEW MINUTES ON MY MOVE"
150 PRINT "WHICH VERSION (1 OR 2)";
160 INPUT V
170 V=V+5
171 PRINT:PRINT"THINK OF A 4 ITEM SEQUENCE USING";
172 PRINT      "RED      BLUE     GREEN   YELLOW";
173 PRINT      "ORANGE   BROWN    SPACE(FOR V.2)";
180 REM ASSIGN COLORS AT RANDOM FOR ROW 1
190 FOR J=0 TO 3
200 R$(0,J)=A$(INT(V*RND(0)))
210 NEXT J:J=J-1
220 REM START MAIN PLAY OF GAME HERE
230 REM I IS THE ROW COUNTER
240 FOR I=0 TO 9
245 PRINT
250 PRINT "MY MOVE FOR ROW"I+1"IS"
260 PRINT R$(I,0)TAB(8)R$(I,1)TAB(16)R$(I,2)TAB(24)R$(I,3)
270 PRINT "HOW MANY BLACK PEGS";
280 INPUT S(I,0)
290 IF S(I,0)<>4 THEN 320
300 PRINT "THANKS FOR THE GAME"
305 PRINT
310 GOTO 870
320 IF S(I,0)<>3 THEN 360
330 S(I,1)=0:REM IF 3 BLACKS THEN 0 WHITES
340 GOTO 380
360 PRINT "HOW HANY WHITE PEGS";
370 INPUT S(I,1)
380 REM GENERATE HYPOTHESIS
390 FOR I0=L0 TO V-1:IFL0>V-1THEN730
400 FOR I1=L1 TO V-1:IFL1>V-1THEN720
410 FOR I2=L2 TO V-1:IFL2>V-1THEN710
420 FOR I3=L3 TO V-1:IFL3>V-1THEN700
430 D$(0)=A$(I0)
440 D$(1)=A$(I1)
450 D$(2)=A$(I2)
460 D$(3)=A$(I3)
470 REM CHECK ALL ROWS FROM FIRST TO CURRENT FOR CONSISTENCY
490 FOR R=0 TO I
500 FOR J=0 TO 3
510 C$(J)=R$(R,J)
520 B$(J)=D$(J)
530 NEXT J:J=J-1
540 REM USE ROW EVALUAT1ON SUBROUTINE TO CHECK CONSISTENCY OF
550 REM HYPOTHESIS AGAINST EACH ROW
555 N=0:M=0
560 GOSUB 930
570 REM CHECK FOR AGREEMENT OF BLACK & WHITE COUNT
580 IF N<>S(R,0) THEN 700
590 IF M<>S(R,1) THEN 700
600 NEXT R:R=R-1
610 REM MAKE SURE THAT HYPOTHESIS ROW DOESNT DUPLICATE ROW 1
620 Z=0
630 FOR J=0 TO 3
640 IF R$(0,J)<>D$(J) THEN 660
650 Z=Z+1
660 NEXT J:J=J-1
670 IF Z=4 THEN 700
690 GOTO 820
700 NEXT I3:I3=I3-1
710 NEXT I2:I2=I2-1
720 NEXT I1:I1=I1-1
730 NEXT I0:I0=I0-1
740 PRINT "I HAVE REACHED AN IMPASSE IN MY":PRINT"THINKING..."
750 PRINT "COULD YOU HAVE MADE AN ERROR?"
760 GOTO 870
770 L0=I0
780 L1=I1
790 L2=I2
800 L3=I3+1
810 REM DO NOT RECHECK ELIMINATED POSSIBILITIES
820 REM ASSIGN NEXT ROW
830 FOR J=0 TO 3
840 R$(I+1,J)=D$(J)
845 NEXT J:J=J-1
850 NEXT I:I=I-1
860 PRINT "I AM STUMPED-- YOU WIN"
870 PRINT "ANOTHER GAME";
880 INPUT R$
890 IF R$="Y" THEN 150
900 END
910 REM SUBROUTINE TO EVALUATE RESPONSE
920 REM COUNT BLACKS FIRST
930 FOR J1=0 TO 3
940 IF C$(J1)<>B$(J1) THEN 960
950 N=N+1
960 NEXT J1:J1=J1-1
970 REM NOW COUNT WHITES
980 FOR J1=0 TO 3
990 FOR J2=0 TO 3
1000 IF J1=J2 THEN 1080
1010 IF C$(J1)=B$(J1) THEN 1080
1020 IF C$(J2)=B$(J2) THEN 1080
1030 IF C$(J1)<>B$(J2) THEN 1080
1040 M=M+1
1050 B$(J2)="X":REM DUMMY WRONG VALUE
1070 GOTO 1090
1080 NEXT J2:J2=J2-1
1090 NEXT J1:J1=J1-1
1100 RETURN
1110 STOP
2000 END
