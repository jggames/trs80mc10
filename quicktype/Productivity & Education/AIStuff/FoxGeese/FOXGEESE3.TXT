5 CLS:PRINT@235,"PLEASE WAIT":GOSUB4000
10 GOTO2010
210 P=B(G(1))+B(G(2))+B(G(3))+B(G(4))
220 X=F:IFP<B(31)THENP=P-BX:X=31-F
230 P=P*B(X):RETURN
250 F=FNA(ABS(P))-31:B=P/B(F):IFB<0THENB=B+BX:F=31-F
260 FORA=1TO4:G(A)=FNA(B):B=B-B(G(A)):NEXT:RETURN
410 V=H:FORA=X(F)TO0STEP-1:X=M(A,F):IFFNX(X)<0THENNEXT:L=1:RETURN
420 B=F:F=X:GOSUB210:V=P:F=B:L=1:A=0:NEXT:RETURN
500 V=-1:FORC=1TO4:G=G(C):IFZ(G)<0THENNEXT:RETURN
502 FORA=0TOZ(G):X=M(A,G):V=V AND(FNX(X) ORX=F):NEXTA,C:RETURN
510 V=E:FORC=1TO4:G=G(C):IF-B(G)>V THEN530
520 FORA=0TOZ(G):X=M(A,G):IFFNX(X)<>0ORX=F THENNEXT:GOTO530
528 V=B(X)-B(G):D=C:B=X
530 NEXT:G=G(D):G(D)=B:GOSUB210:V=P:G(D)=G:IFSG=1THENG(D)=B:C=D
540 RETURN
1010 SCREEN1,0:GOSUB210:GOTO1030
1020 XX=FNW(G):YY=FNYY(G):PUT(XX,YY)-(XX+19,YY+19),SQ,PSET
1021 XX=FNW(G(C)):YY=FNYY(G(C))+5:PUT(XX,YY)-(XX+19,YY+9),GS,PSET
1030 CLS:IFF>27THENPLAYV$:PRINT@7,"THE FOX HAS WON":GOTO1410
1032 GOSUB410:IFV=H THENPLAYV$:PRINT@6,"THE GEESE HAVE WON":GOTO1410
1040 LINE(180,0)-(255,191),PRESET,BF:IFPF THENDRAW"BM180,50C4"+TH$:GOTO1110
1050 DRAW"BM180,80C4"+MW$:XX=FNW(F):YY=FNYY(F):GOSUB1810:B=4*INT(YY/20):B=FNCN(B)
1055 IFB=-1THENPLAYV$:PRINT@6,"THE GEESE HAVE WON":GOTO1410
1060 FORA=0TOX(F):X=M(A,F)
1061 IFX=B AND NOT FNX(X)THENA=X(F):XX=FNW(F):YY=FNYY(F):PUT(XX,YY)-(XX+19,YY+19),SQ,PSET:F=B:GOSUB210:GOTO1200
1070 NEXT:GOSUB5000:GOTO1040
1110 L=SF:M=SF:V(M)=E*M:IFM>4THENFORA=0TOHF:R(A)=0:NEXT
1112 LF=F:GOSUB1120:XX=FNW(LF):YY=FNYY(LF):PUT(XX,YY)-(XX+19,YY+19),SQ,PSET:GOTO1200
1120 IFL=1THEN410
1122 IFL<M-2THENGOSUB1610:IFV<>0THENRETURN
1130 L=L-1:V(L)=H*L:A(L)=X(F):F(L)=F
1140 F=M(A(L),F(L))
1150 IFFNX(F)=0THENGOSUB1320:IFV>V(L)THENV(L)=V:P(L)=F:IFV>V(L+1)THENF=F(L):L=L+1:RETURN
1160 A(L)=A(L)-1:IFA(L)>=0THEN1140
1170 V=V(L):F=F(L):L=L+1:IFL=M THENF=P(M-1):GOSUB210:RETURN
1172 IFL<M-2THENGOSUB1510
1180 RETURN
1200 GOSUB250:XX=FNW(F):YY=FNYY(F)+5:PUT(XX,YY)-(XX+19,YY+8),FX,PSET
1202 IFF>27THENPLAYV$:PRINT@6,"THE FOX HAS WON":GOTO1410
1204 GOSUB500:IFV THENPLAYV$:PRINT"THE FOX HAS WON":GOTO1410
1210 LINE(180,0)-(255,191),PRESET,BF:IFPG THENDRAW"BM180,50C4"+TH$:GOTO1310
1220 XX=FNW(G(1)):YY=FNYY(G(1)):DRAW"BM180,80C2"+WG$:GOSUB1810
1225 G=4*INT(YY/20):G=FNCN(G)
1230 C=FNZ(G):IFC=0THENGOSUB5000:GOTO1210
1240 DRAW"BM180,110C3"+MW$:GOSUB1810:I=4*INT(YY/20):I=FNCN(I)
1245 IFI=1THENPLAYV$:PRINT@7,"THE FOX HAS WON":GOTO1410
1250 IFFNX(I)THENGOSUB5000:GOTO1210
1260 IFI=F THENGOSUB5000:GOTO1210
1270 FORA=-1TOZ(G):IFA>=0THENIFM(A,G)=I THENXX=FNW(G(C)):YY=FNYY(G(C)):PUT(XX,YY)-(XX+19,YY+19),SQ,PSET:G(C)=I:XX=FNW(I):YY=FNYY(I):PUT(XX,YY+5)-(XX+19,YY+14),GS,PSET:A=Z(G):NEXT:GOTO1010
1280 NEXT:GOSUB5000:GOTO1210
1310 L=SG:M=SG:V(M)=H*M:IFM>4 THENFORA=0TOHF:R(A)=0:NEXT
1312 GOSUB1320:GOTO1020
1320 IFL=1THEN510
1322 IFL<M-2THENGOSUB1610:IFV<>0THENRETURN
1324 L=L-1:V(L)=E*L:C=1
1330 C(L)=C:F(L)=G(C):A(L)=0:IFA(L)>Z(G(C))THEN1362
1340 B=M(A(L),F(L)):X=FNX(B):G=G(C):G(C)=B:IFX<>0ORB=F THEN1360
1350 GOSUB1120:C=C(L):IFV<V(L)THENV(L)=V:P(L)=G(C)+C*32:IFV<V(L+1)THENG=G(C):G(C)=F(L):L=L+1:RETURN
1360 A(L)=A(L)+1:IFA(L)<=Z(F(L))THEN1340
1362 G=G(C):G(C)=F(L):C=C+1:IFC<5THEN1330
1370 V=V(L):L=L+1:IFL=M THENC=INT(P(L-1)/32):G=G(C):G(C)=P(L-1)AND31:GOSUB210:RETURN
1372 IFL<M-2 THENGOSUB1510
1380 RETURN
1410 PRINT@390,"ANOTHER GAME (Y/N)?"
1420 K$=INKEY$:IFK$="Y"THENGOSUB4040:CLS:GOTO2700
1430 IFK$<>"N"THEN1420
1440 END
1510 GOSUB210:C=P
1520 C=C-INT((C/HF+C)-C)*HF:IFC<0ORC>=HF THEN1520
1550 FORA=C TOC+C:IFR(A)<>0ANDR(A)<>P THENNEXT:RETURN
1560 R(A)=P:S(A)=V:A=C+C:NEXT:RETURN
1610 GOSUB210:C=P
1620 C=C-INT((C/HF+C)-C)*HF:IFC<0ORC>=HF THEN1620
1650 FORA=C TOC+C:IFR(A)<>0ANDR(A)<>P THENNEXT:V=0:RETURN
1660 V=-S(A)*(R(A)=P):A=C+C:NEXT:RETURN
1810 SCREEN1,0:PUT(XX,YY)-(XX+19,YY+19),SQ,NOT:FORZ=1TO100:NEXT
1820 PUT(XX,YY)-(XX+19,YY+19),SQ,NOT
1830 K$=INKEY$:IFK$="^"ANDYY>8ANDXX>8THENYY=YY-20:XX=XX-20:GOTO1810
1840 IFK$=CHR$(10)ANDYY<129 ANDXX<129 THENYY=YY+20:XX=XX+20:GOTO1810
1850 IFK$=CHR$(8)ANDXX>28THENXX=XX-40:GOTO1810
1860 IFK$=CHR$(9)ANDXX<128THENXX=XX+40:GOTO1810
1870 IFK$=CHR$(13)THENRETURN
1875 IFK$="Q"THENYY=0:XX=-12:RETURN
1880 GOTO1810
2010 DIMG(4),B(31),M(3,31),X(31),Z(31)
2020 B(0)=1:FORK=1TO31:B(K)=B(K-1)*2:NEXT
2026 BX=B(31)*2-B(24):E=1E30:H=1E30
2030 L2=LOG(2):DEFFNA(F)=INT(LOG(F)/L2+.001)
2110 DEFFNF(B)=((B>3)+(B<28))*(((3ANDB)<3)-1)-1
2120 DEFFNG(B)=(B>3)*(((3ANDB)<3)-1)-1
2140 DEFFNX(B)=(B=G(1)OR B=G(2)OR B=G(3)OR B=G(4))
2142 DEFFNZ(B)=-(B=G(1))-(B=G(2))*2-(B=G(3))*3-(B=G(4))*4
2150 DEFFNW(B)=-((7ANDB)<4)*(28+40*(3ANDB))-((7ANDB)>3)*(128-40*(3ANDB))
2155 DEFFNYY(B)=8+20*INT(B/4)
2156 DEFFNCN(B)=B-((7ANDB)<4)*(XX-28)/40-((7ANDB)>3)*(128-XX)/40
2160 FORB=0TO31:FORA=0TO3:M(A,B)=B-2*(3ANDB)-2-8*(B<4ORA>1)-(1+A*7)*((3ANDB)=3)+(1ANDA):NEXT:X(B)=FNF(B):Z(B)=FNG(B):NEXT
2500 DIMR(1500),S(1500)
2700 F=1:G(1)=28:G(2)=29:G(3)=30:G(4)=31:GOSUB2710:GOTO1010
2710 CLS:PRINT"DO YOU WANT HUMAN TO PLAY FOX   (Y/N)?";
2720 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN2720
2730 PRINTK$:PF=1:IFK$="Y"THENPF=0:GOTO2760
2740 PRINT:PRINT"LEVEL OF FOX SKILL (0-9)?";
2745 K$=INKEY$:IFK$<"0"ORK$>"9"THEN2745
2746 SF=VAL(K$)+1:PRINTK$
2750 HF=-131*(SF=5)-613*(SF=6)-1499*(SF>6)
2760 PRINT:PRINT"DO YOU WANT HUMAN TO PLAY GEESE (Y/N)?";
2770 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN2770
2780 PRINTK$:PG=1:IFK$="Y"THENPG=0:GOTO2860
2790 PRINT:PRINT"LEVEL OF GEESE SKILL (0-9)?";
2795 K$=INKEY$:IFK$<"0"ORK$>"9"THEN2795
2796 SG=VAL(K$)+1:PRINTK$
2800 HG=-131*(SG=5)-613*(SG=6)-1499*(SG>6):IFHF<HG THENHF=HG
2860 PRINT:PRINT"CHANGE STARTING POSITIONS":PRINT"(Y/N)?";
2870 K$=INKEY$:IFK$<>"Y"ANDK$<>"N"THEN2870
2880 IFK$="N"THEN3000
2890 GOSUB210
2920 DRAW"BM180,80"+MW$:XX=FNW(1):YY=FNYY(1):GOSUB1810:F=4*INT(YY/20):F=FNCN(F)
2925 PUT(68,8)-(87,27),SQ,PSET:PUT(XX,YY+5)-(XX+19,YY+13),FX,PSET
2930 FORG=1TO4:GOSUB210
2940 XX=FNW(G(G)):X1=XX:YY=FNYY(G(G)):Y1=YY:GOSUB1810:PUT(X1,Y1)-(X1+19,Y1+19),SQ,PSET
2950 I=4*INT(YY/20):I=FNCN(I)
2960 IF(FNX(I)ORI=F)ANDI<>G(G)THENGOSUB5000:GOTO2940
2970 PUT(XX,YY+5)-(XX+19,YY+14),GS,PSET:G(G)=I
2990 NEXT:IFFNX(F)THENGOSUB5000:GOTO2920
2995 C=1:G=G(1)
3000 RETURN
4000 PMODE3:PCLS:DIMGS(5),FX(4),SQ(10)
4010 DRAW"BM3,0C2FGR3FR5E3RED3FDGLGNFLNG2LH2LH":PAINT(12,5),2:PSET(0,1,4):PSET(14,5,4):PSET(16,4,4):GET(0,0)-(19,9),GS,G
4020 DRAW"BM18,20C4GL13HLG2R2F2ND4R10ND4U2":PAINT(10,22),4:PSET(2,21,1):GET(0,20)-(19,28),FX,G
4030 LINE(0,0)-(175,175),PSET,BF:COLOR3:LINE(8,8)-(167,167),PSET,BF
4040 FORK=8TO128STEP40:FORL=28TO148STEP40:PUT(L,K)-(L+19,K+19),SQ,PSET:PUT(L-20,K+20)-(L-1,K+39),SQ,PSET:NEXTL,K
4050 PUT(68,13)-(87,21),FX,PSET
4060 FORK=8TO128 STEP40:PUT(K,153)-(K+19,162),GS,PSET:NEXT
4070 TH$="R2ND6R2BR4D6BR4U3LBR6ND3BU2UBF3ND3R4D3BR5U6D3NR3F3BR4U3BU2UBF3BR2ND3R4D3BR7L3U3R3D6L3BE3BR4RBR5RBR5R"
4080 MW$="ND6F3RU3D6BR9L4U3R4D3BE3F3UE2BR3R3DL3D2R3BR6NU6E2F2NU6BR3U6D3R4D3BR7L3U3R3DL3BR7NUNR2D2BR8L3U3R3DL3BE4BR7R4D3L4D3"
4090 WG$="RD6E2F2U6BR4D6BR4U3NL2BF3U3BU2UBF3BRNR3D3R3BR4NU6BR4U3NL3BE3BR8L4D6R4U3BR4D3R4U3L4BR8D3R4U3L3BR9BUL2D2R2D2L2BR9L3U3R3"
4100 V$="T4O2DEFBGACDEGGDCDE"
4200 C=1:G=0:RETURN
5000 FORK=1TO14:PUT(200,5)-(210,15),SQ,PRESET:PLAY"T50AC":PUT(200,5)-(210,15),SQ,PSET:PLAY"DA":NEXT:RETURN
