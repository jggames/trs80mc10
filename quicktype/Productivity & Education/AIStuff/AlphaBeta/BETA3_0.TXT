0 CLS:CLEAR128:M=16384:C1=0:C2=0:GOTO110
100 REM ** BETA PSYCH MAINLINE, V.3-- TRS-80 **
110 CLS:PRINT
115 PRINT" FUNDAMENTAL BETA WITH SCORING"
120 PRINT"     AND LEARNING DETECTION":PRINT
125 PRINT"   STRIKE 'ENTER' TO START..."
130 S$=INKEY$:IFS$=""THENN=RND(0):GOTO130
135 CLS:DIMM(4,4,2)
140 CLS:PRINT"ENTER NUMBER OF CONSECUTIVE":PRINT"GOOD MOVES TO DEFINE FULL"
145 PRINT"LEARNING":PRINT
150 INPUTLC:IFLC<=0THEN140
155 FORMI=0TO4:FORMJ=0TO4:FORKL=1TO2
160 M(MI,MJ,KL)=2:NEXTKL,MJ,MI
165 CP=M+206+RND(5)+32*(RND(5)-3)
170 CI=RND(5)-3:CJ=RND(5)-3:IFCI=0ANDCJ=0THEN170
175 CS=0:GS=0:GM=0
180 CLS0:GOSUB1000:POKECP,42
185 GOSUB2000
190 IFNOT(CX=128ANDCY=128)THEN205
195 POKECP,128:CP=NP:POKECP,42
200 GOTO185
205 CS=CS+1
210 MI=CI+2:MJ=CJ+2
215 CI=M(MI,MJ,1):CJ=M(MI,MJ,2)
220 IFNOT(CI=2ANDCJ=2)THEN240
225 CS=CS+1:GM=0
230 CI=RND(5)-1:CJ=RND(5)-1:IFCI=2ANDCJ=2THEN230
235 GOTO220
240 CI=CI-2:CJ=CJ-2:GOSUB2000:CI=CI+2:CJ=CJ+2
245 IFNOT(CX=128ANDCY=128)THEN225
250 M(MI,MJ,1)=CI(MI,MJ,2)=CJ
255 CI=CI-2:CJ=CJ-2
260 GS=GS+1:GM=GM+1:GOSUB3000
265 IFGM<LCTHEN195
270 PRINT@448,""
275 PRINT@448,"LEARNING COMPLETE:"CS-GM"CONTACTS";
280 PRINT@480,"STRIKE 'ENTER' FOR NEW CREATURE";
281 S$=INKEY$:IFS$=CHR$(13)THEN155
282 IFS$="Q"THENEND
283 GOTO281
1000 REM ** BORDER, V.1-- TRS-80 **
1005 F0=M:F1=M+31:F2=M+384:F3=F2+31
1010 FORN=F0TOF1:POKEN,188:NEXT
1015 FORN=F2TOF3:POKEN,179:NEXT
1020 FORN=F0TOF2STEP32:POKEN,186:NEXT
1025 FORN=F1TOF3STEP32:POKEN,181:NEXT
1030 POKEF0,177:POKEF1,178:POKEF2,180:POKEF3,184
1035 RETURN
1040 REM
1045 REM
2000 REM ** SEARCH AHEAD, V.1-TRS-80 **
2005 NP=CP:CX=128:CY=128
2010 SI=SGN(CI):SJ=SGN(CJ):AI=ABS(CI):AJ=ABS(CJ)
2015 IFAI=0THEN2030
2016 AI=AI-1
2020 IFSI>0THENNP=NP+1:GOTO2025
2021 NP=NP-1
2025 CX=PEEK(NP)
2030 IFAJ=0THEN2045
2031 AJ=AJ-1
2035 IFSJ>0THENNP=NP+32:GOTO2040
2036 NP=NP-32
2040 CY=PEEK(NP)
2045 IFNOT(CX=128ANDCY=128)THENRETURN
2050 IFAI=0ANDAJ=0THENRETURN
2051 GOTO2015
2055 REM
2060 REM
3000 AS=GS/CS
3010 PRINT@448,"CONTACTS    GOOD MOVES  SCORE"
3015 PRINT@480,"                               ";:POKEM+511,96
3020 PRINT@480,CS;TAB(12)GS;TAB(24)LEFT$(STR$(AS),6);
3025 RETURN
4000 REM FUNDAMENTAL BETA WITH
4005 REM SCOREING AND LEARNING
4010 REM DETECTION    TRS-80
