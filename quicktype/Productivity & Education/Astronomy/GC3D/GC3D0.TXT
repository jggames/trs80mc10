1000 REM GALACTIC COLLISIONS

1010 REM BY M.C. Schroeder and N.F. Comins

1020 REM Originally published in ASTRONOMY, DEC 1988

1030 DIM X(100),Y(100),VX(100),VY(100),Z(100),VZ(100),ANS$(5)

1040 GRAPHICS 0

1050 POSITION 2,1:PRINT "# of rings of stars:_";

1060 INPUT NRR

1070 POSITION 2,3:PRINT "# of stars per ring_";

1080 INPUT NRS

1090 NS=NRR*NRS:DR=20/(NRR-1)

1100 POSITION 2,5:PRINT "Mass of INTRUDER galaxy in units of"

1110 PRINT "_TARGET galaxy mass:_";

1120 INPUT M2

1130 POSITION 2,8:PRINT "Initial X,Y,Z coordinates of the"

1140 PRINT "_INTRUDER galaxy:"

1150 PRINT "_TARGET galaxy is located at 0,0,0."

1160 POSITION 20,9:INPUT X2,Y2,Z2

1170 POSITION 2,13:PRINT "Initial X,Y,Z velocities of the"

1180 PRINT "_INTRUDER galaxy:"

1190 PRINT "_TARGET galaxy is initially at rest._";

1200 POSITION 20,14:INPUT VX2,VY2,VZ2

1210 REM

1220 POSITION 2,17:PRINT "# of time steps for this run:_";

1230 INPUT NTSPR

1240 POSITION 2,20:PRINT "Are these inputs correct (Y or N)";

1250 INPUT ANS$

1260 IF ANS$(1,1)="Y" THEN 1280

1270 GOTO 1040

1280 GRAPHICS 8:COLOR 1

1290 POSITION 0,1:PRINT "______________Thinking...."

1300 M1=5:X1=150:Y1=100:VX1=0:VY1=0:Z1=0:VZ1=0:SF2=2

1310 REM Initialize star positions

1320 TSTEP=0:M2=M2*M1:X2=X2+X1:Y2=Y2+Y1:Z2=Z2+Z1

1330 REM

1340 FOR IR=1 TO NRR

1350 R=10+(IR-1)*DR

1360 V=SQR(M1/R):TH=(0.5*V/R)*(180/3.14159)

1370 IF R=10 THEN V=0.9*V

1380 FOR IT=1 TO NRS

1390 T=(IT-1)*360/NRS

1400 T1=3.14159*(T-TH)/180

1410 I=I+1

1420 REM 

1430 X(I)=R*COS(T/57.2958)+150

1440 Y(I)=R*SIN(T/57.2958)+100

1450 VZ(I)=0:Z(I)=0

1460 REM Initialize star velocities

1470 VX(I)=-V*SIN(T1)

1480 VY(I)=V*COS(T1)

1490 NEXT IT

1500 NEXT IR

1510 GOSUB 1880

1520 REM Particle pusher routine

1530 FOR K=1 TO NTSPR

1540 FOR J=1 TO 1

1550 FOR I=1 TO NS

1560 REM

1570 R1=M1/((X(I)-X1)^2+(Y(I)-Y1)^2+(Z(I)-Z1)^2+SF2)^1.5

1580 R2=M2/((X(I)-X2)^2+(Y(I)-Y2)^2+(Z(I)-Z2)^2+SF2)^1.5

1590 REM

1600 AX=R1*(X1-X(I))+R2*(X2-X(I))

1610 AY=R1*(Y1-Y(I))+R2*(Y2-Y(I))

1620 AZ=R1*(Z1-Z(I))+R2*(Z2-Z(I))

1630 REM

1640 VX(I)=VX(I)+AX

1650 VY(I)=VY(I)+AY

1660 VZ(I)=VZ(I)+AZ

1670 X(I)=X(I)+VX(I)

1680 Y(I)=Y(I)+VY(I)

1690 Z(I)=Z(I)+VZ(I)

1700 NEXT I

1710 REM Update positions and velocities of galactic centers.

1720 RR=((X1-X2)^2+(Y1-Y2)^2+(Z1-Z2)^2+SF2)^1.5

1730 AX=(X2-X1)/RR:AY=(Y2-Y1)/RR:AZ=(Z2-Z1)/RR

1740 VX1=VX1+M2*AX:VY1=VY1+M2*AY:VZ1=VZ1+M2*AZ

1750 VX2=VX2-M1*AX:VY2=VY2-M1*AY:VZ2=VZ2-M1*AZ

1760 X1=X1+VX1

1770 Y1=Y1+VY1:Z1=Z1+VZ1

1780 X2=X2+VX2

1790 Y2=Y2+VY2:Z2=Z2+VZ2

1800 TSTEP=TSTEP+1

1810 NEXT J

1820 GOSUB 1880

1830 NEXT K

1840 PRINT "____Continue (Y OR N)";:INPUT ANS$

1850 IF ANS$="Y" THEN 1530

1860 GOTO 2290

1870 REM Update screen display

1880 GRAPHICS 8:SETCOLOR 2,0,0:SETCOLOR 1,0,14:POKE 77,0

1890 REM

1900 XC=(M1*X1+M2*X2)/(M1+M2)

1910 YC=(M1*Y1+M2*Y2)/(M1+M2)

1920 ZC=(M1*Z1+M2*Z2)/(M1+M2)

1930 REM

1940 PLOT 0,150:DRAWTO 319,150

1950 PLOT 160,0:DRAWTO 160,150

1960 REM

1970 XX1=(X1-XC):YY1=(Y1-YC):ZZ1=(Z1-ZC)

1980 XX2=(X2-XC):YY2=(Y2-YC):ZZ2=(Z2-ZC)

1990 REM CIRCLE (XX1+80),(YY1+75),2

2000 FOR I=-1 TO 1:PLOT XX1+80+I,YY1+75-2:NEXT I

2010 FOR I=-2 TO 2 STEP 4:PLOT XX1+80+I,YY1+75-1:NEXT I

2020 FOR I=-2 TO 2 STEP 4:PLOT XX1+80+I,YY1+75:NEXT I

2030 FOR I=-2 TO 2 STEP 4:PLOT XX1+80+I,YY1+75+1:NEXT I

2040 FOR I=-1 TO 1:PLOT XX1+80+I,YY1+75+2:NEXT I

2050 REM CIRCLE (XX1+240),(2*ZZ1+75),2

2060 FOR I=-1 TO 1:PLOT XX1+240+I,(2*ZZ1+75)-2:NEXT I

2070 FOR I=-2 TO 2 STEP 4:PLOT XX1+240+I,(2*ZZ1+75)-1:NEXT I

2080 FOR I=-2 TO 2 STEP 4:PLOT XX1+240+I,(2*ZZ1+75):NEXT I

2090 FOR I=-2 TO 2 STEP 4:PLOT XX1+240+I,(2*ZZ1+75)+1:NEXT I

2100 FOR I=-1 TO 1:PLOT XX1+240+I,(2*ZZ1+75)+2:NEXT I

2110 REM CIRCLE (XX2+80),(YY2+75),1

2120 FOR I=-1 TO 1:PLOT XX2+80+I,YY2+75-1:NEXT I

2130 FOR I=-1 TO 1 STEP 2:PLOT XX2+80+I,YY2+75:NEXT I

2140 FOR I=-1 TO 1:PLOT XX2+80+I,YY2+75+1:NEXT I

2150 REM CIRCLE (XX2+240),(2*ZZ2+75),1

2160 FOR I=-1 TO 1:PLOT XX2+240+I,(2*ZZ2+75-1):NEXT I

2170 FOR I=-1 TO 1 STEP 2:PLOT XX2+240+I,(2*ZZ2+75):NEXT I

2180 FOR I=-1 TO 1:PLOT XX2+240+I,(2*ZZ2+75)+1:NEXT I

2190 REM

2200 FOR I=1 TO NS

2210 XP=(X(I)-XC):YP=(Y(I)-YC):ZP=2*(Z(I)-ZC)

2220 PLOT (XP+80),(YP+75):PLOT (XP+240),(ZP+75)

2230 NEXT I

2240 REM

2250 POSITION 0,0

2260 PRINT "______X-Y","_______X-Z"

2270 POSITION 0,2:PRINT "time=";TSTEP;

2280 RETURN

2290 GRAPHICS 0:END