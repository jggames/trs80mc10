0 REM SOLAR SYSTEM SIMULATION
1 REM BY SCOTT TERESI, WWW.TERESI.US
2 REM JULY 5, 1996
3 REM AUGUST 5, 1996-GOT RANDOM ORBITS AND MAGNITUDES WORKING
4 CLS:DIML,RE,TP,BE,I$,R,SR,P,BL
5 L=1
6 RE=639
7 TP=1
8 BE=479:PRINT"HIT A KEY"
9 I$=INKEY$:R=RND(1000):IFI$=""THEN9
10 P=8
11 R=20:REM 200 WORKS BETTER FOR ASTEROID CLOUD
12 DIM MS(P),CL(P),PX(P),PY(P),SX(P),SY(P),AX(P),AY(P)
13 CLS0
14 REM SUN
15 MS(1)=100000
16 CL(1)=6
17 PX(1)=320
18 PY(1)=240
19 SX(1)=0
20 SY(1)=0
21 REM GENERIC PLANET
22 MS(2)=3
23 CL(2)=15
24 PX(2)=15
25 PY(2)=200
26 SX(2)=3
27 SY(2)=-3
28 REM BINARY P
29 MS(3)=300
30 CL(3)=15
31 PX(3)=440
32 PY(3)=240
33 SX(3)=0
34 SY(3)=5.8
35 MS(4)=300
36 CL(4)=3
36 PX(4)=430
38 PY(4)=240
39 SX(4)=0
40 SY(4)=7.5
41 REM ORBITING MOON AROUND EARTH
42 MS(5)=1000
43 CL(5)=15
44 PX(5)=200
45 PY(5)=240
46 SX(5)=0
47 SY(5)=-6.5
48 MS(6)=1
49 CL(6)=15
50 PX(6)=212
51 PY(6)=240
52 SX(6)=0
53 SY(6)=-8.5
54 REM TWO PLANETS STARTING AT SIMILAR POSITIONS
55 MS(7)=1
56 CL(7)=15
57 PX(7)=400
58 PY(7)=300
59 SX(7)=4
60 SY(7)=-4
61 MS(8)=1
62 CL(8)=15
63 PX(8)=400
64 PY(8)=300
65 SX(8)=4.1
66 SY(8)=-4
67 GOTO95:REM (TO BYPASS THE FOLLOWING ROUTINE)
68 REM TRY TO GET AN ORBITING CLOUD
69 REM ONLY SPECIFY MS AND MAGNITUDE OF SPEED; SEE BELOW
70 MS(1)=20000
71 FORI=2TOP:CL(I)=15:MS(I)=3000:MG(I)=2:NEXT
76 REM RANDOMLY CHOOSE PLACEMENT OF PLANET WITHIN CERTAIN RADIUS OF SUN
77 FORI=2TOP
80 PX(I)=RND*640:PY(I)=RND*480:D=SQR((PX(I)-PX(1))^2+(PY(I)-PY(1))^2):IFD<140ORD>150THEN80
84 NEXT
85 REM FIGURE OUT VECTOR FOR CIRCULAR ORBIT (CORRECT MAGNITUDE MUST BE SET ABOVE)
86 FORI=2TOP:D=SQR((PX(I)-PX(1))^2+(PY(I)-PY(1))^2):UX=(PX(I)-PX(1))/D:UY=(PY(I)-PY(1))/D:SY(I)=MG(I)*UX*-1:SX(I)=MG(I)*UY:NEXT
95 BL=0:GOSUB1000
99 REM *** MAIN PROGRAM ***
100 FORSR=2TOP:IFMS(SR)=0THEN300
103 AX(SR)=0:AY(SR)=0:FORTA=1TOP:IFTA=SRORMS(TA)=0THEN200
107 D=SQR((PX(SR)-PX(TA))^2+(PY(SR)-PY(TA))^2)
108 IFD<1THEND=1
109 REM THE LINE BELOW WILL COMBINE PLANETS' MASSES IF THEY COME TO CLOSE TOGETHER
110 REM IF D<1 THEN MS(SR)=MS(SR)+MS(TA):MS(TA)=0:GOTO200
111 UX=(PX(TA)-PX(SR))/D:UY=(PY(TA)-PY(SR))/D:AX(SR)=AX(SR)+1/(D^2)*MS(TA)*UX/R:AY(SR)=AY(SR)+1/(D^2)*MS(TA)*UY/R
200 NEXT
201 SX(SR)=SX(SR)+AX(SR):SY(SR)=SY(SR)+AY(SR)
300 NEXT
301 BL=1:GOSUB1000:FORI=1TOP:PX(I)=PX(I)+SX(I):PY(I)=PY(I)+SY(I)
305 IFPX(I)>RE THEN PX(I)=RE
306 IFPX(I)<L THEN PX(I)=L
307 IFPY(I)<TP THEN PY(I)=TP
308 IFPY(I)>BE THEN PY(I)=BE
309 NEXT
310 BL=0:GOSUB1000:GOTO100
999 REM DRAW OR ERASE A PLANET
1000 FORI=1TOP
1010 ONBLGOTO1030:SET(PX(I)/10,PY(I)/15,I):ONIGOSUB1040:NEXT:RETURN
1030 RESET(PX(I)/10,PY(I)/15):ONIGOSUB1050:NEXT:RETURN
1040 SET(PX(I)/10+1,PY(I)/15,2):RETURN
1050 RESET(PX(I)/10+1,PY(I)/15):RETURN
