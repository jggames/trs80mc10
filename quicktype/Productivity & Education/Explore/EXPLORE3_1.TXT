REM 1
10 CLS0:X=3:R$="":FORN=1TO32:R$=R$+"ê":NEXT
20 PRINT@160,"       DO YOU REMEMBER MY":PRINT"       PERIPHERALS?"
25 SOUND50,10:GOSUB340:CLS0
30 A$="œœœœœœœœœ"
35 FORN=108TO268STEP32
40 IFN>140THENA$=" ¡¡¡¡¡¡¡≈"
45 IFN=268THENA$="À√√√√√√√«"
50 PRINT@N,A$;:NEXT
55 GOSUB335
60 X=1:B=173
65 A=353:A$="WITH MY KEYBOARD YOU CAN ":GOSUB285
70 A=385:A$="GIVE ME INFORMATION      ":GOSUB285
75 PRINT@449,"THIS IS AN INPUT DEVICE  ";:GOSUB330
80 GOSUB280
85 A$="ŒÃÃÃÃÃÃÃÕ"
90 FORN=33TO193STEP32
95 IFN>33THENA$="        ≈"
100 IFN=193THENA$="À√√√√√√√«"
105 PRINT@N,A$;:NEXT
110 PRINT@321,"ON THE TV SCREEN,           ";
115 X=2:B=1
120 RESTORE
125 A=353:A$="I SHOW WHAT YOU HAVE        ":GOSUB285
130 A=385:A$="TYPED OR MY RESULTS.........":GOSUB285
135 PRINT@449,"THIS IS AN OUTPUT DEVICE    ";:GOSUB330
140 GOSUB280
145 PRINT@98,"      ";
150 A$="üüüü"
155 FORN=152TO312STEP32
160 IFN=280THENA$="öêêï"
165 IFN=312THENA$="úúúú"
170 PRINT@N,A$;:NEXT
175 X=3
180 RESTORE
185 PRINT@353,"IF YOU UNPLUG ME,           ";
190 A=385:A$="MY RAM WILL EMPTY AND I WILL":GOSUB285
195 A=417:A$="FORGET EVERYTHING THAT IS   ":GOSUB285
200 PRINT@449,"STORED THERE...             ";:GOSUB280:X=0:RESTORE
205 A=321:A$="THE TAPE CASSETTE RECORDER":GOSUB285
210 A=353:A$="SERVES AS AUXILIARY MEMORY":GOSUB285
215 PRINT@417,"THE TAPE RECORDER IS AN   ";
220 PRINT@449,"INPUT/OUTPUT DEVICE       ";:GOSUB330
225 GOSUB280
230 PRINT@24,"øøøøøø";:PRINT@55,"øÃÃÃÃÃÃø";:PRINT@87,"øøøøøøøø";
235 X=3:RESTORE
240 PRINT@321,"WITH A PRINTER, I CAN    ";
245 A=353:A$="PRINT ON YOUR PAPER      ":GOSUB285
250 A=385:A$="INFORMATION OR MY ANSWERS":GOSUB285
255 PRINT@449,"THIS IS AN OUTPUT DEVICE ";:GOSUB330
260 GOSUB280
265 GOSUB340
270 SOUND150,5:GOSUB340:GOTO1000
280 GOSUB340:PRINT@320,R$;:FORN=1TO4:PRINTR$;:NEXT:RETURN
285 FORN=1TO32
290 IFX=1THENZ=RND(6):PRINT@B+Z,"¿";
295 IFX=2ANDN/6=INT(N/6)THENPRINT@98,LEFT$("SCREEN",B);:B=B+1:IFB>6THENB=6
300 IFX=0THENPRINT@281,"œœ";
305 PRINT@A,LEFT$(A$,N);
310 IFN/2=INT(N/2)THENREADS:SOUNDS,1
315 IFX=1THENPRINT@B+Z,"¡";:B=B+32:IFB>237THENB=173
320 IFX=0THENPRINT@281,"¿¿";
325 NEXT:RETURN
330 FORM=100TO150STEP5:SOUNDM,1:NEXT:RETURN
335 FORM=1TO500:NEXT:RETURN
340 FORN=1TO3000:NEXT:RETURN
345 DATA50,1,50,72,93,50,93,119,93,72,50
350 DATA93,102,119,119,102,93,102,102
355 DATA72,93,102,102,93,72,93,93,50,93,72,1,50

REM 2
1000 CLS0:A$="œœœœœœœœœ"
1005 FORN=67TO227STEP32
1010 IFN>99THENA$=" ¡¡¡¡¡¡¡≈"
1015 IFN=227THENA$="À√√√√√√√«"
1020 PRINT@N,A$;:NEXT
1025 PRINT@320," DID YOU CONNECT ME TO A"
1030 PRINT" SERIAL PRINTER?"
1035 GOSUB1360:GOSUB1370
1040 PRINT@147,"øøøøøøøø";:PRINT@178,"øÃÃÃÃÃÃÃÃø";:PRINT@210,"øøøøøøøøøø";
1045 SOUND150,5
1050 PRINT@416," TYPE Y FOR YES OR N FOR NO"
1055 G$=INKEY$:FORN=1TO1000:G$=INKEY$
1060 IFG$="Y"THEN1130
1065 IFG$="N"THEN1085
1070 NEXT
1075 PRINT@416," IT'S TOO LATE! YOU DIDN'T":PRINT" ANSWER CORRECTLY        "
1080 SOUND1,10:GOSUB1370:GOTO1095
1085 PRINT@416,"        THAT'S TOO BAD:(":SOUND1,10
1090 SOUND1,10
1095 CLS0:PRINT
1100 PRINT" WITH A PRINTER, I CAN"
1105 PRINT" PRINT ON PAPER WHAT'S":PRINT" DISPLAYED ON THE SCREEN":GOSUB1360
1110 GOSUB1370:PRINT:PRINT:PRINT" IT IS VERY USEFUL FOR"
1115 PRINT" KEEPING A RECORD OF":PRINT" YOUR PROGRAMS...":PRINT:GOSUB1360
1120 GOSUB1370
1125 GOTO1340
1130 CLS0:FORN=1TO100:PRINT@198,"IMMEDIATELY STOP";:PRINT@230,"YOUR RECORDER   ";:SOUND1,1:NEXT:CLS0
1135 PRINT@128," CHECK THAT THERE IS PAPER":GOSUB1360
1140 PRINT" THEN SWITCH THE PRINTER TO":PRINT" START AT 600 BAUD"
1145 GOSUB1360:FORN=1TO5000:NEXT:PRINT@256," DID YOU DO IT?"
1150 PRINT " TYPE YES OR NO THEN enter"
1155 SOUND150,5
1160 INPUT" ";R$
1165 IFLEFT$(R$,1)<>"Y"THENPRINT" THAT'S TOO BAD!":SOUND1,15:GOSUB1355:GOTO1095
1170 IFPEEK(3)<>75THENGOSUB1345:GOTO1095
1175 CLS:PRINT@33,"WE'LL TRY IT:":GOSUB1360
1180 PRINT:PRINT" TYPE LPRINT THEN enter"
1185 INPUT" ";R$
1190 IFR$<>"LPRINT"ANDR$<>"L"+CHR$(134)THEN1185
1195 GOSUB1365
1200 LPRINT"THANK YOU SO MUCH FOR LINKING ME TO A":LPRINT"PRINTER, YOU WON'T REGRET IT!"
1205 LPRINT"THE LPRINT INSTRUCTION FOR THE PRINTER":LPRINT"IS THE EQUIVALENT OF PRINT FOR THE SCREEN"
1210 LPRINT"WITH LLIST, I CAN PRINT ALL":LPRINT"YOUR PROGRAMS"
1215 LPRINT"ON PAPER, THEY WILL BE EASIER TO":LPRINT"CORRECT AND YOU WILL NOT RISK ANY MORE"
1220 LPRINT"LOSING THEM DUE TO A CRASH":LPRINT"OR SAVE ERROR"
1225 GOSUB1370:CLS:PRINT@33,"I CAN ALSO WRITE IN":PRINT" LOWERCASE:":GOSUB1360
1230 PRINT:PRINT" PRESS SHIFT ZERO":PRINT" (ZERO IS TO RIGHT OF 9)
1235 PRINT:PRINT" THEN TYPE mc-10 AND PRESS":PRINT" enter":GOSUB1360
1240 INPUT" ";R$
1245 IFR$<>"mc-10"THEN1260
1250 PRINT:GOSUB1365:LPRINTR$
1255 GOTO1270
1260 PRINT:PRINT" YOU SHOULD HAVE GOT:"
1265 PRINT" MC-10":SOUND1,10:LPRINT"MC-10"
1270 GOSUB1370:CLS
1275 PRINT@33,"TO RETURN TO UPPERCASE MODE":PRINT" PRESS shift 0 AGAIN"
1280 GOSUB1360
1285 GOSUB1370:PRINT:PRINT" TYPE THE LETTER A THEN enter":GOSUB1360
1290 INPUT" ";R$
1295 IFR$<>"A"ANDR$<>"a"THEN1290
1300 IFR$="A"THENPRINT:PRINT" THE LETTER APPEARS IN BLACK ON":PRINT" GREEN BACKGROUND. SO I AM GOOD":PRINT" IN UPPERCASE MODE"
1305 IFR$="a"THENPRINT:PRINT" I'M IN REVERSE MODE":PRINT" RETURN TO UPPERCASE MODE":GOSUB1360:GOTO1270
1310 GOSUB1365:GOSUB1370
1315 CLS:G$=INKEY$:PRINT@193,"PLEASE TURN OFF THE PRINTER"
1320 PRINT" THEN PRESS enter"
1325 G$=INKEY$
1330 IFG$<>CHR$(13)THEN1325
1335 GOSUB1355:CLS0
1340 GOSUB1370:GOTO2000
1345 CLS0:PRINT@128," THE CONNECTION IS INCORRECT":PRINT" SORRY, THE TEST IS IMPOSSIBLE"
1350 SOUND1,10:GOSUB1370
1355 CLS0:FORN=1TO50:SOUND1,1:PRINT@224,"PRESS PLAY AGAIN ON YOUR TAPE";:NEXT:RETURN
1360 FORM=100TO150STEP5:SOUNDM,1:NEXT:RETURN
1365 SOUND154,3:SOUND175,3:SOUND165,5:SOUND119,5:SOUND154,3:RETURN
1370 FORM=1TO2000:NEXT:RETURN

REM 3
2000 CLEAR:RESTORE:FORS=1TO32:READD:NEXT:CLS0
2005 R$="":FORN=1TO32:R$=R$+"∞":NEXT:A$="œœœœœœœœœœœ":FORN=107TO299STEP32
2010 IFN>171THENA$=" ¡¡¡¡¡¡¡¡¡≈"
2015 IFN=299THENA$="À√√√√√√√√√«"
2020 PRINT@N,A$;:NEXT
2025 A=352:A$="DID YOU KNOW YOU CAN ALSO GIVE":GOSUB2135
2030 A=384:A$="ME A MEMORY EXTENSION?        ":GOSUB2135
2035 GOSUB1370:PRINT@78,"œœœœœ";:SOUND150,5:GOSUB1370
2040 PRINT@352,R$;R$;
2045 PRINT@354," IT ALLOWS YOU TO INCREASE THE";
2050 RESTORE:FORS=1TO32:READD:NEXT:A=386:A$=" CAPACITY OF MY RAM MEMORY    ":GOSUB2135
2055 A=418:A$=" BY 16K (OR 16384 BYTES)      ":GOSUB2135
2060 GOSUB1370
2065 FORN=139TO267STEP32:PRINT@N," ∞∞∞∞∞∞∞∞∞≈";:NEXT
2070 PRINT@78,"∞∞∞∞∞";
2075 GOSUB1370
2080 PRINT@176," ";:PRINT@207,"ram";:SOUND150,2
2085 GOSUB1370:PRINT@143,"   ";:PRINT@175,"   ";
2090 PRINT@78,"œœœœœ";:PRINT@45,"ext∞mem";
2095 SOUND150,10:GOSUB1370:CLS0
2100 RESTORE:FORS=1TO32:READD:NEXT
2105 A=32:A$=" THANKS TO EXPANSION MEMORY,   ":GOSUB2135
2110 GOSUB1370
2115 PRINT@128,"":PRINT" YOU CAN ENTER MUCH LONGER     ":PRINT" PROGRAMS":PRINT
2120 SOUND50,5:GOSUB1370
2125 PRINT" AND DISCOVER NEW":PRINT" POSSIBILITIES...":PRINT
2130 SOUND50,5:GOSUB1370:GOTO3000
2135 FORN=1TO33
2140 PRINT@A,LEFT$(A$,N);
2145 IFN/3=INT(N/3)THENREADS,D:SOUNDS,D
2150 NEXT:RETURN
2160 DATA50,2,1,2,50,3,72,3,93,2,50,2,93,2,119,2,93,3,72,3,50,3
2165 DATA93,2,102,2,119,3,119,2,102,1,93,1,102,2,102,2,72,2,93,2,102,3
2170 DATA102,93,72,93,93,50,93,72,1,50

REM 4
3000 CLEAR:RESTORE:FORS=1TO86:READD:NEXT
3001 CLS6:FORN=256TO287:FORM=NTON+192STEP32:PRINT@M,"∞";:NEXT:NEXT
3005 FORN=50TO200STEP10:SOUNDN,1:NEXT
3010 T$="∂æª":C$="˜   ¸¸":J$="óêó"
3015 A=1
3020 FORN=409TO385STEP-1
3025 PRINT@N+3,"ê";:PRINT@N+37,"ê";:PRINT@N+67,"ê";
3030 PRINT@N,T$;:PRINT@N+31,C$;:PRINT@N+64,J$;
3035 SOUND(N-340)*2,1
3040 PRINT@N+38,LEFT$("PROGRAM ME...",A);
3045 A=A+1:NEXT
3050 PRINT@385,"∑Ωπ";
3055 PRINT@449,"óêõ";:GOSUB3410
3060 A=420:B=455
3065 A$="HERE ARE INSTRUCTIONS":B$="IN BASIC...":GOSUB3370
3070 FORN=1TO23
3075 FORM=1TO100:NEXT
3080 READA$,P
3085 PRINT@P,A$;
3090 SOUNDRND(150)+50,1
3095 NEXT
3100 GOSUB3410
3105 A$="THESE WORDS ARE PART OF":B$="OF A LANGUAGE THAT...":GOSUB3370
3110 A$="I CAN UNDERSTAND--":B$="BASIC":GOSUB3370
3115 A$="THESE ARE INSTRUCTIONS":B$="THAT TELL...":GOSUB3370
3120 A$="ME TO EXECUTE A":B$="SPECIFIC TASK":GOSUB3370
3125 PRINT@424,"":PRINT@456,""
3130 S=100:PRINT@0," "
3135 FORN=1TO7
3140 PRINT:SOUNDS,2
3145 S=S-10:NEXT
3150 FORN=1TO5:SOUND1,2:NEXT
3155 A$=" BASIC,":B$=" IT'S EASY!":GOSUB3370
3160 PRINT@33,"EVEN IF YOU DON'T UNDERSTAND":PRINT" ENGLISH WELL YOU WILL LEARN"
3165 PRINT" VERY QUICKLY THE FIFTY OR SO":GOSUB3405
3170 PRINT" INSTRUCTIONS AND THE FEW":PRINT " SIMPLE RULES THAT WILL"
3175 PRINT" ALLOW YOU TO PROGRAM IN BASIC":PRINT:GOSUB3405
3180 FORN=1TO5000:NEXT
3185 R$="":FORN=1TO32:R$=R$+"∞":NEXT
3190 PRINT@480,R$;:FORN=1TO11:PRINTR$;:SOUNDN*10,1:NEXT
3195 A=36:B=71
3200 F=1:C$="MY MICROPROCESSOR":A$="ONLY RECOGNIZES":B$="MACHINE LANGUAGE":GOSUB3370
3205 PRINT@128," TO TRANSLATE BASIC CODE TO":PRINT" MACHINE CODE, IT CALLS ON"
3210 PRINT" A PROGRAM LOCATED IN THE":PRINT" ROM, THE INTERPRETER":GOSUB3405:GOSUB3410
3215 PRINT@393,"ˇˇˇˇˇˇˇˇˇˇˇ";
3220 PRINT@425,"˙";:PRINT@435,"ı";
3225 PRINT@457,"˙";:PRINT@467,"ı";
3230 PRINT@461,"rom";
3235 PRINT@333,"ˆˇ˘";:PRINT@429,"óêõ";
3240 PRINT@364,"óœœœõ";
3245 SOUND150,5
3250 GOSUB3410:PRINT@128,R$;R$;R$;R$;
3255 C$="THE INTERPRETER COMPARES":A$="THE MESSAGES THAT IT":B$="RECEIVES FROM BASIC.":GOSUB3370
3260 C$="IF IT CONFORMS TO":A$="BASIC, THE MESSAGE IS":B$="TRANSLATED INTO M/L":GOSUB3370
3265 PRINT@360,"CLSúú";:SOUND1,5:GOSUB3410
3270 PRINT@365,"OK!";:SOUND150,5:GOSUB3410
3275 PRINT@368,"úú";:PRINT@372,"translation";:SOUND50,5:GOSUB3410
3280 PRINT@360,"êêêêó";:PRINT@368,"õê";:PRINT@372,"êêêêêêêêêêê";
3285 PRINT@365,"œœœ";
3290 C$="IF THIS MESSAGE IS NOT":A$="COMPLIANT WITH THE RULES":B$="OF BASIC...":GOSUB3370
3295 C$="?SN ERROR IS DISPLAYED":A$="ON SCREEN TO INDICATE":B$="A SYNTAX ERROR":GOSUB3370
3300 PRINT@324,"CLEAR ";:PRINT@356,"SCREENúúú";:SOUND50,5
3305 GOSUB3410:PRINT@366,"?";:SOUND150,5:GOSUB3410
3310 PRINT@368,"úú";
3315 PRINT@373,"snêerror";:SOUND1,5:GOSUB3410
3320 CLS0:CLEAR
3325 S=150
3330 T$="ı∂øπ˙":C$="õ   ó"
3335 FORN=32TO448STEP32
3340 PRINT@N-32,"∞∞∞∞∞";
3345 PRINT@N,T$;:PRINT@N+32,C$;
3350 SOUNDS,1:S=S-10:NEXT:CLS0
3355 FORN=1TO5:SOUND50,1:NEXT
3365 GOTO4000
3370 PRINT@A,"˚":PRINT@B,""
3375 IFF=1THENPRINT@7," "
3380 SOUND50,3:FORN=1TO500:NEXT
3385 PRINT@A,"¸¸ "A$;:PRINT@B," "B$;
3390 IFF=1THENPRINT@7," "C$;
3395 SOUND100,3:GOSUB3410
3400 RETURN
3405 FORM=50TO100STEP10:SOUNDM,1:NEXT:RETURN
3410 FORM=1TO3000:NEXT:RETURN
3415 DATAGOSUB,33,RETURN,143,NEW,195,SOUND,102,CLEAR,83,INKEY$,184
3420 DATARUN,43,RESTORE,233,INPUT,57,LIST,244,RESET,168,CSAVE,121
3425 DATAPEEK,129,POKE,251,READ,50,PRINT,74,STOP,208
3430 DATACLOAD,225,CLS,67,FOR/NEXT,110,SET,156,GOTO,201,IF/THEN,216

REM 5
4000 CLS0:CLEAR:P=391
4005 FORN=1TO32:R$=R$+"ê":NEXT
4010 PRINT@385,"∑Ωπ";:PRINT@449,"óêõ";
4015 PRINT@416,"˜   ¸¸  HERE IS A PROGRAM":PRINT@455,"  IN BASIC..."
4020 SOUND150,5:FORN=1TO500:NEXT
4025 A=64:GOSUB4335
4030 GOSUB3410:A$="DON'T PANIC!":B$="IT'S LESS COMPLICATED":C$="THAN IT LOOKS":GOSUB4360
4035 GOSUB3410
4040 A$="THIS PROGRAM TELLS ME TO":B$="MAKE A SMALL AIRPLANE":C$="FLY ON SCREEN":GOSUB4360
4045 GOSUB3410
4050 A$="TO RUN IT,":B$="TYPE RUN OR control 1":C$="THEN enter":GOSUB4360
4055 G=257:D$="RUN":D=142:GOSUB4265
4060 CLS0:X=224
4065 PRINT@X,"õì";:SOUND150,1
4070 PRINT@X,"êê";:X=X+1:IFX=254THEN4080
4075 GOTO4065
4080 CLS0:PRINT@385,"∑Ωπ";:PRINT@416,"˜   ˚";:PRINT@449,"óêõ";
4085 A$="TO REVIEW THE PROGRAM":B$="GIVE ME THE ORDER TO":C$="LIST...":GOSUB4360
4090 G=65:D$="LIST":D=148:GOSUB4265
4095 A=128:GOSUB4335:GOSUB3410
4100 PRINT@480," ":FORN=1TO3:PRINT:SOUNDN*50,3:NEXT
4105 A$=" HERE ARE SOME":B$=" EXPLANATIONS...":C$="":P=263:GOSUB4360
4110 GOSUB3410
4115 PRINT@385,"DO NOT TRY TO REMEMBER":PRINT" EVERYTHING, JUST UNDERSTAND"
4120 PRINT" HOW THE PROGRAM RUNS"
4125 GOSUB4255
4130 A$="":B$="AFTER A RUN COMMAND":GOSUB4360
4135 PRINT@385,"I EXECUTE THE LINES":PRINT" OF INSTRUCTIONS IN ORDER"
4140 PRINT" OF THEIR NUMBERS":GOSUB4255
4145 A$="WHEN EXECUTING":B$="LINE 1":GOSUB4360
4150 PRINT@385,"I CLEAR THE SCREEN TO":PRINT" COLOUR 0 (BLACK)"
4155 PRINT" AND I REMEMBER THAT X=224":GOSUB4255
4160 B$="LINE 2":GOSUB4360
4165 PRINT@353,"I DISPLAY ON SCREEN AT THE":PRINT" POSITION X THE 2 GRAPHIC"
4170 PRINT" CHARACTERS õ AND ì THAT":PRINT" FORM THE LITTLE PLANE"
4175 GOSUB4255:SOUND100,3
4180 PRINT@385,"AND I MAKE A SOUND OF PITCH":PRINT" 150 AND DURATION 1":GOSUB4255
4185 B$="LINE 3":GOSUB4360
4190 PRINT@385,"I ERASE THE AIRPLANE BY":PRINT" DISPLAYING IN ITS PLACE"
4195 PRINT" 2 BLACK GRAPHIC CHARACTERS":GOSUB4255
4200 B$="LIGNE 4":GOSUB4360
4205 PRINT@385,"I REMEMBER THAT THE VALUE OF X":PRINT" (AIRPLANE POSITION) INCREASES"
4210 PRINT" BY 1":GOSUB4255:SOUND100,3
4215 PRINT@385,"WHEN X REACHES VALUE 254":PRINT" (RIGHT EDGE OF SCREEN),"
4220 PRINT" I END THE PROGRAM":GOSUB4255:B$="LINE 5":GOSUB4360
4225 PRINT@385,"I RETURN TO LINE 2 TO":PRINT" DISPLAY THE PLANE IN ITS"
4230 PRINT" NEW POSITION":GOSUB4255
4235 A$="FROM THIS":B$="EXAMPLE":GOSUB4360
4240 PRINT@385,"YOU CAN CREATE YOUR OWN":PRINT" ANIMATION PROGRAMS"
4250 GOSUB3410:GOTO5000
4255 FORN=1TO6000:NEXT
4260 PRINT@352,"":PRINT:PRINT:PRINT:RETURN
4265 I$="":PRINT@G-1,"?";
4270 G$=INKEY$:FORN=1TO200:G$=INKEY$
4275 IFG$>CHR$(90)ANDG$<>CHR$(D)THEN4325
4280 IFG$=CHR$(D)THENG$=D$
4285 IFG$=CHR$(8)THEN4325
4290 IFG$<>""THENI$=I$+G$:PRINT@G,I$;
4295 IFLEN(I$)>6THEN4325
4300 IFG$=CHR$(13)THEN4320
4305 NEXT
4310 PRINT@0,"TOO SLOW! I'LL TYPE "D$" FOR YOU";
4315 SOUND50,20:PRINT@G,D$:PRINT@0,R$;:RETURN
4320 IFI$=CHR$(D)+CHR$(13)ORI$=D$+CHR$(13)THENGOSUB4380:RETURN
4325 PRINT@0,"THAT'S NOT IT... TRY AGAIN";
4330 SOUND1,10:GOSUB3410:I$="":N=N+50:PRINT@G,R$;:PRINT@0,R$;:GOTO4305
4335 PRINT@A," 1 CLS0:X=224
4340 PRINT" 2 PRINT@X,"CHR$(34)"õì"CHR$(34)";:SOUND150,1"
4345 PRINT" 3 PRINT@X,"CHR$(34)"êê"CHR$(34)";"
4350 PRINT" 4 X=X+1:IFX=254 THEN END"
4355 PRINT" 5 GOTO 2":RETURN
4360 PRINT@P," ":PRINT@P+29,"˚":PRINT@P+64," "
4365 SOUND50,3:FORN=1TO500:NEXT
4370 PRINT@P+1,A$;:PRINT@P+29,"¸¸ ";B$;:PRINT@P+65,C$;
4375 SOUND100,3:RETURN
4380 SOUND154,3:SOUND175,3:SOUND165,5:SOUND119,5:SOUND154,3:RETURN

REM 6
5000 CLEAR:RESTORE:FORS=1TO86:READD:NEXT:FORS=1TO23:READR$,D:NEXT
5002 OD=PEEK(173)+256*PEEK(174)
5003 CLS0:PRINT@231,"letÄsÄplayÄaÄgame";:POKE16618,39:SOUND50,10:GOSUB3410
5005 CLS0:PRINT@96," YOU ARE DRIVING THE":PRINT" YELLOW CAR..."
5010 PRINT@224," BEWARE OF THE ROAD EDGES":PRINT" AND VEHICLES THAT"
5015 PRINT" ARE COMING YOUR WAY!"
5020 SOUND150,5:FORN=1TO5000:NEXT
5025 CLS0:PRINT@96," TO CHANGE YOUR DIRECTION,":PRINT" PRESS ON THE FOLLOWING KEYS:"
5030 PRINT@225," A ÄTO GO LEFT":PRINT@289," D ÄTO GO RIGHT"
5035 SOUND150,5:FORN=1TO7000:NEXT:CLS
5040 R$=" ¿¿¿¿¿¿¿¿¿¿≈"
5045 V$="ü"
5050 PR=491:PV=175:DR=0
5055 FORN=1TO16:PRINT@PR,R$;:PRINT@511," ";:NEXT
5060 FORN=1TO63
5065 IFN/3=INT(N/3)THENREADDR
5070 IFDR=99THENDR=1:POKE174,INT(OD/256):POKE173,OD-256*PEEK(174)
5075 IFPEEK(2)THENIF1ANDNOTPEEK(16946)THENPV=PV-2:GOTO5085
5080 IFPEEK(2)THENIF1ANDNOTPEEK(16949)THENPV=PV+2
5085 PRINT@PV,V$;
5090 PRINT@PR,R$;
5095 IFN/5=INT(N/5)THENPRINT@PR+2+RND(6),CHR$(159+16*RND(6));
5100 SOUND50,1
5105 PRINT@511," ";
5110 PRINT@PV-32,"ê";
5115 X=(PV-160)*2
5120 PR=PR+DR
5125 IFPOINT(X,12)ORPOINT(X+1,12)THEN5140
5130 NEXT:GOTO5150
5140 FORN=1TO10:CLS4:SOUND1,1:CLS8:NEXT:SOUND1,20
5145 PRINT@196,"ÄhmmmmÄÄiÄwouldn'tÄwantÄ";:POKE16596,39:PRINT@228,"ÄÄÄbeÄyourÄpassengerÄÄÄÄ";:SOUND1,20:GOTO5160
5150 CLS6:PRINT@229,"youÄhaveÄgoodÄreflexes";
5155 SOUND154,3:SOUND175,3:SOUND165,5:SOUND119,5:SOUND154,3
5160 GOSUB3410:CLS3
5170 PRINT@193,"ÄpleaseÄtakeÄaÄfewÄmomentsÄÄÄ";
5175 PRINT@225,"ÄnowÄtoÄbecomeÄfamiliarÄwithÄ";
5180 PRINT@257,"ÄtheÄprogramÄforÄthisÄgameÄÄÄ";
5185 SOUND150,10:GOSUB3410:GOTO6000
5195 DATA1,1,0,0,-1,-1,0,0,-1,0,1,-1,0,-1,-1,1,1,99

REM 7
6000 CLEAR:CLS0
6005 PRINT@1,"∑Ωπ";:PRINT@32,"˜   ˚";:PRINT@65,"óêõ";
6010 A$="YOU WILL DISCOVER":B$="HOW BASIC WORKS WITH":C$="THE HELP OF YOUR MC-10":GOSUB6255
6015 A$="BUT FOR NOW,":B$="JUST CONSIDER":C$="THIS LISTING":GOSUB6255
6020 A$="HERE ARE THE VARIABLES":B$="OF THE PROGRAM":C$="":GOSUB6255
6025 PRINT@160," R$ REPRESENTS THE ROAD":PRINT" V$ REPRESENTS THE YELLOW CAR"
6030 PRINT" PR = ROAD POSITION":PRINT" PV = CAR POSITION":PRINT" DR = ROAD DIRECTION"
6035 FORN=1TO32:R$=R$+"Ä":NEXT:GOSUB6230
6040 A$="PART 1 INITIALIZES THE":B$="VARIABLES AND DISPLAYS":C$="THE START SCREEN"
6045 GOSUB6255:PRINT@160," 10 CLS"
6050 PRINT" 20 R$="CHR$(34)" ¿¿¿¿¿¿¿¿¿¿≈"CHR$(34)":V$="CHR$(34)"ü"CHR$(34)
6055 PRINT" 30 PR=491:PV=175:DR=0"
6060 PRINT" 40 FOR N=1 TO16:PRINT@PR,R$;:":PRINT" PRINT@511,"CHR$(34)" "CHR$(34)";:NEXT"
6065 GOSUB6230
6070 A$="PART 2 IS THE MOST":B$="IMPORTANT. IT'S A":C$="LOOP OF 190 CYLES":GOSUB6255
6075 PRINT@128," 50 FOR N=1 TO 190":PRINT" 60 IF N/3=INT(N/3) THEN READ DR";
6080 PRINT" 70 IF DR=99 THEN DR=1:RESTORE":PRINT" 80 G$=INKEY$"
6085 PRINT" 90 IF G$="CHR$(34)"Q"CHR$(34)" THEN PV=PV-2"
6090 PRINT"100 IF G$="CHR$(34)"S"CHR$(34)" THEN PV=PV+2"
6095 PRINT"110 PRINT@PV,V$;:PRINT@PR,R$;"
6100 A$="LINES 60-70: READ INTO":B$="DR VALUES GIVEN AT":C$="THE END OF PROGRAM":GOSUB6255
6105 F=1:GOSUB6235:A$="LINES 80-100: CHANGE PV":B$="ACCORDING TO THE":C$="KEY WHICH IS PRESSED":GOSUB6255:GOSUB6235
6110 A$="LINE 110: SHOWS THE":B$="ROAD AND YELLOW":C$="CAR":GOSUB6255:F=0:GOSUB6235
6115 PRINT@128,"120 IF N/5=INT(N/5) THEN PRINT@":PRINT"PR+2+RND(6),CHR$(143+16*RND(7));";
6120 PRINT"130 SOUND 50,1"
6125 PRINT"140 PRINT@511,"CHR$(34)" "CHR$(34)";:PRINT@PV-32,"
6130 PRINTCHR$(34)"Ä"CHR$(34)";"
6135 PRINT"150 PR=PR+DR":PRINT"160 X=(PV-160)*2:IF POINT(X,12)"
6140 PRINT"<>0 OR POINT(X+1,12)<>0 THEN 190";
6145 PRINT"170 NEXT"
6150 A$="LINE 120: PLACES":B$="CARS ON THE ROAD":C$="OF RANDOM COLOURS":GOSUB6255
6155 F=1:GOSUB6235
6160 A$="LINES 130-150: ERASES":B$="THE YELLOW CAR AND":C$="MOVES THE ROAD":GOSUB6255
6165 GOSUB6235:A$="LINE 160: LEAVES THE":B$="LOOP IF THE CAR":C$="HITS AN OBSTACLE":GOSUB6255
6170 F=0:GOSUB6235:A$="PART 3 ENDS THE GAME":B$="AND RESETS THE VALUE":C$="OF DR":GOSUB6255
6175 PRINT@128,"180 CLS0:PRINT"CHR$(34)"YOU WON!"CHR$(34)":FOR N=1";
6180 PRINT"TO 2000:NEXT:GOTO200":PRINT"190 FOR N=1 TO 10:CLS4:SOUND1,1":PRINT":CLS8:NEXT"
6185 PRINT"200 CLS0:PRINT"CHR$(34)"ANOTHER RACE?"CHR$(34)
6190 PRINT"220 INPUT R$:IFR$="CHR$(34)"YES"CHR$(34)" THEN RUN";
6195 PRINT"230 END"
6200 PRINT"230 DATA 1,1,0,0,-1,-1,0,0,-1":PRINT"0,1,-1,0,-1,-1,1,1,99"
6205 GOSUB6230:CLS0
6210 PRINT@160," TURN ON YOUR TAPE PLAYER"
6215 PRINT@224," WHEN YOU SEE OK BELOW, TYPE":PRINT" CLOAD"CHR$(34)"RACEDEMO"CHR$(34)" AND HIT enter"
6220 PRINT" THEN TYPE run AND HIT enter. ":SOUND150,5
6225 END
6230 SOUND150,5
6235 PRINT@452,"pressÄENTERÄtoÄcontinue";
6240 G$=INKEY$:IFG$<>CHR$(13)THEN6240
6245 IFF=1THENPRINT@448,R$;:RETURN
6250 PRINT@96,R$;:FORN=1TO11:PRINTR$;:NEXT:RETURN
6255 PRINT@7," ":PRINT@36,"˚":PRINT@71," "
6260 SOUND50,3:FORN=1TO500:NEXT
6265 PRINT@8,A$;:PRINT@36,"¸¸ ";B$;:PRINT@72,C$;
6270 SOUND100,3:FORN=1TO3000:NEXT:RETURN
