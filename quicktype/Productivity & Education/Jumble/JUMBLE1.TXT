5 CLEAR300:MC=16384
10 DIMT1$(4),T2$(4),T3$(5),N1(10),Z(5),X$(0)
12 T1$(1)="SIMPLE":T1$(2)="MEDIUM":T1$(3)="DIFFICULT":T1$(4)="VERY DIFFICULT":T2$(1)="NORMAL":T2$(2)="FAST":T2$(3)="SUPER FAST"
16 T3$(1)="FIRST":T3$(2)="SECOND":T3$(3)="THIRD":T3$(4)="FOURTH":T3$(5)="FIFTH"
17 T2$(4)="DANGEROUS"
18 W=0:SC=-1:GOSUB9500
19 FORI=1TO3000:NEXTI
20 Y=7:SC=-1:W1=0:CLS0
25 A=128
30 FORC=32TO64STEP16
35 G$=CHR$(143+C):FORI=1TO5:G$=G$+G$:NEXTI
40 FORX=1TO3:PRINT@A+X*32,G$;:NEXTX
50 A=A+3*32:NEXTC
80 PRINT@32,"DO YOU WANT TO PLAY WITH:"
85 H=0
90 PRINT@192," -1- THREE LETTER WORDS"
100 PRINT@288," -2- FOUR LETTER WORDS"
110 PRINT@384," -3- FIVE LETTER WORDS"
120 A$=INKEY$:IFA$=""THEN120
130 L=VAL(A$):IFL<1ORL>3THEN120
135 SOUND200,2:SOUND190,1:SOUND205,1:SOUND210,2
137 A=64
140 CLS0:FORC=64TO112STEP16
145 G$=CHR$(143+C):FORI=1TO5:G$=G$+G$:NEXTI
150 FORX=1TO3:PRINT@A+X*32,G$;:NEXTX
155 A=A+96:NEXTC
165 PRINT@0,"   CHOOOSE DIFFICULTY LEVEL:"
170 A=128
175 FORI=1TO4:PRINT@A,"       -";I;"-  ";T1$(I):A=A+96:NEXTI
190 B$=INKEY$:IFB$=""THEN190
195 B=VAL(B$):IFB<1ORB>4THEN190
197 SOUND206,2:SOUND217,1:SOUND212,3:SOUND200,5
230 PRINT@0,"          SELECT SPEED:"
235 A=128
236 FORI=1TO4:PRINT@A,"         -";I;"-  ";T2$(I):A=A+96:NEXTI
250 C$=INKEY$:IFC$=""THEN250
255 C=VAL(C$):IFC<1ORC>4THEN250
256 SOUND206,2:SOUND217,1:SOUND212,3:SOUND219,2:SOUND217,4
257 IFC=4THENLETC=RND(3)
258 VI=1050-C*200
259 CLS0:GOSUB1050:GOTO265
260 IFW1>9GOTO600
261 FORI=1TO1500:NEXTI
262 PRINT@256:PRINT@224,"         YOUR SCORE:";SC
265 RESTORE:FORBH=1TO4:READHB,HB$:NEXTBH
266 S1=10
267 W1=W1+1
270 READM$:IFM$<>A$THEN270
275 READXM:MX=INT(XM/4)
280 N=RND(MX)+MX*(B-1)
281 FORI=1TOW1-1:IFN=N1(I)THEN280
282 NEXTI
283 N1(W1)=N
285 FORI=1TON:READM$:NEXTI
300 I=1
305 Y=RND(L+2)
310 FORJ=1TOI-1
315 IFY=Z(J)THENY=0
320 NEXTJ
321 IFY=0THEN305
322 Z(I)=Y
325 I=I+1:IFI<L+2THEN305
326 FORJ=1TOL+1:I=1
327 IFZ(I)=JTHENNEXTJ
328 I=I+1:IFI<L+2THEN327
329 Z(L+2)=J
330 FORI=1TOL+2:IFZ(I)<>ITHEN333
331 NEXTI
332 GOTO300
333 M1$=""
334 FORI=1TOL+2
335 M1$=M1$+MID$(M$,Z(I),1)
336 NEXTI
337 X=INT((43-10*L)/2):Y=9:X1=X:X=X+3:D=10:Y1=10
338 W=X
339 C=RND(8):C1=1
340 IFSC>=0THEN1000
341 CLS0:C=5:GOSUB9700
342 Y1=31:C=1:GOSUB9700
344 SC=0
345 GOSUB9750
350 X=W:N$=M1$:GOSUB9600
355 A=INT((53-10*L)/4)+320
360 IFL=2THENA=A-1
365 Q=1
367 C=RND(8):C1=0
369 FORO=1TOL+2:PRINT@A,O:A=A+5:NEXTO
370 PRINT@256,"":PRINT@224,"   SELECT THE LETTER TO PLACE"
371 L$=INKEY$:H=H+1:IFH>VITHEN500
372 IFL$=""THEN371
373 IFASC(L$)=13ORASC(L$)=145THEN405
374 IFASC(L$)<65ORASC(L$)>90THEN371
375 O=0:FORI=1TOL+2:IFL$=MID$(M$,I,1)THENO=1
376 NEXTI:IFO=0THEN371
377 SOUND220,1:PRINT@271,L$
378 PRINT@224,"       IN WHICH POSITION?"
380 T$=INKEY$:H=H+1
385 IFH>VITHEN500
387 IFT$=""THEN380
388 IFASC(T$)=13THEN405
389 IFASC(T$)=145GOTO600
390 IFASC(T$)<49ORASC(T$)>50+LTHEN380
391 T=VAL(T$):IFT<QTHEN378
392 SOUND200,1:Y=30:X=W+(T-1)*10
393 PRINT@224
394 PRINT@272,T
395 MN$(T)=L$:N$=L$:GOSUB9600
400 GOTO370
405 M2$="":FORI=1TOL+2:M2$=M2$+MN$(I):MN$(I)="":NEXTI
407 IFASC(L$)=145GOTO600
410 IFM2$<>M$THENPRINT@224," YOU HAVEN'T GUESSED THE":PRINT" RIGHT WORD":S1=S1-4
411 IFM2$<>M$THENSOUND50,1:SOUND30,2:SOUND20,3:SOUND10,1:SOUND1,1
412 FORI=1TO500:NEXTI
415 IFS1<=0GOTO9000
417 IFM2$=M$THENSOUND206,1:SOUND206,1:SOUND217,1:SOUND212,3
420 IFM2$=M$THENSC=SC+S1:PRINT@224," YOUR GUESS IS CORRECT!":PRINT" HERE IS THE NEXT WORD...":GOTO260
460 FORK=Q-1TOL+1:Y=30:X=W+10*K:GOSUB9800
465 NEXTK
480 PRINT@224," I'LL GIVE YOU A HINT.":PRINT" THE ";T3$(Q);" LETTER IS..."
482 Q=Q+1:IFQ>L+2THENQ=Q-1:GOTO9000
485 Y=30:X=W+10*(Q-2):N$=MID$(M$,Q-1,1):GOSUB9600
486 FORK=1TOQ-1:MN$(K)=MID$(M$,K,1):NEXTK
487 FORI=1TO750:NEXTI
488 H=0
490 GOTO370
500 S1=S1-2
502 M2$=""
503 SOUND100,3:SOUND90,4:SOUND80,5
505 FORI=1TOL+2:M2$=M2$+MN$(I):MN$(I)="":NEXTI
510 GOTO415
600 CLS0
607 PRINT@64
610 PRINT@0:PRINT@32,"  YOU FINAL SCORE:";SC
615 C=4:C1=0
620 X=23:Y=19
625 D=7:N$="END":GOSUB9600
630 PRINT@384:PRINT@416:PRINT@448
635 PRINT@416,"  WANT TO PLAY AGAIN? (Y/N)
640 L$=INKEY$:IFL$=""THEN640
642 W1=0:H=0
645 IFL$="Y"THEN20
647 PRINT@416
650 PRINT@420,"GOODBYE..."
660 FORI=1TO2000:NEXTI
670 CLS
680 END
1000 FORK=0TOL+1:X=W+10*K
1010 Y=30:GOSUB9800:Y=9:GOSUB9800
1020 NEXTK:GOTO350
1050 PRINT@32," THE GOAL OF THE GAME IS TO":PRINT" GUESS 10 WORDS"
1055 PRINT@160," YOU MUST CORRECTLY ORDER THE":PRINT" JUMBLED LETTERS OF A RANDOM":PRINT" WORD AND THEN HIT ENTER"
1060 PRINT@320," EACH WORD FOUND WITHIN THE":PRINT" ALLOTED TIME EARNS 10 POINTS"
1062 X$="PRESS ANY KEY TO BEGIN":X=5:Y=15:GOSUB9550
1063 I=RND(0):IFINKEY$=""THEN1063
1065 CLS0:RETURN
9000 PRINT@224,"THE WORD TO FIND WAS:"
9002 PRINT@256
9005 X=W+(Q-1)*D:N$=RIGHT$(M$,L+3-Q)
9010 Y=30:GOSUB9600
9020 GOTO260
9500 CLS0
9502 Y=7:X=43:FORW=1TO4:P=0:READC,X$
9504 FORI=1TO7:FORJ=1TO5
9506 P=P+1:V=VAL(MID$(X$,P,1)):IFV=0THENRESET(X+J,Y-I):GOTO9510
9508 SET(X+J,Y-I,C)
9510 NEXTJ:NEXTI:X=X+5:NEXTW
9512 DATA 5,00100001000101001010100011000110001
9514 DATA 4,01110001000010000100001000010001110
9516 DATA 1,10000100001000011110100001000011111
9518 DATA 3,01110001000010000100001000010001110
9520 X$="NATHAN":Y=4:X=26:GOSUB9550:X$="MC-10":X=0:Y=0:GOSUB9550:PRINT@15*32,"  (C) VIFI-INTERNATIONAL 1984  ";
9530 PRINT@8*32+8,"JUMBLED LETTERS";:X$="VIVIANE BARBIER":X=8:Y=12:GOSUB9550:X$="CLAUDE  BARBIER":X=8:Y=13:GOSUB9550
9540 RETURN
9550 X1=X+Y*32-1:FORI=1TOLEN(X$):POKEX1+I+MC,ASC(MID$(X$,I,1))AND63:NEXTI:RETURN
9600 FORU=1TOLEN(N$)
9605 C$=MID$(N$,U,1)
9607 RESTORE
9610 READCA$:IFCA$<>C$THEN9610
9615 C=C+C1:IFC>8THENC=C-8
9620 READX$(0):P=0:FORI=1TO7:FORJ=1TO5:P=P+1:V=VAL(MID$(X$(0),P,1)):IFV=.THENRESET(X+J,Y-I):GOTO9645
9640 SET(X+J,Y-I,C)
9645 NEXTJ:NEXTI
9650 X=X+D
9655 NEXTU
9657 SOUND180+RND(50),1
9658 H=0
9660 RETURN
9700 FORI=X1+1TOX1+10*L+21
9705 SET(I,Y1,C):SET(I,Y1-10,C)
9710 NEXTI
9715 FORI=X1TOX1+10*(L+2)STEP10
9720 FORJ=Y1-10TOY1
9725 SET(I+1,J,C)
9730 NEXTJ:NEXTI
9735 RETURN
9750 FORI=0TO63
9755 SET(I,11,5):SET(I,10,5):SET(I,20,1):SET(I,21,1)
9760 NEXTI
9765 RETURN
9800 FORI=1TO7:FORJ=1TO5
9810 RESET(X+J,Y-I)
9820 NEXTJ:NEXTI
9825 RETURN
10000 DATAA,10001100011111110001100010101000100,B,11110010010100101110010010100111110,C,01110100011000010000100001000101110
10005 DATAD,11110010010100101001010010100111110,E,11111100001000011110100001000011111,F,10000100001000011110100001000011111
10010 DATAG,01110100011000110011100001000001111,H,10001100011000111111100011000110001,I,01110001000010000100001000010001110
10015 DATAJ,01100100100001000010000100001000111,K,10001100101010011000101001001010001,L,11111100001000010000100001000010000
10020 DATAM,10001100011000110101101011101110001,N,10001100011001110101110011000110001,O,01110100011000110001100011000101110
10025 DATAP,10000100001000011110100011000111110,Q,01101100101010110001100011000101110,R,10001100101010011110100011000111110
10030 DATAS,01110100010000101110100001000101110,T,00100001000010000100001000010011111,U,01110100011000110001100011000110001
10035 DATAV,00100001000101001010100011000110001,W,10001110111010110101100011000110001,X,10001100010101000100010101000110001
10040 DATAY,00100001000010000100010101000110001,Z,11111100000100000100000100000111111

10500 DATA1,181
10501 DATA MAN,ARE,WAS,SHE,HER,HIS,CAN,HAS,HAD,ANY
10502 DATA ALL,OUT,FOR,THE,AND,YOU,FIX,ARE,FLY,ODD
10503 DATA APE,OUR,ACE,FOR,PET,ACT,GOT,PAT,ASK,GET
10504 DATA PEG,ARM,SEA,PAW,AGE,PUP,GAS,PIT,AIR,HAT
10505 DATA PUT,ATE,HIT,POT,HAS,POP,BUT,PIN,BYE,HOW
10506 DATA RAT,BAD,RAG,BIG,RUB,BED,HEN,ROW,BAT,INK
10507 DATA RUG,BOY,RUN,BUS,RAP,BAG,JAB,RAM,BOX,JUG
10508 DATA SOW,BIT,JET,SEE,BEE,JAM,SAW,BUY,JAR,SET
10509 DATA BUN,JOB,SIT,CUB,JOG,SIR,CAT,KIT,SAT,CAR
10510 DATA KEY,CUT,LOT,TAP,COW,LIT,CRY,LET,TOP,CAB
10511 DATA LAY,TUG,CAN,MAT,TOW,DAD,DAB,MAD,TAN,DAM
10512 DATA MUG,TEN,DID,MIX,TWO,DUG,MAP,USE,DEN,MUM
10513 DATA WIN,EYE,NET,WON,EAT,NEW,WIG,END,NAP,WAR
10514 DATA ELF,NOW,WHY,EGG,NOD,WHO,FAR,NET,WAY,FAT
10515 DATA NOT,WOW,FEW,NUT,FAN,OAR,YES,FUN,ONE,YAK
10516 DATA FIT,YET,DIE,OWL,ZIP,FOX,OLD,ZAP,ICE,ILL
10517 DATA VAN,DOT,MUD,VET,DIP,MOM,DAY,MAY,WET,MET
10518 DATA OWN,FRY,GEL,AGO,SOB,TOE,EAR,SIX,ROT,BOA

10600 DATA2,151
10601 DATA CUBE,CAGE,CODE,DUCK,KISS,LADY,DATE,CAPE,FADE,FIRE
10602 DATA COLD,ALSO,ABLE,ACID,AGED,AWAY,BABY,BACK,BANK,BEEN
10603 DATA BALL,BASE,BUSY,BEND,BELL,BIRD,HELP,PAGE,SITE,NEED
10604 DATA COME,CAME,CALM,CHIP,CARD,COAT,CITY,CHAT,CASH,CROW
10605 DATA COOK,COOL,DARK,EACH,EVIL,EVEN,NAIL,MENU,HOLD,BLUE
10606 DATA EVER,FACE,FACT,FOUR,FIVE,FAIR,FEEL,FELL,DUNE,FINE
10607 DATA FISH,GAME,GONE,GOLD,GIRL,HAVE,HAIR,HERE,HEAR,INTO
10608 DATA JUMP,KICK,KILL,LIFE,LIKE,LOVE,MAIN,MOVE,MEET,JUNE
10609 DATA MORE,NOSE,NEAR,OPEN,ONLY,PUSH,PULL,SELL,SALE,BAKE
10610 DATA WORD,LIST,IRON,IRIS,PAWN,LEAD,GOAT,YARD,PLAN,FOUR
10611 DATA FIVE,NINE,HAND,DOCK,BREW,GOOD,BEST,CUTE,ZERO,HUGE
10612 DATA COOL,TREE,RACE,RICE,KEEP,LACE,BEAM,BLUR,BATH,GAME
10613 DATA TYPE,FLAT,DEAR,WHAT,DUEL,THIS,PLUS,CARE,WIND,LATE
10614 DATA MARS,TIDE,RIDE,HIDE,EXIT,HOPE,GALA,FROM,UNIT,STAY
10615 DATA COME,UGLY,WORM,FAWN,WOOD,BOND,VOLT,VAIN,TACT,TUNA

10700 DATA3,501
10701 DATA ABOUT,ALERT,ARGUE,BEACH,ABOVE,ALIKE,ARISE,BEGAN,ABUSE,ALIVE
10702 DATA ARRAY,BEGIN,ACTOR,ALLOW,ASIDE,BEGUN,ACUTE,ALONE,ASSET,BEING
10703 DATA ADMIT,ALONG,AUDIO,BELOW,ADOPT,ALTER,AUDIT,BENCH,ADULT,AMONG
10704 DATA AVOID,BILLY,AFTER,ANGER,AWARD,BIRTH,AGAIN,ANGLE,AWARE,BLACK
10705 DATA AGENT,ANGRY,BADLY,BLAME,AGREE,APART,BAKER,BLIND,AHEAD,APPLE
10706 DATA BASES,BLOCK,ALARM,APPLY,BASIC,BLOOD,ALBUM,ARENA,BASIS,BOARD
10707 DATA BOOST,BUYER,CHINA,COVER,BOOTH,CABLE,CHOSE,CRAFT,BOUND,CALIF
10708 DATA CIVIL,CRASH,BRAIN,CARRY,CLAIM,CREAM,BRAND,CATCH,CLASS,CRIME
10709 DATA BREAD,CAUSE,CLEAN,CROSS,BREAK,CHAIN,CLEAR,CROWD,BREED,CHAIR
10710 DATA CLICK,CROWN,BRIEF,CHART,CLOCK,CURVE,BRING,CHASE,CLOSE,CYCLE
10711 DATA BROAD,CHEAP,COACH,DAILY,BROKE,CHECK,COAST,DANCE,BROWN,CHEST
10712 DATA COULD,DATED,BUILD,CHIEF,COUNT,DEALT,BUILT,CHILD,COURT,DEATH
10713 DATA DEBUT,ENTRY,FORTH,GROUP,DELAY,EQUAL,FORTY,GROWN,DEPTH,ERROR
10714 DATA FORUM,GUARD,DOING,EVENT,FOUND,GUESS,DOUBT,EVERY,FRAME,GUEST
10715 DATA DOZEN,EXACT,FRANK,GUIDE,DRAFT,EXIST,FRAUD,HAPPY,DRAMA,EXTRA
10716 DATA FRESH,HARRY,DRAWN,FAITH,FRONT,HEART,DREAM,FALSE,FRUIT,HEAVY
10717 DATA DRESS,FAULT,FULLY,HENCE,DRILL,FIBRE,FUNNY,NIGHT,DRINK,FIELD
10718 DATA GIANT,HORSE,DRIVE,FIFTH,GIVEN,HOTEL,DROVE,FIFTY,GLASS,HOUSE
10719 DATA DYING,FIGHT,GLOBE,HUMAN,EAGER,FINAL,GOING,IDEAL,EARLY,FIRST
10720 DATA GRACE,IMAGE,EARTH,FIXED,GRADE,INDEX,EIGHT,FLASH,GRAND,INNER
10721 DATA ELITE,FLEET,GRANT,INPUT,EMPTY,FLOOR,GRASS,ISSUE,ENEMY,FLUID
10722 DATA GREAT,IRONY,ENJOY,FOCUS,GREEN,JUICE,ENTER,FORCE,GROSS,JOINT
10723 DATA JUDGE,METAL,MEDIA,NEWLY,KNOWN,LOCAL,MIGHT,NOISE,LABEL,LOGIC
10724 DATA MINOR,NORTH,LARGE,LOOSE,MINUS,NOTED,LASER,LOWER,MIXED,NOVEL
10725 DATA LATER,LUCKY,MODEL,NURSE,LAUGH,LUNCH,MONEY,OCCUR,LAYER,LYING
10726 DATA MONTH,OCEAN,LEARN,MAGIC,MORAL,OFFER,LEASE,MAJOR,MOTOR,OFTEN
10727 DATA LEAST,MAKER,MOUNT,ORDER,LEAVE,MARCH,MOUSE,OTHER,LEGAL,MUSIC
10728 DATA MOUTH,OUGHT,LEVEL,MATCH,MOVIE,PAINT,LIGHT,MAYOR,NEEDS,PAPER
10729 DATA LIMIT,MEANT,NEVER,PARTY,PEACE,POWER,RADIO,ROUND,PANEL,PRESS
10730 DATA RAISE,ROUTE,PHASE,PRICE,RANGE,ROYAL,PHONE,PRIDE,RAPID,RURAL
10731 DATA PHOTO,PRIME,RATIO,SCALE,PIECE,PRINT,REACH,SCENE,PILOT,PRIOR
10732 DATA READY,SCOPE,PITCH,PRIZE,REFER,SCORE,PLACE,PROOF,RIGHT,SENSE
10733 DATA PLAIN,PROUD,RIVAL,SERVE,PLANE,PROVE,RIVER,SEVEN,PLANT,QUEEN
10734 DATA QUICK,SHALL,PLATE,SIXTH,STAND,SHAPE,POINT,QUIET,ROMAN,SHARE
10735 DATA POUND,QUITE,ROUGH,SHARP,SHEET,SPARE,STYLE,TIMES,SHELF,SPEAK
10736 DATA SUGAR,TIRED,SHELL,SPEED,SUITE,TITLE,SHIFT,SPEND,SUPER,TODAY
10737 DATA SHIRT,SPENT,SWEET,TOPIC,SHOCK,SPLIT,TABLE,TOTAL,SHOOT,SPOKE
10738 DATA TAKEN,TOUCH,SHORT,SPORT,TASTE,TOUGH,SHOWN,STAFF,TAXES,TOWER
10739 DATA SIGHT,STAGE,TEACH,TRACK,SINCE,STAKE,TEETH,TRADE,SIXTY,START
10740 DATA TEXAS,TREAT,SIZED,STATE,THANK,TREND,SKILL,STEAM,THEFT,TRIAL
10741 DATA SLEEP,STEEL,THEIR,TRIED,SLIDE,STICK,THEME,TRIES,SMALL,STILL
10742 DATA THERE,TRUCK,SMART,STOCK,THESE,TRULY,SMILE,STONE,THICK,TRUST
10743 DATA SMITH,STOOD,THING,TRUTH,SMOKE,STORE,THINK,TWICE,SOLID,STORM
10744 DATA THIRD,UNDER,SOLVE,STORY,THOSE,UNDUE,SORRY,STRIP,THREE,UNION
10745 DATA SOUND,STUCK,THREW,UNITY,SOUTH,STUDY,THROW,UNTIL,SPACE,STUFF
10746 DATA TIGHT,UPPER,UPSET,WHOLE,WASTE,WOUND,URBAN,WHOSE,WATCH,WRITE
10747 DATA USAGE,WOMAN,WATER,WRONG,USUAL,TRAIN,WHEEL,WROTE,VALID,WORLD
10748 DATA WHERE,YIELD,VALUE,WORRY,WHICH,YOUNG,VIDEO,WORSE,WHILE,YOUTH
10749 DATA VIRUS,WORST,WHITE,WORTH,VISIT,WOULD,VITAL,VOICE,THYME,GROIN
10750 DATA IGLOO,PAVER,FLINT,FLESH,OXIDE,GREED,TRUNK,PROBE,STEEP,BEARD
