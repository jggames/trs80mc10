20 GOSUB510:REM INITIALISE
30 REM MAKE A GUESS
40 IFGU=.THENFORZ=1TO4:B(Z)=Z:NEXTZ:GOTO70
50 IFGU=1THENFORZ=1TO4:B(Z)=Z+2:NEXTZ:GOTO70
60 GOSUB280
70 CLS
80 GU=GU+1
90 REM
100 PRINT"GUESS NUMBER"GU
110 PRINT:PRINT"YOUR CODE:  ";:GOSUB565:PRINT
120 PRINT"MY GUESS IS ";:GOSUB572
130 PRINT
140 INPUT"HOW MANY BLACKS (RIGHT POSITION)";B
150 IFB=4THEN580
160 IFB=3THENW=0:GOTO190
170 PRINT
180 INPUT"AND HOW MANY WHITES (RIGHT      COLOUR WRONG POSITION)";W:IFW<0ORW>4THEN140
190 IFW+B=4THENQ=4:C(1)=B(1):C(2)=B(2):C(3)=B(3):C(4)=B(4)
200 IFB+W=.THENC(B(1))=.:C(B(2))=.:C(B(3))=.:C(B(4))=.:GOTO30
205 IFB(2)=B(4)ORB(3)=B(4)THEN290
210 IFB>AITHENFORZ=1TO4:E(Z)=B(Z):NEXTZ:AI=B
220 FORZ=1TO6
230 FORD=1TO4
240 IFB(D)=C(Z)THENC(Z)=C(Z)+(B+W)*100+W*10
250 NEXTD
260 NEXTZ
270 GOTO30
280 REM PICK FOUR NUMBERS
290 FORZ=1TO4
300 D1=C(RND(Q))
310 IFD1=.THEN300
320 D2=C(RND(Q))
330 IFD2=.THEN320
340 IFINT(D1/10)>INT(D2/10)THENB(Z)=D1
350 IFINT(D1/10)<INT(D2/10)THENB(Z)=D2
360 IFINT(D1/10)=INT(D2/10)THENB(Z)=D1
370 IFB(Z)>100THENB(Z)=B(Z)-100*INT(B(Z)/100):GOTO370
380 IFB(Z)>10THENB(Z)=B(Z)-10*INT(B(Z)/10):GOTO380
390 NEXTZ
400 IFB(1)=B(2)ORB(1)=B(3)ORB(1)=B(4)ORB(2)=B(3)THEN290
405 IFB(2)=B(4)ORB(3)=B(4)THEN290
410 IFAI>.THENCO=.:FORZ=1TO4:IFB(Z)=E(Z)THENCO=CO+1
420 IFAI>.THENNEXTZ:IFCO<AI-1THEN290
430 M=1000*B(1)+100*B(2)+10*B(3)+B(4)
440 K(GU)=M
450 IFGU<3THEN500
460 CO=1
470 CO=CO+1
480 IFK(CO)=MTHEN290
490 IFCO<GU-1THEN470
500 RETURN
510 REM INITIALISE
520 GU=0:Q=6:AI=0
530 DIMB(4),C(6),E(4),K(100)
540 FORZ=1TO6
550 C(Z)=Z
560 NEXTZ
561 GOSUB630
562 FORC1=1TO4:PRINTC1;"ENTER A NUMBER FROM 1-6";:INPUTCD(C1):IFCD(C1)<1ORCD(C1)>6THENC1=C1-1:SOUND1,1:NEXT
563 FORZZ=0TOC1-1:IFCD(ZZ)=CD(C1)THENC1=C1-1:SOUND100,1:NEXT
564 NEXT:NEXT:RETURN
565 FORC1=1TO4:PRINTMID$("ßŸ¯¿ïÿ",CD(C1),1)" ";:NEXT:PRINT
570 RETURN
572 FORC1=1TO4:PRINTMID$("ßŸ¯¿ïÿ",B(C1),1)" ";:NEXT:PRINT:RETURN
580 PRINT
590 PRINT"I GUESSED YOUR CODE OF ";:GOSUB572
600 PRINTTAB(6);"IN JUST"GU"GUESSES."
610 PRINT:INPUT"TRY ANOTHER";M$:IFLEFT$(M$,1)="Y"THENRUN
620 END
630 CLS
640 INPUT"DO YOU WANT INSTRUCTIONS";M$:IFLEFT$(M$,1)<>"Y"THENRETURN
690 CLS
700 PRINT"electronic brain IS AN ATTEMPT"
710 PRINT"TO CRACK A 'MASTERMIND' TYPE"
720 PRINT"CODE USING AI. PICK A COLOUR"
730 PRINT"CODE (NO REPEATED DIGITS) USING"
740 PRINT"NUMBERS FROM 1-6 AND THE MC-10"
750 PRINT"TRIES TO GUESS IT. YOU TELL"
760 PRINT"THE MC-10 ANY COLOUR IN THE"
770 PRINT"RIGHT POSITION USING BLACK PEGS"
780 PRINT"AND A WHITE PEG FOR ANY COLOURS"
790 PRINT"IN YOUR CODE, BUT NOT IN THE"
800 PRINT"POSITION THE MC-10 INDICATED."
810 PRINT"THE PROBLEM IS TRICKY, AS THE"
820 PRINT"MC-10 DOES NOT KNOW FOR CERTAIN"
830 PRINT"WHICH COLOUR PRODUCES WHICH"
840 PRINT"RESULT. ";:INPUT"HIT enter";M$:CLS
850 PRINT"EVERY TIME A COLOUR APPEARS"
860 PRINT"IN A CODE AWARDED A BLACK PEG"
870 PRINT"EVERY COLOUR IN IT IS WEIGHTED"
871 PRINT"SO IT APPEARS MORE. THE MORE"
872 PRINT"BLACKS, THE HIGHER THE WEIGHT."
873 PRINT"A SMALLER WEIGHTING IS AWARDED"
874 PRINT"FOR WHITES. ANY CODE GETTING"
875 PRINT"NO BLACKS OR WHITES LEADS TO"
876 PRINT"ALL THE COLOURS BEING USED IN"
877 PRINT"THE CODE TO BE REMOVED FROM"
878 PRINT"FUTURE CONSIDERATION.":INPUT"HIT enter";M$:CLS:RETURN

900 REM ELECTRONIC BRAIN
905 REM - 4 DIGITS
910 REM FROM VZ200 
915 REM GIANT BOOK OF GAMES
920 REM TIM HARTNELL 1983
930 REM MC-10 EDITS JIM GERRIE
