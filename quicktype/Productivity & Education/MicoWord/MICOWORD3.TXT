5 CLEAR2000,30999:DIMT$(50),C1:FORI=1TO30:BK$=BK$+CHR$(13):NEXTI:GOSUB7090
30 CLS
40 PRINT"MICOWORD 1.0"
50 PRINT"(C) 1984 BY TINO DELBOURGO"
60 PRINT
70 PRINT"1. CLEAR MEMORY"
80 PRINT"2. CREATE TEXT"
90 PRINT"3. EDIT TEXT"
100 PRINT"4. SAVE TEXT" 
110 PRINT"5. LOAD TEXT"
120 PRINT"6. PRINT TEXT"
125 PRINT"7. SET PRINTER PARAMETERS"
130 PRINT 
140 INPUT"CHOICE NUMBER";C
150 ONCGOSUB1000,2000,3000,4000,5000,6000,7000:GOTO30
200 PRINTCU$;
210 A$=INKEY$:IFA$=""THEN210
220 PRINTCHR$(8);:RETURN
250 GOSUB200
260 IFA$<="z"ANDA$>="a"THENA$=CHR$(ASC(A$)-32)
265 RETURN
270 GOSUB200:IFASC(A$)<32THEN270
280 RETURN
300 IFC$="."ORC$="!"ORC$="?"ORC$=";"THENT=1:RETURN
310 T=0:RETURN
350 IFC$="."ORC$="!"ORC$="?"ORC$=";"ORC$=":"ORC$=","ORC$=" "THENT=1:RETURN
360 T=0:RETURN
400 B$=""
410 GOSUB200
420 IFA$=CHR$(8)THENIFB$<>""THENB$=LEFT$(B$,LEN(B$)-1):GOTO460
430 IFA$=CHR$(10)ORA$=CHR$(13)THENRETURN
440 IFASC(A$)<32THEN410
450 B$=B$+A$
460 PRINTA$;:GOTO410
500 IFD=.THENPRINTTAB(M)B$D$
510 IFDTHENLPRINTTAB(M)B$D$
515 V=V+1:GOSUB600
520 RETURN
600 IFD=.THENRETURN
610 VE=VE+DS
620 IFVE<VP-VBTHENRETURN
630 VE=VT
640 IFVT+VB<1THENRETURN
650 LPRINTLEFT$(BK$,VT+VB);:RETURN
1000 PRINT"ARE YOU SURE (Y/N)?";
1010 GOSUB250:IFA$="Y"THENRUN
1020 RETURN
2000 CLS:S=S+1:ST=S
2010 GOSUB200:IFA$=CHR$(21)THEN2300
2030 IFA$=CHR$(8)THEN2100
2035 IFA$=CHR$(13)THEN2200
2040 C$=A$:GOSUB300:T$(S)=T$(S)+A$:IFTTHENS=S+1
2065 PRINTA$;:GOTO2010
2100 IFT$(S)<>""THENT$(S)=LEFT$(T$(S),LEN(T$(S))-1):GOTO2065
2110 IFS=STTHEN2010
2120 IFT$(S-1)=CHR$(13)THEN2010
2130 S=S-1:GOTO2100
2200 IFT$(S)=""THENS=S+1
2205 IFT$(S)<>""THENS=S+2
2210 T$(S-1)=CHR$(13):GOTO2065
2300 IFT$(S)=""THENS=S-1
2310 RETURN
3000 X=0
3001 IFS=.THENRETURN
3003 X=X+1:IFX>STHENX=1:GOTO3020
3010 IFT$(X)=""THEN3003
3020 CLS:PRINT"SENTENCE"STR$(X)". LENGTH"STR$(LEN(T$(X)))".":PRINTT$(X);:GOSUB250:PRINT
3070 IFA$=CHR$(21)THENRETURN
3080 IFA$=CHR$(185)THEN3880
3100 IFA$=CHR$(130)THEN3750
3110 IFA$=CHR$(138)THEN3800
3120 IFA$=CHR$(9)THENIFX<STHEN3003
3130 IFA$=CHR$(8)THENIFX>1THEN3900
3140 IFA$=CHR$(132)THEN3950
3145 IFA$=CHR$(178)THEN3985
3147 IFA$=CHR$(156)THEN3770
3150 IFA$<>CHR$(155)THEN3020
3200 P=1:E$=T$(X)
3205 N=1:GOSUB250
3220 IFA$="D"THEN3480
3230 IFA$="C"THEN3510
3240 IFA$="I"THEN3600
3245 IFA$=CHR$(13)THENT$(X)=E$:GOTO3020
3250 IFA$="H"THEN3450
3255 IFA$="A"THEN3430
3260 IFA$="X"THEN3660
3265 IFA$="L"THEN3400
3270 IFA$=CHR$(8)THENIFP>1THEN3360
3290 IFA$<="9"THENIFA$>"0"THEN3700
3300 IFA$<>" "THENGOTO3205
3305 IFP>LEN(E$)THENSOUND1,1:GOTO3205
3310 IFP+N>LEN(E$)+1THENN=LEN(E$)+1-P
3320 FORI=P+1TOP+N:PRINTMID$(E$,I-1,1);:NEXTI:P=I-1:GOTO3205
3360 IFP-N<1THENN=P-1
3370 FORI=P-1TOP-NSTEP-1:PRINTCHR$(8);:NEXTI
3395 P=I+1:GOTO3205
3400 PRINT
3410 PRINTE$:P=1
3420 GOTO3205
3430 E$=T$(X)
3440 GOTO3400
3450 IFP=1THENE$="":GOTO3600 
3460 E$=LEFT$(E$,P-1)
3470 GOTO3600
3480 IFP=1THENE$=MID$(E$,P+N):GOTO3205
3490 E$=LEFT$(E$,P-1)+MID$(E$,P+N)
3500 GOTO3205
3510 IFP>LEN(E$)THEN3205
3520 IFP+N>=LEN(E$)THENN=LEN(E$)-P+1
3530 FORI=PTOP+N-1
3540 GOSUB270
3545 PRINTA$;
3550 IFI=1THENE$=A$+MID$(E$,2)
3560 IFI>1THENE$=LEFT$(E$,I-1)+A$+MID$(E$,I+1)
3570 NEXTI
3580 P=I
3590 GOTO3205
3600 GOSUB400
3610 IFP=1THENE$=B$+E$
3620 IFP>1THENE$=LEFT$(E$,P-1)+B$+MID$(E$,P)
3630 P=P+LEN(B$)
3640 IFA$=CHR$(13)THEN3245
3650 GOTO3205
3660 FORI=P+1TOLEN(E$)+1:PRINTMID$(E$,I-1,1);:NEXTI
3690 P=I-1:GOTO3600
3700 N$=A$
3710 N=VAL(N$)
3720 GOSUB200
3730 IFA$<="9"ANDA$>="0"THENN$=N$+A$:GOTO3710
3740 GOTO3220
3750 PRINT"ARE YOU SURE (Y/N)?";
3752 GOSUB250 
3754 IFA$<>"Y"THEN3020
3756 T$(X)=""
3760 GOTO3003
3770 J=0
3771 PRINT"ARE YOU SURE (Y/N)?";
3772 GOSUB250
3773 IFA$<>"Y"THEN3020
3775 FORI=1TOS
3780 IFT$(I)=""THENNEXTI:GOTO3795
3785 J=J+1:T$(J)=T$(I)
3790 NEXTI
3795 S=J:GOTO3000
3800 CLS
3810 GOSUB400
3815 IFB$=""THEN3020
3820 S=S+1
3830 FORI=S-1TOXSTEP-1:T$(I+1)=T$(I):NEXTI:T$(X)=B$:GOTO3020
3880 B$=CHR$(13):GOTO3020
3900 X=X-1
3910 IFT$(X)=""THENIFX>1THEN3900
3920 GOTO3020
3950 INPUT"SENTENCE TO GO TO";X
3970 IFX<1ORX>STHEN3950
3980 GOTO3020
3985 INPUT"SENTENCE TO COPY";I:IFI=.THEN3020
3986 IFI<1ORI>STHEN3985
3990 T$(X)=T$(I):GOTO3020
4000 CLS:M=31000
4010 PRINT"COMPILING TEXT"
4015 IFS=0THENRETURN
4020 FORX=1TOS
4030 IFT$(X)=""THEN4080
4040 FORI=1TOLEN(T$(X))
4050 POKEM,ASC(MID$(T$(X),I,1))
4060 M=M+1
4070 NEXTI
4080 POKEM,0:M=M+1
4085 PRINT@32,X
4090 NEXTX
4100 POKEM,1:M=M+1
4110 POKE17004,121:POKE17005,24
4120 POKE17007,121:POKE17008,24
4130 POKE17009,INT(M/256):POKE17010,M-INT(M/256)*256
4140 POKE17002,0:POKE17003,0:POKE16999,2
4150 INPUT"FILENAME";B$
4160 PRINT"PRESS <PLAY/RECORD>"
4170 INPUT"PRESS <ENTER> TO CONTINUE";A$
4180 EXEC64603 B$
4190 RETURN
5000 CLS:M=31000
5010 S=S+1:T$(S)=""
5020 INPUT"FILENAME";B$
5030 PRINT"PRESS <PLAY>"
5040 CLOADMB$
5045 PRINT"READING COMPILED TEXT"
5050 C1=PEEK(M):IFC1=1THEN5120
5060 IFC1=.THEN5095
5070 T$(S)=T$(S)+CHR$(C1)
5080 M=M+1:GOTO5050
5095 PRINT@96,S
5100 S=S+1:T$(S)=""
5110 GOTO5080
5120 S=S-1:RETURN
6000 IFS=.THENRETURN
6005 V=0
6010 CLS:INPUT"ENTER <0> FOR SCREEN, <1> FOR   PRINTER";D
6012 INPUT"ENTER TEXT WIDTH";W
6014 INPUT"ENTER MARGIN SIZE";M
6020 K=0:INPUT"ENTER <1> FOR JUSTIFICATION";K
6030 IFKTHENINPUT"ENTER <1> FOR LEFT AND RIGHT    JUSTIFICATION";L
6040 C=0:INPUT"ENTER <1> FOR DOUBLE SPACING";C
6050 IFC=.THEND$="":DS=1
6052 IFCTHEND$=CHR$(13):DS=2
6060 B$="":CLS:FORX=1TOS
6070 IFT$(X)=""THEN6140
6080 FORI=1TOLEN(T$(X))
6090 A$=MID$(T$(X),I,1)
6100 IFA$=CHR$(13)THEN6170
6110 B$=B$+A$
6120 IFLEN(B$)=WTHEN6200
6130 NEXTI
6140 NEXTX
6150 GOSUB500
6160 GOSUB200
6165 RETURN
6170 GOSUB500
6180 B$=""
6190 GOTO6130
6200 IFK=.THEN6170
6210 FORN=LEN(B$)TO1STEP-1
6220 C$=MID$(B$,N,1):GOSUB350
6230 IFTTHEN6260
6240 NEXTN
6250 GOTO6170
6260 L$=LEFT$(B$,N)
6263 B$=MID$(B$,N+1)
6266 N=0:NEXTN
6267 IFLEFT$(L$,2)="  "THEN6270
6268 IFLEFT$(L$,1)=" "THENL$=MID$(L$,2)
6270 IFRIGHT$(L$,1)=" "THENL$=LEFT$(L$,LEN(L$)-1):GOTO6270
6280 IFLEN(L$)=WORL=.THEN6420
6290 IFLEFT$(L$,2)="  "THEN6360
6295 IFINT(V/2)=V/2THEN6360
6300 U=2
6310 C$=MID$(L$,U,1):GOSUB350
6320 IFTTHENL$=LEFT$(L$,U-1)+" "+MID$(L$,U):U=U+1
6330 IFLEN(L$)=WTHEN6420
6340 U=U+1:IFU>LEN(L$)THEN6300
6350 GOTO6310
6360 U=LEN(L$)
6370 C$=MID$(L$,U,1):GOSUB350
6380 IFTTHENL$=LEFT$(L$,U-1)+" "+MID$(L$,U):U=U-1
6390 IFLEN(L$)=WTHEN6420
6400 U=U-1:IFU<2THEN6360
6410 GOTO6370
6420 IFD=.THENPRINTTAB(M)L$D$
6430 IFDTHENLPRINTTAB(M)L$D$
6435 V=V+1
6437 GOSUB600
6450 GOTO6130
7000 CLS:INPUT"NUMBER OF LINES PER PAGE";VP
7010 IFVP<10THEN7000
7020 INPUT"NO. OF BLANK LINES AT TOP";VT
7030 IFVT>30THEN7020
7040 INPUT"NO. OF BLANK LINES AT BOTTOM";VB
7050 IFVB>30THEN7040
7060 IFVT>0THENLPRINTLEFT$(BK$,VT);
7070 VE=VT
7080 RETURN
7090 CU$=CHR$(175):RETURN
