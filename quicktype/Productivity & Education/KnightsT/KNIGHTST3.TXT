10 :::::::::::::::::::::::::::::
20 REM     KNIGHT'S TOUR
30 REM BY TIMOTHY J. O'MALLEY
40 REM (C) 1983, TAB BOOKS INC
50 REM FOR THE COMMODORE 64)
55 REM MC-EDITS BY JIM GERRIE
60 :::::::::::::::::::::::::::::
70 REM *** MAIN PROGRAM ***
80 GOSUB 200:REM PRINT INSTRUCTIONS
90 GOSUB 400:REM DEFINE GRAPHICS
100 GOSUB 600:REM DISPLAY SOLUTION
110 SOUND1,5
120 M$=INKEY$:IFM$=""THEN120
130 IFM$="P"THEN150
131 PRINT@0,"HIT ENTER, OR P TO PRINT BOARD.";
135 IFM$<>CHR$(13)THEN120
140 CLS:PRINT"DONE.":END
150 FORY=0TO16:FORX=0TO31:ZZ=PEEK(16384+Y*32+X):IFZZ<112ORZZ>121THENLPRINT" ";:GOTO170
160 LPRINTCHR$(ZZ-64);
170 NEXT:LPRINT:NEXT:GOTO140
190 ::::::::::::::::::::::::::::
200 REM *** INSTRUCTIONS ***
210 CLS:REM CLEAR SCREEN
220 PRINT:PRINTTAB(10)"KNIGHT'S TOUR":PRINT
230 PRINT"THIS PROGRAM DISPLAYS A KNIGHT'S";
240 PRINT"TOUR-- A KNIGHT IN THE GAME"
250 PRINT"OF CHESS CAN JUMP ON ALL 64"
260 PRINT"SQUARES OF THE BOARD WITHOUT"
270 PRINT"LANDING ON ANY SQUARE MORE THAN":PRINT"ONCE."
280 PRINT
300 PRINT"ENTER KNIGHT'S START POSITION,":PRINT
310 PRINT"ENTER THE ROW";:INPUTR:PRINT
320 PRINT"ENTER THE COLUMN";:INPUTC
340 IFR<1ORR>8ORC<1ORC>8THENSOUND1,1:PRINT@384,;:GOTO310
350 RETURN
400 REM *** DEFINE GRAPHICS ***
410 B1$(2)="ÏßßÏ":B1$(3)="€ßß€"
415 B2$(2)="ÏÏßÏ":B2$(3)="ÀÀßÀ"
420 B1$(0)="ÏÏÏÏ":B1$(1)="€€€€"
430 B2$(0)="ÏÏÏÏ":B2$(1)="€€€€"

440 CLS
450 FORY=0TO7:FORX=0TO7
500 PRINT@32*(Y*2)+X*4,B1$(-CC);
510 ZZ=32*(Y*2)+X*4+32:IFZZ<508THENPRINT@ZZ,B2$(-CC);:GOTO530
520 PRINT@ZZ,LEFT$(B2$(-CC),3);
530 CC=NOT(CC):IFX=7THENCC=NOT(CC)
535 NEXT:NEXT:POKE16895,207:RETURN

540 IFPEEK(16384+32*(Y*2)+X*4)=207THENCC=2:GOTO550
545 CC=3
550 PRINT@32*(Y*2)+X*4,B1$(CC);
560 ZZ=32*(Y*2)+X*4+32:IFZZ<508THENPRINT@ZZ,B2$(CC);:RETURN
570 PRINT@ZZ,LEFT$(B2$(CC),3);
580 RETURN

600 REM *** DISPLAY SOLUTION ***
601 DATA3,42,5,20,37,40,15,18
602 DATA6,21,2,41,16,19,36,39
603 DATA43,4,57,54,59,38,17,14
604 DATA22,7,62,1,56,53,60,35
605 DATA49,44,55,58,61,64,13,28
606 DATA8,23,48,63,52,29,34,31
607 DATA45,50,25,10,47,32,27,12
608 DATA24,9,46,51,26,11,30,33
610 DIM BD(8,8),MD(8,8)
615 FORJ=1TO8:FORK=1TO8:READMD(J,K):NEXTK,J
620 N=0:X1=0:Y1=0
625 FC=MD(R,C)-1
626 FORJ=1TO8:FORK=1TO8:BD(J,K)=MD(J,K)-FC:IFBD(J,K)<1THENBD(J,K)=BD(J,K)+64
627 NEXTK,J
630 N=N+1:FORJ=1TO8:FORK=1TO8:IFBD(J,K)=NTHENR=J:C=K
640 NEXTK,J
660 X=C-1:Y=R-1:GOSUB540:FORC1=1TO2000:NEXT:CC=CC-2:GOSUB550
670 PRINT@ZZ,MID$(STR$(N),2,2);
710 IFN=64THENRETURN
750 GOTO630
