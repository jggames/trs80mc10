1 REM ORIGINAL PLOTTING PROGRAM BY MARK BRAMHALL
2 REM REWRITTEN FOR TRS-80 LEVEL II BY MILAN D. CHEPKO
3 REM THIEF RIVER FALLS, MN 10 DEC 78
10 CLS0:RESTORE
20 PRINT"THIS PROGRAM WILL GRAPHICALLY":PRINT"DISPLAY A 3-D PLOT OF"
30 PRINT"A MATHEMATICAL FUNCTION FROM":PRINT"THE FOLLOWING TABLE..."
40 FOR I=1 TO 2500:NEXTI:CLS:CC=0:X=0:Y=0:Z=0:Y1=0:S=0
50 CC=CC+1
51 READM$:IFM$="9999"THEN60
52 PRINTCC;LEFT$(M$,24):GOTO 50
60 PRINT"NEW ONES CAN BE ADDED. PICK #";:INPUTM
100 CLS0:S=1
110 FOR X=-30 TO 30 STEP 1.5
120 L=0
130 Y1=5*INT(SQR(900-X*X)/5)
140 FOR Y=Y1 TO -Y1 STEP-5:Z=SQR(X*X+Y*Y)
145 ONMGOSUB400,500,600,700,800,900,1000,1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300,2400
150 Z=INT(25+Z-.7*Y)
160 IFZ<=L THEN 190
170 L=Z
180 SET(Z*.76,S*.76,5)
190 NEXT Y
210 S=S+1:NEXT X
250 M$=INKEY$:IFM$=""THEN250
260 GOTO10
300 REM ** CURRENT FUNCTIONS **
400 Z=30*EXP(-Z*Z/100):RETURN:DATA"Z=30*EXP(-Z*Z/100)"
500 Z=30-30*SIN(Z/18):RETURN:DATA"Z=30-30*SIN(Z/18)"
600 Z=30*EXP(-COS(Z/16))-30:RETURN:DATA"Z=30*EXP(-COS(Z/16))-30"
700 Z=30*SIN(Z/10):RETURN:DATA"Z=30*SIN(Z/10)"
800 Z=45*EXP(-Z*Z/100):RETURN:DATA"Z=45*EXP(-Z*Z/100)"
900 Z=30*(COS(Z/16))^2:RETURN:DATA"Z=30*(COS(Z/16)^2"
1000 Z=SQR(900.01-Z*Z)*.9-2:RETURN:DATA"Z=SQR(900.01-Z*Z)*.9-2"
1100 Z=.001*(Z*Z*Z+Z-25):RETURN:DATA"Z=.001*(Z*Z*Z+Z-25)"
1200 Z=30*SIN(Z/5):RETURN:DATA"Z=30*SIN(Z/5)"

9999 DATA"9999"
