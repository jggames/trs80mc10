0 CLS0:CLEAR200:DIMZZ,CC,C1,X0,Y0,X1,Y1,A$(4):GOSUB30000:GOTO10
3 POKE49151,4:RETURN
4 POKE39151,16:RETURN
5 ZZ=USR((X0*256+Y0)*65537):RETURN:REM FOR SINGLE POINT
6 ZZ=USR((X0*256+Y0)*65536+X1*256+Y1):RETURN:REM TO DRAW LINE

10 CLS0:GOSUB4:RESTORE
20 PRINT"THIS PROGRAM WILL GRAPHICALLY":PRINT"DISPLAY A 3-D PLOT OF"
30 PRINT"A MATHEMATICAL FUNCTION FROM":PRINT"THE FOLLOWING TABLE..."
40 FOR ZZ=1 TO 2500:NEXT:CLS:CC=0:X=0:Y=0:Z=0:Y1=0:S=0
50 CC=CC+1
51 READM$:IFM$="9999"THEN60
52 PRINTCC;LEFT$(M$,24):GOTO 50
60 PRINT"NEW ONES CAN BE ADDED.":INPUT"PICK #";M
100 CLS0:GOSUB3:S=1
110 FOR X=-30 TO 30 STEP 1.5
120 L=0
130 Y1=5*INT(SQR(900-X*X)/5)
140 FOR Y=Y1 TO -Y1 STEP-5:Z=SQR(X*X+Y*Y)
145 ONMGOSUB400,500,600,700,800,900,1000,1100,1200
150 Z=INT(25+Z-.7*Y)
160 IFZ<=L THEN 190
170 L=Z
180 X0=INT(Z*.8):Y0=S:ZZ=USR((X0*256+Y0)*65537)
190 NEXT Y
210 S=S+1:NEXT X
250 M$=INKEY$:IFM$=""THEN250
260 GOTO10
300 REM ** CURRENT FUNCTIONS **
400 Z=30*EXP(-Z*Z/100):RETURN:DATA"Z=30*EXP(-Z*Z/100)"
500 Z=30-30*SIN(Z/18):RETURN:DATA"Z=30-30*SIN(Z/18)"
600 Z=30*EXP(-COS(Z/16))-30:RETURN:DATA"Z=30*EXP(-COS(Z/16))-30"
700 Z=30*SIN(Z/10):RETURN:DATA"Z=30*SIN(Z/10)"
800 Z=45*EXP(-Z*Z/100):RETURN:DATA"Z=45*EXP(-Z*Z/100)"
900 Z=30*(COS(Z/16))^2:RETURN:DATA"Z=30*(COS(Z/16)^2"
1000 Z=SQR(900.01-Z*Z)*.9-2:RETURN:DATA"Z=SQR(900.01-Z*Z)*.9-2"
1100 Z=.001*(Z*Z*Z+Z-25):RETURN:DATA"Z=.001*(Z*Z*Z+Z-25)"
1200 Z=30*SIN(Z/5):RETURN:DATA"Z=30*SIN(Z/5)"
9999 DATA"9999"

29999 REM LINE DRAW SUBROUTINE BY GREG DIONNE
30000 A$(0)="LNPDALMMABABNNLLNMMMJAMKCCAJCFADHPAALLHAAALLEANAMLCCAJCFADHPAALMHAAALMFANNNGBBCDHCEPJHMOJHNLJGNGJHNMINDCJG"
30010 A$(1)="MKJBMMCHCIJLLLJHMKJGMOJLNHJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNGNHNMNGMLNLLMNHMLCANAFPHOOMOCNMMKNNNI"
30020 A$(2)="JGNJMGKLDNIGIAMAFFEEEEMLFFMBFFCEPIHEAANICEABEEIKIAJHNKIGCADNNLNIIJAAIKEAIEEBNNNINONINGNKOKAAOHAADJEPJHMOJH"
30030 A$(3)="NLJGNHJHNMINMAJGMLJBMNCHLGJLLMJHMLJGMOJLNGJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNHNHNMNGMKNLLLNHMKCANA"
30040 ZZ=32768:FORCC=0TO3:GOSUB30050:NEXT:ZZ=16917:A$(4)="HOIAAA"
30050 FORC1=1TOLEN(A$(CC))/2:POKEZZ,(ASC(MID$(A$(CC),2*C1-1,1))-65)*16+ASC(MID$(A$(CC),2*C1,1))-65:ZZ=ZZ+1:NEXT:RETURN

40001 REM ORIGINAL PLOTTING
40002 REM PROG BY MARK BRAMHALL
40003 REM REWRITTEN FOR TRS-80
40004 REM LV2 BY MILAN D.CHEPKO
40005 REM 10 DEC 78
