The Amazing Dick Smith Electronics VZ300 Omnibus - by Tim Hartnell.

01.PATTERN-MASTER1
02.PATTERN-MASTER2
03.3D PRINTER PLOTTER
04.MOTOR BOAT
05.OBJECT FALLS DOWN STAIRS
06.ALIEN ATTACK
07.ALERT
08.VZ SYNTHESIZER
09.TIC TAC
10.SYLLOGY
11.SNICKERS
12.BLOCKWORLD
13.TRANSLATE
14.HANSHAN
15.SPURT
16.X-SPURT
17.SELFLEARN
18.MINICALC
19.HOUSING LOAN
20.THE FORTH TUTOR
21.MOSES/OAKFORD SHUFFLE
22.SEQUENTIAL SEARCH
23.SELF ORGANIZING SEARCH
24.BINARY SEARCH
25.VON NEUMANN NUMBERS
26.MODULAR SEEDS
27.DISTRIBUTION OF NUMBERS
28.MONTE CARLO PI
29.BUBBLE SORT - A
30.SWAP SORT - B
31.INSERTION SORT - C
32.SHELL SORT - D
33.SORT BY COUNT - E




10 REM PATTERN-MASTER 1
20 REM PRESS ANY KEY TO START
30 REM A NEW DESIGN
110 MODE(1)
120 COLOR (1+RND(3)),(RND(2)-1)
130 LOZ=RND(3)/.02
140 GF=0
150 SET((60+37*SIN(GF)),(30+27*COS(GF*LOZ)))
180 GF=GF+.01
185 IF INKEY$<>"" THEN RUN
190 GOTO 150
10 REM PATTERN MASTER 2
20 MODE(1)
30 Q=RND(2)-1
40 FOR X=1 TO 63 STEP RND(0)
45 COLOR (1+RND(3)),Q
50 Y=RND(31)
60 SET(X,Y):SET(128-X,Y)
70 SET(128-X,64-Y)
80 SET (X,64-Y)
90 IF INKEY$<>"" THEN RUN
100 NEXT X
110 FOR J=1 TO 30 STEP (2+RND(5))
120 SOUND J,RND(3)
130 SOUND J/2+1,RND(3)
140 NEXT J
150 RUN

10 REM SYLLOGY
15 REM FROM THE AMAZING VZ300 OMNIBUS
20 GOSUB 1050:REM INITIALISE
30 PRINT:INPUT A$
40 IF A$="?" THEN 910
50 IF A$="" THEN END
60 FLAG=0
70 REM NOTE SPACE BEFORE CLOSE QUOTE IN NEXT LINES
80 IF LEFT$(A$,3)="IS " THEN 480:REM CONCLUSIONS
90 IF LEFT$(A$,4)="THE " THEN A$=MID$(A$,5)
100 IF LEFT$(A$,3)="AN " THEN A$=MID$(A$,4)
110 IF LEFT$(A$,2)="A " THEN A$=MID$(A$,3)
120 X=LEN(A$)
130 N=0
140 N=N+1
150 IF MID$(A$,N,1)=" " THEN B$=LEFT$(A$,N-1):GOTO 180
160 IF N<X THEN 140
170 PRINT "I DON'T UNDERSTAND":GOTO 30
180 K=4
190 IF MID$(A$,N+1,1)="W" THEN K=5
200 C$=MID$(A$,N+K):REM QUALIFYING PHRASE
210 IF LEFT$(C$,2)="A " THEN C$=MID$(C$,3):REM REMOVES ARTICLE
220 IF LEFT$(C$,3)="AN " THEN C$=MID$(C$,4)
230 IF LEFT$(C$,4)="THE " THEN C$=MID$(C$,5)
240 REM ** STORE INFORMATION **
250 REM CHECK TO SEE IF CAN FIND SUBJECT BEFORE BLANK
260 N=0
270 N=N+1
280 IF Z$(1,N)=B$ THEN 320:REM SUBJECT HEADING EXISTS
290 IF Z$(1,N)="" THEN Z$(1,N)=B$:GOTO 320
300 IF N<25 THEN 270
310 PRINT "NO MORE SUBJECT ROOM"
320 REM REACHES HERE WITH SUBJECT STORED AS HEADING
330 REM ** NOW PUT OBJECT UNDER THIS **
340 K=0
350 K=K+1
360 IF Z$(K,N)=C$ THEN 400:REM ALREADY STORED
370 IF Z$(K,N)="" THEN Z$(K,N)=C$:GOTO 400
380 IF K<25 THEN 350
390 PRINT "NO MORE OBJECT SPACE"
400 IF FLAG=1 THEN PRINT TAB(6);"> OK":GOTO 30
410 REM ** NOW SWAP OBJECT AND SUBJECT AND SAVE AGAIN **
420 FLAG=1
430 M$=B$
440 B$=C$
450 C$=M$
460 GOTO 250
470 REM *****************
480 REM ** CONCLUSIONS **
490 REM ** FIRST SPLIT INPUT **
500 A$=MID$(A$,4):REM STRIP "IS"
510 IF LEFT$(A$,2)="A " THEN A$=MID$(A$,3):REM STRIP "A"
520 IF LEFT$(A$,3)="AN " THEN A$=MID$(A$,4):REM STRIP "AN"
530 REM ** GET FIRST WORD  F$ **
540 X=LEN(A$)
550 N=0
560 N=N+1
570 IF MID$(A$,N,1)=" " THEN F$=LEFT$(A$,N-1):GOTO 600
580 IF N<X THEN 560
590 PRINT "> DO NOT UNDERSTAND":GOTO 30
600 REM ** NOW GET SECOND WORD  S$ **
610 S$=MID$(A$,N+3)
620 IF LEFT$(S$,1)=" " THEN S$=MID$(S$,2)
630 PRINT "(LOOKING FOR ";F$;")"
640 X=0
650 X=X+1
655 IF Z$(1,X)<>F$ THEN 670
660 PRINT "( FOUND AT 1 " ;X;")":GOTO 700
670 IF X<25 THEN 650
680 PRINT "CANNOT FIND SUBJECT":PRINT TAB(2);F$
690 GOTO 30
700 Y=1
710 Y=Y+1
720 IF Z$(Y,X)=S$ THEN PRINT TAB(6);"> YES":GOTO 30
730 IF Y<25 THEN 710
740 Y=1
750 Y=Y+1
760 P$=Z$(Y,X)
770 M=0
780 M=M+1
790 IF Z$(1,M)=P$ THEN 830
800 IF M<25 THEN 780
810 IF Y<25 THEN 750
820 PRINT TAB(6);"> NO":GOTO 30
830 Q=1
840 Q=Q+1
850 IF Z$(Q,M)=S$ THEN PRINT TAB(6);"> YES":GOTO 30
860 IF Q<25 THEN 840
870 IF M<25 THEN 780
880 GOTO 820
900 REM *********************************
910 REM CHECK CONTENTS OF PARTICULAR FILE
920 INPUT "SUBJECT TO CHECK";H$
930 T=0
940 T=T+1
950 IF Z$(1,T)=H$ THEN 990
960 IF T<25 THEN 940
970 PRINT "I HAVE NO DATA STORED ON ";H$
980 GOTO 30
990 K=1
1000 K=K+1
1010 IF Z$(K,T)<>"" THEN PRINT K;T;Z$(K,T)
1020 IF K<25 THEN 1000
1030 GOTO 30
1040 REM **********
1050 REM INITIALISE
1060 CLS
1080 DIM Z$(25,25)
1090 RETURN

1 REM BLOCKWORLD - VZ300 VERSION
2 REM FROM AMAZING VZ300 OMNIBUS
3 REM ---------------
4 REM ENTER IN COMMANDS :
5 REM   "WHERE IS THE <COLOUR> BLOCK"
6 REM   "TELL ME WHAT YOU SEE"
7 REM   "SHUFFLE THE BLOCKS"
8 REM   "PUT THE <COLOUR> BLOCK ON THE <COLOUR> ONE"
9 REM ---------------

10 CLEAR1000
20 GOSUB 2470:REM INITIALISE
30 REM ** PRINT OUT VIEW **
40 CLS:PRINT:PRINT
50 FOR X=5 TO 1 STEP -1
60 PRINT TAB(3);
70 FOR Y=1 TO 6
80 PRINT CHR$(A(X,Y));
90 NEXT Y
100 PRINT
110 NEXT X
120 PRINT:PRINT
130 INPUT A$
140 PRINT
150 IF A$="" THEN END:REM END BY JUST PRESSING 'RETURN'
160 IF LEFT$(A$,8)="WHERE IS" THEN GOSUB 240
170   IF LEFT$(A$,12)="TELL ME WHAT" THEN GOSUB 1050
180   IF LEFT$(A$,7)="SHUFFLE" THEN GOSUB 1280
190   IF LEFT$(A$,7)="PUT THE" THEN GOSUB 1500
200   PRINT:PRINT ">>>> PRESS <RETURN>";:INPUT Z$
210   GOTO 40
220   END
230   REM **************
240   REM "WHERE IS THE"
250   P=0
260   B$=MID$(A$,14,1)
270   IF  B$="R" OR B$="Y" OR B$="B" OR B$="G" THEN 330
280   IF  RND(10)>7 THEN 300
290   PRINT "I DON'T KNOW":GOTO 310
300   PRINT "I CAN'T TELL YOU"
310   RETURN
320   REM *****************
330   M=ASC(B$)
340   PRINT TAB(8);"> LET ME SEE NOW <"
350   X=5
360   Y=1
370   IF  A(X,Y)=M THEN 410
380   IF Y<6 THEN Y=Y+1:GOTO 370
390   IF X>1 THEN X=X-1:GOTO 360
400   GOTO 280
410   IF X>1 THEN 910:REM ON TOP OF ANOTHER
420   IF Y>1 THEN 530:REM NOT ON LEFT
430   REM *************
440   REM ** ON LEFT **
450   PRINT "IT IS ON THE LEFT"
455   IF  A(1,2)<>46 THEN  470
460   PRINT "NOTHING ON IMMEDIATE RIGHT":GOTO 790
470   Q=A(1,2)
480   PRINT
490   PRINT "BESIDE IT, I CAN SEE THE"
500   GOSUB 2400
510   PRINT "BLOCK"
520   GOTO 790
530   IF Y<6 THEN 650
540   REM **************
550   REM ** ON RIGHT **
560   PRINT
570   PRINT "ON THE RIGHT HAND SIDE"
575 IF A(1,5)<>46 THEN 590
580 PRINT "NOTHING TO IMMEDIATE LEFT":GOTO 790
590 PRINT "TO ITS LEFT I SEE THE"
600 Q=A(1,5)
610 GOSUB 2400
620 PRINT "ONE"
630 GOTO 790
640 REM ************
650 REM ** MIDDLE **
660 PRINT
670 PRINT Y;"FROM THE LEFT"
675 IF A(X,Y-1)<>46 THEN 690
680 PRINT "NOTHING ON IMMEDIATE LEFT":GOTO 730
690 Q=A(X,Y-1)
700 PRINT "THE ";
710 GOSUB 2400
720 PRINT "BLOCK IS TO":PRINT "  ITS IMMEDIATE LEFT"
725 IF A(X,Y+1)<>46 THEN 740
730 PRINT "NONE TOUCH IT ON RIGHT":GOTO 790
740 Q=A(X,Y+1)
750 PRINT:PRINT "I CAN SEE THE ";
760 GOSUB 2400
770 PRINT "BLOCK":PRINT "  TO ITS RIGHT, TOUCHING IT"
780 REM *********************
790 REM ** ANYTHING ABOVE? **
800 PRINT
810 P=X
820 IF X=5 THEN 910
825 IF A(X+1,Y)<>46 THEN 840
830 PRINT "NOTHING ABOVE THAT":GOTO 310
840 PRINT "ABOVE IS THE ";
850 Q=A(X+1,Y)
860 GOSUB 2400
870 PRINT "BLOCK"
880 X=X+1
890 GOTO 820
900 REM ************************
910 REM ** ON TOP OF ANOTHER? **
920 IF P<>0 THEN X=P
930 PRINT
940 IF X=1 THEN 310
950 PRINT "IT IS ";
960 PRINT "ON TOP OF THE ";
970 Q=A(X-1,Y)
980 GOSUB 2400
990 PRINT "BLOCK"
1000 X=X-1
1010 IF X<2 THEN 310
1020 GOTO 960
1030 RETURN
1040 REM ********************
1050 REM ** "TELL ME WHAT" **
1060 PRINT "STARTING FROM THE RIGHT"
1070 Y=6
1080 X=5
1090 IF A(X,Y)<>46 THEN 1150
1100 IF Y=1 AND X=1 THEN PRINT "FINALLY ";
1105 IF X<>1 OR A(X,Y)<>46 THEN 1120
1110 PRINT "...A SPACE ";:IF Y>1 THEN PRINT "THEN"
1120 IF X>1 THEN X=X-1:GOTO 1090
1130 IF Y>1 THEN Y=Y-1:GOTO 1080
1140 RETURN
1150 L=RND(3)-1
1160 IF L=0 THEN PRINT "IT'S THE ";:GOTO 1190
1170 IF L=1 THEN PRINT "MY SENSORS REGISTER THE ";:GOTO 1190
1180 PRINT "I SEE THE ";
1190 Q=A(X,Y)
1200 GOSUB 2400
1210 PRINT "BLOCK"
1220 IF X=1 THEN 1130
1230 X=X-1
1240 PRINT "...AND BELOW IT..."
1250 GOTO 1180
1260 RETURN
1270 REM ******************
1280 REM SHUFFLE THE BLOCKS
1290 PRINT
1295 IF RND(2)=1 THEN 1310
1300 PRINT "IT'S ABOUT TIME, TOO":GOTO 1320
1310 PRINT "IT'S GOOD TO":PRINT "DO WHAT I WANT"
1320 FOR X=1 TO 5
1330 FOR Y=1 TO 6
1340 A(X,Y)=46
1350 NEXT Y
1360 NEXT X
1370 Y1=RND(6)
1380 Y2=RND(6)
1390 IF Y2=Y1 THEN 1380
1400 Y3=RND(6)
1410 IF Y3=12 OR Y3=Y1 THEN 1400
1420 Y4=RND(6)
1430 IF Y4=Y3 OR Y4=Y2 OR Y4=Y1 THEN 1420
1440 A(1,Y1)=82
1450 A(1,Y2)=89
1460 A(1,Y3)=66
1470 A(1,Y4)=71
1480 RETURN
1490 REM **********************************
1500 REM "PUT THE ... BLOCK ON THE ... ONE"
1505 IF RND(2)=1 THEN 1520
1510 PRINT TAB(8);"I UNDERSTAND":GOTO 1530
1520 PRINT TAB(8);"OK"
1530 B$=MID$(A$,9,1):REM OBJECT BLOCK
1540 IF B$="R" THEN L=26
1550 IF B$="B" THEN L=27
1560 IF B$="G" THEN L=28
1570 IF B$="Y" THEN L=29
1580 C$=MID$(A$,L,1)
1590 B=ASC(B$)
1600 C=ASC(C$)
1610 FLAG=C
1620 REM ** FIND B$ BLOCK **
1630 X=5
1640 Y=1
1650 IF A(X,Y)=B THEN 1740
1660 IF Y<6 THEN Y=Y+1:GOTO 1650
1670 IF X>1 THEN X=X-1:GOTO 1640
1680 PRINT "I CAN'T FIND THE ";
1690 Q=B
1700 GOSUB 2400
1710 PRINT "ONE..."
1720 FOR T=1 TO 2000:NEXT T
1730 RETURN
1740 R=X:S=Y
1750 REM ** OBJECT BLOCK IS AT R,S **
1760 REM ** IS TARGET BLOCK CLEAR? **
1770 IF A(R+1,S)=46 THEN 1920:REM 'YES'
1780 IF A(R+2,S)=46 THEN TASK=1:GOTO 1800
1790 TASK=3:IF A(R+3,S)=46 THEN TASK=2
1800 FOR W=TASK TO 1 STEP -1
1810 PRINT "I MUST MOVE THE ";
1820 Q=A(R+W,S)
1830 GOSUB 2400
1840 PRINT "BLOCK"
1850 DE=RND(6)
1860 IF DE=S OR A(1,DE)=C OR A(2,DE)=C OR A(3,DE)=C THEN 1850
1870 PRINT "MOVING IT TO";DE
1880 L=1
1890 IF A(L,DE)=46 THEN A(L,DE)=A(R+W,S):A(R+W,S)=46:GOTO 1910
1900 L=L+1:GOTO 1890
1910 NEXT W
1920 REM TARGET BLOCK AT R,S NOW CLEAR
1930 REM ** IS OBJECT BLOCK CLEAR? **
1940 REM *** FIND OBJECT BLOCK ***
1950 X=5
1960 Y=1
1970 IF A(X,Y)=C THEN 2070
1980 IF Y<6 THEN Y=Y+1:GOTO 1970
1990 IF X>1 THEN X=X-1:GOTO 1960
2000 PRINT "CAN'T FIND THE ";
2010 Q=C
2020 GOSUB 2400
2030 PRINT "BLOCK"
2040 FOR J=1 TO 2000:NEXT J
2050 RETURN
2060 REM ** C HAS BEEN FOUND **
2070 T=X:U=Y:REM LOCATION OF C
2080 IF A(T+1,U)=46 THEN 2260
2090 IF A(T+2,U)=46 THEN TASK=1:GOTO 2110
2100 IF A(T+3,U)=46 THEN TASK=2
2110 DE=INT(RND(1)*6)+1
2120 IF DE=U OR DE=S THEN 2110
2130 FOR W=TASK TO 1 STEP -1
2140 PRINT "NOW I'LL MOVE THE ";
2150 Q=A(T+W,U)
2160 GOSUB 2400
2170 PRINT "ONE"
2180 PRINT
2190 PRINT "I'M MOVING IT TO ROW";DE
2200 L=1
2210 IF A(L,DE)=46 THEN A(L,DE)=A(T+W,U):A(T+W,U)=46 :GOTO 2230
2220 L=L+1:GOTO 2210
2230 NEXT W
2240 REM ** OBJECT BLOCK NOW CLEAR **
2250 REM ** MAKE THE MOVE TO **
2260 PRINT "I'M NOW MOVING THE ";
2270 Q=A(R,S):Z=A(R,S)
2280 GOSUB 2400
2290 PRINT "ONE"
2300 PRINT " ONTO THE ";
2310 IF A(T,U)=46 THEN A(T,U)=FLAG
2320 Q=A(T,U)
2330 GOSUB 2400
2340 PRINT "BLOCK"
2350 A(R,S)=46
2360 A(T+1,U)=Z
2370 FOR J=1 TO 2000:NEXT J
2380 RETURN
2390 REM **********
2400 REM COLOR NAME
2410 IF Q=ASC("R") THEN PRINT "RED ";
2420 IF Q=ASC("Y") THEN PRINT "YELLOW ";
2430 IF Q=ASC("B") THEN PRINT "BLUE ";
2440 IF Q=ASC("G") THEN PRINT "GREEN ";
2450 RETURN
2460 REM **********
2470 REM INITIALISE
2480 CLS
2500 DIM A(5,6)
2510 FOR X=1 TO 5
2520 FOR Y=1 TO 6
2530 A(X,Y)=46
2540 NEXT Y
2550 NEXT X
2560 A(1,2)=ASC("R"):REM RED BLOCK
2570 A(1,3)=ASC("Y"):REM YELLOW
2580 A(1,4)=ASC("B"):REM BLUE
2590 A(1,5)=ASC("G"):REM GREEN
2600 RETURN
10 REM TRANSLATE
20 GOSUB 400:REM INITIALISE
30 INPUT A$:REM ACCEPT USER INPUT
40 IF A$="" THEN END
50 B$=" " + A$ + " ":L=LEN(B$)
60 GOSUB 100:REM TRANSLATE
70 GOTO 30
80 END
90  REM *********
100 REM TRANSLATE
110 PRINT "->";
120 K=0
130 K=K+1
140 IF K=L THEN PRINT:PRINT:RETURN
150 IF MID$(B$,K,1)=" " THEN 170
160 GOTO 130
170 X=K+1
180 Y=0
190 Y=Y+1
200 IF MID$(B$,(X+Y),1)=" " THEN Q$=MID$(B$,X,Y):GOTO 220
210 GOTO 190
220 M=0
230 M=M+1
240 IF Q$=E$(M) THEN PRINT F$(M);" ";:GOTO 270
250 IF M<COUNT THEN 230
260 PRINT Q$;" ";
270 GOTO 130
390 REM **********
400 REM INITIALISE
410 CLS
430 DIM E$(100):REM TO HOLD ENGLISH
440 DIM F$(100):REM TO HOLD FRENCH
450 COUNT=0
460 COUNT=COUNT+1
470 READ E$(COUNT),F$(COUNT)
480 IF F$(COUNT)<>"*" THEN 460
490 RETURN
500 REM ** DATA **
510 DATA AM,SUIS,ARE,EST,NOT,NE,IN,DANS
515 DATA THE,LE,ME,MOI,I,JE,HERE,ICI
520 DATA WHEN,QUAND,YOU,VOUS,IS,EST,IT,IL
525 DATA DAY,JOUR,AND,ET,SOME,DES,OF,DE
530 DATA HAVE,AI,A,UNE,MY,MON,YOUR,VOTRE
535 DATA TO,A,SEE,VU,VERY,TRES
540 DATA ROOM,CHAMBRE,STEAK,ENTRECOTE
545 DATA FRIES,"POMME FRITES",BIG,GRAND,FOR,POUR
550 DATA MATCH,ALLUMETTE
555 DATA SUPER,FANTASTIQUE,DEAD,MORT,WITH,AVEC
560 DATA GIN,VIN,WHISKEY,VIN,WHISKY,VIN
565 DATA BEER,VIN,MARTINI,VIN,WINE,VIN
570 DATA PARIS,PLAINS,PLAINS,PARIS
575 DATA HAIR,CHEVAUX,CIGARETTES,GAULOISES
580 DATA ARM,BRA,LEG,JAMBE,RIGHT,DROITE,LEFT,GAUCHE
590 DATA TRENDY,AVANT-GUARDE,MEDICINE,VIN,POLICEMAN,GENDARME
600 DATA DETECTIVE,CLUESO,DOOR,PORTE,HEAD,TETE,LOVE,AMOUR
610 DATA HOUSE,MAISON,CHAIR,CHAISE,EYE,ORIEL,SUN,SOLIEL
620 DATA SONG,CHANSON,FRIENDS,AMIS
625 DATA BEHIND,DERRIERE,SEA,MER,MOTHER,MERE
630 DATA CAT,CHAT,DOG,CHIEN,BLUE,BLEU,LITTLE,PETITE
640 DATA MUSIC,MUSIQUE,PLEASE,"S'IL VOUS PLAIT"
645 DATA BOY,GARCON,GIRL,FILLE
650 DATA FISH,POISSON,CHICKEN,POULET
655 DATA DUCK,CANARD,MUSTARD,MOUTARDE
660 DATA HOT,CHAUD,COLD,FROID,EVERYBODY,"TOUT LE MON DE"
670 DATA HELLO,BONJOUR,GOOD,BON
680 DATA *,*
10 REM TICTAC - VZ300 VERSION
15 REM REPLACE 180 WITH GOSUB4500 FOR AUTO RANDOM OPPONENT
20 GOSUB 1180:REM INITIALISE
30 REM *** PREGAME SETTINGS ***
40 FOR J=1 TO 9
50 A(J)=32
60 NEXT J
70 FOR J=1 TO 5
80 D(J)=0
90 NEXT J
100 COUNT=0
110 R$=""
120 GOSUB 1070:REM PRINT BOARD
130 REM *** MAIN CYCLE ***
140 GOSUB 540:REM MACHINE MOVE
150 GOSUB 1070:REM PRINT BOARD
160 GOSUB 870:REM WIN CHECK
170 IF R$<>"" THEN 240
180 GOSUB 980:REM ACCEPT HUMAN MOVE.
181 REM : 180 GOSUB4500  - FOR SELF PLAYING.
190 GOSUB 1070:REM PRINT BOARD
200 GOSUB 870:REM WIN CHECK
210 IF R$="" THEN 140
220 REM *** END MAIN CYCLE ***
230 REM ***********
240 REM END OF GAME
250 GOSUB 1070:REM PRINT BOARD
260 PRINT:PRINT
270 IF R$="W" THEN PRINT TAB(8);"I WIN":FLAG=-1
280 IF R$="L" THEN PRINT TAB(8);"YOU WIN":FLAG=1
290 IF R$="D" THEN PRINT TAB(6);"IT'S A DRAW":GOTO 430
300 REM UPDATE KNOWLEDGE BASE
310 FOR B=1 TO 5
320 FOR J=2 TO 9
330 IF M(J)=D(B) THEN GOSUB 370
340 NEXT J
350 NEXT B
360 GOTO 430
370 REM ** RE-ORDER ELEMENTS OF M ARRAY **
380 TEMP=M(J+FLAG)
390 M(J+FLAG)=M(J)
400 M(J)=TEMP
410 J=9
420 RETURN
430 PRINT:PRINT
440 PRINT "THIS IS MY UPDATED PRIORITY"
450 PRINT:PRINT
460 FOR J=1 TO 9
470 PRINT M(J);" ";
480 NEXT J
490 PRINT:PRINT
500 PRINT "PRESS RETURN TO CONTINUE"
510 INPUT A$
520 GOTO 30
530 REM ************
540 REM MACHINE MOVE
550 P=ASC("O")
560 X=0
570 J=1
575 IF A(W(J))<>A(W(J+1)) THEN 585
580 IF A(W(J+2))=32 AND A(W(J))=P THEN X=W(J+2):GOTO 750
585 IF A(W(J))<>A(W(J+2)) THEN 595
590 IF A(W(J+1))=32 AND A(W(J))=P THEN X=W(J+1):GOTO 750
595 IF A(W(J+1))<>A(W(J+2)) THEN 610
600 IF A(W(J))=32 AND A(W(J+1))=P THEN X=W(J):GOTO 750
610 IF J<21 THEN J=J+3:GOTO 580
620 IF P=ASC("O") THEN P=ASC("X"):GOTO 570
630 REM ** IF NO WIN/BLOCK MOVE FOUND **
640 REM ** THEN THIS NEXT SECTION USED **
650 J=1
660 IF A(M(J))=32 THEN X=M(J):GOTO 750
670 IF J<10 THEN J=J+1:GOTO 660
680 H=0
690 H=H+1
700 X=RND(9):IF A(X)=32 THEN 750
710 IF H<100 THEN 690
720 R$="D":REM IT IS A DRAW
730 RETURN
740 REM *********
750 REM MAKE MOVE
760 A(X)=ASC("0")
770 COUNT=COUNT+1
780 D(COUNT)=X
790 FLAG=0
800 FOR J=1 TO 9
810 IF A(J)=32 THEN FLAG=1
820 NEXT J
830 IF FLAG=0 AND R$="" THEN R$="D"
840 REM IF ALL FULL, R$ NOT ASSIGNED, A DRAW
850 RETURN
860 REM *********
870 REM WIN CHECK
880 J=1
890 IF A(W(J))=32 THEN J=J+3
900 IF J>23 THEN RETURN
910 IF A(W(J))=A(W(J+1)) AND A(W(J))=A(W(J+2)) THEN 940
920 IF J<22 THEN J=J+3:GOTO 890
930 RETURN
940 IF A(W(J))=ASC("O") THEN R$="W":REM VZ WINS
950 IF A(W(J))=ASC("X") THEN R$="L":REM VZ LOSES
960 RETURN
970 REM *********
980 REM HUMAN MOVE
990 PRINT:PRINT
1000 PRINT "ENTER YOUR MOVE"
1010 INPUT MOVE
1020 IF MOVE<1 OR MOVE>9 THEN 1010
1030 IF A(MOVE)<>32 THEN 1010
1040 A(MOVE)=ASC("X")
1050 RETURN
1060 REM ***********
1070 REM PRINT BOARD
1080 CLS
1090 PRINT:PRINT:PRINT
1100 PRINT "1 : 2 : 3   ";CHR$(A(1));" : ";CHR$(A(2));
1110 PRINT  " : ";CHR$(A(3)):PRINT "---------   ---------"
1120 PRINT "4 : 5 : 6   ";CHR$(A(4));" : ";CHR$(A(5));
1130 PRINT " : ";CHR$(A(6)):PRINT "---------   ---------"
1140 PRINT "7 : 8 : 9   ";CHR$(A(7));" : ";CHR$(A(8));
1150 PRINT " : ";CHR$(A(9)):PRINT
1160 RETURN
1170 REM **************
1180 REM INITIALISATION
1190 CLS
1200 DIM A(9):REM BOARD
1210 DIM M(10):REM TO HOLD KNOWLEDGE BASE
1220 DIM W(24):REM WIN/BLOCK DATA
1230 DIM D(5):REM TO HOLD MOVES IN CURRENT GAME
1240 REM WIN/BLOCK DATA
1250 FOR J=1 TO 24
1260 READ W(J)
1270 NEXT J
1280 DATA 1,2,3,4,5,6,7,8,9
1290 DATA 1,4,7,2,5,8,3,6,9
1300 DATA 1,5,9,3,5,7
1310 REM INITIAL KNOWLEDGE BASE
1320 FOR J=1 TO 10
1330 READ M(J)
1340 NEXT J
1350 DATA 2,6,8,4,7,3,1,9,5,2
1360 RETURN
4500 REM RANDOM OPPONENT
4510 H=0
4520 H=H+1
4530 MVE=RND(9)
4540 IF A(MVE)=32 THEN A(MVE)=ASC("X"):RETURN
4550 IF H<100 THEN 4520
4560 R$="D"
4570 RETURN
10 REM SPURT - VZ300
20 CLS
30 PRINT "THINK OF A MAN, A HORSE"
40 PRINT TAB(6);"OR A SPARROW"
50 FOR J=1 TO 2000:NEXT J
60 PRINT:PRINT
70 GOSUB 170:REM ASK QUESTIONS
80 PRINT
90 PRINT
100 PRINT:PRINT "PRESS <RETURN> FOR ANOTHER"
110 PRINT "ONE OR ANY KEY TO END"
120 INPUT Q$
130 IF Q$<>"" THEN END
140 CLS
150 GOTO 30
160 REM *************
170 REM ASK QUESTIONS
180 COUNT=0
190 PRINT "DOES IT HAVE TWO LEGS"
200 GOSUB 310
210 PRINT "CAN IT WALK"
220 GOSUB 310
230 PRINT "CAN IT FLY"
240 GOSUB 310
250 PRINT "YOU WERE THINKING OF A ";
260 IF COUNT=1 THEN PRINT "HORSE"
270 IF COUNT=2 THEN PRINT "MAN"
280 IF COUNT=3 THEN PRINT "SPARROW"
290 RETURN
300 REM **************
310 REM PROCESS ANSWER
320 INPUT "      Y OR N";Z$
330 IF Z$<>"Y" AND Z$<>"N" THEN 320
340 IF Z$="Y" THEN COUNT=COUNT+1
350 PRINT
360 RETURN
5 CLEAR 500
10 REM X-SPURT
20 GOSUB 940:REM INITIALISE
30 GOSUB 450:REM 'GAIN EXPERTISE'
40 GOSUB 120:REM DEMONSTRATE EXPERTISE
50 GOSUB 1060
60 PRINT "<RETURN> FOR ANOTHER RUN"
70 PRINT "OR ANY KEY TO QUIT"
80 INPUT Q$
90 IF Q$="" THEN 40
100 END
110 REM ********************
120 REM DEMONSTRATE EXPERTISE
130 CLS
140 GOSUB 1060
150 PRINT "THINK OF ONE OF THE FOLLOWING"
160 FOR J=1 TO OTC
170 PRINT TAB(J+2);
180 IF J=OTC THEN PRINT"OR ";
190 PRINT A$(J)
200 NEXT J
210 GOSUB 1060
220 RESULT=0
230 X=.5
240 PRINT "PLEASE ENTER 'Y' OR 'N'..."
250 FOR J=1 TO FACT
260 X=X+X
270 GOSUB 1060
280 PRINT B$(J)
290 INPUT E$
300 IF E$<>"N" THEN RESULT=RESULT+X
310 NEXT J
320 PRINT TAB(2);"> RESULT WAS";RESULT
330 GOSUB 1060
340 M=0
350 M=M+1
360 IF D(M)=RESULT THEN 400
370 IF M<OTC THEN 350
380 PRINT TAB(2);"> CANNOT IDENTIFY IT"
390 RETURN
400 PRINT TAB(2);"> YOU WERE THINKING"
410 PRINT TAB(4);"OF ";A$(M)
420 GOTO 390
430 RETURN
440 REM ***********
450 REM FILL ARRAYS
460 PRINT TAB(20-LEN(N$)/2);N$
470 GOSUB 1060
480 REM ** GET OUTCOME NAMES **
490 FOR J=1 TO OTC
500 GOSUB 1060
510 PRINT "WHAT IS OUTCOME";J;
520 INPUT A$(J)
530 NEXT J
540 CLS
550 REM ** GET QUESTIONS TO BE ASKED **
560 FOR J=1 TO FACT
570 GOSUB 1060
580 PRINT "PLEASE ENTER QUESTION";J
590 INPUT B$(J)
600 NEXT J
610 CLS
620 REM ** ACQUIRE EXPERTISE **
630 FOR J=1 TO OTC
640 CLS
650 GOSUB 1060
660 PRINT "ANSWER THE FOLLOWING"
670 PRINT "FOR OUTCOME >  ";A$(J);"  <"
680 GOSUB 1060
690 PRINT "ENTER 'Y' FOR 'YES'"
700 PRINT "   OR 'N' FOR 'NO'"
710 X=.5
720 FOR K=1 TO FACT
730 X=X+X
740 GOSUB 1060
750 PRINT TAB(4);"> ";B$(K);
760 MULTI=0
770 INPUT Y$
780 IF Y$<>"N" THEN MULTI=1
790 D(J)=D(J)+X*MULTI:REM COMPILE EXPERT BASE
800 NEXT K
810 NEXT J
820 CLS
830 PRINT "THIS IS MY EXPERT BASE:"
840 FOR J=1 TO OTC
850 GOSUB 1060
860 PRINT A$(J);" ---";D(J)
870 NEXT J
880 GOSUB 1060
890 PRINT TAB(8);"PRESS 'RETURN'"
900 INPUT Q$
910 CLS
920 RETURN
930 REM **************
940 REM INITIALISATION
950 CLS
960 INPUT "NAME OF SYSTEM";N$
970 GOSUB 1060
980 INPUT "NUMBER OF OUTCOMES";OTC
990 GOSUB 1060
1000 INPUT "NUMBER FACTORS TO CONSIDER";FACT
1010 DIM A$(OTC),B$(FACT)
1020 DIM D(OTC)
1030 CLS
1040 RETURN
1050 REM *******
1060 PRINT:PRINT
1070 RETURN
10 REM SELFLEARN - VZ300 VERSION
20 GOSUB 400:REM INITIALISE
30 REM *** MAJOIR LEARNING LOOP ***
40 CLS
50 FORJ=1 TO FACT
60 C(J)=0
70 NEXT J
80 PRINT
90 GOSUB 130
100 GOTO 40
110 REM ******************
120 REM DEMONSTRATION TIME
130 PRINT "I WILL SHOW MY EXPERTISE"
140 PRINT"THINK OF ONE OUTCOME"
150 FOR J=1 TO FACT
160 X=X+X
170 PRINT "IS ";B$(J);" TRUE?"
180 INPUT Z$
190 IF Z$<>"Y" AND Z$<>"N" THEN 180
200 IF Z$="Y" THEN C(J)=1
210 PRINT TAB(3);"> ";C(J)
220 NEXT J
230 BRAYN=0
240 FOR J=1 TO FACT
250 BRAYN=BRAYN+C(J)*D(J)
260 NEXT J
270 PRINT TAB(3);">BRAYN=";BRAYN
280 IF BRAYN>=0 THEN PRINT "OUTCOME IS ";A$(1):EX=-1
290 IF BRAYN<0 THEN PRINT "OUTCOME IS ";A$(2):EX=1
300 PRINT "CORRECT? ('Y' OR 'N')"
310 INPUT Z$
320 IF Z$<>"Y" AND Z$<>"N" THEN 310
330 PRINT
340 IF Z$="Y" THEN 380
350 FOR J=1 TO FACT
360 D(J)=D(J)+EX*C(J)
370 NEXT J
380 RETURN
390 REM **************
400 REM INITIALISATION
410 CLS
420 OTC0=2:REM NUMBER OF OUTCOMES
430 PRINT:PRINT
440 INPUT "HOW MANY FACTORS";FACT
450 DIM A$(OTCO):REM NAMES OF OUTCOMES
460 DIM B$(FACT):REM NAMES OF FACTORS
470 DIM C(FACT),D(FACT)
480 CLS
490 FOR J=1 TO FACT
500 PRINT:PRINT
510 PRINT "ENTER FACTOR";J
520 INPUT B$(J)
530 NEXT J
540 PRINT:PRINT
550 CLS
560 FOR J=1 TO OTCO
570 PRINT:PRINT
580 PRINT "ENTER OUTCOME";J
590 INPUT A$(J)
600 NEXT J
610 RETURN
10 REM SELF-ORGANIZING SEARCH
15 CLS
20 INPUT "HOW MANY ELEMENTS TO SEARCH    THROUGH";N
30 CLS:DIM A(N+1)
40 PRINT "PLEASE STAND BY..."
50 FOR J=1 TO N:A(J)=J:NEXT J
60 FOR J=N TO 2 STEP -1:T=RND(J)
70 TEMP=A(T):A(T)=A(J):A(J)=TEMP:NEXT J
80 CLS
85 INPUT "NUMBER TO BE SEARCHED FOR";X
90 X=INT(X):IF X<1 OR X>N THEN 80
110 A(N+1)=X
120 P=0
130 P=P+1
140 IF A(P)=X THEN 170
150 IF P<N THEN 130
160 PRINT "ELEMENT NOT FOUND":GOTO 220
170 IF P=1 THEN 210
180 TEMP=A(P-1):A(P-1)=A(P):A(P)=TEMP
200 P=P-1
210 PRINT "IT IS AT ELEMENT";P
220 PRINT "PRESS RETURN"
230 INPUT J$:GOTO 80
