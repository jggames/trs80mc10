2 CLEAR 1000:C1=RND(-(PEEK(9)*256+PEEK(10)))
4 BOX$=" BOX A   BOX B    BOX C   BOX D "
6 B$(1)="ƒƒƒ‚":B$(2)="… ? Š":B$(3)="„ŒŒŒˆ"
8 FORX=1 TO 3:B$(X)=" "+B$(X)+" ":B$(X)=B$(X)+" "+B$(X):B$(X)=B$(X)+"  "+B$(X):B$=B$+B$(X):NEXT:GOSUB 124
10 CLS:PRINT@198,"HOW MANY TO PLAY";:INPUT NU$:NU=VAL(NU$):IF NU=0 THEN 10
12 IF NU>6 THENPRINT@194,"MAX. NUMBER OF PLAYERS IS 6":T=1111:GOSUB142:GOTO10
14 FORX=1 TO NUMBER:PRINT:PRINT"NAME OF PLAYER"X;CHR$(8);:INPUTN$(X):IF LEN(N$(X))>10 THEN N$(X)=LEFT$(N$(X),10)
15 N(X)=0
16 NEXT
18 CLS:PRINT@102,"HOW HARD DO YOU WANT":PRINT@168,"THE GAME TO BE?"
20 PRINT@263,"LEVEL  1   (EASIEST)":PRINT@327,"LEVEL  2":PRINT@391,"LEVEL  3":PRINT@455,"LEVEL  4  (HARDEST)"
22 GOSUB 144:EASY=VAL(K$):IF EASY>4 OR EASY<1 THEN 18
24 CLS:PRINT@192,"HOW MANY ROUNDS OF COMPETITION?":PRINT@334,"";:GOSUB 144:CHOICE=VAL(K$):IF CH<1 THEN 24
26 GOTO44
28 CLS:PRINT@23,"ROUND";ROUND
30 X$="YOUR TURN "+N$:PRINT@48-LEN(X$)/2,X$
32 PRINT@99,"MAKE  THIS  STATEMENT  TRUE       ":PRINTBOX$;B$;" "
34 LA=PL-1:IF LA<1 THEN LA=NU
36 IF OP=2 THEN S1$="-":GOTO38
37 S1$="+"
38 IF OS=2 THEN S2$="-":GOTO40
39 S2$="+"
40 PRINT@231,S1$;:PRINT@248,S2$;
42 PRINT@239,CA$;:PRINT@456,"RUNNING  TOTALS" :PRINT@481,N$(PL);N(PL);:IF NU>1 THEN PRINT@505-LEN(N$(LA)),N$(LA);N(LA);
43 RETURN
44 FOR ROUNDS=1 TO CHOICE:FOR PL=1 TO NUMBER:N$=N$(PL):A=0:B=0:C=0:D=0:TEST=0
46 CASE=CASE+1:IF CASE/2=INT(CASE/2) THEN SIGN=2:GOTO48
47 SIGN=1
48 IF SIGN=1 THEN CA$=" >":CH$="GREATER":GOTO50
49 CA$="< ":CH$="  LESS "
50 IF CASE=1 OR CASE=3 OR CASE=4 OR CASE=8 THEN OP=1:GOTO52
51 OP=2
52 IF CASE=2 OR CASE=3 OR CASE=4 OR CASE=7 THEN OS=1:GOTO54
53 OS=2
54 GOSUB 28:FOR TURN=1 TO 4:X$="HIT ANY KEY TO START DIE ROLLING ":P=320:GOSUB 174:PRINT@384,"":GOSUB 144
56 X$=" DIE ROLLING  1 2 3 4 5 6 7 8 9 ":GOSUB 174
58 X$=" HIT ANY KEY TO STOP DIE ":P=384 :GOSUB 174
60 DIE=RND(9):B1=48+DIE:L=16716+2*DIE:FORX=1 TO (5-EASY):POKEL,B1:POKEL+32,94
62 GOSUB 140
64 IF MM=1THEN MM=0:GOTO 72
66 K$=INKEY$:IF K$<>"" THEN MM=1
68 POKEL,B1+64:POKEL+32,96:NEXT X
70 GOTO 60
72 PRINT@384," IN WHICH BOX SHALL I PUT";DIE;CHR$(8)"? ";
74 GOSUB 144:PRINTK$
76 FORC1=1TO4:IFMID$("ABCD",C1,1)=K$THENC2=C1:C1=4:NEXT:ONC2GOTO 80,82,84,86
77 NEXT
78 PRINT@384,"YOU MUST CHOOSE A B C OR D":T=1500:GOSUB 142:GOTO 72
80 IF A=0 THEN A=DIE:PRINT@226,A;:NEXT TURN:GOTO82
81 S=A:GOTO176
82 IF B=0 THEN B=DIE:PRINT@234,B;:NEXT TURN:GOTO84
83 S=B:GOTO176
84 IF C=0 THEN C=DIE:PRINT@243,C;:NEXT TURN:GOTO86
85 S=C:GOTO176
86 IF D=0 THEN D=DIE:PRINT@251,D;:NEXT TURN:GOTO88
87 S=D:GOTO176
88 IF OP=2 THEN U1=A-B:GOTO90
89 U1=A+B
90 IF OS=2 THEN U2=C-D:GOTO92
91 U2=C+D
92 IF SIGN=1 AND U1-U2>0 THEN TEST=1:P=308:W=C
94 IF SIGN=2 AND U1-U2<0 THEN TEST=1:P=291:W=A
96 IF TEST=1 THENPRINT@P,CHR$(94):PRINT"YOU HAVE WON ";W;"POINTS. <ENTER>     ":PRINT@384,"":GOSUB999
98 IF TEST=1 THEN N(PL)=N(PL)+W:NEXT PL:GOSUB 112:NEXT ROUND:GOTO116
100 PRINT@320," HARD LUCK! IT DID NOT WORK OUT ":PRINT@384,"":X$="CAN YOU SEE WHY? "+N$(PL):PRINT@399-LEN(X$)/2,X$
102 GOSUB 144
104 IF K$="Y"THEN PRINT@320,"":NEXT PL:GOSUB 112:NEXT ROUND:GOTO116
106 PRINT@320," YOU WERE TO MAKE BOX A ";S1$;" BOX B":PRINTCH$;" THAN BOX C ";S2$;" BOX D":PRINT""
107 FOR X=1 TO 9:T=800:GOSUB 142:PRINT@395,LEFT$("w a t c h",X);:GOSUB 140:NEXT
108 P=338:P1=A:GOSUB122:P=346:P1=B:GOSUB122:P=365:P1=C:GOSUB122:P=373:P1=D:GOSUB122
110 PRINT@385,U1;" IS NOT ";CH$;" THAN ";U2:T=2500:GOSUB 144:NEXT PL:GOSUB 112:NEXT ROUND:GOTO 116
112 CLS:PRINT@136,"after";ROUND;"rounds ";:PRINT:FORX=1 TO NU:PRINT:PRINT"    ";N$(X),N(X):NEXT
114 PRINT:PRINT:PRINT" hit any key";:GOSUB 144:RETURN
116 FORC1=1TO12:PRINTCHR$(8);:NEXT:PRINT"ANOTHER GAME?"
118 GOSUB 144:IF K$="Y" THEN 10
120 END
122 PRINT@P,"  ";P1;:GOSUB142:GOSUB140:RETURN
124 CLS:T=189
126 S1=192:S2=202:S3=1:S$=" DRAGON USER":GOSUB138
128 S1=248:S2=236:S3=-1:S$="PRESENTS ":GOSUB138
130 S1=288:S2=296:S3=1:S$=" IT'S IN THE BOX":GOSUB138
132 S1=479:S2=484:S3=1:IFGG<1THENGG=GG+1:S$=" WRITTEN BY MIKE HARRISON":GOSUB138:GOSUB142:GOTO124
133 S$=" DO YOU WANT INSTRUCTIONS?":S2=483:GOSUB138:GOSUB144
134 IF K$="Y" THEN GOSUB 148
136 RETURN
138 FOR S=S1 TO S2 STEP S3:PRINT@S,S$;:GOSUB 140:NEXT:RETURN
140 SOUND136,1:SOUND87,1:RETURN
142 FOR DE=1 TO T:NEXT DE:RETURN
144 K$=INKEY$:IF K$<>""THEN144
146 K$=INKEY$:IF K$=""THEN 146
147 RETURN
148 CLS:FOR P=62 TO 510 STEP 64:GOSUB 150:NEXT P:GOSUB 144:GOTO 152
150 READ X$:FOR X=1 TO 29:PRINT@P-X,LEFT$(X$,X);:SOUND113,1:SOUND113,1:NEXT X:RETURN
152 P=510:FOR XX=1 TO 8:PRINT:PRINT:GOSUB 150:NEXT XX:GOSUB 144
154 PRINT:PRINT:PRINT:PRINTBOX$;B$:GOSUB 144
156 PRINT:PRINT" SO YOU SHOULD TRY TO  MAKE THE  SCORING BOX AS  LARGE  AS  YOU  DARE":PRINT
157 PRINT" HIT 'R' TO SEE THE RULES AGAIN  ANY OTHER KEY TO START PLAYING"
158 GOSUB 144:IFK$="R" THEN RESTORE:GOTO 148
159 RETURN
160 DATA IT'S IN THE BOX IS A THINKING,GAME. IT  WILL  HELP  YOU  TO,UNDERSTAND ABOUT INEQUALITIES.
161 DATA ANY NUMBER OF PEOPLE CAN PLAY
162 DATA UP TO A MAXIMUM OF 6. YOU CAN, CHOOSE  ONE OF  4 LEVELS  OF ,DIFFICULTY  AND ANY NUMBER OF
163 DATA ROUNDS. hit key for new pages
164 DATA BY PUTTING NUMBERS THROWN BY,A DIE INTO EACH OF FOUR BOXES,YOU  MUST TRY  TO  MAKE THE
165 DATA STATEMENT TRUE. 
166 DATA IF THE TASK IS 'GREATER THAN' ,AND YOU WIN  -  YOU SCORE THE ,VALUE IN BOX C. IF YOU WIN AT
167 DATA 'LESS THAN'  YOU SCORE BOX A.
174 FOR X=1 TO 33:PRINT@P,RIGHT$(X$,X):NEXT:RETURN
176 IF TURN>4 THEN 88
177 PRINT@385,"BOX ";K$;" ALREADY HAS";S;"IN IT":T=1500:GOSUB 142:GOTO72
178 REM MC-10 EDITS JIM GERRIE
999 FORC1=0TO1STEP0:GOSUB144:C1=-(K$=CHR$(13)):NEXT:RETURN
