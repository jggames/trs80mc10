1 CLS:PRINT"A BASIC PROGRAM FOR SOLVING":PRINT"THE EIGHT QUEENS PROBLEM."
2 PRINT"BYTE MAGAZINE, OCTOBER 1978":PRINT"PG. 122-126"
3 PRINT"BY TERRY SMITH":PRINT
5 DIM A(8,8),A$(10),F(8)
10 FOR T=1 TO 10:READ A$(T):NEXT T:DATA "0","1","2","3","4","5","6","7","8","9"
15 INPUT"OUTPUT TO PRINTER (Y/N)";M$
20 C=0
30 C=C+1
40 IF C=9 THEN 250
50 FOR E=1 TO 8
60 IF A(C,E)=0 THEN ZZ=E:E=8:NEXT E:E=ZZ:GOTO89
70 NEXT E
80 GOTO 330
89 F(C)=E
90 A(C,E)=-1
91 D=1
92 GOSUB 100
93 GOTO 30
100 FOR X=-1 TO 1
110 FOR Y=-1 TO 1
120 IF X*Y+X+Y=0 THEN 220
130 FOR Z=1 TO 8
140 A=C+Y*Z
150 B=E+X*Z
160 IF A>8 THEN ZZ=Z:Z=8:NEXT Z:Z=ZZ:GOTO 220
170 IF A<1 THEN ZZ=Z:Z=8:NEXT Z:Z=ZZ:GOTO 220
180 IF B>8 THEN ZZ=Z:Z=8:NEXT Z:Z=ZZ:GOTO 220
190 IF B<1 THEN ZZ=Z:Z=8:NEXT Z:Z=ZZ:GOTO 220
200 A(A,B)=A(A,B)+D
210 NEXT Z
220 NEXT Y
230 NEXT X
240 RETURN
250 CC=CC+1:PRINTTAB(2)CC:IF M$="Y"THEN LPRINTTAB(2)CC
251 FOR X=8 TO 1 STEP -1
260 FOR Y=1 TO 8
270 IF A(Y,X)=-1 THEN 290
280 PRINT A$(A(Y,X)+1);:IF M$="Y"THEN LPRINTA$(A(Y,X)+1);
281 GOTO 300
290 PRINT "q";
300 NEXT Y
310 PRINT:IF M$="Y"THEN LPRINT
320 NEXT X
321 PRINT:IF M$="Y"THEN LPRINT
330 REM NO SPACES, NOW WHAT?
340 C=C-1
345 IF C=0 THEN 480
350 A(C,F(C))=0
360 E=F(C)
380 D=-1
390 GOSUB 100
391 IF E=8 THEN 340
400 FOR X=E+1 TO 8
410 IF A(C,X)=0 THEN ZZ=X:X=8:NEXT X:X=ZZ:GOTO440
420 NEXT X
430 GOTO 340
440 A(C,X)=-1
441 F(C)=X
450 D=1
451 E=X
460 GOSUB 100
470 GOTO 30
480 END
