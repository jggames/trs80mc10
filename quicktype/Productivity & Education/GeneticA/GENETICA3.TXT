0 CLEAR500:DIMCC,ZZ,M,M$,I$:M=16384:GOTO10:REM LINES 1-2 WORD WRAP ROUTINE
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$<"!"THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=.
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":C1=C1+1:IFC1<2THENRETURN:REM LINES 3-5 SCREEN PAUSE
3 PRINT@480,;:M$="HIT A KEY":GOSUB7:FORCC=0TO1STEP0:CC=-(INKEY$=""):NEXT
4 I$=INKEY$:IFI$=""THEN4
5 C1=0:CLS:RETURN:REM LINE 7 PRINT REVERSE TEXT
7 CC=(PEEK(17024)AND1)*256+PEEK(17025)+16383:FORZZ=1TOLEN(M$):POKECC+ZZ,ASC(MID$(M$,ZZ))AND63:NEXT:?@CC-M+ZZ,:RETURN
10 CLS:PRINT:PRINT:PRINT"PROBLEM SOLVING":PRINT"GENETIC ADAPTATION":PRINT:A=0:B=0:C=0:D=0:E=0:F=0:G=0:X=0:Y=0
20 DIM A(7,13),B(7),C(7,13),D(7),R(7),V(7),X(7),Y(7),Z(7)
30 PRINT "CONTACT ART RADCLIFFE"
40 PRINT "1521 HATCHER CRESCENT"
50 PRINT "ANN ARBOR. MI 48103"
60 PRINT "(313)-769-3039
70 PRINT:PRINT"WANT INSTRUCTIONS? (Y/N)"
80 I$=INKEY$:ZZ=RND(0):IFI$="Y"THEN1000
81 IFI$<>"N"THEN80
82 GOTO900
99 REM TRANSLATE BINARY STRING TO X & Y COORDINATES!
100 PRINT@495,;:M$=MID$(STR$(H),2):GOSUB7
110 FOR D=0 TO 7:X(D)=0:Y(D)=0:NEXT D
120 FOR A=0 TO 7:FOR B=0 TO 6
130 C=2*B
140 D=C+1
150 E=B(B)
160 X(A)=X(A)+C(A,C)*E
170 Y(A)=Y(A)+C(A,D)*E
180 NEXT B:NEXT A
190 PRINT@0,"€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€€";
199 REM PLOT:
200 FOR A=0 TO 7
210 X=W(A)
220 Y=Z(A)
230 SET(X/2,Y/4,1):RESET(X/2,Y/4):REM SCALED FOR MC-10 RESET(X+1,Y):RESET(X,Y+1):RESET(X+1,Y+1)
240 X=X(A)
250 W(A)=X
260 Y=Y(A)
270 Z(A)=Y
280 SET(X/2,Y/4,3):REM SCALED FOR MC-10 SET(X+1,Y,3):SET(X,Y+1,3):SET(X+1,Y+1,3)
290 NEXT A
299 REM CALC DISTANCES FROM TARGET:
300 FOR A=0 TO 7
310 D(A)=INT(.5+SQR((X(A)-64)^2+(Y(A)-64)^2))
320 R(A)=A
330 NEXT A
399 REM ESTABLISH RANK ORDER:
400 FOR A=0 TO 6:FOR B=A TO 7
410 C=D(A)
420 D=D(B)
430 IF C<=D THEN 490
440 D(A)=D
450 D(B)=C
460 E=R(A)
470 R(A)=R(B)
480 R(B)=E
490 NEXT B:NEXT A
499 REM MUTATE AND COPY INTO ARRAY 'A':
500 FOR A=0 TO 7
510 B=R(A)
520 IF INT(2*RND(0))=0 THEN 550
530 D=INT(14*RND(0))
540 C(A,D)=1-C(A,D)
550 FOR C=0 TO 13
560 A(A,C)=C(B,C)
570 NEXT C:NEXT A
599 REM INITIALIZE:
600 PRINT@0,;:M$="BEST 4:"
610 FOR A=0 TO 3
620 M$=M$+STR$(D(A))
630 FOR B=0 TO 13
640 C(A,B)=A(A,B)
650 NEXT B:NEXT A
660 GOSUB7
699 REM CUT & SPLICE 1ST 4 TO MAKE NEW 2ND 4:
700 FOR A=0 TO 2 STEP 2
710 B=A+1
720 C=A+4
730 D=A+5
740 E=1+INT(12*RND(0))
750 FOR F=0 TO E
760 C(C,F)=A(A,F)
770 C(D,F)=A(B,F)
780 NEXT F
790 FOR G=E+1 TO 13
800 C(C,F)=A(B,F)
810 C(D,F)=A(A,F)
820 NEXT G:NEXT A
830 H=H+1
840 IF H=21 THEN H=0:GOTO900
850 GOTO 100
899 REM INITIALIZE:
900 GOSUB 3:FOR A=0 TO 7:FOR B=0 TO 13
910 C(A,B)=INT(2*RND(0))
920 NEXT B:NEXT A
930 FOR A=0 TO 6
940 B(A)=INT(2^A)
950 NEXT A
970 CLS0
980 FORX=64TO66:FORY=64TO66:SET(X/2,Y/4,4):NEXT Y:NEXT X
990 GOTO 100
1000 CLS:M$="THIS IS AN APPLICATION OF A PERFECTLY GENERAL PROBLEM SOLVING SYSTEM "
1001 M$=M$+"WHICH I HAVE ALSO USED TO SOLVE THE TRAVELING SALESMAN PROBLEM "
1002 M$=M$+"(FIND THE SHORTEST ROUTE THROUGH N CITIES).":GOSUB1
1003 M$="THIS TECHNIQUE WAS DEVELOPED UNDER PPOFESSOR JOHN HOLLAND OF THE UNIVERSITY OF MICHIGAN "
1004 M$=M$+"COMPUTER SCIENCE DEPARTMENT BY D. J. CAVICCHIO AND P. B. HOLLSTIEN ABOUT 1970.":GOSUB1
1005 M$="THIS TECHNIQUE CAN BE APPLIED TO ANY PROBLEM WHOSE SOLUTION CAN BE EXPPESSED "
1006 M$=M$+"AS A STRING, SUCH AS BY FORMING A STRING FROM THE COEFFICIENTS OF AN EQUATION "
1007 M$=M$+"OR FROM ANY SYMBOLS DEFINING A PATTERN OR EVENT.":GOSUB1
1008 M$="THERE IS ONE OTHER ESSENTIAL CONDITION: THAT THE POPULATION OF STRINGS CAN BE "
1009 M$=M$+"RANKED AS TO MERIT (EITHER ANALYTICALLY, BY EXPERIMENT OR BY JUDGEMENT).":GOSUB1
1010 M$="THE INITIAL FAMILY OF STRINGS CAN BE BASED ON ESTIMATE OR CAN BE COMPLETELY RANDOM.":GOSUB1
1011 M$="THE PROCEDURE STARTS WITH ASSIGNING A MERIT FACTOR TO EACH STRING.":GOSUB1
1012 M$="THE NEXT STEP IS TO PLACE THE STRINGS IN ORDER OF MERIT.":GOSUB1
1013 M$="PAIRS OF STRINGS FROM THE TOP OF THE LIST ARE THEN 'MATED' TO PRODUCE OFFSPRING WHICH "
1014 M$=M$+"PEPLACE STRINGS AT THE BOTTOM OF THE LIST.":GOSUB1
1015 M$="THE STRINGS ARE ANALOGOUS TO CHROMOSOMES AND THE PROCESS IS ANALOGOUS TO SELECTIVE BREEDING.":GOSUB1
1016 M$="THE MATING CAN CONVENIENTLY BE ACCOMPLISHED BY CUTTING A PAIR AT A RANDOM LOCATION "
1017 M$=M$+"AND SPLICING THE HEADS AND TAILS TO FORM TWO NEW STRINGS "
1018 M$=M$+"(WHICH EACH CONTAIN ATTRIBUTES OF BOTH PARENTS.":GOSUB1
1019 M$="IT IS HELPFUL TO THE EVOLUTIONARY PROCESS TO CREATE RANDOM MUTATIONS, AS BY RANDOMLY "
1020 M$=M$+"SELECTING HALF THE POPULATION FOR ALTERATION OF A RANDOMLY CHOSEN ELEMENT OF THE STRING.":GOSUB1
1021 M$="THE POPULATION WITH ITS NEW MEMBERS AND MUTATIONS IS RECYCLED THROUGH THE SAME STEPS, "
1022 M$=M$+"STARTING WITH RATING AND RANKING. ACH CYCLE IS TERMED A GENERATION.":GOSUB1
1023 M$="IN THIS EXAMPLE EIGHT STRINGS OF 14 1'S AND 0'S CONSTITUTE THE POPULATION.":GOSUB1
1024 M$="THE ODD POSITIONS IN A STRING ARE THE BINARY EXPRESSION OF THE X COORDINATE OF A POINT "
1025 M$=M$+"AND THE EVEN POSITIONS DEFINE THE Y COORDINATE.":GOSUB1
1026 M$="A TARGET IS DEFINED AS THE POINT AT THE CENTER OF THE SCREEN.":GOSUB1
1027 M$="THE STRINGS APE RATED ACCORDING TO THE DISTANCE OF THE POINT THEY DEFINE FROM THIS TARGET.":GOSUB1
1028 M$="ARRAY C CONTAINS 8 STRINGS AND ARRAY A IS USED TO BUFFER ARRAY C DURING BREEDING.":GOSUB1
1029 M$="ARRAY B CONTAINS THE VALUES OF THE POSITIONS OF A BINARY NUMBER.":GOSUB1
1030 M$="ARRAY D CONTAINS THE DISTANCE OF EACH POINT FROM THE TARGET.":GOSUB1
1031 M$="ARRAY R CONTAINS THE SERIAL NUMBERS OF THE STRINGS IN RANK OPDER.":GOSUB1
1032 M$="ARRAYS X AND Y STORE THE X AND Y COORDINATES DEFINED BY EACH STRING.":GOSUB1
1033 M$="ARRAYS W AND Z BUFFER ARRAYS X AND Y.":GOSUB1
1034 M$="LINES 0-90: GET ORGANIZED.":GOSUB1
1035 M$="LINES 900-990: INITIALIZE.":GOSUB1
1036 M$="LINES 100-180: TRANSLATE STRING TO X AND Y COORDINATE.":GOSUB1
1037 M$="LINES 200-290: PLOT THE POINTS.":GOSUB1
1038 M$="LINES 300-330: CALCULATED DISTANCES OF POINTS FROM TARGET.":GOSUB1
1039 M$="LINES 400-490: ESTABLISH RANK ORDER.":GOSUB1
1040 M$="LINES 500-570: MUTATE AND COPY STRINGS INTO ARRAY 'A' IN RANK ORDER.":GOSUB1
1041 M$="LINES 600-660: COPY TOP (BEST) 4 STRINGS INTO ARRAY.":GOSUB1
1042 M$="LINES 700-850: CUT AND SPLICE COPIES OF THE BEST 4 TO REPLACE THE WORST 4.":GOSUB1
1043 M$="THIS PROGRAM WAS WRITTEN TO ILLUSTRATE THE PROCESS.":GOSUB1
1044 M$="SECTION 200 CAN BE MERGED WITH SECTION 100 TO ELIMINATE ARRAY S W AND Z, FOR INSTANCE.":GOSUB1
1045 M$="IT IS INTERESTING TO EXPLORE POPULATION SIZES, MATING PPOCESSES AND MUTATION RATES.":GOSUB1
1446 M$="20 GENERATIONS SEEMS TO BE A MAGIC NUMBER FOR APPROACHING DIMINISHING RETURNS.":GOSUB1
1047 M$="THE STRING ELEMENTS COULD BE SYMBOLS CODING A MUSICAL PIECE OR OTHER ARTISTIC COMPOSITION "
1048 M$=M$+"WITH THE RANKING BEING BASED ON ESTHETIC MERIT.":GOSUB1
1049 M$="THE STRING ELEMENTS COULD ALSO REPRESENT CONTROL SETTINGS FOR A CHEMICAL PROCESSING PLANT.":GOSUB1
1050 M$="THE RANKING COULD BE PROVIDED BY MEASUREMENTS OF PLANT GROWTH OR BY ANIMAL ACTIVITY "
1051 M$=M$+"RECORDED WITH APPROPRIATE SENSORS.":GOSUB1:PRINT
1052 M$="PLEASE CONTACT ART RADCLIFFE IN ANN ARBOR AND SHARE YOUR EXPERIENCE, "
1053 M$=M$+"PROBLEMS AND RESULTS, WITH THIS SYSTEM. THANX.":GOSUB1
1054 GOTO900
