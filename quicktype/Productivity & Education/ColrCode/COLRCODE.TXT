10 GOSUB 490
20 GOSUB 470
30 PRINT "COLOUR CODES HELPER"
40 PRINT:PRINT "A. COLOUR CODES LIST"
50 PRINT "B. VALUE TO COLOURS"
60 PRINT "C. COLOURS TO VALUE"
70 PRINT:PRINT "TYPE IN A,B OR C"
80 INPUT I$
90 IF I$="A" THEN GOSUB 130
100 IF I$="B" THEN GOSUB 180
110 IF I$="C" THEN GOSUB 320
120 GOTO 20
130 GOSUB 470
140 PRINT "COLOUR CODES ARE :":PRINT
150 FOR I=1 TO 10
160 PRINT TAB(2);C$(I);TAB(14);I-1
170 NEXT I:GOSUB 450:RETURN
180 GOSUB 470
190 PRINT "WHAT VALUE":INPUT V
200 PRINT "WHAT MULTIPLIER":PRINT "M=MEGOHMS"
210 PRINT "K=KILOHMS":PRINT "O=OHMS"
220 INPUT M$
230 IF M$="M" THEN LET V=V*1E6
240 IF M$="K" THEN LET V=V*1E3
250 LET NZ=0
260 IF V>=100 THEN LET V=V/10:LET NZ=NZ+1:GOTO 260
270 LET V=INT(V):PRINT:PRINT "COLOUR CODING IS"
280 PRINT C$(INT(V/10)+1);" ";
290 PRINT C$((V/10-INT(V/10))*10+1.1);" ";
300 PRINT C$(NZ+1):GOSUB 450
310 RETURN
320 GOSUB 470
330 FOR I=1 TO 3
340 PRINT "TYPE COLOUR ";I:INPUT I$
350 LET F=0:FOR J=1 TO 10
360 IF I$=C$(J) THEN LET F=1:LET C(I)=J-1
370 NEXT J:IF F=0 THEN PRINT "CHECK SPELLING":GOTO 340
380 NEXT I
390 LET V=(C(2)+10*C(1))*10^C(3):LET M$=""
400 IF V>=1E6 THEN LET V=V/1E6:LET M$="MEG"
410 IF V>=1E3 AND M$<>"MEG" THEN LET V=V/1E3:LET M$="KIL"
420 PRINT:PRINT "VALUE IS ";V;" ";M$;"OHMS"
430 GOSUB 450
440 RETURN
450 PRINT:PRINT "PRESS RETURN TO CONTINUE"
460 INPUT X$:RETURN
470 CLS
480 PRINT:RETURN
490 DIM C$(10):DIM C(3)
500 FOR I=1 TO 10:READ C$(I):NEXT I
510 RETURN
520 DATA "BLACK","BROWN","RED"
530 DATA "ORANGE","YELLOW","GREEN"
540 DATA "BLUE","PURPLE","GREY"
550 DATA "WHITE"