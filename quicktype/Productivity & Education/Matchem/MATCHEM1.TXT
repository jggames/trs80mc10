10 REM MATCHEM
20 REM COPYRIGHT FRED HARRIS
25 REM TANDY/DRAGON VERSION: MIKE MOORE
30 REM******************************
45 REM INITIALISE
50 GOSUB 1000
60 REM******************************
105 REM CHOOSE FIRST CARD
110 GOSUB 2000
120 FG=I:N1=N:M1=M
124 REM SHOW CARD
125 GOSUB 3000
130 REM*****************************
135 REM CHOOSE SECOND CARD
140 GOSUB 2000
150 IF I=FG THEN 140
152 REM*****************************
154 REM SHOW CARD
155 GOSUB 3000
170 G=G+1
182 FOR T=I TO DE:NEXT T
183 REM*****************************
185 REM CHECK FOR MATCH
190 GOSUB 4000
200 IF MATCH=1 THEN GOSUB 5000
205 REM IF CARDS DO NOT MATCH
210 IF MATCH=0 THEN GOSUB 6000
230 PRINT@480,"TRIES";G;" SCORE";S;
250 IF S<10 THEN 110
260 REM*****************************
270 PRINT@416,"RATING"INT(1000/G+.5)
280 PRINT@448,"DO WISH TO TRY AGAIN (Y/N)?"
290 C$=INKEY$:IFC$=""THEN290
300 IFC$="Y"THENRUN
310 IFC$="N"THENEND
320 GOTO290
330 REM **SUBROUTINES**
990 REM INITIALISE ROUTINE
1000 G=RND(-(PEEK(9)*256+PEEK(10))):CLS0:PRINT@480,"                      *MATCHEM*";:POKE16384+511,96
1010 G=0
1020 A$="AABBCCDDEEFFGGHHIIJJ"
1030 J$=""
1035 DE=2500
1040 REM****************************
1050 REM SHUFFLE
1055 FOR K=1 TO 20
1060 L=LEN(A$)
1080 N=RND(L)
1090 J$=J$+MID$(A$,N,1)
1100 A$=LEFT$(A$,N-1)+RIGHT$(A$,L-N)
1105 NEXT K
1110 REM****************************
1120 REM DISPLAY BACKS
1130 FOR N=0 TO 4
1140 FOR M=0 TO 3
1150 V=N+5*M+1:PRINT@(N*4+5)+((3*M)*32),LEFT$(STR$(V)+"  ",3);:PRINT@(N*4+5)+((3*M)*32)+32,"   ";
1160 NEXT M
1170 NEXT N
1180 RETURN
1190 REM****************************
1990 REM CHOOSE A CARD
2000 PRINT@416,""
2007 PRINT@416,;
2010 INPUT I$:I=VAL(I$)
2015 I=INT(I)
2020 IF I<1 OR I>20 THEN 2000
2022 C$=MID$(J$,I,1):REM FIND CARD
2023 IF C$="-" THEN 2000
2025 PRINT@416,""
2040 M=INT((I-1)/5)
2050 N=I-5*M-1
2070 RETURN
2090 REM****************************
2990 REM SHOW CARD
3000 IF C$="A" THEN X$="++":Y$="++"
3010 IF C$="B" THEN X$="%%":Y$="%%"
3020 IF C$="C" THEN X$="$$":Y$="$$"
3030 IF C$="D" THEN X$="II":Y$="II"
3040 IF C$="E" THEN X$="@@":Y$="@@"
3050 IF C$="F" THEN X$="XX":Y$="XX"
3060 IF C$="G" THEN X$="**":Y$="**"
3070 IF C$="H" THEN X$=")(":Y$=")("
3080 IF C$="I" THEN X$="==":Y$="=="
3090 IF C$="J" THEN X$="##":Y$="##"
3095 PRINT@((5+N*4)+(3*M)*32)," "X$;
3100 PRINT@((5+N*4)+(3*M+1)*32)," "Y$;
3110 RETURN
3120 REM****************************
3990 REM CHECK FOR MATCH.
4000 MATCH=0
4010 IF MID$(J$,FG,1)=MID$(J$,I,1) THEN MATCH=1
4020 RETURN
4030 REM****************************
4990 REM IF CARDS DO MATCH
5000 J$=LEFT$(J$,FG-1)+"-"+RIGHT$(J$,LEN(J$)-FG)
5010 J$=LEFT$(J$,I-1)+"-"+RIGHT$(J$,LEN(J$)-I)
5020 S=S+1
5030 FORC1=50TO150STEP10:SOUNDC1,1:NEXT
5040 RETURN
5050 REM****************************
5990 REM IF CARDS DO NOT MATCH
6000 SOUND1,5
6010 PRINT@(5+N1*4+(3*M1)*32),LEFT$(STR$(FG)+"  ",3);
6020 PRINT@(5+N1*4+(3*M1+1)*32),"   ";
6030 PRINT@(5+N*4+(3*M)*32),LEFT$(STR$(I)+"  ",3);
6040 PRINT@(5+N*4+(3*M+1)*32),"   ";
6080 RETURN
