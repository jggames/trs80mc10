0 CLEAR1000:DIMG(20,30),K(255),A,B,C,D,E,F,G,H,X,Y,Z,G$(20,30),I$,N$,FN$
5 GOSUB1000:GOSUB2000:GOSUB100:GOTO200
6 N$=N$+I$:G(E+A,F+B)=VAL(N$):PRINT@(32*F)+(E*9)+36,G(E+A,F+B);:RETURN
7 PRINT@480,"WAIT...";:GOSUB2000:GOSUB100:RETURN
8 H=LEN(N$):IFH>0THENN$=LEFT$(N$,H-1):G(E+A,F+B)=VAL(N$):PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
9 RETURN
100 CLS:C=3:X=A:PRINT@C,X;:PRINT@C+9,X+1;:PRINT@C+18,X+2:C=C+1
110 FORY=BTOB+13:C=C+32:PRINT@C-4,Y;
120 FORD=0TO2:IFG$(X+D,Y)<>""THENPRINT@C+(9*D),G$(X+D,Y);:GOTO140
130 PRINT@C+(9*D),G(X+D,Y);
140 NEXT:NEXT:RETURN
200 FORC=1TO65000:FORT=1TO25:PRINT@(32*F)+(E*9)+36,CHR$(128);
210 I$=INKEY$:IFI$<>""THENT=25:C=65000:GOTO250
220 NEXT
230 FORT=1TO15:IFG$(E+A,F+B)<>""THENPRINT@(32*F)+(E*9)+36,G$(E+A,F+B);:GOTO240
235 PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
240 I$=INKEY$:IFI$<>""THENT=15:C=65000
250 NEXT:NEXT:IFG$(E+A,F+B)<>""THENPRINT@(32*F)+(E*9)+36,G$(E+A,F+B);:GOTO260
255 PRINT@(32*F)+(E*9)+36,G(E+A,F+B);
260 ONK(ASC(I$+" "))GOSUB300,310,320,330,6,6,6,6,6,6,6,6,6,6,6,6,7,8,600,700
290 GOTO200
300 IFF-1=-1THENIFB>1THENB=B-1:GOSUB100
301 IFF>0THENF=F-1
305 N$="":RETURN
310 IFE-1=-1THENIFA>1THENA=A-1:GOSUB100
311 IFE>0THENE=E-1
315 N$="":RETURN
320 IFE+1=3THENIFA<18THENA=A+1:GOSUB100
321 IFE<2THENE=E+1
325 N$="":RETURN
330 IFF+1=14THENIFB<17THENB=B+1:GOSUB100
331 IFF<13THENF=F+1
335 N$="":RETURN
600 CLS:INPUT"FILENAME TO SAVE";FN$
610 IFFN$<>""THENCSAVE*G,FN$
620 GOSUB100:RETURN
700 CLS:INPUT"FILENAME TO LOAD";FN$
720 IFFN$<>""THENCLOAD*G,FN$
730 GOSUB100:RETURN
999 GOTO 999
1000 K(87)=1:K(65)=2:K(83)=3:K(90)=4:K(79)=19:K(76)=20
1005 K(48)=5:K(49)=6:K(50)=7:K(51)=8:K(52)=9:K(53)=10:K(54)=11:K(55)=12:K(56)=13:K(57)=14:K(45)=15:K(46)=16:K(13)=17:K(8)=18
1010 X=1:A=1:B=1:C=0:E=0:F=0
1090 RETURN
1999 REM PLACE INSTRUCTIONS HERE
2000 DATA JAN,FEB,MAR,APR,MAY,JUN,JUL,AUG,SEP,OCT,NOV,DEC
2001 RESTORE:FORX=1TO12:READG$(X,1):NEXT
2010 FORX=1TO12:G(X,14)=0:FORY=2TO13:G(X,14)=G(X,14)+G(X,Y):NEXTY,X
10000 RETURN
