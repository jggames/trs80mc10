0 DIMCC,C1,T,M$,MC:MC=16384:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
5 C1=(PEEK(17024)AND1)*256+PEEK(17025):PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1
6 FORT=C1+MCTOCC+MC:POKET,PEEK(T)-64:NEXT:RETURN
10 L=INT(RND(0)*3+5)
20 P=INT(RND(0)*60+40)
30 M=INT(RND(0)*50+10)*P
40 FP=INT(RND(0)*40+80)
50 CE=INT(RND(0)*40+80)
60 C=0
70 S=1
80 Y=1
90 LP=INT(RND(0)*2000+2000)
100 CP=INT(RND(0)*12+7)
110 CLS
120 M$="YEAR:":GOSUB5:PRINTY
130 PRINT
140 M$="THERE ARE"+STR$(P)+" WORKERS IN THE COLONY.":GOSUB1
150 M$="YOU HAVE"+STR$(L)+" MINES, AND $"+STR$(M):GOSUB1
160 M$="SATISFACTION FACTOR:":GOSUB5:PRINT S
180 M$="YOUR MINES PRODUCED"+STR$(CE)+" TONS EACH.":GOSUB1
190 C=C+CE*L
200 PRINT"ORE IN STORE=";C;"TONS"
210 M$="SELLING:":GOSUB5:PRINT
220 PRINT"ORE SELLING PRICE=";CP
230 M$="MINES SELLING PRICE="+STR$(LP)+"/MINE":GOSUB1
231 PRINT
240 INPUT"HOW MUCH ORE TO SELL";CS
260 IFCS<0ORCS>CTHEN240
280 M=M+CS*CP
290 INPUT"HOW MANY MINES TO SELL";LS
310 IFLS<0ORLS>LTHEN290
320 L=L-LS
330 M=M+LS*LP
340 CLS
349 M$="YEAR:":GOSUB5:PRINTY
350 PRINT"YOU HAVE $";M
360 PRINT"YOU HAVE";P;"WORKERS."
361 PRINT"YOU HAVE";L;"MINES LEFT"
362 M$="SATISFACTION FACTOR:":GOSUB5:PRINTS
363 PRINT"$ ";LP;"/PER MINE"
370 M$="BUYING:":GOSUB5:PRINT
380 PRINT"HOW MUCH TO SPEND ON FOOD?":INPUTFB
400 IFFB<0ORFB>MTHEN380
410 M=M-FB
420 IFFB/P>100THENS=S+.1
430 IFFB/P<80THENS=S-.2
440 PRINT"HOW MANY MORE MINES TO BUY?":INPUTLB
460 IFLB<0ORLB*LP>MTHEN440
470 L=L+LB
480 M=M-LB*LP
490 IFS<.6THEN660
500 IFS>1.1THENCE=CE+INT(RND(0)*20+1)
510 IFS<.9THENCE=CE-INT(RND(0)*20+1)
520 IFP/L<10THEN680
530 IFS>1.1THENP=P+INT(RND(0)*10+1)
540 IFS<.9THENP=P-INT(RND(0)*10+1)
550 IFP<30THEN700
560 IFRND(0)>.01THEN590
570 M$="RADIOACTIVE LEAK....MANY DIE.":GOSUB1
580 P=INT(P/2)
590 IFCE<150THEN620
600 M$="MARKET GLUT - PRICE DROPS.":GOSUB1
610 CE=INT(CE/2)
620 Y=Y+1:FORZ=1TO2000:NEXT
630 IFY<11THEN90
640 M$="YOU'VE SURVIVED YOUR TERM OF OFFICE.":GOSUB1
650 END
660 PRINT"THE WORKERS REVOLTED."
670 END
680 M$="YOU'VE OVERWORKED EVERYONE.":GOSUB1
690 END
700 PRINT"NOT ENOUGH PEOPLE LEFT."
710 END
