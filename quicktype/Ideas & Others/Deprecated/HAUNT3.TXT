0 REM *** HAUNTED HOUSE
1 GOTO10
2 SET(X,Y,3):SET(X+1,Y,3):SET(X-1,Y,3):SET(X,Y-1,3):SET(X,Y+1,3):SET(X-1,Y+2,3):SET(X+1,Y+2,3):RETURN
3 RESET(X,Y):RESET(X+1,Y):RESET(X-1,Y):RESET(X,Y-1):RESET(X,Y+1):RESET(X-1,Y+2):RESET(X+1,Y+2):RETURN
10 CLEAR250:CLS:GOSUB4000:PRINT@170,"HAUNTED HOUSE":PRINT@238,"BY:":PRINT@265,"DARREN DELOACH"
11 PRINT@302,"AND":PRINT@331,"TIM KOONCE";:PRINT@331+24+32,"MC-10 EDITS BY JIM GERRIE";:FORT=1TO2000:NEXT:GOTO2770
20 DIMLV$(21),X,Y,IK,UO,OV,P,KN,AM,FO,KY,RP,SW,PC,G1,G2,G3,A1,A2,SN,OP,Q$,V$
21 X=57:Y=12:OV=18:UO=62:A=3:P=1:SC=3:KN=3:BU=3:AM=3:FO=3:KY=3:RP=3:SW=3:PC=3:G1=3:G2=3:G3=3:A1=3:A2=3:A3=3:SN=3:Q$="MOVE"
22 V$=S2$:OP=3
25 GOSUB3000
30 CLS0:PRINT@320,S3$:P=1:R=0:PL=0:PRINT@320,"YOU ARE STANDING IN FRONT OF AN OLD HOUSE":GOSUB1170:GOSUB2:FORA=1TO1000:NEXTA
31 PRINT@129,"p";:PRINT@416,"THERE IS A CRUMPLED PIECE OF    PAPER AT 'P'."
40 GOSUB1183:GOTO1260
50 R=0:GOSUB1050:IFR=1THEN1250
51 IFX=3ANDY=9ANDOP=3THEN60
52 GOTO50
60 IFX=3ANDY=9THENPRINT@320,V$:PRINT@320,"YOU PICKED UP THE PAPER.":OP=1:RETURN
90 CLS0:OV=18:IFFO=3THENPRINT@320,"INSTANTLY YOU MATERIALIZE INSIDETHE FOYER";S1$:FO=1:GOTO110
100 CLS0:PRINT@320,"YOU ARE NOW IN THE FOYER.";S4$;
110 P=2:GOSUB2:GOSUB1170
120 R=0:GOSUB1181:GOSUB1182:GOSUB1184:RETURN
130 R=0
140 GOSUB1050:IFR=1THEN150
141 GOTO140
150 RETURN
170 CLS0:PRINT@320,"YOU ARE NOW IN THE LIVING ROOM.";S3$:IFSC=3THENPRINT@249,"s";:PRINT@448,"THERE IS A MAGIC SCROLL AT 'S'";
190 IFKN=3THENPRINT@97,"k";:PRINT@416,"A KNIFE IS LEVITATING AT 'K'.";
200 P=3:GOSUB2:GOSUB1170:GOSUB1181:GOSUB1182
210 RETURN
220 R=0:GOSUB1050:IF(X=51ANDY=15)ANDSC=3THENPRINT@320,"YOU PICKED UP THE MAGIC SCROLL.":SC=1:RETURN
230 IF(X=3ANDY=6)ANDKN=3THEN2670
235 IFKN=3THENPRINT@97,"k";
240 IFR=1THENRETURN
241 GOTO220
260 CLS0:GOSUB1170:GOSUB1181:GOSUB1184:PRINT@320,"YOU ARE NOW IN THE DINING ROOM.";S1$:P=4:GOSUB2:R=0:RETURN
290 R=0:GOSUB1050:IFR=1THENRETURN
291 GOTO290
310 CLS0:GOSUB1170:GOSUB1181:GOSUB1183:GOSUB1184:PRINT@320,"YOU ARE NOW IN THE KITCHEN.";S1$
320 IFKN<>1THEN2710
330 IFKN=1ANDAM<>1THENGOSUB2730
340 IFBU=3THENPRINT@51,"b";:PRINT@416,"THERE'S A BUCKET OF WATER AT'B'."
350 P=5:GOSUB2:RETURN
360 R=0:GOSUB1050:IF(X=39ANDY=3)ANDBU=3THENPRINT@320,"YOU PICKED UP THE BUCKET.":BU=1:RETURN
370 IFR=1THENRETURN
371 GOTO360
390 CLS0:PRINT@320,"YOU ARE NOW IN THE DEN.";S1$:P=6:GOSUB2:GOSUB1170:GOSUB1182:GOSUB1183:RETURN
400 R=0:GOSUB1050:IFR=1THENRETURN
401 GOTO400
420 CLS0:PRINT@320,"YOU'RE NOW IN THE BREAKFAST RM.";S1$:P=7:GOSUB2:GOSUB1170:GOSUB1182:GOSUB1183:RETURN
430 R=0:GOSUB1050:IFR=1THENRETURN
431 GOTO430
450 CLS0:PRINT@320,"YOU'RE NOW IN THE SERVANT'S QTRS";S1$:P=8:GOSUB2:PRINT@416,"THERE IS A CABINET ON THE SOUTH WALL."
451 FORA=28TO35:SET(A,16,3):NEXT:SET(27,17,3):SET(36,17,3):GOSUB1170:GOSUB1181:RETURN
460 R=0:GOSUB1050:IF(Y>10ANDKY=3)THENPRINT@416,"YOU SEE A KEY AT 'K'."CHR$(13):KY=2:PRINT@225,"k";
470 IF(X=3ANDY=15)ANDKY=2THENPRINT@320,"YOU PICKED UP THE KEY.":KY=1:RETURN
480 IFR=1THENRETURN
481 GOTO460
500 Y=9:CLS0:PRINT@320,"YOU'RE AT THE EAST END OF A HALL";S1$:P=9:GOSUB2:GOSUB1180:FORA=4TO9:RESET(63,A):RESET(0,A):NEXT
501 GOSUB1183:OV=12:RETURN
510 R=0:GOSUB1050:IFR=1THENRETURN
511 GOTO510
530 OV=18:P=10:CLS0:PRINT@320,"YOU'RE IN THE VIOLET BEDROOM.";S1$:PRINT@416,"THERE IS A COLORED PANEL IN THE WEST WALL."
531 GOSUB2:GOSUB1170:GOSUB1184:FORA=7TO12:SET(1,A,7):NEXT:RETURN
540 R=0:GOSUB1050:IFR=1THENRETURN
541 GOTO540
560 OV=18:P=12:CLS0:PRINT@320,"YOU ARE NOW IN THE BLUE BEDROOM.";S1$:PRINT@416,"THERE IS A COLORED PANEL IN THE EAST WALL."
561 GOSUB2:GOSUB1170:GOSUB1184:FORA=7TO12:SET(62,A,6):NEXT:RETURN
570 R=0:GOSUB1050:IFR=1THENRETURN
571 GOTO570
590 CLS0:UO=62:OV=12:Y=9:PRINT@320,"YOU'RE AT THE WEST END OF HALL.";S1$:PRINT@416,"A LARGE DOOR BARS THE WAY SOUTH."
591 GOSUB2:P=13:GOSUB1180:FORA=27TO35:RESET(A,0):SET(A,12,5):NEXT:FORA=4TO9:RESET(63,A):NEXT:RETURN
600 R=0:GOSUB1050:IFR=1THENRETURN
601 GOTO600
620 P=14:X=12:OV=18:UO=38:CLS0:PRINT@320,"YOU'RE IN THE MASTER BEDROOM.";S1$:PRINT@416,"A WALL OF RAGING FIRE BLOCKS THE";
621 PRINT"WAY EAST.":GOSUB1170:GOSUB1183:GOSUB1182:GOSUB2:FORA=40TO41:FORT=1TO17:SET(A,T,4):NEXTT,A
630 PRINT@21,CHR$(189);CHR$(190);CHR$(179);:PRINT@53,CHR$(187);CHR$(189);CHR$(189)CHR$(184);
631 PRINT@85,CHR$(188);CHR$(190);CHR$(188);:PRINT@117,CHR$(189);CHR$(182);CHR$(179);
632 PRINT@149,CHR$(191);CHR$(189);CHR$(188);CHR$(184);:PRINT@181,CHR$(182);CHR$(187);CHR$(180);
640 PRINT@213,CHR$(183);CHR$(183);CHR$(187);:PRINT@245,CHR$(190);CHR$(187);:PRINT@277,CHR$(183);CHR$(185);CHR$(190);:RETURN
650 RETURN
660 R=0:GOSUB1050:IFR=1THENRETURN
661 GOTO660
680 CLS0:P=15:OV=18:UO=62:PRINT@320,"YOU ARE NOW IN THE LIBRARY.";S1$:PRINT@416,"THERE IS A LARGE HOLE IN THE    CEILING."
681 GOSUB1170:GOSUB1181:GOSUB2:RETURN
690 R=0:GOSUB1050:IFR=1THENRETURN
691 GOTO690
710 CLS0:PRINT@320,"YOU'RE IN A DIM ROOM WITH A HOLEIN THE FLOOR.";S1$
711 IFSW=3THENPRINT@251,"s";:PRINT@416,"THERE IS A MAGIC SWORD AT 'S'."
720 P=16:GOSUB2:GOSUB1170:GOSUB1181:GOSUB1182:GOSUB1184
730 IFPC=3THENGOSUB5000
740 RETURN
750 R=0:GOSUB1050:IFSW=3AND(X=54ANDY=15)THENPRINT@320,"YOU PICKED UP THE MAGIC SWORD.":SW=1:RETURN
760 IFR=1THENRETURN
761 GOTO750
780 CLS0:PRINT@320,"YOU'RE IN A DIMLY LIT ROOM.";S1$:P=17:GOSUB1170:GOSUB1183
781 IFG1=3THENGOSUB2920:PRINT@416,"THERE IS A GHOST HERE.":GOSUB3:Y=3:GOSUB2:RETURN
790 IFG1=2THENPRINT@416,"THERE IS A BODY OF A DEAD GHOST.":GOSUB3:Y=3:GOSUB2:GOSUB2930:RETURN
800 R=0:GOSUB1050:IF(X>20ANDX<36)AND(Y>3ANDY<12)THENGOSUB3:X=60
810 GOSUB2:IFR=1THENRETURN
811 GOTO800
830 CLS0:PRINT@320,"YOU ARE IN A DIMLY LIT ROOM.";S1$:P=18:GOSUB1170:GOSUB1181
831 IFG2=3THENGOSUB2920:PRINT@416,"THERE IS A GHOST HERE.":GOSUB3:X=6:GOSUB2:RETURN
840 IFG2=2THENPRINT@416,"THERE IS A BODY OF A DEAD GHOST.":GOSUB3:X=6:GOSUB2:GOSUB2930:RETURN
850 R=0:GOSUB1050:IF(X>20ANDX<36)AND(Y>3ANDY<12)THENGOSUB3:Y=15
860 GOSUB2:IFR=1THENRETURN
861 GOTO850
880 CLS0:PRINT@320,"YOU ARE IN A DIMLY LIT ROOM.";S1$:P=19:GOSUB1170:GOSUB1181:GOSUB1182
881 IFG3=3THENGOSUB2920:PRINT@416,"THERE IS A GHOST HERE.":GOSUB3:X=60:GOSUB2:RETURN
890 IFG3=2THENPRINT@416,"THERE IS A BODY OF A DEAD GHOST.":GOSUB3:X=60:GOSUB2:GOSUB2930:RETURN
900 R=0:GOSUB1050:IF(X>20ANDX<36)AND(Y>3ANDY<12)THENGOSUB3:X=60
910 GOSUB2:IFR=1THENRETURN
911 GOTO900
930 A1=3:A2=3:A3=3:CLS0:PRINT@320,"YOU ARE IN A DIMLY LIT ROOM.";S1$:P=20:GOSUB1170:GOSUB1182:GOSUB1184
931 PRINT@416,"THERE IS A GHOST HERE.":GOSUB2920:Y=3:GOSUB2:RETURN
940 R=0:GOSUB1050:IF(X>20ANDX<36)AND(Y>3ANDY<12)THENGOSUB3:X=6
950 IF(X>20ANDX<50)ANDY<4THENA1=1
960 IF(X<21ANDY<10)ANDA1=1THENA2=1
970 IF(X<21ANDY>9)ANDA2=1THENA3=1
980 GOSUB2:IFR=1THENRETURN
981 GOTO940
1010 CLS0:PRINT@320,"YOU ARE IN A DIMLY LIT ROOM.";S1$:P=21:GOSUB1170:GOSUB1181:GOSUB1182:GOSUB1183:GOSUB1184
1011 IFSN=3THENPRINT@416,"THERE IS A SIGN IN THE CORNER OFTHE ROOM AT 'S'.":PRINT@33,"s";
1015 GOSUB2:RETURN
1030 R=0:GOSUB1050:IF(X=3ANDY=3)ANDSN=3THENPRINT@320,"YOU PICKED UP THE SIGN.":SN=1:RETURN
1040 IFR=1THENRETURN
1041 GOTO1030
1050 PRINT@320,V$:PRINT@330,"DIRECTION";
1056 IK=PEEK(17023)ANDPEEK(2):IFIK<>65ANDIK<>87ANDIK<>83ANDIK<>90ANDIK<>13THEN1056
1060 GOSUB3:IFIK=87THENY=Y-3
1070 IFY=0THENY=Y+3
1080 IFIK=90THENY=Y+3
1090 IFY>=OV THENY=Y-3
1100 IFIK=83THENX=X+3
1110 IFX>=UO THENX=X-3
1120 IFIK=65THENX=X-3
1130 IFX=0THENX=X+3
1140 IFIK=13THENR=1
1150 IFP>16ANDP<21THENRETURN
1160 GOSUB2:RETURN
1170 FORA=0TO63:SET(A,0,1):SET(A,18,1):NEXT:FORA=0TO18:SET(0,A,1):SET(63,A,1):NEXT:RETURN
1180 FORA=0TO63:SET(A,0,1):SET(A,12,1):NEXTA:FORA=0TO12:SET(0,A,1):SET(63,A,1):NEXTA:RETURN
1181 FORA=6TO10:RESET(0,A):NEXT:RETURN
1182 FORA=6TO10:RESET(63,A):NEXT:RETURN
1183 FORA=27TO35:RESET(A,0):NEXT:RETURN
1184 FORA=27TO35:RESET(A,18):NEXT:RETURN
1200 IFP<>10ANDP<>12THEN1250
1210 OV=12:CLS0:P=11:Y=9:GOSUB1180:GOSUB2:FORA=6TO11:RESET(0,A):RESET(63,A):NEXT
1211 PRINT@320,"YOU'RE NOW IN THE SECRET PASSAGE";S1$:IFRP=3THENPRINT@416,"THERE IS A ROPE AT 'R'.":PRINT@33,"r";
1215 RETURN
1230 R=0:GOSUB1050:IF(X=3ANDY=3)ANDRP=3THENPRINT@320,"YOU PICKED UP THE ROPE.":RP=1:RETURN
1240 IFR=1THENRETURN
1241 GOTO1230
1250 Q$="":PRINT@352,V$:PRINT@352,"COMMAND";:INPUTQ$
1255 PRINT@352,V$
1260 IFLEFT$(Q$,4)="READ"THEN1400
1261 IFLEFT$(Q$,4)="DROP"THEN1550
1262 IFLEFT$(Q$,3)="SAY"THEN1650
1263 IFLEFT$(Q$,4)="MOVE"THENONPGOSUB50,130,220,290,360,400,430,460,510,540,1230,570,600,660,690,750,800,850,900,940,1030:A=1
1300 IFLEFT$(Q$,5)="LEAVE"ORLEFT$(Q$,4)="EXIT"THEN1690
1301 IFLEFT$(Q$,6)="INVENT"THEN2060
1302 IFLEFT$(Q$,4)="OPEN"THEN1369
1303 IFQ$="CLIMB ROPE"THEN2760
1350 IFLEFT$(Q$,5)="DRINK"THEN1380
1351 IFQ$="HIT GHOST"ANDP>16THENGOSUB2950:A=1:GOTO1360
1352 IFQ$="THROW BUCKET"THEN1375
1360 IFQ$="JUMP HOLE"ANDP=16THENPRINT@320,V$:PRINT@320,"YOU FELL DOWN THE HOLE AND BROKEYOUR NECK!!";:END
1366 IFQ$="HIT SWORD"THENPRINT@416,"OUCH!!! YOU HURT YOUR HAND!!":A=1
1367 IFA<>1THENPRINT@416,V$:PRINT@416,"I DON'T UNDERSTAND."
1368 A=3:GOTO1250
1369 W$=RIGHT$(Q$,5):IFW$=" DOOR"THEN2370
1370 IFW$="PANEL"THENGOSUB1200:GOTO1250
1371 IFW$="BINET"ANDP=8THENRESET(30,16):RESET(33,16):PRINT@416,V$:PRINT@416,"IT'S EMPTY.":GOTO1250
1372 GOTO1250
1373 PRINT@416,"I DON'T UNDERSTAND.":GOTO1250
1375 PRINT@416,V$:PRINT@416,"THE BUCKET MAGICALLY REFILLS!":GOTO1250
1380 IFBU=1THEN6000
1390 PRINT@416,V$:PRINT@416,"YOU DON'T HAVE ONE.":GOTO1250
1400 W$=RIGHT$(Q$,6):IFW$=" PAPER"THEN1470
1410 IFW$="SCROLL"THEN1500
1411 IFW$=" SWORD"THEN1530
1412 IFW$="D SIGN"THEN1450
1440 GOTO1373
1450 IFSN=3THEN1480
1451 PRINT@416,V$:PRINT@320,"THE SIGN SAYS,--'THERE ARE THREEEXITS FROM THIS ROOM, ONLY ONE  IS TRUE.  KNOW, BUT BE NOT BUR-"
1452 PRINT"DENED BY THIS CLUE.!!":FORA=1TO2000:A$=INKEY$:IFA$=""THENNEXT
1455 PRINT@320,S3$:GOTO1250
1470 IFOP=1THENPRINT@416,V$:PRINT@416,"THE PAPER SAYS, 'MAGIC WORD-----PLUGH'.":GOTO1250
1480 PRINT@416,V$:PRINT@416,"YOU DON'T HAVE ONE."
1490 GOTO1250
1500 IFSC=3THENPRINT@416,V$:PRINT@416,"YOU DON'T HAVE ONE.":GOTO1250
1510 IFKN=3ANDP=3THEN2620
1520 IFSC=1THENPRINT@416,V$:PRINT@416,"THE SCROLL SAYS,'THERE IS ESCAPEFROM THE SECOND FLOOR.'":GOTO1250
1530 IFSW<>1THENPRINT@416,"YOU DON'T HAVE ONE.":GOTO1250
1540 PRINT@416,"AN INSCRIPTION READS,--","'GHOST KILLER!!'":GOTO1250
1550 W$=RIGHT$(Q$,4):IFW$="ROPE"THENRP=3:GOSUB1630
1560 IFW$=" KEY"THENKY=3
1561 IFW$="SIGN"THENSN=3
1570 W$=RIGHT$(Q$,6):IFW$=" PAPER"THENOP=3:GOTO1620
1571 IFW$=" KNIFE"THENKN=3
1572 IFW$="SCROLL"THENSC=3
1573 IFW$="BUCKET"THENBU=3
1574 IFW$=" SWORD"THENSW=3
1620 PRINT@416,"OK."CHR$(13):GOTO1250
1630 IFP=15THENPRINT@320,"THE ROPE MAGICALLY LEVITATES TO THE CEILING!!!!":RP=2
1640 RETURN
1650 W$=RIGHT$(Q$,5):IFW$="PLUGH"ANDPL=0THENP=2:PL=1:GOSUB90:GOTO1250
1660 IFW$="PLUGH"ANDPL=1THENPRINT@416,V$:PRINT@416,"SORRY--ONLY ONE PLUGH PER       CUSTOMER.":GOTO1250
1670 PRINT@416,V$:PRINT@416,"I DON'T UNDERSTAND":GOTO1250
1690 IFRIGHT$(Q$,2)=" N"ORRIGHT$(Q$,2)=" S"ORRIGHT$(Q$,2)=" E"ORRIGHT$(Q$,2)=" W"THENQ$=RIGHT$(Q$,1):GOTO1700
1695 PRINT@352,"WHICH DIRECTION";:Q$=INKEY$:IFQ$=""THEN1695
1700 IFQ$="N"THENQ=1:GOTO1710
1701 IFQ$="S"THENQ=3:GOTO1710
1702 IFQ$="E"THENQ=5:GOTO1710
1703 IFQ$="W"THENQ=7:GOTO1710
1704 GOTO1250
1710 Q1=VAL(MID$(LV$(P),Q,2)):IFQ1=0THEN1250
1711 ONQ1GOSUB30,90,170,260,310,390,420,450,500,530,1200,560,590,620,680,710,780,830,880,930,1010,3020,2370,2330,2590,2582
1712 GOTO1250
2060 PRINT@320,V$:PRINT@320,"";:IFOP=1THENPRINT"PAPER ";
2070 IFSC=1THENPRINT"SCROLL ";
2080 IFKN=1THENPRINT"KNIFE ";
2090 IFBU=1THENPRINT"BUCKET ";
2100 IFRP=1THENPRINT"ROPE ";
2110 IFKY=1THENPRINT"KEY ";
2120 IFSW=1THENPRINT"MAGIC SWORD ";
2130 IFSN=1THENPRINT"SIGN ";
2140 GOTO1250
2330 PRINT@416,V$:PRINT@416,"ARE YOU JUST GOING TO WALK      THROUGH THAT RAGING FIRE?":PRINT@352,""
2331 PRINT@352,"COMMAND";:INPUTQ$:IFLEFT$(Q$,1)<>"Y"ANDLEFT$(Q$,1)<>"N"THEN1255
2335 IFLEFT$(Q$,1)="N"THENPRINT@416,"A WISE DECISION"CHR$(13):GOTO1250
2340 IFLEFT$(Q$,1)="Y"ANDBU=1THENGOSUB680:GOTO1250
2350 IFLEFT$(Q$,1)="Y"ANDBU<>1THEN2750
2370 IFP=1THENGOSUB3020:GOTO1250
2371 IFP<>13THEN1250
2380 IFKY=3THENPRINT@416,"YOU'LL NEED A KEY !!":GOTO1250
2390 IFKY=1THENGOSUB620:GOTO1250
2582 IF(Q$="S"ORQ$="W"ORQ$="E")ANDSN<>1THEN7000
2583 PRINT@320,S3$:PRINT@320,"YOU FELL DOWN A TRAPDOOR AND    BROKE YOUR NECK!!"
2584 FORA=100TO70STEP-1:POKE49151,64:FORX=1TO5:NEXT:SOUNDA,1:NEXT:SOUND1,5:END
2590 IFA3=3ORSW=1THENPRINT@416,"THE GHOST WILL NOT LET YOU PASS!":GOTO1250
2600 GOSUB1010:GOTO1250
2610 END
2620 PRINT@97,CHR$(128);:PRINT@320,S3$
2621 PRINT@320,"THE KNIFE FLIES DOWN AND SLASHESYOUR THROAT.YOUR BLOOD IS POOLEDON THE FLOOR!!!!!"
2650 FORA=1TO34:READD,C:X=X+D:Y=Y+C:SET(X,Y,4):FORT=1TO20:NEXTT,A
2660 END
2670 IFSC=3THENPRINT@320,"THE KNIFE FLOATS OUT OF REACH."
2680 IFSC=1THENPRINT@320,"YOU PICKED UP THE KNIFE.":KN=1
2690 RETURN
2700 RETURN
2710 GOSUB2700:PRINT@352,"AN ANIMATED SUIT OF ARMOR KICKS YOU OUT OF THE ROOM!!"
2711 FORA=1TO2500:NEXTA:IFP=4THENGOSUB260:GOTO1250
2720 GOSUB 390:GOTO1250
2730 GOSUB2700:PRINT@352,"A SUIT OF ARMOR FLEES WHEN IT   SEES YOUR KNIFE!!":FORA=1TO2000:NEXTA
2740 AM=1:RETURN
2750 PRINT@320,S3$:PRINT@320,"YOUR CLOTHES ARE ON FIRE. AS A  MATTER OF FACT YOUR WHOLE BODY"
2751 PRINT"IS BURNING. WHAT A WAY TO GO!!":END
2760 IFP<>15ORRP<>2THENPRINT@416,"NO.";CHR$(13):GOTO1250
2765 GOSUB710:GOTO1250
2770 CLS:PRINT@128,"YOU ARE STANDING IN FRONT OF AN OLD HOUSE.  IT IS REPORTED TO BE";
2771 PRINT"HAUNTED, AND YOU ARE DARED TO   SEE FOR YOURSELF!":PRINT:PRINT:INPUT"HIT <ENTER> TO CONTINUE";A$:CLS
2780 PRINT"OBJECT ... TO ESCAPE THE HAUNTED   HOUSE ALIVE !!!":PRINT:PRINT"COMMAND SUMMARY :"
2781 PRINT"* 'MOVE' => TO MOVE ABOUT OR TO     PICK UP OBJECTS, USE ARROW      KEYS. USE <ENTER> TO EXIT"
2782 PRINT"* 'INVENT' => FOR AN INVENTORY"
2790 PRINT"    OF OBJECTS CARRIED":PRINT"* 'LEAVE' => TO LEAVE A ROOM.       IN RESPONSE TO 'WHICH DIR-"
2791 PRINT"    ECTION, PRESS 'S','N','E',      OR 'W'":PRINT:INPUT"HIT <ENTER> TO CONTINUE";A$:CLS
2800 PRINT"* ALL OTHER COMMANDS ARE TWO        WORDS LONG,":PRINT"    I.E. -- 'DROP SCROLL'"
2801 PRINT"* IF NOTHING HAPPENS, TRY ANOTH-    ER COMMAND.":PRINT:PRINT"YOU WILL START IN FRONT OF THE      HOUSE.":PRINT
2810 PRINT"THE FIRST COMMAND OF THE GAME IS    'MOVE'.":PRINT:PRINT"GOOD LUCK!!!  (YOU'LL NEED IT.)"
2880 PRINT@450,"COMMAND";:INPUTQ$:GOTO20
2920 PRINT@108,CHR$(203);CHR$(193);CHR$(205);CHR$(201);:PRINT@140,CHR$(196);S5$;CHR$(203);:PRINT@173,CHR$(197);S6$;:RETURN
2925 RETURN
2930 PRINT@108,CHR$(128);CHR$(195);CHR$(207);CHR$(195);CHR$(128);:PRINT@140,S7$;CHR$(207);S7$;
2931 PRINT@173,CHR$(199);CHR$(207);CHR$(203);:RETURN
2950 IFSW=3THENPRINT@416,"YOU CAN'T KILL A GHOST WITH YOURBARE HANDS!!!":GOTO1250
2960 IFP=20THENPRINT@416,"YOUR SWORD DIDN'T KILL THE GHOST":GOTO1250
2970 PRINT@416,"YOUR SWORD ENABLES YOU TO KILL  THE GHOST!!!":GOSUB2930:IFP=17THENG1=2:GOTO2980
2971 IFP=18THENG2=2:GOTO2980
2972 IFP=19THENG3=2
2980 GOTO1250
3000 FORF=1TO21:READLV$(F):NEXTF:RETURN
3010 DATA 22000000,00060309,00000402,00050003,04070006,02000500,05000800,00000007,10000213,00090011,00001012,00131100
3011 DATA 12230900,13002400,00000014,00171819,16000000,00000016,00001620,00251900,20262626
3015 DATA 0,-1,1,0,0,1,0,1,-1,0,-1,0,0,-1,0,-1,-1,-1,0,1,-1,0,0,1,1,0,0,1,-1,0,-1,0,2,1,1,0,1,0,1,0,1,0,0,-1,1,0,1,0
3016 DATA -1,-1,-1,0,0,-1,0,-1,-1,0,-1,0,1,-1,1,0,1,0,0,1
3020 PRINT@416,"YOU CAN'T OPEN THE DOOR.":RETURN
4000 S1$=CHR$(13)+CHR$(13)+CHR$(13):FORT=1TO63:S2$=S2$+" ":NEXT:S3$=S1$+CHR$(13):S4$=S3$+CHR$(13)
4010 S5$=CHR$(207)+CHR$(207)+CHR$(207):S6$=CHR$(205)+CHR$(205):S7$=CHR$(128)+CHR$(128):RETURN
5000 PRINT@448,"YOU HAD TO DROP EVERYTHING TO   CLIMB THE ROPE.                ";
5001 POKE16895,96:SC=3:RP=3:KY=3:KN=3:BU=3:OP=3:PC=1:RETURN
6000 PRINT@320,"YOU ARE BEGINNING TO FEEL SICK. AS A MATTER OF FACT, YOU ARE    DEAD! THE WATER IN THE BUCKET   WAS POISON!!"
6001 END
7000 PRINT@320,"YOU CLIMBED OUT OF A WINDOW AND ONTO A BALCONY. CONGRATULATIONS,YOU HAVE ESCAPED THE HAUNTED    HOUSE!!!"
7001 END
