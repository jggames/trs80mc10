0 GOSUB5500:CLEAR2000:DIM S1$(6),S2$(6),B$(4),N$(10),B$,BL$,T$,I$
1 DIM RN(1000),K(128),HS(10),S,SS,C,D,T,U,W,J,K,R1,R2,R3,R4,TT,VI,V1,V2,V3,V4,V5,VC,CL
2 DIM E,F,G,G2,GG,RL,TK,HT,SC,TC,CR,LC,PK,LF,RT,CH,Z,L,LV,R8:GOTO5000
3 POKEVC,CHOR(PEEK(VC)AND7):RETURN
5 R1=W:R2=E:RETURN
6 R1=E:R2=F:RETURN
7 R1=F:R2=U:R4=RN(C):RETURN
8 R1=U:R2=W:R3=R4:RETURN
9 R1=U:TC=C:C=RL:RETURN
10 IFSS=LFTHENRETURN
15 D=J:S=S-U:SS=SS-U:V3=VI+SS+U:V5=V3-U:RETURN
20 IFSS=RTTHENRETURN
25 D=U:S=S+U:SS=SS+U:V3=VI+SS+U:V5=V3+U:RETURN
30 R1=K:RETURN
35 G=PEEK(V3):G2=PEEK(V5):HT=HT+U:PRINT@S,S1$(W);:PRINT@SS,S2$(W);
40 FORZ=UTOLF:NEXT:IFG=R8ORG2=R8THENR1=K:TK=U
45 RETURN
100 IFLV<5THEN5400
105 LC=0:PRINT"GET READY FOR SLOPE #";(10-(LV/5))+1:GOSUB5300
110 CLS4:GOSUB3:D=0:S=80:SS=112:V3=VI+SS+U:V5=V3-U:TK=0:HT=0:TC=0
120 R1=U:R2=W:R3=RN(J):R4=R3:B$=MID$(B$(R1),R3,TT)+MID$(B$(R2),R4,CL)
200 FORC=UTORL:PRINT@S,BL$;:PRINT@SS,BL$;:PRINT@V4,B$;:POKEV1,V2
210 ONK(PEEK(PK)ANDPEEK(W))GOSUB10,20,30
220 IFPEEK(V3)<>V2ORPEEK(V5)<>V2THENGOSUB35
230 PRINT@S,S1$(D);:PRINT@SS,S2$(D);:ONR1GOSUB5,6,7,8,9
240 REM ** PUT JOYSTICK ROUTINE AT LINE 215
250 B$=MID$(B$(R1),R3,TT)+MID$(B$(R2),R4,CL)
260 NEXT
270 IFTC<>0THENSOUND100,1:CLS:PRINT"YOUR RUN IS OVER.":GOTO305
280 CLS4:TC=C-1:FORT=1TO12:PRINT@S+(T*32),S1$(D);:PRINT@SS+(T*32),S2$(D);:FORZ=1TO10:NEXTZ
290 PRINT@S+(T*32),BL$;:PRINT@SS+(T*32),BL$;:NEXTT 
300 SOUND150,1:CLS:PRINT"YOU COMPLETED SLOPE #";(10-(LV/5));CHR$(8);"!":LC=1 
305 SC=TC-(HT*(LV/5)):L=10-(LV/5)
310 PRINT "SCORE =";SC
325 PRINT "BRANCHES HIT =";HT
330 PRINT "TRUNKS OR BOULDERS HIT =";TK
335 IFSC>=HS(L)THENINPUT"INPUT YOUR NAME";N$(L):N$(L)=LEFT$(N$(L)+" ",5):HS(L)=SC
340 PRINT "HIGH SCORE =";HS(L);"/";L;"BY ";N$(L)
345 GOTO6000
5000 VI=16384:VC=49151:PK=17023:TT=32:CH=16:LF=96:RT=125:R8=128:RL=1000
5010 J=0:U=1:W=2:E=3:F=4:K=5:G=0:GG=218:V1=16895:V2=191:V4=480:CL=31:LV=50:CR=0
5020 K(65)=1:K(83)=2:K(13)=3:FORT=1TO10:HS(T)=500:N$(T)="JIMG":NEXT
5040 B$=CHR$(191):BL$=B$+B$+B$:FORT=1TO63:NEXTT
5050 S1$(0)=CHR$(155)+CHR$(145)+CHR$(189)
5060 S2$(0)=CHR$(184)+CHR$(152)+CHR$(191)
5070 S1$(1)=CHR$(190)+CHR$(162)+CHR$(167)
5080 S2$(1)=CHR$(191)+CHR$(164)+CHR$(180)
5081 S1$(2)=CHR$(255)+CHR$(255)+CHR$(255)
5082 S2$(2)=CHR$(255)+CHR$(255)+CHR$(255)
5100 S1$(3)=CHR$(191)+CHR$(191)+CHR$(096)+CHR$(191)+CHR$(191)
5105 S2$(3)=CHR$(191)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(191)
5110 S3$(3)=CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)+CHR$(096)
5115 S4$(3)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
5120 S1$(4)=CHR$(191)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(191)
5125 S2$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
5130 S3$(4)=CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)+CHR$(085)
5135 S4$(4)=CHR$(191)+CHR$(191)+CHR$(128)+CHR$(191)+CHR$(191)
5140 S1$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
5145 S2$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
5150 S3$(5)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
5155 S4$(5)=CHR$(191)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(191)
5160 S1$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5165 S2$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5170 S3$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5185 S4$(6)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
5190 PRINT "     PRESS ANY KEY TO BEGIN"
5200 I$=INKEY$:Z=RND(10):IFI$=""THEN5200
5210 CLS:GOTO100
5300 FORT=1TO4:B$(T)="":NEXT
5310 Z=0:FORT=UTO50
5315 R1=RND(LV+K):IFR1<KTHEN5319
5317 IFZ<10THENIFRND(W)=UGOTO5315
5318 R1=6:GOTO5320
5319 Z=Z+U:R1=RND(W)+W:IFRND(10)=UTHENR1=K
5320 B$(U)=B$(U)+S1$(R1)
5330 B$(W)=B$(W)+S2$(R1)
5331 B$(E)=B$(E)+S3$(R1)
5332 B$(F)=B$(F)+S4$(R1)
5340 NEXT
5350 FORT=ETORLSTEPF:RN(T)=RND(GG):NEXT:RN(J)=RND(GG):RN(RL)=RND(GG)
5360 LV=LV-K
5370 RETURN
5400 PRINT"YOU HAVE TRIED ALL 10 RUNS.":IFCR=10THENPRINT"AND YOU COMPLETED ALL 10 RUNS!":SOUND200,5
5410 PRINT"WOULD YOU LIKE TO TRY A"
5420 PRINT"DIFFERENT MOUNTAIN (Y/N)?"
5430 LV=50:CR=0:GOTO6060
5500 CLS:PRINT@41,"snowboarder ii":PRINT
5520 PRINT "USE <A> TO MOVE LEFT,":PRINT"AND <S> TO MOVE RIGHT."
5530 PRINT "HIT <ENTER> TO QUIT."
5540 PRINT:PRINT "TRY TO COMPLETE ALL 10 SLOPES."
5545 PRINT "THE DIFFICULTY LEVEL INCREASES"
5546 PRINT "FOR EACH SLOPE YOU TRY."
5550 PRINT "HITTING BRANCHES = LOSE POINTS."
5560 PRINT "IF YOU HIT A TREE STUMP, OR A"
5570 PRINT "BOULDER THEN THE RUN IS OVER.":PRINT 
5599 RETURN
6000 PRINT "TRY AGAIN (Y), NEXT RUN (N),    OR QUIT (Q)?"
6005 I$=INKEY$:IFI$=""THEN6005
6010 IFI$="Y" THEN 110
6020 IFI$="N" THEN 6040
6025 IFI$="Q" THEN 6090
6030 GOTO6005
6040 IFLC=1THENCR=CR+1
6050 GOTO100
6060 I$=INKEY$:IFI$=""THEN6060
6070 IFI$="Y"THEN100
6080 IFI$="N"THEN6090
6085 GOTO6060
6090 CLS:PRINT"HIGH SCORES:":FORT=1TO10:PRINTHS(T);"ON LEVEL";T;"BY ";N$(T):NEXT
6100 END
