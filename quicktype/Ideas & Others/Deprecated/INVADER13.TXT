0 CLS4:CLEAR999:DIML(511),Y(511),T(511),K(255),L$,B$,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,AK,BC,N1,LV,SC:GOTO900
1 RETURN
2 FORU=ITOB:RESET(Y,K):K=K-I:ONPOINT(Y,K)GOTO5,5,5,3,3,3,3,27:SET(Y,K,I):NEXT:RESET(Y,K):RETURN
3 H=INT(K/W)*G+INT(Y/W):PRINT@H-I,CHR$(N2);CHR$(N2+RND(N7));CHR$(N2+RND(N7));:AK=AK+I:IFAK>N1THENU=B:NEXT:L=E:RETURN
4 VP=VARPTR(L$):FORZ=-ITOI:POKE(PEEK(VP+W)*V)+PEEK(VP+R)+(H-L)+Z,N2:NEXT:SET(Y,K,I):U=B:K=O:NEXT:RETURN
5 RESET(Y,K):U=B:K=O:Y=O:NEXT:RESET(Y,K):RETURN
6 RESET(C,M):M=M+W:ONPOINT(C,M)GOTO24,14,14:SET(C,M,I):RETURN
7 T=R:NEXT:OND+IGOTO8,8:S=L:L=E:NEXT:D=I:F=O:E=N9:X=61:GOTO9
8 S=L:L=E:NEXT:D=-I:F=N1:E=N1:X=R:MS=MS+W:PRINT@MS,M$;:IFMS>N8THENPRINT@MS,LEFT$(B$,5);:MS=O
9 BC=BC+I:IFBC>(G-AK)/WANDS<N6THENBC=O:PRINT@S,LEFT$(B$,G);:S=S+G
10 FORL=STOESTEPD:PRINT@L,LEFT$(L$,L(L));:ONABS(POINT(P,N)>RORPOINT(P+W,N)>R)GOSUB25:PRINT@Q,P$;:J=L+F
11 ONMGOSUB21,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,6,20,20,20:FORT=OTOT(J):ONABS(POINT(X,Y(J)+V(T))>R)GOTO7:NEXT
13 ONK(PEEK(A)ANDPEEK(W))GOSUB15,16,19:ONKGOSUB2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2,2:NEXT:GOTO99
14 SET(C,M,I):GOSUB25:RETURN
15 ON(P>B)+IGOTO1:PRINT@Q,Q$;:P=P-W:Q=Q-I:RETURN
16 ON(P<N4)+IGOTO1:PRINT@Q,Q$;:P=P+W:Q=Q+I:RETURN
19 ONKGOTO1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1:K=N5:Y=P:RETURN
20 RESET(C,M):M=I
21 ONRND(R)GOTO1,1:FORZ=N3TORSTEP-W:ONPOINT(P,Z)GOSUB1,1,1,23,23,23,23,23:NEXT:RETURN
23 M=Z:Z=R:C=P+I:RETURN
24 RESET(C,M):RESET(C,M-I):M=I:RETURN
25 SOUND1,3:NP=NP-I:POKE16895,48+NP:IFNP<ITHENS=L:L=E:NP=O:POKE16895,48
26 RETURN
27 H=INT(K/W)*G+INT(Y/W):PRINT@H-I,CHR$(240+RND(14));CHR$(240+RND(14));CHR$(240+RND(14));:PRINT@MS,LEFT$(B$,6);:MS=0:SC=SC+25
28 SOUND200,1:SOUND155,2:SOUND225,1:RESET(Y,K):U=B:K=O:NEXT:RESET(Y,K):RETURN
99 IFNP>OANDAK=32THEN160
100 PRINT@MS,LEFT$(B$,6);:FORL=STO479STEP32:PRINT@L-N1,LEFT$(B$,N1);:PRINT@L,LEFT$(L$,L(L));:SOUND1,2:NEXT
105 PRINT@0, "YOU ARE DEAD.      ";:SC=SC+(AK*LV):PRINT"SCORE=";SC:IFSC>HSTHENHS=SC
110 PRINT@32,"PLAY AGAIN (Y/N)?  ";:PRINT"HIGH=";HS
115 IFAK<32THENPRINT@196,"THE ALIENS HAVE LANDED!";:SOUND100,15
120 I$=INKEY$:IFI$=""THEN120
130 IFI$="Y"THEN1570
140 IFI$="N"THENEND
150 GOTO120
160 PRINT@0,"ANOTHER WAVE IS COMING!"
170 SOUND100,3:SOUND50,3:SOUND100,3:SOUND50,5:SOUND50,5:SOUND100,3:SOUND50,3
180 SC=SC+(32*LV):PRINT@266,"SCORE=";SC;:FORT=1TO2000:NEXT
185 IFNP=SSTHENPRINT@199,"+100 SURVIVAL BONUS";:FORT=1TO2:FORZ=1TO24:READAA,BB:SOUNDAA,BB:NEXT:RESTORE:NEXT:SC=SC+100:GOTO200
190 XM=XM+I:IFXM=WTHENPRINT@199,"BONUS LIFE EARNED!";:FORT=1TO2:FORZ=1TO24:READAA,BB:SOUNDAA,BB:NEXT:RESTORE:NEXT:XM=0:NP=NP+1
195 IFNP>3THENNP=3
200 GOSUB1600:GOTO10
900 PRINT@231,"* SPACE INVADERS *";
910 PRINT@291,"FOR THE MC-10 BY JIM GERRIE";
920 PRINT@485,"PRESS ANY KEY TO BEGIN";
930 P$=INKEY$:R=RND(1000):IFP$=""THEN930
999 CLS:PRINT"PLEASE WAIT..."
1000 FORT=1TO40:B$=B$+CHR$(128):NEXT
1010 O=0:I=1:W=2:R=3:B=4:G=32:N=29:V=256:N1=31
1020 K(65)=1:K(83)=2:K(32)=3:A=17023
1030 DIMN2,N3,N4,N5,N6,N7,N8,N9,MS,NS,XM,NP,SS,Q$,P$,I$,M$,V(3)
1040 P$=CHR$(135+32)+CHR$(139+32)
1050 Q$=CHR$(128)+CHR$(128):N2=128:N3=27:N4=58:N5=28:N6=480:N7=14:N8=24:N9=479
1500 FORT=0TO256:L(T)=224:NEXT:FORT=257TO479:L(T)=L(T-1)-1:NEXT
1510 FORT=0TO286:T(T)=3:Y(T)=INT((T/16)+.1):NEXT
1520 FORT=287TO350:T(T)=2:Y(T)=INT((T/16)+.1):NEXT
1530 FORT=351TO414:T(T)=1:Y(T)=INT((T/16)+.1):NEXT
1540 FORT=415TO478:T(T)=0:Y(T)=INT((T/16)+.1):NEXT
1550 FORT=479TO511:T(T)=0:Y(T)=28:NEXT
1560 FORT=0TO3:V(T)=B*T:NEXT:M$=CHR$(128)+CHR$(128)+CHR$(241)+CHR$(255)+CHR$(251)
1570 SC=0:LV=0:NS=0:NP=3:MS=0:XM=0:GOSUB1600
1580 GOTO10
1600 CLS0:D=1:E=479:X=61:NS=NS+1:LV=LV+1:IFLV>3THENLV=3
1605 PRINT@480,NS;"    ";:FORT=16864TO16868:POKET,PEEK(T)-64:NEXT:FORT=16869TO16895:POKET,G:NEXT:POKE16895,48+NP
1610 FORT=1TO3:PRINT@348+(9*T),CHR$(135);CHR$(143);CHR$(139);:PRINT@348+(9*T)+32,CHR$(143);CHR$(143);CHR$(143);:NEXT
1630 P=31:Q=448+15:M=1:L=LV*32:S=L:AK=0:BC=0:F=0:H=0:K=0:U=0:Z=0
1700 L$="":C=32:FORY=1TO4:C=C+16:FORT=1TO8:L$=L$+CHR$(128+C)+CHR$(133+C)+CHR$(141+C):NEXT:IFY=4THEN:L$=L$+LEFT$(B$,39):GOTO1715
1712 L$=L$+LEFT$(B$,40)
1715 NEXT:C=0:SS=NP:RETURN
2100 DATA 89,1,125,1,147,1,176,1,147,1,125,1
2110 DATA 89,1,125,1,147,1,89,1,147,1,125,1
2120 DATA 89,1,125,1,147,1,176,1,147,1,125,1
2130 DATA 89,1,125,1,147,1,89,1,147,1,125,1
