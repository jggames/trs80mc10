1000 DIMX2(40),Y2(40),C2(40),X,Y,A,B,I,J,S,P,F,PI:PI=3.1415:CLS:PRINT"            TREBUCHET"
1020 PRINT " ------------------------------" 
2000 PRINT " A TREBUCHET IS A TYPE OF      "
2001 PRINT " MEDIEVAL WEAPON THAT WAS USED "
2002 PRINT " TO THROW PROJECTILES INTO AN  "
2003 PRINT " ENEMY FORTIFICATION.  IN THIS "
2004 PRINT " GAME YOUR JOB IS TO THROW A   "
2005 PRINT " PROJECTILE FROM THE GREEN BASE"
2006 PRINT " TO HIT THE RED TARGET.  YOU   "
2007 PRINT " CONTROL THE FLIGHT BY         "
2008 PRINT " SELECTING THE INITIAL SPEED   "
2009 PRINT " AND ANGLE.  THE SPEED MUST BE "
2010 PRINT " BETWEEN 1 AND 10.  THE ANGLE  "
2011 PRINT " UP) AND 0 (STRAIGHT TO THE 
2012 PRINT " LEFT)."
2013 PRINT "   PRESS ANY KEY TO CONTINUE";
2015 I$=INKEY$:IFI$=""THEN2015
2016 CLS0
2020 REM PICK TWO RANDOM ELEVATIONS FOR THE GROUND
2030 A=RND(20):IFINT(A/2)-(A/2)=0THEN2030
2040 B=RND(20):IFINT(B/2)-(B/2)=0THEN2040
2050 REM DRAW THE TERRAIN
2060 FORI=0TO24 
2065 FORJ=0TO31
2070 IF J<=A THEN RESET(I,ABS(31-J)):GOTO2075
2071 SET(I,ABS(31-J),6)
2075 NEXT
2080 NEXT
2090 FORI=25TO49 
2095 FORJ=0TO31
2100 IF J<=B THEN RESET(I,ABS(31-J)):GOTO2105
2101 SET(I,ABS(31-J),6)
2105 NEXT
2110 NEXT
2120 IF A>B THEN S=-1:GOTO2130
2121 S=1
2130 FOR I=A TO B STEP S
2140 RESET(25,ABS(31-I))
2150 NEXT I
2160 REM SET(THE BASE
2170 SET(10, ABS(31-A),1)
2180 REM SET(THE TARGET
2190 SET(39,ABS(31-B),4)
2200 SET(40,ABS(31-B),4)
2210 SET(41,ABS(31-B),4)
2220 REM DRAW THE CASTLE
2230 FOR I=B+1 TO B + 3
2240 RESET(39,ABS(31-I))
2250 RESET(40,ABS(31-I))
2260 RESET(41,ABS(31-I))
2270 RESET(38,ABS(31-(I+2)))
2280 RESET(42,ABS(31-(I+1)))
2290 RESET(40,ABS(31-(I+3)))
2300 NEXT
3000 REM STARTING VALUES FOR THE PROJECTILE
3010 X=10
3020 Y=A+1
3030 REM ASK THE USER FOR THE PROJECTILE'S INITIAL SPEED AND DIRECTION
3040 PRINT@(32*0)+26,"SPEED ";:PRINT@(32*1)+26,"(1-10)";:PRINT@(32*2)+26,"";:INPUT P
3050 IF P>=1 AND P<=10 THEN 3080
3060 SOUND 1,1
3070 GOTO 3040
3080 PRINT@(32*4)+26,"ANGLE ";:PRINT@(32*5)+26,"(0-90)";:PRINT@(32*6)+26,"";:INPUT F
3090 IF F>=0 AND F<=90 THEN GOTO 3130
3100 SOUND 1,1
3120 GOTO 3080
3130 REM TRANSFORM FROM SPEED AND ANGLE TO VELOCITY IN THE X AND Y DIRECTIONS
3140 REM U IS THE X VELOCITY
3150 U=P/5*COS(F/180*PI)
3160 REM V IS THE Y VELOCITY
3170 V=P/5*SIN(F/180*PI)
3172 REM INITIALIZE Z -- THE BACKGROUND COLOR
3175 Z=6
3180 REM REMOVE THE OLD POINT BY PAINTING IT THE SAME COLOR AS THE BACKGROUND
3190 IFY<32THENSET(X,ABS(31-Y),Z)
3200 REM UPDATE THE PROJECTILE'S COORDINATES BY ADDING THE VELOCITY 
3210 X=X+U
3220 Y=Y+V
3230 REM COLLISION DETECTION
3240 IF X<25 AND Y<A THEN 4000
3250 IF X>50 THEN 4000
3260 IF X>25 AND Y<B THEN 3500
3262 REM SAVE THE BACKGROUND COLOR IN Z
3270 REM SET(THE PROJECTILE AT ITS NEW POSITION
3275 IFY<32THENZ=POINT(X,ABS(31-Y))
3280 IFY<32THENRESET(X,ABS(31-Y))
3300 REM GRAVIATION ON THE PROJECTILE IN THE NEGATIVE Y DIRECTION 
3310 V=V-0.02
3320 GOTO 3190
3500 REM CHECK IF THE PROJECTILE IS CLOSE ENOUGH TO THE TARGET
3510 IF ABS(X-40)>2 THEN 4000
3520 PRINT@(32*13)+26,"HIT! ";
3530 REM DEFINE 40 FLAMES BY THE ARRAYS X2, Y2, AND C2
3570 FOR I=1 TO 40
3580 X2(I)=RND(4)+38
3590 Y2(I)=RND(4)+B
3600 C2(I)=8
3610 NEXT I
3620 FOR J=1 TO 5
3630 FOR I=1 TO 40
3640 IF Y2(I)<B+3 THEN RESET(X2(I),ABS(31-Y2(I))):GOTO3650
3641 SET(X2(I),ABS(31-Y2(I)),6) 
3650 Y2(I)=Y2(I)+1
3660 SET(X2(I),ABS(31-Y2(I)),C2(I))
3670 IF RND(0)>0.8 THEN C2(I)=4
3680 IF RND(0)>0.8 THEN C2(I)=8
3690 IF RND(0)>0.8 THEN C2(I)=2
3700 IF RND(0)>0.3 AND Y2(I)<B+9 THEN 3750
3710 IF Y2(I)<B+3 THEN RESET(X2(I),ABS(31-Y2(I))):GOTO3720
3711 SET(X2(I),ABS(31-Y2(I)),6) 
3720 X2(I)=RND(5)+38
3730 Y2(I)=B
3740 C2(I)=8
3750 NEXT
3770 NEXT
3780 END
3790 IF Y2(I)<B+3 THEN RESET(X2(I),ABS(31-Y2(I))):GOTO3800
3791 SET(X2(I),ABS(31-Y2(I)),6)
3800 NEXT I
4000 PRINT@(32*8)+26,"MISS!";
4010 GOTO 3000
