100 CLEAR1500:T$="":Z=0:N=Z:I=1:GOSUB3100:CLS
102 PRINT:PRINTT$;"TIME: 4:37 PM, MID NOVEMBER":CT=Z:GOSUB1650
104 PRINTT$;"LOCATION: ";LO$(CH(Z))
106 GOSUB400:GOSUB500:MF=Z:MG=Z:INPUTIN$:CT=CT+1
108 GOSUB600:IFMGTHENPRINTT$;MG$(MG):GOTO106
110 IFMFTHENGOSUB1200:IFMG=ZTHENGOTO104
111 IFMFANDMG<>ZTHENPRINTT$;MG$(MG):GOTO106
112 IFVE<3ORVE=7ORVE=15ORVE=21THEN116
114 GOSUB700:IFMGTHENPRINTT$;MG$(MG):GOTO106
116 ONVEGOSUB1700,1650,1950,1400,1450,1750,2150,2200,1300,1350,1500,1550,1600,1800,1850,1900,2000,2050,2100,2250,1860
117 IFMGTHENPRINTT$;MG$(MG)
118 IFMFTHEN104
119 GOTO106
200 OF=Z:OD=Z:FORN=ITO10:IFOC=IT(CH(Z),N)THENOF=I:OD=N:N=11
202 NEXT:RETURN
300 OF=Z:OA=Z:FORN=4TO8:IFOC=CH(N)THENOF=I:OA=N:N=9
302 NEXT:RETURN
400 IFCH(Z)<>ZTHENRETURN
402 OC=33:GOSUB200:IFOF=ZTHENRETURN
404 IFOB(33,4)<>32THENRETURN
406 FORN=ITO200:NEXT
408 CLS:PRINT"CONGRATULATIONS!!"
410 PRINT"MISSION ACCOMPLISHED...";:GOTO1852
500 IFCT<19THENRETURN
502 IFCT=19THENPRINTT$;"IT'S GETTING DARK"
504 IFCT<29THENRETURN
506 IFCT=29THENPRINTT$;"IT'S COLD HERE"
508 IFCT<39THENRETURN
510 OC=19:GOSUB300:IFOFAND(CT=39ORCT=59)THENPRINTT$;"THE FLASHLIGHT IS GETTING DIM"
512 IFCT<49THENRETURN
514 IFCT=49THENCH(I)=I:PRINTT$;"YOU'RE THIRSTY":RETURN
516 IFCT<63THENRETURN
517 IFCT=63ANDCF=ZANDSM=ZTHENCH(2)=I:PRINTT$;"YOU'RE GETTING COLD":RETURN
518 IFCT<73THENRETURN
520 IFOF=ZANDLT=ZTHENCH(3)=Z:IFCT=73THENPRINTT$;MG$(13):RETURN
522 IFCT<79THENIFOFTHENCH(3)=I:RETURN
523 IFCT<79THENIFNOT(OF)THENRETURN
524 IFCT=79THENOB(19,5)=Z:IFOFANDLT=ZTHENCH(3)=Z:PRINTT$;MG$(13)
525 IFOFANDCT=79THENPRINTT$;"THE BATTERIES ARE DEAD":RETURN
526 IFCT>=79THENIFLT=ZTHENCH(3)=Z
527 IFCT>=79THENIFNOT(LT=Z)THENCH(3)=I
528 IFCH(I)AND(CT/11=INT(CT/11))THENPRINTT$;"YOU'RE VERY THIRSTY":RETURN
530 IFCH(2)AND(CT/13=INT(CT/13))THENPRINTT$;"YOU'RE FREEZING COLD":RETURN
532 IFSMAND(CT/8=INT(CT/8))ANDCH(Z)>2ANDCH(Z)<10THENPRINTT$;"IT'S REALLY SMOKY IN HERE":RETURN
534 IFCT<199THENRETURN
536 IFSMANDCH(Z)>ITHENDD=I:GOTO800
538 IFCH(I)THENDD=2:GOTO800
540 IFCH(2)THENDD=3:GOTO800
542 RETURN
550 MF=0:IFIN$="N"THENMF=1
551 IFIN$="S"THENMF=1
552 IFIN$="E"THENMF=1
553 IFIN$="W"THENMF=1
554 IFIN$="U"THENMF=1
555 IFIN$="D"THENMF=1
556 RETURN
560 SP=0:FORCK=1TOLEN(IN$):IFMID$(IN$,CK,1)=" "THENSP=CK:CL=LEN(IN$)
561 NEXT:RETURN
600 VE=Z:OJ=Z:IFLEN(IN$)=ITHENGOSUB550:IFMF=ITHENRETURN
601 IFLEN(IN$)=ITHENIFMF=0THENMG=11:RETURN
602 GOSUB560:IFSP=ZTHENVE$=IN$:GOTO606
604 VE$=LEFT$(IN$,SP-I)
606 FORN=ITO21:IFVE$=VB$(N)THENVE=N:N=22
610 NEXT
612 IFVE=ZTHENMG=11:RETURN
613 IFVE<3ORVE=15ORVE=21THENRETURN
614 OJ$=RIGHT$(IN$,LEN(IN$)-SP)
616 LO=I:HI=48
618 IFLO>HITHENMG=11:RETURN
620 MD=INT((HI+LO)/2)
622 IFOJ$=OB$(MD)THENOJ=MD:RETURN
624 IFOJ$<OB$(MD)THENHI=MD-1:GOTO618
626 IFOJ$>OB$(MD)THENLO=MD+1:GOTO618
700 IFVE=5THENRETURN
701 X=VM(VE)
702 FORN=XTOX+21
704 IFVM(N)=OJTHENN=X+22:GOTO708
706 IFVM(N)=ZTHENMG=12:N=X+22
708 NEXT:RETURN
800 CLS:PRINT"SORRY!":PRINT"YOU DIED FROM ";DD$(DD):GOTO1852
900 FORCK=1TOLEN(MV$(CH(Z))):IFMID$(MV$(CH(Z)),CK,LEN(IN$))=IN$THENCC=CK:CK=LEN(MV$(CH(Z)))
901 NEXT:RETURN
1200 GOSUB900:IFCC=ZTHENMG=8:RETURN
1202 ONCH(Z)+IGOTO1205,1210,1215,1220,1225,1230,1235,1240,1245,1250,1255,1260,1265,1270,1275,1280,1285
1205 MG=14:RETURN
1210 IFIN$="N"THENCH(Z)=2:RETURN
1211 MG=14:RETURN
1215 IFIN$="E"THENCH(Z)=13:RETURN
1216 IFIN$="W"THENCH(Z)=16:RETURN
1217 IFIN$="S"THENCH(Z)=I:RETURN
1218 IFIN$="N"THENCH(Z)=3:RETURN
1220 IFIN$="N"THENCH(Z)=7:RETURN
1221 IFIN$="S"THENCH(Z)=2:RETURN
1222 IFIN$="W"THENCH(Z)=4:RETURN
1225 IFIN$="N"THENCH(Z)=6:RETURN
1226 IFIN$="W"THENCH(Z)=5:RETURN
1227 IFIN$="E"THENCH(Z)=3:RETURN
1230 IFIN$="E"THENCH(Z)=4:RETURN
1231 CH(Z)=8:RETURN
1235 IFIN$="S"THENCH(Z)=4:RETURN
1240 IFIN$="S"THENCH(Z)=3:RETURN
1241 IFIN$="D"THENCH(Z)=9:RETURN
1242 IFIN$="N"THENCH(Z)=11:RETURN
1245 IFIN$="D"THENCH(Z)=5:RETURN
1250 IFIN$="U"THENCH(Z)=7:RETURN
1255 IFIN$="N"THENCH(Z)=14:RETURN
1256 CH(Z)=13:RETURN
1260 IFIN$="E"THENCH(Z)=14:RETURN
1261 IFIN$="W"THENCH(Z)=15:RETURN
1262 IFIN$="S"THENCH(Z)=7:RETURN
1265 IFIN$="N"THENCH(Z)=15:RETURN
1266 CH(Z)=16:RETURN
1270 IFIN$="N"THENCH(Z)=10:RETURN
1271 CH(Z)=2:RETURN
1275 IFIN$="S"THENCH(Z)=10:RETURN
1276 CH(Z)=11:RETURN
1280 IFIN$="S"THENCH(Z)=12:RETURN
1281 CH(Z)=11:RETURN
1285 IFIN$="N"THENCH(Z)=12:RETURN
1286 CH(Z)=2:RETURN
1300 IFCH(Z)=ITHEN1312
1302 IFCH(Z)=ZTHENCH(Z)=I:GOTO1316
1304 IFCH(Z)=7THENCH(Z)=9:GOTO1316
1306 IFCH(Z)=9THENCH(Z)=7:GOTO1316
1308 IFCH(Z)=5THENCH(Z)=8:GOTO1316
1310 IFCH(Z)=8THENCH(Z)=5:GOTO1316
1311 MG=5:RETURN
1312 OC=33:GOSUB300:IFOFTHENPRINTT$;"OOF!! TOO HEAVY!":RETURN
1314 CH(Z)=Z
1316 MF=I:RETURN
1350 OC=7:GOSUB300:IFOF=ZTHENMG=6:RETURN
1352 IFOB(7,4)=46THENOB(7,4)=Z:CH(I)=Z:MG=I:RETURN
1354 MG=4:RETURN
1400 OC=OJ:GOSUB300:IFOF=ZTHENMG=6:RETURN
1402 OC=Z:GOSUB200:IFOF=ZTHENPRINTT$;"THIS ROOM IS FULL":RETURN
1404 IFCH(Z)=3ANDOJ=18THENOB(17,4)=18:OB(18,3)=17
1410 CH(OA)=Z:IT(CH(Z),OD)=OJ:MG=I
1412 IFOJ=26THENLT=Z
1414 RETURN
1450 IFCH(3)=ZTHENMG=13:RETURN
1451 IFOJ=9THEN1478
1452 IFOJ=15THEN1480
1453 IFOJ=20THEN1470
1454 IF(OJ=3OROJ=26OROJ=30)AND(OB(OJ,Z)=CH(Z))THENOC=Z:GOSUB200:IT(CH(Z),OD)=OJ:OB(OJ,Z)=19:GOTO1478
1455 IFOB(OJ,Z)=CH(Z)THEN1460
1456 OC=OJ:GOSUB300:IFOFTHEN1460
1458 OC=OJ:GOSUB200:IFOF=ZTHENMG=7:RETURN
1460 IFOB(OJ,3)>ZTHENIFOB(OB(OJ,3),I)<>3THENMG=7:RETURN
1462 IFOB(OJ,I)=ZTHEN1478
1464 IFOB(OJ,I)=ITHENMG=2:IFOB(OJ,5)<>ZTHEN1478
1465 IFOB(OJ,I)=ITHENIFNOT(OB(OJ,5)<>Z)THENRETURN
1466 IFOB(OJ,I)=2THENMG=3:IFOB(OJ,5)<>ZTHEN1478
1467 IFOB(OJ,1)=2THENIFNOT(OB(OJ,5)<>Z)THENRETURN
1468 IFOB(OJ,I)=3THENIFOB(OJ,4)=ZTHENMG=4:IFOB(OJ,5)<>ZTHEN1478
1469 IFOB(OJ,I)=3THENIFOB(OJ,4)=ZTHENIFNOT(OB(OJ,5)<>Z)THENRETURN
1470 PRINTT$;"IT CONTAINS: ";OB$(OB(OJ,4))
1471 IF(OB(OJ,4)=3OROB(OJ,4)=26OROB(OJ,4)=30)AND(OB(OB(OJ,4),Z)=CH(Z))THENOB(OB(OJ,4),Z)=19
1472 OC=OB(OJ,4):GOSUB200:IFOFTHENRETURN
1474 OC=Z:GOSUB200:IT(CH(Z),OD)=OB(OJ,4):RETURN
1478 PRINTT$;DE$(OB(OJ,5)):RETURN
1480 IFCH(Z)=2ANDFD=ZTHENMG=3:RETURN
1481 IFCH(Z)=3ANDFD=ZTHENMG=2:RETURN
1482 IF(CH(Z)=2ORCH(Z)=3)ANDFD=ITHENPRINTT$;DE$(Z):RETURN
1484 IF(CH(Z)=7ORCH(Z)=11)ANDOB(OJ,I)=ITHENMG=2:RETURN
1486 IFCH(Z)=7THENPRINTT$;DE$(Z):RETURN
1488 IFCH(Z)<>11THENMG=5:RETURN
1490 IFOB(OJ,I)=2THENMG=3:RETURN
1492 PRINTT$;DE$(Z):RETURN
1500 IFOJ=33THEN1508
1502 OC=OJ:GOSUB300:IFOF=ZTHENMG=5:RETURN
1504 IFCH(Z)<>6THENPRINTT$;"THERE'S NO WATER HERE!":RETURN
1506 OB(OJ,4)=46:MG=I:RETURN
1508 OC=33:GOSUB300:IFOF=ZTHENMG=6:RETURN
1510 OC=32:GOSUB200:IFOF=ZTHENMG=15:RETURN
1512 OB(33,4)=32:OB(33,5)=Z:OB(32,3)=33:OB(32,5)=15:OC=32:GOSUB200:IT(CH(Z),OD)=Z:MG=I:RETURN
1550 IFCH(Z)<>4THENMG=5:RETURN
1552 PRINTT$;"THERE'S A PAPER UNDER IT"
1554 OB(34,3)=Z:OB(31,5)=Z:OC=Z:GOSUB200:IT(4,OD)=34:RETURN
1600 OC=30:GOSUB300:IFOF=ZTHENMG=15:RETURN
1602 IFOJ=16THEN1608
1604 OC=26:GOSUB300:IFOF=ZTHENMG=12:RETURN
1606 CH(3)=I:LT=I:MG=I:RETURN
1608 IFCH(Z)<>3THENMG=12:RETURN
1610 IFOB(17,4)<>18THENMG=12:RETURN
1612 OB(17,4)=16:OB(18,3)=Z:OB(16,3)=17:PRINTT$;MG$(I):OC=18:GOSUB200:IT(3,OD)=Z
1614 IFOB(20,4)=6THENPRINTT$;"SMOKE GETS IN YOUR EYES":SM=I
1616 OB(16,Z)=3:OC=Z:GOSUB200:IT(3,OD)=16:CH(2)=Z:RETURN
1650 X=Z:PRINTT$;"YOU HAVE: ":FORN=4TO8:IFCH(N)THENPRINTT$;OB$(CH(N)):X=X+1
1652 NEXT:IFX=ZTHENPRINTT$;N$
1654 RETURN
1700 IFCH(3)=ZTHENMG=13:RETURN
1702 X=Z:PRINTT$;"YOU SEE: "
1704 FORN=ITO10:IFIT(CH(Z),N)THENPRINTT$;OB$(IT(CH(Z),N)):X=X+1
1706 NEXT:IFX=ZTHENPRINTT$;N$
1708 RETURN
1750 IFOB(OJ,I)=ZTHENMG=12:RETURN
1751 IFOJ=15THEN1764
1752 IFOB(OJ,Z)<19ANDOB(OJ,Z)<>CH(Z)THENMG=5:RETURN
1753 IFOB(OJ,I)=2THENMG=3:RETURN
1754 IFOB(OJ,2)=ZTHEN1758
1756 OC=OB(OJ,2):GOSUB300:IFOF=ZTHENMG=12:RETURN
1758 OB(OJ,I)=3:IFOB(OJ,4)=ZTHENMG=I:RETURN
1760 PRINTT$;"THERE'S SOMETHING INSIDE!"
1762 OC=Z:GOSUB200:IT(CH(Z),OD)=OB(OJ,4):RETURN
1764 IF(CH(Z)=3ORCH(Z)=2)ANDFDTHENMG=16:RETURN
1765 IFCH(Z)=3THENFD=I:MV$(2)="NSEW":MV$(3)="NSW":MG=I:RETURN
1766 IFCH(Z)=2THENMG=12:RETURN
1768 IFCH(Z)<>7ANDCH(Z)<>11THENMG=5:RETURN
1769 IFOB(OJ,I)=3THENMG=16:RETURN
1770 IFCH(Z)=7THEN1774
1772 IFOB(OJ,I)=2THENMG=3:RETURN
1774 OB(OJ,I)=3:MV$(11)="SEW":MV$(7)="NSD":MG=I:RETURN
1800 OC=35:GOSUB300:IFOF=ITHEN1814
1802 OC=7:GOSUB300:IFOF=ZTHENMG=12:RETURN
1804 IFOB(7,4)=ZTHENMG=4:RETURN
1805 OB(7,4)=Z
1806 IFCH(Z)<>3THENMG=I:RETURN
1808 IFSM=ITHENSM=Z:OB(16,Z)=21:OB(16,3)=Z:OB(17,4)=18:OB(18,3)=I:PRINTT$;"THE FIRE'S OUT"
1810 IFCF=0ANDCT>=49THENCH(2)=I
1812 RETURN
1814 IFOB(35,4)=ZTHENMG=4:RETURN
1815 OB(35,4)=Z:GOTO1806
1850 CLS
1852 PRINT"YOU HAVE COMPLETED";CT-1;"TURNS.":FORN=ITO600:NEXT:END
1860 MF=I:RETURN
1900 IFCH(3)=ZTHENMG=13:RETURN
1902 IFOJ=28THENIFOB(5,I)<>3THENMG=7:RETURN
1903 IF(OJ=3OROJ=28)ANDOB(OJ,Z)=19THENMG=1:RETURN
1904 IFOJ=3OROJ=28THENOC=Z:GOSUB200:IT(CH(Z),OD)=OJ:OB(OJ,Z)=19:MG=1:RETURN
1906 IFOB(34,3)<>0THENMG=7:RETURN
1908 PRINTT$;"IT READS: 11-6-23":PA=I:RETURN
1950 IF(OJ=3OROJ=26OROJ=30)AND(OB(OJ,Z)=CH(Z))THEN1966
1952 OC=OJ:GOSUB300:IFOFTHENMG=10:RETURN
1954 GOSUB200:IFOF=ZTHENMG=7:RETURN
1958 OC=Z:GOSUB300:IFOF=ZTHENMG=9:RETURN
1960 IT(CH(Z),OD)=Z
1962 CH(OA)=OJ:IFOB(OJ,3)=ZTHENMG=I:RETURN
1964 OB(OB(OJ,3),4)=Z:OB(OJ,3)=Z:MG=I:RETURN
1966 OB(OJ,Z)=19:OC=Z:GOSUB300:IFOF<>ZTHEN1962
1968 OC=Z:GOSUB200:IT(CH(Z),OD)=OJ:MG=9:RETURN
2000 IFOJ=33ANDCH(Z)=ITHEN2004
2002 GOSUB1400:RETURN
2004 OC=OJ:GOSUB300:IFOF=ITHENCH(OA)=Z
2005 MG=6:RETURN
2006 CH(Z)=Z:OC=Z:GOSUB200:IT(Z,OD)=33:CH(Z)=I:MG=I:RETURN
2050 IFCH(Z)<>5THENMG=5:RETURN
2052 IFPA=ZTHENPRINTT$;"WHAT'S THE COMBINATION?":RETURN
2054 OB(36,I)=I:PRINTT$;"IT CLICKED!":MG=I:RETURN
2100 OC=10:GOSUB300:IFOF=ITHENCH(OA)=Z:GOTO2106
2102 GOSUB200:IFOF=ITHENIT(CH(Z),OD)=Z:GOTO2106
2104 MG=12:RETURN
2106 MG=I:CF=I:CH(2)=Z:RETURN
2150 OC=I:GOSUB300:IFOF=ZTHENMG=12:RETURN
2152 OC=OJ:GOSUB200:IFOF=ZTHENMG=5:RETURN
2154 IFOJ=18THENMG=1:RETURN
2156 PRINTT$;"DESTRUCTIVE BUGGER, AREN'T YOU?":IFOJ<>47THENRETURN
2158 IFCH(Z)=2THENOB(47,I)=3:OB(47,5)=Z:MV$(2)="NSEW":MV$(3)="NSW"
2160 PRINTT$;"YOU BROKE THROUGH":RETURN
2200 OC=25:GOSUB300:IFOF=ZTHENMG=12:RETURN
2202 OC=6:GOSUB300IFOFTHEN2204
2203 GOSUB200
2204 IFOFTHENMG=I:GOSUB1762:OB(6,I)=3:OB(6,4)=Z:RETURN
2206 MG=5:RETURN
2250 IFOJ=36THENIFCH(Z)=5THENPRINTT$;"HOW?":RETURN
2251 IFOJ=36THENIFNOT(CH(Z)=5)THENMG=5:RETURN
2252 IF((CH(Z)=11ORCH(Z)=7)ANDOB(15,I)<>2)ORCH(Z)=3THENPRINTT$;"IT'S NOT LOCKED":RETURN
2253 OC=24:GOSUB300:IFOF=ZTHENPRINTT$;"WITH WHAT?":RETURN
2254 IFCH(Z)<>11THENPRINTT$;"THAT KEY DOESN'T FIT HERE":RETURN
2255 IFOB(15,I)<>2THENPRINTT$;"IT'S NOT LOCKED":RETURN
2256 OB(15,I)=I:OB(15,2)=Z:MG=I:RETURN
2258 MG=5:RETURN
3100 DIMCH(8),LO$(16),IT(16,12),MV$(16),OB$(48),MG$(16),DE$(17),VB$(21),OB(48,5),VM(83),DD$(3)
3102 CLS:PRINT"TEENY TINY TEXT ADVENTURE"
3104 PRINT"     BY TIM EKDOM":
3106 FORN=ZTO8:READCH(N):NEXT
3108 FORN=ZTO16:READLO$(N),MV$(N):NEXT
3110 FORN=ZTO16:FORJ=ITO8:READX:IFX=99THENJ=9:GOTO3112
3111 IT(N,J)=X
3112 NEXT:NEXT
3120 CLS:PRINT:PRINT"RUMORS SAY THAT THE OLD MAN":PRINT"HAD A MILLION IN CASH WHEN HE"
3122 PRINT"DIED. BUT IT WASN'T IN THE BANK.":PRINT"WHERE IS IT? THERE ARE NO":PRINT"HEIRS. IT'S FINDERS KEEPERS!"
3124 PRINT:PRINT"YOUR MISSION: TO SEARCH HIS OLD HOUSE...";
3132 FORN=ITO48:READOB(N,Z),OB(N,I),OB(N,2),OB(N,3),OB(N,4),OB(N,5):NEXT
3134 FORN=ITO48:READOB$(N):NEXT
3136 FORN=ITO16:READMG$(N):NEXT
3138 FORN=ZTO17:READDE$(N):NEXT
3140 FORN=ITO21:READVB$(N):NEXT
3141 FORN=ITO83:READVM(N):NEXT
3142 FORN=ITO3:READDD$(N):NEXT
3144 N$="NOTHING":PRINT:PRINT "CAPS LOCK ON?  PRESS enter";
3146 A$=INKEY$:IFA$=""THEN3146
3147 RETURN
3200 DATA 0,0,0,1,19,7,0,0,0
3202 DATA ROAD,N,PATH,NS,FRONT PORCH,SEW,LIVING ROOM,NW,BEDROOM,NWE,CLOSET,EU,BATH,S
3203 DATA KITCHEN,SD,ATTIC,D,CELLAR,U,EAST SIDE,NS,BACK SIDE,EW,WEST SIDE,NS
3204 DATA S.E. CORNER,NW,N.E. CORNER,SW,N.W. CORNER,SE,S.W. CORNER,NE
3205 DATA 22,29,45,99,22,45,23,99,15,47,99,4,13,17,15,99,2,8,99,10,36,41,99,43,44,99
3206 DATA 12,21,39,42,41,15,99,5,41,99,38,48,41,99,18,1,99,15,99,99,99,99,99,99
3211 DATA 19,0,0,0,0,0,4,3,0,0,31,0,3,0,0,4,0,0,3,3,0,0,3,0
3215 DATA 19,1,0,0,28,0,19,1,25,0,32,1,19,3,0,0,46,0,4,1,37,0,40,2
3219 DATA 3,3,0,0,20,3,19,0,0,0,0,0,3,0,0,13,0,14,7,1,0,0,25,0
3223 DATA 3,1,0,0,11,0,5,0,0,0,0,0,11,2,24,0,0,0,21,0,0,0,0,0
3227 DATA 3,3,0,0,0,16,19,0,0,0,0,0,19,0,0,0,0,7,3,3,0,0,6,0
3231 DATA 7,1,0,0,35,0,0,0,0,0,0,17,1,0,0,0,0,0,19,0,0,29,0,0
3235 DATA 19,0,0,12,0,0,9,0,0,38,0,8,11,2,24,0,0,0,19,0,0,5,0,9
3239 DATA 0,1,0,0,24,0,7,0,0,42,0,0,4,0,0,0,0,6,3,0,0,6,0,13
3243 DATA 19,3,0,36,0,0,19,0,0,31,0,11,19,3,0,0,0,0,5,2,0,0,33,4
3247 DATA 19,0,0,48,0,0,9,3,0,0,26,0,7,3,0,0,0,0,19,0,0,8,0,15
3251 DATA 7,0,0,0,0,0,7,3,0,0,30,0,6,1,0,0,0,0,6,3,0,0,46,0
3255 DATA 0,0,0,0,0,0,6,0,0,0,0,10,2,0,0,0,0,12,9,3,0,0,37,0
3298 DATA AX,BED,BOOKS,BOOKSHELF,BOX,BUNDLE,CANTEEN,CHEST,CHIMNEY,COAT,COMPUTER,CUPBOARD,DESK,DIAL
3300 DATA DOOR,FIRE,FIREPLACE,FIREWOOD,FLASHLIGHT,FLUE,FRIDGE,GATE,HOUSE,KEY,KNIFE,LANTERN,LOCK
3302 DATA MAGAZINES,MAILBOX,MATCHES,MATTRESS,MONEY,MONEYBAG,PAPER,POT,SAFE,SCREWDRIVER,SHELF
3304 DATA SINK,SOX,STAIRS,STOVE,TOILET,TUB,WALL,WATER,WINDOW,WORKBENCH
3306 DATA OK,IT'S CLOSED,IT'S LOCKED,IT'S EMPTY,IT'S NOT HERE,YOU DON'T HAVE IT
3307 DATA YOU CAN'T SEE IT,YOU CAN'T GO THAT WAY,YOU CAN'T TAKE IT,YOU ALREADY HAVE IT
3308 DATA I DON'T UNDERSTAND,YOU CAN'T DO THAT,IT'S TOO DARK TO SEE,THE GATE IS SHUT,WITH WHAT?,IT'S OPEN
3310 DATA IT LOOKS UNINTERESTING,IT'S A HEAVY NYLON BAG,THE DRAWER IS STUCK,THE FLUE IS BLOCKED
3312 DATA IT HAS A NUMBERED DIAL,IT HAS A MATTRESS,THERE'S SOMETHING UNDERNEATH,THE BATTERIES ARE WEAK,IT'S FULL
3314 DATA RAINBOW BACK ISSUES,IT'S WET,THERE'S WRITING ON IT,IT'S BOARDED UP,IT'S SCATTERED ALL OVER
3315 DATA IT'S NOT A PORTABLE,PHEW!!,IT HAS A LARGE CHIMNEY,"IT'S PADLOCKED, BUT NOT TOO HIGH"
3320 DATA LOOK,LIST,TAKE,DROP,EXAMINE,OPEN,CHOP,CUT,CLIMB,DRINK,FILL,LIFT
3322 DATA LIGHT,POUR,QUIT,READ,THROW,TURN,WEAR,UNLOCK,WHERE
3324 DATA 0,0,22,22,0,41,0,55,57,60,62,66,68,60,0,71,22,75,77,79,0
3326 DATA 1,3,5,6,7,10,18,19,24,25,26,28,30,33,34,35,37,40,0,5,6,7,8,12,13,15,21,27,29,33,36,43,0
3328 DATA 6,0,22,41,0,46,0,7,33,35,0,31,0,16,26,0,3,28,34,0,14,0,10,0,15,27,36,0,0
3330 DATA SMOKE INHALATION,DEHYDRATION,HYPOTHERMIA
