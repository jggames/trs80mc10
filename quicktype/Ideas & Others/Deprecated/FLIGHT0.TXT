
2 GOTO 8000
5 CLS:INPUT"CAN YOUR MACHINE HANDLE HIGH    SPEED (Y/N)";H$
6 H$=LEFT$(H$,1)
10 IF H$="Y"THENPOKE65495,0
110 Y=96:P=1
210 PMODE0,3:PCLS:LINE(128,92)-(128,84),PSET:LINE(128,100)-(128,108),PSET:LINE(124,96)-(116,96),PSET:LINE(20,146)-(237,146),PSET:PSET(0,96,3):PSET(255,96,3):CIRCLE(80,170),18:CIRCLE(120,170),18:LINE(240,162)-(240,184),PSET
310 LINE(132,96)-(140,96),PSET:CIRCLE(18,166),20,3,1,.5,.75:CIRCLE(237,166),20,3,1,.75,0
410 LINE(20,150)-(40,188),PSET,B
510 PSET(22,178,3):PSET(22,154,3)
610 LINE(160,168)-(186,172),PSET,B
710 PAINT(128,190),3,3
810 CIRCLE(80,170),20,0:CIRCLE(120,170),20,0:LINE(18,148)-(42,190),PRESET,B:LINE(158,166)-(188,174),PRESET,B:LINE(223,158)-(227,180),PRESET,B
910 CLS
1010 PRINT"       FLIGHT SIMULATOR":PRINT:PRINT"   STEER YOUR PLANE BY USING":PRINT" THE JOYSTICK; FORWARD TO DIVE,":PRINT" BACK TO CLIMB, LEFT TO TURN":PRINT" LEFT, AND RIGHT TO TURN RIGHT.":PRINT:PRINT"  PRESS ANY KEY TO CONTINUE."
1110 I$=INKEY$:IFI$=""THEN1110
1210 CLS:PRINT"     INSTRUMENTS:"
1310 PRINT"  FROM LEFT TO RIGHT THEY ARE;":PRINT" ALTIMETER-TO JUDGE ALTITUDE":PRINT:PRINT" ROLL INDICATOR-TO JUDGE TURNS":PRINT:PRINT" CLIMB/DIVE INDICATOR-TO JUDGE":PRINT" ANGLE OF FLIGHT":PRINT:PRINT" RADAR-TO READ THE LOCATION OF":PRINT" THE RUN
1410 PRINT:PRINT" FUEL GUAGE-FLIGHT TIME LEFT":PRINT:PRINT" PRESS ANY KEY TO CONTINUE."
1510 I$=INKEY$:IFI$=""THEN1510
1610 CLS:PRINT"        WARNING SOUNDS":PRINT:PRINT"  A SHORT LOW BEEPING SOUND":PRINT" MEANS THAT YOUR INCLINE IS TOO":PRINT" STEEP. IF YOU DON'T CORRECT":PRINT" THIS IMMEDIATELY, YOU WILL":PRINT" STALL AND GO INTO A STEEP DIVE."
1710 PRINT:PRINT"  A SHORT HIGHER BEEPING SOUND":PRINT" MEANS THAT YOU HAVE REACHED":PRINT" THE NECESSARY HEIGHT AND YOU":PRINT" MAY RETURN TO THE RUNWAY."
1810 PRINT:PRINT" DO YOU WANT TO FLY BY NIGHT?"
1910 I$=INKEY$:IFI$=""THEN1910ELSEIFI$="Y"THENNT=1
2010 CLS:PRINT:PRINT:PRINT:PRINT" PULL JOYSTICK BACK. THEN PRESS":PRINT" ANY KEY TO BEGIN."
2110 I$=INKEY$:IFI$=""THEN2110
2210 J0=JOYSTK(0):J1=JOYSTK(1)
2310 IFALT>635THENCY=CY+(J0-33)/4ELSECY=0:X=X-(J0-33)/4
2410 X=X-CY/10
2510 IFY>4THENY=Y+(J1-33)/6ELSEY=5
2610 IFP=1THENP=2ELSEP=1
2710 PMODE0,P
2810 PCOPY3TOP
2910 ALT=ALT+.50*(Y-96)
3010 IFALT>3200THENALT=3200:TP=1:SOUND200,1
3110 IFALT<0ANDY<82THEN4810
3210 IFALT<=0ANDTP=1ANDX<15ANDX>-15THEN5710ELSEIFALT<=0THEN4810
3310 IFALT>634ORY>142ORY<2THEN3510
3410 GOTO 6510
3510 IFY>144THEN4310ELSEST=0
3610 LINE(22,186-ALT/100)-(38,186-ALT/100),PSET
3710 LINE(70,-1*CY/12+170)-(90,CY/12+170),PSET
3810 LINE(110,(Y-96)/10+170)-(130,170-(Y-96)/10),PSET
3910 IFNT=0ANDY+CY>0ANDY+CY<255ANDY-CY>0ANDY-CY<250THENLINE(0,Y+CY)-(255,Y-CY),PSET
4010 IFX<0THENPSET(164,170,3)ELSEPSET(182,170,3)
4110 FL=FL+.03:PSET(225,160+FL,0):IFFL>20THEN4310
4210 SCREEN1,1:GOTO2210
4310 '***STALL***
4410 ST=ST+1:SOUND1,3:IFST<6THEN3610
4510 FORQ=1TO5:SOUND100,3:SOUND1,1:FORW=1TOQ*100:NEXTW:NEXTQ:FORQ=255TO10STEP-10:SOUNDQ,1:NEXTQ:Y=-20:ALT=ALT-500
4610 IFFL>20THEN4810
4710 GOTO3610
4810 '***CRASH***
4910 SOUND1,1
5010 IFY>0ANDX+ALT/5+2>=0ANDX+ALT/5+2<=255THENLINE(X+ALT/5+2,144)-(128,Y),PSET
5110 IFY>0ANDX-ALT/5+255>=0ANDX-ALT/5+255<=255THENLINE(X-ALT/5+255,144)-(128,Y),PSET
5210 IFY+CY>0ANDY-CY>0THENLINE(0,Y+CY)-(255,Y-CY),PSET
5310 SCREEN1,1:LINE(0,0)-(70,20),PSET:LINE-(50,70),PSET:LINE-(128,96),PSET:LINE-(100,100),PSET:LINE-(70,130),PSET:LINE-(0,144),PSET:LINE(255,0)-(240,50),PSET:LINE-(140,60),PSET:LINE-(128,96),PSET:LINE-(144,120),PSET:LINE-(220,110),PSET
5410 POKE65494,0:FORQ=1TO10:PLAY"T250;ECECECECECEC":NEXTQ 
5510 SOUND1,10
5610 GOTO7710
5710 '***LAND***
5810 FORQ=1TO5
5910 FORE=1TO3:PLAY"O1;T255;L255;ABCDEFG":NEXTE
6010 FORW=1TO(5-Q)*50:NEXTW
6110 NEXTQ
6210 FORQ=1TO10:PLAY"ABCDEFG":NEXTQ
6310 GOTO7310
6410 '***RUN WAY***
6510 IFNT=0ANDX+ALT/5>-2ANDX+ALT/5<253THENLINE(X+ALT/5+2,144)-(128,Y),PSET
6610 IFNT=0ANDX-ALT/5>-255ANDX-ALT/5<0THENLINE(X-ALT/5+255,144)-(128,Y),PSET
6710 RL=RL+.25:IFRL>.75THENRL=.25
6810 RY=Y+RL*(144-Y)':X1=RL*(128-(X+ALT/5+2)):X2=RL*(128-(X-ALT/5+255))
6910 X1=RL*X
7010 IFX1>-128ANDX1<128THENPSET(X1+128,RY,3)
7110 IFY>0THENPSET(128,Y,3)
7210 GOTO3510
7310 '*** LAND ***
7410 CLS:PRINT:PRINT"      GOOD LANDING PILOT!":PRINT:PRINT" YOU SUCCESSFULLY LANDED WITH":PRINT(INT((20-FL)*100))/100;"GALLONS OF FUEL LEFT IN":PRINT" THE TANK."
7510 PRINT:PRINT:PRINT"   DO YOU WISH TO FLY AGAIN?":I$=INKEY$
7610 I$=INKEY$:IFI$=""THEN7610ELSEIFI$="Y"THENRUNELSECLS:POKE65494,0:END
7710 '*** CRASH ***
7810 CLS:PRINT:PRINT"         YOU CRASHED!":PRINT:PRINT" NEXT TIME BE SURE TO BE AWARE":PRINT" OF YOUR ALTIMETER, YOUR WARN-":PRINT" INGS, AND YOUR FUEL."
7910 GOTO 7510
8000 PMODE0,3:PCLEAR3:GOTO5
