0 POKE &HFFBC,2:CLS:DIM D(31,14),A(255),M$(11),I$,X,Y,XV,YV,S,RI,SI,XX,YY,A,B,T
2 GOSUB 6000:GOSUB 7100:GOSUB 1000:GOSUB 3:GOSUB 9:GOTO 700
3 PRINT@480,"                               ";:POKEMC,96:PRINT@480,"";:RETURN
8 FOR B=0 TO 14:FOR A=0 TO 31:PRINT@A+(32*B),CHR$(D(A,B));:NEXT:NEXT
9 PRINT@480,"SAv LoD mON dOR LOc tRP gP Wl i";:POKEMC,84:RETURN
10 PRINT@X+(32*Y),CHR$(175);:RETURN
11 PRINT@X+(32*Y),CHR$(D(X,Y));:RETURN
60 I$=INKEY$:IF I$="" THEN 60
65 I=ASC(I$):RETURN
400 FOR T=1 TO 11:GOSUB 3:PRINTM$(T);" (y/n)?";:GOSUB 60:IF I$="Y" THEN  D(X,Y)=T:GOSUB 500:T=11
410 NEXT
420 RETURN
500 IF D(X,Y)>0ANDD(X,Y)<12 THEN GOSUB 3:PRINTM$(D(X,Y));:GOTO 520
505 IF D(X,Y)=143 THEN GOSUB 3:PRINT"TRAP";:GOTO 520
510 GOSUB 9
520 RETURN
700 X=15:Y=14
800 I$=INKEY$:GOSUB 11
805 IF I$=CHR$(8) THEN IF X>0 THEN X=X-1:GOSUB 500
810 IF I$=CHR$(9) THEN IF X<31 THEN X=X+1:GOSUB 500
815 IF I$=CHR$(94) THEN IF Y>0 THEN Y=Y-1:GOSUB 500
820 IF I$=CHR$(10) THEN IF Y<14 THEN Y=Y+1:GOSUB 500
825 IF I$="V" THEN GOSUB 900
830 IF I$="O" THEN GOSUB 910
835 IF I$="M" THEN GOSUB 400
840 IF I$="D" THEN D(X,Y)=68
845 IF I$="T" THEN D(X,Y)=143
850 IF I$="C" THEN D(X,Y)=100
855 IF I$="L" THEN D(X,Y)=255
856 IF I$="K" THEN D(X,Y)=191
860 IF I$="G" THEN D(X,Y)=36
862 IF I$="I" THEN D(X,Y)=73
865 IF I$=" " THEN D(X,Y)=32
870 GOSUB 10
890 GOTO 800
900 GOSUB 3:PRINT"SAVE DUNGEON (y/n)?";:GOSUB 60
905 IF I$="Y" THEN GOSUB 9400:GOSUB 8
907 GOSUB 9:RETURN
910 GOSUB 3:PRINT"LOAD DUNGEON (y/n)?";:GOSUB 60
920 IF I$="Y" THEN GOSUB 9500:GOSUB 8
930 GOSUB 9:RETURN
1000 MC=1535:FOR X=0 TO 31:FOR Y=0 TO 14:D(X,Y)=255:NEXT:NEXT:FOR X=0 TO 31:D(X,0)=191:D(X,14)=191:NEXT
1005 FOR Y=0 TO 14:D(0,Y)=191:D(31,Y)=191:NEXT
1010 D(15,14)=32:XX=0:YY=0:XV=31:YV=14:D(15,13)=32:CLS0:GOSUB 8:RETURN
1015 FOR T=1 TO 10:D(RND(29)+1,RND(12)+1)=143:NEXT:FOR T=1 TO 10:D(RND(29)+1,RND(9)+1)=36:NEXT
1020 C=RND(3)+4:A=RND(31-C):B=RND(14-C):C=C-1:FOR X=A TO A+C:FOR Y=B TO B+C:D(X,Y)=32:NEXT:NEXT
1025 FOR X=A TO A+C:D(X,B)=255:D(X,B+C)=255:NEXT:FOR Y=B TO B+C:D(A,Y)=255:D(A+C,Y)=255:NEXT
1030 Y=B+1+RND(C-2):D(A,Y)=100:D(A+C,Y)=100:X=A+1+RND(C-2):D(X,B)=100:D(X,B+C)=100
1035 D(A+1+RND(C-2),B+1+RND(C-2))=73:D(A+1+RND(C-2),B+1+RND(C-2))=36:D(A+1+RND(C-2),B+1+RND(C-2))=RND(11)
6000 PRINT"   MAKE DUNGEON BY JIM GERRIE"
6005 PRINT"A=MOVE LEFT"
6010 PRINT"S=MOVE RIGHT"
6015 PRINT"Z=MOVE DOWN"
6020 PRINT"W=MOVE UP   SPACE=OPEN PASSAGE"
6025 PRINT"V=SAVE DUNGEON"
6030 PRINT"O=LOAD DUNGEON"
6035 PRINT"M=PLACE A MONSTER"
6040 PRINT"D=PLACE AN UNLOCKED DOOR"
6045 PRINT"T=PLACE A TRAP"
6050 PRINT"C=PLACE A LOCKED DOOR"
6055 PRINT"L=PLACE SECRET PASSAGE WALL"
6056 PRINT"K=PLACE IMPASSABLE WALL"
6060 PRINT"G=PLACE GOLD"
6062 PRINT"I=PLACE A MAGIC ITEM"
6065 PRINT"    PRESS ANY KEY TO CONTINUE";
6070 I$=INKEY$:IF I$="" THEN 6070
6080 CLS0:RETURN
7100 A(87)=1:A(90)=2:A(83)=3:A(65)=4:A(67)=5:A(82)=6:A(77)=7:A(84)=8:A(79)=9:A(100)=1:A(73)=2:A(36)=3:A(120)=4:A(143)=4
7110 FOR T=1 TO 11:READ M$(T):NEXT T
7120 RETURN
8000 DATA "SKELETON","ZOMBIE","KOBOLD","GIANT BAT","ORC","GOBLIN","BERSERKER","STIRGE","GIANT SPIDER"
8030 DATA "WIZARD","MONSTER JELLY"
9400 REM SAVE DUNGEON
9410 CLS:INPUT"ENTER FILENAME TO SAVE";I$
9415 IFI$=""THENI$="DUNGEON"
9420 I$=LEFT$(I$,8)+"/DAT"
9425 OPEN "O", #1, I$
9430 FORX=0TO31:FORY=0TO14:WRITE #1,D(X,Y):NEXT:NEXT
9440 CLOSE #1
9490 X=15:Y=14:RETURN
9500 REM LOAD DUNGEON
9510 CLS:INPUT"ENTER FILENAME TO LOAD";I$
9515 IFI$=""THENI$="DUNGEON"
9520 I$=LEFT$(I$,8)+"/DAT"
9525 OPEN "I", #1, I$
9530 FORX=0TO31:FORY=0TO14:INPUT #1,D(X,Y):NEXT:NEXT
9540 CLOSE #1
9590 X=15:Y=14:RETURN
