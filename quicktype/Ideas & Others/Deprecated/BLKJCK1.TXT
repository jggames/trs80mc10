5 REM COPYRIGHT (C) T&D SOFTWARE 1982
10 REM
20 CLS:PRINT@10,"blackjack";:PRINT@132,"INSTRUCTIONS (Y/N)?
30 IN$=INKEY$:IF IN$="" THEN 30
40 IF IN$="Y" THEN 50
45 GOTO 130
50 PRINT@132,"                       ";
52 PRINT@70,"THIS VERSION OF         "
53 PRINT"BLACKJACK IS A TWO PLAYER GAME"
54 PRINT"THAT IS PLAYED USING THE a,s"
55 PRINT"AND enter KEYS. PLAYERS BET"
56 PRINT"BETWEEN $10 AND $90 THEN ":PRINT"RECEIVE THEIR CARDS."
60 PRINT:PRINT"     THE RESPONSES CAN EITHER"
61 PRINT"BE A 'H' FOR HIT, A 'S' FOR"
62 PRINT"STAND OR A 'D' FOR DOUBLE. YOU"
63 PRINT"CAN ONLY DOUBLE ON FIRST CARD."
70 PRINT:PRINT"    HIT ANY KEY TO CONT."
80 IN$=INKEY$:IF IN$="" THEN 80
90 CLS:PRINT@11,"blackjack":PRINT@64," YOU START OFF WITH $500. THE
91 PRINT"FIRST ONE TO GO BROKE LOSES.":PRINT"THE FIRST ONE TO HAVE $1000     DOLLARS OR MORE WINS!"
100 PRINT:PRINT" IF YOU ARE UNDER 21 IN 5 CARDS YOUR TOTAL WILL BE TREATED AS"
101 PRINT"IF IT WAS 21. SAME WITH THE     DEALER. A SCORE OF 21 WILL"
102 PRINT"GIVE YOU A BONUS OF HALF YOUR   BET!      GOOD LUCK!"
110 PRINT:PRINT"    HIT ANY KEY TO CONT."
120 IN$=INKEY$:IF IN$="" THEN 120
130 CLS:PRINT@164,"ENTER PLAYER #1 LAST NAME":INPUT A$:A=LEN(A$):IF A>6 THEN A$=MID$(A$,1,6)
140 PRINT@257,A$;", YOU USE THE SPACE BAR.":PRINT@422,"HIT ANY KEY TO CONT."
150 IN$=INKEY$:IF IN$="" THEN 150
160 CLS:PRINT@164,"ENTER PLAYER #2 LAST NAME":INPUT B$:IF LEN(B$)>6 THEN B$=MID$(B$,1,6)
170 PRINT@257,B$;", YOU USE THE ENTER KEY.":PRINT@420,"HIT ANY KEY TO CONT."
180 IN$=INKEY$:IF IN$="" THEN 180
190 A(0)=500:A(2)=500:DIM C$(52)
200 GOSUB 210:GOTO 320
210 SH=0:CLS:PRINT@235,"SHUFFLING";
220 GOTO 240
230 FOR X=1 TO 20:PRINT@1,"SHUFFLING";:PRINT@21,"SHUFFLING";:FOR PA=1 TO 50:NEXT PA:PRINT@1,"         ";:PRINT@21,"         ";:FOR PA=1 TO 50:NEXT PA:NEXT X
240 DATA "A","2","3","4","5","6","7","8","9","t","j","q","k"
250 DATA 16,32,48,96,1
260 FOR X=1 TO 13:READ C$(X):NEXT X:RESTORE
270 FOR X=14 TO 26:READ C$(X):NEXT X:RESTORE
280 FOR X=27 TO 39:READ C$(X):NEXT X:RESTORE
290 FOR X=40 TO 52:READ C$(X):NEXT X
300 FOR X=1 TO 5:READ C(X):NEXT X
310 RETURN
320 P$=A$:PL=0
330 B3=0:T1=0:T2=0:T3=0:CLS:PRINT@45,"bets";
340 IF A(0)<0 OR A(0)>1000 OR A(2)<0 OR A(2)>1000 THEN FOR Q=1 TO 30:CLS(RND(8)):FOR PA=1 TO 20:NEXT PA:NEXT Q
350 IF A(0)<0 OR A(2)>1000 THEN PRINT@194,B$;", YOU ARE THE WINNER!!":END
360 IF A(2)<0 OR A(0)>1000 THEN PRINT@194,A$;", YOU ARE THE WINNER!!":END
370 PRINT@97,"BETS ARE MADE IN INCREMENTS OF  $10"
380 PRINT@197,"                        ";
390 PRINT@194,P$;" YOU HAVE $";A(PL);
400 PRINT@226,"WHAT IS YOUR BET?";
410 PRINT@291,"10 20 30 40 50 60 70 80 90 ";
419 J0=32:GOTO423
420 V$=INKEY$:IFV$=""THEN420
423 IFV$="A"ORV$="W"THENJ0=J0-8:IFJ0<0THENJ0=0
424 IFV$="S"ORV$="Z"THENJ0=J0+8:IFJ0>63THENJ0=63
425 L1=INT(J0/7.4)*3
430 IFL1<>L2THEN PRINT@323+L2,CHR$(143);CHR$(143);
440 PRINT@323+L1,CHR$(143+1);CHR$(143+1);
450 L2=L1:IFPL=0THEN490
470 IFV$=CHR$(13)THENP=125
471 IFP=125THENPL=1:B2=((L1/3)+1)*10:GOTO510
480 GOTO420
490 IFV$=CHR$(13)THENP=126
491 IFP=126THENPL=2:P$=B$:B1=((L1/3)+1)*10:V$="":GOTO380
500 GOTO420
510 L2=0
520 A1=0:A2=0:A3=0:CLS:PRINT@11,"blackjack"
530 PRINT@65,"DEALER";
540 PRINT@225,A$;
550 PRINT@385,B$;
560 L=73:GOSUB 620
570 FOR X=77 TO 79:PRINT@X,CHR$(143+1);:PRINT@X+32,CHR$(143+1);:PRINT@X+64,CHR$(143+1);:NEXT X
580 L=233:GOSUB 620:L=237:GOSUB 620
590 L=393:GOSUB 620:L=397:GOSUB 620
600 PRINT@97,"$";D;:PRINT@129,"BET";B1+B2;:PRINT@257,"$";A(0);:PRINT@289,"BET=";B1;:PRINT@417,"$";A(2);:PRINT@449,"BET=";B2;
610 GOTO 760
620 REM
630 SH=SH+1:IF SH>49 THEN SH=0:RESTORE:GOSUB 230
640 X=RND(52):IF C$(X)="E" THEN 640
650 IF C$(X)="t" OR C$(X)="k" OR C$(X)="q" OR C$(X)="j" THEN B=10:GOTO 680
660 XB=0:IF C$(X)="A" THEN B=1:XB=1:GOTO 680
670 B=VAL(C$(X)):GOTO 680
680 IF L<320 AND L>128 THEN T1=T1+B:A1=A1+XB
690 IF L>319 THEN T2=T2+B:A2=A2+XB
700 IF L<128 THEN T3=T3+B:A3=A3+XB
710 IF X<14 THEN CO=96
720 IF X>13 AND X<27 THEN CO=16
730 IF X>26 AND X<40 THEN CO=32
740 IF X>39 THEN CO=48
750 FOR Y=0 TO 64 STEP 32:PRINT@L+Y,CHR$(143+CO);:PRINT@L+1+Y,C$(X);:PRINT@L+2+Y,CHR$(143+CO);:NEXT Y:C$(X)="E":RETURN
760 PRINT@253,"H";:PRINT@285,"S";:PRINT@317,"D";:PRINT@413,"H";:PRINT@445,"S";:PRINT@477,"D";
770 PRINT@51,"TOTAL IS";T3;:PRINT@211,"TOTAL IS";T1;:PRINT@371,"TOTAL IS";T2;
779 J0=32:V$="":GOTO783
780 V$=INKEY$:IFV$=""THEN780
783 IFV$="A"ORV$="W"THENJ0=J0-32:IFJ0<0THENJ0=0
784 IFV$="S"ORV$="Z"THENJ0=J0+32:IFJ0>63THENJ0=63
790 L1=INT(J0/26)*32:IFL1<>L2THENPRINT@254+L2+(80*(PL-1)),CHR$(143);
800 L2=L1:PRINT@254+L2+(80*(PL-1)),CHR$(143+1);:IFV$=CHR$(13)THEN870
840 GOTO780
870 LO=254+L2+(80*(PL-1)):IF LO=254 THEN L=241+(C1*4):C1=C1+1:GOSUB 620
900 IF LO=254 AND T1>21 THEN 1500
910 IF LO=254 AND C1<3 AND T1<22 THEN PRINT@219,;T1;:GOTO779
920 IF LO=286 THEN PL=3:GOSUB 1380:GOTO779
930 IF LO=318 AND C1<1 THEN B1=B1*2:PRINT@292,;B1;:L=241+(C1*4):GOSUB 620:GOSUB 1380:PRINT@219,;T1;
940 IF LO=318 AND C1<1 AND T1>21 THEN LO=254:GOTO 900
950 IF LO=318 AND C1<1 AND T1<22 THEN PL=3:GOSUB 1380:GOTO779
960 IF C1>2 AND T1<22 THEN T1=21:C1=0:PRINT@219,T1;:PL=3:GOTO779
970 IF LO=414 THEN L=401+(C2*4):C2=C2+1:GOSUB 620
980 IF LO=414 AND T2>21 THEN1510
990 IF LO=414 AND C2<3 AND T2<22 THEN PRINT@379,;T2;:GOTO779
1000 IF LO=446 THEN GOSUB 1400:GOTO 1060
1010 IF LO=478 AND C2<1 THEN B2=B2*2:PRINT@452,;B2;:L=401+(C2*4):GOSUB 620:GOSUB 1400:PRINT@379,;T2;
1020 IF LO=478 AND C2<1 AND T2>21 THEN LO=414:GOTO 980
1030 IF LO=478 AND C2<1 AND T2<22 THEN :GOSUB 1400:GOTO 1060
1040 IF C2>2 AND T2<22 THEN T2=21:PRINT@379,T2;:GOTO 1060
1050 GOTO779
1060 REM
1070 L=77:GOSUB 620:GOSUB 1420:PRINT@59,;T3;
1080 IF T3<17 AND C3<3 THEN L=81+(C3*4):C3=C3+1:FOR PA=1 TO 200:NEXT PA:GOSUB 620:GOSUB 1420:PRINT@59,;T3;:GOTO 1080
1090 IF C3>2 THEN T3=21:PRINT@59,;T3;
1100 IF T3>21 THEN GOSUB1520
1110 IF C3>3 AND T3<22 THEN T3=21:PRINT@59,;T3;
1120 FOR PA=1 TO 800:NEXT PA:CLS
1130 PRINT@132,A$;" YOU ";
1140 IF T1=21 THEN B1=INT(B1*1.5)
1150 IF T2=21 THEN B2=INT(B2*1.5)
1160 IF T1<22 AND T3<22 AND T1=T3 THEN PRINT;"PUSHED":GOTO 1220
1170 IF T1>21 THEN PRINT;"BUSTED";:A(0)=A(0)-B1:B3=B3+B1
1180 IF T1<22 AND T3<22 AND T1>T3 THEN PRINT;"WIN";:A(0)=A(0)+B1:B3=B3-B1
1190 IF T1<22 AND T3>21 THEN PRINT;"WIN";:A(0)=A(0)+B1:B3=B3-B1
1200 IF T1<22 AND T3<22 AND T1<T3 THEN PRINT;"LOSE";:A(0)=A(0)-B1:B3=B3+B1
1210 PRINT;"  $";B1
1220 PRINT@196,B$;" YOU ";
1230 IF T2<22 AND T3<22 AND T2=T3 THEN PRINT;"PUSHED":GOTO 1290
1240 IF T2>21 THEN PRINT;"BUSTED";:A(2)=A(2)-B2:B3=B3+B2
1250 IF T2<22 AND T3<22 AND T2>T3 THEN PRINT;"WIN";:A(2)=A(2)+B2:B3=B3-B2
1260 IF T2<22 AND T3>21 THEN PRINT;"WIN";:A(2)=A(2)+B2:B3=B3-B2
1270 IF T2<22 AND T3<22 AND T2<T3 THEN PRINT;"LOSE";:A(2)=A(2)-B2:B3=B3+B2
1280 PRINT;"  $";B2
1290 PRINT:PRINT"    I ";:IF B3>0 THEN PRINT"WON  $"B3;
1300 IF B3<0 THEN PRINT"LOST  $";ABS(B3);
1310 IF B3=0 THEN PRINT"BROKE EVEN!";
1320 A1=0:A2=0:A3=0:PL=0:C1=0:C2=0:C3=0:P$=A$
1330 PRINT@422,"HIT ANY KEY TO CONT."
1340 D=D+B3
1360 V$=INKEY$:IF V$="" THEN 1360
1370 GOTO 330
1380 IF A1>0 AND T1<12 THEN T1=T1+10:PRINT@219,T1;:RETURN
1390 RETURN
1400 IF A2>0 AND T2<12 THEN T2=T2+10:PRINT@379,T2;:RETURN
1410 RETURN
1420 IF A3>0 AND T3>6 AND T3<12 THEN T3=T3+10
1430 RETURN
1500 FOR Y=1 TO 12:PRINT@206,"                ";
1505 FOR PA=1 TO 50:NEXT PA:PRINT@206,"YOU BUSTED AT";T1;:FOR PA=1 TO 50:NEXT PA:NEXT Y:PL=3:GOTO779
1510 FOR Y=1 TO 12:PRINT@366,"                ";
1515 FOR PA=1 TO 50:NEXT PA:PRINT@366,"YOU BUSTED AT";T2;:FOR PA=1 TO 50:NEXT PA:NEXT Y:GOTO 1060
1520 FOR Y=1 TO 8:PRINT@48,;"               ";
1530 FOR PA=1 TO 50:NEXT PA:PRINT@48,;"I BUSTED AT";T3;:FOR PA=1 TO 50:NEXT PA:NEXTY
1540 RETURN
