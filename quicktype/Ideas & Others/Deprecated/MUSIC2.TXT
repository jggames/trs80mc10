110 DIM T$(12),T(12,2),S(99,3)
120 DATA g,38,452,a,58,422,b,78,392,c,89,362,d,108,332,e,125,302,f,133,272,g,147,242
125 DATA a,159,212,b,170,182,c,176,152,d,185,122
130 FOR I=1 TO 12
140 READ T$(I),T(I,1),T(I,2):NEXT:GOSUB150:GOTO290
150 CLS:PRINT@9,"MUSIC PROGRAM"
160 FOR I=34 TO 290 STEP 64
170 FOR J=1 TO 25
180 PRINT@I+J,"-";
190 NEXT J,I
200 PRINT@361,"-  -";
210 PRINT@421,"-  -";
220 J=92
230 FOR I=1 TO 12
240 J=J+30
250 PRINT@J,T$(13-I);
260 NEXT
270 J=4:RETURN
290 C=1:E=1:S(C,1)=1:S(C,2)=1:S(C,3)=1:S(1,0)=T(1,2)
300 A$=INKEY$
310 IFA$=""THEN300
320 A=ASC(A$)
325 IF(A>48)AND(A<58)THENI=A-48:T=T(I,1):GOTO410
330 IF(A>32)AND(A<42)THENI=A-32:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
335 IFA=45THENI=12:T=T(I,1):GOTO410
340 IFA=64THENI=12:T=T(I,1)-5:GOTO410
345 IFA=48THENI=10:T=T(I,1):GOTO410
350 IFA=79THENI=10:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
355 IFA=58THENI=11:T=T(I,1):GOTO410
360 IFA=80THENI=11:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
365 IFA=83THENCLS:PRINT"HIT ENTER TO SAVE":GOSUB610:IFA$=CHR$(13)THENPRINT"SAVING":CSAVE*S:GOSUB600:GOSUB150:GOTO300
370 IFA=76THENCLS:PRINT"HIT ENTER TO LOAD":GOSUB610:IFA$=CHR$(13)THENPRINT"LOADING":CLOAD*S:GOSUB700:GOSUB150:GOTO300
392 IFA=70THENJ=8:REM-FULL NOTE
393 IFA=84THENJ=6:REM-3/4 NOTE
394 IFA=72THENJ=4:REM-HALF NOTE
395 IFA=81THENJ=2:REM-QTR NOTE
396 IFA=62THENCLS:FORT=1TOE:A$="SOUND"+STR$(S(T,1))+","+STR$(S(T,2)):LPRINTA$:PRINTA$:NEXT:GOSUB600:GOSUB150:GOTO300
397 IFA=13THENE=C-1:C=1
398 IFA=32THENFORT=1TOE:SOUNDS(T,1),S(T,2):N=S(T,0):I=S(T,3):GOSUB500:NEXT
399 IFA=65THENIFC>1THEN:C=C-1:E=C-1
400 GOTO300
410 SOUND T,J:S(C,1)=T:S(C,2)=J:S(C,3)=I:N=T(I,2):S(C,0)=N:E=C:C=C+1:GOSUB500:GOTO300
500 PRINT@N,CHR$(191);
510 FORZ=1TO30:NEXT
520 PRINT@N,T$(I);
530 RETURN
600 PRINT"PRESS ANY KEY TO CONTINUE"
610 A$=INKEY$:IFA$=""THEN610
620 CLS:RETURN
700 FORT=1TO99:IFS(T,1)=0THENC=T:E=C-1:GOTO720
710 NEXT
720 RETURN
