0 CLEAR64:GOSUB6995:DIMD(31,14),A(255),P(33,5,1),CL(7),E(19,3),EO(6),AD(6),LV(7),X,Y,A,B,C,H,N,T,XX,YY,XV,YV,MX,MY,MC,Z
1 DIML$(7),E$(19),S$(9),M$(11,1),K$(4),AB$(12),XC$(7),W(3,3),M(11,4),G(3,5,1),I$,NM,I,D,O,AL,V,S,SI,RI,RO,K,J,LV,MO
2 GOSUB7100:GOTO800
3 PRINT@480,"                               ";:POKEMC,96:PRINT@480,"";:RETURN
4 FORI=1TO6:PRINTCHR$(P(I,N,0));:NEXT:RETURN
5 K=1:J=7:GOSUB18:IFD(X+XV,Y)=100THENIFRND(6)>4+(J>1)THEND(X+XV,Y)=32:X=X+XV:GOTO7
6 IFD(X,Y+YV)=100THENIFRND(6)>4+(J>1)THEND(X,Y+YV)=32:Y=Y+YV
7 GOSUB150:GOSUB200:RETURN
8 FORB=YYTOYV:FORA=XXTOXV:PRINT@A+(32*B),CHR$(D(A,B));:NEXT:NEXT
9 PRINT@480,"SEcRET TrAP tHIEF mISSILE+ oRDE";:POKEMC,82:RETURN
10 ONRND(6)GOSUB700,700,700,700,750,750:D(X,Y)=32:GOSUB9:RETURN
12 POKEMC,31:GOSUB60:IFI$<>CHR$(13)THEN12
14 RETURN
18 Z=0:FORN=1TO5:Z=Z+P(J,N,K):NEXT:J=Z:RETURN
19 PRINT" (y/n)?";:GOSUB60:RETURN
20 GOSUB3:H=RND(LV*20):PRINT"TREASURE WORTH";H;"GP";:GOSUB80:GOSUB90:GOSUB12:D(X,Y)=32:GOSUB9:RETURN
22 GOSUB3:D(X+XV,Y+YV)=120:GOSUB50:IFRND(6)>2THEN25
23 H=RND(6):N=RND(5):IFP(13,N,0)>0THENGOSUB26:PRINT" TOOK";H;"POINTS DAMAGE";:GOSUB2530:GOSUB2090
25 GOSUB9:RETURN
26 GOSUB3:GOSUB4:RETURN
28 ONVGOSUB32,34,36,37:RETURN
32 S=D(X-1,Y):RETURN
34 S=D(X+1,Y):RETURN
36 S=D(X,Y-1):RETURN
37 S=D(X,Y+1):RETURN
38 ONVGOSUB42,44,46,48:RETURN
42 D(X-1,Y)=RI:RETURN
44 D(X+1,Y)=RI:RETURN
46 D(X,Y-1)=RI:RETURN
48 D(X,Y+1)=RI:RETURN
49 FORI=1TO6:PRINTCHR$(P(I,C,0));:NEXT:RETURN
50 PRINT@X-1+(32*Y),CHR$(D(X-1,Y));:PRINT@X+(32*Y),"P";:PRINT@X+1+(32*Y),CHR$(D(X+1,Y));
55 PRINT@X+(32*(Y-1)),CHR$(D(X,Y-1));:PRINT@X+(32*(Y+1)),CHR$(D(X,Y+1));:RETURN
60 I$=INKEY$:IFI$=""THEN60
65 I=ASC(I$):RETURN
70 PRINT@W(T,0)+(32*(W(T,1))),CHR$(128);:PRINT@W(T,0)+(32*(W(T,1))),CHR$(D(W(T,0),W(T,1)));:RETURN
75 PRINT@W(T,0)+(32*(W(T,1))),"M";:RETURN
80 AL=0:FORI=1TO5:IFP(21,I,0)>0THENAL=AL+1
85 NEXT:RETURN
90 FORN=1TO5:IFP(13,N,0)>0THENP(18,N,0)=P(18,N,0)+INT(H/AL)
95 NEXT:RETURN
96 IFNM<2THENGOSUB50:MO=S:GOSUB211
98 RETURN
100 NM=NM+1:W(NM,2)=MO:W(NM,3)=RND(M(W(NM,2),4)):GOSUB3:PRINT"YOU HEAR ";M$(W(NM,2),1);:GOSUB12:GOSUB9
110 FORT=1TOW(NM,3):G(NM,T,0)=RND(6)+RND(6)+RND(6):G(NM,T,1)=0:FORD=1TOM(W(NM,2),1):G(NM,T,1)=G(NM,T,1)+RND(6)
120 NEXT:NEXT:W(NM,0)=X+MX:W(NM,1)=Y+MY:T=NM:GOSUB75:RETURN
150 FORV=1TO4:GOSUB28:IFS>11THEN160
152 RI=32:GOSUB38:IFS>0THENGOSUB96:GOTO160
155 IFRND(2)=1THENRI=255:GOSUB38:IFRND(10)=1THENRI=100:GOSUB38
160 NEXT:GOSUB50:RETURN
200 IFRND(200)>LVORNM>1THEN220
210 V=RND(4):MO=RND(11)
211 MX=0:MY=0:ONVGOSUB222,224,226,228
220 RETURN
222 IFD(X-1,Y)=32THENMX=-1:GOSUB100
223 RETURN
224 IFD(X+1,Y)=32THENMX=1:GOSUB100
225 RETURN
226 IFD(X,Y-1)=32THENMY=-1:GOSUB100
227 RETURN
228 IFD(X,Y+1)=32THENMY=1:GOSUB100
229 RETURN
230 FORT=1TONM:MX=0:MY=0:IFRND(200)>M(W(T,2),2)THEN242
232 IFW(T,0)>XTHENMX=-1
234 IFW(T,0)<XTHENMX=1
236 IFW(T,1)>YTHENMY=-1
238 IFW(T,1)<YTHENMY=1
240 IFD(W(T,0)+MX,W(T,1))<96THENGOSUB70:W(T,0)=W(T,0)+MX
241 IFD(W(T,0),W(T,1)+MY)<96THENGOSUB70:W(T,1)=W(T,1)+MY
242 GOSUB75:IFW(T,0)=XANDW(T,1)=YTHENGOSUB2000
248 NEXT:RETURN
250 FORV=1TO4:GOSUB28:IFS=255THENRI=191:GOSUB38:IFRND(10)=1THENRI=32:GOSUB38
260 NEXT:RETURN
300 GOSUB80:FORV=1TO4:GOSUB28:IFS=143THENIFRND(6)<=ALTHENRI=120:GOSUB38
320 NEXT:RETURN
350 S=1:FORN=1TO5:IFP(21,N,0)=4ORP(21,N,0)=7THENS=0:H=P(15,N,0)*5
355 NEXT:IFSTHEN390
360 GOSUB3:PRINT"REMOVE TRAPS";:GOSUB19:IFI$="Y"THENSI=120:RI=32:GOTO370
365 GOSUB3:PRINT"PICK LOCKS";:GOSUB19:IFI$="Y"THENSI=100:RI=68
370 FORV=1TO4:GOSUB28:IFS=SITHENIFRND(100)<(11+H)THENGOSUB38
380 NEXT
390 GOSUB9:RETURN
400 RO=1:FORN=1TO5:ONP(21,N,0)GOSUB450,500,440,450,450,450,450:NEXT:GOSUB9:GOSUB1990:RETURN
440 GOSUB26:PRINT"USE MAGIC ";:GOSUB19:IFI$="Y"THENGOSUB500:RETURN
450 IFP(23,N,0)<1ORP(24,N,1)<1THEN460
455 GOSUB26:PRINT" FIRE (a/s/w/z/n)? ";:GOSUB60:ONA(I)GOSUB465,470,475,480
460 RETURN
465 XV=-1:GOSUB1900:RETURN
470 XV=1:GOSUB1900:RETURN
475 YV=-1:GOSUB1900:RETURN
480 YV=1:GOSUB1900:RETURN
500 FORZ=1TO6:IFP(27+Z,N,1)<1THEN530
510 GOSUB26:PRINT" USE ";S$(P(27+Z,N,0));" ";K$(P(12+Z,N,1));:GOSUB19:IFI$="N"THEN530
520 ONP(27+Z,N,0)GOSUB1300,1350,1400,1450,1500,1550,1600,1650,1800
530 NEXT:RETURN
600 XV=-1:GOSUB32:C=S:IFS<96THENXX=-1
605 RETURN
610 XV=1:GOSUB34:C=S:IFS<96THENXX=1
615 RETURN
620 YV=-1:GOSUB36:C=S:IFS<96THENYY=-1
625 RETURN
630 YV=1:GOSUB37:C=S:IFS<96THENYY=1
635 RETURN
700 H=RND(5)-3:C=RND(5):IFRND(10)=1THENC=19
705 GOSUB3:PRINT"ITEM FOUND: ";E$(C);:GOSUB12
710 FORN=1TO5:GOSUB3:PRINT"GIVE IT TO ";:GOSUB4:GOSUB19:IFI$="N"THEN730
720 IFMID$(XC$(P(21,N,0)),C+1,1)="1"THENP(22+E(C,2),N,0)=E(C,3):P(22+E(C,2),N,1)=E(C,1)+H:N=5
730 NEXT:RETURN
750 GOSUB3:H=1+RND(2):D=RND(9):PRINT"ITEM FOUND: ";S$(D);" ";K$(H);:GOSUB12
755 FORN=1TO5:IFP(21,N,0)=2ORP(21,N,0)=3ORP(21,N,0)=6THENGOSUB3:PRINT"GIVE IT TO ";:GOSUB4:GOSUB19:IFI$="Y"THENGOSUB765:N=5
760 NEXT:RETURN
765 FORC=28TO30:IFP(C,N,0)>0THENGOSUB3:PRINT"DROP ";S$(P(C,N,0));" ";K$(P(C-15,N,1));:GOSUB19:IFI$="N"THEN775
770 P(C,N,0)=D:P(C,N,1)=RND(3):P(C-15,N,1)=H:C=30
775 NEXT:RETURN
800 GOSUB6000:GOSUB6100:GOSUB1000:X=15:Y=13:GOSUB1990:GOSUB80
905 GOSUB150:GOSUB200:ONA(C)GOSUB5,10,20,22,22:IFNM>0THENGOSUB230:IFAL=0THEN980
910 GOSUB60:GOSUB1990
945 ONA(I)GOSUB600,610,620,630,250,300,400,350,1100:X=X+XX:Y=Y+YY:IFY<14ANDY>0ANDX<31ANDX>0ANDAL>0THEN905
980 PRINT@(X-XX)+(32*(Y-YY))," ";:PRINT@X+(32*Y),"P";:GOSUB3:PRINT"TRY ANOTHER DUNGEON";:GOSUB19:IFI$="Y"THENCLS:GOTO800
990 CLS:PRINT"SAVE PARTY (y/n)?":GOSUB60:IFI$="Y"THENPRINT"PRESS play & record":CSAVE* P,"DNDPARTY"
995 END
1000 GOSUB6995:FORX=0TO31:FORY=0TO14:D(X,Y)=0:NEXT:NEXT:FORX=0TO31:D(X,0)=191:D(X,14)=191:NEXT
1007 FORY=0TO14:D(0,Y)=191:D(31,Y)=191:NEXT:D(15,14)=32:XX=0:YY=0:XV=31:YV=14:CLS0:GOSUB8
1010 GOSUB3:PRINT"LOAD A MAP";:GOSUB19:IFI$="Y"THENGOSUB3:PRINT"PRESS play";:CLOAD* D,"DUNGEON":GOTO1040
1015 FORT=1TO10:D(RND(29)+1,RND(12)+1)=143:NEXT:FORT=1TO10:D(RND(29)+1,RND(9)+1)=36:NEXT
1020 C=RND(3)+4:A=RND(31-C):B=RND(14-C):C=C-1:FORX=ATOA+C:FORY=BTOB+C:D(X,Y)=32:NEXT:NEXT
1025 FORX=ATOA+C:D(X,B)=255:D(X,B+C)=255:NEXT:FORY=BTOB+C:D(A,Y)=255:D(A+C,Y)=255:NEXT
1030 Y=B+1+RND(C-2):D(A,Y)=100:D(A+C,Y)=100:X=A+1+RND(C-2):D(X,B)=100:D(X,B+C)=100
1035 D(A+1+RND(C-2),B+1+RND(C-2))=73:D(A+1+RND(C-2),B+1+RND(C-2))=36:D(A+1+RND(C-2),B+1+RND(C-2))=RND(11)
1040 GOSUB9:FORX=1TO3:FORY=1TO5:G(X,Y,0)=0:G(X,Y,1)=0:NEXT:NEXT:D(15,13)=32:K=0:J=15:GOSUB18:LV=J:NM=0:RETURN
1100 GOSUB80:FORN=1TO5:P(17,N,0)=0:NEXT:FORN=1TOAL
1155 Z=1:FORC=1TO5:IFP(17,C,0)=0ANDP(21,C,0)>0THENGOSUB3:PRINT"MAKE ";:GOSUB49:PRINTN;:GOSUB19:IFI$="Y"THENP(17,C,0)=N:C=5:Z=0
1160 NEXT:IFZTHEN1155
1165 NEXT:GOSUB9:RETURN
1200 GOSUB50:P(27+Z,N,1)=P(27+Z,N,1)-1:IFP(27+Z,N,1)<1THENP(27+Z,N,0)=0:P(12+Z,N,1)=0
1210 Z=6:RETURN
1300 XX=RND(X):YY=RND(Y):XV=X+(31-X):YV=Y+(14-Y):GOSUB8:GOSUB1200:GOSUB1990:RETURN
1350 FORC=1TO5:GOSUB3:IFP(21,C,0)>0THENPRINT"HEAL ";:GOSUB49:PRINTP(13,C,0);:GOSUB19:IFI$="Y"THENP(13,C,0)=P(19,C,0):C=5
1360 NEXT:GOSUB1200:RETURN
1400 RI=100:FORV=1TO4:GOSUB28:IFS=68THENGOSUB38
1410 NEXT:GOSUB1200:RETURN
1450 RI=100:FORV=1TO4:GOSUB28:IFS<96THENGOSUB38
1460 NEXT:GOSUB1200:RETURN
1500 K=P(10,N,1):P(10,N,1)=99:P(23,N,0)=1:P(23,N,1)=99:P(24,N,1)=1:GOSUB450
1510 P(10,N,1)=K:P(23,N,0)=0:P(23,N,1)=0:GOSUB1200:RETURN
1550 FORV=1TO4:GOSUB28:IFS=191THENRI=32:GOSUB38
1555 GOSUB28:IFS=255THENRI=32:GOSUB38
1560 NEXT:GOSUB1200:RETURN
1600 FORXX=X-1TOX+1:FORYY=Y-1TOY+1:FORC=1TONM:IFW(C,0)=XXANDW(C,1)=YYANDW(C,2)<3ANDW(C,3)>0THENGOSUB1630
1610 NEXT:NEXT:NEXT:GOSUB1200:RETURN
1630 GOSUB3:H=RND(W(C,3)):PRINTH;M$(W(C,2),0);"S TURNED";:FORI=1TOH:G(C,I,1)=0:NEXT:T=C:GOSUB2070:GOSUB12:RETURN 
1650 PRINT@X+(32*Y)," ";:A=1+RND(29):B=1+RND(12):IFD(A,B)<>32THENPRINT@X+(32*Y),"P";:GOTO1650
1660 X=A:Y=B:GOSUB1200:RETURN
1800 FORC=1TO5:IFP(21,C,0)<1THENGOSUB3:PRINT"REVIVE ";:GOSUB49:GOSUB19:IFI$="Y"THENGOSUB1850:C=5
1810 NEXT:GOSUB1200:RETURN
1850 P(21,C,0)=P(21,C,1):P(13,C,0)=RND(3):GOSUB80:P(17,C,0)=AL:RETURN
1900 XX=X:YY=Y:MX=0:MY=0
1910 PRINT@XX+(32*YY),CHR$(D(XX,YY));:XX=XX+XV:YY=YY+YV:IFD(XX,YY)<32ORD(XX,YY)>95ORYY>13ORYY<1ORXX>30ORXX<1THEN1930
1912 PRINT@XX+(32*YY),"*";:H=0:FORT=1TONM:IFW(T,0)=XXANDW(T,1)=YYTHENMX=XX:MY=YY:D=1:GOSUB1992:GOSUB2600:GOSUB2070 
1920 NEXT:IFH=0THEN1910
1930 PRINT@XX+(32*YY),CHR$(D(XX,YY));:GOSUB50:P(24,N,1)=P(24,N,1)-1:IFMX>0THENPRINT@MX+(32*MY),"M";
1990 XX=0:YY=0:XV=0:YV=0:MX=0:MY=0:C=0:RETURN
1992 IFW(T,2)=10THENIFRND(2)=1THENO=RND(5):C=RND(3):IFP(27+C,O,0)>0THENGOSUB3:PRINT"HE'S CASTING A SPELL";:GOSUB12:GOSUB1998
1994 RETURN
1998 GOSUB3:PRINT"HE'S TAKEN A ";S$(P(27+C,O,0));" ";K$(P(12+C,O,1));:P(27+C,O,0)=0:P(12+C,O,1)=0:P(27+C,O,1)=0:GOSUB12
1999 RETURN
2000 RO=0:FORD=1TO2:FORN=1TO5:IFP(17,N,0)<>DTHEN2050
2010 IFG(T,D,0)>P(10,N,0)THENGOSUB2500:GOSUB2600:GOTO2050
2020 GOSUB2600:GOSUB2500
2050 NEXT:NEXT:GOSUB2090
2070 FORI=1TOW(T,3):IFG(T,I,1)<1ANDG(T,I,0)>0THENGOSUB2200
2075 NEXT:IFW(T,3)<1THENGOSUB70:GOSUB50:FORO=TTONM-1:FORI=0TO3:W(O,I)=W(O+1,I):NEXT:NEXT:NM=NM-1:GOSUB2085:T=T+1:MX=0
2080 RETURN
2085 FORO=1TOW(T,3):FORI=0TO1:G(T,O,I)=G(T+1,O,I):G(T+1,O,I)=0:NEXT:NEXT:RETURN
2090 FORI=1TO5:IFP(13,I,0)<1ANDP(17,I,0)>0THENGOSUB2300:P(17,I,0)=0
2095 NEXT:GOSUB80:GOSUB9:RETURN
2200 FORO=ITOW(T,3)-1:G(T,O,0)=G(T,O+1,0):G(T,O,1)=G(T,O+1,1):NEXT:G(T,O,0)=0:G(T,O,1)=0:W(T,3)=W(T,3)-1:RETURN
2300 FORO=1TO5:IFP(17,O,0)>P(17,I,0)THENP(17,O,0)=P(17,O,0)-1
2320 NEXT:RETURN
2500 GOSUB3:H=0:IFG(T,D,1)<1ORP(13,N,0)<1THEN2590
2505 IFRND(20)>(20-P(14,N,0)-M(W(T,2),0))THENH=RND(M(W(T,2),3)):GOSUB4:PRINT" HIT FOR";H;"POINTS DAMAGE";:GOTO2530
2510 PRINT"THE ";M$(W(T,2),0);" MISSED ";:GOSUB4
2530 GOSUB12:GOSUB3:P(13,N,0)=P(13,N,0)-H:IFP(13,N,0)<1THENP(21,N,0)=0:GOSUB4:PRINT" IS DEAD";:GOSUB12
2590 RETURN
2600 GOSUB3:H=0:IFP(13,N,0)<1ORG(T,D,1)<1THEN2690
2605 IFRND(20+P(10,N,1))>(19-M(W(T,2),0))THENH=RND(P(22+RO,N,1)-P(7,N,1)):GOSUB4:PRINT" HIT THE ";M$(W(T,2),0);:GOTO2630
2610 GOSUB4:PRINT" MISSED THE ";M$(W(T,2),0);
2630 GOSUB12:GOSUB3:G(T,D,1)=G(T,D,1)-H:IFG(T,D,1)<1THENPRINT"THE ";M$(W(T,2),0);" IS DEAD";:GOSUB2700:GOSUB12
2690 RETURN
2700 P(16,N,0)=P(16,N,0)+M(W(NM,2),1)*10:RETURN
6000 PRINT"LOAD PARTY (y/n)?"
6010 I$=INKEY$:I=RND(0):IFI$=""THEN6010
6020 IFI$="Y"THENPRINT"PRESS play":CLOAD* P,"DNDPARTY"
6030 RETURN
6080 P(13,N,0)=P(13,N,0)+RND(CL(P(21,N,0))+P(11,N,1)):RETURN
6100 FORN=1TO5:IFP(1,N,0)>0THEN6160
6105 I=P(17,N,0):FORT=1TO33:P(T,N,0)=0:P(T,N,1)=0:NEXT:FORT=7TO12:P(T,N,0)=RND(6)+RND(6)+RND(6):NEXT
6120 H=P(7,N,0):IF P(7,N,0)>12THENP(21,N,0)=1
6122 IFP(8,N,0)>8ANDP(8,N,0)>HTHENP(21,N,0)=2:H=P(8,N,0)
6124 IFP(9,N,0)>8ANDP(9,N,0)>HTHENP(21,N,0)=3:H=P(9,N,0)
6126 IFP(10,N,0)>8ANDP(10,N,0)>HTHENP(21,N,0)=4:H=P(10,N,0)
6128 IFP(11,N,0)>8ANDP(7,N,0)>8ANDP(11,N,0)>H THEN P(21,N,0)=5:H=P(11,N,0)
6130 IFP(12,N,0)>8ANDP(7,N,0)>8ANDP(8,N,0)>8 AND P(12,N,0)>HTHENP(21,N,0)=6:H=P(12,N,0)
6132 IFP(11,N,0)>12ANDP(10,N,0)>12THENP(21,N,0)=7:P(10,N,1)=1
6140 IFP(21,N,0)=0THENP(21,N,0)=RND(7)
6142 IFP(21,N,0)=2ORP(21,N,0)=3THENP(20,N,1)=1
6143 IFP(21,N,0)=3THENP(31,N,0)=7:P(31,N,1)=60000:P(16,N,1)=4
6145 P(21,N,1)=P(21,N,0):P(15,N,0)=1:FORC=7TO11:P(C,N,1)=AD(INT(P(C,N,0)/3)):NEXT:GOSUB6080
6150 P(19,N,0)=P(13,N,0):P(12,N,1)=LV(P(21,N,0)):P(18,N,0)=(RND(6)+RND(6)+RND(6))*5:P(17,N,0)=N:IFI>0THENP(17,N,0)=I
6160 GOSUB7000:GOSUB3:PRINT"ROLL AGAIN";:GOSUB19:IFI$="Y"THEN6105
6180 IFP(1,N,0)=0THENPRINT@416,"";:INPUT"NAME (6 CHAR MAX)";I$:I$=I$+"      ":FORT=1TO6:P(T,N,0)=ASC(MID$(I$,T,1)):NEXT
6200 GOSUB3:PRINT"BUY ARMS & ARMOUR";:GOSUB19:IFI$="N"THEN6300
6210 FORC=0TO17:IFMID$(XC$(P(21,N,0)),C+1,1)="0"ORP(18,N,0)<E(C,0)THEN6240
6215 GOSUB3:PRINT"BUY ";E$(C);" FOR";E(C,0)"GP";:GOSUB19:IFI$="N"ORP(18,N,0)<E(C,0)THEN6240
6220 P(18,N,0)=P(18,N,0)-E(C,0):P(22+E(C,2),N,0)=E(C,3):P(22+E(C,2),N,1)=E(C,1):GOSUB7000
6240 NEXT
6300 IFP(20,N,1)=0ORP(21,N,0)=0THEN6390
6305 GOSUB3:PRINT"LEARN NEW SPELLS";:GOSUB19:IFI$="N"THEN6390
6310 FORT=1TOP(20,N,1):IFP(21,N,0)=3ANDT=1THEN6350
6315 FORC=1TO9:GOSUB3:PRINT"LEARN ";S$(C);:GOSUB19:IFI$="N"THEN6340
6330 P(30+T,N,0)=C:P(30+T,N,1)=1:P(15+T,N,1)=1:C=9
6340 NEXT
6350 NEXT
6390 NEXT:RETURN
6995 CLS:PRINT"PREPARE TO ENTER THE DUNGEON...":RETURN
7000 CLS:PRINT"NM: ";:GOSUB4:PRINT"",L$(P(21,N,0))
7010 P(14,N,0)=(9+P(25,N,1)+P(26,N,1)+P(27,N,1))-P(10,N,1):P(13,N,0)=P(19,N,0):IFP(16,N,0)+P(18,N,0)>P(12,N,1)THENGOSUB7050
7020 FORT=1TO6:PRINTAB$(T);":";P(6+T,N,0);P(6+T,N,1),E$(P(21+T,N,0)+EO(T));P(21+T,N,1):NEXT
7030 FORT=7TO12:PRINTAB$(T);":";P(6+T,N,0),S$(P(21+T,N,0));" ";K$(P(6+T,N,1)):NEXT:RETURN
7050 P(15,N,0)=P(15,N,0)+1:P(18,N,0)=P(18,N,0)-(P(12,N,1)-P(16,N,0)):P(16,N,0)=0
7060 IFP(15,N,0)>3THENP(15,N,0)=3:GOTO7080
7070 IFP(21,N,0)=2ORP(21,N,0)=3ORP(21,N,0)=6THENP(20,N,1)=P(20,N,1)+1
7080 RETURN
7100 FORT=1TO9:READC:A(C)=T:NEXT:FORT=1TO5:READC:A(C)=T:NEXT
7105 FORT=1TO12:READAB$(T):NEXT:FORT=1TO6:READEO(T):NEXT:FORT=0TO7:READL$(T),CL(T):NEXT:FORT=1TO7:READXC$(T):NEXT
7110 FORT=0TO19:READE$(T):FORC=0TO3:READE(T,C):NEXT:NEXT:FORT=1TO4:READK$(T):NEXT
7150 FORT=1TO9:READS$(T):NEXT:FORT=1TO11:READM$(T,0),M$(T,1):FORC=0TO4:READM(T,C):NEXT:NEXT
7160 FORT=1TO6:READAD(T):NEXT:FORT=1TO7:READLV(T):NEXT:MC=16895:RETURN
7800 DATA65,83,87,90,67,82,77,84,79,100,73,36,120,143
7805 DATA"ST","IN","WI","DX","CO","CA","HP","AC","LV","EX","MO","$$",0,6,9,12,16,18
7810 DATA"DEAD",0,"FIGHTER",8,"MAGIC USER",4,"CLERIC",6,"THIEF",4,"DWARF",8,"ELF",6,"HALFLING",6
7820 DATA"01111101101101110101","01010000000000000000","01100001001001110101","01010001101101000000"
7830 DATA"01111101101101110101","01111101101101110101","01110001101101100101"
7850 DATA"MAIN WEAPON",0,0,0,0,"STAFF",3,4,0,1,"MACE",5,6,0,2,"DAGGER",3,4,0,3,"AXE",7,8,0,4,"SWORD",15,8,0,5
7855 DATA"RANGED",0,0,1,0,"SLING",5,4,1,1,"BOW",25,6,1,2,"MISSILES",0,0,2,0,"STONES",2,20,2,1,"ARROWS",5,20,2,2
7860 DATA"CLOAK",0,0,3,0,"LEATHER",15,-2,3,1,"CHAIN MAIL",30,-4,3,2,"PLATE",50,-6,3,3
7861 DATA"NO SHIELD",0,0,4,0,"SHIELD",10,-1,4,1,"HAT",0,0,5,0,"HELMET",10,-1,5,1
7862 DATA"SPELL","RING","WAND","UNDEAD","REVEAL","HEALING","LOCKING","SHIELD","MISSILE","BLAST","TURN","APARATE","REVIVE"
8000 DATA"SKELETON","CLICK CLACKS",7,1,60,6,5,"ZOMBIE","'BRAINS!'",8,2,120,8,5,"KOBOLD","BARKS",7,1,60,4,5
8010 DATA"GIANT BAT","FLAPPING",6,2,180,4,5,"ORC","GRUNTS",6,1,120,6,5,"GOBLIN","MUTTERING",6,1,60,6,5
8020 DATA"BERSERKER","SHOUTING",7,1,120,8,5,"STIRGE","BUZZING",7,1,180,3,5,"GIANT SPIDER","CLICK CLICK",7,2,120,8,1
8030 DATA"WIZARD","WHISPERING",2,3,120,10,1,"MONSTER JELLY","BLOOP BLOOP",8,4,60,8,1
8040 DATA-2,-1,0,1,2,3,1500,2500,1500,1200,2200,4000,2000
