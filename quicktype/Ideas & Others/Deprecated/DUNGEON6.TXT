0 GOSUB6995:DIM D(31,14),A(255),P(33,5,1),CL(7,1),MW(5,1),RW(1,1),RA(1,1),AR(3,1),HL(1,1),SH(1,1),X,Y,A,B,C,R,XX,YY,XV,YV,MX,MY
1 DIM CL$(7),MW$(5),RW$(1),RA$(1),AR$(3),HL$(1),SH$(1),S$(11),M$(11,2),MI$(7),WM(5,5),M(11,6),MP(6,1),I$,NM,I,D,O,AL,V,S,SI,RI
2 DIM EQ(6):GOSUB7100:GOTO800
3 PRINT@480,"                               ";:POKE16895,96:PRINT@480,"";:RETURN
4 FORI=1TO6:PRINTCHR$(P(I,N,0));:NEXT:RETURN
5 GOSUB3:PRINT@480,"smash door";:IFD(X+XV,Y)=100THENIFRND(6)<3THEND(X+XV,Y)=32:X=X+XV:GOTO7
6 IFD(X,Y+YV)=100THENIFRND(6)<3THEND(X,Y+YV)=32:Y=Y+YV
7 GOSUB50:RETURN
8 CLS0:FORB=0TO14:FORA=0TO31:PRINT@A+(32*B),CHR$(D(A,B));:NEXT:NEXT
9 GOSUB3:PRINT"SeCRETS? TrAPS? mAGIC&BOW tHIEF";:RETURN
10 GOSUB3:PRINT"object";:RETURN
20 GOSUB3:PRINT"treasure";:RETURN
25 GOSUB3:PRINT"trap sprung";:D(X+XV,Y+YV)=120:GOSUB50:RETURN
28 ONVGOSUB32,34,36,37
30 RETURN
32 S=(D(X-1,Y)=SI):RETURN
34 S=(D(X+1,Y)=SI):RETURN
36 S=(D(X,Y-1)=SI):RETURN
37 S=(D(X,Y+1)=SI):RETURN
38 ONVGOSUB42,44,46,48
40 RETURN
42 D(X-1,Y)=RI:RETURN
44 D(X+1,Y)=RI:RETURN
46 D(X,Y-1)=RI:RETURN
48 D(X,Y+1)=RI:RETURN
50 PRINT@X-1+(32*Y),CHR$(D(X-1,Y));:PRINT@X+(32*Y),"P";:PRINT@X+1+(32*Y),CHR$(D(X+1,Y));
55 PRINT@X+(32*(Y-1)),CHR$(D(X,Y-1));:PRINT@X+(32*(Y+1)),CHR$(D(X,Y+1));:RETURN
60 I$=INKEY$:IFI$=""THEN60
65 I=ASC(I$):RETURN
70 PRINT@WM(T,0)+(32*(WM(T,1))),CHR$(D(WM(T,0),WM(T,1)));:RETURN
75 PRINT@WM(T,0)+(32*(WM(T,1))),"M";:RETURN
80 AL=0:FORI=1TO5:IFP(13,I,0)>0THENAL=AL+1
85 NEXT:RETURN
100 WM(NM,2)=RND(11):WM(NM,3)=RND(M(WM(NM,2),4))
110 FORT=1TOWM(NM,3):MP(T,0)=RND(6)+RND(6)+RND(6):MP(T,1)=0:FORD=1TOM(WM(NM,2),1):MP(T,1)=MP(T,1)+RND(6):NEXT:NEXT:RETURN
200 FORV=1TO4:SI=0:GOSUB28:IFSTHENRI=32:GOSUB38:IFRND(2)=1THENRI=255:GOSUB38:IFRND(10)=1THENRI=100:GOSUB38
202 NEXT
208 IFRND(50)<50ORNM>4THEN226
210 V=RND(4):ONVGOTO212,216,220,224
212 IFD(X-1,Y)=32THENNM=NM+1:WM(NM,0)=X-1:WM(NM,1)=Y:GOSUB100:GOTO226
216 IFD(X+1,Y)=32THENNM=NM+1:WM(NM,0)=X+1:WM(NM,1)=Y:GOSUB100:GOTO226
220 IFD(X,Y-1)=32THENNM=NM+1:WM(NM,0)=X:WM(NM,1)=Y-1:GOSUB100:GOTO226
224 IFD(X,Y+1)=32THENNM=NM+1:WM(NM,0)=X:WM(NM,1)=Y+1:GOSUB100:GOTO226
226 RETURN
230 FORT=1TONM:IFRND(3)<3THEN242
232 MX=0:MY=0:IFWM(T,0)>XTHENMX=-1:GOTO236
234 IFWM(T,0)<XTHENMX=1
236 IFWM(T,1)>YTHENMY=-1:GOTO240
238 IFWM(T,1)<YTHENMY=1
240 IFD(WM(T,0)+MX,WM(T,1))>31ANDD(WM(T,0)+MX,WM(T,1))<96THENGOSUB70:WM(T,0)=WM(T,0)+MX
241 IFD(WM(T,0),WM(T,1)+MY)>31ANDD(WM(T,0),WM(T,1)+MY)<96THENGOSUB70:WM(T,1)=WM(T,1)+MY
242 GOSUB75:IFWM(T,0)=XANDWM(T,1)=YTHENGOSUB2000
248 NEXT:RETURN
250 GOSUB3:PRINT"searching for secret passages";
255 FORV=1TO4:SI=255:RI=191:GOSUB28:IFSTHENGOSUB38:IFRND(10)=1THENRI=32:GOSUB38
260 NEXT:RETURN
300 GOSUB3:GOSUB3:PRINT"searching for traps";
310 FORV=1TO4:SI=143:GOSUB28:IFSTHENIFRND(6)<3THENRI=120:GOSUB38
320 NEXT:RETURN
350 GOSUB3:PRINT"picking locks";
355 FORV=1TO4:SI=100:GOSUB28:IFSTHENIFRND(100)<=15THENRI=68:GOSUB38
360 NEXT:RETURN
400 FORN=1TO5:ONP(21,N,0)GOSUB450,500,450,450,450,450,450
406 NEXT:RETURN
450 IFP(24,N,0)<1THEN499
451 GOSUB3:GOSUB4:PRINT" FIRE (A,S,W,Z,N)? ";:GOSUB60:IFI$="N"THEN499
452 IFI$="W"THENYV=-1:GOSUB1900
454 IFI$="Z"THENYV=1:GOSUB1900
456 IFI$="S"THENXV=1:GOSUB1900
458 IFI$="A"THENXV=-1:GOSUB1900
499 RETURN
500 PRINT" USE";
502 FORT=28TO33:IFP(28,N,1)>0THENPRINT@491,S$(P(28,N,0));MI$(P(13,N,1));" (Y/N)?":GOSUB60:IFI$="Y"THENSP=P(28,N,0)
504 ONSPGOSUB1300,1350,1400,1450,1500,1550,1600,1650,1700,1750,1800
504 NEXT
549 RETURN
800 GOSUB6000:GOSUB6100:GOSUB6995:FORX=0TO31:FORY=0TO14:D(X,Y)=0:NEXT:NEXT:GOSUB1000
900 X=15:Y=13:D(X,Y)=32:PRINT@32+X+(32*Y)," ";:D(X,Y+1)=32:C=0:NM=0:XX=0:YY=0:XV=0:YV=0:GOSUB80
905 GOSUB200:GOSUB50:ONA(C)GOSUB5,10,20,25
906 IFNM>0THENGOSUB230:IFAL=0THEN980
910 GOSUB60:XX=0:YY=0:XV=0:YV=0:C=0
920 IFI$="W"THENC=D(X,Y-1):YV=-1:IFC>31ANDC<96THENYY=-1:GOTO970
930 IFI$="Z"THENC=D(X,Y+1):YV=1:IFC>31ANDC<96THENYY=1:GOTO970
935 IFI$="S"THENC=D(X+1,Y):XV=1:IFC>31ANDC<96THENXX=1:GOTO970
940 IFI$="A"THENC=D(X-1,Y):XV=-1:IFC>31ANDC<96THENXX=-1:GOTO970
945 ONA(I)GOSUB250,400,300,350,9
970 X=X+XX:Y=Y+YY:IFY<14THEN905
980 PRINT@X+(32*Y)-32," ";:PRINT@X+(32*Y),"P";:SOUND 1,1
982 GOSUB3:PRINT"TRY ANOTHER DUNGEON (Y/N)?";
984 GOSUB60:IFI$="Y"THENCLS:GOTO800
988 IFI$="N"THENCLS:GOSUB6050:END
990 GOTO984
1000 FORX=0TO31:D(X,0)=191:D(X,14)=191:NEXT:FORY=0TO14:D(0,Y)=191:D(31,Y)=191:NEXT:REM GOSUB8
1002 FORT=1TO10:D(RND(29)+1,RND(12)+1)=143:NEXT
1003 FORT=1TO10:D(RND(29)+1,RND(12)+1)=73:NEXT
1005 R=RND(3)+4:A=RND(31-R):B=RND(14-R):R=R-1:FORX=ATOA+R:FORY=BTOB+R:D(X,Y)=32:NEXT:NEXT
1008 FORX=ATOA+R:D(X,B)=255:D(X,B+R)=255:NEXT
1009 FORY=BTOB+R:D(A,Y)=255:D(A+R,Y)=255:NEXT
1010 Y=B+1+RND(R-2):X=A:D(X,Y)=100:X=A+R:D(X,Y)=100:X=A+1+RND(R-2):Y=B:D(X,Y)=100:Y=B+R:D(X,Y)=100
1011 X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=73:X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=36
1099 GOSUB8:REM ** REMOVE **
1200 RETURN
1300 REM SPELL 1
1349 RETURN
1350 REM SPELL 2
1399 RETURN
1400 REM SPELL 3
1449 RETURN
1450 REM SPELL 4
1499 RETURN
1500 REM SPELL 5
1549 RETURN
1550 REM SPELL 6
1599 RETURN
1600 REM SPELL 7
1649 RETURN
1650 REM SPELL 8
1699 RETURN
1700 REM SPELL 9
1749 RETURN
1750 REM SPELL 10
1799 RETURN
1800 REM SPELL 11
1899 RETURN
1900 XX=X:YY=Y:MX=0:MY=0:IFP(24,N,1)<1THEN1990
1910 PRINT@XX+(32*YY),CHR$(D(XX,YY));:XX=XX+XV:YY=YY+YV:IFD(XX,YY)<32ORD(XX,YY)>95THEN1930
1912 PRINT@XX+(32*YY),"*";:H=0:FORT=1TONM:IFWM(T,0)=XXANDWM(T,1)=YYTHENMX=XX:MY=YY:D=1:GOSUB2600:GOSUB2070
1920 NEXT:IFH=0THEN1910
1930 PRINT@XX+(32*YY),CHR$(D(XX,YY));:PRINT@X+(32*Y),"P";
1940 IFMX>0THENPRINT@MX+(32*MY),"M";
1990 XX=0:YY=0:XV=0:YV=0:MX=0:MY=0:RETURN
2000 FORD=1TO2:FORN=1TO5:IFP(19,N,0)<>DTHEN2050
2010 IFMP(D,0)>P(10,N,0)THENGOSUB2500:GOSUB2600:GOTO2050
2020 GOSUB2600:GOSUB2500
2050 NEXT:NEXT
2055 FORI=1TO5:IFP(13,I,0)<1ANDP(19,I,0)>0THENGOSUB2300:P(19,I,0)=0
2060 NEXT:GOSUB80:GOSUB9
2070 FORI=1TOWM(T,3):IFMP(I,1)<1THENGOSUB2200
2075 NEXT:IFWM(T,3)<1THENGOSUB70:GOSUB50:FORO=TTOWM(T,3)-1:FORI=1TO4:WM(O,I)=WM(O+1,I):NEXT:NEXT:NM=NM-1
2080 RETURN
2200 FORO=ITOWM(T,3)-1:MP(O,0)=MP(O+1,0):MP(O,1)=MP(O+1,1):NEXT:MP(O,0)=0:MP(O,1)=0:WM(T,3)=WM(T,3)-1
2220 RETURN
2300 FORO=1TO5:IFP(19,O,0)>P(19,I,0)THENP(19,O,0)=P(19,O,0)-1
2320 NEXT:RETURN
2500 GOSUB3:H=0:IFMP(D,1)<1ORP(13,N,0)<1THEN2590
2505 IFRND(20)>(20-P(15,N,0)-M(WM(T,2),0))THENH=RND(M(WM(T,2),3)):GOSUB4:PRINT" HIT FOR";H;"POINTS DAMAGE";:GOTO2530
2510 PRINT"THE ";M$(WM(T,2),0);" MISSED ";:GOSUB4
2530 GOSUB60:GOSUB3:P(13,N,0)=P(13,N,0)-H:IFP(13,N,0)<1THENP(21,N,0)=0:GOSUB4:PRINT" IS DEAD";:GOSUB60
2590 RETURN
2600 GOSUB3:H=0:IFP(13,N,0)<1ORMP(D,1)<1THEN2690
2605 IFRND(20)>(19-M(WM(T,2),0))THENH=RND(MW(P(22,N,0),1)):GOSUB4:PRINT" HIT THE ";M$(WM(T,2),0);:GOTO2630
2610 GOSUB4:PRINT" MISSED THE ";M$(WM(T,2),0);
2630 GOSUB60:GOSUB3:MP(D,1)=MP(D,1)-H:IFMP(D,1)<1THENPRINT"THE ";M$(WM(T,2),0);" IS DEAD";:MX=0:GOSUB60
2690 RETURN
6000 PRINT"LOAD PARTY (Y/N)?"
6010 GOSUB60:IFI$="Y"THENPRINT"PRESS play":CLOAD* P,"DNDPARTY"
6020 RETURN
6050 PRINT"SAVE PARTY (Y/N)?"
6060 GOSUB60:IFI$="Y"THENPRINT"PRESS play & record":CSAVE* P,"DNDPARTY"
6070 RETURN
6100 FORN=1TO5:IFP(1,N,0)>0THEN6160
6105 FORT=1TO33:P(T,N,0)=0:NEXT
6110 FORT=7TO12:P(T,N,0)=RND(6)+RND(6)+RND(6):NEXT
6120 H=P(7,N,0):IF P(7,N,0)>12THENP(21,N,0)=1
6122 IFP(8,N,0)>8ANDP(8,N,0)>HTHENP(21,N,0)=2:H=P(8,N,0)
6124 IFP(9,N,0)>8ANDP(9,N,0)>HTHENP(21,N,0)=3:H=P(9,N,0)
6126 IFP(10,N,0)>8ANDP(10,N,0)>HTHENP(21,N,0)=4:H=P(10,N,0)
6128 IFP(11,N,0)>8ANDP(7,N,0)>8ANDP(11,N,0)>H THEN P(21,N,0)=5:H=P(11,N,0)
6130 IFP(12,N,0)>8ANDP(7,N,0)>8ANDP(8,N,0)>8 AND P(12,N,0)>HTHENP(21,N,0)=6:H=P(12,N,0)
6132 IFP(11,N,0)>12ANDP(10,N,0)>12THENP(21,N,0)=7
6140 IFP(21,N,0)=0THENP(21,N,0)=RND(7)
6145 P(21,N,1)=P(21,N,0)
6150 P(17,N,0)=1:P(13,N,0)=RND(CL(P(21,N,0),0))
6152 P(19,N,0)=N
6155 P(20,N,0)=(RND(6)+RND(6)+RND(6))*5
6160 GOSUB7000
6162 PRINT"ROLL AGAIN (Y/N)?"
6164 GOSUB60
6166 IFI$="Y"THEN6105
6168 IFI$="N"THEN6180
6170 GOTO6164
6180 IFP(1,N,0)=0THENINPUT"NAME (6 CHAR MAX)";I$:I$=I$+"      ":FORT=1TO6:P(T,N,0)=ASC(MID$(I$,T,1)):NEXT
6200 FORT=REM ** PURCHASE ITEMS **
6210 P(22,N,0)=1:P(23,N,0)=1
6212 P(24,N,0)=1:P(24,N,0)=1:P(24,N,1)=6
6300 REM ** PICK SPELLS **
6800 P(15,N,0)=9+AR(P(25,N,0),1)+HL(P(26,N,0),1)+AR(P(27,N,0),1)
6810 NEXT
6990 RETURN
6995 CLS:PRINT"PREPARE TO ENTER THE DUNGEON...":RETURN
7000 CLS:PRINT"NM: ";:GOSUB4:PRINT"",CL$(P(21,N,0))
7008 PRINT"ST:";P(7,N,0);P(7,N,1),MW$(P(22,N,0));P(22,N,1)
7012 PRINT"IN:";P(8,N,0);P(8,N,1),RW$(P(23,N,0));P(23,N,1)
7014 PRINT"WI:";P(9,N,0);P(9,N,1),RA$(P(24,N,0));P(24,N,1)
7016 PRINT"DX:";P(10,N,0);P(10,N,1),AR$(P(25,N,0));P(25,N,1)  
7018 PRINT"CO:";P(11,N,0);P(11,N,1),HL$(P(26,N,0));P(26,N,1)
7020 PRINT"CA:";P(12,N,0);P(12,N,1),SH$(P(27,N,0));P(27,N,1)
7022 PRINT"HP:";P(13,N,0),S$(P(28,N,0));MI$(P(13,N,1))
7024 PRINT"AC:";P(15,N,0),S$(P(29,N,0));MI$(P(14,N,1))  
7026 PRINT"LV:";P(17,N,0),S$(P(30,N,0));MI$(P(15,N,1)) 
7028 PRINT"EX:";P(18,N,0),S$(P(31,N,0));MI$(P(16,N,1))
7030 PRINT"MO:";P(19,N,0),S$(P(32,N,0));MI$(P(17,N,1))
7032 PRINT"$$:";P(20,N,0),S$(P(33,N,0));MI$(P(18,N,1)) 
7090 RETURN
7100 A(100)=1:A(73)=2:A(36)=3:A(120)=4:A(143)=4:A(69)=1:A(77)=2:A(82)=3:A(84)=4:A(72)=5
7105 FORT=1TO7:READCL$(T),CL(T,0):NEXT:CL$(0)="DEAD":FORT=1TO5:READMW$(T),MW(T,0),MW(T,1):NEXT:MW$(0)="NO WEAPON"
7115 RW$(1)="BOW":RW(1,0)=25:RW(1,1)=4:RW$(0)="":RA$(1)="ARROWS":RA(1,0)=.5:RA(1,1)=4:RA$(0)=""
7120 FORT=1TO3:READAR$(T),AR(T,0),AR(T,1):NEXT:AR$(0)="CLOAK":HL$(1)="HELMET":HL(1,0)=10:HL(1,1)=-1:HL$(0)="HAT"
7130 SH$(1)="SHIELD":SH(1,0)=10:SH(1,1)=-1:SH$(0)="":FORT=1TO7:READMI$(T):NEXT:MI$(0)=""
7150 FORT=1TO11:READS$(T):NEXT:S$(0)="":FORT=1TO6:READEQ(T):NEXT
7160 FORT=1TO11:READM$(T,0),M$(T,1),M$(T,2),M(T,0),M(T,1),M(T,2),M(T,3),M(T,4),M(T,5),M(T,6):NEXT
7190 RETURN
7800 DATA"FIGHTER",8,"MAGIC USER",4,"CLERIC",6,"THIEF",4,"DWARF",8,"ELF",6,"HALFLING",6
7850 DATA"STAFF",3,4,"MACE",5,6,"DAGGER",3,4,"AXE",7,8,"SWORD",15,8
7860 DATA"LEATHER",15,-2,"CHAIN MAIL",30,-4,"PLATE",50,-6
7862 DATA"SPELL","RING","WAND","SCROLL","UNDEAD","DOOR","TRAPS"
7900 DATA"HEALING","LOCK","UNLOCK","MISSILE","BLAST","TURN","STONE","SECRETS","REMOVE","FREEZE","REVIVE"
7990 DATA5,1,1,3,1,1
8000 DATA"KOBOLD","BARKS","",7,1,60,4,5,0,0
8002 DATA"GOBLIN","MUTTERING","",6,1,60,6,5,0,0
8004 DATA"GIANT BAT","FLAPPING","",6,2,180,4,5,0,0
8006 DATA"ORC","GRUNTS","",6,1,120,6,5,0,0
8008 DATA"SKELETON","CLICK CLACKS","",7,1,60,6,5,0,1
8010 DATA"BERSERKER","SHOUTING","",7,1,120,8,5,0,0
8012 DATA"STIRGE","BUZZING","",7,1,180,3,5,0,0
8014 DATA"ZOMBIE","'BRAINS!'","",8,2,120,8,5,0,1
8016 DATA"GIANT SPIDER","CLICK CLICK",POISON",7,2,120,8,1,1,0
8018 DATA"WIZARD","WHISPERING","SPELL",2,3,120,10,1,2,0
8020 DATA"MONSTER JELLY","BLOOP BLOOP","WEAPON LOST",8,2,60,8,3,0,0
