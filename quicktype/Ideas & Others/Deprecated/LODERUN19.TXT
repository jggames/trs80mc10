0 CLS:DIMK(255),M(88),P(31,15),X(5),Y(5),D(5,5),A(5),B(5),N(5),F(5),C(5),U(31),V(63),T(5),W(2),W2(4),H(15),I(15),LV,LC,OL
1 DIMA,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,S,T,U,V,W,X,Y,Z,MC,NX,NY,NT,I$,A$:GOSUB100:GOSUB800:GOTO50
2 RETURN
3 M=C(T):GOTO7
4 C(T)=W(RND(2)):M=C(T):GOTO7
5 ONK(N(1))GOTO2,2,2,2,8:RETURN
6 P=PEEK(P(A(T),B(T)+Y(M))):ONK(P)GOTO9,3,15,9,9,42,3,9,9,61
7 P=PEEK(P(A(T)+X(M),B(T))):ONK(P)GOTO9,12,15,9,9,42,12,9,9,61
8 P=PEEK(P(A(T)+X(M),B(T)+Y(M))):ONK(P)GOTO9,2,15,9,9,42,2,9,9,61
9 POKEP(A(T),B(T)),N(T):N(T)=P:A(T)=A(T)+X(M):B(T)=B(T)+Y(M):POKEP(A(T),B(T)),D(M,T)
10 F(T)=PEEK(P(A(T),B(T)+1)):ONK(N(T))GOTO11,2,2,2,11:RETURN
11 F(T)=2:RETURN
12 C(T)=W2(M):M=C(T):P=PEEK(P(A(T)+X(M),B(T))):ONK(P)GOTO9,2,15,9,9,42,2,9,9,61
13 M=5:GOSUB8:GOTO22
14 M=5:GOSUB6:NEXT:NEXT:GOSUB43:NEXT:GOTO50
15 P=G:G(T)=G(T)+1:SOUND100,1:ON1-(G(T)=W)GOSUB9,60:RETURN
16 ONK(N(T))GOSUB3,3,3,3,6,42,3:NEXT:NEXT:GOSUB43:NEXT:GOTO50
17 GOSUB6:NEXT:NEXT:GOSUB43:NEXT:GOTO50
18 GOSUB7:ONRND(4)GOTO22:NEXT:NEXT:GOSUB43:NEXT:GOTO50
20 FORZ=1TO64000:FORS=1TOV:FORC=2TOF:T=1:ONK(F(T))GOTO13,21,21,13,21,21,21,38,39
21 M=K(PEEK(K)ANDPEEK(2)):ONMGOSUB25,8,5,8,8,27
22 T=C:ONK(F(T))GOTO14,23,23,14,23,14,23,30,31
23 M=U(L+(B(T)-B(1))):ONMGOTO24,24,16,24,17:M=V(N+(A(T)-A(1))):ONMGOTO24,18,24,18,24
24 NEXT:NEXT:GOSUB43:NEXT:GOTO50
25 POKEP(A(T),B(T)),141:P=PEEK(P(A(T)-1,B(T))):ON1-((P=GORP=Q)ANDPEEK(P(A(T)-1,B(T)+1))=175)GOTO2
26 S=1:O=O+1:H(O)=A(T)-1:I(O)=B(T)+1:POKEP(H(O),I(O)),Q:ONOGOTO2,2,2,2,2,2,2,2,2,2,2,2,2,2:O=0:RETURN
27 POKEP(A(T),B(T)),142:P=PEEK(P(A(T)+1,B(T))):ON1-((P=GORP=Q)ANDPEEK(P(A(T)+1,B(T)+1))=175)GOTO2
28 S=1:O=O+1:H(O)=A(T)+1:I(O)=B(T)+1:POKEP(H(O),I(O)),Q:ONOGOTO2,2,2,2,2,2,2,2,2,2,2,2,2,2:O=0:RETURN
29 NEXT:RETURN
30 ON1-(G(T)>0)GOSUB36,37:POKEP(A(T),B(T)+1),190:F(T)=144:NEXT:NEXT:GOSUB43:NEXT:GOTO50
31 POKEP(A(T),B(T)+1),RND(2)+D:T(T)=T(T)+1:ON1-(T(T)<E)GOTO32:NEXT:NEXT:GOSUB43:NEXT:GOTO50
32 P=PEEK(P(A(T),B(T))):ONK(P)GOSUB2,2,35,34,2,41,2,2,2:NEXT:NEXT:GOSUB43:NEXT:GOTO50
33 FORJ=1TO15:ON1-(H(J)=A(T)ANDI(J)=B(T)+1)GOTO29:I(J)=1:J=15:NEXT:RETURN
34 POKEP(A(T),B(T)+1),175:POKEP(A(T),B(T)),190:F(T)=175:T(T)=0:SOUND1,1:GOTO33
35 POKEP(A(T),B(T)+1),175:POKEP(A(T),B(T)),190:F(T)=175:T(T)=0:SOUND200,1:G(T)=G(T)+1:GOTO33
36 POKEP(A(T),B(T)),G:RETURN
37 POKEP(A(T),B(T)),146:G(T)=G(T)-1:RETURN
38 POKEP(A(T),B(T)),N(T):B(T)=B(T)+1:ON1-(B(T)<15)GOTO54:POKEP(A(T),B(T)),D(M,T):F(T)=PEEK(P(A(T),B(T)+1)):N(T)=Q:GOTO22
39 POKEP(A(T),B(T)),D(M,T):F(T)=144:GOTO22
40 PRINT@0,"YOU GOT CAUGHT!";:Z=65000:C=F:S=999:RETURN
41 GOSUB35:GOSUB40:RETURN
42 GOSUB9:GOSUB40:RETURN
43 ON1-(U=O)GOTO44,2
44 U=U+1:ONUGOTO45,45,45,45,45,45,45,45,45,45,45,45,45,45,45:U=1
45 ONI(U)GOTO2:ON1-(PEEK(P(H(U),I(U)))=Q)GOTO47,46
46 POKEP(H(U),I(U)),I:I(U)=1:RETURN
47 FORJ=2TOF:ON1-(H(U)=A(J)ANDI(U)=B(J)+1)GOTO49:N(J)=PEEK(P(NX,NY)):A(J)=NX:B(J)=NY:POKEP(A(J),B(J)),190:SOUND1,5
48 POKEP(A(J),B(J)),190:F(J)=PEEK(P(A(J),B(J)+1)):J=F:NEXT:GOTO46
49 NEXT:GOSUB40:PRINT@0,"BURIED ALIVE!!!";:RETURN
50 IFS>=999THEN55
51 PRINT@480+20,"HIT ANY KEY";:I$=INKEY$
52 I$=INKEY$:J=RND(1000):IFI$=""THEN52
53 GOTO900
54 PRINT@0,"YOU FELL IN A BOTTOMLESS PIT!";:Z=65000:C=F:S=999:GOTO22
55 PRINT@480,"PLAY AGAIN (Y/N)?";
56 I$=INKEY$:IFI$=""THEN56
57 IFI$="Y"THENOL=0:LC=0:RESTORE:GOTO900
58 IFI$="N"THENEND
59 GOTO56
60 POKEP(E(1),E(2)),240:RETURN
61 GOSUB9:LC=LC+1:PRINT@480,"READY FOR LEVEL";STR$(LV+1)"?";:Z=65000:C=F:S=V:RETURN
100 PRINT@2,"mc-lode runner by jim gerrie"
110 PRINT
120 PRINT"TO MOVE USE:"
130 PRINT"    W           T"
140 PRINT"  A   S  OR   F G H"
155 PRINT"    Z"
156 PRINT"                  TO DIG USE:"
160 PRINT"                     < >":PRINT
180 PRINT"AFTER LEVEL IS DISPLAYED PRESS"
190 PRINT"N TO SKIP TO NEXT LEVEL. GUARDS"
195 PRINT"ONLY GIVE UP ONE GOLD WHEN THEY"
196 PRINT"FALL IN PITS. THERE ARE 5":PRINT"LEVELS."
200 RETURN
800 W2(4)=2:W2(2)=4:D=188:Q=160
805 K(44)=1:K(65)=2:K(87)=3:K(83)=4:K(90)=5:K(46)=6:MC=16384:K=17023:G=128:H=220:I=175:L=16:N=32:W(1)=4:W(2)=2
806 K(70)=2:K(84)=3:K(72)=4:K(71)=5
810 M(32)=128:M(88)=175:M(72)=220:M(45)=204:M(42)=146:M(79)=239:FORT=1TO5:C(T)=W(RND(2)):NEXT
815 K(204)=1:K(175)=2:K(239)=2:K(146)=3:K(128)=4:K(220)=5:K(142)=6:K(141)=6:K(189)=7:K(190)=7:K(160)=8:K(144)=9:K(240)=10
820 FORX=0TO31:FORY=0TO15:P(X,Y)=(32*Y)+X+MC:NEXT:NEXT
830 X(2)=-1:Y(2)=0:D(2,1)=141:FORT=2TO5:D(2,T)=189:NEXT
831 X(3)=0:Y(3)=-1:D(3,1)=141:FORT=2TO5:D(3,T)=189:NEXT
832 X(4)=1:Y(4)=0:D(4,1)=142:FORT=2TO5:D(4,T)=190:NEXT
833 X(5)=0:Y(5)=1:D(5,1)=142:FORT=2TO5:D(5,T)=190:NEXT
840 FORT=0TO15:U(T)=5:NEXT:FORT=17TO31:U(T)=3:NEXT
845 FORT=0TO31:V(T)=4:NEXT:FORT=33TO63:V(T)=2:NEXT
860 RETURN
900 CLS0:OL=LV:READLV,V:E=V:IFLV=999ANDLC=OLTHENPRINT"ALL LEVELS COMPLETED!":END
901 IFLV=999THENPRINT"GAME OVER.":END
905 FORT=16384TO16415:POKET,175:NEXT
910 FORJ=1TO14:READA$:FORC=1TO32:POKET,M(ASC(MID$(A$,C,1))):T=T+1:NEXT:NEXT
920 FORC=TTO16895:POKEC,175:NEXT:READW,E(1),E(2)
930 READA(1),B(1):N(1)=128:POKEP(A(1),B(1)),141:F(1)=PEEK(P(A(1),B(1)+1)):G(1)=0
940 READF:F=F+1:FORT=2TOF:READA(T),B(T):N(T)=128:POKEP(A(T),B(T)),190:F(T)=PEEK(P(A(T),B(T)+1)):G(T)=0:NEXT
950 READ NX,NY:U=0:O=0:FORJ=0TO15:I(J)=1:NEXT
960 I$=INKEY$:IFI$=""THEN960
970 IFI$="N"THEN900
990 GOTO20
1000 DATA 1,8
1001 DATA"XXX    *            H        XXX"
1002 DATA"XXXXXXXXXHXXXXXXX   H        XXX"
1003 DATA"XXX      H----------H    *   XXX"
1004 DATA"XXX      H    XXH   XXXXXXHXXXXX"
1005 DATA"XXX      H    XXH         H  XXX"
1006 DATA"XXX      H    XXH       * H  XXX"
1007 DATA"XXXXXHXXXX    XXXXXXXXHXXXXXXXXX"
1008 DATA"XXX  H                H      XXX"
1009 DATA"XXX  H                H      XXX"
1010 DATA"XXXXXXXXXXXHXXXXXXXXXXH      XXX"
1011 DATA"XXX        H          H      XXX"
1012 DATA"XXX      * H----------H  *   XXX"
1013 DATA"XXX    HXXXXX         XXXXXXHXXX"
1014 DATA"XXX    H           *        HXXX"
1015 DATA 6,20,0,16,14
1016 DATA 3,8,6,25,6,16,9
1017 DATA 16,1
1100 DATA 2,8
1101 DATA"XXX   *                     HXXX"
1102 DATA"XXXHXXOXXH          *       HXXX"
1103 DATA"XXXH     H    HXXXXXXXXH *  HXXX"
1104 DATA"XXXH *   H    H        HXXX HXXX"
1105 DATA"XXXHOXOXOH    H        H    HXXX"
1106 DATA"XXXH     H----H-----   H    HXXX"
1107 DATA"XXXH     H    H    HXXXOOOOOHXXX"
1108 DATA"XXXH     H    H *  H        HXXX"
1109 DATA"XXXH     H *  HXXXXH        HXXX"
1110 DATA"XXXOXXXOXXOXXOH        HXXHXXXXX"
1111 DATA"XXXOXXXO      H        H  H  XXX"
1112 DATA"XXXO*  O      H  ------H  H *XXX"
1113 DATA"XXXOXXXOXXXHXXXOOO     H XXXXXXX"
1114 DATA"XXX        H           H     XXX"
1115 DATA 8,28,0,15,14
1116 DATA 3,7,4,22,6,7,9
1117 DATA 16,1
1200 DATA 3,8
1201 DATA"XXX---------   *            HXXX"
1202 DATA"XXXH*      HXXXXXXXXXH      HXXX"
1203 DATA"XXXXXXXH   H         HOOOOOOOXXX"
1204 DATA"XXX    H   H    *    H       XXX"
1205 DATA"XXX    HXXXXXHXXXXXHXX       XXX"
1206 DATA"XXX *  H     H     H  --     XXX"
1207 DATA"XXXXXXHX     H     H    --   XXX"
1208 DATA"XXX   H    HXXXXXHXX      --*XXX"
1209 DATA"XXX   H----H     H          XXXX"
1210 DATA"XXX   H      HXXXXXXXXXH     XXX"
1211 DATA"XXX   H      HXXXXXXXXXH     XXX"
1212 DATA"XXXXXHXXXXXXXXX   *   XXXXXHXXXX"
1213 DATA"XXXXXHXXXXXXXXX HXXXH XXXXXHXXXX"
1214 DATA"XXX  H          HXXXH      H XXX"
1215 DATA 6,28,0,12,14
1216 DATA 3,9,4,16,7,20,9
1217 DATA 16,1
1300 DATA 4,8
1301 DATA"XXXH----------               XXX"
1302 DATA"XXXH    H     X * X     H    XXX"
1303 DATA"XXXH*  HHH  * XXXXX *  HHH  *XXX"
1304 DATA"XXX HH  H  HH       HH  H  HHXXX"
1305 DATA"XXX H HHHHH H       H HHHHH HXXX"
1306 DATA"XXX H  * *  H   H   H  * *  HXXX"
1307 DATA"XXXH HXXXXXH   HHH   HXXXXXH XXX"
1308 DATA"XXXH  HHHHH HH  H  HH HHHHH  XXX"
1309 DATA"XXXH        H HHHHH H        XXX"
1310 DATA"XXXH   *    H  * *  H    *   XXX"
1311 DATA"XXXHXXXXXH   HXXXXXH  HXXXXXXXXX"
1312 DATA"XXXH     H    HHHHH   H      XXX"
1313 DATA"XXXH     H            H      XXX"
1314 DATA"XXXH     H       *    H      XXX"
1315 DATA 14,3,0,20,14
1316 DATA 3,8,6,24,6,16,10
1317 DATA 16,1
1400 DATA 5,10
1401 DATA"XXX        H---      *     H XXX"
1402 DATA"XXX    *   H   XXXXXXXX    H XXX"
1403 DATA"XXXXXXXXXXXXXH        *    H XXX"
1404 DATA"XXX          HXXXXXXHXXXXHXX XXX"
1405 DATA"XXX          H      H    H   XXX"
1406 DATA"XXXXXXXXXXXXXH      H    H  *XXX"
1407 DATA"XXXXXXXXXXXXXHXXXXXXXOO  HXXXXXX"
1408 DATA"XXXXXXXXXXXXXH           H   XXX"
1409 DATA"XXXXXXXXXXXXXH      * ---H   XXX"
1410 DATA"XXXXX      XXH      XX   H   XXX"
1411 DATA"XXXXX  **  XXH    *      H   XXX"
1412 DATA"XXXXXXXXXXXXXXHXXXXXXH   H   XXX"
1413 DATA"XXX           H      XXXXXXXHXXX"
1414 DATA"XXX    *      H             HXXX"
1415 DATA 9,27,0,12,14
1416 DATA 2,3,5,21,6
1417 DATA 16,1
9999 DATA 999,999
