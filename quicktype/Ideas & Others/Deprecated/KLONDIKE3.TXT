10 CLEAR3000:CLS
15 DIM DK$(52),PRE(52),SU$(4),CA$(13),CLR(4,2),ROW$(7,19,2),ND$(52),RL
20 DIM ACES$(4)
30 PRINT "KLONDIKE SOLITAIRE":PRINT:PRINT "DO YOU NEED INSTRUCTIONS? ";
35 A$=INKEY$:IF A$="" THEN 35
36 IF A$<>"Y" AND A$<>"N" THEN 35
37 IF A$="Y" THEN GOSUB 5000
38 CLS
40 PRINT "INITIALIZING ... "
45 GOSUB 1000
50 PRINT "SHUFFLING ... "
60 GOSUB 1200
65 PRINT "DEALING ... "
70 GOSUB 1500
80 GOSUB 1700
90 T1=3
91 PRINT@32*0+16,"C=MORE CARDS";
92 PRINT@32*1+16,"D=MOVE FROM DECK";
93 PRINT@32*2+16,"ROW # TO MOVE";
100 PRINT@32*13+28,"";
110 IF ND<1 THEN PRINT "NONE";:GOTO 140
120 C$=ND$(T1):GOSUB 1300
140 PRINT@32*9+28,"CARD";:PRINT@32*10+28,T1;:PRINT@32*11+29,"OF";:PRINT@32*12+28,ND;
150 PRINT@32*15+0,"                              ";
152 IF NC>51 THEN 900
155 PRINT@32*14+0,"WHAT DO YOU WANT TO DO?  ";CHR$(8);
160 A$=INKEY$:IF A$="" THEN 160
170 IF A$="C" THEN PRINT A$;:GOSUB 300:GOTO100
180 IF A$="D" THEN PRINT A$;:GOSUB 350:GOTO100
190 IF A$>="1" AND A$<="7" THEN PRINT A$;:GOSUB 600:GOTO 100
200 IF A$="Q" THEN 900
210 GOTO 160
300 REM GET MORE CARDS FROM DECK
310 IF T1=ND THEN T1=3:GOTO320
311 T1=T1+3
320 IF T1>ND THEN T1=ND
330 RETURN
350 REM MOVE FROM DECK TO A ROW
360 PRINT@32*15+0,"FROM DECK TO ROW? ";
380 A$=INKEY$:IF (A$>="1" AND A$<="7") OR A$="A" THEN 390
381 GOTO380
390 IF A$="A" THEN PRINT "ACES";:GOTO395
391 PRINT A$;
395 IF A$="A" THEN 540
400 TW=VAL(A$)
410 FOR PL=19 TO 1 STEP -1
420 IF ROW$(TW,PL,2)<>"   " THEN 460
430 NEXT PL
432 IF ROW$(TW,1,2)="   " AND LEFT$(ND$(T1),1)="K" THEN PL=0:GOTO 500
440 GOSUB 990
450 RETURN
460 BELOW$=ROW$(TW,PL,2)
470 AB$=ND$(T1)
480 GOSUB 2000:REM CHECK IF MATCH
490 IF OK$="NO" THEN 440
500 ROW$(TW,PL+1,1)=ND$(T1)
510 ROW$(TW,PL+1,2)=ND$(T1)
520 ROW=TW:GOSUB 3100
525 GOSUB 2200:REM REPACK DECK
530 RETURN
540 REM PLAY DECK TO ACES
550 AB$=ND$(T1)
560 GOSUB 2300:REM PLAY TO ACES
570 IF OK$="NO" THEN 440
580 GOTO 525
600 REM MOVE FROM ONE ROW TO ANOTHER
610 FW=VAL(A$)
620 FOR FE=1 TO 19
630 IF ROW$(FW,FE,2)<>DN$ THEN 660
640 NEXT FE
650 GOSUB 990:RETURN
660 PRINT@32*15+0,"FROM ROW";FW;"TO ROW? ";
680 A$=INKEY$:IF (A$>="1" AND A$<="7") OR A$="A" THEN 690
681 GOTO680
690 IF A$="A" THEN PRINT "ACES";:GOTO695
691 PRINT A$;
695 IF A$="A" THEN 830
700 TW=VAL(A$)
710 FOR T0=19 TO 1 STEP -1
720 IF ROW$(TW,T0,2)<>"   " THEN 760
730 NEXT T0
732 IF ROW$(TW,1,2)="   " AND LEFT$(ROW$(FW,FE,2),1)="K" THEN T0=0:GOTO 800
740 GOSUB 990
750 RETURN
760 REM
770 AB$=ROW$(FW,FE,2)
775 BELOW$=ROW$(TW,T0,2)
780 GOSUB 2000:REM CHECK IF MATCH
790 IF OK$="NO" THEN 740
800 GOSUB 3200:REM MOVE THE STACK
810 ROW=FW:GOSUB 3100:REM REDRAW A ROW
815 ROW=TW:GOSUB 3100
820 RETURN
830 REM PLAY FROM A ROW TO ACES
832 FOR FE=19 TO 1 STEP -1
834 IF ROW$(FW,FE,2)<>"   " THEN 840
836 NEXT FE
838 GOSUB 990:RETURN
840 AB$=ROW$(FW,FE,2)
850 GOSUB 2300:REM PLAY TO ACES
851 IF OK$="NO" THEN 740
853 REM ROW$(FW,FE,1)="   "
854 ROW$(FW,FE,2)="   "
855 IF FE>1 THEN ROW$(FW,FE-1,2)=ROW$(FW,FE-1,1)
856 ROW=FW:GOSUB 3100:REM REDRAW A ROW
860 RETURN
900 REM EXIT GAME
910 GOSUB 1800
920 GOSUB 1900
925 FOR I=1 TO 20:A$=INKEY$:NEXT I
930 GOSUB 3400:PRINT "PLAY AGAIN? (Y/N)";
940 A$=INKEY$:IF A$="" THEN 940
960 IF A$="N" THEN 970 
961 IF A$="Y" THEN CLS:PRINT "RE-";:GOTO 40
965 GOTO 940
970 CLS:PRINT "BYE":END
990 REM BAD PLAY
992 PRINT@32*14+0,"CAN'T DO THAT!           ";
994 PRINT@32*15+0,"PRESS ANY KEY TO CONTINUE";
996 IF INKEY$="" THEN 996
997 PRINT@32*14+0,"                         ";
998 PRINT@32*15+0,"                         ";
999 RETURN
1000 REM
1005 DN$=CHR$(207):DN$=DN$+DN$+DN$
1009 IF COL$="N" THEN 1020
1010 FOR I=1 TO 2:CLR(I,1)=4:CLR(I,2)=7:NEXT
1015 FOR I=3 TO 4:CLR(I,1)=0:CLR(I,2)=7:NEXT
1019 GOTO 1030
1020 FOR I=1 TO 2:CLR(I,1)=0:CLR(I,2)=7:NEXT
1025 FOR I=3 TO 4:CLR(I,1)=7:CLR(I,2)=0:NEXT
1030 SU$(1)=CHR$(178)+CHR$(130+48):SU$(2)=CHR$(177)+CHR$(128)
1031 SU$(3)=CHR$(163)+CHR$(130+32):SU$(4)=CHR$(161)+CHR$(128)
1035 FOR I=1 TO 4:ACES$(I)="   ":NEXT I
1040 FOR I=2 TO 9:CA$(I)=RIGHT$(STR$(I),1):NEXT I
1050 CA$(1)="A":CA$(10)="T":CA$(11)="J":CA$(12)="Q":CA$(13)="K"
1060 FOR I=1 TO 4
1070 FOR J=1 TO 13
1080 K=(I-1)*13+J
1090 DK$(K)=CA$(J)+SU$(I)
1100 NEXT J
1110 NEXT I
1120 FOR I=1 TO 7
1130 FOR J=1 TO 19
1140 ROW$(I,J,1)="   ":ROW$(I,J,2)="   "
1150 NEXT J,I
1160 NC=0
1170 RETURN
1200 REM SHUFFLE
1210 FOR I=1 TO 52:ND$(I)="":NEXT I
1220 FOR I=1 TO 52
1230 R=RND(52)
1240 IF ND$(R)="" THEN ND$(R)=DK$(I):GOTO1250
1241 GOTO1230
1250 NEXT I
1260 FOR I=1 TO 52
1270 DK$(I)=ND$(I)
1280 NEXT I
1290 RETURN
1300 REM DISPLAY A CARD (C$)
1310 IF C$=DN$ THEN 1350
1320 IF C$="   " THEN 1350
1350 PRINT C$;" ";
1360 RETURN
1500 REM DEAL THE GAME
1510 C=1
1520 FOR PL=1 TO 7
1530 FOR ROW=1 TO 7
1535 IF ROW<PL THEN ROW$(ROW,PL,1)="   ":ROW$(ROW,PL,2)="   ":GOTO 1570
1540 ROW$(ROW,PL,1)=DK$(C)
1550 C=C+1
1560 IF ROW=PL THEN ROW$(ROW,PL,2)=ROW$(ROW,PL,1):GOTO1570
1561 ROW$(ROW,PL,2)=DN$
1570 NEXT ROW,PL
1580 CLS
1590 L=10:PRINT@1,"A=ACES":AC=0
1640 PRINT@32*4,1
1645 PRINT@32*3+0," 1   2   3   4   5   6   7"
1650 FOR PL=1 TO 7
1660 FOR ROW=1 TO 7
1661 C$=ROW$(ROW,PL,2)
1665 C$=ROW$(ROW,PL,2)
1670 GOSUB 1300
1680 NEXT ROW:PRINT:NEXT PL
1690 RETURN
1700 REM PREPARE TO PLAY
1710 FOR I=1 TO 24
1715 J=I+28
1720 ND$(I)=DK$(J)
1730 NEXT I
1740 FOR I=25 TO 52
1750 ND$(I)=""
1760 NEXT I
1770 ND=24
1780 RETURN
1800 REM SHOW DECK
1810 RETURN:
1900 REM SHOW CARDS
1910 CN=0
1920 FOR PL=1 TO 19
1925 RL=(32*(3+PL)):IFPL>7THENRL=352
1926 PRINT@RL,"";
1930 FOR ROW=1 TO 7
1935 C$=ROW$(ROW,PL,2)
1940 IF C$=DN$ THEN C$=ROW$(ROW,PL,1):CN=CN+1
1950 GOSUB 1300
1960 NEXT ROW
1965 IF CN=0 THEN PL=20
1970 PRINT@RL,"";:CN=0
1980 NEXT PL
1990 RETURN
2000 REM CHECK IF AB$ CAN GO ON BELOW$
2010 IN$=AB$:GOSUB 3000:A=N:AS=SUIT
2020 IN$=BELOW$:GOSUB 3000:BN=N:BSUIT=SUIT
2030 OK$="YES"
2040 IF A+1<>BN THEN OK$="NO"
2050 IF AS<=2 AND BSUIT<=2 THEN OK$="NO"
2060 IF AS>=3 AND BSUIT>=3 THEN OK$="NO"
2070 RETURN
2200 REM REPACK DECK
2205 IF ND=1 THEN T1=0:ND=0:RETURN
2210 FOR I=T1+1 TO ND
2220 ND$(I-1)=ND$(I)
2230 NEXT I
2240 ND=ND-1
2250 T1=T1-1
2260 IF T1<1 THEN T1=3
2280 IF T1>ND THEN T1=ND
2290 RETURN
2300 REM PLAY AB$ TO ACES
2310 IN$=AB$:GOSUB 3000:A=N:AS=SUIT
2315 IN$=ACES$(AS):IF IN$="   " THEN BN=0:GOTO 2330
2320 GOSUB 3000:BN=N
2330 IF A=BN+1 THEN OK$="YES":GOTO2340
2331 OK$="NO":RETURN
2340 ACES$(AS)=AB$
2350 IFAC=0THENAC=1:PRINT@0,"           ";
2355 PRINT@(AS-1)*4,"";:C$=AB$
2360 GOSUB 1300
2365 NC=NC+1:PRINT@32*3+27,"SCORE";:PRINT@32*4+28,NC;
2370 RETURN
3000 REM CHANGE IN$ TO NUM AND SUIT
3005 IF A$="   " THEN N=0:SUIT=0:RETURN
3010 A$=LEFT$(IN$,1)
3020 IF A$="A" THEN A$=" 1"
3030 IF A$="J" THEN A$="11"
3040 IF A$="Q" THEN A$="12"
3050 IF A$="K" THEN A$="13"
3055 IF A$="T" THEN A$="10"
3060 N=VAL(A$)
3071 IFMID$(IN$,2,1)=CHR$(178)THENSUIT=1
3072 IFMID$(IN$,2,1)=CHR$(177)THENSUIT=2
3073 IFMID$(IN$,2,1)=CHR$(163)THENSUIT=3
3074 IFMID$(IN$,2,1)=CHR$(161)THENSUIT=4
3080 RETURN
3100 REM REDRAW A ROW
3110 R=((ROW-1)*4)
3130 FOR PL=1 TO 19
3140 IF ROW$(ROW,PL,1)="   " THEN PL=20:GOTO 3180
3145 RL=(32*(3+PL))+R:IFPL>7THENRL=352+R
3155 PRINT@RL,"";
3160 C$=ROW$(ROW,PL,2)
3170 GOSUB1300
3180 NEXT PL
3190 RETURN
3200 REM COPY PART OF A ROW
3205 FP=FE:TP=T0
3207 IF TP=1 AND LEFT$(ROW$(FW,FE,2),1)="K" THEN 3220
3210 TP=TP+1
3220 ROW$(TW,TP,1)=ROW$(FW,FP,1)
3225 ROW$(TW,TP,2)=ROW$(FW,FP,2)
3230 REM ROW$(FW,FP,1)="   "
3235 ROW$(FW,FP,2)="   "
3240 FP=FP+1
3250 IF ROW$(FW,FP,2)<>"   " THEN 3210
3260 IF FE>1 THEN 3300
3270 ROW$(FW,FP,1)="   "
3280 ROW$(FW,1,2)="   "
3290 RETURN
3300 ROW$(FW,FE-1,2)=ROW$(FW,FE-1,1)
3310 RETURN
3400 REM EVALUATE GAME PERFORMANCE
3405 PRINT@32*14-1,NC;"CARDS PLACED ON ACES PILES.";:PRINT@32*15,"";
3410 SC=INT(NC/10)+1
3415 IF NC=52 THEN SC=7
3420 IF NC=0 THEN GOSUB 3520:RETURN
3430 ON SC GOSUB 3450,3460,3470,3480,3490,3500,3510
3440 RETURN:REM SCORE 0-9  10-19 20-29 30-39 40-49 50+
3450 PRINT "TOO BAD. ";:RETURN
3460 PRINT "NOT TOO BAD. ";:RETURN
3470 PRINT "KEEP TRYING. ";:RETURN
3480 PRINT "GOOD JOB! ";:RETURN
3490 PRINT "VERY GOOD! ";:RETURN
3500 PRINT "ALMOST GOT IT. ";:RETURN
3510 PRINT "YOU DID IT! CONGRATS! ";:RETURN
3520 PRINT "BAD SHUFFLE! ";:RETURN
5000 REM INSTRUCTIONS
5010 CLS:PRINT "       KLONDIKE SOLITAIRE"
5030 PRINT "THIS PROGRAM PLAYS A SOLITAIRE  GAME.  A DECK OF 52 CARDS IS" 
5040 PRINT "USED AND 28 CARDS ARE DEALT     INTO 7 ROWS (PILES).  THE FIRST" 
5050 PRINT "ROW AT THE LEFT HAS ONE CARD,   THE SECOND TWO, AND SO ON, UP" 
5060 PRINT "TO SEVEN IN THE LAST ROW. THE   LAST CARD OF EACH ROW IS FACE" 
5070 PRINT "UP AND ALL THE REST ARE FACE    DOWN.  ON EACH ROW YOU MAY"
5080 PRINT "BUILD IN DESCENDING SEQUENCE:   RED ON BLACK, SUCH AS THE TEN"
5090 PRINT "OF HEARTS ON THE JACK OF CLUBS  OR SPADES.":GOSUB5500
5100 PRINT "YOU CAN MOVE THE FACE UP CARDS  IN A ROW AS A UNIT. THE TOP"
5110 PRINT "CARD BEING MOVED MUST FIT IN    SEQUENCE AND COLOR WITH THE"
5120 PRINT "CARD BEING MOVED UNDER IN THE   OTHER ROW. WHEN YOU UNCOVER A"
5130 PRINT "FACE-DOWN CARD ON A ROW, IT WILLBE TURNED UP."
5140 PRINT "YOU ARE ALWAYS ENTITLED TO HAVE 7 ROWS, AND IF ONE IS"
5150 PRINT "ENTIRELY OPEN YOU MAY PUT A KINGIN THE SPACE. WHENEVER YOU"
5160 PRINT "FREE AN ACE, MOVE IT TOO THE ACEFOUNDATIONS. ON THE ACES":GOSUB5500
5170 PRINT "YOU MAY BUILD UP IN SUIT AND    SEQUENCE AND THEN TO WIN THE"
5180 PRINT "GAME, YOU HAVE TO BUILD EACH    SUIT UP TO A KING.  A CARD MUST"
5190 PRINT "BE THE TOP CARD OF A ROW TO BE  PLAYED FROM, TO THE ROWS ONTO"
5200 PRINT "THE ACE FOUNDATIONS. ONCE PLAYEDON THE ACE FOUNDATIONS, A"
5210 PRINT "CARD CAN NOT BE REMOVED TO HELP ELSEWHERE.":GOSUB5500
5240 PRINT "THE REMAINING 24 CARDS IN THE   DECK ARE USED AS THE STOCK."
5250 PRINT "EVERY THIRD CARD MAY BE TURNED  UP AND THE DECK MAY BE GONE"
5260 PRINT "THROUGH ANY NUMBER OF TIMES.    THE TOP CARD IS AVAILABLE FOR"
5270 PRINT "PLAY TO ANY ROW OR THE ACE FOUND-ATIONS.  WHEN THE TOP CARD"
5280 PRINT "IS PLAYED, THE NEXT CARD WILL   THEN BE AVAILABLE. YOU WIN"
5290 PRINT "IF YOU CAN BUILD ALL 4 ACE      FOUNDATIONS UP TO KINGS."
5300 PRINT "YOU LOSE WHEN YOU CAN NOT MAKE  ANY FURTHER PLAYS.":GOSUB5500
5320 PRINT "PLAYS ARE CONTROLLED BY KEYBOARDCOMMANDS AS FOLLOWS:"
5330 PRINT "D = PLAY TOP CARD OF THE DECK TOACES (A) OR ROW (#1-7)."
5340 PRINT "    ANSWER 'A' OR ROW NUMBER 1-7TO NEXT QUESTION TO"
5350 PRINT "    INDICATE WHERE THE CARD IS  TO BE MOVED."
5360 PRINT "C = GET NEXT CARD IN THE DECK   AND WAIT FOR NEXT COMMAND."
5370 PRINT "Q = QUIT GAME AND TURN OVER ALL FACE-DOWN CARDS."
5380 PRINT "#1-7 = PLAY CARDS FROM ROW# TO  ACES OR ANOTHER ROW (1-7)":GOSUB5500
5400 PRINT "AN ERROR MESSAGE WILL DISPLAY   IF AN INVALID RESPONSE IS"
5410 PRINT "GIVEN TO ANY QUESTION OR ANY    INVALID PLAYS ARE ATTEMPTED.":GOSUB5500
5420 RETURN
5500 PRINT@480,"PRESS ANY KEY TO CONTINUE";
5510 IFINKEY$=""THEN5510
5520 CLS:RETURN
