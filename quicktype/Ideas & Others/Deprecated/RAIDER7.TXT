0 GOSUB3500:CLEAR1200:DIMS$(13),LN$,SH$,B$,L$,Y(500),B(500),K(255),EC(32),A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z
1 DIMG(500),SS(13),ML,PK,ZZ,EZ,SH,ST,ES,HS,MC,TC,F3,S4,CV,DL,EC,SC,CC,GV,VP,LO,SL,I$:GOSUB1000
2 FORML=ITO64000:GOSUB3000:FORG=ITO500STEPW:RESET(X,Y(G)):RESET(A,Y(G+I)):RESET(X,B(G)):RESET(A,B(G+I))
4 FORC=ITOQ:S$(C)=RIGHT$(S$(C),U)+CHR$(PEEK(SS(C))):NEXT:EC=EC+I:PRINT@S,RIGHT$(S$(I),U);B$;
5 PRINTRIGHT$(S$(W),U);B$;RIGHT$(S$(E),U);B$;RIGHT$(S$(F),U);B$;RIGHT$(S$(D),U);B$;RIGHT$(S$(H),U);B$;RIGHT$(S$(J),U);B$;
6 PRINTRIGHT$(S$(K),U);B$;RIGHT$(S$(M),U);B$;RIGHT$(S$(N),U);B$;RIGHT$(S$(P),U);B$;RIGHT$(S$(T),U);B$;RIGHT$(S$(Q),U);B$;
7 ONK(PEEK(PK)ANDPEEK(W))GOSUB20,30,40,50,60:EZ=EZ-I:ONABS(Z=EZ)GOSUB75:ONK(PEEK(Z))GOSUB85,70:POKEZ,SH:ONRND(DL)GOSUB95,96
8 ONEC(EC)GOSUB90:ONRND(J)GOSUB63,64,66,65,65,67,69:ONK(PEEK(EZ+ZZ))GOSUB68,69,75,80:EZ=EZ+ZZ:POKEEZ,ES:NEXT:NEXT:GOTO99
20 PRINT@Z-ST,SH$;:RETURN
30 Z=Z-I:RETURN
40 Z=Z+I:RETURN
50 Z=Z-S:RETURN
60 Z=Z+S:RETURN
63 ZZ=-I:EC=EC+I:ONEC(EC)GOSUB90:RETURN
65 ZZ=(SGN(Z-EZ)*S)*ABS(ABS(Z-EZ)>Q):RETURN
64 ZZ=-S:RETURN
66 ZZ=S:RETURN
67 ZZ=O:RETURN
68 ONRND(H)GOSUB63,64,66,65,65,67:ONK(PEEK(EZ+ZZ))GOTO68:RETURN
69 RETURN
70 SOUNDSH,I:SC=SC+100:SL=INT((Z-MC)/S):LO=S*(((Z-MC)/S)-SL):L$=S$(SL)
71 VP=VARPTR(L$):POKE(PEEK(VP+W)*T6)+PEEK(VP+E)+LO+I,F3:S$(SL)=L$
72 PRINT@448,"SCORE:";SC;"            ";:RETURN
75 PRINT@448,"TORPEDO HIT!   ";:POKEZ,191:SOUND1,10:POKEV-(3-CC),32:CC=CC-1:GOSUB72:GOSUB90:IFCC<1THENGOSUB85
77 RETURN
80 PRINT@448,"YOU GOT IT!    ";:POKEEZ+ZZ,191:SOUND100,10:SC=SC+1000:GOSUB72:GOSUB90:RETURN
85 GOSUB72:IFSC>HSTHENHS=SC
86 PRINT@448+16,"HIGH:";HS;:G=G+W:RESET(X,Y(G)):RESET(A,Y(G+I)):RESET(X,B(G)):RESET(A,B(G+I)):ML=64000:G=500
87 RETURN
90 EC=O:EZ=G(G)-I:ZZ=O:RETURN
95 POKEG(G+(2-RND(2))),42:RETURN
96 POKEG(G+(2-RND(2))),109:RETURN
99 PRINT@480,"GAME OVER. PRESS c TO CONTINUE.";
120 I$=INKEY$:IFI$<>"C"THEN120
140 CLS1:SC=0:DL=11:CV=0:CC=3:GOTO2
1000 U=31:V=16415:S=32:N=10:L=27:B$=CHR$(143):FORC=27TO32:EC(C)=1:NEXT:SH$="----":ST=16383:CC=3
1005 O=0:I=1:W=2:E=3:F=4:D=5:H=6:J=7:K=8:M=9:N=10:P=11:T=12:Q=13:PK=17023:SH=126:ES=95:GV=96
1010 FORC=ITOQ:S$(0)=S$(0)+B$:LN$=LN$+B$:NEXT:FORC=QTOS:S$(0)=S$(0)+B$:LN$=LN$+CHR$(131):NEXT
1020 FORC=ITOQ:SS(C)=V+(S*C):NEXT
2000 FORC=128TO142:K(C)=1:NEXT:K(30)=1:K(95)=1:K(13)=1:K(126)=3:K(109)=4:K(106)=2
2020 K(32)=1:K(65)=2:K(83)=3:K(87)=4:K(90)=5
2030 HS=0:MC=16384:T6=256:F3=143:S4=64:DL=11
2090 GOSUB6080:RETURN
3000 PRINT@O,"PREPARE FOR THE NEXT CAVERN...  ";:X=62:Y=8:A=63:B=22:Z=16655:EZ=16670:EC=0
3002 CV=CV+1:IFCV>1THENGOSUB5000
3003 DL=DL-1:IFDL<5THENDL=5
3005 FORG=ITO500STEPW:R=W-RND(E):IFY+R<WORY+R>B-NTHENR=O
3010 Y=Y+R:Y(G)=Y:Y=Y+R:Y(G+I)=Y:R=W-RND(E):IFB+R+R>LORB+R<Y+NTHENR=O
3020 B=B+R:B(G)=B:B=B+R:B(G+I)=B:G(G)=V+(INT(Y(G)/W)*S)+GV:G(G+I)=V+(INT(B(G)/W)*S)-GV:NEXT
3040 FORC=ITOQ:S$(C)=S$(O):NEXT:S$(F)=LN$:S$(P)=LN$:GOSUB72:GOSUB4000:FORC=MCTOV:POKEC,PEEK(C)-S4:NEXT:RETURN
3500 GOSUB4000:GOSUB6000:RETURN
4000 CLS1:PRINT" CAVERN RAIDER BY JIM GERRIE ";LEFT$(CHR$(95)+CHR$(95)+CHR$(95),CC);"   ";
4010 RETURN
5000 IFCV=10THENGOSUB5050
5010 IFCV=5ORCV=15ORCV=20ORCV=30ORCV=40ORCV=50THENGOSUB5070
5020 PRINT@160+64+3,"CONGRATUALTIONS ON MAKING";
5030 PRINT@160+96+3,"IT TO UNDERSEA CAVERN";STR$(CV);"!";
5040 RETURN
5050 PRINT@160+3,   "DADDY LOVES HIS GOOD CHUM";
5060 PRINT@160+32+3,"AND HIS BOO AND HIS BABY!";:RETURN
5070 PRINT@160+3,   "DAMAGE CONTROL REPORTS ALL";
5080 PRINT@160+32+3,"SYSTEMS HAVE BEEN REPAIRED.";:CC=3:RETURN
6000 PRINT 
6010 PRINT"YOU HAVE DISCOVERED A SERIES OF"
6015 PRINT"CAVERNS LEFT BY THE LOST CITY  "
6020 PRINT"OF ATLANTIS. USING YOUR TRUSTY "
6025 PRINT"SUBMARINE > YOU SEARCH FOR THE ":POKE16554,62
6030 PRINT"GOLDEN TREASURE * WHILE TRYING ":POKE16592,42
6035 PRINT"TO AVOID THE EXPLOSIVE MINES m "
6040 PRINT"LEFT TO PROTECT IT. THEY HAVE  "
6045 PRINT"ALSO LEFT AUTOMATIC TORPEDO    ":POKE16700,31
6050 PRINT"LAUNCHERS, SO WATCH OUT!  YOU  "
6055 PRINT"CAN ONLY TAKE 3 HITS BEFORE YOU"
6060 PRINT"DIE. HIT THE WALLS OR MINES AND"
6065 PRINT"YOU SHIP WILL BE DESTROYED     "
6070 PRINT"IMMEDIATELY.":RETURN
6080 PRINT"     PRESS ANY KEY TO BEGIN  ";
6085 I$=INKEY$:R=RND(1000):IFI$=""THEN6085
6090 CLS1:RETURN
