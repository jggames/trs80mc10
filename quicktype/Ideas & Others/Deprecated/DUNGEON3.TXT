0 DIM D(31,14),A(255),P(33,5,1),CL(7,1),MW(5,1),RW(1,1),RA(1,1),AR(3,1),HL(1,1),SH(1,1),X,Y,A,B,C,R
1 DIM CL$(7),MW$(5),RW$(1),RA$(1),AR$(3),HL$(1),SH$(1),S$(11),I$:GOSUB3000:GOSUB6100
2 CLS0:GOSUB1000:GOTO900
3 POKE16895,96:RETURN
5 PRINT@480,"door                           ";:GOSUB3:RETURN
10 PRINT@480,"object                         ";:GOSUB3:RETURN
15 PRINT@480,"monster                        ";:GOSUB3:RETURN
20 PRINT@480,"treasure                       ";:GOSUB3:RETURN
25 PRINT@480,"trap                           ";:GOSUB3:RETURN
30 CLS0:FORB=0TO14:FORA=0TO31:PRINT@A+(32*B),CHR$(D(A,B));:NEXT:NEXT
32 PRINT@480,"SeARCH tRAP MAgIC bOW dOOR mENU";:GOSUB3:RETURN
34 PRINT"MO";P(19,N,0);CHR$(P(1,N,0));CHR$(P(2,N,0));CHR$(P(3,N,0));CHR$(P(4,N,0));CHR$(P(5,N,0));CHR$(P(6,N,0));
36 PRINT"HP";P(13,N,0);"AC:";P(15,N,0);"(Y/N)";:POKE16895,96:RETURN
50 PRINT@X+(32*Y),"P";:PRINT@X-1+(32*Y),CHR$(D(X-1,Y));
52 PRINT@X-1+(32*(Y-1)),CHR$(D(X-1,Y-1));:PRINT@X+(32*(Y-1)),CHR$(D(X,Y-1));
54 PRINT@X+1+(32*(Y-1)),CHR$(D(X+1,Y-1));:PRINT@X+1+(32*Y),CHR$(D(X+1,Y));
56 PRINT@X+1+(32*(Y+1)),CHR$(D(X+1,Y+1));:PRINT@X+(32*(Y+1)),CHR$(D(X,Y+1));
58 PRINT@X-1+(32*(Y+1)),CHR$(D(X-1,Y+1));:RETURN
200 IFD(X-1,Y)=0THEND(X-1,Y)=32:IFRND(2)=1THEND(X-1,Y)=255:IFRND(10)=1THEN D(X-1,Y)=100
210 IFD(X+1,Y)=0THEND(X+1,Y)=32:IFRND(2)=1THEND(X+1,Y)=255:IFRND(10)=1THEN D(X+1,Y)=100
220 IFD(X,Y-1)=0THEND(X,Y-1)=32:IFRND(2)=1THEND(X,Y-1)=255:IFRND(10)=1THEN D(X,Y-1)=100
230 IFD(X,Y+1)=0THEND(X,Y+1)=32:IFRND(2)=1THEND(X,Y+1)=255:IFRND(10)=1THEN D(X,Y+1)=100
240 RETURN
250 IFD(X-1,Y)=255THEND(X-1,Y)=191:IFRND(10)=1THEND(X-1,Y)=32
260 IFD(X+1,Y)=255THEND(X+1,Y)=191:IFRND(10)=1THEND(X+1,Y)=32
270 IFD(X,Y-1)=255THEND(X,Y-1)=191:IFRND(10)=1THEND(X,Y-1)=32
280 IFD(X,Y+1)=255THEND(X,Y+1)=191:IFRND(10)=1THEND(X,Y+1)=32
285 RETURN
350 IFD(X-1,Y)=100THEND(X-1,Y)=68:IFRND(6)>2THEND(X-1,Y)=100
360 IFD(X+1,Y)=100THEND(X+1,Y)=68:IFRND(6)>21THEND(X+1,Y)=100
370 IFD(X,Y-1)=100THEND(X,Y-1)=68:IFRND(6)>2THEND(X,Y-1)=100
380 IFD(X,Y+1)=100THEND(X,Y+1)=68:IFRND(6)>2THEND(X,Y+1)=100
385 RETURN
900 X=15:Y=13:D(X,Y)=32:PRINT@32+X+(32*Y)," ";:D(X,Y+1)=32:C=0
905 GOSUB200:GOSUB50:ONA(C) GOSUB5,10,15,20,25
910 I$=INKEY$:IFI$=""THEN910
920 IFI$="W"THENC=D(X,Y-1):IFC>31ANDC<96THENY=Y-1:GOTO980
930 IFI$="Z"THENC=D(X,Y+1):IFC>31ANDC<96THENY=Y+1:GOTO980
935 IFI$="S"THENC=D(X+1,Y):IFC>31ANDC<96THENX=X+1:GOTO980
940 IFI$="A"THENC=D(X-1,Y):IFC>31ANDC<96THENX=X-1:GOTO980
945 IFI$="E"THENGOSUB250:GOTO980
950 IFI$="G"THENGOSUB30:GOTO980
952 IFI$="M"THENGOSUB32:GOTO980
954 IFI$="D"THENGOSUB350:GOTO980
980 IFY=14THEN2000
990 GOTO905
1000 FORX=0TO31:D(X,0)=191:D(X,14)=191:NEXT:FORY=0TO14:D(0,Y)=191:D(31,Y)=191:NEXT:GOSUB30
1002 FORT=1TO10:D(RND(29)+1,RND(12)+1)=143:NEXT
1005 R=RND(3)+4:A=RND(31-R):B=RND(14-R):R=R-1:FORX=ATOA+R:FORY=BTOB+R:D(X,Y)=32:NEXT:NEXT
1008 FORX=ATOA+R:D(X,B)=255:D(X,B+R)=255:NEXT
1009 FORY=BTOB+R:D(A,Y)=255:D(A+R,Y)=255:NEXT
1010 Y=B+1+RND(R-2):X=A:D(X,Y)=100:X=A+R:D(X,Y)=100:X=A+1+RND(R-2):Y=B:D(X,Y)=100:Y=B+R:D(X,Y)=100
1011 X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=73:X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=77:X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=36
1015 A(68)=1:A(73)=2:A(77)=3:A(36)=4:A(120)=5:A(143)=5
1030 REM GOSUB30:REM REMOVE
1040 RETURN
2000 PRINT@X+(32*Y)-32," ";:PRINT@X+(32*Y),"P";:SOUND 1,1
2010 PRINT@480,"TRY ANOTHER DUNGEON (Y/N)?     ";:GOSUB3
2020 I$=INKEY$:IFI$=""THEN2020
2030 IFI$="Y"THENRUN
2040 IFI$="N"THENEND
2050 GOTO2020
3000 FORT=1TO7:READCL$(T),CL(T,0):NEXT:CL$(0)="NONE"
3010 FORT=1TO5:READMW$(T),MW(T,0),MW(T,1):NEXT:MW$(0)="NONE"
3015 RW$(1)="BOW":RW(1,0)=25:RW(1,1)=4:RW$(0)="NONE"
3017 RA$(1)="ARROWS":RA(1,0)=.5:RA(1,1)=4:RA$(0)="NONE"
3020 FORT=1TO3:READAR$(T),AR(T,0),AR(T,1):NEXT:AR$(0)="NONE"
3025 HL$(1)="HELMET":HL(1,0)=10:HL(1,1)=-1:HL$(0)="NONE"
3030 SH$(1)="SHIELD":SH(1,0)=10:SH(1,1)=-1:SH$(0)="NONE"
3040 FORT=1TO11:READS$(T):NEXT:S$(0)="":RETURN
6100 FORN=1TO5:IFP(21,N,0)<>0THEN6200
6105 P(21,N,0)=0
6110 FORT=7TO12:P(T,N,0)=RND(6)+RND(6)+RND(6):NEXT
6120 H=P(7,N,0):IF P(7,N,0)>12THENP(21,N,0)=1
6122 IFP(8,N,0)>8ANDP(8,N,0)>HTHENP(21,N,0)=2:H=P(8,N,0)
6124 IFP(9,N,0)>8ANDP(9,N,0)>HTHENP(21,N,0)=3:H=P(9,N,0)
6126 IFP(10,N,0)>8ANDP(10,N,0)>HTHENP(21,N,0)=4:H=P(10,N,0)
6128 IFP(11,N,0)>8ANDP(7,N,0)>8ANDP(11,N,0)>H THEN P(21,N,0)=5:H=P(11,N,0)
6130 IFP(12,N,0)>8ANDP(7,N,0)>8ANDP(8,N,0)>8 AND P(12,N,0)>H THEN P(21,N,0)=6:H=P(12,N,0)
6132 IFP(11,N,0)>12ANDP(10,N,0)>12 THEN P(21,N,0)=7
6140 IFP(21,N,0)=0THENP(21,N,0)=RND(7)
6150 P(17,N,0)=1:P(13,N,0)=RND(CL(P(21,N,0),0))
6152 P(19,N,0)=N
6155 P(20,N,0)=(RND(6)+RND(6)+RND(6))*5
6160 GOSUB7000
6162 PRINT"ROLL AGAIN (Y/N)?"
6164 I$=INKEY$:IFI$=""THEN6164
6166 IFI$="Y"THEN6105
6168 IFI$="N"THEN6180
6170 GOTO6164
6180 INPUT"NAME (6 CHAR MAX)";I$:I$=I$+"      ":FORT=1TO6:P(T,N,0)=ASC(MID$(I$,T,1)):NEXT
6200 REM ** PURCHASE ITEMS **
6300 REM ** LEARN SPELLS **
6800 P(15,N,0)=9+AR(P(25,N,0),1)+HL(P(26,N,0),1)+AR(P(27,N,0),1)
6810 NEXT
6990 RETURN
7000 CLS
7004 PRINT"NM: ";CHR$(P(1,N,0));CHR$(P(2,N,0));CHR$(P(3,N,0));CHR$(P(4,N,0));CHR$(P(5,N,0));CHR$(P(6,N,0)),
7006 PRINT" CL: ";CL$(P(21,N,0))
7008 PRINT"ST:";P(7,N,0);P(7,N,1)," MW: ";MW$(P(22,N,0));P(22,N,1)
7012 PRINT"IN:";P(8,N,0);P(8,N,1)," RW: ";RW$(P(23,N,0));P(23,N,1)
7014 PRINT"WI:";P(9,N,0);P(9,N,1)," RA: ";RA$(P(24,N,0));P(24,N,1)
7016 PRINT"DX:";P(10,N,0);P(10,N,1)," AR: ";AR$(P(25,N,0));P(25,N,1)  
7018 PRINT"CO:";P(11,N,0);P(11,N,1)," HL: ";HL$(P(26,N,0));P(26,N,1)
7020 PRINT"CA:";P(12,N,0);P(12,N,1)," SH: ";SH$(P(27,N,0));P(27,N,1)
7022 PRINT"HP:";P(13,N,0)," M1: ";S$(P(28,N,0));P(28,N,1)
7024 PRINT"AC:";P(15,N,0)," M2: ";S$(P(29,N,0));P(29,N,1)  
7026 PRINT"LV:";P(17,N,0)," M3: ";S$(P(30,N,0));P(30,N,1) 
7028 PRINT"EX:";P(18,N,0)," S1: ";S$(P(31,N,0))
7030 PRINT"MO:";P(19,N,0)," S2: ";S$(P(32,N,0))
7032 PRINT"$$:";P(20,N,0)," S3: ";S$(P(33,N,0)) 
7090 RETURN
7800 DATA "FIGHTER",8,"MAGIC USER",4,"CLERIC",6,"THIEF",4,"DWARF",8,"ELF",6,"HALFLING",6
7850 DATA "STAFF",3,4,"MACE",5,6,"DAGGER",3,4,"AXE",7,8,"SWORD",15,8
7860 DATA "LEATHER",15,-2,"CHAIN MAIL",30,-4,"PLATE",50,-6
7862 DATA "RING","WAND","SCROLL"
7900 DATA "HEALING"
7902 DATA "LOCKING"
7904 DATA "OPENING"
7906 DATA "MISSILE"
7908 DATA "BLAST"
7910 DATA "TURNING"
7912 DATA "PETRIFY"
7914 DATA "REVEAL"
7916 DATA "TRAPS"
7918 DATA "FREEZE"
7920 DATA "SLEEP"
8000 DATA "KOBOLDS","GRUNT"
8002 DATA "GOBLINS","ARGH"
8004 DATA "BANDITS","TALKING"
8006 DATA "ORCS","ARRRR"
8008 DATA "SKELETONS","CLICK CLACK"
8010 DATA "BERSERKERS","SHOUTING"
8012 DATA "STIRGES","BUZZING"
8014 DATA "ZOMBIES","BRAINS!"
8016 DATA "GIANT RATS","SQUEAK SQUEAK"
8018 DATA "WIZARD","WHISPERING"
8020 DATA "GELATINOUS CUBE","BLOOP BLOOP"
