100 CLEAR2000:DIMMC,I,PI,X1,A6,C1,ROW,COL,PK,PC,PR,I$,QQ$:MC=16384:PI=3.1415:GOTO130
101 X1=INT(X1*10+.5)/10:RETURN:REM DEF FNR(X)=INT(X*10+.5)/10
102 X1=INT(X1*100+.5)/100:RETURN:REM DEF FNS(X)=INT(X*100+.5)/100
110 PK=PK-1023:PR=INT(PK/40)-1:PC=PK-(PR+1)*40:RETURN
120 IFROW=16ANDLEN(QQ$)=32THENFORI=1TOLEN(QQ$):POKEMC+(32*(ROW-1))+COL+(I-1),ASC(MID$(QQ$,I,1)):NEXTI:RETURN
125 PRINT@(32*(ROW-1))+COL,QQ$;:RETURN
130 MM$="041015040":CLS:PRINT@7,"**** SKYSCAPE ****";:PRINT@(32*15)+9,"INITIALIZING...";
140 D$="000031059090120151181212243273304334":K1=1440:DIM HC(22):M$="286317345011041072102133164194225255"
150 ES=149.6:D1$(1)="S":D1$(2)="N"
160 A$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC":OO$="OUT OF RANGE!":MD$="312831303130313130313031"
161 D9=3.14159265/180:READ EE,M9
170 DIMP(6,6)
180 FORY=1TO2:FORX=1TO6:READP(X,Y):NEXT X:NEXT Y:Y=0
190 FORX=1TO6:READP$(X),P(X,3):NEXT
200 PP(1)=121:PP(2)=118:PP(3)=109:PP(4)=106:PP(5)=115:PP(6)=117:PP(7)=127
210 J$="SATSUNMONTUEWEDTHUFRI":FORX=1TO12:READ F$
220 CC$=CC$+"     "+F$:NEXT:CC$=CC$+CC$:F$=MID$(CC$,LEN(CC$)-8,9):CC$=F$+CC$
230 FORX=1TO8:READ PH$(X):NEXT:FOR X=1 TO 22:READ HC(X):NEXT:GOTO 830
240 CC=MT-720:IF CC<0 THEN CC=CC+K1
250 CC=CC/120:CD=CC-INT(CC):CC=INT(CC):CD=INT(CD*7+.2):CC=81-(CC*7+CD)
260 GOSUB1890:QQ$=CD$:ROW=16:COL=0:GOSUB120
270 IF LL>=0THENRETURN
280 FORI=1TO16:Z=PEEK(MC+(32*15)+(I-1)):Z1=PEEK(MC+(32*15)+((33-I)-1))
281 POKEMC+(32*15)+(I-1),Z1:POKEMC+(32*15)+((33-I)-1),Z:NEXT I
290 FORI=1TO31:Z=PEEK(MC+(32*15)+(I-1)):IF Z=32 THEN 310
300 Z1=PEEK(MC+(32*15)+((I+1)-1)):POKEMC+(32*15)+(I-1),Z1:POKEMC+(32*15)+((I+1)-1),Z:I=I+1
310 NEXT I:RETURN
320 CLS:PRINT@(32*1)+9,"** DAY'S SKY **";:Q=1:GOSUB1680
330 PRINT@(32*5),"INPUT THE TIME:":PRINT@(32*6)+(1-1),"---------------":T1=0:T2=0
340 PRINT@(32*8)+3,"HOUR (0-23)";:INPUTT1:IF T1<0ORT1>23THENQ=10:GOSUB2290:GOTO340
350 PRINT@(32*10)+3,"MINUTE (0-59)";:INPUTT2:IF T2<0ORT2>59THENQ=12:GOSUB2290:GOTO350
360 TM$=STR$(T1):R$=RIGHT$(TM$,2):T$=STR$(T2):IF T2<10 THEN TM$=RIGHT$(T$,1):T$="0"+TM$
365 T$=RIGHT$(T$,2)
370 PRINT@(32*14),"TIME-- ";R$;":";RIGHT$(T$,2)
380 GOSUB2050:IF Z$="R"THEN320
390 CLS:T3=T1*60+T2+AA-720:IF T3<0THENT3=T3+K1
400 IF T3>K1THENT3=T3-K1
410 MT=T3-360:IF MT<0THEN MT=MT+K1
420 PT=T3+360:IF PT>K1THEN PT=PT-K1
430 C=175:SK=3:TM=VAL(R$+"."+T$):IF TM<6 OR TM>18 THEN C=128:SK=0
440 CLSSK:PRINT@0,K$;"-";TEM$;STR$(Y);TAB(17);STR$(ABS(LL));LL$;"  ";R$;":";RIGHT$(T$,2)
450 XX=7+LC:FORI=1TO31STEP2:PRINT@(32*XX)+(I-1),CHR$(172);:NEXTI
460 GOSUB240:GOTO510
465 ROW=16:COL=0:IF LL<0THEN490
470 IF LL>24THENQQ$="E              S               W":GOSUB120:GOTO505
480 QQ$="UP-N      -OVERHEAD-      DOWN-N":GOSUB 120:GOTO505
490 IF ABS(LL)>24 THEN QQ$="W              N               E":GOSUB120:GOTO505
500 QQ$="UP-S      -OVERHEAD-      DOWN-N":GOSUB 120
505 RETURN
510 T4=AA:GOSUB710:Y8=888:IF Y9=999THEN550
520 Y8=Y9:GOSUB2380:IF A1<0THEN550
530 IF PK>1703ORPK<1144THEN550
540 GOSUB110:IF PC>4 AND PC<37 THENPRINT@(32*(PR-1))+((PC-4)-1),CHR$(159);
550 T4=AA+M2*K1:IF T4>K1THENT4=T4-K1
560 GOSUB710:IF Y9=999THEN600
570 MM=INT(M1/9.83333)+1:GOSUB810:IF Y9=999THEN600
580 GOSUB2380:IF PK>1703 OR PK<1144 THEN600
590 GOSUB110:IF PC>4 AND PC<37 THEN POKEMC+(32*(PR-1))+((PC-4)-1),MM
591 IF ABS(Y8-Y9)<=.5THEN PRINT@(32*(PR-1))+((PC-4)-1),"n";
600 FOR X=1 TO 7:IF X=7 THEN2170
610 T4=P(X,6):GOSUB710:IF Y9=999 THEN670
620 U9=SIN(P(X,6)*D9/4):U9=-3*U9+.5:U9=INT(U9):U(X)=U9*40
630 PK=1423-Y9+U(X)+LB:GOSUB2390:IF PK>1703 OR PK<1144 THEN670
640 GOSUB110
650 IF PC>4 AND PC<37 THEN Z=PEEK(MC+(32*(PR-1))+((PC-4)-1))
651 IF Z<>128 AND Z<>175 AND Z<>172 THEN PR=PR-1:IFPR>1THEN651
652 IF PR<2 THEN PR=2
660 IF PC>4 AND PC<37 THEN PRINT@(32*(PR-1))+((PC-4)-1),CHR$(PP(X));
670 NEXTX:GOTO1810
675 QQ$="MERCUR"+CHR$(PP(1))+"  "+CHR$(PP(2))+"ENUS   "+CHR$(PP(3))+"ARS   "+CHR$(PP(4))+"UPITER ":ROW=12:COL=0:GOSUB120
680 QQ$=CHR$(PP(5))+"ATURN   "+CHR$(PP(6))+"RANUS  "+CHR$(159)+"SUN  )o(MOON  ":ROW=13:GOSUB120
681 QQ$="nEW MOON + SUN    (H)ORIZON HELP":ROW=14:GOSUB120
690 IF B$<>"" THEN QQ$=B$:ROW=15:COL=9:GOSUB120
700 QQ$="NEW (T)IME,(P)TAB,(D)DATE,(L)AT":ROW=16:COL=0:GOSUB120:POKE16895,110:RETURN
710 Y9=999:IF MT<PT THEN760
720 IF(T4>=MT)OR(T4<=PT)THEN 740
730 RETURN
740 IF(T4>=MT)AND(T4<=K1)THEN 780
750 T4=T4+K1:GOTO780
760 IF(T4>=MT)AND(T4<=PT)THEN 780
770 RETURN
780 Y9=INT((T4-MT)/18+.5):IF Y9=40 THENY9=39
790 RETURN
800 U9=SIN(T4/4*D9):U9=INT(-3*U9+.5)*40:RETURN
810 MM=VAL(MID$(MM$,3*MM-2,3)):IF LL<0 AND MM<>15 THEN MM=81-MM
820 RETURN
830 Q=1
840 CLS:PRINT@39,"**** SKYSCAPE ****":PRINT@(32*3),"DATE INPUT";:PRINT@(32*4),"----------";:S1=0
850 IF Y<>0THENGOSUB1680
860 PRINT@(32*5),"YEAR";:INPUTY:IF Y>=1977 THEN880
870 PRINT@(32*5),"MUST BE AFTER 1977";:FORI=1TO1000:NEXTI:PRINT@(32*5),"                  ";:GOTO860
880 GOSUB1730:PRINT@(32*6),"MONTH (1-12)";:INPUTM:IF M<1ORM>12THEN Q=Q+8:GOSUB2290:Q=Q-8:GOTO880
890 DI=VAL(MID$(MD$,2*M-1,2)):DI=DI-(M=2)*LY:DI$=STR$(DI)
900 PRINT@(32*7),"DAY(1-";DI$;")";:INPUTD:IF D<1 OR D>DI THENQ=Q+10:GOSUB2290:Q=Q-10:GOTO900
910 H$=MID$(A$,(M*3)-2,3)
920 PRINT@(32*9),"LATITUDE (0-90)";:INPUTLL:IF ABS(LL)>90 THENQ=Q+12:GOSUB2290:Q=Q-12:GOTO920
930 GOSUB2410
940 TEM$=H$+STR$(D)+",":PRINT@(32*11)+10,TEM$;Y:GOSUB2050:IF Z$="R"THENQ=4:GOTO840
950 D2=VAL(MID$(M$,(M*3)-2,3))+D:GOSUB1760:IF M>2THEND1=D1+LY:Y1=Y1+LY
960 D3=D2-185:IF M=3ANDD<20THEND2=D2+LY:D3=D3+LY
970 S5=0:IF D3<=0THENA=180*D2/185:GOTO990
980 A=(180*D3/(180+ZY))+180
990 IF A<>180 THEN S5=23.43333333*SIN(D9*D2*180/185)
1000 IF A>180 THEN S5=-23.4333333*SIN(D9*D3)
1010 IF A>=360 THEN A=A-360
1020 X1=A:GOSUB101:A=X1:X1=S5:GOSUB102:S5=X1:A1=(SGN(LL)-(LL=0))*S5+90-ABS(LL):X1=A1:GOSUB101:A1=X1:GOSUB1380:GOSUB1330
1030 W=1-(SGN(LL)<0):IF A1>90THENA1=180-A1:W=ABS(W-3)
1040 CLS:PRINTK$;"-";TEM$;Y;TAB(19);ABS(LL);LL$:PRINT"--------------------------"
1050 PRINT"DAY OF THE YEAR--- ";STR$(D1)
1070 PRINT"SUN'S DECLINATION- ";STR$(S5);"'"
1071 PRINT"GEOCENTRIC ANGLE-- ";STR$(A);"'"
1080 PRINT"ALTITUDE AT NOON-- ";STR$(A1);"'";D1$(W)
1090 PRINT"RIGHT ASCENSION---  ";A3$
1100 PRINT"R.A. AT 9:00 PM---  ";A5$
1110 PRINT"MOON'S AGE-------- ";STR$(M1);"DY"
1120 PRINT"ELONGATION-------- ";STR$(M8);"'";L$
1130 PRINT"MOON'S PHASE - ";PH$(M3):PRINT
1140 PRINT"(P)LANET TABLE OR NEW (D)ATE";:GOTO1810
1150 CLS:PRINTTAB(6);"** PLANET TABLE **":PRINT K$;"-- ";TEM$;Y;TAB(20);STR$(ABS(LL));LL$:S1=1
1160 PRINT"PLANET  DIST. ANG.W/SUN R.A.":PRINT"-----------------------------"
1170 FORX=1TO6:A2=Y1/P(X,2)-INT(Y1/P(X,2)):Q3=1
1180 A2=(A2*360)+P(X,1):IF A2>360THENA2=A2-360
1190 E=180+A:IF E>360 THEN E=E-360
1200 E1=ABS(E-A2):IF E1>180 THEN E1=360-E1
1210 GOSUB1440:E1=E1*D9:P5=P(X,3):IF X=3THENGOSUB1870
1220 P(X,4)=SQR(1+P5^2-2*P5*COS(E1)):XX=(P5^2-1-P(X,4)^2)/(-2*P(X,4))
1230 X1=(XX/SQR(-XX*XX+1)):GOSUB30690:P(X,5)=-A6+PI/2:P(X,4)=INT(P(X,4)*ES+.5):P(X,5)=P(X,5)/D9
1240 X1=P(X,5):GOSUB102:P(X,5)=X1:Q1$=STR$(P(X,4)):Q2$=STR$(P(X,5))
1250 Q1=LEN(Q1$):Q2=LEN(Q2$):GOSUB1540
1260 PRINTP$(X);TAB(13-Q1);Q1$;TAB(20-Q2);Q2$;:IF Q3=-1THENPRINT"'W";
1270 IF Q3=1THEN PRINT"'E";
1280 GOSUB1590:Q4$=STR$(Q4):Q5$=STR$(Q5):IF Q5<10THENQ5$="0"+RIGHT$(Q5$,1)
1290 Q4$=Q4$+":"+RIGHT$(Q5$,2):Z=LEN(Q4$)
1300 PRINTTAB(22);QQ$;TAB(29-Z);Q4$:NEXT X:PRINT"* - VISIBLE AT 9 P.M."
1310 PRINT"SUN'S R.A. ----------   ";A3$:PRINT"R.A. AT 9:00 P.M. ---   ";A5$
1320 PRINT:PRINTTAB(3);"DAY'S (S)KY   NEW (D)ATE";:GOTO1810
1330 A2=K1*A/360:IF A2>K1THENA2=A2-K1
1340 A3=INT(A2/60):A4=A2-A3*60:A5=A3+9:IF A5>23THENA5=A5-24
1350 A4=INT(A2-A3*60+.5):IF A4=60THENA4=0:A3=A3+1
1360 IF A3=24THENA3=0
1370 AA=A3*60+A4:GOTO1690
1380 M1=((Y1/M9)-INT(Y1/M9))*M9+10:IF M1>M9THENM1=M1-M9
1390 GOSUB2080:M8=360*M2:IF M8>180THENL$="W"
1400 IF M8<=180THENL$="E"
1410 IF M8>180THENM8=360-M8
1420 X1=M1:GOSUB101:M1=X1:X1=M8:GOSUB101:M8=X1:YY=INT(7*(Y1/7-INT(Y1/7))+.2):IF YY=0THENYY=7
1430 K$=MID$(J$,(YY*3)-2,3):RETURN
1440 Q3=0:Q1=E+180:IF Q1>360THEN1480
1450 IF A2>EANDA2<Q1THEN1470
1460 Q3=1:RETURN
1470 Q3=-1:RETURN
1480 Q1=Q1-360:IF A2<=360ANDA2>ETHEN1470
1490 IF Q3<>0THENRETURN
1500 IF A2>0ANDA2<=Q1THEN1470
1510 IF Q3<>0THENRETURN
1520 IF A2>Q1THEN1460
1530 RETURN
1540 Q5=Q3*P(X,5)*4+AA:IF Q5<0 THEN Q5=Q5+K1
1550 IF Q5>K1 THEN Q5=Q5-K1
1560 P(X,6)=Q5:Q4=INT(Q5/60):Q5=INT(Q5-Q4*60+.5):IF Q5=60 THEN Q5=0:Q4=Q4+1
1570 IF Q4=24 THEN Q4=0
1580 RETURN
1590 SU=A5*60+A4:PS=SU+360:MS=SU-360:IF PS>K1THENPS=PS-K1
1600 IF MS<0THENMS=MS+K1
1610 IF MS>PSTHEN1640
1620 IF P(X,6)<PS AND P(X,6)>MS THEN1670
1630 QQ$=" ":RETURN
1640 IF P(X,6)<K1 AND P(X,6)>MS THEN1670
1650 IF P(X,6)<PS THEN 1670
1660 GOTO1630
1670 QQ$="*":RETURN
1680 PRINT@(32*2),K$;"-- ";TEM$;Y;TAB(20);STR$(ABS(LL));LL$:RETURN
1690 A3$=RIGHT$(STR$(A3),2):IF A3<10 THEN A3$=" "+RIGHT$(A3$,1)
1700 A4$=RIGHT$(STR$(A4),2):IF A4<10 THEN A4$="0"+RIGHT$(A4$,1)
1710 A3$=A3$+":"+RIGHT$(A4$,2):A5$=STR$(A5):IF A5<10 THEN A5$=" "+RIGHT$(A5$,1)
1720 A5$=RIGHT$(A5$,2)+":"+RIGHT$(A4$,2):Q8=7-LEN(A3$):Q9=7-LEN(A5$):RETURN
1730 LY=0:IF Y/4=INT(Y/4)THENLY=1
1740 IF Y/100=INT(Y/100)ANDY/400=INT(Y/400)ANDY/1000=INT(Y/1000)ANDY/4000=INT(Y/4000)THENLY=0
1750 RETURN
1760 Y9=Y+1:IF Y9/4=INT(Y9/4)THENZY=1
1770 IF Y9/100=INT(Y9/100)ANDY9/400<>INT(Y9/400)ANDY9/1000=INT(Y9/1000)ANDY9/4000=INT(Y/4000)THENZY=0
1780 Y1=Y-1977:Y1=Y1*365+INT(Y1/4)+D1:IF Y<2000THEN1800
1790 Y1=Y1-INT((Y-2001)/100)+INT((Y-2001)/400)-INT((Y-1)/4000)
1800 RETURN
1810 I$=INKEY$:IF I$=""THEN1810
1820 KK=ASC(I$):SS=1:IF I$="D" THEN Q=4:GOTO840
1830 IF (I$="S" OR I$="T") AND S1=1 THEN320
1840 IF I$="P" THEN1150
1850 IF I$="L" AND S1=1 THEN2460
1855 IF I$=" " OR I$="M" THEN GOSUB675
1856 IF I$="H" THEN GOSUB465
1857 IF I$="Q" THEN END
1860 GOTO1810
1870 P5=1.376344086:K5=A2*4
1880 K5=ABS(K5-1233.73)*90/K1:K5=K5*D9
1890 IF CC<=1THENCC=CC+84
1900 CD$=MID$(CC$,CC+3,34)
1910 IF MID$(CD$,2,1)<>CHR$(32) AND MID$(CD$,3,1)=CHR$(32) THEN TM$=CD$:CD$=MID$(TM$,1,32):GOTO1940
1920 IF MID$(CD$,33,1)<>CHR$(32) AND MID$(CD$,32,1)=CHR$(32) THEN TM$=CD$:CD$=MID$(TM$,3,32):GOTO1940
1930 CD$=MID$(CD$,2,32)
1940 RETURN
1950 DATA 365.26,29.53059
1951 DATA 59.818184,42.719626,262.364294,52.916763,134.69697,218.79464
1952 DATA 87.97,224.7,686.98,4332.79813,10759.7195,30686.5884
1980 DATA "MERCURY",.3871,"VENUS",.7233,"MARS",1.5237,"JUPITER",5.2028
1990 DATA "SATURN",9.5308,"URANUS",19.182
2000 DATA "SA","SC","LI","VI","LE","CA","GE","TA","AR","PI","AQ","CP"
2010 DATA "NEW","WAXING CRESCENT","1ST QUARTER","WAXING GIBBOUS","FULL"
2020 DATA "WANING GIBBOUS","3RD QUARTER","WANING CRESCENT"
2030 DATA 1770,1719,1620,1500,1418,1365,1335,1310,1290,1275,1260
2040 DATA 1238,1220,1200,1178,1115,915,720,660,640,625,610
2050 PRINT@448,"(R)E-INPUT OR (C)ONTINUE"
2060 Z$=INKEY$:IF Z$=""THEN2060
2070 KK=ASC(Z$):RETURN
2080 M2=M1/M9:IF M1<1 OR M1>28.5 THEN M3=1
2090 IF M1>=1 AND M1<6.9 THEN M3=2
2100 IF M1<=8 AND M1>=6.9 THEN M3=3
2110 IF M1>8 AND M1<14.2 THEN M3=4
2120 IF M1>=14.2 AND M1<=15.2 THEN M3=5
2130 IF M1>15.2 AND M1<21.6 THEN M3=6
2140 IF M1>=21.6 AND M1<=22.6 THEN M3=7
2150 IF M1>22.6 AND M1<=28.5 THEN M3=8
2160 RETURN
2170 B$="":IF Y<>1985 AND Y<>1986 THEN670
2180 IF (Y=1985 AND D1<305)OR(Y=1986 AND D1>149)THEN670
2190 HD=D1+365:IF HD>516 THEN HD=HD-365
2200 H1=(HD-295)/10:HD=INT(H1):H1=H1-HD
2210 T4=HC(HD)-HC(HD+1):T4=HC(HD)-H1*T4:IF T4>K1 THEN T4=T4-K1
2220 GOSUB710:IF Y9=999 THEN670
2230 GOSUB800:IF T4>1115 AND T4<1200 THEN U9=U9+40
2240 IF T4>1290 THEN U9=U9-40
2250 IF T4>615 AND T4<=1115 THEN U9=U9+80
2260 U(7)=U9:B$=CHR$(PP(7))+"HALLEY'S COMET":GOTO630
2290 PRINT@(32*(Q-1)),OO$;:FORI=1TO1000:NEXTI:PRINT@(32*(Q-1)),"              ";:RETURN
2380 GOSUB800:PK=1423-Y9+U9+LB
2390 IF LL<0 THEN PK=2247+80*XX-PK
2400 RETURN
2410 LL$="'N":IF LL<0THENLL$="'S"
2420 L1=ABS(LL):IF ABS(LL)<24THENL1=40
2430 LC=INT((L1-40)/7+.5):LB=LC*40:D1=VAL(MID$(D$,M*3-2,3))+D
2440 IF ABS(LL)<24THENLB=40*INT(LL/7+.5)
2450 RETURN
2460 GOSUB2510:PRINT@(32*5),"LATITUDE CHANGE":PRINT@(32*6),"----------------"
2470 PRINT@(32*8),"INPUT NEW LATITUDE";:INPUTLL
2480 IF ABS(LL)>90THEN2470
2490 GOSUB2050:IF Z$="R"THEN2460
2500 GOSUB2410:I$="S":GOTO1830
2510 CLS:PRINT@(32*1)+7,"**** SKYSCAPE ****";:Q=1:GOSUB1680:RETURN
30000 REM *ARCTANGENT* INPUT X, OUTPUT C1,A6
30670 C1 IS IN DEGREES. A6 IS IN RADIANS
30690 T=SGN(X1):X1=ABS(X1):C1=0
30700 IF X1>1THENC1=1:X1=1/X1
30710 A6=X1*X1
30720 B=((2.86623E-3*A6-1.61657E-2)*A6+4.29096E-2)*A6
30730 B=((((B-7.5289E-2)*A6+.106563)*A6-.142089)*A6+.199936)*A6
30740 A6=((B-.333332)*A6+1)*X1
30750 IF C1=1 THEN A6=1.570796-A6
30760 A6=T*A6:C1=A6*57.29577951:RETURN
