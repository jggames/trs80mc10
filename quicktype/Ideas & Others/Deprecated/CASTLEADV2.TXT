9 CLS:GOSUB8600
10 DIM D(8,8,8),T1$(11),T2(11)
11 FOR Z=1 TO 8
12 FOR Y=1 TO 8
13 FOR X=1 TO 8
14 D(X,Y,Z)=0
15 NEXT:NEXT:NEXT
16 B$="":V=0
17 X1=1:Y1=1
18 Z1=1:G=0
19 A=10:F=60:L=10:W=0
22 Z2=RND(2)+6
23 GOSUB9000:X2=X:Y2=Y
24 O=0 
36 FOR K=1 TO 8
37 T2(K)=0
38 NEXT K
310 FOR Z=1 TO 8
311 PRINT".";
320 FOR K=1 TO 10
330 GOSUB9000
340 IF D(X,Y,Z)<>0 THEN 330
350 K1=RND(10)
355 K1=K1*2
360 D(X,Y,Z)=K1 
370 NEXT K
390 FOR K=1 TO 3
400 GOSUB9000
410 IF D(X,Y,Z)<>0 THEN 400
420 D(X,Y,Z)=30
430 GOSUB9000
440 IF D(X,Y,Z)<>0 THEN 430
450 D(X,Y,Z)=40
460 GOSUB9000
470 IF D(X,Y,Z)<>0 THEN 460
480 D(X,Y,Z)=50
490 GOSUB9000
500 IF D(X,Y,Z)<>0 THEN 490
510 D(X,Y,Z)=60
512 GOSUB9000
513 IF D(X,Y,Z)<>0 THEN 512
514 D(X,Y,Z)=150
520 NEXT K
530 GOSUB9000
540 IF D(X,Y,Z)<>0 THEN 530
550 D(X,Y,Z)=70
560 GOSUB9000
570 IF D(X,Y,Z)<>0 THEN 560
580 D(X,Y,Z)=80
600 GOSUB9000
610 IF D(X,Y,Z)<>0 THEN 600
620 IF Z<8 THEN D(X,Y,Z)=100
630 IF Z<8 THEN D(X,Y,Z+1)=90 
650 GOSUB9000
660 IF D(X,Y,Z)<>0 THEN 650
670 D(X,Y,Z)=110
690 GOSUB9000
700 IF D(X,Y,Z)<>0 THEN 690
710 D(X,Y,Z)=118+(2 * Z)
720 NEXT Z
731 GOSUB9000
732 IF D(X,Y,Z1)<>0 THEN 731
733 D(X,Y,Z1)=90
900 FOR Y=1 TO 8:FOR X=1 TO 8
903 IF D(X,Y,Z1)=90 THEN X1=X
904 IF D(X,Y,Z1)=90 THEN Y1=Y
905 NEXT:NEXT
906 PRINT
907 GOTO2000
910 CLS
921 K=D(X1,Y1,Z1)/2-INT(D(X1,Y1,Z1)/2)
922 IF K=0 THEN D(X1,Y1,Z1)=D(X1,Y1,Z1)+1
1000 FOR Y=1 TO 8
1011 B1$=""
1020 FOR X=1 TO 8
1021 B$="?"
1030 IF (D(X,Y,Z1)/2)-INT(D(X,Y,Z1)/2)=0 THEN 1210
1040 IF D(X,Y,Z1)=1 THEN B$="."
1050 IF D(X,Y,Z1)>1 AND D(X,Y,Z1)<22 THEN B$="M"
1060 IF D(X,Y,Z1)=31 THEN B$="P"
1070 IF D(X,Y,Z1)=41 THEN B$="B"
1080 IF D(X,Y,Z1)=51 THEN B$="C"
1090 IF D(X,Y,Z1)=61 THEN B$="O"
1100 IF D(X,Y,Z1)=71 THEN B$="W"
1110 IF D(X,Y,Z1)=81 THEN B$="S"
1120 IF D(X,Y,Z1)=91 THEN B$="U"
1130 IF D(X,Y,Z1)=101 THEN B$="D"
1140 IF D(X,Y,Z1)=111 THEN B$="V"
1141 IF D(X,Y,Z1)=151 THEN B$="L"
1142 IF D(X,Y,Z1)>120 AND D(X,Y,Z1)<136 THEN B$="T"
1200 IF X1=X AND Y1=Y THEN B$="<"+B$+">"
1210 IF X1<>X OR Y1<>Y THEN B$=" "+B$+" "
1220 B1$=B1$+B$
1222 B$=""
1230 IF X=8 THEN PRINTB1$
1240 NEXT X
1250 NEXT Y
1260 RETURN
2000 IF Z1=0 THEN GOSUB5400
2007 K=0
2011 IF A=0 THEN 9900
2012 IF F=0 THEN 9900
2050 GOSUB910
2100 PRINT"YOU ARE AT (";X1;",";Y1;") LEVEL";Z1
2110 PRINT"THERE IS";:GOSUB9100:GOSUB10200
2155 IF D(X1,Y1,Z1)>1 AND D(X1,Y1,Z1)<22 THEN GOSUB7000
2156 IF D(X1,Y1,Z1)=71 THEN GOSUB7500
2157 IF D(X1,Y1,Z1)=81 THEN GOSUB7800
2158 IF D(X1,Y1,Z1)=111 THEN GOSUB6000
2159 IF D(X1,Y1,Z1)=151 THEN GOSUB 5210
2160 K1=D(X1,Y1,Z1)
2161 IF K1>120 AND K1<136 THEN GOSUB8500
2162 IF X1=X2 AND Y1=Y2 AND Z1=Z2 THEN GOSUB5600
2165 IF K=999 THEN 2000
2166 IF F<15 THEN PRINT"YOU FEEL VERY HUNGRY."
2170 PRINT"ENTER YOUR COMMAND"
2172 PRINT"('?' FOR HELP):"
2186 GOSUB6900
2188 C1$=C$:C2=0
2190 IF C$="E" OR C$="N" THEN GOSUB8000
2191 IF C$="S" OR C$="W" THEN GOSUB8000
2192 IF C$="U" OR C$="D" THEN GOSUB8000
2193 IF C$="Q" AND D(X1,Y1,Z1)=31 THEN GOSUB4100
2194 IF C$="O" AND D(X1,Y1,Z1)=51 THEN GOSUB4500
2195 IF C$="O" AND D(X1,Y1,Z1)=41 THEN GOSUB4900
2196 IF C$="G" AND D(X1,Y1,Z1)=61 THEN GOSUB5220
2198 IF C$="L" THEN GOSUB6700
2199 IF C$="X" THEN GOSUB9996
2200 IF C1$<>"?" AND C1$<>"I" THEN 3990
2202 IF C$="?" THEN GOSUB9500
2205 IF C$="I" THEN GOSUB9650
2210 IF C1$="?" THEN 2000
2215 IF C1$="I" THEN 2000
3990 GOSUB5650
4000 GOTO2000
4100 PRINT"YOU TAKE A SOOTHING DRAUGHT"
4115 PRINT"FROM THE COOL POOL OF WATER...."
4120 K=RND(0)
4125 IF K>.55 THEN 4200
4130 IF K>.45 THEN 4300
4135 PRINT"THE WATER REFRESHES YOU."
4140 GOTO4400
4200 PRINT"...THE WATER HAS MAGICAL":PRINT"PROPERTIES!"
4210 PRINT"YOU NOTICE YOUR CUTS HEALING."
4215 IF A<10 THEN A=A+1
4220 GOTO4400
4300 PRINT"...YOU FEEL SLEEPY...YOU REST"
4301 PRINT"YOUR HEAD ON YOUR PACK"
4305 PRINT"FOR A MOMENT AND RELAX..."
4310 PRINT"HOW MUCH TIME DID YOU SLEEP?"
4315 PRINT"YOU FEEL A BIT HUNGRIER..."
4320 F=F-1
4400 GOSUB10100
4411 F=F-1
4420 RETURN
4500 PRINT"YOU OPEN THE CHEST..."
4515 K=RND(0)
4520 IF K>.85 THEN 4600
4525 IF K>.15 THEN 4700
4530 PRINT"...AND FIND THAT IT IS EMPTY."
4535 GOTO4800
4600 PRINT"BANG! THE CHEST EXPLODES!"
4610 K=RND(0):IF K>.5 THEN A=A-1
4615 GOTO4800
4700 K=RND(150)+24
4705 PRINT"YOU FIND";K;"GOLD COINS!"
4706 G=G+K
4710 K=RND(0)
4715 IF K>.5 THEN PRINT"YOU FIND PRESERVED FOOD!"
4716 IF K>.5 THEN F=F+RND(6)
4720 IF K>.7 THEN PRINT"YOU FIND A HEALING POTION!"
4722 IF K>.7 AND A<10 THEN A=A+1
4725 IF K>.9 THEN PRINT"YOU FIND A DAGGER!"
4728 IF K>.9 AND W<2 THEN W=2
4730 IF K>.9 AND W>2 THEN PRINT"BUT YOUR WEAPON "
4735 IF K>.9 AND W>2 THEN PRINT"IS SUPERIOR TO IT."
4800 D(X1,Y1,Z1)=1
4801 GOSUB10100
4811 F=F-1 
4815 RETURN
4900 PRINT"YOU STUDY THE OLD BOOK..."
4915 K=RND(0)
4920 IF K>.9 THEN 5050
4925 IF K>.7 THEN 5100
4930 IF K>.3 THEN 5000
4950 PRINT"IT'S A GUIDE TO PROGRAMMING"
4951 PRINT"GAMES ON THE MC-10."
4955 GOTO5200
5000 PRINT"YOU READ A DELIGHTFUL STORY":PRINT"ABOUT AN UNDERACHIEVING GENIUS"
5001 PRINT"WHO WORKS AT A FACTORY."
5003 GOTO5200
5050 PRINT"IT'S A MAP OF THIS LEVEL":PRINT"OF THE CASTLE!"
5051 FOR Y=1 TO 8:FOR X=1 TO 8
5052 K=D(X,Y,Z1)
5053 IF (K/2)-INT(K/2)=0 THEN K=K+1
5054 D(X,Y,Z1)=K
5055 NEXT X:NEXT Y
5056 GOTO5200
5100 PRINT"IT'S A MAP OF THE CASTLE!"
5101 PRINT"(TREASURE CHESTS ARE MARKED!)"
5102 FOR Z=1 TO 8:FOR Y=1 TO 8:FOR X=1 TO 8
5103 IF D(X,Y,Z)=50 THEN D(X,Y,Z)=D(X,Y,Z)+1
5104 NEXT X:NEXT Y:NEXT Z
5200 D(X1,Y1,Z1)=1
5201 GOSUB10100
5203 F=F-1
5204 RETURN
5210 L=L+RND(3)
5212 PRINT"YOU NOW HAVE";L;"TURNS OF OIL."
5213 D(X1,Y1,Z1)=1
5214 RETURN
5220 PRINT"YOU GAZE INTO THE CRYSTAL ORB."
5223 PRINT"YOU SEE HAZY CLOUDY IMAGES..."
5224 PRINT"YOU GAZE...GAZE...GAZE..."
5225 K=RND(0)
5226 IF K>.75 THEN 5260
5227 IF K>.4 THEN 5240
5228 IF K>.2 THEN 5236
5229 PRINT"YOU WATCH A RERUN OF 'THE GEEK'"
5235 GOTO5300
5236 PRINT"YOU SEE NOTHING BUT CLOUDY":PRINT"SHADOWS."
5237 GOTO5300
5240 GOSUB9000
5241 Z=RND(8)
5242 K=D(X,Y,Z):IF (K/2)-INT(K/2)=0 THEN K=K+1
5243 GOSUB9108
5244 PRINT"AT (";X;",";Y;") LEVEL";Z
5245 PRINT"YOU SEE";:GOSUB10200
5246 D(X,Y,Z)=K
5250 GOTO5300
5260 PRINT"YOU SEE THE ORB OF ZOT AT":PRINT"(";X2;",";Y2;") LEVEL";Z2
5300 GOSUB10100
5302 F=F-1 
5303 RETURN
5400 PRINT"YOU WANT TO EXIT THE CASTLE?"
5411 PRINT"(Y)ES OR (N)O?"
5415 GOSUB6900
5420 IF C$<>"Y" THEN 5500
5422 PRINT"YOU LEAVE THE DUNGEON AND INHALE";
5423 PRINT"THE FRESH AIR OF THE COUNTRYSIDE";
5424 PRINT"AS THE CASTLE COLLAPSES BEHIND":PRINT"YOU."
5425 GOSUB9800
5428 GOTO9925
5500 Z1=1
5501 GOSUB10100
5505 RETURN
5600 PRINT"YOU FOUND THE ORB OF ZOT!"
5602 PRINT"FROM BENEATH A PILE OF OLD":PRINT"DEBRIS YOU REMOVE A"
5603 PRINT"SHIMMERING GLOBE OF POLISHED"
5604 PRINT"CRYSTAL. IT IS NOW YOURS!"
5605 X2=0
5606 O=1
5607 RETURN
5650 IF RND(0)>.25 THEN 5700
5654 K=RND(5)
5656 IF K=1 THEN Y2=Y2-1:GOTO5668
5657 IF K=2 THEN Y2=Y2+1:GOTO5668
5658 IF K=3 THEN X2=X2+1:GOTO5668
5660 IF K=4 THEN X2=X2-1:GOTO5668
5662 IF K=5 AND D(X2,Y2,Z2)=90 THEN Z2=Z2-1:GOTO5668
5664 IF K=5 AND D(X2,Y2,Z2)=91 THEN Z2=Z2-1:GOTO5668
5668 IF X2>8 THEN X2=8
5669 IF X2<1 THEN X2=1
5670 IF Y2>8 THEN Y2=8
5671 IF Y2<1 THEN Y2=1
5672 IF Z2>8 THEN Z2=8
5673 IF Z2<1 THEN Z2=1
5700 RETURN 
6000 IF V=1 THEN GOSUB7000
6006 IF V=1 THEN 6390
6010 PRINT"A VENDOR IS HERE SELLING WARES."
6020 PRINT"(T)RADE,(I)GNORE,(A)TTACK."
6030 GOSUB6900
6040 IF C$="I" THEN 6390
6060 IF C$="A" THEN GOSUB7000
6065 IF V=1 THEN 6390
6070 FOR K=1 TO 8
6080 IF T2(K)>0 THEN PRINT"SELL A ";T1$(K);
6090 IF T2(K)>0 THEN PRINT" FOR";T2(K)/4;"?"
6100 IF T2(K)>0 THEN GOSUB6900
6101 GOTO6130
6110 IF C$="Y" THEN G=G+(T2(K)/4)
6120 IF C$="Y" THEN T2(K)=0
6130 NEXT K
6140 IF G>999 THEN PRINT"WEAPONS FOR SALE:"
6145 IF G>999 THEN PRINT"(N)OTHING"
6150 IF G>999 THEN PRINT"(D)AGGER-1000"
6160 IF G>1999 THEN PRINT"(S)WORD-2000"
6170 IF G>3499 THEN PRINT"(A)XE-3500"
6180 IF G>999 THEN GOSUB6900
6190 IF C$="D" AND G>999 THEN W=2
6195 IF C$="D" AND G>999 THEN G=G-1000
6200 IF C$="S" AND G>1999 THEN W=4
6205 IF C$="S" AND G>1999 THEN G=G-2000
6210 IF C$="A" AND G>2999 THEN W=6
6220 IF C$="A" AND G>2999 THEN G=G-3000
6250 IF A<10 AND G>499 THEN PRINT"HEAL WOUNDS?"
6260 IF A<10 AND G>499 THEN PRINT"(Y)ES OR (N)O 500GP?"
6265 IF A<10 AND G>499 THEN GOSUB6900
6270 IF C$="Y" THEN A=A+2:GOTO6272
6271 GOTO6300
6272 IF A>10 THEN A=10
6275 PRINT"YOUR STRENGTH IS NOW";A
6280 IF A<10 AND G>499 THEN 6250
6299 IF G<5 THEN 6330
6300 PRINT"YOU HAVE";L;"LAMP OIL."
6310 PRINT"BUY SOME OIL FOR 10GP?"
6320 PRINT"(Y)ES OR (N)O."
6325 GOSUB6900
6330 IF C$="Y" THEN L=L+10:GOTO6331
6331 GOTO6350
6335 IF C$="Y" THEN G=G-10
6340 IF G>9 THEN 6300
6345 IF G<10 THEN 6380
6350 PRINT"YOU HAVE";F;"TURNS WORTH OF":PRINT"FOOD."
6355 PRINT"BUY 10 TURNS OF FOOD FOR 10GP?"
6360 GOSUB6900
6365 IF C$="Y" THEN F=F+10:GOTO6370
6366 GOTO6380
6370 IF C$="Y" THEN G=G-10
6375 IF G>9 THEN 6350
6380 PRINT"THANK YOU FOR SHOPPING HERE!"
6381 GOSUB10100
6385 GOSUB910
6389 F=F-1
6390 RETURN
6700 IF L<1 THEN PRINT"YOU'RE OUT OF OIL!"
6702 IF L<1 THEN 6780
6705 Y3=0:X3=0
6710 PRINT"SHINE LAMP IN WHICH DIRECTION?"
6715 PRINT"(N)ORTH,(S)OUTH,(E)AST,(W)EST?"
6720 GOSUB6900
6721 X3=X1:Y3=Y1
6725 IF C$="N" THEN Y3=Y1-1
6730 IF C$="S" THEN Y3=Y1+1
6735 IF C$="W" THEN X3=X1-1
6740 IF C$="E" THEN X3=X1+1
6745 IF X3>8 THEN X3=8
6746 IF X3<1 THEN X3=1
6750 IF Y3>8 THEN Y3=8
6751 IF Y3<1 THEN Y3=1
6755 K=D(X3,Y3,Z1)
6756 IF (K/2)-INT(K/2)=0 THEN K=K+1
6760 D(X3,Y3,Z1)=K
6765 GOSUB9108
6770 PRINT"YOU SEE";:GOSUB10200
6775 GOSUB10100
6778 L=L-1:F=F-1
6780 RETURN
6900 C$=INKEY$:R=RND(1000):IFC$=""THEN6900
6930 RETURN
6940 F=F+RND(20)+9
6942 PRINT"YOU TAKE THE VENDOR'S FOOD."
6943 IF A<10 THEN A=10
6944 PRINT"YOU DRINK HIS HEALING POTIONS."
6945 L=L+INT(RND(20))+10
6946 PRINT"YOU TAKE ALL HIS LAMP OIL."
6947 IF W<4 THEN PRINT"YOU TAKE HIS SWORD."
6948 IF W<4 THEN W=4
6950 PRINT"YOU MURDEROUS ROGUE!"
6951 RETURN
7000 PRINT"YOU ARE FACING AN EVIL ";M$
7015 PRINT"YOUR STRENGTH IS";A
7020 PRINT"(A)TTACK OR (R)ETREAT."
7030 GOSUB6900
7070 IF C$="R" THEN 7400
7080 K=RND(A+W) 
7085 K1=RND(M1)
7090 IF K>=K1 THEN 7200
7100 PRINT"THE EVIL ";M$:PRINT"WOUNDED YOU!":A=A-1
7102 IF A=0 THEN 9900
7110 GOTO7000
7200 PRINT"YOU SLAY THE EVIL ";M$;"!"
7201 D(X1,Y1,Z1)=1
7210 K1=(M1*15)+RND(99)
7215 G=G+K1
7220 PRINT"YOU COLLECT IT'S HOARD OF";K1:PRINT"GOLD COINS."
7221 IF M1=20 THEN GOSUB6940
7230 K=RND(0)
7235 IF K>.5 THEN 7490
7240 F=F+(2*M1)
7250 PRINT"YOU SPEND AN HOUR EATING":PRINTM$; 
7255 K=RND(0)
7260 IF K<.5 THEN PRINT" KABOBS.":GOTO7490
7261 PRINT" STEW.":GOTO7490
7400 IF RND(0)<.5-(M1/100) THEN 7450
7410 PRINT"THE ";M$;" BLOCKS YOUR WAY!"
7412 K=RND(0)
7415 IF RND(0)<.5+(M1/100) THEN 7100
7421 IF A=0 THEN 9900
7425 GOTO7000
7450 PRINT"YOU PICK A PASSAGE RANDOMLY":PRINT"AND ESCAPE!"
7455 K=RND(4)
7460 IF K=1 THEN Y1=Y1+ 1
7462 IF K=2 THEN Y1=Y1-1
7464 IF K=3 THEN X1=X1-1
7466 IF K=4 THEN X1=X1+1
7480 IF X1<1 THEN X1=1
7481 IF X1>8 THEN X1=8
7482 IF Y1<1 THEN Y1=1
7483 IF Y1>8 THEN Y1=8
7490 GOSUB10100
7495 K=999
7496 IF M1=20 AND V=0 THEN V=1
7499 RETURN
7500 GOSUB9000:Z=RND(8)
7520 PRINT"YOU ARE SUCKED INTO A VORTEX!"
7525 PRINT"YOU TRAVEL DOWN A MAGIC TUNNEL!"
7526 PRINT"THUMP!  YOU GET UP AND GET":PRINT"YOUR BEARINGS."
7527 GOSUB10100
7530 X1=X:Y1=Y:Z1=Z
7538 K=999
7540 RETURN
7800 PRINT"THE FLOOR CAVES AT YOUR STEP!"
7815 PRINT"JUST AS YOU THINK YOU WILL BE"
7820 PRINT"DROWNED IN QUICKSAND, YOU HIT"
7825 PRINT"A HARD FLOOR.  THUMP!"
7826 GOSUB10100
7830 Z1=Z1+1:IF Z1>8 THEN Z1=1
7840 K=999
7850 RETURN
8000 IF C$="E" THEN X1=X1+1
8010 IF C$="S" THEN Y1=Y1+1 
8015 IF C$="W" THEN X1=X1-1
8020 IF C$="N" THEN Y1=Y1-1
8025 IF X1>8 THEN X1=8
8026 IF X1<1 THEN X1=1
8030 IF Y1>8 THEN Y1=8
8031 IF Y1<1 THEN Y1=1
8040 IF C$="D" AND D(X1,Y1,Z1)=101 THEN Z1=Z1+1
8050 IF C$="U" AND D(X1,Y1,Z1)=91 THEN Z1=Z1-1
8091 IF C$="D" AND D(X1,Y1,Z1)=101 THEN F=F-1
8092 IF C$="U" AND D(X1,Y1,Z1)=101 THEN F=F-1
8093 IF C$="E" OR C$="W" THEN F=F-1
8094 IF C$="N" OR C$="S" THEN F=F-1
8100 RETURN
8500 PRINT"YOU FOUND THE ";M$;"!":PRINT"WELL DONE!"
8511 D(X1,Y1,Z1)=1
8514 K=0
8515 FOR K1=1 TO 8
8516 IF K=1 THEN 8522 
8517 IF T2(K1)=0 THEN K=1
8518 IF T2(K1)=0 THEN T1$(K1)=M$
8520 IF T2(K1)=0 THEN T2(K1)=M1
8522 NEXT K1
8530 RETURN
8600 PRINT"THE CASTLE ADVENTURE"
8601 PRINT"ORIGINALLY PUBLISHED IN DAVID"
8602 PRINT"AHL'S CREATIVE COMPUTING":PRINT"MAGAZINE."
8603 PRINT
8609 PRINT"DO YOU NEED INSTRUCTIONS? (Y/N)"
8610 GOSUB6900:IF C$="" THEN 8610
8612 IF C$="N" THEN 8800
8613 CLS:PRINT"LONG AGO, THE WIZARD BERGAL DIED";
8614 PRINT"AND LEFT HIS 'ORB OF ZOT' BEHIND";
8615 PRINT"ALONG WITH A COLLECTION OF"
8616 PRINT"JEWELS GUARDED BY MANY MONSTERS"
8618 PRINT"ENTER YOU, A PEASANT FARMER WITH";
8619 PRINT"NO HOPE BUT TO VENTURE INTO THE"
8620 PRINT"CASTLE AND TRY TO CLAIM THE"
8621 PRINT"ORB FOR YOURSELF."
8622 PRINT"YOU SET OFF INTO THE CASTLE WITH";
8623 PRINT"ONLY A SACK OF FOOD AND A LAMP."
8624 GOSUB10100:CLS
8626 PRINT"THE CASTLE IS REPRESENTED BY"
8627 PRINT"AN 8X8 GRID."
8628 PRINT"YOU START ON THE TOP AND MUST"
8629 PRINT"MAKE YOUR WAY TOWARD THE 8TH"
8630 PRINT"LEVEL TO FIND THE ORB OF ZOT."
8631 PRINT"ON THE WAY YOU FIGHT MONSTERS,"
8632 PRINT"FIND TREASURES, AND BUY FROM"
8633 PRINT"SHADY DUNGEON VENDORS."
8634 PRINT"IN THE GAME HIT '?' FOR HELP."
8635 GOSUB10100:CLS
8637 PRINT"THE MAP IS UNREVEALED AT FIRST."
8638 PRINT"EXPLORE WITH N,S,E,W,U,D. "
8639 PRINT"USE YOUR (L)AMP TO SEE INTO"
8640 PRINT"ROOMS. (O)PEN BOOKS AND CHESTS."
8641 PRINT"(Q)UAFF WATER FROM POOLS. "
8643 PRINT"CHECK YOUR (I)NVENTORY AND SEE"
8644 PRINT"YOUR STRENGTH, FOOD, AND GOLD."
8645 PRINT"(G)AZE INTO ORBS TO REVEAL THE "
8646 PRINT"LOCATION OF THE ORB OF ZOT."
8647 GOSUB10100
8800 CLS:RETURN
9000 X=RND(8)
9010 Y=RND(8)
9015 RETURN
9100 K=D(X1,Y1,Z1):M1=0
9105 IF (K/2)-INT(K/2)=0 THEN K=K+1
9107 D(X1,Y1,Z1)=K 
9108 M$="NOT IMPLEMENTED"
9110 IF K=1 THEN M$="EMPTY ROOM." 
9120 IF K=3 THEN M$="BAT"
9121 IF K=3 THEN M1=3
9130 IF K=5 THEN M$="RAT"
9131 IF K=5 THEN M1=4
9140 IF K=7 THEN M$="KOBOLD"
9141 IF K=7 THEN M1=5
9150 IF K=9 THEN M$="BEETLE"
9151 IF K=9 THEN M1=5
9160 IF K=11 THEN M$="SPIDER" 
9161 IF K=11 THEN M1=6
9170 IF K=13 THEN M$="GOBLIN"
9171 IF K=13 THEN M1=7
9180 IF K=15 THEN M$="ORC"
9181 IF K=15 THEN M1=8
9190 IF K=17 THEN M$="GARGOYLE" 
9191 IF K=17 THEN M1=10
9200 IF K=19 THEN M$="TROLL"
9201 IF K=19 THEN M1=12
9210 IF K=21 THEN M$="DRAGON"
9211 IF K=21 THEN M1=15
9220 IF K=31 THEN M$="POOL OF WATER."
9230 IF K=41 THEN M$="OLD DUSTY BOOK."
9240 IF K=51 THEN M$="WOODEN CHEST."
9250 IF K=61 THEN M$="CRYSTAL ORB."
9260 IF K=71 THEN M$="MAGICAL WARP."
9270 IF K=81 THEN M$="SINK HOLE!"
9280 IF K=91 THEN M$="STAIRWAY LEADING UP."
9290 IF K=101 THEN M$="STAIRWAY GOING DOWN."
9300 IF K=111 THEN M$="VENDOR" 
9301 IF K=111 THEN M1=20
9310 IF K=121 THEN M$="AMETHYST"
9311 IF K=121 THEN M1=400
9313 IF K=123 THEN M$="AGATE"
9314 IF K=123 THEN M1=600
9315 IF K=125 THEN M$="BLOODSTONE"
9316 IF K=125 THEN M1=800
9317 IF K=127 THEN M$="OPAL"
9318 IF K=127 THEN M1=1600
9319 IF K=129 THEN M$="RUBY"
9320 IF K=129 THEN M1=2400
9321 IF K=131 THEN M$="EMERALD"
9322 IF K=132 THEN M1=3200
9323 IF K=133 THEN M$="SAPPHIRE"
9324 IF K=133 THEN M1=4000
9325 IF K=135 THEN M$="DIAMOND"
9326 IF K=135 THEN M1=5600
9328 IF K=151 THEN M$="LAMP OIL"
9450 RETURN
9500 PRINT"LIST OF COMMANDS"
9520 PRINT"?-GET HELP  X-EXIT GAME"
9523 PRINT"N-NORTH E-EAST S-SOUTH W-WEST"
9526 PRINT"D-DOWN  U-UP I-INVENTORY"
9529 PRINT"G-GAZE INTO CRYSTAL ORB"
9532 PRINT"Q-QUAFF WATER FROM A POOL"
9535 PRINT"O-OPEN A BOOK OR CHEST"
9538 PRINT
9540 PRINT"MAP SYMBOL KEY"
9543 PRINT"==============="
9545 PRINT". EMPTY ROOM   O-CRYSTAL ORB"
9548 PRINT"B-BOOK   C-CHEST  L-LAMP OIL"
9551 PRINT"M-MONSTER   P-POOL OF WATER"
9555 PRINT"D-DOWNSTAIR U-UPSTAIR"
9556 PRINT"T-TREASURE"
9560 GOSUB10100
9580 RETURN
9650 PRINT"INVENTORY AND STATUS"
9655 PRINT"CURRENT STRENGTHA
9660 PRINT"WEAPON: ";
9662 IF W=2 THEN PRINT"DAGGER"
9663 IF W=4 THEN PRINT"SWORD"
9664 IF W=6 THEN PRINT"AXE"
9665 IF W=0 THEN PRINT"NONE"
9670 PRINT"TURNS OF FOOD:";F
9675 PRINT"TURNS OF LAMP OIL:";L
9680 PRINT"GOLD COINS:";G
9685 PRINT"TREASURES FOUND:"
9690 FOR K=1 TO 8
9700 IF T2(K)>0 THEN PRINTT1$(K),
9710 NEXT K:PRINT
9720 IF O=1 THEN PRINT"YOU HAVE THE ORB OF ZOT."
9722 GOSUB10100
9730 RETURN
9800 K1=0
9810 PRINT"YOUR FINAL SCORE:"
9815 FOR K=1 TO 8
9816 K1=K1+T2(K)
9818 NEXT K
9820 PRINTK1;"POINTS OF TREASURE"
9822 PRINTG;"POINTS FOR YOUR GOLD"
9824 IF O=1 THEN PRINT"10,000 FOR THE ORB OF ZOT"
9825 IF A=0 OR F=0 THEN PRINT"HALF SCORE FOR DYING"
9826 K1=K1+G 
9827 IF O=1 THEN K1=K1+10000
9828 IF A=0 OR F=0 THEN K1=INT(K1/2)
9828 PRINT"FOR A FINAL SCORE OF";K1
9830 RETURN
9900 IF A=0 THEN PRINT"YOU DIED FOR LACK OF STRENGTH."
9905 IF F=0 THEN PRINT"YOU STARVED TO DEATH!"
9910 PRINT
9915 PRINT"IT WAS A VALIANT EFFORT."
9920 PRINT"WHAT YOU HAD WHEN YOU DIED:"
9922 GOSUB9660
9924 GOSUB9800
9925 IF O=0 THEN PRINT"THE ORB OF ZOT WAS AT:";
9930 IF O=0 THEN PRINT"(";X2;",";Y2;") LEVEL";Z2
9935 PRINT
9940 PRINT"PLAY AGAIN?"
9944 GOSUB6900
9955 IF C$<>"N" THEN 9970
9956 PRINT"SEE YA."
9956 END
9970 PRINT"PLEASE WAIT WHILE I RESET:PRINT"THE DUNGEON"
9975 GOTO11
9996 PRINT"ARE YOU SURE?"
9997 GOSUB6900
9998 IF C<>"Y" THEN RETURN
9999 GOTO9915
10000 END
10100 PRINT"-PRESS ANY KEY TO CONTINUE-";
10110 GOSUB6900:RETURN
10200 PRINT" A";:TM$=LEFT$(M$,1):IFTM$="A"ORTM$="I"ORTM$="E"ORTM$="O"ORTM$="U"THENPRINT"N";
10210 PRINT" ";M$:RETURN
