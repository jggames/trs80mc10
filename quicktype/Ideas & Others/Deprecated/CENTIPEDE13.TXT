0 CLS:GOSUB9200:DIMA(11),D(11),X(11),Y(11),H(255),K(255):GOSUB9300:CLS0
1 DIMA,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,N,MC,LV,HT,HH,GG,SC,HS,SF,L$,A$,I$:GOSUB9000
2 FORC=ITOZ:FORT=ITOL:ONK(PEEK(W))GOSUB5
3 ONH(PEEK(A(T)))GOSUB7,4,4,4,4,35,4:POKEX(T),R:POKEA(T),O:X(T)=A(T):A(T)=A(T)+D(T):NEXT:NEXT:GOTO100
4 A(T)=A(T)-D(T)+Y(T):D(T)=D(T)*N:ONH(PEEK(A(T)))GOSUB7:RETURN
5 ONK(PEEK(Q))GOSUB8:ONK(PEEK(D))GOSUB26:ONK(PEEK(K))GOSUB28
6 RETURN
7 G=1:M=31:POKEJ,M:SOUND1,2:C=Z:RETURN
8 IFINKEY$=A$THENRETURN
9 U=R:FORS=J-RTOVSTEP-R:ONH(PEEK(S))GOSUB6,11,12,13,6,6,17:GOSUB16:NEXT:RETURN
11 U=239:GOSUB16:GOSUB14:RETURN
12 U=255:GOSUB16:GOSUB14:RETURN
13 U=R:GOSUB16:GOSUB14:RETURN
14 U=96:S=MC:SC=SC+10:PRINT@I,SC;:RETURN
16 POKES,42:FORX=ITOE:NEXT:POKES,U:RETURN
17 FORP=1TOL:IFS=X(P)THENU=159:GOSUB16:ONRND(E)GOSUB20,21,21:U=96:S=MC:PRINT@I,SC;:IFSF=RTHENSC=SC+150:PRINT@I,SC
18 NEXT:RETURN
19 RETURN
20 SF=D(P):X(P)=X(P)+R:A(P)=X(P)+R:D(P)=R:Y(P)=R:POKES+R,0:SC=SC+100:RETURN
21 SF=D(P):GOSUB30:A(P)=X:X(P)=X:D(P)=O:Y(P)=O:HT=HT+I:SC=SC+100:IFHT=LTHENC=Z
22 RETURN
26 POKEJ,R:ONH(PEEK(J-I))GOSUB6,32,32,32,32,6,34
27 J=J-I:POKEJ,M:RETURN
28 POKEJ,R:ONH(PEEK(J+I))GOSUB6,33,33,33,33,6,34
29 J=J+I:POKEJ,M:RETURN
30 Y=VARPTR(L$):A=PEEK(Y+2):B=PEEK(Y+3)
31 X=(A*256)+B+P:RETURN
32 J=J+I:RETURN
33 J=J-I:RETURN
34 G=1:M=31:POKEJ,M:SOUND1,2:POKEJ,R
35 C=Z:RETURN
100 IFG=1THEN200
105 IFHT=LTHEN120
110 L=L-I:IFL<ITHENL=I
120 GOSUB9100:GOTO2
200 PRINT@256,"GAME OVER";:FORX=1TO10STEP-1:SOUND150-(10*T),1:SOUND200-(10*T),1:NEXT
205 IFSC>HSTHENHS=SC:SOUND200,2:SOUND100,2:PRINT@16,"HIGH:";HS;
210 PRINT@277,"PLAY AGAIN?";
220 I$=INKEY$:IFI$=""THEN220
230 IFI$="Y"THENGOSUB9050:GOTO2
240 IFI$="N"THENCLS:END
250 GOTO220
9000 O=0:I=1:W=2:E=3:F=4:Z=64000:MC=16384:V=MC+32:K=16948:R=32:LV=80:HS=0:N=-1
9005 D=16946:GG=254:HH=251:Q=16952
9010 H(30)=1:H(159)=2:H(239)=3:H(255)=4:H(96)=5:H(143)=6:H(0)=7
9020 K(254)=1:K(251)=1:K(247)=1:K(127)=1:A$=""
9030 L$="                                                                "
9050 L=10:SC=0
9100 FORT=16416TO16864:POKET,R:NEXT:FORX=0TO30:PRINT@X," ";:PRINT@X+480," ";:NEXT
9110 FORX=1TOLV:P=32+RND(416):PRINT@P,CHR$(159);:NEXT
9120 FORY=1TO15:PRINT@(Y*32)-1,"  ";:NEXT
9130 FORT=16864TO16895:POKET,143:NEXT
9140 C=16384+33+RND(19):FORT=1TOL:X(T)=C:A(T)=C-1:C=C+1:D(T)=-1:Y(T)=R:NEXT
9160 J=MC+448+16:M=30:POKEJ,M:HT=0:G=0
9170 PRINT@16,"HIGH:";HS;:PRINT@1,SC;
9190 RETURN
9200 PRINT"  @@@@@@@@ CENTIPEDE @@@@@@@@ "
9202 PRINT"              FOR             "
9203 PRINT"           THE MC-10          "
9205 PRINT"         BY JIM GERRIE        "
9206 PRINT"                              "
9207 PRINT"  10 PTS FOR EACH MUSHROOM    "
9208 PRINT"  (YELLOW, PURPLE, & ORANGE). "
9209 PRINT"  100 FOR CENTIPEDE PIECES.   "
9210 PRINT"  250 PTS FOR FALLING PIECES. "
9211 PRINT"  3 SHOTS TO CLEAR MUSHROOMS. "
9212 PRINT"  TO KEEP YOUR GUN READY TO   "
9213 PRINT"  FIRE, KEEP MOVING! QUICK    "
9214 PRINT"  BACK-AND-FORTH MOVEMENTS    "
9215 PRINT"  WORK BEST USING a & s KEYS. "
9216 PRINT:RETURN
9300 PRINT"  PRESS space TO FIRE & BEGIN ";
9310 I$=INKEY$:X=RND(1000):IFI$=""THEN9310
9320 RETURN
