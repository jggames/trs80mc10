1 CLS:CLEAR400
2 PRINT@7,"* * * QUEST 1 * * *"
3 PRINT@67,"WRITTEN BY BRIAN REYNOLDS":PRINT@133,"MODIFIED BY JIM GERRIE":PRINT:GOTO600
4 PRINT@(TX+S4*TY)*S1,"*";:RETURN
6 FORZZ=1TO1500:NEXT:RETURN
7 FORT=1TOSL*Q:PRINTCHR$(IS);:NEXTT:RETURN
8 PRINT@(X5+(S4*Y5))*S1,YY$;:RETURN
9 PRINT:PRINT"HIT ANY KEY TO CONTINUE.";
10 A$=INKEY$:X=RND(1000):IFA$=""THEN10
11 RETURN
100 DATA "WORTHLESS  ODDS & ENDS",0,0,"A BAG OF   PENNIES    ",1,3,"A BRASS    STATUETTE  ",2,5
101 DATA "A BAG FULL OF COINS   ",3,7,"A BAG OF   GOLD COINS ",5,12,"3 GOLD     NUGGETS    ",8,17
102 DATA "4 SMALL    TURQUOISES ",7,15,"LARGE RUBY            ",15,30
105 DATA "A HUGE     SAPPHIRE   ",150,150,"A HEALING  POTION     ",10,0,"10 MAGIC   ARROWS     ",15,0
106 DATA "10 NORMAL  ARROWS     ",10,0
110 DATA "SKELETON","S",2,"ORC","O",3,"ZOMBIE","Z",4,"GHOUL","g",6,"HUGE SPIDER","H",7
111 DATA "MUMMY","M",8,"GIANT","G",9,"WRAITH","W",9.9
115 DATA 1,12,3,2,18,0,0,0
120 DATA 2,0,0,0,1,4,2,8
125 DATA 1,1,0,4,19,0,0,1
130 DATA 1,0,0,5,3,3,1,1
135 DATA 2,6,38,0,4,1,3,6
140 DATA 1,8,5,9,7,0,0,0
145 DATA 1,0,0,6,0,0,0,1
150 DATA 2,0,6,0,11,2,11,2
155 DATA 2,0,0,10,6,2,3,1
160 DATA 2,0,0,0,9,5,1,4
165 DATA 1,0,0,8,12,0,0,1
170 DATA 2,0,1,11,13,2,5,3
175 DATA 1,0,0,12,14,0,0,1
180 DATA 2,15,26,13,17,5,1,1
185 DATA 2,0,14,0,0,0,0,1
190 DATA 2,0,17,0,0,1,2,5
195 DATA 1,16,20,14,0,4,1,1
200 DATA 2,0,19,1,26,2,2,7
205 DATA 2,18,30,3,27,3,2,2
210 DATA 1,17,21,0,0,0,0,1
215 DATA 1,20,22,0,0,6,2,9
220 DATA 1,21,23,0,0,2,3,12
225 DATA 1,22,24,0,0,4,2,10
240 DATA 1,23,25,34,0,0,0,11
250 DATA 2,24,0,0,0,7,3,9
260 DATA 2,14,0,18,0,3,2,1
270 DATA 2,0,28,19,0,4,1,2
280 DATA 1,27,29,31,0,0,0,1
290 DATA 2,28,0,0,0,5,1,10
300 DATA 2,19,0,0,0,1,2,3
310 DATA 1,0,32,0,28,0,0,4
320 DATA 1,31,33,43,0,0,0,1
330 DATA 2,32,35,0,0,5,1,8
340 DATA 1,0,0,35,24,0,0,12
350 DATA 1,33,36,45,34,0,0,5
360 DATA 1,35,0,37,0,7,1,10
370 DATA 2,0,0,0,36,8,3,9
380 DATA 1,5,49,0,39,0,0,1
390 DATA 1,0,40,38,0,0,0,6
400 DATA 1,39,0,0,41,2,3,2
410 DATA 1,42,46,40,43,4,1,7
420 DATA 2,0,41,0,0,7,3,8
430 DATA 2,0,44,41,32,6,1,11
440 DATA 1,43,45, 0,0,0,0,5
450 DATA 1,44,0,47,35,0,0,1
460 DATA 2,41,47,48,0,5,1,7
470 DATA 1,46,0,50,45,0,0,3
480 DATA 1,0,0,49,46,0,0,1
490 DATA 2,38,51,52,48,6,1,6
500 DATA 1,0,0,51,47,2,5,10
510 DATA 1,49,0,53,50,4,3,5
520 DATA 2,0,0,0,49,6,1,6
530 DATA 2,0,54,0,51,5,1,8
540 DATA 1,53,0,0,55,0,0,1
550 DATA 2,0,0,54,56,2,3,2
560 DATA 1,0,0,55,57,6,1,8
570 DATA 1,0,0,56,58,7,3,11
580 DATA 2,0,0,57,0,8,4,9
600 DIM MN$(8),M$(8),MS$(8),R1(58),R2(58,4),M1(58),M2(58),T1(58),GS(2,63),T$(12),EP(12),GP(12)
601 DIM Y,X,RC,DX,W,WX,WY,X6,Y6,X9,X7,X5,S4,Y5,S1,S2,SP,Y8,Y7,MS,TW,Q,PP,PL,BL$,YY$
603 FORX=1TO12:READT$(X),EP(X),GP(X):NEXT
605 FORX=1TO8:READMN$(X),M$(X),MS(X):NEXT
610 FORX=1TO58:READR1(X):FORY=1TO4:READR2(X,Y):NEXT
615 READM1(X),M2(X),T1(X):NEXT
620 RM=1:A1=1000:A2=1000:W=1:PT=2:BL$=CHR$(143):MC=16384:YY$="@":BB$="           "
630 WL=223:WL$=CHR$(WL):S1=.5:S2=.667:S3=16:S4=64:Q=.5
800 IFB1=1THENGOSUB20000
805 IFB1=1THENB1=0:GOTO900
850 INPUT"LOAD IN AN OLD GAME";A$:IFLEFT$(A$,1)<>"Y"THENGOSUB21000:GOTO900
860 INPUT"HIT enter TO BEGIN LOAD";A$:CLOAD*GS,"QUESTDAT"
870 FORX=1TO58:M1(X)=GS(0,X):M2(X)=GS(1,X):T1(X)=GS(2,X):NEXT
880 ST=GS(0,0):DX=GS(1,0):W=GS(2,0)
881 EP=GS(0,59):RC=GS(1,59):A1=GS(2,59)
882 A2=GS(0,60):PT=GS(1,60):HW=GS(2,60)
883 GP=GS(0,61):NM$=CHR$(GS(1,61)):NM$=NM$+CHR$(GS(2,61))
884 NM$=NM$+CHR$(GS(0,62)):NM$=NM$+CHR$(GS(1,62)):NM$=NM$+CHR$(GS(2,62))
885 NM$=NM$+CHR$(GS(0,63)):NM$=NM$+CHR$(GS(1,63)):NM$=NM$+CHR$(GS(2,63))
890 GOSUB20031:GOSUB9
900 CLS:PRINT"GOLD PIECES (GP): ";GP
901 PRINT"YOU ARE AT THE MARKET."
902 PRINT"THE PRICES HERE ARE:"
903 PRINT"1. MAGIC ARROW      2 GP"
904 PRINT"2. 4 NORMAL ARROWS  1 GP"
905 PRINT"3. HEALING POTION  15 GP"
906 PRINT"4. HOLY WATER       3 GP"
909 PRINT"OK, ";NM$:PRINT"WHAT WOULD YOU LIKE";
910 INPUTIT:IFIT>4ORIT<0THENPRINT"I DON'T SELL THAT.":GOTO910
911 IFIT=0THEN990
912 IFIT=1THENP1=2
913 IFIT=2THENP1=1
914 IFIT=3THENP1=15
915 IFIT=4THENP1=3
916 PRINT"HOW MANY WILL YOU BUY";:INPUTNM
917 IFNM<1THENPRINT"I DON'T BUY THINGS, I SELL THEM.":GOTO916
920 P1=P1*NM
921 OP=P1
925 PRINT"THE PRICE COMES TO";P1;"GP."
930 PRINT"HOW MUCH WILL YOU GIVE ME,":PRINTNM$;:INPUTA
935 IFA<(OP/10)THENPRINT"FORGET IT!":GOTO901
940 IFA<OP*QTHENPRINT"NOT INTERESTED.":GOTO901
941 IFA>=P1THENPRINT"YOU GOT A DEAL!":GOTO950
942 Y=A/P1:X=RND(0):IFX>YTHENPRINT"NOT INTERESTED.":P1=INT((OP+P1)*Q):GOTO930
945 P1=INT((P1*2+A)/3):IFP1>=ATHEN941
946 PRINT"HOW ABOUT";P1;", ";NM$".":GOTO930
950 IFGP<P1THENPRINT"WHAT! CAN'T PAY YER DEBTS?":PRINT"YOU'LL BE THROWN IN PRISON,":PRINTNM$;"!":END
955 GP=GP-P1::PRINT:PRINT"YOU NOW HAVE" ;GP;"GP, ";NM$;"."
960 IFIT=1THENA2=A2+NM
965 IFIT=2THENA1=A1+NM*4
970 IFIT=3THENPT=PT+NM
975 IFIT=4THENHW=HW+NM
980 GOTO901
990 GOSUB22000
991 EL=0:X5=31:Y5=8
992 IFEP>100THENEP=EP-100:EL=EL+100:FORX=1TO58:M2(X)=M2(X)*1.1:NEXT:GOTO992
993 EP=EP+EL
994 IFEL>500THENFORU=ELTO500STEP-100:FORX=1TO58:M2(X)=M2(X)/1.1:NEXT:NEXT
995 INPUT"HIT enter TO GO TO THE DUNGEON. ";A$:CLS1
997 A3=0:A4=0
998 IFA2>ST*2THENA4=A2-ST*2:A2=ST*2:PRINT"MORE THAN";ST*2;"MAGIC ARROWS":PRINT"WOULD WEIGH TOO MUCH.":GOSUB6
999 IFA1>ST*2THENA3=A1-ST*2:A1=ST*2:PRINT"MORE THAN";ST*2;"ARROWS":PRINT"WOULD WEIGH TOO MUCH.":GOSUB6:GOSUB6
1000 CLS1
1001 IFB1=0THENB1=1:GOTO1005
1002 IFRM=1THENINPUT"LEAVE THE DUNGEON";A$:IFLEFT$(A$,1)="Y"THEN800
1003 CLS1
1005 ONR1(RM)GOSUB10000,11000
1010 IFT1(RM)>0THENTX=RND(16)+23:TY=RND(4)+5:GOSUB4
1020 IFI$="W"THENY5=14:GOTO1030
1021 IFI$="Z"THENY5=1:GOTO1030
1022 IFI$="S"THENX5=1:GOTO1030
1023 IFI$="A"THENX5=62
1025 PRINT@(X5+S4*Y5)*S1,YY$;
1030 IFM2(RM)>=1THENWX=RND(39-23)+23:WY=RND(4)+5
1031 MS=MS(M1(RM))/10
1050 IFM2(RM)>=1THENPRINT@(WX+S4*WY)*S1,M$(M1(RM));
1055 PRINT@0,"RM";RM;:PRINT@32,"HL";INT(W*100);"% ";
1060 PRINT@64,"AR";A1;:PRINT@96,"MA";A2;
1061 PRINT@128,"DX";DX;:PRINT@352,"ST";ST;
1062 PRINT@384,"PO";PT;
1063 PRINT@416,"HW";HW;
1065 IFM2(RM)>=1THENPRINT@21,LEFT$(MN$(M1(RM)),11);
1070 IFM2(RM)<1THENPRINT@21,BB$;
1075 IFM2(RM)>1THENPRINT@53,INT(M2(RM));:GOTO1080
1076 PRINT@106*Q,BB$;
1080 PRINT@448,"EX";INT(EP);
1085 PRINT@480,"GP";GP;
1086 IFM2(RM)=0ANDRND(100)=1THEN1088
1087 GOTO1090
1088 FORX=1TO10:PRINT@21,"MONSTER!";
1089 FORY=1TO50:NEXT:PRINT@21,BB$;:FORY=1TO50:NEXT:NEXT:M2(RM)=RND(3):M1(RM)=RND(8):GOTO1030
1090 FORX=1TO(DX*10)-EP:A$=INKEY$:IFA$=""THENNEXT:GOTO1093
1091 X=3550:NEXT
1093 IFT1(RM)>0THENPRINT@(TX+S4*TY)*S1,"*";
1095 IFA$<>""THENI$=A$
1100 IFI$="W"THENGOSUB15100
1105 IFI$="Z"THENGOSUB15200
1110 IFI$="S"THENGOSUB15300
1115 IFI$="A"THENGOSUB15400
1120 IFI$="H"THENI$="":IFPT>0THENPT=PT-1:W=1
1125 IFI$="M"ANDA2>0THENI$="":A2=A2-1:GOSUB15500
1130 IFI$="N"ANDA1>0THENI$="":A1=A1-1:GOSUB15600
1135 IFI$="F"THENGOSUB16000
1140 IFI$="O"THENGOSUB17000
1145 IFI$="T"ANDHW>0THENI$="":HW=HW-1:GOSUB18000
1200 IFM2(RM)<1THEN1030
1201 IFMS<=0THENFORX=128TO143:POKEMC+(WX+(S4*WY))*S1,X:NEXT:M2(RM)=M2(RM)-1:EP=EP+MS(M1(RM)):GOTO1030
1205 IFWX>X5THENMX=-1:GOTO1210
1206 IFWX<X5THENMX=1:GOTO1210
1207 MX=0
1210 IFWY>Y5THENMY=-1:GOTO1215
1211 IFWY<Y5THENMY=1:GOTO1215
1212 MY=0
1215 PRINT@(WX+S4*WY)*S1,BL$;
1220 IFPEEK(MC+(WX+MX+S4*WY)*S1)=143THENWX=WX+MX
1225 IFPEEK(MC+(S4*(WY+MY)+WX)*S1)=143THENWY=WY+MY
1230 IF(ABS(INT(WX*Q)-INT(X5*Q))>1)OR(ABS(WY-Y5)>1)THEN1050
1235 X=RND(0):IFX>MSTHEN1050
1240 X=RND(0)*MS:W=W-X:IFW<0THENPRINT@32,"HL 0 %  ";:GOTO5000
1250 GOTO1050
5000 FORX=128TO143:POKEMC+(X5+S4*Y5)*S1,X:NEXT:FORX=1TO1000:NEXT:CLS1
5005 PRINT"WELCOME TO HEAVEN, ";NM$;"!"
5010 PRINT"I HOPE YOU ENJOYED YOUR SHORT"
5011 PRINT"LIFE AS AN ADVENTURER IN WHICH"
5015 PRINT"YOU ACCUMULATED";GP;"GP'S"
5016 PRINT"AND";EP;"EXPERIENCE POINTS,"
5020 PRINT"WOULD YOU LIKE TO BE"
5021 INPUT"REINCARNATED";A$:IFLEFT$(A$,1)="N"THENEND
5030 RUN
10000 X1=R2(RM,1)
10010 IFX1>0THENFORX=0TO320STEP64:PRINT@(X+23)*S1,WL$;:PRINT@(X+40)*S1,WL$;:NEXT:GOTO10015
10011 PRINT@171,"";:SL=20:IS=WL:GOSUB7
10015 X1=R2(RM,2)
10020 IFX1>0THENFORX=640TO960STEP64:PRINT@(X+23)*S1,WL$;:SL=16:IS=143:GOSUB7:PRINTWL$;:NEXT:GOTO10025
10021 PRINT@331,"";:SL=20:IS=WL:GOSUB7
10025 X1=R2(RM,3)
10030 IFX1>0THENFORX=41TO63:PRINT@(320+X)*S1,WL$;:PRINT@(640+X)*S1,WL$;:NEXT:GOTO10035
10031 FORX=384TO576STEP64:PRINT@(X+40)*S1,WL$;:NEXT
10035 X1=R2(RM,4)
10040 IFX1>0THENFORX=0TO22:PRINT@(320+X)*S1,WL$;:PRINT@(640+X)*S1,WL$;:NEXT:GOTO10045
10041 FORX=384TO576STEP64:PRINT@(X+23)*S1,WL$;:NEXT
10045 RETURN
11000 PRINT@104,WL$;:SL=7:IS=WL:GOSUB7:SL=16:IS=143:GOSUB7:SL=7:IS=WL:GOSUB7:PRINTWL$;
11010 PRINT@392,WL$;:SL=7:IS=WL:GOSUB7:SL=16:IS=143:GOSUB7:SL=7:IS=WL:GOSUB7:PRINTWL$; 
11011 FORX=192TO320STEP64:PRINT@(X+16)*S1,WL$;:PRINT@(X+47)*S1,WL$;:NEXT
11012 FORX=640TO768STEP64:PRINT@(X+16)*S1,WL$;:PRINT@(X+47)*S1,WL$;:NEXT
11015 X1=R2(RM,1)
11020 IFX1>0THENFORX=0TO128STEP64:PRINT@(X+23)*S1,WL$;:SL=16:IS=143:GOSUB7:PRINTWL$;:NEXT:GOTO11025
11021 PRINT@108,"";:SL=16:IS=WL:GOSUB7
11025 X1=R2(RM,2)
11030 IFX1>0THENFORX=832TO960STEP64:PRINT@(X+23)*S1,WL$;:SL=16:IS=143:GOSUB7:PRINTWL$;:NEXT:GOTO11035
11031 PRINT@396,"";:SL=16:IS=WL:GOSUB7
11035 X1=R2(RM,3)
11040 IFX1>0THENPRINT@184,"";:SL=16:IS=WL:GOSUB7:PRINT@344,"";:SL=16:IS=WL:GOSUB7:GOTO11045
11041 FORX=384TO576STEP64:PRINT@(X+47)*S1,WL$;:NEXT
11045 X1=R2(RM,4)
11050 IFX1>0THENPRINT@160,"";:SL=16:IS=WL:GOSUB7:PRINT@320,"";:SL=16:IS=WL:GOSUB7:GOTO11055
11051 FORX=384TO576STEP64:PRINT@(X+16)*S1,WL$;:NEXT
11055 RETURN
15100 IFY5=0THEN15105
15101 M=PEEK(MC+(X5+((Y5-1)*S4))*S1):IFM=143THEN15105
15102 RETURN
15105 PRINT@(X5+S4*Y5)*S1,BL$;
15110 Y5=Y5-1:IFY5<1THENRM=R2(RM,1):GOTO1000
15120 PRINT@(X5+S4*Y5)*S1,YY$;:RETURN
15200 IFY5=15THEN15205
15201 M=PEEK(MC+(X5+((Y5+1)*S4))*S1):IFM<>143THENRETURN
15205 PRINT@(X5+S4*Y5)*S1,BL$;
15210 Y5=Y5+1:IFY5>14THENRM=R2(RM,2):GOTO1000
15220 PRINT@(X5+S4*Y5)*S1,YY$;:RETURN
15300 IFX5>61THEN15305
15302 M=PEEK(MC+(X5+2+S4*Y5)*S1):IFM<>143THENRETURN
15305 PRINT@(X5+S4*Y5)*S1,BL$;
15310 X5=X5+2:IFX5>61THENRM=R2(RM,3):GOTO1000
15320 PRINT@(X5+S4*Y5)*S1,YY$;:RETURN
15400 IFX5<2THEN15405
15402 M=PEEK(MC+(X5-2+S4*Y5)*S1):IFM<>143THENRETURN
15405 PRINT@(X5+S4*Y5)*S1,BL$;
15410 X5=X5-2:IFX5<2THENRM=R2(RM,4):GOTO1000
15420 PRINT@(X5+S4*Y5)*S1,YY$;:RETURN
15500 GOSUB15699
15505 X=RND(0)*S1:IFRC=1THENX=X-.1
1550B IFRC=2THENX=X+.1
15507 X=X-(EP/1000)
15510 X=X-.2
15511 X=X-(DX/100)
15515 IFX>WTHENRETURN
15520 X=RND(0):IFRC=1THENX=X+.2:GOTO15522
15521 X=X+.1
15522 IFRC=2THENX=X-.1
15525 MS=MS-X:RETURN
15600 GOSUB15699
15601 IFM1(RM)=8THENRETURN
15605 X=RND(0)*S1:IFRC=1THENX=X-.1
15606 X=X-(DX/100)
15607 IFRC=2THENX=X+.1
15608 X=X-(EP/1000)
15610 IFX>WTHENRETURN
15620 X=RND(0):IFRC=1THENX=X+.1
15621 IFRC=2THENX=X-.1
15625 MS=MS-X:RETURN
15699 IFWX=0THENWX=31:IFWY=0THENWY=8
15700 X6=X5*2:Y6=Y5*3:X7=WX*2:Y7=WY*3
15701 IFX6=X7THENX6=X6-1:X7=X7+1
15702 SP=(Y6-Y7)/(X6-X7):X8=X6:X9=X7
15708 Y8=Y6:Y9=Y7:Y=Y8
15709 SP=SP*SGN(Y8-Y9):IFY6<Y7THENSP=-SP
15710 IFX6>X7THENSP=-SP
15711 PL=(X5+(S4*Y5))*S1:FORX=X8TOX9-1STEPSGN(X9-X8):IFY>47ORY<0ORX>127ORX<0THENX=X9-1:NEXT:GOTO15750
15715 PP=POINT(X*S1,Y*S2):IFPP=-1THENPRINT@PL,BL$;:PP=1
15720 RESET(X*S1,Y*S2):SET(X*S1,Y*S2,PP):PRINT@PL,YY$;:Y=Y+SP:NEXT
15750 GOSUB8:GOSUB4:RETURN
16000 IFABS(INT(X5*Q)-INT(WX*Q))>1ORABS(Y5-WY)>1THENRETURN
16001 IFM1(RM)=8THENRETURN
16002 IFM1(RM)=7THENRETURN
16003 IFM1(RM)=6THENW=W-.05
16005 X=RND(0):IFRC=OTHENX=X-.1
16006 X=X-(DX/100)
16007 IFRC=2THENX=X-.3
16008 X=X-(EP/1000)
16010 IFX>WTHENRETURN
16015 X=RND(0):IFRC=0THENX=X+.1
16016 X=X+(ST/100)
16017 IFRC=2THENX=X+.2
16020 MS=MS-X:RETURN
17000 IFABS(INT(X5*Q)-INT(TX*Q))>1ORABS(Y5-TY)>1THENRETURN
17010 PRINT@(TX+S4*TY)*S1,BL$;
17011 TX=0:TY=0
17015 PRINT@437,LEFT$(T$(T1(RM)),11);:PRINT@437+32,MID$(T$(T1(RM)),12,11);
17017 GOSUB6:PRINT@437,BB$;
17020 PRINT@437+32,BB$;
17021 IFT1(RM)=10THENPT=PT+1:GOTO17026
17022 IFT1(RM)=11THENA2=A2+10:GOTO17026
17023 IFT1(RM)=12THENA1=A1+10:GOTO17026
17024 TS(T1(RM))=TS(T1(RM))+1
17025 GP=GP+GP(T1(RM))
17026 EP=EP+EP(T1(RM)):T1(RM)=0
17030 RETURN
18000 M=M1(RM):IFM=2ORM=5ORM=7THENRETURN
18005 GOSUB15699:PRINT@(WX+(S4*WY))*S1,BL$;:WA=WX:WB=WY:WX=X5:WY=Y5:GOSUB16000:WX=WA:WY=WB
18010 GOSUB8:RETURN
20000 INPUT"SEE THE TREASURES YOU RETRIEVED ";A$
20001 IFLEFT$(A$,1)="Y"THENFORX=1TO9:PRINTT$(X);TS(X):NEXT
20010 FORX=1TO9:TS(X)=0:NEXT
20011 A1=A1+A3:A2=A2+A4
20015 INPUT"SAVE THIS GAME";A$:IFLEFT$(A$,1)<>"Y"THEN20029
20016 PRINT"PLEASE WAIT...":FORX=1TO58:GS(0,X)=M1(X):GS(1,X)=M2(X):GS(2,X)=T1(X):NEXT
20020 GS(0,0)=ST:GS(1,0)=DX:GS(2,0)=W
20021 GS(0,59)=EP:GS(1,59)=DX:GS(2,59)=A1
20022 GS(0,60)=A2:GS(1,60)=PT:GS(2,60)=HW
20023 GS(0,61)=GP:GS(1,61)=ASC(MID$(NM$+BB$,1,1)):GS(2,61)=ASC(MID$(NM$+BB$,2,1))
20024 GS(0,62)=ASC(MID$(NM$+BB$,3,1)):GS(1,62)=ASC(MID$(NM$+BB$,4,1)):GS(2,62)=ASC(MID$(NM$+BB$,5,1))
20025 GS(0,63)=ASC(MID$(NM$+BB$,6,1)):GS(1,63)=ASC(MID$(NM$+BB$,7,1)):GS(2,63)=ASC(MID$(NM$+BB$,8,1))
20026 INPUT"HIT enter TO BEGIN SAVE";A$
20027 CSAVE*GS,"QUESTDAT"
20028 PRINT"SAVE COMPLETE."
20029 INPUT"WOULD YOU LIKE TO STOP NOW";A$:IFLEFT$(A$,1)<>"Y"THENRETURN
20030 GOTO20065
20031 CLS:PRINT"OK, HERE'S YOUR CHARACTER INFO:"
20035 PRINT"NAME: ";NM$,"RACE: ";:IFRC=0THENPRINT"HUMAN"
20036 IFRC=1THENPRINT"ELF":GOTO20040
20037 PRINT"DWARF"
20040 PRINT"HEALTH:";INT(W*100);"%"
20045 PRINT"HEALING POTIONS:";PT:PRINT"HOLY WATER:";HW
20050 PRINT"ARROWS:";A1:PRINT"MAGIC ARROWS:";A2
20055 PRINT"GOLD:";GP:PRINT"EXPERIENCE:";EP
20060 PRINT"STRENGTH:";ST:PRINT"DEXTERITY:";DX:RETURN
20065 GOSUB20031:INPUT"TRY AGAIN AS A *NEW* CHARACTER";A$:IFLEFT$(A$,1)="Y"THENRUN
20099 PRINT:PRINT"COME QUESTING AGAIN SOMETIME!":END
21000 PRINT:PRINT"OK, I'LL MAKE YOU A CHARACTER."
21005 GP=RND(20)+5:ST=RND(17)+3:DX=RND(17)+3:RC=RND(3)-1:A1=3:A2=RND(10):PT=RND(3)+1:HW=RND(5):EP=0:W=1
21010 PRINT"STRENGTH:";ST,"DEXTERITY:";DX
21015 PRINT"GOLD:";GP,"HEALING P'S:";PT
21020 PRINT"HOLY WATER:";HW,"RACE: ";:IFRC=1THENPRINT"ELF":GOTO21025
21021 IFRC=2THENPRINT"DWARF":GOTO21025
21022 PRINT"HUMAN"
21025 PRINT"ARROWS:";A1,"M.ARROWS:";A2
21030 INPUT"CHARACTER'S NAME";NM$:PRINT"HAVE A FUN QUEST, ";NM$;"!":GOSUB6:CLS1
21040 RETURN
22000 CLS:PRINT"N: SHOOT A NORMAL ARROW"
22010 PRINT"M: SHOOT MAGIC ARROW"
22020 PRINT"T: TOSS VIAL OF HOLY WATER"
22030 PRINT"F: FIGHT IN CLOSE COMBAT"
22040 PRINT"O: OPEN A TREASURE CHEST"
22050 PRINT"H: DRINK A HEALING POTION":PRINT:RETURN
