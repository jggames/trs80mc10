0 CLS:CLEAR5000:GOSUB4000:GOSUB4100:GOTO1000
1 RETURN
2 P=P-Q:H=H-1:RETURN
3 P=P+Q:H=H+1:RETURN
4 U=P:X=H:S=1:O=3:BB=109:N=1:I=0:RETURN
5 FORY=PTOESTEPR:ONK(PEEK(Y))GOTO6:NEXT:RETURN
6 POKEY,96:U(M)=Y-R(RND(3)):M(M)=1:Y=E:NEXT:RETURN
7 U=P:X=H:S=1:O=3:BB=106:N=33:I=1:RETURN
8 U(M)=U(M)-R:ONH(PEEK(U(M)))GOTO10,11,11:POKEU(M),J:RETURN
9 FORS=STOS+12:U=U+N:L=PEEK(U):POKEU,BB:X=X+I:O=O+1:ONH(L)GOTO1,51,52,50,49:POKEU,96:NEXT:RETURN
10 M(M)=2:RETURN
11 POKEP,42:SOUND1,1:LV=LV-1:S$=STR$(LV):PRINT@29,S$;:FORT=MC+29TOMC+28+LEN(S$):POKET,PEEK(T)-64:NEXT
12 IFLV=0THENZZ=5:Z=100:FU=-1
13 GOTO10
15 FU=FU+1
16 S$=STR$(FU):PRINT@25,S$;:FORT=MC+25TOMC+24+LEN(S$):POKET,PEEK(T)-64:NEXT:RETURN
20 FORZZ=1TO3:FORZ=1TO100:ONK(PEEK(K)ANDPEEK(2))GOSUB2,3,4,7
21 FORT=0TO9:A$(T)=MID$(A$(T)+B$(C(Z,T)),2):NEXT:PRINT@F,A$A$(0)A$(1)A$(2)A$(3)A$(4)A$(5)A$(6)A$(7)A$(8)A$(9);
22 ONH(PEEK(P))GOSUB3,39,39,39,15:POKEP,D:FORM=1TO3:ONM(M)GOSUB8,5:NEXT:ONSGOSUB9,9,9,9,9,9,9,9,9,9,9,9,9,9:NEXT
23 IFFU=0THEN:S$="RETURN TO BASE":PRINT@50,S$;:FORT=MC+50TOMC+49+LEN(S$):POKET,PEEK(T)-64:NEXT:ZZ=4:SOUND100,20
24 GOSUB61:FU=0:GOSUB16:NEXT:IFZZ=4THENLV=LV+1:GOTO1080
29 POKEP,62:S$="PLAY AGAIN (Y/N)?":PRINT@32,S$;:FORT=MC+32TOMC+31+LEN(S$):POKET,PEEK(T)-64:NEXT
30 I$=INKEY$:R=RND(1000):IFI$=""THEN30
31 IFI$="Y"THEN1060
32 IFI$="N"THENEND
33 GOTO30
39 POKEP,62:S$="CRASHED!":PRINT@52,S$;:FORT=MC+52TOMC+51+LEN(S$):POKET,PEEK(T)-64:NEXT:FU=-1
40 ZZ=6:Z=100:SOUND1,10:SOUND10,10:RETURN
49 FU=FU+1:GOSUB16
50 POKEU,42:L$=A$(X):VP=VARPTR(L$):POKE(PEEK(VP+2)*256)+PEEK(VP+3)+O,32:A$(X)=L$:SOUND100,1:POKEU,96:S=100:NEXT:GOSUB62:RETURN
51 POKEU,128:S=100:RETURN
52 POKEU,126:S=100:RETURN
60 S=100:NEXT:RETURN
61 SC=SC+10*FU
62 SC=SC+10:S$=STR$(SC):PRINT@0,S$;:FORT=MCTOMC-1+LEN(S$):POKET,PEEK(T)-64:NEXT:RETURN
80 C(C,T)=1:ON-(H=T)GOSUB82:RETURN
81 C(C,T)=2:RETURN
82 ONRND(4)GOSUB83,83,83,84:RETURN
83 C(C,T)=3:S(C)=MC+128+(32*T)+96:RETURN
84 C(C,T)=4:RETURN
90 FORT=0TO9:ON1-(H<T)GOSUB80,81:NEXT:RETURN
1000 CLS:DIMS(100),L(100),R(3),J(12),VP,L$,LV,FU,S$,BB,MN
1015 B$(1)=" ":B$(2)=CHR$(128):B$(3)="A":B$(4)="Q":Q=32:K=17023:B=65:D=126:F=64:E=16832:J=94:MC=16384:HS=0
1020 FORT=1TO96:A$=A$+" ":NEXT:FORT=1TO100STEP4:L(T)=1:NEXT
1030 K(87)=1:K(90)=2:K(32)=3:K(65)=1:K(66)=4:R(1)=0:R(2)=32:R(3)=-32
1040 H(32)=1:H(128)=2:H(126)=3:H(65)=4:H(81)=5:FORT=1TO6:J(T)=6:NEXT:FORT=7TO12:J(T)=T-(T-6):NEXT
1060 IFSC>HSTHENHS=SC
1070 SC=0:LV=3:FU=0:MN=0
1080 A$(0)="":FORC=1TO32:A$(0)=A$(0)+" ":NEXT:FORT=1TO9:A$(T)=A$(0):NEXT:GOSUB2000:GOSUB3000:GOSUB3100
1085 S$=STR$(HS):PRINT@10,S$;:FORT=MC+10TOMC+9+LEN(S$):POKET,PEEK(T)-64:NEXT
1086 S$=STR$(LV):PRINT@29,S$;:FORT=MC+29TOMC+28+LEN(S$):POKET,PEEK(T)-64:NEXT
1087 S$=STR$(SC):PRINT@0,S$;:FORT=MCTOMC-1+LEN(S$):POKET,PEEK(T)-64:NEXT:GOSUB16
1090 S=0:H=3:FORT=1TO4:M(T)=2:NEXT:GOTO20
2000 CLS:P=MC+256+3:C=1:MN=MN+1:PRINT@256,"GET READY FOR MISSION";MN
2010 H=5+RND(4):S=(6-RND(12))/2:R=RND(25):FORG=1TOR:IFG=(R/2)THEN2030
2020 H=H+S:IFH>=0ANDH<9THENGOSUB90:GOTO2040
2030 S=(S*-1)/RND(2):H=H+S:IFH>=0ANDH<9THENGOSUB90:GOTO2040
2035 H=5+RND(4):GOSUB90
2040 C=C+1:IFC>100THENG=R:NEXT:GOTO2070
2050 NEXT:GOTO2010
2070 R=33:RETURN
3000 FORT=16384TO16447:POKET,32:NEXT
3090 RETURN
3100 FORT=16864TO16895:POKET,128:NEXT
3190 RETURN
4000 CLS
4010 PRINT"   mc-scramble by jim gerrie"
4030 PRINT"FLY YOUR AIRCRAFT '>' OVER THE"
4035 PRINT"MOUNTAINOUS TERRAIN SHOOTING"
4040 PRINT"AT ANTI-AIRCRAFT EMPLACEMENTS"
4045 PRINT"'A' THAT WILL SHOOT BACK AT YOU"
4050 PRINT"UNLESS YOU DESTROY THEM WITH"
4055 PRINT"YOUR GUNS 'SPACE BAR.'  USE"
4060 PRINT"YOUR BOMBS 'B' TO DESTROY THE"
4065 PRINT"FUEL EMPLACEMENTS 'Q' OF THE"
4070 PRINT"ENEMY. YOU MUST DESTROY ENOUGH"
4075 PRINT"FUEL EMPLACEMENTS OR YOU WILL"
4080 PRINT"BE RE-CALLED FROM YOUR MISSION."
4085 PRINT"SUCCESSFULLY DESTROY ENOUGH,"
4090 PRINT"AND YOU WILL BE ASSIGNED NEW"
4095 PRINT"MISSIONS.  GOOD LUCK!"
4098 DIMA$(9),B$(4),C(100,9),K(128),H(128),M(4),U(4),A$,A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,MC
4099 RETURN
4100 PRINT@480,"     press any key to begin";
4200 I$=INKEY$:R=RND(1000):IFI$=""THEN4200 
4210 IFI$=""THEN4210
4220 RETURN
