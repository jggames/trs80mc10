100 CLEAR100,20458
101 DATA 189,239,79,60,50,51,254,66,22,168,18,232,19,237,18,126,236,227
102 FOR X=20458TO20475:READY:POKEX,Y:NEXTX
103 POKE 16917,126
104 POKE 16918,79
105 POKE 16919,234
200 CLS:GOSUB1100:?
201 ?"TAKE TURNS AGAINST THE COMPUTER."
202 ?"YOU MAY TAKE ANY NUMBER OF COINS";
203 ?"FROM ANY ONE PILE ON YOUR TURN.":?
204 ?"YOU WIN IF YOU CAN LEAVE THE"
205 ?"COMPUTER WITH NOTHING."
206 ?:?"THE COMPUTER WINS"
207 ?"IF IT LEAVES YOU WITH NOTHING."
208 ?:?"PRESS ANY KEY TO CONTINUE"
209 IFINKEY$=""THEN209
300 N=9
301 FORI=1TO9:CO(I)=RND(1000):NEXTI
302 CLS:GOSUB 1000
303 ?@352,"WOULD YOU LIKE TO GO FIRST?"
304 GOSUB2100
305 IF A$<>"Y"THEN500
400 ?@352,"ENTER PILE # 1-9: ";CHR$(128):GOSUB2100
401 IF C<49 OR C>58 THEN ?@384,"ENTER A NUMBER FROM 1-9":GOTO400
402 P=C-48:IF CO(P)=0 THEN ?@384,"PILE #";A$;" HAS NO COINS.":GOTO400
403 MX$=STR$(CO(P)):MX$=MID$(MX$,2)
410 ?@370,A$:?@384,"ENTER # OF COINS 1-";MX$;": ":NC=0:PL=405+LEN(MX$):PR=PL
411 ?@PL,CHR$(128):GOSUB2100:IFC=21THENPL=PR:NC=0:GOTO411
412 IFC=8ANDPL>PRTHENPL=PL-1:NC=INT(NC/10):?@416,"":GOTO411
413 IFNC>0ANDC=13THEN420
414 IF(NC=0ANDC<49)ORC>58ORC<48THEN411
415 ?@PL,A$:PL=PL+1:NC=NC*10+C-48:IFNC<=CO(P)THEN?@416,"":GOTO411
416 ?@416,NC;"IS TOO BIG (MAX =";STR$(CO(P));")":PL=PL-1:NC=INT(NC/10):GOTO411
420 ?@352,"YOU TOOK";NC;"FROM PILE #";CHR$(P+48)
421 CO(P)=CO(P)-NC
422 GOSUB 1000
423 GOSUB 600
424 IFP<10THEN500
430 ?@384,"LEAVING ME WITH NOTHING!"
431 ?@68,"I CAN'T BELIEVE YOU WON,"
432 ?@103,"YOU FOOLISH HUMAN!"
433 GOTO 700
500 ?@384,"HMM..."
501 Z=USR(USR(RND(1000))):FORP=1TO9:Z=USR(CO(P)):NEXT:CD=RND(10):P=1:IFZ=0THEN505
502 IF CO(P)<USR(CO(P))THENZ=USR(CO(P)):P=P+1+9*(P=9):GOTO502
503 CD=CD-1:IFCD>0THENZ=USR(CO(P)):P=P+1+9*(P=9):GOTO502
504 Z=USR(0):D=CO(P)-Z:GOTO510
505 IFCO(P)=0THENP=P+1+9*(P=9):GOTO505
506 CD=CD-1:IFCD>0THENP=P+1+9*(P=9):GOTO505
507 D=RND(CO(P))
510 ?@384,"I'LL TAKE";D;"COINS FROM PILE";P:SOUND255,50
511 CO(P)=CO(P)-D:GOSUB1000
512 ?@384,"I TOOK";D;"COINS FROM PILE";P
513 GOSUB 600
514 IFP<10THEN400
520 ?@416,"LEAVING YOU WITH NOTHING!"
521 ?@68,"HA HA HA! YOU HAVE LOST!"
522 ?@100,"YOU CAN'T EVER DEFEAT ME!"
523 GOTO700
600 FORP=1TO9:IFCO(P)=0THENNEXTP
601 RETURN
700 ?@480,"WOULD YOU LIKE TO PLAY AGAIN?";
701 FORI=16448TO16511:POKEI,PEEK(I)-64:NEXTI
702 FORI=16864TO16895:POKEI,PEEK(I)-64:NEXTI
703 GOSUB2100
704 IFA$="Y"THEN300
999 END
1000 GOSUB1100
1010 FORI=1TO3:FORJ=1TO3
1020 MX$=STR$(CO(3*I+J-3)):MX$=MX$+MID$(" ",LEN(MX$))
1030 ?@88+64*J+10*I,"#";CHR$(48+3*I+J-3);MX$;
1040 NEXTJ,I:RETURN
1100 ?@3,"CAN YOU TAKE THE LAST COIN?":RETURN
2100 A$=INKEY$:IFA$=""THEN2100
2110 C=ASC(A$):RETURN
