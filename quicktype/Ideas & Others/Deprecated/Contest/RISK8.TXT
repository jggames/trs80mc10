0 GOSUB850:DIMTR$(42),NW(42),N(42),NE(42),E(42),SE(42),W(42),SW(42),S(42),XY(42),CO(42),CU(42),C(3),O(42),LM(150),PP(2),V
1 DIMI$,A$,RE(2),TT(2),NC(8),OW(42,2),CC(2,6),AR(3),DR(2),TC(2),BC(2),PL,L,ZZ,PC,F,FL,AC,NA,TE,T,X,Y,P,LR,R,BR,GG,KF(2),VN
2 DIM N1,N2,N3,N4:GOSUB900:GOSUB890:GOTO100
3 FORT=N1 TOX:POKELM(T),C(PL):NEXT:FORT=N1TON4:IFOW(T,N1)=N3 THENGOSUB5
4 NEXT:RETURN
5 A$=RIGHT$(STR$(O(T)),N2):IFMID$(A$,N1,N1)=" "THENA$="0"+MID$(A$,N2,N1)
6 PRINT@XY(T),A$;:IFOW(T,N1)=N2OR(PL=N1AND OW(T,N1)=N3)THENPOKEXY(T)+MC,ASC(MID$(A$,N1,N1)):POKEXY(T)+MC+N1,ASC(MID$(A$,N2,N1))
7 RETURN
8 PRINT@480+16,"               ";:PRINT@480+16,TR$(LR);:RETURN
9 PRINT@480,"PLAYER";PL;RE(PL);CHR$(8);")    ";:PRINT@489,"(";:RETURN
18 PRINT@480,"                               ";
50 NC(1)=NW(LR):NC(2)=N(LR):NC(3)=NE(LR):NC(4)=E(LR):NC(5)=SE(LR):NC(6)=W(LR):NC(7)=SW(LR):NC(8)=S(LR)
55 PC=0:FORT=1TO8:IFNC(T)=AC THENPC=2
60 NEXT:RETURN
70 IFOW(IC,1)=1THENTT(1)=TT(1)-1
71 IFOW(IC,1)=2THENTT(2)=TT(2)-1
72 OW(IC,1)=PL:O(IC)=NA:OW(IC,2)=NA:T=IC:GOSUB5:O(AC)=O(AC)-NA:OW(AC,2)=1:T=AC:GOSUB5
73 GOSUB8:PRINT@480,"# TO REINFORCE?";
74 I$=INKEY$:IFI$=""THEN74
75 IFI$=CHR$(13)THEN80
76 IFI$=","THENIF O(IC)>OW(IC,2) THEN O(IC)=O(IC)-1:T=IC:GOSUB5:O(AC)=O(AC)+1:T=AC:GOSUB5
78 IFI$="."OR I$=" "THENIF O(AC)>OW(AC,2) THEN O(AC)=O(AC)-1:T=AC:GOSUB5:O(IC)=O(IC)+1:T=IC:GOSUB5
79 GOTO74
80 RETURN
100 CLS:PRINT@480,"DRAWING MAP";:PRINT@0,"";
110 FORT=MC TO VN:READA:POKET,C(A):IFA=1THEN X=X+1:LM(X)=T 
115 NEXT:C(2)=32:PRINT@480,"CHOOSING TERRITORIES";
120 FORT=1TO42:READ TR$(T),NW(T),N(T),NE(T),E(T),SE(T),W(T),SW(T),S(T),XY(T),CO(T),CU(T):NEXT
125 FORT=1TO9
130 R=RND(42):IFO(R)<>0THEN130
132 O(R)=1:OW(R,1)=1:PP(1)=R:NEXT
134 FORT=1TO9 
136 R=RND(42):IFO(R)<>0THEN136
138 O(R)=1:OW(R,1)=2:PP(2)=R:NEXT
140 FORT=1TO42:IFOW(T,1)=0THEN O(T)=4:OW(T,1)=3
142 NEXT:PRINT@480,"DEPLOYING ARMIES        ";
145 FORT=1TO42:GOSUB5:OW(T,2)=O(T):NEXT:TC(1)=9:TC(2)=9:TT(1)=9:TT(2)=9:GOTO305
149 REM ** BATTLE **
150 FORT=1TO3:AR(T)=0:NEXT:FORT=1TONA:AR(T)=RND(6):NEXT
152 DR(1)=0:DR(2)=0:DR(1)=RND(6):IF O(IC)>1 THEN DR(2)=RND(6)
156 IF DR(1)<DR(2)THEN TE=DR(1):DR(1)=DR(2):DR(2)=TE
158 FOR T=1 TO NA-1:FOR J=1 TO NA-T:IF AR(J)>=AR(J+1) THEN165
160 TE=AR(J):AR(J)=AR(J+1):AR(J+1)=TE 
165 NEXTJ:NEXTT
170 GOSUB18:PRINT@480,AR(1);DR(1);
172 IF AR(1)>DR(1) THEN O(IC)=O(IC)-1:T=IC:GOSUB5:GOTO176
174 O(AC)=O(AC)-1:T=AC:GOSUB5:NA=NA-1
176 IF AR(2)=0 OR DR(2)=0 THEN180
177 PRINT@496,AR(2);DR(2);
178 IF AR(2)>DR(2) THEN O(IC)=O(IC)-1:T=IC:GOSUB5:GOTO180
179 O(AC)=O(AC)-1:T=AC:GOSUB5:NA=NA-1
180 FORT=1TO800:NEXTT
182 IF O(IC)=0THENGOSUB70:KF(PL)=KF(PL)+1:TC(PL)=TC(PL)+1:TT(PL)=TT(PL)+1
195 LR=AC:RETURN
199 REM ** REINFORCE **
200 GOSUB9:GOSUB290
220 IFPC=2ORPC=4THENIF OW(LR,1)=PL AND RE(PL)>0 THEN RE(PL)=RE(PL)-1:O(LR)=O(LR)+1:T=LR:GOSUB5:GOTO200
225 IFPC=3THENIF OW(LR,1)=PL AND O(LR)>OW(LR,2) THEN RE(PL)=RE(PL)+1:O(LR)=O(LR)-1:T=LR:GOSUB5:GOTO200
230 IFRE(PL)>0THEN200
240 IFPC<>1THEN200
250 RETURN
259 REM ** NAV MAP ** F=1:FL=PL FOR RE-DEPLOY **
260 I$=INKEY$:IFZZ<20THENPOKEMC+L,P:ZZ=ZZ+1:GOTO262
261 PRINT@L,CHR$(CU(LR));:ZZ=ZZ+1:IFZZ=40THENZZ=0
262 IFI$=""THEN260
263 IFI$=" "ORI$="1"ORI$="2"ORI$="3"THENPC=2:V=VAL(I$):GOTO299
265 IFI$="Q"THENIFNW(LR)>0AND OW(NW(LR),F)=FL THENPOKEMC+L,P:LR=NW(LR):GOTO290
266 IFI$="W"THENIF N(LR)>0AND OW(N(LR),F)=FL THENPOKEMC+L,P:LR=N(LR):GOTO290
267 IFI$="E"THENIFNE(LR)>0AND OW(NE(LR),F)=FL THENPOKEMC+L,P:LR=NE(LR):GOTO290
268 IFI$="A"THENIF W(LR)>0AND OW(W(LR),F)=FL THENPOKEMC+L,P:LR=W(LR):GOTO290
269 IFI$="D"THENIF E(LR)>0AND OW(E(LR),F)=FL THENPOKEMC+L,P:LR=E(LR):GOTO290
270 IFI$="Z"THENIFSW(LR)>0AND OW(SW(LR),F)=FL THENPOKEMC+L,P:LR=SW(LR):GOTO290
271 IFI$="X"THENIF S(LR)>0AND OW(S(LR),F)=FL THENPOKEMC+L,P:LR=S(LR):GOTO290
272 IFI$="C"THENIFSE(LR)>0AND OW(SE(LR),F)=FL THENPOKEMC+L,P:LR=SE(LR):GOTO290
273 IFI$="S"THENIF E(LR)>0AND OW(E(LR),F)=FL THENPOKEMC+L,P:LR=E(LR):GOTO290
275 IFI$=CHR$(13)THEN:PC=1:GOTO299
276 IFI$=","THENPC=3:GOTO299
277 IFI$="."ORI$=" "THENPC=4:GOTO299
285 GOTO260
290 L=XY(LR):P=PEEK(MC+L):PRINT@L,CHR$(CU(LR));:GOSUB8
295 GOTO260
299 POKEMC+L,P:RETURN
300 REM ** MAIN LOOP **
305 FOR J=1 TO 9:RE(1)=3:RE(2)=3
310 FOR PL=1 TO 2:GOSUB3:LR=PP(PL):F=0:GOSUB200:OW(LR,2)=O(LR):PP(PL)=LR:NEXTPL
325 NEXT J:GG=0:PRINT@480,"GET READY TO BATTLE...         ";:BP=RND(2)
330 IFGG>0THEN800
340 FOR PL=BP TO 2
399 REM CALC PLAYER'S REINFORCEMENTS
400 GOSUB3:GOSUB650
410 RE(PL)=INT(TC(PL)/3):IF RE(PL)<3 THEN RE(PL)=3
420 RE(PL)=RE(PL)+BC(PL)
440 FORT=1TO42:OW(T,2)=O(T):NEXT
449 REM PLACE REINFORCEMENTS
450 LR=PP(PL):L=XY(LR):F=0:GOSUB200
460 KF(PL)=0
499 REM BATTLE (INVADE, RESOLVE BATTLE, CALL OFF INVASION, CHANGING TERRITORY) 
500 PRINT@480,"ATTACK FROM?    ";:GOSUB290
502 IFPC=1THEN540
504 IFPC=2THENIF OW(LR,1)=PL AND O(LR)>1 THEN AC=LR:GOTO510
506 GOTO500
510 PRINT@480,"INVADE WHERE?   ";:GOSUB290
511 IFPC=1THEN500
512 GOSUB50
514 IFPC=2THENIF OW(LR,1)<>PL THEN IC=LR:GOTO518
516 GOTO510
518 PRINT@480,"INVADERS (1-3)? ";
520 IFV>0THEN524
521 I$=INKEY$:IFI$=""THEN521
522 IFI$=CHR$(13)THEN500
524 IFI$="1"THEN NA=1:GOTO530
525 IFI$="2"THEN IF O(AC)>2 THENNA=2:GOTO530
526 IFI$="3"THEN IF O(AC)>3 THENNA=3:GOTO530
528 SOUND1,1:GOTO521
529 REM ** ANOTHER BATTLE? **
530 GOSUB150:IFTT(2)=0THENGG=1:PL=2:GOTO590
532 IFTT(1)=0THENGG=2:PL=2:GOTO590
535 GOTO500
539 REM FORTIFY POSITION FOR PLAYER 1
540 PRINT@480,"REDEPLOY FROM?  ";:GOSUB290
542 IFPC=1THEN590
544 IFPC=2THENIF OW(LR,1)=PL AND O(LR)>1 THEN AC=LR:GOTO550
546 GOTO540
550 PRINT@480,"REDEPLOY TO?    ";
555 F=1:FL=PL:GOSUB290:F=0:FL=0
562 IFPC=1THEN500
564 IFPC=2THENIF OW(LR,1)=PL AND O(AC)>1THEN IC=LR:GOTO570
566 GOTO550
570 PRINT@480,"# OF UNITS?     ";
574 I$=INKEY$:IFI$=""THEN574
575 IFI$=CHR$(13)THEN590
576 IFI$=","THENIF O(IC)>1THEN O(IC)=O(IC)-1:T=IC:GOSUB5:O(AC)=O(AC)+1:T=AC:GOSUB5
578 IFI$="."ORI$=" "THENIF O(AC)>1THEN O(AC)=O(AC)-1:T=AC:GOSUB5:O(IC)=O(IC)+1:T=IC:GOSUB5
579 GOTO574
589 REM ** END OF MAIN LOOP **
590 PP(PL)=LR:NEXTPL:BP=1:GOTO330
599 REM COLLECT A TERRITORY CARD
650 PRINT@480,"CALCULATING REINFORCEMENTS...  ";
652 FORY=1TO6:CC(PL,Y)=0:NEXT:TC(PL)=0:FORT=1TO42:FORY=1TO6
654 IFOW(T,1)=PL AND CO(T)=Y THEN CC(PL,Y)=CC(PL,Y)+1:TC(PL)=TC(PL)+1
656 NEXT:NEXT
660 CC(PL,0)=0:BC(PL)=0
661 IF CC(PL,1)=4THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+2
662 IF CC(PL,2)=9THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+5
663 IF CC(PL,3)=7THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+5
664 IF CC(PL,4)=6THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+3
665 IF CC(PL,5)=12THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+7
667 IF CC(PL,6)=4THEN CC(PL,0)=CC(PL,0)+1:BC(PL)=BC(PL)+2
670 IF KF(PL)>0THEN IF RND(5)=1 THEN BR=BR+5:BC(PL)=BC(PL)+BR:SOUND 200,1:SOUND100,1
690 RETURN
800 PRINT@480,"PLAYER";GG;"WON! PLAY AGAIN (Y/N)?";:SOUND 100,10
810 I$=INKEY$:IFI$=""THEN810
820 IFI$="Y" THENRUN
830 IFI$="N" THENEND
840 GOTO810
850 CLS:PRINT"two player risk FOR THE MC-10"
852 PRINT"VER.5 BY JIM GERRIE"
855 PRINT"BETA TESTING BY CHARLIE GERRIE"
860 PRINT
865 PRINT"USE <ENTER> KEY TO COMPLETE,"
870 PRINT"SKIP, OR ABORT ACTIONS."
875 PRINT"USE Q W E"
877 PRINT"    A S D"
879 PRINT"    Z X C"
880 PRINT"TO NAVIGATE THE WORLD MAP. USE "
882 PRINT"<SPACE> TO SELECT TERRITORIES  "
884 PRINT"ON THE MAP. USE <.> OR <SPACE> "
886 PRINT"TO INCREASE AND <,> TO DECREASE"
888 PRINT"AMOUNTS WHEN PROMPTED FOR #S."
889 PRINT"CURSOR COLOUR INDICATES":PRINT"CONTINENT.";:RETURN
890 PRINT@496,"press any key";
894 I$=INKEY$:R=RND(1000):IFI$=""THEN894
895 RETURN
900 C(0)=175:C(1)=96:C(2)=96:MC=16384:REM CHANGE MC=1024 FOR COCO
950 VN=MC+479:N1=1:N2=2:N3=3:N4=42
999 RETURN
1002 DATA 1,1,1,2,2,0,0,1,1,0,0,1,1,0,2,2,0,2,2,1,1,1,1,1,1,1,1,1,2,2,1,1
1003 DATA 2,2,1,1,1,1,1,1,1,1,0,2,2,0,0,0,0,0,0,1,2,2,1,2,2,1,2,2,1,1,2,2
1004 DATA 0,0,2,2,1,2,2,1,2,2,0,1,0,0,1,0,0,2,2,1,1,1,1,1,1,1,1,2,2,1,0,0
1005 DATA 0,0,1,1,1,1,1,1,1,0,0,0,0,2,2,0,1,1,1,2,2,1,1,2,2,1,1,2,2,1,0,0
1006 DATA 0,0,1,2,2,1,2,2,0,0,0,0,0,0,0,0,2,2,0,1,0,1,1,1,1,1,2,2,0,1,0,1 
1007 DATA 0,0,0,1,1,1,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,2,2,1,1,1,1,0,0,2,2
1008 DATA 0,0,0,2,2,0,0,0,0,0,0,0,0,0,0,0,1,1,1,1,2,2,1,0,2,2,1,1,1,0,0,0
1009 DATA 0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,2,2,1,1,1,1,1,0,0,0,1,0,2,2,0,0,0
1010 DATA 0,0,1,2,2,1,1,1,0,0,0,0,0,0,0,1,1,1,1,1,2,2,1,0,0,0,0,0,0,0,0,0
1011 DATA 0,1,1,1,1,1,1,2,2,0,0,0,0,0,0,0,1,1,1,1,1,1,1,0,0,0,0,2,2,0,2,2
1012 DATA 0,0,2,2,1,1,1,1,0,0,0,0,0,0,0,0,0,0,2,2,1,1,0,0,0,0,0,0,0,0,0,0
1013 DATA 0,0,0,1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,1,1,1,0,0,0,0,0,0,0,2,2,0,1
1014 DATA 0,0,0,1,2,2,0,0,0,0,0,0,0,0,0,0,0,0,1,2,2,0,1,0,0,0,0,0,1,1,2,2
1015 DATA 0,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0,2,2,0,0,0,0,0,0,1,1
1016 DATA 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
2001 DATA "ARGENTINA",2,2,3,3,0,0,0,0,388,1,159
2002 DATA "PERU",4,4,3,3,1,0,1,1,322,1,159
2003 DATA "BRAZIL",4,4,21,21,0,2,1,1,295,1,159
2004 DATA "VENEZUELA",5,5,5,3,3,0,2,2,259,1,159
2005 DATA "CENTRAL AMERICA",6,6,7,7,4,0,4,4,195,2,239
2006 DATA "WESTERN U.S.",8,8,9,7,5,0,5,5,131,2,239
2007 DATA "EASTERN U.S.",9,9,10,10,0,6,5,5,134,2,239
2008 DATA "WESTERN CANADA",11,12,12,9,7,11,6,6,66,2,239
2009 DATA "CENTRAL CANADA",12,12,13,10,7,8,6,7,69,2,239
2010 DATA "EASTERN CANADA",9,13,13,13,7,9,7,7,72,2,239
2011 DATA "ALASKA",37,0,12,12,8,37,8,8,32,2,239
2012 DATA "NW TERRITORIES",11,0,13,13,9,11,8,8,3,2,239
2013 DATA "GREENLAND",12,0,14,14,14,12,9,10,43,2,239
2014 DATA "ICELAND",13,0,18,18,0,13,15,15,14,3,207
2015 DATA "GREAT BRITAIN",14,14,18,17,16,0,16,16,109,3,207
2016 DATA "WESTERN EUROPE",15,17,17,19,21,15,21,21,144,3,207
2017 DATA "NORTHERN EUROPE",15,18,20,20,19,15,16,16,81,3,207
2018 DATA "SCANDINAVIA",14,0,20,20,20,14,15,17,17,3,207
2019 DATA "SOUTHERN EUROPE",17,17,20,27,27,16,22,22,115,3,207
2020 DATA "UKRAINE",18,18,29,29,28,17,19,27,52,3,207
2021 DATA "NORTH AFRICA",16,16,19,22,23,3,24,24,239,4,223
2022 DATA "EGYPT",19,19,27,27,23,21,23,23,212,4,223
2023 DATA "EAST AFRICA",21,22,27,27,26,21,24,25,276,4,223
2024 DATA "CONGO",21,21,23,23,25,0,25,25,338,4,223
2025 DATA "SOUTH AFRICA",24,24,23,26,26,0,26,26,403,4,223
2026 DATA "MADAGASCAR",23,23,0,0,0,25,0,0,438,4,223
2027 DATA "MIDDLE EAST",20,20,28,30,30,19,22,23,182,5,191
2028 DATA "AFGHANISTAN",20,29,29,31,30,20,27,30,119,5,191
2029 DATA "URAL",20,0,32,32,31,20,28,28,55,5,191
2030 DATA "INDIA",28,28,31,33,33,27,33,33,216,5,191
2031 DATA "CHINA",29,32,34,34,33,28,30,33,154,5,191
2032 DATA "SIBERIA",0,36,36,36,35,29,31,34,58,5,191
2033 DATA "SIAM",30,31,31,0,39,30,39,39,251,5,191
2034 DATA "MONGOLIA",32,35,37,38,38,32,31,31,123,5,191
2035 DATA "IRKUTSK",32,36,37,37,37,32,34,34,91,5,191
2036 DATA "YAKUTSK",0,0,37,37,37,32,32,35,28,5,191
2037 DATA "KAMCHATKA",36,0,11,11,38,36,35,38,62,5,191
2038 DATA "JAPAN",34,37,37,0,0,34,0,0,190,5,191
2039 DATA "INDONESIA",33,33,33,40,41,0,41,41,315,6,255
2040 DATA "NEW GUINEA",39,0,0,0,42,39,41,42,318,6,255
2041 DATA "W. AUSTRALIA",39,39,40,42,42,0,42,42,380,6,255
2042 DATA "E. AUSTRALIA",41,40,40,0,0,41,0,0,414,6,255
