1 CLS:CLEAR1500:DIM CA$,CN$,TM$,N1$,SU(80),A,B,K,BX,IX,TR,Z,O,RW,AR(9)
2 DIM GR$(18,9),RG$(18,9),UG$(9,9),OG$(9,9),S$,A$,X,Y,Z,S,L,F,SL,C1,CT,FL,I$
5 DIM I,J,LA,LS,SC,CC,R1,CL,PP
6 Z=0:O=1:TR=-1
20 CLS1
30 GOSUB6210
60 C=1:FORA=1TO35:RESET(A,1):RESET(A,20)
63 C=C+1:IFC=13THENRESET(A,0):RESET(A,21):C=1
67 NEXT
70 C=1:FORB=1TO20:RESET(1,B):RESET(36,B)
73 C=C+1:IFC=8THENRESET(0,B):RESET(37,B):C=1
77 NEXT
90 GOSUB6000:X=5:Y=5:VV=1:C=0:FL=1:A=0:B=0
100 PRINT@480-96," * SUDOKU SOLVER *"
110 PRINT"aswz=MOVE  enter=SOLVE ONCE"
120 PRINT"space=CLEAR NUMBER  g=WIPE GRID"
130 PRINT"m=MAKE A SUDOKU  u=UNIQUE?";
140 PRINT@20+96, "  MC-10   ";
150 PRINT@20+128,"  TOOLS   ";
160 PRINT@20+160,"FOR SUDOKU";
170 PRINT@20+192,"JIM GERRIE";:PRINT@20+256,"           ";:
180 IFFL=1THENI$=CHR$(128)
190 IFFL=-1THENI$=GR$(X,Y)
200 GOSUB330
210 I$=""
220 C=C+1
230 IFC>5THENC=0:FL=FL*-1
240 I$=INKEY$
250 IFVAL(I$)>0THENGOSUB500:IFFL=-1THENGR$(X,Y)=I$:GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
260 IFI$=" "THENGR$(X,Y)=" ":GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
270 IFI$="A"ANDX>1THENGOSUB400:X=X-1:GOTO180
280 IFI$="S"ANDX<9THENGOSUB400:X=X+1:GOTO180
285 IFI$="W"ANDY>1THENGOSUB400:Y=Y-1:GOTO180
290 IFI$="Z"ANDY<9THENGOSUB400:Y=Y+1:GOTO180
295 IFI$=CHR$(13)THENGOSUB400:GOSUB6100:GOSUB700:GOTO90
300 IFI$="G"THENRUN
305 IFI$="M"THENGOSUB400:GOSUB6100:GOSUB4000:GOTO90
306 IFI$="U"THENGOSUB400:GOSUB6110:GOSUB7000:GOTO90
310 GOTO180
320 REM CUR:
330 PRINT@(32*Y)+((X*2)-1),I$;:RETURN
400 REM FF:
410 PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:RETURN
500 REM HH:
510 FL=-1:A=1
530 IFGR$(A,Y)=I$ORGR$(X, A)=I$THENFL=1:RETURN
540 A=A+1
550 IFA<10THEN530
560 A=INT((X-1)/3)
570 A=(A*3)+1
580 B=INT((Y-1)/3)
590 B=(B*3)+1
600 FORC=ATOA+2:FORD=BTOB+2:IFGR$(C,D)=I$THENFL=1:D=B+2:NEXT:C=A+2:NEXT:GOTO640
630 NEXT:NEXT
640 RETURN
700 GOSUB6000:PRINT@480-96," * SUDOKU SOLVER *     "
710 LF=0
720 O=49
730 A=1
740 B=1
750 FORJ=49TO57
760 FORY=1TO9
770 FORX=1TO9
780 IFJ=49THENRG$(X,Y)=""
790 IFGR$(X,Y)<>" "THEN900
800 I$=CHR$(J):GOSUB500
810 IFFL=-1THENRG$(X,Y)=RG$(X,Y)+I$
820 IFJ=57ANDLEN(RG$(X,Y))=1THENGR$(X,Y)=RG$(X,Y):GOSUB400:LF=1:RG$(X,Y)=""
900 REM ZZ:
910 NEXT:NEXT:NEXT
920 IFLF=1THEN710
1000 REM PIP:
1010 I$=CHR$(O):Y=1
1030 CT=0:IFY>=10THEN2050
1040 FORX=1TO9:IFRG$(X,Y)=""THEN2000
1050 S=1:A$=RG$(X,Y):S$=I$:GOSUB3000:IFF>0THENCT=CT+1:V=X
2000 REM RA:
2010 NEXT
2020 IFCT=1THENX=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2030 Y=Y+1
2040 IFY<10THEN1030
2050 X=1
2060 TC=0:IFX>=10THEN2230
2070 FORY=1TO9
2080 IFRG$(X,Y)=""THEN2210
2100 S=1:A$=RG$(X,Y):S$=I$:IFF>0THENTC=TC+1:V=Y
2200 REM RI:
2210 NEXT:IFTC=1THENY=V:GR$(X,Y)=I$:GOSUB400:GOTO710
2220 X=X+1
2225 IFX<10THEN2060
2230 CT=0:IFB>=10THEN2360
2240 FORC=ATOA+2
2250 FORD=BTOB+2
2260 IFRG$(C,D)=""THEN2310
2270 S=1:A$=RG$(C,D):S$=I$:IFF>0THENCT=CT+1:V=C:W=D
2300 REM RK:
2310 NEXT:NEXT
2320 IFCT=1THENX=V:Y=W:GR$(X,Y)=I$:GOSUB400:GOTO710
2330 A=A+3
2340 IFA>7THENA=1:B=B+3
2350 IFB<10THEN2230
2360 O=O+1:IFO<58THEN1010
2380 IFVV=1THENVV=2:J=0:M=9:GOSUB2710
2390 MF=0:CO=0
2410 FORY=1TO9:FORX=1TO9:I$=GR$(X,Y):IFI$=" "THENCO=1:GOTO2510
2450 GR$(X,Y)=" ":GOSUB500:GR$(X,Y)=I$:IFFL=1OR(GR$(X,Y)=" "ANDRG$(X,Y)="")THENMF=1
2500 REM BL:
2510 NEXT:NEXT
2520 IFCO=0ANDMF=0THENFORY=1TO9:FORX=1TO9:GOSUB400:NEXT:NEXT:SOUND200,5:RETURN:REM MAIN MENU
2530 IFMF=1THENJ=9:M=0:GOSUB2710
2600 REM RB:
2610 X=RND(9):Y=RND(9):IFLEN(RG$(X,Y))<2THEN2610
2630 C1=INT(RND(LEN(RG$(X,Y))))
2640 GR$(X,Y)=MID$(RG$(X,Y),C1,1):GOTO710
2700 REM KL:
2710 FORY=1TO9:FORX=1TO9:GR$(X+M,Y)=GR$(X+J,Y):RG$(X+M,Y)=RG$(X+J,Y):NEXT:NEXT:RETURN
3000 L=LEN(S$):SL=LEN(A$):F=0
3010 FORZ=STOSL:IFMID$(A$,Z,L)=S$THENF=Z:Z=SL:NEXT:GOTO3030
3020 NEXT
3030 RETURN
3100 CN$="":RW=INT(I/9):CL=I-(RW*9)
3140 FORB=1TO9:AR(B)=Z:FORA=(RW*9)TO((RW*9)+8):IFSU(A)=BTHENAR(B)=TR
3190 NEXT:FORA=CLTO(72+CL)STEP9:IFSU(A)=BTHENAR(B)=TR
3230 NEXT:NEXT
3260 BX=(INT(I/27)*27)+(INT(CL/3)*3)
3270 FORA=0TO2:FORB=0TO2:IX=BX+(A*9)+B:FORK=1TO9:IFSU(IX)=KTHENAR(K)=TR
3320 NEXT:NEXT:NEXT:FORA=1TO9:IFAR(A)THEN3380
3370 CN$=CN$+CHR$(A+48)
3380 NEXT:RETURN
4000 GOSUB6000:PRINT@480-96," * SUDOKU MAKER * ":FORC=0TO80:SU(C)=0:NEXT:SC=0
4010 FORJ=1TO81:LS=10:FORI=0TO80:IFSU(I)THEN4095
4050 GOSUB3100:TM$=CN$:CC=LEN(TM$):IFCC=0THENI=80:NEXT:J=81:NEXT:GOTO4000
4090 IFCC<LSTHENLS=CC:CA$=TM$:LA=I
4095 NEXT
4100 IFLEN(CA$)=1THENSU(LA)=VAL(CA$):GOTO4200
4110 R1=RND(LS):N1$=MID$(CA$,R1,1):SU(LA)=VAL(N1$)
4200 GOSUB4500:PRINT@480,CA$;"        ";:NEXT
4210 REM CHECK THE NUMBER OF SYMBOLS FOUND SO FAR ...
4220 FORI=0TO80:IFSU(I)>0THENSC=SC+1
4240 NEXT:IFSC<>81THEN4000
4310 REM FINISHED
4330 PRINT@20+256,"           ";:PRINT@480,"PRESS ANY KEY TO CONTINUE";
4340 I$=INKEY$:IFI$=""THEN4340
4350 GOSUB5000
4360 FORY=1TO9:FORX=1TO9:PP=PEEK(16384+(32*Y)+((X*2)-1))
4370 IFPP<58THENGR$(X,Y)=CHR$(PP):UG$(X,Y)=CHR$(PP):OG$(X,Y)=CHR$(PP):PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:GOTO4390
4380 GR$(X,Y)=" ":UG$(X,Y)=" ":OG$(X,Y)=CHR$(PP):PRINT@(32*Y)+((X*2)-1),GR$(X,Y);
4390 NEXT:NEXT
4400 RETURN:REM TO MAIN MENU
4500 C=0:FORY=1TO9:FORX=1TO9:PRINT@(32*Y)+((X*2)-1),CHR$(SU(C)+48);:C=C+1:NEXT:NEXT
4510 RETURN
5000 FORCL=1TO5
5010 A=RND(4)-1:B=RND(4)-1
5030 GOTO5050
5040 NEXT:PRINT@480,"c=CONTINUE a=ANOTHER PATTERN";
5045 I$=INKEY$:IFI$=""THEN5045
5046 IFI$="A"THENGOSUB4500:GOTO5000
5047 IFI$="C"THENRETURN
5048 GOTO5045
5050 X=4-A:Y=4+B:IFPEEK(16384+(32*Y)+((X*2)-1))<58THEN5010
5051 X=6-A:Y=4-B:IFPEEK(16384+(32*Y)+((X*2)-1))<58THEN5010
5052 X=4+A:Y=6+B:IFPEEK(16384+(32*Y)+((X*2)-1))<58THEN5010
5053 X=6+A:Y=6-B:IFPEEK(16384+(32*Y)+((X*2)-1))<58THEN5010
5055 X=4-A:Y=4+B:POKE16384+(32*Y)+((X*2)-1),(PEEK(16384+(32*Y)+((X*2)-1)))-64
5060 X=6-A:Y=4-B:POKE16384+(32*Y)+((X*2)-1),(PEEK(16384+(32*Y)+((X*2)-1)))-64
5070 X=4+A:Y=6+B:POKE16384+(32*Y)+((X*2)-1),(PEEK(16384+(32*Y)+((X*2)-1)))-64
5080 X=6+A:Y=6-B:POKE16384+(32*Y)+((X*2)-1),(PEEK(16384+(32*Y)+((X*2)-1)))-64
5090 GOTO5040
6000 PRINT@480-96,"                             "
6010 PRINT"                               "
6020 PRINT"                               "
6030 PRINT"                              ";
6040 RETURN
6100 PRINT@20+256,"WORKING... ";:RETURN
6110 PRINT@20+256,"TESTING... ";:RETURN
6200 FORY=1TO9:FORX=1TO9:GR$(X,Y)=UG$(X,Y):PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:NEXT:NEXT:RETURN
6210 FORA=1TO9:FORB=1TO9:GR$(A,B)=" ":UG$(A,B)=" ":NEXT:NEXT:RETURN
7000 FS=0:GOSUB6000
7010 PRINT@480-96," * TEST UNIQUENESS *    ":GOSUB6200
7020 GOSUB710
7030 FF=0:FORY=1TO9:FORX=1TO9:IFGR$(X,Y)<>OG$(X,Y)THENFF=1:FS=FS+1:X=9:NEXT:Y=9:NEXT:GOTO7050
7040 NEXT:NEXT
7050 IFFF=0THENPRINT@480-32,"ORIGINAL SOLUTION FOUND";:FS=FS+1:PRINT@480-64,"SOLUTIONS:";FS;:GOTO7100
7060 PRINT@480-64,"SOLUTIONS:";FS;
7070 GOTO7010
7100 PRINT@20+256,"           ";:PRINT@480,"PRESS ANY KEY TO CONTINUE";
7110 I$=INKEY$:IFI$=""THEN7110
7120 RETURN
