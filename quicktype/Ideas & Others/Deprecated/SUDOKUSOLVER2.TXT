10 CLEAR1000:DIM GR$(18,9),RG$(18,9),S$,A$,A,B,X,Y,Z,S,L,F,SL,C1,CT,FL,I$
20 CLS1
30 FORA=1TO9
40 FORB=1TO9
50 GR$(A,B)=" ":NEXT:NEXT
60 C=1:FORA=1TO35:RESET(A,1):RESET(A,20)
63 C=C+1:IFC=13THENRESET(A,0):RESET(A,21):C=1
67 NEXT
70 C=1:FORB=1TO20:RESET(1,B):RESET(36,B)
73 C=C+1:IFC=8THENRESET(0,B):RESET(37,B):C=1
77 NEXT
90 X=5:Y=5:VV=1:C=0:FL=1
100 PRINT@480-96," * SUDOKU SOLVER *
120 PRINT"ASWZ=MOVE  ENTER=SOLVE  1-9"
130 PRINT"SPACE=CLEAR NUMBER  D=WIPE GRID"
160 REM CC:
170 REM
180 IFFL=1THENI$=CHR$(128)
190 IFFL=-1THENI$=GR$(X,Y)
200 GOSUB330
210 I$=""
220 C=C+1
230 IF C>5 THEN C=0:FL=FL*-1
240 I$=INKEY$
250 IFVAL(I$)>0THENGOSUB500:IFFL=-1THENGR$(X, Y)=I$:GOSUB400:X=X+1:IFX>9THENX=1:Y=Y+1:IFY>9THENY=1
260 IFI$=" "THENGR$(X, Y)=" ":GOSUB400:X=X+1:IF X>9THENX=1:Y=Y+1:IFY>9THENY=1
270 IFI$=CHR$(13)THENGOSUB400:GOTO700
280 IFI$="A"ANDX>1THENGOSUB400:X=X-1
285 IFI$="S"ANDX<9THENGOSUB400:X=X+1
290 IFI$="W"ANDY>1THENGOSUB400:Y=Y-1
295 IFI$="Z"ANDY<9THENGOSUB400:Y=Y+1
300 IFI$="D"THENRUN
310 GOTO180
320 REM CUR:
330 PRINT@(32*Y)+((X*2)-1),I$;:RETURN
400 REM FF:
410 PRINT@(32*Y)+((X*2)-1),GR$(X,Y);:RETURN
500 REM HH:
510 FL=-1:A=1
530 IFGR$(A,Y)=I$ORGR$(X, A)=I$THENFL=1:RETURN
540 A=A+1
550 IFA<10THEN530
560 A=INT((X-1)/3)
570 A=(A*3)+1
580 B=INT((Y-1)/3)
590 B=(B*3)+1
600 FORC=ATOA+2:FORD=BTOB+2:IFGR$(C,D)=I$THENFL=1:D=B+2:NEXT:C=A+2:NEXT:GOTO640
630 NEXT:NEXT
640 RETURN
700 REM RN:
710 LF=0
720 O=49
730 A=1
740 B=1
750 FORJ=49TO57
760 FORY=1TO9
770 FORX=1TO9
780 IFJ=49THENRG$(X,Y)=""
790 IFGR$(X,Y)<>" "THEN900
800 I$=CHR$(J):GOSUB500
810 IFFL=-1THENRG$(X,Y)=RG$(X,Y)+I$
820 IFJ=57ANDLEN(RG$(X,Y))=1THENGR$(X,Y)=RG$(X,Y):GOSUB400:LF=1:RG$(X,Y)=""
900 REM ZZ:
910 NEXT:NEXT:NEXT
920 IFLF=1THEN700
1000 REM PIP:
1010 I$=CHR$(O)
1020 Y=1
1030 CT=0
1040 FORX=1TO9:IFRG$(X,Y)=""THEN2000
1050 S=1:A$=RG$(X,Y):S$=I$:GOSUB3000:IFF>0THENCT=CT+1:V=X
2000 REM RA:
2010 NEXT
2020 IFCT=1THENX=V:GR$(X,Y)=I$:GOSUB400:GOTO700
2030 Y=Y+1
2040 IFY<10THEN1030
2050 X=1
2060 TC=0:IFX>=10THEN2230
2070 FORY=1TO9
2080 IFRG$(X,Y)=""THEN2210
2100 S=1:A$=RG$(X,Y):S$=I$:IFF>0THENTC=TC+1:V=Y
2200 REM RI:
2210 NEXT:IFTC=1THENY=V:GR$(X,Y)=I$:GOSUB400:GOTO700
2220 X=X+1
2225 IFX<10THEN2060
2230 CT=0:IFB>=10THEN2360
2240 FORC=ATOA+2
2250 FORD=BTOB+2
2260 IFRG$(C,D)=""THEN2310
2270 S=1:A$=RG$(C,D):S$=I$:IFF>0THENCT=CT+1:V=C:W=D
2300 REM RK:
2310 NEXT:NEXT
2320 IFCT=1THENX=V:Y=W:GR$(X,Y)=I$:GOSUB400:GOTO700
2330 A=A+3
2340 IFA>7THENA=1:B=B+3
2350 IFB<10THEN2230
2360 O=O+1
2370 IFO<58THEN1010
2380 IFVV=1THENVV=2:J=0:M=9:GOSUB2710
2390 MF=0
2400 CO=0
2410 FORY=1TO9
2420 FORX=1TO9
2430 I$=GR$(X,Y)
2440 IFI$=" "THENCO=1:GOTO2510
2450 GR$(X,Y)=" "
2460 GOSUB500:GR$(X,Y)=I$
2470 IFFL=1OR(GR$(X,Y)=" "ANDRG$(X,Y)="")THENMF=1
2500 REM BL:
2510 NEXT:NEXT
2520 IFCO=0ANDMF=0THENFORY=1TO9:FORX=1TO9:GOSUB400:NEXT:NEXT:SOUND200,5:GOTO60
2530 IFMF=1THENJ=9:M=0:GOSUB2710
2600 REM RB:
2610 X=RND(9):Y=RND(9):IFLEN(RG$(X,Y))<2THEN2600
2630 C1=INT(RND(LEN(RG$(X,Y))))
2640 GR$(X,Y)=MID$(RG$(X,Y),C1,1):GOTO700
2700 REM KL:
2710 FORY=1TO9
2720 FORX=1TO9
2730 GR$(X+M,Y)=GR$(X+J,Y)
2740 RG$(X+M,Y)=RG$(X+J,Y)
2750 NEXT:NEXT
2760 RETURN
3000 L=LEN(S$):SL=LEN(A$):F=0
3010 FORZ=STOSL:IFMID$(A$,Z,L)=S$THENF=Z:Z=SL:NEXT:GOTO3030
3020 NEXT
3030 RETURN
