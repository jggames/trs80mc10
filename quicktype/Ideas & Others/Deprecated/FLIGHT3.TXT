0 CLEAR1000:DIMX,Y,Z,X1,X2,ALT,TP,RY,RL,FL,VC,CH,US,XC,YC,CY,PC,P,R,XX,YY,ALT,J,J0,J1,ST,VE,A$,B$,S1$,S2$
2 CLS:PRINT"PLEASE WAIT...":PRINT@230,"FLIGHT SIMULATOR 1":GOSUB9000:GOTO8000
3 POKEVC,CH:RETURN
4 POKEVC,J:RETURN
30 XC=0:YC=R:PC=3-2*R 
35 GOSUB70
40 IFPC<0THENPC=PC+4*XC+6:GOTO50
45 PC=PC+4*(XC-YC)+10:YC=YC-1   
50 XC=XC+1 
55 IFXC<YCTHEN35
60 IFXC=YCTHENGOSUB70 
65 RETURN
70 XX=CX+XC:YY=CY+YC:US=USR((INT(XX)*256+INT(YY))*65537)
71 XX=CX-XC:YY=CY+YC:US=USR((INT(XX)*256+INT(YY))*65537)
72 XX=CX+XC:YY=CY-YC:US=USR((INT(XX)*256+INT(YY))*65537)
73 XX=CX-XC:YY=CY-YC:US=USR((INT(XX)*256+INT(YY))*65537)
74 XX=CX+YC:YY=CY+XC:US=USR((INT(XX)*256+INT(YY))*65537)
75 XX=CX-YC:YY=CY+XC:US=USR((INT(XX)*256+INT(YY))*65537) 
76 XX=CX+YC:YY=CY-XC:US=USR((INT(XX)*256+INT(YY))*65537)
77 XX=CX-YC:YY=CY-XC:US=USR((INT(XX)*256+INT(YY))*65537)
79 RETURN
110 GOSUB3:CLS0:PRINT@384,"";
120 FORY=1TO4:FORX=1TO32:READ T
130 ON T GOTO 140,142,144,160,160,160,160,160,160
135 PRINT CHR$(128);:GOTO160
140 PRINT CHR$(191);:GOTO160
142 PRINT CHR$(135);:GOTO160
144 PRINT CHR$(139);:GOTO160
160 NEXT:NEXT:POKE16895,191:B$=CHR$(128)
190 US=USR(((128/4)*256+(92/4))*65536+(128/4)*256+(84/4))
215 US=USR(((128/4)*256+(100/4))*65536+(128/4)*256+(108/4))
220 US=USR(((124/4)*256+(96/4))*65536+(116/4)*256+(96/4))
225 US=USR(((20/4)*256+INT(146/4))*65536+INT(237/4)*256+INT(146/4))
230 US=USR(((0/4)*256+(96/4))*65537)
235 US=USR(((255/4)*256+(96/4))*65537)
240 R=5:CX=19:CY=170/4:GOSUB30
242 R=5:CX=30:CY=170/4:GOSUB30
244 R=6:CX=19:CY=170/4:GOSUB30
246 R=6:CX=30:CY=170/4:GOSUB30
248 FORT=0TO16:PRINT@T,B$;:NEXT
250 US=USR(((132/4)*256+(96/4))*65536+(140/4)*256+(96/4))
262 US=USR(((20/4)*256+INT(188/4))*65536+(40/4)*256+INT(188/4))
265 US=USR((INT(22/4)*256+INT(178/4))*65537)
270 US=USR((INT(22/4)*256+INT(154/4))*65537)
290 US=USR((4*256+36)*65536+4*256+47)
295 US=USR((0*256+47)*65536+63*256+47)
300 FORT=16384TO16638:S1$=S1$+CHR$(PEEK(T)):NEXT
310 FORT=16640TO16894:S2$=S2$+CHR$(PEEK(T)):NEXT
320 Y=96:P=1:X=0:CY=0:Z=0
910 GOSUB4:CLS
1010 PRINT"       FLIGHT SIMULATOR":PRINT
1015 PRINT"  STEER YOUR PLANE BY USING:"
1020 PRINT" w FORWARD TO DIVE, z BACK TO"
1030 PRINT" CLIMB, a LEFT TO TURN LEFT,"
1040 PRINT" AND s RIGHT TO TURN RIGHT."
1045 PRINT:PRINT"   PRESS ANY KEY TO CONTINUE."
1110 A$=INKEY$:IFA$=""THEN1110
1210 CLS:PRINT"          INSTRUMENTS"
1310 PRINT"  FROM LEFT TO RIGHT THEY ARE:"
1315 PRINT" ALTIMETER-TO JUDGE ALTITUDE"
1320 PRINT
1330 PRINT" ROLL INDICATOR-TO JUDGE TURNS"
1335 PRINT:PRINT" CLIMB/DIVE INDICATOR-TO JUDGE"
1340 PRINT" ANGLE OF FLIGHT":PRINT
1345 PRINT" RADAR-TO READ THE LOCATION OF"
1350 PRINT" THE RUNWAY"
1410 PRINT:PRINT" FUEL GAUGE-FLIGHT TIME LEFT"
1420 PRINT
1430 PRINT"   PRESS ANY KEY TO CONTINUE."
1510 A$=INKEY$:IFA$=""THEN1510
1610 CLS:PRINT"        WARNING LIGHTS"
1620 PRINT
1630 PRINT"  A RED FLASHING SIGNAL LIGHT"
1640 PRINT" MEANS THAT YOUR INCLINE IS TOO"
1650 PRINT" STEEP. IF YOU DON'T CORRECT"
1660 PRINT" THIS IMMEDIATELY, YOU WILL"
1670 PRINT" STALL AND GO INTO A STEEP DIVE."
1710 PRINT:PRINT"  A GREEN FLASHING SIGNAL LIGHT"
1720 PRINT" MEANS THAT YOU HAVE REACHED"
1730 PRINT" THE NECESSARY HEIGHT AND YOU"
1740 PRINT" MAY RETURN TO THE RUNWAY."
1810 PRINT:PRINT" HIT ANY KEY TO BEGIN FLIGHT."
1910 A$=INKEY$:IFA$=""THEN1910
2120 J0=32:J1=48:CLS0:GOSUB3
2210 A$=INKEY$:IFA$="A"THENJ0=J0-8:IFJ0<8THENJ0=8
2230 IFA$="S"THENJ0=J0+8:IFJ0>56THENJ0=56
2240 IFA$="Z"THENJ1=J1+8:IFJ1>56THENJ1=56
2250 IFA$="W"THENJ1=J1-8:IFJ1<8THENJ1=8
2310 IFALT>635THENCY=CY+(J0-33)/4:GOTO2410
2320 CY=0:X=X-(J0-33)/4
2410 X=X-CY/10
2510 IFY>4THENY=Y+(J1-33)/6:GOTO2610
2520 Y=5
2610 IFP=1THENP=2:GOTO2710
2620 P=1
2710 PRINT@Z,S1$;B$;S2$;:POKEVE,191:ALT=ALT+.50*(Y-96)
3010 IFALT>3200THENALT=3200:TP=1:POKE16895,Z
3110 IFALT<=ZANDY<82THEN4910
3210 IFALT<=ZANDTP=1ANDX<15ANDX>-15THEN5810
3310 IFALT>634ORY>142ORY<2THEN3510
3410 GOTO6510
3510 IFY>144THEN4310
3520 ST=Z
3610 US=USR((5*256+INT((186-ALT/100)/4))*65536+9*256+INT((186-ALT/100)/4))
3710 US=USR((16*256+INT(((-1*CY)/12+170)/4))*65536+22*256+INT((CY/12+170)/4))
3810 US=USR((27*256+INT(((Y-96)/10+170)/4))*65536+33*256+INT((170-(Y-96)/10)/4))
3910 IFY+CY>0ANDY+CY<255ANDY-CY>0ANDY-CY<250THENUS=USR((0*256+INT((Y+CY)/4))*65536+63*256+INT((Y-CY)/4))
4010 IFX<ZTHENUS=USR(690628906):GOTO4110
4020 US=USR(774516266)
4110 FL=FL+.03:US=USR((56*256+INT((160+FL)/4))*65537):IFFL>20THEN4310
4210 GOTO2210
4310 REM ***STALL***
4410 ST=ST+1:POKE16864,255:IFST<6THEN3610
4510 FORQ=1TO5:SOUND100,3:SOUND1,1:GOSUB3:FORW=1TOQ*100:NEXTW:NEXTQ
4520 FORQ=255TO10STEP-10:SOUNDQ,1:NEXTQ:GOSUB3:Y=-20:ALT=ALT-500
4610 IFFL>20THEN4910
4710 GOTO3610
4810 REM ***CRASH***
4910 SOUND1,1:GOSUB3
5010 IFY>0ANDX+ALT/5+2>=0ANDX+ALT/5+2<=255THEN US=USR((INT((X+ALT/5+2)/4)*256+INT(144/4))*65536+INT(128/4)*256+INT(Y/4))
5110 IFY>0ANDX-ALT/5+255>=0ANDX-ALT/5+255<=255THEN US=USR((INT((X-ALT/5+255)/4)*256+INT(144/4))*65536+INT(128/4)*256+INT(Y/4))
5210 IFY+CY>0ANDY-CY>0THENUS=USR((INT(0/4)*256+INT((Y+CY)/4))*65536+INT(255/4)*256+INT((Y-CY)/4))
5220 FORT=1TO11:READ X,Y,X1,Y1:US=USR((INT(X/4)*256+INT(Y/4))*65536+INT(X1/4)*256+INT(Y1/4)):NEXT
5410 FORQ=1TO500:NEXTQ:CLS0
5510 SOUND1,10:GOSUB3
5610 GOTO7710
5710 REM ***LAND***
5810 FORQ=1TO5
5910 FORE=1TO3:SOUND100,1:NEXTE:GOSUB3
6010 FORW=1TO(5-Q)*50:NEXTW
6110 NEXTQ
6210 FORQ=1TO10:SOUND50,1:NEXTQ:GOSUB3
6310 GOTO7410
6410 REM ***RUN WAY***
6510 IFX+ALT/5>-2ANDX+ALT/5<253THENUS=USR((INT((X+ALT/5+2)/4)*256+36)*65536+32*256+INT(Y/4))
6610 IFX-ALT/5>-255ANDX-ALT/5<0THENUS=USR((INT((X-ALT/5+255)/4)*256+36)*65536+32*256+INT(Y/4))
6710 RL=RL+.25:IFRL>.75THENRL=.25
6810 RY=Y+RL*(144-Y)
6910 X1=RL*X
7010 IFX1>-128ANDX1<128THENUS=USR((INT((X1+128)/4)*256+INT(RY/4))*65537)
7110 IFY>0THENUS=USR((32*256+INT(Y/4))*65537)
7210 GOTO3510
7310 REM *** LAND ***
7410 GOSUB4:CLS:PRINT:PRINT"      GOOD LANDING PILOT!":PRINT
7420 PRINT" YOU SUCCESSFULLY LANDED WITH"
7430 PRINT(INT((20-FL)*100))/100;"GALLONS OF FUEL LEFT IN":PRINT" THE TANK."
7510 PRINT:PRINT:PRINT"   DO YOU WISH TO FLY AGAIN?":A$=INKEY$
7610 A$=INKEY$:IFA$=""THEN7610
7615 IFA$="Y"THENRUN
7620 IFA$="N"THENEND
7630 GOTO7610
7710 REM *** CRASH ***
7810 GOSUB4:CLS:PRINT:PRINT"         YOU CRASHED!":PRINT
7820 PRINT" NEXT TIME BE SURE TO BE AWARE"
7830 PRINT" OF YOUR ALTIMETER, YOUR WARN-":PRINT" INGS, AND YOUR FUEL."
7910 GOTO7510
8000 CLS:VC=49151:VE=16895:CH=4:J=0:Y=96:P=1
8900 GOTO110
8920 REM LINE DRAW SUBROUTINE BY GREG DIONNE
8930 REM US=USR((X0*256+Y0)*65537) FOR SINGLE POINT
8940 REM US=USR((X0*256+Y0)*65536+X1*256+Y1) TO DRAW LINE
9000 DATALNPDALMMABABNNLLNMMMJAMKCCAJCFADHPAALLHAAALLEANAMLCCAJCFADHPAALMHAAALMFANNNGBBCDHCEPJHMOJHNLJGNGJHNMINDCJG
9001 DATAMKJBMMCHCIJLLLJHMKJGMOJLNHJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNGNHNMNGMLNLLMNHMLCANAFPHOOMOCNMMKNNNI
9002 DATAJGNJMGKLDNIGIAMAFFEEEEMLFFMBFFCEPIHEAANICEABEEIKIAJHNKIGCADNNLNIIJAAIKEAIEEBNNNINONINGNKOKAAOHAADJEPJHMOJH
9003 DATANLJGNHJHNMINMAJGMLJBMNCHLGJLLMJHMLJGMOJLNGJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNHNHNMNGMKNLLLNHMKCANA
9004 Z=32768:FORX=0TO3:READA$::GOSUB9005:NEXTX:Z=16917:A$="HOIAAA"
9005 FORY=1TOLEN(A$)/2:POKEZ,(ASC(MID$(A$,2*Y-1,1))-65)*16+ASC(MID$(A$,2*Y,1))-65:Z=Z+1:NEXTY:RETURN
9007 REM US=USR((INT(/4)*256+INT(/4))*65536+INT(/4)*256+INT(/4))
9008 REM US=USR((INT(/4)*256+INT(/4))*65537)
9200 DATA 2,1,0,0,0,1,1,1,0,0,0,1,1,1,0,0,0,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3
9210 DATA 1,1,0,0,0,1,1,0,0,0,0,0,1,0,0,0,0,0,1,1,1,1,1,1,1,1,1,1,0,1,1,1
9220 DATA 1,1,0,0,0,1,1,0,0,0,0,0,1,0,0,0,0,0,1,1,0,0,0,0,1,1,1,1,0,1,1,1
9230 DATA 1,1,0,0,0,1,1,1,0,0,0,2,1,3,0,0,0,1,1,1,1,1,1,1,1,1,1,1,0,1,1,9
9300 DATA 0,0,70,20
9301 DATA 70,20,50,70
9302 DATA 50,70,128,96
9303 DATA 128,96,100,100
9304 DATA 100,100,70,130
9305 DATA 70,130,0,144
9306 DATA 255,0,240,50
9307 DATA 240,50,140,60
9308 DATA 140,60,128,96
9309 DATA 128,96,144,120
9310 DATA 144,120,220,110
