5 DIMA(285)A,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,UU,VV,RR,FF,ZZ,MC,D$:D$="-":FF=.5:MC=16384
10 CLS1:U=25:V=14:I=5:GOSUB1935:U=100:V=25:I=1:GOSUB1935:U=80:V=12:I=4:GOSUB1935
12 FORZ=0TO10:RR=RND(351):IFPEEK(MC+RR)=143THENPRINT@RR,"*";
13 NEXTZ
15 PRINT@FF*704,"TREK III BY LANCE MICKLUS"
25 PRINT"BURLINGTON, VT."
30 PRINT"COPYRIGHT 1979"
50 FORX=0TO279:A(X)=0:NEXTX:RESTORE
55 READB$,J:IFJ<>999THEN55
60 P=RND(191)-1:IFA(P)<>0THEN60
65 READJ:IFJ>0THENA(P)=-J:GOTO60
70 P=RND(150):M=P:IFA(P)<>0THEN70
75 GOSUB1640:GOSUB1700
80 FORB=A(256)-1TOA(256)+1:FORC=A(257)-1TOA(257)+1
85 FORD=A(258)-1TOA(258)+1
90 GOSUB1625
95 IFP=-1THEN100
96 IFA(P)>=0THENA(P)=10*INT(A(P)/10)-5:GOTO100
97 A(P)=10*(-INT(-A(P)/10))-5
100 NEXTD:NEXTC:NEXTB:A(M)=-4
105 P=RND(191)-1:IFA(P)<>0THEN105
110 READA(P):IFA(P)<>0THENA(P)=-A(P):GOTO105
115 FORN=0TO190:IFA(N)=0THENA(N)=-1
120 NEXTN:A(256)=7:A(257)=7:A(258)=2:A(276)=3
125 E=3999:T=2200:H=2470:Q=0:A(191)=1006:S=0
126 PRINT@480,"PRESS ANY KEY TO BEGIN MISSION.";
127 I$=INKEY$:RR=RND(1000):IFI$=""THEN127
130 CLS1:A(259)=3:A(260)=3:A(261)=1:GOSUB955
135 CLS1:RESTORE
140 PRINT"CREW AWAITING YOUR ORDERS"
145 READB$,J:PRINTTAB(5);J;:PRINTTAB(9);B$:IFJ<>11THEN145
150 O=99:INPUT"ORDERS";O
155 IFO<0THEN2500
160 IF(O>11)+(INT(O)<>O)THEN135
165 IFO>4THEN175
170 ONO+1GOSUB1570,455,385,1240,1390:GOTO180
175 ONO-4GOSUB2050,1410,880,1160,1125,320,520
180 GOSUB1705:GOSUB1625:IFABS(A(P))<10000THEN210
185 GOSUB1040
190 IFA(261)=2THENA(261)=3:GOSUB345
195 IF(A(268)=0)*(A(271)=0)THENO=3:GOSUB400
200 IFA(271)=0THENGOSUB2050
205 GOTO220
210 B$="  SPACE STORM"
215 IFRND(100)=1THENGOSUB355:GOSUB555
220 IFA(261)>1THENGOSUB1570
225 T=T+.2:E=E-A(261)*A(261)*10
230 IFE<0THEN2185
235 J=1:GOSUB520
240 IFT<HTHEN135
245 CLS1:REM 32
247 PRINT@FF*384,"FLEET COMMAND REQUESTS"
250 PRINT"YOU RETURN TO"
255 PRINT"FLEET HQ BASE 7,7,2":H=H+10:GOSUB1675:GOTO135
260 GOTO135
265 CLS1:RESTORE:N=2500-T:IFN<0THENS=S+N*10
270 FORP=0TO191:IFA(P)<0THEN280
271 S=S+1:F=INT(A(P)/10)*10:IFA(P)<0THENF=-INT(-A(P)/10)*10
275 IFA(P)-F=9THENS=S+20
280 NEXTP
285 READB$,Z:IFZ<>425THEN285
290 IFS<ZTHENREADB$,Z:GOTO290
295 PRINT@FF*256,"RATING:";INT(S/4.91)
300 PRINT:PRINT"ADMIRAL GERRIE HERE...":PRINT
305 PRINT"CAPTAIN, AFTER REVIEWING YOUR"
310 PRINT"LOG I'M RECOMMENDING THAT":PRINT"YOU BE ";B$;".";
315 PRINT:PRINT:END
320 CLS1:REM 32
325 PRINT@FF*320,"1 - GREEN":PRINT"2 - YELLOW":PRINT"3 - RED
330 INPUT"ENTER CONDITION CODE";N
335 IF(N<1)+(N>3)+(INT(N)<>N)THEN330
340 A(261)=N:IFN=1THENRETURN
345 RESTORE
350 READB$,J:IFJ-20<>A(261)THEN350
355 CLS1:GOSUB2600
365 FORK=0TO4:PRINT@FF*530,B$:FORZZ=1TO250:NEXT
370 PRINT@FF*530,"                ":FORZZ=1TO250:NEXT
375 IFINKEY$=CHR$(13)THENK=4
380 NEXTK:RETURN
385 CLS1:IFA(268)<>0THENI=2:GOTO505
390 IFA(271)<>0THENI=5:GOTO505
395 O=0
400 FORP=192TO255:IF(A(P)=1)+(A(P)=0)THEN445
405 IF(O=3)*(A(P)<25)THEN445
410 CLS1:U=62:V=14
415 GOSUB1930
420 FORZ=0TORND(5)+2:RR=RND(287):IFPEEK(MC+RR)=143THENPRINT@RR,"*";
421 NEXTZ:GOSUB2095:GOSUB1640
425 PRINT:PRINT"OBJECT AT";STR$(B);STR$(C):PRINT"IS A ";B$;"."
430 IFA(P)<25THEN440
435 PRINT"READING";A(P)-25;"UNITS OF ENERGY."
440 GOSUB2105
445 NEXTP
450 RETURN
455 CLS1:RESTORE:READB$,Z
460 IFA(267)<>0THENI=1:GOTO505
465 PRINT"TACTICAL HERE..."
470 PRINT"DAMAGE CONTROL REPORTS:":PRINT
475 FORZ=267TO275:READB$,J:PRINTB$,
480 IF(Z=275)*(A(276)=0)THENPRINT"*INOPERATIVE*":GOTO500
485 IFA(Z)=0THENPRINT"OPERATIONAL":GOTO500
490 IFA(Z)<10THENPRINT"NEARLY"
495 IFA(Z)>9THENPRINT"*INOPERATIVE*"
500 NEXTZ:GOTO1675
505 CLS1:RESTORE:REM 32
510 READB$,J:IFI<>JTHEN510
515 PRINT@FF*320,B$;" INOPERATIVE":GOTO2105
520 FORN=1TOJ:FORZ=267TO275
525 IFA(Z)=0THEN550
530 A(Z)=A(Z)-RND(5):IFA(Z)>0THEN550
535 A(Z)=0:RESTORE:IFA(267)<>0THEN550
540 READB$,J:IFJ<>Z-266THEN540
545 CLS1:REM 32
546 PRINT@FF*320,B$;" OPERATIONAL":GOSUB2105
550 NEXTZ:NEXTN:RETURN
555 FORJ=1TO4-A(261)
560 Z=266+RND(9):IFA(Z)<>0THEN585
565 A(Z)=RND(25)*J:IFA(267)<>0THEN585
570 CLS1:RESTORE:REM 32
575 READB$,J:IFJ<>Z-266THEN575
580 PRINT@FF*320,"DAMAGE TO ";B$:GOSUB2105
585 NEXTJ:RETURN
590 CLS1:P=X*8+Y+192:U=30:V=14
595 GOSUB1930:GOSUB2095:Z=338:M=I
600 PRINT:PRINTTAB(10);"SECTOR:";X;Y
605 B$=" *":IF(Q=1)*(I=5)THENGOSUB830:GOTO625
610 B$="-":IFI=5THENGOSUB830:GOTO620
615 Z=336:B$=")":GOSUB830
620 B$=" ":GOSUB830
625 L=L*4
630 IFQ=1THEN650
635 IFRND(2)=1THENA(282)=A(282)-X:A(283)=A(283)-Y:GOTO650
640 Z=1
641 L=L*.6:Z=Z+1:IFZ*Z<=(X-A(282))*(X-A(282))+(Y-A(283))*(Y-A(283))THEN641
645 X=A(282):Y=A(283):GOTO670
650 GOSUB2130
655 X=X+A(282):Y=Y+A(283)
660 IF(X<0)+(X>7)+(Y<0)+(Y>7)THENP=-1:CLS1:GOTO705
665 L=L*.6
670 P=INT(X)*8+INT(Y)+192
675 IFA(P)=0THEN655
680 IFQ=2THENRETURN
685 CLS1:V=15:IFA(P)=18THENU=28
690 IF(Q<>1)*(I=5)*(A(P)<25)THENP=-1:GOTO710
695 IF(Q<>1)*(I=1)*(A(P)<>18)THENP=-1:GOTO710
700 GOSUB1930
705 IFQ=2THENRETURN
710 GOSUB2095:PRINT:PRINTTAB(10);"SECTOR:";INT(X);INT(Y)
715 B$="* ":IF(M=5)*(Q=1)THENGOSUB850:GOTO735
720 B$="-":IFM=5THENGOSUB850:GOTO730
725 B$="(":GOSUB850
730 B$=" ":GOSUB850
735 Q=0:L=INT(L):IF(P=-1)+(I=0)THENRETURN
740 GOSUB1890
745 IF(M=5)*(I<>1)THENGOSUB795:GOSUB2105:GOTO2195
750 IFI<>5THEN775
755 GOSUB870:E=E-L
760 IFL-A(261)*75>0THENGOSUB555:L=L-A(261)*75:GOTO760
765 IFE>0THENRETURN
770 GOTO2190
775 IFI<>1THEN795
780 A(P)=A(P)-L:IFA(P)>25THENRETURN
785 GOTO795
790 IFL<100THENRETURN
795 PRINTTAB(4);B$;" DESTROYED"
800 FORN=0TO30
805 SET(FF*(21+RND(13)),(12+RND(5))*.66,1):RESET(FF*21+RND(13),(12+RND(5))*.66)
810 NEXTN
815 A(P)=0:GOSUB1705:GOSUB1625
820 A(P)=ABS(A(P))-10000:S=S+10
825 RETURN
830 FORN=Z+2TO380
835 PRINT@FF*N,B$;:FORZZ=1TO5:NEXT
840 NEXTN:IFQ=1THENPRINT@FF*380,"  "
845 RETURN
850 FORN=380TO340STEP-1
855 PRINT@FF*N,B$;:FORZZ=1TO5:NEXT
860 NEXTN:IFQ=1THENPRINT@FF*338," ";
865 PRINT@FF*640,"";:RETURN
870 FORW=1TOINT(L/50):POKE49151,64:POKE49151,0:NEXTW:GOTO865
880 CLS1:IFA(273)<>0THENI=7:GOTO505
885 GOSUB1705
890 REM 32
891 PRINT@FF*320,"HELM HERE..."
895 INPUT"COURSE (X,Y,Z)";B,C,D:GOSUB1625
900 IFP=-1THEN895
905 INPUT"WARP FACTOR (0-8)";K:IFK=0THENRETURN
910 IF(K<0)+(K>8)+(INT(K)<>K)THEN905
915 F=(A(256)-B)*(A(256)-B)+(A(257)-C)*(A(257)-C)+(A(258)-D)*(A(258)-D)
916 L=F/2:N=F/4
917 IFL*L>FTHENL=L-N:GOTO919
918 L=L+N
919 N=N/2:IFABS(L*L-F)>.01THEN917
920 IFE-100>L*K*K*KTHEN945
925 CLS1:REM 32
926 PRINT@FF*320,"ENGINEERING HERE...":PRINT
930 PRINT"SORRY CAPTAIN,":PRINT"BUT WE JUST DON'T HAVE"
935 PRINT"ENOUGH ENERGY."
940 GOTO2105
945 E=E-L*K*K*K:T=T+2*L*L/K:GOSUB1700
950 J=INT(L*L/K):IFJ>0THENGOSUB520
955 GOSUB1705:GOSUB1625:GOSUB1655
960 IFA(266)=0THENA(266)=8:A(P)=INT(ABS(A(P))/10)*10+8
965 IF(A(265)>0)*(A(266)=1)THENA(266)=7:GOSUB1645
970 B=X:C=Y:D=3:GOSUB1630
975 IF(A(266)>1)*(A(266)<5)THEN2150
980 IFA(266)>7THENA(265)=A(265)-1
985 FORN=192TO255:A(N)=0:NEXTN:A(P)=18
990 FORN=262TO265
995 IFA(N)=0THEN1025
1000 IFN=262THENM=RND(1500)+250
1005 IFN=263THENM=20
1010 IFN=264THENM=19
1015 IFN=265THENM=3
1020 GOSUB2020:A(N)=A(N)-1:GOTO995
1025 NEXTN
1030 IFA(266)>7THENM=2:GOSUB2020
1035 R=0:GOTO2050
1040 FORR=192TO255
1045 IFA(R)<25THEN1120
1050 X=INT((R-192)/8):Y=R-192-(X*8):A(280)=X:A(281)=Y
1055 IFRND(2)>1THEN1085
1060 A(282)=ABS(A(259)>X)-ABS(A(259)<X):A(283)=ABS(A(260)>Y)-ABS(A(260)<Y)
1065 IFGTHENA(282)=2-RND(3):A(283)=2-RND(3):GOTO1070
1066 A(282)=A(282)*(RND(2)-1):A(283)=A(283)*(RND(2)-1)
1070 IF(X+A(282)>=0)*(X+A(282)<=7)THENA(280)=X+A(282)
1075 IF(Y+A(283)>=0)*(Y+A(283)<=7)THENA(281)=Y+A(283)
1080 IFA(A(280)*8+A(281)+192)=0THENZ=A(X*8+Y+192):A(X*8+Y+192)=0:X=A(280):Y=A(281):A(X*8+Y+192)=Z
1085 IFGTHEN1120
1090 A(282)=A(259)-X:A(283)=A(260)-Y
1095 A(280)=X:A(281)=Y:A(284)=A(282):A(285)=A(283):Q=2:GOSUB650
1100 IFP=-1THEN1110
1105 IFA(P)<>18THEN1120
1110 X=A(280):Y=A(281):A(282)=A(259):A(283)=A(260):Q=0:L=RND(A(R)*.7)
1115 IFL>100THENA(R)=A(R)-L:GOSUB590
1120 NEXTR:G=0:RETURN
1125 CLS1:IF(A(275)<>0)+(A(276)=0)THENI=9:GOTO505
1130 Q=1:GOSUB2050
1135 INPUT"TORPEDO HEADING (0-8)";I:PRINTTAB(3);" ";
1140 IF(I<0)+(I>8)THEN1135
1145 G=-1:GOSUB1040
1150 GOSUB2400:GOSUB1705
1155 A(276)=A(276)-1:Q=1:L=8000:GOTO590
1160 CLS1:IFA(274)<>0THENI=8:GOTO505
1165 REM 32
1166 PRINT@FF*320,"WEAPONS OFFICER HERE..."
1170 INPUT"ENERGY";O:IFO<=0THENRETURN
1175 PRINT"BEAM WEAPON LOCKED ON TARGET."
1180 FORZZ=1TO2500:NEXT
1185 FORA=192TO255
1190 IFA(A)<25THEN1235
1195 G=-1:GOSUB1040
1200 X=INT((A-192)/8):Y=A-192-X*8
1205 A(284)=X:A(285)=Y
1210 A(282)=X-A(259):A(283)=Y-A(260):GOSUB1705
1215 Q=2:GOSUB650:Q=0:A(282)=A(284):A(283)=A(285):L=O:GOSUB1705
1220 IFP=-1THEN1230
1225 IFA(P)<25THEN1235
1230 IFE-100-O>0THENE=E-O:GOSUB590
1235 NEXTA:RETURN
1240 CLS1:RESTORE:IFA(269)<>0THENI=3:GOTO505
1245 READB$,J:IFJ<>50THEN1245
1250 PRINT"SHIP'S COMPUTER FUNCTIONS:":PRINT
1255 PRINTJ-50;" LOCATE ";B$
1260 IFJ<>54THENREADB$,J:GOTO1255
1265 PRINT" 5  LRS SCAN FROM DATA BASE"
1270 PRINT" 6  QUADRANT DETAILED DISPLAY"
1275 PRINT:INPUT"ENTER FUNCTION";O
1280 IF(O<0)+(O>6)+(INT(O)<>O)THEN1240
1285 IFO<5THEN1295
1290 ONO-4GOTO1375,1350
1295 CLS1:RESTORE
1300 READB$,J:IFJ-50<>OTHEN1300
1305 PRINT"CURRENT LOCATTION:";A(256);A(257);A(258)
1310 PRINT"DATA BASE SCAN FOR ":PRINTB$;":":PRINT
1315 FORP=0TO191
1320 IF(O=4)*(A(P)<0)THEN1340
1325 IF(A(P)<0)+(O=4)THEN1345
1330 GOSUB1655
1335 IFA(262+O)=0THEN1345
1340 GOSUB1640:PRINTB;C;D,
1345 NEXTP:PRINT:GOTO1675
1350 INPUT"ENTER QUADRANT (X,Y,Z)";B,C,D:GOSUB1625
1355 IFP=-1THEN1350
1360 GOSUB1655
1365 IFA(P)<0THENPRINT"NO DATA AVAILABLE.":GOTO2105
1370 GOTO1710
1375 INPUT"ENTER QUADRANT (X,Y,Z)";U,V,W:CLS
1380 PRINT@FF*904,"LR SCAN OF";U;V;W;
1385 R=1:GOTO1760
1390 CLS:IFA(270)<>0THENI=4:GOTO505
1395 U=A(256):V=A(257):W=A(258):R=0
1400 PRINT@FF*899,"LR SENSOR SCAN QUADRANT";STR$(U);STR$(V);STR$(W);
1405 GOTO1760
1410 CLS1:IFA(272)<>0THENI=6:GOTO505
1415 Q=1:GOSUB2050
1420 INPUT"HEADING (0-8)";I:IFA(271)=0THENPRINTTAB(3);" ";
1425 IF(I<0)+(I>8)THEN1420
1430 GOSUB2400:GOSUB2130
1435 INPUT"SPEED (0-9)";J:IFA(271)=0THENPRINTTAB(3);" ";
1440 IF(J<0)+(J>9)THEN1435
1445 IFJ=0THENRETURN
1450 GOSUB1705:A(X*8+Y+192)=0
1455 FORI=1TOJ:X=X+A(282):Y=Y+A(283)
1460 GOSUB2120:GOSUB1630
1465 B$="ENERGY BARRIER"
1470 IFP=-1THENE=RND(E):GOTO1510
1475 IF(B<>A(256))+(C<>A(257))THEN1525
1480 W=A(INT(X)*8+INT(Y)+192):B$="   COLLISION"
1485 IFW<>20THEN1505
1490 IFJ-I>1THENS=S-100:GOTO1510
1495 IFA(A(256)*8+A(257)+A(258)*64)=1006THEN265
1500 A(276)=3:E=4000:GOTO1520
1505 IFW=0THEN1525
1510 IFJ-I>1THENGOSUB355:GOSUB555:W=0:GOTO1520
1515 IF(W<>2)*(W<>3)*(W<>19)THENGOSUB355:GOSUB555
1520 X=X-A(282):Y=Y-A(283):GOSUB2120:GOTO1530
1525 NEXTI
1530 X=INT(X):Y=INT(Y):A(X*8+Y+192)=18
1535 IF(B<>A(256))+(C<>A(257))THENGOSUB1700:GOTO955
1540 GOSUB1700:Q=1:GOSUB2050
1545 IF(W=2)+(W=3)+(W=19)THEN1560
1550 IFE=4000THENPRINT"        DOCKED":PRINTTAB(3);" ";:GOSUB1680:T=T+1:J=2:GOTO520
1555 GOTO1680
1560 IFW=2THENGOSUB1705:GOSUB1625:A(P)=INT(ABS(A(P))/10)*10+9
1565 PRINTTAB(12);"ORBIT":PRINTTAB(3);" ";:GOTO1680
1570 CLS1:REM 32
1575 PRINT@FF*266,"STATUS REPORT:":PRINT"     --------------"
1580 PRINT"     DATE:";T
1585 PRINT"     ENERGY:";E
1590 B$="GREEN":IFA(261)=2THENB$="YELLOW"
1595 IFA(261)=3THENB$="RED"
1600 PRINT"     CONDITION: ";B$
1605 PRINT"     QUADRANT:";A(256);A(257);A(258)
1610 PRINT"     SECTOR:";A(259);A(260)
1615 PRINT"     TORPEDOES:";A(276)
1620 GOTO1675
1625 IF(D<0)+(D>2)THENP=-1:RETURN
1630 IF(B<0)+(B>7)+(C<0)+(C>7)THENP=-1:RETURN
1635 P=8*B+C+64*D:RETURN
1640 D=INT(P/64):B=INT((P-D*64)/8):C=P-D*64-B*8:RETURN
1645 A(P)=A(262)*1E4+A(263)*1E3+A(264)*1E2+A(265)*10+A(266)
1650 RETURN
1655 A(266)=ABS(A(P)):K=1E4
1660 FORN=0TO3
1665 A(262+N)=INT(A(266)/K):A(266)=A(266)-A(262+N)*K:K=K/10
1670 NEXTN:RETURN
1675 PRINT
1680 IFQ=1THENQ=0:RETURN
1685 PRINT"HIT (ENTER) TO CONTINUE.";:GOSUB2600
1690 IFINKEY$<>CHR$(13)THEN1690
1695 CLS1:RETURN
1700 A(256)=B:A(257)=C:A(258)=D:A(259)=X:A(260)=Y:RETURN
1705 B=A(256):C=A(257):D=A(258):X=A(259):Y=A(260):RETURN
1710 CLS1:PRINT@128,"COORDINATES:";B;C;D
1715 PRINT"WARSHIPS:";A(262):PRINT"BASES:";A(263)
1720 PRINT"STARS:";A(264):PRINT"PLANETS:";A(265)
1725 RESTORE:IF(A(266)>6)+(A(266)<2)THENB$="NONE":GOTO1735
1730 READB$,J:IFJ-30<>A(266)THEN1730
1735 PRINT"ASTRONOMICAL FEATURE: ":PRINTB$:RESTORE
1740 IFA(266)<7THENB$="NONE":GOTO1750
1745 READB$,J:IFJ-30<>A(266)THEN1745
1750 PRINT"SCIENTIFIC INTEREST: ":PRINTB$:PRINT:PRINT
1755 GOTO1680
1760 PRINT@32,""
1765 FORC=V-1TOV+1:FORD=W-1TOW+1:FORB=U-1TOU+1
1770 GOSUB1625
1775 IFP=-1THENPRINT" UNKNOWN ";:GOTO1835
1780 IFABS(R=0)*(ABS(A(P))-ABS(INT(ABS(A(P))/10)*10)<>5)THENA(P)=ABS(A(P))
1785 IFABS(R=1)*(A(P))<0THENPRINT" NO DATA ";:GOTO1835
1790 IF(R=1)*(A(P)=5)THEN1800
1795 IFABS(A(P))<>1THENGOSUB1655:GOTO1805
1800 PRINT"  VOID   ";:GOTO1835
1805 IFA(266)=2THENPRINT" BL HOLE ";:GOTO1835
1810 IFA(266)=3THENPRINT" O STAR  ";:GOTO1835
1815 IFA(266)=4THENPRINT" PULSAR  ";:GOTO1835
1820 IF(R=0)*(A(266)=5)THENPRINT"  NOISE  ";:GOTO1835
1825 IFA(266)=6THENPRINT"   HQ    ";:GOTO1835
1830 PRINT" W";RIGHT$(STR$(A(262)),1);"B";RIGHT$(STR$(A(263)),1);"S";RIGHT$(STR$(A(264)),1);"P";RIGHT$(STR$(A(265)),1);
1835 NEXTB:PRINT" ";D:NEXTD:PRINT:NEXTC
1840 PRINT@3,U-1;:PRINT@12,U;:PRINT@21,U+1;
1845 PRINT@FF*253,V-1;:PRINT@FF*509,V;:PRINT@FF*765,V+1;
1850 PRINT@FF*979,"";:I=191
1855 FORZ=0TO27:RESET(0,Z):RESET(18,Z):RESET(36,Z):RESET(54,Z):NEXT
1875 FORZ=0TO63:RESET(Z,2):RESET(Z,10):RESET(Z,18):RESET(Z,26):NEXT
1885 PRINT@484,"";:GOTO1680
1890 I=0:B$="MR. FIBBLE"
1895 IFA(P)=2THENI=4:B$="CLASS M PLANET"
1900 IFA(P)=3THENI=4:B$="CLASS G PLANET"
1905 IFA(P)=18THENI=5:B$="STAR SHIP"
1910 IFA(P)=19THENI=3:B$="CLASS F STAR"
1915 IFA(P)=20THENI=2:B$="FLEET BASE"
1920 IFA(P)>24THENI=1:B$="WARSHIP"
1925 RETURN
1930 GOSUB1890
1935 ONI+1GOTO1940,2005,1995,1975,1960,1945
1940 RETURN
1945 UU=INT(U*.5):VV=INT(V*.66):FORZ=UU-6TOUU-2:IFZ>=0THENRESET(Z,VV-1)
1946 NEXTZ
1950 FORZ=UU-3TOUU+2:RESET(Z,VV+1):NEXTZ
1955 FORZ=UUTOUU+4:RESET(Z,VV):NEXTZ:RESET(UU-3,VV):RETURN
1960 UU=INT(U*.5):VV=INT(V*.66):FORZ=UU-1TOUU+2:RESET(Z,VV-1):RESET(Z,VV+1):NEXTZ
1965 FORZ=UU-2TOUU+3:RESET(Z,VV):NEXTZ
1970 RETURN
1975 UU=INT(U*.5):VV=INT(V*.66):FORZ=UU-2TOUU+1:RESET(Z,VV):NEXTZ
1980 FORZ=VV-1TOVV+1STEP2
1985 RESET(UU-2,Z):RESET(UU+1,Z):RESET(UU-3,Z):RESET(UU+2,Z)
1990 NEXTZ:RETURN
1995 UU=INT(U*.5):VV=INT(V*.66):FORZ=UU-4TOUU+4:IFZ>=0THENRESET(Z,VV+1)
1996 NEXTZ
2000 RESET(UU,VV):RESET(UU-2,VV-1):RESET(UU-1,VV-1):RESET(UU,VV-1):RETURN
2005 UU=INT(U*.5):VV=INT(V*.66):FORZ=UU-4TOUU+4:IFZ>=0THENRESET(Z,VV)
2006 NEXTZ:IFUU-4>=0THENRESET(UU-4,VV-1)
2007 IFUU-3>=0THEN:RESET(UU-3,VV-1)
2008 RESET(UU+3,VV-1):RESET(UU+4,VV-1)
2015 RESET(UU-1,VV+1):RESET(UU,VV+1):RESET(UU+1,VV+1):RETURN
2020 B=RND(8)-1:C=RND(8)-1:D=3
2025 FORU=B-1TOB+1:FORV=C-1TOC+1
2030 IF(U<0)+(U>7)+(V<0)+(V>7)THEN2040
2035 P=8*U+V+192:IFA(P)<>0THEN2020
2040 NEXTV:NEXTU
2045 GOSUB1630:A(P)=M:RETURN
2050 CLS1:GOSUB2600:IFA(271)<>0THENI=5:GOTO505
2055 GOSUB1705:GOSUB1625:A(P)=ABS(A(P))
2060 FORC=0TO7:FORB=0TO7:P=B*8+C+192:IFA(P)<>0THENU=B*16+7:V=3*C+4:GOSUB1930
2061 NEXT:NEXT
2063 PRINT@32,"";:FORC=0TO7:FORB=0TO7:ZZ=(INT((B*16+7)*.5)/2)+((INT((3*C+4)*.66)/2)*32)
2066 ON1-(PEEK(MC+ZZ)=143)GOTO2067:PRINT@ZZ,D$;:NEXT:GOTO2075
2067 NEXT
2075 IFINKEY$=CHR$(13)THEN2090
2076 NEXTC:GOSUB2095
2080 PRINT@324,"SHORT RANGE SENSOR SCAN":PRINT"       OF QUADRANT";
2085 PRINTA(256);A(257);A(258):PRINTTAB(3);" ";:GOTO1680
2090 CLS1:PRINTTAB(3);"";:FORZZ=1TO2500:NEXT:RETURN
2095 PRINT@FF*576,"";:GOSUB2101
2100 RETURN
2101 FORF=1TO32:PRINTCHR$(140);:NEXT:RETURN
2105 GOSUB2600:FORN=0TO80:FORZZ=1TO50:NEXT
2110 IFINKEY$=CHR$(13)THEN2115
2111 NEXTN:RETURN
2115 CLS1:FORN=0TO0:NEXTN:RETURN
2120 X=B*8+X:B=INT(X/8):X=X-B*8:Y=C*8+Y:C=INT(Y/8):Y=Y-C*8
2125 RETURN
2130 IFABS(A(282))>ABS(A(283))THENA(283)=A(283)/ABS(A(282)):A(282)=A(282)/ABS(A(282)):GOTO2140
2135 A(282)=A(282)/ABS(A(283)):A(283)=A(283)/ABS(A(283))
2140 A(282)=A(282)*1000+.5*ABS((A(282)>0)-ABS(A(282)<0)):IFA(282)<0THEN2142
2141 A(282)=INT(A(282))/1000:GOTO2145
2142 A(282)=-INT(-A(282))/1000
2145 A(283)=A(283)*1000+.5*ABS((A(283)>0)-ABS(A(283)<0)):IFA(283)<0THEN2147
2146 A(283)=INT(A(283))/1000:GOTO2148
2147 A(283)=-INT(-A(283))/1000
2148 RETURN
2150 B$="LARGE BLACK HOLE":IFA(266)=3THENB$="CLASS O STAR"
2160 IFA(266)=4THENB$="PULSAR"
2165 GOSUB2193:PRINT"AFTER FLYING INTO A ":PRINTB$
2170 PRINT"THE SHIP WAS LOST TO SPACE."
2180 PRINT:PRINT:END
2185 GOSUB2193:PRINT"AFTER DEPLETING ITS ENERGY":GOTO2170
2190 GOSUB2193:PRINT"WHILE IN BATTLE WITH THE ENEMY":GOTO2170
2193 CLS1:PRINT@FF*320,"ON DATE";T:RETURN
2195 CLS1
2200 PRINT@FF*448,"DESTRUCTION OF A ";B$:PRINT"IS GROUNDS FOR COURT-MARTIAL.
2205 PRINT:PRINT"YOU ARE RELIEVED OF YOUR":PRINT"COMMAND." 
2210 PRINT:PRINT:END
2215 DATA "STATUS",0,"DAMAGE CONTROL",1,"SCIENCE STATION",2
2220 DATA "SHIP'S COMPUTER",3,"LR SENSORS",4,"SR SENSORS",5
2225 DATA "IMPULSE",6,"WARP",7,"BEAM WEAPON",8
2230 DATA "TORPEDOES",9,"ALERT",10,"REPAIR",11
2235 DATA "WARSHIPS",50,"FLEET BASES",51
2240 DATA "CLASS F STARS",52,"PLANETS",53
2245 DATA "UNEXPLORED AREAS",54
2250 DATA "     GREEN",21," YELLOW ALERT",22,"   RED ALERT",23
2255 DATA "BLACK HOLE",32,"O STAR",33,"PULSAR",34
2260 DATA "SPACE NOISE",35,"FLEET HQ",36,"G PLANET",37
2265 DATA "UNEXPLORED M",38,"EXPLORED M",39
2270 DATA "PROMOTED",425,"DECORATED",350,"REASSIGNED",290
2275 DATA "DEMOTED",190,"DISCHARGED",-1,"EXECUTED",-32000
2280 DATA "E",999,30111,31111,30101,20121,20201,20001
2285 DATA 10001,10111,10211,10321,10221,-99
2290 DATA 1110,110,110,110,110
2295 DATA 1111,1111,1211,1121,1121
2300 DATA 3,3,2,2
2305 DATA 121,121,211,211,311,311,321,321,221,221
2310 DATA 121,121,211,211,311,311,321,321,221,221,0
2400 F=I:GOSUB2410:A(283)=A(282):F=I+6:IFF>8THENF=F-8
2410 A=F:IFF>4THENF=F-4
2420 IFF>2THENF=4-F
2430 F=F*0.785398:A(282)=F-F*F*F/6+F*F*F*F*F/120+F*F*F*F*F*F*F/5040
2440 IFA<4THENA(282)=-A(282)
2450 RETURN
2500 CLS1:INPUT"0 TO SAVE, 1 TO LOAD";N:PRINT:REW.
2510 ONN+1GOTO2520,2540:GOTO135
2520 A$=",":PRINT#E;A$;H;A$;T;A$;S:FORN=0TO279
2525 PRINT#A(N):NEXTN:PRINT"COPY DATA FROM CASSETTE WINDOW TO A SAFE PLACE":GOSUB1685:GOTO135
2540 PRINT"PASTE SAVED DATA INTO THE <F3> CASSETTE WINDOW":GOSUB1685
2550 INPUT#E,H,T,S:FORN=0TO279:INPUT#A(N):NEXTN:GOTO135
2600 IFINKEY$<>""THEN2600
2610 RETURN
3000 CLS:PRINT"MISSION GOALS:"
3010 PRINT"1. Map out the region. IT IS"
3020 PRINT"divided into 8x8x3 Quadrants"
3030 PRINT"(co-ordinates 0,0,0 - 7,7,2). "
3040 PRINT"Use the Ship's Computer to "
3050 PRINT"identify areas not yet mapped."

3060 PRINT"2. Locate and orbit 5 Class M"
3070 PRINT"planets. To orbit a planet, use"
3080 PRINT"Impulse to manoeuvre into the"
3090 PRINT"same Sector as the planet.

3100 PRINT"3. Find and destroy the 20 "
3110 PRINT"enemy Warships"
3120 PRINT"4. Complete mission by date"
3130 PRINT"2500 and return to and dock"
3140 PRINT"with HQ in Quadrant 7,7,2."
3150 RETURN
