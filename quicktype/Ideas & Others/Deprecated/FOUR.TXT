10 REM COPYRIGHT (C) T&D SOFTWARE 1985
20 REM *********************
30 REM * FOUR IN A ROW     *
40 REM *   BY:             *
50 REM * HAROLD SNYDER     *
60 REM * P.O. BOX 3330     *
70 REM * CHEYENNE,  WY     *
80 REM *            82003  *
90 REM *********************
100 MC=15360
110 CLEAR500:CLS(0):PRINT@192,"    **** FOUR IN A ROW ****":PRINT@256,"        BY HAROLD SNYDER":FORI=1TO999:NEXTI:C$="                               ":D$=CHR$(128)+CHR$(128)
120 E$="":FORZZ=1TO13:E$=E$+CHR$(128):NEXT
121 X$=CHR$(156):O$=CHR$(188):H$=CHR$(140):DIMB$(8,9),L(8),S(49),F(4),V(16),N(4)
130 DATA1,100,500,1E20,1,800,4000,1E20
140 DATA1,75,900,1E18,1,450,3000,1E18
150 FORZ1=1TO16:READV(Z1):NEXTZ1:CLS:PRINT:PRINT"  DO YOU WANT INSTRUCTIONS Y/N"
160 A$=INKEY$:IFA$="N"THEN220
170 IFA$="Y"THEN190
180 GOTO160
190 CLS:PRINT:PRINT:PRINT"THIS GAME CONSISTS OF STACKING  MARKERS(THE COMPUTER PLAYS RED) ON THE BOARD UNTIL ONE OF THE"
191 PRINT"PLAYERS GETS FOUR IN A ROW.     THIS CAN BE DONE HORIZONTALLY,  VERTICALLY OR DIAGONALLY."
200 PRINT:PRINT:PRINT:PRINT"      HIT ANY KEY TO START"
210 A$=INKEY$:IFA$=""THEN210
220 CLS:FORI=1TO8:FORJ=1TO8:B$(I,J)=H$:NEXTJ:NEXTI:FORZ1=1TO8:L(Z1)=0:NEXTZ1:PRINT:PRINT"  DO YOU WANT TO GO FIRST Y/N"
230 A$=INKEY$:IFA$="N"THENM9=RND(2)+4:GOTO520
240 IFA$<>"Y"THEN230
250 CLS:GOSUB260:GOTO280
260 PRINT@0,"";:PRINT:PRINT:PRINTTAB(3):PRINTE$;E$:FORI=8TO1STEP-1:PRINTTAB(3):FORJ=1TO9:PRINTD$;B$(I,J);:NEXTJ:PRINT:NEXTI
261 PRINT"   ";:FORI=1TO2:PRINTE$;:SOUNDI*75+50,1:NEXTI
262 POKEMC+1379,32:POKEMC+1380,32:POKEMC+1381,49:POKEMC+1382,32:POKEMC+1383,32:POKEMC+1384,50:REM MISSING PO
270 POKEMC+1388,32:POKEMC+1389,32:POKEMC+1390,52:POKEMC+1391,32:POKEMC+1392,32:POKEMC+1393,53:POKEMC+1394,32
271 POKEMC+1395,32:POKEMC+1396,54:POKEMC+1397,32:POKEMC+1398,32:POKEMC+1399,55:POKEMC+1400,32:POKEMC+1401,32
272 POKEMC+1402,56:POKEMC+1403,32:POKEMC+1404,32:PRINT:RETURN
280 PRINT@416,"        ** YOUR MOVE **"
290 M$=INKEY$:IFM$=""THEN290
300 SOUND200,1:M=VAL(M$):IFM<1ORM>8THENPRINT@416,"     ILLEGAL MOVE,TRY AGAIN":PRINT@448,C$:SOUND40,2:GOTO290
310 L=L(M):IFL>7THENPRINT@416,"     ILLEGAL MOVE,TRY AGAIN":PRINT@448,C$:SOUND40,2:GOTO290
320 L(M)=L+1:L=L+1:B$(L,M)=X$:GOSUB260:PRINT@416,"         ** THINKING **":PRINT@448,C$:P$=X$:GOSUB560:FORZ=1TO4
321 IFS(Z)<4THEN340
330 PRINT@416,"         ** YOU WIN **":PRINT@448,C$:GOTO680
340 NEXTZ:M9=0:V1=0:N1=1:FORM4=1TO8:L=L(M4)+1:IFL>8THEN500
350 V=1:P$=O$:W=0:M=M4
360 GOSUB560:FORZ1=1TO4:N(Z1)=0:NEXTZ1:FORZ=1TO4:S=S(Z):IFS-W>3THEN530
370 T=S+F(Z):IFT<4THEN390
380 V=V+4:N(S)=N(S)+1
390 NEXTZ:FORI=1TO4:N=N(I)-1:IFN=-1THEN410
400 I1=8*W+(4*SGN(N)+I):V=V+V(I1)+(N*V(8*W+1))
410 NEXTI:IFW=1THEN430
420 W=1:P$=X$:GOTO360
430 L=L+1:IFL>8THEN460
440 GOSUB560:FORZ=1TO4:IFS(Z)>3THENV=2
450 NEXTZ
460 IFV<V1 THEN500
470 IFV>V1 THENN1=1:GOTO490
480 N1=N1+1
490 V1=V:M9=M4
500 NEXTM4:IFM<>0THEN520
510 PRINT@416,"         ** TIE GAME **":GOTO680
520 M=M9
530 PRINT@448,"         MY MOVE WAS";M:L=L(M)+1:L(M)=L(M)+1:B$(L,M)=O$:P$=O$:GOSUB260:GOSUB560:FORZ=1TO4:IFS(Z)<4THEN550
540 PRINT@416,"      ** COMPUTER WINS **":GOTO680
550 NEXTZ:GOTO280
560 Q$=X$:IFP$=X$ THENQ$=O$
570 D2=1:D1=0:Z=0:GOSUB580:D1=1:D2=1:GOSUB580:D2=0:D1=1:GOSUB580:D2=-1:D1=1:GOSUB580:RETURN
580 D=1:S=1:T=0:Z=Z+1
590 C=0:FORK=1TO3:M5=M+(K*D1):L1=L+(K*D2):IFM5<1ORL1<1ORM5>8ORL1>8THEN650
600 B$=B$(L1,M5):IFC=0THEN630
610 IFB$=Q$ THENK=3:GOTO650
620 T=T+1:GOTO650
630 IFB$=P$ THENS=S+1:GOTO650
640 C=1:GOTO610
650 NEXTK:IFD=0THEN670
660 D=0:D1=-D1:D2=-D2:GOTO590
670 S(Z)=S:F(Z)=T:RETURN
680 END
