0 DIM D(31,14),A(255),P(33,5,1),MW(5),RW(1),RA(1),AR(3),HL(1),SH(1),X,Y,A,B,C,R
1 DIM CL$(7),MW$(5),RW$(1),RA$(1),AR$(3),HL$(1),SH$(1),S$(11),I$:GOSUB6000
2 CLS0:GOSUB1000:GOTO900
5 PRINT@480,"door                           ";:POKE16895,96:RETURN
10 PRINT@480,"object                         ";:POKE16895,96:RETURN
15 PRINT@480,"monster                        ";:POKE16895,96:RETURN
20 PRINT@480,"treasure                       ";:POKE16895,96:RETURN
30 CLS0:FORB=0TO14:FORA=0TO31:PRINT@A+(32*B),CHR$(D(A,B));:NEXT:NEXT
32 PRINT@480,"SeARCH tRAP MAgIC bOW dOOR mENU";:POKE16895,96:RETURN
34 PRINT"MO";P(19,N,0);CHR$(P(1,N,0));CHR$(P(2,N,0));CHR$(P(3,N,0));CHR$(P(4,N,0));CHR$(P(5,N,0));CHR$(P(6,N,0));
36 PRINT"HP";P(13,N,0);"AC:";P(15,N,0);"(Y/N)";:POKE16895,96:RETURN
50 PRINT@X+(32*Y),"P";:PRINT@X-1+(32*Y),CHR$(D(X-1,Y));
52 PRINT@X-1+(32*(Y-1)),CHR$(D(X-1,Y-1));:PRINT@X+(32*(Y-1)),CHR$(D(X,Y-1));
54 PRINT@X+1+(32*(Y-1)),CHR$(D(X+1,Y-1));:PRINT@X+1+(32*Y),CHR$(D(X+1,Y));
56 PRINT@X+1+(32*(Y+1)),CHR$(D(X+1,Y+1));:PRINT@X+(32*(Y+1)),CHR$(D(X,Y+1));
58 PRINT@X-1+(32*(Y+1)),CHR$(D(X-1,Y+1));:RETURN
200 IFD(X-1,Y)=0THEND(X-1,Y)=32:IFRND(2)=1THEND(X-1,Y)=255:IFRND(10)=1THEN D(X-1,Y)=100
210 IFD(X+1,Y)=0THEND(X+1,Y)=32:IFRND(2)=1THEND(X+1,Y)=255:IFRND(10)=1THEN D(X+1,Y)=100
220 IFD(X,Y-1)=0THEND(X,Y-1)=32:IFRND(2)=1THEND(X,Y-1)=255:IFRND(10)=1THEN D(X,Y-1)=100
230 IFD(X,Y+1)=0THEND(X,Y+1)=32:IFRND(2)=1THEND(X,Y+1)=255:IFRND(10)=1THEN D(X,Y+1)=100
240 RETURN
250 IFD(X-1,Y)=255THEND(X-1,Y)=191:IFRND(10)=1THEND(X-1,Y)=32
260 IFD(X+1,Y)=255THEND(X+1,Y)=191:IFRND(10)=1THEND(X+1,Y)=32
270 IFD(X,Y-1)=255THEND(X,Y-1)=191:IFRND(10)=1THEND(X,Y-1)=32
280 IFD(X,Y+1)=255THEND(X,Y+1)=191:IFRND(10)=1THEND(X,Y+1)=32
285 RETURN
350 IFD(X-1,Y)=100THEND(X-1,Y)=68:IFRND(6)>2THEND(X-1,Y)=100
360 IFD(X+1,Y)=100THEND(X+1,Y)=68:IFRND(6)>21THEND(X+1,Y)=100
370 IFD(X,Y-1)=100THEND(X,Y-1)=68:IFRND(6)>2THEND(X,Y-1)=100
380 IFD(X,Y+1)=100THEND(X,Y+1)=68:IFRND(6)>2THEND(X,Y+1)=100
385 RETURN
900 X=15:Y=13:D(X,Y)=32:PRINT@32+X+(32*Y)," ";:D(X,Y+1)=32:C=0
905 GOSUB200:GOSUB50:ONA(C) GOSUB5,10,15,20
910 I$=INKEY$:IFI$=""THEN910
920 IFI$="W"THENC=D(X,Y-1):IFC>31ANDC<96THENY=Y-1:GOTO980
930 IFI$="Z"THENC=D(X,Y+1):IFC>31ANDC<96THENY=Y+1:GOTO980
935 IFI$="S"THENC=D(X+1,Y):IFC>31ANDC<96THENX=X+1:GOTO980
940 IFI$="A"THENC=D(X-1,Y):IFC>31ANDC<96THENX=X-1:GOTO980
945 IFI$="E"THENGOSUB250:GOTO980
950 IFI$="G"THENGOSUB30:GOTO980
952 IFI$="M"THENGOSUB32:GOTO980
954 IFI$="D"THENGOSUB350:GOTO980
980 IFY=14THEN2000
990 GOTO905
1000 FORX=0TO31:D(X,0)=191:D(X,14)=191:NEXT:FORY=0TO14:D(0,Y)=191:D(31,Y)=191:NEXT:REM GOSUB30
1005 R=RND(3)+4:A=RND(31-R):B=RND(14-R):R=R-1:FORX=ATOA+R:FORY=BTOB+R:D(X,Y)=32:NEXT:NEXT
1008 FORX=ATOA+R:D(X,B)=255:D(X,B+R)=255:NEXT
1009 FORY=BTOB+R:D(A,Y)=255:D(A+R,Y)=255:NEXT
1010 Y=B+1+RND(R-2):X=A:D(X,Y)=100:X=A+R:D(X,Y)=100:X=A+1+RND(R-2):Y=B:D(X,Y)=100:Y=B+R:D(X,Y)=100
1011 X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=73:X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=77:X=A+1+RND(R-2):Y=B+1+RND(R-2):D(X,Y)=36
1015 A(68)=1:A(73)=2:A(77)=3:A(36)=4
1030 GOSUB 30:RETURN
1100 PRINT"X=TRAP D=DOOR P=PARTY M=MONSTER"
1120 PRINT
2000 PRINT@X+(32*Y)-32," ";:PRINT@X+(32*Y),"P";:SOUND 1,1
2010 PRINT@480,"TRY ANOTHER DUNGEON (Y/N)?     ";:POKE16895,96
2020 I$=INKEY$:IFI$=""THEN2020
2030 IFI$="Y"THEN RUN
2040 IFI$="N"THEN END
2050 GOTO2020
6000 FORT=1TO7:READCL$(T):NEXT:CL$(0)="NONE"
6010 FORT=1TO5:READMW$(T),MW(T),DA(T):NEXT:MW$(0)="NONE"
6015 RW$(1)="BOW":RW(1)=25:RW$(0)="NONE":RW=25
6017 RA$(1)="ARROWS":RA(1)=.5:RA$(0)="NONE":RA(0)=6
6020 FORT=1TO3:READAR$(T),AR(T):NEXT:AR$(0)="NONE"
6025 HL$(1)="HELMET":HL(1)=10:HL$(0)="NONE"
6030 SH$(1)="SHIELD":SH(1)=10:SH$(0)="NONE"
6040 FORT=1TO11:READS$(T):NEXT:S$(0)=""
6050 FORN=1TO5
6100 FORT=1TO6:P(T,N,0)=32:NEXT:P(21,N,0)=0
6110 FORT=7TO12:P(T,N,0)=RND(6)+RND(6)+RND(6):NEXT
6120 H=P(7,N,0):IF P(7,N,0)>12THENP(21,N,0)=1
6122 IFP(8,N,0)>8ANDP(8,N,0)>HTHENP(21,N,0)=2:H=P(8,N,0)
6124 IFP(9,N,0)>8ANDP(9,N,0)>HTHENP(21,N,0)=3:H=P(9,N,0)
6126 IFP(10,N,0)>8ANDP(10,N,0)>HTHENP(21,N,0)=4:H=P(10,N,0)
6128 IFP(11,N,0)>8ANDP(7,N,0)>8ANDP(11,N,0)>H THEN P(21,N,0)=5:H=P(11,N,0)
6130 IFP(12,N,0)>8ANDP(7,N,0)>8ANDP(8,N,0)>8 AND P(12,N,0)>H THEN P(21,N,0)=6:H=P(12,N,0)
6132 IFP(11,N,0)>12ANDP(10,N,0)>12 THEN P(21,N,0)=7
6140 IFP(21,N,0)=0THENP(21,N,0)=RND(7)
6900 GOSUB7000
6910 PRINT "ROLL AGAIN (Y/N)?"
6920 I$=INKEY$:IFI$=""THEN6920
6930 IFI$="Y"THEN 6100
6940 IFI$="N"THEN 6980
6945 GOTO6920
6980 NEXT
6990 RETURN
7000 CLS
7004 PRINT"NM: ";CHR$(P(1,N,0));CHR$(P(2,N,0));CHR$(P(3,N,0));CHR$(P(4,N,0));CHR$(P(5,N,0));CHR$(P(6,N,0)),
7006 PRINT" CL: ";CL$(P(21,N,0))
7008 PRINT"ST:";P(7,N,0);P(7,N,1)," MW: ";MW$(P(22,N,0));P(22,N,1)
7012 PRINT"IN:";P(8,N,0);P(8,N,1)," RW: ";RW$(P(23,N,0));P(23,N,1)
7014 PRINT"WI:";P(9,N,0);P(9,N,1)," RA: ";RA$(P(24,N,0));P(24,N,1)
7016 PRINT"DX:";P(10,N,0);P(10,N,1)," AR: ";AR$(P(25,N,0));P(25,N,1)  
7018 PRINT"CO:";P(11,N,0);P(11,N,1)," HL: ";HL$(P(26,N,0));P(26,N,1)
7020 PRINT"CA:";P(12,N,0);P(12,N,1)," SH: ";SH$(P(27,N,0));P(27,N,1)
7022 PRINT"HP:";P(13,N,0);P(13,N,1)," M1: ";S$(P(28,N,0));P(28,N,1)
7024 PRINT"AC:";P(15,N,0);P(16,N,1)," M2: ";S$(P(29,N,0));P(29,N,1)  
7026 PRINT"LV:";P(17,N,0);P(17,N,1)," M3: ";S$(P(30,N,0));P(30,N,1) 
7028 PRINT"EX:";P(18,N,0);P(18,N,1)," S1: ";S$(P(31,N,0))
7030 PRINT"MO:";P(19,N,0);P(19,N,1)," S2: ";S$(P(32,N,0))
7032 PRINT"$$:";P(20,N,0);P(20,N,1)," S3: ";S$(P(33,N,0)) 
7090 RETURN
7800 DATA "FIGHTER","MAGIC USER","CLERIC","THIEF","DWARF","ELF","HALFLING"
7850 DATA "DAGGER",3,4
7851 DATA "MACE",5,6,
7852 DATA "STAFF",3,4
7854 DATA "AXE",7,8
7856 DATA "SWORD",15,8
7860 DATA "LEATHER",15
7862 DATA "CHAIN MAIL",30
7864 DATA "PLATE",50
7900 DATA "CURE WOUNDS"
7902 DATA "OPEN PORTAL"
7904 DATA "HOLD PORTAL"
7906 DATA "MISSILE"
7908 DATA "BLAST"
7910 DATA "TURN UNDEAD"
7912 DATA "PETRIFY"
7914 DATA "REVEAL"
7916 DATA "SHOW TRAPS"
7918 DATA "WEB"
7920 DATA "SLEEP"
8000 DATA "KOBOLDS","GRUNT"
8002 DATA "GOBLINS","ARGH"
8004 DATA "BANDITS","TALKING"
8006 DATA "ORCS","ARRRR"
8008 DATA "SKELETONS","CLICK CLACK"
8010 DATA "BERSERKERS","SHOUTING"
8012 DATA "STIRGES","BUZZING"
8014 DATA "ZOMBIES","BRAINS!"
8016 DATA "DWARVES","TALKING"
8018 DATA "ELVES","TALKING"
8020 DATA "GELATINOUS CUBE","BLOOP BLOOP"
