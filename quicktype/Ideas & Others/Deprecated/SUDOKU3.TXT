1 CLS:DIM CAN$,CN$,TMP$,N1$
2 DIM SU(80),A,B,K,IND,BOX,INX,TR,Z,O
3 DIM ROW
4 DIM ARR(9)
5 DIM I,J,LST,LES,SYC,CAC,R1
10 Z=0:O=1:TR=-1:GOTO1000
100 CN$="":IND=I
120 ROW=INT(IND/9)
130 COL=IND-(ROW*9)
140 FORB=1TO9
150 ARR(B)=Z
170 FORA=(ROW*9)TO((ROW*9)+8):IFSU(A)=BTHENARR(B)=TR
190 NEXT
210 FORA=COLTO(72+COL)STEP9:IFSU(A)=BTHENARR(B)=TR
230 NEXT
240 NEXT
260 BOX=(INT(IND/27)*27)+(INT(COL/3)*3)
270 FORA=0TO2
280 FORB=0TO2
290 INX=BOX+(A*9)+B
300 FORK=1TO9:IFSU(INX)=KTHENARR(K)=TR
320 NEXT
330 NEXT
340 NEXT
360 FORA=1TO9:IFARR(A)THEN380
370 CN$=CN$+RIGHT$(STR$(A),1)
380 NEXT
390 RETURN
1000 FORC=0TO80:SU(C)=0:NEXT:SYC=0
1010 FOR J=1 TO 81
1020 LES=10
1030 FOR I=0 TO 80
1040 IFSU(I)THEN1095
1050 GOSUB100
1060 TMP$=CN$
1070 CAC=LEN(TMP$)
1080 IF CAC=0 THEN RUN
1090 IF CAC<LES THEN LES=CAC:CAN$=TMP$:LST=I
1095 NEXT
1100 IF LEN(CAN$)=1 THEN SU(LST)=VAL(CAN$):GOTO2000
1110 R1=RND(LES):N1$=MID$(CAN$,R1,1):SU(LST)=VAL(N1$)
2000 GOSUB3500:PRINT@480,CAN$;"        ";:NEXT
2010 REM CHECK THE NUMBER OF SYMBOLS FOUND SO FAR ...
2020 FOR I=0 TO 80
2030 IF SU(I)>0 THEN SYC=SYC+1
2040 NEXT:PRINT"SYMBOL COUNT=";SYC
3000 IF SYC<>81 THEN 1000
3010 REM FINISHED
3020 GOSUB3500:END
3500 PRINT@0,"";:C=0:FORY=1TO9:FORX=1TO9:PRINTSU(C);:C=C+1:NEXT:PRINT:NEXT
3510 RETURN
4000 REM -----------------------------------------
4010 REM NOW WE SHOULD HAVE A SOLVED SUDOKU STORED
4020 REM IN AN ARRAY. REPLACE THE FOLLOWING
4030 REM CODE DEPENDING ON HOW YOU WILL IMPLEMENT
4040 REM YOUR GAME GUI. IN THIS DEMO WE WILL
4050 REM FILL THE SUDOKU GRID (TEXTBOX CONTROL ARRAY)
4060 REM WITH SYMBOLS ON FORM1
4070 REM FOR I = 0 TO 80
4080 REM    FORM1.TXTSYMBOL(I) = SU(I)
4090 REM NEXT
5000 REM ------------------------------------------
