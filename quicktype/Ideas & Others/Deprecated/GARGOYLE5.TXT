1 CLEAR200
2 POKE 49151,64:POKE17026,118:CLS
3 PRINT@136,CHR$(34);"GARGOYLE CASTLE";CHR$(34)
4 PRINT@266,"KIT DOMENICO":PRINT@325,"MODIFIED BY JIM GERRIE"
5 GOTO105
6 P=0:FORX=1TOLEN(I$):PRINTMID$(I$,X,1);:P=P+1:IFP>24ANDMID$(I$,X,1)=" "THENQ=P:P=0:IFQ<32THENPRINT
8 NEXT:PRINT:I$="":RETURN
105 READ IT:REM THIS IS THE NUMBER OF ITEMS ON THE NOUN LIST
108 DIM NA$(IT),NM$(IT),LO(IT),Q,X,P,J,C,I,PL,PO,IT,I$,A$,V$
110 FOR I=1 TO IT:READ NA$(I),NM$(I),LO(I):NEXT I
120 PL=6:PO=1
160 READ LO
170 DIM CO(LO,6),LI(LO),D$(LO)
180 FOR I=0 TO LO
190 READ D$(I)
200 FOR J=1 TO 6
210 READ CO(I,J)
220 NEXT J
225 READ LI(I)
230 NEXT I
460 LC=5
480 SS$=NM$(4)
500 REM START OF MAIN LOOP
504 CLS
505 IF LI(LC)=0 AND L=0 THENI$="IT'S DARK IN HERE!":GOTO560
510 CLS:I$="I AM IN "+D$(LC):GOSUB6
520 IFLO(32)=99ANDLC=28THENI$="MY ARMOUR PULLS ME STRAIGHT TO THE BOTTOM, AND I'M DROWNED.":GOSUB6:LC=0:GOTO5300
525 VI=0
530 FOR I=1 TO IT
540 IF LO(I)<>LC THEN 545
541 I$="I CAN SEE "+NM$(I)+NA$(I)+".":GOSUB6
544 VI=VI+1
545 NEXT I
550 IF VI=0 THEN I$="I CAN SEE THAT THERE ARE NO ITEMS IN SIGHT.":GOSUB6
552 I$="THE EXITS ARE:"
554 IF CO(LC,1)<>0 THEN I$=I$+" NORTH,"
555 IF CO(LC,2)<>0 THEN I$=I$+" EAST,"
556 IF CO(LC,3)<>0 THEN I$=I$+" SOUTH,"
557 IF CO(LC,4)<>0 THEN I$=I$+" WEST,"
558 IF CO(LC,5)<>0 THEN I$=I$+" UP,"
559 IF CO(LC,6)<>0 THEN I$=I$+" DOWN,"
560 I$=LEFT$(I$,LEN(I$)-1)+".":GOSUB6
569 I$="":FORX=1TO32:I$=I$+CHR$(243):NEXT:PRINTI$;
570 A$=""
571 IF (LO(31)=LC OR LO(31)=99) AND LO(32)<>99 AND NM$(31)="A VICIOUS GREEN AND PURPLE " THEN 2012
572 I$="WHAT SHOULD I DO?":GOSUB6:INPUT A$
574 IF LEN(A$)=1 THEN 1000
580 REM
590 REM
600 V$=LEFT$(A$,3)
610 FOR I=1 TO LEN(A$)
620 IF RIGHT$(LEFT$(A$,I),1)=" " THEN A$=LEFT$(RIGHT$(A$,LEN(A$)-I),3):GOTO650
630 NEXT I
650 REM VERB LIST
660 IF V$="GO " THEN 1000
664 IF V$="WAL" THEN 1000
665 IF V$="RUN" THEN 1000
670 IF V$="GET" THEN 2000
672 IF V$="TAK" THEN 2000
675 IF V$="PIC" THEN 2000
680 IF V$="DRO" THEN 2500
685 IF V$="PUT" THEN 2500
690 IF V$="LOO" THEN 500
700 IF V$="INV" THEN 3000
710 IF V$="REA" THEN 4000
720 IF V$="UNR" THEN 4300
725 IF V$="BUR" THEN 4400
730 IF V$="LIG" THEN 4400
740 IF V$="CLI" THEN 4500
750 IF V$="RUB" THEN 4600
760 IF V$="HEL" THEN 4700
770 IF V$="DIG" THEN 4800
780 IF V$="OPE" THEN 5000
790 IF V$="POL" THEN 5100
800 IF V$="TIE" THEN 5200
810 IF V$="SCO" THEN 5300
820 IF V$="QUI" THEN 5600
830 IF V$="REM" THEN 5800
840 IF V$="COV" THEN 5900
850 IF V$="SCR" THEN 6000
860 IF V$="EAT" THEN 6100
870 IF V$="MEL" THEN 6200
880 IF V$="PLA" THEN 6300
980 I$="SORRY, I DON'T KNOW HOW TO DO THAT.":GOSUB6
990 GOTO570
1000 REM
1003 IF TR=1 THEN LO(21)=14:TR=0
1004 IF L=0 AND LI(LC)=0 THENI$="MOVING IN THE DARK CAN BE DANGEROUS!":GOSUB6:FORI=1TO600:NEXTI
1005 NM$(5)="A "
1006 NM$(4)=SS$
1007 IF LO(10)<>99 OR NM$(10)<>"A LIT " THEN L=0
1009 A$=LEFT$(A$,3)
1010 IF A$="NOR" THEN A$="N"
1020 IF A$="WES" THEN A$="W"
1030 IF A$="SOU" THEN A$="S"
1040 IF A$="EAS" THEN A$="E"
1041 IF A$="DOW" THEN A$="D"
1042 IF A$="UP"  THEN A$="U"
1050 REM
1060 IF CO(LC,1)<>0 AND A$="N" THEN LC=CO(LC,1):GOTO1200
1070 IF CO(LC,2)<>0 AND A$="E" THEN LC=CO(LC,2):GOTO1200
1080 IF CO(LC,3)<>0 AND A$="S" THEN LC=CO(LC,3):GOTO1200
1090 IF CO(LC,4)<>0 AND A$="W" THEN LC=CO(LC,4):GOTO1200
1092 IF A$="U" AND CO(1,5)<>0 AND LC=1 AND LO(6)<>99 AND LO(6)<>1 THENI$="IT'S TOO HIGH TO REACH.":GOSUB6:GOTO 570
1094 IF A$="U" AND LO(6)=99 THENI$="I CAN'T CLIMB A LADDER I'M CARRYING.":GOSUB5:GOTO 570
1100 IF CO(LC,5)<>0  AND A$="U" THEN LC=CO(LC,5):GOTO1200
1110 IF CO(LC,6)<>0 AND A$="D" THEN LC=CO(LC,6):GOTO1200
1180 I$="I CAN'T GO THAT DIRECTION.":GOSUB6
1190 GOTO 570
1200 IF LO(6)=LC THEN CO(6,6)=LC
1220 GOTO 500
2000 REM
2004 IF A$="SOC" THEN2040
2005 IF PO=>PL THENI$="I HAVE TOO MUCH ALREADY!":GOSUB6:GOTO2040
2010 IF A$<>"GAR" THEN 2018
2011 IF LO(31)<>LC THENI$="IT ISN'T HERE":GOSUB6:GOTO 570
2012 IF NM$(31)="A VICIOUS GREEN AND PURPLE "AND LO(32)<>99THENI$="A LOOSE AND VICIOUS GARGOYLE ATTACKS ME. I AM DEAD.":GOSUB6
2013 LC=0:GOTO5300:REM GOTO SCORE
2018 FOR I=1TOIT
2020 IF A$<>LEFT$(NA$(I),3) OR LO(I)<>LC THEN 2030
2022 IF A$="COA" AND MID$(NM$(30),6,1)="G" AND LO(19)<>99THENI$="YOU DON'T HAVE ANYTHING TO CARRY THEM IN.":GOSUB6:GOTO570
2023 IFA$="COA"ANDMID$(NM$(30),6,1)="G"ANDLO(19)=99ANDMID$(NM$(19),3,1)="F"THENI$="NOT WHILE THE URN IS CLOSED":GOSUB6:GOTO570
2024 IF A$="BIR" AND(LO(9)<>99 AND  LO(9)<>LC)THENI$="IT FLYS AWAY FROM ME.":GOSUB6:LO(22)=RND(6):FORJ=1TO600:NEXTJ:GOTO500
2025 IF A$="BIR" THEN LO(22)=0:NM$(9)="* A CRYSTAL BIRD LOCKED IN A ":GOTO500
2028 LO(I)=99:PO=PO+1:GOTO500
2030 NEXT I
2040 I$="I CAN'T PICK THAT UP.":GOSUB6
2100 GOTO570
2500 REM
2510 FOR I=1 TO IT
2516 IF A$="URN" AND LO(30)=99 THEN LO(30)=LC:PO=PO-1
2518 IF A$="LAD" AND LO(6)=99 THEN CO(7,6)=LC
2520 IF A$=LEFT$(NA$(I),3) AND LO(I)=99 THEN LO(I)=LC:PO=PO-1:GOTO500
2530 NEXT I
2535 I$="YOU DON'T HAVE THAT.":GOSUB6
2540 GOTO570
3000 REM
3001 PO=0
3005 C=0
3010 I$="I AM CARRYING:":GOSUB6
3020 FOR I=1 TO IT
3030 IF LO(I)=99 THENI$=NM$(I)+NA$(I):GOSUB6:C=C+1:PO=PO+1
3040 NEXT
3045 IF C=0 THENI$="NOTHING.":GOSUB6
3060 GOTO570
4000 REM
4005 IF L=0 AND LI(LC)=0 THENI$="IN THE DARK?":GOSUB6:GOTO4190
4010 IF A$="BOO" AND (LO(2)=LC OR LO(2)=99)THENI$="I DON'T READ GERMAN.":GOSUB6:GOTO570
4015 IF A$="SCR"AND LEFT$(NM$(4),4)="A TI"AND(LO(4)=99 OR LO(4)=LC)THENGOSUB4200:GOTO570
4020 IF A$="SCR" AND NM$(4)="AN UNROLLED " AND(LO(4)=LC OR LO(4)=99)THENI$="IT SAYS, BURN IT UP, CHUCK.":GOSUB6:GOTO570
4030 IF A$="CLO"ANDNM$(7)="A FAINTLY LETTERED "AND(LO(7)=99OR LO(7)=LC)THENI$="IT SAYS, IN THE CLOSET, DUMMY.":GOSUB6:GOTO570
4040 IF A$="SIG"ANDLC=26THENI$="IT SAYS, 'STAND BY FOR MORE ADVENTURE.' CALL YOUR LOCAL DISTRIBUTOR TODAY.":GOSUB6:GOTO570
4050 IF A$="PLA"AND LC=13AND(L=1 OR LI(13)=1)THENI$="GLOWING LETTERS SAY DROP *TREASURES HERE AND SAY SCORE.":GOSUB6:GOTO570
4190 I$="SORRY CHARLIE, CAN'T DO IT.":GOSUB6
4195 GOTO570
4200 I$="ON THE OUTSIDE, IT SAYS 'ROLLED BY SCROLL ROLLER'S UNION 207.'":GOSUB6:RETURN
4300 REM
4310 IF A$<>"SCR" THENI$="I DON'T KNOW HOW.":GOSUB6:GOTO 570
4320 IF A$="SCR" THEN LO(5)=LC:NM$(4)="AN UNROLLED ":SS$=NM$(4):I$="A LIGHTER FELL OUT.":GOSUB6:GOTO570
4400 REM
4410 IF A$<>"LIG" AND A$<>"FLA"AND A$<>"SCR"THENI$="SORRY, IT WON'T LIGHT":GOSUB6:GOTO570
4420 IF A$="LIG" AND(LO(5)=LC OR LO(5)=99)THEN NM$(5)="A LIT ":L=1:I$="THE LIGHTER FLARES.":GOSUB6:FORI=1TO600:NEXTI:GOTO500
4425 IF A$="SCR" AND LO(4)<>LC AND LO(4)<>99THENI$="IT ISN'T HERE.":GOSUB6:GOTO570
4430 IF A$="SCR" AND NM$(5)="A LIT "THEN NM$(4)="A BURNING ":I$="THE SCROLL BURNS BRIGHTLY.":GOSUB6:L=1:GOTO4460
4440 IF A$="FLA" AND LO(10)=99 THEN L=1:NM$(10)="A LIT ":GOTO 500
4450 I$="WHAT DO YOU MEAN?":GOSUB6:GOTO570
4460 IF LC=1 THENI$="IN THE LIGHT, I SEE AN OPENING IN THE CEILING.":GOSUB6:CO(1,5)=7
4470 IF LC=1  THEN CO(1,5)=7:CO(6,6)=7:CO(7,5)=6:GOTO570
4480 GOTO 570
4500 REM
4510 IF A$<>"LAD" THENI$="I CAN'T CLIMB THAT.":GOSUB6:GOTO570
4525 IF LO(6)<>LC AND LO(6)<>99THENI$="WHAT LADDER?":GOSUB6:GOTO570
4530 IF CO(LC,5)=0THENI$="I CLIMBED IT, BUT THERE WAS NOTHING THERE SO I CAME BACK DOWN.":GOSUB6:GOTO570
4540 A$="U":GOTO1050
4600 REM
4605 FOR I=1 TO IT
4606 IF A$=LEFT$(NA$(I),3)AND(LO(I)<>LC AND LO(I)<>99)THENI$="IT'S NOT HERE.":GOSUB6:GOTO570
4607 NEXT I
4610 IF A$<>"RIN" THENI$="NOTHING, SPORT.":GOSUB6:GOTO570
4620 IF LO(3)<>LC AND LO(3)<>99THENI$="WHAT RING?":GOSUB6:GOTO570
4625 IF LO(7)<>99 AND LO(7)<>LCTHENI$="I HAVE NOTHING TO RUB IT WITH.":GOSUB6:GOTO570
4630 I$="THE RING GLOWS GOLD.":GOSUB6:NM$(3)="* A SHIMMERING "
4640 NM$(7)="A FAINTLY LETTERED "
4650 GOTO570
4700 REM
4710 I$="HELP? YOU'RE SUPPOSED TO BE HELPING ME!":GOSUB6:GOTO570
4800 REM
4810 IF LO(8)<>99 AND LO(20)<>99 THENI$="WITH WHAT?":GOSUB6:GOTO570
4820 IF LC<>5AND LC<>12AND LC<>14AND LC<>15AND LC<>24AND LC<>26THENI$="I CAN'T DIG HERE.":GOSUB6:GOTO570
4830 I$="OKAY. HERE'S A HOLE.":GOSUB6
4840 IF LC=5 THEN CO(27,5)=5:CO(5,6)=27 :IF LO(22)=90 THEN LO(22)=27
4850 IF LC=12 THEN CO(27,5)=12:CO(12,6)=27
4860 IF LC=14 THEN CO(27,5)=14:CO(14,6)=27
4870 IF LC=15 THEN CO(27,5)=15:CO(15,6)=27:IF LO(25)=90 THEN LO(25)=27
4880 IF LC=24 THEN CO(27,5)=24:CO(24,6)=27:IF LO(23)=90 THEN LO(23)=27
4885 IF LC=24 AND LO(15)=90 THEN LO(15)=24
4890 IF LC=26 THEN CO(27,5)=26:CO(26,6)=27:IF LO(31)=90 THEN LO(31)=27
4900 GOTO570
5000 REM
5005 IF A$="SCR" THEN 4300
5010 IF A$<>"CAG" AND A$<>"URN" THENI$="IT DOESN'T SEEM TO OPEN.":GOSUB6:GOTO570
5015 IF A$="CAG" AND LO(9)<>99 AND LO(9)<>LC THENI$="WHAT CAGE":GOSUB6:GOTO570
5020 IF A$="CAG" AND (LO(9)=99 OR LO(9)=LC) THENI$="THE DOOR OPENED.":GOSUB6:NM$(9)="AN OPEN "
5030 IF A$="URN"AND(LO(19)=99 OR LO(19)=LC)THENI$="IT'S OPEN":GOSUB6:NM$(19)="AN OPEN FUNERARY ":IF LO(26)=90THEN LO(26)=LC
5035 I$="SOMETHING FELL OUT.":GOSUB6
5040 GOTO570
5100 REM
5110 IF A$<>"CRO" AND A$<>"RIN" THENI$="I CAN'T POLISH SUCH A THING":GOSUB6:GOTO570
5113 IF A$="RIN" AND LO(3)<>LC AND LO(3)<>99 THENI$="WHAT RING?":GOSUB6:GOTO570
5115 IF LO(16)<>LC AND LO(16)<>99 THENI$="I DON'T HAVE ANY POLISH.":GOSUB6:GOTO570
5118 IF A$="CRO" AND LO(23)<>LC AND LO(23)<>99 THENI$="WHAT CROWN?":GOSUB6:GOTO570
5120 IF A$="RIN" THENI$="I DON'T NEED POLISH FOR THIS. IT'S JUST A LITTLE DULL.":GOSUB6:GOTO570
5130 IF NM$(16)="AN EMPTY BOTTLE OF " THENI$="SORRY, I'M OUT OF POLISH.":GOSUB6:GOTO 570
5140 IF A$="CRO"AND NM$(16)="A BOTTLE FULL OF " THEN NM$(23)="*A GLISTENING SILVER ":NM$(16)="AN EMPTY BOTTLE OF "
5145 I$="IT SHINES OF SILVER.":GOSUB6:GOTO570
5200 REM
5210 IF A$<>"GAR" THENI$="I AM UNABLE TO TIE THAT.":GOSUB6:GOTO 570
5220 IF(LO(31)<>99 AND LO(31)<>LC) OR (LO(17)<>99 AND LO(17)<>LC)THENI$="I'M SORRY. I CANNOT.":GOSUB6:GOTO 570
5230 NM$(17)="A KNOTTED "
5235 LO(17)=0
5240 NM$(31)="* A ROPE BOUND "
5250 I$="IT'S FRIENDLY WHEN IT'S TIED UP.":GOSUB6
5260 GOTO570
5300 REM CALCULATE SCORE
5305 SC=0
5307 SM=0
5310 FOR I=1 TO IT
5320 IF LEFT$(NM$(I),1)="*" AND LO(I)=13 THEN SC=SC+1:GOTO5330
5325 IF LO(I)<>27 AND LO(I)<>99 AND LO(I)<>1 AND LO(I)<>28 AND LO(I)<>0 AND LO(I)<>90 AND LO(I)<>24 THEN SM=SM+1
5330 NEXT I
5340 I$="YOU SCORED "+STR$(SC*10)+" POINTS OUT OF 100"
5350 IF SM=0 THEN I$=I$+".":GOSUB6:GOTO5370
5355 I$=I$+",":GOSUB6
5360 I$="BUT I AM PENALIZING YOU "+STR$(SM)+" POINTS FOR SLOPPINESS.":GOSUB6
5370 I$="AND YOUR NET SCORE IS "+STR$(SC*10-SM)+".":GOSUB6
5380 IF SM>6 THENI$="CLEAN UP AFTER YOURSELF.":GOSUB6
5390 IF SC*10-SM=100 THENI$="CONGRATULATIONS! A PERFECT SCORE.":GOSUB6:LC=0
5400 IF SC*10-SM>90 AND SC*10-SM<100 THENI$="VERY, VERY GOOD.":GOSUB6
5490 IF LC=0 THENI$="THE GAME IS OVER":GOSUB6:POKE17026,15:POKE 49151,16:END
5500 GOTO570
5600 REM
5610 I$="DO YOU WANT TO QUIT?":GOSUB6:INPUT X$
5620 IF LEFT$(X$,1)<>"Y" THEN570
5630 LC=0
5635 CLS
5640 GOTO5300
5800 REM
5810 IF A$<>"ARM" THENI$="I CANNOT REMOVE THAT.":GOSUB6:GOTO570
5815 IF LO(32)<>99 THENI$="I AM NOT WEARING IT.":GOSUB6:GOTO570
5820 LO(32)=LC
5830 I$="AH, THAT FEELS BETTER.":GOSUB6
5840 GOTO570
5900 REM
5910 IF A$<>"BOO"THENI$="I CAN'T COVER THAT.":GOSUB6:GOTO570
5920 IF(LO(2)<>99 AND LO(2)<>LC)OR(LO(11)<>99AND LO(11)<>LC)THENI$="I CAN'T YET.":GOSUB6:GOTO570
5930 LO(11)=0:REM GET RID OF COVER
5940 NM$(2)="* A COMPLETE GUTENBURG "
5945 NA$(2)="BIBLE"
5950 I$="THAT LOOKS NICE.":GOSUB6
5960 GOTO570
6000 REM
6010 IF A$<>"BUL" THENI$="THE ONLY THING I CAN SCREW IS A BULB.":GOSUB6:GOTO570
6020 IF(LO(13)<>99 AND LO(13)<>13)AND(LO(12)=99 OR LO(12)=13)THENI$="THAT'S THE WRONG KIND OF BULB!":GOSUB6:GOTO570
6030 REM SUCCESS
6040 LO(13)=0
6045 LI(13)=1
6050 NA$(14)="LAMP":NM$(14)="* A TIFFANY "
6070 GOTO500
6100 REM
6110 IF A$<>"COM" THENI$="I CAN'T EAT THAT!":GOSUB6:GOTO570
6120 IF LO(15)=99 OR LO(15)=LC THENI$="GAG......YURP! BAD!":GOSUB6:GOTO570
6130 I$="THANK GOD, IT ISN'T HERE!":GOSUB6
6140 GOTO570
6200 REM
6210 IF A$<>"ICE"THENI$="THAT WON'T MELT":GOSUB6:GOTO570
6215 IF NA$(29)<>"ICE" THENI$="ONLY ONCE!":GOSUB6:GOTO570
6220 IF(LO(29)<>LC AND LO(29)<>99)OR(LO(30)<>LC AND LO(30)<>99)THENI$="I CAN'T YET.":GOSUB6:GOTO570
6230 NM$(29)="SOME THAWED "
6240 NA$(29)="WATER"
6250 NM$(30)="SOME COLD "
6260 I$="THAT DID IT. THE WATER FLOWS.":GOSUB6
6270 GOTO570
6300 REM
6310 IF A$<>"BUL" AND A$<>"TUL" THENI$="I CAN'T PLANT THAT.":GOSUB6:GOTO570
6320 IF(LO(13)=99 OR LO(13)=LC) AND NOT(LO(12)=99 OR LO(12)=LC)THENI$="PLANT THAT BULB?":GOSUB6:GOTO570
6330 IF(LO(12)=99 OR LO(12)=LC) AND(LO(15)=99 OR LO(15)=LC)AND NA$(29)="WATER"THEN6350
6340 I$="I DON'T HAVE EVERYTHING I NEED.":GOSUB6:GOTO570
6350 IF LC<>14 THENI$="IT WON'T GROW HERE.":GOSUB6:GOTO570
6360 IF LO(20)<>99 AND LO(20)<>LC AND LO(8)<>LC AND LO(8)<>99THENI$="NOT WITH THESE BARE HANDS!":GOSUB6:GOTO570
6370 TR=1:REM THIS IS THE TULIP READY COUNTER
6380 I$="OKAY. I BURIED THAT MESS":GOSUB6
6390 LO(12)=0:LO(15)=0:LO(29)=0
6400 GOTO570
8002 REM NA$(I),NM$(I),LO(I)
8004 REM ITEM COUNT
8005 DATA 32
8010 DATA"RUBY","* A HUGE ",4
8020 DATA"BOOK","AN OLD ",3
8030 DATA"RING","A DULL ",2
8040 DATA"SCROLL","A TIGHTLY ROLLED ",3
8050 DATA"LIGHTER","A ",0
8060 DATA"LADDER","A LONG ",4
8070 DATA"CLOTH","A FUZZY ",3
8080 DATA"SHOVEL","AN ANTIQUE ",16
8090 DATA"CAGE","A WICKER ",18
8100 DATA"FLASHLIGHT","AN UNLIT ",15
8110 DATA"BOOKCOVER","AN EMPTY ",11
8120 DATA"BULB","A TULIP ",25
8130 DATA"BULB","A LIGHT ",27
8140 DATA"SOCKET","AN EMPTY ",13
8150 DATA"COMPOST","SOME WELL ROTTED ",90
8160 DATA"POLISH","A BOTTLE FULL OF ",13
8170 DATA"ROPE","A COILED ",21
8180 DATA"TRASH","A MOUND OF ",21
8190 DATA"URN","A FUNERARY ",15
8200 DATA"TROWEL","A GARDEN ",1
8210 DATA"TULIP","* A RAINBOW COLORED ",90
8220 DATA"BIRD","* A LIVING CRYSTAL ",5
8230 DATA"CROWN","A DISCARDED, BLACKENED ",90
8240 DATA"D","D",90
8250 DATA"COINS","* SOME TRIANGULAR GOLD ",90
8260 DATA"PIPE","* A PLATINUM SMOKING ",90
8270 DATA"D","D",90
8280 DATA"D","D",90
8290 DATA"ICE","SOME GREENISH ",26
8300 DATA"COALS","SOME GLOWING ",18
8310 DATA"GARGOYLE","A VICIOUS GREEN AND PURPLE ",90
8320 DATA"ARMOUR","SOME VERY, VERY HEAVY ",99
9004 REM LOCATION COUNT
9005 DATA 28
9010 DATA"NOWHERE.",0,0,0,0,0,0,0
9020 DATA"A DIM CLOSET.",0,2,3,0,0,0,0
9030 DATA"A GATHERING ROOM WITH WIDE BENCHES DOWN BOTH SIDES.",5,0,4,1,0,0,1
9040 DATA"AN OLD STONE ARMS ROOM.",1,4,0,0,0,0,1
9050 DATA"A DANK AND MUSTY DUNGEON.",2,0,0,3,0,0,0
9060 DATA"A LUSH MEADOW, WITH A TOWER TO THE SOUTH. TO THE WEST THERE'S A DEEP MOAT AND HIGH CASTLE WALL.",0,0,2,28,0,0,1
9070 DATA"A HIGH TOWER. THROUGH A WESTERN PORTAL A BRIDGE LEADS TO ANOTHER CASTLE.",0,0,0,8,0,7,1
9080 DATA"THE TOP OF THE LADDER.",0,0,0,0,0,0,0
9090 DATA"A BRIDGE TO A TOWER OFF TO THE WEST.",0,6,0,9,0,0,1
9100 DATA"A NORTH-SOUTH CORRIDOR IN THE TOWER.",10,8,19,0,0,0,1
9110 DATA"A SQUARE ROOM WITH A VAULTED CEILING.",0,0,9,11,0,24,1
9120 DATA"A LIBRARY FULL OF ANCIENT AND WONDERFUL BOOKS.",0,10,12,0,0,0,1
9130 DATA"A GRASSY ROOF-TOP WITH POTTED PALMS AND A CLEAR BLUE SKY.",11,13,14,25,0,0,1
9140 DATA"A ROOM WITH A TALL GOLDEN THRONE, AND A SMALL PLASTER PLAQUE SET IN THE FLOOR..",0,0,0,12,0,0,0
9150 DATA"A BEAUTIFUL FLOWER GARDEN, WITH BEDS OF PLANTS DOWN BOTH SIDES OF A LONG WALK.",12,0,15,0,0,0,1
9160 DATA"A TOMB WITH A ROW OF BRONZE MARKERS FOR FALLEN SCHOLARS.",18,16,0,14,0,0,0
9170 DATA"A MUSEUM WITH BROKEN AND DESTROYED DISPLAYS.",17,0,0,15,0,0,0
9180 DATA"AN EAST-WEST CORRIDOR WITH PORCELAIN TILE WALLS.",0,22,16,18,0,0,0
9190 DATA"A TORTURE CHAMBER WITH A BRAZIER IN THE CORNER.",0,17,15,0,0,0,0
9200 DATA"A SQUARE ROOM WITH A VAULTED CEILING.",9,20,0,0,0,0,1
9210 DATA"A TWIST IN THE HALLWAY, WITH A TERRAZZO EAGLE IN THE FLOOR.",0,0,22,19,0,0,1
9220 DATA"A BATHROOM WITH PRECIOUS METAL FIXTURES.",0,0,23,22,0,0,0
9230 DATA"A HALLWAY WITH WARRIORS IN BAS-RELIEF IN THE WALL STONES.",20,21,23,17,0,0,1
9240 DATA"A BEDROOM FIT FOR A KING. IT SAYS 'KING' RIGHT ON THE CURTAIN.",22,21,0,0,0,0,0
9250 DATA"A PIT FULL OF SMELLY GARBAGE AND FOUL REFUSE.",0,0,0,0,10,0,0
9260 DATA"A PASSAGE WITH A BLINDING GLARE TO THE WEST AND A DARK HOLE TO THE EAST.",0,12,0,26,0,0,1
9270 DATA"A WIND SWEPT ICEFIELD. THERE'S A SIGN HALF-BURIED IN THE ICE. WHOOPS-- WRONG ADVENTURE.",0,25,0,0,0,0,1
9280 DATA"THE BOTTOM OF A HOLE.",0,0,0,0,0,0,0
9290 DATA"THE WATER OF THE MOAT.",0,5,0,0,0,0,1
