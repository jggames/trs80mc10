
100 REM version 8/18/84, 01:53
102 REM haunted house
104 REM by ROGER TORRE  71056,766
106 CLS:PRINTTAB(10)"HAUNTED HOUSE"
108 PRINT:PRINT
110 PRINT"    YOU ARE ABOUT TO ENTER A            ABANDONED HOUSE"
112 PRINT"YOU USUALLY GIVE ME ONE WORD    COMMANDS TO DIRECT YOUR MOVEMENTTHROUGH THE HOUSE."
114 PRINT" HERE ARE THE VALID  COMMANDS: "
116 PRINT" N, S, E, W, U, D, GET, RUB,     QUIT, LOOK, READ, SCORE, TAKE,  INV(ENTORY), EXAM"
118 GOSUB336
120 CLS:PRINT" YOU CAN GO THE DIRECTIONS SHOWN FOR EXAMPLE *E*W* MEANS YOU CAN GO (E)AST OR (W)EST.
122 PRINT" DON'T FORGET THAT TWO WORD      COMMANDS CAN ALSO BE USED."
124 GOSUB 336
126 SOUND89,3:SOUND133,3:SOUND159,3:SOUND176,6:SOUND159,3:SOUND176,9
128 DIMD$(29),WP(29,6),TL(7),T$(7),TC$(14),TH(7)
130 RESTORE:FOR J=1 TO 29:READ D$(J):NEXT J
132 FOR J=1 TO 6:FOR K=1 TO 29:READ WP(K,J):NEXT K,J
134 FOR J=1 TO 14:READ TC$(J):NEXT J
136 FOR J=1 TO 7:READ T$(J):NEXT J
138 FOR J=1 TO 7:READ TL(J):NEXT J
140 FOR J=1 TO 7:TH(J)=0:NEXT J:L=1:T=0:Z=0:X=0:S=0:Z4=0:CLS
142 CLS:PRINT D$(L)
144 IF L=1 AND X=7 THEN PRINT"YOU MADE IT. GOOD GOING !!!" :GOTO148
146 GOTO 152
148 PRINT"IT TOOK ";T;"TURNS"
150 PRINT"TO SCORE ";S;"OUT OF 175.":END
152 IF X=7 AND Z=0 THEN PRINT"YOU HAVE ALL THE TREASURES. NOW YOU MUST ESCAPE THE HOUSE.":Z=1
154 IF L=1 AND X>1 THEN PRINT"YOU MADE IT. BUT YOU DON'T HAVE ALL THE TREASURES."
156 IF L=1 OR L=2 OR L=3 OR L=4 OR L=5 OR L=6 OR L=26 OR L=27 OR L=28 OR L=29 THEN 160
158 IF TH(7)=0 AND X=6 THEN PRINT"YOU HAVE ALL THE TREASURES IN   THE HOUSE"
160 IF L=15 THEN PRINT" A GHOST GRABS YOU AND CARRIES   YOU AWAY":L=RND(29):GOSUB336:GOTO140
162 IF L=13 AND X>=1 THEN PRINT"A EVIL SPIRIT KILLS YOU FOR YOURTREASURE !!":GOSUB 342:GOTO130
164 TN=0:FOR J=1 TO 7:IF L=TL(J) THEN TN=J
166 NEXT J
168 IF TH(4)=1 THEN Z4=1
170 IF L=10 OR L=18 OR L= 26 OR L=20 OR L=23 OR L=24THEN 176
172 IF TN<>0 THEN PRINT"THERE IS ";T$(TN);" HERE."
174 IF T=75 THENPRINT"YOU JUST RAN OUT OF TIME AND A  UNFRIENDLY GHOST FOUND YOU !":GOSUB342:GOTO130
176 C$="":INPUT C$:IF C$="" THEN 176
178 IF LEFT$(C$,4)="EXAM"THEN C$="EXAM"
180 IF C$>"" THEN T=T+1
182 IF L=6 AND C$="LOOK DOORMAT" THEN GOSUB 352:GOTO 176
184 IF C$="TAKE"THEN C$="GET"
186 IF L=7 AND Z4=1 AND C$="S"THEN GOSUB 380:GOSUB336:L=6:GOTO 142
188 IF L=6 AND Z4=1 AND C$="N" THEN GOSUB 384:GOSUB336:L=7:GOTO142
190 IF L=4 AND Z4=1 AND C$="S" THEN GOSUB384:GOSUB336:L=16:GOTO142
192 IF L=16 AND Z4=1 AND C$="N" THEN GOSUB380:GOSUB336:L=4:GOTO 142
194 C=0:FOR J=1 TO 14:IF C$=TC$(J) THEN C=J
196 NEXT J
198 IF C=1 THEN PRINT"IT TOOK ";T;"TURNS TO SCORE ";S;"OUT OF 175.":END
200 IF C=2 AND L=6 THEN PRINT" THE NAME ON THE MAILBOX READS          'A. HITCHCOCK'.":GOTO 176
202 IF C=2 THEN PRINT"I DON'T KNOW THAT WORD HERE.":GOTO 176
204 IF L=18 AND C=14THEN GOSUB 362:GOTO176
206 IF L=20 AND C=14 THEN GOSUB 366:GOTO176
208 IF L=24 AND C=14 THEN GOSUB370:GOTO 176
210 IF L=26 AND C=14 THEN GOSUB374:GOTO176
212 IF C=3 AND TH(3)=1 THEN PRINT" A EVIL GENIE APPEARS AND KILLS  YOU.":GOSUB342:GOTO 130
214 IF C<>4 THEN 224
216 IF X=0 THEN PRINT"YOU AREN'T HOLDING ANYTHING.":GOTO 176
218 PRINT"YOU ARE HOLDING THE FOLLOWING:"
220 FOR J=1 TO 7:IF TH(J)=1 THEN PRINT T$(J)
222 NEXT J:GOTO 176
224 IF C<>5 THEN 236
226 FOR J=1 TO 7
228 IF L=TL(J) THEN TL(J)=0:TH(J)=1:GOTO 234
230 NEXT J
232 PRINT"THERE ARE NO TREASURES TO GET.":GOTO 176
234 PRINT" TAKEN.":X=X+1:S=S+25:GOTO 152
236 IF C=12 THEN 142
238 IF C=13 THEN PRINT"SO FAR YOU USED";T:GOTO242
240 GOTO 244
242 PRINT" TURNS TO SCORE ";S:GOTO 176
244 IF C<6 OR C>11 THEN 250
246 C=C-5:IF WP(L, C)=0 THEN PRINT" THERE IS NO WAY TO GO IN THAT  DIRECTION.":S=S-1:GOTO 176
248 IF WP(L, C)<>0 THEN L=WP(L, C):GOTO 142
250 ON RND(3) GOTO 252,254,256
252 PRINT"WHAT ?":GOTO 176
254 PRINT" I DON'T UNDERSTAND.":GOTO 176
256 PRINT" I DON'T KNOW WHAT YOU MEAN.":GOTO 176
258 DATA"YOU ARE ON THE SIDEWALK IN FRONTOF THE PORCH. *N*E*W*"
260 DATA"YOU YOU ARE IN THE FRONT YARD    *W*N*"
262 DATA"YOU ARE IN THE YARD TO YOUR LEFTIS THE EAST SIDE OF THE HOUSE   WITH A OPEN WINDOW *U*S*N*"
264 DATA"YOU ARE IN THE BACKYARD TO THE  SOUTH IS A PORCH.  *S*W*E*"
266 DATA"YOU ARE IN THE YARD TO THE EAST IS THE HOUSE AND A OPEN WINDOW.    *U*S*N*"
268 DATA"YOU ARE ON THE FRONT PORCH, YOU SEE A DOORMAT AND THAT THE DOOR IS CLOSED.  *N*S*"
270 DATA"YOU ARE IN THE FRONT HALLWAY.   *N*S*E*W*U*"
272 DATA"YOU ARE IN THE RECEPTION ROOM.  YOU SEE A TABLE WITH WEDDING    PRESENTS.   *N*W*E*"
274 DATA" YOU ARE IN THE MUSIC ROOM. YOU  SEE AN OPEN WINDOW TO THE EAST.    *E*W*"
276 DATA"YOU ARE IN THE DINING ROOM.     YOU SEE A TABLE SET FOR DINNER.     *S*W*"
278 DATA"YOU ARE IN THE REAR HALLWAY.     *N*S*E*W*"
280 DATA"YOU SEE THE KITCHEN. *E*W*D*"
282 DATA" YOU ARE IN THE PANTRY.  *E*"
284 DATA"YOU ARE IN THE DRAWING ROOM      *E*N*"
286 DATA"THE STUDY. *S*"
288 DATA"YOUR ON THE REAR PORCH.          *N*S*E*"
290 DATA"YOUR ON THE NORTH CONSERVATORY.   *W*S*"
292 DATA"YOUR ON THE EAST CONSERVATORY   YOU SEE A CLOSED TRUNK.   *N*"
294 DATA"YOUR ON THE STAIRS GOING DOWN TOTHE BASEMENT.  *U*D*"
296 DATA"YOUR IN THE BASEMENT. YOU SEE   A SET OF SHELVES.    *U*"
298 DATA"YOUR ON THE STAIRS GOING UP TO THE SECOND FLOOR.   *U*D*"
300 DATA"YOUR ON THE LANDING AT THE TOP  OF THE STAIRS  *D*N*E*"
302 DATA"YOU ARE IN THE MASTER BEDROOM   YOU SEE A PAINTING ON THE WALL      *S*E*"
304 DATA"YOU ARE IN THE GUEST BEDROOM YOUSEE A DRESSER, AND A BED *N*W*"
306 DATA"YOU ARE IN A BATHROOM.   *W*S*"
308 DATA"YOU SEE A SMALL MUSTY ROOM. AND A SAFE IN THE CORNER.   *W*"
310 DATA"YOU ARE IN THE YARD TO THE EAST IS THE HOUSE.  *N*E*"
312 DATA"YOU ARE IN THE BACK YARD.  *S*E*
314 DATA"YOU ARE EAST OF THE HOUSE.        *W*S*"
316 DATA 6,3,29,0,28,0,11,10,0,0,16,0,0,15,0,0,0,17,0,0,0,23,0,25,0,0,5,0,0
318 DATA 2,0,0,29,0,0,8,9,3,0,10,11,12,7,0,17,0,0,0,0,0,24,25,0,0,0,1,4,0
320 DATA 0,0,2,0,27,1,0,0,0,8,7,0,0,0,14,11,18,0,0,0,0,0,22,0,24,0,0,5,3
322 DATA 27,1,0,28,0,0,14,7,8,11,12,13,0,0,0,0,16,0,0,0,0,0,0,22,23,5,0,0,4
324 DATA 0,0,9,0,26,0,21,0,0,0,0,0,0,0,0,0,0,0,12,19,22,0,0,0,0,0,0,0,0
326 DATA 0,0,0,0,0,0,0,0,0,0,0,19,0,0,0,0,0,0,20,0,7,21,0,0,0,0,0,0,0
328 DATA "QUIT","READ","RUB","INV","GET","N","E","S","W","U","D","LOOK","SCORE","EXAM"
330 DATA "A CRYSTAL BOWL","SILVER PLACE SETTINGS","A BRASS LAMP","A BRASS KEY"
332 DATA "A REMBRANDT PAINTING","SOME GOLD JEWELRY","A BAG OF MONEY"
334 DATA 8,10,18,20,23,24,26
336 PRINT:PRINT"   PRESS ANY KEY TO CONTINUE"
338 IF INKEY$=""THEN 338
340 RETURN
342 PRINT" YOU ARE NOW DEAD, TRY AGAIN ?    (Y/N) "
344 A9$=INKEY$:IF A9$=""THEN 344
346 IF A9$="Y" THEN RETURN
348 IF A9$<>"N" THEN CLS:GOTO 342
350 END
352 PRINT" YOU LOOK UNDER THE DOORMAT AND  SEE A MESSAGE "
354 INPUT C$
356 IF C$="READ MESSAGE" OR C$="READ" THEN PRINT"IT SAYS GONE FOR THE DAY BUT YOU";:GOTO 360
358 RETURN
360 PRINT" CAN USE THE MUSIC ROOM WINDOW.":PRINT:PRINT"                    'AL'":RETURN
362 IF TH(3)=1 THEN RETURN
364 PRINT"THERE'S SOMETHING THERE ALRIGHT IT'S A BRASS LAMP.":RETURN
366 IF TH(4)=1 THEN RETURN
368 PRINT" THERE'S A KEY ON THE SHELF":RETURN
370 IF TH(6)=1 THEN RETURN
372 PRINT"YOU SEE SOME GOLD JEWELRY ON THEDRESSER":RETURN
374 IF TH(7)=1 THEN RETURN
376 PRINT"YOU FOUND THE SAFE COMBINATION  AND OPENED THE SAFE AND YOU SEE"
378 PRINT"A BAG OF MONEY":RETURN
380 PRINT"YOU USE THE KEY AND THE DOOR    OPENS YOU EXIT THE HOUSE THRU"
382 PRINT"THE DOOR AND THEN THE DOOR SLAMSSHUT BEHIND YOU.":RETURN
384 PRINT" YOU USE THE KEY TO UNLOCK THE   DOOR, YOU THEN ENTER THE HOUSE"
386 PRINT" WHEN THE DOOR BLOWS SHUT AND    LOCKS BEHIND YOU.":RETURN
