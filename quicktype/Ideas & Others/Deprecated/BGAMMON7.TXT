5 DIM A(64):CLS0:OS=.666666666
10 O=0:B=0:A(0)=0:A(25)=0:Q=4
20 DATA 2,0,0,0,0,-5,0,-3,0,0,0,5,-5,0,0,0,3,0,5,0,0,0,0,-2
30 FOR X=1 TO 24:READ A(X):NEXT
40 GOSUB 1000
60 P=RND(6):C=RND(6)
70 PRINT@0,"YOU";P;
75 PRINT"ME";STR$(C)". ";
80 IF P=C THEN 60
90 IF P>C THEN PRINT"YOU";:GOTO110
100 PRINT"I";
110 INPUT" GO FIRST";A$
120 IF P>C THEN 600
130 REM ME'S TURN
140 T=0:GOSUB 1300
150 GOSUB1350:PRINT" THINKING...";
155 REM ARE ALL PIECES ON INNER BOARD?
160 C=1:FORZ=0TO18:IFA(Z)>0THENC=0
170 NEXT Z
171 REM PLAYER HASN'T PASSED COMPUTER?
172 V=0:P=0:FOR Z=1 TO 24:IF (A(Z)<0)*P THEN V=1
174 IF A(Z)>0 THEN P=1
176 NEXT Z
178 H=0:L=0:R=0:FOR Y=0 TO 3:IF A(Y+26)=0 THEN 315
180 FOR X=0 TO 25-A(Y+26):E=0:IF A(X)<=0 THEN 310
190 IF (X=0)*(A(X)>0) THEN E=E+128
200 IF X+A(26+Y)=25 THEN E=E+64
210 IF (A(X)=1)*(A(X+A(26+Y)))*V THEN E=E+16
220 IF (A(X)<>2)*(A(X+A(26+Y)))*V THEN E=E+8
230 IF (A(X+A(26+Y))=-1)+(X<7) THEN E=E+32
240 IF X<19 THEN E=E+4
250 IF (A(X+A(26+Y))<-1)+((C=0)*(X+A(26+Y)>24)) THEN 310
260 E=E+1:IF (X+A(26+Y)=25)+(Y=3) THEN 300
270 FOR Z=0 TO 3:D=X+A(26+Y)-A(26-Z):IF (Y=Z)+(A(26+Z)=0)+(D<0)+(D>24) THEN 290
280 IF (A(D)=1)+(A(D)>2) THEN E=E+2:Z=3:GOTO290
290 NEXT Z
300 IF (H<E)+(H=E)*((X<L)+(X=L)*(R<A(26+Y))) THEN H=E:R=A(26+Y):L=X
310 NEXT X
315 NEXT Y
320 IF (H<128)*(A(0)>0) THEN 580
330 IF H<>0 THEN 390
340 R=7:FOR Y=0 TO 3:IF (A(26+Y)<R)*(A(26+Y)>1) THEN R=A(26+Y):L=Y
350 NEXT Y:FOR X=0 TO 25-R:IF A(X)>0 THEN 580
360 NEXT X:FOR X=26-R TO 24:IF A(X)>0 THEN A(26+L)=25-X:GOTO 178
370 NEXT X:GOTO580
390 IF L>0 THEN C=A(L):P=L:GOSUB1600
400 A(L)=A(L)-1:GOSUB1280
430 IFL=0THENGOSUB1550
440 IF A(L+R)=-1THEN B=B+1:A(L+R)=0:C=1:P=L+R:GOSUB1600:GOSUB1550
450 Y=A(L+R)+1:A(L+R)=Y:IFL+R<25THENC=A(L+R):P=L+R:GOSUB2000:GOTO453
452 GOSUB1550
453 FORZ=0TO3:IF A(26+Z)<>RTHENNEXT Z
454 GOSUB1350:PRINT" FROM";:X=L:GOSUB1850:PRINT"TO";:X=L+R:GOSUB1850:A(26+Z)=0
455 INPUTA$
460 IFA(25)=15THEN560
470 FORX=0TO3:IFA(26+X)>0THEN150
480 NEXT X:GOTO600
560 GOSUB 1500:PRINT@0,"I WIN. AGAIN?";:GOTO 860
580 GOSUB 1350:PRINT" CAN'T";
590 INPUTA$
600 REM PLAYER'S TURN
605 IFPEEK(16384+32)<>66THENGOSUB1000
610 T=1:GOSUB 1300
620 GOSUB 1350:IF B THEN F=25:PRINT" FROM BAR";:GOTO 640
630 R=999:N=99:INPUT" FROM";F$:F=INT(VAL(F$))
635 IF F$="N" THEN 130
637 IFPEEK(16384+32)<>66THENGOSUB1000:GOTO 620
640 IF F$="R" THEN GOSUB 1000:GOTO 620
650 IF ((F=25)*(B=0))+(F<1)+(F>25) THEN 980
660 IF (F<25)*(A(F)>=0) THEN 980
670 PRINT@24,"";:X=O:O=0:N=99:R=999:INPUT"TO";D$:O=X:D=INT(VAL(D$))
671 IFPEEK(16384+32)<>66THENGOSUB1000:GOTO 620
672 IF D$="N" THEN 130
673 IF D$="R" THEN GOSUB 1000:GOTO 620
675 IF (D<0)+(D>24) THEN 980
680 IF A(D)>1 THEN 980
682 M=0:G=0:FOR W=0 TO 3:IF A(26+W)=F-D THEN 690
683 IF M<A(26+W) THEN M=A(26+W):G=W
684 NEXT W:IF (D<>0)+(B<>0) THEN 980
685 REM DICE EXCEED WHAT'S NEEDED TO MOVE OFF?
686 W=G:IF F<M THEN M=F+1
687 FOR X=M TO 6:IF A(X)<0 THEN 980
688 NEXT X:GOTO706
689 REM PUT COMPUTER ON BAR?
690 IF (D>0)*(A(D)=1) THEN A(0)=A(0)+1:GOSUB 1550:A(D)=0
695 REM FROM BAR?
700 IF F=25 THEN B=B-1:GOSUB 1550:GOTO 730
702 REM FROM NORMAL
704 IF D<>0 THEN 710
706 FOR X=7 TO 24:IF A(X)<0 THEN 980
708 NEXT X
710 C=A(F):P=F:GOSUB1600:A(F)=A(F)+1:GOSUB1295
720 IF D=0 THEN 800
730 A(D)=A(D)-1:P=D:C=A(D):GOSUB2000:A(26+W)=0
740 REM ANY MORE DICE LEFT?
750 FOR W=0 TO 3:IF A(26+W)<>0 THEN 620
760 NEXT W:GOTO 130
800 REM JUST MOVED OFF
840 O=O+1:GOSUB 1550:A(26+W)=0:IF O<15 THEN 750
850 GOSUB 1500:PRINT@.5*0,"YOU WIN! AGAIN?";
860 Y=1:N=0:INPUT A:IF (A<>Y)*(A<>N) THEN GOTO 820
870 IF A=Y THEN GOTO 10
880 END
970 REM ILLEGAL MOVE
980 GOSUB 1500:PRINT@0,"";
990 INPUT"ILLEGAL";A$:GOTO620
999 REM REDRAW SCREEN
1000 CLS0
1003 FOR X=0 TO 31:PRINT" ";:NEXT X
1004 PRINT@1*32, "BAR 242322 21201918 17161514 13 ";
1005 PRINT@15*32,"OFF 1  2 3 4 5  6 7 8 9 1011 12";
1010 PK=16415:FORCL=1TO32:A$=MID$("    24  22   20  18   16  14    ",CL,1):IFA$<>" "THENPOKEPK+CL,ASC(A$)
1020 NEXT
1030 PK=16863:FORCL=1TO32:A$=MID$("    1    3   5    7   9   11    ",CL,1):IFA$<>" "THENPOKEPK+CL,ASC(A$)
1040 NEXT:POKE16895,96
1050 FORX=9TO54STEP9
1060 FORY=5TO8:SET(X,Y,5)
1070 SET(X+5,Y,5):NEXT
1090 FORY=8TO12:SET(X,Y,5)
1100 SET(X+5,Y,5):NEXT
1110 FORY=12TO15:SET(X,Y,5):SET(X+5,Y,5):NEXT
1120 FORY=27TO25STEP-1:SET(X,Y,5)
1130 SET(X+5,Y,5):NEXT
1150 FORY=24TO21STEP-1:SET(X,Y,5)
1160 SET(X+5,Y,5):NEXT
1170 FORY=20TO18STEP-1:SET(X,Y,5):SET(X+5,Y,5):NEXT
1180 NEXT X
1190 GOSUB1550
1200 FOR X=4 TO 9:SET(.5*X,12*OS+2,8):SET(.5*X,30*OS+2,8)
1230 SET(.5*X,19*OS,3):SET(.5*X,37*OS,3):NEXT
1240 FORP=1TO24:S=1:IF A(P)<0 THEN S=-1
1250 FORC=0TOA(P)STEPS:GOSUB 2000:NEXT:NEXT
1260 PRINT@0,"";
1270 RETURN
1280 S=1:IFA(L)<0THENS=-1
1290 FORC=0TOA(L)STEPS:GOSUB2000:NEXTC:RETURN
1295 S=1:IFA(F)<0THENS=-1
1296 FORC=0TOA(F)STEPS:GOSUB2000:NEXTC:RETURN
1300 REM ROLL DICE
1310 A(26)=RND(6):A(27)=RND(6):A(28)=0:A(29)=0
1320 IF A(26)=A(27) THEN A(28)=A(26):A(29)=A(26)
1325 FOR X=0 TO 3:A(30+X)=A(26+X):NEXT
1330 RETURN
1350 REM PRINT DICE
1360 GOSUB1500:IFT=0THENPRINT@2,"ROLL";:CL=3:GOTO1370
1365 PRINT@2,"ROLL";:CL=8
1370 FOR X=0 TO 4:SET(.5*X,1*OS,CL):NEXT
1375 FOR X=26 TO 29
1380 IF A(X)<>0 THEN ON A(X+4) GOSUB 1400, 1410, 1420, 1430, 1440, 1450
1390 NEXT:RETURN
1400 PRINT" 1";:RETURN
1410 PRINT" 2";:RETURN
1420 PRINT" 3";:RETURN
1430 PRINT" 4";:RETURN
1440 PRINT" 5";:RETURN
1450 PRINT" 6";:RETURN
1500 REM CLEAR TOP LINE
1510 PRINT@0,"                                ";
1520 RETURN
1550 REM UPDATE BAR/OFF COUNTERS
1560 PRINT@96,"   ";:PRINT@96,STR$(B);:PRINT@224,"   ";:PRINT@224,STR$(A(0));
1570 PRINT@288,"";:PRINT@288,STR$(O);:PRINT@416,"   ";:PRINT@416,STR$(A(25));
1580 PRINT@0,"";:RETURN
1600 REM ERASE A PIECE
1610 IF C=0 THEN RETURN
1620 C=ABS(C):X=9*P+9:Y=44-C*2
1630 IF P>12 THEN X=9*(26-P):Y=6+C*2
1640 IF C>9 THEN RETURN
1650 IF P-INT(P/2)*2 THEN 1750
1660 IF C=9 THEN 1740
1680 RESET((.5*X)-1+.5,Y*OS):RESET((.5*X)+1+.5,Y*OS):SET((.5*X)+.5,Y*OS,5):RETURN
1740 FOR A=X-1 TO X+4:SET(.5*A,Y*OS,4):NEXT:RETURN
1750 IF C=9 THEN 1820
1760 RESET((.5*X)-1+.5,Y*OS):RESET((.5*X)+1+.5,Y*OS):SET((.5*X)+.5,Y*OS,5):RETURN
1820 FOR A=X-1 TO X+4:SET(.5*A,Y*OS,4):NEXT:RETURN
1850 REM PRINT TWO DIGIT NUMBER
1860 IFX=0THENPRINT" BAR ";:RETURN
1997 IFX=25THENPRINT" OFF ";:RETURN
1870 PRINTX;:RETURN
2000 REM DRAW A PIECE
2010 IF C=0 THEN RETURN
2030 X=9*P+9:Y=44-ABS(C)*2
2040 IF P>12 THEN X=9*(26-P):Y=6+ABS(C)*2
2050 IF ABS(C)>9 THEN RETURN
2060 CL=3:IF C<0 THENCL=8
2070 FORA=X-1TOX+3:SET(.5*A,Y*OS,CL):NEXT
2080 RETURN
