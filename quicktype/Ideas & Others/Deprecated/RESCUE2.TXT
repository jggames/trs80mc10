0 DIMK(255),TW,PK,MC
1 TW=2:PK=17023:K(44)=1:K(65)=2:K(46)=3:K(83)=4:K(30)=5:K(87)=6:K(32)=7:MC=16384
2 CLS0:GOTO1100:GOTO15
3 IFX>3THENX1=X-2
4 RETURN
5 IFX>2THENX1=X-1
6 RETURN
10 IFX<30THENX1=X+2
11 RETURN
12 IFX<30THENX1=X+1
13 RETURN
14 Y1=Y-.5:BU=1:RETURN
15 D1=0:SC=0:SL=3
20 CLS0:S=25:D=-1:P1=6:P2=0:SH$=CHR$(166)+CHR$(169):FL$=CHR$(244)+CHR$(248):BL$=CHR$(128)+CHR$(128)
25 FORI=13TO15:PRINT@(I*32+1),"x";:POKEMC+I*32+31,24:NEXT
30 L=RND(19)+4:PRINT@447+L,CHR$(128);CHR$(135);CHR$(143);CHR$(143);CHR$(139);
31 PRINT@479+L,CHR$(143);CHR$(143);CHR$(143);CHR$(143);CHR$(143);CHR$(143);:B=0:G=0
40 FORI=1TOD1*10+15:PRINT@(1+RND(9))*32+RND(31),CHR$(179);:NEXT
41 PRINT@479+L,SC;:FORAZ=1TOSL:SOUND37,1:SOUND1,1:NEXT
50 S=S+D:PRINT@S-1,M1$;:PRINT@S-1+32,M2$;:IFS<2ORS>25THEND=-D
65 IF(PEEK(PK)ANDPEEK(TW))>0THEN80
70 FORW=1TO25:NEXT:GOTO50
80 X=S+3:Y=1
100 BU=0:U=0:X1=X:ONK(PEEK(PK)ANDPEEK(TW))GOSUB3,5,10,12,14,14,120
120 Y1=Y+1
130 FORI=X1-1TOX1:A=PEEK(MC+(I+(INT(Y1+.5)*32))):IFA=179THEN:I=X1:NEXT:GOTO500
133 IFB=179THEN500
135 NEXT
140 PRINT@X-1+(INT(Y+.5)*32),BL$;:PRINT@(X-1+(INT(Y+.5)*32))+32,BL$;
141 X=X1:Y=Y1:PRINT@X-1+(INT(Y+.5)*32),SH$;
145 B=PEEK(MC+(X+(INT(Y+1.5)*32)))
146 IFBU=1THENPRINT@(X-1+(INT(Y+.5)*32))+32,FL$;:FORAZ=1TO3:SOUNDY*8,1:SOUNDY*3,1:NEXT
150 IFX=L+2ANDINT(Y+.5)=13THEN400
155 IFINT(Y)>12THEN500
160 FORW=1TO25:NEXT:GOTO100
200 X=L+2:Y=13:FORI=2TO12:PRINT@I*32,"";:FORZ=1TO32:PRINTCHR$(128);:NEXT:NEXT
205 FORI=1TOD1*15+20:PRINT@(1+RND(9))*32+RND(31),CHR$(179);:NEXT:PRINT@479+L,SC;
207 PRINT@447+L,CHR$(128);CHR$(135);CHR$(143);CHR$(143);CHR$(139);
210 X1=X:Y1=Y:GOTO215:FORAZ=1TO2:SOUNDY*8,1:SOUNDY*3,1:NEXT
215 ONK(PEEK(PK)ANDPEEK(TW))GOSUB3,5,10,12
220 C=C+1:IFC=3THENC=0:Y1=Y-1
225 FORI=X1-1TOX1:A=PEEK(MC+I+(INT(Y1+.5)*32)):IFA=179THEN500
227 NEXT
230 PRINT@X-1+(INT(Y+.5)*32),BL$;:PRINT@(X-1+(INT(Y+.5)*32))+32,BL$;:X=X1:Y=Y1
231 PRINT@X-1+(INT(Y+.5)*32),SH$;:PRINT@(X-1+(INT(Y+.5)*32))+32,FL$;
240 IF(PEEK(PK)ANDPEEK(TW))=32ANDY>3.5THENGOSUB340
250 IFG=1THENGOSUB350
255 IFINT(Y)>1THEN210
270 IFX<>S+3THEN500
275 P2=P2+1
280 FORI=2TO15:PRINT@I*32+2,"";:FORZ=1TO29:PRINTCHR$(128);:NEXT:NEXT
285 IFP1=0THENP1=6:D1=D1+1:GOTO650
290 GOTO30
320 RETURN
340 FORAZ=5TO35STEP10:SOUNDAZ,1:NEXT:IFG=1THENPRINT@XM+(INT(YM+.5)*32),CHR$(128);
345 G=1:XM=X-1:YM=Y
350 PRINT@XM+(INT(YM+.5)*32),CHR$(128);:YM=YM-1
360 A=PEEK(MC+(XM+(INT(YM+.5)*32))):IFA=179THENGOSUB390:RETURN
370 PRINT@XM+(INT(YM+.5)*32),CHR$(197);:IFYM<3THENPRINT@XM+(INT(YM+.5)*32),CHR$(128);:G=0
380 RETURN
390 PRINT@XM+(INT(YM+.5)*32),CHR$(128);:G=0:SC=SC+20:PRINT@479+L,SC;:RETURN
395 RETURN
400 PRINT@(X-1+(INT(Y+.5)*32))+32,"OK";:SC=SC+50:PRINT@L+479,SC;
405 Y=19-P1:X=31:X1=29:X2=L+5:IFP1<4THENY=16-P1:X=1:X1=2:X2=L-2
410 FORI=1TO4:FORJ=24TO25:POKEMC+(Y*32+X),J:FORW=1TO30:NEXTW,J
420 PRINT@Y*32+X1,"OK";:IFI/2=INT(I/2)THENGOSUB480
430 NEXTI:POKEMC+(Y*32+X),128
440 FORI=X+SGN(X1-X)TO X2 STEP SGN(X2-X)
450 FORJ=24TO25:POKEMC+480+I,J:FORW=1TO40:NEXTW
455 PRINT@0,"";:SOUND100,1:SOUND200,1:NEXTJ:POKEMC+(480+I),128:NEXTI
460 X=I+SGN(X1-X2):Y=15:FORI=1TO2:X=X+SGN(L-X1):Y=Y-1
465 FORJ=24TO25:POKEMC+(Y*32+X),J:FORW=1TO40:NEXTW:POKEMC+(Y*32+X),128:NEXTI
470 P1=P1-1:U=1:GOTO200
480 PRINT@Y*32+X1,CHR$(128);CHR$(128);
481 FORAZ=1TO2:FORAY=77TO7STEP-7:SOUNDAY,1:NEXT:FORAY=7TO77STEP7:SOUNDAY,1:NEXT:NEXT
485 RETURN
500 REM
520 X1=X-1:X2=X+1:X0=X:YS=2:PRINT@X-1+(INT(Y+.5)*32),BL$;:PRINT@(X-1+(INT(Y+.5)*32))+32,BL$;
530 FORI=YTO14:PRINT@X2+I*32,CHR$(169);:PRINT@X1+I*32,CHR$(166);:IFU=1THENPRINT@X0+I*32,"x";
540 FORAZ=1TO10:SOUNDRND(255),RND(2):NEXT:YS=YS-.25:IFYS<0THENYS=0
550 PRINT@X1+I*32,BL$;:PRINT@X2+I*32,BL$;:IFU=1THENPRINT@X0+I*32,BL$;
560 X1=X1-YS:IFX1<2THENX1=29
570 X2=X2+YS:IFX2>58THENX2=2
580 NEXTI:SL=SL-1:IFSL=0THEN600
590 GOTO280
600 CLS:PRINT@204,"GAME OVER";:PRINT@263,"YOUR SCORE IS";SC;
610 PRINT@322,"PRESS (ENTER) TO PLAY AGAIN";
620 IFINKEY$<>CHR$(13)THEN620
625 GOTO15
650 CLS0
660 PRINT@205,"*BONUS*";:PRINT@267,"SCORE=";SC;
670 FORI=1TOP2:PRINT@329+I*2,"x";:SC=SC+50:PRINT@273,SC;
680 FORAZ=ITO10:SOUNDI*7+15,1:SOUNDDI*7+30,1:NEXT:FORW=1TO50:NEXTW:NEXTI
690 FORAZ=ITO50:SOUND51-AZ,1:SOUNDAZ,1:NEXT
691 IFP2=6THENPRINT@203,"BONUS SHIP!";:SL=SL+1:FORP2=1TO4:FORAZ=40TO60STEP3:SOUNDAZ,1:SOUNDAZ+25,1:NEXT:NEXT
695 FORW=8TO16STEP2:FORAZ=WTOW-7STEP-1:SOUNDAZ,1:NEXT:NEXT
700 GOTO20
1100 CLS0
1110 PRINT@108,CHR$(143);CHR$(140);CHR$(140);CHR$(140);CHR$(140);CHR$(140);CHR$(140);CHR$(143);
1111 PRINT@140,CHR$(143);"space";CHR$(128);CHR$(143);:PRINT@172,CHR$(143);"rescue";CHR$(143);
1112 PRINT@204,CHR$(143);CHR$(131);CHR$(131);CHR$(131);CHR$(131);CHR$(131);CHR$(131);CHR$(143);
1120 PRINT@324,"ORIGINAL BY MATT RUTTER";:PRINT@358,"MC-10 BY JIM GERRIE";:POKEMC+140+6,32
1125 PRINT@485,"PRESS ANY KEY TO BEGIN";
1130 I$=INKEY$:Z=RND(1000):IFI$=""THEN:GOTO1130
1140 M1$=CHR$(128)+CHR$(141)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(142)+CHR$(128)
1150 M2$=CHR$(128)+CHR$(128)+CHR$(141)+CHR$(128)+CHR$(128)+CHR$(142)+CHR$(128)
1160 GOTO15
