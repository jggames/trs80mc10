0 GOTO120
10 C1=1:CC=32
20 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO20
30 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN20
40 RETURN
100 REM SANTA PARAVIA AND FIUMACCIO VERSION 6.3 
110 REM COPYRIGHT 1978 REV. GEORGE BLANK LEECHBURG, PA 15656
115 REM MC-10 EDITS BY JIM GERRIE
120 CLEAR1000
125 POKE49151,64:POKE17026,118:CLS
140 DIM A(6),B(6),C(6),D(6),G(6),H(6),I(6),J(6),K(6),L(6),M(6),N$(6),N(6),P(6),Q(6),R(6),S(6)
145 DIM T$(6),L$(6),O(6),T(7),U(6),V(6),Y(0),C1,CC,M$,X1,Y1,K,KV,A,B,C,F,H,I1,C1,S1,D1,Z2,X2,J1,ZZ,S,G1,D1
160 Y(0)=1400
170 DATA "SIR ","BARON ","COUNT ","MARQUIS ","DUKE ","GRAND DUKE ","PRINCE ","*H.R.H. KING "
180 DATA "LADY ","BARONESS ","COUNTESS ","MARQUISE ","DUCHESS ","GRAND DUCHESS ","PRINCESS ","*H.R.H. QUEEN "
190 DATA "SANTA PARAVIA","FIUMACCIO","TORRICELLA","MOLINETTO","FONTANILE","ROMAGNA"
210 PRINT"santa paravia and fiumaccio"
260 PRINT:FORKV=1TO16:READ A$:NEXT
270 PRINT"HOW MANY PEOPLE WANT TO PLAY?";
280 GOSUB5630:F=KV
300 IF F<1 THEN 315
310 IF F<=6 THEN 320
315 GOTO270
320 FOR A=1 TO F
330 READ L$(A)
350 CLS:PRINT"WHO IS THE RULER OF":PRINTL$(A);
360 INPUT N$(A):PRINT
370 N$(A)=N$(A)+" OF "+L$(A):L$(A)="MAP OF "+L$(A)
380 M$="IS "+N$(A):GOSUB10:PRINT"A MAN OR A WOMAN";
390 V(A)=0:INPUT A$
410 IF LEFT$(A$,1)="W" THEN V(A)=8
420 G(A)=25:H(A)=10:I(A)=5:J(A)=2:O(A)=1420+RND(35)
460 K(A)=1000:L(A)=10000:R(A)=5000:T(A)=1:U(A)=1
510 N(A)=4:P(A)=25:Q(A)=5:M(A)=25:S(A)=2000
560 NEXT
570 FOR A=1 TO F
580 RESTORE
590 B=V(A)+T(A)
600 FOR C=1 TO B
610 READ T$(A)
620 NEXT C
630 NEXT A
635 PRINT:INPUT"DO YOU DESIRE INSTRUCTIONS";A$:IF LEFT$(A$,1)="Y" THEN GOSUB10000
640 PRINT:PRINT"1. APPRENTICE 2. JOURNEYMAN":PRINT"3. MASTER 4. GRAND MASTER"
645 INPUT"ENTER LEVEL OF PLAY";U(0)
650 IF U(0)<1 THEN U(0)=1
655 IF U(0)>4 THEN U(0)=4
660 U(0)=U(0)+5
670 E=E+1
672 IF T(E)=-1 THEN E=E+1
674 IF(T(1)<1)AND(T(2)<1)AND(T(3)<1)AND(T(4)<1)AND(T(5)<1)AND(T(6)<1) THEN 11010
676 IF E>F THEN E=0:Y(0)=Y(0)+1:GOTO670
678 IF Y(0)>O(E) THEN 670
680 IF Y(0)=O(E) THEN 6100
710 GOSUB1300
740 GOSUB2040
750 GOSUB2950
760 GOSUB2420
780 GOSUB4020
790 GOSUB4550
800 GOTO670
810 CLS
820 PRINT TAB(9);"COMPARE NOBLES":PRINT
830 FOR A=1 TO F
834 M$=T$(A)+N$(A):GOSUB10
836 PRINT"HAS";N(A);"NOBLES":PRINT:IF A=3 AND F>3 THENGOSUB1110
850 NEXT A
854 PRINT
855 GOSUB1110
860 PRINT TAB(8);"COMPARE SOLDIERS":PRINT
870 FOR A=1 TO F
875 M$=T$(A)+N$(A):GOSUB10
880 PRINT"HAS";P(A);"SOLDIERS":PRINT:IF A=3 AND F>3 THENGOSUB1110
890 NEXT A
891 PRINT
895 GOSUB1110
900 PRINT TAB(9);"COMPARE CLERGY":PRINT
910 FOR A=1 TO F
920 M$=T$(A)+N$(A):GOSUB10
925 PRINT"HAS";Q(A);"CLERGYMEN":PRINT:IF A=3 AND F>3 THENGOSUB1110
930 NEXT A
931 PRINT
935 GOSUB1110
940 PRINT TAB(7);"COMPARE MERCHANTS":PRINT
950 FOR A=1 TO F
960 M$=T$(A)+N$(A):GOSUB10
965 PRINT"HAS";M(A);"MERCHANTS":PRINT:IF A=3 AND F>3 THENGOSUB1110
970 NEXT A
971 PRINT
975 GOSUB1110
980 PRINT TAB(9);"COMPARE SERFS":PRINT
990 FOR A=1 TO F
1000 M$=T$(A)+N$(A):GOSUB10
1005 PRINT"HAS";S(A);"SERFS":PRINT:IF A=3 AND F>3 THENGOSUB1110
1010 NEXT A
1011 PRINT
1015 GOSUB1110
1020 PRINT TAB(6);"COMPARE LAND OWNED":PRINT
1030 FOR A=1 TO F
1040 M$=T$(A)+N$(A):GOSUB10
1045 PRINT"HAS";L(A);"HECTARES OF LAND":PRINT:IF A=3 AND F>3 THENGOSUB1110
1050 NEXT A
1051 PRINT
1055 GOSUB1110
1060 PRINT TAB(7);"COMPARE TREASURIES":PRINT
1070 FOR A=1 TO F
1080 M$=T$(A)+N$(A):GOSUB10
1085 PRINT"HAS ";K(A);"GOLD FLORINS":PRINT:IF A=3 AND F>3 THENGOSUB1110
1090 NEXT A
1091 PRINT
1110 GOSUB7000
1115 CLS
1120 RETURN
1130 I1=INT(I1):RETURN
1150 C1=INT(C1):RETURN
1170 S1=INT(S1):RETURN
1190 K(E)=INT(K(E)):RETURN
1210 Z=RND(A)*S(E)/100:Z2=INT(Z):PRINT Z2;"SERFS BORN THIS YEAR.":S(E)=S(E)+Z2:RETURN
1250 Z=RND(A)*S(E)/100:Z2=INT(Z):PRINT Z2;"SERFS DIED THIS YEAR.":S(E)=S(E)-Z2:RETURN
1300 W=INT((RND(5)+RND(6))/2)
1310 ON W GOTO1320,1340,1360,1380,1400
1320 W$="DROUGHT - FAMINE THREATENS"
1330 GOTO1410
1340 W$="BAD WEATHER - POOR HARVEST"
1350 GOTO1410
1360 W$="NORMAL WEATHER AND HARVEST"
1370 GOTO1410
1380 W$="GOOD WEATHER - FINE HARVEST"
1390 GOTO1410
1400 W$="EXCELLENT WEATHER & HARVEST"
1410 R=RND(50)
1430 R(E)=(R(E)*100-R(E)*R)/100
1440 X=L(E):Y=(S(E)-D(E)*100)*5:IF Y<0 THEN Y=0
1470 IF Y<X THEN X=Y
1480 Y=R(E)*2:IF Y<X THEN X=Y
1510 Y=W-.5:H1=X*Y:R(E)=R(E)+H1
1540 D1=N(E)*100+Q(E)*40+M(E)*30+P(E)*10+S(E)*5
1550 L=(3*W+RND(6)+RND(6)+10)/10
1570 IF H1<1 THEN Y=2:GOTO1610
1590 Y=D1/H1:IF Y>2 THEN Y=2
1610 IF Y<.8 THEN Y=.8
1620 L=L*Y:L=INT(L*10):L=L/10
1650 Z=6-W
1660 G=INT((Z*3+RND(5)+RND(5))/5*Y*20)
1670 RETURN
1690 PRINT"RATS ATE";R;"% OF YOUR RESERVES"
1700 PRINT W$:PRINT"(";INT(H1);"STERES )"
1720 PRINT:IF K(E)<32766 THEN GOSUB1190
1730 PRINT"GRAIN RESERVE =";INT(R(E));"STERES"
1740 PRINT"GRAIN DEMAND  =";INT(D1);"STERES"
1750 PRINT"PRICE OF GRAIN=";LEFT$(STR$(G)+"     ",7);"/1000ST"
1760 PRINT"PRICE OF LAND =";LEFT$(STR$(L)+"     ",7);"/HECTARE"
1770 PRINT"(THE TREASURY CONTAINS ";INT(K(E)):PRINT"GOLD FLORINS.)"
1780 RETURN
1790 J=(J(E)*300-500)*T(E):ON J(E)GOTO1810,1830,1850,1870
1810 J$="VERY FAIR"
1820 GOTO1880
1830 J$="MODERATE"
1840 GOTO1880
1850 J$="HARSH"
1860 GOTO1880
1870 J$="OUTRAGEOUS"
1880 Y=150-G(E)-H(E)-I(E):IF Y<1 THEN Y=1
1900 C1=(N(E)*180+Q(E)+75+M(E)*20)*(Y/100)+U(E)*100
1910 S1=(N(E)*50+M(E)*25+U(E)*10)*(Y/100)*(5-J(E))/2
1920 I1=N(E)*250+U(E)*20+(10*J(E)*N(E))*(Y/100)
1930 C1=C1*G(E)/100:IF C1<32760 THEN GOSUB1150
1950 S1=S1*H(E)/100:IF S1<32760 THEN GOSUB1170
1970 I1=I1*I(E)/100:IF I1<32760 THEN GOSUB1130
1990 PRINT"STATE REVENUES ARE ";J+C1+S1+I1:PRINT"GOLD FLORINS."
1995 PRINT"TAX";TAB(14);"RATE";TAB(25);"AM'T"
2000 PRINT"CUSTOMS DUTY";TAB(13);G(E);"%";TAB(24);C1
2005 PRINT"SALES TAX";TAB(13);H(E);"%";TAB(24);S1
2010 PRINT"WEALTH TAX";TAB(13);I(E);"%";TAB(24);I1
2015 PRINT"JUSTICE";TAB(14);J$;TAB(24);J:PRINT
2030 RETURN
2040 CLS:PRINT TAB(10);Y(0);"A.D.":M$=T$(E)+N$(E):GOSUB10
2070 GOSUB1690
2080 PRINT"BUY  1.GRAIN 3.LAND"
2090 PRINT"SELL 2.GRAIN 4.LAND 0.CONTINUE"
2100 PRINT"YOUR CHOICE?";
2110 GOSUB5630:I1=KV
2130 IF I1>4 THEN 2100
2140 IF I1<1 THEN RETURN
2160 ON I1 GOTO2165,2235,2305,2345
2165 PRINT"HOW MUCH GRAIN DO YOU WANT"
2170 INPUT"TO BUY";I1:IFI1<0THEN2200
2180 K(E)=K(E)-(I1*G/1000):R(E)=R(E)+I1
2200 CLS:M$=T$(E)+N$(E):GOSUB10:GOSUB1720
2230 GOTO2080
2235 PRINT"HOW MUCH GRAIN DO YOU WANT"
2240 INPUT"TO SELL";I1:IFI1<0THEN2200
2250 IF I1>R(E)THEN PRINT"YOU DON'T HAVE IT.":PRINT:GOTO2235
2280 K(E)=K(E)+(I1*G/1000):R(E)=R(E)-I1
2300 GOTO2200
2305 PRINT"HOW MANY HECTARES DO YOU"
2310 INPUT"WANT TO BUY";I1:IFI1<0THEN2200
2320 L(E)=L(E)+I1:K(E)=K(E)-(I1*L)
2340 GOTO2200
2345 PRINT"HOW MANY HECTARES DO YOU"
2350 INPUT"WANT TO SELL";I1:IFI1<0THEN2200
2360 IF I1>(L(E)-5000)THEN PRINT"YOU CAN'T SELL THAT MUCH!":GOTO2345
2390 L(E)=L(E)-I1:K(E)=K(E)+(I1*L)
2410 GOTO2200
2420 CLS:PRINT TAB(10);Y(0);"A.D.":M$=T$(E)+N$(E):GOSUB10
2460 PRINT:GOSUB1790
2470 PRINT"0.NO CHANGE TO TAXES OR JUSTICE"
2480 PRINT"1.CUSTOMS DUTY 2.SALES TAX":PRINT"3.WEALTH TAX   4.JUSTICE"
2490 PRINT"YOUR CHOICE?";
2500 GOSUB5630:I=KV
2520 IF I>4 THEN 2490
2550 IF I<1 THEN 2870
2560 ON I GOTO2570,2640,2710,2780
2570 INPUT"NEW CUSTOMS DUTY (0-100)";I
2580 IF I>100 THEN I=100
2600 IF I<0 THEN I=0
2620 G(E)=I:GOTO2420
2640 INPUT"NEW SALES TAX (0 TO 50)";I
2650 IF(I>50) OR (I<0)THEN I=5
2690 H(E)=I:GOTO2420
2710 INPUT"NEW WEALTH TAX (0 TO 25)";I
2720 IF(I<0) OR (I>25)THEN I=0
2760 I(E)=I:GOTO2420
2780 PRINT"JUSTICE:":PRINT"1.VERY FAIR 2.MODERATE":PRINT"3.HARSH     4.OUTRAGEOUS";
2790 GOSUB5630:I=KV
2810 IF(I>4) OR (I<1)THEN I=1
2850 J(E)=I:GOTO2420
2870 K(E)=K(E)+C1+S1+I1+J
2880 IF K(E)<0 THEN K(E)=K(E)*1.5
2900 IF K(E)<(-10000*T(E)) THEN 5020
2940 RETURN
2950 PRINT"HOW MUCH GRAIN WILL YOU RELEASE":PRINT"FOR CONSUMPTION";
2960 INPUT G1
2970 IF G1<(R(E)/5) THEN PRINT"YOU MUST RELEASE AT LEAST 20%":PRINT"OF YOUR RESERVES.":GOTO2960
3000 IF G1>(R(E)-(R(E)/5))THEN PRINT"YOU MUST KEEP AT LEAST 20%":PRINT"OF YOUR RESERVES.":GOTO2960
3030 R(E)=R(E)-G1:CLS:PRINT TAB(10);Y(0);"A.D.":M$=T$(E)+N$(E):GOSUB10
3070 Z=G1/D1-1:IF Z>0 THEN Z=Z/2
3100 IF Z>.25 THEN Z=Z/10+.25
3120 Z2=INT(50-G(E)-H(E)-I(E)):IF Z2<0 THEN Z2=INT(Z2*J(E))
3150 Z2=INT(Z2/10):IF Z2>0 THEN Z2=INT(Z2+3-J(E))
3180 Z=Z+(Z2/10):IF Z>.5 THEN Z=.5
3210 IF G1<(D1-1)THEN 3600
3220 A=7:GOSUB1210
3240 A=3:GOSUB1250
3260 IF (G(E)+H(E))<35 THEN M(E)=INT(M(E)+RND(4))
3280 IF I(E)<RND(20) THEN N(E)=INT(N(E)+RND(2)-1):Q(E)=INT(Q(E)+RND(3)-1)
3310 IF G1<(D1+D1*.3) THEN 3490
3320 Z2=INT(S(E)/1000)
3330 Z=(G1-D1)/D1*10
3340 Z=Z*Z2*RND(25)+RND(40):IF Z>32000 THEN Z=32000
3370 Z2=INT(Z):Z=RND(Z2)
3390 PRINT Z;"SERFS MOVE TO THE CITY.":S(E)=S(E)+Z
3420 Z2=INT(Z/5):Z=RND(Z2):IF Z>50 THEN Z=50
3460 M(E)=INT(M(E)+Z)
3470 N(E)=INT(N(E)+1)
3480 Q(E)=INT(Q(E)+2)
3490 IF J(E)<3 THEN 3590
3500 J1=S(E)/100*(J(E)-2)*(J(E)-2)
3510 J1=RND(J1)
3570 S(E)=S(E)-J1
3580 PRINT J1;"SERFS FLEE HARSH JUSTICE."
3590 GOTO3720
3600 X=(D1-G1)/D1*100-9:X2=INT(X):IF X>65 THEN X=65
3620 IF X<0 THEN X2=0:X=0
3650 A=3:GOSUB1210
3670 A=X2+8:GOSUB1250
3710 GOTO3490
3720 Z=A(E)*75:K(E)=K(E)+Z:IF Z>0 THEN PRINT"YOUR MARKET EARNED ":PRINTZ;"FLORINS RENT."
3760 IF S(E)<32766 THEN S1=S(E):GOSUB1170:S(E)=S1
3800 Z=D(E)*(55+RND(250)):IF Z>0 THEN K(E)=K(E)+Z:PRINT"YOUR WOOLEN MILL EARNED":PRINTZ;"FLORINS."
3840 Z=P(E)*3:PRINT"YOU PAID YOUR SOLDIERS":PRINTZ;"FLORINS.":K(E)=K(E)-Z
3870 IF (L(E)/1000)>P(E) THEN 5170
3880 IF (L(E)/500)<P(E) THEN 3960
3920 FOR A=1TOF:IF A=E THEN 3950
3940 IF P(A)>(P(E)*2.4) THEN 5170
3950 NEXT
3960 GOSUB7000
3970 RETURN
4020 CLS:PRINT TAB(10);Y(0);"A.D.":M$=T$(E)+N$(E):GOSUB10
4050 PRINT"***** STATE  PURCHASES *****"
4060 PRINT"1.MARKETPLACE   1000 FLORINS"
4070 PRINT"2.WOOLEN MILL   2000 FLORINS"
4080 PRINT"3.PALACE        3000 FLORINS"
4090 PRINT"4.CATHEDRAL     5000 FLORINS"
4100 PRINT"5.EQUIP ONE PLATOON OF SERFS":PRINT"  AS SOLDIERS    500 FLORINS"
4110 PRINT"6.COMPARE STANDINGS":PRINT"7.";L$(E):PRINT"0.TO CONTINUE"
4120 GOSUB1770
4150 PRINT"YOUR CHOICE?";
4160 GOSUB5630:I=KV:IF I<1 THEN RETURN
4170 IF I>7 THEN 4020
4270 ON I GOSUB4360,4320,4410,4460,4510,810,8000
4310 GOTO4020
4320 D(E)=D(E)+1
4330 K(E)=K(E)-2000
4340 U(E)=U(E)+.25
4350 RETURN
4360 A(E)=A(E)+1
4370 M(E)=INT(M(E)+5)
4380 K(E)=K(E)-1000
4390 U(E)=U(E)+.1
4400 RETURN
4410 B(E)=B(E)+1
4420 N(E)=INT(N(E)+RND(2))
4430 K(E)=K(E)-3000
4440 U(E)=U(E)+.5
4450 RETURN
4460 C(E)=C(E)+1
4470 Q(E)=INT(Q(E)+RND(6))
4480 K(E)=K(E)-5000
4490 U(E)=U(E)+1
4500 RETURN
4510 P(E)=INT(P(E)+20)
4520 S(E)=S(E)-20
4530 K(E)=K(E)-500
4540 RETURN
4550 Z=0
4560 A=A(E):GOSUB4910
4580 A=B(E):GOSUB4910
4600 A=C(E):GOSUB4910
4620 A=D(E):GOSUB4910
4640 A=K(E)/5000:GOSUB4910
4660 A=L(E)/6000:GOSUB4910
4680 A=M(E)/50:GOSUB4910
4700 A=N(E)/5:GOSUB4910
4720 A=P(E)/50:GOSUB4910
4740 A=Q(E)/10:GOSUB4910
4760 A=S(E)/2000:GOSUB4910
4780 A=U(E)/5:GOSUB4910
4800 A=Z/U(0)-J(E):A=INT(A):IF A>8 THEN A=8
4820 IF (Y(0)+2)=O(E) THEN T(E)=T(E)+1
4830 IF T(E)>=A THEN 4900
4840 T(E)=A
4850 RESTORE
4860 FOR B=1 TO (T(E)+V(E)):READ T$(E):NEXT
4890 IF T(E)=8 THEN 4980
4900 RETURN
4910 IF A>10 THEN A=10
4940 A=INT(A)
4950 Z=Z+A
4960 RETURN
4980 CLS0:M$="GAME OVER. "+T$(E)+N$(E)+" WINS!":GOSUB10:POKE17026,15
4990 SOUND50,1:FORZ=1TO100:NEXT:SOUND50,1:FORZ=1TO100:NEXT:SOUND50,1:FORZ=1TO100:NEXT:SOUND100,8
5000 GOSUB8010:GOSUB810:GOTO11010
5020 CLS:M$=T$(E)+N$(E):GOSUB10:PRINT"IS BANKRUPT."
5040 PRINT"CREDITORS HAVE SEIZED MUCH OF":PRINT"YOUR ASSETS."
5050 PRINT
5060 GOSUB7000
5070 A(E)=0:B(E)=0:C(E)=0:D(E)=0:L(E)=6000:U(E)=1:K(E)=100
5160 RETURN
5170 Z=0:FOR A=1 TO F
5190 IF A=E THEN 5240
5200 IF P(A)<P(E)THEN 5240
5210 IF P(A)<(1.2*(L(A)/1000))THEN 5240
5220 IF P(A)>P(Z)THEN Z=A
5240 NEXT
5250 IF Z=0 THEN T$(0)="BARON ":N$(0)="PEPPONE OF MONTERANA":A1=RND(9000)+1000:GOTO5310
5300 A1=P(Z)*1000-L(Z)/3
5310 IF A1>(L(E)-5000) THEN A1=(L(E)-5000)/2
5330 M$=T$(Z)+N$(Z):GOSUB10:PRINT"INVADES AND SEIZES";A1
5335 PRINT"HECTARES OF LAND."
5340 L(Z)=L(Z)+A1:L(E)=L(E)-A1
5350 Z=RND(40):IF Z>(P(E)-15)THEN Z=P(E)-15
5380 M$=T$(E)+N$(E):GOSUB10:PRINT"LOSES";Z;"SOLDIERS IN BATTLE."
5390 P(E)=INT(P(E)-Z):GOSUB7000
5410 RETURN
5630 K$=INKEY$:KV=RND(1000):IFK$=""THEN5630
5640 KV=ASC(K$):IF KV<48 OR KV>57 THEN 5630
5650 KV=VAL(K$):PRINT KV:FOR ZZ=1 TO 50:NEXT
5670 RETURN
6100 CLS:PRINT"VERY SAD NEWS--"
6110 M$=T$(E)+N$(E)+" HAS JUST DIED "
6120 T(E)=-1:Y=RND(8)
6130 IF Y(0)>1450 THEN M$=M$+"OF OLD AGE AFTER A LONG REIGN."
6140 IF Y<4THEN M$=M$+"OF PNEUMONIA AFTER A COLD WINTER IN A DRAFTY CASTLE."
6150 IF Y=5THEN M$=M$+"IN A SMALLPOX EPIDEMIC."
6160 IF Y=4THEN M$=M$+"OF TYPHOID AFTER DRINKING CONTAMINATED WATER."
6170 IF Y=6THEN M$=M$+"AFTER BEING ATTACKED BY ROBBERS WHILE TRAVELLING."
6180 IF Y>6THEN M$=M$+"OF FOOD POISONING."
6190 GOSUB10:PRINT
6200 GOSUB7000
6210 GOSUB8000
6220 GOSUB810
6290 GOTO670
7000 INPUT"(PRESS ENTER)";A$:RETURN
8000 CLS0:PRINT@(32-LEN(L$(E)))/2,L$(E);
8010 H=E*16:IFB(E)>0THENGOSUB9000
8020 IFC(E)>0THENGOSUB9100
8030 IFA(E)>0THENGOSUB9200
8040 IFD(E)>0THENGOSUB9300
8050 GOSUB9400:GOSUB8300
8070 PRINT@480,"(PRESS ENTER)";
8080 IFINKEY$<>CHR$(13)THEN8080
8090 RETURN
8300 Z=S(E)-D(E)*100:IFZ<1 THEN Z=1
8305 Z=Z*5/L(E)*10+1:IF Z>10 THEN Z=10
8310 Z=INT(30-(Z*2))
8320 FORA=27TO35:SET(A,Z,1):NEXT A:RESET(30,Z):RESET(31,Z):RESET(33,Z)
8330 FORA=27TO35 STEP 2:SET(A,Z+1,1):NEXT:SET(26,Z-1,1):SET(35,Z-1,1):
8340 RETURN
9000 X1=38
9010 PRINT@X1+(32*1),CHR$(H+139);CHR$(H+135);CHR$(H+128);CHR$(H+128);CHR$(H+133);CHR$(H+131);CHR$(H+138);
9020 PRINT@X1+(32*2),CHR$(H+143);CHR$(H+143);CHR$(H+135);CHR$(H+135);CHR$(H+135);CHR$(H+143);CHR$(H+138);
9030 PRINT@X1+(32*3),CHR$(H+143);CHR$(H+142);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9040 PRINT@X1+(32*4),CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9050 PRINT@X1+(32*5),CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+139);CHR$(H+143);CHR$(H+138);
9060 PRINT@X1+(32*6),CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9090 PRINT@X1+(32*7),B(E);:RETURN
9100 X1=51
9110 PRINT@X1+(32*1),CHR$(H+128);CHR$(H+128);CHR$(H+129);CHR$(H+139);CHR$(H+128);CHR$(H+128);CHR$(H+128);
9120 PRINT@X1+(32*2),CHR$(H+128);CHR$(H+128);CHR$(H+131);CHR$(H+139);CHR$(H+130);CHR$(H+128);CHR$(H+128);
9130 PRINT@X1+(32*3),CHR$(H+129);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+139);CHR$(H+128);
9140 PRINT@X1+(32*4),CHR$(H+143);CHR$(H+143);CHR$(H+142);CHR$(H+132);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9150 PRINT@X1+(32*5),CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+135);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9160 PRINT@X1+(32*6),CHR$(H+143);CHR$(H+143);CHR$(H+138);CHR$(H+128);CHR$(H+143);CHR$(H+143);CHR$(H+138);
9190 PRINT@X1+(32*7),C(E);:RETURN
9200 X1=230
9250 PRINT@X1+(32*5),CHR$(H+135);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+143);CHR$(H+130);
9260 PRINT@X1+(32*6),CHR$(H+143);CHR$(H+133);CHR$(H+133);CHR$(H+133);CHR$(H+133);CHR$(H+133);CHR$(H+138);
9290 PRINT@X1+96,A(E);:RETURN
9300 X1=243
9340 PRINT@X1+(32*4),CHR$(H+131);CHR$(H+131);CHR$(H+131);CHR$(H+131);CHR$(H+131);CHR$(H+130);CHR$(H+128);
9350 PRINT@X1+(32*5),CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+130);
9360 PRINT@X1+(32*6),CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+139);CHR$(H+138);CHR$(H+138);
9390 PRINT@X1+96,D(E);:RETURN
9400 S=1:IF(P(E)-5)<(L(E)/1000)THENS=3:GOTO9410
9405 IF(P(E)/2)<(L(E)/1000)THENS=2
9410 K=E+1:FORY1=7TO28STEPS:SET(7,Y1,K):SET(55,Y1,K):NEXT
9420 FORX1=7TO55STEPS:SET(X1,7,K):SET(X1,28,K):NEXT:RETURN
10000 CLS:PRINT"   santa paravia and fiumaccio"
10010 PRINT"YOU ARE THE RULER OF A 15TH CENT";
10020 PRINT"ITALIAN CITY-STATE. IF YOU RULE"
10030 PRINT"WELL, YOU RECEIVE HIGHER TITLES.";
10040 PRINT"THE FIRST PLAYER TO BECOME KING"
10050 PRINT"OR QUEEN WINS. LIFE EXPECTANCY"
10060 PRINT"THEN WAS BRIEF, SO YOU MAY NOT"
10070 PRINT"LIVE LONG ENOUGH TO WIN. IF YOU"
10080 PRINT"ARE GENEROUS WITH GRAIN, YOUR"
10090 PRINT"POPULATION WILL GROW. IF THE"
10100 PRINT"HORSE AND PLOWMAN AT THE CENTRE"
10110 PRINT"OF THE MAP OF YOUR CITY IS CLOSE";
10120 PRINT"TO THE TOP WALL, ALL YOUR LAND"
10125 PRINT"IS IN PRODUCTION. A SOLID WALL"
10126 PRINT"INDICATES AN ADEQUATE DEFENSE."
10130 PRINT"(PRESS ENTER TO BEGIN GAME)";
10160 INPUT A$
10170 CLS:RETURN
11010 PRINT "GAME IS OVER.  PLAY AGAIN?"
11020 INPUT A$
11025 IFLEFT$(A$,1)="N"THENEND
11030 RUN
