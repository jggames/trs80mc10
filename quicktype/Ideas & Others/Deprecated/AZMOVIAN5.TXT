0 CLS:CLEAR1000:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
10 PRINT@236,"AFTERMATH":PRINT"    OF THE ASIMOVIAN DISASTER":PRINT@485,"PRESS ANY KEY TO BEGIN";
15 I$=INKEY$:R=RND(1000):IFI$=""THEN15
20 GOSUB 2600:REM INITIALISE
30 GOSUB 160
35 IF RO=13 THEN QQ=QQ+1:IF QQ=2 THEN 1385
40 GOTO 30
150 REM ******************
160 REM MAIN INPUT ROUTINE
170 SG=SG-5
180 IF SG<10 THEN M$="WARNING, CAPTAIN "+N$+", YOUR STRENGTH IS RUNNING LOW YOU NEED AN OXYGEN BOOST.":GOSUB1
190 IF SG<1 THEN 2300:REM DEATH
200 TA=TA+1
210 M$="CAPTAIN "+N$+", YOUR STRENGTH IS "+STR$(SG)+". "
220 IF WEA>0 THEN M$=M$+"YOU HAVE $"+STR$(WEA)+" IN SOLARIAN CREDITS. "
230 IF OXY>0 THEN M$=M$+"YOUR RESERVE TANKS HOLD "+STR$(OXY)+" UNITS OF OXYGEN. "
240 IF SUI=1 THEN M$=M$+"YOU ARE WEARING BATTLE ARMOR."
250 GOSUB1:IF IO=0 AND LR=0 AND TP=0 THEN 320
260 M$="YOU ARE CARRYING"
270 IF IO=1 THEN M$=M$+" AN ION GUN"
280 IF LR=1 THEN M$=M$+" A LASER"
290 IF LR+IO>0 AND TP=1 THEN M$=M$+" AND "
300 IF TP=1 THEN M$=M$+"THE MATTER TRANSPORTER"
310 M$=M$+".":GOSUB1
320 IF LI=0 THEN M$="IT IS TOO DARK TO SEE ANYTHING.":GOSUB1
330 IF LI=1 THEN GOSUB 990:REM ROOM DESCRIPTION
340 K=A(RO,7):REM K IS SET TO CONTENTS OF ROOM
350 IF K=0 THEN 440:REM ROOM IS EMPTY
360 IF K>9 THEN M$="THERE IS TREASURE HERE WORTH $"+STR$(K)+" SOLARIAN CREDITS.":GOSUB1:GOTO 440
370 PRINT"THERE IS DANGER HERE...":GOSUB 3520
380 IF K=-1 THEN MO$="BERSERK ANDROID":FF=5
390 IF K=-2 THEN MO$="DERANGED DEL-FIEVIAN":FF=10
400 IF K=-3 THEN MO$="RAMPAGING ROBOTIC DEVICE":FF=15
410 IF K=-4 THEN MO$="SNIGGERING GREEN ALIEN":FF=20
420 M$="IT IS A "+MO$+".":GOSUB1:
430 M$="YOUR PERSONAL DANGER METER REGISTERS "+STR$(FF)+"!":GOSUB1
440 GOSUB 3520
450 PRINT"WHAT DO YOU WANT TO DO";
460 INPUTA$:A$=LEFT$(A$,1)
470 IF K<0 AND A$<>"F" AND A$<>"R" THEN 450
480 PRINT"-------------------------------"
490 IF A$="Q" THEN 1390
500 IF A$="N" AND A(RO,1)=0 THEN M$="NO EXIT THAT WAY.":GOSUB1:GOTO 440
510 IF A$="S" AND A(RO,2)=0 THEN M$="THERE IS NO EXIT SOUTH.":GOSUB1:GOTO 440
520 IF A$="E" AND A(RO,3)=0 THEN M$="YOU CAN'T GO IN THAT DIRECTION.":GOSUB1:GOTO 440
530 IF A$="W" AND A(RO,4)=0 THEN M$="YOU CANNOT MOVE THROUGH WALLS.":GOSUB1:GOTO 440
540 IF A$="U" AND A(RO,5)=0 THEN M$="THERE IS NO WAY UP FROM HERE.":GOSUB1:GOTO 440
550 IF A$="D" AND A(RO,6)=0 THEN M$="YOU CANNOT DESCEND FROM HERE.":GOSUB1:GOTO 440
560 IF A$="R" AND RND(0)>.7 THEN 2420
570 IF A$="R" THEN K=0:M$="WHICH WAY DO YOU WANT TO RUN?":GOSUB1:GOTO 460
580 IF A$="F" AND A(RO,7)>-1 THEN M$="THERE IS NOTHING TO FIGHT HERE.":GOSUB1:GOTO 440
590 IF A$="I" THEN GOSUB 3010:RETURN
600 IF A$="B" AND OXY=0 THEN M$="YOU HAVE NO OXYGEN.":GOSUB1:GOTO 440
610 IF A$="P" OR A$="G" THEN GOSUB 2350:RETURN
620 IF A$="F" THEN 730
625 IF A$="M" AND (RO=13 OR TP=0) THEN M$="THAT IS NOT POSSIBLE.":GOSUB1:GOSUB 3520:GOTO 710
630 IF A$="M" THEN 4000
640 IF A$="B" THEN GOSUB 2480:RETURN
650 IF A$="N" THEN RO=A(RO,1)
660 IF A$="S" THEN RO=A(RO,2)
670 IF A$="E" THEN RO=A(RO,3)
680 IF A$="W" THEN RO=A(RO,4)
690 IF A$="U" THEN RO=A(RO,5)
700 IF A$="D" THEN RO=A(RO,6)
710 RETURN
720 REM **************************
730 REM FIGHT
740 R=RND(1000):IF INKEY$<>"" THEN 740
750 PRINT"PRESS ANY KEY TO FIGHT"
760 IF INKEY$="" THEN 760
770 IF SUI=1 THEN M$="YOUR SPACE ARMOR INCREASES YOUR CHANCE OF SUCCESS.":GOSUB1:FF=3*(INT(FF/4)):GOSUB 3520
780 CLS:FOR J=1 TO INT(RND(0)*6)+1:PRINT"*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*":NEXTJ
790 IF IO=0 AND LR=0 THEN M$="YOU HAVE NO WEAPONS. YOU MUST USE YOUR BARE HANDS.":GOSUB1:FF=INT(FF+FF/5):GOTO 870
800 IF IO=1 AND LR=0 THEN M$="YOU HAVE ONLY THE ION GUN TO FIGHT WITH.":GOSUB1:FF=4*INT(FF/5):GOTO 870
810 IF IO=0 AND LR=1 THEN M$="YOU MUST FIGHT WITH YOUR LASER.":GOSUB1:FF=3*INT(FF/4):GOTO 870
820 INPUT "WHICH WEAPON? 1-ION GUN, 2-LASER";Z
830 IF Z<1 OR Z>2 THEN 820
840 IF Z=1 THEN FF=4*INT(FF/5)
850 IF Z=2 THEN FF=3*INT(FF/4)
860 REM **********
870 REM THE BATTLE
880 PRINT
890 IF RND(0)>.5 THEN M$="THE "+MO$+" ATTACKS.":GOSUB1:GOTO930
900 M$="YOU ATTACK.":GOSUB1:GOSUB 3520
910 IF RND(0)>.5 THEN M$="YOU GIVE THE "+MO$+" A GLANCING BLOW.":GOSUB1:FF=INT(5*FF/6)
920 GOSUB 3520
930 IF RND(0)>.5 THEN M$="THE "+MO$+" WOUNDS YOU!":GOSUB1:SG=SG-5
940 IF RND(0)>.35 THEN 890
950 IF RND(0)*16>FF THEN M$="YOU SUCCEED IN KILLING THE "+MO$+".":GOSUB1:MK=MK+1:GOTO 970
960 M$="THE "+MO$+" SERIOUSLY WOUNDS YOU, BUT YOU MANAGE TO KILL IT.":GOSUB1:SG=INT(SG/2)
970 A(RO,7)=0:GOSUB 3410:GOSUB 3520:RETURN
980 REM *****************
990 REM ROOM DESCRIPTIONS
1000 PRINT"*******************************"
1010 ON RO GOSUB 1040,1100,1170,1230,1280,1360,1410,1470,1540,1620,1700,1730,1790,1860,1960,2030,2100,2160,2230
1020 RETURN
1030 REM ******
1040 REM ROOM 1
1050 M$="YOU ARE IN THE FORMER RECREATION CENTER. EQUIPMENT FOR MUSCLE-TRAINING IN ZERO GRAVITY LITTERS THE AREA.":GOSUB1
1080 RETURN
1090 REM ******
1100 REM ROOM 2
1110 M$="THIS WAS THE REPAIR AND MAINTENANCE HOLD OF THE SHIP. YOU CAN ONLY LEAVE IT VIA THE GIANT HANGAR DOOR TO "
1120 M$=M$+"THE WEST.":GOSUB1
1150 RETURN
1160 REM ******
1170 REM ROOM 3
1180 M$="YOU ARE IN THE WRECKED HOLD OF A SPACESHIP. THE CAVERNOUS INTERIOR IS LITTERED WITH FLOATING WRECKAGE, AS IF "
1205 M$=M$+"FROM SOME TERRIBLE EXPLOSION EONS AGO....":GOSUB1
1210 RETURN
1220 REM ******
1230 REM ROOM 4
1240 M$="":IF RND(0)>.6 THEN M$="WHAT A SUPERB SIGHT.... "
1242 M$=M$+"THE VIEW OF THE STARS FROM THIS OBSERVATION PLATFORM IS MAGNIFICENT, AS FAR AS THE EYE "
1246 M$=M$+"CAN SEE. THE SINGLE EXIT IS BACK WHERE YOU CAME FROM.":GOSUB1
1260 RETURN
1270 REM ******
1280 REM ROOM 5
1290 M$="ACRE UPON ACRE OF DRIED-UP HYDROPONIC PLANT BEDS STRETCH AROUND YOU. ONCE THIS AREA FED THE THOUSAND ON BOARD "
1300 M$=M$+"THE SHIP.":GOSUB1
1320 IF RND(0)>.5 THEN M$="THE SOLAR LAMPS ARE STILL SHINING.":GOSUB1:GOTO1340
1330 IF RND(0)>.5 THEN M$="A FEW PLANTS ARE STILL ALIVE TO THE EAST.":GOSUB1
1340 RETURN
1350 REM ******
1360 REM ROOM 6
1370 M$="YOU ARE FREE. YOU HAVE MADE IT. YOUR POD SAILS FREE INTO SPACE....":GOSUB1
1385 M$="YOUR SCORE WAS "+STR$(3*TA+5*SG+2*WEA+10*OXY+30*MK):GOSUB1
1390 END
1400 REM ******
1410 REM ROOM 7
1420 M$="YOU ARE IN THE CREW'S SLEEPING QUARTERS.":GOSUB1
1430 IF RND(0)>.5 THEN M$="MOST OF THE SLEEPING SHELLS ARE EMPTY.":GOSUB1:GOTO1450
1440 IF RND(0)>.5 THEN M$="THE FEW REMAINING CREW LIE SILENTLY IN THEIR ENDLESS, DREAMLESS SLEEP.":GOSUB1:GOTO1450
1445 IF RND(0)>.7 THEN M$="THERE ARE EXITS TO THE NORTH, EAST AND WEST.":GOSUB1
1450 RETURN
1460 REM ******
1470 REM ROOM 8
1480 M$="THE FORMER PASSENGER SUSPENDED ANIMATION DORMITORY...":GOSUB1
1490 IF RND(0)>.5 THEN M$="PASSENGERS FLOAT BY AT RANDOM.":GOSUB1
1500 IF RND(0)>.5 THEN M$="IT IS ENORMOUS, IT SEEMS TO GO ON FOREVER.":GOSUB1
1510 IF RND(0)>.9 THEN M$="THE ONLY EXITS ARE TO THE WEST AND SOUTH.":GOSUB1
1520 RETURN
1530 REM ******
1540 REM ROOM 9
1550 M$="THIS IS THE SHIP'S HOSPITAL, WHITE AND STERILE. A BUZZING SOUND, AND A STRANGE WARMTH COME FROM "
1570 M$=M$+"THE SOUTH, WHILE A CHILL IS FELT TO THE NORTH.":GOSUB1
1600 RETURN
1610 REM *******
1620 REM ROOM 10
1630 M$="FOOD FOR ALL THE CREW WAS PREPARED IN THIS GALLEY. THE REMAINS FROM PREPARATIONS OF "
1650 M$=M$+"THE FINAL MEAL CAN BE SEEN. DOORS LEAVE THE GALLEY TO THE SOUTH AND TO THE WEST.":GOSUB1
1680 RETURN
1690 REM *******
1700 REM ROOM 11
1701 M$="AHA...THAT LOOKS LIKE AN ESCAPE SPACE POD. AND ITS OUTSIDE DIALS INDICATE THAT IT IS STILL IN "
1705 M$=M$+"PERFECT CONDITION.":GOSUB1
1710 RETURN
1720 REM *******
1730 REM ROOM 12
1740 M$="":IF RND(0)>.5 THEN M$="THIS IS THE SHIP'S MAIN NAVIGATION ROOM. "
1750 M$=M$+"STRANGE MACHINERY LINES THE WALLS, WHILE OVERHEAD, A HOLOGRAPHIC STAR MAP SLOWLY TURNS.":GOSUB1
1765 IF RND(0)>.8 THEN 1770
1766 M$="BY THE FLICKERING GREEN LIGHT YOU CAN JUST MAKE OUT EXITS TO THE SOUTH AND TO THE EAST.":GOSUB1
1770 RETURN
1780 REM *******
1790 REM ROOM 13
1800 M$="YOU ARE CAUGHT IN A DEADLY RADIATION FIELD. "
1810 IF RND(0)>.5 THEN M$=M$+"YOUR BODY TWISTS AND BURNS...":GOTO1840
1820 IF RND(0)>.5 THEN M$=M$+"SLOWLY YOU REALIZE THIS IS THE END.":GOTO1840
1830 M$=M$+"NO MATTER WHAT YOU DO YOU ARE DOOMED TO DIE HERE."
1840 GOSUB1:RETURN
1850 REM *******
1860 REM ROOM 14
1870 M$="THIS IS THE POWER CENTER OF THE SHIP THE CHARACTERISTIC BLUE METAL LIGHT OF THE STILL-FUNCTIONING ION DRIVE " 
1900 M$=M$+"FILLS THE ENGINE ROOM.":GOSUB1
1920 IF RND(0)>.9 THEN M$="THROUGH THE HAZE YOU CAN SEE DOORS TO THE NORTH AND WEST.":GOSUB1:GOTO1940
1930 IF RND(0)>.6 THEN M$="A SHAFT LEADS DOWNWARDS TO THE REPAIR CENTER.":GOSUB1
1940 RETURN
1950 REM *******
1960 REM ROOM 15
1970 M$="YOU ARE STANDING IN THE ANDROID STORAGE HOLD ROW UPON ROW OF METAL MEN STAND STIFFLY AT "
1990 M$=M$+"ATTENTION, AWAITING THE DISTINCTIVE SOUND OF THEIR LONG-DEAD CAPTAIN TO SET THEM INTO MOTION.":GOSUB1
2010 IF RND(0)>.5 THEN M$="A LIGHT COMES FROM THE WEST AND THROUGH THE GRAVITY WELL SET INTO THE FLOOR.":GOSUB1
2016 RETURN
2020 REM *******
2030 REM ROOM 16
2040 M$="ANOTHER CAVERNOUS, SEEMINGLY ENDLESS HOLD, THIS ONE CRAMMED WITH GOODS FOR TRADING..."
2060 IF RND(0)>.7 THEN M$=M$+"RARE METALS AND VENUSIAN SCULPTURES.":GOTO2080
2065 IF RND(0)>.8 THEN M$=M$+"PRESERVED SCALAPIAN DESERT FISH.":GOTO2080
2066 IF RND(0)>.7 THEN M$=M$+"FLASHING EBONY SCITH STONES FROM XARIAX IV.":GOTO2080
2067 IF RND(0)>.8 THEN M$=M$+"AWESOME TRADER ANT EFFIGIES FROM THE QWERTYIOPIAN EMPIRE.":GOTO2080
2070 M$=M$+"THE LIGHT IS STRONGER TO THE WEST."
2080 GOSUB1:RETURN
2090 REM *******
2100 REM ROOM 17
2110 M$="A STARK, METALLIC ROOM, REEKING OF LUBRICANTS WEAPONS LINE THE WALL, RANK UPON RANK. EXITS FOR "
2130 M$=M$+"SOLDIER ANDROIDS ARE TO THE NORTH AND THE EAST.":GOSUB1
2140 RETURN
2150 REM *******
2160 REM ROOM 18
2170 M$="ABOVE YOU IS THE GRAVITY SHAFT LEADING TO THE ENGINE ROOM. THIS IS THE SHIP REPAIR "
2190 M$=M$+"CENTER WITH EMERGENCY EXITS TO THE SOLDIER ANDROIDS STORAGE AND TO THE TRADING GOODS HOLD.":GOSUB1
2210 RETURN
2220 REM *******
2230 REM ROOM 19
2240 M$="YOU'VE STUMBLED ON THE SECRET COMMAND CENTER WHERE SCREENS BRING VIEWS FROM ALL AROUND "
2260 M$=M$+"THE SHIP. THERE ARE TWO EXITS....":GOSUB1
2270 IF RND(0)>.5 THEN M$="ONE OF WHICH IS THE GRAVITY WELL.":GOSUB1:GOTO2280
2275 M$="ONE OF WHICH LEADS TO THE GOODS HOLD.":GOSUB1
2280 RETURN
2290 REM ********
2300 REM DEAD END
2310 M$="YOU HAVE RUN OUT OF OXYGEN....":GOSUB1
2320 GOSUB 3520
2330 GOTO 1385
2340 REM ****************
2350 REM PICK UP TREASURE
2360 IF A(RO,7)<10 THEN M$="THERE IS NOTHING OF VALUE HERE.":GOSUB1:GOSUB 3520:RETURN
2370 IF LI=0 THEN M$="YOU CANNOT SEE WHERE IT IS.":GOSUB1:GOSUB 3520:RETURN
2380 WEA=WEA+A(RO,7)
2390 A(RO,7)=0
2400 RETURN
2410 REM ***************************
2420 REM UNSUCCESSFUL ATTEMPT TO RUN
2430 M$="NO YOU MUST STAND AND FIGHT.":GOSUB1
2440 A$="F"
2450 GOSUB 3520
2460 GOTO 620
2470 REM ****************
2480 REM REPLENISH OXYGEN
2490 CLS
2500 IF OXY<1 THEN RETURN
2510 M$="YOU HAVE "+STR$(OXY)+" UNITS OF OXYGEN LEFT.":GOSUB1
2520 INPUT"HOW MANY DO YOU WANT TO ADD TO  YOUR TANKS";Z
2540 IF Z>OXY THEN 2520
2550 OXY=INT(OXY-Z)
2560 SG=INT(SG+5*Z)
2570 GOSUB 3410:CLS
2580 RETURN
2590 REM **********
2600 REM INITIALISE
2610 CLS
2620 SG=INT(RND(0)*50)+75
2630 WEA=INT(RND(0)*50)+50
2640 OXY=INT(RND(0)*16)
2650 TA=0
2655 QQ=0
2660 MK=0:REM NO. OF ANDROIDS/ALIENS DESTROYED
2670 REM *****************
2680 REM SET UP SPACE SHIP
2690 DIM A(19,7)
2700 FOR B=1 TO 19
2710 FOR C=1 TO 7
2720 READ A(B,C)
2730 NEXT C
2740 NEXT B
2750 M$="WHAT IS YOUR NAME, SPACE HERO?":GOSUB1:INPUT N$
2760 CLS
2770 RO=3:REM STARTING POSITION (RO=ROOM NUMBER)
2780 LR=0
2790 TP=0
2800 IO=0
2810 SUI=0
2820 LI=0
2830 REM ***************
2840 REM ALLOT VALUABLES
2850 FOR J=1 TO 7
2860 M=INT(RND(0)*19)+1
2870 IF M=6 OR M=11 OR RO=13 OR A(M,7)<>0THEN 2860
2880 A(M,7)=INT(RND(0)*100)+10
2890 NEXT J
2900 REM *********************
2910 REM ALLOT ALIENS/ANDROIDS
2915 FOR T=1 TO 2
2920 FOR J=1 TO 4
2930 M=INT(RND(0)*18)+1
2940 IF M=6 OR M=11 OR M=13 OR A(M,7)<>0THEN 2930
2950 A(M,7)=-J
2960 NEXT J
2965 NEXT T
2990 RETURN
3000 REM **************
3010 REM SUPPLY ANDROID
3020 M$="A SUPPLY ANDROID HAS ARRIVED.":GOSUB1
3030 GOSUB 3260
3040 IF WEA<.1 THEN Z=0:GOTO 3130
3050 PRINT"1-NUCLEONIC LIGHT ($15)"
3060 PRINT"2-ION GUN ($10)"
3070 PRINT"3-LASER ($20)"
3080 PRINT"4-OXYGEN ($2 PER UNIT)"
3090 PRINT"5-MATTER TRANSPORTER ($30)"
3100 PRINT"6-COMBAT SUIT ($50)"
3110 PRINT"0-TO CONTINUE"
3120 M$="ENTER NO. OF ITEM REQUIRED:":GOSUB1:INPUT Z
3130 IF Z=0 THEN CLS:RETURN
3140 IF Z=1 THEN LI=1:WEA=WEA-15
3150 IF Z=2 THEN IO=1:WEA=WEA-10
3160 IF Z=3 THEN LR=1:WEA=WEA-20
3170 IF Z=5 THEN TP=1:WEA=WEA-30
3180 IF Z=6 THEN SUI=1:WEA=WEA-50
3190 IF WEA<0 THEN M$="YOU HAVE TRIED TO CHEAT ME!":GOSUB1:WEA=0:SUI=0:LI=0:IO=0:LR=0:TP=0:OXY=INT(OXY/4):GOSUB 3520
3200 IF Z<>4 THEN 3030
3210 INPUT "HOW MANY UNITS OF OXYGEN";Q:Q=INT(Q)
3220 IF 2*Q>WEA THEN M$="YOU HAVEN'T GOT ENOUGH MONEY":GOSUB1:GOTO 3210
3230 OXY=OXY+Q
3240 WEA=WEA-2*Q
3250 GOTO 3030
3260 IF WEA>0 THEN M$="YOU HAVE $"+STR$(WEA)+" IN SOLARIAN CREDITS.":GOSUB1
3270 IF WEA=0 THEN M$="YOU HAVE NO SOLARIAN CREDITS LEFT.":GOSUB1:GOSUB 3520:RETURN
3280 RETURN
3300 REM **************
3310 DATA 0,5,2,0,0,0,0
3320 DATA 0,0,0,1,0,0,0
3330 DATA 3,7,4,3,3,3,0
3340 DATA 0,0,0,3,0,0,0
3350 DATA 1,5,7,5,5,5,0
3360 DATA 6,6,6,6,6,6,0
3370 DATA 3,0,8,5,0,0,0
3380 DATA 8,12,8,7,8,8,0
3390 DATA 11,13,10,0,0,0,0
3400 DATA 0,14,0,9,0,0,0
3410 DATA 6,9,6,6,6,6,0
3420 DATA 8,16,19,0,0,0,0
3430 DATA 13,0,0,13,0,13,0
3440 DATA 10,0,15,17,0,18,0
3450 DATA 0,0,0,14,0,19,0
3460 DATA 12,16,16,18,16,16,0
3470 DATA 14,0,18,0,0,0,0
3480 DATA 0,0,16,17,14,0,0
3490 DATA 0,12,0,0,15,0,0
3500 REM **********
3510 REM DELAY LOOP
3520 FOR T=1 TO 1200:NEXT T
3530 RETURN
4000 RO=INT(RND(0)*19)+1:CLS2:SOUND250,1:CLS4:SOUND255,1:CLS
4010 IF RO=6 OR RO=11 THEN 4000
4020 GOTO 710
