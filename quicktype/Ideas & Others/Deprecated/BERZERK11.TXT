0 CLS0:CLEAR1000:DIMA$(11),B$(11),X(6),Y(6),R(6),O(2),P(2),K(255),RX(12,1),RY(6,1),DR(4,6,1),EX(3),B$
1 DIMA,B,C,D,E,F,G,H,I,J,K,L,M,N,O,P,Q,R,S,T,U,V,W,X,Y,Z,SX,SY,LV:GOTO100
2 RETURN
3 G=2:A=1:J=2*X(T)+4:K=2*Y(T):ONPOINT(J,K)GOTO34,51,2:ONPOINT(J,K+2)GOTO34,51,2,2
4 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:X(T)=X(T)+1:PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);:RETURN
5 G=1:A=-1:J=2*X(T)-2:K=2*Y(T):ONPOINT(J,K)GOTO34,51,2:ONPOINT(J,K+2)GOTO34,51,2,2
6 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:X(T)=X(T)-1:PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);:RETURN
7 B=1:J=2*X(T):K=2*Y(T)+4:ONPOINT(J,K)GOTO34,51,2:ONPOINT(J+2,K)GOTO34,51,2,2
8 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:Y(T)=Y(T)+1:PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);:RETURN
9 B=-1:J=2*X(T):K=2*Y(T)-2:ONPOINT(J,K)GOTO34,51,2:ONPOINT(J+2,K)GOTO34,51,2,2
10 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:Y(T)=Y(T)-1:PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);:RETURN
11 SX=A:U=A*2:SY=B:V=B*2:R=X(T)*2+O(A+1):S=Y(T)*2+P(B+1):C=4:ONPOINT(R,S)GOTO33,36,33,33,33:SET(R,S,C):RETURN
12 RESET(R,S):ONPOINT(R+U,S+V)GOTO33,36,33,33,33:R=R+U:S=S+V:SET(R,S,C):ONPOINT(R+SX,S+SY)GOSUB2,36,2,36:RETURN
13 ON1-(L=0)GOTO2:N=H*2:O=I*2:L=X*2+O(H+1):M=Y*2+P(I+1):ONPOINT(L,M)GOTO42,36,38,38,38:SET(L,M,7):RETURN
14 RESET(L,M):ONPOINT(L+N,M+O)GOTO42,36,38,38,38:L=L+N:M=M+O:SET(L,M,7):RETURN
15 FORE=1TO65000:FORT=1TO6:ONRND(R(T))GOTO18,19,18,19,18,19
16 G=1:A=0:B=0:ON1-((X-X(T))<=0)GOSUB3:ON1-((X-X(T))>=0)GOSUB5:ON1-((Y-Y(T))<=0)GOSUB7:ON1-((Y-Y(T))>=0)GOSUB9:ONRGOSUB11
18 ON1-(R>1)GOSUB2,12:ON1-(L>0)GOSUB2,14:ONK(PEEK(P)ANDPEEK(2))GOSUB25,26,27,28,29,30,31,32,13:NEXT:NEXT:GOTO60
19 ONRND(4)GOSUB3,5,7,9:GOTO16
20 J=(X+H)*2:K=(Y+I)*2:ONPOINT(J,K)GOTO39,21,39,48,50:ONPOINT(J+2,K)GOTO39,21,39,48,50
21 ONPOINT(J,K+2)GOTO39,24,39,48,50:ONPOINT(J+2,K+2)GOTO39,24,39,48,50
24 PRINT@X+Y*F,B$;:PRINT@X+Y*F+F,B$;:X=X+H:Y=Y+I:PRINT@X+Y*F,A$(D);:PRINT@X+Y*F+F,B$(D);:RETURN
25 H=1:I=0:D=3:GOTO20
26 H=1:I=W:D=4:GOTO20
27 H=1:I=1:D=5:GOTO20
28 H=W:I=0:D=6:GOTO20
29 H=W:I=W:D=7:GOTO20
30 H=W:I=1:D=8:GOTO20
31 H=0:I=1:D=9:GOTO20
32 H=0:I=W:D=9:GOTO20
33 R=1:S=0:SX=0:SY=0:RETURN
34 PRINT@X(T)+Y(T)*F,B$(11);:PRINT@X(T)+Y(T)*F+F,B$(11);:SOUND1,1
35 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:R(T)=1:G=0:RETURN
36 RESET(R,S):SET(R+U,S+V,C):SOUND100,2:GOSUB33:PRINT@X+Y*F,A$(D);:PRINT@X+Y*F+F,B$(D);:HT=HT+1:IFHT>2THEN41
37 RETURN
38 L=0:M=0:N=0:O=0:RETURN
39 GOSUB24:PRINT@X+Y*F,B$(11);:PRINT@X+Y*F+F,B$(11);:SOUND200,4:GOTO41
41 PRINT@480,"YOU'RE DEAD";:E=65000:T=6:RETURN
42 SET(L+N,M+O,7)
43 GOSUB38:FORQ=1TO6:ONR(Q)GOTO45
44 ON1-(POINT(2*X(Q),2*Y(Q))=7ORPOINT(2*X(Q)+2,2*Y(Q))=7ORPOINT(2*X(Q),2*Y(Q)+2)=7ORPOINT(2*X(Q)+2,2*Y(Q)+2)=7)GOTO45,46
45 NEXT:RETURN
46 PRINT@X(Q)+Y(Q)*F,B$(11);:PRINT@X(Q)+Y(Q)*F+F,B$(11);:SOUND1,1
47 PRINT@X(Q)+Y(Q)*F,B$;:PRINT@X(Q)+Y(Q)*F+F,B$;:R(Q)=1:G=0:RETURN
48 GOSUB24:SET(R,S,C):SOUND100,2:GOSUB33:PRINT@X+Y*F,A$(D);:PRINT@X+Y*F+F,B$(D);:HT=HT+1:IFHT>2THEN41
49 RETURN
50 GOSUB24:PRINT@480,"YOU ESCAPED!";:E=65000:T=6:RETURN
51 PRINT@X+Y*F,B$(11);:PRINT@X+Y*F+F,B$(11);
52 PRINT@X(T)+Y(T)*F,B$;:PRINT@X(T)+Y(T)*F+F,B$;:Y(T)=Y(T)+B:X(T)=X(T)+A:PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);
53 SOUND200,4:GOTO41
60 PRINT@480,"GET READY.......";:SOUND1,10:CLS0:GOTO150
80 FORQ=1TO6:IFDR(EN,Q,0)=RY(Y,0)ANDDR(EN,Q,1)=RX(X,0)THENQ=6:NEXT:GOTO155
90 NEXT:X(T)=RX(X,0):Y(T)=RY(Y,0):RX(X,1)=1:RY(Y,1)=1:NEXT:GOTO165
100 B$=CHR$(128)+CHR$(128):LV=8:GOSUB500
105 GOSUB800:FORT=0TO11:FORY=0TO3:READA$:FORX=1TO4:I=VAL(MID$(A$,X,1)):IFI>0THENSET(X-1,Y,I)
110 NEXT:NEXT:A$(T)=CHR$(PEEK(MC))+CHR$(PEEK(MC+1)):B$(T)=CHR$(PEEK(MC+32))+CHR$(PEEK(MC+33))
120 GOSUB450:NEXT:GOSUB460:GOSUB514:GOSUB460:W=-1:F=32:O(0)=-2:O(1)=2:O(2)=4:P(0)=-2:P(1)=1:P(2)=4:I=0:CLS0
130 FORT=1TO12:READRX(T,0):NEXT:FORT=1TO6:READRY(T,0):NEXT:FOREN=1TO4:FORT=1TO6:READDR(EN,T,0),DR(EN,T,1):NEXT:NEXT
140 EN=4:EX=RND(3)
150 FORT=1TO6:R(T)=LV:NEXT:FORT=1TO12:RX(T,1)=0:NEXT:FORT=1TO6:RY(T,1)=0:NEXT:FORT=1TO6
155 X=RND(12):Y=RND(6):IFRX(X,1)=0ANDRY(Y,1)=0THEN80
160 GOTO155
165 FORX=0TO62:SET(X,0,3):SET(X,10,3):SET(X,20,3):SET(X,30,3):NEXT
170 FORY=1TO30:SET(0,Y,3):SET(16,Y,3):SET(32,Y,3):SET(48,Y,3):SET(62,Y,3):NEXT
180 GOSUB210:FORY=1TO30:SET(62,Y,3):NEXT:GOSUB300
190 Y=DR(EN,1,0):X=DR(EN,1,1):FORT=1TO6:G=RND(2):PRINT@X(T)+Y(T)*F,A$(G);:PRINT@X(T)+Y(T)*F+F,B$(G);:NEXT
200 PRINT@X+Y*32,A$(3);:PRINT@X+Y*32+32,B$(3);:D=3:C=4:R=1:GOSUB38:GOSUB33:EN=NE:EX=EX(RND(3)):GOTO15
210 FORY=0TO29STEP10:FORX=0TO62STEP16
220 READT:ONTGOSUB250,260,270:NEXT:NEXT:RETURN
250 FORR=X+1TOX+15:RESET(R,Y):NEXT:RETURN
260 FORS=Y+1TOY+9:RESET(X,S):NEXT:RETURN
270 GOSUB250:GOSUB260:RESET(X,Y):RETURN
300 ONEXGOSUB310,330,340,360:RETURN 
310 FORX=18TO31:SET(X,0,5):NEXT:NE=4:EX(1)=1:EX(2)=2:EX(3)=3:RETURN
330 FORY=12TO18:SET(0,Y,5):NEXT:NE=3:EX(1)=1:EX(2)=2:EX(3)=4:RETURN
340 FORY=12TO18:SET(62,Y,5):NEXT:NE=2:EX(1)=1:EX(2)=3:EX(3)=4:RETURN
360 FORX=34TO46:SET(X,30,5):NEXT:NE=1:EX(1)=2:EX(2)=3:EX(3)=4:RETURN
400 SOUND100,2:SOUND50,2:SOUND1,2
450 PRINT@0,B$;:PRINT@32,B$;:RETURN
460 PRINT@480+6,"PRESS ANY KEY TO CONTINUE";
470 I$=INKEY$:IFI$=""THEN470
480 RETURN
500 CLS0:PRINT@4,"THE ASTRO DATE IS 3200"
501 PRINT@(32*1)+4,"AND YOU ARE THE LAST"
502 PRINT@(32*2)+4,"SURVIVOR OF A SMALL GROUP"
503 PRINT@(32*3)+4,"OF EARTH PEOPLE WHO CAME"
504 PRINT@(32*4)+4,"TO EXPLORE THE PLANET"
505 PRINT@(32*5)+4,"MAZEON. SOON AFTER LANDING"
506 PRINT@(32*6)+4,"YOU DISCOVERED THE PLANET"
507 PRINT@(32*7)+4,"IS A DARK UNINHABITABLE"
508 PRINT@(32*8)+4,"PLACE. BUT BY THEN IT WAS"
509 PRINT@(32*9)+4,"TOO LATE TO TURN BACK"
510 PRINT@(32*10)+4,"BECAUSE YOUR SPACE CRAFT"
511 PRINT@(32*11)+4,"HAD BEEN DESTROYED BY THE"
512 PRINT@(32*12)+4,"AUTO-MAZEONS. NOW YOUR ARE"
513 PRINT@(32*13)+4,"A PRISONER HERE.":RETURN
514 CLS0:PRINT"YOU ARE TRAPPED IN A MAZE"
515 PRINT"WHERE EVEN THE WALLS ARE"
516 PRINT"DEATH TO TOUCH. GRIM ROBOT"
517 PRINT"THUGS STALK YOU 
518 PRINT"RELENTLESSLY....":RETURN
800 P=17023:K(68)=1:K(69)=2:K(67)=3:K(65)=4:K(81)=5:K(90)=6:K(88)=7:K(87)=8:K(32)=9
805 K(72)=1:K(85)=2:K(78)=3:K(71)=4:K(84)=5:K(86)=6:K(66)=7:K(89)=8
810 MC=16384:RETURN
910 DATA "...."
920 DATA "...."
930 DATA "...."
940 DATA "...."
954 DATA "1111"
956 DATA "1.11"
958 DATA "1111"
960 DATA "1..1"
970 DATA "1111"
972 DATA "11.1"
973 DATA "1111"
974 DATA "1..1"
1010 DATA ".2.."
1020 DATA "2222"
1030 DATA ".2.."
1040 DATA "2.2."
2010 DATA ".2.2"
2020 DATA "222."
2030 DATA ".2.."
2040 DATA "2.2."
3010 DATA ".2.."
3020 DATA "222."
3030 DATA ".2.2"
3040 DATA "2.2."
4010 DATA "..2."
4020 DATA "2222"
4030 DATA "..2."
4040 DATA ".2.2"
5010 DATA "2.2."
5020 DATA ".222"
5030 DATA "..2."
5040 DATA ".2.2"
6010 DATA "..2."
6020 DATA ".222"
6030 DATA "2.2."
6040 DATA ".2.2"
7010 DATA "..2."
7020 DATA ".222"
7030 DATA "..2."
7040 DATA ".2.2"
7110 DATA "..2."
7120 DATA ".222"
7130 DATA "..2."
7140 DATA ".2.2"
7210 DATA "4.4."
7220 DATA ".4.4"
7230 DATA "4.4."
7240 DATA ".4.4"
7300 DATA 1,3,5,9,11,13,17,19,21,25,27,29
7310 DATA 1,3,6,8,11,13
7400 DATA 1,11,1,9,1,13,3,9,3,11,1,13
7410 DATA 6,1,6,3,6,5,8,1,8,3,8,5
7420 DATA 6,29,6,25,6,27,8,25,8,27,8,29
7430 DATA 13,19,11,17,11,19,11,21,13,17,13,21
8000 DATA 0,0,2,2,1,2,3,2,1,0,3,2
8010 DATA 0,2,2,2,1,1,3,1,1,3,3,3
8020 DATA 0,2,2,2,1,0,2,1,1,3,3,3
