0 DIMK(255),M(88),P(31,15),X(5),Y(5),D(5,7),A(7),B(7),N(7),F(7),C,O,P,T,Z,MC,G,H,I,J,K,L,M,N,A,B,C(7),U(31),V(63),T(7),W(2)
1 CLS:PRINT@256,"  mc-lode runner by jim gerrie";:GOSUB800:GOTO900
4 C(T)=W(RND(2))
5 M=C(T):GOSUB8:RETURN
7 ONK(N(T))GOTO50,50,50,50,8:RETURN
8 P=PEEK(P(A(T)+X(M),B(T)+Y(M))):ONK(P)GOTO9,12,15,9,9,51,12,9,9,61
9 POKEP(A(T),B(T)),N(T):N(T)=P:A(T)=A(T)+X(M):B(T)=B(T)+Y(M):POKEP(A(T),B(T)),D(M,T)
10 F(T)=PEEK(P(A(T),B(T)+1)):ONK(N(T))GOTO11:RETURN
11 F(T)=2:RETURN
12 C(T)=W(RND(2)):RETURN
13 M=5:GOSUB8:GOTO22
14 M=5:GOSUB8:NEXT:NEXT:GOTO40
15 P=G:G(T)=G(T)+1:SOUND100,1:ON1-(G(T)=W)GOSUB9,60:RETURN
16 M=3:ONK(N(T))GOSUB5,5,5,5,8,51,5:NEXT:NEXT:GOTO40
17 M=5:P=PEEK(P(A(T),B(T)+1)):ONK(P)GOSUB5,5,5,9,9,51,4:NEXT:NEXT:GOTO40
18 M=4:P=PEEK(P(A(T)+1,B(T))):ONK(P)GOSUB9,5,15,9,9,51,4:NEXT:NEXT:GOTO40
19 M=2:P=PEEK(P(A(T)-1,B(T))):ONK(P)GOSUB9,5,15,9,9,51,4:NEXT:NEXT:GOTO40
20 FORZ=1TO64000:FORC=2TOF:T=1:ONK(F(T))GOTO13,21,13,13,21,21,21,38,39
21 M=K(PEEK(K)ANDPEEK(2)):ONMGOSUB26,8,7,8,8
22 T=C:ONK(F(T))GOTO14,23,23,14,23,17,23,30,31
23 ONU(L+(B(T)-B(1)))GOTO17,16:ONV(N+(A(T)-A(1)))GOTO18,19
25 GOSUB4:NEXT:NEXT:GOTO40
26 ON1-(PEEK(P(A(T),B(T)))=142)GOTO27,28
27 P=PEEK(P(A(T)-1,B(T))):ON1-((P=GORP=160)ANDPEEK(P(A(T)-1,B(T)+1))=175)GOTO29:POKEP(A(T)-1,B(T)+1),160:RETURN
28 P=PEEK(P(A(T)+1,B(T))):ON1-((P=GORP=160)ANDPEEK(P(A(T)+1,B(T)+1))=175)GOTO29:POKEP(A(T)+1,B(T)+1),160:RETURN
29 RETURN
30 ON1-(G(T)>0)GOSUB36,37:POKEP(A(T),B(T)+1),190:F(T)=144:NEXT:NEXT:GOTO40
31 POKEP(A(T),B(T)+1),RND(2)+188:T(T)=T(T)+1:ON1-(T(T)<15)GOTO32:NEXT:NEXT:GOTO40
32 P=PEEK(P(A(T),B(T))):ONK(P)GOSUB29,29,29,34,29,35,29,29,29:NEXT:NEXT:GOTO40
33 ON1-(B(T)<14GOTO38,13
34 POKEP(A(T),B(T)+1),175:POKEP(A(T),B(T)),190:F(T)=175:T(T)=0:SOUND1,1:RETURN
35 POKEP(A(T),B(T)+1),175:POKEP(A(T),B(T)),190:F(T)=175:T(T)=0:SOUND200,1:G(T)=G(T)+1:RETURN
36 POKEP(A(T),B(T)),G:RETURN
37 POKEP(A(T),B(T)),146:G(T)=G(T)-1:RETURN
38 POKEP(A(T),B(T)),G:B(T)=B(T)+1:ON1-(B(T)<15)GOTO39:POKEP(A(T),B(T)),D(M,T):F(T)=PEEK(P(A(T),B(T)+1)):GOTO22
39 POKEP(A(T),B(T)),D(M,T):F(T)=144:GOTO22
40 PRINT@480+20,"HIT ANY KEY";:I$=INKEY$
41 I$=INKEY$:IFI$=""THEN41
42 GOTO900
50 RETURN
51 PRINT"DEAD":END
55 STOP
60 POKEP(E(1),E(2)),240:RETURN
61 PRINT@480,"NOW FOR LEVEL";LV+1;:Z=65000:C=F:RETURN
800 K(32)=1:K(65)=2:K(87)=3:K(83)=4:K(90)=5:MC=16384:K=17023:G=128:H=220:I=175:L=16:N=32:W(1)=2:W(2)=4
810 M(32)=128:M(88)=175:M(72)=220:M(45)=204:M(42)=146:M(79)=239:FORT=1TO5:C(T)=W(RND(2)):NEXT
815 K(204)=1:K(175)=2:K(239)=2:K(146)=3:K(128)=4:K(220)=5:K(142)=6:K(141)=6:K(189)=7:K(190)=7:K(160)=8:K(144)=9:K(240)=10
820 FORX=0TO31:FORY=0TO15:P(X,Y)=(32*Y)+X+MC:NEXT:NEXT
830 X(2)=-1:Y(2)=0:D(2,1)=141:FORT=2TO7:D(2,T)=189:NEXT
831 X(3)=0:Y(3)=-1:D(3,1)=141:FORT=2TO7:D(3,T)=189:NEXT
832 X(4)=1:Y(4)=0:D(4,1)=142:FORT=2TO7:D(4,T)=190:NEXT
833 X(5)=0:Y(5)=1:D(5,1)=142:FORT=2TO7:D(5,T)=190:NEXT
840 FORT=0TO15:U(T)=1:NEXT:FORT=17TO31:U(T)=2:NEXT
845 FORT=0TO31:V(T)=1:NEXT:FORT=33TO63:V(T)=2:NEXT
850 RETURN
900 CLS0:READLV:IFLV=999THENEND
905 FORT=16384TO16415:POKET,175:NEXT
910 FORJ=1TO14:READA$:FORC=1TO32:POKET,M(ASC(MID$(A$,C,1))):T=T+1:NEXT:NEXT
920 FORC=TTO16895:POKEC,175:NEXT:READW,E(1),E(2)
930 READA(1),B(1):N(1)=128:POKEP(A(1),B(1)),141:F(1)=POINT(A(1)*2,B(1)*2+2):G(1)=0
940 READF:F=F+1:FORT=2TOF:READA(T),B(T):N(T)=128:POKEP(A(T),B(T)),190:F(T)=POINT(A(T)*2,B(T)*2+2):G(T)=0:NEXT
990 GOTO20
1000 DATA 1
1001 DATA"X    *                H        X"
1002 DATA"XXXXXXXXHXXXXXXXX     H        X"
1003 DATA"XXXXXXXXH-------------H    *   X"
1004 DATA"X       H     XXH     XXXXXXXHXX"
1005 DATA"X       H     XXH            H X"
1006 DATA"X       H     XXH         *  H X"
1007 DATA"XXXHXXXXX     XXXXOOOXXHXXXXXXXX"
1008 DATA"X  H                   H       X"
1009 DATA"X  H                   H       X"
1010 DATA"XXXXXXXXXXHXXXXXXXXXXXXHXXXXXXXX"
1011 DATA"X         H            H       X"
1012 DATA"X       * H------------H   *   X"
1013 DATA"X    HXXXXXX           XXXXXXXHX"
1014 DATA"X    H              *         HX"
1015 DATA 6,22,0,14,14
1016 DATA 3,16,9,5,6,27,6
1100 DATA 2
1101 DATA"X   *                         HX"
1102 DATA"XHOOXOOH             *        HX"
1103 DATA"XH     H     HXXXXXXXXXH *    HX"
1104 DATA"XH *   H     H         HXXXXXXHX"
1105 DATA"XHXOXOXH-----H------   H       X"
1106 DATA"XH     H     H     HXXXOOOOOOOHX"
1107 DATA"XH     H     H  *  H          HX"
1108 DATA"XH     H  *  HXXXXXH          HX"
1109 DATA"XOXXXOXXOOXXOH         HXXXXHXXX"
1110 DATA"XOXXXO       H         H    H  X"
1111 DATA"XO*  O       H   ------H    H *X"
1112 DATA"XXXXXXXXXXHXXXOOOO     H   XXXXX"
1113 DATA"X         H            H       X"
1114 DATA"X         H            H       X"
1115 DATA 8,30,0,14,14
1116 DATA 3,5,4,5,8,27,5
1200 DATA 999
