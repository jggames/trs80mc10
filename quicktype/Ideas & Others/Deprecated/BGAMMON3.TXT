5 DIM A(64):CLS0:OS=.6667
10 O=0:B=0:A(0)=0:A(25)=0:Q=4
20 DATA 2,0,0,0,0,-5,0,-3,0,0,0,5,-5,0,0,0,3,0,5,0,0,0,0,-2
30 FOR X=1 TO 24:READ A(X):NEXT
40 GOSUB 1000
60 P=RND(6):C=RND(6)
70 PRINT@.5*0,"YOU";P;
75 PRINT"ME";C;
80 IF P=C THEN 60
90 IF P>C THEN PRINT"YOU";:GOTO110
100 PRINT"ME";
110 INPUT" FIRST";A$
120 IF P>C THEN 600
130 REM ME'S TURN
140 T=0:GOSUB 1300
150 GOSUB 1350:PRINT" THINKING...";
155 REM ARE ALL PIECES ON INNER BOARD?
160 C=1:FOR Z=0 TO 18:IF A(Z)>0 THEN C=0
170 NEXT Z
171 REM PLAYER HASN'T PASSED COMPUTER?
172 V=0:P=0:FOR Z=1 TO 24:IF (A(Z)<0)*P THEN V=1
174 IF A(Z)>0 THEN P=1
176 NEXT
178 H=0:L=0:R=0:FOR Y=0 TO 3:IF A(Y+26)=0 THEN 315
180 FOR X=0 TO 25-A(Y+26):E=0:IF A(X)<=0 THEN 310
190 IF (X=0)*(A(X)>0) THEN E=E+128
200 IF X+A(26+Y)=25 THEN E=E+64
210 IF (A(X)=1)*(A(X+A(26+Y)))*V THEN E=E+16
220 IF (A(X)<>2)*(A(X+A(26+Y)))*V THEN E=E+8
230 IF (A(X+A(26+Y))=-1)+(X<7) THEN E=E+32
240 IF X<19 THEN E=E+4
250 IF (A(X+A(26+Y))<-1)+((C=0)*(X+A(26+Y)>24)) THEN 310
260 E=E+1:IF (X+A(26+Y)=25)+(Y=3) THEN 300
270 FOR Z=0 TO 3:D=X+A(26+Y)-A(26-Z):IF (Y=Z)+(A(26+Z)=0)+(D<0)+(D>24) THEN 290
280 IF (A(D)=1)+(A(D)>2) THEN E=E+2:GOTO 300
290 NEXT
300 IF (H<E)+(H=E)*((X<L)+(X=L)*(R<A(26+Y))) THEN H=E:R=A(26+Y):L=X
310 NEXT
315 NEXT
320 IF (H<128)*(A(0)>0) THEN 580
330 IF H<>0 THEN 390
340 R=7:FOR Y=0 TO 3:IF (A(26+Y)<R)*(A(26+Y)>1) THEN R=A(26+Y):L=Y
350 NEXT:FOR X=0 TO 25-R:IF A(X)>0 THEN 580
360 NEXT:FOR X=26-R TO 24:IF A(X)>0 THEN A(26+L)=25-X:GOTO 178
370 NEXT:GOTO580
390 IF L>0 THEN C=A(L):P=L:GOSUB 1600
400 A(L)=A(L)-1:IF L=0 THEN GOSUB 1550
440 IF A(L+R)=-1 THEN B=B+1:A(L+R)=0:C=1:P=L+R:GOSUB 1600:GOSUB 1550
450 Y=A(L+R)+1:A(L+R)=Y:IF L+R<25 THEN C=A(L+R):P=L+R:GOSUB 2000:GOTO 453
452 GOSUB1550
453 FOR Z=0 TO 3:IF A(26+Z)<>R THEN NEXT Z
454 GOSUB 1350:PRINT" FROM";:X=L:GOSUB 1850:PRINT" TO";:X=L+R:GOSUB 1850:A(26+Z)=0
455 INPUTA$
460 IF A(25)=15 THEN 560
470 FOR X=0 TO 3:IF A(26+X)>0 THEN 150
480 NEXT:GOTO600
560 GOSUB 1500:PRINT@.5*0,"I WIN. AGAIN?";:GOTO 860
580 GOSUB 1350:PRINT" CAN'T";
590 INPUTA$
600 REM PLAYER'S TURN
605 REM IF POINT(.5*0,6*OS)<>Q THEN GOSUB 1000
610 T=1:GOSUB 1300
620 GOSUB 1350:IF B THEN F=25:PRINT" FROM BAR";:GOTO 640
630 R=999:N=99:INPUT" FROM";F:F=INT(F)
635 IF F=N THEN 130
637 REM IF POINT(.5*0,6*OS)<>Q THEN GOSUB 1000:GOTO 620
640 IF F=R THEN GOSUB 1000:GOTO 620
650 IF ((F=25)*(B=0))+(F<1)+(F>25) THEN 980
660 IF (F<25)*(A(F)>=0) THEN 980
670 PRINT@.5*41,"";:X=O:O=0:N=99:R=999:INPUT"TO";D:O=X:D=INT(D)
671 REM IF POINT(.5*0,6*OS)<>Q THEN GOSUB 1000:GOTO 620
672 IF D=N THEN 130
673 IF D=R THEN GOSUB 1000:GOTO 620
675 IF (D<0)+(D>24) THEN 980
680 IF A(D)>1 THEN 980
682 M=0:G=0:FOR W=0 TO 3:IF A(26+W)=F-D THEN 690
683 IF M<A(26+W) THEN M=A(26+W):G=W
684 NEXT W:IF (D<>0)+(B<>0) THEN 980
685 REM DICE EXCEED WHAT'S NEEDED TO MOVE OFF?
686 W=G:IF F<M THEN M=F+1
687 FOR X=M TO 6:IF A(X)<0 THEN 980
688 NEXT:GOTO706
689 REM PUT COMPUTER ON BAR?
690 IF (D>0)*(A(D)=1) THEN A(0)=A(0)+1:GOSUB 1550:A(D)=0
695 REM FROM BAR?
700 IF F=25 THEN B=B-1:GOSUB 1550:GOTO 730
702 REM FROM NORMAL
704 IF D<>0 THEN 710
706 FOR X=7 TO 24:IF A(X)<0 THEN 980
708 NEXT
710 C=A(F):P=F:GOSUB 1600:A(F)=A(F)+1
720 IF D=0 THEN 800
730 A(D)=A(D)-1:P=D:C=A(D):GOSUB 2000:A(26+W)=0
740 REM ANY MORE DICE LEFT?
750 FOR W=0 TO 3:IF A(26+W)<>0 THEN 620
760 NEXT W:GOTO 130
800 REM JUST MOVED OFF
840 O=O+1:GOSUB 1550:A(26+W)=0:IF O<15 THEN 750
850 GOSUB 1500:PRINT@.5*0,"YOU WIN! AGAIN?";
860 Y=1:N=0:INPUT A:IF (A<>Y)*(A<>N) THEN GOTO 820
870 IF A=Y THEN GOTO 10
880 END
970 REM ILLEGAL MOVE
980 GOSUB 1500:PRINT@.5*0,"";
990 INPUT"ILLEGAL";A$:GOTO620
1000 REM REDRAW SCREEN
1003 FOR X=0 TO 32:PRINT" ";:NEXTX
1004 PRINT@1*32, "BAR 24 2322212019 18171615 1413 ";
1005 PRINT@15*32,"OFF  1 2 3 4  5 6 7 8 9 10 1112";
1006 GOTO1050
1010 FOR X=0 TO 127:SET(.5*X,6*OS,5):SET(.5*X,44*OS,5):NEXT
1020 FOR Y=6 TO 44:SET(.5*0,Y*OS,5):SET(.5*1,Y*OS,5):SET(.5*12,Y*OS,5):SET(.5*13,Y*OS,5)
1030 SET(.5*126,Y*OS,5):SET(.5*127,Y*OS,5):NEXT
1040 FOR X=2 TO 11:SET(.5*X,25*OS,5):NEXT
1050 FOR X=18 TO 108 STEP 18
1060 FOR Y=8 TO 12:SET(.5*(X+2),Y*OS,5)
1070 SET(.5*(X+11),Y*OS,5):NEXT
1090 FOR Y=13 TO 18:SET(.5*(X+2),Y*OS,5)
1100 SET(.5*(X+11),Y*OS,5):NEXT
1110 FOR Y=19 TO 23:SET(.5*(X+2),Y*OS,5):SET(.5*(X+11),Y*OS,5):NEXT
1120 FOR Y=43 TO 38 STEP -1:SET(.5*(X+2),Y*OS,5)
1130 SET(.5*(X+11),Y*OS,5):NEXT
1150 FOR Y=37 TO 32 STEP -1:SET(.5*(X+2),Y*OS,5)
1160 SET(.5*(X+11),Y*OS,5):NEXT
1170 FOR Y=31 TO 27 STEP -1:SET(.5*(X+2),Y*OS,5):SET(.5*(X+11),Y*OS,5):NEXT
1180 NEXT
1190 GOSUB1550
1200 REM PRINT@.5*322,"";
1210 REM PRINT "BAR";:PRINT@.5*706,"OFF";
1220 FOR X=4 TO 9:SET(.5*X,12*OS,8):SET(.5*X,30*OS,8)
1230 SET(.5*X,19*OS,3):SET(.5*X,37*OS,3):NEXT
1240 FOR P=1 TO 24:S=1:IF A(P)<0 THEN S=-1
1250 FOR C=0 TO A(P) STEP S:GOSUB 2000:NEXT:NEXT
1260 PRINT@.5*0,"";
1270 RETURN
1300 REM ROLL DICE
1310 A(26)=RND(6):A(27)=RND(6):A(28)=0:A(29)=0
1320 IF A(26)=A(27) THEN A(28)=A(26):A(29)=A(26)
1325 FOR X=0 TO 3:A(30+X)=A(26+X):NEXT
1330 RETURN
1350 REM PRINT DICE
1360 GOSUB 1500:IF T=0 THENPRINT@.5*4,"ME";:GOTO1370
1365 PRINT@.5*4,"YOU";
1370 FOR X=0 TO 6:SET(.5*X,1*OS,8):NEXT
1375 FOR X=26 TO 29
1380 IF A(X)<>0 THEN ON A(X+4) GOSUB 1400, 1410, 1420, 1430, 1440, 1450
1390 NEXT:RETURN
1400 PRINT" 1";:RETURN
1410 PRINT" 2";:RETURN
1420 PRINT" 3";:RETURN
1430 PRINT" 4";:RETURN
1440 PRINT" 5";:RETURN
1450 PRINT" 6";:RETURN
1500 REM CLEAR TOP LINE
1510 PRINT@.5*0,"                                ";
1520 RETURN
1550 REM UPDATE BAR/OFF COUNTERS
1560 PRINT@.5*193,"";:PRINT B;:PRINT@.5*449,A(0);
1570 PRINT@.5*577,A(25);:PRINT@.5*833,O;
1580 PRINT@.5*0,"";:RETURN
1600 REM ERASE A PIECE
1610 IF C=0 THEN RETURN
1620 C=ABS(C):X=9*P+9:Y=44-C*2
1630 IF P>12 THEN X=9*(26-P):Y=6+C*2
1640 IF C>9 THEN RETURN
1650 IF P-INT(P/2)*2 THEN 1750
1660 IF C=9 THEN 1740
1670 ON INT((C-1)/3)+1 GOTO 1680, 1700, 1720
1680 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,5)
1690 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1700 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,5)
1710 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1720 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,5)
1730 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1740 FOR A=X-1 TO X+5:SET(.5*A,Y*OS,5):NEXT:RETURN
1750 IF C=9 THEN 1820
1755 ON INT((C-1)/3)+1 GOTO 1760, 1780, 1800
1760 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,4)
1770 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1780 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,4)
1790 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1800 RESET(.5*(X-1),Y*OS):RESET(.5*X,Y*OS):RESET(.5*(X+1),Y*OS):SET(.5*(X+2),Y*OS,4)
1810 RESET(.5*(X+3),Y*OS):RESET(.5*(X+4),Y*OS):RESET(.5*(X+5),Y*OS):RETURN
1820 FOR A=X-1 TO X+5:SET(.5*A,Y*OS,4):NEXT:RETURN
1850 REM PRINT TWO DIGIT NUMBER
1860 IFX=0THENPRINT" BAR";:RETURN
1997 IFX=25THENPRINT" OFF";:RETURN
1870 PRINTX;:RETURN
2000 REM DRAW A PIECE
2010 IF C=0 THEN RETURN
2030 X=9*P+9:Y=44-ABS(C)*2
2040 IF P>12 THEN X=9*(26-P):Y=6+ABS(C)*2
2050 IF ABS(C)>9 THEN RETURN
2060 CL=3:IF C<0 THENCL=8
2070 FOR A=X-1 TO X+5:SET(.5*A,Y*OS,CL):NEXT
2080 RETURN
