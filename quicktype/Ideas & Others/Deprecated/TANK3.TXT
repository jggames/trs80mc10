0 CLS:DIM D(8,4),P(2),B(2),T(2),L(2),V(2),R(2),K(255),P,D,A,X,Y,T,N,C,W,ML,MC,P1,P2,SS,BC,BB,N1,N2:GOTO8000
1 D=1:P=1:GOSUB50:RETURN
2 D=2:P=1:GOSUB50:RETURN
3 D=3:P=1:GOSUB50:RETURN
4 D=4:P=1:GOSUB50:RETURN
5 D=5:P=1:GOSUB50:RETURN
6 D=6:P=1:GOSUB50:RETURN
7 D=7:P=1:GOSUB50:RETURN
8 D=8:P=1:GOSUB50:RETURN
9 D=1:P=2:GOSUB50:RETURN
10 D=2:P=2:GOSUB50:RETURN
11 D=3:P=2:GOSUB50:RETURN
12 D=4:P=2:GOSUB50:RETURN
13 D=5:P=2:GOSUB50:RETURN
14 D=6:P=2:GOSUB50:RETURN
15 D=7:P=2:GOSUB50:RETURN
16 D=8:P=2:GOSUB50:RETURN
17 IFS(1)=0 THEN L(1)=B(1):S(1)=1:V(1)=R(1)
18 RETURN
19 IFS(2)=0 THEN L(2)=B(2):S(2)=1:V(2)=R(2)
20 RETURN
21 N=PEEK(L(P)+V(P)):IF N<>BC THEN GOSUB25:RETURN
22 L(P)=L(P)+V(P):POKE L(P),SS:S(P)=2:RETURN
23 N=PEEK(L(P)+V(P)):IF N<>BC THEN POKEL(P),BC:GOSUB25:RETURN
24 POKEL(P),BC:L(P)=L(P)+V(P):POKE L(P),SS:RETURN
25 IF N=BB THEN L(P)=L(P)+V(P):POKE L(P),BC:GOTO29
26 IF N=T(1) OR N=T(2) OR N=D(1,4) OR N=D(2,4) OR N=D(3,4) OR N=D(4,4) THEN W=W+P:ML=64000:GOTO29
28 IF N=SS THEN30
29 S(P)=0
30 RETURN
50 IFPEEK(P(P)+D(D,1))=D(D,4) THEN GOSUB60:RETURN
52 IFPEEK(P(P)+D(D,1))=BC THEN GOSUB 70:RETURN
54 RETURN
60 IFPEEK(P(P)+(D(D,1)*N2))=BC THENPOKEP(P),BC:P(P)=P(P)+D(D,1):POKEP(P),T(P):B(P)=P(P)+D(D,1):POKEB(P),D(D,4)
65 RETURN
70 IFPEEK(P(P)+D(D,1))=BC THENPOKEB(P),BC:B(P)=P(P)+D(D,1):POKEB(P),D(D,4):R(P)=D(D,1)
75 RETURN
100 FORML=1TO64000:A=0
110 I$=INKEY$:IFI$<>""THENA=ASC(I$)
120 ON K(A) GOSUB 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,19
130 P=1:ONS(P)GOSUB21,23
140 P=2:ONS(P)GOSUB21,23
150 NEXT
7000 IF W=1 THEN PRINT@480,"TANK # WON!  PLAY AGAIN (Y/N)? ";:POKEP(2),191:P1=P1+1
7005 IF W=2 THEN PRINT@480,"TANK * WON!  PLAY AGAIN (Y/N)? ";:POKEP(1),191:P2=P2+1
7010 IF W=3 THEN PRINT@480,"IT WAS A TIE! PLAY AGAIN (Y/N)?";:POKEP(1),191:POKEP(2),191:P1=P1+1:P2=P2+1
7020 FORX=MC+480TOMC+510:POKEX,PEEK(X)-64:NEXT:POKEMC+511,32
7030 SOUND 10,10
7050 I$=INKEY$:C=RND(1000):IFI$=""THEN7050
7060 IFI$="Y"THENCLS:GOTO8020
7070 IFI$="N"THENCLS:PRINT"# VICTORIES =";P1:PRINT"* VICTORIES =";P2:END
7080 GOTO7050
8000 GOSUB8500:P1=0:P2=0:N1=1:N2=2:SS=103:BC=96:BB=128:FOR Y=1TO8:FORX=1TO4:READ D(Y,X):NEXT:NEXT
8010 MC=16384:X=1:FOR Y=1TO16:READ I$:A=ASC(I$):K(A)=X:X=X+1:NEXT:K(32)=17:K(13)=18
8020 FORX=1TO100:POKEMC+RND(511),128:NEXT
8030 P=0:D=0:A=0:X=0:Y=0:T=0:N=0:C=0:W=0:ML=0:S(1)=0:S(2)=0
8100 P(1)=MC+64+4:B(1)=P(1)+1:R(1)=D(5,1):T(1)=35
8110 P(2)=MC+511-64-4:B(2)=P(2)+1:R(2)=D(5,1):T(2)=42
8150 FORX=1TO2:POKEP(X),T(X):POKEP(X)+D(5,1),D(5,4):NEXT
8160 C=RND(7):FORY=0TO31:SET(0,Y,C):SET(63,Y,C):NEXT
8170 FORX=0TO63:SET(X,0,C):SET(X,31,C):NEXT
8230 GOTO100
8500 PRINT"           ***TANK***          ":PRINT
8510 PRINT"USE THE FOLLOW KEYS TO MOVE:   ":PRINT
8520 PRINT"         Q W E          I O P  "
8530 PRINT"TANK1 =  A   D  TANK2 = K   ;  "
8540 PRINT"         Z X C          , . /  ":PRINT
8541 PRINT"FIRE =  <SPACE>   AND  <ENTER> ":PRINT
8542 PRINT"TRY TO HIT THE TANK'S BODY OR  "
8543 PRINT"GUN.  A TALLY OF EACH TANK'S   "
8545 PRINT"VICTORIES IS KEPT.  DESTROY    "
8546 PRINT"OBSTACLES TO GET A CLEAR SHOT. "
8550 PRINT:PRINT"    PRESS ANY KEY TO BEGIN ";
8560 I$=INKEY$:C=RND(1000):IFI$=""THEN8560
8570 CLS:RETURN
9000 DATA -33,-1,-1,92
9010 DATA -32,0,-1,97
9020 DATA -31,1,-1,111
9030 DATA -1,-1,0,109
9040 DATA 1,1,0,109
9050 DATA 31,-1,1,111
9060 DATA 32,0,1,97
9070 DATA 33,1,1,92
9100 DATA "Q","W","E","A","D","Z","X","C"
9110 DATA "I","O","P","K",";",",",".","/"
