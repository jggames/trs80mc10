10 REM ***********************                            
20 REM *SATELLITES OF JUPITER*                            
30 REM *     VERSION 1.2     *                            
40 REM *       2.6.1991      *                            
50 REM ***********************                            
60 REM                                                                           
70 DIM Z$(4),B$(4),Y$(4),M$(2),DA$(2),LT$(5)                                  
80 DIM TZ$(2),ND$(1),IN$(1)                                                   
90 REM                                                                           
100 CLS                                            
110 PRINT "      AN ASTRONOMY PROGRAM"                                       
120 PRINT " +---------------------------+"                              
130 PRINT " !    GALILEAN SATELLITES    !"                              
140 PRINT " +---------------------------+"                              
160 PRINT "   BY ERIC BURGESS F.R.A.S."                                      
170 PRINT "        VERSION APR.82"                                           
180 PRINT "    ALL RIGHTS RESERVED BY"                                       
190 PRINT "    S & T SOFTWARE SERVICE"                                       
200 PRINT " EDITED BY EARL W. PHILLIPS, JR"                                  
210 PRINT "           19.09.89."                                             
220 PRINT "   REEDITED FOR THE MC-10 BY"                                
230 PRINT "           JIM GERRIE"                                           
240 PRINT "           16.11.11."                                             
250 PRINT                                                               
260 FOR K=2000 TO 1 STEP-1:NEXT                                              
270 REM                                                                           
280 CLS                                            
290 PRINT "THIS PROGRAM SHOWS THE GALILEAN"                                    
300 PRINT "SATELLITES POSITIONED RELATIVE"                                     
310 PRINT "TO JUPITER AT THEIR APPROXIMATE"                                    
320 PRINT "RELATIVE POSITIONS."                                                 
340 PRINT "YOU CAN CHOOSE TO HAVE THEM"                              
350 PRINT "SHOWN WITH NORTH AT THE TOP, OR"
360 PRINT "WITH SOUTH AT THE TOP AS SEEN"                                
370 PRINT "IN A TELESCOPE, AND YOU CAN"
375 PRINT "SELECT UP TO FIVE DISPLAYS OF"                            
380 PRINT "THEIR CONFIGURATIONS AT UP TO"                             
390 PRINT "24 HOURS APART"                                                          
400 INPUT "DO YOU WANT SOUTH AT TOP (Y/N)? ";A$                                
410 IF A$="Y" OR A$="Y" THEN FL=1:GOTO 460                                    
420 IF A$<>"N" AND A$<>"N" THEN PRINT "INVALID RESPONSE":PRINT:GOTO 400      
430 FL=0                                                                       
440 REM                                                                           
460 CLS                                                      
470 REM ENTER DATE                                                                
480 REM                                                                           
490 REM                                                                           
500 LY=0                                                                       
510 PRINT "ENTER THE DATE:"                                                    
520 PRINT                                                                
530 INPUT "YEAR          ";Y$:Y=VAL(Y$)                                
540 IF Y=0 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 530                      
550 IF Y/4-INT(Y/4)=0 AND Y/100-INT(Y/100)<>0 THEN LY=1                        
560 INPUT "MONTH         ";M$                                          
570 M=VAL(M$)                                                                  
580 IF M=0 OR M>12 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 560              
590 INPUT "DAY           ";D$:DA=VAL(D$)                               
600 IF DA=0 OR DA>31 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 590            
610 IF LY=1 AND M=2 AND DA<30 THEN GOTO 650                                    
620 IF LY=0 AND M=2 AND DA<29 THEN GOTO 650                                    
630 IF M<>2 THEN GOTO 650                                                      
640 PRINT "INVALID RESPONSE": PRINT: GOTO 590                                  
650 INPUT "TIME ZONE     ";TZ$:TZ=VAL(TZ$)                             
660 IF TZ<-12 OR TZ>12 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 650          
670 INPUT "LOCAL TIME (DEC.HRS.)";LT$                                         
680 LT=VAL(LT$)                                                                
690 IF LT<0 OR LT>24 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 670            
700 PRINT                                                                      
710 INPUT "HOW MANY DISPLAYS (1 TO 5)";ND$: ND=VAL(ND$)                  
720 IF ND<1 OR ND>5 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 710             
730 INPUT "WHAT IS INTERVAL (UP TO 24 HR)? ";IN                                
740 IF IN<0 OR IN>24 THEN PRINT "INVALID RESPONSE":PRINT:GOTO 730            
750 CLS                                                   
760 PRINT "PLEASE WAIT..."                                     
770 L=1                                                                        
780 YP=Y: MP=M: DP=DA                                                            
790 IF L>1 GOTO 820                                                            
800 UT=LT+TZ                                                                   
810 REM
820 REM CORRECT FOR DAY MONTH AND YR ENDS                                         
830 REM
840 IF UT>24 THEN UT=UT-24: DA=DA+1
850 IF (LY=1 AND M=2 AND DA=30) THEN M=3:DA=1:GOTO 900
860 IF (LY=0 AND M=2 AND DA=29) THEN M=3:DA=1:GOTO 900
870 IF DA<31 GOTO 900
880 IF (M=4 OR M=6 OR M=9 OR M=11) THEN M=M+1: DA=1: GOTO 900                  
890 IF DA>31 THEN M=M+1:DA=1
900 IF M=13 THEN M=1:Y=Y+1
910 IF FL=1 THEN 930
920 YP=Y:MP=M:DP=DA
930 T=UT/24
940 YC=Y:MC=M:DC=DA+T
950 IF MC>2 THEN 980
960 YC=Y-1
970 MC=M+12
980 TD=INT(365.2422*YC)+INT((MC+1)*30.6001)+DC
990 REM
1000 YZ=INT(YC/100)
1010 REM 
1020 REM CALC DAYS SINCE 1900 EPOCH
1030 REM 
1040 I=TD-YZ+2+INT(YZ/4)-694025.5
1050 REM 
1060 REM DEG/DAY*NUMBR DAYS + POSTN AT EPOCH
1070 REM 
1080 A=I*203.40586+84.55061
1090 ZZ=A: GOSUB 2550
1100 A=ZZ
1110 B=I*101.2916323+41.50155
1120 ZZ=B:GOSUB 2550
1130 B=ZZ
1140 C=I*50.22571780750001+109.9594416067791
1150 ZZ=C: GOSUB 2550
1160 C=ZZ
1170 D=I*21.48798021+176.3586094824219
1180 ZZ=D: GOSUB 2550
1190 D=ZZ
1200 REM 
1210 REM ORBITAL MOTION IN PERIOD
1220 REM EARTH AND JUPITER
1230 REM 
1240 P1=I*.9856003+358.476
1250 ZZ=P1:GOSUB 2550
1260 P1=ZZ
1270 I2=I*8.308528E-02+225.3279
1280 ZZ=I2: GOSUB 2550:I2=ZZ
1290 P2=I*.9025179+221.647
1300 ZZ=P2:GOSUB 2550
1310 P2=ZZ
1320 P2=P2+(SIN((2*P1)*3.14159/180)/50)+(SIN(P1*3.14159/180)/.5210001)
1330 P3=(SIN((2*I2)*3.14159/180)*.1673)+(SIN(I2*3.14159/180)*5.5372)
1340 P2=P2-P3
1350 P1=SQR(28.07-(COS(P2*3.14159/180)*10.40599))
1360 J=(SIN((P2)*3.14159/180)/P1)
1370 X1=((SIN(J*3.14159/180))/SQR(1-(SIN(J*3.14159/180))^2)):GOSUB30000
1371 I3=57.2958*(A6)
1380 I3=I3*57.2958
1390 P1=P1/173
1400 REM 
1410 REM CALC RADIAL DISTANCE FROM JUPITER
1420 REM 
1430 S1=5.904897*SIN((A+I3-P3-(P1*203.405863))*3.14159/180)
1440 S2=9.397001*SIN((B+I3-P3-(P1*101.2916323))*3.14159/180)
1450 S3=14.9885*SIN((C+I3-P3-(P1*50.2257278075))*3.14159/180)
1460 S4=26.364*SIN((D+I3-P3-(P1*21.48798021))*3.14159/180)
1470 IF FL<>1 THEN 1560
1480 IF S1<0 THEN S1=0+ABS(S1):GOTO 1500
1490 IF S1>0 THEN S1=0-ABS(S1)
1500 IF S2<0 THEN S2=0+ABS(S2):GOTO 1520
1510 IF S2>0 THEN S2=0-ABS(S2)
1520 IF S3<0 THEN S3=0+ABS(S3):GOTO 1540
1530 IF S3>0 THEN S3=0-ABS(S3)
1540 IF S4<0 THEN S4=0+ABS(S4):GOTO 1560
1550 IF S4>0 THEN S4=0-ABS(S4)
1560 IF L>1 THEN GOTO 1780
1570 CLS
1580 PRINT "DIST IN TERMS OF JUPITER RADII"
1590 PRINT "WHICH IS 71,100 KM"
1610 PRINT "FOR DATE ";YP;MP;DP:PRINT"AT ";LT;" HRS TZ ";TZ
1620 PRINT "FOR DATE ";Y;M;DA:PRINT"AT ";UT;" HRS UT"
1630 EM=M
1650 PRINT "IO (I)..........";S1
1660 PRINT "EUROPA (E)......";S2
1670 PRINT "GANYMEDE (G)....";S3
1680 PRINT "CALLISTO (C)....";S4
1700 PRINT "MINUS IS TO THE LEFT OF JUPITER"
1710 PRINT "     PLUS IS TO THE RIGHT"
1730 IF FL=1 THEN PRINT "       WITH S AT THE TOP":GOTO 1750
1740 PRINT "       WITH N AT THE TOP"
1750 PRINT
1760 INPUT "HIT ENTER FOR GRAPHIC DISPLAY";A$
1770 CLS
1780 GOSUB 2350
1790 REM 
1800 REM REGENERATE LMT
1810 REM 
1820 LT=UT-TZ
1830 IF LT>0 THEN 1910
1840 DP=DA-1: LT=LT+24: IF DP>0 THEN 1910
1850 IF DP<1 THEN MP=EM-1
1860 IF (LY=1 AND MP=2)THEN DP=29: GOTO 1910
1870 IF (LY=0 AND MP=2)THEN DP=28: GOTO 1910
1880 IF (MP=4 OR MP=6 OR MP=9 OR MP=11) THEN DP=30: GOTO 1910
1890 DP=31
1900 PRINT
1910 IF MP=0 THEN MP=12: YP=Y-1
1920 IF L>1 THEN 1980
1930 IF FL=1 THEN PRINT "               S":GOTO 1950
1940 PRINT "               N"
1950 PRINT " JUPITER...J   !   IO.......I"
1960 PRINT " EUROPA....E   !   GANYMEDE.G"
1970 PRINT " CALLISTO..C   !"
1980 PRINT "---------------+----------------";
1990 PRINT YP;MP;DP;"AT";LT;"TZ";TZ
2000 PRINT " UT ";UT;"HRS ";EM;DA
2010 PRINT
2020 M1=15+INT(.7*VAL(B$(0)))
2030 PRINT TAB(M1);RIGHT$(B$(0),1);
2040 M2=15+INT(.7*VAL(B$(1)))
2050 PRINT TAB(M2);RIGHT$(B$(1),1);
2060 M3=15+INT(.7*VAL(B$(2)))
2070 PRINT TAB(M3);RIGHT$(B$(2),1);
2080 M4=15+INT(.7*VAL(B$(3)))
2090 PRINT TAB(M4);RIGHT$(B$(3),1);
2100 M5=15+INT(.7*VAL(B$(4)))
2110 PRINT TAB(M5);RIGHT$(B$(4),1)
2120 IF L=ND THEN GOTO 2260
2130 REM 
2140 REM INCREMENT FOR INTERVAL
2150 REM AND ADJUST MONTH ENDS
2160 REM 
2170 UT=UT+IN
2180 IF UT>24 THEN UT=UT-24:DA=DA+1
2190 IF(LY=1 AND M=2 AND DA=30)THEN M=3:DA=1:GOTO 2240
2200 IF(LY=0 AND M=2 AND DA=29)THEN M=3:DA=1:GOTO 2240
2210 IF DA<31 GOTO 2240
2220 IF(M=4 OR M=6 OR M=9 OR M=11) THEN M=M+1:DA=1:GOTO 2240
2230 IF DA>31 THEN M=M+1:DA=1
2240 IF M=13 THEN M=1:Y=Y+1
2250 L=L+1:GOTO 780
2260 PRINT
2270 INPUT "ANOTHER DATE (Y/N)";A$
2280 IF A$="N" OR A$="N" THEN 2320
2290 IF A$<>"Y" AND A$<>"Y" THEN PRINT "INVALID RESPONSE":PRINT:GOTO 2270
2300 CLS
2310 FL=0:GOTO 400
2320 CLS
2330 END
2340 REM 
2350 REM SUB TO DISPLAY SATELLITES
2360 REM 
2370 Z$(0)=STR$(S1)+"I":Z$(1)=STR$(S2)+"E"
2380 Z$(2)=STR$(S3)+"G":Z$(3)=STR$(S4)+"C"
2390 Z$(4)=STR$(0)+"J"
2400 FOR I=0 TO 4
2410 B$(I)="****"
2420 NEXT I
2430 K=0
2440 FOR I=0 TO 4
2450 IF Z$(I)="****" THEN 2530
2460 FOR J=1 TO 4
2470 IF Z$(J)="****" THEN 2490
2480 IF VAL(Z$(J))<VAL(Z$(I)) THEN I=J:GOTO 2490
2490 NEXT J
2500 B$(K)=Z$(I): Z$(I)="****"
2510 K=K+1
2520 I=-1
2530 NEXT I
2540 RETURN
2550 REM 
2560 ZZ=(ZZ/360-INT(ZZ/360))*360
2570 IF ZZ<0 THEN ZZ=ZZ+360
2580 RETURN
30000 REM *ARCTANGENT* INPUT X1, OUTPUT C1,A6
30670 REM C1 IS IN DEGREES. A6 IS IN RADIANS
30690 T=SGN(X1):X1=ABS(X1):C1=0
30700 IF X1>1THENC1=1:X1=1/X1
30710 A6=X1*X1
30720 B1=((2.86623E-3*A6-1.61657E-2)*A6+4.29096E-2)*A6
30730 B1=((((B1-7.5289E-2)*A6+.106563)*A6-.142089)*A6+.199936)*A6
30740 A6=((B1-.333332)*A6+1)*X1
30750 IF C1=1 THEN A6=1.570796-A6
30760 A6=T*A6:C1=A6*57.29577951:RETURN
