0 DIMX2(40),Y2(40),C2(40),X,Y,A,B,D,F,G,I,J,S,P,U,V,PI,SH,HS,SC,R,I$,HS$:PI=3.1415:GOTO1000
5 RESET(X,ABS(31-Y)):RETURN
1000 CLS:PRINT"            TREBUCHET":HS=10:HS$="JBG"
1010 PRINT " ------------------------------" 
2000 PRINT " A TREBUCHET IS A TYPE OF "
2001 PRINT " MEDIEVAL WEAPON THAT WAS USED "
2002 PRINT " TO THROW PROJECTILES INTO AN  "
2003 PRINT " ENEMY FORTIFICATION.  IN THIS "
2004 PRINT " GAME YOUR JOB IS TO THROW A   "
2005 PRINT " PROJECTILE FROM THE GREEN BASE"
2006 PRINT " TO HIT THE RED TARGET.  YOU   "
2007 PRINT " CONTROL THE FLIGHT BY         "
2008 PRINT " SELECTING THE INITIAL SPEED   "
2009 PRINT " AND ANGLE.  YOU HAVE 10 ROUNDS"
2010 PRINT " TO ACHIEVE YOUR SCORE/AVERAGE."
2011 PRINT " ------------------------------":PRINT
2012 SH=0
2013 PRINT@489,"PRESS ANY KEY";
2015 I$=INKEY$:G=RND(1000):IFI$=""THEN2015
2016 CLS0
2017 PRINT@(32*13)+26,"HIGH:";
2018 PRINT@(32*14)+26,RIGHT$(" "+STR$(HS),4);"%";
2019 PRINT@(32*15)+26," ";HS$;" ";
2020 REM PICK TWO RANDOM ELEVATIONS FOR THE GROUND
2025 G=26+RND(18)
2030 A=RND(25):IFINT(A/2)-(A/2)=0THEN2030
2040 B=RND(25):IFINT(B/2)-(B/2)=0THEN2040
2050 REM DRAW THE TERRAIN
2060 FORI=0TO24:FORJ=0TO31:ON1-(J<=A)GOTO2070,2075
2070 SET(I,ABS(31-J),6)
2075 NEXT:NEXT
2090 FORI=25TO49:FORJ=0TO31:ON1-(J<=B)GOTO2100,2105
2100 SET(I,ABS(31-J),6)
2105 NEXT:NEXT
2120 IFA>BTHENS=-1:GOTO2130
2121 S=1
2130 FORI=ATOBSTEPS:RESET(25,ABS(31-I)):NEXT
2160 REM SET(THE BASE
2170 SET(10, ABS(31-A),1)
2180 REM SET(THE TARGET
2190 SET(G+1,ABS(31-B),4)
2200 SET(G+2,ABS(31-B),4)
2210 SET(G+3,ABS(31-B),4)
2220 REM DRAW THE CASTLE
2230 FOR I=B+1 TO B+3
2240 RESET(G+1,ABS(31-I))
2250 RESET(G+2,ABS(31-I))
2260 RESET(G+3,ABS(31-I))
2270 RESET(G,ABS(31-(I+2)))
2280 RESET(G+4,ABS(31-(I+1)))
2290 RESET(G+2,ABS(31-(I+3)))
2300 NEXT
3000 REM STARTING VALUES FOR THE PROJECTILE
3010 X=10
3020 Y=A+1
3030 REM ASK THE USER FOR THE PROJECTILE'S INITIAL SPEED AND DIRECTION
3040 PRINT@(32*0)+26,"SPEED ";:PRINT@(32*1)+26,"(1-10)";:PRINT@(32*2)+26,"";:INPUTP
3050 IFP>=1ANDP<=10THEN3080
3060 SOUND1,1
3070 GOTO3040
3080 PRINT@(32*4)+26,"ANGLE ";:PRINT@(32*5)+26,"(0-90)";:PRINT@(32*6)+26,"";:INPUTF
3090 IFF>=0ANDF<=90THEN3130
3100 SOUND1,1
3120 GOTO3080
3130 SH=SH+1:GOSUB4020
3150 U=P/5*COS(F/180*PI)
3170 V=P/5*SIN(F/180*PI)
3172 REM INITIALIZE Z -- THE BACKGROUND COLOR
3175 Z=6
3180 REM REMOVE THE OLD POINT BY PAINTING IT THE SAME COLOR AS THE BACKGROUND
3190 ON1-(Y<32)GOTO3210:SET(X,ABS(31-Y),Z):ON1-(Z>0)GOSUB5
3210 X=X+U:Y=Y+V
3240 IF(X<25ANDY<A)ORX>50THEN4000
3260 IFX>25ANDY<BTHEN3500
3275 ON1-(Y<32)GOTO3280:Z=POINT(X,ABS(31-Y))
3280 ON1-(Y<32)GOTO3310:RESET(X,ABS(31-Y))
3310 V=V-0.02:GOTO3190
3500 REM CHECK IF THE PROJECTILE IS CLOSE ENOUGH TO THE TARGET
3510 IFABS(X-(G+2))>2THEN4000
3520 PRINT@(32*8)+26,"HIT!";CHR$(128);CHR$(128);
3530 REM DEFINE 40 FLAMES BY THE ARRAYS X2, Y2, AND C2
3570 FORI=1TO40:X2(I)=RND(5)+(G-1):Y2(I)=RND(5)+B:C2(I)=8:NEXT
3620 FORJ=1TO5:FORI=1TO40:ON1-(Y2(I)<B+3)GOTO3641:SET(X2(I),ABS(31-Y2(I)),6):RESET(X2(I),ABS(31-Y2(I))):GOTO3650
3641 SET(X2(I),ABS(31-Y2(I)),6)
3650 Y2(I)=Y2(I)+1
3660 SET(X2(I),ABS(31-Y2(I)),C2(I))
3670 IFRND(0)>0.8THENC2(I)=4
3680 IFRND(0)>0.8THENC2(I)=8
3690 IFRND(0)>0.8THENC2(I)=2
3700 IFRND(0)>0.3ANDY2(I)<B+9THEN3750
3710 ON1-(Y2(I)<B+3)GOTO3711:SET(X2(I),ABS(31-Y2(I)),6):RESET(X2(I),ABS(31-Y2(I))):GOTO3720
3711 SET(X2(I),ABS(31-Y2(I)),6) 
3720 X2(I)=RND(5)+(G-1):Y2(I)=B+1:C2(I)=8
3750 NEXT:NEXT
3780 FORI=1TO40
3790 IFY2(I)<B+3THENSET(X2(I),ABS(31-Y2(I)),6):RESET(X2(I),ABS(31-Y2(I))):GOTO3800
3791 SET(X2(I),ABS(31-Y2(I)),6)
3800 NEXT:R=R+1:IFR<10THEN2013
3805 SC=10/SH*100:IFSC<HSTHEN3830
3810 HS=SC
3815 RR=8:GOSUB4040:PRINT@(32*8)+26,"INIT-";:RR=9:GOSUB4040:PRINT@(32*9)+26,"IALS";
3820 RR=10:GOSUB4040:PRINT@(32*10)+26,"";:INPUTHS$:HS$=LEFT$(HS$,3) 
3830 RR=8:GOSUB4040:PRINT@(32*10)+26,"SCORE:";:RR=9:GOSUB4040:PRINT@(32*11)+26,LEFT$(STR$(SC)+"   ",5);"%";
3840 PRINT@(32*13)+26,"HIGH:";
3850 PRINT@(32*14)+26,LEFT$(STR$(HS)+"   ",5);"%";
3855 PRINT@(32*15)+26," ";HS$;" ";
3860 PRINT@489," PLAY AGAIN? ";
3880 I$=INKEY$:IFI$=""THEN3880
3885 IFI$="N"THENEND
3890 IFI$="Y"THEN2012
3895 GOTO3880
4000 PRINT@(32*8)+26,"MISS!";
4010 GOTO3000
4020 RR=8:GOSUB4040
4030 PRINT@(32*10)+26,"ROUND:";:PRINT@(32*11)+26,RIGHT$("  "+STR$(R+1)+"  ",6);:RETURN
4040 PRINT@(32*RR)+26,CHR$(128);CHR$(128);CHR$(128);CHR$(128);CHR$(128);CHR$(128);:RETURN