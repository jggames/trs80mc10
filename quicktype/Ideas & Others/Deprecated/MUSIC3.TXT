110 DIM T$(12),T(12,2),S(99,3)
120 DATA g,38,452,a,58,422,b,78,392,c,89,362,d,108,332,e,125,302,f,133,272,g,147,242
125 DATA a,159,212,b,170,182,c,176,152,d,185,122
130 GOSUB800:FOR I=1 TO 12
140 READ T$(I),T(I,1),T(I,2):NEXT:GOSUB600:GOSUB150:GOTO290
150 CLS:GOSUB790
160 FORI=34TO290STEP64
170 FORJ=1TO25
180 PRINT@I+J,"-";
190 NEXTJ,I
200 PRINT@361,"- -";
210 PRINT@421,"- -";
220 J=92
230 FORI=1TO12
240 J=J+30
250 PRINT@J,T$(13-I);
260 NEXT
270 J=4:RETURN
290 C=1:E=1:S(C,1)=1:S(C,2)=1:S(C,3)=1:S(1,0)=T(1,2)
300 A$=INKEY$
310 IFA$=""THEN300
320 A=ASC(A$)
325 IF(A>48)AND(A<58)THENI=A-48:T=T(I,1):GOTO410
330 IF(A>32)AND(A<42)THENI=A-32:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
335 IFA=45THENI=12:T=T(I,1):GOTO410
340 IFA=64THENI=12:T=T(I,1)-5:GOTO410
345 IFA=48THENI=10:T=T(I,1):GOTO410
350 IFA=79THENI=10:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
355 IFA=58THENI=11:T=T(I,1):GOTO410
360 IFA=80THENI=11:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
365 IFA=83THENCLS:PRINT"HIT ENTER TO SAVE":GOSUB610:IFA$=CHR$(13)THENPRINT"SAVING":CSAVE*S:GOSUB600:GOSUB150:GOTO300
370 IFA=76THENCLS:PRINT"HIT ENTER TO LOAD":GOSUB610:IFA$=CHR$(13)THENPRINT"LOADING":CLOAD*S:GOSUB700:GOSUB150:GOTO300
392 IFA=70THENJ=8:GOTO300
393 IFA=84THENJ=6:GOTO300
394 IFA=72THENJ=4:GOTO300
395 IFA=81THENJ=2:GOTO300
396 IFA=62THENCLS:FORT=1TOE:A$="SOUND"+STR$(S(T,1))+","+STR$(S(T,2)):LPRINTA$:PRINTA$:NEXT:GOSUB600:GOSUB150:GOTO300
397 IFA=13THENE=C-1:C=1:GOTO300
398 IFA=32THENFORT=1TOE:SOUNDS(T,1),S(T,2):N=S(T,0):I=S(T,3):GOSUB500:NEXT:GOTO300
399 IFA=65THENIFC>1THEN:C=C-1:E=C-1:GOTO300
400 GOTO300
410 IFC<99THENSOUNDT,J:S(C,1)=T:S(C,2)=J:S(C,3)=I:N=T(I,2):S(C,0)=N:E=C:C=C+1:GOSUB500:GOTO300
420 SOUND1,1:GOTO300
500 PRINT@N,CHR$(191);
510 FORZ=1TO30:NEXT
520 PRINT@N,T$(I);
530 RETURN
600 PRINT"PRESS ANY KEY TO CONTINUE"
610 A$=INKEY$:IFA$=""THEN610
620 CLS:RETURN
700 FORT=1TO499:IFS(T,1)=0THENC=T:E=C-1:GOTO720
710 NEXT
720 RETURN
790 CLS:PRINT@9,"MUSIC PROGRAM":RETURN
800 GOSUB790
810 PRINT"1- =PLAY NOTES GABCDEFGABCD":POKE16384+(32*1+0),49:POKE16384+(32*1+2),45
815 PRINT"shift 1-9 TO GET FLATS":POKE16384+(32*2+6),49:POKE16384+(32*2+8),57
816 PRINT"o,p & @=FLATS FOR 0,:, & -":POKE16384+(32*3+6),0:POKE16384+(32*3+18),48:POKE16384+(32*3+20),58
817 POKE16384+(32*3+25),45
820 PRINT"f=CHANGE TO FULL NOTES"
830 PRINT"t=3/4 NOTES"
840 PRINT"h=HALF NOTES"
850 PRINT"q=QUARTER NOTES"
860 PRINT"space=PLAY NOTES TYPED SO FAR"
870 PRINT"enter=RESET MEM TO BEGINNING"
880 PRINT"a=ERASE PREVIOUS NOTE TYPED"
890 PRINT"s=SAVE MUSICAL NOTES"
900 PRINT"l=LOAD MUSICAL NOTES"
910 PRINT">=OUTPUT AS SOUND STATEMENTS":POKE16384+(32*13),62
920 RETURN
