0 GOTO10
1 FORT=1TOSL:PRINTCHR$(TRS(ST-128));:NEXT:RETURN
2 IFPEEK(MC+(M/E))=G(VV)THENRETURN
3 IFX=0THENSB=SB+VV:GOTO5
4 SW=SW+VV
5 VV=RND(9):M=E*RND(29)+66+64*RND(13)
6 IFM=266ORM=268ORM=818ORM=270ORM=820ORM=822ORPEEK(MC+(M/E))>128THENM=544
7 POKEMC+(M/E),G(VV):RETURN
10 CLS:CLEAR999:GOTO30
20 CLS:PRINT"PLEASE WAIT":CLEAR999:G=130
30 DIMA$(1,7),B$(1,7),R(2),TRS(63),G(9),K(255),O,V,L,N,P,S,U,Q,T,Y,W,X,Z,E,F,CD,TT,TA,MC,I$,D$,D2$,E$,E2$,BL$,F$,C$
40 D$=CHR$(128)+CHR$(128)+CHR$(124):D2$=CHR$(128)+CHR$(124)+CHR$(128)
50 E$=CHR$(128)+CHR$(128)+"I":E2$=CHR$(128)+"I"+CHR$(128)
60 K(74)=1:K(75)=2:K(76)=3:K(65)=4:K(83)=5:K(68)=6:BL$=CHR$(128):F=0:CD=.015:W=1:TT=32:E=2:MC=16384
100 FORT=0TO63:READ TRS(T):NEXT
130 FORJ=0TO7
140 READ A,B,C:A$(0,J)=CHR$(TRS(A-128))+CHR$(TRS(B-128))+CHR$(TRS(C-128))
145 A$(1,J)=CHR$(TRS(A-128)+64)+CHR$(TRS(B-128)+64)+CHR$(TRS(C-128)+64)
150 READ A,B,C:B$(0,J)=CHR$(TRS(A-128))+CHR$(TRS(B-128))+CHR$(TRS(C-128))
155 B$(1,J)=CHR$(TRS(A-128)+64)+CHR$(TRS(B-128)+64)+CHR$(TRS(C-128)+64)
160 NEXT
170 FORJ=1TO9:READG(J):NEXT
500 IFG=0THEN5000
510 CLS0:POKE49151,7:PRINT@66/2,"";:ST=176:SL=31:GOSUB1:PRINT@962/E,"";:SL=30:ST=140:GOSUB1
511 FORI=130TO898STEP64:PRINT@I/E,CHR$(TRS(149-128));:NEXT:PRINT@I/E,CHR$(TRS(141-128));
520 FORI=190TO958STEP64:PRINT@I/E,CHR$(TRS(170-128));:NEXT:POKE16895,TRS(142-128)
521 PRINT@268/E,CHR$(TRS(185-128));CHR$(TRS(149-128));:PRINT@818/E,CHR$(TRS(170-128));CHR$(TRS(167-128));
600 P=132:Q=888:N=3:S=1:U=7:Y=S:T=2000:PRINT@P/E,A$(L,N);:PRINT@P/E+TT,B$(L,N);:PRINT@Q/E,A$(S,U);:PRINT@Q/E+TT,B$(S,U);
601 C$="UUUUUUUUUUUUUUUUUUUUUUUUUUUUUUU":ONGGOTO615
610 IFINKEY$=""THEN610
615 GOSUB5
620 IFN=8THENN=F:GOTO625
621 IFN<FTHENN=7:GOTO625
622 IFU=8THENU=F:GOTO625
623 IFU<FTHENU=7
625 IFX=WTHENP=Z:V=O:O=F
626 TA=ABS(P+O-Q-V):IFTA<8ORTA=60ORTA=64ORTA=68OR(V=FANDO=F)THENO=F:V=F:GOTO632
627 ONW-XGOTO631:PRINT@Q/E,E$;:PRINT@Q/E+TT,E2$;:Q=Q+V:GOTO632
631 PRINT@P/E,D$;:PRINT@P/E+TT,D2$;:P=P+O
632 PRINT@P/E,A$(L,N);:PRINT@P/E+TT,B$(L,N);:PRINT@Q/E,A$(S,U);:PRINT@Q/E+TT,B$(S,U);:GOSUB2
635 T=T-W:PRINT@F,LEFT$(C$,T*CD)+BL$;:IFT<WTHEN1000
640 O=F:V=O:Y=W-Y:ONYGOTO645:ONK(ASC(INKEY$+BL$))GOTO642,780,644:GOTO635
642 U=U-W:GOTO620
644 U=U+W:GOTO620
645 ONK(ASC(INKEY$+BL$))GOTO635,635,635,647,650,648:GOTO635
647 N=N-W:GOTO620
648 N=N+W:GOTO620
650 X=F:ONNGOTO710,720,730,740,750,760,770
700 IFP-880THENIFP-884THENIFP-328THENIFP-332THENIFP>191THENO=-64
705 GOTO625
710 IFP-812THENIFP-876THENIFP-880THENIFP-260THENIFP-324THENIFP-328THENIFP>191THENIFP+8-64*INT((P+8)/64)THENO=-60
715 GOTO625
720 IFP-748THENIFP-812THENIFP-196THENIFP-260THENIFP+8-64*INT((P+8)/64)THENO=4
725 GOTO625
730 IFP-684THENIFP-688THENIFP-748THENIFP-132THENIFP-136THENIFP-196THENIFP<832THENIFP+8-64*INT((P+8)/64)THENO=68
735 GOTO625
740 IFP-136THENIFP-140THENIFP-688THENIFP-692THENIFP<832THENO=64
745 GOTO625
750 IFP-760THENIFP-692THENIFP-696THENIFP-140THENIFP-144THENIFP-208THENIFP<832THENIFP-4-64*INT((P-4)/64)THENO=60
755 GOTO625
760 IFP-824THENIFP-760THENIFP-208THENIFP-272THENIFP-4-64*INT((P-4)/64)THENO=-4
765 GOTO625
770 IFP-884THENIFP-888THENIFP-824THENIFP-272THENIFP-332THENIFP-336THENIFP>191THENIFP-4-64*INT((P-4)/64)THENO=-68
775 GOTO625
780 Z=P:P=Q:X=W:ONU+WGOTO700,710,720,730,740,750,760,770
800 STOP
1000 POKE49151,16:CLS:PRINT@32,"GAME OVER:";:PRINT@110/2,"HIT ENTER";
1010 ON SGN(SW-SB)+1 GOTO 1020,1030:G$="BLUE WINS:":GOTO1040
1020 G$="TIE SCORE:":GOTO1040
1030 G$="RED WINS:"
1040 PRINT@88/E,G$;
1045 PRINT@0,"RED SCORE:";SW,"BLUE SCORE:";SB;
1050 IF INKEY$<>CHR$(13)THEN1050
1051 RUN20
5000 CLS:PRINT@4,"TANK (SEARCH AND CAPTURE)":PRINT
5001 PRINT"THE OBJECT OF THE GAME IS TO"
5002 PRINT"COLLECT AS MANY POINTS AS POS-"
5003 PRINT"SSIBLE BEFORE TIME RUNS OUT. A"
5004 PRINT"SYMBOL WILL APPEAR ON THE PLAY-"
5005 PRINT"ING FIELD AT A RANDOM LOCATION."
5006 PRINT"THE PLAYER WHO DRIVES HIS TANK"
5010 PRINT"OVER THE SYMBOL WILL HAVE HIS"
5011 PRINT"SCORE INCREASED BY THE VALUE OF"
5012 PRINT"THAT SYMBOL. ON THE NEXT SCREEN"
5013 PRINT"YOU WILL SEE THE TANKS AND THE"
5014 PRINT"SYMBOLS IN ASCENDING ORDER OF "
5015 PRINT"OF VALUE.":GOSUB6000
5016 CLS0:POKE49151,7:PRINT@0,A$(0,2);:PRINT@16,A$(1,6);:PRINT@32,B$(0,2);:PRINT@48,B$(1,6);
5017 FORT=1TO9:POKEMC+256+(T*2),G(T):NEXT:GOSUB6010:POKE49151,16:CLS
5030 P=268:Q=292:N=2:U=6:PRINT"PLAYERS CONTROL THE TANKS BY"
5031 PRINT"MEANS OF SIX KEYS AS SHOWN:"
5032 PRINT
5033 PRINT"     BLUE        RED"
5034 PRINT"     A S D       J K L"
5035 PRINT
5036 PRINT"THESE KEYS ARE IN TWO GROUPS ON"
5037 PRINT"EACH SIDE OF THE KEYBOARD. THE"
5038 PRINT"OUTER KEYS IN EACH GROUP ARE"
5039 PRINT"FOR TURNING.":PRINT
5040 PRINT"PRESSING 'A' TURNS COUNTERCLOCK"
5041 PRINT"WISE. (PRESS ENTER FOR A DEMO).":GOSUB6010
5050 CLS0:POKE49151,7
5060 FORN=7TO1STEP-1
5070 PRINT@P/E,A$(0,N);:PRINT@P/E+TT,B$(0,N);:GOSUB7000:NEXT
5100 POKE49151,16:CLS
5110 PRINT"SIMILARLY, THE 'D' AND 'L' KEYS"
5111 PRINT"ROTATE THE TANKS CLOCKWISE."
5160 PRINT"THE 'S' AND 'K' KEYS (THE MIDDLEKEYS IN EACH GROUP) WILL MOVE"
5161 PRINT"THE TANKS FORWARD. NOTICE THAT"
5162 PRINT"EACH TANK LEAVES A TRAIL BEHIND"
5163 PRINT"IT AS IT TRAVELS. AS THE GAME"
5180 PRINT"PROGRESSES, THE SYMBOLS BECOME"
5181 PRINT"HARDER TO FIND AMONG THE TANK"
5182 PRINT"TRAILS, BUT THERE IS always A"
5183 PRINT"SYMBOL OUT THERE. YOU CANNOT"
5184 PRINT"TRAVEL THROUGH ANY OF THE BLUE"
5185 PRINT"BARRIERS.  GOOD LUCK!!":GOSUB6000
5200 GOTO510
7000 FORI=1TO1000:NEXT:RETURN
6000 PRINT@974/E+2,"(PRESS ENTER)";
6010 IF INKEY$=CHR$(13)THENRETURN
6020 I=RND(1000):GOTO6010
8000 DATA 128,160,144,176,136,168,152,184,132,164,148,180,140,172,156,188
8001 DATA 130,162,146,178,138,170,154,186,134,166,150,182,142,174,158,190
8002 DATA 129,161,145,177,137,169,153,185,133,165,149,181,141,173,157,189
8003 DATA 131,163,147,179,139,171,155,187,135,167,151,183,143,175,159,191
9000 DATA 128,168,128
9001 DATA 170,183,191
9010 DATA 160,180,134
9011 DATA 139,158,129
9020 DATA 168,188,128
9021 DATA 170,190,131
9030 DATA 184,173,144
9031 DATA 130,135,164
9040 DATA 168,156,188
9041 DATA 130,171,131
9050 DATA 160,158,180
9051 DATA 152,139,129
9060 DATA 128,168,188
9061 DATA 130,171,190
9070 DATA 137,184,144
9071 DATA 130,173,135
9100 DATA 0,1,3,7,15,31,63,64,66
