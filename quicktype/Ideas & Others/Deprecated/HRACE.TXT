
1 ' *** HORSE RACING ***
2 ' BY ROB BECKER
10 BL$=CHR$(128):B1$="horse":B2$="racing":B3$="by":B4$="rob":B5$="becker":FORX=1TO15:CL=RND(8):CLS(CL):PRINT@106,B1$;BL$;B2$;:SOUNDX*3,1:NEXT:CLS0:PRINT@106,B1$;BL$;B2$;:PRINT@265,B3$;BL$;BL$;B4$;BL$;B5$;:SCREEN0,1
20 HT=0:FORJK=1TO7:HT=HT+16:H$(JK)=CHR$(143+HT):NEXT:H$(8)="G":HC=0:FORHN=1TO7:HC=HC+16:H1$(HN)=CHR$(128)+CHR$(131+HC)+CHR$(142+HC):H2$(HN)=CHR$(128)+CHR$(136+HC)+CHR$(136+HC):NEXT:H1$(8)=CHR$(128)+CHR$(131)+CHR$(142)
30 FORX=160TO189:PRINT@X,H1$(7);:PRINT@X+32,H2$(7);:SCREEN0,1:FORY=1TO100:NEXTY:NEXTX
40 H3$=CHR$(128)+CHR$(128):H4$=CHR$(128)+CHR$(128):H2$(8)=CHR$(128)+CHR$(136)+CHR$(136):POKE65495,0:CLS:PRINT"*** HORSE RACING ***":PRINT"BY ROB BECKER":PRINT:INPUT"DO YOU WANT INSTRUCTIONS";I$:IFI$="Y"THENGOSUB410
50 PRINT:INPUT"HOW MANY PLAYERS(1-8)";PL:IFPL<1 OR PL>8THEN50ELSEFORX=1TOPL:DD(X)=100:NEXT
60 FORX=1TOPL:PRINT:PRINT"BETTER";X;"'S NAME";:INPUTA$(X):NEXT
70 IFRA=10THEN320ELSEFORX=1TO8:MV=RND(7):H(X)=MV:NEXTX:CLS0:RA=RA+1:PRINT"*** HORSE RACING ***":PRINT"RACE #";RA:PRINT"HORSES"
80 FORX=1TO8:P=RND(0):SH=RND(0):P=P+1:SH=SH+2:PRINTX") ";H$(X);" ";:PRINTUSING"$##.##";H(X);:PRINT" ";:PRINTUSING"$##.##";H(X)/P;:PRINT" ";:PRINTUSING"$##.##";H(X)/SH:NEXT
90 IFRA>1THEN100ELSEFORX=1TOPL:GOTO140
100 PRINT@41,"*":PRINT@73,"*":PRINT@43,"RESULTS OF RACE #";RA-1:PRINT@75,"1ST. ";H$(HP(1));" 2ND. ";H$(HP(2));" 3RD. ";H$(HP(3)):PA=0:FORX=1TOPL
110 IFDD(X)=<0THENNEXTX
120 FORI=1TOPL:IFDD(I)>0THEN140ELSENEXTI
130 GOTO320
140 PRINT@384,A$(X)"'S BET":PRINT"YOU HAVE ";:IFDD(X)<100THENPRINTUSING"$###.##";DD(X)ELSEPRINTUSING"$ ###.##";DD(X)
150 FORS=448TO479:PRINT@S,CHR$(143);:NEXTS:PRINT@448,"WHICH HORSE";:INPUTBB(X):IFBB(X)>8 OR BB(X)<1THEN140
160 PRINT@448,"1-WIN 2-PLACE 3-SHOW";:INPUTWS(X):IFWS(X)<1 OR WS(X)>3THEN160
170 FORY=448TO479:PRINT@Y,CHR$(143);:NEXTY:PRINT@448,"MONEY(1-";:PRINTUSING"###.##";DD(X);:PRINT")";:INPUTMN(X):IFMN(X)<.01 OR MN(X)>DD(X)THEN170ELSENEXTX
180 CLS0:FORX=62TO63:FORY=0TO31:SET(X,Y,5):NEXTY,X:L=0:FORX=0TO511STEP64:L=L+1:PRINT@X,H1$(L);:PRINT@X+32,H2$(L);:NEXT
190 FORX=1TO1000:NEXT:PLAY"L4T5O2CFAO3L8CT7P8CP64CP64CP8O2T5L8AT7P8AP64AP64AP8T5L8.FO3CO2FL1CL4P2CFAO3L8CT7P8CP64CP64CP8O2T5L8AT7P8AP64AP64AP8T5L8.CP64CP64CL1F"
200 A=0:B=128:C=192:D=256:E=320:F=384:G=448:HH=64
210 X=RND(8)
220 ON X GOTO 230,240,250,260,270,280,290,300
230 IFA=29THENPRINT@A+1,H3$;:PRINT@A+33,H4$;:GOTO210ELSEIFX=1THENS=RND(8):IFS<H(1)THEN210ELSEA=A+1:PRINT@A,H1$(1);:PRINT@A+32,H2$(1);:IFA=29THENZC=1:GOSUB400
240 IFHH=93THENPRINT@HH+1,H3$;:PRINT@HH+33,H4$;:GOTO210ELSEIFX=2THENS=RND(8):IFS<H(2)THEN210ELSEHH=HH+1:PRINT@HH,H1$(2);:PRINT@HH+32,H2$(2);:IFHH=93THENZC=2:GOSUB400
250 IFB=157THENPRINT@B+1,H3$;:PRINT@B+33,H4$;:GOTO210ELSEIFX=3THENS=RND(8):IFS<H(3)THEN210ELSEB=B+1:PRINT@B,H1$(3);:PRINT@B+32,H2$(3);:IFB=157THENZC=3:GOSUB400
260 IFC=221THENPRINT@C+1,H3$;:PRINT@C+33,H4$;:GOTO210ELSEIFX=4THENS=RND(8):IFS<H(4)THEN210ELSEC=C+1:PRINT@C,H1$(4);:PRINT@C+32,H2$(4);:IFC=221THENZC=4:GOSUB400
270 IFD=285THENPRINT@D+1,H3$;:PRINT@D+33,H4$;:GOTO210ELSEIFX=5THENS=RND(8):IFS<H(5)THEN210ELSED=D+1:PRINT@D,H1$(5);:PRINT@D+32,H2$(5);:IFD=285THENZC=5:GOSUB400
280 IFE=349THENPRINT@E+1,H3$;:PRINT@E+33,H4$;:GOTO210ELSEIFX=6THENS=RND(8):IFS<H(6)THEN210ELSEE=E+1:PRINT@E,H1$(6);:PRINT@E+32,H2$(6);:IFE=349THENZC=6:GOSUB400
290 IFF=413THENPRINT@F+1,H3$;:PRINT@F+33,H4$;:GOTO210ELSEIFX=7THENS=RND(8):IFS<H(7)THEN210ELSEF=F+1:PRINT@F,H1$(7);:PRINT@F+32,H2$(7);:IFF=413THENZC=7:GOSUB400
300 IFG=476THENPRINT@G+1,H3$;:PRINT@G+33,H4$;:GOTO210ELSEIFX=8THENS=RND(8):IFS<H(8)THEN210ELSEG=G+1:PRINT@G,H1$(8);:PRINT@G+32,H2$(8);:IFG=476THENZC=8:GOSUB400
310 GOTO210
320 CLS:PRINT"*** HORSE RACING ***":PRINT"GAME OVER":PRINT:PRINT"FINAL SCORES :":PRINT:FORY=1TO800:NEXT:FORX=1TOPL:PRINTA$(X);" $";DD(X):FORY=1TO800:NEXTY:NEXTX:END
330 FORQ=1TOPL:IFWS(Q)=1THEN340ELSEIFWS(Q)=2THEN360ELSE380
340 IFBB(Q)=HP(1)THENDD(Q)=DD(Q)+MN(Q)*H(BB(Q))/2ELSEDD(Q)=DD(Q)-MN(Q):NEXTQ:GOTO70
350 NEXTQ:GOTO70
360 IFBB(Q)=HP(1) OR BB(Q)=HP(2)THENDD(Q)=DD(Q)+MN(Q)*H(BB(Q))/P/2ELSEDD(Q)=DD(Q)-MN(Q):NEXTQ:GOTO70
370 NEXTQ:GOTO70
380 IFBB(Q)=HP(1) OR BB(Q)=HP(2) OR BB(Q)=HP(3)THENDD(Q)=DD(Q)+MN(Q)*H(BB(Q))/SH/2ELSEDD(Q)=DD(Q)-MN(Q):NEXTQ:GOTO70
390 NEXTQ:GOTO70
400 PA=PA+1:HP(PA)=ZC:IFPA=>3THEN330ELSERETURN
410 CLS:PRINT"*** HORSE RACING ***":PRINT:PRINT" YOU ARE GIVEN $100 TO START    OFF THE GAME. ONCE THE GAME HAS STARTED, YOU WILL SEE THE LIST  OF HORSES, AND HOW MUCH THEY    PAY OFF."
420 PRINT" EACH HORSE IS NUMBERED, SO     WHEN YOU CHOOSE THE HORSE YOU   WISH TO BET ON, TYPE THE NUMBER THAT REPRESENTS THAT HORSE.     YOUR HORSE IS IDENTIFIED BY     IT'S COLOR. 'G' REPRESENTS      GREEN ON THE CHART."
430 A$=INKEY$:IFA$=""THEN430ELSECLS:PRINT" THE ROWS OF PRICES, REPRESENT  THE AMOUNT OF MONEY YOU WIN FOR THAT HORSE. ROW 1 TELLS YOU HOW MUCH MONEY YOU WIN, IF YOUR     HORSE COMES IN FIRST."
440 PRINT" ROW 2 TELLS YOU HOW MUCH YOU   WIN IF YOUR HORSE COMES IN 1ST. OR 2ND. AND ROW 3 TELLS YOU HOW MUCH MONEY IF YOUR HORSE        FINISHES 1ST,2ND OR 3RD."
450 A$=INKEY$:IFA$=""THEN450ELSECLS:PRINT" DURING THE TIME YOU ARE ASKED  TO BET ON HORSE, YOU ARE ASKED  TO CHOOSE WIN, PLACE OR SHOW.   I WILL DESCRIBE THIS PROCEDURE."
460 PRINT" WIN-- IF YOU CHOOSE WIN, YOUR  HORSE MUST FINISH FIRST. FOR    EVERY $2 YOU BET YOU RECIEVE    THE AMOUNT OF MONEY THAT YOUR   HORSE PAYS FOR A WIN. IF YOUR   HORSE DOES NOT FINISH FIRST,    YOU LOSE THE AMOUNT OF MONEY YOUBET."
470 A$=INKEY$:IFA$=""THEN470ELSECLS:PRINT" PLACE-- IF YOU CHOOSE PLACE    YOUR HORSE MUST COME IN FIRST   OR SECOND. ALL THE OTHER        PROCEDURES FOLLOW THE SAME RULE AS 'WIN'.":PRINT" SHOW-- YOUR HORSE MUST FINISH  1ST, 2ND, OR 3RD."
480 PRINT"ALL OF THE REST OF THE          PROCEDURES ARE THE SAME AS 'WIN'AND 'PLACE'"
490 A$=INKEY$:IFA$=""THEN490ELSECLS:PRINT" REMEMBER : YOU WIN MORE MONEY  IF YOU CHOOSE 'WIN' AND YOUR    HORSE COMES IN FIRST, BUT YOU   WIN MORE OFTEN CHOOSING 'PLACE' OR 'SHOW'. ALSO, A HORSES SPEED DEPENDS ON HOW MUCH A HORSE PAYSOFF."
500 PRINT" A HORSE THAT PAYS OFF $3, IS   FASTER THAN A HORSE THAT PAYS   OFF $6. THE LOWER THE HORSE PAYSOFF, THE FASTER THE HORSE IS.   IF YOU RUN OUT OF MONEY, YOU    LOSE, AND YOU ARE OUT OF THE    GAME."
510 A$=INKEY$:IFA$=""THEN510ELSECLS:PRINT" IF THERE ARE ANY OTHER PLAYERS IN THE GAME, THE GAME CONTINUES.AFTER 10 RACES THE GAME IS OVER AND EVERYONES MONEY LEFT IS     SHOWN. YOU CAN NOT BET MORE     MONEY THEN YOU HAVE."
520 RETURN
