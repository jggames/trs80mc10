10 PRINT CHR$(12);"!!!!! ATTENTION: THE BEES ARE ATTACKING !!!!!!"
15 PRINT "BEGIN DEFENSE PLAN: "
20 PRINT "TIME:     1"
30 PRINT "ENTER YOUR LAST NAME FOR IDENTIFICATION CHECK"
40 INPUT N$
50 PRINT "ENTER CODE WORD FOR LATER VERIFICATION"
60 INPUT C$
3370 REM CREATE TWO INITIAL SWARMS
3380 RANDOMIZE
3382 DIM E(21),S(21),A$(21),U(21),G(21),M(21),D(21),C(21)
3384 DIM K(21),V(21),R(21)
3386 FOR A= 1 TO 21
3388 E(A)=0
3390 S(A)=0
3392 U(A)=0
3394 G(A)=0
3396 M(A)=0
3398 D(A)=0
3400 C(A)=0
3402 V(A)=0
3406 K(A)=0
3408 R(A)=0
3410 NEXT A
3412 A1=INT(RND*21+1)
3414 A2=INT(RND*21+1)
3420 IF A1=A2 THEN 3410
3430 S(A1) = INT(RND*5+2)
3440 S(A2) = INT(RND*5+2)
3450 E(A1) = INT(RND*9+6)
3460 E(A2) = INT(RND*9+6)
3480 REM SUBTRACT ONE HOUR FROM ALL ETA'S
3500 FOR A=1 TO 21
3510 IF E(A)=0 THEN 3530
3520 GOTO 3540
3530 IF U(A) = 0 THEN 3750
3540 IF S(A) = 1 THEN 3750
3550 IF ABS(U(A)) = 1 THEN 3660
3560 IF ABS(E(A)) = 1 THEN 3600
3570 E(A)=E(A) - 1
3580 C(A)=C(A)+(17-E(A))
3590 GOTO 3750
3600 IF E(A) = -1 THEN 3640
3610 PRINT "THE BEES HAVE ARRIVED IN THE MAJOR CITY IN SECTION ";A
3620 E(A)=-1
3630 U(A)=6
3640 U(A)=U(A)-1
3650 GOTO 3750
3660 IF U(A) = -1 THEN 3750
3670 PRINT "THE BEES HAVE DESTROYED THE MAJOR CITY IN SECTION ";A
3680 U(A)=-1
3690 IF V(A)<>-1 THEN 3720
3700 PRINT "BUT THE POPULATION HAS BEEN EVACUATED"
3710 GOTO 3740
3720 K(A)=1
3730 C(A)=(1000000*(RND*A+1))+C(A)
3740 GOTO 3750
3750 NEXT A
3760 REM ADD ONE UNIT TO TIME
3770 T=T+1
3780 REM CREATE NEW SWARM
3790 IF T/30<>INT(T/30) THEN 3880
3800 A=INT(RND*21+1)
3810 IF S(A)<>0 THEN 3790
3820 IF R(A)<>0 THEN 3790
3830 IF K(A)<>0 THEN 3790
3840 S(A)=INT(RND*5+2)
3850 E(A)=INT(RND*9+16)
3860 C(A)=INT(RND*10)
3870 PRINT "A NEW SWARM IS REPORTED IN SECTION ";A
3880 REM CHECK COMBATED SWARMS
3890 FOR A=1 TO 21
3900 IF T<>G(A) THEN 4060
3910 IF S(A)<> 1 THEN 4000
3920 S(A)=0
3930 G(A)=0
3940 E(A)=0
3950 M(A)=0
3960 K(A)=0
3970 U(A)=0
3980 PRINT "** THE SWARM IN SECTION ";A;" IS TOTALLY DESTROYED."
3990 GOTO 4060
4000 S(A)=S(A)-M(A)
4010 PRINT "** THE PHASE ON SECTION ";A;" WAS SUCCESSFUL."
4020 IF S(A)>=1 THEN 4040
4030 S(A)=1
4040 G(A)=0
4050 M(A)=0
4060 NEXT A
4070 REM ADD ONE UNIT TO EACH UNCOMBATTED SWARM
4080 FOR A=1 TO 21
4090 IF E(A)=1 THEN 4340
4100 IF S(A)=1 THEN 4310
4110 IF S(A)=0 THEN 4340
4120 IF S(A)+1<9 THEN 4280
4130 IF A=1 THEN 4210
4140 IF S(A-1)>0 THEN 4200
4150 IF R(A-1)<>0 THEN 4340
4160 PRINT "** THE SWARM IN SECTION ";A;" HAS SPREAD TO SECTION ";A-1
4170 S(A-1)=3
4180 E(A-1)=INT(RND*10+5)
4190 GOTO 4340
4200 IF A=21 THEN 4340
4210 IF S(A+1)>0 THEN 4340
4220 IF R(A+1)<>0 THEN 4340
4230 PRINT "** THE SWARM IN SECTION ";A;" HAS SPREAD TO SECTION ";A+1
4240 IF R(A+1)<>0 THEN 4340
4250 S(A+1)=3
4260 E(A+1)=INT(RND*10+5)
4270 GOTO 4340
4280 S(A)=S(A)+1
4290 C(A)=C(A)+S(A)
4300 GOTO 4340
4310 IF D(A)=1 THEN 4340
4320 PRINT "** THE SWARM IN SECTION ";A;" IS READY TO BE DESTROYED."
4330 D(A)=1
4340 NEXT A
4350 REM WINNER CHECK
4360 W=0
4370 FOR A=1 TO 21
4380 W=S(A)+W
4390 NEXT A
4400 IF W>=1 THEN 4470
4410 PRINT CHR$(7);"*******ALL SWARMS ARE NOW DESTROYED*******";CHR$(7)
4420 PRINT "FINAL STATISTICS ON ATTACKS AND DESTRUCTION ARE NOW "
4430 PRINT "BEING COMPUTED,   ...   FINAL RESULTS FOLLOW ....."
4440 A=0
4450 PRINT
4460 GOTO 7920
4470 REM EVACUATION CHECK
4480 FOR A=1 TO 21
4490 IF V(A)<>T THEN 4590
4500 IF U(A)=-1 THEN 4560
4510 IF K(A)>0 THEN 4560
4520 PRINT "** POPULATION IN SECTION ";A;" IS EVACUATED"
4530 C(A)=C(A)+INT(RND*17)
4540 V(A)=-1
4550 GOTO 4590
4560 PRINT "** POPULATION IN SECTION ";A;" WAS DESTROYED BEFORE "
4570 PRINT "   IT COULD BE EVACUATED"
4580 V(A)=0
4590 NEXT A
4600 REM RETURN EVACUATION CHECK
4610 FOR A=1 TO 21
4620 IF R(A)<>T THEN 4670
4630 IF R(A)<1 THEN 4670
4640 K(A)=0
4650 R(A)=0
4655 V(A)=0
4660 PRINT "** THE POPULATION HAS RETURNED TO THE CITY IN SECTION ";A
4670 NEXT A
4680 REM LOSER CHECK
4690 Q=0
4700 FOR A=1 TO 21
4710 Q=C(A)+Q
4720 NEXT A
4730 IF Q<75000000 THEN 4830
4740 PRINT "THE BEES HAVE DEVASTATED THE UNITED STATES AND ";CHR$(7)
4750 PRINT "THERE ARE NOW OVER 75 MILLION CASUALTIES,  THE BEES"
4760 PRINT "ARE NOW CONSIDERED TO BE THE VICTORS OVER MODERN"
4770 PRINT "TECHNOLOGY.";CHR$(7);CHR$(7)
4780 A=0
4790 Q=0
4800 W=0
4810 PRINT
4820 GOTO 7920
4830 REM COMMAND INPUT
4840 PRINT "TIME:   ";T+1
4850 PRINT "COMMAND";
4860 INPUT COM
4880 IF COM>8 THEN 4850
4890 ON COM GOTO 4910,5400,5810,7040,7260,7880,8150,8420
4900 GOTO 4860
4910 REM MAP PRINT OUT
4920 PRINT CHR$(12);"1)        ATTACK SCAN MAP"
4930 PRINT
4940 FOR A=1 TO 21
4950 IF S(A)>6 THEN 5070
4960 IF S(A)>4 THEN 5050
4970 IF S(A)>1 THEN 5030
4980 IF S(A)>0 THEN 5010
4990 A$(A)= "?"
5000 GOTO 5080
5010 A$(A)="."
5020 GOTO 5080
5030 A$(A)="+"
5040 GOTO 5080
5050 A$(A)="*"
5060 GOTO 5080
5070 A$(A)="#"
5080 NEXT A
5090 PRINT "----------------------------------------------------"
5100 PRINT TAB(48);"---"
5110 PRINT TAB(20);"CANADA- NO INFORMATION     /  /"
5120 Z1$="-----------------------        / "+A$(18)+"/"
5125 PRINT TAB(15);Z1$
5130 PRINT TAB(15);"!   !   !   !    !    !\-    -/  /"
5140 Z1$="! "+A$(1)+" ! "+A$(3)+" ! "+A$(6)+" ! "
5142 Z1$=Z1$+A$(9)+"  ! "+A$(13)+"  !  \--/---/"
5144 PRINT TAB(15);Z1$
5150 Z1$=          "!   !---!---!----!----! "+A$(16)+" !    /"
5155 PRINT TAB(15);Z1$
5160 Z1$=          "!---!   !   !    !    !   ! "+A$(19)+" !"
5165 PRINT TAB(15);Z1$
5170 Z1$="!   ! "+A$(4)+" ! "+A$(7)+" ! "+A$(10)+"  ! "
5172 Z1$=Z1$+A$(14)+"  !---!---!"
5174 PRINT TAB(15);Z1$
5180 Z1$="! "+A$(2)+" !---!---!----!----!   !   !"
5184 PRINT TAB(15);Z1$
5190 Z1$=" \  !   !   !    !    ! "+A$(17)+" ! "+A$(20)+" !"
5194 PRINT TAB(15);Z1$
5200 Z1$="  \ ! "+A$(5)+" ! "+A$(8)+" ! "+A$(11)+"  ! "
5202 Z1$=Z1$+A$(15)+"  !   !   /"
5204 PRINT TAB(15);Z1$
5210 Z1$="   \!---!---!----!----!---! "+A$(21)+"!"
5212 PRINT TAB(15);Z1$
5220 PRINT TAB(27);"\    !";TAB(42);"\ !"
5230 Z1$="              \ "+A$(12)+"!"
5232 PRINT TAB(15);Z1$;TAB(42);"! !"
5240 PRINT TAB(30);"\-!";TAB(42);"!-!"
5250 PRINT TAB(20);"MEXICO- NO INFORMATION"
5260 PRINT
5270 PRINT "----------------------------------------------------"
5390 GOTO 3480
5400 REM ETA REPORT
5410 PRINT CHR$(12);"2)         ETA REPORT"
5420 PRINT
5430 PRINT "ENTER SECTION #";
5440 INPUT A
5450 PRINT "*************************************"
5460 IF A<>0 THEN 5490
5470 D7=-1
5480 FOR A = 1 TO 21
5490 IF S(A)=1 THEN 5710
5500 IF R(A)=-1 THEN 5740
5510 IF E(A)=0 THEN 5690
5520 IF U(A)=-1 THEN 5570
5530 IF E(A)=-1 THEN 5600
5540 PRINT "THE BEES WILL ARRIVE AT THE MAJOR CITY IN"
5550 PRINT "         SECTION ";A;" AT ";E(A)+T;" HOURS."
5560 GOTO 5760
5570 PRINT "THE BEES HAVE ALREADY DESTROYED THE CITY IN"
5580 PRINT "SECTION ";A;" AND ARE NOW INHABITING IT."
5590 GOTO 5760
5600 PRINT "THE BEES HAVE ARRIVED AT THE CITY IN"
5610 IF V(A)<>-1 THEN 5650
5620 PRINT "SECTION ";A;" BUT THE POPULATION HAS BEEN"
5630 PRINT "        EVACUATED"
5640 GOTO 5760
5650 PRINT "SECTION ";A;" AND THE POPULATION OF THAT CITY"
5660 PRINT "CAN ONLY SURVIVE FOR ABOUT ";U(A);" MORE HOURS."
5670 PRINT "   USE OF URBAN DEFENSES IS RECOMMENDED."
5680 GOTO 5760
5690 PRINT "     NO SWARMS REPORTED IN SECTION ";A
5700 GOTO 5760
5710 PRINT "THE BEES IN SECTION ";A;" ARE READY TO BE "
5720 PRINT "DESTROYED."
5730 GOTO 5760
5740 PRINT "SECTION ";A;" IS A RADIOACTIVE WASTELAND THAT"
5750 PRINT "        IS COMPLETELY UNPOPULATED"
5760 PRINT "*************************************"
5770 IF D7<>-1 THEN 5790
5780 NEXT A
5790 D7=0
5800 GOTO 3480
5810 REM BATTLE PPHASE OPTIONS
5820 PRINT CHR$(12);
5830 PRINT "3)        BATTLE PHASE OPTIONS"
5840 PRINT "ENTER SECTION #";
5850 INPUT A
5860 PRINT "ENTER PHASE";
5870 INPUT P
5880 IF V(A)<1 THEN 5910
5890 PRINT "SECTION ";A;" IS BEING EVACUATED"
5900 GOTO 3480
5910 IF G(A)>0 THEN 5930
5920 GOTO 5950
5930 PRINT "THE SWARM IN SECTION ";A;" IS ALREADY BEING COMBATTED"
5940 GOTO 3480
5950 IF S(A)=0 THEN 5970
5960 GOTO 5990
5970 PRINT "NO SWARM IS REPORTED IN SECTION ";A
5980 GOTO 3480
5990 IF S(A)<>1 THEN 6030
6000 IF P=5 THEN 6070
6010 PRINT "THE DESTRUCTION PHASE SHOULD BE USED IN SECTION ";A
6020 GOTO 3480
6030 IF E(A)<>-1 THEN 6070
6040 IF P=6 THEN 6070
6050 PRINT "URBAN DEFENSES SHOULD BE USED IN SECTION ";A
6060 GOTO 3480
6070 N=RND
6080 ON P GOTO 6090,6180,6270,6330,6420,6480
6090 REM PHASE 1
6100 PRINT "BEE COCKTAIL: PHASE ONE, NOW BEING ATTEMPTED."
6110 IF N>.95 THEN 3480
6120 G(A)=T+INT(RND*3+1)
6130 IF S(A)>5 THEN 6160
6140 M(A)=S(A)-2
6150 GOTO 3480
6160 M(A)=S(A)-5
6170 GOTO 3480
6180 REM PHASE TWO
6190 PRINT "PROJECT QUEEN: PHASE TWO, NOW BEING ATTEMPTED."
6200 IF N>.92 THEN 3480
6210 G(A) = T+INT(RND*3+4)
6220 IF S(A)>3 THEN 6250
6230 M(A)=1
6240 GOTO 3480
6250 M(A)=S(A)-1
6260 GOTO 3480
6270 REM PHASE THREE
6280 PRINT "PROJECT BRUSH FIRE: PHASE THREE, NOW BEING ATTEMPTED."
6290 IF N>.96 THEN 3480
6300 G(A)=T+1
6310 M(A)=2
6320 GOTO 3480
6330 REM PHASE 4
6340 PRINT "PROJECT STERILE MALE: PHASE FOUR, NOW BEING ATTEMPTED."
6350 IF N>.89 THEN 3480
6360 G(A)=T+INT(RND*3+3)
6370 IF S(A)>6 THEN 6400
6380 M(A)=S(A)-1
6390 GOTO 3480
6400 M(A)=4
6410 GOTO 3480
6420 REM PHASE 5
6430 IF S(A)=1 THEN 6440
6432 PRINT "SWARM TOO LARGE: DESTRUCTION NOT POSSIBLE"
6434 GOTO 3480
6440 PRINT "DESTRUCTION: PHASE FIVE, NOW BEING ATTEMPTED."
6450 IF N>.7 THEN 3480
6460 G(A)=T+INT(RND*3+2)
6470 GOTO 3480
6480 REM PHASE 6
6490 IF E(A)<>-1 THEN 6010
6500 PRINT "ENTER URBAN DEFENSE CODE";
6510 INPUT D$
6520 C(A)=C(A)+INT(200*RND)
6530 IF RND >.8 THEN 3480
6540 IF D$="A" THEN 6630
6550 IF D$="B" THEN 6690
6560 IF D$="C" THEN 6750
6570 IF D$="D" THEN 6810
6580 IF D$="E" THEN 6870
6590 IF D$="F" THEN 6910
6600 IF D$="G" THEN 6950
6610 PRINT "INVALID URBAN DEFENSE CODE"
6620 GOTO 3480
6630 REM *A*
6640 IF S(A)>5 THEN 6670
6650 S(A)=S(A)-1
6660 GOTO 6980
6670 S(A)=2
6680 GOTO 6980
6690 REM *B*
6700 IF S(A)>4 THEN 6730
6710 S(A)=S(A)-3
6720 GOTO 6980
6730 S(A)=S(A)-2
6740 GOTO 6980
6750 REM *C*
6760 IF S(A)>7 THEN 6790
6770 S(A)=S(A)-3
6780 GOTO 6980
6790 S(A)=1
6800 GOTO 6980
6810 REM *D*
6820 IF S(A)>3 THEN 6850
6830 S(A)=1
6840 GOTO 6980
6850 S(A)=S(A)-3
6860 GOTO 6980
6870 REM *E*
6880 IF RND>.5 THEN 6980
6890 S(A)=2
6900 GOTO 6980
6910 REM *F*
6920 IF RND>.4 THEN 6980
6930 S(A)=1
6940 GOTO 6980
6950 REM *G*
6960 IF RND>.8 THEN 6980
6970 S(A)=S(A)-4
6980 PRINT "THE URBAN DEFENSE IN SECTION ";A;" WAS SUCCESSFUL"
6990 IF S(A)>1 THEN 3480
7000 S(A)=1
7010 D(A)=1
7020 PRINT "** SWARM IN SECTION ";A;" IS READY TO BE DESTROYED"
7030 GOTO 3480
7040 REM EVACUATION PROCEDURE
7050 PRINT CHR$(12);
7060 PRINT "4)      EVACUATION PROCEDURE"
7070 PRINT "ENTER SECTION #";
7080 INPUT A
7090 IF S(A)=0 THEN 7150
7100 IF V(A)>0 THEN 7170
7110 IF V(A)=-1 THEN 7190
7120 IF K(A)>0 THEN 7210
7130 IF U(A)=-1 THEN 7210
7140 GOTO 7230
7150 PRINT "COMPUTER FAILSAFE...NO SWARMS REPORTED IN SECTION ";A
7160 GOTO 3480
7170 PRINT "SECTION ";A;" IS BEING EVACUATED ALREADY"
7180 GOTO 3480
7190 PRINT "CITY IN SECTION ";A;" IS ALREADY EVACUATED"
7200 GOTO 3480
7210 PRINT "POPULATION IN SECTION ";A;" HAS BEEN DESTROYED"
7220 GOTO 3480
7230 PRINT "EVACUATION PROCEDURE NOW IN PROGRESS"
7240 V(A)=5+T
7250 GOTO 3480
7260 REM ***** NUCLEAR DESTRUCTION SEQUENCE *****
7270 PRINT CHR$(12);CHR$(7);
7280 PRINT "5)       NUCLEAR DESTRUCTION SEQUENCE"
7290 PRINT "PLEASE ENTER YOUR NAME"
7300 INPUT N1$
7310 IF N$<>N1$ THEN 7490
7320 PRINT "PLEASE ENTER YOUR CODE WORD"
7330 INPUT C1$
7340 IF C$<>C1$ THEN 7490
7350 PRINT "POSITIVE IDENTIFICATION CHECK";CHR$(7)
7360 PRINT "CODE WORD CHECK IS VALID"
7370 PRINT "ID SEQUENCE COMPLETED"
7380 PRINT "PLEASE ENTER SECTION #";
7390 INPUT A
7400 IF E(A)<>-1 THEN 7440
7410 IF S(A)=0 THEN 7470
7420 PRINT
7430 GOTO 7530
7440 PRINT "COMPUTER FAILSAFE: BEES NOT ARRIVED IN THE MAJOR"
7450 PRINT "CITY IN SECTION ";A
7460 GOTO 3480
7470 PRINT "COMPUTER FAILSAFE: NO SWARM REPORTED IN SECTION ";A
7480 GOTO 3480
7490 PRINT "ID SEQUENCE DEFAULT....IGNORED"
7500 GOTO 3480
7510 PRINT "NUCLEAR DESTRUCTION ABORTED."
7520 GOTO 3480
7530 PRINT "BOMB IN SECTION ";A;" IS NOW ACTIVE"
7540 IF K(A)=1 THEN 7600
7550 IF V(A)=-1 THEN 7600
7560 PRINT "SECTION ";A;" HAS NOT BEEN EVACUATED"
7570 PRINT "DO YOU WISH TO CONTINUE";
7580 INPUT K$
7590 IF K$<>"YES" THEN 7510
7600 PRINT "TYPE 'X' FOR BOMB DETONATION"
7610 INPUT F$
7612 IF F$<>"X" THEN 7510
7620 PRINT CHR$(12)+CHR$(7);
7630 PRINT "!!!!!!!!!!!!!!!! BOMB DETONATED !!!!!!!!!!!!!!!!"
7640 PRINT "SWARM HAS BEEN DESTROYED!!!!!!!!!"
7642 PRINT "CITY HAS BEEN DESTROYED!!!!!!!!!!"
7650 C(A)=C(A)=2413
7660 S(A)=0
7670 E(A)=0
7680 U(A)=0
7690 D(A)=0
7700 G(A)=0
7710 M(A)=0
7720 IF K(A)=1 THEN 7810
7730 IF V(A)<>-1 THEN 7800
7740 PRINT "THE POPULATION WILL MOVE BACK TO THE CITY"
7750 PRINT "IN SECTION ";A;" WHEN THE RADIATION LEVEL"
7760 PRINT "HAS DECREASED."
7770 K(A)=2
7780 R(A)=T+7
7790 GOTO 3480
7800 C(A)=INT(2000000*(RND*A+1))+C(A)
7810 PRINT "** NO SURVIVORS ARE REPORTED IN SECTION ";A
7820 PRINT "** THE BEES WILL NOT ENTER A SECTION WITH"
7830 PRINT "** NO HUMAN INHABITANTS, SO SECTION ";A
7840 PRINT "** IS COMPLETELY LACKIN LIFE OF ANY KIND."
7850 K(A)=2
7860 R(A)=-1
7870 GOTO 3480
7880 REM CASUALTY REPORT
7890 PRINT CHR$(12);"6)      CASUALTY REPORT"
7900 PRINT "ENTER SECTION #";
7910 INPUT A
7920 PRINT "************************************"
7930 IF A<>0 THEN 7980
7940 D7=-1
7950 FOR A=1 TO 21
7960 IF C(A)=0 THEN 8000
7970 IF C(A)>=1000000 THEN 8020
7980 PRINT "SECTION ";A;":    ";C(A);" CASUALTIES REPORTED"
7990 GOTO 8030
8000 PRINT "SECTION ";A;":      NO BEE RELATED CASUALTIES"
8010 GOTO 8030
8020 PRINT "SECTION ";A;":    ";C(A)/1000000;" MILLION CASUALTIES"
8030 PRINT "************************************"
8040 F=C(A)+F
8050 IF D7<>-1 THEN 8120
8060 NEXT A
8070 D7=0
8080 IF F<1000000 THEN 8110
8090 PRINT "TOTAL CASUALTIES REPORTED: ";F/1000000;" MILLION CASUALTIES"
8100 GOTO 8120
8110 PRINT "TOTAL CASUALTIES REPORTED: ";F
8120 IF W=0 THEN 8430
8130 F=0
8140 GOTO 3480
8150 REM PRINT COMMANDS IN SHORT
8160 PRINT CHR$(12);
8165 PRINT "************************COMMANDS*********************"
8170 PRINT
8180 PRINT "             1) ATTACK SCAN MAP"
8190 PRINT "             2) ETA REPORT"
8200 PRINT "             3) BATTLE PHASE OPTIONS"
8210 PRINT "                   1. BEE COCKTAIL"
8220 PRINT "                   2. PROJECT QUEEN"
8230 PRINT "                   3. PROJECT BRUSH FIRE"
8240 PRINT "                   4. PROJECT STERILE MALE"
8250 PRINT "                   5. DESTRUCTI<N"
8260 PRINT "                   6. URBAN DEFENSES"
8270 PRINT "                        A) FLIGHT PATTERNS"
8280 PRINT "                        B) SONIC BOOM"
8290 PRINT "                        C) SUPER-SONIC BEAMS"
8300 PRINT "                        D) POLLUTION"
8310 PRINT "                        E) METHYL PARATHION"
8320 PRINT "                        F) FIRE WALL"
8330 PRINT "                        G) STROBE LIGHT"
8340 PRINT "             4) EVACUATION PROCEDURES"
8350 PRINT "             5) NUCLEAR DESTRUCTION"
8360 PRINT "             6) CASUALTY REPORT"
8370 PRINT "             7) COMMANDS (SHORT)"
8380 PRINT "             8) CANCEL GAME"
8400 GOTO 3480
8410 REM GAME CANCELLED
8420 REM
8430 REM
9999 END
