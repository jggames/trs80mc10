10 REM *** WRITTEN BY DAVID AHL, DIGITAL
20 &"BOOMERANG PUZZLE FROM ARITHMETICA OF NICOMACHUS -- A.D. 90!"
30 &\&"PLEASE THINK OF A NUMBER BETWEEN 1 AND 100."\&
40 &"GOT IT?     O.K. LET'S START....."
50 INPUT "YOUR NUMBER DIVIDED BY 3 HAS A REMAINDER OF";A
60 INPUT "YOUR NUMBER DIVIDED BY 5 HAS A REMAINDER OF";B
70 INPUT "YOUR NUMBER DIVIDED BY 7 HAS A REMAINDER OF";C
80 &\&"LET ME THINK A MOMENT"\SLEEP(5)\D=70*A+21*B+15*C
90 IF D>105 THEN D=D-105\GOTO 90
110 &\&"YOUR NUMBER WAS"D", RIGHT";\INPUT A$\&
120 IF A$="YES" THEN 150 ELSE IF A$="NO" THEN 160
140 &"EH?  I DON'T UNDERSTAND '"A$"'.  TRY 'YES' OR 'NO'."\GOTO 110
150 &"HOW ABOUT THAT!!"\GOTO 170
160 &"I FEAR YOUR ARITHMETIC IS IN ERROR."
170 &\&\&\&"LET'S TRY ANOTHER."\GOTO 30\END
