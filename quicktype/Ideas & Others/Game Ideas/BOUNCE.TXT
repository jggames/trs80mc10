90 DIM T(20)
100 PRINT "THIS SIMULATION LETS YOU SPECIFY THE INITIAL VELOCITY"
110 PRINT "OF A BALL THROWN STRAIGHT UP, AND THE COEFFICIENT OF" 
120 PRINT "ELASTICITY OF THE BALL. PLEASE USE A DECIMAL FRACTION"
130 PRINT "COEFFICIENT (LESS THAN 1)." 
131 PRINT
132 PRINT "YOU ALSO SPECIFY THE TIME INCREMENT TO BE USED IN"
133 PRINT "'STROBING' THE BALL'S FLIGHT (TRY .1 INITIALLY)." 
134 PRINT
135 PRINT "TIME INCREMENT (SEC)";
136 INPUT S2 
140 PRINT
150 PRINT "VELOCITY (FPS)";
160 INPUT V
165 PRINT
170 PRINT "COEFFICIENT"; 
180 INPUT C
184 PRINT
185 PRINT "FEET" 
186 PRINT
187 S1=INT(70/(V/(16*S2))) 
190 FOR I=1 TO S1
200 T(I)=V*C^(I-1)/16
210 NEXT I 
220 FOR H=INT(-16*(V/32)^2+V^2/32+.5) TO 0 STEP -.5
221 IF INT(H)<>H THEN 225
222 PRINT H; 
225 L=0
230 FOR I=1 TO S1
240 FOR T=0 TO T(I) STEP S2
245 L=L+S2 
250 IF ABS(H-(.5*(-32)*T^2+V*C^(I-1)*T))>.25 THEN 270
260 PRINT TAB(L/S2);"O"; 
270 NEXT T 
275 T=T(I+1)/2 
276 IF -16*T^2+V*C^(I-1)*T<H THEN 290
280 NEXT I 
290 PRINT
300 NEXT H 
310 PRINT TAB(1);
320 FOR I=1 TO INT(L+1)/S2+1 
330 PRINT "."; 
340 NEXT I 
350 PRINT
355 PRINT " 0";
360 FOR I=1 TO INT(L+.9995)
380 PRINT TAB(INT(I/S2));I;
390 NEXT I 
400 PRINT
410 PRINT TAB(INT(L+1)/(2*S2)-2);"SECONDS" 
420 PRINT
430 GO TO 135
440 END
