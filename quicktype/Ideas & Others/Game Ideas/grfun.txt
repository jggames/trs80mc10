NEW

10 GOSUB 4000
20 GOSUB 4300
30 GOSUB 3200
40 GOSUB 3000
50 GOSUB 3100
60 GOSUB 3400

890 GOTO 20
900 A$=INKEY$:IF A$=""THEN900
1000 CLS:END

REM STRIPES
3000 FOR J=0 TO 511:POKE S+J,C( (J AND 7)+1):NEXT:REM STRIPES
3020 RETURN


REM DIAGONAL
3100 FOR Y=0TOSW-1+SH
3110 FOR X=0TO Y:H=Y-X
3120 IF H<SH AND X<SW THEN POKE S+H*SW+X,C((Y AND7)+1)
3140 NEXT:NEXT
3150 RETURN

REM RANDOM FILL OF SCREEN
3200 FOR J=0 TO 999
3210 R=RND(9)-1
3220 RX=RND(SW)-1:RY=RND(SH)-1
3230 POKE S+RY*SW+RX,C(R)
3240 NEXT
3250 RETURN

REM 2X3 RECTANGLE
3300 POKE S+OY*SW+OX,C(R)
3310 POKE S+(OY+1)*SW+OX,C(R)
3330 POKE S+OY*SW+1+OX,C(R)
3340 POKE S+(OY+1)*SW+1+OX,C(R)
3360 POKE S+OY*SW+2+OX,C(R)
3370 POKE S+(OY+1)*SW+2+OX,C(R)
3390 RETURN


REM FOUR PART KALEIDOSCOPE OF RECTANGLES
3400 FOR J=0TO199
3410 R=RND(9)-1
3415 FOR K=0TO3
3420 RX=RND(14)-1:RY=RND(7)-1
3430 OX=29-RX:OY=14-RY:GOSUB 3300
3440 OX=RX:OY=RY:GOSUB 3300
3450 OX=RX:OY=14-RY:GOSUB 3300
3460 OX=29-RX:OY=RY:GOSUB 3300
3470 NEXT:NEXT
3480 RETURN

REM INIT
4000 S=16384:SW=32:SH=16:REM INIT
4010 DIM C(9)
4020 C(0)=128:C(1)=143:C(2)=159
4030 C(3)=175:C(4)=191:C(5)=207
4040 C(6)=223:C(7)=239:C(8)=255
4050 CLS:PRINT"SCREEN FUN, BY JAMES"
4060 PRINT"**PRESS A KEY TO BEGIN**"
4070 A$=INKEY$:J=RND(2)
4080 IF A$=""THEN4070
4100 X=0:Y=0:W=0:H=0:N=0
4110 RETURN
4300 FOR J=0TO511:POKES+J,C(0):NEXT:RETURN:REM BLACK STRIPES

REM TYPE RUN

