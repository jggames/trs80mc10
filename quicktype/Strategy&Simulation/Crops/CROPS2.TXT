5 CLS:PRINT"CROPS":PRINT
7 A=0:B=0:C=0
10 REM* CROPS PLANTED * 
12 D=500:T=500 
14 REM* INITIAL INSECT POPULATION *
16 F=INT(RND(0)*500)+1:G=0 
18 N=INT(RND(0)*500)+1:I=0 
19 S=INT(RND(0)*500)+1:E=INT(RND(0)*100)-9 
20 REM* DAY'S REPORT * 
22 C=C+1
24 W=INT(RND(0)*10)+1
26 CLS:PRINT"YOU HAVE:":PRINT"DRECKMELON PLANTS"D 
28 PRINT"TREPHULE BUSHES"T:PRINT"IRRIGATION DITCHES"I
30 PRINT"FRUBEES"F:PRINT"NELLUM SPIDERS"N
32 PRINT"STRUNKFLIES"S:PRINT"GUNTHERBUGS"G
34 PRINT"DAY #";C:PRINT"TEMPERATURE";E;"DEGREES"
36 IF E<40 THEN GOSUB 235 
40 IF A>5 THEN 200 
45 IF W>4 THEN 205 
50 A=A+1 
52 PRINT"DRY DAY"
54 E=E+RND(25)-12 
56 X=RND(F)/1000
58 Y=T*X:T=T+INT(Y):D=D-INT(Y)
60 X=RND(S) 
62 Y=RND(10)+4
64 IF B>Y THEN GOSUB 220 
66 X=X/1500 
68 T=INT(T-(T*X))
70 X=RND(N)/5
72 S=S-INT(X):F=F-INT(X)
74 G=G+(I*RND(100))
76 IF G<5 THEN 87
78 REM* LINES 55 TO 95 ARE CROP & INSECT ADJUST* 
80 X=INT(RND(G)/20)
82 D=D-INT(X)
84 X=INT(RND(G)/20)
86 T=T-X
87 X=A-I
88 IF X>5 THEN GOSUB 260 
90 X=C/20
92 IF X=INT(X) THEN 275 
93 X=D+T 
94 IF X<50 THEN 350 
95 IF X>4000 THEN 370 
96 REM* THE PLAY* 
97 PRINT"1 RELEASE INSECTS  2 SPRAY" 
98 G=INT(G+(G*.1)):F=INT(F+(F*.11)) 
99 S=INT(S+(S*.12)):N=INT(N+(N*.15)) 
100 PRINT"3 DIG A DITCH","4 CLOSE A DITCH"
102 PRINT"OR 0 TO PASS"
104 INPUT L 
106 IF L=1 THEN 300 
108 IF L=2 THEN 125 
110 IF L=3 THEN I=I+1 
115 IF L=4 THEN I=I-1 
117 IF I<1 THEN I=0
120 GOTO 20 
125 REM* INSECTICIDE * 
127 PRINT"1 FOR DDS, 2 FOR RQL":PRINT"OR 3 FOR MPN"
130 INPUT L
132 IF L=1 THEN 160
135 IF L=2 THEN 180
137 REM* MPN EFFECTS * 
140 D=INT(D-(D*.58)) 
142 T=INT(T-(T*.58)) 
144 S=INT(S-(S*.86)) 
146 N=INT(N-(N*.86)) 
148 F=INT(F-(F*.86)) 
150 G=INT(G-(G*.86)) 
155 GOTO 20
160 REM* DDS EFFECTS *
162 D=D-INT(D*.025) 
164 T=T-INT(T*.025) 
166 S=S-INT(S*.13) 
168 N=N-INT(N*.13) 
170 F=F-INT(F*.13) 
172 G=G-INT(G*.13) 
175 GOTO 20
180 REM* RQL EFFECTS * 
182 D=D-INT(D*.27) 
184 T=T-INT(T*.27) 
186 S=S-INT(S*.38) 
188 N=N-INT(N*.38) 
190 F=F-INT(F*.38) 
192 G=G-INT(G*.38) 
195 GOTO 20
200 REM* RAIN SELECT * 
202 IF W<7 THEN 50 
205 PRINT "RAIN"
207 A=A-3
210 IF A<1 THEN A=0 
215 GOTO 54 
220 REM* WEATHER EFFECTS * 
222 Z=INT(X/200)
224 IF D>Z THEN 230
226 D=INT(D-(RND(D/6)):RETURN
230 D=INT(RND(Z/3)+2*(Z/3)):RETURN
235 B=B+1
240 IF E<20 THEN B=B+.5 
245 IF E<10 THEN B=B+.75 
250 IF E<0 THEN B=B+1 
255 RETURN 
260 REM * DROUGHT DAMAGE * 
262 J=RND(100)/100
264 K=RND(100)/100
266 D=D-(J*D) 
268 T=T-(T*K) 
270 D=INT(D):T=INT(T)
272 RETURN
275 REM* CROP GROWTH * 
280 Y=D*1.5 
285 D=D+INT(RND(0)*Y+1) 
290 Y=T*1.5 
292 T=T+INT(RND(0)*Y-1) 
295 RETURN
300 PRINT"1 FOR STRUNKFLIES":PRINT"2 FOR NELLUM SPIDERS"
305 PRINT"3 FOR FRUBEES":PRINT"4 FOR GUNTHERBUGS"
307 INPUT X
310 PRINT"HOW MANY ";
312 INPUT Y 
315 IFY<0THENY=0-Y 
317 Y=INT(Y) 
320 IF X=4 THEN 340 
322 IF X=1 THEN S=S+Y 
325 IF X=2 THEN N=N+Y 
330 IF X=3 THEN F=F+Y 
335 GOTO 20 
340 PRINT"WHY WOULD YOU WANT ";Y:PRINT"MORE GUNTHER BUGS!?"
342 G=G+Y:FORZZ=1TO2500:NEXT
345 GOTO 20
350 REM* LOSE * 
352 PRINT"YOU'VE SUCCEEDED IN KILLING":PRINT"OFF MOST OF YOUR CROPS."
355 PRINT"YOU'RE DOWN TO";D:PRINT"DRECKMELON PLANTS"
360 PRINT"AND ";T;" TREPHULE BUSHES"
362 PRINT 
365 PRINT"WHAT A LOUSY FARMER!"
367 END 
370 PRINT"WOW!" 
375 PRINT"YOU HAVE";D;"DRECKMELON":PRINT"PLANTS"
380 PRINT"AND";T;"TREPHULE BUSHES" 
385 PRINT 
390 REM * WIN* 
395 PRINT"YOU DESERVE AN AWARD IN":PRINT"AGRICULTURE!"
400 END
