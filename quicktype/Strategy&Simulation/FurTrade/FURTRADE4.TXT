0 CLEAR500:DIMCC,ZZ,M$,I$:GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 CC=CC+(CC>255)*(CC-255):NEXT:M$="":RETURN
7 ?M$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
8 CC=VAL(M$):ZZ=LEN(STR$(INT(CC)))-1:I$=RIGHT$("        "+STR$(INT(CC)),ZZ):I$=I$+MID$(STR$((CC-INT(CC))+1.00001)+"000",3,3)
9 RETURN
10 CLS
20 PRINT@139,"FUR TRADER"
30 PRINT@202,"COPYRIGHT BY"
40 PRINT@263,"CREATIVE COMPUTING"
50 PRINT@322,"MORRISTOWN, NEW JERSEY 07960"
60 P=1:GOSUB1850
70 DIM F(4)
80 PRINT@9,"* FUR TRADER *"
90 GOSUB460
100 I=600
110 PRINT@13*32,"DO YOU WISH TO TRADE FURS";
120 INPUTB$
130 IF LEFT$(B$,1)="Y"THEN160
140 P=1:GOSUB1850
150 PRINT"FAIRWELL MY FRIEND...":END
160 P=1:GOSUB1850
170 M$=STR$(I):GOSUB8:M$="YOU HAVE "+I$+" IN SAVINGS "
180 M$=M$+"AND 190 FURS TO BEGIN THE EXPEDITION.":GOSUB1
190 E1=INT((.15*RND(0)+.95)*10^2+.5)/10^2
200 B1=INT((.25*RND(0)+1)*10^2+.5)/10^2
210 PRINT
220 M$="YOUR 190 FURS ARE DISTRIBUTED AMONG THE FOLLOWING "
230 M$=M$+"KINDS OF PELTS: MINK, BEAVER, ERMINE AND FOX.":GOSUB1
240 GOSUB1800
250 RESTORE
260 FORJ=1TO4
270 READB$
280 PRINT
290 M$="HOW MANY "+B$+" PELTS DO YOU HAVE":GOSUB1
300 INPUTF(J)
310 F(0)=F(1)+F(2)+F(3)+F(4)
320 IFF(0)=190THEN540
330 IFF(0)>190THEN370
340 NEXTJ
350 FORI=1TO1000:NEXT
360 GOTO540
370 PRINT
380 CLS4:PRINT@6*32,"YOU MAY NOT HAVE THAT MANY FURS.";
390 PRINT"DO NOT TRY TO CHEAT. I CAN ADD."
400 PRINT"YOU MUST START AGAIN."
410 P=3:GOSUB1850
420 GOTO80
430 PRINT
440 PRINT"WANT TO TRADE FURS NEXT YEAR";
450 GOTO120
460 REM  INTRODUCTION
470 M$="YOU ARE THE LEADER OF A FRENCH FUR TRADING EXPEDITION IN "
480 M$=M$+"1776 LEAVING THE LAKE ONTARIO AREA TO SELL FURS AND GET "
490 M$=M$+"SUPPLIES FOR THE NEXT YEAR.":GOSUB1:M$="YOU HAVE A CHOICE OF THREE "
500 M$=M$+"FORTS AT WHICH YOU MAY TRADE. THE COST OF SUPPLIES "
510 M$=M$+"AND THE AMOUNT YOU RECEIVE FOR YOUR FURS WILL DEPEND "
520 M$=M$+"ON THE FORT THAT YOU CHOOSE.":GOSUB1
530 RETURN
540 CLS:PRINT@8,"* FUR TRADER *"
550 M$="YOU MAY TRADE FURS AT:":GOSUB1
560 M$="FORT 1":GOSUB7:PRINT": HOCHELAGA (MONTREAL)"
570 M$="IT IS UNDER THE PROTECTION OF THE FRENCH ARMY.":GOSUB1
580 M$="FORT 2":GOSUB7:PRINT": STADACONA (QUEBEC)"
590 M$="IT IS UNDER THE PROTECTION OF THE FRENCH ARMY. HOWEVER, YOU MUST "
600 M$=M$+"MAKE A PORTAGE AND CROSS THE LACHINE RAPIDS.":GOSUB1
610 M$="FORT 3":GOSUB7:PRINT": NEW YORK"
620 M$="IT IS UNDER DUTCH CONTROL. YOU MUST CROSS THROUGH IROQUOIS LAND.":GOSUB1
630 PRINT"WHICH FORT WOULD YOU LIKE TO    TRADE AT";
640 INPUT B:PRINT
650 IF B=1THEN690
660 IF B=2THEN760
670 IF B=3THEN830
680 GOTO630
690 CLS:GOSUB2000:M$="YOU HAVE CHOSEN THE EASIEST ROUTE.":GOSUB1:M$="HOWEVER, "
700 M$=M$+"FORT HOCHELAGA IS FAR FROM ANY SEAPORT.":GOSUB1:M$="THE VALUE "
710 M$=M$+"YOU RECEIVE FOR YOUR FURS WILL BE LOW AND THE COST "
720 M$=M$+"OF SUPPLIES HIGHER THAN FORTS STADACONA AND NEW YORK.":GOSUB1
730 GOSUB1680
740 IF LEFT$(B$,1)="Y"THEN540
750 GOTO900
760 CLS:GOSUB2050:M$="YOU HAVE CHOSEN A HARD ROUTE.":GOSUB1:M$="IT IS, IN COMPARISON, "
770 M$=M$+"HARDER THAN THE ROUTE TO HOCHELAGA BUT EASIER THAN "
780 M$=M$+"THE ROUTE TO NEW YORK.":GOSUB1:M$="YOU WILL RECEIVE AN AVERAGE "
790 M$=M$+"VALUE FOR YOUR FURS AND THE COST OF YOUR SUPPLIES WILL BE AVERAGE."
791 GOSUB1
800 GOSUB1680
810 IF LEFT$(B$,1)="Y"THEN540
820 GOTO1000
830 CLS:GOSUB2100:M$="YOU HAVE CHOSEN THE MOST DIFFICULT ROUTE.":GOSUB1
840 M$="AT FORT NEW YORK YOU WILL RECEIVE THE HIGHEST VALUE "
850 M$=M$+"FOR YOUR FURS.":GOSUB1:M$="THE COST OF YOUR SUPPLIES "
860 M$=M$+"WILL BE LOWER THAN AT ALL THE OTHER FORTS.:GOSUB1
870 GOSUB1680
880 IF LEFT$(B$,1)="Y"THEN540
890 GOTO1350
900 I=I-160
910 PRINT
920 M1=INT((.2*RND(0)+.7)*10^2+.5)/10^2
930 E1=INT((.2*RND(0)+.65)*10^2+.5)/10^2
940 B1=INT((.2*RND(0)+.75)*10^2+.5)/10^2
950 D1=INT((.2*RND(0)+.8)*10^2)/10^2
960 P=1:GOSUB1850
970 CLS:M$="SUPPLIES AT FORT HOCHELAGA COST $150.00":GOSUB1
980 M$="YOUR TRAVEL EXPENSES TO HOCHELAGA WERE $10.00":GOSUB1
990 GOTO1710
1000 I=I-140
1010 PRINT
1020 M1=INT((.3*RND(0)+.85)*10^2+.5)/10^2
1030 E1=INT((.15*RND(0)+.8)*10^2+.5)/10^2
1040 B1=INT((.2*RND(0)+.9)*10^2+.5)/10^2
1050 P=INT(10*RND(0))+1
1060 IF P<=2THEN1100
1070 IF P<=6THEN1170
1080 IF P<=8THEN1200
1090 IFP<=10THEN1260
1100 F(2)=0
1110 CLS:M$="YOUR BEAVER WERE TOO HEAVY TO CARRY ACROSS "
1120 M$=M$+"THE PORTAGE.":GOSUB1:M$="YOU HAD TO LEAVE THE PELTS, BUT FOUND "
1130 M$=M$+"THEM STOLEN WHEN YOU RETURNED.":GOSUB1
1140 P=2:GOSUB1850
1150 GOSUB1320
1160 GOTO1730
1170 CLS:M$="YOU ARRIVED SAFELY AT FORT STADACONA.":GOSUB1
1180 P=2:GOSUB1850
1190 GOTO1300
1200 GOSUB1800
1210 CLS:M$="YOUR CANOE UPSET IN THE LACHINE RAPIDS. "
1220 M$=M$+"YOU LOST ALL YOUR FURS.":GOSUB1
1230 P=2:GOSUB1850
1240 GOSUB1320
1250 GOTO1760
1260 F(4)=0
1270 CLS:M$="YOUR FOX PELTS WERE NOT CURED PROPERLY.":GOSUB1
1280 M$="NO ONE WILL BUY THEM.":GOSUB1
1290 P=2:GOSUB1850
1300 GOSUB1320
1310 GOTO1710
1320 M$="SUPPLIES AT FORT STADACONA COST $125.00":GOSUB1
1330 M$="YOUR TRAVEL EXPENSES TO STADACONA WERE $15.00":GOSUB1:PRINT
1340 RETURN
1350 I=I-105
1360 PRINT
1370 M1=INT((.15*RND(0)+1.05)*10^2+.5)/10^2
1380 D1=INT((.25*RND(0)+1.1)*10^2+.5)/10^2
1390 P=INT(10*RND(0))+1
1400 IF P<=2THEN1440
1410 IF P<=6THEN1480
1420 IF P<=8THEN1520
1430 IFP<=10THEN1580
1440 CLS:M$="YOU WERE ATTACKED BY A PARTY OF IROQUOIS.":GOSUB1
1450 M$="ALL PEOPLE IN YOUR TRADING GROUP WERE KILLED.":GOSUB1
1460 M$="THIS ENDS THE GAME.":GOSUB1
1470 END
1480 CLS:M$="YOU WERE FORTUNATE. YOU ARRIVED SAFELY "
1490 M$=M$+"AT FORT NEW YORK.":GOSUB1
1500 P=2:GOSUB1850
1510 GOTO1630
1520 GOSUB1800
1530 CLS:M$="YOU NARROWLY ESCAPED AN IROQUOIS RAIDING PARTY.":GOSUB1
1540 M$="HOWEVER, YOU HAD TO LEAVE ALL YOUR FURS BEHIND.":GOSUB1
1550 P=2:GOSUB1850
1560 GOSUB1650
1570 GOTO1760
1580 B1=B1/2
1590 M1=M1/2
1600 CLS:M$="YOUR MINK AND BEAVER WERE DAMAGED ON YOUR TRIP.":GOSUB1
1610 M$="YOU RECEIVE ONLY HALF THE CURRENT PRICE FOR THESE FURS.":GOSUB1
1620 P=2:GOSUB1850
1630 GOSUB1650
1640 GOTO1710
1650 M$="SUPPLIES AT NEW YORK COST $80.00":GOSUB1
1660 M$="YOUR TRAVEL EXPENSES TO NEW YORK WERE $25.00":GOSUB1
1670 RETURN
1680 PRINT:PRINT"DO YOU WANT TO TRADE AT ANOTHER FORT";
1690 INPUT B$:CLS
1700 RETURN
1710 REM
1720 M$=STR$(B1*F(2)):GOSUB8:PRINT"YOUR BEAVER SOLD FOR $";I$
1730 M$=STR$(D1*F(4)):GOSUB8:PRINT"YOUR FOX SOLD FOR    $";I$
1740 M$=STR$(E1*F(3)):GOSUB8:PRINT"YOUR ERMINE SOLD FOR $";I$
1750 M$=STR$(M1*F(1)):GOSUB8:PRINT"YOUR MINK SOLD FOR   $";I$
1760 I=M1*F(1)+B1*F(2)+E1*F(3)+D1*F(4)+I
1770 PRINT
1780 M$=STR$(I):GOSUB8:PRINT"YOU NOW HAVE $";I$
1790 GOTO430
1800 FORJ=1TO4
1810 F(J)=0
1820 NEXTJ
1830 RETURN
1840 DATA"MINK","BEAVER","ERMINE","FOX"
1850 FORD=1TOP
1860 FORF=1TO1500
1870 NEXTF
1880 NEXTD
1890 CLS
1900 RETURN
2000 PRINT"ŒŒŒŒ"
2010 PRINT"Žˆ€€€€€"
2020 PRINT"Õß €…‚€ƒ€€‡"
2030 PRINT"…Œ€„ˆŒ„ˆŒˆ€€‚€€"
2040 PRINT"…€€€€€€€€€€…€€"
2045 RETURN
2050 PRINT
2055 PRINT"ÕßŽ€ˆ€€„ˆ„"
2060 PRINT"…ˆ€„ˆŒ„ˆ€€„ˆŒ„€€"
2070 PRINT"…€€€€€€€…Š€€€€€€…"
2075 RETURN
2100 PRINT
2101 PRINT"Šÿÿ"
2105 PRINT"Š"
2110 PRINT"ŽŒ€ŒŒŒŒˆ€€€€"
2120 PRINT"€€‚€€€€€€€€€ˆˆˆˆ…"
2130 PRINT"€€€€€€€€€€€€€€€€€€…"
2135 PRINT"¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯";
2140 RETURN
3000 REM MC-10 EDITS JIM GERRIE
