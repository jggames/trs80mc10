0 CLS:CLEAR2000:DIMDD,CC,M$,D$(20),C$(5),MP(15),PL$(15):DD=RND(-(PEEK(9)*256+PEEK(10))):GOTO199
1 DD=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,DD,CC-DD):DD=CC+1:CC=DD+31:IFDD<=LEN(M$)THEN2
4 RETURN
120 DATA6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
150 DATA"APRIL 12","APRIL 26","MAY 10","MAY 24","JUNE 7"
160 DATA"JUNE 21","JULY 5","JULY 19","AUGUST 2","AUGUST 16"
170 DATA"AUGUST 31","SEPTEMBER 13","SEPTEMBER 27","OCTOBER 11"
180 DATA"OCTOBER 25","NOVEMBER 8","NOVEMBER 22","DECEMBER 20"
190 DATA5,"ON THE HIGH PRAIRIE."
191 DATA200,"NEAR INDEPENDENCE CROSSING ON THE BIG BLUE RIVER.",350,"FOLLOWING THE PLATTE RIVER."
193 DATA450,"NEAR FORT KEARNEY.",600,"FOLLOWING THE NORTH PLATTE RIVER.",750,"WITHIN SIGHT OF CHIMNEY ROCK."
194 DATA850,"NEAR FORT LARAMIE.",1000,"CLOSE UPON INDEPENDENCE ROCK.",1050,"IN THE BIG HORN MOUNTAINS."
195 DATA1150,"FOLLOWING THE GREEN RIVER.",1250,"NOT TOO FAR FROM FORT HALL.",1400,"FOLLOWING THE SNAKE RIVER."
196 DATA1550,"NOT FAR FROM FORT BOISE.",1850,"IN THE BLUE MOUNTAINS.",2040,"FOLLOWING THE COLUMBIA RIVER."
197 DATAMONDAY,TUESDAY,WEDNEDAY,THURSDAY,FRIDAY,SATURDAY,SUNDAY
199 FORA=1TO15:READZ:NEXTA:FORA=1TO18:READD$(A):NEXT:FORI=1TO15:READMP(I),PL$(I):NEXTI
200 AA=4:CLS:PRINT:PRINT:PRINTTAB(AA)"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ‚":PRINTTAB(AA)"…     O R E G O N      Š"
201 PRINTTAB(AA)"…                      Š":PRINTTAB(AA)"…      T R A I L       Š"
202 PRINTTAB(AA)"„ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒˆ":PRINT:PRINT:PRINT
210 PRINT@292,"DO YOU NEED INSTRUCTIONS        (Y OR N)";CHR$(31);:INPUT N$:IF N$="N"THEN560
211 IFN$<>"Y"THEN210  
220 CLS:PRINT:PRINT:PRINT"THIS PROGRAM SIMULATES A TRIP   OVER THE ";
230 PRINT"OREGON TRAIL FROM      INDEPENDENCE, MISSOURI, ";
240 PRINT"TO      OREGON CITY, OREGON, IN 1847.":PRINT
250 PRINT"YOUR FAMILY WILL COVER THE 2040 MILE OREGON ";
260 PRINT"TRAIL IN 5-6 MONTHS --- IF YOU  MAKE IT ALIVE."
270 PRINT:PRINT"YOU HAVE SAVED $900 TO SPEND FORTHE TRIP, AND YOU'VE JUST PAID"
280 PRINT"$200 FOR A WAGON.";:GOSUB310:GOTO330
300 PRINT@0,"YOU WILL NEED TO SPEND THE REST OF YOUR MONEY ON THE FOLLOWING  ITEMS:"
310 AA$="PRESS <ENTER> TO CONTINUE":PRINT@495-(LEN(AA$)/2),AA$;
320 Y$=INKEY$:IFY$<>CHR$(13)THEN320
321 CLS:RETURN
330 PRINT@160,"OXEN - YOU CAN SPEND $200-$300.        THE MORE YOU SPEND THE"
331 PRINT"       FASTER YOU WILL GO              BECAUSE YOU WILL HAVE           BETTER ANIMALS.":GOSUB300
360 PRINT@160,"FOOD - THE MORE YOU HAVE THE           LESS CHANCE THERE IS OF         GETTING SICK."
370 GOSUB300
380 PRINT@160,"AMMUNITION - $1 BUYS A BELT OF               50 BULLETS FOR"
381 PRINT"             ATTACKS BY ANIMALS,             BANDITS AND FOR                 HUNTING FOOD."
400 GOSUB300
410 PRINT@160,"CLOTHING - THIS IS ESPECIALLY              IMPORTANT FOR THE"
411 PRINT"           COLD WEATHER YOU WILL           ENCOUNTER WHEN"
412 PRINT"           CROSSING THE                    MOUNTAINS."   
430 GOSUB300
440 PRINT@160,"MISCELLANEOUS SUPPLIES - THIS   INCLUDES MEDICINE AND OTHER"
441 PRINT"THINGS YOU WILL NEED FOR        SICKNESS AND EMERGENCY          REPAIRS." 
460 GOSUB300
470 PRINT@128,"YOU CAN SPEND ALL OF YOUR MONEY BEFORE YOU START YOUR TRIP"
480 PRINT"OR YOU CAN SAVE SOME OF YOUR    CASH TO SPEND AT FORTS ALONG THE";
490 PRINT"WAY WHEN YOU RUN LOW ON FOOD ANDOTHER SUPPLIES."
500 GOSUB310:PRINT@128,"HOWEVER, ITEMS COST MORE AT THE FORTS. YOU CAN ALSO GO HUNTING"
510 PRINT"ALONG THE WAY TO GET MORE FOOD. WHENEVER YOU HAVE TO USE YOUR"
520 PRINT"TRUSTY RIFLE, YOU WILL BE TOLD  TO TYPE IN A WORD THAT SOUNDS   LIKE A GUNSHOT.":GOSUB310
530 PRINT@128,"THE FASTER YOU TYPE IN THE WORD AND PRESS THE <ENTER> KEY, THE"
540 PRINT"BETTER CHANCE YOU WILL HAVE OF  HITTING YOUR TARGET."
550 GOSUB310:PRINT@128,"AT EACH TURN, ALL ITEMS ARE     SHOWN IN DOLLAR AMOUNTS EXCEPT  BULLETS. WHEN YOU ARE ASKED TO"
551 PRINT"ENTER DOLLAR AMOUNTS, DON'T USE THE '$'.":GOSUB310
560 CLS:PRINTTAB(8)"----------------":FORD=1TO14:PRINTTAB(8)"G O O D  L U C K":FORDD=1TO25:NEXTDD,D:FORDD=1TO2000:NEXT
570 CLS:PRINT"HOW GOOD A SHOT ARE YOU WITH    YOUR RIFLE?"
580 PRINT"      1. ACE MARKSMAN"
590 PRINT"      2. GOOD SHOT"
600 PRINT"      3. FAIRLY FAIR"
610 PRINT"      4. NEED PRACTICE"
620 PRINT"      5. SHAKY KNEES"
630 PRINT:PRINT"ENTER ONE OF THE ABOVE -- THE   BETTER YOU CLAIM YOU ARE, THE"
631 PRINT"FASTER YOU'LL HAVE TO BE WITH   YOUR GUN TO BE SUCCESSFUL."
660 D8=0:PRINT@416,"":PRINT@416,;:INPUT"WELL, HOW GOOD A SHOT ARE YOU   (1 TO 5)";D8$:D8=VAL(D8$)
670 IFD8<1ORD8>5THEN660  
690 X1=-1:T=700
700 K8=0:S4=0:F1=0:F2=0:M=0:M9=0:D3=0
710 CLS:PRINT"YOU HAVE"T"DOLLARS.":PRINT
720 PRINT"HOW MUCH DO YOU WANT TO SPEND ONYOUR ";
730 INPUT"OXEN TEAM ($200-$300)";IN$
740 IFVAL(IN$)>199ANDVAL(IN$)<301THEN760  
750 PRINT" !!!!! NOT POSSIBLE !!!!!!!":GOSUB310:GOTO710
760 A=INT(VAL(IN$)):P=A:GOSUB880  
770 M$="FOOD":GOSUB820:F=P
780 M$="AMMUNITION?  ($1 = 50 BULLETS)":GOSUB820:B=P
790 M$="CLOTHING":GOSUB820:C=P
800 M$="MISCELLANEOUS SUPPLIES":Q0=1:GOSUB820:M1=P
810 GOTO900  
820 GOSUB885:P$="":PRINT"HOW MUCH DO YOU WANT TO SPEND ON";M$;:INPUTP$
830 P=VAL(P$):IFT-P>0ANDP>-1THEN850  
840 PRINT"IMPOSSIBLE!!":GOSUB310:GOTO820  
850 GOSUB880  
860 IFM1<0THEN3860 
870 RETURN
880 T=T-P:RETURN
885 CLS:PRINT"YOU HAVE"T"DOLLARS LEFT.":PRINT:RETURN
900 B=B*50
910 CLS:PRINT"AFTER ALL YOUR PURCHASES, YOU   HAVE"T"DOLLARS LEFT."
920 PRINT"MONDAY, MARCH 29, 1847":GOSUB310  
930 GOTO1100
940 IFM>=2040THEN4140 
950 IFF<0THEN3810 
960 IFB<0THENB=0
970 IFC<0THENC=0
980 IFM1<0THENM1=0
990 IFT<0THENT=0
1000 D3=D3+1
1010 CLS
1030 IFD3=18THEN1060 
1040 M$="MONDAY, "+D$(D3)+", 1847":GOSUB1:M$="YOU ARE "
1045 FORI=1TO15:IFM>MP(I)THENDD=I+1
1046 NEXT:M$=M$+PL$(DD):GOSUB1
1050 GOTO1100 
1060 PRINT"YOU HAVE BEEN ON THE TRAIL TOO  LONG."
1070 PRINT"---------YOUR FAMILY DIES IN THE";
1080 PRINT"FIRST BLIZZARD OF WINTER."
1090 GOTO3920
1100 IFF>=13THEN1120 
1110 PRINT"YOU'D BETTER DO SOME HUNTING OR BUY FOOD, AND SOON!"
1120 F=INT(F):B=INT(B):C=INT(C):M1=INT(M1):T=INT(T):M=INT(M)
1130 M2=M
1140 IFS4=1ORK8=1THEN1160 
1150 GOTO1190
1160 T=T-20:IFT<0THEN3830 
1170 PRINT"THE DOCTOR BILL WAS $20."
1180 K8=0:S4=0
1190 PRINT@256,"YOU HAVE"T"DOLLARS LEFT.";:GOSUB310:CLS
1210 GOSUB5530:X=0:PRINT@384,TAB(9)"DO YOU WANT TO"
1211 PRINTTAB(6)"1. HUNT  2. CONTINUE":A2=0
1220 IFX1=1THENA2=1:PRINTTAB(6);"3. STOP AT NEXT FORT "
1230 X$=INKEY$:IFX$=""THEN1230
1231 X=VAL(X$)
1240 IFX<1THEN1230
1250 IFX>2+A2THEN1230
1260 IFX<>0ORB>39THEN1290 
1270 PRINT"TOUGH --- YOU NEED MORE BULLETS TO GO HUNTING."
1280 GOTO1210 
1290 PRINT@0,"":PRINT@0,;:X1=-X1
1300 ONXGOTO1550,1690,1310
1310 IFA2<>1THENX1=0:GOTO1210
1311 CLS:PRINT@0,"HOW MUCH DO YOU WISH TO SPEND ONTHE FOLLOWING: "
1320 GOTO1410 
1330 P=0:P$="":INPUTP$:IFP$=""THEN1400
1331 P=VAL(P$):IFP<0THEN1400
1340 T=T-P
1350 IFT>-1THEN1400 
1360 PRINT"YOU DO NOT HAVE THAT MUCH MONEY."
1370 PRINT"KEEP YOUR SPENDING DOWN."
1380 T=T+P
1390 P=0
1400 RETURN
1410 PRINT"FOOD";
1420 GOSUB1330
1430 F=F+2/3*P
1440 PRINT"AMMUNITION";
1450 GOSUB1330 
1460 B=INT(B+2/3*P*50)
1470 PRINT"CLOTHING";
1480 GOSUB1330 
1490 C=C+2/3*P
1500 PRINT"MISC. SUPPLIES";
1510 GOSUB1330 
1520 M1=M1+2/3*P
1530 M=M-45
1540 GOTO1690
1550 M=M-45
1560 A1=1:GOSUB4740:CLS
1570 IFB1<=D8THEN1630 
1580 IFRND(100)<13*B1THEN1680 
1590 F=F+48-2*B1
1600 PRINT"NICE SHOT ---- RIGHT ON TARGET  ---- GOOD EATING!!"
1610 B=B-10-3*B1
1620 GOTO1670 
1630 PRINT"RIGHT BETWEEN THE EYES -- YOU   GOT A BIG ONE"
1640 PRINT"-- FULL BELLIES TONIGHT!!"
1650 F=F+52+RND(6)
1660 B=B-10-RND(4)
1670 GOSUB310:GOTO1690 
1680 PRINT"YOU MISSED -- YOUR DINNER GOT   AWAY!!":GOTO1670 
1690 IFF>13THEN1710 
1700 GOTO3810 
1710 CLS:PRINT@192,"":PRINT@192,TAB(8);"DO YOU WANT TO EAT:"
1720 PRINTTAB(8)"1. POORLY"
1730 PRINTTAB(8)"2. MODERATELY "
1740 PRINTTAB(8)"3. WELL"
1750 PRINT@320,"":PRINT@328,;:INPUTE$:IFE$=""THEN1750
1751 E=VAL(E$)
1760 IFE<1THEN1750 
1770 IFE>3THEN1750 
1780 E=INT(E)
1790 F=F-(8+5*E)
1800 IFF>0THEN1830 
1810 F=F+(8+5*E)
1820 PRINT"YOU CAN'T EAT THAT WELL!!":GOSUB310:GOTO1710 
1830 PRINT@0,;:M=M+200+(A-220)/5+RND(10)
1840 L1=0:C1=0
1850 IFRND(10)>((M/100)^2+72)/((M/100-4)^2+12)-1THEN2340 
1860 CLS:PRINT@0,;:M$="RIDERS AHEAD. THEY "
1870 S5=0
1880 IFRND(10)>8THEN1900
1890 M$=M$+"DON'T "
1900 M$=M$+"LOOK HOSTLE.":GOSUB1:PRINTTAB(8)"TACTICS:"
1910 PRINTTAB(8)"1. RUN"
1920 PRINTTAB(8)"2. ATTACK"
1930 PRINTTAB(8)"3. CONTINUE"
1940 PRINTTAB(8)"4. CIRCLE WAGONS"
1950 IFRND(10)<2THENS5=1-S5
1960 PRINTTAB(8);:INPUTIN$
1970 IFVAL(IN$)<0ORVAL(IN$)>4THEN1960
1980 T1=VAL(IN$)
1990 T1=INT(T1)
2000 IFS5=1THEN2170 
2010 IFT1>1THEN2030
2020 M=M+20:M1=M1-15:B=B-150:GOTO2270
2030 A=A-40:IFT1>2THEN2120 
2040 A1=2:GOSUB4740:CLS
2050 B=B-B1*40-80
2060 IFB1>1.5THEN2080 
2070 PRINT"NICE SHOOTING -- YOU DROVE THEM OFF!!":GOTO2270 
2080 IFB1<=4THEN2110
2090 PRINT"LOUSY SHOT -- YOU GOT KNIFED!!":K8=1
2100 PRINT"YOU'LL HAVE TO GO SEE OL' DOC   BLANCHARD.":GOTO2270
2110 PRINT"KINDA SLOW WITH YOUR COLT .44!!":GOTO2270
2120 IFT1>3THEN2150
2130 IFRND(10)>8THEN2250
2140 B=B-150:M1=M1-15:GOTO2270
2150 M=M-50:GOTO2340
2160 B=B-B1*30-80:M=M-25:GOTO2060
2170 IFT1>1THEN2190
2180 M=M+15:A=A-10:GOTO2270
2190 IFT1>2THEN2210
2200 B=B-100:M=M-5:GOTO2270
2210 IFT1>3THEN2230
2220 GOTO2270
2230 M=M-20
2240 GOTO2270
2250 CLS:PRINT"THEY DID NOT ATTACK."
2260 GOTO2320
2270 IFS5=0THEN2290
2280 PRINT"THE RIDERS WERE FRIENDLY, BUT   CHECK FOR POSSIBLE LOSSES.":GOTO2320
2290 PRINT"THE RIDERS WERE HOSTILE -->     CHECK FOR LOSSES!!"
2320 GOSUB310
2340 D1=0
2350 RESTORE
2360 R1=RND(100)
2370 D1=D1+1
2380 PRINT@0,;:IFD1=16THEN3410
2390 READD
2400 IFR1>DTHEN2370
2410 IFD1>10THEN2430
2420 CLS:OND1GOTO2440,2480,2510,2550,2580,2610,2640,2710,2880,2940
2430 CLS:OND1-10GOTO2970,3050,3100,3300,3350,3410,3460
2440 PRINT"WAGON BREAKS DOWN -- YOU LOSE   TIME AND SUPPLIES FIXING IT."
2450 M=M-15-5*RND(0)
2460 M1=M1-8
2470 GOTO3430
2480 PRINT"AN OX HURTS ITS LEG AND SLOWS   YOU DOWN THE REST OF THE WAY."
2490 M=M-25:A=A-20
2500 GOTO3430
2510 PRINT"BAD LUCK -- YOUR DAUGHTER BROKE HER ARM.  YOU HAD TO STOP AND   USE SUPPLIES TO MAKE A SLING."
2520 M=M-5-4*RND(0)
2530 M1=M1-2-3*RND(0)
2540 GOTO3430
2550 PRINT"AN OX WANDERS OFF AND YOU SPEND TIME LOOKING FOR IT."
2560 M=M-17
2570 GOTO3430
2580 M$="YOUR SON GETS LOST -- YOU SPEND HALF THE DAY LOOKING FOR HIM.":GOSUB1
2590 M=M-10
2600 GOTO3430
2610 M$="UNSAFE WATER -- LOSE TIME LOOKING FOR A CLEAN SPRING.":GOSUB1
2620 M=M-10*RND(0)-2
2630 GOTO3430
2640 IFM>950THEN3230
2650 M$="HEAVY RAINS -- TIME AND SUPPLIES LOST.":GOSUB1
2660 F=F-10
2670 B=B-500
2680 M1=M1-15
2690 M=M-10*RND(0)-5
2700 GOTO3430
2710 PRINT"BANDITS ARE ATTACKING!!":FORDD=1TO2500:NEXT
2720 GOSUB4740
2730 B=B-20*B1
2740 IFB>=0THEN2780
2750 M$="YOU RAN OUT OF BULLETS AND THEY GOT A LOT OF YOUR CASH.":GOSUB1
2760 T=INT((T/3*100)/100):PRINT"YOU NOW HAVE $"T
2770 GOTO2790
2780 IFB1<1.5THEN2850
2790 M$="YOU GOT SHOT IN THE LEG -- AND THEY TOOK SOME OF YOUR SUPPLIES.":GOSUB1
2800 K8=1
2810 M$="YOU'D BETTER HAVE A DOC LOOK AT YOUR WOUND.":GOSUB1
2820 M1=M1-5
2830 A=A-20
2840 GOTO3430
2850 PRINT"QUICKEST DRAW OUTSIDE OF DODGE  CITY!!"
2860 PRINT"----- YOU GOT 'EM-----"
2870 GOTO3430
2880 M$="THERE WAS A FIRE IN YOUR WAGON -- FOOD AND SUPPLIES DAMAGED.":GOSUB1
2890 F=F-40
2900 B=B-400
2910 M1=M1-RND(0)*8-3
2920 M=M-15
2930 PRINT@0,;:GOTO3430
2940 M$="LOSE YOUR WAY IN HEAVY FOG -- TIME IS LOST.":GOSUB1
2950 M=M-10-5*RND(0)
2960 GOTO3430
2970 PRINT@0,"YOU KILL A -- RATTLESNAKE!!"
2980 GOSUB310
2990 PRINT@32,"AFTER IT BIT YOU!!"
3000 B=B-10
3010 M1=M1-5
3020 IFM1>0THEN3040
3030 GOSUB310:CLS:PRINT"YOU DIE OF SNAKE-BITE SINCE YOU HAVE NO MEDICINE.":GOTO3920
3040 GOTO3430 
3050 M$="THE WAGON GETS SWAMPED FORDING A RIVER -- LOSE FOOD AND CLOTHES.":GOSUB1
3060 F=F-30
3070 C=C-20
3080 M=M-20-20*RND(0)
3090 PRINT@128,"";:GOTO3430
3100 PRINT"WILD ANIMALS ARE ATTACKING!!"
3110 IFB>39THEN3150
3120 PRINT@0,"YOU WERE TOO LOW ON BULLETS --  THE WOLVES OVERPOWERED YOU.":GOSUB310
3130 K8=1
3140 GOTO3870
3150 GOSUB310:GOSUB4740:IFB1>2THEN3180
3160 PRINT"NICE SHOOTING -- THEY DIDN'T GETMUCH."
3170 GOTO3190
3180 PRINT"SLOW ON THE DRAW -- THEY GOT AT YOUR FOOD AND CLOTHES."
3190 B=B-20*B1
3200 C=C-4*B1
3210 F=F-8*B1
3220 GOTO3430
3230 PRINT"COLD WEATHER -- BRRRR!!!!":PRINT
3240 M$="YOU ":IFC>22+4*RND(0)THEN3270
3250 M$=M$+"DON'T "
3260 C1=1
3270 M$=M$+"HAVE ENOUGH CLOTHING TO KEEP YOU WARM.":GOSUB1
3280 IFC1=0THEN3430
3290 GOTO4960
3300 PRINT"HAIL STORM -- SUPPLIES DAMAGED."
3310 M=M-5-RND(0)*10
3320 B=B-200
3330 M1=M1-4-RND(0)*3
3340 GOTO3430
3350 IFE=1THEN4960
3360 IFE=3THEN3390
3370 IFRND(100)>25THEN4960
3380 GOTO3460
3390 IFRND(10)<5THEN4960
3400 GOTO3460
3410 CLS:PRINT"HELPFUL INDIANS SHOW YOU WHERE  TO FIND MORE FOOD."
3420 F=F+14
3430 GOTO3420
3460 IFM<=950THEN940
3480 IFRND(0)*10>9-((M/100-15)^2+12)THEN3620
3490 PRINT"RUGGED MOUNTAINS!!"
3500 IFRND(100)>11THEN3540
3510 PRINT"YOU GET LOST -- LOSE TIME       LOOKING FOR TRAIL."
3520 M=M-60
3530 GOTO3620
3540 IFRND(100)>11THEN3600
3550 PRINT"WAGONS DAMAGED!! LOSE TIME AND  SUPPLIES."
3560 M1=M1-5
3570 B=B-200
3580 M=M-20-RND(0)*30
3590 GOTO3620
3600 PRINT"THE GOING GETS SLOW."
3610 M=M-45-RND(0)/.2
3620 IFF1=1THEN3660
3630 F1=1
3640 IFRND(10)<8THEN3730
3650 CLS:PRINT"YOU MADE IT THROUGH THE SOUTH   PASS (NO SNOW).":GOTO5210
3660 IFM<1700THEN3700
3670 IFF2=1THEN3700
3680 F2=1
3690 IFRND(10)<7THEN3730
3700 IFM>950THEN940
3710 M9=1
3720 GOTO5210
3730 CLS:PRINT"BLIZZARD IN MOUNTAIN PASS --    LOSE TIME AND SUPPLIES."
3740 L1=1
3745 GOSUB310
3750 F=F-25
3760 M1=M1-10
3770 B=B-300
3780 M=M-30-40*RND(0)
3790 IFC<18+2*RND(0)THEN4960
3800 GOTO3700
3810 CLS:PRINT"YOU HAVE RUN OUT OF FOOD AND    STARVED TO DEATH."
3820 GOTO3920
3830 T=0
3840 PRINT"YOU CAN'T AFFORD A DOCTOR."
3850 GOTO3870
3860 CLS:PRINT"YOU RAN OUT OF MEDICAL SUPPLIES."
3870 PRINT"YOU DIED OF ";
3880 IFK8=1THEN3910
3890 PRINT"PNEUMONIA."
3900 GOTO3920
3910 PRINT"INJURIES."
3920 PRINT
3930 PRINT"DUE TO YOUR UNFORTUNATE         SITUATION, THERE ARE A FEW"
3940 PRINT"FORMALITIES WE MUST GO THROUGH."
3950 INPUT"WOULD YOU LIKE A MINISTER       (Y OR N) THEN PRESS 'ENTER'";Y1$
3960 IFLEFT$(Y1$,1)="Y"THENPRINT:GOTO3990
3970 IFLEFT$(Y1$,1)<>"N"THEN3950
3980 GOTO4010
3990 PRINT@352,"":PRINT@320,"":PRINT@320,;:INPUT"WOULD YOU LIKE A FANCY FUNERAL  (Y OR N)";IN$
4000 IFLEFT$(IN$,1)<>"N"ANDLEFT$(IN$,1)<>"Y"THEN3990
4010 PRINT"WOULD YOU LIKE US TO NOTIFY YOURNEXT OF KIN (Y OR N)?"
4020 INPUTIN$
4030 IFLEFT$(IN$,1)="Y"THENPRINT:GOTO4070
4040 IFLEFT$(IN$,1)<>"N"THEN4020
4050 PRINT:CLS:PRINT"BUT YOUR AUNT SADIE IS WORRIED  ABOUT YOU!!"
4060 GOTO4080
4070 CLS:PRINT"THAT WILL BE $4.50 FOR THE      TELEGRAPH CHARGE."
4080 PRINT:PRINT:PRINT"WE THANK YOU FOR THIS           INFORMATION AND WE ARE SORRY YOU";
4090 PRINT"DIDN'T MAKE IT TO THIS GREAT    TERRITORY OF OREGON. BETTER LUCKNEXT TIME."
4110 PRINTTAB(15);"SINCERELY,"
4120 PRINT:PRINT"THE OREGON CITY CHAMBER OF      COMMERCE"
4130 GOTO5130
4140 F9=(2040-M2)/(M-M2)
4150 F=F+(1-F9)*(8+5*E)
4160 PRINT
4170 CLS:M$="YOU FINALLY ARRIVED AT OREGON CITY AFTER AFTER 2040 LONG MILES":GOSUB1
4200 PRINT"-------- HOORAY!!"
4210 PRINT:PRINT:PRINT"A REAL PIONEER!!"
4220 PRINT
4230 F9=INT(F9*14)
4240 D3=D3*14+F9
4250 F9=F9+1
4260 IFF9<8THEN4280
4270 F9=F9-7
4280 PRINT"PRESS <ENTER> TO CONTINUE."
4290 I1$=INKEY$:IFI1$<>CHR$(13)THEN4290
4300 CLS
4450 IFD3>124THEN4490
4460 D3=D3-93
4470 PRINT"JULY"D3"1847"
4480 GOTO4670
4490 IFD3>155THEN4530
4500 D3=D3-124
4510 PRINT"AUGUST"D3"1847"
4520 GOTO4670
4530 IFD3>185THEN4570
4540 D3=D3-155
4550 PRINT"SEPTEMBER"D3"1847"
4560 GOTO4670
4570 IFD3>216THEN4610
4580 D3=D3-185
4590 PRINT"OCTOBER"D3"1847"
4600 GOTO4670
4610 IFD3>246THEN4650
4620 D3=D3-216
4630 PRINT"NOVEMBER"D3"1847"
4640 GOTO4670
4650 D3=D3-246
4660 PRINT"DECEMBER"D3"1847"
4670 PRINT:PRINT
4690 PRINT"PRESIDENT POLK SENDS YOU HIS    HEARTIEST CONGRATULATIONS AND"
4691 PRINT"WISHES YOU A PROSPEROUS LIFE    AHEAD IN YOUR NEW HOME."
4720 GOTO5130
4740 CLS:PRINT@0,"PRESS <ENTER> TO START SHOOTING.";
4750 INPUTY$:PRINT@0,;:IFA1=1THENGOSUB5540
4760 S$(1)="BANG"
4770 S$(2)="BLAM"
4780 S$(3)="SHOT"
4790 S$(4)="WHAM"
4800 S=INT(RND(4)+.5)
4810 PRINT@0,"TO SHOOT, TYPE "CHR$(34)S$(S)CHR$(34);
4820 PRINT" AND PRESS <ENTER>.";
4830 B1=0
4840 VV=0:XX=0:S$=""
4850 IN$=INKEY$:IFIN$=""THEN4870
4860 XX=ASC(IN$):IFXX=13THEN4890
4870 B1=B1+1:IFIN$=""THEN4850
4880 VV=VV+1:S$=S$+IN$:GOSUB5550
4881 GOTO4850
4890 IFVV<3ORS$<>S$(S)THENPRINT"TRY AGAIN  ";:GOTO4840
4900 B1=B1*.045
4940 PRINT:PRINTB1:GOSUB310
4950 IFA1=1THENRETURN
4951 PRINT@0,;:RETURN
4960 IF100*RND(0)<10+35*(E-1)THEN5030
4970 IF100*RND(0)<100-(40/4^(E-1))THEN5070
4980 PRINT"SERIOUS ILLNESS --"
4990 PRINT:PRINT"YOU MUST STOP FOR MEDICAL       ATTENTION."
5000 M1=M1-10
5010 S4=1
5020 GOTO5100
5030 PRINT"MILD ILLNESS -- YOUR MEDICINE   CURED YOU."
5040 M1=M1-5
5050 M=M-5
5060 GOTO5100
5070 PRINT"BAD ILLNESS -- YOU USED YOUR    MEDICINE"
5080 M=M-5
5090 M1=M1-2
5100 IFM1<0THEN3860
5110 IFL1=1THEN3700
5120 GOTO3430
5130 GOSUB310
5160 CLS:PRINT"ARE THERE ANY MORE PIONEERS     WAITING TO ";
5170 INPUT"MAKE THE JOURNEY     (TYPE Y OR N)";G$
5180 IFLEFT$(G$,1)="Y"THENRUN
5190 GOTO5200
5200 CLS:END
5210 GOSUB310:GOTO940
5220 RETURN
5530 AA$="O R E G O N  T R A I L":PRINT@16-(LEN(AA$)/2),AA$
5531 PRINT"€€€€€€€€€€€€€‘““““’€€€€€“““€€€€€";:PRINT"€€€€€€€€€€€€€žŸŸŸŸ“‘“‘—ŸŸŸš€€€€";
5532 PRINT"€€€€€€€€€€€€€’ŸŸŸšŸŸšŸŸŸŸš€€€€";:PRINT"€€€€€”“—€“€€€€›”œœœ˜œœ˜œœœœ€€€€€";
5533 PRINT"€€€€€ŸŸŸŸŸŸŸ›€•“œžŸŸŸžœŸœ€€€€€";:PRINT"€€€€€€”ŸŸŸŸŸŸœ›“›“žŸ“—“—š€€€€";
5534 PRINT"€€€€€€•žŸ€‘žŸ€”’š‘˜€”’•€—˜€€€€€";:PRINT"€€€€€€”˜œ”˜œ€€”œœ˜€€”œœ˜€€€€€€";
5535 PRINT"MONEY:"T;TAB(16)"FOOD:"INT(F):PRINT"CLOTHING:"INT(C);TAB(16)"AMMO:"INT(B)
5536 PRINT"MISC:"INT(M1);TAB(16);"TO GO:";2040-M;:RETURN
5540 CLS:RETURN
5550 SOUND100,1:RETURN
6000 REM OREGON TRAIL WAS WORKED ON BY THE FOLLOWING PEOPLE
6010 REM TRANSLATION: JEREMY GRATT LOWERCASE + FORMAT BOB JACKSON
6020 REM MC-10 JIM GERRIE
