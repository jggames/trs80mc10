0 GOSUB10000:DIMC$(3),H$(3),X(460),Y(460),T,M,Q,P,C,O,I,A,MC,WD(2),N,E,N$(2),P1$,P2$,TA,TP,TQ:GOSUB5000:GOSUB1000
10 CLS0
20 GOSUB100
25 SOUND100,1:SOUND200,1:SOUND150,1:PRINT@320+12,"ROUND";RO;:GOSUB120
26 GOSUB910:GOSUB50:ONWP GOSUB130,140,150,160,170:GOSUB800
27 GOSUB900:GOSUB50:ONWP GOSUB130,140,150,160,170:GOSUB570:IFGE=1 THEN85
28 RO=RO+1:ONRND(4) GOTO30:GOTO25
30 GOSUB550:PRINT@192+11,"act"CHR$(128)"of"CHR$(128)"god";:FORT=1 TO2500:NEXT:GOSUB310:P=(X+4)*2:Q=4:L=RND(50)-100:I=((ABS(L)*.64)/26)*SGN(L)
31 ONRND(5) GOSUB131,141,151,161,171:GOSUB570:IFGE=1 THEN85
35 GOSUB560:GOTO25
40 PRINT@320,STR$(INT(P1))"%";:PRINT@320+27,STR$(INT(P2))"%";:PRINT@320+12,"ROUND";RO;:RETURN
50 GOSUB40:PRINT@192,"WEAPON (H,L,R,S,T,Q)";:INPUTWP$
55 IFLEFT$(WP$,1)="L" THENWP=1:PRINT@214,"lightening";:RETURN
60 IFLEFT$(WP$,1)="H" THENWP=2:PRINT@214,"hail"STRING$(6,CHR$(128));:RETURN
70 IFLEFT$(WP$,1)="R" THENWP=3:PRINT@214,"rain"STRING$(6,CHR$(128));:RETURN
75 IFLEFT$(WP$,1)="S" THENWP=4:PRINT@214,"snow"STRING$(6,CHR$(128));:RETURN
80 IFLEFT$(WP$,1)="T" THENWP=5:PRINT@214,"tornado"STRING$(3,CHR$(128));:RETURN
82 IFLEFT$(WP$,1)="Q" THEN85
84 GOTO50
85 PRINT@192,STRING$(32,CHR$(128))"PLAY AGAIN (Y/N)?"STRING$(7,CHR$(128));
86 I$=INKEY$:IFI$="" THEN86
87 IFI$="N" THENGOSUB10003:END
88 IFI$="Y" THENRUN
89 GOTO86
90 FORT=1 TON:SET(X(T),Y(T),1):RESET(X(T),Y(T)):NEXT:RETURN
100 RO=1:P1=100:P2=100:GE=0:X=0:Y=12:GOSUB500:X=27:Y=12:GOSUB500:X=0:Y=0:RETURN
120 GOSUB560:X=RND(22)-1:Y=0:W=RND(100):D=RND(2):WD=WD(D):O=((W*.64)/26)*WD:RETURN
130 REM LIGHTENING
131 A=0:N=0:FORC=1 TO10:SCREEN0,1:FORT=1 TORND(15)+5:NEXT:SCREEN0,0:NEXT:GOSUB310:C=2
132 P=P+C:ON1-(P>0ANDP<64) GOTO137:ON1-(POINT(P,Q)<>-1) GOTO138:N=N+1:X(N)=P:Y(N)=Q:SET(P,Q,2):Q=Q+1:C=WD(RND(2))
133 ONN GOTO132,132,132,132,132,132,132,132,132,132,132,132,132,132,132,132,134:GOTO135
134 M=P:T=0
135 M=M+T:ON1-(M>0ANDM<64) GOTO137:ON1-(POINT(M,Q)<>-1) GOTO138:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,2):T=SGN(O):GOTO132
137 GOSUB200
138 GOSUB90:FORT=1 TO7:SOUND1,RND(2):NEXT:RETURN
140 GOSUB300
141 N=0:A=0
142 FORC=0 TO6 STEP3:M=P+C:ON1-(M>0ANDM<64) GOTO145:ON1-(POINT(M,Q)=0) GOTO146:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,6):NEXT:ONA GOTO148
143 P=P+(O*2)+(I*2):Q=Q+2:SOUND250-(Q*2),1:GOTO142
145 GOSUB200:C=7:NEXT:GOTO148
146 A=1:ON1-(POINT(M,Q)<>-1) GOTO148:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,6):SOUND255,1
147 N=N+1:X(N)=M:Y(N)=Q-1:SET(M,Q-1,6):SOUND255,1:NEXT:GOTO148
148 GOSUB90:RETURN
150 GOSUB300
151 N=0:A=0
152 FORC=0 TO6 STEP3:M=P+C:ON1-(M>0ANDM<64) GOTO154:ON1-(POINT(M,Q)=0) GOTO155:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,3):NEXT:ONA GOTO158
153 P=P+O+I:Q=Q+1:SOUND250-(Q*2),1:GOTO152
154 GOSUB200:C=7:NEXT:GOTO158
155 A=1:ON1-(POINT(M,Q)<>-1) GOTO158:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,3):SOUND255,1:ON1-(M+1>0ANDM+1<64) GOTO157:RESET(M+1,Q)
156 ONRND(4) GOTO157,157,157:ON1-(M-1>0ANDM-1<64) GOTO157:RESET(M-1,Q)
157 NEXT:GOTO158
158 GOSUB90:RETURN
160 GOSUB300
161 N=0:A=0
162 FORC=0 TO6 STEP2:M=P+C:ON1-(M>0ANDM<64) GOTO165:ON1-(POINT(M,Q)=0) GOTO166:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,5):NEXT:ONA GOTO168
163 P=P+O+I+RND(3)*(RND(0)-.5):Q=Q+1:SOUND250-(Q*2),1:GOTO162
165 GOSUB200:C=7:NEXT:GOTO168
166 A=1:ON1-(POINT(M,Q)<>-1) GOTO168:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,5):SOUND255,1:ON1-(M+1>0ANDM+1<64) GOTO167:RESET(M+1,Q)
167 NEXT:GOTO168
168 GOSUB90:RETURN
170 GOSUB300
171 N=0:A=0:E=25:P=P-10:FORT=0 TO32 STEP32:PRINT@T,STRING$(32,CHR$(223));:NEXT
172 FORT=0 TOE:M=P+T:ON1-(M>0ANDM<64) GOTO175:ON1-(POINT(M,Q)<>-1) GOTO178:N=N+1:X(N)=M:Y(N)=Q:SET(M,Q,6):NEXT:SOUND255-Q,1
173 P=P+O+I+.5+(RND(0)-.5)*3:Q=Q+1:E=E-1:GOTO172
175 GOSUB201:T=E:NEXT
178 GOSUB90
179 FORT=0 TO32 STEP32:PRINT@T,STRING$(32,CHR$(128));:NEXT:RETURN
200 PRINT@0,"out"CHR$(128)"of"CHR$(128)"bounds";:SOUND20,15:PRINT@0,STRING$(13,CHR$(128));:GOSUB550:RETURN
201 PRINT@0,"out"CHR$(223)"of"CHR$(223)"bounds";:SOUND20,15:PRINT@0,STRING$(13,CHR$(223));:RETURN
300 PRINT@224,"CHARGE";:INPUTL:I=((ABS(L)*.64)/26)*SGN(L)
310 FORT=192 TO352 STEP32:PRINT@T,STRING$(32,CHR$(128));:NEXT:RETURN
500 FORT=1 TO3:PRINT@32*Y+X,H$(T);:Y=Y+1:NEXT:RETURN
550 PRINT@32*Y+X,C$(1);:PRINT@32*Y+X+32,C$(2);:RETURN
560 PRINT@32*Y+X,C$(0);:PRINT@32*Y+X+32,C$(0);:RETURN
570 GOSUB800:IFP1<50ANDP2<50 THENIFP1<P2 THENT=4:I$=P2$+" WINS!":GOSUB7000:GOSUB8000:GOSUB40:GE=1:RETURN
571 IFP1<50ANDP2<50 THENIFP2<P1 THENT=4:I$=P1$+" WINS!":GOSUB7000:GOSUB8000:GOSUB40:GE=1:RETURN
572 IFP1<50ANDP2<50 THENIFP2=P1 THENT=4:I$="IT'S A TIE!":GOSUB7000:GOSUB40:GE=1:RETURN
575 IFP1<50 THENT=4:I$=P2$+" WINS!":GOSUB7000:GOSUB8000:GOSUB40:GE=1:RETURN
580 IFP2<50 THENT=4:I$=P1$+" WINS!":GOSUB7000:GOSUB8000:GOSUB40:GE=1
590 RETURN
700 PRINT@480,"                               ";:PRINT@492,W;WD$(D);
710 PRINT@480,P$(1)" ";:PRINT@506," "P$(2);:POKEMC+511,TA:FORT=MC+480 TOMC+511:POKET,PEEK(T)-64:NEXT:RETURN
800 A=0:FORT=25 TO29:FORC=0 TO9:ONPOINT(C,T) GOTO801:NEXT:NEXT:GOTO802
801 A=A+1:NEXT:NEXT
802 P1=A*100/34
810 A=0:FORT=25 TO29:FORC=53 TO63:ONPOINT(C,T) GOTO811:NEXT:NEXT:GOTO812
811 A=A+1:NEXT:NEXT
812 P2=A*100/34:RETURN
900 GOSUB550:P=(X+4)*2:Q=4:GOSUB700:PRINT@480,"target";:RETURN
910 GOSUB550:P=(X+4)*2:Q=4:GOSUB700:PRINT@506,"targe";:POKEMC+511,20:RETURN
1000 MC=1024:WD(1)=1:WD(2)=-1:WD$(1)="-->":WD$(2)="<--"
1100 C$(0)=CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
1110 C$(1)=CHR$(211)+CHR$(215)+CHR$(223)+CHR$(219)+CHR$(211)+CHR$(211)+CHR$(223)+CHR$(223)+CHR$(219)+CHR$(210)+CHR$(128)
1120 C$(2)=CHR$(212)+CHR$(221)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(223)+CHR$(220)+CHR$(220)
1130 H$(1)=CHR$(128)+CHR$(131)+CHR$(131)+CHR$(131)+CHR$(128)
1140 H$(2)=CHR$(135)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(139)
1150 H$(3)=CHR$(133)+CHR$(135)+CHR$(128)+CHR$(139)+CHR$(138)
1999 RETURN
5000 P=RND(-TIMER)
5005 PRINTTAB(6)"CURSOR #21 OURANOS!"
5010 PRINTTAB(6)"BY KATHY RIGBY 1980"
5015 PRINT"  MC-10 VERSION BY JIM GERRIE
5020 PRINT" "STRING$(29,CHR$(140))
5030 PRINT:PRINT" TAKE THE ENEMY BY STORM":PRINT
5040 PRINT:INPUT" PLAYER 1'S NAME";P1$
5050 PRINT:INPUT" PLAYER 2'S NAME";P2$
5060 ONRND(2) GOTO5070,5080
5070 P$(1)=LEFT$(P1$,5):P$(2)=LEFT$(P2$,5):TA=96:P2$=MID$(P$(2),5,1):IFP2$<>"" THENTA=ASC(P2$):P$(2)=LEFT$(P$(2),4):GOTO5090
5075 GOTO5090
5080 P$(1)=LEFT$(P2$,5):P$(2)=LEFT$(P1$,5):TA=96:P2$=MID$(P$(2),5,1):IFP2$<>"" THENTA=ASC(P2$):P$(2)=LEFT$(P$(2),4):GOTO5090
5090 P1$=P$(1):P2$=P$(2):IFTA<>96 THENP2$=P2$+CHR$(TA)
6000 RETURN
7000 PRINT@T*32+INT((32-LEN(I$))/2),I$;:RETURN
8000 SOUND25,4:SOUND58,4:SOUND38,4:SOUND58,4:SOUND89,4:SOUND58,4:SOUND89,4:SOUND58,4:SOUND89,4:SOUND125,4
8010 SOUND89,4:SOUND125,4:SOUND89,4:SOUND125,4:SOUND147,4:SOUND38,4:SOUND89,4:SOUND38,4:SOUND89,4:SOUND125,8:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:WIDTH32:POKE&HFF,2:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
