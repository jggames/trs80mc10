0 CLEAR750:DIMCC,ZZ,M$,I$:CC=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 ZZ=1:CC=32:FORCC=CCTOZZSTEP-1:I$=MID$(M$,CC,1):IFI$=" "ORI$=""THEN?MID$(M$,ZZ,CC-ZZ):ZZ=CC+1:CC=ZZ+32:IFI$=""THENCC=0
2 NEXT:M$="":RETURN
5 PRINT@490,;:M$="PRESS ANY KEY":GOSUB7
6 A$=INKEY$:ON-(A$="")GOTO6:CLS:RETURN
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORZZ=CC-LEN(M$)+1TOCC:POKEZZ,PEEK(ZZ)-64:NEXT:RETURN
10 DIMWA(31),D(31):X$="X=":Y$="Y=":DIMWT(24),S(24):FORA=1TO3:X(A)=22:Y(A)=22:B(A)=8640:NEXT
20 CLS:M$="    ATLANTIC BALLOON CROSSING   ":GOSUB7
21 PRINT"THIS IS A SIMULATION OF   ОААДП ";
22 PRINT"THE FLIGHT OF THE DOUBLE  КАААП ";
23 PRINT"EAGLE II ACROSS THE       ЛНПЙЕ ";
24 PRINT"ATLANTIC, SOME 3105       ПЖМЖЗ ";
25 PRINT"MILES TO A SAFE LANDING   ПКИЗП ";
26 PRINT"NEAR PARIS FRANCE IN A TIME"
27 PRINT"OF 137 HOURS (ABOUT 6 DAYS!)"
28 PRINT"THIS WAS AN OPEN BASKET, LOW "
29 PRINT"ALTITUDE FLIGHT STARTING AT"
30 PRINT"PRESQUE ISLE MAINE AUG 11, 1978.";
31 PRINT"ALTITUDE MAX WAS 24,950 FT."
32 PRINT"& ABOUT 1/3 WAS ABOVE 15,000."
33 PRINT"USING OXYGEN MASKS TO BREATHE.":PRINT
80 Z=100:FORT=0TO5:S(T)=Z:Z=Z-20:NEXT:Z=0:FORT=10TO21:S(T)=Z:Z=Z+10:NEXT:FORT=22TO24:Z=Z-10:S(T)=Z:NEXT:GOTO111
105 X(U)=X:Y(U)=Y:AL(U)=AL:B(U)=B:IFH1=1THENH(U)=0
106 PRINT@416,;:M$="TURN OVER...YOU'VE GONE"+STR$(INT(TD(U)))+" MLS":GOSUB1:FORI=1TO3000:NEXT:GOTO118
111 INPUT"DO YOU WANT INSTRUCTIONS";A$:IFLEFT$(A$,1)="Y"GOTO10000
112 CLS:INPUT"HOW MANY PLAYING (1-3)";UP:IFUP>3THEN112
113 FORA=1TOUP:AL(A)=100
114 PRINT"VOL (140000-160000 CUBIC FEET)":PRINT"BALLOON #";A;
115 INPUTZ:V(A)=Z:IFZ>160000THENPRINT"TOO MUCH: VOLUME=150000":V(A)=150000
116 IFZ<140000THENPRINT"TOO LITTLE: VOLUME=140000":V(A)=140000
117 A$(1)="EAGLE II":A$(2)="ZANUSSI":A$(3)="SILVER FOX":PRINT"YOUR NAME-- ";A$(A):NEXTA:FORI=1TO4000:NEXT
118 U=U+1:IFU>UPTHEN150
119 IF(AL(1)=<0)AND(AL(2)=<0)AND(AL(3)=<0)THEN50000
120 HR=H(U):IFAL(U)=<0THENCLS:PRINTA$(U);" IS DOWN! NEXT!":FORI=1TO2500:NEXTI:GOTO118
121 CLS:PRINT:PRINTA$(U):PRINT"WEATHER REPORT COMING...":GOTO300
150 U=1:D5=D5+1:DA=INT(D5/2):H1=H1+1
151 IFH1=2THENH1=0:HR=0:GOTO119
152 GOSUB9000:FORI=1TOUP:B2=INT(Y(I)/3)*64+INT(X(I)/2):POKE16384+(B2/2),48+I
153 PRINT@352,;:M$=RIGHT$(STR$(I),1):GOSUB7:PRINT"=POSITION OF "A$(I):PRINT:PRINT:PRINT
154 FORI1=1TO2500:NEXTI1:NEXTI:GOTO119
300 REM WIND
310 W=RND(5)+2
320 FORT=0TO5:WT(T)=RND(5)+3:NEXT:FORT=6TO11:WT(T)=RND(5):NEXT:FORT=12TO17:WT(T)=RND(5)+3:NEXT:FORT=18TO23:WT(T)=RND(7)+4
330 D1=RND(9)-6:FORA=0TO4:D(A)=D1:NEXT:D1=RND(5):FORA=5TO9:D(A)=D1:NEXT:D1=RND(5)+8:FORA=10TO14:D(A)=D1:NEXT:D1=RND(5)+13
331 FORA=15TO19:D(A)=D1:NEXT:D1=RND(9)+19:FORA=20TO30:D(A)=D1:NEXT
340 W1=RND(15)-8:FORA=0TO4:WA(A)=W1:NEXT:W1=RND(5)+5:FORA=5TO9:WA(A)=W1:NEXT:W1=RND(6)+8:FORA=10TO14:WA(A)=W1:NEXT
341 W1=RND(7)+9:FORA=15TO19:WA(A)=W1:NEXT:W1=RND(5)+13:FORA=20TO30:WA(A)=W1:NEXT
500 X=X(U):Y=Y(U):V=V(U):B=B(U):AL=AL(U)
510 REM TURN
511 CL=1-RND(0)*.3:H=H+.5:IFH>12THENH=0:H(U)=12:GOTO105
516 V(U)=V(U)-RP(U)
519 REM TEMP
520 CT=280+RND(9)+CL*S(HR)-(3*AL/1000)
524 REM PRESSURE
525 P=30*(1-.000015*AL)
529 REM VOL,LIFT,&RISE@TEMP&PRES
530 CV=V(U)/12*CT/P:IFCV>160000THENCV=160000:IFRND(0)>.9THENPRINT@416,"* a leak *";:RP(U)=RP(U)+RND(300):FORI=1TO2000:NEXT
535 L=.07*CV*(1-.015*AL/1000)
536 AR=(L-B-IC)/(B+IC)*250
539 REM WIND:DIR,SPD(ALT,TIME,POSI)
540 CW=W+20-Y+WT(HR)+WA(INT(AL/1000)):IFF=9THENF=0:RETURN
541 D=D(INT(AL/1000))+53-X+18-Y:IFF=9THENF=0:RETURN
545 IFINT(H)=0THENGOSUB9000:GOSUB9995
560 J=COS(D*.0174533)*CW:X=X+J/70:MF=CW/20:TD(U)=TD(U)+CW/2
569 REM ICE
570 IF(Y<15)AND(CT<273)THENIC=IC+273-CT
571 IF(IC>0)AND(CT>273)THENIC=IC-CT+273:IFIC<0THENIC=0
656 K=SIN(D*.014533)*CW:Y=Y-K/120
1960 IFAL<=0THEN20000
7000 REM SMALL BALLOON
7001 CLS:IFRND(0)>.7THENGOSUB30000
7002 IFCL<.9THENPRINT@64,"-  -  --  - -    -    --   - -   --  -  -   - -  -   --   --    -    - -    ---   - - "
7003 IFCL<.8THENPRINT@64,"- === -  -  = -    =   =    =  == = = ===  == = == - == - == = = = == - --== - =   --  ---- "
7004 PRINT@438,A$(U)
7006 S1=15363+(10-INT(AL/2500))*64:IFS1<15360THENS1=15363:GOTO7008
7007 IFS1>16028THEN20000
7008 FORZ=1TO10:XC=XC+MF:M=M+3:S=S1+INT(XC*1.3):AL=AL+AR*3:IFAL<=0THENZ=10
7010 PRINT@0,"b,g,r ALT";INT(AL);"WIND";INT(CW);"RISE";INT(AR);
7020 PRINT@448,"TMP: VOL: PRES: dir: WGT: D-H-M";
7025 PRINT@480,MID$(STR$(INT(CT)),2)STR$(INT(CV))STR$(INT(P))" ";:M$=STR$(INT(D)):GOSUB7
7026 PRINTSTR$(INT(B+IC))STR$(DA);STR$(-HR);"-";RIGHT$(STR$(M),2);
7050 PRINT@192,"--------------------------------";
7090 S0=(S-15362)/2
7100 PRINT@S0-1,"  ОААДП ";:PRINT@S0+30,"   КАААП ";:PRINT@S0+62,"   ЛНПЙЕ ";:PRINT@S0+96," ПЖМЖЗ ";:PRINT@S0+128," ПКИЗП";
7940 A$=INKEY$:IFA$="B"THENB=B-100:GOTO7947
7941 IFA$="G"THENV(U)=V(U)-1000:GOTO7947
7942 ZZ=RND(100):IFA$="R"ANDZZ>80THENGOSUB9000:GOSUB9995:CLS
7943 IFA$="R"ANDZZ<=80THENPRINT@376,"no radio";:PRINT@408,"contact!";
7947 IFB<2400THENPRINT@398,"ballast gone!";:B=2400
7950 NEXTZ
7960 IFAL<0THENAL=0:GOTO20000
7990 G9=G9+1:IFG9>1THENG9=0:XC=0:M=0:HR=HR+1
7991 IFAL>28500THEN7995
7992 GOTO511
7995 V(U)=V(U)-10000:PRINT@398,"too high-vent open";:IF(AL>30000)AND(CV=160000)THENPRINT@398,"rip in balloon";:RP(U)=1000
7999 GOTO511
8000 PRINT@CC*32+25," ОААДП ";
8001 PRINT@(CC+1)*32+25," КАААП ";
8002 PRINT@(CC+2)*32+25," ЛНПЙЕ ";
8003 PRINT@(CC+3)*32+25," ПЖМЖЗ ";
8004 PRINT@(CC+4)*32+25," ПКИЗП ";:RETURN
9000 REM MAP
9001 CLS:PRINT"ааааазпмбзпплаааааагпппппппппоаа";
9002 PRINT"ааапппоивнпоаааааззпппппппппиааа";
9003 PRINT"агпгпплвбпппааазпппаанппппппаааг";
9004 PRINT"епппиампппппаабппппппппоппоиабпа";
9005 PRINT"мпппаамидппппппппппппппоппкбвбпм";
9006 PRINT"ааеиааааммппппппппппппонепплппка";
9007 PRINT"аамиааааазппппппппппппаоапиааааа";
9008 PRINT"пааааагпммпппппппппппппомиаааааа";
9009 PRINT"аоггагмзппппппппппппппппкааайааа";
9010 PRINT"аааггпппппппппппппппппоаагзмлдла";
9011 PRINT"аааппппппппппппппзпппппгзммммлпг";
9965 PRINT@352,A$(U);" POSITION ";X$;INT(X);Y$;INT(Y)
9975 PRINT"ALT=  GRND 5000  10   15 20000+"
9981 I1=CW:I2=D:I=AL:PRINT"WIND=";:ZZ=6:FORAL=0TO20000STEP5000:F=9:GOSUB540:PRINTTAB(ZZ)INT(CW);:ZZ=ZZ+5:NEXT
9985 PRINT@448,"DIR=";:ZZ=6:FORAL=0TO20000STEP5000:F=9:GOSUB541:PRINTTAB(ZZ)INT(D);:ZZ=ZZ+5:NEXT:AL=I:CW=I1:D=12
9990 PRINT@480,"MAKE HEADING";INT((64-X)/1.4-22+Y);"TOT MLS=";STR$(INT(TD(U)));:RETURN
9995 PRINT@250,"p";:B1=INT(Y/3)*64+INT(X/2):POKE16384+(B1/2),63:FORI=1TO3000:NEXT:RETURN
10000 CLS
10001 M$="BETWEEN 1873 AND 1978, SOME 14 BALLOONS HAVE MANAGED TO GET AIRBORNE IN AN ATTEMPT TO CROSS THE ATLANTIC.":GOSUB1
10002 M$="MOST HAD TO DITCH AT SEA, 1 BURST IN MIDAIR, 2 VANISHED, OTHERS WERE FORCED DOWN BY UNFAVORABLE CONDITIONS--"
10003 GOSUB1
10010 M$="WINDS SHIFTING NORTH, SOUTH OR EVEN REVERSING ALTOGETHER; OR LOSS OF GAS BY TEARS OR VALVING.":GOSUB1
10011 M$="IN ALL SOME 5 AERONAUTS DIED. THE HAZARDS ARE MANY.":GOSUB1:GOSUB5
10012 M$="FROM THESE ATTEMPTS, A SUCCESSFUL STRATEGY DEVELOPED.":GOSUB1
10020 M$="BY GOING ACROSS WITH A HIGH PRESSURE WEATHER SYSTEM, YOU CAN FOLLOW ITS WINDS IN A CLOCKWISE ROTATION--":GOSUB1
10021 M$="FIRST NORTHEAST THEN SOUTHEAST TO LAND IN EUROPE.":GOSUB1
10022 M$="THE WINDS VARY WITH ALTITUDE AND POSITION SO THAT YOU MUST STAY IN THE GROOVE OF CORRECT ALTITUDE.":GOSUB1
10023 M$="TO GO TOO HIGH OR TOO LOW FOR VERY LONG WILL CARRY YOU OFF COURSE--":GOSUB1:GOSUB5
10024 M$="NORTH IF YOU ARE TOO HIGH AND SOUTHWARD IF YOU ARE TOO LOW.":GOSUB1
10025 M$="THE MORE OFF COURSE YOU GET, THE MORE DIFFICULT IT IS TO RECOVER.":GOSUB1
10036 M$="YOUR TRIP STARTS IN THE EVENING AT 6PM WHEN THE COOLING PERIOD BEGINS.":GOSUB1
10037 M$="BY DOING THIS THE BALLOON DOES NOT RISE TOO FAR NOR DO YOU HAVE TO DROP MUCH BALLAST TILL THE COOLING "
10038 M$=M$+"PERIOD THE NEXT DAY.":GOSUB1:GOSUB5
10039 M$="COVERING 500 MILES/DAY IS A GOOD RATE AND WILL GET YOU OVER IN GOOD TIME.":GOSUB1
10040 M$="IT'S POSSIBLE TO GO ACROSS FASTER-- SET A RECORD!":GOSUB1
10041 M$="ASCEND OR DESCEND TO MAINTAIN A FAVORABLE COURSE BY RELEASING BALLAST TO ASCEND OR VALVING GAS TO DESCEND.":GOSUB1
10042 M$="YOU MAY RELEASE BALLAST BY PRESSING THE b KEY.":GOSUB1
10043 M$="THIS DROPS 100 LBS AND IS RECORDED ON THE INSTRUMENTS YOU HAVE ON BOARD.":GOSUB1:GOSUB5
10044 M$="TO RELEASE GAS YOU PRESS g AND 1000 CUBIC FEET IS RELEASED.":GOSUB1
10045 M$="YOU START WITH A 140000 - 150000 CUBIC FT VOLUME IN A 160000 CUBIC FT BALLOON AS IS DETERMINED BY YOUR STRATEGY--"
10046 GOSUB1
10052 M$="MORE VOLUME MEANS FASTER RISE & TO HIGHER LEVEL. BEWARE OF MAX VOLUME AS YOU MAY CAUSE LEAKS!":GOSUB1
10060 M$="IF YOU CLIMB ABOVE 28,000 FT YOUR BALLOON AUTOMATICALLY VALVES "
10061 M$=M$+"OFF 10000 CUBIC FEET TO PREVENT UNDUE PRESSURE AND BURSTING OF THE BALLOON.":GOSUB1:GOSUB5
10062 M$="YOUR BASE WEIGHT IS 2400 LBS AND YOU CAN NOT GO BELOW THIS WITHOUT CUTTING OFF YOUR "
10070 M$=M$+" GONDOLA AND RIDING ON THE WEIGHT RING * VERY PRECARIOUS *":GOSUB1
10071 M$="YOU MAY ALSO RADIO FOR YOUR EXACT POSITION AND THE WEATHER--"
10072 M$=M$+"WIND SPEED AND DIRECTION FOR THE VARIOUS ALTITUDES BY PRESSING r BUT, YOU WILL NOT ALWAYS GET ":GOSUB1
10080 M$="A REPORT AS THE RADIO MAY NOT WORK WHEN NEEDED.":GOSUB1:GOSUB5
10081 M$="THE SUCCESSFUL STRATEGY WAS TO RISE ABOVE 5000 FT QUICKLY TO GET GOOD WIND, "
10082 M$=M$+"BUT NOT TOO HIGH OR THE DIRECTION WOULD BE TOO FAR NORTH.":GOSUB1
10083 M$="TRY TO MAINTAIN A FAIRLY LEVEL COURSE. IF YOU GET TOO FAR NORTH YOU WILL GET ICE FORMING WHEN "
10084 M$=M$+"THE TEMPERATURE IS LOW, BELOW 273 ON THE KELVIN SCALE...":GOSUB1
10085 M$="THIS IS THE TEMP GIVEN ON YOUR GAUGE.":GOSUB1:GOSUB5
10090 M$="YOU WILL ALSO FIND MORE WIND WITH INCREASED HEIGHT "
10092 M$=M$+"THE LIFT OF THE BALLOON IS DUE TO THE DISPLACEMENT OF AIR.":GOSUB1
10093 M$="THIS AIR IS THINNER AT HIGHER ALTITUDE SO THE LIFT DECREASES, "
10094 M$=M$+"BUT THE PRESSURE BEING LESS CAUSES THE GAS WITHIN THE BALLOON TO EXPAND TO A GREATER VOLUME.":GOSUB1
10095 M$="THE BALLOON EXPANDS WITH INCREASE IN THE TEMPERATURE AND SHRINKS WITH A DECREASE.":GOSUB1:GOSUB5
10096 M$="TEMPERATURE DECREASES WITH ALTITUDE AT 7 DEGREES/1000.":GOSUB1
10097 M$="THE COMPUTER DOES THE COMPLEX CALCULATIONS FOR YOU.":GOSUB1
10098 M$="YOU WILL GAIN UNDERSTANDING AS YOU PLAY SO THAT THE PROPER COMBINATION OF EXPERIENCE, WEATHER, "
10099 M$="AND A BIT OF LUCK WILL LEAD TO A SUCCESSFUL CROSSING.":GOSUB1
10100 M$="FOR THE GAME A WINNER IS THE ONE LANDING NEAREST PARIS AT X=104, Y=22 SAFELY.":GOSUB1:GOSUB5
10101 M$="STARTING COORDINATES ARE X=22, Y=22 AND THE ORIGINAL TRS-80 SYSTEM IS Y=0 AT THE TOP "
10102 M$=M$+"INCREASING TO 47 AT THE BOTTOM OF THE DISPLAY WITH X INCREASING AS YOU MOVE EAST (RIGHT).":GOSUB1
10103 M$="TO BE SAFE YOUR IMPACT MUST BE LESS THAN 100 FEET/MIN LOSS OF ALTITUDE OR YOU MAY BE HURT ON IMPACT!"
10104 GOSUB1:GOSUB5
10105 M$="YOUR INSTRUMENTS INCLUDE:":GOSUB1
10106 M$="- YOUR CURRENT VOLUME INCLUDING EFFECT OF PRESSURE AND TEMPERATURE.":GOSUB1
10107 M$="- THE BAROMETRIC PRESSURE AT PRESENT ALTITUDE IN INCHES OF MURCURY.":GOSUB1
10108 M$="- THE WIND SPEED IN MPH":GOSUB1
10109 M$="- THE DIRECTION OF THE WIND IN DEGREES WITH POSITIVE ANGLE IF MOVING NORTHEAST AND MINUS IF SOUTHEAST":GOSUB1
10110 M$="- YOUR CURRENT WEIGHT IN LBS AND ELAPSED TIME IN DAYS- HOURS- MINUTES.":GOSUB1:GOSUB5
10112 M$="THERE WERE THREE PEOPLE ON BOARD THE DOUBLE EAGLE II ..... GOOD LUCK !":GOSUB1
10113 GOSUB5:GOTO112
20000 REM TOUCHDOWN
20001 AR=-AR:CLS:IFAR<30THENPRINT"PERFECT LANDING AT";INT(X);INT(Y):GOTO20090
20010 IFAR<60THENPRINT"LANDING WAS A BIT ROUGH":GOTO20090
20020 IFAR<100THENPRINT"OOOOOFFFF!!! YOU'RE DOWN!":GOTO20090
20030 IFAR>100THENPRINT"*C R A S H* YOU KLUTZ!"
20040 IFRND(0)>.8THENPRINT"YOU BROKE YOUR LEG!":GOTO20090
20050 IFRND(0)>.7THENPRINT"YOU SPRAINED YOUR ANKLE!":GOTO20090
20060 PRINT"YOU AND ARE BEING DRAGGED!"
20090 PRINT"AND THIS IS WHERE YOU ENDED UP--":FORI=1TO4000:NEXT:GOSUB9000
21000 REM WHERE, SAVE FINAL STATS
22000 IFPOINT(X*.5,Y*.66)=0THENPRINT@384,"YOU'RE ON LAND!":GOTO22300
22100 PRINT@384,"SPLASH! YOU'RE IN THE DRINK!":FORI=1TO2000:NEXT
22250 IFRND(0)>.8THENPRINTA$(U);" LOST AT SEA":GOTO22300
22260 IFPOINT(X*.5,Y*.66)=3THENPRINT"A PASSING BOAT PICKED YOU UP"
22300 FORI=1TO2500:NEXT:GOSUB9995:FORI=1TO2000:NEXT:TT(U)=DA*24+HR+M/60:X(U)=X:Y(U)=Y:AL(U)=0:G9=0:XC=0:M=0:H=0:GOTO118
30000 PRINT@256,"@@@@@@";
30001 PRINT@288,"@@@@@@@@@@";
30002 PRINT@320,"@@";
31000 RETURN
50000 REM END
50100 CLS:M$="        FINAL POSITIONS         ":GOSUB7
50101 CC=1:FORI=1TOUP:PRINT@CC*32,"BALLOON #";I;"TIME=";INT(TT(I)):PRINT"TOTAL DISTANCE=";INT(TD(I))
50102 PRINT"END POSITION":PRINTX$;INT(X(I));Y$;INT(Y(I)):D=SQR(ABS(X(I)-104)^2+ABS(Y(I)-22)^2)*30
50103 PRINT"DISTANCE TO PARIS=";INT(D);:CC=CC+5:NEXTI:CC=1:GOSUB8000:PRINT@475,"try";:PRINT@506,"again";:POKE16895,127
50104 I$=INKEY$:IFI$=""THEN50104
50105 IFI$="Y"THENRUN
50106 IFI$="N"THENEND
50107 GOTO50104
50108 REM ATLANTIC BALLOON CROSSING 3.3, BY DEAN POWELL
50109 REM DEAN'S GAMES, 2222 20TH, LUBBOCK, TX 79411
50110 REM MC-10 EDITS J GERRIE
