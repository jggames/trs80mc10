0 CLS:DIMX,Y,Z,X(3),Y(3),Q,J,B,W
10 SCREEN0,0:CLS:PRINT"MISSILE COMMAND":PRINT"ANDREW BLACK DRAGON USER 1983":PRINT
20 PRINT"AWSD TO MOVE CURSOR":PRINT"SPACE TO FIRE":PRINT"V TO TOGGLE CURSOR VELOCITY":PRINT
25 INPUT"MAX NO OF MISSILES (5-50)";MM:IFMM<5ORMM>50ORINT(MM)<>MMTHEN25
30 CLS:X=120:Y=96:C=4:N=0:Q=2:S=0
40 FORZ=1TO4:C(Z)=1:NEXT
50 INPUT"ENTER SKILL LEVEL (1-9)";L
60 IFL<1ORL>9ORINT(L)<>LTHEN50
70 INPUT"            ANGLE (1-3)";B
80 IFB<1ORB>3ORINT(B)<>BTHEN70
90 IFL<4THENM=1:GOTO120
100 IFL<7THENM=2:GOTO120
110 M=3
120 ONLGOTO130,140,150,130,140,150,130,140,150
130 A=3:GOTO160
140 A=2:GOTO160
150 A=1:REM A=NO OF PIXELS AWAY
160 PMODE1,1:SCREEN1,0:PCLS2:COLOR3,2
170 IFC=0THEN630
180 IFC(1)=1THENCIRCLE(10,191),10,4:PAINT(10,188),4,4
190 IFC(2)=1THENCIRCLE(64,191),10,4:PAINT(64,188),4,4
200 IFC(3)=1THENCIRCLE(192,191),10,4:PAINT(192,188),4,4
210 IFC(4)=1THENCIRCLE(245,191),10,4:PAINT(245,188),4,4
220 DRAW"C4BM118,191;E10F10"
230 PAINT(128,190),4,4
240 GOSUB330
250 FORZ=1TOM:W=RND(2):X(Z)=X(Z)-B*(W=1)+B*(W=2)+B*(X(Z)>253)-B*(X(Z)<2):Y(Z)=Y(Z)+2
251 IFPPOINT(X(Z),Y(Z))=4THENN=N+1:T=Z:Z=M:NEXT:Z=T:GOTO460
252 IFY(Z)>190THENX(Z)=RND(255):Y(Z)=0:N=N+1
253 IFN=MMTHENZ=M:NEXT:GOTO630
254 PSET(X(Z),Y(Z),3):NEXT
270 J=PEEK(2)ANDPEEK(383):CIRCLE(X,Y),2,2:X=X-(J=68)*Q+(J=65)*Q:Y=Y-(J=83)*Q+(J=87)*Q
271 IFX>254THENX=254
272 IFX<1THENX=1
273 IFY>176THENY=176
274 IFY<1THENY=1
300 IFJ=32THEN560
310 IFJ=86THENQ=Q+10:SOUND200,1:IFQ>12THENQ=2
320 CIRCLE(X,Y),3,3:GOTO250
330 FORZ=1TOM:X(Z)=RND(255):Y(Z)=0:NEXT:RETURN
460 IFX(Z)<21THENC=C-1:C(1)=0:P=10:GOSUB510:GOTO170
470 IFX(Z)<75THENC=C-1:C(2)=0:P=64:GOSUB510:GOTO170
480 IFX(Z)<139THENP=128:GOSUB510:GOTO630
490 IFX(Z)<203THENC=C-1:C(3)=0:P=192:GOSUB510:GOTO170 
500 C=C-1:C(4)=0:P=245:GOSUB510:GOTO170
510 FORD=1TO300STEP3:CIRCLE(P,191),D,4:NEXT
540 PCLS2:RETURN
560 LINE(128,179)-(X,Y),PSET:FORZ=1TOM:IFX>=X(Z)-ATHENIFX<=X(Z)+ATHEN610
590 NEXT:LINE-(128,179),PRESET:GOTO320
610 IFY>=Y(Z)-ATHENIFY<=Y(Z)+ATHENSOUND100,1:X(Z)=RND(255):Y(Z)=0:N=N+1:S=S+1
620 IFN<MMTHEN590
625 Z=M:NEXT
630 PLAY"T8V31O4GL3CO3CDDFCCCFDECCO4GL3C"
640 CLS:S=S*L*B
650 PRINT@256,"YOUR SCORE";S
660 IFS>HSTHENHS=S
670 PRINT@320,"HIGH SCORE";HS
680 PRINT@352,"DO YOU WANT ANOTHER GO (Y/N)?"
690 SCREEN0,1
700 R$=INKEY$:IFR$=""THEN700
710 IFR$="Y"THEN10
720 IFR$="N"THENSCREEN0,0:END
721 GOTO700
