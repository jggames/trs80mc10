10 GOSUB4000:PMODE0,1:PCLS:SCREEN1,1
20 FORX=1TO60:CIRCLE(128,96),X:NEXTX
30 FORX=60TO1STEP-1:CIRCLE(128,96),X,.
40 DRAW"S8;BM95,80;D4R4BR2NU4R4NU4BR2U4F4NU4BR2E4F3NL6F1BR2U4R4D2L2NF2L2"
50 DRAW"BM90,100;D4R4BR2E4F3NL6F1BR2U4F4NU4BR2U4R3F1D2G1L3BR7NR4U2NR2U2R4BD4BR2U4R4D2L2NF2L2"
60 NEXTX:GOSUB80:GOTO90
80 PLAY"L4;V31;T2;O3;1;T6;6;1;6;T2;11;T6;10;6;1;O2T2A#O3T6D#O2A#O3D#T2G#T6GFD#O3T26":RETURN
90 CLS
100 PRINT@170, "LUNAR LANDER"
110 PRINT@232,"BY  GREG ZUMWALT"
115 PRINT@300,"02/12/81"
120 PRINT@419,"FOR INSTRUCTIONS PRESS 'I'"
125 PRINT@449,"ELSE PRESS RIGHT 'SPACE' KEY"
130 PRINT@490,"TO START GAME";
135 GOTO240
210 GOTO220
215 PRINT@448,"         TO START AGAIN"
220 PRINT@416, "   PRESS 'SPACE' KEY BUTTON":W=0:Z=0
240 M$=INKEY$:IFM$=" "THEN280
265 IFM$="I"THEN2000
270 GOTO240
280 X=20:Y=20
290 B=RND(140):D=RND(140):F=RND(140):H=RND(140)
300 I=RND(8):J=RND(8):K=RND(8):L=RND(8):S=4500
310 N=0:V=0:A=0:C=0:E=255:G=255:O=90:P=32:Q=0
320 T=0:U=2
330 PMODE0,1
340 PCLS
350 SCREEN1,1:LINE(8,0)-(8,16),PSET:LINE(0,8)-(16,8),PSET
360 FORM=1TO10:PSET(RND(255),RND(140)):NEXTM
370 LINE(0,191)-(45,146),PSET
380 LINE-(53,154),PSET
390 LINE-(67,141),PSET
400 LINE-(90,161),PSET
410 LINE-(105,146),PSET
420 LINE-(165,191),PSET
430 LINE(200,191)-(240,161),PSET
440 LINE-(255,141),PSET
450 DRAW"S4"
460 DRAW"BM53,154;F5R5F14"
470 DRAW"BM90,161;G30"
480 DRAW"BM105,146;D4F20G10L3G10"
490 DRAW"BM67,141;D10F6"
500 DRAW"BM240,161;F10G10"
510 X=X+N:Y=Y+V:SOUNDY,1
520 IFX>255THENX=255
530 IFX<.THENX=.
540 IFY>191THENY=191
550 IFY<.THENY=.
560 A$="BM"+STR$(X)+","+STR$(Y)
570 DRAW"S2C0L7H5U2E5R10F5D2G5L15D3R3NG6R14NF6R3U3L8"
580 DRAWA$
590 DRAW"S2C1L7H5U2E5R10F5D2G5L15D3R3NG6R14NF6R3U3L8"
600 PSET(A,B,0):A=A+I:IFA<255THEN620
610 B=RND(140):I=RND(8):A=.
620 PSET(A,B,1)
630 PSET(C,D,0):C=C+J:IFC<255THEN650
640 D=RND(140):J=RND(8):C=.
650 PSET(C,D,1)
660 PSET(E,F,0):E=E-K:IFE>.THEN680
670 F=RND(140):K=RND(8):E=255
680 PSET(E,F,1)
690 PSET(G,H,0):G=G-L:IFG>0THEN710
700 H=RND(140):L=RND(8):G=255
710 PSET(G,H,1)
720 IFPEEK(2)=.THEN726
721 IF4ANDNOTPEEK(16952-MC)THENPSET(8,8+(Z/4),1):Z=Z-8:IFZ<-32THENZ=-32:GOTO725
722 IF1ANDNOTPEEK(16946-MC)THENPSET(8+(W/4),8,1):W=W-8:IFW<-32THENW=-32:GOTO725
723 IF4ANDNOTPEEK(16948-MC)THENPSET(8+(W/4),8,1):W=W+8:IFW>32THENW=32:GOTO725
724 IF8ANDNOTPEEK(16947-MC)THENPSET(8,8+(Z/4),1):Z=Z+8:IFZ>32THENZ=32
725 PRESET(8,8+(Z/4)):PRESET(8+(W/4),8)
726 T=T+W/O:N=INT(T)
730 IFS>.THEN760
740 U=U+2:V=INT(U)
750 GOTO840
760 U=U+(Z)/O:V=INT(U)
770 S=S-ABS(Z*2)
790 IFS<0THENS=.
800 IFS>1000THEN840
810 PLAY"V31;T255;L255;O5CB"
840 IFY<186THEN870
850 IFU>1.5THENQ=1:GOTO1160
860 GOTO1200
870 IFY>6THEN890
880 Q=3:GOTO1160
890 IFX<249THEN910
900 Q=3:GOTO1160
910 IFX>8THEN925
920 Q=3:GOTO1160
925 IFY>140THEN970
930 IFINT(ABS(X-A)+ABS(Y-B))<8THENQ=2:GOTO1160
940 IFINT(ABS(X-C)+ABS(Y-D))<8THENQ=2:GOTO1160
950 IFINT(ABS(X-E)+ABS(Y-F))<8THENQ=2:GOTO1160
960 IFINT(ABS(X-G)+ABS(Y-H))<8THENQ=2:GOTO1160
965 GOTO510
970 IFX>45THEN990
980 Y1=INT(181-(1*(X)+0)):GOTO1140
990 IFX>53THEN1010
1000 Y1=INT(181-(-1*(X)+90)):GOTO1140
1010 IFX>67THEN1030
1020 Y1=INT(181-(.93*(X)-12.214)):GOTO1140
1030 IFX>90THEN1050
1040 Y1=INT(181-(-.87*(X)+108.3)):GOTO1140
1050 IFX>105THEN1070
1060 Y1=INT(181-(1*(X)-60)):GOTO1140
1070 IFX>165THEN1090
1080 Y1=INT(181-(-.75*(X)+123.75)):GOTO1140
1090 IFX>200THEN1110
1100 GOTO510
1110 IFX>240THEN1130
1120 Y1=INT(181-(.75*(X)-150)):GOTO1140
1130 Y1=INT(181-(1.33*(X)-290))
1140 IFY>Y1THENQ=4:GOTO1160
1150 GOTO510
1160 FORR=1TO4*(ABS(U)+6)
1170 CIRCLE(X,Y),R,5:NEXTR
1180 FORR=4*(ABS(U)+6)TO1STEP-1
1190 CIRCLE(X,Y),R,0:NEXTR
1194 FORXX=1TO2
1195 PLAY"T255;L255;O1CDEFGABAGFEDC;O2;CDEFGABAGFEDC;O3;CDEFGABAGFEDC;O4;CDEFGABAGFEDC;O5;CDEFGABAGFEDC"
1196 NEXTXX
1200 FORM=1TO750:NEXTM
1210 CLS:PCLS
1220 IFQ<>0THEN1230
1222 PRINT"CONGRATULATIONS, YOU MADE IT"
1224 GOSUB80
1226 GOTO1270
1230 IFQ=1THENPRINT"YOU CRASHED, TOUCHDOWN TOO HARD":GOTO1270
1240 IFQ=2THENPRINT"YOU WERE HIT BY A METEOR":GOTO1270
1250 IFQ=3THENPRINT"YOU HIT THE EDGE OF THE GALAXY":GOTO1270
1260 PRINT"YOU CRASHED ON THE MOUNTAINS"
1270 PRINT:PRINT"FLIGHT STATISTICS:"
1280 PRINT" FUEL REMAINING  :";S
1290 PRINT" VERT. VELOCITY  :";-U
1300 PRINT" HOR.  VELOCITY  :";T
1310 GOTO215
2000 CLS
2010 PRINT"YOU ARE THE PILOT OF THE LUNAR"
2020 PRINT" LANDER.":PRINT
2030 PRINT"YOU CONTROL THE CRAFT BY USING"
2040 PRINT" THE a, w, s, AND z KEYS.":PRINT
2050 PRINT"THE OBJECT OF THE GAME IS TO"
2060 PRINT" LAND THE CRAFT ON LEVEL GROUND"
2070 PRINT" AT A VERTICAL SPEED OF LESS"
2080 PRINT" THAN -1.5 FEET PER SECOND.":PRINT
2090 PRINT"(ANY KEY TO CONTINUE)"
2100 IF INKEY$=""THEN2100
2110 CLS
2120 PRINT"HOWEVER, IT IS NOT VERY EASY.":PRINT
2130 PRINT"IN OUTER SPACE, THERE ARE"
2140 PRINT" RANDOM METEORS THAT WILL"
2145 PRINT" EXPLODE YOUR CRAFT IF YOU"
2147 PRINT" GET TOO CLOSE.":PRINT
2160 PRINT"THE METEORS ARE THE MOVING"
2170 PRINT" POINTS OF LIGHT. THE STARS"
2180 PRINT" ARE THE STATIONARY POINTS OF"
2190 PRINT" OF LIGHT AND ARE HARMLESS.":PRINT
2200 PRINT"(ANY KEY TO CONTINUE)"
2210 IF INKEY$=""THEN2210
2220 CLS
2230 PRINT"AND TO MAKE THINGS WORSE,"
2240 PRINT" YOU HAVE A LIMITED AMOUNT"
2250 PRINT" OF FUEL TO LAND WITH.":PRINT
2260 PRINT"WHEN YOUR FUEL GETS LOW, A"
2270 PRINT" 'LOW FUEL' ALARM WILL SOUND"
2280 PRINT" TO INFORM YOU OF THE IMPENDING"
2290 PRINT" DANGER.":PRINT
2300 PRINT"WHEN THE ALARM STOPS, YOU ARE"
2310 PRINT" OUT OF FUEL, AND THE REST WILL"
2320 PRINT" BE HISTORY.":PRINT
2330 PRINT"(ANY KEY TO CONTINUE)"
2340 IF INKEY$=""THEN2340
2350 CLS
2360 PRINT"FINALLY, DO NOT ATTEMP TO LAND"
2370 PRINT" ON THE MOUNTAINS.":PRINT
2380 PRINT"YOUR ENGINES ARE NOT 'TIP'"
2390 PRINT" PROOF SO ANY ATTEMP TO DO"
2400 PRINT" SO WILL RESULT IN AN EXPLOSION.":PRINT
2410 PRINT"PRESS ANY KEY TO START THE"
2420 PRINT" GAME, AND GOOD LUCK."
2430 IFINKEY$=""THEN2430
2440 GOTO280
4000 MC=16640:REM MAKE MC=16640 FOR MCX128
4010 RETURN
