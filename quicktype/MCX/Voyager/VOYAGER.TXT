10 PCLEAR 2:CLEAR100
20 NL=15:NS=3:NG=10:CC=0:SP=100:CH=100:ZE=0
30 MR=5:MZ=3:DIMMA(MR,MR,MZ):MC=INT((MR+1)^2):DIMFG(14):FG(0)=1:FORX=1TO14:FG(X)=FG(X-1)*2:NEXTX
35 TI=60:HP=100
40 DIMDX(3):DIMDY(3):DX(1)=1:DX(3)=-1:DY(0)=-1:DY(2)=1:DIMMO(3):MO(0)=2:MO(1)=3:MO(2)=0:MO(3)=1
55 LX=RND(MR+1)-1:LY=RND(MR+1)-1:LZ=RND(MZ+1)-1
60 Q3=1:CLS
70 PRINT@165,"THE VOYAGER PROGRAM";:PRINT@232,"AN AVALON HILL";:PRINT@294,"MICROCOMPUTER GAME";:PRINT@355,"                       ";:PRINT@393," ";
300 PRINT"NEW GAME Y/N?";
310 A$=INKEY$:X=RND(100):IFA$<>"Y"ANDA$<>"N"THEN310
320 PRINTA$;:IFA$="N"THENGOSUB5000ELSEGOSUB2000
330 DIMDR$(3):DR$(0)="u":DR$(1)="r":DR$(2)="d":DR$(3)="l"
400 BK$=CHR$(143):CE$="BM110,145S9A0"
410 RW$="BR7U6NM+7,-6D12NM+7,6"
420 FW$="BM+7,-6D12L14U12R14"
430 RP$="BR7U6NR7D12R7"
440 RD$="BM+9,8U11M+3,-2D15"
450 LD$="BM-9,8U11M-3,-2D15"
460 FD$="BM+4,6U7L8D7"
470 LR$="D1NR1NL1D2R1D1R1NR5D8F4L2H3L2G3L2E4U8NL5R1U1R2"
480 SH$="D2R3F4NL16D6NL16G5NL4D3NR2NL2H3L1F1L1E1L1G3NR2NL2U3NR4H5U6E4R5"
490 GE$="D1NR1NL1D1NR2NL2D1NR3NL3D2ND1R3ND1R2NU2R2D5NF9L2NL9D2NL9D2NL9D1L2D4NR2NL2BU4L5D4NR2NL2BU4L2U5L2ND9NL2U5R1NU2R2ND1R3"
500 EL$="ND10R6D10NG3L3NL6NU8NE3NH3D3L6NH3U3L3U10R3NG3ND8NF3R6"
510 DIMRO$(2):RO$(1)="ND1R2NU4F2D2G2NL6D2NL6R5NE4D1L7D1F2D2L1F5NR2NL2BH5L5D4NR2NL2U6E2U1L5U1NL3R3U2H2U2E2NH4R1ND1R3"
520 RO$(2)="ND1R2NE4F2D2G2NL6D2NL6R5NR6D1L7D1F2D2L1D4NR2NL2BU4L5G5NR2NL2BE5U2E2U1L6NG4U1R4U2H2U2E2NU5R2ND1R2"
530 LA$="BM110,145A0BF5F1BH6BG5G1"
540 FR$="U2R1E1R1F3D1L1G2H1L1U1H1"
550 EX$="BM110,145A0BR10;XFR$;BL20;XFR$;BE10;XFR$;BD30;XFR$;BE20;XFR$;"
560 CK$="BM240,20A0S4NU8NE8NR8NF8ND8NG8NL8NH8"
570 AH$="BM233,20A0S4C0NM-6,15NM+6,15BM+10,5ND13BR7D6NL20D7"
740 PCLS:GOSUB3000
750 A$=INKEY$:SW=MA(LX,LY,LZ):IFA$<>""THENAC=ASC(A$)ELSEAC=0
760 IF(SW AND FG(8))OR(CC AND FG(0))THENDRAW"C5BM110,130S10":DRAWRO$(Q3):Q3=RND(2):DRAW"C0BM110,130S10":DRAWRO$(Q3)
765 IFSP<100THENSP=SP+1
770 IFAC=9THENDI=DI+1:IFDI>3THENDI=DI-4
780 IFAC=8THENDI=DI-1:IFDI<0THENDI=DI+4
785 IFAC=9ORAC=8THENSP=SP*.9:CC=(CC OR FG(3))
790 IF(CC AND FG(0))THENGOSUB8000:IFRND(100)>SP THEN820
795 IF(CC AND FG(0))THENSC=SC+5
800 IFAC=94AND(SW AND FG(DI))THENLX=LX+DX(DI):LY=LY+DY(DI):SP=SP*.8:CC=(CC OR FG(3))
805 BF=DI+2:IFBF>3THENBF=BF-4
810 IFAC=10AND(SW AND FG(BF))THENLX=LX+DX(BF):LY=LY+DY(BF):SP=SP*.8:CC=(CC OR FG(3))
815 IFA$="U"AND(SW AND FG(4))THENLZ=LZ+1:CC=(CC OR FG(3))
817 IFA$="D"AND(SW AND FG(5))THENLZ=LZ-1:CC=(CC OR FG(3))
820 IFA$="F"AND(CC AND FG(1))THENGOSUB6000
830 IFA$="A"AND(SW AND FG(12))THENGOTO7000
840 IFA$="C"AND(SW AND FG(10))THENIF(CC AND FG(1))THENCH=100:CC=(CC OR FG(4))
850 IFA$="Q"THENCLS:INPUT"SAVE GAME Y/N";A$:IFA$="Y"THENGOSUB5100ELSECLS:GOSUB5140
860 IF(MA(LX,LY,LZ) AND FG(8))THENCC=(CC OR FG(0)):RX=LX:RY=LY:RZ=LZ
865 IF(MA(LX,LY,LZ) AND FG(8))=0THENIF(CC AND FG(0))=FG(0) ANDRND(100)<SP THENCC=CC-FG(0)
870 IF(CC AND FG(0))OR(CC AND FG(1))THEN880ELSEIFA$="T"AND(SW AND FG(11))THENA$="":CC=(CC OR FG(1)):MA(LX,LY,LZ)=SW-FG(11):DRAW"XCE$;C5;XLR$;"
880 IF(CC AND FG(0))THENGOSUB8000
890 IFSP<=0THENGOTO9000
895 IFRN<=0THENSC=SC+40*NG:GOTO9500
900 IFNG<=0THENTI=TI-1:SOUND(255-(TI*3)),1:CC=CC OR FG(4)
910 IFTI<=0THENGOTO9400
920 IFABS(SP-OS)>10THENCC=(CC OR FG(4))
930 IFA$="T"AND(SW AND FG(11))THENCH=100:CC=CC OR FG(4)
935 IF(CC AND FG(0))=0THENIFA$="M"THENGOSUB1500
940 IF(CC AND FG(3))THENIF(CC AND FG(4))THENCC=CC-FG(4)
950 IF(CC AND FG(4))THENGOSUB3500:CC=CC-FG(4)
960 IF(CC AND FG(3))THENGOSUB3000:CC=CC-FG(3)
990 GOTO 750
1500 CLS:Z=LZ:PRINT@480,"EL-ELEVATOR,RO-ROBOT,LA-LASER,GE-GENERATOR,SH-SHUTTLE";RN;"ROBOTS     REMAIN...HIT C TO CONTINUE";
1510 FORZ1=0TO384STEP32:PRINT@Z1,STRING$(25,128);:NEXTZ1:FORX=0TOMR:FORY=0TOMR:W1=MA(X,Y,Z):IF(W1 AND FG(6))=0THEN1630
1520 PL=34+(X*4)+(Y*64):PRINT@(PL-34),BK$;:PRINT@(PL-30),BK$;:PRINT@(PL+30),BK$;:PRINT@(PL+34),BK$;
1530 IF(W1 AND FG(0))=0THENPRINT@(PL-33),STRING$(3,BK$);
1540 IF(W1 AND FG(1))=0THENPRINT@(PL+2),BK$;
1550 IF(W1 AND FG(2))=0THENPRINT@(PL+31),STRING$(3,BK$);
1560 IF(W1 AND FG(3))=0THENPRINT@(PL-2),BK$;
1570 IF(W1 AND FG(9))THENPRINT@PL,"EL";
1580 IF(W1 AND FG(8))THENPRINT@PL,"RO";
1590 IF(W1 AND FG(10))THENPRINT@PL,"GE";
1600 IF(W1 AND FG(11))THENPRINT@PL,"LA";
1610 IF(W1 AND FG(12))THENPRINT@PL,"SH";
1620 IFX=LX AND Y=LY ANDZ=LZ THENPRINT@(PL-1),DR$(DI);
1630 NEXTY:NEXTX
1635 PRINT@27,"LEVEL";:PRINT@59,Z;:PRINT@91,"SCORE";:PRINT@123,INT(SC);
1637 PRINT@0,"AVALON HILL'S VOYAGER";:IFNG<=0THENPRINT@384,"EXPLOSION IN";TI;" SECONDS";
1640 A$=INKEY$:IFA$=""THEN1640ELSEIFA$="C"THENCC=(CC OR FG(3)):RETURN
1650 IFA$="^" THEN Z=Z+1:IFZ>MZ THENZ=0
1660 IFA$=CHR$(10)THENZ=Z-1:IFZ<0THENZ=MZ
1670 IFA$=CHR$(10)ORA$="^"THEN1510ELSE1640
2000 PRINT@430,"WORKING";:FORLV=0TOMZ:X=RND(MR+1)-1:Y=RND(MR+1)-1
2010 MA(X,Y,LV)=(MA(X,Y,LV) OR FG(6))
2020 NC=1
2030 IFNC>=MC THEN2170
2040 DI=RND(4)-1
2050 NX=X+DX(DI):NY=Y+DY(DI)
2060 IFRND(0)>.9THEN2140
2070 IFNX>MR ORNY>MR THEN2040
2080 IFNX<0ORNY<0THEN2040
2090 IF(MA(NX,NY,LV) AND FG(6))THEN2040
2100 MA(X,Y,LV)=(MA(X,Y,LV) OR FG(DI))
2110 MA(NX,NY,LV)=(MA(NX,NY,LV) OR FG(MO(DI)))
2120 MA(NX,NY,LV)=(MA(NX,NY,LV) OR FG(6))
2130 X=NX:Y=NY:NC=NC+1:GOTO2030
2140 X=RND(MR+1)-1:Y=RND(MR+1)-1
2150 IF(MA(X,Y,LV) AND FG(6))THEN2030
2160 GOTO 2140
2170 NEXTLV
2180 FORZ=1TO4:X=RND(MR+1)-1:Y=RND(MR+1)-1:FORLV=0TO(MZ-1)
2190 MA(X,Y,LV)=(MA(X,Y,LV) OR FG(4))
2195 MA(X,Y,LV)=(MA(X,Y,LV) OR FG(9)) OR FG(7)
2200 MA(X,Y,(LV+1))=MA(X,Y,(LV+1)) OR FG(5)
2205 MA(X,Y,(LV+1))=(MA(X,Y,(LV+1)) OR FG(9)) OR FG(7)
2210 NEXTLV:NEXTZ
2220 FORLV=0TOMZ:FORX=0TOMR:FORY=0TOMR:MA(X,Y,LV)=MA(X,Y,LV)-FG(6)
2230 IF(MA(X,Y,LV) AND FG(9))THEN2270
2250 IFRND(10)=10THENMA(X,Y,LV)=(MA(X,Y,LV) OR FG(8)) OR FG(7):RN=RN+1
2270 NEXTY:NEXTX:NEXTLV
2275 FORZ=0TONG
2280 X=RND(MR+1)-1:Y=RND(MR+1)-1:LV=RND(MZ+1)-1
2290 IF(MA(X,Y,LV) AND FG(7))THEN2280
2300 MA(X,Y,LV)=(MA(X,Y,LV) OR FG(10)) OR FG(7):NEXTZ
2310 FORZ=0TONS
2320 X=RND(MR+1)-1:Y=RND(MR+1)-1:IF(MA(X,Y,0) AND FG(7))THEN2320
2330 MA(X,Y,0)=(MA(X,Y,0) OR FG(12)) OR FG(7):NEXTZ
2340 FORZ=0TONL
2350 X=RND(MR+1)-1:Y=RND(MR+1)-1:L=RND(MZ+1)-1:IF(MA(X,Y,L) AND FG(7))THEN2350
2360 MA(X,Y,L)=(MA(X,Y,L) OR FG(11))OR FG(7):NEXTZ
2900 RETURN
3000 PMODE 2,1:SCREEN 1,1:PCLS5:DRAW "S4C0BM0,0M255,0M255,191M0,191M0,0BM222,0D191"
3010 GOSUB 3400:RF=DI+1:IFRF>3THENRF=RF-4
3020 LF=DI+3:IFLF>3THENLF=LF-4
3030 IF(MA(LX,LY,LZ) AND FG(7))THENGOSUB3100:RETURN
3040 SR=1:RD=2:PX=LX:PY=LY:PZ=LZ
3050 GOSUB3200:MA(PX,PY,PZ)=(MA(PX,PY,PZ) OR FG(6)):IFSR<>0THENPX=PX+DX(DI):PY=PY+DY(DI):RD=RD+1:GOTO3050
3090 RETURN
3100 S$="BM110,95S32":DRAW "XS$;A0;XRW$;XS$;A2;XRW$;A0;XS$;XFW$;":MA(LX,LY,LZ)=MA(LX,LY,LZ) OR FG(6)
3110 IF(MA(LX,LY,LZ) AND FG(LF))THENDRAW"XS$;A0;XLD$;"
3120 IF(MA(LX,LY,LZ) AND FG(RF))THENDRAW"XS$;A0;XRD$;"
3130 IF(MA(LX,LY,LZ) AND FG(DI))THENDRAW"XS$;A0;XFD$;"
3140 IF(MA(LX,LY,LZ) AND FG(9))THENDRAW"XCE$;XEL$;"
3150 IF(MA(LX,LY,LZ) AND FG(10))THENDRAW"XCE$;XGE$;"
3160 IF(MA(LX,LY,LZ) AND FG(12))THENDRAW"XCE$;XSH$;"
3170 IF(MA(LX,LY,LZ) AND FG(11))THENDRAW"XCE$;XLR$;"
3190 RETURN
3200 S$="BM110,95S"+STR$(FG(5-(RD-2))):SR=0:IF(MA(PX,PY,PZ) AND FG(LF))THEN3220
3210 DRAW"XS$;A2;XRW$;":GOTO3250
3220 IF(MA(PX+DX(LF),PY+DY(LF),PZ) AND FG(7))THEN3240
3230 DRAW"XS$;A2;XRP$;":GOTO3250
3240 DRAW"XS$;A2;XRW$;XS$;A0;XLD$;"
3250 IF(MA(PX,PY,PZ) AND FG(RF))THEN3270
3260 DRAW"XS$;A0;XRW$;":GOTO3300
3270 IF(MA(PX+DX(RF),PY+DY(RF),PZ) AND FG(7))THEN3290
3280 DRAW"XS$;A0;XRP$;":GOTO3300
3290 DRAW"XS$;A0;XRW$;XS$;XRD$;"
3300 IF(MA(PX,PY,PZ) AND FG(DI))THENSR=1:GOTO3320
3310 DRAW"XS$;A0;XFW$;":RETURN
3320 IF(MA(PX+DX(DI),PY+DY(DI),PZ) AND FG(7))THENSR=0:DRAW"XS$;A0;XFW$;XS$;XFD$;BR1":RETURN
3330 RETURN
3400 DRAW "C0BM225,191S16A0U7NR1U7NR1U7NR1U7NR1U7NR1"
3440 GOSUB3510:RETURN
3500 DRAW"BM235,191;S4;C5;NU140;BR6NU140BR6U140"
3510 S$="U"+STR$(INT(SP*1.4)):DRAW"BM235,191;C0;S4;XS$;BR1"
3520 S$="U"+STR$(INT(CH*1.4)):DRAW"BM241,191;C0;S4;XS$;BR1"
3530 S$="U"+STR$(NG*14):DRAW"BM247,191;C0;S4;XS$;BR1"
3540 IFNG>0THENDRAWAH$ ELSEDRAW"C5;XCK$;C0":DRAWLEFT$(CK$,12+INT(TI/2.5))
3590 OS=SP:RETURN
5000 CLS:PRINT"PLACE TAPE IN CASSETTE.HIT PLAY"
5010 PRINT"HIT S TO START LOADING"
5020 IFINKEY$<>"S"THEN5020
5030 OPEN"I",#-1,"GAME"
5040 INPUT#-1,A$:PRINT"LOADING FILE NAMED -";A$
5050 INPUT#-1,TI,RN,NG,SP,CH,CC,LX,LY,LZ,RX,RY,RZ,SC
5060 FORX=0TOMR:FORY=0TOMR:INPUT#-1,MA(X,Y,0),MA(X,Y,1),MA(X,Y,2),MA(X,Y,3):NEXTY:NEXTX
5070 CLOSE#-1:PRINT"FILE LOADED":PRINT"WORKING":RETURN
5100 CLS:PRINT"PLACE TAPE IN CASSETTE.HIT RECORD"
5110 INPUT"NAME OF FILE";A$
5120 OPEN "O",#-1,"GAME":PRINT#-1,A$:PRINT#-1,TI,RN,NG,SP,CH,CC,LX,LY,LZ,RX,RY,RZ,SC
5130 FORX=0TOMR:FORY=0TOMR:PRINT#-1,MA(X,Y,0),MA(X,Y,1),MA(X,Y,2),MA(X,Y,3):NEXTY:NEXTX:CLOSE#-1:PRINT"FILE SAVED"
5140 INPUT"RESUME GAME Y/N";A$:IFA$="Y"THENA$="Q"ELSEEND
5150 CC=(CC OR FG(3)):RETURN
6000 GOSUB6300:IF(CC AND FG(0))THENHP=HP-CH/RND(3):SC=SC+10:IFHP<=0THENSC=SC+50:HP=100:MA(RX,RY,RZ)=MA(RX,RY,RZ)-FG(8):CC=(CC-FG(0)) OR FG(3):RN=RN-1:GOSUB6200
6005 CH=CH*.8
6015 FORZ=10TO12
6020 SW=MA(LX,LY,LZ):IF(SW AND FG(Z))THENMA(LX,LY,LZ)=SW-FG(Z):CC=CC OR FG(3):GOSUB6200
6025 IF(SW AND FG(10))THENNG=NG-1ELSESC=SC-1
6030 NEXTZ
6100 CC=CC OR FG(4):RETURN
6200 FORS=1TO10:S$="S"+STR$(S):DRAW"XS$;C0;XEX$;":SOUNDRND(255),1:DRAW"XS$;C5;XEX$;":NEXTS:RETURN
6300 FORS=25TO1STEP-5:S$="S"+STR$(S):DRAW"XS$;C0;XLA$;":SOUND S*3,1:DRAW"XS$;C5;XLA$;":NEXTS:RETURN
7000 PCLS(5):FORS=10TO1STEP-1:S$="BM110,130S"+STR$(S)+"BM+0,-10":DRAW"XS$;C0;XSH$;":SOUNDS*10,1:DRAW"XS$;C5;XSH$;":NEXTS
7005 IFNG=0THENSC=SC+100:CLS:PRINT"ROBOTS DESTROYED IN SHIP EXPLOSION":FORDE=1TO200:NEXTDE:GOTO9500
7010 GOSUB6200:CLS:PRINT"ROBOTS DESTROYED THE SHUTTLE":SC=SC*.5:GOTO9040
8000 PMODE0,1:FORDE=1TO3:SOUNDRND(255),1:NEXTDE:PMODE 2,1:SCREEN 1,1:SP=SP-HP/(3+RND(5)):IFSP<0THENSP=0
8010 CC=CC OR FG(4):RETURN
9000 PCLS(5):FORX=9TO1STEP-1:Z=RND(2):S$="BM110,10S"+STR$(X)+"BM+0,40":DRAW"C0;XS$;":DRAWRO$(Z):SOUNDRND(255),1:DRAW"C5;XS$;":DRAWRO$(Z):NEXTX:CLS:PRINT"YOU'RE DEAD"
9040 PRINT"SCORE-";SC:INPUT"ANOTHER TRY Y/N";A$:IFA$<>"Y"THENEND
9050 SP=100:CH=100:LX=RND(MR+1)-1:LY=RND(MR+1)-1:LZ=RND(MZ+1)-1:IF(CC AND FG(0))THENCC=CC-FG(0)
9060 SC=SC*.7:CC=CC OR FG(3):GOTO750
9400 FORX=1TO5:CLS:PRINT:SOUNDRND(255),1:PMODE2,1:SCREEN1,1:NEXTX:PRINT"SHIP,ROBOTS,AND YOU DESTROYED":PRINT"SCORE-";SC:INPUT"TRY AGAIN Y/N";A$:IFA$="Y"THENRUNELSEEND
9500 CLS:PRINT"YOU KILLED ALL ROBOTS":PRINT"SCORE-";SC:INPUT"NEW GAME Y/N";A$:IFA$="Y"THENRUN
