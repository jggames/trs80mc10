10 CLS0:DIMC,D,F,R,S,W,Z,X$,Y$,CC
50 SCREEN0,0
60 PRINT@106,"LUNAR";:PRINT@112,"LANDER";
70 PRINT@388,"";:INPUT"DO YOU NEED DIRECTIONS";A$
80 IFLEFT$(A$,1)="Y"THEN900
90 PMODE2,1:PCLS:SCREEN1,1
100 W=2:Z=256:SH=3:P=0
110 GOSUB760:C=0:D=0
120 X$="125":Y$="17":A$=X$:B$=Y$:T$="BR6;L1D1L2U1L1"
125 CC=INT(VAL(X$)+INT(D)):IFCC/2-INT(CC/2)THENCC=CC+1
126 X$=STR$(CC)
130 DRAW"C0;BM"+A$+","+B$+"E3BH2D1U4R4L4D1E2BR1F2D2G2F3"
140 DRAW"C0;BM"+A$+","+B$+T$:DRAW"C1;BM"+X$+","+Y$+"E3BH2D1U4R4L4D1E2BR1F2D2G2F3"
150 IFZ<=0THENC=5:Z=0:LINE(Z,185)-(255,187),PRESET,BF:GOTO180
160 LINE(Z,185)-(255,187),PRESET,BF
170 IF(PEEK(383)ANDPEEK(2))=32THENC=C-.5:Z=Z-W:DRAW"C1;BM"+X$+","+Y$+T$ELSEC=C+.5
171 IFPEEK(2)THENIF8ANDNOTPEEK(16952)THENC=C-.5:Z=Z-W:DRAW"C1;BM"+X$+","+Y$+T$ELSEC=C+.5
180 A$=X$:B$=Y$:R=VAL(X$):IFR<0THENR=0
190 IFR>255THENR=255
195 IFS<0THENS=0
200 S=VAL(Y$):IFPPOINT(R+3,S-5)THEN460
220 IFPPOINT(R+3,S-1)THEN460
290 IFPPOINT(R+9,S-2)THEN460
300 IFPPOINT(R+9,S-1)THEN460
310 IFPPOINT(R,S+1)THEN460
320 IFPPOINT(R+9,S+1)THEN460
330 IFPPOINT(R+4,S-10)THEN460
340 IFVAL(X$)<3THENX$="5":D=-D
350 IFVAL(X$)>240THENX$="240":D=-D
360 IFVAL(Y$)<17THENY$="17"
370 IFC<-3THENC=-3
380 IFC>5THENC=5
390 Y$=STR$(VAL(Y$)+INT(C))
400 IF(PEEK(383)ANDPEEK(2))=65THEND=D-.5
410 IF(PEEK(383)ANDPEEK(2))=83THEND=D+.5
411 IFPEEK(2)THENIF1ANDNOTPEEK(16946)THEND=D-.5
412 IFPEEK(2)THENIF4ANDNOTPEEK(16948)THEND=D+.5
420 IFD<-5THEND=-5
430 IFD>5THEND=5
440 GOTO125
460 IFC>3THEN580
470 IFZ<0THENZ=256
480 IFR<6THENIFR>0THENIFS<152ANDS>148THENF=3:GOTO690
490 IFR<48THENIFR>36THENIFS<118ANDS>114THENF=2:GOTO690
500 IFR<106THENIFR>98THENIFS>150ANDS<155THENF=2:GOTO690
510 IFR<138THENIFR>128THENIFS<118ANDS>114THENF=1:GOTO690
520 IFR>144THENIFR<154THENIFS<149ANDS>144THENF=4:W=W+1:GOTO690
530 IFR>180THENIFR<190THENIFS<159ANDS>155THENF=3:GOTO690
540 IFR>220THENIFR<226THENIFS<139ANDS>135THENF=3:GOTO690
550 IFR>218THENIFR<226THENIFS<171ANDS>167THENF=5:W=W+1:GOTO690
560 IFR<62THENIFR>56THENIFS<169ANDS>165THENF=5:W=W+1:GOTO690
570 IFW>5THENW=5
580 DRAW"C0;BM"+A$+","+B$+"E3;H2;U2;E2;BR2;F2;D2;G2;F2;":DRAW"C0;BM"+A$+","+B$+T$
590 FORZZ=1TO9:CIRCLE(R+4,S-4),ZZ,1:PLAY"L1O1T255V"+STR$(ZZ*3+4)+"ABDGEFAD":NEXT
600 FORZZ=9TO1STEP-1:CIRCLE(R+4,S-4),ZZ,0:PLAY"O1L1T255V"+STR$(ZZ*3+4)+"DEBFADEA":NEXT
610 FORA=0TO1000:NEXTA
620 SH=SH-1
630 CLS:SCREEN 0,0
640 PRINT@37, "SHIPS REMAINING    "SH:IFP>HSTHENHS=INT(P)
650 PRINT@197,"POINTS             "INT(P):PRINT@261,"HIGH               "HS
660 PRINT:PRINT
670 PRINT@448,;:IFSH<1THENINPUT"DO YOU WANT TO PLAY AGAIN";A$:GOTO750ELSEINPUT"PRESS enter TO CONTINUE";A$
680 PCLS:SCREEN 1,1:GOTO110
690 PLAY"L1O3V30T32CDET16GT32ET8G"
700 CLS:SCREEN0,0
710 Q=Z*F:P=P+Q
720 PRINT@130,Z;"X";F;"=";Q
730 IFF=4ORF=5THENZ=256
740 GOTO640
750 IFLEFT$(A$,1)="N"THENEND
755 IFLEFT$(A$,1)="Y"ORA$=CHR$(13)THEN90
756 SOUND1,1:GOTO670
760 PCLS:SCREEN1,1
761 DRAW"C1;BM0,150;R15;D2;F3;U5;E5;L1;E6;H3;R5;E10;U7;H3;R20;F8;D5;F6;R3;G3;F6;D5;H3;L2;H3;G4;L3;G5;D9;G6;R20;D5;E3;R2;E6;F6;E7;U5;E4;R15;U5;E4;R2;U6;G3;U6;E6;R2;U4;E4;U5;R15;F5;L3;D2;F4;E3;D4;R2;F4;L15;D15;R15;D5;F4;R2;U3;F6;E3;D5;R3;E4;R15;F4;R3"
770 DRAW"U5;E5;F10;G3;D4;E4;D5;R15;E5;U3;H2;R3;U2;H20;R12;F4;BD13;BR8;R10"
780 LINE(0,185)-(Z,187),PSET,BF
790 DRAW"BM4,181;U4;R2;L2;U3;R4;BR2;D6;F1;R2;E1;U6;BR3;BD7;R4;L4;U4;R2;L2;U3;R4;BR2;D7;R4;"
800 DRAW"C1;BM44,123;E1;R2;F1;D2;G4;D1;R5;BR4;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
810 DRAW"BM2,155;E1;R2;F1;D2;G1;L1;R1;F1;D2;G1;L2;H1;BD1;BR8;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
820 DRAW"BM57,171;L5;D4;R3;F1;D2;G1;L2;H1;BD1;BR8;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
830 DRAW"BM100,158;E1;R2;F1;D2;G4;D1;R5;BR4;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
840 DRAW"BM132,122;E2;D8;L2;R4;BR2;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
850 DRAW"BM150,155;L7;E5;D8;BR4;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
860 DRAW"BM182,162;E1;R2;F1;D2;G1;L1;R1;F1;D2;G1;L2;H1;BD1;BR8;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
870 DRAW"BM225,172;L5;D4;R3;F1;D2;G1;L2;H1;BD1;BR8;U2;E2BE2;U2;BL4;D2;F2BF2;D2"
880 DRAW"BM240,144;E1;R2;F1;D2;G1;L1;R1;F1;D2;G1;L2;H1;BD1;BR8;U2;E2BE2;U2;BL4;D2;F2BF2;D2;BD3;BL9;H10;U4;G2R6"
890 RETURN
900 CLS:PRINT"OBJECT:    TO LAND ON A PAD AND            SCORE AS MANY POINTS            AS POSIBLE."
910 PRINT:PRINT"CONTROLS:  A      MOVE LEFT                S      MOVE RIGHT               SPACE  THRUST"
920 PRINT:PRINT"HINTS:     AS YOU THRUST, THE              SHIP GAINS MOMENTUM             SO WATCH OUT! YOU               ALSO GET A NEW LOAD             OF FUEL IF YOU LAND             ON A 4X OR 5X."
930 PRINT:INPUT"PRESS ENTER TO PLAY";A$
940 GOTO90
950 END
980 REM MCX128 DEL 171,411,412
990 REM MCXSD  DEL 170,400,410
995 REM THEN RE-SAVE
999 REM MC-10 EDITS JIM GERRIE
