5 CLS:GOTO100
REM -LABYRINTH- BY GERALD WERNER
REM *** EXECUTE A PCLEAR1 BEFORE CLOADING THIS PROGRAM ***
20 Y=INT((J-MP)/L1):X=J-MP-Y*L1:HV=99:FORZ=1TOQ:V=INT(A(Z)/L1)
22 H=A(Z)-V*L1-X:V=V-Y:U=ABS(H)+ABS(V):IFU>HV GOTO28
24 IFU<HVTHENHV=U:H1=H:V1=V:GOTO28
26 IFRND(10)<6THENH1=H:V1=V
28 NEXT:GOTO1400
REM -- TEST FOR PATH
30 X=P1:Z=D1:IFZ=4THENX=X-L1:Z=Z-2
32 IFZ=3THENX=X-1:Z=Z-2
34 T=.:Y=M(X):IFY-Z=146ORY-Z=147ORY=145THENT=1
36 RETURN
REM -- TURN RIGHT
40 D1=D+1:IFD1>4THEND1=D1-4
42 RETURN
REM -- TURN LEFT
45 D1=D-1:IFD1<1THEND1=D1+4
47 RETURN
REM -- MOVE
50 IFD>2GOTO56
52 IFD=1THENP1=J+1:RETURN
54 P1=J+L1:RETURN
56 IFD=3THENP1=J-1:RETURN
58 P1=J-L1:RETURN
REM -- CLEAR GRAPH SCREEN
60 PMODE0,1:PCLS:SCREEN1,1:RETURN
REM -- FIND R
70 P1=J:D1=D:GOSUB30:IFT=.THENRETURN
72 GOSUB50:Y=P1-MP:T=.:FORX=1TOQ:IFY=A(X)THENT=1:R=X
74 NEXT:RETURN
REM -- PRINT X$ AT P,PY
76 FORY=1TOLEN(X$):X=ASC(MID$(X$,Y))-42:GOSUB86:NEXT:RETURN
REM -- DISPLAY X AT P,PY
80 Y=X-INT(X/10)*10:X=INT(X/10):LINE(P,PY)-(P+14,PY-14),PRESET,BF:DRAW"BM"+STR$(P)+","+STR$(PY)+";"+DR$(Y):P=P-16:IFX>.THEN80
84 RETURN
REM -- DRAW DR$(X) AT P,PY
86 LINE(P,PY)-(P+14,PY-14),PRESET,BF:DRAW"BM"+STR$(P)+","+STR$(PY)+";"+DR$(X):P=P+16:RETURN
REM -- GET KEY
90 IFT=4THENT=.:RETURN
92 X=.:X$=INKEY$:IFX$<>""THENX=ASC(X$)
94 RETURN
REM -- RANDOM PLACE
96 X=L1*RND(15)+RND(31):RETURN
REM --
REM -- INIT
100 CLEAR1000:DIMM(511),M$(2):M=16384:SB=1E9:L1=32:OT=511:PC=OT
110 C(1)=147:C(2)=149:C(3)=145:L=L1*2
120 DIMDR$(48):FORX=0TO48:READDR$(X):NEXT
130 PMODE0,1:PCLS:E=RND(-(PEEK(9)*256+PEEK(10))):REM E=PEEK(X*L-17)-L
135 E=18:GOSUB4000
REM 140 DRAW"BM10,10D6BR4U6":GET(10,10)-(14,16),A2,G
200 CLS:PRINT"   YOU HAVE ENTERED THE":PRINT"   LABYRINTH TO BATTLE THE
210 PRINT"   IRON MONSTERS.  THEY CAN":PRINT"   BE SHOT ONLY BETWEEN THE
220 PRINT"   EYES.  THE KEYS ARE:":PRINT:PRINT"   LEFT ARROW or A --- TURN L
230 PRINT"   RIGHT ARROW OR s -- TURN R":PRINT"   UP ARROW OR W - MOVE AHEAD
240 PRINT"   DOWN ARROW/Z - TURN AROUND":PRINT"   M --------------- SHOW MAP
250 PRINT"   , AND . KEYS ----- AIM L/R":PRINT"   SPACE -------------- SHOOT
260 PRINT"   R --------- START NEW GAME
270 PRINT:PRINT"DIFFICULTY (1-9)?";:IF(L1-E)=14THENZ=L:Y=PC-43
280 X=RND(60):GOSUB90:IFX=.GOTO280
290 IFX<49ORX>57THEN200ELSEDF=VAL(X$)
300 GOSUB60:P=8:PY=32:X$="PLEASE<WAIT>":GOSUB76
310 P=8:PY=64:X$="I<AM<LOOKING":GOSUB76
320 P=8:PY=86:X$="FOR<THE<DOOR<TO":GOSUB76
330 P=8:PY=108:X$="THE<LABYRINTH>":GOSUB76
340 S=.:MP=Z*(Y+1)-1024:DF=10-DF:Q=9

350 FORX=MP+L1TOOT-L1:M(X)=C(1+INT(RND(25)/10)):NEXT
360 M(MP)=C(3)
370 FORX=MP+1TOMP+31:M(X)=C(1):M(X+15*L1)=C(1):NEXT
380 FORX=MP+L1TOMP+15*L1STEPL1:M(X)=C(2):M(X+31)=C(2):NEXT
390 FORY=1TO9:GOSUB96:A(Y)=X:NEXT:J=OT:D=4:X$=INKEY$
391 FORC1=0TO2:M$(C1)="":NEXT
392 ZZ=0:CC=1:FORC1=0TO511:M$(ZZ)=M$(ZZ)+CHR$(M(C1)):CC=CC+1:IFCC=254THENCC=1:ZZ=ZZ+1
394 NEXT

REM -- MAIN LOOP
400 GOSUB1300
REM -- DRAW NEAR LEFT
410 GOSUB60:GOSUB45:P1=J:GOSUB30:IFT=1GOTO430
420 DRAW"BM32,0F24D142G24":GOTO500
430 DRAW"BM0,24R56D142L56"
REM -- DRAW NEAR RIGHT
500 GOSUB40:GOSUB30:IFT=1GOTO520
510 DRAW"BM222,0G24D142F24":GOTO600
520 DRAW"BM254,24L56D142R56"
REM -- DRAW NEAR CENTER
600 D1=D:GOSUB30:IFT=1GOTO620
610 DRAW"BM56,24R144;BM56,166R144":GOTO1000
620 DRAW"BM56,24F40D62G40;BM198,24G40D62F40"
REM -- DRAW FAR LEFT
700 IFOT<>JORD<>2GOTO730
710 DRAW"BM96,96R62;BM110,104U6L2E6R2F6L2D6L6U4L2":PAINT(116,94)
720 PAINT(116,98):DRAW"BM142,98U6L2E2F2L6E4F4":GOTO1000
730 GOSUB50:GOSUB45:GOSUB30:IFT=.GOTO750
740 DRAW"BM96,78R14D34L14":GOTO800
750 DRAW"BM96,64F14D34G14"
REM -- DRAW FAR RIGHT
800 GOSUB40:GOSUB30:IFT=.GOTO820
810 DRAW"BM158,78L14D34R14":GOTO900
820 DRAW"BM158,64G14D34F14"
REM -- DRAW FAR CENTER
900 D1=D:GOSUB30:IFT=1GOTO920
910 DRAW"BM110,78R34;BM110,112R34":GOTO1000
920 DRAW"BM110,78F34;BM110,112E34"
REM -- DRAW CP
1000 SN=-(10-DF)*7
1010 DRAW"BM82,190U20R92D20"
1020 X=Q:P=90:PY=188:GOSUB80:IFQ=.GOTO2600
REM -- NEXT KEY
1100 S=S+DF:P=146:PY=18:X=S:GOSUB80:GOSUB20:IFT=3GOTO400
1110 FORS1=1TO6:Y=RND(20):GOSUB90:IFX=82GOTO200
1120 IFX=65THENGOSUB1300:AD=D:GOSUB45:D=D1:GOTO410
1130 IFX=83THENGOSUB1300:AD=D:GOSUB40:D=D1:GOTO410
1140 IFX=90THENGOSUB1300:AD=D:GOSUB40:D=D1:GOSUB40:D=D1:GOTO410
1150 IFX=77GOTO1210
1160 IFX<>87GOTO1190
1170 IFOT=JTHENIFD=2GOTO1190
1180 P1=J:D1=D:GOSUB30:IFT=1THENGOSUB1300:AD=D:GOSUB50:J=P1:GOTO410
1190 NEXT:GOSUB1300:SN=SN+(10-DF)*7:IFSN>.THENSOUNDSN,1:IFSN>180THENSN=180
1200 GOTO1100
REM -- SHOW MAP
1210 SCREEN0:CLS:PRINTM$(0)M$(1)LEFT$(M$(2),LEN(M$(2))-1);:POKE16895,ASC(RIGHT$(M$(2),1))
1220 S=S+DF:GOSUB1300:Z=M(J):POKEJ+M,Z+8:FORY=1TO14:IFY=7THENPOKEJ+M,Z
1230 NEXT:IFPEEK(2)AND2ANDNOTPEEK(16950-MC)THEN1220
1240 GOTO400
REM -- MOVE ROBOTS
1300 R=S/DF-INT(S/DF/10)*10:X=RND(2):IFJ<(A(R)+MP)THENX=X+2
1310 D1=X:P1=A(R)+MP:GOSUB30:IFT=.THENIFD1=1ORD1=3THEND1=D1+1:GOTO1330
1320 IFT=.THENIFD1=2ORD1=4THEND1=D1-1
1330 GOSUB30:IFT=.THENRETURN
1340 AD=D1+2:IFAD>4THENAD=AD-4
1350 X=D:D=D1:Y=J:J=A(R):GOSUB50:A(R)=P1:IFY=(P1+MP)THENA(R)=J
1360 D=X:J=Y:RETURN
REM -- UPDATE CP CONT.
1400 P=152:PY=188:IFHV>9THENX=18:GOSUB86:P=P-48:GOSUB86:RETURN
1410 X=HV:GOSUB80:P=P-16:X=14:Y=.:IFH1>.THENY=-1
1420 IFH1<.THENY=1
1430 IFV1>.THENX=X+Y
1440 IFV1=.THENX=X+2*Y
1450 IFV1<.THENX=X+3*Y:IFH1=.THENX=10
1460 X=X+(4-D)*2:IFX>17THENX=X-8
1470 IFHV>.THENGOSUB86:IFHV>1THENRETURN
REM -- ACTION
1500 IFHVTHEN1530ELSEGOSUB2800
1510 P=8:PY=116:X$="YOU<RAN<INTO":GOSUB76:P=8:PY=140:X$="ONE>>>"
1520 GOSUB76:GOTO1630
1530 D2=D:GOSUB70:IFT=1GOTO1700
1540 FORU=1TO3:GOSUB40:D=D1:GOSUB70:IFT=1THEND=D2:GOTO1600
1550 NEXT:D=D2:RETURN
REM -- SIDE ACTION
1600 FORY=1TO20*DF:GOSUB90:IFXTHENT=4:RETURN
1610 NEXT:GOSUB2800:P=8:PY=116:X$="DIDN@T<YOU<SEE":GOSUB76:P=8:PY=140
1620 X$="HIM???<":GOSUB76
1630 X$="READY<TO":GOSUB76:P=8:PY=164:X$="TRY<AGAIN?":GOSUB76
1640 FORX=1TO600:NEXT:P=64:PY=188:X$="==POOF==":GOSUB76
1650 FORX=255TO1STEP-20:SOUNDX,1:NEXT:T=3:D=RND(4):IFQ<9THENQ=Q+1
1660 FORX=1TO400:NEXT:S=S+50*DF:GOSUB96:J=X+MP:X$=INKEY$:RETURN
REM -- MAIN ACTION
1700 X=D-AD:IFABS(X)>1THENX=X+4:IFX>1THENX=X-8:IFABS(X)>1THENX=.
1710 N=X+1:GOSUB50:D1=D:IFN=.THENGOSUB40
1720 IFN=2THENGOSUB45
1730 GOSUB30:IFT=.THENN=3
1740 N=N*5+1:P=126:PR=.:H=DF:GX=102:GY=168:PSET(GX-2,GY,1):PSET(GX+2,GY,1)
REM -- ACTION LOOP, ROBOT
1800 ONNGOTO1900,1930,1930,1930,1990,2100,2110,2140
1810 ONN-9GOTO2000,1910,2050,2060,2070,2080,2200,2240,2300
REM -- SIDE ROBOTS
1900 RD=-12:GOTO1920
1910 RD=12
1920 H=H/2:N=RND(4):P=P-RD*3
1930 P=P+RD:LINE(P-4,94)-(P+6,122),PSET,BF:Y=P+RD:X=P-RD
1940 IFRD=12THENDRAW"BM"+STR$(P-4)+",92U10F10L10":GOTO1960
1950 DRAW"BM"+STR$(P+6)+",92U10G10R10"
1960 PAINT(P,90):IFX>101THENIFX<151THENLINE(P-RD-4,82)-(P-RD+6,92),PSET,BF
1970 IFY<150THENIFY>102GOTO2240
1980 LINE(P-3*RD-4,82)-(P-3*RD+6,122),PRESET,BF:GOTO2240
1990 N=9:GOTO1930
REM -- ROBOT TURNING
2000 X=P-RD:IFX<114ORX>138THEN2040:ELSELINE(P-2*RD-4,82)-(P-2*RD+6,92),PRESET,BF
2010 IFRD=12THENDRAW"BM"+STR$(P-2*RD+6)+",92U10G10R10":GOTO2030
2020 DRAW"BM"+STR$(P-2*RD-4)+",92U10F10L10"
2030 PAINT(P-2*RD,90)
2040 P=P+1.5*RD:GOSUB2700:N=N+2:GOTO2400
2050 PR=1:GOSUB2700:GOTO2240
2060 GOSUB2700:GOTO2240
2070 GOSUB2700:GOTO2240
2080 GOSUB2700:N=18:GOTO2400
REM -- CENTER ROBOT
2100 LINE(P-4,94)-(P+6,102),PSET,BF:RD=RND(3)-2:GOTO2240
2110 PR=1:P=P+6*RD:LINE(P-10,94)-(P+12,110),PSET,BF
2120 LINE(122,104)-(132,110),PSET,BF:PRESET(P-4,98):PRESET(P+6,98)
2130 PRESET(P-10,94):PRESET(P+12,94):LINE(P-6,92)-(P+8,92),PSET:GOTO2240
2140 N=17:P=P+6*RD:LINE(P-16,94)-(P+18,122),PSET,BF
2150 LINE(P-4,82)-(P+6,92),PSET,BF:DRAW"BM"+STR$(P-6)+",92U10G10R10"
2160 PAINT(P-12,90):DRAW"BM"+STR$(P+8)+",92U10F10L10"
2170 PAINT(P+12,90):RD=.:GOSUB2700:GOTO2400
REM -- STANDING ROBOT
2200 PR=1:RD=.:P=P+(RND(3)-2)*12:LINE(P-16,94)-(P+18,122),PSET,BF
2210 LINE(P-4,82)-(P+6,92),PSET,BF:DRAW"BM"+STR$(P-6)+",92U10G10R10"
2220 PAINT(P-12,90):DRAW"BM"+STR$(P+8)+",92U10F10L10"
2230 PAINT(P+12,90):GOSUB2700:N=17:GOTO2400
2240 N=N+1:GOTO2400
REM -- ROBOT FIRE
2300 FORY=124TO168STEP2:PSET(P,Y):NEXT
2310 GOSUB2800:P=8:PY=116:X$="YOU<LOST<THAT":GOSUB76
2320 P=8:PY=140:X$="ONE>>>":GOSUB76:GOTO1630
REM -- PLAYER ACTION
2400 FORU=1TO2*H:GOSUB90
2410 IFX=90ORX=87ORX=65ORX=83ORX=82THENT=4:RETURN
2420 IFPEEK(2)AND8ANDNOTPEEK(16952-MC)THEN2480
2430 IFPEEK(2)AND32ANDNOTPEEK(16949-MC)THEN2440
2431 IFPEEK(2)AND32ANDNOTPEEK(16951-MC)THEN2460
2432 NEXT:GOTO1800
2440 PRESET(GX-2,GY):PRESET(GX+2,GY):GX=GX-12:IFGX<102THENGX=102:GOTO2550
2450 GOTO2470
2460 PRESET(GX-2,GY):PRESET(GX+2,GY):GX=GX+12:IFGX>150THENGX=150:GOTO2550
2470 PSET(GX-2,GY,1):PSET(GX+2,GY,1):FORX=1TO10*(DF-1):NEXT:GOTO2420
2480 U=2*H:FORY=164TO104STEP-4:PRESET(GX,Y):PSET(GX,Y-2):NEXT
2490 IFPPOINT(GX+2,100)=.ORPR=.ORGX<(P-6)ORGX>(P+6)GOTO2530
2500 GOSUB2800:FORX=1TO300:NEXT:T=3:Q=Q-1:IFR>QGOTO2520
2510 Y=A(R):FORX=R TOQ:A(X)=A(X+1):NEXT:A(X)=Y
2520 PLAY"L1O3T32CDET16GT32ET8G":RETURN
2530 FORY=164TO100STEP-2:IFPPOINT(GX+4,Y)=.THENPRESET(GX,Y):ELSEPSET(GX,Y)
2540 NEXT
2550 PSET(GX-2,GY,1):PSET(GX+2,GY,1):NEXT:GOTO1800
REM -- GAME END
2600 CLS:SCREEN0,0:PRINT:PRINT:PRINT:PRINT:IFS<SBTHENSB=S
2610 PRINT" YOU HAVE COMPLETED YOUR TASK.":PRINT:PRINT"     YOUR TIME WAS:"S
2620 PRINT"     THE BEST TIME IS:"SB:PRINT:PRINT" PRESS R FOR NEW GAME";
2630 FORX=1TO12:X$=STR$(X):PLAY"L1T128V31O4XX$;O2XX$;":NEXT
2640 GOSUB90:IFX=82GOTO200
2650 GOTO2640
REM -- EYE MOVEMENT
2700 IFPPOINT(P-8,100)THENPSET(P-8,94):PSET(P-8,96):PSET(P-8,98)
2710 IFPPOINT(P+10,100)THENPSET(P+10,94):PSET(P+10,96):PSET(P+10,98)
2720 P=P-(RD/2):PRESET(P-8,94):PRESET(P-8,96):PRESET(P-8,98)
2730 PRESET(P+10,94):PRESET(P+10,96):PRESET(P+10,98):RETURN
REM -- SHOT
2800 FORX=1TO2:PLAY"L1T255V31O1ABDGCFEBBCGDEFCGAEDF":NEXT:RETURN
REM -- ALPHA-NUMERIC ARRAY
3000 DATA"BR2BU4U8E2R4F2D8G2L4"
3010 DATA"BE2BR2R4L2U12G2"
3020 DATA"BR2BU12E2R4F2D2G2L2G4D2R8"
3030 DATA"BE2BU12R8D2G4R2F2D2G2L4H2"
3040 DATA"BR8BU2U12G6D2R8"
3050 DATA"BE14BL4L8D4R6F2D4G2L4H2"
3060 DATA"BE14BL4L4G4D6F2R4E2U2H2L4"
3070 DATA"BR4BU2U4E6U2L8"
3080 DATA"BE10U2H2L4G2D2F2R4F2D2G2L4H2U2"
3090 DATA"BE2R4E4U6H2L4G2D2F2R4"

3100 DATA"BR6U14G4E4R2F4H4D14"
3110 DATA"BU2BE2E10L4R6D6U4G10"
3120 DATA"BU6R14G4E4U2H4F4L14"
3130 DATA"BR8R6U6D4H10G2F10"
3140 DATA"BR6H4F4U14R2D14E4"
3150 DATA"U6D4E10F2G10R4"
3160 DATA"BU6F4H4R14U2L14E4"
3170 DATA"BU8U6R6L4F10G2H10"

3180 DATA"BU2"
3190 DATA"BU8BR2R8"
3200 DATA"BR6BU2U0"
3210 DATA"BR6BU2U0BU4U2E4H2L2G2"
3220 DATA"BR6BU10U4"
3230 DATA"BE2U8E4F4D4NL6D4"
3240 DATA"BE2U12R6F2D2G2NL4F2D2G2L4"
3250 DATA"BU2"
3260 DATA"BE2U12R6F2D8G2L4"
3270 DATA"BE2NR8U6NR6U6R8"
3280 DATA"BE2U6NR6U6R8"
3290 DATA"BE14BL4L6G2D8F2R6U4L2"
3300 DATA"BE2U6NU6R8ND6U6"
3310 DATA"BU2BR4R4L2U12R2L4"
3320 DATA"BU2"
3330 DATA"BE2U6NU6R2NE6F6"
3340 DATA"BE2NU12R8"
3350 DATA"BE2U12F4ND2E4D12"
3360 DATA"BE2U12D2F8D2U12"
3370 DATA"BR2BU4U8E2R4F2D8G2L4"
3380 DATA"BE2U12R6F2D2G2L4"
3390 DATA"BU2"
3400 DATA"BE2U12R6F2D2G2L4F6"
3410 DATA"BU4BR2D0F2R4E2U2H2L4H2U2E2R4F2"
3420 DATA"BU14BR2R8L4D12"
3430 DATA"BU14BR2D10F2R4E2U10"
3440 DATA"BU2"
3450 DATA"BE2NU12E4NU2F4U12"
3460 DATA"BU2"
3470 DATA"BU14BR2D2F4ND6E4U2"
3480 DATA"BU2"
4000 MC=0:REM MAKE MC=16640 FOR MCX128
4010 RETURN
