1 REM GUNNER - MAINTAINED BY SJD - V002 - LAST CHANGE 6-SEP-77
90 RANDOMIZE
100 PRINT "THIS COMPUTER DEMONSTRATION SIMULATES THE"
110 PRINT "RESULTS OF FIRING A FIELD ARTILLERY WEAPON"
120 PRINT
130 PRINT "YOU ARE THE OFFICER IN CHARGE, GIVING ORDERS TO THE GUN"
140 PRINT "CREW, TELLING THEM THE DEGREES OF ELEVATION YOU ESTIMATE"
150 PRINT "WILL PLACE THE PROJECTILE ON TARGET. A HIT WITHIN 100 YARDS"
160 PRINT "OF THE TARGET WILL DESTROY IT. TAKE MORE THAN 5 SHOTS,"
170 PRINT "AND THE ENEMY WILL DESTROY YOU!"\PRINT
180 PRINT "THE MAXIMUM RANGE OF YOUR GUN IS 46500 YARDS."
185 LET Z=0
190 PRINT
195 LET S1=0
200 LET T=43000-30000*RND
210 LET S=0
220 GOTO 370
230 PRINT "MINIMUM ELEVATION OF GUN IS ONE DEGREE"
240 GOTO 400
250 PRINT "MAXIMUM ELEVATION OF GUN IS 89 DEGREES"
260 GOTO 400
270 PRINT "OVER TARGET BY";ABS(E);"YARDS"
280 GOTO 400
290 PRINT "SHORT OF TARGET BY";ABS(E);"YARDS"
300 GOTO 400
310 GOTO 320
320 PRINT "*******TARGET DESTROYED******   ";S;"ROUNDS OF AMMUNITION";
321 PRINT "EXPENDED"
322 GOSUB 600
325 LET S1=S1+S
330 IF Z=4 THEN 490
340 LET Z=Z+1
345 PRINT
350 PRINT "THE FORWARD OBSERVER HAS SIGHTED MORE ENEMY ACTIVITY."
360 GOTO 200
370 PRINT "THE DISTANCE TO THE TARGET IS";INT(T);"YARDS....."
380 PRINT
400 PRINT "ELEVATION:";
410 INPUT B
420 IF B>89 THEN 250
430 IF B<1 THEN 230
440 LET S=S+1
442 IF S<6 THEN 450
444 PRINT\PRINT "BOOM!!!!    YOU HAVE JUST BEEN DESTROYED  ";
445 GOSUB 600
446 PRINT "BY THE ENEMY"\PRINT\GOTO 495
450 LET B2=2*B/57.3\LET I=46500*SIN(B2)\LET X=T-I\LET E=INT(X)
460 IF ABS(E)<100 THEN 310
470 IF E>100 THEN 290
480 IF E<-100 THEN 270
490 PRINT\PRINT"TOTAL ROUNDS EXPENDED WERE";S1
491 IF S1>15 THEN 495\PRINT "NICE SHOOTING !!"\GOSUB 600\GOTO 500
495 PRINT "BETTER GO BACK TO FORT SILL FOR REFRESHER TRAINING!"
500 PRINT "THANK YOU FOR PLAYING!"
505 PRINT "TRY AGAIN.... "\PRINT\GOTO 180
600 PRINT ""
610 RETURN
999 END
