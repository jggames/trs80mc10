0 CLS:CLEAR100:DIMSW(4,7,1),SP$(1),LB(32,4),AR$(1),PT(4,16),SC(1,8),LY,NY,Q1,Q2,QT,NX:GOTO10
1 PRINT@32*(LY*4+NY+Q2-(QT<15))+(NX+Q1),"*";:RETURN
2 PRINT@32*CY+CX+O,M$;:RETURN 
10 DIMJ,DY,DX,VX,SD,WX,WY,NB,SX,JX,RR,TY,EX,WP,K,SF,TX,SG,AC,A,A$:QR=1:GOSUB3000
20 SP$(0)="€„":SP$(1)="Œ€…":AR$(0)="<-":AR$(1)="->" 
30 Q1=-3:Q2=0:O=-1:FORJ=1TO4:READPT(J,0)
40 FORA=0TO1:FORB=0TO8:SC(A,B)=0:NEXT:NEXT 
50 FORK=1TO7:READL:PT(J,K+7)=L:PT(J,8-K)=L:NEXTK,J 
60 DATA 10 
70 DATA 2,2,2,2,2,2,2 
80 DATA 40 
90 DATA 1,2,3,5,8,13,21 
100 DATA 20 
110 DATA 2,3,4,5,6,7,8 
120 DATA 80 
130 DATA 1,4,9,16,25,36,49 
140 INPUT"PLAYER 1";P1$ 
150 INPUT"PLAYER 2";P2$:P1$=LEFT$(P1$,3):P2$=LEFT$(P2$,3):PRINTP1$;" VS ";P2$
160 INPUT"IS THIS CORRECT";G$:IFG$<>"Y"ANDG$<>"YES"THEN140
170 GOSUB410:GOSUB510 
180 FORRR=1TO4:PRINT@32*RR,"*";:PRINT@32*RR+27,"*";
200 QR=1-QR:TY=QR*20:TX=26-TY:CX=TX:CY=0
210 M$=RIGHT$(STR$(PT(RR,0)),2):GOSUB2
220 CX=6+TY:CY=0:M$=AR$(QR):GOSUB2
230 GOSUB660:IFSC(1-QR,RR)>=PT(RR,0)THEN250:REM END OF ROUND 
240 GOTO200
250 FORJ=0TO1:FORK=5TO8:SC(J,K)=0:NEXTK,J
260 FORJ=0TO1:FORK=1TO4:GL=PT(K,0):AC=SC(J,K):SC(J,5)=SC(J,5)+AC
270 SC(J,6)=SC(J,6)-(AC>=GL)*GL:SC(J,7)=SC(J,7)+(SC(J,K)-SC(1-J,K)):NEXTK,J 
280 FORJ=0TO1:FORK=6TO7:SC(J,K)=SC(J,K)+SC(J,5):NEXTK,J 
290 FORJ=0TO1:FORK=5TO7:SC(J,8)=SC(J,8)+SC(J,K):NEXTK,J
300 FORJ=0TO1:FORK=5TO8:Y$=STR$(SC(J,K)):L=LEN(Y$):TX=6+J*27-L
310 TY=K:CX=TX+(TX<20):CY=TY:M$=Y$:GOSUB2:NEXTK,J 
320 NEXTRR:REM END OF MAIN LOOP 
330 REM GAME OVER
340 PRINT@32*10+7,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
350 PRINT@32*11+7,"ŸPLAY AGAIN? (Y/N)Ÿ"; 
360 PRINT@32*12+7,"ŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸŸ";
390 I$=INKEY$:IFI$=""THEN390
391 IFI$="Y"THENRUN
392 IFI$="N"THENEND
393 GOTO390
410 CLS:GOSUB2000
440 RETURN
510 FORJ=0TO3:SY=4+J*4:FORK=0TOJ+3:SX=12-J*2+K*4
520 CX=SX-1:CY=SY-2:M$=" ":GOSUB2
530 WP=INT(RND(0)*2)
540 SW(J,K,0)=WP:SW(J,K,1)=0:GOSUB650 
550 NEXTK,J 
560 CX=2:CY=0:M$=P1$:GOSUB2 
570 CX=29:CY=0:M$=P2$:GOSUB2
571 RETURN 
650 CX=SX-2:CY=SY-1:M$=SP$(WP):GOSUB2:RETURN 
660 FORJ=0TO32:LB(J,0)=0:NEXT:NB=1 
670 A$=INKEY$:IFA$=""THEN670 
680 IFA$="-"THENRETURN 
690 IFA$="+"THENA$=STR$(RND(8))
700 A=VAL(A$):IF(A<1)OR(A>8)THEN670 
710 LB(0,0)=1:FORJ=1TO3:LB(0,J)=0:NEXT:LB(0,4)=10+A*2
720 EX=1 
730 FORJ=0TO32:IFLB(J,0)THENEX=0:GOSUB760 
740 NEXT:SOUND1,1:IFEXTHENRETURN
750 GOTO720 
760 DY=LB(J,0):DX=LB(J,1):LY=LB(J,2):NY=LB(J,3):NX=LB(J,4):QT=LY*4+NY
770 IF(LY+NY)ANDLY<4THENPRINT@32*(LY*4+NY+Q2)+(NX+Q1)," ";
780 LB(J,3)=(NY+1)AND3:ONNY+1GOTO790,810,860,880
790 IFLY>3THENLB(J,0)=0:GOTO950:REM SCORING ROUTINE 
800 GOSUB1:ONRND(3)GOTO1000,1010,1020 
810 VX=0:GOSUB940:IFSW(WY,WX,1)=0OR(SW(WY,WX,0)=SD)=0THEN840 
820 VX=1-2*SD:LB(J,1)=VX:LB(J,3)=NY+1
830 LB(J,4)=NX+VX:PRINT@32*(LY*4+NY+Q2-(QT<15))+(NX+Q1+VX),"*";:RETURN
840 IFSW(WY,WX,0)=SDTHENLB(J,0)=0:SW(WY,WX,1)=1:GOSUB1:RETURN
850 LB(J,3)=NY+1:GOSUB1:ONRND(3)GOTO1000,1010,1020 
860 LB(J,1)=0:LB(J,4)=NX+DX:PRINT@32*(LY*4+NY+Q2-(QT<15))+(NX+Q1+DX),"*"; 
870 SOUND200,1:RETURN
880 IFQT<15THENGOSUB1
890 LB(J,2)=LY+1:GOSUB940:SW(WY,WX,0)=1-SW(WY,WX,0)
900 IFSW(WY,WX,1)=0THEN930 
910 LB(NB,0)=1:LB(NB,1)=0:LB(NB,2)=LY:LB(NB,3)=0:LB(NB,4)=NX+2-SD*4:NB=NB+1
920 SW(WY,WX,1)=0:PRINT@32*(LY*4+NY+Q2-1)+(NX+Q1+2-SD*4)," ";:SOUND250,1
930 SX=12-WY*2+WX*4:SY=4+WY*4:WP=SW(WY,WX,0):GOSUB650:RETURN
940 WY=LY:JX=INT(NX/2)+LY-6:WX=INT(JX/2):SD=JX AND 1:RETURN
950 SF=PT(RR,NX/2-2)
960 SG=SC(QR,RR)+SF 
970 TX=4+27*QR+(SG>9)+(SG>99)+(SG>999)
980 TY=RR:A$=MID$(STR$(SG),2)
990 CX=TX:CY=TY:M$=A$:GOSUB2:SC(QR,RR)=SG:RETURN
1000 SOUND10,1:RETURN 
1010 SOUND40,1:RETURN 
1020 SOUND60,1:RETURN 
1100 REM CHAR COMMAND 
2000 PRINT"        …1…2…3…4…5…6…7…8…    "
2011 PRINT"        … … … … … … … … …    "
2012 PRINT"        … … … … … … … … …    "
2013 PRINT"        … … … … … … … … …    "
2014 PRINT"        … … … … … … … … …    "
2015 PRINT"      ŒŒ… … … … … … … … „Œ  "
2016 PRINT"      … … … … … … … … … … …  "
2017 PRINT"      … … … … … … … … … … …  "
2018 PRINT"      … … … … … … … … … … …  "
2019 PRINT"    ŒŒ… … … … … … … … … … „Œ"
2020 PRINT"    … … … … … … … … … … … … …"
2021 PRINT"    … … … … … … … … … … … … …"
2022 PRINT"    … … … … … … … … … … … … …"
2023 PRINT"  ŒŒ… … … … … … … … … … … … „Œ"
2024 PRINT"  … … … … … … … … … … … … … … …"
2025 PRINT"  … … … … … … … … … … … … … … …";
2040 RETURN
3000 CLS:PRINTTAB(11);"SWITCHBOX"
3010 PRINT"PLAYERS ALTERNATE DROPPING"
3020 PRINT"BALLS INTO ONE OF 8 SLOTS."
3030 PRINT"THESE BALLS MIGHT MAKE IT"
3040 PRINT"TO ONE OF THE 14 EXIT PATHS"
3050 PRINT"DEPENDING ON THE SWITCHBOXES"
3060 PRINT"THEY MUST PASS THROUGH. THE"
3070 PRINT"GAME LASTS 4 ROUNDS. ROUND 1"
3080 PRINT"EACH EXIT = 2 PTS. YOUR GOAL"
3090 PRINT"IS 10 PTS. THE OTHER ROUNDS"
3100 PRINT"HAVE THE FOLLOWING SEQUENCES"
3101 PRINT"FROM THE CENTER OUTWARDS:
3110 PRINT"2. 1,2,3,5,8,13,21    goal 40"
3120 PRINT"3. 2,3,4,5,6,7,8      goal 20"
3130 PRINT"4. 1,4,9,16,25,36,49  goal 80"
3140 PRINT"    HIT ANY KEY TO CONTINUE";
3150 A$=INKEY$:IFA$=""THEN3150
3160 CLS:PRINT"BONUS PTS ARE AWARDED AT THE"
3170 PRINT"END OF EACH ROUND. 4 NUMBERS"
3180 PRINT"APPEAR BELOW THE SCORECARDS."
3190 PRINT"THE 1ST IS SIMPLY THE TOTAL"
3200 PRINT"SO FAR. THE 2ND IS THE TOTAL"
3210 PRINT"PLUS A BONUS. BONUS = GOAL,"
3220 PRINT"IF PLAYER'S PTS >= THE GOAL."
3230 PRINT"THE 3RD IS THE DIFFERENCE" 
3240 PRINT"BETWEEN PLAYERS, WHICH IS"
3250 PRINT"ADDED TO THE WINNER'S SCORE"
3260 PRINT"AND SUBTRACTED FROM THE LOSER."
3270 PRINT"THE FINAL NUMBER IS THE GRAND"
3280 PRINT"TOTAL. USE ROUNDS 1 AND 3"
3282 PRINT"TO SEED THE FIELD WITH BALLS."
3283 PRINT"'+'=RANDOM #. '-'=SKIP TURN."
3290 PRINT"    HIT ANY KEY TO CONTINUE";
3300 A$=INKEY$:IFA$=""THEN3300
3310 CLS:RETURN
