0 GOTO100
3 REM LINE DRAW ROUTINE
4 IFA-X=0THENSET(X,Y,5):RETURN
5 L=(B-Y)/(A-X):FORZ=XTOASTEP1/SQR(L*L+1)*SGN(A-X):SET(Z+.5,L*Z+(Y-L*X)+.5,5):NEXT:RETURN

100 REM GENERATE A SPHERE USING; THE SPHERICAL COORDINATE SYSTEM
110 DIM X(100),Y(100),Z(100)
120 RO=30:I=1:CLS0:PRINT@32,"WAIT...";
130 FOR TH=0 TO 2.51327 STEP .628317
140 FOR PH=0 TO 6.28319 STEP .330694
150 X(I)=RO*COS(TH)*SIN(PH)
160 Y(I)=RO*SIN(TH)*SIN(PH)
170 Z(I)=RO*COS(PH)
180 I=I+1
190 NEXT PH
200 NEXT TH
210 P=100
1080 REM DIMETRIC PROJECTION ROUTINE
1090 REM FROM I MATHEMATICAL ELEMENTS FOR (I)MPUTER GRAPHICS I
1100 REM P = NUMBER OF X,Y,Z TRIPLETS
1110 REM X ( ) = ARRAY CONTAINING X-COORDINATES
1120 REM Y ( ) = ARRAY CONTAINING Y-COORDINATES
1130 REM Z ( ) = ARRAY CONTAINING Z-COORDINATES
1140 DIM U(100,4),V(100,4),T(4,4)
1150 FOR I=1 TO P:FOR J=1 TO 4:U(I,J)=0:V(I,J)=0:NEXT J:NEXT I
1160 FOR I=1 TO P
1170 U(I,1)=X(I):U(I,2)=Y(I):U(I,3)=Z(I):U(I,4)=1
1180 NEXT I
1190 FOR I=1 TO 4:FOR J=1 TO 4:T(I,J)=0:NEXT J:NEXT I
1200 T(1,1)=.92582:T(1,2)=.133631:T(1,3)=-.353553
1210 T(2,2)=.935414:T(2,3)=.353553
1220 T(3,1)=.377964:T(3,2)=-.327327:T(3,3)=.866025
1230 T(4,4)=1.0
1240 FOR I=1 TO P:FOR J=1 TO 4:FOR K=1 TO 4
1250 V(I,J)=U(I,K)*T(K,J)+V(I,J)
1260 NEXT K:NEXT J :NEXT I
1270 FOR I=1 TO P
1280 X(I)=V(I,1):Y(I)=V(I,2):Z(I)=V(I,3)
1290 NEXT I
1300 N=3
1310 REM AXONOMETRIC PROJECTION ROUTINE
1320 REM FRCM I MATHEMATICAL ELEMENTS FOR (I)MPUTER GRAPHICS I
1330 REM P = NUMBER OF X, Y, Z TRIPLETS
1340 REM X ( ) = ARRAY CONTAINING X-COORDINATES
1350 REM Y ( ) = ARRAY CONTAINING Y-COORDINATES
1360 REM Z ( ) = ARRAY CONTAINING Z-COORDINATES
1370 REM N = NUMBER INDICATING THE PERPENDICULAR AXIS
1380 REM N=1 X-AXIS, N=2 Y-AXIS, N=3 Z-AXIS
1390 FOR I=1 TO P:FOR J=1 TO 4:U(I,J)=0:V(I,J)=0:NEXT J:NEXT I
1400 FOR I=1 TO P
1410 U(I,1)=X(I):U(I,2)=Y(I):U(I,3)=Z(I):U(I,4)=1
1420 NEXT I
1430 FOR I=1 TO 4:FOR J=1 TO 4:T(I,J)=0:NEXT J:NEXT I
1440 T(1,1)=1:T(2,2)=1:T(3,3)=1:T(4,4)=1
1450 IF N=3 THEN 1490
1460 IF N=2 THEN 1480
1470 T(1,1)=0:GOTO 1500
1480 T(2,2)=0:GOTO 1500
1490 T(3,3)=0
1500 FOR I=1 TO P:FOR J=1 TO 4:FOR K=1 TO 4
1510 V(I,J)=U(I,K)*T(K,J)+V(I,J)
1520 NEXT K:NEXT J:NEXT I
1530 FOR I=1 TO P
1540 HX=X(1):LX=X(1):HY=Y(1):LY=Y(1)
1550 FOR I=2 TO P
1560 IF X(I)>HX THEN HX=X(I):GOTO 1580
1570 IF X(I)<LX THEN LX=X(I)
1580 IF Y(I)>HY THEN HY=Y(I):GOTO 1600
1590 IF Y(I)<LY THEN LY=Y(I)
1600 NEXT I
1610 DX=HX-LX:DY=HY-LY:MX=127/DX:MY=127/DY
1620 X=INT(0.5+MX*(X(1)-LX))/2:Y=INT(0.5+MY*(Y(1)-LY))/4:SET(X,Y,3)
1630 REM PLOT 242
1640 FOR I=4 TO P
1650 X(I)=INT(0.5+MX*(X(I)-LX))
1660 Y(I)=INT(0.5+MY*(Y(I)-LY))
1670 X=ABS(INT(X(I-1)/2)):Y=ABS(INT(Y(I-1)/4)):A=ABS(INT(X(I)/2)):B=ABS(INT(Y(I)/4)):GOSUB4
1680 NEXT I
1690 REM PLOT 255
2000 REM PROMPT FOR END
2010 PRINT@32,"€€€€€€€";:PRINT@32,;:INPUT"AGAIN";A$
2020 IF A$="N" THEN END
2030 RUN
