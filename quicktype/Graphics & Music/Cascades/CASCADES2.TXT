100 REM CASCADES
110 REM COPYRIGHT 1979, HARRY J. SAAL
120 REM PERMISSION GRANTED FOR NON-COMMERCIAL USE
130 WALL=RND(8):IF WALL=3 THEN 130
140 BOX=WALL:MAX=180:BALL=3:LNG=6:DIM X(LNG),Y(LNG)
150 CLS0:X=RND(-(PEEK(9)*256+PEEK(10)))
160 REM MAKE THE WALLS
170 FOR I=0 TO 31
180 SET(0,I,WALL)
190 SET(63,I,WALL)
200 NEXT I
210 FOR I=0 TO 63
220 SET(I,31,WALL):NEXT I
230 REM MAKE THE BARRIERS
240 FOR I=1 TO MAX
250 X=RND(62):Y=RND(30)
260 FOR J=0 TO 3
270 IF POINT(X+J,Y) THEN J=3:GOTO280
275 SET(X+J,Y,BOX)
280 NEXT J,I
290 FORI=1TO62:RESET(I,1):RESET(I,.):NEXT:REM HAKE A HOLE
300 REM DROP A NEW DRIP
310 X=1+RND(61):Y=0
315 DX=0:DY=1:CLNG=LNG
320 FOR I=1 TO CLNG:X(I)=0:Y(I)=0:NEXT I
330 X(CLNG)=X:Y(CLING)=Y
340 IF POINT(X,Y)<>0 THEN 660
350 REM DRAW HEAD AND TRY TO MOVE
360 SET(X,Y,BALL)
370 REM TRY DOWN FIRST
380 IF POINT(X,Y+1)=0 THEN DY=1:DX=0:GOTO 510
390 NX=X+DX:NY=Y+DY:IFNX<0ORNX>63THEN660:REM NEW POSITION
400 IF POINT(NX,NY)=0 THEN 510
410 REM IF WE ARE GOING SIDWAYS, GIVE UP
420 IF DX THEN 600
430 REM CANT MOVE, CHECK LEFT AND RIGHT
440 R=(POINT(X+1,Y)<>WALL):L=(POINT(X-1,Y)<>WALL)
450 REM IF CAN GO BOTH WAYS, RANDOMLY CHOOSE
460 IF R THEN IF L THEN DX=1+2*(RND(2)=1):DY=0:GOTO 510
470 IF R THEN DX=1:DY=0:GOTO 510
480 IF L THEN DX=-1:DY=0:GOTO 510
490 GOTO 600:REM STUCK. SPLIT OFF THE HEAD AND TRY AGAIN
500 REM HERE WE MOVE BY D
510 TX=X(1):TY=Y(1):REM FIRST GET THE TAIL
520 IF TX>0 AND TY>0 THEN RESET(TX,TY):REM BLANK TAIL
530 REM ADVANCE ALL PARTS
540 FOR I=2 TO CLNG
550 X(I-1)=X(I):Y(I-1)=Y(I)
560 NEXT I
570 X(CLNG)=X:Y(CLNG)=Y
580 X=X+DX:Y=Y+DY:X(CLNG)=X:Y(CLNG)=Y
590 GOTO 360
600 REM WE ARE STUCK. TRY THE NEXT ONE IN
610 IF CLNG<=2 THENON-(INKEY$="")GOTO290:RUN
620 CLNG=CLNG-1
630 X=X(CLNG):Y=Y(CLNG)
640 IF X=0 OR Y=0 THEN 670:REM DONE IF OFF SCREEN
650 DY=1:DX=0:GOTO 360:REM TRY TO GO DOWN
660 REM WE ARE FINISHED. START ALL OVER
670 ON-(INKEY$="")GOTO680:RUN
680 X=1+RND(61):Y=1+RND(28):IFPOINT(X,Y)<>3THEN680
690 IFPOINT(X,Y+1)=.THENY=Y+1:GOTO315
700 GOTO680
