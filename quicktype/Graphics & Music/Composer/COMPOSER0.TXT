100 REM *** RANDOM MELODY GENERATOR WITH STRINGS ***
110 REM *** COPYRIGHT 1972 BY DYMAX ***
120 CLS:DIM M(100),N(12)
130 A$="A A#B C C#D D#E F F#G G#":DATA 87,95,103,113,121,129,135,142,149,155,161,166
140 FORCC=1TO12:READN(CC):NEXT
150 PRINT "DO YOU WISH INSTRUCTIONS (Y/N)?";
160 CC=RND(0):Q$=INKEY$:IF Q$="" THEN 160
170 CLS:IF Q$="N" THEN 260
180 PRINT "I AM A (SLIGHTLY) MAD COMPOSER."
181 PRINT "MY MELODIES MAY AMUSE YOU. YOU"
190 PRINT "TELL ME THE KEY, AND HOW MANY"
200 PRINT "NOTES TO COMPOSE, AND I WILL DO"
210 PRINT "THE REST. THE KEY MAY BE A"
211 PRINT "SINGLE LETTER, A B C D E F G,"
220 PRINT "OR IT MAY BE A LETTER FOLLOWED"
221 PRINT "BY '#' (SHARP). NO FLATS"
222 PRINT "PLEASE. FOR EXAMPLE. TYPE F FOR"
240 PRINT "F NATURAL, F# FOR F-SHARP. THE"
250 PRINT "MELODY MAY BE UP TO 100 NOTES
251 PRINT "LONG."
260 PRINT "WHAT KEY DO YOU WISH";
270 INPUT K$
280 J$=" "
290 IF LEN(K$)=2 THEN 320
310 K$=K$+J$
320 K=0:FOR J=1 TO LEN(A$)-1 STEP 2
330 IF K$=MID$(A$,J,2) THEN K=(J+1)/2
340 NEXT J
350 IF K<1 OR K>12 THEN 270
360 REM ** K IS NOW A NUMBER, 1<=K<=12, REPRESENTING THE KEY **
370 PRINT "HOW MANY NOTES DO YOU WISH";
380 INPUT N:IF N<1 OR N>100 THEN N=10
400 PRINT
410 REM ** THIS IS THE MELODY GENERATOR **
420 FOR I=1 TO N
430 Y=INT(7*RND(0))+1
440 M(I)=7*(Y-2)+K+12
450 REM NOW TO REDUCE THE TONE MOD 12 **
460 M(I)=M(I)-INT(M(I)/12)*12
470 IF M(I)<>0 THEN 490
480 M(I)=12
490 Q=2*M(I)-1
500 PRINT MID$(A$,Q,2)" ";:SOUNDN(M(I)),4
510 NEXT I
540 PRINT
550 PRINT "HOW ABOUT THAT ONE?":PRINT"WANT ANOTHER";
560 INPUT Q$
570 IF LEFT$(Q$,1)="N" THEN 580
571 GOTO 260
580 PRINT
590 PRINT "THAT'S ALL .. FOLLS !!"
600 END
