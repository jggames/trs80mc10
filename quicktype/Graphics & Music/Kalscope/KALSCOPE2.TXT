100 CLS0
110 PRINT@10,"真真真真真真真";:PRINT@42,CHR$(191);"KALIEDOSCOPE";CHR$(191);:PRINT@74,"真真真真真真真";
120 PRINT@256,"DO YOU WANT INSTRUCTIONS? (Y/N)";
130 A$=INKEY$
140 ON1-(A$<>"")GOTO130:ON1-(A$<>"N")GOTO460:ON1-(A$<>"Y")GOTO150,130
150 REM INSTRUCTIONS
160 PRINT@5*32,"   KALIEDOSCOPE PRODUCES PRETTY"
170 PRINT"PATTERNS ON THE SCREEN. THE"
180 PRINT"PROGRAM TAKES A PATTERN THAT YOU";
190 PRINT"INPUT AND REPEATS IT, MIRRORED"
200 PRINT"AROUND THE HORIZONTAL AND"
210 PRINT"VERTICAL AXES. EACH TIME THE"
220 PRINT"PATTERN IS REPEATED, THE COLOUR"
230 PRINT"IS CHANGED.
240 INPUT"<ENTER> TO CONTINUE";A$
250 PRINT@5*32,"   YOU INPUT THE PATTERN USING"
260 PRINT"THE FOUR ARROW KEYS (AWSD)."
270 PRINT"YOUR CURSOR IS THE FLASHING DOT"
280 PRINT"WHICH IS INITIALLY IN THE"
290 PRINT"MIDDLE OF THE SCREEN. AS YOU"
300 PRINT"INPUT THE PATTERN, IT IS"
310 PRINT"REFLECTED THE SAME WAY AS WHEN"
320 PRINT"THE PROGRAM IS REPEATING THE"
330 PRINT"PATTERN AUTOMATICALLY."
340 INPUT"<ENTER> TO CONTINUE";A$
350 PRINT@5*32,"   WHEN YOU ARE SATISFIED WITH"
360 PRINT"YOUR PATTERN, HIT THE <ENTER>"
370 PRINT"KEY. THE PROGRAM WILL THEN BEGIN";
380 PRINT"TO REPEAT THE PATTERN. WHEN THE"
410 PRINT"PATTERN BECOMES BORING, HIT"
420 PRINT"ANY KEY, AND YOU WILL RETURN TO"
430 PRINT"INPUT MODE. TO EXIT THE PROGRAM"
440 PRINT"HIT THE <Q> KEY.":PRINT
450 INPUT "HIT <ENTER> TO BEGIN";A$
460 CLEAR 600
490 CO=1
500 CLS0
510 REM
520 A=0:X=31:Y=15
530 LI$=""
540 GOSUB870
550 P=POINT(X,Y)
560 A$=INKEY$
570 TI=TI+1:IFTI<50THEN600
580 TI=0
590 IFPOINT(X,Y)THENRESET(X,Y):GOTO600
591 SET(X,Y,CO)
600 IFA$=""THEN 560
601 IFPTHENSET(X,Y,CO):GOTO610
602 RESET(X,Y)
610 ON1-(A$<>"W")GOTO621:ON1-(A$<>"Z")GOTO622:ON1-(A$<>"A")GOTO623:ON1-(A$<>"S"ANDA$<>"D")GOTO624:ON1-(A$<>CHR$(13))GOTO625
620 ON1-(A$<>"Q")GOTO626:CH=0:GOTO630
621 CH=1:GOTO630
622 CH=2:GOTO630
623 CH=3:GOTO630
624 CH=4:GOTO630
625 CH=5:GOTO630
626 CH=6:GOTO630
627 IFLI$=""THEN490
628 GOTO740
630 IFCH=6THEN710
640 IFCH=5THEN627
650 IFCH=0THEN550
660 IFA=250THENSOUND1,1:GOTO550
670 GOSUB850
680 A=A+1
690 LI$=LI$+CHR$(CH)
700 GOTO550
710 REM QUIT PROGRAM
720 CLS0
730 END
740 REM START REPEATING STRING
780 A1=A-1
790 IFCOL=8THENCOL=1:GOTO800
791 COL=COL+1
800 FORA=0TOA1:CH=ASC(MID$(LI$,A+1,1))
810 GOSUB850
820 IFINKEY$<>""THEN490
830 NEXT
840 GOTO790
850 ONCHGOSUB920,940,960,980
870 IFPOINT(X,Y)THENRESET(X,Y):GOTO880
871 SET(X,Y,CO)
880 IFPOINT(63-X,Y)THENRESET(63-X,Y):GOTO890
881 SET(63-X,Y,CO)
890 IFPOINT(X,31-Y)THENRESET(X,31-Y):GOTO900
891 SET(X,31-Y,CO)
900 IFPOINT(63-X,31-Y)THENRESET(63-X,31-Y):GOTO910
901 SET(63-X,31-Y,CO)
910 RETURN
920 Y=Y-1:IFY<0THENY=15:GOTO980
930 RETURN
940 Y=Y+1:IFY>15THENY=0:GOTO960
950 RETURN
960 X=X-1:IFX<0THENX=31:GOTO940
970 RETURN
980 X=X+1:IFX>31THENX=0:GOTO920
990 RETURN
991 REM
992 REM *** KALIEDOSCOPE ***
993 REM COPYRIGHT BY CLOAD 1982
994 REM ADAPTED FROM A PROGRAM
995 REM FOR THE VIP 1802 FROM
996 REM RCA, BY JOE WEISBECKER
997 REM THIS VERSION
998 REM BY RUSS NELSON
