0 DIM DS(20),DZ(20):CLS:FF=.6666:GOTO10
9 PRINT@448,"":PRINT@448,;:RETURN 
10 RESTORE
15 GOSUB 1200:REM BACKGROUND
20 GOSUB 2000:REM DRAW MOUTH
30 GOSUB 3000:REM NOSE
40 GOSUB 4000:REM EYES
50 GOSUB 6000:REM LOAD FACE AND HAIR DATA
60 GOSUB 5000:REM FACE EDGE
70 GOSUB 7000:REM DRAW HAIR
80 GOSUB 8000:REM MOVE EYES
90 FOR DE=1 TO 2500:NEXT DE
100 GOSUB9:INPUT "MAKE ANOTHER FACE? (Y/N)";YN$
110 IF LEFT$(YN$,1)="Y" THEN 10
120 IF LEFT$(YN$,1)="N" THEN END
130 GOTO 100
1200 REM BACKGROUND COLOR
1205 GOSUB9
1210 INPUT"ENTER BACKGROUND COLOR (0-9)";BC
1220 CLS BC
1260 RETURN
2000 REM GET MOUTH COLOR
2010 GOSUB9
2015 INPUT"EMTER MOUTH COLOR (0-8)";MC
2020 REM DRAW MOUTH
2040 FOR X=11 TO 14:SET(X*2,28*FF,MC):SET(X*2+1,28*FF,MC):NEXT
2045 FOR X=11*2+1 TO 14*2:RESET(X,28*FF):NEXT
2050 RETURN
3000 REM NOSE
3010 GOSUB9
3015 INPUT"ENTER NOSE COLOR. (0-8)";NC
3020 FOR Y=16 TO 24:SET(13*2,Y*FF,NC):NEXT
3030 FOR X=11 TO 13:SET(X*2,24*FF,NC):NEXT
3040 RETURN
4000 REM EYES
4010 GOSUB9
4020 INPUT"ENTER EYE COLOR. (0-8)";OC
4040 FOR K=1 TO 3
4050 FOR X=8 TO 10:SET(X*2,(15+K)*FF,OC):NEXT
4060 FOR X=15 TO 17:SET(X*2,(15+K)*FF,OC):NEXT
4070 NEXT K
4080 REM PUPIL
4090 X=10:RESET(X*2,18*FF)
4095 X=17:RESET(X*2,18*FF)
4100 RETURN
5000 REM DRAW FACE OUTLINE
5005 REM STARTING POINTS
5010 X1=4:X2=13:X3=21:X4=12
5015 GOSUB9
5020 INPUT"ENTER FACE COLOR (0-8)";FC
5030 FOR I=1 TO 16
5040 X1=X1+DS(I)
5050 X2=X2+DS(18-I)
5060 X3=X3-DS(I)
5070 X4=X4-DS(18-I)
5085 SET(X1*2,(19-I)*FF,FC)
5095 SET(X2*2,(2+I)*FF,FC)
5105 SET(X3*2,(18+I)*FF,FC)
5115 SET(X4*2,(35-I)*FF,FC)
5120 NEXT I
5130 RETURN
6000 REM LOAD HAIR AND FACE DATA
6030 FOR I=1 TO 17
6040 READ DS(I)
6050 NEXT I
6060 FOR J=1 TO 9
6070 READ DZ(J)
6080 NEXT J
6090 RETURN
7000 REM HAIR
7010 GOSUB9
7020 INPUT"ENTER HAIR COLOR (0-8)";HC
7030 FOR J=1 TO 9
7040 FOR Y=DZ(J) TO DZ(J)+3:SET((J+3)*2,Y*FF,HC):NEXT
7050 FOR Y=DZ(10-J) TO DZ(10-J)+3:SET((J+12)*2,Y*FF,HC):NEXT
7060 NEXT J
7070 FOR DE=1 TO 500:NEXT DE:RETURN
8000 REM MOVE EYES
8010 FOR K=1 TO 3
8020 FOR X=8 TO 10:SET(X*2,(15+K)*FF,OC):NEXT
8030 FOR X=15 TO 17:SET(X*2,(15+K)*FF,OC):NEXT
8040 NEXT K
8050 X=9:RESET(X*2,18*FF)
8060 X=16 :RESET(X*2,18*FF)
8070 RETURN
10000 REM FACE DATA
10005 DATA 0,0,0,1,0,0,1,0,1,0,1,1,0,1,1,1,0
10010 REM HAIR DATA
10015 DATA 12,9,7,5,4,3,3,3,3
10020 REM JEFF NILSON
10030 REM ENTER MAGAZINE
10040 REM OCT 1983
10050 REM MC-10 JIM GERRIE
