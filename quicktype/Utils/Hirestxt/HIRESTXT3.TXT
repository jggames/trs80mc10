10 CLEAR1000:GOSUB 915:REM LOAD CHARACTER ARRAY
20 PMODE 2,1
30 PCLS
40 SCREEN 1,0
50 HT=6:VT=3
60 A$="AVAILABLE CHARACTERS:"
70 GOSUB 800
75 HT=0:VT=6:A$=""
80 FORI=32TO95:A$=A$+CHR$(I):NEXT
90 GOSUB 800
100 GOTO 100
800 SH=HT:SV=VT:REM SAVE TAB VALUES
805 FOR N=1 TO LEN(A$)
810 I=8*HT:J=12*VT
815 SP$=MID$(A$,N,1)
820 REM SP$ IS THE CHARACTER WE WILL PRINT
825 GOSUB895:REM GET THE DATA FROM A$(#) INTO U$
830 REM U$ IS A STRING OF 7 BYTES
835 FOR JC=0 TO 6:REM 7 ROWS
840 T$=MID$(U$,JC+1,1)
845 GOSUB 910:REM SET THE PROPER BITS IN KC
850 FOR IC=0 TO 4:REM 5 BITS EACH ROW
855 IF ((KC)AND(2^IC))>0 THENPSET(I+IC,J+JC)
860 REM DRAW THE CHARACTER
865 NEXT IC:NEXT JC
870 HT=HT+1:IF HT=32 THEN HT=0:VT=VT+1
875 NEXT N
880 HT=SH:VT=SV:REM RESTORE TAB VALUES
885 RETURN
890 REM SUBROUTINE TO SELECT THE PROPER ELEMENT OF A$(#)
895 T=ASC(SP$)
900 U$=A$(T-32):RETURN
905 REM SUBROUTINE TO DECODE THE ARRAY A$(#) INTO BINARY
910 KC=ASC(T$)-64:RETURN
915 DIMA$(63):CC=0
920 REM A$(I) IS THE BINARY CODE FOR CHR$(32+I)
925 M$="@@@@@@@DDDDD@D"+CHR$(91)+CHR$(91)+"@@@@@JJ"+CHR$(95)+"J"+CHR$(95)+"JJJ"+CHR$(95)+"A"+CHR$(95)+"P"+CHR$(95)+"JSSHDBYYFIEBMIVDD@@@@@DBAAABDDHPPPHD"
930 GOSUB960:M$="@DN"+CHR$(95)+"ND@@DD"+CHR$(95)+"DD@@@@@HHD@@@"+CHR$(95)+"@@@@@@@@XXPPHDBAANQYUSQNDFDDDDNNQPNAA"+CHR$(95)+"NQPHPQNHLJI"+CHR$(95)+"HH"
935 GOSUB960:M$=CHR$(95)+"AAOPQNNAAOQQN"+CHR$(95)+"PPHDBANQQNQQNNQQ^PQN@DD@DD@@DD@DDBHDBABDH@@"+CHR$(95)+"@"+CHR$(95)+"@@BDHPHDBFIHDD@D"
940 GOSUB960:M$="@OIHOIODJQ"+CHR$(95)+"QQQOQQOQQONQAAAQNOQQQQQO"+CHR$(95)+"AAOAA"+CHR$(95)+""+CHR$(95)+"AAOAAANQAAYQN"
945 GOSUB960:M$="QQQ"+CHR$(95)+"QQQNDDDDDN"+CHR$(92)+"HHHHIFQIECEIQAAAAAA"+CHR$(95)+"Q"+CHR$(91)+"UQQQQQSUUYQQNQQQQQNOQQOAAANQQQQY^OQQOEIQ"
950 GOSUB960:M$="NQANPQN"+CHR$(95)+"DDDDDDQQQQQQNQQQQJJDQQQQU"+CHR$(91)+"QQQJDJQQQQJDDDD"+CHR$(95)+"PHDBA"+CHR$(95)
955 GOSUB960:M$="FBBBBBFAABDHPPLHHHHHLDNUDDDD@DBOBD@":GOSUB960:RETURN
960 FORI=1TOLEN(M$)STEP7:A$(CC)=MID$(M$,I,7):CC=CC+1:NEXT:RETURN
