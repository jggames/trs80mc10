Hi Jim,
I’ve noticed the keyboard issue as well with my setup. This seems to happen even without the compiler. For example, if I enter in LODERUN32.TXT I will often find I cannot move right by pressing “S” without pressing some other key first. It’s not always reproducible though.
I think perhaps for something more reliable that should work in BASIC you can try this instead of PEEK(2) AND PEEK(17023):
PEEK(2) AND MASK AND NOT PEEK(KEYGROUP) using the keygroup and mask shown below:
KEYGROUP	1	2	4	8	16	32
16945		@	H	P	X	0	8
16946		A	I	Q	Y	1	9
16947		B	J	R	Z	2	:*
16948		C	K	S		3	;+
16949		D	L	T		4	,<
16950		E	M	U		5	-=
16951		F	N	V	ENT	6	.>
16952		G	O	W	SPC	7	/?

This should work most of the time even when more than one key is down. Here is sample code to get you started.
5 CLS
10 IF PEEK(2) AND 4 AND NOT PEEK(16952) THEN PRINT@1,"W";
20 IF PEEK(2) AND 1 AND NOT PEEK(16946) THEN PRINT@2,"A";
30 IF PEEK(2) AND 4 AND NOT PEEK(16948) THEN PRINT@3,"S";
40 IF PEEK(2) AND 1 AND NOT PEEK(16949) THEN PRINT@4,"D";
50 IF PEEK(2) AND 8 AND NOT PEEK(16951) THEN PRINT@5,"ENT";
60 IF PEEK(2) AND 8 AND NOT PEEK(16952) THEN PRINT@6,"SPC";
70 GOTO 5
Try pressing multiple keys at once to get a sense of its limitations.
If this interests you, I can tweak the compiler to support it.
Best,
-Greg

IJKL
10 IF PEEK(2) AND 2 AND NOT PEEK(16946) THEN PRINT@1,"I";
20 IF PEEK(2) AND 2 AND NOT PEEK(16947) THEN PRINT@2,"J";
30 IF PEEK(2) AND 2 AND NOT PEEK(16948) THEN PRINT@3,"K";
40 IF PEEK(2) AND 2 AND NOT PEEK(16949) THEN PRINT@4,"L";

Joypad
POKE3,0:POKE16925,0:POKE16926,1
J=J-((PEEK(2)AND1ANDNOTPEEK(P))ORPEEK(3)=74ANDJ>.)+((PEEK(2)AND1ANDNOTPEEK(Q))ORPEEK(3)=70ANDJ<31)

AWSZ Diamond
5 CLS
10 IFPEEK(2)AND4ANDNOTPEEK(16952)THENPRINT@1,"W";
20 IFPEEK(2)AND1ANDNOTPEEK(16946)THENPRINT@2,"A";
30 IFPEEK(2)AND4ANDNOTPEEK(16948)THENPRINT@3,"S";
40 IFPEEK(2)AND8ANDNOTPEEK(16947)THENPRINT@4,"Z";
50 IFPEEK(2)AND8ANDNOTPEEK(16951)THENPRINT@5,"ENT";
60 IFPEEK(2)AND8ANDNOTPEEK(16952)THENPRINT@6,"SPC";
70 IFPEEK(2)AND8ANDNOTPEEK(16946)THENPRINT@2,"Y";
80 IFPEEK(2)AND2ANDNOTPEEK(16951)THENPRINT@5,"N";
85 IFPEEK(2)AND1ANDNOTPEEK(16948)THENPRINT@3,"C";
90 GOTO 5

0 CLS0:GOTO10
1 PRINT@1,"*";:RETURN
2 PRINT@2,"*";:RETURN
3 PRINT@34,"*";:RETURN
4 PRINT@66,"*";:RETURN
5 PRINT@65,"*";:RETURN
6 PRINT@64,"*";:RETURN
7 PRINT@32,"*";:RETURN
8 PRINT@0,"*";:RETURN
10 GOSUB2420:ONSTGOSUB1,2,3,4,5,6,7,8:IFSTTHEN10
20 PRINT@0,"   ";:PRINT@32,"   ";:PRINT@64,"   ";:GOTO10
2420 ST=.:IFPEEK(2)=.THENRETURN
2421 IF4ANDNOTPEEK(16952)THENST=1
2422 IF1ANDNOTPEEK(16950)THENST=2
2423 IF1ANDNOTPEEK(16949)THENST=3
2424 IF1ANDNOTPEEK(16948)THENST=4
2425 IF8ANDNOTPEEK(16945)THENST=5
2428 IF8ANDNOTPEEK(16947)THENST=6
2427 IF1ANDNOTPEEK(16946)THENST=7
2426 IF4ANDNOTPEEK(16946)THENST=8
2430 IFST=.THENRETURN
2440 RETURN

5 REM PILOT TIE FIGHTER
10 CLS:P=32*8+16:PRINT@P,")*(";
20 C2=-(PEEK(2)AND4ANDNOTPEEK(16952))*8-(PEEK(2)AND1ANDNOTPEEK(16946))*2
25 C2=C2+(PEEK(2)AND4ANDNOTPEEK(16948))/2+(PEEK(2)AND8ANDNOTPEEK(16947))*4
30 IFPEEK(2)AND8ANDNOTPEEK(16951)THENPRINT@0,P:STOP
35 IFC2=0THEN20
40 IFP+C2<0ORP+C2>508THEN20
50 PRINT@P,"   ";:P=P+C2:PRINT@P,")*(";:GOTO20

5 REM SCREEN CURSOR PROGRAM
10 CLS:P=32*8+16:PRINT@P,"+";
20 C2=-(PEEK(2)AND4ANDNOTPEEK(16952))*8-(PEEK(2)AND1ANDNOTPEEK(16946))*1
25 C2=C2+(PEEK(2)AND4ANDNOTPEEK(16948))/4+(PEEK(2)AND8ANDNOTPEEK(16947))*4
30 IFPEEK(2)AND8ANDNOTPEEK(16951)THENPRINT@0,P:STOP
35 IFC2=0THEN20
40 IFP+C2<0ORP+C2>510THEN20
50 PRINT@P," ";:P=P+C2:PRINT@P,"+";:GOTO20




0 CLS
10 PRINT@0,-(PEEK(2)AND4ANDNOTPEEK(16952))*8
20 PRINT@32,-(PEEK(2)AND1ANDNOTPEEK(16946))
30 PRINT@64,(PEEK(2)AND4ANDNOTPEEK(16948))*8
40 PRINT@96,(PEEK(2)AND1ANDNOTPEEK(16949))
50 GOTO10


0 CLS:GOTO2
1 K=-(4ANDNOTPEEK(16952))*8-(1ANDNOTPEEK(16946))+(4ANDNOTPEEK(16948))*8+(1ANDNOTPEEK(16949)):RETURN
2 K=.:IFPEEK(2)THENGOSUB1
3 PRINT@0,K;:GOTO2