REM Jim Butterfield Compute! magazine Feb 1981
100 REM DEMO PROGRAM FOR SUB-ROUTINE
110 FOR J = 1 TO 20
120 V = EXP (RND (0)*14-6)* SGN(RND(0)-.2)
130 V1 = 4 : V2 = 0 : GOSUB 50000: PRINTV$; " ";
140 V1 = 3 : V2 = 1 : GOSUB 50000: PRINTV$; " ";
150 V2 = 4 : GOSUB 50000 : PRINT V$
160 NEXT J
170 END
50000 REM 'USING' ARRANGE IN COLUMNS
50010 REM V IS VALUE; V1.V2 PRINTS
50020 V4=INT(V*INT(10^V2)+.5)
50030 V$=RIGHT$("     "+ STR$(V4), V1 + V2 + 1)
50040 IF V2 < 1 GOTO 50080
50050 FOR V5 = V1 + 2 TO V1 + V2 + 1 : IF ASC(MID$(V$, V5)+CHR$(255)) < 48 THEN NEXT V5
50060 V6 = V5 - V1 - 1
50070 V$=MID$(V$, V6, V1 + 1) + LEFT$(".00000",V6) + MID$(V$, V5)
50080 IF ASC(V$)> 47 THEN V$ = LEFT$ ("**********", V1 + V2 + 2 + (V2 =0))
50090 RETURN
