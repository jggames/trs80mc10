10 GOSUB 915:REM LOAD CHARACTER ARRAY
20 PMODE 4,1
30 PCLS
40 SCREEN 1,0
50 HT=6:VT=3
60 A$="AVAILABLE CHARACTERS:"
70 GOSUB 800
75 HT=0:VT-=6:A$=""
80 FORI=32TO95:A$=A$+CHR$(I):NEXT
90 GOSUB 800
100 GOTO 100
800 SH=HT:SV=VT:REM SAVE TAB VALUES
805 FOR N=1 TO LEN(A$)
810 I=8*HT:J=l2*VT
815 SP$=MID$(A$,N,1)
820 REM SP$ IS THE CHARACTER WE WILL PRINT
825 GOSUB895:REM GET THE DATA FROM A$(#) INTO U$
830 REM U$ IS A STRING OF 7 BYTES
835 FOR JC=0 TO 6:REM 7 ROWS
840 T$=MID$(U$,JC+1,1)
845 GOSUB 910:REM SET THE PROPER BITS IN KC
850 FOR IC=0 TO 4:REM 5 BITS EACH ROW
855 IF ((KC)AND(2^IC))>0 THENPSET(I+IC,J+JC)
860 REM DRAW THE CHARACTER
865 NEXT IC:NEXT JC
870 HT=HT+1:IF HT=32 THEN HT=0:VT=VT+1
875 NEXT N
880 HT=SH:VT=SV:REM RESTORE TAB VALUES
885 RETURN
890 REM SUBROUTINE TO SELECT THE PROPER ELEMENT OF A$(#)
895 T=ASC(SP$)
900 U$=A$(T-32):RETURN
905 REM SUBROUTINE TO DECODE THE ARRAY A$(#) INTO BINARY
910 KC=ASC(T$)-64:RETURN
915 DIM A$(63)
920 REM A$(I) IS THE BINARY CODE FOR CHR$(32+I)
925 DATA@@@@@@@,DDDDD@D,[[@@@@@,JJ_J_JJ,J_A_P_J,SSHDBYY,FIEBMIV,DD@@@@@,DBAAABD,DHPPPHD
930 DATA@DN_ND@,@DD_DD@,@@@@HHD,@@@_@@@,@@@@@XX,PPHDBAA,NQYUSQN,DFDDDDN,NQPNAA_,NQPHPQN,HLJI_HH
935 DATA_AAOPQN,NAAOQQN,_PPHDBA,NQQNQQN,NQQ^PQN,@DD@DD@,@DD@DDB,HDBABDH,@@_@_@@,BDHPHDB,FIHDD@D
940 DATA@OIHOIO,DJQ_QQQ,OQQOQQO,NQAAAQN,OQQQQQO,_AAOAA_,_AAOAAA,NQAAYQN
945 DATAQQQ_QQQ,NDDDDDN,\HHHHIF,QIECEIQ,AAAAAA_,Q[UQQQQ,QSUUYQQ,NQQQQQN,OQQOAAA,NQQQQY^,OQQOEIQ
950 DATANQANPQN,_DDDDDD,QQQQQQN,QQQQJJD,QQQQUYQ,QQJDJQQ,QQJDDDD,_PHDBA_
955 DATAFBBBBBF,AABDHPP,LHHHHHL,DNUDDDD,@DBOBD@
960 FORI=0TO63:READA$(I):NEXT
965 RETURN
