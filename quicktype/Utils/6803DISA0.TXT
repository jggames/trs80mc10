5 REM ** 6803 DISASSEMBLER
10 REM ** RLS - 72415,1607
15 REM ** RAM PACK REQUIRED
20 DIMOP$(255),AM(255)
30 GOTO520
40 REM* HEX CONVERT
50 HX$="":POKE232,OT
60 ON N GOTO90,70
70 T=INT(A/4096):GOSUB110:IFT>0THENA=A-T*4096
80 T=INT(A/256):GOSUB110:IFT>0THENA=A-T*256
90 T=INT(A/16):GOSUB110:IFT>0THENA=A-T*16
100 T=A
110 X$=STR$(T):IFT>9THENX$=CHR$(55+T)
120 HX$=HX$+RIGHT$(X$,1):RETURN
130 REM* PRINT HEX ADDRESS
140 A=CA:N=2:GOSUB40:PRINTHX$;":  ";:RETURN
150 REM* DUMP IT
160 GOSUB130:A=X:N=1:GOSUB40:PRINTHX$;
170 CA=CA+1:A=PEEK(CA)
180 IFAM(X)=3ORAM(X)=7THEN210
190 TA=A:N=1:GOSUB40:PRINTHX$;"    ";OP$(X);"  ";
200 A=TA:N=1:GOSUB40:PRINTO$;HX$:GOTO240
210 CA=CA+1:A=A*256+PEEK(CA)
220 TA=A:N=2:GOSUB40:PRINTHX$;"  ";OP$(X);"  ";
230 A=TA:N=2:GOSUB40:PRINTO$;HX$
240 RETURN
250 REM* ILLEGAL OR INHER. 
260 GOSUB130:A=X:N=1:GOSUB40:PRINTHX$;"      ";OP$(X):RETURN
280 REM* IMMED.
290 O$="#$":GOSUB150:RETURN
310 REM* DIRECT & EXTENDED
320 O$="$":GOSUB150:RETURN
340 REM* INDEXED
350 O$="X,$":GOSUB150:RETURN
370 REM* RELATIVE
380 GOSUB130:A=X:N=1:GOSUB40:PRINTHX$;
390 CA=AA+1:A=PEEK(CA)
400 TA=A:N=1:GOSUB40:PRINTHX$;"    ";OP$(X);
410 A=CA+TA+1:IFTA>127THENA=CA-(255-TA)
420 N=2:GOSUB40:PRINT"  $";HX$
430 RETURN
440 REM* HEX INPUT
450 X=1:N=0
460 FORKT=LEN(HX$)TO2STEP-1
470 Y=ASC(MID$(HX$,KT,1))-48:IFY>9THENY=ASC(MID$(HX$,KT,1))-55
480 N=N+Y*X:X=X*16:NEXTKT:HX$=STR$(N)
490 RETURN
500 ST$=INKEY$:IFST$=""THEN515
510 ST$=INKEY$:IFST$=""THEN510
515 RETURN
520 REM* MAIN
530 POKE49151,64:CLS
540 PRINT"6803 DISASSEMBLER V1.0"
550 FORKT=0TO255:READOP$(KT),AM(KT):NEXT
560 PRINT:PRINT
570 INPUT"STARTING ADDRESS >>";ST$
580 INPUT"  ENDING ADDRESS >>";EN$
590 PRINT:PRINT"S)CREEN OR P)RINTER? >>";
600 HX$=INKEY$:IFHX$=""OR HX$<>"S"ANDHX$<>"P"THEN600
605 PRINTHX$
610 OT=0:IFHX$="P"THEN OT=254
620 IFLEFT$(ST$,1)="$"THENHX$=ST$:GOSUB440:ST$=HX$
630 IFLEFT$(EN$,1)="$"THENHX$=EN$:GOSUB440:EN$=HX$
640 CLS:ST=VAL(ST$):EN=VAL(EN$)
650 FORCA=ST TO EN:X=PEEK(CA):ON AM(X)+1GOSUB250,250,280,280,310,340,370,310:GOSUB500:NEXTCA
660 END
30000 DATA????,0,NOP ,1,????,0,????,0,LSRD,1,ASLD,1,TAP ,1,TPA ,1
30005 DATAINX ,1,DEX ,1,CLV ,1,SEV ,1,CLC ,1,SEC ,1,CLI ,1,SEI ,1
30010 DATASBA ,1,CBA ,1,????,0,????,0,????,0,????,0,TAB ,1,TBA ,1
30015 DATA????,0,DAA ,1,????,0,ABA ,1,????,0,????,0,????,0,????,0
30020 DATABRA ,6,BRN ,6,BHI ,6,BLS ,6,BCC ,6,BCS ,6,BNE ,6,BEQ ,6
30025 DATABVC ,6,BVS ,6,BPL ,6,BMI ,6,BGE ,6,BLT ,6,BGT ,6,BLE ,6
30030 DATATSX ,1,INS ,1,PULA,1,PULB,1,DES ,1,TXS ,1,PSHA,1,PSHB,1
30035 DATAPULX,1,RTS ,1,ABX ,1,RTI ,1,PSHX,1,MUL ,1,WAI ,1,SWI ,1
30040 DATANEGA,1,????,0,????,0,COMA,1,LSRA,1,????,0,RORA,1,ASRA,1
30045 DATAASLA,1,ROLA,1,DECA,1,????,0,INCA,1,TSTA,1,????,0,CLRA,1
30050 DATANEGB,1,????,0,????,0,COMB,1,LSRB,1,????,0,RORB,1,ASRB,1
30055 DATAASLB,1,ROLB,1,DECB,1,????,0,INCB,1,TSTB,1,????,0,CLRB,1
30060 DATANEG ,5,????,0,????,0,COM ,5,LSR ,5,????,0,ROR ,5,ASR ,5
30065 DATAASL ,5,ROL ,5,DEC ,5,????,0,INC ,5,TST ,5,JMP ,5,CLR ,5
30070 DATANEG ,7,????,0,????,0,COM ,7,LSR ,7,????,0,ROR ,7,ASR ,7
30075 DATAASL ,7,ROL ,7,DEC ,7,????,0,INC ,7,TST ,7,JMP ,7,CLR ,7
30080 DATASUBA,2,CMPA,2,SBCA,2,SUBD,3,ANDA,2,BITA,2,LDAA,2,????,0
30085 DATAEORA,2,ADCA,2,ORAA,2,ADDA,2,CPX ,3,BSR ,6,LDS ,3,????,0
30090 DATASUBA,4,CMPA,4,SBCA,4,SUBD,4,ANDA,4,BITA,4,LDAA,4,STAA,4
30095 DATAEORA,4,ADCA,4,ORAA,4,ADDA,4,CPX ,4,JSR ,4,LDS ,4,STS ,4
30100 DATASUBA,5,CMPA,5,SBCA,5,SUBD,5,ANDA,5,BITA,5,LDAA,5,STAA,5
30105 DATAEORA,5,ADCA,5,ORAA,5,ADDA,5,CPX ,5,JSR ,5,LDS ,5,STS ,5
30110 DATASUBA,7,CMPA,7,SBCA,7,SUBD,7,ANDA,7,BITA,7,LDAA,7,STAA,7
30115 DATAEORA,7,ADCA,7,ORAA,7,ADDA,7,CPX ,7,JSR ,7,LDS ,7,STS ,7
30120 DATASUBB,2,CMPB,2,SBCB,2,ADDD,3,ANDB,2,BITB,2,LDAB,2,????,0
30125 DATAEORB,2,ADCB,2,ORAB,2,ADDB,2,LDD ,3,????,0,LDX ,3,????,0
30130 DATASUBB,4,CMPB,4,SBCB,4,ADDD,4,ANDB,4,BITB,4,LDAB,4,STAB,4
30135 DATAEORB,4,ADCB,4,ORAB,4,ADDB,4,LDD ,4,STD ,4,LDX ,4,STX ,4
30140 DATASUBB,5,CMPB,5,SBCB,5,ADDD,5,ANDB,5,BITB,5,LDAB,5,STAB,5
30145 DATAEORB,5,ADCB,5,ORAB,5,ADDB,5,LDD ,5,STD ,5,LDX ,5,STX ,5
30150 DATASUBB,7,CMPB,7,SBCB,7,ADDD,7,ANDB,7,BITB,7,LDAB,7,STAB,7
30155 DATAEORB,7,ADCB,7,ORAB,7,ADDB,7,LDD ,7,STD ,7,LDX ,7,STX ,7
