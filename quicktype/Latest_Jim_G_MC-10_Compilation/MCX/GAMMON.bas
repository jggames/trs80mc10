10 GOSUB9020:CLS:PRINT@40,"backgammon";:PRINT:DIMS(25),A(25),C(8):C(3)=32:C(5)=64:X=RND(-(PEEK(9)*256+PEEK(10))):X=0
20 PRINT:A$="NORMAN":PRINT"   MY NAME IS "A$:PRINT
30 INPUT"   WHAT'S YOURS";B$:PRINT:PRINT"OUR PIECES :":CO=32
40 PRINT:PRINTA$"'S",CHR$(CO+131)CHR$(CO+130):CO=64:PRINT:PRINTB$"'S",CHR$(CO+131)CHR$(CO+130);
50 PRINT@455,"HIT ENTER KEY";
60 IFINKEY$<>""THEN60
70 IFINKEY$=""THEN70
80 FORX=0TO25:READA(X):NEXT:GOSUB900:W=RND(2)-1:RESTORE
90 DATA,-2,,,,,5,,3,,,,-5,5,,,,-3,,-5,,,,,2,,
260 IFW=0THEN300
261 PRINT@0,LEFT$(B$,4);
270 IFINKEY$<>""THEN270
271 PRINT" 1=ROLL,2=REDRAW,3=EXIT      ";
272 F$=INKEY$:IFF$<"1"ORF$>"3"THEN272
273 X=ASC(F$)-48:PRINT@28,X
280 IFX=1THEN300
281 IFX=2THEN290
282 IFX=3THEN8000
283 GOTO260
290 GOSUB900:GOTO260
300 L=RND(6):M=RND(6):D=2:IFM=L THEN D=4:GOTO330
301 IFL<M THENX=L:L=M:M=X
330 IFW=0 THEN1500
331 GOTO2000
900 CLS:PRINT:FORX=1TO14
910 PRINT:PRINT"       "CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133);
911 PRINTCHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128)CHR$(133)CHR$(128);
912 NEXT
920 PRINT@128,LEFT$(A$,4)"->";:PRINT@32*6,A(25);:CO=32:PRINT@32*7,CHR$(CO+131)CHR$(CO+130);
930 PRINT@32*8,"B      X W V U T S R Q P O N M ";
940 PRINT@32*9,"A      A B C D E F G H I J K L ";:CO=64
950 PRINT@32*10,"R";:PRINT@32*11,CHR$(CO+131)CHR$(CO+130);:PRINT@32*12,A(0);:PRINT@32*14,LEFT$(B$,4)"->";
960 FORX=13TO24:IFA(X)=0THEN990
970 U=5+(A(X)>0)*2:CO=C(U):Y=(24-X)*2+7
980 FORZ=1TOABS(A(X)):S=(Z+1)*32:PRINT@S+Y,CHR$(CO+140)CHR$(CO+136);:NEXT
990 NEXT:FORX=1TO12:IFA(X)=0THEN1020
1000 U=5+(A(X)>0)*2:CO=C(U):Y=(X-1)*2+7
1010 FORZ=1TOABS(A(X)):S=(16-Z)*32:PRINT@S+Y,CHR$(CO+131)CHR$(CO+130);:NEXT
1020 NEXT:RETURN
1500 E=0:GOSUB7000:PRINT@0,LEFT$(A$,4)"'S ROLL"L;M;
1501 FORZ=0TO25:S(Z)=A(Z):NEXT:U=0:S1=0
1502 J=24:IFA(25)>0 THEN1626
1503 IF A(J)<1 THENJ=J-1:GOTO1503
1504 IF J<7 THEN1650
1505 FORX=L+1TO24:IFA(X)>0ANDA(X-L)>-2 THENGOSUB1511
1506 NEXT:IFU=0 THENU=L:L=M:M=U:GOTO1505
1507 IFS1>0 THEN1700
1508 GOSUB1575:U=L:L=M:M=U:F1=F:T1=T:GOSUB1575
1509 IFS1=0 THEN1845
1510 F=F1:T=T1:F1=0:T1=0:GOTO1638
1511 S(X)=S(X)-1:IFS(X-L)=-1 THENS(0)=S(0)+1:S(X-L)=0
1515 S(X-L)=S(X-L)+1
1520 B=0:FORY=M+1TO24:IFS(Y)<1 OR S(Y-M)<-1 THEN1560
1535 B=(A(X)=2)*(25-X)/25+(A(X-L)<1)*(25-X-L)/25
1540 B=B+(S(Y)=2)*(25-Y)/25+(S(Y-M)<1)*(25-Y-M)/25
1543 B=B-(A(X)=1)*(25-X)/25-(A(X-L)=1)*(25-X-L)/25
1545 B=B-(S(Y)=1)*(25-Y)/25-(S(Y-M)=1)*(25-Y-M)/25
1547 IFA(Y-M)=0 OR A(X-L)=0 THEN E=1/2
1550 C=(A(X-L)=-1)*(X-L)/24+(S(Y-M)=-1)*(Y-M)/24
1555 S=X+Y/48
1557 S=S+B/2+5-C/2+E:IF S>=S1 THEN S1=S:F=X:T=X-L:F1=Y:T1=Y-M
1560 NEXT:S(X)=S(X)+1:S(X-L)=S(X-L)-1
1563 IFS(0)<>A(0) THENS(X-L)=-1:S(0)=A(0)
1564 RETURN
1575 FORX=1+LTOJ:IFS(X)<1 OR S(X-L)<-1 THEN1625
1580 S=(A(X)=1)+(A(X-L)=1)-(A(X)=2)-(A(X-L)<1)
1585 S=-S+3+ABS(S(X-L)=-1)/2
1590 IF S>S1 THENS1=S:F=X:T=X-L
1625 NEXT:RETURN
1626 F=25:F1=F:T=F-L:T1=F-M:S=(A(T)>-2):U=(A(T1)>-2)
1627 IFS=0 AND U=0 THEN1845
1628 IFA(F)>1 AND S AND U THEN1700
1629 IFA(F)=1 THEN1635
1630 IF S THENF1=0:T1=0:GOTO1634
1631 F=F1:T=T1:T1=0:F1=0
1634 GOTO1700
1635 X=25:IF S THENGOSUB1511
1636 IF U THENU=L:L=M:M=U:GOSUB1511
1637 GOTO1700
1638 IFT-F=L THENL=M
1639 S(J)=S(J)-1:IFS(J-L)=-1 THENS(J-L)=0
1640 S(J-L)=S(J-L)+1
1641 IFS(J)<1 THENJ=J-1:GOTO1641
1642 IFJ>6 THEN1700
1644 IFS(L)>0 THENF1=L:T1=26:GOTO1646
1645 IFL>J THENF1=J:T1=26
1646 GOTO1700
1650 F=0:F1=0:IFA(L)>0 THENF=L:T=26:GOTO1652
1651 IFJ<L THENF=J:T=26
1652 U=J:IF F THENS(F)=S(F)-1
1653 IFS(U)<1 AND U>0 THENU=U-1:GOTO1653
1654 IFU=0 THEN5000
1655 IFS(M)>0 THEN F1=M:T1=26:GOTO1657
1656 IFU<M THENF1=U:T1=26
1657 IFF1 THENS(F1)=S(F1)-1
1658 IFS(U)<1 AND U>0 THENU=U-1:GOTO1658
1659 IFU=0 THEN5000
1673 IFF>0 AND F1 THEN1700
1674 IFF=0 AND F1=0 THEN1680
1676 IF F THENL=M:F1=F:T1=T
1677 GOSUB1575:IFS1=0 THENF=F1:T=T1:F1=0:T1=0
1678 GOTO1700
1680 IFA(M+L)>0 AND A(M)>-2 THEN F=M+L:T=M:F1=M:T1=26:GOTO1700
1681 IFA(M+L)>0 AND A(L)>-2 THEN F=M+L:T=L:F1=L:T1=26:GOTO1700
1682 IFA(J)<>1 THEN U=0:GOTO1505
1684 IFA(J-M)<-1 THEN1692
1685 U=J:S(J)=0:S(J-M)=S(J-M)+1
1686 IF S(U)<1 AND U>0 THENU=U-1:GOTO1686
1687 IF U=0 THEN5000
1688 IF U<L THENF=J:T=F-M:F1=U:T1=26:GOTO1700
1692 IFA(J-L)<-1 THENU=0:GOTO1505
1693 S(J-L)=S(J-L)+1:S(J)=0
1694 IFS(U)<0 AND U>0 THENU=U-1:GOTO1694
1695 IF U=0 THEN5000
1696 IF J<M THENF=J:T=F-L:F1=U:T1=26:GOTO1700
1697 U=0:GOTO1505
1700 PRINT@4," FROM  "CHR$(F+64)"  TO "CHR$(T+64)
1701 IF F1 OR T1 THEN PRINT@19,"&  "CHR$(F1+64)"  TO "CHR$(T1+64)
1703 IFT1=26 THEN PRINT@25,"HOME"
1704 IFT=26 THEN PRINT@14,"HOME"
1705 IFF=0 AND T THEN PRINT@10,"BAR"
1706 IFF1=0 ANDT1 THEN PRINT@21,"BAR"
1707 A(F)=A(F)-1:IF T=26 THEN1711
1708 IFA(T)=-1 THEN A(T)=0:A(0)=A(0)+1:SOUND22,2:SOUND11,3
1710 A(T)=A(T)+1
1711 GOSUB3000:IF F1=0 THENGOSUB9001:GOTO1735
1715 A(F1)=A(F1)-1:F=F1:T=T1:IFT=26 THEN1730
1720 IFA(T)=-1 THENA(T1)=0:A(0)=A(0)+1:SOUND20,2:SOUND9,3
1725 A(T1)=A(T1)+1
1730 GOSUB3000:GOSUB9001
1735 E=0:FORX=1TO25:IFA(X)<1 THENE=E+1
1740 NEXT:IFE=25 THEN5000
1750 W=1:IFD=4 THEN D=2:GOTO1501
1751 GOTO260
1845 E=32:GOSUB7000:PRINT@E,"CAN'T MOVE";:W=ABS(W-1):GOSUB9001
1850 GOSUB7000:GOTO260
2000 E=0:GOSUB7000:PRINT@0,LEFT$(B$,4)"'S ROLL";L;:IFD>1 THENPRINTM;
2030 J=0:IFA(0)>0ANDA(L)>1ANDA(M)>1THEN1845
2035 IFA(J)>=0THENJ=J+1:GOTO2035
2040 E=0:FORX=1TO24-L:IFA(X)<0ANDA(X+L)<2THENE=1
2050 NEXT:FORX=1TO24-M:IFA(X)<0ANDA(X+M)<2THENE=1
2060 NEXT:IFETHEN2070
2061 IFJ<19THEN1845
2062 IFA(25-M)>-1ANDA(25-L)>-1AND25-L>JTHEN1845
2070 IFA(0)>0THENF=0:PRINT@17,"FROM BAR";:GOTO2100
2080 IFINKEY$<>""THEN2080
2081 PRINT@17,"FROM--";
2085 F$=INKEY$:IFF$=""THEN2085
2086 PRINTF$"      ";
2090 F=ASC(F$)-64:IFF<1ORF>24THEN2140
2091 IFA(F)>-1THEN2140
2100 IFINKEY$<>""THEN2100
2101 PRINT@25," TO--";
2104 T$=INKEY$:IFT$=""THEN2104
2105 T=ASC(T$)-64
2110 IFT<1ORT>24THENPRINT@25," HOME ";:GOTO2300
2111 IFA(T)>1THEN2140
2120 PRINTT$:IFT-F=LTHENL=M:GOTO2200
2130 IFT-F=MTHENM=L:GOTO2200
2140 E=0:GOSUB7000:PRINT@0,"INVALID MOVE";:GOSUB9001:GOTO2000
2200 IFF=0THENA(0)=A(0)-2
2205 A(F)=A(F)+1:IFA(T)=1THENA(25)=A(25)+1:A(T)=0:SOUND19,2:SOUND8,3
2210 A(T)=A(T)-1
2220 GOSUB3000:D=D-1:E=0:FORX=1TO25:IFA(X)>-1THENE=E+1
2240 NEXT:IFE=25THEN6000
2241 IFD=0THENW=0:GOTO300
2242 GOTO2000
2300 IFJ<19THEN2140
2320 IFF=25-MORJ>25-MANDF=JTHENM=L:GOTO2350
2330 IFF=25-LORJ>25-LANDF=JTHENL=M:GOTO2350
2340 GOTO2140
2350 A(F)=A(F)+1:GOTO2220
3000 PRINT@192,A(25);:PRINT@384,A(0);:IFF=0 OR F=25 THEN3050
3010 H=F:GOSUB3100:IFT<1ORT>24THENRETURN
3050 H=T:GOSUB3100:RETURN
3100 U=5+(A(H)>0)*2:P=A(H):IFP>9 THENP=9
3120 IFH<13THEN3140
3121 X=(24-H)*2+7:G=1:IFH=F THENG=2
3135 Y=(ABS(P)+G)*32:GOTO3150
3140 X=(H-1)*2+7:G=16:IFH=F THENG=15
3145 Y=(G-ABS(P))*32
3150 IFH=T THEN3170
3160 CO=0:PRINT@X+Y,CHR$(CO+133)CHR$(CO+128);:RETURN
3170 IFH<13THENCO=C(U):PRINT@X+Y,CHR$(CO+131)CHR$(CO+130);:RETURN
3180 CO=C(U):PRINT@X+Y,CHR$(CO+140)CHR$(CO+136);:RETURN
5000 E=0:GOSUB7000:PRINT@0,"I, NORMAN HAVE WON AGAIN!!";
5001 SOUND9,3:SOUND19,5:SOUND13,3:SOUND25,5:GOTO6001
6000 E=0:GOSUB7000:PRINT@0,"LOOKS LIKE YOU WIN "B$;:SOUND9,3:SOUND19,5
6001 IFINKEY$<>""THEN6001
6002 PRINT@32,"DO YOU WANT TO PLAY AGAIN ?";
6003 F$=INKEY$:IFF$="" THEN6003
6004 IFF$="Y" THEN80
6005 CLS:END
6006 PRINT@32,"DO YOU WANT TO PLAY AGAIN ?";
7000 PRINT@E,"                               ";:RETURN
8000 IFINKEY$<>""THEN8000
8001 PRINT"DO YOU REALLY WANT TO STOP?";
8002 F$=INKEY$:IFF$=""THEN8002
8003 IFF$="Y"THEN6001
8004 GOTO290
9001 FORX=1TO2500:NEXT:RETURN
9020 CLS:PRINT@40,"backgammon"
9030 PRINT:PRINT"HI, DO YOU WANT INSTRUCTIONS ?"
9040 IFINKEY$<>""THEN9040
9050 A$=INKEY$:IFA$=""THEN9050
9051 IFA$="N"THEN9210
9100 CLS:PRINT
9110 PRINT"THERE ARE JUST A FEW THINGS YOU NEED TO KNOW:":PRINT
9120 PRINT"   1) TO MOVE, TYPE A LETTER IN RESPONSE TO THE";
9130 PRINT" 'FROM-' PROMPT  AND ALSO TO THE 'TO-' PROMPT.":PRINT
9140 PRINT"   2) TO BEAR OFF, TYPE 'Z' IN  RESPONSE TO THE 'TO-'";
9150 PRINT" PROMPT.":PRINT
9160 PRINT"THE REST OF THE RULES ARE THE   SAME AS NORMAL";
9170 PRINT" BACKGAMMON."
9180 PRINT:PRINT"   PRESS ENTER WHEN READY"
9190 IFINKEY$<>""THEN9190
9200 IFINKEY$=""THEN9200
9210 RETURN
