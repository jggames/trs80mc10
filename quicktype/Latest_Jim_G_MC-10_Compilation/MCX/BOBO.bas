10 REM BOBO BY GEORGE TREPAL C 1982
20 REM 1709 KINGS WOODS DR, AUGUSTA, GA.
25 REM  COPYRIGHT (C) CLOAD 1982
26 REM 
27 REM 
30 CLS
40 PRINT:PRINT:PRINT:PRINT:PRINT"              bobo":PRINT:PRINT
50 PRINT"        BY GEORGE TREPAL"
60 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
70 PRINT"PRESS THE SPACE BAR TO CONTINUE"
80 IF INKEY$=" " THEN GOTO 100
90 GOTO 80
100 CLEAR 1000
110 CLS
120 PRINT:PRINT"           COLOR TEST":PRINT
130 PRINT"        ADJUST YOUR SET.":PRINT
140 PRINT"YELLOW ";CHR$(159),"BLUE ";CHR$(175)
150 PRINT"RED ";CHR$(191),"BUFF ";CHR$(207)
160 PRINT"CYAN ";CHR$(223),"MAGENTA ";CHR$(239)
170 PRINT"ORANGE ";CHR$(255):PRINT:PRINT:PRINT
180 PRINT"PRESS THE SPACE BAR TO CONTINUE"
190 IF INKEY$=" " THEN GOTO 210
200 GOTO 190
210 DIM A$(7),  E$(10), EE$(10), K$(3)
220 CLS
230 A$(1)=CHR$(128)+CHR$(241)+CHR$(243)+CHR$(243)+CHR$(243)+CHR$(242)+CHR$(128)
240 A$(2)=CHR$(241)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(242)
250 A$(3)=CHR$(247)+CHR$(207)+CHR$(207)+CHR$(255)+CHR$(207)+CHR$(207)+CHR$(251)
260 A$(4)=CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)
270 A$(5)=CHR$(245)+CHR$(244)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(248)+CHR$(250)
280 A$(6)=CHR$(245)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(250)
290 A$(7)=CHR$(128)+CHR$(252)+CHR$(253)+CHR$(255)+CHR$(254)+CHR$(252)+CHR$(128)
300 M$(1)=A$(5):REM SMILE
310 M$(2)=CHR$(245)+CHR$(241)+CHR$(243)+CHR$(243)+CHR$(243)+CHR$(242)+CHR$(250):REM FROWN
320 M$(3)=CHR$(245)+CHR$(250)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(245)+CHR$(250):REM FULL OPEN
330 M$(4)=CHR$(245)+CHR$(242)+CHR$(179)+CHR$(179)+CHR$(179)+CHR$(241)+CHR$(250):REM TONGUE
340 M$(5)=CHR$(245)+CHR$(254)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(253)+CHR$(250):REM LINE
350 M$(6)=CHR$(245)+CHR$(255)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(255)+CHR$(250)
360 EE$(1)=CHR$(205)+CHR$(207):REM LOWER LEFT
370 EE$(2)=CHR$(206)+CHR$(207):REM LOWER LEFT MIDLE
380 EE$(3)=CHR$(207)+CHR$(205):REM LOWER RIGHT MIDDLE
390 EE$(4)=CHR$(207)+CHR$(206):REM LOWER RIGHT
400 EE$(5)=CHR$(207)+CHR$(203):REM UPPER RIGHT
410 EE$(6)=CHR$(207)+CHR$(199):REM UPPER RIGHT MIDDLE:REM UPPER LEFT MIDDLE
420 EE$(7)=CHR$(203)+CHR$(207):REM UPPER LEFT
430 EE$(8)=CHR$(199)+CHR$(207)
440 EE$(9)=CHR$(252)+CHR$(252):REM HALF BLINK
450 EE$(10)=CHR$(207)+CHR$(128)
460 FOR J=1 TO 10
470 E$(J)=CHR$(247)+EE$(J)+CHR$(255)+EE$(J)+CHR$(251)
480 NEXT J
490 EE$(9)=CHR$(252)+CHR$(252):REM BLINK
500 PRINT@300,"THIS IS BOBO."
510 GOTO 570
520 FORJ=1 TO 7
530 PRINT@((J-1)*32)+289,A$(J);
540 NEXT J
550 PRINT@353,E$(2);
560 RETURN
570 GOSUB 520
580 FOR T=1 TO 1300:NEXT
590 PRINT@330,"BOBO DOESN'T LIKE YOU";
600 GOTO 640
610 REM EYE ROLL*********
620 FOR J=1 TO 8:PRINT@353,E$(J):SOUND(J*10),1:NEXT
630 RETURN
640 GOSUB 620
650 SOUND 90,1
660 PRINT@353,E$(2)
670 GOTO 770
680 REM BEGIN TOUNGUE*******
690 PRINT@417,M$(4);
700 FORT=1 TO 100:NEXT
710 PRINT@449,M$(6);
720 FOR T=1 TO 200:NEXT
730 PRINT@449,A$(6);
740 FORT=1 TO 10
750 PRINT@417,M$(4);
760 RETURN
770 GOSUB 690
780 PRINT@428,"NYAH NYAH NYAH ";:SOUND 100,1
790 GOSUB 690
800 REM KETCHUPBAG***********
810 K$(1)=CHR$(138)+CHR$(138)+CHR$(138)+CHR$(138)+CHR$(138)+CHR$(138)+CHR$(138)
820 K$(2)=CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)
830 K$(3)=CHR$(143)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(143)
840 PRINT@10,"THIS IS A BAG OF"
850 PRINT@42,"CATSUP."
860 SOUND 200,3
870 GOTO 890
880 FOR J=1 TO 3:PRINT@((J-1)*32)+1,K$(J);:NEXT:RETURN
890 GOSUB 880
900 PRINT@74,"ONLY SEVEN BREAKABLE":PRINT@106,"STRINGS KEEP IT FROM";:PRINT@138,"FALLING ON BOBO."
910 PRINT@300,"                    ";
920 PRINT@330,"                     ";
930 FOR T=1 TO 1500:NEXT T
940 GOTO 970
950 :REM READ ABOVE EYE MOVEMENT*****
960 FOR J=4 TO 1 STEP-1:PRINT@353,E$(J+4);:SOUND 65,1:NEXT:RETURN
970 FOR K=1 TO 5:GOSUB 960:FOR T=1 TO 50:NEXT T:NEXT K:PRINT@353,E$(6);
980 PRINT@417,M$(2);
990 PRINT@428,"    OH OH       ":
1000 FORJ=1 TO 20:SOUNDRND(150)+100,1:NEXT J
1010 FOR J=1 TO 15:PRINT@((J-1)*32)+10,"                     ";:NEXT J
1020 PRINT@10,"YOU CAN GET BOBO. 
1030 PRINT@42,"THE MACHINE WILL GO"
1040 PRINT@74,"FROM EVEN TO ODD VERY"
1050 PRINT@106,"FAST.  YOU CAN STOP";
1060 PRINT@138,"IT BY PRESSING THE";
1070 PRINT@170,"SPACE BAR.  IF YOU ";
1080 PRINT@202,"GET A MATCH A STRING";
1090 PRINT@234,"WILL BREAK.  IF YOU";
1100 PRINT@266,"DON'T MATCH, A STRING";
1110 PRINT@298,"THAT IS BROKEN WILL";
1120 PRINT@330,"BE REPAIRED."
1130 PRINT@394,"PRESS THE SPACE BAR"
1140 PRINT@426,"TO PLAY."
1150 PRINT@353,E$(RND(6)+4);
1160 M=M+1:SOUND M,1:IF M=255 THEN M=0
1170 IF RND(100)=50 THEN PRINT@417,M$(RND(2)+1);
1180 IF INKEY$=" " THEN GOTO 1200
1190 GOTO 1180
1200 FOR J=0 TO 15:PRINT@(J*32)+10,"                     ";:NEXT J
1210 PRINT@42,"TO ADD INTEREST TO";
1220 PRINT@74,"THE GAME AND TO ";
1230 PRINT@106,"KEEP YOU FROM BEING";
1240 PRINT@138,"TRIGGER HAPPY IF YOU";
1250 PRINT@170,"PRESS THE SPACE BAR";
1260 PRINT@202,"BEFORE THE READY";
1270 PRINT@234,"SIGN IS ON YOU MAY";
1280 PRINT@266,"LOSE A POINT."
1290 PRINT@362,"PRESS THE SPACE BAR"
1300 PRINT@394,"TO PLAY."
1310 PRINT@417,M$(1);
1320 PRINT@353,E$(RND(8));
1330 FOR T=1 TO 20:NEXT T
1340 IF INKEY$=" " THEN GOTO 1360
1350 GOTO 1310
1360 FORJ=1 TO 15:PRINT@((J-1)*32)+10,"                      ";:NEXT J
1370 PRINT@42,"SELECT YOUR SKILL";
1380 PRINT@74,"LEVEL.  THE LOWER";
1390 PRINT@106,"NUMBERS NEED HIGHER";
1400 PRINT@138,"SKILL";:INPUT TI
1410 FORJ=1 TO 4:PRINT@(J*32)+10,"                     ";:NEXT J
1420 FORJ=1 TO 7:A(J)=138:NEXT J
1430 REM START PLAY LOOP******
1440 PRINT@138,"YOU HAVE TO MATCH"
1450 MS=RND(2):IF MS=1 THEN PRINT@177,"ODD ";:GOTO1460
1451 PRINT@177,"EVEN";
1460 RN=RND(2)
1470 IF RN=1 THEN PRINT@433,"ODD ";:GOTO1480
1471 PRINT@433,"EVEN";
1480 FOR T=1 TO TI:NEXT T
1490 PRINT@162,"ready";
1500 IF INKEY$=CHR$(32) THEN KO=KO+1:GOTO4000
1510 GOTO 1460
1520 REM MATCH ROUTINE*****
1530 FLAG=1
1540 PRINT@162,"     ";
1550 PRINT@330,"YOU MADE THE MATCH";
1560 FOR J=1 TO 7
1570 IF (A(J)=138) AND (FLAG=1) THEN A(J)=143:FLAG=0
1580 NEXT J
1590 FOR J=1 TO 7
1600 READ X
1610 PRINT@X,CHR$(A(J));
1620 NEXT J
1630 DATA 1,7,2,6,3,5,4
1640 RESTORE
1650 RB=0
1660 PRINT@353,E$(RND(10));
1670 MP=RND(4)+1:IF MP=4 THEN 1670
1680 PRINT@417,M$(MP);
1690 C=0:FOR L=1 TO 7:IF A(L)=143 THEN C=C+1
1700 NEXT L:IF C=7 THEN GOTO 1950
1710 FORZZ=1TO2000:NEXT
1720 PRINT@330,"                  ";
1730 GOTO 1450
1740 REM NO MATCH ROUTINE******
1750 PRINT@162,"     ";
1760 PRINT@330,"   YOU BLEW IT     ";
1770 FLAG=1
1780 FOR J=7 TO 1 STEP-1
1790 IF (FLAG=1) AND (A(J)=143) THEN A(J)=138:FLAG=0
1800 NEXT J
1810 FOR J=1 TO 7
1820 READ X
1830 PRINT@X,CHR$(A(J));
1840 NEXT J
1850 RESTORE
1860 PRINT@353,E$(RND(10));
1870 PRINT@417,M$(1);
1880 RB=RB+1:IF RB>2 THEN RB=2
1890 IF RB=2 THEN GOSUB 620:GOSUB 680
1900 IFC>1 THEN C=C-1
1910 FOR J=10 TO 1 STEP-1:SOUNDJ*20,1:NEXT J
1920 PRINT@330,"                ";
1930 GOTO 1450
1940 REM DROP THE KETCHUP******
1950 G$="       "
1960 FOR J=1 TO 8
1970 PRINT@(J*32)+1,G$;
1980 PRINT@(J*32)+33,K$(2);
1990 PRINT@(J*32)+65,K$(3);
2000 NEXT J
2010 FOR J=10 TO 15
2020 PRINT@(J*32)+1,CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(191);
2030 NEXT J
2040 PRINT@353,CHR$(191)+EE$(4)+CHR$(191)+EE$(1)+CHR$(191);
2050 FOR K=1 TO 6
2060 FORZZ=1TO1300:NEXT
2070 NEXT K
2080 CLS
2090 PRINT@0," ":PRINT:PRINT"   YOU FINALLY WON.  IT TOOK    ";KO;"TRIES AT A SKILL LEVEL OF "
2100 PRINTTI;"TO DO IT.":PRINT
2110 KO=0:PRINT"   DO YOU WANT TO PLAY THIS "
2120 INPUT" GAME AGAIN";Z$
2130 IF ASC(Z$)=78 THEN END
2140 IF ASC(Z$)=89 THEN 2160
2150 CLS:PRINT@173,"WHAT ?":GOTO 2110
2160 PRINT:PRINT"   WHAT SKILL LEVEL DO YOU WANT"
2170 INPUT"THIS TIME";TI
2180 CLS
2190 GOSUB 520
2200 GOSUB 880
2210 FOR J=1 TO 7:A(J)=138:NEXT J
2220 GOTO 1440
4000 IF RN=MS THEN 1530
4010 GOTO 1750
