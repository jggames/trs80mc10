0 CLS:PRINT"THIS GAME REQUIRES +20K RAM":CLEAR255:DIMC1,CC,M$,A(45,7):C1=RND(-(PEEK(9)*256+PEEK(10))):GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 CLS:PRINTTAB(2)"YOUR STRANGE FRIEND DR.TEMPUS HAS CALLED YOU TO HIS HOUSE."
11 PRINTTAB(7)"'I HAVE CREATED A":PRINT:PRINTTAB(9)"*TIME MACHINE*":PRINT
12 PRINT"  I CALL IT TM & IT SEEMS TO BE TEMPERMENTAL--ONLY GOES TO SEVENTIME ZONES, BUT JUST THINK! WE  ";
13 PRINT"COULD RETURN WITH A DINOSAUR EGGOR THE HOLY GRAIL..AND WHO KNOWSWHAT ELSE WE MAY FIND.":PRINTTAB(11)"LET'S GO!!'"
14 FORRO=1TO45:FORC=1TO7:READA(RO,C):NEXTC:NEXTRO:FORX=1TO8:READT$(X):NEXTX:FORX=1TO7:READE$(X):NEXTX
16 PRINT@484,;:INPUT"HIT <ENTER> WHEN READY";X$
18 H=30:N$="NOTHING HAPPENS":RO=0
20 GOTO770
22 FORO=1TO1500:NEXTO:RETURN
24 FORO=1TO2500:NEXTO
26 H=H-1:IFH<1THENPRINT"HUNGER HAS TAKEN YOU BOTH":GOTO348
28 CLS:PRINT:PRINT"YOU ARE ";:GOSUB88
30 IFH<7THENPRINT"DR.T SAYS'I FEEL VERY HUNGRY'"
32 IFA(RO,6)>20THENU=A(RO,6)-20:GOSUB22:PRINT:PRINT"HORRORS!  ";E$(U)
34 IFA(RO,6)=2ANDSW=0THENPRINT"ON THE GROUND LIES A SWORD"
36 PRINT:INPUT"WHAT WILL YOU DO";A$:IFA$=""THEN36
38 B$=LEFT$(A$,3):N=1:M=LEN(A$)
40 IFA$="HELP"THENPRINT"DON'T ASK ME,ASK DR.T":GOTO36
42 IFA$="LOOK"THEN524
44 IFB$="FIG"THEN264
46 IFB$="QUI"THEN348
48 IFB$="INV"THENGOSUB736:GOTO36
50 IFA$="CAROUSE"THENPRINT"YAHOO!!":GOTO36
52 IFA$="SING"THEN62
54 IFMID$(A$,N,1)=" "THEN4000
56 IFN<MTHENN=N+1:GOTO54
58 PRINT"TWO WORDS PLEASE":GOTO36
60 IFB$="GO "ORB$="WAL"ORB$="RUN"THEN382
62 IFB$="SIN"THEN724
64 IFB$="GET"THEN546
66 IFB$="DRO"THEN600
68 IFB$="EAT"THEN428
70 IFB$="ASK"THEN448
72 IFB$="UNL"THEN496
74 IFB$="DRI"THEN510
76 IFB$="WHI"THEN520
78 IFB$="KNO"THEN518
80 IFB$="OPE"THEN478
82 IFB$="SHO"THEN730
84 IFB$="LOO"THEN536
86 PRINT"I DON'T UNDERSTAND ";A$:GOTO36
88 IFRO<21THENONROGOSUB92,94,112,114,118,120,126,128,130,132,144,150,156,158,160,164,166,168,170,172:RETURN
89 ONRO-20GOSUB178,180,182,184,188,190,192,198,200,202,206,212,216,218,222,226,228,230,236,238,240,250,252,254,256:RETURN
92 PRINT"BESIDE TM ON A GRASSY   ISLAND WITH SWAMP WEST & SOUTH":RETURN
94 PRINT"ON A GRASSY ISLAND WITH SWAMP TO NORTH AND  WEST":GOSUB22
96 PRINT"BEFORE YOU LOOMS A TYRANNOSAURUSREX":TI=0
98 TI=TI+1:IFTI>3600THEN124
99 INPUT"WHAT WILL YOU DO";A$:B$=LEFT$(A$,5):IFA$="RUN"THENPRINT"RUN WHERE?":GOTO98
100 IFA$="FIGHT"THENPRINT"YOU CANNOT FIGHT THE KING OF    TYRANTS":GOTO98
102 IFB$="RUN S"THENRO=1:GOTO26
104 IFB$="RUN E"THENRO=3:GOTO26
106 IFLEFT$(A$,2)="GO"THENPRINT"DR.T YELLS'FASTER...FASTER!!":GOTO98
108 IFB$="RUN W"ORB$="RUN N"THENRO=7:GOTO26
110 PRINT"WITH A DINOSAUR CHASING YOU,YOU WANT TO ";A$"?":GOTO98
112 PRINT"ON A GRASSY ISLAND":PRINT"NORTH IS SWAMP;EAST A VOLCANO":RETURN
114 PRINT"ON A GRASSY ISLAND":PRINT"SOUTH IS SWAMP;  EAST A CLIFF.  YOU SEE A BUSH WITH BERRIES AND A MAMA TRICERATOPS ";
115 IFE=0THENPRINT"WITH AN EGG"
116 RETURN
118 PRINT"ON A VOLCANO":PRINT"SWAMP-NORTH&EAST;WEST-THE PLAIN SOUTH-A DARK CAVE":RETURN
120 PRINT"IN A DARK CAVE"
122 GOSUB22:SOUND1,20:PRINT"FROM BELOW EMERGES A FOUL DINO- SAUR"
124 SOUND1,20:PRINT"HE GOT YOU AND YOU END YOUR DAYSAS A SNACK OF THE MESOZOIC AGE":GOTO348
126 PRINT"SLOSHING IN THE SWAMP":GOTO122
128 PRINT"ON PALATIN HILL NEXT TO TM":PRINT"NORTH IS THE ROMAN FORUM, SOUTH THE APPIAN WAY, WEST A TEMPLE.  ";
129 PRINT"EAST YOU SEE CLOSED BUILDINGS & MEAN STREETS.":RETURN
130 PRINT"IN THE ROMAN FORUM.":PRINT"WEST & NORTH ARE MEAN STREETS.  EAST IS THE COLOSSEUM; SOUTH IS PALATIN HILL."
131 A(10,6)=0:RETURN
132 PRINT"IN THE COLOSSEUM":PRINT"IT IS FULL OF PEOPLE":IFA(10,6)=0THENGOSUB22
133 PRINT"YOU ARE CAUGHT BY A GUARD AND   PRESENTED TO CAESAR WHO CRIES:":GOTO136
134 PRINT"RAINING COINS UPON YOUR HEAD":PRINT"THE ONLY EXIT YOU SEE IS WEST":RETURN
136 O=RND(4)
138 IFO=1THENPRINT"'LET THEM FIGHT THE GLADIATORS'":GOTO358
140 IFO=2THENPRINT"'THROW THEM TO THE LIONS'":GOTO370
142 PRINT"'LET'S PARTY! EVERYONE TO MY PALACE'":GOSUB22:PRINT"YOU ARE SURROUNDED BY CHEERING  PEOPLE AND LED TO THE PALACE"
143 RO=15:SL=0:GOTO24
144 PRINT"LOST IN A MAZE OF DIRTY STREETS":GOSUB22
146 PRINT"A CROWD OF RUFFIANS APPEAR AND  FEARFUL OF YOUR STRANGE ATTIRE  STONE YOU"
148 PRINT"THEY GOT YOU AND YOU ARE LOST INTHE MISTS OF TIME":GOTO348
150 PRINT"IN THE TEMPLE OF THE    VESTAL VIRGINS.":PRINT"THE TIBER FLOWS PAST WEST&SOUTH.NORTH ARE MEAN STREETS"
151 PRINT"EAST PALATINE HILL."
152 PRINT"YOU SEE AN ALTAR";:IFBX=0THENPRINT" ON WHICH RESTS A BOX"
154 RETURN
156 PRINT"ON THE APPIAN WAY":PRINT"IT WINDS FROM NORTH THEN EAST":RETURN
158 KL$="IN A MAZE OF CATACOMBS":ML$="MOLDY LOAF OF BREAD":DL$="THE SOUND OF HYMNS SEEMS TO ECHOAROUND YOU"
159 TL$="A SACRED SCROLL":GOTO646
160 PRINT"IN CAESARS PALACE.":IFSL=0THENPRINT"EVERYONE IS EATING, DRINKING,   CAROUSING, SINGING.":GOTO162
161 PRINT"EVERYONE IS ASLEEP"
162 PRINT"THE DOOR NORTH HAS BEEN BARRED; SOUTH YOU SEE THE APPIAN WAY;   WEST IS PALATIN HILL":RETURN
164 PRINT"BESIDE TM IN A CLEARING IN DARK WOODS. A LIGHT GLOWS TO THE NORTH.":A(20,7)=2:RETURN
166 PRINT"AT CAMELOT":PRINT"WEST IS A HALL; SOUTH A PATH":RETURN
168 PRINT"IN A HALL GOING NORTH & EAST":RETURN
170 PRINT"IN A HALL GOING SOUTH & EAST":RETURN
172 PRINT"IN A ROOM WITH A LARGE  ROUND TABLE ";:IFBO=0THENPRINT"ON WHICH IS A SMALL JEWELLED CASKET":GOTO174
173 PRINT
174 IFA(20,7)=2THENPRINT"ON THE TABLE IS A LOAF OF BREAD"
176 PRINT"NORTH IS A PATH THROUGH DARK    WOODS, WEST A HALL":RETURN
178 PRINT"ON N/S PATH THROUGH THE WOODS":RETURN
180 PRINT"ON N/S PATH THROUGH DEEPWOODS. NORTH YOU SEE A BEAUTIFULTEMPLE.":RETURN
182 PRINT"IN DEEP DARK FOREST":GOSUB22:PRINT"A BAND OF BANDITS SURROUND YOU":GOTO148
184 PRINT"IN A BEAUTIFUL TEMPLE.":IFGR=0THENPRINT"ON THE ALTAR STANDS":GOSUB22:PRINT"...THE HOLY GRAIL"
186 RETURN
188 PRINT"BESIDE TM IN A DESERT   SOUTH&WEST IS UNCLIMBABLE CLIFF":A(27,7)=2:RETURN
190 PRINT"IN A DESERT":PRINT"UNCLIMBABLE CLIFF WEST & NORTH; EAST IS A WALLED SETTLEMENT":A(27,6)=21:GD=0:RETURN
192 PRINT"IN A SPANISH SETTLEMENT":IFA(27,7)=2THENPRINT"THERE'S A TABLE IN ONE CORNER   WITH BREAD ON IT."
194 IFGD=0THENPRINT"AND IN THE OPPOSITE CORNER":PRINT"     ***GOLD***"
196 RETURN
198 KL$="SURROUNDED BY DESERT":ML$="CACTUS":DL$="NOTHING BUT TUMBLEWEED FOR MILES":TL$="A TREASURE CHEST OF GOLD":GOTO646
200 PRINT"AT 221B BAKER ST."
201 PRINT"SHERLOCK HOLMES STUDIES YOU&TM  LACONICALLY AND REMARKS THAT YOUCAN GO ANY DIRECTION IN THE CAB AT THE DOOR":RETURN
202 PRINT"IN REGENTS PARK":PRINT"THERE IS A VENDOR SELLING BREAD AND WINE.";:IFBG=0THENPRINT" ON A BENCH  YOU SEE A BAG."
204 PRINT"THE CAB WAITS":A(30,7)=2:RETURN
206 PRINT"AT PROFESSOR MORIARITY'S HOUSE."
208 IFA(31,6)=0THENPRINT"IT IS CLOSED UP TIGHT"
210 RETURN
212 PRINT"AT SCOTLAND YARD.":IFA(33,6)>0THENGOSUB4020
214 RETURN
216 PRINT"AT A LARGE HOUSE":RETURN
218 PRINT"ON AN AVENUE THAT DEAD- ENDS WEST AT THE POLICE STATION.NORTH AND SOUTH ARE BUILDINGS,  ";
219 PRINT"EAST WHAT SEEMS TO BE AN ENDLESSMAZE OF STREETS.TM WAITS FOR YOU":A(35,7)=2
220 GOSUB22:PRINT"A MAN SIDLES UP TO YOU AND MUT- TERS'LOOKING FOR HOOCH?'AND NODSHIS HEAD AT A DOOR NORTH":RETURN
222 PRINT"IN A DARK & GLOOMY SPEAKEASY. THE ONLY WAY OUT IS THE   DOOR TO THE SOUTH."
223 IFA(RO,7)=2THENGOSUB22:PRINT"A WAITER BRINGS BREAD AND FOUL  SMELLING BREW"
224 RETURN
226 PRINT"IN A POLICE STATION":PRINT"SERGEANT O'HARA GIVES YOU A LEC-TURE ON THE EVILS OF DEMON RUM. THE ONLY EXIT IS EAST"
227 RETURN
228 KL$="IN A MAZE OF STREETS.":ML$="CRUST OF BREAD ON THE STREET":DL$="RAUCOUS NOISE EMINATES FROM A   DINGY DOOR"
229 TL$="A DIAMOND STICKPIN":GOTO646
230 PRINT"IN A LARGE AUDITORIUM   WITH TM IN CORNER.":PRINT"THERE ARE DOORS NORTH,WEST&SOUTHEAST IS A THICK GLASS AND ";
231 PRINT"LEAD  DOOR THAT SHOWS AN ALIEN & DESO-LATE LAND WHERE NOTHING SEEMS TOBE ALIVE"
232 O=RND(3):IFO=1THENA(39,6)=27:GOTO234
233 GOSUB4200
234 A(41,6)=27:RETURN
236 PRINT"IN AN L-SHAPED ROOM WITHDOORS S/E/W.  A NORTH DOOR IS   LABELLED danger-radiation zone":RETURN
238 PRINT"IN LONG E/W HALL":RETURN
240 PRINT"ON A ROCKET SHIP WITH   PORTALS EAST AND SOUTH.":IFA(41,7)>0THENPRINT"AGAINST THE WALL ARE FOOD TUBES":GOSUB22"
242 IFA(41,6)=27THENPRINT"THE ROCKET SUDDENLY SHAKES AND  ROARS AS IT TAKES OFF...WITH YOUIN IT!"
244 IFA(41,6)=1THENPRINT"THROUGH THE WINDOWS YOU CAN SEE STARS AND PLANETS AND GALAXIES"
246 IFA(41,6)=0THENPRINT"THE ROCKET HAS JUST RETURNED TO EARTH"
248 RETURN
250 PRINT"IN A LABORATORY":PRINT"A HEAVY DOOR TO WEST IS LABELED     'danger-radiation zone'"
251 PRINT"NORTH AND EAST ARE REGULAR DOORS":RETURN
252 PRINT"IN A HALL":PRINT"THERE ARE DOORS WEST AND NORTH":RETURN
254 PRINT"IN AN UNINHABITABLE      radiation zone":GOSUB22:PRINT"YOU FEEL YOURSELVES BECOMING    ";
255 PRINT"SICK AND YOU SINK TO THE GROUND":GOSUB22:PRINT"YOUR BODIES ARE NOW LOST IN THE FUTURE":GOTO348
256 PRINT"IN THE CAB.":PRINT"THE CABBIE SAYS YOU NEED A TOUR OF LONDON":GOSUB22:IFRND(2)=1THENPRINT"'HERE'S HYDE PARK":GOTO258
257 PRINT"'HERE'S BUCKINGHAM PALACE"
258 GOSUB22:IFRND(2)=1THENPRINT"THERE'S THE TOWER OF LONDON":GOTO260
259 PRINT"THERE'S BEDLAM..ER..BETHLEHELM  LUNATIC ASYLUM"
260 GOSUB22:IFRND(2)=1THENPRINT"AND THE BRITISH MUSEUM'":GOTO262
261 PRINT"AND MADAME TUSSAUD'S'"
262 GOSUB22:PRINT"AND HE BRINGS YOU BACK TO BAKER STREET":RO=29:GOTO24
264 IFA(RO,6)<15THENPRINT"THERE'S NOTHING TO FIGHT HERE":GOTO36
266 IFSW=1THENJ=RND(7):GOTO270
268 J=RND(4)
270 IFJ=1THENPRINT"HE GOT YOU":GOTO286
272 IFJ=2THENPRINT"HE STRIKES A TELLING BLOW":GOSUB22:GOTO266
274 IFJ=3THENPRINT"YOU HAVE WOUNDED HIM":GOSUB22:GOTO266
276 IFRO=33THENPRINT"DRACULA TURNED INTO A BAT AND   FLEW AWAY":A(33,6)=0:GOTO24
278 IFRO=31THEN4060
280 IFRO=41THENPRINT"YOU GOT HIM":A(RO,6)=1:GOTO24
282 IFRO=10THENPRINT"YOU BESTED HIM.MAGNANIMOUSLY YOULET HIM GO.":A(10,6)=3:GOTO24
284 PRINT"YOU GOT HIM":A(RO,6)=0:GOTO24
286 IFRO=33THENPRINT"NOW YOU ARE A VAMPIRE TOO":GOTO348
288 IFRO<>22THEN348
289 PRINT"THE SORCEROR HAS TURNED YOU BOTHINTO FROGS...":SOUND100,2:SOUND30,3:SOUND150,1:PRINT"I HOPE YOU LIKE FLIES."
290 GOTO348
300 DATA2,7,4,7,1,,,7,1,3,7,,,,7,4,5,2,,,,3,7,,1,,,1,7,6,7,3,,,,,,,,,,,,,,,,,,9,13,11,12,1,2,,11,8,10,11,,,
308 DATA0,,,9,,,,,,,,,,,11,,8,,,3,,8,,14,,,,,14,14,14,13,,4,,,13,,8,,,2,17,23,23,23,1,,2,,16,,18,,2,
316 DATA19,,17,,,,,,18,20,,,,,21,,,19,,5,2,22,20,23,23,,22,,24,21,23,23,,23,,,,,,,,,,22,,,,6,,26,,28,,1,,,,25,27,,,,
324 DATA0,,,26,,21,2,28,28,28,25,,,,30,31,32,33,1,,,45,29,32,33,,3,2,29,45,32,33,,24,,30,31,45,29,,,
330 DATA30,31,29,45,,25,,,,37,36,1,26,,0,34,,,,1,2,,,34,,,,,37,37,37,34,,,,39,43,44,39,1,,,44,38,38,40,,,
338 DATA0,,39,41,,,,,42,40,,,27,2,41,,43,44,,9,,38,,,42,,,,,,,,,,,30,30,30,30,,,,DIAMOND,SWORD,COINS,SCROLL,CROSS
339 DATAHOLY GRAIL,GOLD,VACCINE,A CONQUISTADOR,A DRAGON,A SORCERER,PROF MORIARITY,DRACULA,A GANGSTER,A ROBOT
348 GOSUB22:INPUT"ANOTHER GAME? (Y/N)";A$:IFA$="Y"THENRUN
350 END
358 PRINT"THE GLADIATORS APPEAR"
360 INPUT"WHAT WILL YOU DO";A$
362 IFA$="STAB"ORA$="FIGHT"ORA$="HIT"THEN266
364 PRINT"YOU CAN'T ";A$:GOTO360
366 M$="YOU HAVE BESTED THEM. MAGNANIMOUSLY YOU SPARE THEM.":GOSUB1
368 M$="THE CROWD CHEERS AND COINS RAIN UPON YOUR HEAD. THE ONLY EXIT IS WEST":GOSUB1:GOTO36
370 PRINT"THE LIONS APPEAR":SOUND3,20
372 IFSW=1THENT=RND(3)
374 IFSW=0THENT=RND(9)
376 GOSUB22:IFT=2THENPRINT"YOU HAVE SLAIN THE LIONS.":A(10,6)=3:GOTO24
380 GOTO148
382 IFA(RO,6)>19THENPRINT"YOUR FOE WILL NOT LET YOU GO":GOTO24
384 IFC$="HOU"ANDRO=33THEN422
386 IFY<1THENA(33,6)=25
388 IFRO=15AND SL=0THENPRINT"CAESAR SHOUTS--YOU CAN'T GO. EAT-DRINK-SING-CAROUSE":GOTO24
390 IFRO=4ANDE=1THEN590
392 IFRO=41ANDA(RO,6)>0THENPRINT"YOU DON'T WANT TO LEAVE THE ROC-KET WHILE IT'S IN OUTER SPACE":GOTO24
394 IFC$="TM"ANDA(RO,5)=0THENPRINT"IT ISN'T HERE":GOTO36
396 IFC$="TM"GOTO770
398 D$=LEFT$(C$,1)
400 IFD$="N"ANDA(RO,1)=0THENPRINT"YOU CAN'T GO THAT WAY":GOTO36
402 IFD$="S"ANDA(RO,2)=0THEN422
404 IFD$="E"ANDA(RO,3)=0THENPRINT"NO EAST EXIT":GOTO36
406 IFD$="W"ANDA(RO,4)=0THEN422
408 IFD$="N"ANDRO=22THEN424
410 IFD$="N"THENRO=A(RO,1):GOTO26
412 IFD$="S"THENRO=A(RO,2):GOTO26
414 IFD$="E"THENRO=A(RO,3):GOTO26
416 IFD$="W"THEN RO=A(RO,4):GOTO26
418 IFC$="TEM"ANDRO=8THENRO=12:GOTO26
420 IFC$="TEM"ANDRO=22THEN424
422 PRINT"YOU CAN'T "A$:GOTO36
424 IFL=0THENPRINT"THE DOOR IS LOCKED":GOTO36
426 RO=24:GOTO24
428 IFC$="BER"ANDA(RO,7)=1THEN444
430 IFC$="BER"ANDB=1THEN444
432 IFC$="BRE"ANDA(RO,7)=2THENPRINT"RATHER TASTY AND VERY FILLING":GOTO446
434 IFC$="BRE"ANDBR>0THENPRINT"RATHER TASTY":BR=BR-1:GOTO446
436 IFC$="BRE"THENPRINT"YOU DON'T HAVE ANY":GOTO36
438 IFC$="FOO"ANDA(RO,7)>0THEN444
440 IFC$="FOO"ANDF=1THEN444
442 PRINT"YUCK":GOTO36
444 PRINT"MMM,NOT BAD"
446 H=H+15:GOTO36
448 IFC$="DR."THEN456
450 IFRO=34ANDC$="FOR"ORC$="JOE"THEN454
452 PRINT"WHO?":GOTO36
454 PRINT"THE DOOR OPENS ON A DINGY BAR":RO=35:GOTO24
456 IFRO=15THENFORZZ=1TO5:SOUND200,1:SOUNDZZ,1::NEXT:GOTO36
458 IFRO=22THENPRINT"ONE MUST GO WHERE ALL ROADS LEAD":GOTO36
460 IFRO=5THENPRINT"DIAMOND'RE A DROP IN THE BUCKET":GOTO36
462 IFRO=34THENSOUND1,5:GOTO36
464 IFRO=33ANDY<1THENPRINT"ALL MUST CROSS HIS PATH SOMETIME":GOTO36
466 IFRO=41OR RO=35 ORRO=43THENPRINT"SEEK AND YE SHALL FIND":GOTO36
468 O=RND(3)
470 IFO=1THENPRINT"COMMON SENSE IS NOT SO COMMON"
472 IFO=2THENPRINT"NOBODY CAN GIVE YOU WISER ADVICETHAN YOURSELF"
474 IFO=3ANDRO>7THENPRINT"HALF A LOAF IS BETTER THAN NONE":GOTO36
475 IFO=3THENPRINT"BEWARE THE DARK,DAMP,OR MEAN"
476 GOTO36
478 IFC$="BOX"THEN490
480 IFC$="CAS"THEN492
482 IFC$="DOO"ANDRO=22ANDL=0THENPRINT"IT IS LOCKED":GOTO36
484 IFC$="DOO"ANDRO=22ANDL=1THENPRINT"IT IS OPEN":GOTO36
486 IFC$="BAG"ANDRO=30THENPRINT"COINS FELL OUT":GOTO36
488 PRINT"IT CANNOT BE OPENED":GOTO36
490 IFRO=12ORBX=1THENPRINT"THE BOX IS OPEN";:IFKY=0THENPRINT", AND INSIDE YOU SEE A KEY"
492 IFRO=20ORBO=1THENPRINT"THE CASKET IS OPEN ";:IFCR=0THENPRINT"AND INSIDE   YOU SEE A SMALL GOLD CROSS"
494 GOTO36
496 IFKY<>1THEN504
498 IFRO=22ANDL=1THEN508
499 INPUT"WITH WHAT";A$
500 IFRO=22ANDA$="KEY"THEN506
502 IFRO=22ANDA$="WITH KEY"THEN506
504 PRINT"YOU DO NOT HAVE A KEY FOR THAT  DOOR":GOTO36
506 SOUND150,1:L=1
508 PRINT"THE DOOR IS UNLOCKED":GOTO36
510 IFC$="WIN"THENPRINT"GAGGG, YOU ARE ABOUT TO BE SICK":SOUND55,10:H=H-6:GOTO36
512 IFC$="RUM"ORC$="BRE"THENPRINT"BARF, WHAT FOUL ROT THEY DRINK":H=H-5:GOTO36
514 IFC$="WAT"ANDRO>7THENPRINT"MMM, MOST REFRESHING":H=H+5:GOTO36
516 PRINT"YOU WANT TO DRINK WHAT???":GOTO36
518 IFRO=34ANDC$="THR"THEN454
520 IFRO=34ANDC$="LOW"ORC$="JOE"THEN454
522 PRINTN$:GOTO36
524 IFRO=35ANDA(35,6)=1THENPRINT"HEY!!THAT FLAPPER JUST DROPPED ADIAMOND ON OUR TABLE!":GOTO36
526 IFRO=42THENPRINT"ALONG THE WALLS ARE SHELVES FILLED WITH VARIOUS CONTAINERS":GOTO24
528 IFRO=41ANDA(41,6)>0THENPRINT"THE ROBOT HAS A DIAMOND":GOTO36
530 IFRO=3THENPRINT"PTERADACTYLS GRACE THE SKY":GOTO24
532 IFRO=9THENPRINT"A GROUP OF SENATORS CONVERSE IN THE CORNER":GOTO24
534 GOTO26
536 IFRO=42THENPRINT"DR.T EXCLAIMS'WE'RE RICH--A VAC-CINE FOR THE COMMON COLD!":GOTO36
538 IFC$="DOO"ANDRO<38THENPRINT"IT'S A HEAVY WOODEN DOOR ";:GOTO4100
540 IFC$="VOL"ANDRO<8THENPRINT"IT EMITS WHIFFS OF SMOKE":GOTO36
542 IFC$="TEM"THENPRINT"BEAUTIFUL WHITE MARBLE GLEAMS INTHE SUNLIGHT":GOTO36
544 PRINT"YOU SEE NOTHING SPECIAL":GOTO36
546 IFC$="BER"ANDRO=4THENB=1:PRINT"YOU HAVE SOME BERRIES":GOTO24
548 IFC$="BRE"ANDA(RO,7)=2THENBR=BR+1:A(RO,7)=0:W$="BREAD":GOTO588
550 IFRO=41ANDC$="TUB"ORC$="FOO"THENW$="FOOD":F=1:A(RO,7)=0:GOTO588
552 IFC$="DIA"ANDA(RO,6)=1THEND=D+1:W$=T$(1):A(RO,6)=0:GOTO588
554 IFC$="EGG"ANDRO=4THENE=1:PRINT"YOU GOT THE EGG BUT MAMA IS VERYUPSET":GOTO36
556 IFC$="SWO"ANDA(RO,6)=2THENSW=1:W$=T$(2):GOTO588
558 IFC$="BOX"ANDRO=12THENW$="BOX":BX=1:GOTO588
560 IFC$="CAS"ANDRO=20THENBO=1:W$="CASKET":GOTO588
562 IFC$="KEY"ANDRO=12THENKY=1:Z=Z+1:W$="KEY":GOTO588
564 IFC$="KEY"ANDBX=1THENKY=1:Z=Z+1:W$="KEY":GOTO588
566 IFC$="VAC"ANDRO=42THENV=1:W$=T$(8):GOTO588
568 IFC$="BAG"ANDRO=30ANDA(30,6)=3THENPRINT"COINS FELL OUT":BG=1:GOTO36
570 IFC$="BAG"THENPRINT"IT IS EMPTY":GOTO36
572 IFC$="COI"ANDA(RO,6)=3THENZ=Z+1:W$=T$(3):GOTO588
574 IFC$="GOL"ANDRO=27ANDGD=0THEN4120
576 IFC$="GOL"ANDRO=27ANDGD=1THENPRINT"THERE'S NO TIME. MORE MEN ARE   COMING":GOTO36
578 IFC$="CRO"ANDRO=20THENCR=1:Z=Z+1:W$=T$(5):GOTO588
580 IFC$="CRO"ANDBO=1THENCR=1:Z=Z+1:W$=T$(5):GOTO588
582 IFC$="WIN"THENW$="WINE":GOTO588
584 IFC$="GRA"ORC$="HOL"ANDRO=24THENGR=1:W$=T$(6):GOTO588
586 PRINT"YOU CANNOT "A$:GOTO36
588 PRINT"YOU HAVE THE "W$:GOTO24
590 IFB$="RUN"THEN398
592 O=RND(3)
594 IFO=1THENPRINT"DR.T YELLS FASTER":GOTO36
596 IFO=2THENPRINT"MAMA IS ALMOST ON YOU. YOU DROP THE EGG":E=0:GOTO36
598 IFO=3THENPRINT"SHE'S GAINING!":GOTO36
600 IFRO=5ANDC$="BER"ANDB=1THENW$="BERRIES":B=0:GOTO632
602 IFRO=5ANDC$="EGG"ANDE=1THENW$="EGG":E=0:GOTO632
604 IFC$="BER"ANDB=1THEN PRINT"SQUISH":B=0:GOTO36
606 IFC$="SWO"ANDSW=1THEN642
608 IFC$="EGG"ANDE=1THENPRINT"YOU HAVE DROPPED THE EGG AND IT CRACKED.":E=0:GOTO36
610 IFC$="DIA"ANDD>0THENW$=T$(1):D=0:GOTO632
612 IFC$="VAC"THENV=0:PRINT"IT SPILLS ACROSS THE GROUND":GOTO36
614 IFC$="BOX"ANDBX=1THENW$="BOX":BX=2:GOTO632
616 IFC$="CAS"ANDBO=1THENW$="CASKET":BO=2:GOTO632
618 IFC$="SCR"ANDSC=1THENW$=T$(4):SC=2:GOTO632
620 IFC$="GOL"ANDG>0THENW$=T$(7):G=0:GOTO632
622 IFC$="KEY"ANDKY=1THENW$="KEY":KY=2:Z=Z-1:GOTO632
624 IFC$="CRO"ANDCR=1THENW$=T$(5):CR=2:Z=Z-1:GOTO632
626 IFC$="GRA"ORC$="HOL"ANDGR=1THENPRINT"YOU DON'T WANT TO DROP THE ";T$(6):GOTO36
628 IFC$="BAG"ANDBG=1THENW$="BAG":BG=0:GOTO632
630 PRINT"YOU DON'T HAVE IT":GOTO36
632 PRINT"YOU HAVE DROPPED THE "W$
634 IFRO=5THENPRINT"INTO THE VOLCANO.IT ERUPTED BUT ALL THAT CAME OUT WAS A DIAMOND":A(5,6)=1:GOTO36
636 IFRO<8THENPRINT" AND IT FALLS INTO A HOLE":GOTO24
638 IFRO>37THENPRINT" AND A ROBOT GRABS IT AND DISAPPEARS":GOTO24
640 PRINT"A STRANGE MAN CRIES OUT IN GLEE,GRABS IT AND RUNS OFF":GOTO24
642 SW=0:IFRO=5THEN632
643 IFA(RO,6)=0THENA(RO,6)=2:PRINT"YOU DROPPED THE SWORD":GOTO24
644 GOTO632
646 K=100
648 CLS:H=H-1:IFH=0THENPRINT@225,"YOU HAVE DIED OF HUNGER":GOTO348
650 IFK=99THENRO=A(RO,4):GOTO26
652 PRINT"YOU ARE ";KL$
654 IFK=303ORK=6THENPRINT"YOU SEE A ";ML$
656 IFK=506ANDRO=14ANDSC=1THEN662
658 IFK=506THENPRINT"YOU SEE "TL$
660 IFK=107ORK=204ORK=-103THENPRINTDL$
662 IFH<7THENPRINT"DR.T GROANS'I FEEL HUNGRY'"
664 GOTO668
666 GOSUB22
668 INPUT"WHAT WILL YOU DO";A$:B$=LEFT$(A$,4):C$=LEFT$(A$,3):D$=MID$(A$,5,3)
670 IFB$="GO N"THENK=K-100:GOTO648
672 IFB$="GO S"THENK=K+100:GOTO648
674 IFB$="GO E"THENK=K+1:GOTO648
676 IFB$="GO W"THENK=K-1:GOTO648
678 IFC$="EAT"THEN698
680 IFA$="HELP"THENPRINT"DON'T ASK ME ASK DR.T":GOTO666
682 IFA$="ASK DR.T"THENPRINT"EVERY ACTION HAS AN EQUAL AND   OPPOSITE REACTION":GOTO666
684 IFC$="GET"THEN710
686 IFA$="LOOK"THENPRINT"IT LOOKS THE SAME IN EVERY DIRECTION":GOTO666
688 IFC$="INV"THENGOSUB736:GOTO666
690 IFC$="FIG"ORC$="OPE"ORC$="GO "THENPRINT"YOU CAN'T "A$:GOTO666
692 IFC$="QUI"THEN348
694 IFB$="DROP"THENPRINT"DON'T "A$" YOU'LL NEED IT LATER":GOTO666
696 PRINT"I DON'T UNDERSTAND ";A$:GOTO668
698 IFK=303ORK=6ORBR>0ORB=1THEN700
699 GOTO706
700 IFD$="CAC"ORD$="FOO"THENPRINT"UGH..";:GOTO708
702 IFD$="BER"ANDB=1THENPRINT"SOUR";:GOTO708
704 IFD$="BRE"ORD$="CRU"THENPRINT"A BIT MOLDY";:GOTO708
706 PRINT"YUCK":GOTO666
708 PRINT" BUT YOU FEEL BETTER":H=H+10:GOTO666
710 IFD$="SCR"ANDK=506THENSC=1:W$=T$(4):GOTO722
712 IFD$="DIA"ANDK=506THEND=D+1:W$=T$(1):GOTO722
713 IFD$="STI"ANDK=506THEND=D+1:W$=T$(1):GOTO722
714 IFD$="GOL"ANDK=506THENW$="GOLD":G=G+1:GOTO722
716 IFD$="CHE"THENPRINT"IT IS TOO HEAVY":GOTO666
718 IFD$="BRE"THENBR=BR+1:W$="BREAD":GOTO722
720 PRINT"YOU CAN'T "A$:GOTO668
722 PRINT"YOU HAVE THE "W$:GOTO666
724 IFC$="LUL"ANDRO=15THENPRINT"EVERYONE IS LULLED TO SLEEP AND YOU MAY GO YOUR WAY":SL=1:GOTO36
726 IFC$="HYM"ANDRO=33ANDA(33,6)=25THENPRINT"DRACULA FLEES BACK TO HIS HOUSE":A(33,6)=0:GOTO36
728 PRINT"LA-LA-LA":SOUND233,5:GOTO36
730 IFC$="CRO"ANDCR=1ANDRO=33THEN4140
732 PRINTN$:GOTO36
734 Y=2:GOSUB22:PRINT"IN GRATITUDE THE MAYOR OF LONDONGIVES YOU THE KEY TO THE CITY   AND A REWARD OF 500GRAND"
735 Z=Z+500:A(33,6)=0:GOSUB22:GOTO24
736 M$="YOU HAVE HOPE, "
738 IFSW=1THENM$=M$+"A SWORD, "
740 IFB=1THENM$=M$+"SOME BERRIES, "
742 IFBR>0THENM$=M$+"SOME BREAD, "
744 IFF=1THENM$=M$+"TUBES OF FOOD, "
746 IFE=1THENM$=M$+"A DINOSAUR EGG, "
748 IFG>0THENM$=M$+"GOLD WORTH $"+STR$(G*10000)+", "
750 IFD>0THENM$=M$+STR$(D)+"DIAMOND(S), "
752 IFV=1THENM$=M$+T$(8)", "
754 IFKY=1THENM$=M$+"A GOLD KEY, "
756 IFBX=1THENM$=M$+"A BOX, "
758 IFCR=1THENM$=M$+"A SILVER CROSS, "
760 IFBO=1THENM$=M$+"A JEWELLED CASKET, "
762 IFGR=1THENM$=M$+T$(6)+", "
764 IFSC=1THENM$=M$+"A SACRED SCROLL, "
766 IFZ>0THENM$=M$+"TREASURE WORTH $"+STR$(Z*1000)+", "
768 M$=M$+"AND MUCH COURAGE.":GOSUB1:RETURN
770 PRINT:CLS:PRINT"YOU ARE IN THE TIME MACHINE":PRINT"IT FLASHES THE TIMES ZONES:":PRINT:O=0
772 IFRO=1THEN778
774 IFRND(3)=1THENPRINT"<D>AGE OF DINOSAURS":O=1
776 IFRO=8THEN782
778 IFRND(3)=1THENPRINT"<R>ANCIENT ROME":O=1
780 IFRO=16THEN786
782 IFRND(3)=1THENPRINT"<A>KING ARTHUR'S COURT":O=1
784 IFRO=25THEN790
786 IFRND(3)=1THENPRINT"<E>ELDORADO":O=1
788 IFRO=29THEN794
790 IFRND(3)=1THENPRINT"<S>SHERLOCK HOLMES' ENGLAND":O=1
792 IFRO=34THEN798
794 IFRND(3)=1THENPRINT"<T>ROARING TWENTIES":O=1
796 IFRO=38THEN802
798 IFRND(3)=1THENPRINT"<F>THE FUTURE":O=1
800 IFRO=0THEN804
802 IFRND(5)=1THENPRINT"<P>RETURN TO THE PRESENT"
804 IFO<1THEN772
806 A$=INKEY$:IFA$=""THEN806
808 IFA$="D"THENRO=1:DO=1
810 IFA$="R"THENRO=8:R=1
812 IFA$="A"THENRO=16:AR=1
814 IFA$="E"THENRO=25:EL=1
816 IFA$="S"THENRO=29:S=1:BG=0
818 IFA$="T"THENRO=34:RT=1
820 IFA$="F"THENRO=38:FU=1
822 IFA$="P"THEN828
824 IFA$="X"THEN770
826 P=P+1:IFRO=0THEN770
827 GOTO26
828 M=Z+(10*G)+(100*D)+(500*V)+(1000*GR):ZN=DO+R+AR+EL+S+RT+FU
830 IFSC=1THENM=M+1000
832 CLS:PRINT:M$="CONGRATULATIONS! YOU HAVE RETURNED ALIVE FROM YOUR JOURNEY BEYOND TIME HAVING MADE"+STR$(P):GOSUB1
833 M$="TIME JUMPS & VISITED"+STR$(ZN)+" TIME ZONES":GOSUB1
834 M$="YOU BROUGHT BACK ":IFE=1THENM$=M$+"A DINOSAUR EGG AND "
836 IFM=0THENM$=M$+"NO TREASURE":GOTO840
838 M$=M$+"TREASURE WORTH"+STR$(M*1000)
840 GOSUB1:M$="":IFM>0THENM$=M$+"YOU SELL EVERYTHING":GOTO842
841 M$=M$+"YOU HAVE  NOTHING, "
842 IFE=1THENM$=M$+"BUT THE DINO EGG "
844 M$=M$+"& ARE LIVING IN "
846 IFM>3420THENM$=M$+"PALATIAL SPLENDOR IN SOUTHERN CALIFORNIA":GOTO854
848 IFM>999THENM$=M$+"A CONDO IN MIAMI":GOTO854
850 IFM>50THENM$=M$+"A COTTAGE IN PROSPECT, KY":GOTO854
852 M$=M$+"A TENEMENT IN NYC"
854 IFE=1THENM$=M$+" WITH YOUR TWO-TON FRIEND"
856 GOSUB1:GOTO348
4000 C$=MID$(A$,N+1,3):IFLEFT$(C$,1)<>" "THEN60
4010 GOTO58
4020 M$="THE SERGEANT TELLS YOU THERE'S A STRANGE ILLNESS IN THE CITY. HE CONFIDES HE THINKS A VAMPIRE IS "
4030 M$=M$+"IN TOWN AND ANYONE WHO COULD RID THE TOWN OF THIS SCOURGE WOULD  BE RICHLY REWARDED.":GOSUB1:RETURN
4060 M$="YOU KILLED THE ARCH CRIMINAL MORIARITY AND MADE THE WORLD A LITTLE SAFER FOR GOOD MEN":GOSUB1
4070 A(31,6)=0:GOSUB22:GOTO24
4100 IFRO=15THENPRINT"WITH AN IRON BAR ACROSS IT":GOTO36
4110 PRINT"WITH A BRASS HANDLE":GOTO36
4120 G=G+1:PRINT"YOU SCOOPED UP SOME OF THE GOLD-THERES MORE LEFT BUT NO TIME TO GET IT ALL NOW":GD=1:GOTO24
4140 PRINT"HE SHRIEKS AND FLEES FOREVER FORTHAT WAS A SACRED CROSS BORNE BYSIR GALAHAD OF THE ROUND TABLE":GOTO734
4200 IFO=2THENA(42,6)=27:RETURN
4210 IFO=3THENA(43,6)=27:RETURN
4220 RETURN
