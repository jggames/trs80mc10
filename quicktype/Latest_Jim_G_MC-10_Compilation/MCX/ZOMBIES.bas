15 DIM I,J,K,K1,S,S1,Z,Z(11),Z1(11),WC,W(11),C$,W$,B$
16 C$=CHR$(134+32):W$=CHR$(143+64):B$=CHR$(143+32)
20 CLS3:PRINT@53,"ZOMBIES";
25 WC=0
30 FORI=0TO15
40 PRINT@I+1,C$;
45 PRINT@32*I,C$;
50 PRINT@(32*I+16),C$;
60 Z=RND(15)
70 IFRND(0)<0.4THENWC=WC+1:W(WC)=32*Z+I:PRINT@(32*Z+I),W$;
80 S=RND(10):S1=S
90 Z(INT(I/2)+1)=4+S
100 PRINT@480+I,C$;
110 NEXTI
115 FORJ=1TO4:Z(J)=RND(10)+4:Z(J+4)=RND(10)+4:Z1(J)=Z(J):Z1(J+4)=Z(J+4):PRINT@(32*Z(J)+Z(J+4)),"z";:NEXTJ
120 K=RND(14)+1:K1=K:FORJ=1TO4:IFS=Z(J) AND K=Z(J+4)THENJ=999
125 NEXTJ:IFJ>=999THEN120
126 GOTO200
130 FORI=1TO4
135 IFZ(I)=0THEN190
140 PRINT@(32*Z(I)+Z(I+4)),B$;
150 Z(I)=Z(I)-SGN(Z(I)-S)
160 Z(I+4)=Z(I+4)-SGN(Z(I+4)-K)
165 IFPEEK(16384+(32*Z(I)+Z(I+4)))=207THENZ(I)=0:GOTO190
170 PRINT@(32*Z(I)+Z(I+4)),"z";
180 IFZ(I)=SANDZ(I+4)=KTHEN280
190 NEXTI
200 PRINT@(32*S+K),"*";
210 FORI=1TO250:A$=INKEY$:IFA$="A"ORA$="S"ORA$="W"ORA$="Z"THEN220
215 NEXTI
220 PRINT@(32*S+K),B$;
230 IFZ(1)+Z(2)+Z(3)+Z(4)=0THENPRINT@(32*S+K),"*SAFE!";:SOUND230,15:GOTO300
235 K=K-2*((A$="S")-(A$="A"))
240 IFK<=0ORK>=16THEN280
250 S=S-2*((A$="Z")-(A$="W"))
260 IFS<=0ORS>=15THEN280
270 GOTO130
280 PRINT@309-96,"YOU'RE";
285 PRINT@309-64,"DEAD!!";:SOUND150,20
290 PRINT@310-2,"REDO? (R)";
295 PRINT@310+64+1,"OR";
300 PRINT@466-32+2,"NEW MAZE?";
305 PRINT@466+4,"(Y/N)";
310 A$=INKEY$:IFA$=""THEN310
320 IFA$="Y"THEN20
330 IFA$="N"THEN350
335 IFA$="R"THENGOSUB400:GOTO200
340 GOTO310
350 END
400 CLS3:PRINT@53,"ZOMBIES";:S=S1:K=K1
405 FORI=1TOWC:PRINT@W(I),W$;:NEXTI
410 FORI=0TO15
415 PRINT@I+1,C$;
420 PRINT@32*I,C$;
430 PRINT@(32*I+16),C$;
440 PRINT@480+I,C$;
450 NEXTI
470 FORJ=1TO4:PRINT@(32*Z1(J)+Z1(J+4)),"z";:Z(J)=Z1(J):Z(J+4)=Z1(J+4):NEXTJ
480 RETURN
