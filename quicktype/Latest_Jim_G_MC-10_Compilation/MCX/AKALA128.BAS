0 CLS:CLEAR400:DIMT,A,G,I,J,M$,P(5),C$(5),C(5),M$(10),L(10,1),Z(12,1):GOTO9
1 DRAW"BD6":FORG=1TOLEN(M$):A=ASC(MID$(M$,G)):IFA>ITHENA=(A-I):DRAWL$(A)+"BR2":NEXT:DRAW"BL255BD2":RETURN
2 IFA>JTHENA=(A-J):DRAW"BU6"+N$(A)+"BR2BD6":NEXT:DRAW"BL255BD2":RETURN
3 IFA=32THENDRAWN$(.)+"BR2":NEXT:DRAW"BL255BD2":RETURN ELSE DRAW"BR2RNUBR3BR2":NEXT:DRAW"BL255BD2":RETURN
4 Q$=INKEY$:ON-(Q$="")GOTO4:RETURN
5 PRINTM$;:A=((PEEK(17024)AND1)*256+PEEK(17025))+16383:FORT=A-LEN(M$)+1TOA:POKET,PEEK(T)-I:NEXT:RETURN
6 LINE(167,191)-(0,160),0,BF:RETURN
7 IFG1THENLINE(0,0)-(255,159),0,BF:RETURN
8 PMODE2,1:SCREEN1,1:G1=1:CM=0:PCLS:DRAW"BM168,168":M$=LEFT$("FO"+STR$(INT(P(0)*10+0.5)/10),11):GOSUB1:DRAW"BM168,176":M$=LEFT$("HP"+STR$(C(0)),11):GOSUB1:DRAW"BM168,184":M$=LEFT$("GP"+STR$(C(5)),11):GOSUB1:RETURN
9 GOSUB800
12 GOSUB297:LE=0:G1=0:PRINT@198,"WELCOME TO AKALABETH,":PRINT@233,"WORLD OF DOOM!"
13 DIMD(10,10),T(20,20),X(10),Y(10),E(10,3),Q(10,5),R(10,3),F(10,5),A(10,3)
14 FORX=0TO20:T(X,0)=1:T(0,X)=1:T(X,20)=1:T(20,X)=1:NEXT:PRINT@448,"PLEASE WAIT";
15 FORX=1TO19:FORY=1TO19:T(X,Y)=INT(RND(0)^5*4.5):IFRND(0)>.5THENIFT(X,Y)=3THENT(X,Y)=0
16 NEXT:PRINT".";:NEXT
17 T(INT(RND(0)*19+1),INT(RND(0)*19+1))=5:TX=INT(RND(0)*19+1):TY=INT(RND(0)*19+1):T(TX,TY)=3
18 X(0)=127:Y(0)=79
19 FORX=2TO20STEP2:X(X/2)=INT(ATN(1/X)/ATN(1)*128+.5):Y(X/2)=INT(X(X/2)*4/7):E(X/2,0)=127-X(X/2):E(X/2,1)=127+X(X/2):E(X/2,2)=79-Y(X/2):E(X/2,3)=79+Y(X/2):NEXT
20 E(0,0)=0:E(0,1)=255:E(0,2)=0:E(0,3)=159
21 FORX=1TO10:R(X,0)=127-X(X)/3:R(X,1)=127+X(X)/3:R(X,2)=79-Y(X)*.7:R(X,3)=79+Y(X):NEXT:PRINT".";
22 FORX=0TO9:Q(X,0)=(E(X,0)*2+E(X+1,0))/3:Q(X,1)=(E(X,0)+2*E(X+1,0))/3:W=Q(X,0)-E(X,0):Q(X,2)=E(X,2)+W*4/7:Q(X,3)=E(X,2)+2*W*4/7:Q(X,4)=(E(X,3)*2+E(X+1,3))/3:Q(X,5)=(E(X,3)+2*E(X+1,3))/3
23 Q(X,2)=Q(X,4)-(Q(X,4)-Q(X,2))*.8:Q(X,3)=Q(X,5)-(Q(X,5)-Q(X,3))*.8:IFQ(X,3)=Q(X,4)THENQ(X,3)=Q(X,3)-1
24 NEXT
25 FORX=0TO9:F(X,0)=127-X(X)/3:F(X,1)=127+X(X)/3:F(X,2)=127-X(X+1)/3:F(X,3)=127+X(X+1)/3:F(X,4)=79+(Y(X)*2+Y(X+1))/3:F(X,5)=79+(Y(X)+2*Y(X+1))/3:NEXT
26 FORX=0TO9:A(X,0)=(F(X,0)*2+F(X,1))/3:A(X,1)=(F(X,0)+2*F(X,1))/3:A(X,3)=F(X,4):A(X,2)=159-A(X,3):NEXT
27 GOSUB28:GOTO154
28 GOSUB7:FORY=-1TO1:FORX=-1TO1:LINE(126,75)-(130,75),1:LINE(128,73)-(128,77),1
29 G=T(TX+X,TY+Y):P=53+(X+1)*50:O=(Y+1)*50:IFG=2THENLINE(P+20,O+20)-(P+30,O+20),1:LINE-(P+30,O+30),1:LINE-(P+20,O+30),1:LINE-(P+20,O+20),1
30 IFG=3THENLINE(P+10,O+10)-(P+20,O+10),1:LINE-(P+20,O+40),1:LINE-(P+10,O+40),1:LINE-(P+10,O+30),1:LINE-(P+40,O+30),1
31 IFG=3THENLINE-(P+40,O+40),1:LINE-(P+30,O+40),1:LINE-(P+30,O+10),1:LINE-(P+40,O+10),1:LINE-(P+40,O+20),1:LINE-(P+10,O+20),1:LINE-(P+10,O+10),1
32 IFG=4THENLINE(P+20,O+20)-(P+30,O+30),1:LINE-(P+20,O+30),1:LINE-(P+30,O+20),1
33 IFG=5THENLINE(P,O)-(P+50,O),1:LINE-(P+50,O+50),1:LINE-(P,O+50),1:LINE-(P,O),1:LINE(P+10,O+10)-(P+10,O+40),1
34 IFG=5THENLINE-(P+40,O+40),1:LINE-(P+40,O+10),1:LINE-(P+10,O+10),1:LINE-(P+40,O+40),1:LINE(P+10,O+40)-(P+40,O+10),1
35 IFG=1THENLINE(P+10,O+50)-(P+10,O+40),1:LINE-(P+20,O+30),1:LINE-(P+40,O+30),1:LINE-(P+40,O+50),1:LINE(P,O+10)-(P+10,O+10),1:LINE(P+50,O+10)-(P+40,O+10),1:LINE(P,O+40)-(P+10,O+40),1:LINE(P+40,O+40)-(P+50,O+40),1
36 IFG=1THENLINE(P+10,O)-(P+10,O+20),1:LINE-(P+20,O+20),1:LINE-(P+20,O+30),1:LINE-(P+30,O+30),1:LINE-(P+30,O+10),1:LINE-(P+40,O+10),1:LINE-(P+40,O),1
37 NEXT:NEXT:RETURN
38 GOSUB7:D=0:MP=0
39 CE=D(PX+DX*D,PY+DY*D):LE=D(PX+DX*D+DY,PY+DY*D-DX):RI=D(PX+DX*D-DY,PY+DY*D+DX)
40 L1=E(D,0):R1=E(D,1):T1=E(D,2):B1=E(D,3):L2=E(D+1,0):R2=E(D+1,1):T2=E(D+1,2):B2=E(D+1,3)
41 CE=INT(CE):LE=INT(LE):RI=INT(RI)
42 MC=INT(CE/10):CE=CE-MC*10:LE=INT((LE/10-INT(LE/10))*10):RI=INT((RI/10-INT(RI/10))*10)
43 IFD=0THEN47
44 IFCE=1ORCE=3ORCE=4THENLINE(L1,T1)-(R1,T1),1:LINE-(R1,B1),1:LINE-(L1,B1),1:LINE-(L1,T1),1
45 IFCE=1ORCE=3THENEN=1:GOTO67
46 IFCE=4THENLINE(R(D,0),R(D,3))-(R(D,0),R(D,2)),1:LINE-(R(D,1),R(D,2)),1:LINE-(R(D,1),R(D,3)),1:EN=1:GOTO67
47 IFLE=1ORLE=3ORLE=4THENLINE(L1,T1)-(L2,T2),1:LINE(L1,B1)-(L2,B2),1
48 IFRI=1ORRI=3ORRI=4THENLINE(R1,T1)-(R2,T2),1:LINE(R1,B1)-(R2,B2),1
49 IFLE=4ANDD>0THENLINE(Q(D,0),Q(D,4))-(Q(D,0),Q(D,2)),1:LINE-(Q(D,1),Q(D,3)),1:LINE-(Q(D,1),Q(D,5)),1
50 IFLE=4ANDD=0THENLINE(0,Q(D,2)-3)-(Q(D,1),Q(D,3)),1:LINE-(Q(D,1),Q(D,5)),1
51 IFRI=4ANDD>0THENLINE(255-Q(D,0),Q(D,4))-(255-Q(D,0),Q(D,2)),1:LINE-(255-Q(D,1),Q(D,3)),1:LINE-(255-Q(D,1),Q(D,5)),1
52 IFRI=4ANDD=0THENLINE(255,Q(D,2)-3)-(255-Q(D,1),Q(D,3)),1:LINE-(255-Q(D,1),Q(D,5)),1
53 IFLE=3ORLE=1ORLE=4THEN56
54 IFD THENLINE(L1,T1)-(L1,B1),1
55 LINE(L1,T2)-(L2,T2),1:LINE-(L2,B2),1:LINE-(L1,B2),1
56 IFRI=3ORRI=1ORRI=4THEN59
57 IFD THENLINE(R1,T1)-(R1,B1),1
58 LINE(R1,T2)-(R2,T2),1:LINE-(R2,B2),1:LINE-(R1,B2),1
59 IFCE=7ORCE=9THENLINE(F(D,0),F(D,4))-(F(D,2),F(D,5)),1:LINE-(F(D,3),F(D,5)),1:LINE-(F(D,1),F(D,4)),1:LINE-(F(D,0),F(D,4)),1
60 IFCE=8THENLINE(F(D,0),158-F(D,4))-(F(D,2),158-F(D,5)),1:LINE-(F(D,3),158-F(D,5)),1:LINE-(F(D,1),158-F(D,4)),1:LINE-(F(D,0),158-F(D,4)),1
61 IFCE=7ORCE=8THENBA=A(D,3):TP=A(D,2):LX=A(D,0):RX=A(D,1):LINE(LX,BA)-(LX,TP),1:LINE(RX,TP)-(RX,BA),1
62 IFCE=7ORCE=8THENO=(BA*4+TP)/5:Y2=(BA*3+TP*2)/5:Y3=(BA*2+TP*3)/5:Y4=(BA+TP*4)/5:LINE(LX,O)-(RX,O),1:LINE(LX,Y2)-(RX,Y2),1:LINE(LX,Y3)-(RX,Y3),1:LINE(LX,Y4)-(RX,Y4),1
63 IFD>0ANDCE=5THENLINE(127-10/D,E(D,3))-(127-10/D,E(D,3)-10/D),1:LINE-(127+10/D,E(D,3)-10/D),1:LINE-(127+10/D,E(D,3)),1:LINE-(127-10/D,E(D,3)),1
64 IFCE=5ANDD>0THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1
65 IFD>0ANDCE=5THENLINE(127-10/D,E(D,3)-10/D)-(127-5/D,E(D,3)-15/D),1:LINE-(127+15/D,E(D,3)-15/D),1:LINE-(127+15/D,E(D,3)-5/D),1:LINE-(127+10/D,E(D,3)),1:LINE(127+10/D,E(D,3)-10/D)-(127+15/D,E(D,3)-15/D),1
67 IFMC<1THEN131
68 B=79+Y(D):C=127
69 IFMC=8THENLINE(32,7)-(0,0),0,BF:M$="CHEST":GOSUB1:GOTO71
70 IFMP=0THENMP=1:M$=M$(MC):A=LEN(M$)*8:LINE(A,15)-(0,8),0,BF:GOSUB1
71 IFD=0THEN131
72 ONMCGOTO73,79,82,86,93,100,106,109,112,120:GOTO131
73 LINE(C-23/D,B)-(C-15/D,B),1:LINE-(C-15/D,B-15/D),1:LINE-(C-8/D,B-30/D),1:LINE-(C+8/D,B-30/D),1:LINE-(C+15/D,B-15/D),1:LINE-(C+15/D,B),1:LINE-(C+23/D,B),1
74 LINE(C,B-26/D)-(C,B-65/D),1:LINE(C-2/D+.5,B-38/D)-(C+2/D+.5,B-38/D),1:LINE(C-3/D+.5,B-45/D)-(C+3/D+.5,B-45/D),1:LINE(C-5/D+.5,B-53/D)-(C+5/D+.5,B-53/D),1
75 LINE(C-23/D,B-56/D)-(C-30/D,B-53/D),1:LINE-(C-23/D,B-45/D),1:LINE-(C-23/D,B-53/D),1:LINE-(C-8/D,B-38/D),1
76 LINE(C-15/D,B-45/D)-(C-8/D,B-60/D),1:LINE-(C+8/D,B-60/D),1:LINE-(C+15/D,B-45/D),1:LINE(C+15/D,B-42/D)-(C+15/D,B-57/D),1:LINE(C+12/D,B-45/D)-(C+20/D,B-45/D),1
77 LINE(C,B-75/D)-(C-5/D+.5,B-80/D),1:LINE-(C-8/D,B-75/D),1:LINE-(C-5/D+.5,B-65/D),1:LINE-(C+5/D+.5,B-65/D),1:LINE-(C+5/D+.5,B-68/D),1:LINE-(C-5/D+.5,B-68/D),1:LINE-(C-5/D+.5,B-65/D),1
78 LINE-(C+5/D+.5,B-65/D),1:LINE-(C+8/D,B-75/D),1:LINE-(C+5/D+.5,B-80/D),1:LINE-(C-5/D+.5,B-80/D),1:PSET(C-5/D+.5,B-72/D):PSET(C+5/D+.5,B-72/D):GOTO131
79 LINE(C,B-56/D)-(C,B-8/D),1:LINE-(C+10/D,B),1:LINE-(C+30/D,B),1:LINE-(C+30/D,B-45/D),1:LINE-(C+10/D,B-64/D),1:LINE-(C,B-56/D),1
80 LINE-(C-10/D,B-64/D),1:LINE-(C-30/D,B-45/D),1:LINE-(C-30/D,B),1:LINE-(C-10/D,B),1:LINE-(C,B-8/D),1:LINE(C-10/D,B-64/D)-(C-10/D,B-75/D),1:LINE-(C,B-83/D),1:LINE-(C+10/D,B-75/D),1
81 LINE-(C,B-79/D),1:LINE-(C-10/D,B-75/D),1:LINE-(C,B-60/D),1:LINE-(C+10/D,B-75/D),1:LINE-(C+10/D,B-64/D),1:GOTO131
82 LINE(C+5/D,B-30/D)-(C,B-25/D),1:LINE-(C-5/D,B-30/D),1:LINE-(C-15/D,B-5/D),1:LINE-(C-10/D,B),1:LINE-(C+10/D,B),1:LINE-(C+15/D,B-5/D),1
83 LINE-(C+20/D,B-5/D),1:LINE-(C+10/D,B),1:LINE-(C+15/D,B-5/D),1:LINE-(C+5/D,B-30/D),1:LINE-(C+10/D,B-40/D),1:LINE-(C+3/D+.5,B-35/D),1:LINE-(C-3/D+.5,B-35/D),1:LINE-(C-10/D,B-40/D),1:LINE-(C-5/D,B-30/D),1
84 LINE(C-5/D,B-33/D)-(C-3/D+.5,B-30/D),1:LINE(C+5/D,B-33/D)-(C+3/D+.5,B-30/D),1:LINE(C-5/D,B-20/D)-(C-5/D,B-15/D),1
85 LINE(C+5/D,B-20/D)-(C+5/D,B-15/D),1:LINE(C-7+D,B-20/D)-(C-7/D,B-15/D),1:LINE(C+7/D,B-20/D)-(C+7/D,B-15/D),1:GOTO131
86 LINE(C,B)-(C-15/D,B),1:LINE-(C-8/D,B-8/D),1:LINE-(C-8/D,B-15/D),1:LINE-(C-15/D,B-23/D),1:LINE-(C-15/D,B-15/D),1:LINE-(C-23/D,B-23/D),1
87 LINE-(C-23/D,B-45/D),1:LINE-(C-15/D,B-53/D),1:LINE-(C-8/D,B-53/D),1:LINE-(C-15/D,B-68/D),1:LINE-(C-8/D,B-75/D),1:LINE-(C,B-75/D),1
88 LINE(C,B)-(C+15/D,B),1:LINE-(C+8/D,B-8/D),1:LINE-(C+8/D,B-15/D),1:LINE-(C+15/D,B-23/D),1:LINE-(C+15/D,B-15/D),1:LINE-(C+23/D,B-23/D),1
89 LINE-(C+23/D,B-45/D),1:LINE-(C+15/D,B-53/D),1:LINE-(C+8/D,B-53/D),1:LINE-(C+15/D,B-68/D),1:LINE-(C+8/D,B-75/D),1:LINE-(C,B-75/D),1
90 LINE(C-15/D,B-68/D)-(C+15/D,B-68/D),1:LINE(C-8/D,B-53/D)-(C+8/D,B-53/D),1:LINE(C-23/D,B-15/D)-(C+8/D,B-45/D),1
91 LINE(C-8/D,B-68/D)-(C,B-60/D),1:LINE-(C+8/D,B-68/D),1:LINE-(C+8/D,B-60/D),1:LINE-(C-8/D,B-60/D),1:LINE-(C-8/D,B-68/D),1
92 LINE(C,B-38/D)-(C-8/D,B-38/D),1:LINE-(C+8/D,B-53/D),1:LINE-(C+8/D,B-45/D),1:LINE-(C+15/D,B-45/D),1:LINE-(C,B-30/D),1:LINE-(C,B-38/D),1:GOTO131
93 LINE(C-10/D,B-15/D)-(C-10/D,B-30/D),1:LINE-(C-15/D,B-20/D),1:LINE-(C-15/D,B-15/D),1:LINE-(C-15/D,B),1:LINE-(C+15/D,B),1:LINE-(C+15/D,B-15/D),1:LINE  -(C-15/D,B-15/D),1
94 LINE(C-15/D,B-10/D)-(C+15/D,B-10/D),1:LINE(C-15/D,B-5/D)-(C+15/D,B-5/D),1
95 LINE(C,B-15/D)-(C-5/D,B-20/D),1:LINE-(C-5/D,B-35/D),1:LINE-(C+5/D,B-35/D),1:LINE-(C+5/D,B-20/D),1:LINE-(C+10/D,B-15/D),1
96 LINE(C-5/D,B-20/D)-(C+5/D,B-20/D),1:LINE(C-5/D,B-25/D)-(C+5/D,B-25/D),1:LINE(C-5/D,B-30/D)-(C+5/D,B-30/D),1
97 LINE(C-10/D,B-35/D)-(C-10/D,B-40/D),1:LINE-(C-5/D,B-45/D),1:LINE-(C+5/D,B-45/D),1:LINE-(C+10/D,B-40/D),1:LINE-(C+10/D,B-35/D),1
98 LINE(C-10/D,B-40/D)-(C,B-45/D),1:LINE-(C+10/D,B-40/D),1
99 LINE(C-5/D,B-40/D)-(C+5/D,B-40/D),1:LINE-(C+15/D,B-30/D),1:LINE-(C,B-40/D),1:LINE-(C-15/D,B-30/D),1:LINE-(C-5/D+.5,B-40/D),1:GOTO131
100 LINE(C-20/D,79-Y(D))-(C-20/D,B-88/D),1:LINE-(C-10/D,B-83/D),1:LINE-(C+10/D,B-83/D),1:LINE-(C+20/D,B-88/D),1:LINE-(C+20/D,79-Y(D)),1:LINE-(C-20/D,79-Y(D)),1
101 LINE(C-20/D,B-88/D)-(C-30/D,B-83/D),1:LINE-(C-30/D,B-78/D),1:LINE(C+20/D,B-88/D)-(C+30/D,B-83/D),1:LINE-(C+40/D,B-83/D),1
102 LINE(C-15/D,B-86/D)-(C-20/D,B-83/D),1:LINE-(C-20/D,B-78/D),1:LINE-(C-30/D,B-73/D),1:LINE-(C-30/D,B-68/D),1:LINE-(C-20/D,B-63/D),1
103 LINE(C-10/D,B-83/D)-(C-10/D,B-58/D),1:LINE-(C,B-50/D),1:LINE(C+10/D,B-83/D)-(C+10/D,B-78/D),1:LINE-(C+20/D,B-73/D),1:LINE-(C+20/D,B-40/D),1
104 LINE(C+15/D,B-85/D)-(C+20/D,B-78/D),1:LINE-(C+30/D,B-76/D),1:LINE-(C+30/D,B-60/D),1
105 LINE(C,B-83/D)-(C,B-73/D),1:LINE-(C+10/D,B-68/D),1:LINE-(C+10/D,B-63/D),1:LINE-(C,B-58/D),1:GOTO131
106 LINE(C+5/D+.5,B-10/D)-(C-5/D+.5,B-10/D),1:LINE-(C,B-15/D),1:LINE-(C+10/D,B-20/D),1:LINE-(C+5/D+.5,B-15/D),1:LINE-(C+5/D+.5,B-10/D),1
107 LINE-(C+7/D+.5,B-6/D),1:LINE-(C+5/D+.5,B-3/D),1:LINE-(C-5/D+.5,B-3/D),1:LINE-(C-7/D+.5,B-6/D),1:LINE-(C-5/D+.5,B-10/D),1:LINE(C+2/D+.5,B-3/D)-(C+5/D+.5,B),1:LINE-(C+8/D,B),1
108 LINE(C-2/D+.5,B-3/D)-(C-5/D+.5,B),1:LINE-(C-8/D,B),1:PSET(C+3/D+.5,B-8/D):PSET(C-3/D+.5,B-8/D):LINE(C+3/D+.5,B-5/D)-(C-3/D+.5,B-5/D),1:GOTO131
109 LINE(127-10/D,E(D,3))-(127-10/D,E(D,3)-10/D),1:LINE-(127+10/D,E(D,3)-10/D),1:LINE-(127+10/D,E(D,3)),1:LINE-(127-10/D,E(D,3)),1
110 LINE(127-10/D,E(D,3)-10/D)-(127-5/D,E(D,3)-15/D),1:LINE-(127+15/D,E(D,3)-15/D),1:LINE-(127+15/D,E(D,3)-5/D),1:LINE-(127+10/D,E(D,3)),1
111 LINE(127+10/D,E(D,3)-10/D)-(127+15/D,E(D,3)-15/D),1:GOTO131
112 LINE(C-14/D,B-46/D)-(C-12/D,B-37/D),1:LINE-(C-20/D,B-32/D),1:LINE-(C-30/D,B-32/D),1:LINE-(C-22/D,B-24/D),1:LINE-(C-40/D,B-17/D),1:LINE-(C-40/D,B-7/D),1:LINE-(C-38/D,B-5/D),1:LINE-(C-40/D,B-3/D),1:LINE-(C-40/D,B),1
113 LINE-(C-36/D,B),1:LINE-(C-34/D,B-2/D),1:LINE-(C-32/D,B),1:LINE-(C-28/D,B),1:LINE-(C-28/D,B-3/D),1:LINE-(C-30/D,B-5/D),1:LINE-(C-28/D,B-7/D),1:LINE-(C-28/D,B-15/D),1:LINE-(C,B-27/D),1
114 LINE(C+14/D,B-46/D)-(C+12/D,B-37/D),1:LINE-(C+20/D,B-32/D),1:LINE-(C+30/D,B-32/D),1:LINE-(C+22/D,B-24/D),1:LINE-(C+40/D,B-17/D),1:LINE-(C+40/D,B-7/D),1:LINE-(C+38/D,B-5/D),1:LINE-(C+40/D,B-3/D),1:LINE-(C+40/D,B),1
115 LINE-(C+36/D,B),1:LINE-(C+34/D,B-2/D),1:LINE-(C+32/D,B),1:LINE-(C+28/D,B),1:LINE-(C+28/D,B-3/D),1:LINE-(C+30/D,B-5/D),1:LINE-(C+28/D,B-7/D),1:LINE-(C+28/D,B-15/D),1:LINE-(C,B-27/D),1
116 LINE(C+6/D,B-48/D)-(C+38/D,B-41/D),1:LINE-(C+40/D,B-42/D),1:LINE-(C+18/D,B-56/D),1:LINE-(C+12/D,B-56/D),1:LINE-(C+10/D,B-57/D),1:LINE-(C+8/D,B-56/D),1:LINE-(C-8/D,B-56/D),1:LINE-(C-10/D,B-58/D),1:LINE-(C+14/D,B-58/D),1
117 LINE-(C+16/D,B-59/D),1:LINE-(C+8/D,B-63/D),1:LINE-(C+6/D,B-63/D),1:LINE-(C+2/D+.5,B-70/D),1:LINE-(C+2/D+.5,B-63/D),1:LINE-(C-2/D+.5,B-63/D),1:LINE-(C-2/D+.5,B-70/D),1:LINE-(C-6/D,B-63/D),1:LINE-(C-8/D,B-63/D),1
118 LINE-(C-16/D,B-59/D),1:LINE-(C-14/D,B-58/D),1:LINE-(C+10/D,B-57/D),1:LINE-(C-12/D,B-56/D),1:LINE-(C-18/D,B-56/D),1:LINE-(C-36/D,B-47/D),1:LINE-(C-36/D,B-39/D),1:LINE-(C-28/D,B-41/D),1:LINE-(C-28/D,B-46/D),1
119 LINE-(C-20/D,B-50/D),1:LINE-(C-18/D,B-50/D),1:LINE-(C-14/D,B-46/D),1:GOTO242
120 LINE(C+6/D,B-60/D)-(C+30/D,B-90/D),1:LINE-(C+60/D,B-30/D),1:LINE-(C+60/D,B-10/D),1:LINE-(C+30/D,B-40/D),1:LINE-(C+15/D,B-40/D),1
121 LINE(C-6/D,B-60/D)-(C-30/D,B-90/D),1:LINE-(C-60/D,B-30/D),1:LINE-(C-60/D,B-10/D),1:LINE-(C-30/D,B-40/D),1:LINE-(C-15/D,B-40/D),1
122 LINE(C,B-25/D)-(C+6/D,B-25/D),1:LINE-(C+10/D,B-20/D),1:LINE-(C+12/D,B-10/D),1:LINE-(C+10/D,B-6/D),1:LINE-(C+10/D,B),1:LINE-(C+14/D,B),1:LINE-(C+15/D,B-5/D),1:LINE-(C+16/D,B),1:LINE-(C+20/D,B),1
123 LINE-(C+20/D,B-6/D),1:LINE-(C+18/D,B-10/D),1:LINE-(C+18/D,B-20/D),1:LINE-(C+15/D,B-30/D),1:LINE-(C+15/D,B-45/D),1:LINE-(C+40/D,B-60/D),1:LINE-(C+40/D,B-70/D),1
124 LINE-(C+10/D,B-55/D),1:LINE-(C+6/D,B-60/D),1:LINE-(C+10/D,B-74/D),1:LINE-(C+6/D,B-80/D),1:LINE-(C+4/D+.5,B-80/D),1:LINE-(C+3/D+.5,B-82/D),1:LINE-(C+2/D+.5,B-80/D),1:LINE-(C,B-80/D),1
125 LINE(C,B-25/D)-(C-6/D,B-25/D),1:LINE-(C-10/D,B-20/D),1:LINE-(C-12/D,B-10/D),1:LINE-(C-10/D,B-6/D),1:LINE-(C-10/D,B),1:LINE-(C-14/D,B),1:LINE-(C-15/D,B-5/D),1:LINE-(C-16/D,B),1:LINE-(C-20/D,B),1
126 LINE-(C-20/D,B-6/D),1:LINE-(C-18/D,B-10/D),1:LINE-(C-18/D,B-20/D),1:LINE-(C-15/D,B-30/D),1:LINE-(C-15/D,B-45/D),1:LINE-(C-40/D,B-60/D),1:LINE-(C-40/D,B-70/D),1
127 LINE-(C-10/D,B-55/D),1:LINE-(C-6/D,B-60/D),1:LINE-(C-10/D,B-74/D),1:LINE-(C-6/D,B-80/D),1:LINE-(C-4/D+.5,B-80/D),1:LINE-(C-3/D+.5,B-82/D),1:LINE-(C-2/D+.5,B-80/D),1:LINE-(C,B-80/D),1
128 LINE(C-6/D,B-25/D)-(C,B-6/D),1:LINE-(C+10/D,B),1:LINE-(C+4/D+.5,B-8/D),1:LINE-(C+6/D,B-25/D),1:LINE(C-40/D,B-64/D)-(C-40/D,B-90/D),1:LINE-(C-52/D,B-80/D),1:LINE-(C-52/D,B-40/D),1
129 LINE(C+40/D,B-86/D)-(C+38/D,B-92/D),1:LINE-(C+42/D,B-92/D),1:LINE-(C+40/D,B-86/D),1:LINE-(C+40/D,B-50/D),1
130 LINE(C+4/D+.5,B-70/D)-(C+6/D,B-74/D),1:LINE(C-4/D+.5,B-70/D)-(C-6/D,B-74/D),1:LINE(C,B-64/D)-(C,B-60/D),1
131 IFEN=1THENEN=0:RETURN
132 D=D+1:GOTO39
133 G=RND(-ABS(LN)-TX*10-TY*1000+IN*31.4):FORX=1TO9:FORY=1TO9:D(X,Y)=0:NEXT:NEXT:FORX=0TO10:D(X,0)=1:D(X,10)=1:D(0,X)=1:D(10,X)=1:NEXT
134 FORX=2TO8STEP2:FORY=1TO9:D(X,Y)=1:D(Y,X)=1:NEXT:NEXT:FORX=2TO8STEP2:FORY=1TO9STEP2
135 IFRND(0)>.95THEND(X,Y)=2
136 IFRND(0)>.95THEND(Y,X)=2
137 IFRND(0)>.6THEND(Y,X)=3
138 IFRND(0)>.6THEND(X,Y)=3
139 IFRND(0)>.6THEND(X,Y)=4
140 IFRND(0)>.6THEND(Y,X)=4
141 IFRND(0)>.97THEND(Y,X)=9
142 IFRND(0)>.97THEND(X,Y)=9
143 IFRND(0)>.94THEND(X,Y)=5
144 IFRND(0)>.94THEND(Y,X)=5
145 NEXT:NEXT
146 D(2,1)=0:IFIN/2=INT(IN/2)THEND(7,3)=7:D(3,7)=8
147 IFIN/2<>INT(IN/2)THEND(7,3)=8:D(3,7)=7
148 IFIN=1THEND(1,1)=8:D(7,3)=0
149 GOSUB237:RETURN
150 PRINT@480,;:M$="PRESS ENTER TO CONTINUE":GOSUB5
151 Q$=INKEY$:ON-(Q$<>CHR$(13))GOTO151:RETURN
152 M$="BLOCKED BY MOUNTAINS":GOSUB1:RETURN
153 LINE(255,167)-(168,160),0,BF:M$=";HIT ENTER":GOSUB1:GOSUB151:LINE(255,167)-(168,160),0,BF:RETURN
154 IFCM THENLINE(167,191)-(0,168),0,BF:ELSEGOSUB6:M$="COMMAND:":CM=1:GOSUB1
155 X$=INKEY$:IFX$=""THEN155ELSEX=ASC(X$)
156 IFX=73THENONSGN(IN)+1GOTO173,175
157 IFX=76THENONSGN(IN)+1GOTO180,182
158 IFX=74THENONSGN(IN)+1GOTO184,186
159 IFX=75THENONSGN(IN)+1GOTO188,190
160 IFX=13THENONSGN(IN)+1GOTO191,195
161 IFX=65THENONSGN(IN)+1GOTO166,203
162 IFX=32THENM$="PASS":GOSUB1:GOTO166
163 IFX=83THEN236
164 IFX=80THENIFPA=1THENPA=0:M$="UNPAUSED":GOSUB1:GOTO154ELSEPA=1:M$="PAUSED":GOSUB1:GOTO154
165 GOTO194
166 P(0)=P(0)-1+SGN(IN)*.9:IFP(0)<0THENGOSUB6:C(0)=0:M$="YOU HAVE STARVED":GOSUB1:GOTO168
167 P(0)=INT(P(0)*10)/10
168 IFC(0)<=0THEN277
169 IFIN>0THENGOSUB245:IFC(0)<=0THEN277
170 IFIN=0THENGOSUB28
171 IFIN>0THENGOSUB38
172 LINE(255,191)-(184,168),0,BF:M$=STR$(INT(P(0)*10+0.5)/10):GOSUB1:DRAW"BM184,176":M$=STR$(C(0)):GOSUB1:DRAW"BM184,184":M$=STR$(C(5)):GOSUB1:GOTO154
173 M$="NORTH":GOSUB1:IFT(TX,TY-1)=1THENGOSUB152:GOTO166
174 TY=TY-1:GOTO166
175 IFD(PX+DX,PY+DY)<>1THENIFD(PX+DX,PY+DY)<10THENPX=PX+DX:PY=PY+DY
176 M$="FORWARD":GOSUB1:IFD(PX,PY)=2THENM$="A TRAP":GOSUB1:C(0)=C(0)-INT(RND(0)*IN+3):MR=1:IN=IN+1:M$="FALL TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:CM=0:GOSUB6:GOTO166
177 Z=0:IFD(PX,PY)=5THEND(PX,PY)=0:Z=INT(RND(0)*5*IN+IN):M$=MID$(STR$(Z),2)+" GP":GOSUB1:C(5)=C(5)+Z
178 IFZ>0THENZ=INT(RND(0)*6):M$="AND A "+W$(Z):GOSUB1:P(Z)=P(Z)+1:GOTO218
179 GOTO166
180 M$="EAST":GOSUB1:IFT(TX+1,TY)=1THENGOSUB152:GOTO166
181 TX=TX+1:GOTO166
182 M$="RIGHT":GOSUB1:IFDX THENDY=DX:DX=0:GOTO166
183 DX=-DY:DY=0:GOTO166
184 M$="WEST":GOSUB1:IFT(TX-1,TY)=1THENGOSUB152:GOTO166
185 TX=TX-1:GOTO166
186 M$="LEFT":GOSUB1:IFDX THENDY=-DX:DX=0:GOTO166
187 DX=DY:DY=0:GOTO166
188 M$="SOUTH":GOSUB1:IFT(TX,TY+1)=1THENGOSUB152:GOTO166
189 TY=TY+1:GOTO166
190 M$="TURN AROUND":GOSUB1:DX=-DX:DY=-DY:GOTO166
191 IFT(TX,TY)=3THENGOSUB304:GOSUB309:GOSUB7:GOTO166
192 IFT(TX,TY)=4ANDIN=0THENM$="GO DUNGEON":GOSUB1:M$="PLEASE WAIT":GOSUB1:IN=1:GOSUB133:DX=1:DY=0:PX=1:PY=1:GOTO166
193 IFT(TX,TY)=5THEN282
194 M$="HUH":GOSUB1:GOTO154
195 IFD(PX,PY)<>7ANDD(PX,PY)<>9THEN197
196 IN=IN+1:M$="GO DOWN TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:MR=1:GOTO166
197 IFD(PX,PY)<>8THENM$="HUH":GOSUB1:GOTO166
198 IFIN=1THENM$="LEAVE DUNGEON":GOSUB1:IN=0:GOTO200
199 IN=IN-1:M$="GO UP TO LEVEL"+STR$(IN):GOSUB1:GOSUB133:MR=1
200 IFIN=0THENM$="GAINED"+STR$(LK)+" HP":GOSUB1:C(0)=C(0)+LK:LK=0
201 GOTO166
202 M$="NOT OWNED":GOSUB1:GOTO203
203 MN=0:DA=0:M$="ATTACK WEAPON:":GOSUB1:GOSUB4:CM=0:GOSUB6
204 IFQ$="R"THENDA=10:M$="RAPIER":GOSUB1:IFP(1)<1THEN202
205 IFQ$="A"THENDA=5:M$="AXE":GOSUB1:IFP(2)<1THEN202
206 IFQ$="S"THENDA=1:M$="SHIELD":GOSUB1:IFP(3)<1THEN202
207 IFQ$="B"THENDA=4:M$="BOW":GOSUB1:IFP(4)<1THEN202
208 IFQ$="M"THENM$="MAGIC AMULET":GOSUB1:GOTO225
209 IFQ$="B"ORQ$="R"THENIFPT$="M"THENM$="A MAGE CANNOT":GOSUB1:GOTO203
210 IFDA=0THENM$="HANDS":GOSUB1
211 IFDA=5ORDA=4THEN220
212 MN=D(PX+DX,PY+DY)/10:MN=INT(MN)
213 IFMN<1ORC(2)-RND(0)*25<IN+MN THENM$="MISS":GOSUB1:GOTO218
214 M$="HIT":GOSUB1:DA=(RND(0)*DA+C(1)/5):Z(MN,1)=Z(MN,1)-DA:Z=INT(Z(MN,1)):IFZ<1THEN216
215 M$=LEFT$(M$(MN),9)+" HP"+STR$(Z):GOSUB1
216 IFZ(MN,1)<1THENM$=LEFT$(M$(MN),13)+" KILLED":GOSUB1:DA=INT(MN+IN):M$="GP WON"+STR$(DA):GOSUB1:C(5)=INT(C(5)+DA):D(L(MN,0),L(MN,1))=D(L(MN,0),L(MN,1))-10*MN:Z(MN,0)=0
217 LK=LK+INT(MN*IN/2):IFMN=TA THENTA=-TA
218 IFPA=1THENGOSUB153
219 GOTO166
220 IFDA=5THENM$="THROW OR SWING:":GOSUB1:GOSUB4:CM=0:GOSUB6:IFQ$<>"T"THENM$="SWING":GOSUB1:GOTO212
221 IFDA=5THENM$="THROW":GOSUB1:P(2)=P(2)-1
222 FORY=1TO5:IFPX+DX*Y<1ORPX+DX*Y>9ORPY+DY*Y>9ORPY+DY*Y<0THEN213
223 MN=D(PX+DX*Y,PY+DY*Y):MN=INT(MN/10):IFMN>0THEN213
224 NEXT:GOTO213
225 IFP(5)<1THEN202
226 IFPT$="F"THENQ=RND(4):GOTO229
227 M$="1 UP 2 DOWN LADDER":GOSUB1:M$="3 KILL 4 BAD;CHOICE:":GOSUB1:GOSUB4:CM=0:GOSUB6:Q=VAL(Q$):IFQ<1ORQ>4THEN227
228 IFRND(0)>.75THENM$="LAST CHARGE":GOSUB1:P(5)=P(5)-1:GOSUB6
229 ONQ GOTO230,231,232,233
230 M$="UP":GOSUB1:D(PX,PY)=8:GOTO166
231 M$="DOWN":GOSUB1:D(PX,PY)=7:GOTO166
232 M$="MAGIC ATTACK":GOSUB1:DA=10+IN:GOTO222
233 ONRND(3)GOTO234,235:M$="YOU HAVE BEEN":GOSUB1:M$="TURNED INTO A TOAD":GOSUB1:FORT=1TO4:C(T)=3:NEXT:GOTO218
234 M$="YOU HAVE BECOME":GOSUB1:M$="A LIZARD MAN":GOSUB1:FORY=0TO4:C(Y)=INT(C(Y)*2.5):NEXT:GOTO218
235 M$="BACKFIRE":GOSUB1:C(0)=C(0)/2:GOTO218
236 GOSUB304:GOSUB150:GOSUB7:GOTO166
237 NM=0:FORX=1TO10:Z(X,0)=0:Z(X,1)=X+3+IN:IFX-2>IN ORRND(0)>.4THEN241
238 L(X,0)=INT(RND(0)*9+1):L(X,1)=INT(RND(0)*9+1):IFD(L(X,0),L(X,1))THEN238
239 IFL(X,0)=PXANDL(X,1)=PYTHEN238
240 D(L(X,0),L(X,1))=X*10:Z(X,0)=1:NM=NM+1:Z(X,1)=X*2+IN*2*LP
241 NEXT:RETURN
242 LINE(C-28/D,B-41/D)-(C+30/D,B-55/D),1:LINE(C+28/D,B-58/D)-(C+22/D,B-56/D),1:LINE-(C+22/D,B-53/D),1:LINE-(C+28/D,B-52/D),1:LINE-(C+34/D,B-54/D),1:LINE-(C+20/D,B-50/D),1:LINE-(C+26/D,B-47/D),1
243 LINE(C+10/D,B-58/D)-(C+10/D,B-61/D),1:LINE-(C+4/D,B-58/D),1:LINE(C-10/D,B-58/D)-(C-10/D,B-61/D),1:LINE-(C-4/D,B-58/D),1:LINE(C+40/D,B-9/D)-(C+50/D,B-12/D),1:LINE-(C+40/D,B-7/D),1
244 LINE(C-8/D,B-25/D)-(C+6/D,B-7/D),1:LINE-(C+28/D,B-7/D),1:LINE-(C+28/D,B-9/D),1:LINE-(C+20/D,B-9/D),1:LINE-(C+6/D,B-25/D),1:GOTO131
245 FORM=1TO10:IFZ(M,0)=0THEN276
246 RA=SQR((PX-L(M,0))^2+(PY-L(M,1))^2)
247 IFZ(M,1)<IN*LPTHEN250
248 IFRA<1.3THEN265
249 IFM=8ANDRA<3THEN276
250 P=SGN(PX-L(M,0)):O=SGN(PY-L(M,1))
251 IFZ(M,1)<IN*LP THENP=-P:O=-O
252 IFO=0THEN255
253 T=D(L(M,0),L(M,1)+O):IFT=1ORT>9ORT=2THEN255
254 P=0:GOTO257
255 O=0:IFP=0THEN257
256 T=D(L(M,0)+P,L(M,1)):IFT=1ORT>9ORT=2THENP=0:GOTO261
257 D(L(M,0),L(M,1))=D(L(M,0),L(M,1))-10*M
258 IFL(M,0)+P=PX ANDL(M,1)+O=PY THEN276
259 L(M,0)=L(M,0)+P:L(M,1)=L(M,1)+O
260 D(L(M,0),L(M,1))=D(L(M,0),L(M,1))+10*M
261 IFP<>0ORO<>0THEN276
262 IFZ(M,1)<IN*LP THENIFRA<1.3THEN265
263 IFZ(M,1)<IN*LP THENZ(M,1)=Z(M,1)+M+IN
264 GOTO276
265 IFM=2ORM=7THEN271
266 CM=0:GOSUB6:M$=LEFT$(M$(M),9)+" ATTACKING":GOSUB1
267 IFRND(0)*20-SGN(P(3))-C(3)+M+IN<0THENM$="MISS":GOSUB1:GOTO269
268 M$="HIT":GOSUB1:C(0)=C(0)-INT(RND(0)*M+IN)
269 IFPA=1THENGOSUB153:CM=0:GOSUB6
270 GOTO276
271 IFRND(0)<.5THEN266
272 IFM=7THENP(0)=INT(P(0)/2):GOSUB6:M$="GREMLIN STOLE FOOD":GOSUB1:GOTO269
273 IFP(0)+P(1)+P(2)+P(3)+P(4)+P(5)<1THEN276
274 Z=INT(RND(0)*6):IFP(Z)<1THEN274
275 GOSUB6:M$=LEFT$("THIEF STOLE "+W$(Z),20):GOSUB1:P(Z)=P(Z)-1:GOTO269
276 NEXT:RETURN
277 FORG=1TO2500:NEXT:SCREEN0,0:CLS:PRINTTAB(5)"WE MOURN THE PASSING OF":IFLEN(PN$)>12THENPN$=""
278 IFPN$=""THENPN$="THE PEASANT"
279 PN$=PN$+" AND HIS COMPUTER"
280 PRINTTAB((32-LEN(PN$))/2)PN$:PRINT:M$="FOR RESURRECTION, PRESS R":GOSUB5
281 IFINKEY$="R"THENRUNELSE281
282 SCREEN0,0:CLS:G1=0:IFPN$<>""THEN289
283 PRINT"WELCOME PEASANT TO THE HALLS OF":PRINT"MIGHTY LORD BRITISH!"
284 PRINT:INPUT"WHAT IS THY NAME";PN$
285 PRINT:PRINT"THY FIRST TASK IS TO GO TO":PRINT"THE DUNGEONS AND RETURN AFTER":TA=INT(C(4)/3):IFTA>10THENTA=10
286 PRINT"KILLING A(N) "M$(TA)
287 PRINT:PRINT"ALSO I, BRITISH, HAVE INCREASED":PRINT"EACH OF THY ATTRIBUTES BY ONE!"
288 GOSUB150:FORX=0TO5:C(X)=C(X)+1:NEXT:CLS:GOTO166
289 IFTA>0THENPRINTPN$"!?":PRINT"WHY HAST THOU RETURNED?":PRINT"THY QUEST IS TO KILL A(N)":PRINTM$(TA):GOSUB150:CLS:GOTO166
290 PRINT"THY QUEST IS ACOMPLISHED!":IFABS(TA)=10THEN293
291 PRINT"BUT IT IS STILL NOT ENOUGH TO":PRINT"BECOME A KNIGHT. THOU MUST NOW":TA=ABS(TA)+1:PRINT"KILL A(N) "M$(TA)
292 GOTO287
293 PRINT:PN$="LORD "+PN$:PRINTPN$","
294 PRINT"YOU HAVE EARNED A KNIGHTHOOD!"
295 PRINT:IFLP=10THENPRINT"CONGRATULATIONS!":GOTO288
296 PRINT"BE THOU FOOLHEARTY ENOUGH TO":PRINT"TRY DIFFICULTY LEVEL"STR$(LP+1)"?":GOTO288
297 SCREEN0,0:CLS:G1=0:INPUT"TYPE THY LUCKY NUMBER";Q$:LN=VAL(Q$):PRINT:INPUT"LEVEL OF PLAY (1-10)";Q$:LP=INT(VAL(Q$)):IFLP<1ORLP>10THEN297
298 G=RND(-ABS(LN)):FORX=0TO5:READC$(X):NEXT:FORX=1TO10:READM$(X):NEXT
299 DATA"HP ","STR","DEX","STA","WIS","GP "
300 DATA"SKELETON","THIEF","GIANT RAT","ORC","VIPER","CARRION CRAWLER","GREMLIN","MIMIC","DAEMON","BALROG","FOOD","RAPIER","AXE","SHIELD","BOW AND ARROWS","MAGIC AMULET"
301 FORX=0TO5:C(X)=INT(SQR(RND(0))*21+4):NEXT:PRINT@128,;:FORX=0TO5:PRINTC$(X)C(X):NEXT:PRINT@352,"PLAY THOU WITH THESE QUALITIES?":GOSUB4:IFQ$<>"Y"THEN301
302 PRINT@352,"BE THOU A FIGHTER OR A MAGE?":GOSUB4:IFQ$<>"M"ANDQ$<>"F"THEN302
303 PT$=Q$:DIMW$(5):FORX=0TO5:READW$(X):NEXT:GOSUB304:GOSUB309:RETURN
304 SCREEN0,0:CLS:G1=0
305 PRINT@2,"STAT'S":PRINT@20,"WEAPONS":FORX=0TO5:PRINT@(X+1)*32,C$(X)C(X)TAB(11)RIGHT$("     "+STR$(INT(P(X))),5)TAB(17)W$(X);:NEXT
306 PRINT@480,;:M$="Q=QUIT":GOSUB5:PRINT"                         ";
307 PRINT@256,"PRICE"TAB(9)"DAMAGE"TAB(17)"ITEM"
308 PRINT"1 FOR 10"TAB(9)"N/A"TAB(17)W$(0):PRINT"8"TAB(9)"1-10"TAB(17)W$(1):PRINT"5"TAB(9)"1-5"TAB(17)W$(2):PRINT"6"TAB(9)"1"TAB(17)W$(3):PRINT"3"TAB(9)"1-4"TAB(17)W$(4):PRINT"15"TAB(9)"?"TAB(17)W$(5):RETURN
309 PRINT@224,"":PRINT@224,;:M$="WHAT DOST THOU WANT? ":GOSUB5:GOSUB4:IFQ$="Q"THENCLS:G1=0:RETURN
310 Z=-1
311 IFQ$="F"THENPRINT"FOOD":Z=0:PR=1
312 IFQ$="R"THENPRINT"RAPIER":Z=1:PR=8
313 IFQ$="A"THENPRINT"AXE":Z=2:PR=5
314 IFQ$="S"THENPRINT"SHIELD":Z=3:PR=6
315 IFQ$="B"THENPRINT"BOW":Z=4:PR=3
316 IFQ$="M"THENPRINT"AMULET":Z=5:PR=15
317 PRINT@487,;:IFZ=-1THENPRINT"WE DON'T HAVE THAT.     ";:GOTO309
318 IF(Q$="R"ORQ$="B")ANDPT$="M"THENPRINT"MAGES CAN'T USE THAT!   ";:GOTO309
319 IFC(5)-PR<0THENPRINT"THOU CANNOT AFFORD THAT.";:GOTO309
320 IFZ=0THENP(Z)=P(Z)+9
321 P(Z)=P(Z)+1:C(5)=C(5)-PR:GOSUB305:GOTO309
800 DIM N$(12),L$(26):I=64:J=47
810 N$(0)="BR6":N$(1)="BDD4BDBR2RBR2BUU4BUBL2LBR4":N$(2)="BR2NGD6NLR2BU6BR2":N$(3)="BDER2FDG4R4BU6BR2":N$(4)="BDER2FDGNLFDGL2HBR6BU5":N$(5)="BD3NR4E3ND6BR3":N$(6)="NR4D3R3FDGL2HBR6BU5":N$(7)="BDNED4FR2EUHNL3BU3NL2NFBR3"
820 N$(8)="BD6UE4UNL4BR2":N$(9)="BR2GDFNR1GDFR1EUHEUHNL1BR3":N$(10)="BD5FR2EU4HL2GDFR3BU3BR2":N$(11)="BDER3DG2DBD2R1BR3BU6":N$(12)="BD3R3BU3BR3"
830 L$(0)="BR6":L$(1)="U5BEBR1R1FD2NL4D3BR2":L$(2)="U6R3FDGNL3FDGNL3BR3":L$(3)="BRNR2HU4ER2FBD4GBR3":L$(4)="U6R3FD4GNL3BR3":L$(5)="NR4U3NR3U3R4BD6BR2":L$(6)="U3NR3U3R4BD6BR2":L$(7)="BRNR2HU4ER2FBD3NL2DGBR3":L$(8)="U3NU3R4NU3D3BR2"
840 L$(9)="BRRU6NLR2BD6RL2BR3"
850 L$(10)="BU2DFR2ENU5BDBR2":L$(11)="U3NU3RNE3F3BR2":L$(12)="NU6R4BR2":L$(13)="U6F2NDE2D6BR2":L$(14)="U6R3BR1BDD5BR2":L$(15)="BRBHU4BEBRRFD4GNLBR3":L$(16)="U6R3FDGNL3BD3BR3":L$(17)="BRBHU4BEBRRFD4GNLRBR2":L$(18)="U6R3FDGLNL2F2DBR2"
860 L$(19)="BUFR2EUHL2HUER2FBD5BR2":L$(20)="BR2U6NL2R2BD6BR2":L$(21)="BUNU5FR2ENU5DBR2":L$(22)="BU2NU4F2BR2BU2NU4BD2BR2":L$(23)="NU6UR3UDR2U5D6BR1":L$(24)="UE4UBL4DF4DBU2C0U2D2BD2C1BR2":L$(25)="BU4UNUF2ND3E2NUBD5BR2":L$(26)="BU6R4DG4DR4BR2":RETURN
