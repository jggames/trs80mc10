0 CLEAR400:C1=RND(-(PEEK(9)*256+PEEK(10))):POKE49151,64:POKE17026,118:GOTO50
1 C1=1:CC=32
2 IFMID$(A$,CC,1)<>""ANDMID$(A$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(A$,C1,CC-C1):C1=CC+1:CC=C1+31:ON1-(C1>LEN(A$))GOTO2:RETURN
5 PRINT"OK":GOTO650
6 GOSUB1:GOTO650
7 FORC1=1TO2500:NEXT:RETURN
9 FORC1=1TOCC:PRINT"ÿ";:NEXT:RETURN
10 PRINT@492,"hit a key";:ON1-(INKEY$<>"")GOTO10:RETURN
30 PRINT@0,"PLAYER IS: "A$(Z):GOTO45
40 PRINT@7,"THE HIGH MOUNTAINS"
45 FORC1=16384TO16415:POKEC1,PEEK(C1)-64:NEXT:RETURN
50 CLS:GOSUB40:PRINT" IF YOU ARE EITHER A MASTER OR ATRIPOD THEN THE OBJECT OF YOUR"
114 PRINT"GAME IS TO DESTROY ALL THE HUMANCHARACTERS WHO ARE NOT BEING","CONTROLLED BY OTHER PLAYERS."
117 PRINT" IF YOU ARE NOT A MASTER OR A","TRIPOD THEN YOU MUST COLLECT THEAPPROPRIATE OBJECTS AND CARRY"
120 PRINT"OUT THE TASKS TO ENABLE YOU TO","DESTROY THE GOLDEN CITY."," WHEN YOU THINK YOU HAVE THE"
123 PRINT"REQUIRED OBJECTS IN THEIR","CORRECT FORM, MOVE TO THE GOLDENCITY AND FIRE AWAY.":GOSUB9000:GOSUB10
141 CLS:GOSUB40
142 PRINT"COMMANDS ARE:",,"CLIMB",,"RIDE ... DIRECTION","KILL ...","FIGHT ...","EXAMINE","LOOK","INVENTORY","GET","TAKE",
144 PRINT"SWIM","FILL ... WITH ...","DROP","UNLOCK","FIRE","LOAD","SAVE","HELP","TALK",,"AND DIRECTIONS TO MOVE.":GOSUB10
150 CLS:PRINT@8,"THE CHARACTERS":GOSUB45:FORJ=0TO29:PRINT@32+J*16,A$(J+1);:NEXT:GOSUB7
170 PRINT@0,"":PRINT@0,"NUMBER OF PLAYERS";:INPUTA$:NO=VAL(A$):IFNO<1ORNO>5THEN170
190 FORJ=1TONO
200 PRINT@0,"":PRINT@0,"PLAYER";J;:INPUTS$(J)
202 IFJ<2THEN250
210 FORI=1TOJ-1:IFLEFT$(S$(J),4)=LEFT$(A$(A(T+I)),4)THEN200
220 NEXT
250 FORK=1TO30:IFLEFT$(S$(J),4)=LEFT$(A$(K),4)THENA(T+J)=K:K=30:NEXT:GOTO270
260 NEXT:GOTO200
270 NEXT:H=2:CLS:PRINT@480,"";
280 FORQ=1TONO:Z=A(T+Q):D$(1)="LOOK"
281 F=1:FORJ=1TONO:IFA(E+A(T+J))>0THENF=0
282 NEXTJ:IFFTHENGOSUB1055:A$="NO-ONE HAS WON.":GOSUB1:GOTO1030
283 IFA(E+Z)<=0THENNEXTQ:GOTO280
284 IFNO>1THENCC=38:GOSUB9:PRINT" NEXT PLAYER'S TURN ";:GOSUB9
285 FORR=1TOA(G+Z):IFA(E+Z)<=0THENNEXTQ:GOTO280
287 F=1:K=RND(30):FORU=1TONO:IFK=A(T+U)THENF=0
288 NEXTU:IFF=1ANDA(K)<105THENGOSUB8900
290 H=1:IFD$(1)="LOOK"ORD$(1)="N"ORD$(1)="S"ORD$(1)="E"ORD$(1)="W"ORD$(1)="SWIM"ORD$(1)="RIDE"THEN310
297 IFD$(1)="CLIMB"ORD$(1)="NORTH"ORD$(1)="SOUTH"ORD$(1)="EAST"ORD$(1)="WEST"ORD$(1)="GO"THEN310
300 GOTO380
310 RESTORE:FORC1=1TOA(Z):READV$,V,W,X,Y:NEXT:A$="YOU ARE "+V$:GOSUB1:IFA(Z)=78ANDNM=0THENA$="THE SHED IS LOCKED.":GOSUB1
342 IFA(Z)=78ANDNM=1THENA$="THE SHED IS NOT LOCKED.":GOSUB1
343 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)ANDN(J)<M(J)THENA$="THE CANAL IS TO YOUR EAST.":GOSUB1
345 IFN(J)=A(Z)ANDM(J)<N(J)THENA$="THE CANAL IS TO YOUR WEST.":GOSUB1
346 NEXTJ
348 A$="YOU CAN MOVE: "
350 IFV=1THENA$=A$+"NORTH "
352 IFW=1THENA$=A$+"SOUTH "
354 IFX=1THENA$=A$+"EAST "
356 IFY=1THENA$=A$+"WEST "
357 IFA$<>""THENGOSUB1
370 FORJ=1TO15:IFA(S+J)=A(Z)THENA$="YOU CAN SEE: "+B$(J):GOSUB1
371 NEXTJ
376 O=1:FORJ=1TO30:IFA(J)=A(Z)ANDJ<>ZTHENO=O+1:A$="YOU CAN SEE: "+A$(J):GOSUB1
379 NEXTJ
380 II=RND(10-O):IFNOT(II<2)THEN390
381 FF=0:GOSUB8700:IFFF=1THEN650
382 IFFF=2THEN2800
390 IFFF=3THEN2570
400 PRINT@511," ";:CC=63:GOSUB9:POKE16895,255:GOSUB30:PRINT@416,"command";:INPUTC$
410 PRINT@416,"COMMAND";:PRINT@448,"";:FORC1=1TO63:PRINT" ";:NEXT:POKE16895,96:PRINT@448,"";:IFC$=""THEN460
430 BE=1:PO=1:FORJ=1TOLEN(C$):IFMID$(C$,J,1)=" "THEND$(PO)=MID$(C$,BE,J-BE):BE=J+1:PO=PO+1:IFPO>4THENJ=255
450 NEXTJ:D$(PO)=MID$(C$,BE):IFPO<5THEND$(PO+1)=""
460 IFD$(1)="CLIMB"THEN1500
480 IFD$(1)="RIDE"THEN1800
500 IFD$(1)="KILL"ORD$(1)="FIGHT"THEN2400
510 IFLEFT$(D$(1),4)="EXAM"THEN3200
520 IFD$(1)="LOOK"THEN310
530 IFLEFT$(D$(1),3)="INV"THEN3800
540 IFD$(1)="GET"ORD$(1)="TAKE"THEN4100
570 IFD$(1)="SWIM"THEN4700
580 IFD$(1)="FILL"THEN5000
590 IFD$(1)="DROP"THEN5300
599 IFD$(1)="LOAD"THEN6840
600 IFD$(1)="SAVE"THEN6800
601 IFD$(1)="N"ORD$(1)="NORTH"THEN5600
602 IFD$(1)="S"ORD$(1)="SOUTH"THEN5700
603 IFD$(1)="E"ORD$(1)="EAST"THEN5800
604 IFD$(1)="W"ORD$(1)="WEST"THEN5900
605 IFD$(1)="UNLOCK"THEN6600
606 IFD$(1)="FIRE"THEN8800
607 IFD$(1)="HELP"THENA$=H$(H):GOSUB1:GOTO380
608 IFD$(1)="GO"THEND$(1)=D$(2):GOTO601
610 IFD$(1)="QUIT"THEN1029
612 IFD$(1)="JUMP"ANDD$(2)="RIVER"THENPRINT"YOU CAN'T DO THAT.":H=3:GOTO380
614 IFD$(1)="TALK"THEN1060
619 PRINT"I DON'T UNDERSTAND.":H=1:GOTO380
650 FF=0:FORJ=17TO30:F=0:FORU=1TONO:IFJ=A(T+U)THEN660
652 NEXTU:IFFTHEN660
655 IFA(E+J)>0THENFF=J
660 NEXTJ:IFFFTHENJ=FF:GOTO770
662 IFZ<17THENGOSUB1055:A$=A$(Z)+" HAS WON!":GOSUB1:GOTO1030
770 NEXTR:NEXTQ:GOTO280
780 Z=A(T+Q):D$(1)="LOOK":GOTO281
1000 A$=A$+" DOES NOT MOVE BECAUSE YOU ARE NOT NEAR ANY WATER SO YOU CLIMB BACK OUT.":GOTO6
1010 A$=A$+" IS NOT HERE.":GOTO6
1022 FORK=1TO15:IFA(S+K)=Z+200THENA(S+K)=A(Z):GOSUB1028
1023 NEXTK:A(P+Z)=0:RETURN
1025 FORK=1TO15:IFA(S+K)=J+200THENA(S+K)=A(J):GOSUB1028
1027 NEXTK:A(P+J)=0:RETURN
1028 PRINTB$(K);" WAS DROPPED.":RETURN
1029 ON1-(D$(2)="ALL")GOTO1050:GOSUB1055
1030 SOUND30,1:SOUND20,1:SOUND10,1:POKE49151,64:GOSUB7:PRINT:PRINT"PRESS space TO PLAY AGAIN"
1045 ON1-(INKEY$=" ")GOTO1045:RUN
1050 PRINT"YOU HAVE QUIT!":GOSUB1022:A(Z)=999:A(E+Z)=0
1051 IFNO=1THEN1030
1052 NEXTQ:GOTO280
1055 CC=42:GOSUB9:PRINT"END OF GAME.";:GOSUB9:RETURN
1060 A$="HE SAYS '"+Q$(RND(5))+"'":GOTO6
1200 IFD$(3)="BOAT"THEN1300
1220 IFLEFT$(D$(3),4)="RIVE"THEN1400
1230 A$="THERE IS NOTHING TO CLIMB INTO HERE.":GOTO6
1300 A$="THE BOAT":IFA(S+13)<>A(Z)THEN1010
1310 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN1330
1320 NEXTJ:GOTO1000
1330 J=18:NEXTJ:A(Z)=M:A(S+13)=M
1360 IFA(E+Z)>4THENA$="YOU'VE CROSSED THE RIVER IN SAFETY.":GOTO6
1370 PRINT"YOU CROSSED THE RIVER, BUT ARE WEAK AND HAVE LOST SOME MANEUVERABILITY.":GOTO6
1400 A$="THE RIVERBOAT":IFA(S+14)<>A(Z)THEN1010
1410 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN1440
1420 NEXTJ:GOTO1000
1440 J=18:NEXTJ:A(Z)=M:A(S+14)=M:GOTO1360
1500 IFD$(2)="IN"THEN1200
1510 A$="THERE IS NOTHING HERE TO CLIMB.":GOTO6
1800 ON1-(Z>16)GOTO1840:IFD$(2)="IN"THEND$(2)=D$(3):D$(3)=D$(4)
1810 IFD$(2)="HORSE"THEN1880
1820 IFD$(2)="CAR"THEN1990
1830 IFD$(2)="TRAIN"THEN2060
1835 IFD$(2)="BOAT"ORLEFT$(D$(2),4)="RIVE"THEND$(3)=D$(2):GOTO1200
1840 A$="YOU ARE NOT ABLE TO RIDE THAT OBJECT.":GOTO6
1880 IFA(Z)<>A(S+9)THENA$="THE HORSE":GOTO1010
1885 F=9:FORJ=1TO4
1890 RESTORE:FORC1=1TOA(Z):READV$,V,W,X,Y:NEXTC1:IFLEFT$(D$(3),1)="N"ANDV=1THENA(Z)=A(Z)-13
1900 IFLEFT$(D$(3),1)="S"ANDW=1THENA(Z)=A(Z)+13
1910 IFLEFT$(D$(3),1)="E"ANDX=1THENA(Z)=A(Z)+1
1920 IFLEFT$(D$(3),1)="W"ANDY=1THENA(Z)=A(Z)-1
1930 NEXTJ:A(S+F)=A(Z):GOTO650
1990 IFA(Z)<>A(S+8)THENA$="THE CAR":GOTO1010
2000 F=8:FORJ=1TO6:GOTO1890
2060 IFA(Z)<>A(S+10)THENA$="THE TRAIN":GOTO1010
2070 IFLEFT$(D$(3),1)="E"ORLEFT$(D$(3),1)="W"THENA$="THE RAILWAY DOES NOT GO IN THAT DIRECTION.":GOTO6
2090 F=10:FORJ=1TO9:GOTO1890
2400 D$(2)=D$(2)+" "+D$(3):FORJ=1TO30:IFA(J)=A(Z)ANDLEFT$(D$(2),4)=LEFT$(A$(J),4)THEN2440
2420 NEXTJ:A$=D$(2):GOTO1010
2440 F=J:J=30:NEXTJ:J=F:FORK=1TONO:IFA(T+K)=JTHEN2599
2450 NEXTK
2480 GOSUB2580
2485 IFI$="Y"THEN2500
2490 IFI$="N"THENA$="NO BLOWS WERE TAKEN.":GOTO6
2500 K=RND(5)-3:KK=RND(5)-3:DD=(A(E+Z)+KK)-(A(E+J)+K):IFDD>-2THEN2520
2512 A(E+Z)=A(E+Z)-4:PRINT"YOU WERE BADLY INJURED.":IFA(E+Z)<=0THEN2570
2516 GOTO2480
2520 IFDD<2THEN2530
2522 A(E+J)=A(E+J)-4:A$=A$(J)+" WAS BADLY INJURED.":GOSUB1
2525 IFA(E+J)<=0THENGOSUB8768:GOTO650
2527 GOTO2480
2530 IFDD=-1THENA$="YOU WERE SLIGHTLY INJURED":A(E+Z)=A(E+Z)-1:GOTO2560
2540 IFDD=1THENA$=A$(J):GOSUB2678:A(E+J)=A(E+J)-1:GOTO2560
2550 GOSUB2677
2560 GOSUB1:GOTO2480
2570 A$="IN FACT, YOU HAVE BEEN KILLED!":GOSUB1:GOSUB1022:A(Z)=999:NEXTQ:GOTO280
2580 PRINT"DO YOU WANT TO ATTACK? ";
2590 I$=INKEY$:ON1-(I$="Y"ORI$="N")GOTO2590:PRINTI$:RETURN
2599 K=NO:NEXTK
2600 IFA(E+J)<=0THENA$=A$(J)+" WAS KILLED!"
2601 IFA(E+J)<=0THENGOSUB1:GOSUB1025:A(J)=999:NEXTQ:GOTO280
2602 IFA(E+Z)<=0THENJ=Z:A$=A$(J)+" WAS KILLED!":GOTO2601
2605 PRINTA$(J)+" DO YOU WANT TO":PRINT"DROP ALL ITEMS AND CONCEDE? ";:GOSUB8797:IFI$="Y"THEN2800
2635 GOSUB2580:ON1-(I$<>"Y")GOTO2640:ON1-(I$="N")GOTO2635,650
2640 K=RND(10)-3:KK=RND(10)-3:DD=(A(E+Z)+KK)-(A(E+J)+K):IFDD<2THEN2670
2660 A(E+J)=A(E+J)-4:A$=A$(J)+" WAS BADLY INJURED.":GOSUB1:GOTO2600
2670 IFDD>-2THEN2680
2672 A(E+Z)=A(E+Z)-4:A$=A$(Z)+" WAS BADLY INJURED.":GOSUB1:IFA(E+Z)<=0THEN2602
2676 GOTO2600
2677 A$="NO ONE WAS INJURED.":RETURN
2678 A$=A$+" WAS SLIGHTLY INJURED.":RETURN
2680 IFDD=-1THENA$=A$(Z):GOSUB2678:GOSUB1:A(E+Z)=A(E+Z)-1:GOTO2600
2690 IFDD=1THENA$=A$(J):GOSUB2678:GOSUB1:A(E+J)=A(E+J)-1:GOTO2600
2695 GOSUB2677:GOSUB1:GOTO2600
2800 A$=A$(J)+" CONCEDES. NO MORE BLOWS WERE TAKEN.":GOSUB1025:GOTO6
3200 A$="I DON'T SEE ANYTHING UNUSUAL ABOUT "+D$(2)+" "+D$(3)+" "+D$(4):GOTO6
3800 A$="INVENTORY IS: ":IFA(P+Z)=0THENA$=A$+"EMPTY":GOTO6
3830 FORJ=1TO15:IFA(S+J)=200+ZTHENA$=A$+B$(J)+" "
3835 NEXTJ:GOTO6
4100 I$=LEFT$(D$(2),3):IFI$="BAL"ANDNM=0THEN4140
4110 IFI$="GRE"ANDZ<17THENA$="BADDIES CANNOT PICK UP THE GRENADES.":GOSUB1:GOTO650
4120 F=0:FORJ=1TO15:IFI$=LEFT$(B$(J),3)THENF=J
4130 NEXTJ:IFFTHEN4150
4140 GOTO4232
4150 IFA(S+F)>200THEN4232
4160 IFF=8ORF=9ORF=10ORF=13ORF=14THENA$="IT'S TOO HEAVY.":H=5:GOTO6
4170 IFF=15ANDA(S+5)<>200+ZTHENA$="YOU HAVE NOTHING TO CARRY IT IN.":H=4:GOTO6
4180 IFA(P+Z)>1THENA$="YOU HAVE TOO MANY OBJECTS.":GOTO6
4195 IFA(S+F)<>A(Z)THEN4232
4200 A(E+Z)=A(E+Z)+G(F)
4210 A(P+Z)=A(P+Z)+1:A(S+F)=200+Z:GOTO5
4232 A$="I DON'T SEE THAT OBJECT.":GOTO6
4700 FORJ=1TO18:M=M(J):N=N(J):IFN(J)=A(Z)THEN4710
4702 NEXTJ:A$="THERE IS NOWHERE TO SWIM ACROSS HERE!":GOTO6
4710 J=18:NEXT:A$="YOU MADE IT ACROSS.":GOSUB1:IFA(P+Z)=0THEN4730
4712 A$="BUT YOU HAD TO LEAVE ALL THINGS YOU WERE CARRYING BEHIND.":GOSUB1
4720 FORK=1TO15:IFA(S+K)=200+ZTHENA(S+K)=A(Z):A(E+Z)=A(E+Z)-G(K)
4725 NEXTK:A(P+Z)=0
4730 IFA(E+Z)<=0THENGOSUB5400:NEXTR
4750 A(Z)=M:IFN+1=MTHENA$="YOU SWAM EAST.":GOTO6
4760 IFN-1=MTHENA$="YOU SWAM WEST.":GOTO6
5000 IFD$(2)="BALLOON"THEN5020
5011 A$="YOU CANNOT FILL THE "+D$(2)+" WITH "+D$(4)+".":GOTO6
5020 IFLEFT$(D$(4),3)="HYD"THEN5030
5021 A$="YOU CANNOT FILL THE BALLOON WITH THAT.":GOTO6
5030 IFA(S+7)<>200+ZTHENA$="YOU DO NOT HAVE THE BALLOON":GOTO6
5040 IFA(S+15)<>200+ZTHENA$="YOU DO NOT HAVE THE HYDROGEN":GOTO6
5050 A$="THE BALLOON FILLS UP WITH HYDROGEN, A DANGEROUS BUT USEFUL GAS.":GOSUB1
5060 A(P+Z)=1:A(S+15)=0:GOSUB5070:A(171)=-1:GOTO650
5070 B$(7)="INFLATED BALLOON":RETURN
5300 F=0:FORJ=1TO15:IFLEFT$(D$(2),3)=LEFT$(B$(J),3)THENF=J
5320 NEXTJ:IFFTHEN5340
5330 A$="YOU'RE NOT CARRYING THAT!":GOTO6
5340 IFA(S+F)=200+ZTHENA(S+F)=A(Z):A$="OK":GOSUB1:A(E+Z)=A(E+Z)-G(F):GOTO5360
5350 GOTO5330
5360 A(P+Z)=A(P+Z)-1:IFA(E+Z)<=0THENGOSUB5400:J=Z:GOTO2601
5363 GOTO650
5400 A$="YOU DIED FROM LACK OF ENERGY.":GOSUB1:RETURN
5500 A$="YOU CAN'T GO THAT WAY.":H=3:GOSUB1:GOTO348
5600 IFV=0THEN5500
5620 A(Z)=A(Z)-13:GOTO5
5700 IFW=0THEN5500
5720 A(Z)=A(Z)+13:GOTO5
5800 IFX=0THEN5500
5820 A(Z)=A(Z)+1:GOTO5
5900 IFY=0THEN5500
5920 A(Z)=A(Z)-1:GOTO5
6600 IFD$(2)="SHED"THEN6630
6620 A$="IT'S NOT POSSIBLE TO UNLOCK THAT.":GOTO6
6630 IFD$(4)="PEN"THEN6645
6640 A$="YOU CAN'T UNLOCK THE SHED WITH THAT!":GOTO6
6645 IFA(S+3)<>200+ZTHENA$="YOU DON'T HAVE THE PEN.":GOTO6
6660 A$="THE SHED IS NOW UNLOCKED.":NM=1:A(S+7)=78:A(S+3)=0:A(P+Z)=A(P+Z)-1:GOTO6
6800 PRINT"SAVING":A(166)=NO:A(167)=Z:A(168)=Q:A(169)=R:A(170)=NM:SAVE*A,"MTNDATA":GOTO380
6840 PRINT"LOADING":LOAD*A,"MTNDATA":NO=A(166):Z=A(167):Q=A(168):R=A(169):NM=A(170):IFA(171)THENGOSUB5070
6850 GOTO280
7500 DATA"NEAR A SMALL HUT AND A FEW LOGS.",0,1,1,0,"ON A SMALL PATH NEXT TO SOME DARK WOODS.",0,1,1,1
7520 DATA"ON A PATHWAY THAT CONTINUES INTO THE DISTANCE.",0,0,1,1
7530 DATA"ON A PATH. IN THE DISTANCE YOU SEE THE SHMAND FAIR.",0,0,1,1
7540 DATA"AT A DERELICT RAILWAY STATION. THE RAILWAY LINE LEADS SOUTH.",0,1,1,1
7550 DATA"ON A FLAT MISTY MOOR.",0,1,0,1,"ON A FLAT MISTY MOOR.",0,1,1,0
7570 DATA"IN THE EASTERN PART OF WICHESTER.",0,1,1,1,"IN A MYSTERIOUS GRAVEYARD.",0,1,1,1
7590 DATA"IN THE VILLAGE OF WICHESTER. HOUSES LINE THE SIDES OF A DUSTY STREET.",0,1,1,1
7600 DATA"ON THE EAST SIDE OF WICHESTER.",0,1,1,1,"IN A FIELD OF TURNIPS WITH A SCARECROW NEARBY.",0,1,0,1
7620 DATA"AT THE BACK OF A CAVE. YOU CAN SEE THE WORDS 'YEK EHT SI NEP EHT'.",0,1,0,0
7630 DATA"IN A TUNNEL THROUGH THE CLIFFS THAT SURROUND THE DENSE FOREST.",1,1,0,0
7640 DATA"AT THE EDGE OF THE DENSE FOREST.",1,1,1,0
7650 DATA"IN A VALLEY IN A DENSE FOREST, WHICH IS SURROUNDED BY HIGH CLIFFS.",0,0,1,1
7660 DATA"AT THE NORTH EAST EDGE OF THE DENSE FOREST.",0,1,1,1,"IN A LARGE FIELD WITH A RAILWAY RUNNING THROUGH IT.",1,1,1,1
7680 DATA"IN A FIELD OF BARLEY. YOU CAN SEE THE GOLDEN CITY IN THE DISTANCE.",1,0,0,1
7690 DATA"IN THE GOLDEN CITY. THE WALLS OF THE BUILDINGS GLEAM IN THE SUNLIGHT.",1,1,1,0
7700 DATA"IN SOME FIELDS. YOU SEE A FENCE TO YOUR EAST.",1,1,0,1
7710 DATA"NEAR A CHURCH.",1,1,1,0,"IN THE SOUTHERNMOST PART OF WICHESTER.",1,0,1,1
7730 DATA"NEAR WILL'S FATHER'S SAW MILL.",1,1,1,1,"IN A FIELD OF MAIZE.",1,1,0,1,"JUST INSIDE A DARK CAVE.",1,1,0,0
7760 DATA"NEAR A PART OF THE TUNNEL THAT HAS FALLEN IN.",1,0,0,0
7770 DATA"IN A THICK FOREST.",1,1,1,0,"IN THE MIDDLE OF A DENSE FOREST.",0,1,1,1
7790 DATA"IN A DENSE FOREST.",1,1,0,1,"NEXT TO THE RAILWAY.",1,1,1,0
7810 DATA"ON A FLAT MOOR.",0,1,0,1,"ON A PLAIN SOUTH OF THE GOLDEN CITY.",1,1,1,0
7830 DATA"IN A VAST FIELD OF FLOWERS.",1,1,0,1,"IN THE VILLAGE PARK.",1,0,1,0
7850 DATA"AT THE VILLAGE ENTRANCE.",0,1,1,1,"IN THE VILLAGE FARMING AREA.",1,0,1,1
7870 DATA"AT THE EXTREME EDGE OF THE VILLAGE FARMLAND.",1,0,0,1,"AT THE ENTRANCE TO A CAVE.",1,1,0,0
7890 DATA"AT A PART OF THE TUNNEL THAT HAS FALLEN IN.",0,1,0,0
7900 DATA"APPROACHING THE HIGH CLIFFS THAT SURROUND THE FOREST.",1,0,1,0
7910 DATA"NEAR THE HIGH CLIFFS THAT SURROUND THE FOREST.",1,0,1,1,"APPROACHING THE FOREST'S EDGE.",1,0,1,1
7930 DATA"NEAR THE RAILWAY THAT PASSES THE FOREST HERE.",1,1,1,1,"NEAR AN OLD DEEP PIT.",1,1,0,1
7950 DATA"NEXT TO A LARGE TRIPOD FOOTPRINT.",1,1,0,0,"NEXT TO A HERD OF GOATS.",1,1,1,0
7970 DATA"NEXT TO A WALL WITH VINES GROWING ALL OVER IT.",0,1,0,1,"APPROACHING THE VILLAGE.",1,1,1,0
7990 DATA"NEAR A LARGE FIELD.",0,1,1,1,"NEAR A FIELD.",0,1,1,1
8010 DATA"IN A SMALL GLEN. YOU SEE A CAVE TO THE NORTH.",1,1,0,1,"SOUTH OF THE LARGE CLIFFS.",1,1,1,0
8030 DATA"APPROACHING THE LARGE CLIFFS.",0,1,1,1,"APROACHING THE REMAINS OF A TALL PYLON.",0,1,1,1
8050 DATA"IN FRONT OF A SIGN SAYING 'HI;H =+-T.^E'.",0,1,1,1
8060 DATA"IN FRONT OF A LARGE ROCK RESEMBLING A PEN OR IS IT A KEY?",1,1,0,1
8070 DATA"ON A FLAT PLAIN NEXT TO THE CANAL.",1,1,0,0,"NEXT TO SOME HOOF MARKS.",1,1,0,0,"ON A BLEAK MOOR.",1,1,1,0
8100 DATA"ON A BARREN ROCK NEXT TO A SHARP DROP TO THE SOUTH.",1,0,1,1
8110 DATA"STANDING ON A WELL TRODDEN PATH.",1,0,1,1,"VIEWING MOUNTAINS GLISTENING IN THE DISTANCE.",1,0,1,1
8130 DATA"IN THE REMAINS OF A GREAT CITY.",1,1,1,1,"SURROUNDED BY HIGH MOUNTAINS.",1,1,0,1
8150 DATA"STANDING ON A WHITE DOTTED LINE.",1,1,1,0,"NEAR AN OLD WAREHOUSE.",1,1,1,1
8170 DATA"IN A DRY RIVER BED.",1,1,1,1,"NEXT TO THE REMAINS OF A BRIDGE.",1,1,1,1
8190 DATA"OUTSIDE AN OLD RAILWAY STATION.",1,0,1,1,"VIEWING THE CANAL GLISTENING IN THE SUN.",1,1,0,1
8210 DATA"AT A PUB ALONGSIDE THE CANAL.",1,1,1,0,"NEXT TO THE REMAINS OF A LARGE BOAT.",1,1,1,1
8230 DATA"NEAR THE REMAINS OF A TRIPOD.",0,1,1,1,"NEAR THE REMAINS OF A DEAD SHEEP.",0,1,1,1
8250 DATA"NEAR A LARGE MAN-MADE TUNNEL.",0,0,1,1,"IN A SMALL GREEN VALLEY.",1,1,1,1
8270 DATA"VIEWING SOME GLOWING LIGHT BULBS IN AN OLD SHED. YOU WONDER WHAT POWERS THEM.",1,1,0,1
8280 DATA"IN A FLAT AND BARREN AREA.",1,1,1,0,"IN A CITY OF THE ANCIENTS.",1,0,1,1
8300 DATA"SURROUNDED BY LARGE BUILDINGS.",1,1,0,1,"SURROUNDED BY MANY LONG CARRIAGES.",1,1,1,0
8320 DATA"VIEWING THE CITY OF THE ANCIENTS. IT LOOKS DARK AND MYSTERIOUS.",0,0,1,1
8330 DATA"IN A SMALL MEADOW BY THE CANAL.",1,1,0,1,"LISTENING TO THE CANAL HORSES.",1,1,1,0
8350 DATA"ON AN OLD TRACK.",1,1,1,1,"IN A PLACE WHERE THE OLD TRACK DIPS A LITTLE.",1,1,1,1
8370 DATA"IN A VILLAGE BY THE MOUNTAINS.",1,1,1,1,"ON TOP OF A LARGE MOUNTAIN. YOU CAN SEE A LIGHT TO THE EAST.",0,1,1,1
8390 DATA"IN THE FOOTHILLS OF THE MOUNTAINS.",1,1,1,1,"IN A LARGE FLAT VALLEY.",1,1,0,1
8410 DATA"NEAR A TALL CHIMNEY.",1,0,1,0,"IN A LARGE FACTORY.",0,0,1,1
8430 DATA"SURROUNDED BY TALL SKYSCRAPERS.",1,0,1,1,"NEAR SOME LARGE WHITE BUILDINGS.",1,0,1,1
8450 DATA"SUDDENLY IN DARKNESS.",0,0,1,1,"ON A WEIRD FLAT METALLIC SURFACE.",1,0,0,1
8470 DATA"NEXT TO THE CANAL.",1,0,1,0,"VIEWING THE SEA DOWN A LONG DROP.",1,0,0,1
8490 DATA"NEXT TO A STONE WALL.",1,0,1,0,"IN A FOGGY PLACE WHERE THE AIR IS THIN.",1,0,1,1
8510 DATA"SURROUNDED BY HIGH MISTY MOUNTAINS.",1,0,1,1
8520 DATA"IN A FOG SHROUDED PLACE. IT IS RAINING HARD.",1,0,1,1,"IN AN UNBEARABLE DOWNPOUR.",1,0,0,1
8700 F=0:FORJ=30TO17STEP-1:IFA(Z)=A(J)ANDZ<>JANDZ<17THENF=J
8703 NEXTJ:IFFTHENJ=F:GOTO8710
8705 F=0:FORJ=1TO17:IFA(Z)=A(J)ANDJ<>ZANDZ>16THENF=J
8707 NEXTJ:IFFTHENJ=F:GOTO8710
8708 RETURN
8710 F=0:FORK=1TONO:IFA(T+K)=JTHENF=1
8715 NEXTK:IFFTHENRETURN
8720 A$="YOU ARE BEING ATTACKED BY":GOSUB1:A$=A$(J):GOSUB1
8735 K=RND(8)-5
8740 DD=(A(E+Z)+5)-(A(E+J)+K):IFDD>-2THEN8760
8745 A$="YOU WERE BADLY INJURED.":A(E+Z)=A(E+Z)-4:GOSUB1:IFA(E+Z)<=0THENFF=3:RETURN
8750 GOTO8790
8760 IFDD<2THEN8770
8762 A$=A$(J)+" WAS BADLY INJURED.":GOSUB1:A(E+J)=A(E+J)-4
8764 IFA(E+J)<=0THENFF=1:GOTO8768
8767 GOTO8790
8768 A$="IN FACT, "+A$(J)+" IS DEAD.":GOSUB1:GOSUB1025:A(J)=999:RETURN
8770 IFDD=-1THENA$="YOU WERE SLIGHTLY INJURED.":GOSUB1:A(E+Z)=A(E+Z)-1:GOTO8790
8775 IFDD=1THENA(E+J)=A(E+J)-1:A$=A$(J)+" WAS SLIGHTLY INJURED.":GOSUB1:GOTO8790
8790 PRINT"DO YOU WANT TO DROP ALL ITEMS   AND CONCEDE? ";:GOSUB8797:IFI$="Y"THENFF=2:RETURN
8792 GOTO8735
8797 I$=INKEY$:ON1-(I$="Y"ORI$="N")GOTO8797:PRINTI$:RETURN
8800 IFA(Z)<>20THENA$="YOU CANNOT FIRE HERE TO WIN THE GAME.":GOTO6
8804 IFA(S+6)<>200+ZTHEN8815
8810 IFZ>16ANDB$(7)="INFLATED BALLOON"ANDA(S+7)=200+ZTHENGOSUB1055:A$=A$(Z)+" HAS WON!":GOSUB1:GOTO1030
8815 A$="YOU DO NOT HAVE THE RIGHT THING TO FIRE WITH.":GOTO6
8900 RESTORE:FORC1=1TOA(K):READV$,VV,WW,XX,YY:NEXTC1:KK=RND(4)
8920 IFKK=1ANDVV=1THENA(K)=A(K)-13
8930 IFKK=2ANDWW=1THENA(K)=A(K)+13
8935 IFKK=3ANDXX=1THENA(K)=A(K)+1
8940 IFKK=4ANDYY=1THENA(K)=A(K)-1
8950 RETURN
9000 DIMA$(30),A(171),B$(16),G(16),S$(30),D$(5),N(18),M(18),Q$(5),C$(5),H$(5):NM=0:E=30:G=60:P=90:S=150:T=120
9010 FORJ=1TO104:READV$,V,W,X,Y:NEXT:FORX=1TO30:READA$(X):NEXT:FORJ=1TO30:READA(J):NEXT:FORJ=1TO30:READA(E+J):NEXT
9020 FORJ=1TO30:READA(G+J):NEXT:FORX=1TO15:READB$(X):NEXT:FORJ=1TO15:READA(S+J):NEXT:FORJ=1TO15:READG(J):NEXT
9030 FORJ=1TO18:READN(J),M(J):NEXT:FORJ=1TO5:READQ$(J):NEXT:FORJ=1TO5:READH$(J):NEXT:RETURN
9820 DATASMALL MASTER,"DREADFUL MASTER",VIOLENT MASTER,GREEDY MASTER,FAT MASTER,BIG MASTER,UGLY MASTER,TALL TRIPOD,MEAN TRIPOD
9822 DATAWEAK TRIPOD,STRONG TRIPOD,SHINY TRIPOD,SLOW TRIPOD,LITTLE TRIPOD,HUGE TRIPOD,INJURED TRIPOD,LARGE VAGRANT
9824 DATACLEVER VAGRANT,SCRUFFY VAGRANT,NUTTY NOMAD,OLD NOMAD,WEEDY NOMAD,HENRY,BENPOLE,WILLIAM,WISEMAN,MISTER PARKER
9826 DATAOZYMANDIS,CAPTAIN CURTAS,COMTESS
9828 DATA20,20,20,20,20,20,20,6,7,8,19,21,32,33,34,100,94,15,36,4,76,41,10,1,11,78,23,24,73,47,8,7,9,8,7,6,8,8,9,4,9,9,6,5,9
9829 DATA6,6,8,6,7,4,2,8,6,8,7,8,7,6,4,8,5,10,7,4,8,9,8,6,4,9,9,5,6,10,5,7,8,6,7,6,6,8,7,8,5,9,7,7,4
9831 DATASWORD,KNIFE,PEN,WATCH,BAG,GRENADE,BALLOON,CAR,HORSE,TRAIN,FLICKNIFE,ROPE,BOAT,RIVERBOAT,HYDROGEN
9832 DATA1,93,40,81,10,80,0,82,50,5,99,44,7,72,13,17,16,0,0,2,20,15,9,8,6,5,1,1,0,9
9833 DATA6,7,7,6,19,20,20,19,32,33,33,32,45,46,46,45,58,59,59,58,71,72,72,71,84,85,85,84,97,98,98,97,110,111,111,110
9834 DATA"HELLO","NICE WEATHER ISN'T IT","NOTHING","SHUT UP!","GO JUMP IN THE LAKE","TRY OTHER WORDS"
9835 DATA"NO HELP AVAILABLE NOW.","IT IS POSSIBLE TO GET ACROSS.","GET SOMETHING TO CARRY IT IN.","IT'S NO USE TRYING."
