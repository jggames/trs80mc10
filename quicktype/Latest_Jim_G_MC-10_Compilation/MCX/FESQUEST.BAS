0 GOSUB1750:CLEAR800:DIMC1,CC,A$(135),B$(10,4),B(18),O,AN$(4),MA(4):GOSUB2900:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<LEN(M$)THEN2
4 RETURN
5 LV=-1:RETURN
6 LV=1:RETURN
7 READ Q1$,AN$(1),AN$(2),AN$(3),AN$(4):RETURN
10 A$(12)="JIM GERRIE":A$(13)="REV KEITH KOUNOVSKY":A$(14)="CHET SIMPSON":A$(15)="ERIC CRICHLOW"
42 A$(16)="JOEL HEGBERG":A$(17)="MARK MARTIN":A$(18)="MIKE ROWEN":A$(19)="CARL ENGLAND"
50 A$(20)="TIM FRANKLIN":A$(21)="MARGE CHASTEEN":A$(22)="ROGELIO PEREA":A$(23)="JASON TIMMONS"
58 A$(24)="RICHARD TUCKER":A$(25)="DAN KELLER":A$(26)="SANDY BAIR":A$(27)="JIM O'KEEFE"
66 A$(28)="GEORGE BETHEA":A$(29)="SANDY WIEMER":A$(30)="DAVID KEIL":A$(31)="ANDREW WEILER"
74 A$(32)="TOM SCHAEFGES":A$(33)="TERRY JACKSON":A$(34)="SCOTT GRIEPENTROG":A$(35)="NOEMI GARCIA"
82 A$(36)="SHIRLEY STRONG":A$(37)="BARB MORRIS":A$(38)="ANNETTE SWOGER":A$(39)="NEIL BLANCHARD"
90 A$(40)="BYRON CANFIELD":A$(41)="BILL LOGUIDICE":A$(42)="BRENDA CRISLIP":A$(43)="LINDA PODRAZA"
98 A$(44)="JUSTIN WAGNER":A$(45)="BRETT GORDON":A$(46)="STEVE NOSKOWICZ":A$(47)="BILL YAKOWENKO"
106 A$(48)="BILL ASTLE":A$(49)="CARL BOLL":A$(50)="KARL SEFCIK":A$(51)="MICHAEL LEE"
114 A$(52)="MARKUS BLUMRICH":A$(53)="MARY KRAMER":A$(54)="DAVE BARNES":A$(55)="JOHN TURNER"
122 A$(56)="WALLY GROSSMAN":A$(57)="FRANK PITTEL":A$(58)="RICH CRISLIP":A$(59)="ROB GIBONS"
130 A$(60)="BRIAN SCHUBRING":A$(61)="NANCY MYERS":A$(62)="ROY JUSTUS":A$(63)="NANCY HAWKS"
138 A$(64)="JOHN STRONG":A$(65)="GEORGE SCHNEEWEISS":A$(66)="ALLEN HUFFMAN":A$(67)="SALVADOR GARCIA"
146 A$(68)="BRIAN GOERS":A$(69)="JOHN CHASTEEN":A$(70)="BOB SWOGER":A$(71)="JOHN MARK MOBLEY"
154 A$(72)="CHRIS HAWKS":A$(73)="RICH BAIR":A$(74)="DAVE MYERS":A$(75)="NICK MARENTEZ"
162 A$(76)="MARK MARLETTE":A$(77)="BOISY PITRE":A$(78)="AARON WOLFE":A$(79)="JOHN LINVILLE"
170 A$(80)="TONY PODRAZA":A$(81)="MIKE SNYDER":A$(82)="PAUL BURGIN":A$(83)="JEFF VAVASOUR"
178 A$(84)="ROGER TAYLOR":A$(85)="SOCK MASTER":A$(86)="BROTHER JEREMY":A$(87)="STEVE BLYN"
186 A$(88)="DAN DOWNARD":A$(89)="BOB VAN DER POEL":A$(90)="RAMON ZAMORA":A$(91)="DOCTOR ASCII"
204 A$(92)="CRAY AUGSBURG":A$(93)="DAVID DIES":A$(94)="ALAN DEKOK":A$(95)="RICHARD RAMELLA"
212 A$(96)="JAKE COMMANDER":A$(97)="DENNIS KITSZ":A$(98)="DALE LEAR":A$(99)="L. CURTIS BOYLE"
220 A$(100)="TONY DISTEFANO":A$(101)="GREG LAW":A$(102)="FRANK HOGG":A$(103)="KENNETH REIGHARD"
228 A$(104)="WILLIAM BARDEN":A$(105)="MARTY GOODMAN":A$(106)="EDDIE KUNS":A$(107)="DALE PUCKETT"
236 A$(108)="FRED SCERBO":A$(109)="STEVE BJORK":A$(110)="LONNIE FALK":A$(111)="HOWARD LUCKEY"
244 A$(112)="MALCOLM CLEVELAND"
246 A$(113)="COCO BUCK GIFT CERTIFICATES"
248 A$(114)="+ A FD502 ":A$(115)="DISK DRIVE"
252 A$(116)="+ A GLENSIDE":A$(117)=" IDE INTERFACE"
256 A$(118)="+ A BASIC":A$(119)=" MANUAL"
260 A$(120)="+ A LWTOOLS ":A$(121)="ASSEMBLER"
264 A$(122)="+ AN OS-9 ":A$(123)="MANUAL"
268 A$(124)="+ A RAINBOW ":A$(125)="MAGAZINE"
272 A$(126)="+ SOME MORE ":A$(127)="FLOPPIES"
276 A$(128)="+ A LOGICALL":A$(129)=" V7.0 DISK"
280 A$(130)="+ A COCO ":A$(131)="SOFTWARE CD"
284 A$(132)="+ DRIVE-":A$(133)="WIRE CABLE"
290 T=RND(-(PEEK(9)*256+PEEK(10))):FORT=1TORND(40):GOSUB7:NEXT
300 C=0:E=6:L=20:K=0:H=100:F=0:S=10:G=0:O=0:Q$=""
316 GOSUB1750:GOSUB2198:GOTO1760
350 FORZZ=1TO1500:NEXT:RETURN
360 FORZZ=1TO500:NEXT:RETURN
400 IF F=1 THEN 426
402 IF RND(200)<5 THEN GOSUB1000
403 SOUND100,1:SOUND50,1:FORZZ=1TO18:B(ZZ)=0:NEXT:GOSUB350
404 IF L>1 THEN E=6
405 IF L=1 THEN E=7
406 IF L<1 OR G>150 AND S>2 AND RND(100)<5 THEN GOSUB750
407 FOR Z=1 TO 3
408 B(Z)=RND(E)
410 ON1-(B(Z)<3)GOTO412:GOSUB1100
412 NEXT
414 ON1-(B(1)=3 AND B(2)=3 AND B(3)=3)GOTO416,407
416 CLS:GOSUB2020
426 F=0
428 PRINT"WHAT DIRECTION? ";
430 Z=0
432 Z$=INKEY$:IF Z$="" THEN 432
435 IF Z$="Q" THEN GOSUB716:GOTO300
436 IF Z$="L" THEN Z=1
438 IF Z$="F" THEN Z=2
440 IF Z$="R" THEN Z=3
441 IF Z$="S" THEN GOSUB550
442 IF Z<1 THEN 432
444 PRINTZ$:LV=0
446 ONB(Z)GOSUB900,800,600,4,5,6,700
460 IF INT(RND(0)*100)>90 AND Q$<>"Y" THEN GOSUB1300
462 L=L+LV:ON1-(Q$="Y")GOTO400,300
500 V=7+RND(2)
502 TM=B(Z):B(Z)=V:ON Z GOSUB2100,2110,2120:B(Z)=TM:RETURN
510 R=INT(RND(0)*101)+12:RETURN
516 V=10:GOSUB502:PRINT@32*7,"";:M$="IN THE ROOM YOU FIND "+A$(R):RETURN
536 W=INT(RND(0)*101)+12:RETURN
540 W=(INT(RND(0)*16)+1)*2:RETURN
550 GOSUB2196
552 PRINTTAB(13)"STATUS":PRINT
554 PRINT"YOU HAVE BESTED"K"GEEKS. YOU"
558 PRINT"ARE ON FLOOR "STR$(21-L)". YOU HAVE"
560 PRINTCHR$(8)H"POINTS OF GEEK CRED."
562 PRINT"YOU HAVE"S"FLOPPY DISKS AND"
564 PRINT"$"G"FOR COCO STUFF."
570 GOSUB2198:GOSUB2196
571 PRINT@32*7,"WHAT DIRECTION? ";:RETURN
600 GOSUB2196:PRINT"NO-ENTRY. IT'S A WALL!":F=1:GOSUB536:IF W>50 THEN620
610 M$="EVEN A GEEK LOOKS STUPID WALKING INTO A WALL! YOU LOSE"+STR$(INT(W))+" POINTS OF GEEK CRED.":GOSUB1:H=H-W
620 PRINT:GOSUB360:RETURN
700 GOSUB2150:CLS:PRINT"YOU GOT TO THE AUCTION.":PRINT
704 SC=G*K+S+H:PRINT"YOU BESTED"K"GEEKS.":PRINT"YOU HAVE $"G"OF COCO STUFF":PRINT"YOU SCORED"SC"GEEK CRED."
712 IF SC>P THEN P=SC
714 PRINT"BEST SCORE SO FAR"P
716 PRINT:PRINT"ANOTHER GAME?";
718 Q$=INKEY$:IF Q$="" THEN 718
720 ON1-(Q$<>"N")GOTO2000:ON1-(Q$="Y")GOTO718:RETURN
750 CLS:PRINT:PRINT"  a geek squad apprehends you   ":M$="THEY TRICK YOU OUT OF YOUR COCO BUCKS, "
755 M$=M$+"WHICH REDUCES YOUR CRED BY"+STR$(INT(H/2))+".":GOSUB1
758 M$="THEY TAKE"+STR$(INT(S/2))+" FLOPPIES AND LEAVE YOU ON FLOOR "+STR$(21-(L+1))+".":GOSUB1
759 G=0:H=H-INT(H/2):S=S-INT(S/2):L=L+1:GOSUB2198:RETURN
800 GOSUB2196
801 PRINT"DOOR...O-OPEN,L-LISTEN ";
802 Z$=INKEY$:IF Z$="" THEN 802
806 IF Z$="L" THEN 811
808 IF Z$="O" THEN 898
810 GOTO802
811 PRINTZ$:M$="YOU HEAR "
814 IF B(Z+3)>0 THEN M$=M$+A$(B(Z+3))+" SPEAKING.":GOSUB1
816 IF B(Z+3)=0 THEN M$=M$+"NOTHING.":GOSUB1
818 PRINT"NOW WHAT?":PRINT:PRINT"G-GO IN,H-HELP,L-LEAVE ";
820 Z$=INKEY$:IF Z$="" THEN 820
824 IF Z$="G" THEN 850
826 IF Z$="H" THEN 950
828 IF Z$="L" THEN 832
830 GOTO820
832 PRINTZ$:PRINT"CHICKEN!":GOSUB360
838 F=1
840 IF RND(100)<10 THEN GOSUB1000
842 RETURN
850 GOSUB2196:M$="":IF B(Z+3)>0 THEN R=B(Z+3):GOSUB516
858 IF B(Z+3)=0 THEN M$="A GEEKLESS ROOM"
862 IF B(Z+6)>0 THEN M$=M$+" WITH"+STR$(B(Z+6))+" "+A$(113)
863 GOSUB1
864 IF B(Z+9)>0 THEN PRINTA$(B(Z+9)+100);A$(B(Z+9)+101)
866 IF B(Z+12)>0 THEN PRINTA$(B(Z+12)+100);A$(B(Z+12)+101)
868 IF B(Z+15)>0 THEN PRINTA$(B(Z+15)+100);A$(B(Z+15)+101)
869 GOSUB350
870 IF B(Z+3)>0 THEN GOSUB1314
871 IF B(Z+3)>0 AND C=1700 THEN 838
872 G=G+B(Z+6)
874 FOR X=9 TO 15 STEP 3
876 IF B(Z+X)>0 AND B(Z+X)<25 THEN H=H+30
878 IF B(Z+X)>23 THEN S=S+1
880 NEXT X
882 RETURN
898 PRINTZ$
900 GOSUB2196
902 IF B(Z+3)>0 THEN R=B(Z+3):GOSUB516
905 W=0
906 FOR X=6 TO 15 STEP 3
908 IF B(Z+3)>0 AND B(Z+X)>0 THEN W=2
910 IF B(Z+3)=0 AND B(Z+X)>0 THEN W=1
912 NEXT X
914 IF W=2 THEN M$=M$+" AND COCO STUFF."
916 IF W=1 THEN GOSUB500:M$="THE ROOM CONTAINS COCO STUFF."
918 IF B(Z+3)=0 AND W=0 THEN M$="THE ROOM IS EMPTY."
919 PRINT@32*7,"";:GOSUB1
920 GOTO818
950 PRINTZ$:GOSUB2196
952 IF B(Z+3)>0 THEN M$=A$(B(Z+3))+" HAS"+STR$(B(Z+3)-12)+" GEEK CRED.":GOSUB1
954 IF B(Z+3)=0 THEN PRINT"NO NERDS!"
958 GOTO818
1000 GOSUB2196
1001 F=0:W=1+RND(5):L=L+W
1002 M$="YOU ACCIDENTALLY WANDER INTO AN ELEVATOR WHICH GOES DOWN"+STR$(W)+" FLOORS TO FLOOR "+STR$(21-L)+"."
1005 GOSUB1
1010 GOSUB536
1012 IF W>95 THEN M$="AND SLIP ON A GUM WRAPPER LOSING"+STR$(H)+" POINTS OF GEEK CRED.":GOSUB1:H=0
1016 IF W<40 THEN M$="AND BEND A FLOPPY, LOSING"+STR$(INT(H/2))+" POINTS OF GEEK CRED.":GOSUB1:H=H-INT(H/2)
1020 GOSUB2198:RETURN
1100 FOR X=3 TO 6 STEP 3
1102 GOSUB536
1104 IF W>70 THEN B(Z+X)=W
1106 GOSUB536
1108 IF B(Z+X)>1 THEN B(Z+X)=W
1109 NEXT X
1110 FOR X=9 TO 15 STEP 3
1112 GOSUB536
1113 IF W>75 THEN B(Z+X)=W
1114 GOSUB540
1116 IF B(Z+X)>0 AND W>12 THEN B(Z+X)=W
1117 IF B(Z+X)>0 AND W<13 THEN B(Z+X)=0
1118 NEXT X
1120 RETURN
1300 GOSUB2196:GOSUB510:M$=A$(R)+" ARRIVES":GOSUB1
1310 PRINT"WHAT NOW?"
1314 PRINT:PRINT"B-BATTLE,T-TRADE DISKS,R-RETREAT";
1316 C=0
1318 Z$=INKEY$:IF Z$="" THEN 1318
1322 IF Z$="B" THEN C=1
1324 IF Z$="T" THEN C=2
1326 IF Z$="R" THEN C=3
1328 IF C<1 THEN 1316
1329 PRINTZ$
1330 ON C GOSUB1500,1600,1700
1332 IF C<1 THEN 1314
1334 RETURN
1500 SA=0:IFRND(2)=1 THEN GOSUB1800:GOSUB350
1502 GOSUB2196:M$="YOU HAD A TERRIFIC GEEK BATTLE WITH "+A$(R)
1506 IF (H<(R-12)AND SA=1)OR(H>=(R-12)AND SA=2)THENR=H+12:M$=M$+" AND YOU JUST MANAGED TO BEAT HIM/HER.":GOTO1514
1508 IFH<(R-12)THENM$=M$+" BUT HE/SHE BESTED YOU AND TOOK ALL YOUR STUFF.":GOSUB1:FORZZ=1TO10:SOUND1+RND(50),1:NEXT:GOTO712
1512 M$=M$+" AND YOU BESTED HIM/HER."
1514 GOSUB1:PRINT:FORZZ=1TO10:SOUND100+RND(50),1:NEXT:GOSUB360:K=K+1:H=H+12-R:RETURN
1520 PRINT"SORRY... YOU'RE OUT OF DISKS.":PRINT"NOW WHAT?":C=0:RETURN
1600 GOSUB2196:GOSUB536:IF S<1 THEN 1520
1606 S=S-1
1608 IF W>R THEN M$=A$(R)+" IS HAPPY WITH THE TRADE, AND LEAVES.":GOSUB1:GOSUB360:K=K+1:RETURN
1614 M$=A$(R)+" MAKES A COUNTER OFFER...":GOSUB1
1616 IF W<27 THEN M$=A$(R)+" CHALLENGES YOU...":GOSUB1:GOSUB350:GOTO1500
1620 PRINT"WHAT NOW?"
1622 C=0
1624 RETURN
1700 GOSUB2196:GOSUB536
1702 IF W<40 THEN PRINT"BAD LUCK. SHE/HE CHALLENGES!":GOSUB350:GOTO1500
1706 PRINT"...CHICKEN":GOSUB360:F=0:LV=0:RETURN
1750 CLS:PRINT@75,"fest quest":PRINT@233,"BY JIM GERRIE":PRINT@386,"BASED ON A GAME BY JIM ENNES":PRINT@488,"";:RETURN
1760 CLS:PRINT"YOU ARE AT THE 22ND ANNUAL LAST COCO FEST, WHICH HAS COCO STUFF"
1762 PRINT"BUT ALSO MANY HAZARDS. THE GOAL OF THE GAME IS TO MAKE YOUR WAY"
1764 PRINT"UP THE STAIRS THROUGH THE HOTEL TO THE EXIT ON THE 20TH FLOOR,"
1766 PRINT"WHICH WILL TAKE YOU TO THE 'NO  MINIMUM BID AUCTION.'"
1768 PRINT"PRESS 'S' TO SEE YOUR STATUS.   IN BATTLES WITH GEEKS, YOU USE"
1770 PRINT"'GEEK CRED' POINTS TO DEFEAT    YOUR OPPONENT AND SOMETIMES"
1772 PRINT"KNOWLEDGE. TRADING FLOPPIES IS  ALSO OFTEN SUCCESSFUL (BUT NOT  ALWAYS)."
1774 GOSUB2198:CLS:M$="WELCOME TO HERON POINT CONVENTION CENTER! WE HOPE YOU HAVE A PLEASANT STAY!":GOSUB1:GOTO403
1800 GOSUB7:IFQ1$="999"THEN RESTORE:GOTO1800
1810 IFRND(4)>1THEN1800
1830 FORZZ=1TO4:MA(ZZ)=0:NEXT
1840 FORT=1TO4:ON1-(T=1)GOTO1850:ON1-(LEFT$(AN$(T),3)="ALL")GOTO1845:ZZ=4:GOTO1860
1845 ON1-(LEFT$(AN$(2),3)="ALL")GOTO1850:ZZ=4:MA(4)=2:MA(RND(3))=1:T=2:NEXT 
1850 ZZ=RND(4):IFMA(ZZ)<>0THEN1850
1860 MA(ZZ)=T:NEXT:GOSUB2196:M$=Q1$:GOSUB1
1862 FORT=1TO4:M$=CHR$(64+T)+". "+AN$(MA(T)):GOSUB1:NEXT
1870 I$=INKEY$:ON1-(I$<"A"ORI$>"D")GOTO1880,1870
1880 IFMA(ASC(I$)-64)=1THENPRINTI$" IS RIGHT!";:SA=1:RETURN
1890 PRINTI$" IS WRONG.";:SA=2:RETURN
2000 PRINT:PRINT"GOODBYE.  SEE YOU NEXT YEAR!":END
2020 GOSUB2100:GOSUB2110:GOSUB2120:GOSUB2130:RETURN
2100 PRINT@0+O,B$(B(1),0);
2101 PRINT@32+O,B$(B(1),1);
2102 PRINT@64+O,B$(B(1),2);
2103 PRINT@96+O,B$(B(1),3);
2104 PRINT@128+O,B$(B(1),4);:RETURN
2110 PRINT@11+O,B$(B(2),0);
2111 PRINT@43+O,B$(B(2),1);
2112 PRINT@75+O,B$(B(2),2);
2113 PRINT@107+O,B$(B(2),3);
2114 PRINT@139+O,B$(B(2),4);:RETURN
2120 PRINT@22+O,B$(B(3),0);
2121 PRINT@54+O,B$(B(3),1);
2122 PRINT@86+O,B$(B(3),2);
2123 PRINT@118+O,B$(B(3),3);
2124 PRINT@150+O,B$(B(3),4);:RETURN
2130 PRINT@160+O,"   LEFT     FORWARD     RIGHT   ";:PRINTB$(1,0)B$(1,0)B$(1,0)CHR$(128)CHR$(128);:RETURN
2140 PRINT@0+O,B$(1,0);:PRINT@11+O,B$(1,0);:PRINT@22+O,B$(1,0);:RETURN
2150 B(1)=10:B(2)=10:B(3)=10:FORT=1TO10:O=160:GOSUB2140:O=0:GOSUB2100:GOSUB2110:GOSUB2120:SOUNDRND(50)+100,2
2160 O=0:GOSUB2140:O=32:GOSUB2100:GOSUB2110:GOSUB2120:SOUNDRND(50)+200,2:NEXT:RETURN
2196 FORT=7TO14:PRINT@32*T,BL$;:NEXT:PRINT@32*15,LEFT$(BL$,31);:PRINT@32*7,"";:RETURN
2198 PRINT" * HIT A KEY *";
2199 I$=INKEY$:ON1-(I$<>"")GOTO2199:FORT=1TO14:PRINTCHR$(8);:NEXT:RETURN
2900 BL$="                                "
2910 V$=CHR$(128):W$=CHR$(223):X$=CHR$(175)
3000 B$(1,0)=V$+V$+V$+V$+V$+V$+V$+V$+V$+V$
3001 B$(1,1)=B$(1,0)
3002 B$(1,2)=B$(1,0)
3003 B$(1,3)=B$(1,0)
3004 B$(1,4)=B$(1,0)
3005 B$(2,0)=W$+W$+X$+X$+X$+X$+X$+X$+W$+W$
3006 B$(2,1)=W$+W$+X$+X$+X$+X$+X$+X$+W$+W$
3007 B$(2,2)=W$+W$+X$+X$+X$+X$+X$+CHR$(173)+W$+W$
3008 B$(2,3)=W$+W$+X$+X$+X$+X$+X$+X$+W$+W$
3009 B$(2,4)=W$+W$+X$+X$+X$+X$+X$+X$+W$+W$
3010 B$(3,0)=W$+W$+W$+W$+W$+W$+W$+W$+W$+W$
3011 B$(3,1)=B$(3,0)
3012 B$(3,2)=B$(3,0)
3013 B$(3,3)=B$(3,0)
3014 B$(3,4)=B$(3,0)
3015 B$(4,0)=W$+CHR$(219)+CHR$(210)+V$+V$+V$+CHR$(208)+CHR$(209)+CHR$(215)+W$
3016 B$(4,1)=W$+W$+W$+CHR$(219)+CHR$(210)+CHR$(209)+CHR$(215)+W$+W$+W$
3017 B$(4,2)=W$+W$+W$+W$+W$+W$+W$+W$+W$+W$
3018 B$(4,3)=W$+W$+W$+CHR$(222)+CHR$(216)+CHR$(212)+CHR$(221)+W$+W$+W$
3019 B$(4,4)=W$+CHR$(222)+CHR$(216)+CHR$(208)+V$+V$+V$+CHR$(212)+CHR$(221)+W$
3020 B$(5,0)=B$(1,0)
3021 B$(5,1)=CHR$(228)+CHR$(239)+V$+V$+V$+V$+V$+V$+V$+V$
3022 B$(5,2)=CHR$(230)+CHR$(228)+V$+V$+V$+V$+V$+V$+V$+V$
3023 B$(5,3)=V$+CHR$(224)+V$+CHR$(211)+CHR$(215)+W$+W$+W$+W$+W$
3024 B$(5,4)=CHR$(211)+CHR$(215)+W$+W$+W$+W$+W$+W$+W$+W$
3025 B$(6,0)=B$(1,0)
3026 B$(6,1)=V$+V$+V$+V$+V$+V$+V$+CHR$(224)+CHR$(233)+CHR$(225)
3027 B$(6,2)=V$+V$+V$+V$+V$+V$+V$+V$+CHR$(225)+CHR$(239)
3028 B$(6,3)=W$+W$+W$+W$+W$+CHR$(219)+CHR$(211)+V$+V$+V$
3029 B$(6,4)=W$+W$+W$+W$+W$+W$+W$+W$+CHR$(219)+CHR$(211)
3030 B$(7,0)=V$+CHR$(158)+CHR$(157)+CHR$(157)+CHR$(157)+CHR$(157)+CHR$(156)+CHR$(157)+V$+V$
3031 B$(7,1)=V$+CHR$(154)+CHR$(157)+CHR$(150)+CHR$(151)+CHR$(149)+CHR$(154)+CHR$(159)+V$+V$
3032 B$(7,2)=V$+CHR$(154)+CHR$(157)+CHR$(149)+CHR$(149)+CHR$(149)+CHR$(154)+CHR$(159)+V$+V$
3033 B$(7,3)=V$+CHR$(158)+CHR$(156)+CHR$(156)+CHR$(156)+CHR$(156)+CHR$(156)+CHR$(157)+CHR$(144)+CHR$(144)
3034 B$(7,4)=CHR$(144)+CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)+CHR$(159)+V$+V$
3035 B$(8,0)=V$+CHR$(240)+V$+V$+CHR$(240)+CHR$(240)+CHR$(240)+CHR$(240)+CHR$(240)+V$
3036 B$(8,1)=V$+V$+CHR$(241)+CHR$(243)+CHR$(243)+CHR$(243)+CHR$(243)+CHR$(243)+CHR$(243)+V$
3037 B$(8,2)=V$+CHR$(241)+CHR$(255)+CHR$(255)+CHR$(111)+CHR$(255)+CHR$(255)+CHR$(255)+CHR$(255)+V$
3038 B$(8,3)=V$+CHR$(245)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(253)+CHR$(245)+V$
3039 B$(8,4)=V$+CHR$(245)+V$+V$+V$+V$+V$+CHR$(245)+CHR$(240)+V$
3040 B$(9,0)=V$+V$+V$+V$+CHR$(131)+CHR$(131)+CHR$(131)+V$+V$+V$
3041 B$(9,1)=V$+CHR$(240)+CHR$(241)+CHR$(243)+CHR$(143)+CHR$(143)+CHR$(143)+CHR$(243)+CHR$(243)+V$
3042 B$(9,2)=V$+CHR$(241)+CHR$(255)+CHR$(198)+CHR$(198)+CHR$(198)+CHR$(199)+CHR$(255)+CHR$(255)+V$
3043 B$(9,3)=V$+CHR$(245)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(252)+CHR$(253)+CHR$(245)+V$
3044 B$(9,4)=V$+CHR$(245)+V$+V$+V$+V$+V$+CHR$(245)+V$+V$
3045 B$(10,0)=V$+V$+CHR$(158)+CHR$(158)+CHR$(154)+V$+V$+V$+V$+V$
3046 B$(10,1)=CHR$(144)+CHR$(144)+CHR$(159)+CHR$(156)+CHR$(154)+CHR$(176)+CHR$(195)+V$+V$+V$
3047 B$(10,2)=CHR$(149)+CHR$(189)+CHR$(191)+CHR$(191)+CHR$(191)+CHR$(156)+CHR$(204)+V$+V$+V$
3048 B$(10,3)=CHR$(148)+CHR$(165)+CHR$(174)+CHR$(172)+CHR$(175)+V$+V$+V$+V$+V$
3049 B$(10,4)=V$+CHR$(211)+CHR$(218)+V$+CHR$(219)+CHR$(210)+V$+V$+V$+V$
3060 RETURN
3080 DATA ORIGIN OF THE NAME SUB-ETHA SOFTWARE,ALL OF THE ABOVE,SCI FI BOOK,DOUGLAS ADAMS,HITCH-HIKERS GUIDE TO THE GALAXY
3090 DATA "BOB ROSEN'S SPECTRUM PROJECTS CO. BEGAN AS",A BBS,ALL OF THE ABOVE,A HAIRSTYLING STUDIO,A MAIL-ORDER WIG COMPANY
3100 DATA THE RAINBOWFEST MASCOT WAS,COCO CAT,THE FALK FALCON,THE COCO BUNNY,RUFUS THE COCO COLLIE
3110 DATA GIME IS AN ACRONYM FOR,GRAPHICS INTERRUPT MEMORY ENHANCER,GRAPHIC & MULTITASKING ENABLER
3111 DATA GRAPHICS WITH MULTIPLE ELEMENTS,GRAPHIC MEMORY EXTENSION
3120 DATA WHICH IS NOT AN ERROR CODE IN RS BASIC,DU,FC,VF,RG
3130 DATA BINARY DIGITS CAN HAVE,2 VALUES,8 VALUES,16 VALUES,256 VALUES
3140 DATA WHICH IS NOT A COCO MAGAZINE,ANTIC,HOT COCO,RAINBOW,COLOR COMPUTER MAGAZINE
3150 DATA THE COLOR COMPUTER FIRST APPEARED IN,1980,1979,1981,1982
3160 DATA CLS(0) WILL CLEAR THE SCREEN TO,BLACK,GREEN,RED,BLUE
3170 DATA PCLEAR 0 IS,POSSIBLE WITH A LITTLE CREATIVITY,A FUNCTIONAL COMMAND,A WAY TO CLEAR THE SCREEN,A C64 COMMAND
3180 DATA THE 80 IN TRS-80 STANDS FOR THE,Z80 CHIP,8088 CHIP,COCO RELEASE YEAR,8-BIT DECADE
3190 DATA NOT A COCO 1 MEMORY AMOUNT,128K,64K,32K,16K
3200 DATA NOT A COCO 3 MEMORY AMOUNT,64K,128K,512K,1024K
3210 DATA PROCESSOR AT THE HEART OF THE COCO,6809,6502,Z80,8088
3220 DATA MAKER OF OS9,MICROWARE,MICROSOFT,MOTOROLA,RADISYS
3230 DATA WHO ARE THE 3 MUGETEERS,AN EASTER EGG,MAKERS OF RSDOS,MAKERS OF OS9,FOUNDERS OF TANDY
3240 DATA THE COCO 2'S HIGHEST RESOLUTION,256X192,320X192,320X200,640X200
3250 DATA COCO CLONE WITH SOME NICE ADDITIONS,DRAGON,ZX81,TIMEX 1000,MC-10
3260 DATA TOM MIX PRIMARILY SOLD,GAMES,UTILITIES,HARDWARE,PRODUCTIVITY SOFTWARE
3270 DATA NOT A DOS,GIMIX,OS-9,FLEX,DECB
3280 DATA THE MC6847 IS ALSO KNOW AS THE,VDG,Z80,6809E,6309
3290 DATA NOT A COLOR BASIC FUNCTION,ATN,POINT,JOYSTK,EOF
3300 DATA HOW DO YOU PRONOUNCE LED,ALL OF THE ABOVE,EL E DE,LEAD,LIGHT EMITTING DIODE
3310 DATA LINUS TORVALDS WAS THE INVENTOR OF,ALL OF THE ABOVE,A GOOD OS,LINUX,A PIECE OF OPEN SOURCE SOFTWARE
3320 DATA SEMIGRAPHICS ARE,A COCO 1 GRAPHIC MODE,THE LOWEST COCO RES,THE HIGHEST COCO RES,A COCO 3 GRAPHIC MODE
3330 DATA CLS(9) PRODUCES,MICROSOFT,AN ORANGE SCREEN,A PURPLE SCREEN,A CYAN SCREEN
3340 DATA IN OS9 DISPLAY C,CLEARS THE SCREEN,TURNS ON LOWERCASE,EXPLODES YOUR COCO,PRINTS A 'C'
3350 DATA RESISTOR WITH COLORS BROWN BLACK RED GOLD,ALL OF THE ABOVE,1000 OHMS,1K OHMS,.001M OHMS
3360 DATA WIRE GAGE RUN FROM A CAR BATTERY TO STARTER,0 AWG OR 1 AWG,30 AWG,22 AWG,12 AWG
3370 DATA HEX FF IN DECIMAL,255,256,16,64
3380 DATA LASER STANDS FOR,LIGHT AMPLIFICATION BY STIMULATED EMISSION OF RADIATION
3381 DATA LIGHT AS EFFECTIVE RESOURCE,LIGHT ARC SOURCE EMITTER,LUMINOUS AMBIENT SYSTEM RESOURCE
3390 DATA VACUUM TUBES ARE ALSO KNOWN AS,THERMIONIC VALVES,IC'S,THERMAL RESISTERS,DIFFERENCE CAPACITORS
3400 DATA HOW DO YOU MAKE A BOOLEAN INVERTER,"B=NOT A AND 1","B=SGN(A)","B=NOT A/128","B=NOT A"
3410 DATA "MAXIMUM COLORS A COCO 1/2 CAN DISPLAY",44,2,4,8
3420 DATA ASCII STANDS FOR,AMERICAN STANDARD FOR COMPUTER INFO INTER-CHANGE,ALPHA STANDARD CODES II
3421 DATA ALPHA SEQUENCE OF CODES FOR INFO INPUT,ALPHA SEQUENCE COMPUTER INPUT OF INFO
3430 DATA "POKE 65495,0 DOES WHAT",HI-SPEED POKE,DISPLAYS COLOR BASIC VERSION,WARM START,STOPS CURSOR FLASH
3440 DATA NOT ONE OF THE FIRST COLOR COMPUTER ROM-PAKS,CYRUS CHESS,CHECKERS,QUASAR COMMANDER,PERSONAL FINANCE
3450 DATA NOT A TYPICAL UPGRADE TO EARLY COCOS,ADD A MC6883 SAM CHIP,16K MEM,EXTENDED COLOR BASIC ROM,NEW KEYBOARD
3460 DATA EARLY COCO SOFTWARE COMPANY,ALL OF THE ABOVE,STAR-KITS,MARK DATA,FRANK HOGG
3470 DATA VDG IS AN ACRONYM FOR,VIDEO DISPLAY GENERATOR,ALL OF THE ABOVE,VIDEO DISPLAY GRID,VIDEO DISPLAY OF GRAPHICS
3480 DATA "COCO'S SET/RESET GRAPHICS RESOLUTION",64X32,64X48,64X96,128X48
3490 DATA "TRS-80 MODEL I/III SET/RESET RESOLUTION",128X48,64X32,64X48,64X96
3500 DATA PRINTF IS A COMMAND,IN THE C LANGUAGE,ALL OF THE ABOVE,TO SEND A FILE TO THE PRINTER,IN COLOR BASIC
3510 DATA THE NAME OF OS9'S GUI,MULTI-VUE,MULTIVIEW,WINDOWS-O9,WINDOW MASTER
3520 DATA DEFAULT BAUD RATE OF THE COCO'S PRINTER PORT,600,300,1200,9600
3530 DATA 1ST COMPUTER DESIGNED WITH ITS OWN GUI,APPLE LISA,AMSTRAD CPC 464,COLOR COMPUTER 3,COMMODORE PET
3540 DATA PROGRAMMING LANGUAGE THAT USES A TURTLE,LOGO,JAVA,PASCAL,C
3550 DATA GROUNDBREAKING ULTRAVIOLENT COMPUTER GAME RELEASED IN 1993,DOOM,HALO,COUNTER-STRIKE,DINO WARS
9999 DATA 999,999,999,999,999
