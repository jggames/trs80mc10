0 CLS0:DIMB(72),D(72),R(72),K(255),C,T,B,N,X,Y,MC,S,V(4),TI,I$,M,S,R,G,BL,SC,WC,ST:GOTO1000
2 POKEC,128:C=C+T:POKEC,N:GOTO15
3 T=32:GOTO15
4 POKEC,128:GOSUB10:GOSUB195:GOSUB80:RETURN
5 ON1-(B=N ORN=159)GOTO6:M=1:POKEC,128:C=C+T:N=B:POKEC,N:BL=BL-1:S=S+1:GOTO15
6 ONMGOTO7:POKEC,128:GOSUB10:SOUND1,5:FORT=1TO50:GOSUB11:GOSUB10:NEXT:TI=TI-100:RETURN
7 POKEC,128:C=C+T:POKEC,N:N=B:GOSUB10:GOSUB195:GOSUB80:RETURN
8 ON1-(SC>TR)GOTO9:WC=WC+1:GOSUB8080:TR=TR+30000:B=MC+407:FORT=1TO10:C=PEEK(B):POKEB,C-64:SOUND100,1:POKEB,C:SOUND150,1:NEXT
9 RETURN
10 PRINT@P-32,"˝˝";:PRINT@P,"˚ˇ";:PRINT@P-1,CHR$(N);:RETURN
11 PRINT@P-32,"ùù";:PRINT@P,"õü";:RETURN
12 PRINT@P-32,"ŒÃ";:PRINT@P,"ŒÃ";:PRINT@P-1,CHR$(128);:RETURN
15 B=PEEK(C+T):ONK(B)GOTO2,3,4,5,5,5,5:RETURN
20 I$=INKEY$:ONAD GOTO21:TI=TI-1:PRINT@180,RIGHT$(+"  "+STR$(TI),4);:ON1-(TI>0)GOTO46
21 ON1-(I$<>"")GOTO20:ONK(ASC(I$))GOTO22,23,24,25,28:GOTO20
22 ON1-(P>45)GOTO20:GOSUB12:P=P-32:GOSUB10:SOUND180,1:SOUND183,1:TI=TI-20:GOTO20
23 ON1-(P<397)GOTO20:GOSUB12:P=P+32:GOSUB10:SOUND180,1:SOUND183,1:TI=TI-20:GOTO20
24 ON1-(BL>CL)GOTO70:TI=TI-20:GOTO40
25 M=0:S=0:C=P-1+MC:T=-1:GOSUB15:TI=TI-50
26 ONAD GOTO27:SC=INT(SC+100*S^2):PRINT@454,SC;:GOSUB8
27 ONM GOTO40,20,50,70:GOTO20
28 PRINT@480,"QUIT? (y/n)?";
29 I$=INKEY$:ON1-(I$<>"")GOTO29:ON1-(I$<>"Y")GOTO46:PRINT@480,;:FORT=1TO12:PRINTCHR$(128);:NEXT:GOTO20
40 WC=WC-1:ONAD GOTO45:ON1-(WC>-1)GOTO46:N=159:PRINT@P-1,CHR$(N);:GOSUB8080:M=2:GOTO20
45 ON1-(WC>-1)GOTO46:PRINT@480,"START AGAIN";:SOUND1,20:GOSUB9300:PRINT@480,;:FORC=1TO11:PRINTCHR$(128);:NEXT:GOTO1090
46 PRINT@180,RIGHT$(+" 0  ",4);:PRINT@15*32+16,"TRY AGAIN (y/n)";:POKEMC+511,127:GOTO150
50 ON1-(ST<16)GOTO60,1090
60 PRINT@480,"YOU WIN!";:GOSUB3100:GOTO46
70 PRINT@480,"CONGRATULATIONS!";:GOSUB190:PRINT@480,;:FORC=1TO16:PRINTCHR$(128);:NEXT
71 SC=SC+INT(TI/100)*60:SC=SC+1000+((CL-BL)*1000):PRINT@454,SC;:GOSUB8
72 TI=5500:GOTO1090
78 WC=WC+1:RETURN
80 C=128:FORT=7TO36:ON1-(PEEK(D(T))=C)GOTO81:POKED(T),PEEK(B(T)):POKEB(T),C
81 NEXT:FORB=1TO3:FORT=12TO36STEP6:ON1-(PEEK(D(T))=C)GOTO82:POKED(T),PEEK(B(T)):POKEB(T),C
82 NEXT:NEXT:FORT=1TO72:ON1-(PEEK(B(T))=C)GOTO83:NEXT:GOTO90
83 ON1-(PEEK(B(T))<>N)GOTO84:T=T+R(T):NEXT:GOTO90
84 M=2:T=72:NEXT
90 PRINT@276,BL;:ON1-(BL>CL)GOTO95:ONM GOTO92:RETURN
92 PRINT@6*32+8,"MISS!";:SOUND50,5:SOUND25,5:PRINT@6*32+8,;:FORT=1TO5:PRINTCHR$(128);:NEXT:RETURN
95 ONAD GOTO100:ONM GOTO96,97,96
96 M=4:RETURN
97 RETURN
100 B=0:R=0:G=0:C=0:FORT=1TO36:ONK(PEEK(D(T)))GOSUB9,9,9,101,102,103,104:NEXT:ON1-(B>1ORR>1ORG>1ORC>1)GOTO105:M=1:RETURN
101 B=B+1:RETURN
102 R=R+1:RETURN
103 G=G+1:RETURN
104 C=C+1:RETURN
105 PRINT@480,"SUCCESS!";:GOSUB190:PRINT@480,;:FORT=1TO8:PRINTCHR$(128);:NEXT:M=3:RETURN
150 I$=INKEY$:IFI$=""THEN150
160 IFI$="Y"THEN1030
170 IFI$="N"THENEND
180 GOTO150
190 SOUND140,2:SOUND155,2:SOUND170,2:FORT=1TO2500:NEXT:RETURN
195 SOUND240,1:SOUND245,1:SOUND240,1:RETURN
1000 MC=16384:C=RND(-(PEEK(9)*256+PEEK(10)))
1010 C=0:FORY=6TO1STEP-1:FORX=6TO1STEP-1
1011 C=C+1:B(C)=(Y+6)*32+X+MC:D(C)=(Y+6)*32+X+MC+32:R(C)=5:NEXT:NEXT
1012 FORX=1TO6:B=5:FORY=1TO6
1013 C=C+1:R(C)=B:B=B-1:B(C)=(Y+6)*32+X+MC:D(C)=(Y+6)*32+X+MC+32:NEXT:NEXT
1015 K(87)=1:K(90)=2:K(83)=3:K(32)=4:K(65)=4:K(81)=5
1020 K(128)=1:K(197)=2:K(207)=2:K(204)=3:K(175)=4:K(191)=5:K(143)=6:K(223)=7:V(1)=175:V(2)=191:V(3)=143:V(4)=223
1030 CLS0:RESTORE:GOSUB9400:GOSUB9050:PRINT@0,"     FLIPULL     ";:PRINT@32,"  BY JIM GERRIE  ";:PRINT@96,"1. NORMAL PLAY   ";
1031 PRINT@128,"2. ADVANCED PLAY ";:PRINT@160,"3. INSTRUCTIONS  ";:PRINT@448,"AN EXCITING CUBE GAME";
1032 PRINT@192,"4. QUIT          "; 
1033 IFSC>HSTHENHS=SC
1034 PRINT@32*10,"HIGH SCORE:";HS;:PRINT@480," ORIGINALLY BY TAITO ";
1035 I$=INKEY$:ON1-(I$<>"")GOTO1035:ONVAL(I$)GOTO1060,1040,9600,9999:GOTO1035
1040 AD=1:TI=0:LV=4:CL=3:TR=100000:WC=25:GOTO1080
1060 AD=0:TI=5500:LV=5:CL=9:TR=20000:WC=2
1080 GOSUB9000:GOSUB9400:P=6*32+13:SC=0:ST=0
1090 ONAD+1GOTO2000:ST=ST+1:ON1-(ST=16)GOTO1095:LV=5
1095 BL=LV*LV:GOSUB8000:N=159:GOSUB10:GOSUB9200:GOSUB9250:M=0:GOTO20
2000 ST=ST+1:ON1-(ST=20)GOTO2001:LV=6
2001 ON1-(ST=40)GOTO2010:CL=8
2010 BL=LV*LV:GOSUB8000:GOSUB8090:N=159:GOSUB10:GOSUB9500:M=0:GOTO20
3100 SOUND 89, 2
3101 SOUND 108, 2
3102 SOUND 125, 4
3103 SOUND 89, 2
3104 SOUND 108, 2
3105 SOUND 125, 2
3106 SOUND 108, 2
3107 SOUND 89, 2
3108 SOUND 78, 2
3109 SOUND 58, 2
3110 SOUND 78, 2
3111 SOUND 89, 2
3112 SOUND 108, 2
3113 SOUND 78, 2
3114 SOUND 38, 2
3115 SOUND 89, 2
3116 SOUND 108, 2
3117 SOUND 125, 4
3118 SOUND 89, 2
3119 SOUND 108, 2
3120 SOUND 125, 2
3121 SOUND 108, 2
3122 SOUND 89, 2
3123 SOUND 78, 2
3124 SOUND 58, 2
3125 SOUND 108, 2
3126 SOUND 78, 2
3127 SOUND 38, 2
3128 SOUND 89, 4:RETURN
8000 PRINT@1*32+20,"CLEAR";
8010 PRINT@2*32+20," "CL" ";
8020 PRINT@4*32+20,"TIME ";
8030 PRINT@5*32+20," 0   ";
8040 PRINT@7*32+20,"BLOCK";
8050 PRINT@8*32+20,"     "CHR$(8)CHR$(8)CHR$(8)CHR$(8)CHR$(8)BL;
8060 PRINT@10*32+20,"STAGE";
8070 PRINT@11*32+20,"     "CHR$(8)CHR$(8)CHR$(8)CHR$(8)CHR$(8)ST;
8080 PRINT@12*32+20,"s"CHR$(159);"=";MID$(STR$(WC)+" ",2,2);:RETURN
8090 PRINT@14*32,"SCORE:";:RETURN
9000 CLS0:FORX=1TO62:SET(X,1,5):SET(X,26,5):NEXT
9010 FORY=1TO26:SET(1,Y,5):SET(62,Y,5):NEXT:FORY=1TO26:SET(26,Y,5):SET(31,Y,5):NEXT
9040 FORY=0TO26STEP2:FORX=26TO31:SET(X,Y,5):NEXT:NEXT:FORX=27TO30:RESET(X,1):NEXT:RETURN
9050 FORX=48TO62:SET(X,1,5):SET(X,26,5):NEXT:FORY=1TO26:SET(48,Y,5):SET(62,Y,5):NEXT:RETURN
9200 FORY=1TO4:READI$:FORX=1TO5:ON1-(MID$(I$,X,1)="*")GOTO9210:PRINT@Y*32+X,CHR$(207);:NEXT:NEXT:RETURN
9210 PRINT@Y*32+X,CHR$(128);:NEXT:NEXT:RETURN
9250 FORY=1TO6:READI$:FORX=1TO6:ON1-(MID$(I$,X,1)="T")GOTO9252:PRINT@(Y+6)*32+X,CHR$(V(1));:NEXT:NEXT:RETURN
9252 ON1-(MID$(I$,X,1)="o")GOTO9253:PRINT@(Y+6)*32+X,CHR$(V(2));:NEXT:NEXT:RETURN
9253 ON1-(MID$(I$,X,1)="x")GOTO9254:PRINT@(Y+6)*32+X,CHR$(V(4));:NEXT:NEXT:RETURN
9254 ON1-(MID$(I$,X,1)="#")GOTO9255:PRINT@(Y+6)*32+X,CHR$(V(3));:NEXT:NEXT:RETURN
9255 PRINT@(Y+6)*32+X,CHR$(128);:NEXT:NEXT:RETURN
9300 ST=ST-1:RESTORE:ON1-(ST>0)GOTO9310:FORC=1TOST:FORY=1TO10:READI$:FORX=1TO5:NEXT:NEXT:NEXT
9310 RETURN
9400 PRINT@1*32+27,"„‚";
9401 PRINT@2*32+27,"Ó";:PRINT@3*32+27,"‚";:PRINT@4*32+27,"ÏË";:PRINT@5*32+27,"Â‡";
9405 PRINT@6*32+27,"„‚";:PRINT@7*32+27,"ÓË";:PRINT@8*32+27,"‚‚";:PRINT@9*32+27,"ÏË";
9410 PRINT@10*32+27,"Î‚";:PRINT@11*32+27,"‚";:PRINT@12*32+27,"ÏË";:RETURN
9500 OL=ST:ST=1+RND(15):GOSUB9300:GOSUB9200:FORY=6TO7-LVSTEP-1:FORX=1TOLV:PRINT@(Y+6)*32+X,CHR$(V(RND(4)));:NEXT:NEXT
9510 ST=OL:RETURN
9600 CLS
9605 PRINT"s =     USE SUPER BLOCK/NO MOVE":PRINT"w =     MOVE UP LADDER"
9608 PRINT"z =     MOVE DOWN LADDER":PRINT"space = THROW BLOCK (OR a)"
9614 PRINT"-THE OBJECT OF FLIPULL IS TO":PRINT"THROW COLOURED BLOCKS AT THE"
9616 PRINT"PILE TO MAKE IT SMALLER.":PRINT"-A STAGE IS COMPLETE WHEN NO"
9618 PRINT"MOVES ARE LEFT AND THE PILE":PRINT"EQUALS OR IS BELOW THE CLEAR #."
9619 PRINT"-TRY TO LINE UP THE COLOUR YOU":PRINT"HOLD WITH THE SAME COLOUR"
9621 PRINT"BLOCKS IN THE PILE.":GOSUB9695
9622 PRINT"-WHEN YOU HIT THE *THROW*":PRINT"BUTTON, YOUR BLOCK WILL REMOVE"
9624 PRINT"ANY SIMILAR COLOURED BLOCKS UN-":PRINT"TIL IT HITS A DIFFERENT COLOUR."
9627 PRINT"-IF YOUR BLOCK HITS A WALL,":PRINT"IT WILL BEGIN TO MOVE DOWN."
9633 PRINT"-YOU LOSE A LIFE WHEN YOU":PRINT"ARE UNABLE TO REMOVE ANY"
9635 PRINT"BLOCKS FROM THE PILE.":PRINT"-YOU CAN BANK YOUR SHOTS DOWN"
9643 PRINT"OFF THE DEFLECTORS ALONG THE":PRINT"TOP LEFT, OR SIMPLY THROW AT
9647 PRINT"THE BLOCKS TO YOUR LEFT.":GOSUB9695
9648 PRINT"-THE BLOCK YOU HIT LAST ON":PRINT"YOUR THROW BECOMES THE BLOCK"
9650 PRINT"YOU GET FOR YOUR NEXT THROW.":PRINT"-THE COMPUTER GENERALLY KNOWS"
9670 PRINT"WHEN YOU CANNOT MAKE A SHOT,":PRINT"BUT IF YOU NOTICE THAT NO"
9680 PRINT"SHOT IS AVAILABLE, HIT THE":PRINT"s KEY, WHICH WILL SWITCH YOUR"
9686 PRINT"BLOCK TO A YELLOW SUPER BLOCK":PRINT"(=WILDCARD/LIVES REMAINING),"
9687 PRINT"OR IF YOU ARE BELOW THE CLEAR":PRINT"NUMBER THEN IT WILL SIGNAL THE"
9689 PRINT"THE COMPLETION OF THE STAGE.":GOSUB9695:GOTO9700
9695 PRINT:PRINT"* HIT ANY KEY TO CONTINUE *"
9696 I$=INKEY$:ON1-(I$<>"")GOTO9696:CLS:RETURN
9700 PRINT"-ADVANCED MODE OF FLIPULL HAS":PRINT"16 SET-PIECE STAGES."
9725 PRINT"-THE OBJECT FOR EACH STAGE IS":PRINT"TO WIND UP WITH 4 BLOCKS OF EACH";
9728 PRINT"COLOUR INCLUDING THE SHOT BLOCK.";:PRINT"-YOU ONLY HAVE 25 LIVES/SUPER-"
9732 PRINT"BLOCKS TO SOLVE ALL 16 STAGES.":PRINT"-IN NORMAL MODE YOU EARN A"
9735 PRINT"SUPER BLOCK AT 20000 POINTS,":PRINT"AND THEN EVERY 30000 POINTS.":PRINT"-USE q TO QUIT AT ANY TIME."
9736 GOSUB9695:GOTO1030
9800 DATA"****"
9801 DATA"***"
9802 DATA"**"
9803 DATA"*"
9804 DATA" "
9805 DATA" "
9806 DATA"x###"
9807 DATA"oxxx"
9808 DATA"TTTT"
9809 DATA"#ooo"
9810 DATA"****"
9811 DATA"***"
9812 DATA"**"
9813 DATA"*"
9814 DATA" "
9815 DATA" "
9816 DATA"xooo"
9817 DATA"#xxx"
9818 DATA"Tooo"
9819 DATA"oxxx"
9820 DATA"****"
9821 DATA"***"
9822 DATA"**"
9823 DATA"*"
9824 DATA" "
9825 DATA" "
9826 DATA"oooo"
9827 DATA"TTTo"
9828 DATA"oooo"
9829 DATA"Tx#o"
9830 DATA"****"
9831 DATA"***"
9832 DATA"**"
9833 DATA"*"
9834 DATA" "
9835 DATA" "
9836 DATA"xoxo"
9837 DATA"xoxo"
9838 DATA"oxox"
9839 DATA"#Tox"
9840 DATA"****"
9841 DATA"***"
9842 DATA"**"
9843 DATA"*"
9844 DATA" "
9845 DATA" "
9846 DATA"x#To"
9847 DATA"#Tox"
9848 DATA"#Tox"
9849 DATA"#Tox"
9850 DATA"*****"
9851 DATA"**"
9852 DATA"*"
9853 DATA" "
9854 DATA" "
9855 DATA" "
9856 DATA"TT##"
9857 DATA"ooTT"
9858 DATA"Txxx"
9859 DATA"o#Tx"
9860 DATA"****"
9861 DATA"***"
9862 DATA"**"
9863 DATA"*"
9864 DATA" "
9865 DATA" "
9866 DATA"x##T"
9867 DATA"TxxT"
9868 DATA"TTT#"
9869 DATA"oxoT"
9870 DATA"****"
9871 DATA"***"
9872 DATA"**"
9873 DATA"*"
9874 DATA" "
9875 DATA" "
9876 DATA"##xx"
9877 DATA"T##T"
9878 DATA"TToo"
9879 DATA"oT##"
9880 DATA"****"
9881 DATA"***"
9882 DATA"**"
9883 DATA"*"
9884 DATA" "
9885 DATA" "
9886 DATA"TT#x"
9887 DATA"oTxT"
9888 DATA"To#o"
9889 DATA"#xTo"
9890 DATA"****"
9891 DATA"***"
9892 DATA"*"
9893 DATA"*"
9894 DATA" "
9895 DATA" "
9896 DATA"xoTo"
9897 DATA"o##o"
9898 DATA"Txoo"
9899 DATA"x#To"
9900 DATA"****"
9901 DATA"**"
9902 DATA"**"
9903 DATA"*"
9904 DATA" "
9905 DATA" "
9906 DATA"oxxT"
9907 DATA"oxo#"
9908 DATA"oT##"
9909 DATA"Tooo"
9910 DATA"****"
9911 DATA"***"
9912 DATA"*"
9913 DATA"*"
9914 DATA" "
9915 DATA" "
9916 DATA"##T#"
9917 DATA"#To#"
9918 DATA"#Tx#"
9919 DATA"To#T"
9920 DATA"****"
9921 DATA"**"
9922 DATA"**"
9923 DATA"*"
9924 DATA" "
9925 DATA" "
9926 DATA"ooxx"
9927 DATA"TTxT"
9928 DATA"Tox#"
9929 DATA"TTTT"
9930 DATA"****"
9931 DATA"***"
9932 DATA"**"
9933 DATA"*"
9934 DATA" "
9935 DATA" "
9936 DATA"oxT#"
9937 DATA"TxTx"
9938 DATA"To#x"
9939 DATA"#xxT"
9940 DATA"****"
9941 DATA"***"
9942 DATA"*"
9943 DATA"*"
9944 DATA" "
9945 DATA" "
9946 DATA"T#T#"
9947 DATA"####"
9948 DATA"Tooo"
9949 DATA"x#oo"
9950 DATA"*****"
9951 DATA"**"
9952 DATA"*"
9953 DATA" "
9954 DATA" "
9955 DATA"x#TxT"
9956 DATA"#oTxx"
9957 DATA"o##oo"
9958 DATA"x#Txx"
9959 DATA"TxToo"
9999 END
