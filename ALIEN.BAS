0 DIMX,Y,A,B,H,V,P,F,D,J,K,L,M,M$(15),E$(10),W(15):P=RND(-(PEEK(9)*256+PEEK(10))):GOSUB9000:GOTO60
7 J=ABS(A-X):K=ABS(B-Y):IFJ<10ANDK<10THENRESET(A,B)
8 C1=C1+1:IFC1>3THENIFRND(2)=1THEN800
9 IFF=0THENRETURN
10 H=0:V=0:FORP=1TO20-D:I$=INKEY$:SET(X,Y,5):SET(X,Y,8):SET(X,Y,1):RESET(X,Y)
11 IFI$<>""THENH=(I$="Q"ORI$="A"ORI$="Z")-(I$="E"ORI$="D"ORI$="C"):V=(I$="Q"ORI$="W"ORI$="E")-(I$="Z"ORI$="X"ORI$="C")
12 NEXT:IFH=0ANDV=0THENRETURN
13 J=X:K=Y:FORP=1TO5:J=J+H:K=K+V:IFPOINT(J,K)>0THENRESET(J,K):SOUND200,1:SET(J,K,1):NEXT:GOTO16
14 IFPOINT(J,K)<0THENPOKE16384+INT(J/2)+32*INT(K/2),143:SOUND100,1:SOUND50,1
15 P=5:NEXT
16 IFJ=AANDK=BTHENGOSUB1000
17 SOUND1,1:I$="":RETURN
20 H=(I$="Q"ORI$="A"ORI$="Z")-(I$="E"ORI$="D"ORI$="C"):V=(I$="Q"ORI$="W"ORI$="E")-(I$="Z"ORI$="X"ORI$="C")
22 P=POINT(X+H,Y+V):IFPTHENSET(X,Y,ABS(P)):X=X+H:Y=Y+V:IFX<44THENSC=SC+D:PRINT@5,SC;
31 IFP<0THENGOSUB900
32 SET(X,Y,P):RESET(X,Y):H=(A>X)-(A<X):V=(B>Y)-(B<Y):IFRND(D)=1THENH=-H
45 IFRND(D)=1THENV=-V
48 IFPOINT(A+H,B)=1THENSET(A,B,1):A=A+H
49 IFPOINT(A,B+V)=1THENSET(A,B,1):B=B+V
50 I$=INKEY$:J=ABS(A-X):K=ABS(B-Y):IFJ<10ANDK<10THENRESET(A,B):IFJ<2ANDK<2THENGOSUB3000:IFHP<1THEN60
51 SET(A,B,1):IFI$=" "THENGOSUB7
52 IFHP>0THEN20
60 CLS0:SC=0:N=4:X=44:Y=17:HP=200:F=0:GOSUB8000:FORJ=1TO15:W(J)=0:NEXT
61 FORJ=1TO15
62 K=RND(15):IFW(K)<>0THEN62
63 W(K)=J:NEXT
65 CLS:GOSUB90:GOSUB70:GOSUB100:GOTO50
70 PRINT@32,;
71 PRINT"    ŽŒƒ‚                       ";
72 PRINT"ƒƒƒ‚AŠŒ                      ";
73 PRINT"…‹ƒƒ‹‚‚ŒŒ                  ";
74 PRINT"…BC…DŠŠƒ‚ŒŒ              ";
75 PRINT"„ŒŽŒ„EFŠ‹ƒ‚           ";
76 PRINT"…‡…ŠŠŠG‚ Žˆƒƒ„Œ   ";
77 PRINT"…ŒŒ„ˆˆˆŒŽŒ€Œ€ŒŒˆƒ„";
78 PRINT"€€H€";
79 PRINT"…ƒƒ‚‡‚‡‚ƒ‹ƒ€ƒ€ƒ‡ƒ‚Œ‡";
80 PRINT"……ŠŠŠIˆ‡ ‹‚ŒŒƒ   ";
81 PRINT"ƒ‹ƒ‡KJŠŽŒˆ‡           ";
82 PRINT"…N‡M…LŠŠŒˆƒƒ              ";
83 PRINT"…ŽŒŒ„Žˆˆƒƒ                  ";
84 PRINT"„ŒŒŒˆOŠƒ  YOU ARE CARRYING:-  ";
85 PRINT"    ‹ƒ„Œˆ   ";:GOSUB150:RETURN
90 PRINT@0,"SCORE"SC;TAB(12)"CREW"N;TAB(22)"ALIEN"HP;:RETURN
100 A=1+RND(23):B=7+RND(20):IFPOINT(A,B)<>1THEN100
101 RETURN
150 PRINT@32*8+21,"Ÿ";
151 PRINT@492,M$(W(F));
152 PRINT@32*1+14,"":PRINT@32*2+14,"":PRINT@32*3+14,"":RETURN
800 ONRND(2)GOTO810,820
810 F=0:C1=0:PRINT@32*1+14,"YOUR WEAPON HAS";:PRINT@32*2+14,"MALFUNCTIONED!";:SOUND240,5:FORP=1TO2000:NEXT:GOSUB151:RETURN
820 PRINT@32*1+14,"YOU DROPPED YOUR";:PRINT@32*2+14,"WEAPON!";:SOUND50,1:SOUND10,1:FORP=1TO2000:NEXT:GOSUB151
830 P=16384+INT(X/2)+32*INT(Y/2):GOSUB950:F=0:C1=0:RETURN
900 P=16384+INT(X/2)+32*INT(Y/2):TF=PEEK(P)-64:SOUND1,1:POKEP,143
910 IFF<>0THENGOSUB950
920 F=TF:C1=0:GOSUB151:P=1:RETURN
950 IFPEEK(P-32)=143THENPOKEP-32,64+F:RETURN
952 IFPEEK(P+32)=143THENPOKEP+32,64+F:RETURN
953 IFPEEK(P-1)=143THENPOKEP-1,64+F:RETURN
954 IFPEEK(P+1)=143THENPOKEP+1,64+F:RETURN
960 IFPEEK(P-33)=143THENPOKEP-33,64+F:RETURN
961 IFPEEK(P-31)=143THENPOKEP-31,64+F:RETURN
962 IFPEEK(P+31)=143THENPOKEP+31,64+F:RETURN
963 IFPEEK(P+33)=143THENPOKEP+33,64+F:RETURN
990 RETURN
1000 REM FIGHT
1080 H=RND(((W(F)/2)+.5)*10)+W(F):IFH<10THENH=0:GOTO1090
1085 SET(A,B,4):RESET(A,B)
1090 HP=HP-H:IFHP<0THENHP=0
1095 SC=SC+H:GOSUB90:PRINT@1*32+18,E$(INT(H/10));:IFH>=10ANDHP>0THENPRINT@2*32+18,"IT FLEES...";
1100 FORP=1TO2500:NEXT:GOSUB152:SET(A,B,1):IFH>=10ANDHP>0THENGOSUB100
1110 IFHP<1THENGOSUB5000
1120 RETURN
3000 SET(X,Y,4):RESET(X,Y)
3001 PRINT@32*1+14,"A SICKENING SCREAM";
3002 PRINT@32*2+14,"RINGS THROUGH THE";
3003 PRINT@32*3+14,"SHIP. IT GOT YOU!";
3004 N=N-1:GOSUB90:FORP=1TO2500:NEXT:F=0:C1=0
3005 IFN=0THENGOSUB4000:HP=0:RETURN
3010 SET(X,Y,1):SET(A,B,1):GOSUB100:X=44:Y=17:GOSUB150:I$=INKEY$:I$=INKEY$:RETURN
4000 CLS:PRINTTAB(8)"THE FATAL DEFEAT":PRINTTAB(8)"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
4010 PRINT"1000 YEARS AFTER THIS GRUESOME"
4011 PRINT"EPISODE A STRANGE SHIP APPEARS  IN THE SOLAR SYSTEM. AS IT"
4012 PRINT"SWINGS TOWARDS THEM THE PEOPLE  OF EARTH PUZZLE OVER ITS ORIGINS";
4013 PRINT"WHILE ON BOARD THE ANCIENT      NOSTRODAMUS THE ALIEN BROOD SITSAND WAITS."
4014 PRINT"YOUR ATTEMPT TO DEFEAT THE      CREATURE, THOUGH NOBLE, FAILED."
4015 PRINT"YOU ATTAINED A SCORE OF        "SC"POINTS":IFSC>HSTHENHS=SC
4020 PRINT"HIGH SCORE ="HS:PRINT@480,"DO YOU WANT ANOTHER GAME?";
4025 I$=INKEY$:IFI$=""THEN4025
4030 IFI$="Y"THENRETURN
4040 IFI$="N"THENEND
4050 GOTO4025
5000 CLS:SC=SC+(100*N):PRINT
5005 PRINT"       ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ‚"
5010 PRINT"       … ";:I$="YOU ARE SAVED!":GOSUB8200:PRINT" Š"
5015 PRINT"       „ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒˆ":PRINT:PRINT
5020 PRINT"YOU MANAGED TO DESTROY THE ALIEN";:IFN>1THENPRINT"BEFORE IT WIPED OUT YOUR CREW!":PRINT:GOTO4015
5025 PRINT"BUT ONLY YOU SURVIVED TO TELL   THE TALE...":PRINT:GOTO4015
8000 M$(0)="NOTHING          "
8005 M$(1)="A LASER GUN      "
8010 M$(2)="A FLAMETHROWER   "
8015 M$(3)="A WELDING TORCH  "
8020 M$(4)="BOTTLES OF ACID  "
8025 M$(5)="GAS GRENADES     "
8030 M$(6)="EXPLOSIVES       "
8035 M$(7)="A SONIC GUN      "
8040 M$(8)="A FORCE GENERATOR"
8045 M$(9)="GAS GRENADES     "
8050 M$(10)="A LASER GUN      "
8055 M$(11)="AN ELECTRIC PROD "
8060 M$(12)="GAS GRENADES     "
8065 M$(13)="EXPLOSIVES       "
8070 M$(14)="A FLAMETHROWER   "
8075 M$(15)="A FORCE GENERATOR"
8100 REM EFFECTS
8100 E$(0)="NO EFFECT"
8110 E$(1)="STUNNED"
8115 E$(2)="STUNNED"
8120 E$(3)="SLIGHT DAMAGE"
8125 E$(4)="SLIGHT DAMAGE"
8130 E$(5)="MINOR DAMAGE"
8135 E$(6)="MINOR DAMAGE"
8140 E$(7)="MAJOR DAMAGE"
8150 E$(8)="MAJOR DAMAGE"
8160 E$(9)="MAJOR DAMAGE"
8170 E$(10)="MAJOR DAMAGE":RETURN
8200 PRINTI$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+16384:FORC1=CC-LEN(I$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
8300 PRINTTAB(10);:I$="PRESS ANY KEY":GOSUB8200
8310 I$=INKEY$:IFI$=""THEN8310
8320 RETURN
9000 CLS:PRINT"DO YOU WANT INSTRUCTIONS?"
9001 I$=INKEY$:IFI$="Y"THEN9005
9002 IFI$="N"THENPRINT:GOTO9180
9003 GOTO9001
9005 CLS 
9007 PRINT"       ŒŒŽŒŒŽ"
9008 PRINT"       „Œ…Š…„Š‹…"
9009 PRINT"       ……ŠŒŒ…„ŒŠ‹…"
9010 PRINTTAB(9)"BY P.MERRIMAN"
9012 PRINT"  YOU ARE ON BOARD THE TRANSPORTSHIP 'NOSTRODAMUS' BOUND FOR"
9013 PRINT"EARTH WHEN YOU PICK UP A MAYDAY SIGNAL FROM A NEARBY PLANET."
9015 PRINT"  WHEN YOU INVESTIGATE YOU      DISCOVER AN ANCIENT ALIEN SHIP."
9016 PRINT"  WHILE EXPLORING IT, ONE OF    THE CREW MEMBERS IS ATTACKED."
9020 PRINT"  YOU MANAGE TO GET HIM BACK TO THE SHIP WITH A STRANGE LIVING"
9021 PRINT"CREATURE ATTACHED TO HIS FACE.":GOSUB8300
9030 CLS:PRINT"  EVENTUALLY THE CREWMAN DIES   AND THE ORGANISM WHICH HAS BEEN"
9031 PRINT"GROWING IN HIS STOMACH ESCAPES  AND GROWS AT A PHENOMENAL RATE."
9032 PRINT"  EARTH FORBIDS YOU TO RETURN   WITH THE CREATURE STILL ALIVE SO";
9033 PRINT"YOU MUST TRY AND KILL IT.":PRINT
9034 PRINT"THE GAME SIMULATES YOUR ATTEMPTSTO DO SO AS YOU SEARCH THE SPACE";
9035 PRINT"CRAFT HOPING THE WEAPON YOU HAVEPICKED UP WILL WORK AGAINST THE":PRINT"CREATURE.":GOSUB8300
9038 CLS
9040 PRINT"          HOW TO PLAY"
9045 PRINT"          ƒƒƒƒƒƒƒƒƒƒƒ"
9050 PRINT"MOVEMENT IS IN EIGHT DIRECTIONS USING THE KEYS SHOWN BELOW"
9055 PRINT"Q  W  E     ‰‚"
9056 PRINT"            †Š‰"
9057 PRINT"            †Š‰"
9058 PRINT"            Ž"
9060 PRINT"A  S  D  =  †ƒƒƒƒ‰"
9061 PRINT"            ŽŽ"
9065 PRINT"            Ž‡Š‹"
9067 PRINT"            ‡‰‹Ž"
9070 PRINT"Z  X  C     ƒ‹ƒ"
9075 PRINT"WHERE 'S' CORRESPONDS TO YOUR   POSITION.":GOSUB8300
9090 CLS 
9095 PRINT"  YOU ARE SHOWN AS A SMALL PIXELNEAR THE BRIDGE."
9096 PRINT"  THE VARIOUS LETTERS SCATTERED AROUND THE SHIP ARE WEAPONS."
9097 PRINT"YOU WON'T KNOW WHICH PARTICULAR KIND THEY ARE UNTIL YOU PICK":PRINT"THEM UP."
9100 PRINT"  TO PICK UP A WEAPON SIMPLY    MOVE THROUGH A LETTER. HOWEVER"
9101 PRINT"YOU CAN ONLY CARRY ONE WEAPON ATA TIME."
9102 PRINT"  THE DISPLAY WILL INDICATE THE NAME OF THE WEAPON, THOUGH YOU"
9103 PRINT"WON'T KNOW ITS EFFECT ON THE    ALIEN UNTIL YOU TRY IT OUT.":GOSUB8300
9109 CLS 
9110 PRINT"  TO LEAVE THE WEAPON JUST MOVE OVER ONE OF THE OTHER WEAPONS."
9111 PRINT"  IF THERE IS NO ROOM FOR IT"
9112 PRINT"TO BE DROPPED THEN IT WILL BE   DESTROYED."
9115 PRINT"  IN ORDER TO USE A WEAPON      AGAINST THE ALIEN IT MUST BE"
9116 PRINT"WITHIN FIVE MOVES OF YOU AND    THERE MUST BE A CLEAR LINE OF"
9117 PRINT"SIGHT (WHICH MEANS NO WEAPONS   OR WALLS IN THE WAY).":GOSUB8300
9125 CLS 
9130 PRINT"  ONCE THESE CONDITIONS HAVE    BEEN SATISFIED YOU CAN FIRE BY"
9131 PRINT"PRESSING THE space KEY AND THEN WHILE YOUR PIXEL IS FLASHING,"
9132 PRINT"PRESSING A DIRECTION KEY."
9133 PRINT"  THE ALIEN IS ALSO DISPLAYED   AS A SINGLE PIXEL BUT IT CAN"
9134 PRINT"ONLY BE SEEN WHEN IT IS WITHIN  ABOUT TEN MOVES OF YOU. IN "
9135 PRINT"GENERAL IT WILL MOVE DIRECTLY   TOWARDS YOU  NAVIGATING ITS WAY AROUND WALLS ETC.":GOSUB8300
9143 CLS 
9145 PRINT"  THE CREATURE STARTS WITH 200  POINTS BUT ANY HITS WILL REDUCE"
9146 PRINT"THIS VALUE. WHEN THE POINTS     GO BELOW ZERO THE CREATURE IS   DEAD."
9150 PRINT"  WHEN ALL FOUR MEMBERS OF YOUR CREW HAVE BEEN KILLED THE GAME  ENDS. THE SQUARE Ÿ IS AN"
9151 PRINT"ENERGY BARRIER THROUGH WHICH    ONLY YOU CAN PASS.":PRINT:PRINTTAB(10)"GOOD LUCK...":PRINT
9155 PRINT"TO START THE GAME PRESS KEY s.  TO GET THE INSTRUCTIONS AGAIN   PRESS KEY r.";
9160 I$=INKEY$:IFI$="S"THENCLS:GOTO9180
9165 IFI$="R"THEN9005
9170 GOTO9160
9180 PRINT"LEVEL OF DIFFICULTY (1-3)?";
9185 I$=INKEY$:IFI$="1"THEND=2:RETURN
9186 IFI$="2"THEND=3:RETURN
9187 IFI$="3"THEND=5:RETURN
9189 GOTO9185
9200 REM RE-MAKE FOR THE MC-10
9210 REM BY JIM GERRIE
9220 REM INSPIRED BY A ZX-81
9230 REM GAME BY P. MERRIMAN
